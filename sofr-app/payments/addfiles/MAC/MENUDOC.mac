import general, Utils;
/*----------------------------------------------------------------------------*/
/*Установить состояние                                                        */
/*----------------------------------------------------------------------------*/
macro SetCurrState(Id, IdForm, vr)
  var PaymObj, state, tmp;

  PaymObj = id;
  id = PaymObj.Paym.id_paym;
  state = PaymObj.paym.id_sstate;
  state = int(GetColByCol("sstate", "code", "id_sstate", state));
  if(not VclGetInt(state,"Введите состояние","Установка состояния",0,5))
     return;
  end;
  tmp = state ;
  state = int(GetColByCol("sstate", "id_sstate", "code", state));
  if (not state)
    VclMsgBox ("Не найден идентификатор состояния");
    return;
  end;

  if (not PaymObj.ChangeState(state,0,"макрос установки состояния"))
    VclMsgBox ("Ошибка "+ PaymObj.Error + " при изменении состояния платежа " + Id);
    OraTrnRollback();
  else
    OraTrnCommit();
    AddPtk ("Пользователь изменил состояние платежа " + ToId(Id) + " на " + tmp);
  end;

  ExitScreen();
END;

/*----------------------------------------------------------------------------*/
/*Справка о маршруте документа                                                */
/*----------------------------------------------------------------------------*/
macro DocSpr(Id)
  var PaymObj, Pay;
  var SACCCURRAMOUNT = "";
  var PHist = TRslOraQuery();
  var ok;
  var KorrCodeINPUT, KorrNameINPUT, KorrCodeCURRAMOUNT, KorrNameCURRAMOUNT, KorrCodeOUTPUT, KorrNameOUTPUT;  
  var KSCodeINPUT, KSNameINPUT, FrnSysINPUT, KSCodeCURRAMOUNT, KSNameCURRAMOUNT, FrnSysCURRAMOUNT, KSCodeOUTPUT, KSNameOUTPUT, FrnSysOUTPUT;  
  
  KorrCodeINPUT=KorrNameINPUT=KorrCodeCURRAMOUNT=KorrNameCURRAMOUNT=KorrCodeOUTPUT=KorrNameOUTPUT="";  
  KSCodeINPUT=KSNameINPUT=FrnSysINPUT=KSCodeCURRAMOUNT=KSNameCURRAMOUNT=FrnSysCURRAMOUNT=KSCodeOUTPUT=KSNameOUTPUT=FrnSysOUTPUT="";

  PaymObj = id;
  Pay = PaymObj.Paym;

  id = Pay.id_paym;
  if(ValType(Pay.ID_SACCCURRAMOUNT)!=V_UNDEF)
    SACCCURRAMOUNT = GetColByCol("sacc", "code", "id_sacc", Pay.ID_SACCCURRAMOUNT);
  end;

  KorrCodeINPUT      = GetKorrCode(Pay.ID_NKORRINPUT,KorrNameINPUT);
  KorrCodeCURRAMOUNT = GetKorrCode(Pay.ID_NKORRCURRAMOUNT,KorrNameCURRAMOUNT);
  KorrCodeOUTPUT     = GetKorrCode(Pay.ID_NKORROUTPUT,KorrNameOUTPUT);

  KSCodeINPUT      = GetKSCode(Pay.ID_NKORRINPUT, Pay.ID_NKSINPUT, Pay.ID_SFIINPUT, KSnameINPUT, FrnSysINPUT);
  KSCodeCURRAMOUNT = GetKSCode(Pay.ID_NKORRCURRAMOUNT, Pay.ID_NKSCURRAMOUNT, Pay.ID_SFICURRAMOUNT, KSnameCURRAMOUNT, FrnSysCURRAMOUNT);
  KSCodeOUTPUT     = GetKSCode(Pay.ID_NKORROUTPUT, Pay.ID_NKSOUTPUT, Pay.ID_SFIOUTPUT, KSnameOUTPUT, FrnSysOUTPUT);

  [
   Внутренний системный номер: #################

   Поступил от
      Контрагент: ######## #################### ##############################################################
           Схема: ######## #######################################
        Id шлюза: ##################
          Валюта: ###

       Тек. счет: ########################
      Контрагент: ######## #################### ##############################################################
           Схема: ######## #######################################
       Тип счета:       ##
          Валюта: ###

   Отправлен на
      Контрагент: ######## #################### ##############################################################
           Схема: ######## #######################################
        Id шлюза: ##################
          Валюта: ###


                  История состояний

     Код            Название           Дата/Время      Опер      Ошибка                      Комментарий
   ------- ----------------------- ----------------- -------- ----------- ------------------------------------------
  ]
  
   (ToId(Id):l,
    KorrCodeINPUT:r,GetKorrBic(BICRus_ID,Pay.ID_NKORRINPUT),KorrNameINPUT,
    KSCodeINPUT:r,KSnameINPUT,
    FrnSysINPUT,
    int(GetCodeFi(Pay.ID_SFIINPUT)),

    Pay.AccCurrAmount:r,
    KorrCodeCURRAMOUNT:r,GetKorrBic(BICRus_ID,Pay.ID_NKORRCURRAMOUNT),KorrNameCURRAMOUNT,
    KSCodeCURRAMOUNT:r,KSnameCURRAMOUNT,
    SACCCURRAMOUNT,
    int(GetCodeFi(Pay.ID_SFICURRAMOUNT)),

    KorrCodeOUTPUT:r,GetKorrBic(BICRus_ID,Pay.ID_NKORROUTPUT),KorrNameOUTPUT,
    KSCodeOUTPUT:r,KSnameOUTPUT,
    FrnSysOUTPUT,
    int(GetCodeFi(Pay.ID_SFIOUTPUT)));

  /*История*/
  PHist.SqlText = " select (select s.code from sstate s where s.id_sstate = t.id_sstate) as state_code, "+
                  "        (select s.text from sstate s where s.id_sstate = t.id_sstate) as state_name, "+
                  "        t.systemdate as systemdate, "+
                  "        t.usernick as usernick,     "+
                  "        t.varlen as varlen,         "+
                  "        t.id_serror as id_serror    "+
                  " from PAYHST t                      "+
                  " where t.id_paym = :id_paym         "+
                  " order by t.id_payhst";

  PHist.DeclareAndSet("id_paym",id);

  if(not PHist.Execute())
    RunError(toansi("макрофункция DocSpr:\r\n"+ToANSi(PHist.Error)));
  end;

  ok = not PHist.Eof;
  while(ok)
    [####### ####################### ################# ######## #### ###### #]
    (PHist.GetByName("state_code").AsInteger,
     PHist.GetByName("state_name").AsString,
     PHist.GetByName("systemdate").AsString,
     PHist.GetByName("usernick").AsString,
     PHist.GetByName("id_serror").AsString,
     GetColByCol("serror", "code", "id_serror", PHist.GetByName("id_serror").AsString),
     PHist.GetByName("varlen").AsString);
    ok = PHist.Next();
  end;

end;



