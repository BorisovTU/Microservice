/*
$Name:             ws_validation.mac
$Module:           WEB 
$Description:      Валидация
*/

class TMessageSeverity
    const ValidationError = "validationError";
    const RuntimeError = "runtimeError";
    const Warning = "warning";
    const Info = "info";
end;
const MessageSeverity = TMessageSeverity;

// обязательные Severity, (Code или Message)
class WsErrorMessage
    var Severity : string = MessageSeverity.RuntimeError;
    var Code : integer = 0;
    var Message : string = "";
    var Help : string = ""; //help page
    var PropertyName : string = "";
end;

class WebServiceResponse(_userObject)
    var UserObject = _userObject;
    var ErrorMessages = TArray(); // of WsErrorMessage
end;

class WebResponseBuilder
    private var response = WebServiceResponse;
    private var _hasErrors : bool = false;

    macro AddMessageObj(error : WsErrorMessage)
        if ((not _hasErrors)
        and ((error.Severity == MessageSeverity.ValidationError)
            or (error.Severity == MessageSeverity.RuntimeError)))
            _hasErrors = true;
        end;
        response.ErrorMessages[response.ErrorMessages.size] = error;
    end;

    macro AddMessage(
        severity: string, 
        errorMessage: string, 
        propertyName: string, 
        code: integer
        )
        var message = WsErrorMessage;
        message.Severity = severity;

        message.Message = errorMessage;

        if (ValType(code) != V_UNDEF)
            message.Code = code;
        end;

        if (ValType(propertyName) != V_UNDEF)
            message.PropertyName = propertyName;
        end;

        AddMessageObj(message);
    end;

    macro AddValidationError(errorMessage: string, propertyName: string, code: integer)
        AddMessage(MessageSeverity.ValidationError, errorMessage, propertyName, code);
    end;

    macro AddWarning(errorMessage: string, propertyName: string, code: integer)
        AddMessage(MessageSeverity.Warning, errorMessage, propertyName, code);
    end;

    macro AddInfo(errorMessage: string, propertyName: string, code: integer)
        AddMessage(MessageSeverity.Info, errorMessage, propertyName, code);
    end;

    // ошибка выполнения к полю не привязывается
    macro AddRuntimeError(errorMessage: string, code: integer)
        AddMessage(MessageSeverity.RuntimeError, errorMessage, null, code);
    end;

    macro SetUserObject(obj)
        response.UserObject = obj;    
    end;

    macro Result()
        return response;
    end;

    macro HasErrors()
        return _hasErrors;
    end;
end;
