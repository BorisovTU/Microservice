/* Справка по корсхемам */

import ws_positioning;


private macro GetSQLTextCorSchemesHelp(): string
    CaptureOutput();
[
SELECT p.id_sfi,
       s.code AS sfi_code,
       s.text AS sfi_text,
       n.id_nks,
       n.id_sfi AS id_sfiout,
       nvl(n.text, 'Не спозиционировано') AS text,
       n.id_nkorr,
       k.id_node,
       COUNT(*) AS cnt,
       SUM(p.amount) AS amount
  FROM paym p
  LEFT JOIN nks n
    ON p.id_nksoutput = n.id_nks
  LEFT JOIN sfi s
    ON p.id_sfi = s.id_sfi
  LEFT JOIN nkorr k
    ON n.id_nkorr = k.id_nkorr
 WHERE p.id_sstate = (SELECT mpl.id_sstate
                        FROM nmenu_paramlist mpl
                       WHERE mpl.id_nmenu = :menuid)
 GROUP BY p.id_sfi,
          s.code,
          s.text,
          n.id_nks,
          n.id_sfi,
          n.text,
          n.id_nkorr,
          k.id_node
 ORDER BY s.code, nvl(n.id_nks, 0)
];
    return StopCaptureOutput();
end;

macro GetRowsCorSchemesHelp(params)
    var cmd = TRslOraQuery();
    cmd.SqlText = GetSQLTextCorSchemesHelp();
    cmd.DeclareAndSet("menuid", StrSubst(params.ScrollingId, "_CorSchemesHelp", ""));
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd;
end;

macro GetRowCountCorSchemesHelp(params)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT COUNT(*) CNT FROM (" + GetSQLTextCorSchemesHelp() + ")";
    cmd.DeclareAndSet("menuid", StrSubst(params.ScrollingId, "_CorSchemesHelp", ""));
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.GetByName("CNT").AsInteger;
end;

class (IScrolling) ScrollingCorSchemesHelp(_id)
    InitIScrolling(_id);

    private macro SetColumns()
        Columns = TArray();
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "SFICode",
            "Type", ScrollingColumnType.Numeric,//Text,
            "Title", "Валюта",
            "Source", "SFI_CODE",
            "Tooltip", "Валюта"
        );
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "Text",
            "Type", ScrollingColumnType.Hyperlink,
            "Title", "Корсхема",
            "Source", "TEXT",
            "Tooltip", "Корсхема"
        );
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "Count",
            "Type", ScrollingColumnType.Numeric,
            "Title", "Количество",
            "Source", "CNT",
            "Tooltip", "Количество"
        );
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "Amount",
            "Type", ScrollingColumnType.Money,
            "Title", "Сумма",
            "Source", "AMOUNT",
            "Tooltip", "Сумма"
        );
    end;

    private macro SetToolbarItems()
        SysToolbarItems = TArray();
        SysToolbarItems[SysToolbarItems.Size] = makeObj(
            "ISysButton",
            "Name", PredefinedSysButton.Open,
            "Service", WebServiceCallback("ws_corschemes_help", "OpenRowCorSchemesHelp")
        );
    end;

    Title = "Справка по корсхемам";
    MultiPage = false;
    MultiSelect = false;
    SetColumns();
    SetToolbarItems();
    GetRowsService = WebServiceCallback("ws_corschemes_help", "GetRowsCorSchemesHelp");
    GetRowCountService = WebServiceCallback("ws_corschemes_help", "GetRowCountCorSchemesHelp");
end;

class (IWindow) ScrollingCorSchemesHelpWindow(scrollingId)
    Id = scrollingId + "_CorSchemesHelp";
    Title = "Справка по корсхемам";
    WindowParams = makeObj(
        "IWindowParams",
        "Width", 800,
        "Height", 480,
        "HelpPage", "",
        "IsModal", false,
        "IsResizing", true
    );
    Appearance = WindowAppearanceType.Scrolling;
    Fields = makeArray(
        makeObj(
            "IPanelItem",
            "Id", Id,
            "Row", 0,
            "Type", PanelItemType.Scrolling,
            "Field", ScrollingCorSchemesHelp(Id)
        )
    );
end;

macro GetScrollingCorSchemesHelp(params)
    return WebServiceResult(WebServiceResultType.Window, ScrollingCorSchemesHelpWindow(params.WindowId));
end;

macro OpenRowCorSchemesHelp(params)
    if (params.CurrentRow != null)
        var alteredModel = PositioningModel();
        if (params.CurrentRow.ID_SFI)
            alteredModel.LeftCurrencyId = params.CurrentRow.ID_SFI;
            alteredModel.LeftCurrencyCode = params.CurrentRow.SFI_Code;
            alteredModel.LeftCurrencyText = params.CurrentRow.SFI_Text;
            alteredModel.RightCurrencyId = params.CurrentRow.ID_SFI;
            alteredModel.RightCurrencyCode = params.CurrentRow.SFI_Code;
            alteredModel.RightCurrencyText = params.CurrentRow.SFI_Text;
            var cmd = GetFirstOrDefaultCorrespondentSchemeCommand(params.CurrentRow.ID_SFI);
            if (not cmd.Eof)
                alteredModel.CorrespondentSchemeId = cmd.GetByName("CorrespondentSchemeId").AsInteger;
                alteredModel.CorrespondentSchemeText = cmd.GetByName("CorrespondentSchemeText").AsString;
                alteredModel.Correspondent = cmd.GetByName("Correspondent").AsInteger;
                alteredModel.Node = cmd.GetByName("Node").AsInteger;
            end;
        end;
        if (params.CurrentRow.ID_NKS)
            alteredModel.CorrespondentSchemeId = params.CurrentRow.ID_NKS;
            alteredModel.CorrespondentSchemeText = params.CurrentRow.Text;
            alteredModel.Correspondent = params.CurrentRow.ID_NKorr;
            alteredModel.Node = params.CurrentRow.ID_Node
        end;
        return WebServiceResult(
            WebServiceResultType.Action,
            makeArray(
                IAction(WebServiceActionType.UpdateParentModel, alteredModel),
                IAction(WebServiceActionType.RefreshParent),
                IAction(WebServiceActionType.Close)
            )
        );
    end;
end;
