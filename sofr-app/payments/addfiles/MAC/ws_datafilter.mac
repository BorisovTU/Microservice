/*
$Name:        ws_datafilter.mac
$Module:      Web-общее
$Description: Функции для работы с условиями фильтрации
*/

const FilterCondition_Undefined = -1;
const FilterCondition_Equal = 0; // = ? / like '?'
const FilterCondition_Null = 1; // is null
const FilterCondition_Include = 2; // in ( ? ) / like '%?%'
const FilterCondition_StartWith = 4; // >= ? / like '?%'
const FilterCondition_From = 8; // >= ?
const FilterCondition_To = 16; // <= ?
const FilterCondition_Between = 32; // between ? and ?
const FilterCondition_CurrentDate = 64; // = {curdate}
const FilterCondition_Greater = 128; // > ?
const FilterCondition_GreaterOrEqual = 256; // >= ?
const FilterCondition_Less = 512; // < ?
const FilterCondition_LessOrEqual = 1024; // <= ?
const FilterCondition_All = 2048; // not use

const FilterParameterType_Int = 0;
const FilterParameterType_Float = 1;
const FilterParameterType_Money = 2;
const FilterParameterType_String = 3;
const FilterParameterType_Date = 4;
const FilterParameterType_Time = 5;
const FilterParameterType_Bool = 6;
const FilterParameterType_UserType = 7;
const FilterParameterType_CheckList = 8;

class FilterConditionItem()
    var Id:Integer;
    var Condition:Integer;
    var Value = TArray();
    var IsNot:Bool;
    var ValueType:Integer;
end;

class FilterParser(
    _fltrCondItemArr
)
    private var fltrCondItemArr = _fltrCondItemArr;
    private var fldCondArr = TArray();

    private class FieldCondition()
        var Id:Integer;
        var TableName:String;
        var FieldName:String;
        var UserOperation:Variant;
        var IsNullOperand:String;
        var SQLCondition:String;
    end;

    private macro IIF(
        condition
       ,valueTrue
       ,valueFalse
    )
        var value;

        if( condition )
            value = valueTrue;
        else
            value = valueFalse;
        end;

        return value;
    end;

    macro AddFieldCondition(
        id:Integer
       ,tableName:String
       ,fieldName:String
       ,isNullOperand:String
       ,userOperation:Variant
    )
        var fldCond = FieldCondition();

        fldCond.Id = id;
        fldCond.TableName = tableName;
        fldCond.FieldName = fieldName;
        fldCond.IsNullOperand = isNullOperand;
        fldCond.UserOperation = userOperation;
        fldCond.SQLCondition = "";

        fldCondArr[fldCondArr.Size] = fldCond;
    end;

    macro AddUserFieldCondition(
        id:Integer
       ,userOperation:Variant
    )
        AddFieldCondition( id, null, null, null, userOperation );
    end;

    private macro IsNumeric(
        type:Integer
    ):Bool
        return ( ValType( type ) == V_INTEGER )
              and ( ( type == FilterParameterType_Int )
                 or ( type == FilterParameterType_Float )
                 or ( type == FilterParameterType_Money ) );
    end;

    private macro IsString(
        type:Integer
    ):Bool
        return ( ( ValType( type ) == V_INTEGER ) and ( type == FilterParameterType_String ) );
    end;

    private macro IsDate(
        type:Integer
    ):Bool
        return ( ( ValType( type ) == V_INTEGER ) and ( type == FilterParameterType_Date ) );
    end;

    private macro IsTime(
        type:Integer
    ):Bool
        return ( ( ValType( type ) == V_INTEGER ) and ( type == FilterParameterType_Time ) );
    end;

    private macro IsBool(
        type:Integer
    ):Bool
        return ( ( ValType( type ) == V_INTEGER ) and ( type == FilterParameterType_Bool ) );
    end;

    private macro IsUserType(
        type:Integer
    ):Bool
        return ( ( ValType( type ) == V_INTEGER ) and ( type == FilterParameterType_UserType ) );
    end;

    private macro IsCheckListType(
        type:Integer
    ):Bool
        return ( ( ValType( type ) == V_INTEGER ) and ( type == FilterParameterType_CheckList ) );
    end;

    private macro GetSQLNumeric(
        value
    ):String
        var sqlValue:String = "";

        if( ( ValType( value ) == V_INTEGER )
         or ( ValType( value ) == V_MONEY )
         or ( ValType( value ) == V_DECIMAL )
         or ( ValType( value ) == V_DOUBLE ) )
            sqlValue = String( value:0:18 );
        else
            sqlValue = " 0 ";
        end;

        return sqlValue;
    end;

    private macro GetSQLString(
        value:String
    ):String
        var sqlValue:String = "";

        if( ( ValType( value ) == V_STRING ) and ( value != "" ) )
            sqlValue = value;
            sqlValue = StrSubst( sqlValue, "?", "_" );
            sqlValue = StrSubst( sqlValue, "*", "%" );
        else
            sqlValue = "''";
        end;

        return sqlValue;
    end;

    private macro GetSQLDate(
        value:Variant
    ):String
        var sqlValue:String = "";
        var dateValue:Date = Date(00, 00, 0000);
        var timeValue:Time = Time(00, 00, 00);

        if( ValType( value ) == V_DTTM )
            DtTmSplit( value, dateValue, timeValue );

            if( dateValue != Date(00, 00, 0000) )
                sqlValue = " TO_DATE('" + dateValue + " " + timeValue + "', 'dd.mm.yyyy HH24:MI:SS') ";
            else
                sqlValue = " TO_DATE('01.01.0001 00:00:00', 'dd.mm.yyyy HH24:MI:SS') ";
            end;
        elif( ValType( value ) == V_DATE )
            if( value != Date(00, 00, 0000) )
                sqlValue = " TO_DATE('" + value + "', 'DD.MM.YYYY') ";
            else
                sqlValue = " TO_DATE('01.01.0001', 'DD-MM-YYYY') ";
            end;
        else
            sqlValue = " TO_DATE('01.01.0001', 'DD-MM-YYYY') ";
        end;

        return sqlValue;
    end;

    private macro GetSQLTime(
        value:Time
    ):String
        var sqlValue:String = "";

        if( ( ValType( value ) == V_TIME ) and ( value != Time(00, 00, 00) ) )
            sqlValue = " TO_DATE('01.01.0001 " + value + "', 'dd.mm.yyyy HH24:MI:SS')";
        else
            sqlValue = " TO_DATE('01.01.0001 00:00:00', 'dd.mm.yyyy HH24:MI:SS') ";
        end;

        return sqlValue;
    end;

    private macro GetSQLBool(
        value:Bool
    ):String
        var sqlValue:String = "";

        if( ( ValType( value ) == V_BOOL ) and ( value == true ) )
            sqlValue = " chr(88) ";
        else
            sqlValue = " chr(0) ";
        end;

        return sqlValue;
    end;

    private macro GetSQLValue(
        value
       ,type:Integer
    ):String
        var sqlValue:String = "";
        if( IsNumeric( type ) )
            sqlValue = GetSQLNumeric( value );
        elif( IsString( type ) )
            sqlValue = GetSQLString( value );
        elif( IsDate( type ) )
            sqlValue = GetSQLDate( value );
        elif( IsTime( type ) )
            sqlValue = GetSQLTime( value );
        elif( IsBool( type ) )
            sqlValue = GetSQLBool( value );
        elif( IsUserType( type ) )
            sqlValue = " null ";
        elif( IsCheckListType( type ) )
            sqlValue = GetSQLNumeric( value );
        else
            sqlValue = " null ";
        end;
        return sqlValue;
    end;

    private macro HasValue(
        value
    ):Bool
        return ( ( ValType( value ) != V_UNDEF )
             and ( value.Size > 0 )
             and ( ValType( value[0] ) != V_UNDEF ) )
    end;

    private macro IsEmptyString(s)
        return (s == null) or (s == "");
    end;

    private macro InsertValueOperand(
        operand:String
       ,value1:String
       ,value2:String
    ):String
        var sqlValue:String = "";
        var symbPos:Integer = 0;

        symbPos = Index( operand, "?" );
        if( symbPos > 0 )
            sqlValue = SubStr( operand, 1, symbPos - 1 ) + value1 + SubStr( operand, symbPos + 1 );
        else
            sqlValue = operand;
        end;

        if( ValType( value2 ) != V_UNDEF )
            sqlValue = InsertValueOperand( sqlValue, value2 );
        end;
        return sqlValue;
    end;

    private macro GetSQLEnum(
        value
       ,type:Integer
    ):String
        var sqlValue:String = "";
        var valueCount:Integer = 0;

        if( HasValue( value ) )
            while( valueCount < value.Size )
                if( sqlValue != "" )
                    sqlValue = sqlValue + ", ";
                end;
                sqlValue = sqlValue + InsertValueOperand( IIF( IsString( type ) ," '?' ", " ? " ), GetSQLValue( value[valueCount], type ) );
                valueCount = valueCount + 1;
            end;
        else
            sqlValue = " null "
        end;

        return sqlValue;
    end;

    private macro FormFieldSQLCondition(
        fldCond:FieldCondition
       ,fltrCondItem/*:FilterConditionItem*/
    ):String
        var strSQLCond:String = "";
        var fullFieldName:String = "";

        if( ( ValType( fltrCondItem.Condition ) != V_UNDEF )
        and ( ( fltrCondItem.Condition == FilterCondition_Null )
           or ( fltrCondItem.Condition == FilterCondition_CurrentDate )
           or ( ( fltrCondItem.Condition != FilterCondition_Undefined )
            and ( HasValue( fltrCondItem.Value ) ) ) ) )

            if( ValType( fldCond.FieldName ) != V_UNDEF )
                if( ValType( fldCond.TableName ) != V_UNDEF )
                    fullFieldName = fldCond.TableName + "." + fldCond.FieldName;
                else
                    fullFieldName = fldCond.FieldName;
                end;
            end;

            if( ValType( fldCond.UserOperation ) == V_PROC )
                strSQLCond = fullFieldName + " " + ExecMacro2( @fldCond.UserOperation, fltrCondItem.Condition, fltrCondItem.Value, fltrCondItem.ValueType );
            elif( ValType( fldCond.UserOperation ) == V_STRING )
                strSQLCond = fullFieldName + " " + InsertValueOperand( fldCond.UserOperation, GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
            else
                if( fltrCondItem.Condition == FilterCondition_Null ) // is null
                    if( ValType( fldCond.IsNullOperand ) != V_UNDEF )
                        strSQLCond = "((" + fullFieldName + fldCond.IsNullOperand + ") or (" + fullFieldName + " is null))";
                    else
                        if( ValType( fltrCondItem.ValueType ) != V_UNDEF )
                            strSQLCond = "((" + fullFieldName + InsertValueOperand( " = ? ", GetSQLValue( null, fltrCondItem.ValueType ) ) + ") or (" + fullFieldName + " is null))";
                        else
                            strSQLCond = fullFieldName + " is null ";
                        end;
                    end;
                elif( fltrCondItem.Condition == FilterCondition_CurrentDate ) // = {curdate}
                    strSQLCond = fullFieldName + InsertValueOperand( " = ? ", GetSQLValue( {curdate}, FilterParameterType_Date ) );
                elif( ( fltrCondItem.Condition != FilterCondition_Undefined )
                  and ( HasValue( fltrCondItem.Value ) ) )
                    if( fltrCondItem.Condition == FilterCondition_Equal ) // = ? / like '?'
                        if( fltrCondItem.Value.Size == 1 ) // single choice
                            strSQLCond = fullFieldName + InsertValueOperand( IIF( IsString( fltrCondItem.ValueType ), " like '?' ", " = ? " ), GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                        elif( fltrCondItem.Value.Size > 1 ) // multiple choice
                            strSQLCond = fullFieldName + InsertValueOperand( " in ( ? ) ", GetSQLEnum( fltrCondItem.Value, fltrCondItem.ValueType ) );
                        end;
                    elif( fltrCondItem.Condition == FilterCondition_Include ) // in ( ? ) / like '%?%'
                        strSQLCond = fullFieldName + InsertValueOperand( IIF( IsString( fltrCondItem.ValueType ), " like '%?%' ", " in ( ? ) " ), GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_StartWith ) // >= ? / like '?%'
                        strSQLCond = fullFieldName + InsertValueOperand( IIF( IsString( fltrCondItem.ValueType ), " like '?%' ", " >= ? " ), GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_From ) // >= ?
                        strSQLCond = fullFieldName + InsertValueOperand( " >= ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_To ) // <= ?
                        strSQLCond = fullFieldName + InsertValueOperand( " <= ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_Between ) // between ? and ?
                        strSQLCond = fullFieldName + InsertValueOperand( " between ? and ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ), GetSQLValue( fltrCondItem.Value[1], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_Greater ) // > ?
                        strSQLCond = fullFieldName + InsertValueOperand( " > ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_GreaterOrEqual ) // >= ?
                        strSQLCond = fullFieldName + InsertValueOperand( " >= ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_Less ) // < ?
                        strSQLCond = fullFieldName + InsertValueOperand( " < ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    elif( fltrCondItem.Condition == FilterCondition_LessOrEqual ) // <= ?
                        strSQLCond = fullFieldName + InsertValueOperand( " <= ? ", GetSQLValue( fltrCondItem.Value[0], fltrCondItem.ValueType ) );
                    end;
                end;
            end;

            if (fltrCondItem.IsNot)
                strSQLCond = "not (" + strSQLCond + ")";
            end;
        end;

        return strSQLCond;
    end;

    private macro AddToFieldSQLCondition(
        _fltrCondItem/*:FilterConditionItem*/
    )
        var fltrCondItem = _fltrCondItem;
        var fldCondCount = 0;

        if( ValType( fltrCondItem ) != V_UNDEF )
            while( fldCondCount < fldCondArr.Size )
                if( (fltrCondItem.Id == fldCondArr[fldCondCount].Id) AND /*SSH*/ (fldCondArr[fldCondCount].SQLCondition=="") )
                    /*SSH
                    if (not IsEmptyString(fldCondArr[fldCondCount].SQLCondition))
                        fldCondArr[fldCondCount].SQLCondition = fldCondArr[fldCondCount].SQLCondition + " and ";
                    end;
                    */
                    fldCondArr[fldCondCount].SQLCondition = fldCondArr[fldCondCount].SQLCondition + FormFieldSQLCondition( fldCondArr[fldCondCount], fltrCondItem );
                    fldCondCount=fldCondArr.Size;//SSH заполнили одно и выходим
                end;
                fldCondCount = fldCondCount + 1;
            end;
        end;
    end;

    private macro ClearSQLCondition()
        var fldCondCount = 0;

        while( fldCondCount < fldCondArr.Size )
            fldCondArr[fldCondCount].SQLCondition = "";
            fldCondCount = fldCondCount + 1;
        end;
    end;

    private macro FormSQLCondition()
        var condCount = 0;

        if( ValType( fltrCondItemArr ) != V_UNDEF )
            while( condCount < fltrCondItemArr.Size )
                AddToFieldSQLCondition( fltrCondItemArr[condCount] );
                condCount = condCount + 1;
            end;
        end;
    end;

    macro GetFullSQLCondition():String
        var fldCondCount = 0;
        var strSQLCond:String = "";

        ClearSQLCondition();

        FormSQLCondition();



        while( fldCondCount < fldCondArr.Size )
            // все условия объединим через И
            if (not IsEmptyString(fldCondArr[fldCondCount].SQLCondition))

                if(strlen(strSQLCond))
                  strSQLCond = strSQLCond + " and ";
                end;
                strSQLCond = strSQLCond + fldCondArr[fldCondCount].SQLCondition;

            end;
            fldCondCount = fldCondCount + 1;
        end;
        return strSQLCond;
    end;

    macro GetFullSQLConditionDebug(
        outFileName:String
    )  /*
        var fullOutFileName:String = "";
        FILE outFile() txt write;

        if( ValType( outFileName ) == V_STRING )
            if( Open( outFile, GetTxtFileName( outFileName ) ) )
                Insert( outFile, GetFullSQLCondition() );
                Close( outFile );
            end;
        end;
        */
        AddPtk(outFileName);
    end;
end;

