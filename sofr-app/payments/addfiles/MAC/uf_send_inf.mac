/*16.02.2015 karlov*/
import uf_general, uf_utils, ips_utils, uf_send;

macro CrtXMLUpd(Pack, EDNo, NameFile, ListNum, isList, TmpTblName)
  var CrtXMLUpd   = TRslOraQuery();
  var OutXML = "";
  var GetValidErr = TRslOraQuery();

  CrtXMLUpd.SqlText = " begin  :OutXML := RSP_UFEBS.CrtUpdXMLEPD(:Pack,:EDNo,:FileName,:TBN,:ST); end;";
  CrtXMLUpd.DeclareAndSet("Pack",Pack,"EDNo",EDNo,"FileName", NameFile,"TBN",TmpTblName,"ST",UFStateValidError);
  CrtXMLUpd.DeclareAndSetLOB("OutXML",OutXML);

  if(not CrtXMLUpd.Execute)
    RunError(toansi("CrtUpdXMLEPD: "+CrtXMLUpd.Error));
  end;

  OutXML = CrtXMLUpd.GetVariable("OutXML");
  /*Сохранение XML в файл*/
  if(StrLen(OutXML))
    SaveToFile(NameFile, UFDirUFEBSArc, UFDirUFEBSOut, OutXML);
    AddPtk("Отправка ЭПС: Создан файл "+UFDirUFEBSOut+NameFile);
  end;

  return true;

end;

macro InfEPDAndCrtXML(XML, NameF, TmpTblName, Pack, ErrStr, /*IsInf,*/ _ValidErrNum, ListNum, isList)
  var EDNo;
  var NameFile;
  var CNT = 0;
  var i = 1;
  var RBIC = "";
  var TypPack = 0;
  var IDEID = "";
  var EDNoEID = 0;

  var InsPacket   = TRslOraQuery();
  var GroupInf    = TRslOraQuery();
  var InsertEPD   = TRslOraQuery();
  var GetValidErr = TRslOraQuery();

  var ValidErrNum = 0;
  var ValidErrIs = false; /*признак существования документов, не прошедших валидацию*/

  GroupInf.SqlText = "SELECT count(*) as CNT                  "+
                     "  FROM (select p.payerbankid            "+
                     "          from "+TmpTblName+" t, paym p "+
                     "          where t.id_paym = p.id_paym   "+
                     "        group by p.payerbankid)"; 

  if(not GroupInf.Execute())
    RunError(toansi("Макрофункция InsEPDAndCrtXML:\r\n"+ToANSi(InsEPDAndCrtXML.Error)));
  end;

  if(Index(UFPacketEID,"ED273"))
    TypPack = 3;

    InsPacket.SqlText = "begin RSP_UFEBS.InsertPacket(:TypPack,:IDPack,:EDNo,null,null); end;";
    InsPacket.DeclareAndSet("TypPack",TypPack,"IDPack",IDEID,"EDNo",EDNoEID);

    if(not InsPacket.Execute)  
      addptk("InsPacket: "+InsPacket.Error);
      RunError(toansi("InsPacket: "+InsPacket.Error));
    else
      IDEID   = InsPacket.GetVariable("IDPack");
      EDNoEID = InsPacket.GetVariable("EDNo");
    end;
  end;

  if(not GroupInf.Eof)
    CNT = GroupInf.GetByName("CNT").AsInteger;
    while(i < 1 + CNT)
      GroupInf.SqlText = "SELECT rbic                                          "+
                         "  FROM (select rownum rn, rbic                       "+
                         "          from (select p.payerbankid rbic            "+
                         "                  from "+UFTblTmpStepDoc+" t, paym p "+
                         "                  where t.id_paym = p.id_paym        "+
                         "                    and p.payerbankid is not null    "+
                         "                group by p.payerbankid))             "+
                         "  WHERE rn = :i";

      GroupInf.DeclareAndSet("i",i);
      if(not GroupInf.Execute())
        RunError(toansi("Макрофункция InsEPDAndCrtXML:\r\n"+ToANSi(InsEPDAndCrtXML.Error)));
      end;

      RBIC = GroupInf.GetByName("RBIC").AsString;
      InsertEPD.SqlText = " begin  :EDNo := RSP_UFEBS.InsertINFEPD(:Tmp_Tbl,:RBIC,:IDPack); end;";
      InsertEPD.DeclareAndSet("EDNo",0,"Tmp_Tbl",TmpTblName,"RBIC",RBIC,"IDPack",IDEID);

      if(not InsertEPD.Execute)  
        addptk("InsertTMPEPD/InsertEPD: "+InsertEPD.Error);
        RunError(toansi("InsertTMPEPD/InsertEPD: "+InsertEPD.Error));
      end;
  
      EDNo = InsertEPD.GetVariable("EDNo");
      if(not EDNo)
        SetParm(4,"Ошибка получения идентификатора документа EDNo");
        return false;
      end;

      if(TypPack != 3)
        NameFile = GetXMLFileName(EDNo);
        if(not CrtXMLUpd(Pack, EDNo, NameFile, ListNum, isList, TmpTblName))  
          addptk("CrtXMLUpd: "+CrtXMLUpd.Error);
          RunError(toansi("CrtXMLUpd: "+CrtXMLUpd.Error));
        end;
      end;

      if(isList)
        VclRemProgress();
        RefreshScreen;
      end;

      i = i + 1;
    end;

    if(TypPack == 3)
      NameFile = GetXMLFileName(EDNoEID);
      if(not CrtXMLUpd(Pack, EDNoEID, NameFile, ListNum, isList, TmpTblName))  
        addptk("CrtXMLUpd: "+CrtXMLUpd.Error);
        RunError(toansi("CrtXMLUpd: "+CrtXMLUpd.Error));
      end;
    end;

    GetValidErr.SQLText = " select t.ID_PAYM from "+TmpTblName+" t where t.ID_SSTATE = :State";
    GetValidErr.DeclareAndSet("State",UFStateValidError);

    /*Выберем документы, не прошедшие валидацию*/
    if(not GetValidErr.Execute)
      RunError(GetValidErr.Error);
    end;

    ValidErrNum = GetValidErr.RowCount;

    /*Если есть документы, ЭПС которых не прошли валидацию*/
    if(ValidErrNum)
      ValidErrIs = true;
    end;

    if(ValidErrIs and isList)
      if(ListNum == 1)/*отправляем один документ*/
        MsgBox("ЭПС по выбранному документу не прошел валидацию. Документ перемещен в отдельный список.");
      else /*отправляем несколько документов по одному*/
        MsgBox("ЭПС по некоторым выбранным документам не прошли валидацию. Такие документы перемещены в отдельный список.");
      end;
    end;

  end;

  return true;
end;

macro SendList_EPDInf(SystemCode)
/*отправка УФЭБС без пакета*/
  var i;
  var ErrStr = "";
  var ListNum = PayListNum();
  var ID = "";
  var VersionRec = "";
  var IsInf = 0;
  var XML = "";
  var NameFile = "";
  var InsertTMP = TRslOraQuery();
  var StateSent;
  var IsPacket = 0;/*признак отправки в пакете*/
  var ValidErrNum = 0;

  VclInitProgress(ListNum, "Отправка ЭПС");
  i = 0;
  InsertTMP.SqlText = " insert into "+UFTblTmpStepDoc+"(ID_PAYM,VARLEN,VR,ID_SSTATE) values(:ID_PAYM,:SendComment,:VR,:ID_SSTATE)";
  InsertTMP.DeclareAndSet("SendComment",SendComment);

  while(i < ListNum)
    ID = VersionRec = 0;
    PayListField(i,0,"id_paym",ID,"VersionRec",VersionRec);

    if(not ID)
      OraTrnRollBack();
      MsgBox("Ошибка получения идентификатора\nОтправка отменена");
      return;
    end;

    /*ID состояния для документов, прошедших валидацию*/
      StateSent = UFStateSentINFO;
      IsPacket = 1;/*информационные всегда в пакете*/

    /*Вставка во временную таблицу*/
    InsertTMP.DeclareAndSet("ID_PAYM",ID, "VR", VersionRec,"ID_SSTATE",StateSent);
    if(not InsertTMP.Execute)
      RunError(InsertTMP.Error);
    end;

    VclUseProgress(i = i + 1);
  end;

  /*Вставка в EPD и получения XML*/
  if(not InfEPDAndCrtXML(XML,NameFile,UFTblTmpStepDoc,IsPacket,ErrStr,ValidErrNum,ListNum, true))
    OraTrnRollBack();
    MsgBox(ErrStr);
    return;
  end;

  /*Смена состояния*/
  if(ChgState2(UFTblTmpStepDoc) == 0)
    OraTrnRollBack();
    MsgBox("Не удалось сменить состояние платежу\nОтправка отменена");
    return;
  end;

  OraTrnCommit();

  OnError(e)
    VclRemProgress();
    OraTrnRollBack();
    AddPtk ("Отправка ЭПС: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
    MsgBox(ToOem(e.Message));  

  return true;

end;

macro SendList_INF()
/*отправка информационных*/
  SendList_EPDInf(UFSystemCodeUFEBS);
end;

