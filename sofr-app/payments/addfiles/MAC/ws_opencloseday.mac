import ws_login;
import ws_types;


/* Проверка монопольного режима */
private macro CountUser(): integer
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT count(*) cnt"
                  "  FROM v$session"
                  " WHERE schemaname = SYS_CONTEXT('USERENV', 'CURRENT_SCHEMA')"
                  "   AND program <> 'java.exe'";
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.GetByName("CNT").AsInteger;
end;

class (IWindow) OpenDayPanelWindow(panelId)
    Id = panelId;
    Title = "Открытие операционного дня";
    WindowParams = makeObj(
        "IWindowParams",
        "Width", 350,
        "Height", 150,
        "HelpPage", "",
        "IsModal", true,
        "IsResizing", false
    );
    Appearance = WindowAppearanceType.ExecutePanel;
    SysButtons = makeArray(
        makeObj(
            "ISysButton",
            "Name", PredefinedSysButton.Execute,
//            "Tooltip", "Выполнить открытие операционного дня",
            "Service", WebServiceCallback("ws_opencloseday", "OpenDay")
        )
    );
    Fields = makeArray(
        makeObj(
            "IPanelItem",
            "Id", Id,
            "Row", 0,
            "Type", PanelItemType.Input,
            "Field", makeObj(
                "IInput",
                "Type", PanelInputFieldType.Date,
                "Label", "Новый день: ",
                "LabelTop", false,
                "ReadOnly", false,
                "Enabled", true,
                "TabStop", true,
                "ClearButton", false,
                "Required", true,
                "Source", "OperDate",
                "Design", makeObj(
                    "IDesign",
                    "Font", makeObj("IFont", "Bold", true)
                ),
                "TextWrapping", false,
                "Location", makeObj(
                    "ILocation",
                    "MinWidth", 200,
                    "Width", 250,
                    "StretchHorizontal", false,
                    "StretchVertical", false,
                    "MarginLeft", 0
                )
            )
        )
    );
    Model = makeObj("TDynamicBean", "OperDate", Date());
end;

/* Открытие опердня */
macro OpenDay(params, scrollingId, model)
    var userNumber = CountUser();
    var result: WebServiceResult;
    if (userNumber > 0)
        result = WebServiceResult(
            WebServiceResultType.Message,
            IMessage("Открытие дня", "Ошибка: Не могу перейти в монопольный режим! Есть " + userNumber + " пользователей, работающие с системой.", MessageBoxType.Error)
        );
    else
        var cmd = TRslOraQuery();
        cmd.SqlText = "SELECT count(*) cnt"
                      "  FROM dates t"
                      " WHERE t.DATES = '" + model.OperDate + "'";
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        var n = cmd.GetByName("CNT").AsInteger;
        if (n > 0) // Уже был такой день - переоткрытие
            cmd.SqlText = "UPDATE dates t"
                          "   SET t.ISZ = 0"
                          " WHERE t.DATES = '" + model.OperDate + "'";
        else // Открытие нового дня
            cmd.SqlText = "INSERT INTO dates (dates, isv, isz, isblk) "
                          "VALUES ('" + model.OperDate + "', 0, 0, 0)";
        end;
        if (not cmd.Execute())
            RunError(ToANSI("Не могу открыть день! " + cmd.Error));
        end;
        OraTrnCommit();
        result = WebServiceResult(WebServiceResultType.Action, IAction(WebServiceActionType.Close));
//        result = WebServiceResult(WebServiceResultType.Message, IMessage("Открытие дня", "День открыт", MessageBoxType.Ok));
    end;
    return result;
end;

// Панель закрытия дня
macro GetOpenDayPanel(params)
    return WebServiceResult(WebServiceResultType.Window, OpenDayPanelWindow(params));
end;

class (IWindow) CloseDayPanelWindow(panelId)
    Id = panelId;
    Title = "Закрытие операционного дня";
    WindowParams = makeObj(
        "IWindowParams",
        "Width", 350,
        "Height", 150,
        "HelpPage", "",
        "IsModal", true,
        "IsResizing", false
    );
    Appearance = WindowAppearanceType.ExecutePanel;
    SysButtons = makeArray(
        makeObj(
            "ISysButton",
            "Name", PredefinedSysButton.Execute,

//            "Tooltip", "Выполнить закрытие операционного дня",
            "Service", WebServiceCallback("ws_opencloseday", "CloseDay")
        )
    );
    Fields = makeArray(
        makeObj(
            "IPanelItem",
            "Id", Id,
            "Row", 0,
            "Type", PanelItemType.Input,
            "Field", makeObj(
                "IInput",
                "Type", PanelInputFieldType.Date,
                "Label", "Закрыть день ",
                "ReadOnly", true,
                "Enabled", false,
                "TabStop", false,
                "ClearButton", false,
                "Required", false,
                "Source", "OperDate",
                "Design", makeObj(
                    "IDesign",
                    "Font", makeObj("IFont", "Bold", true)
                ),
                "TextWrapping", false,
                "Location", makeObj(
                    "ILocation",
                    "MinWidth", 200,
                    "Width", 250,
                    "StretchHorizontal", false,
                    "StretchVertical", false,
                    "MarginLeft", 0
                )
            )
        )
    );
    Model = makeObj("TDynamicBean", "OperDate", {CurDate});
end;

/* Закрытие опердня */
macro CloseDay()
    var userNumber = CountUser();
    var result: WebServiceResult;
    if (userNumber > 0)
        result = WebServiceResult(
            WebServiceResultType.Message,
            IMessage("Закрытие дня", "Ошибка: Не могу перейти в монопольный режим! Есть " + userNumber + " пользователей, работающие с системой.", MessageBoxType.Error)
        );
    else
        var cmd = TRslOraQuery();
        cmd.SqlText = "UPDATE dates t"
                      "   SET t.ISZ = 1"
                      " WHERE t.DATES = '" + {CurDate} + "'";
        if (not cmd.Execute())
            RunError(ToANSI("Не могу закрыть день! " + cmd.Error));
        end;
        OraTrnCommit();
        result = WebServiceResult(WebServiceResultType.Action, IAction(WebServiceActionType.Close));
//        result = WebServiceResult(WebServiceResultType.Message, IMessage("Закрытие дня", "День закрыт", MessageBoxType.Ok));
    end;
    return result;
end;

// Панель закрытия дня
macro GetCloseDayPanel(params)
    if ({CurDate} == Date(0, 0, 0))
        return WebServiceResult(WebServiceResultType.Message, IMessage("Закрытие дня", "Ошибка: День еще не открыт", MessageBoxType.Error));
    else
        return WebServiceResult(WebServiceResultType.Window, CloseDayPanelWindow(params));
//        return WebServiceResult(WebServiceResultType.Message, IMessage("Закрытие операционного дня", "Закрыть операционный день " + {CurDate} + " ?", MessageBoxType.Question));
    end;
end;
