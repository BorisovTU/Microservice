import rtutils;
import captureOutput;
import likepy;
import ws_general;
import ws_datafilter;


//---------------------------------------------------------------------------
/* описание типов */

// типы возвращаемого значения сервиса
class TWebServiceResultType
    const None = "none";
    const Action = "action";
    const Window = "window";
    const Message = "message";
    const Calendar = "calendar";
    const ReportFile = "reportFile";
end;
const WebServiceResultType = TWebServiceResultType;

/*
результат работы сервиса
возможные комбинации:
TWebServiceResultType.Action => IAction | Array<IAction>
TWebServiceResultType.Window => IWindow
TWebServiceResultType.Message => IMessage
TWebServiceResultType.Calendar => ICalendar
TWebServiceResultType.ReportFile => IReportFile
*/

/*
macro ReportFile(params)
    return WebServiceResult(WebServiceResultType.ReportFile,
        IReportFile(makeArray(
            IReportFileItem(44, "Desert.jpg"),
            IReportFileItem(45, "Hydrangeas.jpg"),
            IReportFileItem(46, "Jellyfish.jpg"),
            IReportFileItem(47, "Koala.jpg"))
        )
    );
end;

macro ShowMainCalendar(params)
    return WebServiceResult(WebServiceResultType.ReportFile,
        IReportFile(makeArray(
            IReportFileItem(44, "Desert.jpg"))
        )
    );
end;
*/

class WebServiceResult(t, v)
    var Type: string = IIF(t != null, t, WebServiceResultType.None);
    var Value: variant = v;
end;

class WebServiceCallback(m, p, prm/*, t*/)
    var MacroName : string = m; // Имя макроса сервиса.
    var ProcName : string = p; // Имя вызываемой процедуры сервиса.
    var Params : string = prm; // Произвольная текстовая строка параметров.
    // var ResultType = IIF(t != null, t, WebServiceResultType.None); // Тип возвращаемого значения WebServiceResultType
end;

// возвращаемое значение сервиса суммирующего по столбцу
class ColumnSumServiceResult
    var TotalSum: money; // сумма по всем записям
    var SelectedItemsSum: money; // сумма по выделенным по записям
end;

// направления перемещения записей
class TMoveDirection
    const Up = "up";
    const Down = "down";
    const Top = "top";
    const Bottom = "bottom";
end;
const MoveDirection = TMoveDirection;

// тип запроса подтверждения пользователя перед запуском сервиса
class TServiceConfirmationType
    const YesNo = "yesNo"; // запрос подтверждения да/нет
    const Comment = "comment"; // запрос комментария
end;
const ServiceConfirmationType = TServiceConfirmationType;

// параметры отображения окна
class IWindowParams
    var Width: integer;
    var Height: integer;
    var HelpPage: string;
    var IsModal: bool;
    var IsResizing: bool;
end;

class TPredefinedSysButton
    const Save = "save";
    const Apply = "apply";
    const Execute = "execute";
    const Cancel = "cancel";

    const Open = "open";
    const Add = "add";
    const Edit = "edit";
    const Delete = "delete";
    const Reorder = "reorder";
    const Calc = "calc";
    const AutoRefresh = "autoRefresh";
    const SelectFile = "selectFile";
end;
const PredefinedSysButton = TPredefinedSysButton;

// параметры стандартных сервисов - это объекты
/* Open
    Params;
    ScrollingId;
    CurrentRow;
    ColumnId;
    PanelModelParams;
*/
/* Add
{
    Params;
    ScrollingId;
    PanelModelParams;
}
*/
/* Edit
{
    Params;
    ScrollingId;
    CurrentRow;
    PanelModelParams;
}
*/
/* Delete
{
    Params;
    ScrollingId;
    CurrentRow;
    SelectedRows;
    PanelModelParams;
}
*/
/* Reorder
{
    Params;
    ScrollingId;
    SelectedRows;
    Direction;
    FilterItems;
    PanelModelParams;
}
*/
/* Calc
{
    Params;
    ScrollingId;
    ColumnId;
    SelectedRows;
    FilterItems;
    PanelModelParams;
}
*/
/* SelectFile
{
    Params;
    ScrollingId;
    FileId;
}
*/
/* GetRows
{
    Params;
    ScrollingId;
    PageSize;
    PageNumber;
    FilterItems;
    OrderItems;
    PanelModelParams;
}
*/
/* GetRowsCount
{
    Params;
    ScrollingId;
    FilterItems;
    PanelModelParams;
}
*/
/* обобщенный сервис
{
    Params;
    ScrollingId;
    CurrentRow;
    SelectedRows;
    Param;
    Comment;
    PanelModelParams;
}
*/
// описание предопределенных кнопок
class ISysButton
    var Name: string; // : PredefinedSysButton
    var Visible: bool;
    var Text: string;
    var Image: string;
    var HotKey: string;
    var Tooltip: string;
    var Service: WebServiceCallback;
end;

// возможные типы элементов тулбара
class TToolbarItemType
    const Separator = "Separator";
    const Button = "Button";
    const ToggleButton = "ToggleButton";
    const SplitButton = "SplitButton";
    const MenuButton = "MenuButton";
    const SelectFileButton = "SelectFileButton";
    const CheckBox = "CheckBox";
end;
const ToolbarItemType = TToolbarItemType;

// возможные типы для пунктов меню у SplitButton и MenuButton
class TWebMenuItemType
    const Separator = "Separator";
    const SimpleItem = "SimpleItem";
    const CheckedItem = "CheckedItem";
end;
const WebMenuItemType = TWebMenuItemType;

// описание одного элемента тулбара
class IToolbarItem
    var Type: string; // ToolbarItemType Тип элемента
    var Text: string; // Текст выводимый на элементе, где это допустимо.
    var Image: string; // Картинка выводимая на элемента, где это допустимо.
    var HotKey: string; // Горячая клавиша.
    var Tooltip: string; // Всплывающая подсказка.
    var MenuItems; // Array of IToolbarItem, // Набор пунктов выпадающего списка. Используется там, где это допустимо (SplitButton и  MenuButton).
    var Service; // WebServiceCallback Вызываемый сервис.
    var ConfirmationType; // ServiceConfirmationType
    // признак проверки наличия выбранных строк скроллинга перед вызовом сервиса (default: false)
    // для системных кнопок edit, delete, reorder, calc всегда true
    var NeedSelectedRows: bool;
end;

// описание свойств шрифта
class IFont
    var FontFamily: string;
    var Bold: bool;
    var Italic: bool;
    var Color: string;
end;

// описание части внешнего вида контрола
class IDesign
    var Font; // IFont
    var Background: string;
end;

// информационные сообщения к полю ввода
class ITips
    var Tooltip: string;
    var DescriptionViewer: string;
    var Watermark: string;
end;

// Ширина поля, начиная с которого поле растягивается
const HORIZONTAL_STRETCH_WIDTH = 100;

// размеры и местоположение поля ввода
class ILocation
    var MinWidth: integer;
    var Width: integer;
    var StretchHorizontal: bool;
    var StretchVertical: bool;
    var MarginLeft: integer;
end;

// типы полей ввода
class TPanelInputFieldType
    const String = "string";
    const Multiline = "multiline";
    const Integer = "integer";
    const Float = "float";
    const Money = "money";
    const Date = "date";
    const Time = "time";
    const Datetime = "datetime";
    const Timestamp = "timestamp";
    const Flag = "flag";
    const Radio = "radio";
    const Richtext = "richtext";
    const Xml = "xml";
end;
const PanelInputFieldType = TPanelInputFieldType;

// типы столбцов листалки
class TListItemType
    const String = "string";
    const Bool = "bool";
end;
const ListItemType = TListItemType;

// столбец листалки
class IListItem
    var ID: string;
    var Type: string = ListItemType.String; // ListItemType
    var Title: string;
    var Tooltip: string;
    var Width: integer = 0;
    var Source: string;
end;

// соответствие поля листалки и поля панели
class IMappingItem
    var ListSource: string;
    var ObjSource: string;
end;

// листалка для поля ввода
class IList
    var ID: string;
    var MultiPage: bool = false;
    var MultiSelect: bool = false;
    var ContextChar: integer = 0;
    var SelectOnly: bool = true;
    var GetRowsService; // WebServiceCallback
    var GetRowCountService; // WebServiceCallback
    var SelProcService; // WebServiceCallback
    var ViewFields; // TArray<IListItem>
    var MappingFields; // TArray<IMappingItem>
    var FilterFields; // не используется TArray<string>
    var PanelFieldsForService; // список имен свойств модели панели для передачи в сервис получения записей и количества записей Array<string>
end;

// описание поля ввода
class IInput
    var Type: string; // PanelInputFieldType
    var Label: string;
    var LabelTop: bool;
    var Tips; // ITips
    var ReadOnly: bool;
    var Enabled: bool;
    var TabStop: bool;
    var ClearButton: bool;
    var Required: bool;
    var MaxLength: integer;
    var Mask: string;
    var Pattern: string;
    var DefaultValue: string;
    var CheckValueService; // WebServiceCallback
    var ValidateOnLostFocus: bool;
    var Source: string;
    var List: IList;
    var Design; // IDesign
    var TextWrapping: bool;
    var LinesCount: integer;
    var Location; // ILocation
end;

// возможные типы элементов группировки
class TGroupItemType
    const Panel = "Panel";
    const TabItem = "TabItem";
    const GroupBox = "GroupBox";
    const GroupBoxLite = "GroupBoxLite";
    const Expander = "Expander";
    /*
        в массиве Fields внурти IGroup для Splitter должны быть два группирующих элемента "Panel"
    */
    const Splitter   = "Splitter";
    const RadioGroup = "RadioGroup";
end;
const GroupItemType = TGroupItemType;

// опции Splitter для IGroup
class ISplitterOptions
    var Vertical: bool; // положение разделителя
end;

// опции Expander для IGroup
class IExpanderOptions
    var Expanded: bool;
end;

// опции Panel для IGroup
class IPanelOptions
    var RowDefinitions; // array of number массив коэфициентов растяжения строк по вертикали (0 - не растягивать)
end;

// описание группирующего элемента
class IGroup
    var Type: string; // GroupItemType
    var Label: string;
    /* объект с опциями для конкретного типа элемента группировки
        Splitter -> ISplitterOptions */
    var Options;
    // массив элементов или массив из массивов элементов для группировки в строки
    var Fields; // Array of IPanelItem or Array of Array<IPanelItem>
end;

// возможжные значения
class TFilterValueType
    const Integer = "integer";
    const Float = "float";
    const Money = "money";
    const String = "string";
    const Date = "date";
    const Time = "time";
    const Boolean = "boolean";
    const UserType = "usertype";
    const CheckList = "checklist";
    const DateTime = "datetime";
end;
const FilterValueType = TFilterValueType;

class TFilterConditionType
    const Equal = "equal";
    const Null = "null";
    const Include = "include";
    const StartWith = "startWith";
    const From = "from";
    const To = "to";
    const Between = "between";
    const CurrentDate = "currentDate";
    const Greater = "greater";
    const GreaterOrEqual = "greaterOrEqual";
    const Less = "less";
    const LessOrEqual = "lessOrEqual";
    const All = "all";
    const Direct = "direct";
    const Hierarchy = "hierarchy";
end;
const FilterConditionType = TFilterConditionType;

// описание провайдера фильтра
class IFilterItem
    var Id: string;
    var Type: string; // FilterValueType
    var Name: string; // название фильтра
    var ColumnId: string;
    var DefaultValue; // Array of values
    var DefaultCondition: string; // FilterConditionType;
    var Params: string;
end;

// типы столбцов скроллинга
class TScrollingColumnType
    const Text = "text";
    const Numeric = "numeric";
    const Money = "money";
    const Flag = "flag";
    const Date = "date";
    const Time = "time";
    const Datetime = "datetime";
    const Hyperlink = "hyperlink";
end;
const ScrollingColumnType = TScrollingColumnType;

// описание столбца скроллинга
class IScrollingColumn
    var Id: string; // уникальный идентификатор столбца скроллинга
    var Type: string; // ScrollingColumnType
    var Title: string;
    var Source: string;
    var Tooltip: string;
    var Width: string;
end;

// описание экземпляра скроллинга
class IScrolling(_id, _InitialRow)
    var Id: string = _id; // уникальный идентификатор скроллинга
    var Columns; // Array of IScrollingColumn
    var Title: string; // название скроллинга для выгрузки в Excel
    var MultiPage: bool;
    var MultiSelect: bool;
    var SysToolbarItems; // Array of ISysButton
    var ToolbarItems; // Array of IToolbarItem
    var FilterItems; // Array of IFilterItem
    var GetRowCountService; // WebServiceCallback
    var GetRowsService; // WebServiceCallback
    var RowKeyFields; // Array<string> имена ключевых полей записи
    var Model; // Array of objects;
    var InitialRow = _InitialRow; // активная строка при открытии (такая же что возвращает GetRowsService)
    var PanelFieldsForService; // список имен свойств модели панели для передачи в сервис получения записей и количества записей Array<string>
end;

// возможные типы элементов панели
class TPanelItemType
    const Group = "Group"; // IPanelItemGroup
    const Input = "Input"; // IPanelItemInput
    const Scrolling = "Scrolling"; // IPanelItemScrolling
end;
const PanelItemType = TPanelItemType;

// описание элемента расположенного на панели
class IPanelItem
    var Id: string;
    var Row: integer;
    var Type: string; // TPanelItemType
    var Field; // соответствующий объект для поля Type
end;

// типы стилизации окна
class TWindowAppearanceType
    const Panel = "panel";
    const Scrolling = "scrolling";
    const ExecutePanel = "executePanel";
    const Custom = "custom";
end;
const WindowAppearanceType = TWindowAppearanceType;

// описание корневого контейнера (окна)
class IWindow
    var Id: string;
    var Title: string;
    var WindowParams; // IWindowParams
    var Appearance: string; // WindowAppearanceType
    var SysButtons; //  Array of ISysButton
    var ToolbarItems; // Array of IToolbarItem
    var Fields; // Array of IPanelItem
    var GetObjectService; // WebServiceCallback
    var GetInitialObjectService; // WebServiceCallback
    var ValidationService; // WebServiceCallback (windowModel, source) => WebServiceResponse
    var Model;
end;

// типы ссобщений
class TMessageBoxType
    const None = "none"; // без изображения
    const Info = "info";
    const Ok = "ok";
    const Warning = "warning";
    const Error = "error";
    const Question = "question";
    const Custom = "custom";
end;
const MessageBoxType = TMessageBoxType;

// типы ссобщений
class TMessageBoxButtonType
    const Ok = "ok";
    const Cancel = "cancel";
    const Yes = "yes";
    const No = "no";
end;
const MessageBoxButtonType = TMessageBoxButtonType;

// результат сервиса для отображения сообщения пользователю
class IMessage(t, m, tp, bt, s)
    var Title: string = t;
    var Message: string = m;
    var Type: string = IIF(tp != null, tp, MessageBoxType.Ok);
    var Buttons = bt; // Array<TMessageBoxButtonType>
    var Service= s; // WebServiceCallback параметры (params, button)
end;

// тип действия выполняемого в результате работы сервиса
class TWebServiceActionType
    const RefreshScrolling = "refreshScrolling"; // Обновляет скроллинг по Id на текущем окне, Id передавать напрямую в Params
    const RefreshGlobalScrolling = "refreshGlobalScrolling"; // Обновляет скроллинг по Id вне зависимости от окна, Id передавать напрямую в Params
    const RefreshParent = "refreshParent";
    const CloseParent = "closeParent";
    const Refresh = "refresh";
    const Close = "close";
    const UpdateModel = "updateModel";
    const UpdateParentModel = "updateParentModel";
    const UpdateWindowDefinition = "updateWindowDefinition"; // Обновляет у текущего окна динамическое определение, IWindow передавать напрямую в Params
    const UpdateParentWindowDefinition = "updateParentWindowDefinition"; // Обновляет у родительского окна динамическое определение, IWindow передавать напрямую в Params
end;
const WebServiceActionType = TWebServiceActionType;

// результат работы сервиса действие
class IAction(a, p)
    var Action: string = a; // WebServiceActionType
    var Params = p;
end;

class ICalendar(t)
    var TypeId: integer = t;
end;

class IReportFileItem(id, t)
    var FileId = id;
    var Title = t;
end;

class IReportFile(items)
    var Files = items; // array of IReportFileItem
end;

class WebServiceParams(id_r, version_r, id_ref, id_scroll, id_vis)
    const d = "#";
    var id_record: string = id_r; // Идентификатор записи
    var version_record: string = version_r; // Версия записи
    var id_refrecord: string = id_ref; // Идентификатор ссылки
    var id_scrolling: string = id_scroll; // Идентификатор скроллинга
    var id_visa: string = id_vis; // Идентификатор визы

    macro ToStr(): string
        return d + id_record + d +
            IIF(version_record == null, "", version_record + d) +
            IIF(id_refrecord == null, "", id_refrecord + d) +
            IIF(id_scrolling == null, "", id_scrolling + d) +
            IIF(id_visa == null, "", id_visa + d);
    end;

    MACRO GetValue (str, tag, nextTag)
      var idx, ret = "", tmpstr, nxt = nextTag, num  = tag;

      idx = index (str, num);
      if (idx)
        tmpstr = substr (str, idx + strlen(num));
        if (valtype (nxt) != V_UNDEF)
         idx = index (tmpstr, nxt);
         if (idx)
          ret = substr (tmpstr, 1, idx - 1);
         else
          ret = tmpstr;
         end;
        else
         ret = tmpstr;
        end;
      end;

      idx = index (str, num + ret);
      if (idx)
        str = Substr (str, 1,  idx - 1) + Substr (str, idx + strlen(num + ret));
        SetParm (1, str);
      else
        str = "";
      end;
      SetParm (1, str);
      return ret;
    END;


    macro FromStr(str)
        id_record = GetValue(str, d, d);
        var vr = GetValue(str, d, d);
        if (vr)
            version_record = vr;
        end;
        var ref = GetValue(str, d, d);
        if (ref)
            id_refrecord = ref;
        end;
        var scr = GetValue(str, d, d);
        if (scr)
            id_scrolling = scr;
        end;
        var vis = GetValue(str, d, d);
        if (vis)
            id_visa = vis;
        end;
    end;
end;

// параметры для общего сервиса скроллинга
// это просто информационный класс, в функции обратных вызовов приходит динамический объект, а не типизированный
class CommonServiceParams
    var ScrollingId: string;
    var Params: string; // поле из соответствующего сервиса
    var CurrentRow; // object
    var SelectedRows; // Array<object>
    var Param; // дополнительный параметр
    var Comment: string; // необязательный комментарий
    var Model; // object
end;

// Возвращает строку для фильтрации
macro GetSqlFilterCondition(filterItems): string
    var filterStr: string = "";
    if ((filterItems != null) and (filterItems.Size > 0))
        var fp = FilterParser(filterItems);
        for (var item, filterItems)
            fp.AddFieldCondition(item.Id, null, item.Params);
        end;
        filterStr = fp.GetFullSQLCondition();
    end;
    return filterStr;
end;

// Возвращает строку для сортировки
macro GetOrderStr(orderItems): string
    var sortStr: string = "";
    if ((orderItems != null) and (orderItems.Size > 0))
        sortStr = " " + orderItems[0].Column + IIF(orderItems[0].Direction == 0, " asc", " desc");
        var i: integer = 1;
        while (i < orderItems.Size)
            sortStr = sortStr + ", " + orderItems[i].Column + IIF(orderItems[i].Direction == 0, " asc", " desc");
            i = i + 1;
        end;
        sortStr = sortStr + " ";
    end;
    return sortStr;
end;

// Описание скроллинга
class (IScrolling) Scrolling(_id, recordId, scrollingName)
    InitIScrolling(_id);
    var FormId: integer;

    private macro SetColumns(id_smenu)
        var cmd = TRslOraQuery();
        CaptureOutput();
[
SELECT DECODE (decode(reffield, null, typefield, typereffield),
               1, /*строка*/               'text', /*строка*/
               2, /*число: длинное целое*/ 'numeric', /*число*/
               3, /*число: короткое целое*/'numeric', /*число*/
               7, /*дробное число*/        'numeric', /*число*/
               4, /*сумма*/                'money', /*деньги*/
               8, /*булево*/               'flag', /*флаг*/
               5, /*дата*/                 'date', /*дата*/
               6, /*время*/                'time', /*время*/
               9, /*ДатаВремя*/            'datetime', /*дата и время*/
               10,/*ТаймШтамп*/            'datetime', /*дата и время*/
               11,/*текстовые данные*/     'text', /*строка*/
               12,/*бинарные данные*/      'text', /*строка*/
               13,/*xml данные*/           'text', /*строка*/
               typefield)
       ColumnType,
       DECODE (decode(reffield, null, typefield, typereffield),
               1, /*строка*/               'string', /*строка*/
               2, /*число: длинное целое*/ 'integer', /*число*/
               3, /*число: короткое целое*/'integer', /*число*/
               7, /*дробное число*/        'float', /*число*/
               4, /*сумма*/                'money', /*деньги*/
               8, /*булево*/               'boolean', /*флаг*/
               5, /*дата*/                 'date', /*дата*/
               6, /*время*/                'time', /*время*/
               9, /*ДатаВремя*/            'datetime', /*дата и время*/
               10,/*ТаймШтамп*/            'datetime', /*дата и время*/
               11,/*текстовые данные*/     'string', /*строка*/
               12,/*бинарные данные*/      'string', /*строка*/
               13,/*xml данные*/           'string', /*строка*/
               typefield)
       FilterType,
       t.text Title,
];
        if (
            (id_smenu == TypeMainMenuAllDoc) or
            (id_smenu == TypeMainMenuState) or
            (id_smenu == TypeMainMenuVisa) or
            (id_smenu == TypeMainMenuPosit) or
            (id_smenu == TypeMenuFormDep) or
            (id_smenu == TypeMenuFormRef)
        )
[
       DECODE(t.aliastable, NULL, t.tbname, t.aliastable) tbname,
       DECODE(t.aliasfield, NULL, t.name, t.aliasfield) Source,
       t.id_nfield ID
  FROM (select a.*, b.*,
               (select RSP_FORM.ConvertTypeOracle(u.DATA_TYPE, u.DATA_PRECISION, u.DATA_SCALE)
                  from user_tab_columns u
                 where u.TABLE_NAME = b.lkptable_name
                   and u.COLUMN_NAME = a.reffield) as typereffield
          from (select t.*, tb.name as tbname, tf.code AS typefield
                  from nfield t,
                       ntable tb,
                       stypefield tf
                 where t.id_nform = :id_nform
                   and t.ISGRID > 0
                   and t.id_ntable = tb.id_ntable
                   and t.id_stypefield = tf.id_stypefield
                 order by t.orderfield) a,
               (select cons.constraint_type, col.table_name as tb, col.column_name as cl,
                       col1.table_name as lkptable_name, col1.column_name as lkpcolumn_name
                  from user_cons_columns col, user_constraints cons, user_cons_columns col1
                 where cons.owner = col.owner
                   and cons.constraint_name = col.constraint_name
                   and cons.constraint_type = 'R'
                   and cons.r_constraint_name = col1.constraint_name
                   and cons.r_owner = col1.owner
                   and col.table_name in (select upper(t.name)
                                            from ntable t, nform f
                                           where t.id_ntable = f.id_ntable
                                      start with f.id_nform = :id_nform
                                connect by prior f.id_nform_parent = f.id_nform)) b
         where a.tbname= b.tb(+)
           and a.Name = cl(+)) t
];
        elif ((id_smenu == TypeMainMenuDict) or (id_smenu == TypeMenuDictRef))
[
       t.tbname tbname,
       t.name||CASE WHEN t.islkp = 1
                    THEN '8'||(select ld.name from field ld where ld.id_field = reffield)
               END Source,
       t.id_field ID
  FROM (SELECT fl.*, tb.name AS tbname, tf.code AS typefield,
               lkptypefield AS typereffield, l.id_field_child as reffield
          FROM field fl, ntable tb, stypefield tf, form f,
               (select typf.code lkptypefield, lk.id_field, lf.id_field_child
                  from lkp lk, lkpfl lf, field fl, stypefield typf
                 where lk.id_lkp = lf.id_lkp
                   and lf.id_field_child = fl.id_field
                   and fl.id_stypefield = typf.id_stypefield) l
         WHERE f.id_form = :id_nform
           AND fl.id_form = f.id_form
           AND f.id_ntable = tb.id_ntable
           AND fl.id_field = l.id_field(+)
           AND fl.ISGRID > 0
           AND fl.id_stypefield = tf.id_stypefield
      ORDER BY fl.orderfield) t
];
        else
            RunError(ToANSI("Недопустимый тип меню"));
        end;
        cmd.SqlText = StopCaptureOutput();
        if ((id_smenu == TypeMainMenuDict) or (id_smenu == TypeMenuDictRef))
            cmd.DeclareAndSet("id_nform", FormId);
        else
            cmd.DeclareAndSet("id_nform", FormId, "id_nform", FormId);
        end;
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        Columns = TArray();
        FilterItems = TArray();
        if (id_smenu == TypeMainMenuVisa)
            Columns[Columns.Size] = makeObj(
                "IScrollingColumn",
                "Id", "ID_NVTYP",
                "Type", ScrollingColumnType.Numeric,
                "Title", "№ визы",
                "Source", "ID_NVTYP",
                "Tooltip", "№ визы"
            );
            FilterItems[FilterItems.Size] = makeObj(
                "IFilterItem",
                "Id", "9000001",
                "Type", FilterValueType.Integer,
                "Name", "№ визы",
                "ColumnId", "ID_NVTYP",
                "Params", "ID_NVTYP"
            );
            Columns[Columns.Size] = makeObj(
                "IScrollingColumn",
                "Id", "TEXT",
                "Type", ScrollingColumnType.Text,
                "Title", "Виза",
                "Source", "TEXT",
                "Tooltip", "Наименование визы"
            );
            FilterItems[FilterItems.Size] = makeObj(
                "IFilterItem",
                "Id", "9000002",
                "Type", FilterValueType.String,
                "Name", "Наименование визы",
                "ColumnId", "TEXT",
                "Params", "TEXT"
            );
        end;
        while (not cmd.Eof)
            Columns[Columns.Size] = makeObj(
                "IScrollingColumn",
                "Id", cmd.Rec.TBNAME + "8" + cmd.Rec.SOURCE,
                "Type", cmd.Rec.ColumnType,
                "Title", cmd.Rec.TITLE,
                "Source", cmd.Rec.TBNAME + "8" + cmd.Rec.SOURCE,
                "Tooltip", cmd.Rec.TITLE
            );
            FilterItems[FilterItems.Size] = makeObj(
                "IFilterItem",
                "Id", cmd.GetByName("ID").AsInteger,
                "Type", cmd.Rec.FilterType,
                "Name", cmd.Rec.TITLE,
                "ColumnId", cmd.Rec.TBNAME + "8" + cmd.Rec.SOURCE,
                "Params", cmd.Rec.TBNAME + "8" + cmd.Rec.SOURCE
            );
            cmd.Next();
        end;
    end;

    private macro GetMenuItem(cmd): IToolbarItem
        var item = makeObj(
            "IToolbarItem",
            "Type", ToolbarItemType.Button,
            "Text", cmd.GetByName("Text").AsString,
            "Image", cmd.GetByName("Icon").AsString,
            "HotKey", cmd.GetByName("HotKey").AsString,
            "Tooltip", cmd.GetByName("ToolTip").AsString
        );
        // все документы или документы в состоянии
        if ((cmd.Rec.IDCodeMenu == TypeMainMenuAllDoc) or (cmd.Rec.IDCodeMenu == TypeMainMenuState) or (cmd.Rec.IDCodeMenu == TypeMainMenuVisa))
            item.Service = WebServiceCallback("ws_window", "GetScrolling", cmd.GetByName("ID_NMENU").AsString);
            item.NeedSelectedRows= true;

        // подчиненные или связанные формы или справочник
        elif ((cmd.Rec.IDCodeMenu == TypeMenuFormDep) or (cmd.Rec.IDCodeMenu == TypeMenuFormRef) or (cmd.Rec.IDCodeMenu == TypeMainMenuDict) or (cmd.Rec.IDCodeMenu == TypeMenuDictRef))
            item.Service = WebServiceCallback("ws_scrolling", "GetScrollingToolbar", cmd.GetByName("ID_NMENU").AsString);
            item.NeedSelectedRows= true;
        elif (cmd.Rec.IDCodeMenu == TypeMenuFormMain)
            item.Service = WebServiceCallback("ws_panel", "OpenMainFormScrolling", cmd.GetByName("ID_NMENU").AsString);
            item.NeedSelectedRows= true;
        else
            var wParams = makeObj("TDynamicBean", "IDNMENU", cmd.GetByName("ID_NMENU").AsString);
            item.Service = WebServiceCallback(cmd.GetByName("MacroName").AsString, cmd.GetByName("ProcName").AsString, Rsl2Xml(wParams));
        end;
        return item;
    end;

    private macro GetToolbarItem(menuItem: IToolbarItem): IToolbarItem
        return makeObj(
            "IToolbarItem",
            "Type", ToolbarItemType.Button,
//            "Text", menuItem.Text,
            "Image", menuItem.Image,
            "HotKey", menuItem.HotKey,
            "Tooltip", menuItem.ToolTip,
            "Service", menuItem.Service,
            "NeedSelectedRows", menuItem.NeedSelectedRows
        );
    end;

    private macro SetToolBarItems(id_menuowner, typemenu)
debugbreak;
        var cmd = TRslOraQuery();
        CaptureOutput();
[
  SELECT t.ID_NMENU,
         t.MAINNAME menu,
         t.MENUNAME Text,
         RSP_ENCODE.BLOB_TO_HEX(t.ICON_PNG) Icon,
         (select s.text
            from skey s
           where s.id_skey = t.id_skey) HotKey,
         t.HINT Tooltip,
         t.MACROFILEWEB MacroName,
         t.MACROPROCWEB ProcName,
         t.id_smenu IDCodeMenu,
         t.IsToolbar,
         t.ButtonText
    FROM TABLE(RSP_FORM.Get_MenuForm_By_User(P_TYPEMENU     => :typemenu,
                                             P_IDNFORM      => :IDNFORM,
                                             P_IDNMENUOWNER => :IDNMENUOWNER)) t
ORDER BY NUM
];
        cmd.SqlText = StopCaptureOutput();
        cmd.DeclareAndSet("typemenu", typemenu);
        cmd.DeclareAndSet("IDNFORM", FormId);
        cmd.DeclareAndSet("IDNMENUOWNER", id_menuowner);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        var groupName: string = "";
        var toolbarItem, menuItem;
        var toolbarButtons = TArray();
        while (not cmd.Eof)
            if (cmd.GetByName("menu").AsString != groupName)
                if (groupName != "")
                    ToolbarItems[ToolbarItems.Size] = toolbarItem;
                end;
                groupName = cmd.GetByName("menu").AsString;
                toolbarItem = makeObj(
                    "IToolbarItem",
                    "Type", ToolbarItemType.MenuButton,
                    "Text", cmd.GetByName("menu").AsString,
                    "MenuItems", TArray()
                );
            end;
            menuItem = GetMenuItem(cmd);
            if (cmd.Rec.IsToolbar)
//                var buttonItem = makeObj()menuItem;
//                buttonItem.Text = cmd.GetByName("ButtonText").AsString;
                toolbarButtons[toolbarButtons.Size] = GetToolbarItem(menuItem);
            end;
            toolbarItem.MenuItems[toolbarItem.MenuItems.Size] = menuItem;
            cmd.Next();
        end;
        if (groupName != "")
            ToolbarItems[ToolbarItems.Size] = toolbarItem;
        end;
        for (var toolbarButton, toolbarButtons)
            ToolbarItems[ToolbarItems.Size] = toolbarButton;
        end;
    end;

    private macro SetKeyFields()
        var tableName: string = "";
        var PKField: string = "";
        var cmd = TRslOraQuery();
        cmd.SqlText = "begin RSP_FORM.GET_TABLE_PK_FORMMENU(:IDNMenu, :Table, :PK); end;";
        cmd.DeclareAndSet("IDNMenu", Id, "Table", tableName, "PK", PKField);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        tableName = cmd.GetVariable("Table");
        PKField = cmd.GetVariable("PK");
        RowKeyFields = makeArray(tableName + "8" + PKField);
    end;

    private macro Init(recordId, scrollingName)
        var wParams = WebServiceParams(recordId, 0, 0, Id);
        var cmd = TRslOraQuery();
        CaptureOutput();
[
SELECT RSP_FORM.GET_RIGHT_BY_USER(t.id_sright, null) usrright,
       RSP_FORM.GET_RIGHT_BY_USER(t.id_sright_i, null) usrrighti,
       RSP_FORM.GET_RIGHT_BY_USER(t.id_sright_d, null) usrrightd,
       n.*, t.id_sstatenext, t.id_sstateback,
       t1.textnext, t2.textback,
       t.macroprocbackweb, t.macrofilebackweb, t.macrofilenextweb, t.macroprocnextweb,
       decode(t1.textnext, null, null,
                                 (select RSP_ENCODE.BLOB_TO_HEX(ICON_PNG)
                                    from sicon
                                   where code = 1)) IconNext,
       decode(t2.textback, null, null,
                                 (select RSP_ENCODE.BLOB_TO_HEX(ICON_PNG)
                                    from sicon
                                   where code = 2)) IconBack,
       decode(t.isedit, 0, 0, 1) isedit, t.isuserorder, t.timerefresh,
       (SELECT RSP_ENCODE.BLOB_TO_HEX(t.ICON_PNG) Icon FROM sicon t WHERE t.text = 'Print') ICONPRINT
  FROM NMENU n, NMENU_PARAMLIST t,
       (select p.id_nmenu_paramlist,
               decode(p.textnext, null, 'Провести',
                                        p.textnext) textnext
          from nmenu_paramlist p
         where nvl(p.id_sstatenext, 0) <> 0) t1,
       (select p.id_nmenu_paramlist,
               decode(p.textback, null, 'Отказать',
                                        p.textback) textback
          from nmenu_paramlist p
         where nvl(p.id_sstateback, 0) <> 0) t2
 WHERE n.id_nmenu = t.id_nmenu(+)
   AND t.id_nmenu_paramlist = t1.id_nmenu_paramlist(+)
   AND t.id_nmenu_paramlist = t2.id_nmenu_paramlist(+)
   AND n.ID_NMENU = :ID_NMENU
];
        cmd.SqlText = StopCaptureOutput();
        cmd.DeclareAndSet("ID_NMENU", Id);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        Title = cmd.GetByName("menuname").AsString;
        MultiPage = true;
        MultiSelect = true;
        SysToolbarItems = TArray();
        ToolbarItems = TArray();
        var id_formspr = cmd.GetByName("id_formspr").AsInteger;
        var timerefresh = cmd.GetByName("timerefresh").AsInteger;
        var id_smenu = cmd.GetByName("id_smenu").AsInteger;
        var id_menuowner = cmd.GetByName("id_nmenu").AsInteger;
        if (id_formspr != 0) // Для формы справочника
            FormId = id_formspr;
            cmd.SqlText = "SELECT RSP_FORM.GET_RIGHT_BY_USER(t.id_sright_read, null) usrrightr,"
                          "       RSP_FORM.GET_RIGHT_BY_USER(t.id_sright_edit, null) usrrighte,"
                          "       n.*"
                          "  FROM NMENU n, FORM t"
                          " WHERE n.id_formspr = t.id_form AND n.ID_NMENU = :ID_NMENU";
            cmd.DeclareAndSet("ID_NMENU", Id);
            if (not cmd.Execute())
                RunError(ToANSI(cmd.Error));
            end;
            if (cmd.Rec.usrrighte != 0) // Если разрешено редактирование
                SysToolbarItems[SysToolbarItems.Size] = makeObj(
                    "ISysButton",
                    "Name", PredefinedSysButton.Add,
                    "Service", WebServiceCallback("ws_scrolling", "AddAction", IIF(id_smenu = TypeMenuDictRef, recordId))
                );
            end;
            if (cmd.Rec.usrrightr != 0) // Если разрешено чтение
                SysToolbarItems[SysToolbarItems.Size] = makeObj(
                    "ISysButton",
                    "Name", PredefinedSysButton.Edit,
                    "Service", WebServiceCallback("ws_scrolling", "EditAction")
                );
            end;
            if (cmd.Rec.usrrighte != 0) // Если разрешено редактирование
                SysToolbarItems[SysToolbarItems.Size] = makeObj(
                    "ISysButton",
                    "Name", PredefinedSysButton.Delete,
                    "Service", WebServiceCallback("ws_scrolling", "DeleteAction")
                );
            end;
            SysToolbarItems[SysToolbarItems.Size] = makeObj(
                 "ISysButton",
                 "Name", PredefinedSysButton.Calc,
                 "Service", WebServiceCallback("ws_scrolling", "CalculateSum")
            );
            if (timerefresh != 0)
                SysToolbarItems[SysToolbarItems.Size] = makeObj(
                     "ISysButton",
                     "Name", PredefinedSysButton.AutoRefresh
                );
            end;
        else // Для формы документа
            FormId = cmd.GetByName("id_nformdn").AsInteger;
            if ((cmd.Rec.isedit != 0) and (cmd.Rec.usrrighti != 0)) // Если разрешено редактирование И задано полномочие ввода новой записи И заданное полномочие "подходит" мне
                SysToolbarItems[SysToolbarItems.Size] = makeObj(
                    "ISysButton",
                    "Name", PredefinedSysButton.Add,
                    "Service", WebServiceCallback("ws_scrolling", "AddAction", recordId)
                );
            end;
            SysToolbarItems[SysToolbarItems.Size] = makeObj(
                "ISysButton",
                "Name", PredefinedSysButton.Edit,
                "Service", IIF(
                    id_smenu == TypeMainMenuPosit,
                    WebServiceCallback("ws_positioning", "PositioningEditAction", wParams.ToStr()),
                    WebServiceCallback("ws_scrolling", "EditAction")
                )
            );
            if ((cmd.Rec.isedit == 1) and (cmd.Rec.usrrightd != 0)) // Если разрешено редактирование И задано полномочие удаления записи И заданное полномочие "подходит" мне
                SysToolbarItems[SysToolbarItems.Size] = makeObj(
                    "ISysButton",
                    "Name", PredefinedSysButton.Delete,
                    "Service", WebServiceCallback("ws_scrolling", "DeleteAction")
                );
            end;
            if (cmd.Rec.isuserorder == 1) // Используется пользовательская сортировка
                SysToolbarItems[SysToolbarItems.Size] = makeObj(
                    "ISysButton",
                    "Name", PredefinedSysButton.Reorder,
                    "Service", WebServiceCallback("ws_scrolling", "ReorderRows")
                );
            end;
            SysToolbarItems[SysToolbarItems.Size] = makeObj(
                "ISysButton",
                "Name", PredefinedSysButton.Calc,
                "Service", IIF(
                    id_smenu == TypeMainMenuPosit,
                    WebServiceCallback("ws_positioning", "PositioningCalculateSum", wParams.ToStr()),
                    WebServiceCallback("ws_scrolling", "CalculateSum")
                )
            );
            if (timerefresh != 0)
                SysToolbarItems[SysToolbarItems.Size] = makeObj(
                     "ISysButton",
                     "Name", PredefinedSysButton.AutoRefresh
                );
            end;
            ToolbarItems[ToolbarItems.Size] = makeObj(
                "IToolbarItem",
                "Type", ToolbarItemType.Button,
                "Tooltip", "Печать",
                "Text", "",
                "Image", cmd.GetByName("IconPrint").AsString,
                "Service", WebServiceCallback("ws_print", "printList"),
                "NeedSelectedRows", true

            );
            if ((id_smenu == TypeMainMenuState) or (id_smenu == TypeMainMenuVisa))
                if (cmd.GetByName("id_sstateback").AsInteger != 0)
                    ToolbarItems[ToolbarItems.Size] = makeObj(
                        "IToolbarItem",
                        "Type", ToolbarItemType.Button,
                        "Text", cmd.GetByName("TextBack").AsString,
                        "Image", cmd.GetByName("IconBack").AsString,
                        "Service", IIF(
                            id_smenu == TypeMainMenuVisa,
                            WebServiceCallback("ws_stepvisa", "VisaNO", cmd.GetByName("id_sstateback").AsInteger),
                            WebServiceCallback("ws_step", "StepBackWeb", cmd.GetByName("id_sstateback").AsInteger)
                        ),
                        "ConfirmationType", ServiceConfirmationType.Comment,
                        "NeedSelectedRows", true
                    );
                end;
                if (cmd.GetByName("id_sstatenext").AsInteger != 0)
                    ToolbarItems[ToolbarItems.Size] = makeObj(
                        "IToolbarItem",
                        "Type", ToolbarItemType.Button,
                        "Text", cmd.GetByName("TextNext").AsString,
                        "Image", cmd.GetByName("IconNext").AsString,
                        "Service", IIF(
                            id_smenu == TypeMainMenuVisa,
                            WebServiceCallback("ws_stepvisa", "VisaOK", cmd.GetByName("id_sstatenext").AsInteger),
                            WebServiceCallback("ws_step", "StepNextWeb", cmd.GetByName("id_sstatenext").AsInteger)
                        ),
                        "ConfirmationType", ServiceConfirmationType.YesNo,
                        "NeedSelectedRows", true
                    );
                end;
            end;
            if (id_smenu == TypeMainMenuPosit)
                ToolbarItems[ToolbarItems.Size] = makeObj(
                    "IToolbarItem",
                    "Type", ToolbarItemType.Button,
                    "Text", IIF(scrollingName == LeftPositioningSrollingName, cmd.GetByName("TextBack").AsString, "Вернуть"),
                    "Image", cmd.GetByName("IconBack").AsString,
                    "Service", IIF(
                        scrollingName == LeftPositioningSrollingName,
                        WebServiceCallback("ws_step", "StepBackWeb", Rsl2Xml(makeObj("TDynamicBean", "ScrollingId", Id, "State", cmd.GetByName("id_sstateback").AsInteger))),
                        WebServiceCallback("ws_positioning", "MoveLeft")
                    ),
                    "ConfirmationType", IIF(scrollingName == LeftPositioningSrollingName, ServiceConfirmationType.Comment, null),
                    "NeedSelectedRows", true

                );
                ToolbarItems[ToolbarItems.Size] = makeObj(
                    "IToolbarItem",
                    "Type", ToolbarItemType.Button,
                    "Text", IIF(scrollingName == LeftPositioningSrollingName, "Перенести", cmd.GetByName("TextNext").AsString),
                    "Image", cmd.GetByName("IconNext").AsString,
                    "Service", IIF(
                        scrollingName == LeftPositioningSrollingName,
                        WebServiceCallback("ws_positioning", "MoveRight"),
                        WebServiceCallback("ws_step", "StepNextWeb", Rsl2Xml(makeObj("TDynamicBean", "ScrollingId", Id, "State", cmd.GetByName("id_sstatenext").AsInteger)))
                    ),
                    "ConfirmationType", IIF(scrollingName == RightPositioningSrollingName, ServiceConfirmationType.YesNo, null),
                    "NeedSelectedRows", true

                );
            end;
        end;
        SetColumns(id_smenu);
        if (id_formspr != 0) // Для связанного справочника
            SetToolBarItems(0, 5);
        else
            SetToolBarItems(id_menuowner, 1);
        end;
        GetRowsService = IIF(
            id_smenu == TypeMainMenuPosit,
            WebServiceCallback("ws_positioning", "GetPositioningRows", wParams.ToStr()),
            WebServiceCallback("ws_scrolling", "GetRowsService", wParams.ToStr())
        );
        GetRowCountService = IIF(
            id_smenu == TypeMainMenuPosit,
            WebServiceCallback("ws_positioning", "GetPositioningRowCount", wParams.ToStr()),
            WebServiceCallback("ws_scrolling", "GetRowCountService", wParams.ToStr())
        );
        SetKeyFields();
        if ((scrollingName == LeftPositioningSrollingName) or (scrollingName == RightPositioningSrollingName))
            PanelFieldsForService = makeArray("LeftCurrencyId", "RightCurrencyId", "CorrespondentSchemeId", "Correspondent", "Node");
        end;
    end;

    Init(recordId, scrollingName);
    if ((scrollingName == LeftPositioningSrollingName) or (scrollingName == RightPositioningSrollingName))
        Id = scrollingName;
    end;
end;


class (IWindow) ScrollingWindow(menuId, recordId)
    private macro GetTitle(): string
        var cmd = TRslOraQuery();
        CaptureOutput();
[
SELECT nvl(t.namelist, n.menuname) menuname
  FROM NMENU n,
       NMENU_PARAMLIST t
 WHERE n.id_nmenu = t.id_nmenu(+)
   AND n.ID_NMENU = :ID_NMENU
];
        cmd.SqlText = StopCaptureOutput();
        cmd.DeclareAndSet("ID_NMENU", Id);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        return cmd.GetByName("menuname").AsString;
    end;

    Id = menuId;
    Title = GetTitle();
    WindowParams = makeObj(
        "IWindowParams",
        "Width", 800,
        "Height", 480,
        "HelpPage", "",
        "IsModal", false,
        "IsResizing", true
    );
    Appearance = WindowAppearanceType.Scrolling;
    Fields = makeArray(
        makeObj(
            "IPanelItem",
            "Id", Id,
            "Row", 0,
            "Type", PanelItemType.Scrolling,
            "Field", Scrolling(Id, recordId)
        )
    );
end;
