import general, ws_types;

macro ChgStateStep(TmpTblName)
/*Меняет состояние платежам, используя временную таблицу с заполненными состояниями
TmpTblName - имя таблицы с платежами
Возвращает количество обработанных строк */

  var ret;
  var ChangeSt  = TRslOraQuery();

  ChangeSt.SqlText = " begin  :ret := RSP_GATE.MASSCHANGESTATE(:Tmp_Tbl); end;";
  ChangeSt.DeclareAndSet("ret",0,"Tmp_Tbl",TmpTblName);

  if(not ChangeSt.Execute)
    runerror(toansi("процедура RSP_GATE.MASSCHANGESTATE:\r\n"+ChangeSt.Error));
  end;

  ret = ChangeSt.GetVariable("ret");

  return ret;

end;

private macro GetMacroProc(NextStep)

  CaptureOutput();
  if(NextStep)
[SELECT mp.macrofilenextweb as macrofile, mp.macroprocnextweb as macroproc FROM nmenu_paramlist mp WHERE mp.id_nmenu = :scrollingId AND mp.macroprocnextweb IS NOT NULL];
  else
[SELECT mp.macrofilebackweb as macrofile, mp.macroprocbackweb as macroproc FROM nmenu_paramlist mp WHERE mp.id_nmenu = :scrollingId AND mp.macroprocbackweb IS NOT NULL];
  end;

  return StopCaptureOutput();
end;

macro Ws_Step(state, currentItem, items, comment, scrollingId, NextStep)
    if (currentItem == null) // Пустой список
        return;
    end;
    var recordId = 0, recordVersion = 0, varlen = "", i = 0, item;
    var GetMacro = TRslOraQuery();
    var MacroFile, MacroProc;
    var payObj;
    var result;
    var resProv;
    var formPay;

    // Получение причины
    if (comment)
        varlen = comment;
    end;

    GetMacro.SqlText = GetMacroProc(NextStep);
    GetMacro.DeclareAndSet("scrollingId", scrollingId);

    if(not GetMacro.Execute)
      runerror(toansi("Макропроцедура GetMacroProc:\r\n"+GetMacro.Error));
    end;

    if(not GetMacro.Eof)
      MacroFile = GetMacro.rec.macrofile;
      MacroProc = GetMacro.rec.macroproc;

      if(items and items.Size)
        for(item, items)
          payObj = TRslPayDoc (item.ID_NFORM, item.PAYM8ID_PAYM);
          payObj.rec.PAYM8ID_SSTATE = state;

          if(NextStep)
            if(Not StepNext(payObj, MacroFile, MacroProc))
              resProv = WebServiceResult(WebServiceResultType.Message, IMessage("Внимание!", payObj.Error, MessageBoxType.Error));
              break;
            end;
          end;

          i = i + 1;
        end;
      else
        if(items)
          formPay = currentItem.ID_NFORM;
        else
          formPay = currentItem.PAYM8ID_NFORM;
        end;

        payObj = TRslPayDoc (formPay, currentItem.PAYM8ID_PAYM);
        payObj.rec.PAYM8ID_SSTATE = state;

        if(NextStep)
          if(Not StepNext(payObj, MacroFile, MacroProc))
            resProv = WebServiceResult(WebServiceResultType.Message, IMessage("Внимание!", payObj.Error, MessageBoxType.Error));
          end;
        else
          if(Not StepBack(payObj, comment, MacroFile, MacroProc))
            resProv = WebServiceResult(WebServiceResultType.Message, IMessage("Внимание!", payObj.Error, MessageBoxType.Error));
          end;
        end;
      end;

      if(items)
        result = makeArray(WebServiceResult(WebServiceResultType.Action, IAction(WebServiceActionType.Refresh)),
                           resProv);
      else
        result = makeArray(WebServiceResult(WebServiceResultType.Action, makeArray(IAction(WebServiceActionType.Close), IAction(WebServiceActionType.RefreshParent))),
                           resProv);
      end;
    else
      var cmd = TRslOraQuery();
      cmd.SqlText = "INSERT INTO TMP_PAYM_PACK (id_paym, id_sstate, varlen, vr) VALUES (:IDRec, :State, :VARLEN, :VR)";
      if (items and items.Size)
            for (item, items)
              // Получение ИД записи
              recordId = item.PAYM8ID_PAYM;
              // Получение версии записи
              recordVersion = item.VersionRec;
              cmd.DeclareAndSet("IDRec", recordId, "State", state, "VARLEN", varlen, "VR", recordVersion);
              if (not cmd.Execute())
                  RunError(ToANSI(cmd.Error));
              end;
              i = i + 1;
          end;
      else // Нет выделенных, есть только текущая
          // Получение ИД записи
          recordId = currentItem.PAYM8ID_PAYM;
          // Получение версии записи
            if(items)
              recordVersion = currentItem.VersionRec;
          else
              recordVersion = currentItem.PAYM8VERSIONREC;
          end;
          cmd.DeclareAndSet("IDRec", recordId, "State", state, "VARLEN", varlen, "VR", recordVersion);
          if (not cmd.Execute())
              RunError(ToANSI(cmd.Error));
          end;
          i = 1;
      end;

      // Смена состояния
      if (ChgStateStep("TMP_PAYM_PACK") /*== i*/)
        OraTrnCommit();

        if (items)
            result = WebServiceResult(WebServiceResultType.Action, IAction(WebServiceActionType.Refresh));
        else
            result = WebServiceResult(WebServiceResultType.Action, makeArray(IAction(WebServiceActionType.Close), IAction(WebServiceActionType.RefreshParent)));
        end;
      else
        OraTrnRollBack();
        result = WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Не удалось сменить состояние платежу: запись конкурентно изменена", MessageBoxType.Error));
      end;
    end;

    return result;
end;

/* Сервис "Шаг назад" */
macro StepBackWeb(params)
    var scrollingId, currentItem, items, stateStep, wParams;
    if (params.PropIndex("WindowId") != -1)
        currentItem = params.Model;
        wParams = Xml2Rsl(params.Params);
        scrollingId = wParams.ScrollingId;
        stateStep = wParams.State;
    elif (params.ScrollingId == LeftPositioningSrollingName)
        currentItem = params.CurrentRow;
        items = params.SelectedRows;
        wParams = Xml2Rsl(params.Params);
        scrollingId = wParams.ScrollingId;
        stateStep = wParams.State;
    else
        currentItem = params.CurrentRow;
        items = params.SelectedRows;
        scrollingId = params.ScrollingId;
        stateStep = params.Params;
    end;
    return Ws_Step(stateStep, currentItem, items, params.Comment, scrollingId, false);
end;

/* Сервис "Шаг вперед" */
macro StepNextWeb(params)
    var scrollingId, currentItem, items, stateStep, wParams;
    if (params.PropIndex("WindowId") != -1)
        currentItem = params.Model;
        wParams = Xml2Rsl(params.Params);
        scrollingId = wParams.ScrollingId;
        stateStep = wParams.State;
    elif (params.ScrollingId == RightPositioningSrollingName)
        currentItem = params.CurrentRow;
        items = params.SelectedRows;
        wParams = Xml2Rsl(params.Params);
        scrollingId = wParams.ScrollingId;
        stateStep = wParams.State;
    else
        currentItem = params.CurrentRow;
        items = params.SelectedRows;
        scrollingId = params.ScrollingId;
        stateStep = params.Params;
    end;
    return Ws_Step(stateStep, currentItem, items, "", scrollingId, true);
end;
