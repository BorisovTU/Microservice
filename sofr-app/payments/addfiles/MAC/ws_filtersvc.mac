 /*
 $Name: ws_filtersvc.mac
 $Module: WEB
 $Description: Сервисы для работы с фильтрами
 */

import tarrayext;
import Utils;
import ws_filter;


macro TrimErrText(e)
    return StrSubst(e, "Error: Ошибка выполнения", "");
end;

private const WEB_FILTER_SYSTEM = 0; // системный (дистрибутивный) = 0
private const WEB_FILTER_SHARED = 1; // общий (по подразделениям ТС) = 1
private const WEB_FILTER_PRIVATE = 2; // личный (по пользователям) = 2

private const WEB_FILTER_FLAG_INHERITED = 1;
private const WEB_FILTER_FLAG_CHILD = 2;

private const WEB_FILTER_ERROR_ALREADY_EXISTS = -2;
private const WEB_FILTER_SQL_CONCURRENT_CHANGES = 20100;

private class UsrError(err)
    var ErrCode = err;
end;

class ResultInfo(code, text, id)
    var ResultCode: integer = code;
    var ErrorText: string = text;
    var FilterId: integer = id;
    var Filter;
end;

class TFilterIdNamePair(p1, p2)
    var Id = p1;
    var Name = p2;
end;

// OperID Номер пользователя (persn.Oper), для которого применяется фильтр. Указывается, если тип фильтра Type. = <личный>.
// Для остальных = 0.

/*
macro IsFilterAdministratorUser()
    return not (({cTypePerson} == 3 /*TP_OPER*/) or ({cTypePerson} == 6/*TP_AUDITOR*/));
end;
*/


private macro iif(cond, iftrue, iffalse)
    if(cond == true)
        return(iftrue);
    end;
    return(iffalse);
end;




macro FindFilter(filterId: integer)
    if (filterId <= 0)
        RunError(ToANSI("FilterId должен быть > 0"));
    end;
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT ID_WFILTER, PANEL, TYPE, USERNICK, FLTNAME, VERSIONREC FROM WFILTER WHERE ID_WFILTER = :FilterId";
    cmd.DeclareAndSet("FilterId", filterId);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    if (not cmd.Eof)
        return cmd;
    end;
end;

macro SelectFiltersByPanel(panel: string)
    if (panel == "")
        RunError(ToANSI("Имя панели не может быть пустым"));
    end;
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT * FROM WFILTER WHERE PANEL = :panel";
    cmd.DeclareAndSet("panel", panel);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    return cmd;
end;

macro ClearDefaultFiltersByPanel(panel: string)
    if (panel == "")
        RunError(ToANSI("Имя панели не может быть пустым"));
    end;
    var cmd = TRslOraQuery();
    cmd.SqlText = "UPDATE WFILTER SET BRANCHID = NULL WHERE PANEL = :panel and USERNICK = :oper";
    cmd.DeclareAndSet("panel", panel);
    cmd.DeclareAndSet("oper", string({oper}));
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
end;

macro InsertNewDefaultFilter(filterId: integer, oper: integer)
    if (filterId <= 0)
        RunError(ToANSI("ID фильтра должен быть больше нуля"));
    elif (oper <= 0)
        RunError(ToANSI("ID операциониста должен быть больше нуля"));
    end;
    var cmd = TRslOraQuery();
    cmd.SqlText = "insert into WFILTER (ID_WFILTER, USERNICK) VALUES (:FilterId, :oper)";
    cmd.DeclareAndSet("FilterId", filterId);
    cmd.DeclareAndSet("oper", string(oper));
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
end;

macro IsSharedFilterApplicable(filterId: integer)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT id_wfilter FROM WFILTER WHERE id_wfilter = :FilterId AND branchid = :branch";
    cmd.DeclareAndSet("FilterId", filterId);
    cmd.AddParam("branch", {NumNode});
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    return not cmd.Eof;
end;

macro IsFilterApplicable(filterId: integer)
    var filter = FindFilter(filterId);
    if (filter == null)
        RunError(ToANSI("Фильтр с ID = " + filterId + " не найден"));
    end;
    if (filter.GetByName("type") == WEB_FILTER_PRIVATE)
        if (filter.GetByName("usernick") != string({oper}))
            RunError(ToANSI("Фильтр не принадлежит текущему операционисту"));
        end;
    elif (filter.GetByName("type") == WEB_FILTER_SHARED)
        if (not IsSharedFilterApplicable(filterId))
            RunError(ToANSI("Фильтр не действует в данном подразделении"));
        end;
    end;
end;

macro SetDefaultFilter(filterId: integer, set: bool)
    var result = ResultInfo(0, "", 0);
    var filter = FindFilter(filterId);
    if (filter == null)
        RunError(ToANSI("Фильтр с ID = " + filterId + " не найден"));
    end;
    var panel = filter.GetByName("panel");
    ClearDefaultFiltersByPanel(panel);
    if (set)
        IsFilterApplicable(filter.GetByName("id_wfilter"));
        InsertNewDefaultFilter(filterId, string({oper}));
    end;
    return result;

OnError(e)
    result.ResultCode = 1;
    result.ErrorText = TrimErrText(e.Message);
    return result;
end;

macro RemoveFilterByID(filterId: integer)
    if (filterId <= 0)
        RunError(ToANSI("ID фильтра должен быть больше нуля"));
    end;
    var cmd = TRslOraQuery();
    cmd.SqlText = "DELETE FROM WFILTER WHERE ID_WFILTER = :FilterId";
    cmd.DeclareAndSet("FilterId", filterId);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
end;

// удаляет привязки фильтров к подразделениям для текущего и подчиненых
macro RemoveFilterDepForCurrentAndSlave(filterId: integer, branch: integer)
    if (filterId <= 0)
        RunError(ToANSI("ID фильтра должен быть больше нуля"));
    elif (branch <= 0)
        RunError(ToANSI("ID подразделения должен быть больше нуля"));
    end;
    var cmd = TRslOraQuery();
    cmd.SqlText = "UPDATE WFILTER SET BRANCHID = NULL WHERE ID_WFILTER = :FilterId and BRANCHID = :branch";
    cmd.DeclareAndSet("FilterId", filterId);
    cmd.DeclareAndSet("branch", branch);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
end;

// привязан ли фильтр хоть к одному подразделению
macro IsFilterDepExists(filterId: integer)
    if (filterId <= 0)
        RunError(ToANSI("FilterId должен быть > 0"));
    end;
    var cmd = TRslOraQuery();
    cmd.SqlText = "select count(ID_WFILTER) cnt from WFILTER where ID_WFILTER = :FilterId";
    cmd.DeclareAndSet("FilterId", filterId);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    if (cmd.Eof)
        return cmd.Rec.cnt > 0;
    end;
    RunError(ToANSI("Фильтр не привязан ни к одному подразделению"));
end;

macro DeleteFilter(filterId: integer)
    var result = ResultInfo(0, "", 0);
    var filter = FindFilter(filterId);
    if (filter == null)
        RunError(ToANSI("Фильтр с ID = " + filterId + " не найден"));
    end;
    const filterType = filter.GetByName("type");
    if (filterType == WEB_FILTER_PRIVATE)
        if (filter.GetByName("usernick") != string({oper}))
            RunError(ToANSI("Фильтр \"" + filter.GetByName("fltname") + "\" не может быть удален.\nДанный операционист не владелец фильтра."));
        end;
        RemoveFilterByID(filterId);
    elif (filterType == WEB_FILTER_SHARED)
        // удалим привязки к текущему узлу и подчиненным
        RemoveFilterDepForCurrentAndSlave(filterId, {NumNode});
        if (not IsFilterDepExists(filterId))
            RemoveFilterByID(filterId);
        end;
    elif (WEB_FILTER_SYSTEM == filterType)
        RunError(ToANSI("Фильтр \"" + filter.GetByName("fltname") + "\" не может быть удален. Фильтр системный."));
    end;
    return result;

OnError(e)
    result.ResultCode = 1;
    result.ErrorText = TrimErrText(e.Message);
    return result;
end;

macro SelectSlaveFilters(panel: string, branch: integer)
    if (panel == "")
        RunError(ToANSI("Имя панели не может быть пустым"));
    elif (branch <= 0)
        RunError(ToANSI("ID подразделения должен быть больше нуля"));
    end;
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT flt.* FROM WFILTER flt WHERE flt.TYPE = 1 and PANEL = :panel and flt.BRANCHID = :branch";
    cmd.DeclareAndSet("panel", panel);
    cmd.DeclareAndSet("branch", branch);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    return cmd;
end;

macro SelectDefaultFiltersByPanel(panel: string, oper: integer)
    if (panel == "")
        RunError(ToANSI("Имя панели не может быть пустым"));
    elif (oper <= 0 )
        RunError(ToANSI("ID операциониста должен быть больше нуля"));
    end;
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT flt.* FROM WFILTER flt WHERE flt.PANEL = :panel and flt.USERNICK = :oper";
    cmd.DeclareAndSet("panel", panel);
    cmd.DeclareAndSet("oper", string(oper));
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    return cmd;
end;

macro GetDefaultFilterId(panel: string, oper: integer)
    var defFilters = SelectDefaultFiltersByPanel(panel, oper);
    if (defFilters.Eof)
        return 0;
    else
        return defFilters.GetByName("id_wfilter");
    end;
end;

macro GetFilterConditionItems(filterId: integer)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT 1, ITEMS FILTERITEMS FROM WFILTER WHERE ID_WFILTER = :FilterId";
    cmd.DeclareAndSet("FilterId", filterId);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    if (cmd.Eof)
        return "";
    else
        return cmd.Rec.FILTERITEMS;
    end;
end;

macro SelectFiltersForPanelAndUser(panel: string, oper: integer, branch: integer)
    if (panel == "")
        RunError(ToANSI("Имя панели не может быть пустым"));
    elif (oper <= 0)
        RunError(ToANSI("ID операциониста должен быть больше нуля"));
    end;
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT ID_WFILTER, PANEL, TYPE, USERNICK, FLTNAME, VERSIONREC FROM WFILTER wflt"
                  " WHERE WFLT.PANEL = :panel"
                  "   AND ((WFLT.TYPE = 2 AND WFLT.USERNICK = :oper)"
                  "    OR (WFLT.TYPE = 1 AND WFLT.BRANCHID = :branch)"
                  "    OR (WFLT.TYPE = 0))";
    cmd.DeclareAndSet("panel", panel);
    cmd.DeclareAndSet("oper", string(oper));
    cmd.DeclareAndSet("branch", branch);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    return cmd;
end;

macro SelectParentAndChildFilterBranches(filterId: integer, branch: integer)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT * FROM WFILTER WHERE ID_WFILTER = :FilterId and BRANCHID = :branch";
    cmd.DeclareAndSet("FilterId", filterId);
    cmd.DeclareAndSet("branch", branch);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    return cmd;
end;

/*
macro GetBranchName(branch: integer)
    var cmd, cmdText, rs;
    cmdText = ;
    cmd = TRslOraQuery();
    cmd.SqlText = "SELECT t_name FROM ddp_dep_dbt where t_Code = :branch";
    cmd.DeclareAndSet("branch", branch);
    cmd.Execute();
    rs = RsdRecordset(cmd);
    if (not rs.moveNext)
        RunError(ToANSI("Подразделение с ID = " + branch + " не найдено"));
    end;
    return rs.value(0);
end;
*/

macro GetBranches(filterId: integer, branch: integer);
    var flt_branches = SelectParentAndChildFilterBranches(filterId, branch);
    var fltdep = null;
    var result = TArray();
    while (not flt_branches.Eof)
        fltdep = FltByBranch();
        fltdep.Id = flt_branches.GetByName("BRANCHID");
        fltdep.Name = string({Name_Bank});
       // fltdep.Name = GetBranchName(flt_branches.value("T_BRANCHID"));
       // fltdep.AppliedToChild = Char2Bool(flt_branches.value("T_WITHCHILDDEP"));
        result[result.Size] = fltdep;
        flt_branches.Next();
    end;
    return result;
end;

macro IsDefaultFilter(filterId)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT 1 FROM WFILTER WHERE ID_WFILTER = :FilterId and USERNICK = :operid";
    cmd.DeclareAndSet("FilterId", filterId);
    cmd.DeclareAndSet("operid", string({oper}));
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    return not cmd.Eof;
end;

macro SelectChildFilters(panel: string)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT ID_WFILTER, PANEL, TYPE, USERNICK, FLTNAME, VERSIONREC"
                  "  FROM WFILTER wflt"
                  " WHERE wflt.panel = :panel"
                  "   AND WFLT.TYPE = 1"
                  "   AND WFLT.BRANCHID = :branch";
    cmd.DeclareAndSet("panel", panel);
    cmd.DeclareAndSet("branch", {NumNode});
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    return cmd;
end;

macro IsInheritedFilter(filterId, branch)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT ID_WFILTER, BRANCHID"
                  "  FROM WFILTER"
                  " WHERE ID_WFILTER = :FilterId"
                  "   AND BRANCHID = :branch";
    cmd.DeclareAndSet("FilterId", filterId);
    cmd.DeclareAndSet("branch", branch);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    if (not cmd.Eof)
        return true;
    end;
    return false;
end;

macro IsChildFilter(filterId, branch)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT ID_WFILTER"
                  "  FROM WFILTER wflt"
                  " WHERE wflt.ID_WFILTER = :FilterId"
                  "   AND WFLT.TYPE = 1"
                  "   AND WFLT.BRANCHID = :branch";
    cmd.DeclareAndSet("FilterId", filterId);
    cmd.DeclareAndSet("branch", branch);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    if (not cmd.Eof)
        return true;
    end;
    return false;
end;

macro ParseFilter(rs)
    var result = Filter();
    result.Id = rs.GetByName("id_wfilter").AsInteger;
    result.Panel = rs.GetByName("panel").AsString;
    result.Name = rs.GetByName("fltname").AsString;
    result.Conditions = Xml2Rsl(GetFilterConditionItems(result.Id));
    result.IsDefault = IsDefaultFilter(result.Id);
    result.Type = rs.GetByName("type").AsInteger;
//    result.Branches = GetBranches(result.Id, {NumDprt});
    result.Version = rs.GetByName("versionrec").AsInteger;
    return result;
end;

macro ReadFilter(filterId)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT ID_WFILTER, PANEL, TYPE, USERNICK, FLTNAME, VERSIONREC FROM WFILTER WHERE ID_WFILTER = :FilterId";
    cmd.DeclareAndSet("FilterId", filterId);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    if (cmd.Eof)
        RunError(ToANSI("Фильтр не найден"));
    end;
    var filter = ParseFilter(cmd);
    if (IsInheritedFilter(filter.Id, {NumNode}))
        filter.Flags = WEB_FILTER_FLAG_INHERITED;
    end;
    if (IsChildFilter(filter.Id, {NumNode}))
        filter.Flags = WEB_FILTER_FLAG_CHILD;
    end;
    return filter;
end;

macro SelectPanelFilters(panel: string)
    var result = TArray();
    var user_filters = SelectFiltersForPanelAndUser(panel, {oper}, {NumNode});
    var flt;
    while (not user_filters.Eof)
        flt = ParseFilter(user_filters);
        if (IsInheritedFilter(flt.Id, {NumNode}))
            flt.Flags = WEB_FILTER_FLAG_INHERITED;
        end;
        result[result.Size] = flt;
        user_filters.Next();
    end;
//    if (IsFilterAdministratorUser())
    var child_filters = SelectChildFilters(panel);
    while (not child_filters.Eof)
        flt = ParseFilter(child_filters);
        flt.Flags = WEB_FILTER_FLAG_CHILD;
        result[result.Size] = flt;
        child_filters.Next();
    end;
    return result;
end;

macro CheckPrivateFilterNameExists(panel, name)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT ID_WFILTER FROM WFILTER WHERE PANEL = :panel and TYPE = :type and UPPER(FLTNAME) = UPPER(:name) and USERNICK = :operid";
    cmd.DeclareAndSet("panel", panel);
    cmd.DeclareAndSet("type", WEB_FILTER_PRIVATE);
    cmd.DeclareAndSet("name", name);
    cmd.DeclareAndSet("operid", string({oper}));
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    if (cmd.Eof)
        return -1;
    else
        return cmd.Rec.ID_WFILTER;
    end;
end;

macro CheckSharedFilterNameExists(panel, name)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT flt.ID_WFILTER FROM WFILTER flt WHERE flt.PANEL = :panel and UPPER(flt.FLTNAME) = UPPER(:name) and flt.BRANCHID = :branch";
    cmd.DeclareAndSet("panel", panel);
    cmd.DeclareAndSet("name", name);
    cmd.DeclareAndSet("branch", {NumNode});
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    if (cmd.Eof)
        return -1;
    else
        return cmd.Rec.ID_WFILTER;
    end;
end;

macro InsertNewFilter(panel, type, oper, name, items)
    var cmd = TRslOraQuery();
    cmd.SqlText = "begin"
                  "  insert into WFILTER (PANEL, TYPE, USERNICK, FLTNAME, ITEMS)"
                  "  VALUES (:panel, :type, :oper, :name, :items)"
                  "  RETURNING ID_WFILTER into :FilterId;"
                  "end;";
    cmd.DeclareAndSet("panel", panel);
    cmd.DeclareAndSet("type", type);
    cmd.DeclareAndSet("oper", string(oper));
    cmd.DeclareAndSet("name", name);
    cmd.DeclareAndSet("items", items);
    cmd.DeclareAndSet("FilterId", 0);
    if (not cmd.Execute())
       RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    return cmd.GetVariable("FilterId");
end;

macro UpdateFilterRecord(filterId, panel, type, name, items, version)
    var cmd = TRslOraQuery();
    cmd.SqlText = "UPDATE WFILTER"
                  "   SET PANEL = :panel, TYPE = :type, USERNICK = :oper, FLTNAME = :name, ITEMS = :items, VERSIONREC = :version"
                  " WHERE ID_WFILTER = :FilterId";
    cmd.DeclareAndSet("panel", panel);
    cmd.DeclareAndSet("type", type);
    cmd.DeclareAndSet("oper", IIF(type == WEB_FILTER_PRIVATE, {oper}, 0));
    cmd.DeclareAndSet("name", name);
    cmd.DeclareAndSet("items", items);
    cmd.DeclareAndSet("version", version);
    cmd.DeclareAndSet("FilterId", filterId);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
end;

macro InsertNewFilterDep(filterId, branchId, withchilddep)
    var cmd = TRslOraQuery();
    cmd.SqlText = "insert into WFILTER (ID_WFILTER, BRANCHID) VALUES (:FilterId, :branchid)";
    cmd.DeclareAndSet("FilterId", filterId);
    cmd.DeclareAndSet("branchid", branchid);
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
end;

macro InsertFilterBranchesLink(filterId, branches)
    var i = 0;
    while (i < branches.Size)
        InsertNewFilterDep(filterId, branches[i].Id, branches[i].AppliedToChild);
        i = i + 1;
    end;
end;

macro CreateNewFilter(panel: string, type: integer, branches, filterName: string, conditions :TArray)
    var result = ResultInfo(0, "", 0);
    if (panel == "")
        RunError(ToANSI("Имя панели не может быть пустым"));
    elif ((type <= WEB_FILTER_SYSTEM) or (type > WEB_FILTER_PRIVATE))
        RunError(ToANSI("Неверное значение типа фильтра"));
    elif (filterName == "")
        RunError(ToANSI("Имя фильтра не может быть пустым"));
    elif (conditions.Size == 0)
        RunError(ToANSI("Должно быть задано хотя бы одно условие"));
    end;
/*
    if (not IsFilterAdministratorUser())
        RunError(ToANSI("Отсутствуют права доступа"));
    end;
*/
    var filterId = 0;
    var xml_obj = Rsl2Xml(conditions);
    if (type == WEB_FILTER_PRIVATE)
        filterId = CheckPrivateFilterNameExists(panel, filterName);
        if (filterId != -1)
            result.FilterId = filterId;
            result.ResultCode = WEB_FILTER_ERROR_ALREADY_EXISTS;
            result.Filter = ReadFilter(filterId);
            RunError(ToANSI("Фильтр с именем " + filterName + " уже существует"));
        end;
        filterId = InsertNewFilter(panel, type, {oper}, filterName, xml_obj);
    elif (type == WEB_FILTER_SHARED)
        filterId = CheckSharedFilterNameExists(panel, filterName);
        if (filterId != -1)
            result.FilterId = filterId;
            result.ResultCode = WEB_FILTER_ERROR_ALREADY_EXISTS;
            result.Filter = ReadFilter(filterId);
            RunError(ToANSI("Фильтр с именем " + filterName + " уже существует"));
        end;
        filterId = InsertNewFilter(panel, type, 0, filterName, xml_obj);
        InsertFilterBranchesLink(filterId, branches);
    end;

    OraTrnCommit();
    result.FilterId = filterId;
    result.Filter = ReadFilter(filterId);
    return result;

OnError(e)
    if (result.ResultCode == 0)
        result.ResultCode = 1;
    end;
//    result.ErrorText = TrimErrText(e.Message);
    result.ErrorText = ToOEM(TrimErrText(e.Message));
    return result;
end;

/*
private macro IsSqlException(e)
    return isEqClass("TRsdError", e.err);
end;
*/

/*
private macro GetSqlExceptionCode(e)
    var i = 0;
    if (isEqClass("TRsdError", e.err))
        while (i < e.err.environment.ErrorCount)
            if (e.err.environment.Error(i).NativeError != 0)
                return e.err.environment.Error(i).NativeError;
            end;
            i = i + 1;
        end;
    end;
    return 0;
end;
*/


macro ConvertToXML(str, unused_param, xml)
  var err = "";
  if (str == null)
     SetParm(1, "");
  else
     SetParm(2, Rsl2Xml(str, err));
  end;

  if (err == "")
     return 0;
  else
     return err;
  end;
end;


macro UpdateFilter(filterId: integer, panel: string, type: integer, branches, filterName: string, conditions :TArray, Version)
    if (panel == "")
        RunError(ToANSI("Имя панели не может быть пустым"));
    elif ((type < WEB_FILTER_SYSTEM) or (type > WEB_FILTER_PRIVATE))
        RunError(ToANSI("Неверное значение типа фильтра"));
    elif (filterName == "")
        RunError(ToANSI("Имя фильтра не может быть пустым"));
    elif (conditions.Size == 0)
        RunError(ToANSI("Должно быть задано хотя бы одно условие"));
    end;
/*
    if (not IsFilterAdministratorUser())
        RunError(ToANSI("Отсутствуют права доступа"));
    end;
*/
    var result = ResultInfo(0, "", 0);
    var id = -1;
    result.Filter = Filter();
    var filter = FindFilter(filterId);
    if (filter == null)
        RunError(ToANSI("Фильтр с ID = " + filterId + " не найден"));
    end;
    if (filterName != filter.GetByName("fltname"))
        if (type == WEB_FILTER_PRIVATE)
            id = CheckPrivateFilterNameExists(panel, filterName);
            if (id != -1)
                result.FilterId = id;
                result.ResultCode = WEB_FILTER_ERROR_ALREADY_EXISTS;
                result.Filter = ReadFilter(id);
                RunError(ToANSI("Фильтр с именем " + filterName + " уже существует"));
            end;
        elif (type == WEB_FILTER_SHARED)
            id = CheckSharedFilterNameExists(panel, filterName);
            if (id != -1)
                result.FilterId = id;
                result.ResultCode = WEB_FILTER_ERROR_ALREADY_EXISTS;
                result.Filter = ReadFilter(id);
                RunError(ToANSI("Фильтр с именем " + filterName + " уже существует"));
            end;
        end;
    end;
    var xml_obj = null;
    if (ConvertToXML(conditions, null, xml_obj) != 0)
        RunError(ToANSI("Ошибка преобразования объекта в XML"));
    end;
    UpdateFilterRecord(filterId, panel, type, filterName, xml_obj, Version);
    RemoveFilterDepForCurrentAndSlave(filterId, {NumNode});
    InsertFilterBranchesLink(filterId, branches);
    result.Filter = ReadFilter(filterId);
    OraTrnCommit();
    return result;

OnError(e)
    if (result.ResultCode == 0)
        result.ResultCode = 1;
    end;
    result.ErrorText = TrimErrText(e.Message);
/*
    if (IsSqlException(e))
        var exceptionCode = GetSqlExceptionCode(e);
        if (exceptionCode == WEB_FILTER_SQL_CONCURRENT_CHANGES)
            result.ErrorText = "Фильтр конкурентно изменен";
        end;
    end;
*/
    return result;
end;

macro GetFilterById(filterId: integer)
    var result = ResultInfo(0, "", 0);
    result.Filter = null;
    result.Filter = ReadFilter(filterId);
    result.FilterId = result.Filter.Id;
    return result;

OnError(e)
    if (result.ResultCode == 0)
        result.ResultCode = 1;
    end;
    result.ErrorText = TrimErrText(e.Message);
    return result;
end;

private macro ListToString(idList)
    var result = "";
    if (idList.Size > 0)
        result = result + string(idList[0]);
        var i = 1;
        while (i < idList.Size)
          result = result + ", " + string(idList[i]);
          i = i + 1;
        end;
    end;
    return result;
end;

macro GetFilterNameByIdList(idList) // : TArray of TFilterIdNamePair
    var result = TArray();
    var list = ListToString(idList);
    var cmdText = "select ID_WFILTER, FLTNAME from WFILTER where ID_WFILTER in (" + list + ")";
    var cmd = TRslOraQuery();
    cmd.SqlText = cmdText;
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    var item;
    while (not cmd.Eof)
      item = TFilterIdNamePair(cmd.Rec.ID_WFILTER, cmd.Rec.FLTNAME);
      result[result.Size] = item;
      cmd.Next();
    end;
    return result;
end;

/*Условное определение корневого подразделения */
/*Для RS-Payments это РЦ с наименьшим значением code*/
macro GetRootDepatment()
    var cmd = TRslOraQuery();
    cmd.SqlText = "select min(code) code from node";
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    if (not cmd.Eof)
        return cmd.Rec.code;
    end;
    return -1;
end;

macro GetDepartmentTree(branch: integer)
    if (branch == -1)
        branch = GetRootDepatment();
    end;
    if (branch <= 0)
        branch = GetRootDepatment();
    end;
    var result: string = "";
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT 1,"
                  "  XMLELEMENT(BRANCH_LIST,(XMLElement(BRANCHES, XMLElement(\"CODE\", " + BankNode + "), XMLElement(\"NAME\", '" + BankName + "'), XMLElement(\"TYPE\", 0), XMLElement(\"PARENT\", 0))))"
                  "  .GETCLOBVAL() xmldoc"
                  "  FROM DUAL";
/*
            "SELECT 1, XMLELEMENT ( "
            "          BRANCH_LIST, "
            "          (SELECT DBMS_XMLGEN.getxmltype ( "
            "                     DBMS_XMLGEN.newcontextfromhierarchy ( "
            "                        'select level, XMLElement( BRANCHES, "
            "                            XMLElement(\"CODE\", code),  "
            "                            XMLElement(\"NAME\", text), "
            "                            XMLElement(\"TYPE\", connecttype), "
            "                            XMLElement(\"PARENT\", id_node)) "
            "                            FROM NODE "
            "                                 CONNECT BY PRIOR Code = id_node START WITH Code = "  + branch +
            "                                 ORDER SIBLINGS BY id_node ')) "
            "             FROM DUAL)).GETCLOBVAL () xmldoc  FROM DUAL ";
*/
    if (not cmd.Execute())
        RunError("", UsrError(ToANSI(cmd.Error)));
    end;
    if (not cmd.Eof)
       result = cmd.Rec.XMLDOC;
    end;
    return result;
end;
