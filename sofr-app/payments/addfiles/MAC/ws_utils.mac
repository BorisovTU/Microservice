/*********************************************************************
 Macro  : ws_utils.mac
 Created: 22-01-2019
 Вспомогательные функции
**********************************************************************/
import general;
import ws_types;
import "MTFORMS.cls";


/*Получение иконки*/
macro GetIcon(Name)
    var SelectIcon = TRslOraQuery();
    SelectIcon.SqlText = "SELECT RSP_ENCODE.Blob_To_Hex(t.icon_png) Icon"
                         "  FROM sicon t                                "
                         " WHERE t.text = :Name";
    SelectIcon.DeclareAndSet("Name", Name);

    if(not SelectIcon.Execute)
        AddPtk("GetIcon: " + SelectIcon.Error);
        RunError(toansi("GetIcon: " + SelectIcon.Error));
    end;

    var Icon = SelectIcon.GetByName("Icon").AsString;
    return Icon;
end;

private macro GetListSQLText(directoryType: integer): string
    var filter: string = "";
    var result: string;
    if (directoryType == Type_Spr_SFI)
        result = "SELECT SFI.ID_SFI as SFI8ID_SFI, SFI.CODE as SFI8CODE, SFI.TEXT as SFI8TEXT, SFI.STRCODE as SFI8STRCODE FROM SFI SFI ORDER BY SFI.CODE";
    elif (directoryType == Type_Spr_NKS)
        result = "SELECT NKS.ID_NKS as NKS8ID_NKS, NKS.TEXT as NKS8TEXT, NKS.ID_NKORR as NKS8ID_NKORR, NKORR.ID_NODE as NKORR8ID_NODE"
                 "  FROM NKS NKS JOIN NKORR NKORR ON NKS.ID_NKORR = NKORR.ID_NKORR"
                 " WHERE NKS.ISPOZ = 1"
                 " ORDER BY NKS.PRIO";
    else
        var cmd = TRslOraQuery();
        cmd.SqlText = "select RSP_WEB.GET_FIELDSPR(:FormId, :Filter) QueryText from DUAL";
        cmd.DeclareAndSet("FormId", directoryType, "Filter", filter);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        result = cmd.Rec.QueryText;
    end;
    return result;
end;

macro GetListRowsService(params)
    var cmd = TRslOraQuery();
    cmd.SqlText = SQL_WrapSelect(GetListSQLText(params.Params), params.PageNumber, params.PageSize);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd;
end;

macro GetListRowCountService(params): integer
    var cmd = TRslOraQuery();
    cmd.SqlText = "select count(*) CNT from (" + GetListSQLText(params.Params) + ")";
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.GetByName("CNT").AsInteger;
end;

macro GetViewFields(directoryType: integer): TArray
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT * FROM TABLE(RSP_WEB.GET_LIST_FIELDSPR(:P_FORM))";
    cmd.DeclareAndSet("P_FORM", directoryType);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    var viewFields = TArray();
    while (not cmd.Eof)
        viewFields[viewFields.Size] = makeObj(
            "IListItem",
            "ID", cmd.GetByName("ID").AsInteger,
            "Type", cmd.Rec.ColumnType,
            "Title", cmd.Rec.Title,
            "Tooltip", cmd.Rec.Title,
            "Source", cmd.Rec.Source
        );
        cmd.Next();
    end;
    return viewFields;
end;

class TMTErrorStreamVclMsgBox()
    var Errors: TArray = TArray();

    macro ShowMsg(msg)
        Errors[Errors.Size] = msg;
    end;
end;

macro ConvertToType(value, type: integer)
    if (ValType(value) != type)
        if (type == V_UNDEF)
            value = null;
        elif (type == V_INTEGER)
            value = int(value);
        elif (type == V_DOUBLE)
            value = double(value);
        elif (type == V_DOUBLEL)
            value = doubleL(value);
        elif (type == V_MONEY)
            value = Money(value);
        elif (type == V_MONEYL)
            value = MoneyL(value);
        elif (type == V_STRING)
            value = string(value);
        elif (type == V_DATE)
            value = Date(value);
        elif (type == V_TIME)
            value = Time(value);
        end;
    end;
    return value;
end;

macro GetKeyField(formId, isSpr)
    var tableName: string = "";
    var fieldName: string = "";
    var cmd = TRslOraQuery();
    cmd.SqlText = "begin :TableName := RSP_FORM.GET_MAINTABLEFORM(:FormId, :isSpr); :FieldName := RSP_FORM.GET_PK_TABLE(:TableName); end;";
    cmd.DeclareAndSet("TableName", tableName, "FormId", formId, "isSpr", isSpr, "FieldName", fieldName);

    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;

    tableName = cmd.GetVariable("TableName");
    fieldName = cmd.GetVariable("FieldName");
    var KeyField = tableName + "8" + fieldName;
    return KeyField;
end;
