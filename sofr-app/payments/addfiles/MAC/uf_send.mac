/*28.11.2013 voikin*/
import uf_general, uf_utils, utils, ips_utils/*, uf_sign*/;

const SendComment="Отправлен пользователем";

/* запись строки str в файл Arc\fname и копирование в Out */


macro DFile(fname)
  if(ExistFile(fname))
    if(not DeleteFile(fname))
      RunError(toansi("Не могу удалить файл "+fname));
    end;
  end;
end;


/* запись строки str в файл Arc\fname и копирование в Out */
/*
MACRO SaveToFile(fname, ArcPath, OutPath, str)


  DFile(ArcPath+fname);

  if(UF_ISSIGN)//работаем с подписью
    DFile(ArcPath+fname+".xml");

    if(not WriteStrToFile(toansi(str), ArcPath+fname+".xml"))
      RunError(toansi("Ошибка при записи в файл "+ArcPath+fname+".xml"));
    end;

    if(not WriteStrToFile(ufebssign(toansi(str)), ArcPath+fname))
      RunError(toansi("Ошибка при записи в файл "+ArcPath+fname));
    end;
  else
    if(not WriteStrToFile(toansi(str), ArcPath+fname))
      RunError(toansi("Ошибка при записи в файл "+ArcPath+fname));
    end;
  end;

  DFile(OutPath+fname);

  if(not Copyfile(ArcPath+fname, OutPath+fname))
    RunError(toansi("Не могу скопировать "+ArcPath+fname+" в "+OutPath+fname));
  end;
END;
*/

macro InsEPDAndCrtXML(XML, NameF, TmpTblName, SysCode, Pack, ErrStr, IsInf, VR, _ValidErrNum)
  var EDNo;
  var OutXML = "";
  var NameFile;
  var ValidErrNum = 0;

  var InsertEPD = TRslOraQuery();
  var CrtXMLUpd = TRslOraQuery();
  var GetValidErr = TRslOraQuery();

  InsertEPD.SqlText = " begin  :EDNo := RSP_UFEBS.InsertTMPEPD(:Tmp_Tbl,:SysCode,:Pack); end;";
  InsertEPD.DeclareAndSet("EDNo",0,"Tmp_Tbl",TmpTblName,"SysCode",SysCode,"Pack",Pack);

  CrtXMLUpd.SqlText = " begin  :OutXML := RSP_UFEBS.CrtUpdXMLEPD(:Pack,:EDNo,:FileName,:TBN,:ST); end;";
  CrtXMLUpd.DeclareAndSet("Pack",Pack,"TBN",TmpTblName,"ST",UFStateValidError);
  CrtXMLUpd.DeclareAndSetLOB("OutXML",OutXML);

  GetValidErr.SQLText = " select count(t.ID_PAYM) cnt from "+TmpTblName+" t where t.ID_SSTATE = :State";


  GetValidErr.DeclareAndSet("State",UFStateValidError);

  if(not InsertEPD.Execute)
    addptk("InsertTMPEPD/InsertINFEPD: "+InsertEPD.Error);
    addptk("Tmp_Tbl="+TmpTblName);
    addptk("SysCode="+SysCode);
    addptk("Pack="+Pack);
    RunError(toansi("InsertTMPEPD/InsertINFEPD: "+InsertEPD.Error));
  end;

  EDNo = InsertEPD.GetVariable("EDNo");

  if(not EDNo)
    SetParm(5,"Ошибка получения идентификатора документа EDNo");
    return false;
  end;
  NameFile = GetXMLFileName(EDNo);

  CrtXMLUpd.DeclareAndSet("EDNo",EDNo,"FileName", NameFile);
  if(not CrtXMLUpd.Execute)
    addPtk(" EDNo = " + EDNo +  " NameFile = " + NameFile + " OutXML = " + OutXML);
    RunError(toansi("CrtUpdXMLEPD: "+CrtXMLUpd.Error));
  end;

  /*Выберем документы, не прошедшие валидацию*/
  if(not GetValidErr.Execute)
    RunError(GetValidErr.Error);
  end;

  //ValidErrNum = GetValidErr.RowCount; zmp 18.05.2015 RowCount считает записи по количеству вызовов метода Next()
  ValidErrNum = GetValidErr.GetByName("cnt").AsInteger;

  OutXML = CrtXMLUpd.GetVariable("OutXML");

  if((OutXML == "")and(ValidErrNum == 0))
    SetParm(5,"Ошибка получения XML");
    return false;
  end;

  SetParm(0, OutXML);
  SetParm(1, NameFile);
  SetParm(9, ValidErrNum);
  return true;
end;

macro SendList_Packet(SystemCode)
/*Отправка УФЭБС в пакете*/
  var i;
  var ErrStr = "";
  var ListNum = PayListNum();
  var ID = "";
  var VersionRec = "";
  var XML = "";
  var NameFile = "";
  var InsertTMP = TRslOraQuery();
  var ValidErrNum = 0;

  VclInitProgress(ListNum, "Отправка пакета");

  i = 0;
  InsertTMP.SqlText = " insert into "+UFTblTmpStepDoc+"(ID_PAYM,VARLEN,VR,ID_SSTATE) values(:ID_PAYM,:SendComment,:VR,:ID_SSTATE)";
  InsertTMP.DeclareAndSet("SendComment",SendComment);

  /*Сначала собираем все выделенные документы во временную таблицу*/
  while(i < ListNum)
    ID = VersionRec = 0;
    PayListField(i,0,"id_paym",ID,"VersionRec",VersionRec);

    if(not ID)
      OraTrnRollBack();
      MsgBox("Ошибка получения идентификатора\nОтправка отменена");
      return;
    end;

    InsertTMP.DeclareAndSet("ID_PAYM",ID, "VR", VersionRec,"ID_SSTATE",UFStateSentRKC);
    if(not InsertTMP.Execute)/*Вставка во временную таблицу*/
      RunError(InsertTMP.Error);
    end;

    VclUseProgress(i = i + 1);
  end;
  /*И после этого уже с ними работаем*/

  /*Вставка в EPD и получения XML*/
  if(not InsEPDAndCrtXML(XML,NameFile,UFTblTmpStepDoc,SystemCode,1,ErrStr,Null,Null,Null,ValidErrNum))
    OraTrnRollBack();
    MsgBox(ErrStr);
    return;
  end;

  /*Смена состояния*/
  if(ChgState2(UFTblTmpStepDoc) < ListNum)
    OraTrnRollBack();
    MsgBox("Не удалось сменить состояние платежу\nОтправка отменена");
    return;
  end;

  /*Сохранение XML в файл*/
  if(StrLen(XML))
    SaveToFile(NameFile, UFDirUFEBSArc, UFDirUFEBSOut, XML);
    AddPtk("Отправка пакета: Создан файл "+UFDirUFEBSOut+NameFile);
  end;

  OraTrnCommit();

  /*Если есть документы, ЭПС которых не прошли валидацию*/
  if(ValidErrNum)
    if(ListNum == 1)/*отправляем один документ*/
      MsgBox("ЭПС по выбранному документу не прошел валидацию. Документ перемещен в отдельный список.");
    else /*отправляем несколько документов*/
      MsgBox("ЭПС по некоторым выбранным документам не прошли валидацию. Такие документы перемещены в отдельный список.");
    end;
  end;

  VclRemProgress();
  RefreshScreen;

  OnError(e)
    VclRemProgress();
    OraTrnRollBack();
    AddPtk ("Отправка пакета: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
    MsgBox(ToOem(e.Message));

end;

macro SendList_EPD()
/*отправка срочного перевода*/
  var i;
  var ErrStr = "";
  var ListNum = PayListNum();
  var ID = "";
  var VersionRec = "";
  var XML = "";
  var NameFile = "";
  var InsertTMP = TRslOraQuery();
  var ValidErrIs = false; /*признак существования документов, не прошедших валидацию*/
  var ValidErrNum = 0;


  VclInitProgress(ListNum, "Отправка срочного перевода");
  i = 0;
  InsertTMP.SqlText = " insert into "+UFTblTmpStepDoc+"(ID_PAYM,VARLEN,VR,ID_SSTATE) values(:ID_PAYM,:SendComment,:VR,:ID_SSTATE)";
  InsertTMP.DeclareAndSet("SendComment",SendComment);

  while(i < ListNum)
    ID = VersionRec = 0;
    PayListField(i,0,"id_paym",ID,"VersionRec",VersionRec);

    if(not ID)
      OraTrnRollBack();
      MsgBox("Ошибка получения идентификатора\nОтправка отменена");
      return;
    end;

    /*Вставка во временную таблицу*/
    InsertTMP.DeclareAndSet("ID_PAYM",ID, "VR", VersionRec,"ID_SSTATE",UFStateSentRKC);
    if(not InsertTMP.Execute)
      RunError(InsertTMP.Error);
    end;

    /*Вставка в EPD и получения XML*/
    if(not InsEPDAndCrtXML(XML,NameFile,UFTblTmpStepDoc,UFSystemCodeBESP,0,ErrStr,Null,Null,Null,ValidErrNum))/*!!*/
      OraTrnRollBack();
      MsgBox(ErrStr);
      return;
    end;

    /*Если есть документы, ЭПС которых не прошли валидацию*/
    if(ValidErrNum)
      ValidErrIs = true;
    end;

    /*Смена состояния*/
    if(ChgState2(UFTblTmpStepDoc) != 1)
      OraTrnRollBack();
      MsgBox("Не удалось сменить состояние платежу\nОтправка отменена");
      return;
    end;

    /*Сохранение XML в файл*/
    if(StrLen(XML))
      SaveToFile(NameFile, UFDirBESPArc, UFDirBESPOut, XML);
      AddPtk("Отправка срочного перевода: Создан файл "+UFDirBESPOut+NameFile);
    end;

    OraTrnCommit();

    VclUseProgress(i = i + 1);
  end;

  if(ValidErrIs)
    if(ListNum == 1)/*отправляем один документ*/
      MsgBox("ЭПС по выбранному документу не прошел валидацию. Документ перемещен в отдельный список.");
    else /*отправляем несколько документов по одному*/
      MsgBox("ЭПС по некоторым выбранным документам не прошли валидацию. Такие документы перемещены в отдельный список.");
    end;
  end;

  VclRemProgress();
  RefreshScreen;

  OnError(e)
    VclRemProgress();
    OraTrnRollBack();
    AddPtk ("Отправка срочного перевода: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
    MsgBox(ToOem(e.Message));
end;

macro SendList_UFEBS()
  SendList_Packet(UFSystemCodeUFEBS)
end;

macro SendList_BESP()
  if  (int(UFPacketEPD) == 0)
    SendList_EPD();
  elif(int(UFPacketEPD) == 1)
    SendList_Packet(UFSystemCodeBESP)
  end;
end;

/*Повторная отправка ЭПС*/
macro CreateXMLAgain_EPD(epd)
  var OutXML = "";
  var ArcDir, OutDir;
  var CrtXML;

  if(not VclGetTrue("Отправить повторно ЭПС?"))
    return;
  end;

  if(epd.epd.eddate != {curdate})
     msgbox("Повторное формирование файла не возможно. Дата ЭПС не равна дате операционного дня");
     return;
  end;

  if(epd.epd.ID_PACKETED)
     msgbox("Повторное формирование файла не возможно. ЭПС входит в состав пакета");
     return;
  end;

  CrtXML = TRslOraQuery();

  CrtXML.SqlText = " begin  :OutXML := RSP_UFEBS.CreateXMLEPD(:EDNo,:Pack); end;";
  CrtXML.DeclareAndSet("EDNo",epd.epd.EDNo,"Pack",0);
  CrtXML.DeclareAndSetLOB("OutXML",OutXML);

  if(not CrtXML.Execute)
    RunError(toansi("CreateXMLEPD: "+CrtXML.Error));
  end;

  OutXML = CrtXML.GetVariable("OutXML");

  if(OutXML == "")
    OraTrnRollBack();
    MsgBox("Ошибка получения XML");
    return;
  end;

  if(epd.epd.SYSTEMCODE == UFSysCodeMOP)
    ArcDir = UFDirUFEBSArc;
    OutDir = UFDirUFEBSOut;
  elif(epd.epd.SYSTEMCODE == UFSysCodeBESP)
    ArcDir = UFDirBESPArc;
    OutDir = UFDirBESPOut;
  end;

  /*Сохранение XML в файл*/
  SaveToFile(epd.epd.FILENAME, ArcDir, OutDir, OutXML);
  AddPtk("Повторное формирование XML: Создан файл "+OutDir+epd.epd.FILENAME);

  OraTrnCommit();

  OnError(e)
    OraTrnRollBack();
    AddPtk ("Повторное формирование XML: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
    MsgBox(ToOem(e.Message));

end;

/*Повторная отправка пакета*/
macro CreateXMLAgain_Packet(packet)

  var OutXML = "";
  var CrtXML;

  if(not VclGetTrue("Отправить повторно пакет?"))
    return;
  end;

  if(packet.packeted.eddate != {curdate})
     msgbox("Повторное формирование файла не возможно. Дата пакета не равна дате операционного дня");
     return;
  end;

  CrtXML = TRslOraQuery();

  CrtXML.SqlText = " begin  :OutXML := RSP_UFEBS.CreateXMLEPD(:EDNo,:Pack); end;";
  CrtXML.DeclareAndSet("EDNo",packet.packeted.EDNo,"Pack",1);
  CrtXML.DeclareAndSetLOB("OutXML",OutXML);

  if(not CrtXML.Execute)
    RunError(toansi("CreateXMLEPD: "+CrtXML.Error));
  end;

  OutXML = CrtXML.GetVariable("OutXML");

  if(OutXML == "")
    OraTrnRollBack();
    MsgBox("Ошибка получения XML(пакет)");
    return;
  end;

  /*Сохранение XML в файл*/
  SaveToFile(packet.packeted.FILENAME, UFDirUFEBSArc, UFDirUFEBSOut, OutXML);
  AddPtk("Повторное формирование XML(пакет): Создан файл "+UFDirUFEBSOut+packet.packeted.FILENAME);

  OraTrnCommit();

  OnError(e)
    OraTrnRollBack();
    AddPtk ("Повторное формирование XML(пакет): " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
    MsgBox(ToOem(e.Message));

end;

/*Изменение состояния пакета, к которому привязан платеж-запрос/ответ, при отправке ЭСИС */
macro IPS_SetStateSentPCK(ID_PAYM_QUERY, IsAnswer)

  /*Найдем кейс*/
  var PckObj = GetIPSPCKbyPAYM(ID_PAYM_QUERY);
  var OldState;
  var NewState;
  var IDPack;
  var ID_PAYM;
  var ID_EPD;
  var PayObj;
  var Specific;
  var ESISObj;
  var EPDSel;
  var pckNewState;
  var IDKart;
  var IDKorrAW;

  if(ValType(PckObj) != V_GENOBJ) /*Нет кейса у запроса/ответа*/
    /*нужно создавать кейс*/

    /*сначала найдем платеж, на который создан запрос*/

    ESISObj = GetPaymByID(int(ID_PAYM_QUERY));

    if(IsAnswer)
      EPDSel = GetEDbyID("EPD", ESISObj.EDANSWER.ID_EPD);
    else
      EPDSel = GetEDbyID("EPD", ESISObj.EDQUERY.ID_EPD);
    end;

    if(ValType(EPDSel) == V_GENOBJ) /* нашли ЭПС*/
      ID_PAYM = EPDSel.GetByName("ID_PAYM").AsInteger;

      if(EPDSel.GetByName("IN_OUT").AsInteger == 1)/*начальный*/
        IDKart = IDKART_RECT;
      else/*ответный*/
        IDKart = IDKART_UNCL;
      end;

      PayObj = GetPaymByID(ID_PAYM);
    else
      return true;
    end;

    PckObj = GetIPSPCKbyPAYM(ID_PAYM);

    if(  (ValType(PckObj) == V_GENOBJ)/*возможно, у платежа, на который создан запрос/ответ, уже есть кейс*/
       and(PckObj.ipspck.ID_SSTPCK != ST_IPSPCK_UNDEF_CTG)/*и он не в этих состояниях*/
       and(PckObj.ipspck.ID_SSTPCK != ST_IPSPCK_CLOSED)
       and(PckObj.ipspck.ID_SSTPCK != ST_IPSPCK_ARH)
       and(PckObj.ipspck.ID_SSTPCK != ST_IPSPCK_INVLD)
       and(PckObj.ipspck.ID_NKART  != IDKART_STND))/*и картотека кейса не стандартная*/

       /*Тогда просто нужно добавить запрос/ответ в кейс*/

    else/*у платежа, на который создан запрос/ответ, уже нет кейса*/
      /*создадим кейс*/
      if(ValType(ESISObj.paym.ID_NKORROUTPUT) != V_UNDEF)
        IDKorrAW = ESISObj.paym.ID_NKORROUTPUT;
      elif((EPDSel.GetByName("IN_OUT").AsInteger == 1)and(ValType(PayObj.paym.ID_NKORROUTPUT) != V_UNDEF))
        IDKorrAW = PayObj.paym.ID_NKORROUTPUT;
      elif((EPDSel.GetByName("IN_OUT").AsInteger == 2)and(ValType(PayObj.paym.ID_NKORRINPUT) != V_UNDEF))
        IDKorrAW = PayObj.paym.ID_NKORRINPUT;
      end;

      IDPack = create_ipspck(PayObj, Specific, "Создан при отправке запроса/ответа", IDKart, IDKorrAW);
      if(IDPack)
        AddPtk("Создан кейс ИД " +int(IDPack)+ " для платежа ИД "+int(ID_PAYM));
      else
        OraTrnRollBack();
        AddPtk("Ошибка при создании кейса для платежа ИД "+int(ID_PAYM));
      end;

      if(not PayObj.Save())
        OraTrnRollBack();
        AddPtk("Ошибка при сохранении платежа ИД "+int(ID_PAYM));
      end;

    end;

    PckObj = GetIPSPCKbyPAYM(ID_PAYM);

    if(ValType(PckObj) == V_GENOBJ)
      /*Добавляем запрос/ответ в кейс*/
      if(IsAnswer)
        pckNewState = ST_IPSPCK_AW_CREATED;
      else
        pckNewState = ST_IPSPCK_QU_CREATED;
      end;

      if(PACKADDPAYM(int(PckObj.ipspck.ID_IPSPCK), int(PckObj.ipspck.VERSIONREC), pckNewState, int(ID_PAYM_QUERY), "Добавлен запрос/ответ ИД "+int(ID_PAYM_QUERY)))
        AddPtk("Добавлен запрос/ответ ИД " +  int(ID_PAYM_QUERY)+" в кейс ИД "+int(PckObj.ipspck.ID_IPSPCK));
        PckObj = GetIPSPCKbyPAYM(ID_PAYM);
      else
        OraTrnRollBack();
        AddPtk("Ошибка при добавлении запроса/ответа ИД " +  int(ID_PAYM_QUERY)+" в кейс ИД "+int(PckObj.ipspck.ID_IPSPCK));
      end;
    end;

  end;

  OldState = int(PckObj.IPSPCK.ID_SSTPCK);

  if(OldState == ST_IPSPCK_ARH)
    AddPtk("Пакет ИД "+int(PckObj.IPSPCK.ID_IPSPCK) +" находится в состоянии "+OldState+". Изменение запрещено.");
    return false;
  end;

  if(IsAnswer)
    NewState = ST_IPSPCK_AW_SENT;
  else
    NewState = ST_IPSPCK_QU_SENT;
  end;
  /*меняем состояние*/
  var ret = PACKCHANGESTATE(int(PckObj.IPSPCK.ID_IPSPCK), int(PckObj.IPSPCK.VERSIONREC), NewState, "Запрос/ответ ИД "+int(ID_PAYM_QUERY)+" отправлен");
  if(ret)
    AddPtk("Изменено состояния ("+OldState+"->"+NewState+") кейса ИД "+int(PckObj.IPSPCK.ID_IPSPCK));
  else
    RunError(toansi("Ошибка при смене состояния кейса ИД "+int(PckObj.IPSPCK.ID_IPSPCK)));
  end;

  return true;
end;


macro Send_ESID(ESID, IsAnswer, IsList)
/*Отправка ЭСИС*/

  var ID, VersionRec;
  var IsPacket = 0;
  var OutXML = "";
  var EDNo = 0;
  var NameFile = "";
  var InsertTMP, CrtXMLESID, UpdFilNamEPD;
  var ArcDir, OutDir;
  var EDMainTable;
  var SYSTEMCODE;
  var Ans;
  var Txt = "";
  var ErrStr;

  if(IsAnswer)
    EDMainTable = ESID.EDAnswer;
    ID         = EDMainTable.ID_EDANSWER;
    if(ValType(EDMainTable.ID_EDQUERY) != V_UNDEF)
      SYSTEMCODE = GetSystemCodeByID(EDMainTable.ID_EDQUERY,true);
    elif(ValType(EDMainTable.ID_EPD) != V_UNDEF)
      SYSTEMCODE = GetSystemCodeByID(EDMainTable.ID_EPD);
    else
      SYSTEMCODE = UFSystemCodeUFEBS;
    end;
    Ans = 1;
  else
    EDMainTable = ESID.EDQuery;
    ID         = EDMainTable.ID_EDQUERY;
    SYSTEMCODE = EDMainTable.SYSTEMCODE;
    Ans = 0;
  end;
  VersionRec = EDMainTable.VersionRec;

  InsertTMP = TRslOraQuery();
  InsertTMP.SqlText = " insert into "+UFTblTmpIDVR+"(ID,VR) values(:ID,:VR)";
  InsertTMP.DeclareAndSet("ID",ID,"VR", VersionRec);

  if(not InsertTMP.Execute)/*Вставка во временную таблицу*/
    RunError(InsertTMP.Error);
  end;

  if((SYSTEMCODE != UFSysCodeBESP)and((Index(UFPacketESID,EDMainTable.TYP))or(Index(UFPacketEID,EDMainTable.TYP))or(Index(UFPacketCash,EDMainTable.TYP))))/*не БЭСП*/
    if(Index(UFPacketESID,EDMainTable.TYP))
      IsPacket = 2;
    elif(Index(UFPacketEID,EDMainTable.TYP))
      IsPacket = 3;
    elif(Index(UFPacketCash,EDMainTable.TYP))
      IsPacket = 5;
    end;
  else
    EDNo = EDMainTable.EDNo;
    NameFile = GetXMLFileName(EDNo);
  end;

  CrtXMLESID = TRslOraQuery();
  CrtXMLESID.SqlText = " begin  RSP_UFEBS.CreateXMLESID(:TMPTable,:NameFile,:EDNo,:OutXML,:Txt,:Ans,:IsPacket); end;";
  CrtXMLESID.DeclareAndSet("TMPTable",UFTblTmpIDVR,"NameFile",NameFile,"EDNo",EDNo,"Txt",Txt,"Ans",Ans,"IsPacket",IsPacket);
  CrtXMLESID.DeclareAndSetLOB("OutXML",OutXML);

  if(not CrtXMLESID.Execute)
    RunError(toansi("CreateXMLESID: "+CrtXMLESID.Error));
  end;

  Txt = CrtXMLESID.GetVariable("Txt");
  if(Txt)
    ErrStr = Txt;
  else
    OutXML = CrtXMLESID.GetVariable("OutXML");
    if(OutXML == "")
      OraTrnRollBack();
      ErrStr = "Ошибка получения XML";
      SetParm(3, ErrStr);
      return false;
    end;
  end;

  if(IsPacket)
    EDNo = CrtXMLESID.GetVariable("EDNo");
    if(int(EDNo) == 0)
      OraTrnRollBack();
      if(IsList)
        AddPtk("Отправка ЭСИС(ИД "+int(ID)+"): Ошибка получения EDNo пакета");
      else
        MsgBox("Ошибка получения EDNo пакета");
      end;
      return false;
    end;

    NameFile = GetXMLFileName(EDNo);
    Txt = "";

    UpdFilNamEPD = TRslOraQuery();
    UpdFilNamEPD.SqlText = "begin RSP_UFEBS.UpdFilNamED(:NameFile,:EDNo,:Txt); end;";
    UpdFilNamEPD.DeclareAndSet("NameFile",NameFile,"EDNo",EDNo,"Txt",Txt);
    if(not UpdFilNamEPD.Execute)
      RunError(toansi("UpdFilNamED: "+UpdFilNamEPD.Error));
    end;

    Txt = UpdFilNamEPD.GetVariable("Txt");

  end;

  if(ValType(EDMainTable.ID_PAYM) != V_UNDEF)
  /*формирование кейса*/
    IPS_SetStateSentPCK(EDMainTable.ID_PAYM,IsAnswer);
  end;

  /*Сохранение XML в файл*/
  if(SYSTEMCODE == UFSysCodeBESP)
    ArcDir = UFDirBESPArc;
    OutDir = UFDirBESPOut;
  else
    ArcDir = UFDirUFEBSArc;
    OutDir = UFDirUFEBSOut;
  end;

  SaveToFile(NameFile, ArcDir, OutDir, OutXML);
  AddPtk ("Отправка ЭСИС: создан файл "+OutDir+NameFile);

  OraTrnCommit();
  if(not IsList)
    ExitScreen();
  end;
  return true;

  OnError(e)
    OraTrnRollBack();
    AddPtk ("Отправка ЭСИС: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
    MsgBox(ToOem(e.Message));
    return false;

end;

macro Send_ESCOS(ES, IsList)
/*Отправка ЭС в ЦОС*/

  var ID, VersionRec;
  var OutXML = "", out = "";
  var EDNo = 0;
  var NameFile = "";
  var InsertTMP, CrtXMLESCOS, UpdFilNamEPD;
  var ArcDir, OutDir;
  var EDMainTable;
  var SYSTEMCODE;
  var Ans;

  EDMainTable = ES.EDCOS;
  ID         = EDMainTable.ID_EDCOS;
  SYSTEMCODE = EDMainTable.SYSTEMCODE;
  VersionRec = EDMainTable.VersionRec;

  InsertTMP = TRslOraQuery();
  InsertTMP.SqlText = " insert into "+UFTblTmpIDVR+"(ID,VR) values(:ID,:VR)";
  InsertTMP.DeclareAndSet("ID",ID,"VR", VersionRec);

  if(not InsertTMP.Execute)/*Вставка во временную таблицу*/
    RunError(InsertTMP.Error);
  end;

  EDNo = EDMainTable.EDNo;
  NameFile = GetXMLFileName(EDNo);

  CrtXMLESCOS = TRslOraQuery();
  CrtXMLESCOS.SqlText = " begin  RSP_UFEBS.CreateXMLESCOS(:TMPTable,:NameFile,:OutXML, :out); end;";
  CrtXMLESCOS.DeclareAndSet("TMPTable",UFTblTmpIDVR,"NameFile",NameFile);
  CrtXMLESCOS.DeclareAndSetLOB("OutXML",OutXML);
  CrtXMLESCOS.DeclareAndSetLOB("out"   ,out   );

  if(not CrtXMLESCOS.Execute)
    RunError(toansi("Send_ESCOS: "+CrtXMLESCOS.Error));
  end;


  OutXML = CrtXMLESCOS.GetVariable("OutXML");
  Out = CrtXMLESCOS.GetVariable("Out");

  if(OutXML == "")
    OraTrnRollBack();
    MsgBox("Ошибка получения XML: "+Out);
    return false;
  end;

  /*Сохранение XML в файл*/
  if(SYSTEMCODE == UFSysCodeBESP)
    ArcDir = UFDirBESPArc;
    OutDir = UFDirBESPOut;
  else
    ArcDir = UFDirUFEBSArc;
    OutDir = UFDirUFEBSOut;
  end;
  SaveToFile(NameFile, ArcDir, OutDir, OutXML);
  AddPtk ("Отправка ЭС ЦОС: создан файл "+OutDir+NameFile);

  OraTrnCommit();
  if(not IsList)
    ExitScreen();
  end;
  return true;

  OnError(e)
    OraTrnRollBack();
    AddPtk ("Отправка ЭС ЦОС: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
    MsgBox(ToOem(e.Message));
    return false;

end;

macro Send_ESID_Query_Card(ESID)
/*Отправка ЭСИС-запросов из карточки*/
  return Send_ESID(ESID, false, false);
end;

macro Send_ESID_Answer_Card(ESID)
/*Отправка ЭСИС-запросов из карточки*/
  return Send_ESID(ESID, true, false);
end;


macro Send_ESID_list(IsAnswer)
/*Отправка ЭСИС из списка*/
  var i;
  var ListNum = PayListNum();
  var ESID;

  VclInitProgress(ListNum, "Отправка ЭСИС");
  i = 0;

  while(i < ListNum)
    ESID = PayListElem(i);

    if(not Send_ESID(ESID, IsAnswer, true))
      VclRemProgress();
      RefreshScreen;
      return
    end;

    VclUseProgress(i = i + 1);
  end;

  VclRemProgress();
  RefreshScreen;

end;

macro Send_ESCOS_list()
/*Отправка ЭС в ЦОС из списка*/
  var i;
  var ListNum = PayListNum();
  var ES;

  VclInitProgress(ListNum, "Отправка ЭС");
  i = 0;

  while(i < ListNum)
    ES = PayListElem(i);

    if(not Send_ESCOS(ES, true))
      VclRemProgress();
      RefreshScreen;
      return
    end;

    VclUseProgress(i = i + 1);
  end;

  VclRemProgress();
  RefreshScreen;

end;

macro Send_ESID_Query_list()
/*макропроцедура "вешается" на пункт меню списка запросов*/
  Send_ESID_list(false);
end;

macro Send_ESID_Answer_list()
/*макропроцедура "вешается" на пункт меню списка ответов*/
  Send_ESID_list(true);
end;

macro Send_ESID_EDCOS_list()
/*макропроцедура "вешается" на пункт меню списка ЭС ЦОС*/
  Send_ESCOS_list();
end;

macro Send_ESID_Query_ARM()
  /*макропроцедура для АРМ-Х*/
  var ESID;
  var qtext, qry, PaymObj;
  var count = 0, ErrCount = 0;

  qtext = " select q.id_edquery, q.id_nform \n" +
          "  from edquery q\n"+
          " where \n"+
          " nvl(q.STATUS,0) = 0 AND q.IN_OUT = 1 \n";

  qry = ExecQuery (qtext);

  if (not TstObj(qry))
     RunError (toansi("Отправка запросов: ошибка в запросе выборки запросов для отправки"));
  end;

  while(not qry.eof)

    ESID = TRslPayDoc(int(qry.rec.id_nform),int(qry.rec.ID_EDQUERY));

    if (not TstObj(ESID))
       ErrCount = ErrCount + 1;
       qry.next();
       continue;
    end;

    if(not Send_ESID(ESID, false, true))
       ErrCount = ErrCount + 1;
    else
       count    = count    + 1;
    end;

    qry.next();
  end;

  if(qry.rowCount != 0)
    AddPtk("---Отправка запросов---");
    AddPtk("Отправлено:" + count + ". Пропущено:" + ErrCount);
    AddPtk("----------------------");
  end;

  return true;

onError(e)
  AddPtk ("Отправка ЭПС: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
  AddPtk("----------------------");

  return false;
end;

macro Send_ESID_Answer_ARM()
  /*макропроцедура для АРМ-Х*/

  var ESID;
  var qtext, qry, PaymObj;
  var ErrCount = 0, Count = 0;


  qtext = " select a.id_edanswer, a.id_nform \n" +
          "  from edanswer a\n"+
          " where \n"+
          " nvl(a.STATUS,0) = 0 AND a.IN_OUT = 1 \n";

  qry = ExecQuery (qtext);

  if (not TstObj(qry))
     RunError (toansi("Отправка ответов: ошибка в запросе выборки ответов для отправки"));
  end;

  while(not qry.eof)

    ESID = TRslPayDoc(int(qry.rec.id_nform), int(qry.rec.ID_EDANSWER));

    if (not TstObj(ESID))
       ErrCount = ErrCount + 1;
       qry.next();
       continue;
    end;

    if(not Send_ESID(ESID, true, true))
       ErrCount = ErrCount + 1;
    else
       count = count + 1;
    end;

    qry.next();
  end;

  if(qry.rowCount != 0)
     AddPtk("---Отправка ответов---");
     AddPtk("Отправлено:" + count + ". Пропущено:" + ErrCount);
     AddPtk("----------------------");
  end;

  return true;

onError(e)
  AddPtk ("Отправка ЭПС: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
  AddPtk("----------------------");

  return false;
end;

macro SendList_Packet_Arm(SystemCode)
/*Отправка УФЭБС в пакете*/
  var ErrStr    = "";
  var XML       = "";
  var NameFile  = "";
  var InsertTMP = TRslOraQuery();
  var cmd       = TRslOraQuery();
  var count     = 0, ErrCount = 0;

  InsertTMP.SqlText = " insert into " + UFTblTmpStepDoc + "(ID_PAYM, VARLEN, VR, ID_SSTATE) values(:ID_PAYM, :SendComment, :VR, :ID_SSTATE)";
  InsertTMP.DeclareAndSet("SendComment", "Отправлен автоматически");


  cmd.sqlText = " SELECT P.ID_PAYM, P.VERSIONREC                 "+
                "  FROM paym p                                   "+
                " WHERE P.ID_SSTATE = :ID_SSTATE AND P.ISINF != 1";

  if(SystemCode == UFSysCodeBESP)
    cmd.DeclareAndSet("ID_SSTATE", UFStateBESPReadyToSend);
  else
    cmd.DeclareAndSet("ID_SSTATE", UFStateUFBSReadyToSend);
  end;

  cmd.Execute();

  if(not cmd.Execute)
     RunError(toansi(cmd.Error));
  end;

  while(not cmd.eof)

    InsertTMP.DeclareAndSet("ID_PAYM", cmd.GetByName("ID_PAYM").AsInteger, "VR", cmd.GetByName("VERSIONREC").AsInteger, "ID_SSTATE", UFStateSentRKC);

    if(not InsertTMP.Execute)
       RunError(toansi(InsertTMP.Error));
    end;

    cmd.next();
  end;


  if(cmd.RowCount != 0)
    AddPtk("---Отправка несрочным переводом---");
    /*Вставка в EPD и получения XML*/
    if(not InsEPDAndCrtXML(XML, NameFile, UFTblTmpStepDoc, SystemCode, 1, ErrStr, Null, Null, Null, ErrCount))
       RunError(toansi(ErrStr));
    end;

    /*Смена состояния*/
    if(ChgState2(UFTblTmpStepDoc) == 0)
       RunError(toansi("Не удалось сменить состояние платежу\nОтправка отменена"));
    end;

  end;

  /*Сохранение XML в файл*/
  if(StrLen(XML))
     SaveToFile(NameFile, UFDirUFEBSArc, UFDirUFEBSOut, XML);
     AddPtk("Отправка пакета: Создан файл " + UFDirUFEBSOut + NameFile);
  end;

  OraTrnCommit();

  if(cmd.RowCount != 0)
     count = cmd.RowCount - ErrCount;
     if(count < 0) count = 0; end;

     AddPtk("Отправлено:" + count + ". Пропущено:" + ErrCount);
     AddPtk("----------------------");
  end;

  return true;

OnError(e)
  OraTrnRollBack();
  AddPtk ("Отправка пакета: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
  AddPtk("----------------------");

  return false;
end;

macro SendList_EPD_Arm()
  /*отправка сервисом срочного перевода*/
  var XML       = "";
  var NameFile  = "";
  var ErrStr    = "";
  var InsertTMP = TRslOraQuery();
  var cmd       = TRslOraQuery();
  var ValidErrNum = 0, count = 0, ErrCount = 0;


  InsertTMP.SqlText = " insert into " + UFTblTmpStepDoc + "(ID_PAYM, VARLEN, VR, ID_SSTATE) values(:ID_PAYM,:SendComment, :VR, :ID_SSTATE)";
  InsertTMP.DeclareAndSet("SendComment", "Отправлен автоматически");


  cmd.sqlText = " SELECT P.ID_PAYM, P.VERSIONREC                 "+
                "  FROM paym p                                   "+
                " WHERE P.ID_SSTATE = :ID_SSTATE AND P.ISINF != 1";

  cmd.DeclareAndSet("ID_SSTATE", UFStateBESPReadyToSend);
  cmd.Execute();

  if(not cmd.Execute)
     RunError(toansi(cmd.Error));
  end;

  if(cmd.RowCount != 0)
     AddPtk("---Отправка срочным переводом---");
  end;

  while(not cmd.eof)

    InsertTMP.DeclareAndSet("ID_PAYM", cmd.GetByName("ID_PAYM").AsInteger, "VR", cmd.GetByName("VERSIONREC").AsInteger, "ID_SSTATE", UFStateSentRKC);

    /*Вставка во временную таблицу*/
    if(not InsertTMP.Execute)
       RunError(toansi(InsertTMP.Error));
    end;

    /*Вставка в EPD и получения XML*/
    if(not InsEPDAndCrtXML(XML, NameFile, UFTblTmpStepDoc, UFSystemCodeBESP, 0, ErrStr, Null, Null, Null, ValidErrNum))/*!!*/
       RunError(toansi(ErrStr));
    end;

    /*Смена состояния*/
    if(ChgState2(UFTblTmpStepDoc) != 1)
       RunError(toansi("Не удалось сменить состояние платежу\nОтправка отменена"));
    end;

    /*Сохранение XML в файл*/
    if(StrLen(XML))
       SaveToFile(NameFile, UFDirBESPArc, UFDirBESPOut, XML);
       AddPtk("Отправка срочным переводом: Создан файл " + UFDirBESPOut + NameFile);
    end;

    ErrCount = ErrCount + ValidErrNum;

    OraTrnCommit();
    cmd.next();

  end;

  if(cmd.RowCount != 0)
     count = cmd.RowCount - ErrCount;

     if(count < 0) count = 0; end;

     AddPtk("Отправлено:" + count + ". Пропущено:" + ErrCount);
     AddPtk("----------------------");

  end;

  return true;

OnError(e)
  OraTrnRollBack();
  AddPtk ("Отправка срочным переводом: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
  AddPtk("----------------------");

  return false;
end;

macro SendList_UFEBS_ARM()
  SendList_Packet_Arm(UFSystemCodeUFEBS)
end;

macro SendList_BESP_ARM()
  if(int(UFPacketEPD) == 0)
    SendList_EPD_Arm();
  elif(int(UFPacketEPD) == 1)
    SendList_Packet_Arm(UFSystemCodeBESP)
  end;
end;
