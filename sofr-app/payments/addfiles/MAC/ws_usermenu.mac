import captureOutput, ws_types;



class TWsSubsystem
  var Id              : integer;  // Цифровой номер подсистемы
  var Symbol          : string ;  // Символ подсистемы
  var Name            : string ;  // Наименование подсистемы
  var HelpSubSystem   : string ;  // Адрес страницы помощи по подсистеме

  ID            = 1;               //-1;
  Symbol        = "P";             //"";
  Name          = "RS-Payments";   //"";
  HelpSubSystem = "";
end;

class TWsMenuItem
  var iIdentProgram   : integer; // Цифровой номер подсистемы
  var iNumberPoint    : integer; // Уникальный номер узла меню в рамках iIdentProgram
  var iNumberParent   : integer; // Номер вышестоящего пункта меню
  var iNumberLine     : integer; // Номер пункта в подменю
  var iCaseItem       : integer; // Системный номер выбора
  var cSystemItem     : string ; // Признак системного модуля
  var szNameItem      : string ; // Наименование пункта меню
  var szNamePrompt    : string ; // Подсказка к названию выбора (выводится в Тултип)
  var iHelp           : integer; // Номер страницы помощи
  var iProgItem       : integer; // Идентификатор приложения модуля
  var Parm            : string ; // Параметры вызова пункта меню. Лежат в ROW, передаем как строку.

  iIdentProgram   = 1;
  iNumberPoint    = 0;
  iNumberParent   = 0;
  iNumberLine     = 0;
  iCaseItem       = 1; //0;
  cSystemItem     = "true";
  szNameItem      = "";
  szNamePrompt    = "";
  iHelp           = 0;
  iProgItem       = iIdentProgram;//1;
  Parm            = "";
end;

class TWsProgramsWithMenu
  var Subsystem : TArray;
  var MenuItem  : TArray;
  var DefaultSubsystem : integer = 0; // Подсистема по-умолчанию
end;

class TWsMacro(m,p)
  var macrofile : string = m; //имя макроса
  var macroproc : string = p; //имя процедуры
end;

private macro _GetListMenuItem()
    var cmd = TRslOraQuery();
    var params: TArray;
    var MenuItem: TWsMenuItem;
    var ListMenuItem = TArray();
    var NextIndex;
    var WsMacro: /*TWsMacro*/WebServiceCallback;

    CaptureOutput;
[
WITH t AS (select rownum rn, id_nmenu, groupname, id_nmenugroup, id_nmenuparent,
                  numgr, num, hint, code, macrofileweb macrofile, macroprocweb macroproc
             from (select a.id_nmenu, a.groupname, a.id_nmenugroup, a.id_nmenuparent, a.num, a.numgr,
                          a.hint, a.code, a.macrofileweb, a.macroprocweb
                     from (select t.id_nmenu, t.menuname as groupname, null as id_nmenugroup, t.id_nmenugroup as id_nmenuparent,
                                  t.num, null as numgr, t.hint, s.code, t.macrofileweb, t.macroprocweb
                             from table(RSP_FORM.Get_MenuForm_By_User) t, smenu s
                             where t.type = 0 and t.id_smenu = s.id_smenu
                               and t.id_smenu in (:smenu1,:smenu2,:smenu3,:smenu4,:smenu5,:smenu6,:smenu7,:smenu10,:smenu20)
                           union
                           select null, groupname, id_nmenugroup, id_nmenuparent,
                                  num, num, null, null, null, null
                             from nmenugroup
                            where id_nmenugroup in(select id_nmenugroup
                                                     from table(RSP_FORM.Get_MenuForm_By_User))
                              and id_nmenugroup in(select t.id_nmenugroup
                                                     from table(RSP_FORM.Get_MenuForm_By_User) t, smenu s
                                                    where t.type = 0 and t.id_smenu = s.id_smenu
                                                      and t.id_smenu in (:smenu1,:smenu2,:smenu3,:smenu4,:smenu5,:smenu6,:smenu7,:smenu10,:smenu20))
                           union
                           select null, '|', id_nmenugroup, id_nmenuparent,
                                  num, num, null, null, null, null
                             from nmenugroup
                            where regexp_like (groupname, '-{2}')
                           union
                           select null, groupname, id_nmenugroup, id_nmenuparent,
                                  num, num, null, null, null, null
                             from nmenugroup
                            where groupname in(select mainname
                                                 from table(RSP_FORM.Get_MenuForm_By_User))
                              and id_nmenugroup in(select id_nmenuparent
                                                     from nmenugroup
                                                    where id_nmenugroup in(select id_nmenugroup
                                                                             from table(RSP_FORM.Get_MenuForm_By_User))
                                                      and id_nmenugroup in(select t.id_nmenugroup
                                                                             from table(RSP_FORM.Get_MenuForm_By_User) t, smenu s
                                                                            where t.type = 0 and t.id_smenu = s.id_smenu
                                                                              and t.id_smenu in (:smenu1,:smenu2,:smenu3,:smenu4,:smenu5,:smenu6,:smenu7,:smenu10,:smenu20)))) a
                   start with a.id_nmenuparent is null
                   connect by prior a.id_nmenugroup = a.id_nmenuparent
                   order siblings by a.numgr, a.num))
  SELECT t2.id_nmenu,
         rn iNumberPoint,
         CASE WHEN t2.id_nmenuparent IS NULL THEN NULL
              ELSE (select rn
                      from t t1
                      where t1.id_nmenugroup = t2.id_nmenuparent)
              END iNumberParent,
         t2.num iNumberLine,
         replace(t2.groupname, '&', null) szNameItem,
         t2.hint, t2.code, t2.macrofile, t2.macroproc
    FROM t t2, nmenu m
   WHERE t2.id_nmenu = m.id_nmenu(+)
     AND decode(t2.code, 10, length(t2.macrofile), 0) = nvl(length(t2.macrofile), 0)
];
    cmd.sqlText = StopCaptureOutput();

    cmd.DeclareAndSet("smenu1", TypeMainMenuAllDoc, "smenu2", TypeMainMenuState, "smenu3", TypeMainMenuVisa,
                      "smenu4", TypeMainMenuPosit, "smenu5", TypeMainMenuMonitor, "smenu6", TypeMainMenuOpDay,
                      "smenu7", TypeMainMenuClDay, "smenu10", TypeMainMenuMacro, "smenu20", TypeMainMenuDict);

    if(not cmd.execute)
        runError(toansi(cmd.error));
    end;

    while(not cmd.Eof)
        MenuItem = TWsMenuItem();

        MenuItem.iNumberPoint  = /*MenuItem.iProgItem =*/ cmd.GetByName("iNumberPoint").AsInteger;
        MenuItem.iNumberParent = cmd.GetByName("iNumberParent").AsInteger;
        MenuItem.iNumberLine   = cmd.GetByName("iNumberLine").AsInteger;
        MenuItem.szNameItem    = cmd.GetByName("szNameItem").AsString;
        MenuItem.szNamePrompt  = cmd.GetByName("HINT").AsString;
        MenuItem.iCaseItem     = cmd.GetByName("code").AsInteger;

        if ((MenuItem.iCaseItem == 1) // Все документы
            or (MenuItem.iCaseItem == 2) // Документы в состоянии
            or (MenuItem.iCaseItem == 20)) // Справочник
            WsMacro = WebServiceCallback("ws_window", "GetScrolling", cmd.GetByName("ID_NMENU").AsInteger);
        elif (MenuItem.iCaseItem == 3) // Визирование
            WsMacro = WebServiceCallback("ws_window", "GetScrollingViz", cmd.GetByName("ID_NMENU").AsInteger);
        elif (MenuItem.iCaseItem == 4) // Позиционирование
            WsMacro = WebServiceCallback("ws_positioning", "GetPositioningWindow", cmd.GetByName("ID_NMENU").AsInteger);
        elif (MenuItem.iCaseItem == 5) // Монитор
            WsMacro = WebServiceCallback("ws_monitor", "GetScrollingMonitor", cmd.GetByName("ID_NMENU").AsInteger);
        elif (MenuItem.iCaseItem == 6) // Открытие дня
            WsMacro = WebServiceCallback("ws_opencloseday", "GetOpenDayPanel", cmd.GetByName("ID_NMENU").AsInteger);
        elif (MenuItem.iCaseItem == 7) // Закрытие дня
            WsMacro = WebServiceCallback("ws_opencloseday", "GetCloseDayPanel", cmd.GetByName("ID_NMENU").AsInteger);
        elif (MenuItem.iCaseItem == 10) // Пункт меню макрос
//            MenuItem.Parm = cmd.GetByName("macrofile").AsString + "." + cmd.GetByName("macroproc").AsString;
//            WsMacro = TWsMacro(cmd.GetByName("macrofile").AsString, cmd.GetByName("macroproc").AsString);
            WsMacro = WebServiceCallback(cmd.GetByName("MacroFile").AsString, cmd.GetByName("MacroProc").AsString);
        else
            MenuItem.Parm = cmd.GetByName("id_nmenu").AsInteger;
        end;

        MenuItem.Parm = Rsl2Xml(WsMacro);
        ListMenuItem[ListMenuItem.size] = MenuItem;

        cmd.next();
    end;

    return ListMenuItem;
end;

macro GetProgramsWithMenu()

  var ProgramsWithMenu = TWsProgramsWithMenu;
  var Subsystem  = TWsSubsystem();
  var SubsystemList = Tarray();

  SubsystemList[SubsystemList.size] = Subsystem;

  ProgramsWithMenu.Subsystem        =  SubsystemList;
  ProgramsWithMenu.MenuItem         = _GetListMenuItem();
  ProgramsWithMenu.DefaultSubsystem =  CodeFor(Subsystem.Symbol); //_GetDefaultSubsystem();

  return ProgramsWithMenu;
end;

macro main()
var p = _GetListMenuItem(), i = 0;

  while(i < p.size)
     addptk(p[i].szNameItem);
     i = i + 1;
  end;

end;
