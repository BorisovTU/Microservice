/*09.12.2013 voikin*/
import mb_general, DELZERO, unistr;

const EV_FILEERR = 80;/*Файл перемещен в каталог ERR*/

/*Формирование Емайл-нотификации*/
macro InsertEmailNotifyMB(EmailGroup, Head, Text)
  if(EmailGroup)
      var cmd = TRslOraQuery();
      cmd.SqlText = "begin RSB_PAYMENTS_API.InsertEmailNotify(:EmailGroup, :Head, :Text); end;";
      cmd.DeclareAndSet("EmailGroup", EmailGroup, "Head", Head);
      cmd.DeclareAndSetLob("Text", Text);
      if (not cmd.Execute())
         AddPtk ("Ошибка отправки e-mail - "+cmd.Error);
      end;
  end;
  return true;
end;

//чтение файла и вызов процедуры разбора
Macro mb_LoadMultiMsgFile(NameFile, GateId, prc, isUTF, NameFile_,dt)

  var Ret = 0;
  var SQLErr = "";
  var InpStr="";

  if (isUTF) // UTF
    InpStr = ReadUniStr(NameFile,0 /*, 0 - ANSI, 1 - OEM по умолчанию*/);
  else       //1251
    InpStr  = LoadFileString(NameFile);
  end;

  //убираем кавычки 1251
  InpStr = StrSubst(InpStr, "л","");
  InpStr = StrSubst(InpStr, "╗","");
  InpStr = tooem(InpStr);

  var qry = TRslOraQuery();


  if(VALTYPE(dt)==V_DATE)
     qry.SqlText = " begin  :Ret :="+prc+"(:NAME_FILE, :IMP_STRING, :DT); end;";
     qry.DeclareAndSet("Ret", Ret, "NAME_FILE", NameFile_, "DT", dt);
  else
     qry.SqlText = " begin  :Ret :="+prc+"(:NAME_FILE, :IMP_STRING); end;";
     qry.DeclareAndSet("Ret", Ret, "NAME_FILE", NameFile_);
  end;

  qry.DeclareAndSetLOB("IMP_STRING", InpStr);

  if(not qry.Execute)
    SQLErr = "\r\n"+qry.Error;
    RunError(toansi(GateId+" Ошибка в процедуре разбора файла"));
  end;

  Ret = qry.GetVariable("Ret");

  OraTrnCommit();

  if(Ret==1)
    AddPtk(GateId+" : повторный приход файла "+NameFile_);
  else
    AddPtk(GateId+" : загружен файл "+NameFile_);
  end;
  return true;

  OnError(e)
    OraTrnRollBack();
    AddPtk (GateId+" : " + ToOem(e.Message)+ SQLErr + " Модуль " + e.Module + " Строка " + e.Line);

    InsertEmailNotifyMB(EmailGroupMB,GateId +" - ошибка приема файла "+NameFile_, ToOem(e.Message)+ SQLErr);
    //MsgBox(ToOem(e.Message));
    return false;
End;






// меняет расширение в имени файла
macro chext (filename, ext)
   var i = strlen(filename);
   while (i>0)
     if( substr(filename,i,1)==".")
        filename= substr(filename,1,i-1);
        break;
     end;
     i=i-1;
   end;

  return filename+"."+ext;
end;




//cоздание пустого файла
macro crfile(GateId, fullname)
            If (ExistFile (fullname) and DeleteFile(fullname))
              AddPtk(GateId+":Удалён файл "+fullname);
            End;

            if(not WriteStrToFile("", fullname))
              AddPtk(GateId+":Ошибка при записи в файл "+fullname);
            end;
end;

//поиск файлов по маске
Macro mb_ImpFiles(GateId, prc, isUTF, ImpDir, ArcDir, ErrDir, ImpFileNameMask, dt)

  var NameFile_ = ImpDir + ImpFileNameMask;


  Var OnceMoreFile = FindFirst(NameFile_), newNameFile, errNameFile, OrigNameFile_;
  var ID_Event;

  if(OnceMoreFile)
    MakeDir(substr(ArcDir,1,strlen(ArcDir)-1));
    MakeDir(substr(ErrDir,1,strlen(ErrDir)-1));
  end;

  While(OnceMoreFile)
    newNameFile  = ArcDir+NameFile_;
    errNameFile  = ErrDir+NameFile_;
    OrigNameFile_ = ImpDir+NameFile_;
    If (ExistFile (newNameFile) and DeleteFile(newNameFile))
      AddPtk(GateId+":Удалён файл "+newNameFile);
    End;

    If (MoveFile( OrigNameFile_, newNameFile))
      AddPtk(GateId+":Перемещен файл "+NameFile_+ " из "+ImpDir+" в "+ArcDir);
      If(not mb_LoadMultiMsgFile(newNameFile, GateId, prc, isUTF, NameFile_, dt))
        /* нужно попытаться перенести файл в каталог файлов с ошибками */
        If (ExistFile (ErrNameFile) and DeleteFile(ErrNameFile))
          AddPtk(GateId+":Удалён файл "+ErrNameFile);
        End;

        If (MoveFile(newNameFile, ErrNameFile))
           AddPtk(GateId+":Файл "+NameFile_+" из "+ArcDir+" перемещен в "+ErrDir);
           /*Создание события*/

           crfile(GateId, ImpDir+chext(NameFile_,"err"));

/*
           ID_Event = CreatJrnEventMail(EV_FILEERR,
                                        ToANSI(GateId+":Файл "+NameFile_+" из "+ArcDir+" перемещен в "+ErrDir),
                                       "",
                                       "",
                                       1);

           if(not ID_Event)
             addptk(GateId+":Ошибка при создании события");
           end;
*/
        Else
           addptk(GateId+":Ошибка при переносе файла "+NameFile_+" из "+ArcDir+" в "+ErrDir);
        End;
      else //все хорошо
            crfile(GateId, ImpDir+chext(NameFile_,"ok"));
      End;
    Else
      addptk(GateId+":Ошибка переноса файла "+NameFile_+" из "+ImpDir+" в "+ArcDir);
    End;
    OnceMoreFile = FindNext(NameFile_);
  End;
End;

//возвращает строку вида DDMMYY на основе системной даты
MACRO Date2Dir (dat, delim)
  var DD, MM, YY;

  DateSplit(dat, DD, MM, YY);
  YY = String(10000 + YY);
  DD = String(100 + DD);
  MM = String(100 + MM);

  if(VALTYPE(delim)!=V_STRING)
     delim = "";
  end;
  return    SubStr(DD, 2, 2)+delim+SubStr(MM, 2, 2)+delim+SubStr(YY, 4, 2);
END;

//основная функция загрузки файлов МБ
macro LoadMB()
  var i=0;
  var dt = date() - 1;
  var datedir = AddSl(Date2Dir(dt));
  while (i<ASize(MBImpFileNameMask))
     mb_ImpFiles("Шлюз МБ","RSP_MB.LOAD_MB", false /*не UTF*/, MBDirIn+datedir, MBDirIn+datedir+MBDirArc, MBDirIn+datedir+MBDirErr, MBImpFileNameMask[i]);
     i=i+1;
  end;
end;


//основная функция загрузки файлов Валютного рынка
macro LoadCM()
  var i=0;
  var dt = date() - 1;
  var datedir = AddSl(Date2Dir(dt));
  while (i<ASize(CMImpFileNameMask))
     mb_ImpFiles("Шлюз ВР","RSP_CURRMARKET.Load_CurrMarket", false /*не UTF*/, CMDirIn+datedir, CMDirIn+datedir+CMDirArc, CMDirIn+datedir+CMDirErr, CMImpFileNameMask[i]);
     i=i+1;
  end;
end;


macro getListDir(ph)
  var Dir = TArray();
  var atr = 16; //искать еще и каталоги

  Var OnceMoreDir = FindFirst(ph , atr);
  while(OnceMoreDir)
    if((CheckBits (atr,16) == 16 )and(ph!=".")and(ph!=".."))
       Dir.Value(Dir.Size)= ph;
       addptk("Найден каталог "+ph+" " + atr);
    end;
    OnceMoreDir = FindNext(ph,atr);
  end;
  return  Dir;
end;


macro Dir2Date(dateStr)
   var yy, mm, dd;
   yy = Int(SubStr(dateStr, 7, 2));
   mm = Int(SubStr(dateStr, 4, 2));
   dd = Int(SubStr(DateStr, 1, 2));
   If (yy > 80)
      yy = yy + 1900;
   Else
      yy = yy + 2000;
   End;

   return date(dd,mm,yy);
onerror (e)
  addptk ("Dir2Date: ошибка при преобразовании даты =" + dateStr);
  runerror (e.Message);
end;




//основная функция загрузки файлов Срочного рынка
macro LoadFM()
  var i=0;
  var dt = date() - 1;
  var datedir = AddSl(Date2Dir(dt,"."));
  var Dir;
  var y = 0;

  while (i<ASize(FMImpFileNameMask))
     mb_ImpFiles("Шлюз СР","RSP_FMARKET.Load_FMarket", false /*не UTF*/, FMDirIn+datedir, FMDirIn+datedir+FMDirArc, FMDirIn+datedir+FMDirErr, FMImpFileNameMask[i], dt);
     i=i+1;
  end;

//а теперь по каталогам
  Dir =  getListDir(FMDirIn+datedir+"??.??.??");
  while (y<Dir.Size)
     dt= Dir2Date(Dir.Value(y));
     i=0;

     while (i<ASize(FMImpFileNameMask))
        mb_ImpFiles("Шлюз СР","RSP_FMARKET.Load_FMarket", false /*не UTF*/, FMDirIn+datedir+AddSl(Dir.Value(y)), FMDirIn+datedir+AddSl(Dir.Value(y))+FMDirArc, FMDirIn+datedir+AddSl(Dir.Value(y))+FMDirErr, FMImpFileNameMask[i], dt);
        i=i+1;
     end;

     y=y+1;
  end;
end;




//получить дату из имени архива
macro getdatefromName (namefil,delim)
//debugbreak;
   namefil = strsubst(namefil,"-","");
   var i=1;
   var dat="";
   var tmp;

   if(ValType(delim)!=V_STRING)
     delim="";
   end;

   while (i< strlen(namefil))
       tmp = substr(namefil,i,1);
       if((tmp >= "0" ) and (tmp<="9"))
         dat=dat+tmp;
         if(strlen(dat)==8)
           break;
         end;
       else
          dat="";
       end;
       i=i+1;
   end;


   if(strlen(dat)==8)
     return substr(dat,7,2)+delim+substr(dat,5,2)+delim+substr(dat,3,2);
   else
     return "";
   end;
end;


//распаковка одного файла
macro  unZipFile(newNameFile, NameFile_,DirOut, nameLog, delim)
  var datdir =  getdatefromName(NameFile_,delim);
  var ret = 0;
  var InpStr  = "";

  if(datdir=="")
      AddPtk("Распаковка архивов: Невозможно получить дату из имени файла "+ NameFile_);
      return false;
  end;

  if(ValType(nameLog)!=V_STRING)
     nameLog="7z.log";
  end;




//  AddPtk("Распаковка архивов: создние каталога "+ DirOut+datdir);
  MakeDir(DirOut+datdir);

//  addptk ("Команда - 7z e -y -o"+SPBDirIn+datdir+"\\ "+newNameFile);
  ret = spawn("7z e -y -o"+DirOut+datdir+"\\ "+newNameFile+ " > "+nameLog);

  AddPtk("Распаковка архивов: Ret- "+ Ret);
  InpStr  = LoadFileString(nameLog);

  if(ret)
    addptk ("Команда - 7z e -y -o"+DirOut+datdir+"\\ "+newNameFile);
    addptk(" протокол архиватора - "+InpStr);
    return false;
  else
    addptk(" протокол архиватора - "+InpStr);
    return true;
  end;
end;


//поиск архивов
macro ImpZip(DirIn,DirArc,DirErr,ZipMask,DirOut,nameLog, delim)
  var NameFile_ = DirIn + ZipMask;
  var newNameFile, errNameFile,OrigNameFile_;

  Var OnceMoreFile = FindFirst(NameFile_);


  While(OnceMoreFile)
    newNameFile   = DirIn+DirArc+NameFile_;
    errNameFile   = DirIn+DirErr+NameFile_;
    OrigNameFile_ = DirIn+NameFile_;


    If(not unZipFile(OrigNameFile_, NameFile_,DirOut, nameLog, delim))
      /* нужно попытаться перенести файл в каталог файлов с ошибками */
      If (ExistFile (ErrNameFile) and DeleteFile(ErrNameFile))
        AddPtk("Распаковка архивов: Удалён файл "+ErrNameFile);
      End;

      If (MoveFile(OrigNameFile_, ErrNameFile))
         AddPtk("Распаковка архивов: Файл "+NameFile_+" из "+DirIn+" перемещен в "+DirIn+DirErr);
      Else
         addptk("Распаковка архивов: Ошибка при переносе файла "+NameFile_+" из "+DirIn+" в "+DirIn+DirErr);
      End;
    else //все хорошо
      If (ExistFile (newNameFile) and DeleteFile(newNameFile))
        AddPtk("Распаковка архивов: Удалён файл "+newNameFile);
      End;
      If (MoveFile(OrigNameFile_, newNameFile))
        AddPtk("Распаковка архивов: Перемещен файл "+NameFile_+ " из "+DirIn+" в "+DirIn+DirArc);
      Else
        addptk("Распаковка архивов: Ошибка переноса файла "+NameFile_+" из "+DirIn+" в "+DirIn+DirArc);
      End;

      if(index(NameFile_,".p7s"))
        //переносим и файл с серимфикатом
        newNameFile=newNameFile+".p7e";
        NameFile_=NameFile_+".p7e";
        OrigNameFile_=OrigNameFile_+".p7e";
        if(ExistFile (OrigNameFile_))
            If (ExistFile (newNameFile) and DeleteFile(newNameFile))
              AddPtk("Распаковка архивов: Удалён файл "+newNameFile);
            End;
            If (MoveFile(OrigNameFile_, newNameFile))
              AddPtk("Распаковка архивов: Перемещен файл "+NameFile_+ " из "+DirIn+" в "+DirIn+DirArc);
            Else
              addptk("Распаковка архивов: Ошибка переноса файла "+NameFile_+" из "+DirIn+" в "+DirIn+DirArc);
            End;
        end;
      end;

    End;

    OnceMoreFile = FindNext(NameFile_);
  End;
end;

//основная функция распаковки файлов архивов СПБ
macro UnZipSpb()
  var i=0;

  MakeDir(substr(SPBZipDirIn+SPBZipDirArc,1,strlen(SPBZipDirIn+SPBZipDirArc)-1));
  MakeDir(substr(SPBZipDirIn+SPBZipDirErr,1,strlen(SPBZipDirIn+SPBZipDirErr)-1));

  while (i<ASize(SPBZipFileNameMask))
     ImpZip(SPBZipDirIn,SPBZipDirArc,SPBZipDirErr,SPBZipFileNameMask[i],SPBDirIn,"7z_SPB.log");
     i=i+1;
  end;
end;


//основная функция распаковки файлов архивов срочного рынка
macro UnZipFM()
  var i=0;

  MakeDir(substr(FMZipDirIn+FMZipDirArc,1,strlen(FMZipDirIn+FMZipDirArc)-1));
  MakeDir(substr(FMZipDirIn+FMZipDirErr,1,strlen(FMZipDirIn+FMZipDirErr)-1));

  while (i<ASize(FMZipFileNameMask))
     ImpZip(FMZipDirIn,FMZipDirArc,FMZipDirErr,FMZipFileNameMask[i],FMDirIn,"7z_FM.log",".");
     i=i+1;
  end;
end;


//основная функция загрузки файлов СПБ
macro LoadSPB()
  var i=0;
  var datedir = AddSl(Date2Dir(date()-1));
  var IsUtf;
  while (i<ASize(SPBImpFileNameMask))
     if(index(SPBImpFileNameMask[i],".txt"))
       IsUtf=false;
     else
       IsUtf=true;
     end;

     mb_ImpFiles("Шлюз СПБ","RSP_SPB.Load_SPB", IsUtf /*UTF*/, SPBDirIn+datedir, SPBDirIn+datedir+SPBDirArc, SPBDirIn+datedir+SPBDirErr, SPBImpFileNameMask[i]);
     i=i+1;
  end;
end;
