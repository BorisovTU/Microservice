/*01.08.2014 voikin*/
import Utils;


/*Настройки*/
const DEF_FIELD_HEIGHT   = 21;/*высота поля по умолчанию*/
const DEF_FIELD_WIDTH    = 120;/*ширина поля по умолчанию*/
const DEF_WITH_OF_CHAR   = 6.5;/*ширина символа для вычисления положения лейбла, ширины кнопки и др.*/
const DEF_MIN_LEFT_INTRV = 5;/*Минимаотный интервал между контроллами*/
const ADD_PANEL_HEIGHT   = 70;/*высота доп. панели для кнопок*/
const ADD_WIDTH          = 70;/*на это значение увеличивается ширина формы*/

/**/
const BTN_START_LEFT = 0;
const BTN_START_CNTR = 1;


class FormField(_Name, _Table, _Edit, _Lable)
  var FName, FTable, FEdit, FLable;

  FName  = _Name;
  FTable = _Table;
  FEdit  = _Edit;
  FLable = _Lable;
end;

/*Создает форму по настройкам полей форм*/
/* id_nform - ИД формы, которую русуем, 
   ErrMsg - сообщение об ошибке, 
   Proc - имя макропроцедуры обработки событий формы, 
   _BtnArr - массив кнопок,
   _FldArr - массив полей, содержащий объекты класса FormField, 
   BtnPlaceMode - режим расположения кнопок: 0 - начинать распологать от левого края формы, 1 - начинать от середины,
   BtnName, BtnTag, ... - пары "Имя кнопки", "Tag кнопки"*/

/*Размеры кнопок вычисляются на основании длины названия кнопки*/
macro CreateForm(_id_nform, _ErrMsg, _Proc, _File, _BtnArr, _FldArr, _BtnPlaceMode, _BtnName, _BtnTag)
  var SQLtext;
  var q;
  var CODE, TEXT, TOP, LEFT, HEIGHT, WIDTH;
  var BtnLeft, BtnWidth;
  var LblTop, LblLeft;
  var FName, FTable, FEdit, FLable, FEditType;
  var LastTop, LastLeft, LastWidth;
  var BtnName, BtnTag;
  var i,p;
  var Scr = 0;

  Array BtnArr;
  Array FNmArr;
  Array FldArr;
  Array LblArr;

  SQLtext = " select t.code, t.text, nvl(t.top,0) as top, nvl(t.left,0) as left, nvl(t.height,0) as height, nvl(t.width,0) as width "+
            " from nform t "+
            " where t.id_nform = :p1 ";

  q = ExecQuery (SQLtext, _id_nform);

  if (not TstObj (q))
    SetParm(1, "Получение настроек формы ИД "+_id_nform+": ошибка в запросе");
    return null;
  end;

  if(q.eof)
    SetParm(1, "Форма с ИД "+_id_nform+" не найдена");
    return null;
  end;

  CODE   = int(q.rec.CODE);
  TEXT   = q.rec.TEXT;
  TOP    = int(q.rec.TOP);
  LEFT   = int(q.rec.LEFT);
  HEIGHT = int(q.rec.HEIGHT);
  WIDTH  = int(q.rec.WIDTH);

  if((not HEIGHT)or(WIDTH))
  /*Нужно вычислять*/
    SQLtext = " select MAX(t.top + case nvl(t.height,0) when 0 then :p1 else t.height end)as height, "+
              "        MAX(t.left + case nvl(t.width,0) when 0 then :p2 else t.width end) as width  "+     
              " from NFIELD t "+
              " where t.id_nform = :p3 "+
              " and t.isvisible = 1";

    q = ExecQuery (SQLtext, DEF_FIELD_HEIGHT, DEF_FIELD_WIDTH, _id_nform);

    if (not TstObj (q))
      SetParm(1, "Вычисление размеров формы ИД "+_id_nform+": ошибка в запросе");
      return null;
    end;

    if(not HEIGHT)
		  HEIGHT = int(q.rec.HEIGHT);
    end;

    if(not WIDTH)
		  WIDTH = int(q.rec.WIDTH);
    end;
  end;

  Scr = ScrCreate(TOP,LEFT,HEIGHT+ADD_PANEL_HEIGHT,WIDTH+ADD_WIDTH,TEXT,_Proc,_File);

  /*Кнопки*/
  i = 7; /*Названия кнопок берем с 7-го параметра*/
  if(_BtnPlaceMode == BTN_START_CNTR)
    BtnLeft = int((WIDTH+ADD_WIDTH)/2);
  else
    BtnLeft = DEF_MIN_LEFT_INTRV;
  end;

  while (GetParm (i, BtnName) and GetParm (i+1, BtnTag))
    BtnWidth = int(DEF_WITH_OF_CHAR+StrLen(BtnName)*DEF_WITH_OF_CHAR+DEF_WITH_OF_CHAR);
    BtnArr(ASize(BtnArr)) = ScrAddButton(Scr, HEIGHT+10 , BtnLeft ,  0, BtnWidth, BtnName, BtnTag);
    BtnLeft = BtnLeft + BtnWidth + DEF_MIN_LEFT_INTRV;
    i = i + 2;
  end;


  /*Эдиты и Лейблы к ним*/
  SQLtext = " select t.id_nfield, t.name,(select tb.name from ntable tb where tb.id_ntable=t.id_ntable) as tbname, nvl(t.text,'') as text,"+
            " t.istextup, t.orderfield, t.top, t.left, (case nvl(t.height,0) when 0 then :p1 else t.height end) as height,"+
            " (case nvl(t.width,0) when 0 then :p2 else t.width end ) as width, t.isedit, t.id_sright, t.id_stypefield "+
            " from NFIELD t "+
            " where t.id_nform = :p3 "+
            " and t.isvisible = :p4 "+
            " order by t.top, t.left";/*Обязательно сортируем для отрисовки поля по положению на форме, чтобы корректировать текст по отношению к предыдущему полю*/

  q = ExecQuery (SQLtext, DEF_FIELD_HEIGHT, DEF_FIELD_WIDTH, _id_nform, 1);

  if (not TstObj (q))
    SetParm(1, "Получение настроев полей формы ИД "+_id_nform+": ошибка в запросе");
    return null;
  end;

  LastTop   = 0;
  LastLeft  = 0;
  LastWidth = 0;

  while(not q.Eof)
    FName  = StrUpr(q.rec.Name);
    FTable = StrUpr(q.rec.tbname);

//    FEditType = int(q.rec.id_stypefield);
    FEditType = GetColByCol("STYPEFIELD", "CODE", "ID_STYPEFIELD", int(q.rec.id_stypefield));
    if(FEditType > 6)/*Другие типы не поддерживаются, пускай будет "строка"*/
      FEditType = 1;
    end;

    FEdit  = ScrAddEdit(Scr, int(q.rec.top), int(q.rec.Left), int(q.rec.Height), int(q.rec.Width), "", FEditType );

//    if(StrLen(q.rec.Text))
    if(int(q.rec.istextup))
      LblTop  = int(q.rec.top)-DEF_FIELD_HEIGHT; 
      LblLeft = int(q.rec.Left);
    else
      LblTop  = int(q.rec.top); 
      LblLeft = int(q.rec.Left)-int(StrLen(Trim(q.rec.Text))*DEF_WITH_OF_CHAR+DEF_WITH_OF_CHAR);

      /*поправки координат лейбла*/
      if((LblTop == LastTop)and(LblLeft < LastLeft+LastWidth+DEF_MIN_LEFT_INTRV))/*Если находимся на "уровне" предыдущего эдита и "залазим" на него*/
        LblLeft = LastLeft+LastWidth+DEF_MIN_LEFT_INTRV;
      elif(LblLeft <= 0)/*выходим за границы*/
        LblLeft = DEF_MIN_LEFT_INTRV;
      end;

    end;

    LblTop = LblTop + 5;/*чтобы текст не был "подпрыгнутым"*/

    FLable = ScrAddLabel(Scr, LblTop, LblLeft, Trim(q.rec.Text));

    FldArr(ASize(FldArr)) = FormField(FName, FTable, FEdit, FLable);
//    end;

    LastTop   = int(q.rec.top);
    LastLeft  = int(q.rec.left);
    LastWidth = int(q.rec.width);

    q.Next();
  end;

  SetParm(4, BtnArr);
  SetParm(5, FldArr);

  return Scr;
end;
