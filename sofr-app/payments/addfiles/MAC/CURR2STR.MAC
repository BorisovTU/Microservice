/* ╓────────────────────────── R-Style Ukraine ────────────────────────────╖*/
/* ║ NAME:      : CurrToStr.mac                                            ║*/
/* ║ DESCRIPTION: Макрос принимает число и код валюты и возвращает тексто- ║*/
/* ║ DESCRIPTION: вый эквивалент валюты. Если передается число INT - то    ║*/
/* ║ DESCRIPTION: принимается что эта сумма - в копейках, если DOUBLE или  ║*/
/* ║ DESCRIPTION: MONEY - копейками считаются две цифры после запятой -    ║*/
/* ║ DESCRIPTION: остальные знаки отсекаются. Если не указан код валюты -  ║*/
/* ║ DESCRIPTION: считается, что код валюты - 0.                           ║*/
/* ║ DESCRIPTION: CUR2TEXT.DBF и NUM2TEXT.DBF должны находится в директории║*/
/* ║ DESCRIPTION: \MAC\, или в теле макроса пропишите путь этим файлам.    ║*/
/* ║ USED FILES : CUR2TEXT.DBF - коды и названия всех валют с падежами,    ║*/
/* ║ USED FILES :        коды валют должны совпадать с используемыми в ОДБ,║*/
/* ║ USED FILES :        поле SEX - "пол" валюты (T-муж., F-жен.)          ║*/
/* ║ USED FILES : NUM2TEXT.DBF - символьное представление всех чисел       ║*/
/* ║ BY:        : Kravtchenko S.F.                                         ║*/
/* ║ DATES:     : 22.04.97                                                 ║*/
/* ║ MODIFY:    : 09.01.98 - заглавная буква преобразуется в верхний р-стр ║*/
/* ╙───────────────────────────────────────────────────────────────────────╜*/
/* ╓───────────────────────────────────────────────────────────────────────╖*/
/* ║ Functions:                                                            ║*/
/* ║ CurrToStr -                                                           ║*/
/* ║ └─ GetTxtNum - возвращает символьный эквивалент для одного числа из   ║*/
/* ║                базы NUM2TEXT.DBF.                                     ║*/
/* ╙───────────────────────────────────────────────────────────────────────╜*/
file FNum  () DBF;
file FCurr () DBF;
open (FNum,  GetInit("MACRODIR") + "\\num2text.dbf") ;
open (FCurr, GetInit("MACRODIR") + "\\cur2text.dbf");

MACRO GetTxtNum(s)
   rewind(FNum);
   while(next(FNum))
      if (FNum.kod == s)
         return trim(FNum.txt);
      end;
   end;
   msgbox(string(s," - такого числа не найдено в базе NUM2TEXT.DBF"));
   return " ";
END;


MACRO CurrToStr (Summ, Curr)
   
   type = ValType(Summ);
   if   (type == 1)                              /* V_INTEGER */
      S = double(Summ/100);                      /* целое в копейках */
   elif ((type == 2) or (type == 4))             /* V_MONEY  */
      S = double(Summ/100);
   elif ((type == 3) or (type == 5))             /* V_DOUBLE  */
      S = Summ;
   elif (type == 6)                              /* V_STRING */
      S = double(Summ);
   else
      MsgBox(string("Не числовой параметр в функции CurrToStr - ", Curr));
      return " ";
   end;

   sOut = "";
   Array aSumm;
   StrSumm = trim(string(S));
   i = 0;
   fl = True;
   while (fl)                    /* Заполняем массив aSumm цифрами по одной */
      c = SubStr(StrSumm, i+1, 1);
      if (c != ".")
         aSumm(i) = c;
      else
         fl = False;
         kop = int(SubStr(StrSumm, i+2, 2));
      end;
      i = i + 1;
   end;

   iLastNum = int(aSumm(i-2));
   if (i > 2)
      iLast2Num = int(aSumm(i-3))*10 + iLastNum;
   else
      iLast2Num = iLastNum;
   end;

   type = ValType(Curr);
   if   (type == 0)          /* Если код валюты не указан - печатаем гривню */
      Curr = 0;
   else
      Curr = int(double(Curr));
   end;

   fl = false;
   rewind(FCurr);                            /* Ищем название валюты */
   while(next(FCurr))
      if (FCurr.kod == Curr)
         fl = true;
         bSex = FCurr.sex;
         a1 = trim(FCurr.txt1);
         a2 = trim(FCurr.txt2);
         a3 = trim(FCurr.txt3);
         k1 = trim(FCurr.kop1);
         k2 = trim(FCurr.kop2);
         k3 = trim(FCurr.kop3);
      end;
   end;
   if (not fl)
      MsgBox(string("Не найден код валюты ", Curr, ". Настройте файл CUR2TEXT.DBF!!!"));
      return " ";
   end;

/* Хвост возвращаемой строки */
   if ((iLast2Num > 4) and (iLast2Num < 20))
      sOut = a3;
   else
      if ((iLastNum == 0) or (iLastNum > 4))
         sOut = a3;
      elif (iLastNum == 1)
         sOut = a1;
      else
         sOut = a2;
      end;
   end;

   if (S == 0)                        /* Если НОЛЬ */
      return (GetTxtNum(0) + " " + sOut);
   end;

/****************************************************************************/
/****************************************************************************/
/* Собственно конвертация Currency -> Srt */
   iTri = 0;                                              /* номер "тройки" */
   iLen = aSize(aSumm)-1;
   if((iLen==0) and (aSumm(0)=="0"))
    sOut=GetTxtNum(0)+" "+sOut;
   end;
   k = 1;                 /* коэф. для вставки в строку слов "тысяча" и т.д.*/
   while (iTri*3 <= iLen)                   /* a1,a2,a3 - "тройка" чисел    */
      d = iLen - iTri*3;                    /* которые будут конвертированы */
      a3 = aSumm(d);
      i3 = int(a3);
      if (d > 0) a2 = aSumm(d-1);
      else       a2 = "0";
      end;
      if (d > 1) a1 = aSumm(d-2);
      else       a1 = "0";
      end;

      if (iTri != 0)
        k = k*1000;
        if (int(a1+a2+a3) != 0)                  /*Если 3 нуля - пропускаем */
          if (int(a2) == 1)            str = GetTxtNum(k+2);  /* сдвиги для */
          elif (i3 == 1)               str = GetTxtNum(k);    /*   падежей  */
          elif ((i3 == 0) or (i3 > 4)) str = GetTxtNum(k+2);  /* для тысяч, */
          else                         str = GetTxtNum(k+1);  /* милл.и т.д.*/
          end;
          sOut = str + " " + sOut;
        end;
      end;

/* ОСНОВНОЙ ЦИКЛ !!! */
      if (int(a3) == 0)
         if (int(a2) == 0)
            if (int(a1) == 0)
            else
               str = GetTxtNum(int(a1+"00"));
               sOut = str + " " + sOut;
            end;
         else
            str = GetTxtNum(int(a2+"0"));
            sOut = str + " " + sOut;
            if (int(a1) != 0)
               str = GetTxtNum(int(a1+"00"));
               sOut = str + " " + sOut;
            end;
         end;
      elif (int(a2) == 1)
         str = GetTxtNum(int(a2+a3));
         sOut =  str + " " + sOut;
         if (int(a1) != 0)
            str = GetTxtNum(int(a1+"00"));
            sOut =  str + " " + sOut;
         end;
      else
         a = int(a3);
         if   (iTri == 0)         /* Если жен.род и последняя цифра 1 или 2 */
            if (bSex == False)
               if ((a == 1) or (a == 2))
                  a = -a;
               end;
            end;
         elif (iTri == 1)                /* тысячи жен.рода */
            if ((int(a3) == 1) or (int(a3) == 2))
               a = -a;
            end;
/*          elif (iTri == 2) ;                                /* миллионы  */ */
/*          elif (iTri == 3) ;                                /* миллиарды */ */
/*          elif (iTri == 3) ;                                /* трильоны  */ */
         end;
         str = GetTxtNum(a);
         sOut = str + " " + sOut;
         if (int(a2) != 0)
            str = GetTxtNum(int(a2+"0"));
            sOut = str + " " + sOut;
         end;
         if (int(a1) != 0)
            str = GetTxtNum(int(a1+"00"));
            sOut = str + " " + sOut;
         end;
      end;
/* КОНЕЦ ОСНОВНОГО ЦИКЛА !!! */
      iTri = iTri + 1;
   end;

   if(strlen(sOut)>1)
    Head = SubStr (sOut, 1, 1);
    if (Head == "о")
      Head = "О";
    elif (Head == "д")
      Head = "Д";
    elif (Head == "т")
      Head = "Т";
    elif (Head == "ч")
      Head = "Ч";
    elif (Head == "п")
      Head = "П";
    elif (Head == "ш")
      Head = "Ш";
    elif (Head == "с")
      Head = "С";
    elif (Head == "в")
      Head = "В";
    elif (Head == "н")
      Head = "Н";
    end;
    sOut = Head + SubStr (sOut, 2);
   end;
/* Привязываем копейки и возврат */
   if (kop < 10)
      sOut = sOut + " 0" + string(kop) + " ";
   else
      sOut = sOut + " " + string(kop) + " ";
   end;
   kop = kop - (kop/10)*10;
   if ((kop == 0) or (kop > 4))
      sOut = sOut + k3;
   elif (kop == 1)
      sOut = sOut + k1;
   else
      sOut = sOut + k2;
   end;
   return sOut;

END;
