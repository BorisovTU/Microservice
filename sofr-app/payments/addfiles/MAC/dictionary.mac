import vector;

macro PrintElement(pair)
    println("key = ", pair.Key, " Value = ", pair.Value);
end;

class static_class_DictionaryUtils
    class KVPair(k, v)
        var Key = k;
        var Value = v;
    end;

    macro Pair(k, v)
        return KVPair(k, v);
    end;
end;
const DictUtils = static_class_DictionaryUtils();

class Dictionary(keyComp)

    private var vec = vector();
    private var keyCompFunc = keyComp; // функция сравнения ключа

    macro GetKeyComparer()
        return keyCompFunc;
    end;

    macro SetKeyComparer(comp)
        keyCompFunc = comp;
    end;

    macro DictionaryCompareByKey(key, pair)
        if (keyCompFunc != null)
            return ExecMacro2(keyCompFunc, key, pair.Key);
        end;
        
        if (pair.Key == key)
            return 0;
        end;
        return -1
    end;

    macro Add(k, v)
        var i = vec.lsearch(k, R2M(this, "DictionaryCompareByKey"));
        var pair = null;
        if (i != -1)
            vec[i].Value = v;
        else
            pair = DictUtils.Pair(k, v);
            vec.push_back(pair);
        end;
        return this;
    end;

    macro Count()
        return vec.size;
    end;

    macro Contains(k)
        var i = vec.lsearch(k, R2M(this, "DictionaryCompareByKey"));
        return i != -1;
    end;

    macro Get(k)
        var i = vec.lsearch(k, R2M(this, "DictionaryCompareByKey"));
        if (i == -1)
            //RunError("Ключ не найден")
            return null;
        end;
        return vec[i];
    end;

    macro print(fn)
	if (fn == null)
		fn = "PrintElement";
	end;
        vec.for_each(fn);
    end;

    macro ToArray()
        return vec.to_tarray();
    end;
end;

/*
var d = Dictionary();
d.Add(10, 10);
d.Add(10, 20);
d.Add(20, 10);
d.Add(30, 10);
d.print();
if (d.Contains(10))
    println("Contains");
end;

var pair = d.Get(20);
pair.Value = 77;
d.print();
var x = 0;
*/