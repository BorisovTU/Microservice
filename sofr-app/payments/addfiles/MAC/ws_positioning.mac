/* Позиционирование */

import ws_utils;
import ws_window;


// Возвращает текст системных фильтров для скроллингов Позиционирования
private macro GetSystemFilterText(scrollingName: string): string
    var systemFilter: string = "";
    if (scrollingName == LeftPositioningSrollingName)
        systemFilter = "PAYM.ID_SFI = :CurrencyId and nvl(PAYM.ID_NKSOUTPUT, 0) = 0";
    elif (scrollingName == RightPositioningSrollingName)
        systemFilter = "PAYM.ID_NKSOUTPUT = :CorrespondentSchemeId";
    end;
    return systemFilter;
end;

// Возвращает текст запроса для скроллингов Позиционирования
private macro GetSQLTextPositioning(scrollingId, systemFilter: string, filter, order)
    var cmd = TRslOraQuery();
    cmd.SqlText = "select RSP_WEB.GET_LISTDOC_FROM_MENU(:ID_NMENU, :P_ORDER, :P_FILTER, :P_SystemFilter) QueryText from DUAL";
    cmd.DeclareAndSet("ID_NMENU", scrollingId, "P_ORDER", order, "P_FILTER", filter, "P_SystemFilter", systemFilter);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.Rec.QueryText;
end;

// Возвращает записи для скроллингов Позиционирования
macro GetPositioningRows(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var cmd = TRslOraQuery();
    cmd.SqlText = SQL_WrapSelect(
        GetSQLTextPositioning(wParams.id_scrolling, GetSystemFilterText(params.ScrollingId), GetSqlFilterCondition(params.FilterItems), GetOrderStr(params.OrderItems)),
        params.PageNumber,
        params.PageSize
    );
    if (Index(cmd.SqlText, ":CurrencyId"))
        cmd.DeclareAndSet("CurrencyId", params.PanelModelParams.LeftCurrencyId);
    elif (Index(cmd.SqlText, ":CorrespondentSchemeId"))
        cmd.DeclareAndSet("CorrespondentSchemeId", params.PanelModelParams.CorrespondentSchemeId);
    end;
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd;
end;

// Возвращает количество записей для скроллингов Позиционирования
macro GetPositioningRowCount(params): integer
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT COUNT(*) CNT FROM (" +
        GetSQLTextPositioning(wParams.id_scrolling, GetSystemFilterText(params.ScrollingId), GetSqlFilterCondition(params.FilterItems), "") +
    ")";
    if (Index(cmd.SqlText, ":CurrencyId"))
        cmd.DeclareAndSet("CurrencyId", params.PanelModelParams.LeftCurrencyId);
    elif (Index(cmd.SqlText, ":CorrespondentSchemeId"))
        cmd.DeclareAndSet("CorrespondentSchemeId", params.PanelModelParams.CorrespondentSchemeId);
    end;
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.GetByName("CNT").AsInteger;
end;

macro GetFirstOrDefaultCorrespondentSchemeCommand(currencyId: integer)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT s.id_sfi, s.code, s.text, n.id_nks CorrespondentSchemeId, n.text CorrespondentSchemeText, n.id_nkorr Correspondent, k.id_node Node"
                  "  FROM sfi s JOIN nks n ON s.id_sfi = n.id_sfi JOIN nkorr k ON n.id_nkorr = k.id_nkorr"
                  " WHERE n.ispoz = 1 AND nvl(n.isblk, 0) = 0 AND n.id_sfi = :CurrencyId AND rownum = 1"
                  " ORDER BY n.prio";
    cmd.DeclareAndSet("CurrencyId", currencyId);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd;
end;

class PositioningModel
    var LeftCurrencyId: integer = 1;
    var LeftCurrencyCode: integer = 0;
    var LeftCurrencyText: string = "Рубли Россия";
    var RightCurrencyId: integer = 1;
    var RightCurrencyCode: integer = 0;
    var RightCurrencyText: string = "Рубли Россия";
    var CorrespondentSchemeId: integer = 445;
    var CorrespondentSchemeText: string = "Рубли через РКЦ";
    var Correspondent: integer = 0;
    var Node: integer = 0;

    private macro Init(currencyId: integer)
        var cmd = GetFirstOrDefaultCorrespondentSchemeCommand(currencyId);
        if (not cmd.Eof)
            CorrespondentSchemeId = cmd.GetByName("CorrespondentSchemeId").AsInteger;
            CorrespondentSchemeText = cmd.GetByName("CorrespondentSchemeText").AsString;
            Correspondent = cmd.GetByName("Correspondent").AsInteger;
            Node = cmd.GetByName("Node").AsInteger;
        end;
    end;

    Init(RightCurrencyId);
end;

macro GetPositioningModel(params): PositioningModel
    return PositioningModel();
end;

class (IWindow) PositioningWindow(menuId)
    Id = menuId;
    Title = "Позиционирование";
    WindowParams = makeObj(
        "IWindowParams",
        "Width", 800,
        "Height", 480,
        "HelpPage", "",
        "IsModal", false,
        "IsResizing", true
    );
    Appearance = WindowAppearanceType.Custom;

    private macro SetToolbarItems()
        ToolbarItems = TArray();
        var cmd = TRslOraQuery();
        cmd.SqlText = "SELECT rsp_encode.blob_to_hex(icon_png) AS icon FROM sicon WHERE code = 8";
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        var Icon = "";
        if(not cmd.Eof)
          Icon=cmd.GetByName("Icon").AsString;
        end;

        ToolbarItems[ToolbarItems.Size] = makeObj(
            "IToolbarItem",
            "Type", ToolbarItemType.Button,
            "Image", Icon,
            "Tooltip", "Справка по корсхемам",
            "Service", WebServiceCallback("ws_corschemes_help", "GetScrollingCorSchemesHelp")
        );
    end;

    private macro SetFields()
        var wParamsSFI = WebServiceParams(Type_Spr_SFI);
        var wParamsCorrespScheme = WebServiceParams(Type_Spr_NKS);
        Fields = makeArray(
            makeObj(
                "IPanelItem",
                "Id", "PositioningSplitter",
                "Type", PanelItemType.Group,
                "Field", makeObj(
                    "IGroup",
                    "Type", GroupItemType.Splitter,
                    "Options", makeObj("ISplitterOptions", "Vertical", true),
                    "Fields", makeArray(
                        makeObj(
                            "IPanelItem",
                            "Id", "LeftPositioningPanel",
                            "Row", 0,
                            "Type", PanelItemType.Group,
                            "Field", makeObj(
                                "IGroup",
                                "Type", GroupItemType.Panel,
                                "Options", makeObj("IPanelOptions", "RowDefinitions", makeArray(0, 0, 1)),
                                "Fields", makeArray(
                                    makeObj(
                                        "IPanelItem",
                                        "Id", "LeftPositioningCurrencyCode",
                                        "Row", 0,
                                        "Type", PanelItemType.Input,
                                        "Field", makeObj(
                                            "IInput",
                                            "Type", PanelInputFieldType.Integer,
                                            "Label", "Валюта ",
                                            "LabelTop", false,
                                            "Tips", makeObj("ITips", "Tooltip", "Валюта"),
                                            "ReadOnly", false,
                                            "Enabled", true,
                                            "TabStop", true,
                                            "ClearButton", false,
                                            "Required", false,
//                                            "MaxLength", 0,
//                                            "CheckValueService", WebServiceCallback(),
                                            "Source", "LeftCurrencyCode",
                                            "List", makeObj(
                                                "IList",
                                                "ID", "LeftPositioningCurrencyList",
                                                "MultiPage", false,
                                                "MultiSelect", false,
                                                "ContextChar", 2,
                                                "SelectOnly", true,
                                                "GetRowsService", WebServiceCallback("ws_positioning", "GetPositioningListRowsService", wParamsSFI.ToStr()),
                                                "GetRowCountService", WebServiceCallback("ws_positioning", "GetPositioningListRowCountService", wParamsSFI.ToStr()),
                                                "SelProcService", WebServiceCallback("ws_positioning", "LeftCurrencySelectionChanged"),
                                                "ViewFields", GetViewFields(Type_Spr_SFI)/*,
                                                "MappingFields", makeArray(
                                                    makeObj("IMappingItem", "ListSource", "SFI8ID_SFI", "ObjSource", "LeftCurrencyId"),
                                                    makeObj("IMappingItem", "ListSource", "SFI8CODE", "ObjSource", "LeftCurrencyCode"),
                                                    makeObj("IMappingItem", "ListSource", "SFI8TEXT", "ObjSource", "LeftCurrencyText"),
                                                    makeObj("IMappingItem", "ListSource", "SFI8ID_SFI", "ObjSource", "RightCurrencyId"),
                                                    makeObj("IMappingItem", "ListSource", "SFI8CODE", "ObjSource", "RightCurrencyCode"),
                                                    makeObj("IMappingItem", "ListSource", "SFI8TEXT", "ObjSource", "RightCurrencyText")
                                                )
*/
                                            ),
                                            "Design", makeObj(
                                                "IDesign",
                                                "Font", makeObj(
                                                    "IFont",
//                                                    "FontFamily", null,
                                                    "Bold", true,
                                                    "Italic", false,
                                                    "Color", 0
                                                ),
                                                "Background", 0
                                            ),
                                            "TextWrapping", false,
                                            "Location", makeObj(
                                                "ILocation",
                                                "MinWidth", 50,
                                                "Width", 50,
                                                "StretchHorizontal", false,
                                                "StretchVertical", false,
                                                "MarginLeft", 80
                                            )
                                        )
                                    ),
                                    makeObj(
                                        "IPanelItem",
                                        "Id", "LeftPositioningCurrencyText",
                                        "Row", 0,
                                        "Type", PanelItemType.Input,
                                        "Field", makeObj(
                                            "IInput",
                                            "Type", PanelInputFieldType.String,
                                            "LabelTop", false,
                                            "ReadOnly", true,
                                            "Enabled", false,
                                            "TabStop", false,
                                            "ClearButton", false,
                                            "Required", false,
                                            "Source", "LeftCurrencyText",
                                            "Design", makeObj(
                                                "IDesign",
                                                "Font", makeObj(
                                                    "IFont",
//                                                    "FontFamily", null,
                                                    "Bold", true,
                                                    "Italic", false,
                                                    "Color", 0
                                                ),
                                                "Background", 0
                                            ),
                                            "TextWrapping", false,
                                            "Location", makeObj(
                                                "ILocation",
                                                "MinWidth", 100,
                                                "Width", 200,
                                                "StretchHorizontal", true,
                                                "StretchVertical", false,
                                                "MarginLeft", 10
                                            )
                                        )
                                    ),
                                    makeObj(
                                        "IPanelItem",
                                        "Id", "NullPanelItem",
                                        "Row", 1,
                                        "Type", PanelItemType.Input,
                                        "Field", makeObj(
                                            "IInput",
                                            "Type", PanelInputFieldType.String,
                                            "LabelTop", false,
                                            "ReadOnly", true,
                                            "Enabled", false,
                                            "TabStop", false,
                                            "ClearButton", false,
                                            "Required", false,
                                            "Source", "LeftCurrencyId",
                                            "TextWrapping", false,
                                            "Location", makeObj(
                                                "ILocation",
                                                "MinWidth", 1,
                                                "Width", 1,
                                                "StretchHorizontal", false,
                                                "StretchVertical", false,
                                                "MarginLeft", 80
                                            )
                                        )
                                    ),
                                    makeObj(
                                        "IPanelItem",
                                        "Id", LeftPositioningSrollingName,
                                        "Row", 2,
                                        "Type", PanelItemType.Scrolling,
                                        "Field", Scrolling(Id, 0, LeftPositioningSrollingName)
                                    )
                                )
                            )
                        ),
                        makeObj(
                            "IPanelItem",
                            "Id", "RightPositioningPanel",
                            "Row", 1,
                            "Type", PanelItemType.Group,
                            "Field", makeObj(
                                "IGroup",
                                "Type", GroupItemType.Panel,
                                "Options", makeObj("IPanelOptions", "RowDefinitions", makeArray(0, 0, 1)),
                                "Fields", makeArray(
                                    makeObj(
                                        "IPanelItem",
                                        "Id", "RightPositioningCurrencyCode",
                                        "Row", 0,
                                        "Type", PanelItemType.Input,
                                        "Field", makeObj(
                                            "IInput",
                                            "Type", PanelInputFieldType.Integer,
                                            "Label", "Валюта ",
                                            "LabelTop", false,
                                            "Tips", makeObj("ITips", "Tooltip", "Валюта"),
                                            "ReadOnly", false,
                                            "Enabled", true,
                                            "TabStop", true,
                                            "ClearButton", false,
                                            "Required", false,
//                                            "MaxLength", 0,
//                                            "CheckValueService", WebServiceCallback(),
                                            "Source", "RightCurrencyCode",
                                            "List", makeObj(
                                                "IList",
                                                "ID", "RightPositioningCurrencyList",
                                                "MultiPage", false,
                                                "MultiSelect", false,
                                                "ContextChar", 2,
                                                "SelectOnly", true,
                                                "GetRowsService", WebServiceCallback("ws_positioning", "GetPositioningListRowsService", wParamsSFI.ToStr()),
                                                "GetRowCountService", WebServiceCallback("ws_positioning", "GetPositioningListRowCountService", wParamsSFI.ToStr()),
                                                "SelProcService", WebServiceCallback("ws_positioning", "RightCurrencySelectionChanged"),
                                                "ViewFields", GetViewFields(Type_Spr_SFI)/*,
                                                "MappingFields", makeArray(
                                                    makeObj("IMappingItem", "ListSource", "SFI8ID_SFI", "ObjSource", "RightCurrencyId"),
                                                    makeObj("IMappingItem", "ListSource", "SFI8CODE", "ObjSource", "RightCurrencyCode"),
                                                    makeObj("IMappingItem", "ListSource", "SFI8TEXT", "ObjSource", "RightCurrencyText")
                                                )
*/
                                            ),
                                            "Design", makeObj(
                                                "IDesign",
                                                "Font", makeObj(
                                                    "IFont",
//                                                    "FontFamily", null,
                                                    "Bold", true,
                                                    "Italic", false,
                                                    "Color", 0
                                                ),
                                                "Background", 0
                                            ),
                                            "TextWrapping", false,
                                            "Location", makeObj(
                                                "ILocation",
                                                "MinWidth", 50,
                                                "Width", 50,
                                                "StretchHorizontal", false,
                                                "StretchVertical", false,
                                                "MarginLeft", 80
                                            )
                                        )
                                    ),
                                    makeObj(
                                        "IPanelItem",
                                        "Id", "RightPositioningCurrencyText",
                                        "Row", 0,
                                        "Type", PanelItemType.Input,
                                        "Field", makeObj(
                                            "IInput",
                                            "Type", PanelInputFieldType.String,
                                            "LabelTop", false,
                                            "ReadOnly", true,
                                            "Enabled", false,
                                            "TabStop", false,
                                            "ClearButton", false,
                                            "Required", false,
                                            "Source", "RightCurrencyText",
                                            "Design", makeObj(
                                                "IDesign",
                                                "Font", makeObj(
                                                    "IFont",
//                                                    "FontFamily", null,
                                                    "Bold", true,
                                                    "Italic", false,
                                                    "Color", 0
                                                ),
                                                "Background", 0
                                            ),
                                            "TextWrapping", false,
                                            "Location", makeObj(
                                                "ILocation",
                                                "MinWidth", 100,
                                                "Width", 200,
                                                "StretchHorizontal", true,
                                                "StretchVertical", false,
                                                "MarginLeft", 10
                                            )
                                        )
                                    ),
                                    makeObj(
                                        "IPanelItem",
                                        "Id", "PositioningCorrespScheme",
                                        "Row", 1,
                                        "Type", PanelItemType.Input,
                                        "Field", makeObj(
                                            "IInput",
                                            "Type", PanelInputFieldType.String,
                                            "Label", "Корсхема ",
                                            "LabelTop", false,
                                            "Tips", makeObj("ITips", "Tooltip", "Корсхема"),
                                            "ReadOnly", false,
                                            "Enabled", true,
                                            "TabStop", true,
                                            "ClearButton", false,
                                            "Required", false,
//                                            "MaxLength", 0,
//                                            "CheckValueService", WebServiceCallback(),
                                            "Source", "CorrespondentSchemeText",
                                            "List", makeObj(
                                                "IList",
                                                "ID", "PositioningCorrespSchemeList",
                                                "MultiPage", false,
                                                "MultiSelect", false,
                                                "ContextChar", 2,
                                                "SelectOnly", true,
                                                "GetRowsService", WebServiceCallback("ws_positioning", "GetPositioningListRowsService", wParamsCorrespScheme.ToStr()),
                                                "GetRowCountService", WebServiceCallback("ws_positioning", "GetPositioningListRowCountService", wParamsCorrespScheme.ToStr()),
                                                "SelProcService", WebServiceCallback("ws_positioning", "CorrespondentSchemeSelectionChanged"),
                                                "ViewFields", GetViewFields(Type_Spr_NKS),
/*
                                                "MappingFields", makeArray(
                                                    makeObj("IMappingItem", "ListSource", "NKS8ID_NKS", "ObjSource", "CorrespondentSchemeId"),
                                                    makeObj("IMappingItem", "ListSource", "NKS8TEXT", "ObjSource", "CorrespondentSchemeText")
                                                )
*/
                                                "PanelFieldsForService", makeArray("RightCurrencyId")
                                            ),
                                            "Design", makeObj(
                                                "IDesign",
                                                "Font", makeObj(
                                                    "IFont",
//                                                    "FontFamily", null,
                                                    "Bold", true,
                                                    "Italic", false,
                                                    "Color", 0
                                                ),
                                                "Background", 0
                                            ),
                                            "TextWrapping", false,
                                            "Location", makeObj(
                                                "ILocation",
                                                "MinWidth", 100,
                                                "Width", 200,
                                                "StretchHorizontal", false,
                                                "StretchVertical", false,
                                                "MarginLeft", 80
                                            )
                                        )
                                    ),
                                    makeObj(
                                        "IPanelItem",
                                        "Id", RightPositioningSrollingName,
                                        "Row", 2,
                                        "Type", PanelItemType.Scrolling,
                                        "Field", Scrolling(Id, 0, RightPositioningSrollingName)
                                    )
                                )
                            )
                        )
                    )
                )
            )
        );
    end;

    SetToolbarItems();
    SetFields();
    GetObjectService = WebServiceCallback("ws_positioning", "GetPositioningModel");
end;

// Окно позиционирования
macro GetPositioningWindow(params)
    return WebServiceResult(WebServiceResultType.Window, PositioningWindow(params));
end;

private macro GetPositioningListSQLText(directoryType: integer, panelModelParams): string
    var filter: string = "";
    if (panelModelParams)
        filter = "NKS.ID_SFI = " + panelModelParams.RightCurrencyId;
    end;
    var result: string;
    if (directoryType == Type_Spr_SFI)
        result = "SELECT SFI.ID_SFI as SFI8ID_SFI, SFI.CODE as SFI8CODE, SFI.TEXT as SFI8TEXT, SFI.STRCODE as SFI8STRCODE FROM SFI SFI ORDER BY SFI.CODE";
    elif (directoryType == Type_Spr_NKS)
        result = "SELECT NKS.ID_NKS as NKS8ID_NKS, NKS.TEXT as NKS8TEXT, NKS.ID_NKORR as NKS8ID_NKORR, NKORR.ID_NODE as NKORR8ID_NODE"
                 "  FROM NKS NKS JOIN NKORR NKORR ON NKS.ID_NKORR = NKORR.ID_NKORR"
                 " WHERE NKS.ISPOZ = 1 AND nvl(NKS.ISBLK, 0) = 0" + IIF(filter == "", "", " AND " + filter) +
                 " ORDER BY NKS.PRIO";
    else
        var cmd = TRslOraQuery();
        cmd.SqlText = "select RSP_WEB.GET_FIELDSPR(:FormId, :Filter) QueryText from DUAL";
        cmd.DeclareAndSet("FormId", directoryType, "Filter", filter);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        result = cmd.Rec.QueryText;
    end;
    return result;
end;

macro GetPositioningListRowsService(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var cmd = TRslOraQuery();
    cmd.SqlText = SQL_WrapSelect(GetPositioningListSQLText(wParams.id_record, params.PanelModelParams), params.PageNumber, params.PageSize);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd;
end;

macro GetPositioningListRowCountService(params): integer
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var cmd = TRslOraQuery();
    cmd.SqlText = "select count(*) CNT from (" + GetPositioningListSQLText(wParams.id_record, params.PanelModelParams) + ")";
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.GetByName("CNT").AsInteger;
end;

macro LeftCurrencySelectionChanged(params)
    var correspondentSchemeId: integer = 0;
    var correspondentSchemeText: string = "";
    var correspondent: integer = 0;
    var node: integer = 0;
    var cmd = GetFirstOrDefaultCorrespondentSchemeCommand(params.Item.SFI8ID_SFI);
    if (not cmd.Eof)
        correspondentSchemeId = cmd.GetByName("CorrespondentSchemeId").AsInteger;
        correspondentSchemeText = cmd.GetByName("CorrespondentSchemeText").AsString;
        correspondent = cmd.GetByName("Correspondent").AsInteger;
        node = cmd.GetByName("Node").AsInteger;
    end;
    var alteredModel = makeObj(
        "PositioningModel",
        "LeftCurrencyId", params.Item.SFI8ID_SFI,
        "LeftCurrencyCode", params.Item.SFI8CODE,
        "LeftCurrencyText", params.Item.SFI8TEXT,
        "RightCurrencyId", params.Item.SFI8ID_SFI,
        "RightCurrencyCode", params.Item.SFI8CODE,
        "RightCurrencyText", params.Item.SFI8TEXT,
        "CorrespondentSchemeId", correspondentSchemeId,
        "CorrespondentSchemeText", correspondentSchemeText,
        "Correspondent", correspondent,
        "Node", node
    );
    return WebServiceResult(
        WebServiceResultType.Action,
        makeArray(
            IAction(WebServiceActionType.UpdateModel, alteredModel),
            IAction(WebServiceActionType.RefreshScrolling, LeftPositioningSrollingName),
            IAction(WebServiceActionType.RefreshScrolling, RightPositioningSrollingName)
        )
    );
end;

macro RightCurrencySelectionChanged(params)
    var correspondentSchemeId: integer = 0;
    var correspondentSchemeText: string = "";
    var correspondent: integer = 0;
    var node: integer = 0;
    var cmd = GetFirstOrDefaultCorrespondentSchemeCommand(params.Item.SFI8ID_SFI);
    if (not cmd.Eof)
        correspondentSchemeId = cmd.GetByName("CorrespondentSchemeId").AsInteger;
        correspondentSchemeText = cmd.GetByName("CorrespondentSchemeText").AsString;
        correspondent = cmd.GetByName("Correspondent").AsInteger;
        node = cmd.GetByName("Node").AsInteger;
    end;
    var alteredModel = makeObj(
        "PositioningModel",
        "LeftCurrencyId", params.PanelModel.LeftCurrencyId,
        "LeftCurrencyCode", params.PanelModel.LeftCurrencyCode,
        "LeftCurrencyText", params.PanelModel.LeftCurrencyText,
        "RightCurrencyId", params.Item.SFI8ID_SFI,
        "RightCurrencyCode", params.Item.SFI8CODE,
        "RightCurrencyText", params.Item.SFI8TEXT,
        "CorrespondentSchemeId", correspondentSchemeId,
        "CorrespondentSchemeText", correspondentSchemeText,
        "Correspondent", correspondent,
        "Node", node
    );
    return WebServiceResult(
        WebServiceResultType.Action,
        makeArray(
            IAction(WebServiceActionType.UpdateModel, alteredModel),
            IAction(WebServiceActionType.RefreshScrolling, RightPositioningSrollingName)
        )
    );
end;

macro CorrespondentSchemeSelectionChanged(params)
    var alteredModel = makeObj(
        "PositioningModel",
        "LeftCurrencyId", params.PanelModel.LeftCurrencyId,
        "LeftCurrencyCode", params.PanelModel.LeftCurrencyCode,
        "LeftCurrencyText", params.PanelModel.LeftCurrencyText,
        "RightCurrencyId", params.PanelModel.RightCurrencyId,
        "RightCurrencyCode", params.PanelModel.RightCurrencyCode,
        "RightCurrencyText", params.PanelModel.RightCurrencyText,
        "CorrespondentSchemeId", params.Item.NKS8ID_NKS,
        "CorrespondentSchemeText", params.Item.NKS8TEXT,
        "Correspondent", params.Item.NKS8ID_NKORR,
        "Node", params.Item.NKORR8ID_NODE
    );
    return WebServiceResult(
        WebServiceResultType.Action,
        makeArray(
            IAction(WebServiceActionType.UpdateModel, alteredModel),
            IAction(WebServiceActionType.RefreshScrolling, RightPositioningSrollingName)
        )
    );
end;

private macro PositioningMove(document, panelModelParams)
    var paym = document.Paym;
    if (panelModelParams)
        paym.Id_NKorrOutPut = panelModelParams.Correspondent;
        paym.Id_NKSOutPut = panelModelParams.CorrespondentSchemeId;
        paym.Id_SFIOutPut = panelModelParams.RightCurrencyId;
        paym.Id_NodeOutPut = IIF(panelModelParams.Node == 0, null, panelModelParams.Node);
    else
        paym.Id_NKorrOutPut = null;
        paym.Id_NKSOutPut = null;
        paym.Id_SFIOutPut = null;
        paym.Id_NodeOutPut = null;
    end;
    document.Save();
end;

macro MoveRight(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var document;
    var result: WebServiceResult;
    if (params.Params)
        if (int(wParams.id_record) == 0)
            return WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Не выбрана исходящая корсхема", MessageBoxType.Error));
        end;
        document = TRslPayDoc(params.Model.Paym8Id_NForm, params.Model.Paym8Id_Paym, params.Model.Paym8VersionRec);
        var cmd = TRslOraQuery();
        cmd.SqlText = "SELECT n.id_sfi RightCurrencyId, n.id_nkorr Correspondent, k.id_node Node"
                      "  FROM NKS n JOIN NKORR k ON n.id_nkorr = k.id_nkorr"
                      " WHERE n.ispoz = 1 AND n.id_nks = :CorrespondentSchemeId AND rownum = 1";
        cmd.DeclareAndSet("CorrespondentSchemeId", wParams.id_record);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        var model = makeObj(
            "PositioningModel",
            "RightCurrencyId", cmd.GetByName("RightCurrencyId").AsInteger,
            "CorrespondentSchemeId", int(wParams.id_record),
            "Correspondent", cmd.GetByName("Correspondent").AsInteger,
            "Node", cmd.GetByName("Node").AsInteger
        );
        PositioningMove(document, model);
        result = WebServiceResult(WebServiceResultType.Action, makeArray(IAction(WebServiceActionType.Close), IAction(WebServiceActionType.RefreshParent)));
    elif (params.SelectedRows.Size == 0)
        if (params.PanelModelParams.CorrespondentSchemeId == 0)
            return WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Не выбрана исходящая корсхема", MessageBoxType.Error));
        end;
        if (params.CurrentRow == null)
            result = WebServiceResult(WebServiceResultType.Message, IMessage("Внимание!", "Не задана текущая строка", MessageBoxType.Info));
        else
            document = TRslPayDoc(params.CurrentRow.Id_NForm, params.CurrentRow.Paym8Id_Paym, params.CurrentRow.VersionRec);
            PositioningMove(document, params.PanelModelParams);
            result = WebServiceResult(
                WebServiceResultType.Action,
                makeArray(IAction(WebServiceActionType.RefreshScrolling, LeftPositioningSrollingName), IAction(WebServiceActionType.RefreshScrolling, RightPositioningSrollingName))
            );
        end;
    else
        if (params.PanelModelParams.CorrespondentSchemeId == 0)
            return WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Не выбрана исходящая корсхема", MessageBoxType.Error));
        end;
        for (var selectedRow, params.SelectedRows)
            document = TRslPayDoc(selectedRow.Id_NForm, selectedRow.Paym8Id_Paym, selectedRow.VersionRec);
            PositioningMove(document, params.PanelModelParams);
        end;
        result = WebServiceResult(
            WebServiceResultType.Action,
            makeArray(IAction(WebServiceActionType.RefreshScrolling, LeftPositioningSrollingName), IAction(WebServiceActionType.RefreshScrolling, RightPositioningSrollingName))
        );
    end;
    OraTrnCommit();
    return result;
end;

macro MoveLeft(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var document;
    var result: WebServiceResult;
    if (params.Params)
        document = TRslPayDoc(params.Model.Paym8Id_NForm, params.Model.Paym8Id_Paym, params.Model.Paym8VersionRec);
        PositioningMove(document);
        result = WebServiceResult(WebServiceResultType.Action, makeArray(IAction(WebServiceActionType.Close), IAction(WebServiceActionType.RefreshParent)));
    elif (params.SelectedRows.Size == 0)
        if (params.CurrentRow == null)
            result = WebServiceResult(WebServiceResultType.Message, IMessage("Внимание!", "Не задана текущая строка", MessageBoxType.Info));
        else
            document = TRslPayDoc(params.CurrentRow.Id_NForm, params.CurrentRow.Paym8Id_Paym, params.CurrentRow.VersionRec);
            PositioningMove(document);
            result = WebServiceResult(
                WebServiceResultType.Action,
                makeArray(IAction(WebServiceActionType.RefreshScrolling, LeftPositioningSrollingName), IAction(WebServiceActionType.RefreshScrolling, RightPositioningSrollingName))
            );
        end;
    else
        for (var selectedRow, params.SelectedRows)
            document = TRslPayDoc(selectedRow.Id_NForm, selectedRow.Paym8Id_Paym, selectedRow.VersionRec);
            PositioningMove(document);
        end;
        result = WebServiceResult(
            WebServiceResultType.Action,
            makeArray(IAction(WebServiceActionType.RefreshScrolling, LeftPositioningSrollingName), IAction(WebServiceActionType.RefreshScrolling, RightPositioningSrollingName))
        );
    end;
    OraTrnCommit();
    return result;
end;

macro PositioningEditAction(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var newParams = WebServiceParams(params.PanelModelParams.CorrespondentSchemeId, 0, 0, params.ScrollingId);
    params.ScrollingId = wParams.id_scrolling;
    params.Params = newParams.ToStr();
    return EditAction(params);
end;

macro PositioningCalculateSum(params): ColumnSumServiceResult
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var result = ColumnSumServiceResult();
    if (params.SelectedRows.Size > 0)
        var sum: double = 0;
        for (var selectedRow, params.SelectedRows)
            var value = GenGetProp(selectedRow, params.ColumnId);
            if (value != null)
                sum = sum + value;
            end;
        end;
        result.SelectedItemsSum = sum * 100;
    else
        result.SelectedItemsSum = 0;
    end;
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT SUM(" + params.ColumnId + ") SUM FROM (" + GetSQLTextPositioning(wParams.id_scrolling, GetSystemFilterText(params.ScrollingId), GetSqlFilterCondition(params.FilterItems), "") + ")";
    if (Index(cmd.SqlText, ":CurrencyId"))
        cmd.DeclareAndSet("CurrencyId", params.PanelModelParams.LeftCurrencyId);
    elif (Index(cmd.SqlText, ":CorrespondentSchemeId"))
        cmd.DeclareAndSet("CorrespondentSchemeId", params.PanelModelParams.CorrespondentSchemeId);
    end;
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    result.TotalSum = IIF(cmd.Rec.SUM == null, 0, cmd.Rec.SUM) * 100;
    return result;
end;
