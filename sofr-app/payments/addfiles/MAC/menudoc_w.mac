import Utils, ws_window, ws_file, ws_utils;

/*сервис для смены состояния*/
macro changestate(params,button)

   addptk("buttun -"+button);

   if(button != MessageBoxButtonType.Ok)
     return;
   end;

   var result;
   var wParams = Xml2Rsl(params);
   var errtext = "";
   var iderr = "";

   if(wParams.Propindex("errtext")!=-1)
     errtext = wParams.errtext;
   end;
   if(wParams.Propindex("iderr")!=-1)
     iderr = wParams.iderr;
   end;


   if(ChgStatePay(wParams.id,wParams.state, errtext, wParams.vr,iderr))
        OraTrnCommit();
        result = WebServiceResult(WebServiceResultType.Action, makeArray(IAction(WebServiceActionType.Close), IAction(WebServiceActionType.RefreshParent)));
   else
        OraTrnRollBack();
        result = WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Не удалось сменить состояние платежу: запись конкурентно изменена", MessageBoxType.Error));
   end;

   return result;
end;

/*----------------------------------------------------------------------------*/
/*Установить состояние                                                        */
/*----------------------------------------------------------------------------*/

//отрабатывает после выбора из справочника
macro wSetCurrState(Params)

   if (params.CurrentRow == null)
     return WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Не выбрано состояние", MessageBoxType.Error));
   end;

   var result;
   var wParams = Xml2Rsl(params.params);
   wParams.State = params.CurrentRow.SSTATE8ID_SSTATE;

   result=changestate(Rsl2Xml(wParams),MessageBoxButtonType.Ok);

   if(result.Type== WebServiceResultType.Action)
      result.Value[result.Value.Size-1]=IAction(WebServiceActionType.CloseParent);
      result.Value[result.Value.Size]=IAction(WebServiceActionType.RefreshGlobalScrolling,wParams.ScrollingID);
   end;
   return result;

end;

//висит на меню
macro SetCurrState(Params)
  var wParams = makeObj("TDynamicBean",
                        "ID", Params.Model.PAYM8ID_PAYM,
                        "VR", Params.Model.PAYM8VERSIONREC,
                        "Row", makeObj("TDynamicBean",
                                       "SSTATE8ID_SSTATE", Params.Model.PAYM8ID_SSTATE),
                        "ScrollingID",Xml2Rsl(params.params).IDNMenu
                       );


  return GetScrollingSpr(GetIdByCode("FORM","ID_FORM",560), "menudoc_w", "wSetCurrState", Rsl2Xml(wParams));

END;




/*Игнорировать*/
macro ToIgnore(Params)

  var wParams = makeObj("TDynamicBean",
                        "ID", Params.Model.PAYM8ID_PAYM,
                        "VR", Params.Model.PAYM8VERSIONREC,
                        "STATE",StateIgnored);

  var mes = IMessage(null, "Игнорировать ?", MessageBoxType.Question,
                      makeArray(MessageBoxButtonType.Ok,MessageBoxButtonType.Cancel),
                      WebServiceCallback("menudoc_w", "changestate", Rsl2Xml(wParams)));

  return WebServiceResult(WebServiceResultType.Message, mes);

end;




/*----------------------------------------------------------------------------*/
/*Справка о маршруте документа                                                */
/*----------------------------------------------------------------------------*/
macro DocSpr(Params)
  var Pay, id;
  var SACCCURRAMOUNT = "";
  var PHist;
  var ok;
  var KorrCodeINPUT, KorrNameINPUT, KorrCodeCURRAMOUNT, KorrNameCURRAMOUNT, KorrCodeOUTPUT, KorrNameOUTPUT;
  var KSCodeINPUT, KSNameINPUT, FrnSysINPUT, KSCodeCURRAMOUNT, KSNameCURRAMOUNT, FrnSysCURRAMOUNT, KSCodeOUTPUT, KSNameOUTPUT, FrnSysOUTPUT;
  var ErrCode = 0, ErrStr = "";
  var QueryError;

  KorrCodeINPUT=KorrNameINPUT=KorrCodeCURRAMOUNT=KorrNameCURRAMOUNT=KorrCodeOUTPUT=KorrNameOUTPUT="";
  KSCodeINPUT=KSNameINPUT=FrnSysINPUT=KSCodeCURRAMOUNT=KSNameCURRAMOUNT=FrnSysCURRAMOUNT=KSCodeOUTPUT=KSNameOUTPUT=FrnSysOUTPUT="";

  Pay = Params.Model;

  id = Pay.PAYM8id_paym;
  if(ValType(Pay.PAYM8ID_SACCCURRAMOUNT)!=V_UNDEF)
    SACCCURRAMOUNT = GetColByCol("sacc", "code", "id_sacc", Pay.PAYM8ID_SACCCURRAMOUNT);
  end;

  KorrCodeINPUT      = GetKorrCode(Pay.PAYM8ID_NKORRINPUT,KorrNameINPUT);
  KorrCodeCURRAMOUNT = GetKorrCode(Pay.PAYM8ID_NKORRCURRAMOUNT,KorrNameCURRAMOUNT);
  KorrCodeOUTPUT     = GetKorrCode(Pay.PAYM8ID_NKORROUTPUT,KorrNameOUTPUT);

  KSCodeINPUT      = GetKSCode(Pay.PAYM8ID_NKORRINPUT, Pay.PAYM8ID_NKSINPUT, Pay.PAYM8ID_SFIINPUT, KSnameINPUT, FrnSysINPUT);
  KSCodeCURRAMOUNT = GetKSCode(Pay.PAYM8ID_NKORRCURRAMOUNT, Pay.PAYM8ID_NKSCURRAMOUNT, Pay.PAYM8ID_SFICURRAMOUNT, KSnameCURRAMOUNT, FrnSysCURRAMOUNT);
  KSCodeOUTPUT     = GetKSCode(Pay.PAYM8ID_NKORROUTPUT, Pay.PAYM8ID_NKSOUTPUT, Pay.PAYM8ID_SFIOUTPUT, KSnameOUTPUT, FrnSysOUTPUT);

  CaptureOutput();


  [
   Внутренний системный номер: #################

   Поступил от
      Контрагент: ######## #################### ##############################################################
           Схема: ######## #######################################
        Id шлюза: ##################
          Валюта: ###

       Тек. счет: ########################
      Контрагент: ######## #################### ##############################################################
           Схема: ######## #######################################
       Тип счета:       ##
          Валюта: ###

   Отправлен на
      Контрагент: ######## #################### ##############################################################
           Схема: ######## #######################################
        Id шлюза: ##################
          Валюта: ###


                  История состояний

     Код            Название                Дата/Время        Опер      Ошибка               Комментарий
   ------- ---------------------------- -------------------- -------- ----------- ------------------------------------------
  ]

   (ToId(Id):l,
    KorrCodeINPUT:r,GetKorrBic(BICRus_ID,Pay.PAYM8ID_NKORRINPUT),KorrNameINPUT,
    KSCodeINPUT:r,KSnameINPUT,
    FrnSysINPUT,
    int(GetCodeFi(Pay.PAYM8ID_SFIINPUT)),

    Pay.PAYM8AccCurrAmount:r,
    KorrCodeCURRAMOUNT:r,GetKorrBic(BICRus_ID,Pay.PAYM8ID_NKORRCURRAMOUNT),KorrNameCURRAMOUNT,
    KSCodeCURRAMOUNT:r,KSnameCURRAMOUNT,
    SACCCURRAMOUNT,
    int(GetCodeFi(Pay.PAYM8ID_SFICURRAMOUNT)),

    KorrCodeOUTPUT:r,GetKorrBic(BICRus_ID,Pay.PAYM8ID_NKORROUTPUT),KorrNameOUTPUT,
    KSCodeOUTPUT:r,KSnameOUTPUT,
    FrnSysOUTPUT,
    int(GetCodeFi(Pay.PAYM8ID_SFIOUTPUT)));

  /*История*/

  PHist = GetHist(id, ErrCode, ErrStr, QueryError);

  if (not TstObj (PHist))
    RunError (toansi("Функция GetHist: "+QueryError));
  end;

  ok = not PHist.Eof;
  while(ok)

    [####### ############################ #################### ######## #### ###################################################### ]
    (PHist.GetByName("state_code").AsInteger:c,
     PHist.GetByName("state_name").AsString:r,
     PHist.GetByName("systemdate").AsString:c,
     PHist.GetByName("usernick"  ).AsString:c,
     GetColByCol("serror", "code", "id_serror", PHist.GetByName("id_serror").AsString):r,
     PHist.GetByName("varlen").AsString:c);
    ok = PHist.Next();
  end;

  var RepStr = StopCaptureOutput();
  var idrep = SaveReportHTML(RepStr, "Route.html","");
  return GetReportFile(idrep, "Route.html");
end;
