 /*
 $Name: ws_file.mac
 $Module: WEB
 $Description:
 */

import "unistr.d32";
import ws_types;


macro ConvertTxt2Html(TxtStr:String):String

  TxtStr=strsubst(TxtStr,"&","&amp;");
  TxtStr=strsubst(TxtStr,"\"","&quot;");
  TxtStr=strsubst(TxtStr,"`","&#x60;");
  TxtStr=strsubst(TxtStr,"<","&lt;");
  TxtStr=strsubst(TxtStr,">","&gt;");

  return("<!DOCTYPE html PUBLIC \"-//W3C//DTD XHTML 1.0 Transitional//EN\" \"http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd\">\n" +
      "<html xmlns=\"http://www.w3.org/1999/xhtml\" xml:lang=\"en-gb\" lang=\"en-gb\">\n" +
        "<head>\n" +
          "<meta http-equiv=\"Content-type\" CONTENT=\"text/html; charset=utf-8\">\n" +
        "</head>\n" +
        "<body>\n" +
          "<pre>\n"+
             OEM2UTF8(TxtStr)+
          "</pre>\n" +
        "</body>\n" +
      "</html>");

end;

// Сервис открытия отчетов, изображений и пр.
macro GetReportFile(id, nameFile, isClose);
    var result = makeArray(WebServiceResult(WebServiceResultType.ReportFile, IReportFile(makeArray(IReportFileItem(id, nameFile)))));
    if(isClose)
      result[result.Size] = WebServiceResult(WebServiceResultType.Action, IAction(WebServiceActionType.Close));
    end;
    return result;
end;

//
macro SaveReport(report, name, type)
  var InsertRep = TRslOraQuery();
  var Id = 0;

  InsertRep.SqlText = "begin "+
                      "  INSERT INTO strgfile(content, name, type) "+
                      "    VALUES(:report, '" + name + "', '" + type + "') "+
                      "  RETURNING id_strgfile INTO :ID; "+
                      "end;";
  InsertRep.DeclareAndSetLOB("report", report, StrLen(report));
  InsertRep.DeclareAndSet("ID", Id);
  if(not InsertRep.Execute)
    addptk("SaveReport: "+InsertRep.Error);
    RunError(toansi("SaveReport: "+InsertRep.Error));
  end;

  Id = InsertRep.GetVariable("ID");
  OraTrnCommit();
  return Id;
end;


macro SaveReportHTML(report, name, type)
  return SaveReport(ConvertTxt2Html(report), name, type)
end;
