/*09.02.2015 karlov
Макрос формирования ЭПС ED503*/

import uf_general, uf_utils, GENERAL, uf_send, sswift, "mtforms.cls", uf_swift;

macro swiftheaderFrnSys503(form, FrnSys, bicRecv, SESID, ISN )

  var tag3="";

  if(StrLen(bicRecv) == 8)
    bicRecv = bicRecv + "XXXX";
  elif(StrLen(bicRecv) == 11)
    bicRecv = SubStr(bicRecv,1,8) + "X" + SubStr(bicRecv,9);
  end;
  if  (FrnSys == ID_NKS_RUR6)
       tag3="{3:{113:RUR6}}";
  elif(FrnSys == ID_NKS_RUS9)
       tag3="{3:{113:RUS9}}";
  end;
  return mkstr(" ", 4)+ "{1:F01"+MySWIFTBic+"AXXX"+SESID+ISN+"}{2:I"+form +bicRecv+"N}"+tag3+"{4:";
end;

MACRO GetSWIFT(paymObj, SESID, ISN)
   var SentReference = "", NameFile, ArcPath = "", OutPath="", payment,
       hdrform = "", mtReccls, SerialStr, mtrec, transType = WITH_FIELD, tmp,
       Receiver, Mt202CovStr ="", CommentAdd ="";

   CommentStr = "";
   payment = paymObj.paym;

   if(in(payment.id_nform, Type_Doc_MT518, Type_Doc_MT53X, Type_Doc_MT54X))
     tmp = Sec_UnloadDocTrn(paymObj);
     CommentStr = "";

     if (not SaveExp (paymObj, Tmp, Type_Exp_Origin, "Отправленное сообщение", CommentStr))
        return false;
     end;

     SetParm(3,tmp);
     return true;
   end;

   if(not in(payment.id_nform, Type_Doc_MT200, Type_Doc_MT202, Type_Doc_MT103, Type_Doc_MTn95, Type_Doc_MTn96, Type_Doc_MTn9X))
    CommentStr = "Неверная форма "+ ToID(payment.id_nform) +" платежа " + ToID(payment.id_paym) + " для выгрузки в SWIFT";
    return false;
   end;
   /* определение имени файла */
   NameFile = GetNewNameFile (paymObj);
   ArcPath = SwiftArcDir + NameFile;
   OutPath = SwiftOutDir + NameFile;

   /* подготовка стереолазеров */
   if (payment.id_nform == Type_Doc_MT103)
     hdrform = "103";
     mt103Cls = InitSerial (mt103Cls, "TMTForm103", MT103SerializersPal);
     mtReccls = mt103Cls;
   elif(payment.id_nform== Type_Doc_MT200)
     hdrform = "200";
     mt200Cls = InitSerial (mt200Cls, "TMTForm200", MT200SerializersPal);
     mtReccls = mt200Cls;
   elif(payment.id_nform == Type_Doc_MT202)
     hdrform = "202";
     mt202Cls = InitSerial (mt202Cls, "TMTForm202", MT202SerializersPal);
     mtReccls = mt202Cls;
   elif(payment.id_nform == Type_Doc_MTn95)
     hdrform = int(paymObj.MTn9X.Type);
     mtRecclsn95 = InitSerial (mtRecclsn95, "TMTFormN95", MTn95Serializers);
     mtReccls = mtRecclsn95;
   elif(payment.id_nform == Type_Doc_MTn96)
     hdrform = int(paymObj.MTn9X.Type);
     mtRecclsn96 = InitSerial (mtRecclsn96, "TMTFormN96", MTn96Serializers);
     mtReccls = mtRecclsn96;
   elif(payment.id_nform == Type_Doc_MTn9X)
     hdrform = int(paymObj.MTn9X.Type);
     if(SubStr(String(paymObj.MTn9X.Type:0:0),2) == "92")
       mtRecclsn92 = InitSerial (mtRecclsn92, "TMTFormN92", MTn92Serializers);
       mtReccls = mtRecclsn92;
     else
       mtRecclsn99 = InitSerial (mtRecclsn99, "TMTFormN99", MTn99Serializers);
       mtReccls = mtRecclsn99;
     end;
   end;
   if((payment.id_nform == Type_Doc_MTn95)or(payment.id_nform == Type_Doc_MTn96)or(payment.id_nform == Type_Doc_MTn9X))
     mtrec = paymobj.mtn9X;
     SerialStr = FIELD_STRING_SPLITTER + MTRec.Text;
   else
     mtrec = paymobj.mtrec;
     SerialStr = FIELD_STRING_SPLITTER + MTRec.Str;
   end;
   if (not MTRecCls.SerializeUp(SerialStr, WITH_FIELD))
     CommentStr = "ошибка " + MTRecCls.LastError + " при чтении текста 4-го блока сообщения, платеж " + payment.id_paym;
     return false;
   end;
   SerialStr = "";

   SentReference = MTRecCls.GetField("20").GetSubField("Reference").Value;
   transType = WITH_FIELD;
   Receiver = mtrec.Receiver;

   if (not MTRecCls.SerializeDown(SerialStr, transType))
     CommentStr = "ошибка " + MTRecCls.LastError + " при формировании текста сообщения, платеж " + toid(payment.id_paym);
     return false;
   end;

   /* подготовка строки сообщения для записи в файл */
   var FrnSys = GetFrnSys(payment.ID_NKSOUTPUT);/*для исходящих стандарт транслитерации определяем по ИД шлюза, заданого в коррсхеме*/
   tmp = swiftheaderFrnSys503(hdrform, FrnSys, Receiver, SESID, ISN);
   tmp = tmp + RN + printstr("",SerialStr);
   tmp = tmp + RN + prend();

   CommentStr = "";
   if (not SaveExp (paymObj, Tmp, Type_Exp_Origin, "Отправленное сообщение", CommentStr))
      return false;
   end;

   /* Сохранение исходящего референса */
   CommentStr = "";
   if(not AddAss(paymObj, 0, ASS_SENT_REF, SentReference, {curdate}, CommentStr))
     return false;
   end;

   /*меняем статус пакета*/
   if  ((payment.id_nform == Type_Doc_MTn95)or(payment.id_nform == Type_Doc_MTn96)or(payment.id_nform == Type_Doc_MTn9X))
     if(not IPS_SetStateSentPCK(Payment.id_paym, MsgIsAnswer(paymObj)))
       CommentStr = "ошибка при изменении статуса пакета ";
       return false;
     end;
   end;

   SetParm(3,tmp);
   return true;
END;


macro Send_ED503(EDNo)
/*Отправка ЭС503*/

  var ID, VR;
  var OutXML = "", out = "";
  var NameFile = "";
  var SelectEDCOS, InsertTMP, CrtXMLESCOS, UpdFilNamEPD;
  var ArcDir, OutDir;
  var EDMainTable;
  var SYSTEMCODE;
  var Ans;

  SelectEDCOS = TRslOraQuery();
  SelectEDCOS.SqlText = "SELECT id_edcos, versionrec                                                        "+
                        "  FROM edcos                                                                       "+
                        "  WHERE eddate = RSP_COMMON.Get_DateOperDay AND edno = "+EDNo+"                    "+
                        "    AND edauthor = Rsp_Setup.GET_VALUE('Обмен с ПС БР\\УИС составителя')";

  if(not SelectEDCOS.Execute())
    RunError(toansi("Макрофункция Send_ED503:\r\n")+ToANSi(SelectEDCOS.Error));
  end;
  ID = SelectEDCOS.GetByName("ID_EDCOS").AsInteger;
  VR = SelectEDCOS.GetByName("versionrec").AsInteger;

  InsertTMP = TRslOraQuery();
  InsertTMP.SqlText = " insert into "+UFTblTmpIDVR+"(ID,VR) values(:ID,:VR)";
  InsertTMP.DeclareAndSet("ID",ID,"VR", VR);

  if(not InsertTMP.Execute)/*Вставка во временную таблицу*/
    RunError(toansi(InsertTMP.Error));
  end;

  NameFile = GetXMLFileName(EDNo);

  CrtXMLESCOS = TRslOraQuery();

  CrtXMLESCOS.SqlText = " begin  RSP_UFEBS.CreateXMLESCOS(:TMPTable,:NameFile,:OutXML, :out); end;";
  CrtXMLESCOS.DeclareAndSet("TMPTable",UFTblTmpIDVR,"NameFile",NameFile,"out",out);
  CrtXMLESCOS.DeclareAndSetLOB("OutXML",OutXML);

  if(not CrtXMLESCOS.Execute)
    RunError(toansi("Send_ED503: "+CrtXMLESCOS.Error));
  end;

  OutXML = CrtXMLESCOS.GetVariable("OutXML");

  if(OutXML == "")
     RunError(toansi("Ошибка получения XML"));
  end;

  /*Сохранение XML в файл*/
  if(SYSTEMCODE == UFSysCodeBESP)
    ArcDir = UFDirBESPArc;
    OutDir = UFDirBESPOut;
  else
    ArcDir = UFDirUFEBSArc;
    OutDir = UFDirUFEBSOut;
  end;
  SaveToFile(NameFile, ArcDir, OutDir, OutXML);
  AddPtk ("Отправка ЭС503: создан файл "+OutDir+NameFile);

  return true;

  OnError(e)
    OraTrnRollBack();
    AddPtk ("Отправка ЭС503: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
    return false;

end;

macro GenED503(CNT)
/*Макропроцедура создания ЭС503*/

  var i = 0;
  var Ret;
  var Txt = "";
  var ED503 = TRslOraQuery();

  VclInitProgress(CNT, "Генерация платежей...");
  while(i < CNT)
    i = i + 1;
    Ret = 0;

    ED503.SqlText = " begin  :Ret := RSP_UFEBS.InsertED503(:NumGr, :NForm, :Text); end;";
    ED503.DeclareAndSet("Ret", Ret, "NumGr", i, "NForm", Type_Doc_ED503, "Text", Txt);

    if(not ED503.Execute)
      MsgBox("ЭС503 НЕ создан");
      OraTrnRollback();
      addptk("ED503: "+ED503.Error);
      RunError(toansi("ED503: "+ED503.Error));
      return;
    end;

    Txt = ED503.GetVariable("Text");
    Ret = ED503.GetVariable("Ret");
    addptk(Txt);

    if(Ret)
      if(not Send_ED503(Ret))
        msgBox("Не удается отправить ЭС503");
        addptk(Txt);
      end;
    end;

  end;

  VclRemProgress();

end;

macro GroupPaymTMP()
/*Макропроцедура вызова ХП группировки платежей согласно переданным условиям*/

  var SQLWhere = "";
  var SQLGroup = "";
  var CNT      = 0;
  var GroupPay = TRslOraQuery();
  var GetSQL   = TRslOraQuery();

  SQLWhere = "mtrec.receiver is not null";
  SQLGroup = "mtrec.receiver";

  GroupPay.SqlText = " begin  :CNT := RSP_PAYM_API.MAKEPAYMGROUP(:Where,:Group); end;";
  GroupPay.DeclareAndSet("CNT",CNT,"Where",SQLWhere,"Group",SQLGroup);

  if(not GroupPay.Execute)
    addptk(toansi("GroupPay: "+GroupPay.Error));
    RunError(toansi("GroupPay: "+GroupPay.Error));
  end;

  CNT = GroupPay.GetVariable("CNT");

  if(not CNT)
    SetParm(5,"Ошибка группировки платежей");
    addptk("GroupPay: ошибка группировки платежей.");
    return 0;
  end;

  AddPTk("Платежи сгруппированы по "+CNT+" группам.");

  GenED503(CNT);

  return CNT;
end;

macro InsertED503TMP()
/*Макропроцедура помещения выделенных документов во временную таблицу*/
  var i;
  var ListNum    = PayListNum();
  var ID         = "";
  var VersionRec = "";
  var InsertTMP  = TRslOraQuery();
  var MTREC      = TRslOraQuery();
  var SESID, ISN, SWIFTStr;
  var paymObj;

  VclInitProgress(ListNum, "Создание реестра распоряжений");
  i = 0;
  InsertTMP.SqlText = " insert into "+UFTblTmpGroup+"(ID_PAYM,VR,ID_SSTATE,SESSIONID,SWIFTDOC) values(:ID_PAYM,:VR,:ID_SSTATE,:SESID,:DOC)";

  while(i < ListNum)
    ID = VersionRec = 0;
    PayListField(i, 0, "id_paym", ID, "VersionRec", VersionRec, "PAYM8NODEID", SESID, "PAYM8NODEPAYMENTID", ISN);

    if(not ID)
      MsgBox("Ошибка получения идентификатора. Отправка отменена");
      VclRemProgress();
      return false;
    end;

    if(not VersionRec)
      MsgBox("Ошибка получения VersionRec. Отправка отменена");
      VclRemProgress();
      return false;
    end;

    if(not SESID)
      MsgBox("Ошибка получения номера сессии. Отправка отменена");
      VclRemProgress();
      return false;
    end;

    if(not ISN)
      MsgBox("Ошибка получения номера последовательности. Отправка отменена");
      VclRemProgress();
      return false;
    end;

    MTREC.SqlText = "SELECT str FROM MTREC WHERE id_paym = :ID";
    MTREC.DeclareAndSet("ID",ID);
    if(not MTREC.Execute)/*Получение сообщения*/
      MsgBox("Функция InsertED503TMP:"+MTREC.Error);
      VclRemProgress();
      return false;
    end;

    ISN = String(ISN);
    if(StrLen(ISN) > 6)
      ISN = SubStr(ISN,StrLen(ISN)-5);
    else
      ISN = Padd(ISN,48,6,1);
    end;

    SESID = Padd(int(SESID),48,4,1);

    paymObj = GetPaymByID(ID);
    if(not GetSWIFT(paymObj, SESID, ISN, SWIFTStr))
      MsgBox("Ошибка получения сообщения SWIFT. Отправка отменена");
      VclRemProgress();
      return false;
    end;

    InsertTMP.DeclareAndSet("ID_PAYM",ID, "VR", VersionRec,"ID_SSTATE",UFStateSentCOS,"SESID",SESID+ISN);
    InsertTMP.DeclareAndSetLOB("DOC",SWIFTStr);

    if(not InsertTMP.Execute)/*Вставка во временную таблицу*/
      MsgBox("Функция InsertED503TMP:"+InsertTMP.Error);
      VclRemProgress();
      return false;
    end;

    VclUseProgress(i = i + 1);
  end;

  VclRemProgress();
  RefreshScreen;
  AddPTk("Выбрано "+i+" платеж(ей) для формирования ЭС503.");
  return true;
end;

macro SendList_ED503()
/*Макропроцедура создания ЭС 503*/

  if(not InsertED503TMP())
    OraTrnRollBack();
    return;
  end;

  if(not GroupPaymTMP())
    OraTrnRollBack();
    return;
  end;

  OraTrnCommit();

onError(e)
  OraTrnRollBack();
  AddPtk ("Отправка ЦОС: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
  return false;
end;

macro SendList_ED503_Arm()
  var cmd       = TRslOraQuery();
  var InsertTMP = TRslOraQuery();
  var MTREC     = TRslOraQuery();
  var GroupPay  = TRslOraQuery();
  var GetSQL    = TRslOraQuery();
  var ED503     = TRslOraQuery();
  var SESID,   ISN, SWIFTStr, paymObj, cnt = 0;

  InsertTMP.SqlText = " insert into " + UFTblTmpGroup + "(ID_PAYM, VR, ID_SSTATE, SESSIONID, SWIFTDOC) values(:ID_PAYM, :VR, :ID_SSTATE, :SESID, :DOC)";

  cmd.sqlText = " SELECT P.ID_PAYM, P.VERSIONREC, P.NODEPAYMENTID, P.NODEID "+
                "  FROM paym p                                              "+
                " WHERE P.ID_SSTATE = :ID_SSTATE                            "+
                " ORDER BY P.ID_PAYM";

  cmd.DeclareAndSet("ID_SSTATE", UFStateSOCReadyToSend);
  cmd.Execute();

  if(not cmd.Execute)
     RunError(toansi(cmd.Error));
  end;

  while(not cmd.eof)

     MTREC.SqlText = "SELECT str FROM MTREC WHERE id_paym = :ID";
     MTREC.DeclareAndSet("ID", int(cmd.GetByName("ID_PAYM").AsInteger));

     if(not MTREC.Execute)/*Получение сообщения*/
        RunError(toansi(MTREC.Error));
     end;

     ISN = String(cmd.GetByName("NODEPAYMENTID").AsInteger);

     if(StrLen(ISN) > 6) ISN = SubStr(ISN,StrLen(ISN) - 5);
     else                ISN = Padd(ISN, 48, 6, 1);
     end;

     SESID = Padd(int(cmd.GetByName("NODEID").AsInteger), 48, 4, 1);

     paymObj = GetPaymByID(int(int(cmd.GetByName("ID_PAYM").AsInteger)));

     if(not GetSWIFT(paymObj, SESID, ISN, SWIFTStr))
        RunError(toansi("Ошибка получения сообщения SWIFT. Отправка отменена"));
     end;

     InsertTMP.DeclareAndSet("ID_PAYM",cmd.GetByName("ID_PAYM").AsInteger, "VR", cmd.GetByName("VERSIONREC").AsInteger, "ID_SSTATE", UFStateSentCOS, "SESID", SESID + ISN);
     InsertTMP.DeclareAndSetLOB("DOC", SWIFTStr);

     if(not InsertTMP.Execute)/*Вставка во временную таблицу*/
        RunError(toansi(InsertTMP.Error));
     end;

     cmd.next();
  end;

  if(cmd.rowCount != 0)
    var SQLWhere = "mtrec.receiver is not null";
    var SQLGroup = "mtrec.receiver";

    GroupPay.SqlText = " begin  :CNT := RSP_PAYM_API.MAKEPAYMGROUP(:Where,:Group); end;";
    GroupPay.DeclareAndSet("CNT", CNT, "Where", SQLWhere, "Group", SQLGroup);

    if(not GroupPay.Execute)
       RunError(toansi(GroupPay.Error));
    end;

    CNT = GroupPay.GetVariable("CNT");

    if(not CNT)
       RunError(toansi("Ошибка группировки платежей"));
    end;

    var i = 1, ret, txt = "";

    while(i < 1 + CNT)

      ret = 0;

      ED503.SqlText = " begin  :Ret := RSP_UFEBS.InsertED503(:NumGr, :NForm, :Text); end;";
      ED503.DeclareAndSet("Ret", Ret, "NumGr", i, "NForm", Type_Doc_ED503, "Text", Txt);

      if(not ED503.Execute)
         RunError(toansi("ED503: " + ED503.Error));
      end;

      Txt = ED503.GetVariable("Text");
      Ret = ED503.GetVariable("Ret");

      if(not Ret)
         addptk("Не удается  отправить ЭС503: "+Txt);
      else
        if(not Send_ED503(Ret))
           RunError(toansi("Не удается  отправить ЭС503 "));
        end;
        addptk(Txt);
      end;
      i = i + 1;
    end;

    OraTrnCommit();
  end;

  return true;
onError(e)
  OraTrnRollBack();
  AddPtk ("Отправка ЦОС: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
  AddPtk("----------------------");

  return false;
end;

macro ProcessED503(Obj)
  var cmd = TRslOraQuery();
  var upd = TRslOraQuery();
  var ListNum = PayListNum();
  var EDCOS = Obj.Rec;
  var PaymID, i = 0, IDEd;

  VclInitProgress(ListNum, "Обработка ED503");
  cmd.sqlText = " SELECT es.id_ed503swift,       "+
                "        es.docswift             "+
                "  FROM ed503swift es,           "+
                "       ed503 e                  "+
                " WHERE es.id_ed503 = e.id_ed503 "+
                "   AND e.id_ed503 = :ID         "+
                "   AND e.versionrec = :VR";

  cmd.DeclareAndSet("ID", EDCOS.ED5038ID_ED503, "VR", EDCOS.ED5038VERSIONREC);

  if(not cmd.Execute)
     RunError(toansi(cmd.Error));
  end;

  var SWIFTStr;

  while(not cmd.eof)
    SWIFTStr = cmd.Rec.docswift;
    IDEd = EDCOS.ED5038ID_ED503;
    LoadMultiMsgFileTrn(SWIFTStr, IDEd, @PaymID);

    upd.sqlText = " UPDATE ed503swift es           "+
                  "    SET es.id_paym = "+PaymID+" "+
                  "  WHERE es.id_ed503swift = "+cmd.Rec.id_ed503swift;

    if(not upd.Execute)
       RunError(toansi(cmd.Error));
    end;

    i = i +1;
    cmd.next();
  end;

  AddPtk("Обработка ED503 ID="+int(IDEd)+": создано "+i+" платежей.");

  upd.sqlText = " UPDATE edcos es      "+
                "    SET es.status = 2 "+
                "  WHERE es.id_edcos = "+EDCOS.EDCOS8ID_EDCOS;

  if(not upd.Execute)
     RunError(toansi(cmd.Error));
  end;

  OraTrnCommit();
  VclRemProgress();
  RefreshScreen;
  return true;

onError(e)
  OraTrnRollBack();
  AddPtk ("Обработка ED503: " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
  AddPtk("----------------------");

  return false;
end;
