import ws_panel, general;



// Возвращает текст запроса для скроллинга
private macro GetSQLText(scrollingId, filter, order)
    var cmd = TRslOraQuery();
    cmd.SqlText = "select RSP_WEB.GET_LISTDOC_FROM_MENU(:ID_NMENU, :P_ORDER, :P_FILTER) QueryText from DUAL";
    cmd.DeclareAndSet("ID_NMENU", scrollingId, "P_ORDER", order, "P_FILTER", filter);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.Rec.QueryText;
end;

// Возвращает записи для списка
macro GetRowsService(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var cmd = TRslOraQuery();
    cmd.SqlText = SQL_WrapSelect(GetSQLText(params.ScrollingId, GetSqlFilterCondition(params.FilterItems), GetOrderStr(params.OrderItems)), params.PageNumber, params.PageSize);
    if (Index(cmd.SqlText, ":ID_RECORD"))
        cmd.DeclareAndSet("ID_RECORD", wParams.id_record);
    end;
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd;
end;

// Возвращает количество записей для списка
macro GetRowCountService(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT COUNT(*) CNT FROM (" + GetSQLText(params.ScrollingId, GetSqlFilterCondition(params.FilterItems), "") + ")";
    if (Index(cmd.SqlText, ":ID_RECORD"))
        cmd.DeclareAndSet("ID_RECORD", wParams.id_record);
    end;
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.GetByName("CNT").AsInteger;
end;

// Сервис скроллинга тулбара
macro GetScrollingToolbar(params)
    // Получение таблицы формы и первичного ключа таблицы
    var tableName: string = "";
    var PKField: string = "";
    var cmd = TRslOraQuery();
    cmd.SqlText = "begin RSP_FORM.GET_TABLE_PK_FORMMENU(:IDNMenu, :Table, :PK); end;";
    cmd.DeclareAndSet("IDNMenu", params.ScrollingId, "Table", tableName, "PK", PKField);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    tableName = cmd.GetVariable("Table");
    PKField = cmd.GetVariable("PK");
    // Получение ИД записи
    var recordId = 0;
    if (params.CurrentRow != null)
        recordId = GenGetProp(params.CurrentRow, tableName + "8" + PKField);
    end;
    return WebServiceResult(WebServiceResultType.Window, ScrollingWindow(params.Params, int(recordId)));
end;

// Сервис скроллинга справочника тулбара панели
macro GetScrollingDictToolbar(params)
    // Получение таблицы формы и первичного ключа таблицы
    var tableName: string = "";
    var PKField: string = "";
    var cmd = TRslOraQuery();
    cmd.SqlText = "begin RSP_FORM.GET_TABLE_PK_FORMMENU(:IDNMenu, :Table, :PK); end;";
    cmd.DeclareAndSet("IDNMenu", params, "Table", tableName, "PK", PKField);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    tableName = cmd.GetVariable("Table");
    PKField = cmd.GetVariable("PK");
    // Получение ИД записи
    var recordId = tableName + "8" + PKField;
    return WebServiceResult(WebServiceResultType.Window, ScrollingWindow(params, int(recordId)));
end;

macro AddAction(params)
    var result: WebServiceResult;
    var cmd = TRslOraQuery();
    cmd.SqlText = "select id_smenu, id_formspr, decode(id_nformdn, null, id_nform, id_nformdn) FormId from NMENU where id_nmenu = :MenuId";
    cmd.DeclareAndSet("MenuId", params.ScrollingId);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    var menuType = cmd.GetByName("id_smenu").AsInteger;
    var formId;
    if ((menuType == TypeMainMenuDict) or (menuType == TypeMenuDictRef))
        formId = cmd.GetByName("id_formspr").AsInteger;
    else
        formId = cmd.GetByName("FormId").AsInteger;
    end;
    cmd.Clear();
    cmd.SqlText = "select id_wpanel, panel from WPANEL where usernick = :Oper and form = :FormId and menu = :ScrollingId";
    cmd.DeclareAndSet("Oper", string({Oper}), "FormId", formId, "ScrollingId", params.ScrollingId);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    if (cmd.Eof)
        var panel = PanelWindow(params);
        var panelXml = Rsl2Xml(panel);
        var wParams = WebServiceParams(0, 0, 0, 0, 0);
        panel.GetObjectService.Params = wParams.ToStr();
        cmd.Clear();
        cmd.SqlText = "insert into WPANEL (USERNICK, FORM, MENU, PANEL) values (:Oper, :FormId, :ScrollingId, :PanelXml)";
        cmd.DeclareAndSet("Oper", string({Oper}), "FormId", formId, "ScrollingId", params.ScrollingId);
        cmd.DeclareAndSetLOB("PanelXml", Rsl2Xml(panel));
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        OraTrnCommit();
        result = WebServiceResult(WebServiceResultType.Window, makeObj("TRslXmlStr", "XmlStr", panelXml));
    else
        panel = Xml2Rsl(cmd.GetByName("panel").AsString);
        var tableName: string = "";
        var PKField: string = "";
        cmd.Clear();
        cmd.SqlText = "begin RSP_FORM.GET_TABLE_PK_FORMMENU(:IDNMenu, :Table, :PK); end;";
        cmd.DeclareAndSet("IDNMenu", params.ScrollingId, "Table", tableName, "PK", PKField);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        tableName = cmd.GetVariable("Table");
        PKField = cmd.GetVariable("PK");
        var id = 0;
        var version = 0;
        var visaTypeId: integer = 0;
        wParams = WebServiceParams(
            id,
            version,
            IIF((menuType == TypeMenuFormDep) or (menuType == TypeMenuDictRef), IIF(params.Params, params.Params, 0), 0),
            IIF((menuType == TypeMenuFormDep) or (menuType == TypeMenuDictRef), params.ScrollingId, 0),
            visaTypeId
        );
        panel.GetObjectService.Params = wParams.ToStr();
        result = WebServiceResult(WebServiceResultType.Window, panel);
    end;
    return result;
end;

private macro ChangeMenuProperties(toolbarItems: TArray, scrollingName: string, params)
    for (var toolbarItem, toolbarItems)
        if (toolbarItem.Type == ToolbarItemType.MenuButton)
            var menuItemIndex: integer = -1;
            var i: integer = 0;
            for (var menuItem, toolbarItem.MenuItems)
                if (menuItem.Type == ToolbarItemType.Button)
                    menuItemIndex = i;
                    if (scrollingName == LeftPositioningSrollingName)
                        toolbarItem.MenuItems[menuItemIndex + 1].Text = "Перенести";
                        toolbarItem.MenuItems[menuItemIndex + 1].Tooltip = "Перенести";
                        toolbarItem.MenuItems[menuItemIndex + 1].Service = WebServiceCallback("ws_positioning", "MoveRight", params);
                        toolbarItem.MenuItems[menuItemIndex + 1].ConfirmationType = null;
                        toolbarItem.MenuItems[menuItemIndex + 1].NeedSelectedRows = true;
                    elif (scrollingName == RightPositioningSrollingName)
                        toolbarItem.MenuItems[menuItemIndex].Text = "Вернуть";
                        toolbarItem.MenuItems[menuItemIndex].Tooltip = "Вернуть";
                        toolbarItem.MenuItems[menuItemIndex].Service = WebServiceCallback("ws_positioning", "MoveLeft", params);
                        toolbarItem.MenuItems[menuItemIndex].ConfirmationType = null;
                        toolbarItem.MenuItems[menuItemIndex].NeedSelectedRows = true;
                    end;
                    return;
                end;
                i = i + 1;
            end;
        end;
    end;
end;

private macro ChangeButtonsProperties(toolbarItems: TArray, scrollingName: string, params)
    var buttonIndex: integer = -1;
    var i: integer = 0;
    for (var toolbarItem, toolbarItems)
        if (toolbarItem.Type == ToolbarItemType.Button)
            buttonIndex = i;
            if (scrollingName == LeftPositioningSrollingName)
                toolbarItems[buttonIndex + 1].Text = "Перенести";
                toolbarItems[buttonIndex + 1].Tooltip = "Перенести";
                toolbarItems[buttonIndex + 1].Service = WebServiceCallback("ws_positioning", "MoveRight", params);
                toolbarItems[buttonIndex + 1].ConfirmationType = null;
                toolbarItems[buttonIndex + 1].NeedSelectedRows = true;
            elif (scrollingName == RightPositioningSrollingName)
                toolbarItems[buttonIndex].Text = "Вернуть";
                toolbarItems[buttonIndex].Tooltip = "Вернуть";
                toolbarItems[buttonIndex].Service = WebServiceCallback("ws_positioning", "MoveLeft", params);
                toolbarItems[buttonIndex].ConfirmationType = null;
                toolbarItems[buttonIndex].NeedSelectedRows = true;
            end;
            return;
        end;
        i = i + 1;
    end;
end;

macro EditAction(params)
    var result: WebServiceResult;
    if (params.CurrentRow == null)
        result = WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Нет выделенных записей", MessageBoxType.Error));
    else
        var wParams = WebServiceParams();
        wParams.FromStr(params.Params);
        var scrollingName = wParams.id_scrolling;
        var correspondentSchemeId = wParams.id_record;
        var cmd = TRslOraQuery();
        cmd.SqlText = "select id_smenu, id_formspr, decode(id_nformdn, null, id_nform, id_nformdn) FormId from NMENU where id_nmenu = :MenuId";
        cmd.DeclareAndSet("MenuId", params.ScrollingId);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        var menuType = cmd.GetByName("id_smenu").AsInteger;
        var formId = params.CurrentRow.ID_NFORM;
        cmd.Clear();
        cmd.SqlText = "select id_wpanel, panel from WPANEL where usernick = :Oper and form = :FormId and menu = :ScrollingId";
        cmd.DeclareAndSet("Oper", string({Oper}), "FormId", formId, "ScrollingId", params.ScrollingId);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        var panel;
        if (cmd.Eof)
            params.Params = null;
            panel = PanelWindow(params);
            if (panel.Fields.Size == 0)
                return WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка открытия формы!", "Для данной формы не заданы настройки отображения", MessageBoxType.Error));
            end;
            var panelXml;
            if ((scrollingName == LeftPositioningSrollingName) or (scrollingName == RightPositioningSrollingName))
                var objectServiceParams = panel.GetObjectService.Params;
            else
                panelXml = Rsl2Xml(panel);
            end;
            wParams = WebServiceParams(0, 0, 0, 0, 0);
            panel.GetObjectService.Params = wParams.ToStr();
            cmd.Clear();
            cmd.SqlText = "insert into WPANEL (USERNICK, FORM, MENU, PANEL) values (:Oper, :FormId, :ScrollingId, :PanelXml)";
            cmd.DeclareAndSet("Oper", string({Oper}), "FormId", formId, "ScrollingId", params.ScrollingId);
            cmd.DeclareAndSetLOB("PanelXml", Rsl2Xml(panel));
            if (not cmd.Execute())
                RunError(ToANSI(cmd.Error));
            end;
            OraTrnCommit();
            if ((scrollingName == LeftPositioningSrollingName) or (scrollingName == RightPositioningSrollingName))
                wParams = WebServiceParams(correspondentSchemeId);
                ChangeMenuProperties(panel.ToolbarItems, scrollingName, wParams.ToStr());
                ChangeButtonsProperties(panel.ToolbarItems, scrollingName, wParams.ToStr());
                panel.GetObjectService.Params = objectServiceParams;
                result = WebServiceResult(WebServiceResultType.Window, panel);
            else
                result = WebServiceResult(WebServiceResultType.Window, makeObj("TRslXmlStr", "XmlStr", panelXml));
            end;
        else
            panel = Xml2Rsl(cmd.GetByName("panel").AsString);
            if ((scrollingName == LeftPositioningSrollingName) or (scrollingName == RightPositioningSrollingName))
                wParams = WebServiceParams(correspondentSchemeId);
                ChangeMenuProperties(panel.ToolbarItems, scrollingName, wParams.ToStr());
                ChangeButtonsProperties(panel.ToolbarItems, scrollingName, wParams.ToStr());
            end;
            var tableName: string = "";
            var PKField: string = "";
            cmd.Clear();
            cmd.SqlText = "begin RSP_FORM.GET_TABLE_PK_FORMMENU(:IDNMenu, :Table, :PK); end;";
            cmd.DeclareAndSet("IDNMenu", params.ScrollingId, "Table", tableName, "PK", PKField);
            if (not cmd.Execute())
                RunError(ToANSI(cmd.Error));
            end;
            tableName = cmd.GetVariable("Table");
            PKField = cmd.GetVariable("PK");
            var id = GenGetProp(params.CurrentRow, tableName + "8" + PKField);
            var version = params.CurrentRow.VersionRec;
            var visaTypeId: integer = 0;
            if (menuType == TypeMainMenuVisa)
                visaTypeId = params.CurrentRow.ID_NVTYP;
            end;
            wParams = WebServiceParams(
                id,
                version,
                IIF(menuType == TypeMenuFormDep, IIF(params.Params, params.Params, 0), 0),
                IIF(menuType == TypeMenuFormDep, params.ScrollingId, 0),
                visaTypeId
            );
            panel.GetObjectService.Params = wParams.ToStr();
            result = WebServiceResult(WebServiceResultType.Window, panel);
        end;
    end;
    return result;
end;

macro DeleteAction(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    // Получение таблицы формы и первичного ключа таблицы
    var tableName: string = "";
    var PKField: string = "";
    var cmd = TRslOraQuery();
    cmd.SqlText = "begin RSP_FORM.GET_TABLE_PK_FORMMENU(:IDNMenu, :Table, :PK); end;";
    cmd.DeclareAndSet("IDNMenu", params.ScrollingId, "Table", tableName, "PK", PKField);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    tableName = cmd.GetVariable("Table");
    PKField = cmd.GetVariable("PK");
    var IDRec = 0, VR = 0, Rec = 0;
    var result: WebServiceResult;
    // ХП удаления записей
    cmd.Clear();
    cmd.SqlText = "begin :Rec := RSP_WEB.DEL_RECFROMLIST(:Table, :PK, :IDRec, :VRec); end;";
    if (params.SelectedRows.Size == 0) // Нет выделенных, есть только текущая
        // Получение ИД записи
        IDRec = GenGetProp(params.CurrentRow, tableName + "8" + PKField);
        // Получение версии записи
        VR = params.CurrentRow.VERSIONREC;
        cmd.DeclareAndSet("Rec", Rec, "Table", tableName, "PK", PKField, "IDRec", IDRec, "VRec", VR);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        if (cmd.GetVariable("Rec") == 0)
            OraTrnCommit();
            result = WebServiceResult(WebServiceResultType.Action, IAction(WebServiceActionType.Refresh));
        else
            OraTrnRollBack();
            result = WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Запись конкурентно изменена", MessageBoxType.Error));
        end;
    else // Бежим по выбранным записям
        for (var selectedRow, params.SelectedRows)
            // Получение ИД записи
            IDRec = GenGetProp(selectedRow, tableName + "8" + PKField);
            // Получение версии записи
            VR = selectedRow.VERSIONREC;
            // Удаление записи
            cmd.DeclareAndSet("Rec", Rec, "Table", tableName, "PK", PKField, "IDRec", IDRec, "VRec", VR);
            if (not cmd.Execute())
                RunError(ToANSI(cmd.Error));
            end;
            if (cmd.GetVariable("Rec") == 0)
                OraTrnCommit();
                result = WebServiceResult(WebServiceResultType.Action, IAction(WebServiceActionType.Refresh));
            else
                OraTrnRollBack();
                result = WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Запись конкурентно изменена", MessageBoxType.Error));
                break;
            end;
        end;
    end;
    return result;
end;

// Пользовательская сортировка
macro ReOrderRows(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var result: WebServiceResult;
    if (params.SelectedRows.Size == 0) // Нет выделенных записей
        result = WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Нет выделенных записей", MessageBoxType.Error));
    else
        var IDRec = 0, VR = 0, RN = 0, Table = "", PK = "", tableFieldName= "";
        var cmd = TRslOraQuery();
        cmd.SqlText = "begin RSP_FORM.Get_Table_PK_FormMenu(:MenuId, :Table, :PK); end;";
        cmd.DeclareAndSet("MenuId", params.ScrollingId, "Table", Table, "PK", PK);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        tableFieldName = cmd.GetVariable("Table")+"8"+cmd.GetVariable("PK");
        cmd.Clear();
        cmd.SqlText = "INSERT INTO TMP_USERORDER (ID, "+FieldVersion+", ISMOVE, RN) VALUES (:IDRec, :VR, :Move, :RN)";
        for (var selectedRow, params.SelectedRows)
            // Получение ИД записи
            IDRec = GenGetProp(selectedRow, tableFieldName);
//            IDRec = selectedRow.PAYM8ID_PAYM;
            // Получение версии записи
            VR = selectedRow.VERSIONREC;
            // Получение rownum записи
            RN = selectedRow.RN;
            // Вставка перемещаемой записи во временную таблицу
            cmd.DeclareAndSet("IDRec", IDRec, "VR", VR, "Move", 1, "RN", RN);
            if (not cmd.Execute())
                RunError(ToANSI(cmd.Error));
            end;
        end;
        // Выполнение пользовательской сортировки
        var operation: integer;
        if (params.Direction == MoveDirection.Up)
            operation = 1;
        elif (params.Direction == MoveDirection.Down)
            operation = 2;
        elif (params.Direction == MoveDirection.Top)
            operation = 3;
        elif (params.Direction == MoveDirection.Bottom)
            operation = 4;
        end;
        var Ret = 0;
        cmd.Clear();
        cmd.SqlText = "begin :Ret := RSP_WEB.ReOrderUser(" + params.ScrollingId + ", " + operation + ", :filter); end;";
        cmd.DeclareAndSet("Ret", Ret, "filter", GetSqlFilterCondition(params.FilterItems));
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        // Кол-во перемещенных записей
        Ret = cmd.GetVariable("Ret");
        if (Ret == params.SelectedRows.Size)
            OraTrnCommit();
            result = WebServiceResult(WebServiceResultType.Action, IAction(WebServiceActionType.Refresh));
        else
            OraTrnRollback();
            result = WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Запись конкурентно изменена", MessageBoxType.Error));
        end;
    end;
    return result;
end;

// Подсчет суммы по столбцу
macro CalculateSum(params): ColumnSumServiceResult
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var result = ColumnSumServiceResult();
    if (params.SelectedRows.Size > 0)
        var sum: double = 0;
        for (var selectedRow, params.SelectedRows)
            var value = GenGetProp(selectedRow, params.ColumnId);
            if (value != null)
                sum = sum + value;
            end;
        end;
        result.SelectedItemsSum = sum * 100;
    else
        result.SelectedItemsSum = 0;
    end;
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT nvl(SUM(" + params.ColumnId + "), 0) SUM FROM (" + GetSQLText(params.ScrollingId, GetSqlFilterCondition(params.FilterItems), "") + ")";
    if (Index(cmd.SqlText, ":ID_RECORD"))
        cmd.DeclareAndSet("ID_RECORD", wParams.id_record);
    end;
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    result.TotalSum = IIF(cmd.Rec.SUM == null, 0, cmd.Rec.SUM) * 100;
    return result;
end;
