import ws_types;



macro Ws_Visa(params, CurrentItem, items, Step, Varlen, Visa)

    if(CurrentItem == null)//пустой список
      Return;
    end;

    var cmd = TRslOraQuery();
    var item;
    var i = 0, IDRec = 0, IDNVTyp = 0, Coment = "", VR = 0;
    var Ret = "", Txt = "";

    cmd.sqlText = " INSERT INTO tmp_paym_stepvisa(id_paym, id_sstate, id_nvtyp, varlen, vr) "+
                  "   VALUES(:IDRec, :State, :NVTYP, :VARLEN, :VR)";

    if((not items) or (not items.Size))//Нет выделенных, есть только текущая
    //Получение ИД записи
      IDRec = CurrentItem.PAYM8ID_PAYM;
    //Получение ИД визы
      if(Visa)
        IDNVTyp = Visa;
      else
        IDNVTyp = CurrentItem.ID_NVTYP;
      end;
    //Получение версии записи
      if(Visa)
        VR = CurrentItem.PAYM8VERSIONREC;
      else
        VR = CurrentItem.VERSIONREC;
      end;
    //Получение комментария
      if(Varlen)
        Coment = Varlen;
      else
        if(Visa)
          Coment = GetColByColStr("NVTYP", "TEXT", "ID_NVTYP", IDNVTyp);
        else
          Coment = CurrentItem.TEXT;
        end;
      end;

      cmd.DeclareAndSet("IDRec", IDRec, "State", params, "NVTYP", IDNVTyp, "VARLEN", Coment, "VR", VR);
      if(not cmd.execute)
        runError(toansi(cmd.error));
      end;
    else
      while(i<items.Size)
        item = items[i];

      //Получение ИД записи
        IDRec = item.PAYM8ID_PAYM;
      //Получение ИД визы
        IDNVTyp = item.ID_NVTYP;
      //Получение версии записи
        VR = item.VERSIONREC;
      //Получение комментария
        if(Varlen)
          Coment = Varlen;
        else
          Coment = item.TEXT;
        end;

        cmd.DeclareAndSet("IDRec", IDRec, "State", params, "NVTYP", IDNVTyp, "VARLEN", Coment, "VR", VR);
        if(not cmd.execute)
           runError(toansi(cmd.error));
        end;

        i = i + 1;
      end;
    end;

    cmd.Clear;

    cmd.sqlText = " begin :Ret := RSP_STEPS.VS_VisaRun(:Step, :Text); end; ";

    cmd.DeclareAndSet("Ret", Ret, "Step", Step, "Text", Txt);

    if(not cmd.execute)
      runError(toansi(cmd.error));
    end;

    Txt = cmd.GetVariable("Text");
    Ret = cmd.GetVariable("Ret");

    if(Ret != 0)
      OraTrnRollBack();
      runError(toansi(Txt));
      return;
    else
      OraTrnCommit();
    end;

end;

/*-----------------------------------------------*/
/*Сервис "Завизировать"*/
macro VisaOK(params, FormID, CurrentItem)
  var wParams = WebServiceParams();
  var State, Visa, items;

  if (params.CurrentRow == null)
      return WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Нет выделенных записей", MessageBoxType.Error));
  else
    if(CurrentItem)
      wParams.FromStr(params);
      State = wParams.id_record;
      Visa = wParams.version_record;
    else
      CurrentItem = params.CurrentRow;
      items = params.SelectedRows;
      State = params.Params
    end;
  end;

  Ws_Visa(State, CurrentItem, items, 1, "", Visa);
end;

/*-----------------------------------------------*/
/*Сервис "Отказать"*/
macro VisaNO(params, FormID, CurrentItem, Varlen)
  var wParams = WebServiceParams();
  var State, Visa, items;

  if (params.CurrentRow == null)
      return WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Нет выделенных записей", MessageBoxType.Error));
  else
    if(CurrentItem)
      wParams.FromStr(params);
      State = wParams.id_record;
      Visa = wParams.version_record;
    else
      CurrentItem = params.CurrentRow;
      items = params.SelectedRows;
      State = params.Params;
      Varlen = params.Comment;
    end;
  end;

  Ws_Visa(State, CurrentItem, items, 2, Varlen, Visa);
end;

/*-----------------------------------------------*/
