/*****************************************************************************
  Формирование выписок SWIFT/TELEX                    Транскредитбанк,Россия

21-09-2001 ДК
******************************************************************************/
import general, utils, formvip, transfld;

file pay(p_Paym);
file doclink(p_DocLnk);
file korr(p_nKorr);
file ks(p_nKS);
file ksa(p_nKSA);
file route(p_nRoute);
file ass(p_PayAss);
file cur(p_sFI);

file ftrr("trans_sw.usr","swift.def");
file ftrr5("trans_s5.usr","swift.def");
file ftr("translit.usr","swift.def");
file emsg() txt 4096 write;  /* Файл выписки SWIFT/TELEX */

const OpName = "Формирование выписок ";
const CREDIT_TAG  = "C",
      DEBET_TAG   = "D";
const DocsPerPage  = 37;
array StrNext, VipFiles;

var TotalVipCount = 0,
    DateIn, DateOut, RestIn, RestMOut = 0,
    Name_Kor = "", FIID_Kor = "",
    Reference, ClearRef,
    VipNumber, /* Номер выписки */
    BicKorr,   /* Для заголовка SWIFT  */
    TrlTbl,   /* Ссылка на таблицу транслитерации */
    FIID, Code_Currency, ISOFIID, Account, VipDate, MakeEmpty,
    isPlanRest ,  /* true - учитывать ли планируемый остаток */
    VipDocCount,  /* Количество документов в выписке */
    PageDocCount, /* Количество документов на странице для SWIFT */
    PageNumber,   /* номер текущей страницы для SWIFT */
    PageBeginDate,  /* Дата для 60-го поля */
    IntermBalance,
    WorkDir, WorkPath, NameFile, OutDir,
    drNotEOF, AssNotEOF, NumReferDoc, OldRefer,
    ksCount, KsNotEOF,
    TrnMsg, DocRef,
    i = 0,
    scr,lb1,ed1,lb2,ed2,lb3,ed3,lb4,ed4,bt1,bt2,bt3,lb5;

Array days_per_month;
days_per_month (0)  = days_per_month (2) = days_per_month (4) =
days_per_month (6)  = days_per_month (7) = days_per_month (9) =
days_per_month (11) = 31;
days_per_month (3)  = days_per_month (5) = days_per_month (8) =
days_per_month (10) = 30;
days_per_month (1)  = 28;

/*
macro BrkPoint()
MsgBox("Reference ", Reference,
   "|ClearRef ",     ClearRef,
   "|VipNumber ",    VipNumber,
   "|BicKorr ",      BicKorr,
   "|FIID ",         FIID,
   "|Code_Currency ",Code_Currency,
   "|ISOFIID ",      ISOFIID,
   "|Account ",      Account,
   "|VipDocCount ",  VipDocCount,
   "|PageDocCount ", PageDocCount,
   "|PageNumber ",   PageNumber,
   "|cDate ",        cDate,
   "|IntermBalance ",IntermBalance,
   "|WorkDir ",      WorkDir,
   "|WorkPath ",     WorkPath,
   "|NameFile ",     NameFile,
   "|NumReferDoc ",  NumReferDoc,
   "|OldRefer ",     OldRefer
);
end;
*/
MACRO prnMsg (str)
  [#
  ](str);
END;
/* номер  дня по порядку */
MACRO GetDayNumber (dt)
  var d,m,y, ret = 0, i;
  datesplit (dt,d,m,y);
  i = 0;
  if ((y - int(y /2)) > 0  )
    days_per_month(1) = 29;
  end;
  while ( i< (m - 1))
   ret = ret + days_per_month(i);
   i = i + 1;
  end;
  ret = ret + d;
  return ret;
END;

macro RslMsgBox()
  var retval = "", parm, i=0;
  while(GetParm(i, parm))
    retval = retval + string(parm);
    i=i+1;
  end;
  VclMsgBox(retval);
end;
ReplaceMacro("MsgBox", "RslMsgBox");


macro FIID2Str(FIID)
  Keynum(Cur,0);
  Cur.FIID = FIID;
  if(GetEQ(Cur))
    return Cur.StrCode;
  end;
  return "";
end;

macro Sum2SWIFT(Sum, FIID)
  var retval = string(Sum);
  strset(retval, index(retval, "."), ",");
  if(substr(retval, strlen(retval), 1) == "0")
    retval  = substr(retval, 1, strlen(retval)-1);
  end;
  if(substr(retval, strlen(retval), 1) == "0")
    retval  = substr(retval, 1, strlen(retval)-1);
  end;
  retval = trim (retval);
  if (substr(retval,1,1) == "-")
   retval = substr (retval, 2);
  end;
  return FIID2Str(FIID) + retval;
end;


/* Вставляет всегда в emsg */
macro tInsert(str)
  if(valtype(str) != V_UNDEF)
    if(not insert(emsg, str))
      TrnMsg = "Ошибка вывода в файл " + FileName(emsg);
      return false;
    end;
  elif(strlen(emsg.str))
    if(not insert(emsg))
      TrnMsg = "Ошибка вывода в файл " + FileName(emsg);
      return false;
    end;
  end;
  return true;
end;


macro Date2SWIFT(_date);
  var dd=0, mm=0, yy=0;
  DateSplit(_date, dd, mm, yy);
  yy = yy - (yy / 100) * 100;
  return LeadZero1(String(yy), 2) + LeadZero1(String(mm), 2) + LeadZero1(String(dd), 2);
end;

macro Date2Vadim(dt)
  dt = trim(string(dt));
  if (strlen(dt) < 10)
   dt = "0"+ dt;
  end;
  return ChSubStr(dt, "-", ".");
end;

macro SchNumber(ks)
  return ks.Korr + "/" + ks.Num + "/" + ks.FIID;
end;

macro FindKorr(KorrNum)
  Korr.Num = KorrNum;
  return GetEQ(Korr);
end;

macro FindKS(KorrNum, SchNum, FIID)
  keynum(ks, 0);
  ks.Korr = KorrNum;
  ks.Num  = SchNum;
  ks.FIID = FIID;
  return GetEQ(ks);
end;

macro FindKSA(KorrNum, SchNum, FIID, AccKind)
  keynum(ksa, 0);
  ksa.KorrNum = KorrNum;
  ksa.SchNum  = SchNum;
  ksa.FIID    = FIID;
  ksa.AccKind = AccKind;
  return GetEQ(ksa);
end;

macro FindK_KSA(KorrNum, SchNum, FIID, AccKind)
  return ((FindKorr(KorrNum)) AND
          (FindKSA(KorrNum, SchNum, FIID, AccKind)))
end;

macro GetFirstLine(str)
  if(index(str, RN))
    return substr(str, 1, index(str, RN)-1);
  end;
  return str;
end;

/*
   Формирует строку в формате SWIFT остаток/сумму, например C010126RUR1233373,60
*/
macro GetSwiftRest(Rest, date_Rest, FIID)
  if(Rest < 0)
    return DEBET_TAG + Date2SWIFT(date_Rest) + Sum2SWIFT(-Rest, FIID);
  else
    return CREDIT_TAG + Date2SWIFT(date_Rest) + Sum2SWIFT(Rest, FIID);
  end;
end;

macro PrintVipLog()
  var fstr = "", i=1;

  if(asize(VipFiles))
    [ ### ######################## ###############################################]
      (ksa.FIID:r, ksa.AccNum:f, OutDir + VipFiles(0));

    while(i<asize(VipFiles))
    [                              ###############################################]
      (OutDir + VipFiles(i));
      i=i+1;
    end;
/*  StrNext(TotalVipCount + 4) =  "│"  + Padd(ks.Name,32,28) + "│ "+  Padd(SubStr(Sum2SWIFT(RestInp,ksa. FIID),1,3),32,12)
    + "│ "  +   Padd(Date2SWIFT(LastDate),32,14) + "│ "+   Padd(Abs(RestInp),32,17) + "│ "+   Padd(Date2SWIFT(VipDate),32,16)
     + "│ " + Padd(IntermBalance,32,16) + "│" ;
    StrNext(TotalVipCount + 4) = StrNext(TotalVipCount + 4) + RN + "├────────────────────────────┼─────────────┼───────────────┼──────────────────┼─────────────────┼─────────────────┤";

    TotalVipCount = TotalVipCount + 1;*/
  end;

end;


/* Печать заголовка SWIFT */
macro HeaderSWIFT()
  var ind, Name_Depart, AddressMail, tmpDate, NewExt = "";

  if(ks.FrnSys == TelexIdGate)
    NewExt = TelexFileExt;
  elif (Index(ks.FrnSys,SwiftIdGate))
    NewExt = ".mxl";
  end;

  NameFile = "v" + LeadZero1(korr.Num, 4)
                 + substr(ClearRef, 11, 3)
                 + LeadZero1(PageNumber, 2) + NewExt;
  WorkPath = WorkDir + NameFile;
  if(not Open(emsg, WorkPath))
    TrnMsg = "Ошибка открытия файла " + FileName(emsg);
    return false;
  end;

  if(ks.FrnSys == TelexIdGate)
    if(ind=Index(korr.Name,","))
      Name_Depart=Trim(Substr(korr.Name,1,ind-1));
      AddressMail=Trim(Substr(korr.Name,ind+1));
    else
      Name_Depart=Trim(korr.Name);
      AddressMail="";
    end;


    emsg.str = "YZYZ"+RN + RN +
            "TO  : " + string(transfield("", Name_Depart, 0, ftr):35) + RN +
            "FROM: TRANSKREDITBANK" + RN +
            "DATE: "+ Date2SWIFT(date()) + RN +RN + "::950" + RN;
  elif (Index(ks.FrnSys,SwiftIdGate))
   NewExt = ".mxl";
   emsg.str = "    {1:F01" + BankSwift + "AXXX0000000000}{2:I950" +
             substr(BICKorr,1,8) + "X" + substr(BICKorr, 9, 3) +
             "N}{4:" + RN;
  end;

  emsg.str =  emsg.str + ":20:" + Reference + RN +
             ":25:" + Account + RN +
            /* ":28C:" + string(VipNumber,"/",PageNumber) + RN;*/
             ":28C:" + string(GetDayNumber(VipDate),"/",PageNumber) + RN;
  if(PageNumber == 1)
    emsg.str  = emsg.str + ":60F:";
    tmpDate = LastDate;
  elif(PageNumber > 1)
    emsg.str  = emsg.str + ":60M:";
    tmpDate = PageBeginDate;
  end;
  if (tmpDate == date(0,0,0) )
     tmpDate = VipDate - 1;
  end;
  emsg.str = emsg.str + GetSwiftRest(IntermBalance, tmpDate, ksa.FIID);
  Name_Kor = Padd(ks.Name,32,28);
  FIID_Kor = Padd(SubStr(Sum2SWIFT(RestInp,ksa. FIID),1,3),32,12);
  DateIn = tmpDate;
  RestIn = IntermBalance;

  if ( not tinsert())
    TrnMsg = "Ошибка записи заголовка выписки";
    return false;
  end;
  return true;

end;

/* Печать окончания */
MACRO FooterSWIFT(isFinal)

  if(isFinal)
    emsg.str = ":62F:";
  else
    emsg.str = ":62M:";
  end;

  emsg.str = emsg.str +  GetSwiftRest(IntermBalance, VipDate, ksa.FIID);
  if(ks.FrnSys == TelexIdGate)
    emsg.str = emsg.str + RN + RN + "NNN" + RN + RN;
  elif(Index(ks.FrnSys,SWIFTIdGate))
    emsg.str = emsg.str + RN + "-}";
  end;
  DateOut = VipDate;
  RestMOut = IntermBalance;

     StrNext(TotalVipCount + 4) =  "│"  + Name_Kor  + "│ "+ FIID_Kor  +
   "│ "  +   Padd(Date2SWIFT(DateIn),32,14) + "│ "+   Padd(Abs(RestIn),32,17) + "│ "+   Padd(Date2SWIFT(DateOut),32,16)
     + "│ " + Padd(RestMOut,32,16) + "│" ;
    StrNext(TotalVipCount + 4) = StrNext(TotalVipCount + 4) + RN + "├────────────────────────────┼─────────────┼───────────────┼──────────────────┼─────────────────┼─────────────────┤";

    TotalVipCount = TotalVipCount + 1;

  PageBeginDate = VipDate;
  if(not tinsert())
    TrnMsg = "Ошибка печати окончания выписки ";
    return false;
  end;
  if(not close(emsg))
    TrnMsg = "Ошибка закрытия файла " + FileName(emsg);
    return false;
  end;
  VipFiles(asize(VipFiles)) = NameFile;

  return true;
end;


macro ProcessVipDoc( idx )

  var DC_TAG, tc, ground = "", Sum;

  DocRef = Strings(idx);

  NumReferDoc = oldRefer = "";


  if( DocRef.AmmD)
    DC_TAG = DEBET_TAG;
    Sum = Money(DocRef.AmmD);
  elif(DocRef.AmmC)
    DC_TAG = CREDIT_TAG;
    Sum = Money(DocRef.AmmC);
  end;

  clearrecord(doclink);  /* Обязательно чистить, иначе - гайки */
  clearrecord(pay);
  doclink.ApplicationKey = DocRef.AppKey /*"40860586"*/;
  doclink.ApplicationKind = 1;
  doclink.Id = 0;
  if((GetGE(doclink)) AND
     (doclink.ApplicationKey == DocRef.AppKey) AND
     (doclink.ApplicationKind == 1))
    /* Найти уже тогда и платёж */
    pay.NodeId = doclink.NodeId;
    pay.NodePaymentId = doclink.NodePaymentId;
    if(GetEQ(pay))
      if(pay.DocKind == Type_Doc_MT200)
        NumReferDoc = mt200.Reference;
      elif(pay.DocKind == Type_Doc_MT202)
        NumReferDoc = mt202.Reference;
      elif(pay.DocKind == Type_Doc_MT103)
        NumReferDoc  = Substr(mt103str.str, 5, 16);
      end;

      NumReferDoc= trim(NumReferDoc);

      if ((pay.Docclass != Class_doc_SWIFT) and
          (pay.Docclass != Class_doc_TELEX) and
          (DC_TAG == CREDIT_TAG))
        OldRefer = NumReferDoc;
      else
        keynum(ass, 1);
        ass.NodeId = doclink.NodeId;
        ass.NodePaymentId = doclink.NodePaymentId;
        ass.Id = 0;
        AssNotEOF = GetGE(ass);
        while((AssNotEOF) AND
              (ass.NodeId == doclink.NodeId) AND
              (ass.NodePaymentId == doclink.NodePaymentId))
          if(ass.AssType == 1)
            OldRefer = ass.Associate;
            AssNotEOF = false;
          else
            AssNotEOF = next(ass);
          end;
        end;
      end;
    else /* не найден payment */
      NumReferDoc = oldRefer = DocRef.DocNumb;
    end;
  else /* не найден doclnk */
      NumReferDoc = oldRefer = DocRef.DocNumb;
  end;

  if(OldRefer == "")
    OldRefer = DocRef.DocNumb;
  end;

  /* Напечатать конец предыдущего файла */
  if(PageDocCount == DocsPerPage)
    if(not FooterSWIFT(false))
      return false;
    end;
    PageDocCount = 0;
  end;


  /* Заголовок нового файла */
  if(PageDocCount == 0)
    PageNumber = PageNumber + 1;
    if(not HeaderSWIFT())
      return false;
    end;
  end;

  /* Документ */

  PageDocCount = PageDocCount+ 1;

  if((pay.DocClass == Class_Doc_SWIFT) OR
     (pay.DocClass == Class_Doc_TELEX))
    if(pay.DocKind == Type_Doc_MT100)
      tc = "S100";
    elif(pay.DocKind == Type_Doc_MT103)
      tc = "S103";
    elif(pay.DocKind == Type_Doc_MT200)
      tc = "S200";
    elif(pay.DocKind == Type_Doc_MT202)
      tc = "S202";
    elif(pay.DocKind == Type_Doc_MT910)
      tc = "S910";
    end;
/*  elif((int(DocRef.kind_oper)==1) OR
       (int(DocRef.kind_oper)==3))
    tc = "NTRF";*/
  else
    tc = "NMSC";
  end; /*if*/

  if(trim(DocRef.Ground) != "")
    ground = trim(Substr(transfield("", DocRef.Ground, 0, TrlTbl),1,34));
  end;
 /* VclMsgBox (" vv " + ValType(VipDate) + " " + ValType(Sum) + " " + ValType(OldRefer)  + " " + ValType (NumReferDoc));*/
  /*emsg.str = ":61:" + Date2SWIFT(VipDate) + substr(Date2SWIFT(VipDate), 3, 4) +
         DC_TAG + substr(Sum2SWIFT(Sum, Code_Currency),3) + tc + OldRefer;*/

   emsg.str = ":61:" + Date2SWIFT(DocRef.PrvDate) + substr(Date2SWIFT(DocRef.PrvDate), 3, 4) +
         DC_TAG + substr(Sum2SWIFT(Sum, Code_Currency),3) + tc + OldRefer;

  /* назначение убрали по просьбе ТКБ
   + "//" + NumReferDoc;
  if(strlen(ground))
    emsg.str = emsg.str + RN + ground;
  end;
  */
  if(not tinsert())
    TrnMsg = "Ошибка записи строки выписки в файл";
    return false;
  end;
  IntermBalance = IntermBalance - Money(DocRef.AmmD) + Money(DocRef.AmmC);
  VipDocCount  = VipDocCount + 1;
  return true;
end;



macro FillStatMsg()
  var RnotEOF, Cnt;
  Reference = ClearRef = VipNumber = BicKorr = "";
  /*FIID = ksa.FIID;*/
  Code_Currency = int(ksa.FIID);
  ISOFIID = FIID2Str(ksa.FIID);
  Account = ksa.AccNum;
  PageDocCount = PageNumber = 0;
  PageBeginDate = VipDate; /* текущая дата выписки */
  IntermBalance = Money(0);
  WorkDir = WorkPath = NameFile = "";

  asize(VipFiles, 0);

  if(index(ks.FrnSys, SwiftIdGate))
    WorkDir = SwiftArcDir;
    OutDir  = SwiftOutDir;
  elif(index(ks.FrnSys, TelexIdGate))
    WorkDir = TelexArcDir;
    OutDir  = TelexOutDir;
  else
    TrnMsg = string("Неизвестный идентификатор шлюза (", ks.FrnSys, ") для схемы расчетов ", ks.Korr, "/", ks.Num, "/", ks.FIID);
    return false;
  end;

  if((korr.TypBic == BICSWIFT) OR (korr.TypBic == BICTELEX))
    BICKorr = korr.BIC;
  else
    rewind (route);
    route.Korr = korr.Num;
    route.Id = 0;
    RnotEOF = GetGE(route);
    while((RnotEOF) AND
          (route.Korr == korr.Num) AND
          (route.TBId != BICSWIFT) AND (route.TBId != BICTELEX))
      RnotEOF = next(route);
    end;
    if((route.Korr == korr.Num) AND ((route.TBId == BICSWIFT) OR (route.TBId == BICTELEX)))
      BicKorr = route.BIC;
    end;
  end;
  if(BicKorr == "")
    TrnMsg = string(OpName, ": Для респоднента ", korr.Num, " не задан код SWIFT(TELEX)");
    return false;
  end;
  if(index(ks.FrnSys, SWIFTIdGate) AND (strlen(BICKorr) < 11))
    BICKorr = BICKorr + MkStr("X", 11 - strlen(BICKorr));
  end;
  Vyp( Account, VipDate, isPlanRest);
  Cnt = asize(strings);
  if (index (ks.FrnSys, TelexIdGate))
   Reference = ClearRef = genref(Korr.Num, "000", false);
  elif(index (ks.FrnSys, SWIFTIdGate))
   Reference = ClearRef = genref(Korr.Num, "000", true);
  end;
  VipNumber = string(int(substr(ClearRef, 14,3)));
  if((ks.FIID == "0") AND
     (ks.FrnSys == SwiftRUR4IdGate))
    Reference = "'" + Reference;
    TrlTbl = ftrr;
  elif((ks.FIID == "0") AND
       (ks.FrnSys == SwiftRUR5IdGate))
    if (strlen(Reference) > 15)
      Reference = substr(Reference,1,15);
    end;
    Reference = "+" + Reference;
    TrlTbl = ftrr5;
  else
    TrlTbl = ftr;
  end;
  IntermBalance = Money(RestInp);
  PageBeginDate = date (LastDate);
  var i = 0;
  VipDocCount = 0;
/*  println ("Last date " + String(LastDate));
  while (i < Cnt )
    println ("D " + Strings(i).AmmD + " C " + Strings(i).AmmC);
    i = i + 1;
  end;
*/
  while(i < Cnt)
    /* Вывод док-та выписки */
   if ((Money(Strings(i).AmmD)) OR
    (Money(Strings(i).AmmC)))
    if(not ProcessVipDoc(i))
      return false;
    end;
   end;
   i = i + 1;
  end;

  /*
     Печать окончания !!!
  */
  if(VipDocCount)
    if(not FooterSWIFT(true))
      return false;
    end;
  elif(PageDocCount != DocsPerPage)
    if(MakeEmpty)
      PageNumber = 1;
      if(not HeaderSWIFT())
        return false;
      end;
      if(not FooterSWIFT(true))
        return false;
      end;
    end;
  end;

  return true;
end;


macro CopyStatMsg()
/*
  if(NOT insert(gf))
    TrnMsg = string(OpName, ":Ошибка добавления записи в ", Filename(gf));
    AbortTrn();
  end;
*/
  var i=0;
  while(i < asize(VipFiles))   /* /Q /C */
    if(not CopyFile(WorkDir + VipFiles(i), OutDir + VipFiles(i)))
      TrnMsg = string("Ошибка при копировании ",WorkDir,VipFiles(i)," в ",OutDir,VipFiles(i));
      return false;
    end;
    i=i+1;
  end;
  return true;
end;


macro MakeStatementMessage(/* ksa, ks, korr */)

  if(FillStatMsg())
    if(asize(VipFiles) AND
       CopyStatMsg())
     PrintVipLog();
    elif (asize(VipFiles))
     println(TrnMsg);
    end;
  else
   if(strlen(TrnMsg))
     /*AddPtk(TrnMsg);*/
     println(TrnMsg);
     TrnMsg = "";
   else
      println("Произошла неизвестная ошибка");
   end;
  end;

end; /* MakeStatMsgSWIFT */


macro DlgProc(type, sender)
  if(type == 1)  /* Нажатие на кнопку */
    if(sender == bt1)
      FIID = trim(ScrGetText(ed1));
      if (FIID=="")
        VipDate = ScrGetText(ed3);
        VipDate = date(int(substr(VipDate,1,2)),
                       int(substr(VipDate,4,2)),
                       int(substr(VipDate,7,4)));
        return 1;
      end;
      cur.FIID = FIID;
      if(not GetEQ(cur))
        MsgBox("В справочнике валют отсутсвует валюта с кодом ", FIID);
        return;
      else
        Account = ScrGetText(ed2);

        if(strlen(Account = trim(Account)))
          keynum(ksa, 1);
          ksa.Chapter = 0;
          ksa.FIID = substr(Account, 6,3);
          if(ksa.FIID == "810")
            ksa.FIID = "0";
          end;
          ksa.AccNum = Account;
          if(not GetEQ(ksa))
            MsgBox("В справочнике счетов корсхем ВПС отсутствует счет ", Account, "/", ksa.FIID);
            return;
          else
            if((not FindKS(ksa.KorrNum, ksa.SchNum, ksa.FIID)) OR
               (not ks.IsLoro) OR
               (not FindKorr(ksa.KorrNum)) OR
               IsFilial (korr.Node, Korr))
              MsgBox("Схема расчетов ВПС ", SchNumber(ks), "\nотсутствует, принадлежит филиалу или является ностро-схемой");
              return;
            else
            end;
          end;
        else
          VipDate = ScrGetText(scr, ed3);
        end;
      end;
      VipDate = ScrGetText(ed3);
      VipDate = date(int(substr(VipDate,1,2)),
                     int(substr(VipDate,4,2)),
                     int(substr(VipDate,7,4)));
      return 1;
    elif (sender == bt2)
      return 2;
    elif (sender == bt3)
     isPlanRest = not isPlanRest;
     if (isPlanRest)
      ScrSetText(bt3, "План.+Факт.");
     else
      ScrSetText(bt3, "Факт.");
     end;
    elif(sender == ed4)
      MakeEmpty = not MakeEmpty;
      if(MakeEmpty)
        ScrSetText(ed4, "Да");
      else
        ScrSetText(ed4, "Нет");
      end;
    end;
  end;
end;


/************************************************
  Тело макроса
*************************************************/
macro MakeStatMsg()

  VipDocCount=PageDocCount=PageNumber=TotalVipCount = Code_Currency =
     ksCount = IntermBalance = 0;
  Reference=ClearRef= VipNumber=BicKorr=ISOFIID=TrnMsg = WorkDir=
     WorkPath=NameFile=OutDir = NumReferDoc=OldRefer = "";
  drNotEOF=KsNotEOF=AssNotEOF= false;
  TrlTbl = DocRef= null;
  VipDate = {curdate};
  FIID = "";
  MakeEmpty = false;
  isPlanRest = true;

  keynum(pay, 1);
  keynum(doclink, 1);
  keynum(ksa, 1); /* будет меняться */
  keynum(route, 2);

  SetRecordAddr(mt100, pay, 0, GetRecordSize(rmain));
  SetRecordAddr(mt200, pay, 0, GetRecordSize(rmain));
  SetRecordAddr(mt202, pay, 0, GetRecordSize(rmain));
  SetRecordAddr(mt103str, pay , 0, GetRecordSize(rmain));

FIID = "";
/*Account = "30109810900000009838";*/
Account = "";

  scr = ScrCreate(0,0, 165, 420, OpName, "DlgProc");
  lb1 = ScrAddLabel(scr,13, 20, "Код валюты (пусто - все валюты)");
  ed1 = scrAddEdit(scr, 10, 190, 0,30, FIID, 1);
  lb2 = ScrAddLabel(scr,38, 20, "Номер счета (пусто - все счета)");
  ed2 = scrAddEdit(scr, 35, 190, 0,135, Account, 1);
  lb3 = ScrAddLabel(scr,63, 20, "Дата");
  ed3 = scrAddEdit(scr, 60, 190, 0,65, Date2Vadim(VipDate), 5);
  lb4 = ScrAddLabel(scr,88, 20, "Формировать пустые выписки");
  ed4 = scrAddButton(scr, 85, 190, 23, 50, "Нет", 0);
  lb5 = ScrAddLabel(scr,120, 20, "Остаток");
  bt3 = ScrAddButton(scr, 112, 190, 23, 80, "План.+Факт.", 0);
  bt1 = ScrAddButton(scr, 10, 335, 23, 60, "О&К", 0);
  bt2 = ScrAddButton(scr, 50, 335, 23, 60, "&Отмена", 0);

  if(ScrShowModal(scr) != 1)
    ScrDelete(scr);
    return;
  end;
  ScrDelete(scr);




  [  Отчет о сформированных выписках за #
   ┌───┬────────────────────────┬───────────────────────────────────────────────┐
   │Вал│         Счет           │                Файл(-ы) выписок               │
   └───┴────────────────────────┴───────────────────────────────────────────────┘

  ](VipDate);
  StrNext(0) = "                                                         Выписки  МТ950";
  StrNext(1) =  "┌────────────────────────────┬─────────────┬───────────────┬──────────────────┬─────────────────┬─────────────────┐";
  StrNext(2) =  "│Наименование банка          │ Код валюты  │  Дата вх.ост. │  Вх.остаток      │   Дата исх.ост. │  Исх.остаток    │";
  StrNext(3) =  "├────────────────────────────┼─────────────┼───────────────┼──────────────────┼─────────────────┼─────────────────┤";

  if((FIID!="")AND(Account != "")) /* Один счет */
    MakeStatementMessage();
  else    /* Все счета по всем валютам или по одной валюте */
    ksCount = 0;
    if(FIID == "")
      VclInitProgress(NRecords(ks), " ~Ждите...~", OpName + "по всем валютам");
      keynum(ks, 0);
      rewind(ks);
      ksNotEOF = next(ks);
    else
      VclInitProgress(NRecords(ks), " ~Ждите...~", OpName + "по валюте "+ FIID);
      keynum(ks, 2);
      ks.FIID = FIID;
      ks.IsPoz = ks.Prio = 0;
      ksNotEOF = ((GetGE(ks)) AND (ks.FIID == FIID))
    end;
    while(ksNotEOF)
      if((ks.isLoro) AND
         ((FIID == "") OR
          ((FIID != "") AND (FIID == ks.FIID))))
        if((FindKorr(ks.Korr)) AND
           (korr.Node  == 0) AND
           ((index(ks.FrnSys, SwiftIdGate))OR(index(ks.FrnSys, TelexIdGate))))
          if(FindKSA(ks.Korr, ks.Num, ks.FIID, 20))
            prnMsg (string(" Счет ~", ksa.AccNum:f, "~", " / ~", ksa.FIID, "~"));
            MakeStatementMessage();
          else  /* Счет не найден */
            println("Не найден счет ответных для схемы ", SchNumber(ks));
          end;
        end;
      end;
      ksNotEOF = next(ks);
      if(FIID != "")
        ksNotEOF = ksNotEOF AND (ks.FIID == FIID);
      end;
      VclUseProgress(ksCount = ksCount + 1);
    end;
    VclRemProgress();
  end;

  [------------------------------------------------------------------------------
     Сформировано выписок : ####
  ](TotalVipCount);
  println ("");
  while(i< TotalVipCount + 4)
     println (StrNext(i));
     i=i+1;
  end;
end;
