import ws_file;
import ws_utils;

// Возвращает текст запроса для скроллинга
private macro GetSQLTextFiles(tableNameFile, fieldNameParent, filter, order)
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT RSP_WEB.GET_LIST_FILES(:P_TABLENAME, :P_FIELDNAME, :P_ORDER, :P_FILTER) QueryText from DUAL";
    cmd.DeclareAndSet("P_TABLENAME", tableNameFile, "P_FIELDNAME", fieldNameParent, "P_ORDER", order, "P_FILTER", filter);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.Rec.QueryText;
end;

// Возвращает записи для списка
macro GetRowsFiles(params)
    var wParams = Xml2Rsl(params.params);
    var cmd = TRslOraQuery();
    cmd.SqlText = SQL_WrapSelect(GetSQLTextFiles(wParams.TABFILE, wParams.FIELDNAME, GetSqlFilterCondition(params.FilterItems), GetOrderStr(params.OrderItems)), params.PageNumber, params.PageSize);
    if (Index(cmd.SqlText, ":ID_RECORD"))
        cmd.DeclareAndSet("ID_RECORD", wParams.RECORDID);
    end;
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd;
end;

// Возвращает количество записей для списка
macro GetRowCountFiles(params)
    var wParams = Xml2Rsl(params.params);
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT COUNT(*) CNT FROM (" + GetSQLTextFiles(wParams.TABFILE, wParams.FIELDNAME, GetSqlFilterCondition(params.FilterItems), "") + ")";
    if (Index(cmd.SqlText, ":ID_RECORD"))
        cmd.DeclareAndSet("ID_RECORD", wParams.RECORDID);
    end;
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.GetByName("CNT").AsInteger;
end;

class (IScrolling) ScrollingFiles(params) //TABFILE, FORM, IDMENU, RECORDID, FIELDNAME
    var tableNameFile = params.TABFILE;
    var id_menuowner = params.IDMENU;
    var formId = params.FORM;

    InitIScrolling(tableNameFile);

    var cmd = TRslOraQuery();
    var fieldName: string = "";
    cmd.SqlText = "begin :FieldName := RSP_FORM.GET_PK_TABLE(:TableName); end;";
    cmd.DeclareAndSet("TableName", tableNameFile, "FieldName", fieldName);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    fieldName = cmd.GetVariable("FieldName");

    private macro SetColumns()
        Columns = TArray();
        FilterItems = TArray();
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", tableNameFile+"8"+fieldName,
            "Type", ScrollingColumnType.Numeric,
            "Title", "ИД",
            "Source", tableNameFile+"8"+fieldName,
            "Tooltip", "ИД"
        );
        FilterItems[FilterItems.Size] = makeObj(
            "IFilterItem",
            "Id", FilterItems.Size + 1,
            "Type", FilterValueType.Integer,
            "Name", "ИД",
            "ColumnId", tableNameFile+"8"+fieldName,
            "Params", tableNameFile+"8"+fieldName
        );
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "NAME",
            "Type", ScrollingColumnType.Text,
            "Title", "Имя файла",
            "Source", "NAME",
            "Tooltip", "Имя файла"
        );
        FilterItems[FilterItems.Size] = makeObj(
            "IFilterItem",
            "Id", FilterItems.Size + 1,
            "Type", FilterValueType.String,
            "Name", "Имя файла",
            "ColumnId", "NAME",
            "Params", "NAME"
        );
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "USERNICK",
            "Type", ScrollingColumnType.Text,
            "Title", "Пользователь",
            "Source", "USERNICK",
            "Tooltip", "Пользователь"
        );
        FilterItems[FilterItems.Size] = makeObj(
            "IFilterItem",
            "Id", FilterItems.Size + 1,
            "Type", FilterValueType.String,
            "Name", "Пользователь",
            "ColumnId", "USERNICK",
            "Params", "USERNICK"
        );
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "FSIZE",
            "Type", ScrollingColumnType.Numeric,
            "Title", "Размер",
            "Source", "FSIZE",
            "Tooltip", "Размер"
        );
        FilterItems[FilterItems.Size] = makeObj(
            "IFilterItem",
            "Id", FilterItems.Size + 1,
            "Type", FilterValueType.Integer,
            "Name", "Размер",
            "ColumnId", "FSIZE",
            "Params", "FSIZE"
        );
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "ISPACK",
            "Type", ScrollingColumnType.Numeric,
            "Title", "Упакован",
            "Source", "ISPACK",
            "Tooltip", "Упакован"
        );
        FilterItems[FilterItems.Size] = makeObj(
            "IFilterItem",
            "Id", FilterItems.Size + 1,
            "Type", FilterValueType.Integer,
            "Name", "Упакован",
            "ColumnId", "ISPACK",
            "Params", "ISPACK"
        );
    end;

    private macro SetSysToolBarItems(params);
      SysToolbarItems = TArray();
      SysToolbarItems[SysToolbarItems.Size] = makeObj(
          "ISysButton",
          "Name", PredefinedSysButton.SelectFile,
          "Tooltip", "Прикрепить файл",
          "Service", WebServiceCallback("ws_scrollfiles", "SelectFile", Rsl2Xml(params))
      );
      SysToolbarItems[SysToolbarItems.Size] = makeObj(
          "ISysButton",
          "Name", PredefinedSysButton.Delete,
          "Service", WebServiceCallback("ws_scrollfiles", "DeleteFileRow", Rsl2Xml(params))
      );
    end;

    private macro SetToolBarItems();
      ToolbarItems = TArray();
      ToolbarItems[ToolbarItems.Size] = makeObj(
           "IToolbarItem",
           "Type", ToolbarItemType.Button,
           "Image", GetIcon("Save"),
           "Tooltip", "Сохранить файл",
           "Service", WebServiceCallback("ws_scrollfiles", "SaveFileRow")
      );
    end;

    MultiPage = true;
    MultiSelect = true;
    SetColumns();
    SetToolBarItems();
    SetSysToolBarItems(params);
    GetRowsService = WebServiceCallback("ws_scrollfiles", "GetRowsFiles", Rsl2Xml(params));
    GetRowCountService = WebServiceCallback("ws_scrollfiles", "GetRowCountFiles", Rsl2Xml(params));
end;

class (IWindow) ScrollingFilesWindow(params) //TABFILE, FORM, IDMENU, RECORDID, FIELDNAME
    var formId = params.FORM;
    var recordId = params.RECORDID;

    private macro GetTitle(formId): string
        var cmd = TRslOraQuery();
        CaptureOutput();
[
SELECT f.text
  FROM NFORM f
 WHERE f.id_nform = :ID_FORM
];
        cmd.SqlText = StopCaptureOutput();
        cmd.DeclareAndSet("ID_FORM", formId);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        return cmd.GetByName("text").AsString;
    end;

//    Title = GetTitle(formId) + ": Список файлов";
    Title = "Список файлов";

    WindowParams = makeObj(
        "IWindowParams",
        "Width", 800,
        "Height", 480,
        "HelpPage", "",
        "IsModal", true,
        "IsResizing", true
    );
    Appearance = WindowAppearanceType.Scrolling;
    Fields = makeArray(
        makeObj(
            "IPanelItem",
            "Id", recordId,
            "Row", 0,
            "Type", PanelItemType.Scrolling,
            "Field", ScrollingFiles(params)
        )
    );
end;

/*Загрузка файла*/
macro SelectFile(params)
    var wParams = Xml2Rsl(params.params);
    var tableNameFile = wParams.TABFILE;
    var fieldNameParent = wParams.FIELDNAME;
    var valueFieldParent = wParams.RECORDID;
    var InsertFile = TRslOraQuery();
    var Id = 0;

    InsertFile.SqlText = "INSERT INTO " +tableNameFile+ "  "+
                         "       (" +fieldNameParent+ ",   "+
                         "        NAME,                    "+
                         "        USERNICK,                "+
                         "        FSIZE,                   "+
                         "        ISPACK,                  "+
                         "        BINARYDATA)              "+
                         "  SELECT " +valueFieldParent+ ", "+
                         "         t.name,                 "+
                         "         " +{Oper}+ ",           "+
                         "         length(t.content),      "+
                         "         0,                      "+
                         "         t.content               "+
                         "    FROM STRGFILE t              "+
                         "   WHERE t.id_strgfile = :fileId";
    InsertFile.DeclareAndSet("fileId", params.fileId);

    if(not InsertFile.Execute)
      addptk("SaveFile: "+InsertFile.Error);
      RunError(toansi("SaveFile: "+InsertFile.Error));
    end;

    OraTrnCommit();
    return WebServiceResult(WebServiceResultType.Action, IAction(WebServiceActionType.Refresh));;
end;

/*Удаление файла(ов)*/
macro DeleteFileRow(params)
    var tableNameFile = params.ScrollingId;
    // Получение первичного ключа таблицы
    var cmd = TRslOraQuery();
    var fieldName: string = "";
    cmd.SqlText = "begin :FieldName := RSP_FORM.GET_PK_TABLE(:TableName); end;";
    cmd.DeclareAndSet("TableName", tableNameFile, "FieldName", fieldName);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    fieldName = cmd.GetVariable("FieldName");
    var IDRec = 0, VR = 0, Rec = 0;
    var result;//: WebServiceResult;
    // ХП удаления записей
    cmd.Clear();
    cmd.SqlText = "begin :Rec := RSP_WEB.DEL_RECFROMLIST(:Table, :IDRec, :VRec); end;";
    if (params.SelectedRows.Size == 0) // Нет выделенных, есть только текущая

       if (params.CurrentRow != null)
        // Получение ИД записи
        IDRec = GenGetProp(params.CurrentRow, tableNameFile + "8" + fieldName);
        // Получение версии записи
        VR = params.CurrentRow.VERSIONREC;
        cmd.DeclareAndSet("Rec", Rec, "Table", tableNameFile, "IDRec", IDRec, "VRec", VR);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        if (cmd.GetVariable("Rec") == 0)
            OraTrnCommit();
            result = WebServiceResult(WebServiceResultType.Action, IAction(WebServiceActionType.Refresh));
        else
            OraTrnRollBack();
            result = WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Запись конкурентно изменена", MessageBoxType.Error));
        end;
       end;
    else // Бежим по выбранным записям
        for (var selectedRow, params.SelectedRows)
            // Получение ИД записи
            IDRec = GenGetProp(selectedRow, tableNameFile + "8" + fieldName);
            // Получение версии записи
            VR = selectedRow.VERSIONREC;
            // Удаление записи
            cmd.DeclareAndSet("Rec", Rec, "Table", tableNameFile, "IDRec", IDRec, "VRec", VR);
            if (not cmd.Execute())
                RunError(ToANSI(cmd.Error));
            end;
            if (cmd.GetVariable("Rec") == 0)
                OraTrnCommit();
                result = WebServiceResult(WebServiceResultType.Action, IAction(WebServiceActionType.Refresh));
            else
                OraTrnRollBack();
                result = WebServiceResult(WebServiceResultType.Message, IMessage("Ошибка", "Запись конкурентно изменена", MessageBoxType.Error));
                break;
            end;
        end;
    end;
    return result;
end;

/*Сохранение файла(ов)*/
macro SaveFileRow(params)
    var tableNameFile = params.ScrollingId;
    // Получение первичного ключа таблицы
    var cmd = TRslOraQuery();
    var fieldName: string = "";
    cmd.SqlText = "begin :FieldName := RSP_FORM.GET_PK_TABLE(:TableName); end;";
    cmd.DeclareAndSet("TableName", tableNameFile, "FieldName", fieldName);

    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;

    fieldName = cmd.GetVariable("FieldName");
    var fileId = 0, VR = 0, Rec = 0, Id = 0;
    var result;
    var InsertFile = TRslOraQuery();
    var nameFile = "";
    InsertFile.SqlText = "declare                               "+
                         "  l_Name varchar2(80);                "+
                         "  l_Blob blob;                        "+
                         "begin                                 "+
                         "  SELECT name, binarydata             "+
                         "    INTO :l_Name, l_Blob              "+
                         "    FROM " +tableNameFile+ "          "+
                         "   WHERE " +fieldName+ " = :fileId;   "+
                         "  INSERT INTO strgfile(content, name) "+
                         "    VALUES(l_Blob, :l_Name)           "+
                         "  RETURNING id_strgfile INTO :ID;     "+
                         "end;";

    if (params.SelectedRows.Size == 0) // Нет выделенных, есть только текущая
       if (params.CurrentRow != null)
        fileId = GenGetProp(params.CurrentRow, tableNameFile + "8" + fieldName);
        InsertFile.DeclareAndSet("l_Name", nameFile, "fileId", fileId, "ID", Id);

        if(not InsertFile.Execute)
          addptk("SaveFileRow: "+InsertFile.Error);
          RunError(toansi("SaveFileRow: "+InsertFile.Error));
        end;

        Id = InsertFile.GetVariable("ID");
        nameFile = InsertFile.GetVariable("l_Name");
        OraTrnCommit();
        result = GetReportFile(id, nameFile);//WebServiceResult(WebServiceResultType.ReportFile, IReportFile(makeArray(IReportFileItem(id, nameFile))));
       end;
    else // Бежим по выбранным записям
        result = TArray();
        for (var selectedRow, params.SelectedRows)
            // Получение ИД записи
            fileId = GenGetProp(selectedRow, tableNameFile + "8" + fieldName);
            InsertFile.DeclareAndSet("l_Name", nameFile, "fileId", fileId, "ID", Id);

            if(not InsertFile.Execute)
              addptk("SaveFileRow: "+InsertFile.Error);
              RunError(toansi("SaveFileRow: "+InsertFile.Error));
            end;

            Id = InsertFile.GetVariable("ID");
            nameFile = InsertFile.GetVariable("l_Name");
            OraTrnCommit();
            result[result.Size] = WebServiceResult(WebServiceResultType.ReportFile, IReportFile(makeArray(IReportFileItem(id, nameFile))));
        end;
    end;
    return result;
end;

// Сервис скроллинга файлов
macro OpenSubordinateScrolFiles(params)//, TABFILE, FORM, IDMENU
    var wParams = Xml2Rsl(params.params);
    var tableNameFile: string = wParams.TABFILE;
    var formId = wParams.FORM;
    var panelObject = params.Model;

    var cmd = TRslOraQuery();
    var tableName: string = "";
    var fieldName: string = "";
    cmd.SqlText = "begin :TableName := RSP_FORM.GET_MAINTABLEFORM(:FormId, 0); :FieldName := RSP_FORM.GET_PK_TABLE(:TableName); end;";
    cmd.DeclareAndSet("TableName", tableName, "FormId", formId, "FieldName", fieldName);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    tableName = cmd.GetVariable("TableName");
    fieldName = cmd.GetVariable("FieldName");
    // Получение ИД записи
    var recordId: integer = 0;
    if (panelObject != null)
        var mainFieldName = tableName + "8" + fieldName;
        recordId = GenGetProp(panelObject, mainFieldName);
    end;

    wParams.recordId = recordId;
    wParams.fieldName = fieldName;
    return WebServiceResult(WebServiceResultType.Window, ScrollingFilesWindow(wParams));
end;
