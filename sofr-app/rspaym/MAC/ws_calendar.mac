import ws_types;


macro GetCalendarTitle(typeId: integer): string
    var cmd = TRslOraQuery();
    cmd.SqlText = "select c.id_sclndr Type, c.text Name from SCLNDR c where c.id_sclndr = :Id";
    cmd.DeclareAndSet("Id", typeId);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.Rec.Name;
end;

macro GetDaysOfMonth(calendarTypeId: integer, month: integer, year: integer): TArray
    var cmd = TRslOraQuery();
    cmd.SqlText = "select * from table(RSP_CALENDAR.GETDAYSMONTH(:P_CALTYP, :P_MONTH, :P_YEAR))";
    cmd.DeclareAndSet("P_CALTYP", calendarTypeId, "P_MONTH", month, "P_YEAR", year);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    var daysOfMonth = TArray();
    while (not cmd.Eof)
        daysOfMonth[daysOfMonth.Size] = makeObj("TDynamicBean", "Day", cmd.Rec.DayDate, "IsWorkDay", cmd.Rec.Working == 1);
        cmd.Next();
    end;
    return daysOfMonth;
end;

macro IsWorkDayChange(calendarTypeId: integer, dt: DateTime, isWorkDay: bool): bool
    var result: bool = false;
    var d: Date = Date(dt);
    var pWorking: integer = IIF(isWorkDay, 1, 0);
    var cmd = TRslOraQuery();
    cmd.SqlText = "begin RSP_CALENDAR.MODIFYCALENDAR(:P_CALTYP, :P_CURDATE, :P_WORKING); end;";
    cmd.DeclareAndSet("P_CALTYP", calendarTypeId, "P_CURDATE", d, "P_WORKING", pWorking);
    if (cmd.Execute())
        OraTrnCommit();
        result = true;
    else
        OraTrnRollback();
        RunError(ToANSI(cmd.Error));
    end;
    return result;
end;

macro GetInitialModel(calendarTypeId: integer, dt: DateTime)
    var date, mon, year;
    DtTmSplit(dt, date);
    DateSplit(date, null, mon, year);
    return makeObj("TDynamicBean", "DaysOfMonth", GetDaysOfMonth(calendarTypeId, mon, year), "Title", GetCalendarTitle(calendarTypeId));
end;
