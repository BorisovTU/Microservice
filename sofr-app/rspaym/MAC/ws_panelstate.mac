private const COMMON_STATE = 0;


/*
class PanelStateHolder
    var PrivateState :string;
    var CommonState :string;
end;

class TPatternSettings
  var Name :string;
  var Pattern :string;
  var Mask :string;
  var PatternHelpText :string;
  var PatternErrorText :string;
    macro Equals(other)
        if (other == null)
            return false;
        end;
        return (Name == other.Name) and (Pattern == other.Pattern) and (Mask == other.Mask) and (PatternHelpText == other.PatternHelpText) and (PatternErrorText == other.PatternErrorText)
    end;
end;

macro PatternSettingsToString(i)
    return "Name = " + i.Name  + "; Pattern = " + i.Pattern + "; Mask = " + i.Mask  + "; PatternHelpText = " + i.PatternHelpText  + "; PatternErrorText = " + i.PatternErrorText + ";";
end;

macro IsPatternSettingsEmpty(i)
    if (i == null)
        return true;
    end;
    return ("" == i.Pattern) and ("" == i.Mask) and ("" == i.PatternHelpText) and ("" == i.PatternErrorText);
end;

macro PatternSettingsKeyEqComparer(a, b)
    return a.Name == b.Name;    
end;
*/

class PanelCommonState
    var PatternSettings :TArray = TArray(); //TArray of TPatternSettings
end;


private macro IsPanelStateExists(panel :string, oper :string)
  var cmd = TRslOraQuery();

  cmd.sqlText = "SELECT 1 FROM WINFORM WHERE USERNICK = :p1 AND PANEL = :p2";
  cmd.DeclareAndSet("p1", oper,
                    "p2", panel);
  if(not cmd.execute) 
    runError(toansi(cmd.error));    
  end;
  if (not cmd.Eof)
    return true;
  else
    return false;
  end;
end;

private macro UpdatePanelState(panel :string, oper :string, xmlstate :string)
  var cmd = TRslOraQuery();

  cmd.sqlText = "update WINFORM set STATE = :p1 WHERE USERNICK = :p2 AND PANEL = :p3";
  cmd.DeclareAndSetLOB("p1", xmlstate);
  cmd.DeclareAndSet("p2", oper,
                    "p3", panel);
  if(not cmd.execute) 
    runError(toansi(cmd.error));    
  else
    OraTrnCommit();
  end;
end;

private macro InsertPanelState(panel :string, oper :string, xmlstate :string)
  var cmd = TRslOraQuery();

  cmd.sqlText = "insert into WINFORM(USERNICK, PANEL, STATE) VALUES(:p1, :p2, :p3)";
  cmd.DeclareAndSet("p1", oper,
                    "p2", panel);
  cmd.DeclareAndSetLOB("p3", xmlstate);
  if(not cmd.execute) 
    runError(toansi(cmd.error));    
  else
    OraTrnCommit();
  end;
end;

private macro UpdateState(panel :string, oper :string, xmlstate :string)
  if ((xmlstate == null) or (xmlstate == ""))
    return;
  end;
  if (IsPanelStateExists(panel, oper))
    UpdatePanelState(panel, oper, xmlstate);
  else
    InsertPanelState(panel, oper, xmlstate);
  end;
  return 0;
end;


private macro GetPanelStateByOper(panel :string, oper :string)
  var cmd = TRslOraQuery();
  cmd.sqlText = " SELECT STATE as PERSSTATE "
                "   FROM WINFORM            "
                "   WHERE USERNICK = :p1    "
                "     AND PANEL = :p2       ";
 
  cmd.DeclareAndSet("p1", oper);
  cmd.DeclareAndSet("p2", panel);

  if(not cmd.execute) 
    runError(toansi(cmd.error));    
  end;

  if (not cmd.Eof)
    return cmd.rec.PERSSTATE;
  end;

  return "";
end;


private macro StoreCommonState(panel :string, commonState)
    var xml = Rsl2Xml(commonState);
    UpdateState(panel, COMMON_STATE, xml);
end;

private macro GetCommonState(panel :string)
    var xml = DecodeFromBase64(GetPanelStateByOper(panel, COMMON_STATE));
    var t = Xml2Rsl(xml);
    var commonState = PanelCommonState();
	if ((t != null) and (ValType(t.PatternSettings) != V_UNDEF))
        commonState.PatternSettings = t.PatternSettings;
    end;
    return commonState;
end;


// сохранение панели по умолчанию
macro StoreCommonPanelState(panel :string, commonState)
  addptk("StoreCommonPanelState panel:"+panel);
  addptk("StoreCommonPanelState xmlstate:"+xmlstate);
  if (panel == "")
    RunError("Имя панели не может быть пустым");
  end;
  if (commonState == null)
    RunError("Не задано состояние панели");
  end;
  StoreCommonState(panel, commonState);
  return 0;
end;



//восстановление панели по умолчанию
macro GetCommonPanelState(panel :string)
  addptk("GetCommonPanelState panel:"+panel);
  if (panel == "")
    RunError("Имя панели не может быть пустым");
  end;

  return GetCommonState(panel);
end;


//восстановление панели для опера
macro GetPanelState(panel :string)
  if (panel == "")
    RunError("Имя панели не может быть пустым");
  end;

  return GetPanelStateByOper(panel, {oper});
end;

//сохранение панели для опера
macro StorePanelState(panel :string, xmlstate :string)
  if (panel == "")
    RunError("Имя панели не может быть пустым");
  end;

  if (xmlstate == null)
    RunError("Не задано состояние панели");
  end;

  UpdateState(panel, {oper}, xmlstate);

  return 0;
end;
