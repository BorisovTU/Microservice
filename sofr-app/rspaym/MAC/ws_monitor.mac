/* Сервис скроллинга монитора */

import ws_positioning;


// Возвращает текст запроса для монитора состояний
private macro GetSQLTextMonitor(filter, order)
    var cmd = TRslOraQuery();
    cmd.SqlText = "select RSP_WEB.GET_MONITORSTATE(:P_ORDER, :P_FILTER) QueryText from DUAL";
    cmd.DeclareAndSet("P_ORDER", order, "P_FILTER", filter);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.Rec.QueryText;
end;

// Возвращает записи для монитора состояний
macro GetRowsMonitor(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var monitorSQLText = GetSQLTextMonitor(GetSqlFilterCondition(params.FilterItems), GetOrderStr(params.OrderItems));
    if (monitorSQLText)
        var cmd = TRslOraQuery();
        cmd.SqlText = SQL_WrapSelect(monitorSQLText, params.PageNumber, params.PageSize);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        return cmd;
    else
        return null;
    end;
end;

// Возвращает количество записей для монитора состояний
macro GetRowCountMonitor(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var monitorSQLText = GetSQLTextMonitor(GetSqlFilterCondition(params.FilterItems), "");
    if (monitorSQLText)
        var cmd = TRslOraQuery();
        cmd.SqlText = "SELECT COUNT(*) CNT FROM (" + monitorSQLText + ")";
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        return cmd.GetByName("CNT").AsInteger;
    else
        return 0;
    end;
end;

class (IScrolling) ScrollingMonitor(_id)
    InitIScrolling(_id);

    private macro SetColumns()
        Columns = TArray();
        FilterItems = TArray();
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "MENUNAME",
            "Type", ScrollingColumnType.Hyperlink,
            "Title", "Меню",
            "Source", "MENUNAME",
            "Tooltip", "Меню"
        );
        FilterItems[FilterItems.Size] = makeObj(
            "IFilterItem",
            "Id", FilterItems.Size + 1,
            "Type", FilterValueType.String,
            "Name", "Меню",
            "ColumnId", "MENUNAME",
            "Params", "MENUNAME"
        );
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "CODE",
            "Type", ScrollingColumnType.Text,
            "Title", "Состояние",
            "Source", "CODE",
            "Tooltip", "Состояние"
        );
        FilterItems[FilterItems.Size] = makeObj(
            "IFilterItem",
            "Id", FilterItems.Size + 1,
            "Type", FilterValueType.String,
            "Name", "Состояние",
            "ColumnId", "CODE",
            "Params", "CODE"
        );
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "TEXT",
            "Type", ScrollingColumnType.Text,
            "Title", "Название",
            "Source", "TEXT",
            "Tooltip", "Название"
        );
        FilterItems[FilterItems.Size] = makeObj(
            "IFilterItem",
            "Id", FilterItems.Size + 1,
            "Type", FilterValueType.String,
            "Name", "Название",
            "ColumnId", "TEXT",
            "Params", "TEXT"
        );
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "C",
            "Type", ScrollingColumnType.Numeric,
            "Title", "Количество",
            "Source", "C",
            "Tooltip", "Количество"
        );
        FilterItems[FilterItems.Size] = makeObj(
            "IFilterItem",
            "Id", FilterItems.Size + 1,
            "Type", FilterValueType.Integer,
            "Name", "Количество",
            "ColumnId", "C",
            "Params", "C"
        );
    end;

    private macro SetToolBarItems()
        SysToolbarItems = TArray();
        ToolbarItems = TArray();
        var cmd = TRslOraQuery();
        cmd.SqlText = "select RSP_ENCODE.BLOB_TO_HEX(ICON_PNG) as Icon from sicon where code = 3";
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        SysToolbarItems[SysToolbarItems.Size] = makeObj(
            "ISysButton",
            "Name", PredefinedSysButton.Open,
            "Service", WebServiceCallback("ws_monitor", "OpenRowMonitor")
        );
        SysToolbarItems[SysToolbarItems.Size] = makeObj(
             "ISysButton",
             "Name", PredefinedSysButton.AutoRefresh
        );
        ToolbarItems[ToolbarItems.Size] = makeObj(
            "IToolbarItem",
            "Type", ToolbarItemType.Button,
            "Text", "Открыть",
            "Image", cmd.GetByName("Icon").AsString,
            "Tooltip", "Открыть список",
            "Service", WebServiceCallback("ws_monitor", "OpenRowMonitor")
        );
        cmd.Clear();
        cmd.SqlText = "select RSP_ENCODE.BLOB_TO_HEX(ICON_PNG) as Icon from sicon where code = 4";
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        ToolbarItems[ToolbarItems.Size] = makeObj(
            "IToolbarItem",
            "Type", ToolbarItemType.Button,
            "Text", "Настроить",
            "Image", cmd.GetByName("Icon").AsString,
            "Tooltip", "Настройка монитора",
            "Service", WebServiceCallback("ws_monitor", "GetSettingMonitor")
        );
    end;

    Title = "Наличие документов";
    MultiPage = true;
    MultiSelect = false;
    SetColumns();
    SetToolBarItems();
    GetRowsService = WebServiceCallback("ws_monitor", "GetRowsMonitor");
    GetRowCountService = WebServiceCallback("ws_monitor", "GetRowCountMonitor");
end;

class (IWindow) ScrollingMonitorWindow(scrollingId)
    Id = scrollingId;
    Title = "Наличие документов";
    WindowParams = makeObj(
        "IWindowParams",
        "Width", 800,
        "Height", 480,
        "HelpPage", "",
        "IsModal", false,
        "IsResizing", true
    );
    Appearance = WindowAppearanceType.Scrolling;
    Fields = makeArray(
        makeObj(
            "IPanelItem",
            "Id", Id,
            "Row", 0,
            "Type", PanelItemType.Scrolling,
            "Field", ScrollingMonitor(Id)
        )
    );
end;

// Скроллинг монитора
macro GetScrollingMonitor(params)
    return WebServiceResult(WebServiceResultType.Window, ScrollingMonitorWindow("Monitor"));
end;


class (IScrolling) ScrollingSettingMonitor(_id)
    InitIScrolling(_id);

    private macro SetColumns()
        Columns = TArray();
        Columns[Columns.Size] = makeObj(
            "IScrollingColumn",
            "Id", "Name",
            "Type", ScrollingColumnType.Text,
            "Title", "Меню",
            "Source", "Name",
            "Tooltip", "Меню"
        );
    end;

    private macro SetToolBarItems()
//        SysToolbarItems = TArray();
        ToolbarItems = TArray();
/*        SysToolbarItems[SysToolbarItems.Size] = makeObj(
             "ISysButton",
             "Name", PredefinedSysButton.AutoRefresh
        );
*/
        var cmd = TRslOraQuery();
        cmd.SqlText = "select RSP_ENCODE.BLOB_TO_HEX(ICON_PNG) as Icon from sicon where code = 5";
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        ToolbarItems[ToolbarItems.Size] = makeObj(
            "IToolbarItem",
            "Type", ToolbarItemType.Button,
            "Text", "Сохранить",
            "Image", cmd.GetByName("Icon").AsString,
            "Tooltip", "Сохранение настроек",
            "Service", WebServiceCallback("ws_monitor", "SaveSettingMonitor")
        );
    end;

    Title = "Настройка списка монитора";
    MultiPage = false;
    MultiSelect = true;
    SetColumns();
    SetToolBarItems();
    GetRowsService = WebServiceCallback("ws_monitor", "GetRowsSettingMonitor");
    GetRowCountService = WebServiceCallback("ws_monitor", "GetRowsCountSettingMonitor");
end;

class (IWindow) ScrollingSettingMonitorWindow(scrollingId)
    Id = scrollingId;
    Title = "Настройка списка монитора";
    WindowParams = makeObj(
        "IWindowParams",
        "Width", 800,
        "Height", 480,
        "HelpPage", "",
        "IsModal", false,
        "IsResizing", true
    );
    Appearance = WindowAppearanceType.Scrolling;
    Fields = makeArray(
        makeObj(
            "IPanelItem",
            "Id", Id,
            "Row", 0,
            "Type", PanelItemType.Scrolling,
            "Field", ScrollingSettingMonitor(Id)
        )
    );
end;

macro OpenRowMonitor(params)
    if (params.CurrentRow != null )
      if(params.CurrentRow.ID_NMENU != null)
        if(params.CurrentRow.ID_SMENU == TypeMainMenuPosit)
          return GetPositioningWindow(params.CurrentRow.ID_NMENU);
        else
          return GetScrolling(params.CurrentRow.ID_NMENU);
        end;
      else
        return WebServiceResult(WebServiceResultType.Message, IMessage("Сообщение", "Виды документов не выбраны. Используйте настройку для выбора", MessageBoxType.Info));
      end;
    end;
end;

private macro GetQueryTextSettingMonitor()
    return "SELECT isSelected, ID, MenuName FROM TABLE(RSP_WEB.GET_SETUPMONITOR_BY_USER)";
end;

macro GetRowsSettingMonitor()
    var listMenuItem = TArray();
    var cmd = TRslOraQuery();
    cmd.SqlText = GetQueryTextSettingMonitor();
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    while (not cmd.Eof)
        listMenuItem[listMenuItem.Size] = makeObj("TDynamicBean", "isSelected", cmd.Rec.isSelected == 1, "ScrollingId", cmd.GetByName("ID").AsInteger, "Name", cmd.Rec.MenuName);
        cmd.Next();
    end;
    return listMenuItem;
end;

macro GetRowsCountSettingMonitor()
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT COUNT(*) CNT FROM (" + GetQueryTextSettingMonitor() + ")";
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.GetByName("CNT").AsInteger;
end;

// Сохранение настроек списка монитора
macro SaveSettingMonitor(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    // Удаление записей
    var cmd = TRslOraQuery();
    cmd.SqlText = "DELETE FROM setupu s WHERE nvl(s.usernick, 'ADMIN') = nvl(SYS_CONTEXT('RSP_ADM_CTX', 'USERID'), 'ADMIN')";
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    cmd.Clear();
    cmd.SqlText = "INSERT INTO setupu (id_nmenu, usernick, code, name) VALUES (:ID_NMENU, nvl(SYS_CONTEXT('RSP_ADM_CTX', 'USERID'), 'ADMIN'), 1, 'меню монитора состояний')";
    // Бежим по выбранным записям
    for (var selectedRow, params.SelectedRows)
        // Получение ИД записи
        cmd.DeclareAndSet("ID_NMENU", selectedRow.ScrollingId);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
    end;
    OraTrnCommit();
    return WebServiceResult(WebServiceResultType.Action, makeArray(IAction(WebServiceActionType.Close), IAction(WebServiceActionType.RefreshParent)));
end;

// Список настройки монитора
macro GetSettingMonitor(params)
    return WebServiceResult(WebServiceResultType.Window, ScrollingSettingMonitorWindow("SettingMonitor"));
end;
