/*09.12.2013 voikin*/
import uf_general, delzero, utils, uf_utils;
//import cnt_dlm;

var OrigNameFile_="";
//var OutNameFile_="";
var Ret_ = "";


macro AddFileToEvent(ID_Event, FPath)
  var FName, FExt, FSize = 0, FData;

  SplitFile (FPath, FName, FExt);

  FName = FName + "." + FExt;

  FData = LoadFileString(FPath);

  var Insert_EVFILE = TRslOraQuery();

  Insert_EVFILE.SqlText = " insert into EVFILE(ID_JEVENT, NAME, FSIZE, ISPACK, BINARYDATA) values(:ID_JEVENT, :NAME, :FSIZE, :ISPACK, :BINARYDATA) ";
  Insert_EVFILE.DeclareAndSet("ID_JEVENT",ID_Event, "NAME", FName, "FSIZE", FSize, "ISPACK", 0);
  Insert_EVFILE.DeclareAndSetLOB("BINARYDATA", FData, StrLen(FData));

  if(not Insert_EVFILE.Execute())
    return false;
  end;

  return true;
end;


Macro uf_LoadMultiMsgFileTrn(NameFile, GateId)

  var Insert_TMP_XML = TRslOraQuery();
  var LoadUFEBS;
  /*Помещаем во временную таблицу*/
  Insert_TMP_XML.SqlText = " insert into "+UFTblTmpXML+" (VALXML) values(XMLTYPE(:IMP_STRING)) ";
  Insert_TMP_XML.DeclareAndSetLOB("IMP_STRING", InpStr_);

  if(not Insert_TMP_XML.Execute())
//    AddPtk(Insert_TMP_XML.Error);
    RunError(toansi(Insert_TMP_XML.Error));
  end;

  /*Вызываем LoadUFEBS*/
  LoadUFEBS = TRslOraQuery();

  Ret_ = "";

  if(GateId == "UFEBS")
    LoadUFEBS.SqlText = " begin  :Ret := RSP_UFEBS.LoadUFEBS(:TMP_TABLE, :NAME_FILE); end;";
  elif(GateId == "SPFS")
    LoadUFEBS.SqlText = " begin  :Ret := RSP_UFEBS.LoadSPFS(:TMP_TABLE, :NAME_FILE); end;";
  end;

  LoadUFEBS.DeclareAndSet("Ret", Ret_, "TMP_TABLE", UFTblTmpXML, "NAME_FILE", NameFile);

  if(not LoadUFEBS.Execute)
    RunError(toansi(LoadUFEBS.Error));
  end;

  Ret_ = LoadUFEBS.GetVariable("Ret");

  if(Ret_ == "")
    RunError(toansi(" Функция LoadUFEBS(LoadSPFS) вернула пустую строку"));
  end;

  /*Создание события*/
  var New_Event = 0;
  var ID_Event;
  if(Index(Ret_,"PacketEPD"))
     New_Event = EV_FILUFRC;
  elif(Index(Ret_, "ED243") or Index(Ret_, "ED273"))
     New_Event = EV_QUERYRC;
  elif(Index(Ret_, "ED219"))
     New_Event = EV_ED219RC;
  end;

  if(New_Event)
    ID_Event = CreatJrnEventMail(New_Event,
                                 ToANSI(Ret_),
                                "",
                                "",
                                1);

    if(not ID_Event)
      RunError(toansi("Ошибка при создании события"));
    end;
    if(New_Event == EV_ED219RC)
      if(not AddFileToEvent(ID_Event, NameFile))
        RunError(toansi("Ошибка при добавлении файла к событию"));
      end;
    end;
  end;

End;


Macro uf_LoadMultiMsgFile(NameFile, GateId)

  InpStr_ = tooem(LoadFileString(NameFile));

  uf_LoadMultiMsgFileTrn(NameFile, GateId);

  OraTrnCommit();

  AddPtk("LoadUFEBS:"+Ret_);

  return true;

  OnError(e)
    OraTrnRollBack();
    AddPtk ("УФЭБС загрузка : " + ToOem(e.Message) + " | Модуль " + e.Module + " | Строка " + e.Line);
    //MsgBox(ToOem(e.Message));
    return false;
End;


Macro uf_ImpFiles(GateId, ImpDir, ArcDir, ErrDir, ImpFileNameMask)

  NameFile_ = ImpDir + ImpFileNameMask;
  Var OnceMoreFile = FindFirst(NameFile_), newNameFile, errNameFile;
  var ID_Event;
  While(OnceMoreFile)
    newNameFile  = ArcDir+NameFile_;
    errNameFile  = ErrDir+NameFile_;
    OrigNameFile_ = ImpDir+NameFile_;
    If (ExistFile (newNameFile) and DeleteFile(newNameFile))
      AddPtk(GateId+":Удалён файл "+newNameFile);
    End;

    If (MoveFile( OrigNameFile_, newNameFile))
      AddPtk(GateId+":Перемещен файл "+NameFile_+ " из "+ImpDir+" в "+ArcDir);
      If(not uf_LoadMultiMsgFile(newNameFile, GateId))
        /* нужно попытаться перенести файл в каталог файлов с ошибками */
        If (ExistFile (ErrNameFile) and DeleteFile(ErrNameFile))
          AddPtk(GateId+":Удалён файл "+ErrNameFile);
        End;

        If (MoveFile(newNameFile, ErrNameFile))
           AddPtk(GateId+":Файл "+NameFile_+" из "+ArcDir+" перемещен в "+ErrDir);
           /*Создание события*/

           ID_Event = CreatJrnEventMail(EV_FILEERR,
                                        ToANSI(GateId+":Файл "+NameFile_+" из "+ArcDir+" перемещен в "+ErrDir),
                                       "",
                                       "",
                                       1);

           if(not ID_Event)
             RunError(toansi("Ошибка при создании события"));
           end;

        Else
           MakeErrorMsg (GateId+":Ошибка при переносе файла "+NameFile_+" из "+ArcDir+" в "+ErrDir);
        End;
      End;
    Else
      MakeErrorMsg (GateId+":Ошибка переноса файла "+NameFile_+" из "+ImpDir+" в "+ArcDir);
    End;
    OnceMoreFile = FindNext(NameFile_);
  End;
End;

Macro kbr_LoadMultiMsgFile(NameFile, ImpDir)

  var xmlStr:string = "";
  var toXmlStr:string = "";
  var NewFileName;
  var Ext;
  var ErrStr;

  InpStr_ = LoadFileString(NameFile);

  xmlStr = GetValue (InpStr_, "<sen:Object>", "</sen:Object>");
  if(not StrLen(xmlStr))
    AddPtk("Не найден узел 'Object' в файле "+NameFile);
    return false;
  end;


//  if(DecodeFromBase64(xmlStr, toXmlStr) )
  toXmlStr = DecodeFromBase64(xmlStr, ErrStr);
  if(not StrLen(toXmlStr))
    AddPtk("Ошибка при выполнении преобразования Base64 над тегом 'Object' файла "+NameFile+":");
    AddPtk(ErrStr);
    return false;
  end;

  SplitFile (NameFile, NewFileName, Ext);

  NewFileName = NewFileName+".xml";

  if(WriteStrToFile(toXmlStr, ImpDir+NewFileName))
    AddPtk("Создан файл "+ImpDir+NewFileName);
  else
    AddPtk("Ошибка при записи в файл "+ImpDir+NewFileName);
    return false;
  end;

  return true;
End;


Macro kbr_ImpFiles(GateId, ImpDir, ArcDir, ErrDir, NewDir, ImpFileNameMask)

  NameFile_ = ImpDir + ImpFileNameMask;
  Var OnceMoreFile = FindFirst(NameFile_), newNameFile, errNameFile;
  While(OnceMoreFile)
    newNameFile  = ArcDir+NameFile_;
    errNameFile  = ErrDir+NameFile_;
    OrigNameFile_ = ImpDir+NameFile_;
    If (ExistFile (newNameFile) and DeleteFile(newNameFile))
      AddPtk(GateId+":Удалён файл "+newNameFile);
    End;

    If (MoveFile( OrigNameFile_, newNameFile))
      AddPtk(GateId+":Перемещен файл "+NameFile_+ " из "+ImpDir+" в "+ArcDir);
      If(not kbr_LoadMultiMsgFile(newNameFile, NewDir))
        /* нужно попытаться перенести файл в каталог файлов с ошибками */
        If (ExistFile (ErrNameFile) and DeleteFile(ErrNameFile))
          AddPtk(GateId+":Удалён файл "+ErrNameFile);
        End;

        If (MoveFile(newNameFile, ErrNameFile))
           AddPtk(GateId+":Файл "+NameFile_+" из "+ArcDir+" перемещен в "+ErrDir);
        Else
           MakeErrorMsg (GateId+":Ошибка при переносе файла "+NameFile_+" из "+ArcDir+" в "+ErrDir);
        End;
      End;
    Else
      MakeErrorMsg (GateId+":Ошибка переноса файла "+NameFile_+" из "+ImpDir+" в "+ArcDir);
    End;
    OnceMoreFile = FindNext(NameFile_);
  End;
End;


macro LoadUFEBS()

  kbr_ImpFiles(UFEBSIdGate, UFDirUFEBSInKbr, UFDirUFEBSArc, UFDirUFEBSErr, UFDirUFEBSIn, UFImpFileNameMask);
  kbr_ImpFiles(UFEBSIdGate, UFDirBESPInKbr, UFDirBESPArc, UFDirBESPErr, UFDirBESPIn, UFImpFileNameMask);

  uf_ImpFiles(UFEBSIdGate, UFDirUFEBSIn, UFDirUFEBSArc, UFDirUFEBSErr, UFImpFileNameMask);
  uf_ImpFiles(UFEBSIdGate, UFDirBESPIn,  UFDirBESPArc,  UFDirBESPErr,  UFImpFileNameMask);

end;
macro LoadSPFS()

  uf_ImpFiles(SPFSIdGate, UFDirSPFSIn, UFDirSPFSArc, UFDirSPFSErr, UFImpFileNameMask);

end;
