import ws_types;


// Возвращает текст запроса для скроллинга справочника из макроса
private macro GetSQLTextSpr(scrollingId, filter, order)
    var cmd = TRslOraQuery();
    cmd.SqlText = "select RSP_WEB.GET_LISTDOC_FOR_FORM_CACHE(:ID_FORM, :P_ORDER, :P_FILTER) QueryText from DUAL";
    cmd.DeclareAndSet("ID_FORM", scrollingId, "P_ORDER", order, "P_FILTER", filter);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.Rec.QueryText;
end;
/*
private macro GetSQLTextSpr(formId)
    var cmd = TRslOraQuery();
    cmd.SqlText = "select RSP_WEB.GET_LISTDOC_FOR_FORM_CACHE(:ID_FORM) QueryText from DUAL";
    cmd.DeclareAndSet("ID_FORM", formId);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.Rec.QueryText;
end;
*/
// Возвращает записи для списка справочника
macro GetRowsServiceSpr(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var cmd = TRslOraQuery();
    cmd.SqlText = SQL_WrapSelect(GetSQLTextSpr(params.ScrollingId, GetSqlFilterCondition(params.FilterItems), GetOrderStr(params.OrderItems)), params.PageNumber, params.PageSize);
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd;
end;

// Возвращает количество записей для списка справочника
macro GetRowCountServiceSpr(params)
    var wParams = WebServiceParams();
    wParams.FromStr(params.Params);
    var cmd = TRslOraQuery();
    cmd.SqlText = "SELECT COUNT(*) CNT FROM (" + GetSQLTextSpr(params.ScrollingId, GetSqlFilterCondition(params.FilterItems), GetOrderStr(params.OrderItems)) + ")";
    if (not cmd.Execute())
        RunError(ToANSI(cmd.Error));
    end;
    return cmd.GetByName("CNT").AsInteger;
end;

class (IScrolling) ScrollingSpr(formId, macroFile, macroProc, Params)
    InitIScrolling(formId, IIF(Xml2Rsl(Params).PropIndex("Row") != -1, Xml2Rsl(Params).Row));
    var wParams = WebServiceParams(0, 0, 0, formId);

    private macro SetColumns(formId)
        var cmd = TRslOraQuery();
        CaptureOutput();
[
SELECT DECODE (decode(reffield, null, typefield, nvl(typereffield, typefield)),
               1, /*строка*/               'text', /*строка*/
               2, /*число: длинное целое*/ 'numeric', /*число*/
               3, /*число: короткое целое*/'numeric', /*число*/
               7, /*дробное число*/        'numeric', /*число*/
               4, /*сумма*/                'money', /*деньги*/
               8, /*булево*/               'flag', /*флаг*/
               5, /*дата*/                 'date', /*дата*/
               6, /*время*/                'time', /*время*/
               9, /*ДатаВремя*/            'datetime', /*дата и время*/
               10,/*ТаймШтамп*/            'datetime', /*дата и время*/
               11,/*текстовые данные*/     'text', /*строка*/
               12,/*бинарные данные*/      'text', /*строка*/
               13,/*xml данные*/           'text', /*строка*/
               typefield)
       ColumnType,
      DECODE (decode(reffield, null, typefield, nvl(typereffield, typefield)),
               1, /*строка*/               'string', /*строка*/
               2, /*число: длинное целое*/ 'integer', /*число*/
               3, /*число: короткое целое*/'integer', /*число*/
               7, /*дробное число*/        'float', /*число*/
               4, /*сумма*/                'money', /*деньги*/
               8, /*булево*/               'boolean', /*флаг*/
               5, /*дата*/                 'date', /*дата*/
               6, /*время*/                'time', /*время*/
               9, /*ДатаВремя*/            'datetime', /*дата и время*/
               10,/*ТаймШтамп*/            'datetime', /*дата и время*/
               11,/*текстовые данные*/     'string', /*строка*/
               12,/*бинарные данные*/      'string', /*строка*/
               13,/*xml данные*/           'string', /*строка*/
               typefield)
       FilterType,
       t.text Title,
       tbname,
/*       t.name Source,*/
       t.name||CASE WHEN t.islkp = 1
                    THEN '8'||(select ld.name from field ld where ld.id_field = reffield)
               END Source,
       t.id_field ID
  FROM (SELECT fl.*, tb.name AS tbname, tf.code AS typefield,
               lkptypefield AS typereffield, l.id_field_child as reffield
          FROM field fl, ntable tb, stypefield tf, form f,
               (select typf.code lkptypefield, lk.id_field, lf.id_field_child
                 from lkp lk, lkpfl lf, field fl, stypefield typf
                 where lk.id_lkp = lf.id_lkp
                   and lf.id_field_child = fl.id_field
                   and fl.id_stypefield = typf.id_stypefield) l
         WHERE f.id_form = :id_form
           AND fl.id_form = f.id_form
           AND f.id_ntable = tb.id_ntable
           AND fl.id_field = l.id_field(+)
           AND fl.ISGRID > 0
           AND fl.id_stypefield = tf.id_stypefield
      ORDER BY fl.orderfield) t
];
        cmd.SqlText = StopCaptureOutput();
        cmd.DeclareAndSet("id_form", formId);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        Columns = TArray();
        FilterItems = TArray();
        while (not cmd.Eof)
            Columns[Columns.Size] = makeObj(
                "IScrollingColumn",
                "Id", cmd.Rec.TBNAME + "8" + cmd.Rec.SOURCE,
                "Type", cmd.Rec.ColumnType,
                "Title", cmd.Rec.TITLE,
                "Source", cmd.Rec.TBNAME + "8" + cmd.Rec.SOURCE,
                "Tooltip", cmd.Rec.TITLE
            );
            FilterItems[FilterItems.Size] = makeObj(
                "IFilterItem",
                "Id", cmd.GetByName("ID").AsInteger,
                "Type", cmd.Rec.FilterType,
                "Name", cmd.Rec.TITLE,
                "ColumnId", cmd.Rec.TBNAME + "8" + cmd.Rec.SOURCE,
                "Params", cmd.Rec.TBNAME + "8" + cmd.Rec.SOURCE
            );
            cmd.Next();
        end;
    end;

    private macro SetKeyFields(formId)
        var tableName: string = "";
        var fieldName: string = "";
        var cmd = TRslOraQuery();
        cmd.SqlText = "begin :TableName := RSP_FORM.GET_MAINTABLEFORM(:FormSprId, 1); :FieldName := RSP_FORM.GET_PK_TABLE(:TableName); end;";
        cmd.DeclareAndSet("TableName", tableName, "FormSprId", formId, "FieldName", fieldName);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        tableName = cmd.GetVariable("TableName");
        fieldName = cmd.GetVariable("FieldName");
        RowKeyFields = makeArray(tableName + "8" + fieldName);
    end;

    private macro SetToolBarItemsSpr(formId, macroFile, macroProc, Params)
        SysToolbarItems = TArray();
        ToolbarItems = TArray();
        var cmd = TRslOraQuery();
        cmd.SqlText = "select RSP_ENCODE.BLOB_TO_HEX(ICON_PNG) as Icon from sicon where code = 3";
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;

        ToolbarItems[ToolbarItems.Size] = makeObj(
            "IToolbarItem",
            "Type", ToolbarItemType.Button,
            "Text", "Выбрать",
            "Image", cmd.GetByName("Icon").AsString,
            "Tooltip", "Выбрать запись",
            "Service", WebServiceCallback(macroFile, macroProc,  Params)
        );
    end;

        SetColumns(formId);
        SetToolBarItemsSpr(formId, macroFile, macroProc, Params);
        SetKeyFields(formId);
        GetRowsService = WebServiceCallback("ws_scrollspr", "GetRowsServiceSpr", wParams.ToStr());
        GetRowCountService = WebServiceCallback("ws_scrollspr", "GetRowCountServiceSpr", wParams.ToStr());
end;

class (IWindow) ScrollingWindowSpr(formId, macroFile, macroProc, Params)

    private macro GetTitle(formId): string
        var cmd = TRslOraQuery();
        CaptureOutput();
[
SELECT f.text
  FROM FORM f
 WHERE f.id_form = :ID_FORM
];
        cmd.SqlText = StopCaptureOutput();
        cmd.DeclareAndSet("ID_FORM", formId);
        if (not cmd.Execute())
            RunError(ToANSI(cmd.Error));
        end;
        return "Справочник "+cmd.GetByName("text").AsString;
    end;

    Title = GetTitle(formId);
    ID= "selspr"+formId;

    WindowParams = makeObj(
        "IWindowParams",
        "Width", 800,
        "Height", 480,
        "HelpPage", "",
        "IsModal", true,
        "IsResizing", true
    );
    Appearance = WindowAppearanceType.Scrolling;
    Fields = makeArray(
        makeObj(
            "IPanelItem",
            "Id", formId,
            "Row", 0,
            "Type", PanelItemType.Scrolling,
            "Field", ScrollingSpr(formId, macroFile, macroProc, Params)
        )
    );
end;
