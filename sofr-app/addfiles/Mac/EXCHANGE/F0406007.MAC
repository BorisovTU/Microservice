/*-RSL---------------------------------------- R-Style Software Lab --
               Обменный пункт

               Печать справки свободной формы вместо БСО

               RA 11-06-04 Создан
*/
  
import "formBSO.mac", ChkPrn, QueryRes, rsd;

record exoperat ( "exoperat.dbt", "exchange.def" );
file   f_operat ( "exoperat.dbt", "exchange.def" );
record paydocop ( "paydocop.str", "exchange.def" );
private var sb_casln = TBFile( "sb_casln.dbt", "R", 1, null, "sbbank.def" );
private var pay_doc  = TBFile( "pay_doc.dbt" , "R", 2, null, "sbbank.def" );
private file FileExOper ( exoperat,       "exchange.def" ) key 1 write;


private var ОТЧЕТ_В_EXCEL = false;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO FindComissionDoc()

  var mainKind = 0;
  var mainKey  = "";
  var stat;
  var wasFind = false;

  sb_casln.KeyNum = 1;
  sb_casln.Clear;
  sb_casln.rec.ApplicationKind2 = exoperat.ApplicationKind;
  sb_casln.rec.ApplicationKey2  = exoperat.ApplicationKey;

  if ( sb_casln.GetEQ )
    mainKind = sb_casln.rec.ApplicationKind1;
    mainKey  = sb_casln.rec.ApplicationKey1;
  end;

  if ( mainKey != "" )
    sb_casln.KeyNum = 0;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = mainKind;
    sb_casln.rec.ApplicationKey1  = mainKey;

    stat = sb_casln.GetGE;

    while ( stat )

      if ( ( sb_casln.rec.ApplicationKind1 == mainKind ) and
           ( sb_casln.rec.ApplicationKey1  == mainKey  )    )

        if ( ( sb_casln.rec.ApplicationKind2 == 200 ) and ( sb_casln.rec.RefValue2 == 0 ) )

          pay_doc.clear();
          pay_doc.keynum( 2 );

          pay_doc.rec.iApplicationKind = sb_casln.rec.ApplicationKind2;
          pay_doc.rec.ApplicationKey   = sb_casln.rec.ApplicationKey2;

          if( pay_doc.getEQ() )
            if ( pay_doc.rec.GroupOpert == 21 )
              wasFind = true;
              stat = false;
            end;
          end;
        end;
      else
        stat = false;
      end;

      if ( stat )
        stat = sb_casln.Next;
      end;
    end;
  end;

  return wasFind;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro IsFirstExOperat()

  var cmd = RsdCommand( "select 0 from dsb_casln_dbt c1, dsb_casln_dbt c2 "
                        "where c1.t_applicationKind1 = c2.t_applicationKind1 "
                        "  and c1.t_applicationKey1 = c2.t_applicationKey1 "
                        "  and c1.t_numLinkDoc < c2.t_numLinkDoc "
                        "  and c1.t_applicationKind2 = c2.t_applicationKind2 "
                        "  and c1.t_refValue2 = 0 "
                        "  and c2.t_refValue2 = 0 "
                        "  and c2.t_applicationKind2 = ? "
                        "  and c2.t_applicationKey2 = ? " );

  cmd.addParam( "p_appKind", RsdBp_In );
  cmd.value( "p_appKind" ) = exoperat.ApplicationKind;
  cmd.addParam( "p_appKey", RsdBp_In );
  cmd.value( "p_appKey" ) = exoperat.ApplicationKey;

  cmd.execute;
  var rs = RsdRecordset( cmd );
  return not rs.moveNext;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro CalcSumForPD ( inSum, outSum, inQuantum, outQuantum )

  var mainKind = 0;
  var mainKey  = "";
  var stat;

  inSum = $0L;
  outSum = $0L;
  inQuantum = 0;
  outQuantum = 0;

  sb_casln.KeyNum = 1;
  sb_casln.Clear;
  sb_casln.rec.ApplicationKind2 = exoperat.ApplicationKind;
  sb_casln.rec.ApplicationKey2  = exoperat.ApplicationKey;

  if ( sb_casln.GetEQ )
    mainKind = sb_casln.rec.ApplicationKind1;
    mainKey  = sb_casln.rec.ApplicationKey1;
  end;

  if ( mainKey != "" )
    sb_casln.KeyNum = 0;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = mainKind;
    sb_casln.rec.ApplicationKey1  = mainKey;

    stat = sb_casln.GetGE;

    while ( stat )

      if ( ( sb_casln.rec.ApplicationKind1 == mainKind ) and
           ( sb_casln.rec.ApplicationKey1  == mainKey  )    )

        if ( ( sb_casln.rec.ApplicationKind2 == 260 ) and ( sb_casln.rec.RefValue2 == 0 ) )

          ClearRecord( FileExOper );
          FileExOper.ApplicationKind  = sb_casln.rec.ApplicationKind2;
          FileExOper.ApplicationKey   = sb_casln.rec.ApplicationKey2;

          if ( GetEQ( FileExOper ) and ( FileExOper.Kind_Oper == exoperat.Kind_Oper ) and 
                                       ( FileExOper.IncomeRefVal == exoperat.IncomeRefVal ) and
                                       ( FileExOper.OutRefVal == exoperat.OutRefVal )          )
            inSum = inSum + FileExOper.IncomeAmount;
            outSum = outSum + FileExOper.OutAmount;

            if ( FileExOper.IncomeTypeValue == TYPERES_PAYDOC )
              SetRecordAddr( paydocop, FileExOper, 0, FldOffset( FileExOper, "ExtInfo" ), True );
              inQuantum = inQuantum + paydocop.Amount;
            elif ( exoperat.OutTypeValue == TYPERES_PAYDOC )
              SetRecordAddr( paydocop, FileExOper, 0, FldOffset( FileExOper, "ExtInfo" ), True );
              outQuantum = outQuantum + paydocop.Amount;
            end;

          end;

        end;

        stat = sb_casln.Next;
      else
        stat = false;
      end;
    end;
  end;

  SetParm( 0, inSum );
  SetParm( 1, outSum );
  SetParm( 2, inQuantum );
  SetParm( 3, outQuantum );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro IsExOperatWithPD
                                
  if ( ( exoperat.Kind_Oper ==  3 ) or
       ( exoperat.Kind_Oper ==  4 ) or
       ( exoperat.Kind_Oper == 20 ) or
       ( exoperat.Kind_Oper == 21 ) or
       ( exoperat.Kind_Oper == 38 ) or
       ( exoperat.Kind_Oper == 39 ) or
       ( exoperat.Kind_Oper == 40 ) or
       ( exoperat.Kind_Oper == 41 )   )
    return true;
  end;

  return false;

end;


/*========================================================================
       Головной макрос
========================================================================*/
MACRO PutF0406007( addr )

  var Отчет;
  var stat;
  var flagFirstExOperat;

  SetBuff( exoperat, addr ); /* буфер записи текущей операции */
  ALG_RESULT = ERR_RES;

  flagFirstExOperat = IsFirstExOperat( );

  /*
  const RegPath = "RS-RETAIL\\ПЕЧАТИ\\ВКЛАДЫ\\СПРАВКА_0406007";
  if ( ( GetRegistryValue( RegPath, V_STRING, Value, ErrCode ) == V_STRING ) and ( Value != "CON" ) )
    CheckPrinter( RegPath, alg_param.prmC );
  end;
  */

  Отчет = COrderBSO();
    
  Отчет.Rate_Ref = exoperat.Rate_Ref;
  /* По умолчанию текущая системная дата и время
  Отчет.formDate     = exoperat.Date;
  Отчет.formTime     = exoperat.Time;
  */
  Отчет.KindOper = GetCodeExchOperation( exoperat.Branch, exoperat.Kind_Oper, exoperat.SubOper );
  Отчет.Rate     = exoperat.Kurs;

  if ( ( StrLen( Trim( exoperat.Sname ) ) != 0 ) and ( Trim( exoperat.Sname ) != "???" ) )
    Отчет.Sname = exoperat.Sname;
  else
    Отчет.Sname = "";
  end;
  if ( ( StrLen( Trim( exoperat.Name ) ) != 0 ) and ( Trim( exoperat.Name ) != "???" ) )
    Отчет.Name = exoperat.Name;
  else
    Отчет.Name = "";
  end;
  if ( ( StrLen( Trim( exoperat.Pname ) ) != 0 ) and ( Trim( exoperat.Pname ) != "???" ) )
    Отчет.Pname = exoperat.Pname;
  else
    Отчет.Pname = "";
  end;

  Отчет.ClientDocType = exoperat.ClientDocType;
  if ( ( StrLen( Trim( exoperat.ClientDocSeria ) ) != 0 ) and ( Trim( exoperat.ClientDocSeria ) != "XXX" ) )
    Отчет.ClientDocSeria = exoperat.ClientDocSeria;
  else
    Отчет.ClientDocSeria = "";
  end;
  if ( ( StrLen( Trim( exoperat.ClientDocNumber ) ) != 0 ) and ( Trim( exoperat.ClientDocNumber ) != "XXX" ) )
    Отчет.ClientDocNumber = exoperat.ClientDocNumber;
  else
    Отчет.ClientDocNumber = "";
  end;
  if ( ( StrLen( Trim( exoperat.ClientDocInfo ) ) != 0 ) and ( Trim( exoperat.ClientDocInfo ) != "XXX" ) )
    Отчет.ClientDocInfo = exoperat.ClientDocInfo;
  else
    Отчет.ClientDocInfo = "";
  end;
  Отчет.ClientDocDate = "";

  Отчет.IncomeRefVal    = exoperat.IncomeRefVal;
  Отчет.IncomeTypeValue = exoperat.IncomeTypeValue;
  Отчет.IncomeAmount    = exoperat.IncomeAmount;
  Отчет.OutRefVal       = exoperat.OutRefVal;
  Отчет.OutTypeValue    = exoperat.OutTypeValue;
  Отчет.OutAmount       = exoperat.OutAmount;
  Отчет.ClntIdentType   = exoperat.ClntIdentType;

  if ( flagFirstExOperat and FindComissionDoc( ) )
    Отчет.CommCodCur = pay_doc.Rec.CodCur;
    Отчет.CommAmount = pay_doc.Rec.InSum;
  end;

  Copy( f_operat, exoperat );
  SetRecordAddr( paydocop, f_operat, 0, FldOffset( exoperat, "ExtInfo" ), True );
  if ( exoperat.IncomeTypeValue == TYPERES_PAYDOC )
    Отчет.IncomeQuantum  = paydocop.Amount;
  elif ( exoperat.OutTypeValue == TYPERES_PAYDOC )
    Отчет.OutQuantum  = paydocop.Amount;
  end;

  Отчет.ApplicationKind = exoperat.ApplicationKind;
  Отчет.ApplicationKey  = exoperat.ApplicationKey;  

  /* Операции по инкассо, экспертизе и подлинности не должны попасть в реестр */
  if ( ( exoperat.Kind_Oper == 16 ) or  /* Прием ин. вал. на инкассо */
       ( exoperat.Kind_Oper == 17 ) or  /* Прием ин. вал. на экспертизу */
       ( exoperat.Kind_Oper == 24 ) or  /* Возврат ин. вал. с инкассо */
       ( exoperat.Kind_Oper == 25 ) or  /* Возврат ин. вал. с экспертизы */
       ( exoperat.Kind_Oper == 26 ) or  /* Проверка на подлинность */
       ( exoperat.Kind_Oper == 27 ) or  /* Прием ПД на инкассо */
       ( exoperat.Kind_Oper == 28 ) or  /* Прием ПД на экспертизу */
       ( exoperat.Kind_Oper == 29 ) or  /* Возврат ПД с инкассо */
       ( exoperat.Kind_Oper == 30 )   ) /* Возврат ПД с экспертизы */
    Отчет.OnlyInformation = true;
  elif ( IsExOperatWithPD( ) )
    if ( flagFirstExOperat )
      CalcSumForPD( Отчет.IncomeAmount, Отчет.OutAmount, Отчет.IncomeQuantum, Отчет.OutQuantum );
    else
      Отчет.OnlyInsert = true;
    end;
  end; 

  Отчет.ОТЧЕТ_В_EXCEL = ОТЧЕТ_В_EXCEL;
  Отчет.Print( );

  ClearRecord( FileExOper );
  FileExOper.ApplicationKind  = exoperat.ApplicationKind;
  FileExOper.ApplicationKey   = exoperat.ApplicationKey;
  stat = GetEQ( FileExOper );
  if ( stat )
    FileExOper.F0406007Issued = "X";
    stat = Update( FileExOper );
  end;

  if ( stat )
    ALG_RESULT = OK_RES;
  else
    ALG_RESULT = ERR_RES;
  end;

  if ( Отчет.OnlyInsert )
    Exit( 1 ); // Отчет не печатать
  end;

END;
