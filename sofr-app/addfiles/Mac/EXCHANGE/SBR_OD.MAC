/****************************************************************************
   RS-Bank        Обменный пункт
   sbr_od.mac - макрос формирует операционные дневники ОП для отчетности СБ
   Copyright 1998 (c) R-Style Software Lab.
   Создание: 15-05-98 by AV

   ОперационныйДневник_1() - стандартное название функции, формирующей
                             дневники (ОД)   Форма N24
****************************************************************************/

/*Массив для итоговых значений по всем ОД */
/* Код Валюты + Значения полей (3,4,5,6) */
ARRAY ИтогиПоВалютам;
var КоличествоЭлементов = 5;/*количество элементов для каждой валюты  */
var НомерВалюты = 0;        /*текущий номер валюты при формировании ОД*/

/*массив различных валют использованных в реестрах */
ARRAY DiffCurr;
/*массив для полученных сумм по конверсии валют*/
ARRAY ConvIncSum;
/*массив для выданных сумм по конверсии валют*/
ARRAY ConvOutSum;

/*Общая сумма комиссии для ОД*/
var ОбщаяКомиссия = $0.00;

/* Настройки для формирования операционных дневников ************************/
/*  !!!!! шапка и дно дневника переделываются вручную                       */
  ШиринаКолонок    = 15; /* ширина колонок в операционном дневнике          */
  МаксимумКолонок  = 8;  /* максимальное количество колонок                 */
  НачальнаяКолонка = 3;  /* начальная колонка для вывода чисел              */
  ШиринаНазвания   = 38; /* ширина графы для названия операции              */
/****************************************************************************/


/****************************************************************************/
/*  Процедура обнуляет массив с итгами по колонкам для ОД                   */
/****************************************************************************/
macro ОбнулениеИтоговОД()
   var i = 0;
   While( i < МаксимумКолонок )
      ИтогКолонки( i ) = 0;
      i = i + 1;
   end;
end;
/****************************************************************************/
/* Процедура заполняет массив с итогами по всем ОД для разных валют         */
/****************************************************************************/
macro ЗаполнениеИтоговПоВалютам( КодВалюты )
   var Cмещение = НомерВалюты * КоличествоЭлементов;
   ИтогиПоВалютам( Cмещение     ) = КодВалюты;
   ИтогиПоВалютам( Cмещение + 1 ) = ИтогКолонки( 2 );/*3-я графа*/
   ИтогиПоВалютам( Cмещение + 2 ) = ИтогКолонки( 3 );/*4-я графа*/
   ИтогиПоВалютам( Cмещение + 3 ) = ИтогКолонки( 4 );/*5-я графа*/
   ИтогиПоВалютам( Cмещение + 4 ) = ИтогКолонки( 5 );/*6-я графа*/
end;
/****************************************************************************/
/* Процедура заполняет массивы с разными номерами смен курсов и валют       */
/****************************************************************************/
macro ЗаполнитьМассивВалют()
 asize(DiffCurr, 0);
 var j = 0;

 ClearRecord(Reestr);
 Reestr.ExchangeNumber = office.ID;
 Reestr.Emp_Ref        = Session.Emp_Ref;
 Reestr.Sess_Ref       = Session.Number;

 rewind( Reestr );/*перемотать на начало*/
 var BreakLoop = GetGE( Reestr );

 /*цикл по реестрам*/
 While( BreakLoop AND (Reestr.Emp_Ref == Session.Emp_Ref ) AND (Reestr.Sess_Ref== Session.Number) )
    PutNumberInArray(    DiffCurr,    Reestr.OutCurr_Ref, j );
    PutNumberInArray(    DiffCurr, Reestr.IncomeCurr_Ref, j );
    BreakLoop = Next( Reestr );
 end;

 TotalNumCurr = j;
end;

macro СтрокаИтогов
 var Строка = "        |              ИТОГО:                  |";
 var i = НачальнаяКолонка;
 While( i <= МаксимумКолонок )
   /*добавление пустых колонок*/
   if( ИтогКолонки(i-1) )
      Строка = Строка + Число_Строка( ИтогКолонки(i-1) ) + "|";
   else
      Строка = Строка + ПустаяГрафа();
   end;
   i = i + 1;
 end;
 println( Строка );
end;


MACRO ШапкаОперационногоДневника( НазваниеВалюты, КодИСО )
[
  Сберегательная касса                                                                                                                 Форма N24
                               N___________    ОПЕРАЦИОННЫЙ ДНЕВНИК  N _______  за  #
                           в_______#_______
 #
 -----------------------------------------------------------------------------------------------------------------------------------------------+
   N N   |                                      |      Наличные деньги          |     Мемориальные обороты      |           Ценности            |
 лицевых |                                      |-------------------------------+-------------------------------+-------------------------------|
 счетов и|   Краткое содержание операций        |               |               |               |               |               |               |
 докумен-|                                      |    приход     |    расход     |   зачислено   |    списано    |    приход(###)|    расход(###)|
 тов     |                                      |               |               |               |               |               |               |
 --------+--------------------------------------+---------------+---------------+---------------+---------------+---------------+---------------|
     1   |                  2                   |       3       |       4       |       5       |      6        |       7       |       8       |
 --------+--------------------------------------+---------------+---------------+---------------+---------------+---------------+---------------|
]
( StrAddZeroDate( Session.StartDate ),
  {ExRegNumber}:c,
  НазваниеВалюты,
  КодИСО,
  КодИСО
);
END;

macro РазделительСтрок()
return
"--------+--------------------------------------+---------------+---------------+---------------+---------------+---------------+---------------|";
end;

macro ДноОперационногоДневника()
[-----------------------------------------------------------------------------------------------------------------------------------------------|
     Кассир________________ #                                                                                                                   |
 -----------------------------------------------------------------------------------------------------------------------------------------------+


 ---------------------------------------------------------------------------------------------------------------------------------------------


]
( GetFIOEmployee( Session.Emp_Ref ) );
end;


/*процедуры формирующие строки операционного дневника*/
macro ПроданоВалюты( КурсПродажи, Приход, Расход )
 println( "        |" + СтрокаОД( Название(" Продано  Кпр = " + КурсПродажи),  3, Приход, 8, Расход ) );
end;
macro ПроданоПД( КурсПродажи, Приход, Расход )
 println( "        |" + СтрокаОД( Название(" Продано ПД Кпр = " + КурсПродажи),  3, Приход, 8, Расход ) );
end;
macro КурсПродажи_ОК( Разница )
 println( "        |" + СтрокаОД( Название(" Разница между Кпр и ОК "), 3, Разница ) );
end;
macro КупленоВалюты( КурсПокупки, Расход, Приход )
 println( "        |" + СтрокаОД( Название(" Куплено Кпок = " + КурсПокупки), 4, Расход, 7, Приход ) );
end;
macro КупленоПД( КурсПокупки, Расход, Приход )
 println( "        |" + СтрокаОД( Название(" Куплено ПД Кпок = " + КурсПокупки), 4, Расход, 7, Приход ) );
end;
macro КурсПокупки_ОК( Разница )
 println( "        |" + СтрокаОД( Название(" Разница между Кпок и ОК "), 5, Разница ) );
 println( РазделительСтрок() );
end;
macro ПолученАвансВалюты( Зачислено, Приход )
 println( "        |" + СтрокаОД( Название(" Получен аванс "), 5, Зачислено, 7, Приход ) );
 println( РазделительСтрок() );
end;
macro ПолученАвансРублевый( Приход )
 println( "        |" + СтрокаОД( Название(" Получен аванс "), 3, Приход ) );
 println( РазделительСтрок() );
end;
macro ПереданоВалюты( Списано, Расход )
 println( "        |" + СтрокаОД( Название(" Передано другому кассиру "), 6, Списано, 8, Расход ) );
 println( РазделительСтрок() );
end;
macro ПереданоРублевый( Расход )
 println( "        |" + СтрокаОД( Название(" Передано другому кассиру "), 4, Расход ) );
 println( РазделительСтрок() );
end;
macro НалогНаПродажу( Налог )
 println( "        |" + СтрокаОД( Название(" Налог на продажу "), 3, Налог ) );
 println( РазделительСтрок() );
end;
macro КомиссионныйСбор( Комиссия )
 println( "        |" + СтрокаОД( Название(" Комиссионный сбор "), 3, Комиссия ) );
 println( РазделительСтрок() );
end;
macro СданоВалюты( Списано, Расход )
 println( "        |" + СтрокаОД( Название(" Сдано во вн. кассу "), 6, Списано, 8, Расход ));
 println( РазделительСтрок() );
end;
macro СданоРублевый( Расход )
 println( "        |" + СтрокаОД( Название(" Сдано во вн. кассу "), 4, Расход ));
 println( РазделительСтрок() );
end;
macro СуммыПоВалюте( КодВалюты, Приход, Расход, Зачислено, Списано )
 println( "        |" + СтрокаОД( Название(" " + GetNameCurr(КодВалюты) ), 3, Приход, 4, Расход, 5, Зачислено, 6, Списано) );
 println( РазделительСтрок() );
end;
macro ПолученоСправок( Количество )
 println( "        |" + СтрокаОД( Название(" Получено справок 0406007 "), 7, Количество) );
 println( РазделительСтрок() );
end;
macro ИзрасходованоСправок( Количество )
 println( "        |" + СтрокаОД( Название(" Израсходовано справок 0406007 "), 8, Количество) );
 println( РазделительСтрок() );
end;
macro ВозвращеноСправок( Количество )
 println( "        |" + СтрокаОД( Название(" Возвращено справок 0406007 "), 8, Количество) );
 println( РазделительСтрок() );
end;

/****************************************************************************/
/* Процедура выводит строки с итогами по операциям всех смен курсов         */
/****************************************************************************/
macro ИтогиПоОперации( KindOper, IncCurr, OutCurr )
 var TypeReestr = GetTypeReestr(KindOper), КурсОперации, ПроцентнаяКомиссия;
 var Тип, IncomeSumCB, IncomeSum, РазницаПрод, OutSumCB, РазницаПок;
 Reestr.ExchangeNumber = office.ID;
 Reestr.Emp_Ref        = Session.Emp_Ref;
 Reestr.Sess_Ref       = Session.Number;
 Reestr.Type_Ref       = TypeReestr;
 Reestr.OutCurr_Ref    = OutCurr;
 Reestr.IncomeCurr_Ref = IncCurr;
 Reestr.Rate_Ref       = 0;
 var Loop = GetGE( Reestr );
 While( (Loop == True) AND
        (Reestr.ExchangeNumber  == office.ID)               AND
        (Reestr.Emp_Ref         == Session.Emp_Ref)         AND
        (Reestr.Sess_Ref        == Session.Number )         AND
        ( Reestr.Type_Ref       == TypeReestr )             AND
        ( Reestr.OutCurr_Ref    == OutCurr    )             AND
        ( Reestr.IncomeCurr_Ref == IncCurr    )
 )

    /*курс операции*/
    КурсОперации = Число_Курс( Reestr.Rate, " руб" );

    ПроцентнаяКомиссия = $0.00;
    Тип = "";
    ОбщаяКомиссия = ОбщаяКомиссия + Комиссия( ПроцентнаяКомиссия, Тип );

    if( (KindOper == CDF_OperCurrSale) OR (KindOper == CDF_OperPayDocSale) )
       /*полученная сумма при продаже ( по ЦБ-курсу )*/
       IncomeSumCB     = money( Reestr.CBRate * Reestr.OutSum    );
       /*полученная сумма при продаже (по курсу продажи)*/
       IncomeSum    = money( Reestr.Rate * Reestr.OutSum );
       /*разница между суммами продажи пересчитанными в разных курсах*/
       РазницаПрод  = money( Abs( IncomeSum  - IncomeSumCB ));

       if( KindOper == CDF_OperCurrSale )
          ПроданоВалюты ( КурсОперации,
                          IncomeSumCB,
                          Reestr.OutSum
                        );
       elif( KindOper == CDF_OperPayDocSale )
          ПроданоПД     ( КурсОперации,
                          IncomeSumCB,
                          Reestr.OutSum
                        );

       end;
       КурсПродажи_ОК( РазницаПрод );
       НалогНаПродажу( Reestr.TaxSum );

    elif( (KindOper == CDF_OperCurrBuy) OR (KindOper == CDF_OperPayDocBuy) )
       /*учитываем процентную комиссию в выданной сумме в зависимости от    */
       /*   типа процентной комиссии                                        */
       if  ( Тип == "ОтВыданнойСуммы"   )
          Reestr.OutSum = Reestr.OutSum - ПроцентнаяКомиссия;
       elif( Тип == "ОтПолученнойСуммы" )
          Reestr.OutSum = Reestr.OutSum + Money( ПроцентнаяКомиссия * Reestr.Rate );
       end;
       /*выданная сумма при покупке (по ЦБ-курсу)*/
       OutSumCB  = money( Reestr.CBRate * Reestr.IncomeSum );
       /*разница между суммами покупки пересчитанными в разных курсах*/
       РазницаПок   = money( Abs(OutSumCB - Reestr.OutSum ));

       /*вывод строк с информацией по операциям*/
       if( KindOper == CDF_OperCurrBuy )/*покупка валюты*/
          КупленоВалюты( КурсОперации, Reestr.OutSum, Reestr.IncomeSum );
       elif( KindOper == CDF_OperPayDocBuy )/*покупка платежных документов за рубли*/
          КупленоПД( КурсОперации, Reestr.OutSum, Reestr.IncomeSum );
       end;
       КурсПокупки_ОК( РазницаПок );

    end;

    Loop = Next( Reestr );
 end;
end;

/****************************************************************************/
/* Процедура выводит строки по всем авансам или передачам                   */
/*  тип == "Аванс" или "Передача"                                           */
/****************************************************************************/
macro Авансы_Передачи( КодВалюты, Тип )
  var OutCurr_Ref, IncomeCurr_Ref, TypeOperation, Loop;
  var Зачислено, Приход, Списано, Расход;
  if( тип == "Аванс" )
     OutCurr_Ref    = ANY_CURR;
     IncomeCurr_Ref = КодВалюты;
     TypeOperation = CDF_OperAvance;
  elif( тип == "Передача" )
     OutCurr_Ref    = КодВалюты;
     IncomeCurr_Ref = ANY_CURR;
     TypeOperation = CDF_OperTransfer;
  end;
  Operat.OutCurr_Ref    = OutCurr_Ref;
  Operat.IncomeCurr_Ref = IncomeCurr_Ref;
  Operat.Emp_Ref        = Session.Emp_Ref;
  Operat.Sess_Ref       = Session.Number;;
  Operat.Oper_Ref       = TypeOperation;    /*операция аванса*/
  Operat.State_Ref      = CDF_OperCompleted;/* завершена     */
  Operat.Rate_Ref       = 0;

  Loop = GetEQ( Operat );
  While( (Loop == True) AND
         (Operat.Emp_Ref        == Session.Emp_Ref)         AND
         (Operat.Sess_Ref       == Session.Number)          AND
         (Operat.Oper_Ref       == TypeOperation)           AND
         (Operat.State_Ref      == CDF_OperCompleted)       AND
         (Operat.Rate_Ref       == 0)                       AND
         (Operat.OutCurr_Ref    == OutCurr_Ref)             AND
         (Operat.IncomeCurr_Ref == IncomeCurr_Ref)
       )
     if( TypeOperation == CDF_OperAvance )
        Зачислено = Money(Operat.IncomeAmount * Operat.CBRate);
        Приход    = Operat.IncomeAmount;
        ПолученАвансВалюты( Зачислено, Приход );
     elif( TypeOperation == CDF_OperTransfer )
        Списано = Money(Operat.OutAmount * Operat.CBRate);
        Расход  = Operat.OutAmount;
        ПереданоВалюты( Списано, Расход );
     end;
     Loop = Next( Operat );
  end;
end;

/****************************************************************************/
/* Процедура выводит строку по сводному авансу или передаче                 */
/*  тип == "Аванс" или "Передача"                                           */
/****************************************************************************/
macro Авансы_Передачи_Рублевые( Тип )
  var OutCurr_Ref, IncomeCurr_Ref, TypeOperation, Loop;
  var ПолученнаяСумма, ВыданнаяСумма;
  if( Тип == "Аванс" )
     OutCurr_Ref    = ANY_CURR;
     IncomeCurr_Ref = {NationalCur};
     TypeOperation = CDF_OperAvance;
  elif( Тип == "Передача" )
     IncomeCurr_Ref = ANY_CURR;
     OutCurr_Ref    = {NationalCur};
     TypeOperation = CDF_OperTransfer;
  end;

  Operat.Emp_Ref        = Session.Emp_Ref;
  Operat.Sess_Ref       = Session.Number;
  Operat.Oper_Ref       = TypeOperation;    /* вид операции*/
  Operat.State_Ref      = CDF_OperCompleted;/* завершена     */
  Operat.Rate_Ref       = 0;
  Operat.IncomeCurr_Ref = IncomeCurr_Ref;
  Operat.OutCurr_Ref    = OutCurr_Ref;

  ПолученнаяСумма = $0.00;
  ВыданнаяСумма = $0.00;
  Loop = GetEQ( Operat );
  While( (Loop == True) AND
         (Operat.Emp_Ref        == Session.Emp_Ref)         AND
         (Operat.Sess_Ref       == Session.Number)          AND
         (Operat.Oper_Ref       == TypeOperation)           AND
         (Operat.State_Ref      == CDF_OperCompleted)       AND
         (Operat.Rate_Ref       == 0)                       AND
         (Operat.OutCurr_Ref    == OutCurr_Ref)             AND
         (Operat.IncomeCurr_Ref == IncomeCurr_Ref)
       )
        ПолученнаяСумма = ПолученнаяСумма + Operat.IncomeAmount;
        ВыданнаяСумма   = ВыданнаяСумма   + Operat.OutAmount;

     Loop = Next( Operat );
  end;

  if( (TypeOperation == CDF_OperAvance) AND ПолученнаяСумма )
     ПолученАвансРублевый( ПолученнаяСумма );
  elif( (TypeOperation == CDF_OperTransfer) AND ВыданнаяСумма )
     ПереданоРублевый( ВыданнаяСумма );
  end;

end;
/****************************************************************************/
/* Процедура формирует строку о возврате для данной валюты                  */
/****************************************************************************/
macro ВозвратВалюты( КодВалюты )
  Operat.Emp_Ref        = Session.Emp_Ref;
  Operat.Sess_Ref       = Session.Number;
  Operat.Oper_Ref       = CDF_OperCashOut;  /*операция сдачи кассы*/
  Operat.State_Ref      = CDF_OperCompleted;/* завершена       */
  Operat.Rate_Ref       = 0;
  Operat.OutCurr_Ref    = КодВалюты;
  Operat.IncomeCurr_Ref = ANY_CURR;
  var Loop = GetEQ( Operat );
  var Списано = $0.00; var Расход = $0.00;
  While( (Loop == True) AND
         (Operat.Emp_Ref        == Session.Emp_Ref)         AND
         (Operat.Sess_Ref       == Session.Number )         AND
         (Operat.Oper_Ref       == CDF_OperCashOut)         AND
         (Operat.State_Ref      == CDF_OperCompleted)       AND
         (Operat.Rate_Ref       == 0)                       AND
         (Operat.OutCurr_Ref    == КодВалюты)               AND
         (Operat.IncomeCurr_Ref == ANY_CURR)
       )
     Списано  = Списано + Money(Operat.OutAmount * Operat.CBRate);
     Расход   = Расход + Operat.OutAmount;
     Loop = Next( Operat );
  end;

  if( Расход )
     СданоВалюты( Списано, Расход );
  end;
end;
/****************************************************************************/
/* Процедура формирует строку о рублевом возврате                           */
/****************************************************************************/
macro ВозвратРублевый()
  Operat.Emp_Ref        = Session.Emp_Ref;
  Operat.Sess_Ref       = Session.Number;
  Operat.Oper_Ref       = CDF_OperCashOut;  /*операция сдачи кассы*/
  Operat.State_Ref      = CDF_OperCompleted;/* завершена       */
  Operat.Rate_Ref       = 0;
  Operat.OutCurr_Ref    = {NationalCur};
  Operat.IncomeCurr_Ref = ANY_CURR;
  var Сумма = $0.00;
  var Loop = GetEQ( Operat );
  While( (Loop == True) AND
         (Operat.Emp_Ref        == Session.Emp_Ref  )      AND
         (Operat.Sess_Ref       == Session.Number   )      AND
         (Operat.Oper_Ref       == CDF_OperCashOut  )      AND
         (Operat.State_Ref      == CDF_OperCompleted)      AND
         (Operat.Rate_Ref       == 0                )      AND
         (Operat.OutCurr_Ref    == {NationalCur}    )      AND
         (Operat.IncomeCurr_Ref == ANY_CURR         )
       )
     Сумма = Сумма + Operat.OutAmount;
     Loop = Next( Operat );
  end;
  if( Сумма )
     СданоРублевый( Сумма );
  end;
end;
/****************************************************************************/
/* Процедура печатает суммарные строки по всем валютам в сводный ОД         */
/****************************************************************************/
macro СуммарныеСтрокиПоВалютам()
   var j = 0, Смещение;
   /*печать суммарных строк для каждой валюты*/
   While( j < TotalNumCurr )
      if( DiffCurr(j) != {NationalCur} )
         Смещение = j * КоличествоЭлементов;
         СуммыПоВалюте( ИтогиПоВалютам( Смещение    ), /*код валюты*/
                        ИтогиПоВалютам( Смещение + 1), /*приход    */
                        ИтогиПоВалютам( Смещение + 2), /*расход    */
                        ИтогиПоВалютам( Смещение + 3), /*зачислено */
                        ИтогиПоВалютам( Смещение + 4)  /*списано   */
                      );
      end;
      j = j + 1;
   end;
end;
/****************************************************************************/
/*процедура формирует строки о справках БСО для сводного ОД                 */
/****************************************************************************/
macro Справки0406007()
  var NumGetOutSprv = 0; var NumKillSprv = 0;
  var AllDebetSprv = 0;  var AllKreditSprv = 0;
  var NumAvanceSprv = CalcTransferBSO(Session.Emp_Ref,Session.Number,CDF_OperAvance);
  var NumTransfSprv = CalcTransferBSO(Session.Emp_Ref,Session.Number,CDF_OperTransfer);
  var NumReturnSprv = CalcTransferBSO(Session.Emp_Ref,Session.Number,CDF_OperCashOut);

 /*всего израсходовано справок за смену*/
 var NumDebetSprv = NumAvanceSprv - NumReturnSprv;
 /*печать результатов в ОД*/
 if( NumAvanceSprv )
    ПолученоСправок( NumAvanceSprv );
 end;
 if( NumDebetSprv  )
    ИзрасходованоСправок(  NumDebetSprv );
 end;
 if( NumReturnSprv )
    ВозвращеноСправок( NumReturnSprv );
 end;
end;
/****************************************************************************/
/*  Процедура формирует один операционный дневник для определенной валюты   */
/****************************************************************************/
macro ОперационныйДневник( КодВалюты )
  ОбнулениеИтоговОД();
  ОбщаяКомиссия = $0.00;/*общая сумма комиссии для ОД*/
  ШапкаОперационногоДневника( GetNameCurr( КодВалюты ), GetISOCurr( КодВалюты ) );
  Авансы_Передачи( КодВалюты, "Аванс" );
  Авансы_Передачи( КодВалюты, "Передача" );
  /*продажа валюты за рубли*/
  ИтогиПоОперации( CDF_OperCurrSale,   {NationalCur}, КодВалюты     );
  /*покупка валюты за рубли*/
  ИтогиПоОперации( CDF_OperCurrBuy,    КодВалюты,     {NationalCur} );
  /*продажа платежных документов за рубли*/
  ИтогиПоОперации( CDF_OperPayDocSale, {NationalCur}, КодВалюты     );
  /*покупка платежных документов за рубли*/
  ИтогиПоОперации( CDF_OperPayDocBuy,  КодВалюты,     {NationalCur} );
  if( ОбщаяКомиссия )
     КомиссионныйСбор( ОбщаяКомиссия );
  end;
  ВозвратВалюты( КодВалюты );

  СтрокаИтогов();
  ДноОперационногоДневника();

  ЗаполнениеИтоговПоВалютам( КодВалюты );
end;

/****************************************************************************/
/*  Процедура формирует сводный операционный дневник (рублевый)             */
/****************************************************************************/
macro СводныйОперационныйДневник()
  ОбнулениеИтоговОД();
  ШапкаОперационногоДневника( "Сводный(рублевый)", "шт." );
  Справки0406007();
  Авансы_Передачи_Рублевые( "Аванс"    );
  Авансы_Передачи_Рублевые( "Передача" );
  СуммарныеСтрокиПоВалютам();
  ВозвратРублевый();

  /*КомиссионныйСбор( Комиссия );           */
  СтрокаИтогов();
  ДноОперационногоДневника();
end;
/****************************************************************************/
/*  Процедура выводит в файл отчета все операционные дневники за смену      */
/*  по образцу от Белгородского СБ                                          */
/****************************************************************************/
macro ОперационныйДневник_1()
   TotalNumCurr = 0;
   НомерВалюты  = 0;
   /* Настройки для формирования операционных дневников ************************/
   /*  !!!!! шапка и дно дневника переделываются вручную                       */
     ШиринаКолонок    = 15; /* ширина колонок в операционном дневнике          */
     МаксимумКолонок  = 8;  /* максимальное количество колонок                 */
     НачальнаяКолонка = 3;  /* начальная колонка для вывода чисел              */
     ШиринаНазвания   = 38; /* ширина графы для названия операции              */
   /****************************************************************************/

   /*создаем и открываем файл отчета*/
   MakeReportFile( ReportFile, "sbr_f24" );
   /*заполняем массив валют: DiffCurr*/
   ЗаполнитьМассивВалют();
   /*базовую валюту(доллары) перенести на первое место*/
   MoveElementArray( DiffCurr, {BaseCur}, 0, TotalNumCurr );
   /*Формирование валютных операционных дневников*/
   While( НомерВалюты < TotalNumCurr )
      if( DiffCurr(НомерВалюты) != {NationalCur} )
         ОперационныйДневник( DiffCurr(НомерВалюты) );
      end;
      НомерВалюты = НомерВалюты + 1;
   end;
   if( TotalNumCurr > 0 )
      СводныйОперационныйДневник();/*сводный(рублевый) операционный дневник*/
   end;
   /*смотрим файл отчета*/
   SetOutPut( 0 );
   ViewFile( ReportFile );
   Close( ReportFile );
end;

