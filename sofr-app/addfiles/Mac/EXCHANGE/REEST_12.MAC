/*-RSL---------------------------------------- R-Style Software Lab --*/
/*--------------- Печать реестра -------------------------------------*/
/* RS-Bank      подсистема Обменный пункт            AV  14.05.97     */

import ExchangeInter, "rstrfunc.mac", "string.mac", "regnumb.mac";

var FirstRecord = True;  /* VS Печатаем первый! реестр */
var PrintedAll  = False; /* VS Печатаем ВСЕ реестры    */

if( Office.MergeReestrs ) ACCUMULATE_REESTR = True;  end;


MACRO ПечатьШапки()
   var ВремяКурс;
   var ValFlag = GetRegVal( "RS-RETAIL\\INI\\ПЕРЕМЕННЫЕ\\ОБМЕННЫЙ_ПУНКТ\\SETTIMERATE" );
   if( ValFlag != "OFF" )
      ВремяКурс = ValFlag;
   else
      ВремяКурс = Реестр.RateStartTime;
   end;

[
                                 РЕЕСТР
          ПЛАТЕЖНЫХ ДОКУМЕНТОВ В ИНОСТРАННОЙ ВАЛЮТЕ, ПРОДАННЫХ
               ЗА СЧЕТ СРЕДСТВ НА РУБЛЕВОМ СЧЕТЕ КЛИЕНТА
];
if( PRINT_CURRENCY == TRUE )
[
        Валюта:  #
]( GetNameCurr( FindCodCurByRefVal(Реестр.OutRefVal) ) );
end;
[
    Курс продажи на текущую дату ##################  ##########  ########
          (указывается время в часах и минутах, с которого действует
                   установленный банком курс продажи)

        Курс Банка России на текущую дату ##################

+--------------------------------+---+-----------+---+-----------+-------------------------+----------+--------------+----------+--------+--------+
|    Наименование платежного     |КОД|   Сумма   |КОД|   Сумма   |  Рублевый счет клиента  | N справки|  КОД, сумма  |Резидент\ | Сумма  | Сумма  |
|          документа             |   | проданного|   | наличных  |                         | ф.0406007| комиссионного|Нерезидент| налога | подо-  |
|                                |ва-| платежного|на-|  рублей,  |                         |          |вознаграждения|          |        | ход-   |
|                                |лю-| документа |лич|полученных |                         |          |              |          | (циф-  | ного   |
|                                |ты | в ин. вал.|ных|за пл. док.|                         |          |              |          | рами)  | налога |
|                                |   | за рубли  |   |в ин. вал. |                         |          |              |          |        | (циф-  |
|                                |ПД | (цифрами) |руб| (цифрами) |                         |          |              |          |        | рами)  |
+--------------------------------+---+-----------+---+-----------+-------------------------+----------+--------------+----------+--------+--------+
|               1                | 2 |     3     | 4 |     5     |            6            |    7     |       8      |    9     |   10   |   11   |
+--------------------------------+---+-----------+---+-----------+-------------------------+----------+--------------+----------+--------+--------+
] ( КурсСНужнойТочностью( Реестр.Rate,   FindCodCurByRefVal(Реестр.OutRefVal) ):r,
    StrAddZeroDate_Short( Реестр.RateStartDate ),
    ВремяКурс,
    КурсСНужнойТочностью( Реестр.CBRate, FindCodCurByRefVal(Реестр.OutRefVal) )
  );
END;

MACRO ПечатьПодвала()
  var OutSum    = Реестр.OutSum,
      IncomeSum = SumConv( Реестр.OutSum, Реестр.Rate ),
      TaxSum    = Реестр.TaxSum,
      IncTaxSum = Реестр.IncTaxSum;

  if( ACCUMULATE_REESTR or PrintedAll )
    OutSum    = TotalsPage( 0 );
    IncomeSum = TotalsPage( 1 );
    TaxSum    = TotalsPage( 3 );
    IncTaxSum = TotalsPage( 4 );
  end;

[+--------------------------------+---+-----------+---+-----------+-------------------------+----------+--------------+----------+--------+--------+
Итого:                                ###########     ###########                           ##########                           ######## ########
                                                                                                                                 
ИТОГО:                                ###########     ###########                           ########## ##############            ######## ########

                                                                           
        Кассир обменного пункта: _________________________ (###############################)
                                        (подпись)


---------------------------------------------------------------------------------------------------------------------------------------------------
#
](
    TotalsPage( 0 ),
    TotalsPage( 1 ),
    TotalsPage( 2 ):c,
    TotalsPage( 3 ),
    TotalsPage( 4 ),
    OutSum,
    IncomeSum,
    TotalsPage( 6 ):c,
    MakeStrReestrComiss():w,
    TaxSum,
    IncTaxSum,
    Реестр.FIO,
    StrFor(12)
  );
END;

MACRO ПечатьПодвала_ОдностраничныйРеестр()
  var OutSum    = Реестр.OutSum,
      IncomeSum = SumConv( Реестр.OutSum, Реестр.Rate ),
      TaxSum    = Реестр.TaxSum,
      IncTaxSum = Реестр.IncTaxSum;

  if( ACCUMULATE_REESTR or PrintedAll )
    OutSum    = TotalsPage( 0 );
    IncomeSum = TotalsPage( 1 );
    TaxSum    = TotalsPage( 3 );
    IncTaxSum = TotalsPage( 4 );
  end;

[+--------------------------------+---+-----------+---+-----------+-------------------------+----------+--------------+----------+--------+--------+
ИТОГО:                                ###########     ###########                           ########## ##############            ######## ########


        Кассир обменного пункта: _________________________ (###############################)
                                        (подпись)


---------------------------------------------------------------------------------------------------------------------------------------------------
#
](
    OutSum,
    IncomeSum,
    TotalsPage( 6 ):c,
    MakeStrReestrComiss():w,
    TaxSum,
    IncTaxSum,
    Реестр.FIO,
    StrFor(12)
  );
END;


MACRO ПечатьПодвалаСтраницы()
[+--------------------------------+---+-----------+---+-----------+-------------------------+----------+--------------+----------+--------+--------+
Итого:                                ###########     ###########                           ##########                           ######## ########


        Кассир обменного пункта: _________________________ (###############################)
                                        (подпись)


---------------------------------------------------------------------------------------------------------------------------------------------------
#

](
    TotalsPage( 0 ),
    TotalsPage( 1 ),
    TotalsPage( 2 ):c,
    TotalsPage( 3 ),
    TotalsPage( 4 ),
    Реестр.FIO,
    StrFor(12)
  );
END;

MACRO ПечатьСтрокиРеестра()

  var Seria, Number;
  GetSprvSerNum( СоставРеестра.ApplicationKind, 
                 СоставРеестра.ApplicationKey, 
                 GetBSOShortName (), Seria, Number );
  Number = string(Number);
  Number = StrFillBeg( Number, "0", 7 ); /* дополнение ведущими нулями */

  СоставРеестра.IncomeAmount = SumConv( СоставРеестра.OutAmount, СоставРеестра.Kurs );

[|################################|###|###########|###|###########|#########################|## #######|##############|##########|########|########|]
(
  (GetNamePayDoc( paydocop.PayDoc_Ref ) +
  "  " + ПолучитьСтрокуСерийНомеров( СоставРеестра.ApplicationKind, 
                                     СоставРеестра.ApplicationKey, 
                                     СоставРеестра.OutRefVal, 
                                     CASHOP_DEBIT /* Расход */
                                   ) ):w,
  GetCurrNumericalCode( FindCodCurByRefVal(Реестр.OutRefVal) ),
  СоставРеестра.OutAmount,
  GetCurrNumericalCode( FindCodCurByRefVal(Реестр.IncomeRefVal) ),
  СоставРеестра.IncomeAmount,
  ПолучитьВкладнойСчетДляВОО( СоставРеестра.ApplicationKind, СоставРеестра.ApplicationKey ):c,
  Seria,
  Number,
  MakeComissString():w,
  MakeResidentName(СоставРеестра.Resident):c,
  СоставРеестра.TaxValue,
  СоставРеестра.IncTaxValue
);
 AddTotalsPage( 0, СоставРеестра.OutAmount    );
 AddTotalsPage( 1, СоставРеестра.IncomeAmount );
 AddTotalsPage( 3, СоставРеестра.TaxValue     );
 AddTotalsPage( 4, СоставРеестра.IncTaxValue  );
 if( Seria != "" )
   AddTotalsPage( 2, 1 );/* справки - страница    */
   AddTotalsPage( 6, 1 );/* справки - весь реестр */
 end;

END;

/*-------------------------------------------------------------------------*/
/* рекурсивная функция - печатает строки реестра с разбиением на страницы  */
/*-------------------------------------------------------------------------*/
MACRO СодержимоеРеестраПостранично_Рекурсия( NumPage, NumberStrings )
  var LoopSetRstr = True;

  if( FirstRecord )
    ОбщаяШапка( Реестр.Date, Реестр.UserRegNumb, NumPage );
    ПечатьШапки();
    FirstRecord = False;
  end;

  while( (LoopSetRstr == True) AND (ФильтрРеестр() == True) )
    NumberStrings = NumberStrings + 1;
    /* наложение структуры paydocop на поле СоставРеестра.ExtInfo */
    SetRecordAddr( paydocop, СоставРеестра, 0, FldOffset( СоставРеестра,"ExtInfo"), True );
    if( ФильтрСтрокаРеестра() )
      ПечатьСтрокиРеестра();
    end;
    LoopSetRstr = next( СоставРеестра );
    if( (LoopSetRstr == True) AND (ФильтрРеестр() == True)
        AND ( (NumberStrings/NumPage) == MaxNumberStrings)
      )
       ПечатьПодвалаСтраницы();
       NumPage = NumPage + 1;
       ClearTotalsPage( 6 );
       LoopSetRstr = СодержимоеРеестраПостранично_Рекурсия( NumPage, NumberStrings );
    end;
  end;
  return LoopSetRstr;
END;
/*-------------------------------------------------------------------------*/
/* Вызов рекурсии для печати строк реестра с разбиением на страницы        */
/*-------------------------------------------------------------------------*/
MACRO СодержимоеРеестраПостранично( NumPage, NumberStrings )
   if( InitSetRstrKey() )
      SetRegNumb( Реестр ); /* Заполним Реестр.UserRegNumb, если он пустой */
      СодержимоеРеестраПостранично_Рекурсия( NumPage, NumberStrings );
   end;
   SetParm( 0, NumPage ); SetParm( 1, NumberStrings );
END;

/* 26.09.2000 VS Функции обеспечения аккумулирования реестров по ПД. */
MACRO FilterOnTypeReestr()
  return ( (Реестр.Branch       == r.Branch       ) /* N ОП в банке            */
       and (Реестр.CurDate      == r.CurDate      ) /* Дата опердня            */
       and (Реестр.Oper         == r.Oper         ) /* Код сотрудника          */
       and (Реестр.Kind_Oper    == r.Kind_Oper    ) /* Тип реестра             */
       and (Реестр.OutRefVal    == r.OutRefVal    ) /* Выданная валюта         */
       and (Реестр.IncomeRefVal == r.IncomeRefVal ) /* Полученная валюта       */
         );
END;

/* Найти первый реестр этого типа этого опера в этой смене */
MACRO TakeThisTypeFirstReestr()
  ClearRecord( Реестр );
  Реестр.Branch       = r.Branch;           /* N ОП в банке            */
  Реестр.CurDate      = r.CurDate;          /* Дата опердня            */
  Реестр.Oper         = r.Oper;             /* Код сотрудника          */
  Реестр.Kind_Oper    = r.Kind_Oper;        /* Тип реестра             */
  Реестр.OutRefVal    = r.OutRefVal;        /* Выданная валюта         */
  Реестр.IncomeRefVal = r.IncomeRefVal;     /* Полученная валюта       */

  return (getGE( Реестр ) and FilterOnTypeReestr());
END;

MACRO NextReestr( r )
  var RetVal;

  if( FirstRecord ) /* Это самый первый вход */
     /*FirstRecord = False;*/ /* !!! Назначим после печати первого реестра. */
     if( ACCUMULATE_REESTR )
        /* Найти первый реестр этого типа этого опера в этой смене */
        RetVal = TakeThisTypeFirstReestr();
     else
        RetVal = FindReestr( r );
     end;
  else /* а иначе рекурсия, и печать второго и далее в одну бумагу,*/
     if( ACCUMULATE_REESTR )  /* если, конечно, это задано. */
        RetVal = (next( Реестр ) and FilterOnTypeReestr());
     else
        RetVal = False;
     end;
  end;
  return RetVal;
END;

MACRO PrintFooter( НомерСтраницы )
   if( НомерСтраницы > 1 )
      ПечатьПодвала();
   else
      ПечатьПодвала_ОдностраничныйРеестр();
   end;
END;

/*-------------------------------------------------------------------------*/
/*    печать реестра данного типа для одной пары валют                     */
/*-------------------------------------------------------------------------*/
MACRO ПечатьОдногоРеестра( Reestr )
   var НомерСтраницы = 1;
   var НомерСтроки   = 0;

   ClearTotalsPage( 7 );/* вначале обнуляем весь массив */
   setbuff( r, Reestr );
   asize( ArSum,  0 ); /* суммы комиссий  */
   asize( ArCurr, 0 ); /* валюты комиссий */

   /*if( FindReestr( r ))*/
   /* 26.09.2000 VS Сделал while для объединения разных реестров в один при печати. */
   while( NextReestr( r ) )
      СодержимоеРеестраПостранично( НомерСтраницы, НомерСтроки );/* рекурсия */
      /* 26.09.2000 VS Для подсчета комиссий. */
      if( ACCUMULATE_REESTR )
         GetSumReestrComiss();
      end;
   end;
   PrintFooter( НомерСтраницы );

END;


/*-------------------------------------------------------------------------*/
/*    печать всех реестров данного типа                                    */
/*-------------------------------------------------------------------------*/
MACRO ПечатьВсехРеестров( Kind_Oper )
   var НомерСтраницы = 1;
   var НомерСтроки   = 0;
   var LoopRstr      = True;
   var CurCur        = 0;   /* VS Текущая валюта. Для аккумулированных реестров. */

   if( InitReestrKey( Kind_Oper ) )
      PrintedAll = True;
      PrintDate_n_Time();

      while( LoopRstr and ФильтрШапки( Kind_Oper ) )
         if( not ACCUMULATE_REESTR or 
             (ACCUMULATE_REESTR and (Реестр.OutRefVal != CurCur)) )
            if( not ACCUMULATE_REESTR and CurCur ) 
               prev( Реестр ); /* !!! Только для подсчета комиссий по оборотам реестра в MakeStrReestrComiss. */
               PrintFooter( НомерСтраницы );
               next( Реестр );
            end;
            ClearTotalsPage( 7 );/* вначале обнуляем весь массив */
            FirstRecord = True;
            CurCur = Реестр.OutRefVal; /* !!! OutRefVal -> OutCurr_Ref RA 13-07-01 */
            asize( ArSum , 0 ); 
            asize( ArCurr, 0 );
         end;
         СодержимоеРеестраПостранично( НомерСтраницы, НомерСтроки );/* рекурсия */
         /* 26.09.2000 VS Для подсчета комиссий по разным реестрам в одних массивах. */
         if( ACCUMULATE_REESTR )
            GetSumReestrComiss();
         end;
         LoopRstr = next( Реестр );
      end;

      if( not ACCUMULATE_REESTR ) prev( Реестр ); end;
      PrintFooter( НомерСтраницы );
   end;

END;

