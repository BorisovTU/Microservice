/*-RSL---------------------------------------- R-Style Software Lab --*/
/*-------------- Ведомость учета подоходного налога ------------------*/
/* RS-Bank        Обменный пункт                       RA 30-04-99    */
/* Сделано на основе taxlist.mac (теперь tax.mac)                     */

/*шапка реестра ведомости учета налога (в разрезе кода и смены курсов)*/

MACRO ПечатьШапкиПодохНалогаСПок( DateReestr, NumReestr, NumPage )
[
  ___________#_____________            __________________#_____________________
  (дата составления отчета)            (регистрационный номер обменного пункта)

  Курс ЦБ РФ   #

  Курс покупки ##################

                  ВЕДОМОСТЬ УЧЕТА ПОДОХОДНОГО НАЛОГА
    НА ПРОДАЖУ ИНОСТРАННЫХ ДЕНЕЖНЫХ ЗНАКОВ И ПЛАТЕЖНЫХ ДОКУМЕНТОВ,
                   ВЫРАЖЕННЫХ В ИНОСТРАННОЙ ВАЛЮТЕ
                       Смена за  #
(### стр.###)
+----+-------------------------+----------+----------------------+------------------+------------------+
|Nп/п|         Клиент          |N справки |    Сумма продажи     | Налогооблагаемая |   Сумма налога   |
|    | Резидент Ф.И.О. Документ|ф. 0406007|  иностранной валюты  |   база (руб.)    |                  |
+----+-------------------------+----------+----------------------+------------------+------------------+
](
  StrAddZeroDate( date ):c,
  Office.RegNumb:c,
  Реестр.CBRate:l,
  Реестр.Rate:l,
  StrAddZeroDate( ReportDate ),
  GetCurrNumericalCode( FindCodCurByRefVal(СоставРеестра.IncomeRefVal) ),
  NumPage:c
);
END;

MACRO ПечатьШапкиПодохНалогаСПрод( DateReestr, NumReestr, NumPage )
[
  ___________#_____________            __________________#_____________________
  (дата составления отчета)            (регистрационный номер обменного пункта)

  Курс ЦБ РФ   #

  Курс продажи ##################

                  ВЕДОМОСТЬ УЧЕТА ПОДОХОДНОГО НАЛОГА
    НА ПОКУПКУ ИНОСТРАННЫХ ДЕНЕЖНЫХ ЗНАКОВ И ПЛАТЕЖНЫХ ДОКУМЕНТОВ,
                   ВЫРАЖЕННЫХ В ИНОСТРАННОЙ ВАЛЮТЕ
                       Смена за  #
(### стр.###)
+----+-------------------------+----------+----------------------+------------------+------------------+
|Nп/п|         Клиент          |N справки |    Сумма покупки     | Налогооблагаемая |   Сумма налога   |
|    | Резидент Ф.И.О. Документ|ф. 0406007|  иностранной валюты  |   база (руб.)    |                  |
+----+-------------------------+----------+----------------------+------------------+------------------+
](
  StrAddZeroDate( date ):c,
  Office.RegNumb:c,
  Реестр.CBRate:l,
  Реестр.Rate:l,
  StrAddZeroDate( ReportDate ),
  GetCurrNumericalCode( FindCodCurByRefVal(СоставРеестра.OutRefVal) ),
  NumPage:c
);
END;

MACRO ПечатьШапкиПодохНалога( DateReestr, NumReestr, NumPage )

  if(   ( СоставРеестра.Kind_Oper == CDF_OperPayDocBuy    )  OR
        ( СоставРеестра.Kind_Oper == CDF_OperPayDocBuyAcc )  OR
        ( СоставРеестра.Kind_Oper == CDF_OperCurrBuy    ) )
    ПечатьШапкиПодохНалогаСПок( DateReestr, NumReestr, NumPage )  /* Покупка */

  elif( ( СоставРеестра.Kind_Oper == CDF_OperPayDocSale    ) OR
        ( СоставРеестра.Kind_Oper == CDF_OperPayDocSaleAcc ) OR
        ( СоставРеестра.Kind_Oper == CDF_OperCurrSale    ) )
    ПечатьШапкиПодохНалогаСПрод( DateReestr, NumReestr, NumPage ) /* Продажа */
  end;
END;

/* Расчет налогооблагаемой базы */
MACRO GetIncTaxBase( Op )
  return Op.IncTaxBase;
  /*
  var ПроцентНалога;

  if(Op.BaseIncTaxValue != 0.) /* Если налогооблагаемая база сохранялась при совершении операции */
    return Op.BaseIncTaxValue;
  else /* Для старых версий ОП - расчет налогоблагаемой базы на основе суммы налога */
    if( Op.Resident == SET_CHAR )
       ПроцентНалога = Office_Add.IncTaxResident;
    else
       ПроцентНалога = Office_Add.IncTaxNonResident;
    end;
    return money( Op.IncTaxValue / ПроцентНалога * 100 );
  end;
  */
END;

/*-------------------------------------------------------------------------*/
/*-------------------------------------------------------------------------*/
MACRO ПечатьПодвалаПодохНалога( )
[+----+-------------------------+----------+----------------------+------------------+------------------+

ИТОГО:                          ########## ###################### ################## ##################


        Кассир обменного пункта: _________________________ (#                     )
                                        (подпись)

--------------------------------------------------------------------------------------------------------------------------------------
#
](
   TotalsPage( 4 ):c,  TotalsPage( 5 ), TotalsPage( 6 ), TotalsPage( 7 ),
   Реестр.FIO,
   StrFor(12)
 );
END;

/*-------------------------------------------------------------------------*/
/*-------------------------------------------------------------------------*/
MACRO ПечатьПодвалаСтраницыПодохНалога( )
[+----+-------------------------+----------+----------------------+------------------+------------------+
Итого:                          ########## ###################### ################## ##################

        Кассир обменного пункта: _________________________ (#                     )
                                        (подпись)

--------------------------------------------------------------------------------------------------------------------------------------


](
   TotalsPage( 0 ):c,  TotalsPage( 1 ), TotalsPage( 2 ), TotalsPage( 3 ),
   Реестр.FIO
 );
END;


MACRO ПечатьСтрокиРеестраПодохНалог()

  var Seria, Number;
  var СуммаОперации, ВалютаОперации;

  var ClientInfo = СоставРеестра.Resident                     + " " +
                   СоставРеестра.Sname                        + " " +
                   СоставРеестра.Name                         + " " +
                   СоставРеестра.Pname;
  if( СоставРеестра.ClientDocNumber or СоставРеестра.ClientDocSeria )
    ClientInfo = ClientInfo                                   + " " +
                 PaperTypeName( СоставРеестра.ClientDocType ) + " " +
                 СоставРеестра.ClientDocSeria                 + " " +
                 СоставРеестра.ClientDocNumber;
  end;

  if(   ( СоставРеестра.Kind_Oper == CDF_OperPayDocBuy    )  OR
        ( СоставРеестра.Kind_Oper == CDF_OperPayDocBuyAcc )  OR
        ( СоставРеестра.Kind_Oper == CDF_OperCurrBuy    ) )
      СуммаОперации  = СоставРеестра.IncomeAmount;     /* Покупка */
      ВалютаОперации = GetCurrNumericalCode( FindCodCurByRefVal(СоставРеестра.IncomeRefVal) );

  elif( ( СоставРеестра.Kind_Oper == CDF_OperPayDocSale    ) OR
        ( СоставРеестра.Kind_Oper == CDF_OperPayDocSaleAcc ) OR
        ( СоставРеестра.Kind_Oper == CDF_OperCurrSale    ) )
      СуммаОперации  = СоставРеестра.OutAmount;        /* Продажа */
      ВалютаОперации = GetCurrNumericalCode( FindCodCurByRefVal(СоставРеестра.OutRefVal) );
  end;
  GetSprvSerNum( СоставРеестра.ApplicationKind, 
                 СоставРеестра.ApplicationKey, 
                 GetBSOShortName (), Seria, Number );
  Number = string(Number);
  Number = StrFillBeg( Number, "0", 7 ); /* дополнение ведущими нулями */

[|####|#########################|## #######|### ##################|##################|##################|]
(
  TotalsPage(8):c,
  /*СоставРеестра.DocInfo:w,*/  /* 28.08.2000 VS Добавил резидентность и данные документа */
  ClientInfo:w,
  Seria, 
  Number,
  ВалютаОперации,
  СуммаОперации,
  GetIncTaxBase(СоставРеестра),
  СоставРеестра.IncTaxValue
);
 /* подсчет итогов по странице */
 AddTotalsPage( 0, 1 );/* справки на странице */
 AddTotalsPage( 1, СуммаОперации                );
 AddTotalsPage( 2, GetIncTaxBase(СоставРеестра) );
 AddTotalsPage( 3, СоставРеестра.IncTaxValue    );
 /* подсчет итогов по всей ведомости */
 AddTotalsPage( 4, 1 );/* справки по всей ведомости */
 AddTotalsPage( 5, СуммаОперации                );
 AddTotalsPage( 6, GetIncTaxBase(СоставРеестра) );
 AddTotalsPage( 7, СоставРеестра.IncTaxValue    );
 AddTotalsPage( 8, 1 );/* порядковый номер записи */
END;

/*-------------------------------------------------------------------------*/
/* рекурсивная функция - печатает строки реестра с разбиением на страницы  */
/*-------------------------------------------------------------------------*/
MACRO СодержимоеРеестраПодохНалогПостранично_Рекурсия( NumPage, NumberStrings )
  /*ОбщаяШапка( Реестр.Date, Реестр.UserRegNumb, NumPage );*/
  ПечатьШапкиПодохНалога( Реестр.Date, Реестр.UserRegNumb, NumPage );
  var LoopSetRstr = True;
  while( (LoopSetRstr == True) and (ФильтрРеестр() == True) )
    NumberStrings = NumberStrings + 1;
    if( ФильтрСтрокаРеестра() AND СоставРеестра.IncTaxValue )/* если есть подох. налог по операции и операция не удалена */
       ПечатьСтрокиРеестраПодохНалог();
    end;
    LoopSetRstr = next( СоставРеестра );
    if( (LoopSetRstr == True) AND (ФильтрРеестр() == True) AND ( (NumberStrings/NumPage) == MaxNumberTaxStrings) )
       ПечатьПодвалаСтраницыПодохНалога();
       NumPage = NumPage + 1;
       ClearTotalsPage( 4 );
       LoopSetRstr = СодержимоеРеестраПодохНалогПостранично_Рекурсия( NumPage, NumberStrings );
    end;
  end;
  return LoopSetRstr;
END;
/*-------------------------------------------------------------------------*/
/* Вызов рекурсии для печати строк реестра с разбиением на страницы        */
/*-------------------------------------------------------------------------*/
MACRO СодержимоеРеестраПодохНалогПостранично( NumPage, NumberStrings )
   if( InitSetRstrKey() )
      СодержимоеРеестраПодохНалогПостранично_Рекурсия( NumPage, NumberStrings );
   end;
   SetParm( 0, NumPage ); SetParm( 1, NumberStrings );
END;
/*-------------------------------------------------------------------------*/
/*    печать ведомости учета налогов по одному реестру                     */
/*-------------------------------------------------------------------------*/
MACRO ПечатьОдногоРеестраПодохНалог( Reestr )
   var НомерСтраницы = 1;
   var НомерСтроки   = 0;
   ClearTotalsPage( 9 );/* вначале обнуляем весь массив */
   TotalsPage( 8 ) = 1; /* номер записи в ведомости     */
   setbuff( r, Reestr );
   if( FindReestr( r ))
       СодержимоеРеестраПодохНалогПостранично( НомерСтраницы, НомерСтроки );
       ПечатьПодвалаПодохНалога();
   end;
END;

/*-------------------------------------------------------------------------*/
/*    печать полной ведомости учета налогов за смену                       */
/*-------------------------------------------------------------------------*/
MACRO ПечатьПодохНалоговойВедомостиДляТипаРеестра( Kind_Oper )
   var НомерСтраницы = 1;
   var НомерСтроки   = 0;
   var LoopRstr      = True;
   ClearTotalsPage( 9 );/* вначале обнуляем весь массив */
   TotalsPage( 8 ) = 1; /* номер записи в ведомости     */
   if( ИнициализацияРеестров_НалогВедомость( Kind_Oper ) )
     while( LoopRstr and ФильтрШапки_НалогВедомость( Kind_Oper ) )
       if( Реестр.IncTaxSum != 0.0 )
          СодержимоеРеестраПодохНалогПостранично( НомерСтраницы, НомерСтроки );/*рекурсия*/
          ПечатьПодвалаПодохНалога();
       end;
       LoopRstr =  next( Реестр );
       ClearTotalsPage( 9 );/* обнуляем весь массив     */
       TotalsPage( 8 ) = 1; /* номер записи в ведомости */
     end;
   end;
END;

/* генерация полной ведомости по учету налогов */
MACRO IncTaxList()
      PrintDate_n_Time();
      /* продажа наличной ин.валюты физическим лицам */
      ПечатьПодохНалоговойВедомостиДляТипаРеестра( CDF_OperCurrSale      );
      /* продажа платежных документов в ин.валюте физическим лицам */
      ПечатьПодохНалоговойВедомостиДляТипаРеестра( CDF_OperPayDocSale    );
      /* продажа платежных документов в ин.валюте физическим лицам через счет */
      ПечатьПодохНалоговойВедомостиДляТипаРеестра( CDF_OperPayDocSaleAcc );
      /* покупка наличной ин.валюты у физических лиц */
      ПечатьПодохНалоговойВедомостиДляТипаРеестра( CDF_OperCurrBuy       );
      /* покупка платежных документов в ин.валюте у физических лиц */
      ПечатьПодохНалоговойВедомостиДляТипаРеестра( CDF_OperPayDocBuy     );
      /* покупка платежных документов в ин.валюте у физических лиц через счет */
      ПечатьПодохНалоговойВедомостиДляТипаРеестра( CDF_OperPayDocBuyAcc  );
END;
