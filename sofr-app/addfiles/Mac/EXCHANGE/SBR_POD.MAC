/****************************************************************************
   RS-Bank        Обменный пункт
   sbr_pod.mac - макрос формирует приложение к операционным дневникам ОП
   для отчетности СБ (форма 25)
   Copyright 1998 (c) R-Style Software Lab.
   Создание: 19-05-98 by AV
             15-10-98 AV добавил учет кол-ва всех кассовых операций ОП
             29-12-98 RA добавил учет операций с начала квартала и года
   25.05.2000 VS Убран раздел V по сообщению Гевчука от 25.05.2000.

  ПриложениеОД() - стандартное название функции, формирующей приложение к ОД
                   Форма N25-a
****************************************************************************/

FILE   SessE   ("session.dbt","exchange.def") key 1; /* по операционисту */
Record PanelChoise("SBR_POD", "exchange.lbr") dialog;

/* Массивы с количеством операций покупки продажи по каждой валюте*/
/*   заполняются в той же последовательности, что и массив валют  */
/* Количество покупок - Количество продаж */
ARRAY NumOperYear; /* с начала года на начало квартала, не включая квартал  */
ARRAY NumOperQuar; /* с начала квартала на начало месяца, не включая месяц */
ARRAY NumOperMonth;/* с начала месяца на начало дня, не включая день */
ARRAY NumOperDay;  /* за текущий день   */
/* Массив со всеми валютами, использованными в операциях за месяц */
ARRAY DiffAllCurr;
/* всего видов операций в массиве */
CONST NumKindOper = 5;
/* номера видов операций в массиве */
CONST OperBuy    = 0,/* покупка валюты */
      OperSale   = 1,/* продажа валюты */
      OperConv   = 2,/* конверсия  */
      OperPDBuy  = 3,/* покупка ПД */
      OperPDSale = 4;/* продажа ПД */

   /* глобализм, показывает есть или нет долларовых операций за смену */
VAR ДолларыЕсть = False;

VAR
   OperToBeginQuar= 0,
   OperToBeginMon = 0,
   OperToBeginDay = 0,/* всего операций к началу дня */
          OperDay = 0,/* всего операций за день      */
   ChangeUnPayDay = 0,/* всего операций замены неплатежных за день  */
   ChangeUnPayMon = 0,/* всего операций замены неплатежных за месяц */
   ChangeUnPayQuar= 0,/* всего операций замены неплатежных за квартал */
   ChangeUnPayYear= 0,/* всего операций замены неплатежных за год     */
      BuyUnPayDay = 0,/* всего операций покупки неплатежных за день  */
      BuyUnPayMon = 0,/* всего операций покупки неплатежных за месяц */
      BuyUnPayQuar= 0,/* всего операций покупки неплатежных за квартал */
      BuyUnPayYear= 0,/* всего операций покупки неплатежных за год     */
         SplitDay = 0,/* всего операций размена валюты за день  */
         SplitMon = 0,/* всего операций размена валюты за месяц */
         SplitQuar= 0,/* всего операций размена валюты за квартал */
         SplitYear= 0,/* всего операций размена валюты за год     */
       IncassoDay = 0,/* всего операций приема на инкассо за день  */
       IncassoMon = 0,/* всего операций приема на инкассо за месяц */
       IncassoQuar= 0,/* всего операций приема на инкассо за квартал */
       IncassoYear= 0,/* всего операций приема на инкассо за год     */
    RetIncassoDay = 0,/* всего операций возврата с инкассо за день  */
    RetIncassoMon = 0,/* всего операций возврата с инкассо за месяц */
    RetIncassoQuar= 0,/* всего операций возврата с инкассо за квартал */
    RetIncassoYear= 0,/* всего операций возврата с инкассо за год     */
        ExpertDay = 0,/* всего операций приема на инкассо за день  */
        ExpertMon = 0,/* всего операций приема на инкассо за месяц */
        ExpertQuar= 0,/* всего операций приема на инкассо за квартал */
        ExpertYear= 0,/* всего операций приема на инкассо за год     */
     RetExpertDay = 0,/* всего операций возврата с инкассо за день  */
     RetExpertMon = 0,/* всего операций возврата с инкассо за месяц */
     RetExpertQuar= 0,/* всего операций возврата с инкассо за квартал */
     RetExpertYear= 0,/* всего операций возврата с инкассо за год     */
     IncassoPDDay = 0,/* всего операций приема ПД на инкассо за день  */
     IncassoPDMon = 0,/* всего операций приема ПД на инкассо за месяц */
     IncassoPDQuar= 0,/* всего операций приема ПД на инкассо за квартал */
     IncassoPDYear= 0,/* всего операций приема ПД на инкассо за год     */
  RetIncassoPDDay = 0,/* всего операций возврата ПД с инкассо за день  */
  RetIncassoPDMon = 0,/* всего операций возврата ПД с инкассо за месяц */
  RetIncassoPDQuar= 0,/* всего операций возврата ПД с инкассо за квартал */
  RetIncassoPDYear= 0,/* всего операций возврата ПД с инкассо за год     */
      ExpertPDDay = 0,/* всего операций приема ПД на инкассо за день  */
      ExpertPDMon = 0,/* всего операций приема ПД на инкассо за месяц */
      ExpertPDQuar= 0,/* всего операций приема ПД на инкассо за квартал */
      ExpertPDYear= 0,/* всего операций приема ПД на инкассо за год     */
   RetExpertPDDay = 0,/* всего операций возврата ПД с инкассо за день  */
   RetExpertPDMon = 0,/* всего операций возврата ПД с инкассо за месяц */
   RetExpertPDQuar= 0,/* всего операций возврата ПД с инкассо за квартал */
   RetExpertPDYear= 0,/* всего операций возврата ПД с инкассо за год     */
      PayCheckDay = 0,/* всего операций оплаты дорожных чеков за день */
      PayCheckMon = 0,/* всего операций оплаты дорожных чеков за месяц */
      PayCheckQuar= 0,/* всего операций оплаты дорожных чеков за квартал */
      PayCheckYear= 0,/* всего операций оплаты дорожных чеков за год     */
     PDBuyCurrDay = 0,/* всего операций продажи ПД за валюту за день */
     PDBuyCurrMon = 0,/* всего операций продажи ПД за валюту за месяц */
     PDBuyCurrQuar= 0,/* всего операций продажи ПД за валюту за квартал */
     PDBuyCurrYear= 0,/* всего операций продажи ПД за валюту за год     */
      CardTakeDay = 0,/* всего операций начисления на карту за день  */
      CardTakeMon = 0,/* всего операций начисления на карту за месяц */
      CardTakeQuar= 0,/* всего операций начисления на карту за квартал */
      CardTakeYear= 0,/* всего операций начисления на карту за год     */
      CardGiveDay = 0,/* всего операций возврата с карты за день  */
      CardGiveMon = 0,/* всего операций возврата с карты за месяц */
      CardGiveQuar= 0,/* всего операций возврата с карты за квартал */
      CardGiveYear= 0;/* всего операций возврата с карты за год     */

macro ОбнулениеГлобальныхПеременных()

   OperToBeginQuar= 0;
   OperToBeginMon = 0;
   OperToBeginDay = 0;/* всего операций к началу дня */
          OperDay = 0;/* всего операций за день      */
   ChangeUnPayDay = 0;/* всего операций замены неплатежных за день  */
   ChangeUnPayMon = 0;/* всего операций замены неплатежных за месяц */
   ChangeUnPayQuar= 0;/* всего операций замены неплатежных за квартал */
   ChangeUnPayYear= 0;/* всего операций замены неплатежных за год     */
      BuyUnPayDay = 0;/* всего операций покупки неплатежных за день  */
      BuyUnPayMon = 0;/* всего операций покупки неплатежных за месяц */
      BuyUnPayQuar= 0;/* всего операций покупки неплатежных за квартал */
      BuyUnPayYear= 0;/* всего операций покупки неплатежных за год     */
         SplitDay = 0;/* всего операций размена валюты за день  */
         SplitMon = 0;/* всего операций размена валюты за месяц */
         SplitQuar= 0;/* всего операций размена валюты за квартал */
         SplitYear= 0;/* всего операций размена валюты за год     */
       IncassoDay = 0;/* всего операций приема на инкассо за день  */
       IncassoMon = 0;/* всего операций приема на инкассо за месяц */
       IncassoQuar= 0;/* всего операций приема на инкассо за квартал */
       IncassoYear= 0;/* всего операций приема на инкассо за год     */
    RetIncassoDay = 0;/* всего операций возврата с инкассо за день  */
    RetIncassoMon = 0;/* всего операций возврата с инкассо за месяц */
    RetIncassoQuar= 0;/* всего операций возврата с инкассо за квартал */
    RetIncassoYear= 0;/* всего операций возврата с инкассо за год     */
        ExpertDay = 0;/* всего операций приема на инкассо за день  */
        ExpertMon = 0;/* всего операций приема на инкассо за месяц */
        ExpertQuar= 0;/* всего операций приема на инкассо за квартал */
        ExpertYear= 0;/* всего операций приема на инкассо за год     */
     RetExpertDay = 0;/* всего операций возврата с инкассо за день  */
     RetExpertMon = 0;/* всего операций возврата с инкассо за месяц */
     RetExpertQuar= 0;/* всего операций возврата с инкассо за квартал */
     RetExpertYear= 0;/* всего операций возврата с инкассо за год     */
      PayCheckDay = 0;/* всего операций оплаты дорожных чеков за день */
      PayCheckMon = 0;/* всего операций оплаты дорожных чеков за месяц */
      PayCheckQuar= 0;/* всего операций оплаты дорожных чеков за квартал */
      PayCheckYear= 0;/* всего операций оплаты дорожных чеков за год     */
     PDBuyCurrDay = 0;/* всего операций продажи ПД за валюту за день */
     PDBuyCurrMon = 0;/* всего операций продажи ПД за валюту за месяц */
     PDBuyCurrQuar= 0;/* всего операций продажи ПД за валюту за квартал */
     PDBuyCurrYear= 0;/* всего операций продажи ПД за валюту за год     */
      CardTakeDay = 0;/* всего операций начисления на карту за день  */
      CardTakeMon = 0;/* всего операций начисления на карту за месяц */
      CardTakeQuar= 0;/* всего операций начисления на карту за квартал */
      CardTakeYear= 0;/* всего операций начисления на карту за год     */
      CardGiveDay = 0;/* всего операций возврата с карты за день  */
      CardGiveMon = 0;/* всего операций возврата с карты за месяц */
      CardGiveQuar= 0;/* всего операций возврата с карты за квартал */
      CardGiveYear= 0;/* всего операций возврата с карты за год     */

end;



MACRO ОбработчикДиалога( IP, cmd, id, key )
var stat;
  stat = CM_DEFAULT;
/*перед выводом панели*/
  if( cmd == DLG_INIT )
    PanelChoise.BegMonth   = SET_CHAR;
    PanelChoise.BegQuarter = UNSET_CHAR;
    PanelChoise.BegYear    = UNSET_CHAR;
    UpdateFields(IP);
  end;

  if (cmd == DLG_KEY)
    if (key == EX_SPACE )
       if( id == FldIndex(PanelChoise,"BegMonth") )
         if( PanelChoise.BegMonth == SET_CHAR )
            PanelChoise.BegMonth = UNSET_CHAR;
         else
            PanelChoise.BegMonth = SET_CHAR;
         end;
       elif( id == FldIndex(PanelChoise,"BegQuarter") )
         if( PanelChoise.BegQuarter == SET_CHAR )
            PanelChoise.BegQuarter = UNSET_CHAR;
         else
            PanelChoise.BegQuarter = SET_CHAR;
         end;
       elif( id == FldIndex(PanelChoise,"BegYear") )
         if( PanelChoise.BegYear == SET_CHAR )
            PanelChoise.BegYear = UNSET_CHAR;
         else
            PanelChoise.BegYear = SET_CHAR;
         end;
       end;
    /* Esc - Выход с запросом ---------------------------------------- */
    elif( key == EX_ESC )
       PanelChoise.BegMonth   = SET_CHAR;
       PanelChoise.BegQuarter = UNSET_CHAR;
       PanelChoise.BegYear    = UNSET_CHAR;
       stat = CM_SAVE;
    /* F9 - Выполнить разбиение     ---------------------------------- */
    elif( key == EX_F9 )
    end;
  end;

  return stat;
END;

/***************************************************************************/
/*  Процедура помещает валюту в массив, доллар на первое место             */
/*  если долларов вообще нет - первый элемент массива - пустой             */
/***************************************************************************/
MACRO ВалютуВМассив_ПОД( Curr )
   if( Curr == {BaseCur} )  /* !!! Считается, что базовая -- всегда доллары */
      ДолларыЕсть = True;
   end;
   /* глобализмы: DiffAllCurr и NumCurrency*/
   var OldNumCurrency = NumCurrency;
   var Индекс = PutNumberInArray( DiffAllCurr, Curr, NumCurrency );
   /* если была добавлена валюта */
   if( OldNumCurrency != NumCurrency )
      /* базовую валюту(доллары) перенести на первое место */
      if( (Индекс != 0) AND (Curr == {BaseCur}) )
         DiffAllCurr(Индекс) = 0;
         DiffAllCurr(   0  ) = {BaseCur};
         return 0;
      end;
      /* небазовую валюту(не доллары) перенести на второе место */
      if( (Индекс == 0) AND (Curr != {BaseCur}) )
         MoveElementArray( DiffAllCurr, Curr, 1, NumCurrency );
         NumCurrency = NumCurrency + 1;
         return 1;
      end;
   end;

   return Индекс;
END;

macro ШапкаПриложенияОД( )
 PrintDate_n_Time();
 MakeStrStamp();/* печатаем штамп */
[                                                                            Форма N25-a
                               ПРИЛОЖЕНИЕ К ОПЕРАЦИОННОМУ ДНЕВНИКУ  N _______
                                 за  #

]( StrAddZeroDate( Session.StartDate )
 );
end;

macro ДноПриложенияОД()
[

 Ст.контролер (контролер) ______________________________  Кассир  __________ #
 _____________________________________________________________________________________________

 Отчетность проверил "_____" ___________________ _____г.    Бухгалтер ________________________
](
   GetFIOEmployee( Session.Emp_Ref )
);

end;

macro ЦенныеБланки()
  ARRAY arr_sprv, arr_sprv31;
  var NumGetOutSprv   = 0; var NumKillSprv     = 0;
  var AllDebetSprv    = 0; var AllKreditSprv   = 0;
  var NumGetOutSprv31 = 0; var NumKillSprv31   = 0;
  var AllDebetSprv31  = 0; var AllKreditSprv31 = 0;

  var NumAvanceSprv   = CalcTransferBSO( Session.Emp_Ref, Session.Number, CDF_OperAvance  );
  var NumReturnSprv   = CalcTransferBSO( Session.Emp_Ref, Session.Number, CDF_OperCashOut );
  var NumAvanceSprv31 = CalcTransferF31( Session.Emp_Ref, Session.Number, CDF_OperAvance  );
  var NumReturnSprv31 = CalcTransferF31( Session.Emp_Ref, Session.Number, CDF_OperCashOut );

  var NumDebetSprv   = 0, NumRestSprv   =  0;
  var NumDebetSprv31 = 0, NumRestSprv31 =  0;

  ОставшиесяСправки( BSO, arr_sprv,   Session.Emp_Ref, Session.Number, NumRestSprv   );
  ОставшиесяСправки( F31, arr_sprv31, Session.Emp_Ref, Session.Number, NumRestSprv31 );

  NumDebetSprv   = NumAvanceSprv   - NumRestSprv   - NumReturnSprv;
  NumDebetSprv31 = NumAvanceSprv31 - NumRestSprv31 - NumReturnSprv31;
[
                                        IV. ЦЕННЫЕ БЛАНКИ
 ------------------------+--------------+--------------+--------------+--------------+
                         |              |              |              |   Отослано   |
   Наименование бланков  | Оставалось к |  Поступило   | Израсходовано|  учреждениям |
                         |  началу дня  |              |              |   Сбербанка  |
 ------------------------+--------------+--------------+--------------+--------------+
  Справки БСО ф.0406007  |              |##############|##############|##############|
 ------------------------+--------------+--------------+--------------+--------------+
  Справки БСО ф.31       |              |##############|##############|##############|
 ------------------------+--------------+--------------+--------------+--------------+
  Итого                                  ############## ############## ##############
]
( NumAvanceSprv:c,
  NumDebetSprv:c,
  NumReturnSprv:c,
  NumAvanceSprv31:c,
  NumDebetSprv31:c, 
  NumReturnSprv31:c,
  NumAvanceSprv + NumAvanceSprv31:c,
  NumDebetSprv  + NumDebetSprv31:c, 
  NumReturnSprv + NumReturnSprv31:c
);
end;

/****************************************************************************/
/* Процедура заполняет массив с количествами операций продажи               */
/* и покупки по валютам за определенную смену для определенного операциониста*/
/* за определенный месяц и день                                             */
/****************************************************************************/
MACRO КоличествоОперацийВМасс( Операционист, НомерСмены, Массив, Месяц, Год )

  Operat.Emp_Ref        = Операционист;
  Operat.Sess_Ref       = НомерСмены;
  Operat.Oper_Ref       = 0;
  Operat.State_Ref      = 0;
  Operat.Rate_Ref       = 0;
  Operat.OutCurr_Ref    = 0;
  Operat.IncomeCurr_Ref = 0;
  var Day, Mon, Quar, Year, Индекс, Квартал;
  var Loop = GetGE( Operat );

  While( (Loop == True) AND
         (Operat.Emp_Ref        == Операционист     )      AND
         (Operat.Sess_Ref       == НомерСмены       )
       )
    /* если операция покупки-продажи занесена в реестр */
    if( /*(Месяц == Mon) AND*/ ((Operat.State_Ref == CDF_OperInReestr) OR (Operat.State_Ref == CDF_OperCompleted)) )
       /* Конверсия наличной валюты */
       if( Operat.Oper_Ref == CDF_OperCurrConversion )
          Индекс = ВалютуВМассив_ПОД( Operat.IncomeCurr_Ref );
          if( ValType(Массив(Индекс*NumKindOper+OperConv)) == V_UNDEF )
             Массив(Индекс*NumKindOper+OperConv) = 0;/* обнуление нового элемента массива */
          end;
          Массив(Индекс*NumKindOper+OperConv) = Массив(Индекс*NumKindOper+OperConv) + 1;
       /* продажа валюты */
       elif( Operat.Oper_Ref == CDF_OperCurrSale )
          Индекс = ВалютуВМассив_ПОД( Operat.OutCurr_Ref );
          if( ValType(Массив(Индекс*NumKindOper+OperSale)) == V_UNDEF )
             Массив(Индекс*NumKindOper+OperSale) = 0;/* обнуление нового элемента массива */
          end;
          Массив(Индекс*NumKindOper+OperSale) = Массив(Индекс*NumKindOper+OperSale) + 1;
       /* покупка валюты */
       elif( Operat.Oper_Ref == CDF_OperCurrBuy )
          Индекс = ВалютуВМассив_ПОД( Operat.IncomeCurr_Ref );
          if( ValType(Массив(Индекс*NumKindOper)) == V_UNDEF )
             Массив(Индекс*NumKindOper) = 0;/* обнуление нового элемента массива */
          end;
          Массив(Индекс*NumKindOper) = Массив(Индекс*NumKindOper) + 1;
       /* покупка валютных ПД */
       elif( Operat.Oper_Ref == CDF_OperPayDocBuy )
          Индекс = ВалютуВМассив_ПОД( Operat.IncomeCurr_Ref );
          if( ValType(Массив(Индекс*NumKindOper+OperPDBuy )) == V_UNDEF )
             Массив(Индекс*NumKindOper+OperPDBuy) = 0;/* обнуление нового элемента массива */
          end;
          Массив(Индекс*NumKindOper+OperPDBuy) = Массив(Индекс*NumKindOper+OperPDBuy) + 1;
       /* продажа валютных ПД */
       elif( Operat.Oper_Ref == CDF_OperPayDocSale )
          Индекс = ВалютуВМассив_ПОД( Operat.OutCurr_Ref );
          if( ValType(Массив(Индекс*NumKindOper+OperPDSale)) == V_UNDEF )
             Массив(Индекс*NumKindOper+OperPDSale) = 0;/* обнуление нового элемента массива */
          end;
          Массив(Индекс*NumKindOper+OperPDSale) = Массив(Индекс*NumKindOper+OperPDSale) + 1;
       end;
    end;
    Loop = Next( Operat );
  end;
END;

/****************************************************************************/
/* Процедура заполняет глобализмы с количествами операций продажи           */
/* и покупки по валютам за определенную смену для определенного операциониста*/
/* за определенный месяц и день                                             */
/****************************************************************************/
MACRO КоличествоОперацийВГлоб( Операционист, НомерСмены, Месяц, Год )

     Operat.Emp_Ref        = Операционист;
     Operat.Sess_Ref       = НомерСмены;
     Operat.Oper_Ref       = 0;
     Operat.State_Ref      = 0;
     Operat.Rate_Ref       = 0;
     Operat.OutCurr_Ref    = 0;
     Operat.IncomeCurr_Ref = 0;
     var Day, Mon, Quar, Year, Индекс, Квартал;
     var Loop = GetGE( Operat );

     While( (Loop == True) AND
            (Operat.Emp_Ref        == Операционист     )      AND
            (Operat.Sess_Ref       == НомерСмены       )
          )
       DateSplit( Operat.Date, Day, Mon, Year );
       Квартал = ( Месяц - 1 ) / 3 + 1;
       Quar    = ( Mon - 1 ) / 3 + 1;
       /* если операция покупки-продажи занесена в реестр */
       if( /*(Месяц == Mon) AND*/ ((Operat.State_Ref == CDF_OperInReestr) OR (Operat.State_Ref == CDF_OperCompleted)) )

          /* Замена неплатежеспособной валюты */
          if( Operat.Oper_Ref == CDF_OperChange )
             if( Operat.Sess_Ref == Session.Number )
                ChangeUnPayDay = ChangeUnPayDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                ChangeUnPayMon = ChangeUnPayMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                ChangeUnPayQuar = ChangeUnPayQuar + 1;/* с начала квартала */
             elif( Год == Year )
                ChangeUnPayYear = ChangeUnPayYear + 1;/* с начала года */
             end;
          /* Покупка неплатежеспособной валюты */
          elif( Operat.Oper_Ref == CDF_OperUnpayCurrBuy )
             if( Operat.Sess_Ref == Session.Number )
                BuyUnPayDay = BuyUnPayDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                BuyUnPayMon = BuyUnPayMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                BuyUnPayQuar = BuyUnPayQuar + 1;/* с начала квартала */
             elif( Год == Year )
                BuyUnPayYear = BuyUnPayYear + 1;/* с начала года */
             end;
          /* Размен валюты */
          elif( Operat.Oper_Ref == CDF_OperSplit )
             if( Operat.Sess_Ref == Session.Number )
                SplitDay = SplitDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                SplitMon = SplitMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                SplitQuar = SplitQuar + 1;/* с начала квартала */
             elif( Год == Year )
                SplitYear = SplitYear + 1;/* с начала года */
             end;
          /* Оплата дорожных чеков */
          elif( Operat.Oper_Ref == CDF_OperPayDocBuyCurr )
             if( Operat.Sess_Ref == Session.Number )
                PayCheckDay = PayCheckDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                PayCheckMon = PayCheckMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                PayCheckQuar = PayCheckQuar + 1;/* с начала квартала */
             elif( Год == Year )
                PayCheckYear = PayCheckYear + 1;/* с начала года */
             end;
          /* Продажа платежного документа за валюту */
          elif( Operat.Oper_Ref == CDF_OperPayDocSaleCurr )
             if( Operat.Sess_Ref == Session.Number )
                PDBuyCurrDay = PDBuyCurrDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                PDBuyCurrMon = PDBuyCurrMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                PDBuyCurrQuar = PDBuyCurrQuar + 1;/* с начала квартала */
             elif( Год == Year )
                PDBuyCurrYear = PDBuyCurrYear + 1;/* с начала года */
             end;
          /* Прием вал. на инкассо */
          elif( Operat.Oper_Ref == CDF_OperIncasso )
             if( Operat.Sess_Ref == Session.Number )
                IncassoDay = IncassoDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                IncassoMon = IncassoMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                IncassoQuar = IncassoQuar + 1;/* с начала квартала */
             elif( Год == Year )
                IncassoYear = IncassoYear + 1;/* с начала года */
             end;
          /* Возврат вал. с инкассо */
          elif( Operat.Oper_Ref == CDF_OperReturnIncasso )
             if( Operat.Sess_Ref == Session.Number )
                RetIncassoDay = RetIncassoDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                RetIncassoMon = RetIncassoMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                RetIncassoQuar = RetIncassoQuar + 1;/* с начала квартала */
             elif( Год == Year )
                RetIncassoYear = RetIncassoYear + 1;/* с начала года */
             end;
          /* Прием вал. на экспертизу */
          elif( Operat.Oper_Ref == CDF_OperExpert )
             if( Operat.Sess_Ref == Session.Number )
                ExpertDay = ExpertDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                ExpertMon = ExpertMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                ExpertQuar = ExpertQuar + 1;/* с начала квартала */
             elif( Год == Year )
                ExpertYear = ExpertYear + 1;/* с начала года */
             end;
          /* Возврат вал. с экспертизы */
          elif( Operat.Oper_Ref == CDF_OperReturnExpert )
             if( Operat.Sess_Ref == Session.Number )
                RetExpertDay = RetExpertDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                RetExpertMon = RetExpertMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                RetExpertQuar = RetExpertQuar + 1;/* с начала квартала */
             elif( Год == Year )
                RetExpertYear = RetExpertYear + 1;/* с начала года */
             end;
          /* Прием ПД на инкассо */
          elif( Operat.Oper_Ref == CDF_OperPayDocIncasso )
             if( Operat.Sess_Ref == Session.Number )
                IncassoPDDay = IncassoPDDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                IncassoPDMon = IncassoPDMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                IncassoPDQuar = IncassoPDQuar + 1;/* с начала квартала */
             elif( Год == Year )
                IncassoPDYear = IncassoPDYear + 1;/* с начала года */
             end;
          /* Возврат ПД с инкассо */
          elif( Operat.Oper_Ref == CDF_OperPayDocReturnIncasso )
             if( Operat.Sess_Ref == Session.Number )
                RetIncassoPDDay = RetIncassoPDDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                RetIncassoPDMon = RetIncassoPDMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                RetIncassoPDQuar = RetIncassoPDQuar + 1;/* с начала квартала */
             elif( Год == Year )
                RetIncassoPDYear = RetIncassoPDYear + 1;/* с начала года */
             end;
          /* Прием ПД на экспертизу */
          elif( Operat.Oper_Ref == CDF_OperPayDocExpert )
             if( Operat.Sess_Ref == Session.Number )
                ExpertPDDay = ExpertPDDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                ExpertPDMon = ExpertPDMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                ExpertPDQuar = ExpertPDQuar + 1;/* с начала квартала */
             elif( Год == Year )
                ExpertPDYear = ExpertPDYear + 1;/* с начала года */
             end;
          /* Возврат ПД с экспертизы */
          elif( Operat.Oper_Ref == CDF_OperPayDocReturnExpert )
             if( Operat.Sess_Ref == Session.Number )
                RetExpertPDDay = RetExpertPDDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                RetExpertPDMon = RetExpertPDMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                RetExpertPDQuar = RetExpertPDQuar + 1;/* с начала квартала */
             elif( Год == Year )
                RetExpertPDYear = RetExpertPDYear + 1;/* с начала года */
             end;
          /* Начисление на Карту */
          elif( Operat.Oper_Ref == CDF_OperCardTake )
             if( Operat.Sess_Ref == Session.Number )
                CardTakeDay = CardTakeDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                CardTakeMon = CardTakeMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                CardTakeQuar = CardTakeQuar + 1;/* с начала квартала */
             elif( Год == Year )
                CardTakeYear = CardTakeYear + 1;/* с начала года */
             end;
          /* Снятие с Карты */
          elif( Operat.Oper_Ref == CDF_OperCardGive )
             if( Operat.Sess_Ref == Session.Number )
                CardGiveDay = CardGiveDay + 1;/* за текущую смену */
             elif( Месяц == Mon )
                CardGiveMon = CardGiveMon + 1;/* с начала месяца  */
             elif( Квартал == Quar )
                CardGiveQuar = CardGiveQuar + 1;/* с начала квартала */
             elif( Год == Year )
                CardGiveYear = CardGiveYear + 1;/* с начала года */
             end;
          end;
       end;
       Loop = Next( Operat );
     end;
END;

macro ЗаполнитьНеопределенныеЭлементы( Массив, Всего )
 var i = 0;
 While( i < Всего )
    if( ValType(Массив(i)) == V_UNDEF ) Массив(i) = 0;
    end;
    i = i + 1;
 end;
end;

/****************************************************************************/
/* Процедура заполняет массивы с количествами операций по валютам           */
/****************************************************************************/
MACRO ЗаполнитьМассивыКоличествОпераций()

  var CurrDay, CurrMon, CurrQuar, CurrYear, Day, Mon, Quar, Year, Loop=False;

   /* Session - смена, по которой формируется отчетность */
   DateSplit( Session.StartDate, CurrDay, CurrMon, CurrYear );
   CurrQuar = ( CurrMon - 1 ) / 3 + 1;
   /* Подсчет количества операций за указанный срок */
   ClearRecord( SessE );
   SessE.Emp_Ref = Session.Emp_Ref;
   SessE.Number  = Session.Number;
   if( GetEQ( SessE ) )
     Loop = True;
   end;

   While( Loop )
      DateSplit( SessE.StartDate, Day, Mon, Year );
      Quar = ( Mon - 1 ) / 3 + 1;
      /* если дошли до начала месяца, */
      /* (года, квартала - зависит от выбора) */
      /* выходим из цикла */
      if( Year != CurrYear
          OR
          ( ( Quar != CurrQuar ) AND ( PanelChoise.BegYear == UNSET_CHAR) )
          OR
          ( ( Mon != CurrMon  ) AND
            ( PanelChoise.BegQuarter == UNSET_CHAR) AND
            ( PanelChoise.BegYear    == UNSET_CHAR)
          )
          OR
          ( ( PanelChoise.BegMonth   == UNSET_CHAR ) AND
            ( PanelChoise.BegQuarter == UNSET_CHAR ) AND
            ( PanelChoise.BegYear    == UNSET_CHAR )
          )
        )
         Loop = False;
      end;
      if( Loop )
        if( SessE.Number == Session.Number )
          ; /* смена, по которой формируется отчетность - не надо учитывать */
        elif( Mon == CurrMon )
          /* подсчет количества операций покупки и продажи с начала месяца */
          КоличествоОперацийВМасс( SessE.Emp_Ref, SessE.Number, NumOperMonth, CurrMon, CurrYear );
        elif( Quar == CurrQuar )
          /* подсчет количества операций покупки и продажи с начала квартала */
          КоличествоОперацийВМасс( SessE.Emp_Ref, SessE.Number, NumOperQuar, CurrMon, CurrYear );
        elif( Year == CurrYear )
          /* подсчет количества операций покупки и продажи с начала года */
          КоличествоОперацийВМасс( SessE.Emp_Ref, SessE.Number, NumOperYear, CurrMon, CurrYear );
        end;
        /* подсчет количества операций покупки и продажи в глобализмах */
        /* (такой же if-elif-elif-end содержтся внутри) */
        КоличествоОперацийВГлоб( SessE.Emp_Ref, SessE.Number, CurrMon, CurrYear );
      end;
      Loop = Prev( SessE ) AND ( SessE.Emp_Ref == Session.Emp_Ref );
   end;
   /* подсчет количества операций покупки и продажи за текущий день */
   КоличествоОперацийВМасс( Session.Emp_Ref, Session.Number, NumOperDay, CurrMon, CurrYear );
   КоличествоОперацийВГлоб( SessE.Emp_Ref, SessE.Number, CurrMon, CurrYear );
   ЗаполнитьНеопределенныеЭлементы( NumOperYear,  NumCurrency*NumKindOper );
   ЗаполнитьНеопределенныеЭлементы( NumOperQuar,  NumCurrency*NumKindOper );
   ЗаполнитьНеопределенныеЭлементы( NumOperMonth, NumCurrency*NumKindOper );
   ЗаполнитьНеопределенныеЭлементы( NumOperDay,   NumCurrency*NumKindOper );
END;

macro ШапкаУчетаОпераций()
[
                                  V. УЧЕТ ОПЕРАЦИЙ
 --------------------------------------+---------------+---------------+---------------+---------------+---------------+
             Виды операций             |  К началу дня |    За день    |С начала месяца|С нач. квартала| С начала года |
                                       |   за месяц    |               |               |               |               |
 --------------------------------------+---------------+---------------+---------------+---------------+---------------+
];
end;
macro РазделительСтрокУчетаОпераций()
[--------------------------------------+---------------+---------------+---------------+---------------+---------------+
];
end;
macro ДноУчетаОпераций()
[--------------------------------------+---------------+---------------+---------------+---------------+---------------+
];
end;

MACRO ПечатьСтрокиПОД( Заголовок, ЗаДень, ЗаМесяц, ЗаКвартал, ЗаГод )
  var За_День, КНачДня, СНачМес, СНачКвар, СНачГода;
   За_День = Число_Строка2(ЗаДень);

   if( PanelChoise.BegMonth == UNSET_CHAR )
     КНачДня = Число_Строка2(0);
     СНачМес = Число_Строка2(0);
   else
     КНачДня = Число_Строка2(ЗаМесяц);
     СНачМес = Число_Строка2(ЗаМесяц + ЗаДень);
   end;
   if( PanelChoise.BegQuarter == UNSET_CHAR )
     СНачКвар = Число_Строка2(0);
   else
     СНачКвар = Число_Строка2(ЗаКвартал + ЗаМесяц + ЗаДень);
   end;
   if( PanelChoise.BegYear == UNSET_CHAR )
     СНачГода = Число_Строка2(0);
   else
     СНачГода = Число_Строка2(ЗаГод + ЗаКвартал + ЗаМесяц + ЗаДень);
   end;
   if( ЗаДень OR ЗаМесяц OR ЗаКвартал OR ЗаГод )
      [######################################|###############|###############|###############|###############|###############|
      ](
       Заголовок:c,
       КНачДня:c,
       За_День:c,
       СНачМес:c,
       СНачКвар:c,
       СНачГода:c
      );
      РазделительСтрокУчетаОпераций();

      OperToBeginQuar= OperToBeginQuar+ ЗаГод;
      OperToBeginMon = OperToBeginMon + ЗаКвартал;
      OperToBeginDay = OperToBeginDay + ЗаМесяц;
      OperDay        = OperDay        + ЗаДень;
   end;
END;

macro СтрокаПокупкиВалюты_ПОД( Индекс, ISOCurr )
 ПечатьСтрокиПОД( "Покупка " + ISOCurr,
                 NumOperDay  (Индекс*NumKindOper+OperBuy),
                 NumOperMonth(Индекс*NumKindOper+OperBuy),
                 NumOperQuar (Индекс*NumKindOper+OperBuy),
                 NumOperYear (Индекс*NumKindOper+OperBuy) );
end;

macro СтрокаПродажиВалюты_ПОД( Индекс, ISOCurr )
 ПечатьСтрокиПОД( "Продажа " + ISOCurr,
                 NumOperDay  (Индекс*NumKindOper+OperSale),
                 NumOperMonth(Индекс*NumKindOper+OperSale),
                 NumOperQuar (Индекс*NumKindOper+OperSale),
                 NumOperYear (Индекс*NumKindOper+OperSale) );
end;

macro СтрокаПокупкиПД_ПОД( Индекс, ISOCurr )
 ПечатьСтрокиПОД( "Покупка ПД " + ISOCurr,
                 NumOperDay  (Индекс*NumKindOper+OperPDBuy),
                 NumOperMonth(Индекс*NumKindOper+OperPDBuy),
                 NumOperQuar (Индекс*NumKindOper+OperPDBuy),
                 NumOperYear (Индекс*NumKindOper+OperPDBuy) );
end;

macro СтрокаПродажиПД_ПОД( Индекс, ISOCurr )
 ПечатьСтрокиПОД( "Продажа ПД " + ISOCurr,
                 NumOperDay  (Индекс*NumKindOper+OperPDSale),
                 NumOperMonth(Индекс*NumKindOper+OperPDSale),
                 NumOperQuar (Индекс*NumKindOper+OperPDSale),
                 NumOperYear (Индекс*NumKindOper+OperPDSale) );
end;

macro СтрокаКонверсииВалюты_ПОД( Индекс, ISOCurr )
 ПечатьСтрокиПОД( "Конверсия " + ISOCurr,
                 NumOperDay  (Индекс*NumKindOper+OperConv),
                 NumOperMonth(Индекс*NumKindOper+OperConv),
                 NumOperQuar (Индекс*NumKindOper+OperConv),
                 NumOperYear (Индекс*NumKindOper+OperConv) );
end;

macro СтрокаЗаменыНеплатежных_ПОД()
 ПечатьСтрокиПОД( "Замена неплатежных денежных знаков",
                  ChangeUnPayDay, ChangeUnPayMon, ChangeUnPayQuar, ChangeUnPayYear );
end;

macro СтрокаПокупкиНеплатежных_ПОД()
 ПечатьСтрокиПОД( "Покупка неплатежных денежных знаков",
                  BuyUnPayDay, BuyUnPayMon, BuyUnPayQuar, BuyUnPayYear );
end;

macro СтрокаРазменаВалюты_ПОД()
 ПечатьСтрокиПОД( "Размен валюты",
                  SplitDay, SplitMon, SplitQuar, SplitYear );
end;

macro СтрокаПриемНаИнкассо_ПОД()
 ПечатьСтрокиПОД( "Прием на инкассо",
                  IncassoDay, IncassoMon, IncassoQuar, IncassoYear );
end;

macro СтрокаВозвратСИнкассо_ПОД()
 ПечатьСтрокиПОД( "Возврат с инкассо",
                  RetIncassoDay, RetIncassoMon, RetIncassoQuar, RetIncassoYear );
end;

macro СтрокаПриемНаЭкспертизу_ПОД()
 ПечатьСтрокиПОД( "Прием на экспертизу",
                  ExpertDay, ExpertMon, ExpertQuar, ExpertYear );
end;

macro СтрокаВозвратСЭкспертизы_ПОД()
 ПечатьСтрокиПОД( "Возврат с экспертизы",
                  RetExpertDay, RetExpertMon, RetExpertQuar, RetExpertYear );
end;

macro СтрокаОплатыЧеков_ПОД()
 ПечатьСтрокиПОД( "Оплата дорожных чеков",
                  PayCheckDay, PayCheckMon, PayCheckQuar, PayCheckYear );
end;

macro СтрокаПродажаПДзаВалюту_ПОД()
 ПечатьСтрокиПОД( "Продажа ПД за валюту",
                  PDBuyCurrDay, PDBuyCurrMon, PDBuyCurrQuar, PDBuyCurrYear );
end;

macro СтрокаНачисленияНаКарту_ПОД()
 ПечатьСтрокиПОД( "Начисление на карту",
                  CardTakeDay, CardTakeMon, CardTakeQuar, CardTakeYear );
end;

macro СтрокаСнятияСКарты_ПОД()
 ПечатьСтрокиПОД( "Снятие с карты",
                  CardGiveDay, CardGiveMon, CardGiveQuar, CardGiveYear );
end;

MACRO ИтогУчетаОпераций()
  var За_День, КНачДня, СНачМес, СНачКвар, СНачГода;

   За_День = Число_Строка2(OperDay);
   if( PanelChoise.BegMonth == UNSET_CHAR )
     КНачДня = Число_Строка2(0);
     СНачМес = Число_Строка2(0);
   else
     КНачДня = Число_Строка2(OperToBeginDay);
     СНачМес = Число_Строка2(OperToBeginDay+OperDay);
   end;
   if( PanelChoise.BegQuarter == UNSET_CHAR )
     СНачКвар = Число_Строка2(0);
   else
     СНачКвар = Число_Строка2(OperToBeginMon+OperToBeginDay+OperDay);
   end;
   if( PanelChoise.BegYear == UNSET_CHAR )
     СНачГода = Число_Строка2(0);
   else
     СНачГода = Число_Строка2(OperToBeginQuar+OperToBeginMon+OperToBeginDay+OperDay);
   end;

[                                ИТОГО |###############|###############|###############|###############|###############|
](     КНачДня:c,
       За_День:c,
       СНачМес:c,
       СНачКвар:c,
       СНачГода:c
  );
END;

macro УчетОпераций()
 ШапкаУчетаОпераций();
 var j, ISOCurr;
 if( ДолларыЕсть )
 /* первый элемент -доллары */
    j = 0;
 else
 /* первый элемент - пустой */
    j = 1;
 end;

 While( j < NumCurrency )
    if( DiffAllCurr(j) != {NationalCur} )
       ISOCurr = GetISOCurr( DiffAllCurr( j ) );
       СтрокаПокупкиВалюты_ПОД  ( j, ISOCurr );
       СтрокаПродажиВалюты_ПОД  ( j, ISOCurr );
       СтрокаПокупкиПД_ПОД      ( j, ISOCurr );
       СтрокаПродажиПД_ПОД      ( j, ISOCurr );
       СтрокаКонверсииВалюты_ПОД( j, ISOCurr );
    end;
    j = j + 1;
 end;

 СтрокаОплатыЧеков_ПОД       ();
 СтрокаПродажаПДзаВалюту_ПОД ();
 СтрокаЗаменыНеплатежных_ПОД ();
 СтрокаПокупкиНеплатежных_ПОД();
 СтрокаРазменаВалюты_ПОД     ();
 СтрокаПриемНаИнкассо_ПОД    ();
 СтрокаВозвратСИнкассо_ПОД   ();
 СтрокаПриемНаЭкспертизу_ПОД ();
 СтрокаВозвратСЭкспертизы_ПОД();
 СтрокаНачисленияНаКарту_ПОД ();
 СтрокаСнятияСКарты_ПОД      ();


 ИтогУчетаОпераций();
 ДноУчетаОпераций();
end;

macro ПриложениеОД()
   MakeReportFile( ReportFile, "sbr_f25" );
/* 25.05.2000 VS Убран раздел V 
   asize(NumOperYear, 0);
   asize(NumOperQuar, 0);
   asize(NumOperMonth,0);
   asize(NumOperDay,  0);
*/
   ОбнулениеГлобальныхПеременных();
/* 25.05.2000 VS Убран раздел V 
   RunDialog( PanelChoise, "ОбработчикДиалога" );
*/
   /* Заполняем массивы с количествами операций и валютами */
/* 25.05.2000 VS Убран раздел V 
   ЗаполнитьМассивыКоличествОпераций();
*/
   ШапкаПриложенияОД();
   ЦенныеБланки();
/* 25.05.2000 VS Убран раздел V 
   УчетОпераций();
*/
   ДноПриложенияОД();

   /* смотрим файл отчета */
   SetOutPut( 0 );
   ViewFile( ReportFile );
   Close( ReportFile );
end;
