/****************************************************************

  R-Style SoftLab.

  RS-Retail.

  Валютно-обменные операции.

  Сводная справка об отсутствии ВОО.

*****************************************************************/

import CommonInter, ExchangeInter, rsd;

private const cBranch = NumFNCash();  // NumRealFNcash();

private var Person = TBFile( "person.dbt","bank.def", "r", 0 );

private var ddep  = TRecHandler( "i_dp_dep.rec" );
private var party = TRecHandler( "i_party.rec" );

private var {Post_Addr};
private var {Post_Addr_Branch};
private var {Name_Bank};
private var {curdate};

private var day, month, year;

var vDate = {curdate};

var regNumBank = {ExBankNumber};
var ordNumBank = {ExRegNumber};

var mainBranch = getFilialNum( );
var curBranch = NumFNCash( );
var nameMain;
var nameCur;

var cmd,  rs;
var cmdG, rsG;

  if ( GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ОТЧЕТЫ\\СПРАВКА_ОБ_ОТСУТСТВИИ_ВОО" ) == 0 )
    MsgBox( "Справка об отсутствии ВОО должна печататься|по каждому операционисту отдельно." );
    Exit( 1 );
  end;

  if ( NOT GetDate( vDate, "Введите дату отчета" ) )
    vDate = {curdate};
  end;

  cmd = RsdCommand( "select distinct gm.t_oper as rOper "
                    "from drtgroup_dbt g, "
                          "dgroupmem_dbt gm "
                    "where "
                          "G.T_BRANCH = :p_b "
                          "and G.T_DATE = :p_d "
                          "and g.T_TYPE = 1 "
                          "and GM.T_BRANCH = G.T_BRANCH "
                          "and GM.T_DATE = G.T_DATE "
                          "and GM.T_GROUP = G.T_ID "
                    "MINUS "
                       "select exop.t_oper "
                       "from (select distinct gm.t_oper "
                                "from drtgroup_dbt g, "
                                      "dgroupmem_dbt gm "
                                "where "
                                       "G.T_BRANCH = :p_b "
                                       "and G.T_DATE = :p_d "
                                       "and g.T_TYPE = 1 "
                                       "and GM.T_BRANCH = G.T_BRANCH "
                                       "and GM.T_DATE = G.T_DATE "
                                       "and GM.T_GROUP = G.T_ID ) exop "
                       "where "
                          "exists (select 'r' "
                                      "from dreestr_2_dbt rstr "
                                      "where "
                                          "RSTR.T_BRANCH = :p_b "
                                          "and RSTR.T_CURDATE = :p_d "
                                          "and RSTR.T_OPER = exop.t_oper "
                                   ") "
                    "order by 1" );

  rs = RsdRecordset( cmd );

  cmd.addParam( "p_b", RSDBP_IN, cBranch );
  cmd.addParam( "p_d", RSDBP_IN, vDate );

  cmd.execute;

  if ( NOT rs.moveNext() )
    cmdG = RsdCommand( "select distinct gm.t_oper as rOper "
                       "from drtgroup_dbt g, "
                             "dgroupmem_dbt gm "
                       "where "
                             "G.T_BRANCH = :p_b "
                             "and G.T_DATE = :p_d "
                             "and g.T_TYPE = 1 "
                             "and GM.T_BRANCH = G.T_BRANCH "
                             "and GM.T_DATE = G.T_DATE "
                             "and GM.T_GROUP = G.T_ID " );

    rsG = RsdRecordset( cmd );

    cmdG.addParam( "p_b", RSDBP_IN, cBranch );
    cmdG.addParam( "p_d", RSDBP_IN, vDate );

    cmdG.execute;

    if ( NOT rsG.moveNext() )
      MsgBox( "Не была открыта смена за дату ", vDate );
      Exit( 1 );
    end;

    MsgBox( "Все операционисты подразделения выполняли операции,|требующие отражения в Реестре 136-И." );
    Exit( 1 );
  end;

  // {Post_Addr}
  if ( findDepartment( mainBranch, NULL, ddep, party ) )
    if ( party.rec.Address != "" )
      nameMain = party.rec.Address;
    else
      nameMain = {Post_Addr};
    end;
  end;

  // {Post_Addr_Branch}
  if ( findDepartment( curBranch, NULL, ddep, party ) )
    if ( party.rec.Address != "" )
      nameCur = Trim( party.rec.Name + "; " + party.rec.Address );
    else
      nameCur = Trim( party.rec.Name + "; " + {Post_Addr_Branch} );
    end;
  end;

  DateSplit( vDate, day, month, year );

[

   Полное (сокращенное) фирменное наименование   +--------------------------+
   ############################################  |##########################|
                                                 +--------------------------+

   Регистрационный номер уполномоченного банка   +-------+   +-------+
   (порядковый номер филиала)                    | ##### | / | ##### |
                                                 +-------+   +-------+

   Местонахождение (адрес) уполномоченного банка +--------------------------+
   #####################                         |##########################|
                                                 +--------------------------+

   Наименование внутреннего структурного         +--------------------------+
   ###################################           |##########################|
                                                 +--------------------------+

                                                        +--+  +--+  +----+
   Дата выпуска Справки                                 |##|  |##|  |####|
                                                        +--+  +--+  +----+
                                                        День  Месяц  Год



                                      Справка

       В течение рабочего дня отсутствуют операции с наличной иностранной 
   валютой и чеками.


] 
( 
 "уполномоченного банка (наименование филиала)",
 {Name_Bank}:w, 
 regNumBank,
 ordNumBank,
 "(филиала)",
 nameMain:w,
 "подразделения уполномоченного банка и его местонахождение (адрес)":w,
 nameCur:w,
 day:r:o,
 month:r:o,
 year:r:o
);

    cmd.execute;

    while ( rs.moveNext( ) )
      Person.Clear();
      Person.Rec.Oper = rs.value( "rOper" );
      if ( Person.GetEQ() )
        [    Кассовый работник ___________________ #
                                    (подпись)
          ]
        ( Person.Rec.Name );
      end;
    end;
