/*-RSL---------------------------------------- R-Style Software Lab --*/
/*---------------    Печать отчета по оборотам ОП   ------------------*/
/* RS-Bank      подсистема Обменный пункт  Азарцов В.В.  20.05.97     */
/*RA  9-02-99   закомментировал упоминание неденоминированных сумм    */

IMPORT ExchangeInter, "define.mac";

 FILE TurnOper("turnover.dbt", "exchange.def" ) key 0;
 FILE   Operat("operat.dbt",   "exchange.def")  key 1;/*Сотрудник-Смена-...*/

/*  TurnOper - ключ 0                                          */
/*    int32   Emp_Ref;          Код сотрудника                   */
/*    int32   Sess_Ref;         Код смены сотрудника             */
/*    int32   Oper_Ref;         Ссылка на операцию в operat.dbt  */
/*    bdate   Date;             Дата  проведения оборота         */
/*    btime   Time;             Время проведения оборота         */
/*    int32   Trs_Ref;          Код ценности                     */
/*    int32   SubTrs_Ref;       Код подценности                  */
/*    int32   Curr_Ref;         Код валюты                       */


MACRO Init( Emp, Sess, Oper, Trs, Curr )
    ClearRecord( TurnOper );
    TurnOper.Emp_Ref   =  Emp;  /* Сотрудник, проведший операцию   */
    TurnOper.Sess_Ref  =  Sess; /* Номер смены сотрудника          */
    TurnOper.Oper_Ref  =  Oper; /* Ссылка на операцию в operat.dbt */
    TurnOper.Trs_Ref   =  Trs;  /* Код ценности                    */
    TurnOper.Curr_Ref  =  Curr; /* Код валюты                      */
    return getGE( TurnOper );
END;

MACRO Filter( Emp, Sess, Oper, BegDate, EndDate, Curr, Type, Trs )
    if( ( (not Emp )    OR (TurnOper.Emp_Ref  == Emp    ) ) AND
        ( (not Sess)    OR (TurnOper.Sess_Ref == Sess   ) ) AND
        ( (not Oper)    OR (TurnOper.Oper_Ref == Oper   ) ) AND
        ( (BegDate == date( 0,0,0 ) ) OR (TurnOper.Date     >= BegDate) ) AND
        ( (EndDate == date( 0,0,0 ) ) OR (TurnOper.Date     <= EndDate) ) AND
        ( (not Curr)    OR (TurnOper.Curr_Ref == Curr   ) ) AND
        ( (not Type)    OR (TurnOper.Type     == Type   ) ) AND
        ( (not Trs )    OR (TurnOper.Trs_Ref  == Trs    ) )
      )  return True;
    else return False;
    end;
END;

/****************************************************************************/
/*Процедура заполняет массив разных валют оборотов для операциониста и смены*/
/****************************************************************************/
MACRO ЗаполнитьМассивВалютОборотов( CurrTurn, Emp, Sess )
 var j = 0, BreakLoop;
 asize( CurrTurn, 0 );

 ClearRecord(Operat);

 Operat.Emp_Ref        = Emp;
 Operat.Sess_Ref       = Sess;

 rewind( Operat );/*перемотать на начало*/

 if( BreakLoop = GetGE( Operat ) )
    /*цикл по реестрам*/
    While(   BreakLoop                 AND
           ( Operat.Emp_Ref  == Emp  ) AND
           ( Operat.Sess_Ref == Sess ) )
       if( Operat.OutCurr_Ref != ANY_CURR )
          PutNumberInArray( CurrTurn, Operat.OutCurr_Ref, j );
       end;
       if( Operat.IncomeCurr_Ref != ANY_CURR )
          PutNumberInArray( CurrTurn, Operat.IncomeCurr_Ref, j );
       end;
       BreakLoop = Next( Operat );
    end;
    return j; /*количество занесенных валют*/
 end;
 return 0;
END;

MACRO ШапкаОтчетаПоОборотам( )
/*шапка*/
PrintDate_n_Time();
[                     ОТЧЕТ ПО ОБОРОТАМ ОБМЕННОГО ПУНКТА
      Адрес о/п: #
  Рег.номер о/п: #

                  ###################   Номер смены #

]( {ExAddress}, {ExRegNumber}, StrAddZeroDate( date ), {SessionNumber}:l );
END;

MACRO ШапкаОднотипныхОборотов( NameCurr, TrsTurn, TypeTurn )
[Ценность:#
      Тип:#
   Валюта:#
+-------------------+-----------------+
|       Время       |  Сумма оборота  |
+-------------------+-----------------+]
( GetNameCodif( TrsTurn,  TrsRoot  ),
  GetNameCodif( TypeTurn, CDF_Turn ),
  NameCurr
);
END;

MACRO ДноОднотипныхОборотов( NumTurns, KredSum, DebSum )
[+-------------------+-----------------+
Кол-во:#
Приход:#
Расход:#
-----------------------------------------------------------------------------
] ( NumTurns:l, KredSum:l, Abs(DebSum):l );

END;

/* Проверяет, какое состояние у операции, соответствующей текущему обороту */
/* RA  2-06-99 */
MACRO СостояниеОперации_ОК( )
   FILE Oper ("operat.dbt", "exchange.def")  key 0; /*ID*/
   Oper.ID = TurnOper.Oper_Ref;
   if( GetEQ( Oper ) )
     if( (Oper.State_Ref == CDF_OperCompleted) OR
         (Oper.State_Ref == CDF_OperInReestr)  )
        return TRUE;
     end;
   end;
   return FALSE;
END;

MACRO ПечатьОтчетаПоОборотам( )
 /*var FIO = GetFIOEmployee( {exoper} ) == {FIO_oper}*/
 array DiffCurrTurn, DiffTrsTurn, DiffTypeTurn;
 var   LoopTurn, i = 0, CurCurr, j = 0, CurTrs, k = 0, CurType, l = 0,
       NumCurr = ЗаполнитьМассивВалютОборотов( DiffCurrTurn, {exoper}, {SessionNumber} ),
       NumTrs  = 5,
       NumType = 8, /* RA 9-02-99 исправил(т.к.убрал 2 CDF-кода), было 10 */
       AllNum  = NumType * NumTrs * NumCurr;

 var   FirstEnter, KredSum = 0, DebSum = 0, NumTurns = 0;

 /*рубли на первое место*/
 MoveElementArray( DiffCurrTurn, {NationalCur}, 0, NumCurr );
 /*доллары на второе место*/
 MoveElementArray( DiffCurrTurn, {BaseCur},     1, NumCurr );
 /*немецкие марки на третье место*/
 MoveElementArray( DiffCurrTurn, 276,           2, NumCurr );

 DiffTrsTurn(0) = TrsCash;
 DiffTrsTurn(1) = TrsMonet;
 DiffTrsTurn(2) = TrsCheck;
 DiffTrsTurn(3) = TrsAccreditive;
 DiffTrsTurn(4) = TrsTratt;

 DiffTypeTurn(0) = CDF_TurnIncome;
 DiffTypeTurn(1) = CDF_TurnOut;
 DiffTypeTurn(2) = CDF_TurnCom;
 DiffTypeTurn(3) = CDF_TurnFix;
 DiffTypeTurn(4) = CDF_TurnTaxWithCom;
 DiffTypeTurn(5) = CDF_TurnTaxWithoutCom;
 DiffTypeTurn(6) = CDF_TurnPComInc;
 DiffTypeTurn(7) = CDF_TurnPComOut;
 /*DiffTypeTurn(8) = CDF_TurnNonDenomIncomeAll; RA 9-02-99 закомментировал*/
 /*DiffTypeTurn(9) = CDF_TurnNonDenomOutAll;    RA 9-02-99 закомментировал*/

 InitProgress( AllNum,
               "Формирование отчета по оборотам за текущую смену",
               "Отчет по оборотам" );

 ШапкаОтчетаПоОборотам();

 /*тело отчетности*/
 while( i < NumCurr )/*цикл по валютам оборотов*/
    CurCurr = DiffCurrTurn(i);

    while( j < NumTrs )/*цикл по ценностям оборотов*/
       CurTrs = DiffTrsTurn(j);

       while( k < NumType )/*цикл по типам оборотов*/
          CurType = DiffTypeTurn(k);

          if( (LoopTurn = Init( {exoper}, {SessionNumber}, 0, CurTrs, CurCurr )) )
            FirstEnter = True;
            while( LoopTurn )/*цикл по оборотам из реестров*/
              if( Filter( {exoper}, {SessionNumber}, 0, date(0,0,0), date(0,0,0), CurCurr, CurType, CurTrs ))
                 if( FirstEnter )
                    ШапкаОднотипныхОборотов( GetNameCurr(CurCurr), CurTrs, CurType );
                    FirstEnter = False;
                 end;
                 if( СостояниеОперации_ОК() )
                    [|########## ########|#################|]
                    ( StrAddZeroDate_Short(TurnOper.Date),
                      TurnOper.Time,
                      TurnOper.Sum
                    );
                    NumTurns = NumTurns + 1;
                    if( TurnOper.Sum > 0 )
                       KredSum  = KredSum + TurnOper.Sum;
                    else
                       DebSum   = DebSum + TurnOper.Sum;
                    end;
                 end;
              end;
              LoopTurn = Next( TurnOper );
              if( (not LoopTurn) AND (not FirstEnter) )
                 ДноОднотипныхОборотов( NumTurns, KredSum, DebSum );
                 KredSum = 0; DebSum = 0; NumTurns = 0;
              end;
            end;

          end;
          k = k + 1;
          l = l + 1;
          UseProgress( l );
       end;
       k = 0;
       j = j + 1;
    end;
    j = 0;
    i = i + 1;
 end;
 RemProgress();
END;

ПечатьОтчетаПоОборотам();