/*-RSL---------------------------------------- R-Style Software Lab --*/
/*--------------- Печать реестра -------------------------------------*/
/* RS-Bank      подсистема Обменный пункт            AV  14.05.97     */
/*AV 13-02-98   скорректировал в соответствии с инструкцией 31-Т      */
/*AV 27-08-98   сделал постраничное формирование реестров             */
/*RA  9-02-99   закомментировал упоминание неденоминированных сумм    */

import ExchangeInter, "rstrfunc.mac", "regnumb.mac";

MACRO ПечатьШапки
   var ВремяКурс;
   var ValFlag = GetRegVal( "RS-RETAIL\\INI\\ПЕРЕМЕННЫЕ\\ОБМЕННЫЙ_ПУНКТ\\SETTIMERATE" );
   if( ValFlag != "OFF" )
      ВремяКурс = ValFlag;
   else
      ВремяКурс = Реестр.RateStartTime;
   end;

[
                                 РЕЕСТР
          НЕПЛАТЕЖНЫХ ДЕНЕЖНЫХ ЗНАКОВ ИНОСТРАННЫХ ГОСУДАРСТВ,
                      КУПЛЕННЫХ ЗА НАЛИЧНЫЕ РУБЛИ
];
if( PRINT_CURRENCY == TRUE )
[
        Валюта:  #
]( GetNameCurr( FindCodCurByRefVal(Реестр.IncomeRefVal) ) );
end;
[
    Курс покупки на текущую дату ##################  ##########  ########
           (указывается время в часах и минутах, с которого действует
                    установленный банком курс покупки)

       Курс Банка России на текущую дату ##################

+---------+-----------+---------------+--------+------------------+----------+--------------+--------------+----------+
|   КОД   |  Номинал  |     Сумма     |  КОД   | Сумма выплаченных| N справки|     КОД      |    Cумма     |Резидент\ |
|купленной|неплатежной|  неплатежной  |наличных|  наличных рублей | ф.0406007| комиссионного| комиссионного|Нерезидент|
|  валюты |  валюты   |    валюты     | рублей |   за купленные   |          |вознаграждения|вознаграждения|          |
|         |           |               |        | неплатежные ден. |          |              |              |          |
|         |           |               |        |  знаки иностр.   |          |              |              |          |
|         |           |               |        | гос-в (цифрами)  |          |              |              |          |
+---------+-----------+---------------+--------+------------------+----------+--------------+--------------+----------+
|    1    |     2     |       3       |    4   |         5        |    6     |       7      |       8      |    9     |
+---------+-----------+---------------+--------+------------------+----------+--------------+--------------+----------+
] ( КурсСНужнойТочностью( Реестр.Rate,   FindCodCurByRefVal(Реестр.IncomeRefVal) ):r,
    StrAddZeroDate_Short( Реестр.RateStartDate ),
    ВремяКурс,
    КурсСНужнойТочностью( Реестр.CBRate, FindCodCurByRefVal(Реестр.IncomeRefVal) )
  );
END;

MACRO ПечатьПодвала
  var ItogoComiss = 0, CodComiss = "", ManyComiss = False, i = 0;

  var NumCurr = GetSumReestrComiss();
  if (NumCurr != 1)
     ManyComiss = TRUE;
  end;
  ItogoComiss = string( abs(ArSum( 0 )) );
  CodComiss   = GetCurrNumericalCode( ArCurr( 0 ) );

[+---------+-----------+---------------+--------+------------------+----------+--------------+--------------+----------+
Итого:                 ###############          ################## ##########

ИТОГО:                 ###############          ################## ##########       ######## ##############
](
    TotalsPage(0),/*Реестр.IncomeSum*/
    TotalsPage(1),/*Реестр.OutSum*/
    TotalsPage(2):c,

    /* VD 4.03.98 обороты по All */
    /*SumNonDenomToString( TotalsPage(3) ):w, RA 9-02-99 закомментировал*/
    /*SumNonDenomToString( TotalsPage(4) ):w, RA 9-02-99 закомментировал*/
    Реестр.IncomeSum,
    Реестр.OutSum,
    TotalsPage(5):c,
    CodComiss,
    ItogoComiss:r
);

  if (ManyComiss)
    i = 1;
    while( i < NumCurr )
       ItogoComiss = string( abs(ArSum( i )) );
       CodComiss   = GetCurrNumericalCode( ArCurr( i ) );
[ИТОГО:                                                                              ######## ##############
](
    CodComiss,
    ItogoComiss:r
 );
       i = i + 1;
    end;
  end;
[        Кассир обменного пункта: _________________________ (#######################)
                                        (подпись)

-----------------------------------------------------------------------------------------------------------------------

](
    Реестр.FIO
 );
END;

MACRO ПечатьПодвала_ОдностраничныйРеестр( )
  var ItogoComiss = 0, CodComiss = "", ManyComiss = False, i = 0;

  var NumCurr = GetSumReestrComiss();
  if (NumCurr != 1)
     ManyComiss = TRUE;
  end;
  ItogoComiss = string( abs(ArSum( 0 )) );
  CodComiss   = GetCurrNumericalCode( ArCurr( 0 ) );

[+---------+-----------+---------------+--------+------------------+----------+--------------+--------------+----------+
ИТОГО:                 ###############          ################## ##########       ######## ##############
](
    Реестр.IncomeSum,
    Реестр.OutSum,
    TotalsPage(5):c,
    CodComiss,
    ItogoComiss:r
 );
  if (ManyComiss)
    i = 1;
    while( i < NumCurr )
       ItogoComiss = string( abs(ArSum( i )) );
       CodComiss   = GetCurrNumericalCode( ArCurr( i ) );
[ИТОГО:                                                                              ######## ##############
](
    CodComiss,
    ItogoComiss:r
 );
       i = i + 1;
    end;
  end;
[        Кассир обменного пункта: _________________________ (#######################)
                                        (подпись)

-----------------------------------------------------------------------------------------------------------------------

](
    Реестр.FIO
 );
END;

MACRO ПечатьПодвалаСтраницы
[+---------+-----------+---------------+--------+------------------+----------+--------------+--------------+----------+
Итого:                 ###############          ################## ##########

        Кассир обменного пункта: _________________________ (#######################)
                                        (подпись)

-----------------------------------------------------------------------------------------------------------------------
#

](
    TotalsPage(0),/*Реестр.IncomeSum*/
    TotalsPage(1),/*Реестр.OutSum*/
    TotalsPage(2):c,/*кол-во справок БСО - страница*/
    /* VD 4.03.98 обороты по All */
    /*SumNonDenomToString( TotalsPage(3) ):w, RA 9-02-99 закомментировал*/
    /*SumNonDenomToString( TotalsPage(4) ):w, RA 9-02-99 закомментировал*/
    Реестр.FIO,
    StrFor(12)
 );
END;

MACRO ПечатьСтрокиРеестра

  /* Восстанавливаем исходные суммы по операции   RA 1-09-99 */
  /* RA 19-06-01 больше не нужно! УчетНекассовыхОборотов( "По операции", СоставРеестра.IncomeAmount, СоставРеестра.OutAmount ); */
  /*var IncNon = MakeNonDenomSum( IncNonDenSum ); RA 9-02-99 закомментировал*/
  /*var OutNon = MakeNonDenomSum( OutNonDenSum ); RA 9-02-99 закомментировал*/
  var Seria, Number;
  GetSprvSerNum( СоставРеестра.ApplicationKind, 
                 СоставРеестра.ApplicationKey, 
                 GetBSOShortName (), Seria, Number );
  Number = string(Number);
  Number = StrFillBeg( Number, "0", 7 ); /* дополнение ведущими нулями */

[|   ###   |###########|###############|   ###  |##################|## #######|      ########|##############|##########|]
(
  GetCurrNumericalCode( FindCodCurByRefVal(Реестр.IncomeRefVal) ),
  GetIncomeNominal( СоставРеестра ),
  СоставРеестра.IncomeAmount,
  GetCurrNumericalCode( FindCodCurByRefVal(Реестр.OutRefVal) ),
  СоставРеестра.OutAmount,
  Seria,
  Number,
  SubStr (MakeComissString():w, 1, 3, 1),
  SubStr (MakeComissString():w, 5, StrLen (Trim(MakeComissString():w)) - 4):r,
/*  MakeComissString():w,*/
  MakeResidentName(СоставРеестра.Resident):c
  /*SumNonDenomToString( IncNon ):w, RA 9-02-99 закомментировал*//*MakeNonDenomSumString( IncNonDenSum ):w,*/
  /*SumNonDenomToString( OutNon ):w  RA 9-02-99 закомментировал*//*MakeNonDenomSumString( OutNonDenSum ):w */
);

 /*подсчет итогов по странице*/
 AddTotalsPage( 0, СоставРеестра.IncomeAmount );
 AddTotalsPage( 1, СоставРеестра.OutAmount    );
 /*AddTotalsPage( 3, IncNon                     ); RA 9-02-99 закомментировал*/
 /*AddTotalsPage( 4, OutNon                     ); RA 9-02-99 закомментировал*/
 if( Seria != "" )
   AddTotalsPage( 2, 1  );/*справки - страница   */
   AddTotalsPage( 5, 1  );/*справки - весь реестр*/
 end;
END;

/*-------------------------------------------------------------------------*/
/* рекурсивная функция - печатает строки реестра с разбиением на страницы  */
/*-------------------------------------------------------------------------*/
MACRO СодержимоеРеестраПостранично_Рекурсия( NumPage, NumberStrings )
  ОбщаяШапка( Реестр.Date, Реестр.UserRegNumb, NumPage );
  ПечатьШапки();
  var LoopSetRstr = True;
  while( (LoopSetRstr == True) and (ФильтрРеестр() == True) )
    NumberStrings = NumberStrings + 1;
    if( ФильтрСтрокаРеестра() )
      ПечатьСтрокиРеестра();
    end;
    LoopSetRstr = next( СоставРеестра );
    if( (LoopSetRstr == True) AND (ФильтрРеестр() == True) AND ( (NumberStrings/NumPage) == MaxNumberStrings) )
       ПечатьПодвалаСтраницы();
       NumPage = NumPage + 1;
       ClearTotalsPage( 5 );
       LoopSetRstr = СодержимоеРеестраПостранично_Рекурсия( NumPage, NumberStrings );
    end;
  end;
  return LoopSetRstr;
END;
/*-------------------------------------------------------------------------*/
/* Вызов рекурсии для печати строк реестра с разбиением на страницы        */
/*-------------------------------------------------------------------------*/
MACRO СодержимоеРеестраПостранично( NumPage, NumberStrings )
   if( InitSetRstrKey() )
      SetRegNumb( Реестр ); /* Заполним Реестр.UserRegNumb, если он пустой */
      СодержимоеРеестраПостранично_Рекурсия( NumPage, NumberStrings );
   end;
   SetParm( 0, NumPage ); SetParm( 1, NumberStrings );
END;
/*-------------------------------------------------------------------------*/
/*    печать реестра данного типа для одной пары валют                     */
/*-------------------------------------------------------------------------*/
MACRO ПечатьОдногоРеестра( Reestr )
   var НомерСтраницы = 1;
   var НомерСтроки   = 0;
   ClearTotalsPage( 6 );/*вначале обнуляем весь массив*/
   setbuff( r , Reestr );
   asize( ArSum,  0 ); /* суммы комиссий  */
   asize( ArCurr, 0 ); /* валюты комиссий */

   if( FindReestr( r ))
       /* Восстанавливаем исходные суммы по реестру  RA 16-12-99 */
       /* RA 19-06-01 больше не нужно! УчетНекассовыхОборотов( "По реестру", Реестр.IncomeSum, Реестр.OutSum ); */
       СодержимоеРеестраПостранично( НомерСтраницы, НомерСтроки );
       if( НомерСтраницы > 1 )
          ПечатьПодвала();
       else
          ПечатьПодвала_ОдностраничныйРеестр();
       end;
   end;
END;

/*-------------------------------------------------------------------------*/
/*    печать всех реестров данного типа                                    */
/*-------------------------------------------------------------------------*/
MACRO ПечатьВсехРеестров( Kind_Oper )
   var НомерСтраницы = 1;
   var НомерСтроки   = 0;
   var LoopRstr      = True;
   if(InitReestrKey( Kind_Oper ))
     PrintDate_n_Time();
     while( LoopRstr and ФильтрШапки( Kind_Oper ) )
       ClearTotalsPage( 6 );/*вначале обнуляем весь массив*/
       asize( ArSum,  0 ); /* суммы комиссий  */
       asize( ArCurr, 0 ); /* валюты комиссий */
       /* Восстанавливаем исходные суммы по реестрам   RA 1-09-99 */
       /* RA 19-06-01 больше не нужно! УчетНекассовыхОборотов( "По реестру", Реестр.IncomeSum, Реестр.OutSum ); */
       СодержимоеРеестраПостранично( НомерСтраницы, НомерСтроки );/*рекурсия*/
       if( НомерСтраницы > 1 )
          ПечатьПодвала();
       else
          ПечатьПодвала_ОдностраничныйРеестр();
       end;
       LoopRstr =  next( Реестр );
     end;
   end;
END;

