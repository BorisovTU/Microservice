/****************************************************************************
   RS-Bank        Обменный пункт
   sb_jrnl.mac - формирование журнала смен курсов валют для Сбербанка
   Copyright 1998 (c) R-Style Software Lab.
   Создание: 19-06-98 by AV
****************************************************************************/
IMPORT ExchangeInter, BankInter, CommonInter, "define.mac", "office.mac", "sqlconv.mac";

var СменаКурсов : TBFile,
    Курс        : TBFile; /* Смена-КодВалюты-Котируемость*/
var {curdate};
/* Панель ввода диапазона дат */
record dl("P_DATE", "exchange.lbr") dialog;

/****************************************************************************/
/*  Обработчик панели ввода диапазона дат                                              */
/****************************************************************************/
MACRO ДипазонДат( dlg, cmd, id, key )

  if( cmd == DLG_INIT )
    dl.BeginDate = {curdate};
    dl.EndDate   = {curdate};
    UpdateFields(dl);
  end;

  if (cmd == DLG_KEY)
     if( key == 323 )
       if( ( {curdate} < dl.BeginDate ) OR ( {curdate} < dl.EndDate ) OR
                       ( dl.BeginDate > dl.EndDate ) )
         msgbox("Неверный ввод дат");
         return CM_IGNORE;
       end;
     end;
  end;

  return CM_DEFAULT;
END;

/****************************************************************************/
/****************************************************************************/
MACRO ШапкаКурсов()
[
+---------------------+------------+----------------+----------------+---------------+----------------+
|       Валюта        |   Номинал  |    Курс ЦБ     |  Курс продажи  |  Курс покупки |   Кросс-курс   |
];
END;
/****************************************************************************/
/****************************************************************************/
MACRO СтрокаКурса()

  if ( Курс.rec.MinSum != 0 )
[|    от ############  |            |                |################|###############|                |]
         (
           Курс.rec.MinSum:12:2:a,
           КурсСНужнойТочностью( Курс.rec.SellRate, Курс.rec.Curr_Ref ):r,
           КурсСНужнойТочностью( Курс.rec.BuyRate , Курс.rec.Curr_Ref ):r
         )
  else
[+---------------------+------------+----------------+----------------+---------------+----------------+
|#####################|############|################|################|###############|################|]
         ( GetNameCurr(Курс.rec.Curr_Ref):l,
           Курс.rec.ChangeNom,
           КурсСНужнойТочностью( Курс.rec.CBRate  , Курс.rec.Curr_Ref ):r,
           КурсСНужнойТочностью( Курс.rec.SellRate, Курс.rec.Curr_Ref ):r,
           КурсСНужнойТочностью( Курс.rec.BuyRate , Курс.rec.Curr_Ref ):r,
           ExecExp( "String( Курс.rec.CrossRate:0:" + Office.CrossRoundNum + ")" ):r
         );
  end;
END;
/****************************************************************************/
/****************************************************************************/
MACRO ДноКурсов()
[+---------------------+------------+----------------+----------------+---------------+----------------+
    Операционист  ____________  #
](  GetFIOEmployee( СменаКурсов.rec.Emp_Ref ) );
END;
/****************************************************************************/
/*  Процедура выводит информацию по всем курсам смены курсов "НомерСмены"   */
/****************************************************************************/
MACRO КурсыВалютСмены( НомерСмены )
   var Loop;

   Курс.addFilter( "t.T_CHANGE_REF = " + String( НомерСмены ) );

   Курс.rec.Change_Ref = НомерСмены;
   Курс.rec.Curr_Ref   = 0;
   Курс.rec.RateFlag   = 0;

   if( Loop = Курс.GetGE() )
      ШапкаКурсов();
      while( Loop == True )
         if( Курс.rec.Change_Ref == НомерСмены )
           СтрокаКурса();
           Loop = Курс.next();
         else
           Loop = False;
         end;
      end;
      ДноКурсов();
   end;

   Курс.dropFilter();

END;


/****************************************************************************/
/****************************************************************************/
MACRO ШапкаЖурнала()
PrintDate_n_Time();
[
                      ЖУРНАЛ РЕГИСТРАЦИИ СМЕН КУРСОВ ВАЛЮТ
                             от #


]
( StrAddZeroDate( {curdate} )
);
END;
/****************************************************************************/
/****************************************************************************/
MACRO ИнформацияОСменеКурсов()
[ ----------------+----------------------+---------------------+------------------------+
   СМЕНА КУРСОВ   | Номер и дата приказа | Дата и время начала | Дата и время получения |
                  |                      |      действия       |    информации о курсе  |
  ----------------+----------------------+---------------------+------------------------+
  ################|######################| ########## ######## | ########## ########    |
  ----------------+----------------------+---------------------+------------------------+
]
( СменаКурсов.rec.Descr:w,
  СменаКурсов.rec.DictateInfo:w,
  StrAddZeroDate_Short( СменаКурсов.rec.StartDate ),
  СменаКурсов.rec.StartTime,
  StrAddZeroDate_Short( СменаКурсов.rec.SetDate ),
  СменаКурсов.rec.SetTime
);
  /* вывод курсов данной смены курсов валют */
  КурсыВалютСмены( СменаКурсов.rec.ID );

END;

/****************************************************************************/
/****************************************************************************/
MACRO  РазделительСменКурсов()
[
______________________________________________________________________________________________________

]
END;

/***************************************************************************/
/*  Процедура формирования журнала регистрации смен курсов валют           */
/***************************************************************************/
MACRO SB_Journal()

   СменаКурсов = TBFile( "ratechng.dbt", "R", 4, "ratechng.dbt", "exchange.def" );
   Курс        = TBFile( "cur_rate.dbt", "R", 0, "cur_rate.dbt", "exchange.def" );

   if( not RunDialog( dl, "ДипазонДат" ) )
      exit(1);
   end;

   ШапкаЖурнала();
   СменаКурсов.rewind();

   СменаКурсов.addFilter( "( t.T_STATE_REF <> 2 ) AND " +
                          "( t.T_STARTDATE >= " + sqlDateToStr( dl.BeginDate ) + " AND " +
                          "  t.T_STARTDATE <= " + sqlDateToStr( dl.EndDate   ) +
                          ") AND t.T_BRANCH = " + String( Office.BranchForRate ) );

   while( next( СменаКурсов ) )
      if( ( ( СменаКурсов.rec.State_Ref != 2             ) ) and
          ( ( СменаКурсов.rec.StartDate >= dl.BeginDate  ) and
            ( СменаКурсов.rec.StartDate <= dl.EndDate    ) ) and
          ( СменаКурсов.rec.Branch == Office.BranchForRate )     )
        ИнформацияОСменеКурсов();
        РазделительСменКурсов();
      end;
   end;

   СменаКурсов.dropFilter();

END;

/* формирование регистрационного журнала смен курсов валют */
SB_Journal();