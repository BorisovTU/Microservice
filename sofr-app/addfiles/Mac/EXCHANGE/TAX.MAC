/*-RSL---------------------------------------- R-Style Software Lab --*/
/*-------------------- Ведомость учета налога ------------------------*/
/* RS-Bank        Обменный пункт                       VD 3.12.97     */
/* AV 3-09-98 исправления для учета замечаний СБ                      */
/* RA 6-05-99 переименовал из taxlist.mac                             */

var ReportDate: date;

/* максимальное количество записей на одной странице */
CONST MaxNumberTaxStrings = 20;

macro  ФильтрШапки_НалогВедомость( Kind_Oper )
 if(( Реестр.Branch         == Branch           ) AND /* N ОП в банке            */
    ( Реестр.CurDate        == ReportDate       ) AND /* Дата опердня            */
    ( Реестр.Oper           == {oper}           ) AND /* Номер сотрудника        */
    ( Реестр.Kind_Oper      == Kind_Oper        )     /* Вид операции            */
   )
   return True;
 end;
 return False;
end;

MACRO ИнициализацияРеестров_НалогВедомость( Kind_Oper )
  ClearRecord( Реестр );
  Реестр.Branch         =  Branch;           /* N ОП в банке            */
  Реестр.CurDate        =  ReportDate;       /* Дата опердня            */
  Реестр.Oper           =  {oper};           /* Код сотрудника          */
  Реестр.Kind_Oper      =  Kind_Oper;        /* Вид операции            */
  Реестр.IncomeRefVal   =  ANY_CURR;         /* Полученная ценность     */
  Реестр.OutRefVal      =  ANY_CURR;         /* Выданная ценность       */

  if( getGE( Реестр ) == True )
     return (
       ( Реестр.Branch         ==  Branch           )  AND
       ( Реестр.CurDate        ==  ReportDate       )  AND
       ( Реестр.Oper           ==  {oper}           )  AND
       ( Реестр.Kind_Oper      ==  Kind_Oper        )
     );
  else return False;
  end;
END;


/* шапка реестра ведомости учета налога (в разрезе кода и смены курсов) */
MACRO ПечатьШапки( DateReestr, NumReestr, NumPage )
[
  ___________#_____________            __________________#_____________________
  (дата составления отчета)            (регистрационный номер обменного пункта)

  Курс ЦБ РФ   #

  Курс продажи ##################

                        ВЕДОМОСТЬ УЧЕТА НАЛОГА
    НА ПОКУПКУ ИНОСТРАННЫХ ДЕНЕЖНЫХ ЗНАКОВ И ПЛАТЕЖНЫХ ДОКУМЕНТОВ,
                   ВЫРАЖЕННЫХ В ИНОСТРАННОЙ ВАЛЮТЕ
                       Смена за  #
(### стр.###)
+----+-------------------------+----------+----------------------+------------------+------------------+
|Nп/п|         Клиент          |N справки |        Сумма         | Налогооблагаемая |   Сумма налога   |
|    | Резидент Ф.И.О. Документ|ф. 0406007|  иностранной валюты  |   база (руб.)    |                  |
+----+-------------------------+----------+----------------------+------------------+------------------+
](
  StrAddZeroDate( date ):c,
  Office.RegNumb:c,
  Реестр.CBRate:l,
  Реестр.Rate:l,
  StrAddZeroDate( ReportDate ),
  GetCurrNumericalCode( FindCodCurByRefVal(СоставРеестра.OutRefVal) ),
  NumPage:c
);
END;

/* Расчет налогооблагаемой базы */
MACRO GetTaxBase( Op )
  return Op.TaxBase;
/*
  if(Op.TaxBase != 0.) /* Если налогооблагаемая база сохранялась при совершении операции */
    return Op.TaxBase;
  else /* Для старых версий ОП - расчет налогоблагаемой базы на основе ТЕКУЩИХ настроек ОП */
    return money(Op.TaxValue / Office.SellTax);
  end;
*/
END;

/*-------------------------------------------------------------------------*/
/*-------------------------------------------------------------------------*/
MACRO ПечатьПодвала( )
[+----+-------------------------+----------+----------------------+------------------+------------------+
ИТОГО:                          ########## ###################### ################## ##################


        Кассир обменного пункта: _________________________ (#                     )
                                        (подпись)

--------------------------------------------------------------------------------------------------------------------------------------
#
](
   TotalsPage( 4 ):c,  TotalsPage( 5 ), TotalsPage( 6 ), TotalsPage( 7 ),
   Реестр.FIO,
   StrFor(12)
  );
END;

/*-------------------------------------------------------------------------*/
/*-------------------------------------------------------------------------*/
MACRO ПечатьПодвалаСтраницы( )
[+----+-------------------------+----------+----------------------+------------------+------------------+
Итого:                          ########## ###################### ################## ##################

        Кассир обменного пункта: _________________________ (#                     )
                                        (подпись)

--------------------------------------------------------------------------------------------------------------------------------------


](
   TotalsPage( 0 ):c,  TotalsPage( 1 ), TotalsPage( 2 ), TotalsPage( 3 ),
   Реестр.FIO
  );
END;


MACRO ПечатьСтрокиРеестра

  var Seria, Number;
  var ClientInfo = СоставРеестра.Resident                     + " " +
                   СоставРеестра.Sname                        + " " +
                   СоставРеестра.Name                         + " " +
                   СоставРеестра.Pname;
  if( СоставРеестра.ClientDocNumber or СоставРеестра.ClientDocSeria )
    ClientInfo = ClientInfo                                   + " " +
                 PaperTypeName( СоставРеестра.ClientDocType ) + " " +
                 СоставРеестра.ClientDocSeria                 + " " +
                 СоставРеестра.ClientDocNumber;
  end;
  GetSprvSerNum( СоставРеестра.ApplicationKind, 
                 СоставРеестра.ApplicationKey, 
                 GetBSOShortName (), Seria, Number );
  Number = string(Number);
  Number = StrFillBeg( Number, "0", 7 ); /* дополнение ведущими нулями */

[|####|#########################|## #######|### ##################|##################|##################|]
(
  TotalsPage(8):c,
  /*СоставРеестра.DocInfo:w,*/  /* 28.08.2000 VS Добавил резидентность и данные документа */
  ClientInfo:w,
  Seria,
  Number,
  GetCurrNumericalCode( FindCodCurByRefVal(СоставРеестра.OutRefVal) ),
  СоставРеестра.OutAmount,
  GetTaxBase(СоставРеестра),
  СоставРеестра.TaxValue
);
 /* подсчет итогов по странице */
 AddTotalsPage( 0, 1 );/* справки на странице */
 AddTotalsPage( 1, СоставРеестра.OutAmount   );
 AddTotalsPage( 2, GetTaxBase(СоставРеестра) );
 AddTotalsPage( 3, СоставРеестра.TaxValue    );
 /* подсчет итогов по всей ведомости */
 AddTotalsPage( 4, 1 );/* справки по всей ведомости */
 AddTotalsPage( 5, СоставРеестра.OutAmount   );
 AddTotalsPage( 6, GetTaxBase(СоставРеестра) );
 AddTotalsPage( 7, СоставРеестра.TaxValue    );
 AddTotalsPage( 8, 1 );/* порядковый номер записи */

END;

/*-------------------------------------------------------------------------*/
/* рекурсивная функция - печатает строки реестра с разбиением на страницы  */
/*-------------------------------------------------------------------------*/
MACRO СодержимоеРеестраПостранично_Рекурсия( NumPage, NumberStrings )
  /*ОбщаяШапка( Реестр.Date, Реестр.UserRegNumb, NumPage );*/
  ПечатьШапки( Реестр.Date, Реестр.UserRegNumb, NumPage );
  var LoopSetRstr = True;
  while( (LoopSetRstr == True) and (ФильтрРеестр() == True) )
    NumberStrings = NumberStrings + 1;
    if( ФильтрСтрокаРеестра() AND СоставРеестра.TaxValue )/* если есть налог по операции и операция не удалена */
       ПечатьСтрокиРеестра();
    end;
    LoopSetRstr = next( СоставРеестра );
    if( (LoopSetRstr == True) AND (ФильтрРеестр() == True) AND ( (NumberStrings/NumPage) == MaxNumberTaxStrings) )
       ПечатьПодвалаСтраницы();
       NumPage = NumPage + 1;
       ClearTotalsPage( 4 );
       LoopSetRstr = СодержимоеРеестраПостранично_Рекурсия( NumPage, NumberStrings );
    end;
  end;
  return LoopSetRstr;
END;
/*-------------------------------------------------------------------------*/
/* Вызов рекурсии для печати строк реестра с разбиением на страницы        */
/*-------------------------------------------------------------------------*/
MACRO СодержимоеРеестраПостранично( NumPage, NumberStrings )
   if( InitSetRstrKey() )
      СодержимоеРеестраПостранично_Рекурсия( NumPage, NumberStrings );
   end;
   SetParm( 0, NumPage ); SetParm( 1, NumberStrings );
END;
/*-------------------------------------------------------------------------*/
/*    печать ведомости учета налогов по одному реестру                     */
/*-------------------------------------------------------------------------*/
MACRO ПечатьОдногоРеестра( Reestr )
   var НомерСтраницы = 1;
   var НомерСтроки   = 0;
   ClearTotalsPage( 9 );/* вначале обнуляем весь массив */
   TotalsPage( 8 ) = 1; /* номер записи в ведомости     */
   setbuff( r, Reestr );
   if( FindReestr( r ))
       СодержимоеРеестраПостранично( НомерСтраницы, НомерСтроки );
       ПечатьПодвала();
   end;
END;

/*-------------------------------------------------------------------------*/
/*    печать полной ведомости учета налогов за смену                       */
/*-------------------------------------------------------------------------*/
MACRO ПечатьНалоговойВедомостиДляТипаРеестра( Kind_Oper )
   var НомерСтраницы = 1;
   var НомерСтроки   = 0;
   var LoopRstr      = True;
   ClearTotalsPage( 9 );/* вначале обнуляем весь массив */
   TotalsPage( 8 ) = 1; /* номер записи в ведомости     */
   if( ИнициализацияРеестров_НалогВедомость( Kind_Oper ) )
     while( LoopRstr and ФильтрШапки_НалогВедомость( Kind_Oper ) )
       СодержимоеРеестраПостранично( НомерСтраницы, НомерСтроки );/* рекурсия */
       ПечатьПодвала();
       LoopRstr =  next( Реестр );
       ClearTotalsPage( 9 );/* обнуляем весь массив     */
       TotalsPage( 8 ) = 1; /* номер записи в ведомости */
     end;
   end;
END;

/* генерация полной ведомости по учету налогов */
MACRO TaxList()
      PrintDate_n_Time();
      /* продажа наличной ин.валюты физическим лицам */
      ПечатьНалоговойВедомостиДляТипаРеестра( CDF_OperCurrSale );
      /* продажа платежных документов в ин.валюте физическим лицам */
      ПечатьНалоговойВедомостиДляТипаРеестра( CDF_OperPayDocSale );
      /* продажа платежных документов в ин.валюте физическим лицам через счет */
      ПечатьНалоговойВедомостиДляТипаРеестра( CDF_OperPayDocSaleAcc );
END;
