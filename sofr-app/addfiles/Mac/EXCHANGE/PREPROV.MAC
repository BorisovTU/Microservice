/*-RSL---------------------------------------- R-Style Software Lab --*/
/*------------------ Печать препроводительной ведомости --------------*/
/*---------------------- к инкассаторской сумке ----------------------*/
/* RS-Bank      подсистема Обменный пункт            AV  18.05.97     */
/*                                                   VD  29.09.97     */
/*RA  9-02-99   закомментировал упоминание неденоминированных сумм    */
/*31.10.2000 VS Вставил различие ПД, купленных за валюту и за "рубли",*/
/*              привел в порядок текст.                               */
/*RA 21-01-02   Переделал под ретейловские файлы передачи             */

Import DeskInter, ExchangeInter, "define.mac", "RD_Comm.mac", "office.mac";

private file GroupMember ( "groupmem.dbt", "sbbank.def" ) key 0;
private file BagList     ( "bag.dbt"     , "sbbank.def" ) key 0;
private file RegNum      ( "reg_num.dbt" , "sbbank.def" ) key 0;
private file CashVal     ( "sb_casvl.dbt", "sbbank.def" ) key 3;
private file CashDC      ( "sb_casdc.dbt", "sbbank.def" ) key 0;
private file Bunch       ( "bunch.dbt"   , "sbbank.def" ) key 0;

const 
      БланкиПД     = 1,
      Валюта_и_ПД  = 2,
      БСО_ValueRef = 6,
      БСО_IntCode  = 6,
      Ф31_ValueRef = 51,
      Ф31_IntCode  = 31;

const 
      CASHOP_DEBIT     = 5,  /* Расход */

      TYPEDOC_CARRYOFF = 1,  /* Отложен (не подтвержден) */
      TYPEDOC_CARRYON  = 2;  /* Проведен (подтвержден)   */

private var 
  НомерВедомости = 0,
  ProgID = StrFor( GetProgramID() ), 
  PrintFIO = GetRegSetting( V_INTEGER, "КЛАДОВАЯ\\ФИО_ИНКАС_В_ОПИСИ" ), 
  ColName;

/* Поиск ценности */
MACRO FindCashValueIntCode( Type, TypeValue, CodIntValue )

  CashVal.Type        = Type;
  CashVal.TypeValue   = TypeValue;
  CashVal.CodIntValue = CodIntValue;

  return GetEQ(CashVal);
END;

MACRO GetNativeSafe()

  file Group("rtgroup.dbt") key 2;

  var
     OperBrigade = GetOperBrigade();
 
  Group.Branch = Branch;
  Group.Date = {curdate};
  Group.ID = OperBrigade;
  if(GetEQ(Group))
    return Group.SafeID;
  else
    MsgBox("Не найден сейф бригады № ", OperBrigade);
    Exit;
  end;
END;

MACRO WorkDeskName( Oper, Brigade )
  var Name, name1;

  if ( Oper == 0 )
    Name = "0";
  else
    Name = string(Oper);    Name = mkstr( "0", 4 - strlen(Name )) + Name;
    name1= string(Brigade); name1= mkstr( "0", 2 - strlen(name1)) + name1;
    Name = Name + "\/" + name1;
  end;
  return Name;  
END;

/*  Формирует строку из серий и номеров ценностей */
MACRO MakeRegNumString( Series, MinNumber, MaxNumber, Num, Str )
  Num = Num + (MaxNumber - MinNumber + 1);
  if( Str != "" )
    Str = Str + ", ";
  end;
  Str = Str + Series + " " + string(MinNumber);
  if( MinNumber != MaxNumber )
    Str = Str + "-" + string(MaxNumber);
  end;
  SetParm( 3, Num );
  SetParm( 4, Str );
END;

/* Подсчитывает кол-во данного ресурса и формирует строку из серий-номеров */
MACRO ПачкиСправок( ValueRef, Num, Str, Parm )

  var
    RecFound,
    BunchFound,
    NominalCost,
    TypeDoc,
    CashOper;
  
  Num = 0; Str = "";
  if  ( Parm == "ВСумке"   )
    TypeDoc  = CashDoc.TypeDoc;
    CashOper = CashDoc.CashOper;
  elif( Parm == "Выданные" )
    TypeDoc  = TYPEDOC_CARRYON;
    CashOper = WorkDeskName( GroupMember.Oper, CashDoc.Brigade );
  end;
 
  ClearRecord( CashDC );
  CashDC.Branch      = CashDoc.Branch;
  CashDC.CashDateDoc = CashDoc.CashDateDoc;
  CashDC.TypeDoc     = TypeDoc;
  CashDC.CashOper    = CashOper;
  CashDC.RefValue    = ValueRef;
  RecFound = GetGE( CashDC ); 

  while (     RecFound
        and ( CashDC.Branch      == CashDoc.Branch      )
        and ( CashDC.CashDateDoc == CashDoc.CashDateDoc )
        and ( CashDC.TypeDoc     == TypeDoc             )
        and ( CashDC.CashOper    == CashOper            )
        and ( CashDC.RefValue    == ValueRef            )
        )
    if( 
         ( ( Parm == "ВСумке"   ) and ( CashDC.BagID   == CashDoc.BagID ) )
      or ( ( Parm == "Выданные" ) and ( CashDC.NumOper == CASHOP_DEBIT  ) )
      )

      /* не было детализации - возьмем из кассового документа */
      if( CashDC.Series != "" )
        MakeRegNumString( CashDC.Series, 
                          CashDC.MinRegistrNumber, 
                          CashDC.MaxRegistrNumber, 
                          Num, Str );
      /* была детализация - возьмем из бунчей */
      else
        ClearRecord( Bunch );
        Bunch.BagAppKind = CashDC.ApplicationKind;
        Bunch.BagAppKey  = CashDC.ApplicationKey;
        Bunch.Nominal    = {DMONEY_MAX};
        BunchFound = GetGE( Bunch );
      
        while (     BunchFound
              and ( Bunch.BagAppKind == CashDC.ApplicationKind )
              and ( Bunch.BagAppKey  == CashDC.ApplicationKey  )
              )
          MakeRegNumString( Bunch.Series, 
                            Bunch.Min, 
                            Bunch.Max, 
                            Num, Str );
          BunchFound = Next( Bunch );
        end;
      end;
    end;

    RecFound = Next( CashDC );
  end;

  /*
  ClearRecord( BagList );
  BagList.Date       = Bag.Date;
  BagList.ClientType = Bag.ClientType;
  BagList.ClientID   = Bag.ClientID;
  BagList.Operation  = Bag.Operation;
  BagList.Number     = Bag.Number;
  BagList.ValueRef   = ValueRef;
  RecFound = GetGE( BagList );

  while ( RecFound
    and ( BagList.Date       == Bag.Date       )
    and ( BagList.ClientType == Bag.ClientType )
    and ( BagList.ClientID   == Bag.ClientID   )
    and ( BagList.Operation  == Bag.Operation  )
    and ( BagList.Number     == Bag.Number     ) 
    and ( BagList.ValueRef   == ValueRef       )
    )
    Num = Num + BagList.DeclaredItems;
    if( Str != "" )
      Str = Str + ", ";
    end;
    Str = Str + BagList.Series + " " 
              + string(BagList.MinRegNum) + "-" 
              + string(BagList.MinRegNum + BagList.DeclaredItems - 1);
    RecFound = Next( BagList );
  end;
  */
  SetParm( 1, Num );
  SetParm( 2, Str );
END;

MACRO ПачкиСправокВСумке( ValueRef, Num, Str )
  ПачкиСправок( ValueRef, Num, Str, "ВСумке" );
  SetParm( 1, Num );
  SetParm( 2, Str );
END;

MACRO ПачкиВыданныхСправок( ValueRef, Num, Str )
  
  var 
      RecFound,
      OperBrigade = GetOperBrigade(),
      Num_,
      Str_ ;

  Num = 0;
  Str = "";

  /* по всем операционистам */
  ClearRecord( GroupMember );
  GroupMember.Branch = Branch;
  GroupMember.Date   = CashDoc.CashDateDoc;
  GroupMember.Group  = OperBrigade;
  RecFound = GetGE( GroupMember );

  while(       RecFound
         and ( GroupMember.Branch == Branch              )
         and ( GroupMember.Date   == CashDoc.CashDateDoc )
         and ( GroupMember.Group  == OperBrigade         )
       )

    ПачкиСправок( ValueRef, Num_, Str_, "Выданные" );
    Num = Num + Num_;
    Str = Str + Str_;

    RecFound = Next( GroupMember );
  end;

  SetParm( 1, Num );
  SetParm( 2, Str );
END;

MACRO GetNominalCost()
  if ( CashValue.UnitMeas == RESMESUNIT_POINT )
    return $1 * BagList.DeclaredItems;
  else
    return BagList.DeclaredSum;
  end;
END;

MACRO Filter( Mode, ФлагВал )

  var 
    Result = false;
  
  if  (     ( ФлагВал               == false            )
        and ( CashValue.TypeValue   == TYPERES_CURRENCY )
        and ( CashValue.CodIntValue == {NationalCur}    )
      )
    Result = true;

  elif(     (                Mode   == БланкиПД         )
        and (             ФлагВал   == true             )
        and ( CashValue.TypeValue   == TYPERES_VALFORM  )
        and ( CashValue.CodIntValue != БСО_IntCode      )
        and ( CashValue.CodIntValue != Ф31_IntCode      )
      )
    Result = true;

  elif(     (                Mode   == Валюта_и_ПД      )
        and (             ФлагВал   == true             )
        and ( CashValue.TypeValue   != TYPERES_VALFORM  )
      )
    Result = true;
  end;

  return Result;
END;

/* Печатает строки ценостей - рублей, бланков ПД или валютных ценностей */
MACRO СтрокиЦенностейВСумке( Mode, ФлагВал, Total, TotalRub )

  var
    RecFound,
    Stat = true,
    NominalCost,
    RubCost,
    CBRate,
    CodeCurr,
    tm;
  
  ClearRecord( BagList );
  BagList.Branch     = Bag.Branch;
  BagList.Date       = Bag.Date;
  BagList.ClientType = Bag.ClientType;
  BagList.ClientID   = Bag.ClientID;
  BagList.Operation  = Bag.Operation;
  BagList.Number     = Bag.Number;
  RecFound = GetGE( BagList );

  while ( RecFound and Stat
    and ( BagList.Branch     == Bag.Branch     )
    and ( BagList.Date       == Bag.Date       )
    and ( BagList.ClientType == Bag.ClientType )
    and ( BagList.ClientID   == Bag.ClientID   )
    and ( BagList.Operation  == Bag.Operation  )
    and ( BagList.Number     == Bag.Number     ) 
    )
    Stat = FindCashValue( BagList.ValueRef );
    
    if ( Stat and ( Filter( Mode, ФлагВал ) ) )

      tm = null;
      if( Bag.ApplicationKey != "" )
        tm = Time( int( substr( Bag.ApplicationKey, 11, 2 ) ),
                   int( substr( Bag.ApplicationKey, 13, 2 ) ),
                   int( substr( Bag.ApplicationKey, 15, 2 ) ) );
      end;
      CodeCurr    = FindCodCurByRefVal( BagList.ValueRef );
      NominalCost = GetNominalCost();
      GetAllCurrRate (Bag.Date, CodeCurr, CBRate, null, null, tm);
      RubCost     = money( NominalCost * CBRate );

      if  ( Mode == БланкиПД )
        [  ############################################################### ######]
        (
          CashValue.NameValue:w,
          BagList.DeclaredItems
        );
      elif( Mode == Валюта_и_ПД )
        [  |#######################################| ###  |#############|##############| ]
        (
          CashValue.NameValue:w,
          GetCurrNumericalCode(CodeCurr),
          NominalCost,
          RubCost
        );
        Total    = Total    + NominalCost;
        TotalRub = TotalRub + RubCost;
      end;

    end;

    RecFound = Next( BagList );
  end;

  if ( not Stat )
    MsgBox( "Ошибка при печати препроводительной ведомости" );
    Exit( -1 );
  end;

  SetParm( 2, Total    );
  SetParm( 3, TotalRub );
END;

/* Подсчитывает кол-во данного ресурса и формирует строку из серий-номеров */
MACRO ПачкиИспорченныхСправокОдногоСубъекта( ValueRef, Num, Str, CashOper )
   
  var
    RecFound;

  Num = 0; 
  Str = "";

  ClearRecord( RegNum );
  RegNum.Branch     = CashDoc.Branch;
  RegNum.CashOper   = CashOper;
  RegNum.SubAccount = "";
  RegNum.ValueRef   = ValueRef;
  RegNum.Date       = Bag.Date;
  RegNum.Nominal    = {DMONEY_MAX};
  RecFound = GetGE( RegNum );

  while (     RecFound
        and ( RegNum.Branch     == CashDoc.Branch )
        and ( RegNum.CashOper   == CashOper       )
        and ( RegNum.SubAccount == ""             )
        and ( RegNum.ValueRef   == ValueRef       )
        and ( RegNum.Date       == Bag.Date       )
      /*and ( RegNum.Nominal    == 0              )*/
        )
    MakeRegNumString( RegNum.Series,
                      RegNum.Min,
                      RegNum.Max,
                      Num, Str );    
    RecFound = Next( RegNum );
  end;

  SetParm( 1, Num );
  SetParm( 2, Str );
END;

/* Подсчитывает кол-во данного ресурса и формирует строку из серий-номеров */
MACRO ПачкиИспорченныхСправок( ValueRef, Num, Str )

  var
      RecFound,
      CashOper,
      OperBrigade = GetOperBrigade(),
      Num_ = 0,
      Str_ = "";
  
  Num = 0;
  Str = "";

  /* по сейфу смены */
  CashOper = GetNativeSafe();
  ПачкиИспорченныхСправокОдногоСубъекта( ValueRef, Num_, Str_, CashOper );
  Num = Num + Num_;
  Str = Str + Str_;

  /* по всем операционистам */
  ClearRecord( GroupMember );
  GroupMember.Branch = Branch;
  GroupMember.Date   = CashDoc.CashDateDoc;
  GroupMember.Group  = OperBrigade;
  RecFound = GetGE( GroupMember );

  while(       RecFound
         and ( GroupMember.Branch == Branch              )
         and ( GroupMember.Date   == CashDoc.CashDateDoc )
         and ( GroupMember.Group  == OperBrigade         )
       )

    CashOper = WorkDeskName( GroupMember.Oper, CashDoc.Brigade );
    ПачкиИспорченныхСправокОдногоСубъекта( ValueRef, Num_, Str_, CashOper );
    Num = Num + Num_;
    Str = Str + Str_;

    RecFound = Next( GroupMember );
  end;

  SetParm( 1, Num );
  SetParm( 2, Str );
END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Препроводительная ведомость. Лицевая сторона                           │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO ПрепроводительнаяВедомостьЛицевая( CopyNo, ФлагВал )
  var 
      Num, Str;

[ # экз.                                                                 ]( CopyNo );
[                                                                        ];
[                  ПРЕПРОВОДИТЕЛЬНАЯ ВЕДОМОСТЬ N #                       ]( НомерВедомости );
[          К ИНКАССАТОРСКОЙ СУМКЕ N ##### С ВАЛЮТНЫМИ И ДРУГИМИ          ]( BagNum.NumberBag );
[                              ЦЕННОСТЯМИ                                ];
[                                                                        ];
[                                  #                                     ]( StrAddZeroDate( Bag.Date ):c );
[                                                                        ];
[      Указанные на  обороте  ценности отправлены из обменного пункта    ];
[  ##################################################################    ]( Office.Address + ", N " + Office.RegNumb );
[  __________________________________________________________________    ];
[  (адрес  обменного  пункта,  его   номер,   если   обменный   пункт    ];
[  зарегистрирован в установленном порядке).                             ];

  if( ФлагВал )                                 /* Печать справок ф.0406007 */
    ПачкиСправокВСумке( БСО_ValueRef, Num, Str );
[                                                                        ];
[      В сумку  N #____  вложены  неиспользованные   бланки   строгой    ]( BagNum.NumberBag );
[  отчетности "Справка ф. N 0406007":                                    ];
[                                                                        ];
[      Количество _#_________________________________________________    ]( Num );
[      ################################################################# ]( "Номера "+Str:w );
  end;

  if( ФлагВал and (Office.UseF31 == SET_CHAR) ) /* Печать справок ф.31 */
    ПачкиСправокВСумке( Ф31_ValueRef, Num, Str );
[                                                                        ];
[      В сумку  N #____  вложены  неиспользованные   бланки   строгой    ]( BagNum.NumberBag );
[  отчетности "Справка ф. N 31":                                         ];
[                                                                        ];
[      Количество _#_________________________________________________    ]( Num );
[      ################################################################# ]( "Номера "+Str:w );
  end;

  if( ФлагВал )                                 /* Печать бланков ПД */
[                                                                        ];
[      В сумку N #____  вложены   неиспользованные  бланки  платежных    ]( BagNum.NumberBag );
[  документов:_______________________________________________________    ];
[                       (в разрезе видов и количества)                   ];
    СтрокиЦенностейВСумке( БланкиПД, ФлагВал, null, null );
  end;

[                                                                        ];
[      Сумка N #____ с валютными  и другими ценностями сформирована и    ]( BagNum.NumberBag );
[      сдана кассиром обменного пункта                                   ];
[                                                                        ];
[                   __________________ (###############################).]( GetFIOEmployee( Bag.Oper ) );
[                        (подпись)                  ( ФИО )              ];
[                                                                        ];
[      инкассатором __________________ (_______________________________).];
[                        (подпись)                  ( ФИО )              ];
[                                                                        ];
[      Сумка N #____  с валютными и другими ценностями принята           ]( BagNum.NumberBag );
[                                                                        ];
[      инкассатором __________________ (###############################).]( ColName );
[                        (подпись)                  ( ФИО )              ];
[                                                                        ];
[      Указанные на  обороте  валютные  и  другие  ценности   приняты    ];
[  полностью  с  пересчетом  с  учетом  расхождений,  перечисленных в    ];
[  нижеприведенном акте.                                                 ];
[                                                                        ];
[  Кассир банка ______________________ (_______________________________).];
[                        (подпись)                  ( ФИО )              ];
[                                                                        ];
[  Контролер   _______________________ (_______________________________).];
[                        (подпись)                  ( ФИО )              ];
[                                                                        ];
[                                 АКТ                                    ];
[                                                                        ];
[  При вскрытии сумки N #____ и пересчете вложенных валютных и других    ]( BagNum.NumberBag );
[  ценностей обнаружены:                                                 ];
[                                                                        ];
[  Недостача ________________________________________________________    ];
[  __________________________________________________________________    ];
[  __________________________________________________________________    ];
[           (наименование и сумма / количество ценностей по номиналу)    ];
[                                                                        ];
[  Излишек __________________________________________________________    ];
[  __________________________________________________________________    ];
[  __________________________________________________________________    ];
[          (наименование и сумма / количество ценностей по номиналу)     ];
[                                                                        ];
[  Неплатежные / неподлинные ________________________________________    ];
[  __________________________________________________________________    ];
[  __________________________________________________________________    ];
[  (наименование, серия, номер, достоинство и общая сумма по номиналу)   ];
[                                                                        ];
[  Кассир банка ______________________ (_______________________________).];
[                        (подпись)                  ( ФИО )              ];
[                                                                        ];
[  Контролер   _______________________ (_______________________________).];
[                        (подпись)                  ( ФИО )              ];
   PrintLn( StrFor( 12 ) );

END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Препроводительная ведомость. Оборотная сторона                         │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO ПрепроводительнаяВедомостьОборотная( CopyNo, ФлагВал )

   var 
       Total    = $0, 
       TotalRub = $0,
       Num, 
       Str;

   ARRAY ИТОГО;
[                                                                                ];
[                                ОПИСЬ                                           ];
[        ВАЛЮТНЫХ И ДРУГИХ ЦЕННОСТЕЙ, ВЛОЖЕННЫХ В СУМКУ N #____,                 ]( BagNum.NumberBag );
[          ДЛЯ НАПРАВЛЕНИЯ В КАССУ БАНКА ИЗ ОБМЕННОГО ПУНКТА                     ];
[                                                                                ];
[ #########################################################################      ]( Office.Address + ", N " + Office.RegNumb );
[ _________________________________________________________________________      ];
[ (адрес обменного пункта и его регистрационный  номер, если обменный пункт      ];
[               зарегистрирован  в установленном порядке)                        ];
[                                                                                ];
[  +---------------------------------------+------+-------------+--------------+ ];
[  |         Наименование инвалюты         | Код  |    Сумма    |Сумма в рублях| ];
[  |                                       |валюты| по номиналу |              | ];
[  +---------------------------------------+------+-------------+--------------+ ];
[  |                    1                  |  2   |      3      |       4      | ];
[  +---------------------------------------+------+-------------+--------------+ ];
   СтрокиЦенностейВСумке( Валюта_и_ПД, ФлагВал, Total, TotalRub );

   StrSplit( CurToStrAlt( TotalRub, null, null, int (GetCurrNumericalCode({NationalCur})) ), ИТОГО, 45, 45, 2 );
   if( ИТОГО(0) == "" )     ИТОГО(0) = "_";   end;
   if( ИТОГО(1) == "" )     ИТОГО(1) = "_";   end;

[  +---------------------------------------+------+-------------+--------------+ ];
[  |Итого рублей               _______________________________________________#| ]( TotalRub:r );
[  |(сумма цифрами и прописью) _#______________________________________________| ]( ИТОГО(0) );
[  |                           _#______________________________________________| ]( ИТОГО(1) );
[  +---------------------------------------------------------------------------+ ];

   if( ФлагВал )                                 /* Печать испорченных справок */
     if( FindCashValueIntCode( TYPE_RES_NOPAY, TYPERES_VALFORM, БСО_IntCode ) )
       ПачкиИспорченныхСправок( CashVal.RefValue, Num, Str );
[                                                                                ];
[  Опись испорченных справок:                                                    ];
[  ############################################################################# ]( "Номера "+Str:w );
     else
       MsgBox("Не найден ресурс кассы - испорченные справки ф.0406007");
     end;
   end;
   if( ФлагВал and (Office.UseF31 == SET_CHAR) )
     if( FindCashValueIntCode( TYPE_RES_NOPAY, TYPERES_VALFORM, Ф31_IntCode ) )
       ПачкиИспорченныхСправок( CashVal.RefValue, Num, Str );
[                                                                                ];
[  Справки ф.31:                                                                 ];
[  ############################################################################# ]( "Номера "+Str:w );
     else
       MsgBox("Не найден ресурс кассы - испорченные справки ф.31");
     end;
   end;

   if( ФлагВал )                                 /* Печать выданных справок */
     ПачкиВыданныхСправок( БСО_ValueRef, Num, Str );
[                                                                                ];
[  Опись выданных справок:                                                       ];
[  ############################################################################# ]( "Номера "+Str:w );
   end;
   if( ФлагВал and (Office.UseF31 == SET_CHAR) )
     ПачкиВыданныхСправок( Ф31_ValueRef, Num, Str );
[                                                                                ];
[  Справки ф.31:                                                                 ];
[  ############################################################################# ]( "Номера "+Str:w );
   end;

   PrintLn( StrFor( 12 ) );

END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Печать препроводительной ведомости при возврате остатков из ОП         │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO ПечатьПрепроводительнойВедомости( ФлагВал )
  
  if ( Bag.Number == 0 )
    MsgBox( "Не задана инкассаторская сумка!" );
    Exit( 1 );
  end;
  if ( ProgID != "X" ) /* не ОП */
    MsgBox( "Препроводительная ведомость выпускается только в Обменном пункте!" );
    Exit( 1 );
  end;
  if ( not GetTrue( true, "Печатать препроводительную ведомость?" ) )
    Exit( 1 );
  end;
  GetInt( НомерВедомости, "Введите номер ведомости:", 5 );

  /*GetClientInfo( Bag.ClientType, Bag.ClientID, PeerName );*/
  FindBagNum( Bag.Number );
  
  /* ФИО инкассатора */
  if ( PrintFIO )
    ColName = GetCollectorName( CashDoc.Branch, CashDoc.Collector );
    if ( ColName == "" )
      ColName = MkStr("_", 31);
    end;
  end;
                              /* N экз ,  ФлагВалютности */
  ПрепроводительнаяВедомостьЛицевая  ( 1, ФлагВал );
  ПрепроводительнаяВедомостьОборотная( 1, ФлагВал );
  ПрепроводительнаяВедомостьЛицевая  ( 2, ФлагВал );
  ПрепроводительнаяВедомостьОборотная( 2, ФлагВал );
  ПрепроводительнаяВедомостьЛицевая  ( 3, ФлагВал );
  ПрепроводительнаяВедомостьОборотная( 3, ФлагВал );

END;
