import office, rsd, office, dnamealg, commoninter, deprintr;

file    change( ratechng );
file    rate( cur_rate );
private file listfdep( listfdep );
private file dpdep( dp_dep );
private file party( party );

private const MainRateKind = 1;

private const ALG_UseCurrRates = 7619;
private const UCR_Bank = 0;
private const UCR_Filial = 1;
private const UCR_Own = 2;

private const CDF_CrossCalcType = 29;

macro printHeader( changeNumber, changeDate, startDate, startTime, department, own )
    [КУРСЫ ИНОСТРАННЫХ ВАЛЮТ И ДРАГМЕТАЛЛОВ ДЛЯ ОМС,
    УСТАНОВЛЕННЫЕ РАСПОРЯЖЕНИЕМ № ########## от ##########
                  ВСТУПАЮЩИМ В ДЕЙСТВИЕ С ########## ########
    
    Подразделение ############### Используются курсы ##########
    
    ]( changeNumber:r, changeDate:l, startDate:l, startTime:l, department:r, own:r)
end;

macro getAllCurrencys( changeID )
    var cmd, rs;   
    cmd = rsdcommand( "SELECT C.T_EXTERNALCODE, "
            "C.T_SHORT_NAME, "
            "C.T_NAME_CURRENCY, "
            "R.T_CHANGENOM, "
            "R.T_CBRATE, "
            "R.T_CURR_REF, "
            "C.T_MATERIALREF "
         "FROM dcur_rate_dbt r, dcurrency_dbt c "
         "WHERE     R.T_CURR_REF = C.T_CODE_CURRENCY "
            "AND t_change_ref = :1 "
            "AND r.t_kind_ref = :2 "
            "AND r.t_minsum = :3 "
         "ORDER BY r.t_curr_ref" );
         
    cmd.addparam( ":1", rsdbp_in, changeID );
    cmd.addparam( ":2", rsdbp_in, MainRateKind );
    cmd.addparam( ":3", rsdbp_in, 0.0 );
    
    cmd.execute();
    rs = rsdrecordset(cmd);
    
    return rs;
end;

macro printCurrencyHeader( EXTERNALCODE, SHORT_NAME, NAME_CURRENCY, CHANGENOM, CBRATE, MATERIALREF )
var FIELDNAME = "";
    if( MATERIALREF > 0 )
        FIELDNAME = "Учетная цена"
    else
        FIELDNAME = "Курс ЦБ"
    end;
    [
        Валюта  ###  ###  #########################
        Масштаб ( за... )  ###################
        ############       ###################]
        (   EXTERNALCODE, SHORT_NAME, NAME_CURRENCY, CHANGENOM:r, FIELDNAME:l, CBRATE:r );
end;

macro getAllRateKinds( changeID, currencyID )
    var cmd, rs;
    cmd = rsdcommand(
            "SELECT RK.T_NAME, "
                     "C.T_SHORT_NAME, "
                     "CR.T_OUTRATE, "
                     "COD.T_DESCR, "
                     "CR.T_KIND_REF "
                "FROM dcur_Rate_dbt cr, dratekind_dbt rk, dcurrency_dbt c, dcodif_dbt cod "
               "WHERE     CR.T_KIND_REF = RK.T_ID "
                     "AND C.T_CODE_CURRENCY = RK.T_BASE_CODE_CURRENCY "
                     "and ( COD.T_EXT_NUMB = RK.T_CROSSCALCTYPE "
                     "and COD.T_GROUPE_REF = ( select t_code from dcodif_dbt where t_ext_numb = " + String( CDF_CrossCalcType ) + " and t_root_ref = 0) ) "
                     "AND CR.T_CHANGE_REF = :1 "
                     "AND CR.T_CURR_REF = :2 "
                     "AND cr.t_minsum = :3 "
            "ORDER BY cr.t_kind_ref"
        );
        cmd.addparam( ":1", rsdbp_in, changeID );
        cmd.addparam( ":2", rsdbp_in, currencyID );
        cmd.addparam( ":3", rsdbp_in, 0.0 );
        cmd.execute();
        rs = rsdrecordset(cmd);
        
        return rs;
end;

macro fillComplexRate( changeID, currencyID, rateKindID )
    var rs, cmd;
    cmd = rsdcommand(
        "SELECT count(*) as cnt FROM dcur_rate_dbt "
        "WHERE t_change_ref = :1 and t_curr_ref = :2 and t_kind_ref = :3"
    );
    cmd.addparam( ":1", rsdbp_in, changeID );
    cmd.addparam( ":2", rsdbp_in, currencyID );
    cmd.addparam( ":3", rsdbp_in, rateKindID );
    
    cmd.execute();
    rs = rsdrecordset(cmd);
    rs.movenext();
    
    var cnt = rs.value( "cnt", V_INTEGER );
    if( cnt > 1 )
        return "X";
    end;
    return "";
end;

macro printRateKindHeader( NAME, SHORT_NAME, OUTRATE, DESCR, COMPLEXRATE )
    [
    Вид курсов: ###################################################
    Базовая валюта:  ###   Обратная котировка [#]
    Способ рассчета кросс-курса: ########################################
    Сложный курс [#]
    -----------------------------------------------------------
    Мин. сумма   | Курс в баз.ед.| Курс покупки | Курс продажи 
    -----------------------------------------------------------
    ]( NAME, SHORT_NAME, OUTRATE, DESCR, COMPLEXRATE );
end;

macro getAllSums( changeID, currencyID, rateKindID )
    var rs, cmd;
    cmd = rsdcommand(
        "SELECT t_minsum, t_crossrate, t_buyrate, t_sellrate "
        "FROM dcur_rate_dbt "
        "WHERE t_change_ref = :1 AND t_curr_ref = :2 AND t_kind_ref = :3 "
        "ORDER BY t_minsum"
    );
    cmd.addparam( ":1", rsdbp_in, changeID );
    cmd.addparam( ":2", rsdbp_in, currencyID );
    cmd.addparam( ":3", rsdbp_in, rateKindID );
    
    cmd.execute();
    rs = rsdrecordset(cmd);
    
    return rs;
end;

macro printSums( MINSUM, CROSSRATE, BUYRATE, SELLRATE )
    [#############|###############|##############|##############]
    (MINSUM, CROSSRATE, BUYRATE, SELLRATE);
    [-----------------------------------------------------------]
end;

macro printBody( changeID )
var currencys = getAllCurrencys( changeID );
    
    while( currencys.moveNext() )
        printCurrencyHeader
        (   currencys.value( "T_EXTERNALCODE" ),
            currencys.value( "T_SHORT_NAME" ),
            currencys.value( "T_NAME_CURRENCY" ),
            currencys.value( "T_CHANGENOM" ),
            currencys.value( "T_CBRATE" ),
            currencys.value( "T_MATERIALREF" )
        );
        
        var currRef = currencys.value( "T_CURR_REF" );
        var rateKinds = getAllRateKinds( changeID, currRef );
        
        while( rateKinds.moveNext() )
            var rateKind = rateKinds.value( "T_KIND_REF" );
            printRateKindHeader( rateKinds.value( "T_NAME" ),
                                 rateKinds.value( "T_SHORT_NAME" ),
                                 rateKinds.value( "T_OUTRATE" ),
                                 rateKinds.value( "T_DESCR" ),
                                 fillComplexRate( changeID, currRef, rateKind )
                               );
            var sums = getAllSums( changeID, currRef, rateKind );
            
            while( sums.movenext() )
                printSums( sums.value( "t_minsum" ), sums.value( "t_crossrate" ), sums.value( "t_buyrate" ), sums.value( "t_sellrate" ) );
            end;
        end;
    end;
end;

macro PrintReport( changeID )
var stat = false;
var owncurrate = 0;
var printBranch = "";
var curBranch = NumFNCash( );

    ClearRecord( change );
    rewind( change );
    change.id = changeID;
    stat = GetEQ( change );
    
    if ( stat )
        ClearRecord( listfdep );
        rewind( listfdep );
        listfdep.fncash = curBranch;
        stat = GetEQ( listfdep );
        
        if( stat )
            if ( CodeFor( listfdep.owncurrrate ) != UCR_Bank )
                printBranch = String( office.branchforrate );
            end;
        end;
            
        if ( stat )
            printHeader(    change.dictatenumber, 
                            change.dictatedate, 
                            change.startdate, 
                            change.starttime, 
                            printBranch, 
                            getnamealg( ALG_UseCurrRates, CodeFor( listfdep.owncurrrate ) ) );
            printBody( changeID );
        end;
    end;
end;
