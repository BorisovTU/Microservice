/*-RSL---------------------------------------- R-Style Software Lab --*/
/*------------------- Печать акта передачи ценностей -----------------*/
/*---------- отчет формируется по связке кассовых документов ---------*/
/*--------------------------------------------------------------------*/
/* RS-Bank      подсистема Обменный пункт             RA 18-09-02     */

import ExchangeInter, DeskInter,
       "define.mac", "RD_Comm.mac", "office.mac";

private file CashLn  ( "sb_casln.dbt",   "sbbank.def" ) key 0;
private file CashVl  ( "sb_casvl.dbt",   "sbbank.def" ) key 1;
private file Alg     ( "namealg.dbt" ,   "bank.def" ) key 0;
private file PayDoc  ( "paydoc.dbt"  , "exchange.def" ) key 0;

private const
  BS_BRANCH       = 0,    /* Филиал                 */
  BS_DEP          = 1,    /* Отделение              */
  BS_EXCH         = 2,    /* Обменный пункт         */
  BS_OPERDEP      = 3;    /* Оперчасть              */

private const
  ALG_CASH_VAL_TYPE = 811;/* Типы ресурсов кассы в namealg.dbt */

private const
  FLAGDEBCRED_IN   = 1,  /* Кредит */
  FLAGDEBCRED_OUT  = 2;  /* Дебет */

private const 
  FNCash  = NumFNCash();  /* Номер филиала */

private var 
  ApplicationKind, 
  ApplicationKey;

private var 
  StatusBranch = "",
  Placement    = "";

private var 
   ValueMoney,
   ValueCol,
   RegNumString;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Получить название типа ресурса кассы                                    │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
private MACRO GetCashTypeName( Type )
  Alg.iTypeAlg   = ALG_CASH_VAL_TYPE;
  Alg.iNumberAlg = Type;
  if(GetEQ(Alg))
    return Alg.szNameAlg;
  else
    return "???";
  end;
END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Сформировать название ресурса кассы                                    │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
private MACRO MakeCashValueName()
  if( CashVl.Type != TYPE_RES_PAY )
    return CashVl.NameValue + " (" + GetCashTypeName( CashVl.Type ) + ")";
  else
    return CashVl.NameValue;
  end;
END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Является ли данный бланк бланком ПД                                    │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO БланкПД( ValFormID )
  ClearRecord( PayDoc );
  Rewind( PayDoc );
  
  while( Next(PayDoc) )
    if( PayDoc.ValFormID == ValFormID )
      return true;
    end;
  end;

  return false;
END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Находит в связке ссылку на кассовый документ по нужной ценности        │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO FindCashLink( RefValue )

  var RecFound;
  ClearRecord(CashLn);
  CashLn.ApplicationKind1 = ApplicationKind;
  CashLn.ApplicationKey1  = ApplicationKey;
  RecFound=GetGE(CashLn);
  while( RecFound
    and (CashLn.ApplicationKind1 == ApplicationKind )
    and (CashLn.ApplicationKey1  == ApplicationKey  )
       )
    if( CashLn.RefValue2 == RefValue )
      return true;
    end;
    RecFound = Next(CashLn);
  end;

  return false;
  
END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Выбирает из связки ссылку на кассовый документ по нужной ценности      │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO FilterCashLink( RefValue )
  return(     (CashLn.ApplicationKind1 == ApplicationKind )
          and (CashLn.ApplicationKey1  == ApplicationKey  )
          and (CashLn.RefValue2        == RefValue        ) );
END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Становится в связке на следующий кассовый документ по нужной ценности  │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO NextCashLink( RefValue )
  return ( Next(CashLn) and FilterCashLink( RefValue ) );
END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Обнуляет остатки по текущей ценности                                   │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO ClearRests()
  ValueMoney   = $0;
  ValueCol     = 0;
  RegNumString = "";
END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Проверяет текущие остатки                                              │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO CheckRests()
  return ( (ValueMoney != 0) or (ValueCol != 0 ) );
END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Накапливает остатки по текущей ценности                                   │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO CollectRests()
  ValueMoney = ValueMoney + CashDocList.ValueMoney;
  ValueCol   = ValueCol   + CashDocList.ValueCol;
  if( (CashDocList.ValueMoney == 0) and (CashDocList.Nominal != 0) )
    ValueMoney = ValueMoney + CashDocList.Nominal * CashDocList.ValueCol;
  end;
  if( (CashDocList.Series != "") or (CashDocList.MaxRegistrNumber != 0) )
/*    RegNumString = RegNumString + ", " + CashDocList.Series + " " + 
                                         CashDocList.MinRegistrNumber;*/
    RegNumString = RegNumString + ", " + RegNumToStr(CashDocList.Series,CashDocList.MinRegistrNumber);
    if( CashDocList.ValueCol > 1 )
      RegNumString = RegNumString + "-" + CashDocList.MaxRegistrNumber;
    end;
  end;
END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Перебирает ценности по заданным значениям Type, TypeValue, CodIntValue │ */
/* │ и для каждой вызывает Func                                             │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO ЦенностиПоВиду( TypeValue, CodIntValue, Filter, Func )

  var 
      RecFound,
      RecFound2,
      Количество,
      NoFilter = true,
      HasFunc  = false;

  if( ValType( Filter ) != V_UNDEF )
    NoFilter = false;
  end;

  if( ValType( Func ) != V_UNDEF )
    HasFunc = true;
  end;
      
  ClearRecord( CashVl );
  CashVl.TypeValue   = TypeValue;
  if( CodIntValue == -1 )
    CashVl.CodIntValue = 0;
  else
    CashVl.CodIntValue = CodIntValue;
  end;

  RecFound = GetGE( CashVl );

  /* цикл по кассовым ценностям */
  while(    RecFound
      and ( CashVl.TypeValue   == TypeValue   )
      and ((CashVl.CodIntValue == CodIntValue ) or (CodIntValue == -1))
       )

    ClearRests();
    if( NoFilter or ExecMacro2( Filter ) )
    
      RecFound2 = FindCashLink(CashVl.RefValue);
      /* цикл по документам связки */
      while( RecFound2 and FilterCashLink(CashVl.RefValue) )

        if( FindCashDoc ( CashLn.RefValue2, 
                          CashLn.ApplicationKind2, 
                          CashLn.ApplicationKey2 ) )
          if(CashDocList.FlagDebCredOper == FLAGDEBCRED_OUT)
            CollectRests();
          end;
        end;
      
      RecFound2 = NextCashLink(CashVl.RefValue);
      end;

    end;
    /* печать строки по данной ценности */
    if( HasFunc and CheckRests() )
      ExecMacro2( Func );
    end;
    RecFound = Next( CashVl );
  end;

END;


/* ┌───────────────────────── Наличные рубли ───────────────────────────────┐ */
MACRO ПечатьРублей() 
[ ############################################################################ ]
 ( ValueMoney + " " + 
   CurToStrAlt( ValueMoney, null, null, {NationalCur} ):w 
 );
END;

MACRO НаличныеРубли()
  ЦенностиПоВиду( TYPERES_CURRENCY, {NationalCur}, null, @ПечатьРублей );
END;
/* └────────────────────────────────────────────────────────────────────────┘ */

/* ┌───────────────────────── Наличная валюта ──────────────────────────────┐ */
MACRO ПечатьВалюты()
[ ############################################################################ ]
 ( ValueMoney + " " + 
   CurToStrAlt( ValueMoney, null, null, int(GetCurrNumericalCode(FindCodCurByRefVal(CashDocList.RefValue))) ):w 
 );
END;

MACRO ФильтрВалюты()
  if( CashVl.CodIntValue == {NationalCur} )
    return false;
  else
    return true;
  end;
END;

MACRO НаличнаяВалюта()
 ЦенностиПоВиду( TYPERES_CURRENCY, -1, @ФильтрВалюты, @ПечатьВалюты );
END;
/* └────────────────────────────────────────────────────────────────────────┘ */

/* ┌───────────────────────── Платежные документы ──────────────────────────┐ */
MACRO ПечатьПД()
var CurrNumericalCode = GetCurrNumericalCode(FindCodCurByRefVal(CashDocList.RefValue));
[ ############################################################################ ]
 ( MakeCashValueName + ", " + 
   CurrNumericalCode + ", " +
   ValueMoney        + " " +
   CurToStrAlt( ValueMoney, null, null, int(CurrNumericalCode) ):w
 );
END;

MACRO ПлатежныеДокументы()
 ЦенностиПоВиду( TYPERES_PAYDOC, -1, null, @ПечатьПД );
END;
/* └────────────────────────────────────────────────────────────────────────┘ */

/* ┌───────────────────────── Бланки платежных документов ──────────────────┐ */
MACRO ПечатьБланковПД()
var CurrNumericalCode = GetCurrNumericalCode(FindCodCurByRefVal(CashDocList.RefValue));
[ ############################################################################ ]
 ( CurrNumericalCode + ", " +
   MakeCashValueName + ", " + 
   ValueMoney        + " " +
   CurToStrAlt( ValueMoney, null, null, int(CurrNumericalCode) ):w
 );
END;

MACRO ФильтрБланковПД()
  if( БланкПД( CashVl.CodIntValue ) )
    return true;
  else
    return false;
  end;
END;

MACRO БланкиПлатежныхДокументов()
 ЦенностиПоВиду( TYPERES_VALFORM, -1, @ФильтрБланковПД, @ПечатьБланковПД );
END;
/* └────────────────────────────────────────────────────────────────────────┘ */

/* ┌───────────────────────── Бланки строгой отчетности ────────────────────┐ */
MACRO ПечатьБланков()
[ ############################################################################ ]
 ( MakeCashValueName + " " +
   ValueCol          + " шт." +
   RegNumString:w
 );
END;

MACRO ФильтрБланков()
  if( not БланкПД( CashVl.CodIntValue ) )
    return true;
  else
    return false;
  end;
END;

MACRO БланкиСтрогойОтчетности()
 ЦенностиПоВиду( TYPERES_VALFORM, -1, @ФильтрБланков, @ПечатьБланков );
END;
/* └────────────────────────────────────────────────────────────────────────┘ */

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Акт передачи ценностей                                                 │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO АктПередачи()

[                                                                              ];
[ ############################################################################ ]( Office.BankName:w:r );
[                                                                              ];
[                                    АКТ                                       ];
[               ПЕРЕДАЧИ НАЛИЧНОЙ ИНОСТРАННОЙ ВАЛЮТЫ, ПЛАТЕЖНЫХ                ];
[              ДОКУМЕНТОВ В ИНОСТРАННОЙ ВАЛЮТЕ, БЛАНКОВ ПЛАТЕЖНЫХ              ];
[                ДОКУМЕНТОВ, НАЛИЧНЫХ РУБЛЕЙ И БЛАНКОВ СТРОГОЙ                 ];
[                      ОТЧЕТНОСТИ "СПРАВКА Ф. N 0406007"                       ];
[                                                                              ];
[                                     #                                        ]( StrAddZeroDate( {curdate} ):c );
[                                                                              ];
[     Мы, нижеподписавшиеся, операционисты-кассиры #                           ]( StatusBranch );
[ #                                                                            ]( Placement );
[ ############################################################################ ]( Office.Address + ", рег.номер " + Office.RegNumb:w );
[ (адрес обменного пункта и его номер, если пункт зарегистрирован ГУ ЦБ РФ)    ];
[                                                                              ];
[ составили настоящий акт в том, что:                                          ];
[                                                                              ];
[ #______________________________________________________________ передал (а), ]( GetFullNameEmployee( {oper} ) );
[     (Ф.И.О. операциониста-кассира, передавшего ценности)                     ];
[                                                                              ];
[ _______________________________________________________________ принял (а)   ];
[     (Ф.И.О. операциониста-кассира, принявшего ценности)                      ];
[ следующие ценности                                                           ];
[                                                                              ];
[ 1.  Наличные рубли:                                                          ];
      НаличныеРубли();
[                        (сумма цифрами и прописью)                            ];
[                                                                              ];
[ 2.  Наличная иностранная валюта:                                             ];
      НаличнаяВалюта();
[(сумма цифрами и прописью с указанием наименования наличной иностранной валюты)];
[                                                                              ];
[ 3.  Платежные документы в иностранной валюте:                                ];
      ПлатежныеДокументы();
[  (наименование платежного документа, код валюты, сумма цифрами и прописью)   ];
[                                                                              ];
[ 4.  Бланки платежных документов:                                             ];
      БланкиПлатежныхДокументов();
[             (код валюты, наименование, сумма цифрами и прописью)             ];
[                                                                              ];
[ 5.  Бланки строгой отчетности:                                               ];
      БланкиСтрогойОтчетности();
[                         (количество и их номера)                             ];
[                                                                              ];
[ 6. Другие ценности _________________________________________                 ];
[                                                                              ];
[                                                                              ];
[        ПРИНЯЛ:                                ПЕРЕДАЛ:                       ];
[                                                                              ];
[        ____________________                   ____________________           ];
[             (подпись)                               (подпись)                ];
[                                                                              ];

END;


/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Получение cтатуса подразделения                                        │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO GetStatusBranch()

  file listfdep ( "listfdep.dbt", "sbbank.def" ) key 0;
  listfdep.FNCash = FNCash;
  
  if ( GetEQ( listfdep ) )
/*    if( listfdep.Placement )
      Placement      = "вне кассового узла";
    end;*/
    if(   GetBranchStatus() == BS_EXCH    )
      StatusBranch = "обменного пункта";
    elif( GetBranchStatus() == BS_OPERDEP )
      StatusBranch = "операционной кассы";
    end;
  end;

END;


/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Печать акта передачи ценностей                                         │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO ПечатьАктаПередачи()
  
  var RecFound;

  if ( not GetTrue( true, "Печатать акт передачи?" ) )
    Exit( 1 );
  end;
 
  GetStatusBranch();
  InterDesk_GetBunchApplic( ApplicationKind, ApplicationKey );
  АктПередачи();

END;

ПечатьАктаПередачи();