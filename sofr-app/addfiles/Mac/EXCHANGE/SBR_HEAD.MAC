/****************************************************************************
   RS-Bank        Обменный пункт
   sbr_head.mac - заголовочный макрос для формирования отчетности ОП
                  для Сбербанка
   Copyright 1998 (c) R-Style Software Lab.
   Создание: 15-05-98 by AV
****************************************************************************/
IMPORT ExchangeInter,
       BankInter,/*для GetIniString*/
       "define.mac";

/*суммарные обороты по реестрам*/
FILE   Turnrstr   ("turnrstr.dbt", "exchange.def") key 0;/* Смена-Сотрудник-Ценность-Валюта */
FILE   Operat     ("operat.dbt",   "exchange.def") key 1;
FILE   TurnOper   ("turnover.dbt", "exchange.def") key 0;/* Сотрудник-Смена-Операция */
FILE   TurnIdOper ("turnover.dbt", "exchange.def") key 1;/* операция-дата-время */
/*реестры*/
FILE   Reestr     ("reestr.dbt",   "exchange.def") key 2;/* N ОП - Сотрудник - Смена */
/*справочник типов реестров*/
FILE   RstTypes   ("rsttypes.dbt", "exchange.def") key 1;/* Вид операции */
/*справки БСО*/
FILE   Spravka    ("sprav.dbt",    "exchange.def") key 1;/* Номер операции-Состояние справки */
/*справки F31*/
FILE   Spravka31  ("sprav31.dbt",  "exchange.def") key 1;/* Номер операции-Состояние справки */
/*справки БСО, отсортированные по сериям и номерам*/
FILE   SprvSort   ("sprav.dbt",    "exchange.def") key 0;/* Серия - Номер */
/*справки F31, отсортированные по сериям и номерам*/
FILE   Sprv31Sort ("sprav31.dbt",  "exchange.def") key 0;/* Серия - Номер */
/*текстовый файл отчета*/
/*рабочие смены ОП*/
FILE   Sess       ("session.dbt",  "exchange.def") key 1000;/*физическая последовательность*/
FILE   ReportFile()txt;

RECORD Session( session, "exchange.def" );
/*глобальная структура с информацией об ОП*/
RECORD Office  ( office, "exchange.def" );
/*довесок к операциям аванса,возврата,передачи*/
RECORD avanceop ( "avance.str","exchange.def");
/*структура с информацией по операции с ПД */
Record paydocop ("paydocop.str","exchange.def");
/*структура с информацией по операции учета излишков или недостачи */
RECORD deficitop("deficit.str","exchange.def");

CONST Прочерк = "---";

const КодПодсистемыОП = 250; /* Значение кода подсистемы "Обменный пункт" */

/*Итоговые значения для соответствующих колонок в операционном дневнике*/
ARRAY ИтогКолонки;

/* глобальная переменная - количество валют в операциях за месяц (sbr_pod.mac) */
var NumCurrency  = 0;
/* общее число валют использованных за смену (sbr_od.mac) */
var TotalNumCurr = 0;
/*глобализм - количество строк в ОД (номер текущей строки)*/
var НомерСтрокиОД = 0;

/* Настройки для формирования операционных дневников ************************/
/*  !!!!! шапка и дно дневника переделываются вручную                       */
  var ШиринаКолонок    = 0; /* ширина колонок в операционном дневнике          */
  var МаксимумКолонок  = 0;  /* максимальное количество колонок                 */
  var НачальнаяКолонка = 0;  /* начальная колонка для вывода чисел              */
  var ШиринаНазвания   = 0; /* ширина графы для названия операции              */
/****************************************************************************/

/***************************************************************************/
/***************************************************************************/
MACRO Число_Строка2( Число )
   if( Число ) return String(Число);
   else return Прочерк;
   end;
END;
/****************************************************************************/
/*Процедура при необходимости добавляет разделитель в конец строки          */
/****************************************************************************/
MACRO Разделитель( Строка )
 if( SubStr( Строка, StrLen(Строка) ) != "|")
    return Строка = Строка + "|";
 else
    return Строка;
 end;
END;
/****************************************************************************/
/* Функция считает количество справок с определенным статусом для операции  */
/****************************************************************************/
MACRO CalcSprv( OperID, state )
  var Num = 0;
  Spravka.Oper_Ref  = OperID;
  Spravka.State_Ref = state;
  if( GetEQ( Spravka ) )
     Num = Num + 1;
     While( Next(Spravka) AND (Spravka.Oper_Ref == OperID) AND (Spravka.State_Ref == state) )
        Num = Num + 1;
     end;
  end;
  return Num;
END;
/****************************************************************************/
/* Функция считает количество справок с определенным статусом для операции  */
/****************************************************************************/
MACRO CalcSprv31( OperID, state )
  var Num = 0;
  Spravka31.Oper_Ref  = OperID;
  Spravka31.State_Ref = state;
  if( GetEQ( Spravka31 ) )
     Num = Num + 1;
     While( Next(Spravka31) AND (Spravka31.Oper_Ref == OperID) AND (Spravka31.State_Ref == state) )
        Num = Num + 1;
     end;
  end;
  return Num;
END;
/****************************************************************************/
/* Процедура получает тип реестра по виду операции                          */
/****************************************************************************/
MACRO GetTypeReestr( KindOper )
   RstTypes.Oper_Ref = KindOper;
   if( GetEQ( RstTypes ) )
      return RstTypes.Type;
   else
      return "";
   end;
END;
/****************************************************************************/
/*  Процедуры для формирования ОД (с учетом настроек ОД)                    */
/****************************************************************************/
/****************************************************************************/
/* Процедура выводит требуемое количество пустых граф, разделенные          */
/*  разделителем                                                            */
/****************************************************************************/
MACRO ПустыеГрафы( N )
   var Строка;
   if( N > 0 )
      Строка = "|";
      while( N )
         N = N - 1;
         Строка = Строка + MkStr(" ", ШиринаКолонок ) + "|";
      end;
   else
      Строка = "";
   end;
   return Строка;
END;
/****************************************************************************/
/* Процедура возвращает одну пустую графу                                   */
/****************************************************************************/
MACRO ПустаяГрафа()
   var Строка;
   return  Строка = MkStr(" ", ШиринаКолонок ) + "|";
END;
/****************************************************************************/
/* Процедура формирует содержимое графы названия операции                   */
/****************************************************************************/
MACRO Название( Строка/*,КодВалюты, КурсОперации, КодЦенности*/ )
   var КодВалюты, Buff, НазваниеВалюты, ДлинаСтроки;
   var КурсОперации = 0, СтрокаКурс = "";
   var КодЦенности  = 0, СтрокаЦенность = "";
   if( GetParm( 1, КодВалюты ) AND ( ValType(КодВалюты) == V_INTEGER ) )
      Buff = CurToStrAlt( 500, null, null, КодВалюты );
      НазваниеВалюты = SubStr( Buff, 5, (Index( Buff, "0" ) - 5) );
   else
      НазваниеВалюты = "";
   end;
   if( GetParm( 2, КурсОперации ) AND ( ValType(КурсОперации) == V_DOUBLE ) )
      КурсОперации = money(КурсОперации * 100);
      СтрокаКурс = " по курсу " + string(КурсОперации);
   end;
   if( GetParm( 3, КодЦенности ) AND ( ValType(КодЦенности) == V_INTEGER ) )
      if( TrsIsIncass(КодЦенности) )
         СтрокаЦенность = "(инкассо)";
      end;
      if( TrsIsExpert(КодЦенности) )
         СтрокаЦенность = "(эксперт.)";
      end;
   end;

   Строка = Строка + НазваниеВалюты;/*добавок-название валюты операции в родит.падеже*/
   if( КодЦенности != 0 )
      Строка = Строка + СтрокаЦенность;/*добавок - сомнительнось ценности*/
   end;
   if( КурсОперации != 0 )
      Строка = Строка + СтрокаКурс;    /*добавок - курс операции*/
   end;
   ДлинаСтроки = StrLen( Строка );
   if( ДлинаСтроки < ШиринаНазвания )
      return Строка = Строка + MkStr(" ", ШиринаНазвания - ДлинаСтроки );
   elif( ДлинаСтроки > ШиринаНазвания )
      /*MsgBox("Ошибка: ДлинаСтроки > ШиринаНазвания (Название)");*/
      return SubStr( Строка, 1, ШиринаНазвания );
   end;
  return Строка;
END;
/****************************************************************************/
/*  Процедура преобразует число в строку в соответствии с настройками ОД    */
/****************************************************************************/
MACRO Число_Строка( Число /*[ Влево ]*/)
   var Влево, Строка, ДлинаСтроки;
   if( GetParm(1, Влево) == False )
      Влево = False;
   end;
   Строка = string( Trim(string( Число:20:2 )) );
   ДлинаСтроки = StrLen( Строка );
   if( ДлинаСтроки == ШиринаКолонок )
      return Строка;
   elif( ДлинаСтроки <  ШиринаКолонок )
      if( NOT Влево )
         return MkStr( " ", ШиринаКолонок - ДлинаСтроки ) + Строка;
      else
         return Строка + MkStr( " ", ШиринаКолонок - ДлинаСтроки );
      end;
   elif( ДлинаСтроки >  ШиринаКолонок )
      return MkStr( "*", ШиринаКолонок );
   end;
END;
/***************************************************************************/
/* Процедура преобразует число в строку-информацию о курсе в соответствии  */
/*  с настройками ОД                                                       */
/***************************************************************************/
MACRO Число_Курс( Число, Добавка/*[ Влево ]*/ )
   var Влево, Строка, ДлинаСтроки, ВсяДлинаСтроки, Максимум;
   if( GetParm(1, Влево) == False )
      Влево = False;
   end;
   Строка = string( Trim(string( Число:20:4 )) );
   ДлинаСтроки = StrLen( Строка );
   ВсяДлинаСтроки = ДлинаСтроки + StrLen( Добавка );
   Максимум = ШиринаКолонок + StrLen( Добавка );
   if( ВсяДлинаСтроки == Максимум )
      return Строка + Добавка;
   elif( ВсяДлинаСтроки <  Максимум )
      if( NOT Влево )
         return MkStr( " ", ШиринаКолонок - ДлинаСтроки ) + Строка + Добавка;
      else
         return Строка + Добавка + MkStr( " ", ШиринаКолонок - ДлинаСтроки );
      end;
   elif( ВсяДлинаСтроки > Максимум )
      return MkStr( "*", ШиринаКолонок + StrLen( Добавка ) );
   end;
END;
/****************************************************************************/
/* Процедура формирует строку ОД                                            */
/****************************************************************************/
MACRO СтрокаОД( Строка /*[Номерколонки1, Число1, НомерКолонки2, Число2 ...]*/)
 var i = 1;/*номер считываемого параметра*/
 var j = НачальнаяКолонка;/*начальная колонка для вывода чисел*/
 var НомерКолонки, Число;
 Разделитель(Строка);
 While( GetParm( i, НомерКолонки ) )
    if( GetParm( i + 1, Число ) )
       if( (НомерКолонки > 0) AND
           (НомерКолонки > j) AND
           (НомерКолонки <= МаксимумКолонок) )
          /*добавление пустых граф*/
          Строка = Строка + ПустыеГрафы( НомерКолонки - j );
          j = НомерКолонки;
       end;
       if( НомерКолонки == j )
          Строка = Разделитель(Строка) + Число_Строка( Число );
          ИтогКолонки(j-1) = ИтогКолонки(j-1) + Число;
       else
          MsgBox("Параметр" + string(i) + " - ошибочный! (СтрокаОД)");
          return "!";
       end;
       i = i + 2; j = j + 1;
    end;
 end;
 if( НомерКолонки != МаксимумКолонок )
    Строка = Строка + ПустыеГрафы(МаксимумКолонок - НомерКолонки);
 end;
 НомерСтрокиОД  = НомерСтрокиОД + 1;
 return Разделитель(Строка);
END;

/****************************************************************************/
/*Завершение секции процедур для формирования ОД                            */
/****************************************************************************/

/****************************************************************************/
/* процедура находит оборот определенного типа по реестру                   */
/* если задан код валюты, то и для определенной валюты                      */
/****************************************************************************/
MACRO GetTurnrstr( TypeTurnover/*, Curr_Ref*/ )
  var Curr_Ref;
  if( NOT GetParm( 1, Curr_Ref ) )
     Curr_Ref = ANY_CURR;
  end;
  ClearRecord(Turnrstr);
  Turnrstr.Emp_Ref    = Reestr.Emp_Ref;
  Turnrstr.Sess_Ref   = Reestr.Sess_Ref;
  Turnrstr.Oper_Ref   = Reestr.AutoKey;
  /*Turnrstr.Date       = Reestr.Date;*/
  /*Turnrstr.Time       = Reestr.Time;*/
  /*Turnrstr.Trs_Ref    = TrsCash;    */
  /*Turnrstr.SubTrs_Ref = 0;          */
  Turnrstr.Curr_Ref   = ANY_CURR;

  var Loop = GetGE( Turnrstr );

  while( Loop )
     if( (Turnrstr.Oper_Ref == Reestr.Autokey) AND
         (Turnrstr.Type     == TypeTurnover  )
       )
       if( (Curr_Ref == ANY_CURR) OR ( (Curr_Ref != ANY_CURR) AND (Turnrstr.Curr_Ref == Curr_Ref)) )
          return True;
       end;
     elif(Turnrstr.Oper_Ref != Reestr.Autokey)
        return False;
     end;
     Loop = next(Turnrstr);
  end;

  return False;
END;

/****************************************************************************/
/* процедура находит следующий оборот определенного типа по реестру         */
/* если задан код валюты, то и для определенной валюты                      */
/* применять только после GetTurnrstr !!!                                   */
/****************************************************************************/
MACRO GetNextTurnrstr( TypeTurnover/*, Curr_Ref*/ )
  var Curr_Ref;
  var Loop;
  if( NOT GetParm( 1, Curr_Ref ) )
     Curr_Ref = ANY_CURR;
  end;

  /* Считаем, что буфер заполнен предыдущим успешным позиционированием */
  Loop = GetGT( Turnrstr );

  while( Loop )
     if( (Turnrstr.Oper_Ref == Reestr.Autokey) AND
         (Turnrstr.Type     == TypeTurnover  )
       )
       if( (Curr_Ref == ANY_CURR) OR ( (Curr_Ref != ANY_CURR) AND (Turnrstr.Curr_Ref == Curr_Ref)) )
          return True;
       end;
     elif(Turnrstr.Oper_Ref != Reestr.Autokey)
        return False;
     end;
     Loop = next(Turnrstr);
  end;

  return False;

END;

/****************************************************************************/
/* процедура находит оборот определенного типа по операции    RA 29-03-99   */
/* если задан код валюты, то и для определенной валюты                      */
/****************************************************************************/
MACRO GetTurnoper( TypeTurnover/*, Curr_Ref*/ )
  var Curr_Ref;
  if( NOT GetParm( 1, Curr_Ref ) )
     Curr_Ref = ANY_CURR;
  end;
  ClearRecord(TurnOper);
  TurnOper.Emp_Ref    = Operat.Emp_Ref;
  TurnOper.Sess_Ref   = Operat.Sess_Ref;
  TurnOper.Oper_Ref   = Operat.ID;
  TurnOper.Curr_Ref   = ANY_CURR;

  var Loop = GetGE( TurnOper );

  while( Loop )
     if( (TurnOper.Oper_Ref == Operat.ID) AND
         (TurnOper.Type     == TypeTurnover  )
       )
       if( (Curr_Ref == ANY_CURR) OR ( (Curr_Ref != ANY_CURR) AND (TurnOper.Curr_Ref == Curr_Ref)) )
          return True;
       end;
     elif(TurnOper.Oper_Ref != Operat.ID)
        return False;
     end;
     Loop = next(TurnOper);
  end;

  return False;
END;

/****************************************************************************/
/* процедура находит следующий оборот определенного типа по операции        */
/* если задан код валюты, то и для определенной валюты                      */
/* применять только после GetTurnoper !!!                                   */
/****************************************************************************/
MACRO GetNextTurnoper( TypeTurnover/*, Curr_Ref*/ )
  var Curr_Ref;
  var Loop;
  if( NOT GetParm( 1, Curr_Ref ) )
     Curr_Ref = ANY_CURR;
  end;

  /* Считаем, что буфер заполнен предыдущим успешным позиционированием */
  Loop = GetGT( TurnOper );

  while( Loop )
     if( (TurnOper.Oper_Ref == Operat.ID) AND
         (TurnOper.Type     == TypeTurnover  )
       )
       if( (Curr_Ref == ANY_CURR) OR ( (Curr_Ref != ANY_CURR) AND (TurnOper.Curr_Ref == Curr_Ref)) )
          return True;
       end;
     elif(TurnOper.Oper_Ref != Operat.ID)
        return False;
     end;
     Loop = next(TurnOper);
  end;

  return False;

END;

/****************************************************************************/
/* процедура находит неплатежеспосбный оборот определенного типа по операции*/
/****************************************************************************/
MACRO GetTurnIDOper( OperID, Insolve )

  ClearRecord(TurnIdOper);
  TurnIdOper.Oper_Ref   = OperID;

  var Loop = GetGE( TurnIdOper );

  while( Loop )
     if( (TurnIdOper.Oper_Ref  == OperID  ) AND
         (TurnIdOper.Insolvent  == Insolve )
       )
       return True;
     elif(TurnIdOper.Oper_Ref != OperID)
        return False;
     end;
     Loop = next(TurnIdOper);
  end;

  return False;
END;
/****************************************************************************/
/* Процедура возвращает рублевую сумму комиссии для реестра                 */
/*   в параметр заносится процентная комиссия от полученной суммы           */
/****************************************************************************/
MACRO Комиссия( ПроцентнаяКомиссия, ТипКомиссии )
  /*Сумма и валюта процентной комиссии*/
  var RealProcComSum  = $0.0;    /*сумма */
  var ProcComFromInc  = $0.0;
  var RealProcComCurr = ANY_CURR;/*валюта*/
  var FixComSum       = $0.0;    /*сумма */
  var FixComCurr      = ANY_CURR;/*валюта*/
  /*получение через обороты значений сумм комиссий по операции*/
  if( GetTurnrstr( CDF_TurnFix ) )/*фиксированная*/
      FixComSum   = abs(Turnrstr.Sum);
      FixComCurr  = Turnrstr.Curr_Ref;
  end;
  if  ( GetTurnrstr( CDF_TurnPComInc ) )/*процентная от полученной*/
      RealProcComSum =  abs(Turnrstr.Sum);
      RealProcComCurr = Turnrstr.Curr_Ref;
      SetParm( 0, Turnrstr.Sum );
      SetParm( 1, "ОтПолученнойСуммы" );
  elif( GetTurnrstr( CDF_TurnPComOut ) )/*процентная от выданной*/
      RealProcComSum = abs(Turnrstr.Sum);
      RealProcComCurr = Turnrstr.Curr_Ref;
      SetParm( 0, Turnrstr.Sum );
      SetParm( 1, "ОтВыданнойСуммы" );
  end;
  /*перевод в рубли по курсу ЦБ для валюты комиссии*/
  /*                   ^^^^^^^^ RA 4-08-99         */
  if( FixComCurr      != {NationalCur} )
     FixComSum = Money( FixComSum * ПолучитьКурсЦБ( FixComCurr, Reestr.Rate_Ref ) );
  end;
  if( RealProcComCurr != {NationalCur} )
     RealProcComSum = Money( RealProcComSum * ПолучитьКурсЦБ( RealProcComCurr, Reestr.Rate_Ref ) );
  end;

  /*возвращаем суммарное значение комиссии*/
  return FixComSum + RealProcComSum;
END;

/****************************************************************************/
/* Процедура возвращает рублевую сумму комиссии по операции RA 29-03-99     */
/*   в параметр заносится процентная комиссия от полученной суммы           */
/****************************************************************************/
MACRO Комиссия_ПоОперации( ПроцентнаяКомиссия, ТипКомиссии )
  /*Сумма и валюта процентной комиссии*/
  var RealProcComSum  = $0.0;    /*сумма */
  var ProcComFromInc  = $0.0;
  var RealProcComCurr = ANY_CURR;/*валюта*/
  var FixComSum       = $0.0;    /*сумма */
  var FixComCurr      = ANY_CURR;/*валюта*/
  /*получение через обороты значений сумм комиссий по операции*/
  if( GetTurnoper( CDF_TurnFix ) )/*фиксированная*/
      FixComSum   = abs(TurnOper.Sum);
      FixComCurr  = TurnOper.Curr_Ref;
  end;
  if  ( GetTurnoper( CDF_TurnPComInc ) )/*процентная от полученной*/
      RealProcComSum =  abs(TurnOper.Sum);
      RealProcComCurr = TurnOper.Curr_Ref;
      SetParm( 0, TurnOper.Sum );
      SetParm( 1, "ОтПолученнойСуммы" );
  elif( GetTurnoper( CDF_TurnPComOut ) )/*процентная от выданной*/
      RealProcComSum = abs(TurnOper.Sum);
      RealProcComCurr = TurnOper.Curr_Ref;
      SetParm( 0, TurnOper.Sum );
      SetParm( 1, "ОтВыданнойСуммы" );
  end;
  /*перевод в рубли по курсу ЦБ для валюты комиссии*/
  /*                   ^^^^^^^^ RA 4-08-99         */
  if( FixComCurr      != {NationalCur} )
     FixComSum = Money( FixComSum * ПолучитьКурсЦБ( FixComCurr, Operat.Rate_Ref ) );
  end;
  if( RealProcComCurr != {NationalCur} )
     RealProcComSum = Money( RealProcComSum * ПолучитьКурсЦБ( RealProcComCurr, Reestr.Rate_Ref ) );
  end;

  /*возвращаем суммарное значение комиссии*/
  return FixComSum + RealProcComSum;
END;

/****************************************************************************/
/* Процедура возвращает суммы фиксированной комиссии для реестра            */
/*   в параметры заносятся рублевая и валютная фиксированные комиссия       */
/* если КодВал ANY_CURR, то в него заносится найденный код фиксир. комиссии */
/****************************************************************************/
MACRO ПолучитьФиксированнуюКомиссию( КодВал, ФиксКомиссияРуб, ФиксКомиссияВал )
  if( GetTurnrstr( CDF_TurnFix, {NationalCur} ) ) /*фиксированная рублевая*/
     if( Turnrstr.Cash == SET_CHAR ) /*признак отражения в кассе*/
        SetParm( 1, abs(Turnrstr.Sum) );
     else
        SetParm( 1, 0 );
     end;
  else
     SetParm( 1, 0 );
  end;
  if( GetTurnrstr( CDF_TurnFix, КодВал ) ) /*фиксированная валютная*/
     if( Turnrstr.Cash == SET_CHAR ) /*признак отражения в кассе*/
        SetParm( 2, abs(Turnrstr.Sum) );
     else
        SetParm( 2, 0 );
     end;
     if( КодВал == ANY_CURR )
        SetParm( 0, Turnrstr.Curr_Ref ); /*код валюты фиксированной комиссии*/
     end;
  else
     SetParm( 2, 0 );
  end;
END;

/****************************************************************************/
/* Процедура возвращает суммы фиксированной комиссии для операции RA 30-03-99*/
/*   в параметры заносятся рублевая и валютная фиксированные комиссия       */
/****************************************************************************/
MACRO ПолучитьФиксированнуюКомиссиюПоОперации( КодВал, ФиксКомиссияРуб, ФиксКомиссияВал )
  if( GetTurnoper( CDF_TurnFix, {NationalCur} ) )/*фиксированная рублевая*/
     if( TurnOper.Cash == SET_CHAR ) /*признак отражения в кассе*/
        SetParm( 1, abs(TurnOper.Sum) );
     else
        SetParm( 1, 0 );
     end;
  else
     SetParm( 1, 0 );
  end;
  if( GetTurnoper( CDF_TurnFix, КодВал ) )/*фиксированная валютная*/
     if( TurnOper.Cash == SET_CHAR ) /*признак отражения в кассе*/
        SetParm( 2, abs(TurnOper.Sum) );
     else
        SetParm( 2, 0 );
     end;
     if( КодВал == ANY_CURR )
        SetParm( 0, TurnOper.Curr_Ref );
     end;
  else
     SetParm( 2, 0 );
  end;
END;

/****************************************************************************/
/* Процедура возвращает суммы процентной комиссии для реестра               */
/*   в параметры заносятся рублевая и валютная процентные комиссия          */
/*   если валютная не нужна КодВал = ANY_CURR                               */
/****************************************************************************/
MACRO ПолучитьПроцентнуюКомиссию( КодВал, ПроцКомиссияРуб, ПроцКомиссияВал, СтрокаНеКассовая )
  var СумРуб = 0, СумВал = 0;
  var НеКассовая = "";                  /*признак отражения в кассе*/
  if( GetTurnrstr( CDF_TurnPComInc, {NationalCur} ) )/*процентная рублевая от полученной*/
      if( Turnrstr.Cash == UNSET_CHAR ) /*признак не отражения в кассе*/
         НеКассовая = "от полученной";
      end;
      СумРуб = СумРуб + abs(Turnrstr.Sum);
  end;
  if( GetTurnrstr( CDF_TurnPComOut, {NationalCur} ) )/*процентная рублевая от выданной*/
      if( Turnrstr.Cash == UNSET_CHAR ) /*признак не отражения в кассе*/
         НеКассовая = "от выданной";
      end;
      СумРуб = СумРуб + abs(Turnrstr.Sum);
  end;
  if( СумРуб )
     SetParm( 1, СумРуб );
  end;
  if( КодВал != ANY_CURR )
     if( GetTurnrstr( CDF_TurnPComInc, КодВал ) )/*процентная валютная от полученной*/
        if( Turnrstr.Cash == UNSET_CHAR ) /*признак не отражения в кассе*/
           НеКассовая = "от полученной";
        end;
        СумВал = СумВал + abs(Turnrstr.Sum);
     end;
     if( GetTurnrstr( CDF_TurnPComOut, КодВал ) )/*процентная валютная от выданной*/
        if( Turnrstr.Cash == UNSET_CHAR ) /*признак не отражения в кассе*/
           НеКассовая = "от выданной";
        end;
        СумВал = СумВал + abs(Turnrstr.Sum);
     end;
     if( СумВал )
        SetParm( 2, СумВал );
     end;
  end;
  SetParm( 3, НеКассовая );
END;

/****************************************************************************/
/* Процедура возвращает суммы процентной комиссии для операции RA 30-03-99  */
/*   в параметры заносятся рублевая и валютная процентные комиссия          */
/*   если валютная не нужна КодВал = ANY_CURR                               */
/****************************************************************************/
MACRO ПолучитьПроцентнуюКомиссиюПоОперации( КодВал, ПроцКомиссияРуб, ПроцКомиссияВал, СтрокаНеКассовая )
  var СумРуб = 0, СумВал = 0;
  var НеКассовая = "";                  /*признак отражения в кассе*/
  if( GetTurnoper( CDF_TurnPComInc, {NationalCur} ) )/*процентная рублевая от полученной*/
      if( TurnOper.Cash == UNSET_CHAR ) /*признак не отражения в кассе*/
         НеКассовая = "от полученной";
      end;
      СумРуб = СумРуб + abs(TurnOper.Sum);
  end;
  if( GetTurnoper( CDF_TurnPComOut, {NationalCur} ) )/*процентная рублевая от выданной*/
      if( TurnOper.Cash == UNSET_CHAR ) /*признак не отражения в кассе*/
         НеКассовая = "от выданной";
      end;
      СумРуб = СумРуб + abs(TurnOper.Sum);
  end;
  if( СумРуб )
     SetParm( 1, СумРуб );
  end;
  if( КодВал != ANY_CURR )
     if( GetTurnoper( CDF_TurnPComInc, КодВал ) )/*процентная валютная от полученной*/
        if( TurnOper.Cash == UNSET_CHAR ) /*признак не отражения в кассе*/
           НеКассовая = "от полученной";
        end;
        СумВал = СумВал + abs(TurnOper.Sum);
     end;
     if( GetTurnoper( CDF_TurnPComOut, КодВал ) )/*процентная валютная от выданной*/
        if( TurnOper.Cash == UNSET_CHAR ) /*признак не отражения в кассе*/
           НеКассовая = "от выданной";
        end;
        СумВал = СумВал + abs(TurnOper.Sum);
     end;
     if( СумВал )
        SetParm( 2, СумВал );
     end;
  end;
  SetParm( 3, НеКассовая );
END;

/*-------------------------------------------------------------------------*/
/* Возвращает рублевую сумму по безналичной конверсии части выданной суммы */
/*      по реестру                                             RA 24-11-00 */
/*-------------------------------------------------------------------------*/
MACRO ПолучитьБезналКонв()
  if( GetTurnrstr( CDF_TurnCnvCoins ) )
    if( (Turnrstr.Cash == SET_CHAR) AND (Turnrstr.Curr_Ref == {NationalCur} ) )
      return abs(Turnrstr.Sum);
    end;
    while( GetNextTurnrstr( CDF_TurnCnvCoins ) )
      if( (Turnrstr.Cash == SET_CHAR) AND (Turnrstr.Curr_Ref == {NationalCur} ) )
        return abs(Turnrstr.Sum);
      end;
    end;
  end;

  return 0;
END;

/*-------------------------------------------------------------------------*/
/* Возвращает рублевую сумму по безналичной конверсии части выданной суммы */
/*      по операции                                            RA 24-11-00 */
/*-------------------------------------------------------------------------*/
MACRO ПолучитьБезналКонвПоОперации()
  if( GetTurnoper( CDF_TurnCnvCoins ) )
    if( (TurnOper.Cash == SET_CHAR) AND (TurnOper.Curr_Ref == {NationalCur} ) )
      return abs(TurnOper.Sum);
    end;
    while( GetNextTurnoper( CDF_TurnCnvCoins ) )
      if( (TurnOper.Cash == SET_CHAR) AND (TurnOper.Curr_Ref == {NationalCur} ) )
        return abs(TurnOper.Sum);
      end;
    end;
  end;

  return 0;
END;

/* RA 13-09-99 ------------------------------------------------------------*/
/* Восстанавливает исходные суммы по операциям или реестрам                */
/* Учитывает некассовые обороты в полученной/выданной сумме:               */
/* вычитает из полученной и добавляет к выданной                           */
/*-------------------------------------------------------------------------*/
MACRO УчетНекассовыхОборотов( Вид, Income, Out /*, ОтсекатьНалоги, ОтсекатьКонвМелочи */ )

  var WithoutTax = "", WithoutCnvCoins = "кроме вал. мелочи";

  MACRO ОборотПоОперации()
    if( Operat.IncomeCurr_Ref == Operat.OutCurr_Ref )
      if( TurnOper.Sum > 0 ) /* оборот - в полученной сумме */
        Income = Income - abs(TurnOper.Sum);
      else                   /* оборот - в выданной сумме */
        Out = Out + abs(TurnOper.Sum);
      end;
    elif( TurnOper.Curr_Ref == Operat.IncomeCurr_Ref )
      Income = Income - abs(TurnOper.Sum);
    elif( TurnOper.Curr_Ref == Operat.OutCurr_Ref )
      Out = Out + abs(TurnOper.Sum);
    end;
  END;

  MACRO ОборотПоРеестру()
    if( Reestr.IncomeCurr_Ref == Reestr.OutCurr_Ref )
      if( Turnrstr.Sum > 0 ) /* оборот - в полученной сумме */
        Income = Income - abs(Turnrstr.Sum);
      else                   /* оборот - в выданной сумме */
        Out = Out + abs(Turnrstr.Sum);
      end;
    elif( Turnrstr.Curr_Ref == Reestr.IncomeCurr_Ref )
      Income = Income - abs(Turnrstr.Sum);
    elif( Turnrstr.Curr_Ref == Reestr.OutCurr_Ref )
      Out = Out + abs(Turnrstr.Sum);
    end;
  END;

  GetParm(3, WithoutTax);
  GetParm(4, WithoutCnvCoins);

  if( Вид == "По операции" )
    if( ( GetTurnoper( CDF_TurnPComInc     ) AND (TurnOper.Cash == UNSET_CHAR) ) OR
        ( GetTurnoper( CDF_TurnPComOut     ) AND (TurnOper.Cash == UNSET_CHAR) )  )
      ОборотПоОперации();
    end;
    if( (WithoutTax != "кроме налогов") AND
        GetTurnoper( CDF_TurnTaxWithoutCom ) AND (TurnOper.Cash == UNSET_CHAR) ) 
      ОборотПоОперации();
    end;
    if( (WithoutCnvCoins != "кроме вал. мелочи") AND
        GetTurnoper( CDF_TurnCnvCoins ) )
      if(TurnOper.Cash == UNSET_CHAR)
        ОборотПоОперации();
      end;
      while( GetNextTurnoper( CDF_TurnCnvCoins ) )
        if(TurnOper.Cash == UNSET_CHAR)
          ОборотПоОперации();
        end;
      end;
    end;

  elif( Вид == "По реестру" )
    if( GetTurnrstr( CDF_TurnPComInc       ) AND (Turnrstr.Cash == UNSET_CHAR) ) 
      ОборотПоРеестру()               
    end;
    if( GetTurnrstr( CDF_TurnPComOut       ) AND (Turnrstr.Cash == UNSET_CHAR) )
      ОборотПоРеестру()               
    end;
    if( (WithoutTax != "кроме налогов") AND
        GetTurnrstr( CDF_TurnTaxWithoutCom ) AND (Turnrstr.Cash == UNSET_CHAR) )
      ОборотПоРеестру()               
    end;
    if( (WithoutCnvCoins != "кроме вал. мелочи") AND
        GetTurnrstr( CDF_TurnCnvCoins ) )
      if(Turnrstr.Cash == UNSET_CHAR)
        ОборотПоРеестру();
      end;
      while( GetNextTurnrstr( CDF_TurnCnvCoins ) )
        if(Turnrstr.Cash == UNSET_CHAR)
          ОборотПоРеестру();
        end;
      end;
    end;

  end;
  SetParm( 1, Income );
  SetParm( 2, Out    );
END;

/****************************************************************************/
/* Процедура формирует название и открывает файл отчета                     */
/****************************************************************************/
MACRO MakeReportFile( id, NameFile )
   var StrNameFile;
   /*создание уникального имени файла отчета*/
   fnmerge( StrNameFile,              /*полное название файла-отчета*/
            GetIniString( "TEXTDIR", "exchange.ini", TRUE ),/*директория текстовых файлов-отчетов*/
            Trim( NameFile ),         /*имя файла-отчета без расширения*/
            Trim(string(UserNumber))  /*расширение - уникальный номер пользователя в сети*/
          );
   /*удаляем, если уже существует*/
   if( ExistFile(StrNameFile, 0) )
      DelFile(StrNameFile);
   end;
   /*перенаправление вывода в файл отчета*/
   SetOutPut( StrNameFile );
   /*открытие файла отчета*/
   if( Open( id, StrNameFile ) )
      return NameFile;
   else
      return "";
   end;
END;
/***************************************************************************/
/*  Процедура получает строку-штамп с инфой об обменном пункте             */
/***************************************************************************/
/*MACRO MakeStrStamp( )
   return Trim( Office.Name ) + " " +
          "Код ОКПО " + Trim( Office.OKPO ) + " " +
          "Регистрационный номер N " + Trim( Office.RegNumb ) + " " +
          Trim( Office.Address );
END;*/
/***************************************************************************/
/*  Процедура получает строку-штамп с инфой об обменном пункте             */
/***************************************************************************/
MACRO MakeStrStamp( )
   [########################################################
    ########################################################
    ########################################################
    ########################################################
   ]
   (
    Trim( Office.Name ):w,
    "Код ОКПО " + Trim( Office.OKPO ):w,
    "Регистрационный номер N " + Trim( Office.RegNumb ):w,
    Trim( Office.Address ):w
    );
END;