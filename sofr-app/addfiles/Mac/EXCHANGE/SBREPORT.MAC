/****************************************************************************
   RS-Bank        Обменный пункт
   sbreport.mac - макрос формирует отчетность по смене ОП для Сбербанка
   Copyright 1997 (c) R-Style Software Lab.
   Создание: 14-05-98 by AV

   SB_Report - стандартное название функции, формирующей отчет
   SessAddr  - адрес структуры данных по смене

****************************************************************************/
IMPORT "sbr_head.mac", /* заголовочный файл                          */
       "sbr_od.mac",   /* операционный дневник                       */
       "sbr_pod.mac",  /* приложение к операционному дневнику        */
       "sbr_oth.mac",  /* остальные формы отчетности: 53 54 61 63 78 */
       /* 7-09-98 ОД по образцу от уфимского СБ */
       "sbr_odn.mac";  /* операционный дневник                       */


ARRAY ReportsArray;
      ReportsArray(0) ="ф.24 : Операционный дневник (ОД)";
      ReportsArray(1) ="ф.25 : Приложение к ОД";
      ReportsArray(2) ="ф.53 : Приходные ордера на общие суммы";
      ReportsArray(3) ="ф.54 : Расходные ордера на общие суммы";
      ReportsArray(4) ="ф.61 : Ордера на пересылку денег и ценностей";
      ReportsArray(5) ="ф.63 : Ордера на общие суммы безналичных операций";
      ReportsArray(6) ="ф.78 : Учет ценных бланков";
      ReportsArray(7) ="     : Вся отчетность";

macro ВсяОтчетность()
   MsgBox( "Операционные дневники" );
   /*ОперационныйДневник_1();*//*образец от белгородского СБ*/
   ОперационныйДневник_2();   /*образец от уфимского СБ    */
   MsgBox( "Приложение к операционным дневникам" );
   ПриложениеОД();
   MsgBox( "Приходные ордера" );
   ВсеПриходныеОрдера();
   MsgBox( "Расходные ордера" );
   ВсеРасходныеОрдера();
   MsgBox( "Ордера на пересылку" );
   /*ВсеОрдераНаПересылку();*/
   Ордер61();
   MsgBox( "Ордера на безналичные суммы" );
   ОрдерНаБезНаличные();
   MsgBox( "Учет ценных бланков" );
   УчетЦенныхБланков();
end;

macro СформироватьОтчетностьСБ( ВидОтчетности )
   if( ВидОтчетности == 0 )
      /*ОперационныйДневник_1();*//*образец от белгородского СБ*/
      ОперационныйДневник_2();   /*образец от уфимского СБ    */
   elif( ВидОтчетности == 1 )
      ПриложениеОД();
   elif( ВидОтчетности == 2 )
      ВсеПриходныеОрдера();
   elif( ВидОтчетности == 3 )
      ВсеРасходныеОрдера();
   elif( ВидОтчетности == 4 )
      Ордер61();
   elif( ВидОтчетности == 5 )
      ОрдерНаБезНаличные();
   elif( ВидОтчетности == 6 )
      УчетЦенныхБланков();
   elif( ВидОтчетности == 7 )
      ВсяОтчетность();
   end;
end;

macro SB_Report( SessAddr )

  SetBuff( Session, SessAddr ); /* Session указывает на текущую смену */

  var Loop = True, i;

  While( Loop )
     i = Menu( ReportsArray,
               " ~Enter~ Выбор вида отчетности  ~Esc~ Выход",
               "Отчетность СБ"
             );
     if( i >= 0 )
        СформироватьОтчетностьСБ( i );
     else
       if( GetTrue( False, "Выйти из меню?") )
          Loop = False;
       end;
     end;
  end;

  return TRUE; /* успешное завершение */

end;


