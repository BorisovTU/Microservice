/*-RSL---------------------------------------------- R-Style Software Lab --*/
/*--------------- Подоходный налог с материальной выгоды -------------------*/
/*--------------- при проведении валютообменных операций -------------------*/
/* RS-Bank             подсистема Обменный пункт
   Создание: RA 6-04-99
   История:
*/

IMPORT BankInter, DeprIntr, ExchangeInter, rpctax_c, rtkladr;

IMPORT "define.mac";

/*глобальная структура с информацией об ОП*/
RECORD Office_Add   ( offadd,  "exchange.def" );
RECORD ArraySums    ( "arrsums.str" , "exchange.def" );
RECORD IncomTax_Data( "exoperat.dbt", "exchange.def" );

MACRO ПодоходныйНалог( СуммаОперации, КодВалюты, Курс, КурсЦБ, Резидент, КодОперации, МассивСумм )
   var СуммаНалога = $0.0, СуммаПоКурсуОп, СуммаПоКурсуЦБ;
   var БазаНалога = $0.0,  ПроцентНалога = 0.0;
   var ТипОперации = "";

   SetBuff( ArraySums, МассивСумм );

   if(   ( КодОперации == CDF_OperPayDocBuy )    OR
         ( КодОперации == CDF_OperPayDocBuyAcc ) OR
         ( КодОперации == CDF_OperCurrBuy )      OR
         ( КодОперации == CDF_OperUnpayCurrBuy ) )

       ТипОперации = "Покупка";

   elif( ( КодОперации == CDF_OperPayDocSale ) OR
         ( КодОперации == CDF_OperPayDocSaleAcc ) OR
         ( КодОперации == CDF_OperCurrSale ) ) 

       ТипОперации = "Продажа";
   end;

   if( ( ( ТипОперации == "Покупка" ) AND ( Курс > КурсЦБ ) ) OR
       ( ( ТипОперации == "Продажа" ) AND ( Курс < КурсЦБ ) )  )

      if( КодВалюты == {NationalCur} )
         СуммаПоКурсуОп = СуммаОперации;
         СуммаПоКурсуЦБ = ( СуммаОперации / Курс ) * КурсЦБ;
      else
         СуммаПоКурсуОп = СуммаОперации * Курс;
         СуммаПоКурсуЦБ = СуммаОперации * КурсЦБ;
      end;

      БазаНалога = Abs( СуммаПоКурсуОп - СуммаПоКурсуЦБ );

      if( Резидент == SET_CHAR )
         ПроцентНалога = Office_Add.IncTaxResident;
      elif( Резидент == UNSET_CHAR )
         ПроцентНалога = Office_Add.IncTaxNonResident;
      end;

      СуммаНалога = БазаНалога * ПроцентНалога / 100;
      /* округление до рублей (до ближайшего целого) */
      СуммаНалога = Floor( СуммаНалога/100 + 0.5 ) * 100;
      if( ValType(СуммаНалога) != V_MONEY )
         СуммаНалога = money(СуммаНалога);
      end;
   end;

   /*через МассивСумм значения передаются в программу*/
   ArraySums(0) = СуммаНалога;
   ArraySums(1) = БазаНалога;

   SetParm( 6, ArraySums );

   return True;
END;

/* ******************** Печать Справки О Доходах *********************** */

MACRO ПечатьСправкиОДоходах( buf )

  SetBuff( IncomTax_Data, buf );
  InputPanel_IdentClient = IncomTax_Data.ClientCode;
  COMCLNT.GetRecord(InputPanel_IdentClient);
  DateSplit( IncomTax_Data.Date, null, null, InputPanel_Year_InAll );
  BeginDate = Date ( 1, 1,InputPanel_Year_InAll);
  EndDate   = Date (31,12,InputPanel_Year_InAll);
  Выбрать_Данные_По_Очередному_клиенту( 1 );
    
  var stat = True;
  var Citizenship;
  var sCountry1 = "RUS";
  var sCountry2 = "RUS";
  var IDType = "",
      IDCode = "";
  var db,mb,yb;
  var p_1_54 = m_1_31,
      p_1_55 = m_1_33,
      p_1_56 = p_1_35,
      p_1_57 = p_1_37,
      p_1_58 = p_1_39,
      p_1_59 = p_1_45,
      p_1_60 = p_1_47,
      p_1_61 = p_1_49,
      p_1_62 = p_1_51,
      p_1_63 = p_1_53;
      /* Переменные для округления сумм налогов */
  var r_1_31 = RoundToRuble( m_1_31 ),
      r_1_33 = RoundToRuble( m_1_33 ),
      r_1_45 = RoundToRuble( p_1_45 ),
      r_1_47 = RoundToRuble( p_1_47 );
  var sCity = "";
  var sCorpus = "", iCorpus = 0;
  var sPostIndex = "";
  var sRegion = "";
  var sDistrict = "";
  var sPlace = "";
  var i;

  if (COMCLNT.CurRec.rec.NotResident == StrFor(0))
    Citizenship = 1;
    if ( COMCLNT.CurRec.rec.Country != "" )
      sCountry1 = COMCLNT.CurRec.rec.Country;
    else
      sCountry1 = "RUS";
    end;
    sCountry2  = "";
    sPostIndex = COMCLNT.CurRec.rec.PostIndex;
    sRegion    = getRegionNumber( COMCLNT.CurRec );
  else
    Citizenship = 2;
    sCountry1   = COMCLNT.CurRec.rec.Country;
    sCountry2   = COMCLNT.CurRec.rec.Country;
    sPostIndex  = "";
    sRegion     = "";
  end;

  sCity = COMCLNT.CurRec.rec.District;

  sDistrict = COMCLNT.CurRec.rec.Province;  // Район
  sPlace = TRIM(COMCLNT.CurRec.rec.codePlace)+" "+
           TRIM(COMCLNT.CurRec.rec.Place);

  if (GetPaprKind(COMCLNT.CurRec.rec.PaperKind))
    IDType = PAPRKIND.Name;
    IDCode = PAPRKIND.CodeDocum;
  end;
  DateSplit(COMCLNT.CurRec.rec.Born,db,mb,yb);

  /* Обработка строки "Корпус" */
  iCorpus = Index( StrUpr( COMCLNT.CurRec.rec.NumCorp ), "КОРПУС" );
  if ( iCorpus > 0 )
    if ( StrLen(COMCLNT.CurRec.rec.NumCorp) > ( iCorpus + 5 ) )
      sCorpus = SubStr( COMCLNT.CurRec.rec.NumCorp, iCorpus + 6 );
    else
      sCorpus = "";
    end;
  else
    sCorpus = COMCLNT.CurRec.rec.NumCorp;
  end;
  /* печать сокращенного варианта справки */
[                                                                                          ];
[                                                                             Форма 2-НДФЛ ];
[                СПРАВКА О ДОХОДАХ ФИЗИЧЕСКОГО ЛИЦА ЗА #### ГОД                            ]( InputPanel_Year_InAll );
[                     N ____________   В ИМНС ____________                                 ];
[                                                                                          ];
[ 1. Данные о налоговом агенте (источнике дохода)                                          ];
[ 1.1. ИНН/КПП (для физических лиц только ИНН)  ______________/_____________               ];
[ 1.2 Контактный телефон #########__________________________________________               ]( Bank_Phone );
[ ############################################################################             ]( ( "1.3 Наименование организации " +
                                                                                                Name_Bank +
                                                                                                Name_Depart
                                                                                              ):w );
[ 2. Данные о физическом лице - получателе дохода     2.1. ИНН ###############             ]( COMCLNT.CurRec.rec.INN );
[ 2.2. Статус # (1 - резидент, 2 - нерезидент)        2.3. Фамилия, Имя,                   ]( Citizenship );
[ ############################################################################             ]( ( "Отчество " +
                                                                                                COMCLNT.CurRec.rec.Name1 + " " +
                                                                                                COMCLNT.CurRec.rec.Name2 + " " +
                                                                                                COMCLNT.CurRec.rec.Name3
                                                                                              ):w );
[ 2.4. Код вида документа, удостоверяющего личность  #######                               ]( IDCode );
[ 2.5. Серия, номер документа ############ ###################################             ]( COMCLNT.CurRec.rec.PaperSeries,
                                                                                              COMCLNT.CurRec.rec.PaperNumber );
[ 2.6. Дата рождения ##.##.#### г.       2.7. Гражданство (код страны)                     ]( db, mb, yb );
[ _____________________###_______________________________________________                  ]( sCountry1 );
[ 2.8. Адрес постоянного места жительства:  Код страны   ###                               ]( sCountry2 );
[ Почтовый индекс ######  Код региона #####################                                ]( sPostIndex,
                                                                                              sRegion );
[ Район ###################################################                                ]( sDistrict );
[ ############################################################################             ]( ( "Город " + sCity +
                                                                                                "  Населенный пункт " + sPlace ) );
[ ######################################################################################## ]( "Улица " + TRIM(COMCLNT.CurRec.rec.StreetType) +
                                                                                              " " + TRIM(COMCLNT.CurRec.rec.StreetName) +
                                                                                              "  Дом " + COMCLNT.CurRec.rec.House +
                                                                                              "  Корпус " + sCorpus +
                                                                                              "  Квартира " + COMCLNT.CurRec.rec.Flat );
[                                                                                          ];
[ 3. Доходы, облагаемые налогом по ставке 13% (30%)                                        ];
[ 3.1. Доходы по видам                                                                     ];
[                                                                                          ];
[     Код       Сумма дохода         Код         Сумма вычета                              ];
[    дохода                         вычета                                                 ];
i = 0;
while( i < ASize( p_1_23 ) )
if( ( p_1_25(i) != $0l ) or ( ( СуммаМассива(p_1_25) == $0 ) and ( p_1_23(i) == "2900" ) ) )
[     #####    ###############     __________   ___________.__                             ]( p_1_23(i), p_1_25(i) );
end;
i = i + 1;
end;
[                                                                                          ];
[ 3.2. Доходы по месяцам                                                                   ];
[                                                                                          ];
[ Январь   ###########   Февраль  ###########   Март     ###########                       ]( ОбщаяСуммаЗаМесяц( p_1_24, 0 ),
                                                                                              ОбщаяСуммаЗаМесяц( p_1_24, 1 ),
                                                                                              ОбщаяСуммаЗаМесяц( p_1_24, 2 ) );
[ Апрель   ###########   Май      ###########   Июнь     ###########                       ]( ОбщаяСуммаЗаМесяц( p_1_24, 3 ),
                                                                                              ОбщаяСуммаЗаМесяц( p_1_24, 4 ),
                                                                                              ОбщаяСуммаЗаМесяц( p_1_24, 5 ) );
[ Июль     ###########   Август   ###########   Сентябрь ###########                       ]( ОбщаяСуммаЗаМесяц( p_1_24, 6 ),
                                                                                              ОбщаяСуммаЗаМесяц( p_1_24, 7 ),
                                                                                              ОбщаяСуммаЗаМесяц( p_1_24, 8 ) );
[ Октябрь  ###########   Ноябрь   ###########   Декабрь  ###########                       ]( ОбщаяСуммаЗаМесяц( p_1_24, 9 ),
                                                                                              ОбщаяСуммаЗаМесяц( p_1_24, 10 ),
                                                                                              ОбщаяСуммаЗаМесяц( p_1_24, 11 ) );
[                                                                                          ];
[ 3.4. Общая сумма дохода ################# 3.5. Облагаемая сумма дохода ################# ]( СуммаМассива( p_1_25 ),
                                                                                              p_1_29 );
[ 3.6. Сумма налога исчисленная #############  3.7. Сумма налога удержанная  ############# ]( r_1_31,
                                                                                              r_1_33 );


if ( ( r_1_45 != $0 )  OR  ( r_1_47 != $0 ) )
  [                                                                                          ];
  [ 5. Доходы, облагаемые налогом по ставке 35%                                  ];
  [ 5.1. Доходы по видам                                                         ];
  [                                                                              ];
  [   Код       Сумма дохода         Код         Сумма вычета                    ];
  [  дохода                         вычета                                       ];
i = 0;
while( i < ASize( p_1_23 ) )
if( p_1_23(i) == КодыДоходов( RPCB_TAX_CHANGE_RETAIL ) ) /* ВОО - только 13% & 30% */
  [   #####    ###############     __________   ___________.__                   ]
  ( p_1_23(i), p_1_41(i) );
  i = ASize( p_1_23 );
end;
i = i + 1;
end;
  [                                                                              ];
  [ 5.2. Общая сумма дохода       ##################  5.3. Облагаемая сумма дохода  ################## ]
  ( СуммаМассива( p_1_41 ), p_1_43 );
  [ 5.4. Сумма налога исчисленная ##################  5.5. Сумма налога удержанная  ################## ]
  ( r_1_45, r_1_47 );
  [                                                                              ];
end;



[                                                                                          ];
[ 6. Общая сумма налога на доходы по итогам налогового периода                             ];
[ 6.1. Общая сумма налога исчисленная ##################                                   ]( r_1_31 );
[ 6.2. Общая сумма налога удержанная  ##################                                   ]( r_1_33 );
[ 6.3. Сумма возврата налогов по перерасчету с доходов                                     ];
[ прошлых лет ##################                                                           ]( r_1_55 + p_1_39 );
[ 6.5. Долг по налогу за налогоплательщиком ##################                             ]( r_1_56 + p_1_35 );
[ 6.6. Долг по налогу за налоговым агентом  ##################                             ]( r_1_57 + p_1_37 );
[                                                                                          ];
[                                                                                          ];
[ Главный бухгалтер ______________ ______________  Дата составления ##########             ]( String({curdate}) );
[                      (Подпись)        (ФИО)                                              ];
[                                                                                          ];
[                                                                                          ];
[          М.П.                                                                            ];

END;