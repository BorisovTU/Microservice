/*-RSL---------------------------------------- R-Style Software Lab --*/
/*---------------- Сортирока серий и номеров чего угодно -------------*/
/* RS-Bank      подсистема Обменный пункт             RA 11-02-02     */

/* Серии и номера документов */
CLASS SerNum( Series_, Min_, Max_ )

  var
  Series : string ,
  Min    : integer,
  Max    : integer;

  /* конструктор, даже умеет работать без параметров ;-)) */
  if( valtype(Series_) == V_UNDEF ) Series_ = ""; end;
  if( valtype(Min_   ) == V_UNDEF ) Min_    =  0; end;
  if( valtype(Max_   ) == V_UNDEF ) Max_    =  0; end;

  if( Min_ > Max_ ) 
    MsgBox( "Ошибка номеров: Min > Max\n", Series_, " ( ", Min_, " > ", Max_ , " )" ); 
    Exit(1); 
  end;

  Series = Series_;
  Min    = Min_   ;
  Max    = Max_   ;
  
END;

/*
            QuickSort. Быстрая сортировка. Рекурсивный вариант.

  Макрос CmpMacro( key, elem ):integer принимает параметры - сравниваемые
  элементы массива и возвращает: <0 - key <  elem,
                                  0 - key == elem,
                                 >0 - key >  elem.
*/
private MACRO qsort( arr:TArray, CmpMacro )
      macro QuickSort(left:integer, right:integer)
            Var i:integer = left, j:integer = right, flag:bool = TRUE,
                swap,
                test = arr[Int((left+right)/2.0)];

            while(flag)
                 while(ExecMacro2(CmpMacro, arr[i], test) < 0) i=i+1; end; /* < */
                 while(ExecMacro2(CmpMacro, test, arr[j]) < 0) j=j-1; end; /* < */

                 if(i <= j)
                     swap = arr[i]; arr[i] = arr[j]; arr[j] = swap;
                     i=i+1; j=j-1;
                 end;

                 if(i > j) flag = FALSE; end;
            end;

            if(left < j)  QuickSort(left, j);  end;
            if(i < right) QuickSort(i, right); end;
     end;

     /* --------------------------------------------------------------------- */
     if(arr.Size() > 1) QuickSort(0, arr.Size()-1); end;
END;

/* Сравнение серий и номеров документов */
MACRO CmpSerNum( SerNum1 : SerNum, SerNum2 : SerNum )

  if  ( SerNum1.Series > SerNum2.Series )
    return +1;
  elif( SerNum1.Series < SerNum2.Series )
    return -1;
  else /* одинаковая серия */
    if  ( SerNum2.Max <= SerNum1.Min )
      return +1;
    elif( SerNum1.Max <= SerNum2.Min )
      return -1;
    elif(    ( SerNum1.Min == SerNum2.Min )
          and( SerNum1.Max == SerNum2.Max )
        )
      return 0;
    else
      MsgBox( "Ошибка: пачки ", 
              SerNum1.Series, " ", SerNum1.Min, " ", SerNum1.Max, "\n и ",
              SerNum2.Series, " ", SerNum2.Min, " ", SerNum2.Max, " пересекаются" );
      Exit(1);
    end;
  end;
END;

/* Формирует строку из массива справок, упорядочивая его */
MACRO MakeSerNumString( SerNums: TArray, Num, Delimiter )
  var i=1;
  var ResultString = "";
  var BeginOfPack: SerNum;

  qsort( SerNums, "CmpSerNum" );

  Num = 0;
  if( SerNums.Size > 0 )

    Num          = SerNums[0].Max - SerNums[0].Min + 1;
    ResultString = SerNums[0].Series + " " + SerNums[0].Min;

    while( i < SerNums.Size ) 
     if(   ( SerNums[i-1].Series != SerNums[i].Series  ) /* сменилась серия */
         or( SerNums[i-1].Max    != SerNums[i].Min - 1 ) /* пропущен номер  */
       )
       if( BeginOfPack.Min != SerNums[i-1].Max )
         ResultString = ResultString + "-" + SerNums[i-1].Max;
       end;
       ResultString = ResultString + Delimiter + SerNums[i].Series + " " + SerNums[i].Min;
       BeginOfPack = SerNums[i];
     end;
     Num = Num + SerNums[i].Max - SerNums[i].Min + 1;
     i   = i+1;
    end;
    if( BeginOfPack.Min != SerNums[i-1].Max )
      ResultString = ResultString + "-" + SerNums[i-1].Max;
    end;

  end;

  SetParm( 1, Num );
  return ResultString;
END;

