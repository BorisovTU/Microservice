/*-RSL---------------------------------------- R-Style Software Lab --*/
/*---------------------- Работа со строками --------------------------*/
/* RS-Bank             подсистема Обменный пункт                      */
/*                                                                    */
/*   Copyright 1999 (c) R-Style Software Lab.                         */
/*   Создание: Сияльский В.Д.                                         */
/*--------------------------------------------------------------------*/

macro StrRNChr( str, sym, len ) /* Позиция последнего включения символа */
  var i = index( str, sym ),    /* в строку длиной не более len        */
      k = 0,
      l = 0;
  while( i )
    str = substr( str, i+1 );
    k = k + i;
    i = index( str, sym );
    if( k > len ) i = 0; else l = k; end;
  end;
  return l;
end;

/* Заполнить конец строки заданным символом на заданную длину.
   Возвращает итоговую строку. Если заданная длина <= длины строки,
   возвращается исходная строка.
*/
macro StrFillEnd( str, symb, len )
  return str + mkstr( symb, len - strlen( str ) );
end;

macro StrFillBeg( str, symb, len )
  return mkstr( symb, len - strlen( str ) ) + str;
end;

/*
   Замена в строке str символов What на символ Symb.
   Заимствован из \KVITER\format.mac и преобразован.
*/
macro ChangeSymbol( str, What, Symb )
  var i;
  while( (i = index( str, What )) != 0 )
    strset( str, i, Symb );
  end;
  setparm( 0, str );
  return str;
end;

/* Удаление всех указанных символов из строки */
macro DelSymbol( str, Symb )
  var str1, str2, i;
  while( (i = index( str, Symb )) != 0 )
    str1 = substr( str, 1, i-1 );
    str2 = substr( str, i+1 );
    str = str1 + str2;
  end;
  setparm( 0, str );
  return str;
end;

/* -------------------------------------------------------------------------- */
/* Преобразует сумму в строку с заменой "." на "-"   RA 02-09-99              */
/* -------------------------------------------------------------------------- */
MACRO StrHyphen( Sum )
   var str1, str2 = "0   ";
   str1 = string( Sum );
   if( Index(str1,".") == 1 ) /* RA 15-09-99 */
     StrSet(str2, 2, str1);
   else
     str2 = str1;
   end;
   ChangeSymbol( str2, ".", "-" );
   return str2;
END;

/* -------------------------------------------------------------------------- */
/* Возвращает строку в формате "746-23 французких франков"   RA 02-09-99      */
/* -------------------------------------------------------------------------- */
MACRO CurToStrHyphen( Sum, Целых, Сотых, Curr_Ref )
   var str, Len, Валюта;
   str = CurToStrAlt( $0.0, Целых, Сотых, Curr_Ref );
   /* StrLen("Ноль ")+1 = 6 */
   Len = Index( str, " 0" ) - 6;
   Валюта = SubStr( str, 6, Len );
   return StrHyphen( Sum ) + " " + Валюта;
END;


