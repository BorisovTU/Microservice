/*-RSL---------------------------------------- R-Style Software Lab --*/
/*----------- Корректировка настроек по операции макросом. -----------*/
/*            Обменный пункт                                          */
/*                                                                    */
/* Copyright 2000 (c) R-Style Software Lab.                           */
/* Создание: Сияльский В.Д. 14-08-00                                  */
/*           Рытик А.И.     01-09-00                                  */
/*           Рытик А.И.     14-03-02  все переделал                   */
/*                                                                    */
/* Принимаемые значения:                                              */
/*   addr         адрес буфера настроек операции                      */
/*   IncomeRefVal код полученной ценности                             */
/*   OutRefVal    код выданной ценности                               */
/*                                                                    */
/* Возвращаемые значения:                                             */
/*   retval   выполнялась ли коррекция (True/False)                   */
/*   addr     адрес буфера настроек операции  (скорректированного)    */
/*  ------------------------------------------------------------------*/

import CommonInter, ExchangeInter, DeskInter, "define.mac";
var ComNumTarif;

record operset_buff( exopset, "exchange.def" );

/* Вызываемый макрос -- имя изменять нельзя!!!
   В дальнейшем сделать шагом и вызывать один макрос с шагом SET_COMISSION.
*/
MACRO ChangeOperset( addr, IncomeRefVal, OutRefVal )
  var retval = false;
  var IncomeCode_Currency, OutCode_Currency;

/*---- корректировка тарифа для конкретного ПД --------
  file CashVal     ( "sb_casvl.dbt", "sbbank.def" ) key 0;
  MACRO FindCashValue( RefValue )
    CashVal.RefValue = RefValue;
    return GetEQ(CashVal);
  END;
  -----------------------------------------------------*/

  setbuff( operset_buff, addr );

  ComNumTarif         = operset_buff.ComNumTarif;
  IncomeCode_Currency = FindCodCurByRefVal( IncomeRefVal );
  OutCode_Currency    = FindCodCurByRefVal( OutRefVal    );

/*---- корректировка тарифа для конкретного ПД --------
  if(    (operset_buff.Kind_Oper == CDF_OperPayDocIncasso)
      and FindCashValue( IncomeRefVal )
      and (CashVal.TypeValue == 4 /*TYPERES_PAYDOC*/)
    )
    if( CashVal.CodIntValue == 1 )
      operset_buff.GuaranteeNumTarif = 24;
    end;
    retval = true;
  end;
  -----------------------------------------------------*/
  
  return retval;
END;

/**************************************************************************\
|                               Поиск тарифа                               |
\**************************************************************************/
file tarif      ( sb_tarif, "sbbank.def" ) key 0;
file tarif_plus ( sb_tarpl, "sbbank.def" ) key 0;

MACRO FindTarif( Num, _Date, _Sum )
  var stat;

  tarif.IsCur = 1;
  tarif.NumTarif = Num;

  stat = GetEQ(tarif);

  if ( stat  AND  ( tarif.TarifPlus ) )
    tarif_plus.IsCur    = 0;
    tarif_plus.NumTarif = Num;
    tarif_plus.Date     = _Date;
    tarif_plus.SumOper  = _Sum;
    if ( GetGE( tarif_plus )            AND
         ( tarif_plus.IsCur    == 0 )   AND
         ( tarif_plus.NumTarif == Num ) AND
         ( tarif_plus.Date     == _Date ) )
      tarif.Percent = tarif_plus.Percent;
      tarif.MinSum  = tarif_plus.MinSum;
      tarif.MaxSum  = tarif_plus.MaxSum;
      tarif.Summa   = tarif_plus.Summa;
    end;
  end;

  return stat;
END;

/*-RSL---------------------------------------- R-Style Software Lab --*/
/*---------- Расчет сумм по валютнообменной операции макросом. -------*/
/*           Обменный пункт                                           */
/*                                                                    */
/* Создание: Рытик А.И. 12-03-02                                      */
/*                                                                    */
/* Принимаемые значения:                                              */
/*   addr              адрес буфера операции                          */
/*   CalcType          тип расчета:        0 - от полученной суммы    */
/*                                         1 - от выданной суммы      */
/*   ComSumType        тип суммы комиссии: 0 - от полученной суммы    */
/*                                         1 - от выданной суммы      */
/*   CalcPComInDefCurr признак расчета комиссии в нац валюте          */
/*                                                                    */
/* Возвращаемые значения:                                             */
/*   retval   выполнялся ли расчет (True/False)                       */
/*   addr     адрес буфера операции  (скорректированного)             */
/*  ------------------------------------------------------------------*/

Import DeskInter, "cur_rate.mac";
record operat( exoperat, "exchange.def" );

/* Вызываемый макрос -- имя изменять нельзя!!! */
MACRO Calculate( addr, CalcType, ComSumType, CalcPComInDefCurr )
  var retval      = false;
  var Code_Currency, ISOCurr;
  var RateKind    = 2;   /* Кросс-курсы для ЕВРО */
  var EURO_RefVal = 171; /* EUR */
  var EURO_CodCur = 5;
  var CrossRate   = 0;
  var SellRate     = 0;
  var dtRate, tmRate;

  setbuff( operat, addr );

  PrepareDateTimeForRates( true, operat.CurDate, null, dtRate, tmRate );

  if(    /* операция приема на инкассо или экспертизу */
         ( operat.Kind_Oper == CDF_OperIncasso       ) /* Прием на инкассо       */
      OR ( operat.Kind_Oper == CDF_OperExpert        ) /* Прием на экспертизу    */
      OR ( operat.Kind_Oper == CDF_OperPayDocIncasso ) /* Прием ПД на инкассо    */
      OR ( operat.Kind_Oper == CDF_OperPayDocExpert  ) /* Прием ПД на экспертизу */
    )    
    Code_Currency = FindCodCurByRefVal( operat.IncomeRefVal );
    ISOCurr       = GetISOCurr( Code_Currency );

    if(   /* принимаемая ценность - в валюте страны Евросоюза  */
          ( ISOCurr == "DEM" )                   /* Германия   */
       OR ( ISOCurr == "ATS" )                   /* Австрия    */
       OR ( ISOCurr == "FRF" )                   /* Франция    */
       OR ( ISOCurr == "ITL" )                   /* Италия     */
       OR ( ISOCurr == "BEF" )                   /* Бельгия    */
       OR ( ISOCurr == "NLG" )                   /* Нидерланды */
       OR ( ISOCurr == "LUF" )                   /* Люксембург */
       OR ( ISOCurr == "ESP" )                   /* Испания    */
       OR ( ISOCurr == "PТЕ" )                   /* Португалия */
       OR ( ISOCurr == "IEP" )                   /* Ирландия   */
       OR ( ISOCurr == "FIM" )                   /* Финляндия  */
      )
      if( FindCurrRate( Code_Currency, RateKind, dtRate, null, null, null, tmRate ) )
        CrossRate = CurrRate.CrossRate / CurrRate.ChangeNom;
        if( CurrRate.OutRate and CrossRate )
          CrossRate = 1 / CrossRate;
        end;
         
        if(   ( ComNumTarif != 0 )
            and FindTarif( ComNumTarif, operat.CurDate, operat.IncomeAmount )
          )
          operat.ComAmount     = Money( operat.IncomeAmount * tarif.Persent / 1000000 ); /* % от принятой суммы */
          operat.ComCurrRefVal = EURO_RefVal;                   /* EUR */
          operat.ComAmount     = operat.ComAmount * CrossRate;

          retval = true;

          if( CalcPComInDefCurr )                  /* пересчитать в рубли */
            if( FindCurrRate( EURO_CodCur, MAINRATEKIND, dtRate, null, null, SellRate, tmRate, operat.ComAmount ) )
              operat.ComCurrRefVal = Office.RVNationalCurr;      /* нацвалюта */
              operat.ComAmount     = operat.ComAmount * SellRate;
            end;
          end;
        end;
      end;
    end;

  end;

  return retval;
END;
