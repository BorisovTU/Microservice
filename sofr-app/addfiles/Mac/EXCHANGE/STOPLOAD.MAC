/*-RSL---------------------------------------- R-Style Software Lab --*/
/*---------   Загрузка обновлений СТОП-листа по ценностям  -----------*/
/*                        из текстового файла.                        */
/*--------------------------------------------------------------------*/
/* RS-Bank      подсистема Обменный пункт            AV  24-12-97     */

IMPORT ExchangeInter, "sprvfunc.mac";

file stoplist("stoplist.dbt","exchange.def") write key 2;
record StopRec("stoplist.dbt","exchange.def");
file txt()txt;

const UpdateRec = 0, /*обновит запись*/
      InsertRec = 1, /*вставить запись*/
      EqualRec  = 2; /*проброс записи, т.к. такая уже есть в базе*/

var NumRec  = 0,  /*количество прочитанных записей*/
    NumIns  = 0,  /*количество вставленных записей*/
    NumUpdt = 0,  /*количество обновленных записей*/
    PrintEqual,   /*флаг печати информации о дублирующихся записях*/
    NameTxtFile;  /*имя тексового файла загрузки  */

/*массивы имен статусов*/
array KindUpdt, NameState;



/*PrintEqual = False;/* не печатать */*/
PrintEqual = True;/* печатать */

KindUpdt(UpdateRec) = "Обновлена";
KindUpdt(InsertRec) = "Добавлена";
KindUpdt(EqualRec ) = "Пропущена";

NameState(0) = "Не активна";
NameState(1) = "Активна";
NameState(2) = "Удалена";

/*Номера полей в текстовом файле(обновления СТОП-листа)*/
const
      NF_CodeTrs    = 0,  /*код ценности в кодификаторе  */
      NF_CodeCurr   = 1,  /*код валюты по справочнику    */
      NF_Nominal    = 2,  /*номинал ценности             */
      NF_Seria      = 3,  /*серия ценности               */
      NF_Number     = 4,  /*номер ценности               */
      NF_State      = 5,  /*признак активности ценности  */
      NF_Note       = 6,  /*примечание                   */
                          /*составляющие дат и времен добавления и изменения*/
      NF_di         = 7,  /**/
      NF_mi         = 8,  /**/
      NF_yi         = 9,  /**/
      NF_dc         = 10, /**/
      NF_mc         = 11, /**/
      NF_yc         = 12, /**/
      NF_hi         = 13, /**/
      NF_mini       = 14, /**/
      NF_seci       = 15, /**/
      NF_hc         = 16, /**/
      NF_minc       = 17, /**/
      NF_secc       = 18; /**/

/*процедура выделения имени файла из строки с путем до него*/
macro GetNameFromPath( PathName )
  var PosSlash;
  while( (PosSlash = Index( PathName , "\\" )) != 0 )
   PathName = SubStr(PathName, PosSlash+1);
  end;
  return PathName;
end;

/*получения кода группы ценностей по коду самой ценности*/
macro GetTrsGroup( CodeTrs )
 /* Тратты */
  if( CodeTrs == TrsTratt )
      return TrsRoot;
 /* Монета или Банкноты и казначейские билеты */
  elif( (CodeTrs == TrsCash) OR (CodeTrs == TrsMonet) )
      return TrsNoteGrp;
 /* Чеки или Аккредитивы */
  elif( (CodeTrs == TrsCheck) OR (CodeTrs == TrsAccreditive) )
     return TrsPayDoc;
  end;
end;

/*процедура инициализации временного буфера StopRec значениями из текстовой строки*/
macro GetRecFromTxtString

 StopRec.Kind_Trs   = int(txt(NF_CodeTrs));
 StopRec.Group_Trs  = int(GetTrsGroup(StopRec.Kind_Trs));
 StopRec.Curr_Ref   = int(txt(NF_CodeCurr));
 StopRec.Nominal    = money(double(txt(NF_Nominal)) * 100.);
 StopRec.Seria      = txt(NF_Seria);
 StopRec.Number     = txt(NF_Number);
 StopRec.State      = int(txt(NF_State));
 StopRec.DateInput  = Date( int(txt(NF_di)),int(txt(NF_mi)),int(txt(NF_yi)) );
 StopRec.TimeInput  = Time( int(txt(NF_hi)),int(txt(NF_mini)),int(txt(NF_seci)) );
 StopRec.DateChange = Date( int(txt(NF_dc)),int(txt(NF_mc)),int(txt(NF_yc)) );
 StopRec.TimeChange = Time( int(txt(NF_hc)),int(txt(NF_minc)),int(txt(NF_secc)) );
 if(txt(NF_Note) == 0) /*если символ конца строки*/
    StopRec.Note  = "";
 else StopRec.Note  = txt(NF_Note)
 end;
end;

macro CheckStoplist

 if( (StopRec.Kind_Trs  == stoplist.Kind_Trs  ) AND
     (StopRec.Group_Trs == stoplist.Group_Trs ) AND
     (StopRec.Curr_Ref  == stoplist.Curr_Ref  ) AND
     (StopRec.Nominal   == stoplist.Nominal   ) AND
     (StopRec.Seria     == stoplist.Seria     ) AND
     (StopRec.Number    == stoplist.Number    ) )
     if( (StopRec.Note    != stoplist.Note      ) OR
         (StopRec.State   != stoplist.State     ) ) return UpdateRec;
     else return EqualRec;
     end;
 else return InsertRec;
 end;
end;

/*печать шапки*/
macro PrintHead

ОбщаяШапка();/*информация об обменном пункте*/
[
                ЗАГРУЗКА ИНФОРМАЦИИ В СТОП-ЛИСТ ПО ЦЕННОСТЯМ
                     #                           #

                          из файла ############

+----------+---+------------+-----+---------------------+----------+---------+
|  Код     |Вал|   Номинал  |Серия|        Номер        |  Статус  | Действие|]
(
   StrAddZeroDate( Date ), Time,
   GetNameFromPath( NameTxtFile ):l
)
end;

/*печать информации по измененным или добавленным записям в СТОП-листе*/
macro PrintRec( Kind )

[+----------+---+------------+-----+---------------------+----------+---------+
|    ##    |###|############|#####|#####################|##########|#########|]
(
   stoplist.Kind_Trs,
   stoplist.Curr_Ref,
   stoplist.Nominal,
   stoplist.Seria:r,
   stoplist.Number:r,
   NameState(stoplist.State),
   KindUpdt( Kind )
);
end;

/*печать дна*/
macro PrintBottom
 var NumSkip = NumRec-( NumIns + NumUpdt );
[+----------+---+------------+-----+---------------------+----------+---------+
 |      Прочитано ###### записей.                                             |
 |      Пропущено ###### записей.                                             |
 |      Добавлено ###### записей.                                             |
 |      Обновлено ###### записей.                                             |
 +----------------------------------------------------------------------------+
]( NumRec:c, NumSkip:c, NumIns:c, NumUpdt:c );
end;

macro UpdateStoplist()
 var CheckStat;   /*статус сравнения найденой записи со строкой из txt-файла*/

 SetDelim(",");
 if( SelectFile(  NameTxtFile, "*.lst"," Выбор файла с информацией по СТОП-листу") == True)
    Open( txt , NameTxtFile );

    PrintHead(); /*печатаем шапку*/

    rewind( txt );
    while( next( txt ) )
       NumRec = NumRec + 1;
   /* Перенос содержимого строки текстового файла во временный буфер StopRec */
       ClearRecord( StopRec );
       GetRecFromTxtString;

   /*установка ключа для поиска записей в stoplist.dbt*/
       ClearRecord( stoplist );
       /*Copy( stoplist, StopRec );*/
       stoplist.State     = 0; /*c любым статусом*/
       stoplist.Group_Trs = StopRec.Group_Trs;
       stoplist.Kind_Trs  = StopRec.Kind_Trs;
       stoplist.Curr_Ref  = StopRec.Curr_Ref;
       stoplist.Nominal   = StopRec.Nominal;
       stoplist.Seria     = StopRec.Seria;
       stoplist.Number    = StopRec.Number;

       if( GetGE(stoplist) == True )
          /*проверка найденной записи*/
          CheckStat = CheckStoplist();
          if( CheckStat == UpdateRec )
             /*обновление записи*/
             Copy( stoplist, StopRec );
             stoplist.DateChange = date;
             stoplist.TimeChange = time;
             update(stoplist);
             PrintRec(UpdateRec);
             NumUpdt = NumUpdt + 1;
          elif( CheckStat == EqualRec )
             /*запись в текстовом файле идентична найденной записи в базе*/
             if( PrintEqual == True ) PrintRec(EqualRec); end;
          elif( CheckStat == InsertRec )
             /*добавление записи*/
             Copy( stoplist, StopRec );
             if( stoplist.State != 2 )/*записи со статусом Удалена не добавляем*/
                stoplist.DateInput  = stoplist.DateChange = date;
                stoplist.TimeInput  = stoplist.TimeChange = time;
                insert(stoplist);
                rewind(stoplist);
                PrintRec(InsertRec);
                NumIns = NumIns + 1;
             else if( PrintEqual ) PrintRec(EqualRec); end;
             end;
          end;
       else
          /*добавление записи*/
          Copy( stoplist, StopRec );
          if( stoplist.State != 2 )/*записи со статусом Удалена не добавляем*/
             stoplist.DateInput = stoplist.DateChange = date;
             stoplist.TimeInput = stoplist.TimeChange = time;
             insert(stoplist);
             rewind(stoplist);
             PrintRec(InsertRec);
             NumIns = NumIns + 1;
          else if( PrintEqual ) PrintRec(EqualRec); end;
          end;
       end;

    end;

    PrintBottom();
 end;

end;

UpdateStoplist;

