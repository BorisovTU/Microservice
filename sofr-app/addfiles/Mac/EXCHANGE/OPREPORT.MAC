/****************************************************************************
   RS-Bank        Обменный пункт
   opreport.mac - макрос формирует отчет по операции
   Copyright 1997 (c) R-Style Software Lab.
   Создание: 23-04-98 by AV

   MakeOperReport - стандартное название функции, формирующей отчет
   RecAddr        - адрес структуры данных по операции

****************************************************************************/

IMPORT ExchangeInter, "define.mac";

RECORD Data( "opreport.str", "exchange.def" );

Const INCOME_SUM =  1, /*полученная сумма*/
         OUT_SUM = -1; /*выданная сумма  */

/****************************************************************************/
/*  Процедура преобразует число в строку                                    */
/*  необязат. параметр ext = true - преобразовать в double, потом в строку  */
/****************************************************************************/
MACRO ValueToString( Value/*, Ext */ )
   var Ext;
   if( (GetParm( 1, Ext ) == true) )
      if( (Ext == true ) )
         return Trim( string((double(Value)/100.):18:4 ) );
      end;
   end;
   return Trim( string(Value) );
END;

MACRO MakeOperReport( RecAddr )
  var StrSignProc, StrSignTax, SumProcCom, TaxValue;
  var StrPr, StrRate, StrProcCom, StrTax, StrtdF, StrCalcSum, StrTaxBase;
  var CalcCurr, SecCurr, StrtP, StrtP2, StrConvRate, StrtdP, PercCurr, SecPercCurr;
  var StrDescrFormula, StrFormula, EndStrForComis, BegStrForComis, StrDescrTax;
  var ComName, StrA, StrtF, StrDescrPr;
  SetBuff( Data, RecAddr ); /* Oper указывает на текущую запись об операции */

  if  ( Data.direction == -1 ) StrSignProc = " + ";
  elif( Data.direction ==  1 ) StrSignProc = " - ";
  else                         StrSignProc = " +- ";
  end;

  if  ( Data.dirTax ==  1 ) StrSignTax  = " + ";
  elif( Data.dirTax == -1 ) StrSignTax  = " - ";
  else                      StrSignTax  = " +- ";
  end;

  /*определение суммы процентной комиссии*/
  if( (Data.type != CDF_ComFix) AND (Data.type != CDF_ComNotUsed) )
     if  ( Data.tdP  ) SumProcCom = Data.tdP;
     elif( Data.tdP1 ) SumProcCom = Data.tdP1;
     else              SumProcCom = 0;
     end;
  else  SumProcCom = 0;
  end;

  TaxValue = (double( Data.TaxValue ) /100.);

  StrPr       = ValueToString(Data.Pr,       true );
  StrRate     = ValueToString(Data.Rate           );
  StrProcCom  = ValueToString(SumProcCom,    true );
  StrTax      = ValueToString(TaxValue            );
  StrtdF      = ValueToString(Data.tF1,      true );
  StrCalcSum  = ValueToString(Data.CalcSum,  true );
  StrTaxBase  = ValueToString(Data.TaxBase,  true );

  /*определение валюты рассчитываемой суммы*/
  if  ( Data.IncomeOrOut == INCOME_SUM )
     CalcCurr = Data.IncomeCurr;
      SecCurr = Data.OutCurr;
  elif( Data.IncomeOrOut ==    OUT_SUM )
     CalcCurr = Data.OutCurr;
      SecCurr = Data.IncomeCurr;
  end;

  if( valtype( CalcCurr ) == V_UNDEF )
     MsgBox("Не задана валюта расчитываемой суммы!");
     return FALSE; /*отчет не сформирован*/
  end;

  if( NOT Data.PComDef )
     StrtP = StrtP2 = ValueToString(Data.tP,       true );
     StrConvRate = ValueToString( Data.ConvRate );
     StrProcCom  = ValueToString( SumProcCom, true );
     StrtdP      = ValueToString( Data.tdP,   true );
     PercCurr    = CalcCurr;
     SecPercCurr = SecCurr;
  else
     StrtP2      = ValueToString( Data.PComDefOpCurrSum,  true );/*начальная расчетная сумма*/
     StrtP       = ValueToString( Data.PComDefBegCurrSum, true );
     StrConvRate = ValueToString( Data.PComDefRate*100.,  true );
     StrProcCom  = ValueToString( Abs(Data.PComDefCurrSum) );
     StrtdP      = ValueToString( Abs(Data.PComDefCurrSum)  );
     PercCurr    = {NationalCur};
     SecPercCurr = Data.PComDefCurr;
  end;


  /*формирование строк с формулами расчета*/
  if( NOT Data.PComDef )/*если % комиссия не в заданной нац валюте*/
     if  ( Data.ComParitySum == 1 )/*сумма пересчитывается через конверс.курс*/
         if( CalcCurr == {NationalCur} )
            StrDescrFormula = "Pr = Rate * tP +- ConvRate * tdP +- Tax (Формула расчета)";
         else
            StrDescrFormula = "Pr = Rate * tP +- ConvRate * tdP (Формула расчета)";
         end;
         StrFormula = StrPr + " = "
                      + StrRate + "*" + StrtP + StrSignProc
                      + StrConvRate + "*" + StrProcCom;
        /*формула расчета процентной комиссии*/
        EndStrForComis = "RealPercComSum = ConvRate * tdP = "
                    + StrConvRate + " * " + StrProcCom + " = "
                    + string( money(SumProcCom*Data.ConvRate) )
                    + " " + GetISOCurr(CalcCurr);
     else
        if( CalcCurr == {NationalCur} )
          StrDescrFormula = "Pr = tP +- tdP +- Tax (Формула расчета)";
        else
          StrDescrFormula = "Pr = tP +- tdP (Формула расчета)";
        end;
        StrFormula = StrPr + " = "
                     + StrtP + StrSignProc + StrProcCom;
        /*формула расчета процентной комиссии*/
        EndStrForComis = "RealPercComSum = tdP = "
                      + string( SumProcCom ) + " " + GetISOCurr(CalcCurr);
     end;
  else/*если % комиссия в заданной нац валюте*/
     StrDescrFormula = "Pr = Rate * tP";
     StrFormula      = StrPr + " = "
                      + StrRate + "*" + StrtP2;
     /*формула расчета процентной комиссии*/
     EndStrForComis = "RealPercComSum = ConvRate * tdP = "
                 + StrConvRate + " * " + StrProcCom + " = "
                 + string( Abs(Data.PComDefSum) )
                 + " " + GetISOCurr( {NationalCur} );
  end;

  BegStrForComis = "Реально взята процентная комиссия: ";

/*учет налога, налог входит в расчет, если расчитывается сумма в нац.валюте*/
  if( TaxValue != 0.0 )
     if( CalcCurr == {NationalCur} )
        StrFormula  = StrFormula + StrSignTax + StrTax;
        StrDescrTax = "";
     else
        StrFormula  = StrFormula + StrSignTax + StrRate + "*" + StrTax;
        StrDescrTax = "";
     end;
  else
     StrDescrTax = "";
  end;

  ComName = GetNameCodif( Data.type, CDF_ComType );

  /*добавление ISO-кодов в строки сумм*/
  StrRate      = StrRate + " " + GetISOCurr(CalcCurr      ) + " за "
                            + GetISOCurr(SecCurr) + " - курс операции";
  StrConvRate  = StrConvRate + " " + GetISOCurr(PercCurr      ) + " за "
                            + GetISOCurr(SecPercCurr) + " - пересчет % комиссии "
                            + "(" + GetNameCodif( Data.TypeConvRate, CDF_TypeCalcRate ) + ")";
  /*начальная сумма расчета*/
  if( NOT Data.PComDef )
     StrtP = StrtP2 = StrtP  + " " + GetISOCurr( Data.Curr_Ref );
  else
     StrtP  = StrtP  + " " + GetISOCurr( Data.PComDefCurr );
     StrtP2 = StrtP2 + " " + GetISOCurr( SecCurr );
  end;

  if( Data.a )/*если процент комиссии ненулевой*/
     if( NOT Data.PComDef )
        StrProcCom = StrProcCom  + " " + GetISOCurr( Data.KcurrID );
     else
        StrProcCom = StrProcCom  + " " + GetISOCurr( Data.PComDefCurr );
     end;
     StrA = ValueToString( data.a );
  else
     StrProcCom = "0.0000";
           StrA = "0.0000";
  end;
  if( Data.tF1 )/*если фиксированная комиссия ненулевая*/
     StrtF =  StrtdF      + " " + GetISOCurr(Data.FcurrID  );
  else
     StrtF = "0.0000";
  end;

  StrPr = StrPr + " " + GetISOCurr(CalcCurr      );

  if(   ( NOT Data.PComDef ) AND TaxValue )
     StrDescrPr = "Итоговая расчетная сумма с учетом процентной комиссии и налога:";
  elif( ( NOT Data.PComDef ) AND (NOT TaxValue) )
     StrDescrPr = "Итоговая расчетная сумма с учетом процентной комиссии:";
  elif( ( NOT Data.PComDef ) AND TaxValue )
     StrDescrPr = "Итоговая расчетная сумма с учетом налога:";
  else
     StrDescrPr = "Итоговая расчетная сумма:";
  end;


[  Rate     = ############################################
   ConvRate = ###############################################################
  ---------------------------------------------------------------------------
  Комиссия по операции - ##################################:
   tP = ###################### (Начальная сумма расчета % комиссии)
        ##################     (Процент (% комиссия))
  tdP = ###################### (Cумма % комиссии )
        ###################### (Сумма фиксированной комиссии)
  ---------------------------------------------------------------------------
  Налог по операции (в рублях):
        ##################  (Сумма с которой взимается налог)
        ##################  (Процент налога)
  Tax = ##################  (Сумма налога)
  ---------------------------------------------------------------------------
  Начальная расчетная сумма:
  tP  = #
  ---------------------------------------------------------------------------
  #
  ###########################################################################
  ###########################################################################
  ###########################################################################

  ###########################################################################
  ###########################################################################
  ]
  (
     StrRate,
     StrConvRate,
     ComName,
     StrtP,
     StrA,
     StrProcCom,
     StrtF,
     StrTaxBase, Data.SellTax:l, (Double(Data.TaxValue)/100.):10:6:l,
     StrtP2,
     StrDescrPr,
     /*StrPr,*/
     StrDescrFormula,
     StrFormula:w,
     StrDescrTax:w,
     BegStrForComis,
     EndStrForComis:w
  );

  return TRUE; /* успешное завершение */

END;

