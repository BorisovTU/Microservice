/*
$Name:             formBSO.mac
$Module:           RS-Retail
$Description:      Обменный пункт.  Класс для печати справки БСО или ей подобной.
*/

import ExchangeInter, CommonInter, DeskInter, "office.mac", "define.mac", "or_rep_h.mac", rsd;
import "alg_vars.mac";
import rsd;

/* Типы ресурсов */
const TYPERES_CURRENCY    =   1, /* Деньги (из справочника валют)           */
      TYPERES_VALFORM     =   2, /* Ценные бланки                           */
      TYPERES_VALUABLES   =   3, /* Др. ценности (из справочника ценностей) */
      TYPERES_PAYDOC      =   4, /* Платежные документы ОП                  */
      TYPERES_CARD        =  10, /* Пластиковые карточки                    */
      TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ             */

/* C-шные константы */
/*
var   {Name_Bank},
      {Post_Addr};
*/

private var {ReissuingReports};

private var Oper_Brigade = GetOperBrigade();


/*========================================================================
 Класс для печати справки БСО или ей подобной

 Нужно заполнить следующие параметры:
      Rate_Ref        (по умолчанию - вычислит для основного вида курсов)
      Date
      Time
      KindOper
      Rate
      Sname
      Name
      Pname           (по умолчанию - пусто)
      ClientDocType
      ClientDocSeria  (по умолчанию - пусто)
      ClientDocNumber (по умолчанию - пусто)
      ClientDocInfo   (по умолчанию - пусто)
      ClientDocDate   (по умолчанию - пусто)

      IncomeRefVal    (по умолчанию - -1   )
      IncomeTypeValue (по умолчанию - -1   )
      IncomeCodCur    (по умолчанию - -1   )
      IncomeAmount    (по умолчанию - 0    )
      IncomeQuantum   (по умолчанию - 0    )
      OutRefVal       (по умолчанию - -1   )
      OutTypeValue    (по умолчанию - -1   )
      OutCodCur       (по умолчанию - -1   )
      OutAmount       (по умолчанию - 0    )
      OutQuantum      (по умолчанию - 0    )
      ApplicationKind
      ApplicationKey
      OnlyInformation (по умолчанию - false)

      Задается ...RefVal или ...CodCur

 При необходимости можно заполнить атрибут ОТЧЕТ_В_EXCEL.
 Затем вызвать метод Print.
========================================================================*/
CLASS ( CMakeReport ) COrderBSO

  private var reestr = TBFile( "reestr_2.dbt", "W", 1, null, "sbbank.def" );

  var ОТЧЕТ_В_EXCEL = true;

  var
      Rate_Ref        = 0,
      DateRate        = Date( 0, 0, 0 ),
      TimeRate        = Time( 0, 0, 0 ),
      formDate        = Date( ),
      formTime        = Time( ),
      KindOper,
      Rate,
      Sname           = "",
      Name            = "",
      Pname           = "",
      ClientDocType,
      ClientDocSeria  = "",
      ClientDocNumber = "",
      ClientDocInfo   = "",
      ClientDocDate   = "",
      IncomeRefVal    = ANY_CURR,
      IncomeTypeValue = ANY_CURR,
      IncomeCodCur    = ANY_CURR,
      IncomeAmount    = $0.0,
      IncomeQuantum   = 0,
      OutRefVal       = ANY_CURR,
      OutTypeValue    = ANY_CURR,
      OutCodCur       = ANY_CURR,
      OutAmount       = $0.0,
      OutQuantum      = 0,
      CommCodCur      = ANY_CURR,
      CommAmount      = $0.0,
      ApplicationKind,
      ApplicationKey,
      OnlyInsert      = false,
      OnlyInformation = false,
      ClntIdentType   = 0;

  private var
      Branch = NumRealFNcash(),
      BankStandart = GetBankStandart( ),
      Number               = 0,
      IncomeCurrencyISO    = "",
      IncomeCurrencyPDISO  = "",
      NameIncomeCurrency   = "",
      NameIncomeCurrencyPD = "",
      IncomeCurrencyAmount = "",
      IncomePayDocAmount   = "",
      IncomePayDocQuantum  = "",
      IncomePDQuantumName  = "",
      CommissionString     = "",
      OutCurrencyISO       = "",
      OutCurrencyPDISO     = "",
      NameOutCurrency      = "",
      NameOutCurrencyPD    = "",
      OutCurrencyAmount    = "",
      OutPayDocAmount      = "",
      OutPayDocQuantum     = "",
      OutPDQuantumName     = "",
      CommCurrencyAmount   = "",
      CommCurrencyISO      = "",
      SubNameBank          = "",
      SubNumBank           = "",
      SubPostBank          = "",
      SubPostExPunkt       = "",
      SubFIO               = "",
      SubPassport          = "",
      DocAttribute         = "",
      ПовторнаяПечать      = false;
      

  private var
      IncomeCurrencyString = TArray(2),
      IncomePayDocString = TArray(2),
      OutCurrencyString = TArray(2),
      OutPayDocString = TArray(2);


  private MACRO GetRate_Ref()

    var RateChange = TBFile( "ratechng.dbt", "R", 1, null, "exchange.def");
    var Loop       = true;

    RateChange.clear();
    RateChange.rec.Branch    = Office.BranchForRate;
    RateChange.rec.StartDate = {curdate};
    RateChange.rec.StartTime = Time( );

    Loop = RateChange.getGE( );

    if ( Loop )
        Rate_Ref = RateChange.rec.ID;
        DateRate = RateChange.rec.StartDate;
        TimeRate = RateChange.rec.StartTime;
    end;

  END;

  private MACRO GetNumber( )

    var searchOper = {exoper};
    var defID      = 1;
    var cmd;
    var rs;

    Number = 0;

    reestr.KeyNum = 2;
    reestr.clear( );
    reestr.rec.ApplicationKind = ApplicationKind;
    reestr.rec.ApplicationKey  = ApplicationKey;
    if ( reestr.GetEQ( ) )
      if ( {ReissuingReports} )
        ПовторнаяПечать = true;
        Number = reestr.rec.Number;

        if ( reestr.rec.Date != Date( 0, 0, 0 ) )
          formDate = reestr.rec.Date;
        end;

        if ( reestr.rec.SysTime != Time( 0, 0, 0 ) )
          formTime = reestr.rec.SysTime;
        end;
      else
        reestr.Delete( );
      end;
    end;
    reestr.KeyNum = 1;

    if ( ПовторнаяПечать )
      return;
    end;

    if ( OnlyInformation )
      return;
    end;

    cmd = RsdCommand( "select max( reestr_2.t_number ) as r_number "
                      "from dreestr_2_dbt reestr_2 "
                      "where reestr_2.t_branch = :p_branch and "
                            "reestr_2.t_curdate = :p_curdate and "
                            "reestr_2.t_oper = :p_oper and "
                            "reestr_2.t_date = :p_sysdate and "
                            "reestr_2.t_brigade = :p_brigade" );

    rs = RsdRecordset( cmd );

    cmd.addParam( "p_branch", RSDBP_IN, Branch );
    cmd.addParam( "p_curdate", RSDBP_IN, {curdate} );
    cmd.addParam( "p_oper", RSDBP_IN, searchOper );
    cmd.addParam( "p_sysdate", RSDBP_IN, formDate );
    cmd.addParam( "p_brigade", RSDBP_IN, Oper_Brigade );

    cmd.execute;

    if ( rs.moveNext( ) )
      if ( ( ValType( rs.value( "r_number" ) ) == V_INTEGER ) or
           ( ValType( rs.value( "r_number" ) ) == V_DOUBLE  )   )
        Number = Int( rs.value( "r_number" ) ) + 1;
      end;
    end;
    
    if ( Number == 0 )
      Number = 1;
    end;

  END;

  private MACRO InsertReestrRec( )

    var defID = 1;
    var i = 0;
    var cmd;
    var rs;

    while ( i < 10 )
      cmd = RsdCommand( "select nvl( max( t_id ), 0 ) from dreestr_2_dbt where t_branch = ?" );
      cmd.addParam( "p_branch", RSDBP_IN, Branch );

      cmd.execute( );

      rs = RsdRecordset( cmd );

      if ( rs.moveNext( ) )
        defID = rs.value( 0 ) + 1;
      else
        defID = 1;
      end;
      
      reestr.KeyNum = 1;

      reestr.clear( );
      reestr.rec.ID              = defID;
      reestr.rec.Branch          = Branch;
      reestr.rec.CurDate         = {curdate};
      reestr.rec.Date            = formDate;
      reestr.rec.Oper            = {exoper};
      reestr.rec.Rate_Ref        = Rate_Ref;
      reestr.rec.Number          = Number;
      reestr.rec.ApplicationKind = ApplicationKind;
      reestr.rec.ApplicationKey  = ApplicationKey;
      reestr.rec.Code113i        = KindOper;
      reestr.rec.DateRate        = DateRate;
      reestr.rec.TimeRate        = TimeRate;
      reestr.rec.SysTime         = formTime;
      reestr.rec.Brigade         = Oper_Brigade;

      if ( reestr.insert( ) )
        return true;
      end;

      i = i + 1;
    end;

    return false;
  END;

  private MACRO SetNumber( )

    if ( ПовторнаяПечать )
      return;
    end;

    if ( OnlyInformation )
      return;
    end;

    if ( not InsertReestrRec( ) )
      MsgBox( "Ошибка вставки в реестр при печати справки. Перевыпустите справку" );
      Exit( 1 );
    end;

  END;

  private MACRO ЗаполнитьПоля( )

    var RateChange = TBFile( "ratechng.dbt", "R", 0, null, "exchange.def");
    var Str;
    var tmpCodCur;
    var day;

    IncomeCurrencyString[0] = "";
    IncomeCurrencyString[1] = "";
    IncomePayDocString[0] = "";
    IncomePayDocString[1] = "";
    OutCurrencyString[0] = "";
    OutCurrencyString[1] = "";
    OutPayDocString[0] = "";
    OutPayDocString[1] = "";

    if ( Rate_Ref == 0 )
      GetRate_Ref( );
    end;

    DateSplit( DateRate, day );
    if ( ( Rate_Ref != 0 ) and ( day == 0 ) ) /* Из за того, что кто-то в этом макрос определил переменную Date. Не надо было так называть */
      RateChange.clear( );
      RateChange.rec.ID = Rate_Ref;

      if ( RateChange.getEQ( ) )
        DateRate = RateChange.rec.StartDate;
        TimeRate = RateChange.rec.StartTime;
      end;
    end;

    GetNumber( );

    if ( Number == 0 )
      Number = " ";
    end;

    if ( ( IncomeTypeValue == TYPERES_CURRENCY ) and ( IncomeAmount != 0 ) )

      if ( IncomeCodCur != ANY_CURR )
        IncomeCurrencyISO  = GetCurrNumericalCode( IncomeCodCur );
        if ( IncomeCodCur >= 0 )
          NameIncomeCurrency = GetCurrNameCurrency( IncomeCodCur );
        end;
      else
        tmpCodCur = FindCodCurByRefVal( IncomeRefVal );
        IncomeCurrencyISO  = GetCurrNumericalCode( tmpCodCur );
        if ( tmpCodCur >= 0 )
          NameIncomeCurrency = GetCurrNameCurrency( tmpCodCur );
        end;
      end;
      IncomeCurrencyAmount = String( IncomeAmount:12:2 );
      Str = CurToStrAlt( IncomeAmount, NULL, NULL, Int( IncomeCurrencyISO ) );
      IncomeCurrencyString = StrSplit2( Str, 63, 63, 2 );

    elif ( ( IncomeTypeValue == TYPERES_PAYDOC ) and ( ( IncomeAmount != 0 ) or ( IncomeQuantum != 0 ) ) )

      tmpCodCur = FindCodCurByRefVal( IncomeRefVal );
      IncomeCurrencyPDISO    = GetCurrNumericalCode( tmpCodCur );
      if ( tmpCodCur >= 0 )
        NameIncomeCurrencyPD = GetCurrNameCurrency( tmpCodCur );
      end;
      IncomePayDocAmount = String( IncomeAmount:12:2 );
      Str = CurToStrAlt( IncomeAmount, NULL, NULL, Int( IncomeCurrencyPDISO ) );
      IncomePayDocString = StrSplit2( Str, 63, 63, 2 );
      IncomePayDocQuantum  = String( IncomeQuantum );
      IncomePDQuantumName = "Количество чеков =";

    end;

    if ( ( OutTypeValue == TYPERES_CURRENCY ) and ( OutAmount != 0 ) )

      if ( OutCodCur != ANY_CURR )
        OutCurrencyISO     = GetCurrNumericalCode( OutCodCur );
        if ( OutCodCur >= 0 )
          NameOutCurrency = GetCurrNameCurrency( OutCodCur );
        end;
      else
        tmpCodCur = FindCodCurByRefVal( OutRefVal );
        OutCurrencyISO     = GetCurrNumericalCode( tmpCodCur );
        if ( tmpCodCur >= 0 )
          NameOutCurrency = GetCurrNameCurrency( tmpCodCur );
        end;
      end;
      OutCurrencyAmount = String( OutAmount:12:2 );
      Str = CurToStrAlt( OutAmount, NULL, NULL, Int( OutCurrencyISO ) );
      OutCurrencyString = StrSplit2( Str, 63, 63, 2 );

    elif ( ( OutTypeValue == TYPERES_PAYDOC ) and ( ( OutAmount != 0 ) or ( OutQuantum != 0 ) ) )

      tmpCodCur = FindCodCurByRefVal( OutRefVal );
      OutCurrencyPDISO = GetCurrNumericalCode( tmpCodCur );
      if ( tmpCodCur >= 0 )
        NameOutCurrencyPD = GetCurrNameCurrency( tmpCodCur );
      end;
      OutPayDocAmount = String( OutAmount:12:2 );
      Str = CurToStrAlt( OutAmount, NULL, NULL, Int( OutCurrencyPDISO ) );
      OutPayDocString = StrSplit2( Str, 63, 63, 2 );
      OutPayDocQuantum  = String( OutQuantum );
      OutPDQuantumName = "Количество чеков =";

    end;

    if ( ( CommAmount > 0 ) and ( CommCodCur != ANY_CURR ) )
      CommCurrencyISO    = GetCurrNumericalCode( CommCodCur );
      CommCurrencyAmount = CommAmount;
      CommissionString = String( CommAmount:0:2 ) + "  (" + CurToStrAlt( CommAmount, NULL, NULL, int( CommCurrencyISO ) ) + ")";
    end;

    if ( CommissionString == "" )
      CommissionString = " ";
    end;

/*
    if ( IncomeCurrencyISO == "" )
      IncomeCurrencyISO = " ";
    end;

    if ( IncomeCurrencyPDISO == "" )
      IncomeCurrencyPDISO = " ";
    end;

    if ( OutCurrencyISO == "" )
      OutCurrencyISO = " ";
    end;

    if ( OutCurrencyPDISO == "" )
      OutCurrencyPDISO = " ";
    end;
*/
 
    if ( BankStandart == 0 ) /* Сбербанк */
      SubNameBank    = " ";
      SubNumBank     = " ";
      SubPostBank    = " ";
      SubPostExPunkt = " ";
      SubFIO         = " ";
      SubPassport    = " ";
    else
      SubNameBank    = "Наименование уполномоченного банка (филиала уполномоченного банка)";
      SubNumBank     = "Регистрационный номер уполномоченного банка (порядковый номер филиала уполномоченного банка)";
      SubPostBank    = "Почтовый адрес уполномоченного банка (филиала уполномоченного банка)";
      SubPostExPunkt = "Почтовый адрес обменного пункта";
      SubFIO         = "Фамилия, имя, отчество физического лица";
      SubPassport    = "Наименование, серия и номер документа, удостоверяющего личность физического лица";
    end;
     
    if ( ( ClientDocSeria != "" ) or ( ClientDocNumber != "" ) )
      DocAttribute = PaperTypeName( ClientDocType ) + " " + ClientDocSeria + " " + ClientDocNumber;
    else
      DocAttribute = "";
    end;

  END;

  MACRO Print( )
   var ClntIdentMark = "";

    if ( OnlyInsert )
      OnlyInformation = false;

      Number = 0;

      if ( Rate_Ref == 0 )
        GetRate_Ref( );
      end;

      reestr.KeyNum = 2;
      reestr.clear( );
      reestr.rec.ApplicationKind = ApplicationKind;
      reestr.rec.ApplicationKey  = ApplicationKey;
      if ( reestr.GetEQ( ) )
        ПовторнаяПечать = true;
        Number = reestr.rec.Number;

        if ( reestr.rec.Date != Date( 0, 0, 0 ) )
          formDate = reestr.rec.Date;
        end;

        if ( reestr.rec.SysTime != Time( 0, 0, 0 ) )
          formTime = reestr.rec.SysTime;
        end;
      else
        GetNumber( );
      end;
      reestr.KeyNum = 1;
                        
      SetNumber( );
      return;
    end;

    ЗаполнитьПоля( );

    if ( ClntIdentType == 1 )
       ClntIdentMark = "(*)";
    end;

    AutoScan(
    "[                                                                                      \n" +
    "                              ###########################                              \n" +
    "                              ###########################                              \n" +
    "                              ###########################                              \n" +
    "                                                                                       \n" +
    "                                                                                       \n" +
    "#######################################################################################\n" +
    "#                                                                                      \n" +
    "                                                                                       \n" +
    "#######################################################################################\n" +
    "#                                                                                      \n" +
    "                                                                                       \n" +
    "#######################################################################################\n" +
    "#                                                                                      \n" +
    "                                                                                       \n" +
    "#######################################################################################\n" +
    "#                                                                                      \n" +
    "                                                                                       \n" +
    "Номер операции                     #                                                   \n" +
    "                                                                                       \n" +
    "Дата и время совершения операции   ########## ########                                 \n" +
    "                                                                                       \n" +
    "Код вида операции                  ####                                                \n" +
    "                                                                                       \n" +
    "Курс (кросс-курс)                  #                                                   \n" +
    "                                                                                       \n" +
    "#######################################################################################\n" +
    "#                                                                                      \n" +
    "                                                                                       \n" +
    "#######################################################################################\n" +
    "#                                                                                      \n" +
    "                                                                                       \n" +
    "                       #                                                               \n" +
    "                                                                                       \n" +
    "#                                                                                      \n" +
    "               ### ####################################################################\n" +
    "      ############ ####################################################################\n" +
    "#                                                                                      \n" +
    "               ### ####################################################################\n" +
    "      ############ ####################################################################\n" +
    "################## ####################################################################\n" +
    "                                                                                       \n" +
    "                                                                                       \n" +
    "                       #                                                               \n" +
    "#                                                                                      \n" +
    "               ### ####################################################################\n" +
    "      ############ ####################################################################\n" +
    "#                                                                                      \n" +
    "               ### ####################################################################\n" +
    "      ############ ####################################################################\n" +
    "################## ####################################################################\n" +
    "                                                                                       \n" +
    "                                                                                       \n" +
    "                                                                                       \n" +
    "           #####################################  ________________________             \n" +
    "                                                           подпись                     \n" +
    "]()",
    "СПРАВКА"                                                               ,"c:ex_FS(b)",
    "О ПРОВЕДЕННОЙ ОПЕРАЦИИ"                                                ,"c:ex_FS(b)",
    "С НАЛИЧНОЙ ВАЛЮТОЙ И ЧЕКАМИ"                                           ,"c:ex_FS(b)",
    {Name_Bank}                                                             ,"ex_FS(u)",
    SubNameBank                                                             ,"ex_FZ(8)",
    {ExRegNumber}                                                           ,"ex_FS(u)",
    SubNumBank                                                              ,"ex_FZ(8)",
    {Post_Addr}                                                             ,"ex_FS(u)",
    SubPostBank                                                             ,"ex_FZ(8)",
    {ExAddress}                                                             ,"ex_FS(u)",
    SubPostExPunkt                                                          ,"ex_FZ(8)",
    Number                                                                  ,"l",   // Порядковый номер операции
    formDate                                                                ,"l",   // Дата         совершения операции
    String( formTime )                                                      ,"l",   //      и время
    KindOper                                                                ,"l",   // Код вида операции
    Rate                                                                    ,"l",   // Курс операции
    Sname + " " + Name + " " + Pname + " " + ClntIdentMark                  ,"ex_FS(u)",
    SubFIO                                                                  ,"ex_FZ(8)",
    DocAttribute + " " + ClntIdentMark                                      ,"ex_FS(u)",
    SubPassport                                                             ,"ex_FZ(8)",
    "ПРИНЯТО ОТ ФИЗИЧЕСКОГО ЛИЦА:"                                          ,"ex_FS(b)",
    "Наличная валюта"                                                       ,"l",
    IncomeCurrencyISO                                                       ,"l",   // Код валюты
    NameIncomeCurrency                                                      ,"l",   // Название валюты
    IncomeCurrencyAmount                                                    ,"l",
    IncomeCurrencyString[0]                                                 ,"l",   // Сумма прописью
    "Чеки"                                                                  ,"l",
    IncomeCurrencyPDISO                                                     ,"l",   // Код валюты
    NameIncomeCurrencyPD                                                    ,"l",   // Название валюты                                                                            
    IncomePayDocAmount                                                      ,"l",
    IncomePayDocString[0]                                                   ,"l",
    IncomePDQuantumName                                                     ,"l",
    IncomePayDocQuantum                                                     ,"l",   // Количество
    "ВЫДАНО ФИЗИЧЕСКОМУ ЛИЦУ:"                                              ,"ex_FS(b)",
    "Наличная валюта"                                                       ,"l",
    OutCurrencyISO                                                          ,"l",   // Код валюты
    NameOutCurrency                                                         ,"l",   // Сумма
    OutCurrencyAmount                                                       ,"l",
    OutCurrencyString[0]                                                    ,"l",   // Сумма прописью
    "Чеки"                                                                  ,"l",
    OutCurrencyPDISO                                                        ,"l",   // Код валюты
    NameOutCurrencyPD                                                       ,"l",   // Сумма
    OutPayDocAmount                                                         ,"l",
    OutPayDocString[0]                                                      ,"l",
    OutPDQuantumName                                                        ,"l",
    OutPayDocQuantum                                                        ,"l",   // Количество
    {FIO_oper}                                                              ,"ex_FS(u)"
    );

    PrintRep( );
    if( ОТЧЕТ_В_EXCEL )
      PrintWinRep("Закладка1");
      ShowWinRep( );
    end;

    /* тут можно спросить, все ли успешно распечатано */
    SetNumber( );

  END;

  initCMakeReport( );

END;

/*========================================================================
       Пример использования:

MACRO PutF0406007( addr )

  SetBuff( exoperat, addr ); /* буфер записи текущей операции */

  var Отчет = COrderBSO();

  Отчет.Rate_Ref        = exoperat.Rate_Ref;
  Отчет.formDate        = exoperat.Date;
  Отчет.formTime        = exoperat.Time;
  Отчет.KindOper        = string(exoperat.Kind_Oper) + "." + string(exoperat.SubOper);
  Отчет.Rate            = exoperat.Kurs;
  Отчет.Sname           = exoperat.Sname;
  Отчет.Name            = exoperat.Name;
  Отчет.Pname           = exoperat.Pname;
  Отчет.ClientDocType   = exoperat.ClientDocType;
  Отчет.ClientDocSeria  = exoperat.ClientDocSeria;
  Отчет.ClientDocNumber = exoperat.ClientDocNumber;
  Отчет.ClientDocInfo   = exoperat.ClientDocInfo;
  Отчет.ClientDocDate   = "";

  Отчет.IncomeRefVal    = exoperat.IncomeRefVal;
  Отчет.IncomeTypeValue = exoperat.IncomeTypeValue;
  Отчет.IncomeAmount    = exoperat.IncomeAmount;
  Отчет.OutRefVal       = exoperat.OutRefVal;
  Отчет.OutTypeValue    = exoperat.OutTypeValue;
  Отчет.OutAmount       = exoperat.OutAmount;

  Отчет.ApplicationKind = exoperat.ApplicationKind;
  Отчет.ApplicationKey  = exoperat.ApplicationKey;

  SetRecordAddr( paydocop, exoperat, 0, FldOffset( exoperat, "ExtInfo" ), True );
  if( exoperat.IncomeTypeValue == TYPERES_PAYDOC )
    Отчет.IncomeQuantum  = paydocop.Amount;
  elif( exoperat.OutTypeValue == TYPERES_PAYDOC )
    Отчет.OutQuantum  = paydocop.Amount;
  end;

  Отчет.ОТЧЕТ_В_EXCEL = false;
  Отчет.Print();

END;
========================================================================*/
