/*-RSL---------------------------------------- R-Style Software Lab --
               Обменный пункт / Операционная касса

               Печать приходных и расходных ордеров
               Общие функции

               RA 19-12-01 Создан
*/

import ExchangeInter, CommonInter, BankInter, "define.mac";

file   Curr_0  ( "currency.dbt", "sbbank.def"   ) key 0;
file   operset ( "exopset.dbt",  "exchange.def" );


const КодПодсистемыКасОп = 200, /* Значение кода подсистемы "Сервисно-кассовые операций" */
      КодПодсистемыОП    = 250; /* Значение кода подсистемы "Обменный пункт" */
const КОМИССИЯ           = 20;  /* Комиссии контируются как операции специального типа */

/* Получение режима работы "Сбербанк - 0/Комбанк - 1" */
MACRO GetStandart()

   return GetBankStandart( );

END;

/* Получить настройку операции */
MACRO GetOperSet( Branch, Kind_Oper )
  operset.Branch    = Branch;
  operset.Kind_Oper = Kind_Oper;
  return getEQ( operset );
END;


/* Данные для печати произвольного кассового ордера           */
/* Поддерживаются формы СБ и коммерческих банков              */
/* Первоначально было ориентировано на нужды Обменного пункта */
CLASS PrintOrderData

 var
 БП            : integer = 0 , /* номер банковского продукта - AppKind */
 Операция      : integer = 0 , /* номер операции данного банковского продукта */
 Подоперация   : integer = 0 , /* номер подоперации данного банковского продукта */
 Покупка       : bool        , /* покупка / продажа  - для нахождения счетов конировки */
 ТипОборота    : integer = 0 , /* значение из группы CDF_Turn */
 Тип           : string  = "", /* "Приходный", "Расходный", "Мемориальный" */
 NФормы        : string  = "",
 НазвБанка     : string  = "",
 ОКПО          : string  = "",
 NФилиала      : string  = "",
 АдрecФилиала  : string  = "",
 NОрдера       : integer = 0 ,
 Дата          : date    = {curdate},
 Сумма         : money   = 0 ,
 КодВалюты     : integer = ANY_CURR ,
 Приход                  = 0 , /* ─┐                       */
 ВалютаПрихода : string  = "", /*  │ Для ордеров           */
 Расход                  = 0 , /*  │ с приходом и расходом */
 ВалютаРасхода : string  = "", /* ─┘                       */
 СтрСлПроводки : integer = 0 ,
 СтрСлПровДТ   : integer = 0 , /*    Для ордеров           */
 СтрСлПровКТ   : integer = 0 , /*    с приходом и расходом */
 ДебетСчета    : string  = "",
 КредитСчета   : string  = "",
 КассСимв                = 0 ,
 Основание     : string  = "",
 НазвОперации  : string  = "",
 СводныйКоммент: string  = "", /* сводный комментарий для вывода нескольких операций в одном ордере */
 ФИОКассира    : string  = "",
 ФИОКонтролера : string  = "",
 N             : integer = 0 ; /* счетчик для вывода нескольких операций в одном ордере */

END;


MACRO Ф5354Широкая( Data : PrintOrderData )

  var Sum2, CurStr2; /* Для печати одновременно сумм прихода и расхода. */
  var SumStr;
  if  ( Data.NФормы == "53" )
    SumStr = CurToStrAlt( Data.Приход, null, null, int (GetCurrNumericalCode({NationalCur})) );
  elif( Data.NФормы == "54" )
    SumStr = CurToStrAlt( Data.Расход, null, null, int (GetCurrNumericalCode({NationalCur})) );
  end;
  array arSum, arGround;
  strsplit( SumStr        , arSum   , 45-1, 45-1, 2 ); /* !!! В версии 876 strsplit врет на 1 символ */
  strsplit( Data.Основание, arGround, 43-1, 43-1, 3 ); 
  if( arSum(1) == "" ) arSum(1) = "_"; end;

[                                                  ];
[                                         Форма N##]( Data.NФормы       );
[  ##############################################  ]( Data.НазвБанка:w  );
[  Код ОКПО #                                      ]( Data.ОКПО         );
[  Регистрационный номер N #                       ]( Data.NФилиала     );
[  #                                               ]( Data.АдрecФилиала );
[                                                  ];
[                     КАССОВЫЙ ОРДЕР               ];
[                                                 #]( Data.Дата:m:r     );
[                                                  ];
    if( Data.Приход == $0 ) Data.Приход        = ""; end;
    if( Data.Приход == "" ) Data.ВалютаПрихода = ""; end;
    if( Data.Расход == $0 ) Data.Расход        = ""; end;
    if( Data.Расход == "" ) Data.ВалютаРасхода = ""; end;

    if  ( Data.NФормы == "53" )
[      ПРИНЯТЬ наличными    |    ВЫДАТЬ ценности   ];
[                           |                      ];
[               # #         |           # #        ]( Data.Приход:r, Data.ВалютаПрихода, Data.Расход:r, Data.ВалютаРасхода );
    elif( Data.NФормы == "54" )
[      ВЫДАТЬ наличными     |    ПРИНЯТЬ ценности  ];
[                           |                      ];
[               # #         |           # #        ]( Data.Расход:r, Data.ВалютаРасхода, Data.Приход:r, Data.ВалютаПрихода );
    end;
    
[     #____________________________________________]( arSum(0) );
[     #____________________________________________]( arSum(1) );
[                  (сумма прописью)                ];
[                                                  ];
[     | ###########################################]( arGround(0) );
[     | ###########################################]( arGround(1) );
[     | ###########################################]( arGround(2) );
[                                                  ];
[    Контролер______________ Кассир _______________];
[     Деньги                                       ];
[    в суммме руб._________________________________];
[                          (прописью)              ];
[    Получил_______________________________________];
[                                                  ];
[    Дебет счета     N #                           ]( Data.ДебетСчета  );
[    Кредит счета    N #                           ]( Data.КредитСчета );
[    Приход по счету N                             ];
[                                                  ];
[    Гл.(ст.) бухгалтер_______________________     ];
[                                                  ];
[--------------------------------------------------];

END;

MACRO Ф5354Узкая( Data : PrintOrderData )

[ +------------------------------------+];
[ |       ===  #      кассы  ===       |];
[ | Филиал N #                ф.N # -c |];
[ |        Кассовый ордер N #          |];
[ | ---------------------------------- |];
[ |                 #                  |];
[ |                 #                  |];
[ |                 #                  |];
[ | ################################## |];
[ | ---------------------------------- |];
[ |        СОДЕРЖАНИЕ ОПЕРАЦИИ:        |];
[ | ################################## |];
[ | ---------------------------------- |];
[ | #                                  |];
[ |              . . . . . . . . . . . |];
[ | ---------------------------------- |];
[ | Контролер         Кассир           |];
[ | N #               #                |];
[ | Жетон N #                          |];
[ | =========== Для отметок ========== |];
[ |                                    |];
[ | ================================== |];
[ |        ОТМЕТКИ БУХГАЛТЕРИИ:        |];
[ | Дебет счета:                       |];
[ | #                                  |]( Data.ДебетСчета  );
[ | Кредит счета:                      |];
[ | #                                  |]( Data.КредитСчета );
[ |                                    |];
[ | Гл.(ст.) бухгалтер . . . . . . . . |];
[ |       ===  #      кассы  ===       |];
[ +------------------------------------+];

END;

MACRO Ф63Широкая( Data : PrintOrderData )

  var SumStr = CurToStrAlt( Data.Сумма, null, null, int (GetCurrNumericalCode(Data.КодВалюты)) ),
      CurStr = GetISOCurr ( Data.КодВалюты );
  
  array arGround;
  strsplit( Data.Основание, arGround, 42-1, 42-1, 3 ); /* !!! В версии 876 strsplit врет на 1 символ */

[                                                                   Форма N63 ];
[ #                                                                           ]( Data.НазвБанка    );
[ Код ОКПО #                                                                  ]( Data.ОКПО         );
[ Регистрационный номер N #                                                   ]( Data.NФилиала     );
[ #                                                                           ]( Data.АдрecФилиала );
[                                                                             ];
[                                #                                            ]( Data.Дата:m       );
[ На сумму #                                                                  ]( string(Data.Сумма) +" "+ CurStr );
[          #_________________________________________________________________ ]( SumStr            );
[                            (сумма цифрами и прописью)                       ];
[    | ##########################################                             ]( arGround(0)       );
[    | ##########################################                             ]( arGround(1)       );
[    | ##########################################                             ]( arGround(2)       );
[                                                                             ];
[ Управляющий отделением             Гл.бухгалтер                             ];
[ ----------------------------------------------------------------------------];
[                          Отметки бухгалтерии:                               ];
[         Начальная проводка:          |       Ответная проводка:             ];
[ Дебет счета  N #                     |Дебет счета  N                        ]( Data.ДебетСчета   );
[ Кредит счета N #                     |Кредит счета N                        ]( Data.КредитСчета  );
[ Гл.бухгалтер_________________________|"___"__________ ____г. Гл.бухг._______];
[                                                                             ];
[ ----------------------------------------------------------------------------];

END;

MACRO Ф63Узкая( Data : PrintOrderData )

[ +------------------------------------+];
[ |        === Мем. Обороты ===        |];
[ | Филиал N #                ф.N 63-с |];
[ |      Мемориальный ордер N #####    |];
[ | ---------------------------------- |];
[ |                 #                  |];
[ |             НА СУММУ:              |];
[ |                 #                  |];
[ | ################################## |];
[ | ---------------------------------- |];
[ |         СОДЕРЖАНИЕ ОПЕРАЦИИ        |];
[ | ################################## |];
[ | ---------------------------------- |];
[ | Контролер                          |];
[ | N ####                             |];
[ | =========== Для отметок ========== |];
[ |                                    |];
[ | ================================== |];
[ |        ОТМЕТКИ БУХГАЛТЕРИИ:        |];
[ | Дебет счета:                       |];
[ | #                                  |]( Data.ДебетСчета   );
[ | Кредит счета:                      |];
[ | #                                  |]( Data.КредитСчета  );
[ | Гл.(ст.) бухгалтер . . . . . . . . |];
[ |                                    |];
[ |                                    |];
[ | Управляющий отд. . . . . . . . . . |];
[ |        === Мем. Обороты ===        |];
[ +------------------------------------+];

END;

MACRO Ф203( Data : PrintOrderData )

  var SumStr = CurToStrAlt( Data.Сумма, null, null, int (GetCurrNumericalCode(Data.КодВалюты)) );

[ #                                                                ф.№ 203 ]( Data.НазвБанка   );
[ Филиал № #                                                               ]( Data.NФилиала    );
[ --------------------                                                     ];
[ наименование филиала банка                                               ];
[                                                                          ];
[ Мемориальный ордер № #########                                           ]( Data.NОрдера:l:z );
[ #                                                                        ]( Data.Дата:m      );
[                                                                          ];
[ Сумма прописью                                                           ];
[ ######################################################################## ]( /*(*/ SumStr /*+ doGetCashTypeName( doCashVal.Type ) )*/:w );
[ ------------------------------------------------------------------------ ];
[ ИНН                                   | Сумма     | #################### ]( Data.Сумма:f     );
[                                       |           |                      ];
[                                       |-----------|--------------------- ];
[ Плательщик                            | Сч. №     | #################### ]( Data.ДебетСчета  );
[ ##################################### |           |                      ]( Data.ДебетСчета  );
[ --------------------------------------|-----------|                      ];
[                                       | БИК       |                      ];
[                                       |-----------|                      ];
[ Банк плательщика                      | Сч. №     |                      ];
[                                       |           |                      ];
[ --------------------------------------|-----------|--------------------- ];
[                                       | БИК       |                      ];
[                                       |-----------|                      ];
[ Банк получателя                       | Сч. №     |                      ];
[                                       |           |                      ];
[ --------------------------------------|-----------|                      ];
[ ИНН                                   | Сч. №     | #################### ]( Data.КредитСчета );
[                                       |           |                      ];
[ Получатель                            |           |                      ];
[ ##################################### |           |                      ]( Data.КредитСчета );
[                                       |-----------|--------------------- ];
[                                       | Вид оп.   |     | Срок пл.|      ];
[                                       |-----------|-----|---------|----- ];
[                                       | Наз. пл.  |     | Очер.п. |      ];
[                                       |-----------|-----|---------|----- ];
[                                       | Код       |     | Рез.поле|      ];
[ ------------------------------------------------------------------------ ];
[ Назначение платежа (содержание операции)                                 ];
[ ######################################################################## ]( Data.Основание:w );
[ ________________________________________________________________________ ];
[                      Подписи                     Отметки банка           ];
[                                                                          ];
[                      _______________________                             ];
[                                                                          ];
[                      _______________________                             ];
[                                                                          ];
[ -------------------------------------------------------------------------];

END;

/* Приходный - расходный ордер для коммерческих банков */
MACRO ПриходныйРасходныйОрдер( Data : PrintOrderData )

  var ПриходныйРасходный, ВалютныйКассовый;
  var Заголовок1, Заголовок2, Заголовок3;
  var Поле1, СуммаПрописью;
  ARRAY СуммаПроп, Осн, Назн;

  if  ( Data.Тип == "Приходный" )
    ПриходныйРасходный = "ПРИХОДНЫЙ";
    if  ( Data.КодВалюты == {NationalCur} )
      Заголовок3 = "Внес:";
    else
      Заголовок3 = "Принять от:";
    end;
  elif( Data.Тип == "Расходный" )
    ПриходныйРасходный = "РАСХОДНЫЙ";
    Заголовок3 = "Получил:";
  end;

  if  ( Data.КодВалюты == {NationalCur} )
    ВалютныйКассовый = "КАССОВЫЙ";
    Заголовок1 = "    Сумма,    |  Код ";
    Заголовок2 = "     руб.     |назнач";
    Поле1      = Data.КассСимв;
  else
    ВалютныйКассовый = "ВАЛЮТНЫЙ";
    Заголовок1 = "    Сумма     |  Код ";
    Заголовок2 = "   инвалюты   |валюты";
    Поле1      = GetCurrNumericalCode(Data.КодВалюты);
  end;

  СуммаПрописью = CurToStrAlt( Data.Сумма, null, null, int (GetCurrNumericalCode(Data.КодВалюты)) );
  strsplit( Data.Основание   , Осн      , 70, 56, 2 );
  strsplit( Data.НазвОперации, Назн     , 70, 50, 2 );
  strsplit( СуммаПрописью    , СуммаПроп, 70, 54, 2 );
 

[                                                           Форма № ####]( Data.NФормы       );
[ #                                                                     ]( Data.НазвБанка    );
[                                                    +----------+       ];
[                         ######### ######## ОРДЕР № | #        |       ]( ПриходныйРасходный, ВалютныйКассовый, Data.NОрдера:l );
[                           #                        +----------+       ]( Data.Дата:m       );
[+-----------------------+-----------------------+--------------+------+];
[|     Дебет счета №     |    Кредит счета №     |#####################|]( Заголовок1        );
[|                       |                       |#####################|]( Заголовок2        );
[+-----------------------+-----------------------+--------------+------+];
[|#######################|#######################|##############|  ### |]( Data.ДебетСчета, Data.КредитСчета, Data.Сумма, Поле1 );
[+-----------------------+-----------------------+--------------+------+];
[                                                                       ];
[#           #                                                          ]( Заголовок3, "физические лица" );
[                                                                       ];
[На основании: #                                                        ]( Осн(0)            );
[#                                                                      ]( Осн(1)            );
[Назначение платежа: #                                                  ]( Назн(0)           );
[#                                                                      ]( Назн(1)           );
[Сумма прописью: #                                                      ]( СуммаПроп(0)      );
[#                                                                      ]( СуммаПроп(1)      );
[Приложение на ________ листах                                          ];
[                                                                       ];
[Кассир _#________________________________                              ]( Data.ФИОКассира   );
[-----------------------------------------------------------------------];

END;

/* Печать шапки сводного мемориального ордера для коммерческих банков */
MACRO ШапкаСводМемОрдера( Data : PrintOrderData )

  if ( NOT BankStandartExch )
    return;
  end;
[ #                                                                                   ]( Data.НазвБанка    );
[                                 СВОДНЫЙ МЕМОРИАЛЬНЫЙ ОРДЕР                          ];
[                                     за   ########## г.                              ]( Data.Дата:r       );
[ Операция: #                                                                         ]( Data.НазвОперации );
[+-+-----------------------+-----------------------+---+--------------+--------------+];
[|№|         ДЕБЕТ         |         КРЕДИТ        |Код|    Сумма     |    Сумма     |];
[| |         счета         |         счета         |вал|    валюты    |    рублей    |];
[+-+-----------------------+-----------------------+---+--------------+--------------+];

END;

/* Печать строки сводного мемориального ордера для коммерческих банков */
MACRO ПечатьСтрокиСводМемОрдера( Data : PrintOrderData )

  var СуммаВ, СуммаР,
      КодВ = GetCurrNumericalCode( Data.КодВалюты );

  if ( NOT BankStandartExch )
    return;
  end;

  if( Data.КодВалюты == {NationalCur} )
    СуммаВ = "      --      ";
    СуммаР = Data.Сумма;
  else
    СуммаВ = Data.Сумма;
    СуммаР = "      --      ";
  end;

[|#|#######################|#######################|###|##############|##############|]
( Data.N, Data.ДебетСчета  , Data.КредитСчета      ,КодВ, СуммаВ      , СуммаР       );
[+-+-----------------------+-----------------------+---+--------------+--------------+];

END;

/* Печать подвала сводного мемориального ордера для коммерческих банков */
MACRO ПодвалСводМемОрдера( Data : PrintOrderData )

  if ( NOT BankStandartExch )
    return;
  end;
[#####################################################################################]( Data.СводныйКоммент:w );
[                                                                                     ];
[ Исполнитель:                                                      /             /   ];
[                                                                                     ];
[ Контролер:                                                        /             /   ];
[                                                                                     ];
[-------------------------------------------------------------------------------------];

END;

