/****************************************************************************
   RS-Bank        Обменный пункт
   sbr_oth.mac - макрос формирует формы отчетности СБ:
     N53 - Приходные ордера на общие суммы
     N54 - Расходные ордера на общие суммы
     N61 - Ордер на пересылку денег и ценностей
     N63 - Общие суммы безналичных операций
     N78 - Ордер на списание израсходованных ценных бланков
   Copyright 1998 (c) R-Style Software Lab.
   Создание: 20-05-98 by AV
    История:
  AV 4-09-98 полностью переработал формирование ордеров по форме N61
  DZ 4-11-98 переработано формирование ордеров 61,53,63,67
  RA 23-12-98 переработан отчет по утраченным справкам БСО
  RA 10-01-99 реализовал печать ордеров Ф53 и Ф54 в две колонки
  RA  1-03-99 реализовал определение счетов контировки операций
              посредством функции GetBookAccounts()
  RA 23-04-99 добавил в ордера Ф53, Ф54 и Ф61 печать серий и номеров ПД
  VS  9.08.2000 Заменил GetFIOEmployee на GetFullNameEmployee для печати ФИО 
              полностью, добавил в ордере 61 параметры и печать учреждения 
              Отправителя и Получателя.
  VS 24.08.2000 В ордера 61 по ПД добавлена печать номиналов.
****************************************************************************/

Import BankInter, DeprIntr;

RECORD Avance  ( "avance.str",   "exchange.def" );
/* AV 15-09-98  для работы с номиналами */
FILE   Nomtrans( "nomtrans.dbt", "exchange.def" );
RECORD Nom     ( "nom.str",      "exchange.def" );
FILE   sprvtmp ( "sprvtmp.dbt",  "exchange.def" );
FILE   ReportF () txt;
/* RA 26-02-99 справочник валют Retail */
FILE   CurrRetail( "currency.dbt", "sbbank.def" ) key 1; /* ExternalCode */
/**/
var ТипОрдеровФ61 = "Все";
/* название текстового файла-отчета для ордеров N61 */
var ФайлОтчетФ61  = "";

const ШиринаКолонки = 56;/* Ширина одной колонки для вывода */
/* приходных и расходных ордеров в две колонки ( Ф53, Ф54 ) */

/* Номера видов кассовых ценностей */
const TypeValueBSO  = 2,
      TypeValueCurr = 1,
      TypeValueRub  = 1,
      TypeValuePD   = 4;

/* Код кассовой ценности внутри вида */
const CodIntValueBSO  = 6;

/* Номера строк сложной проводки */
const СтрПровОВП           = 1, /* проводка с участием счета ОВП */
      СтрПровРубПокр       = 2, /* проводка с участием счета рублевого покрытия */
      СтрПровПолРазнКурсов = 3, /* проводка при курсе операции > курса ЦБ (разница курсов) */
      СтрПровОтрРазнКурсов = 4, /* проводка при курсе операции < курса ЦБ (разница курсов) */
      СтрПровНалог         = 5; /* проводка налога  !!! (и ПОДОХ. налога) !!! */

/* !!! Подоходный налог проводится так же, как и налог на продажу */

/* Номера внебалансовх операций */
const Аванс           = 1,
      Инкассация      = 2,
      Передача        = 7;

/***************************************************************************/
/***************************************************************************/

/***** ОРДЕРА НА СПИСАНИЕ *****/

MACRO ШапкаОрдераНаСписание( Вид )
 var Заголовок, Подразделение;
 if( Вид == "утраченных" )
   Заголовок = "ПЕРЕЧЕНЬ НОМЕРОВ УТРАЧЕННЫХ ЦЕННЫХ БЛАНКОВ";
 else
   Заголовок = "ОРДЕР НА СПИСАНИЕ " + Вид + " ЦЕННЫХ БЛАНКОВ";
 end;

  Подразделение = trim(GetIniString( "PODRAZD", "exchange.ini",FALSE));
  Подразделение = substr(Подразделение,1,StrLen(Подразделение)-1);
[                                                              Форма N78
 -----------------------+-----------------------------------------------
   СБЕРЕГАТЕЛЬНАЯ КАССА |      ОТМЕТКИ БУХГАЛТЕРИИ
   N_____#______________+-----------------------------------------------
   #                    |
 -----------------------+-----------------------------------------------
                                  #
 ------------------------------+-------+-------------------+------------
                               |       |     NN Бланков    |
  Наименование ############### | Серия +---------+---------+   Сумма
           бланков             |       |   с N   |  по N   |(количество)
]( Подразделение,
   StrAddZeroDate( Session.StartDate ),
   StrUpr( Заголовок ):c,
   Вид:c
);
END;

MACRO СтрокаОрдераНаСписание( НазваниеБланков, Серия, НачНомер, КонНомер, Справок )
[     #                        |  ##   | ####### | ####### | ##########
]( НазваниеБланков,
   Серия,
   НачНомер,
   КонНомер,
   Справок:c
);
END;

MACRO ПустаяСтрокаОрдераНаСписание( НазваниеБланков )
[     #                        |       |         |         |
]( НазваниеБланков );
END;

MACRO РазделительСтрокОрдераНаСписание()
[------------------------------+-------+---------+---------+------------
];
END;

MACRO ИтогоОрдераНаСписание( ВсегоСправок )
[------------------------------+-------+---------+---------+------------
                                              ИТОГО .....  |###########
 ----------------------------------------------------------+------------
______________________________________________________________________________

]
( ВсегоСправок:c
);
END;

/*************************************************************************/
/*  Процедура ищет серии справок ЗА СМЕНУ с определенным Статусом среди  */
/*  израсходованных и выводит их по пачкам в ведомость                   */
/*************************************************************************/
var FirstSprvID,   /* ID операций для первой и последней справок смены */
    LastSprvID;

/* Получить ID операций для первой и последней, если она есть, справок смены. */
macro GetFirstLastSpravOnSession()
  file oper (operat, "exchange.def") key 1;
  var retval = False; /* Нет ни одной операции со справками! */

  macro FilterOnSessionOperations()
    return (oper.Emp_Ref  == Session.Emp_Ref) and
           (oper.Sess_Ref == Session.Number);
  end;

  ClearRecord( oper );
  oper.Emp_Ref       = Session.Emp_Ref;
  oper.Sess_Ref      = Session.Number;
  oper.Oper_Ref      =                
  oper.State_Ref     =
  oper.Rate_Ref      =
  oper.OutCurr_Ref   =
  oper.IncomeCurr_Ref= 0;

  if( getGE( oper ) and FilterOnSessionOperations() )
    FirstSprvID = LastSprvID = oper.ID;
    while( next( oper ) and FilterOnSessionOperations() )
      if  ( oper.ID > LastSprvID  ) LastSprvID  = oper.ID;
      elif( oper.ID < FirstSprvID ) FirstSprvID = oper.ID;
      end;
    end;
    retval = True;
  end;

  return retval;
end;

MACRO СодержимоеОрдераНаСписание( Статус )
  file oper (operat, "exchange.def") key 0;

  var Loop      = True;
  var LoopWhile = True;
  var Следующая = True;
  var Индекс       = 0; /* текущая позиция в массиве */
  var Справок      = 0;
  var ВсегоПачек   = 0;
  var ВсегоСправок = 0;
  var Серия, Номер, НачНомер, КонНомер, НазваниеБланков;

  /* Фильтр по справкам одной смены */
  macro FilterOnSessionSpravs()
    return (SprvSort.Oper_Ref >= FirstSprvID) and
           (SprvSort.Oper_Ref <= LastSprvID);
  end;
  /* 12.09.2000 VS Добавлен Фильтр по справкам одного сотрудника */
  macro FilterOnEmployee( Emp_Ref )
    oper.ID = SprvSort.Oper_Ref;
    if( getEQ( oper ) )
      return (oper.Emp_Ref == Emp_Ref);
    end;
    return False;
  end;

   keynum( SprvSort, 1 );
   SprvSort.Oper_Ref  = FirstSprvID;
   SprvSort.State_Ref = 0;
   getGE( SprvSort );

   While( LoopWhile )
      /* проверка статуса справки */
      if( (Loop == True)                 and
          (SprvSort.State_Ref == Статус) and
          /*ПроверкаОперацииПоСсылке( SprvSort.Oper_Ref, Session.Emp_Ref, Session.Number )*/
          FilterOnSessionSpravs()        and
          FilterOnEmployee( {exoper} ) 
        )
         /* в первый вход эта проверка не выполняется */
         if( Справок >= 1 )
            if( ПроверкаСправки( Серия, Номер, SprvSort.Seria, SprvSort.Numb ) )
               /*Следующая == True;*/
               Серия   = SprvSort.Seria;
               Номер   = SprvSort.Numb;
               Справок = Справок + 1;
            else
               Следующая = False;/* серия прервалась */
            end;
         end;

         /* первый вход */
         if( NOT Справок )
            Справок = 1;
            Серия = SprvSort.Seria;
            Номер = НачНомер = SprvSort.Numb;
         end;

         /* распечатать прерванную серию */
         if( (Справок > 0) AND (Следующая == False) )
            КонНомер = Номер;
            РазделительСтрокОрдераНаСписание();
            if( ВсегоПачек == 0 )
               НазваниеБланков = "Справки ф.0406007";
            else
               НазваниеБланков = "";
            end;
            СтрокаОрдераНаСписание( НазваниеБланков, Серия, НачНомер, КонНомер, Справок );

            ВсегоПачек   = ВсегоПачек + 1;
            Серия        = SprvSort.Seria;
            Номер        = НачНомер = SprvSort.Numb;
            КонНомер     = "";
            ВсегоСправок = ВсегоСправок + Справок;
            Справок      = 1;

            Следующая = True;
         end;
      else /* распечатать серию в случае, если вклинилась справка другой смены */
           /* или достигнут конец файла справок                                */
         if( Справок > 0 )
            КонНомер = Номер;
            РазделительСтрокОрдераНаСписание();
            if( ВсегоПачек == 0 )
               НазваниеБланков = "Справки ф.0406007";
            else
               НазваниеБланков = "";
            end;
            СтрокаОрдераНаСписание( НазваниеБланков, Серия, НачНомер, КонНомер, Справок );
            ВсегоПачек    = ВсегоПачек + 1;
            ВсегоСправок  = ВсегоСправок + Справок;
            Номер = Серия = НачНомер = КонНомер = "";
            Справок       = 0;
            Следующая     = True;
         end;
         if( Loop == False )/* конец файла, выходим из цикла */
            LoopWhile = False;
         end;
      end;
      if( Loop == True )
         Loop = Next( SprvSort );
      end;
   end;

   ИтогоОрдераНаСписание( ВсегоСправок );

 return ВсегоПачек;
END;


MACRO УчетЦенныхБланков()
   var StrNameFile, ВсегоСправок, Loop, i, НазваниеБланков, Серия;
   var НачНомер, КонНомер, Справок;

   MakeReportFile( ReportFile, "sbr_f78" );
   PrintDate_n_Time();
   if( not GetFirstLastSpravOnSession() )
      msgbox( "В этой смене не было ни одной|операции со справками." );
      return;
   end;

   /* выданные по операциям справки */
   ШапкаОрдераНаСписание( "израсходованных" );
   СодержимоеОрдераНаСписание( CDF_SprvGetOut );

   /* испорченные по операциям справки */
   ШапкаОрдераНаСписание( "испорченных" );
   СодержимоеОрдераНаСписание( CDF_SprvKill );

   /* информация об утерянных справках RA 23-12-98 */
   if( GetTrue( True,"Есть утраченные справки БСО ?" ) AND
       ( ВводУтраченныхБСО() == 0 ) )

      /* получение уникального имени динамического файла разбиения на номиналы */
      fnmerge( StrNameFile,                       /* полное название файла */
               GetIniString( "WORKDIR", "exchange.ini", TRUE ),/* рабочая директория */
               "sprvtmp",                         /* имя файла без расширения */
               Trim( AddZero( UserNumber, 3 ) )   /* расширение - уникальный номер пользователя в сети */
               );

      if( Open( sprvtmp, StrNameFile ) )
         ВсегоСправок = 0;
         sprvtmp.ID = 1;
         if( getGE( sprvtmp ) )
           Loop = True;
         end;
         ШапкаОрдераНаСписание( "утраченных" );
         i = True;
         while( Loop )
            РазделительСтрокОрдераНаСписание();
            if( i )
               НазваниеБланков = "Справки ф.0406007";
               i = False;
            else
               НазваниеБланков = "";
            end;
            /*ПустаяСтрокаОрдераНаСписание( НазваниеБланков );*/
            Серия    = sprvtmp.Seria;
            НачНомер = sprvtmp.Numb;
            Справок  = sprvtmp.Quantum;
            КонНомер = AddZero( INT( НачНомер ) + Справок - 1, 7 );
            СтрокаОрдераНаСписание( НазваниеБланков, Серия, НачНомер, КонНомер, Справок );
            ВсегоСправок = ВсегоСправок + Справок;
            Loop = next( sprvtmp );
         end;
         ИтогоОрдераНаСписание( ВсегоСправок );

         if( Close( sprvtmp ) )
            if( DelFile( StrNameFile ) == False )
               MsgBox( "Ошибка при удалении временного файла!" );
            end;
         else
            MsgBox( "Ошибка при закрытии временного файла!" );
         end;
      end;
   end;

   /* DZ 05-11-98 */
   [ Ст.Контролер _______________________     Кассир _____________________
       Контролер                                                           ];

   /*смотрим файл отчета*/
   SetOutPut( 0 );
   ViewFile( ReportFile );
   Close( ReportFile );
END;


/****************************************************************************/
/****************************************************************************/

/***** ПРИХОДНЫЕ И РАСХОДНЫЕ ОРДЕРА *****/
/***** ОРДЕРА НА ОБЩИЕ СУММЫ БЕЗНАЛИЧНЫХ ОПЕРАЦИЙ *****/

/*RA 5-08-99*****************************************************************/
/* Процедура заполняет массивы с разными номерами смен курсов и валют       */
/*        Аналогична функции ЗаполнитьМассивВалют()                         */
/*        Дополнительно заносит валюты, по которым                          */
/*        есть операции проверки на подлинность                             */
/* при возможности унифицировать с ЗаполнитьМассивы_НОД()                   */
/****************************************************************************/
MACRO ЗаполнитьМассивы_ПрПодл()
  asize(DiffCurr,   0);
  asize(ConvIncSum, 0);/* массив для полученных сумм по конверсии валют */
  asize(ConvOutSum, 0);/* массив для выданных сумм по конверсии валют   */
  var j = 0;
  /* по реестрам о/п */
  ClearRecord( Reestr );
  Reestr.ExchangeNumber = office.ID;
  Reestr.Emp_Ref        = Session.Emp_Ref;
  Reestr.Sess_Ref       = Session.Number;
  var BreakLoop = GetGE( Reestr );
  /* цикл по реестрам */
  While( BreakLoop AND (Reestr.Emp_Ref == Session.Emp_Ref ) AND (Reestr.Sess_Ref== Session.Number) )
     PutNumberInArray(    DiffCurr,    Reestr.OutCurr_Ref, j );
     PutNumberInArray(    DiffCurr, Reestr.IncomeCurr_Ref, j );
     BreakLoop = Next( Reestr );
  end;
  /* по операциям проверки на подлинность */
  Operat.Emp_Ref        = Session.Emp_Ref;
  Operat.Sess_Ref       = Session.Number;
  Operat.Oper_Ref       = CDF_OperCheck;    /* операция проверки на подлинность */
  Operat.State_Ref      = CDF_OperCompleted;/* завершена     */
  Operat.Rate_Ref       = 0;
  Operat.OutCurr_Ref    = ANY_CURR;
  Operat.IncomeCurr_Ref = ANY_CURR;
 
  BreakLoop = GetGE( Operat );
  While( BreakLoop == True AND
         (Operat.Emp_Ref        == Session.Emp_Ref)         AND
         (Operat.Sess_Ref       == Session.Number)          AND
         (Operat.Oper_Ref       == CDF_OperCheck)           AND
         ( (Operat.State_Ref    == CDF_OperCompleted) OR
           (Operat.State_Ref    == CDF_OperInReestr)   )
       )
     if( Operat.IncomeCurr_Ref != ANY_CURR )
        PutNumberInArray( DiffCurr,   Operat.IncomeCurr_Ref, j );
     end;
     BreakLoop = Next( Operat );
  end;
  TotalNumCurr = j;
END;


/* Процедура получает строку номиналов переданной суммы                     */
/* ( для операций передачи )                                                */
MACRO ПолучитьСтрокуНоминалов( Operat )
   var Строка = " ", i, Loop;
   Nomtrans.Oper_Ref = Operat.ID;/* инициализируем ключ */
   if( GetEQ( Nomtrans ) )
      Loop = True;
      i = 0;
      While( i < 20 )/* больше 20 номиналов не может быть */
         /* 89 - смещение от начала записи до массива номиналов*/
         SetRecordAddr( Nom, Nomtrans, i, 89, True );
         if( (Nom.Nom != 0.) AND (Nom.Numb != 0) )
            Строка = Строка + string( Nom.Nom ) + " X " + string( Nom.Numb ) + "  ";
         end;
         i = i + 1;
      end;
   end;
   return Строка;
END;

/****************************************************************************/

MACRO СтрокаСумм( Сумма, КурсЦБ, СуммаВрублях )
  var str = "Сумма:" + Сумма;
  if( КурсЦБ != "" ) 
    str = str + "  по курсу ЦБ (" + КурсЦБ + ")  ";
  end;
  if( СуммаВрублях != "" )
    str = str + СуммаВрублях + " руб.";
  end;
  return str;
END;

/****************************************************************************/

MACRO РеквизитыПД( Reestr_ID )
  FILE Operat ("operat.dbt", "exchange.def") key 3;  /* Reestr */
  var Loop, Реквизиты = " ";
  var PDName, СтрНоминалов, СтрокаСерийНомеров;
  Operat.Reestr = Reestr_ID;

  Loop = GetEQ( Operat );
  While( Loop )
     /* наложение структуры avanceop на поле Operat.ExtInfo */
     SetRecordAddr( paydocop, Operat, 0, FldOffset( Operat,"ExtInfo"),True);
     PDName = Trim( GetNamePayDoc( paydocop.PayDoc_Ref ) );
     СтрНоминалов = СтрокаНоминалов( Operat );
     СтрокаСерийНомеров = ПолучитьСтрокуСерийНомеровПД( Operat.ID );

     Реквизиты = Реквизиты + PDName + СтрНоминалов + СтрокаСерийНомеров + "; ";
     Loop = ( Next( Operat ) AND ( Operat.Reestr == Reestr_ID ) );
  end;

  return Реквизиты;
END;

/****************************************************************************/

MACRO РеквизитыПД_Недостача( Operat_ID )
  FILE Operat ("operat.dbt", "exchange.def") key 0;  /* ID */
  var PDName = " ";
  Operat.ID = Operat_ID;

  if( GetEQ( Operat ) )
     /* наложение структуры deficitop на поле Operat.ExtInfo */
     SetRecordAddr( deficitop, Operat, 0, FldOffset( Operat,"ExtInfo"),True);
     if( deficitop.PD_Ref != 0 )
       PDName = Trim( GetNamePayDoc( deficitop.PD_Ref ) );
     end;
  end;

  return PDName;
END;

/****************************************************************************/
/****************************************************************************/

macro ПриходныйОрдер( ПолученнаяСумма,ПолученнаяВалюта, СуммаВалюты, КодВалюты, Содержание1, Содержание2, Содержание3, RDate, ДебетСчет1, КредитСчет1 )

  /*var ДебетСчет1  = GetIniString( "ORDERDEBET",  "exchange.ini");*/
  /*var КредитСчет1 = GetIniString( "ORDERKREDIT", "exchange.ini");*/
  var ПриходСчет = "";
  /* DZ 05-11-98 */
  var СуммаПрописью1 = "", СтрСуммаВалюты, СтрКодВалюты, СтрПолученнаяВалюта;
  array СуммаПрописьюРазбитая;
  var Коп = "";
  RubToStrAlt( ПолученнаяСумма, СуммаПрописью1, Коп );
  СуммаПрописью1 = СуммаПрописью1 + " руб. " + Коп + " коп.";
  StrSplit( СуммаПрописью1, СуммаПрописьюРазбитая, 45, 45, 2 );
  
  if( СуммаВалюты ) СтрСуммаВалюты = Trim(String(СуммаВалюты) );
  else СтрСуммаВалюты = "";
  end;

  if( (КодВалюты != ANY_CURR) AND (КодВалюты  != {NationalCur}) )
     СтрКодВалюты = GetISOCurr(КодВалюты);
  elif( КодВалюты  == {NationalCur} )
     СтрКодВалюты = "руб.";
  else
     СтрКодВалюты = "";
  end;

  if( (ПолученнаяВалюта != ANY_CURR) AND (ПолученнаяВалюта  != {NationalCur}) )
     СтрПолученнаяВалюта = GetISOCurr(ПолученнаяВалюта);
  elif( ПолученнаяВалюта == {NationalCur} )
     СтрПолученнаяВалюта = "руб.";
  else
     СтрПолученнаяВалюта = "";
  end;

/* При изменении формы обратите внимание на то, что строка из 40 и более */
/* символов "-" без других символов и ведущих пробелов используется как  */
/* признак конца кассового ордера в макросе РазбиениеВДвеКолонки()       */
[                                       Форма N53];
 MakeStrStamp();/*печатаем штамп*/
[                    КАССОВЫЙ ОРДЕР
                                 #

     ПРИНЯТЬ наличными    |    ВЫДАТЬ ценности
                          |
   ################## ### |  ################# ###
   #______________________________________________
   #______________________________________________
                 (сумма прописью)

    |#
    |#
    |#############################################

   Контролер______________ Кассир _______________
    Деньги
   в суммме руб.__________________________________
                         (прописью)
   Получил________________________________________

   Дебет счета     N##############################
   Кредит счета    N##############################
   Приход по счету N##############################

   Гл.(ст.) бухгалтер_______________________

 ------------------------------------------------------
]
(
 StrAddZeroDate( RDate ),
 ПолученнаяСумма,
 СтрПолученнаяВалюта,
 СтрСуммаВалюты:r,
 СтрКодВалюты,
 СуммаПрописьюРазбитая(0),
 СуммаПрописьюРазбитая(1),
 Содержание1,
 Содержание2,
 Содержание3:w,
 ДебетСчет1,
 КредитСчет1,
 ПриходСчет
);
end;

/****************************************************************************/

macro РасходныйОрдер( СуммаВалюты, КодВалюты, ВыданнаяСумма,ВыданнаяВалюта, Содержание1, Содержание2, Содержание3, RDate, ДебетСчет, КредитСчет )
  
  /*var ДебетСчет  = GetIniString( "ORDERDEBET",  "exchange.ini");*/
  /*var КредитСчет = GetIniString( "ORDERKREDIT", "exchange.ini");*/
  var ПриходСчет = "";
  
  /* DZ 05-11-98 */
  var СуммаПрописью1 = "", СтрСуммаВалюты, СтрКодВалюты, СтрПолученнаяВалюта, СтрВыданнаяВалюта;
  array СуммаПрописьюРазбитая;
  var Коп = "";
  RubToStrAlt( ВыданнаяСумма, СуммаПрописью1, Коп );
  СуммаПрописью1 = СуммаПрописью1 + " руб. " + Коп + " коп.";
  StrSplit( СуммаПрописью1, СуммаПрописьюРазбитая, 45, 45, 2 );
  
  if( СуммаВалюты ) СтрСуммаВалюты = Trim(String(СуммаВалюты));
  else СтрСуммаВалюты = "";
  end;
  
  if( (КодВалюты != ANY_CURR) AND (КодВалюты  != {NationalCur}) )
     СтрКодВалюты = GetISOCurr(КодВалюты);
  elif( КодВалюты  == {NationalCur} )
     СтрКодВалюты = "руб.";
  else
     СтрКодВалюты = "";
  end;

  if( (ВыданнаяВалюта != ANY_CURR) AND (ВыданнаяВалюта  != {NationalCur}) )
     СтрВыданнаяВалюта = GetISOCurr(ВыданнаяВалюта);
  elif( ВыданнаяВалюта == {NationalCur} )
     СтрВыданнаяВалюта = "руб.";
  else
     СтрВыданнаяВалюта = "";
  end;
  
/* При изменении формы обратите внимание на то, что строка из 40 и более */
/* символов "-" без других символов и ведущих пробелов используется как  */
/* признак конца кассового ордера в макросе РазбиениеВДвеКолонки()       */
[                                        Форма N54 ];
 MakeStrStamp();/*печатаем штамп*/
[
                    КАССОВЫЙ ОРДЕР
                                  #

     ВЫДАТЬ наличными     |    ПРИНЯТЬ ценности
                          |
   ################## ### |  ################# ###
   #______________________________________________
   #______________________________________________
                 (сумма прописью)

    |#
    |#
    |#############################################

   Контролер______________ Кассир _______________
    Деньги
   в суммме руб.__________________________________
                         (прописью)
   Получил________________________________________

   Дебет счета     N##############################
   Кредит счета    N##############################
   Приход по счету N##############################

         Гл.(ст.) бухгалтер_______________________


 ------------------------------------------------------
]
(
 StrAddZeroDate( RDate ),
 ВыданнаяСумма,
 СтрВыданнаяВалюта,
 СтрСуммаВалюты:r,
 СтрКодВалюты,
 СуммаПрописьюРазбитая(0),
 СуммаПрописьюРазбитая(1),
 Содержание1,
 Содержание2,
 Содержание3:w,
 ДебетСчет,
 КредитСчет,
 ПриходСчет
);
end;

macro ОрдерБезналичный(Сумма,Валюта,Содержание1,Содержание2,Содержание3,RDate,ДебетСчет,КредитСчет)

  /*var ДебетСчет  = GetIniString( "ORDERDEBET",  "exchange.ini");*/
  /*var КредитСчет = GetIniString( "ORDERKREDIT", "exchange.ini");*/
  var ISOCurr;
  if( Валюта != {NationalCur} )
    ISOCurr = GetISOCurr( Валюта );
  else
    ISOCurr = "руб.";
  end;

  [                                                                Форма N63 ];

  MakeStrStamp();/* печатаем штамп */

[
                                   ОРДЕР  #
  На сумму #
           ##################################################################
                             (сумма цифрами и прописью)
     |#
     |#
     |#

  Управляющий отделением             Гл.бухгалтер
  ---------------------------------------------------------------------------
                           Отметки бухгалтерии:
          Начальная проводка:          |       Ответная проводка:
  Дебет счета  N####################   |Дебет счета  N####################
  Кредит счета N####################   |Кредит счета N####################
  Гл.бухгалтер_________________________|"___"__________ ____г. Гл.бухг._______


-------------------------------------------------------------------------------

]
(  StrAddZeroDate( RDate ):c,
   Trim(string( Сумма )) + " " + ISOCurr,
   CurToStrAlt( Сумма, null, null, Валюта ):w,
   Содержание1,
   Содержание2,
   Содержание3,
   ДебетСчет,
   ДебетСчет,
   КредитСчет,
   КредитСчет
);
end;


/****************************************************************************/
/* Функция получает внутренний код валюты Retail по внешнему коду RA 25-02-99*/
/****************************************************************************/
MACRO GetCurrRetail( ExternalCode )
   if( not ExInRetail ) 
      return ANY_CURR;
   end;
   ClearRecord( CurrRetail );
   CurrRetail.ExternalCode = ExternalCode;
   if( GetEQ(CurrRetail) )
      return CurrRetail.Code_Currency;
   else
      return ANY_CURR;
   end;
END;

/****************************************************************************/
/* Функция получает счета контировки для балансовых операций RA 25-02-99    */
/****************************************************************************/
MACRO ПолучитьСчетаКонтировкиБалансовыхОпераций
  ( IncomeCurr_Ref, OutCurr_Ref, Операция, ДебетСчет, КредитСчет, СтрокаПроводки )

    var Валютность, ВходнойКодВалюты;
    /*
    if( OutCurr_Ref == {NationalCur} )
      Валютность = 0;
    else
      Валютность = 1;
    end;
    */
    Валютность = 1;
    ВходнойКодВалюты = GetCurrRetail( IncomeCurr_Ref );
    if( GetBookAccounts( КодПодсистемыОП,
                         OutCurr_Ref,
                         string(Операция),
                         Office.ID,
                         Валютность,
                         ВходнойКодВалюты,
                         ДебетСчет,
                         КредитСчет,
                         СтрокаПроводки
                       ) == True )
       setparm( 3, ДебетСчет );
       setparm( 4, КредитСчет );
    else
       setparm( 3, "???" );
       setparm( 4, "???" );
    end;
END;

/****************************************************************************/
/* Функция получает счета контировки для внебалансовых операций RA 25-02-99 */
/****************************************************************************/
MACRO ПолучитьСчетаКонтировкиВнебалансовыхОпераций
  ( TypeOper, ВидЦенности, КодЦенностиВнутриВида, ДебетСчет, КредитСчет )

    var Валютность = 1;
    if( GetBookAccounts( SB_NOTBALANCE,
                         TypeOper,
                         ВидЦенности,
                         Office.ID,
                         Валютность,
                         КодЦенностиВнутриВида,
                         ДебетСчет,
                         КредитСчет
                       ) == True )
       setparm( 3, ДебетСчет );
       setparm( 4, КредитСчет );
    else
       setparm( 3, "???" );
       setparm( 4, "???" );
    end;
END;


macro ОрдерПроданоВалюты( КурсОперации, КурсЦБ, IncomeSum, OutSum, OutCurr_Ref, RDate )
    var Содержание1 = " Продано " + GetISOCurr(OutCurr_Ref);
    var Содержание2 = " Кпрод = " + Trim(String(КурсОперации)) + "  ОК = " + Trim(String(КурсЦБ));
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperCurrSale;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( {NationalCur}, OutCurr_Ref, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ПриходныйОрдер(IncomeSum,{NationalCur},OutSum,OutCurr_Ref,Содержание1,Содержание2,Содержание3,RDate,ДебетСчет,КредитСчет);
end;
macro ОрдерПроданоПД( КурсОперации, КурсЦБ, IncomeSum, OutSum, OutCurr_Ref, RDate )
    var Содержание1 = " Продано ПД в " + GetISOCurr(OutCurr_Ref);
    var Содержание2 = " Кпрод = " + Trim(String(КурсОперации)) + "  ОК = " + Trim(String(КурсЦБ));
    var Содержание3 = РеквизитыПД( Reestr.AutoKey );
    var ДебетСчет, КредитСчет, Операция = CDF_OperPayDocSale;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( {NationalCur}, OutCurr_Ref, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ПриходныйОрдер(IncomeSum,{NationalCur},OutSum,OutCurr_Ref,Содержание1,Содержание2,Содержание3,RDate,ДебетСчет,КредитСчет);
end;
macro ОрдерКурсПродажи_ОК( КурсОперации, КурсЦБ, РазницаПрод, СуммаПродажи, КодВалюты, RDate, Операция )
    var Содержание1 = " Разница между ";
    var Содержание2 = " Кпрод = " + Trim(String(КурсОперации)) + "  ОК = " + Trim(String(КурсЦБ));
    var Содержание3 = " от продажи ";
    var ДебетСчет, КредитСчет;

    if  ( Операция == CDF_OperCurrSale )
      Содержание3 = Содержание3 + "валюты ";
    elif( Операция == CDF_OperPayDocSale )
      Содержание3 = Содержание3 + "ПД ";
    end;
    Содержание3 = Содержание3 + "на сумму "+ string( СуммаПродажи ) + " " + GetISOCurr(КодВалюты);

    if( КурсОперации > КурсЦБ )
       ПолучитьСчетаКонтировкиБалансовыхОпераций( {NationalCur}, КодВалюты, Операция, ДебетСчет, КредитСчет, СтрПровПолРазнКурсов );
    else
       ПолучитьСчетаКонтировкиБалансовыхОпераций( {NationalCur}, КодВалюты, Операция, ДебетСчет, КредитСчет, СтрПровОтрРазнКурсов );
    end;
    ОрдерБезналичный( РазницаПрод,{NationalCur},Содержание1,Содержание2, Содержание3, RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерНалогНаПродажуВал( TaxSum, СуммаПродажи, OutCurr_Ref, RDate, Операция )
    var Содержание1 = " Налог на продажу  " + GetISOCurr(OutCurr_Ref);
    var Содержание2 = " от продажи " + string( СуммаПродажи ) + " " + GetISOCurr(OutCurr_Ref);
    var Содержание3 = "";
    var ДебетСчет, КредитСчет;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( {NationalCur}, OutCurr_Ref, Операция, ДебетСчет, КредитСчет, СтрПровНалог );
    ПриходныйОрдер( TaxSum,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет );
end;
macro ОрдерНалогНаПродажуПД( TaxSum, СуммаПродажи, OutCurr_Ref, RDate, Операция )
    var Содержание1 = " Налог на продажу ПД в " + GetISOCurr(OutCurr_Ref);
    var Содержание2 = " от продажи " + string( СуммаПродажи ) + " " + GetISOCurr(OutCurr_Ref);
    var Содержание3 = РеквизитыПД( Reestr.AutoKey );
    var ДебетСчет, КредитСчет;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( {NationalCur}, OutCurr_Ref, Операция, ДебетСчет, КредитСчет, СтрПровНалог );
    ПриходныйОрдер( TaxSum,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет );
end;
macro ОрдерПодохНалогНаПродажуВал( TaxSum, СуммаПродажи, OutCurr_Ref, RDate, Операция )
    var Содержание1 = " Подох. налог на продажу  " + GetISOCurr(OutCurr_Ref);
    var Содержание2 = " от продажи " + string( СуммаПродажи ) + " " + GetISOCurr(OutCurr_Ref);
    var Содержание3 = "";
    var ДебетСчет, КредитСчет;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( {NationalCur}, OutCurr_Ref, Операция, ДебетСчет, КредитСчет, СтрПровНалог );
    ПриходныйОрдер( TaxSum,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет );
end;
macro ОрдерПодохНалогНаПродажуПД( TaxSum, СуммаПродажи, OutCurr_Ref, RDate, Операция )
    var Содержание1 = " Подох. налог на продажу ПД в " + GetISOCurr(OutCurr_Ref);
    var Содержание2 = " от продажи " + string( СуммаПродажи ) + " " + GetISOCurr(OutCurr_Ref);
    var Содержание3 = РеквизитыПД( Reestr.AutoKey );
    var ДебетСчет, КредитСчет;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( {NationalCur}, OutCurr_Ref, Операция, ДебетСчет, КредитСчет, СтрПровНалог );
    ПриходныйОрдер( TaxSum,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет );
end;
macro ОрдерКомиссияНаПродажу( СуммаКомиссии, ВалютаКомиссии, Сумма, Валюта, КурсОперации, RDate, KindOper )
    /*var Содержание1 = " Получена комиссия в " + GetISOCurr(ВалютаКомиссии);*/
    /*комиссия может быть мультивалютной и различной по операциям реестра*/
    var Содержание1 = " Получена комиссия при продаже";
    var Содержание2 = " " + Trim( String(Сумма) ) + " " + GetISOCurr(Валюта) + 
                      " по курсу " + Trim( String( КурсОперации ));
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = 99;

    if  ( KindOper == CDF_OperCurrSale   ) Содержание1 = Содержание1 + " валюты";
    elif( KindOper == CDF_OperPayDocSale ) Содержание1 = Содержание1 + " ПД на сумму";
    end;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ВалютаКомиссии, ANY_CURR, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ПриходныйОрдер( СуммаКомиссии,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерКупленоВалюты( КурсОперации, КурсЦБ, OutSum, IncomeSum, IncomeCurr_Ref, RDate )
    var Содержание1 = " Куплено " + GetISOCurr(IncomeCurr_Ref);
    var Содержание2 = " Кпок = " + Trim(String(КурсОперации)) + "  ОК = " + Trim(String(КурсЦБ));
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperCurrBuy;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( IncomeCurr_Ref, {NationalCur}, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    РасходныйОрдер(IncomeSum,IncomeCurr_Ref,OutSum,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерКупленоПД( КурсОперации, КурсЦБ, OutSum, IncomeSum, IncomeCurr_Ref, RDate )
    var Содержание1 = " Куплено ПД в " + GetISOCurr(IncomeCurr_Ref);
    var Содержание2 = " Кпок = " + Trim(String(КурсОперации)) + "  ОК = " + Trim(String(КурсЦБ));
    var Содержание3 = РеквизитыПД( Reestr.AutoKey );
    var ДебетСчет, КредитСчет, Операция = CDF_OperPayDocBuy;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( IncomeCurr_Ref, {NationalCur}, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    РасходныйОрдер(IncomeSum,IncomeCurr_Ref,OutSum,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерКурсПокупки_ОК( КодВалюты, КурсОперации,КурсЦБ, ВсяСумма, ВсяРазницаПок, RDate, Операция )
    var Содержание1 = " Разница при покупке ";
    var Содержание2 = " Кпок = " + Trim(String(КурсОперации)) + "  ОК = " + Trim(String(КурсЦБ));
    var Содержание3 = " от покупки " + string( ВсяСумма ) + " " + GetISOCurr(КодВалюты);
    var ДебетСчет, КредитСчет;

    if  ( (Операция == CDF_OperCurrBuy   ) or (Операция == CDF_OperCurrSale ) )
      Содержание1 = Содержание1 + "валюты ";
    elif( (Операция == CDF_OperPayDocSale) or (Операция == CDF_OperPayDocBuy) )
      Содержание1 = Содержание1 + "ПД ";
    end;

    if( КурсОперации > КурсЦБ )
       ПолучитьСчетаКонтировкиБалансовыхОпераций( КодВалюты, {NationalCur}, Операция, ДебетСчет, КредитСчет, СтрПровПолРазнКурсов );
    else
       ПолучитьСчетаКонтировкиБалансовыхОпераций( КодВалюты, {NationalCur}, Операция, ДебетСчет, КредитСчет, СтрПровОтрРазнКурсов );
    end;
    ОрдерБезналичный( ВсяРазницаПок,{NationalCur}, Содержание1, Содержание2, Содержание3, RDate, ДебетСчет, КредитСчет );
end;
macro ОрдерПодохНалогНаПокупкуВал( TaxSum, СуммаПродажи, IncCurr_Ref, RDate, Операция )
    var Содержание1 = " Подох. налог на покупку  " + GetISOCurr(IncCurr_Ref);
    var Содержание2 = " от покупки " + string( СуммаПродажи ) + " " + GetISOCurr(IncCurr_Ref);
    var Содержание3 = "";
    var ДебетСчет, КредитСчет;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( IncCurr_Ref, {NationalCur}, Операция, ДебетСчет, КредитСчет, СтрПровНалог );
    ПриходныйОрдер( TaxSum,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет );
end;
macro ОрдерПодохНалогНаПокупкуПД( TaxSum, СуммаПродажи, IncCurr_Ref, RDate, Операция )
    var Содержание1 = " Подох. налог на покупку ПД в " + GetISOCurr(IncCurr_Ref);
    var Содержание2 = " от покупки " + string( СуммаПродажи ) + " " + GetISOCurr(IncCurr_Ref);
    var Содержание3 = РеквизитыПД( Reestr.AutoKey );
    var ДебетСчет, КредитСчет;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( IncCurr_Ref, {NationalCur}, Операция, ДебетСчет, КредитСчет, СтрПровНалог );
    ПриходныйОрдер( TaxSum,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет );
end;
macro ОрдерКомиссияНаПокупку( СуммаКомиссии, ВалютаКомиссии, Сумма, Валюта, КурсОперации, RDate, KindOper )
    /*var Содержание1 = " Получена комиссия в " + GetISOCurr(ВалютаКомиссии);*/
    /*комиссия может быть мультивалютной и различной по операциям реестра*/
    var Содержание1 = " Получена комиссия при покупке";
    var Содержание2 = " " + Trim( String(Сумма) ) + " " + GetISOCurr(Валюта) +
                      " по курсу " + Trim( String( КурсОперации ));
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = 99;

    if  ( KindOper == CDF_OperCurrBuy   ) Содержание1 = Содержание1 + " валюты";
    elif( KindOper == CDF_OperPayDocBuy ) Содержание1 = Содержание1 + " ПД на сумму";
    end;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ВалютаКомиссии, ANY_CURR, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ПриходныйОрдер( СуммаКомиссии,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерКомиссПровНаПодл( СуммаКомиссии, ВалютаКомиссии, Сумма, Валюта, RDate )
    /*var Содержание1 = " Получена комиссия в " + GetISOCurr(ВалютаКомиссии);*/
    /*комиссия может быть мультивалютной и различной по операциям реестра*/
    var Содержание1 = " Получена комиссия ";
    var Содержание2 = " при проверке на подлинность " + Trim( String(Сумма) ) + " " + GetISOCurr(Валюта);
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = 99;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ВалютаКомиссии, ANY_CURR, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ПриходныйОрдер( СуммаКомиссии,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;

/*          излишки \ недостача валюты - ордера по операциям               */

macro ОрдерОбнаружИзлишкиВал( СуммаПрихода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Обнаружены излишки " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = "";
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperDetectOverplus;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ВалютаОперации, ANY_CURR, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ПриходныйОрдер( СуммаПрихода,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерОбнаружИзлишкиПД( СуммаПрихода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Обнаружены излишки ПД " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = РеквизитыПД_Недостача( Operat.ID );
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperDetectOverplus;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ВалютаОперации, ANY_CURR, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ПриходныйОрдер( СуммаПрихода,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерВыданыИзлишкиВал( СуммаРасхода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Выданы излишки " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = "";
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperGiveOutOverplus;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ANY_CURR, ВалютаОперации, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    РасходныйОрдер( 0,ANY_CURR,СуммаРасхода,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерВыданыИзлишкиПД( СуммаРасхода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Выданы излишки ПД " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = РеквизитыПД_Недостача( Operat.ID );
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperGiveOutOverplus;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ANY_CURR, ВалютаОперации, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    РасходныйОрдер( 0,ANY_CURR,СуммаРасхода,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерОбнаружНедостачаВал( СуммаРасхода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Обнаружена недостача " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = "";
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperDetectDeficit;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ANY_CURR, ВалютаОперации, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    РасходныйОрдер( 0,ANY_CURR,СуммаРасхода,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерОбнаружНедостачаПД( СуммаРасхода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Обнаружена недостача ПД " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = РеквизитыПД_Недостача( Operat.ID );
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperDetectDeficit;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ANY_CURR, ВалютаОперации, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    РасходныйОрдер( 0,ANY_CURR,СуммаРасхода,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерВозмещНедостачаВал( СуммаПрихода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Возмещена недостача " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = "";
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperCompensateDeficit;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ВалютаОперации, ANY_CURR, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ПриходныйОрдер( СуммаПрихода,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерВозмещНедостачаПД( СуммаПрихода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Возмещена недостача ПД " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = РеквизитыПД_Недостача( Operat.ID );
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperCompensateDeficit;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ВалютаОперации, ANY_CURR, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ПриходныйОрдер( СуммаПрихода,{NationalCur},0,ANY_CURR,Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;

/*          излишки \ недостача валюты - итоговые ордера                     */

macro ОрдерИтогоОбнаружИзлишкиВал( СуммаПрихода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Обнаружены излишки " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = "";
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperDetectOverplus;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ВалютаОперации, ANY_CURR, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ОрдерБезналичный( СуммаПрихода,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерИтогоОбнаружИзлишкиПД( СуммаПрихода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Обнаружены излишки ПД " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = "";
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperDetectOverplus;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ВалютаОперации, ANY_CURR, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ОрдерБезналичный( СуммаПрихода,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерИтогоВыданыИзлишкиВал( СуммаРасхода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Выданы излишки " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = "";
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperGiveOutOverplus;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ANY_CURR, ВалютаОперации, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ОрдерБезналичный( СуммаРасхода,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерИтогоВыданыИзлишкиПД( СуммаРасхода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Выданы излишки ПД " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = "";
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperGiveOutOverplus;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ANY_CURR, ВалютаОперации, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ОрдерБезналичный( СуммаРасхода,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерИтогоОбнаружНедостачаВал( СуммаРасхода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Обнаружена недостача " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = "";
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperDetectDeficit;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ANY_CURR, ВалютаОперации, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ОрдерБезналичный( СуммаРасхода,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерИтогоОбнаружНедостачаПД( СуммаРасхода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Обнаружена недостача ПД " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = "";
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperDetectDeficit;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ANY_CURR, ВалютаОперации, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ОрдерБезналичный( СуммаРасхода,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерИтогоВозмещНедостачаВал( СуммаПрихода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Возмещена недостача " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = "";
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperCompensateDeficit;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ВалютаОперации, ANY_CURR, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ОрдерБезналичный( СуммаПрихода,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;
macro ОрдерИтогоВозмещНедостачаПД( СуммаПрихода, СуммаОперации, ВалютаОперации, RDate )
    var Содержание1 = " Возмещена недостача ПД " + string(СуммаОперации) + " "+  GetISOCurr(ВалютаОперации);
    var Содержание2 = "";
    var Содержание3 = "";
    var ДебетСчет, КредитСчет, Операция = CDF_OperCompensateDeficit;
    ПолучитьСчетаКонтировкиБалансовыхОпераций( ВалютаОперации, ANY_CURR, Операция, ДебетСчет, КредитСчет, СтрПровОВП );
    ОрдерБезналичный( СуммаПрихода,{NationalCur},Содержание1,Содержание2,Содержание3,RDate, ДебетСчет, КредитСчет);
end;

/****************************************************************************/
/* Процедура выводит все приходные или расходные ордера                     */
/****************************************************************************/
macro ВсеОрдераДляВалюты( KindOper, IncCurr, OutCurr, TypeOrder )
  var СуммаРазницПокупки = 0;  var ВсяКупленнаяСумма = 0;
  var TypeReestr = GetTypeReestr(KindOper);
  Reestr.ExchangeNumber = office.ID;
  Reestr.Emp_Ref        = Session.Emp_Ref;
  Reestr.Sess_Ref       = Session.Number;
  Reestr.Type_Ref       = TypeReestr;
  Reestr.OutCurr_Ref    = OutCurr;
  Reestr.IncomeCurr_Ref = IncCurr;
  Reestr.Rate_Ref       = 0;
  var Loop = GetGE( Reestr );
  var КурсОперации, КурсЦБ, ПроцентнаяКомиссия, Тип, КомиссияПоОперации;
  var ВалютаКомиссии, RDate, IncomeSumCB, IncomeSum, РазницаПрод;
  var ФиксКомиссияРуб, ФиксКомиссияВал, OutSumCB, РазницаПок;
  var СуммаРазницПродажи, ВсяПроданнаяСумма, ВалютаФиксКомисс;
 
  While( (Loop == True) AND
         (Reestr.ExchangeNumber  == office.ID)         AND
         (Reestr.Emp_Ref         == Session.Emp_Ref)   AND
         (Reestr.Sess_Ref        == Session.Number )   AND
         (Reestr.Type_Ref        == TypeReestr )       AND
         (Reestr.OutCurr_Ref     == OutCurr    )       AND
         (Reestr.IncomeCurr_Ref  == IncCurr    )
  )

    /* Восстанавливаем исходные суммы по реестрам   RA 13-09-99 */
    УчетНекассовыхОборотов( "По реестру", Reestr.IncomeSum, Reestr.OutSum );

    /* курс операции */
    КурсОперации = Число_Курс( Reestr.Rate,   " руб" );
    КурсЦБ       = Число_Курс( Reestr.CBRate, " руб" );

    ПроцентнаяКомиссия = $0.00;
    Тип = "";
    КомиссияПоОперации = Комиссия( ПроцентнаяКомиссия, Тип );
    if  ( Тип == "ОтВыданнойСуммы"   )
       ВалютаКомиссии = Reestr.OutCurr_Ref;
    elif( Тип == "ОтПолученнойСуммы" )
       ВалютаКомиссии = Reestr.IncomeCurr_Ref;
    end;
    RDate = Reestr.Date;
    if( (KindOper == CDF_OperCurrSale) OR (KindOper == CDF_OperPayDocSale) )
       /* полученная сумма при продаже ( по ЦБ-курсу ) */
       IncomeSumCB     = money( Reestr.CBRate * Reestr.OutSum    );
       /* полученная сумма при продаже (по курсу продажи) */
       IncomeSum    = money( Reestr.Rate * Reestr.OutSum );
       /* разница между суммами продажи пересчитанными в разных курсах */
       РазницаПрод  = money( Abs( IncomeSum  - IncomeSumCB ));

       if( TypeOrder == "Приход" )
         if( KindOper == CDF_OperCurrSale )
            ОрдерПроданоВалюты( КурсОперации, КурсЦБ, IncomeSumCB, Reestr.OutSum, Reestr.OutCurr_Ref, RDate );
            ОрдерНалогНаПродажуВал( Reestr.TaxSum, Reestr.OutSum, Reestr.OutCurr_Ref, RDate, Reestr.Oper_Ref );
            if( Reestr.IncTaxSum != 0 )
              ОрдерПодохНалогНаПродажуВал( Reestr.IncTaxSum, Reestr.OutSum, Reestr.OutCurr_Ref, RDate, Reestr.Oper_Ref );
            end;
         elif( KindOper == CDF_OperPayDocSale )
            ОрдерПроданоПД( КурсОперации, КурсЦБ, IncomeSumCB, Reestr.OutSum, Reestr.OutCurr_Ref, RDate );
            ОрдерНалогНаПродажуПД( Reestr.TaxSum, Reestr.OutSum, Reestr.OutCurr_Ref, RDate, Reestr.Oper_Ref );
            if( Reestr.IncTaxSum != 0 )
              ОрдерПодохНалогНаПродажуПД( Reestr.IncTaxSum, Reestr.OutSum, Reestr.OutCurr_Ref, RDate, Reestr.Oper_Ref );
            end;
         end;
         if( КомиссияПоОперации AND (Тип != "") )
            ОрдерКомиссияНаПродажу( КомиссияПоОперации, ВалютаКомиссии, Reestr.OutSum, Reestr.OutCurr_Ref, Reestr.Rate, RDate, KindOper );
         elif( КомиссияПоОперации )/* фиксированная комиссия */
            ФиксКомиссияРуб = 0; ФиксКомиссияВал = 0;
            ВалютаФиксКомисс = ANY_CURR; /* найдет и подставит реальную валюту */

            ПолучитьФиксированнуюКомиссию( ВалютаФиксКомисс, ФиксКомиссияРуб, ФиксКомиссияВал );
            if( ФиксКомиссияРуб )
               ОрдерКомиссияНаПродажу( ФиксКомиссияРуб, {NationalCur}, Reestr.OutSum, Reestr.OutCurr_Ref, Reestr.Rate, RDate, KindOper );
            elif( ФиксКомиссияВал )
               КурсЦБ = ПолучитьКурсЦБ( ВалютаФиксКомисс, Reestr.Rate_Ref );
               ФиксКомиссияВал = Money( ФиксКомиссияВал * КурсЦБ );
               ОрдерКомиссияНаПродажу( ФиксКомиссияВал, ВалютаФиксКомисс, Reestr.OutSum, Reestr.OutCurr_Ref, Reestr.Rate, RDate, KindOper );
            end;
         end;
       end;
       if( TypeOrder == "БезналПродажа")
         ОрдерКурсПродажи_ОК( КурсОперации, КурсЦБ, РазницаПрод, Reestr.OutSum, Reestr.OutCurr_Ref, RDate, Reestr.Oper_Ref );
       end;

    elif( (KindOper == CDF_OperCurrBuy) OR (KindOper == CDF_OperPayDocBuy) )
       /*учитываем процентную комиссию в выданной сумме в зависимости от    */
       /*   типа процентной комиссии                                        */
       /*if  ( Тип == "ОтВыданнойСуммы"   )                                           */
       /*   ВыданнаяСПроц = Reestr.OutSum - ПроцентнаяКомиссия;                       */
       /*elif( Тип == "ОтПолученнойСуммы" )                                           */
       /*   ВыданнаяСПроц = Reestr.OutSum + Money( ПроцентнаяКомиссия * Reestr.Rate );*/
       /*end;                                                                         */
       /* выданная сумма при покупке (по ЦБ-курсу) */
       OutSumCB  = money( Reestr.CBRate * Reestr.IncomeSum );
       /* разница между суммами покупки пересчитанными в разных курсах */
       РазницаПок = money( Abs(OutSumCB - Reestr.OutSum ));

       /* полученная сумма при продаже (по ЦБ-курсу) */
       IncomeSumCB  = money( Reestr.CBRate * Reestr.OutSum );
       /* разница между суммами продажи пересчитанными в разных курсах */
       РазницаПрод  = money( Abs(IncomeSumCB - Reestr.IncomeSum ));

       if( TypeOrder == "Расход" )
          /* вывод строк с информацией по операциям */
          if( KindOper == CDF_OperCurrBuy )/* покупка валюты */
             ОрдерКупленоВалюты( КурсОперации, КурсЦБ, Reestr.OutSum, Reestr.IncomeSum,Reestr.IncomeCurr_Ref,RDate );
          elif( KindOper == CDF_OperPayDocBuy )/* покупка платежных документов за рубли */
             ОрдерКупленоПД( КурсОперации,КурсЦБ,Reestr.OutSum, Reestr.IncomeSum,Reestr.IncomeCurr_Ref,RDate  );
          end;
       end;

       if( TypeOrder == "Приход" )
          if( КомиссияПоОперации AND (Тип != "") )
             ОрдерКомиссияНаПокупку( КомиссияПоОперации, ВалютаКомиссии, Reestr.IncomeSum, Reestr.IncomeCurr_Ref, Reestr.Rate, RDate, KindOper );
          elif( КомиссияПоОперации )/* фиксированная комиссия */
             ФиксКомиссияРуб = 0; ФиксКомиссияВал = 0;
             ВалютаФиксКомисс = ANY_CURR; /* найдет и подставит реальную валюту */

             ПолучитьФиксированнуюКомиссию( ВалютаФиксКомисс, ФиксКомиссияРуб, ФиксКомиссияВал );
             if( ФиксКомиссияРуб )
                ОрдерКомиссияНаПокупку( ФиксКомиссияРуб, Reestr.OutCurr_Ref, Reestr.IncomeSum, Reestr.IncomeCurr_Ref, Reestr.Rate, RDate, KindOper );
             elif( ФиксКомиссияВал )
                КурсЦБ = ПолучитьКурсЦБ( ВалютаФиксКомисс, Operat.Rate_Ref );
                ФиксКомиссияВал = Money( ФиксКомиссияВал * КурсЦБ );
                ОрдерКомиссияНаПокупку( ФиксКомиссияВал, ВалютаФиксКомисс, Reestr.IncomeSum, Reestr.IncomeCurr_Ref, Reestr.Rate, RDate, KindOper );
             end;
          end;
          /* подоходный налог на покупку */
          if( (KindOper == CDF_OperCurrBuy) AND (Reestr.IncTaxSum != 0) )
             ОрдерПодохНалогНаПокупкуВал( Reestr.IncTaxSum, Reestr.IncomeSum, Reestr.IncomeCurr_Ref, RDate, Reestr.Oper_Ref );
          elif( (KindOper == CDF_OperPayDocBuy) AND (Reestr.IncTaxSum != 0) )
             ОрдерПодохНалогНаПокупкуПД( Reestr.IncTaxSum, Reestr.IncomeSum, Reestr.IncomeCurr_Ref, RDate, Reestr.Oper_Ref );
          end;
       end;
       if( TypeOrder == "БезналПокупка" )
          СуммаРазницПокупки = СуммаРазницПокупки + РазницаПок;
          ВсяКупленнаяСумма  = ВсяКупленнаяСумма  + Reestr.IncomeSum;
       end;
       if( TypeOrder == "БезналПродажа" )
          СуммаРазницПродажи = СуммаРазницПродажи + РазницаПрод;
          ВсяПроданнаяСумма  = ВсяПроданнаяСумма  + Reestr.OutSum;
       end;

    end;
    Loop = Next( Reestr );
  end;
  /* безналичный суммарный ордер на операции покупки */
  if( TypeOrder == "БезналПокупка" )
    if( СуммаРазницПокупки AND ВсяКупленнаяСумма )
       ОрдерКурсПокупки_ОК( IncCurr, КурсОперации, КурсЦБ, ВсяКупленнаяСумма, СуммаРазницПокупки, RDate, KindOper );
    end;
  end;
  /* безналичный суммарный ордер на операции продажи */
/*  if( TypeOrder == "БезналПродажа" )
    if( СуммаРазницПродажи AND ВсяПроданнаяСумма )
       ОрдерКурсПродажи_ОК( OutCurr, КурсОперации,КурсЦБ,ВсяПроданнаяСумма, СуммаРазницПродажи, RDate, KindOper );
    end;
  end;                                                                                                  
*/
end;

/**************************************************************************/
/* Вывод ордеров по операциям вне реестров                     RA 2-07-99 */
/**************************************************************************/
MACRO ОрдераВнеРеестров( KindOper, IncCurr, OutCurr, TypeOrder )

 var Loop, RDate, КурсЦБ;
 var КомиссияПоОперации, ПроцентнаяКомиссия, Тип;
 var ВалютаКомиссии, ФиксКомиссияРуб, ФиксКомиссияВал, ВалютаФиксКомисс;
 var СуммаКомиссии = $0.0, СуммаОперации = $0.0, КурсОперации     = $0.0, ВалютаОперации;
 var СуммаВалРуб   = $0.0, СуммаПДРуб    = $0.0, СуммаВалОперации = $0.0, СуммаПДОперации = $0.0;
 var СуммаРуб      = $0.0, TrsGroup;

 ClearRecord(Operat);
 Operat.Emp_Ref        = Session.Emp_Ref;
 Operat.Sess_Ref       = Session.Number;
 Operat.Oper_Ref       = KindOper;
 Operat.State_Ref      = CDF_OperCompleted;/* завершена */
 Operat.Rate_Ref       = 0;
 Operat.OutCurr_Ref    = OutCurr;
 Operat.IncomeCurr_Ref = IncCurr;

 Loop = GetGE( Operat );

 While( ( Loop == True ) AND
        (Operat.Emp_Ref        == Session.Emp_Ref)         AND
        (Operat.Sess_Ref       == Session.Number)          AND
        ( (Operat.State_Ref    == CDF_OperCompleted) OR
          (Operat.State_Ref    == CDF_OperInReestr)   )    AND
        (Operat.Oper_Ref       == KindOper)
      )

    if( (
          ( (OutCurr != ANY_CURR) AND (Operat.OutCurr_Ref == OutCurr) )  OR
          ( OutCurr == ANY_CURR )
        )
         AND
        (
          ( (IncCurr != ANY_CURR) AND (Operat.IncomeCurr_Ref == IncCurr) ) OR
          ( IncCurr == ANY_CURR )
        )
      )

      КурсОперации = Operat.CBRate;

      /* Восстанавливаем исходные суммы по операциям   RA 13-09-99 */
      УчетНекассовыхОборотов( "По операции", Operat.IncomeAmount, Operat.OutAmount );

      ПроцентнаяКомиссия = $0.00;
      Тип = "";
      КомиссияПоОперации = Комиссия_ПоОперации( ПроцентнаяКомиссия, Тип );
      if  ( Тип == "ОтВыданнойСуммы"   )
         ВалютаКомиссии = Operat.OutCurr_Ref;
      elif( Тип == "ОтПолученнойСуммы" )
         ВалютаКомиссии = Operat.IncomeCurr_Ref;
      end;
      RDate = Operat.Date;

       /* ордер по проверке на подлинность */
      if( KindOper == CDF_OperCheck )
         if( TypeOrder == "Приход" )
            if( КомиссияПоОперации AND (Тип !="") )
                                  /* СуммаКомиссии,      ВалютаКомиссии,      Сумма,            Валюта,       КурсОперации, RDate )*/
              /*ОрдерКомиссПровНаПодл( КомиссияПоОперации, ВалютаКомиссии, Operat.OutAmount, Operat.OutCurr_Ref, RDate );*/
              СуммаКомиссии = СуммаКомиссии + КомиссияПоОперации;
              СуммаОперации = СуммаОперации + Operat.OutAmount;
            elif( КомиссияПоОперации )/*фиксированная комиссия*/
               ФиксКомиссияРуб = 0; ФиксКомиссияВал = 0;
               ВалютаФиксКомисс = ANY_CURR; /*найдет и подставит реальную валюту*/
               ПолучитьФиксированнуюКомиссиюПоОперации( ВалютаФиксКомисс, ФиксКомиссияРуб, ФиксКомиссияВал );
               if( ФиксКомиссияРуб )
                  /*ОрдерКомиссПровНаПодл( ФиксКомиссияРуб, {NationalCur},      Operat.OutAmount, Operat.OutCurr_Ref, RDate );*/
                  СуммаКомиссии = СуммаКомиссии + ФиксКомиссияРуб;
                  СуммаОперации = СуммаОперации + Operat.OutAmount;
               elif( ФиксКомиссияВал )
                  КурсЦБ = ПолучитьКурсЦБ( ВалютаФиксКомисс, Operat.Rate_Ref );
                  ФиксКомиссияВал = Money( ФиксКомиссияВал * КурсЦБ );
                  /*ОрдерКомиссПровНаПодл( ФиксКомиссияВал, ВалютаФиксКомисс, Operat.OutAmount, Operat.OutCurr_Ref, RDate );*/
                  СуммаКомиссии = СуммаКомиссии + ФиксКомиссияВал;
                  СуммаОперации = СуммаОперации + Operat.OutAmount;
               end;
            end;
         end;

      elif( ( KindOper == CDF_OperDetectOverplus ) OR   /* Отражение излишков   */
            ( KindOper == CDF_OperCompensateDeficit) )  /* Возмещение недостачи */

        ВалютаОперации = Operat.IncomeCurr_Ref;
        СуммаОперации  = Operat.IncomeAmount;
        СуммаРуб       = money(СуммаОперации * КурсОперации);
        /* наложение структуры deficit на поле Operat.ExtInfo */
        SetRecordAddr( deficitop, Operat, 0, FldOffset( Operat,"ExtInfo"),True);
        TrsGroup = deficitop.TrsGroup;
 
        if( ( TypeOrder == "Приход" ) AND ( СуммаРуб != 0 ) )
                               /* ордер по обнаруженным излишкам */
          if( KindOper == CDF_OperDetectOverplus ) 
             if( TrsGroup == TrsNoteGrp) 
               ОрдерОбнаружИзлишкиВал( СуммаРуб, СуммаОперации, ВалютаОперации, RDate );
             elif( ( TrsGroup == TrsPayDoc ) OR ( TrsGroup == TrsTratt ) )
               ОрдерОбнаружИзлишкиПД(  СуммаРуб, СуммаОперации, ВалютаОперации, RDate );
             end;
                               /* ордер по возмещенной недостаче */
          elif( KindOper == CDF_OperCompensateDeficit)
             if( TrsGroup == TrsNoteGrp) 
               ОрдерВозмещНедостачаВал( СуммаРуб, СуммаОперации, ВалютаОперации, RDate );
             end;
             if( ( TrsGroup == TrsPayDoc ) OR ( TrsGroup == TrsTratt ) )
               ОрдерВозмещНедостачаПД(  СуммаРуб, СуммаОперации, ВалютаОперации, RDate );
             end;
          end;

        elif( TypeOrder == "Итого" )
          if( TrsGroup == TrsNoteGrp )
            СуммаВалОперации = СуммаВалОперации + Operat.IncomeAmount;
          elif( ( TrsGroup == TrsPayDoc ) OR ( TrsGroup == TrsTratt ) )
            СуммаПДОперации  = СуммаПДОперации  + Operat.IncomeAmount;
          end;

        end;

      elif( ( KindOper == CDF_OperGiveOutOverplus ) OR  /* Выдача излишков     */
            ( KindOper == CDF_OperDetectDeficit ) )     /* Отражение недостачи */

        ВалютаОперации = Operat.OutCurr_Ref;
        СуммаОперации  = Operat.OutAmount;
        СуммаРуб       = money(СуммаОперации * КурсОперации);
        /* наложение структуры deficit на поле Operat.ExtInfo */
        SetRecordAddr( deficitop, Operat, 0, FldOffset( Operat,"ExtInfo"),True);
        TrsGroup = deficitop.TrsGroup;

        if( ( TypeOrder == "Расход" ) AND ( СуммаРуб != 0 ) )
                               /* ордер по выданным излишкам */
          if( KindOper == CDF_OperGiveOutOverplus )
             if( TrsGroup == TrsNoteGrp )
               ОрдерВыданыИзлишкиВал( СуммаРуб, СуммаОперации, ВалютаОперации, RDate );
             elif( ( TrsGroup == TrsPayDoc ) OR ( TrsGroup == TrsTratt ) )
               ОрдерВыданыИзлишкиПД(  СуммаРуб, СуммаОперации, ВалютаОперации, RDate );
             end;
                               /* ордер по обнаруженной недостаче  */
          elif( KindOper == CDF_OperDetectDeficit ) 
             if( TrsGroup == TrsNoteGrp )
               ОрдерОбнаружНедостачаВал( СуммаРуб, СуммаОперации, ВалютаОперации, RDate );
             elif( ( TrsGroup == TrsPayDoc ) OR ( TrsGroup == TrsTratt ) )
               ОрдерОбнаружНедостачаПД(  СуммаРуб, СуммаОперации, ВалютаОперации, RDate );
             end;
          end;

        elif( TypeOrder == "Итого" )
          if( TrsGroup == TrsNoteGrp )
            СуммаВалОперации = СуммаВалОперации + Operat.OutAmount;
          elif( ( TrsGroup == TrsPayDoc ) OR ( TrsGroup == TrsTratt ) )
            СуммаПДОперации  = СуммаПДОперации  + Operat.OutAmount;
          end;

        end;

      end;

    end;
    Loop = Next( Operat );
 end;

 СуммаВалРуб = money(СуммаВалОперации * КурсОперации);
 СуммаПДРуб  = money(СуммаПДОперации  * КурсОперации);

  /*                        ПЕЧАТЬ ИТОГОВЫХ ОРДЕРОВ                    */
  /* ордер по проверке на подлинность */
 if( ( KindOper == CDF_OperCheck ) AND ( TypeOrder == "Приход" ) )
    if( СуммаКомиссии )
                          /* СуммаКомиссии, не используется,    Сумма,     Валюта,  RDate )*/
      ОрдерКомиссПровНаПодл( СуммаКомиссии, ВалютаКомиссии, СуммаОперации, OutCurr, RDate );
    end;
  /* ордер по обнаруженным излишкам */
 elif( ( KindOper == CDF_OperDetectOverplus    ) AND ( TypeOrder == "Итого" ) )
    if( СуммаВалРуб )
      /* для нал. валюты:   СуммаОперацииРуб СуммаОперации   ВалютаОперации  RDate */
      ОрдерИтогоОбнаружИзлишкиВал( СуммаВалРуб, СуммаВалОперации, ВалютаОперации, RDate );
    end;
    if( СуммаПДРуб )
      /* для ПД:            СуммаОперацииРуб СуммаОперации   ВалютаОперации  RDate */
      ОрдерИтогоОбнаружИзлишкиПД(  СуммаПДРуб,  СуммаПДОперации,  ВалютаОперации, RDate );
    end;
  /* ордер по выданным излишкам */
 elif( ( KindOper == CDF_OperGiveOutOverplus   ) AND ( TypeOrder == "Итого" ) )
    if( СуммаВалРуб )
      ОрдерИтогоВыданыИзлишкиВал( СуммаВалРуб, СуммаВалОперации, ВалютаОперации, RDate );
    end;
    if( СуммаПДРуб )
      ОрдерИтогоВыданыИзлишкиПД(  СуммаПДРуб,  СуммаПДОперации,  ВалютаОперации, RDate );
    end;
  /* ордер по обнаруженной недостаче  */
 elif( ( KindOper == CDF_OperDetectDeficit     ) AND ( TypeOrder == "Итого" ) )
    if( СуммаВалРуб )
      ОрдерИтогоОбнаружНедостачаВал( СуммаВалРуб, СуммаВалОперации, ВалютаОперации, RDate );
    end;
    if( СуммаПДРуб )
      ОрдерИтогоОбнаружНедостачаПД(  СуммаПДРуб,  СуммаПДОперации,  ВалютаОперации, RDate );
    end;
  /* ордер по возмещенной недостаче */
 elif( ( KindOper == CDF_OperCompensateDeficit ) AND ( TypeOrder == "Итого" ) )
    if( СуммаВалРуб )
      ОрдерИтогоВозмещНедостачаВал( СуммаВалРуб, СуммаВалОперации, ВалютаОперации, RDate );
    end;
    if( СуммаПДРуб )
      ОрдерИтогоВозмещНедостачаПД(  СуммаПДРуб,  СуммаПДОперации,  ВалютаОперации, RDate );
    end;
 end;
END;

/**************************************************************************/
/* RA 6-01-99 */
MACRO ЧитаемВБуфер( RepF, Буфер, Пишем, Конец, ЧислоСтрок )
   var Loop = True;
   var i = 0;
   While( Loop AND (NOT Конец) )
      Конец = NOT Next(RepF);
      Буфер(i) = RepF.str;
      if( SubStr( Буфер(i),1,40 ) == MkStr("-",40) )
         Пишем = True;
         Loop = False;
      end;
      i = i+1;
   end;
   setparm(1,Буфер);
   setparm(2,Пишем);
   setparm(3,Конец);
   setparm(4,i);
END;

/**************************************************************************/
/* RA 6-01-99 */
MACRO ПишемВДвеКолонки( RepF, Буфер, Пишем, Конец, ПоследнийОрдер, ЧислоСтрок )
   var Loop = True;
   var i = 0, TxtString, НужноПробелов;
   var EndOfBuf = False, EndOfFileBlock = False;
   ПоследнийОрдер = True;
   Loop = Next(RepF);
   While( Loop )
      ПоследнийОрдер = False;
      if( i > ЧислоСтрок-1 )
         Буфер(i) = "";
      end;
      if( EndOfFileBlock )
         TxtString = "";
      else
         TxtString = RepF.str;
      end;
      НужноПробелов = ШиринаКолонки - StrLen(Буфер(i));
      if( НужноПробелов > 0 )
         println( Буфер(i) + MkStr(" ", НужноПробелов) + TxtString );
      else
         MsgBox( "Неверно указан в макросе SBR_OTH параметр \"ШиринаКолонки\" " );
         MsgBox( "ШиринаКолонки="+string( ШиринаКолонки ) );
         MsgBox( "Реальная длина строки="+string( StrLen(Буфер(i)) ) );
         println( Буфер(i) + TxtString );
      end;
      if( SubStr( Буфер(i),1,40 ) == MkStr("-",40) )
         EndOfBuf = True;
      end;
      if( SubStr( TxtString,1,40 ) == MkStr("-",40) )
         EndOfFileBlock = True;
      end;
      if( ( EndOfBuf == True ) AND ( EndOfFileBlock == True ) )
         Пишем = False;
         Loop = False;
      end;
      i = i+1;
      if( EndOfFileBlock == False )
        Loop = Next(RepF);
      end;
   end;
   setparm(1,Буфер);
   setparm(2,Пишем);
   setparm(3,Конец);
   setparm(4,ПоследнийОрдер);
END;

/**************************************************************************/
/* RA 10-01-99 */
MACRO ПишемПоследнийОрдер( Буфер, Конец )
   var Loop = True;
   var i = 0;
   While( Loop )
      println( Буфер(i) );
      if( SubStr( Буфер(i),1,40 ) == MkStr("-",40) )
         Loop = False;
         Конец = True;
      end;
      i = i+1;
   end;
   setparm(1,Конец);
END;

/**************************************************************************/
/* Функция разбивает файл отчета на две колонки     RA 6-01-99            */
/**************************************************************************/

MACRO РазбиениеВДвеКолонки( FileTxtName )

   ARRAY Буфер;
   /*Open( ReportFile );*/
   var Пишем = False;
   var Конец=False;
   var ЧислоСтрок;
   var ПоследнийОрдер = False;
   SetOutPut( 0 );
   MakeReportFile( ReportF, "sbr_" + FileTxtName );
   ReWind(ReportF);

   While( NOT Конец )
      if( ПоследнийОрдер )
         ПишемПоследнийОрдер( Буфер, Конец );
      elif( Пишем )
         ПишемВДвеКолонки( ReportFile, Буфер, Пишем, Конец, ПоследнийОрдер, ЧислоСтрок );
      else
         ЧитаемВБуфер( ReportFile, Буфер, Пишем, Конец, ЧислоСтрок );
      end;
   end;
   Close( ReportFile );
   DelFile( ReportFile );
END;

/**************************************************************************/
macro ВсеПриходныеОрдера()
   TotalNumCurr = 0;
   НомерВалюты  = 0;
   MakeReportFile( ReportFile, "f53" );
   PrintDate_n_Time();
   /* заполняем массив валют: DiffCurr */
   /* ЗаполнитьМассивВалют(); + в массив валюты по операц. проверки на подл. */
   ЗаполнитьМассивы_ПрПодл();
   /* базовую валюту(доллары) перенести на первое место */
   MoveElementArray( DiffCurr, {BaseCur}, 0, TotalNumCurr );
   /* формирование и печать приходных ордеров */
   While( НомерВалюты < TotalNumCurr )
      if( DiffCurr(НомерВалюты) != {NationalCur} )
         ВсеОрдераДляВалюты( CDF_OperCurrSale,   {NationalCur}, DiffCurr(НомерВалюты), "Приход" );
         ВсеОрдераДляВалюты( CDF_OperPayDocSale, {NationalCur}, DiffCurr(НомерВалюты), "Приход" );
         /* для печати приходных ордеров на комиссию при покупке валюты или плат.док */
         ВсеОрдераДляВалюты( CDF_OperCurrBuy,   DiffCurr(НомерВалюты), {NationalCur},  "Приход"  );
         ВсеОрдераДляВалюты( CDF_OperPayDocBuy, DiffCurr(НомерВалюты), {NationalCur},  "Приход"  );

         ОрдераВнеРеестров(  CDF_OperCheck,     DiffCurr(НомерВалюты), DiffCurr(НомерВалюты), "Приход" );
      end;
      ОрдераВнеРеестров(  CDF_OperDetectOverplus,     DiffCurr(НомерВалюты), ANY_CURR, "Приход" );
      ОрдераВнеРеестров(  CDF_OperCompensateDeficit,  DiffCurr(НомерВалюты), ANY_CURR, "Приход" );
      НомерВалюты = НомерВалюты + 1;
   end;
   /* Close( ReportFile ); */
   РазбиениеВДвеКолонки("f53"); /* ReportF - новый файл в две колонки */
   /* смотрим файл отчета */
   SetOutPut( 0 );
   ViewFile( ReportF );
   Close( ReportF );
end;

macro ВсеРасходныеОрдера()
   TotalNumCurr = 0;
   НомерВалюты  = 0;
   MakeReportFile( ReportFile, "f54" );
   PrintDate_n_Time();
   /* заполняем массив валют: DiffCurr */
   ЗаполнитьМассивВалют();
   /* базовую валюту(доллары) перенести на первое место */
   MoveElementArray( DiffCurr, {BaseCur}, 0, TotalNumCurr );
   /* формирование и печать приходных ордеров */
   While( НомерВалюты < TotalNumCurr )
      if( DiffCurr(НомерВалюты) != {NationalCur} )
         ВсеОрдераДляВалюты( CDF_OperCurrBuy,   DiffCurr(НомерВалюты), {NationalCur}, "Расход" );
         ВсеОрдераДляВалюты( CDF_OperPayDocBuy, DiffCurr(НомерВалюты), {NationalCur}, "Расход" );
      end;
      ОрдераВнеРеестров(  CDF_OperGiveOutOverplus, ANY_CURR, DiffCurr(НомерВалюты), "Расход" );
      ОрдераВнеРеестров(  CDF_OperDetectDeficit,   ANY_CURR, DiffCurr(НомерВалюты), "Расход" );
      НомерВалюты = НомерВалюты + 1;
   end;
   /*Close( ReportFile );*/
   РазбиениеВДвеКолонки("f54"); /* ReportF - новый файл в две колонки */
   /* смотрим файл отчета */
   SetOutPut( 0 );
   ViewFile( ReportF );
   Close( ReportF );
end;

macro ОрдерНаБезНаличные();
   MakeReportFile( ReportFile, "sbr_f63" );
   PrintDate_n_Time();

   TotalNumCurr = 0;
   НомерВалюты  = 0;
   /* заполняем массив валют: DiffCurr */
   ЗаполнитьМассивВалют();
   /* базовую валюту(доллары) перенести на первое место */
   MoveElementArray( DiffCurr, {BaseCur}, 0, TotalNumCurr );
   /* формирование и печать приходных ордеров */
   While( НомерВалюты < TotalNumCurr )
      if( DiffCurr(НомерВалюты) != {NationalCur} )
         /* покупка */
         ВсеОрдераДляВалюты( CDF_OperCurrBuy,    DiffCurr(НомерВалюты), {NationalCur},          "БезналПокупка" );
         ВсеОрдераДляВалюты( CDF_OperPayDocBuy,  DiffCurr(НомерВалюты), {NationalCur},          "БезналПокупка" );
         /* продажа */
         ВсеОрдераДляВалюты( CDF_OperCurrSale,   {NationalCur},         DiffCurr(НомерВалюты),  "БезналПродажа" );
         ВсеОрдераДляВалюты( CDF_OperPayDocSale, {NationalCur},         DiffCurr(НомерВалюты),  "БезналПродажа" );
      end;
      ОрдераВнеРеестров(  CDF_OperDetectOverplus,    DiffCurr(НомерВалюты), ANY_CURR,              "Итого" );
      ОрдераВнеРеестров(  CDF_OperGiveOutOverplus,   ANY_CURR,              DiffCurr(НомерВалюты), "Итого" );
      ОрдераВнеРеестров(  CDF_OperDetectDeficit,     ANY_CURR,              DiffCurr(НомерВалюты), "Итого" );
      ОрдераВнеРеестров(  CDF_OperCompensateDeficit, DiffCurr(НомерВалюты), ANY_CURR,              "Итого" );

      НомерВалюты = НомерВалюты + 1;
   end;
   /* смотрим файл отчета */
   SetOutPut( 0 );
   ViewFile( ReportFile );
   Close( ReportFile );
end;



/****************************************************************************/
/****************************************************************************/

/***** ОРДЕРА ПЕРЕСЫЛКИ *****/
/* AV 4-09-98 ***************************************************************/
/* Сокращенные формы ордеров пересылки ценностей                            */
/****************************************************************************/
MACRO РазделительОрдеровНаПересылку()
[
---------------------------------------------------------------------------------------
];
END;


MACRO СокращенныйОрдерНаПересылкуА( CurrMoney, ИнфоСтрока, Отправитель, Получатель, СтрокаНоминалов, СтрокаСерийНомеров, СуммаПрописью, ФИО, ФИО_Кассир, ДебетСчет, КредитСчет )
  /*var ДебетСчет  = GetIniString( "ORDERDEBET",  "exchange.ini");*/
  /*var КредитСчет = GetIniString( "ORDERKREDIT", "exchange.ini");*/
  /*var Подразделение = trim(GetIniString( "PODRAZD", "exchange.ini", FALSE));
      Подразделение = substr(Подразделение,1,StrLen(Подразделение)-1);*/
  var Номиналы_Серии = trim( СтрокаНоминалов );
  if( trim( СтрокаСерийНомеров ) ) 
    if( Номиналы_Серии ) Номиналы_Серии = Номиналы_Серии + " "; end;
    Номиналы_Серии = Номиналы_Серии + СтрокаСерийНомеров;
  end;
[
_________________________________________________________________________________

   Учреждение Сбербанка N #                                          Форма N61
   ПРИХОДНЫЙ ОРДЕР (экз.А)
   на пересылку наличных денег и ценностей                 #
   Получено #################### для доставки в учреждение Сбербанка N#
   _____________________________________________________________________________

   Наименование:#
   ###########################################################################
   Сумма (прописью): #

   ###########################################################################
   _____________________________________________________________________________

   Контролер_____________________________ Кассир #
   Перечисленные в ордере ценности и наличные деньги всего на сумму
   #
   приняты и оприходованы по операционному дневнику, приложению ф.25-a и книге
   кладовой #                    Контролер                Кассир
   _____________________________________________________________________________

    Дебет сч.N: #
   _____________________________________________________________________________

   Кредит сч.N: #
   _____________________________________________________________________________
]
(
   Отправитель, /*Подразделение,*/
   StrAddZeroDate( Session.StartDate ),
   ФИО,
   Получатель, /*Подразделение,*/
   CurrMoney,
   ИнфоСтрока:w,
   СуммаПрописью,
   /*(СтрокаНоминалов+" "+СтрокаСерийНомеров) :w,*/
   Номиналы_Серии:w, /* 24.08.2000 VS */
   ФИО_Кассир,
   СуммаПрописью,
   StrAddZeroDate( Session.StartDate ),
   ДебетСчет,
   КредитСчет
);
END;

MACRO СокращенныйОрдерНаПересылкуБ( CurrMoney, ИнфоСтрока, Отправитель, Получатель, СтрокаНоминалов, СтрокаСерийНомеров, СуммаПрописью, ФИО, ФИО_Кассир, ДебетСчет, КредитСчет )
  /*var ДебетСчет  = GetIniString( "ORDERDEBET",  "exchange.ini");*/
  /*var КредитСчет = GetIniString( "ORDERKREDIT", "exchange.ini");*/
  /*var Подразделение = trim(GetIniString( "PODRAZD", "exchange.ini", FALSE));
      Подразделение = substr(Подразделение,1,StrLen(Подразделение)-1);*/
  var Номиналы_Серии = trim( СтрокаНоминалов );
  if( trim( СтрокаСерийНомеров ) ) 
    if( Номиналы_Серии ) Номиналы_Серии = Номиналы_Серии + " "; end;
    Номиналы_Серии = Номиналы_Серии + СтрокаСерийНомеров;
  end;
[
_________________________________________________________________________________

   Учреждение Сбербанка N #                                          Форма N61
   РАСХОДНЫЙ ОРДЕР (экз.Б)
   на пересылку наличных денег и ценностей                 #
   Выдать через #################### для доставки в учреждение Сбербанка N#
   _____________________________________________________________________________

   Наименование:#
   ##########################################################################
   Сумма (прописью): #

   ##########################################################################
   _____________________________________________________________________________

   Контролер_____________________________ Кассир #
   Перечисленные в ордере ценности и наличные деньги всего на сумму
   #
   для доставки в учреждение Сбербанка N ########### получил #
   РАСПИСКА ПОЛУЧИВШЕГО ЦЕННОСТЬ _______________________________________________
   _____________________________________________________________________________
   Перечисленные в ордере ценности и наличные деньги всего на руб.______________
   _____________коп. высланы почтой "___"__________ _____г. квитанция N__________
   Контролер                                       Кассир
   _____________________________________________________________________________

    Дебет сч.N: #
   _____________________________________________________________________________

   Кредит сч.N: #
   _____________________________________________________________________________
]
(
   Отправитель, /*Подразделение,*/
   StrAddZeroDate( Session.StartDate ),
   ФИО,
   Получатель, /*Подразделение,*/
   CurrMoney,
   ИнфоСтрока:w,
   СуммаПрописью,
   /*(СтрокаНоминалов+" "+СтрокаСерийНомеров) :w,*/
   Номиналы_Серии:w,  /* 24.08.2000 VS */
   ФИО_Кассир,
   СуммаПрописью,
   Получатель, /*Подразделение,*/
   StrAddZeroDate( Session.StartDate ),
   ДебетСчет,
   КредитСчет
);
END;

MACRO Ордер61ПоСправкамБСО( тип, ИнфоСтрока, ВсяСумма )
   var NameTrs    = " Справки ф. N 0406007:";
   var СуммаПрописью, ФИОПолучено, ФИОВыдано;
   var ДебетСчет, КредитСчет, TypeOper;
   var Отправитель, Получатель;
 
   RubToStrAlt( ВсяСумма*100, СуммаПрописью, null );
   СуммаПрописью  =  СуммаПрописью + " шт.";
   if( тип == "Аванс" )
      ФИОПолучено = GetFullNameEmployee( Session.Emp_Ref );
      ФИОВыдано   = "";
      TypeOper    = Аванс;
      Отправитель = "Кладовая";
      Получатель  = "ОП " + {ExRegNumber};
   elif( тип == "Возврат" )
      ФИОПолучено = "";
      ФИОВыдано   = GetFullNameEmployee( Session.Emp_Ref );
      TypeOper    = Инкассация;
      Отправитель = "ОП " + {ExRegNumber};
      Получатель  = "Кладовая";
   else/*Передача*/
      ФИОПолучено = GetFullNameEmployee( avanceop.ToEmp  );
      ФИОВыдано   = GetFullNameEmployee( Session.Emp_Ref );
      TypeOper    = Передача;
      Отправитель = Получатель = "";
   end;
   ПолучитьСчетаКонтировкиВнебалансовыхОпераций( TypeOper, TypeValueBSO, CodIntValueBSO, ДебетСчет, КредитСчет );
   СокращенныйОрдерНаПересылкуА( NameTrs, ИнфоСтрока, Получатель, Отправитель, " ", " ", СуммаПрописью, ФИОПолучено, ФИОВыдано  , ДебетСчет, КредитСчет );
   СокращенныйОрдерНаПересылкуБ( NameTrs, ИнфоСтрока, Отправитель, Получатель, " ", " ", СуммаПрописью, ФИОВыдано,   ФИОПолучено, ДебетСчет, КредитСчет );
   РазделительОрдеровНаПересылку();
END;

MACRO Ордер61ПоВалюте( Curr_Ref, Sum, Тип, Неплатежность )
   var CurrMoney     = GetNameCurr( Curr_Ref) + Неплатежность;
   var SumMoney      = Trim(String( Sum ));
   var СуммаПрописью = CurToStrAlt( Sum , null, null, Curr_Ref );
   var КурсЦБ        = Trim(String(Operat.CBRate));
   var СуммаВрублях  = Trim(String(Operat.CBRate * Sum/100.:14:2));
   var ИнфоСтрока    = СтрокаСумм( SumMoney, КурсЦБ, СуммаВрублях );
   var СтрокаНоминалов  = ПолучитьСтрокуНоминалов( Operat );
   var ФИОПолучено, ФИОВыдано;
   var ДебетСчет, КредитСчет, TypeOper;
   var CodIntValueCurr = GetCurrRetail( Curr_Ref );
   var Отправитель, Получатель;

   if( тип == "Аванс" )
      ФИОПолучено = GetFullNameEmployee( Session.Emp_Ref );
      ФИОВыдано   = "";
      TypeOper    = Аванс;
      Отправитель = "Кладовая";
      Получатель  = "ОП " + {ExRegNumber};
   elif( тип == "Возврат" )
      ФИОПолучено = "";
      ФИОВыдано   = GetFullNameEmployee( Session.Emp_Ref );
      TypeOper    = Инкассация;
      Отправитель = "ОП " + {ExRegNumber};
      Получатель  = "Кладовая";
   else/*Передача*/
      ФИОПолучено = GetFullNameEmployee( avanceop.ToEmp  );
      ФИОВыдано   = GetFullNameEmployee( Session.Emp_Ref );
      TypeOper    = Передача;
      Отправитель = Получатель = "";
   end;
   ПолучитьСчетаКонтировкиВнебалансовыхОпераций( TypeOper, TypeValueCurr, CodIntValueCurr, ДебетСчет, КредитСчет );
   СокращенныйОрдерНаПересылкуА( CurrMoney, ИнфоСтрока, Получатель, Отправитель, СтрокаНоминалов, "", СуммаПрописью, ФИОПолучено, ФИОВыдано  , ДебетСчет, КредитСчет );
   СокращенныйОрдерНаПересылкуБ( CurrMoney, ИнфоСтрока, Отправитель, Получатель, СтрокаНоминалов, "", СуммаПрописью, ФИОВыдано,   ФИОПолучено, ДебетСчет, КредитСчет );
   РазделительОрдеровНаПересылку();
END;

/* Если есть неплатеж. валюта - печатает два ордера RA  4-06-99 */
MACRO Ордер61ПоИнВалюте( Curr_Ref, Sum, Тип )
   if( GetTurnIDOper( Operat.ID, UNSET_CHAR ) )
      Ордер61ПоВалюте( Curr_Ref, Abs( TurnIdOper.Sum ), Тип, "" );
   end;
   if( GetTurnIDOper( Operat.ID, SET_CHAR ) )
      Ордер61ПоВалюте( Curr_Ref, Abs( TurnIdOper.Sum ), Тип, " (неплатеж.)" );
   end;
END;

MACRO Ордер61ПоРублям( Sum, Тип )
   var CurrMoney     = "НАЛИЧНЫЕ РУБЛИ";
   var SumMoney      = Trim(String( Sum ));
   var СуммаПрописью = CurToStrAlt( Sum , null, null, {NationalCur} );
   var ИнфоСтрока    = "Сумма (цифрами): " + SumMoney;
   var СтрокаНоминалов  = ПолучитьСтрокуНоминалов( Operat );
   var ФИОПолучено, ФИОВыдано;
   var ДебетСчет, КредитСчет, TypeOper;
   var CodIntValueRub = GetCurrRetail( {NationalCur} );
   var Отправитель, Получатель;

   if( тип == "Аванс" )
      ФИОПолучено = GetFullNameEmployee( Session.Emp_Ref );
      ФИОВыдано   = "";
      TypeOper    = Аванс;
      Отправитель = "Кладовая";
      Получатель  = "ОП " + {ExRegNumber};
   elif( тип == "Возврат" )
      ФИОПолучено = "";
      ФИОВыдано   = GetFullNameEmployee( Session.Emp_Ref );
      TypeOper    = Инкассация;
      Отправитель = "ОП " + {ExRegNumber};
      Получатель  = "Кладовая";
   else/*Передача*/
      ФИОПолучено = GetFullNameEmployee( avanceop.ToEmp  );
      ФИОВыдано   = GetFullNameEmployee( Session.Emp_Ref );
      TypeOper    = Передача;
      Отправитель = Получатель = "";
   end;
   ПолучитьСчетаКонтировкиВнебалансовыхОпераций( TypeOper, TypeValueRub, CodIntValueRub, ДебетСчет, КредитСчет );
   СокращенныйОрдерНаПересылкуА( CurrMoney, ИнфоСтрока, Получатель, Отправитель, СтрокаНоминалов, "", СуммаПрописью, ФИОПолучено, ФИОВыдано  , ДебетСчет, КредитСчет );
   СокращенныйОрдерНаПересылкуБ( CurrMoney, ИнфоСтрока, Отправитель, Получатель, СтрокаНоминалов, "", СуммаПрописью, ФИОВыдано,   ФИОПолучено, ДебетСчет, КредитСчет );
   РазделительОрдеровНаПересылку();
END;


MACRO Ордер61ПоПлатДокументам( Curr_Ref, Sum, Тип )
  /* наложение структуры avanceop на поле Oper.ExtInfo */
   SetRecordAddr( avanceop, Operat, 0, FldOffset( Operat,"ExtInfo"),True);
   var CurrPayDoc    = /*Trim(GetNamePayDoc(avanceop.PayDoc_Ref)) + " " + GetNameCurr( Curr_Ref );*/
                       GetNameValForm( avanceop.PayDoc_Ref ); /* 24.08.2000 VS */
/*   var SumPayDoc     = Trim(String( Sum ));*/
   var СуммаПрописью = "";/*CurToStrAlt( Sum, null, null, Curr_Ref );*/
   var КурсЦБ        = "";/*Trim(String(Operat.CBRate));*/
   var СуммаВрублях  = "";/*Trim(String(Operat.CBRate * Sum/100.:14:2));*/
   var СтрокаСерийНомеров = ПолучитьСтрокуСерийНомеровПД_2( Operat.ID, СуммаПрописью );
   var SumPayDoc          = CurToStrAlt( СуммаПрописью*100, null, null, {NationalCur} );
   var ИнфоСтрока         = СтрокаСумм( SumPayDoc, КурсЦБ, СуммаВрублях );
   var СтрокаНоминалов    = ПолучитьСтрокуНоминалов( Operat );
   var ФИОПолучено, ФИОВыдано;
   var ДебетСчет, КредитСчет, TypeOper;
   var Отправитель, Получатель;

   if( тип == "Аванс" )
      ФИОПолучено = GetFullNameEmployee( Session.Emp_Ref );
      ФИОВыдано   = "";
      TypeOper    = Аванс;
      Отправитель = "Кладовая";
      Получатель  = "ОП " + {ExRegNumber};
   elif( тип == "Возврат" )
      ФИОПолучено = "";
      ФИОВыдано   = GetFullNameEmployee( Session.Emp_Ref );
      TypeOper    = Инкассация;
      Отправитель = "ОП " + {ExRegNumber};
      Получатель  = "Кладовая";
   else/*Передача*/
      ФИОПолучено = GetFullNameEmployee( avanceop.ToEmp  );
      ФИОВыдано   = GetFullNameEmployee( Session.Emp_Ref );
      TypeOper    = Передача;
      Отправитель = Получатель = "";
   end;
   ПолучитьСчетаКонтировкиВнебалансовыхОпераций( TypeOper, TypeValuePD, avanceop.PayDoc_Ref, ДебетСчет, КредитСчет );
   СуммаПрописью = СуммаПрописью + " шт.";
   СокращенныйОрдерНаПересылкуА( CurrPayDoc, ИнфоСтрока, Получатель, Отправитель, СтрокаНоминалов, СтрокаСерийНомеров, СуммаПрописью, ФИОПолучено, ФИОВыдано  , ДебетСчет, КредитСчет );
   СокращенныйОрдерНаПересылкуБ( CurrPayDoc, ИнфоСтрока, Отправитель, Получатель, СтрокаНоминалов, СтрокаСерийНомеров, СуммаПрописью, ФИОВыдано,   ФИОПолучено, ДебетСчет, КредитСчет );
   РазделительОрдеровНаПересылку();
END;

/****************************************************************************/
/* Процедура выводит ордера пересылки                                       */
/****************************************************************************/
MACRO ОрдераНаАвансы_Возвраты( Тип )

  var СтрокаАвансСправок   = "";
  var AmountAvanceSprv     = 0;
  var СтрокаВозвратСправок = "";
  var AmountReturnSprv     = 0;
  var TypeOperation, Loop;
  var Seria, StartNum, EndNum, Amount, ИнфоСтрока;

  if( Тип == "Аванс" )
     TypeOperation = CDF_OperAvance;
  elif( Тип == "Возврат" )
     TypeOperation = CDF_OperCashOut;
  elif( Тип == "Передача" )
     TypeOperation = CDF_OperTransfer;
  end;

  Operat.Emp_Ref        = Session.Emp_Ref;
  Operat.Sess_Ref       = Session.Number;
  Operat.Oper_Ref       = TypeOperation;    /* вид операции */
  Operat.State_Ref      = CDF_OperCompleted;/* завершена    */
  Operat.Rate_Ref       = 0;
  Operat.IncomeCurr_Ref = ANY_CURR;
  Operat.OutCurr_Ref    = ANY_CURR;

  Loop = GetGE( Operat );
  While( (Loop == True) AND
         (Operat.Emp_Ref        == Session.Emp_Ref)         AND
         (Operat.Sess_Ref       == Session.Number)          AND
         (Operat.Oper_Ref       == TypeOperation)           AND
         ( (Operat.State_Ref    == CDF_OperCompleted) OR
           (Operat.State_Ref    == CDF_OperInReestr)   )    AND
         (Operat.Rate_Ref       == 0)
       )

      if( TypeOperation == CDF_OperAvance )
         /* получение наличности */
         if( (Operat.IncomeTrs_Ref == TrsCash) AND (Operat.IncomeAmount) AND
             ((ТипОрдеровФ61 == "Наличность") OR (ТипОрдеровФ61 == "Все"))
           )
            if( Operat.IncomeCurr_Ref != {NationalCur} )
               Ордер61ПоИнВалюте( Operat.IncomeCurr_Ref, Operat.IncomeAmount, Тип );
            else
               Ордер61ПоРублям( Operat.IncomeAmount, Тип )
            end;

         /* получение платежных документов */
         elif( ((Operat.IncomeTrs_Ref == TrsCheck      ) OR
                (Operat.IncomeTrs_Ref == TrsAccreditive) OR
                (Operat.IncomeTrs_Ref == TrsValForm/*TrsPayDoc*/ )) /* 24.08.2000 VS Поставил бланки ПД */
                 AND (Operat.IncomeAmount) AND
               ((ТипОрдеровФ61 == "ПлатДокументы") OR (ТипОрдеровФ61 == "Все"))
             )
            Ордер61ПоПлатДокументам( Operat.IncomeCurr_Ref, Operat.IncomeAmount, Тип )

         /* получение справок БСО ф0406007 */
         elif( (Operat.IncomeTrs_Ref  == 0       ) AND
               (Operat.OutCurr_Ref    == ANY_CURR) AND
               (Operat.IncomeCurr_Ref == ANY_CURR) AND
               ((ТипОрдеровФ61 == "СправкиБСО") OR (ТипОрдеровФ61 == "Все"))
             )
            /* наложение структуры avanceop на поле Oper.ExtInfo */
            SetRecordAddr( avanceop, Operat, 0, FldOffset( Operat,"ExtInfo"),True);
            Seria      = avanceop.SprStartSeria;
            StartNum   = avanceop.SprStartNumber;
            EndNum     = AddZero( (Int(avanceop.SprStartNumber) + Int(avanceop.SprAmountIn) - 1) , 7 );
            Amount = Trim( String(avanceop.SprAmountIn) );
            ИнфоСтрока =  Seria + "  " + StartNum + "-" + EndNum + "  :" + Amount;
            СтрокаАвансСправок = СтрокаАвансСправок + ИнфоСтрока +
                                 MkStr(" ", 74 - StrLen(ИнфоСтрока) );
            AmountAvanceSprv   = AmountAvanceSprv + avanceop.SprAmountIn;

            /*Ордер61ПоСправкамБСО( ИнфоСтрока, Amount );*/
         end;

      elif( (TypeOperation == CDF_OperCashOut) OR (TypeOperation == CDF_OperTransfer) )
         /* возврат наличности */
         if( (Operat.OutTrs_Ref == TrsCash) AND (Operat.OutAmount) AND
             ((ТипОрдеровФ61 == "Наличность") OR (ТипОрдеровФ61 == "Все"))
           )
            if( Operat.OutCurr_Ref != {NationalCur} )
               Ордер61ПоИнВалюте( Operat.OutCurr_Ref, Operat.OutAmount, Тип );
            else
               Ордер61ПоРублям( Operat.OutAmount, Тип )
            end;

         /* возврат платежных документов */
         elif( ((Operat.OutTrs_Ref == TrsCheck      ) OR
                (Operat.OutTrs_Ref == TrsAccreditive) OR
                (Operat.OutTrs_Ref == TrsValForm/*TrsPayDoc*/ )) /* 24.08.2000 VS Поставил бланки ПД */
                   AND (Operat.OutAmount) AND
                ((ТипОрдеровФ61 == "ПлатДокументы") OR (ТипОрдеровФ61 == "Все"))
                )
            Ордер61ПоПлатДокументам( Operat.OutCurr_Ref, Operat.OutAmount, Тип )

         /* возврат справок БСО ф0406007 */
         elif( (Operat.OutTrs_Ref     == 0       ) AND
               (Operat.OutCurr_Ref    == ANY_CURR) AND
               (Operat.IncomeCurr_Ref == ANY_CURR) AND
               ((ТипОрдеровФ61 == "СправкиБСО") OR (ТипОрдеровФ61 == "Все"))
             )
            /* наложение структуры avanceop на поле Oper.ExtInfo */
            SetRecordAddr( avanceop, Operat, 0, FldOffset( Operat,"ExtInfo"),True);
            Seria      = avanceop.SprStartSeria;
            StartNum   = avanceop.SprStartNumber;
            EndNum     = AddZero( (Int(avanceop.SprStartNumber) + Int(avanceop.SprAmountIn) - 1) , 7 );
            Amount = Trim( String(avanceop.SprAmountIn) );
            ИнфоСтрока = Seria + "  " + StartNum + "-" + EndNum + "  :" + Amount;
            СтрокаВозвратСправок = СтрокаВозвратСправок + ИнфоСтрока +
                                   MkStr(" ",  74 - StrLen(ИнфоСтрока) );
            AmountReturnSprv     = AmountReturnSprv + avanceop.SprAmountIn;
            /*Ордер61ПоСправкамБСО( Тип );*/
         end;
      end;
     Loop = Next( Operat );
  end;

  if( AmountAvanceSprv )
     СтрокаАвансСправок = СтрокаАвансСправок + "Итого                :" + string(AmountAvanceSprv) +
                          MkStr("  ", 74 - StrLen(СтрокаАвансСправок) );
     Ордер61ПоСправкамБСО( "Аванс", СтрокаАвансСправок, AmountAvanceSprv );
  end;
  if( AmountReturnSprv )
     СтрокаВозвратСправок = СтрокаВозвратСправок + "Итого                :" + string(AmountReturnSprv) +
                          MkStr("  ", 74 - StrLen(СтрокаВозвратСправок) );
     Ордер61ПоСправкамБСО( "Передача", СтрокаВозвратСправок, AmountReturnSprv );
  end;

END;
/****************************************************************************/
/* Процедура печатает ордера на пересылку (аванс или возврат)               */
/****************************************************************************/
MACRO ОрдераНаПересылку()

  ARRAY Order61Array2;
  Order61Array2(0) = "Аванс";
  Order61Array2(1) = "Инкассация";
  Order61Array2(2) = "Все";

  var i = Menu( Order61Array2,
            " ~Enter~ Выбор направления ордеров ф.61  ~Esc~ Выход",
            "Ордера по ф.61"
              );

  if( i == 0 )   /* аванс */
     MakeReportFile( ReportFile, ФайлОтчетФ61 + "0"  );
     PrintDate_n_Time();
     ОрдераНаАвансы_Возвраты( "Аванс"    );
  elif( i == 1 ) /* инкассация */
     MakeReportFile( ReportFile, ФайлОтчетФ61 + "1"  );
     PrintDate_n_Time();
     ОрдераНаАвансы_Возвраты( "Возврат"  );
     ОрдераНаАвансы_Возвраты( "Передача" );
  elif( i == 2 ) /* все */
     MakeReportFile( ReportFile, ФайлОтчетФ61 + "2"  );
     PrintDate_n_Time();
     ОрдераНаАвансы_Возвраты( "Аванс"    );
     ОрдераНаАвансы_Возвраты( "Возврат"  );
     ОрдераНаАвансы_Возвраты( "Передача" );
  end;

  /* смотрим файл отчета */
  SetOutPut( 0 );
  ViewFile( ReportFile );
  Close( ReportFile );
END;

/****************************************************************************/
/* Процедура формирует ордера по ф.61 в разрезе, выбранном из меню          */
/****************************************************************************/
MACRO Ордер61()

  ARRAY Order61Array;
  Order61Array(0)  = "По наличности";
  Order61Array(1)  = "По платежным документам";
  Order61Array(2)  = "По справкам ф.0406007";
  Order61Array(3)  = "Все ордера";

  var Loop = True, i;
  While( Loop )
     i = Menu( Order61Array,
               " ~Enter~ Выбор вида ордеров ф.61  ~Esc~ Выход",
               "Ордера по ф.61"
             );

     if( i == 0 )
        ТипОрдеровФ61 = "Наличность";
        ФайлОтчетФ61  = "sbrf61n";
     elif( i == 1 )
        ТипОрдеровФ61 = "ПлатДокументы";
        ФайлОтчетФ61  = "sbrf61p";
     elif( i == 2 )
        ТипОрдеровФ61 = "СправкиБСО";
        ФайлОтчетФ61  = "sbrf61s";
     elif( i == 3 )
        ТипОрдеровФ61 = "Все";
        ФайлОтчетФ61  = "sbrf61a";
     else
        Loop = False;
     end;
     if( Loop != False )
        ОрдераНаПересылку();
     end;
  end;
END;
