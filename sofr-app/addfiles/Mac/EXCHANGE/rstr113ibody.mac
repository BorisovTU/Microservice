/*
$Name:             rstr113ibody.mac
$Module:           Универсальный фронт-офис
$Description:      Макрос функции печати реестра 113-И, используемый для вызова из других макросов
*/
/*-RSL---------------------------------------- R-Style Software Lab --
               Обменный пункт

               Печать реестра по 113-И

               RA 17-06-04 Создан
*/

import ExchangeInter, CommonInter, DeskInter, RSD;
import  define, or_rep_h, rstrfunc, cur_rate, issrvdoc, sqlconv, FMInter, DocBunch;
import sb_err;

var ОТЧЕТ_В_EXCEL = false;
var ОТЧЕТ;

private var   reestr     = TBFile     ( "reestr_2.dbt", "R", 1, null, "sbbank.def" );
private var   exoperat   = TBFile     ( "exoperat.dbt", "R", 1, null, "exchange.def" );
private var   exoperat2  = TBFile     ( "exoperat.dbt", "R", 1, null, "exchange.def" );
private var   sbdepdoc   = TBFile     ( "sbdepdoc.dbt", "R", 3, null, "sbbank.def" );
private var   pay_doc    = TBFile     ( "pay_doc.dbt" , "R", 2, null, "sbbank.def" );
private var   rmto_op    = TBFile     ( "rmto_op.dbt",  "R", 0, null, "sbbank.def" );
private var   rmto_trans = TBFile     ( "rmto_trans.dbt","R", 0, null, "sbbank.def" );
private var   ratekind   = TBFile     ( "ratekind.dbt", "R", 0, null, "exchange.def" );
private var   ratechng   = TBFile     ( "ratechng.dbt", "R", 0, null, "exchange.def" );
//private var   t_reestr   = TBFile     ( "reestr.dbt"  , "AWC+", 4, "reestr.tmp", "exchange.def" );
private var   cashVal    = TBFile     ( "sb_casvl.dbt", "R", 1, null, "sbbank.def" );
private var   currency   = TBFile     ( "currency.dbt", "R", 1, null, "sbbank.def" );
private var   sb_casln   = TBFile     ( "sb_casln.dbt", "R", 1, null, "sbbank.def" );
private var   person     = TBFile     ( "person.dbt",   "R", 0, null, "bank.def" );
private var   paydocop   = TRecHandler( "paydocop.str", "exchange.def" );
private var   paydocop2  = TRecHandler( "paydocop.str", "exchange.def" );
private var   paydoc_cnv = TRecHandler( "pay_doc.cnv" , "sbbank.def"   );
private var   paydoc_com = TRecHandler( "pay_doc.com" , "sbbank.def"   );
private var   paydoc_n36 = TRecHandler( "pay_doc.n36" , "sbbank.def"   );
private var   ClientList = TClientList;
private var   ddep       = TRecHandler( "i_dp_dep.rec" );
private var   party      = TRecHandler( "i_party.rec" );

private file retop ( "ret_op.dbt" ) key 0;

private const Branch     = NumFNCash( );
private const BankStandart = GetBankStandart( );

private var ControlSum = FM_GetMinSum(); 
private var ControlSumFullIdent = $0L; 
private const ControlSum15 = $15000;

private const CLNT_IDENT_STANDART = 0;     /* стандартная идентификация   */
private const CLNT_IDENT_FREE     = 1;     /* добровольная идентификация  */
private const CLNT_IDENT_TOTAL    = 2;     /* стандартная идентификация   */

/* C-шные константы */
private var {curdate},
            {Name_Bank},
            {Post_Addr},
            {Post_Addr_Branch};

private var Oper_Brigade = GetOperBrigade();

private const
  SB_DEPOSIT        =   1, // Документ вкладной операции
  SB_PAYDOC         = 200, // Документ прочие операции
  SB_MONEY_TRANSFER = 210, // Документ срочного перевода
  SB_EXCHANGE       = 250, // Реестр по обменному пункту
  SB_EXCHNDOC       = 260; // Документ валютнообменной операции

private const CO_Comission  = 21; /* Кассовая операция - Комиссия  */
private const CO_Conversion = 31; /* Кассовая операция - Конверсия */

/* Типы ресурсов */
private const
  TYPERES_CURRENCY    =   1, /* Деньги (из справочника валют)           */
  TYPERES_VALFORM     =   2, /* Ценные бланки                           */
  TYPERES_VALUABLES   =   3, /* Др. ценности (из справочника ценностей) */
  TYPERES_PAYDOC      =   4, /* Платежные документы ОП                  */
  TYPERES_CARD        =  10, /* Пластиковые карточки                    */
  TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ             */

private const
   ACCOUTN_OWNER       = StrFor(  0 ),    /* Владелец счета      */
   ACCOUTN_PROXY       = StrFor(  1 ),    /* Доверенное лицо     */
   ACCOUTN_HEIR        = StrFor(  2 ),    /* Наследник - выдачи  */
   ANONIMUS_CLIENT     = StrFor(  3 ),    /* Анонимный клиент    */
   ACCOUNT_IMPORTER    = StrFor(  4 ),    /* Вноситель           */
   ACCOUNT_TUTOR       = StrFor(  5 ),    /* Опекун              */
   ACCOUNT_OBLIGATOR   = StrFor(  6 ),    /* Под обязательство   */
   ACCOUTN_HEIR_PAY    = StrFor(  7 ),    /* Наследник - выплаты */
   ACCOUNT_UNDER_TRIAL = StrFor(  8 ),    /* По суду             */
   ACCOUNT_PARENT      = StrFor(  9 ),    /* Родитель            */
   ACCOUNT_RETRAST     = StrFor( 10 );    /* Передоверенность    */

private const
   PTCK_BANKREGNUM = 13;  /* Вид субъекта: регистр. номер нашего банка. */

private var
  codNumBank,
  regNumBank,
  ordNumBank,
  reestrNumber,
  day       ,
  month     ,
  year      ,
  rateString,
  _Code113i ,
  _time     ,
  _rate     ,
  _incomeISO,
  _incomeSum,
  _outISO   ,
  _outSum   ,
  _isCard   ,
  _pdNum    ,
  _pdISO    ,
  _pdSum    ,
  _commISO  ,
  _commSum  ,
  _account  ,
  _warrant  ,
  _country  ,
  _client   ,
  _document ,
  _address  ,
  checkSum;

private var
  typePDSum = 0;

private var
  _oper = {exoper};

private var
  _dateReestr = {curdate};

private array
  AComplexCode,
  ACodeISO,
  ACode113,
  ARate,
  AIncomeSum,
  AOutSum,
  AIncomeСheque,
  AOutСheque;

private const
  CodeTotal = "ЯЯЯ";

private var existLine = FALSE;  // Признак непустого Реестра.

private var flagEO = false;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefFIOOper

  var nameOper = "";
  var name1 = "";
  var name2 = "";
  var name3 = "";
  var i;
  var cmd = RsdCommand( "select t_name from dperson_dbt where t_oper = ?" );
  var rs;
                                          
  cmd.AddParam( "t_oper", RSDBP_IN, {oper} );

  rs  = RsdRecordSet(cmd );

  if ( rs.MoveNext )
    nameOper = Trim( rs.Value( "t_name" ) );
    name1 = nameOper;

    i = Index( nameOper, " " );
    if ( i > 1 )
      name1 = Trim( SubStr( nameOper, 1, i - 1 ) );
      nameOper = Trim( SubStr( nameOper, i + 1 ) );
      name2 = nameOper;

      i = Index( nameOper, " " );
      if ( i > 1 )
        name2 = Trim( SubStr( nameOper, 1, i - 1 ) );
        name3 = SubStr( nameOper, i + 1 );
      end;
    end;

  end;

  nameOper = name1;

  if ( StrLen( name2 ) > 0 )
    nameOper = nameOper + " " + SubStr( name2, 1, 1 ) + ".";

    if ( StrLen( name3 ) > 0 )
      nameOper = nameOper + SubStr( name3, 1, 1 ) + ".";
    end;
  end;


  return nameOper;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro DefCheckSumForControl ( sumCur, codCur, dateSum )

  var outSum = $0L;
  var cbRate = 0.0;

  if ( ( ValType( sumCur ) != V_STRING ) and ( sumCur > 0 ) )
    if ( codCur == 0 )
      outSum = sumCur;
    elif ( codCur > 0 )
      if ( GetAllCurrRate( dateSum, codCur, cbRate ) )
        outSum = Money( sumCur * cbRate );
      else
        outSum = sumCur;
      end;
    end;
  end;

  return outSum;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO DefTypePerson( )

  person.clear( );
  person.rec.Oper = _oper;
  if( person.getEQ( ) )
    return person.rec.cTypePerson;
  else
    return "";
  end;

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO FindCashValueIntCode( Type, TypeValue, CodIntValue )

  cashVal.clear();
  cashVal.rec.TypeValue   = TypeValue;
  cashVal.rec.CodIntValue = CodIntValue;
  cashVal.rec.Type        = Type;
  if( cashVal.getEQ( ) )
    return cashVal.rec.RefValue;
  else
    return 0;
  end;

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO GetCurCode( ExternalCode )

  currency.clear();
  currency.rec.ExternalCode = ExternalCode;
  if( currency.getEQ( ) )
    return currency.rec.Code_Currency;
  else
    return ANY_CURR;
  end;

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO getRateKind( id )

  ratechng.clear();
  ratechng.rec.id = id;
  if( ratechng.getEQ( ) )
    return ratechng.rec.Kind_Ref;
  end;

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO nameRateKind( id )

  ratekind.clear();
  ratekind.rec.id = id;
  if( ratekind.getEQ( ) )
    return ratekind.rec.name;
  end;

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO countryNumCode( CodeLat3 )

  var cmd;

  if ( CodeLat3 == "" )
    return "000";
  end;
 
  if ( StrLen( CodeLat3 ) >= 3 )
    cmd = RsdCommand( "select country.t_CodeNum3 as CodeNum3 "
                          "from dcountry_dbt country "
                          "where country.t_CodeLat3 = ?" );
  else
    cmd = RsdCommand( "select country.t_CodeNum3 as CodeNum3 "
                          "from dcountry_dbt country "
                          "where country.t_CodeLat2 = ?" );
  end;

  cmd.AddParam( "", RSDBP_IN, CodeLat3 );

  cmd.NullConversion = true;
  cmd.execute;

  var rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    return rs.value( "CodeNum3" );
  end;

  return "000";

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO InitReestr( Date_ )

  var rateKind = 0;

  reestr.clear();
  reestr.rec.Branch   = Branch;
  reestr.rec.CurDate  = _dateReestr;
  reestr.rec.Oper     = _oper;
  reestr.rec.Date     = Date_;
  reestr.rec.Rate_Ref = 0;
  reestr.rec.Number   = 0;

  if( reestr.getGE()
    and ( reestr.rec.Branch   == Branch      )
    and ( reestr.rec.CurDate  == _dateReestr )
    and ( reestr.rec.Oper     == _oper       )
    )
    SetParm( 0, reestr.rec.Date     );
    DateSplit( reestr.rec.curDate/*Date*/, day, month, year );

    return true;
  else
    return false;
  end;

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO ClearData( )

  _time      = "";
  _rate      = "";
  _incomeISO = "";
  _incomeSum = "";
  _outISO    = "";
  _outSum    = "";
  _isCard    = "";
  _pdNum     = "";
  _pdISO     = "";
  _pdSum     = "";
  _commISO   = "";
  _commSum   = "";
  _account   = "";
  _warrant   = "";
  _country   = "";
  _client    = "";
  _document  = "";
  _address   = "";

  typePDSum = 0;

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO FindComissionDoc( )

  var mainKind = 0;
  var mainKey  = "";
  var stat;
  var wasFind = false;
  var isConversion = false;

  if ( reestr.rec.ApplicationKind == SB_PAYDOC )
    if ( pay_doc.rec.GroupOpert == CO_Conversion )
      isConversion = true;
    end;
  end;

  sb_casln.KeyNum = 1;
  sb_casln.Clear;
  sb_casln.rec.ApplicationKind2 = reestr.rec.ApplicationKind;
  sb_casln.rec.ApplicationKey2  = reestr.rec.ApplicationKey;

  if ( sb_casln.GetEQ )
    mainKind = sb_casln.rec.ApplicationKind1;
    mainKey  = sb_casln.rec.ApplicationKey1;
  end;

  if ( mainKey != "" )
    sb_casln.KeyNum = 0;
    sb_casln.Clear;
    sb_casln.rec.ApplicationKind1 = mainKind;
    sb_casln.rec.ApplicationKey1  = mainKey;

    sb_casln.addFilter( "t.t_ApplicationKind1 = " + String( mainKind ) + " and " +
                        "t.t_ApplicationKey1 = " + GetSQLString( mainKey )        );

    stat = sb_casln.GetGE;

    while ( stat )

      if ( ( sb_casln.rec.ApplicationKind1 == mainKind ) and
           ( sb_casln.rec.ApplicationKey1  == mainKey  )    )

        if ( ( sb_casln.rec.ApplicationKind2 == SB_PAYDOC ) and ( sb_casln.rec.RefValue2 == 0 ) )

          pay_doc.clear();
          pay_doc.keynum( 2 );

          pay_doc.rec.iApplicationKind = sb_casln.rec.ApplicationKind2;
          pay_doc.rec.ApplicationKey   = sb_casln.rec.ApplicationKey2;

          if ( pay_doc.getEQ( ) )
            if ( pay_doc.rec.GroupOpert == CO_Comission )
              paydoc_com.SetRecordAddr( pay_doc, 0, 0, true );
              if ( isConversion )
                if ( ( paydoc_com.rec.DepNumOper == CO_Conversion ) and ( paydoc_com.rec.Kind == "" ) )
                  wasFind = true;
                  stat = false;
                end;
              else
                wasFind = true;
                stat = false;
              end;
            end;
          end;
        end;
      else
        stat = false;
      end;

      if ( stat )
        stat = sb_casln.Next;
      end;
    end;

    sb_casln.dropFilter;
  end;

  return wasFind;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro IsFirstExOperat( )

  var cmd = RsdCommand( "select 0 from dsb_casln_dbt c1, dsb_casln_dbt c2 "
                        "where c1.t_applicationKind1 = c2.t_applicationKind1 "
                        "  and c1.t_applicationKey1 = c2.t_applicationKey1 "
                        "  and c1.t_numLinkDoc < c2.t_numLinkDoc "
                        "  and c1.t_applicationKind2 = c2.t_applicationKind2 "
                        "  and c1.t_refValue2 = 0 "
                        "  and c2.t_refValue2 = 0 "
                        "  and c2.t_applicationKind2 = ? "
                        "  and c2.t_applicationKey2 = ? " );

  cmd.addParam( "p_appKind", RsdBp_In );
  cmd.value( "p_appKind" ) = exoperat.rec.ApplicationKind;
  cmd.addParam( "p_appKey", RsdBp_In );
  cmd.value( "p_appKey" ) = exoperat.rec.ApplicationKey;

  cmd.execute;
  var rs = RsdRecordset( cmd );

  return not rs.moveNext;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro CalcSumForPD

  var cmd, rs;
  var applKind, applKey;

  exoperat.rec.IncomeAmount = $0L;
  exoperat.rec.OutAmount = $0L;
  paydocop.rec.Amount = 0;

  paydocop2.SetRecordAddr( exoperat2, 0, FldOffset( exoperat2, "ExtInfo" ), true );

  cmd = RsdCommand( "select sb_casln.t_ApplicationKind2 as applKind, sb_casln.t_ApplicationKey2 as applKey " +
                    "from dsb_casln_dbt sb_casln, dsb_casln_dbt sb_caslnm " +
                    "where sb_casln.t_ApplicationKind1 = sb_caslnm.t_ApplicationKind1 " +
                      "and sb_casln.t_ApplicationKey1 = sb_caslnm.t_ApplicationKey1 " +
                      "and sb_casln.t_ApplicationKind2 = 260 " +
                      "and sb_casln.t_RefValue2 = 0 " +
                      "and sb_caslnm.t_ApplicationKind2 = :applKind " +
                      "and sb_caslnm.t_ApplicationKey2 = :applKey " +
                      "and sb_caslnm.t_RefValue2 = 0" );


  cmd.addParam( "applKind", RSDBP_IN, exoperat.rec.ApplicationKind );
  cmd.addParam( "applKey", RSDBP_IN, exoperat.rec.ApplicationKey );

  cmd.execute;

  rs = RsdRecordset( cmd );

  while ( rs.moveNext )
    applKind = rs.value( "applKind" );
    applKey = rs.value( "applKey" );

    exoperat2.Clear( );
    exoperat2.rec.ApplicationKind = applKind;
    exoperat2.rec.ApplicationKey  = applKey;

    if ( exoperat2.GetEQ( ) and ( exoperat2.rec.Kind_Oper == exoperat.rec.Kind_Oper ) and 
                                ( exoperat2.rec.IncomeRefVal == exoperat.rec.IncomeRefVal ) and
                                ( exoperat2.rec.OutRefVal == exoperat.rec.OutRefVal )          )
      exoperat.rec.IncomeAmount = exoperat.rec.IncomeAmount + exoperat2.rec.IncomeAmount;
      exoperat.rec.OutAmount = exoperat.rec.OutAmount + exoperat2.rec.OutAmount;

      if ( ( exoperat2.rec.IncomeTypeValue == TYPERES_PAYDOC ) or ( exoperat2.rec.OutTypeValue == TYPERES_PAYDOC ) )
        paydocop.rec.Amount = paydocop.rec.Amount + paydocop2.rec.Amount;
      end;
    end;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro IsDepDocForMonTrans

  var stat = false;
  var cmd;
  var ownPsCode = "";

  sb_casln.KeyNum = 1;
  sb_casln.Clear;
  sb_casln.rec.ApplicationKind2 = sbdepdoc.rec.iApplicationKind;
  sb_casln.rec.ApplicationKey2  = sbdepdoc.rec.ApplicationKey;

  if ( sb_casln.GetEQ )

    if ( sb_casln.rec.ApplicationKind1 == SB_MONEY_TRANSFER ) // Денежные переводы
       
      cmd = RsdCommand( "select rmtr_ps.t_psCode as psCode "
                        "from drmtr_ps_dbt rmtr_ps, "
                             "drmto_op_dbt rmto_op, "
                             "drmto_trans_dbt rmto_trans, "
                             "dsb_casln_dbt sb_casln "
                        "where sb_casln.t_ApplicationKind1 = ? "
                          "and sb_casln.t_ApplicationKey1 = ? "
                          "and sb_casln.t_ApplicationKind2 = 210 "
                          "and rmto_op.t_opApplicationKind = sb_casln.t_ApplicationKind2 "
                          "and rmto_op.t_opApplicationKey = sb_casln.t_ApplicationKey2 "
                          "and rmto_trans.t_transId = rmto_op.t_transId "
                          "and rmto_trans.t_psId = rmto_trans.t_psId" );
          
      cmd.AddParam( "", RSDBP_IN, sb_casln.rec.ApplicationKind1 );
      cmd.AddParam( "", RSDBP_IN, sb_casln.rec.ApplicationKey1 );

      cmd.execute;

      var rs = RsdRecordset( cmd );

      if ( rs.moveNext )
        ownPsCode = rs.value( "psCode" );
      end;

      if ( Index( StrUpr( sbdepdoc.rec.Type_Account ), "БОС" ) > 0 )
        stat = true;
      elif ( ownPsCode != "" )
        if ( Index( StrUpr( sbdepdoc.rec.Type_Account ), ownPsCode ) > 0 )
          stat = true;
        end;
      end;
    end;
  end;

  return stat;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro IsExOperatWithPD
                                
  if ( ( exoperat.rec.Kind_Oper ==  3 ) or
       ( exoperat.rec.Kind_Oper ==  4 ) or
       ( exoperat.rec.Kind_Oper == 20 ) or
       ( exoperat.rec.Kind_Oper == 21 ) or
       ( exoperat.rec.Kind_Oper == 38 ) or
       ( exoperat.rec.Kind_Oper == 39 ) or
       ( exoperat.rec.Kind_Oper == 40 ) or
       ( exoperat.rec.Kind_Oper == 41 )   )
    return true;
  end;

  return false;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO FillData()

  var wasFindDoc = false;
  var codCur;
  var flagFirstExOperat;

  ClearData( );
  checkSum = $0L;

  if ( reestr.rec.ApplicationKind == SB_EXCHNDOC )
    // тут заполнить поля для валютнообменных операций
    exoperat.clear();
    exoperat.rec.ApplicationKind = reestr.rec.ApplicationKind;
    exoperat.rec.ApplicationKey  = reestr.rec.ApplicationKey;
    paydocop.SetRecordAddr( exoperat, 0, FldOffset( exoperat, "ExtInfo" ), true );

    if ( exoperat.getEQ( ) and ( exoperat.rec.Action < 2 ) and ( ( exoperat.rec.IsSuspended == "" ) or ( exoperat.rec.IsSuspended == "X" ) ) and ( exoperat.rec.NotConfirmed == "" ) )

      flagFirstExOperat = IsFirstExOperat( );

      if ( IsExOperatWithPD )
        if ( flagFirstExOperat )
          CalcSumForPD( );
        else
          return wasFindDoc;
        end;
      end;

      wasFindDoc = true;

      _time        = exoperat.rec.Time;
      _rate        = exoperat.rec.Kurs;
      if ( (exoperat.rec.IncomeTypeValue == TYPERES_CURRENCY) and exoperat.rec.IncomeAmount )
        codCur = FindCodCurByRefVal( exoperat.rec.IncomeRefVal );
        _incomeISO = GetCurrNumericalCode( codCur );
        _incomeSum = exoperat.rec.IncomeAmount;
         checkSum = DefCheckSumForControl( _incomeSum, codCur, exoperat.rec.CurDate );
      end;

      if ( (exoperat.rec.OutTypeValue == TYPERES_CURRENCY) and exoperat.rec.OutAmount )
        codCur = FindCodCurByRefVal( exoperat.rec.OutRefVal );
        _outISO = GetCurrNumericalCode( codCur );
        _outSum = exoperat.rec.OutAmount; 
         checkSum = DefCheckSumForControl( _outSum, codCur, exoperat.rec.CurDate );
      end;

      if( (exoperat.rec.Kind_Oper == CDF_OperCardTake) or
          (exoperat.rec.Kind_Oper == CDF_OperCardGive)  )
        _isCard    = "X";
      end;

      if ( (exoperat.rec.IncomeTypeValue == TYPERES_PAYDOC) and exoperat.rec.IncomeAmount )
        if ( ( exoperat.rec.Kind_Oper == CDF_OperPayDocIncasso       ) or
             ( exoperat.rec.Kind_Oper == CDF_OperPayDocExpert        ) or
             ( exoperat.rec.Kind_Oper == CDF_OperPayDocReturnIncasso ) or
             ( exoperat.rec.Kind_Oper == CDF_OperPayDocReturnExpert  )   )
          _pdNum   = 1; /* Структура incasso.str, в которой нет количества */
        else
          _pdNum   = paydocop.rec.Amount; /* Структура paydocop.str */
        end;
        codCur = FindCodCurByRefVal( exoperat.rec.IncomeRefVal );
        _pdISO = GetCurrNumericalCode( codCur );
        _pdSum = exoperat.rec.IncomeAmount;
        typePDSum  = 1;
        checkSum = DefCheckSumForControl( _pdSum, codCur, exoperat.rec.CurDate );
      elif ( (exoperat.rec.OutTypeValue == TYPERES_PAYDOC) and exoperat.rec.OutAmount )
        if ( ( exoperat.rec.Kind_Oper == CDF_OperPayDocIncasso       ) or
             ( exoperat.rec.Kind_Oper == CDF_OperPayDocExpert        ) or
             ( exoperat.rec.Kind_Oper == CDF_OperPayDocReturnIncasso ) or
             ( exoperat.rec.Kind_Oper == CDF_OperPayDocReturnExpert  )   )
          _pdNum = 1; /* Структура incasso.str, в которой нет количества */
        else
          _pdNum = paydocop.rec.Amount; /* Структура paydocop.str */
        end;
        codCur = FindCodCurByRefVal( exoperat.rec.OutRefVal );
        _pdISO = GetCurrNumericalCode( codCur );
        _pdSum = exoperat.rec.OutAmount;
        typePDSum = 2;
        checkSum = DefCheckSumForControl( _pdSum, codCur, exoperat.rec.CurDate );
      end;

      _account = ПолучитьВкладнойСчетДляВОО( exoperat.rec.ApplicationKind, exoperat.rec.ApplicationKey );

      if ( false )
        _warrant   = "X"; // операция по доверенности
      end;

      retop.ApplicationKind = exoperat.rec.ApplicationKind;
      retop.ApplicationKey  = exoperat.rec.ApplicationKey;
      if ( getEQ( retop )  AND  ( GetBitFlag( retop.Flags, 12 ) == 1 ) )
        _warrant   = "X";  // операция по доверенности
      end;

      if ( ( StrLen( Trim( exoperat.rec.Sname ) ) > 0 ) and ( Trim( exoperat.rec.Sname ) != "???" ) )
        if ( (exoperat.rec.ClntIdentType==CLNT_IDENT_TOTAL) OR (checkSum >= ControlSum15) )
           _country = countryNumCode( exoperat.rec.ClientCountry );
        end;

        if ( (checkSum >= ControlSum) OR (exoperat.rec.ClntIdentType!=CLNT_IDENT_STANDART) )
          ClientList.curRec.Clear( );

          if ( Trim( exoperat.rec.Sname ) != "???" )
            ClientList.curRec.rec.Name1 = exoperat.rec.Sname;
          end;
          if ( Trim( exoperat.rec.Name ) != "???" )
            ClientList.curRec.rec.Name2 = exoperat.rec.Name;
          end;
          if ( Trim( exoperat.rec.Pname ) != "???" )
            ClientList.curRec.rec.Name3 = exoperat.rec.Pname;
          end;

          ClientList.curRec.rec.PaperKind = exoperat.rec.ClientDocType;
          if ( ( StrLen( Trim( exoperat.rec.ClientDocSeria  ) ) != 0 ) and ( Trim( exoperat.rec.ClientDocSeria  ) != "XXX" ) )
            ClientList.curRec.rec.PaperSeries = exoperat.rec.ClientDocSeria;
          end;
          if ( ( StrLen( Trim( exoperat.rec.ClientDocNumber ) ) != 0 ) and ( Trim( exoperat.rec.ClientDocNumber ) != "XXX" ) )
            ClientList.curRec.rec.PaperNumber = exoperat.rec.ClientDocNumber;
          end;
          if ( ( StrLen( Trim( exoperat.rec.ClientDocInfo ) ) != 0 ) and ( Trim( exoperat.rec.ClientDocInfo ) != "XXX" ) )
            ClientList.curRec.rec.PaperIssuer = exoperat.rec.ClientDocInfo;
          end;

          _client   = ClientList.curRec.ConvertFioFull( );
          _address  = exoperat.rec.ClientAddress;
          if ( ( ClientList.curRec.rec.PaperSeries != "" ) or 
               ( ClientList.curRec.rec.PaperNumber != "" ) or 
               ( ClientList.curRec.rec.PaperIssuer != "" )   )
            _document = ClientList.curRec.MakeDocStr( );
          end;
          if ( exoperat.rec.ClntIdentType==CLNT_IDENT_FREE )
             _client = _client + "(*)";
             _address = _address + "(*)";
             _document = _document + "(*)";
          end;
        end;
      elif ( ( exoperat.rec.ClientCode != 0 ) and ClientList.GetRecord( exoperat.rec.ClientCode ) )
        if ( (exoperat.rec.ClntIdentType==CLNT_IDENT_TOTAL) OR (checkSum >= ControlSum15) )
          _country = countryNumCode( ClientList.curRec.rec.NRCountry/*Country*/ );
        end;

        if ( (checkSum >= ControlSum) OR (exoperat.rec.ClntIdentType!=CLNT_IDENT_STANDART) )
          _client   = ClientList.curRec.ConvertFioFull( );
          _document = ClientList.curRec.MakeDocStr( );
          _address  = ClientList.curRec.MakeFullStrAddress( );
          if ( exoperat.rec.ClntIdentType==CLNT_IDENT_FREE )
             _client = _client + "(*)";
             _address = _address + "(*)";
             _document = _document + "(*)";
          end;
        end;
      end;

      if ( BankStandart == 0 )
        if ( flagFirstExOperat and FindComissionDoc( ) )
          _commISO     = GetCurrNumericalCode( pay_doc.Rec.CodCur );
          _commSum     = pay_doc.Rec.InSum;
        end;
      end;

      if ( ( ( _incomeISO == _pdISO ) or ( _outISO == _pdISO ) ) and ( _pdISO != "" ) )
        _rate = "";
      end;

      if ( ( _incomeISO == _outISO ) and ( _outISO != "" ) )
        _rate = "";
      end;

      if ( ( ( _incomeISO == "" ) or ( _outISO == "" ) ) and ( _pdISO == "" ) )
        _rate = "";
      end;

/*GAA*/
      if ( ( _incomeISO == "" ) and ( _outISO == "" ) and (_rate == 1) )
        _rate = "";
      end;

    end;

  elif ( reestr.rec.ApplicationKind == SB_DEPOSIT )
    // тут заполнить поля для вкладных операций
    sbdepdoc.clear();
    sbdepdoc.keynum( 3 );

    sbdepdoc.rec.iApplicationKind = reestr.rec.ApplicationKind;
    sbdepdoc.rec.ApplicationKey   = reestr.rec.ApplicationKey;

    if ( sbdepdoc.getEQ( ) and ( sbdepdoc.rec.Action < 2 ) and IsServDoc( sbdepdoc.rec ) and ( sbdepdoc.rec.FlagStorn == "" ) and ( sbdepdoc.rec.NotConfirm == "" ) )
      wasFindDoc = true;

      _time        =  Time( int( substr( sbdepdoc.rec.ApplicationKey, 11, 2 ) ),
                            int( substr( sbdepdoc.rec.ApplicationKey, 13, 2 ) ),
                            int( substr( sbdepdoc.rec.ApplicationKey, 15, 2 ) ) );

      /* FindCurrRate( sbdepdoc.rec.Code_Currency, 1, reestr.rec.Date,  _rate, NULL, NULL, _time ); */
      _rate = "";

      _account = sbdepdoc.rec.Account;

      if ( IsDepDocForMonTrans( ) )
        _account = "";
      end;

      if ( sbdepdoc.rec.InSum )
        _incomeISO = GetCurrNumericalCode( sbdepdoc.rec.Code_Currency );
        _incomeSum = sbdepdoc.rec.InSum;
        checkSum = DefCheckSumForControl( sbdepdoc.rec.InSum, sbdepdoc.rec.Code_Currency, sbdepdoc.rec.Date_Document );
      else
        _outISO  = GetCurrNumericalCode( sbdepdoc.rec.Code_Currency );
        _outSum  = sbdepdoc.rec.OutSum;
        checkSum = DefCheckSumForControl( sbdepdoc.rec.OutSum, sbdepdoc.rec.Code_Currency, sbdepdoc.rec.Date_Document );
      end;

      if ( ( sbdepdoc.rec.Author == ACCOUTN_PROXY )  OR  ( sbdepdoc.rec.Author == ACCOUNT_RETRAST ) )
        _warrant = "X";  // операция по доверенности
      end;

      if (GetBitFlag( TDocBunch(sbdepdoc.rec.iApplicationKind, sbdepdoc.rec.ApplicationKey).retOp.rec.Flags, 12 ) == 1 )
        _warrant   = "X";  // операция по доверенности
      end;

      if ( ClientList.GetRecord( sbdepdoc.rec.CodClient ) )
        _country = countryNumCode( ClientList.curRec.rec.NRCountry/*Country*/ );
        if ( ( checkSum >= ControlSum ) or ( _warrant == "X" ) )
/*GAA*/
//          _country  = "";
          _client   = ClientList.curRec.ConvertFioFull();
          _document = ClientList.curRec.MakeDocStr();
          _address  = ClientList.curRec.MakeFullStrAddress();
        end;
      end;

      if ( BankStandart == 0 )
        if ( FindComissionDoc( ) )
          _commISO     = GetCurrNumericalCode( pay_doc.Rec.CodCur );
          _commSum     = pay_doc.Rec.InSum;
        end;
      end;
    end;

  elif ( reestr.rec.ApplicationKind == SB_PAYDOC )
    // тут заполнить поля для прочих операций
    pay_doc.clear();
    pay_doc.keynum( 2 );

    pay_doc.rec.iApplicationKind = reestr.rec.ApplicationKind;
    pay_doc.rec.ApplicationKey   = reestr.rec.ApplicationKey;

    if ( pay_doc.getEQ( ) and ( pay_doc.rec.Action < 2 ) and ( ( pay_doc.rec.IsSuspended == "" ) or ( pay_doc.rec.IsSuspended == "X" ) ) and ( pay_doc.rec.NotConfirm == 0 ) )
      wasFindDoc = true;

      _time        = Time( int( substr( pay_doc.rec.ApplicationKey, 11, 2 ) ),
                           int( substr( pay_doc.rec.ApplicationKey, 13, 2 ) ),
                           int( substr( pay_doc.rec.ApplicationKey, 15, 2 ) )  );

      /* FindCurrRate( pay_doc.rec.CodCur, 1, reestr.rec.Date,  _rate, NULL, NULL, _time ); */
      _rate = "";

      if ( pay_doc.rec.GroupOpert == 41 )
         _outISO    = GetCurrNumericalCode( pay_doc.rec.CodCur );
         _outSum    = pay_doc.rec.InSum;
         checkSum = DefCheckSumForControl( pay_doc.rec.InSum, pay_doc.rec.CodCur, pay_doc.rec.Date_Document );

      elif ( pay_doc.rec.GroupOpert == CO_Conversion )
          paydoc_cnv.SetRecordAddr( pay_doc, 0, 0, true );

/*GAA*/
         _incomeISO = GetCurrNumericalCode( paydoc_cnv.rec.KonvertCodCur );
/*
         _incomeSum = paydoc_cnv.rec.KonvertSum;
          checkSum  = paydoc_cnv.rec.KonvertSum;
*/

         _rate = paydoc_cnv.rec.Rate;

         _outISO    = GetCurrNumericalCode( pay_doc.Rec.Codcur );
         _outSum    = pay_doc.Rec.Insum;
         checkSum = DefCheckSumForControl( pay_doc.rec.InSum, pay_doc.rec.CodCur, pay_doc.rec.Date_Document );

         if ( ( _incomeISO == "" ) or ( _outISO == "" ) )
           _rate = "";
         end;

      else
         _incomeISO = GetCurrNumericalCode( pay_doc.rec.CodCur );
         _incomeSum = pay_doc.rec.InSum;
         checkSum = DefCheckSumForControl( pay_doc.rec.InSum, pay_doc.rec.CodCur, pay_doc.rec.Date_Document );

      end;

      if ( ( pay_doc.rec.GroupOpert == 41 ) or ( pay_doc.rec.GroupOpert == 11 ) )
        if ( GetVarSize( pay_doc ) == GetRecordSize( paydoc_n36 ) - GetRecordSize( pay_doc ) )
          paydoc_n36.SetRecordAddr( pay_doc, 0, 0, true );
          _account = paydoc_n36.rec.cardNumber;
          _isCard  = "X";
        end;
      end;

      if ( pay_doc.rec.GroupOpert == CO_Conversion ) /* Конверсия по вкладам */
        paydoc_cnv.SetRecordAddr( pay_doc, 0, 0, true );
        if ( paydoc_cnv.rec.Kind != "" )
          if ( ( Index( StrUpr( pay_doc.rec.Ground ), StrUpr( "Счет"        ) ) >  0 ) and /* conv_tax.mac */
               ( Index( StrUpr( pay_doc.rec.Ground ), StrUpr( "Счет вклада" ) ) == 0 )    )
            _account = Trim( SubStr( pay_doc.rec.Ground, 5 ) );
            if ( Index( _account, " " ) > 1 )
              _account = Trim( SubStr( _account, 1, Index( _account, " " ) - 1 ) );
            end;
          end;
        end;
      end;

      if ( false )
        _warrant   = "X";  // операция по доверенности
      end;

      retop.ApplicationKind = pay_doc.rec.iApplicationKind;
      retop.ApplicationKey  = pay_doc.rec.ApplicationKey;
      if ( getEQ( retop )  AND  ( GetBitFlag( retop.Flags, 12 ) == 1 ) )
        _warrant   = "X";  // операция по доверенности
      end;

      if ( StrLen( Trim( pay_doc.rec.ClientLastName ) ) > 0 )
        if ( (pay_doc.rec.ClntIdentType==CLNT_IDENT_TOTAL) OR (checkSum >= ControlSum15) )
          _country = countryNumCode( pay_doc.rec.ClientCountry );
        end;

        if ( (checkSum >= ControlSum) OR (pay_doc.rec.ClntIdentType!=CLNT_IDENT_STANDART) )
          ClientList.curRec.Clear( );
          ClientList.curRec.rec.Name1 = pay_doc.rec.ClientLastName;
          ClientList.curRec.rec.Name2 = pay_doc.rec.ClientFirstName;
          ClientList.curRec.rec.Name3 = pay_doc.rec.ClientSecondName;

          ClientList.curRec.rec.PaperKind = pay_doc.rec.PersIDType;
          ClientList.curRec.rec.PaperSeries = pay_doc.rec.PassportSer;
          ClientList.curRec.rec.PaperNumber = pay_doc.rec.PassportNumb;
          ClientList.curRec.rec.PaperIssuer = pay_doc.rec.PassportIssuer;

          _client   = ClientList.curRec.ConvertFioFull( );
          _document = ClientList.curRec.MakeDocStr( );
          _address  = pay_doc.rec.ClientAddress;
          if ( pay_doc.rec.ClntIdentType==CLNT_IDENT_FREE )
             _client = _client + "(*)";
             _address = _address + "(*)";
             _document = _document + "(*)";
          end;
        end;
      elif ( ( pay_doc.rec.CodClient != 0 ) and ClientList.GetRecord( pay_doc.rec.CodClient ) )
        if ( (pay_doc.rec.ClntIdentType==CLNT_IDENT_TOTAL) OR (checkSum >= ControlSum15) )
           _country = countryNumCode( ClientList.curRec.rec.NRCountry/*Country*/ );
        end;

        if ( (checkSum >= ControlSum) OR (pay_doc.rec.ClntIdentType!=CLNT_IDENT_STANDART) )
          _client = ClientList.curRec.ConvertFioFull( );
          _document = ClientList.curRec.MakeDocStr( );
          _address = ClientList.curRec.MakeFullStrAddress( );
          if ( pay_doc.rec.ClntIdentType==CLNT_IDENT_FREE )
             _client = _client + "(*)";
             _address = _address + "(*)";
             _document = _document + "(*)";
          end;
        end;
      end;

      if ( BankStandart == 0 )
        if ( FindComissionDoc( ) )
          _commISO     = GetCurrNumericalCode( pay_doc.Rec.CodCur );
          _commSum     = pay_doc.Rec.InSum;
        end;
      end;
    end;

  elif ( reestr.rec.ApplicationKind == SB_MONEY_TRANSFER )

    rmto_op.clear( );
    rmto_op.KeyNum( 0 );

    rmto_op.rec.opApplicationKind = reestr.rec.ApplicationKind;
    rmto_op.rec.opApplicationKey  = reestr.rec.ApplicationKey;

    if ( rmto_op.getEQ( ) and ( rmto_op.rec.Action < 2 ) )
               
      rmto_trans.clear( );
      rmto_trans.KeyNum( 0 );

      rmto_trans.rec.branch = rmto_op.rec.branch;
      rmto_trans.rec.transId = rmto_op.rec.transId;

      if ( rmto_trans.getEQ( ) )

        wasFindDoc = true;

        _time = Time( int( substr( rmto_op.rec.opApplicationKind, 11, 2 ) ),
                      int( substr( rmto_op.rec.opApplicationKind, 13, 2 ) ),
                      int( substr( rmto_op.rec.opApplicationKind, 15, 2 ) ) );

        /* FindCurrRate( sbdepdoc.rec.Code_Currency, 1, reestr.rec.Date,  _rate, NULL, NULL, _time ); */
        _rate = "";

        _account = "";

        if ( GetBitFlag( TDocBunch( rmto_op.rec.opApplicationKind, rmto_op.rec.opApplicationKey ).retOp.rec.Flags, 12 ) == 1 )
          _warrant  = "X";  // операция по доверенности
        end;

        if ( ( rmto_op.rec.opNum == 2 ) or ( rmto_op.rec.opNum == 8 ) or ( rmto_op.rec.opNum == 9 ) or ( rmto_op.rec.opNum == 13 ) )
          _outISO  = GetCurrNumericalCode( rmto_trans.rec.curCode );
          _outSum  = rmto_trans.rec.sum;
          checkSum = DefCheckSumForControl( rmto_trans.rec.sum, rmto_trans.rec.curCode, rmto_op.rec.opDate );
        else
          _incomeISO = GetCurrNumericalCode( rmto_trans.rec.curCode );
          _incomeSum = rmto_trans.rec.sum;
          checkSum = DefCheckSumForControl( rmto_trans.rec.sum, rmto_trans.rec.curCode, rmto_op.rec.opDate );
        end;


        if ( rmto_trans.rec.ourFlag != 0 )

          if ( StrLen( Trim( rmto_trans.rec.senderName1 ) ) > 0 )
            if ( ( rmto_trans.rec.senderIdentType == CLNT_IDENT_TOTAL ) or ( checkSum >= ControlSum15 ) )
              _country = countryNumCode( rmto_trans.rec.senderCitizenship );
            end;

            if ( ( checkSum >= ControlSum ) or ( rmto_trans.rec.senderIdentType != CLNT_IDENT_STANDART ) )
              ClientList.curRec.Clear( );
              ClientList.curRec.rec.Name1 = rmto_trans.rec.senderName1;
              ClientList.curRec.rec.Name2 = rmto_trans.rec.senderName2;
              ClientList.curRec.rec.Name3 = rmto_trans.rec.senderName3;

              ClientList.curRec.rec.PaperKind = rmto_trans.rec.senderPaperKind;
              ClientList.curRec.rec.PaperSeries = rmto_trans.rec.senderPaperSeries;
              ClientList.curRec.rec.PaperNumber = rmto_trans.rec.senderPaperNumber;
              ClientList.curRec.rec.PaperIssuer = rmto_trans.rec.senderPaperIssuer;

              _client   = ClientList.curRec.ConvertFioFull( );
              _document = ClientList.curRec.MakeDocStr( );
              _address  = rmto_trans.rec.senderAdress;
              if ( rmto_trans.rec.senderIdentType == CLNT_IDENT_FREE )
                _client = _client + "(*)";
                _address = _address + "(*)";
                _document = _document + "(*)";
              end;
            end;
          end;

        else

          if ( StrLen( Trim( rmto_trans.rec.recName1 ) ) > 0 )
            if ( ( rmto_trans.rec.recIdentType == CLNT_IDENT_TOTAL ) or ( checkSum >= ControlSum15 ) )
              _country = countryNumCode( rmto_trans.rec.recCitizenship );
            end;

            if ( ( checkSum >= ControlSum ) or ( rmto_trans.rec.recIdentType != CLNT_IDENT_STANDART ) )
              ClientList.curRec.Clear( );
              ClientList.curRec.rec.Name1 = rmto_trans.rec.recName1;
              ClientList.curRec.rec.Name2 = rmto_trans.rec.recName2;
              ClientList.curRec.rec.Name3 = rmto_trans.rec.recName3;

              ClientList.curRec.rec.PaperKind = rmto_trans.rec.recPaperKind;
              ClientList.curRec.rec.PaperSeries = rmto_trans.rec.recPaperSeries;
              ClientList.curRec.rec.PaperNumber = rmto_trans.rec.recPaperNumber;
              ClientList.curRec.rec.PaperIssuer = rmto_trans.rec.recPaperIssuer;

              _client   = ClientList.curRec.ConvertFioFull( );
              _document = ClientList.curRec.MakeDocStr( );
              _address  = rmto_trans.rec.recAdress;
              if ( rmto_trans.rec.recIdentType == CLNT_IDENT_FREE )
                _client = _client + "(*)";
                _address = _address + "(*)";
                _document = _document + "(*)";
              end;
            end;
          end;
        end;
      end;
    end;
  end;

  if ( reestr.rec.SysTime != Time( 0, 0, 0 ) )
    _time = reestr.rec.SysTime;
  end;

  return wasFindDoc;

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO PrintAbsent( Отчет )

  var mainBranch = getFilialNum( );
  var curBranch = Branch;
  var nameMain;
  var nameCur;
  var fioOper = {FIO_oper};

  if ( ValType( fioOper ) != V_STRING )
    fioOper = DefFIOOper( );
  end;

  // {Post_Addr}
  if ( findDepartment( mainBranch, NULL, ddep, party ) )
    if ( party.rec.Address != "" )
      nameMain = party.rec.Address;
    else
      nameMain = {Post_Addr};
    end;
  end;

  // {Post_Addr_Branch}
  if ( findDepartment( curBranch, NULL, ddep, party ) )
    if ( party.rec.Address != "" )
      nameCur = Trim( party.rec.Name + "; " + party.rec.Address );
    else
      nameCur = Trim( party.rec.Name + "; " + {Post_Addr_Branch} );
    end;
  end;

[
                                                       ┌────────────────────────────────────────────────────┐ 
  Полное (сокращенное) фирменное наименование          │####################################################│ 
  уполномоченного банка (наименование филиала)         └────────────────────────────────────────────────────┘ 

                                                       ┌────┐   ┌────┐
  Регистрационный номер уполномоченного банка          │####│ / │####│                                        
  (порядковый номер филиала)                           └────┘   └────┘                                        
                                                       ┌───────────────────────────────────────────────────────────────────────────────────────┐ 
  Местонахождение (адрес) уполномоченного банка        │#######################################################################################│ 
  (филиала)                                            └───────────────────────────────────────────────────────────────────────────────────────┘ 
                                                       ┌───────────────────────────────────────────────────────────────────────────────────────┐ 
  Наименование внутреннего струтурного подразделения   │#######################################################################################│ 
  уполномоченного банка и его место нахождение (адрес) └───────────────────────────────────────────────────────────────────────────────────────┘ 
                                                       ┌──┐  ┌──┐  ┌────┐                                     
  Дата выпуска Справки                                 │##│  │##│  │####│                                     
                                                       └──┘  └──┘  └────┘                                     
                                                       День  Месяц  Год                                       
                                                                                                              
                                                                                                              
                                                                   Справка                                    
                                                                                                              
          В течение рабочего дня отсутствуют операции с наличной иностранной валютой                          
       и чеками.                                                                                              
                                                                                                              
                                                                                                              
   Кассовый работник _________________ ###########################                                            
                        (подпись)                                                                             
]
  (
    {Name_Bank}:l,
    regNumBank:l,
    ordNumBank:l,
    nameMain:l,
    nameCur:l,
    day:r:o,
    month:r:o,
    year:r:o,
    fioOper
  );

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO PrintHeader( Отчет )

  var mainBranch = getFilialNum( );
  var curBranch = Branch;
  var nameMain;
  var nameCur;

  // {Post_Addr}
  if ( findDepartment( mainBranch, NULL, ddep, party ) )
    nameMain = party.rec.Address;
  end;

  // {Post_Addr_Branch}
  if ( findDepartment( curBranch, NULL, ddep, party ) )
    if ( party.rec.Address != "" )
      nameCur = Trim( party.rec.Name + "; " + party.rec.Address );
    else
      nameCur = Trim( party.rec.Name + "; " + {Post_Addr_Branch} );
    end;
  end;

  Отчет.AutoScan(
  "[                                                                                                           \n" +
//  "                                                                                                            \n" +
//  "                                                                                                            \n" +
  "                                                     ┌────────────────────────────────────────────────────┐ \n" +
  "Полное (сокращенное) фирменное наименование          │####################################################│ \n" +
  "уполномоченного банка (наименование филиала)         └────────────────────────────────────────────────────┘ \n" +
//  "                                                                                                            \n" +
  "                                                     ┌────┐   ┌────┐                                        \n" +
  "Регистрационный номер уполномоченного банка          │####│ / │####│                                        \n" +
  "(порядковый номер филиала)                           └────┘   └────┘                                        \n" +
//  "                                                                                                            \n" +
  "                                                     ┌───────────────────────────────────────────────────────────────────────────────────────┐ \n" +
  "Местонахождение (адрес) уполномоченного банка        │#######################################################################################│ \n" +
  "(филиала)                                            └───────────────────────────────────────────────────────────────────────────────────────┘ \n" +
//  "                                                                                                            \n" +
  "                                                     ┌───────────────────────────────────────────────────────────────────────────────────────┐ \n" +
  "Наименование внутреннего структурного подразделения  │#######################################################################################│ \n" +
  "уполномоченного банка и его местонахождение (адрес)  └───────────────────────────────────────────────────────────────────────────────────────┘ \n" +
//  "                                                                                                            \n" +
  "                                                     ┌──┐  ┌──┐  ┌────┐                                     \n" +
  "Дата заполнения Реестра                              │##│  │##│  │####│                                     \n" +
  "                                                     └──┘  └──┘  └────┘                                     \n" +
  "                                                     День  Месяц  Год                                       \n" +
//  "                                                                                                            \n" +
  "                                                           ┌──┐                                       \n" +
  "Порядковый номер Реестра в течение рабочего дня            │##│                                       \n" +
  "                                                           └──┘                                       \n" +
  "                                                                                                            \n" +
  "                                                                 РЕЕСТР                                     \n" +
  "                                               операций с наличной валютой и чеками                         \n" +
  "]()",
  {Name_Bank}                   , "l",
  regNumBank                    , "l",
  ordNumBank                    , "l",
  nameMain                      , "l",
  nameCur                       , "l",
  day                           , "r:o",
  month                         , "r:o",
  year                          , "r:o",
  /*rateString                    , "l",*/
  reestrNumber                  , "c"
  );


END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
var Header =
  "┌────┬─────┬────┬─────────┬─────────────────────────────────────────────┬────┬──────────────────────────────┬──────────────┬─────────────────────────┬─────┬──────┐\n" +
  "│По- │Время│Код │  Курс   │         Наличные денежные средства          │Пла-│       Принято (выдано)       │              │       Номер счета       │Дове-│ Граж-│\n" +
  "│ряд-│со-  │вида│ (кросс- ├──────────────────────┬──────────────────────┤теж-│     кассовым работником      │   Принято    │                         │рен- │ данс-│\n" +
  "│ко- │вер- │опе-│  курс)  │   Принято кассовым   │   Выдано кассовым    │ная │      чеков (в том числе      │комиссионного │                         │ность│ тво  │\n" +
  "│вый │шения│ра- │ иност-  │     работником       │      работником      │кар-│       дорожных чеков),       │вознагрождения│                         │     │ физи-│\n" +
  "│но- │опе- │ции │ ранной  │                      │                      │та  │    номинальная стоимость     │              │                         │     │ чес- │\n" +
  "│мер │ра-  │    │ валюты  │                      │                      │    │      которых указана в       │              │                         │     │ кого │\n" +
  "│опе-│ции, │    │         │                      │                      │    │      иностранной валюте      │              │                         │     │ лица │\n" +
  "│ра- │ЧЧ:ММ│    │         ├──────┬───────────────┼──────┬───────────────┤    ├───────┬──────┬───────────────┼──────┬───────┤                         │     │      │\n" +
  "│ции │     │    │         │ код  │     сумма     │ код  │     сумма     │    │Количе-│ код  │     сумма     │ код  │       │                         │     │      │\n" +
  "│    │     │    │         │валюты│               │валюты│               │    │ ство  │валюты│               │валюты│ сумма │                         │     │      │\n" +
  "│    │     │    │         │      │               │      │               │    │ чеков │      │               │      │       │                         │     │      │\n" +
  "├────┼─────┼────┼─────────┼──────┼───────────────┼──────┼───────────────┼────┼───────┼──────┼───────────────┼──────┼───────┼─────────────────────────┼─────┼──────┤\n" +
  "│  1 │  2  │  3 │    4    │   5  │       6       │   7  │       8       │  9 │  10   │  11  │      12       │ 12А  │  12Б  │           13            │  14 │  15  │\n" +
  "├────┼─────┼────┼─────────┼──────┼───────────────┼──────┼───────────────┼────┼───────┼──────┼───────────────┼──────┼───────┼─────────────────────────┼─────┼──────┤";

var HeaderCommercial =
  "┌────┬─────┬────┬─────────┬─────────────────────────────────────────────┬────┬──────────────────────────────┬─────────────────────────┬─────┬──────┐\n" +
  "│По- │Время│Код │  Курс   │         Наличные денежные средства          │Пла-│       Принято (выдано)       │       Номер счета       │Дове-│ Граж-│\n" +
  "│ряд-│со-  │вида│ иност-  ├──────────────────────┬──────────────────────┤теж-│     кассовым работником      │                         │рен- │ дан- │\n" +
  "│ко- │вер- │опе-│ ранной  │   Принято кассовым   │   Выдано кассовым    │ная │      чеков (в том числе      │                         │ность│ cтво │\n" +
  "│вый │шения│ра- │ валюты  │     работником       │      работником      │кар-│       дорожных чеков),       │                         │     │ физи-│\n" +
  "│но- │опе- │ции │ (кросс- │                      │                      │та  │    номинальная стоимость     │                         │     │ чес- │\n" +
  "│мер │ра-  │    │  курс)  │                      │                      │    │      которых указана в       │                         │     │ кого │\n" +
  "│опе-│ции, │    │ по опе- │                      │                      │    │      иностранной валюте      │                         │     │ лица │\n" +
  "│ра- │ЧЧ:ММ│    │ рации   ├──────┬───────────────┼──────┬───────────────┤    ├───────┬──────┬───────────────┤                         │     │      │\n" +
  "│ции │     │    │         │ код  │     сумма     │ код  │     сумма     │    │Количе-│ код  │     сумма     │                         │     │      │\n" +
  "│    │     │    │         │валюты│               │валюты│               │    │ ство  │валюты│               │                         │     │      │\n" +
  "│    │     │    │         │      │               │      │               │    │ чеков │      │               │                         │     │      │\n" +
  "├────┼─────┼────┼─────────┼──────┼───────────────┼──────┼───────────────┼────┼───────┼──────┼───────────────┼─────────────────────────┼─────┼──────┤\n" +
  "│  1 │  2  │  3 │    4    │   5  │       6       │   7  │       8       │  9 │  10   │  11  │      12       │           13            │  14 │  15  │\n" +
  "├────┼─────┼────┼─────────┼──────┼───────────────┼──────┼───────────────┼────┼───────┼──────┼───────────────┼─────────────────────────┼─────┼──────┤";

/**************************************************************************\
|                                                                          |
\**************************************************************************/
var HeaderSummary =
  "┌──────┬────────┬───────────────┬───────────────┬───────────────┬───────────────┐\n" +
  "│ Код  │   Код  │    Расход     │    Приход     │    Расход     │    Приход     │\n" +
  "│валюты│операции│    валюты     │    валюты     │    чеков      │    чеков      │\n" +
  "│      │        │               │               │               │               │\n" +
  "│      │        │               │               │               │               │\n" +
  "│      │        │               │               │               │               │\n" +
  "├──────┼────────┼───────────────┼───────────────┼───────────────┼───────────────┤";


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO ClearSummaryArray

  ASize( AComplexCode, 0 );
  ASize( ACodeISO, 0 );
  ASize( ACode113, 0 );
  ASize( ARate, 0 );
  ASize( AIncomeSum, 0 );
  ASize( AOutSum, 0 );
  ASize( AIncomeСheque, 0 );
  ASize( AOutСheque, 0 );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO AddOneSumToSummary( codeISO, code113, rate, incomeSum, outSum, incomeСheque, outСheque )

  var complexCode;
  var tmpISO = Trim( codeISO );
  var tmpCode = code113;
  var tmpRate = String( rate );
  var numElem = -1;
  var i = 0;

  while ( StrLen( tmpISO ) < 5 )
    tmpISO = "0" + tmpISO;
  end;

  while ( StrLen( tmpCode ) < 5 )
    tmpCode = "0" + tmpCode;
  end;

  while ( StrLen( tmpRate ) < 25 )
    tmpRate = "0" + tmpRate;
  end;

  complexCode = tmpISO + tmpCode + tmpRate;

  while ( i < ASize( AComplexCode ) )
    if ( AComplexCode( i ) == complexCode )
      numElem = i;
      i = ASize( AComplexCode ); /* Чтобы быстрее выйти из цикла */
    end;
    i = i + 1;
  end;

  if ( numElem == -1 )
    numElem = ASize( AComplexCode );
    AComplexCode( numElem ) = complexCode;
    ACodeISO( numElem ) = codeISO;
    ACode113( numElem ) = code113;
    ARate( numElem ) = rate;
    AIncomeSum( numElem ) = incomeSum;
    AOutSum( numElem ) = outSum;

    AIncomeSum( numElem )  = AIncomeSum( numElem ) + incomeСheque;
    AOutSum( numElem ) = AOutSum( numElem ) + outСheque;

//    AIncomeСheque( numElem ) = incomeСheque;         KIN
//    AOutСheque( numElem ) = outСheque;               KIN
  else
    AIncomeSum( numElem ) = AIncomeSum( numElem ) + incomeSum;
    AOutSum( numElem ) = AOutSum( numElem ) + outSum;

    AIncomeSum( numElem ) = AIncomeSum( numElem ) + incomeСheque;
    AOutSum( numElem ) = AOutSum( numElem ) + outСheque;

//    AIncomeСheque( numElem ) = AIncomeСheque( numElem ) + incomeСheque;  KIN
//    AOutСheque( numElem ) = AOutСheque( numElem ) + outСheque;           KIN
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO AddSumsToSummary

  if ( ( ValType( _incomeSum ) != V_STRING ) and ( _incomeSum != 0 ) )
    AddOneSumToSummary( _incomeISO, reestr.rec.Code113i, _rate, _incomeSum, $0L, $0L, $0L );
    AddOneSumToSummary( _incomeISO, CodeTotal, "", _incomeSum, $0L, $0L, $0L );
  end;

  if ( ( ValType( _outSum ) != V_STRING ) and ( _outSum != 0 ) )
    AddOneSumToSummary( _outISO, reestr.rec.Code113i, _rate, $0L, _outSum, $0L, $0L );
    AddOneSumToSummary( _outISO, CodeTotal, "", $0L, _outSum, $0L, $0L );
  end;

  if ( BankStandart == 0 )
    if ( ( ValType( _commSum ) != V_STRING ) and ( _commSum != 0 ) )
      AddOneSumToSummary( _commISO, reestr.rec.Code113i, _rate, _commSum, $0L, $0L, $0L );
      AddOneSumToSummary( _commISO, CodeTotal, "", _commSum, $0L, $0L, $0L );
    end;
  end;

  if ( ( ValType( _pdSum ) != V_STRING ) and ( _pdSum != 0 ) )
    if ( typePDSum == 1 )
      AddOneSumToSummary( _pdISO, reestr.rec.Code113i, _rate, $0L, $0L, _pdSum, $0L  );
      AddOneSumToSummary( _pdISO, CodeTotal, "", $0L, $0L, _pdSum, $0L  );
    elif ( typePDSum == 2 )
      AddOneSumToSummary( _pdISO, reestr.rec.Code113i, _rate, $0L, $0L, $0L, _pdSum  );
      AddOneSumToSummary( _pdISO, CodeTotal, "", $0L, $0L, $0L, _pdSum  );
    end;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO ZeroiseClIdentData
  _client    = "";           
  _document  = "";       
  _address   = "";
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO GetNeedCurCode(ExternalCurCode, CurCode)
   var cmd, rs, stat;
   var strcmd = "SELECT t_code_currency " +
                "FROM dcurrency_dbt " + 
                "WHERE t_externalcode = ?";
   cmd = RsdCommand( strcmd );
   cmd.addParam("externalcode", RSDBP_IN, ExternalCurCode);
   cmd.execute; 
   rs = RsdRecordset(cmd);
   stat = rs.movenext;
   if (stat) 
       SetParm( 1, rs.value("t_code_currency")); 
   end;
   return stat;
  
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO PrintERR( )
  _time      = "ERR";
  _rate      = "ERR";
  _incomeISO = "ERR";
  _incomeSum = "ERR";
  _outISO    = "ERR";
  _outSum    = "ERR";
  _isCard    = "ERR";
  _pdNum     = "ERR";
  _pdISO     = "ERR";
  _pdSum     = "ERR";
  _commISO   = "ERR";
  _commSum   = "ERR";
  _account   = "ERR";
  _warrant   = "ERR";
  _country   = "ERR";
  _client    = "ERR";
  _document  = "ERR";
  _address   = "ERR";
end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO PrintLine( Отчет )

  if ( not FillData( ) )
    return;
  end;

  if ( reestr.rec.Number != 0 )
    Отчет.AddPrintCell( reestr.rec.Number   );
  else
    Отчет.AddPrintCell( " " );
  end;
  Отчет.AddPrintCell( substr( string(_time), 1, 5 ) );
  Отчет.AddPrintCell( reestr.rec.Code113i );
  Отчет.AddPrintCell( _rate,    0, 4, "r" );
  Отчет.AddPrintCell( _incomeISO          );
  Отчет.AddPrintCell( _incomeSum          );
  Отчет.AddPrintCell( _outISO             );
  Отчет.AddPrintCell( _outSum             );
  Отчет.AddPrintCell( _isCard , 0, 0, "c" );
  Отчет.AddPrintCell( _pdNum              );
  Отчет.AddPrintCell( _pdISO              );
  Отчет.AddPrintCell( _pdSum              );
  if ( BankStandart == 0 )
    Отчет.AddPrintCell( _commISO          );
    Отчет.AddPrintCell( _commSum          );
  end;
  Отчет.AddPrintCell( _account            );
  Отчет.AddPrintCell( _warrant, 0, 0, "c" );
  Отчет.AddPrintCell( _country            );
  /*
  if ( BankStandart != 0 )
    Отчет.AddPrintCell( _client           );
    Отчет.AddPrintCell( _document         );
    Отчет.AddPrintCell( _address          );
  end;
  */
  Отчет.AddStr( );

  AddSumsToSummary( );

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO SortSummaryByCurrency

  var i = 0;
  var j = 0;
  var tmpVal;

  while ( i < ASize( AComplexCode ) )
    j = i;
    while (j < ASize( AComplexCode ) )
      if ( AComplexCode( j ) < AComplexCode( i ) )
        // ---------------------------------------------------------------
        tmpVal = AComplexCode( i );
        AComplexCode( i ) = AComplexCode( j );
        AComplexCode( j ) = tmpVal;
        // ---------------------------------------------------------------
        tmpVal = ACodeISO( i );
        ACodeISO( i ) = ACodeISO( j );
        ACodeISO( j ) = tmpVal;
        // ---------------------------------------------------------------
        tmpVal = ACode113( i );
        ACode113( i ) = ACode113( j );
        ACode113( j ) = tmpVal;
        // ---------------------------------------------------------------
        tmpVal = ARate( i );
        ARate( i ) = ARate( j );
        ARate( j ) = tmpVal;
        // ---------------------------------------------------------------
        tmpVal = AIncomeSum( i );
        AIncomeSum( i ) = AIncomeSum( j );
        AIncomeSum( j ) = tmpVal;
        // ---------------------------------------------------------------
        tmpVal = AOutSum( i );
        AOutSum( i ) = AOutSum( j );
        AOutSum( j ) = tmpVal;
        // ---------------------------------------------------------------
        tmpVal = AIncomeСheque( i );
        AIncomeСheque( i ) = AIncomeСheque( j );
        AIncomeСheque( j ) = tmpVal;
        // ---------------------------------------------------------------
        tmpVal = AOutСheque( i );
        AOutСheque( i ) = AOutСheque( j );
        AOutСheque( j ) = tmpVal;
      end;
      j = j + 1;
    end;
    i = i + 1;
  end;

end;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO PrintFooter( Отчет, Date_ )

  var i = 0;
  var strCodeISO;
  var strCode113;
  var fioOper = {FIO_oper};

  var saveCode113 = "ЮЮЮ";
  var saveInSum   = $0.0;
  var saveOutSum  = $0.0;

  if ( ValType( fioOper ) != V_STRING )
    fioOper = DefFIOOper( );
  end;
  
  SortSummaryByCurrency( );

  Отчет.AutoScan(
  "[                                                 \n" +
  " Итого по реестру                                 \n" +
  "                                                  \n" +
  " ┌──────┬────────┬──────────┬───────────────┬───────────────┐\n" +
  " │ Код  │   Код  │   Курс   │    Итого      │    Итого      │\n" +
  " │валюты│операции│          │    приход     │    расход     │\n" +
  " │      │        │          │               │               │\n" +
  "]()"
  );

  while ( i < ASize( AComplexCode ) )
    if ( ACode113( i ) == CodeTotal )

      if ( ( saveInSum != $0.0 )  OR  ( saveOutSum != $0.0 ) )

        Отчет.AutoScan(
        "[├──────┼────────┼──────────┼───────────────┼───────────────┤" +
        "]()"
        );

        Отчет.AutoScan(
        "[│######│########│##########│###############│###############│" +
        "]()",
        strCodeISO, "l",
        "Итого", "l",
        "",  "l",
        saveInSum, "r:0:2",
        saveOutSum,"r:0:2"
        );
      end;

      saveInSum   = $0.0;
      saveOutSum  = $0.0;

      strCodeISO = "ИТОГО";
      strCode113 = "";
    else
      strCodeISO = ACodeISO( i );
      strCode113 = ACode113( i );
      
      if ( ( saveCode113 != "ЮЮЮ" )  AND  ( saveCode113 != strCode113 )  AND  ( ( saveInSum != $0.0 )  OR  ( saveOutSum != $0.0 ) ) )

        Отчет.AutoScan(
        "[├──────┼────────┼──────────┼───────────────┼───────────────┤" +
        "]()"
        );

        Отчет.AutoScan(
        "[│######│########│##########│###############│###############│" +
        "]()",
        strCodeISO, "l",
        "Итого", "l",
        "",  "l",
        saveInSum, "r:0:2",
        saveOutSum,"r:0:2"
        );

        saveInSum   = $0.0;
        saveOutSum  = $0.0;
      end;

      saveCode113 = strCode113;
      saveInSum   = saveInSum  + AIncomeSum( i );
      saveOutSum  = saveOutSum + AOutSum( i )
    end;

/* KIN
    Отчет.AutoScan(
    "[├──────┼────────┼───────────────┼───────────────┼───────────────┼───────────────┤" +
    "]()"
    );

    Отчет.AutoScan(
    "[│######│########│###############│###############│###############│###############│" +
    "]()",
    strCodeISO, "l",
    strCode113, "l",
    AOutSum( i ),"r:0:2",
    AIncomeSum( i ), "r:0:2",
    AOutСheque( i ), "r:0:2",
    AIncomeСheque( i ),"r:0:2"
    );
*/
    Отчет.AutoScan(
    "[├──────┼────────┼──────────┼───────────────┼───────────────┤" +
    "]()"
    );

    Отчет.AutoScan(
    "[│######│########│##########│###############│###############│" +
    "]()",
    strCodeISO, "l",
    strCode113, "l",
    ARate( i ), "r:0:4",
    AIncomeSum( i ), "r:0:2",
    AOutSum( i ),"r:0:2"

//    AOutСheque( i ), "r:0:2",   KIN
//    AIncomeСheque( i ),"r:0:2"  KIN
    );

    i = i  + 1;
  end;

/*     KIN
  Отчет.AutoScan(
  "[└──────┴────────┴───────────────┴───────────────┴───────────────┴───────────────┘" +
  "]()"
  );
*/

  Отчет.AutoScan(
  "[└──────┴────────┴──────────┴───────────────┴───────────────┘" +
  "]()"
  );

  Отчет.AutoScan(
  "[                                                                             \n" +
  "                                                                              \n" +
  " Кассовый работник _________________ ###########################              \n" +
  "                      (подпись)                                               \n" +
  "]()",
  "/ " + fioOper + " /", "l"
  );

END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
MACRO for_each_in_rs( rs, callback )

  while( rs.movenext )
    existLine = TRUE;  // Признак непустого Реестра.
    execmacro( callback, rs );
  end;
END;

MACRO SetReestrFilter( date_to_filter )

  var isOK = false;

  reestr.dropfilter();
  reestr.rewind();
  reestr.clear();
  reestr.addfilter( " t_branch = " + branch
               + " AND t_curdate  = to_date('" + _dateReestr + "', 'dd.mm.yyyy')"
               + " AND t_oper = " + _oper
               + " AND t_date = to_date('" + date(date_to_filter) + "', 'dd.mm.yyyy')" 
               + " AND t_brigade  = " + Oper_Brigade
               );
  isOK = reestr.getGE;
  reestr.rewind();
  
  return isOK; 
END;

MACRO PrintBody( group_date )

  ClearSummaryArray( );
  if( NOT SetReestrFilter(group_date) )
    // RunError( "9999" );
    PrintLn( "Ошибка при устанвоке фильтра Реестра." );
  end;
  
  while( reestr.next )
    PrintLine( Отчет );
  end; 
  
  reestr.dropfilter();
END;


// Типа статические переменные для PrintReestrGroup.
private var prev_date = date(0,0,0);
private var is_first_record = true;
MACRO PrintReestrGroup( rs ) 
    /* rs.value(0) == rs.value("t_date")     == reestr.rec.t_date        
       rs.value(1) == rs.value("t_rate_ref") == reestr.rec.t_rate_ref */
       
    var this_date = rs.value("t_date");

    if( this_date != prev_date )
      reestrNumber = reestrNumber + 1;
      if( is_first_record == true )
          is_first_record = false;
          PrintHeader( Отчет );
      else
          PrintFooter( Отчет, prev_date );
          PrintHeader( Отчет );
      end;
    else 
      reestrNumber = 1;
    end;

    PrintBody( this_date ); 

    prev_date = this_date;
END;

MACRO GetReestrGroups() /* : rsdrecordset */

    var cmd, rs;
    cmd = rsdcommand(
        "SELECT r.t_date"
        "  FROM dreestr_2_dbt r"
        " WHERE r.t_branch = :branch"
        "   AND r.t_curdate = :curdate"
        "   AND r.t_oper = :oper"
        "   AND r.t_brigade  = :operbrig"
        " GROUP BY r.t_date"
        " ORDER BY r.t_date"
        );
    cmd.addparam( "branch", rsdbp_in, branch );
    cmd.addparam( "curdate", rsdbp_in, _dateReestr );
    cmd.addparam( "oper", rsdbp_in, _oper  );
    cmd.addparam( "operbrig", rsdbp_in, Oper_Brigade );
    cmd.execute();
    return rsdrecordset( cmd );
END;

MACRO FillReport()

  var rs = GetReestrGroups;
  
  DateSplit( _dateReestr, day, month, year );
  
  for_each_in_rs( rs, @PrintReestrGroup );

  // Т.к., проходя по рекондсету, мы не можем определить, является ли запись последней,
  // то приходится выводить последний футер вне цикла прохода по рекордсету.
  if( prev_date != date(0,0,0) )
      PrintFooter( Отчет, prev_date );
  end;
END;

/**************************************************************************\
|                                                                          |
\**************************************************************************/
private MACRO ПечатьРеестра()
  var Date_    = Date(1,1,1900);

  existLine = FALSE;  // Признак непустого Реестра.

  ControlSumFullIdent = GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ЛИМИТЫ\\ИДЕНТИФИКАЦИЯ_КЛИЕНТА\\ПОЛНАЯ" );
  ControlSumFullIdent = MoneyL( ControlSumFullIdent );

  if ( ( ControlSumFullIdent > $0L ) and ( ControlSum > ControlSumFullIdent ) )
    ControlSum = ControlSumFullIdent;
  end;

  Отчет    = CMakeReport( Header );

  if ( BankStandart == 0 )
    Отчет = CMakeReport( Header );
  else
    Отчет = CMakeReport( HeaderCommercial );
  end;

  codNumBank = getRegNumOurBank( PTCK_BANKREGNUM ); //{ExRegNumber}
  regNumBank = SubStr( codNumBank, 1, 4 );
  ordNumBank = SubStr( codNumBank, 6, 4 );

  Отчет.AutoScan("[ ]"); // при пустом отчете - ошибка :((

  reestrNumber = 0;
  FillReport();

  Отчет.PrintRep( );
  if ( ( reestrNumber != 0 ) and ОТЧЕТ_В_EXCEL )
    Отчет.PrintWinRep("Закладка1");
    Отчет.ShowWinRep();
  end;

  if ( NOT existLine )  // Реестр пустой.
    if ( GetRegVal( "RS-RETAIL\\СЕРВИСНЫЕ\\ОТЧЕТЫ\\СПРАВКА_ОБ_ОТСУТСТВИИ_ВОО" ) == 1 )
      if ( not flagEO )
        MsgBox( "Реестра 136-И для операциониста ", _oper, " нет.|Справка об отсутствии ВОО формируется по подразделению." );
      end;
      Exit( 1 );
    end;
    DateSplit( _dateReestr, day, month, year );
    Отчет = CMakeReport( "" );
    PrintAbsent( Отчет );
  end;

onError( errObj )
  if ( not flagEO )
    MsgBox( NameError(HandleSbError(errObj), "") );
  end;
END;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintCurrReestr( numOper, dateReestr )

  prev_date = date(0,0,0);
  is_first_record = true;

  if ( ( ValType( numOper ) == V_INTEGER ) and ( numOper > 0 ) )
    flagEO = true;
    _oper = numOper;

    if ( ( ValType( dateReestr ) == V_DATE ) and ( dateReestr != Date( 0, 0, 0 ) ) )
      _dateReestr = dateReestr;
    end;

    ПечатьРеестра( );
    return;
  end;

  if ( DefTypePerson == "С" ) /* Старший смены */
    if ( GetInt( _oper, "Введите № операциониста" ) )
      ПечатьРеестра( );
    end;
  else
    ПечатьРеестра( );
  end;

end;
