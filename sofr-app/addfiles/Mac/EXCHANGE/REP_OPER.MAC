/*-RSL---------------------------------------- R-Style Software Lab --*/
/*---------------   Печать отчета по операциям ОП   ------------------*/
/* RS-Bank      подсистема Обменный пункт  Азарцов В.В.  20.05.97     */
/*                                         Рытик А.И.    18.12.98     */

IMPORT ExchangeInter, "define.mac";

 file curr      ("curr.dbt"    , "exchange.def" ) key 0;
 file oper      ("operat.dbt"  , "exchange.def" ) key 2;
 file turn      ("turnover"    , "exchange.def" ) key 1;
 file RstTypes  ("rsttypes.dbt", "exchange.def" ) key 0;
 file RstTypes_1("rsttypes.dbt", "exchange.def" ) key 1;

 record s ("rep_oper.str","exchange.def");

 const Валюта1 = 840,      /* USD - валюта, выводимая вне очереди первой */
       Валюта2 = 276;      /* DEM - валюта, выводимая вне очереди второй */
 var   i;

 const
 ВысотаЛиста = 40,
 ВысотаЗаголовка = 5,  /* +0 - резерв для ключа печати :w */
 ВысотаШапки   = 8,    /* +0 - резерв для ключа печати :w */
 ВысотаПодвала = 4,
 ВысотаЗаписи  = 2,
 ВысотаИтога   = 4;

 var Строка = 0;
 var Страница = 1;

 array ТипОперации;
 ТипОперации(1) = "Завершена";
 ТипОперации(2) = "Начата   ";
 ТипОперации(3) = "Откатана ";
 ТипОперации(4) = "Отменена ";
 ТипОперации(5) = "В реестре";
 ТипОперации(6) = "В реестре";
 ТипОперации(7) = "Удалена  ";

macro НоваяСтраница();
   [ # ]( StrFor(12) );
   Строка = 0;
   Страница = Страница + 1;
end;

ARRAY ИСОкод;

MACRO ЗаполнитьМассивИСОкодов()
   rewind( curr );
   while( next(curr) )
         ИСОкод( curr.ID + 1 ) = curr.ISO;
   end;
END;


macro ПечатьЗаголовка( Address, RegNumber );
  PrintDate_n_Time();
[                     ОТЧЕТ ПО ОПЕРАЦИЯМ ОБМЕННОГО ПУНКТА

  Регистрационный  номер обменного пункта #
  Адрес обменного пункта                  ##################################################################################################
]( RegNumber, Address:w );
  Строка = Строка + ВысотаЗаголовка;
end;

macro ПечатьШапки( type, name, BegDate, EndDate, curr_name );
var StrResident;
if( s.Resident == SET_CHAR )
  StrResident = "    Резиденты";
else
  StrResident = "    Нерезиденты";
end;
[                                                                                                                  Страница #   ]( Страница );
[      Операция:  #########################################################################################################################
 с ########## по ##########   #################################
 +-----+--------+---------------+---------------+-------------------+-------------------+---------------+----------+-------------------------+--------------+
 |Номер|  Номер |               |               |    Процентная     |   Фиксированная   |   Суммарная   |   Дата   |                         |              |
 |опер.| реестра|    Приход     |    Расход     |     комиссия      |      комиссия     | комиссия, руб |     и    |         Кассир          |  Состояние   |
 |     |        |               |               |                   |                   |  по курсу ЦБ  |   время  |                         |              |
 +-----+--------+---------------+---------------+-------------------+-------------------+---------------+----------+-------------------------+--------------+
]( /*type,*/
   name:w,
   StrAddZeroDate_Short( BegDate ),
   StrAddZeroDate_Short( EndDate ),
   curr_name+StrResident );
  Строка = Строка + ВысотаШапки;
end;

macro ПечатьПодвала( arg1, arg2, arg3, arg4 );
  if ( Строка > ( ВысотаЛиста - ВысотаПодвала) )
    НоваяСтраница();
    ПечатьШапки( s.Oper_Ref, s.NameOper, s.StartDate, s.FinishDate, curr.LongName );
  end;
[+-----+--------+---------------+---------------+-------------------+-------------------+---------------+----------+-------------------------+--------------+
 | Итого:       |               |               |                   |                   |               |          |                         |              |
 |#####         |###############|###############|                   |                   |###############|          |                         |              |
 +-----+--------+---------------+---------------+-------------------+-------------------+---------------+----------+-------------------------+--------------+
]( arg1, arg2, arg3, arg4:c );
  НоваяСтраница();
end;

macro ПечатьИтога( arg1, arg2, arg3, arg4 );
  if ( Строка > ( ВысотаЛиста - ВысотаПодвала) )
    НоваяСтраница();
    ПечатьШапки( s.Oper_Ref, s.NameOper, s.StartDate, s.FinishDate, curr.LongName );
  end;
[+-----+--------+---------------+---------------+-------------------+-------------------+---------------+----------+-------------------------+--------------+
 | ИТОГО:       |               |               |                   |                   |               |          |                         |              |
 |#####         |###############|###############|                   |                   |###############|          |                         |              |
 +-----+--------+---------------+---------------+-------------------+-------------------+---------------+----------+-------------------------+--------------+
]( arg1, arg2, arg3, arg4:c );
  НоваяСтраница();
end;

macro ПечатьШапки1( type, name, BegDate, EndDate, curr_name );
var StrResident;
if( s.Resident == SET_CHAR )
  StrResident = "    Резиденты";
else
  StrResident = "    Нерезиденты";
end;
[                                                                                                                          Страница #  ]( Страница );
[      Операция:  #########################################################################################################################
 с ########## по ##########   #################################
 +-----+--------+-------------------+-------------------+-------------------+-------------------+---------------+----------+-------------------------+--------------+
 |Номер|  Номер |       Приход      |      Расход       |    Процентная     |   Фиксированная   |   Суммарная   |   Дата   |                         |              |
 |опер.| реестра|    с указанием    |   с указанием     |     комиссия      |      комиссия     | комиссия, руб |     и    |         Кассир          |  Состояние   |
 |     |        |       валюты      |      валюты       |                   |                   |  по курсу ЦБ  |   время  |                         |              |
 +-----+--------+-------------------+-------------------+-------------------+-------------------+---------------+----------+-------------------------+--------------+
]( /*type,*/
   name:w,
   StrAddZeroDate_Short( BegDate ),
   StrAddZeroDate_Short( EndDate ),
   curr_name+StrResident );
  Строка = Строка + ВысотаШапки;
end;

macro ПечатьПодвала1( arg1, arg2, arg3 );
  if ( Строка > ( ВысотаЛиста - ВысотаПодвала) )
    НоваяСтраница();
    ПечатьШапки( s.Oper_Ref, s.NameOper, s.StartDate, s.FinishDate, curr.LongName );
  end;
[+-----+--------+-------------------+-------------------+-------------------+-------------------+---------------+----------+-------------------------+--------------+
 | Итого:       |                   |                   |                   |                   |               |          |                         |              |
 |#####         |###################|                   |                   |                   |###############|          |                         |              |
 +-----+--------+-------------------+-------------------+-------------------+-------------------+---------------+----------+-------------------------+--------------+
]( arg1, arg2, arg3:c );
  НоваяСтраница();
end;

macro ПечатьИтога1( arg1, arg2, arg3 );
  if ( Строка > ( ВысотаЛиста - ВысотаПодвала) )
    НоваяСтраница();
    ПечатьШапки1( s.Oper_Ref, s.NameOper, s.StartDate, s.FinishDate, curr.LongName );
  end;
[+-----+--------+-------------------+-------------------+-------------------+-------------------+---------------+----------+-------------------------+--------------+
 | ИТОГО:       |                   |                   |                   |                   |               |          |                         |              |
 |#####         |###################|                   |                   |                   |###############|          |                         |              |
 +-----+--------+-------------------+-------------------+-------------------+-------------------+---------------+----------+-------------------------+--------------+
]( arg1, arg2, arg3:c );
  НоваяСтраница();
end;


macro Init( Emp , Type , State , BegDate , EndDate)
    ClearRecord( oper );
    oper.Date          =  BegDate; /* Дата начала выборки */
    /*
    oper.Emp_Ref       =  Emp;     /* Сотрудник, проведший операцию     */
    oper.Sess_Ref      =  0;       /* Номер смены сотрудника            */
    oper.Oper_Ref      =  Type;    /* Тип операции (из настроек реестра)*/
    oper.State_Ref     =  State;   /* Состояние операции                */
    oper.Rate_Ref      =  0;       /* Номер курса                       */
    oper.OutCurr_Ref   =  0;       /* Валюта выданной ценности          */
    oper.IncomeCurr_Ref=  0;       /* Валюта полученной ценности        */
    */
    return getGE( oper );
end;
                           /* конверсия  */
macro Filter( Emp , Type , State , InCurr , OutCurr , BegDate , EndDate , Resident )
    if( (oper.IncomeCurr_Ref == InCurr  ) AND
        ( (oper.OutCurr_Ref  == OutCurr ) OR ( OutCurr == -1 ) ) AND
        (oper.Oper_Ref       == Type    ) AND
        (oper.Resident       == Resident) AND
        (oper.Date           <= EndDate )
      )
      if( ( (Emp == 0 ) OR (oper.Emp_Ref == Emp ) ) AND
          ( (State == 0 ) OR (oper.State_Ref == State ) ) )
          return True;
      end;
    return False;
    end;
  return False;
end;
                           /* сортировка по валюте прихода */
                           /* ( например, покупка валюты ) */
macro Filter1( Emp , Type , State , InCurr , BegDate , EndDate , Resident )
    if( (oper.IncomeCurr_Ref == InCurr  ) AND
        ( (oper.OutCurr_Ref  == s.OutCurr_Ref ) OR
          ( s.OutCurr_Ref == -1 )       ) AND
        (oper.Oper_Ref       == Type    ) AND
        (oper.Resident       == Resident) AND
        (oper.Date           <= EndDate )
      )
      if( ( (Emp == 0 ) OR (oper.Emp_Ref == Emp ) ) AND
          ( (State == 0 ) OR (oper.State_Ref == State ) ) )
          return True;
      end;
    return False;
    end;
  return False;
end;
                           /* сортировка по валюте расхода */
                           /* ( например, продажа валюты ) */
macro Filter2( Emp , Type , State , OutCurr , BegDate , EndDate , Resident )
    if( (oper.OutCurr_Ref == OutCurr ) AND
        ( (oper.IncomeCurr_Ref  == s.IncomeCurr_Ref ) OR
          ( s.IncomeCurr_Ref == -1 ) ) AND
        (oper.Oper_Ref    == Type    ) AND
        (oper.Resident    == Resident) AND
        (oper.Date        <= EndDate )
      )
      if( ( (Emp == 0 ) OR (oper.Emp_Ref == Emp ) ) AND
          ( (State == 0 ) OR (oper.State_Ref == State ) ) )
          return True;
      end;
    return False;
    end;
  return False;
end;

MACRO ПолучитьТипОперации( TypeReestr )
    ClearRecord( RstTypes );
    RstTypes.Type = TypeReestr;
    if( GetEQ( RstTypes ) )
      return RstTypes.Oper_Ref;
    else
      return -1;
    end;
END;

MACRO GetTypeReestr( KindOper )
   RstTypes_1.Oper_Ref = KindOper;
   if( GetEQ( RstTypes_1 ) )
      return RstTypes_1.Type;
   else
      return "";
   end;
END;

MACRO GetTurnover( TypeTurnover )

  ClearRecord( Turn );
  Turn.Oper_Ref = Oper.ID;
  var Loop = GetGE( Turn );
  while( Loop )
     if( (Turn.Oper_Ref == Oper.ID) AND
         (Turn.Type == TypeTurnover)
       )
     return True;
     end;
     Loop = next(Turn);
  end;
  return False;
END;

/* Получить комиссию  */
MACRO GetCom ( FixCom, FixComCurr, PercCom, PercComCurr )

   FixCom  = $0.0; FixComCurr  = ANY_CURR;  /* фиксированная комиссия */
   PercCom = $0.0; PercComCurr = ANY_CURR;  /* процентная комиссия    */

   if( GetTurnover( CDF_TurnFix ) )         /* фиксированная комиссия */
     FixCom      = abs(Turn.Sum);
     FixComCurr  = Turn.Curr_Ref;
   end;
   if( GetTurnover( CDF_TurnPComInc ) )   /* процентная от полученной суммы */
     PercCom     = abs(Turn.Sum);
     PercComCurr = Turn.Curr_Ref;
   elif( GetTurnover( CDF_TurnPComOut ) ) /* процентная от выданной суммы */
     PercCom     = abs(Turn.Sum);
     PercComCurr = Turn.Curr_Ref;
   end;

   setparm( 0, FixCom);
   setparm( 1, FixComCurr);
   setparm( 2, PercCom);
   setparm( 3, PercComCurr);
END;

/**************************************************************************/
MACRO ОтчетПоОбмену();

VAR SumOper, SumIncomeAmount, SumOutAmount, SumComAmount, Loop_;
VAR FixCom, FixComCurr, PercCom, PercComCurr;
VAR TotalComis, FirstTime;
VAR ID1, ID2;

   SumOper = 0;
   SumIncomeAmount = 0;
   SumComAmount = 0;
   Loop_ = True;
   FirstTime = True;

   if( Init(s.Emp_Ref , s.Oper_Ref , s.State_Ref , s.StartDate , s.FinishDate) )

     while( Loop_ )         /* Цикл по операциям */
       i = i + 1;
       UseProgress( i );
       if( Filter( s.Emp_Ref, s.Oper_Ref, s.State_Ref, curr.ID, s.OutCurr_Ref, s.StartDate, s.FinishDate, s.Resident ))

         GetCom( FixCom, FixComCurr, PercCom, PercComCurr );
         TotalComis = ( PercCom * ПолучитьКурсЦБ(PercComCurr,oper.Rate_Ref) +
                        FixCom  * ПолучитьКурсЦБ(FixComCurr, oper.Rate_Ref) )
                        / 100.;
         if( FirstTime == True )
           ПечатьШапки1( s.Oper_Ref, s.NameOper, s.StartDate, s.FinishDate, curr.LongName );
           FirstTime = False;
         end;
         if ( Строка > ( ВысотаЛиста - ВысотаЗаписи - ВысотаПодвала ) )
            ПечатьПодвала( SumOper, SumIncomeAmount, SumOutAmount, SumComAmount );
            ПечатьШапки1( s.Oper_Ref, s.NameOper, s.StartDate, s.FinishDate, curr.LongName );
         end;
[|#####|########|###################|###################|###################|###################|###############|##########|#########################|      #       |
 |     |        |                   |                   |                   |                   |               | ######## |                         |              |
](       oper.ID,
         GetNumReestr(oper.ID):c,
         string(oper.IncomeAmount) + " " + ИСОКод(oper.IncomeCurr_Ref+1):r,
         string(oper.OutAmount   ) + " " + ИСОКод(oper.OutCurr_Ref   +1):r,
         string( PercCom ) + " " + ИСОКод( PercComCurr +1):r,
         string( FixCom  ) + " " + ИСОКод( FixComCurr  +1):r,
         TotalComis:15:3,
         StrAddZeroDate_Short( oper.Date ),
         GetFIOEmployee( oper.Emp_Ref ):c,
         ТипОперации(oper.State_Ref):c,
         oper.Time
);
         Строка = Строка + ВысотаЗаписи;
         SumOper = SumOper + 1;
         SumIncomeAmount = SumIncomeAmount + oper.IncomeAmount;
         SumComAmount    = SumComAmount    + TotalComis;

       end;
       Loop_ = ( ( oper.Date <= s.FinishDate ) AND next( oper ) );
     end;
   end;

   if( SumOper != 0 )
     ПечатьИтога1( SumOper, SumIncomeAmount, SumComAmount );
   end;

END;

/**************************************************************************/

MACRO ПечатьОтчетаПоОперациямОбмена()

VAR Loop;
VAR FullName, ReportType;

 s.TypeReestr = GetTypeReestr( s.Oper_Ref );
 s.NameOper   = GetNameCodif( s.Oper_Ref, CDF_Oper );

 if( s.IncomeCurr_Ref != -1 )
   curr.ID = s.IncomeCurr_Ref;   /* одна валюта */
   GetEQ(curr);
   ОтчетПоОбмену();

 else
   Loop = True;

   ClearRecord( curr );
   curr.ID = Валюта1;             /* USD */
   if ( GetEQ( curr ) )
     ОтчетПоОбмену( ReportType );
   end;

   ClearRecord( curr );
   curr.ID = Валюта2;             /* DEM */
   if ( GetEQ( curr ) )
     ОтчетПоОбмену( ReportType );
   end;

   ReWind( curr );
   Loop = Next( curr );       /* position 0 - ANY CURRENCY */
   Loop = Next( curr );       /* position 1 - first real curency */

   while ( Loop )             /* Цикл по валютам */
     if( ( curr.ID != Валюта1 ) AND ( curr.ID != Валюта2 ) )
       GetEQ(curr);
       ОтчетПоОбмену();
     end;
     Loop = next( curr );
   end;
 end;

END;


/* ----------------------------------------------------------------------- */
/***************************************************************************/
/* ----------------------------------------------------------------------- */

MACRO ОтчетПо1Валюте( ReportType );

VAR SumOper, SumIncomeAmount, SumOutAmount, SumComAmount, Loop_;
VAR FixCom, FixComCurr, PercCom, PercComCurr;
VAR TotalComis, FirstTime;
VAR ID1, ID2;

   SumOper = 0;
   SumIncomeAmount = 0;
   SumOutAmount = 0;
   SumComAmount = 0;
   Loop_ = False;

   /*if( curr.ID != {NationalCur} )*/
      Loop_ = True;
   /*end;*/
   FirstTime = True;
                     
   if( Init(s.Emp_Ref , s.Oper_Ref , s.State_Ref , s.StartDate , s.FinishDate) )
                     
     while( Loop_ )         /* Цикл по операциям */
       i = i + 1;
       UseProgress( i );
       if( Filter( s.Emp_Ref , s.Oper_Ref , s.State_Ref , curr.ID , s.StartDate , s.FinishDate , s.Resident ))

         GetCom( FixCom, FixComCurr, PercCom, PercComCurr );
         TotalComis = ( PercCom * ПолучитьКурсЦБ(PercComCurr,oper.Rate_Ref) +
                        FixCom  * ПолучитьКурсЦБ(FixComCurr, oper.Rate_Ref) )
                        / 100.;
         if( FirstTime == True )
           ПечатьШапки( s.Oper_Ref, s.NameOper, s.StartDate, s.FinishDate, curr.LongName );
           FirstTime = False;
         end;
         if ( Строка > ( ВысотаЛиста - ВысотаЗаписи - ВысотаПодвала ) )
            ПечатьПодвала( SumOper, SumIncomeAmount, SumOutAmount, SumComAmount );
            ПечатьШапки( s.Oper_Ref, s.NameOper, s.StartDate, s.FinishDate, curr.LongName );
         end;
[|#####|########|###############|###############|###################|###################|###############|##########|#########################|      #       |
 |     |        |               |               |                   |                   |               | ######## |                         |              |
](       oper.ID,
         GetNumReestr(oper.ID):c,
         oper.IncomeAmount,
         oper.OutAmount,
         string( PercCom ) + " " + ИСОКод( PercComCurr +1):r,
         string( FixCom  ) + " " + ИСОКод( FixComCurr  +1):r,
         TotalComis:15:3,
         StrAddZeroDate_Short( oper.Date ),
         GetFIOEmployee( oper.Emp_Ref ):c,
         ТипОперации(oper.State_Ref):c,
         oper.Time
);
         Строка = Строка + ВысотаЗаписи;
         SumOper = SumOper + 1;
         SumIncomeAmount = SumIncomeAmount + oper.IncomeAmount;
         SumOutAmount    = SumOutAmount    + oper.OutAmount;
         SumComAmount    = SumComAmount    + TotalComis;

       end;
       Loop_ = ( ( oper.Date <= s.FinishDate ) AND next( oper ) );
     end;
   end;

   if( /*( curr.ID != {NationalCur} ) AND*/ ( SumOper != 0 ) )
      ПечатьИтога( SumOper, SumIncomeAmount, SumOutAmount, SumComAmount );
   end;
END;

/***************************************************************************/

MACRO ПечатьОтчетаПоОперациямКромеОбмена( ReportType )
 var Loop = True;

 s.TypeReestr = GetTypeReestr( s.Oper_Ref );
 s.NameOper   = GetNameCodif( s.Oper_Ref, CDF_Oper );

 if( (ReportType == 1) AND (s.IncomeCurr_Ref != -1) )
   curr.ID = s.IncomeCurr_Ref;
   GetEQ(curr);
   ОтчетПо1Валюте( ReportType );

 elif( (ReportType == 2) AND (s.OutCurr_Ref != -1) )
   curr.ID = s.OutCurr_Ref;
   GetEQ(curr);
   ОтчетПо1Валюте( ReportType );

 else
   ClearRecord( curr );
   curr.ID = Валюта1;             /* USD */
   if ( GetEQ( curr ) )
     ОтчетПо1Валюте( ReportType );
   end;

   ClearRecord( curr );
   curr.ID = Валюта2;             /* DEM */
   if ( GetEQ( curr ) )
     ОтчетПо1Валюте( ReportType );
   end;

   ReWind( curr );
   Loop = Next( curr );       /* position 0 - ANY CURRENCY */
   Loop = Next( curr );       /* position 1 - first real curency */

   while ( Loop )             /* Цикл по валютам */
     if( ( curr.ID != Валюта1 ) AND ( curr.ID != Валюта2 ) )
       GetEQ(curr);
       ОтчетПо1Валюте( ReportType );
     end;
     Loop = next( curr );
   end;
 end;
END;

/***************************************************************************/

MACRO ПечатьОтчетаПоОперациям( a )       /* -=  ГОЛОВНОЙ МАКРОС  =-  */

VAR FullName, ReportType;
VAR ID1, ID2, NReestrs, NCurrensies, N, NLoop;
VAR stat = True;
N = -1;
i = 0;
NReestrs = 8+3+1;
NCurrensies = NRecords(curr);

ЗаполнитьМассивИСОкодов();

setbuff( s , a );

 ClearRecord( oper );
 oper.Date = s.FinishDate;
 if( GetGE (oper) )
    ID2 = oper.ID;
    while( (oper.Date == s.FinishDate) AND stat )
       ID2 = oper.ID;
       stat = Next( oper );
    end;
 else
    ReWind( oper );
    Prev( oper );
    ID2 = oper.ID;
 end;
 if( Init(s.Emp_Ref , s.Oper_Ref , s.State_Ref , s.StartDate , s.FinishDate) )
   ID1 = oper.ID;

   if( s.Oper_Ref == 0 )
     if  ( ( s.IncomeCurr_Ref == -1 ) AND ( s.OutCurr_Ref == -1 ) )
       NLoop = 16 * NCurrensies + 11 * NCurrensies + 1 * NCurrensies;
     elif( ( s.IncomeCurr_Ref != -1 ) AND ( s.OutCurr_Ref == -1 ) )
       NLoop = 16 * 1 + 11 * NCurrensies + 1 * 1;
     elif( ( s.IncomeCurr_Ref == -1 ) AND ( s.OutCurr_Ref != -1 ) )
       NLoop = 16 * NCurrensies + 11 * 1 + 1 * NCurrensies;
     else
       NLoop = NReestrs;
     end;

   else
     if( (s.Oper_Ref == CDF_OperCurrSale           ) OR
         (s.Oper_Ref == CDF_OperPayDocSale         ) OR
         (s.Oper_Ref == CDF_OperCardGive           ) OR
         (s.Oper_Ref == CDF_OperTransfer           ) OR
         (s.Oper_Ref == CDF_OperCashOut            ) OR
         (s.Oper_Ref == CDF_OperReturnIncasso      ) OR
         (s.Oper_Ref == CDF_OperReturnExpert       ) OR
         (s.Oper_Ref == CDF_OperPayDocReturnIncasso) OR
         (s.Oper_Ref == CDF_OperPayDocReturnExpert ) OR
         (s.Oper_Ref == CDF_OperGiveOutOverplus    ) OR
         (s.Oper_Ref == CDF_OperDetectDeficit      )
       )
       if(s.OutCurr_Ref == -1)
         NLoop = NCurrensies;
       else
         NLoop = 1;
       end;
     elif(s.IncomeCurr_Ref == -1)
       NLoop = NCurrensies;
     else
       NLoop = 1;
     end;
   end;

   N = (ID2-ID1) * NLoop;
   InitProgress( N, " ", "Анализ данных" );
 end;
  ПечатьЗаголовка( {ExAddress}, {ExRegNumber} );

 if( s.Oper_Ref == 0 )              /* все реестры  */
   s.Oper_Ref = CDF_OperCurrBuy;
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperPayDocBuy;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperCurrSale;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter2" );
   ReportType = 2;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperPayDocSale;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter2" );
   ReportType = 2;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperPayDocBuyCurr;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperPayDocSaleCurr;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   s.Oper_Ref = CDF_OperCurrConversion;
   ReportType = 3;
   ПечатьОтчетаПоОперациямОбмена();
   s.Oper_Ref = CDF_OperChange;
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperCardGive;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter2" );
   ReportType = 2;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperCardTake;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperSplit;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperUnpayCurrBuy;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperAvance;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperTransfer;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter2" );
   ReportType = 2;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperCashOut;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter2" );
   ReportType = 2;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperIncasso;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperExpert;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperPayDocIncasso;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperPayDocExpert;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperReturnIncasso;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter2" );
   ReportType = 2;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperReturnExpert;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter2" );
   ReportType = 2;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperPayDocReturnIncasso;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter2" );
   ReportType = 2;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperPayDocReturnExpert;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter2" );
   ReportType = 2;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperCheck;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperDetectOverplus;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperGiveOutOverplus;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter2" );
   ReportType = 2;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperDetectDeficit;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter2" );
   ReportType = 2;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   s.Oper_Ref = CDF_OperCompensateDeficit;
   ReplaceMacro ( "Filter" ); /* чтобы вернуть имя назад */
   ReplaceMacro ( "Filter" , "Filter1" );
   ReportType = 1;
   ПечатьОтчетаПоОперациямКромеОбмена( ReportType );

 else
   if( s.Oper_Ref == CDF_OperCurrConversion )
     ПечатьОтчетаПоОперациямОбмена();
     if( N != -1 )
       RemProgress();
     end;
     RETURN;
   end;

   if   ( ( s.Oper_Ref == CDF_OperCurrBuy           ) OR
          ( s.Oper_Ref == CDF_OperPayDocBuy         ) OR
          ( s.Oper_Ref == CDF_OperPayDocBuyCurr     ) OR
          ( s.Oper_Ref == CDF_OperPayDocSaleCurr    ) OR
          ( s.Oper_Ref == CDF_OperChange            ) OR
          ( s.Oper_Ref == CDF_OperCardTake          ) OR
          ( s.Oper_Ref == CDF_OperSplit             ) OR
          ( s.Oper_Ref == CDF_OperUnpayCurrBuy      ) OR
          ( s.Oper_Ref == CDF_OperAvance            ) OR
          ( s.Oper_Ref == CDF_OperIncasso           ) OR
          ( s.Oper_Ref == CDF_OperExpert            ) OR
          ( s.Oper_Ref == CDF_OperPayDocIncasso     ) OR
          ( s.Oper_Ref == CDF_OperPayDocExpert      ) OR
          ( s.Oper_Ref == CDF_OperCheck             ) OR
          ( s.Oper_Ref == CDF_OperDetectOverplus    ) OR
          ( s.Oper_Ref == CDF_OperCompensateDeficit )   
        )                ReplaceMacro ( "Filter" , "Filter1" );
                         ReportType = 1;
   elif ( ( s.Oper_Ref == CDF_OperCurrSale            ) OR
          ( s.Oper_Ref == CDF_OperPayDocSale          ) OR
          ( s.Oper_Ref == CDF_OperCardGive            ) OR
          ( s.Oper_Ref == CDF_OperTransfer            ) OR
          ( s.Oper_Ref == CDF_OperCashOut             ) OR
          ( s.Oper_Ref == CDF_OperReturnIncasso       ) OR
          ( s.Oper_Ref == CDF_OperReturnExpert        ) OR
          ( s.Oper_Ref == CDF_OperPayDocReturnIncasso ) OR
          ( s.Oper_Ref == CDF_OperPayDocReturnExpert  ) OR
          ( s.Oper_Ref == CDF_OperGiveOutOverplus     ) OR
          ( s.Oper_Ref == CDF_OperDetectDeficit       )   
        )                ReplaceMacro ( "Filter" , "Filter2" );
                         ReportType = 2;
   else [
        Неверная операция: #### ##################################################]
        (s.Oper_Ref, s.NameOper);
                         ReportType = -1;
   end;
   if( ReportType != -1 )
     ПечатьОтчетаПоОперациямКромеОбмена( ReportType );
   end;
 end;
 if( N != -1 )
   RemProgress();
 end;
END;
