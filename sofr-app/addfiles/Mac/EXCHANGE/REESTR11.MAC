/*-RSL---------------------------------------- R-Style Software Lab --*/
/*--------------- Печать реестра -------------------------------------*/
/* RS-Bank      подсистема Обменный пункт            AV  14.05.97     */
/*AV 13-02-98   скорректировал в соответствии с инструкцией 31-Т      */
/*AV 27-08-98   сделал постраничное формирование реестров             */
/*RA  9-02-99   закомментировал упоминание неденоминированных сумм    */
/*RA  6-08-99   добавил подох. налог в соответствии с указанием 610-У */

import ExchangeInter, "rstrfunc.mac", "string.mac", "regnumb.mac";

MACRO ПечатьШапки
  var ВремяКурс;
   var ValFlag = GetRegVal( "RS-RETAIL\\INI\\ПЕРЕМЕННЫЕ\\ОБМЕННЫЙ_ПУНКТ\\SETTIMERATE" );
   if( ValFlag != "OFF" )
      ВремяКурс = ValFlag;
   else
      ВремяКурс = Реестр.RateStartTime;
   end;

[
                                 РЕЕСТР
        НАЛИЧНОЙ ИНОСТРАННОЙ ВАЛЮТЫ, ПРОДАННОЙ ЗА НАЛИЧНЫЕ РУБЛИ
];
if( PRINT_CURRENCY == TRUE )
[
        Валюта:  #
]( GetNameCurr( FindCodCurByRefVal( Реестр.OutRefVal ) ) );
end;
[
     Курс продажи на текущую дату ##################  ##########  ########
          (указывается время в часах и минутах, с которого действует
                   установленный банком курс продажи)

        Курс Банка России на текущую дату ##################

+-------+---------------+------+------------------+----------+--------------+----------+------------+-----------+
|  КОД  |Cумма проданной|  КОД |  Сумма наличных  | N справки|  КОД, сумма  |Резидент\ |Сумма налога|   Сумма   |
|наличн.|   наличной    |налич.|     рублей,      | ф.0406007|комиссионного |Нерезидент|            |подоходного|
| валюты|  иностранной  |рублей|  полученных за   |          |вознаграждения|          |            |  налога   |
|       |валюты за рубли|      |проданную наличную|          |              |          |            | (цифрами) |
|       |   (цифрами)   |      |иностранную валюту|          |              |          |            |           |
|       |               |      |    (цифрами)     |          |              |          |            |           |
+-------+---------------+------+------------------+----------+--------------+----------+------------+-----------+
|   1   |       2       |   3  |         4        |     5    |       6      |    7     |     8      |     9     |
+-------+---------------+------+------------------+----------+--------------+----------+------------+-----------+
] ( КурсСНужнойТочностью( Реестр.Rate,   FindCodCurByRefVal(Реестр.OutRefVal) ):r,
    StrAddZeroDate_Short( Реестр.RateStartDate ),
    ВремяКурс,
    КурсСНужнойТочностью( Реестр.CBRate, FindCodCurByRefVal(Реестр.OutRefVal) )
  );
END;

MACRO ПечатьПодвала
[+-------+---------------+------+------------------+----------+--------------+----------+------------+-----------+
Итого:   ###############        ################## ##########                           ############ ###########

ИТОГО:   ###############        ################## ########## ##############            ############ ###########

        Кассир обменного пункта: _________________________ (###############################)
                                        (подпись)


------------------------------------------------------------------------------------------------------------------
#
](
    TotalsPage( 0 ),  TotalsPage( 1 ), TotalsPage( 2 ):c, TotalsPage( 3 ), TotalsPage( 4 ),
    /*SumNonDenomToString( TotalsPage( 4 ) ):w, RA 9-02-99 закомментировал*/
    /*SumNonDenomToString( TotalsPage( 5 ) ):w, RA 9-02-99 закомментировал*/
    Реестр.OutSum,
    /* VD  5.03.98 полученная - налог */
    Реестр.IncomeSum,
    TotalsPage( 6 ):c,/*все справки по реестру*/
    /*AV  5-05-98 вся комиссия по реестру*/
    MakeStrReestrComiss():w,
    Реестр.TaxSum,
    Реестр.IncTaxSum,
    /* VD 4.03.98 обороты по All */
    /*SumNonDenomToString(Реестр.IncAllUndenomSum):w, RA 9-02-99 закомментировал*/
    /*SumNonDenomToString(Реестр.OutAllUndenomSum):w, RA 9-02-99 закомментировал*/
    Реестр.FIO,
    StrFor(12)
  );
END;

MACRO ПечатьПодвала_ОдностраничныйРеестр( )
[+-------+---------------+------+------------------+----------+--------------+----------+------------+-----------+
ИТОГО:   ###############        ################## ########## ##############           ############# ###########

        Кассир обменного пункта: _________________________ (###############################)
                                        (подпись)


------------------------------------------------------------------------------------------------------------------
#
](
    Реестр.OutSum,
    /* VD  5.03.98 полученная - налог */
    Реестр.IncomeSum,
    TotalsPage( 6 ):c,/*все справки по реестру*/
    /*AV  5-05-98 вся комиссия по реестру*/
    MakeStrReestrComiss():w,
    Реестр.TaxSum,
    Реестр.IncTaxSum,
    /* VD 4.03.98 обороты по All */
    /*SumNonDenomToString(Реестр.IncAllUndenomSum):w, RA 9-02-99 закомментировал*/
    /*SumNonDenomToString(Реестр.OutAllUndenomSum):w, RA 9-02-99 закомментировал*/
    Реестр.FIO,
    StrFor(12)
  );
END;


MACRO ПечатьПодвалаСтраницы
[+-------+---------------+------+------------------+----------+--------------+----------+------------+-----------+
Итого:   ###############        ################## ##########                           ############ ###########

        Кассир обменного пункта: _________________________ (###############################)
                                        (подпись)


------------------------------------------------------------------------------------------------------------------
#
](
    TotalsPage( 0 ),/*Реестр.OutSum*/
    /* VD  5.03.98 полученная - налог */
    TotalsPage( 1 ),/*Реестр.IncomeSum */
    TotalsPage( 2 ):c,/*справки - страница*/
    TotalsPage( 3 ),/*Реестр.TaxSum*/
    TotalsPage( 4 ),
    /* VD 4.03.98 обороты по All */
    /*SumNonDenomToString( TotalsPage(4) ):w, RA 9-02-99 закомментировал*/
    /*SumNonDenomToString( TotalsPage(5) ):w, RA 9-02-99 закомментировал*/
    Реестр.FIO,
    StrFor(12)
  );
END;

MACRO ПечатьСтрокиРеестра

  /* Восстанавливаем исходные суммы по операции   RA 1-09-99 */
  /*RA 14-03-01 больше не нужно! УчетНекассовыхОборотов( "По операции", СоставРеестра.IncomeAmount, СоставРеестра.OutAmount );*/
  /*var IncNon = MakeNonDenomSum( IncNonDenSum ); RA 9-02-99 закомментировал*/
  /*var OutNon = MakeNonDenomSum( OutNonDenSum ); RA 9-02-99 закомментировал*/
  var Seria, Number;
  GetSprvSerNum( СоставРеестра.ApplicationKind, 
                 СоставРеестра.ApplicationKey, 
                 GetBSOShortName(), Seria, Number );
  Number = string(Number);
  Number = StrFillBeg( Number, "0", 7 ); /* дополнение ведущими нулями */

[|  ###  |###############| ###  |##################|## #######|##############|##########|############|###########|]
(
  GetCurrNumericalCode( FindCodCurByRefVal(Реестр.OutRefVal) ),
  СоставРеестра.OutAmount,
  GetCurrNumericalCode( FindCodCurByRefVal(Реестр.IncomeRefVal) ),
  /* VD  5.03.98 полученная - налог */
  СоставРеестра.IncomeAmount,
  Seria,
  Number,
  /*MakeCodeCurr(СоставРеестра.ComCurr_Ref),*/
  /*СоставРеестра.ComAmount,                */
  MakeComissString():w,
  MakeResidentName(СоставРеестра.Resident):c,
  СоставРеестра.TaxValue,
  СоставРеестра.IncTaxValue
  /*SumNonDenomToString( IncNon ):w, RA 9-02-99 закомментировал*/
  /*SumNonDenomToString( OutNon ):w RA 9-02-99 закомментировал*/
);

 /*подсчет итогов по странице*/
 AddTotalsPage( 0, СоставРеестра.OutAmount    );
 AddTotalsPage( 1, СоставРеестра.IncomeAmount );
 AddTotalsPage( 3, СоставРеестра.TaxValue     );
 AddTotalsPage( 4, СоставРеестра.IncTaxValue  );
 /*AddTotalsPage( 4, IncNon                     ); RA 9-02-99 закомментировал*/
 /*AddTotalsPage( 5, OutNon                     ); RA 9-02-99 закомментировал*/
 if( Seria != "" )
   AddTotalsPage( 2, 1  );/*справки - страница   */
   AddTotalsPage( 6, 1  );/*справки - весь реестр*/
 end;

END;

/*-------------------------------------------------------------------------*/
/* рекурсивная функция - печатает строки реестра с разбиением на страницы  */
/*-------------------------------------------------------------------------*/
MACRO СодержимоеРеестраПостранично_Рекурсия( NumPage, NumberStrings )
  ОбщаяШапка( Реестр.Date, Реестр.UserRegNumb, NumPage );
  ПечатьШапки();
  var LoopSetRstr = True;
  while( (LoopSetRstr == True) and (ФильтрРеестр() == True) )
    NumberStrings = NumberStrings + 1;
    if( ФильтрСтрокаРеестра() )
      ПечатьСтрокиРеестра();
    end;
    LoopSetRstr = next( СоставРеестра );
    if( (LoopSetRstr == True) AND (ФильтрРеестр() == True) AND ( (NumberStrings/NumPage) == MaxNumberStrings) )
       ПечатьПодвалаСтраницы();
       NumPage = NumPage + 1;
       ClearTotalsPage( 6 );
       LoopSetRstr = СодержимоеРеестраПостранично_Рекурсия( NumPage, NumberStrings );
    end;
  end;
  return LoopSetRstr;
END;

/*-------------------------------------------------------------------------*/
/* Вызов рекурсии для печати строк реестра с разбиением на страницы        */
/*-------------------------------------------------------------------------*/
MACRO СодержимоеРеестраПостранично( NumPage, NumberStrings )
   if( InitSetRstrKey() )
      SetRegNumb( Реестр ); /* Заполним Реестр.UserRegNumb, если он пустой */
      СодержимоеРеестраПостранично_Рекурсия( NumPage, NumberStrings );
   end;
   SetParm( 0, NumPage ); SetParm( 1, NumberStrings );
END;

/*-------------------------------------------------------------------------*/
/*    печать реестра данного типа для одной пары валют                     */
/*-------------------------------------------------------------------------*/
MACRO ПечатьОдногоРеестра( Reestr )
   var НомерСтраницы = 1;
   var НомерСтроки   = 0;
   ClearTotalsPage( 7 );/*вначале обнуляем весь массив*/
   setbuff( r , Reestr );
   asize( ArSum,  0 ); /* суммы комиссий  */
   asize( ArCurr, 0 ); /* валюты комиссий */

   if( FindReestr( r ))
       /* Восстанавливаем исходные суммы по реестру  RA 16-12-99 */
       /*RA 14-03-01 больше не нужно! УчетНекассовыхОборотов( "По реестру", Реестр.IncomeSum, Реестр.OutSum );*/
       СодержимоеРеестраПостранично( НомерСтраницы, НомерСтроки );
       if( НомерСтраницы > 1 )
          ПечатьПодвала();
       else
          ПечатьПодвала_ОдностраничныйРеестр();
       end;
   end;
END;

/*-------------------------------------------------------------------------*/
/*    печать всех реестров данного типа                                    */
/*-------------------------------------------------------------------------*/
MACRO ПечатьВсехРеестров( Kind_Oper )
   var НомерСтраницы = 1;
   var НомерСтроки   = 0;
   var LoopRstr      = True;
   if(InitReestrKey( Kind_Oper  ))
     PrintDate_n_Time();
     while( LoopRstr and ФильтрШапки( Kind_Oper ) )
       ClearTotalsPage( 7 );/*вначале обнуляем весь массив*/
       asize( ArSum,  0 ); /* суммы комиссий  */
       asize( ArCurr, 0 ); /* валюты комиссий */
       /* Восстанавливаем исходные суммы по реестрам   RA 1-09-99 */
       /*RA 14-03-01 больше не нужно! УчетНекассовыхОборотов( "По реестру", Реестр.IncomeSum, Реестр.OutSum );*/
       СодержимоеРеестраПостранично( НомерСтраницы, НомерСтроки );/*рекурсия*/
       if( НомерСтраницы > 1 )
          ПечатьПодвала();
       else
          ПечатьПодвала_ОдностраничныйРеестр();
       end;
       LoopRstr =  next( Реестр );
     end;
   end;
END;



