/*-RSL---------------------------------------- R-Style Software Lab --*/
/*---------------- Печать квитанции на бланке ф.31 -------------------*/
/* RS-Bank         Обменный пункт                                     */
/*                                                                    */
/* Copyright 1999 (c) R-Style Software Lab.                           */
/* Создание: Рытик А.И.  09-09-99                                     */
/*                                                                    */
/*  ------------------------------------------------------------------*/
                                                                       

import ExchangeInter, BankInter, "rstrfunc.mac", "string.mac";

record Oper       ( operat,         "exchange.def" );
record s          ( "f0406007.str", "exchange.def" );
record Office     ( office,         "exchange.def" );

const
    SINGLECURR_COMIS =  0,/*одновалютная*/
    DOUBLECURR_COMIS =  1,/*двувалютная*/
       ONLYFIX_COMIS =  2,/*только фиксированная*/
      ONLYPERC_COMIS =  3,/*только процентная*/
           NOT_COMIS =  4;/*комиссии нет*/



MACRO PutF31 ( a )

  SetBuff( s, a );         
  SetBuff( Oper, s.OperAddr );   /* буфер записи текущей операции */ 

  var Curr_Ref;                  /* код валюты, по которой печатается квитанция */
  var ClntName = " ";
  var RealProcComSum  = $0.0;    /*сумма */
  var ProcComFromInc  = $0.0;
  var RealProcComCurr = ANY_CURR;/*валюта*/
  var FixComSum       = $0.0;    /*сумма */
  var FixComCurr      = ANY_CURR;/*валюта*/
  var str = " ";
  var str1 = " ";
  var nn = 0;
  var day = 0;
  var month = 0;
  var year = 0;
  var TypeCom;
  var OutTrs_Ref, OutCurr_Ref;
  var Res, UnRes, /*TaxString*/, BegStrCom, BegStrTax;
/*  var BegStrIncTax, IncTaxString;*/

  var ComAmount, ComCurr, ComString;
/*  var СтрокаНалог;*/
  var TotalSum = $0.0, StrTotalSum;
  ARRAY HeaderCom;

  Curr_Ref = s.DocType;       /* Через поле DocType передается код валюты */
  Copy( СоставРеестра, Oper );/*для работы с оборотами*/ 

  /*получение через обороты значений сумм комиссий по операции*/
  if( GetTurnover( CDF_TurnFix ) AND (Turn.Curr_Ref == Curr_Ref) )
      FixComSum   = abs(Turn.Sum);                         /*фиксированная*/  
  end;
  if  ( GetTurnover( CDF_TurnPComInc ) AND (Turn.Curr_Ref == Curr_Ref) )
      RealProcComSum = ProcComFromInc = abs(Turn.Sum);     /*процентная от полученной*/
  elif( GetTurnover( CDF_TurnPComOut ) AND (Turn.Curr_Ref == Curr_Ref) )
      RealProcComSum = abs(Turn.Sum);                      /*процентная от выданной*/
  end;

  /*проверка на двухвалютность комиссии - разные валюты у процентной и    */
  /*  фиксированной комиссии.                                             */
  if( (RealProcComSum != 0.0) AND (FixComSum != 0.0) AND ( RealProcComCurr != FixComCurr) )
    TypeCom = DOUBLECURR_COMIS;
  elif( (FixComSum != 0.0) AND (RealProcComSum != 0.0) )
    TypeCom = SINGLECURR_COMIS;
  elif( FixComSum != 0.0 )
    TypeCom = ONLYFIX_COMIS;
  elif( RealProcComSum != 0.0 )
    TypeCom = ONLYPERC_COMIS;
  else
    TypeCom = NOT_COMIS;/*комиссии нет*/
  end;
/*
  /* Инициализация строки по налогу на операции продажи валюты и ПД за рубли*/
  BegStrTax = "Налог с продажи ";
  TaxString = "";
  if( s.TaxFlag AND (Curr_Ref == {NationalCur}) )
     TaxString = BegStrTax + CurToStrHyphen( Oper.TaxValue, null, null, {NationalCur} );
     TotalSum = TotalSum + Oper.TaxValue;
  end;

  /* Инициализация строки по подоходному налогу */
  BegStrIncTax = "Подоходный налог ";
  IncTaxString = "";
  if(Oper.IncTaxValue != 0 AND (Curr_Ref == {NationalCur}) )
     IncTaxString = BegStrIncTax + CurToStrHyphen( Oper.IncTaxValue, null, null, {NationalCur} );
     TotalSum = TotalSum + Oper.IncTaxValue;
  end;
*/
  /* Инициализация заголовка строки комиссии */
  BegStrCom = "Взыскана комиссия по операции \"" + GetNameCodif( Oper.Oper_Ref, CDF_Oper ) + "\"";

  /* Инициализация строки комиссии */
  if  ( TypeCom == SINGLECURR_COMIS )/*одновалютная комиссия*/
     ComAmount = FixComSum + RealProcComSum;
     ComString = CurToStrHyphen( ComAmount,      null, null, Curr_Ref );
     TotalSum = TotalSum + ComAmount;
  elif( TypeCom == DOUBLECURR_COMIS )/*двухвалютная комиссия*/
     ComString = CurToStrHyphen( RealProcComSum, null, null, Curr_Ref )
                 + " и " +
                 CurToStrHyphen( FixComSum,      null, null, Curr_Ref );
     TotalSum = TotalSum + RealProcComSum + FixComSum;
  elif( TypeCom == ONLYFIX_COMIS )/*только фиксированная комиссия*/
     ComString = CurToStrHyphen( FixComSum,      null, null, Curr_Ref );
     TotalSum = TotalSum + FixComSum;
  elif( TypeCom == ONLYPERC_COMIS )/*только процентная комиссия*/
     ComString = CurToStrHyphen( RealProcComSum, null, null, Curr_Ref );
     TotalSum = TotalSum + RealProcComSum;
  else ComString = "";/*комиссии нет*/
  end;

  /* Инициализация даты */
  DateSplit(Oper.Date, Day, Month, Year);     
  str = string(Day);
  if (Day < 10)
    str = "0" + str;
  end;

  str1 = trim(substr(string(Oper.Date:m),3,strlen(string(Oper.Date:m))-10));
  while (StrLen(str1) < 13)
    str1 = " " + str1;
  end;
  str = str + " " + str1 + "     " + substr(String(Year),4,1);

  /* Инициализация имени клиента */
  ClntName = Oper.DocInfo;
  nn = StrBrk(ClntName,".");
  while (nn != 0)
    ClntName = substr(ClntName,1,nn-1) + substr(ClntName,nn+1);
    nn = StrBrk(ClntName,".");
  end;
/*
  /* Инициализация строк налога */
    СтрокаНалог = TaxString + "  " + IncTaxString;
*/  
  StrTotalSum = CurToStrAlt( TotalSum, null, null, Curr_Ref );
  TotalSum = StrHyphen(TotalSum);

  StrSplit( BegStrCom, HeaderCom, 75, 75, 2 );

  /***********************************************************************/
  /*  +=+=+=+=+=+=+=  Собственно печать в бланк квитанции =+=+=+=+=+=+=+ */
  /***********************************************************************/
[
                                           #




                             #
                     ##################################################################
               ########################################################################
            ###########################################################################
            ###########################################################################
            
                                   ####################
            ###########################################################################
] (
  Trim( Office.RegNumb ),    /* номер отделения (филиала)   */
  str,                       /* дата                        */
  ClntName:c,                /* ФИО клиента - полностью     */
  HeaderCom(0),              /* строка - заголовок комиссии */
  HeaderCom(1),
  ComString,                 /* строка - комиссия           */
  /*СтрокаНалог,               /* строка - налог              */ */
  TotalSum,                  /* сумма                       */
  StrTotalSum                /* сумма прописью              */
  );


END;
