/*-RSL---------------------------------------- R-Style Software Lab --*/
/*----------------   Выгрузка СТОП-листа по ценностям  ---------------*/
/*                       в текстовый файл                             */
/*--------------------------------------------------------------------*/
/* RS-Bank      подсистема Обменный пункт            AV  24-12-97     */

IMPORT ExchangeInter, "sprvfunc.mac";

file stoplist("stoplist.dbt","exchange.def") write;
file txt()txt write;

array NameState;
var      TxtStr, /*строка формируемая для записи в текстовый файл*/
    NameTxtFile, /*имя текстового файла*/
 NumSaveRec = 0; /*количество записей занесенных в текстовый файл*/

NameState(0) = "Не активна";
NameState(1) = "Активна";
NameState(2) = "Удалена";

/*процедура выделения имени файла из строки с путем до него*/
macro GetNameFromPath( PathName )
  var PosSlash;
  while( (PosSlash = Index( PathName , "\\" )) != 0 )
   PathName = SubStr(PathName, PosSlash+1);
  end;
  return PathName;
end;

/*процедура инициализации текстовой строки из записи stoplist.dbt*/
macro GetTxtStringFromRec

 var di,mi,yi,dc,mc,yc,hi,mini,seci,hc,minc,secc;

 DateSplit(stoplist.DateInput,  di, mi, yi);
 DateSplit(stoplist.DateChange, dc, mc, yc);
 TimeSplit(stoplist.TimeInput,  hi, mini, seci);
 TimeSplit(stoplist.TimeChange, hc, minc, secc);

 if( stoplist.Seria  == "" ) stoplist.Seria  = " "; end;
 if( stoplist.Number == "" ) stoplist.Number = " "; end;
 if( stoplist.Note   == "" ) stoplist.Note   = " "; end;

 TxtStr  = string(stoplist.Kind_Trs) + "," +
           string(stoplist.Curr_Ref) + "," +
           string(stoplist.Nominal ) + "," +
           string(stoplist.Seria   ) + "," +
           string(stoplist.Number  ) + "," +
           string(stoplist.State   ) + "," +
           string(stoplist.Note    ) + "," +
           string(di) + "," + string(mi)+ "," + string(yi)+ "," +
           string(dc) + "," + string(mc)+ "," + string(yc)+ "," +
           string(hi) + "," + string(mini)+ "," + string(seci)+ "," +
           string(hc) + "," + string(minc)+ "," + string(secc);
end;

/*печать шапки*/
macro PrintHead

ОбщаяШапка();/*информация об обменном пункте*/

[
            ВЫГРУЗКА ИНФОРМАЦИИ В СТОП-ЛИСТ ПО ЦЕННОСТЯМ
                   #                           #

                      в файл ############

+----------+---+------------+-----+---------------------+----------+
|  Код     |Вал|   Номинал  |Серия|        Номер        |  Статус  |]
(
   StrAddZeroDate( Date ), Time,
   GetNameFromPath( NameTxtFile ):c
)
end;

/*печать информации о записях в СТОП-листе*/
macro PrintRec

[+----------+---+------------+-----+---------------------+----------+
|    ##    |###|############|#####|#####################|##########|]
(
   stoplist.Kind_Trs,
   stoplist.Curr_Ref,
   stoplist.Nominal,
   stoplist.Seria:r,
   stoplist.Number:r,
   NameState(stoplist.State)
);
end;

/*печать дна*/
macro PrintBottom
[+----------+---+------------+-----+---------------------+----------+
| Всего  ##### записей.                                            |
+------------------------------------------------------------------+
]( NumSaveRec:c );



end;

macro DownLoadStoplist()

 SetDelim(",");
 if( SelectFile(  NameTxtFile, "*.lst"," Выбор файла для выгрузки СТОП-листа") == True )
   Open( txt , NameTxtFile );

   PrintHead(); /*печатаем шапку*/

   rewind( stoplist );
   while( next( stoplist ) )
      /*формирование текстовой строки TxtStr для записи в файл*/
      GetTxtStringFromRec;
      /*добавдение строки в файл*/
      insert(txt,TxtStr);
      NumSaveRec = NumSaveRec + 1;

      PrintRec();
   end;

   PrintBottom();
 end;

end;

DownLoadStoplist;

