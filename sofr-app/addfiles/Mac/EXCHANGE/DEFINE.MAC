/*-RSL---------------------------------------- R-Style Software Lab --*/
/*--------------- Общие константы и макрофункции ---------------------*/
/* RS-Bank      подсистема Обменный пункт            AV  16.05.97     */
/*RA  9-02-99   закомментировал упоминание неденоминированных сумм    */

CONST
/* типы ценностей по кодификатору */
 TrsRoot          = 8,  /* Код группы ценностей                               */
 TrsTratt         = 22, /* Тратты                                             */
 TrsNoteGrp       = 3,  /* Банкноты, казначейские билеты и монеты( группа)    */
 TrsCash          = 10, /* Банкноты и казначейские билеты                     */
 TrsMonet         = 11, /* Монета                                             */
 TrsPayDoc        = 6,  /* Платежные документы(группа)                        */
 TrsCheck         = 20, /* Чеки                                               */
                        /*  Введены 19.09.2000 VS */
 TrsCheckRub      = 30, /* ПД, оплаченные рублями */
 TrsCheckCur      = 31, /* ПД, оплаченные валютой */

 TrsAccreditive   = 23, /* Аккредитивы                                        */
 TrsValForm       = 50, /* Ценные бланки                                      */
 /* Ценности по инкассо */
 TrsIncassNoteGrp     = 103, /* Банкноты, казначейские билеты и монеты(группа) по инкассо */
 TrsIncassCash        = 110, /* Банкноты и казначейские билеты по инкассо                 */
 TrsIncassMonet       = 111, /* Монета по инкассо                                         */
 TrsIncassPayDoc      = 106, /* Платежные документы(группа) по инкассо                    */
 TrsIncassCheck       = 120, /* Чеки по инкассо                                           */
 TrsIncassAccreditive = 123, /* Аккредитивы по инкассо                                    */
 TrsIncassTratt       = 122, /* Тратты по инкассо                                         */
 /* Ценности по экспертизе */
 TrsExpertNoteGrp     = 203, /* Банкноты, казначейские билеты и монеты(группа) по экспертизе */
 TrsExpertCash        = 210, /* Банкноты и казначейские билеты по экспертизе                 */
 TrsExpertMonet       = 211, /* Монета по экспертизе                                         */
 TrsExpertPayDoc      = 206, /* Платежные документы(группа) по экспертизе                    */
 TrsExpertCheck       = 220, /* Чеки по экспертизе                                           */
 TrsExpertAccreditive = 223, /* Аккредитивы по экспертизе                                    */
 TrsExpertTratt       = 222, /* Тратты по экспертизе                                         */

/* Типы операций по кодификатору */
 CDF_Oper                   = 11,      /* Операции ОП */
   CDF_OperPayDoc           = 2,     /* Операции покупки/продажи платежных документов */
     CDF_OperPayDocBuy      = 3,       /* Покупка платежного документа за рубли */
     CDF_OperPayDocSale     = 4,       /* Продажа платежного документа за рубли */
     CDF_OperPayDocBuyCurr  = 20,      /* Покупка платежного документа за валюту */
     CDF_OperPayDocSaleCurr = 21,      /* Продажа платежного документа за валюту */
   CDF_OperCurrChange       = 5,     /* Операции обмена валюты */
     CDF_OperUnpayCurrBuy   = 1,       /* Покупка неплатежеспособной валюты за рубли */
     CDF_OperCurrBuy        = 6,       /* Покупка платежеспособной валюты за рубли */
     CDF_OperCurrSale       = 7,       /* Продажа платежеспособной валюты за рубли */
     CDF_OperCurrConversion = 18,      /* Конверсия */
   CDF_OperTreasure         = 8,     /* Операции передачи ценностей */
     CDF_OperTransfer       = 9,       /* Передача ценности между кассирами */
     CDF_OperAvance         = 10,      /* Получение аванса */
     CDF_OperCashOut        = 11,      /* Сдача кассы */
     CDF_OperEvenRests      = 36,      /* Выравнивание остатков с Кассой Retail */
   CDF_OperCard             = 12,    /* Операции по картам */
     CDF_OperCardTake       = 13,       /* Начисление на карту */
     CDF_OperCardGive       = 14,       /* Снятие с карты */
   CDF_OperIncassoExpert    = 15,    /* Операции приема на инкассо или экспертизу */
     CDF_OperIncasso        = 16,       /* Прием на инкассо */
     CDF_OperExpert         = 17,       /* Прием на экспертизу */
     CDF_OperReturnIncasso  = 24,       /* Возврат с инкассо */
     CDF_OperReturnExpert   = 25,       /* Возврат с экспертизы */
     CDF_OperPayDocIncasso       = 27,  /* Прием ПД на инкассо     RA 11-06-99 */
     CDF_OperPayDocExpert        = 28,  /* Прием ПД на экспертизу  RA 11-06-99 */
     CDF_OperPayDocReturnIncasso = 29,  /* Возврат ПД с инкассо    RA 11-06-99 */
     CDF_OperPayDocReturnExpert  = 30,  /* Возврат ПД с экспертизы RA 11-06-99 */

     CDF_OperCheck        = 26,         /* Проверка на подлинность RA 30-07-99 */

   CDF_OperSplitChange   = 19,       /* Операции размена или замены */
     CDF_OperSplit        =  22,        /* Размен валюты */
     CDF_OperChange       =  23,        /* Замена неплатежеспособной валюты */

   CDF_OperDeficitOverplus  = 31,    /* Операции учета недостачи и излишков */
     CDF_OperDetectOverplus    = 32,    /* Отражение излишков               */
     CDF_OperGiveOutOverplus   = 33,    /* Выдача излишков                  */
     CDF_OperDetectDeficit     = 34,    /* Отражение недостачи              */
     CDF_OperCompensateDeficit = 35,    /* Возмещение недостачи             */

   CDF_OperPayDocAcc        = 37,    /* Операции покупки/продажи ПД через счет */
     CDF_OperPayDocBuyAcc      = 38,    /* Покупка ПД за рубли через счет   */
     CDF_OperPayDocSaleAcc     = 39,    /* Продажа ПД за рубли через счет   */
     CDF_OperPayDocBuyCurrAcc  = 40,    /* Покупка ПД за валюту через счет  */
     CDF_OperPayDocSaleCurrAcc = 41,    /* Продажа ПД за валюту через счет  */
   
    /* RA 23-12-99 "операции" - комиссии (нужны для контировки в Retail'е)  */
   CDF_OperComiss           = 90,
     CDF_OperFixCom            = 98,    /* Фиксированная комиссия           */
     CDF_OperPercCom           = 99,    /* Процентная комиссия              */


/* состояние справки БСО */
  CDF_SprvState   = 5,      /* Состояние справки БСО */
     CDF_SprvGetOut = 1,    /* Выдана     */
     CDF_SprvKill   = 2,    /* Уничтожена */
     CDF_SprvIncome = 3,    /* Получена   */

  CDF_SprvPackState = 22,    /* Состояние пачки справок БСО */
     CDF_SprvPackCashOffice = 1,    /* Касса                */
     CDF_SprvPackAvance     = 2,    /* Аванс                */
     CDF_SprvPackReturn     = 3,    /* Возврат\Передача     */

/* типы оборотов */
  CDF_Turn = 23,  /* Типы оборотов по операциям */
    CDF_TurnIncome        =  1, /* Полученная от клиента сумма  */
    CDF_TurnOut           =  2, /* Выданная клиенту сумма       */
    CDF_TurnCom           =  3, /* Общая комиссия               */
    CDF_TurnFix           =  4, /* Фиксированная комиссия       */
    CDF_TurnTaxWithCom    =  5, /* Налог с учетом комиссии      */
    CDF_TurnTaxWithoutCom =  6, /* Налог без учета комиссии     */
    CDF_TurnPComInc       =  7, /* Сумма процентной комиссии от полученной суммы    */
    CDF_TurnPComOut       =  8, /* Сумма процентной комиссии от выданной суммы      */
    CDF_TurnInCnvCoins    =  9, /* Сумма от безналичной конвертации валютной мелочи прихода */
    CDF_TurnMain          = 10, /* Основные суммы по операции (номинальные полученная и выданная) */
    CDF_TurnIncTax        = 11, /* Подоходный налог с резидентов                                  */
    CDF_TurnIncTaxNoRez   = 12, /* Подоходный налог с нерезидентов                                */
    CDF_TurnOutCnvCoins   = 13, /* Сумма от безналичной конвертации валютной мелочи расхода */
    CDF_TurnComCnvCoins   = 14, /* Сумма от безналичной конвертации валютной мелочи платы   */
    CDF_TurnIncomeGuarantee=15, /* Сумма принимаемого залога                                */
    CDF_TurnOutGuarantee  = 16, /* Сумма выдаваемого залога                                 */

  /*18-02-98  документы физического лица - кодификатор */
  CDF_PersonDoc = 10, /*группа в кодификаторе*/
     CDF_Residence      = 1,  /* Вид на жительство               */
     CDF_WarTicket      = 2,  /* Военный билет                   */
     CDF_PersDocOfficer = 3,  /* Удостоверение личности офицера  */
     CDF_ForeignPass    = 4,  /* Заграничный паспорт             */
     CDF_Passport       = 5,  /* Паспорт РФ                      */
     CDF_PassportUSSR   = 6,  /* Паспорт СССР                    */

  CDF_ReestrState = 13,  /* Состояние реестра                    */
     CDF_ReestrOpen     = 1,  /* Открыт                          */
     CDF_ReestrClosed   = 2,  /* Закрыт                          */
     CDF_ReestrStorno   = 3,  /* Сторнирован                     */

  /*AV 24-04-98*/
  CDF_ComType  = 17,  /* Типы комиссии */
     CDF_ComPercLTFix  = 1,   /* %, но не более фиксированной суммы   */
     CDF_ComPercGTFix  = 2,   /*   %, но не менее фиксированной суммы */
     CDF_ComPercAndFix = 3,   /*   Сумма % и фиксированной суммы      */
     CDF_ComFix        = 4,   /*   Фиксированная сумма                */
     CDF_ComNotUsed    = 5,   /*   Комиссии нет                       */
  /*AV 27-05-98*/
  CDF_TypeCalcRate = 25,  /* Тип пересчета сумм */
     CDF_CalcRateOper  = 0, /* По курсу операции */
     CDF_CalcRateCB    = 1, /* По курсу ЦБ       */
     CDF_CalcRateBuy   = 2, /* По курсу покупки  */
     CDF_CalcRateSale  = 3, /* По курсу продажи  */
  /*AV 19-06-98 */
  CDF_RateChngState    = 14,/* Состояние смены */
     CDF_CHANGE_ACTIVE  = 1,  /* Смена активна          */
     CDF_CHANGE_LOCKED  = 2,  /* Смена блокирована      */
     CDF_CHANGE_STOPED  = 3,  /* Смена завершила работу */
     CDF_CHANGE_NOBEG   = 4,  /* Смена не началась      */
  /*RA 18-12-98 */
  CDF_OperState         = 12, /* Состояние операции, проведенной в ОП */
     CDF_OperCompleted  =  1, /*   Завершена            */
     CDF_OperStarted    =  2, /*   Начата               */
     CDF_OperStorn      =  3, /*   Откатана             */
     CDF_OperCancel     =  4, /*   Отменена             */
     CDF_OperReestr     =  5, /*   В реестре            */
        CDF_OperInReestr=  6, /*     В реестре          */
        CDF_OperDeleted =  7, /*     Удалена            */


     SET_CHAR   = "X", /* VD  5.03.98 */
     UNSET_CHAR = "",  /* AR 28-12-98 */
     ANY_CURR   = -1,
     MAINRATEKIND = 1, /* RA 24-03-99 основной вид курсов валют */

/* Типы сумм при печати отчетов по ценностям ОП */
      TYPE_CURREST    = 0, /* текущий остаток по ценности */
      TYPE_AVANCE     = 1, /* аванс, полученный за смену   */
      TYPE_TEMP       = 2, /* сумма номиналов ценности, передаваемой в операции передачи ценностей */
      TYPE_TRANSFER   = 3, /* получено от других кассиров за смену */
      TYPE_CASHOUT    = 4, /* возврат кассы */
      TYPE_ALLAVANCE  = 5, /* все авансы за смену */
      TYPE_ALLAVANCE2 = 6, /* только аванс, полученный за смену без текущего  */

/* виды справок */
      BSO = 0,
      F31 = 1,

/* состояние пачки справок БСО */
      ПередачаПачки =  3, /* или Возврат пачки */
      АвансПачки    =  2,
      ПачкаВКассе   =  1,

/*RA  9-02-99 убрал
/*AV 13-02-98 типы вызова функции MakeNonDenomSumString*/
      IncNonDenSum = 0, /*сумма всех неденоминированных полученных */
      OutNonDenSum = 1, /*сумма всех неденоминированных выданных */
*/
/* максимальное количество строк на одной странице реестра */
      MaxNumberStrings = 20,

/* RA 28-12-98 коды клавиш */
      EX_ESC   =  27,
      EX_SPACE =  32,
      EX_F9    = 323;

array
  /* массив с итоговыми суммами по странице реестра (по колонкам) */
  TotalsPage;

var BankStandartExch = 0;

/****************************************************************************/
/****************************************************************************/

/****************************************************************************/
/* функция преобразовывет число в строку заданной длины дополняя ее нулями  */
/* при переполнении строки число циклически преобразуется                   */
/****************************************************************************/
MACRO AddZero( Num, Len/*, FillSimb*/ )
    VAR
    StrZero = "0",                  /*строка-заполнитель*/
    MaxNum = int( MkStr("9", Len) ),/*максимально возможное число*/
    StrResult = MkStr(" ", Len),    /*строка-результат*/
    N,                              /*индекс переполнения строки*/
    NumS,                           /*переменная для работы со строками*/
    FillSimb;

    if( GetParm( 2, FillSimb ) )
       StrZero = FillSimb;
    end;

    N = doublel(Num)/doublel(MaxNum);
    if( N > 1. )
       Num = Num - int(N) * MaxNum;
    end;
    NumS = StrLen( string(Num) );
    if( NumS < Len )
       NumS = Len - NumS;/*кол-во добавляемых нулей*/
       StrSet( StrResult, NumS+1, string(Num) );
       while( NumS != 0 )
          StrSet( StrResult, NumS, StrZero );
          NumS = NumS - 1;
       end;
    else StrResult = string(Num);
    end;
    return StrResult;
END;

/****************************************************************************/
/* Функция преобразует дату в строку дополняя при необходимости число нулем */
/*  в формате: 09 ноября 1998 года                                          */
/****************************************************************************/
MACRO StrAddZeroDate( Date )
   var Day, Mon, Year, StrDate;
   DateSplit( Date, Day, Mon, Year );
   StrDate = string( Date:m );
   if( Day < 10 )
      StrSet( StrDate, 1, "0" );
   end;
   return StrDate;
END;
/****************************************************************************/
/* Функция преобразует дату в строку дополняя при необходимости число нулем */
/*  в формате: 09.11.98                                                     */
/****************************************************************************/
MACRO StrAddZeroDate_Short( Date )
   var Day, Mon, Year, StrDate;
   DateSplit( Date, Day, Mon, Year );
   StrDate = string( Date );
   if( Day < 10 )
      StrSet( StrDate, 1, "0" );
   end;
   return StrDate;
END;

MACRO PrintDate_n_Time()
[ #                 # 
](
 StrAddZeroDate_Short( Date ),
 Time
 );

END;

/****************************************************************************/
/* Функция добавляет элементы в Массив начиная с позиции Индекс             */
/****************************************************************************/
MACRO ДобавитьВМассив( Массив, Индекс /*[Элемент1,Элемент2,....]*/ )
   var i = 0, Элемент;
   While( GetParm( (2 + i), Элемент ) )
      Массив( Индекс + i ) = Элемент;
      i = i + 1;
   end;
   return i;
END;

/****************************************************************************/
/*  Процедура проверяет наличие числа в массиве                             */
/****************************************************************************/
MACRO CheckNumber( Arr, Number, All )
 var i = 0;
 if( All == 0 ) return -1; end;
 While( i < All )
    if( Arr(i) == Number ) return i;/*номер в массиве есть*/
    else  i = i + 1;
    end;
 end;
 return -1;/*нет такого номера в массиве*/
END;

/***************************************************************************/
/* Процедура заносит число в массив, если такого числа в массиве еще нет   */
/***************************************************************************/
MACRO PutNumberInArray( Arr, Number, All )
 var RetNum = CheckNumber( Arr, Number, All );
 if( (RetNum == -1) )
    Arr( All ) = Number;
    RetNum = All;
    All = All + 1;
    SetParm( 2, All );/*всего элементов - глобализм*/
 end;
 return RetNum;
END;

/****************************************************************************/
/* Процедура переносит определенный элемент массива на определенное место   */
/****************************************************************************/
MACRO MoveElementArray( Array_Ref, Element, Place, AllElements )
   var i = 0, SavePlaceElement;
   while( i < AllElements )
      if( (Array_Ref( i ) == Element) AND (i != Place) )
         /*проверка на то, что элемент номер Place - задан*/
         if( ValType(Array_Ref( Place )) != V_UNDEF )
            SavePlaceElement = Array_Ref( Place );
         else
            SavePlaceElement = 0;
         end;
         Array_Ref( Place ) = Element;
         Array_Ref( i ) = SavePlaceElement;
         return 0;/*перенос выполнен*/
      end;
      i = i + 1;
   end;
   return -1;/*переноса не было*/
END;
/**************************************************************************/
/* Получить полное имя пути из составляющих. Возвращает полное имя файла. */
/***** (c) Сияльский Влад *************************************************/
MACRO fnmerge
(
  Path,         /* Сюда помещается полное имя, оно же возвращается. */
  Dir,          /* Директория вместе с диском                       */
  Fname,        /* Имя файла без расширения                         */
  Ext           /* Расширение                                       */
)
  Path = "";
  Dir = trim(Dir);  Fname = trim(Fname);  Ext = trim(Ext);
  if( Dir != "" )
    if( substr( Dir, strlen(Dir), 1 ) != "\\" )
      Dir = Dir + "\\";
    end;
    Path = Path + Dir;
  end;
  if( Fname != "" )
    Path = Path + Fname;
    if( Ext != "" )
      if( substr( Fname, strlen(Fname), 1 ) != "." )
        Path = Path + ".";
      end;
      if( substr( Ext, 1, 1 ) == "." ) Ext = substr( Ext, 2 ); end;
      Path = Path + Ext;
    end;
  end;
  setparm( 0, Path );
  return Path;
END;

/****************************************************************************/
/* RA 19-06-01                                                              */
/* процедура получает строку номиналов по операции размена\замены           */
/****************************************************************************/
MACRO GetSplitNominal( Operat )
 
 record change ("change.str","exchange.def");

 SetRecordAddr( change, Operat, 0, FldOffset( Operat,"ExtInfo"), True );
 return change.NominalSplitStr;

END;

/****************************************************************************/
/* RA 20-06-02                                                              */
/* процедура получает номинал принятых ценностей                            */
/****************************************************************************/
MACRO GetIncomeNominal( Operat )
 
 record change ("change.str" ,"exchange.def");
 record currop ("curr_op.str","exchange.def");
 var IncomeNominal = $0.0;

 if  ( Operat.Kind_Oper == CDF_OperChange )
   SetRecordAddr( change, Operat, 0, FldOffset( Operat,"ExtInfo"), True );
   IncomeNominal = change.IncomeNominal;
 elif( Operat.Kind_Oper == CDF_OperUnpayCurrBuy )
   SetRecordAddr( currop, Operat, 0, FldOffset( Operat,"ExtInfo"), True );
   IncomeNominal = currop.IncomeNominal;
 end;
 return IncomeNominal;

END;

/***************************************************************************/
/* Процедура проверяет является ли справка Серия2 Номер2 следующей за      */
/*    справкой Серия1 Номер1                                               */
/***************************************************************************/
MACRO ПроверкаСправки( Серия1, Номер1, Серия2, Номер2 )
   var Ном1, Ном2;
   if( Серия1 == Серия2 )
      Ном1 = Int( Номер1 );
      Ном2 = Int( Номер2 );
      if( ((Ном2 - Ном1) == 1) )
         return True;
      end;
   end;
   return False;
END;

/****************************************************************************/
/* Процедура проверяет принадлежность операции                              */
/****************************************************************************/
MACRO ПроверкаОперацииПоСсылке( Ссылка, Сотрудник, НомерСмены )
   FILE   OperID  ( "operat.dbt", "exchange.def" ) key 0; /* ID */

   OperID.ID = Ссылка;
   if( GetEQ( OperID ) )
      /* проверка на принадлежность сотруднику */
      if( (Сотрудник != 0) AND (OperID.Emp_Ref != Сотрудник) )
         return False;
      end;
      /* проверка на принадлежность смене */
      if( (НомерСмены != 0) AND (OperID.Sess_Ref != НомерСмены) )
         return False;
      end;
      return True;
   end;
   return False;
END;

/**RA 4-08-99 перенес из repoper.mac*****************************************/
/* Возвращает курс ЦБ по коду валюты номеру смены курсов                    */
/****************************************************************************/
MACRO ПолучитьКурсЦБ( КодВалюты, НомерСменыКурсов/*т.е. oper.Rate_Ref*/ )
   FILE Rates   ("cur_rate.dbt", "exchange.def" ) key 0;

   Rates.Change_Ref = НомерСменыКурсов;
   Rates.Curr_Ref   = КодВалюты;
   Rates.RateFlag   = SET_CHAR;
   if( GetEQ( Rates ) )
      return Rates.CBRate / Rates.ChangeNom;
   else return 0;
   end;
END;

/****************************************************************************/
/* Получить название валютнообменной операции                               */
/****************************************************************************/
MACRO GetNameExchOperation( Branch, Kind_Oper, SubOper )
  FILE Operset ( "exopset.dbt", "exchange.def" );

  ClearRecord( Operset );
  Operset.Branch    = Branch;
  Operset.Kind_Oper = Kind_Oper;
  Operset.SubOper   = SubOper;

  if( GetEQ( Operset ) )
    return Operset.Name;
  else
    return "";
  end;
END;


/****************************************************************************/
/* Получить внешний код валютнообменной операции (например, по 113-И)       */
/****************************************************************************/
MACRO GetCodeExchOperation( Branch, Kind_Oper, SubOper )
  FILE listfdep ( "listfdep.dbt","sbbank.def" );
  FILE Operset ( "exopset.dbt", "exchange.def" );
  ClearRecord( listfdep );
  listfdep.fncash = Branch;
  if( GetEQ( listfdep ) )
    if ( listfdep.ownexoperset )
      Branch = Branch;
    else
      Branch = 0;
    end;
  end;

  ClearRecord( Operset );
  Operset.Branch    = Branch;
  Operset.Kind_Oper = Kind_Oper;
  Operset.SubOper   = SubOper;

  if( GetEQ( Operset ) )
    return Operset.Code113i;
  else
    Operset.Branch = 0;
    if( GetEQ( Operset ) )
      return Operset.Code113i;
    else
      return "";
    end;
  end;
END;


/****************************************************************************/
/* Получить числовой код валюты                                             */
/****************************************************************************/
MACRO GetCurrNumericalCode( Code_Currency )
  FILE Currency ( "currency.dbt", "sbbank.def" );

  if( ValType(Code_Currency) == V_UNDEF )
    return "";
  end;

  ClearRecord( Currency );
  Currency.Code_Currency = Code_Currency;

  if( GetEQ( Currency ) )
    return Currency.ExternalCode;
  else
    return "";
  end;
END;

/****************************************************************************/
/* Получить название валюты                                                 */
/****************************************************************************/
MACRO GetCurrNameCurrency( Code_Currency )
  FILE Currency ( "currency.dbt", "sbbank.def" );

  if( ValType(Code_Currency) == V_UNDEF )
    return "";
  end;

  ClearRecord( Currency );
  Currency.Code_Currency = Code_Currency;

  if( GetEQ( Currency ) )
    return Currency.Name_Currency;
  else
    return "";
  end;
END;

/****************************************************************************/
/* Формирует строку - курс для валюты Code_Currency с нужной точеостью             */
/****************************************************************************/
MACRO КурсСНужнойТочностью( Rate, Code_Currency )

  FILE Currency ( "currency.dbt", "sbbank.def" );
  var  StrRate, Scale = 4; /* по умолчанию */
  
  ClearRecord( Currency );
  Currency.Code_Currency = Code_Currency;

  if( GetEQ( Currency ) )
    Scale = Currency.PointOfc;
  end;

  StrRate = "String( Rate:0:" + Scale + ")";
 
  return ExecExp( StrRate );

END;
