/*-RSL---------------------------------------- R-Style Software Lab --*/
/*--------------- Печать реестра. Общие функции ----------------------*/
/* RS-Bank      подсистема Обменный пункт            AV  14.05.97     */
/* RA  9-02-99   закомментировал упоминание неденоминированных сумм   */
/* RA 15-03-99   добавил функцию получения строки номиналов ПД        */
/* VS 23.08.2000 Добавлен поиск реестра по НЕключевому полю "Эмитент".*/
/* 22.11.2000 VS Закончена печать раздельных и аккумулированных
                 реестров - модифицирована FindReestr( r ).
*/
IMPORT ExchangeInter, DeprIntr, DeskInter, BankInter, rsd, 
       "define.mac", "od_comm.mac", "office.mac";

file   Реестр        ( "reestr.dbt"  , "exchange.def" ) key 4;
file   СоставРеестра ( "exoperat.dbt", "exchange.def" ) key 6;
record paydocop      ( "paydocop.str", "exchange.def" );
record r             ( "reestr.dbt"  , "exchange.def" ); /*  буфер */
file   casln         ( "sb_casln.dbt", "sbbank.def"   ) key 0;
file   depdoc        ( "sbdepdoc.dbt", "sbbank.def"   ) key 3;

file   casvl         ( "sb_casvl.dbt", "sbbank.def"   ) key 3;


const REESTR_PD_BY_KIND     = 1,
      REESTR_PD_BY_ISSUER   = 2,
      REESTR_PD_BY_CURRENCY = 0,

      REESTR_CARD_BY_KIND     = 1,
      REESTR_CARD_BY_ISSUER   = 2,
      REESTR_CARD_BY_CURRENCY = 0;

var IdentValue_For_GetSprvSerNum,
    Seria_For_GetSprvSerNum,
    Number_For_GetSprvSerNum;

var PRINT_CURRENCY = FALSE; /* печатать в заголовке всех реестров название валюты */

var ACCUMULATE_REESTR = False;  /* При печати сводить в один реестры по ПД или картам,
                                   разделенные по эмитентам или по видам ПД/карт. */
/* 21.11.2000 VS !!! Будем настраивать ее индивидуально для каждого типа реестров
                 прямо в макросах. Это позволит НЕ консолидировать какие-либо
                 реестры по желанию пользователя, несмотря на настройки.
*/

macro ОбщаяШапка( DateReestr, NumReestr, NumPage, FlagLargeForm )

  var NeedLargeForm = FALSE;

  if (ValType(FlagLargeForm) == V_BOOL)
    NeedLargeForm = FlagLargeForm
  end;

  if (NeedLargeForm)
[
  #################################################################################   ОКПО      ###############

  #################################################################################   Рег.номер ###############

   ___________#_____________                                                          _______________#________________
   (дата заполнения реестра)                                                               (учетный номер реестра)
]( {ExName_Bank}:w,
  {OKPO},
  {ExAddress}:w,
  {ExRegNumber},
  StrAddZeroDate_Short( DateReestr ):c,
  NumReestr + "." + Trim(string(NumPage)):c
 );
  else
[
  ##########################################################################
                                                ОКПО      ###############
  ##########################################################################
                                                Рег.номер ###############

   ___________#_____________               _______________#________________
   (дата заполнения реестра)                    (учетный номер реестра)
]( {ExName_Bank}:w,
  {OKPO},
  {ExAddress}:w,
  {ExRegNumber},
  StrAddZeroDate_Short( DateReestr ):c,
  NumReestr + "." + Trim(string(NumPage)):c
 );
  end;

end;

macro  ФильтрШапки( Kind_Oper )
 if(     (Реестр.Branch    == Branch     )  /* N ОП в банке     */
     AND (Реестр.CurDate   == {curdate}  )  /* Дата опердня     */
     AND (Реестр.Oper      == {exoper}   )  /* Код сотрудника   */
     AND (Реестр.Kind_Oper == Kind_Oper  )  /* Вид операции     */
   )
   return True;
 end;
 return False;
end;

macro ФильтрРеестр( )
  if(     ( СоставРеестра.Branch     == Реестр.Branch )
      AND ( СоставРеестра.Reestr_Ref == Реестр.ID     )
    )
     return True;
  end;
  return False;
end;

macro ФильтрСтрокаРеестра( )

  var _retValue = true; 

  if(    ( СоставРеестра.Action == D_INSERT )
      OR ( СоставРеестра.Action == D_UPDATE )
      OR ( СоставРеестра.Action == D_STORN  )
    )
     ;
  else
    _retValue = false; 
  end;

  if ( _retValue )
    if ( ( СоставРеестра.IsSuspended == "" ) or ( СоставРеестра.IsSuspended == "X" ) )
      ;
    else
      _retValue = false; 
    end;
  end;

  return _retValue;

end;

/* Инициализирует начальные сегменты ключа и позиционируется на первом
   реестре этого ТИПА (в любом сочетании валют).
*/
macro InitReestrKey( Kind_Oper )

   ClearRecord( Реестр );
   Реестр.Branch         = Branch;           /* N ОП в банке          */
   Реестр.CurDate        = {curdate};        /* Дата опердня          */
   Реестр.Oper           = {exoper};         /* Код сотрудника        */
   Реестр.Kind_Oper      = Kind_Oper;        /* Вид операции          */
   Реестр.IncomeRefVal   = ANY_CURR;         /* Полученная ценность   */
   Реестр.OutRefVal      = ANY_CURR;         /* Выданная ценность     */
   Реестр.Rate_Ref       = 0;                /* ID смены курсов валют */
   Реестр.ID             = 0;                /* ID смены реестра      */

   if( getGE( Реестр ) == True )
      return (
                   (Реестр.Branch    == Branch     )
               AND (Реестр.CurDate   == {curdate}  )
               AND (Реестр.Oper      == {exoper}   )
               AND (Реестр.Kind_Oper == Kind_Oper  )
             );
   end;

   return False;
end;

/*-------------------------------------------------------------------------*/
/*   поиск  определенного реестра                                          */
/* !!! Осуществляет позиционирование файла Реестр на записи r.             */
/*-------------------------------------------------------------------------*/
macro FindReestr( r )
  var RetVal = False,
      Loop;

  macro ReestrKeyFilter()
    return (
                 ( Реестр.Branch       == r.Branch       ) /* N ОП в банке          */
             AND ( Реестр.CurDate      == r.CurDate      ) /* Дата опердня          */
             AND ( Реестр.Oper         == r.Oper         ) /* Код сотрудника        */
             AND ( Реестр.Kind_Oper    == r.Kind_Oper    ) /* Вид операции          */
             AND ( Реестр.IncomeRefVal == r.IncomeRefVal ) /* Полученная ценность   */
             AND ( Реестр.OutRefVal    == r.OutRefVal    ) /* Выданная ценность     */
             AND ( Реестр.Rate_Ref     == r.Rate_Ref     ) /* ID смены курсов валют */
             AND ( Реестр.ID           == r.ID           ) /* ID смены реестра      */
           );
  end;

  ClearRecord( Реестр );
  Реестр.Branch         = r.Branch;        /* N ОП в банке          */
  Реестр.CurDate        = r.CurDate;       /* Дата опердня          */
  Реестр.Oper           = r.Oper;          /* Код сотрудника        */
  Реестр.Kind_Oper      = r.Kind_Oper;     /* Вид операции          */
  Реестр.IncomeRefVal   = r.IncomeRefVal;  /* Полученная ценность   */
  Реестр.OutRefVal      = r.OutRefVal;     /* Выданная ценность     */
  Реестр.Rate_Ref       = r.Rate_Ref;      /* ID смены курсов валют */
  Реестр.ID             = r.ID;            /* ID смены реестра      */

  RetVal = getEQ( Реестр );

  /* 22.11.2000 VS Для реестров по ПД и карточкам дополнительный поиск по
                  неключевым реквизитам, если это установлено в настройках. */
  /* Ищем реестр по НЕключевому полю "Эмитент" */
  if(
      ( (r.Kind_Oper == CDF_OperPayDocBuy        ) or /* Реестр ПД, купленных за наличные рубли */
        (r.Kind_Oper == CDF_OperPayDocSale       ) or /* Реестр ПД, проданных за наличные рубли */
        (r.Kind_Oper == CDF_OperPayDocBuyCurr    ) or /* Реестр ПД, оплаченных наличной иностранной валютой  */
        (r.Kind_Oper == CDF_OperPayDocSaleCurr   ) or /* Реестр ПД, проданных за наличную иностранную валюту */
        (r.Kind_Oper == CDF_OperPayDocBuyAcc     ) or /* Реестр ПД, купленных за рубли через счет */
        (r.Kind_Oper == CDF_OperPayDocSaleAcc    ) or /* Реестр ПД, проданных за рубли через счет */
        (r.Kind_Oper == CDF_OperPayDocBuyCurrAcc ) or /* Реестр ПД, оплаченных иностранной валютой через счет */
        (r.Kind_Oper == CDF_OperPayDocSaleCurrAcc)    /* Реестр ПД, проданных за иностранную валюту через счет*/
      )
      and (( Office.ReestrPDSeparator == REESTR_PD_BY_ISSUER) or
           ( Office.ReestrPDSeparator == REESTR_PD_BY_KIND  ))
    )
    Loop   = RetVal;
    RetVal = False;

    while ( Loop and ReestrKeyFilter( ) )
      if ( Office.ReestrPDSeparator == REESTR_PD_BY_ISSUER )
        if ( Реестр.Issuer == r.Issuer )
          RetVal = True;
          Loop = False;
        else
          Loop = next( Реестр );
        end;
      else /* (Office.ReestrPDSeparator == REESTR_PD_BY_KIND  ) */
        if( (r.IncomePayDoc_Ref and (Реестр.IncomePayDoc_Ref == r.IncomePayDoc_Ref)) or
            (r.OutPayDoc_Ref    and (Реестр.OutPayDoc_Ref    == r.OutPayDoc_Ref   ))
          )
          RetVal = True;
          Loop = False;
        else
          Loop = next( Реестр );
        end;
      end;
    end;

  elif( (r.Kind_Oper == CDF_OperCardTake) or /* Реестр по Начислениям на карту */
        (r.Kind_Oper == CDF_OperCardGive)    /* Реестр по Снятиям с карты      */
        and ((Office.ReestrCardSeparator == REESTR_CARD_BY_ISSUER) or
             (Office.ReestrCardSeparator == REESTR_CARD_BY_KIND  ))
      )
    Loop   = RetVal;
    RetVal = False;

    while( Loop and ReestrKeyFilter() )
      if( Office.ReestrCardSeparator == REESTR_CARD_BY_ISSUER )
        if( Реестр.Issuer == r.Issuer )
          RetVal = True;
          Loop = False;
        else
          Loop = next( Реестр );
        end;
      else /* (Office.ReestrCardSeparator == REESTR_CARD_BY_KIND  ) */
        if( Реестр.CardType == r.CardType )
          RetVal = True;
          Loop = False;
        else
          Loop = next( Реестр );
        end;
      end;
    end;

  end;

 return RetVal;
end;

macro InitSetRstrKey()
  СоставРеестра.Branch     = Реестр.Branch;
  СоставРеестра.Reestr_Ref = Реестр.ID;
  return getEQ( СоставРеестра );
end;

/* VD  5.03.98 печать резидент/нерезидент прописью */
macro MakeResidentName( ResidentFlag )
  if( ResidentFlag == SET_CHAR )
    return "резидент";
  else
    return "нерезидент";
  end;
end;

/****************************************************************************/
/*  Функции для получения информации о комиссии по операции                 */
/****************************************************************************/
/* AV 29-04-98                                                           */
/*  Функция формирует строку с информацией о комиссии по операции.       */
/*  Учитывается процентная комиссия в валюте расчетной суммы.            */
/* в случае если валюта процентной комиссии отличается от фиксированной  */
/*  результирующая строка может иметь следующий вид:                     */
/*    (например) 1 RUR + .5 USD                                          */
macro MakeComissString()
 var ComString;

 if(     ( СоставРеестра.ComAmount )
     AND ( СоставРеестра.ComCurrRefVal != ANY_CURR )
   )
   ComString = GetCurrNumericalCode( FindCodCurByRefVal( СоставРеестра.ComCurrRefVal ) )
             + " " + string( СоставРеестра.ComAmount );
 else
   ComString = "     0.0";
 end;

 return ComString;
end;

/*************************************************************************/
/* AV 5-05-98  Функции для печати суммарной комиссии по реестру          */
/*************************************************************************/
/*  7.06.2000 VS Заменил названия массивов Sum и Curr на ArSum и ArCurr. */
ARRAY ArSum, /* суммы комиссий  */
      ArCurr;/* валюты комиссий */

/* Процедура заполняет массив с суммами разновалютных комиссий для        */
/* текущего реестра                                                       */
/* AV 10-09-98 теперь суммирую и заношу в массив абсолютные значения сумм */
MACRO GetSumReestrComiss()
  var Num  = asize(ArCurr)/*0 VS*/;/* количество разных валют комиссии по реестру */
  var Loop;
  var NumCurr;    /* № текущей валюты в массиве ArCurr */

  /* проверка - есть ли уже комиссия с такой валютой                     */
  macro CheckCurrComiss( Curr_Ref, Num )
     var i = 0;

     while( i < asize(ArCurr)/*Num VS 26.09.2000*/ )
        if( ArCurr( i ) == Curr_Ref )
           return i;
        end;
        i = i + 1;
     end;

     return -1;
  end;

  ClearRecord( СоставРеестра );
  СоставРеестра.Branch     = Реестр.Branch;
  СоставРеестра.Reestr_Ref = Реестр.ID;
  Loop = GetGE( СоставРеестра );

  while( Loop
         AND ( СоставРеестра.Branch     == Реестр.Branch )
         AND ( СоставРеестра.Reestr_Ref == Реестр.ID     )
       )
    if( ФильтрСтрокаРеестра() ) /* отсекаем удаленные */
       NumCurr = CheckCurrComiss( FindCodCurByRefVal( СоставРеестра.ComCurrRefVal), Num );
       /* увеличить сумму в массиве */
       if( NumCurr >= 0 )
          ArSum( NumCurr ) = ArSum( NumCurr ) + /*abs*/(СоставРеестра.ComAmount);
       /* добавить элемент массива */
       else
          ArSum (Num) = /*abs*/(СоставРеестра.ComAmount);
          ArCurr(Num) = FindCodCurByRefVal( СоставРеестра.ComCurrRefVal );
          Num = Num + 1;
       end;
    end;
    Loop = Next( СоставРеестра );
  end;

  return Num;
END;

/* Процедура возвращает строку с суммами разновалютных комиссий для       */
/* текущего реестра                                                       */
MACRO MakeStrReestrComiss()
  var StrResult = "";
  var StrPlus   = "";
  /* формируем массив с разновалютными суммами комиссии */
  var NumCurr = GetSumReestrComiss();
  var i = 0;

  while( i < NumCurr )
     if( StrResult != "" ) StrPlus = " + "; end;
     
     StrResult = StrResult + StrPlus + GetCurrNumericalCode( ArCurr( i ) )
               + " " + string( /*abs*/(ArSum( i )) );

     i = i + 1;
  end;

  return StrResult;
END;

/* Процедура увеличивает n-ый элемент массива TotalsPage на Сумма */
MACRO AddTotalsPage( n, Сумма )
   TotalsPage( n ) = TotalsPage( n ) + Сумма;
END;
/* Процедура обнуляет первые n элементов массива TotalsPage */
MACRO ClearTotalsPage( n )
   var i = 0;
   While( i < n )
      TotalsPage( i ) = 0;
      i = i + 1;
   end;
END;

/* ( вспомогательный макрос для GetSprvSerNum ) */
MACRO FillSerNum_For_GetSprvSerNum( Credit, Debet )
  if( TmpCashVal.IdentValue == IdentValue_For_GetSprvSerNum )
    Seria_For_GetSprvSerNum  = CashDoc.Series;
    Number_For_GetSprvSerNum = CashDoc.MinRegistrNumber;
  end;
END;

/* Процедура получает серию и номер ресурса из кассового док. по опрации */
MACRO GetSprvSerNum( AppKind, AppKey, IdentValue, Seria, Number )

  Seria_For_GetSprvSerNum  = "";
  Number_For_GetSprvSerNum = 0;

  /* стать куда-нибудь - для GetPos */
  Rewind( CashVal );
  Next  ( CashVal );
  IdentValue_For_GetSprvSerNum = IdentValue;
  CalcValuesTurnsEx( AppKind, AppKey, null, null, 0, @FillSerNum_For_GetSprvSerNum );
  SetParm( 3, Seria_For_GetSprvSerNum  );
  SetParm( 4, Number_For_GetSprvSerNum );
END;

var Строка_СерийНомеров,
    RefValue_For_GetStringSerNum,
    NumDoc_For_GetStringSerNum,
    NumOper_For_GetStringSerNum;

/* ( вспомогательный макрос для ПолучитьСтрокуСерийНомеров ) */
MACRO GetStringSerNum( Credit, Debet )
  var Разделитель = ", \n";
  if(     ( TmpCashVal.RefValue == RefValue_For_GetStringSerNum )
      and (    ( NumOper_For_GetStringSerNum == 0               )
            or ( CashDoc.NumOper == NumOper_For_GetStringSerNum ) )
    )
    if( Строка_СерийНомеров != "" )
      Строка_СерийНомеров = Строка_СерийНомеров + Разделитель;
    end;
    Строка_СерийНомеров = Строка_СерийНомеров 
                        + Trim(CashDoc.Series) + " "
                        + CashDoc.MinRegistrNumber;
    if( CashDoc.MinRegistrNumber != CashDoc.MaxRegistrNumber )
      Строка_СерийНомеров = Строка_СерийНомеров + " - "
                          + CashDoc.MaxRegistrNumber;
    end;
    NumDoc_For_GetStringSerNum = NumDoc_For_GetStringSerNum + CashDoc.ValueCol;
  end;
END;

/****************************************************************************/
/* процедура получает строку серий-номеров ценностей по операции            */
/* NumOper - Приход / Расход  ( 0 или не не задан - все )                   */
/*  CASHOP_DEBIT     = 5,  /* Расход */                                     */
/*  CASHOP_CREDIT    = 6,  /* Приход */                                     */
/*                                                                          */
/* в NumDoc возвращает количество документов                                */
/****************************************************************************/
MACRO ПолучитьСтрокуСерийНомеров( AppKind, AppKey, RefValue/*, NumOper, NumDoc*/ )
  Строка_СерийНомеров         = "";
  NumDoc_For_GetStringSerNum  = 0;
  if( not GetParm( 3, NumOper_For_GetStringSerNum ) ) 
    NumOper_For_GetStringSerNum = 0;  
  end;
  /* стать куда-нибудь - для GetPos */
  Rewind( CashVal );
  Next  ( CashVal );
  RefValue_For_GetStringSerNum = RefValue;
  CalcValuesTurnsEx( AppKind, AppKey, null, null, 0, @GetStringSerNum );

  SetParm( 4, NumDoc_For_GetStringSerNum );
  return Строка_СерийНомеров;
END;

/* процедура получает номер вкладного счета для ВОО, если одна из сумм шла не наличными */
MACRO ПолучитьВкладнойСчетДляВОО( ApplicationKind, ApplicationKey )

  var   stat;
  const SB_DEPOSIT = 1;
  var cmd, rs;
  var retAccount = "";

  cmd = RsdCommand( "select depdoc.t_Account as account " +
                    "from dsbdepdoc_dbt depdoc, dsb_casln_dbt sb_casln " +
                    "where depdoc.t_iApplicationKind = sb_casln.t_ApplicationKind2 " +
                      "and depdoc.t_ApplicationKey = sb_casln.t_ApplicationKey2 " +
                      "and sb_casln.t_ApplicationKind1 = :applKind " +
                      "and sb_casln.t_ApplicationKey1 = :applKey " +
                      "and sb_casln.t_ApplicationKind2 = 1" ); // SB_DEPOSIT

  cmd.addParam( "applKind", RSDBP_IN, ApplicationKind );
  cmd.addParam( "applKey", RSDBP_IN, ApplicationKey );

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    retAccount = rs.value( "account" );

    if ( ( ValType( retAccount ) == V_UNDEF ) or ( retAccount == StrFor( 1 ) ) )
      retAccount = "";
    end;
  end;

  return retAccount;
END;

macro GetCasValShortName (Type, TypeValue, CodIntValue)
/* Возвращает короткое имя вида ресурса кассы */
  var CasValName = "???";
  var saveKey = KeyNum (CashVal, 3);
  ClearRecord (CashVal);
  CashVal.TypeValue = TypeValue;
  CashVal.CodIntValue = CodIntValue;
  CashVal.Type = Type;
  if (GetEQ (CashVal))
    CasValName = CashVal.IdentValue;
  end;
  KeyNum (CashVal, saveKey);
  return CasValName;
end;

macro GetBSOShortName ()
  return GetCasValShortName (0,2,6);
end;