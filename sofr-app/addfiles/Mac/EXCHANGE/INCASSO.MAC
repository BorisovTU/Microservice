/*-RSL---------------------------------------- R-Style Software Lab --*/
/*--------------- Печать квитанции о приеме на инкассо ---------------*/
/* RS-Bank      подсистема Обменный пункт   AV  14.05.97              */
/* 14.11.2000 VS Адаптирован для Оперкассы.                           */

import CommonInter, ExchangeInter, "rstrfunc.mac", "string.mac", "sprvfunc.mac";

record i ("b_incaso.str", "exchange.def");

private var {Post_Addr_Branch};

private const PTCK_BANKREGNUM = 13;  /* Вид субъекта: регистр. номер нашего банка. */

private var ddep  = TRecHandler( "i_dp_dep.rec" );
private var party = TRecHandler( "i_party.rec" );


/***************************************************************************/

MACRO ШапкаСправкиОПриемеIncasso()

  var regNumBank;
  var ordNumBank;
  var mainBranch = getFilialNum( );
  var curBranch = NumFNCash( );
  var nameMain;
  var nameCur;

  regNumBank = {ExBankNumber};
  ordNumBank = {ExRegNumber};

  // {Post_Addr}
  if ( findDepartment( mainBranch, NULL, ddep, party ) )
    if ( party.rec.Address != "" )
      nameMain = party.rec.Address;
    else
      nameMain = {Post_Addr};
    end;
  end;

  // {Post_Addr_Branch}
  if ( findDepartment( curBranch, NULL, ddep, party ) )
    if ( party.rec.Address != "" )
      nameCur = Trim( party.rec.Name + "; " + party.rec.Address );
    else
      nameCur = Trim( party.rec.Name + "; " + {Post_Addr_Branch} );
    end;
  end;
  
[

   Полное (сокращенное) фирменное наименование   +--------------------------+
   ############################################  |##########################|
                                                 +--------------------------+

   Регистрационный номер уполномоченного банка   +-------+   +-------+
   (порядковый номер филиала)                    | ##### | / | ##### |
                                                 +-------+   +-------+

   Местонахождение (адрес) уполномоченного банка +--------------------------+
   #####################                         |##########################|
                                                 +--------------------------+

   Наименование внутреннего структурного         +--------------------------+
   ###################################           |##########################|
                                                 +--------------------------+


] 
( 
 "уполномоченного банка (наименование филиала)",
 {Name_Bank}:w, 
  regNumBank,
  ordNumBank,
  "(филиала)",
  nameMain:w,
  "подразделения уполномоченного банка и его местонахождение (адрес)":w,
  nameCur:w
);


END;

macro PrintReference( Incasso )
  ARRAY SD;
  ARRAY arrAddress;
  
  SetBuff( i, Incasso );
  var Seria, Number;
  
  /* Информация о клиенте */
  var FullName = i.Surname + " " + i.FirstName + " " + i.Patronymic;
  var DocInfo  = i.DocName + " " + i.DocSer + " " + i.DocNum;
  
  /* Информация о ценности */
  var Full = i.TrsName + ", " + trim(i.NameNominal);

  if ( ( StrLen( i.Seria ) > 0 )  OR  ( i.Numb > 0 ) )
    Full = Full + ", " + RegNumToStr( i.Seria, i.Numb ) + ", ";
  end;

  if( i.YearIssuer ) Full = Full + i.YearIssuer + ", "; end;
  Full = Full + i.Issuer;
  StrSplit( Full, SD, 73, 73, 3 );
  
  StrSplit( i.Address, arrAddress, 72, 72, 5);

  ШапкаСправкиОПриемеIncasso( ); /* печать общей шапки */
  
  /* печать содержимого квитанции */
[

                                 Квитанция № #
                о приеме на инкассо денежных знаков иностранных
             государств (группы иностранных государств) или чеков



                                       #

   Настоящая квитанция выдана в подтверждение того, что у,

   ____________________________________#___________________________________,
                    (Ф.И.О. физического лица)

   документ, удостоверяющий личность:
   ____________________________________#___________________________________,
                         (наименование, серия, номер)

   ____________________________________#___________________________________,
                         (каким органом и когда выдан)

   адрес места жительства (места пребывания):
   #_______________________________________________________________________
   #_______________________________________________________________________,
                                        
   был принят на инкассо денежный знак иностранного государства (группы 
   иностранных государств) (чек):

   #________________________________________________________________________

   #________________________________________________________________________

   #________________________________________________________________________
   (для денежных знаков иностранного государства  (группы  иностранных госу-
   дарств): наименование валюты, номинал, серия, номер, год  выпуска, допол-
   нительные реквизиты (для банкнот США - буквенно-цифровое  значение, чеко-
   вая буква, номер квадранта, номер клише лицевой  и  оборотной сторон; для 
   банкнот стран-членов Европейского  союза - буквенно-цифровая метка), наи-
   менование эмитента (при возможности его указания), количество;

   Для чеков: наименование, его номер и дата, сумма с указанием наименования 
   валюты, наименования эмитента  и  лица, выдавшего чек (при возможности их 
   указания), признак (резидент или нерезидент) для эмитента чека  или лица, 
   выдавшего  чек, указание, что чек выдан  на предъявителя, если  такой чек 
   выдан на предъявителя, количество)




](
   i.NumbSprv,
   StrAddZeroDate( i.DateOper ):c,
   FullName:c,
   DocInfo:c,
   i.DescrDoc:c,
   arrAddress[0],
   arrAddress[1],
   SD(0),
   SD(1),
   SD(2)
 );


  if( StrFor(GetProgramID()) != "X" ) /* 14.11.2000 VS !!! Пока нет признака Оперкассы, сделаем так. */
[
   Кассовый работник ________________ (####################)]( {FIO_oper} );   
[                       (подпись)];
  else
[
   Кассовый работник ________________ (####################)]( {FIO_oper} );
[                       (подпись)];
  end;

end;
