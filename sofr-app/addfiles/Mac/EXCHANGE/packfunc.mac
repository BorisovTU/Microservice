/*-RSL---------------------------------------- R-Style Software Lab --*/
/*---------------- Функции работы с пачками ценностей ----------------*/
/* RS-Bank         Обменный пункт                                     */
/*                                                                    */
/* Copyright 2000 (c) R-Style Software Lab.                           */
/* Создание: Рытик А.И.  16-05-00                                     */
/*                                                                    */
/*                 вызывается из контекста sprvfunc.mac               */
/*  ------------------------------------------------------------------*/

FILE   trspack2 ("trspack.dbt", "exchange.def") key 2; /* обратный порядок !!! */
FILE   trsattr1 ("trsattr.dbt", "exchange.def") key 1; /* Oper, State */
FILE   operat1  ("operat.dbt" , "exchange.def") key 1; 

/* установка ключа и поиск первой подходящей под ключ записи            */
/* в файле oper.dbt                                                     */
MACRO InitOperatKey( Emp_Ref , Sess_Ref , Oper_Ref )
   ClearRecord( operat1 );
   operat1.Emp_Ref        = Emp_Ref;
   operat1.Sess_Ref       = Sess_Ref;
   operat1.Oper_Ref       = Oper_Ref;
   operat1.State_Ref      = CDF_OperCompleted;
   operat1.Rate_Ref       = 0;
   operat1.OutCurr_Ref    = ANY_CURR;
   operat1.IncomeCurr_Ref = ANY_CURR;

   if( GetGE( operat1 ) )
      return True;
   else
      return False;
   end;
END;

MACRO FilterOperat( Emp_Ref , Sess_Ref , Oper_Ref  )
  return(
  ( operat1.Emp_Ref   == Emp_Ref   ) AND
  ( operat1.Sess_Ref  == Sess_Ref  ) AND
  ( operat1.Oper_Ref  == Oper_Ref  ) AND
  ( ( operat1.State_Ref == CDF_OperCompleted ) OR
    ( operat1.State_Ref == CDF_OperInReestr  )  )
        );
END;

/* фильтр по нужным кассовым операциям */
MACRO FilterCashOperat( TypeOper, PayDocRef, TrsRef )
  /* наложение структуры avanceop на поле operat1.ExtInfo */
  SetRecordAddr( avanceop, operat1, 0, FldOffset( operat1,"ExtInfo"),True);

  return(
   /*идентификация ПД по коду ценности и по ссылке на ПД*/
   ( avanceop.PayDoc_Ref == PayDocRef ) AND
   ((TypeOper != CDF_OperAvance  ) OR (operat1.IncomeTrs_Ref == TrsRef)) AND
   ((TypeOper != CDF_OperCashOut ) OR (operat1.OutTrs_Ref    == TrsRef)) AND
   ((TypeOper != CDF_OperTransfer) OR (operat1.OutTrs_Ref    == TrsRef))
        );
END;

MACRO FilterTrsPack( TrsRef, SubTrs_Ref, Curr_Ref, ExchangeNumber, Emp_Ref, Sess_Ref )
  return(
  ( trspack2.Trs_Ref        == TrsRef         ) AND
  ( trspack2.SubTrs_Ref     == SubTrs_Ref     ) AND
  ( trspack2.Curr_Ref       == Curr_Ref       ) AND
  ( trspack2.ExchangeNumber == ExchangeNumber ) AND
  ( trspack2.Emp_Ref        == Emp_Ref        ) AND
  ( trspack2.Sess_Ref       == Sess_Ref       )
        );
END;

MACRO FilterTrsPackCash()
  return( ( trspack2.State_Ref == CDF_SprvPackCashOffice ) OR
          ( trspack2.State_Ref == CDF_SprvPackReturn )   );
END;

MACRO FilterTrsPackTemp()
  return( (trspack2.State_Ref == CDF_SprvPackAvance) OR
          (trspack2.State_Ref == CDF_SprvPackReturn)  );
END;

MACRO FilterTrsPackAvance()
  return( trspack2.State_Ref == CDF_SprvPackAvance );
END;

/****************************************************************************/
/* Формирует строку номиналов для ценности в кассе                          */
/****************************************************************************/
MACRO СтрокаНоминаловВКассе( TrsRef, SubTrs_Ref, Curr_Ref, Emp_Ref, Sess_Ref, InsolvFlag )

  var Строка = "";
  var OldNominal = 0;
  var Loop = True, Flag = False;
  var Количество = 0;
  var СтрокаНоминал, ДлинаДоп, Столбец1, СтрокаКоличество, Столбец2;

  MACRO СформироватьСтороку()
    СтрокаНоминал = Trim(string( OldNominal ));
    ДлинаДоп = 12 - StrLen(СтрокаНоминал); 
    if( ДлинаДоп > 0 )                     
       Столбец1 = СтрокаНоминал + MkStr( " ", ДлинаДоп );
    else
       Столбец1 = СтрокаНоминал;
    end;
    СтрокаКоличество = Trim(string( Количество ));
    ДлинаДоп = 7 - StrLen(СтрокаКоличество);
    if( ДлинаДоп > 0 )
       Столбец2 = СтрокаКоличество + MkStr( " ", ДлинаДоп );
    else
       Столбец2 = СтрокаКоличество;
    end;

    Строка = Строка + Столбец1  + "|" + Столбец2;
    Количество = 0;
  END;
  
  ClearRecord( trspack2 );
  trspack2.Trs_Ref        = TrsRef;
  trspack2.SubTrs_Ref     = SubTrs_Ref;
  trspack2.Curr_Ref       = Curr_Ref;
  trspack2.ExchangeNumber = {ExBankNumber};
  trspack2.Emp_Ref        = Emp_Ref;
  trspack2.Sess_Ref       = Sess_Ref;
  trspack2.Nominal        = {DMONEY_MAX}; /* обратный порядок !!! */

  Loop = GetGE( trspack2 );
  while( Loop AND FilterTrsPack( TrsRef, SubTrs_Ref, Curr_Ref, {ExBankNumber}, Emp_Ref, Sess_Ref ) )
    if( FilterTrsPackCash() )
      Flag = True;
      if( ( trspack2.Nominal != OldNominal ) AND ( OldNominal != 0 ) )
        /* печатаем строку для OldNominal */
        СформироватьСтороку();
      end;
      /* накопление количества */
      Количество = Количество + trspack2.Quantum;    
      OldNominal = trspack2.Nominal;
    end;
    Loop = Next( trspack2 );
  end;
  if( Flag == True )
    СформироватьСтороку();
  end;
  if( Строка == "" )
    Строка = MkStr( " ", 12 ) + "|" + MkStr( " ", 7 );
  end;
  return Строка;

END;

/****************************************************************************/
/* Формирует строку номиналов текущей разбивки по кассовой операции         */
/****************************************************************************/
MACRO СтрокаНоминаловТекущаяРазбивка( TrsRef, SubTrs_Ref, Curr_Ref, Emp_Ref, Sess_Ref, InsolvFlag )
  var Строка = "";
  ReplaceMacro ( "FilterTrsPackCash" , "FilterTrsPackTemp" );
  Строка = СтрокаНоминаловВКассе( TrsRef, SubTrs_Ref, Curr_Ref, Emp_Ref, Sess_Ref, InsolvFlag );
  ReplaceMacro ( "FilterTrsPackCash" ); /* чтобы вернуть имя назад */
  return Строка;
END;

/****************************************************************************/
/* Процедура заполняет массивы номиналов из записи в trsattr                */
/*     аналогично ЗаполнитьМассивыНоминалов()                               */
/****************************************************************************/
MACRO trsattr_в_МассивыНоминалов()
  var i = 0, OldNumNoms, Номер;
  if( (trsattr1.Nominal != 0.) AND (trsattr1.Quantum != 0) )
     OldNumNoms = NumNoms;
     Номер = PutNumberInArray( ArrayNoms, trsattr1.Nominal, NumNoms );
     if( NumNoms != OldNumNoms )
           ArrayNumbNoms( Номер ) = 0;
     end;
     ArrayNumbNoms( Номер ) = ArrayNumbNoms( Номер ) + trsattr1.Quantum;
  end;
END;

/****************************************************************************/
/*  Процедура корректирует массивы номиналов                                */
/*  (уменьшает/увеличивает кол-во номиналов)                                */
/*  Действие = -1  уменьшает                                                */
/*  Действие =  1  увеличивает                                              */
/*     аналогично СкорректироватьМассивыНоминалов()                         */
/****************************************************************************/
MACRO trsattr_добавить_в_МассивыНоминалов( Действие )
  var i = 0, OldNumNoms, Номер;
  if( (trspack2.Nominal != 0.) AND (trspack2.Quantum != 0) )
     OldNumNoms = NumNoms;
     Номер = PutNumberInArray( ArrayNoms, trspack2.Nominal, NumNoms );
     /*уже существующий номинал*/
     if( NumNoms == OldNumNoms )
        ArrayNumbNoms( Номер ) = ArrayNumbNoms( Номер ) + Int(Действие * trspack2.Quantum);
     /*новый вид номинала*/
     else
        ArrayNumbNoms( Номер ) = Int( Действие * trspack2.Quantum );
     end;
  end;
END;

/****************************************************************************/
/* Набивает в массив номиналы по данной операции                            */
/****************************************************************************/
MACRO СчитатьПачкиНоминаловПоОперации()
   var Loop = True;
   ClearRecord( trsattr1 );
   trsattr1.Oper_Ref = operat1.ID;
   Loop = GetGE( trsattr1 );
   /* по всем пачкам */
   while( Loop AND (trsattr1.Oper_Ref == operat1.ID) )
      trsattr_в_МассивыНоминалов();
      Loop = Next( trsattr1 );
   end;
END;

/****************************************************************************/
/*Процедура заполняет массивы информ-ей по номиналам сумм(аванс/возвр/перед)*/
/* TypeOper может быть: CDF_OperAvance, CDF_OperTransfer, CDF_OperCashOut   */
/* по аналогии с РазбиениеПоНоминалам()                                     */
/****************************************************************************/
MACRO НоминалыПоКассовымОперациям( TypeOper, PayDocRef, TrsRef, Emp_Ref, Sess_Ref )
   var Loop = True;
   var NumNoms = 0;/*обнуление глобализма*/

   if( InitOperatKey( Emp_Ref , Sess_Ref , TypeOper ) )
     while( Loop AND FilterOperat( Emp_Ref , Sess_Ref , TypeOper ) )
       if( FilterCashOperat( TypeOper, PayDocRef, TrsRef ) )
         СчитатьПачкиНоминаловПоОперации();
       end;
       Loop = Next( operat1 );
     end;
   end;
END;

/****************************************************************************/
/* Формирует строку номиналов прихода(расхода) по кассовой операции         */
/****************************************************************************/
MACRO СтрокаНоминаловАвансЗаСмену( TrsRef, SubTrs_Ref, Curr_Ref, Emp_Ref, Sess_Ref, InsolvFlag )
 НоминалыПоКассовымОперациям( CDF_OperAvance, SubTrs_Ref, TrsRef, Emp_Ref, Sess_Ref );
 return СформироватьСтрокуНоминалов();
END;

/****************************************************************************/
/*  Процедура учитывает текущие номиналы аванса/передачи/возврата           */
/****************************************************************************/
MACRO УчетТекущейРазбивки( TrsRef, SubTrs_Ref, Curr_Ref, Emp_Ref, Sess_Ref )
  ClearRecord( trspack2 );
  trspack2.Trs_Ref        = TrsRef;
  trspack2.SubTrs_Ref     = SubTrs_Ref;
  trspack2.Curr_Ref       = Curr_Ref;
  trspack2.ExchangeNumber = {ExBankNumber};
  trspack2.Emp_Ref        = Emp_Ref;
  trspack2.Sess_Ref       = Sess_Ref;
  trspack2.Nominal        = {DMONEY_MAX}; /* обратный порядок !!! */

  Loop = GetGE( trspack2 );
  while( Loop AND FilterTrsPack( TrsRef, SubTrs_Ref, Curr_Ref, {ExBankNumber}, Emp_Ref, Sess_Ref ) )
    if( FilterTrsPackAvance() )
      trsattr_добавить_в_МассивыНоминалов( 1 );
    end;
    Loop = Next( trspack2 );
  end;
END;

/****************************************************************************/
/* Формирует строку номиналов прихода(расхода) по кассовой операции         */
/*      + cумма текущей операции                                            */
/****************************************************************************/
MACRO СтрокаНоминаловАвансЗаСмену_и_Текущий( TrsRef, SubTrs_Ref, Curr_Ref, Emp_Ref, Sess_Ref, InsolvFlag )
 НоминалыПоКассовымОперациям( CDF_OperAvance, SubTrs_Ref, TrsRef, Emp_Ref, Sess_Ref );
 УчетТекущейРазбивки( TrsRef, SubTrs_Ref, Curr_Ref, Emp_Ref, Sess_Ref );
 return СформироватьСтрокуНоминалов();
END;

