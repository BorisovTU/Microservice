/*-RSL---------------------------------------- R-Style Software Lab --*/
/*--------- Печать отчетов и справок кассира. Общие функции ----------*/
/* RS-Bank      подсистема Обменный пункт            AV  14.05.97     */
/*RA  9-02-99   закомментировал упоминание неденоминированных сумм    */
/* 24.10.2000 VS Вставлено различие ПД, купленных за валюту и за рубли. */
/* 14.11.2000 VS Заголовок общей шапки адаптирован для Оперкассы.     */
Import ExchangeInter, BankInter, CommonInter, "define.mac";

private var {Name_Bank};
private var {Post_Addr};
private var {Post_Addr_Branch};

private var ddep  = TRecHandler( "i_dp_dep.rec" );
private var party = TRecHandler( "i_party.rec" );

private const PTCK_BANKREGNUM = 13;  /* Вид субъекта: регистр. номер нашего банка. */


/***************************************************************************/

MACRO ШапкаСправкиОПриеме()

  array AExAddress;

[
   Наименование уполномоченного банка
   (филиала уполномоченного банка)_#________________________________________
                                          (полное или сокращенное)
   Регистрационный номер уполномоченного банка
   (порядковый номер филиала уполномоченного банка)_#_______________________
] ( {Name_Bank}, {ExRegNumber} );




  if( StrFor(GetProgramID()) == "X" ) /* 14.11.2000 VS !!! Пока нет признака Оперкассы, сделаем так. */
    StrSplit( {ExAddress}, AExAddress, 72, 23, 2 );
    if ( AExAddress( 0 ) == "" )
      AExAddress( 0 ) = "_";
    end;
    if ( AExAddress( 1 ) == "" )
      AExAddress( 1 ) = "_";
    end;
[
   Местонахождение (почтовый адрес) обменного пункта_#______________________
   _#_______________________________________________________________________
] ( AExAddress( 0 ), AExAddress( 1 ) );
  else
    StrSplit( {ExAddress}, AExAddress, 72, 21, 2 );
    if ( AExAddress( 0 ) == "" )
      AExAddress( 0 ) = "_";
    end;
    if ( AExAddress( 1 ) == "" )
      AExAddress( 1 ) = "_";
    end;
[
   Местонахождение (почтовый адрес) операционной кассы_#____________________
   _#_______________________________________________________________________
] ( AExAddress( 0 ), AExAddress( 1 ) );
  end;
/*
[
   Телефон #
   
]( {ExTelNumb} );
*/
END;


/***************************************************************************/

MACRO ШапкаСправкиОПриеме2()

  var codNumBank;
  var regNumBank;
  var ordNumBank;
  var mainBranch = getFilialNum( );
  var curBranch = NumFNCash( );
  var nameMain;
  var nameCur;
  
  array AExAddress;

  codNumBank = getRegNumOurBank( PTCK_BANKREGNUM ); //{ExRegNumber}
  regNumBank = SubStr( codNumBank, 1, 4 );
  ordNumBank = SubStr( codNumBank, 6, 4 );
  if ( regNumBank == "" )
    regNumBank = "_";
  end;
  if ( ordNumBank == "" )
    ordNumBank = "_";
  end;

  // {Post_Addr}
  if ( findDepartment( mainBranch, NULL, ddep, party ) )
    if ( party.rec.Address != "" )
      nameMain = party.rec.Address;
    else
      nameMain = {Post_Addr};
    end;
  end;

  if ( nameMain == "" )
    nameMain = "_";
  end;

  // {Post_Addr_Branch}
  if ( findDepartment( curBranch, NULL, ddep, party ) )
    if ( party.rec.Address != "" )
      nameCur = Trim( party.rec.Name + "; " + party.rec.Address );
    else
      nameCur = Trim( party.rec.Name + "; " + {Post_Addr_Branch} );
    end;
  end;

  StrSplit( nameCur, AExAddress, 73, 44, 2 );
  if ( AExAddress( 0 ) == "" )
    AExAddress( 0 ) = "_";
  end;
  if ( AExAddress( 1 ) == "" )
    AExAddress( 1 ) = "_";
  end;
  
[
   Полное (сокращенное) фирменное наименование  уполномоченного банка 
   (наименование филиала)_#_________________________________________________
                                          (полное или сокращенное)
   Регистрационный номер уполномоченного банка
   (порядковый номер филиала)_#____/_#______________________________________

   Местонахождение (адрес) уполномоченного банка (филиала)
   _#_______________________________________________________________________

   Наименование внутреннего структурного подразделения уполномоченного банка
   и его местонахождение (адрес)_#__________________________________________
   _#_______________________________________________________________________

] 
( 
 {Name_Bank}, 
  regNumBank,
  ordNumBank,
  nameMain,
  AExAddress( 0 ),
  AExAddress( 1 )
);


END;
