/*-RSL---------------------------------------- R-Style Software Lab --*/
/*-------------- Печать книги учета бланков БСО-----------------------*/
/* RS-Bank      подсистема Обменный пункт            AV  22-06-98     */
/*                                                                    */
/* RA 07-02-02  полностью переработал                                 */

IMPORT  ExchangeInter, DeskInter, DeprIntr,
        "define.mac", "rd_comm.mac", "office.mac", "SortRN.mac";

file GroupMember ( "groupmem.dbt", "sbbank.def" ) key 0;
file CashDС      ( "sb_casdc.dbt", "sbbank.def" ) key 5;
file CashAcc     ( "sb_casac.dbt", "sbbank.def" ) key 0;
file RegNum      ( "reg_num.dbt" , "sbbank.def" ) key 0;
file Bunch       ( "bunch.dbt"   , "sbbank.def" ) key 0;

/*номера колонок*/
const ПолученоИзГознака      =  0,
      ПолученоОтБанка        =  1,
      ВозвратИзПодразделения =  2,
      ВозвратИзФилиала       =  3,
      ВыданоПодразделению    =  4,
      Израсходовано          =  5,
      Испорчено              =  6,
      Утрачено               =  7,
      ВозвратВБанк           =  8,
      ВыданоФилиалам         =  9,
      Уничтожено             = 10,
      Остаток                = 11;

const ЧислоКолонок = Остаток + 1;
const БСО_ValueRef = 6;

var
     Справки             = TArray,/*пачки справок,прошедшие по по операциям*/
     ИтогКолонки         = TArray,/*итоговые строки колонок книги*/
     Количество          = TArray;/*количество справок в колонке*/

const 
      CASHOP_DEBIT         =  5, /* Расход */
      CASHOP_CHVALUE       =  9, /* Перевод на другой ресурс той же группы (Расход) */
      CASHOP_REMOVE_SAFE   = 11, /* Внутреннее подкрепление сейфа (Расход) */
      CASHOP_REMOVE_SAFE_1 = 12, /* Внутреннее подкрепление сейфа (Приход) */
      CASHOP_DEBET_SAFE    = 13, /* Расход из сейфа */
      CASHOP_CREDIT_SAFE   = 14; /* Приход в сейф */

const
      TYPEDOC_CARRYOFF     = 1,  /* Отложен (не подтвержден) */
      TYPEDOC_CARRYON      = 2;  /* Проведен (подтвержден) */


MACRO GetNativeSafe()

  file Group("rtgroup.dbt") key 2;

  var
     OperBrigade = GetOperBrigade();
 
  Group.Branch = Branch;
  Group.Date   = {curdate};
  Group.ID     = OperBrigade;
  if(GetEQ(Group))
    return Group.SafeID;
  else
    MsgBox("Не найден сейф бригады № ", OperBrigade);
    Exit;
  end;
END;

MACRO WorkDeskName( Oper, Brigade )
  var Name, name1;

  if ( Oper == 0 )
    Name = "0";
  else
    Name = string(Oper);    Name = mkstr( "0", 4 - strlen(Name )) + Name;
    name1= string(Brigade); name1= mkstr( "0", 2 - strlen(name1)) + name1;
    Name = Name + "\/" + name1;
  end;
  return Name;  
END;

/****************************************************************************/
/****************************************************************************/
MACRO ШапкаКниги()
PrintDate_n_Time();
[                                                                                                                                             ####################################

                                                                                                                                              ####################################
                                                                К Н И Г А    У Ч Е Т А
                                                   БЛАНКОВ СТРОГОЙ ОТЧЕТНОСТИ "СПРАВКА Ф. N 0406007"

+-------------------------------+---------------------------------------------------------------------------+----------------------+
|   Приход в разрезе номеров и  |            Расход в разрезе номеров и количества                          |  Остаток в разрезе   |
|           количества          |                                                                           | номеров и количества |
+-------------------------------+---------------------------------------------------------------------------+                      |
|          в том числе          |                            в том числе                                    |                      |
+---+-------------------+---+---+---+-------------------+-------------------+---+-------------------+---+---+----------------------+
|   | получено от банка |   |   |   |   израсходовано   |     испорчено     |   |  возврат в банк   |   |   |                      |
+---+-------------------+---+---+---+-------------------+-------------------+---+-------------------+---+---+----------------------+
| 1 |         2         | 3 | 4 | 5 |         6         |         7         | 8 |         9         |10 |11 |          12          |
]( Office.BankName:c:w,
   Office.BankAddress:c:w
 );
END;

/****************************************************************************/
/****************************************************************************/
MACRO СтрокаКниги()
[+---+-------------------+---+---+---+-------------------+-------------------+---+-------------------+---+---+----------------------+
 | # |###################| # | # | # |###################|###################| # |###################| # | # |  ################### |]

(
  "-",
  ИтогКолонки[ ПолученоОтБанка ]:w,
  "-",
  "-",
  "-",
  ИтогКолонки[ Израсходовано   ]:w,
  ИтогКолонки[ Испорчено       ]:w,
  "-",
  ИтогКолонки[ ВозвратВБанк    ]:w,
  "-",
  "-",
  ИтогКолонки[ Остаток         ]:w
);
END;
/****************************************************************************/
/****************************************************************************/
MACRO ДноКниги
[+---+-------------------+---+---+---+-------------------+-------------------+---+-------------------+---+---+----------------------+
 | - |###################| - | - | - |###################|###################| - |###################| - | - |  ################### |
 +---+-------------------+---+---+---+-------------------+-------------------+---+-------------------+---+---+----------------------+


 Операционист _____________ #                             #
]
(
  Количество[ ПолученоОтБанка ]:c,
  Количество[ Израсходовано   ]:c,
  Количество[ Испорчено       ]:c,
  Количество[ ВозвратВБанк    ]:c,
  Количество[ Остаток         ]:c,
  GetFIOEmployee( {oper}      ),
  StrAddZeroDate( {curdate}   )
);
END;


MACRO СтрокаПриходаЦенностей( ТипДанных )
  var i = Справки[ ТипДанных ].Size;
  Справки[ ТипДанных ][i] = SerNum( Bunch.Series, Bunch.Min, Bunch.Max );
END;

MACRO СтрокаОстаткаСправок( ТипДанных )
  var i = Справки[ ТипДанных ].Size;
  Справки[ ТипДанных ][i] = SerNum( RegNum.Series, RegNum.Min, RegNum.Max );
END;


MACRO СтрокиПриходаЦенностейИзПачек( ТипДанных )
   
  var
    RecFound;

  ClearRecord( Bunch );
  Bunch.BagAppKind = CashDС.ApplicationKind;
  Bunch.BagAppKey  = CashDС.ApplicationKey;
  RecFound = GetGE( Bunch );

  while (     RecFound
        and ( Bunch.BagAppKind == CashDС.ApplicationKind )
        and ( Bunch.BagAppKey  == CashDС.ApplicationKey  )
        )
    СтрокаПриходаЦенностей( ТипДанных );
    RecFound = Next( Bunch );
  end;

END;

MACRO СтрокиОстаткаСправокИзПачек( ТипДанных, CashOper )
   
  var
    RecFound;

  ClearRecord( RegNum );
  RegNum.Branch   = Branch;
  RegNum.CashOper = CashOper;
  RegNum.SubAccount = "";
  RegNum.ValueRef = БСО_ValueRef;
  RegNum.Date     = {curdate};
  RegNum.Nominal  = $0;
  RecFound = GetGE( RegNum );

  while (     RecFound
        and ( RegNum.Branch   == Branch       )
        and ( RegNum.CashOper == CashOper     )
        and ( RegNum.SubAccount == ""         )
        and ( RegNum.ValueRef == БСО_ValueRef )
        and ( RegNum.Date     == {curdate}    )
        and ( RegNum.Nominal  == $0           )
        )
    СтрокаОстаткаСправок( ТипДанных );
    RecFound = Next( RegNum );
  end;

END;

MACRO FilterПачкиСправок( ТипДанных )
  var RetVal = false;

  if  (   ( ТипДанных      == ПолученоОтБанка      )
     and( ( CashDС.NumOper == CASHOP_ADVANCE       )
        or( CashDС.NumOper == CASHOP_CREDIT_SAFE   )
        or( CashDС.NumOper == CASHOP_REMOVE_SAFE_1 )
        )
      )
      RetVal = true;

  elif(   ( ТипДанных      == Израсходовано        )
       and( CashDС.NumOper == CASHOP_DEBIT         )
      )
      RetVal = true;

  elif(   ( ТипДанных      == Испорчено            )
       and( CashDС.NumOper == CASHOP_CHVALUE       )
      )
      RetVal = true;

  elif(   ( ТипДанных      == ВозвратВБанк         )
     and( ( CashDС.NumOper == CASHOP_COLLECT       )
        or( CashDС.NumOper == CASHOP_REMOVE_SAFE   )
        or( CashDС.NumOper == CASHOP_DEBET_SAFE    )
        )
      )
      RetVal = true;
/*
  elif(    ТипДанных      == Остаток
      and( CashDС.NumOper ==  )
      )
      RetVal = true;
*/
  else
      RetVal = false;
  end;
  return RetVal;
END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Подсчет справок по операциям данного типа за смену                     │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
/* ТипДанных - ПолученоОтБанка, Израсходовано, Испорчено или ВозвратВБанк     */
MACRO ПачкиСправок( ТипДанных, Количество )

  var
      CashPatern,
      Safe   = GetNativeSafe(),
      RecFound,
      ИтогКолонки;
  var IsDeskAutomationTurnedOn; /* TONIK 25-02-03 - включена ли автоматизация рабочего стола ДЛЯ ДАННОГО ОПЕРАЦИОНИСТА */
  GetRegistryValue("RS-RETAIL\\СЕРВИСНЫЕ\\КАССА\\АВТОМАТИЗАЦИЯ_СТОЛА_КАССИРА", V_INTEGER, IsDeskAutomationTurnedOn, null, null, GroupMember.Oper);

  if  ( ( ТипДанных == ПолученоОтБанка  )
     or ( ТипДанных == ВозвратВБанк     )    
     or ( (IsDeskAutomationTurnedOn == 0) and (ТипДанных == Испорчено) )
      )
    CashPatern = Safe;

  elif( ( ТипДанных == Израсходовано    )
     or ( (IsDeskAutomationTurnedOn == 1) and (ТипДанных == Испорчено) )
      )
    CashPatern = WorkDeskName( GroupMember.Oper, GetOperBrigade() );

  end;

  ClearRecord( CashDС );
  CashDС.Branch      = Branch;
  CashDС.CashDateDoc = {curdate};
  CashDС.TypeDoc     = TYPEDOC_CARRYON;
  CashDС.CashPatern  = CashPatern;
  CashDС.RefValue    = БСО_ValueRef;
  RecFound = GetGE( CashDС ); 

  while (     RecFound
        and ( CashDС.Branch      == Branch          )
        and ( CashDС.CashDateDoc == {curdate}       )
        and ( CashDС.TypeDoc     == TYPEDOC_CARRYON )
        and ( CashDС.CashPatern  == CashPatern      )
        and ( CashDС.RefValue    == БСО_ValueRef    )
        )

    if( FilterПачкиСправок( ТипДанных ) )

      /* не было детализации - возьмем из кассового документа */
      if( CashDС.Series != "" )
        Bunch.Series = CashDС.Series;
        Bunch.Min    = CashDС.MinRegistrNumber;
        Bunch.Max    = CashDС.MaxRegistrNumber;
        СтрокаПриходаЦенностей( ТипДанных );
      /* была детализация - возьмем из бунчей */
      else
        СтрокиПриходаЦенностейИзПачек( ТипДанных );
      end;

    end;
    RecFound = Next( CashDС );
  end;

  ИтогКолонки = MakeSerNumString( Справки[ ТипДанных ], Количество, "\n" );
  
  SetParm( 1, Количество );
  return ИтогКолонки;
END;

/* ┌────────────────────────────────────────────────────────────────────────┐ */
/* │ Подсчет остатка справок                                                │ */
/* └────────────────────────────────────────────────────────────────────────┘ */
MACRO ОстатокСправок( ТипДанных, Количество, Тип )

  var 
      CashOper,
      ЗаголовокНапечатан = false,
      RecFound,
      ИтогКолонки;
 
  if  ( Тип == "по операционисту" )
    CashOper = WorkDeskName( GroupMember.Oper, GetOperBrigade() );
  elif( Тип == "по сейфу" )
    CashOper = GetNativeSafe();
  end;

  CashAcc.Branch   = Branch;
  CashAcc.Date     = {curdate};
  CashAcc.CashOper = CashOper;
  CashAcc.SubAccount = "";
  CashAcc.RefValue = БСО_ValueRef;
  RecFound = GetGE( CashAcc ); 

  while (     RecFound
        and ( CashAcc.Branch   == Branch       )
        and ( CashAcc.Date     == {curdate}    )
        and ( CashAcc.CashOper == CashOper     )
        and ( CashAcc.SubAccount == ""         )
        and ( CashAcc.RefValue == БСО_ValueRef )
        )
    СтрокиОстаткаСправокИзПачек( ТипДанных, CashOper );
    RecFound = Next( CashAcc );
  end;
  
  ИтогКолонки = MakeSerNumString( Справки[ ТипДанных ], Количество, "\n" );
  
  SetParm( 1, Количество );
  return ИтогКолонки;
END;


/***************************************************************************/
/*  Процедура формирует книгу учета бланков БСО (ф.0406007)                */
/***************************************************************************/
MACRO CreateBookBSO()

     var i = 0,
         RecFound,
         OperBrigade = GetOperBrigade();


     while( i < ЧислоКолонок )
       Справки    [i] = TArray; /* колонка отчета               */
       ИтогКолонки[i] = "";     /* итог колонки отчета          */
       Количество [i] = 0;      /* количество справок в колонке */
       i = i+1;
     end;

     i = ПолученоОтБанка;
     ИтогКолонки[ i ] = ПачкиСправок  ( i, Количество[ i ] );
     
     i = ВозвратВБанк;
     ИтогКолонки[ i ] = ПачкиСправок  ( i, Количество[ i ] );

     i = Остаток;
     ИтогКолонки[ i ] = ОстатокСправок( i, Количество[ i ], "по сейфу" );

     /* по всем операционистам */
     ClearRecord( GroupMember );
     GroupMember.Branch = Branch;
     GroupMember.Date   = {curdate};
     GroupMember.Group  = OperBrigade;
     RecFound = GetGE( GroupMember );

     while(       RecFound
            and ( GroupMember.Branch == Branch      )
            and ( GroupMember.Date   == {curdate}   )
            and ( GroupMember.Group  == OperBrigade )
          )

       i = Израсходовано;
       ИтогКолонки[ i ] = ПачкиСправок  ( i, Количество[ i ] );

       i = Испорчено;
       ИтогКолонки[ i ] = ПачкиСправок  ( i, Количество[ i ] );

       i = Остаток;
       ИтогКолонки[ i ] = ОстатокСправок( i, Количество[ i ], "по операционисту" );
       
       RecFound = Next( GroupMember );
     end;

     /* печать всей книги учета бланков БСО */
     ШапкаКниги();
     СтрокаКниги();
     ДноКниги();

END;

CreateBookBSO();
