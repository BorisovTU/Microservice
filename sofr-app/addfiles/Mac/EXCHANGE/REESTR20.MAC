/**************************************************************************\
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Обменно-валютные операции                                               *
*                                                                          *
*  Печать реестра принятой наличной иностранной валюты                     *
*                                                                          *
*  Лебедев С.В.                                                            *
*  27.07.2001                                                              *
\**************************************************************************/

import ExchangeInter, "rstrfunc.mac", "string.mac", "regnumb.mac";

file currency  (currency,"sbbank.def"  ) key 0;
file pay_subop (pay_csop,"sbbank.def"  ) key 0;
file sb_casvl  (sb_casvl,"sbbank.def"  ) key 3;
file scCodif   (scCodif, "spcard.def"  ) key 1;
file issuer    (issuer,  "exchange.def") key 0;
file person    (person,  "bank.def"    ) key 0;
file pay_doc   (pay_doc, "sbbank.def"  ) key 0 write;
file reestr    (reestr,  "exchange.def") key 4 write;

record pay_doc_n36 ("pay_doc.n36","sbbank.def");

const REESTR_For_Currency = 0;
const REESTR_For_Issuer   = 2;
const REESTR_For_TypeCard = 1;

var Level_Reestr            = REESTR_For_Currency;
var Print_Level             = FALSE;
var FlagPrintSummaryForPage = FALSE;
var MaxNumberStringsCard    = MaxNumberStrings;
var NumberPage              = 1;
var FirstRecReestr          = TRUE;
var FirstReestr             = TRUE;

var SummaOperPage     = $0L;
var SummaComissPage   = $0L;
var CountSprPage      =   0;
var SummaOperReestr   = $0L;
var SummaComissReestr = $0L;
var CountSprReestr    =   0;


/**************************************************************************\
|                    Поиск типа карточки в кодификаторе                    |
\**************************************************************************/
macro FindCardType

  scCodif.codType = 7;
  scCodif.codCode = pay_doc_n36.cardTypeCode;
  scCodif.psRef   = pay_doc_n36.cardTypeCodePos;

  if (NOT GetEQ(scCodif))
    ClearRecord(scCodif);
    scCodif.codType = 7;
    scCodif.codCode = pay_doc_n36.cardTypeCode;
    scCodif.psRef   = pay_doc_n36.cardTypeCodePos;
  end;

end;


/**************************************************************************\
|                              Поиск ценности                              |
\**************************************************************************/
macro FindCashValue

  sb_casvl.Type        = 0;
  sb_casvl.TypeValue   = 1;
  sb_casvl.CodIntValue = pay_doc.CodCur;

  if (NOT GetEQ(sb_casvl))
    ClearRecord(sb_casvl);
    sb_casvl.Type        = 0;
    sb_casvl.TypeValue   = 1;
    sb_casvl.CodIntValue = pay_doc.CodCur;
  end;

end;


/**************************************************************************\
|                             Поиск подоперации                            |
\**************************************************************************/
macro FindSubOp

  var IsCur;

  if (reestr.IncomeCodIntValue == 0)
    IsCur = 0;
  else
    IsCur = 1;
  end;

  pay_subop.IsCur     = IsCur;
  pay_subop.GroupType = 11;
  pay_subop.NumOperat = 36;
  pay_subop.ApplType  = reestr.IncomePayDoc_Ref;

  return GetEQ(pay_subop);

end;


/**************************************************************************\
|                               Поиск валюты                               |
\**************************************************************************/
macro FindCurrencyForReestr

  currency.Code_Currency = reestr.IncomeCodIntValue;

  if (NOT GetEQ(currency))
    ClearRecord(currency);
    currency.Code_Currency = reestr.IncomeCodIntValue;
  end;

end;


/**************************************************************************\
|                       Поиск реестра для документа                        |
\**************************************************************************/
/* RA 25-11-02 реестры формируются при проведении операции
macro FindReestrForPayDoc

  var stat = FALSE;
  var stat_reestr;

  reestr.Branch       = pay_doc.FNCash;
  reestr.CurDate      = pay_doc.Date_Document;
  reestr.Oper         = pay_doc.Oper;
  reestr.Kind_Oper    = CDF_OperCardTake;
  reestr.IncomeRefVal = 0;
  reestr.OutRefVal    = 0;
  reestr.Rate_Ref     = 0;
  reestr.ID           = 0;
  stat_reestr = GetGE(reestr);
  while (stat_reestr)
    if ((reestr.Branch    == pay_doc.FNCash       ) AND
        (reestr.CurDate   == pay_doc.Date_Document) AND
        (reestr.Oper      == pay_doc.Oper         ) AND
        (reestr.Kind_Oper == CDF_OperCardTake     )    )
      if ((reestr.IncomeTypeValue   == 1               ) AND
          (reestr.IncomeCodIntValue == pay_doc.CodCur  ) AND
          (reestr.IncomeType        == 0               ) AND
          (reestr.OutTypeValue      == 0               ) AND
          (reestr.OutCodIntValue    == 0               ) AND
          (reestr.OutType           == 0               ) AND
          (reestr.Brigade           == pay_doc.Brigade ) AND  /* Бригада     */
          (reestr.IncomePayDoc_Ref  == pay_doc.ApplType)    ) /* Подоперация */
        if   (Level_Reestr == REESTR_For_Issuer  )
          if (reestr.Issuer == pay_doc_n36.cardIssuerCode)
            stat = TRUE;
          end;
        elif (Level_Reestr == REESTR_For_TypeCard)
          if (reestr.CardType == scCodif.codRef)
            stat = TRUE;
          end;
        else
          stat = TRUE;
        end;
      end;
      if (stat)
        stat_reestr = FALSE;
      else
        stat_reestr = Next(reestr);
      end;
    else
      stat_reestr = FALSE;
    end;
  end;

  return stat;

end;
*/

/**************************************************************************\
|                         Создание нового реестра                          |
\**************************************************************************/
/* RA 25-11-02 реестры формируются при проведении операции
macro T_InsertReestr

  var stat = TRUE;
  var ID   = 1;
  var SaveKeyNum;
  var pay_doc_varlen;

  if (stat)
    SaveKeyNum = KeyNum(reestr,0);
    reestr.Branch = pay_doc.FNCash + 1;
    reestr.ID     = 0;
    if (GetLT(reestr))
      if (reestr.Branch == pay_doc.FNCash)
        ID = reestr.ID + 1;
      end;
    end;
    KeyNum(reestr,SaveKeyNum);
  end;

  if (stat)
    ClearRecord(reestr);
    reestr.ID                = ID;
    reestr.Branch            = pay_doc.FNCash;
    reestr.Brigade           = pay_doc.Brigade;
    reestr.CurDate           = pay_doc.Date_Document;
    reestr.Oper              = pay_doc.Oper;
    reestr.iApplicationKind  = 250;
    reestr.ApplicationKey    = FormApplicationKey(250);
    reestr.Kind_Oper         = CDF_OperCardTake;
    reestr.Date              = Date();
    reestr.Time              = Time();
    reestr.State_Ref         = 1;          /* Реестр открыт */
    reestr.IncomeRefVal      = sb_casvl.RefValue;
    reestr.IncomeTypeValue   = 1;
    reestr.IncomeCodIntValue = pay_doc.CodCur;
    reestr.IncomeType        = 0;
    reestr.OutRefVal         = 0;
    reestr.OutTypeValue      = 0;
    reestr.OutCodIntValue    = 0;
    reestr.OutType           = 0;
    reestr.IncomeSum         = Double( pay_doc.InSum );
    reestr.OutSum            = 0;
    reestr.NumberDocs        = 1;
    if (Level_Reestr == REESTR_For_Issuer)
      reestr.Issuer          = pay_doc_n36.cardIssuerCode;
    end;
    reestr.IncomePayDoc_Ref  = pay_doc.ApplType;
    if (Level_Reestr == REESTR_For_TypeCard)
      reestr.CardType        = scCodif.codRef;
    end;
    CalcRegNumb(reestr);
    stat = Insert(reestr);
  end;

  if (stat)
    stat = GetPos(pay_doc);
  end;

  if (stat)
    stat = GetDirect(pay_doc);
  end;

  if (stat)
    pay_doc_varlen = GetVarSize(pay_doc);
    pay_doc_n36.ReestrID = reestr.ID;
    stat = Update(pay_doc,pay_doc_varlen);
  end;

  if (NOT stat)
    AbortTrn();
  end;

  return stat;

end;
*/

/**************************************************************************\
|                Добавление документа в существующий реестр                |
\**************************************************************************/
/* RA 25-11-02 реестры формируются при проведении операции
macro T_UpdateReestr

  var stat = TRUE;
  var pay_doc_varlen;

  if (stat)
    stat = GetPos(reestr);
  end;

  if (stat)
    stat = GetDirect(reestr);
  end;

  if (stat)
    reestr.IncomeSum  = reestr.IncomeSum + Double( pay_doc.InSum );
    reestr.OutSum     = 0;
    reestr.NumberDocs = reestr.NumberDocs + 1;
    stat = Update(reestr);
  end;

  if (stat)
    stat = GetPos(pay_doc);
  end;

  if (stat)
    stat = GetDirect(pay_doc);
  end;

  if (stat)
    pay_doc_varlen = GetVarSize(pay_doc);
    pay_doc_n36.ReestrID = reestr.ID;
    stat = Update(pay_doc,pay_doc_varlen);
  end;

  if (NOT stat)
    AbortTrn();
  end;

  return stat;

end;
*/

/**************************************************************************\
|                            Создание реестров                             |
\**************************************************************************/
macro CreateOrUpdateReestrs

  var stat = TRUE;
  var stat_doc;
  var IsCur = 0;
/* RA 25-11-02 реестры формируются при проведении операции
  while (IsCur <= 1)
    pay_doc.IsCur         = IsCur;
    pay_doc.FNCash        = Branch;
    pay_doc.GroupOpert    = 11;
    pay_doc.Oper          = {exoper};
    pay_doc.Date_Document = {curdate};
    pay_doc.NumOpert      = 36;
    pay_doc.CodCur        = 0;
    stat_doc = GetGE(pay_doc);
    while (stat_doc)
      if ((pay_doc.IsCur         == IsCur    ) AND
          (pay_doc.FNCash        == Branch   ) AND
          (pay_doc.GroupOpert    == 11       ) AND
          (pay_doc.Oper          == {exoper} ) AND
          (pay_doc.Date_Document == {curdate}) AND
          (pay_doc.NumOpert      == 36       )   )
        if ( (pay_doc.Action <= 1) and ( pay_doc.ApplType == 1 ) )
          SetRecordAddr(pay_doc_n36,pay_doc,0,0,TRUE);
          if (GetVarSize(pay_doc) == 0)
            pay_doc_n36.cardNumber      = "";
            pay_doc_n36.cardTypeCode    = "";
            pay_doc_n36.cardTypeCodePos = 0;
            pay_doc_n36.cardIssuerCode  = 0;
            pay_doc_n36.cardBankName    = "";
          end;
          if (pay_doc_n36.ReestrID == 0)
            FindCashValue();
            if (Level_Reestr == REESTR_For_TypeCard)
              FindCardType();
            end;
            if (FindReestrForPayDoc())
              if (NOT ProcessTrn(NULL,"T_UpdateReestr",reestr,pay_doc))
                stat = FALSE;
              end;
            else
              if (NOT ProcessTrn(NULL,"T_InsertReestr",reestr,pay_doc))
                stat = FALSE;
              end;
            end;
          end;
        end;
        stat_doc = Next(pay_doc);
      else
        stat_doc = FALSE;
      end;
    end;
    IsCur = IsCur + 1;
  end;
*/
  return stat;

end;


/**************************************************************************\
|                            Печать шапки отчета                           |
\**************************************************************************/
macro PrintHeadCardReestr (FlagPrintFullHead)

  var NameSubOp     = String(reestr.IncomePayDoc_Ref);
  var Name_Issuer   = "";
  var Name_CardType = "";
  var SaveKeyNum;

  if (FlagPrintFullHead)
    [ ];
    [###############################################################################################################################]
    ("Р Е Е С Т Р":c);
    if (currency.Code_Currency == 0)
      [###############################################################################################################################]
      ("ПРИНЯТЫХ НАЛИЧНЫХ РУБЛЕЙ РФ":c);
    else
      [###############################################################################################################################]
      ("ПРИНЯТОЙ НАЛИЧНОЙ ИНОСТРАННОЙ ВАЛЮТЫ":c);
    end;
    [ ];
    if (FindSubOp())
      NameSubOp = pay_subop.NameCompType;
    end;
    if (Level_Reestr == REESTR_For_Issuer)
      if (Print_Level)
        issuer.ID = reestr.Issuer;
        if (GetEQ(issuer))
          Name_Issuer = issuer.Name;
        end;
        [    Подоперация: #                Эмитент: #](NameSubOp,Name_Issuer);
      /*else
        [    Подоперация: #](NameSubOp);*/
      end;
    elif (Level_Reestr == REESTR_For_TypeCard)
      if (Print_Level)
        SaveKeyNum = KeyNum(scCodif,0);
        scCodif.codRef = reestr.CardType;
        if (GetEQ(scCodif))
          Name_CardType = scCodif.codName;
        end;
        KeyNum(scCodif,SaveKeyNum);
        [    Подоперация: #                Тип карт: #](NameSubOp,Name_CardType);
    /*  else
        [    Подоперация: #](NameSubOp);*/
      end;
    else
     /* [    Подоперация: #](NameSubOp);*/
    end;
    [ ];
    [+---------------------+---------------------+-------------------+------+-----------------+----------+----------+--------------+];
    [|                     |                     |       Номер       | КОД  |     Принята     | N справки|Резидент/ | КОД и Сумма  |];
    [|       Эмитент       |        Банк,        |       карты       |валюты|      сумма      | ф.0406007|нерезидент|комиссионного |];
    [|                     |      выдавший       |                   |      |                 |          |          |вознаграждения|];
    [|                     |  пластиковую карту  |                   |      |                 |          |          |              |];
    [|                     |                     |                   |      |                 |          |          |              |];
  else
    [###############################################################################################################################]
    (("- " + String(NumberPage) + " -"):c);
  end;
  [+---------------------+---------------------+-------------------+------+-----------------+----------+----------+--------------+];
  [|          1          |          2          |         3         |  4   |        5        |    6     |    7     |      8       |];
  [+---------------------+---------------------+-------------------+------+-----------------+----------+----------+--------------+];

end;


/**************************************************************************\
|                  Печать окончания страницы для реестра                   |
\**************************************************************************/
macro PrintBotCardReestrForPage (FlagNewPage)

  var ReportSummaComissPage = "";
  var ReportISOComissPage   = "";
  var ReportCountSprPage    = "";

  if (SummaComissPage > 0)
    ReportSummaComissPage = SummaComissPage;
    ReportISOComissPage   = currency.ExternalCode;
  end;

  if (CountSprPage > 0)
    ReportCountSprPage = CountSprPage;
  end;

  [+---------------------+---------------------+-------------------+------+-----------------+----------+----------+--------------+];
  if (FlagPrintSummaryForPage)
    [|Итого:                                                           ###   ################# ##########            ### ##########|]
    (currency.ExternalCode,SummaOperPage,ReportCountSprPage,ReportISOComissPage,ReportSummaComissPage);
    [+---------------------+---------------------+-------------------+------+-----------------+----------+----------+--------------+];
  end;

  SummaOperPage   = $0L;
  SummaComissPage = $0L;
  CountSprPage    = 0;

  if (FlagNewPage)
    [#](StrFor(12));
    NumberPage = NumberPage + 1;
    CurrentLine(0);
    PrintHeadCardReestr(FALSE);
  end;

end;


/**************************************************************************\
|                       Печать окончания для реестра                       |
\**************************************************************************/
macro PrintBotCardReestr

  var ReportSummaComissReestr = "";
  var ReportISOComissReestr   = "";
  var ReportCountSprReestr    = "";
  var FIO                     = "";

  if (SummaComissReestr > 0)
    ReportSummaComissReestr = SummaComissReestr;
    ReportISOComissReestr   = currency.ExternalCode;
  end;

  if (CountSprReestr > 0)
    ReportCountSprReestr = CountSprReestr;
  end;

  person.Oper = {exoper};
  if (GetEQ(person))
    FIO = person.Name;
  end;

  if (CurrentLine() == 0)
    [+---------------------+---------------------+-------------------+------+-----------------+----------+----------+--------------+];
  end;
  if (NumberPage == 1)
    [+---------------------+---------------------+-------------------+------+-----------------+----------+----------+--------------+];
  end;
  [|ИТОГО:                                                           ###   ################# ##########            ### ##########|]
  (currency.ExternalCode,SummaOperReestr,ReportCountSprReestr,ReportISOComissReestr,ReportSummaComissReestr);
  [+---------------------+---------------------+-------------------+------+-----------------+----------+----------+--------------+];
  [ ];
  [ ];
  [      Кассир обменного пункта: _________________________ (###########################)]
  (FIO);
  [                                      (подпись)];

  SummaOperReestr   = $0L;
  SummaComissReestr = $0L;
  CountSprReestr    = 0;

end;


/**************************************************************************\
|                        Обработка одного документа                        |
\**************************************************************************/
macro WriteStringToReport

  var Issuer_Name    = "";
  var CommSum        = "";
  var ISO_CurrComm   = "";
  var Resid_Name     = "";
  var Seria_Spr      = "";
  var Number_Spr     =  0;
  var Count_Str      =  1;
  var RestForSummary =  1;
  var WasOutSpr      = FALSE;

  if (CurrentLine() == 0)
    if (NumberPage == 1)
      if (FirstReestr)
        FirstReestr = FALSE;
      else
        [#](StrFor(12));
        CurrentLine(0);
      end;
      ОбщаяШапка(pay_doc.Date_Document,reestr.UserRegNumb,NumberPage,TRUE);
      PrintHeadCardReestr(TRUE);
    end;
  end;

  if (pay_doc_n36.CommSum > 0)
    CommSum      = pay_doc_n36.CommSum;
    ISO_CurrComm = currency.ExternalCode;
  end;
  if (pay_doc_n36.cardIssuerCode > 0)
    issuer.ID = pay_doc_n36.cardIssuerCode;
    if (GetEQ(issuer))
      Issuer_Name = issuer.Name;
    end;
  end;
  if (pay_doc.FlagRez == "")
    Resid_Name = "резидент";
  else
    Resid_Name = "нерезидент";
  end;
  if (pay_doc_n36.CodCur > 0)
    GetSprvSerNum(pay_doc_n36.iApplicationKind,
                  pay_doc_n36.ApplicationKey,
                  GetBSOShortName (),Seria_Spr,Number_Spr   );
  end;
  if ((Seria_Spr == "") AND (Number_Spr == 0))
    Number_Spr = "---";
  else
    WasOutSpr  = TRUE;
    Number_Spr = String(Number_Spr);
    Number_Spr = StrFillBeg(Number_Spr,"0",7); /* дополнение ведущими нулями */
  end;

  if ((StrLen(Issuer_Name) > 21) OR (StrLen(pay_doc_n36.cardBankName) > 21))
    Count_Str = 2;
  end;

  if (FlagPrintSummaryForPage)
    RestForSummary = 3;
  end;

  if (CurrentLine() + Count_Str + RestForSummary > MaxNumberStringsCard)
    PrintBotCardReestrForPage(TRUE);
  end;

  [|#####################|#####################|###################| ###  |#################|## #######|##########|### ##########|]
  (Issuer_Name:w,pay_doc_n36.cardBankName:w,pay_doc_n36.cardNumber,currency.ExternalCode,pay_doc.InSum,Seria_Spr,Number_Spr,Resid_Name:c,ISO_CurrComm,CommSum);

  SummaOperPage     = SummaOperPage     + pay_doc.InSum;
  SummaOperReestr   = SummaOperReestr   + pay_doc.InSum;
  SummaComissPage   = SummaComissPage   + pay_doc_n36.CommSum;
  SummaComissReestr = SummaComissReestr + pay_doc_n36.CommSum;
  if (WasOutSpr)
    CountSprPage    = CountSprPage   + 1;
    CountSprReestr  = CountSprReestr + 1;
  end;

  FirstRecReestr = FALSE;

end;


/**************************************************************************\
|                         Обработка одного реестра                         |
\**************************************************************************/
macro WorkWithOneReestr

  var stat_doc;
  var IsCur;
  var RestForSummary = 1;

  NumberPage = 1;
  CurrentLine(0);
  FirstRecReestr = TRUE;

  FindCurrencyForReestr();

  SummaOperReestr   = $0L;
  SummaComissReestr = $0L;
  CountSprReestr    = 0;
  SummaOperPage     = $0L;
  SummaComissPage   = $0L;
  CountSprPage      = 0;

  if (reestr.IncomeCodIntValue == 0)
    IsCur = 0;
  else
    IsCur = 1;
  end;

  pay_doc.IsCur         = IsCur;
  pay_doc.FNCash        = reestr.Branch;
  pay_doc.GroupOpert    = 11;
  pay_doc.Oper          = reestr.Oper;
  pay_doc.Date_Document = reestr.CurDate;
  pay_doc.NumOpert      = 36;
  pay_doc.CodCur        = reestr.IncomeCodIntValue;
  stat_doc = GetGE(pay_doc);
  while (stat_doc)
    if ((pay_doc.IsCur         == IsCur                   ) AND
        (pay_doc.FNCash        == reestr.Branch           ) AND
        (pay_doc.GroupOpert    == 11                      ) AND
        (pay_doc.Oper          == reestr.Oper             ) AND
        (pay_doc.Date_Document == reestr.CurDate          ) AND
        (pay_doc.NumOpert      == 36                      ) AND
        (pay_doc.CodCur        == reestr.IncomeCodIntValue)    )
      if ( ( pay_doc.Action < 2 ) and ( ( pay_doc.IsSuspended == "" ) or ( pay_doc.IsSuspended == "X" ) ) )
        SetRecordAddr(pay_doc_n36,pay_doc,0,0,TRUE);
        if (GetVarSize(pay_doc) == 0)
          pay_doc_n36.cardNumber      = "";
          pay_doc_n36.cardTypeCode    = "";
          pay_doc_n36.cardTypeCodePos = 0;
          pay_doc_n36.cardIssuerCode  = 0;
          pay_doc_n36.cardBankName    = "";
        end;
        if (pay_doc_n36.ReestrID == reestr.ID)
          WriteStringToReport();
        end;
      end;
      stat_doc = Next(pay_doc);
    else
      stat_doc = FALSE;
    end;
  end;

  if (NOT FirstRecReestr)
    if (FlagPrintSummaryForPage)
      RestForSummary = 3;
    end;

    if (CurrentLine() + 6 + RestForSummary > MaxNumberStringsCard)
      PrintBotCardReestrForPage(TRUE);
    else
      if (NumberPage > 1)
        PrintBotCardReestrForPage(FALSE);
      end;
    end;
    PrintBotCardReestr();
  end;

end;


/**************************************************************************\
|                         Печать реестров в отчет                          |
\**************************************************************************/
macro PrintReestrsToReport

  var stat_reestr;

  reestr.Branch       = Branch;
  reestr.CurDate      = {curdate};
  reestr.Oper         = {exoper};
  reestr.Kind_Oper    = CDF_OperCardTake;
  reestr.IncomeRefVal = 0;
  reestr.OutRefVal    = 0;
  reestr.Rate_Ref     = 0;
  reestr.ID           = 0;
  stat_reestr = GetGE(reestr);
  while (stat_reestr)
    if ((reestr.Branch    == Branch          ) AND
        (reestr.CurDate   == {curdate}       ) AND
        (reestr.Oper      == {exoper}        ) AND
        (reestr.Kind_Oper == CDF_OperCardTake)    )
      WorkWithOneReestr();
      stat_reestr = Next(reestr);
    else
      stat_reestr = FALSE;
    end;
  end;

end;


/**************************************************************************\
|                           Печать одного реестра                          |
\**************************************************************************/
macro ПечатьОдногоРеестра ( innerReestr )
  Level_Reestr = Office.ReestrCardSeparator;
  if (MaxNumberStringsCard < 39)
    MaxNumberStringsCard = 39;
  end;

  setbuff( r, innerReestr );
  copy( reestr, r );
  WorkWithOneReestr();
end;


/**************************************************************************\
|                     Печать всех реестров одного типа                     |
\**************************************************************************/
macro ПечатьВсехРеестров (Kind_Oper)

  if (MaxNumberStringsCard < 39)
    MaxNumberStringsCard = 39;
  end;

  Level_Reestr = Office.ReestrCardSeparator;

  if (CreateOrUpdateReestrs())
    PrintReestrsToReport();
  else
    [ ];
    [ Ошибка при создании реестров];
  end;

end;
