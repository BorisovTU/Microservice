
/* Приложение 13. Справка о принятых вечерней кассой сумках */

import DeprIntr, RD_Comm;

file BagList( "bag.dbt" ) key 0;
file BagHead( "bag_head.dbt" ) key 0;
file Route( "route.dbt" ) key 0;

const
  BST_ACPTD_RC    = 4,        
  BST_ACPTD_NORC  = 5, 
  BST_CHKD        = 8, 
  BST_RETURNED    = 10;
 
const
  RST_ACCEPTED = 1;

var
  {DeskOpen}, 
  {EDeskOpen}, 
  Dd,
  TotTotDeclSum = $0,
  TotTotRealSum = $0,
  TotNBags = 0,
  TotNOpenedBags = 0,
  TotNEmptyBags = 0,
  Routes = "";

macro BagWasInState( State )

  BagDoc.BagAppKind = BagList.ApplicationKind;
  BagDoc.BagAppKey = BagList.ApplicationKey;
  BagDoc.State = State;
  return GetEQ( BagDoc );
end;

macro IsSuitableBag

  if ( {EDeskOpen} )
    if ( ( BagWasInState( BST_ACPTD_RC ) )
      or ( BagWasInState( BST_ACPTD_NORC ) ) )
      return true;
    end; 
  end; 

  if ( {DeskOpen} )
    if ( BagWasInState( BST_RETURNED ) 
      or BagWasInState( BST_CHKD ) ) 
      return true;
    end; 
  end; 

  return false;
end;

macro GetBagInfo( TotDeclSum, TotRealSum, WasOpened )

  var
    RecFound;

  TotDeclSum = $0;
  TotRealSum = $0;
  WasOpened = false;

  ClearRecord( BagList );
  BagList.Branch = BagHead.Branch;  
  BagList.Date = BagHead.Date;
  BagList.ClientType = BagHead.ClientType;
  BagList.ClientID = BagHead.ClientID;
  BagList.Operation = BagHead.Operation;
  BagList.Number = BagHead.Number;
  RecFound = GetGE( BagList );
  while ( RecFound 
    and ( BagList.Branch == BagHead.Branch )  
    and ( BagList.Date == BagHead.Date )
    and ( BagList.ClientType == BagHead.ClientType )
    and ( BagList.ClientID == BagHead.ClientID )
    and ( BagList.Operation == BagHead.Operation )
    and ( BagList.Number == BagHead.Number ) )
    if ( IsSuitableBag )
      FindCashValue( BagList.ValueRef );
      if ( CashValue.TypeValue == TYPERES_CURRENCY )
        GetCurrCodeAndCBRate( BagList.ValueRef );

        if ( BagList.DeclaredSum )
          TotDeclSum = TotDeclSum + Cur2Rub( BagList.DeclaredSum );
        else
          TotDeclSum = TotDeclSum + Cur2Rub( Money( BagList.DeclaredItems ) );
        end;
        if ( BagList.RealSum )
          TotRealSum = TotRealSum + Cur2Rub( BagList.RealSum );
        else
          TotRealSum = TotRealSum + Cur2Rub( Money( BagList.RealItems ) );
        end;
        if ( BagWasInState( BST_ACPTD_RC ) 
          or BagWasInState( BST_CHKD ) 
          or BagWasInState( BST_RETURNED ) )
          WasOpened = true;
        end;
      end; 
    end;
    RecFound = Next( BagList );
  end;
  SetParm( 0, TotDeclSum );
  SetParm( 1, TotRealSum );
  SetParm( 2, WasOpened );
end;

macro TreatARoute

  var
    DeclSum,
    RealSum,
    IsOpened,
    NBags = 0,
    NOpenedBags = 0,
    NEmptyBags = 0,
    RecFound;

  ClearRecord( BagHead );
  BagHead.Branch = Route.Branch;  
  BagHead.Date = Route.Date;
  BagHead.RouteID = Route.ID;
  RecFound = GetGE( BagHead );
  while ( RecFound
    and ( BagHead.Branch == Route.Branch )  
    and ( BagHead.Date == Route.Date )
    and ( BagHead.RouteID == Route.ID ) )
    if ( ( BagHead.Operation == CASHOP_PANT_ECRD ) or ( BagHead.Operation == CASHOP_PANT_RCRD ) )
      GetBagInfo( DeclSum, RealSum, IsOpened ); 
      TotTotDeclSum = TotTotDeclSum + DeclSum;
      TotTotRealSum = TotTotRealSum + RealSum;
      NBags = NBags + 1;
      if ( IsOpened )
        NOpenedBags = NOPenedBags + 1;
      end;
    elif ( BagHead.Operation == 0 )
      NEmptyBags = NEmptyBags + 1;
    end;
    RecFound = Next( BagHead ); 
  end;

  if ( NBags or NEmptyBags )
    TotNBags = TotNBags + NBags;
    TotNOpenedBags = TotNOpenedBags + NOpenedBags;
    TotNEmptyBags = TotNEmptyBags + NEmptyBags;
    Routes = Routes + "№" + Route.ID + " ";
  end;
end;

macro IsSuitableRoute

  if ( {EDeskOpen} and ( Route.EDesk == "" ) ) 
    return false;
  end;

  if ( {DeskOpen} and ( Route.EDesk != "" ) ) 
    return false;
  end;

  if ( Route.State != RST_ACCEPTED ) 
    return false;
  end;

  return true;
end;

macro TreatRoutes

  var
    RecFound;

  ClearRecord( Route );
  Route.Branch = Branch;
  Route.Date = Dd;
  RecFound = GetGE( Route );
  while ( RecFound     
    and ( Route.Branch == Branch )
    and ( Route.Date == Dd ) )
    if ( IsSuitableRoute )
      TreatARoute;
    end;
    RecFound = Next( Route );
  end;
end;

macro Report( D )

  var
    NBagsStr = "",
    NOpenedStr = "",
    NEmptyStr = "",
    DeclSumStr = "",
    RealSumStr = "";
   

  Dd = D;
  TreatRoutes;

  if ( TotNBags )
    NBagsStr = String( TotNBags ) + " (" + IntToStr( TotNBags ) + ")";
    DeclSumStr = TotTotDeclSum + " (" + RubToStr( TotTotDeclSum ) + ")";
  end;
  if ( TotNOpenedBags )
    NOpenedStr = String( TotNOpenedBags ) + " (" + IntToStr( TotNOpenedBags ) + ")";
    RealSumStr = TotTotRealSum + " (" + RubToStr( TotTotRealSum ) + ")";
  end;
  if ( TotNEmptyBags )
    NEmptyStr = String( TotNEmptyBags ) + " (" + IntToStr( TotNEmptyBags ) + ")";
  end;

  [                                        СПРАВКА                                   ];
  if ( {DeskOpen} )
    [                О ПРИНЯТЫХ КАССОЙ ПЕРЕСЧЕТА СУМКАХ С ДЕНЕЖНОЙ НАЛИЧНОСТЬЮ         ];
  else
    [                 О ПРИНЯТЫХ ВЕЧЕРНЕЙ КАССОЙ СУМКАХ С ДЕНЕЖНОЙ НАЛИЧНОСТЬЮ         ];
  end;
  [                                   И ПОРОЖНИХ СУМКАХ                              ];
  [ ################################################################################ ]
  ( ( "За " + String( Dd:m ) ):c );
  [                                                                                  ];
  [ Всего по ################################################### маршрутам (заездам) ]
  ( Routes );
  [ принято кассой ################################ сумок на объявленную сумму, руб. ]
  ( NBagsStr );
  [ ################################################################################ ]
  ( DeclSumStr:w );
  [ из них вскрыто и пересчитано ############################## сумок на сумму, руб. ]
  ( NOpenedStr );
  [ ################################################################################ ]
  ( RealSumStr:w );
  [ Кроме того, получено ############################################ порожних сумок ]
  ( NEmptyStr );
  [ Кассовый работник вечерней кассы                     ___________________________ ];
  [                                                                (подпись)         ];
  [ Операционный работник вечерней кассы                 ___________________________ ];
  [                                                                (подпись)         ];
  [                                                                                  ];
  [ Принято ############################### для пересчета __________________________ ]
  ( Dd:m );
  [                                                          (количество цифрами     ];
  [                                                              и прописью)         ];
  [ сумок и такое же количество накладных к ним на сумму, руб. _____________________ ];
  [ ________________________________________________________________________________ ];
  [                                (цифрами и прописью)                              ];
  [                                                                                  ];
  [ Контролирующий работник кассы пересчета              ___________________________ ];
  [                                                                (подпись)         ];
  [ (Расписка повторяется каждым контролирующим работником)                          ];

  [ Заведующий кассой  ____________________                                          ];
  [                         (подпись)                                                ];
  [ Порожние сумки в количестве ############################## принял контролирующий ]
  ( NEmptyStr );
  [ работник кассы пересчета                                                         ];
  [ Контролирующий работник кассы пересчета              ___________________________ ];  
  [                                                                (подпись)         ];
  [                                                                                  ];
  [ В результате пересчета сумок с денежной наличностью оказалось:                   ];
  [ Наличных денег на сумму, руб. __________________________________________________ ];
  [ Чеков              -"-   -"-  __________________________________________________ ];        
  [ Недостач           -"-   -"-  __________________________________________________ ];        
  [ Излишков           -"-   -"-  __________________________________________________ ];        
  [ Неплатежных        -"-   -"-  __________________________________________________ ];        
  [ Сомнительных       -"-   -"-  __________________________________________________ ];       
  [                                                                                  ];
  [ Наличные деньги в сумме, руб. __________________________________________________ ];
  [                                                     (прописью)                   ];
  [ приняты в операционную кассу кредитной организации                               ];
  [                                                                                  ];
  [ Заведующий кассой                                    ___________________________ ];
  [                                                                (подпись)         ];                       
  [ Проверено:                                                                       ]; 
  [                                                                                  ];
  [ Главный бухгалтер:                                   ___________________________ ];
  [                                                                (подпись)         ];                       
  Print( StrFor( 12 ) );
end;