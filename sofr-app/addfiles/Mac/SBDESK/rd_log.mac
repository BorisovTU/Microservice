
/* Журнал учета принятых сумок с ценностями */

import DeprIntr, RD_Comm, RSD;

file BagList( "bag.dbt" ) key 2;
file RouteCol( "routecol.dbt" ) key 0;
file BagHead( "bag_head.dbt" ) key 0;

const
  BST_NOT_ACPTD = 3;

var
  Dd,
  RouteID,
  N = 0,
  TotSum = $0;
private var g_FNcash = NumFNcash();  

macro OutHead

  [ # ]( {Name_Bank} );
  [ ];
  [                                                   Ж У Р Н А Л                                               ];
  [                               УЧЕТА ПРИНЯТЫХ СУМОК С ЦЕННОСТЯМИ И ПОРОЖНИХ СУМОК                            ];
  [ ];
  [                                                                                  Маршрут (заезд) № ######## ]
  ( RouteID );
  [ ];
  [-------------------------------------------------------------------------------------------------------------];
  [ №   | №         | Сумма по          | Сумма по           |Вал| Примечание                                   ];
  [ п/п | сумок     | накладным         | накладным, валюта  |   |                                              ];
  [-------------------------------------------------------------------------------------------------------------];
end;

macro OutLine

  var
    RawSum,
    Sum,
    SumC = $0;

  if ( not FindCashValue( BagList.ValueRef ) )
    MsgBox( "Не найден ресурс кассы с кодом ", BagList.ValueRef );
    Exit( 1 );
  end;

  GetCurrCodeAndCBRate( BagList.ValueRef );

  if ( BagList.DeclaredSum )
    RawSum = BagList.DeclaredSum;
  else
    RawSum = Money( BagList.DeclaredItems );
  end;

  if ( CurrCode )
    SumC = RawSum;
    Sum = Cur2Rub( RawSum );
  else
    Sum = RawSum;    
  end;

  FindBagNum( BagList.Number );
  N = N + 1;

  [ #### ########### ################### #################### ### ############################################# ]
  (
    N,
    BagNum.NumberBag,
    Sum,
    SumC,
    Curr.Short_Name,
    BagList.DamageDesc:w
  ); 
    
  TotSum = TotSum + Sum;
end;

macro OutTotals

  [-------------------------------------------------------------------------------------------------------------];
  [      Итого:      ###################                          ]
  (
    TotSum
  );
end;

macro CollectorName( pBranch, pID )
  var cmd, rs;
  var I1 = "", I2 = "";
  var sFIO = "";

  cmd = RsdCommand( "select t_FirstName  as rFN, " +
                           "t_MiddleName as rMN, " + 
                           "t_LastName   as rLN " +
                    "from dcollectr_dbt " +
                    "where t_Branch = ? and " +
                          "t_ID = ?" );

  cmd.addParam( "branch",  RSDBP_IN );
  cmd.addParam( "id",      RSDBP_IN );
  cmd.value( "branch" ) = pBranch;
  cmd.value( "id" ) = pID;

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    if ( StrLen( rs.value( "rFN" ) ) > 0 )
      I1 = SubStr( rs.value( "rFN" ), 1, 1 ) + ".";
      if ( StrLen( rs.value( "rMN" ) ) > 0 )
        I2 = SubStr( rs.value( "rMN" ), 1, 1 ) + ".";
      end;
    end;
    sFIO = rs.value( "rLN" ) + " " + I1 + " " + I2 + "   ";
  end;
  return sFIO;
end;

macro EnumCollectors

  var
    I1 = "", I2 = "",
    RecFound,
    Str = "";

  ClearRecord( RouteCol );
  RouteCol.Branch = g_FNcash;  
  RouteCol.Date = Dd;
  RouteCol.RouteID = RouteID;
  RecFound = GetGE( RouteCol );
  while ( RecFound
    and ( RouteCol.Branch == g_FNcash )  
    and ( RouteCol.Date == Dd )
    and ( RouteCol.RouteID == RouteID ) )
    Str = Str + CollectorName( RouteCol.Branch, RouteCol.ColID );
    RecFound = Next( RouteCol );
  end;
  return Str;
end;

macro CountBags( Filled, Empty, EmptyNums )

  var
    RecFound;

  Filled = 0;
  Empty = 0;
  EmptyNums = "";

  ClearRecord( BagHead );
  BagHead.Branch = g_FNcash;
  BagHead.Date = Dd;
  BagHead.RouteID = RouteID;
  RecFound = GetGE( BagHead );
  while ( RecFound 
    and ( BagHead.Branch == g_FNcash )  
    and ( BagHead.Date == Dd )
    and ( BagHead.RouteID == RouteID ) )
    if ( BagHead.Operation )
      Filled = Filled + 1;
    else
      Empty = Empty + 1;
      FindBagNum( BagHead.Number );
      EmptyNums = EmptyNums + BagNum.NumberBag + " ";
    end;
    RecFound = Next( BagHead );
  end;

  SetParm( 0, Filled );
  SetParm( 1, Empty );
  SetParm( 2, EmptyNums );
end;

macro OutBottom

  var
    NFilledBags, NEmptyBags,
    NFilledStr = "", NEmptyStr = "",
    EmptyNums;

  CountBags( NFilledBags, NEmptyBags, EmptyNums );
  if ( NFilledBags )
    NFilledStr = IntToStr( NFilledBags );
  end;
  if ( NEmptyBags )
    NEmptyStr = String( NEmptyBags );
  end;

  [ ];
  [ Указанные опломбированные сумки в количестве # ]( NFilledStr );
  [ штук на объявленную сумму, руб.: # ]( String( TotSum ) + " (" + RubToStr( TotSum ) + ")" );   
  [ и накладные к ним по данному маршруту (заезду) приняты в исправной ];
  [ упаковке от инкассаторов ];
  [ ########################################################################################################### ]
  ( EnumCollectors:w );
  [ ];
  [ Кроме того, принято # ]( NEmptyStr );
  [ порожних сумок за номерами # ]( EmptyNums );
  [ ];
  [ Количество и номера сданных инкассаторами сумок с ценностями соответствуют их количеству ];
  [ и номерам по записям в явочных карточках и накладных. ];
  [   Сумма, указанная в явочных карточках и накладных, соответствует сумме по настоящему журналу ];
  [ ];
  [ Сдали: ];
  [ Инкассаторы ___________________________________________________________ ];
  [                                    (подписи)                            ];
  [ ];
  [ Приняли: ];
  [ Кассовый работник       __________________________ ];
  [                                 (подпись)          ];
  [ Операционный работник   __________________________ ];
  [                                 (подпись)          ];
  [ ];
  [ "___" ___________________ ____ г.   _____________ час. _____________ мин. ];
  [ ];
  [   Проверка соответствия количества сданных инкассаторами сумок в кассу кредитной организации ];
  [ количеству принятых ими сумок от организаций, филиалов и операционных касс вне кассового узла ];
  [ произведена. ];
  [ ];
  [ Руководитель подразделения инкассации ];
  [ (дежурный инкассатор)   _________________________ ];
  [                                 (подпись)         ];
  [ ];
  [ "___" ___________________ ____ г. ];
  [ ];
  Print( StrFor( 12 ) );
end;

macro TreatBags

  var
    RecFound;

  ClearRecord( BagList );
  BagList.Branch = g_FNcash;  
  BagList.Date = Dd;
  BagList.State = BST_NOT_ACPTD;
  RecFound = GetGE( BagList );
  while ( RecFound
    and ( BagList.Branch == g_FNcash )   
    and ( BagList.Date == Dd ) )
    if ( BagList.RouteID == RouteID )
      OutLine;
    end;
    RecFound = Next( BagList );
  end;
end;

macro OutLog;

  N = 0;
  TotSum = $0;
  OutHead;
  TreatBags;
  OutTotals;
  OutBottom;
end;

macro Report( D, RID )

  Dd = D;
  RouteID = RID;
  OutLog;
  OutLog;
end;
