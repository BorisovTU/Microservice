
/* превышения лимита на счетах сейфов */

import deprintr;

file CashAcc("sb_casac.dbt") key 0;
file CashVal("sb_casvl.dbt") key 0;
file DeskSubj("desksubj.dbt") key 1;

const
  RESMESUNIT_POINT = 1,  /* Поштучно  */
  RESMESUNIT_MONEY = 2;  /* Стоимость */

const
  DST_IO = 1,				/* Инкассация */
  DST_SAFE = 2,				/* Сейф */
  DST_WORKDESK = 3,			/* Рабочий стол кассира */
  DST_RDESK = 4,			/* Касса пересчета */
  DST_PANTRY = 5;			/* Кладовая отделения */

array
  ValNameArr;

var
  Branch = NumFNCash,
  {curdate};

var
  SmthDisplayed;

macro OutHead

  const
    Title = "Превышения лимитов ресурсов по сейфам на " + String({curdate}:m);

  [ ];
  [###############################################################################################################################]
  (Title:c);
  [ ];
  [-------------------------------------------------------------------------------------------------------------------------------];
  [|                |                                        |                  |              |                  |              |];
  [|    С е й ф     |         Наименование ресурса           | Остаток (деньги) | Остаток (шт) |  Лимит (деньги)  |  Лимит (шт)  |];
  [|                |                                        |                  |              |                  |              |];
  [-------------------------------------------------------------------------------------------------------------------------------];
end;

macro OutValNameTail

  var 
    i = 1;

  while ( i < ASize( ValNameArr ) )
    [                  ######################################## ]
    (
      ValNameArr(i)
    );
    i = i + 1;
  end;
end;

macro FindCashVal(RefValue);

  CashVal.RefValue = RefValue;
  return GetEQ(CashVal);
end;

macro OutLine

  if ( not FindCashVal( CashAcc.RefValue ) )
    MsgBox( "Ценность не найдена|Код: ", CashAcc.RefValue );
    Exit( -1 );
  end;

  if ( CashVal.UnitMeas == RESMESUNIT_MONEY )
    if ( ( CashAcc.LimMoney == 0 ) or ( CashAcc.RestMoney <= CashAcc.LimMoney ) )
      return;
    end; 
  else
    if ( ( CashAcc.LimCol == 0 ) or ( CashAcc.RestCol <= CashAcc.LimCol ) )
      return;
    end; 
  end;

  StrSplit( CashVal.NameValue, ValNameArr, 40, 40, 1 );

  [ ################ ######################################## ################## ############## ################## ############## ]
  (
    CashAcc.CashOper,
    ValNameArr(0),
    CashAcc.RestMoney:z,
    CashAcc.RestCol:z,
    CashAcc.LimMoney:z,
    CashAcc.LimCol:z
  );
  OutValNameTail;
end;

macro TreatDeskSubj

  var
    RecFound;

  ClearRecord( CashAcc );
  CashAcc.Branch = Branch;
  CashAcc.Date = {curdate};
  CashAcc.CashOper = DeskSubj.Name;
  CashAcc.SubAccount = "";
  RecFound = GetGE( CashAcc );
  while ( RecFound
    and ( CashAcc.Branch == Branch )
    and ( CashAcc.Date == {curdate} )
    and ( CashAcc.CashOper == DeskSubj.Name )
    and ( CashAcc.SubAccount == "" ) )
    OutLine;
    RecFound = Next( CashAcc );
  end;
end;

  var
    RecFound;
    
  OutHead;
  ClearRecord( DeskSubj );
  DeskSubj.Branch = Branch;
  DeskSubj.Type = DST_SAFE;
  RecFound = GetGE( DeskSubj );
  while ( RecFound
    and ( DeskSubj.Branch == Branch )
    and ( DeskSubj.Type == DST_SAFE ) )
    TreatDeskSubj;
    RecFound = Next( DeskSubj );
  end;
end;