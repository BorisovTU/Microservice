/*
$Name:             dk_f211.mac
$Module:           RS-Retail
$Description:      Книга учета драгоценных металлов
*/

import "f211_class.mac", chkboss;
import Xbt;

private const
  FLAGDEBCRED_IN   = 1,  /* Кредит */
  FLAGDEBCRED_OUT  = 2;  /* Дебет */

private const
  CASHOP_REINFORCE = 1,  /* Подкрепление кассира */
  CASHOP_RETURN    = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT   = 3,  /* Инкассация */
  CASHOP_ADVANCE   = 4,  /* Аванс */
  CASHOP_DEBIT     = 5,  /* Расход */
  CASHOP_CREDIT    = 6,  /* Приход */
  CASHOP_REMOVE    = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1  = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE   = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1 = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE   = 11,  /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1 = 12,  /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_DEBET_SAFE = 13,  /* Расход из сейфа */
  CASHOP_CREDIT_SAFE = 14, /* Приход в сейф */
  CASHOP_REVAL_POS = 18, /* Переоценка (+) */
  CASHOP_REVAL_NEG = 19, /* Переоценка (-) */
  CASHOP_PANT_RI   = 20, /* Подкрепление филиала */
  CASHOP_PANT_RET  = 21, /* Возврат из филиала */
  CASHOP_PANT_RIO  = 22, /* Подкрепление оперчасти */
  CASHOP_PANT_RETO = 23, /* Возврат из оперчасти */
  CASHOP_PANT_COL  = 24, /* Инкассация */
  CASHOP_PANT_ADV  = 25, /* Аванс */
  CASHOP_PANT_RDEB = 30, /* Расход по кассе пересчета */
  CASHOP_PANT_RCRD = 31, /* Приход по кассе пересчета */
  CASHOP_PANT_RDRT = 32; /* Сдача ресурса в кладовую из Кассы пересчета */

private var 
  OperBrigade = GetOperBrigade;

class (TDragMetExecutor) TDragMetExecutorInDesk

var Cred = 0, Deb = 0, OtherCred = 0, OtherDeb = 0;


private macro IsDebetCredit( _IsDebCred, _Operation )

var
  RetVal = CodeFor( _IsDebCred );
  if ( ( _Operation == CASHOP_ADVANCE )
    or ( _Operation == CASHOP_COLLECT )
    or ( _Operation == CASHOP_REMOVE_SAFE )
    or ( _Operation == CASHOP_REMOVE_SAFE_1 ) 
    or ( _Operation == CASHOP_DEBET_SAFE )
    or ( _Operation == CASHOP_CREDIT_SAFE ) 
    or ( _Operation == CASHOP_REVAL_POS )
    or ( _Operation == CASHOP_REVAL_NEG ) )
    if ( RetVal == FLAGDEBCRED_IN )
      RetVal = FLAGDEBCRED_OUT;
    else
      RetVal = FLAGDEBCRED_IN;
    end;
  end;
  return RetVal;
end;

private macro CalcTurns(RefValue)

  Cred = 0;
  Deb = 0;

  var cmd, rs;
  cmd = RsdCommand( "select D.T_FLAGDEBCREDOPER as dc, D.T_NUMOPER as numop, D.T_VALUECOL as itms " +
                    "from dsb_casdc_dbt d " +
                    "where D.T_BRANCH=? and " +
                          "D.T_BRIGADE=? and " +
                          "D.T_REFVALUE=? and " +
                          "D.T_CASHDATEDOC=?" );

  cmd.addParam( "branch",  RSDBP_IN );
  cmd.addParam( "brigade", RSDBP_IN );
  cmd.addParam( "refval",  RSDBP_IN );
  cmd.addParam( "date",    RSDBP_IN );
  cmd.value( "branch" )  = Branch;
  cmd.value( "brigade" ) = OperBrigade;
  cmd.value( "refval" )  = RefValue;
  cmd.value( "date" )    = {curdate};
  cmd.execute;
  rs = RsdRecordset( cmd );
  while ( rs.moveNext )
    if ( ( Int( rs.value( "numop" ) ) == CASHOP_ADVANCE )      OR
         ( Int( rs.value( "numop" ) ) == CASHOP_COLLECT )      OR
         ( Int( rs.value( "numop" ) ) == CASHOP_REMOVE_SAFE )  OR
         ( Int( rs.value( "numop" ) ) == CASHOP_REMOVE_SAFE_1 ) )
      if ( IsDebetCredit( rs.value( "dc" ), rs.value( "numop" ) ) == FLAGDEBCRED_OUT )
        Cred = Cred + Int( rs.value( "itms" ) );
      else
        Deb  = Deb  + Int( rs.value( "itms" ) );
      end;
    end;
  end;
end;

private macro CalcOtherTurns(RefValue)

  var
    IsCur = 0,
    RecFound;

  OtherCred = 0;
  OtherDeb = 0;

  var cmd, rs;
  cmd = RsdCommand( "select D.T_FLAGDEBCREDOPER as dc, D.T_NUMOPER as numop, D.T_VALUECOL as itms " +
                    "from dsb_casdc_dbt d " +
                    "where D.T_BRANCH=? and " +
                          "D.T_BRIGADE=? and " +
                          "D.T_REFVALUE=? and " +
                          "D.T_CASHDATEDOC=?" );

  cmd.addParam( "branch",  RSDBP_IN );
  cmd.addParam( "brigade", RSDBP_IN );
  cmd.addParam( "refval",  RSDBP_IN );
  cmd.addParam( "date",    RSDBP_IN );
  cmd.value( "branch" )  = Branch;
  cmd.value( "brigade" ) = OperBrigade;
  cmd.value( "refval" )  = RefValue;
  cmd.value( "date" )    = {curdate};
  cmd.execute;
  rs = RsdRecordset( cmd );
  while ( rs.moveNext )
    if ( ( Int( rs.value( "numop" ) ) != CASHOP_ADVANCE )      AND
         ( Int( rs.value( "numop" ) ) != CASHOP_COLLECT )      AND
         ( Int( rs.value( "numop" ) ) != CASHOP_REMOVE_SAFE )  AND
         ( Int( rs.value( "numop" ) ) != CASHOP_REMOVE_SAFE_1 ) )
      if ( IsDebetCredit( rs.value( "dc" ), rs.value( "numop" ) ) == FLAGDEBCRED_OUT )
        OtherCred = OtherCred + Int( rs.value( "itms" ) );
      else
        OtherDeb  = OtherDeb  + Int( rs.value( "itms" ) );
      end;
    end;
  end;
end;

/******************************************************************************/
private macro GetNativeSafe

  file Group("rtgroup.dbt") key 2;

  Group.Branch = Branch;
  Group.Date = {curdate};
  Group.ID = GetOperBrigade;
  if(GetEQ(Group))
    return Group.SafeID;
  else
    MsgBox("Не найден сейф бригады № ", GetOperBrigade);
    Exit;
  end;
end;

/******************************************************************************/
//Получить ID сейфа текущей смены,
//если текущий пользователь ревизор или управляющий то ему предлагается выбрать смену
macro SelectSafe
	var Safe;

	if (IsBoss() OR IsManager() )
  		Safe = SelectBrigadeForBranch(Branch, {curdate}, OperBrigade);
		if(not Safe)
		    Exit;
		end;
	else
		Safe = GetNativeSafe;
	end;

	return Safe;
end;

private macro StayOnIngotRec( issueId )
  CiMisc.Type    = CII_INGOT;
  CiMisc.IssueID = issueId;
  XBT( getEQ(CiMisc), "Не найден вид слитка '" + CashVal.NameValue + "'|" +
                      "Печать отчета не возможна"); 
end;

private macro FillTmpKeyFields( issueId )
  StayOnIngotRec( issueId );
  CiCode.AutoKey = CiMisc.MaterialRef; 
  XBT( getEQ(CiCode), "Драгоценный метал не найден" );

  Tmp.Name       = CashVal.NameValue;
  Tmp.Weight = CiMisc.Weight;
  Tmp.MetallName=CiCode.String;
  end;

private macro FillTmpBookKeepValues( issueId, IngotAcc )
  StayOnIngotRec( issueId );
  CiCode.AutoKey = CiMisc.HallmarkRef; 
  if ( NOT getEQ(CiCode) )
    PrintLn( "***  Проба " + CiMisc.HallmarkRef + " не найдена" );
  end;

  Tmp.KolDeb = Tmp.KolDeb + int(string(Deb)) + int(string(OtherDeb));
  Tmp.MassLigDeb = Tmp.MassLigDeb + (CiMisc.Weight / 10000) * Tmp.KolDeb;
  Tmp.MassHimPureDeb = Tmp.MassHimPureDeb + ((CiMisc.Weight / 10000) * Tmp.KolDeb) * (CiCode.Double/1000);

  Tmp.KolKred = Tmp.KolKred + int(string(Cred)) + int(string(OtherCred));
  Tmp.MassLigKred = Tmp.MassLigKred + (CiMisc.Weight / 10000) * Tmp.KolKred;
  Tmp.MassHimPureKred = Tmp.MassHimPureKred + ((CiMisc.Weight / 10000) * Tmp.KolKred) * (CiCode.Double/1000);

  Tmp.SumOstatok         = IngotAcc.GetRubValue( {curdate} );
  Tmp.MassHimPureOstatok = IngotAcc.GetTotalPureWeight();
  Tmp.MassLigOstatok     = IngotAcc.GetTotalWeight();
  Tmp.KolOstatok         = CashAcc.RestCol;
end;

/******************************************************************************/
private macro FillTmpFile

  var IngotAcc   = TIngotCashAcc( CashAcc.RefValue, CashAcc.CashOper );
  var NeedInsert = false;
  var Name,MetallName,Mass;

  FillTmpKeyFields( CashVal.codIntValue );

  if( not getEQ(Tmp) )
    NeedInsert = true;
    ClearRecord(Tmp);
    FillTmpKeyFields( CashVal.codIntValue );
  end;

  CalcTurns( CashAcc.RefValue );
  CalcOtherTurns( CashAcc.RefValue );
  FillTmpBookKeepValues( CashVal.codIntValue, IngotAcc );
  
  if( not NeedInsert )
      update(Tmp);
  elif( not Insert(Tmp) )
    MsgBox("Не могу вставить строку во временный файл");
  end;

end;
/******************************************************************************/
private macro MainProcess
var 
  RecFound,DeskFound=true,Accfound=true,IngotAcc,
  Safe = SelectSafe();
  KeyNum(CashAcc,0);

  CashAcc.Branch     = Branch;
  CashAcc.Date       = {curdate};
  CashAcc.CashOper   = Safe;
  CashAcc.SubAccount = "";
  CashAcc.RefValue=0;
  Accfound=getGE(CashAcc);	
  while(( Accfound )
       and (CashAcc.CashOper == Safe) 
       and (CashAcc.Date     == {curdate}) )
    FindCashVal( CashAcc.RefValue );
    if(CashVal.TypeValue==DST_DRAGMET)
      FillTmpFile;
    end;
    Accfound=next(CashAcc) ;
  end;    

end;

  initTDragMetExecutor;
end;

/***************************************************/
  var obj = TDragMetExecutorInDesk;

  obj.RunReport();

end;
