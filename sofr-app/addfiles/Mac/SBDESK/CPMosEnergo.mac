/* Коммунальные платежи 
   Макросы для получателя "Мосэнерго_Пример"
   Возвращаемое значение: true/false. Отсутствие return означает true.
*/
import CPVars;

private var {curdate};

/* Инициация ввода данных */
macro INITPANEL
    COMPAY_EXTDOC.PaySum = $0.1;
end;

/* Пользовательское заполнение данных платежа */
macro EXTERNALPAYMENT
    COMPAY_EXTDOC.PaySum = $5;
    COMPAY_EXTDOC.PennySum = $0.2;
    COMPAY_DOC.InSum = $5.2;
end;

/* Расчет суммы платежа при переходе в поле "Сумма" в панели платежа */
macro CALCSUM
    COMPAY_EXTDOC.PaySum = $10;
end;

/* Формированиа уникального идентификатора платежа */
macro FORMUID
    COMPAY_EXTDOC.ChargeId = substr( COMPAY_DOC.ApplicationKey, 19 );
end;

/* Постобработка платежа, в т.ч. пользовательские проверки */
macro POSTPROCESS
    if( COMPAY_EXTDOC.PaySum < 1 )
        PushErrorMessage( "Сумма платежа должна быть больше 1.00 RUR" );
        return false;
    end;
end;

/* Использование сканера штрих-кода */
macro SCANDOCUMENT
    PushErrorMessage( "Сканер чтения данных квитанции коммунального платежа не установлен" );
    return false;
end;

/* Формирование дополнительных данных платежа */
macro FORMRTBP_ATTRIBUTE
    var attrCode, attrValue, isReadOnly, stat, i = 1;
    var attrs = TBPAttrValues( БП_АТРИБУТЫ_GTT, COMPAY_DOC.iApplicationKind, COMPAY_DOC.ApplicationKey, SB_COMPAY, COMPAY_BANKPRODUCTID );
    stat = attrs.MoveFirst;
    while( stat == 0 )
        attrs.Get( attrCode, attrValue, isReadOnly );
        if( isReadOnly )
            attrs.Set( attrCode, string(i) );
            i = i + 1;
        end;
        stat = attrs.MoveNext;
    end;
end;

/* Заполнение поля "Назначение платежа" */
macro FORMGROUND
    var text = COMPAY_DOC.Ground;
    var attrValues, separStr = "//";
    var pos = index( text, separStr );
    if( pos )
        text = trim( substr( text, 1, pos - 1 ) );
    end;

    if( strlen( text ) == 0 )
        text = COMPAY_TYPE.Name;
    end;

    attrValues = GetTmpAttrValuesString( COMPAY_DOC.iApplicationKind, COMPAY_DOC.ApplicationKey );
    if( strlen( attrValues ) != 0 )
        text = text + " " + separStr + " " + attrValues;
    end;

    COMPAY_DOC.Ground = text;
end;

/* Передача платежа получателю */
macro TRANSMIT
    const txtFileDir = GetRegVal( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", true );
    var transFile = txtFileDir + "CPOut_" + COMPAY_DOC.ApplicationKey + ".txt";
    SetOutput( transFile ); 
    print( Date({curdate}), "," );
    print( COMPAY_EXTDOC.CPAccount, "," );
    print( COMPAY_DOC.InSum, "," );
    print( COMPAY_DOC.ClientLastName, "," );
    print( COMPAY_DOC.ClientFirstName, "," );
    print( COMPAY_DOC.ClientSecondName, "," );
    print( COMPAY_TYPE.Name, "," );
    println( GetTmpAttrValuesString( COMPAY_DOC.iApplicationKind, COMPAY_DOC.ApplicationKey ) );
end;

/* Печать бланка квитанции */
macro PRINTBLANK
[
  МОСЭНЕРГО  ║ Лицевой счет № ______________________
             ║ 
             ║ Плательщик _____________________________________________________
             ║ 
             ║ Адрес      _____________________________________________________
             ║ 
             ║            _____________________________________________________
             ║ 
             ║ за    ___________ ______
             ║ 
             ║ Сумма ___________ руб.
             ║
═══╤═════════╩═════════════╤═════════════════════╤══════════════════════════════
 № │        Услуга         │ Расчетные показания │
   │                       ├──────────┬──────────┤
   │                       │ текущие  │предыдущие│
───┼───────────────────────┼──────────┼──────────┤
 1 │ Электроэнергия        │          │          │
───┼───────────────────────┼──────────┼──────────┤
 2 │ Электроэнергия (ночь) │          │          │
───┴───────────────────────┴──────────┴──────────┘
];
end;

/* Формирование электронного реестра платежей */
macro PRINTELREESTR
    var rs = GetPaymentsSet( COMPAY_RECIPIENT.Id );
    println( COMPAY_RECIPIENT.Name, ", Реестр платежей с ", COMPAY_BEGINDATE, " по ", COMPAY_ENDDATE );
    println( "─────" );
    while( rs.moveNext )
        print( Date(rs.value("T_DATE_DOCUMENT")) );
        print( ",", SQL_ConvTypeStr(rs.value("T_CPACCOUNT")) );
        print( ",", SQL_ConvTypeSum(rs.value("T_INSUM")) );
        print( ",", SQL_ConvTypeStr(rs.value("T_CLIENTLASTNAME")) );
        print( ",", SQL_ConvTypeStr(rs.value("T_CLIENTFIRSTNAME")) );
        print( ",", SQL_ConvTypeStr(rs.value("T_CLIENTSECONDNAME")) );
        print( ",", SQL_ConvTypeStr(rs.value("T_TYPENAME")) );
        println( ",", GetAttrValuesString( rs.value("T_IAPPLICATIONKIND"), rs.value("T_APPLICATIONKEY") ) );
        println;
    end;
    println( "──────────────────────────────────────────────────" );
end;

END;