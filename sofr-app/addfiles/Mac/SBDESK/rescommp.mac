
/* Общая часть фф. 25-а,в и т.п. */

import BankInter, deprintr, ChkPrn, CVSettng, Cur_Rate;

file DepType("sb_dtyp.dbt") key 2;
record PrevStat("sb_depst.dbt");
/*file DepStat("sb_depst.dbt") key 0;*/
/*file CashVal("sb_casvl.dbt") key 5;*/
/*file CashAcc("sb_casac.dbt") key 1;*/
var CashVal = TBFile("sb_casvl.dbt","w",5);
var CashAcc = TBFile("sb_casac.dbt","w",1);
var DepStat = TBFile("sb_depst.dbt","w",0);
file CashDoc("sb_casdc.dbt");
file Currency("currency.dbt") key 0;
file NBData("nb_data") key 0 write;
file RegNumList("reg_num.dbt") key 0;
file DeskSubj ("desksubj.dbt") key 0;
file OGSZIss("ogsz_iss.dbt") key 0;
file CIMisc("ci_misc.dbt") key 0;
file Cert("cert.dbt") key 0;
file CICode("ci_code.dbt") key 1;
file IngRate  ("ing_rate.dbt") key 1;

const
  DST_IO          = 1,    /* Инкассация */
  DST_SAFE        = 2,    /* Сейф */
  DST_WORKDESK    = 3,    /* Рабочий стол кассира */
  DST_RDESK       = 4,    /* Касса пересчета */
  DST_PANTRY      = 5,    /* Кладовая отделения */
  DST_EDESK       = 6;    /* Вечерняя касса */

const
  RDesk            = "К. пересчета",
  EDesk            = "Вечер. касса";

const
  CV_Currency = 1,
  CV_ValForm = 2,
  CV_PayDoc = 4,
  CV_MinCI = 500;

const
  FLAGDEBCRED_IN   = 1,  /* Кредит */
  FLAGDEBCRED_OUT  = 2;  /* Дебет */

const
  RESMESUNIT_POINT = 1,  /* Поштучно   */
  RESMESUNIT_MONEY = 2;  /* Стоимость  */

const
  TYPEDOC_CARRYOFF = 1,  /* Отложен (не подтвержден) */
  TYPEDOC_CARRYON  = 2;  /* Проведен (подтвержден) */

const
  CASHOP_REINFORCE = 1,  /* Подкрепление кассира */
  CASHOP_RETURN    = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT   = 3,  /* Инкассация */
  CASHOP_ADVANCE   = 4,  /* Аванс */
  CASHOP_DEBIT     = 5,  /* Расход */
  CASHOP_CREDIT    = 6,  /* Приход */
  CASHOP_REMOVE    = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1  = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE   = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1 = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE   = 11,  /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1 = 12,  /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_PANT_RI   = 20, /* Подкрепление филиала */
  CASHOP_PANT_RET  = 21, /* Возврат из филиала */
  CASHOP_PANT_RIO  = 22, /* Подкрепление оперчасти */
  CASHOP_PANT_RETO = 23, /* Возврат из оперчасти */
  CASHOP_PANT_COL  = 24, /* Инкассация */
  CASHOP_PANT_ADV  = 25, /* Аванс */
  CASHOP_PANT_REMOVE   = 26,  /* Внутреннее подкрепление хранилища (Расход) */
  CASHOP_PANT_REMOVE_1 = 27,  /* Внутреннее подкрепление хранилища (Приход) */
  CASHOP_PANT_RDEB = 30, /* Расход по кассе пересчета */
  CASHOP_PANT_RCRD = 31, /* Приход по кассе пересчета */
  CASHOP_PANT_RDRT = 32, /* Сдача ресурса в кладовую из Кассы пересчета */
  CASHOP_PANT_ECRD = 41, /* Приход по вечерней кассе */
  CASHOP_PANT_EDRT = 42; /* Сдача ресурса из вечерней кассы */

const
  CI_OGSZ=1,
  CI_LOTTERY=2,
  CI_CERT=3,
  CI_COIN=4,
  CI_INGOT=5,
  CI_MISC=10;


/* Добавлено С.Лучко 10.10.2002 tag04 ----------*/
const
  SB_DEPOSIT     =    1,     /* Документ вкладной операции */
  SB_PSDEPOS     =    5,     /* Отложенный документ */
  SB_COMPAY      =   20,     /* Коммунальные платежи */
  SB_SVDOC       =   50,     /* Сводный документ кредитного бухгалтера */
  SB_INCASH      =  100,     /* Документ порожден Кассой */
  SB_EXTCASH     =  150,     /* Внешняя кассовая операция, не отр. в др. подсист. */
  SB_RDESK       =  180,     /* Касса пересчета */
  SB_PANTRY      =  190,     /* Кладовая отделения */
  SB_CASHOPERT   =  200,     /* Документ для сервисно-кассовых операций */
  SB_EXCHANGE    =  250,     /* Реестр по обменному пункту */
  SB_CAPISSUE    =  300,     /* Ценные бумаги */
  SB_TRN         =  400,     /* Безналичные платежи */
  SB_NOTBALANCE  = 1000;     /* Небалансовая ценность */

/* Добавлено С.Лучко 10.10.2002 end tag04 ----------*/

const
  B_RDESK         = 0,
  B_EDESK         = 1;

const
  VALFORM_27      = 6;

var
  {DeskOpen},
  {EDeskOpen},
  {RprtDSubj},
  {curdate},
  Safe,
  OperBrigade = 0,
  Branch=NumFNCash,
  CurCBRate, 
  Chapter;

macro GetCBRate( dd )

  var 
    CBRate = 0;

  GetAllCurrRate( dd, Currency.Code_Currency, CBRate, null, null);
  if ( ( CBRate == 0 ) or ( CBRate == null ) )
    MsgBox( "Не определен курс валюты '" + Currency.Name_Currency +"'" );
    Exit( 1 );   
  end;
  return CBRate; 
end;

macro IsKept

  record tmpCashVal( "sb_casvl.dbt" );
  copy( tmpCashVal, CashVal.rec );

  if ( not FindCVSettings( tmpCashVal ) )
    return true;
  end;
  return CVSettings.KeepRests != StrFor( 0 );
end;

macro IsShown

  record tmpCashVal( "sb_casvl.dbt" );
  copy( tmpCashVal, CashVal.rec );

  if ( not FindCVSettings( tmpCashVal ) )
    return true;
  end;
  return CVSettings.Form24 != StrFor( 0 );
end;

macro FindCIMisc( Type, IssueID )

  CIMisc.Type = Type;
  CIMisc.IssueID = IssueID;
  return GetEQ( CIMisc );
end;

macro FindCICode( AutoKey )

  var
    Stat,
    OldKey = KeyNum( CICode, 0 );

  CICode.AutoKey = AutoKey;
  Stat = GetEQ( CICode );
  KeyNum( CICode, OldKey );
  return Stat;
end;

macro FindCashVal( ValueRef )

  var
    Stat,
    PrevKey = CashVal.KeyNum;
    CashVal.KeyNum = 0;

  CashVal.rec.RefValue = ValueRef;
  Stat = CashVal.GetEQ;
  CashVal.KeyNum = PrevKey;
  return Stat;
end;

macro FindCurr( CurrCode )

  var
    Stat;

  Currency.Code_Currency = CurrCode;
  Stat = GetEQ( Currency );
  if ( Stat )
    CurCBRate = GetCBRate( {curdate} );
  end; 
end;

macro FindNativeCurr

  var
    Stat = true;

  CashVal.GetPos;
  if ( CashVal.rec.GroupValue )
    Stat = FindCashVal( CashVal.rec.GroupValue );
  end;
  if ( Stat )
    if ( CashVal.rec.TypeValue == CV_Currency )
      Stat = FindCurr(CashVal.rec.CodIntValue );
    else
      Stat = false;
    end;
  end;
  CashVal.GetDirect;
  return Stat;
end;

macro ConvertSum( Sum )

  if ( CurCBRate == null )
    return $0;
  end;

  return Money( Sum * CurCBRate );
end;

macro GetDiaryNo();

  file DepParm  ("depparm.dbt")  key 0;

  DepParm.FNCash = Branch;
  DepParm.FlagCur = 0;
  if ( not ( GetGE( DepParm )
    and ( DepParm.FNCash == Branch )
    and ( DepParm.FlagCur == 0 ) ) )
    MsgBox("Ошибка чтения параметров филиала");
    return 0;
  else
    return DepParm.NumOperDy;
  end;
end;

macro WriteNBData(Brigade, PrevRest,CurRest,Cred,OtherCred,Deb,OtherDeb)

  var
    Stat=true;

  NBData.FNCash=Branch;
  NBData.IsCur=CashVal.rec.RefValueReport;
  NBData.Date={curdate};
  NBData.TypeValue=CashVal.rec.TypeValue;
  NBData.CodIntValue=CashVal.rec.CodIntValue;
  NBData.Brigade=Brigade;
  if(GetEQ(NBData))
    Stat=Delete(NBData);
  end;
  if(Stat and ((PrevRest!=0) or (CurRest!=0) or (Cred!=0) or (OtherCred!=0) or
    (Deb!=0) or (OtherDeb!=0)))
    NBData.RefValue=CashVal.rec.RefValue;
    NBData.Brigade=Brigade;
    NBData.KredCol=Money(OtherCred);
    NBData.DebCol=Money(OtherDeb);
    NBData.IncasCol=Money(Deb);
    NBData.AdvCol=Money(Cred);
    NBData.CurRest=Money(CurRest);
    NBData.PrevRest=Money(PrevRest);
    Insert(NBData);
  end;
end;

macro IntTo07Str( n )

  var
    s = String( n );

  while ( StrLen( s ) < 7 )
    s = "0" + s;
  end;
  return s;
end;

macro ToStr( n )

  if ( ( CashVal.rec.TypeValue == CV_ValForm ) and ( CashVal.rec.CodIntValue == VALFORM_27 ) )
    return IntTo07Str( n );
  else
    return String( n );
  end;
end;

macro MakeRangeStr

  var
    Delim;

  if(RegNumList.Series != "")
    Delim = "-";
  else
    Delim = "";
  end;
/*
  return String(RegNumList.Series + Delim + ToStr( RegNumList.Min ):13:r ) + " .. " +
    String(RegNumList.Series + Delim + ToStr( RegNumList.Max ):13:l);
*/
  return String(RegNumToStr(RegNumList.Series,RegNumList.Min) + " .. " + 
		RegNumToStr(RegNumList.Series,RegNumList.Max));
end;

macro CalcTurns(RefValue, Credit, Debet, OtherCredit, OtherDebet)

  var
    RecFound,
    Cred = $0,
    Deb = $0,
    OthCred = $0,
    OthDeb = $0;

  KeyNum( CashDoc, 0 );

  ClearRecord( CashDoc );
  CashDoc.Branch = Branch;
  CashDoc.CashDateDoc = {curdate};
  CashDoc.TypeDoc = TYPEDOC_CARRYON;
  CashDoc.CashOper = Safe;
  CashDoc.RefValue = RefValue;
  RecFound = GetGE( CashDoc );
  while(RecFound
    and ( CashDoc.Branch == Branch )
    and ( CashDoc.CashDateDoc == {curdate} )
    and ( CashDoc.TypeDoc == TYPEDOC_CARRYON )
    and ( CashDoc.CashOper == Safe )
    and ( CashDoc.RefValue == RefValue ) )
    if ( CashDoc.Brigade == OperBrigade )
      if ( ( CashDoc.NumOper != CASHOP_PANT_RDRT )
        and ( CashDoc.NumOper != CASHOP_PANT_EDRT ) )
        if(CashVal.rec.UnitMeas==RESMESUNIT_POINT)
          if(CashDoc.FlagDebCredOper == FLAGDEBCRED_IN)
            Cred = Cred + Money(CashDoc.ValueCol );
          else
            Deb = Deb + Money(CashDoc.ValueCol );
          end;
        else
          if(CashDoc.FlagDebCredOper == FLAGDEBCRED_IN)
            Cred = Cred + CashDoc.ValueMoney;
          else
            Deb = Deb + CashDoc.ValueMoney;
          end;
        end;
      elif ( ( CashDoc.NumOper != CASHOP_PANT_REMOVE )
        and ( CashDoc.NumOper != CASHOP_PANT_REMOVE_1 ) )
        if(CashVal.rec.UnitMeas==RESMESUNIT_POINT)
          if(CashDoc.FlagDebCredOper == FLAGDEBCRED_IN)
            OthCred = OthCred + Money(CashDoc.ValueCol );
          else
            OthDeb = OthDeb + Money(CashDoc.ValueCol );
          end;
        else
          if(CashDoc.FlagDebCredOper == FLAGDEBCRED_IN)
            OthCred = OthCred + CashDoc.ValueMoney;
          else
            OthDeb = OthDeb + CashDoc.ValueMoney;
          end;
        end;
      end;
    end;
    RecFound = Next(CashDoc);
  end;

  KeyNum( CashDoc, 5 );

  ClearRecord( CashDoc );
  CashDoc.Branch = Branch;
  CashDoc.CashDateDoc = {curdate};
  CashDoc.TypeDoc = TYPEDOC_CARRYON;
  CashDoc.CashPatern = Safe;
  CashDoc.RefValue = RefValue;
  RecFound = GetGE( CashDoc );
  while(RecFound
    and ( CashDoc.Branch == Branch )
    and ( CashDoc.CashDateDoc == {curdate} )
    and ( CashDoc.TypeDoc == TYPEDOC_CARRYON )
    and ( CashDoc.CashPatern == Safe ) )
/*    and ( CashDoc.RefValue == RefValue ) ) */
    if ( (CashDoc.Brigade == OperBrigade) and (CashDoc.RefValue == RefValue) )
      if ( ( CashDoc.NumOper != CASHOP_PANT_RDRT )
        and ( CashDoc.NumOper != CASHOP_PANT_EDRT )
        and ( CashDoc.NumOper != CASHOP_PANT_EDRT )
        and ( CashDoc.NumOper != CASHOP_PANT_REMOVE )
        and ( CashDoc.NumOper != CASHOP_PANT_REMOVE_1 ) )
        if(CashVal.rec.UnitMeas==RESMESUNIT_POINT)
          if(CashDoc.FlagDebCredOper == FLAGDEBCRED_OUT)
            Cred = Cred + Money(CashDoc.ValueCol );
          else
            Deb = Deb + Money(CashDoc.ValueCol );
          end;
        else
          if(CashDoc.FlagDebCredOper == FLAGDEBCRED_OUT)
            Cred = Cred + CashDoc.ValueMoney;
          else
            Deb = Deb + CashDoc.ValueMoney;
          end;
        end;
      else
        if(CashVal.rec.UnitMeas==RESMESUNIT_POINT)
          if(CashDoc.FlagDebCredOper == FLAGDEBCRED_OUT)
            OthCred = OthCred + Money(CashDoc.ValueCol );
          else
            OthDeb = OthDeb + Money(CashDoc.ValueCol );
          end;
        else
          if(CashDoc.FlagDebCredOper == FLAGDEBCRED_OUT)
            OthCred = OthCred + CashDoc.ValueMoney;
          else
            OthDeb = OthDeb + CashDoc.ValueMoney;
          end;
        end;
      end;
    end;
    RecFound = Next(CashDoc);
  end;

  SetParm(1, Cred);
  SetParm(2, Deb);
  SetParm(3, OthCred);
  SetParm(4, OthDeb);
end;

macro GetIngRate( Type, Issue, Dd )

  ClearRecord( IngRate );
  IngRate.Type = Type;
  IngRate.Issue = Issue;
  IngRate.Date = Dd + 1;
  if ( GetLT( IngRate ) 
    and ( IngRate.Type == Type )
    and ( IngRate.Issue == Issue ) )
    return IngRate.CBRate;                                          
  else
    return 0;
  end;
end;

macro Quantity2Rub( Quantity, Rate )
  
  return Money( ( Quantity * CIMisc.Weight / 1000000 ) * IngRate.CBRate );
end;

macro Quantity2Weight( Quantity )
  
  return Quantity * CIMisc.Weight / 1000000;
end;

macro GetBranchName

  var
    RetVal;

  if(not findDepartment(Branch, RetVal))
    RetVal=String(Branch);
  end;
  return RetVal;
end;

