
/*
**  Отчетная справка о кассовых оборотах
**
*/

import DeskInter, CommonInter, Cur_Rate;

file CashDoc( "sb_casdc.dbt" ) key 0;
file CashVal( "sb_casvl.dbt" ) key 0;
file Curr( "currency.dbt" ) key 0;
file Person( "person.dbt","bank.def" ) key 0;
file Group( "rtgroup.dbt" ) key 2;

const
  FLAGDEBCRED_IN   = 1,
  TYPERES_CURRENCY = 1, 
  TYPERES_VALFORM = 2, 
  TYPERES_PAYDOC = 4,

  F0406007 = 6,
  
  TYPE_RES_PAY = 0, 

  CASHOP_REINFORCE = 1,  /* Подкрепление кассира */
  CASHOP_RETURN    = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT   = 3,  /* Инкассация */
  CASHOP_ADVANCE   = 4,  /* Аванс */
  CASHOP_DEBIT     = 5,  /* Расход */
  CASHOP_CREDIT    = 6,  /* Приход */
  CASHOP_REMOVE    = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1  = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE   = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1 = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE   = 11,  /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1 = 12,  /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_DEBET_SAFE = 13,  /* Расход из сейфа */
  CASHOP_CREDIT_SAFE = 14, /* Приход в сейф */

  SB_EXCHNDOC    =  260, 
 
  TYPEDOC_CARRYON = 2;

array 
  CurrCodes, Deb, Cred, IODeb, IOCred, RemCred, RemDeb;

var
  {curdate}, 
  {oper}, 
  Branch = NumFNCash, 
  Brigade = GetOperBrigade, 
  IsCur = NumFlagCur;

var
  CBRate, 
  ScaleRate;

var
  CredOrdCount = 0, DebOrdCount = 0;

macro GetBranchName

  var dep_name;
  if ( not findDepartment(Branch, dep_name) )
    dep_name = "<не найден>";
  end;
  return dep_name;
end;

macro FindBrigade

  Group.Branch = Branch;
  Group.ID = Brigade;
  Group.Date = Date( 0, 0, 0 );
  return GetEQ( Group );
end;

macro FindPerson

  ClearRecord( Person );
  Person.Oper = {oper};
  if ( not GetEQ( Person ) )
    Person.Name = "<не найден>";
  end;
end;

macro FindCurrByISO( ISO )
 
  var
    Stat;

  KeyNum( Curr, 1 );
  Curr.ExternalCode = ISO;
  Stat = GetEQ( Curr );
  KeyNum( Curr, 0 );
  return Stat;  

end;

macro PrintReport

  var
    i;

  [ АКБ "Пробизнесбанк"                                                        # ___________________________________________________ ]
  ( Person.Name );
  [ Операционная касса                                                                                                               ];
  [ вне кассового узла                                                                                                               ];
  [ #                                                                                                                                ]
  ( GetBranchName );

  [ ];
  [ ];

  [                                         Отчетная справка по оборотам операционной кассы                                          ];
  [ ################################################################################################################################ ]
  ( ( "за " + String( {curdate}:m ) ):c );
  [ ];
  [----------------------------------------------------------------------------------------------------------------------------------];
  [|                  | Код  |Подкрепление из|Принято ценностей|      Обороты за день         |Передано ценностей|      Сдано в     |];
  [|                  |валюты|   хранилища   |  от кассового   |------------------------------|     кассовому    |     хранилище    |];
  [|                  |      |               |   работника     |     Приход    |    Расход    |     работнику    |                  |];
  [|------------------|------|---------------|-----------------|---------------|--------------|------------------|------------------|];


  if ( ASize( CurrCodes ) > 0 )
    i = 0;
    while ( i < ASize( CurrCodes ) )
      FindCurrByISO( CurrCodes( i ) );
      [ ################## ###### ############### ################# ############### ############## ################## ################# ]
      (
        Curr.Name_Currency, 
        CurrCodes( i ),
        IOCred( i ):z, 
        RemCred( i ):z, 
        Cred( i ):z, 
        Deb( i ):z, 
        RemDeb( i ):z, 
        IODeb( i ):z
      );
      i = i + 1;
    end;
  end;

  [----------------------------------------------------------------------------------------------------------------------------------];
  [ ];
  [ ];
  [ Кассовые документы по приходу в количестве #______  по расходу в количестве #______ ]
  ( CredOrdCount:z, DebOrdCount:z );
  [ ];
  [ Зав. кассой _________________                       Кассир _________________   ];

  Print( StrFor( 12 ) );
end;

macro FindCashVal( Ref )

  CashVal.RefValue = Ref;
  if ( not GetEQ( CashVal ) )
    MsgBox( "Не найден ресурс #" + Ref );
    Exit( 1 );
  end;
end;

macro FindCurr( Code )

  Curr.Code_Currency = Code;
  if ( not GetEQ( Curr ) )
    Curr.Name_Currency = "???";
  end;
end;

macro Add( KeyArr, ValArr, Key, Val )

  var
    i = 0;

  while ( ( i < ASize( KeyArr ) ) and ( KeyArr( i ) != Key ) )
    i = i + 1;
  end; 
        
  if ( i == ASize( KeyArr ) )
    KeyArr( i ) = Key; 
  end;

  if ( ValArr( i ) == null )
    ValArr( i ) = Val;
  else
    ValArr( i ) = ValArr( i ) + Val;
  end;
end;

macro IsDebet

  if ( ( CashDoc.NumOper == CASHOP_DEBIT ) 
    or ( CashDoc.NumOper == CASHOP_DEBET_SAFE ) 
    or ( CashDoc.NumOper == CASHOP_COLLECT ) 
    or ( CashDoc.NumOper == CASHOP_REMOVE_SAFE ) )
    return true;
  end;
  return false;
end;

macro IsIO

  if ( ( CashDoc.NumOper == CASHOP_ADVANCE ) 
    or ( CashDoc.NumOper == CASHOP_COLLECT ) )
    return true;
  end;
  return false;
end;

macro IsRem

  if ( ( CashDoc.NumOper == CASHOP_REMOVE_SAFE ) 
    or ( CashDoc.NumOper == CASHOP_REMOVE_SAFE_1 ) )
    return true;
  end;
  return false;
end;

macro UpdateOrderCounters

  if ( CashDoc.ApplicationKind != SB_EXCHNDOC )
    if ( IsDebet )
      DebOrdCount = DebOrdCount + 1;   
    else
      CredOrdCount = CredOrdCount + 1;
    end; 
  end;
end;

macro ProcessDoc

  var
    Sum = CashDoc.ValueMoney;

  if ( CashDoc.Brigade != Brigade )
    return;
  end;

  if (  ( CashDoc.NumOper != CASHOP_CREDIT ) 
    and ( CashDoc.NumOper != CASHOP_DEBIT ) 
    and ( CashDoc.NumOper != CASHOP_CREDIT_SAFE )
    and ( CashDoc.NumOper != CASHOP_DEBET_SAFE ) 
    and ( CashDoc.NumOper != CASHOP_ADVANCE ) 
    and ( CashDoc.NumOper != CASHOP_COLLECT ) 
    and ( CashDoc.NumOper != CASHOP_REMOVE_SAFE ) 
    and ( CashDoc.NumOper != CASHOP_REMOVE_SAFE_1 ) ) 
    return;
  end;

  FindCashVal( CashDoc.RefValue );
  FindCurr( CashVal.CodIntValue );

  if ( IsCur and ( Curr.Code_Currency == 0 ) )
    return;
  elif ( ( not IsCur ) and ( Curr.Code_Currency != 0 ) )
    return;
  end;

  if ( CashVal.TypeValue == TYPERES_CURRENCY )
    if ( IsDebet )
      if ( IsIO )
        Add( CurrCodes, IODeb, Curr.ExternalCode, Sum );
      elif ( IsRem )
        Add( CurrCodes, RemDeb, Curr.ExternalCode, Sum );
      else
        Add( CurrCodes, Deb, Curr.ExternalCode, Sum );
      end;
    else
      if ( IsIO )
        Add( CurrCodes, IOCred, Curr.ExternalCode, Sum );
      elif ( IsRem )
        Add( CurrCodes, RemCred, Curr.ExternalCode, Sum );
      else
        Add( CurrCodes, Cred, Curr.ExternalCode, Sum );
      end;
    end;
    UpdateOrderCounters;
  end;
end;  

macro ProcessDocs

  var
    RecFound;

  ClearRecord( CashDoc );
  CashDoc.Branch = Branch;
  CashDoc.CashDateDoc = {curdate};
  CashDoc.TypeDoc = TYPEDOC_CARRYON;   
  RecFound = GetGE( CashDoc ); 
  while ( RecFound
    and ( CashDoc.Branch == Branch )
    and ( CashDoc.CashDateDoc == {curdate} )
    and ( CashDoc.TypeDoc == TYPEDOC_CARRYON ) )
    ProcessDoc;
    RecFound = Next( CashDoc );
  end;
end;

  ProcessDocs;
  PrintReport;
end;

