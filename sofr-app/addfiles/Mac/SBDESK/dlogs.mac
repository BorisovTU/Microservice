
/*
**  Кассовые журналы по приходу и по расходу
**
**
*/

import DeskInter, CommonInter, Cur_Rate, RSD, office;

private file person ( person ) key 0;

private var {BatchMode}, {oper}, {curdate}, {OperDprt}, {Name_Bank};
private var Branch = NumFNCash;
private var Brigade = GetOperBrigade;

private var PrevForDebit = true;
private var PrevAccount = "";
private var PrevCodeCurr = -1;
private var firstPage = true;
private var sumForAccount = $0L;
private var headForDebit = true;
private var numShift = GetOperBrigade( );
private array aSymbol;
private array aSymbolSum;
private array aOper;
private array aOperSum;



/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro CanIssueDeskLog

  var stat = true;
  var cmd;
  var rs;
  var prsn = TBFile( "person.dbt", "bank.def", "r", 0 );

  prsn.Clear;
  prsn.rec.Oper = {oper};

  if ( prsn.GetEQ( ) )
    if ( prsn.rec.cTypePerson == "Б" ) // TP_BOOK
      return stat;
    end;
  end;

  cmd = RsdCommand( "select rtgroup.t_type as type " +
                    "from drtgroup_dbt rtgroup " +
                    "where rtgroup.t_branch = ? and " +
                          "rtgroup.t_id = ? " +
                    "order by rtgroup.t_date" );

  cmd.addParam( "branch", RSDBP_IN, Branch );
  cmd.addParam( "id", RSDBP_IN, Brigade );

  cmd.execute;
  
  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    if ( rs.value( "type" ) == 2 ) // GT_GZ Группа зачисления
      if ( not {BatchMode} )
        MsgBox( "Выпуск отчета для группы зачисления не выполняется" );
      else
        println( "Выпуск отчета для группы зачисления не выполняется" );
      end;
      stat = false;
    end;
  end;

  return stat;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro FindPerson( )

  ClearRecord( person );
  person.Oper = {oper};

  if ( not GetEQ( person ) )
    ClearRecord( person );
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro FindCashier( pOper )

  var FIOCashier = "--- Не определен ---";

  if ( pOper )
    ClearRecord( person );
    person.Oper = pOper;

    if ( GetEQ( person ) )
      FIOCashier = person.Name;
    end;
  end;

  return FIOCashier;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro GetBranchName( )

  var nameBranch = "";

  if ( not findDepartment( Branch, nameBranch ) )
    nameBranch = String( Branch );
  end;

  return nameBranch;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro defCurrencyName( codeCurr )

  var currency = TBFile( "currency.dbt", "r", 0 );

  currency.Clear( );
  currency.rec.Code_Currency = codeCurr;

  if ( currency.GetEQ( ) )
    return currency.rec.Short_Name;
  else
    return "";
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro calcCountRateChanges

  var cmd;
  var rs;
  var countChanges = 0;

  /* Считаем кол-во распоряжений на заданную дату */
  cmd = RsdCommand( "select count( * ) nn from dratechng_dbt rc "
                    "where rc.t_startdate = :date1 and rc.t_branch = :branch" );

  cmd.addParam( "date1", RSDBP_IN );
  cmd.addParam( "branch", RSDBP_IN );

  cmd.value( "date1" ) = {curdate};
  cmd.value( "branch" ) = Office.BranchForRate;

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    countChanges = Int( rs.value( "nn" ) );
  end;
  
  /* Находим самое раннее распоряжение за указанную дату */
  cmd = RsdCommand( "SELECT t_starttime FROM ( "
                        "SELECT rc.t_starttime FROM dratechng_dbt rc "
                               "WHERE rc.t_startdate = :date1 AND rc.t_branch = :branch "
                            "ORDER BY t_starttime ) "
                     "WHERE ROWNUM = 1");

  cmd.addParam( "date1", RSDBP_IN );
  cmd.addParam( "branch", RSDBP_IN );

  cmd.value( "date1" ) = {curdate};
  cmd.value( "branch" ) = Office.BranchForRate;

  cmd.execute;

  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    /* Если распоряжение действует не с начала суток, то ищем предыдущее распоряжение. Нашли? countChanges + 1 */
    if( Time( rs.value( "t_starttime" ) ) != Time(0,0,0) )
        cmd = RsdCommand( "select t_id from dratechng_dbt rc "
                    "where rc.t_startdate < :date1 and rc.t_branch = :branch" );

        cmd.addParam( "date1", RSDBP_IN );
        cmd.addParam( "branch", RSDBP_IN );

        cmd.value( "date1" ) = {curdate};
        cmd.value( "branch" ) = Office.BranchForRate;

        cmd.execute;

        rs = RsdRecordset( cmd );

        if ( rs.moveNext )
          countChanges = countChanges + 1;
        end;
    end;
  end;
  
  return countChanges;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro defRetOpState( applKind, applKey )

  var cmd;
  var rs;
  var retOpState = 0;

  cmd = RsdCommand( "select ret_op.* from dret_op_dbt ret_op " +
                         "where ret_op.t_applicationkind = ? and " +
                               "ret_op.t_applicationkey = ?" );

  cmd.addParam( "", RSDBP_IN, applKind );
  cmd.addParam( "", RSDBP_IN, applKey );

  cmd.execute;
  
  rs = RsdRecordset( cmd );

  if ( rs.moveNext )
    retOpState = rs.value( "t_state" );
  end;

  return retOpState;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintHead( forDebit, rs )

  var debitCurrCode;
  var debitAccount;
  var creditCurrCode;
  var creditAccount;
  var account;
  var currCode;
  var nameBankInHead;
  var cbRate = 1.0;
  var buyRate = 1.0;
  var sellRate = 1.0;
  var nameCur = "";
  var nameNationalCur = defCurrencyName( 0 );

  // Обнуление итоговых сумм
  sumForAccount = $0L;
  ASize( aSymbol, 0 );
  ASize( aSymbolSum, 0 );
  ASize( aOper, 0 );
  ASize( aOperSum, 0 );

  headForDebit = forDebit;
          
  debitCurrCode = rs.value( "t_DebitCurrCode" );
  debitAccount = rs.value( "t_DebitAccount" );                     
  creditCurrCode = rs.value( "t_CreditCurrCode" );
  creditAccount = rs.value( "t_CreditAccount" );

  if ( forDebit )
    account = debitAccount;
    currCode = debitCurrCode;
  else
    account = creditAccount;
    currCode = creditCurrCode;
  end;

  nameBankInHead = {Name_Bank} + ", подразделение № " + GetBranchName( );

  if ( firstPage )
    firstPage = false;
  else
    println( StrFor(12) );
  end;

  [ ];
  [                                                                                             +---------+   ];
  if ( forDebit )
    [                                                                                             | 0401704 |   ];
  else
    [                                                                                             | 0401705 |   ];
  end;
  [                                                                                             +---------+   ];
  [       #]( nameBankInHead );
  [       ------------------------------------];
  [        Составитель];
  [ ];
  if ( forDebit )
    [                                    Кассовый журнал по приходу                                             ];
  else
    [                                    Кассовый журнал по расходу                                             ];
  end;
  [                                           #                                                               ]
  ( {curdate}:f );
  [                                            Дата                                                           ];
  [ Счет по учету кассы № #                                                                                   ]
  ( account );
  [ ];

  if ( currCode > 0 )
    GetAllCurrRate( {curdate}, currCode, cbRate, buyRate, sellRate );

    nameCur = defCurrencyName( currCode );

    [ Курс ЦБ      #]( Trim( String( cbRate:0:4 ) ) + " " + nameNationalCur + "/" + nameCur );
    if ( calcCountRateChanges( ) <= 1 )
      [ Курс продажи #]( Trim( String( sellRate:0:4 ) ) + " " + nameNationalCur + "/" + nameCur );
      [ Курс покупки #]( Trim( String( buyRate:0:4 ) ) + " " + nameNationalCur + "/" + nameCur );
    end;
    [ ];
  end;

  [+------------------------------------------------------------------------------------------------------------+];
  [|     №     |        № № счетов        |  Шифр   |       Сумма         | Символ |        Примечание          |];
  [| документа |                          |документа|                     |        |                            |];
  [|-----------|--------------------------|---------|---------------------|--------|----------------------------|];
  [|     1     |            2             |    3    |          4          |   5    |             6              |];
  [|-----------|--------------------------|---------|---------------------|--------|----------------------------|];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintTail( pTotInСashier, pTotInCashSymb )

  var i;
  var j;
  var tmpVal;
  var subName;
   
  // Итоги по кассовым символам
  if ( pTotInCashSymb )
    // Отсортируем символы в порядке возрастания
    if ( ASize( aSymbol ) > 0 )
      i = 0;
      while ( i < ASize( aSymbol ) )
        j = i;
        while ( j < ASize( aSymbol ) )
          if ( aSymbol( j ) < aSymbol( i ) )
            tmpVal = aSymbol( i );
            aSymbol( i ) = aSymbol( j );
            aSymbol( j ) = tmpVal;
            tmpVal = aSymbolSum( i );
            aSymbolSum( i ) = aSymbolSum( j );
            aSymbolSum( j ) = tmpVal;
          end;
          j = j + 1;
        end;
        i = i + 1;
      end;
    end;

    i = 0;
    while ( i < ASize( aSymbol ) )
      subName = "Итого по КС " + aSymbol( i ) + ":";

      [|------------------------------------------------|---------------------|--------|----------------------------|];
      [| #                                              | ################### |        |                            |]
      ( subName, String( aSymbolSum( i ):a ):r );
      i = i + 1;
    end;
  end;

  // Итоги по кассовым работникам
  if ( pTotInСashier )
    // Отсортируем символы в порядке возрастания
    if ( ASize( aOper ) > 0 )
      i = 0;
      while ( i < ASize( aOper ) )
        j = i;
        while ( j < ASize( aOper ) )
          if ( aOper( j ) < aOper( i ) )
            tmpVal = aOper( i );
            aOper( i ) = aOper( j );
            aOper( j ) = tmpVal;
            tmpVal = aOperSum( i );
            aOperSum( i ) = aOperSum( j );
            aOperSum( j ) = tmpVal;
          end;
          j = j + 1;
        end;
        i = i + 1;
      end;
    end;

    i = 0;
    while ( i < ASize( aOper ) )
      subName = "Итого по кассовому работнику " + FindCashier( aOper( i ) ) + ":";

      [|------------------------------------------------|---------------------|--------|----------------------------|];
      [| ############################################## | ################### |        |                            |]
      ( subName:w, String( aOperSum( i ):a ):r );
      i = i + 1;
    end;
  end;

  [|------------------------------------------------|---------------------|--------|----------------------------|];                      
  if ( headForDebit )
    [| Итого по приходу:                              | ################### |        |                            |]
    ( String( sumForAccount:a ):r );
  else
    [| Итого по расходу:                              | ################### |        |                            |]
    ( String( sumForAccount:a ):r );
  end;
  [+------------------------------------------------------------------------------------------------------------+];

  FindPerson( );

  [ ];
  [ ];
  [      Главный бухгалтер ];
  [      (бухгалтерский работник) ____________________________  __________________________________ ];
  [ ];
  [      Заведующий кассой ];
  [      (кассовый работник)      ____________________________  # ]( person.Name );
  [ ];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintLine( forDebit, rs, pTotInСashier, pTotInCashSymb )

  var cmdAcc;
  var rsAcc;
  var documentID;
  var isConsolidated;
  var foldSide;
  var documentNumber;
  var debitAmount;
  var debitCurrCode;
  var debitAccount;
  var creditAmount;
  var creditCurrCode;
  var creditAccount;
  var account;
  var currCode;
  var account2;
  var operationCipher;
  var amount;
  var cashSymbol;
  var equivalentAmount;
  var rubEquivSum = $0L;
  var opApplicationKind;
  var opApplicationKey;
  var ground;
  var retOpState = 0;
  var groundReport = "";
  var wasDocAccount = false;
  var nameNationalCur = defCurrencyName( 0 );
  var cashier = 0;
  var i = 0;
  var j = 0;

  documentID = rs.value( "t_DocumentID" );
  documentNumber = rs.value( "t_documentNumber" );
  operationCipher = rs.value( "t_OperationCipher" );
  isConsolidated = rs.value( "t_isConsolidated" );
  foldSide = rs.value( "t_FoldSide" );
  debitAmount = rs.value( "t_DebitAmount" );
  debitCurrCode = rs.value( "t_DebitCurrCode" );
  debitAccount = rs.value( "t_DebitAccount" );
  creditAmount = rs.value( "t_CreditAmount" );
  creditCurrCode = rs.value( "t_CreditCurrCode" );
  creditAccount = rs.value( "t_CreditAccount" );
  equivalentAmount = rs.value( "t_NatCurrEquivalent" );
  opApplicationKind = rs.value( "t_OpApplicationKind" );
  opApplicationKey = rs.value( "t_OpApplicationKey" );
  ground = rs.value( "t_Ground" );
  cashier = rs.value( "t_Cashier" );

  if ( ValType( cashier ) != V_INTEGER )
    cashier = 0;
  end;

  if ( forDebit )
    account = debitAccount;
    currCode = debitCurrCode;
    account2 = creditAccount;
    amount = creditAmount;
    cashSymbol = rs.value( "t_CreditCashSymbol" );
  else
    account = creditAccount;
    currCode = creditCurrCode;
    account2 = debitAccount;
    amount = debitAmount;
    cashSymbol = rs.value( "t_DebitCashSymbol" );
  end;

  if ( debitCurrCode == 0 )
    rubEquivSum = debitAmount;
  elif ( creditCurrCode == 0 )
    rubEquivSum = creditAmount;
  else
    rubEquivSum = equivalentAmount;
  end;

  if ( cashSymbol == StrFor( 1 ) )
    cashSymbol = "";
  end;
  if ( account == StrFor( 1 ) )
    account = "";
  end;
  if ( account2 == StrFor( 1 ) )
    account2 = "";
  end;
            
  if ( ( PrevForDebit != forDebit ) or
       ( PrevAccount  != account  ) or
       ( PrevCodeCurr != currCode )   )

    if ( PrevCodeCurr != -1 )
      PrintTail( pTotInСashier, pTotInCashSymb );
    end;

    PrintHead( forDebit, rs );

    PrevForDebit = forDebit;
    PrevAccount  = account;
    PrevCodeCurr = currCode;
  end;

  // Может быть единичный документ раскрытый в несколько строк
  if ( ( isConsolidated == "" ) and ( foldSide > 0 ) )
    cmdAcc = RsdCommand( "select rtdocaccount.* from drtdocaccount_dbt rtdocaccount " +
                         "where rtdocaccount.t_DocumentID = ? and " +
                               "rtdocaccount.t_Account not like '202%' and " +
                               "rtdocaccount.t_Account not like '203%'" );

    cmdAcc.addParam( "", RSDBP_IN, documentID );

    cmdAcc.execute;
  
    rsAcc = RsdRecordset( cmdAcc );

    while ( rsAcc.moveNext )
      wasDocAccount = true;

      equivalentAmount = rsAcc.value( "t_NatCurrEquivalent" );
      amount = rsAcc.value( "t_Amount" );
      account2 = rsAcc.value( "t_Account" );

      if ( rsAcc.value( "t_CurrCode" ) == 0 )
        rubEquivSum = amount;
      else
        rubEquivSum = equivalentAmount;
      end;

      if ( account2 == StrFor( 1 ) )
        account2 = "";
      end;

      if ( currCode > 0 )
        groundReport = String( rubEquivSum:0:2) + " " + nameNationalCur;
      end;
      groundReport = Trim( groundReport + " " + ground );

      if ( isConsolidated == "" )
        retOpState = defRetOpState( opApplicationKind, opApplicationKey );

        if ( retOpState == 51 )
          groundReport = Trim( groundReport + " Сторнировано" );
        end;

        if ( ( retOpState == 50 ) or ( retOpState == 51 ) )
          ;
        else
          groundReport = Trim( groundReport + " Не подтвержден" );
        end;
      end;

      if ( Index( account2, "20308" ) == 1 )
        groundReport = Trim( groundReport + " Без отражения в учете" );
      end;

      [| ######### | ######################## | ####### | ################### | ###### | ########################## |]
      ( documentNumber:z, account2, operationCipher, amount:18:2:a, cashSymbol, groundReport:w );
    end;
  end;

  if ( not wasDocAccount )
    if ( currCode > 0 )
      groundReport = String( rubEquivSum:0:2) + " " + nameNationalCur;
    end;
    groundReport = Trim( groundReport + " " + ground );

    if ( isConsolidated == "" )
      retOpState = defRetOpState( opApplicationKind, opApplicationKey );

      if ( retOpState == 51 )
        groundReport = Trim( groundReport + " Сторнировано" );
      end;

      if ( ( retOpState == 50 ) or ( retOpState == 51 ) )
        ;
      else
        groundReport = Trim( groundReport + " Не подтвержден" );
      end;
    end;

    if ( Index( account2, "20308" ) == 1 )
      groundReport = Trim( groundReport + " Без отражения в учете" );
    end;

    [| ######### | ######################## | ####### | ################### | ###### | ########################## |]
    ( documentNumber:z, account2, operationCipher, amount:18:2:a, cashSymbol, groundReport:w );
  end;

  // Подсчет итоговых сумм
  sumForAccount = sumForAccount + amount;

  // Итоги по кассовым символам
  if ( pTotInCashSymb )
    j = -1;
    if ( ASize( aSymbol ) > 0 )
      i = 0;
      while ( i < ASize( aSymbol ) )
        if ( aSymbol( i ) == cashSymbol )
          j = i;
          break;
        end;
        i = i + 1;
      end;
    end;

    if ( j == -1 )
      j = ASize( aSymbol );
      aSymbol( j ) = cashSymbol;
      aSymbolSum( j ) = amount;
    else
      aSymbolSum( j ) = aSymbolSum( j ) + amount;
    end;
  end;

  // Итоги по кассирам
  if ( pTotInСashier )

    j = -1;
    if ( ASize( aOper ) > 0 )
      i = 0;
      while ( i < ASize( aOper ) )
        if ( aOper( i ) == cashier )
          j = i;
          break;
        end;
        i = i + 1;
      end;
    end;

    if ( j == -1 )
      j = ASize( aOper );
      aOper( j ) = cashier;
      aOperSum( j ) = amount;
    else
      aOperSum( j ) = aOperSum( j ) + amount;
    end;
  end;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
private macro PrintReport( pRepMode, pAccMask, pTotInСashier, pTotInCashSymb )

  var cmd;
  var rs;
  var cmdCred;
  var rsCred;
  var cmdStr = "";

  // Кассовые ордера по дебету -------------------------------------------
  if ( pRepMode == 0 )
    cmdStr = "select rtdocument.* from drtdocument_dbt rtdocument, dpmdtype_dbt pmdtype " +
             "where rtdocument.t_Branch = ? and " +
                   "rtdocument.t_Date = ? and " +
                   "rtdocument.t_Chapter <= 1 and " + // !!!!! = 1
                   "( rtdocument.t_DebitAccount like '202%' or " +
                     "rtdocument.t_DebitAccount like '203%' ) and " +
                   "( rtdocument.t_isConsolidated != chr( 0 ) or " +
                     "( rtdocument.t_isConsolidated = chr( 0 ) and " +
                       "nvl( rtdocument.t_ConsDocumentID, 0 ) = 0  ) ) and " +
                   "rtdocument.t_ShiftNumber = ? and " +
                   "( decode( pmdtype.t_SystemAnalogID, 0, pmdtype.t_DocTypeID, pmdtype.t_SystemAnalogID ) <> 9 or " + 
                    "( rtdocument.t_isConsolidated != chr( 0 ) and rtdocument.t_ExchangeOperationCode BETWEEN '01' AND '11' ) or " +
                    "( rtdocument.t_isConsolidated = chr( 0 ) and rtdocument.t_ExchangeOperationCode > '11' ) ) and " +
                   "pmdtype.t_DocTypeID = rtdocument.t_DocTypeID and " +
                   "( pmdtype.t_DocTypeID IN ( 1, 3, 4, 9 ) or " +
                     "pmdtype.t_systemAnalogId IN ( 1, 3, 4, 9 ) ) and " +
                   "exists ( select 1 from dpmdtemplhistory_dbt pmdtemplhistory " +
                            "where pmdtemplhistory.t_HistoryID = rtdocument.t_HistoryID and " +
                                  "pmdtemplhistory.t_numPrintCopies > 0 ) ";
                   // "rtdocument.t_exportState != 2 " + // Выгруженные учитывать не надо

    if ( ValType( pAccMask ) == V_STRING )
      if ( pAccMask != "" )
        cmdStr = cmdStr + "and rtdocument.t_DebitAccount like '" + pAccMask + "%' ";
      end;
    end;

    cmdStr = cmdStr + "order by rtdocument.t_DebitAccount, rtdocument.t_DebitCurrCode, rtdocument.t_isConsolidated, rtdocument.t_DocumentID";

    cmd = RsdCommand( cmdStr );

    cmd.addParam( "", RSDBP_IN, Branch );
    cmd.addParam( "", RSDBP_IN, {curdate} );
    cmd.addParam( "", RSDBP_IN, numShift );
  else // По операционисту
    cmdStr = "select rtdocument.* from drtdocument_dbt rtdocument, dpmdtype_dbt pmdtype " +
             "where rtdocument.t_Branch = ? and " +
                   "rtdocument.t_Date = ? and " +
                   "rtdocument.t_Chapter <= 1 and " + // !!!!! = 1
                   "( rtdocument.t_DebitAccount like '202%' or " +
                     "rtdocument.t_DebitAccount like '203%' ) and " +
                   "rtdocument.t_isConsolidated = chr( 0 ) and " +
                   "rtdocument.t_Cashier = ? and " +
                   "rtdocument.t_ShiftNumber = ? and " +
                   "(decode(pmdtype.t_SystemAnalogID, 0, pmdtype.t_DocTypeID, pmdtype.t_SystemAnalogID) <> 9 or " + 
                    "rtdocument.t_ExchangeOperationCode > '11') and " +
                   "pmdtype.t_DocTypeID = rtdocument.t_DocTypeID and " +
                   "( pmdtype.t_DocTypeID IN (1, 3, 4, 9) or " +
                     "pmdtype.t_systemAnalogId IN (1, 3, 4, 9) ) and " +
                   "exists ( select 1 from dpmdtemplhistory_dbt pmdtemplhistory " +
                            "where pmdtemplhistory.t_HistoryID = rtdocument.t_HistoryID and " +
                                  "pmdtemplhistory.t_numPrintCopies > 0 ) ";
                   // "rtdocument.t_exportState != 2 " + // Выгруженные учитывать не надо

    if ( ValType( pAccMask ) == V_STRING )
      if ( pAccMask != "" )
        cmdStr = cmdStr + "and rtdocument.t_DebitAccount like '" + pAccMask + "%' ";
      end;
    end;

    cmdStr = cmdStr + "order by rtdocument.t_DebitAccount, rtdocument.t_DebitCurrCode, rtdocument.t_isConsolidated, rtdocument.t_DocumentID";

    cmd = RsdCommand( cmdStr );

    cmd.addParam( "", RSDBP_IN, Branch );
    cmd.addParam( "", RSDBP_IN, {curdate} );
    cmd.addParam( "", RSDBP_IN, {oper} );
    cmd.addParam( "", RSDBP_IN, numShift );
  end;

  cmd.execute;
  
  rs = RsdRecordset( cmd );

  while ( rs.moveNext )
    PrintLine( true, rs, pTotInСashier, pTotInCashSymb );
  end;
    
  // Кассовые ордера по кредиту ------------------------------------------
  if ( pRepMode == 0 )
    cmdStr = "select rtdocument.* from drtdocument_dbt rtdocument, dpmdtype_dbt pmdtype " +
             "where rtdocument.t_Branch = ? and " +
                   "rtdocument.t_Date = ? and " +
                   "rtdocument.t_Chapter <= 1 and " + // !!!!! = 1
                   "( rtdocument.t_CreditAccount like '202%' or " +
                     "rtdocument.t_CreditAccount like '203%' ) and " +
                   "( rtdocument.t_isConsolidated != chr( 0 ) or " +
                     "( rtdocument.t_isConsolidated = chr( 0 ) and " +
                       "nvl( rtdocument.t_ConsDocumentID, 0 ) = 0  ) ) and " +
                   "rtdocument.t_ShiftNumber = ? and " +
                   "( decode( pmdtype.t_SystemAnalogID, 0, pmdtype.t_DocTypeID, pmdtype.t_SystemAnalogID ) <> 9 or " + 
                    "( rtdocument.t_isConsolidated != chr( 0 ) and rtdocument.t_ExchangeOperationCode BETWEEN '01' AND '11') or " +
                    "( rtdocument.t_isConsolidated = chr( 0 ) and rtdocument.t_ExchangeOperationCode > '11' ) ) and " +
                   "pmdtype.t_DocTypeID = rtdocument.t_DocTypeID and " +
                   "( pmdtype.t_DocTypeID IN ( 2, 3, 4, 9 ) or " +
                     "pmdtype.t_systemAnalogId IN ( 2, 3, 4, 9 ) ) and " +
                   "exists ( select 1 from dpmdtemplhistory_dbt pmdtemplhistory " +
                            "where pmdtemplhistory.t_HistoryID = rtdocument.t_HistoryID and " +
                                  "pmdtemplhistory.t_numPrintCopies > 0 ) ";
                   // "rtdocument.t_exportState != 2 " + // Выгруженные учитывать не надо

    if ( ValType( pAccMask ) == V_STRING )
      if ( pAccMask != "" )
        cmdStr = cmdStr + "and rtdocument.t_CreditAccount like '" + pAccMask + "%' ";
      end;
    end;

    cmdStr = cmdStr + "order by rtdocument.t_CreditAccount, rtdocument.t_CreditCurrCode, rtdocument.t_isConsolidated, rtdocument.t_DocumentID";

    cmdCred = RsdCommand( cmdStr );

    cmdCred.addParam( "", RSDBP_IN, Branch );
    cmdCred.addParam( "", RSDBP_IN, {curdate} );
    cmdCred.addParam( "", RSDBP_IN, numShift );
  else // По операционисту
    cmdStr = "select rtdocument.* from drtdocument_dbt rtdocument, dpmdtype_dbt pmdtype " +
             "where rtdocument.t_Branch = ? and " +
                   "rtdocument.t_Date = ? and " +
                   "rtdocument.t_Chapter <= 1 and " + // !!!!! = 1
                   "( rtdocument.t_CreditAccount like '202%' or " +
                     "rtdocument.t_CreditAccount like '203%' ) and " +
                   "rtdocument.t_isConsolidated = chr( 0 ) and " +
                   "rtdocument.t_Cashier = ? and " +
                   "rtdocument.t_ShiftNumber = ? and " +
                   "( decode( pmdtype.t_SystemAnalogID, 0, pmdtype.t_DocTypeID, pmdtype.t_SystemAnalogID ) <> 9 or " + 
                    "rtdocument.t_ExchangeOperationCode > '11' ) and " +
                   "pmdtype.t_DocTypeID = rtdocument.t_DocTypeID and " +
                   "( pmdtype.t_DocTypeID IN ( 2, 3, 4, 9 ) or " +
                     "pmdtype.t_systemAnalogId IN ( 2, 3, 4, 9 ) ) and " +
                   "exists ( select 1 from dpmdtemplhistory_dbt pmdtemplhistory " +
                            "where pmdtemplhistory.t_HistoryID = rtdocument.t_HistoryID and " +
                                  "pmdtemplhistory.t_numPrintCopies > 0 ) ";
                   // "rtdocument.t_exportState != 2 " + // Выгруженные учитывать не надо

    if ( ValType( pAccMask ) == V_STRING )
      if ( pAccMask != "" )
        cmdStr = cmdStr + "and rtdocument.t_CreditAccount like '" + pAccMask + "%' ";
      end;
    end;

    cmdStr = cmdStr + "order by rtdocument.t_CreditAccount, rtdocument.t_CreditCurrCode, rtdocument.t_isConsolidated, rtdocument.t_DocumentID";

    cmdCred = RsdCommand( cmdStr );

    cmdCred.addParam( "", RSDBP_IN, Branch );
    cmdCred.addParam( "", RSDBP_IN, {curdate} );
    cmdCred.addParam( "", RSDBP_IN, {oper} );
    cmdCred.addParam( "", RSDBP_IN, numShift );
  end;

  cmdCred.execute;
  
  rsCred = RsdRecordset( cmdCred );

  while ( rsCred.moveNext )
    PrintLine( false, rsCred, pTotInСashier, pTotInCashSymb );
  end;

  if ( PrevCodeCurr != -1 )
    PrintTail( pTotInСashier, pTotInCashSymb );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintDeskLogReport ( pRepMode, pAccMask, pTotInСashier, pTotInCashSymb )

  if ( not CanIssueDeskLog( ) )
    Exit( 1 );
  end;

  printReport( pRepMode, pAccMask, pTotInСashier, pTotInCashSymb );

end;

