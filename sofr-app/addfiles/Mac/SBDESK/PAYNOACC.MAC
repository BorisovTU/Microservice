/*
     R-Style Software Lab.

     RS-Retail 2.01.

     Заявление на перечисление денежных средств
     без открытия банковского счета

*/

import DeprIntr, "fields.mac", ChkPrn;

file   NOA_PAY_DOC ( "pay_doc.dbt" ) key 2;
record OC_PAY_DOC ( "pay_doc.np" );
record OC_PAY_DOC_PMNT_PROP("RTPaymentProp.dbt");
file   ret_op ( ret_op );

const NUMBER_DECLARATION = 3; /* количество экземпляров заявлений */

var OrdNum, i = 0;

var
  RegPath = "RS-RETAIL\\ПЕЧАТИ\\ВКЛАДЫ\\ДОГОВОРА";

 OC_RESULT = 0;

CheckPrinter( RegPath, StrFor( 0 ) );

ClearRecord( NOA_PAY_DOC );
copy( NOA_PAY_DOC, OC_PAY_DOC );
if( GetEQ( NOA_PAY_DOC ) )
  /* после печати ордера номер есть в файле, но нет в буфере OC_PAY_DOC*/
  OC_PAY_DOC.Ndoc  = NOA_PAY_DOC.Ndoc;
  /* номер заявления подхватываем из ret_op */
  /*
  GetRet_Op( NOA_PAY_DOC.iApplicationKind, NOA_PAY_DOC.ApplicationKey, ret_op );
  OrdNum = ret_op.Extra;
  */
  OrdNum = Trim( NOA_PAY_DOC.UserField );
end;


macro Get_ClientName( ClientLastName, ClientFirstName, ClientSecondName )
  var Name = "", IOtmp;

  Name = ClientLastName;
  Name = Name + " " + SubStr( ClientFirstName, 1, 1 ) + ".";
  Name = Name + " " + SubStr( ClientSecondName, 1, 1 ) + ".";

  return Name;
end;


macro Declaration()

  file PayCorr("pay_corr.dbt") key 1;

  var TRANZITACC = "",
      Ground, numpos;
  var Operation;

   macro Ternary( Cond, V1, V2 )

     if ( Cond )
       return V1;
     else
       return V2;
     end;
   end;

   numpos = Index( OC_PAY_DOC.Ground, "г." );
   if ( numpos != 0 )
     Ground = SubStr( OC_PAY_DOC.Ground, numpos+2 );
   else
     Ground = OC_PAY_DOC.Ground;
   end;

   PayCorr.BIK=OC_PAY_DOC_PMNT_PROP.BankCode;/*doc1.MFO_Receiver;*/
   PayCorr.CorrAccount=OC_PAY_DOC_PMNT_PROP.CorrAccount;/*doc1.CorAcc_Receiver;*/
   PayCorr.Account=OC_PAY_DOC_PMNT_PROP.Account;/*doc1.Account_Receiver;*/
   getEQ(PayCorr);


 [
                                    Заявление № ## - ##
            на перечисление денежных средств без открытия банковского счета
                                  от #

    +----------------------------------------+--------------------------------------+
    |  #                                     | #################################### |
    |                                        | #################################### |
    |                                        |                                      |
    |                                        | Прошу перечислить денежные средства, |
    |                                        | внесенные в кассу Банка на основании |
    |                                        | приходного  кассового ордера № ##### |
    |                                        | от #                                 |
    +----------------------------------------+--------------------------------------+
    | Сумма денежных средств                 | #                                    |
    | #                                      | #################################### |
    +----------------------------------------+--------------------------------------+
    | Получатель                             | #################################### |
 ]
 (
   OC_PAY_DOC.FNCash, OrdNum:l,
   String(OC_PAY_DOC.Date_Document:m),
   "КЛИЕНТ-ПЕРЕВОДОДАТЕЛЬ",
   ("Ф.И.О.: "+OC_PAY_DOC.ClientLastName+" "+
              OC_PAY_DOC.ClientFirstName+" "+OC_PAY_DOC.ClientSecondName):w,
   ("Адрес: "+OC_PAY_DOC.ClientAddress):w,
   OC_PAY_DOC.NDoc:c,
   String(OC_PAY_DOC.Date_Document:m),
   OC_PAY_DOC.InSum,
   "(цифрами и прописью)",
   String(OC_PAY_DOC.InSum:m):w,
     PayCorr.CorrName:w
/*   OC_PAY_DOC.RecipientName:w*/
 );
 if( OC_PAY_DOC.NumOpert == 42 )
 [  |                                        | ИНН: #                               |
    +----------------------------------------+--------------------------------------+
    | Банк получателя                        | #################################### |
    |                                        | БИК: #                               |
    |                                        | Корр.счет: #                         |
 ]
 (
   OC_PAY_DOC_PMNT_PROP.INN,
   OC_PAY_DOC_PMNT_PROP.BankName:w,
   OC_PAY_DOC_PMNT_PROP.BankCode,
   Acc_Form(OC_PAY_DOC_PMNT_PROP.CorrAccount)
 );
 end;

 [  +----------------------------------------+--------------------------------------+
    | Расчетный счет получателя              | #                                    |
 ]
 (
   Acc_Form(OC_PAY_DOC_PMNT_PROP.Account)
 );

 if( OC_PAY_DOC.NumOpert == 93 )
 [  +----------------------------------------+--------------------------------------+
    | Транзитный счет                        | #                                    |
 ]
 (
   Acc_Form(TRANZITACC)
 )
 end;

 [  +----------------------------------------+--------------------------------------+
    | #                                      | #################################### |
    +----------------------------------------+--------------------------------------+
 ]
 (
   "Назначение платежа",
   Ground:w
 );

 [  | Комисия и расходы                      | #                                    |
    |                                        |                                      |
    +----------------------------------------+--------------------------------------+
 ]
 (
   Ternary( OC_PAY_DOC.NumOpert == 93, "Нет", OC_PAY_DOC.CommSum )
 );
 [

       Я не буду иметь претензий при не поступлении средств по назначению в результате
    неверного заполнения  бланка перевода, указания мной ошибочной, неразборчивой  или
    неоднозначно толкуемой информации.


    _________________________                     ___________________#_________________
        (подпись)                                                 (Ф.И.О.)



    Отметка Банка о приеме

 ]( Get_ClientName( OC_PAY_DOC.ClientLastName, OC_PAY_DOC.ClientFirstName, 
                    OC_PAY_DOC.ClientSecondName ):c );
 PrintLn( StrFor( 12 ) );

end;

 /* Не печатаем для оплаты товара с переводом со счета */
 if( not( ( OC_PAY_DOC.NumOpert == 93 ) and ( OC_PAY_DOC.ApplType == 1 ) ) )

   /* Заявление печатается в количестве экземпляров NUMBER_DECLARATION */
   while( i < NUMBER_DECLARATION )
     Declaration();
     i = i+1;
   end;

   if( MacroOutput == "PRN" )
     Exit( 1 );
   end;

 else
    exit( 1 );
 end;

end;
