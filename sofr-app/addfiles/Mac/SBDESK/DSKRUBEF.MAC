/**************************************************************************\
*  --------------                                                          *
*  R-Style Soflab                                                          *
*  --------------                                                          *
*  RS-Retail                                                               *
*                                                                          *
*  Касса-кладовая                                                          *
*  АРМ Бухгалтера                                                          *
*                                                                          *
*  Ведение рублевых покрытий в RS-Retail                                   *
*                                                                          *
*  Лебедев С.В.                                                            *
*  27.09.2002                                                              *
\**************************************************************************/

import DeprIntr, BankInter, cur_rate;

private file sb_casdc      ( sb_casdc ) key 8;
private file sb_casvl      ( sb_casvl );
private file paydoc        ( paydoc, "exchange.def" ) key 0;
private file sb_acdep      ( sb_acdep ) key 0;
private file rtgroup       ( rtgroup  ) key 2;
private file sb_casac      ( sb_casac ) key 2;
private file currency      ( currency ) key 0;

private record OpnBkAcc     ( "OpnBkAcc.rec" );

private var Branch  = NumFNCash( );
private var Brigade = GetOperBrigade( );
private var {curdate};
private var {oper};

var RubEq_IsInitialized = false;
var RubEq_IsCalculated = false;
var TypeWorkRubEq   = 0;
var NeedCalc        = true;
var WorkWithBrigade = false;

private const AccSpecName   = "Касса";

private const SB_CASHOPERT  = 200;
private const SB_EXCHANGE   = 250;
private const SB_NOTBALANCE = 1000;
private const SB_PANTRY     = 190;
private const SB_RSBANK51   = 9000;

private const CASHOP_DEBIT  = 5; /* Расход */
private const CASHOP_CREDIT = 6; /* Приход */

private const TYPERES_CURRENCY = 1;
private const TYPERES_PAYDOC   = 4;

private const TYPE_RES_PAY   = 0;  /* Платежеспособные ресурсы                     */
private const TYPE_RES_NOPAY = 1;  /* Неплатежеспособные ресурсы                   */
private const TYPE_RES_PDNAT = 20; /* ПД, оплаченные рублями(национальной валютой) */
private const TYPE_RES_PDCUR = 21; /* ПД, оплаченнные валютой                      */
private const TYPE_RES_PD    = 22; /* Оплаченнные ПД                               */

private const DELETE_REC = 2;
private const STORN_REC  = 11;

array ASafe;
array ACurrCodeAcc, ACashAcc,   ACashAccRub;
array ACurrCode,    ARateCBNow, ARateCBPrev;
array ARefVal,      ADebit,     ACredit,     ARest,   ARestPrev,  ARestPrevByRatePrev, ARate,  ARatePrev,  ARevaluation, ARegulation;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro NeedWorkWithRubEq

  var RegPath = "RS-RETAIL\\СЕРВИСНЫЕ\\БУХГАЛТЕРИЯ\\ВЕДЕНИЕ_РП";
  var Value   = 0;
  var ErrCode;

  if ( GetRegistryValue( RegPath, V_INTEGER, Value, ErrCode ) == V_INTEGER )
    ;
  else
    Value = 0;
  end;

  return Value;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro NeedWorkWithBrigade

  var RegPath = "RS-RETAIL\\СЕРВИСНЫЕ\\БУХГАЛТЕРИЯ\\КОНТИРОВКА\\ПО_БРИГАДАМ";
  var Value   = 0;
  var ErrCode;

  if ( GetRegistryValue( RegPath, V_INTEGER, Value, ErrCode ) == V_INTEGER )
    ;
  else
    Value = 0;
  end;

  if ( Value != 0 )
    return true;
  else
    return false;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro WasCreateRubEqToday 

  var ret_val = false;

  return ret_val;

end;
    

/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CheckComplexOper

  var ret_val = true;
     
  return ret_val;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefCurrencyCBRate

  var stat;
  var RateCBNow  = 0;
  var RateCBPrev = 0;
  var i;

  ClearRecord( currency );
  currency.Code_Currency = 1;
  stat = GetGE( currency );
  while ( stat )
    RateCBNow  = 0;
    RateCBPrev = 0;
    if ( GetAllCurrRate( {curdate}, currency.Code_Currency, RateCBNow ) )
      if ( GetAllCurrRate( {curdate} - 1, currency.Code_Currency, RateCBPrev ) )
        if ( ( RateCBNow != 0 ) and ( RateCBPrev != 0 ) )
          i = ASize( ACurrCode );
          ACurrCode  ( i ) = currency.Code_Currency;
          ARateCBNow ( i ) = RateCBNow;
          ARateCBPrev( i ) = RateCBPrev;
        end;
      end;
    end;
    stat = Next( currency );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefCashAccs

  var stat;
  var need_continue;
  var i;

  ClearRecord( currency );
  currency.Code_Currency = 1;
  stat = GetGE( currency );
  while ( stat )
    /* Ищем по конретной валюте и конкретному подразделению */
    need_continue = true;
    ClearRecord( sb_acdep );
    sb_acdep.IsCur        = 1;
    sb_acdep.FNCash       = Branch;
    sb_acdep.CodCur       = currency.Code_Currency;
    sb_acdep.SpecVariable = AccSpecName;
    if ( GetEQ( sb_acdep ) and ( sb_acdep.Account != "" ) )
      need_continue = false;
    end;
    /* Ищем по конретной валюте и для отделения */
    if ( need_continue )
      ClearRecord( sb_acdep );
      sb_acdep.IsCur        = 1;
      sb_acdep.FNCash       = 0;
      sb_acdep.CodCur       = currency.Code_Currency;
      sb_acdep.SpecVariable = AccSpecName;
      if ( GetEQ( sb_acdep ) and ( sb_acdep.Account != "" ) )
        need_continue = false;
      end;
    end;
    /* Ищем по базовой валюте и конкретному подразделению */
    if ( need_continue )
      ClearRecord( sb_acdep );
      sb_acdep.IsCur        = 1;
      sb_acdep.FNCash       = Branch;
      sb_acdep.CodCur       = 1;
      sb_acdep.SpecVariable = AccSpecName;
      if ( GetEQ( sb_acdep ) and ( sb_acdep.Account != "" ) and ( Index( sb_acdep.Account, "ВВВ" ) != 0 ) )
        need_continue = false;
      end;
    end;
    /* Ищем по базовой валюте и для отделения */
    if ( need_continue )
      ClearRecord( sb_acdep );
      sb_acdep.IsCur        = 1;
      sb_acdep.FNCash       = 0;
      sb_acdep.CodCur       = 1;
      sb_acdep.SpecVariable = AccSpecName;
      if ( GetEQ( sb_acdep ) and ( sb_acdep.Account != "" ) and ( Index( sb_acdep.Account, "ВВВ" ) != 0 ) )
        need_continue = false;
      end;
    end;
    /* Счет был найден, занесем его в список */
    if ( not need_continue )
      i = ASize( ACurrCodeAcc );
      ACurrCodeAcc( i ) = currency.Code_Currency;
      ACashAcc    ( i ) = sb_acdep.Account;
      if ( sb_acdep.BalcAccRubPok != "" )
        ACashAccRub ( i ) = sb_acdep.BalcAccRubPok;
      else
        ACashAccRub ( i ) = sb_acdep.Account;
      end;
    end;
    stat = Next( currency );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefSafes

  var i = 0;
  var j;
  var stat;

  if ( WorkWithBrigade )
    ClearRecord( rtgroup );
    rtgroup.Branch = Branch;
    rtgroup.Date   = {curdate};
    rtgroup.ID     = Brigade;
    if ( GetEQ ( rtgroup ) )
      i = ASize( ASafe );
      ASafe( i ) = rtgroup.SafeID;
    end;
  else
    ClearRecord( rtgroup );
    rtgroup.Branch = Branch;
    rtgroup.Date   = {curdate};
    rtgroup.ID     = 0;
    stat = GetGE ( rtgroup );
    while ( stat )
      if ( ( rtgroup.Branch == Branch    ) and
           ( rtgroup.Date   == {curdate} )    )
        j = -1;
        i = 0;
        while ( i < ASize( ASafe ) )
          if ( ASafe( i ) == rtgroup.SafeID )
            j = i;
            i = ASize( ASafe );
          end;
          i = i + 1;
        end;
        if ( j == -1 )
          i = ASize( ASafe );
          ASafe( i ) = rtgroup.SafeID;
        end;
        stat = Next( rtgroup );
      else
        stat = false;
      end;
    end;
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro AddSumToArray ( CodCurr, RefValue, ValueMoney, FlagDebit, ValueRest, FlagYesterday )

  var ret_val = true;
  var rate_cb;
  var rate_cb_prev;
  var num_el;
  var i;

  if ( CodCurr > 0 )
    ret_val = false;
    i = 0;
    while ( i < ASize( ACurrCode ) )
      if ( CodCurr == ACurrCode( i ) )
        ret_val      = true;
        rate_cb      = ARateCBNow( i );
        rate_cb_prev = ARateCBPrev( i );
        i = ASize( ACurrCode );
      end;
      i = i + 1;
    end;
  end;

  if ( ret_val )
    num_el = -1;
    i = 0;
    while ( i < ASize( ARefVal ) )
      if ( RefValue == ARefVal( i ) )
        num_el = i;
        i = ASize( ARefVal );
      end;
      i = i + 1;
    end;

    if ( num_el == -1 )
      num_el = ASize( ARefVal );
      ARefVal  ( num_el ) = RefValue;
      ADebit   ( num_el ) = $0L;
      ACredit  ( num_el ) = $0L;
      ARest    ( num_el ) = $0L;
      ARestPrev( num_el ) = $0L;
      ARestPrevByRatePrev( num_el ) = $0L;
      ARate    ( num_el ) = rate_cb;
      ARatePrev( num_el ) = rate_cb_prev;
    end;
  end;

  if ( ret_val )
    if ( CodCurr == 0 )
      if ( ValueMoney != 0 )
        if ( FlagDebit )
          ADebit ( num_el ) = ADebit ( num_el ) + ValueMoney;
        else
          ACredit( num_el ) = ACredit( num_el ) + ValueMoney;
        end;
      elif ( ValueRest != 0 )
        if ( FlagYesterday )
          ARestPrev( num_el ) = ARestPrev( num_el ) + ValueRest;
        else
          ARest    ( num_el ) = ARest    ( num_el ) + ValueRest;
        end;
      end;
    elif ( CodCurr > 0 )
      if ( ValueMoney != 0 )
        if ( FlagDebit )
          ADebit ( num_el ) = ADebit ( num_el ) + SumConv( ValueMoney, rate_cb );
        else
          ACredit( num_el ) = ACredit( num_el ) + SumConv( ValueMoney, rate_cb );
        end;
      elif ( ValueRest != 0 )
        if ( FlagYesterday )
          ARestPrev( num_el ) = ARestPrev( num_el ) + SumConv( ValueRest, rate_cb );
          ARestPrevByRatePrev( num_el ) = ARestPrevByRatePrev( num_el ) + SumConv( ValueRest, rate_cb_prev );
        else
          ARest    ( num_el ) = ARest    ( num_el ) + SumConv( ValueRest, rate_cb ); 
        end;
      end;
    end;
  end;

  return ret_val;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindCashValue ( ref_value )
  
  ClearRecord( sb_casvl );
  KeyNum( sb_casvl, 0 );
  sb_casvl.RefValue = ref_value;

  return GetEQ( sb_casvl );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro DefCurrCode ( TypeValue, CodIntValue )

  var ret_code = 0;

  if ( TypeValue == TYPERES_CURRENCY )
    ret_code = CodIntValue;
  elif ( TypeValue == TYPERES_PAYDOC )
    ClearRecord( paydoc );
    paydoc.ID = CodIntValue;
    if ( GetEQ( paydoc ) )
      ret_code = paydoc.Curr_Ref;
    end;
  end;

  return ret_code;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro RecFltrForSbCasDc ( CodCur )

  var ret_val   = false;
  var code_curr = 0;
  var ref_value = 0;

  if ( (   sb_casdc.Action          != DELETE_REC        ) and
       (   sb_casdc.Action          != STORN_REC         ) and
       (   sb_casdc.ApplicationKind == SB_RSBANK51       ) and
       ( ( not WorkWithBrigade                       ) or
         ( sb_casdc.Brigade         == Brigade       )   ) and
       ( ( sb_casdc.NumOper         == CASHOP_DEBIT  ) or
         ( sb_casdc.NumOper         == CASHOP_CREDIT )   )    )
     if ( FindCashValue( sb_casdc.RefValue ) )
       if ( ( ( sb_casvl.TypeValue ==  TYPERES_CURRENCY ) or
              ( sb_casvl.TypeValue ==  TYPERES_PAYDOC   )   ) and          
            ( ( sb_casvl.Type      ==  TYPE_RES_PAY     ) or
              ( sb_casvl.Type      ==  TYPE_RES_NOPAY   ) or
              ( sb_casvl.Type      ==  TYPE_RES_PDNAT   ) or
              ( sb_casvl.Type      ==  TYPE_RES_PDCUR   ) or
              ( sb_casvl.Type      ==  TYPE_RES_PD      )   )    )
         code_curr = DefCurrCode( sb_casvl.TypeValue, sb_casvl.CodIntValue );
         if ( code_curr > 0 )
           ret_val = true;
         end;
       end;
     end;
  end;

  SetParm( 0, code_curr );

  return ret_val;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro RecFltrForSbCasVl ( CodCur )

  var ret_val   = false;
  var code_curr = 0;
  var ref_value = 0;

  if ( ( ( sb_casvl.TypeValue ==  TYPERES_CURRENCY ) or
         ( sb_casvl.TypeValue ==  TYPERES_PAYDOC   )   ) and          
       ( ( sb_casvl.Type      ==  TYPE_RES_PAY     ) or
         ( sb_casvl.Type      ==  TYPE_RES_NOPAY   ) or
         ( sb_casvl.Type      ==  TYPE_RES_PDNAT   ) or
         ( sb_casvl.Type      ==  TYPE_RES_PDCUR   ) or
         ( sb_casvl.Type      ==  TYPE_RES_PD      )   )    )
    code_curr = DefCurrCode( sb_casvl.TypeValue, sb_casvl.CodIntValue );
    if ( code_curr > 0 )
      ret_val = true;
    end;
  end;

  SetParm( 0, code_curr );

  return ret_val;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro IsCashAccInSboksvod ( RefValue )
  var ret_value = false;
  
  return ret_value;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro RecFltrForSbokSvod

  var stat = false;

  return stat;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro CalcRubEqSums

  var stat;
  var i         = 0;
  var j         = 0;
  var code_curr = 0;
  var ret_value = true;
  var deb_cred  = 0;
  var ref_value = 0;

  if ( not RubEq_IsInitialized )
    return true;
  end;

  if ( RubEq_IsCalculated )
    return true;
  end;

  DefCurrencyCBRate( );
  DefCashAccs( );
  DefSafes( );

  /* Цикл по кассовым документам RS-Bank */
  ClearRecord( sb_casdc );
  sb_casdc.Branch      = Branch;
  sb_casdc.CashDateDoc = {curdate};
  stat = GetGE( sb_casdc );
  while ( stat )
    if ( ( sb_casdc.Branch      == Branch    ) and
         ( sb_casdc.CashDateDoc == {curdate} )    )
      if ( RecFltrForSbCasDc( code_curr ) )
        if ( sb_casdc.NumOper == CASHOP_DEBIT )
          if ( not AddSumToArray( code_curr, sb_casdc.RefValue, sb_casdc.ValueMoney, true, $0L, false ) )
            ret_value = false;
          end;
        else
          if ( not AddSumToArray( code_curr, sb_casdc.RefValue, sb_casdc.ValueMoney, false, $0L, false ) )
            ret_value = false;
          end;
        end;
      end;
      stat = Next( sb_casdc );
    else
      stat = false;
    end;
  end;

  /* Цикл по счетам кассы для определения остатков на начало дня и конец дня */
  ClearRecord ( sb_casvl );
  KeyNum( sb_casvl, 0 );
  stat = GetGE( sb_casvl );
  while ( stat )
    if ( RecFltrForSbCasVl( code_curr ) )
      i = 0;
      while ( i <= 1 )
        j = 0;
        while ( j <= ASize( ASafe ) )
          ClearRecord( sb_casac );
          sb_casac.Branch     = Branch;
          sb_casac.CashOper   = ASafe( j );
          sb_casac.SubAccount = "";
          sb_casac.RefValue   = sb_casvl.RefValue;
          sb_casac.Date       = {curdate} - i;
          if ( ( GetLE ( sb_casac )                       ) and
               ( sb_casac.Branch     == Branch            ) and
               ( sb_casac.CashOper   == ASafe( j )        ) and
               ( sb_casac.SubAccount == ""                ) and
               ( sb_casac.RefValue   == sb_casvl.RefValue )    )
            if ( i == 0 )
              if ( not AddSumToArray( code_curr, sb_casac.RefValue, $0L, false, sb_casac.RestMoney, false ) )
                ret_value = false;
              end;
            elif ( i == 1 )
              if ( not AddSumToArray( code_curr, sb_casac.RefValue, $0L, false, sb_casac.RestMoney, true ) )
                ret_value = false;
              end;
            end;
          end;
          j = j  + 1;
        end;
        i = i + 1;
      end;
    end;
    stat = Next( sb_casvl );
  end;

  i = 0;
  while ( i < ASize( ARefVal ) )
    ARevaluation( i ) = ARestPrev( i ) - ARestPrevByRatePrev( i );
    ARegulation( i )  = ARest( i ) - ( ARestPrev( i ) + ACredit( i )  -  ADebit( i ) );
    i = i + 1;
  end;

  RubEq_IsCalculated = true;

  return ret_value;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro T_UpdateSbokSvod

end;


macro RubEq_Init( ErrorMsg )

  if ( RubEq_IsInitialized )
    return true;
  end;

  TypeWorkRubEq = NeedWorkWithRubEq( );
  WorkWithBrigade = NeedWorkWithBrigade( );

  if ( not CheckComplexOper( ) )
    SetParm( 0, "Расчет эквивалента в нац. валюте|Имеются необработанные документы, подлежащие сложной контировке" );
    return false;
  end;
                          
              
  RubEq_IsInitialized = true;
  return true;
end;