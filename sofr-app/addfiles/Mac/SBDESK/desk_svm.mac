/***************************************************************************
*  Оперкасса                                                               *
*                                                                          *
*  PRJ_001145 ОМС                                                          *
*  Сводная справка о кассовых оборотах                                     *
*  по операциям с драгоценными металлами                                   *
*  ф.344                                                                   *
***************************************************************************/
import CommonInter, TDate, RSD;

private var
  {curdate}, {oper}, {PrimaryDataInputMode},
  Branch = NumFNCash, 
  Brigade = GetOperBrigade;
var countStr = 0;

const FLAGDEBCRED_IN = 1;  /* Приход/Кредит */

class TMaterial
  var ref = 0;             /* референс ДМ */
  var name: string = "";   /* название ДМ */  
  /* оборот слитков для совмещения Прихода и Расхода в 1 строке */
  var credCountDoc = 0, credCountVal = 0;
  var credNominalWeight = 0.0, credChemicalWeight = 0.0;
  var debCountDoc = 0, debCountVal = 0;
  var debNominalWeight = 0.0, debChemicalWeight = 0.0;
end;

array Material;


/* наименование подразделения */
Macro GetBranchName()
   file doBrList( "dp_dep.dbt" ) key 0;
   doBrList.Code = Branch;
   if  ( GetEQ( doBrList ) )
      return doBrList.Name;
   else
      return "";
   end;
end;


/* ФИО операциониста */
macro GetOperName
  file person( person ) key 0;
  var operName="", s0, s1, s2, s3, p;

  person.Oper = {oper};
  if (getEQ(person))
    s0 = Trim( person.Name );
    p  = StrBrk( s0, " " );
    s1 = SubStr( s0, 1, p-1 );
    s2 = Trim(SubStr( s0, p ));
    p  = StrBrk( s2, " " );
    s3 = Trim(SubStr( s2, p ));
    operName = s1;
    if (strlen(s2) > 0)
      operName = operName + " " + SubStr( s2, 1, 1 ) + ".";
    end;
    if (strlen(s3) > 0)
      operName = operName + " " + SubStr( s3, 1, 1 ) + ".";
    end;
  end;

  return operName;
end;

/* заполнение массива данными из ci_code */
macro SetMaterialData
  array materialName;
  var cmd, rs, i = 0, j = 0;

  materialName(0) = "Золото";
  materialName(1) = "Серебро";
  materialName(2) = "Платина";
  materialName(3) = "Палладий";

  while ( i < ASize( materialName ) )
    cmd = RsdCommand("SELECT t_AutoKey, t_String FROM dci_code_dbt WHERE UPPER(t_String) = ? AND t_Group = 2");
    cmd.addParam("t_String", RsdBp_in, strupr(materialName(i)));
    cmd.execute();
    rs = RsdRecordset(cmd);

    if ( rs.moveNext() )
      Material(j) = TMaterial;
      Material(j).ref = rs.value(0);
      Material(j).name = rs.value(1);
      j = j + 1;
    end;
    i = i + 1;
  end;
end;

/* сортировка в порядке, заданном в массиве */
macro DecodeMaterialRef( field )
  var j = 0, arrLen, str = field;

  arrLen = ASize( Material );

  if ( arrLen > 1 )
    while ( j < arrLen )
      if (j == 0)
        str = "DECODE(" + field;
      end;
      str = str + ", " + String(Material(j).ref) + ", " + String(j);
      if ( j == arrLen - 1 )
        str = str + ")";
      end;
      j = j + 1;
    end;
  end;

  return str;
end;

/* ограничение заданным в массиве набором ДМ */
macro InMaterialRef( field )
  var j = 0, arrLen, str = "";

  arrLen = ASize( Material );

  if ( arrLen > 1 )
    while ( j < arrLen )
      if (j == 0)
        str = field + " IN (";
      end;
      str = str + String(Material(j).ref);
      if ( j == arrLen - 1 )
        str = str  + ") AND ";
      else
        str = str  + ",";
      end;
      j = j + 1;
    end;
  else
    str = field + " = " + String(Material(0).ref) + " AND ";
  end;

  return str;
end;


macro PrintHead
  var dateStr = TDateStorage;
[##########################################                                              Ф. №344
__________________________________________
(наименование, № филиала 
Сбербанка России  ОАО/№ ВСП)


                            Сводная справка о кассовых оборотах
                           по операциям с драгоценными металлами
                                за #######################

┌────────────┬────────────────────────────────────────┬────────────────────────────────────────┐
│Наименование│                   Приход               │                   Расход               │
│драгоценного├──────────┬───────┬──────────┬──────────┼──────────┬───────┬──────────┬──────────┤
│ металла    │Кол-во    │Кол-во,│ Масса в  │ Масса в  │Кол-во    │Кол-во,│ Масса в  │ Масса в  │
│            │документов│  шт.  │лигатуре,г│химической│документов│  шт.  │лигатуре,г│химической│
│            │          │       │          │чистоте, г│          │       │          │чистоте, г│
]
  ( GetBranchName:c,
    dateStr.getDateInCurStr("\"dd\" month yyyy года")
  );
end;


macro PrintEnd

[└────────────┴──────────┴───────┴──────────┴──────────┴──────────┴───────┴──────────┴──────────┘


 Заведующий кассой    __________________ ___________________________
                          (подпись)         (расшифровка подписи)


 Операционно-
 кассовый работник    __________________ ###########################
                          (подпись)



 Сверено:

 _________________    ___________________________ __________________
   (должность)                   (ФИО)                 (подпись)
]
  ( GetOperName:c
  );
end;


macro PrintReport
  var cmdstr, cmd, rs, cmd_raw, i = 0;
  var debCred, mNum;
  var arrLen, strDecode, strIN;

  SetMaterialData;
  arrLen = ASize( Material );

  if ( arrLen > 0 ) /* вывод оборота слитков только по ДМ, указанным в массиве */
    cmd_raw = RsdCommand("begin RSB_STRUCT.readstruct( 'ddesk_aux_1' ); end;");
    cmd_raw.execute;

    strDecode = DecodeMaterialRef("misc.t_MaterialRef");
    strIN = InMaterialRef("misc.t_MaterialRef");
    cmdstr = "SELECT " + strDecode + " as MaterialNum, casdc.t_FlagDebCredOper as DebCred, "
             " count(*) as CountDoc, sum(casdc.t_ValueCol) as CountVal, "
             " sum(RSB_STRUCT.GETDOUBLE('t_NominalWeight',aux.t_Info))/10000 as NmWeight, "
             " sum(RSB_STRUCT.GETDOUBLE('t_ChemicalWeight',aux.t_Info))/10000 as ChWeight "
            " FROM dsb_casdc_dbt casdc, dsb_casvl_dbt casvl, dci_misc_dbt misc, "
              " dbunch_dbt b, ddesk_aux_dbt aux, dci_code_dbt codeName WHERE "
             " casdc.t_Branch = ? AND casdc.t_CashDateDoc = ? AND casdc.t_Brigade = ? AND "
             " casdc.t_TypeDoc = 2 AND casdc.t_ApplicationKind IN (1,200, 300) AND " /* TypeDoc=2: подтвержденный документ */
             " casvl.t_RefValue = casdc.t_RefValue AND casvl.t_TypeValue = 505 AND "
             " misc.t_IssueID = casvl.t_CodIntValue AND misc.t_Type = 5 AND " + strIN +
             " codeName.t_AutoKey = misc.t_MaterialRef AND "
             " b.t_BagAppKind = casdc.t_ApplicationKind AND b.t_BagAppKey = casdc.t_ApplicationKey AND b.t_Type = 0 AND "
             " aux.t_Branch = casdc.t_Branch AND aux.t_ValueRef = casdc.t_RefValue AND aux.t_Date = casdc.t_cashdatedoc AND "
             " aux.t_Series = b.t_Series AND aux.t_Number = b.t_Min AND aux.t_IsGone = DECODE(casdc.t_FlagDebCredOper, 1, 0, 1) "
            " GROUP BY misc.t_MaterialRef, casdc.t_FlagDebCredOper "
            " ORDER BY " + strDecode + ", casdc.t_FlagDebCredOper";

    cmd = RsdCommand(cmdstr);
    cmd.addParam("t_Branch", RsdBp_in, Branch);
    cmd.addParam("t_CashDateDoc", RsdBp_in, {curdate});
    cmd.addParam("t_Brigade", RsdBp_in, Brigade);

    cmd.execute();
    rs = RsdRecordset(cmd);

    while ( rs.moveNext() )
      mNum = rs.value("MaterialNum");
      debCred = rs.value("DebCred");

      if ( debCred == FLAGDEBCRED_IN )   /* приход */
        Material(mNum).credCountDoc = int(rs.value("CountDoc"));
        Material(mNum).credCountVal = int(rs.value("CountVal"));
        Material(mNum).credNominalWeight = rs.value("NmWeight");
        Material(mNum).credChemicalWeight = rs.value("ChWeight");
      else                               /* расход */
        Material(mNum).debCountDoc = int(rs.value("CountDoc"));
        Material(mNum).debCountVal = int(rs.value("CountVal"));
        Material(mNum).debNominalWeight = rs.value("NmWeight");
        Material(mNum).debChemicalWeight = rs.value("ChWeight");
      end;
    end;

    while ( i < arrLen )  
      if ( (Material(i).credCountDoc > 0) or (Material(i).credCountVal > 0) or 
           (Material(i).credNominalWeight > 0) or (Material(i).credChemicalWeight > 0) or 
           (Material(i).debCountDoc > 0) or (Material(i).debCountVal > 0) or 
           (Material(i).debNominalWeight > 0) or (Material(i).debChemicalWeight > 0) )

        if ( countStr == 0 )
          PrintHead;
        end;

        [├────────────┼──────────┼───────┼──────────┼──────────┼──────────┼───────┼──────────┼──────────┤];
        [│############│   #####  │ ##### │##########│##########│   #####  │ ##### │##########│##########│]
          ( Material(i).name,
            Material(i).credCountDoc:r,
            Material(i).credCountVal:r,
            Material(i).credNominalWeight:0:2:r,
            Material(i).credChemicalWeight:0:2:r,
            Material(i).debCountDoc:r,
            Material(i).debCountVal:r,
            Material(i).debNominalWeight:0:2:r,
            Material(i).debChemicalWeight:0:2:r
          );
        countStr = countStr + 1;
      end;
      i = i + 1;
    end;

    if ( countStr > 0 )
      PrintEnd;
    end;

  end;
end;



  PrintReport;

  if ( countStr == 0 )
    if ( {PrimaryDataInputMode} )
      PrintHead;
      PrintEnd;
    else
      msgbox("Оборот слитков драгметаллов за " + {curdate} + " отсутствует");
      Exit(1);
    end;
  end;

END;