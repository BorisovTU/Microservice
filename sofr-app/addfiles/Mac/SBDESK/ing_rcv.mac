
/* 
   Акт приёма ценностей
   Y2K (c) by Demaio
*/

import rsd, bankinter, deprintr, DeskInter, RD_Comm;

file Ing_Rcv( "ing_rcv.tmp"  ) key 0 write;
file BagList( "bag.dbt"      ) key 0;
file Bunch  ( "bunch.dbt"    ) key 0;
file DeskAux( "desk_aux.dbt" ) key 0 write;
file CIMisc ( "ci_misc.dbt"  ) key 0;
file CICode ( "ci_code.dbt"  ) key 0;

record IngotAux( "desk_aux.1" );

const
  BST_NONE         = 0,			/* Никакое */
  BST_PREPANT      = 1,			/* Сумка в предкладовой (прием документов) */
  BST_REISSUE      = 2,			/* Переоформление */
  BST_NOT_ACPTD    = 3,			/* Сумка у контролера (прием сумки) */
  BST_ACPTD_RC     = 4,			/* Принято от инкассаторов с пересчетом */
  BST_ACPTD_NORC   = 5,			/* Принято от инкассаторов без пересчета */
  BST_EMERG_RC     = 6, 		/* Аварийный пересчет */
  BST_NOT_CHKD     = 7, 		/* Пересчет */
  BST_CHECKED      = 8,                	/* Пересчитана */
  BST_SENT         = 9,			/* Отправлена */
  BST_RETURNED     = 10;		/* Сдана */

var
  NeedWeightReport = false,
  ClientName,
  RegPath = "RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ПАРАМЕТРЫ_ДОГОВОРОВ\\НАЗВ_ГОРОДА",
  Type = V_STRING,
  Value,
  ErrCode,
  City = "",
  str1 = "", str2 = "", str3 = "",
  IsSend=false,
  Gone;

array
  Metals;

macro Mass2String( Mass )

  var
    str1,
    str2,
    str = "";

  RubToStr( Mass, str1, str2 );
  str = str1 + " целых " + str2 + " десятых";
  return str;
end;

macro Num2String( Number )

  var
    str = "";

  RubToStr( Number * 100, str, null );
  return str;
end;

macro FormSpecsNumber( MetalName )

  var
    i = 1,
    str = "";

  str = String( Bag.Number );
  while( i < ASize( Metals ) )
    if( MetalName == Metals( i ) )
      str = str + " / " + i;
      i = ASize( Metals );
    end;
    i = i + 1;
  end;
  return str;
end;

macro ClearTmpFile()
   var cmd;
   cmd = RsdCommand( NULL, string("truncate table ding_rcv_tmp") );
   cmd.execute();
end;

macro FindCICode( AutoKey )

  CICode.AutoKey = AutoKey;
  return GetEQ( CICode );
end;

macro FindCIMisc( Type, IssueID )

  CIMisc.Type = Type;
  CIMisc.IssueID = IssueID;
  return GetEQ( CIMisc );
end;

macro FindDeskAux( ValueRef, Date, Series, Number )

  DeskAux.Branch = Branch;
  DeskAux.ValueRef = ValueRef;
  DeskAux.Date = Date;
  DeskAux.IsGone = Gone;
  DeskAux.Series = Series;
  DeskAux.Number = Number;
  return GetEQ( DeskAux );
end;

macro FillTmpFile()

  var
    i = 1,
    Stat = true,
    BagFound, BunchFound,
    Hallmark, Metal,
    Delta,
    PrevMetal = "";

  ClearRecord( BagList );
  BagList.Branch = Bag.Branch;  
  BagList.Date = Bag.Date;
  BagList.ClientType = Bag.ClientType;
  BagList.ClientID = Bag.ClientID;
  BagList.Operation = Bag.Operation;
  BagList.Number = Bag.Number;
  if( StrFor( GetProgramID ) == "Л" )   /* Кладовая отделения */
    BagFound = GetGE( BagList );
  else                               /* Касса-кладовая */
    Copy( BagList, Bag );
    BagFound = true;
  end;
  while ( BagFound and Stat
    and ( BagList.Branch == Bag.Branch )  
    and ( BagList.Date == Bag.Date )
    and ( BagList.ClientType == Bag.ClientType )
    and ( BagList.ClientID == Bag.ClientID )
    and ( BagList.Number == Bag.Number ) )
    Stat = FindCashValue( BagList.ValueRef );
    if( Stat )
      Stat = FindCIMisc( CashValue.TypeValue - TYPERES_MINCAPISSUE, CashValue.CodIntValue );
    end;
    if( Stat )
      Stat = FindCICode( CIMisc.MaterialRef );
      if( Stat )
        Metal = CICode.String;
        if( PrevMetal != Metal )
          Metals( i ) = Metal;
          i = i + 1;
          PrevMetal = Metal;
        end;
      end;
    end;
    if( Stat )
      Stat = FindCICode( CIMisc.HallmarkRef );
      if( Stat )
        Hallmark = CICode.Double;
      end;
    end;
    if( Stat )
      ClearRecord( Bunch );
      Bunch.BagAppKind = BagList.ApplicationKind;
      Bunch.BagAppKey = BagList.ApplicationKey;
      BunchFound = GetGE( Bunch );
      while ( BunchFound and Stat
        and ( Bunch.BagAppKind == BagList.ApplicationKind )
        and ( Bunch.BagAppKey == BagList.ApplicationKey ) )
        Stat = FindDeskAux( BagList.ValueRef, BagList.Date, Bunch.Series, Bunch.Min );
        if( Stat )
          SetRecordAddr( IngotAux, DeskAux, 0, FldOffset( DeskAux, "Info" ), true );
          ClearRecord( Ing_Rcv );
          Ing_Rcv.Metal = Metal;
          Ing_Rcv.Series = Bunch.Series;
          Ing_Rcv.Number = Bunch.Min;
          Ing_Rcv.Hallmark = Hallmark;
          Ing_Rcv.Specs = IngotAux.Specs;
          Ing_Rcv.Year = IngotAux.Year;
          Ing_Rcv.Name = CIMisc.Name;
          Ing_Rcv.NominalWeight = IngotAux.NominalWeight / 100;
          Ing_Rcv.RealWeight = IngotAux.RealWeight / 100;
          Ing_Rcv.WeightOnBar = CIMisc.Weight / 100;
          Delta = DoubleL( IngotAux.RealWeight / 100 - IngotAux.NominalWeight / 100 ) / 100;
          if(   ( Delta < CIMisc.MinWeightDeviation ) 
             or ( Delta > CIMisc.MaxWeightDeviation )   )
            NeedWeightReport = true;
            if( Delta < CIMisc.MinWeightDeviation )
              Ing_Rcv.Shortage = -( Delta * 100 );
            elif( Delta > CIMisc.MaxWeightDeviation )
              Ing_Rcv.Overplus = Delta * 100;
            end;
          end;
          Stat = Insert( Ing_Rcv );
        end;
        BunchFound = Next( Bunch );
      end;
    end;
    BagFound = Next( BagList );
  end;
  return Stat;
end;

macro PrintSpecification( Metal )

 var No = 0,
     TotalNumber = 0,
     TotalWeight = 0,
     TotalChemicalWeight = 0;

  if(IsSend)
    [                                                         

                             Приложение к Акту выдачи ценностей
                                 №     #       от  ####################                         ]
    ( Bag.Number:c, Bag.Date:m:r );
  else
    [                                                         

                             Приложение к Акту приёма ценностей
                                 №     #       от  ####################                         ]
    ( Bag.Number:c, Bag.Date:m:r );
  end;
  [

                                  СПЕЦИФИКАЦИЯ №      #
                                             #
                                                                                              ]
  ( FormSpecsNumber( Metal ):c, Metal );
  [  ];
  [-------------------------------------------------------------------------------------------];
  [| № |                        |   Масса    |  Проба  |     №№     | Год |    Примечания    |];
  [|п/п|       NN   слитков     |   лигат.,  |         |  заводских |     |                  |];
  [|   |                        |   (гр.)    |         |спецификаций|     |                  |];
  [-------------------------------------------------------------------------------------------];
                
  ReWind( Ing_Rcv );
  while( Next( Ing_Rcv ) )
    No = No + 1;
    TotalNumber = TotalNumber + 1;
    TotalWeight = TotalWeight + Ing_Rcv.NominalWeight;
    TotalChemicalWeight = TotalChemicalWeight + 
       ( Money( Ing_Rcv.NominalWeight * Ing_Rcv.Hallmark ) / 1000 );
    [|###|########################|############|#########|############|#####|##################|]
    ( No:c, RegNumToStr(Ing_Rcv.Series, Ing_Rcv.Number):r, Ing_Rcv.NominalWeight, 
      Ing_Rcv.Hallmark:r:0:2, Ing_Rcv.Specs, Ing_Rcv.Year, Ing_Rcv.WeightOnBar );
  end;

  [
      Итого:   #        ( #                               )  слитков  общей  лигатурной
      массой   #           ( #                                                                   ) грамм
      и  общей  химически  чистого  металла      #           
      ( #                                                                   ) грамм.
                                                                                              ]
  ( TotalNumber:l, Num2String( TotalNumber ):l,
    TotalWeight:l, Mass2String( TotalWeight ):l,
    TotalChemicalWeight:l, Mass2String( TotalChemicalWeight ):l );

  [

      Руководитель                                                   _______________________  
 
      Главный бухгалтер                                              _______________________

      Начальник ОКО (зав. кассой)                                    _______________________

      Специалист по драгметаллам                                     _______________________  ];

  PrintLn( StrFor( 12 ) );

end;

macro PrintWeightReport()

 var No = 0;

  [                                                         

                                          СБЕРБАНК РОССИИ                                     ];
  [                                              #                                            ]
  ( {Name_Bank}:c );
  [                     
                #################                                   ####################      ]
  ( City, Bag.Date:m:r );
  [

                                      АКТ ВЕСОВЫХ РАСХОЖДЕНИЙ                                 ];

  [    
        Настоящий Акт составлен _____________________________________________________________________________
                                                 (должность, Ф.И.О. составивших акт)
        в том, что при приёме партии драгметалла в слитках, полученной от  #
        по договору № #
        обнаружены следующие весовые расхождения:
                                                                                              ]
  ( str2, str3 );
  [------------------------------------------------------------------------------------------------------------------];
  [| № |    спецификация    |Наименование|     Номер слитка   | Проба |    Лигатурная масса   |  Расхождения в массе|];
  [|п/п|--------------------|драгметаллов|                    |       |по специфи-|фактическая|  излишки |недостача |];
  [|   |  номер  |   дата   |            |                    |       |   кации   |           |          |          |];
  [-----------------------------------------------------------------------------------------------------------------|];

  ReWind( Ing_Rcv );
  while( Next( Ing_Rcv ) )                             
    if( Ing_Rcv.Shortage or Ing_Rcv.Overplus )
      No = No + 1;
      [|###|#########|##########|############|####################|#######|###########|###########|##########|##########|]
      ( No:c, FormSpecsNumber( Ing_Rcv.Metal ):c, Bag.Date, Ing_Rcv.Metal,
        RegNumToStr(Ing_Rcv.Series, Ing_Rcv.Number):w,
        Ing_Rcv.Hallmark:r:0:2, Ing_Rcv.NominalWeight, 
        Ing_Rcv.RealWeight, Ing_Rcv.Overplus, Ing_Rcv.Shortage );
    end;
  end;


  [

      Руководитель                                                   _______________________  
 
      Главный бухгалтер                                              _______________________

      Начальник ОКО (зав. кассой)                                    _______________________

      Специалист по драгметаллам                                     _______________________  ];

  PrintLn( StrFor( 12 ) );

end;

macro PrintCopy( CopyNo )

 var i = 1,
     No = 0,
     Weight = 0,
     Number = 0,
     TotalNumber = 0,
     TotalWeight = 0,
     TotalChemicalWeight = 0,
     PrevName = "";

  [                                                                              Экз.№__#__   ]
  ( CopyNo ); 
  [                                                         

                                          СБЕРБАНК РОССИИ                                     ];
  [                                              #                                            ]
  ( {Name_Bank}:c );
  [                     
                #################                                 ####################        ]
  ( City, Bag.Date:m:r );
  if( IsSend )
    [

                                    АКТ ВЫДАЧИ ЦЕННОСТЕЙ №      #                          

                                                                                                ]
    ( Bag.Number:c );
    [     Из  хранилища  ###############################################################                     
          банка  Сбербанка  России  выданы       
          ######################################  в  соответствии  с  договором  купли- 
          продажи  #                               следующие  ценности:                  

                                                                                                ]
    ( str1, str2, str3 );
  else
    [

                                    АКТ ПРИЁМА ЦЕННОСТЕЙ №      #                          

                                                                                                ]
    ( Bag.Number:c );
    [     В  хранилище  ###############################################################                     
          банка  Сбербанка  России  приняты  от       
          ######################################  в  соответствии  с  договором  купли- 
          продажи  #                               следующие  ценности:                  

                                                                                                ]
    ( str1, str2, str3 );
  end;
  [-------------------------------------------------------------------------------------------];
  [| № |             Наименование              |Количество|Масса лигат.,| Проба | Примечания |];
  [|п/п|                                       |          |    (гр.)    |       |            |];
  [-------------------------------------------------------------------------------------------];
                
  ReWind( Ing_Rcv );
  while( Next( Ing_Rcv ) )
    if( PrevName == "" )
      PrevName = Ing_Rcv.Name;
    end;
    if( Ing_Rcv.Name == PrevName )
      Number = Number + 1;
      Weight = Weight + Ing_Rcv.NominalWeight;
    else
      No = No + 1;
      [|###|#######################################|##########|#############|#######|            |]
      ( No:c, PrevName:l, Number:c, Weight:c, Ing_Rcv.Hallmark:r:0:2 );
      Number = 1;
      Weight = Ing_Rcv.NominalWeight;
      PrevName = Ing_Rcv.Name;
    end;
    TotalNumber = TotalNumber + 1;
    TotalWeight = TotalWeight + Ing_Rcv.NominalWeight;
    TotalChemicalWeight = TotalChemicalWeight + 
       ( Money( Ing_Rcv.NominalWeight * Ing_Rcv.Hallmark ) / 1000 );
  end;
  No = No + 1;
  [|###|#######################################|##########|#############|#######|            |]
  ( No:c, PrevName:l, Number:c, Weight:c, Ing_Rcv.Hallmark:r:0:2 );

  [
      Итого:   #        ( #                               )  слитков  общей  лигатурной
      массой   #           ( #                                                                   ) грамм
      и  общей  химически  чистого  металла      #           
      ( #                                                                   ) грамм.
                                                                                              ]
  ( TotalNumber:l, Num2String( TotalNumber ):l,
    TotalWeight:l, Mass2String( TotalWeight ):l,
    TotalChemicalWeight:l, Mass2String( TotalChemicalWeight ):l );

  [   Приложение: Спецификация №    #        от  #                                             ]
  ( Bag.Number:c, Bag.Date:m );
  [

      Руководитель                                                   _______________________  
 
      Главный бухгалтер                                              _______________________

      Начальник ОКО (зав. кассой)                                    _______________________

      Специалист по драгметаллам                                     _______________________  ];

  PrintLn( StrFor( 12 ) );

  while( i < ASize( Metals ) )
    PrintSpecification( Metals( i ) );
    i = i + 1;
  end;

  if( ( NeedWeightReport ) and ( not IsSend) )
    PrintWeightReport();
  end;

end;


/*
                           	Т О Ч К А  В Х О Д А
*/  

  if( GetProgramID != CodeFor( "Л" ) )
    GetInt(Bag.Number,"Введите номер договора:",10);
    Bag.ApplicationKind = CashDoc.ApplicationKind;
    Bag.ApplicationKey = CashDoc.ApplicationKey;
    Bag.ValueRef = CashDoc.RefValue;
    Bag.Date = CashDoc.CashDateDoc;
  else
    if( ( Bag.State != BST_CHECKED )
    and ( Bag.State != BST_RETURNED )
    and ( Bag.State != BST_SENT )
    and ( Bag.State != BST_REISSUE ) )
      Exit( 1 );
    end;
  end;

  var Question;
  if(Bag.Operation==CASHOP_PANT_RDEB) 
    Question = "Печатать Акт выдачи ценностей ?";
    IsSend = true;
    Gone = 1;
  else
    Question = "Печатать Акт приёма ценностей ?";
    IsSend = false;
    Gone = 0;
  end;

  if( GetTrue( false, Question ) )
    GetClientInfo( Bag.ClientType, Bag.ClientID, ClientName );
    if( GetRegistryValue( RegPath, Type, Value, ErrCode ) == V_STRING )
      City = Value;
    end;  
    if( Bag.Operation == CASHOP_PANT_RCRD )
      str1 = {Name_Bank};
      str2 = ClientName;
      str3 = Bag.ContractNumber + " от " + Bag.ContractDate;
    end;
    ClearTmpFile();
    if( FillTmpFile() )
      PrintCopy( 1 );
      PrintCopy( 2 );
      PrintCopy( 3 );
    else
      PrintLn( "Ошибка при заполнении временного файла :o(" );
    end;
  end;
End.