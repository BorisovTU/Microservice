
/* Опись на рублевые сумки ф.234 */

import DeskInter, RD_Comm;

file BagSplit( "bagsplit.dbt" ) key 0;

array
  NoteNames, NoteAmounts,  
  CoinNames, CoinAmounts;

var 
  ProgID = StrFor( GetProgramID ), 
  PrintFIO = GetRegSetting( V_INTEGER, "КЛАДОВАЯ\\ФИО_ИНКАС_В_ОПИСИ" ), 
  PeerName,
  NNotes = 0, 
  NCoins = 0, 
  ColName = "", 
  SealNo, 
  TotalNotes = $0, 
  TotalCoins = $0;

macro LookupUnitName( Par )

  if ( Par == $1 )
    return "1 рубль";
  elif ( Par == $2 )
    return "2 рубля";
  elif ( Par == $0.01 )
    return "1 копейка";
  elif ( Par < $1 )
    return SubStr( String( Par ), Index( String( Par ), "." ) + 1 ) + " копеек";
  else
    return SubStr( String( Par ), 1, Index( String( Par ), "." ) - 1 ) + " рублей";
  end;                                     
end;

macro FillParArrays

  var
    RecFound;
  
  ClearRecord( BagSplit );
  BagSplit.Branch = Bag.Branch;  
  BagSplit.Date = Bag.Date;
  BagSplit.ClientType = Bag.ClientType;
  BagSplit.ClientID = Bag.ClientID;
  BagSplit.Number = Bag.Number;
  BagSplit.ValueRef = Bag.ValueRef;
  RecFound = GetGE( BagSplit );
  while ( RecFound                                                             
    and ( BagSplit.Branch == Bag.Branch )
    and ( BagSplit.Date == Bag.Date )
    and ( BagSplit.ClientType == Bag.ClientType )
    and ( BagSplit.ClientID == Bag.ClientID )
    and ( BagSplit.Number == Bag.Number )
    and ( BagSplit.ValueRef == Bag.ValueRef ) )
    if ( BagSplit.IsCoin != StrFor( 0 ) )
      CoinNames( NCoins ) = LookupUnitName( BagSplit.Character );
      CoinAmounts( NCoins ) = BagSplit.Character * BagSplit.Amount;
      TotalCoins = TotalCoins + CoinAmounts( NCoins );
      NCoins = NCoins + 1;
    else    
      NoteNames( NNotes ) = LookupUnitName( BagSplit.Character );
      NoteAmounts( NNotes ) = BagSplit.Character * BagSplit.Amount;
      TotalNotes = TotalNotes + NoteAmounts( NNotes );
      NNotes = NNotes + 1;
    end;
    RecFound = Next( BagSplit );
  end;
end;

macro PrintListBody( CopyNo )

  var 
    Mark = "", 
    NoteName, NoteAmount, CoinName, CoinAmount, 
    i = 0, n;

  if ( CopyNo == 2 )
    Mark = "-а";
  elif ( CopyNo == 3 )
    Mark = "-б";
  end;

  if ( BankStandart )
    [                                                                   # экз. ]
    ( CopyNo );
  else
    [ Ф. № 234##                                                        # экз. ]
    ( Mark, CopyNo );
  end;
  [                                                                          ];
  [                                О П И С Ь                                 ];
  [                на денежную наличность Российской Федерации               ];
  [ ######################################################################## ]
  ( ( "отправляемую через инкассаторов " + String( Bag.Date:m ) ):c );
  [                                                                          ];
  [                                                                          ];
  if ( Bag.Operation == CASHOP_PANT_RDEB )
    [ Наименование банка-отправителя #                                         ]
    ( {Name_Bank} );
    [ Наименование банка-получателя  #                                         ]
    ( PeerName );
  else
    [ Наименование банка-отправителя #                                         ]
    ( PeerName );
    [ Наименование банка-получателя  #                                         ]
    ( {Name_Bank} );
  end;
  [                                                                          ];
  [ Направляем денежную наличность, вложенную в инкассаторскую сумку (сумки) ];
  [ № #                  опечатанную(ные) пломибиром № #____________________ ]
  ( BagNum.NumberBag, SealNo );
  [                                                                          ];
  [--------------------------------------------------------------------------];
  [| Наименование и | Сумма по каждому | Наименование и  | Сумма по каждому |];
  [| достоинство    | достоинству      | достоинство     | достоинству      |];
  [| купюр          |                  | купюр           |                  |];   
  [|----------------|------------------|-----------------|------------------|];
   
  n = Max( NNotes, NCoins );      
  while ( i < n )
    NoteName = NoteAmount = CoinName = CoinAmount = "";
    if ( i < NNotes )
      NoteName = NoteNames( i );
      NoteAmount = NoteAmounts( i );
    end;                        
    if ( i < NCoins )
      CoinName = CoinNames( i );
      CoinAmount = CoinAmounts( i );
    end;                        
   [ ################ ################## ################# ################## ]
   (
     NoteName, 
     NoteAmount:r, 
     CoinName, 
     CoinAmount:r
   );
   i = i + 1;
 end;   
 [--------------------------------------------------------------------------];
 [ И т о г о :      ##################                   ################## ]
 (
   TotalNotes,  
   TotalCoins 
 ); 
 [ ];
 [##### ####################################################################]
 ( "Всего", ( String( TotalNotes + TotalCoins ) + " (" + RubToStr( TotalNotes + TotalCoins ) + ")" ):w );
 [ ];
 if ( BankStandart )
   [Заведующий кассой _____________________________ ]; 
   [Кассир ________________________________________ ]; 
 else
   [Заведующий кладовой ___________________________ ]; 
   [Операционный работник _________________________ ]; 
 end;  
 [ ];
 [М.П. ];
 [--------------------------------------------------------------------------]
end;

macro PrintSign1

  ["__" _____________ 200_ г. указанные в описи ценности на сумму           ];
  [#########################################################################]
  ( ( String( TotalNotes + TotalCoins ) + " (" + RubToStr( TotalNotes + TotalCoins ) + ")" ):w );
  [приняты];
  if ( BankStandart and ( ProgID != "Л" ) )
    [Заведующий кассой _____________________________ ]; 
    [Кассир ________________________________________ ]; 
  else
    [Заведующий кладовой (ст. кассир) ______________ ]; 
    [Операционный работник _________________________ ]; 
  end;  
end;

macro PrintSign2

  ["__" _____________ 200_ г. указанную в описи опломбированую сумку (сумки)];
  [в исправной упаковке, без пересчета в ней (в них) ценностей              ];
  [принял:];
  if ( PrintFIO )
    [Инкассатор ____________________________________ #] ( ColName ); 
  else
    [Инкассатор ____________________________________]; 
  end; 
  [ ];
  [ М.П. ];
end;

macro PrintSign3

  ["__" _____________ 200_ г. указанную в описи опломбированую сумку (сумки)];
  [в исправной упаковке, без пересчета в ней (в них) ценностей              ];
  [приняли:];
  if ( BankStandart and ( ProgID != "Л" ) )
    [Заведующий кассой _____________________________ ]; 
    [Кассир ________________________________________ ]; 
  else
    [Заведующий кладовой (ст. кассир) ______________ ]; 
    [Операционный работник _________________________ ]; 
  end;  
  [                                                             М.П. ];
  [Ценности сдал:];
  if ( PrintFIO )
    [Инкассатор ____________________________________ #] ( ColName ); 
  else
    [Инкассатор ____________________________________]; 
  end; 
  [ ];
end;

macro PrintCopy( n )

  PrintListBody( n );
  [ ];
  [                           Р А С П И С К А                               ];
  if ( n == 1 )
    PrintSign1;
  elif ( n == 2 )
    PrintSign2;
  elif ( n == 3 )
    PrintSign3;
  end;
  PrintLn( StrFor( 12 ) );
end;

macro f234()

  if ( Bag.Number == 0 )
    Exit( 1 );
  end;

  CheckPersonType( PT_CONTR );
  SealNo = GetSealNo;

  GetClientInfo( Bag.ClientType, Bag.ClientID, PeerName );
  if ( ( ProgID != "Л" ) and PrintFIO )
    ColName = GetCollectorName( CashDoc.Branch, CashDoc.Collector );
    if ( ColName != "" )
      ColName = "(" + ColName + ")";
    end;
  end;
  FindBagNum( Bag.Number );
  if ( SplitCurrBag( Bag ) )
    FillParArrays;
    PrintCopy( 1 );
    PrintCopy( 2 );
    PrintCopy( 3 );
  end;
end;
  