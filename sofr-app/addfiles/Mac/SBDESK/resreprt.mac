/* Отчет по ресурсам кассы */

import deprintr;

file Alg    ("namealg.dbt","bank.def")  key 0;
file CashVal("sb_casvl.dbt") key 1;
file CashAcc("sb_casac.dbt") key 1;
file CashDoc("sb_casdc.dbt") key 5;

const
  CV_Money = 1,
  CV_ValForm = 2;

const
  TYPEDOC_CARRYOFF = 1,  /* Отложен (не подтвержден) */
  TYPEDOC_CARRYON  = 2;  /* Проведен (подтвержден)   */

const
  ALG_CASH_VAL_TYPE = 811;    /* Типы ресурсов кассы в namealg.dbt */

const
  FLAGDEBCRED_IN   = 1,  /* Кредит  */
  FLAGDEBCRED_OUT  = 2;  /* Дебет   */

var
  Branch = NumFNCash,
  {curdate};

macro OutTitle;

  [
                            Отчет по остаткам ресурсов за ##########
  ]
  (String({curdate}));
end;

/* Получить название типа ресурса кассы */
macro GetCashTypeName(Type)

  Alg.iTypeAlg = ALG_CASH_VAL_TYPE;
  Alg.iNumberAlg = Type;
  if(GetEQ(Alg))
    return Alg.szNameAlg;
  else
    return String(Type);
  end;
end;

macro OutMoneySubTitle(ResName,Type);

  PrintLn;
  if ( Type == 0 )
    PrintLn("  Движение средств (",ResName,")");
  else
    PrintLn("  Движение средств (",ResName," (",GetCashTypeName(Type),"))");
  end;
  PrintLn;
end;

macro OutValFormSubTitle(ResName,Type);

  PrintLn;
  if ( Type == 0 )
    PrintLn("  Движение ценностей (",ResName,")");
  else
    PrintLn("  Движение ценностей (",ResName," (",GetCashTypeName(Type),"))");
  end;
  PrintLn;
end;

macro OutHeader;

  [┌──────────────┬───────────────────────────────────────────────┬───────────────────────┐
   │              │       Обороты по операциям с клиентами        │                       │
   │ Пользователь ├───────────────────────┬───────────────────────┤     О с т а т о к     │
   │              │      П р и х о д      │      Р а с х о д      │                       │
   ├──────────────┼───────────────────────┼───────────────────────┼───────────────────────┤];
end;

macro CalcMoneyTurns(Cred,Deb)

  var
    RecFound,
    Cr=$0,
    Db=$0;

  ClearRecord(CashDoc);
  CashDoc.Branch=Branch;
  CashDoc.CashDateDoc={curdate};
  CashDoc.TypeDoc=TYPEDOC_CARRYON;
  CashDoc.CashPatern="Кладовая";
  CashDoc.RefValue=CashVal.RefValue;
  RecFound=GetGE(CashDoc);
  while(RecFound and
    (CashDoc.Branch==Branch) and
    (CashDoc.CashDateDoc=={curdate}) and
    (CashDoc.TypeDoc==TYPEDOC_CARRYON) and
    (CashDoc.CashPatern=="Кладовая") and
    (CashDoc.RefValue==CashVal.RefValue))
    if(CashDoc.CashOper==CashAcc.CashOper)
      if(CashDoc.FlagDebCredOper==FLAGDEBCRED_IN)
        Cr=Cr+CashDoc.ValueMoney;
      else
        Db=Db+CashDoc.ValueMoney;
      end;
    end;
    RecFound=Next(CashDoc);
  end;
  SetParm(0,Cr);
  SetParm(1,Db);
end;

macro OutMoneyLine;

  var
    Cred,
    Deb;

  CalcMoneyTurns(Cred,Deb);

  [│ ############ │ ##################### │ ##################### │ ##################### │]
  (CashAcc.CashOper,CashAcc.KredMoney+Cred,CashAcc.DebMoney+Deb,CashAcc.RestMoney);
end;

macro CalcValFormTurns(Cred,Deb)

  var
    RecFound,
    Cr=0,
    Db=0;

  ClearRecord(CashDoc);
  CashDoc.Branch=Branch;
  CashDoc.CashDateDoc={curdate};
  CashDoc.TypeDoc=TYPEDOC_CARRYON;
  CashDoc.CashPatern="Кладовая";
  CashDoc.RefValue=CashVal.RefValue;
  RecFound=GetGE(CashDoc);
  while(RecFound and
    (CashDoc.Branch==Branch) and
    (CashDoc.CashDateDoc=={curdate}) and
    (CashDoc.TypeDoc==TYPEDOC_CARRYON) and
    (CashDoc.CashPatern=="Кладовая") and
    (CashDoc.RefValue==CashVal.RefValue))
    if(CashDoc.CashOper==CashAcc.CashOper)
      if(CashDoc.FlagDebCredOper==FLAGDEBCRED_IN)
        Cr=Cr+CashDoc.ValueCol;
      else
        Db=Db+CashDoc.ValueCol;
      end;
    end;
    RecFound=Next(CashDoc);
  end;
  SetParm(0,Cr);
  SetParm(1,Db);
end;

macro OutValFormLine(IsFirstForThisOper);

  var
    Cred,
    Deb;

  CalcValFormTurns(Cred,Deb);

  [│ ############ │ ##################### │ ##################### │ ##################### │]
  (CashAcc.CashOper,CashAcc.KredCol+Cred,CashAcc.DebCol+Deb,CashAcc.RestCol:r);
end;

macro OutBottomLine;

  [└──────────────┴───────────────────────┴───────────────────────┴───────────────────────┘];
end;

macro ReportOnCurMoneyResource;

  CashAcc.Branch=Branch;
  CashAcc.RefValue=CashVal.RefValue;
  CashAcc.Date={curdate};
  CashAcc.CashOper="";

  OutMoneySubTitle(CashVal.NameValue,CashVal.Type);
  OutHeader;

  if(GetGE(CashAcc) and
    (CashAcc.Branch==Branch) and
    (CashAcc.RefValue==CashVal.RefValue) and
    (CashAcc.Date=={curdate}))
    if((CashAcc.CashOper!="Инкассация") and (CashAcc.CashOper!="Кладовая"))
      OutMoneyLine;
    end;
    while(Next(CashAcc) and
      (CashAcc.Branch==Branch) and
      (CashAcc.RefValue==CashVal.RefValue) and
      (CashAcc.Date=={curdate}))
      if((CashAcc.CashOper!="Инкассация") and (CashAcc.CashOper!="Кладовая"))
        OutMoneyLine;
      end;
    end;
  end;

  OutBottomLine;
end;

macro ReportOnCurValFormResource;

  CashAcc.Branch=Branch;
  CashAcc.RefValue=CashVal.RefValue;
  CashAcc.Date={curdate};
  CashAcc.CashOper="";

  OutValFormSubTitle(CashVal.NameValue,CashVal.Type);
  OutHeader;

  if(GetGE(CashAcc) and
    (CashAcc.Branch==Branch) and
    (CashAcc.RefValue==CashVal.RefValue) and
    (CashAcc.Date=={curdate}))
    if((CashAcc.CashOper!="Инкассация") and (CashAcc.CashOper!="Кладовая"))
      OutValFormLine;
    end;
    while(Next(CashAcc) and
      (CashAcc.Branch==Branch) and
      (CashAcc.RefValue==CashVal.RefValue) and
      (CashAcc.Date=={curdate}))
      if((CashAcc.CashOper!="Инкассация") and (CashAcc.CashOper!="Кладовая"))
        OutValFormLine;
      end;
    end;
  end;

  OutBottomLine;
end;

macro ReportOnMoneyResources;

  CashVal.TypeValue=CV_Money;
  CashVal.CodIntValue=0;

  if(GetGE(CashVal) and (CashVal.TypeValue==CV_Money))
    ReportOnCurMoneyResource;
    while(Next(CashVal) and (CashVal.TypeValue==CV_Money))
      ReportOnCurMoneyResource;
    end;
  end;
end;

macro ReportOnValFormAndOtherResources;

  CashVal.TypeValue=CV_ValForm;
  CashVal.CodIntValue=0;

  if(GetGE(CashVal))
    ReportOnCurValFormResource;
    while(Next(CashVal))
      ReportOnCurValFormResource;
    end;
  end;
end;

  OutTitle;
  ReportOnMoneyResources;
  ReportOnValFormAndOtherResources;
end;

