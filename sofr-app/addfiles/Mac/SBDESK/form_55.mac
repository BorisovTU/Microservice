/* Форма 55 */

import DeprIntr, ExchangeInter;

file OthStat("sb_othst.dbt") key 2;
file Currency("currency.dbt") key 0;
file PayKind("pay_kind.dbt") key 0;

const
  SB_COMPAY    = 20,   /* Коммунальные платежи */
  SB_CASHOPERT = 200,  /* документ для сервисно-кассовых операций */
  SB_CAPISSUE  = 300;  /* Ценные бумаги                           */

const
  CO_CommPay     =  1, /* Коммунальные платежи */
  CO_NalPer      = 11, /* Наличный перевод     */
  CO_Comission   = 21, /* Комиссия             */
  CO_Conversion  = 31, /* Конверсия            */
  CO_Payed       = 41; /* Выплата              */

var
  {curdate},
  Mode = GetCurrentMode,
  IsCur=NumFlagCur,
  Branch=NumFNCash;

var
  TotOth=$0,
  TotTot=$0;

/*******************************************************************
                Определение кода национальной валюты
*******************************************************************/
macro NatCur()

  var retCur;

  KeyNum( Currency, 1 );
  Currency.ExternalCode = {NationalCur};
  if ( getEQ( Currency ) )
    retCur = Currency.Code_Currency;
  else
    retCur = -1;
  end;

  KeyNum( Currency, 0 );

  return retCur;

end;

macro OutHead

  [Учреждение банка №___________________                                                           Ф. № 55

                                                        СВОДНЫЙ
                              кассовый ордер на сумму доходов, полученных от населения
                                                   #

  ]({curdate}:m);
end;

macro OutSubHead

  if(IsCur)
    PrintLn(Currency.Name_Currency);
    PrintLn;
  end;
  [      +---------------------------------+----------------+----------------+----------------+---------------+
         |          За выданные            |       За       |  За хранение   |                |               |
         |----------------+----------------|    переводы    |     ценных     |     Прочие     |     Всего     |
         | чековые книжки | расчетные чеки |    вкладов     |     бумаг      |                |               |
         +----------------+----------------+----------------+----------------+----------------+---------------+];
end;

macro OutLine(Oth)

  [      |                |                |                |                | ############## | ############# |]
  (
    Oth,
    Oth
  );
  TotOth=TotOth+Oth;
  TotTot=TotTot+Oth;
end;

macro OutBottom

  var
    SpelledSum,m1,m2;

  if(not IsCur)
    if ( NatCur() == 0 )
      SpelledSum=RubToStr(TotTot);
    else
      RubToStr(TotTot,m1,m2);
      SpelledSum=m1+" "+Currency.Short_Name+" "+m2;
    end;
  else
    RubToStr(TotTot,m1,m2);
    SpelledSum=m1+" "+Currency.Short_Name+" "+m2;
  end;

  [ ];
  [#                                                                                Отметки бухгалтерии
   -----------------------------------------------------                   ------------------------------------
                   (сумма прописью)                                         Дебет счета №54 | Кредит счета №126

   Ст. контролер (контролер) ___________________________                             "__" ___________ _____ г.

   Кассир ______________________________________________                             Бухгалтер ________________

  ]
  (SpelledSum);
end;

macro OutTotLine

  [------+----------------+----------------+----------------+----------------+----------------+---------------+];
  [Итого |                |                |                |                | ############## | ############# |]
  (
    TotOth,
    TotTot
  );
end;

var
  IsFirst;

macro ReportOnPayType

  OthStat.FNCash=Branch;
  OthStat.AplicKind=SB_CASHOPERT;
  OthStat.CodCur=Currency.Code_Currency;
  OthStat.GroupOper=PayKind.GroupOpert;
  OthStat.NumOper=PayKind.NumOperat;
  OthStat.DateOper={curdate};
  OthStat.Mode=Mode;

  if(GetEQ(OthStat))
    if(IsFirst)
      OutSubHead;
      IsFirst=false;
    end;
    OutLine(OthStat.KredSum);
  end;
end;

macro ReportOnCommissionsForCurCurr

  var
    RecFound;

  TotOth=$0;
  TotTot=$0;
  IsFirst=true;
  ClearRecord(PayKind);
  PayKind.IsCur=IsCur;
  PayKind.GroupOpert=CO_Comission;
  RecFound=GetGE(PayKind);
  while(RecFound and(PayKind.GroupOpert==CO_Comission))
    ReportOnPayType;
    RecFound=Next(PayKind);
  end;
  OutTotLine;
  OutBottom;
end;

macro ReportOnCommissions

  var
    RecFound;

  OutHead;
  if(not IsCur)
    Currency.Code_Currency=0;
    if(GetEQ(Currency))
      ReportOnCommissionsForCurCurr;
    end;
  else
    Currency.Code_Currency=1;
    RecFound=GetGE(Currency);
    while(RecFound);
      ReportOnCommissionsForCurCurr;
      RecFound=Next(Currency);
    end;
  end;
end;

  ReportOnCommissions
end;