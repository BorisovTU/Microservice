
/* Опись на рублевые сумки ф.235 */

import DeskInter, RD_Comm, ChkPrn, Ingot;

file BagList( "bag.dbt" ) key 0;
file CIMisc( "ci_misc.dbt" ) key 0;
file DeskAux( "desk_aux.dbt" ) key 0;
file Bunch( "bunch.dbt" ) key 0;
record IngotAux( "desk_aux.1" );

const
  CI_INGOT = 5;

var 
  ColName, 
  PeerName,
  TotalRub = $0;

macro FindCIMisc( Type, Issue )

  CIMisc.Type = Type;
  CIMisc.IssueID = Issue;
  return GetEQ( CIMisc );
end;

macro GetIngotBalCost

  var
    RecFound, 
    Rate, 
    BalCost = $0; 

  if ( not FindCashValue( BagList.ValueRef ) )
    MsgBox( "Не найдена ценность: ", BagList.ValueRef );
    Exit( 1 );
  end;

  if ( not FindCIMisc( CashValue.TypeValue - TYPERES_MINCAPISSUE, CashValue.CodIntValue ) )
    MsgBox( "Не найден вид слитков" );
    Exit( 1 );
  end;

  Rate = Ing_GetRubRate( CIMisc.IssueID, Bag.Date );

  ClearRecord( Bunch );
  Bunch.BagAppKind = BagList.ApplicationKind;
  Bunch.BagAppKey = BagList.ApplicationKey;
  RecFound = GetGE( Bunch );
  while ( RecFound 
    and ( Bunch.BagAppKind == BagList.ApplicationKind )
    and ( Bunch.BagAppKey == BagList.ApplicationKey ) )
    DeskAux.Branch = BagList.Branch;
    DeskAux.ValueRef = BagList.ValueRef;
    DeskAux.Date = BagList.Date;
    DeskAux.IsGone = 1;
    DeskAux.Series = Bunch.Series;
    DeskAux.Number = Bunch.Min;
    if ( GetEQ( DeskAux ) )
      SetRecordAddr( IngotAux, DeskAux, 0, FldOffset( DeskAux, "Info" ), true );
      if ( CIMisc.UseWeightType == IWT_LIGATURE )
        BalCost = BalCost + Money( IngotAux.RealWeight * Rate / 100 );
      else
        BalCost = BalCost + Money( IngotAux.ChemicalWeight * Rate / 100 );
      end; 
    end; 
    RecFound = Next( Bunch );
  end;

  return BalCost;
end;

macro PrintLabel

  array 
    PeerNameArr;

  var                  
    Stat = true,
    RecFound,
    RubSum, 
    TotalRub,
    i = 1;

  TotalRub = 0;           
  ClearRecord( BagList );
  BagList.Branch = Bag.Branch;  
  BagList.Date = Bag.Date;
  BagList.ClientType = Bag.ClientType;
  BagList.ClientID = Bag.ClientID;
  BagList.Operation = Bag.Operation;
  BagList.Number = Bag.Number;
  RecFound = GetGE( BagList );
  while ( RecFound and Stat
    and ( BagList.Branch == Bag.Branch )  
    and ( BagList.Date == Bag.Date )
    and ( BagList.ClientType == Bag.ClientType )
    and ( BagList.ClientID == Bag.ClientID )
    and ( BagList.Operation == Bag.Operation )
    and ( BagList.Number == Bag.Number ) ) 
    if ( FindCashValue( BagList.ValueRef ) )
      if ( ( ( CashValue.TypeValue == TYPERES_CURRENCY ) and ( CashValue.CodIntValue != 0 ) ) 
        or ( CashValue.TypeValue == TYPERES_PAYDOC ) )
        GetCurrCodeAndCBRate( BagList.ValueRef );
        RubSum = Cur2Rub( BagList.DeclaredSum );
      elif ( CashValue.TypeValue == TYPERES_MINCAPISSUE + CI_INGOT ) 
        RubSum = GetIngotBalCost;
      else
        if ( BagList.DeclaredSum != $0 )
          RubSum = BagList.DeclaredSum;
        else
          RubSum = $1 * BagList.DeclaredItems;
        end;
      end;       
      TotalRub = TotalRub + RubSum;    
    end;
    RecFound = Next( BagList );
  end;

  if ( not Stat )
    MsgBox( "Ошибка при печати ярлыка" );
    Exit( 1 );
  end;

  StrSplit( PeerName, PeerNameArr, 43, 37 );
  if ( PeerNameArr[1] == null )
    PeerNameArr[1] = "_";
  end;

  [-----------------------------------------------];
  [| #__________________________________________ |] ( SubStr( {Name_Bank}, 1, 43 ) );
  if( strlen( {Name_Bank} ) > 43 )
    [| #__________________________________________ |] ( SubStr( {Name_Bank}, 44, 43 ) );
  end;
  [| (наименование учреждения банка-отправителя) |];
  [|                                             |];
  [| Куда: #____________________________________ |] ( PeerNameArr[0] );
  while( PeerNameArr[i] != null )
    [| #__________________________________________ |] ( PeerNameArr[i] );
    i = i + 1;
  end;
  [| Условная рублевая оценка: #________________ |] ( TotalRub );
  [| Сумка № #_____________  Пломбиры № ________ |] ( BagNum.NumberBag );
  [| ______________________  Подписи  __________ |];
  [| ______________________           __________ |];
  [| (наимен. банка-отправителя)                 |];
  [| Дата: #                                     |] ( Bag.Date ); 
  [-----------------------------------------------];
end;

  if ( not GetTrue( true, "Печатать ярлык ?" ) )
    Exit( 1 );
  end;
  
/*  SetPrintPort( "УЗКИЙ" ); */

  GetClientInfo( Bag.ClientType, Bag.ClientID, PeerName );
  FindBagNum( Bag.Number );
  PrintLabel;
end;
  