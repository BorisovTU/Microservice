/* -@H------------------------------------------------------------------
*   System           : Вкладчики Сбербанка
*
*   File Name        : CarryCV.mac
*   Sourse Name      :
*
*
*   Description      : Макрос прихода или расхода любой кассовой ценности
*
*   Comment          : В первом приближении дает возможность прихода или расхода
*                    : произвольной кассовой ценности
*                    : Вызывается без параметров
*   Restrictions     : - Для ценных бланков
*                    : - Текущий операционист является кассиром
*                    : - Если количество > 0 - приход, иначе - расход
*                    : - Номер жетона,ссылка на первичный докумен, контролер
*                    :   не определены
*   Public routines  :
*   Base files       :
*   Programmer       : Зубов В.Ю.
*
*                 History
*   1.02.1998        : Создан
*
* ----------------------------------------------------------------@H- */
Import deprintr,DeskInter,InputRN;
file CashVal("sb_casvl.dbt") key 0;
record Parm(CashParm); /*Параметры для  InitCashDoc*/
/* Значение параметров

TypeValue               INT         2 Тип ценности
1 - Деньги (из справочника валют)
2 - Ценные бланки
3 - Др. ценности (из справочника ценностей)
>500 -Ценные бумаги

CodIntValue             INT         2 Целочисленный код ценности
CodCharValue            STRING     13 Строчный код ценности
CashOper                STRING     13 Субъект кассы (строка)
Oper                    INT         2 Субъект кассы (число)
CashDateDoc             DATE        4 Дата документа
NumOper                 INT         2 Номер операции
  1 - Подкрепление кассира
  2 - Возврат средств в кладовую
  3 - Инкассация
  4 - Аванс
  5 - Расход
  6 - Приход
  7 - Внутреннее подкрепление
  8 - Перевод на другой ресурс той же группы

ValueMoney              LDMON      10 Денежная сумма документа
ValueCol                LONG        4 Количество экземпляров
MinRegistrNumber        STRING     16 Нижний рег/ номер ценности
MaxRegistrNumber        STRING     16 Верхний рег/ номер ценности
NumbDocumCash           INT         2 Номер кассового жетона
ApplicationKind         INT         2 Ссылка на первичный документ
ApplicationKey          STRING     30 Ссылка на первичный документ
CashPatern              STRING     13 Контролер|Получатель (строка)
OperPatern              INT         2 Контролер|Получатель (число)
*/

Const
/* Для типа ценностей*/
 TYPERES_CURRENCY  = 1, /* Деньги (из справочника валют)           */
 TYPERES_VALFORM   = 2, /* Ценные бланки                           */
 TYPERES_VALUABLES = 3, /* Др. ценности (из справочника ценностей) */
 TYPERES_MINCAPISSUE = 500, /* Минимально возможный код ЦБ         */

/* Для кода операции*/
  CASHOP_REINFORCE = 1,  /* Подкрепление кассира */
  CASHOP_RETURN    = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT   = 3,  /* Инкассация */
  CASHOP_ADVANCE   = 4,  /* Аванс */
  CASHOP_DEBIT     = 5,  /* Расход */
  CASHOP_CREDIT    = 6,  /* Приход */
  CASHOP_REMOVE    = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1  = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE   = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1 = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE   = 11,  /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1 = 12,  /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_PANT_RI   = 20, /* Подкрепление филиала */
  CASHOP_PANT_RET  = 21, /* Возврат из филиала */
  CASHOP_PANT_RIO  = 22, /* Подкрепление оперчасти */
  CASHOP_PANT_RETO = 23, /* Возврат из оперчасти */
  CASHOP_PANT_COL  = 24, /* Инкассация */
  CASHOP_PANT_ADV  = 25, /* Аванс */
  CASHOP_PANT_RDEB = 30, /* Расход по кассе пересчета */
  CASHOP_PANT_RCRD = 31, /* Приход по кассе пересчета */
  CASHOP_PANT_RDRT = 32, /* Сдача ресурса в кладовую из Кассы пересчета */

 SB_EXTCASS       = 150, /* ApplicationKind */

 M_OUT            = 0,  /* Списание */
 M_IN             = 1,  /* Зачисление */

 RESMESUNIT_POINT = 1,  /* Поштучно  */
 RESMESUNIT_MONEY = 2   /* Стоимость */
 ;/* Конец констант*/

Var
    Sum,
    {curdate},
    {oper},
    stat = 0
 ;/* Конец переменных */

array MenuChoices;

MenuChoices(0)="Списание";
MenuChoices(1)="Зачисление";

macro DirectCarryCashVal(AccAddr);

record CashAcc("sb_casac.dbt");

var
  Choice;

SetBuff(CashAcc,AccAddr);
Choice=Menu(MenuChoices,"Выберите операцию","Операции");
if(ValType(Choice)==V_UNDEF)
  Exit(0);
end;
CashVal.RefValue=CashAcc.RefValue;
if(not GetEQ(CashVal))
  MsgBox("Ценность не найдена");
  Exit(-1);
end;
ClearRecord(Parm);
if(CashVal.UnitMeas==RESMESUNIT_POINT)
  GetInt(Sum,"Введите количество:",5);
  if(ValType(Sum)==V_UNDEF)
    Exit(0);
  end;
  Parm.ValueCol = Sum;
else
  GetMoney(Sum,"Введите сумму:");
  if(ValType(Sum)==V_UNDEF)
    Exit(0);
  end;
  Parm.ValueMoney = Sum;
end;
if(CashVal.HasRegNumber)
  if(not InputRegNum(
    Parm.Oper,
    Parm.Oper,
    CashVal.TypeValue,
    CashVal.CodIntValue,
    "Введите нижний рег. номер:",
    Parm.Series, Parm.MinRegistrNumber))
    Exit;
  end;
end;
GetString(Parm.Ground, "Введите основание документа", 37);
if(ValType(Parm.Ground) != V_STRING)
  Parm.Ground = "";
end;
Parm.Type=0;
Parm.TypeValue=CashVal.TypeValue;
Parm.CodIntValue=CashVal.CodIntValue;
if(CashAcc.IsSystem != StrFor(0))
  Parm.Oper=Parm.OperPatern=String({oper});
else
  Parm.Oper=Parm.OperPatern=CashAcc.CashOper;
end;
Parm.CashDateDoc={curdate};
if(Choice==M_IN)
  Parm.NumOper  = CASHOP_CREDIT;
else
  Parm.NumOper  = CASHOP_DEBIT;
end; /* IF */

/*
Parm.NumbDocumCash   = 0;
*/
Parm.ApplicationKind = SB_EXTCASS;
Parm.ApplicationKey  = FormApplicationKey(SB_EXTCASS);/*SetAppKey;*/

stat=InitCashDoc(Parm);
if(stat)
  MsgBox("Операция прошла успешно");
end;/* IF */

end;