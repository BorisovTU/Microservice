/*
$Name:             desk_tot.mac
$Module:           RS-Retail.  Касса.
$Description:      Сводная справка о кассовых оборотах (для оперкассы).
*/

/*
**
**  Есть возможность настраивать вид справки. Смотри "ФЛАГИ". 
**  В расшифровке подписи "Заведующий кассой" берутся ФИО операциониста, выпускающего справку.
**  В расшифровке подписи "Главный бухгалтер" берутся ФИО из настроек 
**  "Сервис роз.услуг\Справочники\Параметры банка\Ф.И.О. гл.кассира"
**
*/

import DeskInter, CommonInter, Cur_Rate, sqlconv, svodrep, ingot;

private var CashDoc = TBFile( "sb_casdc.dbt", "r", 1 );
file CashVal( "sb_casvl.dbt" ) key 0;
private file CI_Misc  (ci_misc                ) key 0;
private file paydoc   (paydoc,  "exchange.def") key 0;

private const
  CK_NEW          = 0, 
  CK_OLD          = 1;

const
  M_BRIGADE = 0,
  M_OPER = 1,

  TYPERES_CURRENCY = 1, 
  TYPERES_VALFORM = 2, 
  TYPERES_PAYDOC = 4,
  TYPERES_MON = 504,
  TYPERES_DRAGMET = 505,

  
  TYPE_RES_PAY    = 0, /* Платежеспособные ресурсы                     */
  TYPE_RES_NOPAY  = 1, /* Неплатежеспособные ресурсы                   */
  TYPE_RES_DOUBT  = 2, /* Сомнительные ресурсы                         */
  TYPE_RES_SAVE   = 3, /* На хранении                                  */
  TYPE_RES_EXP    = 4, /* На экспертизу                                */
  TYPE_RES_INCASS = 5, /* На инкассо                                   */
  TYPE_RES_PDNAT  = 20,/* ПД, оплаченные рублями(национальной валютой) */
  TYPE_RES_PDCUR  = 21,/* ПД, оплаченные валютой                       */
  TYPE_RES_PD     = 22,/* оплаченные ПД                                */

  CASHOP_REINFORCE = 1,  /* Подкрепление кассира */
  CASHOP_RETURN    = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT   = 3,  /* Инкассация */
  CASHOP_ADVANCE   = 4,  /* Аванс */
  CASHOP_DEBIT     = 5,  /* Расход */
  CASHOP_CREDIT    = 6,  /* Приход */
  CASHOP_REMOVE    = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1  = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE   = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1 = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE   = 11,  /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1 = 12,  /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_DEBET_SAFE = 13,  /* Расход из сейфа */
  CASHOP_CREDIT_SAFE = 14, /* Приход в сейф */

  SB_EXCHNDOC    =  260, 
 
  TYPEDOC_CARRYON = 2;

const
  FLAGDEBCRED_IN   = 1,  // Кредит
  FLAGDEBCRED_OUT  = 2;  // Дебет

var
  {curdate}, 
  {oper}, 
  {BatchMode}, 
  {FIO_Book},
  {FIO_Boss},
  Branch = NumFNCash, 
  Brigade = GetOperBrigade;

var
  CBRate, 
  ScaleRate;

var
  RepMode = M_OPER,
  RubCred = $0, RubDeb = $0, 
  CredOrdCount = 0, DebOrdCount = 0,
  RubCurrCod = 0;

var
  objPrint = TSvodReport;  /* Для хранения данных и печати отчета */

array
  MenuItems;

MenuItems[0] = "По операциям смены";
MenuItems[1] = "По операциям операциониста";


macro FindCurrExt( Code )

  KeyNum     (Currency, 1);
  ClearRecord(Currency   );
  Currency.rec.ExternalCode = Code;
  if ( not Currency.GetEQ() )
    MsgBox( "Не найдена валюта #" + Code );
    Exit( 1 );
  end;
end; /*FindCurrExt*/

macro FindCashVal( Ref )

  CashVal.RefValue = Ref;
  if ( not GetEQ( CashVal ) )
    MsgBox( "Не найден ресурс #" + Ref );
    Exit( 1 );
  end;
end;

macro GetRate

  ScaleRate = 1;
  if ( Currency.rec.ScaleOfc != 0 )
    ScaleRate = Currency.rec.ScaleOfc;
  end;

  GetAllCurrRate( {curdate}, Currency.rec.Code_Currency, CBRate, null, null );
  CBRate = CBRate * ScaleRate;
end;

macro Cur2Rub(Sum)

  return SumConv( Sum, ( CBRate / ScaleRate ) );
end;

macro IsDebet

  if ( ( CashDoc.rec.NumOper == CASHOP_DEBIT ) 
    or ( CashDoc.rec.NumOper == CASHOP_DEBET_SAFE ) 
    or ( CashDoc.rec.NumOper == CASHOP_COLLECT ) 
    or ( CashDoc.rec.NumOper == CASHOP_REMOVE_SAFE ) )
    return true;
  end;
  return false;
end;

macro UpdateOrderCounters
/*      
  if ( CashDoc.rec.ApplicationKind != SB_EXCHNDOC )
    if ( IsDebet )
      DebOrdCount = DebOrdCount + 1;   
    else
      CredOrdCount = CredOrdCount + 1;
    end; 
  end;
*/
  if ( IsDebet )
    DebOrdCount = DebOrdCount + 1;   
  else
    CredOrdCount = CredOrdCount + 1;
  end; 
end;

// =========================================================================================================================
//    ВОО: количество документов в связке кассового - для определения: наличный (2 документа) - безналичный (1 документ).
// =========================================================================================================================
private macro AmountExchangeDocs( _ApplKey )
  var cmdD, rsD;
  var ret = -1;
  cmdD = RsdCommand( "select count(*) as amn " +
                     "from dsb_casln_dbt ll " +
                     "where LL.T_APPLICATIONKIND2=260 and " +
                           "LL.T_REFVALUE2<>0 and " +
                           "LL.T_APPLICATIONKEY1=(select L.T_APPLICATIONKEY1 from dsb_casln_dbt l where L.T_APPLICATIONKEY2=?)" );
  cmdD.addParam( "key",  RSDBP_IN );
  cmdD.value( "key" )  = _ApplKey;
  cmdD.execute;
  rsD = RsdRecordset( cmdD );
  if ( rsD.moveNext )
    ret = Int( rsD.value( "amn" ) );
  end;
  return ret;
end;

macro ProcessDoc

  var
    Sum, Sum2Rub, IngotDoc, vAmount = 1;

  if ( CashDoc.rec.Brigade != Brigade )
    return;
  end;

  if (  ( CashDoc.rec.NumOper != CASHOP_CREDIT ) 
    and ( CashDoc.rec.NumOper != CASHOP_DEBIT ) 
    and ( CashDoc.rec.NumOper != CASHOP_CREDIT_SAFE )
    and ( CashDoc.rec.NumOper != CASHOP_DEBET_SAFE ) 
    and ( CashDoc.rec.NumOper != CASHOP_ADVANCE ) 
    and ( CashDoc.rec.NumOper != CASHOP_COLLECT ) 
    and ( CashDoc.rec.NumOper != CASHOP_REMOVE_SAFE ) 
    and ( CashDoc.rec.NumOper != CASHOP_REMOVE_SAFE_1 ) ) 
    return;
  end;

  FindCashVal( CashDoc.rec.RefValue );

  if ( ( CashVal.TypeValue == TYPERES_CURRENCY )  AND 
       ( CashVal.Type == TYPE_RES_PAY ) )  // Только платежеспособные.

    FindCurr( CashVal.CodIntValue ); 
    GetRate;
    Sum2Rub = Cur2Rub( CashDoc.rec.ValueMoney );
    Sum = CashDoc.rec.ValueMoney;
    if( CashVal.Type != TYPE_RES_NOPAY ) 
      if( IsDebet )
        if ( CashDoc.rec.ApplicationKind == SB_EXCHNDOC )
          if ( AmountExchangeDocs( CashDoc.rec.ApplicationKey ) > 1 )
            vAmount = 0;
          else
            vAmount = 1;
          end;
        else
          vAmount = 1;
        end;
        objPrint.addParamForCurrency(Currency.rec.Code_Currency, 0, 0, 0, vAmount, Sum, Sum2Rub);
      else
        objPrint.addParamForCurrency(Currency.rec.Code_Currency, 1, Sum, Sum2Rub, 0, 0, 0);
      end;
    else
      if( IsDebet )
        if ( CashDoc.rec.ApplicationKind == SB_EXCHNDOC )
          if ( AmountExchangeDocs( CashDoc.rec.ApplicationKey ) > 1 )
            vAmount = 0;
          else
            vAmount = 1;
          end;
        else
          vAmount = 1;
        end;
        objPrint.addParamForBadCurrency(Currency.rec.Code_Currency, 0, 0, 0, vAmount, Sum, Sum2Rub);
      else
        objPrint.addParamForBadCurrency(Currency.rec.Code_Currency, 1, Sum, Sum2Rub, 0, 0, 0);
      end;
    end;
  elif( CashVal.TypeValue == TYPERES_PAYDOC ) 
    paydoc.ID = CashVal.CodIntValue;
    if( GetEQ(paydoc) )
      FindCurr( paydoc.Curr_Ref ); 
      GetRate;
      Sum2Rub = Cur2Rub( CashDoc.rec.ValueMoney );
      if( IsDebet )
        objPrint.addParamForPD(CashVal.RefValue,0, 0, 0, CashDoc.rec.ValueMoney, Sum2Rub, 1);
      else
        objPrint.addParamForPD(CashVal.RefValue,CashDoc.rec.ValueMoney, Sum2Rub, 1, 0, 0, 0);
      end;
    end;
  elif( CashVal.TypeValue == TYPERES_MON )
    CI_Misc.Type    = 4;
    CI_Misc.IssueID = CashVal.CodIntValue;
    if( GetEQ(CI_Misc) )
      if(  CI_Misc.KindOfCoin == CK_NEW )
        if ( IsDebet )
          objPrint.addParamForNewMon( 0, 0, 0, CashDoc.rec.ValueMoney);
        else
          objPrint.addParamForNewMon( 0, CashDoc.rec.ValueMoney, 0, 0);
        end;
      else
        if ( IsDebet )
          objPrint.addParamForOldMon( 0, 0, 0, CashDoc.rec.ValueMoney);
        else
          objPrint.addParamForOldMon( 0, CashDoc.rec.ValueMoney, 0, 0);
        end;
      end; 
    end; 
  elif( CashVal.TypeValue == TYPERES_DRAGMET )

    IngotDoc = TIngotCashDoc( CashDoc.rec.ApplicationKind, CashDoc.rec.ApplicationKey, CashDoc.rec.RefValue );
    if ( IsDebet )
      objPrint.addParamForDragMet( 0, 0, CashDoc.rec.ValueCol, IngotDoc.GetRubValue( {curdate} ));
    else
      objPrint.addParamForDragMet( CashDoc.rec.ValueCol, IngotDoc.GetRubValue( {curdate} ), 0, 0);
    end;

  end; 

end;  

// =====================================================
//    Обертка для ProcessDoc() - одиночные документы.
// =====================================================
private macro ProcessDoc_Cash( pApplKind, pApplKey, pSubj )
  CashDoc.Clear();
  CashDoc.rec.ApplicationKind = pApplKind;
  CashDoc.rec.ApplicationKey  = pApplKey;
  if ( CashDoc.GetEQ() )
    if ( ( StrLen( pSubj ) == 0 )  OR  ( CashDoc.rec.CashOper == pSubj ) )
      ProcessDoc();
    end;
  end;
end;

// =====================================================
//    Кассовые документы, учитываемые по отдельности.  
// =====================================================
private macro ProcessDocs_Single( pSubj )
  var cmdD, rsD;

  cmdD = RsdCommand( "select CD.T_APPLICATIONKIND as rApplKind, " +
                            "CD.T_APPLICATIONKEY as rApplKey " +
                     "from dsb_casdc_dbt cd " +
                     "where CD.T_BRANCH = ? " +
                     "and CD.T_CASHDATEDOC = ? " +
                     "and CD.T_TYPEDOC = 2 " +
                     "and CD.T_BRIGADE = ? " +
                     "and CD.T_NUMOPER in (5,6,13,14,4,3,11,12) " +
                     "and CD.T_ACTION <> 2 " +
                     "MINUS " +  // Убираем ВОО с кодами 01 по 11
                     "select CD1.T_APPLICATIONKIND, CD1.T_APPLICATIONKEY " +
                     "from dsb_casdc_dbt cd1, dsb_casln_dbt cl, dreestr_2_dbt r " +
                     "where CD1.T_BRANCH = ? " +
                     "and CD1.T_CASHDATEDOC = ? " +
                     "and CD1.T_TYPEDOC = 2 " +
                     "and CD1.T_BRIGADE = ? " +
                     "and CD1.T_NUMOPER in (5,6,13,14,4,3,11,12) " +
                     "and CD1.T_ACTION <> 2 " +
                     "and CD1.T_APPLICATIONKIND = 260 " +  // SB_EXCHNDOC
                     "and CL.T_APPLICATIONKIND2 = CD1.T_APPLICATIONKIND " +
                     "and CL.T_APPLICATIONKEY2 = CD1.T_APPLICATIONKEY " +
                     "and CL.T_REFVALUE2 = CD1.T_REFVALUE " +
                     "and R.T_APPLICATIONKIND = CL.T_APPLICATIONKIND1 " +
                     "and R.T_APPLICATIONKEY = CL.T_APPLICATIONKEY1 " +
                     "and substr(r.T_CODE113I, 1, 2) >= '01' and substr(r.T_CODE113I, 1, 2) <= '11' " +
                     "MINUS " +  // Убираем прочие с включенным флагом "Сводный документ"
                     "select CD2.T_APPLICATIONKIND, CD2.T_APPLICATIONKEY " +
                     "from dsb_casdc_dbt cd2, dpay_doc_dbt pd, dpay_kind_dbt pk " +
                     "where CD2.T_BRANCH = ? " +
                     "and CD2.T_CASHDATEDOC = ? " +
                     "and CD2.T_TYPEDOC = 2 " +
                     "and CD2.T_BRIGADE = ? " +
                     "and CD2.T_NUMOPER in (5,6,13,14,4,3,11,12) " +
                     "and CD2.T_ACTION <> 2 " +
                     "and CD2.T_APPLICATIONKIND = 200 " + // SB_PAYDOC
                     "and PD.T_IAPPLICATIONKIND = CD2.T_APPLICATIONKIND " +
                     "and PD.T_APPLICATIONKEY = CD2.T_APPLICATIONKEY " +
                     "and PK.T_ISCUR = PD.T_ISCUR " +
                     "and PK.T_GROUPOPERT = PD.T_GROUPOPERT " +
                     "and PK.T_NUMOPERAT = PD.T_NUMOPERT " +
                     "and PK.T_FLAGREESTR <> chr(0)" );  // Флаг "Сводный документ"

  cmdD.addParam( "branch1",  RSDBP_IN );
  cmdD.addParam( "date1",    RSDBP_IN );
  cmdD.addParam( "brigade1", RSDBP_IN );
  cmdD.addParam( "branch2",  RSDBP_IN );
  cmdD.addParam( "date2",    RSDBP_IN );
  cmdD.addParam( "brigade2", RSDBP_IN );
  cmdD.addParam( "branch3",  RSDBP_IN );
  cmdD.addParam( "date3",    RSDBP_IN );
  cmdD.addParam( "brigade3", RSDBP_IN );

  cmdD.value( "branch1" )  = Branch;
  cmdD.value( "date1" )    = {curdate};
  cmdD.value( "brigade1" ) = Brigade;
  cmdD.value( "branch2" )  = Branch;
  cmdD.value( "date2" )    = {curdate};
  cmdD.value( "brigade2" ) = Brigade;
  cmdD.value( "branch3" )  = Branch;
  cmdD.value( "date3" )    = {curdate};
  cmdD.value( "brigade3" ) = Brigade;

  cmdD.execute;
  rsD = RsdRecordset( cmdD );
  while ( rsD.moveNext() )
    ProcessDoc_Cash( rsD.value( "rApplKind" ), rsD.value( "rApplKey" ), pSubj );
  end;
end;

// =====================================================
//     Обертка для ProcessDoc() - сводные документы.
// =====================================================
private macro ProcessDoc_CashCurrency( pApplKind, pApplKey, pSumIn, pSumOut, pSubj )
  CashDoc.Clear();
  CashDoc.rec.ApplicationKind = pApplKind;
  CashDoc.rec.ApplicationKey  = pApplKey;
  if ( CashDoc.GetEQ() )
    if ( ( StrLen( pSubj ) == 0 )  OR  ( CashDoc.rec.CashOper == pSubj ) )
      ProcessDoc();
    end;
  end;
end;

// ==========================================
//      Парный к ВОО кассовый документ.
// ==========================================
private macro CashDoc_Currency( pApplKind, pApplKey, pSumIn, pSumOut, pSubj )
  var cmdD, rsD;
  cmdD = RsdCommand( "select doc.T_APPLICATIONKIND as rApplKind, " +
                            "doc.T_APPLICATIONKEY as rApplKey " +
                     "from dsb_casln_dbt c, dsb_casln_dbt d, dsb_casdc_dbt doc " +
                     "where c.T_APPLICATIONKIND2 = ? and " +
                           "c.T_APPLICATIONKEY2 = ? and " +
                           // "c.T_REFVALUE2 = 0 and " +
                           "d.T_APPLICATIONKIND1 = c.T_APPLICATIONKIND1 and " +
                           "d.T_APPLICATIONKEY1 = c.T_APPLICATIONKEY1 and " +
                           // "d.T_REFVALUE2 = 0 and " +
                           "d.T_APPLICATIONKIND2 = 260 and " +
                           "doc.T_APPLICATIONKIND = d.T_APPLICATIONKIND2 and " +
                           "doc.T_APPLICATIONKEY = d.T_APPLICATIONKEY2" );
  cmdD.addParam( "kind", RSDBP_IN );
  cmdD.addParam( "key",  RSDBP_IN );
  cmdD.value( "kind" ) = pApplKind;
  cmdD.value( "key" )  = pApplKey;
  cmdD.execute;
  rsD = RsdRecordset( cmdD );
  while ( rsD.moveNext )
    ProcessDoc_CashCurrency( rsD.value( "rApplKind" ), rsD.value( "rApplKey" ), 
                             pSumIn, pSumOut, pSubj );
  end;
end;

// ======================================================
//    Своды по наличным ВОО с кодом 136-И  от 01 до 11.
// ======================================================
private macro ProcessDocs_Currency( pSubj )
  var cmdD, rsD;

  cmdD = RsdCommand( "select min(e.T_APPLICATIONKIND) as rApplKind, " +
                            "min(e.T_APPLICATIONKEY) as rApplKey, " +
                            "sum(e.T_INCOMEAMOUNT) as rSumIn, " +
                            "sum(e.T_OUTAMOUNT) as rSumOut " +
                     "from dexoperat_dbt e, dreestr_2_dbt r " +
                     "where r.T_BRANCH = ? " +
                     "and r.T_curDATE = ? " +
                     "and substr(r.T_CODE113I, 1, 2) >= '01' " + 
                     "and substr(r.T_CODE113I, 1, 2) <= '11' " +
                     "and e.T_APPLICATIONKIND = 260 " +
                     "and e.T_APPLICATIONKIND = r.T_APPLICATIONKIND " +
                     "and e.T_APPLICATIONKEY = r.T_APPLICATIONKEY " +
                     "and e.T_ACTION <> 2 " +
                     "and e.T_BRIGADE = ? " +
                     "group by r.T_OPER, r.T_DATE, r.T_CODE113I, e.T_INCOMEREFVAL, e.T_OUTREFVAL " +
                     "order by r.T_DATE, r.T_CODE113I" );

  cmdD.addParam( "branch",  RSDBP_IN );
  cmdD.addParam( "date",    RSDBP_IN );
  cmdD.addParam( "brigade", RSDBP_IN );
  cmdD.value( "branch" )  = Branch;
  cmdD.value( "date" )    = {curdate};
  cmdD.value( "brigade" ) = Brigade;

  cmdD.execute;
  rsD = RsdRecordset( cmdD );
  while ( rsD.moveNext() )
    CashDoc_Currency( rsD.value( "rApplKind" ), rsD.value( "rApplKey" ),
                      rsD.value( "rSumIn" ), rsD.value( "rSumOut" ), 
                      pSubj );
  end;
end;

// =====================================================
//     Обертка для ProcessDoc() - сводные документы.
// =====================================================
private macro ProcessDoc_CashSvod( pApplKind, pApplKey, pSum, pSubj )
  CashDoc.Clear();
  CashDoc.rec.ApplicationKind = pApplKind;
  CashDoc.rec.ApplicationKey  = pApplKey;
  if ( CashDoc.GetEQ() )
    if ( ( StrLen( pSubj ) == 0 )  OR  ( CashDoc.rec.CashOper == pSubj ) )
      CashDoc.rec.ValueMoney = pSum;
      ProcessDoc();
    end;
  end;
end;

// ==================================================================
//    Прочие приходные/расходные операции, объединенные в сводный.
// ==================================================================
private macro ProcessDocs_Other( pSubj )
  var cmdD, rsD;

  cmdD = RsdCommand( "select sum(PD.T_INSUM) as rSum, " +
                            "min(CD2.T_APPLICATIONKIND) as rApplKind, " +
                            "min(CD2.T_APPLICATIONKEY) as rApplKey " +
                     "from dsb_casdc_dbt cd2, dpay_doc_dbt pd, dpay_kind_dbt pk " +
                     "where CD2.T_BRANCH = ? " +
                     "and CD2.T_CASHDATEDOC = ? " +
                     "and CD2.T_TYPEDOC = 2 " +
                     "and CD2.T_BRIGADE = ? " +
                     "and CD2.T_NUMOPER in (5,6,13,14,4,3,11,12) " +
                     "and CD2.T_ACTION <> 2 " +
                     "and cd2.T_VALUEMONEY <> 0 " +
                     "and CD2.T_APPLICATIONKIND = 200 " + // SB_PAYDOC
                     "and PD.T_IAPPLICATIONKIND = CD2.T_APPLICATIONKIND " +
                     "and PD.T_APPLICATIONKEY = CD2.T_APPLICATIONKEY " +
                     "and PK.T_ISCUR = PD.T_ISCUR " +
                     "and PK.T_GROUPOPERT = PD.T_GROUPOPERT " +
                     "and PK.T_NUMOPERAT = PD.T_NUMOPERT " +
                     "and PK.T_FLAGREESTR <> chr(0) " + // Флаг "Сводный документ"
                     "group by PD.T_CODCASHIER, PD.T_GROUPOPERT, PD.T_CODCUR, " +
                              "PD.T_NUMOPERT, PD.T_APPLTYPE" );

  cmdD.addParam( "branch",  RSDBP_IN );
  cmdD.addParam( "date",    RSDBP_IN );
  cmdD.addParam( "brigade", RSDBP_IN );
  cmdD.value( "branch" )  = Branch;
  cmdD.value( "date" )    = {curdate};
  cmdD.value( "brigade" ) = Brigade;

  cmdD.execute;
  rsD = RsdRecordset( cmdD );
  while ( rsD.moveNext() )    
    ProcessDoc_CashSvod( rsD.value( "rApplKind" ), 
                         rsD.value( "rApplKey" ), 
                         rsD.value( "rSum" ),
                         pSubj );
  end;
end;

macro ProcessDocsForOper

  var
    Oper4, 
    Brigade2,  
    Subj, 
    RecFound;

  Oper4 = String( {oper} );
  while ( StrLen( Oper4 ) < 4 )
    Oper4 = "0" + Oper4;
  end;

  Brigade2 = String( Brigade );
  if ( Brigade < 10 )
    Brigade2 = "0" + Brigade2;
  end;

  Subj = Oper4 + "/" + Brigade2;

  ProcessDocs_Single( Subj );
  ProcessDocs_Currency( Subj );
  ProcessDocs_Other( Subj );

end;

macro ProcessDocs
  ProcessDocs_Single( "" );
  ProcessDocs_Currency( "" );
  ProcessDocs_Other( "" );
end;

  if ( {BatchMode} )
    RepMode = M_BRIGADE;
  else
    RepMode = Menu( MenuItems, "Выпустить справку:" );
  end;

  if ( RepMode == M_BRIGADE )
    ProcessDocs;
  else
    ProcessDocsForOper;
  end;

  objPrint.PrintHead();
  objPrint.PrintReport( true );
  objPrint.PrintCellar();
end;

