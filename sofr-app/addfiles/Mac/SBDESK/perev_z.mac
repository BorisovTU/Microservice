/**************************************************************************\
*  ---------------                                                         *
*  R-Style Softlab                                                         *
*  ---------------                                                         *
*  RS-Retail                                                               *
*                                                                          *
*  Оперкасса                                                               *
*                                                                          *
*  Реестр учета переводов без открытия счета                               *
*                                                                          *
*                                                                          *
*  Гробов А.А.                                                             *
*  31.07.2003                                                              *
*  Ярмольчук А.Ю. (переработал под 1433-У)
   30.06.2004                                                                     *
   Макрос необходимо включить в модуль "Оперкасса" меню "Отчеты\"
   пункт меню "Реестр переводов без откр. счета"
   В реестр попадают документы по коммунальным платежам и прочим приходным
   операциям у которых в настройках установлен флаг "Формирование реестра"
   Реестр выпускается за выбранный день.
   Ярмольчук А.Ю. Отчет выпускаем по одному кассиру либо по всем.
\**************************************************************************/

import DeprIntr, issrvdoc;
import PmPropUtil;
array ANumberPayOper; /* Массив номеров прочих приходных пераций, */
                      /*которые должны быть отражены вместе с комуналкой*/
                      /* Заполняется пользователем */

file pay_doc  (pay_doc );         /* Документы платежей           */
file pay_doc_PmntProp ("RTPaymentProp.dbt") key 0;
file pay_doc1 (pay_doc ) key 2;         /* Документы платежей           */
file currency (currency);
file pay_kind (pay_kind);
file sbcasdc  (sb_casdc) key 1;
file sbcasln  (sb_casln);
file cashval  (sb_casvl) key 1;
file Group("rtgroup.dbt") key 1;

file pay_doc_cp ("pay_doc_cp.dbt") key 0;

const FNcash  = NumFNcash();

const GroupOpert_COMM      = 1;   /* Коммунальные платежи */
const GroupOpert_PEREVOD   = 11;  /* Переводы */
const GroupOpert_COMISSION = 21;  /* Комиссия */


private var {curdate}, {oper}, {NAME_BANK}, {FIO_oper};

var DateReport = {curdate}, Choice_oper;
var ObjectTp;
var Operc;
var variant;
array men;

var i_rub = 0, i_val = 0;
var j_rub = 0, j_val = 0;
var   ITOGO_RUB = $0;
var   ITOGO_RUB_COMIS = $0;
array ITOGO_VAL;
array ITOGO_VAL_COMIS;


/******************************************************************************/
class TReestrTranslateCommercial

private macro date_monat(number)

  var monat;

   if(number == 1)
      monat = "января";
   elif(number == 2)
      monat = "февраля";
   elif(number == 3)
      monat = "марта";
   elif(number == 4)
      monat = "апреля";
   elif(number == 5)
      monat = "мая";
   elif(number == 6)
      monat = "июня";
   elif(number == 7)
      monat = "июля";
   elif(number == 8)
      monat = "августа";
   elif(number == 9)
      monat = "сентября";
   elif(number == 10)
      monat = "октября";
   elif(number == 11)
      monat = "ноября";
   elif(number == 12)
      monat = "декабря";
   end;

 return monat;
end;

private macro Chk_value_in_array (user_array, chk_value)
  Var user_array_size,counter;

  counter=0;
  user_array_size=asize(user_array);
  while (counter<user_array_size)
     if (user_array(counter)==chk_value)
        return True;
     end;
     counter=counter+1;
  end;
  return False;

end;

private macro BranchName

  var party = TRecHandler( "i_party.rec" );

  if ( findDepartment(NumFNCash, null, null, party) )
    return party.rec.Name;
  else
    return "";
  end;
end;



/**************************************************************************\
|                         Печать заголовка отчета(рубли)                   |
\**************************************************************************/
private macro HeadReport_rub ()

  var D1,D2,D3;
  var monat;

   DateSplit(DateReport,D1,D2,D3);
   monat = date_monat(D2);

  [      ######################################################################### ]
  ({NAME_BANK}:w);
  [

         #####################################################


                                           РЕЕСТР
                                   учета рублевых переводов
                           совершаемых без открытия текущего счета
                                  за "##" ######## #### года

        ┌─────────────────────────────────────┬──────────────────┬───────────────────┐
        │      Наименование операции          │ Cумма перевода   │   Сумма комиссии  │
        │                                     │без учета комиссии│                   │
  ] ( BranchName, D1, monat:c, D3 );
end;

/**************************************************************************\
|                         Печать заголовка отчета(валюта)                  |
\**************************************************************************/
private macro HeadReport_val ()

  var D1,D2,D3;
  var monat;

   DateSplit(DateReport,D1,D2,D3);
   monat = date_monat(D2);

  [     #########################################################################                ]
  ({NAME_BANK}:w);
  [
        #####################################################


                                           РЕЕСТР
                              учета переводов иностранной валюты,
                            совершаемых без открытия текущего счета
                                   за "##" ######## #### года

        ┌─────────────────────────────────────┬──────┬──────────────────┬───────────────────┐
        │   Наименование операции             │ Код  │ Сумма перевода   │   Сумма комиссии  │
        │                                     │валюты│без учета комиссии│                   │
        │                                     │      │                  │                   │
  ] ( BranchName, D1, monat:c, D3 );
end;


/* Возвращает True если для вида операции
   установлен признак "Формирование реестра" */
private macro FormReestr (IsCur, Groupopert, numoperat)
    rewind (pay_kind);
    clearrecord (pay_kind);
    pay_kind.IsCur=IsCur;
    pay_kind.Groupopert=Groupopert;
    pay_kind.numoperat=numoperat;
    if (GetEQ(pay_kind) and (pay_kind.FlagReestr=="X"))
       return true;
    else
       return false;
    end;
end;

/* Возвращает True если кассовый документ подтвержден.
   False -если не подтвержден или отсутствует */
private macro Check_casdc_type (Akind, Akey)
   rewind (sbcasdc);
   clearrecord (sbcasdc);
   sbcasdc.ApplicationKind=Akind;
   sbcasdc.ApplicationKey=Akey;
   if (GetEQ(sbcasdc))
      /* Проверяем, что документ подтвержден */
      if (sbcasdc.typedoc==2)
         return true;
      else
         return false;
      end;
   else
      return false;
   end;
end;

 /* Возвращает код ресурса по внутреннему коду валюты */
 private macro GetRefValue(Type, IntCode)

   rewind (cashval);
   clearrecord (cashval);
   cashval.TypeValue = Type;
   cashval.CodIntValue = IntCode;
   if(GetEQ(cashval))
     return cashval.RefValue;
   else
     return -1;
   end;
 end;


  /* Возвращает запись документа коммиссии */
  private macro Get_comission_Rec (Groupopert, CodCur, Akind, Akey)

     var stat, rec = null;


      /* Находим документ комиссии */
      rewind (sbcasln);
      clearrecord (sbcasln);
      sbcasln.ApplicationKind1 = Akind;
      sbcasln.ApplicationKey1  = Akey;
      sbcasln.NumLinkDoc       = 0;
      if (GetGE(sbcasln))
         stat=true;
         while (
                stat and (sbcasln.ApplicationKind1==Akind) and
                         (sbcasln.ApplicationKey1 ==Akey)
                )
                  if( sbcasln.ApplicationKey1!=sbcasln.ApplicationKey2 )
                        rewind (pay_doc1);
                        clearrecord (pay_doc1);
                        pay_doc1.iApplicationKind=sbcasln.ApplicationKind2;
                        pay_doc1.ApplicationKey=sbcasln.ApplicationKey2;
                        if (GetEQ(pay_doc1))
                          if (pay_doc1.GroupOpert==GroupOpert_COMISSION)
                            rec = pay_doc1;
                            stat=false;
                          end;
                        end;

                  end;
            stat=next (sbcasln);
         end;
      end;
      return rec;
  end;

  /*Возращает сумму документа комисии */
  private macro Get_comission_sum (Groupopert, CodCur, Akind, Akey)
    var rec = null, sum_comm=0;

    rec = Get_comission_Rec(Groupopert, CodCur, Akind, Akey);

    if( rec != null )
      sum_comm = rec.insum;
    end;

    return sum_comm;
  end;


/* Возвращает наименование операции */
private macro GetNameOperation (IsCur, Groupopert, numoperat, flagStorn)

   var str="";

    rewind (pay_kind);
    clearrecord (pay_kind);
    pay_kind.IsCur=IsCur;
    pay_kind.Groupopert=Groupopert;
    pay_kind.numoperat=numoperat;
    if (GetEQ(pay_kind))
       if (Groupopert==GroupOpert_COMM)
          str="Коммунальные платежи ("+pay_kind.SzNameAlg+")";
       else
          str=pay_kind.SzNameAlg;
       end;
    else
       if (Groupopert==GroupOpert_COMM)
          str= "Коммунальные платежи (без договора)";
       end;
    end;
    if (flagStorn and (pay_doc.Action == 11))
      str="Сторнирование. "+str;
    end;
    return str;
end;

/**************************************************************************\
\**************************************************************************/

private macro curren(code)

  var stat;
  var codecuren;

  KeyNum     (currency, 0);
  ClearRecord(currency   );
  Rewind     (currency   );
  currency.Code_Currency = code;

  if(getEQ(currency))
     codecuren = currency.ExternalCode;
  else
     codecuren = "  ";
  end;

  return codecuren;
end;

/**************************************************************************\
|                         Печать отчета(рубли)                             |
\**************************************************************************/
private macro Report_rub ()

   var stat, Summ_commis, Summ_without_commis;

    /* Повторяющийся фрагмент выносим в отдельную процедурку */
    macro mini_rep

       if (pay_doc.Groupopert==GroupOpert_COMM)
           pay_doc_cp.ApplKind = pay_doc.iApplicationKind;
           pay_doc_cp.ApplKey = pay_doc.ApplicationKey;
           if (GetEQ(pay_doc_cp))
               Summ_commis=pay_doc.insum-pay_doc_cp.PaySum;
               Summ_without_commis=pay_doc_cp.PaySum;
           end;
       elif (pay_doc.Groupopert==GroupOpert_PEREVOD)
           Summ_commis=Get_comission_sum (pay_doc.Groupopert, pay_doc.CodCur, pay_doc.iApplicationKind, pay_doc.ApplicationKey);
           Summ_without_commis=pay_doc.insum;
       end;

       [     ├─────────────────────────────────────┼──────────────────┼───────────────────┤
             │#################################### │##################│###################│]
          (GetNameOperation(pay_doc.IsCur, pay_doc.Groupopert, pay_doc.NumOpert, false), Summ_without_commis:r, Summ_commis:r);
       ITOGO_RUB = ITOGO_RUB + Summ_without_commis;
       ITOGO_RUB_COMIS=ITOGO_RUB_COMIS+Summ_commis;
       i_rub = i_rub + 1;

    end;


    KeyNum     (pay_doc, 7);
    Rewind     (pay_doc   );
    ClearRecord(pay_doc   );

    /* Документы отражаем в порядке их поступления */
    pay_doc.FNCash          = FNcash;
    pay_doc.Date_Document   = DateReport;
    pay_doc.KKMFactoryNum   = "";
    pay_doc.IsCur           = 0;
    pay_doc.CodCur          = 0;


    stat = getGE (pay_doc);

    while(stat AND (pay_doc.FNCash == FNcash)  AND
          (pay_doc.Date_Document == DateReport)
         )
       if( ( pay_doc.IsCur == 0 ) and ( pay_doc.CodCur == 0 ) and
           ( pay_doc.Action < 2 ) and
           ( ( pay_doc.IsSuspended == "" ) or ( pay_doc.IsSuspended == "X" ) ) and
           ( pay_doc.InSum > 0 )
         )
         /* Если отчет по одному кассиру */
         if (Choice_oper==1)
                if (
                    ((pay_doc.Groupopert==GroupOpert_COMM)) OR
                    ((pay_doc.Groupopert==GroupOpert_PEREVOD) and FormReestr(pay_doc.IsCur, pay_doc.Groupopert, pay_doc.NumOpert))
                   )
                     if ((pay_doc.CodCashier==OperC) and Check_casdc_type (pay_doc.iApplicationkind,pay_doc.Applicationkey))
                        mini_rep;
                     end;
                end;
         /* Если отчет по всем кассирам, то на кассира не проверяем */
         elif (Choice_oper==0) /* структуру макроса задаем с учетом того, что меню может быть наращено */
                if (
                    ((pay_doc.Groupopert==GroupOpert_COMM)) OR
                    ((pay_doc.Groupopert==GroupOpert_PEREVOD) and FormReestr(pay_doc.IsCur, pay_doc.Groupopert, pay_doc.NumOpert))
                   )
                     if (Check_casdc_type (pay_doc.iApplicationkind,pay_doc.Applicationkey))
                        mini_rep;
                     end;
                end;
         end;
       end;
       stat = next(pay_doc);
    end;
end;

/**************************************************************************\
|                         Печать отчета(валюта)                            |
\**************************************************************************/
private macro Report_val ()

   var stat, Summ_commis, Summ_without_commis;

    macro mini_rep

        Summ_commis=Get_comission_sum (pay_doc.Groupopert, pay_doc.CodCur, pay_doc.iApplicationKind, pay_doc.ApplicationKey);
        Summ_without_commis=pay_doc.insum;
        [     ├─────────────────────────────────────┼──────┼──────────────────┼───────────────────┤
              │#################################### │  ### │##################│###################│]
           (GetNameOperation(pay_doc.IsCur, pay_doc.Groupopert, pay_doc.NumOpert, false), curren(pay_doc.CodCur), Summ_without_commis:r, Summ_commis:r);
        if(valtype(ITOGO_VAL(pay_doc.CodCur)) == v_undef)
          ITOGO_VAL(pay_doc.CodCur) = $0;
          ITOGO_VAL_COMIS (pay_doc.CodCur)=$0;
        end;
        ITOGO_VAL(pay_doc.CodCur) = ITOGO_VAL(pay_doc.CodCur) + Summ_without_commis;
        ITOGO_VAL_COMIS (pay_doc.CodCur)=ITOGO_VAL_COMIS (pay_doc.CodCur)+Summ_commis;
        i_val = i_val + 1;

    end;

    KeyNum     (pay_doc, 7);
    Rewind     (pay_doc   );
    ClearRecord(pay_doc   );

/*    pay_doc.IsCur           = 1;
    pay_doc.FNCash          = FNcash;
    pay_doc.GroupOpert      = GroupOpert11;
    pay_doc.Date_Document   = DateReport;
    pay_doc.NumOpert        = 1;
    pay_doc.CodCur          = 0;
*/
    /* Документы отражаем в порядке их поступления */
/*
    pay_doc.FNCash          = FNcash;
    pay_doc.IsCur           = 1;
    pay_doc.CodCur          = 0;
    pay_doc.Date_Document   = DateReport;
    pay_doc.GroupOpert      = 0;
    pay_doc.NumOpert        = 0;
*/
    pay_doc.FNCash          = FNcash;
    pay_doc.Date_Document   = DateReport;
    pay_doc.KKMFactoryNum   = "";
    pay_doc.IsCur           = 0;
    pay_doc.CodCur          = 0;

    stat = getGE (pay_doc);

    while(stat AND (pay_doc.FNCash == FNcash) AND
          (pay_doc.Date_Document == DateReport)
         )
       if( ( pay_doc.IsCur == 1 ) and
           ( pay_doc.Action < 2 ) and
           ( ( pay_doc.IsSuspended == "" ) or ( pay_doc.IsSuspended == "X" ) ) and
           ( pay_doc.CodCur > 0 )
         )
         if (
             ((pay_doc.Groupopert==GroupOpert_PEREVOD) and FormReestr(pay_doc.IsCur, pay_doc.Groupopert, pay_doc.NumOpert))
            )
             /* Если отчет по одному кассиру */
             if (Choice_oper==1)
                 if ((pay_doc.CodCashier==OperC) and Check_casdc_type (pay_doc.iApplicationkind, pay_doc.Applicationkey))
                    mini_rep;
                 end;
             /* Если отчет по всем кассирам, то на кассира не проверяем */
             elif (Choice_oper==0) /* структуру макроса задаем с учетом того, что меню может быть наращено */
                 if (Check_casdc_type (pay_doc.iApplicationkind, pay_doc.Applicationkey))
                    mini_rep;
                 end;
             end;
         end;
       end;
       stat = next(pay_doc);
    end;

end;

/**************************************************************************\
|                         Печать окончания отчета                          |
\**************************************************************************/

private macro microend()

  var D1,D2,D3;
  var monat;

  DateSplit(DateReport,D1,D2,D3);
  monat = date_monat(D2);


   [
            Кассир __________________ (#########################################)
   ] ({FIO_oper});

end;

private macro EndReport (flag_cur)

 var ArraySize_VAL = asize(ITOGO_VAL);
 var numb = 0;

 if(flag_cur == 0)
  [     ├─────────────────────────────────────┼──────────────────┼───────────────────┤
        │ ИТОГО:                              │##################│###################│
        └─────────────────────────────────────┴──────────────────┴───────────────────┘](ITOGO_RUB, ITOGO_RUB_COMIS);
  microend();
 end;

 if(flag_cur == 1)

  while(numb != ArraySize_VAL)

   if(valtype(ITOGO_VAL(numb)) == v_money)

  [     ├─────────────────────────────────────┼──────┼──────────────────┼───────────────────┤
        │ ИТОГО:                              │  ### │##################│###################│](curren(numb),ITOGO_VAL(numb), ITOGO_VAL_COMIS(numb));

   end;

   numb = numb + 1;
  end;

  [     └─────────────────────────────────────┴──────┴──────────────────┴───────────────────┘];

  microend();
 end;

end;


private macro GetDateReport()

  var stat = False;

  stat = GetDate( DateReport, " Введите дату отчета ... " );

  if( stat )
   if(DateReport > {curdate})
      MsgBox( " Введенная дата больше текущего операционного дня !!! ");
      stat = False;
   end;
  end;

  return stat;
end;


/**************************************************************************\
|                         Главная процедура                                |
\**************************************************************************/

private macro Main()

  var StatRoute;              /* Управление процедурой */

  array Menu_select;


    StatRoute = GetDateReport();    /* Устанавливаем дату отчета */
    Operc={oper};

    Menu_select(0)=" По всем кассирам ";
    Menu_select(1)=" По текущему кассиру "+Operc+" ";

    Choice_oper=Menu (Menu_select,"Параметры выпуска отчета","Выбирайте...");
    if (Choice_oper<0)
       msgbox (" Отказ от выпуска отчета ");
       exit (1);
    end;
    if( StatRoute )
        HeadReport_rub();
        Report_rub();
        EndReport(0);
        HeadReport_val();
        Report_val();
        EndReport(1);
    else
      [ Отказ от выпуска отчета ];
    end;

end;

/********************** Запускалка ********************************************/
  macro Run()
    Main();
  end;

end;

/****************** Класс выпуска реестра для сбера ***************************/
array SpecCurrCodes,  SpecSumArray;
array PayCurrCodes,   PaySumArray;
array StornCurrCodes, StornSumArray;
array ItogoCurrCodes, ItogoSumArray;

// Массивы валют и сумм по категориям.
array CatCurr01, CatSum01;
array CatCurr02, CatSum02;
array CatCurr03, CatSum03;
array CatCurr04, CatSum04;
array CatCurr05, CatSum05;
array CatCurr06, CatSum06;
array CatCurr07, CatSum07;
array CatCurr08, CatSum08;
array CatCurr09, CatSum09;
array CatCurr10, CatSum10;

class (TReestrTranslateCommercial) TReestrTranslateSber

  var Number = 0;          /* Порядковый номер операции */

  private macro CheckPayOper( NumOper )
    var i = 0, retval = false;
    while( (not retval) and (i < asize(ANumberPayOper) ) )
      if( ANumberPayOper( i ) == NumOper )
        retval = true;
      end;
      i = i + 1;
    end;

    return retval;
  end;

  private macro AddSpec(CurrCode, ValueMoney, CurrArray, SumArray)
    var
      i = 0;

    while ( ( i < ASize( CurrArray ) ) and ( CurrArray( i ) != CurrCode ) )
      i = i + 1;
    end;

    if ( i == ASize( CurrArray ) )
      CurrArray( i ) = CurrCode;
      SumArray( i )  = ValueMoney;
    else
      SumArray( i )  = SumArray( i ) + ValueMoney;
    end;

  end;

  private macro AddCategoryOther( pSum )
    var vOperat = StrLwr( GetNameOperation( pay_doc.IsCur,
                                            pay_doc.Groupopert,
                                            pay_doc.NumOpert, false ) );
	FindPmntProp(pay_doc_PmntProp, pay_doc.iApplicationKind, pay_doc.ApplicationKey);
    var vRecip  = StrLwr( pay_doc_PmntProp.Name );
    var vGround = StrLwr( pay_doc.Ground );

    if ( pay_doc.NumOpert == 75 )
      if ( ( Index( vGround, "беженец" ) > 0 )      OR
           ( Index( vGround, "беженц" ) > 0 )       OR
           ( Index( vGround, "переселенец" ) > 0 )  OR
           ( Index( vGround, "переселенц" ) > 0 )   OR
           ( Index( vGround, "беспроцент" ) > 0 )   OR
           ( Index( vRecip,  "беженец" ) > 0 )      OR
           ( Index( vRecip,  "беженец" ) > 0 )      OR
           ( Index( vRecip,  "переселенец" ) > 0 )  OR
           ( Index( vRecip,  "переселенц" ) > 0 ) )
        AddSpec( pay_doc.CodCur, pSum, CatCurr09, CatSum09 );
      else
        AddSpec( pay_doc.CodCur, pSum, CatCurr08, CatSum08 );
      end;
    else
      if   ( ( Index( vGround, "товар" ) > 0 )  OR
             ( Index( vOperat, "товар" ) > 0 ) )
        AddSpec( pay_doc.CodCur, pSum, CatCurr04, CatSum04 );
      elif ( ( Index( vGround, "страхов" ) > 0 )  OR
             ( Index( vRecip,  "страхов" ) > 0 )   OR
             ( Index( vOperat, "страхов" ) > 0 ) )
        AddSpec( pay_doc.CodCur, pSum, CatCurr05, CatSum05 );
      elif ( ( Index( vGround, "добровол" ) > 0 )  OR
             ( Index( vOperat, "добровол" ) > 0 ) )
        AddSpec( pay_doc.CodCur, pSum, CatCurr06, CatSum06 );
      elif ( ( Index( vGround, "пбоюл" ) > 0 )      OR
             ( Index( vRecip,  "пбоюл" ) > 0 )   OR
             ( Index( vOperat, "пбоюл" ) > 0 ) )
        AddSpec( pay_doc.CodCur, pSum, CatCurr07, CatSum07 );
      else
        AddSpec( pay_doc.CodCur, pSum, CatCurr10, CatSum10 );
      end;
    end;

  end;

  private macro AddCategory( pSum )

    if ( pay_doc.Groupopert == GroupOpert_COMM )
      if ( StrLen( pay_doc_cp.CodeTX ) > 0 )  // Есть КБК.
        AddSpec( pay_doc.CodCur, pSum, CatCurr01, CatSum01 );
      else
        AddSpec( pay_doc.CodCur, pSum, CatCurr02, CatSum02 );
      end;
    else
      AddCategoryOther( pSum );
    end;

  end;

  private macro curren(CurrCode, flag)

    var stat;
    var codecuren;

    KeyNum     (currency, 0);
    ClearRecord(currency   );
    Rewind     (currency   );
    currency.Code_Currency = CurrCode;

    if(getEQ(currency))
       if (flag == 0)
         codecuren = currency.Short_Name;
       elif (flag == 1)
         codecuren = currency.Short_Name + "/" + currency.ExternalCode;
       elif (flag == 2)
         codecuren = currency.Code_Currency;
       elif (flag == 3)
         codecuren = currency.Name_Currency;
       end;
    else
       codecuren = "";
    end;

    return codecuren;
  end;

  private macro BranchName

    var name;
    var party = TRecHandler( "i_party.rec" );

    if ( findDepartment(NumFNCash, name, null, party) )
      return party.rec.Name + ", № " + name;
    else
      return "";
    end;
  end;


  private macro GetOperName( Oper )

    file pers ( person, "sbbank.def" );
    var sName = "", Name = "", ind, IOtmp;

    pers.Oper = Oper;

    if( geteq( pers ) )
       sName = Trim( pers.Name );
       ind = Index( sName, " " );
       if ( ind > 0 )
         Name = SubStr( sName, 1, Ind - 1 );
         IOtmp= Trim( Substr( sName, Ind ) );
         Name = Name + " " + strupr( Substr(IOtmp, 1, 1)) + ".";
         ind = Index( IOtmp, " " );
         if ( ind > 0 )
           IOtmp = Trim( Substr( IOtmp, Ind ) );
           Name = Name + strupr( Substr( IOtmp, 1, 1 ) ) + ".";
         end;
       else
         Name = sName;
       end;
    end;

    return Name;
  end; /*GetOperName*/

  private macro HeadReport()

    var D1,D2,D3;
    var monat;

     DateSplit({curdate},D1,D2,D3);
     monat = date_monat(D2);

    [ ];
    [   Сбербанк России ОАО                                         Ф.№ 350   ];
    [ ];
    [ #######################################################                 ]
    ( BranchName:w );
    [ _____________________________________                                   ];
    [  (Наименование, № филиала Сбербанка                                     ];
    [          России/ № ВСП)                                                 ];
    [ ];
    [ ];
    [                 РЕЕСТР СУММ ПРИНЯТОЙ ДЕНЕЖНОЙ НАЛИЧНОСТИ                ];
    [               ДЛЯ ОСУЩЕСТВЛЕНИЯ ПЕРЕВОДА ДЕНЕЖНЫХ СРЕДСТВ               ];
    [ ];
    [                        от "##" ######## #### года                       ]
    ( D1, monat:c, D3 );
    [ Наименование                                                            ];
    [ #################################  ####################################################### ]
    ( "кредитной организации/филиала/ВСП", BranchName:w );
    [ ];
    [ +-------+----------------------------------------------+---------------------+];
    [ | № п/п |          Наименование операции               |         Сумма       |];
    [ +-------+----------------------------------------------+---------------------+];

  end;

  private macro ProccessReport()
    var stat, i, NeedPrint = true;

     macro PrintCommission()
     end;

     /* Повторяющийся фрагмент выносим в отдельную процедурку */
     macro mini_rep( flagStorn )
       var rec, Summ_commis = 0, Summ_without_commis = 0, NameOperation, Op;

       if ( pay_doc.GroupOpert == GroupOpert_COMM )
         Op = pay_doc.ReceiverCod;
       else
         Op = pay_doc.NumOpert;
       end;

       NameOperation = GetNameOperation(pay_doc.IsCur, pay_doc.Groupopert, Op, flagStorn);
       if( pay_doc.CodCur > 0 )
         NameOperation = NameOperation + " (" + curren(pay_doc.CodCur, 0) + ")";
       end;
       // if( (pay_doc.Groupopert==GroupOpert_COMM) or (CheckPayOper( pay_doc.NumOpert )) )
       if( (pay_doc.Groupopert==GroupOpert_COMM) or
           ((pay_doc.Groupopert==GroupOpert_PEREVOD) and
            not ( (pay_doc.NumOpert == 20) or
                  // ((pay_doc.NumOpert == 40) and (pay_doc.ApplType == 2) ) or
                  (pay_doc.NumOpert == 40) or
                  (pay_doc.NumOpert == 42) ) ) )
         if( NeedPrint )
           [ |                                                                            |];
           [ +-------+----------------------------------------------+---------------------+];
           NeedPrint = false;
         end;
         SetRecordAddr( pay_doc_cp, pay_doc, 0, 0, TRUE );
         if( pay_doc.Groupopert==GroupOpert_PEREVOD )
           Summ_without_commis=pay_doc.insum;
         else
           Summ_without_commis=pay_doc_cp.ComPaySum;
         end;
         Number = Number + 1;
         [ |#######|##############################################|#####################|]
         (Number:c, NameOperation:w, Summ_without_commis);
         [ +-------+----------------------------------------------+---------------------+];
         AddSpec(pay_doc.CodCur, Summ_commis + Summ_without_commis, PayCurrCodes, PaySumArray);

         if ( pay_doc.Action == 11 )
           if ( flagStorn )
             AddSpec( pay_doc.CodCur, Summ_commis + Summ_without_commis, StornCurrCodes, StornSumArray );
           end;
         else
           AddSpec( pay_doc.CodCur, Summ_commis + Summ_without_commis, ItogoCurrCodes, ItogoSumArray );
         end;

         AddCategory( Summ_commis + Summ_without_commis );

         /*
         if( pay_doc.Groupopert==GroupOpert_PEREVOD )
           Summ_commis=Get_comission_sum(pay_doc.Groupopert, pay_doc.CodCur, pay_doc.iApplicationKind, pay_doc.ApplicationKey);
           if( Summ_commis > 0 )
             rec = Get_comission_Rec(pay_doc.Groupopert, pay_doc.CodCur, pay_doc.iApplicationKind, pay_doc.ApplicationKey);
             NameOperation = GetNameOperation(rec.IsCur, rec.Groupopert, rec.NumOpert);
             if( rec.CodCur > 0 )
               NameOperation = NameOperation + " (" + curren(rec.CodCur, 0) + ")";
             end;
             Number = Number + 1;
             [ |#######|##############################################|#####################|]
             (Number:c, NameOperation:w, Summ_commis );
             [ +-------+----------------------------------------------+---------------------+];
             AddSpec(pay_doc.CodCur, Summ_commis, PayCurrCodes, PaySumArray);
           end;
         end;
         */
       elif( (pay_doc.Groupopert==GroupOpert_PEREVOD) and
             ( (pay_doc.NumOpert == 20) or
               // ((pay_doc.NumOpert == 40)and(pay_doc.ApplType == 2) ) or
               (pay_doc.NumOpert == 40) or
               (pay_doc.NumOpert == 42) ) )
         Summ_without_commis=pay_doc.insum;
         Number = Number + 1;
         [ |#######|##############################################|#####################|]
         (Number:c, NameOperation:w, Summ_without_commis );
         [ +-------+----------------------------------------------+---------------------+];
         AddSpec(pay_doc.CodCur, Summ_without_commis, SpecCurrCodes, SpecSumArray);

         if ( pay_doc.Action == 11 )
           if ( flagStorn )
             AddSpec( pay_doc.CodCur, Summ_without_commis, StornCurrCodes, StornSumArray );
           end;
         else
           AddSpec( pay_doc.CodCur, Summ_commis + Summ_without_commis, ItogoCurrCodes, ItogoSumArray );
         end;

         /*
         Summ_commis=Get_comission_sum(pay_doc.Groupopert, pay_doc.CodCur, pay_doc.iApplicationKind, pay_doc.ApplicationKey);
         if( Summ_commis > 0 )
           rec = Get_comission_Rec(pay_doc.Groupopert, pay_doc.CodCur, pay_doc.iApplicationKind, pay_doc.ApplicationKey);
           NameOperation = GetNameOperation(rec.IsCur, rec.Groupopert, rec.NumOpert);
           if( rec.CodCur > 0 )
             NameOperation = NameOperation + " (" + curren(rec.CodCur, 0) + ")";
           end;
           Number = Number + 1;
           [ |#######|##############################################|#####################|]
           (Number:c, NameOperation:w, Summ_commis );
           [ +-------+----------------------------------------------+---------------------+];
           AddSpec(rec.CodCur, Summ_commis, SpecCurrCodes, SpecSumArray);
         end;
         */

       end;
     end;

     KeyNum     (pay_doc, 7);
     Rewind     (pay_doc   );
     ClearRecord(pay_doc   );
     /* Документы отражаем в порядке их поступления */
     pay_doc.FNCash          = FNcash;
     pay_doc.Date_Document   = {curdate};
     pay_doc.KKMFactoryNum   = "";
     pay_doc.IsCur           = 0;
     pay_doc.CodCur          = 0;

     stat = getGE (pay_doc);
     while(stat
           AND (pay_doc.FNCash == FNcash)
           AND (pay_doc.Date_Document == {curdate}) )
        // if( ( pay_doc.Action == 0 ) and
        if( ( ( pay_doc.IsSuspended == "" ) or ( pay_doc.IsSuspended == "X" ) ) and
            ( pay_doc.InSum > 0 )
          )
          //if ( (pay_doc.Groupopert==GroupOpert_PEREVOD) and ( not CheckPayOper( pay_doc.NumOpert )) )
          if ( (pay_doc.Groupopert==GroupOpert_PEREVOD) and
               ( (pay_doc.NumOpert == 20) or
                 //((pay_doc.NumOpert == 40) and (pay_doc.ApplType == 2) ) or
                 (pay_doc.NumOpert == 40) or
                 (pay_doc.NumOpert == 42) ) )
            // if ((pay_doc.CodCashier=={oper}) and Check_casdc_type (pay_doc.iApplicationkind, pay_doc.Applicationkey))
            if (pay_doc.CodCashier=={oper})
               mini_rep( false );
            end;
          end;
        end;
        stat = next(pay_doc);
     end;

     if( SpecCurrCodes(0) != NULL )
       i = 0;
       [ |     Итого по переводам                                                     |];
       [ +-------+----------------------------------------------+---------------------+];
       while( i < asize( SpecCurrCodes ) )
         [ |       |##############################################|#####################|]
         (curren(SpecCurrCodes(i),3), SpecSumArray( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         i = i + 1;
       end;
     end;

     // Number = 0;  Сквозная нумерация.
     ClearRecord(pay_doc   );
     /* Документы отражаем в порядке их поступления */
     pay_doc.FNCash          = FNcash;
     pay_doc.Date_Document   = {curdate};
     pay_doc.KKMFactoryNum   = "";
     pay_doc.IsCur           = 0;
     pay_doc.CodCur          = 0;

     stat = getGE (pay_doc);
     while(stat
           AND (pay_doc.FNCash == FNcash)
           AND (pay_doc.Date_Document == {curdate}) )
        // if( ( pay_doc.Action < 2 ) and
        if( ( ( pay_doc.IsSuspended == "" ) or ( pay_doc.IsSuspended == "X" ) ) and
            ( pay_doc.InSum > 0 )
          )
          // if ( (pay_doc.Groupopert==GroupOpert_COMM) or ((pay_doc.Groupopert==GroupOpert_PEREVOD) and (CheckPayOper( pay_doc.NumOpert ))) )
          if ( (pay_doc.Groupopert==GroupOpert_COMM) or
               ((pay_doc.Groupopert==GroupOpert_PEREVOD) and
                 not ( (pay_doc.NumOpert == 20) or
                       // ((pay_doc.NumOpert == 40) and (pay_doc.ApplType == 2) ) or
                       (pay_doc.NumOpert == 40) or
                       (pay_doc.NumOpert == 42) ) ) )
            // if ((pay_doc.CodCashier=={oper}) and Check_casdc_type (pay_doc.iApplicationkind, pay_doc.Applicationkey))
            if (pay_doc.CodCashier=={oper})
               mini_rep( false );
            end;
          end;
        end;
        stat = next(pay_doc);
     end;

     /*
     if( PayCurrCodes(0) != NULL )
       i = 0;
       [ |     Итого по прочим операциям                                              |];
       [ +-------+----------------------------------------------+---------------------+];
       while( i < asize( PayCurrCodes ) )
         [ |       |##############################################|#####################|]
         (curren(PayCurrCodes(i),3), PaySumArray( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         // AddSpec(PayCurrCodes(i), PaySumArray( i ), SpecCurrCodes, SpecSumArray);
         i = i + 1;
       end;
       [ |                                                                            |];
       [ +-------+----------------------------------------------+---------------------+];
     end;
     */

     if ( ( asize( CatCurr01 ) > 0 )  OR
          ( asize( CatCurr02 ) > 0 )  OR
          ( asize( CatCurr03 ) > 0 )  OR
          ( asize( CatCurr04 ) > 0 )  OR
          ( asize( CatCurr05 ) > 0 )  OR
          ( asize( CatCurr06 ) > 0 )  OR
          ( asize( CatCurr07 ) > 0 )  OR
          ( asize( CatCurr08 ) > 0 )  OR
          ( asize( CatCurr09 ) > 0 )  OR
          ( asize( CatCurr10 ) > 0 ) )
       [ |     Итого по прочим операциям                                              |];
       [ +-------+----------------------------------------------+---------------------+];
     end;

     if ( asize( CatCurr01 ) > 0 )
       i = 0;
       while( i < asize( CatCurr01 ) )
         [ |       |##############################################|#####################|]
         ( ("Платежи в бюджет. " + curren(CatCurr01(i),3) ):w, CatSum01( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         i = i + 1;
       end;
     end;

     if ( asize( CatCurr02 ) > 0 )
       i = 0;
       while( i < asize( CatCurr02 ) )
         [ |       |##############################################|#####################|]
         ( ("Платежи за ЖКУ. " + curren(CatCurr02(i),3) ):w, CatSum02( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         i = i + 1;
       end;
     end;

     if ( asize( CatCurr03 ) > 0 )
       i = 0;
       while( i < asize( CatCurr03 ) )
         [ |       |##############################################|#####################|]
         ( ("Платежи за услуги. " + curren(CatCurr03(i),3) ):w, CatSum03( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         i = i + 1;
       end;
     end;

     if ( asize( CatCurr04 ) > 0 )
       i = 0;
       while( i < asize( CatCurr04 ) )
         [ |       |##############################################|#####################|]
         ( ("Платежи за товары. " + curren(CatCurr04(i),3) ):w, CatSum04( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         i = i + 1;
       end;
     end;

     if ( asize( CatCurr05 ) > 0 )
       i = 0;
       while( i < asize( CatCurr05 ) )
         [ |       |##############################################|#####################|]
         ( ("Страховые платежи. " + curren(CatCurr05(i),3) ):w, CatSum05( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         i = i + 1;
       end;
     end;

     if ( asize( CatCurr06 ) > 0 )
       i = 0;
       while( i < asize( CatCurr06 ) )
         [ |       |##############################################|#####################|]
         ( ("Добровольные взносы. " + curren(CatCurr06(i),3) ):w, CatSum06( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         i = i + 1;
       end;
     end;

     if ( asize( CatCurr07 ) > 0 )
       i = 0;
       while( i < asize( CatCurr07 ) )
         [ |       |##############################################|#####################|]
         ( ("Платежи предпринимателям. " + curren(CatCurr07(i),3) ):w, CatSum07( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         i = i + 1;
       end;
     end;

     if ( asize( CatCurr08 ) > 0 )
       i = 0;
       while( i < asize( CatCurr08 ) )
         [ |       |##############################################|#####################|]
         ( ("Оплата по коммерческому кредиту. " + curren(CatCurr08(i),3) ):w, CatSum08( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         i = i + 1;
       end;
     end;

     if ( asize( CatCurr09 ) > 0 )
       i = 0;
       while( i < asize( CatCurr09 ) )
         [ |       |##############################################|#####################|]
         ( ("Оплата по беспроцентной ссуде. " + curren(CatCurr09(i),3) ):w, CatSum09( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         i = i + 1;
       end;
     end;

     if ( asize( CatCurr10 ) > 0 )
       i = 0;
       while( i < asize( CatCurr10 ) )
         [ |       |##############################################|#####################|]
         ( ("Прочие платежи. " + curren(CatCurr10(i),3) ):w, CatSum10( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         i = i + 1;
       end;
     end;

     if ( ( asize( CatCurr01 ) > 0 )  OR
          ( asize( CatCurr02 ) > 0 )  OR
          ( asize( CatCurr03 ) > 0 )  OR
          ( asize( CatCurr04 ) > 0 )  OR
          ( asize( CatCurr05 ) > 0 )  OR
          ( asize( CatCurr06 ) > 0 )  OR
          ( asize( CatCurr07 ) > 0 )  OR
          ( asize( CatCurr08 ) > 0 )  OR
          ( asize( CatCurr09 ) > 0 )  OR
          ( asize( CatCurr10 ) > 0 ) )
       [ |                                                                            |];
       [ +-------+----------------------------------------------+---------------------+];
     end;

     //
     // Сторнированные операции.
     //
     ClearRecord(pay_doc   );
     /* Документы отражаем в порядке их поступления */
     pay_doc.FNCash          = FNcash;
     pay_doc.Date_Document   = {curdate};
     pay_doc.KKMFactoryNum   = "";
     pay_doc.IsCur           = 0;
     pay_doc.CodCur          = 0;

     stat = getGE (pay_doc);
     while(stat
           AND (pay_doc.FNCash == FNcash)
           AND (pay_doc.Date_Document == {curdate}) )
        if( ( pay_doc.Action == 11 ) and  // Сторнированные операции.
            ( ( pay_doc.IsSuspended == "" ) or ( pay_doc.IsSuspended == "X" ) ) and
            ( pay_doc.InSum > 0 )
          )
          // if ((pay_doc.CodCashier=={oper}) and Check_casdc_type (pay_doc.iApplicationkind, pay_doc.Applicationkey))
          if (pay_doc.CodCashier=={oper})
             mini_rep( true );
          end;
        end;
        stat = next(pay_doc);
     end;

     if( StornCurrCodes(0) != NULL )
       i = 0;
       [ |     Сторнированные операции                                                |];
       [ +-------+----------------------------------------------+---------------------+];
       while( i < asize( StornCurrCodes ) )
         [ |       |##############################################|#####################|]
         (curren(StornCurrCodes(i),3), StornSumArray( i ) );
         [ +-------+----------------------------------------------+---------------------+];
         i = i + 1;
       end;
       [ |                                                                            |];
       [ +-------+----------------------------------------------+---------------------+];
     end;

  end;

  private macro EndReport()
    var i;

    [ | ИТОГО                                                                      |];
    [ +-------+----------------------------------------------+---------------------+];
    if( ItogoCurrCodes(0) != NULL )
      i = 0;
      while( i < asize( ItogoCurrCodes ) )
        [ |       |##############################################|#####################|]
        (curren(ItogoCurrCodes(i),3), ItogoSumArray( i ) );
        [ +-------+----------------------------------------------+---------------------+];
        i = i + 1;
      end;
    end;

    [ ];
    [ Кассовый (операционно-кассовый) работник ____________ ####################### ]
    ( GetOperName( {oper} ):c );
    [                                            (подпись)   (расшифровка подписи)  ];

  end;

  macro Main()
    HeadReport();
    ProccessReport();
    EndReport();
  end;

  InitTReestrTranslateCommercial;
end;

/*******************************************************************************
######################### Точка входа ##########################################
*******************************************************************************/
var obj;

  if ( GetBankStandart() == 0 )
  /* Сберегательный банк РФ */
    obj = TReestrTranslateSber;
    obj.Run();
  else
  /* Коммерческий банк */
    obj = TReestrTranslateCommercial;
    obj.Run();
  end;

end;


