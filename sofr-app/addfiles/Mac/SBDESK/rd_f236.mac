
/* Опись на ценные бумаги и ценные бланки */

import DeskInter, RD_Comm;

file BagList( "bag.dbt" ) key 0;
file CIMisc( "ci_misc.dbt" ) key 0;
file CashLink( "sb_caln.dbt" );

/* Флаг "Печатать отдельную опись на каждую ценность" */

const
  OwnListForEachVal = true;

const
  M_OGSZ = 0, 
  M_ORVVZ = 1, 
  M_LOT = 2, 
  M_PAYED = 3, 
  M_OLD_COINS = 4, 
  M_OTHER = 5;

const
  CI_OGSZ=1,
  CI_LOTTERY=2,
  CI_CERT=3,
  CI_COIN=4,
  CI_INGOT=5,
  CI_ORVVZ=6,
  CI_MISC=10,
  CI_OGSZWARR=11;

const
  CK_NEW = 0, 
  CK_OLD = 1;

var 
  ProgID = StrFor( GetProgramID ), 
  PrintFIO = GetRegSetting( V_INTEGER, "КЛАДОВАЯ\\ФИО_ИНКАС_В_ОПИСИ" ), 
  ColName, 
  PeerName,
  SealNo, 
  Total = $0;

macro FindCIMisc( Type, IssueID )

  CIMisc.Type = Type;
  CIMisc.IssueID = IssueID;
  return GetEQ( CIMisc );
end;

macro GetNominalCost

  var
    RecFound,
    C;

  if ( CashValue.UnitMeas == RESMESUNIT_POINT )
    if ( ProgID == "Л" )
      C = $1 * BagList.RealItems;
    else
      C = $1 * BagList.DeclaredItems;
    end;
  else
    if ( ProgID == "Л" )
      C = BagList.RealSum;
    else
      C = BagList.DeclaredSum;
    end;
  end;

  GetCurrCodeAndCBRate( CashValue.RefValue );
  if ( Curr.Code_Currency != 0 )
    C = Cur2Rub( C );
  end;

  return C;
end;

macro Filter( Mode )

  var 
    Result = false;

  /* Деньги надо сразу прокинуть */

  if ( CashValue.TypeValue == TYPERES_CURRENCY )
    if ( ( CashValue.Type == TYPE_RES_PAY ) or ( CashValue.Type == TYPE_RES_NOPAY ) )
      return false;
    end;
  elif ( CashValue.TypeValue == TYPERES_PAYDOC )
    if ( ( CashValue.Type == TYPE_RES_NOPAY ) 
      or ( CashValue.Type == TYPE_RES_PDNAT ) 
      or ( CashValue.Type == TYPE_RES_PDCUR ) 
      or ( CashValue.Type == TYPE_RES_PD ) )
      return false;
    end;
  end; 

  /* Монеты (новые) - тоже деньги, но они не попадают в результате проверки ниже */

  if ( ( CashValue.TypeValue == TYPERES_MINCAPISSUE + CI_OGSZ ) and ( CashValue.Type == 0 ) )
    if ( Mode == M_OGSZ )
      Result = true;
    end;
  elif ( ( CashValue.TypeValue == TYPERES_MINCAPISSUE + CI_ORVVZ ) and ( CashValue.Type == 0 ) )
    if ( Mode == M_ORVVZ )      
      Result = true;
    end;
  elif ( ( CashValue.TypeValue == TYPERES_MINCAPISSUE + CI_LOTTERY ) and ( CashValue.Type == 0 ) )
    if ( Mode == M_LOT )
      Result = true;
    end;
  elif ( ( ( ( CashValue.TypeValue == TYPERES_VALFORM ) and ( CashValue.UnitMeas != RESMESUNIT_POINT ) )
      or ( CashValue.TypeValue == TYPERES_MINCAPISSUE + CI_OGSZWARR ) ) and ( CashValue.Type == 0 ) )
    if ( Mode == M_PAYED )
      Result = true;
    end;
  elif ( ( CashValue.TypeValue == TYPERES_MINCAPISSUE + CI_COIN ) and ( CashValue.Type == 0 ) )
    if ( Mode == M_OLD_COINS )
      FindCIMisc( CI_COIN, CashValue.CodIntValue );
      if ( CIMisc.KindOfCoin )
        Result = true;
      end; 
    end;
  else 
    if ( Mode == M_OTHER )
      Result = true;
    end;
  end;

  return Result;
end;

macro PrintLine

  var
    NominalCost,
    ItemsCost;

  if ( ProgID == "Л" )
    ItemsCost = $1 * BagList.RealItems;
  else
    ItemsCost = $1 * BagList.DeclaredItems;
  end;

  NominalCost = GetNominalCost;
  [ #################################### ########## ######################## ]
  (
    CashValue.NameValue:w, 
    ItemsCost:z, 
    NominalCost
  );

  Total = Total + NominalCost;    
end;

macro PrintBodyPart( Mode )

  var
    RecFound,
    Stat = true;

  ClearRecord( BagList );
  BagList.Branch = Bag.Branch;  
  BagList.Date = Bag.Date;
  BagList.ClientType = Bag.ClientType;
  BagList.ClientID = Bag.ClientID;
  BagList.Operation = Bag.Operation;
  BagList.Number = Bag.Number;
  RecFound = GetGE( BagList );
  while ( RecFound and Stat
    and ( BagList.Branch == Bag.Branch )  
    and ( BagList.Date == Bag.Date )
    and ( BagList.ClientType == Bag.ClientType )
    and ( BagList.ClientID == Bag.ClientID )
    and ( BagList.Operation == Bag.Operation )
    and ( BagList.Number == Bag.Number ) )
    Stat = FindCashValue( BagList.ValueRef );
    if ( Stat and ( Filter( Mode ) ) )
      PrintLine;
    end;
    RecFound = Next( BagList );
  end;
  if ( not Stat )
    MsgBox( "Ошибка при печати описи" );
    Exit( -1 );
  end;
end;

macro PrintListBody( CopyNo )

  var 
    Mode, 
    Mark = ""; 

  if ( CopyNo == 2 )
    Mark = "-а";
  elif ( CopyNo == 3 )
    Mark = "-б";
  end;

  if ( BankStandart )
    [ # экз.                                                                   ]
    ( CopyNo );
  else
    [ # экз.                                                        Ф. № 236## ]
    ( CopyNo, Mark );
  end;
  [                                                                          ];
  [                                О П И С Ь                                 ];
  [                    на ценные бумаги и ценные бланки                      ];
  [ ######################################################################## ]
  ( ( "отправляемые через инкассаторов " + String( Bag.Date:m ) ):c );
  [                                                                          ];
  [                                                                          ];
  if ( Bag.Operation == CASHOP_PANT_RDEB )
    [ Наименование банка-отправителя #                                         ]
    ( {Name_Bank} );
    [ Наименование банка-получателя  #                                         ]
    ( PeerName );
  else
    [ Наименование банка-отправителя #                                         ]
    ( PeerName );
    [ Наименование банка-получателя  #                                         ]
    ( {Name_Bank} );
  end;
  [                                                                          ];
  [ Направляем ценные бумаги и ценные бланки,                                ];
  [ вложенные в инкассаторские сумки (мешки) № #                             ]
  ( BagNum.NumberBag );
  [ опечатанные пломибиром № #____________________                           ]
  ( SealNo );
  [                                                                          ];
  [--------------------------------------------------------------------------];
  [| Вид и наименование ценых бумаг     |Количество| Номинальная (условная) |];
  [| и ценных бланков                   |          | оценка                 |];
  [|------------------------------------|----------|------------------------|];
       
  Total = 0;           

  if ( not OwnListForEachVal )
    Mode = M_OGSZ;
    while ( Mode <= M_OTHER )
      PrintBodyPart( Mode );
      Mode = Mode + 1;
    end;
  else
    Copy( BagList, Bag );
    if ( FindCashValue( BagList.ValueRef ) )
      PrintLine; 
    end;
  end; 

  [--------------------------------------------------------------------------];
  [ ];
  [Всего #___________________________________________________________________]
  ( String( Total ) + " (" + RubToStr( Total ) + ")" );
  [ ];
  if ( BankStandart and ( ProgID != "Л" ) )
    [Заведующий кассой _____________________________ ]; 
  else
    [Заведующий кладовой ___________________________ ]; 
  end;  
  [Операционный работник _________________________ ]; 
  [ ];
  [М.П. ];
  [--------------------------------------------------------------------------]
end;

macro PrintSign1

  ["__" _____________ 200_ г. указанные в описи ценности на сумму];
  [#########################################################################]
  ( ( String( Total ) + " (" + RubToStr( Total ) + ")" ):w );
  [приняты];
  if ( BankStandart and ( ProgID != "Л" ) )
    [Заведующий кассой _____________________________ ]; 
  else
    [Заведующий кладовой (ст. кассир) ______________ ]; 
  end;  
  [Операционный работник _________________________ ]; 
end;

macro PrintSign2

  ["__" _____________ 200_ г. указанную в описи опломбированую сумку (сумки)];
  [в исправной упаковке, без пересчета в ней (в них) ценностей              ];
  [принял:];
  if ( PrintFIO )
    [Инкассатор __________________________________ #] ( ColName ); 
  else
    [Инкассатор __________________________________]; 
  end; 
  [ ];
  [ М.П. ];
end;

macro PrintSign3

  ["__" _____________ 200_ г. указанную в описи опломбированую сумку (сумки)];
  [в исправной упаковке, без пересчета в ней (в них) ценностей              ];
  [приняли:];
  if ( BankStandart and ( ProgID != "Л" ) )
    [Заведующий кассой _____________________________ ]; 
  else
    [Заведующий кладовой (ст. кассир) ______________ ]; 
  end;  
  [Операционный работник _________________________ ]; 
  [                                                             М.П. ];
  [Ценности сдал:];
  if ( PrintFIO )
    [Инкассатор __________________________________ #] ( ColName ); 
  else
    [Инкассатор __________________________________]; 
  end; 
  [ ];
end;

macro PrintCopy( n )

  PrintListBody( n );
  [ ];
  [                           Р А С П И С К А                               ];
  if ( n == 1 )
    PrintSign1;
  elif ( n == 2 )
    PrintSign2;
  elif ( n == 3 )
    PrintSign3;
  end;
  PrintLn( StrFor( 12 ) );
end;

macro f236()

  if ( Bag.Number == 0 )
    Exit( 1 );
  end;

  CheckPersonType( PT_CONTR );

  if ( not OwnListForEachVal )
    if ( not GetTrue( true, "Печатать опись ф.236?" ) )
      Exit( 1 );
    end;
  end;

  SealNo = GetSealNo;
  GetClientInfo( Bag.ClientType, Bag.ClientID, PeerName );
  FindBagNum( Bag.Number );
  ColName = "";
  if ( ( ProgID != "Л" ) and PrintFIO )
    ColName = GetCollectorName( CashDoc.Branch, CashDoc.Collector );
    if ( ColName != "" )
      ColName = "(" + ColName + ")";
    end;
  end;
  PrintCopy( 1 );
  PrintCopy( 2 );
  PrintCopy( 3 );
end;
  