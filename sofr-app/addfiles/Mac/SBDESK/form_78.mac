/* Ф. 78 */

import BankInter, deprintr, ChkPrn;

file Alg    ("namealg.dbt","bank.def")  key 0;
file CashVal("sb_casvl.dbt") key 1;
file CashAcc("sb_casac.dbt") key 1;
file DepDoc("sbdepdoc.dbt") key 3;
file CashDoc("sb_casdc.dbt") key 0;
file GroupMember ("groupmem.dbt") key 0;
file DeskSubj ("desksubj.dbt") key 0;

const
  ShowNullLines=false;  /* Показывать ли строки, где все нули */

const
  TYPEDOC_CARRYOFF = 1,      /* Отложен (не подтвержден) */
  TYPEDOC_CARRYON  = 2,      /* Проведен (подтвержден)   */
  CV_Currency = 1,
  CV_ValForm = 2,
  CV_MinCI = 500,

  OC_Debet="1",
  OC_Credit="2",

  CASHOP_REINFORCE = 1,  /* Подкрепление кассира */
  CASHOP_RETURN    = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT   = 3,  /* Инкассация */
  CASHOP_ADVANCE   = 4,  /* Аванс */
  CASHOP_DEBIT     = 5,  /* Расход */
  CASHOP_CREDIT    = 6,  /* Приход */
  CASHOP_REMOVE    = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1  = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE   = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1 = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE   = 11,  /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1 = 12,  /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_DEBET_SAFE = 13,
  CASHOP_CREDIT_SAFE = 14,
  CASHOP_PANT_RI   = 20, /* Подкрепление филиала */
  CASHOP_PANT_RET  = 21, /* Возврат из филиала */
  CASHOP_PANT_RIO  = 22, /* Подкрепление оперчасти */
  CASHOP_PANT_RETO = 23, /* Возврат из оперчасти */
  CASHOP_PANT_COL  = 24, /* Инкассация */
  CASHOP_PANT_ADV  = 25, /* Аванс */
  CASHOP_PANT_RDEB = 30, /* Расход по кассе пересчета */
  CASHOP_PANT_RCRD = 31, /* Приход по кассе пересчета */
  CASHOP_PANT_RDRT = 32, /* Сдача ресурса в кладовую из Кассы пересчета */

  RESMESUNIT_POINT = 1,  /* Поштучно  */
  RESMESUNIT_MONEY = 2;  /* Стоимость */

const
  DST_IO          = 1,    /* Инкассация */
  DST_SAFE        = 2,    /* Сейф */
  DST_WORKDESK    = 3,    /* Рабочий стол кассира */
  DST_RDESK       = 4,    /* Касса пересчета */
  DST_PANTRY      = 5,    /* Кладовая отделения */
  DST_EDESK       = 6;    /* Вечерняя касса */

const
  RDesk            = "К. пересчета";

const
  ALG_CASH_VAL_TYPE = 811;    /* Типы ресурсов кассы в namealg.dbt */

var
  {curdate},
  Safe,
  OperBrigade = GetOperBrigade,
  LineCnt=0,
  IsCur=NumFlagCur,
  Branch=NumFNCash;

/* Лицевая сторона */

array
  Ser,
  MinNum,
  MaxNum;

var
  NoLinesPrinted,
  TotOtherDebF=0,
  SegCnt=0,
  RecNum=0;

array NameValArr;

macro IntTo07Str( n )

  var
    s = String( n );

  while ( StrLen( s ) < 7 )
    s = "0" + s;
  end;
  return s;
end;

/**************************************************************************/
/*  Получить название типа ресурса кассы                                  */
macro GetCashTypeName(Type)

  Alg.iTypeAlg = ALG_CASH_VAL_TYPE;
  Alg.iNumberAlg = Type;
  if(GetEQ(Alg))
    return Alg.szNameAlg;
  else
    return String(Type);
  end;
end;

/**************************************************************************/
/*  Получить название ресурса кассы                                       */
macro GetValName
  var str = CashVal.NameValue;
  if ( CashVal.Type != 0 )
    str = str + " (" + GetCashTypeName(CashVal.Type) + ")";
  end;
  return str;
end;


/**************************************************************************/
/*  Получить внешний (строковый) номер филиала                            */

macro DefNameBranch

  var NameBranch;

  if (not findDepartment(Branch, NameBranch))
    NameBranch = "";
  end;

  return NameBranch;

end;

macro GetWorkDeskName( OperID, BrigadeID )

  var
    OperStr = String( OperID ),
    BrigadeStr = String( BrigadeID );

  return MkStr( "0", 4 - StrLen( OperStr ) ) + OperStr + "/" +
    MkStr( "0", 2 - StrLen( BrigadeStr ) ) +BrigadeStr;
end;

macro GetNativeSafe

  file Group("rtgroup.dbt") key 2;

  Group.Branch = Branch;
  Group.Date = {curdate};
  Group.ID = OperBrigade;
  if(GetEQ(Group))
    return Group.SafeID;
  else
    MsgBox("Не найден сейф бригады № ", OperBrigade);
    Exit;
  end;
end;

macro OutTitle;

  [                                                                                   Форма № 78
        Сбербанк России
                                                                                         
        Филиал #                                                                    
   

                                     Ордер на списание
                               израсходованных ценных бланков

  ######################

  ](DefNameBranch(), {curdate}:m);
end;

macro OutVFHeader;

  [+----------------------------------------+------------+----------------------------+------------+
   |                                        |            |         №№ бланков         |   Сумма    |
   |          Наименование бланка           |   Серия    |-------------+--------------| (колич. )  |
   |                                        |            |     с №     |     по №     |            |
   +----------------------------------------+------------+-------------+--------------+------------+];
end;

macro ToStr(n)
                                      
  if( ( ValType( n ) != V_UNDEF ) and n )
    return IntTo07Str(n);
  else
    return "";
  end;
end;

macro OutVFLine(NameVal,OtherDeb);

  var
    TmpNameVal="",
    TmpOtherDeb="";

  if(((not ShowNullLines) and (OtherDeb==0)) or
    ((Ser(LineCnt)=="") and (SegCnt!=1)))
    return;
  end;

  RecNum=RecNum+1;
  NameVal=String(RecNum)+". "+NameVal;

  if(NoLinesPrinted)
    TmpNameVal=NameVal;
    TmpOtherDeb=OtherDeb;
  end;

  [| ###################################### | ########## | ########### | ############ | ########## |]
  (TmpNameVal:w,Ser(LineCnt),ToStr(MinNum(LineCnt)),ToStr(MaxNum(LineCnt)),TmpOtherDeb);
  NoLinesPrinted=false;
end;

macro OutVFTotLine;

  [+----------------------------------------+------------+-------------+--------------+------------+];
  [| И т о г о                              |            |             |              | ########## |]
  (TotOtherDebF);
end;

macro OutVFBottom

  [

     Главный (ст.) бухгалтер ______________                                                                
     
  ];
end;

macro GetSer

  return CashDoc.Series;
end;

macro GetMinNum

  return CashDoc.MinRegistrNumber;
end;

macro GetMaxNum

  if(CashDoc.MaxRegistrNumber != 0);
    return CashDoc.MaxRegistrNumber;
  else
    return CashDoc.MinRegistrNumber;
  end;
end;

macro FillSegArr(OtherDeb)

  var
    OldKey=KeyNum(CashDoc,9),
    CurSer,
    CurMinNum,
    CurMaxNum,
    OthDeb=$0,
    RecFound;

  SegCnt=0;
  ClearRecord(CashDoc);
  CashDoc.Branch=Branch;
  CashDoc.CashDateDoc={curdate};
  CashDoc.RefValue=CashVal.RefValue;
  RecFound=GetGE(CashDoc);
  while(RecFound and
    (CashDoc.Branch==Branch) and
    (CashDoc.CashDateDoc=={curdate}) and
    (CashDoc.RefValue==CashVal.RefValue))
    if((CashDoc.TypeDoc==TYPEDOC_CARRYON) and
      ((CashDoc.NumOper==CASHOP_DEBIT) or
      (CashDoc.NumOper==CASHOP_DEBET_SAFE) or
      (CashDoc.NumOper==CASHOP_CHVALUE) or
      ((CashDoc.NumOper==CASHOP_REMOVE) and
      (CashDoc.FlagDebCredOper==OC_Debet))) and
      (CashDoc.Brigade == OperBrigade))
      if(CashVal.UnitMeas == RESMESUNIT_MONEY)
        OthDeb = OthDeb + CashDoc.ValueMoney;
      else
        OthDeb = OthDeb + Money(CashDoc.ValueCol);
      end;
      CurSer=GetSer;
      CurMinNum=GetMinNum;
      CurMaxNum=GetMaxNum;
      if((SegCnt==0) or (CurSer!=Ser(SegCnt-1))
        or (CurMinNum>MaxNum(SegCnt-1)+1))
        Ser(SegCnt)=CurSer;
        MinNum(SegCnt)=CurMinNum;
        MaxNum(SegCnt)=CurMaxNum;
        SegCnt=SegCnt+1;
      else
        MaxNum(SegCnt-1)=Max(MaxNum(SegCnt-1),CurMaxNum);
      end;
    end;
    RecFound=Next(CashDoc);
  end;
  KeyNum(CashDoc,OldKey);
  SetParm(0, OthDeb);
end;

macro ReportOnCurVFResource;

  var
    OtherDeb=0;

  FillSegArr(OtherDeb);
  NoLinesPrinted=true;
  LineCnt=0;
  while(LineCnt<SegCnt)
    OutVFLine(GetValName, OtherDeb);
    LineCnt=LineCnt+1;
  end;
  TotOtherDebF=TotOtherDebF+OtherDeb;
end;

macro ReportOnVFResources;

  CashVal.TypeValue=CV_ValForm;
  CashVal.CodIntValue=0;

  OutVFHeader;

  if(GetGE(CashVal))
    if(((CashVal.RefValueReport==IsCur) or
        (CashVal.RefValueReport==2)) and (CashVal.FlagIntoBalance==3))
      ReportOnCurVFResource;
    end;
    while(Next(CashVal))
      if(((CashVal.RefValueReport==IsCur) or
          (CashVal.RefValueReport==2)) and (CashVal.FlagIntoBalance==3))
        ReportOnCurVFResource;
       end;
    end;
  end;

  OutVFTotLine;
  OutVFBottom;
end;

/* Обратная сторона */

const
  OP_OPENCAS       = 1,
  SB_CASHDOC       = -10,
  CASHOP_DEBET     = 5,
  VALFORM_31       = 31,
  VALFORM_BOOK     = 1,
  VALFORM_CUR_BOOK = 1,
  VALFORM_27       = 6,
  VALFORM_2O       = 14;

const
  TYPE_RES_PAY    = 0, /* Платежеспособные ресурсы */
  TYPE_RES_NOPAY  = 1, /* Неплатежеспособные ресурсы */
  TYPE_RES_DOUBT  = 2, /* Сомнительные ресурсы */
  TYPE_RES_SAVE   = 3, /* На хранении */
  TYPE_RES_EXP    = 4, /* На экспертизу */
  TYPE_RES_INCASS = 5; /* На инкассо */

array
  NewAcc,
  OnDemand,
  Replace;

var
  NewAccCnt=0,
  OnDemandCnt=0,
  ReplaceCnt=0;

macro OutBackHead;
  [                                                                                                                 Форма № 78];
  [ ];
  [                     Номера лицевых счетов, по которым израсходованы ценные бланки ];
  [ ];
  [---------------------------------------------------------------------------------------------------------------------------];
  [|                                    |                         Указываются номера лицевых счетов                           ];
  [|          Название бланков          |-------------------------------------------------------------------------------------];
  [|                                    |     Выписано по вновь     |     Выписано при явке      |      Выписано взамен       ];
  [|                                    |      открытым счетам      |         вкладчиков         |  исписанных и утраченных   ];
  [---------------------------------------------------------------------------------------------------------------------------];
end;

macro OutBadHead;
  [ ];
  [                          Перечень испорченных бланков, прилагаемых к ордеру ];
  [___________________________________________________________________________________________________________________________];
  [               (указываются наименование, количество, номера и серии испорченных бланков)                                  ];
  [ ];
end;

macro OutBackBottom;

  [___________________________________________________________________________________________________________________________];
  [ ];
  [     
         Ст. контролер ___________________             Кассир __________________
  ];
end;

macro OutBackLine;

  var
    ValName="",
    NewAccItem="",
    OnDemandItem="",
    ReplaceItem="";

  if(LineCnt==0)
    ValName=GetValName;
  end;
  if(LineCnt<NewAccCnt)
    NewAccItem=NewAcc(LineCnt);
  end;
  if(LineCnt<OnDemandCnt)
    OnDemandItem=OnDemand(LineCnt);
  end;
  if(LineCnt<ReplaceCnt)
    ReplaceItem=Replace(LineCnt);
  end;

  [ ###################################  ########################### ############################ ############################]
  (
    ValName:w,
    NewAccItem,
    OnDemandItem,
    ReplaceItem
  );
end;

macro TreatPrimary

  DepDoc.iApplicationKind=CashDoc.ApplicationKind;
  DepDoc.ApplicationKey=CashDoc.ApplicationKey;
  if(GetEQ(DepDoc))
    if(DepDoc.iApplicationKind==SB_CASHDOC)
      if(DepDoc.ApplType != 2 )
        Replace(ReplaceCnt)=DepDoc.Account;
        ReplaceCnt=ReplaceCnt+1;
      else
        OnDemand(OnDemandCnt)=DepDoc.Account;
        OnDemandCnt=OnDemandCnt+1;
      end;
    elif(DepDoc.TypeComplexOper==OP_OPENCAS)
      NewAcc(NewAccCnt)=DepDoc.Account;
      NewAccCnt=NewAccCnt+1;
    else
      OnDemand(OnDemandCnt)=DepDoc.Account;
      OnDemandCnt=OnDemandCnt+1;
    end;
  end;
end;

macro TreatDocsForOper

  var
    Subj = GetWorkDeskName( GroupMember.Oper, OperBrigade ),
    RecFound;

  CashDoc.Branch=Branch;
  CashDoc.CashDateDoc={curdate};
  CashDoc.TypeDoc=TYPEDOC_CARRYON;
  CashDoc.CashOper=Subj;
  CashDoc.RefValue=CashVal.RefValue;
  RecFound=GetGE(CashDoc);
  while(RecFound and
    (CashDoc.Branch==Branch) and
    (CashDoc.CashDateDoc=={curdate}) and
    (CashDoc.TypeDoc==TYPEDOC_CARRYON) and
    (CashDoc.CashOper==Subj) and
    (CashDoc.RefValue==CashVal.RefValue))
    if(CashDoc.NumOper==CASHOP_DEBET)
      TreatPrimary;
    end;
    RecFound=Next(CashDoc);
  end;
end;

macro TreatOpers

  var
    RecFound;

  ClearRecord(GroupMember);
  GroupMember.Branch = Branch;
  GroupMember.Date = {curdate};
  GroupMember.Group = OperBrigade;
  RecFound = GetGE(GroupMember);
  while(RecFound
    and (GroupMember.Branch == Branch)
    and (GroupMember.Date == {curdate})
    and (GroupMember.Group == OperBrigade))
    TreatDocsForOper;
    RecFound = Next(GroupMember);
  end;
end;

macro MakeRangeStr

  var
    Delim = "";

  if ( CashDoc.Series != "" )
    Delim = "-";
  end;
  if ( CashDoc.ValueCol == 1 )
    return CashDoc.Series + Delim + IntTo07Str( CashDoc.MinRegistrNumber );
  else
    return CashDoc.Series + Delim + IntTo07Str( CashDoc.MinRegistrNumber ) + " - " +
      CashDoc.Series + Delim + IntTo07Str( CashDoc.MaxRegistrNumber );
  end;
end;

macro FindCashVal( Type, Code )

  CashVal.TypeValue = Type;
  CashVal.CodIntValue = Code;
  return GetEQ( CashVal );
end;

macro Bad_TreatPrimary

  if ( CashVal.CodIntValue == VALFORM_BOOK )
    if( CashDoc.Ground != "" )
      [ Бланк: ############################################## Счет: ###########################   Кол-во:   1 шт. ]
      (GetValName:w, CashDoc.Ground);
    else
      [ Бланк: ############################################## Кол-во: ### шт. ]
      (GetValName:w, CashDoc.ValueCol);
    end;
  elif ( ( CashVal.CodIntValue == VALFORM_27 ) or ( CashVal.CodIntValue == VALFORM_31 ) )
    [ Бланк: ############################################## Номер(а): ######################### Кол-во: ### шт. ]
    (GetValName:w, MakeRangeStr, CashDoc.ValueCol);
  end;
end;

macro Bad_TreatDocsForOper

  var
    WDName = GetWorkDeskName( GroupMember.Oper, OperBrigade ),
    RecFound;

  CashDoc.Branch=Branch;
  CashDoc.CashDateDoc={curdate};
  CashDoc.TypeDoc=TYPEDOC_CARRYON;
  CashDoc.CashOper=WDName;
  CashDoc.RefValue=CashVal.RefValue;
  RecFound=GetGE(CashDoc);
  while(RecFound and
    (CashDoc.Branch==Branch) and
    (CashDoc.CashDateDoc=={curdate}) and
    (CashDoc.TypeDoc==TYPEDOC_CARRYON) and
    (CashDoc.CashOper==WDName) and
    (CashDoc.RefValue==CashVal.RefValue))
    if(((CashDoc.NumOper==CASHOP_CREDIT) or (CashDoc.NumOper==CASHOP_CHVALUE_1))
      and (CashDoc.Brigade == OperBrigade))
      Bad_TreatPrimary;
    end;
    RecFound=Next(CashDoc);
  end;
end;

macro Bad_TreatDocsForSafe

  var
    OldKey = KeyNum( CashDoc, 5 ),
    RecFound;

  ClearRecord( CashDoc );
  CashDoc.Branch=Branch;
  CashDoc.CashDateDoc={curdate};
  CashDoc.TypeDoc=TYPEDOC_CARRYON;
  CashDoc.CashPatern=Safe;
  CashDoc.RefValue=CashVal.RefValue;
  RecFound=GetGE(CashDoc);
  while(RecFound and
    (CashDoc.Branch==Branch) and
    (CashDoc.CashDateDoc=={curdate}) and
    (CashDoc.TypeDoc==TYPEDOC_CARRYON) and
    (CashDoc.CashPatern==Safe) and
    (CashDoc.RefValue==CashVal.RefValue))
    if((CashDoc.NumOper==CASHOP_CHVALUE_1)
      and (CashDoc.Brigade == OperBrigade))
      Bad_TreatPrimary;
    end;
    RecFound=Next(CashDoc);
  end;
  KeyNum( CashDoc, OldKey );
end;

macro TreatBadVF( Code )

  var
    RecFound;

  CashVal.Type = TYPE_RES_NOPAY;
  CashVal.TypeValue=CV_ValForm;
  CashVal.CodIntValue=Code;
  if(GetEQ(CashVal))
    ClearRecord(GroupMember);
    GroupMember.Branch = Branch;
    GroupMember.Date = {curdate};
    GroupMember.Group = OperBrigade;
    RecFound = GetGE(GroupMember);
    while(RecFound
      and (GroupMember.Branch == Branch)
      and (GroupMember.Date == {curdate})
      and (GroupMember.Group == OperBrigade))
      Bad_TreatDocsForOper;
      RecFound = Next(GroupMember);
    end;
    Bad_TreatDocsForSafe;
  end;
end;

macro TreatValForm(ValFormID)

  var
    NumLines;

  CashVal.Type=0;
  CashVal.TypeValue=CV_ValForm;
  CashVal.CodIntValue=ValFormID;
  if(GetEQ(CashVal))
    NewAccCnt=0;
    OnDemandCnt=0;
    ReplaceCnt=0;
    LineCnt=0;
    ASize(NewAcc,0);
    ASize(OnDemand,0);
    ASize(Replace,0);
    TreatOpers;
    NumLines=NewAccCnt;
    if(OnDemandCnt>NumLines)
      NumLines=OnDemandCnt;
    end;
    if(ReplaceCnt>NumLines)
      NumLines=ReplaceCnt;
    end;
    LineCnt=0;
    while(LineCnt<NumLines);
      OutBackLine;
      LineCnt=LineCnt+1;
    end;
  end;
end;

macro ReportOnBooks

  OutBackHead;
  if(not IsCur)
    TreatValForm(VALFORM_BOOK);
    TreatValForm(VALFORM_31);
    TreatValForm(VALFORM_2O);
  else
    TreatValForm(VALFORM_CUR_BOOK);
    TreatValForm(VALFORM_31);
  end;
  OutBadHead;
  TreatBadVF(VALFORM_BOOK);
  TreatBadVF(VALFORM_27);
  TreatBadVF(VALFORM_31);
  OutBackBottom;
end;

macro TreatCurrentSafe

  PrintLn;
  PrintLn( DeskSubj.Desc );
  PrintLn( MkStr( "=", StrLen( DeskSubj.Desc ) ) );
  PrintLn;
  ReportOnVFResources;
  PrintLn("\xOC");
  ReportOnBooks;
  PrintLn("\xOC");
end;

  var
    RegPath = "RS-RETAIL\\ПЕЧАТИ\\ФОРМА_78",
    Type = V_STRING,
    Value,
    RecFound,
    ErrCode;
  var
    RepeatCheckPrint = TRUE;

  CheckPrinter( RegPath, StrFor( 0 ) );

  OutTitle;

  if( GetProgramID != CodeFor( "Л" ) )       /* не Кладовая отделения */
    Safe = GetNativeSafe;
    DeskSubj.Branch = Branch;
    DeskSubj.Name = Safe;
    if ( GetEQ( DeskSubj ) )
      TreatCurrentSafe;
    end;
  else
    KeyNum( DeskSubj, 1 );
    ClearRecord( DeskSubj );
    DeskSubj.Branch = Branch;
    DeskSubj.Type = DST_PANTRY;
    RecFound = GetGE( DeskSubj );
    while ( RecFound
      and ( DeskSubj.Branch == Branch )
      and ( DeskSubj.Type == DST_PANTRY ) )
      Safe = DeskSubj.Name;
      TreatCurrentSafe;
      RecFound = Next( DeskSubj );
    end;
    KeyNum( DeskSubj, 0 );
    DeskSubj.Branch = Branch;
    DeskSubj.Name = RDesk;
    if ( GetEQ( DeskSubj ) )
      Safe = DeskSubj.Name;
      TreatCurrentSafe;
    end;
  end;
end;
