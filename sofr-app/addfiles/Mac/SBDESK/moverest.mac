
/* Перенос остатков между кассами */

import deprintr, DeskInter;

file DeskSubj("desksubj.dbt") key 0;
file DSubjType("desktype.dbt") key 0;
file CashAcc("sb_casac.dbt") key 0;
file RegNum("reg_num.dbt") key 0;
file CashVal("sb_casvl.dbt") key 0;
record CashParm("CashParm.dbt");

const
  DST_IO = 1,                           /* Инкассация */
  DST_SAFE = 2,                         /* Сейф */
  DST_WORKDESK = 3,                     /* Рабочий стол кассира */
  DST_RDESK = 4,                        /* Касса пересчета */
  DST_PANTRY = 5;                       /* Кладовая отделения */

const
  BS_DEP = 1,        
  BDBA_ONLINE = 0;

const
  RESMESUNIT_POINT = 1,  /* Поштучно   */
  RESMESUNIT_MONEY = 2,  /* Стоимость  */

  CASHOP_REINFORCE = 1,  /* Подкрепление кассира */
  CASHOP_RETURN    = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT   = 3,  /* Инкассация */
  CASHOP_ADVANCE   = 4,  /* Аванс */
  CASHOP_DEBIT     = 5,  /* Расход */
  CASHOP_CREDIT    = 6,  /* Приход */
  CASHOP_REMOVE    = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1  = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE   = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1 = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE   = 11,  /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1 = 12,  /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_PANT_RI   = 20, /* Подкрепление филиала */
  CASHOP_PANT_RET  = 21, /* Возврат из филиала */
  CASHOP_PANT_RIO  = 22, /* Подкрепление оперчасти */
  CASHOP_PANT_RETO = 23, /* Возврат из оперчасти */
  CASHOP_PANT_COL  = 24, /* Инкассация */
  CASHOP_PANT_ADV  = 25, /* Аванс */
  CASHOP_PANT_RDEB = 30, /* Расход по кассе пересчета */
  CASHOP_PANT_RCRD = 31, /* Приход по кассе пересчета */
  CASHOP_PANT_RDRT = 32, /* Сдача ресурса в кладовую из Кассы пересчета */

  SB_INCASH        = 100,
  SB_EXTCASH        = 150,

  INCASS           = "Инкассация";

var
  Dept, 
  Branch = NumFNCash,
  {curdate};

macro GetApplicationKind

  if ( ( CashParm.NumOper == CASHOP_ADVANCE )
    or ( CashParm.NumOper == CASHOP_COLLECT )
    or ( CashParm.NumOper == CASHOP_REMOVE_SAFE ) )
    return SB_EXTCASH;
  else
    return SB_INCASH;
  end;
end;

macro FindCashVal(RefValue);

  CashVal.RefValue = RefValue;
  return GetEQ(CashVal);
end;

macro FindDeskSubj( Name, Br )

  DeskSubj.Branch = Br;
  DeskSubj.Name = Name;
  return GetEQ( DeskSubj );
end;

macro FindDSubjType( ID )

  DSubjType.ID = ID;
  return GetEQ( DSubjType );
end;

macro IsSafe( Subj, Br, SubjType )

  if ( not FindDeskSubj( Subj, Br ) )
    MsgBox( "Субъект Кассы не найден: ", Subj );
    Exit( -1 );
  end;

  if ( not FindDSubjType( DeskSubj.Type ) )
    MsgBox( "Тип субъекта Кассы не найден. Код: ", DeskSubj.Type );
    Exit( -1 );
  end;

  SetParm( 2, DSubjType.ID );
  if ( DSubjType.IsSafe != StrFor( 0 ) )
    return true;
  else
    return false;
  end;
end;

macro MoveRanges

  var
    RecFound,
    Stat = true;

  ClearRecord(RegNum);
  RegNum.Branch = CashAcc.Branch;
  RegNum.CashOper = CashAcc.CashOper;
  RegNum.SubAccount = CashAcc.SubAccount;
  RegNum.ValueRef = CashAcc.RefValue;
  RegNum.Date = CashAcc.Date;
  RegNum.Nominal = $100000000;
  RecFound = GetGE(RegNum);
  while(RecFound and Stat
    and (RegNum.Branch == CashAcc.Branch)
    and (RegNum.CashOper == CashAcc.CashOper)
    and (RegNum.SubAccount == CashAcc.SubAccount)
    and (RegNum.ValueRef == CashAcc.RefValue)
    and (RegNum.Date == CashAcc.Date))
    CashParm.ValueCol = RegNum.Max - RegNum.Min + 1;
    CashParm.ValueMoney = $0;
    CashParm.Series = RegNum.Series;
    CashParm.Nominal = RegNum.Nominal;
    CashParm.MinRegistrNumber = RegNum.Min;
    CashParm.ApplicationKey = FormApplicationKey(GetApplicationKind);
    Stat = InitCashDoc(CashParm);
    RecFound = Next(RegNum);
  end;
  return Stat;
end;

macro MoveAccRests

  var
    Stat = true;

  if(FindCashVal(CashAcc.RefValue))

    /* Несверенные счета пропускаем */
    if ( CashVal.NeedsVerif and ( not CashAcc.IsVerified ) )
      return true;
    end;

    CashParm.Type = CashVal.Type;
    CashParm.TypeValue = CashVal.TypeValue;
    CashParm.CodIntValue = CashVal.CodIntValue;
    if(CashVal.UnitMeas == RESMESUNIT_MONEY)
      CashParm.ValueMoney = CashAcc.RestMoney;
      CashParm.ValueCol = 0;
      CashParm.ApplicationKey = FormApplicationKey(GetApplicationKind);
      Stat = InitCashDoc(CashParm);
    else
      if(not CashVal.HasRegNumber)
        CashParm.ValueCol = CashAcc.RestCol;
        CashParm.ValueMoney = CashAcc.RestMoney;
        CashParm.ApplicationKey = FormApplicationKey(GetApplicationKind);
        Stat = InitCashDoc(CashParm);
      else
        Stat = MoveRanges;
      end;
    end;
    if(not Stat)
      MsgBox("Ошибка при переносе ресурса|", CashVal.NameValue);
    end;
  else
    MsgBox("Ресурс кассы не найден, код:|", CashAcc.RefValue);
    Stat = false;
  end;
  return Stat;
end;

macro MoveDeskRests(OperID_From, OperID_To, Branch_To )

  var
    TypeTo = 0,
    Stat = true,
    FromSafe = false,
    ToSafe = false,
    RecFound;

  ClearRecord(CashParm);
  CashParm.CashDateDoc = {curdate};
  if( IsSafe( OperID_From, Branch ) )
    FromSafe = true;
  end;
  if( IsSafe( OperID_To, Branch_To, TypeTo ) )
    ToSafe = true;
  end;
  if((not FromSafe) and (not ToSafe))
    CashParm.NumOper    = CASHOP_REMOVE;
    CashParm.CashOper   = OperID_From;
    CashParm.CashPatern = OperID_To;  
  elif((FromSafe and ToSafe))
    if ( ( TypeTo == DST_IO ) or ( TypeTo == DST_RDESK ) or ( TypeTo == DST_PANTRY ) )
      CashParm.NumOper  = CASHOP_COLLECT;
    else
      CashParm.NumOper  = CASHOP_REMOVE_SAFE;
    end;    
    CashParm.CashOper   = OperID_To;
    CashParm.CashPatern = OperID_From;  
  elif((not FromSafe) and ToSafe)
    CashParm.NumOper    = CASHOP_RETURN;
    CashParm.CashOper   = OperID_From;
    CashParm.CashPatern = OperID_To;
  else /* From safe but not to safe */
    if ( ( TypeTo == DST_IO ) or ( TypeTo == DST_RDESK ) or ( TypeTo == DST_PANTRY ) )
      CashParm.NumOper  = CASHOP_COLLECT;
    else
      CashParm.NumOper  = CASHOP_REINFORCE;
    end;
    CashParm.CashOper   = OperID_To;
    CashParm.CashPatern = OperID_From;
  end;
  CashParm.DestBranch   = Branch_To;
  CashParm.ApplicationKind = GetApplicationKind;

  ClearRecord(CashAcc);
  CashAcc.Branch=Branch;
  CashAcc.Date = {curdate};
  CashAcc.CashOper = OperID_From;
  CashAcc.SubAccount = "";
  RecFound = GetGE(CashAcc);
  while(RecFound and Stat
    and (CashAcc.Branch==Branch)
    and (CashAcc.Date == {curdate})
    and (CashAcc.CashOper == OperID_From)
    and (CashAcc.SubAccount == ""))
    if((CashAcc.RestMoney != 0) or (CashAcc.RestCol != 0))
      Stat = MoveAccRests;
    end;
    RecFound = Next(CashAcc);
  end;
  return Stat;
end;







