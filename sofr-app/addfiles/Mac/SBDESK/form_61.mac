/* Форма 61 */

import BankInter, deprintr, ChkPrn, cur_rate;

file Alg("namealg.dbt","bank.def") key 0;
file CashD("sb_casdc.dbt") key 1;
file Bunch  ("bunch.dbt")     key 0;
file CashLink("sb_casln.dbt") key 0;
file CashValue("sb_casvl.dbt") key 0;
file currency(currency) key 0;

const
  RESMESUNIT_POINT = 1,  /* Поштучно  */
  RESMESUNIT_MONEY = 2;  /* Стоимость */

const
  CV_Currency = 1,
  CV_ValForm = 2,
  CV_MinCI = 500;

const
  SB_NOTBALANCE = 1000;

const
  CASHOP_REINFORCE = 1,  /* Подкрепление кассира */
  CASHOP_RETURN    = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT   = 3,  /* Инкассация */
  CASHOP_ADVANCE   = 4,  /* Аванс */
  CASHOP_DEBIT     = 5,  /* Расход */
  CASHOP_CREDIT    = 6,  /* Приход */
  CASHOP_REMOVE    = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1  = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE   = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1 = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE   = 11,  /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1 = 12,  /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_PANT_RI   = 20, /* Подкрепление филиала */
  CASHOP_PANT_RET  = 21, /* Возврат из филиала */
  CASHOP_PANT_RIO  = 22, /* Подкрепление оперчасти */
  CASHOP_PANT_RETO = 23, /* Возврат из оперчасти */
  CASHOP_PANT_COL  = 24, /* Инкассация */
  CASHOP_PANT_ADV  = 25, /* Аванс */
  CASHOP_PANT_RDEB = 30, /* Расход по кассе пересчета */
  CASHOP_PANT_RCRD = 31, /* Приход по кассе пересчета */
  CASHOP_PANT_RDRT = 32; /* Сдача ресурса в кладовую из Кассы пересчета */

const
  MultBy100 = true;

const
  ALG_CASH_VAL_TYPE = 811;    /* Типы ресурсов кассы в namealg.dbt */  

var
  {oper},
  {curdate},
  Branch = NumFNCash,
  TotSumCCI=$0,
  TotSumVF=$0,
  CopyCode,
  SingleDoc = false,
  ApplicationKind,
  ApplicationKey;

array ACopyCode;
  ACopyCode(0) = "А";
  ACopyCode(1) = "Б";

/**************************************************************************/
/*  Получить название типа ресурса кассы                                  */
macro GetCashTypeName(Type)

  Alg.iTypeAlg = ALG_CASH_VAL_TYPE;
  Alg.iNumberAlg = Type;
  if(GetEQ(Alg))
    return Alg.szNameAlg;
  else
    return String(Type);
  end;
end;

/**************************************************************************/
/*  Получить название ресурса кассы                                       */
macro GetValName
  var str = CashValue.NameValue;
  if ( CashValue.Type != 0 )
    str = str + " (" + GetCashTypeName(CashValue.Type) + ")";
  end;
  return str;
end;

macro BranchName

  var dep_name;
  
  if ( not findDepartment(Branch, dep_name) )
    dep_name = "";
  end;
  return dep_name;
end;


macro FindCurrency (Code)
  currency.Code_Currency = Code;
  return GetEQ(currency);
end;


macro CashOpToNBalOp(CashOp)

  if(CashOp == CASHOP_CREDIT)
    return 1;
  elif(CashOp == CASHOP_DEBIT)
    return 2;
  elif(CashOp == CASHOP_ADVANCE)
    return 3;
  elif(CashOp == CASHOP_COLLECT)
    return 4;
  elif((CashOp == CASHOP_PANT_RET) or (CashOp == CASHOP_PANT_RETO))
    return 5;
  elif((CashOp == CASHOP_PANT_RI) or (CashOp == CASHOP_PANT_RIO))
    return 6;
  elif(CashOp == CASHOP_PANT_ADV)
    return 7;
  elif(CashOp == CASHOP_PANT_COL)
    return 8;
  end;
  return 0;
end;

macro GetOperName

  file Person( "person.dbt", "bank.def" ) key 0;

  Person.Oper = {oper};
  if ( GetEQ( Person )  )
    return Person.Name;
  else
    return 0;
  end;
end;

macro OutHead

  [Учреждение Сбербанка № #                                                          Ф. № 61

                                          О Р Д Е Р
                   на пересылку наличных денег и прочих ценностей (экз. #)
                                                                                   #
   Выдать через: #
   для доставки в учреждение  Сбербанка № ____________________

  ](BranchName,CopyCode,{curdate}:m, GetOperName);
  if ( CashD.NumOper == CASHOP_PANT_RDEB )
    [ ];
    [ К выдаче        Управляющий __________         Гл. бухгалтер ___________ ];
    [ ];
  end;
end;

macro OutSubHeadCCI

  [+-------------------------------------------------------------------------------------------------------+
   |        Н а и м е н о в а н и е         |      С у м м а      |       Дебет        |      Кредит       |
   |                                        |                     |     или приход     |    или расход     |
   +-------------------------------------------------------------------------------------------------------+];
end;

macro OutLineCCI

  var
    DebetAcc,
    CreditAcc,
    Sum,
    rate = 0.0,
    RubEqv;

  if(CashValue.UnitMeas==RESMESUNIT_POINT)
    Sum=CashD.ValueCol;
    if(MultBy100)
      Sum=Money(Sum*100);
    end;
  else
    Sum=CashD.ValueMoney;
  end;

  GetBookAccounts(
    SB_NOTBALANCE,
    CashOpToNBalOp(CashD.NumOper),
    CashValue.TypeValue,
    Branch,
    CashValue.RefValueReport,
    CashValue.CodIntValue,
    CashValue.Type,
    DebetAcc,
    CreditAcc);

  [ ######################################## ##################### #################### ################### ]
  (GetValName:w,Sum,Acc_Form(DebetAcc),Acc_Form(CreditAcc));
  TotSumCCI=TotSumCCI+Sum;

  /* Для валюты - эквивалент в рублях */
  RubEqv = "";
  if ( ( CashValue.TypeValue == CV_Currency) and ( CashValue.CodIntValue > 0 ) )
    if ( FindCurrency( CashValue.CodIntValue ) )
      if ( not GetAllCurrRate( {curdate}, CashValue.CodIntValue, rate ) )
        rate = 0.0;
      end;

      if ( rate != 0 )
        RubEqv = String( Money( Sum * rate ):21:2 );
      end;
    end;
    [                      рублевый эквивалент #####################]
    (RubEqv);
  end;

end;

macro OutTotalsCCI

  [ ------------------------------------------------------------------------------------------------------- ];
  [ И т о г о                                #####################                                          ]
  (TotSumCCI);
end;

macro OutSubHeadVF

  [+----------------------------------------------------------------------------------------------------+
   |                                        |         |                           |                     |
   |      Наименование ценных бланков       |  Серия  |       №№ бланков          |      С у м м а      |
   |                                        |         |                           |                     |
   +----------------------------------------------------------------------------------------------------+];
end;

macro MakeNumStr(Series, Number)

  if(Series != "")
    return Series + "-" + String(Number);
  else
    return String(Number);
  end;
end;

macro OutLineVF

  var
    ValName = GetValName(),
    NumbersStr,
    Sum = 0,
    rate = 0.0,
    RubEqv,
    BunchFound;

  if(CashD.MinRegistrNumber or ( not CashValue.HasRegNumber ))

    if(CashValue.UnitMeas==RESMESUNIT_POINT)
      Sum=CashD.ValueCol;
      if(MultBy100)
        Sum=Money(Sum*100);
      end;
    else
      Sum=CashD.ValueMoney;
    end;
    TotSumVF=TotSumVF+Sum;

    if(CashD.MaxRegistrNumber
      and (CashD.MinRegistrNumber!=CashD.MaxRegistrNumber))
      NumbersStr = MakeNumStr(CashD.Series, CashD.MinRegistrNumber) +
        " - " + MakeNumStr(CashD.Series, CashD.MaxRegistrNumber);
    elif(( CashD.MinRegistrNumber ) and ( CashD.MinRegistrNumber==CashD.MaxRegistrNumber ))
      NumbersStr = MakeNumStr(CashD.Series, CashD.MinRegistrNumber);
    else                                      
      NumbersStr = "";
    end;
  
    [ ########################################           ########################### ##################### ]
    (
      ValName:w,
      NumbersStr,
      Sum
    );

  else

    ClearRecord( Bunch );
    Bunch.BagAppKind = CashD.ApplicationKind;
    Bunch.BagAppKey = CashD.ApplicationKey;
    BunchFound = GetGE( Bunch );
    while ( BunchFound
      and ( Bunch.BagAppKind == CashD.ApplicationKind )
      and ( Bunch.BagAppKey == CashD.ApplicationKey ) )

         if(Bunch.Min and Bunch.Max and (Bunch.Min!=Bunch.Max))
           NumbersStr = MakeNumStr(Bunch.Series, Bunch.Min) +
             " - " + MakeNumStr(Bunch.Series, Bunch.Max);
         elif(Bunch.Min and Bunch.Max and (Bunch.Min==Bunch.Max))
           NumbersStr = MakeNumStr(Bunch.Series, Bunch.Min);
         else
           NumbersStr = "";
         end;

         Sum = (Bunch.Max - Bunch.Min + 1);
         TotSumVF=TotSumVF+Sum;

         [ ########################################           ########################### ##################### ]
         (
           ValName:w,
           NumbersStr,
           Sum
         );
         BunchFound = Next( Bunch );
    end;

  end;

  /* Для валюты - эквивалент в рублях */
  RubEqv = "";
  if ( ( CashValue.TypeValue == CV_Currency ) and ( CashValue.CodIntValue > 0 ) )
    if ( FindCurrency( CashValue.CodIntValue ) )
      if ( not GetAllCurrRate( {curdate}, CashValue.CodIntValue, rate ) )
        rate = 0.0;
      end;

      if ( rate != 0 )
        RubEqv = String( Money( Sum * rate ):21:2 );
      end;
    end;
    [                      рублевый эквивалент                                       #####################]
    (RubEqv);
  end;

end;


macro OutTotalsVF

  [ ---------------------------------------------------------------------------------------------------- ];
  [ И т о г о                                                                      ##################### ]
  (TotSumVF);
end;

macro DownFirstLetter (InString)

  var OutString = SubStr(InString,2);
  var FirstLetter = SubStr(InString,1,1);

  if (FirstLetter == "А")
    FirstLetter = "а";
  elif (FirstLetter == "Б")
    FirstLetter = "б";
  elif (FirstLetter == "В")
    FirstLetter = "в";
  elif (FirstLetter == "Д")
    FirstLetter = "д";
  elif (FirstLetter == "К")
    FirstLetter = "к";
  elif (FirstLetter == "Л")
    FirstLetter = "л";
  elif (FirstLetter == "М")
    FirstLetter = "м";
  elif (FirstLetter == "Н")
    FirstLetter = "н";
  elif (FirstLetter == "О")
    FirstLetter = "о";
  elif (FirstLetter == "П")
    FirstLetter = "п";
  elif (FirstLetter == "Р")
    FirstLetter = "р";
  elif (FirstLetter == "С")
    FirstLetter = "с";
  elif (FirstLetter == "Т")
    FirstLetter = "т";
  end;

  OutString = FirstLetter + OutString;

  return OutString;

end;

macro OutBottomB

  var TotalSum,rate = 0.0;
  var ValName = "",Rub,Kop,RubEqv,ForCurs;
  var StrNa = "";
  var SS = $0.0L;

  if (CashValue.UnitMeas == RESMESUNIT_POINT)
    RubToStr(TotSumCCI+TotSumVF,Rub,Kop);
    TotalSum = Rub + " шт.";
    StrNa = "штук"
  else
    if ((CashValue.TypeValue == CV_Currency) AND (CashValue.CodIntValue == 0))
      RubToStr(TotSumCCI+TotSumVF,Rub,Kop);
      TotalSum = Rub + " руб. ";
      RubToStr($1.0 * Int(Kop),Rub,Kop);
      Rub = DownFirstLetter(Rub);
      TotalSum = TotalSum + Rub + " коп.";
      StrNa = "на руб.";
    else
      StrNa = "на ";
      if (FindCurrency(CashValue.CodIntValue))
        ValName = currency.Short_Name;
        StrNa = StrNa + currency.Short_Name;
      else
        ValName = "";
      end;
      RubToStr(TotSumCCI+TotSumVF,Rub,Kop);
      RubToStr($1.0 * Int(Kop),Kop,NULL);
      Kop = DownFirstLetter(Kop);
      TotalSum = Rub + " " + ValName + " " + Kop;
    end;
  end;

  [
               Контролер                                                   Кассир
   -----------------------------------------------------------------------------------------------------
   Перечисленные в ордере ценности и наличные деньги всего на сумму
   #]
   (TotalSum);
  /* Для валюты - эквивалент в рублях */
  if ((CashValue.TypeValue == CV_Currency) AND (CashValue.CodIntValue > 0))
    RubEqv = "в рублевом эквиваленте ";
    ForCurs = "по курсу ";
    if (FindCurrency(CashValue.CodIntValue))
      if ( not GetAllCurrRate( {curdate}, CashValue.CodIntValue, rate ) )
        rate = 0.0;
      end;

      if ( rate != 0 )
        RubEqv = RubEqv + RubToStr(Money((TotSumCCI+TotSumVF) * rate));
        ForCurs = ForCurs + Trim(String(rate:15:4)) + " Руб/" + currency.Short_Name;
      end;
    end;
    [#](RubEqv);
    [#](ForCurs);
  end;
  [для доставки в учреждение Сбербанка № _________ получил #                                   _________
                                                                                               (подпись)
   -----------------------------------------------------------------------------------------------------
   Перечисленные в ордере ценности и наличные деньги всего #       #####################################
   высланы почтой #                                    квитанция № _____________________________________

               Контролер                                                   Кассир
  ]({curdate}:m,StrNa,TotSumCCI+TotSumVF,{curdate}:m);
  [ ];
  [---------------------------------------------------------------------------------------------------];
  [ ];
  TotSumCCI=$0;
  TotSumVF=$0;
end;

macro OutBottomA

  var TotalSum;
  var ValName = "",Rub,Kop,RubEqv,ForCurs,rate=0.0;

  if (CashValue.UnitMeas == RESMESUNIT_POINT)
    RubToStr(TotSumCCI+TotSumVF,Rub,Kop);
    TotalSum = Rub + " шт.";
  else
    if ((CashValue.TypeValue == CV_Currency) AND (CashValue.CodIntValue == 0))
      RubToStr(TotSumCCI+TotSumVF,Rub,Kop);
      TotalSum = Rub + " руб. ";
      RubToStr($1.0 * Int(Kop),Rub,Kop);
      Rub = DownFirstLetter(Rub);
      TotalSum = TotalSum + Rub + " коп.";
    else
      if (FindCurrency(CashValue.CodIntValue))
        ValName = currency.Short_Name;
      else
        ValName = "";
      end;
      RubToStr(TotSumCCI+TotSumVF,Rub,Kop);
      RubToStr($1.0 * Int(Kop),Kop,NULL);
      Kop = DownFirstLetter(Kop);
      TotalSum = Rub + " " + ValName + " " + Kop;
    end;
  end;

  [
               Контролер                                                   Кассир
   -----------------------------------------------------------------------------------------------------
   Перечисленные в ордере ценности и наличные деньги всего на сумму
   #]
   (TotalSum);
  /* Для валюты - эквивалент в рублях */
  if ((CashValue.TypeValue == CV_Currency) AND (CashValue.CodIntValue > 0))
    RubEqv = "в рублевом эквиваленте ";
    ForCurs = "по курсу ";
    if (FindCurrency(CashValue.CodIntValue))
      if ( not GetAllCurrRate( {curdate}, CashValue.CodIntValue, rate ) )
        rate = 0.0;
      end;

      if (rate != 0)
        RubEqv = RubEqv + RubToStr(Money((TotSumCCI+TotSumVF) * rate));
        ForCurs = ForCurs + Trim(String(rate):15:4) + " Руб/" + currency.Short_Name;
      end;
    end;
    [#](RubEqv);
    [#](ForCurs);
  end;
  [приняты и оприходованы по операционному дневнику, приложению ф № 25-а и книге кладовой
   #
               Контролер                                        Кассир
  ]({curdate}:m);
  [ ];
  Print("\x0C");
  [ ];
  TotSumCCI=$0;
  TotSumVF=$0;
end;

macro IsSuitableDoc

  return TRUE;

  /* return
    (
      (CopyCode=="А") and
      (
        (CashD.NumOper==CASHOP_ADVANCE) or
        (CashD.NumOper==CASHOP_COLLECT) or
        (CashD.NumOper==CASHOP_PANT_ADV) or
        (CashD.NumOper==CASHOP_PANT_COL) or
        (CashD.NumOper==CASHOP_PANT_RI) or
        (CashD.NumOper==CASHOP_PANT_RET) or
        (CashD.NumOper==CASHOP_PANT_RIO) or
        (CashD.NumOper==CASHOP_PANT_RETO)
      )
    )
    or
    (
      (CopyCode=="Б") and
      (
        (CashD.NumOper==CASHOP_REINFORCE) or
        (CashD.NumOper==CASHOP_RETURN)
      )
    ) */
end;

macro TreatDocCCI

  var
    i;

  if(IsSuitableDoc)
    i = 0;
    while (i < ASize(ACopyCode))
      CopyCode = ACopyCode(i);
      TotSumCCI = 0;
      OutHead;
      OutSubHeadCCI;
      OutLineCCI;
      OutTotalsCCI;
      if(CopyCode == "А")
        OutBottomA;
      else
        OutBottomB;
      end;
      i = i + 1;
    end;
  end;
end;

macro TreatDocVF

  var
    i;

  if(IsSuitableDoc)
    i = 0;
    while (i < ASize(ACopyCode))
      CopyCode = ACopyCode(i);
      TotSumVF = 0;
      OutHead;
      OutSubHeadVF;
      OutLineVF;
      OutTotalsVF;
      if(CopyCode == "А")
        OutBottomA;
      else
        OutBottomB;
      end;
      i = i + 1;
    end;
  end;
end;

macro ReportCCI

  var
    RecFound;

  ClearRecord(CashLink);
  CashLink.ApplicationKind1=ApplicationKind;
  CashLink.ApplicationKey1=ApplicationKey;
  RecFound=GetGE(CashLink);
  while(RecFound
    and (CashLink.ApplicationKind1==ApplicationKind)
    and (CashLink.ApplicationKey1==ApplicationKey))
    if ( CashLink.RefValue2 )
    CashValue.RefValue=CashLink.RefValue2;
    if(not GetEQ(CashValue))
      MsgBox("Ресурс кассы не найден");
    end;
    CashD.ApplicationKind=CashLink.ApplicationKind2;
    CashD.ApplicationKey=CashLink.ApplicationKey2;
    if(not GetEQ(CashD))
      MsgBox("Документ не найден");
    end;
    if((CashValue.TypeValue==CV_Currency) or (CashValue.TypeValue>=CV_MinCI))
      TreatDocCCI;
    end;
    end;
    RecFound=Next(CashLink);
  end;
end;

macro ReportVF

  var
    RecFound;

  ClearRecord(CashLink);
  CashLink.ApplicationKind1=ApplicationKind;
  CashLink.ApplicationKey1=ApplicationKey;
  RecFound=GetGE(CashLink);
  while(RecFound
    and (CashLink.ApplicationKind1==ApplicationKind)
    and (CashLink.ApplicationKey1==ApplicationKey))
    if ( CashLink.RefValue2 > 0 )
      CashValue.RefValue=CashLink.RefValue2;
      if(not GetEQ(CashValue))
        MsgBox("Ресурс кассы не найден");
      end;
      CashD.ApplicationKind=CashLink.ApplicationKind2;
      CashD.ApplicationKey=CashLink.ApplicationKey2;
      if(not GetEQ(CashD))
        MsgBox("Документ не найден");
      end;
      if(CashValue.TypeValue==CV_ValForm)
        TreatDocVF;
      end;
    end;
    RecFound=Next(CashLink);
  end;
end;

macro PrintForm61(AppKind,AppKey,Single)

  var
    RegPath = "RS-RETAIL\\ПЕЧАТИ\\КАССА\\ОРДЕР_61",
    Type = V_STRING,
    Value,
    ErrCode;
  var
    RepeatCheckPrint = TRUE;

  if(ValType(Single) == V_BOOL)
    SingleDoc = Single;
  end;

  CheckPrinter( RegPath, StrFor( 0 ) );

  ApplicationKind=AppKind;
  ApplicationKey=AppKey;

  if(not SingleDoc)
    ReportCCI;
    ReportVF;
  else
    CashD.ApplicationKind=ApplicationKind;
    CashD.ApplicationKey=ApplicationKey;
    if(not GetEQ(CashD))
      MsgBox("Документ не найден");
    else
      CashValue.RefValue=CashD.RefValue;
      if(not GetEQ(CashValue))
        MsgBox("Ресурс кассы не найден");
      else
        if(CashValue.TypeValue==CV_ValForm)
          TreatDocVF;
        else
          TreatDocCCI;
        end;
      end;
    end;
  end;
end;

end;
