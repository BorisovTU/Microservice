
/* Отчет по переоценке драгметаллов */

import CommonInter, DeskInter, ingot, Revalue, rsd;

file CashAcc( "sb_casac.dbt" ) key 0;
file DeskSubj( "desksubj.dbt" ) key 1;
file CIMisc( "ci_misc.dbt" ) key 0;
file CashVal( "sb_casvl.dbt" ) key 0;
file F68Tmp( "f68.tmp" ) key 1 write;
file CICode( "ci_code.dbt" ) key 0;

const
  DST_IO          = 1,    /* Инкассация */
  DST_SAFE        = 2,    /* Сейф */
  DST_WORKDESK    = 3,    /* Рабочий стол кассира */
  DST_RDESK       = 4,    /* Касса пересчета */
  DST_PANTRY      = 5,    /* Кладовая отделения */
  DST_EDESK       = 6;    /* Вечерняя касса */

const               
  CV_Currency = 1,
  CV_ValForm = 2,
  CV_MinCI = 500;

const
  CI_OGSZ = 1,
  CI_LOTTERY = 2,
  CI_CERT = 3,
  CI_COIN = 4,
  CI_INGOT = 5,
  CI_MISC = 10;

const
  SB_NOTBALANCE = 1000;

const
  CASHOP_REVAL_POS = 18, /* Переоценка (+) */
  CASHOP_REVAL_NEG = 19; /* Переоценка (-) */

var
  {curdate},
  {oper},
  OperBrigade      = GetOperBrigade,
  Branch           = NumFNCash,
  HeadPrinted;

const
  TmpFileName = "f68" + String( {oper} ) + ".dbt";

var
  TotOldPrevRestR = $0,
  TotNewPrevRestR = $0,
  TotRevalue = $0;

macro ClearTmpFile
  var cmd;
  cmd = RsdCommand( NULL, string("truncate table df68_tmp") );
  cmd.execute();
end;

macro FindCiCode( ref )

  CiCode.AutoKey = ref;
  if ( not GetEQ( CiCode ) )
    runError( "Не найдена запись в файле ci_code.dbt" );
  end;
end;

macro GetHallmark( ref )

  FindCiCode( ref );
  return CiCode.Double;
end;

macro GetBranchName

  var
    RetVal;    

  if(not findDepartment(Branch, RetVal))     
    RetVal=String(Branch);
  end;          
  return RetVal;
end;

macro FindCIMisc( Type, IssueID )

  CIMisc.Type = Type;
  CIMisc.IssueID = IssueID;
  return GetEQ( CIMisc );
end;

macro FindCashVal( ValueRef )

  CashVal.RefValue = ValueRef;
  return GetEQ( CashVal );
end;

macro GetRates( Type, Issue, Dd, PrevRate, Rate )

  var
    PrevRt = 0,
    Rt = 0;
      
  PrevRt = ing_getRubRate( Issue, Dd - 1 );
  Rt = ing_getRubRate( Issue, Dd );

  SetParm( 3, PrevRt );
  SetParm( 4, Rt );
end;

macro GetPrevRest

  record SaveCashAcc( "sb_casac.dbt" );

  var
    PrevRest  = $0, 
    PrevKey = KeyNum( CashAcc, 2 );

  GetPos( CashAcc );

  Copy( SaveCashAcc, CashAcc );

  if ( GetLT( CashAcc )
    and ( CashAcc.Branch == SaveCashAcc.Branch )
    and ( CashAcc.CashOper == SaveCashAcc.CashOper )
    and ( CashAcc.SubAccount == "" )
    and ( CashAcc.RefValue == SaveCashAcc.RefValue ) )
    PrevRest = CashAcc.RestCol;
  end;

  KeyNum( CashAcc, PrevKey );
  GetDirect( CashAcc );
  return PrevRest;
end;

macro OutHead

  PrintLn;
  PrintLn( DeskSubj.Desc );
  PrintLn( MkStr( "=", StrLen( DeskSubj.Desc ) ) );
  PrintLn;

  [-----------------------------------------------------------------------------------------------------------------------------];
  [|                                            |         | Курс  |       Остаток      |             | Курс  |      Остаток      |];
  [|   В и д   с л и т к а                      | Масса,г |  до   |          до        |   Пересчет  | после |       после       |];
  [|                                            |         |переоц.|      переоценки    |             |переоц.|     переоценки    |];                       
  [-----------------------------------------------------------------------------------------------------------------------------];
end;

macro OutLine

  var
    PrevRate, Rate,
    PrevRest = GetPrevRest,
    OldPrevRestR, NewPrevRestR, 
    Hm;

  FindCIMisc( CashVal.TypeValue - CV_MinCI, CashVal.CodIntValue );

  GetRates( CIMisc.Type, CIMisc.IssueID, CashAcc.Date, PrevRate, Rate );
  Hm = GetHallmark( CIMisc.HallmarkRef );

  if ( CIMisc.UseWeightType == IWT_PURE )
    OldPrevRestR = Money( PrevRest * ing_roundWeight( CIMisc.Weight * Hm / 1000, CIMisc.PWRoundType, CIMisc.PWRoundBase ) * PrevRate / 100 ); 
  else
    OldPrevRestR = Money( PrevRest * CIMisc.Weight * PrevRate / 100 ); 
  end;

  NewPrevRestR = OldPrevRestR + GetRevalueSum( CashAcc.RefValue, CashAcc.Date, CashAcc.CashOper );

  if ( OldPrevRestR != NewPrevRestR )

    if ( not HeadPrinted );
      OutHead;
      HeadPrinted = true;
    end;                              

                        
    [ ########################################## ########### ####### #################### ############# ####### ################### ]                       
    (
      CashVal.NameValue,
      double (PrevRest * CIMisc.Weight / 10000),
      PrevRate:0:3,
      OldPrevRestR,
      NewPrevRestR - OldPrevRestR,
      Rate:0:3,
      NewPrevRestR 
    );

    ClearRecord( F68Tmp );
    F68Tmp.CurrCode = CashVal.CodIntValue;
    F68Tmp.CurrName = SubStr( CashVal.NameValue, 1, 25 );
    F68Tmp.CurrPrevRate = PrevRate;
    F68Tmp.CurrCurRate = Rate;
    F68Tmp.ValueType = CashVal.TypeValue;
    F68Tmp.ValueCode = CashVal.CodIntValue;
    F68Tmp.InRest = F68Tmp.OutRest = PrevRest;
    F68Tmp.InRestR = OldPrevRestR;   
    F68Tmp.OutRestR = NewPrevRestR;   
    Insert( F68Tmp );

    TotOldPrevRestR = TotOldPrevRestR + OldPrevRestR;
    TotNewPrevRestR = TotNewPrevRestR + NewPrevRestR;
  end;
end;

macro OutTotals

  [-----------------------------------------------------------------------------------------------------------------------------];
  [ И т о г о :                                                    #################### #############         ################### ]                       
  (
    TotOldPrevRestR,
    TotNewPrevRestR - TotOldPrevRestR,
    TotNewPrevRestR 
  );
end;

macro TreatCurrentSafe

  var
    RecFound;

  HeadPrinted = false;
  TotOldPrevRestR = TotNewPrevRestR = $0;

  ClearTmpFile;
  ClearRecord( CashAcc );
  CashAcc.Branch = Branch;
  CashAcc.Date = {curdate};
  CashAcc.CashOper = DeskSubj.Name;
  CashAcc.SubAccount = "";
  RecFound = GetGE( CashAcc );
  while ( RecFound
    and ( CashAcc.Branch == Branch )
    and ( CashAcc.Date == {curdate} )
    and ( CashAcc.CashOper == DeskSubj.Name )
    and ( CashAcc.SubAccount == "" ) )
    FindCashVal( CashAcc.RefValue );
    if ( CashVal.TypeValue == CV_MinCI + CI_INGOT )
      OutLine;
    end;
    RecFound = Next( CashAcc );
  end;
  if ( HeadPrinted )
    OutTotals;
    PrintLn( StrFor( 12 ) );
  end;

end;

  var
    RecFound;

  [ Филиал #        ]
  (GetBranchName);
  [ ];
  [                                    Ведомость переоценки драгметаллов ];
  [ ];
  [ ############ ]
  (String({curdate}));
  [ ];
  ClearRecord( DeskSubj );
  DeskSubj.Branch = Branch;
  DeskSubj.Type = DST_PANTRY;
  RecFound = GetGE( DeskSubj );
  while ( RecFound
    and ( DeskSubj.Branch == Branch )
    and ( DeskSubj.Type == DST_PANTRY ) )
    TreatCurrentSafe;
    RecFound = Next( DeskSubj );
  end;
end;