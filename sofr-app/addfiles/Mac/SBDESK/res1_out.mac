
/* Ф. 25-а -- функции вывода */

import ResComm, BrParm, CommonInter;

macro OutTitle;

  var
    brName;

  GetBranchParams( NumFNCash, brName );

  [
   Филиал № #                                                                                                      Форма № 25-а ]
  ( brName );
  [
                                                  Приложение к операционному дневнику № ####
                                                                 за ###########

   Бригада № ###

  ]
  (GetDiaryNo,String({curdate}),OperBrigade);
end;

macro OutCoinHeader;

  [ ];
  [ ];
  [                                                 Монеты, учитываемые по номиналу ];
  [ ];
  [+-------------------------+-------+--------+----------------------+-------------------------------------------+---------------------+];
  [|                         |       |        |       Остаток        |                  Обороты                  |       Остаток       |];
  [|       Наименование      |       |        |      на начало       |---------------------+---------------------+      на  конец      |];
  [|                         | Проба | Номин. |         дня          |       Приход        |       Расход        |         дня         |];
  [|   монеты, год выпуска   |       |        +----------+-----------+----------+----------+----------+----------+----------+----------+];
  [|                         |       |        |  Кол-во  |   Сумма   |  Кол-во  |  Сумма   |  Кол-во  |  Сумма   |  Кол-во  |  Сумма   |];
  [+-------------------------+-------+--------+----------+-----------+----------+----------+----------+----------+----------+----------+];
end;

macro OutCoinLine(NameVal,PrevRest,IPrevRest,CurRest,ICurRest,Cred,ICred,OtherCred,IOtherCred,Deb,IDeb,OtherDeb,IOtherDeb);

  if((not ShowNullLines) and (PrevRest==$0) and (Cred==$0) and (OtherCred==$0)
     and (Deb==$0) and (OtherDeb==$0) and (CurRest==$0)
     and (IPrevRest==0) and (ICred==0) and (IOtherCred==0)
     and (IDeb==0) and (IOtherDeb==0) and (ICurRest==0))
    return;
  end;

  if ( not MetalSubheadPrinted )
    PrintLn;
    PrintLn( CICode.String );
    PrintLn( MkStr( "=", StrLen( CICode.String ) ) );
    PrintLn;
    [+-------------------------+-------+--------+----------+-----------+----------+----------+----------+----------+----------+----------+];
    MetalSubheadPrinted = true;
  end;

  GetPos( CICode );

  FindCICode( CIMisc.HallmarkRef );

  [| ########################|#######|########|##########|###########|##########|##########|##########|##########|##########|##########|]
  (
    ( NameVal + ", " + String( CIMisc.IssueYear ) ):w,
    CICode.Double:0:2,
    CIMisc.Par,
    IPrevRest:z,
    IPrevRest * CIMisc.Par:z,
    ICred + IOtherCred:z,
    ( ICred + IOtherCred ) * CIMisc.Par:z,
    IDeb + IOtherDeb:z,
    ( IDeb + IOtherDeb ) * CIMisc.Par:z,
    ICurRest:z,
    ICurRest * CIMisc.Par:z
  );
  TotPrevRest=TotPrevRest+IPrevRest;
  TotPrevRestA=TotPrevRestA+IPrevRest * CIMisc.Par;
  TotCredit=TotCredit+ICred;
  TotCreditA=TotCreditA+ICred * CIMisc.Par;
  TotDebet=TotDebet+IDeb;
  TotDebetA=TotDebetA+IDeb  * CIMisc.Par;
  TotOtherCred=TotOtherCred+IOtherCred;
  TotOtherCredA=TotOtherCredA+IOtherCred * CIMisc.Par;
  TotOtherDeb=TotOtherDeb+IOtherDeb;
  TotOtherDebA=TotOtherDebA+IOtherDeb * CIMisc.Par;
  TotRest=TotRest+ICurRest;
  TotRestA=TotRestA+ICurRest * CIMisc.Par;

  GetDirect( CICode );
end;

macro OutCoinTotLine

  [+-------------------------+-------+--------+----------+-----------+----------+----------+----------+----------+----------+----------+];
  [| И т о г о :             |       |        |##########|###########|##########|##########|##########|##########|##########|##########|]
  (
    TotPrevRest,
    TotPrevRestA,
    TotCredit + TotOtherCred,
    TotCreditA + TotOtherCredA,
    TotDebet + TotOtherDeb,
    TotDebetA + TotOtherDebA,
    TotRest,
    TotRestA
  );
end;

macro OutCoinHeaderW;

  [ ];
  [ ];
  [                                                 Монеты, учитываемые по массе ];
  [ ];
  [+----+--------------------+----+------+------+-------+-------------------+---------------------------------------+-------------------+];
  [|    |                    |    |      |      |Содерж.|      Остаток      |                Обороты                |      Остаток      |];
  [|Год |    Наименование    |Кач-|      |      |чистого|     на начало     |-------------------+-------------------+     на  конец     |];
  [|    |                    |    |Номин.|Проба |металла|        дня        |      Приход       |      Расход       |        дня        |];
  [|вып.|       монеты,      | во |      |      |   в   +------+------------+------+------------+------+------------+------+------------+];
  [|    |                    |    |      |      |монете |Кол-во|  Вес, гр.  |Кол-во|  Вес, гр.  |Кол-во|  Вес, гр.  |Кол-во|  Вес, гр.  |];
  [+----+--------------------+----+------+------+-------+------+------------+------+------------+------+------------+------+------------+];
end;

macro OutCoinLineW(NameVal,PrevRest,IPrevRest,CurRest,ICurRest,Cred,ICred,OtherCred,IOtherCred,Deb,IDeb,OtherDeb,IOtherDeb);

  if((not ShowNullLines) and (PrevRest==$0) and (Cred==$0) and (OtherCred==$0)
     and (Deb==$0) and (OtherDeb==$0) and (CurRest==$0)
     and (IPrevRest==0) and (ICred==0) and (IOtherCred==0)
     and (IDeb==0) and (IOtherDeb==0) and (ICurRest==0))
    return;
  end;

  if ( not MetalSubheadPrinted )
    PrintLn;
    PrintLn( CICode.String );
    PrintLn( MkStr( "=", StrLen( CICode.String ) ) );
    PrintLn;
    MetalSubheadPrinted = true;
    [+----+--------------------+----+------+------+-------+------+------------+------+------------+------+------------+------+------------+];
  end;

  FindCIMisc( CI_COIN, CashVal.rec.CodIntValue );

  GetPos( CICode );

  FindCICode( CIMisc.HallmarkRef );

  [|####| ###################|####|######|######|#######|######|############|######|############|######|############|######|############|]
  (
    CIMisc.IssueYear,
    NameVal:w,
    CIMisc.Quality,
    CIMisc.Par,
    CICode.Double:0:2,
    String( Double( CIMisc.Weight * CICode.Double / 1000 / 10000 ) ):r,
    IPrevRest:z,
    ( IPrevRest * CIMisc.Weight / 10000 ):0:2:z,
    ICred + IOtherCred:z,
    ( ( ICred + IOtherCred ) * CIMisc.Weight / 10000 ):0:2:z,
    IDeb + IOtherDeb:z,
    ( ( IDeb + IOtherDeb ) * CIMisc.Weight / 10000 ):0:2:z,
    ICurRest:z,
    ( ICurRest * CIMisc.Weight / 10000 ):0:2:z
  );
  TotPrevRest=TotPrevRest+IPrevRest;
  TotPrevRestA=TotPrevRestA+IPrevRest * CIMisc.Weight / 10000;
  TotCredit=TotCredit+ICred;
  TotCreditA=TotCreditA+ICred * CIMisc.Weight / 10000;
  TotDebet=TotDebet+IDeb;
  TotDebetA=TotDebetA+IDeb * CIMisc.Weight / 10000;
  TotOtherCred=TotOtherCred+IOtherCred;
  TotOtherCredA=TotOtherCredA+IOtherCred * CIMisc.Weight / 10000;
  TotOtherDeb=TotOtherDeb+IOtherDeb;
  TotOtherDebA=TotOtherDebA+IOtherDeb * CIMisc.Weight / 10000;
  TotRest=TotRest+ICurRest;
  TotRestA=TotRestA+ICurRest * CIMisc.Weight / 10000;

  GetDirect( CICode );
end;

macro OutCoinTotLineW

  [+----+--------------------+----+------+------+-------+------+------------+------+------------+------+------------+------+------------+];
  [| И т о г о :                                        |######|############|######|############|######|############|######|############|]
  (
    TotPrevRest,
    TotPrevRestA:0:2,
    TotCredit + TotOtherCred,
    TotCreditA + TotOtherCredA:0:2 ,
    TotDebet + TotOtherDeb,
    TotDebetA + TotOtherDebA:0:2,
    TotRest,
    TotRestA:0:2
  );
end;

macro OutCoinTail

  [                                                                           ];
  [                                                                           ];
  [                                                                           ];
  [___________________________________________________________________________];
  [  ( Управляющий учреждением СБ РФ )                                        ];
  [___________________________________________________________________________];
  [  ( Главный бухгалтер )                                                    ];
  [___________________________________________________________________________];
  [  ( Заведующий кладовой )                                                  ];
  PrintLn( StrFor( 12 ) );
end;

macro OutCIHeader;

  var
    S;

  [ ];
  [ ];
  if(Chapter==1)
    [                                                      Раздел I. "Ценные бумаги" ];
    S = "ценных бумаг";
  else
    [                                                      Раздел II. "Прочие ценности" ];
    S = "ценностей";
  end;
  [ ];
  [#]( Currency.Rec.Name_Currency );
  [+-----+------------------------+--------------+------------------------------+-----------------------------+---------------+];
  [|     |                        |              |       П  Р  И  Х  О  Д       |       Р  А  С  Х  О  Д      |               |];
  [|     |      Наименование      |  Остаток к   |------------------------------|-----------------------------|   Остаток к   |];
  [|Счета|                        |              |  Получено от  |    Прочий    |  Выслано в   |    Прочий    |               |];
  [|     |  ####################  | началу  дня  | учреждений СБ |    приход    |учреждения СБ |    расход    |   концу дня   |]
  ( S:c );
  [|     |                        |              | своего района |              | и клиентам   |              |               |];
  [+-----+------------------------+--------------+---------------+--------------+--------------+--------------+---------------+];
end;

macro OutCINameValTail

  var i=1;

  if ( ( CashVal.rec.TypeValue == TYPERES_MINCAPISSUE + CI_MISC ) )
    return;
  end;

  while(i<asize(NameValArr))
    [|     | #######################|              |               |              |              |              |               |]
    (NameValArr(i));
    i=i+1;
  end;
end;

macro OutCITotLine;

  var
    S1, S2;

  [+-----+------------------------+--------------+---------------+--------------+--------------+--------------+---------------+];
  if ( ( Chapter == 1 ) or ( Chapter == 2 ) )
    if ( Chapter == 1 )
      S1 = "И т о г о :";
      S2 = "И т о г о   ш т у к и :";
    else
      S1 = "Всего по I И II  разделам:";
      S2 = "Всего штуки:";
    end;

    [| ############################ |############# | ############# | ############ | ############ | ############ | ############# |]
    (S1, TotPrevRestA,TotCreditA,TotOtherCredA,TotDebetA,TotOtherDebA,TotRestA);
    [| ############################ |############# | ############# | ############ | ############ | ############ | ############# |]
    (S2, TotPrevRest,TotCredit,TotOtherCred,TotDebet,TotOtherDeb,TotRest);
  else
    [| И т о г о :                  |############# | ############# | ############ | ############ | ############ | ############# |]
    (TotPrevRest,TotCredit,TotOtherCred,TotDebet,TotOtherDeb,TotRest);
  end;
end;

macro SumAndPiecesStr( Sum, Pieces )

  const
    FldLen = 13;

  var
    Delim = "",
    S = String( Sum ),
    P = String( Pieces );

  if ( Pieces == 0 )
    return String( Sum );
  end;

  if ( StrLen( S ) + StrLen( P ) + 1 > FldLen )
    Delim = " ";
  end;
  return S + Delim + "/" + P;
end;

macro OutCILine(NameVal,PrevRest,IPrevRest,CurRest,ICurRest,Cred,ICred,OtherCred,IOtherCred,Deb,IDeb,OtherDeb,IOtherDeb,PrintPurposeLine);

  var
    PrevRestA = $0,
    CurRestA = $0,
    CredA = $0,
    OtherCredA = $0,
    DebA = $0,
    OtherDebA = $0,
    PrevRestS = "",
    CurRestS = "",
    CredS = "",
    OtherCredS = "",
    DebS = "",
    OtherDebS = "";

  StrSplit(NameVal,NameValArr,23);

  if((not ShowNullLines) and (PrevRest==$0) and (Cred==$0) and (OtherCred==$0)
     and (Deb==$0) and (OtherDeb==$0) and (CurRest==$0)
     and (IPrevRest==0) and (ICred==0) and (IOtherCred==0)
     and (IDeb==0) and (IOtherDeb==0) and (ICurRest==0)
     and (not PrintPurposeLine))
     return;
  end;

  if ( not SmthPrinted )
    if ( Chapter == 1 )
      Chapter1IsEmpty = false;
    elif ( Chapter1IsEmpty )
      Chapter = 1;
      OutCIHeader;
      OutCITotLine;
      Chapter = 2;
    end;
    OutCIHeader;
    SmthPrinted = true;
  end;

  if ( ( CashVal.rec.TypeValue == TYPERES_MINCAPISSUE + CI_MISC ) )
    if( PrintPurposeLine )
      if(    TotPrevRestPurpose
          or TotCurRestPurpose
          or TotCredPurpose
          or TotDebPurpose
          or TotOtherCredPurpose
          or TotOtherDebPurpose    )
        if( TotPrevRestPurpose )
          PrevRestS = SumAndPiecesStr( TotPrevRestPurpose, TotPrevRestPurposeP );
        end;
        if(  TotCurRestPurpose )
          CurRestS = SumAndPiecesStr( TotCurRestPurpose, TotCurRestPurposeP );
        end;
        if( TotCredPurpose )
          CredS = SumAndPiecesStr( TotCredPurpose, TotCredPurposeP );
        end;
        if( TotDebPurpose )
          DebS = SumAndPiecesStr( TotDebPurpose, TotDebPurposeP );
        end;
        if( TotOtherCredPurpose )
          OtherCredS = SumAndPiecesStr( TotOtherCredPurpose, TotOtherCredPurposeP );
        end;
        if( TotOtherDebPurpose )
          OtherDebS = SumAndPiecesStr( TotOtherDebPurpose, TotOtherDebPurposeP );
        end;

        [|     | #######################|############# | ############# | ############ | ############ | ############ | ############# |]
        (NameValArr(0),PrevRestS:r,CredS:r,OtherCredS:r,DebS:r,OtherDebS:r,CurRestS:r);
      end;
    end;
  else

    PrevRestS = SumAndPiecesStr( PrevRest, IPrevRest );
    CurRestS = SumAndPiecesStr( CurRest, ICurRest );
    CredS = SumAndPiecesStr( Cred, ICred );
    DebS = SumAndPiecesStr( Deb, IDeb );
    OtherCredS = SumAndPiecesStr( OtherCred, IOtherCred );
    OtherDebS = SumAndPiecesStr( OtherDeb, IOtherDeb );

    [|     | #######################|############# | ############# | ############ | ############ | ############ | ############# |]
    (NameValArr(0),PrevRestS:r:w,CredS:r:w,OtherCredS:r:w,DebS:r:w,OtherDebS:r:w,CurRestS:r:w);
  end;

  OutCINameValTail;
  TotPrevRest=TotPrevRest+IPrevRest;
  TotCredit=TotCredit+ICred;
  TotDebet=TotDebet+IDeb;
  TotOtherCred=TotOtherCred+IOtherCred;
  TotOtherDeb=TotOtherDeb+IOtherDeb;
  TotRest=TotRest+ICurRest;
  TotPrevRestA=TotPrevRestA+PrevRest;
  TotCreditA=TotCreditA+Cred;
  TotDebetA=TotDebetA+Deb;
  TotOtherCredA=TotOtherCredA+OtherCred;
  TotOtherDebA=TotOtherDebA+OtherDeb;
  TotRestA=TotRestA+CurRest;
  if ( ( CashVal.rec.TypeValue == TYPERES_MINCAPISSUE + CI_MISC ) and ( not PrintPurposeLine ) )
    TotPrevRestPurposeP=TotPrevRestPurposeP+IPrevRest;
    TotCredPurposeP=TotCredPurposeP+ICred;
    TotDebPurposeP=TotDebPurposeP+IDeb;
    TotOtherCredPurposeP=TotOtherCredPurposeP+IOtherCred;
    TotOtherDebPurposeP=TotOtherDebPurposeP+IOtherDeb;
    TotCurRestPurposeP=TotCurRestPurposeP+ICurRest;
    TotPrevRestPurpose=TotPrevRestPurpose+PrevRest;
    TotCredPurpose=TotCredPurpose+Cred;
    TotDebPurpose=TotDebPurpose+Deb;
    TotOtherCredPurpose=TotOtherCredPurpose+OtherCred;
    TotOtherDebPurpose=TotOtherDebPurpose+OtherDeb;
    TotCurRestPurpose=TotCurRestPurpose+CurRest;
  end;
end;

macro OutVFHeader;

  [ ];
  [ ];
  [                                                  Раздел III. "Ценные бланки" ];
  [ ];
  [+------------------------------+------------+-----------+------------+------------+------------+------------------------------+
   |                              |  Остаток   |           | Израсходов.| Выслано в  |  Остаток   |       №№ ценных бланков      |
   |    Наименование бланка       | к  началу  | Поступило |  по опера- | учреждения |  к концу   |         ( с № по № )         |
   |                              |    дня     |           |    циям    | Сбербанка  |    дня     |                              |
   +------------------------------+------------+-----------+------------+------------+------------+------------------------------+];
end;

macro OutVFNameValTail

  var
   RangeStr = "",
   NameValStr = "",
   RecFound,
   i=1;

  while((RecFound = (Next(RegNumList)
    and (RegNumList.Branch == CashAcc.rec.Branch)
    and (RegNumList.CashOper == CashAcc.rec.CashOper)
    and (RegNumList.Date == CashAcc.rec.Date)
    and (RegNumList.ValueRef == CashAcc.rec.RefValue)))
    or (i < asize(NameValArr)))
    if(i < asize(NameValArr))
      NameValStr = NameValArr(i);
    end;
    if(RecFound)
      RangeStr = MakeRangeStr;
    end;

    [|##############################|            |           |            |            |            | ############################ |]
    (
      NameValStr,
      RangeStr
    );
    i=i+1;
  end;
end;

macro OutVFLine(NameVal,PrevRest,IPrevRest,CurRest,ICurRest,Cred,ICred,OtherCred,IOtherCred,Deb,IDeb,OtherDeb,IOtherDeb);

  var
    RangeStr = "";

  StrSplit(NameVal,NameValArr,30);

  if((not ShowNullLines) and (PrevRest==$0) and (Cred==$0) and (OtherCred==$0)
     and (Deb==$0) and (OtherDeb==$0) and (CurRest==$0)
     and (IPrevRest==0) and (ICred==0) and (IOtherCred==0)
     and (IDeb==0) and (IOtherDeb==0) and (ICurRest==0))
    return;
  end;

  if ( not PrevRest )
    PrevRest = Money( IPrevRest );
  end;
  if ( not CurRest )
    CurRest = Money( ICurRest );
  end;
  if ( not Cred )
    Cred = Money( ICred );
  end;
  if ( not OtherCred )
    OtherCred = Money( IOtherCred );
  end;
  if ( not Deb )
    Deb = Money( IDeb );
  end;
  if ( not OtherDeb )
    OtherDeb = Money( IOtherDeb );
  end;

  ClearRecord(RegNumList);
  RegNumList.Branch = CashAcc.rec.Branch;
  RegNumList.CashOper = CashAcc.rec.CashOper;
  RegNumList.ValueRef = CashAcc.rec.RefValue;
  RegNumList.Date = CashAcc.rec.Date;
  RegNumList.Nominal = $100000000000;

  if(GetGE(RegNumList)
    and (RegNumList.Branch == CashAcc.rec.Branch)
    and (RegNumList.CashOper == CashAcc.rec.CashOper)
    and (RegNumList.Date == CashAcc.rec.Date)
    and (RegNumList.ValueRef == CashAcc.rec.RefValue))
    RangeStr = MakeRangeStr;
  end;

  [|##############################|########### | ######### | ########## | ########## | ########## | ############################ |]
  (NameValArr(0),PrevRest:z,Cred+OtherCred:z,OtherDeb:z,Deb:z,CurRest:z,RangeStr:w);
  OutVFNameValTail;
  TotPrevRestF=TotPrevRestF+PrevRest;
  TotCreditF=TotCreditF+Cred;
  TotDebetF=TotDebetF+Deb;
  TotOtherCredF=TotOtherCredF+OtherCred;
  TotOtherDebF=TotOtherDebF+OtherDeb;
  TotRestF=TotRestF+CurRest;
end;

macro OutVFTotLine;

  [+------------------------------+------------+-----------+------------+------------+------------+------------------------------+];
  [| И т о г о                    |########### | ######### | ########## | ########## | ########## |                              |]
  (TotPrevRestF,TotCreditF+TotOtherCredF,TotOtherDebF,TotDebetF,TotRestF);
end;

macro OutStatHead

  [ ];
  [ ];
  [                                                          Раздел IV. "Учет вкладов"];
  [ ];
  [#]( Currency.Rec.Name_Currency );
  [+-----+------------------------------+-----------------------+---------------------+---------------------+-----------------------+
   |  №  |                              |  Остаток к началу дня |        Приход       |         Расход      |  Остаток к концу дня  |
   |счета|     Наименование счета       |-------+---------------|-------+-------------|-------+-------------|-------+---------------|
   |     |                              |кол-во |     сумма     |кол-во |    сумма    |кол-во |    сумма    |кол-во |     сумма     |
   +-----+------------------------------+-------+---------------+-------+-------------+-------+-------------+-------+---------------+];
end;

macro OutStatTotals

  [+-----+------------------------------+-------+---------------+-------+-------------+-------+-------------+-------+---------------+];
  [| И т о г о:                         |#######|###############|#######|#############|#######|#############|#######|###############|]
  (
    TotPrevAcc,
    TotPrevRest,
    TotOpenedAcc,
    TotCredit,
    TotClosedAcc,
    TotDebet,
    TotAcc,
    TotRest
  );
end;

macro OutStatTotalsBA1( NumBA )

  [|#####|И т о г о   п о   б / с :     |#######|###############|#######|#############|#######|#############|#######|###############|]
  (
    NumBA,
    BATotPrevAcc,
    BATotPrevRest,
    BATotOpenedAcc,
    BATotCredit,
    BATotClosedAcc,
    BATotDebet,
    BATotAcc,
    BATotRest
  );
end;

macro OutStatTotals2

  if (NOT WasPrintEstim)
    [+-----+------------------------------+-------------+-------------+---------------------+-------------------+----------------+];
  end;
  [| И т о г о:                         |#############|#############|#####################|###################|################|]
  (
  TotPrevEstimRest,
  TotEstimIn,
  TotEstinOut,
  TotEstimOverOut,
  TotEstimRest
  );

end;

macro OutStatTotalBA

  [+-----+------------------------------+-------------+-------------+---------------------+-------------------+----------------+];
  [| Итого по б/с ######                |#############|#############|#####################|###################|################|]
  (
  NumBA,
  BAPrevEstimRest,
  BAEstimIn,
  BAEstinOut,
  BAEstimOverOut,
  BAEstimRest
  );
  [+-----+------------------------------+-------------+-------------+---------------------+-------------------+----------------+];

end;


macro OutStatHead2

  var
    brName;

  GetBranchParams( NumFNCash, brName );

  [
   Филиал № #                                                                                                     ]
  ( brName );

  [ ];
  [ ];
  [                                   Ведомость начисленных процентов, отраженных по балансу ];
  [                                   ###################################################### ]
  ( String( "за " + {curdate}:m ):c );
  [ ];
  [#]( Currency.Rec.Name_Currency );
  [+-----+------------------------------+--------------------------------------------------------------------------------------+
   |  №  |                              |                  Сумма начисленных процентов, отраженных по балансу                  |
   |счета|     Наименование счета       |-------------+-------------+-----------------------------------------+----------------|
   |     |                              |  Остаток на |  Начислено  |                  Списано                |   Остаток на   |
   |     |                              |  начало дня |             |---------------------+-------------------|   конец дня    |
   |     |                              |             |             |Причисленные к вкладу|Излишне начисленные|                |
   +-----+------------------------------+-------------+-------------+---------------------+-------------------+----------------+];
end;


macro OutDepLine

  var
    CitStr;
  var BAcc = "";

  if((not ShowNullLines)
     and (PrevStat.ColOpenedAcc==0)
     and (PrevStat.Rest==0)
     and (DepStat.rec.ColAccounts==PrevStat.ColAccounts)
     and (DepStat.rec.KredSum==0)
     and (DepStat.rec.ColClosedAcc==PrevStat.ColClosedAcc)
     and (DepStat.rec.DebSum==0)
     and (DepStat.rec.ColOpenedAcc==0)
     and (DepStat.rec.Rest==0))
    return;
  end;

  if ( not SmthPrinted )
    OutStatHead;
    SmthPrinted = true;
  end;

  WasPrintBA = true;

  if(not DepStat.rec.FlagRez)
    CitStr = " (рез.)";
    BAcc = tBalAcc;
  else
    CitStr = " (нер.)";
    BAcc = tBalAccNR;
  end;

  [|#####|##############################|#######|###############|#######|#############|#######|#############|#######|###############|]
  (
    BAcc,
    (DepType.Rec.Name+CitStr):w,
    PrevStat.ColOpenedAcc:z,
    PrevStat.Rest:z,
    DepStat.rec.CurOpenAc:z,
    DepStat.rec.KredSum:z,
    DepStat.rec.CurClosAc:z,
    DepStat.rec.DebSum:z,
    DepStat.rec.ColOpenedAcc:z,
    DepStat.rec.Rest:z
  );
  TotPrevAcc = TotPrevAcc + PrevStat.ColOpenedAcc;
  TotOpenedAcc = TotOpenedAcc + DepStat.rec.CurOpenAc;
  TotClosedAcc = TotClosedAcc + DepStat.rec.CurClosAc;
  TotAcc = TotAcc + DepStat.rec.ColOpenedAcc;
  TotPrevRest = TotPrevRest + PrevStat.Rest;
  TotCredit = TotCredit + DepStat.rec.KredSum;
  TotDebet = TotDebet + DepStat.rec.DebSum;
  TotRest = TotRest + DepStat.rec.Rest;
  BATotPrevAcc = BATotPrevAcc + PrevStat.ColOpenedAcc;
  BATotOpenedAcc = BATotOpenedAcc + DepStat.rec.CurOpenAc;
  BATotClosedAcc = BATotClosedAcc + DepStat.rec.CurClosAc;
  BATotAcc = BATotAcc + DepStat.rec.ColOpenedAcc;
  BATotPrevRest = BATotPrevRest + PrevStat.Rest;
  BATotCredit = BATotCredit + DepStat.rec.KredSum;
  BATotDebet = BATotDebet + DepStat.rec.DebSum;
  BATotRest = BATotRest + DepStat.rec.Rest;
end;


macro OutDepLine2

  var
    CitStr;
  var BAcc = "";

  if((not ShowNullLines)
     and (PrevStat.EstimRest==0)
     and (DepStat.rec.EstimIn==0)
     and (DepStat.rec.EstimOut==0)
     and (DepStat.rec.EstimOverOut==0)
     and (DepStat.rec.EstimRest==0))
     return;
  end;

  if ( not SmthPrinted )
    OutStatHead2;
    SmthPrinted = true;
    SmthPrinted_Accum = true;
  end;

  if(not DepStat.rec.FlagRez)
    CitStr = " (рез.)";
    BAcc = tBalAcc;
  else
    CitStr = " (нер.)";
    BAcc = tBalAccNR;
  end;

  [|#####|############################# |#############|#############|#####################|###################|################|]
  (
    BAcc,
    (DepType.Rec.Name+CitStr):w,
    PrevStat.EstimRest:z,
    DepStat.rec.EstimIn:z,
    DepStat.rec.EstimOut:z,
    DepStat.rec.EstimOverOut:z,
    DepStat.rec.EstimRest:z
  );

  WasPrintBA    = TRUE;
  WasPrintEstim = TRUE;
  NumBA = BAcc;

  TotPrevEstimRest = TotPrevEstimRest + PrevStat.EstimRest;
  TotEstimIn       = TotEstimIn       + DepStat.rec.EstimIn;
  TotEstinOut      = TotEstinOut      + DepStat.rec.EstimOut;
  TotEstimOverOut  = TotEstimOverOut  + DepStat.rec.EstimOverOut;
  TotEstimRest     = TotEstimRest     + DepStat.rec.EstimRest;

  BAPrevEstimRest = BAPrevEstimRest + PrevStat.EstimRest;
  BAEstimIn       = BAEstimIn       + DepStat.rec.EstimIn;
  BAEstinOut      = BAEstinOut      + DepStat.rec.EstimOut;
  BAEstimOverOut  = BAEstimOverOut  + DepStat.rec.EstimOverOut;
  BAEstimRest     = BAEstimRest     + DepStat.rec.EstimRest;

end;


macro OutOthStatLineCO

  [| ############ | Текущие счета учреждений и    |      | ############## |      | ############## |      | ############## |      | ############## |]
  (
    CashOperAcc,
    TotOthPrevStat.Rest:z,
    TotOthStat.KredSum:z,
    TotOthStat.DebSum:z,
    TotOthStat.Rest:z
  );
  [|              | организаций, сосотоящих на    |      |                |      |                |      |                |      |                |];
  [|              | местных бюджетах              |      |                |      |                |      |                |      |                |];
  TotPrevAcc = TotPrevAcc + 1;
  TotAcc = TotAcc +1;
end;

macro OutOthStatLineCI

  [| ############ | Облигации государственных     | #### | ############## | #### | ############## | #### | ############## | #### | ############## |]
  (
    CapIssueAcc,
    PrevStat.ColOpenedAcc:z,
    PrevStat.Rest:z,
    DepStat.rec.ColAccounts-PrevStat.ColAccounts:z,
    DepStat.rec.KredSum:z,
    DepStat.rec.ColClosedAcc-PrevStat.ColClosedAcc:z,
    DepStat.rec.DebSum:z,
    DepStat.rec.ColOpenedAcc:z,
    DepStat.rec.Rest:z
  );
  [|              | займов, сертификаты и другие  |      |                |      |                |      |                |      |                |];
  [|              | ценные бумаги, принятые на хр.|      |                |      |                |      |                |      |                |];
  TotPrevAcc = TotPrevAcc + PrevStat.ColOpenedAcc;
  TotOpenedAcc = TotOpenedAcc + DepStat.rec.ColAccounts-PrevStat.ColAccounts;
  TotClosedAcc = TotClosedAcc + DepStat.rec.ColClosedAcc-PrevStat.ColClosedAcc;
  TotAcc = TotAcc + DepStat.rec.ColOpenedAcc;
  TotPrevRest = TotPrevRest + PrevStat.Rest;
  TotCredit = TotCredit + DepStat.rec.KredSum;
  TotDebet = TotDebet + DepStat.rec.DebSum;
  TotRest = TotRest + DepStat.rec.Rest;
end;

macro OutOthStatLineCP

  [| ############ | Коммунальные платежи          |      | ############## |      | ############## |      | ############## |      | ############## |]
  (
    CommPayAcc,
    TotOthPrevStat.Rest:z,
    TotOthStat.KredSum:z,
    TotOthStat.DebSum:z,
    TotOthStat.Rest:z
  );
  TotPrevAcc = TotPrevAcc + 1;
  TotAcc = TotAcc +1;
end;

macro OutOpHead

  [ ];
  [                                                       Раздел V. "Учет операций" ];
  [ ];
  [+------------------------------------------------------------------+----------------------------------------------------------+
   |                                                                  |                 Количество операций                      |
   |                    В И Д Ы   О П Е Р А Ц И Й                     |--------------------+-------------------+-----------------|
   |                                                                  |    К началу дня    |     За день       |С начала квартала|
   +------------------------------------------------------------------+--------------------+-------------------+-----------------+];
end;

macro OutMemHead

  [ ];
  [                                       Раздел VI. "Справка о безналичных операциях за квартал" ];
  [ ];
  [+------------------------------------------------------------------+----------------------------------------------------------+
   |                                                                  |    К началу дня    |       За день     |С начала квартала|
   +------------------------------------------------------------------+--------------------+-------------------+-----------------+];
end;

macro OutOpMemLine(OpName, PrevNOps, NOps)

  if(PrevNOps or NOps)
    [|##################################################################|####################|###################|#################|]
    (
      OpName,
      PrevNOps:z,
      NOps - PrevNOps:z,
      NOps:z
    );
    TotPrevNOps = TotPrevNOps + PrevNOps;
    TotNOps = TotNOps + NOps;
  end;
end;

macro OutOpTotals

  [+------------------------------------------------------------------+--------------------+-------------------+-----------------+];
  [| И т о г о :                                                      |####################|###################|#################|]
  (
    TotPrevNOps,
    TotNOps - TotPrevNOps,
    TotNOps
  );
end;

macro OutBottom
  [
   -----------------------------------------------------------------------------------------------------------------------------
   Ст.контролер (контролер) _________________________________________________ Кассир ___________________________________________

   Отчет проверил "___" ________________ ______ г.                            Бухгалтер ________________________________________];
end;
