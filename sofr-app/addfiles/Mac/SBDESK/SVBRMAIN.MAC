/***************************************************************************
*  ---------------------                                                   *
*  R-Style Software Lab.                                                   *
*  ---------------------                                                   *
*  RS-Retail                                                               *
*                                                                          *
*  Касса-кладовая                                                          *
*                                                                          *
*  Сводная справка о кассовых оборотах                                     *
*                                                                          *
*  Лебедев С.В.                                                            *
*  13.12.1999                                                              *
*                                                                          *
*  Конкретный рестаилинг:                                                  *
*  Крестьянинов Е.А.                                                       *
*  13.10.2004                                                              *
***************************************************************************/

import DeprIntr, "cur_rate.mac", sqlconv, svodrep, ingot;

private var desksubj = TBFile( "desksubj.dbt", "r", 1 );
private var sb_casvl = TBFile( "sb_casvl.dbt", "r", 1 );
private var SB_CasAc = TBFile( "sb_casac.dbt", "r" );
private var SB_CasDc = TBFile( "sb_casdc.dbt", "r", 5 );
private var CI_Misc  = TBFile( "ci_misc.dbt", "r", 0 );
private var paydoc   = TBFile( "paydoc.dbt", "r", 0, null, "exchange.def");
private var Alg      = TBFile( "namealg.dbt","r", 0, null, "bank.def" );

private var Branch = NumFNCash();
private var {curdate},{FIO_Book};

private const DST_IO       = 1;    /* Инкассация           */
private const DST_SAFE     = 2;    /* Сейф                 */
private const DST_WORKDESK = 3;    /* Рабочий стол кассира */
private const DST_RDESK    = 4;    /* Касса пересчета      */
private const DST_PANTRY   = 5;    /* Кладовая отделения   */
private const DST_EDESK    = 6;    /* Вечерняя касса       */

private const RDesk = "К. пересчета";
private const EDesk = "Вечер. касса";

private const FLAGDEBCRED_IN  = 1; /* Кредит */
private const FLAGDEBCRED_OUT = 2; /* Дебет */

private const ALG_CASH_VAL_TYPE = 811;    /* Типы ресурсов кассы в namealg.dbt */

private const
  CK_NEW          = 0, 
  CK_OLD          = 1;

private var CountIn  = 0;
private var SumIn    = $0L;
private var SumInR   = $0L;
private var CountOut = 0;
private var SumOut   = $0L;
private var SumOutR  = $0L;
private var PrevSum4 = $0L;


private var objPrint = TSvodReport;


/* Получить название типа ресурса кассы */
private macro TypeStr

  if ( Alg.rec.iNumberAlg == 0 )
    return "";
  end;

  return " (" + Alg.rec.szNameAlg + ")";
end;

/***************************************************************************
                         Поиск остатков по операции
***************************************************************************/
private macro FindCasAc
  
  SB_CasAc.KeyNum = 0;
  SB_CasAc.rec.Branch   = Branch;
  SB_CasAc.rec.Date     = {curdate};
  SB_CasAc.rec.CashOper = desksubj.rec.Name;
  SB_CasAc.rec.SubAccount = "";
  SB_CasAc.rec.RefValue = sb_casvl.rec.RefValue;

  return SB_CasAc.GetEQ;
         
end;


/***************************************************************************
             Определение отчетных параметров для одной ценности
***************************************************************************/
private macro DefParamForCashValue
  
  var PrevSum = $0L;
  var CurRate = 0.0;
  var PrvRate = 0.0;
  var stat;
  var IngotDoc;

  if (FindCasAc())

    if( desksubj.rec.Type == DST_RDESK )
      SB_CasDc.Clear();
      SB_CasDc.rec.Branch          = SB_CasAc.rec.Branch;
      SB_CasDc.rec.CashDateDoc     = SB_CasAc.rec.Date;
      SB_CasDc.rec.TypeDoc         = 2;
      SB_CasDc.rec.CashPatern      = "";
      SB_CasDc.rec.RefValue        = SB_CasAc.rec.RefValue;
      SB_CasDc.rec.FlagDebCredOper = "";
      SB_CasDc.addFilter( " t.t_Branch = " + String(SB_CasAc.rec.Branch) +
  			" and t.t_CashDateDoc = " + sqlDateToStr(SB_CasAc.rec.Date) +
  			" and t.t_TypeDoc = 2 " +
  			" and t.t_RefValue = " + String(SB_CasAc.rec.RefValue) );
      stat = SB_CasDc.GetGE;
      while ((stat                                     ) AND
             (SB_CasDc.rec.Branch      == SB_CasAc.rec.Branch  ) AND
             (SB_CasDc.rec.CashDateDoc == SB_CasAc.rec.Date    ) AND
             (SB_CasDc.rec.TypeDoc     == 2                ) AND
             (SB_CasDc.rec.RefValue    == SB_CasAc.rec.RefValue)    )
        if(SB_CasDc.rec.CashPatern == SB_CasAc.rec.CashOper)
          CountIn  = CountIn  + 1;
        elif(SB_CasDc.rec.CashOper == SB_CasAc.rec.CashOper)
          CountOut = CountOut + 1;
        end;
        stat = SB_CasDc.Next;
      end;
      SB_CasDc.dropFilter();
    else
      SB_CasDc.Clear();
      SB_CasDc.rec.Branch          = SB_CasAc.rec.Branch;
      SB_CasDc.rec.CashDateDoc     = SB_CasAc.rec.Date;
      SB_CasDc.rec.TypeDoc         = 2;
      SB_CasDc.rec.CashPatern      = SB_CasAc.rec.CashOper;
      SB_CasDc.rec.RefValue        = SB_CasAc.rec.RefValue;
      SB_CasDc.rec.FlagDebCredOper = "";
      SB_CasDc.addFilter( " t.t_Branch = " + String(SB_CasAc.rec.Branch) +
  			" and t.t_CashDateDoc = " + sqlDateToStr(SB_CasAc.rec.Date) +
  			" and t.t_TypeDoc = 2 " +
  			" and t.t_CashPatern = " + GetSQLString(SB_CasAc.rec.CashOper) +
  			" and t.t_RefValue = " + String(SB_CasAc.rec.RefValue) );
      stat = SB_CasDc.GetGE;
      while ((stat                                     ) AND
             (SB_CasDc.rec.Branch      == SB_CasAc.rec.Branch  ) AND
             (SB_CasDc.rec.CashDateDoc == SB_CasAc.rec.Date    ) AND
             (SB_CasDc.rec.TypeDoc     == 2                ) AND
             (SB_CasDc.rec.CashPatern  == SB_CasAc.rec.CashOper) AND
             (SB_CasDc.rec.RefValue    == SB_CasAc.rec.RefValue)    )
        if( sb_casvl.rec.TypeValue == 505 ) 
          IngotDoc = TIngotCashDoc( SB_CasDc.rec.ApplicationKind, SB_CasDc.rec.ApplicationKey, SB_CasDc.rec.RefValue );
          if(SB_CasDc.rec.FlagDebCredOper == FLAGDEBCRED_IN)
            SumOut  = SumOut + IngotDoc.GetRubValue( {curdate} );
	  else
            SumIn   = SumIn  + IngotDoc.GetRubValue( {curdate} );
  	  end;
  	end;
        if(SB_CasDc.rec.FlagDebCredOper == FLAGDEBCRED_IN)
          CountOut = CountOut + 1;
        elif (SB_CasDc.rec.FlagDebCredOper == FLAGDEBCRED_OUT)
          CountIn  = CountIn  + 1;
        end;
        stat = SB_CasDc.Next;
      end;
      SB_CasDc.dropFilter();
    end;

    if( (CountIn > 0) or (CountOut > 0) )
      if (sb_casvl.rec.TypeValue == 504)
        SumIn  = SumIn  + SB_CasAc.rec.KredCol * CI_Misc.rec.Par;
        SumOut = SumOut + SB_CasAc.rec.DebCol  * CI_Misc.rec.Par;
      elif( sb_casvl.rec.TypeValue != 505 )
	if( desksubj.rec.Type == DST_EDESK )
          SumIn   = SumIn  + SB_CasAc.rec.EKredMoney;
          SumOut  = SumOut + SB_CasAc.rec.EDebMoney;
	else
          SumIn   = SumIn  + SB_CasAc.rec.KredMoney;
          SumOut  = SumOut + SB_CasAc.rec.DebMoney;
	end;
        if (sb_casvl.rec.TypeValue == 4)
          PrevSum4 = PrevSum4 + SB_CasAc.rec.RestMoney - SB_CasAc.rec.KredMoney + SB_CasAc.rec.DebMoney;
        elif ((Currency.rec.Code_Currency > 0) AND (sb_casvl.rec.TypeValue != 4))
          Copy( RtlCurr, Currency );
          CurRate = GetCurrRate({curdate});
          PrvRate = GetCurrRate({curdate} - 1);
          if ((CurRate > 0) AND (PrvRate > 0))
            SumInR  = Money(SumIn  * CurRate);
            SumOutR = Money(SumOut * CurRate);
            PrevSum = SB_CasAc.rec.RestMoney - SB_CasAc.rec.KredMoney + SB_CasAc.rec.DebMoney;
            if (CurRate > PrvRate)
              if (PrevSum > 0)
                CountIn = CountIn   + 1;
                SumInR  = SumInR    + Money(PrevSum * (CurRate - PrvRate));
              end;
            elif (PrvRate > CurRate) 
              if (PrevSum > 0)
                CountOut = CountOut + 1;
                SumOutR  = SumOutR  + Money(PrevSum * (PrvRate - CurRate));
              end;
            end;
          end; 
        end;
      end;
    end;
  end;
  
end;


/***************************************************************************
                      Обработка данных по одному сейфу
***************************************************************************/
private macro TreatCurrentSafe

  var flag, flagAlg;
  var stat;
  var CurRate = 0.0;
  var PrvRate = 0.0;
  
  Currency.rec.Code_Currency = 0;
  
  flag = Currency.GetGE();

  while (flag)
    Alg.Clear();
    Alg.rec.iTypeAlg = ALG_CASH_VAL_TYPE;
    flagAlg = Alg.GetGE();
    while ( flagAlg and ( Alg.rec.iTypeAlg == ALG_CASH_VAL_TYPE ) )
      if( (Alg.rec.iNumberAlg != 4) and (Alg.rec.iNumberAlg != 6) )
        CountIn  = 0;
        SumIn    = $0L;
        SumInR   = $0L;
        CountOut = 0;
        SumOut   = $0L;
        SumOutR  = $0L;
        /* Отчет по монетам ------------------------------------------------- */
        if ( (Currency.rec.Code_Currency == 0) and (Alg.rec.iNumberAlg == 0))
          sb_casvl.rec.TypeValue   = 504;
          sb_casvl.rec.CodIntValue = 0;
          sb_casvl.rec.Type        = 0;
          sb_casvl.addFilter( " t.t_TypeValue = 504 " +
  			    " and exists (select * from dsb_casac_dbt a " + 
                              "where a.t_branch = " + String( desksubj.rec.Branch ) +
                              "  and a.t_cashoper = " + GetSQLString( desksubj.rec.Name ) +
  			    "  and a.t_date = " + sqlDateToStr({curdate}) + 
  			    "  and a.t_subaccount = " + GetSQLString( "" ) + 
                              "  and a.t_refvalue = t.t_refValue )"                        );
          stat = sb_casvl.GetGE;
          while ((stat) AND (sb_casvl.rec.TypeValue == 504))
            if (sb_casvl.rec.Type == Alg.rec.iNumberAlg)
              CI_Misc.rec.Type    = 4;
              CI_Misc.rec.IssueID = sb_casvl.rec.CodIntValue;
              if ((CI_Misc.GetEQ()) AND (CI_Misc.rec.KindOfCoin == CK_NEW))
                DefParamForCashValue();
                CountIn  = 0;
                CountOut = 0;
              end;
            end;
            stat = sb_casvl.Next;
          end;
          sb_casvl.dropFilter;
          if ((CountIn > 0) OR (SumIn > 0) OR (CountOut > 0) OR (SumOut > 0))
            objPrint.addParamForNewMon(CountIn,SumIn,CountOut,SumOut);
  	  end;
          CountIn  = 0;
          SumIn    = $0L;
          SumInR   = $0L;
          CountOut = 0;
          SumOut   = $0L;
          SumOutR  = $0L;
          sb_casvl.rec.TypeValue   = 504;
          sb_casvl.rec.CodIntValue = 0;
          sb_casvl.rec.Type        = 0;
          sb_casvl.addFilter( " t.t_TypeValue = 504 " +
  			    " and exists (select * from dsb_casac_dbt a " + 
                              "where a.t_branch = " + String( desksubj.rec.Branch ) +
                              "  and a.t_cashoper = " + GetSQLString( desksubj.rec.Name ) +
  			    "  and a.t_date = " + sqlDateToStr({curdate}) + 
  			    "  and a.t_subaccount = " + GetSQLString( "" ) + 
                              "  and a.t_refvalue = t.t_refValue )"                        );
          stat = sb_casvl.GetGE;
          while ((stat) AND (sb_casvl.rec.TypeValue == 504))
            if (sb_casvl.rec.Type == Alg.rec.iNumberAlg)
              CI_Misc.rec.Type    = 4;
              CI_Misc.rec.IssueID = sb_casvl.rec.CodIntValue;
              if ((CI_Misc.GetEQ()) AND (CI_Misc.rec.KindOfCoin == CK_OLD))
                DefParamForCashValue();
                CountIn  = 0;
                CountOut = 0;
              end;
            end;
            stat = sb_casvl.Next;
          end;
          sb_casvl.dropFilter;
          if ((CountIn > 0) OR (SumIn > 0) OR (CountOut > 0) OR (SumOut > 0))
            objPrint.addParamForOldMon(CountIn,SumIn,CountOut,SumOut);
  	  end;
        end;
        /* Отчет по деньгам ------------------------------------------------- */
/*
        if( Alg.rec.iNumberAlg != 1 )
*/
          CountIn  = 0;
          SumIn    = $0L;
          CountOut = 0;
          SumOut   = $0L;
          sb_casvl.KeyNum = 1;
          sb_casvl.rec.TypeValue   = 1;
          sb_casvl.rec.CodIntValue = Currency.rec.Code_Currency;
          sb_casvl.rec.Type        = Alg.rec.iNumberAlg;
          sb_casvl.addFilter( "exists (select * from dsb_casac_dbt a " + 
                              "where a.t_branch = " + String( desksubj.rec.Branch ) +
                              "  and a.t_cashoper = " + GetSQLString( desksubj.rec.Name ) +
  			    "  and a.t_date = " + sqlDateToStr({curdate}) + 
  			    "  and a.t_subaccount = " + GetSQLString( "" ) + 
                              "  and a.t_refvalue = t.t_refValue )"                        );
          if ( sb_casvl.GetEQ )
            DefParamForCashValue( );
            if ((CountIn > 0) OR (SumIn > 0) OR (CountOut > 0) OR (SumOut > 0))
              if( Alg.rec.iNumberAlg != 1 )
                objPrint.addParamForCurrency(Currency.rec.Code_Currency,CountIn, SumIn,SumInR,CountOut,SumOut,SumOutR);
	      else
                objPrint.addParamForBadCurrency(Currency.rec.Code_Currency,CountIn, SumIn,SumInR,CountOut,SumOut,SumOutR);
    	      end;
    	    end;
          end;
          sb_casvl.dropFilter;
/*
        end;
*/
        /* Отчет по дорожным чекам ------------------------------------------ */
        if (Currency.rec.Code_Currency > 0)
          CountIn  = 0;
          SumIn    = $0L;
          SumInR   = $0L;
          CountOut = 0;
          SumOut   = $0L;
          SumOutR  = $0L;
          PrevSum4 = $0L;
          sb_casvl.rec.TypeValue   = 4;
          sb_casvl.rec.CodIntValue = 0;
          sb_casvl.rec.Type        = 0;
          sb_casvl.addFilter( " t.t_TypeValue = 4 " +
  			    " and exists (select * from dsb_casac_dbt a " + 
                              "where a.t_branch = " + String( desksubj.rec.Branch ) +
                              "  and a.t_cashoper = " + GetSQLString( desksubj.rec.Name ) +
  			    "  and a.t_date = " + sqlDateToStr({curdate}) + 
  			    "  and a.t_subaccount = " + GetSQLString( "" ) + 
                              "  and a.t_refvalue = t.t_refValue )"                        );
          stat = sb_casvl.GetGE;
          while ((stat) AND (sb_casvl.rec.TypeValue == 4))
            if (sb_casvl.rec.Type == Alg.rec.iNumberAlg)
              paydoc.rec.ID = sb_casvl.rec.CodIntValue;
              if ((paydoc.GetEQ()) AND (paydoc.rec.Curr_Ref == Currency.rec.Code_Currency))
                DefParamForCashValue();

                Copy( RtlCurr, Currency );
                CurRate = GetCurrRate({curdate});
                PrvRate = GetCurrRate({curdate} - 1);
                if ((CurRate > 0) AND (PrvRate > 0))
                  SumInR  = Money(SumIn  * CurRate);
                  SumOutR = Money(SumOut * CurRate);
                  if (CurRate > PrvRate)
                    if (PrevSum4 > 0)
                      CountIn = CountIn   + 1;
                      SumInR  = SumInR    + Money(PrevSum4 * (CurRate - PrvRate));
                    end;
                  elif (PrvRate > CurRate) 
                    if (PrevSum4 > 0)
                      CountOut = CountOut + 1;
                      SumOutR  = SumOutR  + Money(PrevSum4 * (PrvRate - CurRate));
                    end;
                  end;
                end;

                if ((CountIn > 0) OR (SumIn > 0) OR (CountOut > 0) OR (SumOut > 0))
                  objPrint.addParamForPD(sb_casvl.rec.RefValue,SumIn,SumInR,CountIn,SumOut,SumOutR,CountOut);
		end;

                CountIn  = 0;
                SumIn    = $0L;
                SumInR   = $0L;
                CountOut = 0;
                SumOut   = $0L;
                SumOutR  = $0L;
                PrevSum4 = $0L;
              end;
            end;
            stat = sb_casvl.Next;
          end;
          sb_casvl.dropFilter;
        end;
      end;
      /* Отчет по драгметаллам ---------------------------------------------- */
      if ( (Currency.rec.Code_Currency == 0) and (Alg.rec.iNumberAlg == 0))
        CountIn  = 0;
        SumIn    = $0L;
        SumInR   = $0L;
        CountOut = 0;
        SumOut   = $0L;
        SumOutR  = $0L;
        PrevSum4 = $0L;
        sb_casvl.rec.TypeValue   = 505;
        sb_casvl.rec.CodIntValue = 0;
        sb_casvl.rec.Type        = 0;
        sb_casvl.addFilter( " t.t_TypeValue = 505 " +
        		    " and exists (select * from dsb_casac_dbt a " + 
                            "where a.t_branch = " + String( desksubj.rec.Branch ) +
                            "  and a.t_cashoper = " + GetSQLString( desksubj.rec.Name ) +
        		    "  and a.t_date = " + sqlDateToStr({curdate}) + 
        		    "  and a.t_subaccount = " + GetSQLString( "" ) + 
                            "  and a.t_refvalue = t.t_refValue )"                        );
        stat = sb_casvl.GetGE;
        while ((stat) AND (sb_casvl.rec.TypeValue == 505))
          DefParamForCashValue();
          stat = sb_casvl.Next;
        end;
        sb_casvl.dropFilter;
        if ((CountIn > 0) OR (SumIn > 0) OR (CountOut > 0) OR (SumOut > 0))
          objPrint.addParamForDragMet(CountIn,SumIn,CountOut,SumOut);
        end;
      end;
      flagAlg = Alg.Next();
    end;
    flag = Currency.Next();
  end;
 
end;

private macro PrintResult( dontprint )

  objPrint.PrintHead();
  objPrint.PrintReport( dontprint );
  objPrint.PrintCellar();

end;

/***************************************************************************
                      Г Л А В Н А Я   П Р О Г Р А М М А
***************************************************************************/

macro PrintSvodObr( DeskSubjName, dontprint )
  var RecFound;

  if (DeskSubjName == "")
    desksubj.KeyNum = 1;
    desksubj.Clear;
    desksubj.rec.Branch = Branch;
    desksubj.rec.Type   = DST_PANTRY;
    RecFound = desksubj.GetGE;
    while ((RecFound                         ) AND 
           (desksubj.rec.Branch == Branch    ) AND
           (desksubj.rec.Type   == DST_PANTRY)    )
      TreatCurrentSafe();
      RecFound = desksubj.Next;
    end;

    desksubj.KeyNum = 0;
    desksubj.Clear;
    desksubj.rec.Branch = Branch;
    desksubj.rec.Name = RDesk;
    if ( desksubj.GetEQ )
      TreatCurrentSafe();
    end;
  else
    desksubj.KeyNum = 0;
    desksubj.Clear;
    desksubj.rec.Branch = Branch;
    desksubj.rec.Name   = DeskSubjName;
    if ( desksubj.GetEQ )
      TreatCurrentSafe( );
    end;
  end;

  PrintResult( dontprint );

end;
