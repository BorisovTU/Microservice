
/* Опись на валютные сумки ф.235 */

import DeskInter, RD_Comm;

file BagList( "bag.dbt" ) key 0;
file BagSplit( "bagsplit.dbt" ) key 0;

/* Настройка печати номиналов ВАЛЮТ. 
**   true - печатать
**   false - не печатать
*/                      

const
  PrintParSplit = true;

var 
  ProgID = StrFor( GetProgramID ), 
  PrintFIO = GetRegSetting( V_INTEGER, "КЛАДОВАЯ\\ФИО_ИНКАС_В_ОПИСИ" ), 
  ColName = "", 
  PeerName,
  SealNo, 
  TotalRub = $0;

macro PrintSplit

  var
    TotalItems = 0, TotalSum = $0, Sum, 
    HeadPrinted = false, 
    RecFound;

  ClearRecord( BagSplit );
  BagSplit.Branch = BagList.Branch;  
  BagSplit.Date = BagList.Date;
  BagSplit.ClientType = BagList.ClientType;
  BagSplit.ClientID = BagList.ClientID;
  BagSplit.Number = BagList.Number;
  BagSplit.ValueRef = BagList.ValueRef;
  RecFound = GetGE( BagSplit );
  while ( RecFound                                                             
    and ( BagSplit.Branch == BagList.Branch )  
    and ( BagSplit.Date == BagList.Date )
    and ( BagSplit.ClientType == BagList.ClientType )
    and ( BagSplit.ClientID == BagList.ClientID )
    and ( BagSplit.Number == BagList.Number )
    and ( BagSplit.ValueRef == BagList.ValueRef ) )
    if ( not HeadPrinted )
      PrintLn;
      PrintLn( " ",  CashValue.NameValue );
      [--------------------------------------------------];
      [| Купюры или    |Количество|  Сумма              |];
      [| достоинство   |          |                     |];
      [|---------------|----------|---------------------|];
      HeadPrinted = true;
    end;

    Sum = BagSplit.Character * BagSplit.Amount;
    [ ############### ########## ##################### ]
    ( BagSplit.Character, BagSplit.Amount, Sum );

    TotalItems = TotalItems + BagSplit.Amount;
    TotalSum = TotalSum + Sum;

    RecFound = Next( BagSplit );
  end;
  if ( HeadPrinted )
    [--------------------------------------------------];
    [ Итого           ########## ##################### ]
    ( TotalItems, TotalSum );
  end;
end;

macro PrintListBody( CopyNo )

  var 
    Mark = "", 
    RecFound,
    Sum, 
    RubSum, 
    Stat = true;

  if ( CopyNo == 2 )
    Mark = "-а";
  elif ( CopyNo == 3 )
    Mark = "-в";
  end;

  if ( BankStandart )
    [                                                                   # экз. ]
    ( CopyNo );
  else
    [ Ф. № 235##                                                        # экз. ]
    ( Mark, CopyNo );
  end;
  [                                                                          ];
  [                                О П И С Ь                                 ];
  [     на иностранную валюту и платежные документы в иностранной валюте,    ];
  [ ######################################################################## ]
  ( ( "отправляемые через инкассаторов " + String( Bag.Date:m ) ):c );
  [                                                                          ];
  [                                                                          ];
  if ( Bag.Operation == CASHOP_PANT_RDEB )
    [ Наименование банка-отправителя #                                         ]
    ( {Name_Bank} );
    [ Наименование банка-получателя  #                                         ]
    ( PeerName );
  else
    [ Наименование банка-отправителя #                                         ]
    ( PeerName );
    [ Наименование банка-получателя  #                                         ]
    ( {Name_Bank} );
  end;
  [                                                                          ];
  [ Направляем иностранную валюту и платежные документы в иностранной        ];
  [ валюте, вложенные в инкассаторскую сумку (сумки) № #                     ]
  ( BagNum.NumberBag );
  [ опечатанную (ные) пломибиром № #____________________                           ]
  ( SealNo );
  [                                                                          ];
  [--------------------------------------------------------------------------];
  [| Наименование                     |Код| Сумма по      | Сумма в рублях  |];
  [| иностранной валюты               |вал| номиналу      |                 |];
  [|----------------------------------|---|---------------|-----------------|];
       
  TotalRub = 0;           
  ClearRecord( BagList );
  BagList.Branch = Bag.Branch;  
  BagList.Date = Bag.Date;
  BagList.ClientType = Bag.ClientType;
  BagList.ClientID = Bag.ClientID;
  BagList.Operation = Bag.Operation;
  BagList.Number = Bag.Number;
  RecFound = GetGE( BagList );
  while ( RecFound and Stat
    and ( BagList.Branch == Bag.Branch )  
    and ( BagList.Date == Bag.Date )
    and ( BagList.ClientType == Bag.ClientType )
    and ( BagList.ClientID == Bag.ClientID )
    and ( BagList.Operation == Bag.Operation )
    and ( BagList.Number == Bag.Number ) )
    Stat = FindCashValue( BagList.ValueRef );
    if ( Stat 
      and ( ( CashValue.TypeValue == TYPERES_CURRENCY ) 
         or ( CashValue.TypeValue == TYPERES_PAYDOC ) ) )
      GetCurrCodeAndCBRate( BagList.ValueRef );
      if ( ProgID == "Л" )
        Sum = BagList.RealSum;
      else
        Sum = BagList.DeclaredSum;
      end;
      RubSum = Cur2Rub( Sum );
      [ ################################## ### ############### ################# ]
      (
        Curr.Name_Currency:w,
        Curr.ExternalCode,
        Sum,
        RubSum
      );  
      TotalRub = TotalRub + RubSum;    
    end;
    RecFound = Next( BagList );
  end;

  [--------------------------------------------------------------------------];

  if ( Stat and PrintParSplit )
    ClearRecord( BagList );
    BagList.Branch = Bag.Branch;    
    BagList.Date = Bag.Date;
    BagList.ClientType = Bag.ClientType;
    BagList.ClientID = Bag.ClientID;
    BagList.Operation = Bag.Operation;
    BagList.Number = Bag.Number;
    RecFound = GetGE( BagList );
    while ( RecFound and Stat
      and ( BagList.Branch == Bag.Branch )    
      and ( BagList.Date == Bag.Date )
      and ( BagList.ClientType == Bag.ClientType )
      and ( BagList.ClientID == Bag.ClientID )
      and ( BagList.Operation == Bag.Operation )
      and ( BagList.Number == Bag.Number ) )
      Stat = FindCashValue( BagList.ValueRef );
      if ( Stat and ( CashValue.TypeValue == TYPERES_CURRENCY ) )
        if ( CopyNo == 1 ) 
          if ( GetTrue( true, "Разбивать по номиналам|" + CashValue.NameValue + "?" ) )
            if ( SplitCurrBag( BagList ) )
              PrintSplit;
            end;     
          end;
        end;
      end;
      RecFound = Next( BagList );
    end;
  end;

  if ( not Stat )
    MsgBox( "Ошибка при печати описи" );
    Exit( -1 );
  end;

  [ ];
  [Всего #___________________________________________________________________]
  ( String( TotalRub ) + " (" + RubToStr( TotalRub ) + ")" );
  [ ];
  if ( BankStandart and ( ProgID != "Л" ) )
    [Заведующий кассой _____________________________ ]; 
    [Кассир ________________________________________ ]; 
  else
    [Заведующий кладовой ___________________________ ]; 
    [Контролирующий работник _________________________ ]; 
  end;
  [ ];
  [М.П. ];
  [--------------------------------------------------------------------------]
end;

macro PrintSign1

  ["__" _____________ 200_ г. указанные в описи ценности на сумму];
  [#########################################################################]
  ( ( String( TotalRub ) + " (" + RubToStr( TotalRub ) + ")" ):w );
  [приняты];
  if ( BankStandart and ( ProgID != "Л" ) )
    [Заведующий кассой _____________________________ ]; 
    [Кассир ________________________________________ ]; 
  else
    [Заведующий кладовой ___________________________ ]; 
    [Контролирующий работник _________________________ ]; 
  end;
end;

macro PrintSign2

  ["__" _____________ 200_ г. указанную в описи опломбированую сумку (сумки)];
  [в исправной упаковке, без пересчета в ней (в них) ценностей,             ];
  [принял:];
  if ( PrintFIO )
    [Инкассатор: ____________________________________ #] ( ColName ); 
  else
    [Инкассатор: ____________________________________ ]; 
  end; 
  [ ];
  [ М.П. ];
end;

macro PrintSign3

  ["__" _____________ 200_ г. указанную в описи опломбированую сумку (сумки)];
  [в исправной упаковке, без пересчета в ней (в них) ценностей,             ];
  [приняли:];
  if ( BankStandart and ( ProgID != "Л" ) )
    [Заведующий кассой _____________________________ ]; 
    [Кассир ________________________________________ ]; 
  else
    [Заведующий кладовой (ст. кассир) ______________ ]; 
    [Контролирующий работник _________________________ ]; 
  end;
  [                                                             М.П. ];
  [Ценности сдал:];
  if ( PrintFIO )
    [Инкассатор: ____________________________________ #] ( ColName ); 
  else
    [Инкассатор: ____________________________________ ]; 
  end; 
  [ ];
end;

macro PrintCopy( n )

  PrintListBody( n );
  [ ];
  [                           Р А С П И С К А                               ];
  if ( n == 1 )
    PrintSign1;
  elif ( n == 2 )
    PrintSign2;
  elif ( n == 3 )
    PrintSign3;
  end;
  PrintLn( StrFor( 12 ) );
end;

macro f235()

  if ( Bag.Number == 0 )
    Exit( 1 );
  end;

  CheckPersonType( PT_CONTR );

  if ( not GetTrue( true, "Печатать опись ф.235?" ) )
    Exit( 1 );
  end;

  SealNo = GetSealNo;
  GetClientInfo( Bag.ClientType, Bag.ClientID, PeerName );
  FindBagNum( Bag.Number );
  if ( ( ProgID != "Л" ) and PrintFIO )
    ColName = GetCollectorName( CashDoc.Branch, CashDoc.Collector );
    if ( ColName != "" )
      ColName = "(" + ColName + ")";
    end;
  end;
  PrintCopy( 1 );
  PrintCopy( 2 );
  PrintCopy( 3 );
end;
  