/* Ф. 25-а */

import ResCommP, Pcs2Mon, BrParm, CommonInter,sqlconv;

file PayKind("pay_kind") key 0;
/*file OthStat("sb_othst.dbt") key 2;*/
var OthStat = TBFile("sb_othst.dbt","w",2);
file PayRecip("rtcp_recipient.dbt") key 0;
record PrevOthStat("sb_othst.dbt");
record TotOthStat("sb_othst.dbt");
record TotOthPrevStat("sb_othst.dbt");
file CIAcc("ci_acnt.dbt") key 0;

const
  ShowNullLines=false;  /* Показывать ли строки, где все нули */

/* Сводные счета */

const
  CommPayAcc  = "321",   /* Коммунальные платежи */
  CashOperAcc = "16",    /* Текущие счета учреждений и организаций */
  CapIssueAcc = "218";   /* Ценные бумаги */

const
  CO_CommPay     =  1, /* Коммунальные платежи */
  CO_NalPer      = 11, /* Наличный перевод     */
  CO_Comission   = 21, /* Комиссия             */
  CO_Conversion  = 31, /* Конверсия            */
  CO_Payed       = 41; /* Выплата              */

const
  AS_OPEN = 0,
  AS_CLOSED = 1;

var
  TotPrevRest=$0,
  TotCredit=$0,
  TotDebet=$0,
  TotOtherCred=$0,
  TotOtherDeb=$0,
  TotRest=$0,
  TotPrevRestW=$0,
  TotCreditW=$0,
  TotDebetW=$0,
  TotOtherCredW=$0,
  TotOtherDebW=$0,
  TotRestW=$0,
  TotPrevRestA=$0,
  TotCreditA=$0,
  TotDebetA=$0,
  TotOtherCredA=$0,
  TotOtherDebA=$0,
  TotRestA=$0,
  TotPrevRestF=$0,
  TotCreditF=$0,
  TotDebetF=$0,
  TotOtherCredF=$0,
  TotOtherDebF=$0,
  TotRestF=$0,
  TotPrevAcc=0,
  TotAcc=0,
  TotOpenedAcc=0,
  TotClosedAcc=0,
  TotPrevNOps=0,
  TotNOps=0;

array NameValArr;

macro M2Int( m )

  return Int( Double( m ) );
end;

macro OutTitle;

  var
    brName;

  GetBranchParams( NumFNCash, brName );

  [
   Филиал № #                                                                                                      Форма № 25-а ]
  ( brName );
  [

                                                  Приложение к операционному дневнику № ####
                                                                 за ###########

   #

  ]
  (GetDiaryNo,String({curdate}),DeskSubj.Desc);
end;

macro OutCIHeader;

  if(Chapter==1)
    [                                                      Раздел I. "Ценные бумаги" ];
  else
    [                                                      Раздел II. "Учет ценностей" ];
  end;
  [ ];
  [+--------------+-------------------------+--------------+------------------------------+-----------------------------+---------------+
   |              |                         |              |       П  Р  И  Х  О  Д       |       Р  А  С  Х  О  Д      |               |
   |      №       |                         |  Остаток к   |------------------------------|-----------------------------|   Остаток к   |
   |              |   Наименование счета    |              |   Получено    |    Прочий    |   Выслано    |    Прочий    |               |
   |    счета     |                         | началу  дня  |  от филиалов  |    приход    | учреждениям  |    расход    |   концу дня   |
   |              |                         |              | своего района |              |  Сбербанка   |              |               |
   +--------------+-------------------------+--------------+---------------+--------------+--------------+--------------+---------------+];
end;

macro OutCINameValTail

  var i=1;

  while(i<asize(NameValArr))
    [|              | ####################### |              |               |              |              |              |               |]
    (NameValArr(i));
    i=i+1;
  end;
end;

macro OutCILine(NameVal,PrevRest,CurRest,Cred,OtherCred,Deb,OtherDeb);

  var
    PrevRestA = $0, 
    CurRestA = $0,
    CredA = $0,
    OtherCredA = $0,
    DebA = $0,
    OtherDebA = $0,
    PrevRestS = "", 
    CurRestS = "",
    CredS = "",
    OtherCredS = "",
    DebS = "",
    OtherDebS = "",

    TmpPrevRest,
    TmpCurRest,
    TmpCred,
    TmpOtherCred,
    TmpDeb,
    TmpOtherDeb;

  StrSplit(NameVal,NameValArr,23);

  if((not ShowNullLines) and (PrevRest==$0) and (Cred==$0) and (OtherCred==$0)
     and (Deb==$0) and (OtherDeb==$0) and (CurRest==$0))
    return;
  end;

  if ( Chapter == 1 )
    PrevRestA = Pieces2Money( CashVal.rec.TypeValue, CashVal.rec.CodIntValue, CashVal.rec.Type, PrevRest );
    CurRestA = Pieces2Money( CashVal.rec.TypeValue, CashVal.rec.CodIntValue, CashVal.rec.Type, CurRest );
    CredA = Pieces2Money( CashVal.rec.TypeValue, CashVal.rec.CodIntValue, CashVal.rec.Type, Cred );
    DebA = Pieces2Money( CashVal.rec.TypeValue, CashVal.rec.CodIntValue, CashVal.rec.Type, Deb );
    OtherCredA = Pieces2Money( CashVal.rec.TypeValue, CashVal.rec.CodIntValue, CashVal.rec.Type, OtherCred );
    OtherDebA = Pieces2Money( CashVal.rec.TypeValue, CashVal.rec.CodIntValue, CashVal.rec.Type, OtherDeb );

    if ( PrevRest )
      PrevRestS = String( PrevRestA ) + "/" + M2Int( PrevRest );
    end;
    if ( CurRest )
      CurRestS = String( CurRestA ) + "/" + M2Int( CurRest );
    end;
    if ( Cred )
      CredS = String( CredA ) + "/" + M2Int( Cred );
    end;
    if ( Deb )
      DebS = String( DebA ) + "/" + M2Int( Deb );
    end;
    if ( OtherCred )
      OtherCredS = String( OtherCredA ) + "/" + M2Int( OtherCred );
    end;
    if ( OtherDeb )
      OtherDebS = String( OtherDebA ) + "/" + M2Int( OtherDeb );
    end;

    [|              | ####################### |##############|###############|##############|##############|##############|###############|]
    (NameValArr(0),PrevRestS:r,CredS:r,OtherCredS:r,DebS:r,OtherDebS:r,CurRestS:r);
  else 
    [|              | ####################### |##############|###############|##############|##############|##############|###############|]
    (NameValArr(0),PrevRest:z,Cred:z,OtherCred:z,Deb:z,OtherDeb:z,CurRest:z);
    OutCINameValTail;
    if ( ( CashVal.rec.TypeValue == CV_MinCI + CI_INGOT ) 
      and ( CashVal.rec.UnitMeas = RESMESUNIT_POINT ) )
      FindCIMisc( CI_INGOT, CashVal.rec.CodIntValue );
      GetIngRate( CIMisc.Type, CIMisc.IssueID, {curdate} );
      TmpPrevRest = Quantity2Weight( PrevRest );
      TmpCurRest = Quantity2Weight( CurRest );
      TmpDeb = Quantity2Weight( Deb );
      TmpOtherDeb = Quantity2Weight( OtherDeb );
      TmpCred = Quantity2Weight( Cred );
      TmpOtherCred = Quantity2Weight( OtherCred );
      [|              |   - на массу            |##############|###############|##############|##############|##############|###############|]
      (TmpPrevRest:z,TmpCred:z,TmpOtherCred:z,TmpDeb:z,TmpOtherDeb:z,TmpCurRest:z);
      TotPrevRestW=TotPrevRestW+TmpPrevRest;
      TotCreditW=TotCreditW+TmpCred;
      TotDebetW=TotDebetW+TmpDeb;
      TotOtherCredW=TotOtherCredW+TmpOtherCred;
      TotOtherDebW=TotOtherDebW+TmpOtherDeb;
      TotRestW=TotRestW+TmpCurRest;
  
      TmpPrevRest = Quantity2Rub( PrevRest );
      TmpCurRest = Quantity2Rub( CurRest );
      TmpDeb = Quantity2Rub( Deb );
      TmpOtherDeb = Quantity2Rub( OtherDeb );
      TmpCred = Quantity2Rub( Cred );
      TmpOtherCred = Quantity2Rub( OtherCred );
      [|              |   - в руб. по курсу ЦБ  |##############|###############|##############|##############|##############|###############|]
      (TmpPrevRest:z,TmpCred:z,TmpOtherCred:z,TmpDeb:z,TmpOtherDeb:z,TmpCurRest:z);
      TotPrevRestA=TotPrevRestA+TmpPrevRest;
      TotCreditA=TotCreditA+TmpCred;
      TotDebetA=TotDebetA+TmpDeb;
      TotOtherCredA=TotOtherCredA+TmpOtherCred;
      TotOtherDebA=TotOtherDebA+TmpOtherDeb;
      TotRestA=TotRestA+TmpCurRest;
    end;
  end;
  TotPrevRest=TotPrevRest+PrevRest;
  TotCredit=TotCredit+Cred;
  TotDebet=TotDebet+Deb;
  TotOtherCred=TotOtherCred+OtherCred;
  TotOtherDeb=TotOtherDeb+OtherDeb;
  TotRest=TotRest+CurRest;
  TotPrevRestA=TotPrevRestA+PrevRestA;
  TotCreditA=TotCreditA+CredA;
  TotDebetA=TotDebetA+DebA;
  TotOtherCredA=TotOtherCredA+OtherCredA;
  TotOtherDebA=TotOtherDebA+OtherDebA;
  TotRestA=TotRestA+CurRestA;
end;

macro OutCITotLine;

  [+--------------+-------------------------+--------------+---------------+--------------+--------------+--------------+---------------+];
  if ( Chapter ==  1 )
    [| И т о г о :                            |##############|###############|##############|##############|##############|###############|]
    (TotPrevRest,TotCredit,TotOtherCred,TotDebet,TotOtherDeb,TotRest);
    [| И т о г о   ш т у к и:                 |##############|###############|##############|##############|##############|###############|]
    (M2Int(TotPrevRest),M2Int(TotCredit),M2Int(TotOtherCred),M2Int(TotDebet),M2Int(TotOtherDeb),M2Int(TotRest));
  else
    [| И т о г о :                            |##############|###############|##############|##############|##############|###############|]
    (TotPrevRest,TotCredit,TotOtherCred,TotDebet,TotOtherDeb,TotRest);
    if ( TotPrevRestW or TotCreditW or TotOtherCredW or TotDebetW or TotOtherDebW or TotRestW )
      [|   - на массу                           |##############|###############|##############|##############|##############|###############|]
      (TotPrevRestW,TotCreditW,TotOtherCredW,TotDebetW,TotOtherDebW,TotRestW);
      [|   - в рублях по курсу ЦБ               |##############|###############|##############|##############|##############|###############|]
      (TotPrevRestA,TotCreditA,TotOtherCredA,TotDebetA, TotOtherDebA,TotRestA);
    end;
  end;
end;

macro ReportOnCurCIResource;

  var
    PrevRest=0,
    CurRest=0,
    Deb=0,
    Cred=0,
    OtherDeb=0,
    OtherCred=0;

  if ( not IsShown )
    return;
  end;

  CalcTurns(CashVal.rec.RefValue,Cred,Deb,OtherCred,OtherDeb);
  /*CalcOtherTurns(CashVal.rec.RefValue,OtherCred,OtherDeb);*/

  if ( IsKept )
    CashAcc.addFilter("t.t_Branch = " + string( Branch ) +
		      " and t.t_CashOper = " + GetSQLString( Safe ) + 
		      " and t.t_SubAccount = " + GetSQLString( "" ) +
		      " and t.t_RefValue = " + string( CashVal.rec.RefValue ) );
    CashAcc.KeyNum = 2;
    CashAcc.rec.Branch=Branch;
    CashAcc.rec.CashOper=Safe;
    CashAcc.rec.SubAccount="";
    CashAcc.rec.RefValue=CashVal.rec.RefValue;
    CashAcc.rec.Date={curdate};
    if(CashAcc.GetLT and
      (CashAcc.rec.Branch==Branch) and
      (CashAcc.rec.RefValue==CashVal.rec.RefValue) and
      (CashAcc.rec.CashOper==Safe) and
      (CashAcc.rec.SubAccount==""))
      if(CashVal.rec.UnitMeas==RESMESUNIT_POINT)
        PrevRest=Money(CashAcc.rec.RestCol);
      else
        PrevRest=CashAcc.rec.RestMoney;
      end;
    end;
    CashAcc.dropFilter;

    CashAcc.KeyNum = 1;
    CashAcc.rec.Branch=Branch;
    CashAcc.rec.CashOper=Safe;
    CashAcc.rec.SubAccount="";
    CashAcc.rec.RefValue=CashVal.rec.RefValue;
    CashAcc.rec.Date={curdate};
    if(CashAcc.GetEQ)
      if(CashVal.rec.UnitMeas==RESMESUNIT_POINT)
        CurRest=Money(CashAcc.rec.RestCol);
        if ( {DeskOpen} )
          PrevRest = PrevRest + Money( ( CashAcc.rec.EKredCol - CashAcc.rec.EDebCol ) ); 
        end;
      else
        CurRest=CashAcc.rec.RestMoney;
        if ( {DeskOpen} )
          PrevRest = PrevRest + CashAcc.rec.EKredMoney - CashAcc.rec.EDebMoney; 
        end;
      end;
    end;
  else
    PrevRest = $0;
  end;

  OutCILine(CashVal.rec.NameValue,PrevRest,CurRest,Cred,OtherCred,Deb,OtherDeb);
/*  if(OperBrigade != 0)
    WriteNBData(0,PrevRest,CurRest,Cred,0,Deb,0);
    WriteNBData(OperBrigade,0,0,0,OtherCred,0,OtherDeb);
  else
    WriteNBData(0,PrevRest,CurRest,Cred,OtherCred,Deb,OtherDeb);
  end;   */
end;

macro ReportOnCIResources;

  var stat;

  TotPrevRest  = $0.0L;
  TotCredit    = $0.0L;
  TotDebet     = $0.0L;
  TotOtherCred = $0.0L;
  TotOtherDeb  = $0.0L;
  TotRest      = $0.0L;
  TotPrevRestW = $0.0L;
  TotCreditW   = $0.0L;
  TotDebetW    = $0.0L;
  TotOtherCredW = $0.0L;
  TotOtherDebW = $0.0L;
  TotRestA     = $0.0L;
  TotPrevRestA = $0.0L;
  TotCreditA   = $0.0L;
  TotDebetA    = $0.0L;
  TotOtherCredA = $0.0L;
  TotOtherDebA = $0.0L;
  TotRestA     = $0.0L;

  OutCIHeader;

  CashVal.addFilter("t.t_FlagIntoBalance = " + string( Chapter ) );
  CashVal.rec.FlagIntoBalance = Chapter;
  stat = CashVal.GetGE;
  while(stat and (CashVal.rec.FlagIntoBalance == Chapter) )
    if (/*(CashVal.rec.TypeValue      >=  CV_ValForm) AND*/
        (CashVal.rec.RefValueReport != 1          ) AND
        (CashVal.rec.TypeValue != CV_MinCI + CI_COIN) )
      ReportOnCurCIResource();
    end;
    stat = CashVal.Next;
  end;
  OutCITotLine;

  CashVal.dropFilter;
end;

macro OutVFHeader;

  [                                                  Раздел III. "Ценные бланки" ];
  [ ];
  [+----------------------------------------+------------+-----------+------------+------------+------------+------------------------------+
   |                                        |  Остаток   |           | Израсходов.| Выслано в  |  Остаток   |       №№ ценных бланков      |
   |          Наименование бланка           | к  началу  | Поступило |  по опера- | учреждения |  к концу   |         ( с № по № )         |
   |                                        |    дня     |           |    циям    | Сбербанка  |    дня     |                              |
   +----------------------------------------+------------+-----------+------------+------------+------------+------------------------------+];
end;

macro OutVFNameValTail

  var
   RangeStr = "",
   NameValStr = "",
   RecFound,
   i=1;

  while((RecFound = (Next(RegNumList)
    and (RegNumList.Branch == CashAcc.rec.Branch)
    and (RegNumList.CashOper == CashAcc.rec.CashOper)
    and (RegNumList.SubAccount == "")
    and (RegNumList.Date == CashAcc.rec.Date)
    and (RegNumList.ValueRef == CashAcc.rec.RefValue)))
    or (i < asize(NameValArr)))
    if(i < asize(NameValArr))
      NameValStr = NameValArr(i);
    end;
    if(RecFound)
      RangeStr = MakeRangeStr;
    end;

    [| ###################################### |            |           |            |            |            | ############################ |]
    (
      NameValStr,
      RangeStr
    );
    i=i+1;
  end;
end;

macro OutVFLine(NameVal,PrevRest,CurRest,Cred,OtherCred,Deb,OtherDeb);

  var
    RangeStr = "";

  StrSplit(NameVal,NameValArr,38);

  if((not ShowNullLines) and (PrevRest==$0) and (Cred==$0) and (OtherCred==$0)
     and (Deb==$0) and (OtherDeb==$0) and (CurRest==$0))
    return;
  end;

  ClearRecord(RegNumList);
  RegNumList.Branch = CashAcc.rec.Branch;
  RegNumList.CashOper = CashAcc.rec.CashOper;
  RegNumList.SubAccount = "";
  RegNumList.ValueRef = CashAcc.rec.RefValue;
  RegNumList.Date = CashAcc.rec.Date;
  if(GetGE(RegNumList)
    and (RegNumList.Branch == CashAcc.rec.Branch)
    and (RegNumList.CashOper == CashAcc.rec.CashOper)
    and (RegNumList.SubAccount == "")
    and (RegNumList.Date == CashAcc.rec.Date)
    and (RegNumList.ValueRef == CashAcc.rec.RefValue))
    RangeStr = MakeRangeStr;
  end;

  [| ###################################### | ########## | ######### | ########## | ########## | ########## | ############################ |]
  (NameValArr(0),PrevRest:z,Cred+OtherCred:z,OtherDeb:z,Deb:z,CurRest:z,RangeStr);
  OutVFNameValTail;
  TotPrevRestF=TotPrevRestF+PrevRest;
  TotCreditF=TotCreditF+Cred;
  TotDebetF=TotDebetF+Deb;
  TotOtherCredF=TotOtherCredF+OtherCred;
  TotOtherDebF=TotOtherDebF+OtherDeb;
  TotRestF=TotRestF+CurRest;
end;

macro OutVFTotLine;

  [+----------------------------------------+------------+-----------+------------+------------+------------+------------------------------+];
  [| И т о г о                              | ########## | ######### | ########## | ########## | ########## |                              |]
  (TotPrevRestF,TotCreditF+TotOtherCredF,TotOtherDebF,TotDebetF,TotRestF);
end;

macro ReportOnCurVFResource;

  var
    PrevRest=$0,
    CurRest=$0,
    Deb=$0,
    Cred=$0,
    OtherDeb=$0,
    OtherCred=$0;

  if ( not IsShown )
    return;
  end;

  CalcTurns(CashVal.rec.RefValue, Cred, Deb, OtherCred, OtherDeb);
  /*CalcOtherTurns(CashVal.rec.RefValue,OtherCred,OtherDeb);*/

  if ( IsKept )
    CashAcc.addFilter("t.t_Branch = " + string( Branch ) +
		      " and t.t_CashOper = " + GetSQLString( Safe ) + 
		      " and t.t_SubAccount = " + GetSQLString( "" ) +
		      " and t.t_RefValue = " + string( CashVal.rec.RefValue ) );
    CashAcc.KeyNum = 2;
    CashAcc.rec.Branch=Branch;
    CashAcc.rec.CashOper=Safe;
    CashAcc.rec.SubAccount="";
    CashAcc.rec.RefValue=CashVal.rec.RefValue;
    CashAcc.rec.Date={curdate};
    if(CashAcc.GetLT and
      (CashAcc.rec.Branch==Branch) and
      (CashAcc.rec.RefValue==CashVal.rec.RefValue) and
      (CashAcc.rec.CashOper==Safe) and
      (CashAcc.rec.SubAccount==""))
      if(CashVal.rec.UnitMeas==RESMESUNIT_POINT)
        PrevRest=Money(CashAcc.rec.RestCol);
      else
        PrevRest=CashAcc.rec.RestMoney;
      end;
    end;
    CashAcc.dropFilter;

    CashAcc.KeyNum = 1;
    CashAcc.rec.Branch=Branch;
    CashAcc.rec.CashOper=Safe;
    CashAcc.rec.SubAccount="";
    CashAcc.rec.RefValue=CashVal.rec.RefValue;
    CashAcc.rec.Date={curdate};
    if(CashAcc.GetEQ)
      if(CashVal.rec.UnitMeas==RESMESUNIT_POINT)
        CurRest=Money(CashAcc.rec.RestCol);
        if ( {DeskOpen} )
          PrevRest = PrevRest + Money( ( CashAcc.rec.EKredCol - CashAcc.rec.EDebCol ) ); 
        end;
      else
        CurRest=CashAcc.rec.RestMoney;
        if ( {DeskOpen} )
          PrevRest = PrevRest + CashAcc.rec.EKredMoney - CashAcc.rec.EDebMoney; 
        end;
      end;
    end;
  else
    PrevRest = $0;
  end;

  OutVFLine(CashVal.rec.NameValue,PrevRest,CurRest,Cred,OtherCred,Deb,OtherDeb);
/*  if(OperBrigade != 0)
    WriteNBData(0,PrevRest,CurRest,Cred,0,Deb,0);
    WriteNBData(OperBrigade,0,0,0,OtherCred,0,OtherDeb);
  else
    WriteNBData(0,PrevRest,CurRest,Cred,OtherCred,Deb,OtherDeb);
  end;   */
end;

macro ReportOnVFResources;

  CashVal.rec.FlagIntoBalance = 3;

  OutVFHeader;

  CashVal.addFilter("t.t_FlagIntoBalance = 3");
  if(CashVal.GetGE and ( CashVal.rec.FlagIntoBalance == 3 ) )
    if(CashVal.rec.RefValueReport!=1)
      ReportOnCurVFResource;
    end;
    while(CashVal.Next and ( CashVal.rec.FlagIntoBalance == 3 ) )
      if(CashVal.rec.RefValueReport!=1)
        ReportOnCurVFResource;
      end;
    end;
  end;
  CashVal.dropFilter;

  OutVFTotLine;
end;

macro OutStatHead

  [                                                          Раздел IV. "Учет вкладов" ];
  [ ];
  [+--------------+-------------------------------+-----------------------+-----------------------+-----------------------+-----------------------+
   |       №      |                               | Остаток к началу дня  |        Приход         |         Расход        |  Остаток к концу дня  |
   |    счета     |      Наименование счета       |------+----------------|------+----------------|------+----------------|------+----------------|
   |              |                               |счета |     сумма      |счета |     сумма      |счета |     сумма      |счета |     сумма      |
   +--------------+-------------------------------+------+----------------+------+----------------+------+----------------+------+----------------+];
end;

macro OutStatTotals

  [+--------------+-------------------------------+------+----------------+------+----------------+------+----------------+------+----------------+];
  [| И т о г о:                                   | #### | ############## | #### | ############## | #### | ############## | #### | ############## |]
  (
    TotPrevAcc,
    TotPrevRest,
    TotOpenedAcc,
    TotCredit,
    TotClosedAcc,
    TotDebet,
    TotAcc,
    TotRest
  );
end;

macro OutDepLine

  var
    CitStr;

  if((not ShowNullLines)
     and (PrevStat.ColOpenedAcc==0)
     and (PrevStat.Rest==0)
     and (DepStat.rec.ColAccounts==PrevStat.ColAccounts)
     and (DepStat.rec.KredSum==0)
     and (DepStat.rec.ColClosedAcc==PrevStat.ColClosedAcc)
     and (DepStat.rec.DebSum==0)
     and (DepStat.rec.ColOpenedAcc==0)
     and (DepStat.rec.Rest==0))
    return;
  end;

  if(not DepStat.rec.FlagRez)
    CitStr = " (рез.)";
  else
    CitStr = " (нер.)";
  end;

  [| ############ | ############################# | #### | ############## | #### | ############## | #### | ############## | #### | ############## |]
  (
    DepType.BalAcc,
    DepType.Name+CitStr,
    PrevStat.ColOpenedAcc:z,
    PrevStat.Rest:z,
    DepStat.rec.CurOpenAc:z,
    DepStat.rec.KredSum:z,
    DepStat.rec.CurClosAc:z,
    DepStat.rec.DebSum:z,
    DepStat.rec.ColOpenedAcc:z,
    DepStat.rec.Rest:z
  );
  TotPrevAcc = TotPrevAcc + PrevStat.ColOpenedAcc;
  TotOpenedAcc = TotOpenedAcc + DepStat.rec.CurOpenAc;
  TotClosedAcc = TotClosedAcc + DepStat.rec.CurClosAc;
  TotAcc = TotAcc + DepStat.rec.ColOpenedAcc;
  TotPrevRest = TotPrevRest + PrevStat.Rest;
  TotCredit = TotCredit + DepStat.rec.KredSum;
  TotDebet = TotDebet + DepStat.rec.DebSum;
  TotRest = TotRest + DepStat.rec.Rest;
end;

macro OutOthStatLineCO

  [| ############ | Текущие счета учреждений и    |      | ############## |      | ############## |      | ############## |      | ############## |]
  (
    CashOperAcc,
    TotOthPrevStat.Rest:z,
    TotOthStat.KredSum:z,
    TotOthStat.DebSum:z,
    TotOthStat.Rest:z
  );
  [|              | организаций, сосотоящих на    |      |                |      |                |      |                |      |                |];
  [|              | местных бюджетах              |      |                |      |                |      |                |      |                |];
  TotPrevAcc = TotPrevAcc + 1;
  TotAcc = TotAcc +1;
end;

macro OutOthStatLineCI

  [| ############ | Облигации государственных     | #### | ############## | #### | ############## | #### | ############## | #### | ############## |]
  (
    CapIssueAcc,
    PrevStat.ColOpenedAcc:z,
    PrevStat.Rest:z,
    DepStat.rec.ColAccounts-PrevStat.ColAccounts:z,
    DepStat.rec.KredSum:z,
    DepStat.rec.ColClosedAcc-PrevStat.ColClosedAcc:z,
    DepStat.rec.DebSum:z,
    DepStat.rec.ColOpenedAcc:z,
    DepStat.rec.Rest:z
  );
  [|              | займов, сертификаты и другие  |      |                |      |                |      |                |      |                |];
  [|              | ценные бумаги, принятые на хр.|      |                |      |                |      |                |      |                |];
  TotPrevAcc = TotPrevAcc + PrevStat.ColOpenedAcc;
  TotOpenedAcc = TotOpenedAcc + DepStat.rec.ColAccounts-PrevStat.ColAccounts;
  TotClosedAcc = TotClosedAcc + DepStat.rec.ColClosedAcc-PrevStat.ColClosedAcc;
  TotAcc = TotAcc + DepStat.rec.ColOpenedAcc;
  TotPrevRest = TotPrevRest + PrevStat.Rest;
  TotCredit = TotCredit + DepStat.rec.KredSum;
  TotDebet = TotDebet + DepStat.rec.DebSum;
  TotRest = TotRest + DepStat.rec.Rest;
end;

macro OutOthStatLineCP

  [| ############ | Коммунальные платежи          |      | ############## |      | ############## |      | ############## |      | ############## |]
  (
    CommPayAcc,
    TotOthPrevStat.Rest:z,
    TotOthStat.KredSum:z,
    TotOthStat.DebSum:z,
    TotOthStat.Rest:z
  );
  TotPrevAcc = TotPrevAcc + 1;
  TotAcc = TotAcc +1;
end;

macro ReportOnCurDepType(Citizenship)

  record TmpStat("sb_depst.dbt");

  DepStat.rec.FNCash=Branch;
  DepStat.rec.IsCur=0;
  DepStat.rec.Kind=DepType.Kind;
  DepStat.rec.CodCur=0;
  DepStat.rec.FlagRez=Citizenship;
  DepStat.rec.Date={curdate};
  DepStat.addFilter("t.t_FNCash = " + string(Branch) +
		    " and t.t_IsCur = 0" + 
		    " and t.t_Kind = " + GetSQLString( DepType.Kind ) +
		    " and t.t_CodCur = 0" + 
		    " and t.t_FlagRez = " + sqlChar( Citizenship ) );

  if(not (DepStat.GetLE
     and (DepStat.rec.FNCash==Branch)
     and (DepStat.rec.IsCur==0)
     and (DepStat.rec.Kind==DepType.Kind)
     and (DepStat.rec.CodCur==0)
     and (DepStat.rec.FlagRez==Citizenship)))
    DepStat.dropFilter;
    return;
  end;

  if(DepStat.rec.Date=={curdate})
    Copy(TmpStat,DepStat);
    if(DepStat.Prev
      and (DepStat.rec.FNCash==Branch)
      and (DepStat.rec.IsCur==0)
      and (DepStat.rec.Kind==DepType.Kind)
      and (DepStat.rec.CodCur==0)
      and (DepStat.rec.FlagRez==Citizenship))
      Copy(PrevStat,DepStat);
      Copy(DepStat,TmpStat);
    else
      ClearRecord(PrevStat);
      Copy(DepStat,TmpStat);
    end;
  else
    Copy(PrevStat,DepStat);
    DepStat.rec.CurOpenAc=0;
    DepStat.rec.CurClosAc=0;
    DepStat.rec.DebSum=0;
    DepStat.rec.KredSum=0;
  end;
  OutDepLine;
  DepStat.dropFilter;
end;

macro ReportOnDepTypes

  var
    RecFound;

  TotPrevRest = $0;
  TotCredit = $0;
  TotDebet = $0;
  TotRest = $0;
  ClearRecord(DepType);
  DepType.FlagCur=0;
  RecFound=GetGE(DepType);
  while(RecFound and (DepType.FlagCur==0))
    if(DepType.Kind!="Служебный")
      ReportOnCurDepType(StrFor(0));
      ReportOnCurDepType(StrFor(1));
    end;
    RecFound=Next(DepType);
  end;
end;

macro AddOthStat(s1,s2)

  s1.Rest=s1.Rest+s2.Rest;
  s1.KredSum=s1.KredSum+s2.KredSum;
  s1.DebSum=s1.DebSum+s2.DebSum;
  s1.ColOper=s1.ColOper+s2.ColOper;
end;

macro AddToTotStatCO

  record TmpStat("sb_othst.dbt");

  OthStat.rec.FNCash=Branch;
  OthStat.rec.AplicKind=SB_CASHOPERT;
  OthStat.rec.CodCur=0;
  OthStat.rec.GroupOper=PayKind.GroupOpert;
  OthStat.rec.NumOper=PayKind.NumOperat;
  OthStat.rec.DateOper={curdate};
  OthStat.addFilter("t.t_FNCash = " + string(Branch) +
		    " and t.t_AplicKind = " + string(SB_CASHOPERT) +
		    " and t.t_CodCur = 0 " +
		    " and t.t_GroupOper = " + string( PayKind.GroupOpert ) +
		    " and t.t_NumOper = " + string( PayKind.NumOperat ) );
  if(not (OthStat.GetLE
     and (OthStat.rec.FNCash==Branch)
     and (OthStat.rec.AplicKind==SB_CASHOPERT)
     and (OthStat.rec.CodCur==0)
     and (OthStat.rec.GroupOper==PayKind.GroupOpert)
     and (OthStat.rec.NumOper==PayKind.NumOperat)))
    OthStat.dropFilter;
    return;
  end;

  if(OthStat.rec.DateOper=={curdate})
    Copy(TmpStat,OthStat);
    if(OthStat.Prev
      and (OthStat.rec.FNCash==Branch)
      and (OthStat.rec.AplicKind==SB_CASHOPERT)
      and (OthStat.rec.CodCur==0)
      and (OthStat.rec.GroupOper==PayKind.GroupOpert)
      and (OthStat.rec.NumOper==PayKind.NumOperat))
      AddOthStat(TotOthPrevStat,OthStat.rec);
      AddOthStat(TotOthStat,TmpStat);
    else
      AddOthStat(TotOthStat,TmpStat);
    end;
  else
    OthStat.rec.ColOper=0;
    OthStat.rec.DebSum=0;
    OthStat.rec.KredSum=0;
    AddOthStat(TotOthStat,OthStat.rec);
  end;
  OthStat.dropFilter;
end;

macro ReportOnCashOps

  var
    RecFound;

  ClearRecord(TotOthStat);
  ClearRecord(TotOthPrevStat);

  ClearRecord(PayKind);
  PayKind.IsCur=0;
  PayKind.GroupOpert=CO_NalPer;
  RecFound=GetGE(PayKind);
  while(RecFound
    and (PayKind.IsCur==0)
    and (PayKind.GroupOpert==CO_NalPer))
    AddToTotStatCO;
    RecFound=Next(PayKind);
  end;

  ClearRecord(PayKind);
  PayKind.IsCur=0;
  PayKind.GroupOpert=CO_Payed;
  RecFound=GetGE(PayKind);
  while(RecFound
    and (PayKind.IsCur==0)
    and (PayKind.GroupOpert==CO_Payed))
    AddToTotStatCO;
    RecFound=Next(PayKind);
  end;
  OutOthStatLineCO;
end;

macro AddToTotStatCI(Type,Issue)

  var
    RecFound;

  ClearRecord(CIAcc);
  CIAcc.Branch = Branch;
  CIAcc.Type = Type;
  CIAcc.Issue = Issue;
  RecFound = GetGE(CIAcc);
  while(RecFound
    and (CIAcc.Branch == Branch)
    and (CIAcc.Type == Type)
    and (CIAcc.Issue == Issue))
    DepStat.rec.ColAccounts = DepStat.rec.ColAccounts + 1;
    if(CIAcc.OpenDate != {curdate})
      PrevStat.ColAccounts = PrevStat.ColAccounts + 1;
    end;
    if(CIAcc.State == AS_OPEN)
      DepStat.rec.ColOpenedAcc = DepStat.rec.ColOpenedAcc + 1;
      DepStat.rec.Rest = DepStat.rec.Rest + CIAcc.Sum;
      if(CIAcc.OpenDate != {curdate})
        PrevStat.ColOpenedAcc = PrevStat.ColOpenedAcc + 1;
        PrevStat.Rest = PrevStat.Rest + CIAcc.Sum;
      else
        DepStat.rec.KredSum = DepStat.rec.KredSum + CIAcc.Sum;
      end;
    else
      DepStat.rec.ColClosedAcc = DepStat.rec.ColClosedAcc + 1;
      if(CIAcc.OpenDate != {curdate})
        PrevStat.ColClosedAcc = PrevStat.ColClosedAcc + 1;
      else
        PrevStat.Rest = PrevStat.Rest + CIAcc.Sum;
        DepStat.rec.DebSum = DepStat.rec.DebSum + CIAcc.Sum;
      end;
    end;
    RecFound = Next(CIAcc);
  end;
end;

macro ReportOnCapIssues

  var
    RecFound;

  ClearRecord(DepStat.rec);
  ClearRecord(PrevStat);

  ClearRecord(OGSZIss);
  OGSZIss.Type=CI_OGSZ;
  RecFound=GetGE(OGSZIss);
  while(RecFound and (OGSZIss.Type==CI_OGSZ))
    AddToTotStatCI(OGSZIss.Type,OGSZIss.IssueID);
    RecFound=Next(OGSZIss);
  end;

  Rewind(CIMisc);
  while(Next(CIMisc))
    AddToTotStatCI(CIMisc.Type,CIMisc.IssueID);
  end;

  ClearRecord(Cert);
  Cert.Type=CI_CERT;
  RecFound=GetGE(Cert);
  while(RecFound and (Cert.Type==CI_CERT))
    AddToTotStatCI(Cert.Type,Cert.Issue);
    RecFound=Next(Cert);
  end;

  OutOthStatLineCI;
end;

macro AddToTotStatCP

  record TmpStat("sb_othst.dbt");

  OthStat.rec.FNCash=Branch;
  OthStat.rec.AplicKind=SB_COMPAY;
  OthStat.rec.CodCur=0;
  OthStat.rec.GroupOper=CO_CommPay;
  OthStat.rec.NumOper=PayRecip.ID;
  OthStat.rec.DateOper={curdate};
  OthStat.addFilter("t.t_FNCash = " + string(Branch) +
		    " and t.t_AplicKind = " + string(SB_COMPAY) +
		    " and t.t_CodCur = 0 " +
		    " and t.t_GroupOper = " + string( CO_CommPay ) +
		    " and t.t_NumOper = " + string( PayRecip.ID ) );
  if(not (OthStat.GetLE
     and (OthStat.rec.FNCash==Branch)
     and (OthStat.rec.AplicKind==SB_COMPAY)
     and (OthStat.rec.CodCur==0)
     and (OthStat.rec.GroupOper==CO_CommPay)
     and (OthStat.rec.NumOper==PayRecip.ID)))
    OthStat.dropFilter;
    return;
  end;

  if(OthStat.rec.DateOper=={curdate})
    Copy(TmpStat,OthStat);
    if(OthStat.Prev
      and (OthStat.rec.FNCash==Branch)
      and (OthStat.rec.AplicKind==SB_COMPAY)
      and (OthStat.rec.CodCur==0)
      and (OthStat.rec.GroupOper==CO_CommPay)
      and (OthStat.rec.NumOper==PayRecip.ID))
      AddOthStat(TotOthPrevStat,OthStat.rec);
      AddOthStat(TotOthStat,TmpStat);
    else
      AddOthStat(TotOthStat,TmpStat);
    end;
  else
    OthStat.rec.ColOper=0;
    OthStat.rec.DebSum=0;
    OthStat.rec.KredSum=0;
    AddOthStat(TotOthStat,OthStat.rec);
  end;
  OthStat.dropFilter;
end;

macro ReportOnCommPays

  var
    RecFound;

  ClearRecord(TotOthStat);
  ClearRecord(TotOthPrevStat);

  ClearRecord(PayRecip);
  RecFound=GetGE(PayRecip);
  while(RecFound)
    AddToTotStatCP;
    RecFound=Next(PayRecip);
  end;

  OutOthStatLineCP;
end;

macro TreatCurCrResource;

  var
    PrevRest=$0,
    CurRest=$0,
    Deb=$0,
    Cred=$0,
    OtherDeb=$0,
    OtherCred=$0;

  Currency.Code_Currency=CashVal.rec.CodIntValue;
  if(not GetEQ(Currency))
    ClearRecord(Currency);
    Currency.Name_Currency="не найдена";
  end;

  /*CalcOtherTurns(CashVal.rec.RefValue,OtherCred,OtherDeb);*/

  CashAcc.addFilter("t.t_Branch = " + string( Branch ) +
		    " and t.t_CashOper = " + GetSQLString( Safe ) +
		    " and t.t_SubAccount = " + GetSQLString( "" ) + 
		    " and t.t_RefValue = " + string( CashVal.rec.RefValue ) );
  CashAcc.KeyNum = 2;
  CashAcc.rec.Branch=Branch;
  CashAcc.rec.CashOper=Safe;
  CashAcc.rec.SubAccount="";
  CashAcc.rec.RefValue=CashVal.rec.RefValue;
  CashAcc.rec.Date={curdate};
  if(CashAcc.GetLT and
    (CashAcc.rec.Branch==Branch) and
    (CashAcc.rec.RefValue==CashVal.rec.RefValue) and
    (CashAcc.rec.CashOper==Safe) and
    (CashAcc.rec.SubAccount == ""))
    PrevRest=CashAcc.rec.RestMoney;
  end;
  CashAcc.dropFilter;

  CashAcc.KeyNum = 1;
  CashAcc.rec.Branch=Branch;
  CashAcc.rec.CashOper=Safe;
  CashAcc.rec.SubAccount="";
  CashAcc.rec.RefValue=CashVal.rec.RefValue;
  CashAcc.rec.Date={curdate};
  if(CashAcc.GetEQ)
    CurRest=CashAcc.rec.RestMoney;
  end;

  CashAcc.rec.Branch=Branch;
  CashAcc.rec.CashOper="Инкассация";
  CashAcc.rec.SubAccount="";
  CashAcc.rec.RefValue=CashVal.rec.RefValue;
  CashAcc.rec.Date={curdate};
  if(CashAcc.GetEQ)
    Cred=CashAcc.rec.DebMoney;
    Deb=CashAcc.rec.KredMoney;
  end;
  WriteNBData(0,PrevRest,CurRest,Cred,0,Deb,0);
end;

macro TreatCrResources;

  var
    PrevKey = CashVal.KeyNum;
    CashVal.KeyNum = 1;

  CashVal.rec.TypeValue=CV_Currency;
  CashVal.rec.CodIntValue=0;

  if(CashVal.GetEQ)
    TreatCurCrResource;
  end;

  CashVal.KeyNum = PrevKey;
end;

macro OutOpHead

  [ ];
  [                                                       Раздел V. "Учет операций" ];
  [ ];
  [+--------------------------------------------------------------------+-------------------------------------------------------------------------+
   |                                                                    |                          Количество операций                            |
   |                      В И Д Ы   О П Е Р А Ц И Й                     |-------------------------+------------------------+----------------------|
   |                                                                    |      К началу дня       |         За день        |  С начала квартала   |
   +--------------------------------------------------------------------+-------------------------+------------------------+----------------------+];
end;

macro OutMemHead

  [ ];
  [                                       Раздел VI. "Справка о безналичных операциях за квартал" ];
  [ ];
  [+--------------------------------------------------------------------+-------------------------------------------------------------------------+
   |                                                                    |      К началу дня       |         За день        |  С начала квартала   |
   +--------------------------------------------------------------------+-------------------------+------------------------+----------------------+];
end;

macro OutOpMemLine(OpName, PrevNOps, NOps)

  if(PrevNOps or NOps)
    [| ################################################################## | ####################### | ###################### | #################### |]
    (
      OpName,
      PrevNOps:z,
      NOps - PrevNOps:z,
      NOps:z
    );
    TotPrevNOps = TotPrevNOps + PrevNOps;
    TotNOps = TotNOps + NOps;
  end;
end;

macro ReportOnDepOps

  record TmpStat("sb_depst.dbt");

  DepStat.rec.FNCash=Branch;
  DepStat.rec.IsCur=0;
  DepStat.rec.Kind="Общ.стат.оп.";
  DepStat.rec.CodCur=0;
  DepStat.rec.FlagRez=StrFor(0);
  DepStat.rec.Date={curdate};
  DepStat.addFilter("t.t_FNCash = " + string(Branch) +
		    " and t.t_IsCur = 0" + 
		    " and t.t_Kind = 'Общ.стат.оп.'" + 
		    " and t.t_CodCur = 0" +
		    " and t.t_FlagRez = " + StrFor(0) );
  if(not (DepStat.GetLE
     and (DepStat.rec.FNCash==Branch)
     and (DepStat.rec.IsCur==0)
     and (DepStat.rec.Kind=="Общ.стат.оп.")
     and (DepStat.rec.CodCur==0)
     and (DepStat.rec.FlagRez==StrFor(0))))
    DepStat.dropFilter;
    return;
  end;

  if(DepStat.rec.Date=={curdate})
    Copy(TmpStat,DepStat);
    if(DepStat.Prev
      and (DepStat.rec.FNCash==Branch)
      and (DepStat.rec.IsCur==0)
      and (DepStat.rec.Kind=="Общ.стат.оп.")
      and (DepStat.rec.CodCur==0)
      and (DepStat.rec.FlagRez==StrFor(0)))
      Copy(PrevStat,DepStat);
      Copy(DepStat,TmpStat);
    else
      ClearRecord(PrevStat);
      Copy(DepStat,TmpStat);
    end;
  else
    Copy(PrevStat,DepStat);
  end;

  OutOpMemLine("1. Первоначальные взносы по вкладам населения",
    PrevStat.OpenInOper, DepStat.rec.OpenInOper);
  OutOpMemLine("2. Дополнительные взносы по вкладам населения",
    PrevStat.DopInOper, DepStat.rec.DopInOper);
  OutOpMemLine("3. Частичные выдачи и закрытие счетов по вкладам населения",
    PrevStat.DopInOper, DepStat.rec.DopInOper);
  DepStat.dropFilter;
end;

macro ReportOnCIOps

  record TmpStat("sb_othst.dbt");

  OthStat.rec.FNCash=Branch;
  OthStat.rec.AplicKind=SB_CAPISSUE;
  OthStat.rec.CodCur=0;
  OthStat.rec.GroupOper=0;
  OthStat.rec.NumOper=0;
  OthStat.rec.DateOper={curdate};
  OthStat.addFilter("t.t_FNCash = " + string(Branch) +
		    " and t.t_AplicKind = " + string(SB_CAPISSUE) +
		    " and t.t_CodCur = 0 " +
		    " and t.t_GroupOper = 0" +
		    " and t.t_NumOper = 0" );

  if(not (OthStat.GetLE
     and (OthStat.rec.FNCash==Branch)
     and (OthStat.rec.AplicKind==SB_CAPISSUE)
     and (OthStat.rec.CodCur==0)
     and (OthStat.rec.GroupOper==0)
     and (OthStat.rec.NumOper==0)))
    OthStat.dropFilter;
    return;
  end;

  if(OthStat.rec.DateOper=={curdate})
    Copy(TmpStat,OthStat);
    if(OthStat.Prev
      and (OthStat.rec.FNCash==Branch)
      and (OthStat.rec.AplicKind==SB_CAPISSUE)
      and (OthStat.rec.CodCur==0)
      and (OthStat.rec.GroupOper==0)
      and (OthStat.rec.NumOper==0))
      Copy(PrevOthStat,OthStat);
      Copy(OthStat,TmpStat);
    else
      ClearRecord(PrevOthStat);
      Copy(OthStat,TmpStat);
    end;
  else
    Copy(PrevOthStat,OthStat);
  end;
  OutOpMemLine("5. Операции с ОГЗ и билетами денежно-вещевых лотерей",
    PrevOthStat.ColOper, OthStat.rec.ColOper);
  OthStat.dropFilter;
end;

macro ReportOnCOOps

  var
    RecFound;

  ClearRecord(TotOthStat);
  ClearRecord(TotOthPrevStat);

  ClearRecord(PayKind);
  PayKind.GroupOpert = CO_NalPer;
  AddToTotStatCO;
  PayKind.GroupOpert = CO_Comission;
  AddToTotStatCO;
  PayKind.GroupOpert = CO_Conversion;
  AddToTotStatCO;
  PayKind.GroupOpert = CO_Payed;
  AddToTotStatCO;

  OutOpMemLine("6. Операции по приему платежей от организаций и населения",
    TotOthPrevStat.ColOper, TotOthStat.ColOper);
end;

macro ReportOnCPOps

  var
    RecFound;

  record TmpStat("sb_othst.dbt");

  OthStat.rec.FNCash=Branch;
  OthStat.rec.AplicKind=SB_COMPAY;
  OthStat.rec.CodCur=0;
  OthStat.rec.GroupOper=CO_CommPay;
  OthStat.rec.NumOper=0;
  OthStat.rec.DateOper={curdate};
  OthStat.addFilter("t.t_FNCash = " + string(Branch) +
		    " and t.t_AplicKind = " + string(SB_COMPAY) +
		    " and t.t_CodCur = 0 " +
		    " and t.t_GroupOper = " + string( CO_CommPay ) +
		    " and t.t_NumOper = 0"  );
  if(not (OthStat.GetLE
     and (OthStat.rec.FNCash==Branch)
     and (OthStat.rec.AplicKind==SB_COMPAY)
     and (OthStat.rec.CodCur==0)
     and (OthStat.rec.GroupOper==CO_CommPay)
     and (OthStat.rec.NumOper==0)))
    OthStat.dropFilter;
    return;
  end;

  if(OthStat.rec.DateOper=={curdate})
    Copy(TmpStat,OthStat);
    if(OthStat.Prev
      and (OthStat.rec.FNCash==Branch)
      and (OthStat.rec.AplicKind==SB_COMPAY)
      and (OthStat.rec.CodCur==0)
      and (OthStat.rec.GroupOper==CO_CommPay)
      and (OthStat.rec.NumOper==0))
      Copy(PrevOthStat,OthStat);
      Copy(OthStat,TmpStat);
    else
      ClearRecord(PrevOthStat);
      Copy(OthStat,TmpStat);
    end;
  else
    Copy(PrevOthStat,OthStat);
  end;

  OutOpMemLine("7. Другие кассовые и расчетные операции",
    PrevOthStat.ColOper, OthStat.rec.ColOper);
  OthStat.dropFilter;
end;

macro OutOpTotals

  [+--------------------------------------------------------------------+-------------------------+------------------------+----------------------+];
  [| И т о г о :                                                        | ####################### | ###################### | #################### |]
  (
    TotPrevNOps,
    TotNOps - TotPrevNOps,
    TotNOps
  );
end;

macro ReportOnOps

  TotPrevNOps = 0;
  TotNOps = 0;
  OutOpHead;
  ReportOnDepOps;
  ReportOnCIOps;
  ReportOnCOOps;
  ReportOnCPOps;
  OutOpTotals;
end;

macro ReportOnMemTurns

  record TmpStat("sb_depst.dbt");

  OutMemHead;

  DepStat.rec.FNCash=Branch;
  DepStat.rec.IsCur=0;
  DepStat.rec.Kind="Общ.стат.оп.";
  DepStat.rec.CodCur=0;
  DepStat.rec.FlagRez=StrFor(0);
  DepStat.rec.Date={curdate};
  DepStat.addFilter("t.t_FNCash = " + string(Branch) +
		    " and t.t_IsCur = 0" + 
		    " and t.t_Kind = 'Общ.стат.оп.'" + 
		    " and t.t_CodCur = 0" +
		    " and t.t_FlagRez = " + StrFor(0) );
  if(not (DepStat.GetLE
     and (DepStat.rec.FNCash==Branch)
     and (DepStat.rec.IsCur==0)
     and (DepStat.rec.Kind=="Общ.стат.оп.")
     and (DepStat.rec.CodCur==0)
     and (DepStat.rec.FlagRez==StrFor(0))))
    DepStat.dropFilter;
    return;
  end;

  if(DepStat.rec.Date=={curdate})
    Copy(TmpStat,DepStat);
    if(DepStat.Prev
      and (DepStat.rec.FNCash==Branch)
      and (DepStat.rec.IsCur==0)
      and (DepStat.rec.Kind=="Общ.стат.оп.")
      and (DepStat.rec.CodCur==0)
      and (DepStat.rec.FlagRez==StrFor(0)))
      Copy(PrevStat,DepStat);
      Copy(DepStat,TmpStat);
    else
      ClearRecord(PrevStat);
      Copy(DepStat,TmpStat);
    end;
  else
    Copy(PrevStat,DepStat);
  end;

  OutOpMemLine("1. Безналичные зачисления во вклады",
    PrevStat.MemInOper, DepStat.rec.MemInOper);
  DepStat.dropFilter;
end;

macro OutBottom
  [







   ------------------------------------------------------------------------------------------------------------------------------------------------
   Ст.контролер (контролер) _________________________________________________ Кассир ______________________________________________________________

   Отчет проверил "___" ________________ ______ г.                            Бухгалтер ___________________________________________________________];
end;

  var
    RegPath = "RS-RETAIL\\ПЕЧАТИ\\ФОРМА_25";

  CheckPrinter( RegPath, StrFor( 0 ) );

macro TreatCurrentSafe

  TotPrevRest=$0;
  TotCredit=$0;
  TotDebet=$0;
  TotOtherCred=$0;
  TotOtherDeb=$0;
  TotRest=$0;
  TotPrevRestA=$0;
  TotCreditA=$0;
  TotDebetA=$0;
  TotOtherCredA=$0;
  TotOtherDebA=$0;
  TotRestA=$0;
  TotPrevRestF=$0;
  TotCreditF=$0;
  TotDebetF=$0;
  TotOtherCredF=$0;
  TotOtherDebF=$0;
  TotRestF=$0;
  TotPrevAcc=0;
  TotAcc=0;
  TotOpenedAcc=0;
  TotClosedAcc=0;
  TotPrevNOps=0;
  TotNOps=0;
  if ( Index( DeskSubj.Reports, "A" ) != 0 )
    OutTitle;
    Chapter=1;
    ReportOnCIResources;
    PrintLn;
    PrintLn;
    Chapter=2;
    ReportOnCIResources;
    PrintLn;
    PrintLn;
    ReportOnVFResources;
    PrintLn;
    PrintLn;
    OutBottom;
    PrintLn( StrFor( 12 ) );
  end;
end;

  var
    RecFound;

  if ( {EDeskOpen} )
    OperBrigade = B_EDESK;
  elif ( {DeskOpen} ) 
    OperBrigade = B_RDESK;
  end;

  if ( {RprtDSubj} == "" )
    KeyNum( DeskSubj, 1 );
    ClearRecord( DeskSubj );
    DeskSubj.Branch = Branch;
    DeskSubj.Type = DST_PANTRY;
    RecFound = GetGE( DeskSubj );
    while ( RecFound
      and ( DeskSubj.Branch == Branch )
      and ( DeskSubj.Type == DST_PANTRY ) )
      Safe = DeskSubj.Name;
      TreatCurrentSafe;
      RecFound = Next( DeskSubj );
    end;
    KeyNum( DeskSubj, 0 );
    DeskSubj.Branch = Branch;
    DeskSubj.Name = RDesk;
    if ( GetEQ( DeskSubj ) )
      Safe = DeskSubj.Name;
      TreatCurrentSafe;
    end;
  else
    DeskSubj.Branch = Branch;
    DeskSubj.Name = {RprtDSubj};
    if ( GetEQ( DeskSubj ) )
      Safe = DeskSubj.Name;
      TreatCurrentSafe;
    end;
  end;
  ExitMacro;
end;