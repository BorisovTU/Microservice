
/*
**  Реестр операций с ценностями
**
**
*/

import deprIntr, deskInter, brParm, cur_rate, revalue, rsd;

/*
** Наименование подразделения ( для шапки отчета )
*/

const 
  DEPARTMENT_NAME = "";


/* Количество строк ( без шапки и нижней части отчета ) на странице.
** 0 - не бить по страницам
*/

const
  LINES_PER_PAGE = 40;

/* Типы ресурсов */

const
  TYPERES_CURRENCY    =   1, /* Деньги (из справочника валют)           */
  TYPERES_VALFORM     =   2, /* Ценные бланки                           */
  TYPERES_VALUABLES   =   3, /* Др. ценности (из справочника ценностей) */ 
  TYPERES_PAYDOC      =   4, /* Платежные документы ОП                  */
  TYPERES_CARD        =  10, /* Пластиковые карточки                    */
  TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ             */

/* Состояния ресурсов */

const
  TYPE_RES_PAY    = 0, /* Платежеспособные ресурсы                     */
  TYPE_RES_NOPAY  = 1, /* Неплатежеспособные ресурсы                   */
  TYPE_RES_DOUBT  = 2, /* Сомнительные ресурсы                         */
  TYPE_RES_SAVE   = 3, /* На хранении                                  */
  TYPE_RES_EXP    = 4, /* На экспертизу                                */
  TYPE_RES_INCASS = 5, /* На инкассо                                   */
  TYPE_RES_PDNAT  = 20,/* ПД, оплаченные рублями(национальной валютой) */
  TYPE_RES_PDCUR  = 21,/* ПД, оплаченные валютой                       */
  TYPE_RES_PD     = 22;/* оплаченные ПД                                */

/* Единицы измерения ресурсов */

const
  RESMESUNIT_POINT = 1,  /* Поштучно   */
  RESMESUNIT_MONEY = 2,  /* Стоимость  */
  RESMESUNIT_MIXED = 3;  /* Cмешанный учет  */

/* Состояния кассовых документов */

const
  TYPEDOC_CARRYOFF = 1,  /* Отложен (не подтвержден) */
  TYPEDOC_CARRYON  = 2;  /* Проведен (подтвержден) */

/* Операции */

const
  CASHOP_REINFORCE  = 1,  /* Подкрепление кассира */
  CASHOP_RETURN     = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT    = 3,  /* Инкассация */
  CASHOP_ADVANCE    = 4,  /* Аванс */
  CASHOP_DEBIT      = 5,  /* Расход */
  CASHOP_CREDIT     = 6,  /* Приход */
  CASHOP_REMOVE     = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1   = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE    = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1  = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE    = 11, /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1  = 12, /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_DEBET_SAFE     = 13, /* Расход из сейфа */
  CASHOP_CREDIT_SAFE    = 14, /* Приход в сейф */
  CASHOP_GIVEA      = 15, /* Выдача под отчет (со стороны Оперкассы) */
  CASHOP_RETA       = 16, /* Возврат подотчетной суммы (со стороны оперкассы) */
  CASHOP_REVAL_POS  = 18, /* Переоценка (+) */
  CASHOP_REVAL_NEG  = 19, /* Переоценка (-) */
  CASHOP_PANT_REMOVE    = 26, /* Внутреннее подкрепление хранилища (Расход) */
  CASHOP_PANT_REMOVE_1  = 27, /* Внутреннее подкрепление хранилища (Приход) */
  CASHOP_PANT_CHVALUE   = 28, /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_PANT_CHVALUE_1 = 29, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_PANT_RDEB      = 30, /* Расход по кассе пересчета */
  CASHOP_PANT_RCRD      = 31, /* Приход по кассе пересчета */
  CASHOP_PANT_RDRT      = 32, /* Сдача ресурса в кладовую из Кассы пересчета */
  CASHOP_PANT_ECRD      = 41, /* Приход по вечерней кассе */
  CASHOP_PANT_EDRT      = 42, /* Сдача ресурса из вечерней кассы */
  CASHOP_PANT_GIVEA     = 43, /* Выдача под отчет (со стороны КБ) */
  CASHOP_PANT_RETA      = 44; /* Возврат подотчетной суммы (со стороны КБ) */

/* Подвиды операций Оперкассы */

const 
  CASHOP_DEBET_SHORTAGE       = 2,      /* Отражение недостачи */
  CASHOP_CREDIT_OVERPLUS      = 1,      /* Отражение излишков  */
  CASHOP_DEBET_SAFE_SHORTAGE  = 2,      /* Отражение недостачи */
  CASHOP_CREDIT_SAFE_OVERPLUS = 1;      /* Отражение излишков  */

/* Признак "приход/расход" кассового документа */

const
  FLAGDEBCRED_IN   = 1,  /* Кредит */
  FLAGDEBCRED_OUT  = 2;  /* Дебет */

const
  CP_FLAG = 2;  /* Флаг варианта группировки отчета по коммунальным платежам     */
                /* CP_FLAG = 0 - по получателям без группировки (старый вариант) */
                /* CP_FLAG = 1 - группировка по получателям                      */
                /* CP_FLAG = 2 - группировка по видам платежей                   */

/* Виды прочих кассовых операций */

const
  CO_COMMPAY     =   1, /* Коммунальные платежи */
  CO_NALPER      =  11, /* Наличный перевод */
  CO_COMMISSION  =  21, /* Комиссия */
  CO_CONVERSION  =  31, /* Конверсия */
  CO_PAYED       =  41, /* Выплата */
  CO_BANKPAYED   =  51, /* Банковские расчеты */
  CO_OVERCRED    =  61, /* Документ овердрафтного кредитования */
  CO_OTHERBRANCH =  71, /* Документ по другому филиалу */
  CO_TRN         = 100, /* Безналичные платежи */
  CO_TREASURE    = 101; /* Ценности */

/* Виды ЦБ */

const
  CI_OGSZ          = 1,
  CI_LOTTERY       = 2,
  CI_CERT          = 3,
  CI_COIN          = 4,
  CI_INGOT         = 5,
  CI_ORVVZ         = 6,
  CI_MISC          = 10;

/* Виды монет */

const
  CK_NEW          = 0, 
  CK_OLD          = 1;

const
  DST_IO          = 1,    /* Инкассация */
  DST_SAFE        = 2,    /* Сейф */
  DST_WORKDESK    = 3,    /* Рабочий стол кассира */
  DST_RDESK       = 4,    /* Касса пересчета */
  DST_PANTRY      = 5,    /* Кладовая отделения */
  DST_EDESK       = 6;    /* Вечерняя касса */

const
  EDESKNAME = "Вечер. касса";

var
  cashVal  = TBFile( "sb_casvl.dbt", "r", 0 ), 
  cashAcc  = TBFile( "sb_casac.dbt", "r", 2 ), 
  cashDoc  = TBFile( "sb_casdc.dbt", "r", 8 ), 
  cashLink = TBFile( "sb_casln.dbt", "r", 0 ), 
  pd       = TBFile( "paydoc.dbt",   "r", 0, null, "exchange.def" ), 
  deskSubj = TBFile( "desksubj.dbt", "r", 1 ), 
  subOp    = TBFile( "dsuboper.dbt", "r", 0 ), 
  pantDoc  = TBFile( "pant_doc.dbt", "r", 1 ),
  pantClnt = TBFile( "pclnt.dbt",    "r", 0 ),
  listfdep = TBFile( "listfdep.dbt", "r", 0 ),
  alg      = TBFile( "namealg.dbt",  "r", 0, null, "bank.def" ), 
  tmp;

const
  SB_DEPOSIT     =    1,     /* Документ вкладной операции */
  SB_PSDEPOS     =    5,     /* Отложенный документ */
  SB_COMPAY      =   20,     /* Коммунальные платежи */
  SB_SVDOC       =   50,     /* Сводный документ кредитного бухгалтера */
  SB_INCASH      =  100,     /* Документ порожден Кассой */
  SB_EXTCASH     =  150,     /* Внешняя кассовая операция, не отр. в др. подсист. */
  SB_RDESK       =  180,     /* Касса пересчета */
  SB_PANTRY      =  190,     /* Кладовая отделения */
  SB_CASHOPERT   =  200,     /* Документ для сервисно-кассовых операций */
  SB_EXCHANGE    =  250,     /* Реестр по обменному пункту */
  SB_EXCHNDOC    =  260,     /* Документ порожден обменным пунктом  RA 07-06-00 */
  SB_CAPISSUE    =  300,     /* Ценные бумаги */
  SB_TRN         =  400,     /* Безналичные платежи */
  SB_NOTBALANCE  = 1000;     /* Небалансовая ценность */

private var
  {curdate}, 
  {Name_Bank},
  {CNum},
  branch = NumFNCash, 
  diaryNumber, 
  mode = getCurrentMode, 
  prevRest, rest, prevRestV, prevRestVRevalued; 

var   OpenBankFiles = FALSE;
const BankVersion   = BankVer();


class TLine( _credit, _creditR, _debet, _debetR )

  var
    credit = _credit, 
    creditR = _creditR, 
    debet = _debet, 
    debetR = _debetR;

  macro add( l : TLine )

    credit = credit + l.credit;
    creditR = creditR + l.creditR;
    debet = debet + l.debet;
    debetR = debetR + l.debetR;
  end;

  macro reset

    credit = creditR = debet = debetR = $0; 
  end;

  if ( credit == null )
    credit = $0;
  end;

  if ( creditR == null )
    creditR = $0;
  end;

  if ( debet == null )
    debet = $0;
  end;

  if ( debetR == null )
    debetR = $0;
  end;
end;

class TReport

  var
    linesPrinted = false, 
    pageNumber = 0, 
    lineCounter = 0;

  macro printTop( firstPage )
  end;

  macro printBottom( lastPage )
  end;

  macro doPrintLine
  end;

  macro checkNewPage

    var
      newPg = false;

    if ( pageNumber == 0 )
      newPg = true;
    end;

    if ( LINES_PER_PAGE )
      lineCounter = lineCounter + 1;
   
      if ( lineCounter > LINES_PER_PAGE )
        newPg = true;
        lineCounter = 0;
      end;
    end;

    if ( newPg )
      pageNumber = pageNumber + 1;
      if ( pageNumber > 1 )
        printBottom( false );
      end;
      printTop( pageNumber == 1 );  
    end;
  end;

  macro printLine

    checkNewPage;
    doPrintLine;
    linesPrinted = true;
  end;

  macro proceed
  end;
end;

class ( TReport ) TSBReport 

  var
    totals = TLine, 
    pageTotals = TLine, 
    opNo = 1;

  macro printTop( firstPage )

    [ ########################################################################################################################################################### ]
    ( ( "-- " + pageNumber + " --" ):c );
    [ ];                                                                    
    [                                                  РЕЕСТР ОПЕРАЦИЙ С ЦЕННОСТЯМИ № #]
    ( diaryNumber );
    [                                                                            дата #]
    ( {curdate}:m );
    [ ];
    [ #________________________________________________ ]
    ( "_" + {Name_Bank} );
    [       Наименование филиала Сбербанка России ];
    [ ];
    [ #________________________________________________ ]
    ( "_" + DEPARTMENT_NAME );
    [            Наименование подразделения ];
    [ ];
    [--------------------------------------------------------------------------------------------------------------------------------------------------------------];
    [| Номер |                                            |                                          |           Ценности, ценные бумаги, ценные бланки           |];
    [| счета |               К р а т к о е                |             Н а з в а н и е              |------------------------------------------------------------|];
    [|  или  |                                            |                                          |         П р и х о д          |         Р а с х о д         |];
    [|докуме-|     о п и с а н и е    о п е р а ц и и     |              к л и е н т а               |------------------------------|-----------------------------|];
    [|  нта  |                                            |                                          |      ин.     |     рубли     |      ин.     |     рубли    |];
    [|       |                                            |                                          |    валюта    |  (рубл. экв)  |    валюта    |  (рубл. экв) |];
    [|-------|--------------------------------------------|------------------------------------------|--------------|---------------|--------------|--------------|];
    if ( pageNumber > 1 )
      [                                                                          Перенос со стр. ###   ############## ############### ############## ############## ]
      (
        pageNumber - 1, 
        totals.credit:z, 
        totals.creditR:z, 
        totals.debet:z, 
        totals.debetR:z
      );
    else
      [                Входящий остаток: ################### ]
      ( prevRest );
    end; 
    [-------------------------------------------------------------------------------------------------------------------------------------------------------------];
  end;

  macro printBottom( lastPage )

    [-------------------------------------------------------------------------------------------------------------------------------------------------------------];

    if ( not lastPage )
      [                                                                            Итого по стр. ###   ############## ############### ############## ############## ]
      (
        pageNumber - 1, 
        pageTotals.credit:z, 
        pageTotals.creditR:z, 
        pageTotals.debet:z, 
        pageTotals.debetR:z
      );
      pageTotals.reset;
    else
      [                                                                                 И т о г о      ############## ############### ############## ############## ]
      (
        totals.credit, 
        totals.creditR, 
        totals.debet, 
        totals.debetR
      );

      [               Исходящий остаток: ################### ]
      ( rest );                    

    end;
    [ ];
    [     Операционный работник:                                              _________________________ ];
    [                                                                                 (подпись)         ];
    [ ];
    [     СВЕРЕНО: ];
    [     Заведующий кассой:                                                  _________________________ ];
    [     (кассовый работник)                                                         (подпись)         ];
    printLn( StrFor( 12 ) );
  end;

  macro doPrintLine
   
    var
      l = TLine( tmp.rec.Credit, tmp.rec.CreditR, tmp.rec.Debet, tmp.rec.DebetR );


    [ ####### ############################################ ######################################### ############## ############### ############## ############## ]
    (
      opNo, 
      tmp.rec.Desc:w, 
      tmp.rec.Client:w, 
      l.Credit:z, 
      l.CreditR:z, 
      l.Debet:z, 
      l.DebetR:z 
    );

    opNo = opNo + 1;

    totals.add( l );
    pageTotals.add( l );
  end;

  macro proceed


    tmp.rewind; 
    while ( tmp.next )
      printLine;
    end;

    if ( linesPrinted )
      printBottom( true ); 
    elif ( ( prevRest != $0 ) or ( rest != $0 ) )
      printTop( true );
      printBottom( true ); 
    end; 
  end;

  initTReport;
end;

macro getDocumentTime( appKey )

  return time( int( subStr( appKey, 11, 2 ) ), 
               int( subStr( appKey, 13, 2 ) ), 
               int( subStr( appKey, 15, 2 ) ) );
end;

/* Получить номер опердневника */

macro getDiaryNumber

  var
    depParm = TBFile( "depparm.dbt", "r", 0 );


  depParm.rec.FNCash = branch;
  depParm.rec.FlagCur = 0;
  if ( not ( depParm.getGE
    and ( depParm.rec.FNCash == branch )
    and ( depParm.rec.FlagCur == 0 ) ) )
    msgbox( "Ошибка чтения параметров филиала" );
    exit( 1 );
  else
    return depParm.rec.NumOperDy;
  end;
end;

macro findCashVal( ref )

  cashVal.rec.RefValue = ref;
  if ( not cashVal.getEQ )
    msgBox( "Ценность с кодом " + ref + " не найдена!" );
    exit( 1 );
  end;
end;

macro valFilter

  if ( cashVal.rec.TypeValue == TYPERES_CURRENCY )
    if ( ( cashVal.rec.Type == TYPE_RES_PAY ) 
      or ( cashVal.rec.Type == TYPE_RES_NOPAY ) ) 
      return false;
    end;
  elif ( cashVal.rec.TypeValue == TYPERES_PAYDOC )
    if ( ( cashVal.rec.Type == TYPE_RES_NOPAY ) 
      or ( cashVal.rec.Type == TYPE_RES_PAY   ) 
      or ( cashVal.rec.Type == TYPE_RES_PDNAT )  
      or ( cashVal.rec.Type == TYPE_RES_PDCUR ) 
      or ( cashVal.rec.Type == TYPE_RES_PD    ) ) 
      return false;
    end;
  elif ( cashVal.rec.TypeValue == TYPERES_MINCAPISSUE + CI_COIN ) 
    return false;
  elif ( cashVal.rec.TypeValue == TYPERES_MINCAPISSUE + CI_INGOT ) 
    return false;
  end;
  return true;
end;

macro getSubOpName

  subOp.rec.Branch = branch;
  subOp.rec.OperType = cashDoc.rec.NumOper;
  subOp.rec.Type = cashDoc.rec.SubOper;
  if ( subOp.getEQ )
    return subOp.rec.Name;
  else
    return "";
  end;
end;

macro getAlgName( type, number )

  alg.rec.iTypeAlg = type;
  alg.rec.iNumberAlg = number;
  if ( alg.getEQ )
    return alg.rec.szNameAlg;
  end;
  return "";
end;

macro getValNativeCurr

  var                
    sk,
    nativeCurr;

  if ( cashVal.rec.TypeValue == TYPERES_CURRENCY )
    nativeCurr = cashVal.rec.CodIntValue;
  else
    if ( cashVal.rec.GroupValue == 0 )
      nativeCurr = 0;
    else       
      cashVal.getPos;
      sk = cashVal.keyNum;
      findCashVal( cashVal.rec.GroupValue );
      if ( cashVal.rec.TypeValue == TYPERES_CURRENCY )
        nativeCurr = cashVal.rec.CodIntValue;
      elif( cashVal.rec.TypeValue == TYPERES_PAYDOC )
        pd.rec.ID = cashVal.rec.CodIntValue;
        if( pd.getEQ )
          nativeCurr = pd.rec.Curr_Ref; 
        else
          msgBox( "Не найден платежный документ с кодом " + CashVal.CodIntValue );
          exit( 1 );
        end;
      else
        nativeCurr = 0;
      end;       
      cashVal.keyNum = sk;
      cashVal.getDirect;
    end;                  
  end;

  return nativeCurr;
end;

macro cur2rub( sum, rate )

  return Sumconv( sum, rate );
end;

macro addRests( isPrev )

  var
    currCode = getValNativeCurr, 
    cbRate, prevCbRate, 
    r, rNoRev;
                                   
  if ( cashAcc.rec.RestMoney != $0 )
    r = cashAcc.rec.RestMoney;
    if ( currCode ) 
      GetAllCurrRate( {curdate}, currCode, cbRate );
      r = cur2rub( r, cbRate );  
      if ( isPrev )
        GetAllCurrRate( {curdate} - 1, currCode, prevCbRate );
        rNoRev = cur2rub( cashAcc.rec.RestMoney, prevCbRate );  
      end;
    else
      rNoRev = r;
    end; 
  else
    r = rNoRev = $1 * cashAcc.rec.RestCol;
  end;

  if ( isPrev )
    prevRestVRevalued = prevRestVRevalued + r;
    prevRest = prevRest + rNoRev;
    prevRestV = prevRestV + rNoRev;
  else
    rest = rest + r;
  end;
end;

macro getRestsForSubj

  cashAcc.clear;
  cashAcc.rec.Branch = branch;
  cashAcc.rec.CashOper = deskSubj.rec.Name;
  cashAcc.rec.SubAccount = "";
  cashAcc.rec.RefValue = cashVal.rec.refValue;
  cashAcc.rec.Date = {curdate};
  if ( getEQ( cashAcc ) )
    addRests( false );
  end;

  if ( getLT( cashAcc) 
    and ( cashAcc.rec.Branch == branch )
    and ( cashAcc.rec.CashOper == deskSubj.rec.Name )
    and ( cashAcc.rec.SubAccount == "" )
    and ( cashAcc.rec.RefValue == cashVal.rec.refValue ) )
    addRests( true );
  end;
end;

macro insertRevalue

  var
    diff = getRevalueSum( cashVal.rec.RefValue, {curdate} );

  if ( diff == $0 )
    return;
  end;

  tmp.clear;
  if ( diff > $0 )
    tmp.rec.Desc = "Зачислено курсовая разница";
    tmp.rec.CreditR = diff; 
  else
    tmp.rec.Desc = "Списано курсовая разница";
    tmp.rec.DebetR = -diff; 
  end;
  tmp.rec.Desc = tmp.rec.Desc + " - " + cashVal.rec.NameValue;

  if ( cashVal.rec.Type != TYPE_RES_PAY )
    tmp.rec.Desc = tmp.rec.Desc + " (" + getAlgName( 811, cashVal.rec.Type ) + ")";
  end;

  tmp.insert;
end;

macro getRestsForVal

  var
    recFound;

  prevRestV = prevRestVRevalued = $0;

  deskSubj.clear;
  deskSubj.rec.Branch = branch;
  deskSubj.rec.Type = DST_PANTRY;
  deskSubj.addFilter ("exists (select * from dsb_casac_dbt a " +
                    "where a.t_branch = t.t_branch " +
                    "  and a.t_cashoper = t.t_name " +
                    "  and a.t_subaccount = chr(1)" +
                    "  and a.t_refvalue = " + string( CashVal.rec.refValue ) +
                    "  and a.t_date <= " + sqlDateToStr( {curdate} ) + " )" );
  recFound = deskSubj.getGE;
  while ( recFound 
    and ( deskSubj.rec.Branch == branch )
    and ( deskSubj.rec.Type == DST_PANTRY ) )
    getRestsForSubj;
    recFound = deskSubj.next;
  end;
  deskSubj.dropFilter;

  insertRevalue;
end;

macro getRests

  prevRest = rest = $0;  

  cashVal.rewind;
  while ( cashVal.next )
    if ( valFilter )
      getRestsForVal;
    end;
  end;
end;

macro checkDocument

  if ( not valFilter ) 
    return false;
  end;

  if ( cashDoc.rec.Mode != mode )
    return false;
  end;

  if ( ( cashDoc.rec.NumOper == CASHOP_PANT_RDRT ) 
    or ( cashDoc.rec.NumOper == CASHOP_PANT_EDRT ) )
    return false;
  end;   

  if ( ( cashDoc.rec.NumOper == CASHOP_PANT_ECRD ) and ( cashDoc.rec.CashPatern != EDESKNAME ) )
    return false;
  end;

  return true;
end;

macro isDocDebet

  if ( cashDoc.rec.FlagDebCredOper == FLAGDEBCRED_IN )
    return true;        /* Все наоборот */
  else
    return false;
  end;                         
end;

private macro get_DocDesc()


  var
    valName, 
    s;  

  if ( ( cashDoc.rec.NumOper == CASHOP_PANT_RCRD ) 
    or ( cashDoc.rec.NumOper == CASHOP_PANT_RDEB ) )
    s = getSubOpName;
  else 
    s = getAlgName( 803, cashDoc.rec.numOper ) + " (" + getSubOpName + ")";
  end;
  s = s + ": ";
  s = s + cashVal.rec.NameValue; 
    
  if ( cashVal.rec.Type != TYPE_RES_PAY )
    s = s + " (" + getAlgName( 811, cashVal.rec.Type ) + ")";
  end;

  if ( cashDoc.rec.Ground != "" )
    s = s + " " + cashDoc.rec.Ground;
  end;

  return s;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetBagDoc1 ( cashApplKind, cashApplKey, bagAppKind, bagAppKey )

  var rec_found = false;

  bagAppKind = 0;
  bagAppKey  = "";

  cashLink.KeyNum = 1;
  cashLink.Clear;
  cashLink.rec.ApplicationKind2 = cashApplKind;
  cashLink.rec.ApplicationKey2  = cashApplKey;
  cashLink.rec.RefValue2        = 0;

  if ( ( cashLink.GetGE                               ) and
       ( cashLink.rec.ApplicationKind2 == cashApplKind) and
       ( cashLink.rec.ApplicationKey2  == cashApplKey )    )
    bagAppKind = cashLink.rec.ApplicationKind1;
    bagAppKey  = cashLink.rec.ApplicationKey1;
    rec_found = true;
  end;

  SetParm( 2, bagAppKind); 
  SetParm( 3, bagAppKey); 

  return rec_found;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetPantryDoc1 ( bagApplKind, bagApplKey )

  record Tmp ( "sb_casln.dbt" );

  var rec_found;

  cashLink.KeyNum = 0;
  cashLink.Clear;
  cashLink.rec.ApplicationKind1 = bagApplKind;
  cashLink.rec.ApplicationKey1  = bagApplKey;
  cashLink.rec.NumLinkDoc       = 0;
  rec_found = cashLink.GetGE;
  while ( ( rec_found                                    ) and
          ( cashLink.rec.ApplicationKind1 == bagApplKind ) and
          ( cashLink.rec.ApplicationKey1  == bagApplKey  )    )
    if ( cashLink.rec.ApplicationKind2 == 190 )
      pantDoc.rec.ApplicationKind = cashLink.rec.ApplicationKind2;
      pantDoc.rec.ApplicationKey  = cashLink.rec.ApplicationKey2;
      if ( pantDoc.GetEQ )
        return true;
      end;
    end;
    rec_found = cashLink.Next;
  end;

  return false;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro getNameClient
                     
  var stat    = false;
  var ret_val = "";
  var BagAppKind;
  var BagAppKey;

  stat = GetBagDoc1( cashDoc.rec.ApplicationKind, 
                     cashDoc.rec.ApplicationKey, 
                     BagAppKind, 
                     BagAppKey                   );

  if ( stat )
    stat = GetPantryDoc1( BagAppKind, BagAppKey );
  end;

  if ( stat )
    pantClnt.Clear;
    pantClnt.rec.Branch = pantDoc.rec.Branch;
    pantClnt.rec.ClientId = pantDoc.rec.ClientID;
    stat = pantClnt.GetEQ;
  end;

  if ( stat )
    GetClientInfo( pantClnt.rec.Type, pantClnt.rec.ClientId, ret_val );
  end;

  return ret_val;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro processDocument

  var
    currCode = getValNativeCurr, 
    cbRate, 
    sum = $0, sumR = $0;

  if ( cashDoc.rec.ValueMoney != $0 )
    if ( currCode )
      sum = cashDoc.rec.ValueMoney;
      GetAllCurrRate( {curdate}, currCode, cbRate );
      sumR = cur2rub( sum, cbRate );  
    else
      sumR = cashDoc.rec.ValueMoney;
    end;
  else
    sumR = $1 * cashDoc.rec.ValueCol;
  end; 

  if ( ( sum == $0 ) and ( sumR == $0 ) )
    return;
  end;

  tmp.clear;

  tmp.rec.Time   = getDocumentTime( cashDoc.rec.ApplicationKind );
  tmp.rec.Desc   = get_DocDesc();
  tmp.rec.Client = getNameClient();
  if ( isDocDebet )
    tmp.rec.Debet = sum;
    tmp.rec.DebetR = sumR;
  else
    tmp.rec.Credit = sum;
    tmp.rec.CreditR = sumR;
  end;

  return tmp.insert;
end;

macro processAppKind( appKind )

  var
    recFound;

  cashDoc.clear;
  cashDoc.rec.Branch = branch;
  cashDoc.rec.CashDateDoc = {curdate};
  cashDoc.rec.TypeDoc = TYPEDOC_CARRYON;
  cashDoc.rec.ApplicationKind = appKind;
  cashDoc.addFilter ("t.t_branch = "+string(branch)+
                   "  and t.t_cashdatedoc = " + sqlDateToStr( {curdate} ) +
                   "  and t.t_typedoc = " + string(TYPEDOC_CARRYON) +
                   "  and t.t_applicationkind = " + string(appKind));
  recFound = cashDoc.getGE;
  while ( recFound 
    and ( cashDoc.rec.Branch == branch )
    and ( cashDoc.rec.CashDateDoc == {curdate} )
    and ( cashDoc.rec.TypeDoc == TYPEDOC_CARRYON )
    and ( cashDoc.rec.ApplicationKind == appKind ) )
    findCashVal( cashDoc.rec.RefValue );
    if ( checkDocument )       
      processDocument;
    end;
    recFound = cashDoc.next;
  end;
  cashDoc.dropFilter;
end;

macro fillTmpFile

  RsdCommand( "truncate table drd_valr_tmp" ).execute;

  insertRevalue;
  processAppKind( SB_EXTCASH );
  processAppKind( SB_RDESK );
end;

macro prepareData;

  getRests;
  fillTmpFile;
end;

  var
    rep = TSBReport;
     
  tmp = TBFile( "rd_valr.tmp", "w", -1 );

  diaryNumber = getDiaryNumber;
  prepareData;
  rep.proceed;
end; 