/*
$Name:             inoutnal.mac
$Module:           RS-Retail.  Касса.
$Description:      Отчетная справка о суммах принятой и выданной денежной наличности.
*/

import DeprIntr, DeskInter, BankInter, CommonInter, ExchangeInter, RSD;

private file CI_Misc  ( ci_misc                ) key 0;
private file paydoc   ( paydoc, "exchange.def" ) key 0;
private file opr      ( person, "bank.def"     ) key 0;
private file fCurr    ( currency               ) key 0;
private file Curr     ( "currency.dbt"         ) key 0;

private var person  = TBFile( "person.dbt",  "r", 0 );
private var officer = TBFile( "officer.dbt", "r", 0 );

private var CashDoc = TBFile( "sb_casdc.dbt", "r", 1 );

const Все_валюты_вместе = true; /* True - все валюты печатаются в одной справке, 
                                    False -для каждой валюты своя справка */
/*Флаг печати кода валюты (0 - в буквеном виде(RUR), 1 - буквенный/цифровой(RUR/810), 2 - цифровой(810) ), 3 - Название валюты*/
const flagCurrCode = 3;

const RUB = 0;
const VAL = 1;
const SB_INCASH = 100;
const FLAGDEBCRED_IN   = 1;
const FLAGDEBCRED_OUT  = 2;

private const SB_EXCHNDOC = 260;

file Fsb_casdc (sb_casdc);
file Fsb_casvl (sb_casvl);

private var vBankStandart = GetBankStandart();  // Стандарт работы Банка.

private var DateReport, Branch, OperBrigade, Safe_mode,  AutoDesk,
    {oper}, {OurBank}, {NAME_Bank}, {FIO_oper}, {curdate};

private const
  CK_NEW          = 0, 
  CK_OLD          = 1;


private array IN_SUM,                    /* входящие остатки */
      OUT_SUM,     /*OUT_CURCODE*/,  /* остатки на конец дня */
      IN_DAY_SUM,  IN_DAY_NUMB,  /* приход */
      OUT_DAY_SUM, OUT_DAY_NUMB, /* расход */
      ALL_CURCODE;               /* коды валют, прошедших через кассира за день (входящие+приход) */

private array
  SpecCurrCode, SpecSumIn, SpecCountIn, SpecSumOut, SpecCountOut;

// Массивы по операционистам - валюты.
private array O_CUR,     // Коды валют.
              O_OPER,    // Номера операционистов.
              O_INSUM,   // Приход: сумма.
              O_INNUM,   // Приход: количество документов.
              O_OUTSUM,  // Расход: сумма.
              O_OUTNUM;  // Расход: количество документов.

// Массивы по операционистам - монеты и чеки.
private array OS_CUR,     // Коды валют.
              OS_OPER,    // Номера операционистов.
              OS_INSUM,   // Приход: сумма.
              OS_INNUM,   // Приход: количество документов.
              OS_OUTSUM,  // Расход: сумма.
              OS_OUTNUM;  // Расход: количество документов.

private macro GetDateReport()

  var stat = False;
                            
  DateReport = {curdate};
  stat = GetDate( DateReport, " Введите дату отчета ... " );

  if( stat )
   if(DateReport > {curdate})
      MsgBox( " Введенная дата больше текущего операционного дня !!! ");
      stat = False;
   end;
  end;

  return stat;
end;

/* Возвращает запись по ресурсу */
private macro Get_resource_struct (RefVal)
   rewind (Fsb_casvl);
   clearrecord (Fsb_casvl);
   Fsb_casvl.RefValue=RefVal;
   if (not GetEQ(Fsb_casvl))
      msgbox ("Ресурс с кодом ",RefVal," в справочнике ресурсов не найден");
      return false;
   else
      return true;
   end;
end;

private macro GetOperName( Oper )

  file pers ( person, "sbbank.def" );
  var Name = "", IOtmp;

  file Group("rtgroup.dbt") key 2;

  pers.Oper = Oper;
  if( geteq( pers ) )
     Name = SubStr( pers.Name, 1, Index(pers.Name, " ") - 1 );
     IOtmp= Trim( Substr( pers.Name, Index(pers.Name, " ") ) );
     Name = Name + " " + strupr( Substr(IOtmp, 1, 1)) + ".";
     IOtmp = Trim( Substr( IOtmp, Index(IOtmp," ") ) );
     Name = Name + strupr( Substr( IOtmp, 1, 1 ) ) + ".";
  end;

  return Name;
end;/*GetOperName*/

private macro GetOperPost( _oper )
  var _Name = "Не определен";
  person.Clear();
  person.rec.Oper = _oper;
  if ( person.GetEQ() )
    officer.Clear();
    officer.rec.PartyID  = {OurBank};
    officer.rec.PersonID = person.rec.PartyID;
    if ( officer.GetEQ() )
      _Name = officer.rec.Post;
    end;
  end;
  return _Name;
end;  /*GetOperPost*/

private macro GetNativeSafe

  file Group("rtgroup.dbt") key 2;

  Group.Branch = Branch;
  Group.Date = DateReport;
  Group.ID = OperBrigade;
  if(GetEQ(Group))
    return Group.SafeID;
  else
    msgbox ("Ошибка. Операционист не входит ни в одну смену.");
    exit (1);
  end;
end;


/* Анализируем настройки RS-Retailа */
private macro analize_ret ()

  var SafeID, tmpOper = {oper};
  /* Подразделение */
  Branch=NumFNCash;
  /* Смена */
  OperBrigade=GetOperBrigade;
  /* Определяем режим работы смены (сейф / под отчет) */
  SafeID=GetNativeSafe;
  if (SafeID!="")
     Safe_mode=True;  /* режим работы смены - с сейфом */
  else
     Safe_mode=False; /* режим работы смены - под отчет */
  end;
  /* Если кассир работает с сейфом, то определяем настройки работы кассы */
  if (Safe_mode)
    GetRegistryValue("RS-RETAIL\\СЕРВИСНЫЕ\\КАССА\\АВТОМАТИЗАЦИЯ_СТОЛА_КАССИРА", V_INTEGER, AutoDesk, null, null, {oper});
  end;
end;

private macro GetCurrName( Code_Currency )

var CurrName = "";

  KeyNum     (Curr, 0);
  ClearRecord(Curr   );
  Curr.Code_Currency = Code_Currency;
  if ( not GetEQ( Curr ) )
    MsgBox( "Не найдена валюта #" + Code_Currency );
    Exit( 1 );
  end;

  if (flagCurrCode == 0)
    CurrName = Curr.Short_Name;
  elif (flagCurrCode == 1)
    CurrName = Curr.Short_Name+"/"+Curr.ExternalCode;
  elif (flagCurrCode == 2)
    CurrName = Curr.ExternalCode;
  elif (flagCurrCode == 3)
    CurrName = Curr.Name_Currency;
  end;

  if ( Code_Currency == 0 )
    CurrName = "Российский рубль";
  end;

  return CurrName;
end;

//
// Сумма + наименование валюты
//
private macro sum_curr( _sum, _curr )
  var sSum = String( _sum );
  var sRet = "";
  if ( ( _sum != $0 )  AND  ( StrLen( sSum ) > 0 ) )
    KeyNum     (Curr, 0);
    ClearRecord(Curr   );
    Curr.Code_Currency = _curr;
    if ( not GetEQ( Curr ) )
      MsgBox( "Не найдена валюта #" + _curr );
      Exit( 1 );
    end;
    sRet = String( _sum ) + " " + Curr.Short_Name;
  end;
  return sRet;
end;

/* Получение следующего слова в строке */
macro GetNextWord( Str, Pos, NextWord )
    var len = strlen(Str), symb;
    var ValidSymbols = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyzАБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯабвгдеёжзийклмнопрстуфхцчшщъыьэюя";
    var stat: bool;

    NextWord = "";
    stat = true;
    while( (Pos <= len) and (stat) )
        symb = substr(Str, Pos, 1);
        if( index(ValidSymbols, symb) > 0 )
            NextWord = NextWord + symb;
        else
            if( strlen(NextWord) > 0 )
                stat = false;
            end;
        end;
        Pos = Pos + 1;
    end;
         
    SetParm(1,Pos);
    SetParm(2,NextWord);
end;


/* Определение Фамилии И.О. операциониста */
private macro StrOper( _oper )

  var FIO = "", Family, Name, Patron;
  var pos = 1;

  opr.Oper = _oper;
  if( GetEQ( opr ) )

    FIO = opr.Name;
    GetNextWord(FIO, pos, Family);
    GetNextWord(FIO, pos, Name);
    GetNextWord(FIO, pos, Patron);

    FIO = Family;
    if( strlen(Name) > 0 )
      FIO = FIO + " " + substr(Name, 1, 1) + ".";
    end;
    if( strlen(Patron) > 0 )
      FIO = FIO + " " + substr(Patron, 1, 1) + ".";
    end;

  end;

  return FIO + "               ";
end;

// =========================================================================================================================
//    ВОО: количество документов в связке кассового - для определения: наличный (2 документа) - безналичный (1 документ).
// =========================================================================================================================
private macro AmountExchangeDocs( _ApplKey )
  var cmdD, rsD;
  var ret = -1;
  cmdD = RsdCommand( "select count(*) as amn " +
                     "from dsb_casln_dbt ll " +
                     "where LL.T_APPLICATIONKIND2=260 and " +
                           "LL.T_REFVALUE2<>0 and " +
                           "LL.T_APPLICATIONKEY1=(select L.T_APPLICATIONKEY1 from dsb_casln_dbt l where L.T_APPLICATIONKEY2=?)" );
  cmdD.addParam( "key",  RSDBP_IN );
  cmdD.value( "key" )  = _ApplKey;
  cmdD.execute;
  rsD = RsdRecordset( cmdD );
  if ( rsD.moveNext )
    ret = Int( rsD.value( "amn" ) );
  end;
  return ret;
end;

// ---------------------------------------------------------
//    Массивы по операционистам: валюты - добавление сумм.
// ---------------------------------------------------------
private macro O_Add( pCur, pOper, pInSum, pOutSum )
  var ind = ASize( O_OPER );
  var i = 0, j = 0;
  var vAmount = 1;

  if ( vBankStandart == 0 ) 
    return;  // Для Сбербанка разбиение по операционистам не нужно.
  end;

  // Поиск валюты-операциониста в массивах.
  if ( ind > 0 )
    while ( i < ind )
      if ( ( O_CUR( i ) == pCur )  AND  ( O_OPER( i ) == pOper ) )
        j = i;
        i = ind;
      else
        i = i + 1;
        j = i;
      end;
    end;
  end;

  // Добавление сумм в массивы.
  O_CUR   ( j ) = pCur;
  O_OPER  ( j ) = pOper;

  if ( j == ind )  // Добавление новых элементов массивов.
    O_INSUM ( j ) = pInSum;
    if ( pInSum == $0 )
      O_INNUM ( j ) = 0;
    else
      O_INNUM ( j ) = 1;
    end;
    O_OUTSUM( j ) = pOutSum;
    if ( pOutSum == $0 )
      O_OUTNUM( j ) = 0;
    else
      if ( CashDoc.rec.ApplicationKind == SB_EXCHNDOC )
        if ( AmountExchangeDocs( CashDoc.rec.ApplicationKey ) > 1 )
          vAmount = 0;
        else
          vAmount = 1;
        end;
      else
        vAmount = 1;
      end;
      O_OUTNUM( j ) = vAmount;
    end;
  else             // Дополнение элементов массивов.
    O_INSUM ( j ) = O_INSUM ( j ) + pInSum;
    if ( pInSum != $0 )
      O_INNUM ( j ) = O_INNUM ( j ) + 1;
    end;
    O_OUTSUM( j ) = O_OUTSUM( j ) + pOutSum;
    if ( pOutSum != $0 )
      if ( CashDoc.rec.ApplicationKind == SB_EXCHNDOC )
        if ( AmountExchangeDocs( CashDoc.rec.ApplicationKey ) > 1 )
          vAmount = 0;
        else
          vAmount = 1;
        end;
      else
        vAmount = 1;
      end;
      O_OUTNUM( j ) = O_OUTNUM( j ) + vAmount;
    end;
  end;
end;

// ---------------------------------------------------------
//    Массивы по операционистам: валюты - вывод строк Справки.
// ---------------------------------------------------------
private macro O_Rep( pCur )
  var ind = ASize( O_OPER );
  var i = 0, j = 0;

  if ( vBankStandart == 0 ) 
    return;  // Для Сбербанка разбиение по операционистам не нужно.
  end;

  while ( i < ind )
    if ( O_CUR( i ) == pCur )
      [ +------------+------------+-----------------------+------------+-----------------------+----------------+];
      [ |############|############|#######################|############|#######################|################|]
      ( GetCurrName( pCur ):w:c, 
        O_INNUM ( i ):c, 
        sum_curr( O_INSUM ( i ), pCur ):r,
        O_OUTNUM( i ):c, 
        sum_curr( O_OUTSUM( i ), pCur ):r,
        StrOper( O_OPER( i ) ):w );
    end;
    i = i + 1;
  end;
end;

// ---------------------------------------------------------
//    Массивы по операционистам: валюты - добавление сумм.
// ---------------------------------------------------------
private macro OS_Add( pCur, pOper, pInSum, pOutSum )
  var ind = ASize( OS_OPER );
  var i = 0, j = 0;

  if ( vBankStandart == 0 ) 
    return;  // Для Сбербанка разбиение по операционистам не нужно.
  end;

  // Поиск валюты-операциониста в массивах.
  if ( ind > 0 )
    while ( i < ind )
      if ( ( OS_CUR( i ) == pCur )  AND  ( OS_OPER( i ) == pOper ) )
        j = i;
        i = ind;
      else
        i = i + 1;
        j = i;
      end;
    end;
  end;

  // Добавление сумм в массивы.
  OS_CUR   ( j ) = pCur;
  OS_OPER  ( j ) = pOper;

  if ( j == ind )  // Добавление новых элементов массивов.
    OS_INSUM ( j ) = pInSum;
    if ( pInSum == $0 )
      OS_INNUM ( j ) = 0;
    else
      OS_INNUM ( j ) = 1;
    end;
    OS_OUTSUM( j ) = pOutSum;
    if ( pOutSum == $0 )
      OS_OUTNUM( j ) = 0;
    else
      OS_OUTNUM( j ) = 1;
    end;
  else             // Дополнение элементов массивов.
    OS_INSUM ( j ) = OS_INSUM ( j ) + pInSum;
    if ( pInSum != $0 )
      OS_INNUM ( j ) = OS_INNUM ( j ) + 1;
    end;
    OS_OUTSUM( j ) = OS_OUTSUM( j ) + pOutSum;
    if ( pOutSum != $0 )
      OS_OUTNUM( j ) = OS_OUTNUM( j ) + 1;
    end;
  end;
end;

// ---------------------------------------------------------
//    Массивы по операционистам: валюты - вывод строк Справки.
// ---------------------------------------------------------
private macro OS_Rep( pCur )
  var ind = ASize( OS_OPER );
  var i = 0, j = 0;

  if ( vBankStandart == 0 ) 
    return;  // Для Сбербанка разбиение по операционистам не нужно.
  end;

  while ( i < ind )
    if ( OS_CUR( i ) == pCur )
      [ +------------+------------+-----------------------+------------+-----------------------+----------------+];
      [ |############|############|#######################|############|#######################|################|]
      ( ( GetCurrName( pCur ) + " (ПД)" ):w:c, 
        OS_INNUM ( i ):c, 
        sum_curr( OS_INSUM ( i ), pCur ):r,
        OS_OUTNUM( i ):c, 
        sum_curr( OS_OUTSUM( i ), pCur ):r,
        StrOper( OS_OPER( i ) ):w );
    end;
    i = i + 1;
  end;
end;

/******************************************************************************/
private macro AddSpec(CurrCode,IsDebet,ValueMoney)
  var
    i = 0;

  while ( ( i < ASize( SpecCurrCode ) ) and ( SpecCurrCode( i ) != CurrCode ) )
    i = i + 1;
  end; 

  if ( i == ASize( SpecCurrCode ) )
    SpecCurrCode( i ) = CurrCode; 
    if( IsDebet )
      SpecSumIn( i )  = ValueMoney;
      SpecCountIn( i ) = 1;
      SpecSumOut( i ) = 0;
      SpecCountOut( i ) = 0;
      OS_Add( CurrCode, Fsb_casdc.Oper, ValueMoney, $0 );
    else
      SpecSumIn( i )  = 0;
      SpecCountIn( i ) = 0;
      SpecSumOut( i ) = ValueMoney;
      SpecCountOut( i ) = 1;
      OS_Add( CurrCode, Fsb_casdc.Oper, $0, ValueMoney );
    end;
  else
    if( IsDebet )
      SpecSumIn( i )  = SpecSumIn( i ) + ValueMoney;
      SpecCountIn( i ) = SpecCountIn( i ) + 1;
      OS_Add( CurrCode, Fsb_casdc.Oper, ValueMoney, $0 );
    else
      SpecSumOut( i ) = SpecSumOut( i ) + ValueMoney;
      SpecCountOut( i ) = SpecCountOut( i ) + 1;
      OS_Add( CurrCode, Fsb_casdc.Oper, $0, ValueMoney );
    end;
  end;

end;

private macro FindSpec(CurrCode)
  var
    i = 0;

  while ( ( i < ASize( SpecCurrCode ) ) and ( SpecCurrCode( i ) != CurrCode ) )
    i = i + 1;
  end; 

  if ( i == ASize( SpecCurrCode ) )
    i = -1;
  end;

  return i;  
end;

private macro MaxSpecSize()
  var i = 0, retval = asize(SpecCurrCode);

  while( i < asize(SpecCurrCode) )
    if( SpecCurrCode( i ) > retval )
      retval = SpecCurrCode( i );
    end;
    i = i + 1;
  end;

  return retval;
end;

// ==========================================================
private macro Формируем_Движение_Монет_и_Чеков_Doc()
  var countMonIn = 1, countMonOut = 1;

  if ( Get_resource_struct( Fsb_casdc.RefValue )  AND 
     ( Fsb_casvl.Type == 0 ) )  // Только платежеспособные.

    if ( Fsb_casvl.TypeValue == 504 )
      CI_Misc.Type    = 4;
      CI_Misc.IssueID = Fsb_casvl.CodIntValue;
      if ( ( GetEQ(CI_Misc) )  AND  ( CI_Misc.KindOfCoin == CK_NEW ) )
        if ( Fsb_casdc.FlagDebCredOper == FLAGDEBCRED_IN )
          if ( valtype(IN_DAY_SUM(0)) == v_undef )
            IN_DAY_SUM(0)  = $0;
            ALL_CURCODE(0) =  0;
            IN_DAY_NUMB(0) =  0;
          end;
          IN_DAY_SUM(0)  = IN_DAY_SUM(0)+Fsb_casdc.ValueMoney;
          IN_DAY_NUMB(0) = IN_DAY_NUMB(0)+countMonIn;
          OS_Add( 0, Fsb_casdc.Oper, Fsb_casdc.ValueMoney, $0 );
          countMonIn = 0;
        else
          if ( valtype(OUT_DAY_SUM(0)) == v_undef )
            OUT_DAY_SUM(0) = $0;
            ALL_CURCODE(0)=0;
            OUT_DAY_NUMB(0)=0;
          end;
          OUT_DAY_SUM(0)=OUT_DAY_SUM(0)+Fsb_casdc.ValueMoney;
          OUT_DAY_NUMB(0)=OUT_DAY_NUMB(0)+countMonOut;
          OS_Add( 0, Fsb_casdc.Oper, $0, Fsb_casdc.ValueMoney );
          countMonOut = 0;
        end; 
      end;
    end;

    /*
    if ( Fsb_casvl.TypeValue == 4 )
      paydoc.ID = Fsb_casvl.CodIntValue;
      if ( GetEQ(paydoc) )
        if ( Fsb_casdc.FlagDebCredOper == FLAGDEBCRED_IN )
          AddSpec(paydoc.Curr_ref, 1, Fsb_casdc.ValueMoney);
        else
          AddSpec(paydoc.Curr_ref, 0, Fsb_casdc.ValueMoney);
        end;
      end;
    end;
    */

  end;
end;

// ==========================================================
private macro Формируем_Движение_Монет_и_Чеков()
var cashpatern, operbrigade_str, stat;

  /* Определяем субъект кассы */
  operbrigade_str = string( operbrigade );
  if ( operbrigade < 10 )
    operbrigade_str = "0" + operbrigade_str;
  end;
  cashpatern = mkstr( "0", 4 - strlen( string( {oper} ) ) ) + 
               string( {oper} ) + "/" + operbrigade_str;

  keynum( Fsb_casdc, 0 );
  rewind( Fsb_casdc );
  clearrecord( Fsb_casdc );
  Fsb_casdc.Branch      = Branch;
  Fsb_casdc.CashDateDoc = DateReport;
  Fsb_casdc.TypeDoc     = 2; /* подтвержденные */
  Fsb_casdc.CashOper    = cashpatern;

  stat = GetGE( Fsb_casdc );
  while ( stat  and
          ( Fsb_casdc.Branch == Branch )  and
          ( Fsb_casdc.CashDateDoc == DateReport )  and
          ( Fsb_casdc.TypeDoc == 2 )  and
          ( Fsb_casdc.CashOper == cashpatern ) )

    Формируем_Движение_Монет_и_Чеков_Doc();

    stat = next( Fsb_casdc );
  end;
end;

// ==============================================================
private macro Формируем_Движение_Ресурсов_ВКассе_Doc( mode )
  var vAmount = 0;
  if ( CashDoc.rec.ApplicationKind != SB_INCASH )
    if ( mode == RUB )
      if ( CashDoc.rec.RefValue == 1 ) /* рубли */
        if ( CashDoc.rec.FlagDebCredOper == FLAGDEBCRED_IN )
          if ( valtype( IN_DAY_SUM(0) ) == v_undef )
            IN_DAY_SUM(0)  = $0;
            ALL_CURCODE(0) =  0;
            IN_DAY_NUMB(0) =  0;
          end;
          IN_DAY_SUM(0)  = IN_DAY_SUM(0)  + CashDoc.rec.ValueMoney;
          IN_DAY_NUMB(0) = IN_DAY_NUMB(0) + 1;
          O_Add( 0, CashDoc.rec.Oper, CashDoc.rec.ValueMoney, $0 );
        else
          if ( valtype( OUT_DAY_SUM(0) ) == v_undef )
            OUT_DAY_SUM(0)  = $0;
            ALL_CURCODE(0)  =  0;
            OUT_DAY_NUMB(0) =  0;
          end;               
          if ( CashDoc.rec.ApplicationKind == SB_EXCHNDOC )
            if ( AmountExchangeDocs( CashDoc.rec.ApplicationKey ) > 1 )
              vAmount = 0;
            else
              vAmount = 1;
            end;
          else
            vAmount = 1;
          end;
          OUT_DAY_SUM(0)  = OUT_DAY_SUM(0)  + CashDoc.rec.ValueMoney;
          OUT_DAY_NUMB(0) = OUT_DAY_NUMB(0) + vAmount;
          O_Add( 0, CashDoc.rec.Oper, $0, CashDoc.rec.ValueMoney );
        end;
      end;
    elif ( mode == VAL )
      /* Определяем, что это денежный ресурс */
      if ( Get_resource_struct( CashDoc.rec.RefValue )  AND
           ( Fsb_casvl.Type == 0 )  AND  // Только платежеспособные.
           ( Fsb_casvl.TypeValue == 1 )  AND
           ( Fsb_casvl.CodIntValue != 0 ) )
         if ( CashDoc.rec.FlagDebCredOper == FLAGDEBCRED_IN )
           if ( valtype( IN_DAY_SUM( Fsb_casvl.CodIntValue ) ) == v_undef )
             IN_DAY_SUM( Fsb_casvl.CodIntValue )  = $0;
             ALL_CURCODE( Fsb_casvl.CodIntValue ) = Fsb_casvl.CodIntValue;
             IN_DAY_NUMB( Fsb_casvl.CodIntValue ) = 0;
           end;               
           IN_DAY_SUM( Fsb_casvl.CodIntValue )  = IN_DAY_SUM( Fsb_casvl.CodIntValue ) + CashDoc.rec.ValueMoney;
           IN_DAY_NUMB( Fsb_casvl.CodIntValue ) = IN_DAY_NUMB(Fsb_casvl.CodIntValue) + 1;
           O_Add( Fsb_casvl.CodIntValue, CashDoc.rec.Oper, CashDoc.rec.ValueMoney, $0 );
         else
           if ( valtype( OUT_DAY_SUM( Fsb_casvl.CodIntValue ) ) == v_undef )
             OUT_DAY_SUM( Fsb_casvl.CodIntValue )  = $0;
             ALL_CURCODE( Fsb_casvl.CodIntValue )  = Fsb_casvl.CodIntValue;
             OUT_DAY_NUMB( Fsb_casvl.CodIntValue ) = 0;
           end;
           if ( CashDoc.rec.ApplicationKind == SB_EXCHNDOC )
             if ( AmountExchangeDocs( CashDoc.rec.ApplicationKey ) > 1 )
               vAmount = 0;
             else
               vAmount = 1;
             end;
           else
             vAmount = 1;
           end;
           OUT_DAY_SUM( Fsb_casvl.CodIntValue )  = OUT_DAY_SUM( Fsb_casvl.CodIntValue ) + CashDoc.rec.ValueMoney;
           OUT_DAY_NUMB( Fsb_casvl.CodIntValue ) = OUT_DAY_NUMB( Fsb_casvl.CodIntValue ) + vAmount;
           O_Add( Fsb_casvl.CodIntValue, CashDoc.rec.Oper, $0, CashDoc.rec.ValueMoney );
         end; 
      end;
    end;
  end;
end;

// =================================================================================
//    Обертка для Формируем_Движение_Ресурсов_ВКассе_Doc() - одиночные документы.
// =================================================================================
private macro ProcessDoc_Cash( pApplKind, pApplKey, pSubj, pMode )
  CashDoc.Clear();
  CashDoc.rec.ApplicationKind = pApplKind;
  CashDoc.rec.ApplicationKey  = pApplKey;
  if ( CashDoc.GetEQ() )
    if ( ( StrLen( pSubj ) == 0 )  OR  ( CashDoc.rec.CashOper == pSubj ) )
      Формируем_Движение_Ресурсов_ВКассе_Doc( pMode );
    end;
  end;
end;

// =====================================================
//    Кассовые документы, учитываемые по отдельности.  
// =====================================================
private macro ProcessDocs_Single( pSubj, pMode )
  var cmdD, rsD;

  cmdD = RsdCommand( "select CD.T_APPLICATIONKIND as rApplKind, " +
                            "CD.T_APPLICATIONKEY as rApplKey " +
                     "from dsb_casdc_dbt cd " +
                     "where CD.T_BRANCH = ? " +
                     "and CD.T_CASHDATEDOC = ? " +
                     "and CD.T_TYPEDOC = 2 " +
                     "and CD.T_BRIGADE = ? " +
                     "and CD.T_NUMOPER in (5,6,13,14,4,3,11,12) " +
                     "and CD.T_ACTION <> 2 " +
                     "MINUS " +  // Убираем ВОО с кодами 01 по 11
                     "select CD1.T_APPLICATIONKIND, CD1.T_APPLICATIONKEY " +
                     "from dsb_casdc_dbt cd1, dsb_casln_dbt cl, dreestr_2_dbt r " +
                     "where CD1.T_BRANCH = ? " +
                     "and CD1.T_CASHDATEDOC = ? " +
                     "and CD1.T_TYPEDOC = 2 " +
                     "and CD1.T_BRIGADE = ? " +
                     "and CD1.T_NUMOPER in (5,6,13,14,4,3,11,12) " +
                     "and CD1.T_ACTION <> 2 " +
                     "and CD1.T_APPLICATIONKIND = 260 " +  // SB_EXCHNDOC
                     "and CL.T_APPLICATIONKIND2 = CD1.T_APPLICATIONKIND " +
                     "and CL.T_APPLICATIONKEY2 = CD1.T_APPLICATIONKEY " +
                     "and CL.T_REFVALUE2 = CD1.T_REFVALUE " +
                     "and R.T_APPLICATIONKIND = CL.T_APPLICATIONKIND1 " +
                     "and R.T_APPLICATIONKEY = CL.T_APPLICATIONKEY1 " +
                     "and substr(r.T_CODE113I, 1, 2) >= '01' and substr(r.T_CODE113I, 1, 2) <= '11' " +
                     "MINUS " +  // Убираем прочие с включенным флагом "Сводный документ"
                     "select CD2.T_APPLICATIONKIND, CD2.T_APPLICATIONKEY " +
                     "from dsb_casdc_dbt cd2, dpay_doc_dbt pd, dpay_kind_dbt pk " +
                     "where CD2.T_BRANCH = ? " +
                     "and CD2.T_CASHDATEDOC = ? " +
                     "and CD2.T_TYPEDOC = 2 " +
                     "and CD2.T_BRIGADE = ? " +
                     "and CD2.T_NUMOPER in (5,6,13,14,4,3,11,12) " +
                     "and CD2.T_ACTION <> 2 " +
                     "and CD2.T_APPLICATIONKIND = 200 " + // SB_PAYDOC
                     "and PD.T_IAPPLICATIONKIND = CD2.T_APPLICATIONKIND " +
                     "and PD.T_APPLICATIONKEY = CD2.T_APPLICATIONKEY " +
                     "and PK.T_ISCUR = PD.T_ISCUR " +
                     "and PK.T_GROUPOPERT = PD.T_GROUPOPERT " +
                     "and PK.T_NUMOPERAT = PD.T_NUMOPERT " +
                     "and PK.T_FLAGREESTR <> chr(0)" );  // Флаг "Сводный документ"

  cmdD.addParam( "branch1",  RSDBP_IN );
  cmdD.addParam( "date1",    RSDBP_IN );
  cmdD.addParam( "brigade1", RSDBP_IN );
  cmdD.addParam( "branch2",  RSDBP_IN );
  cmdD.addParam( "date2",    RSDBP_IN );
  cmdD.addParam( "brigade2", RSDBP_IN );
  cmdD.addParam( "branch3",  RSDBP_IN );
  cmdD.addParam( "date3",    RSDBP_IN );
  cmdD.addParam( "brigade3", RSDBP_IN );

  cmdD.value( "branch1" )  = Branch;
  cmdD.value( "date1" )    = DateReport;
  cmdD.value( "brigade1" ) = OperBrigade;
  cmdD.value( "branch2" )  = Branch;
  cmdD.value( "date2" )    = DateReport;
  cmdD.value( "brigade2" ) = OperBrigade;
  cmdD.value( "branch3" )  = Branch;
  cmdD.value( "date3" )    = DateReport;
  cmdD.value( "brigade3" ) = OperBrigade;

  cmdD.execute;
  rsD = RsdRecordset( cmdD );
  while ( rsD.moveNext() )
    ProcessDoc_Cash( rsD.value( "rApplKind" ), rsD.value( "rApplKey" ), pSubj, pMode );
  end;
end;

// ================================================================================
//     Обертка для Формируем_Движение_Ресурсов_ВКассе_Doc() - сводные документы.
// ================================================================================
private macro ProcessDoc_CashCurrency( pApplKind, pApplKey, pSumIn, pSumOut, pSubj, pMode )
  CashDoc.Clear();
  CashDoc.rec.ApplicationKind = pApplKind;
  CashDoc.rec.ApplicationKey  = pApplKey;
  if ( CashDoc.GetEQ() )
    if ( ( StrLen( pSubj ) == 0 )  OR  ( CashDoc.rec.CashOper == pSubj ) )
      if ( CashDoc.rec.FlagDebCredOper == FLAGDEBCRED_IN )
        CashDoc.rec.ValueMoney = pSumIn;
      else
        CashDoc.rec.ValueMoney = pSumOut;
      end;
      Формируем_Движение_Ресурсов_ВКассе_Doc( pMode );
    end;
  end;
end;

// ==========================================
//      Парный к ВОО кассовый документ.
// ==========================================
private macro CashDoc_Currency( pApplKind, pApplKey, pSumIn, pSumOut, pValIn, pValOut, pSubj, pMode )
  var cmdD, rsD;
  cmdD = RsdCommand( "select doc.T_APPLICATIONKIND as rApplKind, " +
                            "doc.T_APPLICATIONKEY as rApplKey " +
                     "from dsb_casln_dbt c, dsb_casln_dbt d, dsb_casdc_dbt doc " +
                     "where c.T_APPLICATIONKIND2 = ? and " +
                           "c.T_APPLICATIONKEY2 = ? and " +
                           // "c.T_REFVALUE2 = 0 and " +
                           "d.T_APPLICATIONKIND1 = c.T_APPLICATIONKIND1 and " +
                           "d.T_APPLICATIONKEY1 = c.T_APPLICATIONKEY1 and " +
                           // "d.T_REFVALUE2 = 0 and " +
                           "d.T_APPLICATIONKIND2 = 260 and " +
                           "doc.T_APPLICATIONKIND = d.T_APPLICATIONKIND2 and " +
                           "doc.T_APPLICATIONKEY = d.T_APPLICATIONKEY2 and " +
                           "((doc.t_flagdebcredoper = 1 and doc.t_refvalue = ?) or " +
                           "(doc.t_flagdebcredoper = 2 and doc.t_refvalue = ?)) ");
  cmdD.addParam( "kind", RSDBP_IN );
  cmdD.addParam( "key",  RSDBP_IN );
  cmdD.addParam( "valin", RSDBP_IN );
  cmdD.addParam( "valout",  RSDBP_IN );
  cmdD.value( "kind" ) = pApplKind;
  cmdD.value( "key" )  = pApplKey;
  cmdD.value( "valin" ) = pValIn;
  cmdD.value( "valout" )  = pValOut;
  cmdD.execute;
  rsD = RsdRecordset( cmdD );
  while ( rsD.moveNext )
    ProcessDoc_CashCurrency( rsD.value( "rApplKind" ), rsD.value( "rApplKey" ), 
                             pSumIn, pSumOut, pSubj, pMode );
  end;
end;

// ======================================================
//    Своды по наличным ВОО с кодом 136-И  от 01 до 11.
// ======================================================
private macro ProcessDocs_Currency( pSubj, pMode )
  var cmdD, rsD;

  cmdD = RsdCommand( "select min(e.T_APPLICATIONKIND) as rApplKind, " +
                            "min(e.T_APPLICATIONKEY) as rApplKey, " +
                            "sum(e.T_INCOMEAMOUNT) as rSumIn, " +
                            "sum(e.T_OUTAMOUNT) as rSumOut, e.T_INCOMEREFVAL as rValIn, e.T_OUTREFVAL rValOut " +
                     "from dexoperat_dbt e, dreestr_2_dbt r " +
                     "where r.T_BRANCH = ? " +
                     "and r.T_curDATE = ? " +
                     "and substr(r.T_CODE113I, 1, 2) >= '01' " + 
                     "and substr(r.T_CODE113I, 1, 2) <= '11' " +
                     "and e.T_APPLICATIONKIND = 260 " +
                     "and e.T_APPLICATIONKIND = r.T_APPLICATIONKIND " +
                     "and e.T_APPLICATIONKEY = r.T_APPLICATIONKEY " +
                     "and e.T_ACTION <> 2 " +
                     "and e.T_BRIGADE = ? " +
                     "group by r.T_OPER, r.T_DATE, r.T_CODE113I, e.T_INCOMEREFVAL, e.T_OUTREFVAL " +
                     "order by r.T_DATE, r.T_CODE113I" );


  cmdD.addParam( "branch",  RSDBP_IN );
  cmdD.addParam( "date",    RSDBP_IN );
  cmdD.addParam( "brigade", RSDBP_IN );
  cmdD.value( "branch" )  = Branch;
  cmdD.value( "date" )    = DateReport;
  cmdD.value( "brigade" ) = OperBrigade;

  cmdD.execute;
  rsD = RsdRecordset( cmdD );
  while ( rsD.moveNext() )
    CashDoc_Currency( rsD.value( "rApplKind" ), rsD.value( "rApplKey" ),
                      rsD.value( "rSumIn" ), rsD.value( "rSumOut" ), rsD.value( "rValIn" ), rsD.value( "rValOut" ), 
                      pSubj, pMode );
  end;
end;

// ================================================================================
//     Обертка для Формируем_Движение_Ресурсов_ВКассе_Doc() - сводные документы.
// ================================================================================
private macro ProcessDoc_CashSvod( pApplKind, pApplKey, pSum, pSubj, pMode )
  CashDoc.Clear();
  CashDoc.rec.ApplicationKind = pApplKind;
  CashDoc.rec.ApplicationKey  = pApplKey;
  if ( CashDoc.GetEQ() )
    if ( ( StrLen( pSubj ) == 0 )  OR  ( CashDoc.rec.CashOper == pSubj ) )
      CashDoc.rec.ValueMoney = pSum;
      Формируем_Движение_Ресурсов_ВКассе_Doc( pMode );
    end;
  end;
end;

// ==================================================================
//    Прочие приходные/расходные операции, объединенные в сводный.
// ==================================================================
private macro ProcessDocs_Other( pSubj, pMode )
  var cmdD, rsD;

  cmdD = RsdCommand( "select sum(PD.T_INSUM) as rSum, " +
                            "min(CD2.T_APPLICATIONKIND) as rApplKind, " +
                            "min(CD2.T_APPLICATIONKEY) as rApplKey " +
                     "from dsb_casdc_dbt cd2, dpay_doc_dbt pd, dpay_kind_dbt pk " +
                     "where CD2.T_BRANCH = ? " +
                     "and CD2.T_CASHDATEDOC = ? " +
                     "and CD2.T_TYPEDOC = 2 " +
                     "and CD2.T_BRIGADE = ? " +
                     "and CD2.T_NUMOPER in (5,6,13,14,4,3,11,12) " +
                     "and CD2.T_ACTION <> 2 " +
                     "and cd2.T_VALUEMONEY <> 0 " +
                     "and CD2.T_APPLICATIONKIND = 200 " + // SB_PAYDOC
                     "and PD.T_IAPPLICATIONKIND = CD2.T_APPLICATIONKIND " +
                     "and PD.T_APPLICATIONKEY = CD2.T_APPLICATIONKEY " +
                     "and PK.T_ISCUR = PD.T_ISCUR " +
                     "and PK.T_GROUPOPERT = PD.T_GROUPOPERT " +
                     "and PK.T_NUMOPERAT = PD.T_NUMOPERT " +
                     "and PK.T_FLAGREESTR <> chr(0) " + // Флаг "Сводный документ"
                     "group by PD.T_CODCASHIER, PD.T_GROUPOPERT, PD.T_CODCUR, " +
                              "PD.T_NUMOPERT, PD.T_APPLTYPE" );

  cmdD.addParam( "branch",  RSDBP_IN );
  cmdD.addParam( "date",    RSDBP_IN );
  cmdD.addParam( "brigade", RSDBP_IN );
  cmdD.value( "branch" )  = Branch;
  cmdD.value( "date" )    = DateReport;
  cmdD.value( "brigade" ) = OperBrigade;

  cmdD.execute;
  rsD = RsdRecordset( cmdD );
  while ( rsD.moveNext() )    
    ProcessDoc_CashSvod( rsD.value( "rApplKind" ), 
                         rsD.value( "rApplKey" ), 
                         rsD.value( "rSum" ),
                         pSubj,
                         pMode );
  end;
end;

// ==============================================================
private macro Формируем_Движение_Ресурсов_ВКассе( mode )

  var cashpatern, operbrigade_str, stat;

  /* Определяем субъект кассы */
  operbrigade_str = string( operbrigade );
  if ( operbrigade < 10 )
    operbrigade_str = "0" + operbrigade_str;
  end;

  cashpatern = mkstr( "0", 4 - strlen( string( {oper} ) ) ) +
               string( {oper} ) + "/" + operbrigade_str;

  ProcessDocs_Single  ( cashpatern, mode );
  ProcessDocs_Currency( cashpatern, mode );
  ProcessDocs_Other   ( cashpatern, mode );

end;

/* Определяем остатки на конец дня. По рублям и всем валютам */
private macro  Формируем_Остатки_Ресурсов_ВКассе (mode)

   var all_codcur_size,in_codcur_size, i, k;
    all_codcur_size=asize (ALL_CURCODE) + MaxSpecSize(); /* количество видов валют, прошедших через кассира */
    in_codcur_size =asize (IN_SUM);      /* количество валют, поступивших в кассу определяем по количеству сумм */

/*    asize (OUT_SUM, all_codcur_size);*/

    if   (mode==RUB)
        OUT_SUM(0)=0;
        if (valtype(IN_SUM(0))!=v_undef)
           OUT_SUM(0)=IN_SUM(0);
        end;
        if (valtype(IN_DAY_SUM(0))!=v_undef)
           OUT_SUM(0)=OUT_SUM(0)+IN_DAY_SUM(0);
        end;
        if (valtype(OUT_DAY_SUM(0))!=v_undef)
           OUT_SUM(0)=OUT_SUM(0)-OUT_DAY_SUM(0);
        end;
 if( (k = FindSpec(0) ) != -1 )
   OUT_SUM( 0 ) = OUT_SUM( 0 ) + SpecSumIn( k ) - SpecSumOut( k );
 end;
    elif (mode==VAL) 
        i=1;
        while  (i<all_codcur_size)
           OUT_SUM(i)=0;
           if ( (i<in_codcur_size) and (valtype(IN_SUM(i))!=v_undef) )
              OUT_SUM(i)=IN_SUM(i);
           end;
           if (valtype(IN_DAY_SUM(i))!=v_undef)
              OUT_SUM(i)=OUT_SUM(i)+IN_DAY_SUM(i);
           end;
           if (valtype(OUT_DAY_SUM(i))!=v_undef)
              OUT_SUM(i)=OUT_SUM(i)-OUT_DAY_SUM(i);
           end;
           if( (k = FindSpec(i) ) != -1 )
             OUT_SUM( i ) = OUT_SUM( i ) + SpecSumIn( k ) - SpecSumOut( k );
             if ( valtype(ALL_CURCODE(i)) == v_undef )
               ALL_CURCODE(i)=SpecCurrCode(k);
             end;
           end;
           i=i+1;
        end;
    end;
end;

/* Определяем входящие ресурсы в кассе с учетом режима работы смены и настроек */
/* Данные помещаем в IN_SUM и в ALL_CURCODE */
/* mode = RUB - входящие рублевые остатки  */ 
/*      = VAL - входящие валютные остатки  */
private macro Формируем_Входящие_Остатки_Ресурсов_ВКассе (mode)

      var cashoper, operbrigade_str, stat;
      var inRest;

      macro Make_it (numoper_)

         /* Определяем субъект кассы */
         operbrigade_str=string (operbrigade);
         if (operbrigade<10)
            operbrigade_str="0"+operbrigade_str;
         end;
         cashoper=mkstr ("0",4-strlen (string ({oper})))+string({oper})+"/"+operbrigade_str;
         keynum (Fsb_casdc,0);
         rewind (Fsb_casdc);
         clearrecord (Fsb_casdc);
         Fsb_casdc.Branch=Branch;
         Fsb_casdc.CashDateDoc=DateReport;
         Fsb_casdc.TypeDoc=2; /* подтвержденные */
         Fsb_casdc.CashOper=cashoper;
         Fsb_casdc.SubAccount="";
         Fsb_casdc.RefValue=0;

         stat=GetGE (Fsb_casdc);
         while ( stat and (Fsb_casdc.Branch==Branch) and (Fsb_casdc.CashDateDoc==DateReport) and
                 (Fsb_casdc.TypeDoc==2) and (Fsb_casdc.CashOper==cashoper)
               )
              /* Осуществляем пробежку по ресурсам */
              if (Fsb_casdc.Numoper==numoper_)
                 if   (mode==RUB) 
                      if (Fsb_casdc.RefValue==1) /* рубли */
                         if(valtype(IN_SUM(0)) == v_undef)
                            IN_SUM(0) = $0;
                            ALL_CURCODE(0)=0;
                         end;               
                         IN_SUM(0)=IN_SUM(0)+Fsb_casdc.ValueMoney;
                      end;
                 elif (mode==VAL)
                       /* Определяем, что это денежный ресурс */
                       if ( Get_resource_struct( Fsb_casdc.RefValue )  AND
                            ( Fsb_casvl.Type == 0 )  AND  // Только платежеспособные.
                            ( Fsb_casvl.TypeValue == 1 )  AND
                            ( Fsb_casvl.CodIntValue != 0 ) )
                          if(valtype(IN_SUM(Fsb_casvl.CodIntValue)) == v_undef)
                             IN_SUM(Fsb_casvl.CodIntValue) = $0;
                             ALL_CURCODE(Fsb_casvl.CodIntValue)=Fsb_casvl.CodIntValue;
                          end;               
                          IN_SUM(Fsb_casvl.CodIntValue)=IN_SUM(Fsb_casvl.CodIntValue)+Fsb_casdc.ValueMoney;
                       end;
                  end;
              end;
              stat=next (Fsb_casdc);
         end;

      end;

      if (not Safe_mode) /* под отчет */
          Make_it (15); /* выдача под отчет из хранилища */
      else /* из сейфа */
          if (AutoDesk)  /* автоматизация рабочего стола кассира включена */
             Make_it (1); /* выдача из сейфа кассиру */
          else           /* автоматизация рабочего стола кассира выключена */
                         /* входящие остатки не формируем (пояснение АО) */
            if ( mode == RUB )
              inRest = $0;
              GetMoney( inRest, "Введите полученную сумму в RUR" );
              if ( inRest > $0 )
                ALL_CURCODE( 0 ) = 0;
                IN_SUM( 0 ) = inRest;
              end;
            else
              ClearRecord( fCurr );
              while ( Next( fCurr ) )
                if ( fCurr.CrDifrRate  AND  fCurr.Code_Currency  AND  ( NOT fCurr.materialref ) )
                  inRest = $0;
                  GetMoney( inRest, "Введите полученную сумму в " + fCurr.Short_Name );
                  if ( inRest > $0 )
                    ALL_CURCODE( fCurr.Code_Currency ) = fCurr.Code_Currency;
                    IN_SUM( fCurr.Code_Currency ) = inRest;
                  end;
                end;
              end;
            end;
          end;
      end;

end;

/* Получаем числовой код валюты */
private macro  GetCurrNumericalCode( Code_Currency )

  file Currency ( "currency.dbt", "sbbank.def" );

  if( ValType(Code_Currency) == V_UNDEF )
    return "";
  end;

  ClearRecord( Currency );
  Currency.Code_Currency = Code_Currency;

  if( GetEQ( Currency ) )
    return Currency.ExternalCode;
  else
    return "";
  end;

end;

/* Возвращает сумму строкой.*/
private macro doMoneyToStr_jar ( Sum, CodeCurrency )
    var  ext_code;

    ext_code = int( GetCurrNumericalCode( CodeCurrency ) );
    return "("+CurToStrAlt( Sum, null, null, ext_code )+")";
end;

private macro GetBranchName

  var ddep = TRecHandler( "i_dp_dep.rec" );
  var party = TRecHandler( "i_party.rec" );

  if ( findDepartment(Branch, null, ddep, party) )
    return party.rec.Name + "  " + ddep.rec.Name;
  else
    return "<не найден>";
  end;
end; /*GetBranchName*/

private macro date_monat(number)
       
  var monat;

   if(number == 1)
      monat = "января";
   elif(number == 2)
      monat = "февраля";
   elif(number == 3)
      monat = "марта";
   elif(number == 4)
      monat = "апреля";
   elif(number == 5)
      monat = "мая";
   elif(number == 6)
      monat = "июня";
   elif(number == 7)
      monat = "июля";
   elif(number == 8)
      monat = "августа";
   elif(number == 9)
      monat = "сентября";
   elif(number == 10)
      monat = "октября";
   elif(number == 11)
      monat = "ноября";
   elif(number == 12)
      monat = "декабря";
   else
      monat = "ошибка в номере месяца";
   end;

 return monat;
end;


private macro titul ()

      var DD, MM, YY;

      DateSplit (DateReport, DD, MM, YY);

 [
                                                                                 +-------------------+
                                                                                 |     Код формы     |
                                                                                 | документа по ОКУД |
                                                                                 +-------------------+
                                                                                 |      0402112      |
                                                                                 +-------------------+

              ######################################################################################
              ______________________________________________________________________________________
           (полное фирменное (сокращенное фирменное) наименование кредитной организации или полное 
            (сокращенное) наименование филиала, или наименование и (или) номер ВСП (при наличии) либо 
            иные идентифицирующие признаки ВСП (при отсутствии наименования и номера) с указанием на 
                              его принадлежность кредитной организации (филиалу)


                                      О Т Ч Е Т Н А Я  С П Р А В К А

           ###########################################################################################


   Получено для совершения операций в сумме
   (суммах) с указанием наименования валюты:


 ]
 ( GetBranchName():c:w, 
   ( "\"" + String(DD) + "\" " + date_monat(MM) + " " + String(YY) + " " + "года" ):c );

end;

private macro in_sums( CurrCode )

  var i,in_sums_arr_size;

  /* В зависимости от настроек RS-Retail определяем входящие остатки */
  if (Все_валюты_вместе)
     /* Формируем строку типа "код валюты + сумма цифрами + сумма прописью + код валюты +..."*/
    i=0;
    in_sums_arr_size=asize (IN_SUM); /* Количество входящих валютных ресурсов */
    while (i<in_sums_arr_size)
      if( (valtype (ALL_CURCODE(i))!=v_undef) and (valtype(IN_SUM(i)) != v_undef) )
 println(" " + GetCurrName(ALL_CURCODE(i))+": "+string (IN_SUM(i))+" "+doMoneyToStr_jar (IN_SUM(i), ALL_CURCODE(i)));
      end;
      i=i+1;
    end;
  else
    if( (valtype (ALL_CURCODE(CurrCode))!=v_undef) and (valtype(IN_SUM(CurrCode)) != v_undef) )
      println(" " + GetCurrName(ALL_CURCODE(CurrCode))+": "+string (IN_SUM(CurrCode))+" "+doMoneyToStr_jar (IN_SUM(CurrCode), ALL_CURCODE(CurrCode)));
    end;
  end;
end;

private macro out_sums( CurrCode )

var i, out_sums_arr_size;

  [ +------------+------------+-----------------------+------------+-----------------------+----------------+];
  [ |  по документам в электронном виде:                                                                    |];
  [ +------------+------------+-----------------------+------------+-----------------------+----------------+];
  [ |            |            |                       |            |                       |                |];
  [ +------------+------------+-----------------------+------------+-----------------------+----------------+];
  [ |            |            |                       |            |                       |                |];
  [ +------------+------------+-----------------------+------------+-----------------------+----------------+];
  [ ];
  [ Остаток (остатки) на конец дня в сумме ];
  [ (суммах) с указанием валюты: ];
  [ ];

/* В зависимости от настроек RS-Retail определяем исходящие остатки */
  if(Все_валюты_вместе)
    /* Формируем строку типа "код валюты + сумма цифрами + сумма прописью + код валюты +..."*/
    i=0;
    out_sums_arr_size=asize (OUT_SUM); /* Количество исходящих валютных ресурсов */
    while (i<out_sums_arr_size)
      if( (valtype (ALL_CURCODE(i))!=v_undef) and (valtype(OUT_SUM(i)) != v_undef) )
        println(" " + GetCurrName(ALL_CURCODE(i))+": "+string (OUT_SUM(i))+" "+doMoneyToStr_jar (OUT_SUM(i), ALL_CURCODE(i)));
      end;
      i=i+1;
    end;
  else
    if( (valtype (ALL_CURCODE(CurrCode))!=v_undef) and (valtype(OUT_SUM(CurrCode)) != v_undef) )
      println(" " + GetCurrName(ALL_CURCODE(CurrCode))+": "+string (OUT_SUM(CurrCode))+" "+doMoneyToStr_jar (OUT_SUM(CurrCode), ALL_CURCODE(CurrCode)));
    end;
  end;
end;

private macro in_out_docs( CurrCode )
      
      var day_doc_number, i, k,
          IN_DAY_NUMB_STR  ="",
          IN_DAY_SUM_STR   ="",
          OUT_DAY_NUMB_STR ="",
          OUT_DAY_NUMB_STZ ="",
          OUT_DAY_SUM_STR  ="";
      var sItogo, sDP;

      if ( vBankStandart == 0 )
        sItogo = "";
        sDP = "";
      else
        sItogo = "Итого, ";
        sDP = ":";
      end;

      /* Определяем строковые значения прихода и расхода за день по валюте */
      macro Set_day_str (i_cur)
         IN_DAY_NUMB_STR="";
         IN_DAY_SUM_STR =""; 
         OUT_DAY_NUMB_STR="";
         OUT_DAY_SUM_STR =""; 
         if (valtype (IN_DAY_NUMB(i_cur))!=v_undef)
            IN_DAY_NUMB_STR=IN_DAY_NUMB(i_cur);
            IN_DAY_SUM_STR =IN_DAY_SUM (i_cur); 
         end;
         if (valtype (OUT_DAY_NUMB(i_cur))!=v_undef)
            OUT_DAY_NUMB_STR=OUT_DAY_NUMB(i_cur);
            OUT_DAY_SUM_STR =OUT_DAY_SUM (i_cur); 
         end;
      end;

      [  _____________________________________________________________________________________________
                                                       (цифрами и прописью)

        +-------------------------------------------------------------------------------------------------------+
        |Наименование|                Приход              |              Расход                |    Фамилии,    |
        |   валюты   +------------+-----------------------+------------+-----------------------+   инициалы и   |
        |            | количество |    сумма цифрами с    | количество |    сумма цифрами с    |    подписи     |
        |            |  кассовых  |       указанием       |  кассовых  |       указанием       | бухгалтерских  |
        |            | документов |  наименования валюты  | документов |  наименования валюты  |   работников   |
        +------------+------------+-----------------------+------------+-----------------------+----------------+
        |      1     |      2     |            3          |      4     |            5          |        6       |
        +------------+------------+-----------------------+------------+-----------------------+----------------+
        |  по документам, составленным на бумажном носителе:                                                    |
      ];
      day_doc_number=asize (ALL_CURCODE); 
      if (Все_валюты_вместе) /* печатаем строки по всем валютам */
         i=0;
         while (i<day_doc_number)
            Set_day_str (i);
            if( (valtype(ALL_CURCODE(i))!=v_undef) and ((IN_DAY_NUMB_STR != "") or (OUT_DAY_NUMB_STR != "") ) )
              if ( OUT_DAY_NUMB_STR != "0" )
                OUT_DAY_NUMB_STZ = OUT_DAY_NUMB_STR;
              end;
              O_Rep( ALL_CURCODE(i) );
              if ( ( IN_DAY_SUM_STR != "" )  AND  ( StrLen( IN_DAY_NUMB_STR ) == 0 ) )
                IN_DAY_NUMB_STR = "0";
              end;
              if ( ( OUT_DAY_SUM_STR != "" )  AND  ( StrLen( OUT_DAY_NUMB_STZ ) == 0 ) )
                OUT_DAY_NUMB_STZ = "0";
              end;
              [ +------------+------------+-----------------------+------------+-----------------------+----------------+];
              [ |############|############|#######################|############|#######################|                |]
              ((sItogo+GetCurrName(ALL_CURCODE(i))+sDP):w:c, IN_DAY_NUMB_STR:c, sum_curr( IN_DAY_SUM_STR, ALL_CURCODE(i)):r, OUT_DAY_NUMB_STZ:c, sum_curr( OUT_DAY_SUM_STR, ALL_CURCODE(i)):r);
            end;
            if( valtype(ALL_CURCODE(i)) != v_undef )
              if( (k = FindSpec(ALL_CURCODE(i))) != -1 )
                SpecCurrCode( k ) = NULL;
                OS_Rep( ALL_CURCODE(i) );
                [ +------------+------------+-----------------------+------------+-----------------------+----------------+];
                [ |############|############|#######################|############|#######################|                |]
                ((sItogo+GetCurrName(ALL_CURCODE(i))+" (ПД)"+sDP):w:c, SpecCountIn( k ):c, sum_curr( SpecSumIn( k ), ALL_CURCODE(i)), SpecCountOut( k ):c, sum_curr( SpecSumOut( k ), ALL_CURCODE(i)));
              end;
            end;
            i=i+1;
         end;
         i = 0;
         while( i < asize(SpecCurrCode) )
           if( SpecCurrCode(i) != NULL )
             OS_Rep( SpecCurrCode( i ) );
             [ +------------+------------+-----------------------+------------+-----------------------+----------------+];
             [ |############|############|#######################|############|#######################|                |]
             ((sItogo+GetCurrName(SpecCurrCode( i ))+" (ПД)"+sDP):w:c, SpecCountIn( i ):c, sum_curr( SpecSumIn( i ), SpecCurrCode(i)), SpecCountOut( i ):c, sum_curr( SpecSumOut( i ), SpecCurrCode(i)));
             SpecCurrCode( i ) = NULL;
           end;
           i = i + 1;
         end;
      else
        Set_day_str (CurrCode);
        if( (valtype(ALL_CURCODE(CurrCode))!=v_undef) and ((IN_DAY_NUMB_STR != "") or (OUT_DAY_NUMB_STR != "") ) )
          if ( OUT_DAY_NUMB_STR != "0" )
            OUT_DAY_NUMB_STZ = OUT_DAY_NUMB_STR;
          end;
          O_Rep( ALL_CURCODE(CurrCode) );
          if ( ( IN_DAY_SUM_STR != "" )  AND  ( StrLen( IN_DAY_NUMB_STR ) == 0 ) )
            IN_DAY_NUMB_STR = "0";
          end;
          if ( ( OUT_DAY_SUM_STR != "" )  AND  ( StrLen( OUT_DAY_NUMB_STZ ) == 0 ) )
            OUT_DAY_NUMB_STZ = "0";
          end;
          [ +------------+------------+-----------------------+------------+-----------------------+----------------+];
          [ |############|############|#######################|############|#######################|                |]
          ((sItogo+GetCurrName(ALL_CURCODE(CurrCode))+sDP):w:c, IN_DAY_NUMB_STR:c, sum_curr( IN_DAY_SUM_STR, ALL_CURCODE(CurrCode)):r, OUT_DAY_NUMB_STZ:c, sum_curr( OUT_DAY_SUM_STR, ALL_CURCODE(CurrCode)):r);
        end;
        if( valtype(ALL_CURCODE(CurrCode)) != v_undef )
          if( (k = FindSpec(ALL_CURCODE(CurrCode))) != -1 )
            SpecCurrCode( k ) = NULL;
            OS_Rep( ALL_CURCODE(CurrCode) );
            [ +------------+------------+-----------------------+------------+-----------------------+----------------+];
            [ |############|############|#######################|############|#######################|                |]
            ((sItogo+GetCurrName(ALL_CURCODE(CurrCode))+" (ПД)"+sDP):w:c, SpecCountIn( k ):c, sum_curr( SpecSumIn( k ), ALL_CURCODE(CurrCode)), SpecCountOut( k ):c, sum_curr( SpecSumOut( k ), ALL_CURCODE(CurrCode)));
   end;
        end;
      end;
end;

macro podval
      [  ____________________________________________________________________________________________            ];
      [                                 (цифрами и прописью)                                                     ];
      [ ];
      [ _Кассир __________________  ______________________________  _#_______________________________            ]
      ( GetOperName( {oper} ) );
      [  (наименование должности)          (личная подпись)                (фамилия и инициалы)                  ];
      [ ];
      [ ];                                                                                                       
      [ _Зав.кассой ______________  ______________________________  _________________________________            ];
      [  (наименование должности)          (личная подпись)                (фамилия и инициалы)                  ];
      [ ];
      [];
end;


macro Main()

    var stat, i, res_number;

    stat = GetDateReport();    /* Устанавливаем дату отчета */
    if( stat )

       /* Получаем настройки и режим работы смены */
       analize_ret;

       // if (not ((Safe_mode and AutoDesk) or (not Safe_mode))) 
       //    msgbox ("Справка выпускается для автоматизированных (раздельных) | рабочих столов кассиров");
       //    exit (1);
       // end;

       /* Доинициализация */
       asize (ALL_CURCODE,0); /* массив кодов валют, прошедших через кассира */
       asize (IN_DAY_SUM,  0);
       asize (IN_DAY_NUMB, 0);
       asize (OUT_DAY_SUM, 0);
       asize (OUT_DAY_NUMB,0);
       asize (IN_SUM,0);
       asize (OUT_SUM, 0);

       Формируем_Движение_Монет_и_Чеков();
       /* Печатаем рублевую справку */
       Формируем_Входящие_Остатки_Ресурсов_ВКассе (RUB);
       Формируем_Движение_Ресурсов_ВКассе (RUB);
       Формируем_Остатки_Ресурсов_ВКассе (RUB);

       Формируем_Входящие_Остатки_Ресурсов_ВКассе (VAL);
       Формируем_Движение_Ресурсов_ВКассе (VAL);
       Формируем_Остатки_Ресурсов_ВКассе (VAL);

       /* Выводим отчет */
       if( Все_валюты_вместе )
         titul;
         in_sums();     /* входящий остаток*/
         in_out_docs(); /* приход и расход за день */
         out_sums();    /* остаток на конец дня*/
         podval;
       else
         /* Для каждой валюты своя справка */
         i=0;
         Res_number=asize (ALL_CURCODE);
         while (i<Res_number)
            if (valtype (ALL_CURCODE(i))!=v_undef)
               titul;
/*               if (valtype (IN_SUM(i))!=v_undef)*/
                  in_sums (i);     /* входящий остаток*/
/*               end;*/
               in_out_docs (i); /* приход и расход за день */
               out_sums (i);    /* остаток на конец дня */
               podval;
            end;
            i=i+1;
         end;
       end;

    else
       [ Отказ от выпуска отчета ];
    end;

end;


main;
