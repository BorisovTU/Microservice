import CommonInter;

/******************************************************************************/
var FIOInit = "",
    CountryInit = "",
    NominalInit = "",
    YearInit = 0,
    SeriesInit = "", 
    vTypeInDesk = "Не определен";


/******************************************************************************/
private file   CashVal( "sb_casvl.dbt" ) key 0;
private record CashDoc( "sb_casdc.dbt" );
private record Bag    ( "bag.dbt" );
private record dlg    (SPRAVEXP) dialog;

private const TP_DESK_NO    =  "",  // Не определен
              TP_DESK_ADM   = "А",  // Администратор системы
              TP_DESK_ADMIN = "З",  // Зав.кассой (администратор)
              TP_DESK_OPER  = "О",  // Контролер
              TP_DESK_CASH  = "К",  // Кассир
              TP_DESK_PANTRY= "Л",  // Кладовщик
              TP_DESK_CONR  = "Р";  // Ревизор (управляющий)

/************************ Класс выпуска справки *******************************/
class TSpravkaExpertPrint

private const CL_DEP   = 0,   /* Отделение */
              CL_BANK  = 1,   /* Банк */
              CL_LEGAL = 2,   /* Юр. лицо */
              CL_TEMP  = 3;   /* Физ. лицо */
 
private var Branch = NumFNCash, {oper}, {curdate};
private var FIO = "", Country = "", Nominal = "", Year = 0,Series = "", 
            DopParm = "", OnExpert = "",Docum = "",Address = "", NumSpravka = 0,
            PaperIssuerCode = "";

/******************************************************************************/
  private macro Interface()
      macro MessProcForSpravka(dl,cmd,id,key)
      const ENTER = 13, ESC = 27, F2 = 316, F3 = 317, SPACE = 32;

      const ne_FIO      = 0,
          ne_Country  = 1,
          ne_Nominal  = 2,
          ne_Year     = 3,
          ne_Series   = 4,
          ne_DopParm  = 5;

        if( cmd == DLG_INIT )
          dl.FIO = FIOInit;
          dl.Country = CountryInit;
          dl.Nominal = NominalInit;
          dl.Year = YearInit;
          dl.Series = SeriesInit;
          dl.DopParm = "";
          UpdateFields(dl);
        end;
        if( cmd == DLG_KEY )
          if( key == F2 )
            return CM_SAVE;
          end;
          if( key == SPACE )
          end;
          if( key == ESC )
            return CM_CANCEL;
          end;
        end;
      end;

    return RunDialog(dlg,"MessProcForSpravka");
  end;

  macro GetNumSpravka()
    var CountValue = double(0), retvalue = 0;

    if ( GetCountValueEx( 2, 4, Branch, CountValue ) )
       retvalue = Int( CountValue );  			
    end;
    
    return retvalue;
  end;

  private macro FillFIODocumAndAddressEx( ClientID, ClientType )
    var clntL = TClientList;

    if( ClientType == CL_TEMP )
      if ( clntL.GetRecord( ClientID ) )
        FIO = clntL.CurRec.rec.Name1 + " " + clntL.CurRec.rec.Name2 + " " + clntL.CurRec.rec.Name3;
        Docum = GetNameOfPAPRKIND ( clntL.CurRec.rec.PaperKind ) + " " + 
                clntL.CurRec.rec.PaperSeries + " № " + 
                clntL.CurRec.rec.PaperNumber;
        Address = clntL.CurRec.rec.Address;
      end;
    else
      FIO = dlg.FIO;
    end;    
  end;

  private macro MakeDate( StringDate )

    var DateGI = "", vPoint = 0, vRet = "";
    DateGI = Trim(String(StringDate:m));
    if ((SubStr(DateGI,2,1) != "0") and
        (SubStr(DateGI,2,1) != "1") and
        (SubStr(DateGI,2,1) != "2") and
        (SubStr(DateGI,2,1) != "3") and
        (SubStr(DateGI,2,1) != "4") and
        (SubStr(DateGI,2,1) != "5") and
        (SubStr(DateGI,2,1) != "6") and
        (SubStr(DateGI,2,1) != "7") and
        (SubStr(DateGI,2,1) != "8") and
        (SubStr(DateGI,2,1) != "9"))
         DateGI = "0" + DateGI;
    end;
    vPoint = Index( DateGI, "." );
    if ( vPoint > 3 )
      vRet = "\"" + SubStr( DateGI, 1, 2 ) + "\"" + SubStr( DateGI, 3, vPoint - 3 ) + "ода";
    else
      vRet = "\"" + SubStr( DateGI, 1, 2 ) + "\"" + SubStr( DateGI, 3 );
    end;
    return vRet; 
  end;

  private macro GetOperName( Oper )

    file pers ( person, "sbbank.def" );
    var Name = "", IOtmp;

    pers.Oper = Oper;
    if( geteq( pers ) )

       if ( pers.cTypeInDesk == TP_DESK_NO )
         vTypeInDesk = "Не определен";
       elif ( pers.cTypeInDesk == TP_DESK_ADM )
         vTypeInDesk = "Администратор";
       elif ( pers.cTypeInDesk == TP_DESK_ADMIN )
         vTypeInDesk = "Зав.кассой";
       elif ( pers.cTypeInDesk == TP_DESK_OPER )
         vTypeInDesk = "Контролер";
       elif ( pers.cTypeInDesk == TP_DESK_CASH )
         vTypeInDesk = "Кассир";
       elif ( pers.cTypeInDesk == TP_DESK_PANTRY )
         vTypeInDesk = "Контролер с сейф.";
       elif ( pers.cTypeInDesk == TP_DESK_CONR )
         vTypeInDesk = "Ревизор";
       else
         vTypeInDesk = "Не определен";
       end;

       if ( Index( pers.Name, " " ) > 0 )
         Name  = SubStr( pers.Name, 1, Index(pers.Name, " ") - 1 );
         IOtmp = Trim( Substr( pers.Name, Index(pers.Name, " ") ) );
         Name  = Name + " " + strupr( Substr(IOtmp, 1, 1)) + ".";
         IOtmp = Trim( Substr( IOtmp, Index(IOtmp," ") ) );
         if ( IOtmp > 0 )
           Name = Name + strupr( Substr( IOtmp, 1, 1 ) ) + ".";
         end;
       else
         Name = pers.Name;
       end;
    end;

    return Name;
  end;

  private macro PrintHead()
    [                                                          +-------------------+];
    [                                                          |     Код формы     |];
    [                                                          | документа по ОКУД |];
    [                                                          +-------------------+];
    [                                                          |      0402159      |];
    [                                                          +-------------------+];
    [ ];
    [ ];
    [                                  СПРАВКА N # ]
    (NumSpravka:l);
    [];
    [              о приеме на экспертизу сомнительных денежных знаков              ];
    [             (задержании имеющих признаки подделки денежных знаков)            ];
    [ ];
    [                        #####################################		    ]
    ( ( ( MakeDate( {curdate} ) ) ):c );
    [ ];
    [ ############################################################################  ]
    ( ("Выдана "+FIO+","):w );
    [ ------------------------------------------------------------------------------];
    [              (фамилия, имя, отчество (наименование организации))              ];
    [ ############################################################################  ]
    ( ("проживающему (находящейся) по адресу: "+Address):w );
    [ ------------------------------------------------------------------------------];
    [              (почтовый адрес физического лица (адрес организации))            ];
    [ ############################################################################  ]
    ( ("документ, удостоверяющий личность, " + Docum):w );
    [ код подразделения ###########](PaperIssuerCode:w);
    [ в том, что предъявленные им денежные знаки:				    ];
    [ +---------+-------------+---------------+------------------------------------+];
    [ | Номинал | Год образца | Серия и номер |      Дополнительные реквизиты      |];
    [ |         |  (выпуска)  |               |                                    |];
    [ +---------+-------------+---------------+------------------------------------+];
    [ |    1    |      2      |       3       |                  4                 |];
    [ +---------+-------------+---------------+------------------------------------+];
  end;

  private macro PrintFoot()
    [ +---------+-------------+---------------+------------------------------------+];
    [ ];
    [ приняты на экспертизу, задержаны как имеющие явные признаки подделки          ];
    [ (ненужное зачеркнуть).                    				    ];
    [ ];
    [ ];
    [ ######################## _________________________ ###########################]
    ( vTypeInDesk:c, GetOperName({oper}):c:w );
    [ (наименование должности)     (личная подпись)          (фамилия и инициалы)   ];
    [ ];
    [ ];
    [ ];
    [       Место печати                                                            ];
    [         (штампа)                                                              ];
    [ ];
    [ ];
    [ ];
    [ ];
  end;

  private macro FindCashVal( Ref )
    CashVal.RefValue = Ref;
    if ( not GetEQ( CashVal ) )
      MsgBox( "Не найден ресурс #" + Ref );
      Exit( 1 );
    end;
  end;

  private macro ProcessBagInfoEx( ValueRef )
    FindCashVal( ValueRef );
    [ |#########|#############|###############|####################################|]
    ( ( Trim( Nominal ) ):w, Year:l, Series:w,
      ( "Валюта \"" + CashVal.NameValue + "\", эмитент \"" + Country + "\", " + DopParm ):w );
  end;
/************* Открытая часть. Можно под разные модули затачивать *************/

  macro ProcessBagInfo()
    ProcessBagInfoEx( Bag.ValueRef );
  end;

  macro PrintReport()
    PrintHead();
    ProcessBagInfo();
    PrintFoot();
  end;


  macro FillFIODocumAndAddress()
    FillFIODocumAndAddressEx(Bag.ClientID,Bag.ClientType);
  end;

  macro RunReport()
    if( Interface() )
      NumSpravka = GetNumSpravka();
      FIO     = dlg.FIO;
      Country = dlg.Country;
      Nominal = dlg.Nominal;
      Year    = dlg.Year;
      Series  = dlg.Series;
      DopParm = dlg.DopParm;
      FillFIODocumAndAddress();
      PrintReport();
      PrintReport();
    end;
  end;

end;