/*
$Name:             svodrep.mac
$Module:           RS-Retail
$Description:      Сводная справка о кассовых оборотах
*/

import TDate;

private var paydoc   = TBFile( "paydoc.dbt",   "r", 0, null, "exchange.def");
private var sb_casvl = TBFile( "sb_casvl.dbt", "r", 0 );
private var rtgroup  = TBFile( "rtgroup.dbt",  "r", 2 );
private var person   = TBFile( "person.dbt",   "r", 0 );
private var officer  = TBFile( "officer.dbt",  "r", 0 );

private var
  {curdate}, 
  {oper}, 
  {OurBank},
  Branch = NumFNCash, 
  Brigade = GetOperBrigade;

private var
  HeadOper = {oper};

  private array  /* Массив валют */
      CurrArr, 
      CountInArr,
      SumInArr,
      SumInArrRub,
      CountOutArr,
      SumOutArr,
      SumOutArrRub;

  private array  /* Массив плохих валют */
      BadCurrArr, 
      BadCountInArr,
      BadSumInArr,
      BadSumInArrRub,
      BadCountOutArr,
      BadSumOutArr,
      BadSumOutArrRub;

  private array   /* Массив ПД */
    SpecCurrCode, 
    SpecSumIn,
    SpecSumInRub, 
    SpecCountIn, 
    SpecSumOut, 
    SpecSumOutRub, 
    SpecCountOut;
/******************************************************************************/
/*     Открытая часть. Необходима для вызова из других макросов.              */

var Currency = TBFile( "currency.dbt", "r", 0 );

macro FindCurr( Code )

  Currency.KeyNum = 0;
  Currency.Clear();
  Currency.rec.Code_Currency = Code;
  if ( not Currency.GetEQ() )
    MsgBox( "Не найдена валюта #" + Code );
    Exit( 1 );
  end;
end;
/******************************************************************************/

/******************************************************************************/
/*    Перетащил все сюда из svbrmain.mac и desk_tot.mac, т.к.                 */
/*                надоела оба этих макроса корячить чуть что...               */
/******************************************************************************/
class TSvodReport

  /*ФЛАГИ*/
  private const 
  /*Флаг печати рублевого эквивалента валюты (True - печатать, False - не печатать)*/
    flagRubEQ = False;

  private var
  /*Флаг печати кода валюты (0 - в буквеном виде(RUR), 1 - буквенный/цифровой(RUR/810), 2 - цифровой(810) ), 3 - Название валюты*/
    flagCurrCode = 3;



  private var                   /* Нужно ли печатать ту или иную строку */
    needPrintCurrency = false,
    needPrintPD = false,
    needPrintDragMet = false,
    needPrintNewMon = false,
    needPrintBadCurrency = false,
    needPrintOldMoney = false;

  private var                         /* Номер счетов. Мне кажется, удобней  */
    accountForCurrency = "20202",   /*  отсюда их править, чем искать в макросе */
    accountForPD = "20203",
    accountForDragMet = "20302",
    accountForNewMon = "20202.01",
    accountForBadCurrency = "20202.02",
    accountForOldMon = "20308";


  private var newMonCountIn = 0, /* Инфо по монетам н. + с. */
	      newMonSumIn = 0,
              newMonCountOut = 0,
	      newMonSumOut = 0,
	      oldMonCountIn = 0,
	      oldMonSumIn = 0,
              oldMonCountOut = 0,
	      oldMonSumOut = 0;

  private var DragMetCountIn = 0, /* Инфо по даргметаллам */
	      DragMetSumIn = 0,
              DragMetCountOut = 0,
	      DragMetSumOut = 0;

  private var vTypeInDesk = "Не определен";

  private const TP_DESK_NO    =  "",  // Не определен
                TP_DESK_ADM   = "А",  // Администратор системы
                TP_DESK_ADMIN = "З",  // Зав.кассой (администратор)
                TP_DESK_OPER  = "О",  // Контролер
                TP_DESK_CASH  = "К",  // Кассир
                TP_DESK_PANTRY= "Л",  // Кладовщик
                TP_DESK_CONR  = "Р";  // Ревизор (управляющий)

/******************************************************************************/
  private macro GetHeadOper()
    rtgroup.Clear();
    rtgroup.rec.Branch = Branch;
    rtgroup.rec.Date   = {curdate};
    rtgroup.rec.ID     = Brigade;
    if ( rtgroup.GetEQ() )
      if ( rtgroup.rec.HeadID > 0 )
        HeadOper = rtgroup.rec.HeadID;
      end;
    end;
  end;

/******************************************************************************/
  private macro GetOperName()

    file pers ( person, "sbbank.def" );
    var Name = "", IOtmp;

    pers.Oper = HeadOper;
    if( geteq( pers ) )

       if ( pers.cTypeInDesk == TP_DESK_NO )
         vTypeInDesk = "Не определен";
       elif ( pers.cTypeInDesk == TP_DESK_ADM )
         vTypeInDesk = "Администратор";
       elif ( pers.cTypeInDesk == TP_DESK_ADMIN )
         vTypeInDesk = "Зав.кассой";
       elif ( pers.cTypeInDesk == TP_DESK_OPER )
         vTypeInDesk = "Контролер";
       elif ( pers.cTypeInDesk == TP_DESK_CASH )
         vTypeInDesk = "Кассир";
       elif ( pers.cTypeInDesk == TP_DESK_PANTRY )
         vTypeInDesk = "Контролер с сейф.";
       elif ( pers.cTypeInDesk == TP_DESK_CONR )
         vTypeInDesk = "Ревизор";
       else
         vTypeInDesk = "Не определен";
       end;

       if ( Index( pers.Name, " " ) > 0 )
         Name  = SubStr( pers.Name, 1, Index(pers.Name, " ") - 1 );
         IOtmp = Trim( Substr( pers.Name, Index(pers.Name, " ") ) );
         Name  = Name + " " + strupr( Substr(IOtmp, 1, 1)) + ".";
         IOtmp = Trim( Substr( IOtmp, Index(IOtmp," ") ) );
         if ( IOtmp > 0 )
           Name = Name + strupr( Substr( IOtmp, 1, 1 ) ) + ".";
         end;
       else
         Name = pers.Name;
       end;
    end;

    return Name;
  end;  /*GetOperName*/

/******************************************************************************/
  private macro GetOperPost()
    var _Name = "Не определен";
    person.Clear();
    person.rec.Oper = HeadOper;
    if ( person.GetEQ() )
      officer.Clear();
      officer.rec.PartyID  = {OurBank};
      officer.rec.PersonID = person.rec.PartyID;
      if ( officer.GetEQ() )
        _Name = officer.rec.Post;
      end;
    end;
    return _Name;
  end;  /*GetOperPost*/

/******************************************************************************/
  private macro GetBranchName

    var name;
    var party = TRecHandler( "i_party.rec" );

    if ( findDepartment(Branch, name, null, party) )
      return party.rec.Name + "  " + name;
    else
      return "<не найден>";
    end;

  end; /*GetBranchName*/
/******************************************************************************/
  private macro PrintSeparator
    [+--------------+------------+--------------------+------------+--------------------+];
  end;
/******************************************************************************/
  /*
  private macro GetStrForSignt()
    var str = "";    
    if( GetBankStandart() == 0 )
      str = "(наименование, № филиала Сбербанка России/№ ВСП)";
    else
      str = "(Наименование кредитной организации)";
    end;
    return str;
  end;
*/
/******************************************************************************/

macro PrintCellar

  GetHeadOper();
	
  [+--------------+------------+--------------------+------------+--------------------+];
  [| по документам в электронном виде:                                                |];
  [+--------------+------------+--------------------+------------+--------------------+];
  [|              |            |                    |            |                    |];
  [+--------------+------------+--------------------+------------+--------------------+];
  [ ];
  [ ];
  [    ############################  __________________  #########################     ]
  ( GetOperPost():c, ( GetOperName() ):c:w );
  [       (наименование должности)     (личная подпись)      (фамилия и инициалы)      ];
  [ ];
  [    С данными бухгалтерского учета сверено:                                         ];
  [    ____________________________  __________________  _________________________     ];
  [       (наименование должности)     (личная подпись)      (фамилия и инициалы)      ];
  [ ];
  [ ];
  [ ];
  [ ];
  [];
end;                                                                                   
/******************************************************************************/
                                                                                      
macro PrintHead                                                                        
  var dateStr = TDateStorage;

  [                                                               +-------------------+];
  [                                                               |     Код формы     |];
  [                                                               | документа по ОКУД |];
  [                                                               +-------------------+];
  [                                                               |      0402114      |];
  [                                                               +-------------------+];
  [ ];
  [ ];
  [        ####################################################################        ]
  ( GetBranchName():c:w );
  [        ____________________________________________________________________        ];
  [  полное фирменное (сокращенное фирменное) наименование кредитной организации или   ];
  [   полное (сокращенное) наименование филиала; или наименование и (или) номер ВСП    ];
  [ (при наличии) либо иные идентифицирующие признаки ВСП (при отсутствии наименования ];
  [     и номера) с указанием на его принадлежность кредитной организации (филиалу)    ];
  [ ];                                                                                 
  [ ];
  [                          СПРАВКА О КАССОВЫХ ОБОРОТАХ                               ];
  [ ];
  [               ##################################################                   ]
  ( dateStr.getDateInCurStr("\"dd\" month yyyy года"):c);
  [ ];
  [ ];
  [+--------------+---------------------------------+---------------------------------+];
  [| Наименование |             Приход              |               Расход            |];
  [|    валюты    +---------------------------------+---------------------------------+];
  [|              | Kоличество |  Сумма цифрами с   | Kоличество |  Сумма цифрами с   |];
  [|              |  кассовых  |     указанием      |  кассовых  |     указанием      |];
  [|              | документов |    наименования    | документов |    наименования    |];
  [|              |            |       валюты       |            |       валюты       |];
  [+--------------+------------+--------------------+------------+--------------------+];
  [|       1      |      2     |          3         |      4     |          5         |];
  [+--------------+------------+--------------------+------------+--------------------+];
  [| по документам, составленным на бумажном носителе:                                |];
end;
/******************************************************************************/

macro PrintLine(CurrCod, CredOrdCount, RubCred, DebOrdCount, RubDeb)
  PrintSeparator();
  [|##############| ########## | ################## | ########## | ################## |]
  (
    CurrCod:w,
    CredOrdCount:z,
    RubCred:z, 
    DebOrdCount:z, 
    RubDeb:z
  );

end;
/******************************************************************************/

macro addParamForNewMon( CountIn, SumIn, CountOut, SumOut)

/*  newMonCountIn  = newMonCountIn  + CountIn;*/
  newMonCountIn  = 1;
  newMonSumIn    = newMonSumIn    + SumIn;
/*  newMonCountOut = newMonCountOut + CountOut;*/
  newMonCountOut = 1;
  newMonSumOut   = newMonSumOut   + SumOut;

  needPrintNewMon = true;

end;
/******************************************************************************/

macro printParamForNewMon()

  if( needPrintNewMon )
    PrintLine( accountForNewMon, 0, 0, 0, 0 );
    PrintLine( "Монеты новых образцов", newMonCountIn, newMonSumIn, newMonCountOut, newMonSumOut );
  end;

end;
/******************************************************************************/

macro printParamForDragMet()

  if( needPrintDragMet )
    PrintLine( accountForDragMet, 0, 0, 0, 0 );
    PrintLine( "Драгметаллы в слитках", DragMetCountIn, DragMetSumIn, DragMetCountOut, DragMetSumOut );
  end;

end;
/******************************************************************************/

macro addParamForOldMon( CountIn, SumIn, CountOut, SumOut)

/*  oldMonCountIn  = oldMonCountIn  + CountIn;*/
  oldMonCountIn  = 1;
  oldMonSumIn    = oldMonSumIn    + SumIn;
/*  oldMonCountOut = oldMonCountOut + CountOut;*/
  oldMonCountOut = 1;
  oldMonSumOut   = oldMonSumOut   + SumOut;

  needPrintOldMoney = true;

end;
/******************************************************************************/

macro printParamForOldMon()

  if( needPrintOldMoney )
    PrintLine( accountForOldMon, 0, 0, 0, 0 );
    PrintLine( "Монеты старых образцов", oldMonCountIn, oldMonSumIn, oldMonCountOut, oldMonSumOut );
  end;

end;
/***************************************************************************/
macro addParamForCurrency(CurrCode, CIn, CInSum, CInSumRub, COut, COutSum, COutSumRub )
  var
    i = 0;

  while ( ( i < ASize( CurrArr ) ) and ( CurrArr( i ) != CurrCode ) )
    i = i + 1;
  end; 
        
  if ( i == ASize( CurrArr ) )
    CurrArr( i )     = CurrCode; 
    CountInArr( i )  = CIn;
    SumInArr( i )    = CInSum;
    SumInArrRub( i ) = CInSumRub;
    CountOutArr( i ) = COut;
    SumOutArr( i )   = COutSum;
    SumOutArrRub( i ) = COutSumRub;
  else
    CountInArr( i )  = CountInArr( i ) + CIn;
    SumInArr( i )    = SumInArr( i ) + CInSum;
    SumInArrRub( i ) = SumInArrRub( i ) + CInSumRub;
    CountOutArr( i ) = CountOutArr( i ) + COut;
    SumOutArr( i )   = SumOutArr( i ) + COutSum;
    SumOutArrRub( i ) = SumOutArrRub( i ) + COutSumRub;
  end;

  needPrintCurrency = true;

end;
/******************************************************************************/

macro addParamForDragMet( CountIn, SumIn, CountOut, SumOut)

  DragMetCountIn  = DragMetCountIn  + CountIn;
  DragMetSumIn    = DragMetSumIn    + SumIn;
  DragMetCountOut = DragMetCountOut + CountOut;
  DragMetSumOut   = DragMetSumOut   + SumOut;

  needPrintDragMet = true;

end;
/******************************************************************************/

macro addParamForPD(CurrCode,SumIn, SumInRub, CountIn, SumOut, SumOutRub, CountOut)
  var
    i = 0;

  while ( ( i < ASize( SpecCurrCode ) ) and ( SpecCurrCode( i ) != CurrCode ) )
    i = i + 1;
  end; 

  if ( i == ASize( SpecCurrCode ) )
    SpecCurrCode( i ) = CurrCode; 
    SpecSumIn( i )  = SumIn;
    SpecSumInRub( i )  = SumInRub;
    SpecCountIn( i ) = CountIn;
    SpecSumOut( i ) = SumOut;
    SpecSumOutRub( i ) = SumOutRub;
    SpecCountOut( i ) = CountOut;
  else
    SpecSumIn( i )  = SpecSumIn( i ) + SumIn;
    SpecSumInRub( i )  = SpecSumInRub( i ) + SumInRub;
    SpecCountIn( i ) = SpecCountIn( i ) + CountIn;
    SpecSumOut( i ) = SpecSumOut( i ) + SumOut;
    SpecSumOutRub( i ) = SpecSumOutRub( i ) + SumOutRub;
    SpecCountOut( i ) = SpecCountOut( i ) + CountOut;
  end;

  needPrintPD = true;

end;
/***************************************************************************/
macro addParamForBadCurrency(CurrCode, CIn, CInSum, CInSumRub, COut, COutSum, COutSumRub )
  var
    i = 0;
  while ( ( i < ASize( BadCurrArr ) ) and ( BadCurrArr( i ) != CurrCode ) )
    i = i + 1;
  end; 
        
  if ( i == ASize( BadCurrArr ) )
    BadCurrArr( i )     = CurrCode; 
    BadCountInArr( i )  = CIn;
    BadSumInArr( i )    = CInSum;
    BadSumInArrRub( i ) = CInSumRub;
    BadCountOutArr( i ) = COut;
    BadSumOutArr( i )   = COutSum;
    BadSumOutArrRub( i ) = COutSumRub;
  else
    BadCountInArr( i )  = BadCountInArr( i ) + CIn;
    BadSumInArr( i )    = BadSumInArr( i ) + CInSum;
    BadSumInArrRub( i ) = BadSumInArrRub( i ) + CInSumRub;
    BadCountOutArr( i ) = BadCountOutArr( i ) + COut;
    BadSumOutArr( i )   = BadSumOutArr( i ) + COutSum;
    BadSumOutArrRub( i ) = BadSumOutArrRub( i ) + COutSumRub;
  end;

  needPrintBadCurrency = true;

end;

/******************************************************************************/

  private macro FindSpec(CurrCode)
    var
      i = 0;

    while ( ( i < ASize( SpecCurrCode ) ) and ( SpecCurrCode( i ) != CurrCode ) )
      i = i + 1;
    end; 

    if ( i == ASize( SpecCurrCode ) )
      i = -1;
    end;

    return i;  
  end;
/******************************************************************************/
  private macro GetCurrName(CurrCode)
  var CurrName = "";

    FindCurr(CurrCode);
  /*Работает флаг печати кода валюты (описан в начале макроса)*/
    if (flagCurrCode == 0)
      CurrName = Currency.rec.Short_Name;
    elif (flagCurrCode == 1)
      CurrName = Currency.rec.Short_Name+"/"+Currency.rec.ExternalCode;
    elif (flagCurrCode == 2)
      CurrName = Currency.rec.Code_Currency;
    elif (flagCurrCode == 3)
      CurrName = Currency.rec.Name_Currency;
    end;

    return CurrName;
  end;

  private macro GetNameForPD( RefValue )
    var Name = "",Name1 = "",
        saveFlag = flagCurrCode;

    sb_casvl.rec.RefValue = RefValue;
    if( sb_casvl.GetEQ() )
      Name1 = sb_casvl.rec.NameValue;

      paydoc.rec.ID = sb_casvl.rec.CodIntValue;
      if( paydoc.GetEQ() )
        flagCurrCode = 0;
        Name = GetCurrName( paydoc.rec.Curr_Ref ) + ", " + Name1;
      else
        Name = Name1;
      end;

    end;

    flagCurrCode = saveFlag;

    return Name;

  end;
/******************************************************************************/
macro printParamForCurrency()
  var i = 0;
  var StrSumInR  = money(0);
  var StrSumOutR = money(0);
  var CurrName    = "";

  if( needPrintCurrency )
    PrintLine( accountForCurrency, 0, 0, 0, 0 );

  /* Печать рублей */
    while( ( i < asize(CurrArr) ) and (CurrArr(i) != 0 ) )
      i = i + 1;
    end;
    if( i != asize(CurrArr) )
      if ((CountInArr( i ) > 0) OR (SumInArr( i ) > 0) OR (CountOutArr( i ) > 0) OR (SumOutArr( i ) > 0))
        CurrName = GetCurrName(CurrArr(i));
        PrintLine( CurrName, CountInArr( i ), SumInArr( i ), CountOutArr( i ), SumOutArr( i ) );
        CountInArr( i ) = 0;
        SumInArr( i ) = 0;
        CountOutArr( i ) = 0;
        SumOutArr( i ) = 0;
      end;
    end;

    i = 0;
    while( i < asize(CurrArr) )

      if ((CountInArr( i ) > 0) OR (SumInArr( i ) > 0) OR (CountOutArr( i ) > 0) OR (SumOutArr( i ) > 0))

     /*Работает флаг печати кода валюты (описан в начале макроса)*/
        CurrName = GetCurrName(CurrArr(i));
        PrintLine( CurrName, CountInArr( i ), SumInArr( i ), CountOutArr( i ), SumOutArr( i ) );

      /*Работает флаг печати рублевого эквивалента (описан в начале макроса)*/
        if ( flagRubEQ )
          /* Формирование сумм эквивалента */
          StrSumInR  = SumInArrRub( i );
          StrSumOutR = SumOutArrRub( i );
          PrintLine( "Рублевый эквивалент:", 0, StrSumInR, 0, StrSumOutR );
        end;
      end;
      i = i + 1;
    end;

  end;
 
end;

/******************************************************************************/
macro printParamForBadCurrency()
  var i = 0;
  var StrSumInR  = money(0);
  var StrSumOutR = money(0);
  var CurrName    = "";

  if( needPrintBadCurrency )
    PrintLine( accountForBadCurrency, 0, 0, 0, 0 );

  /* Печать рублей */
    while( ( i < asize(BadCurrArr) ) and (BadCurrArr(i) != 0 ) )
      i = i + 1;
    end;
    if( i != asize(BadCurrArr) )
      if ((BadCountInArr( i ) > 0) OR (BadSumInArr( i ) > 0) OR (BadCountOutArr( i ) > 0) OR (BadSumOutArr( i ) > 0))
        CurrName = GetCurrName(BadCurrArr(i));
        PrintLine( CurrName, BadCountInArr( i ), BadSumInArr( i ), BadCountOutArr( i ), BadSumOutArr( i ) );
        BadCountInArr( i ) = 0;
        BadSumInArr( i ) = 0;
        BadCountOutArr( i ) = 0;
        BadSumOutArr( i ) = 0;
      end;
    end;

    i = 0;
    while( i < asize(BadCurrArr) )

      if ((BadCountInArr( i ) > 0) OR (BadSumInArr( i ) > 0) OR (BadCountOutArr( i ) > 0) OR (BadSumOutArr( i ) > 0))

     /*Работает флаг печати кода валюты (описан в начале макроса)*/
        CurrName = GetCurrName(BadCurrArr(i));
        PrintLine( CurrName, BadCountInArr( i ), BadSumInArr( i ), BadCountOutArr( i ), BadSumOutArr( i ) );

      /*Работает флаг печати рублевого эквивалента (описан в начале макроса)*/
        if ( flagRubEQ )
          /* Формирование сумм эквивалента */
          StrSumInR  = BadSumInArrRub( i );
          StrSumOutR = BadSumOutArrRub( i );
          PrintLine( "Рублевый эквивалент:", 0, StrSumInR, 0, StrSumOutR );
        end;
      end;
      i = i + 1;
    end;

  end;
 
end;

/******************************************************************************/
macro printParamForPD()
  var i = 0;
  var StrSumInR  = money(0);
  var StrSumOutR = money(0);
  var CurrName    = "";

  if( needPrintPD )
    PrintLine( accountForPD, 0, 0, 0, 0 );

    i = 0;
    while( i < asize(SpecCurrCode) )
      if( SpecCurrCode(i) != NULL )
        CurrName = GetNameForPD(SpecCurrCode(i));
        PrintLine( CurrName, SpecCountIn( i ), SpecSumIn( i ), SpecCountOut( i ), SpecSumOut( i ) );
      /*Работает флаг печати рублевого эквивалента (описан в начале макроса)*/
        if ( flagRubEQ )
          /* Формирование сумм эквивалента */
          StrSumInR  = SpecSumInRub( i );
          StrSumOutR  = SpecSumOutRub( i );
          PrintLine( "Рублевый эквивалент:", 0, StrSumInR, 0, StrSumOutR );
        end;
      end;
      i = i + 1;
    end;
  end;

end;
/***************************************************************************
                         Вывести информацию в отчет
***************************************************************************/
macro PrintReport ( dontprint )

  printParamForCurrency();
  printParamForPD();
  printParamForDragMet();
  printParamForNewMon();
  if ( dontprint == false )
	printParamForBadCurrency();
  end;
  printParamForOldMon();

end;
/******************************************************************************/
/*                Тут у нас как бы конструктор				      */

  CountInArr(0)  = 0;
  SumInArr(0)    = 0;
  CountOutArr(0) = 0;
  SumOutArr(0)   = 0;

end;