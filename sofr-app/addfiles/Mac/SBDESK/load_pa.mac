/* Коммунальные платежи */
/* Загрузка списка лицевых счетов по коммунальным платежам */
import rsd;

private var {curdate};

private const STATUS_INSERTED = "Добавлен";
private const STATUS_UPDATED  = "Обновлен";
private const STATUS_MISSED   = "Пропущен";
private const ERRSTATUS_INSERTED = "Ошибка при добав.";
private const ERRSTATUS_UPDATED  = "Ошибка при обнов.";

private record COMPAY_RECIPIENT( rtcp_recipient );
private record COMPAY_TYPE( rtcp_type );
private record COMPAY_LINK( rtcp_link );
/* Панель для задания параметров загрузки */
private record CP_ALOAD(cp_aload) dialog;

private file cp_account(rtcp_account) key 1 write;
private record cp_account_rec(rtcp_account);
private file InputFile() txt;
private var RecordsCount = 0;

/* Инициализация полей панели */
macro InitPanelFields
    var cmd, rs;
    var recipientId, typeId;
    var typeCodes = "", tCode = "", tName = "", i;

    CP_ALOAD.RecipientName = COMPAY_RECIPIENT.Name;
    
    /* Определение названия вида услуги */
    if ( COMPAY_LINK.TypeId )
        CP_ALOAD.TypeName = COMPAY_TYPE.Name;
    else
        cmd = RsdCommand( 
            "SELECT cptype.T_CODE, cptype.T_NAME" + 
            " FROM DRTCP_TYPE_DBT cptype, DRTCP_LINK_DBT cplink WHERE" +
            " cptype.T_ID = cplink.T_TYPEID AND" +
            " cplink.T_RECIPIENTID = ?" );
        cmd.addParam("id", RsdBp_in, COMPAY_RECIPIENT.Id);
        cmd.execute;
        rs = RsdRecordset(cmd);

        i = 0;
        while ( rs.moveNext )
            i = i + 1;
            tCode = rs.value("T_CODE");
            tName = rs.value("T_NAME");
            if ( strlen(typeCodes) + strlen(tCode) + 2 < 41 )
                if ( strlen(typeCodes) != 0 )
                    typeCodes = typeCodes + ", ";
                end;
                typeCodes = typeCodes + tCode;
            else
                break;
            end;
        end;
        /* Если к получателю привязано несколько услуг, то в поле указываются коды всех связанных услуг */
        if ( i == 1 )
            CP_ALOAD.TypeName = tName;
        else
            CP_ALOAD.TypeName = typeCodes;
        end;
    end;
end;

/* Обработчик клавиш для панели */
macro KeysHandler( IP, cmd, fieldNum, key )
    var stat = CM_DEFAULT;

    if ( cmd == DLG_KEY )
        
        if ( key == 32 )        /* SPACE */
            if ( fieldNum == 1 )
                if ( CP_ALOAD.NeedUpdating == "X")
                    CP_ALOAD.NeedUpdating = "";
                else
                    CP_ALOAD.NeedUpdating = "X";
                end;
            end;
        
        elif ( key == 317 )     /* F3 */
            if ( fieldNum == 0 )
                SelectFile( CP_ALOAD.FileName, "", "Выбор файла для загрузки" );
            end;

        elif ( key == 316 )     /* F2 */
            if ( CP_ALOAD.FileName == "" )
                msgbox("Не задан файл для импорта данных");
            else
                stat = CM_SAVE;
            end;

        elif ( key == 27 )      /* ESC */
            stat = CM_CANCEL;
        end;
    end;

    return stat;
end;

/* Заголовок отчета о загрузке */
macro BeginReport
    var NeedUpdating;
    if ( CP_ALOAD.NeedUpdating == "X" )
        NeedUpdating = "с обновлением данных";
    else
        NeedUpdating = "без обновления данных";
    end;
[ ];
[                                Процедура загрузки лицевых счетов];
[                                   <##########> <########>]( Date({curdate}), Time() );
[ ];
[                                    Получатель #]( CP_ALOAD.RecipientName );
[                                   (#####################)](NeedUpdating);
[ ];
[ ┌──────┬────────────────────┬───────────────────────────────────────────────────┬─────────────────┐];
[ │   №  │         ЛС         │                       ФИО                         │      Статус     │];
[ ├──────┼────────────────────┼───────────────────────────────────────────────────┼─────────────────┤];
end;

/* Запись загружаемых данных в отчет */
macro WriteReport( Status )
    var Name = cp_account_rec.Name1 + " " + cp_account_rec.Name2 + " " + cp_account_rec.Name3;
    RecordsCount = RecordsCount + 1;
[ │######│####################│###################################################│#################│] (
    RecordsCount:r,
    cp_account_rec.Account:r,
    Name:w,
    Status );
end;

/* Подвал отчета о загрузке */
macro EndReport
[ └──────┴────────────────────┴───────────────────────────────────────────────────┴─────────────────┘];
[ ];
end;

/* Загрузка 1 лицевого счета:
   - если ЛС не существует, то его добавление
   - если ЛС существует и в панели установлен флаг <Обновлять> - обновление данных
   - если ЛС существует и в панели НЕ установлен флаг <Обновлять> - пропуск строки */
macro LoadOneAccount( Status )
    ClearRecord(cp_account);
    cp_account.LinkId = COMPAY_LINK.Id;
    cp_account.Account = cp_account_rec.Account;

    if ( GetEQ(cp_account) )
        if ( CP_ALOAD.NeedUpdating == "X") 
            cp_account_rec.Id = cp_account.Id;
            cp_account_rec.LinkId = cp_account.LinkId;
            Copy( cp_account, cp_account_rec );
            if( not Update(cp_account) )
                return ERRSTATUS_UPDATED;
            end;
            return STATUS_UPDATED;
        end;
        return STATUS_MISSED;
    end;

    Copy( cp_account, cp_account_rec );
    cp_account.Id = 0;
    cp_account.LinkId = COMPAY_LINK.Id;
    if ( not Insert(cp_account) )
        return ERRSTATUS_UPDATED;
    end;
    return STATUS_INSERTED;
end;

/* Разделение строки из текстового файла на части по символу ";" */
macro GetNextStr( text, beginIndex )
    var str = substr( text, beginIndex );
    var i = index( str, ";" );
    SetParm( 1, beginIndex + i );
    if ( i > 0 )
        return trim( substr( str, 1, i - 1 ) );
    end;
    return trim( str );
end;

/* Обработка текстового файла для загрузки счетов */
macro ProcessFile
    var i;
    while ( Next(InputFile) )
        i = 1;
        ClearRecord(cp_account_rec);
        cp_account_rec.Account    = GetNextStr( InputFile.str, i );
        cp_account_rec.Name1      = GetNextStr( InputFile.str, i );
        cp_account_rec.Name2      = GetNextStr( InputFile.str, i );
        cp_account_rec.Name3      = GetNextStr( InputFile.str, i );
        cp_account_rec.BirthDate  = Date(GetNextStr( InputFile.str, i ));
        cp_account_rec.Address    = GetNextStr( InputFile.str, i );
        cp_account_rec.Sum        = Money(GetNextStr( InputFile.str, i ));
        cp_account_rec.Penny      = Money(GetNextStr( InputFile.str, i ));
        cp_account_rec.Prepayment = Money(GetNextStr( InputFile.str, i ));
        WriteReport( LoadOneAccount );
    end;
end;

/* Загрузка списка лицевых счетов по коммунальным платежам */
macro LoadCPAcc
    InitPanelFields;
    /* Вывод панели для задания параметров загрузки */
    var ok = RunDialog( CP_ALOAD, "KeysHandler" );
    if ( ok )
        /* Открытие текстового файла */
        ok = Open( InputFile, CP_ALOAD.FileName );
        if ( not ok )
            msgbox("Не открыт файл загрузки");
        end;
    end;
    /* Загрузка счетов с выводом данных в отчет */
    if ( ok )
        Message(" Загрузка лицевых счетов ...");
        BeginReport;
        ProcessFile;
        EndReport;
    end;
    
    if ( not ok )
        exit(1);
    end;
end;

END;