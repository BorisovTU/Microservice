
/* Общая библиотека для отчетов по Кассе пересчета */

import deprintr, BankInter, Cur_Rate, globals;

file CashValue  ( "sb_casvl.dbt" ) key 0;
file Curr       ( "currency.dbt" ) key 0;
file CashDocList( "sb_casdc.dbt" ) key 7;
file PantryDoc  ( "pant_doc.dbt" ) key 1;
file BagDoc     ( "bag_doc.dbt" )  key 0 write;
file Person     ( "person.dbt", "bank.def" )   key 0;
file BagNum     ( "bagnum.dbt" )   key 0;
file Collector  ( "collectr.dbt" ) key 0;

record CashDoc  ( "sb_casdc.dbt" );
record Bag      ( "bag.dbt" );

const
  CASHOP_COLLECT   =  3,     /* Инкассация */
  CASHOP_ADVANCE   =  4,     /* Аванс */
  CASHOP_PANT_RDEB = 30,     /* Расход по кассе пересчета */
  CASHOP_PANT_RCRD = 31,     /* Приход по кассе пересчета */ 
  CASHOP_PANT_ECRD = 41;     /* Приход по вечерней кассе */ 

const
  CL_DEP   = 0,       /* Отделение ( филиал, о/ч ) СБ */
  CL_BANK  = 1,       /* Банк */
  CL_LEGAL = 2;       /* Юр. лицо */

const
  TYPERES_CURRENCY    =   1, /* Деньги (из справочника валют) */
  TYPERES_VALFORM     =   2, /* Ценные бланки */
  TYPERES_VALUABLES   =   3, /* Др. ценности (из справочника ценностей) */
  TYPERES_PAYDOC      =   4, /* Платежные документы ОП */
  TYPERES_CARD        =  10, /* Пластиковые карточки */
  TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ */

const
  TYPE_RES_PAY    = 0, /* Платежеспособные ресурсы                     */
  TYPE_RES_NOPAY  = 1, /* Неплатежеспособные ресурсы                   */
  TYPE_RES_DOUBT  = 2, /* Сомнительные ресурсы                         */
  TYPE_RES_SAVE   = 3, /* На хранении                                  */
  TYPE_RES_EXP    = 4, /* На экспертизу                                */
  TYPE_RES_INCASS = 5, /* На инкассо                                   */
  TYPE_RES_PDNAT  = 20,/* ПД, оплаченные рублями(национальной валютой) */
  TYPE_RES_PDCUR  = 21,/* ПД, оплаченные валютой                       */
  TYPE_RES_PD     = 22;/* оплаченные ПД                                */

const
  RESMESUNIT_POINT = 1,      /* Поштучно */
  RESMESUNIT_MONEY = 2,      /* Стоимость */
  RESMESUNIT_MIXED = 3;

const
  SB_DEPOSIT     =    1,     /* Документ вкладной операции */
  SB_PSDEPOS     =    5,     /* Отложенный документ */
  SB_COMPAY      =   20,     /* Коммунальные платежи */
  SB_SVDOC       =   50,     /* Сводный документ кредитного бухгалтера */
  SB_INCASH      =  100,     /* Документ порожден Кассой */
  SB_EXTCASH     =  150,     /* Внешняя кассовая операция, не отр. в др. подсист. */
  SB_RDESK       =  180,     /* Касса пересчета */
  SB_PANTRY      =  190,     /* Кладовая отделения */
  SB_CASHOPERT   =  200,     /* Документ для сервисно-кассовых операций */
  SB_EXCHANGE    =  250,     /* Реестр по обменному пункту */
  SB_CAPISSUE    =  300,     /* Ценные бумаги */
  SB_TRN         =  400,     /* Безналичные платежи */
  SB_NOTBALANCE  = 1000;     /* Небалансовая ценность */

const
  CO_RDesk   = 200;      /* Касса пересчета */

const
  PT_OPER        = 1,
  PT_CONTR       = 2; 

const
  BPL_RDESK  = 0,
  BPL_PANTRY = 1,
  BPL_EDESK  = 2;

private const
  BST_SENT     = 9,
  BST_RETURNED = 10;

var
  {PantryAutoMode},
  Branch = NumFNCash(),
  BankStandart = GetBankStandart( ), 
  CurrCode,
  CBRate,
  BuyRate,
  SellRate,  
  ScaleRate = 1;

macro FindCashValue( RefValue )

  CashValue.RefValue = RefValue;
  return GetEQ( CashValue );
end;

macro FindCashValForTypeCode( Type, ValType, ValCode )

  var
    Stat,
    SaveKey = KeyNum( CashValue, 1 );

  CashValue.Type = Type;
  CashValue.TypeValue = ValType;
  CashValue.CodIntValue = ValCode;
  Stat = GetEQ( CashValue );
  KeyNum( CashValue, SaveKey );
  return Stat;
end;


macro FindCurr( CurrCode )

  Curr.Code_Currency = CurrCode;
  return GetEQ( Curr );
end;


macro FindCashDoc( ValueRef, ApplicationKind, ApplicationKey )

  CashDocList.RefValue = ValueRef;
  CashDocList.ApplicationKind = ApplicationKind;
  CashDocList.ApplicationKey = ApplicationKey;
  return GetEQ( CashDocList );
end;


macro GetBagDoc

  BagDoc.BagAppKind = Bag.ApplicationKind;
  BagDoc.BagAppKey = Bag.ApplicationKey;
  BagDoc.State = Bag.State;
  return GetEQ( BagDoc );
end;


macro GetPantryDoc( ValueRef )

  file Link( "sb_casln.dbt" );
  record Tmp( "sb_casln.dbt" );

  var
    RecFound;

  if ( ValueRef == null )
    ValueRef = Bag.ValueRef;
  end;

  if ( not GetBagDoc )
    return false;
  end;
  
  KeyNum( Link, 1 );
  Link.ApplicationKind2 = BagDoc.ApplicationKind;
  Link.ApplicationKey2 = BagDoc.ApplicationKey;
  Link.RefValue2 = 0;
  if ( GetEQ( Link ) )
    Copy( Tmp, Link );
    KeyNum( Link, 0 );
    RecFound = GetGE( Link );
    while ( RecFound
      and ( Link.ApplicationKind1 == Tmp.ApplicationKind1 )
      and ( Link.ApplicationKey1 == Tmp.ApplicationKey1 ) )
      if ( Link.ApplicationKind2 == SB_PANTRY )
        PantryDoc.ApplicationKind = Link.ApplicationKind2;
        PantryDoc.ApplicationKey = Link.ApplicationKey2;
        if ( GetEQ( PantryDoc ) and ( PantryDoc.ValueRef == ValueRef ) )
          return true;
        end;
      end;
      RecFound = Next( Link );
    end;
  end;
  return false;
end;


macro GetCBRate

  CurrCode = Curr.Code_Currency;

  if (Curr.ScaleOfc != 0)
    ScaleRate = Curr.ScaleOfc;
  end;

  GetAllCurrRate( {curdate}, CurrCode, CBRate, BuyRate, SellRate);
  if ( CBRate == 0 )
    MsgBox( "Не определен курс валюты '" + Curr.Name_Currency +"'" );
    Exit( 1 );
  end;
end;


macro GetCurrCodeAndCBRate( ValueRef )

  ScaleRate = 1;

  CBRate   = Double(0);
  BuyRate  = Double(0);
  SellRate = Double(0);

  if ( not FindCashValue( ValueRef ) )
    MsgBox( "Не найден ресурс кассы с кодом " + ValueRef );
    Exit( 1 );
  end;

  if ( CashValue.RefValueReport == 1 )
    CurrCode = 1;
  else
    CurrCode = 0;
  end;     

  if ( CashValue.GroupValue )
    GetPos( CashValue );
    if ( FindCashValue( CashValue.GroupValue ) )
      if ( CashValue.TypeValue == TYPERES_CURRENCY )
        CurrCode = CashValue.CodIntValue;
      end;
    end;
    GetDirect( CashValue );
  end;

  if ( not FindCurr( CurrCode ) )
    MsgBox( "Не найдена валюта с кодом " + CurrCode );
    Exit( 1 );
  end;

  GetCBRate;
end;

macro GetAccounts( DebetAcc, CreditAcc, CashSym )

  DebetAcc = CreditAcc = "";

  SetParm(0, DebetAcc );
  SetParm(1, CreditAcc );
  SetParm(2, CashSym );
end;


macro GetNBalAccounts( DebetAcc, CreditAcc )

  DebetAcc = CreditAcc = "";

  SetParm(0, DebetAcc );
  SetParm(1, CreditAcc );
end;


macro GetPerson

  Person.Oper = {oper};
  return GetEQ( Person );
end;


macro FindPerson( Oper )

  Person.Oper = Oper;
  return GetEQ( Person );
end;


macro CanIssueOrders

  Person.Oper = {oper};
  if ( not GetEQ( Person ) )
    MsgBox( "Пользователь не найден: ", {oper} );
    Exit( -1 );
  end;
  return ( Bag.Operation == CASHOP_PANT_RDEB ) 
     or  ( ( Person.cTypePerson != "У" )    /* Ревизор */
       and ( Person.cTypePerson != "Б" )    /* Главбух */
       and ( Person.cTypePerson != "А" ) ); /* Администратор */

end;

macro Cur2Rub(Sum)

  if ( ( ValType( CurrCode ) != V_UNDEF ) and CurrCode )
    return SumConv(Sum, CBRate/ScaleRate);
  else
    return Sum;
  end;
end;

macro FindBagNum( AutoKey )

  BagNum.AutoKey = AutoKey;
  return GetEQ( BagNum );
end;

/*
macro IntToStr( n )

  var
    Str;

  RubToStr( Money( n * 100 ), Str );  
  return Str;
end;
*/

macro IntToStr( n )

  array 
    N0, N1, N2, N11, Tr, Tr1, Tr2, Words;

  var
    NStr = String( n ), NWords = 0, NArr, i, CurDigit, CurWord, Str = "", Thousands = 0;

  if ( n > 999999 )
    return "превесьма немало";
  end;

  N0( 0 ) = "";           N1( 0 ) = "";               N2( 0 ) = "";             N11( 0 ) = "";      
  N0( 1 ) = "одна";       N1( 1 ) = "десять";         N2( 1 ) = "сто";          N11( 1 ) = "одиннадцать";
  N0( 2 ) = "две";        N1( 2 ) = "двадцать";       N2( 2 ) = "двести";       N11( 2 ) = "двенадцать";
  N0( 3 ) = "три";        N1( 3 ) = "тридцать";       N2( 3 ) = "триста";       N11( 3 ) = "тринадцать";
  N0( 4 ) = "четыре";     N1( 4 ) = "сорок";          N2( 4 ) = "четыреста";    N11( 4 ) = "четырнадцать";
  N0( 5 ) = "пять";       N1( 5 ) = "пятьдесят";      N2( 5 ) = "пятьсот";      N11( 5 ) = "пятнадцать";
  N0( 6 ) = "шесть";      N1( 6 ) = "шестьдесят";     N2( 6 ) = "шестьсот";     N11( 6 ) = "шестнадцать";
  N0( 7 ) = "семь";       N1( 7 ) = "семьдесят";      N2( 7 ) = "семьсот";      N11( 7 ) = "семнадцать";
  N0( 8 ) = "восемь";     N1( 8 ) = "восемьдесят";    N2( 8 ) = "восемьсот";    N11( 8 ) = "восемнадцать";
  N0( 9 ) = "девять";     N1( 9 ) = "девяносто";      N2( 9 ) = "девятьсот";    N11( 9 ) = "девятнадцать";

  i = StrLen( NStr );
  NArr = 0;
  while ( i > 0 )
    CurDigit = Int( SubStr( NStr, i, 1 ) );
    if ( NArr == 0 )
      if ( Thousands )
        if ( CurDigit == 1 )
          CurWord = "тысяча";
        elif ( ( CurDigit >= 2 ) and ( CurDigit <= 4 ) )
          CurWord = "тысячи";
        else
          CurWord = "тысяч";
        end;
        Words( NWords ) = CurWord;
        NWords = NWords + 1;
      end;
    end;
    if ( ( NArr == 0 ) and ( CurDigit != 0 ) and ( i != 1 ) and ( Int( SubStr( NStr, i - 1, 1 ) ) == 1 ) )
      NArr = 11;
    end;
    if ( CurDigit )
      if ( NArr == 0 )
        CurWord = N0( CurDigit );
        NArr = 1; 
      elif ( NArr == 1 )
        CurWord = N1( CurDigit );
        NArr = 2; 
      elif ( NArr == 2 )
        CurWord = N2( CurDigit );
        NArr = 0; 
        Thousands = Thousands + 1;
      elif ( NArr == 11 )
        CurWord = N11( CurDigit );
        i = i - 1;
        NArr = 2; 
      end;
      Words( NWords ) = CurWord;
      NWords = NWords + 1;
    end; 
    i = i - 1;
  end;

  if ( NWords )
    while ( NWords > 0 )
      NWords = NWords - 1;
      Str = Str + Words( NWords );
      if ( NWords )
        Str = Str + 1;
      end;
   end;
  else
    Str = "ноль";
  end;

  return Str;
end;


macro BranchName

  var dep_name;
  
  if ( not findDepartment(Branch, dep_name) )
    dep_name = "";
  end;
  return dep_name;
end;


macro CheckPersonType( Type )

  if ( {PantryAutoMode} or ( GetProgramID != CodeFor( "Л" ) ) )
    return;
  elif ( ( Bag.State == BST_SENT ) or ( Bag.State == BST_RETURNED ) )
    if ( Bag.RouteID == 0 )
      return;
    end;
  end;

  GetPerson;
  if ( ( Type == PT_OPER ) and ( Person.cTypePerson != "О" ) )
/*    MsgBox( "Вы не имеете права выпускать приходные ордера" ); */
    Exit( 1 );
  elif ( ( Type == PT_CONTR ) and ( Person.cTypePerson == "О" ) )
/*    MsgBox( "Вы не имеете права выпускать приходные ордера" ); */
    Exit( 1 );
  end;
end;

   
macro GetCity

  var
   RegPath = "RS-RETAIL\\СЕРВИСНЫЕ\\ВКЛАДЫ\\ПАРАМЕТРЫ_ДОГОВОРОВ\\НАЗВ_ГОРОДА",
   Type = V_STRING,
   Value,
   ErrCode;

  if(GetRegistryValue(RegPath, Type, Value, ErrCode) == V_STRING)
    return Value;
  else
    return "???";
  end;
end;


macro GetRegSetting( Type, RelPath )

  var
    RegPath = "RS-RETAIL\\СЕРВИСНЫЕ\\" + RelPath,
    Value;

  if ( GetRegistryValue( RegPath, Type, Value, null ) == Type )
    return Value;
  end;
  MsgBox( "Не найдена настройка АБС ", RegPath );
  Exit( 1 ); 
end;


private macro MakeFio( surname, name, patr )

 var
   fio;

  surname = trim( surname );
  name = trim( name );
  patr = trim( patr );

  fio = surname;
  if ( name != "" )
    fio = fio + " " + subStr( name, 1, 1 ) + ".";
  end;
  if ( patr != "" )
    if ( name != "" )
      fio = fio + " ";
    end;
    fio = fio + subStr( patr, 1, 1 ) + ".";
  end;

  return fio;
end;


macro GetCollectorName( Branch, ID )

  Collector.Branch = Branch;
  Collector.ID = ID;
  if ( GetEQ( Collector ) )
    return MakeFIO( Collector.LastName, Collector.FirstName, Collector.MiddleName );
  end;
  return "";
end;


macro GetSealNo

  var 
    n = "";

  GetString( n, "Введите номер пломбира, которым опечатана сумка", 20 );
  return n;
end;
