
/*
**  Ведомость остатков и оборотов 
**
*/

import CommonInter, Revalue, Acc_Form;

var CashValue = TBFile( "sb_casvl.dbt", "r", 5 );
var CIMisc    = TBFile( "ci_misc.dbt",  "r", 0 );
var Person    = TBFile( "person.dbt",  "bank.def", "r", 0 );
var Alg       = TBFile( "namealg.dbt", "bank.def", "r", 0 );

const
  CI_OGSZ          = 1,
  CI_LOTTERY       = 2,
  CI_CERT          = 3,
  CI_COIN          = 4,
  CI_INGOT         = 5,
  CI_ORVVZ         = 6,
  CI_MISC          = 10;

const
  SB_PANTRY       = 190;

const
  CK_NEW          = 0, 
  CK_OLD          = 1;

const
  TYPE_RES_PAY    = 0, /* Платежеспособные ресурсы                     */
  TYPE_RES_NOPAY  = 1, /* Неплатежеспособные ресурсы                   */
  TYPE_RES_DOUBT  = 2, /* Сомнительные ресурсы                         */
  TYPE_RES_SAVE   = 3, /* На хранении                                  */
  TYPE_RES_EXP    = 4, /* На экспертизу                                */
  TYPE_RES_INCASS = 5, /* На инкассо                                   */
  TYPE_RES_PDNAT  = 20,/* ПД, оплаченные рублями(национальной валютой) */
  TYPE_RES_PDCUR  = 21,/* ПД, оплаченные валютой                       */
  TYPE_RES_PD     = 22;/* оплаченные ПД                                */

const
  TYPERES_CURRENCY    =   1, /* Деньги (из справочника валют) */
  TYPERES_VALFORM     =   2, /* Ценные бланки */
  TYPERES_VALUABLES   =   3, /* Др. ценности (из справочника ценностей) */
  TYPERES_PAYDOC      =   4, /* Платежные документы ОП */
  TYPERES_CARD        =  10, /* Пластиковые карточки */
  TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ */

const
  ChapterNB = 3;

var
  {oper},
  {curdate},
  HeadPrinted = false, 
  TotPrevRest = $0, TotRest = $0, TotDebet = $0, TotCredit = $0,
  TotDiffDebet = $0, TotDiffKred = $0;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindCIMisc ( Type, IssueID )

  CIMisc.rec.Type = Type;
  CIMisc.rec.IssueID = IssueID;

  return CIMisc.GetEQ( );

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetAlgName ( type, number )

  Alg.rec.iTypeAlg = type;
  Alg.rec.iNumberAlg = number;
  if ( Alg.GetEQ( ) )
    return Alg.rec.szNameAlg;
  end;

  return "";

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetValState

  if ( CashValue.rec.Type == 0 )
    return "";
  end;

  return " (" + GetAlgName( 811, CashValue.rec.Type ) + ")";

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro FindPerson

  Person.Clear( );
  Person.rec.Oper = {oper};
  if ( not Person.GetEQ( ) )
    Person.rec.Name = "<не найден>";
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintHead

  [                                         Ведомость остатков и оборотов                                            ];
  [ ################################################################################################################ ]
  ( ( "за " + String( {curdate}:m ) ):c );
  [ ];
  [------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
  [|Номер счета                |    Наименование ценности     |    Остаток на    |      Приход      | Прочий           |         Расход   |    Прочий        |    Остаток на    |];
  [|                           |                              |    начало дня    |                  | приход           |                  |    расход        |    конец  дня    |];
  [|---------------------------|------------------------------|------------------|------------------|------------------|------------------|------------------|-------------------];
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetAccounts ( aOp,  DebetAcc, CreditAcc )

  DebetAcc = CreditAcc = "";

  SetParm(1, DebetAcc );
  SetParm(2, CreditAcc );
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro GetAcc( aOp, aDebetAcc, aCreditAcc )

  var retVal = True;

  GetAccounts( aOp,  ADebetAcc, aCreditAcc );

  SetParm( 2, aDebetAcc );
  SetParm( 3, aCreditAcc );

  return retVal;
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintLine( Data )

var RateCB1 = 0,RateCB2 = 0,stat,CodCur,
    DiffRateC = $0, DiffRateD = $0;
/* Добавлено С.Лучко 10.10.2002 tag01 ----------*/
    var aIncomeOp  = 31;
    var aDebetAcc  = "";
    var aCreditAcc = "";
/* Добавлено С.Лучко 10.10.2002 end tag01 ----------*/

/* изменено С.Лучко 10.10.2002 tag02 ----------*/

  GetAcc( aIncomeOp, aDebetAcc, aCreditAcc );
  [|###########################|##############################|##################|##################|                  |##################|                  |##################|]
  (
    Acc_Form(aCreditAcc):f, 
    ( CashValue.rec.NameValue + GetValState ):w, 
    Data.InRest:z, 
    Data.Credit:z, 
    Data.Debet:z, 
    Data.OutRest:z 
  );
/* изменено С.Лучко 10.10.2002 end tag02 ----------*/
  if(Data.IsCur)
    if ( Data.RevalueSum > $0 )
      DiffRateC = Data.RevalueSum;
    else
      DiffRateD = -Data.RevalueSum;
    end;
    [|                           |Эквивалент в рублях           |##################|##################|##################|##################|##################|##################|]
    (
    Data.PrevInRestR:z,
    Data.CreditR:z,
    DiffRateC:z,
    Data.DebetR:z,
    DiffRateD:z,
    Data.OutRestR:z);
    TotPrevRest = TotPrevRest + Data.PrevInRestR;
    TotRest = TotRest + Data.OutRestR;
    TotDebet = TotDebet + Data.DebetR;
    TotCredit = TotCredit + Data.CreditR;
    TotDiffDebet = TotDiffDebet + DiffRateD;
    TotDiffKred = TotDiffKred + DiffRateC;
  else
    TotPrevRest = TotPrevRest + Data.InRest;
    TotRest = TotRest + Data.OutRest;
    TotDebet = TotDebet + Data.Debet;
    TotCredit = TotCredit + Data.Credit;
  end;
  [|---------------------------|------------------------------|------------------|-------------------------------------|-------------------------------------|------------------|];

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro PrintFooter

  if( (TotPrevRest != 0) or
     (TotRest != 0) or
     (TotDebet != 0) or
     (TotCredit != 0) )
    [|===========================|==============================|==================|=====================================|=====================================|==================|];
    [|                           |Итого                         |##################|################## ##################|################## ##################|##################|]
    (TotPrevRest:z,
     TotCredit:z,
     TotDiffKred:z,
     TotDebet:z,
     TotDiffDebet:z,
     TotRest:z);
  end;
  [------------------------------------------------------------------------------------------------------------------------------------------------------------------------------];
  [ ];
  [  Работник кассы ____________________________ # ] ( Person.rec.Name );
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro ProcessValue

  var
    Data;

  Data = GetRestsAndTurns( CashValue.rec.RefValue, {curdate} );

  if ( ( Data.InRest != $0 ) or ( Data.OutRest != $0 ) or ( Data.Debet != $0 ) or ( Data.Credit != $0 ) )
    if ( not HeadPrinted )
      PrintHead;
      HeadPrinted = true;
    end;
    PrintLine( Data );
  end;

end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro Filter

  if ( CashValue.rec.Chapter != ChapterNB )
    return false;
  end;

  if ( CashValue.rec.TypeValue == TYPERES_CURRENCY )
    if ( ( CashValue.rec.Type == TYPE_RES_PAY ) 
      or ( CashValue.rec.Type == TYPE_RES_NOPAY ) ) 
      return false;
    end;
  elif ( CashValue.rec.TypeValue == TYPERES_PAYDOC )
    if ( ( CashValue.rec.Type == TYPE_RES_NOPAY ) 
      or ( CashValue.rec.Type == TYPE_RES_PAY   ) 
      or ( CashValue.rec.Type == TYPE_RES_PDNAT )  
      or ( CashValue.rec.Type == TYPE_RES_PDCUR ) 
      or ( CashValue.rec.Type == TYPE_RES_PD    ) ) 
      return false;
    end;
  elif ( CashValue.rec.TypeValue == TYPERES_MINCAPISSUE + CI_COIN ) 
    FindCIMisc( CI_COIN, CashValue.rec.CodIntValue );
    if ( CIMisc.rec.KindOfCoin == CK_NEW )
      return false;
    end;
  elif ( CashValue.rec.TypeValue == TYPERES_MINCAPISSUE + CI_INGOT ) 
    return false;
  end;
  return true;         
end;


/**************************************************************************\
|                                                                          |
\**************************************************************************/
macro Proceed

  var
    RecFound;

  CashValue.ReWind( );
  while ( CashValue.Next( ) )
    if ( Filter( ) )
      ProcessValue( );
    end;
  end;  

  if ( HeadPrinted )
    PrintFooter( );
  end; 

end;

           
/**************************************************************************\
|                                                                          |
\**************************************************************************/
  var
    SaveDate = {curdate};

  GetDate( {curdate}, "Введите дату:" );
  FindPerson( );
  Proceed( );
  {curdate} = SaveDate;

end;