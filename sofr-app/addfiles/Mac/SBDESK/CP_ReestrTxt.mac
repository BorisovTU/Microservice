/* Коммунальные платежи 
   Макрос для печати реестра платежей получателю
*/
import CPVars;

macro PRINTREESTR
    var rs = GetPaymentsSet( COMPAY_RECIPIENT.Id );
    var sum = $0;
    var str, FIO = "";
[
                                Реестр платежей
                        за период  с  ########## по ##########
Получатель  #############################################################################
┌──────────┬─────────────────────────┬───────────┬─────────────────────┬───────────────────────┐
│   Дата   │      Лицевой счет       │ Оплачено  │     Плательщик      │       Вид услуги      │
│  оплаты  │                         │           │                     │                       │] (
    Date( COMPAY_BEGINDATE ),
    Date( COMPAY_ENDDATE ),
    COMPAY_RECIPIENT.Name );

    while( rs.moveNext )
        sum = sum + SQL_ConvTypeSum(rs.value("T_INSUM"));
        FIO = SQL_ConvTypeStr(rs.value("T_CLIENTLASTNAME"));
        str = SQL_ConvTypeStr(rs.value("T_CLIENTFIRSTNAME"));
        if( str != "" )
            FIO = FIO + " " + substr(str,1,1) + ".";
        end;
        str = SQL_ConvTypeStr(rs.value("T_CLIENTSECONDNAME"));
        if( str != "" )
            FIO = FIO + " " + substr(str,1,1) + ".";
        end;

[├──────────┼─────────────────────────┼───────────┼─────────────────────┼───────────────────────┤
│##########│#########################│###########│#####################│#######################│] (
        Date(rs.value("T_DATE_DOCUMENT")),
        SQL_ConvTypeStr(rs.value("T_CPACCOUNT")):r,
        SQL_ConvTypeSum(rs.value("T_INSUM")):r,
        FIO,
        SQL_ConvTypeStr(rs.value("T_TYPENAME")) );
    end;

[├──────────┴─────────────────────────┼───────────┼─────────────────────┴───────────────────────┘
│ ИТОГО                              │###########│
└────────────────────────────────────┴───────────┘] (
    sum:r );
end;

END;