
/* остатки на счетах сейфов */

import deprintr,rsd;

file Alg   ( "namealg.dbt","bank.def")  key 0;
file Group ( "rtgroup.dbt" ) key 1;
file Person( "person.dbt", "bank.def" ) key 0;

const
  GT_BRIGADE = 1;

const
  ALG_CASH_VAL_TYPE = 811;    /* Типы ресурсов кассы в namealg.dbt */  

var
  ProgramID = GetProgramID,
  Branch = NumFNCash,
  IsCur = NumFlagCur,
  {curdate},
  {oper};

var             
  OwnOnly,
  SmthDisplayed,
  TotalNum,
  TotalMoney;

var CashVal_NameValue,
    CashVal_Type;

/**************************************************************************/
/*  Получить название типа ресурса кассы                                  */
macro GetCashTypeName(Type)

  Alg.iTypeAlg = ALG_CASH_VAL_TYPE;
  Alg.iNumberAlg = Type;
  if(GetEQ(Alg))
    return Alg.szNameAlg;
  else
    return String(Type);
  end;
end;

/**************************************************************************/
/*  Получить название ресурса кассы                                       */
macro GetValName
  var str = CashVal_NameValue;
  if ( CashVal_Type != 0 )
    str = str + " (" + GetCashTypeName(CashVal_Type) + ")";
  end;
  return str;
end;


macro BrigadeIsActive (DeskSubj_Branch,DeskSubj_Group)

  var
    Stat;

  Group.Branch = DeskSubj_Branch;
  Group.Type = GT_BRIGADE;
  Group.Date = {curdate};
  Group.IsActive = "X";
  Group.ID = DeskSubj_Group;
  Stat = GetEQ( Group );
  if ( Stat and OwnOnly )
    Stat = Group.HeadID == {oper};
  end;
  return Stat;
end;

macro OutHeadMultiPantry

  const
    Title = "Остатки ресурсов по хранилищам на " + String({curdate}:m);

  [ ];
  [###############################################################################]
  (Title:c);
  [ ];
  [-------------------------------------------------------------------------------];
  [|                                           |                  |              |];
  [|  Н а и м е н о в а н и е   р е с у р с а  | Остаток (деньги) | Остаток (шт) |];
  [|                                           |                  |              |];
  [-------------------------------------------------------------------------------];
end;

macro OutHeadMulti

  const
    Title = "Остатки ресурсов по сейфам на " + String({curdate}:m);

  [ ];
  [###############################################################################]
  (Title:c);
  [ ];
  [-------------------------------------------------------------------------------];
  [|                                           |                  |              |];
  [|                 С е й ф                   | Остаток (деньги) | Остаток (шт) |];
  [|                                           |                  |              |];
  [-------------------------------------------------------------------------------];
end;

macro OutLineMulti (RestMoney,RestCol,CashOper)

  var sNV;

  if((RestMoney != 0) or (RestCol != 0))
    if(not SmthDisplayed)
      sNV = GetValName;
      PrintLn;
      PrintLn(" " + sNV);
      PrintLn(" " + MkStr("-", StrLen(sNV)));
      PrintLn;
      SmthDisplayed = true;
    end;
    [ ########################################### ################## ############## ]
    (
      CashOper,
      RestMoney:z,
      RestCol:z
    );
    TotalMoney = TotalMoney + RestMoney;
    TotalNum   = TotalNum   + RestCol;
  end;
end;

macro OutTotalsMulti

  [ ];
  [ И т о г о :                                 ################## ############## ]
  (
    TotalMoney:z,
    TotalNum:z
  );
  [ ];
end;

macro Maker

  var cmd,rs;
  var str_cmd = "select cas.t_CashOper   as CashOper,   " +
                "       cas.t_RestMoney  as RestMoney,  " +
                "       cas.t_RestCol    as RestCol,    " +
                "       cas.t_RefValue   as RefValue,   " +
                "       sb.t_Branch      as Branch,     " +
                "       sb.t_Group       as t_Group,    " +
                "       sb.t_Reports     as Reports,    " +
                "       cv.t_NameValue   as NameValue,  " +
                "       cv.t_Type        as Type        " +
                "from dsb_casac_dbt  cas, ddesktype_dbt tp, ddesksubj_dbt sb, dsb_casvl_dbt cv " +
                "where cas.t_Branch   = ? " +
                "  and cas.t_Date     = ? " +
                "  and tp.t_id    = sb.t_type     " + 
                "  and tp.t_issafe = 'X'          " +
                "  and cas.t_subaccount = chr(1)  " +
                "  and sb.t_branch = cas.t_Branch " +
                "  and sb.t_name = cas.t_cashoper " +
                "  and cv.t_RefValue = cas.t_RefValue ";
  var CashOper,  
      RestMoney, 
      RestCol,
      RefValue,
      DeskSubj_Reports,
      DeskSubj_Branch,
      DeskSubj_Group;
  var SaveRefValue = 0;

  SmthDisplayed = false;
  if (not IsCur)
    str_cmd = str_cmd + " and (Instr(sb.t_Reports, 'О') <> 0) ";
  else
    str_cmd = str_cmd + " and (Instr(sb.t_Reports, 'Т') <> 0) ";
  end;
  str_cmd = str_cmd + "order by cas.t_RefValue";

  cmd = RsdCommand (str_cmd);
  cmd.addParam ("t_Branch",   RSDBP_IN);
  cmd.addParam ("t_Date",     RSDBP_IN);
  cmd.value    ("t_Branch"  ) = Branch;
  cmd.value    ("t_Date"    ) = {curdate};

  cmd.execute;
  rs = RsdRecordset (cmd);
  while (rs.moveNext)
    CashOper   = rs.value ("CashOper"  );
    RestMoney  = rs.value ("RestMoney" );
    RestCol    = rs.value ("RestCol"   );
    RefValue   = rs.value ("RefValue"  );
    CashVal_NameValue  = rs.value ("NameValue" );
    CashVal_Type       = rs.value ("Type" );
    DeskSubj_Reports = rs.value ("Reports");
    DeskSubj_Branch  = rs.value ("Branch");
    DeskSubj_Group   = rs.value ("t_Group");
    if (RefValue != SaveRefValue)
      if ((SaveRefValue != 0) and SmthDisplayed)
        OutTotalsMulti;
      end;
      TotalMoney = $0;
      TotalNum = 0;
      SmthDisplayed = false;
      SaveRefValue = RefValue
    end;
    if ( ( ProgramID == CodeFor( "Л" ) ) or BrigadeIsActive(DeskSubj_Branch,DeskSubj_Group) )
      OutLineMulti (RestMoney,RestCol,CashOper);
    end;
  end;
  if(SmthDisplayed)
    OutTotalsMulti;
  end;
end;

  Person.Oper = {oper};
  if ( not GetEQ( Person ) )
    MsgBox( "Пользователь ", {oper}, "не найден" );
    Exit( -1 );
  end; 

  OwnOnly = ( Person.cTypeInDesk != "А" ) and ( Person.cTypeInDesk != "З" );

  if( ProgramID != CodeFor( "Л" ) )  /* не Кладовая отделения */
    OutHeadMulti;
  else
    OutHeadMultiPantry;
  end;

  Maker;
end;
