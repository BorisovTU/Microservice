
/* Книга учета принятых и выданных денег (ценностей) */

import rsd, DeprIntr, RD_Comm, "sortRN.mac";

/* Настраивается пользователем */

const
  IncludeIncassation = true, /* Включать операции инкассации/аванса */

  IncludePPOps       = 1,    /* Отражение операций с физлицами:
                                  0 - не включать
                                  1 - включать оборотами
                                  2 - включать отдельными записями по каждой операции */

  IncludeLPOps       = 0;    /* Отражение операций с юрлицами:
                                  0 - не включать
                                  1 - включать оборотами
                                  2 - включать отдельными записями по каждой операции */
                                    

file PBookTot( "pbooktot.tmp" ) key 0 write;
file BagList( "bag.dbt" ) key 2;
file CIMisc( "ci_misc.dbt" ) key 0;
file DeskSubj( "desksubj.dbt" ) key 0;
file Group( "rtgroup.dbt" ) key 2;
file CashLink( "sb_casln.dbt" );

const
  TYPE_BAG   = 0,
  TYPE_CASDC = 1;

const
  CI_OGSZ    = 1,
  CI_LOTTERY = 2,
  CI_CERT    = 3,
  CI_COIN    = 4,
  CI_INGOT   = 5,
  CI_ORVVZ   = 6,
  CI_MISC    = 10;

const
  VT_CURR    = 1,
  VT_PAYDOC  = 2,
  VT_COIN_W  = 3,
  VT_COIN_NW = 4, 
  VT_CAPISSUE = 5, 

  VT_PPOP    = 11, 
  VT_LPOP    = 12; 


const
  BST_SENT     = 9,
  BST_RETURNED = 10;

const
  CK_NEW          = 0, 
  CK_OLD          = 1;

const
  IP_PANTRY = CodeFor( "Л" );

const
  SB_RSBANK51 = 9000;

private const
  CASHOP_REINFORCE      = 1,  /* Подкрепление кассира */
  CASHOP_RETURN         = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT        = 3,  /* Инкассация */
  CASHOP_ADVANCE        = 4,  /* Аванс */
  CASHOP_DEBIT          = 5,  /* Расход */
  CASHOP_CREDIT         = 6,  /* Приход */
  CASHOP_REMOVE         = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1       = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE        = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1      = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE    = 11, /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1  = 12, /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_DEBET_SAFE     = 13, /* Расход из сейфа */
  CASHOP_CREDIT_SAFE    = 14, /* Приход в сейф */
  CASHOP_GIVEA          = 15, /* Выдача под отчет (со стороны Оперкассы) */
  CASHOP_RETA           = 16, /* Возврат подотчетной суммы (со стороны оперкассы) */
  CASHOP_REVAL_POS      = 18, /* Переоценка (+) */
  CASHOP_REVAL_NEG      = 19, /* Переоценка (-) */
  CASHOP_PANT_REMOVE    = 26, /* Внутреннее подкрепление хранилища (Расход) */
  CASHOP_PANT_REMOVE_1  = 27, /* Внутреннее подкрепление хранилища (Приход) */
  CASHOP_PANT_CHVALUE   = 28, /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_PANT_CHVALUE_1 = 29, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_PANT_RDEB      = 30, /* Расход по кассе пересчета */
  CASHOP_PANT_RCRD      = 31, /* Приход по кассе пересчета */
  CASHOP_PANT_RDRT      = 32, /* Сдача ресурса в кладовую из Кассы пересчета */
  CASHOP_PANT_ECRD      = 41, /* Приход по вечерней кассе */
  CASHOP_PANT_EDRT      = 42, /* Сдача ресурса из вечерней кассы */
  CASHOP_PANT_GIVEA     = 43, /* Выдача под отчет (со стороны КБ) */
  CASHOP_PANT_RETA      = 44; /* Возврат подотчетной суммы (со стороны КБ) */

const
  FLAGDEBCRED_IN   = 1,  /* Кредит */
  FLAGDEBCRED_OUT  = 2;  /* Дебет */

const
  TYPEDOC_UNDEF    = 0,
  TYPEDOC_CARRYOFF = 1,  /* Отложен (не подтвержден) */
  TYPEDOC_CARRYON  = 2;  /* Проведен (подтвержден) */

const
  GRM_HAS_SAFE  = 0,     /* Имеет собственный сейф */
  GRM_IMMEDIATE = 1;     /* Ресурсы выдаются непосредственно из хранилища КБ */

const
  OT_UNDEF      = 0, 
  OT_PHYS_PERS  = 1, 
  OT_LEGAL_PERS = 2;

array VTNames;
  VTNames( VT_CURR   ) = "";
  VTNames( VT_PAYDOC ) = "(плат. док-ты)";
  VTNames( VT_COIN_W ) = "(монеты по массе)";
  VTNames( VT_COIN_NW ) = "(монеты по номин.)";
  VTNames( VT_CAPISSUE ) = "(прочие ценности)";

  VTNames( VT_PPOP ) = "Операции с физ.лицами";
  VTNames( VT_LPOP ) = "Операции с юр.лицами";

var
  OperBrigade = GetOperBrigade, 
  ProgID = GetProgramID; 
private var Branch = NumFNcash();  

macro GetWorkDeskName( oper, brigade )

  var
    operStr = String( oper ),
    brigadeStr = String( brigade );

  return mkStr( "0", 4 - strLen( operStr ) ) + operStr + "/" +
    mkStr( "0", 2 - strLen( brigadeStr ) ) + brigadeStr;
end;

macro GetNativeSafe( Brig )

  Group.Branch = Branch;
  Group.Date = {curdate};
  Group.ID = Brig;
  if ( GetEQ( Group ) and ( Group.Mode == GRM_HAS_SAFE ) )
    return Group.SafeID;
  end;
  return "";
end;

macro GetBrigadeHead( Brig )

  Group.Branch = Branch;
  Group.Date = {curdate};
  Group.ID = Brig;
  if ( GetEQ( Group ) )
    return Group.HeadID;
  end;
  return "";
end;

macro ClearTotFile
   var cmd;
   cmd = RsdCommand( NULL, string("truncate table dpbooktot_tmp") );
   cmd.execute();
end;

macro AddToTotals( DebCred, CurrCode, ValType, Sum, NumItems )

  ClearRecord( PBookTot );
  PBookTot.CurrCode = CurrCode;
  PBookTot.ValType = ValType;
  if ( GetEQ( PBookTot ) )
    if ( DebCred == FLAGDEBCRED_OUT )
      PBookTot.Debet = PBookTot.Debet + Sum;
      PBookTot.DebetItems = PBookTot.DebetItems + NumItems;
    else
      PBookTot.Credit = PBookTot.Credit + Sum;
      PBookTot.CreditItems = PBookTot.CreditItems + NumItems;
    end;
    Update( PBookTot );
  else
    if ( DebCred == FLAGDEBCRED_OUT )
      PBookTot.Debet = Sum;
      PBookTot.DebetItems = NumItems;
    else
      PBookTot.Credit = Sum;
      PBookTot.CreditItems = NumItems;
    end;
    Insert( PBookTot );
  end;
end;

macro CoinHasWeight( Issue )

  CIMisc.Type = CI_COIN;
  CIMisc.IssueID = Issue;
  if ( not GetEQ( CIMisc ) )
    MsgBox( "Не найден вид монет с кодом ", Issue );
    Exit( 1 );
  end;
  return CIMisc.KindOfCoin == CK_OLD;
end;

macro OutHead

  [                                                                                                                                  ];
  [ ################################################################################################################################ ]
  ( {Name_Bank} );
  [                                                                                                                                  ];
  [                                                   К Н И Г А                                                                      ];
  [                                УЧЕТА ПРИНЯТЫХ И ВЫДАННЫХ ДЕНЕГ ( ЦЕННОСТЕЙ )                                                     ];
  [                                                                                                                                  ];
  [ ################################################################################################################################ ]
  ( Person.Name );
  [                                                                                                                                  ];
  [                                      Записи в настоящей книге производятся                                                       ];
  [                                           до полного ее использования                                                            ];
  [                                                                                                                                  ];
  [+----------+---------------------------------------------+-----------------------------------------------------------------------+];
  [|   дата   |                   ПРИХОД                    |                                  РАСХОД                               |];
  [|          |---------------------------------------------|-----------------------------------------------------------------------|];
  [|          |    от кого принято   |коли- | сумма цифрами |      кому выдано     |коли- | сумма цифрами | сумма прописью |подпись |];
  [|          |      (фамилия и      |чество|  с указанием  |      (фамилия и      |чество|  с указанием  |  с указанием   |в полу- |];
  [|          |       инициалы)      |доку- |  наименования |       инициалы)      |доку- |  наименования |  наименования  |чении   |];
  [|          |                      |ментов|    валюты     |                      |ментов|    валюты     |    валюты      |        |];
  [+----------+----------------------+------+---------------+----------------------+------+---------------+----------------+--------+];
  [|     1    |           2          |   3  |       4       |           5          |   6  |       7       |        8       |    9   |];
  [+----------+----------------------+------+---------------+----------------------+------+---------------+----------------+--------+];
end;

macro MakeSumStr( Sum, NumItems )

  var
    Str = String( Sum );

  if ( Sum == $0 )
    return "";
  end;

  if ( Curr.Code_Currency == 0 )
    Str = Str + " руб";
  else
    Str = Str + " " + Curr.Short_Name;
  end;

  return Str;
end;

macro OutLine( OperName, DebCred, Sum, NumItems )
           
  var
    SumStr = "";
                        
  if ( Sum != $0 )
    SumStr = CurToStrAlt( Sum, null, null, Curr.ExternalCode );
  end;

  if ( DebCred == FLAGDEBCRED_OUT )
    [ ##########                                               ###################### ###### ############### ################ ]               
    (
      {curdate},
      OperName:w,
      NumItems:z, 
      MakeSumStr( Sum ):w:r,
      SumStr:w
    ); 
  else          
    [ ########## ###################### ###### ############### ]
    (
      {curdate},
      OperName:w,
      NumItems:z, 
      MakeSumStr( Sum ):w:r
    ); 
  end;
end;

macro OutTotalsLine

  var
    Desc,
    DebetStr = "";

  if ( ( PBookTot.Debet == $0 ) and ( PBookTot.Credit == $0 ) )
    return;
  end;
         
  FindCurr( PBookTot.CurrCode );
  if ( PBookTot.Debet != $0 )
   DebetStr = CurToStrAlt( PBookTot.Debet, null, null, Int(Curr.ExternalCode) );
  end;
  if ( PBookTot.ValType < 10 )
    Desc = "Итого за день - " + Curr.Short_Name + " " + VTNames( PBookTot.ValType );
  else
    Desc = VTNames( PBookTot.ValType ) + " " + Curr.Short_Name;
  end;

  [            ###################### ###### ###############                        ###### ############### ################ ]                                             
  (
    Desc:w,
    PBookTot.CreditItems,
    PBookTot.Credit,
    PBookTot.DebetItems,
    PBookTot.Debet,
    DebetStr:w
  );
end;

macro OutUnitedLines

  Rewind( PBookTot );
  while ( Next( PBookTot ) )
    if ( PBookTot.ValType > 10 )
      OutTotalsLine;
    end;   
  end;           
end;

macro OutTotals

  [----------------------------------------------------------------------------------------------------------------------------------];
  Rewind( PBookTot );
  while ( Next( PBookTot ) )
    if ( PBookTot.ValType < 10 )
      OutTotalsLine;
    end;   
  end;           
  [ ];
  [ Кассовый работник _______________________________________ ];
  Print( StrFor( 12 ) );
end;

macro OutFooter           

 [ ];
 [ #####################################################################                                                             ]
 ( {Name_Bank} );
 [ ];
 [ ];
 [                                                       ЗАВЕРИТЕЛЬНАЯ НАДПИСЬ                                                       ];
 [                                      Итого в настоящей книге содержится пронумерованных и                                         ];
 [                           прошнурованных _________________________________________________ листов                                 ];
 [                                              (количество листов указывается прописью)                                             ];
 [ ];
 [                                    с № ________________ по № ________________ включительно                                        ];
 [ ];
 [ Руководитель кредитной организации ];
 [ Главный бухгалтер                  ];
 [ Заведующий кассой                  ];
 [ ];
 [                                                     "___" _______________ ____ г.                                                 ];
 Print( StrFor( 12 ) );            
end;

macro TreatVal( DebCred, OperName, Sum, NumItems, Type, OpType )

  var
    ValType, 
    Pos = GetPos( CashValue );

  if ( Type != null )
    FindCashValForTypeCode( Type, CashValue.TypeValue, CashValue.CodIntValue );
  end;

  if ( OpType == null )
    OpType = OT_UNDEF;
  end;

  GetCurrCodeAndCBRate( CashValue.RefValue ); 

  if ( CashValue.TypeValue == TYPERES_PAYDOC )
    ValType = VT_PAYDOC;
  elif ( CashValue.TypeValue == TYPERES_MINCAPISSUE + CI_COIN )
    if ( CoinHasWeight( CashValue.CodIntValue ) )
      ValType = VT_COIN_W;
    else
      ValType = VT_COIN_NW;
    end;
  elif ( ( CashValue.TypeValue > TYPERES_MINCAPISSUE )
    and  ( CashValue.TypeValue <= TYPERES_MINCAPISSUE + CI_MISC ) )
    ValType = VT_CAPISSUE;  
  else
    ValType = VT_CURR;
  end;

  if ( ( Sum != $0 ) or ( NumItems != 0 ) )                 
    if ( ( OpType == OT_PHYS_PERS ) and ( IncludePPOps == 1 ) )
      AddToTotals( DebCred, CurrCode, VT_PPOP, Sum, NumItems );
    elif ( ( OpType == OT_LEGAL_PERS ) and ( IncludeLPOps == 1 ) )
      AddToTotals( DebCred, CurrCode, VT_LPOP, Sum, NumItems );
    else
      OutLine( OperName, DebCred, Sum, NumItems );
    end;
    AddToTotals( DebCred, CurrCode, ValType, Sum, NumItems );
  end;

  GetDirect( CashValue, Pos );
end;

macro TreatBag

  var
    DebCred; 

  if ( BagList.Operation == CASHOP_PANT_RDEB )
    DebCred = FLAGDEBCRED_OUT;
  else
    DebCred = FLAGDEBCRED_IN;
  end;

  FindCashValue( BagList.ValueRef );

  TreatVal( DebCred, BagList.OperName, BagList.RealSum, BagList.RealItems, TYPE_RES_PAY );
  TreatVal( DebCred, BagList.OperName, BagList.Bad, BagList.BadItems, TYPE_RES_NOPAY );
  TreatVal( DebCred, BagList.OperName, BagList.Dubious, BagList.DubiousItems, TYPE_RES_DOUBT );
end;

macro OperName( Oper )

  var
    Name;

  GetPos( Person );              
  if ( FindPerson( Oper ) )
    Name = Person.Name;
  else
    Name = "";
  end;  
  GetDirect( Person );

  return Name; 
end;

macro GetOwnerName( Br, SubjID )

  DeskSubj.Branch = Br;
  DeskSubj.Name = SubjID;
  GetEQ( DeskSubj );

  return OperName( DeskSubj.Owner );
end;

/* класс "Запись для отчета" */
class CRDBookRecord( itemTime_,  itemType_, itemPosition_ )
  var itemTime;
  var itemType;
  var itemPosition;
  itemTime = itemTime_;        
  itemType = itemType_;
  itemPosition = itemPosition_;
end;

/* макрос для quicksort-сортировки массива объектов CRDBookRecord */
macro CmpCRDBookRecord( item1, item2 )
  if( item1.itemTime < item2.itemTime )
    return -1;
  elif( item1.itemTime > item2.itemTime )
    return 1;
  else
    return 0;
  end;
end;

/* отчет для кладовой */
macro CreatePantryReport
  
  var items = TArray, /* найденные bagи & sb_casdc */
      RecFound,
      DebCred,
      TempVariableForPassingErrorTheClassIsUncreatable;
  
  /* первый проход по bagу */
  ClearRecord( BagList );
  BagList.Branch = Branch;  
  BagList.Date = {curdate};
  BagList.State = BST_SENT;
  RecFound = GetGE( BagList );
  while ( RecFound
    and ( BagList.Date == {curdate} ) and ( BagList.Branch == Branch ) )
     items[items.size] = CRDBookRecord( SubStr( BagList.ApplicationKey, 11, 6 ),
                                        TYPE_BAG,
                                        getpos( BagList ) );
     RecFound = Next( BagList );
  end;
  
  /* первый проход по sb_casdc */
  KeyNum( CashDocList, 0 );
  ClearRecord( CashDocList );
  CashDocList.Branch = Branch;
  CashDocList.CashDateDoc = {curdate};
  CashDocList.TypeDoc = TYPEDOC_CARRYON;
  RecFound = GetGE( CashDocList );
  while ( RecFound
    and ( CashDocList.Branch == Branch )
    and ( CashDocList.CashDateDoc == {curdate} )
    and ( CashDocList.TypeDoc == TYPEDOC_CARRYON ) )
    if ( ( CashDocList.NumOper == CASHOP_PANT_GIVEA )
      or ( CashDocList.NumOper == CASHOP_PANT_RETA ) )
      items[items.size] = CRDBookRecord( SubStr( CashDocList.ApplicationKey, 11, 6 ),
                                         TYPE_CASDC,
                                         getpos( CashDocList ) );
    end;  
    RecFound = Next( CashDocList );
  end;
  
  /* отсортируем массивы */
  qsort( items, @CmpCRDBookRecord );
  /* и напечатаем все что нужно */
  RecFound = 0;
  while( RecFound < items.size )
    if( items[RecFound].itemType == TYPE_BAG )
      getdirect( BagList, items[RecFound].itemPosition );
      TreatBag;
    else
      getdirect( CashDocList, items[RecFound].itemPosition );
      if ( CashDocList.NumOper == CASHOP_PANT_GIVEA )
        DebCred = FLAGDEBCRED_OUT;
      else 
        DebCred = FLAGDEBCRED_IN;
      end;
      FindCashValue( CashDocList.RefValue );
      TreatVal( DebCred, 
                GetOwnerName( CashDocList.DestBranch, CashDocList.RemotePeer ), 
                CashDocList.ValueMoney, 
                CashDocList.ValueCol );
    end;
    RecFound = RecFound + 1;
  end;
end;

macro GetPeerNameForDuplexOp( IsSafe )

  record ThisDoc( "sb_casdc.dbt" );

  var
    SubjID,
    Found = false,  
    RecFound, 
    HeadAppKind, 
    HeadAppKey,
    Name = "", 
    sk; 

  Copy( ThisDoc, CashDocList );
  GetPos( CashDocList );
  sk = KeyNum( CashDocList, 7 );

  KeyNum( CashLink, 1 );
  CashLink.ApplicationKind2 = CashDocList.ApplicationKind;
  CashLink.ApplicationKey2 = CashDocList.ApplicationKey;
  CashLink.RefValue2 = CashDocList.RefValue;
  if ( GetEQ( CashLink ) ) 
    HeadAppKind = CashLink.ApplicationKind1;  
    HeadAppKey = CashLink.ApplicationKey1;  
    KeyNum( CashLink, 0 );
    CashLink.NumLinkDoc = 0;
    RecFound = GetGE( CashLink );
    while ( RecFound and ( not Found )
      and ( CashLink.ApplicationKind1 == HeadAppKind )
      and ( CashLink.ApplicationKey1 == HeadAppKey ) )
      if ( ( CashLink.ApplicationKind2 != ThisDoc.ApplicationKind )
        or ( CashLink.ApplicationKey2 != ThisDoc.ApplicationKey ) )
        CashDocList.ApplicationKind = CashLink.ApplicationKind2;
        CashDocList.ApplicationKey = CashLink.ApplicationKey2;
        CashDocList.RefValue = CashLink.RefValue2;
        if ( GetEQ( CashDocList ) )
          if ( IsSafe )
            SubjID = CashDocList.CashPatern; 
          else
            SubjID = CashDocList.CashOper;
          end;
          Name = GetOwnerName( CashDocList.Branch, SubjID );
        end;
        Found = true;
      end;  
      if ( not Found ) 
        RecFound = Next( CashLink ); 
      end; 
    end;
  end;

  KeyNum( CashDocList, sk );
  GetDirect( CashDocList );
  return Name;
end;

macro SafeFilter

  if ( ( CashDocList.NumOper == CASHOP_REINFORCE )
    or ( CashDocList.NumOper == CASHOP_RETURN ) )
    return true;  
  end;

  if ( IncludeIncassation )
    if ( ( CashDocList.NumOper == CASHOP_ADVANCE )
      or ( CashDocList.NumOper == CASHOP_COLLECT )
      or ( CashDocList.NumOper == CASHOP_CREDIT_SAFE )
      or ( CashDocList.NumOper == CASHOP_DEBET_SAFE )
      or ( CashDocList.NumOper == CASHOP_REMOVE_SAFE ) 
      or ( CashDocList.NumOper == CASHOP_REMOVE_SAFE_1 ) )
      return true;  
    end;
  end;

  return false;
end;

macro DeskReportForSafe

  var
    DebCred, 
    CtrName, 
    RecFound, 
    Subj = GetNativeSafe( OperBrigade );

  if ( Subj == "" )
    return;
  end;

  KeyNum( CashDocList, 5 );
  ClearRecord( CashDocList );
  CashDocList.Branch = Branch;
  CashDocList.CashDateDoc = {curdate};
  CashDocList.TypeDoc = TYPEDOC_CARRYON;
  CashDocList.CashPatern = Subj; 
  RecFound = GetGE( CashDocList );
  while ( RecFound
    and ( CashDocList.Branch == Branch )
    and ( CashDocList.CashDateDoc == {curdate} )
    and ( CashDocList.TypeDoc == TYPEDOC_CARRYON ) 
    and ( CashDocList.CashPatern == Subj ) )
    if ( SafeFilter )
      if ( CashDocList.FlagDebCredOper == FLAGDEBCRED_IN )
        DebCred = FLAGDEBCRED_OUT;
      else  
        DebCred = FLAGDEBCRED_IN;
      end;

      if ( ( CashDocList.NumOper == CASHOP_REMOVE_SAFE )
        or ( CashDocList.NumOper == CASHOP_REMOVE_SAFE_1 ) )
        CtrName = GetPeerNameForDuplexOp( true );
      elif ( ( CashDocList.NumOper == CASHOP_ADVANCE )
        or ( CashDocList.NumOper == CASHOP_COLLECT )
        or ( CashDocList.NumOper == CASHOP_CREDIT_SAFE )
        or ( CashDocList.NumOper == CASHOP_DEBET_SAFE ) )
        CtrName = "";
      else
        CtrName = GetOwnerName( CashDocList.Branch, CashDocList.CashOper );
      end;

      FindCashValue( CashDocList.RefValue );
      TreatVal( DebCred, 
                CtrName, 
                CashDocList.ValueMoney, 
                CashDocList.ValueCol ); 
    end;  
    RecFound = Next( CashDocList );
  end;
end;

macro OperFilter

  if ( ( CashDocList.NumOper == CASHOP_REINFORCE )
    or ( CashDocList.NumOper == CASHOP_RETURN )
    or ( CashDocList.NumOper == CASHOP_REMOVE )
    or ( CashDocList.NumOper == CASHOP_REMOVE_1 )
    or ( CashDocList.NumOper == CASHOP_GIVEA )
    or ( CashDocList.NumOper == CASHOP_RETA ) )
    return true;
  end;

  if ( ( CashDocList.NumOper == CASHOP_CREDIT )
    or ( CashDocList.NumOper == CASHOP_DEBIT ) )
    if ( ( CashDocList.ApplicationKind == SB_RSBANK51 ) and IncludeLPOps )
      return true;
    end;
    if (   ( CashDocList.ApplicationKind != SB_RSBANK51 ) 
       and ( CashDocList.ApplicationKind != SB_EXTCASH ) 
       and IncludePPOps )
      return true;
    end;
  end;

  return false;
end;

macro DeskReportForOper

  var
    OpType = OT_UNDEF, 
    DebCred, 
    CtrName, 
    RecFound, 
    Subj = GetWorkDeskName( {oper}, OperBrigade );

  KeyNum( CashDocList, 0 );
  ClearRecord( CashDocList );
  CashDocList.Branch = Branch;
  CashDocList.CashDateDoc = {curdate};
  CashDocList.TypeDoc = TYPEDOC_CARRYON;
  CashDocList.CashOper = Subj; 
  RecFound = GetGE( CashDocList );
  while ( RecFound
    and ( CashDocList.Branch == Branch )
    and ( CashDocList.CashDateDoc == {curdate} )
    and ( CashDocList.TypeDoc == TYPEDOC_CARRYON ) 
    and ( CashDocList.CashOper == Subj ) )
    if ( OperFilter )
      DebCred = CashDocList.FlagDebCredOper;
      if ( ( CashDocList.NumOper == CASHOP_REMOVE )
        or ( CashDocList.NumOper == CASHOP_REMOVE_1 ) )
        CtrName = GetPeerNameForDuplexOp( false );
      elif ( CashDocList.NumOper == CASHOP_GIVEA )
        CtrName = OperName( CashDocList.Oper ); 
      elif ( CashDocList.NumOper == CASHOP_RETA ) 
        CtrName = OperName( CashDocList.ConfirmedBy ); 
      elif ( ( CashDocList.NumOper == CASHOP_CREDIT )
        or ( CashDocList.NumOper == CASHOP_DEBIT ) )
        CtrName = "";
      else
        CtrName = GetOwnerName( CashDocList.Branch, CashDocList.CashPatern );
      end;

      if ( ( CashDocList.NumOper == CASHOP_CREDIT )
        or ( CashDocList.NumOper == CASHOP_DEBIT ) )
        if ( CashDocList.ApplicationKind == SB_RSBANK51 )
          OpType = OT_LEGAL_PERS;
        else 
          OpType = OT_PHYS_PERS;
        end;
      else
        OpType = OT_UNDEF;
      end;

      FindCashValue( CashDocList.RefValue );
      TreatVal( DebCred, 
                CtrName, 
                CashDocList.ValueMoney, 
                CashDocList.ValueCol, 
                null, 
                OpType );
    end;  
    RecFound = Next( CashDocList );
  end;
end;

macro CreateDeskReport

  if ( {oper} == GetBrigadeHead( OperBrigade ) )
    DeskReportForSafe;
  end;
  DeskReportForOper; 
  OutUnitedLines;
end;

    
  GetPerson;
  ClearTotFile;
  OutHead;
  if ( ProgID == IP_PANTRY )
    CreatePantryReport;
  else
    CreateDeskReport;
  end;
  OutTotals;
  /*OutFooter;*/
end;