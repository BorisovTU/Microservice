
/* Ведомость по ОГСЗ для Кладовой */

import ResCommP;

const
  ShowNullLines=false;  /* Показывать ли строки, где все нули */

var
  TotPrevRest=$0,
  TotCredit=$0,
  TotDebet=$0,
  TotOtherCred=$0,
  TotOtherDeb=$0,
  TotRest=$0,
  TotPrevRestA=$0,
  TotCreditA=$0,
  TotDebetA=$0,
  TotOtherCredA=$0,
  TotOtherDebA=$0,
  TotRestA=$0;

macro OutHeader;

  [ ];
  [ Отделение _______________ ];
  [ Филиал # ]( GetBranchName );
  [ ];
  [                                 Ведомость первичного учета операций с эмиссионными ценными бумагами ];
  [                                 ################################################################### ]
  ( {curdate}:m:c );
  [ ];
  [ # ]( DeskSubj.Desc );
  [ ];
  [----------------------------------------------------------------------------------------------------------------------------------------];
  [|    №    | Наименование счета и |    Остаток  к    |  Получено от  | Прочий приход |   Выслано в   | Прочий расход |    Остаток  к    |];
  [|  счета  |    ценных бумаг      |    началу дня    |  учреждений   |               |   учреждения  |               |    концу  дня    |];
  [|         |                      |                  |  Сбербанка    |               |   Сбербанка   |               |                  |];
  [|         |                      |                  |               |               |  или клиентам |               |                  |];                                                
  [|---------|----------------------|------------------|---------------|---------------|---------------|---------------|------------------|];
end;

macro M2I( m )

  return Int( Double( m ) );
end;

macro OutLine(NameVal,PrevRest,CurRest,Cred,OtherCred,Deb,OtherDeb);

  if((not ShowNullLines) and (PrevRest==$0) and (Cred==$0) and (OtherCred==$0)
     and (Deb==$0) and (OtherDeb==$0) and (CurRest==$0))
    return;
  end;

  [ ######### ###################### ################## ############### ############### ############### ############### ################## ]
  (
    CashVal.rec.BalAccount,
    NameVal:w,
    M2I( PrevRest ):z,                                               
    M2I( Cred ):z,                                               
    M2I( OtherCred ):z,                                               
    M2I( Deb ):z,                                               
    M2I( OtherDeb ):z,                                               
    M2I( CurRest ):z
  );
  TotPrevRest=TotPrevRest+PrevRest;
  TotCredit=TotCredit+Cred;
  TotDebet=TotDebet+Deb;
  TotOtherCred=TotOtherCred+OtherCred;
  TotOtherDeb=TotOtherDeb+OtherDeb;
  TotRest=TotRest+CurRest;
end;

macro OutTotLine

  [----------------------------------------------------------------------------------------------------------------------------------------];
  [ И т о г о :                      ################## ############### ############### ############### ############### ################## ]
  (
    M2I( TotPrevRest ),
    M2I( TotCredit ),
    M2I( TotOtherCred ),
    M2I( TotDebet ),
    M2I( TotOtherDeb ),
    M2I( TotRest )
  );
end;

macro OutTail

  [ ];
  [ ];
  [ Заведующий филиалом ( контролер ) __________________________ ];
  [ ];
  [ Кассир                            __________________________   Отчет проверил  __________________________ ];
  [ ];
  PrintLn( StrFor( 12 ) );
end;

macro ReportOnCurIssue

  var
    PrevRest=0,
    CurRest=0,
    Deb=0,
    Cred=0,
    OtherDeb=0,
    OtherCred=0;

  CashVal.rec.UnitMeas = RESMESUNIT_POINT;
  CalcTurns(CashVal.rec.RefValue,Cred,Deb,OtherCred,OtherDeb);

  CashAcc.KeyNum = 2;
  CashAcc.rec.Branch=Branch;
  CashAcc.rec.CashOper=Safe;
  CashAcc.rec.SubAccount="";
  CashAcc.rec.RefValue=CashVal.rec.RefValue;
  CashAcc.rec.Date={curdate};
  if(CashAcc.GetLT and
    (CashAcc.rec.Branch==Branch) and
    (CashAcc.rec.RefValue==CashVal.rec.RefValue) and
    (CashAcc.rec.CashOper==Safe) and
    (CashAcc.rec.SubAccount==""))
    if(CashVal.rec.UnitMeas!=RESMESUNIT_MONEY)
      PrevRest=Money(CashAcc.rec.RestCol);
    else
      PrevRest=CashAcc.rec.RestMoney;
    end;
  end;

  CashAcc.KeyNum = 1;
  CashAcc.rec.Branch=Branch;
  CashAcc.rec.CashOper=Safe;
  CashAcc.rec.SubAccount="";
  CashAcc.rec.RefValue=CashVal.rec.RefValue;
  CashAcc.rec.Date={curdate};
  if(CashAcc.GetEQ)
    if(CashVal.rec.UnitMeas!=RESMESUNIT_MONEY)
      CurRest=Money(CashAcc.rec.RestCol);
      if ( {DeskOpen} )
        PrevRest = PrevRest + Money( ( CashAcc.rec.EKredCol - CashAcc.rec.EDebCol ) ); 
      end;
    else
      CurRest=CashAcc.rec.RestMoney;
      if ( {DeskOpen} )
        PrevRest = PrevRest + CashAcc.rec.EKredMoney - CashAcc.rec.EDebMoney; 
      end;
    end;
  end;
  OutLine(CashVal.rec.NameValue,PrevRest,CurRest,Cred,OtherCred,Deb,OtherDeb);
end;

macro ReportOnOGSZIssues;

  var stat;

  OutHeader;

  CashVal.rec.FlagIntoBalance = 1;
  CashVal.rec.BalAccount      = "";
  stat = CashVal.GetGE;
  while (stat)
    if (CashVal.rec.FlagIntoBalance == 1)
      if ((CashVal.rec.RefValueReport != 1          ) AND
          (CashVal.rec.TypeValue == CV_MinCI + CI_OGSZ) AND
          (CashVal.rec.Type == 0) )
        ReportOnCurIssue();
      end;
      stat = CashVal.Next;
    else
      stat = FALSE;
    end;
  end;

  OutTotLine;
  OutTail;
end;

macro TreatCurrentSafe

  TotPrevRest=$0;
  TotCredit=$0;
  TotDebet=$0;
  TotOtherCred=$0;
  TotOtherDeb=$0;
  TotRest=$0;

  if ( Index( DeskSubj.Reports, "E" ) != 0 )
    ReportOnOGSZIssues;
  end;
end;

  var
    RegPath = "RS-RETAIL\\ПЕЧАТИ\\ВЕДОМОСТЬ_ПО_ЭЦБ",
    RecFound;

  CheckPrinter( RegPath, StrFor( 0 ) );

  if ( {EDeskOpen} )
    OperBrigade = B_EDESK;
  elif ( {DeskOpen} ) 
    OperBrigade = B_RDESK;
  end;

  if ( {RprtDSubj} == "" )
    KeyNum( DeskSubj, 1 );
    ClearRecord( DeskSubj );
    DeskSubj.Branch = Branch;
    DeskSubj.Type = DST_PANTRY;
    RecFound = GetGE( DeskSubj );
    while ( RecFound
      and ( DeskSubj.Branch == Branch )
      and ( DeskSubj.Type == DST_PANTRY ) )
      Safe = DeskSubj.Name;
      TreatCurrentSafe;
      RecFound = Next( DeskSubj );
    end;
    KeyNum( DeskSubj, 0 );
    DeskSubj.Branch = Branch;
    DeskSubj.Name = RDesk;
    if ( GetEQ( DeskSubj ) )
      Safe = DeskSubj.Name;
      TreatCurrentSafe;
    end;
  else
    DeskSubj.Branch = Branch;
    DeskSubj.Name = {RprtDSubj};
    if ( GetEQ( DeskSubj ) )
      Safe = DeskSubj.Name;
      TreatCurrentSafe;
    end;
  end;
  ExitMacro;
end;
