/*
$Name:             rd_rec.mac
$Module:           RS-Retail.  Касса
$Description:      Контрольная ведомость
*/

import deprintr, DeskInter, RD_Comm;

file BagList( "bag.dbt" ) key 0;
file OperDep( "operdep.dbt" ) key 1;

var
  {DeskOpen},
  N = 1,
  Total = $0, 
  EDTotal = $0,
  DeclaredSum,
  RealSum,
  Overplus,
  Shortage,
  Bad,
  Dubious, 
  NBags,
  NEDBags = 0,
  PrevNumber = -1,
  TotalSum,
  TotalOverplus,
  TotalShortage,
  TotalBad, 
  HeadPrinted = false, 
  SomethingPrinted = false;

var
  CommonSum,
  CommonOverplus,
  CommonShortage,
  CommonBad;

const
  BST_NONE         = 0,			/* Никакое */
  BST_PREPANT      = 1,			/* Сумка в предкладовой (прием документов) */
  BST_REISSUE      = 2,			/* Переоформление */
  BST_NOT_ACPTD    = 3,			/* Сумка у контролера (прием сумки) */
  BST_ACPTD_RC     = 4,			/* Принято от инкассаторов с пересчетом */
  BST_ACPTD_NORC   = 5,			/* Принято от инкассаторов без пересчета */
  BST_EMERG_RC     = 6, 		/* Аварийный пересчет */
  BST_NOT_CHKD     = 7, 		/* Пересчет */
  BST_CHECKED      = 8,                	/* Пересчитана */
  BST_SENT         = 9,			/* Отправлена */
  BST_RETURNED     = 10;		/* Сдана */

macro GetBagName( Number )

  if ( FindBagNum( Number ) )
    return BagNum.NumberBag;
  else
    return "???";
  end;
end;

// Копейки суммы _sum
private macro SumKop( _sum )
  return Int( ( _sum - Round( _sum, 0 ) ) * 100.00 );
end;

macro OutHead

  var sName1 = "", sName2 = "", sName3 = "", ind = 0;

  if ( HeadPrinted )
    return;
  end; 

  ind = Index( Person.Name, " " );

  if ( ind == 0 )
    sName1 = Person.Name;
  else
    sName2 = SubStr( Person.Name, ind+1 );
    sName1 = SubStr( Person.Name, 1, ind );
  end;

  ind = Index( sName2, " " );

  if ( ind > 0 )
    sName3 = SubStr( sName2, ind+1 );
    sName2 = SubStr( sName2, 1, ind );
  end;

  [
                                                                                   +-------------------+
                                                                                   |     Код формы     |
                                                                                   | документа по ОКУД |
                                                                                   +-------------------+
                                                                                   |      0402010      |
                                                                                   +-------------------+];
  [ ];
  [   #################################################################################################   ]
  ( {Name_Bank}:c:w );
  [   _________________________________________________________________________________________________   ];
  [ (полное фирменное (сокращенное фирменное) наименование кредитной организации или полное (сокращенное) 
      наименование филиала, или наименование и (или) номер ВСП (при наличии) либо иные идентифицирующие   
            признаки ВСП (при отсутствии наименования и номера) с указанием на его принадлежность 
                                   кредитной организации (филиалу)                                        ];
  [                                                                                                       ];
  [                                                                                                       ];
  [                                          КОНТРОЛЬНАЯ ВЕДОМОСТЬ                                        ];
  [ ##################################################################################################### ]
  ( ( "за " + String({curdate}:m) ):c );
  [ ];
  [ ];
  if ( {DeskOpen} )
    [  #####################    Принято на обработку в начале рабочего дня           ##### сумок на объявленную сумму ################## руб. ## коп. ]
    ( sName1, NEDBags:z, EDTotal:0:0:z, SumKop( EDTotal ):z );
  else
    [  #####################    Принято на обработку в начале рабочего дня           ##### сумок на объявленную сумму ################## руб. ## коп. ]
    ( sName1, NBags, Total:0:0, SumKop( Total ):z );
  end;
  [  #####################    Принято на обработку в течение рабочего дня           ____ сумок на объявленную сумму __________________ руб. __ коп. ]
  ( sName2 );
  [  #####################    Передано другим работникам, контролирующим пересчет,  ____ сумок на объявленную сумму __________________ руб. __ коп. ]
  ( sName3 );
  if ( {DeskOpen} )
    [  _____________________    Всего пересчитано                                     #### сумок на объявленную сумму ################## руб. ## коп. ]
    ( NBags, Total:0:0, SumKop( Total ):z );
  else
    [  _____________________    Всего пересчитано                                     ____ сумок на объявленную сумму __________________ руб. __ коп. ]
  end;
  [    (фамилия, инициалы
    контролирующего работника)                                                                                                              ];
  // PrintLn( Curr.Name_Currency );
  // PrintLn( MkStr( "=", StrLen( Curr.Name_Currency ) ) );
  // PrintLn;
  HeadPrinted = true;
end;

macro OutTableHead

  TotalSum      = $0;
  TotalOverplus = $0;
  TotalShortage = $0;
  TotalBad      = $0;

  FindPerson( OperDep.Oper );
  [ ];
  [ ];
  [+------+-----------------------------------------------------------------------------+];
  [|   №  |                    Фамилия, инициалы кассового работника                    |];
  [|  п/п | ########################################################################### |]
  ( Person.Name:c:w );
  [+------+---------+-----------------+---------------+---------------+-----------------+];
  [|      |    №    |      сумма      |     сумма     |     сумма     |       сумма     |];
  [|      |  сумки  |вложения цифрами |излишка цифрами|   недостачи   |   сомнительных  |];
  [|      |         |                 |               |    цифрами    |     денежных    |];
  [|      |         |                 |               |               |  знаков цифрами |];
  [+------+---------+-----------------+---------------+---------------+-----------------+];
  [|   1  |    2    |        3        |       4       |        5      |         6       |];
  [+------+---------+-----------------+---------------+---------------+-----------------+];
end;                                                                                     

macro OutLine( Number )

  var 
    Sum;

  if ( {DeskOpen} )
    Sum = RealSum;
  else
    Sum = DeclaredSum;
  end;

  if ( not SomethingPrinted )
    OutTableHead;
    SomethingPrinted = true;
  end;
  [| #####|#########|#################|###############|###############|#################|]
  (
    N,
    GetBagName( Number ):r:w,
    Sum,
    Overplus,
    Shortage,
    Bad + Dubious
    // Bad,
    // Dubious 
  );
  [+------+---------+-----------------+---------------+---------------+-----------------+];
  TotalSum      = TotalSum      + Sum;
  TotalOverplus = TotalOverplus + Overplus;
  TotalShortage = TotalShortage + Shortage;
  TotalBad      = TotalBad      + Bad + Dubious;
  // Итого за день;
  CommonSum      = CommonSum      + Sum;
  CommonOverplus = CommonOverplus + Overplus;
  CommonShortage = CommonShortage + Shortage;
  CommonBad      = CommonBad      + Bad + Dubious;
end;

macro OutTotals

  [|Итого:|         |#################|###############|###############|#################|]
  (
    TotalSum, 
    TotalOverplus,
    TotalShortage,
    TotalBad
  );
  [+------+---------+-----------------+-------+-------+---------------+-----------------+];
end;

macro ZeroSums

  DeclaredSum = RealSum = Overplus = Shortage = Bad = Dubious = $0;
end;

macro AddSum( Total, Partial )

  var
    Stat = true;
  var ScaleRate = 1;

  Total = Total + Partial;
  SetParm( 0, Total );
end;

macro AddSums

  if ( BagList.DeclaredSum )
    AddSum( DeclaredSum, BagList.DeclaredSum );
  else
    AddSum( DeclaredSum, Money( BagList.DeclaredItems ) );
  end;
  if ( BagList.RealSum )
    AddSum( RealSum, BagList.RealSum );
  else
    AddSum( RealSum, Money( BagList.RealItems ) );
  end;
  if ( BagList.Overplus )
    AddSum( Overplus, BagList.Overplus );
  else
    AddSum( Overplus, Money( BagList.OverplusItems ) );
  end;
  if ( BagList.Shortage )
    AddSum( Shortage, BagList.Shortage );
  else
    AddSum( Shortage, Money( BagList.ShortageItems ) );
  end;
  if ( BagList.Bad )
    AddSum( Bad, BagList.Bad );
  else
    AddSum( Bad, Money( BagList.BadItems ) );
  end;
  if ( BagList.Dubious )
    AddSum( Dubious, BagList.Dubious );
  else
    AddSum( Dubious, Money( BagList.DubiousItems ) );
  end;
end;

macro GetBagDocForState( State )

  BagDoc.BagAppKind = BagList.ApplicationKind;
  BagDoc.BagAppKey = BagList.ApplicationKey;
  BagDoc.State = State;
  return GetEQ( BagDoc );
end;

macro CameThruRDesk

  return GetBagDocForState( BST_CHECKED );
end;

macro CameThruEDesk

  return GetBagDocForState( BST_ACPTD_NORC ) or GetBagDocForState( BST_ACPTD_RC );
end;

macro Proceed( MakeReport )

  var
    Stat = true,
    RecFound;

  N = 0;
  NBags = 0;
  PrevNumber = -1;
  ZeroSums;

  ClearRecord( BagList );
  BagList.Branch = Branch;
  BagList.Date   = {curdate};
  RecFound = GetGE( BagList );
  while ( RecFound and Stat
    and ( BagList.Branch == Branch )
    and ( BagList.Date == {curdate} ) )
    Stat = FindCashValue( BagList.ValueRef );
    if ( Stat
      and ( ( not MakeReport ) or ( ( BagList.Oper == {oper} ) and ( BagList.Calculator == OperDep.Oper ) ) )  
      and ( CashValue.TypeValue == TYPERES_CURRENCY )
      and ( CashValue.CodIntValue == Curr.Code_Currency )
      and ( ( {DeskOpen} and ( BagList.State >= BST_CHECKED ) and CameThruRDesk and ( BagList.Operation == CASHOP_PANT_RCRD ) )
      or  ( ( not {DeskOpen} ) and ( ( BagList.State == BST_ACPTD_RC ) or ( BagList.State == BST_ACPTD_NORC ) ) ) ) )
      if ( BagList.Number != PrevNumber )
        if ( MakeReport and ( PrevNumber >= 0 ) )
          OutLine( PrevNumber );
        end;   
        Total = Total + DeclaredSum;
        NBags = NBags + 1; 
        if ( ( not MakeReport ) and {DeskOpen} and CameThruEDesk )
          NEDBags = NEDBags + 1;
        end;
        N = N + 1; 
        ZeroSums;
        PrevNumber = BagList.Number;
      end; 
      AddSums;
      if ( ( not MakeReport ) and {DeskOpen} and CameThruEDesk )
        if ( BagList.DeclaredSum )
          AddSum( EDTotal, BagList.DeclaredSum );
        else
          AddSum( EDTotal, Money( BagList.DeclaredItems ) );
        end;
      end;
    end;
    RecFound = Next( BagList );
  end;
  if ( PrevNumber >= 0 )
    Total = Total + DeclaredSum;
    if ( MakeReport )
      OutLine( PrevNumber );
      OutTotals;
    end; 
  end;
  if ( not Stat )
    MsgBox( "Ошибка при печати Контрольной ведомости" );
    Exit( 1 );
  end;
end;

macro TreatEDeskBags

  var                      
    Stat = true,
    RecFound;

  PrevNumber = -1;
          
  NEDBags = 0;
  EDTotal = $0;

  ClearRecord( BagList );
  BagList.Branch = Branch;
  BagList.Date   = {curdate};
  RecFound = GetGE( BagList );
  while ( RecFound and Stat
    and ( BagList.Branch == Branch )
    and ( BagList.Date == {curdate} ) )
    Stat = FindCashValue( BagList.ValueRef );
    if ( Stat
      and ( CashValue.TypeValue == TYPERES_CURRENCY )
      and ( CashValue.CodIntValue == Curr.Code_Currency )
      and {DeskOpen} 
      and CameThruEDesk )
      if ( BagList.Number != PrevNumber )
        NEDBags = NEDBags + 1;
        PrevNumber = BagList.Number;
      end; 
      if ( BagList.DeclaredSum )
        AddSum( EDTotal, BagList.DeclaredSum );
      else
        AddSum( EDTotal, Money( BagList.DeclaredItems ) );
      end;
    end;
    RecFound = Next( BagList );
  end;
  if ( not Stat )
    MsgBox( "Ошибка при печати Контрольной ведомости" );
    Exit( 1 );
  end;
end;

macro RecForOper

  SomethingPrinted = false;
  Proceed( true ); 
  // if ( SomethingPrinted )
  //   PrintLn( StrFor( 12 ) );
  // end; 
end;

macro OutCommonTotals()

  [|                                          |                                         |];
  [|                                          |                                         |];
  [|_____________ _____________ ______________|_____________ _____________ _____________|];
  [|(наименование    (личная      (фамилия и  |(наименование    (личная     (фамилия и  |];
  [|  должности)     подпись)      инициалы)  |  должности)     подпись)     инициалы)  |];
  [ ];
  [+------+-----------------------------------------------------------------------------+];
  [|   №  |                    Фамилия, инициалы кассового работника                    |];
  [|  п/п |                                                                             |];
  [+------+---------+-----------------+---------------+---------------+-----------------+];
  [|      |    №    |      сумма      |     сумма     |     сумма     |       сумма     |];
  [|      |  сумки  |вложения цифрами |излишка цифрами|   недостачи   |   сомнительных  |];
  [|      |         |                 |               |    цифрами    |     денежных    |];
  [|      |         |                 |               |               |  знаков цифрами |];
  [+------+---------+-----------------+---------------+---------------+-----------------+];
  [|   1  |    2    |        3        |       4       |        5      |         6       |];
  [+------+---------+-----------------+---------------+---------------+-----------------+];
  [|      |         |                 |               |               |                 |];
  [+------+---------+-----------------+---------------+---------------+-----------------+];
  [|      |         |                 |               |               |                 |];
  [+------+---------+-----------------+---------------+---------------+-----------------+];
  [|      |         |                 |               |               |                 |];
  [+------+---------+-----------------+---------------+---------------+-----------------+];
  [|Итого:|         |                 |               |               |                 |];
  [+------+---------+-----------------+-------+-------+---------------+-----------------+];
  [|                                          |                                         |];
  [|                                          |                                         |];
  [|_____________ _____________ ______________|_____________ _____________ _____________|];
  [|(наименование    (личная      (фамилия и  |(наименование    (личная     (фамилия и  |];
  [|  должности)     подпись)      инициалы)  |  должности)     подпись)     инициалы)  |];
  [+---------------------------+--------------+------------+----------------------------+--------------------------------+--------------------------------------------+--------------------------------+-------------------------------------+];
  [|                           |  сумма вложения, цифрами, |   сумма излишка, цифрами,  |     сумма недостачи, цифрами,  |      сумма сомнительных денежных знаков    |   Сумма неплатежеспособных не  |    Сумма имеющих признаки подделки  |];
  [|                           |        в руб., коп.       |        в руб., коп.        |         в руб., коп.           |            Банка России, цифрами,          |   имеющих признаков подделки   |             денежных знаков         |];
  [|                           |                           |                            |                                |                 в руб., коп.               |         денежных знаков        |         Банка России, цифрами,      |];
  [|                           |                           |                            |                                |                                            |      Банка России, цифрами,    |              в руб., коп.           |];
  [|                           |                           |                            |                                |                                            |          в руб., коп.          |                                     |];
  [+---------------------------+---------------------------+----------------------------+--------------------------------+--------------------------------------------+--------------------------------+-------------------------------------+];
  [|  Итого за день            | ######################### | ########################## | ############################## | ########################################## |                           0.00 |                                0.00 |]
  ( 
    CommonSum,
    CommonOverplus,
    CommonShortage,
    CommonBad
  );
  [+---------------------------+---------------------------+----------------------------+--------------------------------+--------------------------------------------+--------------------------------+-------------------------------------+];
  [ ];
  [ ];

end;

macro TreatOpers;

  var
    RecFound;

  ClearRecord( OperDep );
  OperDep.FNCash = Branch;
  RecFound = GetGE( OperDep );
  while ( RecFound
    and ( OperDep.FNCash == Branch ) )
    RecForOper;
    RecFound = Next( OperDep );
  end;
  OutCommonTotals();
end;

macro TreatCurrencies

  OperDep.Oper = {oper};

  Rewind( Curr );
  while ( Next( Curr ) )
    Proceed( false );
    TreatEDeskBags;
    if ( NBags > 0 )
      HeadPrinted = false;
      GetPerson;          
      OutHead;
      TotalSum       = $0;
      TotalOverplus  = $0;
      TotalShortage  = $0;
      TotalBad       = $0;
      CommonSum      = $0;
      CommonOverplus = $0;
      CommonShortage = $0;
      CommonBad      = $0;
      TreatOpers;
    end;
  end;
end;

  TreatCurrencies;
end;
