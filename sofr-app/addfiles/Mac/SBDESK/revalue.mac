/*
**  П е р е о ц е н к и
*/

import DeprIntr, cur_rate, ingot, dskRubEF, sqlconv;

private var CashDoc = TBFile( "sb_casdc.dbt", "r", 5 );
private var CashAcc = TBFile( "sb_casac.dbt", "r", 2 );
private file Curr( "currency.dbt" ) key 0;
private file CashVal( "sb_casvl.dbt" ) key 0;
private file Group( "rtgroup.dbt" ) key 2;
private var DeskSubj = TBFile( "desksubj.dbt", "r", 1 );
private file PayDoc( "paydoc.dbt", "exchange.def" ) key 0;

private const
  DST_IO          = 1,                  /* Инкассация */
  DST_SAFE        = 2,                  /* Сейф */
  DST_WORKDESK    = 3,                  /* Рабочий стол кассира */
  DST_RDESK       = 4,                  /* Касса пересчета */
  DST_PANTRY      = 5,                  /* Кладовая отделения */
  DST_EDESK       = 6,                  /* Вечерняя касса */
  DST_TCD         = 11;                 /* Сейф TCD */

private const
  CASHOP_REINFORCE      = 1,  /* Подкрепление кассира */
  CASHOP_RETURN         = 2,  /* Возврат средств в кладовую */
  CASHOP_COLLECT        = 3,  /* Инкассация */
  CASHOP_ADVANCE        = 4,  /* Аванс */
  CASHOP_DEBIT          = 5,  /* Расход */
  CASHOP_CREDIT         = 6,  /* Приход */
  CASHOP_REMOVE         = 7,  /* Внутреннее подкрепление (Расход) */
  CASHOP_REMOVE_1       = 8,  /* Внутреннее подкрепление (Приход) */
  CASHOP_CHVALUE        = 9,  /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_CHVALUE_1      = 10, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_REMOVE_SAFE    = 11, /* Внутреннее подкрепление сейфа (Расход) */
  CASHOP_REMOVE_SAFE_1  = 12, /* Внутреннее подкрепление сейфа (Приход) */
  CASHOP_DEBET_SAFE     = 13, /* Расход из сейфа */
  CASHOP_CREDIT_SAFE    = 14, /* Приход в сейф */
  CASHOP_GIVEA          = 15, /* Выдача под отчет (со стороны Оперкассы) */
  CASHOP_RETA           = 16, /* Возврат подотчетной суммы (со стороны оперкассы) */
  CASHOP_REVAL_POS      = 18, /* Переоценка (+) */
  CASHOP_REVAL_NEG      = 19, /* Переоценка (-) */
  CASHOP_PANT_REMOVE    = 26, /* Внутреннее подкрепление хранилища (Расход) */
  CASHOP_PANT_REMOVE_1  = 27, /* Внутреннее подкрепление хранилища (Приход) */
  CASHOP_PANT_CHVALUE   = 28, /* Перевод на другой ресурс той же группы (Расход) */
  CASHOP_PANT_CHVALUE_1 = 29, /* Перевод на другой ресурс той же группы (Приход) */
  CASHOP_PANT_RDEB      = 30, /* Расход по кассе пересчета */
  CASHOP_PANT_RCRD      = 31, /* Приход по кассе пересчета */
  CASHOP_PANT_RDRT      = 32, /* Сдача ресурса в кладовую из Кассы пересчета */
  CASHOP_PANT_ECRD      = 41, /* Приход по вечерней кассе */
  CASHOP_PANT_EDRT      = 42, /* Сдача ресурса из вечерней кассы */
  CASHOP_PANT_GIVEA     = 43, /* Выдача под отчет (со стороны КБ) */
  CASHOP_PANT_RETA      = 44; /* Возврат подотчетной суммы (со стороны КБ) */

private const
  TYPERES_CURRENCY    =   1, /* Деньги (из справочника валют) */
  TYPERES_VALFORM     =   2, /* Ценные бланки */
  TYPERES_VALUABLES   =   3, /* Др. ценности (из справочника ценностей) */
  TYPERES_PAYDOC      =   4, /* Платежные документы ОП */
  TYPERES_CARD        =  10, /* Пластиковые карточки */
  TYPERES_MINCAPISSUE = 500; /* Минимально возможный код ЦБ */

private const
  CI_OGSZ          = 1,
  CI_LOTTERY       = 2,
  CI_CERT          = 3,
  CI_COIN          = 4,
  CI_INGOT         = 5,
  CI_ORVVZ         = 6,
  CI_MISC          = 10;

private const
  TYPEDOC_CARRYON = 2;

private const
  RESMESUNIT_POINT = 1,  /* Поштучно  */
  RESMESUNIT_MONEY = 2;  /* Стоимость */

private const
  FLAGDEBCRED_IN   = 1,  /* Кредит */
  FLAGDEBCRED_OUT  = 2;  /* Дебет */

private var    
  Branch = NumFNCash,
  Brigade  = GetOperBrigade, 
  IsDep = GetProgramID() == CodeFor( "Л" );

private macro FindCashVal( Ref )

  CashVal.RefValue = Ref;
  return GetEQ( CashVal );
end;

class TValData

  var
    IsCur, 
    CurrCode;

  var
    InRest = $0, 
    OutRest = $0, 
    Debet = $0, 
    Credit = $0;

  var
    PrevInRestR = $0, 
    InRestR = $0, 
    OutRestR = $0, 
    DebetR = $0, 
    CreditR = $0;

  var
    NumDebetDocs = 0, 
    NumCreditDocs = 0;

  var
    RevalueSum = $0;
end;

private class THandler( Dd )

  var
    RepDate = Dd;

  macro GetRestRubValue( Dd )
  end;

  macro GetDocRubValue( Dd )
  end;

  macro GetCurrCode

    return 0;
  end;
end;

private class ( THandler ) TRubValHandler( Dd )

  macro GetRestRubValue( Dd )

    if ( CashAcc.rec.RestMoney != $0 )
      return CashAcc.rec.RestMoney;
    else
      return Money( CashAcc.rec.RestCol );
    end;
  end;

  macro GetDocRubValue( Dd )

    if ( CashDoc.rec.ValueMoney != $0 )
      return CashDoc.rec.ValueMoney;
    else
      return Money( CashDoc.rec.ValueCol );
    end;
  end;

  InitTHandler( Dd );
end;

private class ( THandler ) TSimpleHandler( Dd )

  var
    Rate = $0, 
    PrevRate = $0, 
    Scale = 1;

  private macro GetRate( Dd )
   
    if ( Dd == RepDate )
      return Rate;
    elif ( Dd == RepDate - 1 )
      return PrevRate;
    else
      runError( "Неверная дата!" );
    end;
  end;

  macro GetRestRubValue( Dd )

    var
      Sum;

    if ( CashAcc.rec.RestMoney != $0 )
      Sum = CashAcc.rec.RestMoney;
    else
      Sum = Money( CashAcc.rec.RestCol );
    end;

    return SumConv( Sum, GetRate( Dd ), Scale );
  end;

  macro GetDocRubValue( Dd )

    var
      Sum;

    if ( CashDoc.rec.ValueMoney != $0 )
      Sum = CashDoc.rec.ValueMoney;
    else
      Sum = Money( CashDoc.rec.ValueCol );
    end;

    return SumConv( Sum, GetRate( Dd ), Scale );
  end;

  InitTHandler( Dd );
end;

private class ( TSimpleHandler ) TCurrValHandler( Curr, Dd )

  var
    CurrCode = Curr;

  macro GetCurrCode

    return CashVal.CodIntValue;
  end;

  InitTSimpleHandler( Dd );
  if ( CurrCode )
    GetAllCurrRateNoScaling( RepDate, CurrCode, Scale, Rate, null, null );
    GetAllCurrRateNoScaling( RepDate - 1, CurrCode, Scale, PrevRate, null, null );
  end;    
end;

private class ( TSimpleHandler ) TCurrHandler( Dd )

  macro GetCurrCode

    return CashVal.CodIntValue;
  end;

  InitTSimpleHandler( Dd );
  if ( GetCurrCode )
    GetAllCurrRateNoScaling( RepDate, CashVal.CodIntValue, Scale, Rate, null, null );
    GetAllCurrRateNoScaling( RepDate - 1, CashVal.CodIntValue, Scale, PrevRate, null, null );
  end;
end;

private class ( TSimpleHandler ) TPayDocHandler( Dd )

  private var
    CurrCode;

  private macro DetectCurrCode

    PayDoc.ID = CashVal.CodIntValue;
    if ( GetEQ( PayDoc ) )
      CurrCode = PayDoc.Curr_Ref;
    else
      PrintLn( "   *** Не найден платежный документ с кодом " + PayDoc.ID +
               ". Тип ресурса кассы = ", CashVal.Type );
      ClearRecord( PayDoc );
      CurrCode = 0;
    end;
  end;

  macro GetCurrCode

    return CurrCode;
  end;

  InitTSimpleHandler( Dd );
  DetectCurrCode;
  if ( CurrCode )
    GetAllCurrRate( RepDate, CurrCode, Rate, null, null );
    GetAllCurrRate( RepDate - 1, CurrCode, PrevRate, null, null );
  end;   
end;

private class ( THandler ) TIngotHandler( Dd )

  macro GetRestRubValue( Dd )

    var
      ica = TIngotCashAcc( CashAcc.rec.RefValue, CashAcc.rec.CashOper, CashAcc.rec.Date, CashAcc.rec.Branch );

    return ica.GetRubValue( Dd );
  end;

  macro GetDocRubValue( Dd )

    var
      icd = TIngotCashDoc( CashDoc.rec.ApplicationKind, CashDoc.rec.ApplicationKey, CashDoc.rec.RefValue );

    return icd.GetRubValue( Dd );
  end;

  InitTHandler( Dd );
end;

private macro FindBrigade

  Group.Branch = Branch;
  Group.ID = Brigade;
  Group.Date = Date( 0, 0, 0 );
  return GetEQ( Group );
end;

private macro AddDocToData( Dd, Handler, Data, IsDebet )

  var
    Sum;

  if ( CashDoc.rec.ValueMoney != $0 )
    Sum = CashDoc.rec.ValueMoney;
  else
    Sum = Money( CashDoc.rec.ValueCol );
  end;
  if ( IsDebet )
    Data.NumDebetDocs = Data.NumDebetDocs + 1;
    Data.Debet = Data.Debet + Sum;
    Data.DebetR = Data.DebetR + Handler.GetDocRubValue( Dd );
  else
    Data.NumCreditDocs = Data.NumCreditDocs + 1;
    Data.Credit  = Data.Credit  + Sum;
    Data.CreditR = Data.CreditR + Handler.GetDocRubValue( Dd );
  end;
end;

private macro DeskSubj_AddData( Subj, Dd, Handler, Data )

  var
    IsDebet, 
    RecFound;

  var inRestSubjForDate = $0L;
  var inRestRSubjForDate = $0L;

  CashAcc.Clear;
  CashAcc.rec.Branch = Branch;
  CashAcc.rec.CashOper = Subj;
  CashAcc.rec.SubAccount = "";
  CashAcc.rec.RefValue = CashVal.RefValue;
  CashAcc.rec.Date = Dd;
  if ( CashAcc.GetLT
    and ( CashAcc.rec.Branch == Branch )
    and ( CashAcc.rec.CashOper == Subj )
    and ( CashAcc.rec.SubAccount == "" )
    and ( CashAcc.rec.RefValue == CashVal.RefValue ) )

    if ( CashAcc.rec.RestMoney != $0 )
      inRestSubjForDate = CashAcc.rec.RestMoney;
    else
      inRestSubjForDate = Money( CashAcc.rec.RestCol );
    end;

    inRestRSubjForDate = Handler.GetRestRubValue( Dd );

    Data.InRest = Data.InRest + inRestSubjForDate;
    Data.InRestR = Data.InRestR + inRestRSubjForDate;
    Data.PrevInRestR = Data.PrevInRestR + Handler.GetRestRubValue( Dd - 1 );
  end;

  CashAcc.Clear;
  CashAcc.rec.Branch = Branch;
  CashAcc.rec.CashOper = Subj;
  CashAcc.rec.SubAccount = "";
  CashAcc.rec.RefValue = CashVal.RefValue;
  CashAcc.rec.Date = Dd;
  if ( CashAcc.GetEQ
    and ( CashAcc.rec.Branch == Branch )
    and ( CashAcc.rec.CashOper == Subj )
    and ( CashAcc.rec.SubAccount == "" )
    and ( CashAcc.rec.RefValue == CashVal.RefValue ) )
    if ( CashAcc.rec.RestMoney != $0 )
      Data.OutRest = Data.OutRest + CashAcc.rec.RestMoney;
    else
      Data.OutRest = Data.OutRest + Money( CashAcc.rec.RestCol );
    end;
    Data.OutRestR = Data.OutRestR + Handler.GetRestRubValue( Dd );
  else
    Data.OutRest = Data.OutRest + inRestSubjForDate;
    Data.OutRestR = Data.OutRestR + inRestRSubjForDate;
  end;

  if ( IsDep )
    CashDoc.KeyNum = 5;
    CashDoc.Clear;
    CashDoc.rec.Branch = CashAcc.rec.Branch;
    CashDoc.rec.CashDateDoc = CashAcc.rec.Date;
    CashDoc.rec.TypeDoc = TYPEDOC_CARRYON;
    CashDoc.rec.CashPatern = CashAcc.rec.CashOper;
    RecFound = CashDoc.GetGE;
    while ( RecFound 
      and ( CashDoc.rec.Branch == CashAcc.rec.Branch )
      and ( CashDoc.rec.CashDateDoc == CashAcc.rec.Date )
      and ( CashDoc.rec.TypeDoc == TYPEDOC_CARRYON )
      and ( CashDoc.rec.CashPatern == CashAcc.rec.CashOper ) )
      if ( CashDoc.rec.RefValue == CashAcc.rec.RefValue )
        IsDebet = ( CashDoc.rec.FlagDebCredOper == FLAGDEBCRED_IN );
        AddDocToData( Dd, Handler, Data, IsDebet );
      end;
      RecFound = CashDoc.Next;
    end;
  else
    CashDoc.KeyNum = 0;
    CashDoc.Clear;
    CashDoc.rec.Branch = CashAcc.rec.Branch;
    CashDoc.rec.CashDateDoc = CashAcc.rec.Date;
    CashDoc.rec.TypeDoc = TYPEDOC_CARRYON;
    RecFound = CashDoc.GetGE;
    while ( RecFound
      and ( CashDoc.rec.Branch == CashAcc.rec.Branch )
      and ( CashDoc.rec.CashDateDoc == CashAcc.rec.Date )
      and ( CashDoc.rec.TypeDoc == TYPEDOC_CARRYON ) )
      if ( ( CashDoc.rec.RefValue == CashVal.RefValue ) 
        and ( CashDoc.rec.Brigade == Group.ID ) )
        if ( ( CashDoc.rec.NumOper == CASHOP_DEBIT )
          or ( CashDoc.rec.NumOper == CASHOP_CREDIT )
          or ( CashDoc.rec.NumOper == CASHOP_DEBET_SAFE )
          or ( CashDoc.rec.NumOper == CASHOP_CREDIT_SAFE )
          or ( CashDoc.rec.NumOper == CASHOP_REMOVE_SAFE )
          or ( CashDoc.rec.NumOper == CASHOP_REMOVE_SAFE_1 )
          or ( CashDoc.rec.NumOper == CASHOP_ADVANCE )
          or ( CashDoc.rec.NumOper == CASHOP_COLLECT )
          or ( CashDoc.rec.NumOper == CASHOP_CHVALUE )
          or ( CashDoc.rec.NumOper == CASHOP_CHVALUE_1 ) )
    
          IsDebet = ( ( CashDoc.rec.NumOper == CASHOP_DEBIT )
                   or ( CashDoc.rec.NumOper == CASHOP_DEBET_SAFE )
                   or ( CashDoc.rec.NumOper == CASHOP_REMOVE_SAFE )
                   or ( CashDoc.rec.NumOper == CASHOP_COLLECT )
                   or ( CashDoc.rec.NumOper == CASHOP_CHVALUE ) );

          AddDocToData( Dd, Handler, Data, IsDebet );
        end;  
      end; 
      RecFound = CashDoc.Next;
    end;
  end;
end;

private macro Val_GetData( Dd, Handler, Subj, Data )

  var
    RecFound;

  if ( IsDep )
    if ( Subj == null )
      DeskSubj.Clear;
      DeskSubj.rec.Branch = Branch;
      DeskSubj.rec.Type = DST_PANTRY;
DeskSubj.addFilter( "exists (select * from dsb_casac_dbt a " + 
                    "where a.t_branch = t.t_branch " +
                    "  and a.t_cashoper = t.t_name " +
                    "  and a.t_refvalue = " + string( CashVal.refValue ) +
                    "  and a.t_date <= " + sqlDateToStr( dd ) + " )" );
      RecFound = DeskSubj.GetGE;
      while ( RecFound
        and ( DeskSubj.rec.Branch == Branch )
        and ( DeskSubj.rec.Type == DST_PANTRY ) )
        DeskSubj_AddData( DeskSubj.rec.Name, Dd, Handler, Data );
        RecFound = DeskSubj.Next;
      end;
DeskSubj.dropFilter;
    else
      DeskSubj_AddData( Subj, Dd, Handler, Data );
    end;   
  else
    FindBrigade;
    DeskSubj_AddData( Group.SafeID, Dd, Handler, Data );
  end;

  return Data;
end;

private macro GetNativeCurr;

  var
    NativeCurr = -1;

  if ( CashVal.TypeValue == TYPERES_CURRENCY )
    NativeCurr = CashVal.CodIntValue;
  else
    GetPos( CashVal );
    FindCashVal( CashVal.GroupValue );
    if ( CashVal.TypeValue == TYPERES_CURRENCY )
      NativeCurr = CashVal.CodIntValue;
    end;
    GetDirect( CashVal );
    if ( NativeCurr < 0 )
      if ( CashVal.RefValueReport < 2 )
        NativeCurr = CashVal.RefValueReport;
      else
        NativeCurr = 0;
      end;
    end;
  end;
  return NativeCurr;
end;

private macro GetDskRubEq( Data )

  var
    Size = ASize( ARefVal ), 
    i = 0;

  while ( ( i < Size ) and ( ARefVal[i] != CashVal.RefValue ) )
    i = i + 1;
  end;

  if ( i < Size )
    Data.DebetR  = ADebit[i];
    Data.CreditR = ACredit[i];
    Data.RevalueSum = ARevaluation[i] + ARegulation[i];
  end;
end;

macro GetRestsAndTurns( ValueRef, Dd, Subj )

  var
    IsCur = true,  
    Data = TValData,
    NativeCurr,  
    Handler, 
    Msg;

  if ( not FindCashVal( ValueRef ) )
    MsgBox( "Не найдена ценность с кодом " + ValueRef );
    Exit( 1 );
  end;        

  if ( not RubEq_Init( Msg ) )
    MsgBox( Msg );
    Exit( 1 );
  end;

  if ( TypeWorkRubEq != 0 )
    if ( not CalcRubEqSums )
      MsgBox( "Ошибка при вычислении рублевых эквивалентов" );
      Exit( 1 );
    end; 
  end;

  if ( CashVal.TypeValue == TYPERES_CURRENCY )
    if ( CashVal.CodIntValue == 0 )
      IsCur = false;
    end;      
    Handler = TCurrHandler( Dd );
  elif ( CashVal.TypeValue == TYPERES_PAYDOC )
    Handler = TPayDocHandler( Dd );
  elif ( CashVal.TypeValue == TYPERES_MINCAPISSUE + CI_INGOT )
    Handler = TIngotHandler( Dd );
  elif ( ( NativeCurr = GetNativeCurr ) != 0 )
    Handler = TCurrValHandler( NativeCurr, Dd );
  else
    IsCur = false;
    Handler = TRubValHandler( Dd );
  end;

  Data.CurrCode = Handler.GetCurrCode;
   
  Val_GetData( Dd, Handler, Subj, Data );

  Data.IsCur = IsCur;

  if ( IsCur )
    Data.RevalueSum = Data.OutRestR - ( Data.PrevInRestR + Data.CreditR - Data.DebetR );
    if ( RubEq_IsCalculated )
      GetDskRubEq( Data );
    end;
  else
    Data.RevalueSum = $0;
  end;

  return Data;
end;

macro GetRevalueSum( ValueRef, Dd, Subj )

  return GetRestsAndTurns( ValueRef, Dd, Subj ).RevalueSum;
end;