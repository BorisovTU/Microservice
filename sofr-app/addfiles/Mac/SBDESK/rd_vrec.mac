
/* Сопроводительная ведомость ф. 119 */

import deprintr, DeskInter, RD_Comm;

file BagList( "bag.dbt" ) key 0;
file Bunch( "bunch.dbt" ) key 0;

var
  RcvName,
  RcvShortName,
  DebetAcc,
  CreditAcc;

array
  SumStrArr;

macro GetSum( Sum )

  var
    Stat = true,
    RecFound;

  Sum = 0;
  ClearRecord( BagList );
  BagList.Branch = Bag.Branch;  
  BagList.Date = Bag.Date;
  BagList.ClientType = Bag.ClientType;
  BagList.ClientID = Bag.ClientID;
  BagList.Operation = Bag.Operation;
  BagList.Number = Bag.Number;
  RecFound = GetGE( BagList );
  while ( RecFound and Stat
    and ( BagList.Branch == Bag.Branch )  
    and ( BagList.Date == Bag.Date )
    and ( BagList.ClientType == Bag.ClientType )
    and ( BagList.ClientID == Bag.ClientID )
    and ( BagList.Operation == Bag.Operation )
    and ( BagList.Number == Bag.Number ) )
    if ( BagList.RealSum )
      GetCurrCodeAndCBRate( BagList.ValueRef );
      if ( CurrCode == 0 )
        Sum = Sum + BagList.RealSum;    
      else
        Sum = Sum + Cur2Rub( BagList.RealSum );    
      end; 
    else
      Sum = Sum + Money( BagList.RealItems );  
    end;
    RecFound = Next( BagList );
  end;
  if ( not Stat )
    MsgBox( "Ошибка при расчете суммы" );
    Exit( -1 );
  end;
  SetParm( 0, Sum );  
end;

macro SplitSum( Sum )

  var
    i;

  StrSplit( String( Sum:m ), SumStrArr, 46 );
  
  i = ASize( SumStrArr );
  while ( i < 2 )
    SumStrArr( i ) = "";
    i = i + 1;
  end;
end;

macro RegNumRangeStr

  var
    RecFound, 
    Str = "";

  if ( not CashValue.HasRegNumber )
    return "";
  end;

  ClearRecord( Bunch );
  Bunch.BagAppKind = BagList.ApplicationKind;
  Bunch.BagAppKey = BagList.ApplicationKey;
  Bunch.Type = TYPE_RES_PAY;
  RecFound = GetGE( Bunch );
  while ( RecFound  
    and ( Bunch.BagAppKind == BagList.ApplicationKind )
    and ( Bunch.BagAppKey == BagList.ApplicationKey )
    and ( Bunch.Type == TYPE_RES_PAY ) )
    if ( StrLen( Str ) > 0 )
      Str = Str + ",";
    end;
    if ( Bunch.Min == Bunch.Max )
      Str = Str + " № " + RegNumToStr( Bunch.Series, Bunch.Min );
    else
      Str = Str + 
        " №№ с " + RegNumToStr( Bunch.Series, Bunch.Min ) + 
        " по " + RegNumToStr( Bunch.Series, Bunch.Max );
    end;
    RecFound = Next( Bunch );
  end;
  return Str;
end;

macro PrintSplitting

  var
    Total = 0,
    Sum,
    Stat = true,
    No = 1, 
    RecFound;

  [---------------------------------------------------------------------------------];
  [| № | Наименование ценностей и ценных бланков, вложенных в сумку |     Сумма    |];                    
  [---------------------------------------------------------------------------------];
  
  ClearRecord( BagList );
  BagList.Branch = Bag.Branch;  
  BagList.Date = Bag.Date;
  BagList.ClientType = Bag.ClientType;
  BagList.ClientID = Bag.ClientID;
  BagList.Operation = Bag.Operation;
  BagList.Number = Bag.Number;
  RecFound = GetGE( BagList );
  while ( RecFound and Stat
    and ( BagList.Branch == Bag.Branch )
    and ( BagList.Date == Bag.Date )
    and ( BagList.ClientType == Bag.ClientType )
    and ( BagList.ClientID == Bag.ClientID )
    and ( BagList.Operation == Bag.Operation )
    and ( BagList.Number == Bag.Number ) )
    Stat = FindCashValue( BagList.ValueRef );
    if ( Stat )
      if ( BagList.RealSum )
        GetCurrCodeAndCBRate( BagList.ValueRef );
        Sum = BagList.RealSum;    
        if ( CurrCode != 0 )
          Sum = Cur2Rub( Sum );
        end; 
      else
        Sum = Money( BagList.RealItems );
      end;
      [ ### ############################################################ ############## ]
      (
        No,
        ( CashValue.NameValue + RegNumRangeStr ):w,
        Sum   
      );
      Total = Total + Sum;    
      No = No + 1;
    end;
    RecFound = Next( BagList );
  end;
  if ( not Stat )
    MsgBox( "Ошибка при печати описи" );
    Exit( -1 );
  end;
  [---------------------------------------------------------------------------------];
  [                                                   И т о г о :    ############## ]
  ( Total );
  [        ##### #################################################### ]
  ( "Руб.", String( Total:m ):w );
  [ ];
  [ Заведующий ( контролер ) ________________________ Кассир ______________________ ];
end;

macro PrintCopy( CopyNo )

  var
    Sum;

  GetSum( Sum );
  SplitSum( Sum );

  if ( CopyNo == 1 )                                                                
    [  1 |                                                                            ];     
    [экз.| СОПРОВОДИТЕЛЬНАЯ ВЕДОМОСТЬ                             ------------------- ];                            
    [    | на ценности и ценные бланки, подлежащие                | 0482119 (ф.119) | ];
    [    | доставке утром следующего дня                          ------------------- ];
    [    | ###########################################                                ]
    ( Bag.Date:m );
    [    |                                                        ------------------- ];
    [    | В учреждение Сбербанка ############################# № | ############### | ]
    ( RcvName, RcvShortName );
    [    | -------------------------------------------------------------------------- ];
    [    |                                                | Исправную и надлежаще оп- ];
    [    |                                                | ломбированную сумку № ### ]
    ( Bag.Number );
    [    | Сумка № ##### на сумму ###########             | без пересчета вложенных в ]
    ( Bag.Number, Sum );
    [    | ############################################## | в нее ценностей и ценных  ]
    ( SumStrArr( 0 ) );
    [    | ############################################## | бланков приняли:          ]
    ( SumStrArr( 1 ) );
    [    | Заведующий ( контролер ) _____________________ | Заведующий ( контролер )  ];
    [    |                                                | _________________________ ];
    [    | Кассир _______________________________________ | Кассир __________________ ];
    [    |                                                |                           ];   
    [    |                                                | М.П.                      ];   
    [    |                                                |                           ];   
    [    |                                                | ######################### ]
    ( Bag.Date:m );   
    [    |                                                |                           ];   
    [    |                                                |                           ];   
    [    |                                                |                           ];   
  elif ( CopyNo == 2 )
    [  2 |                                                                            ];     
    [экз.| КОПИЯ СОПРОВОДИТЕЛЬНОЙ ВЕДОМОСТИ                       ------------------- ];                            
    [    | на ценности и ценные бланки, подлежащие                | 0482119 (ф.119) | ];
    [    | доставке утром следующего дня                          ------------------- ];
    [    | ###########################################                                ]
    ( Bag.Date:m );
    [    |                                                        ------------------- ];
    [    | В учреждение Сбербанка ############################# № | ############### | ]
    ( RcvName, RcvShortName );
    [    | -------------------------------------------------------------------------- ];
    [    |                                                | Указанные на обороте цен- ];
    [    |                                                | ности и ценные бланки     ];
    [    | Сумка № ##### на сумму ###########             | оприходованы              ]
    ( Bag.Number, Sum );
    [    | ############################################## | ######################### ]
    ( SumStrArr( 0 ), Bag.Date:m );
    [    | ############################################## |                           ]
    ( SumStrArr( 1 ) );
    [    | Заведующий ( контролер ) _____________________ | Заведующий ( контролер )  ];
    [    |                                                | _________________________ ];
    [    | Кассир _______________________________________ | Кассир __________________ ];
    [    |                                                |                           ];   
    [    |                                                |                           ];   
    [    |                                                |                           ];   
    [    |                                                |                           ];   
    [    |                                                |                           ];   
    [    |                                                |                           ];   
    [    |                                                |                           ];   
  else
    [  3 |                                                                            ];     
    [экз.| КВИТАНЦИЯ                                              ------------------- ];                            
    [    | на ценности и ценные бланки, подлежащие                | 0482119 (ф.119) | ];
    [    | доставке утром следующего дня                          ------------------- ];
    [    | ###########################################                                ]
    ( Bag.Date:m );
    [    |                                                        ------------------- ];
    [    | В учреждение Сбербанка ############################# № | ############### | ]
    ( RcvName, RcvShortName );
    [    | -------------------------------------------------------------------------- ];
    [    |                                                | Опломбированную сумку     ];
    [    |                                                | № ###                     ]
    ( Bag.Number );
    [    | Сумка № ##### на сумму ###########             | без пересчета вложенных в ]
    ( Bag.Number, Sum );
    [    | ############################################## | в нее ценностей и ценных  ]
    ( SumStrArr( 0 ) );
    [    | ############################################## | бланков принял :          ]
    ( SumStrArr( 1 ) );
    [    | Заведующий ( контролер ) _____________________ | Инкассатор спецбанка      ];
    [    |                                                |                           ];
    [    | Кассир _______________________________________ | _________________________ ];
    [    |                                                |                           ];   
    [    |                                                | Штамп спецбанка           ];   
    [    |                                                |                           ];   
    [    |                                                | ######################### ]
    ( Bag.Date:m );   
    [    |                                                |                           ];   
    [    |                                                |                           ];   
    [    |                                                |                           ];   
  end; 

  [ ];
  [ ];
  if ( CopyNo != 3 )
    PrintSplitting;
  end;
  PrintLn( StrFor( 12 ) );
end;        
  
  if ( ( Bag.Operation == CASHOP_PANT_RCRD ) and ( Bag.RouteID != 0 ) )
    CheckPersonType( PT_OPER );
  end;

  GetClientInfo( Bag.ClientType, Bag.ClientID, RcvName, null, RcvShortName );
  GetAccounts( DebetAcc, CreditAcc );
  if ( GetTrue( false, "Печатать ф.119 ?" ) )
    PrintCopy( 1 );
    PrintCopy( 2 );
    PrintCopy( 3 );
  end;    
end;