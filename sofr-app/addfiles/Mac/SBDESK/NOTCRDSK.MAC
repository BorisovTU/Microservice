/*
  RS-Bank Сбербанка
  Касса филиала

  Список неподтвержденных документов на сегодня

  04.09.97
  Ромодин Александр Васильевич
*/
import BankInter,rep_desk,deprintr, rsd;

file doc ("sb_casdc.dbt");
file val ("sb_casvl.dbt");

const TYPEDOC_CARRYOFF 	= 1;	/* Отложенный документ 	*/
const ALG_CASHNUMOPER 	= 803;	/* Номер операции	*/
const ALG_FLAGDEBCRED	= 804;	/* Дебет/Кредит		*/

var {curdate},
     Branch = NumFNCash,
     NcashDoc = 0;

/***********************************************************/

macro Разделитель_1
  [-------------------------------------------------------------------------------------------------]
end;
/***********************************************************/

macro Заголовок_отчета
  [ ];
  [                 Список неподтвержденных кассовых документов за ##########]({curdate});
  [ ];
  Разделитель_1();
  [|Номер|  Кассир    |Ресурс|     Операция              |       Сумма             | Кол-во |Дебет/|];
  [|     |            |      |                           |      (деньги)           |(штуки) |Кредит|];
  Разделитель_1();
/*  ##### ############  ####  ########################### ######################### ######## ######*/
end;
/***********************************************************/

macro Идентификатор_ценности (RefValue)
  var IdentValue;
  KeyNum (val,0);
  val.RefValue = RefValue;
  if (GetEQ(val))
    IdentValue = val.IdentValue;
  else
    IdentValue = "";
  end;
  return IdentValue;
end;
/***********************************************************/

macro Строка_отчета
  var IdentValue = Идентификатор_ценности(doc.RefValue);
  var NumOper = GetNameAlg (ALG_CASHNUMOPER,doc.NumOper);
  var DebetCredit = GetNameAlg (ALG_FLAGDEBCRED,doc.FlagDebCredOper);
  [ ##### ############  ####  ########################### ######################### ######## ######]
   (doc.NumbDocumCash,doc.CashOper,
    IdentValue,
    NumOper,
    doc.ValueMoney,doc.ValueCol,
    DebetCredit);
end;
/***********************************************************/

macro  Конец_отчета
  Разделитель_1 ();
  [ ];
end;
/***********************************************************
************************************************************/

macro Цикл_по_документам
/*
  var stat;

  KeyNum (doc,0);
  doc.Branch = Branch;
  doc.CashDateDoc = {curdate};
  doc.TypeDoc =  TYPEDOC_CARRYOFF;
  doc.CashOper = "";
  doc.RefValue = 0;
  stat = GetGE (doc);
  while (stat)
    if ( (doc.Branch != Branch) OR (doc.CashDateDoc != {curdate}) OR
     (doc.TypeDoc != TYPEDOC_CARRYOFF))
      stat = FALSE;
    else
      Строка_отчета ();
      NcashDoc = NcashDoc+1;
      stat = Next (doc);
    end;
  end;
*/

  var IdentValue, NumOper, DebetCredit;

  var cmd = RsdCommand(" select t_RefValue RefValue, t_NumOper NumOper, t_FlagDebCredOper FlagDebCredOper, t_NumbDocumCash NumbDocumCash, t_CashOper CashOper, t_ValueMoney ValueMoney, t_ValueCol ValueCol " +
                       " from dsb_casdc_dbt " + 
                       " where t_Branch = ? " +
                         " and t_CashDateDoc = ? " +
                         " and t_TypeDoc = ? " );
  cmd.addParam( "Branch", RSDBP_IN );
  cmd.value( "Branch" ) = Branch;
  cmd.addParam( "CurDate", RSDBP_IN );
  cmd.value( "CurDate" ) = {curdate};
  cmd.addParam( "TypeDoc", RSDBP_IN );
  cmd.value( "TypeDoc" ) = TYPEDOC_CARRYOFF;

  cmd.execute;;
  var rs = RsdRecordset( cmd );

  while ( rs.moveNext )
    IdentValue = Идентификатор_ценности(rs.value("RefValue"));
    NumOper = GetNameAlg (ALG_CASHNUMOPER,rs.value("NumOper"));
    DebetCredit = GetNameAlg (ALG_FLAGDEBCRED,rs.value("FlagDebCredOper"));

  [ ##### ############  ####  ########################### ######################### ######## ######]
   (rs.value("NumbDocumCash"),
    rs.value("CashOper"),
    IdentValue,
    NumOper,
    rs.value("ValueMoney"), 
    rs.value("ValueCol"),
    DebetCredit);

    NcashDoc = NcashDoc+1;
  end;

end;

macro Список_непотв_кас_док

  Заголовок_отчета ();
  Цикл_по_документам ();
  Конец_отчета ();

 return NcashDoc;
end;

/***********************************************************/
