/*-RSL---------------------------------------- R-Style Software Lab --*/
/*------- Акт ревизии денежной наличности и других ценностей ---------*/
/*--------------------- (ф. №18 СБ РФ) -------------------------------*/
/* RS-Bank      подсистема Кладовая банка             RA 22-02-02     */

Import DeprIntr, 
       "define.mac", "RD_Comm.mac";

file DeskSubj ( "desksubj.dbt", "sbbank.def" ) key 1;
file CashVal  ( "sb_casvl.dbt", "sbbank.def" ) key 3;
file CashVal_ ( "sb_casvl.dbt", "sbbank.def" ) key 5;
file CashAcc  ( "sb_casac.dbt", "sbbank.def" ) key 1;
file RegNum   ( "reg_num.dbt" , "sbbank.def" ) key 0;
file DeskAux  ( "desk_aux.dbt", "sbbank.def" ) key 0;
file Alg      ( "namealg.dbt" , "bank.def"   ) key 0;
file CIMisc   ( "ci_misc.dbt" , "sbbank.def" ) key 0;

record IngotAux( "desk_aux.1" , "sbbank.def" );

const ПечататьРублевыйЭквивалентВалюты = true, /* ЭТОТ ПАРАМЕТР МОЖНО МЕНЯТЬ */
      ПечататьМассыДрагметаллов        = true; /* ЭТОТ ПАРАМЕТР МОЖНО МЕНЯТЬ */

const {NationalCur} = 0;

const 
      CI_OGSZ              =   1,
      CI_LOTTERY           =   2,
      CI_CERT              =   3,
      CI_COIN              =   4,
      CI_INGOT             =   5,
      CI_ORVVZ             =   6,
      CI_MISC              =  10,
      
      ALG_CASH_VAL_TYPE    = 811, /* Типы ресурсов кассы в namealg.dbt */

      DST_PANTRY           =   5; /* Кладовая отделения */

const
      CK_NEW               = 0, 
      CK_OLD               = 1;

MACRO ЗаголовокАктаРевизии()

  var dep_name;

  findDepartment(Branch, dep_name);

[                                                                                        ];
[  Сбербанк России                                                                       ];
[  ________#_________                                                            ф. №18  ]( dep_name:c );
[  Наименование филиала                                                                  ];
[  Сбербанка России                                                                      ];
[                                                                                        ];
[                                                                                        ];
[                                                                                        ];
[                                       А К Т                                            ];
[                  Ревизии денежной наличности и других ценностей                        ];
[                                                                                        ];
[                                                                                        ];
[#___________________________________________________ в _________________________________]( {Name_Bank} );
[    наименование подразделения                                                          ];
[Ревизия произведена на основании _______________________________________________________];
[В связи с ______________________________________________________________________________];
[________________________________________________________________________________________];
[<<____>>  __________________  ______ г.     по   <<____>> __________________ 200__ г.   ];
[                                                                                        ];
[________________________________________________________________________________________];
[________________________________________________________________________________________];
[в присутствии __________________________________________________________________________];
[________________________________________________________________________________________];
[                                                                                        ];
[По состоянию на #                   оказалось:                                          ]( StrAddZeroDate( {curdate} ) );
[                                                                                        ];
[+----------+------------------------------+--------------+--------------+--------------+];
[| № счета  |         Наименование         | Фактическое  |   По книге   |  Примечание  |];
[|          |                              |   наличие    |   кладовой,  |              |];
[|          |                              |  ценностей   | ф.24, ф.25-а |              |];
[+----------+------------------------------+--------------+--------------+--------------+];

END;

/*
MACRO WorkDeskName( Oper, Brigade )
  var Name, name1;

  if ( Oper == 0 )
    Name = "0";
  else
    Name = string(Oper);    Name = mkstr( "0", 4 - strlen(Name )) + Name;
    name1= string(Brigade); name1= mkstr( "0", 2 - strlen(name1)) + name1;
    Name = Name + "\/" + name1;
  end;
  return Name;  
END;

MACRO GetNativeSafe( OperBrigade )
  file Group("rtgroup.dbt") key 2;

  Group.Branch = Branch;
  Group.Date   = {curdate};
  Group.ID     = OperBrigade;
  if(GetEQ(Group))
    return Group.SafeID;
  else
    MsgBox("Не найден сейф бригады № ", OperBrigade);
    Exit;
  end;
END;

/* Название кассы ( сейфа смены или кассы кассира ) */
MACRO GetCashName( Oper, Brigade )
  var Value;
  var RegPath  = "RS-RETAIL\\СЕРВИСНЫЕ\\КАССА\\АВТОМАТИЗАЦИЯ_СТОЛА_КАССИРА";
  var CashName = "";
  if ( GetRegistryValue( RegPath, V_INTEGER, Value, null ) == V_INTEGER )
    if ( Value )
      CashName = WorkDeskName( Oper, Brigade );
    else
      CashName = GetNativeSafe( Brigade );
    end;
  else
    exit(1);
  end;
  return CashName;
END;
*/

MACRO FindCashAcc()
  ClearRecord( CashAcc );
  CashAcc.Branch   = Branch;
  CashAcc.RefValue = CashVal.RefValue;
  CashAcc.Date     = {curdate};
  CashAcc.CashOper = DeskSubj.Name; /*GetCashName( {oper}, GetOperBrigade() );*/
  
  return GetEQ( CashAcc );
END;

/* Получить название типа ресурса кассы */
MACRO GetCashTypeName( Type )
  Alg.iTypeAlg   = ALG_CASH_VAL_TYPE;
  Alg.iNumberAlg = Type;
  if(GetEQ(Alg))
    return Alg.szNameAlg;
  else
    return "???";
  end;
END;

/* Сформировать название ресурса кассы */
MACRO MakeValueName()
  var Name = CashVal.NameValue;
  if( CashVal.Type != TYPE_RES_PAY )
    Name = Name + "\n(" + GetCashTypeName( CashVal.Type ) + ")";
  end;
  return Name;
END;


MACRO FindDeskAux( ValueRef, Date, Series, Number )

  DeskAux.Branch = NumFNCash;
  DeskAux.ValueRef = ValueRef;
  DeskAux.Date = Date;
  DeskAux.IsGone = 0;
  DeskAux.Series = Series;
  DeskAux.Number = Number;
  if ( GetEQ( DeskAux ) )
    return true;
  else
    DeskAux.IsGone = 1;
    return GetEQ( DeskAux );
  end;
END;


MACRO СтрокаАктаРевизии( NСчета, Наименование, Количество )
[|##########|##############################|              |############# |              |] 
 ( NСчета,   Наименование:w,   Количество );
END;

MACRO СтрокаРазделитель()
[+----------+------------------------------+--------------+--------------+--------------+];
END;


MACRO FindCIMisc( Type, IssueID )
  CIMisc.Type    = Type;
  CIMisc.IssueID = IssueID;
  return GetEQ( CIMisc );
END;


MACRO IsNewCoins()
  return(     FindCIMisc( CI_COIN, CashVal.CodIntValue )
          and ( CIMisc.KindOfCoin == CK_NEW )
        );
END;


MACRO IsOldCoins()
  return(     FindCIMisc( CI_COIN, CashVal.CodIntValue )
          and ( CIMisc.KindOfCoin == CK_OLD )
        );
END;


MACRO CheckValForm()
  return true;
END;


MACRO CalcWeight()

  var 
      RecFound,
      Weight = 0;
  
  if(     ПечататьМассыДрагметаллов
     and ( CashVal.TypeValue    == TYPERES_MINCAPISSUE + CI_INGOT )
     and ( CashVal.HasRegNumber == SET_CHAR                       )
    )

    ClearRecord( RegNum );
    RegNum.Branch   = CashAcc.Branch;
    RegNum.CashOper = CashAcc.CashOper;
    RegNum.SubAccount = "";
    RegNum.ValueRef = CashAcc.RefValue;
    RegNum.Date     = CashAcc.Date;
    RecFound = GetGE( RegNum );

    while (     RecFound
          and ( RegNum.Branch   == CashAcc.Branch   )
          and ( RegNum.CashOper == CashAcc.CashOper )
          and ( RegNum.SubAccount == "" )
          and ( RegNum.ValueRef == CashAcc.RefValue )
          and ( RegNum.Date     == CashAcc.Date     )
          )
      while( RegNum.Min <= RegNum.Max )
        if( FindDeskAux( RegNum.ValueRef, RegNum.Date, RegNum.Series, RegNum.Min ) )
          SetRecordAddr( IngotAux, DeskAux, 0, FldOffset( DeskAux, "Info" ), true );
          Weight = Weight + ( IngotAux.RealWeight / 10000 ); /* подсчет массы */
        end;
        RegNum.Min = RegNum.Min + 1;
      end;
      RecFound = Next( RegNum );
    end;

  СтрокаАктаРевизии( "", "масса, г.", Weight );
  end;
END;


MACRO PrintRegNum()
  
  var 
      RecFound,
      Name = "";
  
  if(   ( CashVal.TypeValue    == TYPERES_VALFORM )
     and( CashVal.HasRegNumber == SET_CHAR      ) )

    ClearRecord( RegNum );
    RegNum.Branch   = CashAcc.Branch;
    RegNum.CashOper = CashAcc.CashOper;
    RegNum.SubAccount = "";
    RegNum.ValueRef = CashAcc.RefValue;
    RegNum.Date     = CashAcc.Date;
    RecFound = GetGE( RegNum );

    while (     RecFound
          and ( RegNum.Branch   == CashAcc.Branch   )
          and ( RegNum.CashOper == CashAcc.CashOper )
          and ( RegNum.SubAccount == "" )
          and ( RegNum.ValueRef == CashAcc.RefValue )
          and ( RegNum.Date     == CashAcc.Date     )
          )

      Name = RegNum.Series + " " + RegNum.Min;
      if( RegNum.Min != RegNum.Max )
        Name = Name + "-" + RegNum.Max;
      end;
      СтрокаАктаРевизии( "", Name, RegNum.Max - RegNum.Min + 1 );

      RecFound = Next( RegNum );
    end;

  end;
END;


MACRO PrintRubSum()
  var CBRate = 0, Количество;
  if(     ПечататьРублевыйЭквивалентВалюты 
      and (CashVal.CodIntValue != {NationalCur} )
    )
    GetAllCurrRate( {curdate}, CashVal.CodIntValue, CBRate, null, null );
    Количество = SumConv( CashAcc.RestMoney, CBRate );
    СтрокаАктаРевизии( "", "(в рублях по курсу "+CBRate+")", Количество );
  end;
END;


/* Ценности по заданным значениям Type, TypeValue, CodIntValue */
MACRO ЦенностиПоВиду( Type, TypeValue, CodIntValue, Filter, Func )

  var 
      RecFound,
      RecFound2,
      Количество,
      NoFilter = true,
      HasFunc  = false;

  if( ValType( Filter ) != V_UNDEF )
    NoFilter = false;
  end;

  if( ValType( Func ) != V_UNDEF )
    HasFunc = true;
  end;
      
  ClearRecord( CashVal );
  CashVal.Type        = Type;
  CashVal.TypeValue   = TypeValue;
  if( CodIntValue == -1 )
    CashVal.CodIntValue = 0;
  else
    CashVal.CodIntValue = CodIntValue;
  end;

  RecFound = GetGE( CashVal );

  while(    RecFound
      and ( CashVal.Type        == Type        )
      and ( CashVal.TypeValue   == TypeValue   )
      and ((CashVal.CodIntValue == CodIntValue ) or (CodIntValue == -1))
       )
    if( NoFilter or ExecMacro2( Filter ) )
    
      ClearRecord( DeskSubj );
      DeskSubj.Branch = Branch;
      DeskSubj.Type   = DST_PANTRY;
      RecFound2 = GetGE( DeskSubj );

      while(     RecFound2
           and ( DeskSubj.Branch == Branch     )
           and ( DeskSubj.Type   == DST_PANTRY )
           )

        if( FindCashAcc() )
          if ( CashAcc.RestMoney )
            Количество = CashAcc.RestMoney;
          else
            Количество = /*$1 * */ CashAcc.RestCol;
          end;
          СтрокаАктаРевизии( "", MakeValueName(), Количество );
          if( HasFunc )
            ExecMacro2( Func );
          end;
          СтрокаРазделитель();
        end;

        RecFound2 = Next( DeskSubj );
      end;
    end;
    RecFound = Next( CashVal );
  end;

END;


MACRO Is_ValForm()
  return ( CashVal_.TypeValue == TYPERES_VALFORM )
END;


/* Ценности, учитываемые по данному счету */
MACRO ЦенностиПоСчету( Глава, Счет, Filter )

  var 
      RecFound,
      RecFound2,
      Количество,
      NoFilter = true;
  
  if( ValType( Filter ) != V_UNDEF )
    NoFilter = false;
  end;
  
  Глава = CodeFor(Глава) - CodeFor("А");

  ClearRecord( CashVal_ );
  CashVal_.FlagIntoBalance = Глава;
  
  RecFound = GetGE( CashVal_ );

  while(    RecFound
      and ( CashVal_.FlagIntoBalance == Глава )      
       )
    if( NoFilter or ExecMacro2( Filter ) )
      copy( CashVal, CashVal_ );

      ClearRecord( DeskSubj );
      DeskSubj.Branch = Branch;
      DeskSubj.Type   = DST_PANTRY;
      RecFound2 = GetGE( DeskSubj );

      while(     RecFound2
           and ( DeskSubj.Branch == Branch     )
           and ( DeskSubj.Type   == DST_PANTRY )
           )

        if( FindCashAcc() )
          if ( CashAcc.RestMoney )
            Количество = CashAcc.RestMoney;
          else
            Количество = /*$1 * */ CashAcc.RestCol;
          end;
          СтрокаАктаРевизии( "", MakeValueName(), Количество );
          СтрокаРазделитель();
        end;

        RecFound2 = Next( DeskSubj );
      end;
    end;
    RecFound = Next( CashVal_ );
  end;

END;


MACRO ДноАктаРевизии()

[                                                                                        ];
[  В ходе ревизии проверено: правильность оформления кассовых документов, ведения книг   ];
[учета ценностей, а также соблюдение лимитов и минимально допустимого остатка денежной   ];
[наличности; недостатков не установлено;  установлены недостатки, отмеченные в           ];
[прилагаемой справке (ненужное зачеркнуть).                                              ];
[                                                                                        ];
[  Особые замечания ревизующих (если имеются):                                           ];
[________________________________________________________________________________________];
[________________________________________________________________________________________];
[                                                                                        ];
[                                                                                        ];
[  Ключи и печати от кладовой ценностей сдал _________________ (_______________________) ];
[                                     принял _________________ (_______________________) ];
[                                                                                        ];
[Должности и подписи                                                                     ];
[                                                                                        ];
[__________________________________________   ______________________________             ];
[                                                                                        ];
[__________________________________________   ______________________________             ];
[                                                                                        ];
[__________________________________________   ______________________________             ];
[                                                                                        ];
[__________________________________________   ______________________________             ];
[                                                                                        ];
[__________________________________________   ______________________________             ];
[                                                                                        ];
[                                                                                        ];
[Акт составлен  <<____>>  __________________  ______ г.                                  ];
[                                                                                        ];
[С данными бухгалтерского учета сверено                                                  ];
[<<____>>  __________________  200__ г.  _______________________                         ];
[                                         (подпись ревизующего)                          ];

END;


ЗаголовокАктаРевизии();

СтрокаАктаРевизии( "20202", "Наличные деньги", "" );
СтрокаРазделитель();
/* Наличные деньги */
ЦенностиПоВиду( TYPE_RES_PAY, TYPERES_CURRENCY, -1, null, @PrintRubSum );
/* Монеты по стоимости */
ЦенностиПоВиду( TYPE_RES_PAY, TYPERES_MINCAPISSUE + CI_COIN , -1, @IsNewCoins );

СтрокаАктаРевизии( "20308", "Монеты и драгоценные металлы в монетах", "" );
СтрокаРазделитель();
/* Монеты по массе */
ЦенностиПоВиду( TYPE_RES_PAY, TYPERES_MINCAPISSUE + CI_COIN , -1, @IsOldCoins );

/* Драгметаллы */
ЦенностиПоВиду( TYPE_RES_PAY, TYPERES_MINCAPISSUE + CI_INGOT, -1, null, @CalcWeight );

СтрокаАктаРевизии( "", "Всего ценностей", "" );
СтрокаРазделитель();

/* Ценные бумаги др. эмитентов  */
ЦенностиПоСчету( "Б", "90801", @Is_ValForm );

/* Ценные бумаги для продажи    */
ЦенностиПоСчету( "Б", "90802", @Is_ValForm );

/* ОГСЗ */
ЦенностиПоВиду( TYPE_RES_PAY, TYPERES_MINCAPISSUE + CI_OGSZ , -1 );

/* ОРВВЗ */
ЦенностиПоВиду( TYPE_RES_PAY, TYPERES_MINCAPISSUE + CI_ORVVZ, -1 );

/* Оплаченные лотерейные билеты */
ЦенностиПоСчету( "В", "90905", @Is_ValForm );

/* Оплаченные ПД */
ЦенностиПоВиду( TYPE_RES_PDNAT , TYPERES_PAYDOC  , -1 );

/* Оплаченные валютой ПД */
ЦенностиПоВиду( TYPE_RES_PDCUR , TYPERES_PAYDOC  , -1 );

/* Оплаченные рублями ПД */
ЦенностиПоВиду( TYPE_RES_PD    , TYPERES_PAYDOC  , -1 );

/* ПД на инкассо */
ЦенностиПоВиду( TYPE_RES_INCASS, TYPERES_PAYDOC  , -1 );

/* валюта на инкассо */
ЦенностиПоВиду( TYPE_RES_INCASS, TYPERES_CURRENCY, -1 );

/* ПД на экспертизу */
ЦенностиПоВиду( TYPE_RES_EXP   , TYPERES_PAYDOC  , -1 );

/* валюта на экспертизу */
ЦенностиПоВиду( TYPE_RES_EXP   , TYPERES_CURRENCY, -1 );

/* оплаченные займы и сертификаты */
ЦенностиПоСчету( "В", "90704", @Is_ValForm );

/* ценные бланки на хранении */
ЦенностиПоСчету( "Б", "90803", @Is_ValForm );

/* Неплатежная валюта */
ЦенностиПоВиду( TYPE_RES_NOPAY , TYPERES_CURRENCY, -1 );

/* Неплатежные ПД */
ЦенностиПоВиду( TYPE_RES_NOPAY , TYPERES_PAYDOC,   -1 );

СтрокаАктаРевизии( "", "Ценные бланки", "" );
СтрокаРазделитель();
/* Ценные бланки */
ЦенностиПоВиду( TYPE_RES_PAY, TYPERES_VALFORM, -1, @CheckValForm, @PrintRegNum );

ДноАктаРевизии();
