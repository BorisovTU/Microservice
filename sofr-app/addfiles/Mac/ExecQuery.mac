/********************************************************************************/
/* DESCRIPTION  :   Вывод результатов sql-запроса в Excel                       */
/* PROGRAMMED BY:   Белохонов П.В.                                              */
/* CREATION DATE:   12.04.2010                                                  */
/********************************************************************************/

import "cb_sql.mac", "or_rep_h.mac";

/*Запрос по умолчанию*/
//PRIVATE VAR sql = "select * from dparty_dbt where t_partyid < 10" ;
PRIVATE VAR sql = "" ;

private var Dt, cmd, i = 0,count = 0, Choice, Con; 
private var Rep:CMakeReport;
private var Fields = TArray(), MenuValues   = TArray();

const Env = RsdEnvironment("RDDrvO","RDDrvO.dll");

private macro sstring(Val);
  var s = "";
  var stat = 0;
  s = string(val);
  if (index(s,".") <= 0)
     return s;
  end;

  while (index(s,".") > 0)
     if ((substr(s,strlen(s),1) == ".") or (substr(s,strlen(s),1) == "0"))
        s = substr(s,1,strlen(s)-1);
     else
        return s;
     end;
  end;
  return s;
end;

/*Печать строки*/
PRIVATE MACRO PrintStr(Ar);
   var i = 0;
   while (i < Ar.size)
     if (ValType(Ar[i]) == 28)
       Rep.AddPrintCell (sstring(Ar[i]), 0, 0, "l");
     elif (ValType(Ar[i]) == 26/*SpecVal*/)
       Rep.AddPrintCell ("NULL", 0, 0, "l");
     else
       Rep.AddPrintCell (Ar[i], 0, 0, "l");
     end;
     i = i + 1;
   end;
   Rep.AddStr (true);
END;

/*Формирование шапки таблицы по sql-запросу*/
PRIVATE MACRO CreateHead(sql);
  macro MakeLine(n)/*линия заданной длины*/
    var i = 0, s= "";
    while (i < n)
      s = s + "─";
      i = i + 1;
    end;
    return s;
  end;

  var Head = "┌", cmd, rs, Count = 0;
               
  cmd = RsdCommand(Con, sql);
  Rs  = RsdRecordSet(cmd);
  if (Rs.movenext())
    Count = rs.FldCount;
    i = 0;
    while (i < Count) /*┌─...─┬───────┬─ ...─┐\n*/
       Head = Head + MakeLine(StrLen(Rs.fld(i).Name));
       i = i + 1;
       if (i == Count)
          Head = Head + "┐\n";
       else
          Head = Head + "┬";
       end;
    end;                                     
    i = 0;                                
    while (i < Count) /*│ИМЯ_ПОЛЯ_1│ИМЯ_ПОЛЯ2│ ... │\n*/
       Head = Head + "│" + Rs.fld(i).Name;
       i = i + 1;
       if (i == Count)
          Head = Head + "│\n├";
       end;                 
    end;                    
    i = 0;
    while (i < Count) /*│...─┼───────┼─ ...─┤\n*/
       Head = Head + MakeLine(StrLen(Rs.fld(i).Name));
       i = i + 1;
       if (i == Count) 
          Head = Head + "┤";
       else                    
          Head = Head + "┼";
       end;               
    end;
  end;
  return Head;
END;

/*************************************************/
/*   Точка входа                                 */
/*************************************************/

/*Задаем меню выбора и получаем строку соединения*/
MenuValues[0] = "Для буферной схемы";
MenuValues[1] = "Для дистрибутивной схемы";
Choice = Menu(MenuValues, "Выбор схемы");
if (Choice == 0)
   Con = RsdConnection(Env, GetIniString( "CONNSTRING_RPL", "rsreq.ini" ));
else
   Con = RsdConnection(Env, GetIniString( "CONNSTRING", "rsreq.ini" ));
end;

if( GetString(sql, "Введите текст sql-запроса"));
  if (sql == "")
     MsgBox("Необходимо ввести запрос");
     break;
  elif((strupr(substr(trim(sql),1,6)) == "INSERT") or (strupr(substr(trim(sql),1,6)) == "UPDATE"))
     MsgBox("Нельзя выполнять изменения в базе через эту форму");
     break;
  end;
  cmd = RsdCommand(Con, sql);
  Dt  = RsdRecordSet(cmd);
  InitProgress(SQL_GetNRecs(sql), "Обработка записей");
  Rep = CMakeReport(CreateHead(sql));

  while (Dt.movenext())/*По записям*/
    i = 0;
    Fields = TArray();
    while (i < Dt.FldCount)/*По полям записи*/
      Fields[Fields.size] = dt.value(i);
      i = i + 1;
    end;
    PrintStr(Fields);
    UseProgress(count = count + 1);
    println;
  end;

  RemProgress;

  if (Count == 0)  /*Если ничего не выбрано*/
     Rep.AddPrintCell ("По запросу не выбрано ни одной записи", 50, 0, "l:ex_FS(b):ex_FZ(12)", REP_ELEM_STR);
  end;

//  println(sql);
//  Rep.PrintRep();/*При необходимости можно выаодить в текстовом виде, но на некоторых данных может слегка падать*/
  Rep.PrintWinRep();
  Rep.ShowWinRep();

end;


exit(1);

OnError(er)
 if (er.Code != 0)
 msgbox("ОШИБКА! \n Неверный синтаксис запроса или неверная схема.");
 exit(1);
 end;
