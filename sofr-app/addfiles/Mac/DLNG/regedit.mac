/**
 @file      regedit.mac
 @brief     Макрос редактирования настроек

 # tag
 - code_type:API

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |07.04.2025 |Фаршатов Г.К.  |BOSS-5795                                       |Создан

*/

import global_classes, dlquery, func_lib;

private macro GetKeyReg(Str)
  var query, cmd, ds;
  var KeyId = -1;

  query = "SELECT RSB_Common.RSI_GetRegParm( ? ) as Key FROM dual";
  
  cmd =  DL_RSDCommand(query);
  cmd.addParam( Str );
  
  ds = cmd.execute();
  
  if(ds.moveNext())
    KeyId = ds.Key;
  end;
  
  return KeyId;
end;

private macro HasValForYear(KeyID, Year)
  var query, cmd, ds;

  query = "SELECT 1 " +
          "  FROM dregparm_dbt rg, dregparmtag_dbt tg, dregparmstory_dbt st " +
          " WHERE rg.t_KeyID = ? " +
          "   AND tg.t_KeyID = rg.t_KeyID " +
          "   AND tg.t_Tag1 = 'TAX' " +
          "   AND st.t_KeyID = tg.t_KeyID " +
          "   AND EXTRACT(YEAR FROM(st.t_DateBegin)) = ? ";
  
  cmd =  DL_RSDCommand(query);
  cmd.addParam( KeyID );
  cmd.addParam( Year );
  
  ds = cmd.execute();
  
  if(ds.moveNext())
    return true;
  end;
  
  return false;
end;

private macro getLastBegDate(KeyID)
  var query, cmd, ds;

  query = "SELECT max(t_DateBegin) as LastBegDate " +
          "  FROM dregparmstory_dbt " +
          " WHERE t_KeyID = ? ";
  
  cmd =  DL_RSDCommand(query);
  cmd.addParam( KeyID );
  
  ds = cmd.execute();
  
  if(ds.moveNext())
    return ds.LastBegDate;
  end;
  
  return Date(0,0,0);
end;

/**
 @brief Проверка налоговой настройки
 @param [in] KeyID ID Настройки
 @param [in] DateVal Дата введенная пользователем
 @return false - Успех, true - Ошибка
*/
macro CheckTaxReg(KeyID:INTEGER, DateVal:@DATE)
  var stat = false;
  var CurYear = 0, YearVal = 0;
  
  DateSplit({curdate}, null, null, CurYear);
  DateSplit(DateVal  , null, null, YearVal);

  if( (stat == false) and ((GetKeyReg("COMMON\\НДФЛ\\УЧЕТ ЗАТРАТ ПРОШЛЫХ ЛЕТ") == KeyID) or (GetKeyReg("COMMON\\НДФЛ\\ВКЛ. КОМ. С ПРОД. В РАСЧ. ДОХ.") == KeyID) or (GetKeyReg("COMMON\\НДФЛ\\ТОЧНОСТЬ_КЦБ_ДЛЯ_ИНВЕСТВЫЧЕТА") == KeyID)) )
    DateVal = date(1,1,YearVal);
  end;

  if( (stat == false) and (DateVal <= getLastBegDate(KeyID)) )
    MsgBox("Дата начала действия нового значения пересекается с периодом действия предыдущего значения");
    stat = true;
  end;

  if( (stat == false) and ((GetKeyReg("COMMON\\НДФЛ\\ВКЛ. КОМ. С ПРОД. В РАСЧ. ДОХ.") == KeyID) or (GetKeyReg("COMMON\\НДФЛ\\УЧЕТ ЗАТРАТ ПРОШЛЫХ ЛЕТ") == KeyID) or (GetKeyReg("COMMON\\НДФЛ\\ТОЧНОСТЬ_КЦБ_ДЛЯ_ИНВЕСТВЫЧЕТА") == KeyID)) )
    var addMSG = "";

    if ((CurYear == YearVal) and (HasValForYear(KeyID, CurYear) == false)) 
      addMSG = " Для корректного расчета НОБ потребуется пересчет за "+YearVal+" год.";
    end;

    if(MsgBoxEx("Значение настройки вступит в силу с 01.01."+YearVal+"."+addMSG+" Вы уверены, что хотите изменить настройку?",  MB_YES + MB_NO, IND_NO) == IND_NO)
      stat = true;
    end;
  end;



  return stat
end; 