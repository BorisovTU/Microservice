/*
$Name:        dlactinacc.mac
$Module:      Ядро Securities
$Description: Отчет "Акты сверки наличия подтверждающих документов"
*/
IMPORT "DLReport_h.mac", "dlmisc.mac", "dlactinacc_form.mac", "dldlngfun.mac";

PRIVATE CONST POI_MODE = TRUE;
PRIVATE CONST TABLE_ROW_LIMIT = MAX_ROWSHEET_XLSX - 48000;

PRIVATE VAR TableHeader =
"┌─────────────┬─────────────┬─────────────┬─────────────────────┬────────────────────┬─────────────────────┬────────────────────────────────────┬──────────────────┬─────────────────────────────────────────────────────────────────────────────────────────────┬─────────────────────────┐\n"+
"│     Код     │    Дата     │    Время    │     Вид операции    │      Вид ФИ        │       Код ФИ        │        Расчеты по сделке           │      Клиент      │                                       Вид расхождения                                       │   Причина расхождения   │\n"+
"│   операции  │ регистрации │ регистрации │                     │                    │                     ├──────────────────┬─────────────────┤                  │                                                                                             │                         │\n"+
"│             │             │             │                     │                    │                     │        вид       │        №        │                  │                                                                                             │                         │\n"+
"│             │             │             │                     │                    │                     │                  │                 │                  │                                                                                             │                         │\n"+
"├─────────────┼─────────────┼─────────────┼─────────────────────┼────────────────────┼─────────────────────┼──────────────────┼─────────────────┼──────────────────┼─────────────────────────────────────────────────────────────────────────────────────────────┼─────────────────────────┤";



PRIVATE CLASS (DL_CReportTemplate) DL_ActInAcc_Report

  var m_DepID, m_DepCode, m_ContrNumber:string = "";
  var m_CurrSheetName = "", m_NoData = true, m_PrintDep = false;
  var m_SheetCount = 0;
  var m_rowLimitCounter = 0;
  const m_templSheetName = "Акт сверки наличия документов";
  PRIVATE MACRO GetDataFromFormFields()
     DLActInAccData.IsUseBO        = m_Form.GetFieldValue(PNFLD_ACTINACC_MODULE_BO);
     DLActInAccData.IsUseDV        = m_Form.GetFieldValue(PNFLD_ACTINACC_MODULE_DV);
     DLActInAccData.IsUseVA        = m_Form.GetFieldValue(PNFLD_ACTINACC_MODULE_VA);
     DLActInAccData.BeginDate      = m_Form.GetFieldValue(PNFLD_ACTINACC_BEGINDATE);
     DLActInAccData.EndDate        = m_Form.GetFieldValue(PNFLD_ACTINACC_FINISHDATE);
     DLActInAccData.ClientID       = m_Form.GetFieldValue(PNFLD_ACTINACC_CLIENT);
     DLActInAccData.ContractID     = m_Form.GetFieldValue(PNFLD_ACTINACC_CONTRACT, @m_ContrNumber);
     DLActInAccData.CountDay       = m_Form.GetFieldValue(PNFLD_ACTINACC_COUNTDAY);
     DLActInAccData.IsExportToExel = m_Form.GetFieldValue(PNFLD_ACTINACC_TO_EXEL);
     DLActInAccData.DepartmentID   = m_Form.GetFieldValue(PNFLD_ACTINACC_DEPARTMENT);
  END;

  
  private macro DateDoc( DocId_Or_XLD:String, GroundKind:Integer):Date
    var cmd,qvery, Dataset,dateReg;
    if (GroundKind == -1)//значит, передан t_SPGROUNDID
    qvery="SELECT T_SIGNEDDATE  FROM dspground_dbt  WHERE T_SPGROUNDID = ? ";
    else//значит, передан номер t_XLD
     qvery="SELECT T_SIGNEDDATE  FROM dspground_dbt  WHERE T_XLD = ? AND T_KIND = ? ";    
    end;
    cmd=RSDCommand(qvery);
    cmd.addParam("",RSDBP_IN, DocId_Or_XLD);
    if (GroundKind != -1)
       cmd.addParam("",RSDBP_IN, GroundKind);
    end;
    cmd.execute();
    Dataset = TRsbDataSet(cmd);
    Dataset.movenext();
    dateReg=Dataset.SIGNEDDATE;
    return dateReg;
  end;

  PRIVATE MACRO PrintJournal()//печать в журнал актов сверки
     var REPKIND_DOCS = 3;
     var query = " insert into DDL_JOURNAL_DBT " +
                 "   (T_DATE, T_TIME, T_REPDATE, T_OPER, T_REPKIND, T_RESULT ) " +
                 " VALUES " +
                 "   ( ?,?,?,?,?,? ) ";
     var sqlQuery = RSDCommand(query); 

     sqlQuery.addParam("", RSDBP_IN, DATE());//программная дата проведения сверки 
     sqlQuery.addParam("", RSDBP_IN, Time());//программное время проведения сверки
     sqlQuery.addParam("", RSDBP_IN, m_Form.GetFieldValue(PNFLD_ACTINACC_FINISHDATE) );//конец отчетного периода
     sqlQuery.addParam("", RSDBP_IN, {oper});//операционист
     sqlQuery.addParam("", RSDBP_IN, REPKIND_DOCS);
     sqlQuery.addParam("", RSDBP_IN, IIF(m_NoData, 0 , 1) );
     sqlQuery.execute();
END;
  
  MACRO GetAuditMsg():String
	GetDataFromFormFields();
	var msg:string = "";
	var beginDate:date = DLActInAccData.BeginDate;
	var endDate:date = DLActInAccData.EndDate;
    var deals1:string = "";
    var deals2:string = "";
	var deals3:string = "";

    deals1 = IIF(DLActInAccData.IsUseBO == "X",
                 "Модуля \"Бэк-офис ценных бумаг\"\n",
                 "");

    deals2 = IIF(DLActInAccData.IsUseVA == "X",
                 "Модуля \"Учтенные векселя\"\n",
                 "");
				 
	deals3 = IIF(DLActInAccData.IsUseDV == "X",
                 "Модуля \"ФИССиКО\"\n",
                 "");

    msg = 
    "Отчет:              Акт сверки наличия документов\n\n" +
    "Отчетный период:          с " + beginDate + " по " + endDate + "\n" +
    "По сделкам:\n"  +
    deals1 + deals2 + deals3 +
    "\n----------------------------------------\n";

    return msg;
  END;

  PRIVATE MACRO PrintMode():INTEGER
     if( m_Form.GetFieldValue(PNFLD_ACTINACC_TO_EXEL) == SET_CHAR )
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  END;

  PRIVATE MACRO BeginReport()
     GetDataFromFormFields();
     Dl_CallInsertStat(PrintMode());
     CreateTotalBook();
  END;

  PRIVATE MACRO EndReport()
     SaveTotalBook();
  END;

  PRIVATE MACRO PrintHeader()
    var Header = "Дата проведения сверки: # #\n"+
		   "Филиал: #\n"+
                 "АКТ О ПРОВЕДЕНИИ СВЕРКИ НАЛИЧИЯ ДОКУМЕНТОВ, ПОДТВЕРЖДАЮЩИХ РАСЧЕТНУЮ ОПЕРАЦИЮ ВУ\n"+
                 "по состоянию на #\n" +
                 "#\n";

    var ClientTxt:string = "";

    if( (DLActInAccData.ClientID > 0) and (DLActInAccData.ContractID > 0) )
       ClientTxt = "Клиент " + DL_getPartyShortName(DLActInAccData.ClientID);
          ClientTxt = ClientTxt + " договор № " + m_ContrNumber;
       end;

    PrintFormatString( Header,
			  "Date" ,DATE(),
			  "Time" ,TIME(),
                       "N1_H1_1", DL_GetDepartmentNameByCode(m_DepCode),
                       "N1_H1_2", DLActInAccData.EndDate,
                       "N1_H2_1", ClientTxt
                     );
    CopyAllSheetInTotalBook(m_templSheetName, false, "N1_RepHeader", 0, m_CurrSheetName, true);
  END;

  PRIVATE MACRO PrintMainFooter()
    var Footer = "\n\nСотрудник, ответственный за" +
                 "\nведение внутреннего учета:                      # " +
                 "\n__________________                     ___________________" +
                 "\n(должность)       (подпись)   (фамилия)\n" +
                 "\nСотрудник Службы внутреннего контроля:\n" +
                 "\n________________________                     ___________________  /__________________/" +
                 "\n(должность)        (подпись)  (фамилия)";
    PrintFormatString(Footer, "N1_F1",{Name_Oper});
    CopyAllSheetInTotalBook(m_templSheetName, false, "N1_Footer", 1, m_CurrSheetName, true);
  END;

  private macro EndPrintTable()
    if( m_PrintDep )
       EndTable();
       CopyAllSheetInTotalBook(m_templSheetName, false, GetTableRange(), 0, m_CurrSheetName, true);
       PrintMainFooter();
       m_NoData = false;
    end;
  end;

  MACRO ПечататьСтрокуТаблицы( A1, A2, A3, A5, A6, A7, A8, A9, A10, A11, A12 )

     if (m_rowLimitCounter >= TABLE_ROW_LIMIT)
       m_rowLimitCounter = 0;
       EndPrintTable();
       m_SheetCount = m_SheetCount + 1;
       m_PrintDep = false;
     end;

     if( not m_PrintDep )
        m_CurrSheetName = DL_GetDepartmentNameByCode(m_DepCode) + IIF(m_SheetCount > 0, "("+m_SheetCount+")","");
        UseNewSheetInTotalBook();
        PrintHeader();

        RegisterTable( "N1_MainTable", TableHeader,
                       "N1_A1",
                       "N1_A2",
                       "N1_A3",
                       "N1_A5",
                       "N1_A6",
                       "N1_A7",
                       "N1_A8",
                       "N1_A9",
                       "N1_A10",
                       "N1_A11",
                       "N1_A12" );

        m_PrintDep = true;
     end;

     PrintTableLine( "N1_A1",  A1,  null,
                     "N1_A2",  A2,  null,
                     "N1_A3",  A3,  null,
                     "N1_A5",  A5,  null,
                     "N1_A6",  A6,  null,
                     "N1_A7",  A7,  null,
                     "N1_A8",  A8,  null,
                     "N1_A9",  A9,  null,
                     "N1_A10", A10, null,
                     "N1_A11", A11, null,
                     "N1_A12", A12, null
                   );
     m_rowLimitCounter = m_rowLimitCounter + 1;
  END;        

  PRIVATE MACRO ПечататьДанные()
  
    MACRO getBOfficeStringList()
       var Delim = false;
       var retStr = "";
       if( DLActInAccData.IsUseBO == SET_CHAR )
          retStr = retStr + IIF(Delim, ",", "") + string(OBJTYPE_BACKOFFICE_SP); 
          Delim = true;
       end;
       
       if( DLActInAccData.IsUseDV == SET_CHAR )
          retStr = retStr + IIF(Delim, ",", "") + string(OBJTYPE_BACKOFFICE_DV);
          Delim = true;
       end;
       
       if( DLActInAccData.IsUseVA == SET_CHAR )
          retStr = retStr + IIF(Delim, ",", "") + string(OBJTYPE_BACKOFFICE_VA);
          Delim = true;
       end;
       return retStr;
    END;
  
    MACRO получитьСелектРучныхРОВУ()
    var Query = " SELECT CAST( dl_acc.t_Ground AS varchar2(20) ) Ground,           " +/*каст в строку для совместимости со запросом по РОВУ по сделкам*/
                "        -1              GroundKind," +//без типа - значит ID
                "        dl_acc.t_ID,               " +
                "        dl_acc.t_DealID,           " +
                "        dl_acc.t_Code,             " +
                "        inacc.t_SysDate,           " +
                "        inacc.t_SysTime,           " +
                "        lvalues.t_Name OperName,   " +
                "        fikind.t_Name t_FIKindName," +
                "        fi.t_FI_Code,              " +   
                "        RSB_SECUR.GetDealTypeName(dl_acc.t_DealType) DealTypeName, " +
                "        NVL(tick.t_DealCode, chr(1)) DealCode,                     " +
                "        dl_acc.t_Client      Client,                               " +
                "        dl_acc.t_ClientContr ClientContr,                          " +
                "        dl_acc.t_Corrective,                                       " +
                "        (select t_ShortName from dparty_dbt where t_PartyID = dl_acc.t_Client) as ClientShortName,  " +
                "        (SELECT T_SIGNEDDATE FROM dspground_dbt                                                     " +
                "         WHERE T_XLD = dl_acc.t_Ground AND T_KIND = -1 and rownum = 1) as DateDoc                   " +
                "   FROM ddl_acc_dbt dl_acc, ddlinacc_dbt inacc, dllvalues_dbt lvalues, dfikinds_dbt fikind, dfininstr_dbt fi, ddl_tick_dbt tick " +
                "  WHERE dl_acc.t_Department = ?                                                                                                 " +  
                "    AND dl_acc.t_Client > 0                                                                                                     " +
                "    AND dl_acc.t_Client = (case when ? = -1 then dl_acc.t_Client else ? end)                                                    " +
                "    AND dl_acc.t_ClientContr = (case when ? = -1 then dl_acc.t_ClientContr else ? end)                                          " +
                "    AND dl_acc.t_BOffice in("+getBOfficeStringList()+")                                                                         " +
                "    AND lvalues.t_List     = " + string(OBJTYPE_CALCOPKIND)                                                                       +
                "    AND lvalues.t_Element  = dl_acc.t_Opertype                                                                                  " +
                "    AND fikind.t_FI_Kind   = dl_acc.t_FIKind                                                                                    " +
                "    AND fi.t_FIID          = dl_acc.t_FIID                                                                                      " +
                "    AND tick.t_DealID   (+)= dl_acc.t_DealID                                                                                    " +
                "    AND inacc.t_DocumentKind = dl_acc.t_DocKind                                                                                 " +
                "    AND inacc.t_DocumentID = dl_acc.t_ID                                                                                        " +
                "    AND inacc.t_FIID       = dl_acc.t_FIID                                                                                      " +
                "    AND inacc.t_Opertype   = dl_acc.t_Opertype                                                                                  " +
                "    AND dl_acc.t_Date   >= ? "+//TO_DATE( ? ,'dd.mm.yyyy')                                                                                                   " +
                "    AND dl_acc.t_Date   <= ? ";//TO_DATE( ? ,'dd.mm.yyyy')                                                                                                  " ;

        return Query;           
    END;
    MACRO получитьСелектРОВУпоСделкам()
    var Query = " SELECT                                                                                                     " +
                "         inacc.t_GroundNum Ground,                                                                          " + 
                "         inacc.t_GroundKind GroundKind,                                                                     " +
                "         tick.t_DealID ID,                                                                                  " + /* похоже, совпадает с 'кодом операции' для РОВУ по Сделкам */
                "         tick.t_DealID,                                                                                     " +
                "         trn.t_Numb_Document Code,                                                                          " +
                "         inacc.t_SysDate,                                                                                   " +
                "         inacc.t_SysTime,                                                                                   " +
                "         lvalues.t_Name OperName,                                                                           " +
                "         fikind.t_Name t_FIKindName,                                                                        " +
                "         fi.t_FI_Code,                                                                                      " +
                "         RSB_SECUR.GetDealTypeName(tick.t_DealType) DealTypeName,                                           " +
                "         NVL(tick.t_DealCode, chr(1)) DealCode,                                                             " +
                "         tick.t_ClientID Client,                                                                            " + 
                "         tick.t_ClientContrID ClientContr,                                                                  " +
                "         chr(0) Corrective,                                                                                 " +
                "         (select t_ShortName from dparty_dbt where t_PartyID = tick.t_ClientID) as ClientShortName,         " +
                "         (SELECT T_SIGNEDDATE FROM dspground_dbt                                                            " +
                "          WHERE T_XLD = inacc.t_GroundNum AND T_KIND = inacc.t_GroundKind and rownum = 1) as DateDoc        " +
                "   FROM  ddlinacc_dbt  inacc,                                                                           " +
                "         dacctrn_dbt trn,                                                                               " + 
                "         ddl_tick_dbt   tick,                                                                           " +
                "         dllvalues_dbt lvalues,                                                                         " +
                "         dfikinds_dbt fikind,                                                                           " + 
                "         dfininstr_dbt fi                                                                               " +
                "  WHERE  tick.t_Department = ?                                                                          " +
                "         AND tick.t_ClientID      > 0                                                                   " +                        
                "         AND tick.t_ClientID      = (case when ? = -1 then tick.t_ClientID else ? end)                  " +                                        
                "         AND tick.T_CLIENTCONTRID = (case when ? = -1 then tick.T_CLIENTCONTRID else ? end)             " +                                      
                "         AND lvalues.t_List     = " + string(OBJTYPE_CALCOPKIND)                                          +
                "         AND lvalues.t_Element  = inacc.t_Opertype                                                      " +
                "         AND fikind.t_FI_Kind   = inacc.t_FIKind                                                        " +
                "         AND fi.t_FIID          = inacc.t_FIID                                                          " +
                "         AND inacc.t_DocumentID = tick.t_DealID                                                         " +
                "         AND inacc.t_Date   >= ? "+//TO_DATE( ? ,'dd.mm.yyyy')                                                                     " +
                "         AND inacc.t_Date   <= ? "+//TO_DATE( ? ,'dd.mm.yyyy')                                                                     " +
                "         AND inacc.t_Boffice in( "+getBOfficeStringList()+")                                            " +
                "         AND inacc.t_AcctrnID   = trn.t_AcctrnID                                                        " ;
         return Query;
    END;
  
    MACRO добавитьПараметрыДляСелектРучныхРОВУ( src: @RSDCommand)
        src.addParam("", RSDBP_IN, m_DepCode);
        src.addParam("", RSDBP_IN, DLActInAccData.ClientID);
        src.addParam("", RSDBP_IN, DLActInAccData.ClientID);
        src.addParam("", RSDBP_IN, DLActInAccData.ContractID);
        src.addParam("", RSDBP_IN, DLActInAccData.ContractID);
        src.addParam("", RSDBP_IN, DLActInAccData.BeginDate);
        src.addParam("", RSDBP_IN, DLActInAccData.EndDate);
    END;
    MACRO добавитьПараметрыДляСелектРОВУпоСделкам( src: @RSDCommand)
        src.addParam("", RSDBP_IN, m_DepCode);
        src.addParam("", RSDBP_IN, DLActInAccData.ClientID);
        src.addParam("", RSDBP_IN, DLActInAccData.ClientID);
        src.addParam("", RSDBP_IN, DLActInAccData.ContractID);
        src.addParam("", RSDBP_IN, DLActInAccData.ContractID);
        src.addParam("", RSDBP_IN, DLActInAccData.BeginDate);
        src.addParam("", RSDBP_IN, DLActInAccData.EndDate);
    END;

    var Count:integer = 0, Num:integer = 0; 
    var A11:string = "";
    var sqlQuery = "";

    sqlQuery = sqlQuery + получитьСелектРучныхРОВУ();
    sqlQuery = sqlQuery + " UNION ALL ";    
    sqlQuery = sqlQuery + получитьСелектРОВУпоСделкам();    
    sqlQuery = sqlQuery + " ORDER BY t_SysDate, Client, ClientContr ";

    var NRecSelect = RSDCommand("select count(1) nRecs from(" + sqlQuery + ")");
    добавитьПараметрыДляСелектРучныхРОВУ(@NRecSelect);
    добавитьПараметрыДляСелектРОВУпоСделкам(@NRecSelect);
    NRecSelect.execute();
    var NRecData = TRsbDataSet(NRecSelect);

    if( NRecData.MoveNext() )
       Count = int(NRecData.nRecs);
    end;

    if( Count > 0 )

       var sqlSelect = RSDCommand(sqlQuery);
       добавитьПараметрыДляСелектРучныхРОВУ(@sqlSelect);
       добавитьПараметрыДляСелектРОВУпоСделкам(@sqlSelect);
       sqlSelect.execute();
       var RepData = TRsbDataSet(sqlSelect);

       InitProgress(Count, "Просмотр РОВУ", "Просмотр РОВУ");
     
       while( RepData.MoveNext() )

          A11 = "";

          if( SQL_ConvTypeInteger(RepData.Ground) <= 0 )
             if( (DLActInAccData.EndDate - SQL_ConvTypeDate(RepData.SysDate)) > DLActInAccData.CountDay )
                A11 = "Нет данных о подтверждающем документе! Истек срок ввода данных о документе: <" + string(DLActInAccData.CountDay) + "> дней с момента регистрации операции.";
             else
                A11 = "Нет данных о подтверждающем документе! Cрок ввода подтверждающего документа истекает " + string(DLActInAccData.EndDate + DLActInAccData.CountDay) + ".";
             end;
          else
             if ( SQL_ConvTypeDate(RepData.DateDoc) == date(0,0,0))
                A11 = "Не найден подтверждающий документ с номером "+RepData.Ground;
             elif( (SQL_ConvTypeDate(RepData.DateDoc) - SQL_ConvTypeDate(RepData.SysDate)) > DLActInAccData.CountDay )
                A11 = "Подтверждающий документ зарегистрирован несвоевременно.";
             end;
          end;

          if( SQL_ConvTypeStr(RepData.Corrective) == SET_CHAR )
             A11 = A11 + IIF(A11 != "", " ", "") + "Корректирующая операция.";
             var Note = readNoteForObject(OBJTYPE_CALCOPER, DL_LZ(SQL_ConvTypeInteger(RepData.ID), 10), 1);
             if( (ValType(Note) != V_UNDEF) and (Note != "") )
                A11 = A11 + " Причина корректировки: " + string(Note);
             end;
          end;

          if( A11 != "" )
             ПечататьСтрокуТаблицы( SQL_ConvTypeStr(RepData.Code),
                                    SQL_ConvTypeDate(RepData.SysDate),
                                    SQL_ConvTypeTime(RepData.SysTime),
                                    SQL_ConvTypeStr(RepData.OperName),
                                    SQL_ConvTypeStr(RepData.FIKindName),
                                    SQL_ConvTypeStr(RepData.FI_Code),
                                    IIF(SQL_ConvTypeInteger(RepData.DealID) > 0, SQL_ConvTypeStr(RepData.DealTypeName), ""),
                                    SQL_ConvTypeStr(RepData.DealCode),
                                    SQL_ConvTypeStr(RepData.ClientShortName),
                                    A11,
                                    ""
                                  );
          end;

          UseProgress( Num = Num + 1 );
       end;

       RemProgress();


    end;
    
    //Плюс проводки вручную
    //для этого - отбираются все проводки по датам, где:
    //доп параметры присутствуют, но T_DOCUMENTID (РОВУ):  отсутствует

    var trn_state_done = 1;

                         
    var Select_Query_2 = " SELECT DISTINCT trn.t_Numb_Document, inacc.t_Date,  inacc.t_Groundnum, inacc.t_GroundKind GroundKind, lvalues.t_Name OperName, fikind.t_Name t_FIKindName, fi.t_FI_Code " + 
                              " , accntD.t_Client Party_Deb, accntC.t_Client Party_Cred " +
                         "   FROM  ddlinacc_dbt inacc,    " +
                         "         dllvalues_dbt lvalues, " +
                         "         dfikinds_dbt fikind,   " + 
                         "         dfininstr_dbt fi,      " +
                         "         dacctrn_dbt trn,       " + 
                         "         daccount_dbt accntD,   " +
                         "         daccount_dbt accntC    ";
    var Where_Querysql_2 =" WHERE inacc.t_Department = ? "
                         "   AND inacc.t_Date >= ? " +
                         "   AND inacc.t_Date <= ? " +
                         "   AND lvalues.t_List     = " + string(OBJTYPE_CALCOPKIND) +
                         "   AND trn.t_AcctrnID = inacc.t_AcctrnID                                                                            " +
                         "   AND lvalues.t_Element  = inacc.t_Opertype                                                                        " +
                         "   AND fikind.t_FI_Kind   = inacc.t_Fikind                                                                          " +
                         "   AND fi.t_FIID          = inacc.t_FIID                                                                            " +
                         "   AND inacc.t_DocumentID <= 0 " + //без ссылки на операцию. в противном случае работает выборка из РОВУ, созданная ранее
                         "   AND inacc.t_PartyID    <= 0 " + //аналогично.
                         //поскольку неизвестно, кто является клиентом в ручной проводке (а клиент для отчёта может быть задан!), то нужно посмотреть по счетам.
                         "   AND accntD.t_Account = inacc.t_Debacc                                                                            " +
                         "   AND accntC.t_Account = inacc.t_Kredacc                                                                           " +
                         "   AND   (     accntD.t_Client = " + IIF ( DLActInAccData.ClientID == -1, "accntD.t_Client", DLActInAccData.ClientID) + 
                         "           OR  accntC.t_Client = " + IIF ( DLActInAccData.ClientID == -1, "accntC.t_Client", DLActInAccData.ClientID) + 
                         "         )                                                                                                          " +
                         "   AND inacc.t_BOffice in( " + getBOfficeStringList() + ") ";
                   /* +  "  GROUP BY inacc.t_AcctrnID " ;*/
               

    
    sqlQuery = Select_Query_2 + Where_Querysql_2;

    var cmd = DL_RSDCommand(sqlQuery);
    cmd.AddParam(m_DepCode);
    cmd.AddParam(DLActInAccData.BeginDate);
    cmd.AddParam(DLActInAccData.EndDate);

    var DataSet = cmd.Execute();
    while(DataSet.moveNext())
       if( SQL_ConvTypeInteger(DataSet.Groundnum) <= 0 )
          if( (DLActInAccData.EndDate - SQL_ConvTypeDate(DataSet.t_Date)) > DLActInAccData.CountDay )
             A11 = "Нет данных о подтверждающем документе! Истек срок ввода данных о документе: <" + string(DLActInAccData.CountDay) + "> дней с момента регистрации операции.";
          else
             A11 = "Нет данных о подтверждающем документе! Cрок ввода подтверждающего документа истекает " + string(DLActInAccData.EndDate + DLActInAccData.CountDay) + ".";
          end;
       else
          if (  DateDoc(SQL_ConvTypeInteger(RepData.Ground), RepData.GroundKind) ==null )
                A11 = "Не найден подтверждающий документ с номером "+RepData.Ground;        
          elif ((DateDoc(SQL_ConvTypeInteger(DataSet.Groundnum), DataSet.GroundKind) - SQL_ConvTypeDate(DataSet.t_Date)) > DLActInAccData.CountDay )
             A11 = "Подтверждающий документ зарегистрирован несвоевременно.";
          end;
       end;

       if( A11 != "" )
          //проверка на то, что проводка клиентская. если оба счета клиентских, не известно кто клиент. Выводим обоих.
          if (DLActInAccData.ClientID == -1)
          ПечататьСтрокуТаблицы(    SQL_ConvTypeStr(DataSet.t_Numb_Document),
                                    SQL_ConvTypeDate(DataSet.t_Date),
                                    "",//SQL_ConvTypeTime(DataSet.SysTime),
                                    SQL_ConvTypeStr(DataSet.OperName),
                                    SQL_ConvTypeStr(DataSet.FIKindName),
                                    SQL_ConvTypeStr(DataSet.FI_Code),
                                    "",//IIF(SQL_ConvTypeInteger(RepData.DealID) > 0, SQL_ConvTypeStr(RepData.DealTypeName), ""),
                                    "",//SQL_ConvTypeStr(RepData.DealCode),
                                    DL_getPartyShortName(DataSet.Party_Deb),
                                    A11,
                                    ""
                                  );
          if (DataSet.Party_Cred != DataSet.Party_Deb) //если проводка между счетами разных лиц
          ПечататьСтрокуТаблицы(    SQL_ConvTypeStr(DataSet.t_Numb_Document),
                                    SQL_ConvTypeDate(DataSet.t_Date),
                                    "",//SQL_ConvTypeTime(DataSet.SysTime),
                                    SQL_ConvTypeStr(DataSet.OperName),
                                    SQL_ConvTypeStr(DataSet.FIKindName),
                                    SQL_ConvTypeStr(DataSet.FI_Code),
                                    "",//IIF(SQL_ConvTypeInteger(RepData.DealID) > 0, SQL_ConvTypeStr(RepData.DealTypeName), ""),
                                    "",//SQL_ConvTypeStr(RepData.DealCode),
                                    DL_getPartyShortName(DataSet.Party_Cred),
                                    A11,
                                    ""
                                  );
          end;
          else//если же клиент задан, то
          ПечататьСтрокуТаблицы(    SQL_ConvTypeStr(DataSet.t_Numb_Document),
                                    SQL_ConvTypeDate(DataSet.t_Date),
                                    "",//SQL_ConvTypeTime(DataSet.SysTime),
                                    SQL_ConvTypeStr(DataSet.OperName),
                                    SQL_ConvTypeStr(DataSet.FIKindName),
                                    SQL_ConvTypeStr(DataSet.FI_Code),
                                    "",//IIF(SQL_ConvTypeInteger(RepData.DealID) > 0, SQL_ConvTypeStr(RepData.DealTypeName), ""),
                                    "",//SQL_ConvTypeStr(RepData.DealCode),
                                    DL_getPartyShortName(DLActInAccData.ClientID),
                                    A11,
                                    ""
                                  );
          end;
       end;
    end;
    
    EndPrintTable();
  END;

  PRIVATE MACRO ПоФилиалу()
    m_PrintDep = false;
    ПечататьДанные();
  END;

  /*на каждый филиал отдельная вкладка*/
  PRIVATE MACRO ПоФилиалам()

    var Query, cmd, cmdData, NRecSql, NRecSqlRsd, NRec = 0, Num = 0;

    Query = " select * from ddp_dep_dbt order by t_code ";

    NRecSql = RSDCommand("select count(1) NRec from ("+Query+")");
    NRecSql.execute();
    NRecSqlRsd = TRsbDataSet(NRecSql);

    if( NRecSqlRsd.MoveNext() and (NRecSqlRsd.NRec!=null) )
       NRec = int(NRecSqlRsd.NRec);
    end;

    if( NRec > 0 )
       cmd = RSDCommand(Query);
       cmd.execute();
       cmdData = TRsbDataSet(cmd);

       InitProgress(NRec, "Просмотр филиалов", "Просмотр филиалов");
       while( cmdData.Movenext() )
          m_DepID   = cmdData.PartyID;
          m_DepCode = cmdData.Code;
          ПоФилиалу();
          UseProgress(Num = Num + 1);
       end;
       RemProgress();
    end;

  END;

  PRIVATE MACRO Create()

    var cmd, cmdData;
    
    SetActiveSheet("Акт сверки наличия документов");
    m_NoData = true;

    if( DLActInAccData.DepartmentID > 0 )
       cmd = RSDCommand(" select * from ddp_dep_dbt where t_Code = ?");
       cmd.addParam("", RSDBP_IN, DLActInAccData.DepartmentID);
       cmd.execute();
       cmdData = TRsbDataSet(cmd);
       if( cmdData.MoveNext() )
          m_DepID   = cmdData.PartyID;
          m_DepCode = DLActInAccData.DepartmentID;
          ПоФилиалу();
       end;
    else
       ПоФилиалам();
    end;

    if( m_NoData == true )
       PrintFormatString("#", "N1_ReportRunFalse", "Нет данных для формирования отчета.");
       CopyAllSheetInTotalBook(null, false, "N1_ReportRunFalse", 0, null, true);
    end;
    
    PrintJournal();
  END;

  initDL_CReportTemplate(DLActInAcc_Panel(), "Report_ActInAcc.xlsx", true, null, null, POI_MODE, true);
END;
var R = DL_ActInAcc_Report();
R.Run(null, "QWERTY"/*затычка для автоформирования лог-аудита. второй параметр НЕ нуль*/ );
exit(1);
