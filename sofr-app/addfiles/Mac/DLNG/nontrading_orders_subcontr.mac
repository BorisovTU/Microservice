//nontrading_orders_subcontr.mac

import oralib, likepy, globals;

//Субдоговор обсулживания
//Использовать только в рамках операции списания/зачисления/перевода денежных средств
class NonTradingOrderSubcontr(_sfContrID:integer)
  private var sfContrID:integer = _sfContrID;
  private var marketID:integer  = null;
  private var servKind:integer  = null;
  private var isEDPvalue:bool   = null;
  private var ekk:string        = "";
  private var mpCode:string     = "";
  private var contrNum:string   = "";
  private var firmID:string     = "";

  private macro Init()
    var query = "select c.t_servkind, "
              + "       rshb_rsi_sclimit.SfcontrIsEDP(p_SfcontrID => c.t_id) is_edp "
              + "  from dsfcontr_dbt c "
              + " where c.t_id = :sfcontrid ";
    var sql = ExecSQLselectPrmDyn(query, sfContrID);
    if (sql.MoveNext()) 
      servKind   = sql.value("t_servkind");
      isEDPvalue = sql.value("is_edp") == 1;
    end;

  end;

  macro GetServKind()
    return servKind;
  end;

  macro IsEDP()
    return isEDPvalue;
  end;

  //lazy init
  macro GetMarketID()
    if (marketID != null)
      return marketID;
    end;
    var query = "select m.t_marketid "
              + "  from ddlcontrmp_dbt m "
              + " where m.t_sfcontrid = :sfcontrid ";
    var sql = ExecSQLselectPrmDyn(query, sfContrID);
    if (sql.MoveNext())
      marketID = sql.value("t_marketid");
    end;
    return marketID;
  end;

  //lazy init
  macro GetMpCode():string
    if (mpCode != "") 
      return mpCode;
    end;
    var query = "select m.t_mpcode "
              + "  from ddlcontrmp_dbt m "
              + " where m.t_sfcontrid = :contrid ";
    var sql = ExecSQLselectPrmDyn(query, sfContrID);
    if (sql.MoveNext())
      mpCode = sql.value("t_mpcode");
    end;

    return mpCode;
  end;

  //lazy init
  macro GetEkk():string
    if (ekk != "") 
      return ekk;
    end;
    var query = "select c.t_code "
              + "  from ddlcontrmp_dbt m "
              + "  join ddlobjcode_dbt c on c.t_objectid = m.t_dlcontrid "
              + "                        and c.t_codekind = 1 "
              + "                        and c.t_objecttype = 207 "
              + " where (   c.t_bankclosedate = to_date('01.01.0001', 'dd.mm.yyyy') "
              + "        or c.t_bankclosedate > sysdate) "
              + "  and m.t_sfcontrid = :contrid ";
    var sql = ExecSQLselectPrmDyn(query, sfContrID);
    if (sql.MoveNext())
      ekk = sql.value("t_code");
    end;

    return ekk;
  end;

  //lazy init
  macro GetContrNum():string
    if (contrNum != "") 
      return contrNum;
    end;
    
    var query = "select s.t_number "
              + "  from ddlcontrmp_dbt m "
              + "  join ddlcontr_dbt d on d.t_dlcontrid = m.t_dlcontrid "
              + "  join dsfcontr_dbt s on s.t_id = d.t_sfcontrid "
              + " where m.t_sfcontrid = :contrid ";
    var sql = ExecSQLselectPrmDyn(query, sfContrID);
    if (sql.MoveNext())
      contrNum = sql.value("t_number");
    end;

    return contrNum;
  end;

  //lazy init
  macro GetFirmID()
    if (firmID != "")
      return firmID;
    end;

    var query = "select t_firmid "
              + "  from ddlcontrmp_dbt m "
              + " where m.t_sfcontrid = :contrid";
    var sql = ExecSQLselectPrmDyn(query, sfContrID);
    if (sql.MoveNext())
      if (valtype(sql.value("t_firmid")) != 26)
        firmID = sql.value("t_firmid");
      end;  
    end;

    return firmID;
  end;

  Init();
end;

//Возвращает экземпляр класса NonTradingOrderSubcontr по договору обсулживания.
//Обязательные параметры: вид обслуживания и подвид обслуживания
macro NewNonTradingOrderSubcontrByContr (sfContrID:integer, servKind:integer, servKindSub:integer, marketID:integer)
  var sfContr:NonTradingOrderSubcontr;
  var query = "select s.t_id "
            + "  from ddlcontr_dbt c "
            + "  join ddlcontrmp_dbt m on m.t_dlcontrid = c.t_dlcontrid "
            + "  join dsfcontr_dbt s on s.t_id = m.t_sfcontrid "
            + " where c.t_sfcontrid = :contrid "
            + "   and s.t_servkind = :servkind "
            + "   and s.t_servkindsub = :servkindsub "
            + "   and m.t_marketid = :marketid ";
  var sql = ExecSQLselectPrmDyn(query, sfContrID, servKind, servKindSub, marketID);
  if (sql.MoveNext()) 
    sfContr = NonTradingOrderSubcontr(sql.value("t_id"));
  end;

  return sfContr;
end;

//Возвращает субдоговор по номеру счёта
//Если ЕДП, то берётся субдоговор фонды, т.к. у всех субдоговоров один счёт
macro NewNonTradingOrderSubcontrByAccount(account:string, dt:date)
  var sfContr:NonTradingOrderSubcontr;

  var query = "select m.t_clientcontrid "
            + "  from dmcaccdoc_dbt m "
            + "  join dsfcontr_dbt s on s.t_id = m.t_clientcontrid "
            + "  join ddlcontrmp_dbt mp on mp.t_sfcontrid = s.t_id "
            + " where m.t_account = :acc "
            + "   and m.t_catid = 70 "
            + "   and m.t_dockind = 3001 "
            + "   and m.t_activatedate < :dt "
            + "   and (m.t_disablingdate > :dt2 or m.t_disablingdate = to_date('01.01.0001', 'dd.mm.yyyy')) "
            + "   and (   rshb_rsi_sclimit.SfcontrIsEDP(p_SfcontrID => s.t_id) != 1 "
            + "        or     s.t_servkind = 1 "
            + "           and mp.t_marketid = 2 "
            + "        ) ";
  var sql = ExecSQLselectPrmDyn(query, account, dt, dt);
  if (sql.MoveNext()) 
    sfContr = NonTradingOrderSubcontr(sql.value("t_clientcontrid"));
  end;

  return sfContr;
end;