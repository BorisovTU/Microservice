/** 
 @file dlnptxiissetting.mac 
 @brief Настроечная таблица для шаблона ОКЗ
  
 Настроечная таблица для шаблона ОКЗ

 #menu
 - БО ЦБ/ <Налоговый учет> / <НДФЛ> / <Настройки для прогрессивной шкалы> / <Настроечная таблица для шаблона ОКЗ>

 # tag 
 - functional_block:Налоги_НДР
 - code_type:GUI
 - AVT_BOSS-467


 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------
 |2025.10.27 |Фаршатов Г. К. |AVT_BOSS-467     | Реализация             
 |           |               |                 |                   

*/
IMPORT DealsInter, SPInter, PTInter, globals;
IMPORT RsbDataSet, "dlquery.mac";
import bnk_common;

import "sp_categ.mac";

/* Уже заполненные структуры, все изменения отразятся в базах */
PRIVATE VAR Документ         = TRecHandler( "nptxtemplateokz" );
PRIVATE VAR СтарыйДокумент   = TRecHandler( "nptxtemplateokz" ); /* заполняется при редактировании */

CLASS HistoryScroll(_ID)
  var Columns = TArray();
  var rs = null;
  var NumColumns = 0;
  var ID = _ID;

  /* атрибуты полей */
  macro AddColumn( ind, fld, head, width )
    Columns.value( ind * 6 + 0 ) = fld;  // fieldName
    Columns.value( ind * 6 + 1 ) = head; // header 
    Columns.value( ind * 6 + 2 ) = width; // width
    Columns.value( ind * 6 + 3 ) = 2;    // fldType (2 = FBT)
    Columns.value( ind * 6 + 4 ) = null; // decPoint
    Columns.value( ind * 6 + 5 ) = 0;    // reserv
    NumColumns = NumColumns + 1;
  end;

  macro BuildSQL()
    return 
      "SELECT to_char(rownum) as rn, ps.t_name as OperName, okz.t_recdate, okz.t_oldenddate, okz.t_newenddate, " +
      "              CASE WHEN okz.t_oldsofr = CHR(88) THEN 'Да' ELSE 'Нет' END as t_oldsofr, CASE WHEN okz.t_newsofr = CHR(88) THEN 'Да' ELSE 'Нет' END as t_newsofr, " +
      "              CASE WHEN okz.t_olddepo = CHR(88) THEN 'Да' ELSE 'Нет' END as t_olddepo, CASE WHEN okz.t_newdepo = CHR(88) THEN 'Да' ELSE 'Нет' END as t_newdepo, okz.t_oldkbk, okz.t_newkbk " +
      "  FROM dnptxtemplateokzhis_dbt okz, dperson_dbt ps where okz.t_id = " + ID + " and okz.t_oper = ps.t_oper";
  end;

  macro BuildRS()
    rs = execSQLselect(BuildSQL(), TArray(), true, RSDVAL_CLIENT, RSDVAL_STATIC);
    return rs;
  end;

  macro EvProc(recset, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Esc*/
      if(key == 27)
        return CM_CANCEL;
      end;
    end;
  end;

  macro ShowScroll()
    return RunScroll(BuildRS(), NumColumns, Columns, "DLOKZHIS", R2M(this,"EvProc"), "Протокол изменений записи в Настроечной таблице для шаблона ОКЗ", 
                 "~Esc~ Выход", true, -1, -1);
  end;

  AddColumn(0, "rn",   "№",   3, 3);
  AddColumn(1, "t_recdate", "Дата изменения", 16, 10);
  AddColumn(2, "OperName", "Пользователь", 16, 32);
  AddColumn(3, "t_oldenddate", "Прежняя дата окончания действия шкалы", 40, 10);
  AddColumn(4, "t_newenddate", "Новая дата окончания действия шкалы", 40, 10);
  AddColumn(5, "t_oldsofr", "Прежнее значение флага для СУД СОФР", 40, 5);
  AddColumn(6, "t_newsofr", "Новое значение флага для СУД СОФР", 40, 5);
  AddColumn(7, "t_olddepo", "Прежнее значение флага для СУД ДЕПО", 40, 5);
  AddColumn(8, "t_newdepo", "Новое значение флага для СУД ДЕПО", 40, 5);
  AddColumn(9, "t_oldkbk", "Прежний КБК", 16, 5);
  AddColumn(10,"t_newkbk", "Новый КБК", 16, 5);
END;


/* Макрофункция инициализации новой записи 
   необходимо заполнить структуру "Документ"
   возвращаемое значение
   0 - ошибок не было
   1 - была ошибка
*/
PRIVATE MACRO Новый_Документ()
  return 0;
END;

PRIVATE MACRO Проверить_Документ( Режим:integer )
  return 0;
END;

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string
  //пример
  //return "/*+FIRST_ROWS LEADING(t) INDEX(t dnptxtretconf_dbt_idx1)*/";
  return DefaultHint;
END;

/* Вызывается по CTRL+Z из скроллинга */
PRIVATE MACRO Функция_Пользователя()
  return 0;
END;

MACRO CheckActual()
  var queryOkz = " select t_typenob, t_rate, t_kbk, t_isresident, t_begdate, t_enddate " +
                 "   from DNPTXTEMPLATEOKZ_DBT  " +
                 "  where t_sofr = 'X' group by t_typenob, t_rate, t_kbk, t_isresident, t_begdate, t_enddate " +
                 " MINUS " +
                 " select t_typesnob as t_typenob, t_taxrate as t_rate, to_char(t_codekbk) as t_kbk, t_isresident, t_begdatescale as t_begdate, t_enddatescale as t_enddate " +
                 "   from DNPTXSNOBLIMIT_DBT";

  var cmdOkz = DL_RSDCommand(queryOkz);
  var DataSetOkz = cmdOkz.Execute();

  if( DataSetOkz.moveNext())
    MsgBox( "В Настроечной таблице для шаблона ОКЗ найдены лишние записи! Необходимо сверить данные с Настроечной таблицей предельных значений СНОБ!");
    return;
  end;

  var querylim = " select t_typesnob as t_typenob, t_taxrate as t_rate, to_char(t_codekbk) as t_kbk, t_isresident, t_begdatescale as t_begdate, t_enddatescale as t_enddate " +
                 "   from DNPTXSNOBLIMIT_DBT " +
                 "  MINUS " +
                 " select t_typenob, t_rate, t_kbk, t_isresident, t_begdate, t_enddate " +
                 "   from DNPTXTEMPLATEOKZ_DBT " +
                 "  where t_sofr = 'X' group by t_typenob, t_rate, t_kbk, t_isresident, t_begdate, t_enddate";
  var cmdlim = DL_RSDCommand(querylim);
  var DataSetlim = cmdlim.Execute();

  if( DataSetlim.moveNext() )
    MsgBox( "Для шкалы с датой начала действия "+DataSetlim.t_begdate+", вида СНОБ = "+DataSetlim.t_typenob+" и ставки = "+DataSetlim.t_rate+" существует расхождение! Необходимо сверить данные с Настроечной таблицей предельных значений СНОБ!");
    return;
  end;

  MsgBox("Данные для СОФР совпадают");
  return;
END;

MACRO ShowHistoryScroll(ID)
  var m_HistoryScroll = HistoryScroll(ID);
  m_HistoryScroll.ShowScroll();
END;
