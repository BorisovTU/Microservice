/*
$Name:        dlsecact_form.mac
$Module:      Ядро Securities
$Description: Отчет " Акты сверки наличия ценных бумаг " - форма ввода данных
*/
import "DlRepForm_h.mac", "globals.mac", "dlmisc.mac";

import RsbFormsInter;
Private MACRO SetCurrBeginDate():DATE
  VAR Month, Year;
  DateSplit( {curdate}, null, null, Year);
  DateSplit( {curdate}, null, Month, null);
  return GetDateAfterWorkDays(DATE( 1, Month, Year ),0);
end;

// Номера полей в форме отчета
CONST
      PNFLD_INACCSECUR_REG_MODULE_BO           = 0,     
      PNFLD_INACCSECUR_REG_MODULE_VA           = 1,    
      PNFLD_INACCSECUR_REG_WITH_NOCHANGING     = 2,    
      PNFLD_INACCSECUR_REG_BEGINDATE           = 3,    
      PNFLD_INACCSECUR_REG_FINISHDATE          = 4,    
      PNFLD_INACCSECUR_REG_FOR_EVERY_DATE      = 5,    
      PNFLD_INACCSECUR_REG_ACCOUNT_BANK        = 6,    
      PNFLD_INACCSECUR_REG_ACCOUNT_CLIENT      = 7,    
      PNFLD_INACCSECUR_REG_CLIENT_CODE         = 8,    
      PNFLD_INACCSECUR_REG_CLIENT_NAME         = 9,    
      PNFLD_INACCSECUR_REG_NUM_CONTR           = 10,    
      PNFLD_INACCSECUR_REG_AVRKIND             = 11,   
      PNFLD_INACCSECUR_REG_ISSIUER_CODE        = 12,   
      PNFLD_INACCSECUR_REG_ISSIUER_NAME        = 13,   
      PNFLD_INACCSECUR_REG_AVRCODE             = 14,   
      PNFLD_INACCSECUR_REG_AVRNAME             = 15,   
      PNFLD_INACCSECUR_REG_WITH_APOSTROFS      = 16,   
      //PNFLD_INACCSECUR_REG_TO_EXEL             = 17,
      PNFLD_INACCSECUR_REG_DEPARTMENT          = 17,
      PNFLD_INACCSECUR_REG_DEPARTMENT_NAME     = 18;

const Alt_F3 = 362;

// 
CLASS DlSecurActFormData()
  VAR 
  DepartmentID,
  BeginDate,
  EndDate,
  ForEveryDate,
  IsUseBO,
  IsUseVA,
  WithNoChanging,
  AccountBank,
  AccountClient,
  ClientCode,
  NumContr,
  AvrKind,
  IssuerCode,
  AvrCode,
  IsUseApp,
  IsExportToExel,
  lastParam,
  ConvertXML2XSLX,
  FullReport; 

  CLASS (TRsbPanel) RepPrmExtPanel ()
      var pConvertXML2XSLX = "", pFullReport = "";
      InitTRsbPanel();
      setSize(28,4);
      setPosition(35,15);
      this.Status("F2,F9 - выполнить");
      this.Caption("Дополнительные параметры поручения");
      var m_FullReport = TRsbCheckbox;
      m_FullReport.setPosition(3,2);
      m_FullReport.name = "m_FullReport";
      m_FullReport.checked = "X";
      addControl(m_FullReport);
      addLabel(TRsbLabel(5, 2, "Формировать полный отчет."));
      //var m_ConvertXML2XLS = TRsbCheckbox;
      //m_ConvertXML2XLS.setPosition(3,4);
      //m_ConvertXML2XLS.name = "m_ConvertXML2XLS";
      //m_ConvertXML2XLS.checked = "";
      //m_ConvertXML2XLS.enabled = false;
      //addControl(m_ConvertXML2XLS);
      //addLabel(TRsbLabel(5, 4, "Конвертировать XML в XLSX."));
      addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, "Panel_ButtonEvent"));
      macro Panel_ButtonEvent(RsbEvent)
         var stat;
         if((RsbEvent.keyCode == 316) or (RsbEvent.keyCode == 323))/*F2 F9*/
             stat = 0;
             //pConvertXML2XSLX = m_ConvertXML2XLS.checked;
             pFullReport = m_FullReport.checked;
             this.close(1);
         elif(RsbEvent.keyCode == 27) /**/
             stat = 1;
         end;
      end;
  END;
  macro SaveExtParm()
      var panel = RepPrmExtPanel();
      panel.run;
      ConvertXML2XSLX = panel.pConvertXML2XSLX;
      FullReport = panel.pFullReport ; 
  end;


  BeginDate = {curdate}; //SetCurrBeginDate();
  EndDate   = {curdate};


  // Значение по умолчанию
  DepartmentID = {OperDprt};
  //BeginDate = {curdate}; //SetCurrBeginDate();
  //EndDate = {curdate};
  ForEveryDate = "";
  IsUseBO = "X";
  IsUseVA = "X";
  if(VA_NeedInnerAcc() == false)
    IsUseVA = "";
  end;

  WithNoChanging = "X";
  AccountBank = "";
  AccountClient = "X";
  ClientCode = -1;
  NumContr = -1;
  AvrKind = -1;
  IssuerCode = -1;
  AvrCode = "";
  IsUseApp = "X";
  IsExportToExel = "X";

END;

VAR DlSActsRepFormData = DlSecurActFormData();

PRIVATE CONST
  H_AVRKIND         = 100,
  H_ISSIUER_CODE    = 101,
  H_ISSIUER_NAME    = 102,
  H_AVRCODE         = 103,
  H_AVRNAME         = 104;
                 
PRIVATE CONST SelCodeKind = 1;

/*Переключатель*/
PRIVATE MACRO DLUSR_FldProc_CheckBoxBO( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var AvrKind;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;

     if( FldRealValue != "X" )
        pThis.SetLinkedValue( H_AVRCODE, -1); 
        DisableFields( pThis.Data(), PNFLD_INACCSECUR_REG_AVRCODE );
     else
        EnableFields( pThis.Data(), PNFLD_INACCSECUR_REG_AVRCODE );
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
           EnableFields( pThis.Data(), PNFLD_INACCSECUR_REG_AVRCODE );
        else
           FldShowValue = FldRealValue = "";

           pThis.SetLinkedValue( H_AVRCODE, -1); 
           DisableFields( pThis.Data(), PNFLD_INACCSECUR_REG_AVRCODE );

           pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE, -1); 
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_CheckBoxVA( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
           pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE, -1); 
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_AvrKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Fininstr = TRecHandler( "fininstr.dbt" ),
      Avoiriss = TRecHandler( "avoiriss.dbt" );
  var Choice, Cond = "1=1", AvrCode;
      
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        DL_GetAvrKindName( FIKIND_AVOIRISS, FldRealValue, null, @FldShowValue );
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_ISSIUER_CODE, -1);
     pThis.SetLinkedValue( H_AVRCODE, -1);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 

     if( (pThis.GetFieldValue( PNFLD_INACCSECUR_REG_MODULE_BO ) == "X") AND ( pThis.GetFieldValue( PNFLD_INACCSECUR_REG_MODULE_VA ) != "X") )                                 
        Cond = "  ( T_ISEMISSIVE = 'X' ) ";
     elif( (pThis.GetFieldValue( PNFLD_INACCSECUR_REG_MODULE_BO ) != "X") AND ( pThis.GetFieldValue( PNFLD_INACCSECUR_REG_MODULE_VA ) == "X") )                                 
        Cond = " ( T_AvoirKind = "+AVOIRISSKIND_BILL+" or T_AvoirKind = "+AVOIRISSKIND_BANKCERT+" or T_AvoirKind = "+AVOIRISSKIND_DEPOSIT_CERTIFICATE+" ) ";/*выбираем вексель\деп.сертификат*/
     end;

     DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), Cond );

     if( pThis.GetFieldValue( PNFLD_INACCSECUR_REG_MODULE_BO ) == "X" )
        AvrCode = pThis.GetFieldValue( PNFLD_INACCSECUR_REG_AVRCODE );
        if( AvrCode > 0 )
           if( ПолучитьФинИн(AvrCode, Fininstr ) == 0 )   
              if( FldRealValue != Fininstr.rec.AvoirKind )
                 pThis.SetLinkedValue( H_AVRCODE, -1 );   
              end;                                        
           else                                           
              pThis.SetLinkedValue( H_AVRCODE, -1 );      
           end;                                           
        end;
     end;

  end;

  if( pThis.GetFieldValue( PNFLD_INACCSECUR_REG_MODULE_BO ) == "X" )
     EnableFields( pThis.Data(), PNFLD_INACCSECUR_REG_AVRCODE );
  else
     DisableFields( pThis.Data(), PNFLD_INACCSECUR_REG_AVRCODE );
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_IssuerCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party    = TRecHandler("party.dbt");

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_ISSIUER_NAME, "");
     else
        FldShowValue = ПолучитьКодСубъекта( FldRealValue, PTCK_CONTR );
        if(ПолучитьСубъекта(FldRealValue, Party) == 0)
           pThis.SetLinkedValue( H_ISSIUER_NAME, Party.rec.ShortName );
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_ISSIUER_NAME, "");
     pThis.SetLinkedValue( H_AVRCODE, -1);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 
     Party = TRecHandler("party.dbt");
     if( ListPT( Party, SelCodeKind, "", PTLIST_ISSUER, PTCK_CONTR) == true ) 
        FldRealValue = Party.rec.PartyID;
        FldShowValue = ПолучитьКодСубъекта( FldRealValue, PTCK_CONTR );
        if(ПолучитьСубъекта(FldRealValue, Party) == 0)
           pThis.SetLinkedValue( H_ISSIUER_NAME, Party.rec.ShortName );
        end;
        pThis.SetLinkedValue( H_AVRCODE, -1 );
     end;
  end;
  return CM_DEFAULT;
END;




/*Начальная дата выпуска отчета*/
private macro DLUSR_FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, BeginDate, EndDate, Period, BegDate, EndD;


private macro AddCol (ar,ind, fld, head, width, rdonly, ty, dp)
   ar.value (ind * 6)     = fld;
   ar.value (ind * 6 + 1) = head;
   ar.value (ind * 6 + 2) = width;
   ar.value (ind * 6 + 3 ) = rdonly;   // fldType
   ar.value (ind * 6 + 4 ) = dp;//   decPoint
   ar.value (ind * 6 + 5 ) = 0;   // reserv
end;

private macro EvProc(rs, cmd, id, key)

  if (cmd == DLG_KEY)
    if (key == 13)
       DlSActsRepFormData.BeginDate = date(rs.value("reportdate"));
       DlSActsRepFormData.EndDate   = date(rs.value("reportdate"));
       return CM_SELECT;
    end;   
  end;
end;

 if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = BeginDate;
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     DateSplit( FldRealValue, null, Month, Year);
     if( pThis.ExistField(DL_PNFLD_YEAR) )
        pThis.SetLinkedValue( DL_PNFLD_YEAR, Year );
     end;
     if( pThis.ExistField(DL_PNFLD_PERIOD) )

          Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);

           if( Period > 12 ) /*указан квартал*/
              Period = Period - 12;
              /*если дата не входит в заданный квартал - переставить период*/
              if( (Month < Period*3-2) OR (Month > Period*3) )
                 pThis.SetLinkedValue( DL_PNFLD_PERIOD, ((Month - 1)/3 + 1) + 12 );
              end;
           elif( Period != Month )
              pThis.SetLinkedValue( DL_PNFLD_PERIOD, Month);
           end;

           if( pThis.ExistField(DL_PNFLD_ENDDATE) )
              /*Пересчитываем дату окончания, если:
                Она не задана  ИЛИ
                Она не входит в указанный отчетный период.
              */
              Period_GetInterval( pThis.GetLinkedValue(DL_PNFLD_PERIOD), Year, @BegDate, @EndDate );
              EndD = pThis.GetLinkedValue(DL_PNFLD_ENDDATE);
              if ((EndD == NULL) OR 
                  (EndD < BegDate) OR
                  (EndD > EndDate)
                 )
                 pThis.SetLinkedValue( DL_PNFLD_ENDDATE, EndDate );
              end;
           end;
    
     elif( pThis.ExistField(DL_PNFLD_ENDDATE) )
        if( pThis.GetLinkedValue(DL_PNFLD_ENDDATE) < FldRealValue )
           pThis.SetLinkedValue( DL_PNFLD_ENDDATE, FldRealValue );
        end;
     end;
     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
       if( FldRealValue > pThis.GetLinkedValue(DL_PNFLD_ENDDATE) )
         MsgBox( "Дата окончания не может быть меньше даты начала периода!");
         return CM_IGNORE;
       end;
     end; 
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;   
  elif( (Cmd == DLG_KEY) AND (Key == ALT_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    var sql_d, cmd_d, rs_d;
    sql_d = "select reportdate from sofr_diasdeporestfull where reportdate between ? and ? group by reportdate order by reportdate desc";
    
    cmd_d = RsdCommand(sql_d);
    cmd_d.AddParam("1", RSDBP_IN, DL_AddMonths(DlSActsRepFormData.BeginDate, -1)); // DEF-56180 минус месяц от даты
    cmd_d.AddParam("2", RSDBP_IN, DlSActsRepFormData.BeginDate);
    cmd_d.Execute();

    rs_d = RsdRecordSet(cmd_d, rsdval_client, rsdval_static);
    
    var col1 = tArray;

    AddCol (col1, 0, "reportdate", "Остатки за дату",   50 ,  0,0);

    var retval = runscroll(rs_d, 1, col1, "Список дат с остатками в архиве.", "EvProc", "", "", true, 50 );

      if (retval)
        FldRealValue = DlSActsRepFormData.BeginDate;
        FldShowValue = FldRealValue;
        pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, date(FldRealValue) );
        pThis.SetLinkedValue( DL_PNFLD_ENDDATE, date(FldRealValue) );
      end; 

  end;
  return CM_DEFAULT;
END;


/*Конечная дата выпуска отчета*/
PRIVATE MACRO DLUSR_FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, BeginDate, EndDate, Period, PeriodYear;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = EndDate;
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     DateSplit( FldRealValue, null, Month, Year);
     /*если есть поле "начальная дата, то год относится к нему"*/
     if( pThis.ExistField(DL_PNFLD_YEAR) AND (pThis.ExistField(DL_PNFLD_BEGINDATE) == false) )
        pThis.SetLinkedValue( DL_PNFLD_YEAR, Year );
     end;
     if( pThis.ExistField(DL_PNFLD_PERIOD) )

        Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);
        if( Period > 12 ) /*указан квартал*/
           Period = Period - 12;
           /*если дата не входит в заданный квартал - переставить период*/
           if( (Month < Period*3-2) OR (Month > Period*3) )
              pThis.SetLinkedValue( DL_PNFLD_PERIOD, ((Month - 1)/3 + 1) + 12 );
           end;
        elif( Period != Month )
           pThis.SetLinkedValue( DL_PNFLD_PERIOD, Month);
        end;
     end;
     if( pThis.ExistField(DL_PNFLD_YEAR) )
       PeriodYear = pThis.GetLinkedValue(DL_PNFLD_YEAR);
       if(Year != PeriodYear)
         pThis.SetLinkedValue( DL_PNFLD_YEAR, Year);
       end;   
     end;
     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( FldRealValue < pThis.GetLinkedValue(DL_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  rep_EndDate = FldRealValue;
  return CM_DEFAULT;
END;











PRIVATE MACRO DLUSR_FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr = TRecHandler( "fininstr.dbt" ),
      Avoiriss = TRecHandler( "avoiriss.dbt" );

  var period_end_date = pThis.GetFieldValue( PNFLD_INACCSECUR_REG_FINISHDATE ); 

  VAR AvrKind, AddTable = "", WhereCond = "", IssuerID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue < 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_AVRNAME, "");
     else
        if( ПолучитьФинИн(FldRealValue, Fininstr, Avoiriss) )
           FldShowValue = Fininstr.rec.FI_Code;
           pThis.SetLinkedValue( H_AVRNAME, Fininstr.rec.Name );
           pThis.SetLinkedValue( H_AVRKIND, Fininstr.rec.Avoirkind );
           pThis.SetLinkedValue( H_ISSIUER_CODE,  FI_GetIssuerOnDate( Fininstr, period_end_date)/* Fininstr.rec.Issuer*/ );
        end;
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( FldShowValue == STR_ALLVALUE )
        FldRealValue = -1;
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) )
           MsgBox( "Неверное значение поля." );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;

        FldRealValue = Fininstr.rec.FIID;
        pThis.SetLinkedValue( H_AVRNAME, Fininstr.rec.Name );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_AVRNAME, "");
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if( pThis.GetFieldValue( PNFLD_INACCSECUR_REG_MODULE_BO ) == "X" )
        AvrKind = pThis.GetFieldValue( PNFLD_INACCSECUR_REG_AVRKIND );

        if( AvrKind > 0 )
           if( FI_IsAvrKindNotEmissive(AvrKind) == true )                                                 
              FldShowValue = "";                               
              return CM_IGNORE;                                
           end;
        else
           AvrKind = FIKIND_ALLAVOIRISS;/*Все ценные бумаги*/
        end;

        AddTable = " davrkinds_dbt avrkind ";
        WhereCond = WhereCond + " avrkind.t_FI_Kind = 2 " +
                                " AND T.T_FI_Kind = avrkind.t_FI_Kind " +
                                " AND T.T_Avoirkind = avrkind.t_Avoirkind " +
                                " AND avrkind.t_ISEmissive = 'X' ";

        IssuerID = pThis.GetFieldValue( PNFLD_INACCSECUR_REG_ISSIUER_CODE );
        if( (IssuerID != null) and (IssuerID > 0) )
           WhereCond = WhereCond + " AND RSI_RSB_FIInstr.FI_GetIssuerOnDate( t.T_FIID, " + GetSQLDate(period_end_date) + "  ) = " + IssuerID
        end;
        Fininstr = TRecHandler( "fininstr.dbt" );

        if( ListAvoiriss( AvrKind, Fininstr, null, null, WhereCond, AddTable ) )
           FldRealValue = Fininstr.rec.FIID;
           FldShowValue = Fininstr.rec.FI_Code;

           pThis.SetLinkedValue( H_AVRNAME, Fininstr.rec.Name );
           pThis.SetLinkedValue( H_AVRKIND, Fininstr.rec.Avoirkind );
           pThis.SetLinkedValue( H_ISSIUER_CODE, FI_GetIssuerOnDate( Fininstr, period_end_date)  /*Fininstr.rec.Issuer*/ );
        else
           return CM_IGNORE;
        end; 
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

// Инициализация
CLASS (DL_CPanel) DL_ActAvr_Panel()

  PRIVATE MACRO Init()

     AddFieldProc( 0, PNFLD_INACCSECUR_REG_MODULE_BO,           DL_PNFLD_EMISS_AVR,          @DLUSR_FldProc_CheckBoxBO,   DlSActsRepFormData.IsUseBO);
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_MODULE_VA,           DL_PNFLD_NOT_EMISS_AVR,      @DLUSR_FldProc_CheckBoxVA,   DlSActsRepFormData.IsUseVA);                 
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_WITH_NOCHANGING,     DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,        DlSActsRepFormData.WithNoChanging);
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_BEGINDATE,           DL_PNFLD_BEGINDATE,          @DLUSR_FldProc_BeginDate,    DlSActsRepFormData.BeginDate);
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_FINISHDATE,          DL_PNFLD_ENDDATE,            @DLUSR_FldProc_EndDate,         DlSActsRepFormData.EndDate );                 
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_FOR_EVERY_DATE,      DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,        DlSActsRepFormData.ForEveryDate );                 

     AddFieldProc( 0, PNFLD_INACCSECUR_REG_ACCOUNT_BANK,        DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,        DlSActsRepFormData.AccountBank);
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_ACCOUNT_CLIENT,      DL_PNFLD_IS_CLIENT,          @DL_FldProc_CheckBox,        DlSActsRepFormData.AccountClient);
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_CLIENT_CODE,         DL_PNFLD_CLIENT_STOCKDL_CODE,@DL_FldProc_Client_BO_Dep,   DlSActsRepFormData.ClientCode);
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_CLIENT_NAME,         DL_PNFLD_CLIENT_STOCKDL_NAME,@Default, "");
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_NUM_CONTR,           DL_PNFLD_CONTRACT_OFBU,      @DL_FldProc_Contract_BO_Dep, DlSActsRepFormData.NumContr);
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_AVRKIND,             H_AVRKIND,                   @DLUSR_FldProc_AvrKind,      DlSActsRepFormData.AvrKind);
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_ISSIUER_CODE,        H_ISSIUER_CODE,              @DLUSR_FldProc_IssuerCode,   DlSActsRepFormData.IssuerCode);
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_ISSIUER_NAME,        H_ISSIUER_NAME,              @Default, "");
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_AVRCODE,             H_AVRCODE,                   @DLUSR_FldProc_AvrCode,      DlSActsRepFormData.AvrCode);
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_AVRNAME,             H_AVRNAME,                   @Default, "");               
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_WITH_APOSTROFS,      DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,        DlSActsRepFormData.IsUseApp);
     //AddFieldProc( 0, PNFLD_INACCSECUR_REG_TO_EXEL,             DL_PNFLD_CHECKBOX,           @DL_FldProc_CheckBox,        DlSActsRepFormData.IsExportToExel);
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_DEPARTMENT,          DL_PNFLD_DEPARTCODE,         @DL_FldProc_Department_Cl_BO,DlSActsRepFormData.DepartmentID);
     AddFieldProc( 0, PNFLD_INACCSECUR_REG_DEPARTMENT_NAME,     DL_PNFLD_DEPARTMENT,         @Default, "");

     if(VA_NeedInnerAcc() == false)
       DisableFields( This.Data(), PNFLD_INACCSECUR_REG_MODULE_VA ); 
     end;


  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;
  
  initDL_CPanel( "sp_res.lbr", "SEC_ACTS" );
 
END;
