/*
    SecurPKOWriteOffPanel.mac
    Макрос формирующий диалоговое окно с выбором дат и чекбоксами со статусами операций
    Author : Emil Mustafaev
*/

import "RsbPanelConstants.mac", RsbFormsInter, "KeyCodes.mac", Календарь, PTInter, globals, "CbForms_h.mac";

Class (TRsbPanel) SecurPKOWriteOffPanel()

    private var rows:integer = 0; //указатель строки на панели, нарисовали на текущей строке поле, надо передвинуть указать ниже и тд.

    //статичные координаты панели и полей
    private var panelPositionX:integer = 45; //координаты где на экране появится наша панель X Y
    private var panelPositionY:integer = 10;
    private var editableFldsPosition   = 12; // сколько сделать отступ чтобы рисовать поля ввода в панели
    private var panelWidth:integer     = 40; //ширина панели

    //статичные размеры полей
    private var fromDateFldLength = 10; //с даты
    private var byDateFldLength = 10; // по дату .длина для полей с датами (в символах)

    //заданные значения фильтра пользователем
    var choosedFromDate:date;
    var choosedByDate:date;
    var choosedDealStatusIs0:bool = true;
    var choosedDealStatusIs10:bool = true;
    var choosedDealStatusIs20:bool = true;

    //Методы формирующие панель
    private macro NewPartName(labelText:string) //рисует заголовок внутри панели, переданный labelText
        addLabel(TRsbLabel(10, rows = rows + 1, labelText));
    end;

    private macro NewLabel(labelText:string) //рисует подпись в полю, пример : За период с   |    |
        addLabel(TRsbLabel(2, rows = rows + 1, labelText));
    end;

    // метод через который создаём само поле, указываем к нему подпись()не обязательно. handlerName связка с обработчиком нажатий на клавиатуре
    private macro NewFld (labelText:string, name:string, value, editable:bool, handlerName:string, dataType:integer, length:integer, textLength:integer)
        if (labelText != null) 
             NewLabel(labelText);
        end;
        var fld = TRsbEditField(dataType); //создаем объект Поле
        fld.setPosition(editableFldsPosition, rows);
        fld.setSize(length, 1);
        fld.textLength = textLength;
        fld.Editable   = editable;
        fld.Name       = name;
        fld.value      = value;
        if (handlerName != null)
            fld.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, handlerName));
        end;
        addControl(fld);
        return fld;
    end;

    // метод обёртка чтобы поле с типом Дата создать
    private macro NewFldDate(labelText:string, name:string, value:date, editable:bool)
        return NewFld(labelText, name, value, editable, "DateKeyPressed", RsbPanelFiledTypes.FT_DATE, fromDateFldLength, fromDateFldLength);
    end;

    //создать чекбокс с подписью
    //Пример:
    //  [X] название опции
    private macro NewCheckBoxWLabel(name:string, value:bool, labelText:string)
        var cb = TRsbCheckBox(1);
        cb.setPosition(3, rows);
        cb.dataType = RsbPanelFiledTypes.FT_CHR;
        cb.Name     = name;
        cb.checked  = value;
        addControl(cb);
        addLabel(TRsbLabel(editableFldsPosition + 2, rows, labelText));
    end;

    //инициализация самой панели
    InitTRsbPanel();
    SetCaption("Отобрать операции"); //задаём заголовок на панели
    SetStatus("~Esc~ Выход | ~F2~ Применить | ~F3~ Задать дату через календарь");
    SetPosition(panelPositionX, panelPositionY);

    NewPartName("Параметры отбора операций с ЦБ");
    rows = rows + 1;

    var fromDateCntrl = NewFldDate("За период с", "fromDealDate", {curdate} - 60, true);
    var byDateCntrl = NewFldDate("До", "byDealDate", {curdate}, true);
    rows = rows + 2;

    NewLabel("Статусы:");
    rows = rows + 1;

    //создание 3-х чекбоксов
    NewCheckBoxWLabel( "deal_status0", choosedDealStatusIs0, "Отложена");
    rows = rows + 1;

    NewCheckBoxWLabel( "deal_status10", choosedDealStatusIs10, "Открыта");
    rows = rows + 1;

    NewCheckBoxWLabel( "deal_status20", choosedDealStatusIs20, "Закрыта");
    rows = rows + 2;

    SetSize(panelWidth, rows);
    addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "KeyHandler"));

     // обработчик нажатия F3 , будет открывать календарь чтобы дату выбрать
    macro DateKeyPressed(RsbEvent:Object)
        if (RsbEvent.KeyCode == KeyCodes.F3)
            RsbEvent.Source.value = GetDateByCalendar(RsbEvent.Source.value);
        end;
    end;

    //Основной обработчик нажатий на панели
    macro KeyHandler(RsbEvent:Object)
        if (RsbEvent.KeyCode == KeyCodes.F2)
            //сохраняем выбранные даты и проверяем корректность
            if (getControl("fromDealDate").value > getControl("byDealDate").value)
                MsgBoxEx("Дата начала периода не может быть больше даты конца периода!");
                return;
            end;
            //if ((getControl("fromDealDate").value == null) or (getControl("byDealDate").value == null))
              //  MsgBoxEx("Поля периода обязательны для заполнения. Заполните их!");
                //return;
            //end;

            //выбранные даты кладём в переменные
            choosedFromDate = getControl("fromDealDate").value;
            choosedByDate = getControl("byDealDate").value;

            //выбор чекбоксов также сохраняем
            choosedDealStatusIs0 = getControl("deal_status0").checked;
            choosedDealStatusIs10 = getControl("deal_status10").checked;
            choosedDealStatusIs20 = getControl("deal_status20").checked;
            
            Close(1);
        end;
    end;

    //гетеры чтобы доставать данные снаружи
    macro GetFromDate()
        return choosedFromDate;
    end;

    macro GetByDate()
        return choosedByDate;
    end;

    macro DealStatusIs0()
        return choosedDealStatusIs0;
    end;

    macro DealStatusIs10()
        return choosedDealStatusIs10;
    end;

    macro DealStatusIs20()
        return choosedDealStatusIs20;
    end;
    
End;    

