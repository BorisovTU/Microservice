/*
$Name:        nutxPaidIncome_form.mac
$Module:      Ядро Securities
$Description: Форма Отчета "Отчет о выплаченных доходах ЮЛН" 
$Programmer:  Речкалов Иван Константинович
$Encoding:    OEM-866
$Date:        01.03.2018
*/
import "dlreport.mac", secinter;



import "DlRepForm_h.mac";

/*Номера полей в форме отчета*/
CONST 

      PNFLD_PAID_INCOME_BEGINDATE   = 0,
      PNFLD_PAID_INCOME_ENDDATE     = 1,
      PNFLD_PAID_INCOME_ISSECUR     = 2,
      PNFLD_PAID_INCOME_ISDV        = 3,
      PNFLD_PAID_INCOME_ISDEPO      = 4,
      PNFLD_PAID_INCOME_CLIENTCODE  = 5,
      PNFLD_PAID_INCOME_CLIENTNAME  = 6,
      PNFLD_PAID_INCOME_IDCONTRACT  = 7,
      PNFLD_PAID_INCOME_KINDDEAL    = 8,
      PNFLD_PAID_INCOME_EXCEL       = 9;

CONST STR_ALLVALUE_1 = "Все"; /*незаданное значение строкового поля*/

PRIVATE CONST
  H_PNFLD_BEGINDATE = 101,
  H_PNFLD_ENDDATE   = 102,
  H_SOURCEKIND      = 103;

PRIVATE CONST PTSK_STOCKDL = 1,
              PTSK_DV = 15,
              PTSK_DEPOS = 7;

/*Виды сделок*/
CONST REPPAID_DKIND_ALL       = 0, /*Все*/
      REPPAID_DKIND_BUY       = 1, /*Покупка*/
      REPPAID_DKIND_SALE      = 2, /*Продажа*/
      REPPAID_DKIND_REPO      = 3, /*РЕПО*/
      REPPAID_DKIND_REPAY     = 4, /*Погашение*/
      REPPAID_DKIND_INCOM     = 5, /*Расчет дохода по ц/б*/
      REPPAID_DKIND_DV        = 6; /*Сделки ПФИ*/

CLASS nutxPaidIncomDataClas()
  var IsUseBO,
      IsUseDV,
      IsUseDep,
      BeginDate,
      EndDate,
      ClientID,
      ContractID,
      CountDay,
      IsExportToExel,
      KindDeal,
      DepartmentID;
    
  // Значение по умолчанию
  BeginDate      = {curdate};
  EndDate        = {curdate};
  ClientID       = -1;
  ContractID     = -1;
  CountDay       = 1;
  IsExportToExel = "X";
  IsUseBO        = "X";
  IsUseDV        = "X";
  IsUseDep        = "X";
  KindDeal = 0;
  DepartmentID = -1;
END;

VAR nutxPaidIncomDataForm = nutxPaidIncomDataClas();
      
      
MACRO DV_KindDeal( Id:INTEGER )
   if( Id == REPPAID_DKIND_ALL )
      return "Все";
   elif( Id == REPPAID_DKIND_BUY )
      return "Покупка";
   elif( Id == REPPAID_DKIND_SALE )
      return "Продажа";
   elif( Id == REPPAID_DKIND_REPO )
      return "РЕПО";
   elif( Id == REPPAID_DKIND_REPAY )
      return "Погашение";
   elif( Id == REPPAID_DKIND_INCOM )
      return "Расчет дохода по ц/б";
   elif( Id == REPPAID_DKIND_DV )
      return "Сделки ПФИ";
   end;
   return "";
END;

PRIVATE MACRO DL_UserFldProc_SourceKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = REPPAID_DKIND_ALL;
     end;
     FldShowValue = DV_KindDeal(FldRealValue);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = REPPAID_DKIND_ALL;
     FldShowValue = DV_KindDeal(FldRealValue);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue  "Умная листалка" */
      if(  (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISSECUR ) == SET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDV ) == SET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDEPO ) == SET_CHAR) )
          DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    DV_KindDeal(REPPAID_DKIND_BUY),   REPPAID_DKIND_BUY,
                    DV_KindDeal(REPPAID_DKIND_SALE),  REPPAID_DKIND_SALE,
                    DV_KindDeal(REPPAID_DKIND_REPO),  REPPAID_DKIND_REPO,
                    DV_KindDeal(REPPAID_DKIND_REPAY), REPPAID_DKIND_REPAY,
                    DV_KindDeal(REPPAID_DKIND_INCOM), REPPAID_DKIND_INCOM,
                    DV_KindDeal(REPPAID_DKIND_DV)   , REPPAID_DKIND_DV
                  );
      elif(  (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISSECUR ) == SET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDV ) == SET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDEPO ) == UNSET_CHAR) )
          DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    DV_KindDeal(REPPAID_DKIND_BUY),   REPPAID_DKIND_BUY,
                    DV_KindDeal(REPPAID_DKIND_SALE),  REPPAID_DKIND_SALE,
                    DV_KindDeal(REPPAID_DKIND_REPO),  REPPAID_DKIND_REPO,
                    DV_KindDeal(REPPAID_DKIND_REPAY), REPPAID_DKIND_REPAY,
                    DV_KindDeal(REPPAID_DKIND_DV)   , REPPAID_DKIND_DV
                  );
      elif(  (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISSECUR ) == SET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDV ) == UNSET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDEPO ) == SET_CHAR) )
          DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    DV_KindDeal(REPPAID_DKIND_BUY),   REPPAID_DKIND_BUY,
                    DV_KindDeal(REPPAID_DKIND_SALE),  REPPAID_DKIND_SALE,
                    DV_KindDeal(REPPAID_DKIND_REPO),  REPPAID_DKIND_REPO,
                    DV_KindDeal(REPPAID_DKIND_REPAY), REPPAID_DKIND_REPAY,
                    DV_KindDeal(REPPAID_DKIND_INCOM), REPPAID_DKIND_INCOM
                  );
      elif(  (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISSECUR ) == UNSET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDV ) == SET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDEPO ) == SET_CHAR) )
          DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    DV_KindDeal(REPPAID_DKIND_INCOM), REPPAID_DKIND_INCOM,
                    DV_KindDeal(REPPAID_DKIND_DV)   , REPPAID_DKIND_DV
                  );
      elif(  (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISSECUR ) == SET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDV ) == UNSET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDEPO ) == UNSET_CHAR) )
          DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    DV_KindDeal(REPPAID_DKIND_BUY),   REPPAID_DKIND_BUY,
                    DV_KindDeal(REPPAID_DKIND_SALE),  REPPAID_DKIND_SALE,
                    DV_KindDeal(REPPAID_DKIND_REPO),  REPPAID_DKIND_REPO,
                    DV_KindDeal(REPPAID_DKIND_REPAY), REPPAID_DKIND_REPAY
                  );
       elif(  (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISSECUR ) == UNSET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDV ) == SET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDEPO ) == UNSET_CHAR) )
          DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    DV_KindDeal(REPPAID_DKIND_DV), REPPAID_DKIND_DV
                  );
      elif(  (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISSECUR ) == UNSET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDV ) == UNSET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDEPO ) == SET_CHAR) )
          DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    DV_KindDeal(REPPAID_DKIND_INCOM)   , REPPAID_DKIND_INCOM
                  );
      elif(  (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISSECUR ) == UNSET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDV ) == UNSET_CHAR) and
           (pThis.GetFieldValue( PNFLD_PAID_INCOME_ISDEPO ) == UNSET_CHAR) )
          DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    " "  , " "
                  );
 
     end;

  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
  end;

  return CM_DEFAULT;
END;


PRIVATE MACRO FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
     if( (FldRealValue != null) AND (pThis.GetLinkedValue(H_PNFLD_ENDDATE) != null) )
        if( FldRealValue > pThis.GetLinkedValue(H_PNFLD_ENDDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar(FldRealValue);
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
     if( (FldRealValue != NULL) AND (pThis.GetLinkedValue(H_PNFLD_BEGINDATE) != null) )
        if( FldRealValue < pThis.GetLinkedValue(H_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar(FldRealValue);
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;


/*листалка клиентов по виду обслуживания*/
PRIVATE MACRO DL_ListClientByServKind( ClientName:@string, FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, 
                               ServKind:TArray, IncludeOurBank:bool, Department:integer, EndDate:DATE ):BOOL
  VAR Choice, ServCond = "", i = 0, flag = false; 
  VAR Select, IncludeBankCond = "";

  if( (ServKind != null) and (ServKind.Size>0) )
     while( i < ServKind.Size )
        if( flag == true )
            ServCond = ServCond + ",";
        else
            flag = true;
        end;

        ServCond = ServCond + string(ServKind[i]);
        i = i + 1;
     end;
  else
     ServCond = 0;
  end;

  if( not IncludeOurBank )
     IncludeBankCond = " and clt.t_PartyID != " + string({OurBank});
  end;

  Select = "select distinct prt.t_Name, prt.t_ShortName, " +
           "       partcode.t_Code, clt.t_PartyID " + 
           "  from dpartcode_dbt partcode, dclient_dbt clt, dparty_dbt prt,  dsfcontr_dbt contr" +
           " where clt.t_ServiceKind in(" + ServCond + ")" +
           IncludeBankCond +
           "   and clt.t_PartyID = partcode.t_PartyID " +
           "   and prt.t_PartyID = clt.t_PartyID " +
           "   and partcode.t_CodeKind = 1 " +
           "   and clt.t_Branch    = 0 "+//+
           "   and prt.t_LegalForm =   "+ legal_form_jur +
           "   and RSB_SECUR.IsTaxResident(prt.t_PartyID, TO_DATE('"+EndDate+"', 'DD-MM-YYYY') ) = 0 "+
           "   and contr.t_PartyID = prt.t_PartyID "
           ;
           

  if( Department != null )
     if( Department > 0 )
        Select = Select + "   and clt.t_Department = "+string(Department);
     end;
  end;


  Choice = DL_Scroll(Select,
                     "Список клиентов", X, Y,
                     "t_Code","Код","t_Name","Наименование"
                    );

  if( Choice != NULL )
     FldRealValue = Choice.Value("t_PartyID"); 
     FldShowValue = Choice.Value("t_Code");
     ClientNAme = Choice.Value("t_ShortName");
  end;

  return (Choice != NULL);
END;


/*договор клиента БОЦБ\ПИ|Депоз в заданном филиале*/
PRIVATE MACRO DL_FldProc_Contract_BO_Dep( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientName = "", 
      IsBO = pThis.GetFieldValue(PNFLD_PAID_INCOME_ISSECUR), 
      IsDV = pThis.GetFieldValue(PNFLD_PAID_INCOME_ISDV),
      IsDep = pThis.GetFieldValue(PNFLD_PAID_INCOME_ISDEPO);
  var DepCode = pThis.GetLinkedValue( DL_PNFLD_DEPARTCODE );
  var ClientID = pThis.GetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE );
  var ServKind = TArray();

  if( Cmd == DLG_INIT )
      if( FldRealValue == null )
         FldRealValue = -1;
      end;
      if( FldRealValue <= 0 )
         FldShowValue = STR_ALLVALUE;
      end;
   elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
      FldRealValue = -1;
      FldShowValue = STR_ALLVALUE;
   elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

      if( (ClientID!= null) and (ClientID > 0) )
         if( (IsBO != null) and (IsBO == SET_CHAR) )
            ServKind[ServKind.Size] = PTSK_STOCKDL;
         end;
         if( (IsDV != null) and (IsDV == SET_CHAR) )
            ServKind[ServKind.Size] = PTSK_DV;
         end;         
         if( (IsDep != null) and (IsDep == SET_CHAR) )
            ServKind[ServKind.Size] = PTSK_DEPOS;
         end;

         DL_ListSfContrByServKind( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ClientID, ServKind, null, DepCode );
      end;
   end;
   return CM_DEFAULT;
END;

/*клиент БОЦБ\ПИ\Депоз в заданном филиале*/
PRIVATE MACRO DL_FldProc_Client_BO_Dep( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var ClientName = "";
  var ServKind = TArray(), 
      IsBO = pThis.GetFieldValue(PNFLD_PAID_INCOME_ISSECUR), 
      IsDV = pThis.GetFieldValue(PNFLD_PAID_INCOME_ISDV),
      IsDep = pThis.GetFieldValue(PNFLD_PAID_INCOME_ISDEPO);
  var IsClient = true;
  var DepCode = pThis.GetLinkedValue( DL_PNFLD_DEPARTCODE );
 
  if( pThis.ExistField(DL_PNFLD_IS_CLIENT) )
     IsClient = pThis.GetLinkedValue(DL_PNFLD_IS_CLIENT);
  end;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_CLIENT_STOCKDL_NAME) )
           pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, "" );
        end;
        if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )
           pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, null );
        end;
     else
        if( not ПолучитьСубъекта( FldRealValue, Party ) )
            FldShowValue = ПолучитьКодСубъекта(FldRealValue, PTCK_CONTR);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) and IsClient)
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;

     if( pThis.ExistField(DL_PNFLD_CLIENT_STOCKDL_NAME) )
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, "" );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) and IsClient)

         if( (IsBO != null) and (IsBO == SET_CHAR) )
            ServKind[ServKind.Size] = PTSK_STOCKDL;
         end;
         if( (IsDV != null) and (IsDV == SET_CHAR) )
            ServKind[ServKind.Size] = PTSK_DV;
         end;         
         if( (IsDep != null) and (IsDep == SET_CHAR) )
            ServKind[ServKind.Size] = PTSK_DEPOS;
         end;

     if(DL_ListClientByServKind( @ClientName, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ServKind, false, DepCode, pThis.GetLinkedValue(H_PNFLD_ENDDATE) ))
        if( pThis.ExistField(DL_PNFLD_CLIENT_STOCKDL_NAME) )
           pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, ClientName );
        end;
        if( pThis.ExistField(DL_PNFLD_CONTRACT_OFBU) )
           pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, null );
        end;
     end;
  end;
  return CM_DEFAULT;
END;

// Инициализация
CLASS (DL_CPanel) PAID_INCOME_Panel()

  PRIVATE MACRO Init()
    //        группа, номер поля              ,    имя поля (задать)             @Указатель на макропроцедуру, дефолтное значение, коодринаты Х,Y   
     AddFieldProc( 0, PNFLD_PAID_INCOME_BEGINDATE,    H_PNFLD_BEGINDATE,            @FldProc_BeginDate,           nutxPaidIncomDataForm.BeginDate);
     AddFieldProc( 0, PNFLD_PAID_INCOME_ENDDATE,      H_PNFLD_ENDDATE,              @FldProc_EndDate,             nutxPaidIncomDataForm.EndDate  );
     AddFieldProc( 0, PNFLD_PAID_INCOME_ISSECUR,      DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,         nutxPaidIncomDataForm.IsUseBO  );
     AddFieldProc( 0, PNFLD_PAID_INCOME_ISDV,         DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,         nutxPaidIncomDataForm.IsUseDV  );
     AddFieldProc( 0, PNFLD_PAID_INCOME_ISDEPO,       DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,         nutxPaidIncomDataForm.IsUseDep );
     AddFieldProc( 0, PNFLD_PAID_INCOME_CLIENTCODE,   DL_PNFLD_CLIENT_STOCKDL_CODE, @DL_FldProc_Client_BO_Dep,    nutxPaidIncomDataForm.ClientID );
     AddFieldProc( 0, PNFLD_PAID_INCOME_CLIENTNAME,   DL_PNFLD_CLIENT_STOCKDL_NAME, @Default,                     ""                             );
     AddFieldProc( 0, PNFLD_PAID_INCOME_IDCONTRACT,   DL_PNFLD_CONTRACT_OFBU,       @DL_FldProc_Contract_BO_Dep,  nutxPaidIncomDataForm.ContractID);
     AddFieldProc( 0, PNFLD_PAID_INCOME_KINDDEAL,     H_SOURCEKIND,                 @DL_UserFldProc_SourceKind,   nutxPaidIncomDataForm.KindDeal , 15, 15 );
     AddFieldProc( 0, PNFLD_PAID_INCOME_EXCEL,        DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,         nutxPaidIncomDataForm.IsExportToExel);
                                                   
  END;

    
  PRIVATE MACRO Check():BOOL
    if(  (this.GetFieldValue( PNFLD_PAID_INCOME_ISSECUR ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_PAID_INCOME_ISDV ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_PAID_INCOME_ISDEPO ) == UNSET_CHAR) )
      msgbox("Необходимо выбрать хотя бы один из модулей для формирования отчёта");
      return false;
    end;
    return true;
  END;

  initDL_CPanel( "dlrep.lbr", "PAID_ULN" ); 
END;
