/*
$Name: ul_preload_files.mac
$Module: Универсальный загрузчик
$Description: Предобработка файловыз форматов
*/

IMPORT Rsexts, "globals.mac";

MACRO UL_PreLoad_Files(_loader)
    var
        idx = 0,
        DirList = NULL,
        DirName = "", FilePath = "",
        day, month, year, prefix = "";

    DateSplit({curdate}, day, month, year);
    if (not IsStandAlone())
        prefix = "$";
    end;

    DirList = TDirList(prefix + _loader.groupConfig.InputDir() + "*.*", "F");
    while (idx < DirList.Count)
        if (not DirList.IsDir(idx))
            FilePath = _loader.groupConfig.InputDir() + DirList.Name(idx);

            // Если в строке пути есть знак ":" или "\\", то это абсолютный путь
            if( Index(toAnsi(FilePath), ":") OR Index(toAnsi(FilePath), "\\\\") )
                ;
            else
                // Добавим текущий путь
                FilePath = GetCurDir(not IsStandAlone()) + "\\" + FilePath;
            end;

            if (_loader.LoadData(FilePath))
                DirName = _loader.groupConfig.ErrDir();
            else
                DirName = _loader.groupConfig.OutputDir();
            end;

            if (strlen(DirName) > 0)
                DirName = DirName + String(day) + String(month) + substr(String(year), 3, 2) + "\\";
                MakeDir(prefix + DirName);

                if (CopyFile((prefix + FilePath), (prefix + DirName + DirList.Name(idx))))
                    RemoveFile(prefix + FilePath);
                end;
            end;
        end;

        idx = idx + 1;
    end;

    return 0;
END;
