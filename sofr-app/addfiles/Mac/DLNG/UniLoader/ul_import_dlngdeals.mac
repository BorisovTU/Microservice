/*
$Name: ul_import_dlngdeals.mac
$Module: Универсальный загрузчик
$Description: Создание сделок МБК и КО
*/

IMPORT "ul_const.mac", DealsInter, PaymInter, Проценты, "gtconst.inc","rgtgtrec.mac", "gtdlfun.mac", "dlref.mac",
       OprInter, PTInter, SfInter, FIInter;

private class DlngPayment()
  private const
      mode_ins = 0,
      mode_upd = 1,
      mode_del = 2;

  private var
      prm_workMode = NULL;

  private var
      prm_pmObj  = NULL;

  macro SetPurpose(_Purpose, _SubPurpose)
      prm_pmObj.Purpose    = _Purpose;
      prm_pmObj.SubPurpose = _SubPurpose;
      prm_pmObj.Number     = String(_SubPurpose);
  end;

  macro SetDocKind(_DocKind)
      prm_pmObj.DocKind = _DocKind;
  end;

  macro SetDocumentID(_DocumentID)
      prm_pmObj.DocumentID = _DocumentID;
  end;

  macro SetDepartment(_Department)
      prm_pmObj.Department = _Department;
  end;

  macro SetFIID(_FIID)
      prm_pmObj.OrderFIID          =
      prm_pmObj.BaseFIID           = 
      prm_pmObj.PayerFIID          =
      prm_pmObj.ReceiverFIID       =
      prm_pmObj.FuturePayerFIID    =
      prm_pmObj.FutureReceiverFIID = _FIID;
  end;

  macro SetLegID(_LegID)
//      dprm_LegID = _LegID;
  end;

  macro SetNetting(_Netting)
      prm_pmObj.Netting  = _Netting;
  end;

  macro SetValueDate(_ValueDate)
      prm_pmObj.Date                 = 
      prm_pmObj.PayDate              = 
      prm_pmObj.OutTransferDate      =
      prm_pmObj.ValueDate            = _ValueDate;
      prm_pmObj.ClientDate           = {curdate};
  end;

  macro SetAmount(_Amount)
      prm_pmObj.OrderAmount          =
      prm_pmObj.PayerAmount          = 
      prm_pmObj.ReceiverAmount       = 
      prm_pmObj.BaseAmount           =
      prm_pmObj.FutureBaseAmount     = _Amount;
      prm_pmObj.ActuateFutureAmounts(TBA_AMOUNT);
  end;

  macro SetGround(_Ground)
      prm_pmObj.Ground = _Ground;
  end;

  macro SetPayer(_Payer, _PayerCodeKind, _PayerBankID, _PayerBankIDCodeKind, _Kind_Operation, _FIID)
      RECORD settacc(settacc);

      if (_Payer == {OurBank})
          prm_pmObj.SetPayerPI(PAYMENTS_GROUP_UNDEF,
                               _PayerBankID,
                               _PayerBankIDCodeKind,
                               "",
                               "",
                               "",
                               _FIID,
                               1,
                               "",
                               _Payer,
                               "",
                               "",
                               _PayerCodeKind,
                               ""
                              );

          prm_pmObj.PayerBankName         = prm_pmObj.PayerName;
          prm_pmObj.PayerBankCorrCodeKind = PTCK_ALL;
          prm_pmObj.PayerBankCorrCode     = "";
          prm_pmObj.ReceiverInOurBalance  = "";
          prm_pmObj.ReceiverOurCorrAcc    = "";
          prm_pmObj.ReceiverOurCorrID     = -1;
      else
          if (not FindSETTACC_PMAUTO(_Payer, FIKIND_CURRENCY, _FIID, PTSK_CURRDL, _Kind_Operation, prm_pmObj.Purpose, 0, settacc))
              ;
          elif (not FindSETTACC_PMAUTO(_Payer, FIKIND_CURRENCY, _FIID, PTSK_CURRDL, _Kind_Operation, 0, 0, settacc))
              ;
          elif (not FindSETTACC_PMAUTO(_Payer, FIKIND_CURRENCY, _FIID, PTSK_CURRDL, 0, prm_pmObj.Purpose, 0, settacc))
              ;
          elif (not FindSETTACC_PMAUTO(_Payer, FIKIND_CURRENCY, _FIID, PTSK_CURRDL, 0, 0, 0, settacc))
              ;
          else
              if ((prm_pmObj.DocKind == DL_FOREXDOC) and 
                  (ВидСубъекта(_Payer, PTK_MARKETPLASE) == true))
                  settacc.BankID       = {OurBank};
                  settacc.BankCodeKind = PTCK_BIC;
                  settacc.FIID         = _FIID;
                  settacc.Chapter      = 1;
                  settacc.PartyID      = _Payer;
                  settacc.CodeKind     = PTCK_BIC;
              end;
          end;

          prm_pmObj.SetPayerPI(PAYMENTS_GROUP_UNDEF,
                               settacc.BankID,
                               settacc.BankCodeKind,
                               settacc.BankCode,
                               settacc.BankName,
                               settacc.CorrAcc,
                               settacc.FIID,
                               settacc.Chapter,
                               settacc.Account,
                               settacc.PartyID,
                               settacc.RecName,
                               settacc.INN,
                               settacc.CodeKind,
                               settacc.Code
                              );

          if (settacc.BankCorrID != {OurBank})
              prm_pmObj.PayerBankCorrAcc      = settacc.CorrAcc;
              prm_pmObj.PayerBankCorrCodeKind = settacc.BankCorrCodeKind;
              prm_pmObj.PayerBankCorrCode     = settacc.BankCorrCode;
              prm_pmObj.PayerBankCorrName     = settacc.BankCorrName;
          end;
      end;
  end;

  macro SetReceiver(_Receiver, _ReceiverCodeKind, _ReceiverBankID, _ReceiverBankIDCodeKind, _Kind_Operation, _FIID);
      RECORD settacc(settacc);

      if (_Receiver == {OurBank})
          prm_pmObj.SetReceiverPI(PAYMENTS_GROUP_UNDEF,
                                  _ReceiverBankID,
                                  _ReceiverBankIDCodeKind,
                                  "",
                                  "",
                                  "",
                                  _FIID,
                                  1,
                                  "",
                                  _Receiver,
                                  "",
                                  "",
                                  _ReceiverCodeKind,
                                  ""
                                 );

          prm_pmObj.ReceiverBankName         = prm_pmObj.ReceiverName;
          prm_pmObj.ReceiverBankCorrCodeKind = PTCK_ALL;
          prm_pmObj.ReceiverBankCorrCode     = "";
          prm_pmObj.PayerInOurBalance        = "";
          prm_pmObj.PayerOurCorrAcc          = "";
          prm_pmObj.PayerOurCorrID           = -1;
      else
          if (not FindSETTACC_PMAUTO(_Receiver, FIKIND_CURRENCY, _FIID, PTSK_CURRDL, _Kind_Operation, prm_pmObj.Purpose, 0, settacc))
              ;
          elif (not FindSETTACC_PMAUTO(_Receiver, FIKIND_CURRENCY, _FIID, PTSK_CURRDL, _Kind_Operation, 0, 0, settacc))
              ;
          elif (not FindSETTACC_PMAUTO(_Receiver, FIKIND_CURRENCY, _FIID, PTSK_CURRDL, 0, prm_pmObj.Purpose, 0, settacc))
              ;
          elif (not FindSETTACC_PMAUTO(_Receiver, FIKIND_CURRENCY, _FIID, PTSK_CURRDL, 0, 0, 0, settacc))
              ;
          else
              if ((prm_pmObj.DocKind == DL_FOREXDOC) and 
                  (ВидСубъекта(_Receiver, PTK_MARKETPLASE) == true))
                  settacc.BankID       = {OurBank};
                  settacc.BankCodeKind = PTCK_BIC;
                  settacc.FIID         = _FIID;
                  settacc.Chapter      = 1;
                  settacc.PartyID      = _Receiver;
                  settacc.CodeKind     = PTCK_BIC;
              end;
          end;

          prm_pmObj.SetReceiverPI(PAYMENTS_GROUP_UNDEF,
                                  settacc.BankID,
                                  settacc.BankCodeKind,
                                  settacc.BankCode,
                                  settacc.BankName,
                                  settacc.CorrAcc,
                                  settacc.FIID,
                                  settacc.Chapter,
                                  settacc.Account,
                                  settacc.PartyID,
                                  settacc.RecName,
                                  settacc.INN,
                                  settacc.CodeKind,
                                  settacc.Code
                                 );

          if (settacc.BankCorrID != {OurBank})
              prm_pmObj.ReceiverBankCorrAcc      = settacc.CorrAcc;
              prm_pmObj.ReceiverBankCorrCodeKind = settacc.BankCorrCodeKind;
              prm_pmObj.ReceiverBankCorrCode     = settacc.BankCorrCode;
              prm_pmObj.ReceiverBankCorrName     = settacc.BankCorrName;
          end;
      end;
  end;

  macro CreateNew()
      prm_pmObj.Actuate();

      if (prm_pmObj.Update())
          RunError("Ошибка при создании платежей.");
      end;
  end;

  private macro ClassConstructorNew()
      prm_workMode = mode_ins;

      prm_pmObj                  = RsbPayment(0);
      prm_pmObj.Department       = {OperDprt};
      prm_pmObj.PaymStatus       = PM_PREPARING;
      prm_pmObj.IsPlanPaym       = "X";
      prm_pmObj.ValueDate        = {curdate};
      prm_pmObj.SubPurpose       = 0;
      prm_pmObj.Oper             = {oper};
      prm_pmObj.Origin           = PAYMENT_OR_AUTO;
  end;

  private macro ClassConstructor(CountParm)
      if (CountParm == 1)
          ClassConstructorNew();
      elif (CountParm == 2)
      //
      elif (CountParm == 5)
      //
      end;
  end;

  ClassConstructor(ParmCount());
end;
/////////////////////////////////////////////////////////////////////////////////
private class UL_ImportDLNG_BO(_datakind, _obj, _log)
  private var
      prm_datakind = _datakind,
      prm_obj = _obj,
      prm_log = _log;

  private var
      prm_tick,
      prm_legBase,
      prm_legRev;

  private var
      prm_deferOp;

  MACRO AddErrorLog(_err)
      if (ValType(this.prm_log) != V_UNDEF)
          this.prm_log.ErrorLog(_err);
      end;
  END;

  private macro CreateTick(_BofficeKind)
    var
        type     = 0,
        DateP1C1 = date(0, 0, 0),
        DateP1C2 = date(0, 0, 0),
        DateP2C1 = date(0, 0, 0),
        DateP2C2 = date(0, 0, 0);

      prm_tick.rec.DealID      = 0;
      prm_tick.rec.BofficeKind = _BofficeKind;

      //Статические параметры   
      prm_tick.rec.TradeSystem = 0;
      prm_tick.rec.DealStatus  = DL_PREPARING;
      prm_tick.rec.RegDate     = {curdate};
      prm_tick.rec.Oper        = {oper};   
      prm_tick.rec.ClientID    = ALLFININSTR;
      prm_tick.rec.DepositID   = ALLFININSTR;
      prm_tick.rec.MarketID    = ALLFININSTR;
      prm_tick.rec.PortfolioID = 0;
      prm_tick.rec.IndocID     = 0;
      prm_tick.rec.NumberPack  = 0;
      prm_tick.rec.Department  = {OperDprt};
      prm_tick.rec.OriginID    = 0;
      prm_tick.rec.Netting     = 3;
      prm_tick.rec.LinkChannel = 1;

      if (prm_tick.rec.BofficeKind == DL_FOREXDOC)
          prm_tick.rec.DealCodeTS = prm_obj.DealCodeTS;
          prm_tick.rec.DealDate = prm_obj.DealDate;
          prm_tick.rec.DealTime = prm_obj.DealTime;
          prm_tick.rec.TraderID = prm_obj.TraderID;
          prm_tick.rec.PartyID = prm_obj.PartyID;
          if (prm_tick.rec.PartyID == -1)
              RunError( "Ошибка при создании новой сделки: не задан контрагент." );
          end;

          prm_tick.rec.BrokerID = prm_obj.BrokerID;
          prm_tick.rec.TypeDoc = prm_obj.TypeDoc;
          prm_tick.rec.Comment = prm_obj.Comment;
          prm_tick.rec.DealType = prm_obj.DealType;
          prm_tick.rec.DealGroup = GetOperationGroup (prm_tick.rec.DealType);
          if (not DL_GenRefByTypeDoc(OBJTYPE_CONVDEAL, prm_tick.rec.DealCode))
              RunError("Ошибка при генерации номера сделки");
          end;

          prm_tick.rec.RevRate = prm_obj.DealPart.RevRate;

          DateP1C1 = prm_obj.DealPart.BaseDate;
          DateP1C2 = prm_obj.DealPart.ContrDate;
          if (DateP1C1 > DateP1C2)
              prm_tick.rec.Flag1 = "X";
          else
              prm_tick.rec.Flag1 = "";
          end;

          if (IsSwap(prm_tick.rec.DealGroup))
              DateP2C1 = prm_obj.RevDealPart.BaseDate;
              DateP2C2 = prm_obj.RevDealPart.ContrDate;
              if (DateP2C1 > DateP2C2)
                  prm_tick.rec.Flag2 = "X";
              else
                  prm_tick.rec.Flag2 = "";
              end;
          end;
      elif (prm_tick.rec.BofficeKind == DL_IBCDOC)
          prm_tick.rec.DealCodeTS = prm_obj.DealCodeTS;
          prm_tick.rec.DealDate = prm_obj.DealDate;
          prm_tick.rec.DealTime = prm_obj.DealTime;
          prm_tick.rec.TraderID = prm_obj.TraderID;
          prm_tick.rec.PartyID = prm_obj.PartyID;
          if (prm_tick.rec.PartyID == -1)
              RunError( "Ошибка при создании новой сделки: не задан контрагент." );
          end;

          prm_tick.rec.BrokerID = prm_obj.BrokerID;
          prm_tick.rec.TypeDoc = prm_obj.TypeDoc;
          prm_tick.rec.Comment = prm_obj.Comment;
          prm_tick.rec.DealType = prm_obj.DealType;
          prm_tick.rec.DealGroup = GetOperationGroup (prm_tick.rec.DealType);
          //генерим номер сделки
          if (not DL_GenRefByTypeDoc(OBJTYPE_MMARKDEAL, prm_tick.rec.DealCode))
              RunError("Ошибка при генерации номера сделки");
          end;
      end;

      if (not prm_tick.insert)
          RunError( "Ошибка при создании новой сделки." );
      end;
  end;

  private macro CreateLeg()
    var
        DataSet = NULL,
        type     = 0,
        DateP1C1 = date(0, 0, 0),
        DateP1C2 = date(0, 0, 0),
        DateP2C1 = date(0, 0, 0),
        DateP2C2 = date(0, 0, 0);

      prm_legBase.rec.DepositID    = ALLFININSTR;
      prm_legBase.rec.Registrar    = ALLFININSTR;
      prm_legBase.rec.Pitch        = 1;
      prm_legBase.rec.Mode         = 1;
      prm_legBase.rec.PeriodNumber = 10000;
      prm_legBase.rec.PeriodType   = 3;
      prm_legBase.rec.Diff         = 0;
      prm_legBase.rec.PayDay       = 0;
      prm_legBase.rec.TablePercent = "";
      prm_legBase.rec.Caption      = 0;
      prm_legBase.rec.TypeDate     = 0;
      prm_legBase.rec.CountDay     = 0;
      prm_legBase.rec.Correct      = 0;
      prm_legBase.rec.DealID       = prm_tick.rec.DealID;

      if (prm_tick.rec.BofficeKind == DL_FOREXDOC)
          //статические параметры    
          prm_legBase.rec.LegID = 0;
          prm_legBase.rec.Scale = 1;
          prm_legBase.rec.Point = 4;
          prm_legBase.rec.Basis = 4;

          prm_legBase.rec.PFI = prm_obj.DealPart.BaseFIID;
          prm_legBase.rec.Principal = prm_obj.DealPart.Principal;
          prm_legBase.rec.LegNumber = prm_tick.rec.DealCode;
          prm_legBase.rec.CFI = prm_obj.DealPart.ContrFIID;
          prm_legBase.rec.Price = prm_obj.DealPart.Price;
          prm_legBase.rec.Price = prm_legBase.rec.Price * pow(10, prm_legBase.rec.Point);

          if (prm_tick.rec.RevRate == "")
              prm_legBase.rec.Cost = prm_legBase.rec.Principal * (prm_legBase.rec.Price/pow(10, prm_legBase.rec.Point));
          else
              prm_legBase.rec.Cost = prm_legBase.rec.Principal / (prm_legBase.rec.Price/pow(10, prm_legBase.rec.Point));
          end;

          prm_legBase.rec.Start = prm_tick.rec.DealDate;
          DateP1C1 = prm_obj.DealPart.BaseDate;
          DateP1C2 = prm_obj.DealPart.ContrDate;
          if (prm_tick.rec.Flag1 == "")
              prm_legBase.rec.Maturity = DateP1C1;
              prm_legBase.rec.Expiry   = DateP1C2;
          else
              prm_legBase.rec.Maturity = DateP1C2;
              prm_legBase.rec.Expiry   = DateP1C1;
          end;

          prm_legBase.rec.Duration     = prm_legBase.rec.Maturity - prm_legBase.rec.Start;

          if (not prm_legBase.insert)
              RunError( "Ошибка при создании новой сделки." );
          end;

          //А если это сделка Своп? Необходимо еще запись для обратной сделки
          if (IsSwap(prm_tick.rec.DealGroup))
              copy(prm_legRev, prm_legBase);

              prm_legRev.rec.ID = 0;
              prm_legRev.rec.LegID = 1;
              prm_legRev.rec.Price = prm_obj.RevDealPart.Price;
              prm_legRev.rec.Price = prm_legBase.rec.Price * pow(10, prm_legBase.rec.Point);
              if (prm_tick.rec.RevRate == "")
                  prm_legRev.rec.Cost = prm_legRev.rec.Principal * (prm_legRev.rec.Price/pow(10, prm_legRev.rec.Point));
              else
                  prm_legRev.rec.Cost = prm_legRev.rec.Principal / (prm_legRev.rec.Price/pow(10, prm_legRev.rec.Point));
              end;

              DateP2C1 = prm_obj.RevDealPart.BaseDate;
              DateP2C2 = prm_obj.RevDealPart.ContrDate;
              if (prm_tick.rec.Flag2 == "")
                  prm_legRev.rec.Maturity = DateP2C1;
                  prm_legRev.rec.Expiry   = DateP2C2;
              else
                  prm_legRev.rec.Maturity = DateP2C2;
                  prm_legRev.rec.Expiry   = DateP2C1;
              end;

              prm_legRev.rec.Duration = prm_legRev.rec.Maturity - prm_legRev.rec.Start;

              if (not DL_GenRefByTypeDoc(OBJTYPE_CONVDEAL, prm_legRev.rec.LegNumber))
                  RunError("Ошибка при генерации номера сделки");
              end;

              if (not prm_legRev.insert)
                  RunError( "Ошибка при создании новой сделки." );
              end;
          end;
      elif (prm_tick.rec.BofficeKind == DL_IBCDOC)
          prm_legBase.rec.LegID = 1;
          prm_legBase.rec.Scale = 100;

          prm_legBase.rec.PFI = prm_obj.PFI;
          prm_legBase.rec.Principal = prm_obj.Principal;
          prm_legBase.rec.LegNumber = prm_tick.rec.DealCode;

          DataSet = TRsbDataSet("select * from DPRCRATE_DBT where T_NUMBER=100 and (T_ISUSER IS NULL or T_ISUSER <> 'X')");

          if (DataSet.MoveNext())
              prm_legBase.rec.Point = DataSet.Point;
          else
              prm_legBase.rec.Point = 4;
          end;

          prm_legBase.rec.CFI = prm_legBase.rec.PFI;

          prm_legBase.rec.Price = prm_obj.PercentData.Value;
          prm_legBase.rec.Price = prm_legBase.rec.Price * pow(10, prm_legBase.rec.Point);
          prm_legBase.rec.Start = prm_obj.Start;
          prm_legBase.rec.Maturity = prm_obj.Start + prm_obj.Duration;
          prm_legBase.rec.Cost = 0;
          prm_legBase.rec.Duration = prm_obj.Duration;
          prm_legBase.rec.Basis = 0;

          if (not prm_legBase.insert)
              RunError( "Ошибка при создании новой сделки." );
          end;
      end;
  end;

  private macro CreatePaym(Purpose)
    var
        paym       = DlngPayment(),
        type       = 0,
        BankID     = -1,
        Ground     = "";

      paym.SetDocKind(prm_tick.rec.BofficeKind);
      paym.SetDocumentID(prm_tick.rec.DealID);
      paym.SetPurpose(Purpose, 0);
      paym.SetDepartment(prm_tick.rec.Department);

      if (prm_tick.rec.Netting == 3)
          paym.SetNetting("X");
      else
          paym.SetNetting("");
      end;

      if   (Purpose == BAi)
          if (prm_tick.rec.Flag1 == "")
              paym.SetValueDate(prm_legBase.rec.Maturity);
          else
              paym.SetValueDate(prm_legBase.rec.Expiry);
          end;

          paym.SetLegID(prm_legBase.rec.LegID);
          paym.SetFIID(prm_legBase.rec.PFI);
          paym.SetAmount(prm_legBase.rec.Principal);
          BankID = -1; // пока оставим так
          Ground = ""; // пока оставим так
          paym.SetGround(Ground);

          if (prm_tick.rec.TypeDoc == "S")
              paym.SetPayer({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
              paym.SetReceiver(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
          elif (prm_tick.rec.TypeDoc == "B")
              paym.SetReceiver({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
              paym.SetPayer(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
          end;
      elif (Purpose == CAi)
          if (prm_tick.rec.Flag1 == "")
              paym.SetValueDate(prm_legBase.rec.Expiry);
          else
              paym.SetValueDate(prm_legBase.rec.Maturity);
          end;

          paym.SetLegID(prm_legBase.rec.LegID);
          paym.SetFIID(prm_legBase.rec.CFI);
          paym.SetAmount(prm_legBase.rec.Cost);
          BankID = -1; // пока оставим так
          Ground = ""; // пока оставим так
          paym.SetGround(Ground);

          if (prm_tick.rec.TypeDoc == "S")
              paym.SetReceiver({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.CFI);
              paym.SetPayer(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.CFI);
          elif (prm_tick.rec.TypeDoc == "B")
              paym.SetPayer({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.CFI);
              paym.SetReceiver(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.CFI);
          end;
      elif (Purpose == BRi)
          if (prm_tick.rec.Flag2 == "")
              paym.SetValueDate(prm_legRev.rec.Maturity);
          else
              paym.SetValueDate(prm_legRev.rec.Expiry);
          end;

          paym.SetLegID(prm_legRev.rec.LegID);
          paym.SetFIID(prm_legRev.rec.PFI);
          paym.SetAmount(prm_legRev.rec.Principal);
          BankID = -1; // пока оставим так
          Ground = ""; // пока оставим так
          paym.SetGround(Ground);

          if (prm_tick.rec.TypeDoc == "B")
              paym.SetPayer({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legRev.rec.PFI);
              paym.SetReceiver(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legRev.rec.PFI);
          elif (prm_tick.rec.TypeDoc == "S")
              paym.SetReceiver({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legRev.rec.PFI);
              paym.SetPayer(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legRev.rec.PFI);
          end;
      elif (Purpose == CRi)
          if (prm_tick.rec.Flag2 == "")
              paym.SetValueDate(prm_legRev.rec.Expiry);
          else
              paym.SetValueDate(prm_legRev.rec.Maturity);
          end;

          paym.SetLegID(prm_legRev.rec.LegID);
          paym.SetFIID(prm_legRev.rec.CFI);
          paym.SetAmount(prm_legRev.rec.Cost);
          BankID = -1; // пока оставим так
          Ground = ""; // пока оставим так
          paym.SetGround(Ground);

          if (prm_tick.rec.TypeDoc == "B")
              paym.SetReceiver({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legRev.rec.CFI);
              paym.SetPayer(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legRev.rec.CFI);
          elif (prm_tick.rec.TypeDoc == "S")
              paym.SetPayer({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legRev.rec.CFI);
              paym.SetReceiver(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legRev.rec.CFI);
          end;
      elif (Purpose == PM_PURP_PRINC)
          paym.SetValueDate(prm_legBase.rec.Start);

          paym.SetLegID(prm_legBase.rec.LegID);
          paym.SetFIID(prm_legBase.rec.PFI);
          paym.SetAmount(prm_legBase.rec.Principal);
          BankID = -1; // пока оставим так
          Ground = ""; // пока оставим так
          paym.SetGround(Ground);

          if (IsCOMMITMENT (prm_tick.rec.DealGroup, Purpose))
              paym.SetPayer({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
              paym.SetReceiver(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
          else
              paym.SetReceiver({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
              paym.SetPayer(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
          end;
      elif (Purpose == PM_PURP_PRINC_RET)
          paym.SetValueDate(prm_legBase.rec.Maturity);

          paym.SetLegID(prm_legBase.rec.LegID);
          paym.SetFIID(prm_legBase.rec.PFI);
          paym.SetAmount(prm_legBase.rec.Principal);
          paym.SetGround(Ground);

          if (IsCOMMITMENT (prm_tick.rec.DealGroup, Purpose))
              paym.SetPayer({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
              paym.SetReceiver(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
          else
              paym.SetReceiver({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
              paym.SetPayer(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
          end;
      elif (Purpose == PM_PURP_PERCENT)
          paym.SetValueDate(prm_legBase.rec.Maturity);

          paym.SetLegID(prm_legBase.rec.LegID);
          paym.SetFIID(prm_legBase.rec.PFI);
          paym.SetAmount(prm_legBase.rec.Cost);
          paym.SetPurpose(Purpose, 1);
          paym.SetGround(Ground);

          if (IsCOMMITMENT (prm_tick.rec.DealGroup, Purpose))
              paym.SetPayer({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
              paym.SetReceiver(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
          else
              paym.SetReceiver({OurBank}, PTCK_BIC, {OurBank}, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
              paym.SetPayer(prm_tick.rec.PartyID, PTCK_BIC, BankID, PTCK_BIC, prm_tick.rec.DealType, prm_legBase.rec.PFI);
          end;
      end;

      paym.CreateNew();
  end;

  private macro CreateDeferOperation()
      ClearRecord(prm_deferOp);

      prm_deferOp.rec.id_operation   = 0;
      prm_deferOp.rec.kind_operation = prm_tick.rec.DealType;
      prm_deferOp.rec.init_oper      = {oper};
      prm_deferOp.rec.dockind        = prm_tick.rec.bofficekind;

      if (prm_tick.rec.bofficekind == DL_FOREXDOC)
          prm_deferOp.rec.documentid = UniID(prm_tick, OBJTYPE_CONVDEAL);
      elif (prm_tick.rec.bofficekind == DL_IBCDOC)
          prm_deferOp.rec.documentid = UniID(prm_tick, OBJTYPE_MMARKDEAL);
      end;

      if (not prm_deferOp.insert)
          RunError( "Ошибка при создании новой сделки." );
      end;
  end;

  private macro CreateQLT()
    var
        qlt = Tbfile("mmdqlt.dbt", "W");

      ClearRecord(qlt);

      qlt.rec.DealID  = prm_tick.rec.DealID;
      qlt.rec.Date    = prm_tick.rec.DealDate;
      qlt.rec.Quality = 3;

      if (not qlt.insert)
          RunError( "Ошибка при создании новой сделки." );
      end;	  
  end;

  private macro CreatePcAcc(_type)
      var 
          prm = TPrcContractPrm,
          Contract  = 0,
          Sum       = Money(0);

      prm.BackOffice     = 2;
      prm.ObjectType     = 103;
      prm.ObjectID       = prm_tick.rec.DealCode;
      if (isSale(GetOperationGroup(prm_tick.rec.DealType)))
          prm.CalcNumber = 100;
      else
          prm.CalcNumber = 101; 
      end;

      prm.IsUserCalc     = "";    
      prm.RateNumber     = 100;
      prm.IsUserRate     = "X";
      prm.ContractType   = _type;  
      prm.ContractNumber = 0;
      prm.Client         = {OurBank};        
      prm.ClientQuality  = 1;
      prm.BeginDate      = prm_legBase.rec.Start;     
      prm.EndDate        = prm_legBase.rec.Maturity;       
      prm.Oper           = {Oper};

      if (Prc_CreateContractByObject(prm, Contract))
          RunError( "Ошибка при создании новой сделки." );
      elif (_type == 7)
          Prc_SetRateVal (Contract, prm_legBase.rec.Start, (prm_legBase.rec.Price / pow(10, prm_legBase.rec.Point)), false); 
          Prc_CalcPeriodOnDate (Contract, 3, prm_legBase.rec.Maturity, 1, Sum); 
      else
          Prc_SetRateVal (Contract, prm_legBase.rec.Start, (prm_legBase.rec.Price / pow(10, prm_legBase.rec.Point)), false); 
      end;
  end;

  private macro CreateForexDeal()
      CreateTick(DL_FOREXDOC);
      CreateLeg();
      CreatePaym(BAi);
      CreatePaym(CAi);

      if (IsSwap(prm_tick.rec.DealGroup))
          CreatePaym(BRi);
          CreatePaym(CRi);

          prm_tick.rec.DealCode = prm_tick.rec.DealCode + "S";

          if (not prm_tick.update)
              RunError( "Ошибка при создании новой сделки." );
          end;
      end;

      CreateDeferOperation();

      return true;
  end;

  private macro CreateMMDeal()
      CreateTick(DL_IBCDOC);
      CreateLeg();

      CreateQLT();

      CreatePcAcc(7);
      CreatePcAcc(9);
      CreatePcAcc(10);

      CreatePaym(PM_PURP_PRINC);
      CreatePaym(PM_PURP_PRINC_RET);
      CreatePaym(PM_PURP_PERCENT);

      CreateDeferOperation();

      return true;
  end;

  private macro CreateDealTrn()
    var
        retVal = true;

      if (prm_datakind == UL_DATAKIND_FRX)
          retVal = CreateForexDeal();
      elif (prm_datakind == UL_DATAKIND_IBC)
          retVal = CreateMMDeal();
      else
          RunError("Неверный тип сделки");
      end;

      return retVal;

    OnError( RslErrObj )
      this.AddErrorLog(RslErrObj.Message);
      AbortTrn();
      return false;
  end;

  macro CreateDeal()
    macro Local_CreateDealTrn()
        return CreateDealTrn();
    end;

      if (not ProcessTrn(0, "Local_CreateDealTrn"))
          return false;
      end;

      return true;
  end;

  private macro ClassConstructor(_datakind, _obj, _log)
      prm_tick    = Tbfile("dl_tick.dbt", "W");
      prm_legBase = Tbfile("dl_leg.dbt", "W");
      prm_legRev  = Tbfile("dl_leg.dbt", "W");
      prm_deferOp = Tbfile("oproper.dbt", "W");
  end;

  ClassConstructor(prm_datakind, prm_obj, prm_log);
end;
/////////////////////////////////////////////////////////////////////////////////////
macro UL_Import_FXDeal(_obj, _log)
  var 
      Obj = UL_ImportDLNG_BO(UL_DATAKIND_FRX, _obj, _log);

    return Obj.CreateDeal();
end;

macro UL_Import_IBCDeal(_obj, _log)
	  var 
      Obj = UL_ImportDLNG_BO(UL_DATAKIND_IBC, _obj, _log);

    return Obj.CreateDeal();
end;
