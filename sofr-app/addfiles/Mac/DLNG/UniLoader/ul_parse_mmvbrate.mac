/*
$Name: ul_parse_mmvbrate.mac
$Module: Универсальный загрузчик
$Description: Разбор xml файла с котировками ММВБ
*/

IMPORT "UL_InputDataObj.mac", "XMLLoader.mac", "or_exl_h.mac";

PRIVATE VAR
    Rows = TArray();

PRIVATE MACRO ParseSECID(_str, BBB_, KKK_, SSS_, CCC_)
  var
      idx = 0;

  macro ConvertTerm(_str)
    var 
        retVal = "", TypeDuration = "", Duration = "";

      if (_str == "TOD")
          retVal = "0D";
      elif (_str == "TOM")
          retVal = "1D";
      elif (_str == "SPT")
          retVal = "2D";
      else
          retVal = _str;
      end;

      TypeDuration = substr(retVal, strlen(retVal), 1);
      if ((TypeDuration == "D") or
          (TypeDuration == "W") or
          (TypeDuration == "M") or
          (TypeDuration == "Y"))
          Duration = String(Int(substr(retVal, 1, strlen(retVal)-1)));
      else
          retVal = "";
      end;

      return retVal;
    OnError(err)
        return "";
  end;

    BBB_ = "";
    KKK_ = "";
    SSS_ = "";
    CCC_ = "";

    BBB_ = substr(_str, idx+1, 3); idx = idx + 3;
    if (substr(_str, idx+1, 1) == "_")
        idx = idx + 1;
    end;
    KKK_ = substr(_str, idx+1, 3); idx = idx + 3;
    if (substr(_str, idx+1, 1) == "_")
        idx = idx + 1;
        if (substr(_str, idx+1, 1) == "_")
            idx = idx + 1;
            CCC_ = substr(_str, idx+1, 3);
        else
            CCC_ = substr(_str, idx+1, 3); idx = idx + 3;
            if (substr(_str, idx+1, 1) != "")
                SSS_ = CCC_;
                CCC_ = substr(_str, idx+1, 2);
            end;
        end;
    else
        SSS_ = substr(_str, idx+1, 3); idx = idx + 3;
        CCC_ = substr(_str, idx+1, 3);
    end;

    if (KKK_ == "000")
        KKK_ = "RUB";
    end;
    SSS_ = ConvertTerm(SSS_);
    CCC_ = ConvertTerm(CCC_);

    SetParm(1, BBB_);
    SetParm(2, KKK_);
    SetParm(3, SSS_);
    SetParm(4, CCC_);
END;

PRIVATE MACRO Parse(_nodes)
  var
      idx = 0, node = NULL, obj = NULL,
      BOARDID , SECID, TRADEDATE, BASERATE, WAPRICE,
      BBB, KKK, SSS, CCC, OtherBoard = TArray(), i = 0;

    while (idx < _nodes.length)
        node = _nodes.item(idx);

        ReadTagAttribut(node, "BOARDID",   V_STRING, BOARDID);
        ReadTagAttribut(node, "SECID",     V_STRING, SECID);
        ReadTagAttribut(node, "TRADEDATE", V_STRING, TRADEDATE);
        ReadTagAttribut(node, "BASERATE",  V_STRING, BASERATE);
        ReadTagAttribut(node, "WAPRICE",   V_STRING, WAPRICE);

        if (BOARDID == "CETS")
            if ( 
                ((strlen(WAPRICE) == 0)or(Double(WAPRICE) == 0)) and
                ((strlen(BASERATE) == 0)or(Double(BASERATE) == 0))
               )
                OtherBoard[OtherBoard.size] = SECID;
                idx=idx+1;
                continue;
            end;
        elif (BOARDID == "CNGD")
            i = 0;
            while (i < OtherBoard.size)
                if (OtherBoard[i] == SECID) break; end;
                i = i + 1;
            end;

              if (i == OtherBoard.size)
                 idx=idx+1;
                 continue; 
              end; 
        end;

        ParseSECID(SECID, BBB, KKK, SSS, CCC);

        if ((BBB == "GLD")or(BBB == "SLV")) continue; end;

        if ((strlen(BASERATE) > 0)and(Double(BASERATE) > 0))
            if ((strlen(SSS) > 0))
                obj = UL_InputDataObj_Base();
                obj.Add("SECID", BBB+"/"+KKK+"_"+SSS);
                obj.Add("TRADEDATE", TRADEDATE);
                if (substr(WAPRICE, 1, 1) == "-")
                    obj.Add("RATEKIND", "30");
                    obj.Add("RATE", substr(WAPRICE, 2, strlen(WAPRICE)-1));
                else
                    obj.Add("RATEKIND", "27");
                    obj.Add("RATE", WAPRICE);
                end;
                Rows[Rows.size] = obj;
            end;

            if (strlen(CCC) > 0)
                obj = UL_InputDataObj_Base();
                obj.Add("SECID", BBB+"/"+KKK+"_"+CCC);
                obj.Add("TRADEDATE", TRADEDATE);
                obj.Add("RATEKIND", "29");
                obj.Add("RATE", BASERATE);
                Rows[Rows.size] = obj;
            end;
        else
            if (strlen(CCC) > 0)
                obj = UL_InputDataObj_Base();
                obj.Add("SECID", BBB+"/"+KKK+"_"+CCC);
                obj.Add("TRADEDATE", TRADEDATE);
                obj.Add("RATEKIND", "28");
                obj.Add("RATE", WAPRICE);
                Rows[Rows.size] = obj;
            end;
        end;

        idx = idx + 1;
    end;
END;

MACRO UL_Parse_MMVBRate_XML(_input_data, _loader)
  var
      retVal = NULL,
      xml:object  = ActiveX("MSXML.DOMDocument");

    if (not xml.load(String(_input_data)))
        _loader.AddErrorLog("Невозможно загрузить xml-документ: " + String(_input_data));
        return NULL;
    end;
    Parse(xml.DocumentElement.selectNodes("//data//rows//row"));

    return Rows;
END;
////////////////////////////////////////////////////////////////////////
MACRO UL_Parse_MMVBRate_XLS(_input_data, _loader)
  var
      retVal = TArray(),
      obj = NULL,
      TF = CDAOMSExcel(String(_input_data), true),
      idxRow = 0, idxColumn = 0,
      CurrentRow = 0, CurrentColumn = 0;

    TF.SheetChange(_loader.formatConfig.List());

    CurrentRow = TF.Sheet.Range(_loader.formatConfig.CellAdr()).Row;
    CurrentColumn = TF.Sheet.Range(_loader.formatConfig.CellAdr()).Column;

    while (true)
        if (idxRow == 0)
            ;
        elif (Mod(CurrentRow, 2) == 0)
            if (strlen(TF.Sheet.Cells(CurrentRow, CurrentColumn).text) > 0)
                idxColumn = 0;
                while (idxColumn < 15)
                    obj = UL_InputDataObj_Base();
                    obj.Add("SECID",     TF.Sheet.Cells((CurrentRow-idxRow), CurrentColumn+idxColumn+1).text);
                    obj.Add("TRADEDATE", TF.Sheet.Cells(CurrentRow, CurrentColumn).text);
                    obj.Add("RATE",      TF.Sheet.Cells(CurrentRow, CurrentColumn+idxColumn+1).text);
                    retVal[retVal.size] = obj;

                    idxColumn = idxColumn + 1;
                end;
            else
                break;
            end;
        end;

        CurrentRow = CurrentRow + 1;
        idxRow = idxRow + 1;
    end;

    TF.Close();

    return retVal;
END;