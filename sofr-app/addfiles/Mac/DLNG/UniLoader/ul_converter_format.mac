/*
$Name: ul_converter_format.mac
$Module: Универсальный загрузчик
$Description: Конвертер внешних данных и заполнение выходной структуры по таблице соответствия
*/

IMPORT RsbDataSet, RSD, "ul_const.mac";

PRIVATE CLASS ConvertRule(_input_fldname, _output_fldname, _datatype, _isRequired, _convert_macro, _convert_fun)
  var
      input_fldname = _input_fldname,
      output_fldname = _output_fldname,
      datatype = 0,
      isRequired = false,
      convert_macro = "",
      convert_fun = "";

    //конструтор
    if (ValType(_datatype) == V_INTEGER)
        datatype = _datatype;
    end;
    if (ValType(_isRequired) == V_BOOL)
        isRequired = _isRequired;
    elif (_isRequired == ULSET_CHAR)
        isRequired = true;
    end;
    if (ValType(_convert_macro) == V_STRING)
        convert_macro = _convert_macro;
    end;
    if (ValType(_convert_fun) == V_STRING)
        convert_fun = _convert_fun;
    end;
END;

CLASS UL_Converter_Format(_formatID)
 PRIVATE VAR
     Format = _formatID,
     ConvertRules = TArray(),
     DefferedRules = NULL,
     ObjMacroName = "",
     ObjClass = "";

  MACRO AddRule(input_fldname, output_fldname, datatype, isRequired, convert_macro, converter_fun)
      ConvertRules[ConvertRules.size] = ConvertRule(input_fldname, output_fldname, datatype, isRequired, convert_macro, converter_fun);
  END;

  PRIVATE MACRO ConvertInType(_val, _datatype)
    var
        retVal = NULL;

      if (ValType(_val) != V_UNDEF)
          if(_datatype == V_INTEGER)
              retVal = int(_val);
          elif(_datatype == V_DATE)
              retVal = date(_val);
          elif(_datatype == V_TIME)
              retVal = time(_val);
          elif(_datatype == V_MONEYL)
              retVal = money(_val);
          elif(_datatype == V_DOUBLE)
              retVal = double(_val);
          else
              retVal = String(_val);
          end;
      else
          if(_datatype == V_INTEGER)
              retVal = int(0);
          elif(_datatype == V_DATE)
              retVal = date(0,0,0);
          elif(_datatype == V_TIME)
              retVal = time();
          elif(_datatype == V_MONEYL)
              retVal = money(0);
          elif(_datatype == V_DOUBLE)
              retVal = double(0);
          else
              retVal = "";
          end;
      end;

      return retVal;
  END;

  PRIVATE MACRO SetOutputValue(_DataObj, _field, _val)
    var
        str = "MACRO SetObjValue(_DataObj, _val) _DataObj." + _field + " = _val;  END;";
      ExecMacroModule(str, "SetObjValue", _DataObj, _val);
  END;

  PRIVATE MACRO Converter(_inputDataObj, _DataObj, _Log)
    var
        idx = 0, input_value, cnvresult = 0,
        DeffRules = TArray;

      while (idx < DefferedRules.size)
          input_value = _inputDataObj.GetValue(DefferedRules[idx].input_fldname);
          if ( 
              (strlen(DefferedRules[idx].convert_macro) > 0) and
              (strlen(DefferedRules[idx].convert_fun) > 0)
             )
              InstLoadModule(DefferedRules[idx].convert_macro);
              cnvresult = ExecMacro2(DefferedRules[idx].convert_fun, input_value, input_value, _inputDataObj, _DataObj, _Log);

              if (cnvresult == 0)
                  ;
              elif (cnvresult == 1)
                 DeffRules[DeffRules.size] = DefferedRules[idx];
                 input_value = NULL
              else
                  _Log.ErrorLog("Ошибка при конвертации данных из <" + DefferedRules[idx].input_fldname + "> в <" + DefferedRules[idx].output_fldname + ">");
                  return NULL;
              end;
          end;

          if ((ValType(input_value) == V_UNDEF)and(DefferedRules[idx].isRequired))
                  _Log.ErrorLog("Ошибка в формате входящих данных. Отсутствует обязательное поле <" + DefferedRules[idx].input_fldname + ">");
                  return NULL;
          end;

          if (ValType(input_value) != V_UNDEF)
              input_value = ConvertInType(input_value, DefferedRules[idx].datatype);
              SetOutputValue(_DataObj, DefferedRules[idx].output_fldname, input_value);
          end;

          idx = idx + 1;
      end;

      DefferedRules = DeffRules;

      return _DataObj;
  END;

  MACRO Convert(_inputDataObj, _Log)
    var
        retVal = NULL; 

      if (
          (strlen(ObjMacroName) > 0)and
          (strlen(ObjClass) > 0)
         )
          retVal = GenObject(ObjClass)
      else
          _Log.ErrorLog("Невозможно создать выходную структуру");
          return NULL;
      end;

      DefferedRules = ConvertRules;
      while ((DefferedRules.size > 0)and(ValType(retVal) != V_UNDEF))
          retVal = Converter(_inputDataObj, retVal, _Log);
      end;

      return retVal;
  END;

  PRIVATE MACRO InitObj()
    var
        sql = NULL, ds = NULL;

      sql = RsdCommand(
                       "select * from ddl_ul_format_dbt f, ddl_ul_datakind_dbt dk where f.t_ID = ? and dk.t_ID = f.t_ID_DataKind"
                       );
      sql.addParam("", RSDBP_IN, Format);
      sql.execute();
      ds = TRsbDataSet(sql);
      if (ds.MoveNext())
          ObjMacroName = string(ds.t_Macro);
          ObjClass = string(ds.t_ObjClass);
      end;

      if (strlen(ObjMacroName) > 0)
          InstLoadModule(ObjMacroName);
      end;

      sql = RsdCommand(
                       "select * from ddl_ul_rule_dbt where t_ID_Format = ?"
                       );
      sql.addParam("", RSDBP_IN, Format);
      sql.execute();
      ds = TRsbDataSet(sql);
      while (ds.MoveNext())
          this.AddRule(ds.InField, ds.OutField, ds.DataType, ds.IsMandatory, ds.ConvertMacro, ds.ConvertFunc);
      end;
  END;

  //конструктор
  InitObj();
END;
