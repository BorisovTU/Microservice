/*
$Name: dl_ul.mac
$Module: Универсальный загрузчик
$Description: Вызов импорта через механизм универсального загрузчика
*/

IMPORT RsbDataSet, RSD, BankInter, "ul_const.mac", "UL_SelectFormat.mac", "UL_UniLoader.mac";

MACRO LoadExternalData(_formatID, _manipulation_func, _log_func)
  var
      selectFormatPanel = NULL, groupConfigPanel = NULL, formatConfigPanel = NULL,
      retVal = 0, sql = NULL, DataSet = NULL, isStartLoader = true,
      loader = NULL, strErr = ""; 

    if (
        (ValType(_formatID) != V_INTEGER) or
        (_formatID < 0)
       )
        _formatID = 0
    end;

    if (
        (ValType(_manipulation_func) != V_STRING)
       )
        _manipulation_func = "";
    end;
    
    // если интерфейсный режим и заранее не передали идентификатор формата, то выберем его через панель
    if (
        (GetDialogFlag()) and
        (_formatID == 0)
       )
        selectFormatPanel = UL_SelectFormat();
        if (selectFormatPanel.run() == -ULK_F9)
            _formatID = selectFormatPanel.GetFormatID();
        end;
    end;

    if (_formatID > 0)
        sql = RsdCommand(
                         "select gf.t_PanelParmMacro t_grMacro, gf.t_PanelParmClass as t_grClass, f.t_PanelParmMacro as t_fMacro, f.t_PanelParmClass as t_fClass " +
                         " from ddl_ul_groupformat_dbt gf, ddl_ul_format_dbt f where " +
                         "f.t_ID = ? and gf.t_ID = f.t_ID_GroupFormat"
                         );
        sql.addParam("", RSDBP_IN, _formatID);
        sql.execute();

        DataSet = TRsbDataSet(sql);
        DataSet.MoveNext();

        if ((strlen(DataSet.fMacro) > 0)and(strlen(DataSet.fClass) > 0))
            InstLoadModule(string(DataSet.fMacro));
            formatConfigPanel = GenObject(String(DataSet.fClass), _formatID);
        end;

        if ((strlen(DataSet.grMacro) > 0)and(strlen(DataSet.grClass) > 0))
            InstLoadModule(string(DataSet.grMacro));
            groupConfigPanel = GenObject(String(DataSet.grClass), 0, _formatID);
            groupConfigPanel.FormatParmPanel = formatConfigPanel;
        end;

        // если интерфейсный режим, то предлагем к изменению параметры импорта, иначе берем параметры умолчанию
        if (GetDialogFlag())
            isStartLoader = false;
            if (
                (ValType(groupConfigPanel) == V_UNDEF)and
                (ValType(formatConfigPanel) == V_UNDEF)
               )
                isStartLoader = true;
            elif (ValType(groupConfigPanel) != V_UNDEF)
                if (groupConfigPanel.run(PANELMODE_SELECT) == -ULK_F2)
                    isStartLoader = true;
                end;
            elif (ValType(formatConfigPanel) != V_UNDEF) // если по группе интерфес не задан, то выведем заполнение параметров по формату
                if (formatConfigPanel.run(PANELMODE_SELECT) == -ULK_F2)
                    isStartLoader = true;
                end;
            end;
        end;

        if (isStartLoader)
            loader = UL_UniLoader(_formatID);

            if (ValType(groupConfigPanel) != V_UNDEF)
                if (not groupConfigPanel.CheckParm(strErr))
                    isStartLoader = false;
                    loader.GetErrorsLog().ErrorLog(strErr);
                end;
            end;
        end;

        if (isStartLoader)
            if (ValType(formatConfigPanel) != V_UNDEF)
                if (not formatConfigPanel.CheckParm(strErr))
                    isStartLoader = false;
                    loader.GetErrorsLog().ErrorLog(strErr);
                end;
            end;
        end;

        if (isStartLoader)
            retVal = loader.LoadData(groupConfigPanel, formatConfigPanel, _manipulation_func);
        end;

        if (ValType(loader) != V_UNDEF)
            loader.GetErrorsLog().SaveLog(_log_func);
        end;
    end;

    return retVal;
END;