/*
$Name:        ul_groupformat_i.mac
$Module:      Универсальный загрузчик
$Description: Справочник "Группы форматов"
*/

IMPORT RsbFormsInter, RsbDataSet, RSD, "ul_format_i.mac", "ul_const.mac";

PRIVATE CLASS GroupFormat_PanelCacheValue()
  VAR
      idField         = 0,
      nameField       = "",
      codeField       = "",
      macroPanField   = "",
      classPanelField = "",
      macroDataField  = "",
      funcDataField   = "",
      isUsingField    = false,
      isUpdateField   = false;
END;

PRIVATE CLASS (TRsbPanel)GroupFormat_Panel(_rs, _x,_y)
  PRIVATE VAR
      rs = _rs,
      x = _x,
      y = _y,
      cache = GroupFormat_PanelCacheValue();

  VAR
      fld_idField         = NULL,
      fld_nameField       = NULL,
      fld_codeField       = NULL,
      fld_macroPanField   = NULL,
      fld_classPanelField = NULL,
      fld_macroDataField  = NULL,
      fld_funcDataField   = NULL,
      fld_isUsingField    = NULL,
      fld_isUpdateField   = NULL;

  PRIVATE MACRO GetId()
    var
        maxVal:integer = 0,
        cmd = RSDCommand ("select max(T_ID) as maxid from DDL_UL_GROUPFORMAT_DBT"),
        rsID = NULL;

      cmd.execute();
      rsID = TRsbDataSet(cmd);

      if (rsID.movenext())
          maxVal = rsID.maxid; 
      else
          maxVal = 100;
      end;

      if (ValType(maxVal) == V_UNDEF)
          maxVal = 100;
      end;

      if (maxVal < 100)
          maxVal = 100;
      elif (maxVal >= 100)
          maxVal = maxVal + 1;
      end;

      return maxVal;
  END;

  MACRO SaveChanges()
    var
        groupFormat = TBfile("DL_UL_GROUPFORMAT", "W", 0);

      groupFormat.rec.ID               = this.fld_idField.Value;
      if (groupFormat.rec.ID == 0)
          groupFormat.rec.ID = GetId();
      else
          groupFormat.getEQ();
      end;
      groupFormat.rec.NAME             = trim(this.fld_nameField.Value);
      groupFormat.rec.CODE             = trim(this.fld_codeField.Value);
      groupFormat.rec.PANELPARMMACRO   = trim(this.fld_macroPanField.Value);
      groupFormat.rec.PANELPARMCLASS   = trim(this.fld_classPanelField.Value);
      groupFormat.rec.DATAPRELOADMACRO = trim(this.fld_macroDataField.Value);
      groupFormat.rec.DATAPRELOADFUNC  = trim(this.fld_funcDataField.Value);
      groupFormat.rec.ISUSE            = StrFor(0);
      groupFormat.rec.ISUPDATE         = StrFor(0);

      if (fld_isUsingField.checked)
          groupFormat.rec.ISUSE = "X";
      end;
      if (fld_isUpdateField.checked)
          groupFormat.rec.ISUPDATE = "X";
      end;

      if (strlen(groupFormat.rec.CODE) == 0)
          msgbox("Не заполнено обязательное поле 'Код группы'");
          return false;
      end;

  /*    if (strlen(groupFormat.rec.PANELPARMMACRO) == 0)
          msgbox("Не заполнено обязательное поле макроса параметра импорта!");
          return false;
      end;

      if (strlen(groupFormat.rec.PANELPARMCLASS) == 0)
          msgbox("Не заполнено обязательное поле класса параметра импорта!");
          return false;
      end;
*/

      if (strlen(groupFormat.rec.NAME) == 0)
          groupFormat.rec.NAME = "Группа №" + groupFormat.rec.ID;
      end;

      if (ValType(rs) == V_UNDEF)
          groupFormat.insert();
      else
          groupFormat.update();
      end;

      return true;

    OnError(RslErrObj)
        if (ValType(rs) == V_UNDEF)
            msgbox("Ошибка при вставке новой группы форматов");
        else
            msgbox("Ошибка при редактировании группы форматов");
        end;

        return false;
  END;

  MACRO CheckChanges()
    var
        changed = false;

      if ((changed == false) AND (cache.idField != this.fld_idField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.nameField != this.fld_nameField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.codeField != this.fld_codeField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.macroPanField != this.fld_macroPanField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.classPanelField != this.fld_classPanelField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.macroDataField != this.fld_macroDataField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.funcDataField != this.fld_funcDataField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.isUsingField != this.fld_isUsingField.checked))
          changed=true;
      end;
      if ((changed == false) AND (cache.isUpdateField != this.fld_isUpdateField.checked))
          changed=true;
      end;
 
      return changed;
  END;

  MACRO eventHandler(EV)
      if (EV.keyCode == ULK_F9)
          if ((this.CheckChanges())and(SaveChanges()))
              close(-EV.keyCode);
          end;
      elif (EV.keyCode == ULK_ESC)
          if (this.CheckChanges())
              var isSave = MsgBoxEx("Сохранить изменения?", MB_YES + MB_NO + MB_CANCEL, IND_YES);
              if (isSave == IND_YES)
                  EV.keyCode = ULK_F9;
                  this.eventHandler(EV);
              elif (isSave == IND_NO)
                  close(-EV.keyCode);
              else
                  EV.keyCode = 0;
              end;
          end;
      end;
  END;

  MACRO MakeCache()
      cache.idField         = this.fld_idField.Value;
      cache.nameField       = this.fld_nameField.Value;
      cache.codeField       = this.fld_codeField.Value;
      cache.macroPanField   = this.fld_macroPanField.Value;
      cache.classPanelField = this.fld_classPanelField.Value;
      cache.macroDataField  = this.fld_macroDataField.Value;
      cache.funcDataField   = this.fld_funcDataField.Value;
      cache.isUsingField    = this.fld_isUsingField.checked;
      cache.isUpdateField   = this.fld_isUpdateField.checked; 
  END;
  
  MACRO InitDefValue()
      if  (ValType(rs) != V_UNDEF)
          fld_idField.Value         = Int(rs.Value("T_ID"));
          fld_nameField.Value       = rs.Value("T_NAME");
          fld_codeField.Value       = rs.Value("T_CODE");
          fld_macroPanField.Value   = rs.Value("T_PANELPARMMACRO");
          fld_classPanelField.Value = rs.Value("T_PANELPARMCLASS");
          fld_macroDataField.Value  = rs.Value("T_DATAPRELOADMACRO");
          fld_funcDataField.Value   = rs.Value("T_DATAPRELOADFUNC");
          fld_isUsingField.Checked  = false;
          fld_isUpdateField.Checked = false;

          if (rs.Value("T_ISUSE") == "X")
              fld_isUsingField.Checked = true;
          end;
          if (rs.Value("T_ISUPDATE") == "X")
              fld_isUpdateField.Checked = true;
          end;
      else
          fld_idField.Value         = 0;
          fld_nameField.Value       = "";
          fld_codeField.Value       = "";
          fld_macroPanField.Value   = "";
          fld_classPanelField.Value = "";
          fld_macroDataField.Value  = "";
          fld_funcDataField.Value   = "";
          fld_isUsingField.Checked = false;
          fld_isUpdateField.Checked = false;
      end;

      this.Redraw();
  END;

  InitTRsbPanel();

  caption = "Группа форматов";
  setPosition(x, y);
  setSize(55, 16);
  status = "~F9~ Сохранить ~Esc~ Выход";

  addLabel(TRsbLabel(3, 2, "Номер группы форматов:"));
  fld_idField = TRsbEditField();
  fld_idField.name = "fld_idField";
  fld_idField.datatype = FT_INT;
  fld_idField.setPosition(20, 2);
  fld_idField.setSize(3, 1);
  fld_idField.editable = false;
  addControl(fld_idField);

  addLabel(TRsbLabel(26, 2, "Код группы форматов:"));
  fld_codeField = TRsbEditField();
  fld_codeField.name = "fld_codeField";
  fld_codeField.datatype = FT_STRING; 
  fld_codeField.textlength = 10;
  fld_codeField.setPosition(42, 2);
  fld_codeField.setSize(10, 1);
  addControl(fld_codeField);

  addLabel(TRsbLabel(3, 4, "Наименование группы форматов:"));
  fld_nameField = TRsbEditField();
  fld_nameField.name = "fld_nameField";
  fld_nameField.datatype = FT_STRING; 
  fld_nameField.textlength = 255;
  fld_nameField.setPosition(25, 4);
  fld_nameField.setSize(27, 1);
  addControl(fld_nameField);                  

  addLabel(TRsbLabel(3, 6, "Панель общих параметры импорта группы форматов:"));

  addLabel(TRsbLabel(4, 7, "Макрос:"));
  fld_macroPanField = TRsbEditField();
  fld_macroPanField.name = "fld_macroPanField";
  fld_macroPanField.datatype = FT_STRING; 
  fld_macroPanField.textlength = 50;
  fld_macroPanField.setPosition(11, 7);
  fld_macroPanField.setSize(41, 1);
  addControl(fld_macroPanField);

  addLabel(TRsbLabel(4, 8, "Класс:"));
  fld_classPanelField = TRsbEditField();
  fld_classPanelField.name = "fld_classPanelField";
  fld_classPanelField.datatype = FT_STRING;
  fld_classPanelField.textlength = 30;
  fld_classPanelField.setPosition(11, 8);
  fld_classPanelField.setSize(41, 1);
  addControl(fld_classPanelField);

  addLabel(TRsbLabel(3, 10, "Предобработка данных:"));

  addLabel(TRsbLabel(4, 11, "Макрос:"));
  fld_macroDataField = TRsbEditField();
  fld_macroDataField.name = "fld_macroDataField";
  fld_macroDataField.datatype = FT_STRING; 
  fld_macroDataField.textlength = 50;
  fld_macroDataField.setPosition(11, 11);
  fld_macroDataField.setSize(41, 1);
  addControl(fld_macroDataField);

  addLabel(TRsbLabel(4, 12, "Функция:"));
  fld_funcDataField = TRsbEditField();
  fld_funcDataField.name = "fld_funcDataField";            
  fld_funcDataField.datatype = FT_STRING; 
  fld_funcDataField.textlength = 30;
  fld_funcDataField.setPosition(11, 12);
  fld_funcDataField.setSize(41, 1);
  addControl(fld_funcDataField);

  addLabel(TRsbLabel(5, 14, "Не используется"));
  fld_isUsingField = TRsbCheckBox();
  fld_isUsingField.name = "fld_isUsingField";          
  fld_isUsingField.datatype = FT_CHR; 
  fld_isUsingField.setPosition(3, 14);
  addControl(fld_isUsingField);

  addLabel(TRsbLabel(5, 15, "Обновлять при переходе на следующую сборку"));
  fld_isUpdateField = TRsbCheckBox();
  fld_isUpdateField.name = "fld_isUpdateField";            
  fld_isUpdateField.datatype = FT_CHR; 
  fld_isUpdateField.setPosition(3, 15);
  addControl(fld_isUpdateField);
  if  ((ValType(rs) == V_UNDEF)or(Int(rs.Value("T_ID")) >= 100))
      fld_isUpdateField.enabled = false;
  end;

  addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));

  InitDefValue();
  MakeCache();
END;

MACRO GroupFormat_Scrol(x, y) : RsdRecordSet
  var
      rs = NULL, cmd = NULL,
      Columns = TArray();

  macro DeleteGroupFormat()
    var
        cmd = NULL;

      RsdCommand("delete from ddl_ul_rule_dbt where t_ID_FORMAT in (select t_ID from ddl_ul_format_dbt where t_ID_GROUPFORMAT = " + String(Int(rs.Value("T_ID"))) + ")").execute;
      RsdCommand("delete from ddl_ul_format_dbt where t_ID_GROUPFORMAT = " + String(Int(rs.Value("T_ID")))).execute;
      RsdCommand("delete from ddl_ul_groupformat_dbt where t_ID = " + String(Int(rs.Value("T_ID")))).execute;

      return true;

    OnError(RslErrObj)
        AbortTrn();
        return false;
  end;

  macro proc(rs, cmd, id, key)
    var
        retVal  = CM_DEFAULT,
        pan     = NULL,
        rsCheck = NULL, isDel = false;

      if (key == ULK_F9)
          retVal = CM_IGNORE;
          pan = GroupFormat_Panel(NULL, 30, 10);
          if (abs(pan.run()) == ULK_F9)
              retVal = CM_SELECT;
          end;
      elif (key == ULK_F8)
          retVal = CM_IGNORE;
          if (rs.RecCount > 0)
              if ((Int(rs.Value("T_ID")) >= 100) and 
                  (MsgBoxEx("Вы действительно хотите удалить?", MB_YES + MB_NO, IND_YES) == IND_YES))
                  isDel = true;

                  rsCheck = TRsbDataSet("select count(1) as cnt from ddl_ul_format_dbt where t_ID_GROUPFORMAT = " + String(Int(rs.Value("T_ID"))));
                  rsCheck.MoveNext();
                  if (rsCheck.CNT > 0)
                      isDel = false;
                      if (MsgBoxEx("Вы действительно хотите удалить привязанные форматы?", MB_YES + MB_NO, IND_YES) == IND_YES)
                          isDel = true;
                      end;
                  end;
              end;

              if (isDel)
                  if (ProcessTrn(0, "DeleteGroupFormat"))
                      retVal = CM_SELECT;
                  else
                      msgbox("Ошибка при удалении вида данных");
                  end;
              end;
          end;
      elif (key == ULK_ENTER)
          retVal = CM_IGNORE;
          if (rs.RecCount > 0)
              pan = GroupFormat_Panel(rs, 30, 10);
              if (abs(pan.run()) == ULK_F9)
                  retVal = CM_SELECT;
              end;
          end;
      elif (key == ULK_ALTG)
          if (rs.RecCount > 0)
              if ((strlen(String(rs.Value("T_PANELPARMMACRO"))) > 0)and
                  (strlen(String(rs.Value("T_PANELPARMCLASS"))) > 0))
                  InstLoadModule(String(rs.Value("T_PANELPARMMACRO")));
                  var groupConfigPanel = GenObject(String(rs.Value("T_PANELPARMCLASS")), Int(rs.Value("T_ID")), 0);
                  groupConfigPanel.run(PANELMODE_EDIT);
                  groupConfigPanel = NULL;
              end;
          end;
      elif (key == ULK_ALT5)
          if (rs.RecCount > 0)
              Format_Scrol(35, 1, Int(rs.Value("T_ID")));
          end;
      end;

      pan = NULL;
      rsCheck = NULL;

      return retVal;
  end;

  macro AddColumn(ar,ind, fld, head, width)
      ar.value( ind * 6 + 0 ) = fld;
      ar.value( ind * 6 + 1 ) = head;
      ar.value( ind * 6 + 2 ) = width;
      ar.value( ind * 6 + 3 ) = 2;
      ar.value( ind * 6 + 4 ) = NULL;
      ar.value( ind * 6 + 5 ) = 0;
  end;

    AddColumn(Columns, 0, "t_id", "№ п/п", 5);
    AddColumn(Columns, 1, "t_name", "Группа форматов", 35);
    AddColumn(Columns, 2, "t_isuse", "И", 1);
    AddColumn(Columns, 3, "t_isupdate", "О", 1);

    cmd = RsdCommand("SELECT * FROM DDL_UL_GROUPFORMAT_DBT ORDER BY T_ID");
    rs = RsdRecordSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);

    while (RunScroll(rs, 4, Columns, NULL, @proc, "Группы форматов", "~F9~ Ввод ~Enter~ Редактирование ~Alt-G~ Настройки ~Alt-5~ Форматы ~Esc~ Выход ", true, x, y, 50, 80))
        rs.Refresh();
    end;

    return rs;
END;
