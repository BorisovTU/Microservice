/*
$Name: ul_dataloader.mac
$Module: Универсальный загрузчик
$Description: Ядро загрузчика
*/

IMPORT RsbDataSet, RSD, "UL_Converter_Format.mac";

CLASS UL_DataLoader(_formatID)
  PRIVATE VAR
      Converter = UL_Converter_Format(_formatID);

  VAR
      Format       = _formatID,
      groupConfig  = NULL,
      formatConfig = NULL,
      Func         = "",
      Log          = NULL;

  MACRO AddErrorLog(_err)
      if (ValType(this.Log) != V_UNDEF)
          this.Log.ErrorLog(_err);
      end;
  END;

  MACRO LoadData(_input_data)
    var
        retVal = 0, idx = 0,
        sql = NULL, ds = NULL,
        IntermediateObj = NULL, DataObj = NULL;

      sql = RsdCommand(
                       "select * from ddl_ul_format_dbt where t_ID = ? "
                       );
      sql.addParam("", RSDBP_IN, Format);
      sql.execute();

      ds = TRsbDataSet(sql);
      if (ds.MoveNext())
          if (
              (strlen(ds.ParseMacro) > 0)and
              (strlen(ds.ParseFunc) > 0)
             )
              InstLoadModule(String(ds.ParseMacro));
              IntermediateObj = ExecMacro2(String(ds.ParseFunc), _input_data, this);
              if (IsEqClass("TArray", IntermediateObj))
                  while (idx < IntermediateObj.size)
                      DataObj = Converter.Convert(IntermediateObj[idx], this.Log);
                      if (ValType(DataObj) != V_UNDEF)
                          if (ExecMacro2(this.Func, this, DataObj))
                              retVal = 0;
                          else
                              retVal = 1;
                          end;
                      end;
    
                      idx = idx + 1;
                  end;
              else
                  this.AddErrorLog("Ошибка при разборе формата внешних данных");
                  retVal = 1;
              end;
          else
              this.AddErrorLog("Не определена макрофункция разбора формата");
              retVal = 1;
          end;
      else
          this.AddErrorLog("Не найден формат");
          retVal = 1;
      end;

      return retVal;
  END;
END;
