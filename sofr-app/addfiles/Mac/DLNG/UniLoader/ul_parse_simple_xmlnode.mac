/*
$Name: ul_parse_simple_xmlnode.mac
$Module: Универсальный загрузчик
$Description: Разбор файловых xml-форматов и формирование массива промежуточных объектов. В одном XML-файле хранится один вид объекта
*/

IMPORT "UL_InputDataObj.mac";

PRIVATE VAR
    Path = NULL,
    obj  = NULL;

PRIVATE MACRO GetPath()
  var
      retVal = "",
      idx = 0;

    while (idx < Path.size)
        if (idx > 0)
            retVal = retVal + ".";
        end;
        retVal = retVal + Path[idx];
        idx = idx + 1;
    end;
    return retVal;
END;

PRIVATE MACRO AddAttributValue(_node)
  var 
      idx = 0, Path = GetPath();

    while (idx < _node.attributes.length)
        obj.Add((Path + "." + _node.attributes.item(idx).nodeName), _node.attributes.item(idx).NodeValue);
        idx = idx + 1;
    end;
END;

PRIVATE MACRO Parse(_node)
  var
      idx = 0;

    if (_node.nodeType == 1)
        Path[Path.size] = _node.tagname;

        AddAttributValue(_node);

        if ((_node.text == NULL) or (_node.text == "") or (_node.text == StrFor(0)) or (_node.text == StrFor(1)))
            obj.Add(GetPath(), "");
        else

            while (idx < _node.childNodes.length)
                Parse(_node.childNodes.item(idx));
                idx = idx + 1;
            end;
        end;

        Path.size = Path.size - 1;
    elif (_node.nodeType == 3)
        obj.Add(GetPath(), _node.text);
    end
END;

MACRO UL_Parse_Simple_XMLNode(_input_data, _loader)
  var
      retVal = TArray(),
      xml:object  = ActiveX("MSXML.DOMDocument");

    Path = NULL; Path = TArray();
    obj  = NULL; obj  = UL_InputDataObj_Base();

    if (not xml.load(String(_input_data)))
        _loader.AddErrorLog("Невозможно загрузить xml-документ: " + String(_input_data));
        return NULL;
    end;
    Parse(xml.DocumentElement);
    retVal[retVal.size] = obj;

    return retVal;
END;