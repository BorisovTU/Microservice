/*
$Name: ul_formatparm_xls.mac
$Module: Универсальный загрузчик
$Description: Панель параметров xls-форматов
*/

IMPORT RsbFormsInter, BankInter, RsbDataSet, RSD, "ul_const.mac";

PRIVATE CLASS XLS_CacheValue()
  var
      List = "",
      CellAdr = "";
END;

CLASS(TRsbPanel) UL_FormatParm_XLS(_formatID)
  PRIVATE VAR
      sql = NULL;

  PRIVATE VAR
      run_mode   = PANELMODE_EDIT,
      fld_List  = NULL,  // поле "Лист",
      fld_CellAdr  = NULL,  // поле "Адрес первой строки с данными",
      cache = XLS_CacheValue(),
      key = 0;

  VAR
      IsInitObj  = false, // признак, что панель уже проинициализирована
      formatID   = _formatID,
      structFormat    = NULL;

  MACRO List()
      return fld_List.Value;
  END;
  MACRO Set_List(_val)
      fld_List.Value = _val;
  END;

  MACRO CellAdr()
      return fld_CellAdr.Value;
  END;
  MACRO Set_CellAdr(_val)
      fld_CellAdr.Value = _val;
  END;

  MACRO CheckParm(errStr_)
    var
        retVal = true;

      errStr_ = "";

      if (strlen(this.List()) == 0)
          errStr_ = "Не задано название листа с данными";
          retVal = false;
      end;


      if (strlen(this.CellAdr()) == 0)
          errStr_ = "Не задана первая ячейка диапазона данных";
          retVal = false;
      end;

      SetParm(1, errStr_);

      return retVal;
  END;

  PRIVATE MACRO GetRegistry(_regKey, _type, _default)
    var
        retVal = _default,
        stat = 0,
        full_regPath = "";

      full_regPath = "COMMON\\UNILOADER\\FORMATS\\" + structFormat.Code + "\\" + _regKey;

      if (strlen(full_regPath) > 0)
          GetRegistryValue(full_regPath, _type, retVal, stat);
      else
          stat = 1;
      end;

      if (stat)
          retVal = _default;
      end;

      return retVal;
  END;

  PRIVATE MACRO SetRegistry(_regKey, _val)
    var
        full_regPath = "";

      full_regPath = "COMMON\\UNILOADER\\FORMATS\\" + structFormat.Code + "\\" + _regKey;
      if (strlen(full_regPath) > 0)
          SetDefaultRegistryValue (full_regPath, _val);
      end;
  END;

  MACRO InitDefValue()
      this.Set_List(GetRegistry("LIST", V_STRING, ""));
      this.Set_CellAdr(GetRegistry("CELLADR", V_STRING, 0));
  END;

  MACRO CheckChanges()
    var
        retVal = false;

      if (this.List() != cache.List)
          retVal = true;
      end;

      if (this.CellAdr() != cache.CellAdr)
          retVal = true;
      end;

      return retVal;
  END;

  MACRO Save()
      if (cache.List != List())
          SetRegistry("LIST", List());
      end;

      if (cache.CellAdr != CellAdr())
          SetRegistry("CELLADR", CellAdr());
      end;

      return true;
  END;

  MACRO Restore()
      this.Set_List(cache.List);
      this.Set_CellAdr(cache.CellAdr);
  END;

  MACRO run(_mode)
      run_mode = _mode;

      if (run_mode == PANELMODE_EDIT) // режим ввода параметров по умолчанию
          status = "~F9~ Сохранить ~Esc~ Выход";
          caption = "Настройка формата";
      else // режим заполнения параметров импорта
          run_mode = PANELMODE_SELECT;
          caption = "Доп. настройки формата";
          status = "~F2~ Продолжить ~Esc~ Выход";
      end;

      if (not IsInitObj)
          addControl(fld_List);
          addControl(fld_CellAdr);
          IsInitObj = true;
      end;

      // кешируем значения
      cache.List = this.List();
      cache.CellAdr = this.CellAdr();

      _extObj.run();
      if (key == -K_ESC)
          Restore();
      end;

      return key;
  END;

  MACRO eventHandler(EV)
    var
        isSave = IND_NO;

      key = -EV.keyCode;

      if ((run_mode == PANELMODE_SELECT)and(EV.keyCode == ULK_F2))
          hide();
      elif ((run_mode == PANELMODE_SELECT)and(EV.keyCode == ULK_ESC))
          EV.keyCode = 0;
          hide();
      elif ((run_mode == PANELMODE_EDIT)and(EV.keyCode == ULK_F9))
          if (this.Save())
              close(-EV.keyCode);
          end;
      elif ((run_mode == PANELMODE_EDIT)and(EV.keyCode == ULK_ESC))
          if (this.CheckChanges())
              isSave = MsgBoxEx("Сохранить изменения?", MB_YES + MB_NO + MB_CANCEL, IND_YES);
              if (isSave == IND_YES)
                  EV.keyCode = ULK_F9;
                  this.eventHandler(EV);
              elif (isSave == IND_NO)
                  close(-EV.keyCode);
              else
                  EV.keyCode = 0;
              end;
          end;
      end;
  END;

  // конструктор
  InitTRsbPanel();

  setPosition(50, 15);
  setSize(41, 6);

  addLabel(TRsbLabel(2, 2, "Лист:"));
  fld_List            = TRsbEditField();
  fld_List.name       = "fld_List";
  fld_List.dataType   = 7; //FT_STRING
  fld_List.textLength = 34;
  fld_List.setPosition(7, 2);
  fld_List.setSize (32, 1);

  addLabel(TRsbLabel(2, 4, "Адрес первой ячейки с данными:"));
  fld_CellAdr            = TRsbEditField();
  fld_CellAdr.name       = "fld_CellAdr";
  fld_CellAdr.dataType   = 7; //FT_STRING
  fld_CellAdr.textLength = 34;
  fld_CellAdr.setPosition(24, 4);
  fld_CellAdr.setSize (15, 1);

  addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));

  sql = RsdCommand(
                   "select * from ddl_ul_format_dbt where t_ID = ?"
                   );
  sql.addParam("", RSDBP_IN, _formatID);
  sql.execute();
  structFormat = TRsbDataSet(sql);
  structFormat.MoveNext();

  InitDefValue();
END;
