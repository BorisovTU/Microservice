/*
$Name: ul_dataKind_i.mac
$Module: Универсальный загрузчик
$Description: Справочник "Виды данных"
*/

IMPORT RsbDataSet, RSD, RsbFormsInter, "ul_const.mac";

PRIVATE CLASS DataKind_PanelCacheValue()
  VAR
      idField         = 0,
      nameField       = "",
      codeField       = "",
      macroField      = "",
      objClassField   = "";
END;

PRIVATE CLASS (TRsbPanel)DataKind_Panel(_rs, _x, _y)
  PRIVATE VAR
      rs = _rs,
      x  = _x,
      y  = _y,
      cache = DataKind_PanelCacheValue();

  VAR
      fld_idField       = NULL,
      fld_nameField     = NULL,
      fld_macroField    = NULL,
      fld_objClassField = NULL;

  PRIVATE MACRO GetId()
    var
        maxVal:integer = 0,
        cmd = RSDCommand ("select max(T_ID) as maxid from DDL_UL_DATAKIND_DBT"),
        rsID = NULL;

      cmd.execute();
      rsID = TRsbDataSet(cmd);

      if (rsID.movenext())
          maxVal = rsID.maxid; 
      else
          maxVal = 100;
      end;

      if (ValType(maxVal) == V_UNDEF)
          maxVal = 100;
      end;

      if (maxVal < 100)
          maxVal = 100;
      elif (maxVal >= 100)
          maxVal = maxVal + 1;
      end;

      return maxVal;
  END;

  MACRO SaveChanges()
    var
        dataKind = TBfile("DL_UL_DATAKIND", "W", 0);

      dataKind.item("ID")         = this.fld_idField.Value;
      if (dataKind.item("ID") == 0)
          dataKind.item("ID") = GetId();
      else
          dataKind.getEQ();
      end;
      dataKind.item("NAME")       = trim(this.fld_nameField.Value);
      dataKind.item("MACRO")    = trim(this.fld_macroField.Value);
      dataKind.item("OBJCLASS")   = trim(this.fld_objClassField.Value);

      if (strlen(dataKind.item("MACRO")) == 0)
          msgbox("Не заполнено обязательное поле 'Макрос построения выходной структуры'");
          return false;
      end;

      if (strlen(dataKind.item("OBJCLASS")) == 0)
          msgbox("Не заполнено обязательное поле 'Класс построения выходной структуры'");
          return false;
      end;

      if (strlen(dataKind.item("NAME")) == 0)
          dataKind.item("NAME") = "Вид данных №" + dataKind.rec.ID;
      end;

      if (ValType(rs) == V_UNDEF)
          dataKind.insert();
      else
          dataKind.update();
      end;

      return true;

    OnError(RslErrObj)
        if (ValType(rs) == V_UNDEF)
            msgbox("Ошибка при вставке нового вида данных");
        else
            msgbox("Ошибка при редактировании вида данных");
        end;

        return false;
  END;

  MACRO CheckChanges()
    var
        changed = false;

      if ((changed == false) AND (cache.idField != this.fld_idField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.nameField != this.fld_nameField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.macroField != this.fld_macroField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.objClassField != this.fld_objClassField.Value))
          changed=true;
      end;
 
      return changed;
  END;

  MACRO eventHandler(EV)
      if (EV.keyCode == ULK_F9)
          if ((this.CheckChanges())and(SaveChanges()))
              close(-EV.keyCode);
          end;
      elif (EV.keyCode == ULK_ESC)
          if (this.CheckChanges())
              var isSave = MsgBoxEx("Сохранить изменения?", MB_YES + MB_NO + MB_CANCEL, IND_YES);
              if (isSave == IND_YES)
                  EV.keyCode = ULK_F9;
                  this.eventHandler(EV);
              elif (isSave == IND_NO)
                  close(-EV.keyCode);
              else
                  EV.keyCode = 0;
              end;
          end;
      end;
  END;

  MACRO MakeCache()
      cache.idField         = this.fld_idField.Value;
      cache.nameField       = this.fld_nameField.Value;
      cache.macroField      = this.fld_macroField.Value;
      cache.objClassField   = this.fld_objClassField.Value;
  END;
  
  MACRO InitDefValue()
      if  (ValType(rs) != V_UNDEF)
          fld_idField.Value       = rs.Value("t_id");
          fld_nameField.Value     = rs.Value("t_name");
          fld_macroField.Value    = rs.Value("t_macro");
          fld_objClassField.Value = rs.Value("t_objClass");
      else
          fld_idField.Value       = 0;
          fld_nameField.Value     = "";
          fld_macroField.Value    = "";
          fld_objClassField.Value = "";
      end;

      this.Redraw();
  END;

  InitTRsbPanel();

  caption = "Вид данных";
  setPosition(x, y);
  setSize(55, 12);
  status = "~Esc~ Выход";

  if (ValType(rs) != V_UNDEF)
      caption = caption + ": " + String(rs.Value("t_name"));
  end;

  addLabel(TRsbLabel(3, 2, "Номер вида данных:"));
  fld_idField = TRsbEditField();
  fld_idField.name = "fld_idField";
  fld_idField.datatype = FT_INT;
  fld_idField.textlength = 3;
  fld_idField.setPosition(22, 2);
  fld_idField.setSize(3, 1);
  fld_idField.editable = false;
  addControl(fld_idField);

  addLabel(TRsbLabel(3, 4, "Наименование вида данных:"));
  fld_nameField = TRsbEditField();
  fld_nameField.name = "fld_nameField";
  fld_nameField.datatype = FT_STRING;
  fld_nameField.textlength = 35;
  fld_nameField.setPosition(22, 4);
  fld_nameField.setSize(32, 1);
  addControl(fld_nameField);

  addLabel(TRsbLabel(3, 6, "Макрос построения выходной структуры:"));
  fld_macroField = TRsbEditField();
  fld_macroField.name = "fld_macroField";
  fld_macroField.datatype = FT_STRING;
  fld_macroField.textlength = 80;
  fld_macroField.setPosition(3, 7);
  fld_macroField.setSize(50, 1);
  addControl(fld_macroField);

  addLabel(TRsbLabel(3, 9, "Класс построения выходной структуры:"));
  fld_objClassField = TRsbEditField();
  fld_objClassField.name = "fld_objClassField";
  fld_objClassField.datatype = 7; //FT_STRING
  fld_objClassField.textlength = 35;
  fld_objClassField.setPosition(3, 10);
  fld_objClassField.setSize(50, 1);
  addControl(fld_objClassField);

  addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));

  InitDefValue();
  MakeCache();
END;

MACRO DataKind_Scrol(x, y) : RsdRecordSet
  var 
      rs = NULL, cmd = NULL,
      Columns = TArray();

  macro DeleteDataKind()
    var
        cmd = NULL;

      RsdCommand("delete from ddl_ul_rule_dbt where t_ID_FORMAT in (select t_ID from ddl_ul_format_dbt where t_ID_DATAKIND = " + String(Int(rs.Value("T_ID"))) + ")").execute;
      RsdCommand("delete from ddl_ul_format_dbt where t_ID_DATAKIND = " + String(Int(rs.Value("T_ID")))).execute;
      RsdCommand("delete from ddl_ul_datakind_dbt where t_ID = " + String(Int(rs.Value("T_ID")))).execute;

      return true;

    OnError(RslErrObj)
        AbortTrn();
        return false;
  end;

  macro proc(rs, cmd, id, key)
    var
        retVal  = CM_DEFAULT,
        pan     = NULL,
        rsCheck = NULL, isDel = false;

      if (key == ULK_F9)
          retVal = CM_IGNORE;
          pan = DataKind_Panel(NULL, 30, 10);
          if (abs(pan.run()) == ULK_F9)
              retVal = CM_SELECT;
          end;
      elif (key == ULK_F8)
          retVal = CM_IGNORE;
          if (rs.RecCount > 0)
              if ((Int(rs.Value("T_ID")) >= 100) and 
                  (MsgBoxEx("Вы действительно хотите удалить?", MB_YES + MB_NO, IND_YES) == IND_YES))
                  isDel = true;

                  rsCheck = TRsbDataSet("select count(1) as cnt from ddl_ul_format_dbt where t_ID_DATAKIND = " + String(Int(rs.Value("T_ID"))));
                  rsCheck.MoveNext();
                  if (rsCheck.CNT > 0)
                      isDel = false;
                      if (MsgBoxEx("Вы действительно хотите удалить привязанные форматы?", MB_YES + MB_NO, IND_YES) == IND_YES)
                          isDel = true;
                      end;
                  end;
              end;

              if (isDel)
                  if (ProcessTrn(0, "DeleteDataKind"))
                      retVal = CM_SELECT;
                  else
                      msgbox("Ошибка при удалении вида данных");
                  end;
              end;
          end;
      elif (key == ULK_ENTER)
          retVal = CM_IGNORE;
          if (rs.RecCount > 0)
              pan = DataKind_Panel(rs, 30, 10);
              if (abs(pan.run()) == ULK_F9)
                  retVal = CM_SELECT;
              end;
          end;
      end;

      pan = NULL;
      rsCheck = NULL;

      return retVal;
  end;

  macro AddColumn(ar,ind, fld, head, width)
      ar.value( ind * 6 + 0 ) = fld;
      ar.value( ind * 6 + 1 ) = head;
      ar.value( ind * 6 + 2 ) = width;
      ar.value( ind * 6 + 3 ) = 2;
      ar.value( ind * 6 + 4 ) = NULL;
      ar.value( ind * 6 + 5 ) = 0;
  end;

    AddColumn(Columns, 0, "t_id", "№ п/п", 3);
    AddColumn(Columns, 1, "t_name", "Вид данных", 35);

    cmd = RsdCommand("SELECT * FROM DDL_UL_DATAKIND_DBT ORDER BY T_ID");
    rs = RsdRecordSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);

    while (RunScroll(rs, 2, Columns, NULL, @proc, "Виды данных", "~F8~ Удаление ~F9~ Ввод ~Enter~ Редактирование ~Esc~ Выход ", true, x, y, 50, 80))
        rs.Refresh();
    end;

    return rs;
END;
