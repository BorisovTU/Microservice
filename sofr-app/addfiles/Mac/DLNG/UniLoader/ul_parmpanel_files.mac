/*
$Name: ul_parmpanel_files.mac
$Module: Универсальный загрузчик
$Description: Параметры импорта для файловых источников
*/

IMPORT RsbFormsInter, RsbDataSet, RSD, BankInter,  Rsexts, "ul_const.mac", "UL_ParmPanel_Lib.mac";

PRIVATE CLASS Files_CacheValue()
  var
      InputDir    = "",
      OutputDir   = "",
      ErrDir      = "",
      isSuccess   = false,
      isDoubleObj = false;
END;

CLASS(TRsbPanel) UL_ParmPanel_Files(_groupformatID, _formatID)
  PRIVATE VAR
      sql = NULL;

  PRIVATE VAR
      run_mode = PANELMODE_EDIT,
      log_block = UL_LogControl(),
      dir_block = UL_DirControl(),
      cache = Files_CacheValue(),
      key = 0;

  VAR
      IsInitObj       = false, // признак, что панель уже проинициализирована
      groupformatID   = _groupformatID,
      formatID        = _formatID,
      FormatParmPanel = NULL,
      structGroup     = NULL,
      structFormat    = NULL;

  MACRO InputDir()
      return dir_block.InputDir();
  END;
  MACRO Set_InputDir(_val)
      dir_block.Set_InputDir(_val);
  END;

  MACRO OutputDir()
      return dir_block.OutputDir();
  END;
  MACRO Set_OutputDir(_val)
      dir_block.Set_OutputDir(_val);
  END;

  MACRO ErrDir()
      return dir_block.ErrDir();
  END;
  MACRO Set_ErrDir(_val)
      dir_block.Set_ErrDir(_val);
  END;

  MACRO isSuccess()
      return log_block.isSuccess();
  END;
  MACRO Set_isSuccess(_val)
      log_block.Set_isSuccess(_val);
  END;

  MACRO isDoubleObj()
      return log_block.isDoubleObj();
  END;
  MACRO Set_isDoubleObj(_val)
      log_block.Set_isDoubleObj(_val);
  END;

  MACRO CheckParm(errStr_)
    var
        retVal = true;

      errStr_ = "";

      if ((strlen(InputDir()) == 0)or(not ExistFile(InputDir())))
          errStr_ = "Неверно задан каталог входных данных";
          retVal = false;
      else
          if ((strlen(OutputDir()) > 0)and(not ExistFile(OutputDir())))
              if (GetDialogFlag())
                  if(GetTRUE (NULL, "Каталог обработанных данных не существует. | Хотите создать его?"))
                      if(not MakeDir(OutputDir()))
                          errStr_ = "Ошибка создания каталога:|" + OutputDir();
                          retVal = false;
                      end;
                  end;
              else
                  errStr_ = "Каталог обработанных данных не существует";
                  retVal = false;
              end;
          end;

          if ((retVal)and(strlen(ErrDir()) > 0)and(not ExistFile(ErrDir())))
              if (GetDialogFlag())
                  if(GetTRUE (NULL, "Каталог ошибочных данных не существует. | Хотите создать его?"))
                      if(not MakeDir(ErrDir()))
                         errStr_ = "Ошибка создания каталога:|" + ErrDir();
                         retVal = false;
                      end;
                  end;
              else
                  errStr_ = "Каталог ошибочных данных не существует";
                  retVal = false;
              end;
          end;
      end;

      SetParm(1, errStr_);

      return retVal;
  END;

  PRIVATE MACRO GetRegistry(_regKey, _type, _default)
    var
        retVal = _default,
        stat = 0,
        full_regPath = "";

      if (formatID > 0)
          full_regPath = "COMMON\\UNILOADER\\GROUPS\\" + structGroup.Code + "\\FORMATS\\" + structFormat.Code + "\\" + _regKey;
      elif (groupformatID > 0)
          full_regPath = "COMMON\\UNILOADER\\GROUPS\\" + structGroup.Code + "\\" + _regKey;
      end;

      if (strlen(full_regPath) > 0)
          GetRegistryValue(full_regPath, _type, retVal, stat);
      else
          stat = 1;
      end;

      if (stat)
          retVal = _default;
      end;

      return retVal;
  END;

  PRIVATE MACRO SetRegistry(_regKey, _val)
    var
        full_regPath = "";

      if (formatID > 0)
          full_regPath = "COMMON\\UNILOADER\\GROUPS\\" + structGroup.Code + "\\FORMATS\\" + structFormat.Code + "\\" + _regKey;
      elif (groupformatID > 0)
          full_regPath = "COMMON\\UNILOADER\\GROUPS\\" + structGroup.Code + "\\" + _regKey;
      end;

      if (strlen(full_regPath) > 0)
          SetDefaultRegistryValue (full_regPath, _val);
      end;
  END;

  MACRO InitDefValue()
      this.Set_InputDir(GetRegistry("INPUTDIR",       V_STRING, ""));
      this.Set_OutputDir(GetRegistry("OUTPUTDIR",     V_STRING, ""));
      this.Set_ErrDir(GetRegistry("ERRDIR",           V_STRING, ""));
      this.Set_isSuccess(GetRegistry("ISSUCCESS",     V_BOOL,   false));
      this.Set_isDoubleObj(GetRegistry("ISDOUBLEOBJ", V_BOOL,   false));

      this.Redraw();
  END;

  MACRO CheckChanges()
    var
        retVal = false;

      if (cache.InputDir != InputDir())
          retVal = true;
      end;
      if (cache.OutputDir != OutputDir())
          retVal = true;
      end;
      if (cache.ErrDir != ErrDir())
          retVal = true;
      end;
      if (cache.isSuccess != isSuccess())
          retVal = true;
      end;
      if (cache.isDoubleObj != isDoubleObj())
          retVal = true;
      end;
      if ((ValType(FormatParmPanel) != V_UNDEF)and(FormatParmPanel.CheckChanges()))
          retVal = true;
      end;

      return retVal;
  END;

  MACRO Save()
      if (cache.InputDir != InputDir())
          SetRegistry("INPUTDIR", InputDir());
      end;
      if (cache.OutputDir != OutputDir())
          SetRegistry("OUTPUTDIR", OutputDir());
      end;
      if (cache.ErrDir != ErrDir())
          SetRegistry("ERRDIR", ErrDir());
      end;
      if (cache.isSuccess != isSuccess())
          SetRegistry("ISSUCCESS", isSuccess());
      end;
      if (cache.isDoubleObj != isDoubleObj())
          SetRegistry("ISDOUBLEOBJ", isDoubleObj());
      end;
      if (ValType(FormatParmPanel) != V_UNDEF)
          FormatParmPanel.Save();
      end;

      return true;
  END;

  PRIVATE MACRO Restore()
      Set_InputDir(cache.InputDir);
      Set_OutputDir(cache.OutputDir);
      Set_ErrDir(cache.ErrDir);
      Set_isSuccess(cache.isSuccess);
      Set_isDoubleObj(cache.isDoubleObj);
      if (ValType(FormatParmPanel) != V_UNDEF)
          FormatParmPanel.Restore();
      end;
  END;

  MACRO run(_mode)
      run_mode = _mode;

      if (run_mode == PANELMODE_EDIT) // режим ввода параметров по умолчанию
          if (formatID > 0)
              status = "~F9~ Сохранить ~F3~ Выбор ~SPACE~ Установка/снятие признака ~ALT-D~ Подстановка из группы ~Esc~ Выход";
          else
              status = "~F9~ Сохранить ~F3~ Выбор ~SPACE~ Установка/снятие признака ~Esc~ Выход";
          end;
      else // режим заполнения параметров импорта
          run_mode = PANELMODE_SELECT;
          if (ValType(FormatParmPanel) != V_UNDEF)
              status = "~F2~ Продолжить ~F3~ Выбор ~SPACE~ Установка/снятие признака ~ALT-D~ Подстановка из группы ~F5~ Доп. параметры ~Esc~ Выход";
          else
              status = "~F2~ Продолжить ~F3~ Выбор ~SPACE~ Установка/снятие признака ~ALT-D~ Подстановка из группы ~Esc~ Выход";
          end;
      end;

      if (not IsInitObj)
          dir_block.X = 2;
          dir_block.Y = 0;
          dir_block.Length = 35;
          dir_block.AddDir(this);

          log_block.X = 2;
          log_block.Y = 5;
          log_block.AddLog(this);

          IsInitObj = true;
      end;

      // кешируем значения
      cache.InputDir    = InputDir();
      cache.OutputDir   = OutputDir();
      cache.ErrDir      = ErrDir();
      cache.isSuccess   = isSuccess();
      cache.isDoubleObj = isDoubleObj();

      _extObj.run();
      if (key == -ULK_ESC)
          Restore();
      end;

      return key;
  END;

  MACRO eventHandler(EV)
    var
        isSave = IND_NO, fID = formatID, errStr = "";

      key = -EV.keyCode;

      if ((run_mode == PANELMODE_SELECT)and(EV.keyCode == ULK_F2))
          if (this.CheckParm(errStr))
              close(-EV.keyCode);
          else
              msgbox(errStr);
              EV.keyCode = 0;
          end;
      elif ((run_mode == PANELMODE_SELECT)and(EV.keyCode == ULK_ESC))
          close(-EV.keyCode);
      elif ((run_mode == PANELMODE_EDIT)and(EV.keyCode == ULK_F9))
          if (this.Save())
              close(-EV.keyCode);
          end;
      elif ((run_mode == PANELMODE_EDIT)and(EV.keyCode == ULK_ESC))
          if (this.CheckChanges())
              isSave = MsgBoxEx("Сохранить изменения?", MB_YES + MB_NO + MB_CANCEL, IND_YES);
              if (isSave == IND_YES)
                  EV.keyCode = ULK_F9;
                  this.eventHandler(EV);
              elif (isSave == IND_NO)
                  close(-EV.keyCode);
              else
                  EV.keyCode = 0;
              end;
          end;
      elif (
            (EV.keyCode == ULK_F5)and
            (run_mode == PANELMODE_SELECT) and
            (ValType(FormatParmPanel) != V_UNDEF)
           )
          FormatParmPanel.run(PANELMODE_SELECT);
      elif ( 
            (EV.keyCode == ULK_ALTD)and
            (((run_mode == PANELMODE_EDIT)and(formatID > 0))or(run_mode == PANELMODE_SELECT))
           )
          formatID = 0;
          InitDefValue();
          formatID = fID;
      end;
  END;

  // конструктор
  InitTRsbPanel();

  caption = "Настройка формата";
  setPosition(50, 15);
  setSize(50, 7);

  addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));

  if ((ValType(formatID) == V_INTEGER)and(formatID > 0))
      sql = RsdCommand(
                       "select * from ddl_ul_format_dbt where t_ID = ?"
                       );
      sql.addParam("", RSDBP_IN, _formatID);
      sql.execute();
      structFormat = TRsbDataSet(sql);
      if (structFormat.MoveNext())
          groupformatID = structFormat.ID_GroupFormat;
      end;
  end;

  if (groupformatID > 0)
      sql = RsdCommand(
                       "select * from ddl_ul_groupformat_dbt where t_ID = ?"
                       );
      sql.addParam("", RSDBP_IN, groupformatID);
      sql.execute();
      structGroup = TRsbDataSet(sql);
      structGroup.MoveNext();
  end;

  InitDefValue();
END;
