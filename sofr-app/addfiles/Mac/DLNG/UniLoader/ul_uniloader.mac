/*
$Name: ul_uniloader.mac
$Module: Универсальный загрузчик
$Description: Инициализация механизма универсального загрузчика и запуск импорта
*/

IMPORT RsbDataSet, RSD, "UL_DataLoader.mac", "UL_Log.mac";

CLASS UL_UniLoader(_formatID)
  PRIVATE VAR
      Format = _formatID,
      LoaderLog = UL_Log();

  MACRO GetErrorsLog()
      return LoaderLog;
  END;

  MACRO LoadData(_groupConfig, _formatConfig, _manipulation_func)
    var
        retVal = 0,
        sql = NULL, ds = NULL,
        Loader = NULL;

      LoaderLog.ClearLog();

      sql = RsdCommand(
                       "select gf.* " +
                       " from ddl_ul_groupformat_dbt gf, ddl_ul_format_dbt f where " +
                       "f.t_ID = ? and gf.t_ID = f.t_ID_GroupFormat"
                       );
      sql.addParam("", RSDBP_IN, Format);
      sql.execute();

      ds = TRsbDataSet(sql);
      if (ds.MoveNext())
          Loader = UL_DataLoader(Format);
          Loader.groupConfig = _groupConfig;
          Loader.formatConfig = _formatConfig;
          Loader.Func = _manipulation_func;
          Loader.Log = LoaderLog;

          if (
              (strlen(ds.DataPreloadMacro) > 0)and
              (strlen(ds.DataPreloadFunc) > 0)
             )
              InstLoadModule(String(ds.DataPreloadMacro));
              retVal = ExecMacro2(String(ds.DataPreloadFunc), Loader)
          else
              LoaderLog.ErrorLog("Не определена макрофункция предобработки");
              retVal = 1;
          end;
      else
          LoaderLog.ErrorLog("Не найдена группа форматов");
          retVal = 1;
      end;

      return retVal;
  END;
END;