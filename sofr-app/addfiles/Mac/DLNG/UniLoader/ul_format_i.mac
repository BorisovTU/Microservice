/*
$Name:        ul_format_i.mac
$Module:      Универсальный загрузчик
$Description: Справочник "Форматы"
*/

IMPORT RsbDataSet, RSD, RsbFormsInter, "ul_rule_i.mac", "ul_const.mac";

PRIVATE MACRO GetRSGroup(_FormatID)
  var rsG = NULL;

    rsG = RsdRecordSet("SELECT gf.* FROM DDL_UL_GROUPFORMAT_DBT gf, DDL_UL_FORMAT_DBT f WHERE gf.T_ID = f.T_ID_GROUPFORMAT AND f.T_ID = " + String(_FormatID));
    rsG.MoveNext();

    return rsG;
END;

PRIVATE CLASS Format_PanelCacheValue()
  VAR
      idField          = 0,
      dataKindField    = "",
      groupFormatField = "",
      nameField        = "",
      codeField        = "",
      macroPanField    = "",
      classPanelField  = "",
      parseMacroField  = "",
      parseFuncField   = "",
      isUsingField     = false,
      isUpdateField    = false;
END;

PRIVATE CLASS (TRsbPanel)Format_Panel(_rs, _x,_y)   
  PRIVATE VAR
      rs = _rs,
      rsGroup = NULL,
      x = _x,
      y = _y,
      cache = Format_PanelCacheValue();

  VAR
      fld_idField          = NULL,
      fld_dataKindField    = NULL,
      fld_groupFormatField = NULL,
      fld_nameField        = NULL,
      fld_codeField        = NULL,
      fld_macroPanField    = NULL,
      fld_classPanelField  = NULL,
      fld_parseMacroField  = NULL,
      fld_parseFuncField   = NULL,
      fld_isUsingField     = NULL,
      fld_isUpdateField    = NULL;

  PRIVATE MACRO GetId()
    var
        maxVal:integer = 0,
        cmd = RSDCommand ("select max(T_ID) as maxid from DDL_UL_FORMAT_DBT"),
        rsID = NULL;

      cmd.execute();
      rsID = TRsbDataSet(cmd);

      if (rsID.movenext())
          maxVal = rsID.maxid;
      else
          maxVal = 100;
      end;

      if (ValType(maxVal) == V_UNDEF)
          maxVal = 100;
      end;

      if (maxVal < 100)
          maxVal = 100;
      elif (maxVal >= 100)
          maxVal = maxVal + 1;
      end;

      return maxVal;
  END;

  MACRO SaveChanges()
    var
        Format = TBfile("DL_UL_FORMAT", "W", 0);

      Format.rec.ID               = this.fld_idField.Value;
      if (Format.rec.ID == 0)
          Format.rec.ID = GetId();
      else
          Format.getEQ();
      end;
      Format.rec.ID_DATAKIND      = this.fld_dataKindField.currentChoice;
      Format.rec.ID_GROUPFORMAT   = this.fld_groupFormatField.currentChoice;
      Format.rec.NAME             = trim(this.fld_nameField.Value);
      Format.rec.CODE             = trim(this.fld_codeField.Value);
      Format.rec.PANELPARMMACRO   = trim(this.fld_macroPanField.Value);
      Format.rec.PANELPARMCLASS   = trim(this.fld_classPanelField.Value);
      Format.rec.PARSEMACRO       = trim(this.fld_parseMacroField.Value);
      Format.rec.PARSEFUNC        = trim(this.fld_parseFuncField.Value);
      Format.rec.ISUSE            = StrFor(0);
      Format.rec.ISUPDATE         = StrFor(0);

      if (fld_isUsingField.checked)
          Format.rec.ISUSE = "X";
      end;
      if (fld_isUpdateField.checked)
          Format.rec.ISUPDATE = "X";
      end;

      if (strlen(Format.rec.CODE) == 0)
          msgbox("Не заполнено обязательное поле 'Код группы'");
          return false;
      end;

      if (Format.rec.ID_DATAKIND == 0)
          msgbox("Не заполнено обязательное поле 'Вид данных'");
          return false;
      end;

      if (Format.rec.ID_GROUPFORMAT == 0)
          msgbox("Не заполнено обязательное поле 'Группа форматов'");
          return false;
      end;

      if (strlen(Format.rec.PARSEMACRO) == 0)
          msgbox("Не заполнено обязательное поле 'Подготовка к конвертации данных: Макрос'");
          return false;
      end;

      if (strlen(Format.rec.PARSEFUNC) == 0)
          msgbox("Не заполнено обязательное поле 'Подготовка к конвертации данных: Функция'");
          return false;
      end;

      if (strlen(Format.rec.NAME) == 0)
          Format.rec.NAME = "Формат №" + Format.rec.ID;
      end;

      if (ValType(rs) == V_UNDEF)
          Format.insert();
      else
          Format.update();
      end;

     return true;

    OnError(RslErrObj)
        if (ValType(rs) == V_UNDEF)
            msgbox("Ошибка при вставке нового формата");
        else
            msgbox("Ошибка при редактировании формата");
        end;

        return false;
  END;

  MACRO CheckChanges()
    var
        changed = false;

      if ((changed == false) AND (cache.idField != this.fld_idField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.dataKindField != this.fld_dataKindField.currentChoice))
          changed=true;
      end;
      if ((changed == false) AND (cache.groupFormatField != this.fld_groupFormatField.currentChoice))
          changed=true;
      end;
      if ((changed == false) AND (cache.nameField != this.fld_nameField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.codeField != this.fld_codeField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.macroPanField != this.fld_macroPanField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.classPanelField != this.fld_classPanelField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.parseMacroField != this.fld_parseMacroField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.parseFuncField != this.fld_parseFuncField.Value))
          changed=true;
      end;
      if ((changed == false) AND (cache.isUsingField != this.fld_isUsingField.checked))
          changed=true;
      end;
      if ((changed == false) AND (cache.isUpdateField != this.fld_isUpdateField.checked))
          changed=true;
      end;
 
      return changed;
  END;

  MACRO eventHandler(EV)
    var
        rsG = rsGroup;

      if (EV.keyCode == ULK_F9)
          if ((this.CheckChanges())and(SaveChanges()))
              close(-EV.keyCode);
          end;
      elif (EV.keyCode == ULK_ESC)
           if (this.CheckChanges())
              var isSave = MsgBoxEx("Сохранить изменения?", MB_YES + MB_NO + MB_CANCEL, IND_YES);
              if (isSave == IND_YES)
                  EV.keyCode = ULK_F9;
                  this.eventHandler(EV);
              elif (isSave == IND_NO)
                  close(-EV.keyCode);
              else
                  EV.keyCode = 0;
              end;
          end;
      elif (EV.keyCode == ULK_ALTG)
          if (ValType(rs) != V_UNDEF)
              if (ValType(rsG) == V_UNDEF)
                  rsG = GetRSGroup(Int(rs.Value("T_ID")))
              end;

              if ((strlen(rsG.Value("T_PANELPARMMACRO")) > 0)and
                  (strlen(rsG.Value("T_PANELPARMCLASS")) > 0))
                  InstLoadModule(String(rsG.Value("T_PANELPARMMACRO")));
                  var groupConfigPanel = GenObject(String(rsG.Value("T_PANELPARMCLASS")), Int(rsG.Value("T_ID")), Int(rs.Value("T_ID")));
                  groupConfigPanel.run(PANELMODE_EDIT);
                  groupConfigPanel = NULL;
              end;
          end;
      elif (EV.keyCode == ULK_ALTT)
          if (ValType(rs) != V_UNDEF)
              Rule_Scrol(1, 1, Int(rs.Value("T_ID")));
          end;
      end;
  END;

  MACRO run(_rsGroup)
    var
        key = 0;

      rsGroup = _rsGroup;

      if (ValType(_rsGroup) != V_UNDEF)
          fld_groupFormatField.enabled = false;
          fld_groupFormatField.currentChoice = Int(rsGroup.Value("T_ID"));
      else
          fld_groupFormatField.enabled = true;
      end;

      key = _extObj.run();
      _rsGroup = NULL;

      return key;
  END;

  MACRO MakeCache()
      cache.idField          = this.fld_idField.Value;
      cache.dataKindField    = this.fld_dataKindField.CurrentChoice;
      cache.groupFormatField = this.fld_groupFormatField.CurrentChoice;
      cache.nameField        = this.fld_nameField.Value;
      cache.codeField        = this.fld_codeField.Value;
      cache.macroPanField    = this.fld_macroPanField.Value;
      cache.classPanelField  = this.fld_classPanelField.Value;
      cache.parseMacroField  = this.fld_parseMacroField.Value;
      cache.parseFuncField   = this.fld_parseFuncField.Value;
      cache.isUsingField     = this.fld_isUsingField.checked;
      cache.isUpdateField    = this.fld_isUpdateField.checked; 
  END;

  MACRO InitDefValue()
    var
        ds = NULL, idx = 0,
        sqlDataKind = RsdCommand("select * from ddl_ul_datakind_dbt"),
        sqlGroup = RsdCommand("select * from ddl_ul_groupformat_dbt where t_IsUse <> 'X'");

      idx = 0;
      sqlDataKind.execute();
      ds = TRsbDataSet(sqlDataKind);
      while (ds.MoveNext())
          fld_dataKindField.addChoice(Int(ds.ID), String(ds.Name));
          idx = idx + 1;
          if (idx == 1)
              fld_dataKindField.currentChoice = Int(ds.ID);
          end;
      end;

      idx = 0;
      sqlGroup.execute();
      ds = TRsbDataSet(sqlGroup);
      while (ds.MoveNext())
          fld_groupFormatField.addChoice(Int(ds.ID), String(ds.Name));
          idx = idx + 1;
          if (idx == 1)
              fld_groupFormatField.currentChoice = Int(ds.ID);
          end;
      end;

      if  (ValType(rs) != V_UNDEF)
          fld_idField.Value                  = Int(rs.Value("T_ID"));
          fld_dataKindField.currentChoice    = rs.Value("T_ID_DATAKIND");
          fld_groupFormatField.currentChoice = rs.Value("T_ID_GROUPFORMAT");
          fld_nameField.Value                = rs.Value("T_NAME");
          fld_codeField.Value                = rs.Value("T_CODE");
          fld_macroPanField.Value            = rs.Value("T_PANELPARMMACRO");
          fld_classPanelField.Value          = rs.Value("T_PANELPARMCLASS");
          fld_parseMacroField.Value          = rs.Value("T_PARSEMACRO");
          fld_parseFuncField.Value            = rs.Value("T_PARSEFUNC");
          fld_isUsingField.Checked           = false;
          fld_isUpdateField.Checked          = false;

          if (rs.Value("T_ISUSE") == "X")
              fld_isUsingField.Checked = true;
          end;
          if (rs.Value("T_ISUPDATE") == "X")
              fld_isUpdateField.Checked = true;
          end;
      else
          fld_idField.Value         = 0;
          fld_nameField.Value       = "";
          fld_codeField.Value       = "";
          fld_macroPanField.Value   = "";
          fld_classPanelField.Value = "";
          fld_parseMacroField.Value  = "";
          fld_parseFuncField.Value   = "";
          fld_isUsingField.Checked = false;
          fld_isUpdateField.Checked = false;
      end;

      this.Redraw();
  END;
  InitTRsbPanel();

  caption = "Настройка формата";
  setPosition(x, y);
  setSize(55, 20);
  status = "~F9~ Сохранить ~Alt-G~ Настройки группы ~Alt-T~ Таблица соответствия ~Esc~ Выход";

  addLabel(TRsbLabel(3, 2, "Номер формата:"));
  fld_idField = TRsbEditField();
  fld_idField.name = "fld_idField";
  fld_idField.datatype = FT_INT;
  fld_idField.setPosition(13, 2);
  fld_idField.setSize(3, 1);
  fld_idField.editable = false;
  addControl(fld_idField);

  addLabel(TRsbLabel(33, 2, "Код формата:"));
  fld_codeField = TRsbEditField();
  fld_codeField.name = "fld_codeField";
  fld_codeField.datatype = FT_STRING; 
  fld_codeField.textlength = 10;
  fld_codeField.setPosition(42, 2);
  fld_codeField.setSize(10, 1);
  addControl(fld_codeField);

  addLabel(TRsbLabel(3, 4, "Вид данных:"));
  fld_dataKindField = TRsbComboBox();
  fld_dataKindField.name = "fld_dataKindField";
  fld_dataKindField.dataType = 7; //FT_STRING
  fld_dataKindField.textLength = 35;
  fld_dataKindField.setPosition(15, 4);
  fld_dataKindField.setSize(37, 1);
  addControl(fld_dataKindField);

  addLabel( TRsbLabel(3, 6, "Группа форматов: ") );
  fld_groupFormatField = TRsbComboBox();
  fld_groupFormatField.name = "fld_groupFormatField";
  fld_groupFormatField.dataType = 7; //FT_STRING
  fld_groupFormatField.textLength = 35;
  fld_groupFormatField.setPosition(15, 6);
  fld_groupFormatField.setSize(37, 1);
  addControl(fld_groupFormatField);

  addLabel(TRsbLabel(3, 8, "Формат:"));
  fld_nameField = TRsbEditField();
  fld_nameField.name = "fld_nameField";
  fld_nameField.datatype = FT_STRING; 
  fld_nameField.textlength = 255;
  fld_nameField.setPosition(10, 8);
  fld_nameField.setSize(42, 1);
  addControl(fld_nameField);                  

  addLabel(TRsbLabel(3, 10, "Панель дополнительных параметры импорта формата группы:"));

  addLabel(TRsbLabel(4, 11, "Макрос:"));
  fld_macroPanField = TRsbEditField();
  fld_macroPanField.name = "fld_macroPanField";
  fld_macroPanField.datatype = FT_STRING; 
  fld_macroPanField.textlength = 50;
  fld_macroPanField.setPosition(11, 11);
  fld_macroPanField.setSize(41, 1);
  addControl(fld_macroPanField);

  addLabel(TRsbLabel(4, 12, "Класс:"));
  fld_classPanelField = TRsbEditField();
  fld_classPanelField.name = "fld_classPanelField";
  fld_classPanelField.datatype = FT_STRING;
  fld_classPanelField.textlength = 30;
  fld_classPanelField.setPosition(11, 12);
  fld_classPanelField.setSize(41, 1);
  addControl(fld_classPanelField);

  addLabel(TRsbLabel(3, 14, "Подготовка к конвертации данных:"));

  addLabel(TRsbLabel(4, 15, "Макрос:"));
  fld_parseMacroField = TRsbEditField();
  fld_parseMacroField.name = "fld_parseMacroField";
  fld_parseMacroField.datatype = FT_STRING; 
  fld_parseMacroField.textlength = 50;
  fld_parseMacroField.setPosition(11, 15);
  fld_parseMacroField.setSize(41, 1);
  addControl(fld_parseMacroField);

  addLabel(TRsbLabel(4, 16, "Функция:"));
  fld_parseFuncField = TRsbEditField();
  fld_parseFuncField.name = "fld_parseFuncField";            
  fld_parseFuncField.datatype = FT_STRING; 
  fld_parseFuncField.textlength = 30;
  fld_parseFuncField.setPosition(11, 16);
  fld_parseFuncField.setSize(41, 1);
  addControl(fld_parseFuncField);

  addLabel(TRsbLabel(5, 18, "Не используется"));
  fld_isUsingField = TRsbCheckBox();
  fld_isUsingField.name = "fld_isUsingField";          
  fld_isUsingField.datatype = FT_CHR; 
  fld_isUsingField.setPosition(3, 18);
  addControl(fld_isUsingField);

  addLabel(TRsbLabel(5, 19, "Обновлять при переходе на следующую сборку"));
  fld_isUpdateField = TRsbCheckBox();
  fld_isUpdateField.name = "fld_isUpdateField";            
  fld_isUpdateField.datatype = FT_CHR; 
  fld_isUpdateField.setPosition(3, 19);
  addControl(fld_isUpdateField);
  if  ((ValType(rs) == V_UNDEF)or(Int(rs.Value("T_ID")) >= 100))
      fld_isUpdateField.enabled = false;
  end;

  addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));

  InitDefValue();
  MakeCache();
END;

MACRO Format_Scrol(x, y, GroupFormatID) : RsdRecordSet
  var
      qStr = "", rs = NULL, rsGroup = NULL,
      cmd = NULL, Columns = TArray(), scrolHeader = "";

  macro DeleteFormat()
    var
        cmd = NULL;

      RsdCommand("delete from ddl_ul_rule_dbt where t_ID_FORMAT = " + String(Int(rs.Value("T_ID")))).execute;
      RsdCommand("delete from ddl_ul_format_dbt where t_ID = " + String(Int(rs.Value("T_ID")))).execute;

      return true;

    OnError(RslErrObj)
        AbortTrn();
        return false;
  end;

  macro proc(rs, cmd, id, key)
    var
        retVal  = CM_DEFAULT,
        pan     = NULL,
        isDel = false,
        rsG = rsGroup; 

      if (key == ULK_F9)
          retVal = CM_IGNORE;
                pan = Format_Panel(NULL, 30, 10, rsGroup);
              if (abs(pan.run()) == ULK_F9)
                  retVal = CM_SELECT;
              end;
      elif (key == ULK_F8)
          retVal = CM_IGNORE;
          if (rs.RecCount > 0)
              if ((Int(rs.Value("T_ID")) >= 100) and 
                  (MsgBoxEx("Вы действительно хотите удалить?", MB_YES + MB_NO, IND_YES) == IND_YES))
                  isDel = true;
              end;

              if (isDel)
                  if (ProcessTrn(0, "DeleteFormat"))
                      retVal = CM_SELECT;
                  else
                      msgbox("Ошибка при удалении формата");
                  end;
              end;
          end;
      elif (key == ULK_ENTER)
          retVal = CM_IGNORE;
          if (rs.RecCount > 0)
              pan = Format_Panel(rs, 30, 10);
              if (abs(pan.run()) == ULK_F9)
                  retVal = CM_SELECT;
              end;
          end;
      elif (key == ULK_ALTG)
          if (rs.RecCount > 0)
              if (ValType(rsG) == V_UNDEF)
                  rsG = GetRSGroup(Int(rs.Value("T_ID")));
              end;
              if ((strlen(rsG.Value("T_PANELPARMMACRO")) > 0)and
                  (strlen(rsG.Value("T_PANELPARMCLASS")) > 0) and 
                  (rsG.Value("T_PANELPARMMACRO")!=StrFor(1)) and
                   (rsG.Value("T_PANELPARMCLASS")!=StrFor(1)) )  
                  InstLoadModule(String(rsG.Value("T_PANELPARMMACRO")));
                  var groupConfigPanel = GenObject(String(rsG.Value("T_PANELPARMCLASS")), Int(rsG.Value("T_ID")), Int(rs.Value("T_ID")));
                  groupConfigPanel.run(PANELMODE_EDIT);
                  groupConfigPanel = NULL;
              end;
          end;
      elif (key == ULK_ALTF)
          if (rs.RecCount > 0)
                if ((strlen(rs.Value("T_PANELPARMMACRO")) > 0)and
                  (strlen(rs.Value("T_PANELPARMCLASS")) > 0))
                  InstLoadModule(String(rs.Value("T_PANELPARMMACRO")));
                  var formatConfigPanel = GenObject(String(rs.Value("T_PANELPARMCLASS")), Int(rs.Value("T_ID")));
                  formatConfigPanel.run(PANELMODE_EDIT);
                  formatConfigPanel = NULL;
              end;
          end;
      elif (key == ULK_ALTT)
          if (rs.RecCount > 0)
              Rule_Scrol(1, 1, Int(rs.Value("T_ID")));
          end;
      end;

      return retVal;
  end;

  macro AddColumn(ar,ind, fld, head, width)
      ar.value( ind * 6 + 0 ) = fld;
      ar.value( ind * 6 + 1 ) = head;
      ar.value( ind * 6 + 2 ) = width;
      ar.value( ind * 6 + 3 ) = 2;
      ar.value( ind * 6 + 4 ) = NULL;
      ar.value( ind * 6 + 5 ) = 0;
  end;

    if ((ValType(GroupFormatID) == V_INTEGER)and(GroupFormatID > 0))
        rsGroup = RsdRecordSet("SELECT * FROM DDL_UL_GROUPFORMAT_DBT WHERE T_ID = " + String(GroupFormatID));
        if (not rsGroup.MoveNext()) return NULL; end;
        qStr = "SELECT * FROM DDL_UL_FORMAT_DBT WHERE T_ID_GROUPFORMAT = " + String(GroupFormatID) + " ORDER BY T_ID";
        scrolHeader = "Формат группы \"" + rsGroup.value("t_name") + "\"";
    else
        qStr = "SELECT * FROM DDL_UL_FORMAT_DBT ORDER BY T_ID";
        scrolHeader = "Формат группы";
    end;

    cmd = RsdCommand(qStr);
    rs = RsdRecordSet(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);

    AddColumn(Columns, 0, "t_id", "№ п/п", 5);
    AddColumn(Columns, 1, "t_name", scrolHeader, 35);
    AddColumn(Columns, 2, "t_isuse", "И", 1);
    AddColumn(Columns, 3, "t_isupdate", "О", 1);

    while (RunScroll(rs, 4, Columns, NULL, @proc, "Форматы группы", "~F9~ Ввод ~Enter~ Редактирование ~Alt-G~ Настройки группы ~Alt-F~ Настройки формата ~Alt-T~ Таблица соответствия ~Esc~ Выход ", true, x, y, 50, 80))
        rs.Refresh();
    end;

    return rs;
END;
