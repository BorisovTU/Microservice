/*
$Name: ul_formatparm_delim.mac
$Module: Универсальный загрузчик
$Description: Панель параметров текстовых форматов через разделитель
*/

IMPORT RsbFormsInter, BankInter, RsbDataSet, RSD, "ul_const.mac";

PRIVATE CLASS Delim_CacheValue()
  var
      Delim = 0;
END;

CLASS(TRsbPanel) UL_FormatParm_Delim(_formatID)
  PRIVATE VAR
      sql = NULL;

  PRIVATE VAR
      run_mode   = PANELMODE_EDIT,
      fld_Delim  = NULL,  // поле "Разделитель",
      cache = Delim_CacheValue(),
      key = 0;

  VAR
      IsInitObj  = false, // признак, что панель уже проинициализирована
      formatID   = _formatID,
      structFormat    = NULL;

  MACRO Delim()
      return fld_Delim.currentChoice;
  END;
  MACRO Set_Delim(_val)
      fld_Delim.currentChoice = _val;
  END;

  MACRO CheckParm(errStr_)
    var
        retVal = true;

      errStr_ = "";

      if (this.Delim() == 0)
          errStr_ = "Не задан разделитель";
          retVal = false;
      end;

      SetParm(1, errStr_);

      return retVal;
  END;

  PRIVATE MACRO GetRegistry(_regKey, _type, _default)
    var
        retVal = _default,
        stat = 0,
        full_regPath = "";

      full_regPath = "COMMON\\UNILOADER\\FORMATS\\" + structFormat.Code + "\\" + _regKey;

      if (strlen(full_regPath) > 0)
          GetRegistryValue(full_regPath, _type, retVal, stat);
      else
          stat = 1;
      end;

      if (stat)
          retVal = _default;
      end;

      return retVal;
  END;

  PRIVATE MACRO SetRegistry(_regKey, _val)
    var
        full_regPath = "";

      full_regPath = "COMMON\\UNILOADER\\FORMATS\\" + structFormat.Code + "\\" + _regKey;
      if (strlen(full_regPath) > 0)
          SetDefaultRegistryValue (full_regPath, _val);
      end;
  END;

  MACRO InitDefValue()
      this.Set_Delim(GetRegistry("DELIM", V_INTEGER, 1));
  END;

  MACRO CheckChanges()
    var
        retVal = false;

      if (this.Delim() != cache.Delim)
          retVal = true;
      end;

      return retVal;
  END;

  MACRO Save()
      if (cache.Delim != Delim())
          SetRegistry("DELIM", Delim());
      end;

      return true;
  END;

  MACRO Restore()
      this.Set_Delim(cache.Delim);
  END;

  MACRO run(_mode)
      run_mode = _mode;

      if (run_mode == PANELMODE_EDIT) // режим ввода параметров по умолчанию
          status = "~F9~ Сохранить ~F3~ Выбор ~Esc~ Выход";
          caption = "Настройка формата";
      else // режим заполнения параметров импорта
          run_mode = PANELMODE_SELECT;
          caption = "Доп. настройки формата";
          status = "~F2~ Продолжить ~F3~ Выбор ~Esc~ Выход";
      end;

      if (not IsInitObj)
          addControl(fld_Delim);
          IsInitObj = true;
      end;

      // кешируем значения
      cache.Delim = this.Delim();

      _extObj.run();
      if (key == -ULK_ESC)
          Restore();
      end;

      return key;
  END;

  MACRO eventHandler(EV)
    var
        isSave = IND_NO;

      key = -EV.keyCode;

      if ((run_mode == PANELMODE_SELECT)and(EV.keyCode == ULK_F2))
          hide();
      elif ((run_mode == PANELMODE_SELECT)and(EV.keyCode == ULK_ESC))
          EV.keyCode = 0;
          hide();
      elif ((run_mode == PANELMODE_EDIT)and(EV.keyCode == ULK_F9))
          if (this.Save())
              close(-EV.keyCode);
          end;
      elif ((run_mode == PANELMODE_EDIT)and(EV.keyCode == ULK_ESC))
          if (this.CheckChanges())
              isSave = MsgBoxEx("Сохранить изменения?", MB_YES + MB_NO + MB_CANCEL, IND_YES);
              if (isSave == IND_YES)
                  EV.keyCode = ULK_F9;
                  this.eventHandler(EV);
              elif (isSave == IND_NO)
                  close(-EV.keyCode);
              else
                  EV.keyCode = 0;
              end;
          end;
      end;
  END;

  // конструктор
  InitTRsbPanel();

  setPosition(50, 15);
  setSize(50, 4);

  addLabel(TRsbLabel(2, 2, "Разделитель:"));
  fld_Delim            = TRsbComboBox();
  fld_Delim.name       = "fld_Delim";
  fld_Delim.dataType   = 7; //FT_STRING
  fld_Delim.textLength = 34;
  fld_Delim.setPosition(15, 2);
  fld_Delim.setSize (34, 1);
  fld_Delim.naId = 2122;

  addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));

  sql = RsdCommand(
                   "select * from ddl_ul_format_dbt where t_ID = ?"
                   );
  sql.addParam("", RSDBP_IN, _formatID);
  sql.execute();
  structFormat = TRsbDataSet(sql);
  structFormat.MoveNext();

  InitDefValue();
END;
