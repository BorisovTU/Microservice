/*
$Name: ul_selectformat.mac
$Module: Универсальный загрузчик
$Description: Панель выбора формата
*/

IMPORT RsbFormsInter, RsbDataSet, RSD, "ul_const.mac";

CLASS(TRsbPanel) UL_SelectFormat()
  private var
      fld_DataKind     = NULL, // поле "Виды данных"
      fld_GroupFormat  = NULL, // поле "Гуппа форматов"
      fld_Format       = NULL; // поле "Формат"

  MACRO GetFormatID()
      return fld_Format.currentChoice;
  END;

  PRIVATE MACRO SetEnabled()
      if (fld_DataKind.currentChoice == 0)
          fld_GroupFormat.enabled = false;
      else
          fld_GroupFormat.enabled = true;
      end;

      if ((fld_DataKind.currentChoice == 0)or(fld_GroupFormat.currentChoice == 0))
          fld_Format.enabled = false;
      else
          fld_Format.enabled = true;
      end;

      redraw();
  END;

  MACRO onDataKindSelected(EV)
      var  sql, ds, idx = 0;

      fld_GroupFormat.currentChoice = 0;
      fld_GroupFormat.Value = "";
      fld_GroupFormat.naId = 0;

      fld_Format.currentChoice = 0;
      fld_Format.Value = "";
      fld_Format.naId = 0;

      if (fld_DataKind.currentChoice != 0)
          sql = RsdCommand(
                           "select * from ddl_ul_groupformat_dbt where t_IsUse <> 'X'"
                           );
          sql.execute();

          ds = TRsbDataSet(sql);
          while (ds.MoveNext())
              fld_GroupFormat.addChoice(Int(ds.ID), substr(String(ds.Name), 1, 35));
              idx = idx + 1;
              if (idx == 1)
                  fld_GroupFormat.currentChoice = Int(ds.ID);
                  this.onGroupFormatSelected(TRsbEvent(RSB_EV_ITEM_SELECTED, fld_DataKind));
              end;
          end;
      end;

      SetEnabled();
  END;

  MACRO onGroupFormatSelected(EV)
      var  sql, ds, idx = 0;

      fld_Format.currentChoice = 0;
      fld_Format.Value = "";
      fld_Format.naId = 0;

      sql = RsdCommand(
                       "select * from ddl_ul_format_dbt where t_IsUse <> 'X' and t_ID_DataKind = ? and t_ID_GroupFormat = ?"
                       );
      sql.addParam("", RSDBP_IN, fld_DataKind.currentChoice);
      sql.addParam("", RSDBP_IN, fld_GroupFormat.currentChoice);
      sql.execute();

      ds = TRsbDataSet(sql);
      while (ds.MoveNext())
          fld_Format.addChoice(Int(ds.ID), substr(String(ds.Name), 1, 35));
          idx = idx + 1;
          if (idx == 1)
              fld_Format.currentChoice = Int(ds.ID);
          end;
      end;

      SetEnabled();
  END;

  PRIVATE MACRO InitDefValue
      var  sql, ds, idx = 0;

      fld_DataKind.currentChoice = 0;
      fld_DataKind.Value = "";
      fld_DataKind.naId = 0;

      fld_GroupFormat.currentChoice = 0;
      fld_GroupFormat.Value = "";
      fld_GroupFormat.naId = 0;

      fld_Format.currentChoice = 0;
      fld_Format.Value = "";
      fld_Format.naId = 0;

      sql = RsdCommand(
                       "select * from ddl_ul_datakind_dbt"
                       );
      sql.execute();

      ds = TRsbDataSet(sql);
      while (ds.MoveNext())
          fld_DataKind.addChoice(Int(ds.ID), substr(String(ds.Name), 1, 35));
          idx = idx + 1;
          if (idx == 1)
              fld_DataKind.currentChoice = Int(ds.ID);
              this.onDataKindSelected(TRsbEvent(RSB_EV_ITEM_SELECTED, fld_DataKind));
          end;
      end;

      SetEnabled();
  END;

  MACRO eventHandler(EV)
      if (EV.keyCode == ULK_F9)
          if (this. GetFormatID() > 0)
              close(-EV.keyCode);
          else
              msgbox("Не выбран формат для импорта данных");
          end;
      end;
  END;

  // конструктор
  InitTRsbPanel();

  caption = "Импорт данных";
  setPosition(50, 15);
  setSize(51, 8);
  status = "~F9~ Продолжить ~F3~ Выбор ~Esc~ Выход";

  // инициализация полей панели
  addLabel(TRsbLabel(2, 2, "Вид данных:"));
  fld_DataKind            = TRsbComboBox();
  fld_DataKind.name       = "fld_DataKind";
  fld_DataKind.dataType   = 7; //FT_STRING
  fld_DataKind.textLength = 35;
  fld_DataKind.setPosition(15, 2);
  fld_DataKind.setSize (35, 1);
  fld_DataKind.onItemSelected(r2m(this, "onDataKindSelected"));
  addControl(fld_DataKind);

  addLabel(TRsbLabel(2, 4, "Группа форматов:"));
  fld_GroupFormat            = TRsbComboBox();
  fld_GroupFormat.name       = "fld_GroupFormat";
  fld_GroupFormat.dataType   = 7; //FT_STRING
  fld_GroupFormat.textLength = 35;
  fld_GroupFormat.setPosition(15, 4);
  fld_GroupFormat.setSize (35, 1);
  fld_GroupFormat.onItemSelected(r2m(this, "onGroupFormatSelected"));
  addControl(fld_GroupFormat);

  addLabel(TRsbLabel(2, 6, "Формат:"));
  fld_Format            = TRsbComboBox();
  fld_Format.name       = "fld_Format";
  fld_Format.dataType   = 7; //FT_STRING
  fld_Format.textLength = 35;
  fld_Format.setPosition(15, 6);
  fld_Format.setSize (35, 1);
  addControl(fld_Format);

  addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
  InitDefValue();
END;
