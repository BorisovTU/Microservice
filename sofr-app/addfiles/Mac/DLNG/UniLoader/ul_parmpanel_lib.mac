/*
$Name: ul_parmpanel_lib.mac
$Module: Универсальный загрузчик
$Description: Библиотека для работы с интерфейсами параметров импорта
*/

IMPORT RsbFormsInter;

CLASS UL_LogControl()
  PRIVATE VAR
      fld_Success     = NULL,  // поле "Выводить в протокол информацию об успешном импорте данных"
      fld_DoubleObj   = NULL;  // поле "Выводить в протокол информацию о повторном импорте данных"

  VAR 
      X = 0,
      Y = 0;

  MACRO isSuccess()
      return fld_Success.Checked;
  END;
  MACRO Set_isSuccess(newval : bool)
      fld_Success.Checked = newval;
  END;

  MACRO isDoubleObj()
      return fld_DoubleObj.Checked;
  END;
  MACRO Set_isDoubleObj(newval : bool)
      fld_DoubleObj.Checked = newval;
  END;

  MACRO AddLog(panel)
      panel.addLabel(TRsbLabel(this.X, this.Y, "Выводить в протокол информацию:"));

      panel.addLabel(TRsbLabel((this.X + 3), (this.Y + 1), " об успешном импорте данных"));
      fld_Success.setPosition((this.X + 1), (this.Y + 1));
      panel.addControl(fld_Success);

      panel.addLabel(TRsbLabel((this.X + 3), (this.Y + 2), " о повторном импорте данных"));
      fld_DoubleObj.setPosition((this.X + 1), (this.Y + 2));
      panel.addControl(fld_DoubleObj);
  END;

  // конструктор
  fld_Success           = TRsbCheckBox();
  fld_Success.name      = "fld_Success";
  fld_Success.dataType  = 12; //FT_CHR

  fld_DoubleObj           = TRsbCheckBox();
  fld_DoubleObj.name      = "fld_Success";
  fld_DoubleObj.dataType  = 12; //FT_CHR
END;

CLASS UL_DirControl()
  PRIVATE VAR
      fld_InputDir     = NULL,  // поле "INPUTDIR"
      fld_OutputDir    = NULL,  // поле "OUTPUTDIR"
      fld_ErrDir       = NULL;  // поле "ERRDIR"

  VAR 
      X = 0,
      Y = 0,
      Length = 25;

  MACRO InputDir()
      return fld_InputDir.Value;
  END;
  MACRO Set_InputDir(newval : string)
      fld_InputDir.Value = newval;
  END;

  MACRO OutputDir()
      return fld_OutputDir.Value;
  END;
  MACRO Set_OutputDir(newval : string)
      fld_OutputDir.Value = newval;
  END;

  MACRO ErrDir()
      return fld_ErrDir.Value;
  END;
  MACRO Set_ErrDir(newval : string)
      fld_ErrDir.Value = newval;
  END;

  MACRO onKeyPressed(EV)
    var
        path = EV.source.Value;

      if (EV.keyCode == 317) // K_F3
          if (SelectFolder(EV.source.Value, EV.source.Value, "", true))
              EV.source.Value = EV.source.Value + "\\";
          else
              EV.source.Value = path;
          end;
      end;
  END;

  MACRO AddDir(panel)
      panel.addLabel(TRsbLabel(this.X, this.Y, "Каталоги данных:"));

      panel.addLabel(TRsbLabel(this.X, (this.Y + 1), "INPUTDIR:"));
      fld_InputDir.setSize(this.Length, 1);
      fld_InputDir.setPosition((this.X + 11), (this.Y + 1));
      panel.addControl(fld_InputDir);

      panel.addLabel(TRsbLabel(this.X, (this.Y + 2), "OUTPUTDIR:"));
      fld_OutputDir.setSize(this.Length, 1);
      fld_OutputDir.setPosition((this.X + 11), (this.Y + 2));
      panel.addControl(fld_OutputDir);

      panel.addLabel(TRsbLabel(this.X, (this.Y + 3), "ERRDIR:"));
      fld_ErrDir.setSize(this.Length, 1);
      fld_ErrDir.setPosition((this.X + 11), (this.Y + 3));
      panel.addControl(fld_ErrDir);
  END;

  // конструктор
  fld_InputDir   = TRsbEditField();
  fld_InputDir.name       = "fld_InputDir";
  fld_InputDir.dataType   = 7; //FT_STRING;
  fld_InputDir.textLength = 255;
  fld_InputDir.onKeyPressed(r2m(this, "onKeyPressed"));

  fld_OutputDir   = TRsbEditField();
  fld_OutputDir.name       = "fld_OutputDir";
  fld_OutputDir.dataType   = 7; //FT_STRING;
  fld_OutputDir.textLength = 255;
  fld_OutputDir.onKeyPressed(r2m(this, "onKeyPressed"));

  fld_ErrDir   = TRsbEditField();
  fld_ErrDir.name       = "fld_InputDir";
  fld_ErrDir.dataType   = 7; //FT_STRING;
  fld_ErrDir.textLength = 255;
  fld_ErrDir.onKeyPressed(r2m(this, "onKeyPressed"));
END;