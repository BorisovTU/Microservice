/*
$Name: ul_rule_i.mac
$Module: Универсальный загрузчик
$Description: Таблица соответствия форматов
*/

IMPORT RsbFormsInter, RSD, "ul_const.mac";

PRIVATE MACRO NA_Scroll(NumberAlg:INTEGER, Header:STRING, X:INTEGER, Y:INTEGER /*...*/):RsdRecordset
  var 
      rs = RsdRecordset("select t_szNameAlg, t_iNumberAlg from dnamealg_dbt where t_itypealg = " + String(NumberAlg), RSDVAL_CLIENT, RSDVAL_STATIC ),
      Column = TArray(),
      i, ColumnValue, ColumnName, NumColumns = 0;

  macro EvProc(rs, cmd, id, key)
      if ((cmd == DLG_KEY) and (key == ULK_ENTER))
          return CM_SELECT;
      end;
      return CM_DEFAULT;
  end;

  /* атрибуты полей */
  macro AddColumn(ar,ind, fld, head)
      ar.value(ind * 6 + 0) = fld;  // fieldName
      ar.value(ind * 6 + 1) = head; // header 
      ar.value(ind * 6 + 2) = NULL; // width
      ar.value(ind * 6 + 3) = 2;    // fldType (2 = FBT)
      ar.value(ind * 6 + 4) = NULL; // decPoint
      ar.value(ind * 6 + 5) = 0;    // reserv
      NumColumns = NumColumns + 1;
  end;
 
    i = 4;
    while (GetParm(i, ColumnValue) AND GetParm(i+1, ColumnName))
        AddColumn(Column, NumColumns, ColumnValue, ColumnName);
        i = i + 2;
    end;

    if (rs.moveNext())
        if (RunScroll(rs, NumColumns, Column, NULL, @EvProc, Header, "Esc Выход F4 Поиск Enter Выбор", true, X, Y, NULL, 25))
            return rs;
        end;
    end;

    return NULL;
END;

/*Номера столбцов*/
PRIVATE CONST
    COL_ID_FORMAT     = 0,
    COL_OUTFIELD      = 1,
    COL_INFIELD       = 2,
    COL_DATATYPE      = 3,
    COL_DATATYPENAME  = 4,
    COL_ISMAND        = 5,
    COL_CONVMAC       = 6,
    COL_CONVFUN       = 7,
    NUM_COLS          = 8;

MACRO Rule_Scrol(x, y, FormatID) : RsdRecordSet
  var
      rs = NULL,
      Columns = TArray();

  macro AddColumn(ar,ind, fld, head, width, fldType)
      ar.value( ind * 6 + 0 ) = fld;
      ar.value( ind * 6 + 1 ) = head;
      ar.value( ind * 6 + 2 ) = width;
      ar.value( ind * 6 + 3 ) = fldType;
      ar.value( ind * 6 + 4 ) = NULL;
      ar.value( ind * 6 + 5 ) = 0;
  end;

    macro ScrolCommand(rs, _ins, _upd, _del) 
        var id, inf, outf, dtt, ism, cnvm, cnvf, rinf, routf;

        getScrollFieldValue(COL_ID_FORMAT, id);
        getScrollFieldValue(COL_INFIELD, inf);   if (strlen(inf) == 0) inf = "chr(1)"; else inf = "'"+inf+"'"; end;
        getScrollFieldValue(COL_OUTFIELD, outf); if (strlen(outf) == 0) outf = "chr(1)"; else outf = "'"+outf+"'"; end;
        getScrollFieldValue(COL_DATATYPE, dtt);  if (dtt == 0) dtt = UL_STRING; end;
        getScrollFieldValue(COL_ISMAND, ism);
        ism = substr(ism, 1, 1);                 if (strlen(ism) == 0) ism = "chr(0)"; else ism = "'"+ism+"'"; end;
        getScrollFieldValue(COL_CONVMAC, cnvm);  if (strlen(cnvm) == 0) cnvm = "chr(1)"; else cnvm = "'"+cnvm+"'"; end;
        getScrollFieldValue(COL_CONVFUN, cnvf);  if (strlen(cnvf) == 0) cnvf = "chr(1)"; else cnvf = "'"+cnvf+"'"; end;

        if ((id == 0) or (rs.EOF))
            rinf = inf;
            routf = outf;
        else
            rinf = rs.Value("T_INFIELD");   if (strlen(rinf) == 0) rinf = "chr(1)"; else rinf = "'"+rinf+"'"; end;
            routf = rs.Value("T_OUTFIELD"); if (strlen(routf) == 0) routf = "chr(1)"; else routf = "'"+routf+"'"; end;
        end;

        if (_ins)
            var cmdIns = RsdCommand("insert into ddl_ul_rule_dbt(T_ID_FORMAT, T_INFIELD, T_OUTFIELD, T_DATATYPE, T_ISMANDATORY, T_CONVERTMACRO, T_CONVERTFUNC) " +
                                    " values(" + FormatID + ", " + inf + ", " + outf + ", " + dtt + ", " + ism + ", " + cnvm + ", " + cnvf + ")");
            rs.InsertCommand = cmdIns;
        end;

        if (_upd)
            var cmdUpd = RsdCommand("update ddl_ul_rule_dbt set T_INFIELD = " + inf + 
                                    ", T_OUTFIELD = " + outf + 
                                    ", T_DATATYPE = " + dtt + ", T_ISMANDATORY = " + ism + ", " +
                                    "T_CONVERTMACRO = " + cnvm + ", T_CONVERTFUNC=" + cnvf + " where T_ID_FORMAT =" + FormatID + "  AND T_INFIELD  = " + rinf + " AND T_OUTFIELD = " + routf);
            rs.UpdateCommand = cmdUpd;
        end;

        if (_del)
            var cmdDel = RsdCommand("delete from ddl_ul_rule_dbt where T_ID_FORMAT = " + FormatID + " AND T_INFIELD  = " + rinf + " AND T_OUTFIELD = " + routf);
            rs.DeleteCommand = cmdDel;	
        end;

        if ((_ins)or(_upd))
            var cmdRef = RsdCommand("select r.T_ID_FORMAT, r.T_OUTFIELD, r.T_INFIELD, " +
                                    "NVL(na.T_SZNAMEALG, 'STRING') as T_DATATYPENAME, " +
                                    "r.T_DATATYPE, " +
                                    "r.T_ISMANDATORY, r.T_CONVERTMACRO, r.T_CONVERTFUNC from ddl_ul_rule_dbt r, DNAMEALG_DBT na " +
                                    " WHERE r.T_ID_FORMAT = " + FormatID + " and r.T_DATATYPE = na.T_INUMBERALG(+) and na.T_ITYPEALG(+) = 2123 " +
                                    " AND T_INFIELD  = " + inf + " AND T_OUTFIELD = " + outf);
            rs.InsUpdCommand = cmdRef;
         end;
    end;
    
  macro proc(rs, cmd, id, key)
    var
        id_format, outf;

      if (key == ULK_SPACE)
          if (id == COL_ISMAND)
              var ch = NULL;
              getScrollFieldValue(COL_ISMAND, ch);
              if (substr(ch, 1, 1) == ULSET_CHAR)
                  setScrollFieldValue(COL_ISMAND, ULUNSET_CHAR, 1);
              else
                  setScrollFieldValue(COL_ISMAND, ULSET_CHAR, 1);
              end;
              UpdateFields(); 
          end;
      elif (key == ULK_F3)
          if (id == COL_DATATYPENAME)
              var rsNA = NA_Scroll(2123, "Тип данных", 20, 10, "t_szNameAlg", "Тип");
              if (ValType(rsNA) != V_UNDEF)
                  setScrollFieldValue(COL_DATATYPE,     rsNA.Value("t_INumberAlg"), NULL);
                  setScrollFieldValue(COL_DATATYPENAME, rsNA.Value("t_szNameAlg"),  strlen(rsNA.Value("t_szNameAlg")));
                  UpdateFields();
              end;
          end;
      elif (key == ULK_PREREC)
          UpdateFields();
          getScrollFieldValue(COL_ID_FORMAT, id_format);
          if (id_format == 0)
              setScrollFieldValue(COL_DATATYPE,     UL_STRING, NULL);
              setScrollFieldValue(COL_DATATYPENAME, "STRING",  6);
              UpdateFields();
              ScrolCommand(rs, false, false, false);
          else
              ScrolCommand(rs, false, false, true);
          end;
      elif (key == ULK_POSTREC)
          getScrollFieldValue(COL_ID_FORMAT, id_format);
          getScrollFieldValue(COL_OUTFIELD, outf);
          if (id_format == 0)
              if (strlen(trim(outf)) == 0)
                  msgbox("Не заполнено обязательное поле 'Атрибут выходной структуры'");
                  return CM_CANCEL;
              end;
              ScrolCommand(rs, true, true, true);
          else
              ScrolCommand(rs, false, true, true);
          end;
      end;

      return CM_DEFAULT;
  end;

    AddColumn(Columns, COL_ID_FORMAT,    "T_ID_FORMAT",    "T_ID_FORMAT",                0,  1);
    AddColumn(Columns, COL_OUTFIELD,     "T_OUTFIELD",     "Атрибут выходной структуры", 35, 1);
    AddColumn(Columns, COL_INFIELD,      "T_INFIELD",      "Атрибут в ТФ",               40, 1);
    AddColumn(Columns, COL_DATATYPE,     "T_DATATYPE",     "T_DATATYPE",                 0,  2);
    AddColumn(Columns, COL_DATATYPENAME, "T_DATATYPENAME", "Тип данных",                 12, 2);
    AddColumn(Columns, COL_ISMAND,       "T_ISMANDATORY",  "Обязательное поле",          1,  2);
    AddColumn(Columns, COL_CONVMAC,      "T_CONVERTMACRO", "Конвертер. Макрос",          50, 1);
    AddColumn(Columns, COL_CONVFUN,      "T_CONVERTFUNC",  "Конвертер. Макрофункция",    30, 1);

    rs = RsdRecordSet("select r.T_ID_FORMAT, r.T_OUTFIELD, r.T_INFIELD, " +
                      "NVL(na.T_SZNAMEALG, 'STRING') as T_DATATYPENAME, " +
                      "r.T_DATATYPE, " +
                      "r.T_ISMANDATORY, r.T_CONVERTMACRO, r.T_CONVERTFUNC from ddl_ul_rule_dbt r, DNAMEALG_DBT na " +
                      " WHERE r.T_ID_FORMAT = " + FormatID + " and r.T_DATATYPE = na.T_INUMBERALG(+) and na.T_ITYPEALG(+) = 2123 ORDER BY r.T_OUTFIELD, r.T_INFIELD",
                      RSDVAL_CLIENT, RSDVAL_STATIC);

    RunScroll(rs, NUM_COLS, Columns, NULL, @proc, "Таблица соответствия", "~F3~ Выбор ~F9~ Сохранить ~Space~ Установить/снять флаг ~Esc~ Выход", false, x, y, NULL, NULL);

    return rs;
END;
