/*
$Name:        nptxsnoblim_rep.mac
$Module:      Ценные бумаги
$Description: Протокол изменений записи настроечной таблицы предельных значений СНОБ
*/
import OprInter, RsbDataSet, "cb_sql.mac", "scsrvrepfun.mac";

MACRO PrintReportNptxSnobLimCh(LimID:integer):integer
  var errcode, errtext;
  var ReportFileName;
  var query, cmd, DataSet;
  var FirstTime = time(), LastTime = time();
  var First:bool = true;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/
  var i = 1;

  ReportFileName = GetTxtFileName("dlcontrfrobch");
  if( not Open(ReportOutFile, ReportFileName) )
     errcode = status(errtext);
     RunError("Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext);
  end;
  SetOutput( ReportFileName );

  query =   " select ch.*, p.t_Name as UserName "
          + "   from dnptxsnoblimitch_dbt ch, dperson_dbt p "
          + "  where ch.t_LimID = ? "
          + "    and p.t_Oper = ch.t_User "
          + " order by ch.t_ID ASC ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(LimID);

  DataSet = cmd.Execute();


  println( "Протокол изменений записи в таблице предельных значений СНОБ");
  println( "┌───┬────────────────┬──────────────────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┐\n" +
           "│ № │ Дата изменения │     Пользователь     │    Прежняя дата    │     Новая дата     │     Прежний КБК    │      Новый КБК     │\n" +
           "│   │                │                      │ окончания действия │ окончания действия │                    │                    │\n" +
           "│   │                │                      │       шкалы        │        шкалы       │                    │                    │\n" +
           "├───┼────────────────┼──────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤");
  while( DataSet.MoveNext() )
     if(i > 1)
       println("├───┼────────────────┼──────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤");

     end;
           
           [│###│################│######################│####################│####################│####################│####################│]
           ( i, 
             string(date(DataSet.Drec):f):c:w, 
             string(string(DataSet.User) + " "+DataSet.UserName):w, 
             string(date(DataSet.EndDateScale_Old):f):c,
             string(date(DataSet.EndDateScale_New):f):c,
             DataSet.CodeKBK_Old:c,
             DataSet.CodeKBK_New:c 
           );     
     
     i = i + 1;
  end;
     
   println("└───┴────────────────┴──────────────────────┴────────────────────┴────────────────────┴────────────────────┴────────────────────┘");

  SetOutput(NULL, true);
  close(ReportOutFile);
  ViewFile(ReportOutFile);

  return 0;
END;