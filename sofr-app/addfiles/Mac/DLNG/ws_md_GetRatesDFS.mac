/*
$Name:         ws_md_GetRatesDFS.mac
$Module:       Монитор дилера
$Description:  Получение и сохранение кортировок от ММВБ
*/

import "ws_md_MTECommon.mac";


private macro PrintDbgInfo(curPair)
  println(" curPair = " + curPair.Id + "  CurPairId = " + curPair.CurPairId);
  println(" Ask = " + curPair.FxRateBuy + "  Bid = " + curPair.FxRateSale);
  println(" Date = " + curPair.InputDate + "  Time = " + curPair.InputTime);
end;

private macro SaveCurPair(curPair)
  var FxRateBuy = 0.0, FxRateSale = 0.0;

  if( (curPair.FxRateBuy > 0.0) or (curPair.FxRateSale > 0.0) )

    if((curPair.FxRateBuy > 0.0) and (curPair.FxRateSale > 0.0) )
      FxRateBuy = curPair.FxRateBuy;
      FxRateSale = curPair.FxRateSale;
    else
      var Query = RSDCommand( " SELECT * FROM drkRate_dbt Where t_CurPairId = " + curPair.CurPairId +" ORDER BY t_InputDate Desc, t_InputTime Desc " );
      Query.execute();
      var DataSet = TRsbDataSet(Query);
      if (DataSet.MoveNext())
        if(curPair.FxRateBuy <= 0.0 )  
          FxRateBuy = DataSet.FxRateBuy;
          FxRateSale = curPair.FxRateSale;
        elif(curPair.FxRateSale <= 0.0 )
          FxRateBuy = curPair.FxRateBuy;
          FxRateSale = DataSet.FxRateSale;
        end;
      end;

    end;

    if((FxRateBuy > 0.0) and (FxRateSale > 0.0) )
      if(curPair.InputTime == time(0,0,0))
        curPair.InputTime = time();
      end;

      if(curPair.InputDate == date(0,0,0))
        curPair.InputDate = date();
      end;

      var rkRateRec = Tbfile("rkrate", "W", 0);
      rkRateRec.Clear();
      rkRateRec.rec.ID = 0;
      rkRateRec.rec.CurPairId = curPair.CurPairId;
      rkRateRec.rec.FxRateBuy =  FxRateBuy;
      rkRateRec.rec.FxRateSale = FxRateSale;
      rkRateRec.rec.InputDate = curPair.InputDate;
      rkRateRec.rec.InputTime = curPair.InputTime;

      if( not rkRateRec.insert() )
         msgbox("Ошибка вставки новой записи в таблицу drkrate_dbt");
      end;
    end;
  end;
end;

macro GetRatesDFS()
  var prm = Settings();
  ReplaceMacro( "msgbox", "MTE_WriteLog" );

  var errMsg = "";
  var stat = 0;
  var curPair = TRecHandler("rkrate.dbt");
  var curName;

  var Idx =  GetConnectIdxFromCache();  
  var Htable =  GetTableIdxFromCache();  

  if( (Idx >= MTE_OK) and (Htable >= MTE_OK) )
    var Query = RSDCommand( " SELECT *  FROM DRKCURPAIR_DBT " );
    Query.execute();
    var DataSet = TRsbDataSet(Query);
    while (DataSet.MoveNext())
      curName = "";
      curPair.rec.Id = 0;
      curPair.rec.CurPairId = SQL_ConvTypeInteger( DataSet.Id );
      curName = prm.curPairMap.GetNameInTradeSystem( ( (ПолучитьКодФинИн(DataSet.PFI, null, FICK_ISOSTRING) )+(ПолучитьКодФинИн(DataSet.CFI, null, FICK_ISOSTRING)) ) );
      /*В этом цикле выполняем только рефреш таблицы для каждой валютной пары*/
      stat = Web_MTEGetQuotes(Idx, Htable, prm.GetTableName(),  curName,  errMsg, curPair, DECIMALS);
      if(stat >= 0)
        SaveCurPair(curPair.rec);
/*        PrintDbgInfo(curPair.rec);*/
      else
        msgbox("Ошибка получения параметров валютной пары " + "CurPairName: " + errMsg);
      end;
    end;
  else
    if(Idx < MTE_OK)
      msgbox("Не открыто соединение со шлюзом ММВБ");
    elif (HTable < MTE_OK)
      msgbox("Не открыта таблица фин.инструментов");
    end;
  end;

  ReplaceMacro( "msgbox", NULL );
end;

GetRatesDFS();