/*
$Name:        dl_cal_param_sys.mac
$Module:      Календари
$Description: Выбор параметров календарей
*/
import dl_calendtls, "CbForms_h.mac";
  
class CCalendParam
  //возвращаемое значение DL_KeyPair,
  //где key - фактическое значение, value - текстовое представление значения
  //либо NULL, если выбор осуществлён не был

  //переменные для наполнения текущих значений параметров Си кодом
  //!не менять!
  var ParamValArr;
  var SubsytemId;
  
  private MACRO ListFI(iso_number:@string)
      var result = 0;
      var select = " SELECT fi.t_FI_Code FICode, fi.t_Name NameFI, fi.t_FIID FI, fi.t_iso_number iso_number" +
                   " FROM dfininstr_dbt fi "  +
                   " WHERE fi.t_FI_Kind in (1,6) " + 
                   " ORDER BY fi.t_FI_Kind, fi.t_FI_Code";
      var Choice = DL_Scroll(select,
                             "Список финансовых инструментов", 10, 5,
                             "FICode","Код", "NameFI","Название", "FI","ID инструмента"
                            );
      if( Choice != NULL )
         result = int(Choice.Value("FI"));
         iso_number = string(Choice.Value("iso_number"));
      end;

      return result;
  END;

  macro GetParamValue(key:string)
    var idx = DL_FindKeyPairInArr(ParamValArr,key);
    if (idx >= 0)
      return ParamValArr[idx].value;
    end;
    return null;
  end;
  
  macro ListAvoirKind()
    var returnVal = FI_ListAvrKinds_Tree(2,0,true);
    if (returnVal != null)
      return DL_KeyPair(returnVal.rec.AvoirKind, returnVal.rec.Name);
    end;
    return null;
  end;

  macro ListObjectType()
    var returnVal = GenChooseScrollAndGetValue(
      "Выберите тип объекта привязки",
      makeArray(
        PrmColumnSettings("Идентификатор","col_1",V_INTEGER,15),
        PrmColumnSettings("Наименование","col_2",V_STRING,40)
      ),
      "select 1 as col_1, 'Биржевая сделка' as col_2 from dual "
      +"union all select 2 as col_1, 'Внебиржевая сделка' as col_2 from dual "
      +"union all select 3 as col_1, 'Сервисная операция' as col_2 from dual "
    );
    if (returnVal != null)
      return DL_KeyPair(SQL_ConvTypeInteger(returnVal.value("col_1")),SQL_ConvTypeStr(returnVal.value("col_2")));
    end;
    return null;
  end;

  macro ListDayType()
    var marketValsNeeded = false;
    var outMarketValsNeeded = false;
    var objectTypeVal = GetParamValue("ObjectType");
    if (objectTypeVal != null)
      if (Int(objectTypeVal) == DL_CALLNK_OUTMARKET)
        outMarketValsNeeded = true;
      end;
      if (Int(objectTypeVal) == DL_CALLNK_MARKET)
        marketValsNeeded = true;
      end;
    end;
    if (not outMarketValsNeeded and not marketValsNeeded)
      RunError("Тип дней используется только для биржевых и внебиржевых типов объектов привязки");
    end;
    var returnVal = GenChooseScrollAndGetValue(
      "Выберите тип дней",
      makeArray(
        PrmColumnSettings("Идентификатор","col_1",V_INTEGER,15),
        PrmColumnSettings("Наименование","col_2",V_STRING,40)
      ),
      "select 0 as col_1, '' as col_2 from dual where 1=0"
      +IIF(marketValsNeeded,
      "union all select 1 as col_1, 'Торговые' as col_2 from dual "
      +"union all select 2 as col_1, 'Расчетные' as col_2 from dual ",
      "")
      +IIF(outMarketValsNeeded,"union all select 3 as col_1, 'Без расчетов' as col_2 from dual ","")
    );
    if (returnVal != null)
      return DL_KeyPair(SQL_ConvTypeInteger(returnVal.value("col_1")),SQL_ConvTypeStr(returnVal.value("col_2")));
    end;
    return null;
  end;

  macro ListMarket()
    var party = TRecHandler("party.dbt");
    if( ListPT( party, 1, "", PTLIST_MARKETPLASE, PTCK_CONTR) == true ) 
      return DL_KeyPair(party.rec.partyid,party.rec.name);
    end;
    return null;
  end;

  macro ListObject()
    var objectTypeVal = GetParamValue("ObjectType");
    if (objectTypeVal == null)
      RunError("Для задания объекта привязки, необходимо вначале задать тип объекта привязки");
    end;
    var calOpr = SC_ShowCalendOprsList(SubsytemId,Int(objectTypeVal));
    if (calOpr != null)
      return DL_KeyPair(calOpr.rec.Name,calOpr.rec.Name);
    end;
    return null;
  end;

  macro ListCurrency()
    var fiid, iso_number;
    if (fiid = ListFI(@iso_number))
      return DL_KeyPair(fiid,iso_number);
    end;
    return null;
  end;

  macro ListMarketPlace()
    var returnVal = GenChooseScrollAndGetValue(
      "Выберите биржевой рынок",
      makeArray(
        PrmColumnSettings("Идентификатор","col_1",V_INTEGER,15),
        PrmColumnSettings("Наименование","col_2",V_STRING,40)
      ),
      "select 1 as col_1, 'Фондовый' as col_2 from dual "
      +"union all select 2 as col_1, 'Валютный' as col_2 from dual "
      +"union all select 3 as col_1, 'Срочный' as col_2 from dual "
      +"union all select 4 as col_1, 'Рынок СПФИ' as col_2 from dual "
    );
    if (returnVal != null)
      return DL_KeyPair(SQL_ConvTypeInteger(returnVal.value("col_1")),SQL_ConvTypeStr(returnVal.value("col_2")));
    end;
    return null;
  end;

  macro ListEmitCountry()
    var country = ListCountry();
    if (country != null)
      return DL_KeyPair(country.rec.countryid,country.rec.fullname);
    end;
    return null;
  end;
end;