/*
$Name:        NotifContrDeal_Form.mac
$Module:      Ядро Securities
$Description: Форма ввода данных для отчета "Уведомление о контролируемых сделках"
*/
import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

CONST PNFLD_NCDEAL_BEGINDATE  = 0,
      PNFLD_NCDEAL_ENDDATE    = 1,
      PNFLD_NCDEAL_CLIENTCODE = 2;

PRIVATE CONST H_BEGINDATE  = 100,
              H_ENDDATE    = 101,
              H_CLIENTCODE = 102;

PRIVATE MACRO SC_UserFldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Year:integer = 0;

  if( Cmd == DLG_INIT ) /*установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        DateSplit({curdate}, null, null, Year);
        FldRealValue = date(1,1,Year-1);
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
     DateSplit(FldRealValue, null, null, Year);
     pThis.SetLinkedValue(H_ENDDATE, date(31,12,Year));
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*вызов листалки*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO SC_UserFldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Year;

  if( Cmd == DLG_INIT ) /*установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        DateSplit({curdate}, null, null, Year);
        FldRealValue = date(31,12,Year-1);
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*вызов листалки*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

// используется также для web
MACRO AddWhereForClientFld( _date )
  var AddWhere:String = "";

  AddWhere = " EXISTS( SELECT attr.* " +
             "           FROM dobjatcor_dbt attr " +
             "          WHERE attr.t_ObjectType = " + string(OBJTYPE_PARTY) +
             "            AND attr.t_GroupID    = 58 " + 
             "            AND attr.t_Object     = LPAD(t.T_PARTYID, 10, '0') " +
             "            AND (SELECT count(1) " +
             "                   FROM DOBJATCOR_DBT at " +
             "                  WHERE at.t_ObjectType = attr.t_ObjectType " +
             "                    AND at.t_GroupID    = attr.t_GroupID " +
             "                    AND at.t_Object     = attr.t_Object " +
             "                    AND at.t_ValidFromDate <= " + GetSQLDate( _date ) +
             "                 ) > 0 " +
             "       ) ";

  return AddWhere;
END;

PRIVATE MACRO SC_UserFldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party = TRecHandler("party.dbt");
  var whereCond:string = "";

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодСубъекта(FldRealValue, PTCK_CONTR);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     whereCond = AddWhereForClientFld( pThis.GetLinkedValue(H_ENDDATE) );
     if( ListPText(Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        FldShowValue = ПолучитьКодСубъекта(FldRealValue, PTCK_CONTR);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_ALTF3) )
     if( ListPText(Party, Party.rec.PartyID, "") == true )
        FldRealValue = Party.rec.PartyID;
        FldShowValue = ПолучитьКодСубъекта(FldRealValue, PTCK_CONTR);
     end;
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SC_NotifContrDeal_Panel()

  PRIVATE MACRO Init()
     var CurYear;
     DateSplit({curdate}, null, null, CurYear);

     AddFieldProc( 0, PNFLD_NCDEAL_BEGINDATE,  H_BEGINDATE,  @SC_UserFldProc_BeginDate );
     AddFieldProc( 0, PNFLD_NCDEAL_ENDDATE,    H_ENDDATE,    @SC_UserFldProc_EndDate );
     AddFieldProc( 0, PNFLD_NCDEAL_CLIENTCODE, H_CLIENTCODE, @SC_UserFldProc_Client );
  END;

  PRIVATE MACRO Check():BOOL
     var RetVal = true;
     if( GetLinkedValue(H_ENDDATE) < GetLinkedValue(H_BEGINDATE) )
        MsgBox( "Дата окончания не может быть меньше даты начала периода.");
        RetVal = false;
     end;

     return RetVal;
  END;

  initDL_CPanel( "sp_rep.lbr", "PNCDEAL" );
END;

CLASS ( TxReportForm ) WS_NotifContrDeal_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей

  ReDefineFieldNum[PNFLD_NCDEAL_BEGINDATE]      = TXREPORTFORM_FLD_DATEBEGIN;
  ReDefineFieldNum[PNFLD_NCDEAL_ENDDATE]        = TXREPORTFORM_FLD_DATEEND;
  ReDefineFieldNum[PNFLD_NCDEAL_CLIENTCODE]     = TXREPORTFORM_WIDGET_CLIENT;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
