/*
$Name:         createNPTXOP2022.mac
$Module:       Ценные бумаги
$Description:  Генерация операций расчета НОБ за 2022 год
*/

import RsbDataSet,spserv, dl_activex;
import "dlQuery.mac";

private const COL_DOCKIND           = 1,
              COL_KIND_OPERATION    = 2,
              COL_SUBKIND_OPERATION = 3,
              COL_CLIENT            = 4,
              COL_BEGDATE           = 5,
              COL_ENDDATE           = 6;

VAR           CL_DOCKIND,
              CL_KIND_OPERATION,
              CL_SUBKIND_OPERATION,
              CL_CLIENT,
              CL_BEGDATE,
              CL_ENDDATE;

private macro CreateTable()
   SQL_Execute("BEGIN EXECUTE IMMEDIATE 'CREATE TABLE DEF45758_DBT ( T_DOCKIND NUMBER (5),T_KIND_OPERATION NUMBER (5), T_SUBKIND_OPERATION NUMBER (5), T_CLIENT NUMBER (10), T_BEGDATE DATE, T_ENDDATE DATE, T_OP_CREATED CHAR (1), T_ERR VARCHAR2 (100))'; EXCEPTION WHEN OTHERS THEN NULL; END;");
   SQL_Execute("BEGIN EXECUTE IMMEDIATE 'DROP INDEX DEF45758_DBT_IDX0'; EXCEPTION WHEN OTHERS THEN NULL; END;");
   SQL_Execute("BEGIN EXECUTE IMMEDIATE 'CREATE UNIQUE INDEX DEF45758_DBT_IDX0 ON DEF45758_DBT (T_CLIENT ASC)'; EXCEPTION WHEN OTHERS THEN NULL; END;");
end;

private macro CheckClient(ClientID)
  var ret = 0;
  var query, cmd, DataSet;

  query =   " select 1 "
          + "   from DEF45758_DBT "
          + "  where T_CLIENT = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ClientID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ret = 1;
  end;
  return ret;
end;

private macro InsertDEF45758(DocKind:integer, Kind_Operation:integer, SubKindOperation:integer, Client:integer, BegDate:date, EndDate:date)
  var query, cmd;

  query =   " INSERT INTO DEF45758_DBT ( T_DOCKIND, T_KIND_OPERATION, T_SUBKIND_OPERATION, T_CLIENT, T_BEGDATE, T_ENDDATE, T_OP_CREATED, T_ERR ) " 
          + " VALUES (?, ?, ?, ?, ?, ?, CHR(0), CHR(1)); ";
  cmd = RSDCommand(query);
  cmd.NullConversion = true;
  cmd.AddParam("", RSDBP_IN, DocKind);
  cmd.AddParam("", RSDBP_IN, Kind_Operation);
  cmd.AddParam("", RSDBP_IN, SubKindOperation);
  cmd.AddParam("", RSDBP_IN, Client);
  cmd.AddParam("", RSDBP_IN, BegDate);
  cmd.AddParam("", RSDBP_IN, EndDate);
  SQL_Execute( cmd, "", false );
end;

private macro InsertNPTXOP()
  var query, cmd, SQL;

  SQL = "  DECLARE "
      + "  v_nptxop                    DNPTXOP_DBT%ROWTYPE; "
      + "   TYPE ListNPTXOP_t IS TABLE OF DNPTXOP_DBT%ROWTYPE; "
      + "   v_ListNPTXOP                ListNPTXOP_t := ListNPTXOP_t (); "
      + "   TYPE nptxobj_t IS TABLE OF DNPTXOBJ_DBT%ROWTYPE INDEX BY BINARY_INTEGER; "
      + "   v_nptxobj nptxobj_t; "
      + "   PROCEDURE insertOP(p_begdate IN DATE, p_operdate IN DATE, p_prevdate IN DATE, pSubKind IN NUMBER, p_PartyID IN NUMBER) "
      + "   IS "
      + "     v_OperDprt                  NUMBER (10) := ?; "
      + "     v_oper                      NUMBER (10) := ?; "
      + "     v_Kind_Operation            NUMBER (10) := 2035; "
      + "   BEGIN "
      + "       v_nptxop.t_Code := 'RC14576_'||p_PartyID; "
      + "       v_nptxop.t_ID := dnptxop_dbt_seq.NEXTVAL; "
      + "       v_nptxop.t_DocKind := RSI_NPTXC.DL_CALCNDFL; "
      + "       v_nptxop.t_OperDate := p_operdate; "
      + "       v_nptxop.t_Kind_Operation := v_Kind_Operation; "
      + "       v_nptxop.t_Client := p_PartyID; "
      + "       v_nptxop.t_Department := v_OperDprt; "
      + "       v_nptxop.t_Oper := v_oper; "
      + "       v_nptxop.t_Status := RSI_NPTXC.DL_TXOP_Prep; "
      + "       v_nptxop.t_SubKind_Operation := pSubKind; "
      + "       v_nptxop.t_IIS := CNST.UNSET_CHAR; "
      + "       v_nptxop.t_BegRecalcDate := p_begdate; "
      + "       v_nptxop.t_EndRecalcDate := p_prevdate; "
      + "       v_nptxop.t_CalcNDFL := CNST.SET_CHAR; "
      + "       v_nptxop.t_PrevDate := p_prevdate; "
      + "       v_nptxop.t_Recalc := CNST.SET_CHAR; "
      + "       v_nptxop.t_Account := RSI_RsbOperation.ZERO_STR; "
      + "       v_nptxop.t_AccountTax := RSI_RsbOperation.ZERO_STR; "
      + "       v_nptxop.t_Contract := 0; "
      + "       v_nptxop.t_Currency := 0; "
      + "       v_nptxop.t_CurrencySum := 0; "
      + "       v_nptxop.t_CurrentYear_Sum := 0; "
      + "       v_nptxop.t_FIID := -1; "
      + "       v_nptxop.t_FlagTax := CNST.UNSET_CHAR; "
      + "       v_nptxop.t_LimitStatus := 0; "
      + "       v_nptxop.t_MarketPlace := 0; "
      + "       v_nptxop.t_MarketPlace2 := 0; "
      + "       v_nptxop.t_MarketSector := 0; "
      + "       v_nptxop.t_MarketSector2 := 0; "
      + "       v_nptxop.t_Method := 0; "
      + "       v_nptxop.t_OutCost := 0; "
      + "       v_nptxop.t_OutSum := 0; "
      + "       v_nptxop.t_Partial := CNST.UNSET_CHAR; "
      + "       v_nptxop.t_Place := 0; "
      + "       v_nptxop.t_Place2 := 0; "
      + "       v_nptxop.t_PlaceKind := 0; "
      + "       v_nptxop.t_PlaceKind2 := 0; "
      + "       v_nptxop.t_PrevTaxSum := 0; "
      + "       v_nptxop.t_Tax := 0; "
      + "       v_nptxop.t_TaxBase := 0; "
      + "       v_nptxop.t_TaxSum := 0; "
      + "       v_nptxop.t_TaxSum2 := 0; "
      + "       v_nptxop.t_TaxToPay := 0; "
      + "       v_nptxop.t_Time := NPTAX.UnknownTime; "
      + "       v_nptxop.t_TotalTaxSum := 0; "
      + "       v_nptxop.t_TOUT := 0; "
      + "       v_nptxop.t_TaxDp := 0; "
      + "       v_ListNPTXOP.EXTEND (); "
      + "       v_ListNPTXOP (v_ListNPTXOP.LAST) := v_nptxop; "
      + "   END; "
      + " BEGIN "
      + "   FOR one_op IN (SELECT * FROM DEF45758_DBT WHERE T_OP_CREATED = CHR(0) ) "
      + "   LOOP "
      + "      insertOP(one_op.T_BEGDATE, one_op.T_ENDDATE, one_op.T_ENDDATE, one_op.t_SubKind_Operation, one_op.t_Client); "
      + "   END LOOP; "
      + "   IF v_ListNPTXOP.COUNT > 0 "
      + "   THEN "
      + "     FORALL i IN v_ListNPTXOP.FIRST .. v_ListNPTXOP.LAST "
      + "       INSERT INTO DNPTXOP_DBT "
      + "            VALUES v_ListNPTXOP (i); "
      + "     v_ListNPTXOP.DELETE; "
      + "   END IF; "
      + " END; "
      ;
      
  cmd = RSDCommand(SQL);
  cmd.addParam("", RSDBP_IN, {OperDprt});     
  cmd.addParam("", RSDBP_IN, {Oper});     
  
  InitProgress(-1, "Идёт генерация операций расчета НОБ", "Формирование операций расчета НОБ");

  cmd.execute();

  RemProgress();
  return true;

OnError(er)
  var err_text = "", delim = "";
  var j = 0;

  RemProgress();

  if(er and (er.err != NULL) and er.err.environment)
    while(j < er.err.environment.ErrorCount)
      err_text = err_text + delim + er.err.environment.error(j).descr;
      delim = "\n";
      j = j + 1;
    end;
    println("Ошибка при создании операций пересчета ' " + err_text);

  else
    println("Ошибка при создании операций пересчета ' " + er.Message);
  end;

  return 0;
end;

private macro GetOperID()
  var OperID = 0;
  var query, cmd, DataSet;

  query =   " SELECT Max(t_ID) OperID "
          + " FROM DNPTXOP_DBT ";
  cmd = DL_RSDCommand(query);
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    OperID = DataSet.OperID;
  end;
  return OperID;
end;

private macro UpdateDEF45758(MaxOperID:integer)
   SQL_Execute("UPDATE DEF45758_DBT DEF SET DEF.T_OP_CREATED = CHR(88) WHERE T_CLIENT IN (SELECT T_CLIENT FROM DNPTXOP_DBT OP WHERE OP.T_ID > "+ MaxOperID +
               " AND OP.T_DOCKIND = DEF.T_DOCKIND  AND OP.T_KIND_OPERATION = DEF.T_KIND_OPERATION AND OP.T_SUBKIND_OPERATION = DEF.T_SUBKIND_OPERATION AND OP.T_CALCNDFL = CHR (88) AND OP.T_RECALC = CHR (88)) ");
end;

private macro InsertDifID()
   SQL_Execute("TRUNCATE TABLE DDL_DIFFERENTID_TMP");
   SQL_Execute("INSERT INTO DDL_DIFFERENTID_TMP(T_ID) (SELECT T_CLIENT FROM DEF45758_DBT)");
end;

private macro PrintResult()
  var query, cmd, DataSet;

  query =   " SELECT T_CLIENT "
          + " FROM DEF45758_DBT, DDL_DIFFERENTID_TMP "
          + " WHERE T_CLIENT = T_ID "
          + " AND T_OP_CREATED = CHR(88) "
          ;
  cmd = DL_RSDCommand(query);
  DataSet = cmd.Execute();
  while(DataSet.moveNext())
     println("Для клиента с ID = " + DataSet.CLIENT + " успешно создалась операция пересчета");
  end;

  query =   " SELECT T_CLIENT "
          + " FROM DEF45758_DBT, DDL_DIFFERENTID_TMP "
          + " WHERE T_CLIENT = T_ID "
          + " AND T_OP_CREATED = CHR(0) "
          ;
  cmd = DL_RSDCommand(query);
  DataSet = cmd.Execute();
  while(DataSet.moveNext())
     println("Для клиента с ID = " + DataSet.CLIENT + " операция пересчета не создалась");
  end;

end;

private macro Execute()
  // проверяем наличии таблицы, если нет - создаем.
  var FullNameFile = "", FileName = "",ExtName = "", DirName = "";
  var PATH_LOAD;
  var sheet;
  var row = 2;
  var MaxOperID = 0;
  PATH_LOAD = "..\\Import\\";
  CreateTable();

  if(MsgBoxEx("Желаете загрузить данные из файла", MB_ERROR + MB_YES + MB_NO, IND_NO) == IND_YES)
    if( SelectFile(FullNameFile, PATH_LOAD, "Выберите файл для загрузки", 0, true))
      splitfile(FullNameFile, FileName, ExtName);
      FileName = FileName + ExtName;
      DirName = substr(FullNameFile, 1, index(FullNameFile, FileName) - 1);
      if(not OpenExcelFile(FileName, false, true, DirName))
        MsgBox("Ошибка открытия файла \"" + DirName + "\\" + FileName + "\"");
        return 0;
      end;
      InitProgress(-1, "Идёт обработка данных файла", "Ообработка данных файла");

      sheet = ExcelApplication.Sheets(1);
      while (valtype(sheet.cells(row, 1).value) != V_UNDEF)
        CL_DOCKIND = INT(sheet.cells(row, COL_DOCKIND).value);
        CL_KIND_OPERATION = INT(sheet.cells(row, COL_KIND_OPERATION).value);
        CL_SUBKIND_OPERATION = INT(sheet.cells(row, COL_SUBKIND_OPERATION).value);
        CL_CLIENT = INT(sheet.cells(row, COL_CLIENT).value);
        CL_BEGDATE = DATE(sheet.cells(row, COL_BEGDATE).value);
        CL_ENDDATE = DATE(sheet.cells(row, COL_ENDDATE).value);
        if(CheckClient(CL_CLIENT))
           println("Для клиента с ID = " + CL_CLIENT + " есть запись в таблице DEF45758_DBT");

        else
           InsertDEF45758(CL_DOCKIND, CL_KIND_OPERATION, CL_SUBKIND_OPERATION, CL_CLIENT, CL_BEGDATE, CL_ENDDATE);
           println("Для клиента с ID = " + CL_CLIENT + " успешно создалась запись в таблице DEF45758_DBT");
        end;
        UseProgress(row-1);
        row = row + 1;
      end;
      RemProgress();
    end;

  end; 

  if(MsgBoxEx("Желаете сгенерировать операции пересчета НОБ за 2022г.?", MB_ERROR + MB_YES + MB_NO, IND_NO) == IND_YES)
    MaxOperID = GetOperID(); //ищем максимальную текущую операцию, понадобится для протокола
    InsertDifID();
    InsertNPTXOP();
    UpdateDEF45758(MaxOperID);
    PrintResult();
  end; 

  return 0;
end;

Execute();