/*
$Name:        dlreport.mac
$Module:      Производные инструменты
$Description: Общие функции отчетов Дилинга
*/

IMPORT globals, cb_sql, CTInter, RsbDataSet, likepy, oprinter, "dlcnst.inc", "trlocale.d32";

/* группа - "Биржевая классификация" */
const КатБиржКлассификация = 15;

/* Список признаков группы "Биржевая классификация" по numinlist*/
const Код_ЦБ_ВклВУровень1 = "1.1",
      Код_ЦБ_ВклВУровень2 = "1.2",
      Код_ЦБ_ВклВУровень3 = "1.3",
      Код_ЦБ_НеДопущенКТоргам = "2";

const ЦБ_Прочие           = 0,
      ЦБ_НеДопущенКТоргам = 1,
      ЦБ_ДопущенКТоргам   = 2,
      ЦБ_ВклВЛистА1       = 3,
      ЦБ_ГосСубъектов     = 4;

/* Список признаков категории "Вид депозитарной расписки" */
const АДР = 1,            /*Американская депозитарная расписка*/
      ГДР = 2,            /*Глобальная депозитарная расписка*/
      РДР = 3;            /*российская депозитарная расписка*/

/* Подвиды договоров*/
const  CONTR_SUBKIND_DEALER        = 1, /*дилерский*/
       CONTR_SUBKIND_AGENT         = 2, /*агентский*/
       CONTR_SUBKIND_DRIVE_CB      = 3, /*Управление ц/б*/
       CONTR_SUBKIND_STORAGE       = 4, /*хранения*/
       CONTR_SUBKIND_ENTRUST       = 5, /*поручения*/
       CONTR_SUBKIND_COMISS        = 6, /*комиссии*/
       CONTR_SUBKIND_EXCHMARKET    = 8, /*биржевой рынок*/
       CONTR_SUBKIND_OUNEXCHMARKET = 9; /*внебиржевой рынок*/

private const OBJGROUP_TICKSETDVEXE = 29;

/* Должность и имя опера
*/
class DL_DepoExecuter_Loc()
   var OperId;
   var Name, Post;
   var person  = TBFile("person");
   var officer = TBFile("officer");

   GetParm ( 1, OperId );
   if(valtype(OperId) != V_INTEGER)
      OperId = {oper};
   end;

   person.rec.oper = OperId;
   if (person.GetEQ)
      Name = person.rec.Name;
   else
      Name = "";
   end;

   officer.rec.PartyID = {OurBank};
   officer.rec.PersonID = person.rec.PartyID;
   if (officer.GetEQ)
      Post = officer.rec.Post;
   else
      Post = "Ответственный сотрудник";
   end;
end;

/* Получить название вида кода субъекта
*/
MACRO DL_GetCodeKindName(CodeKind)
var
   objkcode = TBfile("objkcode.dbt");

   objkcode.KeyNum = 0;
   objkcode.rec.ObjectType = OBJTYPE_PARTY;
   objkcode.rec.CodeKind   = CodeKind;
   if(objkcode.GetEQ())
      return objkcode.rec.Name;
   end;

  return ""; // не нашли
END;

/* Получить 3x-буквенный (лат.) код валюты по ID - для печати
*/
MACRO DL_getCCode( p_id )
   var sql = RSDCommand("select t_Ccy from dfininstr_dbt where t_FIID = ? " );

   sql.addParam( "", RSDBP_IN, p_id );
   sql.execute();

   var fininstr = TRsbDataSet(sql);

   if( fininstr.MoveNext )
      return fininstr.Ccy;
   end;

   return "";
END;

/* Получить сокращенное наименование контрагента - для печати */
MACRO DL_getPartyShortName( p_id )

   var sql = RSDCommand("select t_ShortName from dparty_dbt where t_PartyID = ? ");

   sql.addParam( "", RSDBP_IN, p_id );
   sql.execute();
   var party = TRsbDataSet(sql);

   if( party.MoveNext )
      return party.ShortName;
   end;

   return "";
END;

/* Получить полное наименование контрагента - для печати */
MACRO DL_getPartyFullName( p_id )

   var sql = RSDCommand("select t_Name from dparty_dbt where t_PartyID = ? ");

   sql.addParam( "", RSDBP_IN, p_id );
   sql.execute();
   var party = TRsbDataSet(sql);

   if( party.MoveNext )
      return party.Name;
   end;

   return "";
END;

MACRO DL_getDP_DEP_Name( p_id )

   var sql = RSDCommand("select dParty_dbt.t_Name Name from dParty_dbt, ddp_Dep_dbt "
                      + "where dParty_dbt.t_PartyID = ddp_Dep_dbt.t_PartyID and "
                      + "ddp_Dep_dbt.t_Code = ?");

   sql.addParam( "", RSDBP_IN, p_id );
   sql.execute();
   var v_dp_dep = TRsbDataSet(sql);

   if( v_dp_dep.MoveNext )
      return v_dp_dep.Name;
   end;

   return "";
END;

MACRO DL_getDP_DEP_Code( p_id )

   var sql = RSDCommand("select t_Name from ddp_dep_dbt where t_Code = ? ");

   sql.addParam( "", RSDBP_IN, p_id );
   sql.execute();
   var v_dp_dep = TRsbDataSet(sql);

   if( v_dp_dep.MoveNext )
      return v_dp_dep.Name;
   end;

   return "";
END;

/*Получить значение из namealg.dbt*/
PRIVATE VAR NACache = TArray();
MACRO DL_GetNameAlg( Type:INTEGER, Number:INTEGER ):STRING
  VAR NameAlg;

  if( (NACache[Type] == NULL) OR (NACache[Type][Number] == NULL) )
     if( NACache[Type] == NULL )
        NACache[Type] = TArray();
     end;

     NameAlg = TBFile("namealg");

     NameAlg.rec.iTypeAlg   = Type;
     NameAlg.rec.iNumberAlg = Number;
     if( NameAlg.GetEQ() )
        NACache[Type][Number] = NameAlg.rec.szNameAlg;
     else
        NACache[Type][Number] = "";
     end;
  end;

  return NACache[Type][Number];
END;

/*Получить имя вида ц/б*/
MACRO DL_GetAvrKindName( FI_Kind:INTEGER, AvoirKind:INTEGER, ShortName:@STRING, Name:@STRING ):STRING
  VAR avrkind = TBFile( "avrkinds.dbt" );

  avrkind.rec.FI_Kind   = FI_Kind;
  avrkind.rec.AvoirKind = AvoirKind;
  if( not avrkind.GetEQ() )
     avrkind.Clear();
  end;
  ShortName = avrkind.rec.ShortName;
  Name      = avrkind.rec.Name;
  return avrkind.rec.Name;
END;

/*Получить значение категории*/
MACRO DL_GetMainObjAttr( Obj:VARIANT, KindCateg:INTEGER, ObjType:INTEGER ):INTEGER
  var AttrID;

  if( GetMainObjAttr( null, ObjType, UniID(Obj, ObjType), KindCateg, AttrID ) )
     return AttrID;
  end;
  return 0;
END;

/* Получить название значения категории.*/
MACRO DL_GetObjAttrName( ObjType:INTEGER, GroupID:INTEGER, AttrID:INTEGER, FullName:@STRING, ShortName:@STRING, Number:@STRING, CodeList:@STRING ):STRING
  var ObjAttr = TBFile( "objattr" );

  ObjAttr.rec.ObjectType  = ObjType;
  ObjAttr.rec.GroupID     = GroupID;
  ObjAttr.rec.AttrID      = AttrID;
  if( ObjAttr.GetEQ() != true )
     ObjAttr.Clear();
  end;
  ShortName = ObjAttr.rec.Name;
  FullName  = ObjAttr.rec.FullName;
  Number    = ObjAttr.rec.NumInList;
  CodeList  = ObjAttr.rec.CodeList;
  return ShortName;
END;

/* Получить название значения категории.*/
MACRO DL_GetObjAttrNameByNInL( ObjType:INTEGER, GroupID:INTEGER, NumInList:string, FullName:@STRING, ShortName:@STRING, AttrID:@INTEGER ):STRING

  ShortName = "";
  FullName  = "";
  AttrID    = -1;

  var sql = RSDCommand("select t_AttrID, t_Name, t_FullName "+
                       "  from dobjattr_dbt " +
                       "  where t_ObjectType = ? " +
                       "    and t_GroupID = ? " +
                       "    and t_NumInList = " + string(NumInList) +
                       "    and t_CodeList = chr(1)");

  sql.addParam( "", RSDBP_IN, ObjType );
  sql.addParam( "", RSDBP_IN, GroupID );
  sql.execute();

  var DataSet = TRsbDataSet(sql);

  if( DataSet.MoveNext() )
    ShortName = DataSet.Name;
    FullName  = DataSet.FullName;
    AttrID    = DataSet.AttrID;
  end;
  return FullName;
END;

macro DL_GetRecHandler(RecFromSelect:TRecHandler, Query:string /*, ...*/) :bool
  var cmd = RSDCommand(Query);

  var i:integer = 2, prm:variant;
  while(GetParm(i, prm))
    i = i + 1;

    if(prm != null)
      cmd.AddParam("", RSDBP_IN, prm);
    end;
  end;

  var ds = TRsbDataSet(cmd);
  if(ds.moveNext()) 
    ds.GetRecord().CopyTo(RecFromSelect.rec);
    return true;
  else
    RecFromSelect.Clear();
  end;

  return false;
end;

/*Получить договор обслуживания по договору ДУ*/
MACRO DL_FindSfContrByOrder( Order:VARIANT, SfContr:TRecHandler ) :BOOL
  VAR Query = "SELECT *" +
              "  FROM dsfcontr_dbt "+
              " WHERE t_ID = ";

  if( ValType(Order) == V_INTEGER )
     Query = Query + string(Order)
  else
     Query = Query + string(Order.rec.SfContrID)
  end;

  return DL_GetRecHandler( SfContr, Query );
END;

/*вывести адрес с индексом, дублирования индекса в строке адреса не будет*/
macro Dl_JoinAddress(PostIndex:string, Address:string):string
   if( index(Address, PostIndex) )
      return Address;
   end;
   return join(map(filter(makeArray(PostIndex, Address)), @Trim), ", ");
end;

/*найти юридический адрес с индексом, дублирования индекса в строке адреса не будет*/
macro DL_GetLegalAddress(PartyID:integer):string
   var postadress = TRecHandler("adress.dbt");
   НайтиЮридическийАдресСубъекта(PartyID,postadress);
   return Dl_JoinAddress(string(postadress.rec.PostIndex),string(postadress.rec.ADRESS));
end;

/*найти фактический адрес с индексом, дублирования индекса в строке адреса не будет*/
macro DL_GetRealAddress(PartyID:integer):string
   var postadress = TRecHandler("adress.dbt");
   НайтиАдресСубъекта(PartyID,PTADDR_REAL,postadress);
   return Dl_JoinAddress(string(postadress.rec.PostIndex),string(postadress.rec.ADRESS));
end;

/*найти Почтовый адрес с индексом, дублирования индекса в строке адреса не будет*/
macro DL_GetPostAddress(PartyID:integer):string
   var postadress = TRecHandler("adress.dbt");
   НайтиПочтовыйАдресСубъекта(PartyID,postadress);
   return Dl_JoinAddress(string(postadress.rec.PostIndex),string(postadress.rec.ADRESS));
end;

/* Aналог оператора ?: в си. */
macro DL_IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else
      return value_false;
   end;
end;

/*получить название вида операции по kind_operation*/
macro DL_GetKindOperName(Kind_Operation:integer)
   var oprkoper = TRecHandler( "oprkoper.dbt" ),
       Name = "";

   if( DL_GetRecHandler( oprkoper, " SELECT * FROM doprkoper_dbt WHERE t_Kind_Operation = " + string(Kind_Operation) ) )
      Name = oprkoper.rec.Name;
   end;

   return Name;
end;

private var OperGroupsArray = TArray(); /*кэш*/
private var _Group;
MACRO DL_GetOperationGroup( Deal )

   if( Deal.DealType >= 0 )
      if( OperGroupsArray[Deal.DealType] == null )
         _Group = GetOperationGroup( Deal.DealType );
         if( _Group < 0 )
            return -1;
         end;
      else
         return OperGroupsArray[Deal.DealType];
      end;
   else
      return -1;
   end;

   if( Deal.BofficeKind == DL_RETIREMENT )
      if( Deal.Flag1 == SET_CHAR ) /*ОРЦБ*/
         _Group = bOR( _Group, 4 );
      elif( Deal.Flag4 == SET_CHAR ) /*Брокерское*/
         _Group = bOR( _Group, 16384 ); /*IS_BROKER = 0x4000 (100000000000000)*/
      else /*внебиржевое*/
         _Group = bOR( _Group, 8192 ); /*IS_OUTEXCHANGE = 0x2000 (10000000000000)*/
      end;
   end;
   OperGroupsArray[Deal.DealType] = _Group;

   return _Group;
END;

/*сделка ПФИ*/
macro DL_DealAsResultDVExe(DealID:integer)
   var deal = TRecHandler("dl_tick.dbt");

   if( not DL_GetRecHandler( deal, "select * from ddl_tick_dbt where t_dealid = "+DealID ) )
      return false;
   end;

   return (deal.rec.IsPFI == SET_CHAR);
end;

/*** Работа с временным файлом **********************************************/

   /* Создание и открытие временного файла. Если произошла ошибка, то
      завершаем работу по RunError
         TempFile       - файл
         TempFileName   - имя файла */
   private var errcode, errname;

   macro DL_CreateOpenTempFile( TempFile, TempFileName )

      if( not (Create(TempFile,TempFileName) and Open(TempFile,TempFileName)) )
         errcode = status(errname);
         RunError(   "Ошибка при создании временного файла " + TempFileName
                     + " (" + errcode + ") " + errname );
      end;
   end;

   /* Вставить запись во временный файл. Если произошла ошибка, то завершаем
      работу по RunError. */
   macro DL_InsertRecTempFile( TempFile )

      if( not Insert(TempFile) )
         errcode = status(errname);
         RunError(   "Ошибка при вставке данных во временный файл "
                     + FileName(TempFile)
                     + " (" + errcode + ") " + errname );
      end;
   end;

   /* Обновляем запись временного файла. Если произошла ошибка, то завершаем
      работу по RunError. */
   macro DL_UpdateRecTempFile( TempFile )

      if( not Update(TempFile) )
         errcode = status(errname);
         RunError(   "Ошибка при обновлении записи временного файла "
                     + FileName(TempFile)
                     + " (" + errcode + ") " + errname );
      end;
   end;

   /* Вставить запись во временный файл. Если произошла ошибка, то завершаем
      работу по RunError. */
   macro DL_InsertRecTempTBFile( TempFile: TBFile )

      if( not TempFile.Insert )
         errcode = status(errname);
         RunError(   "Ошибка при вставке данных во временный файл "
                     + TempFile.TblName + " (" + errcode + ") " + errname );
      end;
   end;

macro DL_ClearGlobalTmp( Name )
  var cmd = RSDCommand("TRUNCATE TABLE " + Name + " DROP STORAGE");
   cmd.Execute();
end;

macro DL_GetDaysInYear( year )
  if( (mod(year, 4) == 0) AND ((mod(year,100) != 0) OR (mod(year,400) == 0 )))
     return 366;
  else
     return 365;
  end;
end;

MACRO DL_GetISOCode( FIID:integer ):integer
   var sql = RSDCommand("select t_ISO_Number from dfininstr_dbt where t_FIID = ? " );

   sql.addParam( "", RSDBP_IN, FIID );
   sql.execute();

   var fininstr = TRsbDataSet(sql);

   if( fininstr.MoveNext )
      return fininstr.ISO_Number;
   end;

   return 0;
END;

macro КоличествоЗначащихРазрядов(str:string)
  var len;
  var i = 0;

  str = trim(str);
  len = strlen(str);

  while(i < len)
    if(substr(str, len-i, 1) != "0")
      return len-i;
    end;

    i = i + 1;

  end;

  return len;
end;

/* Функция формирует строку для вывода значения переменной
   с заданным количеством знаков после запятой.
   _var  - значение переменной
   _point - количество знаков
   _parm - спецификатор форматирования
*/
macro ДЛ_ЧислоCЗаданнойТочностью( _var, _point:integer, _parm:string )
  var TmpStr:string;
   if(   (valtype(_var) == V_DOUBLE)   or (valtype(_var) == V_DOUBLEL)
         or (valtype(_var) == V_MONEY) or (valtype(_var) == V_MONEYL) )
      if( (valtype(_parm) == V_UNDEF) OR (_parm == "") )
         TmpStr = "String(_var:0" + ":" + string(_point) + ")";
      else
         TmpStr = "String(_var:0" + ":" + string(_point) + ":" + _parm + ")";
      end;
      return ExecExp( TmpStr );
   else
      return String( _var );
   end;
end;

macro DL_MeanSignAfterPoint(Num, SumPrecision:integer)
  var str;
  var PointPos = 0;
  var len;
  var i = 0;

  if(SumPrecision == 0)
    return 0;
  end;

  str = ДЛ_ЧислоCЗаданнойТочностью(Num, SumPrecision);
  PointPos = Index(str, ".");

  if( PointPos == 0 )
    return 0;
  end;

  str = SubStr(str, PointPos+1, SumPrecision);
  str = trim(str);
  len = strlen(str);

  while(i < len)
    if(substr(str, len-i, 1) != "0")
      return len-i;
    end;

    i = i + 1;

  end;

  return 0;
end;

/*для вывода значения с заданной точностью в excel (если количество знаков в числе > 15, то выводим значение в виде строки, а не числа)*/
macro DL_GetPrintNumber(Num, SumPrecision:integer)
  var str;
  if(SumPrecision == 0)
    return Num;
  end;

  str = ДЛ_ЧислоCЗаданнойТочностью(Num, SumPrecision);
  if(КоличествоЗначащихРазрядов(str) > 15)
    return StrSubst ( str, ".", GetLocaleDecimalSeparator() );
  end;

  return double(Num);
end;

 /* Получить название курса указанного типа. В случае ошибки возвращаем
   пустую строку. */
macro DL_GetRateTypeName( RateType )

  var rtype = TRecHandler("ratetype");

  if( not DL_GetRecHandler( rtype, "select T_TYPENAME from dratetype_dbt where T_TYPE = "+string(RateType)) )
     return "";
  end;

  return rtype.rec.TYPENAME;
end;

/*взять часть строки с From в количестве Len (если получившаяся часть Str меньше чем From+Len, то дополняется пробелами ' ' справа)*/
MACRO DL_PrintPartStr(Str:string,From:integer,Len:integer)
   var EmptyStr = "", PartStr = "";
   PartStr = mkstr(" ",len);
   if( strlen(Str) > 0 )
      PartStr = substr(Str,From,Len);
      if( strlen(PartStr) < Len )
         EmptyStr = mkstr(" ",Len-strlen(PartStr));
         PartStr = PartStr + EmptyStr;
      end;
   end;
   return PartStr;
END;

/*связь позиций с итогами*/
MACRO DL_DVTurnByPosition( POS, TURN, _StrForPos )

   var StrForPos = "";
   if( _StrForPos != null )
      StrForPos   = _StrForPos;
   else
      StrForPos   = "POS";
   end;

   if( POS != NULL )
      return " TURN.T_DEPARTMENT  = " + string(POS.DEPARTMENT)  + " AND " +
             " TURN.T_FIID        = " + string(POS.FIID)        + " AND " +
             " TURN.T_BROKER      = " + string(POS.BROKER)      + " AND " +
             " TURN.T_CLIENTCONTR = " + string(POS.CLIENTCONTR) + " AND " +
             " TURN.t_GenAgrID    = " + string(POS.GenAgrID) + " ";
   elif( TURN != NULL )
      return " " + StrForPos + ".T_DEPARTMENT  = " + string(TURN.DEPARTMENT)  + " AND " +
             " " + StrForPos + ".T_FIID        = " + string(TURN.FIID)        + " AND " +
             " " + StrForPos + ".T_BROKER      = " + string(TURN.BROKER)      + " AND " +
             " " + StrForPos + ".T_CLIENTCONTR = " + string(TURN.CLIENTCONTR) + " AND " +
             " " + StrForPos + ".t_GenAgrID    = " + string(TURN.GenAgrID) + " ";
   else
      return " TURN.T_DEPARTMENT  = " + StrForPos + ".T_DEPARTMENT  AND " +
             " TURN.T_FIID        = " + StrForPos + ".T_FIID        AND " +
             " TURN.T_BROKER      = " + StrForPos + ".T_BROKER      AND " +
             " TURN.T_CLIENTCONTR = " + StrForPos + ".T_CLIENTCONTR AND " +
             " TURN.t_GenAgrID    = " + StrForPos + ".t_GenAgrID ";
   end;

END;

/*связь биржевых сделок с позицией*/
MACRO DL_DVDealByPosition( pos, _StrForPos )

   var StrForPos = "";
   if( _StrForPos != null )
      StrForPos   = _StrForPos;
   else
      StrForPos   = "POS";
   end;

   if( pos != null )
      return " DEAL.T_DEPARTMENT  = " + string(POS.DEPARTMENT)  + " AND " +
             " DEAL.T_FIID        = " + string(POS.FIID)        + " AND " +
             " DEAL.T_BROKER      = " + string(POS.BROKER)      + " AND " +
             " DEAL.T_CLIENTCONTR = " + string(POS.CLIENTCONTR) + " AND " +
             " DEAL.t_GenAgrID    = " + string(POS.GenAgrID) + " ";
   else
      return " DEAL.T_DEPARTMENT  = " + StrForPos + ".T_DEPARTMENT  AND " +
             " DEAL.T_FIID        = " + StrForPos + ".T_FIID        AND " +
             " DEAL.T_BROKER      = " + StrForPos + ".T_BROKER      AND " +
             " DEAL.T_CLIENTCONTR = " + StrForPos + ".T_CLIENTCONTR AND " +
             " DEAL.t_GenAgrID    = " + StrForPos + ".t_GenAgrID ";
   end;

END;

/*является ли клиетом заданного вида обслуживания в заданном филиале*/
MACRO DL_IsClServKind( PartyID:integer, ServiceKind:integer, Department:integer )
  var sqltxt, sqlClient, ClientRNum;

  if( PartyID == -1 )
     return true;
  end;

  sqltxt = "select count(1) RNum from dclient_dbt " +
           " where t_PartyID = ? " +
           "   and t_ServiceKind = ? "+
           "   and t_Branch = 0";

  if( (Department != null) and (Department>0) )
     sqltxt = sqltxt + "   and t_Department = " +string(Department);
  end;

  sqlClient = RSDCommand(sqltxt);

  sqlClient.addParam("", RSDBP_IN, PartyID);
  sqlClient.addParam("", RSDBP_IN, ServiceKind);
  sqlClient.execute();

  ClientRNum = TRsbDataSet(sqlClient);
  if( ClientRNum.MoveNext() )
     if( ClientRNum.RNum > 0 )
        return true;
     end;
  end;

  return false;
END;

/*списано с TS_StrCut*/
/* Привести строку к заданному размеру
     Str       [IN/OUT] - исходная строка
     Len       [IN]     - требуемая длина
     FillSymbol[IN]     - символ-заполнитель до нужной длины, если строка короче
     LeftAlignment[IN]  - если TRUE, то выравнивние по левому краю (по умолчанию по правому)
*/
MACRO DL_StrCut( Str:STRING, Len:INTEGER, FillSymbol:STRING, LeftAlignment:BOOL )
   var SourseLen;

   Str       = trim( Str );
   SourseLen = strlen( Str );

   if( LeftAlignment == null ) /*по умолчанию выравнивание по правому краю*/
      LeftAlignment = false;
   end;

   if( FillSymbol == null ) /*по умолчанию заполнить нулями*/
      FillSymbol = "0";
   end;

   if( SourseLen >= Len )
      if( LeftAlignment ) /*обрезаем справа (последние (SourseLen-Len) символов*/
         Str = SubStr( Str, 1, Len );
      else /*обрезаем слева (первые (SourseLen-Len) символов) */
         Str = SubStr( Str, (SourseLen-Len+1), Len );
      end;
   else
      if( LeftAlignment ) /*дополняем заданным символом справа*/
         Str = Str + mkstr( FillSymbol, Len-SourseLen );
      else /*дополняем заданным символом слева*/
         Str = mkstr( FillSymbol, Len-SourseLen ) + Str;
      end;
   end;

   return Str;
END;

MACRO КОРРЕСП_ГЛАВА_Г():BOOL
  VAR ErrCode = 0;
  VAR RetVal:bool = true;
  GetRegistryValue( "COMMON\\КОРРЕСП_ГЛАВА_Г", V_BOOL, RetVal, ErrCode );
  if( ErrCode != 0 )
     RetVal = true;
  end;
  return RetVal;
END;

/* Найти страну по трехбуквенному латинскому коду и вернуть буфер записи.
   В случае ошибки - пустой буфер. */
macro DL_GetCountryByCodeLat3( CodeLat3, SayError, BreakProg )
   var
      Country = TRecHandler( "country" ), Mes = "";

   if( not DL_GetRecHandler(Country,"select * from dcountry_dbt where t_CodeLat3 = '"+string(CodeLat3)+"'") )
      if( SayError )
         Mes =  "Не могу получить страну по коду (CodeLat3 = "
                     + String(CodeLat3) + ")";
         if( BreakProg )
            RunError( Mes );
         else
            MsgBox( Mes );
         end;
      end;
   end;

   return Country.rec;
end;
/*получить ID центробанка России*/
private var DL_OurCentralBankID = NULL;
macro DL_GetOurCentralBankID()
  if( DL_OurCentralBankID == NULL )
     DL_OurCentralBankID = -1;
     var sqltxt = RSDCommand("select party.t_PartyID "+
                             "  from dparty_dbt party, dpartyown_dbt own " +
                             " where party.t_PartyID = own.t_PartyID " +
                             "   and own.t_PartyKind = ? "+
                             "   and party.t_NotResident = chr(0) "+
                             " order by party.t_PartyID desc"
                            );

     sqltxt.addParam("", RSDBP_IN, PTK_CENTRBANK);
     sqltxt.execute();

     var DataSql = TRsbDataSet(sqltxt);

     if( DataSql.MoveNext() )
        DL_OurCentralBankID = DataSql.PartyID;
     end;
  end;
  return DL_OurCentralBankID;
end;

/*получить ID федерального казначейства*/
private var DL_ExchequerID = NULL;
macro DL_GetExchequerID()
  if( DL_ExchequerID == NULL )
     DL_ExchequerID = -1;
     var sqltxt = RSDCommand("select party.t_PartyID "+
                             "  from dparty_dbt party " +
                             " where party.t_NotResident != 'X' "+
                             "   and party.t_LegalForm = 1 " +
                             "   and exists(SELECT attr.* " +
                             "                FROM dobjatcor_dbt attr " +
                             "               WHERE attr.t_ObjectType = ? " +
                             "                 AND attr.t_GroupID = 1 " + 
                             "                 AND attr.t_Object = LPAD(party.T_PARTYID, 10, '0') " +
                             "                 AND attr.t_AttrID = NVL((select objattr.t_AttrID from dobjattr_dbt objattr " +
                             "                                           where objattr.t_ObjectType = attr.t_ObjectType " + 
                             "                                             and objattr.t_GroupID = attr.t_GroupID " +
                             "                                             and objattr.t_NumInList = '18'),-1) " +
                             "             ) " +
                             " order by party.t_PartyID desc"
                            );

     sqltxt.addParam("", RSDBP_IN, OBJTYPE_PARTY);
     sqltxt.execute();

     var DataSql = TRsbDataSet(sqltxt);

     if( DataSql.MoveNext() )
        DL_ExchequerID = DataSql.PartyID;
     end;
  end;
  return DL_ExchequerID;
end;

macro DL_GetPartyKindNames( PartyKind:integer, Name:@string, Name2:@string )
  var ResFind = false;

  if( PartyKind > 0 )
     var sqltxt = RSDCommand("select t_Name, t_Name2 "+
                             "  from dptkind_dbt " +
                             " where t_PartyKind = ? "
                            );

     sqltxt.addParam("", RSDBP_IN, PartyKind);
     sqltxt.execute();

     var DataSql = TRsbDataSet(sqltxt);

     if( DataSql.MoveNext() )
        Name  = DataSql.Name;
        Name2 = DataSql.Name2;
        ResFind = true;
     end;
  end;

  return ResFind;
end;

/*заполне но ли примечание вида по сделке*/
macro IsFilledNoteByDeal(DealID:integer,NoteKind:integer)

  var sqltxt = RSDCommand( " select count(1) as NRec " +
                           "   from dnotetext_dbt " +
                           "  where t_Objecttype = ? " +
                           "    and t_DocumentID = LPAD( ?, 34, '0' ) " +
                           "    and t_NoteKind = ?");

  sqltxt.addParam("", RSDBP_IN, OBJTYPE_SECDEAL);
  sqltxt.addParam("", RSDBP_IN, DealID);
  sqltxt.addParam("", RSDBP_IN, NoteKind);
  sqltxt.execute();

  var DataSql = TRsbDataSet(sqltxt);

  if( DataSql.MoveNext() and (SQL_ConvTypeInteger(DataSql.NRec) > 0 ) )
     return true;
  end;

  return false;
end;

macro DL_GetDocKindName(DocKind)
  var Name = "";
  var DataSet= TRsbDataSet("select t_name from doprkdoc_dbt where t_dockind = " + DocKind);
  if(DataSet.MoveNext())
     Name = DataSet.Name;
  end;
  return Name;
end;

/* Получить ФИО лица с правом первой или второй подписи */
macro DL_GetFirstPerson(Person, Post:@STRING, Name:@STRING )
var pt       = TRecHandler ("party.dbt"),
    officer  = TBfile("officer.dbt");

 officer.KeyNum = Person; /* 1-е Person искать по первому ключу, 2-е по второму */
 if(Person == 1)
   officer.rec.IsFirstPerson = "X";
 elif(Person == 2)
   officer.rec.IsSecondPerson= "X";
 end;

 officer.rec.PartyID = {OurBank};
 if ( (officer.GetEQ) AND ( not ПолучитьСубъекта (officer.rec.PersonID, pt)) )
   Name = pt.rec.Name; /* ФИО */
   Post = officer.rec.Post; /* Должность */
 end;

END;

macro DL_GetOkatoCode(pt:variant,OnDate:Date)
   var party = TRecHandler ("party.dbt");
   var okatoCode, lenght_okato_code = 5;

   if( ValType(pt)==V_INTEGER )
      ПолучитьСубъекта(pt, party);
   elif( ValType(pt)==V_GENOBJ )
      party = pt;
   end;

   getMainObjAttr (null,
                   OBJTYPE_PARTY,
                   uniID(party, OBJTYPE_PARTY),
                   12,
                   null,
                   null,
                   okatoCode,
                   OnDate);

   var dataSet;
   if (okatoCode == null)
       dataSet =  TRsbDataSet( "    SELECT CASE"
                        + "\n" + "              WHEN t_OKATO = CHR(1)"
                        + "\n" + "              THEN NULL"
                        + "\n" + "              ELSE t_OKATO"
                        + "\n" + "         END t_OKATO"
                        + "\n" + "    FROM dadress_dbt"
                        + "\n" + "   WHERE t_type = 1"
                        + "\n" + "     AND t_partyid = " + party.rec.partyId);

      dataSet.moveNext();
      okatoCode = dataSet.t_OKATO;
   end;

   okatoCode = suBstr(okatoCode, 1, lenght_okato_code);

   if ((okatoCode != NULL) AND
       (okatoCode != "")   AND
       (strLen(okatoCode) < lenght_okato_code))

       okatoCode = mkStr("0", lenght_okato_code - strLen(okatoCode)) + okatoCode;
   end;

   return DL_IIF(okatoCode==null,"",okatoCode);
end;

macro DL_ДатаВключенияВСРО():string
  var ErrCode = 0, Str = "";

  GetRegistryValue( "SECUR\\ДАТА ВКЛЮЧЕНИЯ В СРО", V_STRING, Str, ErrCode );
  if( ErrCode != 0 )
     MsgBox( "Ошибка при получении значения настройки \"SECUR\\ДАТА ВКЛЮЧЕНИЯ В СРО\"");
  end;

  if( Str == "00.00.0000" ) // значение по умолчанию обнулим, что бы в отчете не выводилось
     Str = "";
  end;

  return Str;
end;

macro DL_НаименованиеСРО():string
  var ErrCode = 0, Str = "";

  GetRegistryValue( "SECUR\\НАИМЕНОВАНИЕ СРО", V_STRING, Str, ErrCode );
  if( ErrCode != 0 )
     MsgBox( "Ошибка при получении значения настройки \"SECUR\\НАИМЕНОВАНИЕ СРО\"");
  end;

  return Str;
end;

MACRO DL_GetISOCurr(NumberCode:integer):string
  var Code = "";
  var sqltxt = RSDCommand(" select t_isocode " +
                          "   from disocur_dbt " +
                          "  where t_numbercode = ?");
  sqltxt.addParam("", RSDBP_IN, NumberCode);
  sqltxt.execute();

  var DataSet = TRsbDataSet(sqltxt);
  if( DataSet.MoveNext() )
     Code = DataSet.isocode;
  end;
  return Code;
end;
