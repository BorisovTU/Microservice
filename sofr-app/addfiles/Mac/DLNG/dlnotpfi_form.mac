/*
$Name:        dlnotpfi_form.mac
$Module:      Ядро Securities
$Description: Отчет <Внутренний учет>/ <Отчеты>/ <Регистры ВУ> / <Реестр ВУ сделок ПУРЦБ> / <Сделки с финансовыми активами (кроме ПФИ)> - форма ввода данных
*/
import "DlRepForm_h.mac", "dlmisc.mac";

// Номера полей в форме отчета
CONST
      PNFLD_DLNOTPFI_Dprt         = 0, 
      PNFLD_DLNOTPFI_DprtName     = 1,
 
      PNFLD_DLNOTPFI_BODeals      = 2, 
      PNFLD_DLNOTPFI_VADeals      = 3, 
      PNFLD_DLNOTPFI_DVDeals      = 4,
 
      PNFLD_DLNOTPFI_IsOwn        = 5,
 
      PNFLD_DLNOTPFI_IsClient     = 6, 
      PNFLD_DLNOTPFI_ClientCode   = 7, 
      PNFLD_DLNOTPFI_ClientName   = 8, 
      PNFLD_DLNOTPFI_Cl_Form_Sign = 9, 
      PNFLD_DLNOTPFI_Cl_Form      = 10,
      PNFLD_DLNOTPFI_Cl_Kind_Sign = 11,
      PNFLD_DLNOTPFI_Cl_Kind      = 12,
      PNFLD_DLNOTPFI_Cl_NotRes    = 13,

      PNFLD_DLNOTPFI_IsMarket     = 14,
      PNFLD_DLNOTPFI_MarketCode   = 15,
      PNFLD_DLNOTPFI_MarketName   = 16,
      PNFLD_DLNOTPFI_IsOutMkt     = 17,
      PNFLD_DLNOTPFI_IsBroker     = 18,
      PNFLD_DLNOTPFI_BrokerCode   = 19,
      PNFLD_DLNOTPFI_BrokerName   = 20,

      PNFLD_DLNOTPFI_FI_Kinds     = 21,
      PNFLD_DLNOTPFI_FI_AvoirKind = 22,
      PNFLD_DLNOTPFI_FI_Qualified = 23,
      PNFLD_DLNOTPFI_EmiCode      = 24,
      PNFLD_DLNOTPFI_EmiName      = 25,
      PNFLD_DLNOTPFI_AvrCode      = 26,
      PNFLD_DLNOTPFI_AvrName      = 27,

      PNFLD_DLNOTPFI_DateBegin    = 28,
      PNFLD_DLNOTPFI_DateEnd      = 29,

      PNFLD_DLNOTPFI_IsEveryDate  = 30,

      PNFLD_DLNOTPFI_DealClosed   = 31,
      PNFLD_DLNOTPFI_DealReadied  = 32,
      PNFLD_DLNOTPFI_DealAll      = 33,

      PNFLD_DLNOTPFI_EXCEL        = 34;
                                    
PRIVATE CONST /* Обработчики для нестандарных контролов */
      H_Cl_Form_Sign  = 101,        
      H_Cl_Form       = 102,
      H_Cl_Kind_Sign  = 103,
      H_Cl_Kind       = 104,
      H_Cl_NotRes     = 105,
      H_EXCHANGE      = 106,
      H_IsOutMkt      = 107,
      H_IsBROKER      = 108,
      H_BROKER        = 109,
      H_EXCHANGE_NAME = 110,
      H_BROKER_NAME   = 111,
      H_FI_KIND       = 112,  
      H_QUALIFIED     = 113,
      H_RADIO_CLOSED  = 114, 
      H_RADIO_OPENED  = 115, 
      H_RADIO_ALL     = 116,
      H_IsEXCHANGE    = 117,
      H_OUTEXCHANGE   = 118;
      
PRIVATE CONST SelCodeKind = 1; // Общий параметр для листалок Бирж,Субьектов,Брокеров,Клиентов

PRIVATE MACRO DL_ListAvoiriss( WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT, FiName:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( 0, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     FiName       = Fininstr.rec.Name;
     return true;
  end;
  return false;
END;

/*Листалка avrkinds.dbt*/
PRIVATE MACRO ListAvrKinds( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, IsBO:bool, IsVA:bool ):BOOL
  VAR Choice, Cond = "1=1", flag = false; 

  if( IsBO )
     Cond = " (T_ISemissive = 'X' ";
     flag = true;
  end;

  if( IsVA )
     if( flag )
        Cond = Cond + " or ";
     else
        Cond = Cond + " and ";
     end;
     Cond = Cond + " T_AvoirKind in("+AVOIRISSKIND_BILL+","+AVOIRISSKIND_BANKCERT+","+AVOIRISSKIND_DEPOSIT_CERTIFICATE+") ";
  end;

  if( flag )
     Cond = Cond + ")";
  end;

  DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, X, Y, Cond );

END;

/*Код Ц/Б*/
MACRO DLUSR_FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Fininstr = TRecHandler( "fininstr.dbt" );
  var Avoiriss = TRecHandler( "avoiriss.dbt" );
  VAR AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, IssuerID;
  VAR Fi_Kind = - 1;
  var FiName = "";
  var period_end_date = pThis.GetFieldValue( PNFLD_DLNOTPFI_DateEnd );

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( (FldShowValue == STR_ALLVALUE) OR (FldShowValue == "") )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) )
           MsgBox( "Неверное значение поля." );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;
        FldRealValue = Fininstr.rec.FIID;

        pThis.SetLinkedValue( DL_PNFLD_AVRKIND, Fininstr.rec.AvoirKind );
        pThis.SetLinkedValue( DL_PNFLD_AVRISSUER, FI_GetIssuerOnDate( Fininstr, period_end_date) );

     end;

     pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( DL_PNFLD_AVRNAME, "");
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     Fi_Kind = pThis.GetFieldValue( PNFLD_DLNOTPFI_FI_Kinds );

     if( Fi_Kind == FIKIND_AVOIRISS ) 
        AvrKind = pThis.GetFieldValue( PNFLD_DLNOTPFI_FI_AvoirKind );

        if( (AvrKind != NULL) AND (AvrKind > 0) )
           WhereCond = WhereCond + " AND RSB_FIInstr.FI_AvrKindsEQ( 2, " + AvrKind + ", t.t_AvoirKind ) = 1 ";
        end;
     
        IssuerID = pThis.GetFieldValue( PNFLD_DLNOTPFI_EmiCode );

        if( (IssuerID != NULL) AND (IssuerID > 0) )
          WhereCond = WhereCond + " AND RSI_RSB_FIInstr.FI_GetIssuerOnDate( t.T_FIID, " + GetSQLDate(period_end_date) + "  ) = " + IssuerID;
        end;
     else
        WhereCond = WhereCond + " AND t.T_FI_KIND = " + Fi_Kind;
     end;

     if( Fi_Kind == FIKIND_AVOIRISS ) 
        DL_ListAvoiriss( WhereCond, AddTable, @FldShowValue, @FldRealValue, @FiName );
        pThis.SetLinkedValue( DL_PNFLD_AVRNAME, FiName);
     else
        if( ListFI (Fi_Kind, 0, Fininstr) )
           FldRealValue = Fininstr.rec.FIID;
           FldShowValue = Fininstr.rec.FI_CODE;
           pThis.SetLinkedValue(DL_PNFLD_AVRNAME, Fininstr.rec.Name);
        end;
     end;

  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_Exchange( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_EXCHANGE_NAME, "");
     else
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_EXCHANGE_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     Party = TRecHandler("party.dbt");
     if( ListPT( Party, SelCodeKind, "", PTLIST_MARKETPLASE, PTCK_CONTR) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_EXCHANGE_NAME, Party.rec.ShortName); 
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_Broker( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var ClientID = -1;
  ClientID = pThis.GetFieldValue( PNFLD_DLNOTPFI_ClientCode );
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_BROKER_NAME, "");
     else
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_BROKER_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     Party = TRecHandler("party.dbt");
     if (ClientID == 0)
       return CM_DEFAULT;
     end;
     if( ListPT( Party, ClientID, "", 31, PTCK_CONTR) == true ) //31 - PTLIST_BROKER
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_BROKER_NAME, Party.rec.ShortName); 
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO RadioСlosed( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if (FldShowValue == SET_CHAR)
       pThis.SetLinkedValue( H_RADIO_OPENED, "");
       pThis.SetLinkedValue( H_RADIO_ALL, "");
     end;
     if (FldRealValue != FldShowValue)
      FldShowValue = FldRealValue;
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != SET_CHAR )
           FldShowValue = FldRealValue = SET_CHAR;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO RadioOpened( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if (FldShowValue == SET_CHAR)
       pThis.SetLinkedValue( H_RADIO_CLOSED, "");
       pThis.SetLinkedValue( H_RADIO_ALL, "");
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != SET_CHAR )
           FldShowValue = FldRealValue = SET_CHAR;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO RadioAll( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if (FldShowValue == SET_CHAR)
       pThis.SetLinkedValue( H_RADIO_CLOSED, "");
       pThis.SetLinkedValue( H_RADIO_OPENED, "");
     end;
      
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != SET_CHAR )
           FldShowValue = FldRealValue = SET_CHAR;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

/*Переключатель*/
PRIVATE MACRO DLUSR_FldProc_BODeals( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     pThis.SetLinkedValue(H_FI_KIND);
  elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_IsMarket );
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_IsBroker );
     else
        if( (pThis.GetFieldValue( PNFLD_DLNOTPFI_DVDeals ) == SET_CHAR) or (pThis.GetFieldValue( PNFLD_DLNOTPFI_VADeals ) == SET_CHAR) )
           FldShowValue = FldRealValue = "";
        else
           return CM_IGNORE;
        end;
     end;

     if( (FldRealValue == "") and (pThis.GetFieldValue( PNFLD_DLNOTPFI_DVDeals ) != SET_CHAR) and (pThis.GetFieldValue( PNFLD_DLNOTPFI_VADeals ) == SET_CHAR) )
        DisableFields( pThis.Data(), PNFLD_DLNOTPFI_IsMarket );
        pThis.SetLinkedValue( H_IsEXCHANGE );
        DisableFields( pThis.Data(), PNFLD_DLNOTPFI_IsBroker );
        pThis.SetLinkedValue( H_IsBroker );
     end;
  end;
  return CM_DEFAULT;
END;

/*Переключатель*/
PRIVATE MACRO DLUSR_FldProc_DVDeals( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     pThis.SetLinkedValue(H_FI_KIND);
  elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_IsMarket );
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_IsBroker );
     else
        if( (pThis.GetFieldValue( PNFLD_DLNOTPFI_BODeals ) == SET_CHAR) or (pThis.GetFieldValue( PNFLD_DLNOTPFI_VADeals ) == SET_CHAR) )
           FldShowValue = FldRealValue = "";
        else
           return CM_IGNORE;
        end;
     end;

     if( (FldRealValue == "") and (pThis.GetFieldValue( PNFLD_DLNOTPFI_BODeals ) != SET_CHAR) and (pThis.GetFieldValue( PNFLD_DLNOTPFI_VADeals ) == SET_CHAR) )
        DisableFields( pThis.Data(), PNFLD_DLNOTPFI_IsMarket );
        pThis.SetLinkedValue( H_IsEXCHANGE );
        DisableFields( pThis.Data(), PNFLD_DLNOTPFI_IsBroker );
        pThis.SetLinkedValue( H_IsBroker );
     end;

    if ( FldRealValue != SET_CHAR )
        DisableFields( pThis.Data(), PNFLD_DLNOTPFI_FI_Kinds );
    else
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_FI_Kinds );
    end;
  end;
  return CM_DEFAULT;
END;

/*Переключатель*/
PRIVATE MACRO DLUSR_FldProc_VADeals( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
     else
        if( (pThis.GetFieldValue( PNFLD_DLNOTPFI_DVDeals ) == SET_CHAR) or (pThis.GetFieldValue( PNFLD_DLNOTPFI_BODeals ) == SET_CHAR) )
           FldShowValue = FldRealValue = "";
        else
           return CM_IGNORE;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

/*Переключатель*/
PRIVATE MACRO DLUSR_FldProc_IsOwn( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;
     else
        if( pThis.GetFieldValue( PNFLD_DLNOTPFI_IsClient ) == SET_CHAR )
           FldShowValue = FldRealValue = "";
        else
           return CM_IGNORE;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

/*Переключатель*/
PRIVATE MACRO DLUSR_FldProc_IsClient( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) and (Key == KEY_SPACE) )
     if( FldRealValue != SET_CHAR )
        FldShowValue = FldRealValue = SET_CHAR;

        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_ClientCode );
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_Cl_Form_Sign );
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_Cl_Kind_Sign );
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_Cl_NotRes );
     else
        if( pThis.GetFieldValue( PNFLD_DLNOTPFI_IsOwn ) == SET_CHAR )
           FldShowValue = FldRealValue = "";

           pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE,-1);
           pThis.SetLinkedValue( H_Cl_Form_Sign);
           pThis.SetLinkedValue( H_Cl_Kind_Sign);
           pThis.SetLinkedValue( H_Cl_Form);
           pThis.SetLinkedValue( H_Cl_Kind);
           pThis.SetLinkedValue( H_Cl_NotRes);

           DisableFields( pThis.Data(), PNFLD_DLNOTPFI_ClientCode );
           DisableFields( pThis.Data(), PNFLD_DLNOTPFI_Cl_Form_Sign );
           DisableFields( pThis.Data(), PNFLD_DLNOTPFI_Cl_Kind_Sign );
           DisableFields( pThis.Data(), PNFLD_DLNOTPFI_Cl_NotRes );   
        else
           return CM_IGNORE;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

/*листалка клиентов по виду обслуживания*/
PRIVATE MACRO DLUSR_ListClientByServKind( ClientName:@string, FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, WhereCond:string ):BOOL
  VAR Choice; 
  VAR Select;

  Select = "select distinct prt.t_Name, prt.t_ShortName, " +
           "       partcode.t_Code, clt.t_PartyID " + 
           "  from dpartcode_dbt partcode, dclient_dbt clt, dparty_dbt prt " +
           " where clt.t_PartyID = partcode.t_PartyID " +
           "   and prt.t_PartyID = clt.t_PartyID " +
           "   and partcode.t_CodeKind = 1 " +
           "   and clt.t_Branch = 0 " + WhereCond;


  Choice = DL_Scroll(Select,
                     "Список клиентов", X, Y,
                     "t_Code","Код","t_Name","Наименование"
                    );

  if( Choice != NULL )
     FldRealValue = Choice.Value("t_PartyID"); 
     FldShowValue = Choice.Value("t_Code");
     ClientNAme = Choice.Value("t_ShortName");
  end;

  return (Choice != NULL);
END;

/*клиент БОЦБ\УВ\ПИ в заданном филиале*/
MACRO DLUSR_Client_BO_Dep( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var ClientName = "", ServCond = "", i = 0, flag = false, WhereCond = "";
  var ServKind = TArray(), IsBO = pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR), IsVA = pThis.GetLinkedValue(DL_PNFLD_NOT_EMISS_AVR),
      IsDV = pThis.GetLinkedValue(DL_PNFLD_BY_DERIV);
  var CL_Form_Sign, CL_Kind_Sign,  CL_Form = -1, CL_Kind = -1;
  var DepCode = pThis.GetLinkedValue( DL_PNFLD_DEPARTCODE );
 
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, "" );
     else
        if( not ПолучитьСубъекта( FldRealValue, Party ) )
            FldShowValue = ПолучитьКодСубъекта(FldRealValue, PTCK_CONTR);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;

     pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     if( DepCode > 0 )
        WhereCond = WhereCond + "   and clt.t_Department = "+string(DepCode);
     end;

     if( (IsBO != null) and (IsBO == SET_CHAR)  )
        ServKind[ServKind.Size] = PTSK_STOCKDL;
     end;

     if( (IsVA != null) and (IsVA == SET_CHAR) )
        ServKind[ServKind.Size] = PTSK_VEKSACC;
     end;

     if( (IsDV != null) and (IsDV == SET_CHAR) )
        ServKind[ServKind.Size] = PTSK_CM;
     end;

     if( (ServKind != null) and (ServKind.Size>0) )
        while( i < ServKind.Size )
           if( flag == true )
               ServCond = ServCond + ",";
           else
               flag = true;
           end;
   
           ServCond = ServCond + string(ServKind[i]);
           i = i + 1;
        end;
     end;

     if( ServCond != "" )/*здесь всегда будет задано что-то*/
        WhereCond = WhereCond + " and clt.t_ServiceKind in(" + ServCond + ")";
     end;

     CL_Form_Sign = pThis.GetFieldValue( PNFLD_DLNOTPFI_Cl_Form_Sign );
     CL_Form      = pThis.GetFieldValue( PNFLD_DLNOTPFI_Cl_Form );
     CL_Kind_Sign = pThis.GetFieldValue( PNFLD_DLNOTPFI_Cl_Kind_Sign );
     CL_Kind      = pThis.GetFieldValue( PNFLD_DLNOTPFI_Cl_Kind );

     if( (Cl_Form_Sign == "1") and (Cl_Form != -1) )/*здесь пока так*/
        WhereCond = WhereCond + " and prt.t_LegalForm = " + Cl_Form;
     elif( (Cl_Form_Sign == "2") and (Cl_Form != -1) )/*здесь пока так*/
        WhereCond = WhereCond + " and prt.t_LegalForm != " + Cl_Form;
     end;

     if( (Cl_Kind_Sign == "1") and (Cl_Kind != -1) )/*здесь пока так*/
        WhereCond = WhereCond + " and exists( select 1 from dpartyown_dbt own where own.t_Partyid = clt.t_PartyID and own.t_partykind = " + Cl_Kind + " )";
     elif( (Cl_Kind_Sign == "2") and (Cl_Kind != -1) )/*здесь пока так*/
        WhereCond = WhereCond + " and not exists( select 1 from dpartyown_dbt own where own.t_Partyid = clt.t_PartyID and own.t_partykind = " + Cl_Kind + " )";
     end;

     if( pThis.GetFieldValue( PNFLD_DLNOTPFI_Cl_NotRes ) == SET_CHAR )
        WhereCond = WhereCond + " and prt.t_NotResident = 'X' ";
     end;

     if(DLUSR_ListClientByServKind( @ClientName, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), WhereCond ))
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, ClientName );
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_Cl_Form( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldRealValue = -1;
     FldShowValue = "";
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
     DL_ListNA( 3400, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), NULL );
  end;
  return CM_DEFAULT;        
END;

PRIVATE MACRO DLUSR_Cl_Kind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT )
  if( Cmd == DLG_INIT )
     FldRealValue = -1;
     FldShowValue = "";
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    return DL_ComboBox( "select t_Name2, t_PartyKind from dptkind_dbt ",
                        "t_Name2", "t_PartyKind", 
                        @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY()
                      );
  end; 
  return CM_DEFAULT;        
END;

PRIVATE MACRO DL_FldProc_Sign( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, pos:INTEGER):INTEGER
  if((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
    if ( FldRealValue == 0)
        FldShowValue = "=";
        FldRealValue = "1";
        EnableFields( pThis.Data(), pos );

    elif(FldRealValue == 1)
        FldShowValue = "!";
        FldRealValue = "2";
        EnableFields( pThis.Data(), pos );
    else
        FldShowValue = "";
        FldRealValue = "0";
        DisableFields( pThis.Data(), pos );
    end;
  end;
  return CM_DEFAULT;        
END;

PRIVATE MACRO DLUSR_Cl_Sign_1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldRealValue = "0";
     FldShowValue = "";
     DisableFields( pThis.Data(), PNFLD_DLNOTPFI_Cl_Form );
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     DL_FldProc_Sign( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_DLNOTPFI_Cl_Form);
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_Cl_Sign_2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldRealValue = "0";
     FldShowValue = "";
     DisableFields( pThis.Data(), PNFLD_DLNOTPFI_Cl_Kind );
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     DL_FldProc_Sign( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_DLNOTPFI_Cl_Kind);
  end;
  return CM_DEFAULT;
END;

MACRO DLUSR_FldProc_FI_Kinds( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ret = null;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        if (    (pThis.GetFieldValue(PNFLD_DLNOTPFI_BODeals) == SET_CHAR)
            and (pThis.GetFieldValue(PNFLD_DLNOTPFI_DVDeals) != SET_CHAR))
           FldRealValue = FIKIND_AVOIRISS;
           FldShowValue = "Ценные бумаги";
        else
           FldShowValue = STR_ALLVALUE;
        end;
     end;
  elif( Cmd == DLG_CHANGEVALUE )

     if( FldRealValue == FIKIND_AVOIRISS )
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_FI_AvoirKind);
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_FI_Qualified);
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_EmiCode     );
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_AvrCode     );
     else
        DisableFields( pThis.Data(), PNFLD_DLNOTPFI_FI_AvoirKind);
        pThis.SetLinkedValue( DL_PNFLD_AVRKIND);
        DisableFields( pThis.Data(), PNFLD_DLNOTPFI_FI_Qualified);
        pThis.SetLinkedValue( H_QUALIFIED);
        DisableFields( pThis.Data(), PNFLD_DLNOTPFI_EmiCode     );
        pThis.SetLinkedValue( DL_PNFLD_AVRISSUER);

        if( FldRealValue != -1 )
           EnableFields( pThis.Data(), PNFLD_DLNOTPFI_AvrCode     );
        else
           DisableFields( pThis.Data(), PNFLD_DLNOTPFI_AvrCode     );
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE);
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
      var q  = "select KINDS.T_NAME NAME, KINDS.T_FI_KIND FI_KIND from dfikinds_dbt kinds where KINDS.T_FI_KIND in (1,2,3,6,7)";
      if ( (pThis.GetFieldValue(PNFLD_DLNOTPFI_DVDeals) == SET_CHAR) and
           (pThis.GetFieldValue(PNFLD_DLNOTPFI_IsOwn) == SET_CHAR)   and 
           (pThis.GetFieldValue(PNFLD_DLNOTPFI_IsClient) != SET_CHAR) )
         q = "select KINDS.T_NAME NAME, KINDS.T_FI_KIND FI_KIND from dfikinds_dbt kinds where KINDS.T_FI_KIND in (2,3,7)" ;
      end;
     ret = DL_ComboBox( q,
                        "NAME", "FI_KIND", 
                        @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY()
                      );
  end;
  return CM_DEFAULT;        
END;                              

PRIVATE MACRO DLUSR_FldProc_FI_AvoirKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var MenuValues   = TArray,
      ReturnValues = TArray,
      isOK, Choice, Query,AvrKindsDataSet;
  VAR Fininstr, AvrID;

  var IsBO = false, IsVA = false, IsDV = false;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        DL_GetAvrKindName( FIKIND_AVOIRISS, FldRealValue, null, @FldShowValue );
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

    if( pThis.GetFieldValue( PNFLD_DLNOTPFI_FI_Kinds ) == FIKIND_AVOIRISS )

       IsBO = pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR);
       IsVA = pThis.GetLinkedValue(DL_PNFLD_NOT_EMISS_AVR);
       IsDV = pThis.GetLinkedValue(DL_PNFLD_BY_DERIV);

       ListAvrKinds(@FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), (IsBO or IsDV), IsVA);
    end;
         
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_FI_Qualified( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 3144, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;

  return CM_DEFAULT;
END;

MACRO DLUSR_FldProc_IsMarket( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     if( FldRealValue == "X" )
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_MarketCode );
     else
        DisableFields( pThis.Data(), PNFLD_DLNOTPFI_MarketCode );
        pThis.SetLinkedValue( H_EXCHANGE );
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
           EnableFields( pThis.Data(), PNFLD_DLNOTPFI_MarketCode );
        else
           FldShowValue = FldRealValue = "";
           DisableFields( pThis.Data(), PNFLD_DLNOTPFI_MarketCode );
           pThis.SetLinkedValue( H_EXCHANGE );
        end;
     end;
  end;
  return CM_DEFAULT;
END;

MACRO DLUSR_FldProc_IsBroker( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     if( FldRealValue == "X" )
        EnableFields( pThis.Data(), PNFLD_DLNOTPFI_BrokerCode );
     else
        DisableFields( pThis.Data(), PNFLD_DLNOTPFI_BrokerCode );
        pThis.SetLinkedValue( H_BROKER );
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
           EnableFields( pThis.Data(), PNFLD_DLNOTPFI_BrokerCode );
        else
           FldShowValue = FldRealValue = "";
           DisableFields( pThis.Data(), PNFLD_DLNOTPFI_BrokerCode );
           pThis.SetLinkedValue( H_BROKER );
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
  end; 
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DL_NotPFI_Panel()

  PRIVATE MACRO Init()

   if (GetIdentProgram()==158) // KS 06.02.2020 Для ФИССиКО особые параметры запуска по умолчанию:
     AddFieldProc( 0, PNFLD_DLNOTPFI_Dprt,         DL_PNFLD_DEPARTCODE,          @DL_FldProc_Department_Cl_BO, -1);
     AddFieldProc( 0, PNFLD_DLNOTPFI_DprtName,     DL_PNFLD_DEPARTMENT,          @Default, -1);
                                                                                
     AddFieldProc( 0, PNFLD_DLNOTPFI_BODeals,      DL_PNFLD_EMISS_AVR,           @DLUSR_FldProc_BODeals, SET_CHAR );
     AddFieldProc( 0, PNFLD_DLNOTPFI_VADeals,      DL_PNFLD_NOT_EMISS_AVR,       @DLUSR_FldProc_VADeals, SET_CHAR );
     AddFieldProc( 0, PNFLD_DLNOTPFI_DVDeals,      DL_PNFLD_BY_DERIV,            @DLUSR_FldProc_DVDeals, SET_CHAR );
                                                                                 
     AddFieldProc( 0, PNFLD_DLNOTPFI_IsOwn,        DL_PNFLD_CHECKBOX,            @DLUSR_FldProc_IsOwn,   SET_CHAR );
     AddFieldProc( 0, PNFLD_DLNOTPFI_IsClient,     DL_PNFLD_CHECKBOX,            @DLUSR_FldProc_IsClient,SET_CHAR );
                                                  
     AddFieldProc( 0, PNFLD_DLNOTPFI_ClientCode,   DL_PNFLD_CLIENT_STOCKDL_CODE, @DLUSR_Client_BO_Dep );
     AddFieldProc( 0, PNFLD_DLNOTPFI_ClientName,   DL_PNFLD_CLIENT_STOCKDL_NAME, @DL_FldProc_Client_STOCKDL_Name );

     AddFieldProc( 0, PNFLD_DLNOTPFI_Cl_Form_Sign, H_Cl_Form_Sign,               @DLUSR_Cl_Sign_1 );
     AddFieldProc( 0, PNFLD_DLNOTPFI_Cl_Form     , H_Cl_Form     ,               @DLUSR_Cl_Form, null, 38, 14     );
     AddFieldProc( 0, PNFLD_DLNOTPFI_Cl_Kind_Sign, H_Cl_Kind_Sign,               @DLUSR_Cl_Sign_2 );
     AddFieldProc( 0, PNFLD_DLNOTPFI_Cl_Kind     , H_Cl_Kind     ,               @DLUSR_Cl_Kind, null,  38, 15    );
     AddFieldProc( 0, PNFLD_DLNOTPFI_Cl_NotRes,    H_Cl_NotRes,                  @DL_FldProc_CheckBox );

     AddFieldProc( 0, PNFLD_DLNOTPFI_IsMarket,     H_IsEXCHANGE,                 @DLUSR_FldProc_IsMarket, "");
     AddFieldProc( 0, PNFLD_DLNOTPFI_MarketCode,   H_EXCHANGE,                   @DLUSR_FldProc_Exchange);
     AddFieldProc( 0, PNFLD_DLNOTPFI_MarketName,   H_EXCHANGE_NAME,              @Default, "");
     AddFieldProc( 0, PNFLD_DLNOTPFI_IsOutMkt,     H_OUTEXCHANGE,                @DL_FldProc_CheckBox);
     AddFieldProc( 0, PNFLD_DLNOTPFI_IsBroker,     H_IsBROKER,                   @DLUSR_FldProc_IsBroker, SET_CHAR);
     AddFieldProc( 0, PNFLD_DLNOTPFI_BrokerCode,   H_BROKER,                     @DLUSR_FldProc_Broker);
     AddFieldProc( 0, PNFLD_DLNOTPFI_BrokerName,   H_BROKER_NAME,                @Default, "");
                                                   
     AddFieldProc( 0, PNFLD_DLNOTPFI_FI_Kinds,     H_FI_KIND,                    @DLUSR_FldProc_FI_Kinds, null, 35, 24);
                                                                                
     AddFieldProc( 0, PNFLD_DLNOTPFI_FI_AvoirKind, DL_PNFLD_AVRKIND,             @DLUSR_FldProc_FI_AvoirKind);
     AddFieldProc( 0, PNFLD_DLNOTPFI_FI_Qualified, H_QUALIFIED,                  @DLUSR_FldProc_FI_Qualified, null, 35, 26 );
                                                                                
     AddFieldProc( 0, PNFLD_DLNOTPFI_EmiCode,      DL_PNFLD_AVRISSUER,           @DL_FldProc_AvrIssuer);         
     AddFieldProc( 0, PNFLD_DLNOTPFI_EmiName,      DL_PNFLD_AVRISSUERNAME,       @Default, "");         
                                                                                
     AddFieldProc( 0, PNFLD_DLNOTPFI_AvrCode,      DL_PNFLD_AVRCODE,             @DLUSR_FldProc_AvrCode);         
     AddFieldProc( 0, PNFLD_DLNOTPFI_AvrName,      DL_PNFLD_AVRNAME,             @Default, "");         
                                                                                
     AddFieldProc( 0, PNFLD_DLNOTPFI_DateBegin,    DL_PNFLD_BEGINDATE,           @DL_FldProc_BeginDate, {curdate});
     AddFieldProc( 0, PNFLD_DLNOTPFI_DateEnd,      DL_PNFLD_ENDDATE,             @DL_FldProc_EndDate, {curdate});
     AddFieldProc( 0, PNFLD_DLNOTPFI_IsEveryDate,  DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox);
                                                                                
     AddFieldProc( 0, PNFLD_DLNOTPFI_DealClosed,   H_RADIO_CLOSED,               @RadioСlosed, SET_CHAR);
     AddFieldProc( 0, PNFLD_DLNOTPFI_DealReadied,  H_RADIO_OPENED,               @RadioOpened);
     AddFieldProc( 0, PNFLD_DLNOTPFI_DealAll,      H_RADIO_ALL,                  @RadioAll);

     /*только в excel*/
     DisableFields( This.Data(), PNFLD_DLNOTPFI_EXCEL );

     AddFieldProc( 0, PNFLD_DLNOTPFI_EXCEL,        DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox, SET_CHAR);
   else // KS Для БО ЦБ
     AddFieldProc( 0, PNFLD_DLNOTPFI_Dprt,         DL_PNFLD_DEPARTCODE,          @DL_FldProc_Department_Cl_BO, -1);
     AddFieldProc( 0, PNFLD_DLNOTPFI_DprtName,     DL_PNFLD_DEPARTMENT,          @Default, -1);
                                                                                
     AddFieldProc( 0, PNFLD_DLNOTPFI_BODeals,      DL_PNFLD_EMISS_AVR,           @DLUSR_FldProc_BODeals, SET_CHAR );
     AddFieldProc( 0, PNFLD_DLNOTPFI_VADeals,      DL_PNFLD_NOT_EMISS_AVR,       @DLUSR_FldProc_VADeals, UNSET_CHAR );
     AddFieldProc( 0, PNFLD_DLNOTPFI_DVDeals,      DL_PNFLD_BY_DERIV,            @DLUSR_FldProc_DVDeals, UNSET_CHAR );
                                                                                 
     AddFieldProc( 0, PNFLD_DLNOTPFI_IsOwn,        DL_PNFLD_CHECKBOX,            @DLUSR_FldProc_IsOwn,   UNSET_CHAR );
     AddFieldProc( 0, PNFLD_DLNOTPFI_IsClient,     DL_PNFLD_CHECKBOX,            @DLUSR_FldProc_IsClient,SET_CHAR );
                                                  
     AddFieldProc( 0, PNFLD_DLNOTPFI_ClientCode,   DL_PNFLD_CLIENT_STOCKDL_CODE, @DLUSR_Client_BO_Dep );
     AddFieldProc( 0, PNFLD_DLNOTPFI_ClientName,   DL_PNFLD_CLIENT_STOCKDL_NAME, @DL_FldProc_Client_STOCKDL_Name );

     AddFieldProc( 0, PNFLD_DLNOTPFI_Cl_Form_Sign, H_Cl_Form_Sign,               @DLUSR_Cl_Sign_1 );
     AddFieldProc( 0, PNFLD_DLNOTPFI_Cl_Form     , H_Cl_Form     ,               @DLUSR_Cl_Form, null, 38, 14     );
     AddFieldProc( 0, PNFLD_DLNOTPFI_Cl_Kind_Sign, H_Cl_Kind_Sign,               @DLUSR_Cl_Sign_2 );
     AddFieldProc( 0, PNFLD_DLNOTPFI_Cl_Kind     , H_Cl_Kind     ,               @DLUSR_Cl_Kind, null,  38, 15    );
     AddFieldProc( 0, PNFLD_DLNOTPFI_Cl_NotRes,    H_Cl_NotRes,                  @DL_FldProc_CheckBox );

     AddFieldProc( 0, PNFLD_DLNOTPFI_IsMarket,     H_IsEXCHANGE,                 @DLUSR_FldProc_IsMarket, SET_CHAR);
     AddFieldProc( 0, PNFLD_DLNOTPFI_MarketCode,   H_EXCHANGE,                   @DLUSR_FldProc_Exchange);
     AddFieldProc( 0, PNFLD_DLNOTPFI_MarketName,   H_EXCHANGE_NAME,              @Default, "");
     AddFieldProc( 0, PNFLD_DLNOTPFI_IsOutMkt,     H_OUTEXCHANGE,                @DL_FldProc_CheckBox, SET_CHAR);
     AddFieldProc( 0, PNFLD_DLNOTPFI_IsBroker,     H_IsBROKER,                   @DLUSR_FldProc_IsBroker, UNSET_CHAR);
     AddFieldProc( 0, PNFLD_DLNOTPFI_BrokerCode,   H_BROKER,                     @DLUSR_FldProc_Broker);
     AddFieldProc( 0, PNFLD_DLNOTPFI_BrokerName,   H_BROKER_NAME,                @Default, "");
                                                   
     AddFieldProc( 0, PNFLD_DLNOTPFI_FI_Kinds,     H_FI_KIND,                    @DLUSR_FldProc_FI_Kinds, null, 35, 24);
                                                                                
     AddFieldProc( 0, PNFLD_DLNOTPFI_FI_AvoirKind, DL_PNFLD_AVRKIND,             @DLUSR_FldProc_FI_AvoirKind);
     AddFieldProc( 0, PNFLD_DLNOTPFI_FI_Qualified, H_QUALIFIED,                  @DLUSR_FldProc_FI_Qualified, null, 35, 26 );
                                                                                
     AddFieldProc( 0, PNFLD_DLNOTPFI_EmiCode,      DL_PNFLD_AVRISSUER,           @DL_FldProc_AvrIssuer);         
     AddFieldProc( 0, PNFLD_DLNOTPFI_EmiName,      DL_PNFLD_AVRISSUERNAME,       @Default, "");         
                                                                                
     AddFieldProc( 0, PNFLD_DLNOTPFI_AvrCode,      DL_PNFLD_AVRCODE,             @DLUSR_FldProc_AvrCode);         
     AddFieldProc( 0, PNFLD_DLNOTPFI_AvrName,      DL_PNFLD_AVRNAME,             @Default, "");         
                                                                                
     AddFieldProc( 0, PNFLD_DLNOTPFI_DateBegin,    DL_PNFLD_BEGINDATE,           @DL_FldProc_BeginDate, {curdate});
     AddFieldProc( 0, PNFLD_DLNOTPFI_DateEnd,      DL_PNFLD_ENDDATE,             @DL_FldProc_EndDate, {curdate});
     AddFieldProc( 0, PNFLD_DLNOTPFI_IsEveryDate,  DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox);
                                                                                
     AddFieldProc( 0, PNFLD_DLNOTPFI_DealClosed,   H_RADIO_CLOSED,               @RadioСlosed);
     AddFieldProc( 0, PNFLD_DLNOTPFI_DealReadied,  H_RADIO_OPENED,               @RadioOpened, SET_CHAR);
     AddFieldProc( 0, PNFLD_DLNOTPFI_DealAll,      H_RADIO_ALL,                  @RadioAll);

     /*только в excel*/
     DisableFields( This.Data(), PNFLD_DLNOTPFI_EXCEL );

     AddFieldProc( 0, PNFLD_DLNOTPFI_EXCEL,        DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox, SET_CHAR);
   end;
  END;

  PRIVATE MACRO Check():BOOL
     if( (GetLinkedValue(H_IsEXCHANGE) == UNSET_CHAR) and (GetLinkedValue(H_OUTEXCHANGE) == UNSET_CHAR) and (GetLinkedValue(H_IsBROKER) == UNSET_CHAR) )
        msgbox("Необходимо задать тип отбираемых в отчет сделок");
        return false;
     end;
     return true;
  END;
  
  initDL_CPanel( "dlrep.lbr", "DLNOTPFI" ); 
END;