/*
$Name:        dltxrate.mac
$Module:      Ценные бумаги
$Description: Функции расчета значений ставки (API)
*/

import Проценты, "dlquery.mac", "dldlngfun.mac";

Const СТ_269_ПРИВЛЕЧЕНИЕ = 1,
      СТ_269_РАЗМЕЩЕНИЕ  = 2;

Const СТ_269_РЕЗИДЕНТ    = 1,
      СТ_269_НЕРЕЗИДЕНТ  = 2;

private const БЛИЖ_МЕНЬШИЙ = 1,
              БЛИЖ_БОЛЬШИЙ = 2;


macro DL_IsExistsTxRateCurByFIID(FIID:integer)
  var cmd, DataSet;

  cmd = RSDCommand( "select count(1) NRec "+
                    "  from ddltxrate_cur_dbt txrate "+             
                    " where txrate.t_fiid = ? "
                  );

  cmd.addParam( "", RSDBP_IN, FIID );
  cmd.execute();
  
  DataSet = TRsbDataSet( cmd );
  
  if( DataSet.MoveNext() and (SQL_ConvTypeInteger(DataSet.NRec) > 0) )
     return true;
  end;

  return false;
end;

macro DL_GetTxRateCurByPeriod(CalcDate:date, PeriodKind:integer, FIID:integer, Period:integer, pIsExistsRecs:bool, DlTxRate:TRecHandler)
  var cmd, DataSet, QueryTxt = "";

  DlTxRate.Clear();

  cmd = DL_RSDCommand();

  QueryTxt = "select txrate.* "+
             "  from ddltxrate_cur_dbt txrate "+             
             " where txrate.t_begdate = (select max(txrate_1.t_begdate) "+     
             "                             from ddltxrate_cur_dbt txrate_1 "+                
             "                            where txrate_1.t_begdate   <= ? "+               
             "                              and txrate_1.t_fiid       = txrate.t_fiid "+
             "                              and txrate_1.t_other_fiid =  txrate.t_other_fiid "+
             "                          ) ";

  cmd.addParam( CalcDate );

  if( not pIsExistsRecs )
     QueryTxt = QueryTxt+
             " and txrate.t_other_fiid = 'X'";
  else
     QueryTxt = QueryTxt+
             " and txrate.t_fiid = ? ";
     cmd.addParam( FIID );
  end;

  if( PeriodKind == БЛИЖ_МЕНЬШИЙ )
     QueryTxt = QueryTxt +
             "   and txrate.t_Period <= ? "+
             " order by txrate.t_Period desc";
  else
     QueryTxt = QueryTxt +
             "   and txrate.t_Period > ? "+
             " order by txrate.t_Period asc";
  end;

  cmd.addParam( Period );

  DataSet = cmd.execute(QueryTxt);
  
  if( DataSet.MoveNext() )
    DataSet.GetRecord().CopyTo( DlTxRate.rec );
    return true;
  end;

  return false;
end;

macro DL_GetTxRateNatOnDate(CalcDate:date, DlTxRate:TRecHandler)
  var cmd, DataSet;

  DlTxRate.Clear();

  cmd = RSDCommand( "select * "+
                    "  from ddltxrate_nat_dbt "+
                    " where t_begdate <= ? "+
                    " order by t_begdate desc"
                  );

  cmd.addParam( "", RSDBP_IN, CalcDate );
  cmd.execute();
  
  DataSet = TRsbDataSet( cmd );
  
  if(DataSet.MoveNext())
    DataSet.GetRecord().CopyTo( DlTxRate.rec );
    return true;
  end;

  return false;
end;

macro DL_GetDominIndexRate( FIID:integer, ADate:date, Error:@string )
   var RateDef = TRecHandler("ratedef");
   var CourceDbl = 0.0, CursDate = date(0,0,0);
   var sql, rsd;

   Error = "";

   ClearRecord(RateDef.rec);

   sql = RSDCommand(" SELECT rate.* "
                  + "   FROM dratedef_dbt rate "
                  + "  WHERE rate.t_OtherFI = ? "
                  + "    AND rate.t_IsDominant = 'X' "
                  + "    AND (rate.t_sincedate <= ? or "
                  + "         (SELECT count(1) "
                  + "            FROM dratehist_dbt h "
                  + "           WHERE h.t_rateid  = rate.t_rateid "
                  + "             AND h.t_sincedate <= ?  "
                  + "         ) > 0 "
                  + "        ) "
                  + "    AND ROWNUM = 1 "
                   );

   sql.addParam( "", RSDBP_IN, FIID );
   sql.addParam( "", RSDBP_IN, ADate );
   sql.addParam( "", RSDBP_IN, ADate );
   sql.execute();
                                                                                               
   rsd = TRsbDataSet(sql);
   if( rsd.MoveNext() )
      RateDef = rsd.GetRecord();
      if( (not ExecMacroFile("spserv.mac", "ConvSumDbl", CourceDbl, 1.0, ADate, FIID, RateDef.FIID, false, RateDef.Type, CursDate )) or (CursDate == date(0,0,0) ) )
          Error = "Не найден основной курс индекса с кодом \"" + ПолучитьКодФинИн(fiid) + "\" на " + DateToStr(ADate)+".";
      end;
   else
      Error = "Не найден основной курс индекса с кодом \"" + ПолучитьКодФинИн(fiid) + "\" на " + DateToStr(ADate)+".";
   end;

   return CourceDbl;
end;

macro DL_GetTxRateCurByFIID(CalcDate:date, FIID:integer, Period:integer, DlTxRate_Cur:TRecHandler, StRate:@double, Error:@string)

  var v_IsExistsRecs = DL_IsExistsTxRateCurByFIID(FIID);

  if( DL_GetTxRateCurByPeriod(CalcDate, БЛИЖ_МЕНЬШИЙ, FIID, Period, v_IsExistsRecs, DlTxRate_Cur) )
     StRate = DL_GetDominIndexRate( DlTxRate_Cur.rec.IndexID, CalcDate, @Error );
  else
     if( DL_GetTxRateCurByPeriod(CalcDate, БЛИЖ_БОЛЬШИЙ, FIID, Period, v_IsExistsRecs, DlTxRate_Cur) )
        StRate = DL_GetDominIndexRate( DlTxRate_Cur.rec.IndexID, CalcDate, @Error );
     else
        Error = "В справочнике предельных ставок для сделок в ин. валюте не найдена запись по валюте с кодом \""+ПолучитьКодФинИн(FIID)+"\" на дату "+DateToStr(CalcDate)+".";
     end;
  end;

  return DL_IIF(Error!="",false,true);

end;

/* Функция расчета предельного значения ставки
Параметры функции:
CalcDate - Дата расчета - дата, на которую рассчитывается предельная ставка (для договоров с фикси-рованной ставкой это дата привлечения/ размещения средств, для договоров с плавающей ставкой - дата признания доходов / расходов), 
Kind     - Вид средств - привлечение или размещение.
FIID     - Валюта - валюта долгового обязательства
Period   - Срок долгового обязательства  (параметр необязательный для рублевых долговых обяза-тельств) - количество календарных дней в процентном периоде, для которого рассчитывает-ся предельное значение. 
StRate   - возвращаемое значение ставки
Error    - текст ошибки
DealDate - Дата заключения (параметр необязательный для долговых обязательств в иностранной валюте)
CalcKind - Вид расчета - <резидент> / <нерезидент> (параметр необязательный для  долговых обяза-тельств в иностранной валюте и для рублевых обязательств, дата заключения которых ранее 01.01.2015)
*/
macro DL_GetTxRate_Limit(CalcDate:date, Kind:integer, FIID:integer, Period:integer, StRate:@double, Error:@string, DealDate:Date, CalcKind:integer)
  var DlTxRate_Cur = TRecHandler( "dltxrate_cur.dbt" );
  var DlTxRate_Nat = TRecHandler( "dltxrate_nat.dbt" );
  var cbkrt = TRecHandler( "cbkeyrate.dbt" ); //буфер записи с данными ключевой ставки

  var Diff = 0.0;
  var cmd, DataSet;
  var DiffMax = 0, DiffMin = 0;

  StRate = 0.0;
  Error = "";

  if( FIID == NATCUR )

     if(DealDate < date(1,1,2015))/*Если валюта долгового обязательства  рубли и дата заключения ранее 01.01.2015*/
        if (not СтавкаРефинансированияЦБ (NATCUR, CalcDate, StRate))
           Error = "Не найдена ставка рефинансирования ЦБ РФ на "+DateToStr(CalcDate)+".";
        else
           if( DL_GetTxRateNatOnDate( CalcDate, DlTxRate_Nat ) )
       
              if( Kind == СТ_269_ПРИВЛЕЧЕНИЕ )
                 Diff = SQL_ConvTypeSum(DlTxRate_Nat.rec.max_diff);
              else
                 Diff = SQL_ConvTypeSum(DlTxRate_Nat.rec.min_diff);
              end;
       
              StRate = StRate * Diff / 100;
       
           else
              StRate = 0.0;
              Error = "В справочнике предельных ставок для сделок в нац. валюте не найдена запись на дату "+DateToStr(CalcDate)+".";
           end;
        end;
     else/*Если валюта долгового обязательства  рубли и дата заключения не ранее 01.01.2015*/
        if( (CalcDate >= date(1,1,2015)) and (CalcDate <= date(31,12,2015)) )/*для дат расчета в периоде  с 1 января по 31 декабря 2015 года*/
           if( CalcKind == СТ_269_РЕЗИДЕНТ )
              DiffMin = 0;
              DiffMax = 180;
           else
              DiffMin = 75;
              DiffMax = 180;
           end;
        elif( CalcDate >= date(1,1,2016) )/*для дат расчета, начиная с 1 января 2016 года*/
           DiffMin = 75;
           DiffMax = 125;
        else/*невозможный случай*/
           Error = "Дата расчета предельной ставки "+DateToStr(CalcDate)+" не может быть меньше даты заключения сделки "+DateToStr(DealDate)+".";
           return false;
        end;

        if( Kind == СТ_269_РАЗМЕЩЕНИЕ )
           Diff = DiffMin;
        else
           Diff = DiffMax;
        end;

        /*Для вида расчета <нерезидент>,  даты расчета, входящей в 2015 год,  и  вида средств = размещение  - предельная ставка рассчитывается 
          как  ставка рефинансирования ЦБ РФ, действующая на дату расчета, умноженная на отклонение и деленная на 100*/
        if( (CalcKind == СТ_269_НЕРЕЗИДЕНТ) and (CalcDate >= date(1,1,2015)) and (CalcDate <= date(31,12,2015)) and (Kind == СТ_269_РАЗМЕЩЕНИЕ) )
           if (not СтавкаРефинансированияЦБ (NATCUR, CalcDate, StRate))
              Error = "Не найдена ставка рефинансирования ЦБ РФ на "+DateToStr(CalcDate)+".";
           else
              StRate = StRate * Diff / 100;
           end;
        else/*Для всех остальных условий предельная ставка рассчитывается как  ключе-вая ставка ЦБ РФ, действующая на дату расчета, умноженная на отклоне-ние и деленная на 100*/
           if (КлючеваяСтавкаЦБ(NATCUR, CalcDate, cbkrt) != 0)
              Error = "Не найдена ключевая ставка ЦБ РФ на "+DateToStr(CalcDate)+".";
           else
              StRate = cbkrt.rec.Rate * Diff / 100;
           end;
        end;
     end;

  else

     if( DL_GetTxRateCurByFIID(CalcDate, FIID, Period, DlTxRate_Cur, @StRate, @Error) )
        if( Kind == СТ_269_ПРИВЛЕЧЕНИЕ )
           Diff = SQL_ConvTypeSum(DlTxRate_Cur.rec.max_diff);
        else
           Diff = SQL_ConvTypeSum(DlTxRate_Cur.rec.min_diff);
        end;
       
        StRate = StRate + Diff;
     end;

  end;

  return DL_IIF(Error!="",false,true);
end;

/* Функция расчета значения ставки
Параметры функции:
CalcDate - Дата расчета - дата, на которую рассчитывается предельная ставка (для договоров с фикси-рованной ставкой это дата привлечения/ размещения средств, для договоров с плавающей ставкой - дата признания доходов / расходов), 
FIID     - Валюта - валюта долгового обязательства
Period   - Срок долгового обязательства  (параметр необязательный для рублевых долговых обяза-тельств) - количество календарных дней в процентном периоде, для которого рассчитывает-ся предельное значение. 
StRate   - возвращаемое значение ставки
Error    - текст ошибки
Kind     - Вид средств - привлечение или размещение.
DealDate - Дата заключения (параметр необязательный для долговых обязательств в иностранной валюте)
CalcKind - Вид расчета - <резидент> / <нерезидент> (параметр необязательный для  долговых обяза-тельств в иностранной валюте и для рублевых обязательств, дата заключения которых ранее 01.01.2015)
*/
macro DL_GetTxRate(CalcDate:date, FIID:integer, Period:integer, StRate:@double, Error:@string, Kind:integer, DealDate:Date, CalcKind:integer)
  var DlTxRate_Cur = TRecHandler( "dltxrate_cur.dbt" );
  var DlTxRate_Nat = TRecHandler( "dltxrate_nat.dbt" );
  var cbkrt = TRecHandler( "cbkeyrate.dbt" ); //буфер записи с данными ключевой ставки

  var cmd, DataSet;

  StRate = 0.0;
  Error = "";

  if( FIID == NATCUR )
     if(DealDate < date(1,1,2015))/*Если валюта долгового обязательства  рубли и дата заключения ранее 01.01.2015*/
        if (not СтавкаРефинансированияЦБ (NATCUR, CalcDate, StRate))
           Error = "Не найдена ставка рефинансирования ЦБ РФ на "+DateToStr(CalcDate)+".";
        end;
     else/*Если валюта долгового обязательства  рубли и дата заключения не ранее 01.01.2015*/
        /*Для вида расчета <нерезидент>,  даты расчета, входящей в 2015 год,  и  вида средств = размещение  -  ставка = значение  ставки рефинансирования ЦБ РФ, действующее на дату расчета;*/
        if( (CalcKind == СТ_269_НЕРЕЗИДЕНТ) and (CalcDate >= date(1,1,2015)) and (CalcDate <= date(31,12,2015)) and (Kind == СТ_269_РАЗМЕЩЕНИЕ) )
           if (not СтавкаРефинансированияЦБ (NATCUR, CalcDate, StRate))
              Error = "Не найдена ставка рефинансирования ЦБ РФ на "+DateToStr(CalcDate)+".";
           end;
        else/*Для всех остальных условий ставка = значение ключевой ставки ЦБ РФ, действу-ющее на дату расчета;*/
           if (КлючеваяСтавкаЦБ(NATCUR, CalcDate, cbkrt) != 0)
              Error = "Не найдена ключевая ставка ЦБ РФ на "+DateToStr(CalcDate)+".";
           else
              StRate = cbkrt.rec.Rate;
           end;
        end;

     end;
  else
     DL_GetTxRateCurByFIID(CalcDate, FIID, Period, DlTxRate_Cur, @StRate, @Error);
  end;

  return DL_IIF(Error!="",false,true);
end;
