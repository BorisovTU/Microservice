/*
$Name:         dlobserve372.mac
$Module:       Ядро Securities
$Description:  Установка категории "Наблюдаемый клиент по 372-ФЗ" для клиентов из планировщика
*/

import BankInter, CtInter;
import DealsInter;
import dlquery, nptxfun;

private macro SetObserved(dat)
  var query, cmd, DataSet;
  var err = 0, LimTaxBase = 0;

  GetRegistryValue("SECUR\\НОБ НАБЛЮДАЕМЫЙ КЛИЕНТ 372-ФЗ", V_INTEGER, LimTaxBase, err);
  if(err != 0)
    WriteShedulerLog("Ошибка при определении значения настройки SECUR\\НОБ НАБЛЮДАЕМЫЙ КЛИЕНТ 372-ФЗ");
    return;
  else
    LimTaxBase = money(LimTaxBase);
  end;

  //1. Снять категорию у тех клиентов, у которых она установлена, но превышения нет
  query =   " select at.t_Object as ObjectID"
          + "   from dobjatcor_dbt at "
          + "  where at.t_ObjectType = " + OBJTYPE_PARTY
          + "    and at.t_GroupID = " + PARTY_ATTR_GROUP_OBSERVEDCLIENT372
          + "    and at.t_AttrID = 1" //ДА
          + "    and at.t_ValidFromDate <= ? "
          + "    and at.t_ValidToDate = " + GetSQLDate(date(31,12,9999))
          + "    and NOT EXISTS(select 1 "
          + "                     from dnptxobj_dbt nobj "
          + "                    where nobj.t_Date >= TRUNC(? ,'year')"
          + "                      and nobj.t_Date <= ? "
          + "                      and nobj.t_Kind IN ("+TXOBJ_BASEG1+","+TXOBJ_BASEG2+","+TXOBJ_BASEG3+","+TXOBJ_BASEG4+","+TXOBJ_BASEG5+","+TXOBJ_BASEG6+","+TXOBJ_BASEG7+","+TXOBJ_BASEG8+","+TXOBJ_BASEG9+")"
          + "                      and nobj.t_Client = TO_NUMBER(at.t_Object)"
          + "                    group by nobj.t_Kind "
          + "                   having SUM(nobj.t_Sum0) > ? " 
          + "                  ) ";

  cmd = DL_RSDCommand(query);
  
  cmd.AddParam(dat);
  cmd.AddParam(dat);
  cmd.AddParam(dat);
  cmd.AddParam(LimTaxBase);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    if (not ConnectObjAttr(null, OBJTYPE_PARTY, string(DataSet.ObjectID), PARTY_ATTR_GROUP_OBSERVEDCLIENT372, 2/*Нет*/, null, null, dat))
      WriteShedulerLog("Ошибка при сохранении категории \"Наблюдаемый клиент по 372-ФЗ\" для субъекта с идентификатором t_PartyID = " + int(DataSet.ObjectID));
    end;
  end;

  //2. Установить категорию у тех субъектов, у которых её нет, но появилось превышение
  query =   " select LPAD(q.t_Client, 10, '0') as ObjectID "
          + "   from (select distinct nobj.t_Client "
          + "           from dnptxobj_dbt nobj "
          + "          where nobj.t_Date >= TRUNC(? ,'year')"
          + "            and nobj.t_Date <= ? "
          + "            and nobj.t_Kind IN ("+TXOBJ_BASEG1+","+TXOBJ_BASEG2+","+TXOBJ_BASEG3+","+TXOBJ_BASEG4+","+TXOBJ_BASEG5+","+TXOBJ_BASEG6+","+TXOBJ_BASEG7+","+TXOBJ_BASEG8+","+TXOBJ_BASEG9+")"
          + "          group by nobj.t_Client, nobj.t_Kind "
          + "         having SUM(nobj.t_Sum0) > ? "  
          + "        ) q "
          + "  where NOT EXISTS(select 1 "
          + "                     from dobjatcor_dbt at "
          + "                    where at.t_ObjectType = " + OBJTYPE_PARTY
          + "                      and at.t_Object = LPAD(q.t_Client, 10, '0')"
          + "                      and at.t_GroupID = " + PARTY_ATTR_GROUP_OBSERVEDCLIENT372
          + "                      and at.t_AttrID = 1" //ДА
          + "                      and at.t_ValidFromDate <= ? "
          + "                      and at.t_ValidToDate = " + GetSQLDate(date(31,12,9999))
          + "                  ) ";

  cmd = DL_RSDCommand(query);
  
  cmd.AddParam(dat);
  cmd.AddParam(dat);
  cmd.AddParam(LimTaxBase);
  cmd.AddParam(dat);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    if (not ConnectObjAttr(null, OBJTYPE_PARTY, string(DataSet.ObjectID), PARTY_ATTR_GROUP_OBSERVEDCLIENT372, 1/*Да*/, null, null, dat))
      WriteShedulerLog("Ошибка при сохранении категории \"Наблюдаемый клиент по 372-ФЗ\" для субъекта с идентификатором t_PartyID = " + int(DataSet.ObjectID));
    end;
  end;

end;


var saveDialogFlag = SetDialogFlag( 0 );
if(IsShedulerRunning())
  SetObserved({curdate});
end;
SetDialogFlag(saveDialogFlag);