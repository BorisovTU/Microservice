/*
$Name:        mmcractacc.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Актуализация счетов"
*/
/*-------------------------------------------------------------------------
  Имя файла        : mmactacc.mac

  Описание         : макрос выполнения шага "Актуализация счетов"

  Программист      : Куперин М.В.

  Создан           : 16.06.2008
-------------------------------------------------------------------------*/
IMPORT mmcateg, mccatacf, mmlib, globals, mmcnst_j, PaymInter, mmlibr, mctplprm;

VAR Категории = TArray,
     Роли      = TArray,
     FirstDoc = NULL;

private RECORD contr(sfcontr);

PRIVATE MACRO ПолучитьТекушийНомерСчет(indx, Дата, Счет)
  var FindAccount = "";

  FirstDoc.IsActAccount(Категории[indx], FindAccount, true, Роли[indx], NULL, Дата, NULL);

  SetParm( 2, FindAccount );

  if (FindAccount == "") 
      return false;
  end;
  
  return true;
END;


PRIVATE MACRO АктуализироватьСчет(indx, Дата, Счет)
  var FindAccount = "";

  FirstDoc.ActualAccount(Категории[indx], FindAccount, NULL, Роли[indx], NULL, Дата, NULL);

  SetParm( 2, FindAccount );

  if (FindAccount == "")
      return false;
  end;
  
  return true;
END;

PRIVATE MACRO ЗаполнитьМассивы(Категория, Роль)
      if (Роль == NULL)
          Роль = FirstDoc.GetFIRoleByCategory(Категория, FirstDoc.GetLeg().PFI)
      end;

      Категории[Категории.size] = Категория; 
      Роли[Роли.size]           = Роль;  
END;

PRIVATE MACRO FillArray(contr)
    ЗаполнитьМассивы(tdr_expenses);
    ЗаполнитьМассивы(tdr_expcalcs);
    ЗаполнитьМассивы(tdr_marginP);
    ЗаполнитьМассивы(tdr_plodcreditP);
    ЗаполнитьМассивы(tdr_ploddepositP);
    ЗаполнитьМассивы(tdr_atodcreditP);
    ЗаполнитьМассивы(tdr_atoddepositP);
END;

MACRO ExecuteStep(Buffer, Document)
  var ExecDate = date(0, 0, 0),
      Account  = "",
      i        = 0, stat = 0;

  SetBuff( contr, Document );

  FirstDoc = MMFirstDoc(DL_SFCONTR, contr.ID, false);
  
  FillArray(contr); 

  ExecDate = contr.DateBegin;
  if (ExecDate > {curdate})
    ExecDate = {curdate};
  end;

  if (Категории.size > Роли.size)
      Роли.size = Категории.size;
  end;

  while (ValType(Категории(i)) == V_STRING)
      if (ПолучитьТекушийНомерСчет(i, ExecDate, Account) OR АктуализироватьСчет(i, ExecDate, Account))
          ;
      else
          mm_error("Не могу актуализировать счет для категории " + Категории(i));
          return 1;
      end;

      i = i + 1;
  end;

  return 0;
END;
NameMacroFile = "mmcractacc.mac";
