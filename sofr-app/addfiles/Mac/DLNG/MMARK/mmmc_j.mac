/*
$Name:        mmmc_j.mac
$Module:      МБ Кредиты
$Description: Модификация сделки
*/

import InsCarryDoc, CurrInter, Проценты, MmarkInter, mmlibr, mmcarry, mmcnst_j, mmplib;

PRIVATE FILE leg (dl_leg);
PRIVATE FILE pm ("pmpaym") key 1 write;
PRIVATE RECORD tick (dl_tick);

var НомерСделки;
var НомерТранша = 1;

PRIVATE VAR 
      new_paymentid,
      deal_pd,      
      IP_id = TArray,
      IPtype = TArray,
      PP_id = TArray,
      PPtype = TArray;

PRIVATE MACRO FindPaymByPurpose (DealID, purp, status)
var stat;
    pm.DocKind = DL_IBCDOC;
    pm.DocumentID = DealID;
    pm.Purpose = purp;
    pm.SubPurpose = 0;

    stat = GetGE(pm);
    while (stat)
        if((pm.DocKind == DL_IBCDOC) and
           (pm.DocumentID == DealID) and
           (pm.Purpose == purp) and
           (pm.PaymStatus == status))
            return true;
        end;
        stat = next(pm);
    end;
    return false;
end;


PRIVATE MACRO Move_OD (OD_New, new_paymentid, ExecDate)
    /*Если есть исполненный платеж по ОД надо перенести средства со старого ОД на новый*/
    if (FindPaymByPurpose(tick.dealid, PM_PURP_PRINC_RET, PM_READIED))
        if (not MM_MODIF_InsertDocsPrinc (deal_pd, ExecDate, pm, OD_New))
            return false;
        end;

        IPtype.size = PPtype.size = IP_id.size = PP_id.size = 0;

        IP_id[0] = pm.PaymentID;
        IPtype[0] = OPR_ID_REAL;
        PP_id[0] = new_paymentid;
        PPtype[0] = OPR_ID_REAL;

        if( not PaymentsKvit( IP_id, IPtype, PP_id, PPtype, null, PMLINK_KIND_MODIF) )
            mm_error( "Ошибка связывания платежей" );
            return false;
        end;
    end;

    return true;
END;

PRIVATE MACRO Move_Perc (new_paymentid, ExecDate)
    VAR stat, СчетПроцентов, СуммаПлатежа_всего = 0, sz;

    СчетПроцентов = MM_GetAutoPerc(leg.DealID, leg.LegID, PC_IBC_CON);
        IPtype.size = PPtype.size = IP_id.size = PP_id.size = 0;

    /*Если есть вынесенный на пролонгацию платеж по %% надо выполнить капитализацию на новый ОД*/
    /*на самом деле их может быть несколько */
    stat = FindPaymByPurpose(tick.dealid, PM_PURP_PERCENT, PM_READIED);
    while (stat and limiter (tick, pm) and
           (pm.Purpose == PM_PURP_PERCENT) and
           (pm.PaymStatus == PM_READIED))

        if (pm.FuturePayerAmount)
            if (not InsertPcPay( СчетПроцентов, pm.ValueDate, ExecDate, pm.FuturePayerAmount))
                mm_error ("Ошибка при оплате процентов");
                return false;
            end;

            СуммаПлатежа_всего = СуммаПлатежа_всего + pm.FuturePayerAmount;

            sz = IP_id.size;
            IP_id[sz] = pm.PaymentID;
            IPtype[sz] = OPR_ID_REAL;
            PP_id[sz] = new_paymentid;
            PPtype[sz] = OPR_ID_REAL;
        end;

        stat = next(pm);
    end;

    if (СуммаПлатежа_всего)
        if (not MM_MODIF_InsertDocsPerc ( deal_pd, ExecDate, СуммаПлатежа_всего, leg ))
            return false;
        end;

        if( not PaymentsKvit( IP_id, IPtype, PP_id, PPtype, null, PMLINK_KIND_MODIF) )
            mm_error( "Ошибка связывания платежей" );
            return false;
        end;    
    end;

    return true;
END;

PRIVATE MACRO GetAdjRestSale(deal_pd, ExecDate)
    VAR Остаток = 0, Счет = "";
    ПолучитьОстатокСчетаУчетЗнака(deal_pd, tdr_adjplR, Счет, 1, Остаток, ExecDate, NATCUR);
    if (Остаток == 0)
        Счет = "";
        ПолучитьОстатокСчетаУчетЗнака(deal_pd, tdr_adjplP, Счет, 1, Остаток, ExecDate, NATCUR);
    end;
    return -Остаток;
END;

PRIVATE MACRO GetAdjRestBuy(deal_pd, ExecDate)
    VAR Остаток = 0, Счет = "";
    ПолучитьОстатокСчетаУчетЗнака(deal_pd, tdr_adjatP, Счет, 1, Остаток, ExecDate, NATCUR);
    if (Остаток == 0)
        Счет = "";
        ПолучитьОстатокСчетаУчетЗнака(deal_pd, tdr_adjatR, Счет, 1, Остаток, ExecDate, NATCUR);
    end;
    return Остаток;
END;

PRIVATE MACRO AdjWriteOff(deal_pd, ExecDate)
    VAR КОРост = $0, at;
    // списание корректировки
    at = MM_Carry;
    at.FIIDPayer        = 
    at.FIIDReceiver     = NATCUR;
    at.date_carry       = ExecDate;
    at.PrimaryDoc       = deal_pd;
    at.Chapter          = 1;
    if (IsBuy(deal_pd.GetTick().DealGroup))
        КОРост = GetAdjRestBuy(deal_pd);
        if (КОРост > 0)
            at.CatPayer    = tdr_adjatR;
            at.CatReceiver = tdr_atodR(deal_pd.GetTick().TypeDoc);
            at.Ground      = "Списание корректировки, уменьшающей стоимость сделки привлечения МБК";
        else
            at.CatPayer    = tdr_atodP(deal_pd.GetTick().TypeDoc);
            at.CatReceiver = tdr_adjatP;
            at.Ground      = "Списание корректировки, увеличивающей стоимость сделки привлечения МБК";
        end;
    else
        КОРост = GetAdjRestSale(deal_pd);
        if (КОРост > 0)
            at.CatPayer    = tdr_plodP(deal_pd.GetTick().TypeDoc);
            at.CatReceiver = tdr_adjplP;
            at.Ground      = "Списание корректировки, увеличивающей стоимость сделки размещения МБК";
        else
            at.CatPayer    = tdr_adjplR;
            at.CatReceiver = tdr_plodR(deal_pd.GetTick().TypeDoc);
            at.Ground      = "Списание корректировки, уменьшающей стоимость сделки размещения МБК";
        end;
    end;
    at.Sum = КОРост;

    return at.carry();
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document)
    var НовыйОД = "", new_deal_pd, new_paymentid, stat, AsCategory = 0;

    SetBuff (tick,Document);
    DL_PrepareCarryBuffer (Buffer);
    leg.LegKind = LEG_KIND_DL_TICK;
    leg.DealID = tick.DealID;
    leg.LegID = 1;

    if (not GetEq(leg))
        mm_error ("Не найден транш сделки");
        return 1;
    end;

    deal_pd = MMFirstDoc(DL_IBCDOC, tick.DealID, true);
    new_deal_pd = MMFirstDoc(DL_IBCDOC, НомерСделки, true);

    if (not (deal_pd and new_deal_pd))
        return 1;
    end;

    if(not new_deal_pd.ActualCheckPeriod(tdr_mainrest, НовыйОД, NULL, new_deal_pd.GetFIRoleByCategory(tdr_mainrest, new_deal_pd.GetLeg().PFI), NULL, new_deal_pd.GetTick().DealDate, new_deal_pd.GetLeg().PFI))
        return 1;
    end;    

    if (not FindPaymByPurpose(НомерСделки, PM_PURP_PRINC_RET, PM_PREPARING))
        mm_error ("Не найден платеж по привлечению средств сделки с id=", НомерСделки);
        return 1;
    end;

    pm.Amount               =
    pm.BaseAmount           =
    pm.PayAmount            =
    pm.FuturePayerAmount    =
    pm.FutureReceiverAmount = 0;
    if (not Update(pm))
        msgbox ("Ошибка обновления платежа");
    end;

    new_paymentid = pm.paymentid;
    if (not Move_OD(НовыйОД, new_paymentid, new_deal_pd.tick.rec.DealDate))
        return 1;
    end;

    if (not FindPaymByPurpose(НомерСделки, PM_PURP_PERCENT, PM_PREPARING))
        mm_error ("Не найден платеж по привлечению средств сделки с id=", НомерСделки);
        return 1;
    end;

    new_paymentid = pm.paymentid;
    if (not Move_Perc(new_paymentid, new_deal_pd.tick.rec.DealDate))
        return 1;
    end;

    if (not AdjWriteOff(deal_pd, new_deal_pd.tick.rec.DealDate))
        return 1;
    end;

    MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(tick, OBJTYPE_MMARKDEAL), 7/*MN_AMORTIZED*/, {curdate}, $0);
    GetMainObjAttr(NULL, OBJTYPE_MMARKDEAL, UniID(tick, OBJTYPE_MMARKDEAL), 5, AsCategory, NULL, NULL, {curdate});
    IF ((AsCategory == 1) or (AsCategory == 2))
        MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(tick, OBJTYPE_MMARKDEAL), 10, {curdate}, $0);
    END;
    MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(tick, OBJTYPE_MMARKDEAL), 8/*MN_GROSS*/,     {curdate}, $0);

    return 0;
END;
