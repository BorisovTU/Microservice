/*
$Name:        mmhedg_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Амортизация корректировок хеджирования"
*/

IMPORT mmlibr, mmcarry, mmlib, mmlb_j, dv_categ, mmservop;

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0;

PRIVATE VAR ExecDate;

PRIVATE MACRO GetDVNDealAcc(DVNDealID, cat)
    var deal_pd, Account = "";
    deal_pd = DVFirstDocNDeal(DL_DVNDEAL, DVNDealID);
    deal_pd.OpenAccount(cat, Account, NULL, FIROLE_UNDEF, NULL, ExecDate, NATCUR);
    return Account;
END; 

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
   record deal( dl_tick );
   SetBuff( deal, Document );

   ID_Operation = IDOperation;
   ID_Step      = IDStep;

   VAR cmd, AdjAm, at, query, RsbData, FIRole, OprServDoc, deal_pd = MMFirstDoc(deal.BofficeKind, deal.DealID, true), ExecDate = {curdate};
   if (isServOpMode())
    OprServDoc = GetOprServDoc();
    ExecDate = OprServDoc.rec.CommDate;
   else
    IF (NOT GetDate(ExecDate, "Дата операции:"))
        RETURN 1;
    END;
   end;

   if (not isServOpMode())
        var query1 = "select 1 from DDLHDGRELATION_DBT where t_objid = " + deal.DealID + " and t_objdockind = " + deal.BOfficeKind;
        var RsbData1 = TRsbDataSet(query1);
        if (not RsbData1.MoveNext)
            mm_error("Сделка не участвовала в отношениях хеджирования");
            return 1;
        end;
   end;

   var ind_ihedg = 0, ind_zeropair = 0;
   query = "select t_id, t_instrid from DDLHDGRELATION_DBT where t_objid = " + deal.DealID + " and t_objdockind = " + deal.BOfficeKind + 
        " and t_instrdockind = " + DL_DVNDEAL + " and t_enddate <= " + GetSqlDate(ExecDate) + " and t_enddate != " + GetSqlDate(date(0,0,0));
   RsbData = TRsbDataSet(query);

   while (RsbData.MoveNext)
        ind_ihedg = ind_ihedg + 1;
        FIRole = 10000 + RsbData.Id;
        at = MM_Carry;
        at.FIIDPayer        =
        at.FIIDReceiver     = NATCUR;
        at.Chapter          = 1;
        at.date_carry       = ExecDate;
        at.PrimaryDoc       = deal_pd;  

        // сделки, оцениваемые по АС (Метод ЭПС)
        AdjAm = MM_CalcHedgAdjAm(RsbData.Id, ExecDate, 0);
        if (AdjAm == 0)
            ind_zeropair = ind_zeropair + 1;
        end;
        //ConvSum(AdjAm, AdjAm, ExecDate, deal_pd.GetLeg().pfi, NATCUR);
        IF (AdjAm > 0)
            IF (isSale(GetOperationGroup(deal.DealType)))
                at.CatPayer        = tdr_hedgadjplP;
                at.FIRolePayer     = FIRole;
                at.AccountReceiver = GetDVNDealAcc(RsbData.InstrID, tdr_inchedg);
                at.Ground = "Амортизация корректировки хеджирования, уменьшающей стоимость размещенных денежных средств";
            ELSE
                at.AccountPayer   = GetDVNDealAcc(RsbData.InstrID, tdr_exphedg);
                at.CatReceiver    = tdr_hedgadjatP;
                at.FIRoleReceiver = FIRole;
                at.Ground = "Амортизация корректировки хеджирования, уменьшающей стоимость привлеченных денежных средств";
            END;
        ELSE
            IF (isSale(GetOperationGroup(deal.DealType)))
                at.AccountPayer   = GetDVNDealAcc(RsbData.InstrID, tdr_exphedg);
                at.CatReceiver    = tdr_hedgadjplR;
                at.FIRoleReceiver = FIRole;
                at.Ground = "Амортизация корректировки хеджирования, увеличивающей стоимость размещенных денежных средств";
            ELSE
                at.CatPayer        = tdr_hedgadjatR;
                at.FIRolePayer     = FIRole;
                at.AccountReceiver = GetDVNDealAcc(RsbData.InstrID, tdr_inchedg);
                at.Ground = "Амортизация корректировки хеджирования, увеличивающей стоимость привлеченных денежных средств";
            END;
        END;
        at.SumReceiver = abs(AdjAm);

        IF (not at.carry())
            RETURN 1;
        END;
   END;

   //if (not isServOpMode())
        if (ind_ihedg == 0)
            mm_error("Отношения хеджирования не прекращены");
            return 1;
        elif (ind_zeropair == ind_ihedg)
            mm_error("Сумма амортизации корректировок равна 0");
            return 1;
        end;
   //end;
   
   RETURN 0;
END;

NameMacroFile = "mmhedg_j.mac";