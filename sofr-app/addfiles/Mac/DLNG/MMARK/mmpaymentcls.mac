/*
$Name:        mmpaymentcls.mac
$Module:      МБ Кредиты
$Description: класс для создания платежа в МБК на основе сделки или другого платежа
*/
IMPORT PaymInter, OprInter, PTInter, SfInter, FIInter, MmarkInter, "globals.mac";

const
    MMPAYM_CREATEBYDEALID   = 0,
    MMPAYM_CREATEBYDEALBUF  = 1,
    MMPAYM_CREATEBYPAYMID   = 2,
    MMPAYM_CREATEBYPAYMBUF  = 3,
    MMPAYM_CREATEBYPAYMOBJ  = 4;
/*
Класс для создания платежа в МБК на основе сделки или другого платежа

Входняе параметры:
  _mode - определяет режим работы класса платежа МБК

Назначение остальных параметров зависят от режима работы (пременная _mode):
  MMPAYM_CREATEBYDEALID: на основе ИД сделки
      _parm1 - ИД сделки
      _parm2 - Назначение платжа
      _parm3 - Подвид платежа
      _parm4 - Сумма создаваемого платежа
      _parm5 - Дата валютирования нового платежа
      _parm6 - Не используется
      _parm7 - Не используется

  MMPAYM_CREATEBYDEALBUF: на основе буфера сделки
      _parm1 - Буфер сделки
      _parm2 - Буфер лега
      _parm3 - Назначение платжа
      _parm4 - Подвид платежа
      _parm5 - Сумма создаваемого платежа
      _parm6 - Дата валютирования нового платежа
      _parm7 - Не используется      

  MMPAYM_CREATEBYPAYMID: на основе ИД платежа
      _parm1 - ИД платежа
      _parm2 - Подвид платежа
      _parm3 - Сумма создаваемого платежа
      _parm4 - Дата валютирования нового платежа
      _parm5 - Не используется
      _parm6 - Не используется
      _parm7 - Не используется

  MMPAYM_CREATEBYPAYMBUF: на основе буфера платежа
      _parm1 - Буфер платежа
      _parm2 - Буфер свойств платежа(дебет)
      _parm3 - Буфер свойств платежа(кредит)
      _parm4 - Буфер доп. свойств платежа
      _parm5 - Подвид платежа
      _parm6 - Сумма создаваемого платежа
      _parm7 - Дата валютирования нового платежа

  MMPAYM_CREATEBYPAYMOBJ: на основе объекта платежа
      _parm1 - Объект платежа
      _parm2 - Подвид платежа
      _parm3 - Сумма создаваемого платежа
      _parm4 - Дата валютирования нового платежа
      _parm5 - Не используется
      _parm6 - Не используется
      _parm7 - Не используется
*/
class MM_RsbPayment(_mode, _parm1, _parm2, _parm3, _parm4, _parm5, _parm6, _parm7)
  var 
      Payment = NULL;

  private var
      prm_mode = _mode,
      prm_prm1 = _parm1,
      prm_prm2 = _parm2,
      prm_prm3 = _parm3,
      prm_prm4 = _parm4,
      prm_prm5 = _parm5,
      prm_prm6 = _parm6,
      prm_prm7 = _parm7;

  private var
      tick = TBfile("dl_tick.dbt"),
      leg  = TBfile("dl_leg.dbt");

  private var
      prm_Paym = NULL;

  // Создание платежа на основе данных из сделки/////////////////////////////////////////
  private macro CreatePaymByDeal(Purpose, SubPurpose, PaymSum, ValueDate)
    var
        purp = TBfile("pmpurp.dbt");

    var DefaultCodeKind = NULL;

    RECORD settacc(settacc);

      if (leg.rec.PFI == NATCUR)
          DefaultCodeKind = PTCK_BIC;
      else
          DefaultCodeKind = PTCK_SWIFT;
      end;

      Payment = RsbPayment();
      Payment.DocKind              = DL_IBCDOC;
      Payment.DocumentID           = tick.rec.DealID;
      Payment.Purpose              = Purpose;
      Payment.SubPurpose           = SubPurpose;
      Payment.OrderFIID            =
      Payment.BaseFIID             = 
      Payment.PayerFIID            = 
      Payment.ReceiverFIID         = 
      Payment.FuturePayerFIID      = 
      Payment.FutureReceiverFIID   = leg.rec.PFI;
      Payment.OrderAmount          =
      Payment.PayerAmount          = 
      Payment.ReceiverAmount       = 
      Payment.BaseAmount           = 
      Payment.FutureBaseAmount     = PaymSum;
      Payment.ActuateFutureAmounts(TBA_AMOUNT);
      Payment.IsPlanPaym           = "X";

      purp.KeyNum = 0;
      purp.rec.Purpose = Purpose;
      if(GetEQ(purp))
          Payment.Ground = purp.rec.Name;
      else
          Payment.Ground = "Платеж";
      end;
      Payment.Ground = Payment.Ground + " по сделке №" +String(tick.rec.DealCode + " заключена " +String(tick.rec.DealDate:f));

      Payment.PaymStatus           = PM_PREPARING;
      Payment.Department    = tick.rec.Department; 
      if (tick.rec.Netting == 3)
          Payment.Netting = "X";
      else
          Payment.Netting = "";
      end;
      Payment.Date                 = 
      Payment.PayDate              = 
      Payment.OutTransferDate      =
      Payment.ValueDate            = ValueDate;
      Payment.ClientDate           = {curdate};
      Payment.Number               = String(SubPurpose); 
      Payment.Oper                 = tick.rec.Oper;
      Payment.Origin               = PAYMENT_OR_AUTO;

      if (not FindSETTACC_PMAUTO(tick.rec.PaymAgent, FIKIND_CURRENCY, leg.rec.PFI, PTSK_CURRDL, tick.rec.DealType, Purpose, 0, settacc))
          ;
      elif (not FindSETTACC_PMAUTO(tick.rec.PaymAgent, FIKIND_CURRENCY, leg.rec.PFI, PTSK_CURRDL, tick.rec.DealType, 0, 0, settacc))
          ;
      elif (not FindSETTACC_PMAUTO(tick.rec.PaymAgent, FIKIND_CURRENCY, leg.rec.PFI, PTSK_CURRDL, 0, Purpose, 0, settacc))
          ;
      elif (not FindSETTACC_PMAUTO(tick.rec.PaymAgent, FIKIND_CURRENCY, leg.rec.PFI, PTSK_CURRDL, 0, 0, 0, settacc))
          ;
      end;

      if (IsCOMMITMENT(tick.rec.DealGroup, Purpose))
          Payment.SetPayerPI(PAYMENTS_GROUP_UNDEF,
                             {OurBank},
                             DefaultCodeKind,
                             "",
                             "",
                             "",
                             leg.rec.PFI,
                             1,
                             "",
                             {OurBank},
                             "",
                             "",
                             DefaultCodeKind,
                             ""
                             );

          Payment.SetReceiverPI(PAYMENTS_GROUP_UNDEF,
                                settacc.BankID,
                                settacc.BankCodeKind,
                                settacc.BankCode,
                                settacc.BankName,
                                settacc.CorrAcc,
                                settacc.FIID,
                                settacc.Chapter,
                                settacc.Account,
                                settacc.PartyID,
                                settacc.RecName,
                                settacc.INN,
                                settacc.CodeKind,
                                settacc.Code
                                );

          Payment.PayerBankName = Payment.PayerName;
          Payment.PayerBankCorrCodeKind = PTCK_ALL;
          Payment.PayerBankCorrCode     = "";
          Payment.ReceiverInOurBalance  = "";
          Payment.ReceiverOurCorrAcc    = "";
          Payment.ReceiverOurCorrID     = -1;

          if (settacc.BankCorrID != {OurBank})
              Payment.ReceiverBankCorrAcc      = settacc.CorrAcc;
              Payment.ReceiverBankCorrCodeKind = settacc.BankCorrCodeKind;
              Payment.ReceiverBankCorrCode     = settacc.BankCorrCode;
              Payment.ReceiverBankCorrName     = settacc.BankCorrName;
          end;
      else
          Payment.SetPayerPI(PAYMENTS_GROUP_UNDEF,
                                settacc.BankID,
                                settacc.BankCodeKind,
                                settacc.BankCode,
                                settacc.BankName,
                                settacc.CorrAcc,
                                settacc.FIID,
                                settacc.Chapter,
                                settacc.Account,
                                settacc.PartyID,
                                settacc.RecName,
                                settacc.INN,
                                settacc.CodeKind,
                                settacc.Code
                                );

          Payment.SetReceiverPI(PAYMENTS_GROUP_UNDEF,
                             {OurBank},
                             DefaultCodeKind,
                             "",
                             "",
                             "",
                             leg.rec.PFI,
                             1,
                             "",
                             {OurBank},
                             "",
                             "",
                             DefaultCodeKind,
                             ""
                             );

          Payment.ReceiverBankName = Payment.ReceiverName;
          Payment.ReceiverBankCorrCodeKind = PTCK_ALL;
          Payment.ReceiverBankCorrCode     = "";

          Payment.PayerInOurBalance  = "";
          Payment.PayerOurCorrAcc    = "";
          Payment.PayerOurCorrID     = -1;

          if (settacc.BankCorrID != {OurBank})
              Payment.PayerBankCorrAcc      = settacc.CorrAcc;
              Payment.PayerBankCorrCodeKind = settacc.BankCorrCodeKind;
              Payment.PayerBankCorrCode     = settacc.BankCorrCode;
              Payment.PayerBankCorrName     = settacc.BankCorrName;
          end;
      end;

      Payment.Actuate();
  end;
/////////////////////////////////////////////////////////////////////////////////////////
  // Создание платежа на основе данных из другого платежа.///////////////////////////////
  macro CreatePaymByPaym(Paym, Debet, Credit, RMProp, SubPurpose, PaymSum, ValueDate)
      Payment = RsbPayment();
      Payment.DocKind              = Paym.rec.DocKind;
      Payment.DocumentID           = Paym.rec.DocumentID;
      Payment.Purpose              = Paym.rec.Purpose;
      Payment.SubPurpose           = SubPurpose;
      Payment.OrderFIID            = Paym.rec.OrderFIID;
      Payment.BaseFIID             = Paym.rec.BaseFIID;
      Payment.PayerFIID            = Paym.rec.FIID;
      Payment.ReceiverFIID         = Paym.rec.PayFIID;
      Payment.FuturePayerFIID      = Paym.rec.FIID_FuturePayAcc;
      Payment.FutureReceiverFIID   = Paym.rec.FIID_FutureRecAcc;
      Payment.OrderAmount          = 
      Payment.PayerAmount          = 
      Payment.ReceiverAmount       = 
      Payment.BaseAmount           = 
      Payment.FutureBaseAmount     = PaymSum;

      Payment.ActuateFutureAmounts(TBA_AMOUNT);
      Payment.IsPlanPaym           = Paym.rec.IsPlanPaym;
      Payment.Ground               = RMProp.rec.Ground;
      Payment.PaymStatus           = PM_PREPARING;
      Payment.Department           = Paym.rec.Department; 
      Payment.Netting              = Paym.rec.Netting;
      Payment.Date                 = RMProp.rec.Date;
      Payment.PayDate              = RMProp.rec.PayDate;
      if (Debet.rec.IsSender == "")
          Payment.OutTransferDate = Debet.rec.TransferDate;
          Payment.OutTransport    = Debet.rec.TpID;
          Payment.OutTpSchem      = Debet.rec.TpSchemID;
          Payment.OutRlsForm      = Debet.rec.RlsFormID;
      else                            
          Payment.OutTransferDate = Credit.rec.TransferDate;
          Payment.OutTransport    = Credit.rec.TpID;
          Payment.OutTpSchem      = Credit.rec.TpSchemID;
          Payment.OutRlsForm      = Credit.rec.RlsFormID;

      end;
      Payment.ValueDate            = Paym.rec.ValueDate;
      Payment.ClientDate           = {curdate};
      Payment.Number               = RMProp.rec.Number; 
      Payment.Oper                 = Paym.rec.Oper;
      Payment.Origin               = Paym.rec.Origin;

      Payment.SetPayerPI(PAYMENTS_GROUP_UNDEF,
                           Paym.rec.PayerBankID,
                           Debet.rec.CodeKind,
                           Debet.rec.BankCode,
                           RMProp.rec.PayerBankName,
                           RMProp.rec.PayerCorrAccNostro,
                           Paym.rec.FIID,
                           Paym.rec.Chapter,
                           Paym.rec.PayerAccount,
                           Paym.rec.Payer,
                           RMProp.rec.PayerName,
                           RMProp.rec.PayerINN,
                           Paym.rec.PayerCodeKind,
                           Paym.rec.PayerCode,
                           Debet.rec.Corschem,
                           Debet.rec.CorrPosType
                           );

      Payment.SetReceiverPI(PAYMENTS_GROUP_UNDEF,
                            Paym.rec.ReceiverBankID,
                            Credit.rec.CodeKind,
                            Credit.rec.BankCode,
                            RMProp.rec.ReceiverBankName,
                            RMProp.rec.ReceiverCorrAccNostro,
                            Paym.rec.PayFIID,
                            Paym.rec.Chapter,
                            Paym.rec.ReceiverAccount,
                            Paym.rec.Receiver,
                            RMProp.rec.ReceiverName,
                            RMProp.rec.ReceiverINN,
                            Paym.rec.ReceiverCodeKind,
                            Paym.rec.ReceiverCode,
                            Credit.rec.Corschem,
                            Credit.rec.CorrPosType
                            );

      Payment.FuturePayerAccount       = Paym.rec.FuturePayerAccount;
      Payment.FutureReceiverAccount    = Paym.rec.FutureReceiverAccount;
      Payment.PayerBankCorrCodeKind    = Debet.rec.CorrCodeKind;
      Payment.ReceiverBankCorrCodeKind = Credit.rec.CorrCodeKind;
      Payment.PayerBankCorrCode        = Debet.rec.CorrCode;
      Payment.ReceiverBankCorrCode     = Credit.rec.CorrCode;
      Payment.PayerInOurBalance        = Debet.rec.InOurBalance;
      Payment.ReceiverInOurBalance     = Credit.rec.InOurBalance;
      Payment.PayerOurCorrAcc          = Debet.rec.OurCorrAcc;
      Payment.ReceiverOurCorrAcc       = Credit.rec.OurCorrAcc;
      Payment.PayerOurCorrID           = Debet.rec.OurCorrID;
      Payment.ReceiverOurCorrID        = Credit.rec.OurCorrID;
      Payment.PayerBankCorrName        = RMProp.rec.PayerCorrBankName;
      Payment.ReceiverBankCorrName     = RMProp.rec.ReceiverCorrBankName;
      Payment.Priority                 = RMProp.rec.Priority;
      Payment.ShifrOper                = RMProp.rec.ShifrOper;
      Payment.PaymentKind              = RMProp.rec.PaymentKind;
      Payment.PayerMesBankID           = Paym.rec.PayerMesBankID;
      Payment.ReceiverMesBankID        = Paym.rec.ReceiverMesBankID;
      Payment.PrimDocKind              = Paym.rec.PrimDocKind;
      Payment.StartDepartment          = Paym.rec.StartDepartment;
      Payment.EndDepartment            = Paym.rec.EndDepartment;
      Payment.PayType                  = Paym.rec.PayType;
      Payment.CheckTerror              = Paym.rec.CheckTerror;
      Payment.MCMethodID               = Paym.rec.MCMethodID;
      Payment.PayerDpNode              = Paym.rec.PayerDpNode;
      Payment.ReceiverDpNode           = Paym.rec.ReceiverDpNode;
      Payment.PrimDocOrigin            = Paym.rec.PrimDocOrigin;
      Payment.TypeDocument             = Paym.rec.TypeDocument;
      Payment.UserTypeDocument         = Paym.rec.UserTypeDocument;
      Payment.NumberPack               = Paym.rec.NumberPack;
      Payment.Instancy                 = RMProp.rec.Instancy;
      Payment.NeedNotify               = RMProp.rec.NeedNotify;
      Payment.PartPaymNumber           = Paym.rec.PartPaymNumber;
      Payment.PartPaymShifrMain        = Paym.rec.PartPaymShifrMain;
      Payment.PartPaymNumMain          = Paym.rec.PartPaymNumMain;
      Payment.PartPaymDateMain         = Paym.rec.PartPaymDateMain;
      Payment.PartPaymRestAmountMain   = Paym.rec.PartPaymRestAmountMain;
      Payment.ToBackOffice             = Paym.rec.ToBackOffice;
      Payment.NotForBackOffice         = Paym.rec.NotForBackOffice;
      Payment.IsFixPayerAmount         = Paym.rec.IsFixAmount;
      Payment.IsPurpose                = Paym.rec.IsPurpose;
      Payment.PlaceToIndex             = Paym.rec.PlaceToIndex;
      Payment.ClaimID                  = Paym.rec.ClaimID;
      Payment.PayerBankMarkDate        = Paym.rec.PayerBankMarkDate;
      Payment.ReceiverBankMarkDate     = Paym.rec.ReceiverBankMarkDate;
      Payment.PayerBankEnterDate       = Paym.rec.PayerBankEnterDate;
      Payment.DefComID                 = Paym.rec.DefComID;
      Payment.FeeType                  = Paym.rec.FeeType;
      Payment.I2PlaceDate              = Paym.rec.I2PlaceDate;
      Payment.OperNode                 = Paym.rec.OperNode;
      Payment.MessageType              = RMProp.rec.MessageType;
      Payment.TaxPmGround              = RMProp.rec.TaxPmGround;
      Payment.TaxPmPeriod              = RMProp.rec.TaxPmPeriod;
      Payment.TaxPmNumber              = RMProp.rec.TaxPmNumber;
      Payment.TaxPmDate                = RMProp.rec.TaxPmDate;
      Payment.TaxPmType                = RMProp.rec.TaxPmType;
      Payment.TaxAuthorState           = RMProp.rec.TaxAuthorState;
      Payment.PayerChargeOffDate       = RMProp.rec.PayerChargeOffDate;
      Payment.BttTICode                = RMProp.rec.BttTICode;
      Payment.OKATOCode                = RMProp.rec.OKATOCode;
      Payment.CashSymbolDebet          = RMProp.rec.CashSymbolDebet;
      Payment.CashSymbolCredit         = RMProp.rec.CashSymbolCredit;
      Payment.SymbNotBalDebet          = RMProp.rec.SymbNotBalDebet;
      Payment.SymbNotBalCredit         = RMProp.rec.SymbNotBalCredit;
      Payment.DocDispatchDate          = RMProp.rec.DocDispatchDate;
      Payment.PartyInfo                = RMProp.rec.PartyInfo;
      Payment.ComissCharges            = RMProp.rec.ComissCharges;
      Payment.AdditionalInfo           = RMProp.rec.AdditionalInfo;
      Payment.ReceiverCorrAccNostro    = RMProp.rec.ReceiverCorrAccNostro;
      Payment.PayerName                = RMProp.rec.PayerName;
      Payment.PayerINN                 = RMProp.rec.PayerINN;
      Payment.PayerBankName            = RMProp.rec.PayerBankName;
      Payment.PayerBankCorrAcc         = Debet.rec.CorrAcc;
      Payment.ReceiverName             = RMProp.rec.ReceiverName;
      Payment.ReceiverINN              = RMProp.rec.ReceiverINN;
      Payment.ReceiverBankName         = RMProp.rec.ReceiverBankName;
      Payment.ReceiverBankCorrAcc      = Credit.rec.CorrAcc;

      Payment.Actuate();
  end;
/////////////////////////////////////////////////////////////////////////////////////////
// Сохранить платеж
  macro SaveChanges(InTmp)
    var
        RetVal = 0;

      if (ValType(InTmp) != V_BOOL)
          InTmp = false;
      end;

//      if (InTmp)
//          retVal = MM_InsertPayment(Payment.GetPM_PAYM(), Payment.GetDEBET(), Payment.GetCREDIT(), Payment.GetPMRMPROP());
//      else
          retVal = Payment.Update();
//      end;

      return RetVal;
  end;
/////////////////////////////////////////////////////////////////////////////////////////
// Constructor////////////////////////////////////////////////////////////////////
  if (prm_mode == MMPAYM_CREATEBYDEALID)
      tick.rec.DealID = prm_prm1;
      if (tick.GetEQ)
          leg.rec.DealID = prm_prm1;
          leg.rec.LegID  = 1;
          if (not leg.GetEQ)
              return;
          end;
      else
          return;
      end;
      CreatePaymByDeal(prm_prm2, prm_prm3, prm_prm4, prm_prm5);
  elif (prm_mode == MMPAYM_CREATEBYDEALBUF)
      copy(tick, prm_prm1);
      copy(leg, prm_prm2);
      CreatePaymByDeal(prm_prm3, prm_prm4, prm_prm5, prm_prm6);
  elif (prm_mode == MMPAYM_CREATEBYPAYMID)
       prm_Paym = RsbPayment(prm_prm1);
      CreatePaymByPaym(prm_Paym.GetPM_PAYM(), prm_Paym.GetDEBET(), prm_Paym.GetCREDIT(), prm_Paym.GetPMRMPROP(), prm_prm2, prm_prm3, prm_prm4);
      prm_Paym = NULL;
  elif (prm_mode == MMPAYM_CREATEBYPAYMBUF)
      CreatePaymByPaym(prm_prm1, prm_prm2, prm_prm3, prm_prm4, prm_prm5, prm_prm6, prm_prm7);
  elif (prm_mode == MMPAYM_CREATEBYPAYMOBJ)
      CreatePaymByPaym(prm_prm1.GetPM_PAYM(), prm_prm1.GetDEBET(), prm_prm1.GetCREDIT(), prm_prm1.GetPMRMPROP(), prm_prm2, prm_prm3, prm_prm4);
  else
      prm_mode = NULL;
      prm_prm1 = NULL; 
      prm_prm2 = NULL; 
      prm_prm3 = NULL;
      prm_prm4 = NULL;
      prm_prm5 = NULL;
  end;
end;
