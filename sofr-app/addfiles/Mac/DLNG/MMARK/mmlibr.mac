/*
$Name: mmlibr.mac
$Module: МБК
$Description: Полезные ф-ии.
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : mmlibr.mac
  Created     : 14.07.99
  Programmer  : Сабитов Р.Р.
  Description : Полезные ф-ии.
  Comment     : 
  Leplin  04/05/2018 SubordDeposit LKL underconstruction   
└───────────────────────────────────────────────────────────────────────────*/
IMPORT PaymInter, DealsInter, RsbDataSet, CurrInter, "globals.mac", "mmcateg.mac";
import "dlquery.mac";

VAR NameMacroFile;
const MM_PAYMENTS_GROUP_UNDEF    = 0, // Группа не определена
      MM_PAYMENTS_GROUP_EXTERNAL = 1, // Внешний
      MM_PAYMENTS_GROUP_BRANCH   = 2, // Межфилиальный
      MM_PAYMENTS_GROUP_INTERNAL = 2, // Внутренний платеж
//LKL
      MM_USER_TYPE_SUBORDDEPOSIT = "1"; // Пользовательский тип субординированного депозита в справочнике
//~LKL

/*----------------------------------------------------------------------------
    Печать ошибки
----------------------------------------------------------------------------*/
MACRO mm_error ()
    var i=0,parm,mes="";

    While ( GetParm(i,parm) and (ValType(parm)!=V_UNDEF) )
        mes = String(mes,parm);
        i = i + 1;
    end;

    if (ValType(NameMacroFile)!=V_UNDEF)
	  if ({BPromUse})
     	mes = String(mes);
	  else 
		mes = String(NameMacroFile, ": ", mes);
	  end;
    end;

    msgbox (mes);
END;

MACRO limiter (tick,pm)
    return (pm.DocKind==tick.BofficeKind) and (pm.DocumentID==tick.DealID);
END;

/**
@brief Проверить наличие открытых платежей для сделки
@return V_BOOL true У сделки есть открытые платежи, иначе false
*/
private macro IsDealOpenPaymentsExists(docKind, documentID): Bool
  macro IsPaymentClosed(payment): Bool
    return (payment.PaymStatus == PM_FINISHED) or
           (payment.PaymStatus == PM_CAPITALIZED) or
           (payment.PaymStatus == PM_PROLONGATED) or
           (payment.PaymStatus == PM_CARRYED_OUT_TO_PROLONG) or
           (payment.PaymStatus == PM_CLOSEDBYPROLONGATION) or
           (payment.PaymStatus == PM_OVERDUE) or
           (payment.PaymStatus == PM_CLOSED_W_M_MOVEMENT) or
           (payment.PaymStatus == PM_READY_TO_SEND) or
           (payment.PaymStatus == PM_IS_SENDING) or
           (payment.PaymStatus == PM_CLOSEDBYMODIFICATION) or
           (payment.PaymStatus == PM_KVITPROCESSING);
  end;
  
  var sql = DL_RSDCommand("SELECT * FROM dPmPaym_dbt WHERE t_docKind = ? AND t_documentID = ?");
  sql.AddParam(docKind);
  sql.AddParam(documentID);

  var payment = sql.Execute();
  
  while (payment.MoveNext())
    if (payment.IsPlanPaym and (not IsPaymentClosed(payment)))
      return true;
    end;
  end;
  
  return false;
end;

/*
   Проверить возможность выполнить шаг "Закрытие" для сделок.
*/
MACRO ЗакрытьСделку( tick )
    var  error = 0, flag, stat, account, rest, fd;
    var sql, query, data;

    Message( "Поиск незакрытых платежей." );
    
    if (IsDealOpenPaymentsExists(tick.BofficeKind, tick.DealID))
      error = 1;
      mm_error( "По сделке " + tick.DealCode + " есть незакрытые платежи.|Шаг не выполнен." );
    end;
	
    if (error == 0)
        GetRegistryValue("MMARK\\ОБЩИЕ\\ЗАКРЫВАТЬ_СЧЕТА_ПО_СДЕЛКЕ",
                        V_BOOL, flag, stat);

        if (stat)
          mm_error("Ошибка при работе с реестром");
          error = 1;
        else
            if (flag)
                fd = MMFirstDoc(DL_IBCDOC, tick.DealID, true);

                if ((fd.GetTick().GenAgrID > 0) and
                    (fd.GetGenAgr().AccMode == 2))
                    return 0;
                end;

		query = "select distinct categ.t_code, accdoc.t_account, accdoc.t_currency, " +
                                        "accdoc.t_chapter, accdoc.t_firole from " +
                                        "dmcaccdoc_dbt accdoc, dmccateg_dbt categ " +
                                        " where accdoc.t_dockind = 102 " +
                                        " AND accdoc.t_docid = " + tick.DealID +
                                        " AND accdoc.t_catid = categ.t_id " +
                                        " AND categ.t_updatemode = 0 " +
                                        " AND ACCDOC.T_CATNUM not in (5127, 5121, 5120, 5131, 5004)";

                //SVE проверка на пролонгацию
                //Проверяем наличие связки при пролонгации
	        sql = DL_RSDCommand("select 1 from doprdocs_dbt a, doproper_dbt b where a.t_id_operation=b.t_id_operation and a.t_dockind=102 and b.t_dockind=102 and b.t_DocumentID=lpad(?,34,'0');");
                sql.AddParam(tick.DealID);
	        data = sql.Execute;
	        if ( data.MoveNext() )  // при пролонгации счет по процентам переноситься на следующую сделку и не должен закрываться	           
                   query = query + " AND ACCDOC.T_CATNUM not in (621, 622)"; //SVE 502782 Исключаем счет по процентам по КУ так как выяснилось что дата не обновляется при привязке к новой сделке.
                   //SVE проверяем необходимость закрывать счет открытый по КУ ОД(601)
                   sql = DL_RSDCommand("select acc1.t_account acc1, acc2.t_account acc2 from dmcaccdoc_dbt acc1, dmcaccdoc_dbt acc2  where acc1.t_docid = ? and acc1.t_catnum = acc2.t_catnum and acc1.t_catnum = 601 and acc2.t_docid in (select nvl(sum(a.t_DocumentID),0) from doprdocs_dbt a, doproper_dbt b where a.t_id_operation=b.t_id_operation and a.t_dockind=102 and b.t_dockind=102 and b.t_DocumentID=lpad(acc1.t_docid,34,'0')) order by acc2.t_activatedate desc, acc2.t_id desc");
                   sql.AddParam(tick.DealID);
                   data = sql.Execute;
                   if ( data.MoveNext() )
                      if (data.acc1 == data.acc2)      //SVE Если новый счет по ОД открыт не был, значит при закрытии пролонгированной сделки счет закрывать не нужно
                         query = query + " AND ACCDOC.T_CATNUM not in (601)";
                      end;
                   end;                   
                end;

                account = TRsbDataSet(query);

                while(account.MoveNext)
                    if (MC_FindAndOpenAccount(account.code, fd, date(31,12,9999), 0, MC_OPENACC_CHECKEXIST, NULL, account.currency))
                       
                       rest = Money(0);

                       rest = abs(RestA(account.account, {curdate}, NULL, account.chapter, account.currency)) +
                              abs(RestA(account.account, date(31,12,9999), NULL, account.chapter, account.currency));
          
                       if (rest > 0)
                          mm_error("Не нулевой остаток на счете " + account.account);
                          error = 1;
                          break;
                       elif (MC_FindAndCloseAccount (account.code, fd, {curdate}, account.firole, account.currency, MC_ACC_CLOSE_ALL))
                          mm_error("Невозможно закрть счет " + account.account);
                          error = 1;
                          break;
                       else
                          error = 0;
                       end;
                    end;
                end;
            else
                error = 0;
            end;
        end;
    end;

    return error;
END;

// Существует ли по сделке платеж с данным статусом?
MACRO MM_IsPaymExist(DocKind, DocID, Purpose)
var DataSet = TRsbDataSet("SELECT pm.t_ValueDate FROM dpmpaym_dbt pm " +
        " WHERE pm.t_DocKind = " + DocKind + " and pm.t_DocumentID = " + DocID +
            " and pm.t_Purpose = " + Purpose);
    if(DataSet.MoveNext())
       return true;
    end;

    return false;
END;

MACRO GetAttrID (ObjectType, ObjectID, GroupID, DateAttr, DateState_)
    var 
        rs = NULL;

    if (ValType(DateAttr) == V_UNDEF)
       DateAttr = date(31,12,9999);
    end;

    rs = TRsbDataSet("select t_attrid, t_validfromdate from dobjatcor_dbt" +
                      " where t_objecttype = " + ObjectType +
                      " and ltrim(t_object, 0) = " + ObjectID +
                      " and t_groupid = " + GroupID +
                      " and t_general = 'X'" +
                      " and t_validfromdate <= '" + Date(DateAttr) +"'"
                      " order by t_validfromdate desc");
    if (rs.MoveNext())
        if (ValType(DateState_) != V_UNDEF)
            DateState_ = date(rs.ValidFromDate);
            SetParm(4, DateState_);
        end;

        return rs.AttrID;
    end;

    return 0;
END;

MACRO MM_GetPercContractID(ContractType, ObjID)
    var 
        rs = NULL;

    if ((ContractType != PC_IBC_CON)and
        (ContractType != PC_IBC_CON_EXP)and
        (ContractType != PC_IBC_CON_EXPPC))
        return 0;
    end;

    rs = TRsbDataSet("select t_ContractID from dprccontract_dbt where " +
                      "t_ContractType = " + ContractType + " and t_ObjectID = '" + ObjID +"' " +
                      "and t_ObjectType = 103");


    if (rs.MoveNext())
        return rs.ContractID;
    end;

    return 0;
END;

MACRO GetTaxBank(tick)
    VAR НалогСтавка = 0, 
    party  = TBfile("party.dbt", "R", 0);
    party.rec.PartyID = tick.PartyID;
    party.getEQ;

    if (tick.GenAgrID > 0)
        VAR genagr = TBfile("dl_genagr.dbt", "R", 0);
        genagr.rec.GenAgrID = tick.GenAgrID;
        if (genagr.GetEQ and genagr.rec.Tax)
            НалогСтавка = genagr.rec.Tax_Amount;
        end;
    end;
    if (НалогСтавка == 0)
        НалогСтавка = double(readNoteForObject(OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 59, {curdate}, date(0,0,0)));
        if (VALTYPE(НалогСтавка) == V_UNDEF)
            НалогСтавка = 0;
        end;
    end;
    return НалогСтавка;
END;

MACRO GetTaxContr(tick)
    VAR НалогСтавка = 0, 
    party = TBfile("party.dbt", "R", 0);
    party.rec.PartyID = tick.PartyID;
    party.getEQ;

    if (tick.GenAgrID > 0)
        VAR genagr = TBfile("dl_genagr.dbt", "R", 0);
        genagr.rec.GenAgrID = tick.GenAgrID;
        if (genagr.GetEQ and genagr.rec.TaxContractor)
            НалогСтавка = genagr.rec.TaxContractorAmount;
        end;
    end;
    if (НалогСтавка == 0)
        НалогСтавка = double(readNoteForObject(OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 85, {curdate}, date(0,0,0)));
        if (VALTYPE(НалогСтавка) == V_UNDEF)
            НалогСтавка = 0;
        end;
    end;
    return НалогСтавка;
END;

MACRO UpdateAccInPayment(deal, Account, CatCode)
  var paym       = NULL,
      countpayms = 0,
      i          = 0;

  macro GetCountPercenPayms(DocKind, DocID, Purpose)
    var rs = RsdRecordset("select count(*) as cnt from dpmpaym_dbt where (t_dockind ="+DocKind+")and(t_documentid = "+DocID+")and(t_purpose = "+Purpose+")");
	rs.moveNext;
    return rs.value("cnt");
  end;

  macro HasGraphPayms(DocKind, DocID)
    var rs = RsdRecordset("select count(*) as cnt from dpmpaym_dbt where (t_dockind ="+DocKind+")and(t_documentid = "+DocID+")and(t_subpurpose > 1000)");
	if (rs.moveNext and (rs.value("cnt") > 0))
        return true;
    end;
    return false;
  end;

  // в случае сделок с отрицательными ставками стороны в платежах по процентам меняются
  var isSaleDeal = isSale(GetOperationGroup(deal.GetTick().DealType));
  var SubPurp = 0;
  if (HasGraphPayms(deal.GetTick().BofficeKind, deal.GetTick().DealID))
        SubPurp = 1000;
  end;

  if ((deal.GetTick().NegativeRate == "X") and ((CatCode == tdr_claimR) or (CatCode == tdr_claimP)))
    isSaleDeal = (not isSaleDeal);
  end;
  
  if (isSaleDeal) /*для размещения*/
    if ((CatCode == tdr_rest(deal.GetTick().FlagTypeDeal)) OR (CatCode == tdr_calcs_cmR))
        paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC, 0);
        if (paym.PaymentID > 0)
            paym.SetPayerPI (PAYMENTS_GROUP_UNDEF, 
                            paym.PayerBankID,
                            paym.PayerBankCodeKind,
                            paym.PayerBankCode,
                            paym.PayerBankName,
                            paym.PayerBankCorrAcc,
                            deal.GetLeg().PFI,
                            1,
                            Account,
                            paym.Payer,
                            paym.PayerName,
                            paym.PayerINN,
                            paym.PayerCodeKind,
                            "");

            paym.FuturePayerAccount = Account;
        else
            mm_error("Не найдены платежи");
            return 2;
        end;
    end;
    if ((CatCode == tdr_rest(deal.GetTick().FlagTypeDeal)) OR (CatCode == tdr_retirement))
    	countpayms = GetCountPercenPayms(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC_RET);
        if (SubPurp == 1000)
            SubPurp = 1001;
        end;
        while (i < countpayms)
            paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC_RET, SubPurp+i);
            if (paym.PaymentID > 0)
                paym.SetReceiverPI (PAYMENTS_GROUP_UNDEF, 
                                    paym.ReceiverBankID,
                                    paym.ReceiverBankCodeKind,
                                    paym.ReceiverBankCode,
                                    paym.ReceiverBankName,
                                    paym.ReceiverBankCorrAcc,
                                    deal.GetLeg().PFI,
                                    1,
                                    Account,
                                    paym.Receiver,
                                    paym.ReceiverName,
                                    paym.ReceiverINN,
                                    paym.ReceiverCodeKind,
                                    "");

                paym.FutureReceiverAccount = Account;
            end;
            i = i + 1;
        end;
    end;
    if(CatCode == tdr_claimR)
		countpayms = GetCountPercenPayms(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PERCENT);
        while (i < countpayms)
            paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PERCENT, SubPurp+i+1);
            if (paym.PaymentID > 0)
                paym.SetReceiverPI (PAYMENTS_GROUP_UNDEF, 
                                    paym.ReceiverBankID,
                                    paym.ReceiverBankCodeKind,
                                    paym.ReceiverBankCode,
                                    paym.ReceiverBankName,
                                    paym.ReceiverBankCorrAcc,
                                    deal.GetLeg().PFI,
                                    1,
                                    Account,
                                    paym.Receiver,
                                    paym.ReceiverName,
                                    paym.ReceiverINN,
                                    paym.ReceiverCodeKind,
                                    "");

                paym.FutureReceiverAccount = Account;
            end;
			i = i + 1;
        end;  
    end;
  else
    if (CatCode == tdr_rest(deal.GetTick().FlagTypeDeal))
        paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC, 0);
        if (paym.PaymentID > 0)
            paym.SetReceiverPI (PAYMENTS_GROUP_UNDEF, 
                                paym.ReceiverBankID,
                                paym.ReceiverBankCodeKind,
                                paym.ReceiverBankCode,
                                paym.ReceiverBankName,
                                paym.ReceiverBankCorrAcc,
                                deal.GetLeg().PFI,
                                1,
                                Account,
                                paym.Receiver,
                                paym.ReceiverName,
                                paym.ReceiverINN,
                                paym.ReceiverCodeKind,
                                "");

            paym.FutureReceiverAccount = Account;
        else
            mm_error("Не найдены платежи");
            return 2;
        end;

    	countpayms = GetCountPercenPayms(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC_RET);
        if (SubPurp == 1000)
            SubPurp = 1001;
        end;
        while (i < countpayms)
            paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC_RET, SubPurp+i);
            if (paym.PaymentID > 0)
                paym.SetPayerPI (PAYMENTS_GROUP_UNDEF, 
                                paym.PayerBankID,
                                paym.PayerBankCodeKind,
                                paym.PayerBankCode,
                                paym.PayerBankName,
                                paym.PayerBankCorrAcc,
                                deal.GetLeg().PFI,
                                1,
                                Account,
                                paym.Payer,
                                paym.PayerName,
                                paym.PayerINN,
                                paym.PayerCodeKind,
                                "");

                paym.FuturePayerAccount = Account;
            end;
            i = i + 1;
        end;
    elif(CatCode == tdr_claimP)
        countpayms = GetCountPercenPayms(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PERCENT);
        while (i < countpayms)
            paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PERCENT, SubPurp+i+1);
            if (paym.PaymentID > 0)
                paym.SetPayerPI (PAYMENTS_GROUP_UNDEF, 
                                 paym.PayerBankID,
                                 paym.PayerBankCodeKind,
                                 paym.PayerBankCode,
                                 paym.PayerBankName,
                                 paym.PayerBankCorrAcc,
                                 deal.GetLeg().PFI,
                                 1,
                                 Account,
                                 paym.Payer,
                                 paym.PayerName,
                                 paym.PayerINN,
                                 paym.PayerCodeKind,
                                 "");

                paym.FuturePayerAccount = Account;
            end;
			i = i + 1;
        end;  
    end;
  end;
  return 0;
END;

MACRO ПИ_МБК_ДелатьПроводкуПоИсхПлатежам():bool
  var ErrCode, _ДелатьПроводкуПоИсхПлатежам = false, Path = "COMMON\\WORK_MODE\\ПРОВОДКА ПО ИСХОДЯЩИМ ПЛАТЕЖАМ";

  GetRegistryValue(Path, V_BOOL, _ДелатьПроводкуПоИсхПлатежам, ErrCode);
  if( ErrCode != 0 )
    MsgBox("Ошибка при получении значения настройки \"" + Path + "\"");
  end;

  return _ДелатьПроводкуПоИсхПлатежам;
END;

/*
Функция МБК ищет пару счетов корректировок, соответствующих переданному коду инструмента хеджирования.
В случае ошибки возвращает false, иначе true
*/
MACRO MM_GetHedgCorAcc(HedgInstrID, ExecDate, AccountR, AccountP)
    var deal_pd, HedgInstr = TBfile ("dlhdgrelation.dbt"), accR, accP;
    HedgInstr.rec.ID = HedgInstrID;
    if (not HedgInstr.GetEQ)
        return false;
    end;

    deal_pd = MMFirstDoc (DL_IBCDOC, HedgInstr.rec.ObjID, false);
    if (isSale(GetOperationGroup(deal_pd.GetTick().DealType)))
	    accR = tdr_hedgadjplR;
	    accP = tdr_hedgadjplP;
    else    
	    accR = tdr_hedgadjatR;
	    accP = tdr_hedgadjatP;
    end;
	
    if (not deal_pd.IsExistAccount(accR, NULL, true, 10000+HedgInstrID, AccountR, NULL, NATCUR))
	    deal_pd.OpenAccount(accR, NULL, true, 10000+HedgInstrID, AccountR, ExecDate, NATCUR);
    end;
    if (not deal_pd.IsExistAccount(accP, NULL, true, 10000+HedgInstrID, AccountP, NULL, NATCUR))
	    deal_pd.OpenAccount(accP, NULL, true, 10000+HedgInstrID, AccountP, ExecDate, NATCUR);
    end;

    SetParm( 2, AccountR );
    SetParm( 3, AccountP );
    return true;
END;

/*
API-функция для вставки записей в скроллинг "Изменение справедливой стоимости сделки инструмента хеджирования"
*/
MACRO MM_InsertHedgFairVal(RelationID, FairDate, CurFair, PrevFair, PFI)
    var IHFairVal = TBfile ("dlhdgrfairval.dbt", "W", 1), HedgInstr = TBfile ("dlhdgrelation.dbt");
    HedgInstr.rec.ID = RelationID;
    if (not HedgInstr.GetEQ)
        return false;
    end;
    IHFairVal.rec.RelationID = RelationID;
    IHFairVal.rec.Date        = FairDate; 
    if (IHFairVal.GetEQ)
        IHFairVal.rec.ActualFV    = CurFair;
        IHFairVal.rec.PrevFV      = PrevFair;
        IHFairVal.rec.DFV         = CurFair - PrevFair;
        IHFairVal.rec.CurFIID     = PFI;
        return IHFairVal.Update;
    else
        IHFairVal.rec.InstrHedgID = RelationID;
        IHFairVal.rec.Date        = FairDate;
        IHFairVal.rec.ActualFV    = CurFair;
        IHFairVal.rec.PrevFV      = PrevFair;
        IHFairVal.rec.DFV         = CurFair - PrevFair;
        IHFairVal.rec.CurFIID     = PFI;
        return IHFairVal.Insert;
    end;
    return true;
END;
//dan пользовательская функция
//По ID сделки возвращаем 1 если на ГС установлен признак платежи Т-1, иначе 0
macro CheckIsDealTminus1_ByDealID(dealid)
 var cmd = RSDCommand("Select tick.t_dealid from ddl_tick_dbt tick, ddl_genagr_dbt ga where ga.t_genagrid = tick.t_genagrid and tick.t_dealid = ? and ga.T_OUTPMTMIN1 = chr(88)");
     cmd.AddParam("dealid", rsdbp_in, dealid);
 var rs = RSDRecordset(cmd, rsdval_client, rsdval_static);
 if (rs.movenext())
    return 1;
 end;
    return 0;
end;
