/*
$Name:        mmpenalt.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Генерация штрафов"
*/
/*-------------------------------------------------------------------------
  Имя файла        : mmpenalt.mac

  Описание         : макрос "Генерация штрафов"

  Программист      : Куперин М.В.

  Создан           : 03.07.2008
-------------------------------------------------------------------------*/
IMPORT RSD, Проценты, PaymInter, mmcateg, mmlibr, mmcnst_j, mpclb, 
       mmcarry, MmarkInter, mmrestlist, mmlb_j;

PRIVATE VAR
            ID_Operation = NULL,
            ID_Step      = NULL,
            FirstDoc     = NULL,
            ExecDate     = {curdate};

PRIVATE MACRO CheckPaym(DocKind, DocumentID, Purpose, Status, PaymentID)
  VAR cmd, DataSet;

  PaymentID = 0;
  cmd = "select pm.t_PaymentID as pmid FROM dpmpaym_dbt pm " +
         " WHERE pm.t_DocKind = " + DocKind + " and pm.t_DocumentID = " + DocumentID +
            " and pm.t_Purpose = " + Purpose;

  if (Status)
  	cmd = cmd + " and pm.t_PaymStatus = " + Status;
  end;

  DataSet = TRsbDataSet(cmd);
  if(not DataSet.MoveNext)
      SetParm(4, PaymentID);  	
      return false;
  end;

  PaymentID = DataSet.pmid;
  SetParm(4, PaymentID);
  return true;
END;


PRIVATE MACRO GetPenaltySum(DocKind, DocumentID, Purpose)
  VAR DataSet = TRsbDataSet("select sum(case when t_Amount < t_PayAmount then t_Amount else t_PayAmount end) as Amount "+
                             "from dpmpaym_dbt where t_PaymentID in " +
                             "(select t_PaymentID from dpmpaym_dbt where "+
                             "t_DocKind = " + DocKind + " and t_DocumentID = " + DocumentID + " and t_Purpose = " + Purpose +
                             " minus " +
                             "select tt.t_purposepayment FROM dpmlink_dbt tt where " +
                             "(tt.t_initialpayment <> tt.t_purposepayment)and " +
                             "(tt.t_initialpayment in (select t_PaymentID from dpmpaym_dbt where " +
                             "t_DocKind = " + DocKind + " and t_DocumentID = " + DocumentID + " and t_Purpose = " + Purpose +")))");
 
  if(not DataSet.MoveNext)
      return 0;
  end;

  return DataSet.Amount;
END;

MACRO InsertDocs(deal_pd, Purpose, Sum)

    VAR at;
    at = MM_Carry;
    at.date_carry  = ExecDate;
    at.PrimaryDoc  = deal_pd;
    at.Chapter     = 1;

    if (isSale(GetOperationGroup(deal_pd.GetTick().DealType)))
        // размещение
        at.FIIDPayer    = deal_pd.GetLeg().PFI;
        at.FIIDReceiver = NATCUR;
        at.CatReceiver  = tdr_pennyR;
        if (Purpose == PM_PURP_PENALTY_PRINC)
            at.CatPayer     = tdr_penny_ODR;
            at.Ground       = "Начисление неустоек (штрафов, пеней) за просроченный ОД";

        elif (Purpose == PM_PURP_PENALTY_PERC)
            at.CatPayer     = tdr_penny_percR;
            at.Ground       = "Начисление неустоек (штрафов, пеней) за просроченные %";
        end;
    else
        ConvSum( Sum, Sum, ExecDate, deal_pd.GetLeg().PFI, NATCUR);
        at.FIIDPayer    = NATCUR;
        at.FIIDReceiver = deal_pd.GetLeg().PFI;
        at.CatPayer     = tdr_pennyP;
        if (Purpose == PM_PURP_PENALTY_PRINC)
            at.CatReceiver = tdr_penny_ODP;
            at.Ground      = "Начисление неустоек (штрафов, пеней) за просроченный ОД";

        elif (Purpose == PM_PURP_PENALTY_PERC)
            at.CatReceiver = tdr_penny_percP;
            at.Ground      = "Начисление неустоек (штрафов, пеней) за просроченные %";
        end;

    end;
    at.Sum     = Sum;

    if (not at.carry())
        return false;
    end;
    return true;
END;

PRIVATE MACRO GeneratePenalties(deal_pd, PMPurpose, PMPurposeNP, PenSum, NewCat, Ground)
  VAR PaymentID, WorkMode = 0, Categ, Account = "", isR, SumPenalty, CorSum, NewPR_ID, NewAmount, NewBaseAmount;
  record pm ("pmpaym");
  record d ("pmprop");
  record k ("pmprop");
  record rm ("pmrmprop");

//Если сумма штрафа равна 0, то ничего не делаем
  if (PenSum == 0)
      return true;
  end;
//Поиск существующего штрафного платежа, если его нет, то создаем его сами
//на основе платежа по ОД 
  if(not CheckPaym(DL_IBCDOC, deal_pd.GetTick().DealID, PMPurpose, PM_READIED, PaymentID))
      WorkMode = 1;
      if (not CheckPaym(DL_IBCDOC, deal_pd.GetTick().DealID, PMPurpose, PM_FINISHED, PaymentID))
          WorkMode = 2;
          CheckPaym(DL_IBCDOC, deal_pd.GetTick().DealID, PMPurposeNP, NULL, PaymentID)
      end;
  end;

  if (not PaymentID)
      mm_error("Ошибка при создании штрафного платежа");
      return false;
  end;

  if (WorkMode == 0)
// Обновляем существующий платеж, штрафных платежей может быть несколько
      if (FindPayment (PaymentID, NULL, NULL, NULL, NULL, true, pm));
          mm_error("Ошибка при создании штрафного платежа");
          return false;  	
      end;

      SumPenalty = GetPenaltySum(DL_IBCDOC, deal_pd.GetTick().DealID, PMPurpose);
      if (SumPenalty == 0)
          mm_error("Ошибка при создании штрафного платежа");
          return false;
      end;

      NewBaseAmount = PenSum - SumPenalty;
      if(NewBaseAmount > $0 )
          //pm.FuturePayerAmount = pm.FuturePayerAmount + PenSum - SumPenalty;
          //pm.BaseAmount = pm.Amount = pm.PayAmount = pm.Amount + PenSum - SumPenalty;    

          if (not NewCat)
               ConvSum(NewAmount, NewBaseAmount, ExecDate, deal_pd.GetLeg().PFI, NATCUR);
               isR = MM_IsDSF(FirstDoc.GetTick().DealType);
               if(isR)
                   pm.PayAmount = pm.PayAmount + NewAmount;
                   pm.BaseAmount = pm.Amount = pm.Amount + NewBaseAmount;
               else
                   pm.Amount = pm.BaseAmount = pm.Amount + NewAmount;
                   pm.PayAmount = pm.PayAmount + NewBaseAmount;
                   pm.FuturePayerAmount = pm.FuturePayerAmount + NewAmount;
               end;
          else
              pm.BaseAmount = pm.Amount = pm.PayAmount = pm.Amount + NewBaseAmount; 
          end;

          if (UpdatePayment (pm))
              mm_error("Ошибка при обновлении штрафного платежа");
              return false;  
          end;
      end;
      CorSum = PenSum - SumPenalty;

  elif (WorkMode == 1)
//Создаем новый платеж на основе закрытого штрафного платежа,
//но не забыть о том что штрафных платежей может быть несколько,
//поэтому при расчете суммы штрафа должны учитываться эти штрафные 
//платежи
      if (FindPayment (PaymentID, NULL, NULL, NULL, NULL, true, pm, d, k, rm));
          mm_error("Ошибка при создании штрафного платежа");
          return false;  	
      end;

      SumPenalty = GetPenaltySum(DL_IBCDOC, deal_pd.GetTick().DealID, PMPurpose);
      if (SumPenalty == 0)
          mm_error("Ошибка при создании штрафного платежа");
          return false;
      end;

      NewBaseAmount = PenSum - SumPenalty;
      if(NewBaseAmount > $0 )
          pm.ValueDate  = {curdate};
          pm.PaymStatus = pm.PaymStatus = pm.PaymStatus = PM_READIED;
          d.PaymentID = k.PaymentID = rm.PaymentID = pm.PaymentID = 0;
          
          pm.SubPurpose = pm.SubPurpose + 1;
          rm.Number = pm.SubPurpose;

          isR = MM_IsDSF(FirstDoc.GetTick().DealType);
          if (not NewCat)
               ConvSum(NewAmount, NewBaseAmount, ExecDate, deal_pd.GetLeg().PFI, NATCUR);  
               if(isR)
                   pm.FutureReceiverAmount = pm.PayAmount = NewAmount;
                   pm.BaseAmount = pm.Amount = pm.FuturePayerAmount = NewBaseAmount;
               else
                   pm.Amount = pm.BaseAmount = pm.FuturePayerAmount = NewAmount;
                   pm.FutureReceiverAmount = pm.PayAmount = NewBaseAmount;
               end;
          else
            pm.BaseAmount = pm.Amount = pm.FuturePayerAmount = pm.PayAmount = PenSum - SumPenalty;
          end;
    
          if (not InsertPayment(NewPR_ID, pm, d, k, rm))
              mm_error("Ошибка при создании штрафного платежа");
              return false; 
          end;
      end;

      CorSum = PenSum - SumPenalty; 
  elif (WorkMode == 2)
//Создаем новый платеж на основе платежа по ОД, штрафных платежей еще нет
      if (FindPayment (PaymentID, NULL, NULL, NULL, NULL, true, pm, d, k, rm));
          mm_error("Ошибка при создании штрафного платежа");
          return false;  	
      end;
      pm.ValueDate  = {curdate};
      pm.PaymStatus = pm.PaymStatus = pm.PaymStatus = PM_READIED;
      pm.Purpose    = PMPurpose;
      d.PaymentID = k.PaymentID = rm.PaymentID = pm.PaymentID = 0;
      pm.BaseAmount = pm.Amount = pm.FuturePayerAmount = pm.PayAmount = PenSum;
      pm.SubPurpose = 0;
      rm.Number = pm.SubPurpose;
      rm.Ground = Ground;

      isR = MM_IsDSF(FirstDoc.GetTick().DealType);
 
      if(not NewCat)
        if(isR)
            Categ = tdr_pennyR;
        else
            Categ = tdr_pennyP;
        end;
      else
        if(isR)
            if   (PMPurpose == PM_PURP_PENALTY_PRINC)
                Categ = tdr_penny_ODR;
            elif (PMPurpose == PM_PURP_PENALTY_PERC)
                Categ = tdr_penny_percR;
            end;
        else
            if   (PMPurpose == PM_PURP_PENALTY_PRINC)
                if (IsCLAIMSACQ(GetOperationGroup(deal_pd.GetTick().DealType)))
                    Categ = tdr_penny_ODR;
                else
                    Categ = tdr_penny_ODP;
                end;
            elif (PMPurpose == PM_PURP_PENALTY_PERC)
                if (IsCLAIMSACQ(GetOperationGroup(deal_pd.GetTick().DealType)))
                    Categ = tdr_penny_percR;
                else
                    Categ = tdr_penny_percP;
                end;
            end;
        end;
      end;

      //if (not СчетСделки(Categ, deal_pd.GetTick().DealID, Account))
      if (not deal_pd.FindNumberAccount(Categ, Account, NULL, NULL, deal_pd.GetLeg().PFI))
          mm_error("Ошибка при создании штрафного платежа");
          return false;  
      end;

      if(isR)
          pm.ReceiverAccount = pm.FutureReceiverAccount = Account;
          if (Categ == tdr_pennyR)
            pm.PayFIID = pm.FIID_FutureRecAcc = NATCUR;
            ConvSum(pm.PayAmount, pm.PayAmount, ExecDate, deal_pd.GetLeg().PFI, NATCUR);
          end;
      else
          pm.PayerAccount = pm.FuturePayerAccount = Account;
          if (Categ == tdr_pennyP)
            pm.FIID = pm.BaseFIID = pm.FIID_FuturePayAcc = NATCUR;
            ConvSum(pm.Amount, pm.Amount, ExecDate, deal_pd.GetLeg().PFI, NATCUR);
            pm.BaseAmount = pm.Amount;
          end;
      end;

      if (not InsertPayment(NewPR_ID, pm, d, k, rm))
          mm_error("Ошибка при создании штрафного платежа");
          return false; 
      end;

      CorSum = PenSum;
  end;

  if (NewCat)
    return InsertDocs(deal_pd, PMPurpose, CorSum);
  end;
  return true;
END;

PRIVATE MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    VAR list_princ = false, list_perc = false,
        СчетПроцентов = NULL, sum_princ = $0, 
        sum_perc = $0, ДатаВвода = NULL, 
        tdr_penny_OD = NULL, tdr_penny_perc = NULL, stat = 0, Account = "", NewCat = false;

    record deal( dl_tick );
    SetBuff( deal, Document );
    ID_Operation = IDOperation;
    ID_Step      = IDStep;

    //ExecDate = {curdate};    
    FirstDoc = MMFirstDoc(DL_IBCDOC, deal.DealID, true);

    //Проверить наличие платежей по задолженностям
    if(CheckPaym(DL_IBCDOC, deal.DealID, PM_PURP_OVD_PRINC, PM_READIED))
        list_princ = true;
    end;

    if(CheckPaym(DL_IBCDOC, deal.DealID, PM_PURP_OVD_PERC, PM_READIED))
        list_perc = true;
    end;

    if ((not list_princ) and (not list_perc))
        mm_error("Не найдено просроченных платежей");
        return 1
    end;

    GetRegistryValue("COMMON\\МСФО\\ДАТА_ВВОДА",
        V_STRING, ДатаВвода, stat);
    if (stat)
        mm_error("Ошибка при работе с реестром");
        return 1;
    end;
    ДатаВвода = date (ДатаВвода);

    if (isSale(GetOperationGroup(deal.DealType)))
        tdr_penny_OD   = tdr_penny_ODR;
        tdr_penny_perc = tdr_penny_percR;
    else
        tdr_penny_OD   = tdr_penny_ODP;
        tdr_penny_perc = tdr_penny_percP;
    end;

    if ((ДатаВвода <= ExecDate) or ((ДатаВвода > ExecDate) 
        and СчетСделки(tdr_penny_OD, deal.DealID, Account, ExecDate, NULL, true) 
        and СчетСделки(tdr_penny_perc, deal.DealID, Account, ExecDate, NULL, true)))
        NewCat = true;
    end;

    if (list_princ)
        sum_princ = CalcPercentForPeriod(MM_GetPercContractID(PC_IBC_CON_EXP, FirstDoc.GetTick().DealCode), FirstDoc.GetLeg().Start, {curdate});
        sum_princ = money(round(sum_princ,2));
        if (not GeneratePenalties(FirstDoc, PM_PURP_PENALTY_PRINC, PM_PURP_PRINC_RET, sum_princ, NewCat, "Штраф за просроченный основной долг"))
            return 1;
        end;
    end;

    if (list_perc)
        sum_perc = CalcPercentForPeriod(MM_GetPercContractID(PC_IBC_CON_EXPPC, FirstDoc.GetTick().DealCode), FirstDoc.GetLeg().Start, {curdate});
        sum_perc = money(round(sum_perc,2));
        if (not GeneratePenalties(FirstDoc, PM_PURP_PENALTY_PERC, PM_PURP_PRINC_RET, sum_perc, NewCat, "Штраф за просроченные проценты"))
            return 1;
        end;
    end;

    return 0;
END;

NameMacroFile = "mmpenalt.mac";
