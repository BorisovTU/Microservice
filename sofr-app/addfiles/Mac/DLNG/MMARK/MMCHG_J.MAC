/*-------------------------------------------------------------------------
  Имя файла        : mmchg_j.mac

  Описание         : Проводки шага "Изменение категории качества по сделке"

  Программист      : Куперин М.В.

  Создан           : 01.06.2010
-------------------------------------------------------------------------*/
IMPORT OprInter, mmcnst_j, mmlibr, mmlb_j, mmcarry;

PRIVATE record reslnk(dlreslnk); // Заполняется из сишного кода (MM_OP_PREP_InsRiskInitMacro)

PRIVATE VAR Currency, // Валюта сделки (leg.PFI)
            FirstDoc = NULL;

PRIVATE MACRO Проводка(
    КатегорияДебет, СчДебет, КатегорияКредит, СчКредит,
    Глава, Сумма, Основание, ExecDate,
    ВалДебет, ВалКредит)

    var at = MM_Carry;
    return 0;

    MM_ByDefault(ВалДебет, Currency);
    MM_ByDefault(ВалКредит, Currency);

    at.date_carry      = ExecDate;
    at.PrimaryDoc      = FirstDoc;
    at.AccountPayer    = СчДебет;
    at.AccountReceiver = СчКредит;
    at.CatPayer        = КатегорияДебет;
    at.CatReceiver     = КатегорияКредит;
    at.FIIDPayer       = ВалДебет;
    at.FIIDReceiver    = ВалКредит;
    if (at.CatPayer == tdr_corespacc_vb)
        at.FIIDPayer   = NATCUR;
        at.FiRolePayer = FIROLE_CORACC_ACTIVE;
    end;
    
    if (at.CatReceiver == tdr_corespacc_vb)
        at.FIIDReceiver   = NATCUR;
        at.FiRoleReceiver = FIROLE_CORACC_ACTIVE;
    end;
    
    if (ВалДебет == Currency)
        at.SumPayer    = Сумма;
    elif (ВалКредит == Currency)
        at.SumReceiver = Сумма;
    else
        return 1;
    end;
    
    at.Chapter         = Глава;
    at.Ground          = Основание;

    if (at.carry())
        SetParm(1, at.AccountPayer);
        SetParm(3, at.AccountReceiver);
        return 0;
    else
        return 1;
    end;
END;

PRIVATE MACRO НККС_ПР_дляКонтрагента(Контрагент, НККС, НПР)
    VAR
        RsbReslnk = TRsbDataSet("select max(lnk.t_reservepercent) maxRP, t_qualitycategory maxQC "
                                "from ddlreslnk_dbt lnk, ddl_tick_dbt tick "
                                "where lnk.t_Type = 3 and lnk.t_parentid = tick.t_dealid and "
                                " lnk.t_childid = -1 and tick.t_partyid = " + Контрагент + 
                                " group by t_qualitycategory order by t_qualitycategory desc");

    if (not RsbReslnk.MoveNext()) 
        НККС = 1;
        НПР = 0;
    else
        НПР  = RsbReslnk.maxRP;
        НККС = RsbReslnk.maxQC;
    end;

    SetParm(1, НККС);
    SetParm(2, НПР);
END;

MACRO ExecuteStep(Buffer, Document)
  var
      stat = 0, ExecDate = NULL,
      cmd = "", rs = NULL,
      ККС = 1, ПР = 0,
      КонтрольПКР = 0,
      error = "";

    record deal(dl_tick);

    SetBuff( deal, Document);

    FirstDoc = MMFirstDoc(DL_IBCDOC, deal.DealID, true);

    ExecDate = reslnk.LnkDate;

    if (not IsSALE(deal.DealGroup))
        mm_error("Операция разрешена только для сделок размещения");
        return 1;
    end;

    if (ExecDate > {curdate})
        mm_error("Запрещено менять категорию качества на будущее");
        return 1;
    end;

    if (deal.DealDate >reslnk.LnkDate)
        mm_error("Дата должна быть не меньше даты сделки");
        return 1;
    end;

    cmd = "select count(1) as cnt from ddlreslnk_dbt rlnk where "
           "(rlnk.t_type = 3)and(rlnk.t_parentid = " + deal.DealID + ") "
           "and(rlnk.t_childid <> -1)and(rlnk.t_lnkdate >= '" + ExecDate + "')";

    rs = RsdRecordset(cmd);

    rs.MoveNext();
    if (rs.value("cnt") > 0)
        mm_error("За дату или позднее уже был сформирован резерв");
        return 1;
    end;

    cmd = "select count(1) as cnt from ddlreslnk_dbt rlnk where "
           "(rlnk.t_type = 3)and(rlnk.t_parentid = " + deal.DealID + ") "
           "and(rlnk.t_childid = -1)and(rlnk.t_lnkdate > '" + ExecDate + "')";

    rs = RsdRecordset(cmd);

    rs.MoveNext();
    if (rs.value("cnt") > 0)
        mm_error("Уже задана группа риска на указанную дату");
      //  return 1;
    end;

    cmd = "select rlnk.t_qualitycategory, rlnk.t_reservepercent "
           "from ddlreslnk_dbt rlnk where "
           "(rlnk.t_type = 3)and(rlnk.t_parentid = " + deal.DealID + ") "
           "and(rlnk.t_childid = -1)and(rlnk.t_lnkdate <= '" + ExecDate + "') "
           "order by rlnk.t_lnkdate desc, rlnk.t_id desc";

    rs = RsdRecordset(cmd);

    if (rs.MoveNext())
        ККС = rs.value("t_qualitycategory");
        ПР  = rs.value("t_reservepercent");
    end;

    if ((reslnk.QualityCategory == ККС)and(reslnk.ReservePercent == ПР))
        mm_error("Параметры риска не изменились");
     //   return 1;
    end;

    if (reslnk.isManual == "")
        GetRegistryValue("MMARK\\ОБЩИЕ\\КОНТРОЛЬ ПАРАМ. КРЕД-НОГО РИСКА", V_INTEGER, КонтрольПКР, stat);
        if(stat)
            mm_error ("Ошибка при получении значений реестра");
            return 1;
        end;

        if (КонтрольПКР != 1)
            НККС_ПР_дляКонтрагента(deal.PartyID, ККС, ПР);
            if (reslnk.QualityCategory < ККС)
                if (КонтрольПКР == 3)
                    mm_error("Категория качества " + reslnk.QualityCategory + " сделки меньше наихудшей " + ККС + " для контрагента");
                    return 1;
                else
                    reslnk.QualityCategory = ККС;
                end;
            end;

            if (reslnk.ReservePercent < ПР)
                if(КонтрольПКР == 3)
                    mm_error("Процент резерва " + reslnk.ReservePercent + " меньше наихудшего " + ПР + " для контрагента");
                    return 1;
                else
                    reslnk.ReservePercent = ПР;
                end;
            end;
        end;
    end;

    ККС = ПолучитьКатегориюКачества(deal.DealID, ExecDate);

    УстановитьЗначениеККС(reslnk.QualityCategory);
    if (not ActAccAfterChangeParm(FirstDoc, ExecDate, error))
        mm_error(error);
        return 1;
    end;
    УстановитьЗначениеККС(0);

    MM_InsertDLRESLNK(reslnk); // Вставляем запись в таблицу dlreslnk.dbt

    return 0;
END;

NameMacroFile = "mmchg_j.mac";
