/* ───────────────────────────────────────────────────────────────────────────┐
  RS-Bank                                        R-Style Software Lab Ltd
  Файл подсистемы "МБК"

  Печать подтверждения о перераспределении ресурсов (в RTF-файл).

CREATED : 11.02.00 LA
MODIFICATIONS: IL 03.10.2000 Общие ф-и вынес в mmscmt.mac
NOTES:

└─────────────────────────────────────────────────────────────────────────── */
import mmactx, DealsInter;

/*                              Настройка                                    */
VAR PRINT_ACCNUM = FALSE; /* Если этот параметр установлен в TRUE, то в поле
                         счета "ЛОРО" печатается номер лицевого счета,
                         иначе - код участника */

VAR pcmt_TemplateName = "FCNF050.dot"; /* Название файла с шаблоном формы */

/* ------------------------------------------------------------------------- */
/* Печать отчета, при p_accnick = TRUE вместо счета печатается код участника */
MACRO pcmt_Do( p_pointer, p_accnick )

 FILE pmpaym  ( pmpaym )  key 1; /* DocKind + DocumentID + Purpose */
 FILE dl_leg  ( dl_leg );
 FILE fininstr( fininstr );
 FILE person  ( person );
 FILE partcode( partcode );
 FILE wordfiles () txt append;

 RECORD l_dealrec ( dl_tick );

 setbuff( l_dealrec, p_pointer );

var 
 l_dealgroup  = getOperationGroup( l_dealrec.DealType ),
 l_optype     = "",                    /* вид операции */
 l_dsale      = isSale( l_dealgroup ), /* ?сделка является размещением */
 l_startdate  = date( 0,0,0 ),         /* Дата начала (валютирования) сделки */
 l_principal  = $0,                    /* Объем принципала */
 l_FIID       = 0,                     /* ID валюты */
 l_FIName     = "",                    /* Название валюты */
 l_FIISO      = "",                     /* Цифровой ISO-код валюты */
 l_FICcy      = "",                    /* ISO код лат. */
 l_maturity   = date( 0,0,0 ),         /* Дата завершения сделки */
 l_rate       = 0,                     /* % ставка */
 l_accnum     = "",                    /* № счета "ЛОРО" и все такое */
 l_tradername = "";                    /* ФИО дилера */

var l_next;

 /* Получить данные для печати */
 if( l_dsale )
  l_optype = "РАЗМЕЩЕНИЕ";
 else
  l_optype = "ПРИВЛЕЧЕНИЕ";
 end;

 /* Данные из транша */
 dl_leg.LegKind = LEG_KIND_DL_TICK;
 dl_leg.DealID = l_dealrec.DealID;
 dl_leg.LegID  = 1; /* Скрытый генеральный транш, по идее должно быть -1 */

 if( getEQ( dl_leg ) )
  l_startdate = dl_leg.Start;
  l_principal = dl_leg.Principal;
  l_FIID      = dl_leg.PFI;
  l_maturity  = dl_leg.Maturity;
  l_rate      = dl_leg.Price;
 else
  msgbox( "Не найден транш сделки" );
 end;

/* Получить название и коды ISO валюты */
 fininstr.FIID = l_FIID;
 if( getEQ( fininstr ) )
  l_FIName = fininstr.Name;
  l_FIISO  = fininstr.ISO_Number;
  l_FICcy  = fininstr.Ccy;
 end;

 /* Получить № счета "ЛОРО" либо код участника */
 if( p_accnick ) /* нужен код участника */
  partcode.PartyID  = l_dealrec.PartyID;
  partcode.CodeKind = 5;

  if( getEQ( partcode ) )
   l_accnum = trim( partcode.Code );
  end;

 else /* нужен № счета */ 
  pmpaym.DocKind    = 102; /* сделки МБК */
  pmpaym.DocumentID = l_dealrec.DealID;
  pmpaym.Purpose    = PM_PURP_PRINC;

  l_next = getGE( pmpaym );
  while( ( l_next ) and
         ( pmpaym.DocKind    == 102 ) and
         ( pmpaym.DocumentID == l_dealrec.DealID ) and
         ( pmpaym.Purpose    == PM_PURP_PRINC ) )

   if( ( pmpaym.ValueDate == l_startdate ) and
       ( (pmpaym.Payer=={OurBank}) == l_dsale ) ) /* сделка и платеж имеют одинаковые направления */

    l_accnum = trim( pmpaym.ReceiverAccount );
   end;

   l_next = next( pmpaym );
  end;
 end;

 /* ФИО дилера */
 person.Oper = l_dealrec.TraderID;
 if( getEQ( person ) )
  l_tradername = trim( person.Name );
 end;

 /* Печать отчета */
 var l_D = OpenWordDocumentByDot( pcmt_TemplateName );
 if( l_D == null ) return ""; end;

 l_D.Bookmarks( "Dealt"     ).Range.Text = string( l_dealrec.DealDate:m );
 l_D.Bookmarks( "Sent"      ).Range.Text = string( date:m ) + " " + string( time );

 l_D.Bookmarks( "XId"       ).Range.Text = trim( l_dealrec.DealCode );
 l_D.Bookmarks( "Product"   ).Range.Text = l_optype;
 l_D.Bookmarks( "Currency"  ).Range.Text = l_FIName + "\n" + l_FICcy;
 l_D.Bookmarks( "Principal" ).Range.Text = string( l_principal:a ) + "\n / " +
                                           curtostralt( l_principal, null, null, l_FIISO ) + " /";
 l_D.Bookmarks( "Price"     ).Range.Text = string( l_rate );
 l_D.Bookmarks( "ValueDate" ).Range.Text = string( l_startdate:m );
 l_D.Bookmarks( "Maturity"  ).Range.Text = string( l_maturity:m );
 l_D.Bookmarks( "ToAccount" ).Range.Text = l_accnum;
 l_D.Bookmarks( "Trader"    ).Range.Text = l_tradername;
 l_D.Bookmarks( "Phone"     ).Range.Text = BOPhone;
 l_D.Bookmarks( "Fax"       ).Range.Text = BOFax;
 l_D.Bookmarks( "Tekos"     ).Range.Text = string( BOTekos );

 return SaveRTFandCloseDoc( l_D, "FCNF050_" + trim( l_dealrec.DealCode ) + ".rtf" );
END;

