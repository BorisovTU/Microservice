/*
$Name:        msfo_e.mac
$Module:      МБ Кредиты
$Description: 2.7.1   Отражение затрат, в части, которую нужно будет отражать на расходах с 01.01.2019
*/
import mmlibr, MoCommon, mctplprm, OprInter, mmcnst_j, mmlibr, mmlb_j, mmcarry, msfo_mmupgrdlib;

PRIVATE VAR Currency, FirstDoc = NULL, ExecDate;

PRIVATE VAR MCAccDoc = TBFile("mcaccdoc.dbt", "W", 0),
            dlcomis  = TBFile("dlcomis.dbt", "W", 0);

PRIVATE CONST TRANSFER = "Перенос",
              RECOVERY = "Восстановление";

PRIVATE MACRO Проводка(deal_pd, Type, Sum, AccountCrd)
    VAR at;
    at = MM_Carry;
    at.date_carry       = ExecDate;
    at.PrimaryDoc       = deal_pd;
    at.FIIDPayer        = 
    at.FIIDReceiver     = deal_pd.GetLeg().PFI;
    at.Chapter          = 1;
    at.Ground           = "Перенос по МСФО-9";
    at.Sum              = Sum;

    if (Type == 1)
        at.CatPayer         = tdr_expenses;
        at.AccountReceiver  = AccountCrd;
    elif (Type == 2)
        at.CatPayer         = ПолучитьКатСчетаКорреспонденции(tdr_expenses, true, deal_pd, ExecDate);
        at.AccountReceiver  = AccountCrd;
    elif (Type == 3)
        at.CatPayer         = tdr_expenses;
        at.CatReceiver      = tdr_expcalcs;
    end;

    return at.carry();
END;

MACRO ExecuteStep(Buffer, Document)
   record deal(dl_tick);
   SetBuff(deal, Document);

   ExecDate = {curdate}; // Как дату передать ?

   VAR deal_pd, cmd = RsdCommand(RslDefCon, "SELECT * FROM DMSFO_UPGRADE_DBT WHERE T_DEALID = ?");
   cmd.AddParam("p1", RSDBP_IN); cmd.Value("p1") = deal.DealID;
   cmd.Execute();
   VAR rs = RsdRecordSet(cmd);
   IF (rs.MoveNext())
      VAR ContrID    = rs.Value("T_SUM1");   // ID договора обслуживания
      VAR Cur        = rs.Value("T_SUM2");   // Валюта
      VAR SumAfter   = rs.Value("T_SUM3");   // Сумма произведенных затрат, относящихся к периоду с 01.01.2019
      VAR SumBefore  = rs.Value("T_SUM4");   // Сумма произведенных затрат, относящихся к периоду до 01.01.2019
      VAR NDS        = rs.Value("T_SUM5");   // НДС
      VAR ComType    = rs.Value("T_SUM6");   // ID вида комиссии из ПЗО
      VAR AccTrnType = rs.Value("T_TEXT1");  // Вид проводки
      VAR Account    = rs.Value("T_TEXT2");  // Номер счета

      deal_pd = MMFirstDoc (deal.BOfficeKind, deal.DealID, true);

      InsertMCAccDoc(MCAccDoc, Account, deal.BOfficeKind, deal.DealID, deal_pd.GetLeg().PFI, ExecDate);
      if (AccTrnType == TRANSFER)
         if (not Проводка(deal_pd, 1, SumAfter, Account))
            return 1;
         end;
         if (not Проводка(deal_pd, 2, SumBefore, Account))
            return 1;
         end;
      elif (AccTrnType == RECOVERY)
         if (not Проводка(deal_pd, 3, SumAfter, Account))
            return 1;
         end;
      end;
      MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 14/*MN_UNALLOCATED*/, deal_pd.GetLeg().Start, SumAfter);
      if (not InsertDLCOMIS(dlcomis, deal.BOfficeKind, deal.DealID, ContrID, ComType, SumAfter, NDS, ExecDate, ExecDate))
         return 1;
      end;
   END;

   RETURN 0;
END;

NameMacroFile = "msfo_e.mac";