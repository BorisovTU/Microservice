/*
$Name:        mmp_j.mac
$Module:      МБ Кредиты
$Description: Обработка платежа
*/
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : mmp_j.mac
  Programmer  : Сабитов Р.Р.
  Description : Обработка платежа
  Comment     : 
└───────────────────────────────────────────────────────────────────────────*/
import InsCarryDoc, CurrInter, MmarkInter, mmlibr, mmcarry, mmcnst_j, mmplib, mpclb, mctplprm;
RECORD ПараметрыШага ("mmroprm.rec");

MACRO ProlongatePayments (tick, leg, deal_pd, dat, СчетОснДолг)

var    
    IP_id   = TArray,
    IPtype  = TArray,
    PP_id   = TArray,
    PPtype  = TArray,
    NewPR_ID;

    FILE pm ("pmpaym") key 1;

    macro filter
        return ((pm.PaymStatus==PM_READIED) or
                (pm.PaymStatus==PM_CARRYED_OUT_TO_PROLONG)) and
               (pm.ValueDate<=dat)
    end;

    macro ProlongPrincRetPayment (NewPR_ID:@variant)
    var 
        dbt_prop = TRecHandler ("pmprop"),
        crd_prop = TRecHandler ("pmprop"),
        rm = TRecHandler ("pmrmprop");

        if (FindPayment(ПараметрыШага.PaymentID, 0, 0, 0, 0, true, 
                        pm, dbt_prop, crd_prop, rm) != 0)
            mm_error ("Не найден платеж по сделке");
            return false;
        end;

        if (not MM_PRLNG_InsertDocsPrinc(deal_pd, dat, pm, СчетОснДолг))
            return false;
        end;

        pm.ValueDate = ПараметрыШага.D2;
        pm.BaseAmount = pm.Amount = pm.FuturePayerAmount = pm.PayAmount = pm.RecallAmount = 0;

        if (not InsertPayment (NewPR_ID, pm, dbt_prop, crd_prop, rm))
            mm_error ("Ошибка при создании платежа");
            return false;
        end;

        IPtype.size = PPtype.size = IP_id.size = PP_id.size = 0;
        IP_id[0] = ПараметрыШага.PaymentID;
        IPtype[0] = OPR_ID_REAL;
        PP_id[0] = NewPR_ID;
        PPtype[0] = OPR_ID_TMPFILE;

        if( not PaymentsKvit( IP_id, IPtype, PP_id, PPtype, null, PMLINK_KIND_PROLONG) )
            mm_error( "Ошибка связывания платежей" );
            return false;
        end;

        return true;
    end;

    macro processPaym (paym)
        IP_id[IP_id.size] = pm.PaymentID;
//        if (PrcSetCntSchedFactSumByID(GetCntSchedID(MM_GetPercContractID(PC_IBC_CON, (String(paym.DocumentID) + " 1")), 3, pm.ValueDate), money(pm.FuturePayerAmount), pm.ValueDate, date(0,0,0)) != 0)
//            return false;
//        end;   
        ПараметрыШага.CapitalizationSum = ПараметрыШага.CapitalizationSum + pm.FuturePayerAmount;

        return true;
    end;

    macro CapitalizePayments (NewPR_ID)

    var ok, i, sz, ККС;

        if( isSale( GetOperationGroup( tick.DealType ) ) )
            ККС = ПолучитьКатегориюКачества( tick.DealID, ПараметрыШага.D1 );
            if( ККС == 0 )
                mm_error ("Не задана категория качества сделки");
                return false;
            elif( ККС != 1 )
                mm_error ("Неверная категория качества сделки - "+ККС+"-я|(должна быть 1-я)");
                return false;
            end;
        end;

        IPtype.size = PPtype.size = IP_id.size = PP_id.size = 0;

        pm.DocKind = tick.BofficeKind;
        pm.DocumentID = tick.DealID;
        pm.Purpose = PM_PURP_PERCENT;
        pm.SubPurpose = 0;

        ok = GetGE(pm);
         
        while (ok and limiter(tick,pm) and (pm.Purpose==PM_PURP_PERCENT))
            if (filter and (not processPaym(pm)))
                return false;
            end;
            ok = Next(pm);
        end;

        i = 0;
        sz = IP_id.size;

        if (sz>0)
            while (i<sz)
                IPtype[i] = OPR_ID_REAL;
                PP_id[i] = NewPR_ID;
                PPtype[i] = OPR_ID_TMPFILE;
                i = i + 1;
            end;

            if( not PaymentsKvit( IP_id, IPtype, PP_id, PPtype, null, PMLINK_KIND_CAPITAL) )
                mm_error( "Ошибка связывания платежей" );
                return false;
            end;
        end;

        return MM_PRLNG_InsertDocsPerc( deal_pd, dat, ПараметрыШага.CapitalizationSum, СчетОснДолг, leg );
    end;


//    deal_pd = MMFirstDoc (tick,leg, true);

    if (not ProlongPrincRetPayment(@NewPR_ID))
        return false;
    end;

    if (ПараметрыШага.Capitalization and (not CapitalizePayments(NewPR_ID)))
        return false;
    end;

    return true;
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document)
    FILE leg (dl_leg);
    record tick (dl_tick);
    var deal_pd_new, СчетОснДолг;
    SetBuff (tick,Document);
    DL_PrepareCarryBuffer (Buffer);

    if( ПараметрыШага.D1 > {curdate} )
        mm_error("Преждевременное выполнение шага запрещено.");
        return 1;
    end;

    leg.LegKind = LEG_KIND_DL_TICK;
    leg.DealID = tick.DealID;
    leg.LegID = 1;

    if (not GetEq(leg))
        mm_error ("Не найден транш сделки");
        return 1;
    end;

    if (tick.ParentID > 0)
        var cl_pd = MMFirstDoc (208, tick.ParentID, true);
        if (ПараметрыШага.D2 > cl_pd.GetLeg().Maturity)
            mm_error ("Новая дата валютирования не входит в срок действия договора КЛ");
            return 1;
        end;
    end;

    /*Изменить срочность*/  
    deal_pd_new = MMFirstDoc (DL_IBCDOC, tick.DealID, true);
    deal_pd_new.SetParametr(MC_TYPE_PARAMETR_FINDATE, ПараметрыШага.D2);

    if (MC_GetKindSecurCredit(deal_pd_new.tick.rec.PartyID) == 1)
        deal_pd_new.SetUsParametr(MC_TYPE_USPARAMETR_STATECONT, 2);
        if( not deal_pd_new.ReOpenAccount(tdr_rest(deal_pd_new.GetTick().FlagTypeDeal), СчетОснДолг, NULL, deal_pd_new.GetFIRoleByCategory(tdr_rest(deal_pd_new.GetTick().FlagTypeDeal), deal_pd_new.GetLeg().PFI), NULL, ПараметрыШага.D1, deal_pd_new.GetLeg().PFI))
            return false;
        end;
    else
        /* Получаем счет ОД после пролонгации (на ее дату) */
        if( not deal_pd_new.ActualCheckPeriod(tdr_rest(deal_pd_new.GetTick().FlagTypeDeal), СчетОснДолг, NULL, deal_pd_new.GetFIRoleByCategory(tdr_rest(deal_pd_new.GetTick().FlagTypeDeal), deal_pd_new.GetLeg().PFI), NULL, ПараметрыШага.D1, deal_pd_new.GetLeg().PFI))
            return false;
        end;
    end;

    deal_pd_new.SetParametr(MC_TYPE_PARAMETR_FINDATE, ПараметрыШага.D1);

    ПараметрыШага.CapitalizationSum = 0;
    if (not ProlongatePayments (tick, leg, deal_pd_new, ПараметрыШага.D1, СчетОснДолг))
        return 1;
    end;

    return 0;
END;

NameMacroFile = "mmp_j.mac";
