/*
$Name:        mmscan.mac
$Module:      МБК
$Description: Печать отчета сканирования
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.0                                          R-Style Software Lab

  File Name   : mmscan.mac
  Created     : 26.04.99
  Programmer  : Сабитов Р.Р.
  Description : Печать отчета сканирования

└───────────────────────────────────────────────────────────────────────────*/

IMPORT globals, RsbDataSet, PTInter;

RECORD deal_firstdoc (dl_tick); 
record svop_firstdoc( dl_comm );
RECORD party(party);

PRIVATE VAR
    idx = 0,
    first = true;

VAR PrintOrderOutFileName = "",
    IsGroupAction = false;

PRIVATE const Ошибка   = 0,
    Первый             = 1,
    Последний          = 2,
    ActionOper         = 0,
    ActionPcAdd        = 1,
//    ActionReserv = 2,
    ActionPenalty      = 3,
    ActionPrintConf    = 4,
    ActionDelOp        = 5,
    ActionDelServOp    = 6,
    ActionToPrepServ   = 7,
    ActionServOper     = 8,
    ActionUserFunc     = 9,
    ActionActRiskParm  = 10,
    ActionPrintTickets = 11;

MACRO PrintPageBreak(fileName)
  var saveFileName = "";
  saveFileName = SetOutput(fileName, TRUE);
  println("\f");
  SetOutput(saveFileName, TRUE);
END;

MACRO GetTitle(Action)
  if (Action == ActionOper)
    return "Отчет о групповом создании операций для сделок";
  elif (Action == ActionPcAdd)
    return "Отчет о групповом начислении процентов для сделок";
//  elif (Action == ActionReserv)
//    return "Отчет о групповом формировании резерва для сделок";
  elif (Action == ActionPenalty)
    return "Отчет о групповой генерации штрафов для сделок";
  elif (Action == ActionPrintConf)
    return "Отчет о групповом создании подтверждений для сделок";
  elif (Action == ActionDelOp)
    return "Отчет о массовом переводе в отложенные";
  elif (Action == ActionDelServOp)
    return "Отчет о массовом удалении сервисных операций";
  elif (Action == ActionToPrepServ)
    return "Отчет о массовом переводе в отложенные сервисных операций";
  elif (Action == ActionServOper)
    return "Отчет о групповом формировании резерва для сделок";
  elif (Action == ActionUserFunc)
    return "Отчет о массовом выполнении Функции Пользователя";
  elif (Action == ActionPrintTickets )
    return "Печать тикета сделки";
  else
    return "Отчет о выполнении массовой операции для сделок";
  end;
END;

PRIVATE MACRO GetMessage(Action)
  if (Action == ActionOper)
    return "Операция создана успешно";
  elif (Action == ActionPcAdd)
    return "Проценты начислены успешно";
//  elif (Action == ActionReserv)
//    return "Резерв сформирован успешно";
  elif (Action == ActionPenalty)
    return "Штрафы сгенерированы успешно";
  elif (Action == ActionPrintConf)
    return "Потдверждение создано успешно";
  elif (Action == ActionDelServOp)
    return "Операция удалена";
  elif (Action == ActionToPrepServ)
    return "Операция переведена в отложенные";
  elif (Action == ActionServOper)
    return "Операция выполнена успешно";
  else
    return "Действие выполнено успешно";
  end;
END;

MACRO PrintActRisk(Вызов, firstdoc, Статус, Сообщение, Action)
    var
        KKC = "", ResPerc = "",
        isChg = "", DataSet = NULL;

     SetBuff(deal_firstdoc,firstdoc);

    if (Вызов == 1)
        [ ];
        [                                                      Протокол актуализации параметров крдитного риска                                                             ];
        [ ];
        [                                                                   Дата: ##########                                                                                ]({curdate}:f);
        [ ];
        [ ┌──────┬──────────────┬────────────────┬──────────┬────────────────┬───────────┬────────────────────────────────────────────────────────────────────────────────┐ ];
        [ │  NN  │ Номер сделки │   Контрагент   │ Новая КК │ Новый процент  │ Изменение │               Комментарии                                                      │ ];
        [ │      │              │                │          │ резервирования │    КК     │                                                                                │ ];
        [ ├──────┼──────────────┼────────────────┼──────────┼────────────────┼───────────┼────────────────────────────────────────────────────────────────────────────────┤ ];
    elif (Вызов == 0)
        idx = idx + 1;

        if (not first)
            [ ├──────┼──────────────┼────────────────┼──────────┼────────────────┼───────────┼────────────────────────────────────────────────────────────────────────────────┤ ];
        end;

        first = false;

        ПолучитьСубъекта(deal_firstdoc.PartyID, party);

        if (Статус != 0)
            KKC = "";
            ResPerc = "";
            isChg = "";
        else
            DataSet = TRsbDataSet("select t_qualitycategory, t_reservepercent " +
                                   " from ddlreslnk_dbt " +
                                   " where (t_type = 3) and (t_parentid = " + deal_firstdoc.DealID + ")" +
                                   " and (t_childid = -1) " +
                                   " and (t_lnkdate <= '"+ {curdate} + "')" +
                                   " order by t_lnkdate desc, t_id desc");

            DataSet.MoveNext; 
            KKC = DataSet.QualityCategory;
            ResPerc = DataSet.ReservePercent;
            isChg = "Да";
        end;

        [ │ #### │ ############ │ ############## │ ######## │ ############## │ ######### │ ############################################################################## │ ](idx:c, deal_firstdoc.DealCode:c, party.ShortName:c, KKC:c, ResPerc:c, isChg:c, Сообщение:l);
    elif (Вызов==2)         
        [ └──────┴──────────────┴────────────────┴──────────┴────────────────┴───────────┴────────────────────────────────────────────────────────────────────────────────┘ ];
        [ ];
        [ Операционист: #################################### ]({Name_Oper});
    end;
END;

MACRO PrintTickets(Вызов, firstdoc, Статус, Сообщение, Action)
    var DataSet = NULL;
    
    SetBuff(deal_firstdoc, firstdoc);
    
    ПолучитьСубъекта(deal_firstdoc.PartyID, party);
    
    DataSet = TRsbDataSet("select dl_leg.t_start, dl_leg.t_maturity, dl_leg.t_principal, dl_leg.t_pfi, dl_leg.t_price, FININSTR.T_NAME " +
                          "from ddl_leg_dbt dl_leg, DFININSTR_DBT FININSTR where dl_leg.t_dealid = " + deal_firstdoc.DealID + " and dl_leg.t_pfi = FININSTR.T_FIID");

    DataSet.MoveNext; 

    if ((Вызов != Первый) and (Вызов != Последний))
        println("Номер сделки в БО: "                      + deal_firstdoc.DealCode);
        println("Дата заключения сделки: "                 + deal_firstdoc.DealDate);
        println("Дата начала размещения/ привлечения: "    + DataSet.Start);
        println("Дата окончания размещения/ привлечения: " + DataSet.Maturity);
        println("Контрагент по сделке: "                   + party.ShortName);
        println("Направление сделки: "                     + deal_firstdoc.DealType);
        println("Сумма и валюта сделки: "                  + DataSet.Principal + " " + DataSet.NAME);
        println("Ставка: "                                 + DataSet.Price/100); 
        println("Особые условия: "                         + deal_firstdoc.Comment);

        println();
    end;
END;


MACRO Печать_ОтчетСканирования (Вызов, firstdoc, Статус, Сообщение, Action)

   /* if( Action == ActionServOper ) return; end;*///  77218  

    file out() txt;
    ARRAY tmpMessage;
    var i = 0, DocNumber = "";
    if((Action == ActionDelServOp) or (Action == ActionToPrepServ) or (Action == ActionServOper))
       setbuff (svop_firstdoc,firstdoc); // сервисная операция
       DocNumber = svop_firstdoc.CommCode;
    else
       setbuff (deal_firstdoc,firstdoc);
       DocNumber = deal_firstdoc.DealCode;
    end;
    IsGroupAction = true;

    if (Action == ActionActRiskParm)
        PrintActRisk(Вызов, firstdoc, Статус, Сообщение, Action);
        return;
    end;
    
    if (Action == ActionPrintTickets)
        PrintTickets(Вызов, firstdoc, Статус, Сообщение, Action);
        return;
    end;


    if (Вызов==Первый)
[         #](GetTitle(Action));
[+-------+-------+------------------------------------------------------------+];
[|  Код  |№ошибки|                 Сообщение                                  |];
[| сделки|       |                                                            |];
[+-------+-------+------------------------------------------------------------+];
    elif (Вызов==Ошибка)
            if (Статус == 0)
              Сообщение = GetMessage(Action);
              if(PrintOrderOutFileName != "") 
                PrintPageBreak(PrintOrderOutFileName);
              end;

            end;
            strsplit(Сообщение, tmpMessage, 60, 60, 1);
            
[|#######|#######|############################################################|](DocNumber,Статус,tmpMessage(i));
            i = i + 1;
            while (valType(TmpMessage(i)) == V_STRING)
[|       |       |############################################################|](TmpMessage(i));
              i = i + 1;
            end;
        elif (Вызов==Последний)         
[+-------+-------+------------------------------------------------------------+];
          if(PrintOrderOutFileName != "") 
            open(out, PrintOrderOutFileName);
            ViewFile(out);
          end;

    end;
END;
