/*
$Name:        msfo_c.mac
$Module:      МБ Кредиты
$Description: 2.5 Перенос остатков неиспользованных лимитов невозобновляемых кредитных линий на соответствующие внебалансовые счета 
*/
import mmlibr, MoCommon, mctplprm, OprInter, mmcnst_j, mmlibr, mmlb_j, mmcarry;;

PRIVATE VAR Currency, FirstDoc = NULL;

PRIVATE MACRO Проводка(
   КатегорияДебет, СчДебет, КатегорияКредит, СчКредит,
   Глава, Сумма, Основание, ExecDate,
   ВалДебет, ВалКредит)

   var at = MM_Carry;

   MM_ByDefault(ВалДебет,  Currency);
   MM_ByDefault(ВалКредит, Currency);

   at.date_carry         = ExecDate;
   at.PrimaryDoc         = FirstDoc;
   at.AccountPayer       = СчДебет;
   at.AccountReceiver    = СчКредит;
   at.CatPayer           = КатегорияДебет;
   at.CatReceiver        = КатегорияКредит;
   at.FIIDPayer          = ВалДебет;
   at.FIIDReceiver       = ВалКредит;
   
   IF (at.CatPayer == tdr_corespacc_vb)
      at.FIIDPayer       = NATCUR;
      at.FiRolePayer     = FIROLE_CORACC_ACTIVE;
   END;
   
   IF (at.CatReceiver == tdr_corespacc_vb)
      at.FIIDReceiver    = NATCUR;
      at.FiRoleReceiver  = FIROLE_CORACC_ACTIVE;
   END;
   
   IF (ВалДебет == Currency)
      at.SumPayer        = Сумма;
   ELIF (ВалКредит == Currency)
      at.SumReceiver     = Сумма;
   ELSE
      RETURN 1;
   END;

   at.Chapter            = Глава;
   at.Ground             = Основание;

   IF (at.carry())
      SetParm(1, at.AccountPayer);
      SetParm(3, at.AccountReceiver);
      RETURN 0;
   ELSE
      RETURN 1;
   END;
END;


PRIVATE MACRO GetCategID(CatName, ID, Num)
   VAR cmd = RsdCommand(RslDefCon,
   "SELECT T_ID, T_NUMBER FROM DMCCATEG_DBT mt WHERE mt.T_CODE = ? AND MT.T_LEVELTYPE = 1");
   cmd.AddParam("p1", RSDBP_IN); cmd.Value("p1") = CatName;
   cmd.Execute();
   VAR rs = RsdRecordset(cmd);
   IF (rs.MoveNext())
      SetParm(1, rs.Value(0));
      SetParm(2, rs.Value(1));
      RETURN TRUE;
   END;

   RETURN FALSE;   
END;

PRIVATE MACRO GetAcc(DocKind, DocID, CatNum, CatID, ActiveDate)
   VAR cmd = RsdCommand(RslDefCon,
   "SELECT * FROM DMCACCDOC_DBT mc WHERE MC.T_DOCKIND = ? AND MC.T_DOCID = ? AND MC.T_CATNUM = ? AND MC.T_CATID = ? AND MC.T_ACTIVATEDATE >= ?");
   cmd.AddParam("p1", RSDBP_IN); cmd.Value("p1") = DocKind;
   cmd.AddParam("p2", RSDBP_IN); cmd.Value("p2") = DocID;
   cmd.AddParam("p3", RSDBP_IN); cmd.Value("p3") = CatNum;
   cmd.AddParam("p4", RSDBP_IN); cmd.Value("p4") = CatID;
   cmd.AddParam("p5", RSDBP_IN); cmd.Value("p5") = ActiveDate;

   cmd.Execute();
   VAR rs = RsdRecordset(cmd);
   IF (rs.MoveNext())
      RETURN TRUE;
   END;
   RETURN FALSE;
END;

MACRO ExecuteStep(Buffer, Document)
   record deal(dl_tick);

   SetBuff(deal, Document);

   VAR ExecDate = {curdate}; // Как дату передать ?

   VAR CatID = 0, CatNum = 0;
   IF (NOT GetCategID("-Кредит, неисп.", CatID, CatNum))
      RETURN 0;
   END;

   VAR CatID2 = 0, CatNum2 = 0;
   IF (NOT GetCategID("-УО, размещение", CatID2, CatNum2))
      RETURN 0;
   END;

   VAR FirstDoc = MMFirstDoc(DL_CREDITLN, deal.DealID, true);
   VAR Currency = FirstDoc.GetLeg().PFI;
   VAR Account;

   IF (GetAcc(deal.BOFFICEKIND, deal.DEALID, CatNum, CatID, date(1,1,2019)))

       IF (NOT GetAcc(deal.BOFFICEKIND, deal.DEALID, CatNum2, CatID2, date(1,1,2019)))
          FirstDoc.OpenAccount("-УО, размещение", Account, false, NULL, NULL, ExecDate, Currency);

          VAR Sum = FirstDoc.GetRestAccount("-Кредит, неисп.", ExecDate, Currency, 0, NULL);

          IF ( Проводка("-Кредит, неисп.", "",
                        "-УО, размещение", "",
                        0, Sum,
                       "Перенос по МСФО-9", 
                        ExecDate, NATCUR, Currency))
             RETURN 1;
          END;
       END;

       FirstDoc.CloseAccount("-Кредит, неисп.", ExecDate, NULL, Currency, false);
   END;
END;

NameMacroFile = "msfo_c.mac";