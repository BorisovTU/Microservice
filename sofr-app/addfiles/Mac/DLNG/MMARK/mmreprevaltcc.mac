/*--------------------------------------------------------------------------------------*/
/*  Имя файла        : mmrevaltcc.mac                                                   */
/*                                                                                      */
/*  Описание         : Реестр операций переоценки по ТСС                                */
/*                                                                                      */
/*  Программист      : Куперин М.В.                                                     */
/*                                                                                      */
/*  Создан           : 12.05.2010                                                       */
/*--------------------------------------------------------------------------------------*/
IMPORT RsbDataSet, FIInter, globals;

PRIVATE MACRO ToDate(Date)
    if (ValType(Date) == V_DATE)
        Date = String(Date:10:f);
    end;

    return Date;
END;

MACRO RevalTccPrintReport(commrec)
    var DataSet  = NULL,
        QueryStr = "", 
        idx      = 0;

    RECORD comm(dl_comm);
    SetBuff(comm, commrec);

    if (comm.flag7 == "")
        QueryStr = "select 'По договорам' as name, ";
    else
        QueryStr = "select 'По предметам залога' as name, ";
    end;

    QueryStr = QueryStr +
                "alg.t_sznamealg, decode(comm.t_flag6, 'X', 'Установлен', 'Не установлен') as flag6, ";

    if (comm.ClientID > 0)
        QueryStr = QueryStr + "partcode.t_code, party.t_shortname, ";
    else
        QueryStr = QueryStr + "' ' as code, ' ' as shortname, ";
    end;

    if (comm.Reserve != "")
        QueryStr = QueryStr + "comm.t_reserve, ";
    else
        QueryStr = QueryStr + "' ' as reserve, ";
    end;

    if (Date(comm.BeginDate) != Date(0, 0, 0))
        QueryStr = QueryStr + "comm.t_begindate, ";
    else
        QueryStr = QueryStr + "' ' as begindate, ";
    end;

    if (comm.AvoirKind > -1)
        QueryStr = QueryStr + "avr.t_avoirkind, avr.t_name as avrname, fininstr.t_fi_code, fininstr.t_name as fininstrname ";
    else
        QueryStr = QueryStr + "' ' as avoirkind, ' ' as avrname, ' ' as fi_code, ' ' as fininstrname ";
    end;

    QueryStr = QueryStr + "from ddl_comm_dbt comm, dnamealg_dbt alg";

    if (comm.ClientID > 0)
        QueryStr = QueryStr + ", dparty_dbt party, dpartcode_dbt partcode";
    end;

    if (comm.AvoirKind > -1)
        QueryStr = QueryStr + ", dfininstr_dbt fininstr, davrkinds_dbt avr";
    end;

    QueryStr = QueryStr +
                " where "
                "(comm.t_documentid = " + comm.DocumentID + ")"
                "and(alg.t_itypealg = 2115)and(alg.t_inumberalg = comm.t_value)";

    if (comm.ClientID > 0)
        QueryStr = QueryStr +
                    "and(party.t_partyid = comm.t_clientid)"
                    "and(partcode.t_partyid = party.t_partyid)and(partcode.t_codekind = 1)";
    end;

    if (comm.AvoirKind > -1)
        QueryStr = QueryStr +
                    "and(fininstr.t_fiid = comm.t_fiid)"
                    "and(avr.t_avoirkind = comm.t_avoirkind)";
    end;

    DataSet = TRsbDataSet(QueryStr);

    if (DataSet.MoveNext())
        println("                                                                                                          ");
        println("                                Реестр операции переоценки по ТСС                                       ");
        println("                                                                                                          ");
        println(" Дата операции: " + String(comm.CommDate:10:l:f) + "                                       ");
        println(" Код операции:  " + String(comm.CommCode:30:l) + "                                         ");
        println("                                                                                                          ");
        println(" Параметры учета:  " + String(DataSet.Name:l:z) + "                                                      ");
        println(" Параметры сделки: " + String(DataSet.SzNameAlg:l:z) + "                                                 ");
        println(" Обеспечение по договору: " + String(DataSet.Flag6:l:z) + "                                              ");
        println(" Параметры договора:                                                                                      ");
        println("     залогодатель: " + String(DataSet.Code:l:z) + "  " + String(DataSet.ShortName:l:z) + "              ");
        println("     номер: " + String(DataSet.Reserve:l:z) + "                                                          ");
        println("     дата:  " + String(ToDate(Date(DataSet.BeginDate)):l:z) + "                                          ");

        if (comm.flag7 == "X")
            println("                                                                                                          ");
            println(" Вид ценной бумаги: " + String(DataSet.AvoirKind:15:l:z) + "  " + String(DataSet.AvrName:l:z) + "       ");
            println(" Код ценной бумаги: " + String(DataSet.Fi_Code:15:l:z) + "  " + String(DataSet.FininstrName:l:z) + "    ");
        end;

        println("                                                                                                          ");

        println(" Реестр проводок по переоценке                                                                            ");
        println(" ┌────┬──────────────────────┬────────┬──────────────┬─────────────────────┬─────────┬─────────────┐       ");
        println(" │ NN │     Счет дебета      │ Валюта │ Сумма дебета │    Счет кредита     │ Валюта  │Сумма кредита│       ");
        println(" │    │                      │ дебета │              │                     │ кредита │             │       ");
        println(" ├────┼──────────────────────┼────────┼──────────────┼─────────────────────┼─────────┼─────────────┤       ");

        idx = 0;
        QueryStr = 
                    "select arh.t_account_payer, "
                    "arh.t_fiid_payer as t_code_currency_payer, "
                    "arh.t_sum_payer as t_sum_payer, "
                    "arh.t_account_receiver, "
                    "arh.t_fiid_receiver as t_code_currency_receiver, "
                    "arh.t_sum_receiver as t_sum_receiver, "
                    "arh.t_ground "
                    "from dacctrn_dbt arh, doprdocs_dbt oprdoc "
                    "where "
//                    "((oprdoc.t_status = 1)or(oprdoc.t_status = 2))"
//                    "and"
                    "(oprdoc.t_acctrnid = arh.t_acctrnid)"
                    "and(oprdoc.t_servdockind = 184)"
                    "and(oprdoc.t_servdocid = " + comm.DocumentID + ")";

        DataSet = TRsbDataSet(QueryStr);

        while (DataSet.MoveNext())
            if (idx > 0)
                println(" ├────┼──────────────────────┬────────┬──────────────┬─────────────────────┬─────────┬─────────────┤       ");
            end;

            println(" │" +
                                     String((idx + 1):4:c)                                               + "│" +
                                     String(DataSet.Account_Payer:22)                                  + "│" + 
                                     String(ПолучитьКодФинИн(Int(DataSet.Code_Currency_Payer)):8:c)    + "│" + 
                                     String(Money(DataSet.Sum_Payer):14)                               + "│" + 
                                     String(DataSet.Account_Receiver:21)                               + "│" + 
                                     String(ПолучитьКодФинИн(Int(DataSet.Code_Currency_Receiver)):9:c) + "│" + 
                                     String(Money(DataSet.Sum_Receiver):13)                            + "│       ");
            println(" │    ├──────────────────────┴────────┴──────────────┴─────────────────────┴─────────┴─────────────┤       ");
            println(" │    │" + String(DataSet.Ground:92) +                                                           "│       ");

            idx = idx + 1;
        end;

        if (idx > 0)
            println(" └────┴────────────────────────────────────────────────────────────────────────────────────────────┘       ");
        else
            println(" └────┴──────────────────────┴────────┴──────────────┴─────────────────────┴─────────┴─────────────┘       ");
        end;
    else
        println( "Нет обработанных сделок операцией № ", comm.CommCode);
    end;
END;
