/*
$Name:        mmclfnc.mac
$Module:      МБ Кредиты
$Description: Полезные функции, связанные с кредитными линиями МБК
*/


Import globals,BankInter,DealsInter, MmarkInter, PcRateInter,
       dlref, mmlibr, mmrestlist, mctplprm;

class CGenAgrRep()
  var
      NumDeal = "",
      Result  = "",
      Comment = "";      
end;

PRIVATE VAR TickToGenAgrRep = TArray;

PRIVATE VAR Deal = TBFile("dl_tick.dbt", "w");

MACRO GetСLTrnSum(CLID, Purpose)
    var Sum = TRsbDataSet("SELECT COALESCE(SUM(t_sum_payer),0) as sum_paym " +
                          "FROM dacctrn_dbt " +
                          "INNER JOIN dpmdocs_dbt ON dacctrn_dbt.t_acctrnid = dpmdocs_dbt.t_acctrnid " +
                          "INNER JOIN dpmpaym_dbt ON dpmpaym_dbt.t_paymentid = dpmdocs_dbt.t_paymentid " +
                          "WHERE t_dockind = 102 AND t_purpose = " + Purpose + " AND t_documentid IN " +
								"(SELECT t_dealid FROM DDL_TICK_DBT WHERE t_parentid = " + CLID + ")");

    var stat = Sum.MoveNext;
    if (stat)
        return Sum.sum_paym;
    end;
    return 0.0;
END;

MACRO LimitWasWrittenOff(CLID)
    VAR Res = TRsbDataSet("SELECT 1 " +
                          "FROM doprstep_dbt " +
                          "INNER JOIN doproper_dbt ON doproper_dbt.t_id_operation = doprstep_dbt.t_id_operation " +
                          "WHERE t_documentid = " + CLID + " AND doprstep_dbt.t_dockind = 208 AND t_isexecute = 'X' AND t_symbol = 'S'");
    if (Res.MoveNext)
        return true;
    end;
    return false;
END;

MACRO GetCLUnusedDebtLimit(CLID, DebtLimit)
    return (DebtLimit - GetСLTrnSum(CLID, PM_PURP_PRINC) + GetСLTrnSum(CLID, PM_PURP_PRINC_RET));
END;

MACRO GetCLUnusedIssuanceLimit(CLID, IssuanceLimit)
    return (IssuanceLimit - GetСLTrnSum(CLID, PM_PURP_PRINC));
END;

macro MakeCLCommonRef(Rate, DealType, PartyID, CommonRef)
var BIC_A, BIC_B, error, СделкаПривлечения = true;
var Bank1,Place1,RefCode,Bank2,Place2,
    LastNonZero,i,symb;

    if( isSale( getOperationGroup( DealType ) ) )
        СделкаПривлечения = FALSE;  /* Это сделка размещения */
    end;
     
    if(СделкаПривлечения)
    BIC_A = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
    if( error ) return 0; end;
    BIC_B = ПолучитьКодСубъекта( PartyID, PTCK_SWIFT, error );
    if( error ) return 0; end;
    else
        BIC_B = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
        if( error ) return 0; end;
        BIC_A = ПолучитьКодСубъекта( PartyID, PTCK_SWIFT, error );
        if( error ) return 0; end;    
    end;
     
    if (StrLen(BIC_A) < 8) return 0; 
    end;
    if (StrLen(BIC_B) < 8) return 0;
    end;
       
    if (BIC_A > BIC_B)
    Bank1 = SubStr(BIC_B,1,4);
        Place1 = SubStr(BIC_B,7,2);
    Bank2 = SubStr(BIC_A,1,4);
        Place2 = SubStr(BIC_A,7,2);
    else
        Bank1 = SubStr(BIC_A,1,4);
        Place1 = SubStr(BIC_A,7,2);
    Bank2 = SubStr(BIC_B,1,4);
        Place2 = SubStr(BIC_B,7,2);
    end;
       
    LastNonZero = 0;
    for (i, 1, StrLen(Rate))
        symb = SubStr(Rate,i,1);
        if ((symb != "0") and (symb != ",") and (symb != "."))
       	    LastNonZero = i;
        end;
    end;
       
    if (LastNonZero >=4) 
       	RefCode = SubStr(Rate,LastNonZero-3,4);
    else 
       	RefCode = SubStr(Rate,1,LastNonZero);
        while (StrLen(RefCode)<4)
       		RefCode = "0" + RefCode;
        end;
    end;
       
    CommonRef = Bank1+Place1+RefCode+Bank2+Place2; 
       
    SetParm(3, CommonRef);

  return 0;
END;


MACRO ПривязатьКЛ_к_ГС(DealID, GenAgrID)
    var
        cmd = "", rs = NULL, rsG = NULL, rep = NULL, DealCode = "", retVal = 0;

    if (GenAgrID > 0)   // привязка
        rs = TRsbDataSet("select * from ddl_tick_dbt tick, ddl_leg_dbt leg where" +
                          " leg.t_DealID = tick.t_DealID and tick.t_DealID = " + String(DealID));

        rsG = TRsbDataSet("select * from ddl_genagr_dbt gagr where" +
                          " gagr.t_GenAgrID = " + String(GenAgrID));

        if (not rs.MoveNext)
            rep = CGenAgrRep;
            rep.NumDeal = "";
            rep.Result  = "не привязан";
            rep.Comment = "Договор КЛ с ID = " + String(DealID) + " не найден.";
        else
            DealCode = rs.DealCode;
        end;

        if (not rsG.MoveNext)
            rep = CGenAgrRep;
            rep.NumDeal = "";
            rep.Result  = "не привязан";
            rep.Comment = "ГС с ID = " + String(GenAgrID) + " не найдено.";
        end;

        if (rs.GenAgrID != -1)
            rep = CGenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязан";
            rep.Comment = "Договор КЛ с ID = " + String(DealID) + " уже привязан к другому ГС.";
        end;

        if (date(rs.Start) < date(rsG.Start))
            rep = CGenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязан";
            rep.Comment = "Дата начала договора КЛ меньше даты начала ГС.";
        end;

        if ((date(rs.Start) + Int(rs.Duration)) > (date(rsG.Start) + Int(rsG.Duration)))
            rep = CGenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязан";
            rep.Comment = "Дата окончания договора КЛ больше даты окончания ГС.";
        end;

        if (rs.PartyID != rsG.PartyID)
            rep = CGenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязан";
            rep.Comment = "Контрагент по сделке не совпадает с контрагентом по ГС.";
        end;

        if ((rs.DealStatus == DL_READIED)and
            (rsG.Status != 10 ))
            rep = CGenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязан";
            rep.Comment = "Открытый договор КЛ можно привязывать только к открытому ГС.";
        end;

        // Использовать данные из ГС
        if (rsG.Tax == "X")
            Deal.rec.DealID = DealID;
            Deal.GetEQ();
            Deal.rec.Tax_Amount = rsG.Tax_Amount;
            Deal.Update();
        end;
        // Использовать данные из ГС
        if (rsG.Credit_Tax == "X")
            Deal.rec.DealID = DealID;
            Deal.GetEQ();
            Deal.rec.Credit_Tax_Amount = rsG.Credit_Tax_Amount;
            Deal.rec.Credit_Tax_Cur    = rsG.Credit_Tax_Cur;
            Deal.rec.Credit_Tax_Term   = rsG.Credit_Tax_Term;
            Deal.Update();
        end;

    elif (GenAgrID < 0)// отвязка
        rs = TRsbDataSet("select * from ddl_tick_dbt tick where" +
                          " tick.t_DealID = " + String(DealID));

        if (rs.MoveNext)
            DealCode = rs.DealCode;

            if (rs.DealStatus == DL_CLOSED)
                rep = CGenAgrRep;
                rep.NumDeal = rs.DealCode;
                rep.Result  = "не отвязан";
                rep.Comment = "Нельзя отвязать закрытый договор КЛ от ГС.";
            end;
        else
            rep = CGenAgrRep;
            rep.NumDeal = "";
            rep.Result  = "не отвязан";
            rep.Comment = "Договор КЛ с ID = " + String(DealID) + " не найден.";
        end;

        if (rs.GenAgrID == -1)
            rep = CGenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не отвязан";
            rep.Comment = "Договор КЛ с ID = " + String(DealID) + " не привязан к ГС.";
        end;

        rs = TRsbDataSet("select * from ddl_genagr_dbt gagr where" +
                          " gagr.t_GenAgrID = " + String(GenAgrID));

        if (rs.MoveNext)
            if (rs.Status == 20)
                rep = CGenAgrRep;
                rep.NumDeal = rs.DealCode;
                rep.Result  = "не отвязан";
                rep.Comment = "Нельза отвязать договор КЛ от закрытого ГС.";
            end;
        else
            // все равно отвяжем, если ГС не нашли
        end;
    else
        return 0;
    end;

    if (rep == NULL)
        cmd = "update ddl_tick_dbt tick set tick.t_GenAgrID = " + String(GenAgrID) + 
               " where tick.t_DealID = " + String(DealID);

        RsdCommand(cmd).Execute;

        rep = CGenAgrRep;
        rep.NumDeal = DealCode;
        if (GenAgrID > 0)
            rep.Result  = "привязан";
            rep.Comment = "Договор КЛ привязан к ГС.";
        else
            rep.Result  = "отвязан";
            rep.Comment = "Договор КЛ отвязан от ГС.";

        end;
    else
        retVal = 1;
    end;

   TickToGenAgrRep[TickToGenAgrRep.size] = rep;

    return retVal;
END;

MACRO Отчет_ПривязкиКЛ_к_ГС(GenAgrID)
    var
        ReportFile = NULL, rs = NULL, idx = 0;

    file RepFile() txt write;

    if (TickToGenAgrRep.size == 0)
        ;//ничего не делаем
    elif (TickToGenAgrRep.size == 1)
        msgbox(TickToGenAgrRep[0].Comment);
    else
        open(RepFile, GetTxtFileName("mmgenagr"));

        if (GenAgrID > 0)// привязка
            rs = TRsbDataSet("select * from ddl_genagrview_dbt gagr where" +
                              " gagr.t_GenAgrID = " + String(GenAgrID));
            rs.MoveNext;

            insert(RepFile, "Протокол привязки договоров КЛ к ГС " + rs.Code + ", контрагент " + rs.Party);
        else // отвязка
            insert(RepFile, "Протокол отвязки договоров КЛ от ГС");
        end;

        insert(RepFile, "┌────────────────────┬────────────────┬────────────────────────────────────────────────────────────────────────────────┐");
        insert(RepFile, "│    № договора КЛ   │   Результат    │                              Сообщение                                         │");
        insert(RepFile, "│                    │   процедуры    │                                                                                │");
        insert(RepFile, "├────────────────────┼────────────────┼────────────────────────────────────────────────────────────────────────────────┤");




        while (idx < TickToGenAgrRep.size)
            insert(RepFile, "│" + 
                               String(TickToGenAgrRep[idx].NumDeal:20)  + "│" +  
                               String(TickToGenAgrRep[idx].Result:16)   + "│" +  
                               String(TickToGenAgrRep[idx].Comment:80)  + "│"  );
    
            idx = idx + 1;
        end;

        insert(RepFile, "└────────────────────┴────────────────┴────────────────────────────────────────────────────────────────────────────────┘");

        close(RepFile);
        ViewFile(RepFile);
    end;

    TickToGenAgrRep.size = 0;

    return 0;
END;