/*
$Name:        mmcrcostref_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Отражение расходов по затратам"
*/

IMPORT mmlibr, mmlib, mmservop, mmcarry, SfInter;

PRIVATE CONST
    MM_COSTREF_ERR_GETCLSCOM = 11,
    MM_COSTREF_ERR_CALCCOST  = 12,
    MM_COSTREF_ERR_UNCOSTS   = 13,
    MM_COSTREF_ERR_RECPROC   = 14,
    MM_COSTREF_ERR_PERCTYPE  = 15;   

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0;

PRIVATE VAR
    ExecDate;

PRIVATE VAR mmcomiss  = TBfile("mmcomiss.dbt");
PRIVATE VAR sfcomiss  = TBfile("sfcomiss.dbt");

PRIVATE MACRO GetNDSRateIfСharged(dlcomis, ndsdate)
    var nds_rate:double = 0;
    if (sfcomiss.rec.PayNDS == 2) // облагается
        //return SP_GetNDS(sfcomiss.rec.Date);
        GetNDSRateByDate(nds_rate, BILNDSRATE_BASE, ndsdate);
        return nds_rate;
    else
        return 0;
    end;
END;

PRIVATE MACRO CreateTrn(Sum, Type, contr_pd, catid)
    if (Sum == 0)
        return true;
    end;
    VAR at, mccateg;
    at = MM_Carry;
    at.date_carry       = ExecDate;
    at.PrimaryDoc       = contr_pd;
    at.FIIDPayer        = NATCUR;
    at.FIIDReceiver     = contr_pd.GetContr().FIID;
    at.Chapter          = 1;
    at.Sum              = Sum;
    if (Type == 1)
        mccateg  = TBfile("mccateg.dbt");
        mccateg.rec.ID = catid;
        if (not mccateg.GetEQ())
            return false;
        end;
        at.CatPayer    = mccateg.rec.code;
        at.CatReceiver = tdr_expenses;
        at.Ground      = "Отражение операционных расходов по затратам  единовременно.";
    elif (Type == 2)
        at.CatPayer    = tdr_nds;
        at.CatReceiver = tdr_expenses;
        at.Ground      = "Отражение НДС уплаченного по затратам.";
    elif (Type == 3)
        at.CatPayer    = tdr_tax;
        at.CatReceiver = tdr_nds;
        at.Ground      = "Отражение НДС уплаченного на расходы.";
    end;
    return at.carry();
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    record sfcontr( sfcontr );
    SetBuff( sfcontr, Document );

    ID_Operation = IDOperation;
    ID_Step      = IDStep;

    var OprServDoc, contr_pd, query, RsbData, Сз = $0, ндс = 0.0, Сндс = $0, Нз;
    OprServDoc = GetOprServDoc();
    ExecDate = OprServDoc.rec.CommDate;

    // шаг Отражение расходов по затратам выполнен
    if (StepIsExecute(sfcontr.ID, DL_SFCONTR, "х"))
        return 0;
    end;
    // шаг учет затрат не выполнен
    if (not StepIsExecute(sfcontr.ID, DL_SFCONTR, "я"))
        return 0;
    end;

    contr_pd = MMFirstDoc (DL_SFCONTR, sfcontr.ID, true);
    query = "select * from ddlcomis_dbt " +
                          "where t_dockind = " + DL_SFCONTR + 
                          " and t_docid = " + sfcontr.ID;

    if (OprServDoc.rec.ComNumber > 0)
        query = query + " and t_comnumber = " + OprServDoc.rec.ComNumber + " and t_feetype = 6";
    end;
    RsbData = TRsbDataSet(query);
    while (RsbData.MoveNext)
        Нз = RsbData.Sum;
        
        mmcomiss.rec.FeeType = RsbData.FeeType;
        mmcomiss.rec.Number  = RsbData.ComNumber;
        if (not mmcomiss.GetEQ)
            SvOpSayError(sfcontr.Number, MM_COSTREF_ERR_GETCLSCOM, "Не удалось получить запись из справочника комиссий МБК");
            return 1;
        end;
        if( not SfGetComiss( RsbData.FeeType, RsbData.ComNumber, sfcomiss ) )
            SvOpSayError(sfcontr.Number, MM_COSTREF_ERR_GETCLSCOM, "Не удалось получить комиссию");
            return 1;
        end;     
        if (mmcomiss.rec.RecognitProc == 2)
            SvOpSayError(sfcontr.Number, MM_COSTREF_ERR_RECPROC, "Для равномерного распределения затрат в ОФР шаг должен выполняться по сделке");
            return 1;
        end;
        ConvSumCross(Нз, Нз, ExecDate, sfcomiss.rec.FIID_Comm, sfcontr.FIID);
        if (mmcomiss.rec.Type == 1)
            SvOpSayError(sfcontr.Number, MM_COSTREF_ERR_PERCTYPE, "Для учета затрат в процентных расходах в ОФР шаг должен выполняться по сделке");
            return 1;
        elif (Нз > 0)
            ндс  = GetNDSRateIfСharged(RsbData, ExecDate);
            Сндс = Нз/(100+ндс)*ндс;  
            Нз   = Нз - Сндс;
            if (not (CreateTrn(Нз, 1, contr_pd, mmcomiss.rec.CatID) and CreateTrn(Сндс, 2, contr_pd, mmcomiss.rec.CatID) and CreateTrn(Сндс, 3, contr_pd, mmcomiss.rec.CatID)))
                return 1;
            end;
        else
            SvOpSayError(sfcontr.Number, MM_COSTREF_ERR_UNCOSTS, "Нет затрат для отражения в ОФР");
            return 1;               
        end;   
    end;



    return 0;
END;

NameMacroFile = "mmcostref_j.mac";
