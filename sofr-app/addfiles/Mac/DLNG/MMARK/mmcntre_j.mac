/*
$Name:        mmcntre_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Исполнение входящих платежей"
*/
IMPORT "dlcarry.inc", mmcateg, mmlib, mmlibr, mmcnst_j,
       mmlb_j, mpclb, mmcarry, InsCarryDoc,
       mmadd_j, mmpaymentcls, "sp_car.mac", mmclfnc, mmpenalt, mmservop;

/* Массивы с информацией о исполняемых платежах для квитовки */
/* Инициализируются бэк-офисом */
PRIVATE VAR
    PlanIDs     = TArray,
    FactIDs     = TArray,
    KvitAmounts = TArray,
    ExecDate;

/*Глобальные переменные*/
PRIVATE VAR 
    contr_pd   = NULL,
    PlanPayms = TArray,     /*Массив планируемых обрабатываемых платежей*/
    FactPayms = TArray;     /*Массив фактических обрабатываемых платежей*/

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0;

/*Записать все платежи в массивы*/
PRIVATE MACRO GetAllPayms
    /*плановые платежи*/
  VAR
      i = 0,
      previd = -1;

  while (i < PlanIDs.size)
      if (previd == PlanIDs(i))
          PlanPayms(i) = PlanPayms(i-1);
          i = i+1;
      else
          PlanPayms(i) = RsbPayment(PlanIDs(i));	

          if (PlanPayms(i).PaymentID == 0)
              mm_error ("Не найден платеж");
              return false;
          end;	  
		  
          if (PlanPayms(i).ValueDate != ExecDate)
              if( not GetTrue( true, "Планируемая дата платежа "+
                                      "|не равна дате исполнения."+
                                      "|Исполнить платеж ?" )) 
                  return false;
              end;
          end;
		  
          previd = PlanIDs(i); 
      end;
	  
      i = i+1;
  end;

    /*фактические платежи*/
  i = 0;
  previd = -1;
  while (i<FactIDs.size)
      if (previd == FactIDs(i))
          FactPayms(i) = FactPayms(i-1);
      else
          if (FactIDs(i))
              FactPayms(i) = RsbPayment(FactIDs(i));
		  	  if (FactPayms(i).PaymentID == 0)
                  mm_error ("Не найден платеж");
                  return false;
              end;
              if (FactPayms(i).BaseFIID != contr_pd.GetContr().FIID)
                  mm_error ("Валюта платежа не соответствует валюте договора");
                  return false;
              end;			  
              previd = FactIDs(i);
          end;
      end;
      i = i+1;
  end;

  return true;
END;

PRIVATE MACRO KvitPayms ()
  VAR
    pml = TRecHandler("pmlink.dbt"),
    i = 0;

  // Kirakozov, По запросу #123612:
  // В этом блоке мы списываем квитуемые суммы с фактических платежей, т.к. в сишниках
  // это не работает: pmkvit.c, функция InitPMLINK, case PMLINK_KIND_KVITING, pml->PPSign = '\0'; 
  // а для того, чтобы списывалось, надо pml->PPSign = '-'; и вообще, там списывается
  // с FuturePayerAmount, а надо с FutureReceiverAmount
  i = 0; 
  while( (i < FactIDs.size) and (FactIDs(i) != NULL) and (KvitAmounts(i) > $0) )
      pml.rec.Amount = KvitAmounts(i);
      pml.rec.FIID   = FactPayms(i).BaseFIID;
      pml.rec.IPSign = "-";
      pml.rec.PPSign = "-";
      PlanPayms(i).LinkPayment(FactPayms(i), PMLINK_KIND_KVITING, pml);

      i = i + 1;
  end;
   
  return true;
END;

PRIVATE MACRO SimulateKvitPayments ()
  VAR
      PlanPaym    = NULL,
      MM_PaymObj  = NULL,
      NewPaym     = NULL;

    PlanPaym = RsbPayment(PlanPayms(0).PaymentID);
    MM_PaymObj = MM_RsbPayment(MMPAYM_CREATEBYPAYMOBJ, PlanPaym, PlanPaym.SubPurpose, KvitAmounts(0), ExecDate);
    NewPaym = MM_PaymObj.Payment;

    NewPaym.IsFactPaym = "X";
    NewPaym.PaymStatus = PM_READIED;
	
	NewPaym.ValueDate = ExecDate;

    FactPayms[FactPayms.size] = NewPaym;

    return KvitPayms();
END;

PRIVATE MACRO MakeFactPaym ()
  if ((PlanIDs.size==1) and
       (money(round(PlanPayms(0).FuturePayerAmount,2)) == money(round(KvitAmounts(0),2)) ) 
      ) 
  /*Просто ставим на платеж признак исполненного*/
      PlanPayms(0).PaymStatus = PM_FINISHED;
      PlanPayms(0).IsFactPaym = "X";  

      PlanPayms(0).FutureBaseAmount = 0;
      PlanPayms(0).ActuateFutureAmounts(TBA_AMOUNT);
      
      PlanPayms(0).ValueDate = ExecDate; 

      FactPayms(0) = PlanPayms(0);
  else
      return SimulateKvitPayments;
  end;

  return true;
END;

/***************************************************************************/
PRIVATE MACRO GetPayerAccAndPayFIID (i, Счет, ВП)
  VAR paym = NULL; 

  if (FactIDs(i))
      paym = FactPayms(i);
  else
      paym = PlanPayms(i);
  end;
  
  ВП = paym.PayerFIID;
  Счет = paym.FuturePayerAccount;
  
  SetParm (1, Счет);
  SetParm (2, ВП);
  
  return true;
END;

PRIVATE MACRO UNI_InsertDocs (i, КатегорияД, КатегорияК, ВПД, ВПК, SumD, SumC, Chapter, Ground, FIRoleReceiver)
  VAR СчетДебет = "",
       ВД        = contr_pd.GetContr().FIID,
       ct       = MM_Carry;

  ct.FIIDActive      = ВД;
  ct.CatReceiver     = КатегорияК;

  if (ValType(Chapter) == V_UNDEF)
      Chapter = 1;
  end;
  ct.Chapter         = Chapter;

  if ((ValType(КатегорияД) != V_STRING) OR (КатегорияД == ""))
      if (Chapter == 1)
          if (ValType(ВПД) == V_UNDEF)
              GetPayerAccAndPayFIID(i, СчетДебет, ВПД);
          else
              GetPayerAccAndPayFIID(i, СчетДебет);
          end;
          ct.AccountPayer    = СчетДебет;	  
      elif (Chapter == 3)
          ct.CatPayer        = tdr_corespacc_vb;
          ct.FiRolePayer     = FIROLE_CORACC_ACTIVE;
     end;
  else
    ct.CatPayer = КатегорияД;
    ct.FiRolePayer = FIROLE_FIDOC;
  end;

  if (ValType(ВПД) == V_UNDEF)
      ВПД = ВД;
  end;
  ct.FIIDPayer       = ВПД;

  if (ValType(ВПК) == V_UNDEF)
      ВПК = ВД;
  end;
  ct.FIIDReceiver    = ВПК;

  if (ValType(FIRoleReceiver) == V_UNDEF)
    ct.FiRoleReceiver = contr_pd.GetFIRoleByCategory(КатегорияК, ВПК);
  else
    ct.FiRoleReceiver = FIRoleReceiver;
  end;
  ct.Date_carry      = ExecDate;
  ct.Ground          = Ground;

  if (ValType(SumD) != V_UNDEF)
      ct.SumPayer = SumD;
  end;

  if (ValType(SumC) != V_UNDEF)
      ct.SumReceiver = SumC;
  end;  

  ct.PrimaryDoc      = contr_pd;
  ct.PaymentObj      = FactPayms(i);

  return ct.carry();
END;

PRIVATE MACRO InsertDocsComReceiving (i)
  VAR Ground = "Получение комиссии Банком";
  return UNI_InsertDocs (i, NULL, tdr_comreq, NULL, NULL, KvitAmounts(i), NULL, NULL, Ground, 10000 + FactPayms[i].SubPurpose);
END;

PRIVATE MACRO InsertDocs(i)
  if (KvitAmounts(i) == 0)
      return true;
  end;

  RSDCommand("truncate table dmmnalog_tmp").execute();

  if (PlanPayms(i).Purpose == PM_PURP_COMRECEIVING)
      return InsertDocsComReceiving (i);
  end;

  return true;
END;

PRIVATE MACRO InsertAllDocs
  VAR i = 0;

  while( i < PlanIDs.size )
      if (not InsertDocs (i))
          return false;
      end;
      i = i + 1;
  end;
  
  return true;
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep( Buffer, Document, DocKind, IDOperation, IDStep)

  record sfcontr( sfcontr );
  SetBuff( sfcontr, Document );
  DL_PrepareCarryBuffer (Buffer);

  ID_Operation = IDOperation;
  ID_Step      = IDStep;

  contr_pd = MMFirstDoc (DL_SFCONTR, sfcontr.ID, false);

  if (KvitAmounts[0] < $0)
      return 1;
  end;

  if (not GetAllPayms)
      return 1;
  end;

  if (FactIDs(0) == 0)
      /*Симуляция исполнения - фактический платеж создаем сами*/
      if (not MakeFactPaym ())
          return 1;
      end;
  else
      if (not KvitPayms)
          return 1;
      end;
  end;

  if (not InsertAllDocs)
      return 1;
  end;

  return 0;
END;

NameMacroFile = "mme_j.mac";
