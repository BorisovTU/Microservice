/*
$Name:        msfo_d.mac
$Module:      МБ Кредиты
$Description: 2.6  Учет корректировок на <нерыночность> на 01.01.2019
*/
import mmlibr, MoCommon, mctplprm, OprInter, mmcnst_j, mmlibr, mmlb_j, mmcarry;;

PRIVATE VAR Currency, FirstDoc = NULL;

PRIVATE MACRO Проводка(
   КатегорияДебет, СчДебет, КатегорияКредит, СчКредит,
   Глава, Сумма, Основание, ExecDate,
   ВалДебет, ВалКредит)

   var at = MM_Carry;

   MM_ByDefault(ВалДебет,  Currency);
   MM_ByDefault(ВалКредит, Currency);

   at.date_carry         = ExecDate;
   at.PrimaryDoc         = FirstDoc;
   at.AccountPayer       = СчДебет;
   at.AccountReceiver    = СчКредит;
   at.CatPayer           = КатегорияДебет;
   at.CatReceiver        = КатегорияКредит;
   at.FIIDPayer          = ВалДебет;
   at.FIIDReceiver       = ВалКредит;
   
   IF (at.CatPayer == tdr_corespacc_vb)
      at.FIIDPayer       = NATCUR;
      at.FiRolePayer     = FIROLE_CORACC_ACTIVE;
   END;
   
   IF (at.CatReceiver == tdr_corespacc_vb)
      at.FIIDReceiver    = NATCUR;
      at.FiRoleReceiver  = FIROLE_CORACC_ACTIVE;
   END;
   
   IF (ВалДебет == Currency)
      at.SumPayer        = Сумма;
   ELIF (ВалКредит == Currency)
      at.SumReceiver     = Сумма;
   ELSE
      RETURN 1;
   END;

   at.Chapter            = Глава;
   at.Ground             = Основание;

   IF (at.carry())
      SetParm(1, at.AccountPayer);
      SetParm(3, at.AccountReceiver);
      RETURN 0;
   ELSE
      RETURN 1;
   END;
END;


MACRO ExecuteStep(Buffer, Document)
   record deal(dl_tick);

   SetBuff(deal, Document);

   VAR ExecDate = {curdate}; // Как дату передать ?

   VAR cmd = RsdCommand(RslDefCon, "SELECT * FROM DMSFO_UPGRADE_DBT WHERE T_DEALID = ?");
   cmd.AddParam("p1", RSDBP_IN); cmd.Value("p1") = deal.DealID;
   cmd.Execute();
   VAR rs = RsdRecordset(cmd);
   IF (rs.MoveNext())
      VAR SumCor= rs.Value("T_SUM1"); // Корректировка стоимости по МСФО (+ доходы, - расходы) 
      VAR Neo   = rs.Value("T_SUM2"); // Неучтенная отсроченная разница (+ положительная, - отрицательная) 
      VAR Amo   = rs.Value("T_SUM3"); // Амортизированная стоимость МСФО на 01.01.2019 
      VAR EPS   = rs.Value("T_SUM4"); // Эффективная процентная ставка в % годовых  на 01.01.2019
      VAR Cost  = rs.Value("T_SUM5"); // Справедливая стоимость на ДПП
      VAR Mark  = rs.Value("T_SUM6"); // Рыночная процентная ставка в % годовых на ДПП

      IF (SumCor != 0)
         IF (IsSALE(deal.DealGroup))
            IF (SumCor > 0)
               // размещение, положительная корректировка: Дт <-Коррект. увелич. ст-ть разм. ср-в> Кт <Переходный фин. рез.>
               Проводка("-Коррект. увелич. ст-ть разм. ср-в", "",
                        "Переходный фин. рез.", "",     
                        0, SumCor,
                        "Перенос по МСФО-9", 
                       ExecDate, NATCUR, Currency);
            ELSE
               // размещение, отрицательная корректировка: Дт <Переходный фин. рез.> Кт <+Коррект. уменьш. ст-ть разм. ср-в>
               Проводка("Переходный фин. рез.", "",
                        "+Коррект. уменьш. ст-ть разм. ср-в", "",
                        0, Abs(SumCor),
                        "Перенос по МСФО-9", 
                       ExecDate, NATCUR, Currency);
            END;
         ELSE
            IF (SumCor > 0)
               // привлечение, положительная корректировка: Дт <Переходный фин. рез.> Кт <+Коррект. увелич. ст-ть привл. ср-в>
               Проводка("Переходный фин. рез.", "",
                        "+Коррект. увелич. ст-ть привл. ср-в", "",
                        0, Abs(SumCor),
                        "Перенос по МСФО-9", 
                       ExecDate, NATCUR, Currency);

            ELSE
               // привлечение, отрицательная корректировка: Дт <-Коррект. уменьш. ст-ть привл. ср-в> Кт <Переходный фин. рез.>
               Проводка("-Коррект. уменьш. ст-ть привл. ср-в", "",
                        "Переходный фин. рез.", "",
                        0, Abs(SumCor),
                        "Перенос по МСФО-9", 
                       ExecDate, NATCUR, Currency);
            END;
         END;
      END;

      VAR ObjectType = 0;
      IF   (deal.BOfficeKind == 102)
         ObjectType = 103;
      ELIF (deal.BOfficeKind == 208)
         ObjectType = 204;
      END;

      IF (Neo != 0)
         /* <Тип данных> = <Наблюдаемые>, <Тест на рыночность> = <Пройден>, <SPPI-тест> = <Пройден> */
         VAR catr = RsdCommand(RslDefCon, "SELECT * FROM DOBJATCOR_DBT WHERE T_OBJECT = LPAD(?, 34, '0') AND T_OBJECTTYPE = ? AND T_GROUPID IN (6,8,10) AND T_ATTRID = 1");
         catr.AddParam("p1", RSDBP_IN); cmd.Value("p1") = deal.DealID;
         catr.AddParam("p2", RSDBP_IN); cmd.Value("p2") = ObjectType;
         catr.Execute();
         VAR rsc = RsdRecordset(catr);
         IF (rsc.MoveNext())
            // <Тип данных> на <Ненаблюдаемые>, <Тест на рыночность> и <SPPI-тест> на <Не пройден>
            ConnectObjAttr(NULL, ObjectType, L_Z(deal.DealID, 34), 6,  2, NULL, "", {curdate});
            ConnectObjAttr(NULL, ObjectType, L_Z(deal.DealID, 34), 8,  2, NULL, "", {curdate});
            ConnectObjAttr(NULL, ObjectType, L_Z(deal.DealID, 34), 10, 2, NULL, "", {curdate});
         END;
      END;

      IF (EPS != 0)
         MM_ChangeNoteText(ObjectType, L_Z(deal.DealID, 34), 3, date(1,1,2019), EPS);
      END;

      IF (Cost != 0)
         MM_ChangeNoteText(ObjectType, L_Z(deal.DealID, 34), 6, date(1,1,2019), Cost);
      END;
   END;

   RETURN 0;
END;

NameMacroFile = "msfo_d.mac";