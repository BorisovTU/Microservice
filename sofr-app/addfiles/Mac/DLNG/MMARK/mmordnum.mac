/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : mmordnum.mac
  Programmer  : Велигжанин А.В.
  Comment     : Формирование номера договора.
└───────────────────────────────────────────────────────────────────────────*/
IMPORT PTInter, OprInter, DealsInter, mmmisc, BankInter, RsbDataSet, globals;


/* Виды договоров
*/
PRIVATE CONST
              ВД_Залог          = "з",
              ВД_Неизвестен     = "?";

/* Идентификатор референса
*/
PRIVATE CONST
              REF_ID = 26;

/* Переменные
*/
PRIVATE VAR
              Отладка = true;


/* Возвращает очередной сквозной номер договора,
   определеный через механизм референсов
*/
PRIVATE MACRO СквНомер()
var s = "";

  if(GenerateReference(REF_ID, s) != 0)
    msgbox("Ошибка при генерации референса");
  end;

  return s;
END;


/* Возвращает специальный код контрагента
   по договору
*/
PRIVATE MACRO НомерКл(Contractor)
var s = "";
  if((Contractor != -1) and (Contractor != {OurBank}))
    s = ПолучитьКодСубъектаДляСчета(Contractor);
  elif (Contractor == {OurBank})
     msgbox("Наш банк не может быть контрагентом"); 
  else
    msgbox("Не задан контрагент");
  end;
  return s;
END;


/* Формирование номера документа 'n'
   Возвращается правая часть номера, начиная со второго символа
   справа, до первого вхождения символа '/'.
   Если последний символ - не цифра, то она обрезается.
   Например: n="1/2/3в"  
   Результат:      "3"
*/
PRIVATE MACRO MakeNumber(n)
var i, s = String(n), c, l;
    
    if(s == "")
       return s;
    end;

    l = strlen(s);
    c = SubStr(s, l, 1);        /* последний символ*/
    if( (c < "0") or (c > "9") )     /* если не цифра */
      s = SubStr(s, 1, l - 1);       /* обрежем */
    end;
    i = 0;
    while(i < 2)
      if( Index(s, "/") == 0)
        return s;
      end;
      s = StrAfter(s, "/");  /* вырезаем начало строки до / */
      i = i + 1;
    end;

    return s;
END;

/* Возвращает макс.номер договора по всем договорам
   заданного вида, заключенным с данным контрагентом.
   Для определения используется цикл по всем договорам заданного вида, 
   заключенным с данным контрагентом.
*/
PRIVATE MACRO МаксКлНомер(DocKind, Contractor)
var
   ord = TBfile("dl_order"), Ok, n, MaxNum = 0;

   ord.keyNum = 5;      /* Contractor, DocKind, SignDate, OrderNumber */
   ord.rec.Contractor = Contractor;
   ord.rec.DocKind = DocKind;
   Ok = ord.GetGE;

   while(Ok)
      Ok= ((ord.rec.Contractor == Contractor) and
           (ord.rec.DocKind == DocKind));
      if(Ok)
         n = Int(MakeNumber(ord.rec.OrderNumber));
         if( n > MaxNum )
           MaxNum = n;
         end;
         Ok = ord.next;
      end;
   end;

   return MaxNum;

END;


/* Возвращает символ вида договора
*/
PRIVATE MACRO СимвВидаДоговора(DocKind,ContractKind)
var
   s = ВД_Неизвестен;

   if(DocKind == DL_MMPAWN) 
     /* договор залога МБК */
     s = ВД_Залог;
   end;

   return s;
END;


/* Формирует номер договора.
   Номер формируется по правилу: NNN/RRRRR/PPs,
   где   NNN - сквозной номер договора
       RRRRR - специальный номер субъекта-контрагента для л.сч.
          PP - порядковый номер договора данного вида, заключ. с контр.
           s - символ вида договора
  параметр err - передаваемый в Си-код, номер ошибки для вывода
                 стандартного сообщения из msgbank.dbt или 0,
                 если ошибок не было
*/
MACRO MM_GetOrderNumber( ord )
var s, a,b,err=0;

   s = СимвВидаДоговора( ord.DocKind, ord.ContractKind );
   a = СквНомер();
   b = НомерКл( ord.Contractor );

   if (a == "")
     err = 4792;   /* 4792 ошибка при генерации референса */
   elif (b == "")
     err = 7804;   /* 7804 не задан контрагент или клиент по сделке */
   elif (s == ВД_Неизвестен);
     err = 1301;   /* 1301 нет такого вида первичного документа */
   else 
     ord.OrderNumber = 
             String( a, "/",
                     b, "/",
                     МаксКлНомер(ord.DocKind, ord.Contractor ) + 1,
                                     s );
   end;

  return err;
END;


/* Проверка: номер договора должен быть
   задан.
*/
MACRO ПроверитьНомерДоговора( Дог )
var sqlstr,
    DataSet,    
    err=0;

  if(Дог.OrderNumber == "")
     err = 101;
  else
     sqlstr = "select t_OrderNumber from ddl_order_dbt where t_OrderNumber = '" + Дог.OrderNumber + "' and t_ContractKind = " + DL_ORDER_MMPAWN;
     if(Дог.ContractID > 0)
        sqlstr = sqlstr + " and t_ContractID != " + Дог.ContractID;
     end;
     DataSet = TRsbDataSet(sqlstr);      
     if (DataSet.MoveNext())
        err=106;
     end;   
  end;

  return err;

END;

