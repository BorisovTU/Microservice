/*--------------------------------------------------------------------------------------*/
/*  Имя файла        : mmrsrv.mac                                                       */
/*                                                                                      */
/*  Описание         : Сервисная операция: Формирование резерва по цб в обеспечении     */
/*                                                                                      */
/*  Программист      : Куперин М.В.                                                     */
/*                                                                                      */
/*  Создан           : 21.06.2013                                                       */
/*--------------------------------------------------------------------------------------*/
IMPORT RsbDataSet, mmservop, globals, mmcateg, mmlb_j, mmcnst_j;
/*Печать ошибок**************************************************************************/
MACRO PrintReportError(_ErrorArray)
    VAR 
        idx = 0,
        cntError = _ErrorArray.Size;

    AddSvOpReportLine("┌────────────┬─────────────────────────────┬──────┬─────────────────────────────────────────────────────────────────────────────────────────┐");
    AddSvOpReportLine("│    Код     │    Номер договора залога    │  №   │               Сообщение                                                                 │");
    AddSvOpReportLine("│  операции  │                             │ошибки│                                                                                         │");
    AddSvOpReportLine("├────────────┼─────────────────────────────┼──────┼─────────────────────────────────────────────────────────────────────────────────────────┤");

    while (idx < cntError)
        AddSvOpReportLine("│" +
                           string(_ErrorArray[idx].OpCode:12)  + "│" + 
                           string(_ErrorArray[idx].ObjCode:29) + "│" +  
                           string(_ErrorArray[idx].NumErr:6)   + "│" +  
                           string(_ErrorArray[idx].StrErr:89)  + "│"  );
        idx = idx + 1;
    end;

    AddSvOpReportLine("└────────────┴─────────────────────────────┴──────┴─────────────────────────────────────────────────────────────────────────────────────────┘");

    return true;
END;
/*Печать результатов работы**************************************************************/
MACRO OldReservAmount(ДатаРасчетаРезерва, ContractID)
    VAR ResLnk = TRsbDataSet("select oldrezsum.t_reserveamount from " +
                              "(" +
                              "  select rownum as t_rownum, rez.* from" +
                              "  (" +
                              "    select t_reserveamount " +
                              "    from ddlreslnk_dbt " +
                              "    where t_parentid = " + ContractID +
                              "    and t_reservedate <= " + "'" + Date(ДатаРасчетаРезерва) + "'" +
                              "    and t_ismanual = '0' " +
                              "    order by T_RESERVEDATE DESC, T_ID DESC " +
                              "  ) rez" +
                              ") oldrezsum " +
                              "where oldrezsum.t_rownum = 2");

    if (ResLnk.MoveNext())
        return ResLnk.ReserveAmount;
    else
        return 0;
    end;
END;    

MACRO PrintReport()
    VAR DataSet  = NULL,
         DSAccount = NULL,
         QueryStr = "", 
         flag     = true,
         A10 = 0, A11 = 0,
         acntrbacc = "";

    QueryStr = 
               "select dl_order.t_contractid, dl_comm.t_commcode, reslnk.t_reservedate, dl_order.t_ordernumber, " +
               "party.t_shortname, acntrez.t_account acntrezacc, " +
               "reslnk.t_reservepercent, dl_order.t_fiid, reslnk.t_reserveamount, reslnk.t_securityamount " +
               "from ddl_order_dbt dl_order, ddl_comm_dbt dl_comm, " + 
               "ddlreslnk_dbt reslnk, dparty_dbt party, " +
               "(" +
               "select dl_order.t_contractid, accdoc.t_account " +
               "from ddl_order_dbt dl_order, dmcaccdoc_dbt accdoc, dmccateg_dbt categ " +
               "where " +
               "(accdoc.t_dockind = dl_order.t_dockind)and" +
               "(accdoc.t_docid = dl_order.t_contractid)and" +
               "(categ.t_number = accdoc.t_catnum )and" +
               "(categ.t_code = 'Р, ц/б в обеспеч.')" +
               ") acntrez " +
               "where " +
               "reslnk.t_childid = dl_comm.t_documentid " +
               "and reslnk.t_parentid = dl_order.t_contractid " +
               "and party.t_partyid(+) = dl_order.t_contractor " +
               "and acntrez.t_contractid = dl_order.t_contractid " +
               "and reslnk.t_ismanual = '0' " +
               " and dl_comm.t_documentid = " + GetOprServDoc().rec.DocumentID;

    DataSet = TRsbDataSet(QueryStr);

    if (DataSet.MoveNext())    
        AddSvOpReportLine("  ");
        AddSvOpReportLine("  ");                                                         
        AddSvOpReportLine("  Номер операции  " + String(GetOprServDoc().rec.CommCode:21:l) + "           Дата расчета:  " + String(Date(DataSet.ReserveDate):10:c));
        AddSvOpReportLine("  Вид резерва:    Резервы по ц/б в обеспечении                          ");
        AddSvOpReportLine("  ");
        AddSvOpReportLine("┌──────────────────────────────┬────────────────────┬─────────────────────┬─────────────────────┬──────────┬───────────────────────┬─────────────────────────┬─────────────────────────┬───────────────────────┐");
        AddSvOpReportLine("│      № Договора залога       │    Залогодатель    │ Счет расчетной базы │     Счет резерва    │     %    │     Расчетная база,   │ Расчетная сумма резерва │ Ранее созданный резерв, │  Сумма корректировки, │"); 
        AddSvOpReportLine("│                              │                    │                     │                     │  резерва │          " + {CCYNatCur} + "          │                         │           " + {CCYNatCur} + "           │          " + {CCYNatCur} + "          │");

        while (flag)
            acntrbacc = "";
            A10 = OldReservAmount(DataSet.ReserveDate, DataSet.ContractID);
            A11 = DataSet.ReserveAmount - A10;

            DSAccount = TRsbDataSet("select distinct accdoc.t_account "
                                  "from ddl_order_dbt dl_order, dmcaccdoc_dbt accdoc, dmccateg_dbt categ "
                                  "where "
                                  "(accdoc.t_dockind = dl_order.t_dockind)and"
                                  "(accdoc.t_docid = dl_order.t_contractid)and"
                                  "(categ.t_number = accdoc.t_catnum )and"
                                  "(categ.t_code = '+Обесп. кр., ц/б')"
                                  "and dl_order.t_contractid = " + DataSet.ContractID);
            if (DSAccount.MoveNext())
                acntrbacc = DSAccount.account;
            end;

            AddSvOpReportLine("├──────────────────────────────┼────────────────────┼─────────────────────┼─────────────────────┼──────────┼───────────────────────┼─────────────────────────┼─────────────────────────┼───────────────────────┤");
            AddSvOpReportLine("│" + 
                               String(DataSet.OrderNumber:30:r)     + "│" +
                               String(DataSet.ShortName:20:l:z)     + "│" +
                               String(acntrbacc:21:z)               + "│" +
                               String(DataSet.acntrezacc:21:z)      + "│" +
                               String(DataSet.ReservePercent:10)    + "│" +
                               String(DataSet.SecurityAmount:23)    + "│" +
                               String(DataSet.ReserveAmount:25)     + "│" +
                               String(A10:25)                       + "│" +
                               String(A11:23)                       + 
                               "│");
            while (DSAccount.MoveNext())
                acntrbacc = DSAccount.account;
                AddSvOpReportLine("│" + 
                                   String(" ":30:r)         + "│" +
                                   String(" ":20:l:z)       + "│" +
                                   String(acntrbacc:21:z)   + "│" +
                                   String(" ":21:z)         + "│" +
                                   String(" ":10)           + "│" +
                                   String(" ":23)           + "│" +
                                   String(" ":25)           + "│" +
                                   String(" ":25)           + "│" +
                                   String(" ":23)           + 
                                   "│");
            end;

           flag = DataSet.MoveNext();
       end;

        AddSvOpReportLine("└──────────────────────────────┴────────────────────┴─────────────────────┴─────────────────────┴──────────┴───────────────────────┴─────────────────────────┴─────────────────────────┴───────────────────────┘");
    else
        AddSvOpReportLine("Нет обработанных договоров залога операцией № " + GetOprServDoc().rec.CommCode);
    end;

    return true;
END;
/****************************************************************************************/
MACRO OpFilter(_OprServDoc, _ContractID)
  return true;
END;
