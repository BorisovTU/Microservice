/*
$Name:        mmcostref_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Отражение расходов по затратам"
*/

IMPORT mmlibr, mmlib, mmservop, mmcarry, PTInter, MmarkInter, SfInter;

PRIVATE CONST
    MM_COSTREF_ERR_GETCLSCOM = 11,
    MM_COSTREF_ERR_CALCCOST  = 12,
    MM_COSTREF_ERR_UNCOSTS   = 13;

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0;

/* Массивы с информацией о исполняемых платежах для квитовки */
/* Инициализируются бэк-офисом */
PRIVATE VAR
    PlanIDs       = TArray,
    FactIDs       = TArray,
    KvitAmounts   = TArray,
    PlanValueDate = TArray,
    ExecDate;

PRIVATE VAR mmcomiss  = TBfile("mmcomiss.dbt");
PRIVATE VAR sfcomiss  = TBfile("sfcomiss.dbt");

PRIVATE MACRO GetNDSRateIfСharged(dlcomis, ndsdate)
    var nds_rate:double = 0;
    if (sfcomiss.rec.PayNDS == 2) // облагается
        //return SP_GetNDS(sfcomiss.rec.Date);
        GetNDSRateByDate(nds_rate, BILNDSRATE_BASE, ndsdate);
        return nds_rate;
    else
        return 0;
    end;
END;

PRIVATE MACRO CreateTrn(Sum, Type, deal_pd, FIRole)
    if (Sum == 0)
        return true;
    end;
    VAR at;
    at = MM_Carry;
    at.date_carry       = ExecDate;
    at.PrimaryDoc       = deal_pd;
    at.FIIDPayer        = NATCUR;
    at.FIIDReceiver     = deal_pd.GetLeg().PFI;
    at.Chapter          = 1;
    at.Sum              = Sum;

    if (Type == 1)
        if (isSale(GetOperationGroup(deal_pd.GetTick().DealType)))
            at.CatPayer = tdr_comP(deal_pd.GetTick().TypeDoc);
        else
            at.CatPayer = tdr_comexpP(deal_pd.GetTick().TypeDoc);
        end;
        at.CatReceiver    = get_expenses(deal_pd.GetTick().DealType, ExecDate);
        at.FIRoleReceiver = FIRole;
        at.Ground      = "Отражение процентных расходов по затратам.";

    elif (Type == 2)
        if (isSale(GetOperationGroup(deal_pd.GetTick().DealType)))
            at.CatPayer = tdr_plodP(deal_pd.GetTick().TypeDoc);
        else
            at.CatPayer = tdr_atodP(deal_pd.GetTick().TypeDoc);
        end;
        at.CatReceiver    = get_expenses(deal_pd.GetTick().DealType, ExecDate);
        at.FIRoleReceiver = FIRole;
        at.Ground      = "Отражение операционных расходов по затратам единовременно.";

    elif (Type == 3)
        if (isSale(GetOperationGroup(deal_pd.GetTick().DealType)))
            at.CatPayer = tdr_plodP(deal_pd.GetTick().TypeDoc);
        else
            at.CatPayer = tdr_atodP(deal_pd.GetTick().TypeDoc);
        end;
        at.CatReceiver    = get_expenses(deal_pd.GetTick().DealType);
        at.FIRoleReceiver = FIRole;
        at.Ground      = "Отражение процентных расходов по затратам.";
    elif (Type == 4)
        at.CatPayer       = tdr_nds;
        at.CatReceiver    = get_expenses(deal_pd.GetTick().DealType);
        at.FIRoleReceiver = FIRole;
        at.Ground      = "Отражение НДС уплаченного по затратам.";
    elif (Type == 5)
        at.CatPayer    = tdr_tax;
        at.CatReceiver = tdr_nds;
        at.Ground      = "Отражение НДС уплаченного на расходы.";
    end;
    return at.carry();
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    record deal( dl_tick );
    SetBuff( deal, Document );

    ID_Operation = IDOperation;
    ID_Step      = IDStep;
    var OprServDoc, deal_pd, query, RsbData, Сз = $0, ндс = 0.0, Сндс = $0, Нз, blocksymb, stat, FIRole;

    Нз = ПолучитьЗначениеПримечания(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 14/*MN_UNALLOCATED*/, {curdate});
    if (Нз == 0)
        return 0;
    end;
    // шаг учет затрат выполнен
    if (not StepIsExecute(deal.DealID, deal.BofficeKind, "я"))
        return 0;
    end;

    OprServDoc = GetOprServDoc();
    if (PlanIDs.size == 0)
        ExecDate = OprServDoc.rec.CommDate;
        if (not isServOpMode())
            ExecDate = {curdate};
        end;
    end;

    deal_pd = MMFirstDoc (DL_IBCDOC, deal.DealID, true);
	
    query = "select * from ddlcomis_dbt " +
                          "where t_dockind = " + DL_IBCDOC + 
                          " and t_docid = " + deal.DealID;

    if (OprServDoc.rec.ContractID > 0)
        query = query + " and t_contract = " + OprServDoc.rec.ContractID;
    end;
    if (OprServDoc.rec.ComNumber > 0)
        query = query + " and t_comnumber = " + OprServDoc.rec.ComNumber + " and t_feetype = 6";
    end;

    RsbData = TRsbDataSet(query);
    while (RsbData.MoveNext)
        mmcomiss.rec.FeeType = RsbData.FeeType;
        mmcomiss.rec.Number  = RsbData.ComNumber;
        if (not mmcomiss.GetEQ)
            SvOpSayError(deal.DealCode, MM_COSTREF_ERR_GETCLSCOM, "Не удалось получить запись из справочника комиссий МБК");
            return 1;
        end;
        if( not SfGetComiss( RsbData.FeeType, RsbData.ComNumber, sfcomiss ) )
            SvOpSayError(deal.DealCode, MM_COSTREF_ERR_GETCLSCOM, "Не удалось получить комиссию");
            return 1;
        end; 
        if (sfcomiss.rec.ReceiverID != {HeadBankID})
        FIRole = 10000 + RsbData.Id;
        if ((((deal.CashDecision == "") and (mmcomiss.rec.Type == 1)) or 
        ((deal.CashDecision == "X") and (mmcomiss.rec.Type == 2))) and (mmcomiss.rec.RecognitProc == 2))
            // Если вызов произведен из цепочки шагов сделки привлечения ?Исполнение исходящих платежей ОД? или из цепочки шагов сделки размещения ?Исполнение входящих платежей ОД?:
            blocksymb = GetBlockSymb(ID_Operation, ID_Step);
            if (((blocksymb == "G") and IsBUY(GetOperationGroup(deal.DealType))) or 
                ((blocksymb == "I") and IsSALE(GetOperationGroup(deal.DealType))))
                Сз = MM_CalcCosts(deal.BofficeKind, deal.DealID, ExecDate, 10/*CN_CONTEXT_PM_PRINC_EXEC*/, ID_Operation, ID_Step);
            else
                Сз = MM_CalcCosts(deal.BofficeKind, deal.DealID, ExecDate, 0);
            end;
            ConvSumCross(Сз, Сз, ExecDate, sfcomiss.rec.FIID_Comm, deal_pd.GetLeg().PFI);
            if ((Сз == -1) or (Сз == 0))
                if (not isServOpMode())
                   return 0;
                end;
                SvOpSayError(deal.DealCode, MM_COSTREF_ERR_CALCCOST, "Нет затрат для отражения в ОФР");
                return 1;
            end;

            Нз = ПолучитьЗначениеПримечания(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 14/*MN_UNALLOCATED*/, ExecDate);
            MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 14, ExecDate, money(Нз - Сз));
        end;

        if ((deal.CashDecision == "") and (mmcomiss.rec.Type == 1) and (mmcomiss.rec.RecognitProc == 2))
            ндс  = GetNDSRateIfСharged(RsbData, ExecDate);
            Сндс = Сз/(100+ндс)*ндс;  
            Сз   = Сз - Сндс;
            if ((PlanValueDate.size > 0) and (PlanValueDate[0] > ExecDate))
                stat = CreateTrn(Сз, 3, deal_pd, FIRole);
            else
                stat = CreateTrn(Сз, 1, deal_pd, FIRole);
            end;
            if (not (stat and CreateTrn(Сндс, 4, deal_pd, FIRole) and CreateTrn(Сндс, 5, deal_pd, FIRole)))
                return 1;
            end;
        elif ((deal.CashDecision == "X") and (mmcomiss.rec.Type == 2) and (mmcomiss.rec.RecognitProc == 2))
            if (not CreateTrn(Сз, 3, deal_pd, FIRole))
                return 1;
            end;
        else
            Нз = ПолучитьЗначениеПримечания(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 14/*MN_UNALLOCATED*/, ExecDate);
            ConvSumCross(Нз, Нз, ExecDate, sfcomiss.rec.FIID_Comm, deal_pd.GetLeg().PFI);
            if (Нз > 0)
                ндс  = GetNDSRateIfСharged(RsbData, ExecDate);
                Сндс = Нз/(100+ндс)*ндс; 
                Нз   = Нз - Сндс;
            else
                SvOpSayError(deal.DealCode, MM_COSTREF_ERR_UNCOSTS, "Нет затрат для отражения в ОФР");
                return 1;               
            end;
            MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 14, ExecDate, $0);
            if (not (CreateTrn(Нз, 2, deal_pd, FIRole) and CreateTrn(Сндс, 4, deal_pd, FIRole) and CreateTrn(Сндс, 5, deal_pd, FIRole)))
                return 1;
            end;
        end;
		end;
    end;

    return 0;
END;

NameMacroFile = "mmcostref_j.mac";
