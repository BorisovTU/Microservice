/*
$Name:        mmcostadj_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Корректировка стоимости"
*/

IMPORT mmlibr, mmcarry, mmlib, mmlb_j;

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0;

PRIVATE VAR ExecDate;

/*---------------------------------------------------------------------------
Получить остаток на счете (на конец дня)
----------------------------------------------------------------------------*/
PRIVATE MACRO ПолучитьОстатокСчетаУчетЗнака(FirstDoc, Категория, Счет, Глава, Сумма, Дата, Валюта)
   IF (ValType(Счет) == V_UNDEF)
      Счет = "";
   END;
   
   IF (ValType(Глава) == V_UNDEF)
      Глава = 1;
   END;
   
   IF (ValType(Дата) == V_UNDEF)
      Дата = {curdate};
   END;
   
   IF (ValType(Валюта) == V_UNDEF)
      Валюта = NATCUR;
   END;
   Сумма = Money(0);
   SetParm( 4, Сумма );
  
   if(Счет == "") 
      IF (not СчетСделки(Категория, FirstDoc, Счет, NULL, NULL, 1)) 
          RETURN false; 
      ELSE 
          SetParm( 2, Счет );
      END; 
   END;

   Сумма = RestA(Счет, Дата, NULL, Глава, Валюта);
   SetParm( 4, Сумма );
   RETURN true;
END;

PRIVATE MACRO ПолучитьОстатокСчетаПереоценок(deal_pd, tdr_revP, tdr_revR, AsCategory)
   VAR Остаток = 0, Счет = "";

   IF (isSale(GetOperationGroup(deal_pd.GetTick().DealType)))
      IF (AsCategory == 3)
         tdr_revP = tdr_rev1P;
         tdr_revR = tdr_rev1R;
      ELIF (AsCategory == 4)
         tdr_revP = tdr_rev2P;
         tdr_revR = tdr_rev2R;
      END;
   ELSE
      tdr_revP = tdr_rev3P;
      tdr_revR = tdr_rev3R;
   END;

   ПолучитьОстатокСчетаУчетЗнака(deal_pd, tdr_revP, Счет, 1, Остаток, ExecDate, NATCUR);
   IF (Остаток == 0)
       ПолучитьОстатокСчетаУчетЗнака(deal_pd, tdr_revR, Счет, 1, Остаток, ExecDate, NATCUR);
   END;
   RETURN Остаток;
END;

PRIVATE MACRO ЗаполнитьКатегорииРазныеЗнаки(at, deal, tdr_revP, tdr_revR, Остаток)
   IF (Остаток < 0)
      at.CatReveiver = tdr_revR;
      IF (tdr_revR == tdr_rev1R)
         at.CatPayer = tdr_plmargP(deal.TypeDoc);
      ELIF (tdr_revR == tdr_rev2R)
         at.CatPayer = tdr_rev4P;
      ELIF (tdr_revR == tdr_rev3R)
         at.CatPayer = tdr_atmargP(deal.TypeDoc);
      END;
   ELSE
      at.CatPayer = tdr_revP;
      IF (tdr_revP == tdr_rev1P)
         at.CatReceiver = tdr_plmargR(deal.TypeDoc);
      ELIF (tdr_revP == tdr_rev2P)
         at.CatReceiver = tdr_rev4R;
      ELIF (tdr_revP == tdr_rev3P)
         at.CatReceiver = tdr_atmargR(deal.TypeDoc);
      END;
   END;
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
   record deal( dl_tick );
   SetBuff( deal, Document );

   IF (NOT MM_CheckLic())
      RETURN 1;
   END;

   ID_Operation = IDOperation;
   ID_Step      = IDStep;

   ExecDate = {curdate};

   VAR cmd, AdjAS, AdjSS, at = MM_Carry, MarginOld = 0, tdr_revP, tdr_revR, Sp = 0, SS = 0, AS = 0, GBV = 0, AsCategory = 0;
   VAR deal_pd = MMFirstDoc(deal.BofficeKind, deal.DealID, true);

   at.FIIDPayer        =
   at.FIIDReceiver     = NATCUR;
   at.Chapter          = 1;
   at.date_carry       = ExecDate;
   at.PrimaryDoc       = deal_pd;  

   // получить категорию оценки
   GetMainObjAttr(NULL, OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 5, AsCategory, NULL, NULL, ExecDate);

   // сделки, оцениваемые по АС (Метод ЭПС)
   IF ((AsCategory == 1) or (AsCategory == 2))
       AdjAS    = MM_CalcAdjAS(deal.BOfficeKind, deal.DealID, ExecDate, 0);
       AS       = MM_CalcAS(deal.BOfficeKind, deal.DealID, ExecDate, 0);
       MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 7/*MN_AMORTIZED*/, ExecDate, AS);
       MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 10, ExecDate, AS);
       IF (isSale(GetOperationGroup(deal.DealType)))
           GBV = MM_CalcGBV(deal.BOfficeKind, deal.DealID, ExecDate, 0);
           MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 8/*MN_GROSS*/, ExecDate, GBV);
       END;
       ConvSum(AdjAS, AdjAS, ExecDate, deal_pd.GetLeg().pfi, NATCUR);
       IF (AdjAS > 0)
           IF (isSale(GetOperationGroup(deal.DealType)))
               if (IsCLAIMSACQ(GetOperationGroup(deal.DealType)))
                  at.CatPayer    = get_adjcmR(ExecDate);
                  at.CatReceiver = tdr_plcorR(deal.TypeDoc);
                  at.Ground = "Отражение корректировки, увеличивающей стоимость сделки приобретения прав требования МБК";                 
               else
                  at.CatPayer    = tdr_adjplR;
                  at.CatReceiver = tdr_plcorR(deal.TypeDoc);
                  at.Ground = "Отражение корректировки, увеличивающей стоимость сделки размещения МБК";
               end;
           ELSE
               at.CatPayer    = tdr_atcorP(deal.TypeDoc);
               at.CatReceiver = tdr_adjatR;
               at.Ground = "Отражение корректировки, увеличивающей стоимость сделки привлечения МБК";
           END;
       ELSE
           IF (isSale(GetOperationGroup(deal.DealType)))
               if (IsCLAIMSACQ(GetOperationGroup(deal.DealType)))
                  at.CatPayer    = tdr_plcorP(deal.TypeDoc);
                  at.CatReceiver = get_adjcmP(ExecDate);
                  at.Ground = "Отражение корректировки, уменьшающей стоимость сделки приобретения прав требования МБК";                 
               else
                  at.CatPayer    = tdr_plcorP(deal.TypeDoc);
                  at.CatReceiver = tdr_adjplP;
                  at.Ground = "Отражение корректировки, уменьшающей стоимость сделки размещения МБК";
               end;
           ELSE
               at.CatPayer    = tdr_adjatP;
               at.CatReceiver = tdr_atcorR(deal.TypeDoc);
               at.Ground = "Отражение корректировки, уменьшающей стоимость сделки привлечения МБК";
           END;
       END;
       at.SumReceiver = abs(AdjAS);
   // оцениваемые по СС
   ELIF ((AsCategory == 3) or (AsCategory == 4))
      AdjSS    = MM_CalcAdjSS(deal.BOfficeKind, deal.DealID, ExecDate, 0);
      SS       = MM_CalcSS(deal.BOfficeKind, deal.DealID, ExecDate, 0);
      MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 9/*MN_FAIRCUR*/, ExecDate, SS);
      MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 10, ExecDate, SS);
      ConvSum(AdjSS, AdjSS, ExecDate, deal_pd.GetLeg().pfi, NATCUR);
      MarginOld = ПолучитьОстатокСчетаПереоценок(deal_pd, tdr_revP, tdr_revR, AsCategory);

      IF (MarginOld == AdjSS)
         RETURN 0;
      END;
      
      at.SumReceiver = abs(AdjSS);
      IF (AdjSS > MarginOld)
         at.Ground = "Отражение переоценки, увеличивающей стоимость сделки МБК";
         IF (((AdjSS > 0) and (MarginOld < 0)) or 
            ((AdjSS < 0) and (MarginOld > 0)))
            Sp = abs(MarginOld) + abs(AdjSS);
            ЗаполнитьКатегорииРазныеЗнаки(at, deal, tdr_revP, tdr_revR, MarginOld);
         ELSE
            IF (isSale(GetOperationGroup(deal.DealType)))
               IF (AsCategory == 3)
                  at.CatPayer    = tdr_rev1P;
                  at.CatReceiver = tdr_plmargR(deal.TypeDoc);
               ELSE
                  at.CatPayer    = tdr_rev2P;
                  at.CatReceiver = tdr_rev4R;
               END;
            ELSE
               at.CatPayer    = tdr_atmargP(deal.TypeDoc);
               at.CatReceiver = tdr_rev3R;
            END;
         END;
      ELSE
          at.Ground = "Отражение переоценки, уменьшающей стоимость сделки МБК";
          IF (((AdjSS > 0) and (MarginOld < 0)) or 
              ((AdjSS < 0) and (MarginOld > 0)))
             Sp = abs(MarginOld) + abs(AdjSS);
             ЗаполнитьКатегорииРазныеЗнаки(at, deal, tdr_revP, tdr_revR, MarginOld);
          ELSE
             IF (isSale(GetOperationGroup(deal.DealType)))
                IF (AsCategory == 3)
                   at.CatPayer    = tdr_plmargP(deal.TypeDoc);
                   at.CatReceiver = tdr_rev1R;
                ELSE
                   at.CatPayer    = tdr_rev4P;
                   at.CatReceiver = tdr_rev2R;
                END;
             ELSE
                at.CatPayer    = tdr_rev3P;
                at.CatReceiver = tdr_atmargR(deal.TypeDoc);
             END;
          END;
      END;
   END;

   IF (not at.carry())
       RETURN 1;
   END;
   
   RETURN 0;
END;

NameMacroFile = "mmcostadj_j.mac";