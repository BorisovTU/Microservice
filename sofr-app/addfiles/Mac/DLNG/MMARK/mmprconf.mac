/*
$Name: mmprconf.mac
$Module: МБК
$Description: Печать подверждения сделки МБК
*/
/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v5.1
                 Copyright (c) R-Style Software Lab

  Имя файла        : mprconf.mac

  Библиотека       : 

  Описание         : Печать подверждения сделки МБК

  Программист      : Kalashnikov Michael (KMV)

  Создан           : 22.6.2001
-------------------------------------------------------------------------*/
import OprInter, PTInter, PaymInter, MmarkInter, globals, mmlibr, mmlib, dlreport;

const ALG_PC_BASIS = 2317,
      ALG_MM_NETTING = 2116,
      ALG_MM_LINK_CHANNEL = 2118,
      CODE_BIK = 3,
      CODE_INN = 16;

PRIVATE VAR tick = TBfile("dl_tick"),
            leg = TBfile("dl_leg"),
            orDeal = TBfile("dl_tick");

MACRO NameAlg(type, num)
  var alg = TBfile("namealg");
  alg.KeyNum = 0;
  alg.rec.iTypeAlg = type;
  alg.rec.iNumberAlg = num;
  if( not GetEq(alg) )
    return "";
  else
    return alg.rec.szNameAlg;
  end;
END;

PRIVATE MACRO PartyName(ID)
  if (ID == 0)
    return {Name_Bank};
  end;
  var party = TBfile("party");
  party.KeyNum = 0;
  party.rec.PartyID = ID;
  if( not GetEq(party) )
    return ID;
  else
    return party.rec.Name;
  end;
END;

PRIVATE MACRO PartyCode(ID, type)
  var pcode = TBfile("partcode"),
      saveID = 0;
  if (ID == 0)
    if (type == CODE_BIK)
      return {MFO_Bank};
    else
      pcode.KeyNum = 1;
      pcode.rec.CodeKind = CODE_BIK;
      pcode.rec.Code = {MFO_Bank};
      if (not GetEQ(pcode))
        return "";
      else
        saveID = pcode.rec.PartyID;
        pcode.KeyNum = 0;
        pcode.rec.PartyID = saveID;
        pcode.rec.CodeKind = type;
        if (not GetEQ(pcode))
          return "";
        else
          return pcode.rec.Code;
        end;
      end;
    end;
  end;
  pcode.KeyNum = 0;
  pcode.rec.PartyID = ID;
  pcode.rec.CodeKind = type;
  if( not GetEq(pcode) )
    return "";
  else
    return pcode.rec.Code;
  end;
  
END;

PRIVATE MACRO Direction()
  if (isSale(GetOperationGroup(tick.rec.DealType)))
    return "размещаем";
  else
    return "привлекаем";
  end;
END;

PRIVATE MACRO Product()
  if (Index(tick.rec.TypeDoc, "L") != 0) /* Кредит */
    return "кредит";
  else
    return "депозит";      
  end;
END;

PRIVATE MACRO Currency(PFI)
  var fininstr = TBfile("fininstr");
  fininstr.KeyNum = 0;
  fininstr.rec.FIID = PFI;
  if( not GetEq(fininstr) )
    return "___";
  else
    return fininstr.rec.Ccy;
  end;
END;

PRIVATE MACRO Basis
  return NameAlg(ALG_PC_BASIS, leg.rec.Basis);
END;

PRIVATE MACRO Rate
var rate:string, tmp:string = "", ДлинаRate:integer,    
	delim=0, PointRate:Integer, RateStr:string = "", 
	flag = true;    

	rate = leg.rec.Price;
	delim = StrBrk(rate, ".") - 1;
	rate = SubStr(rate, 1, delim);

    ДлинаRate = StrLen(rate);
    PointRate = leg.rec.Point;
    if (ДлинаRate <= PointRate)
    	delim = PointRate - ДлинаRate;
		RateStr = "0.";
		while (delim > 0)
			RateStr = RateStr +"0";
			delim = delim -1;
		end;
		RateStr = RateStr + rate;
    else
    	delim = ДлинаRate - PointRate;
		RateStr = SubStr(rate, 1, delim);
		RateStr = RateStr + ".";
		delim = delim +1;
		tmp = SubStr(rate, delim);
		RateStr = RateStr + tmp;
    end;

    delim = StrLen(RateStr);
	while (flag and delim)
		if (SubStr(RateStr, delim, 1) == "0")
			ДлинаRate = delim - 1;
			RateStr = SubStr(RateStr, 1, ДлинаRate);	
		else
			flag = false;
		end;
		delim = delim - 1;
	end;

	delim = StrLen(string(RateStr));
	if (SubStr(string(RateStr), delim, 1) == ".")
		RateStr = RateStr + "0000"
	end;

	RateStr = RateStr + " %/год ("+Basis()+")";

  return RateStr;
END;

PRIVATE MACRO PumpOrDeal(OriginalDealID)
  orDeal.KeyNum = 0;
  orDeal.rec.DealID = OriginalDealID;
  if(not GetEq(orDeal))
     return 1;
  end;
  return 0;
END;
PRIVATE MACRO OrDealCode
  return orDeal.rec.DealCode;
END;

PRIVATE MACRO OrDealDate
  return orDeal.rec.DealDate;
END;

PRIVATE MACRO OrDealNetting
  return NameAlg(ALG_MM_NETTING, tick.rec.Netting);
/*return "Да";*/
END;

PRIVATE MACRO TraderFIO
  var persn = TBfile("persn");
  persn.KeyNum = 0;
  persn.rec.PersonID = tick.rec.TraderID;
  if(not GetEq(persn))
    return "______________________________";
  else
    return persn.rec.Name1 + persn.rec.Name2 + persn.rec.Name3;
  end;
END;

PRIVATE MACRO GetBossBO
  var officer = TBfile("officer"),
      ptoffice = TBfile("ptoffice"),
      persn = TBfile("persn"),
      OfficeCode = "",
      stat = 0;

  GetRegistryValue("MMARK\\ОБЩИЕ\\ПОДРАЗД_МБК", V_STRING, OfficeCode, stat);
  if (stat == 0)
    ptoffice.KeyNum = 1;
    ptoffice.rec.PartyID = {OurBank};
    ptoffice.rec.OfficeCode = OfficeCode;
    if (GetEq(ptoffice))
      officer.KeyNum = 5;
      officer.rec.PartyID = {OurBank};
      officer.rec.OfficeID = ptoffice.rec.OfficeID;
      officer.rec.IsFirstOfficePerson = "X";
      if (GetEq(officer))
        persn.KeyNum = 0;
        persn.rec.PersonID = officer.rec.PersonID;
        if (GetEq(persn))
          return persn.rec.Name1 + " " + persn.rec.Name2 + " " + persn.rec.Name3;
        end;
      end;
    end;
  end;
  return "______________________________";
  
END;

PRIVATE MACRO GetBoss
  if ({FIO_Boss} == "")
    return "______________________________";
  else
    return {FIO_Boss};
  end;
END;

PRIVATE MACRO PurposeName(Purpose)
  var pm_purp = TBfile("pmpurp");
  pm_purp.KeyNum = 0;
  pm_purp.rec.Purpose = Purpose;
  if( not GetEq(pm_purp) )
    return ")";
  else
    return pm_purp.rec.ShortName + ")";
  end;
END;

PRIVATE MACRO PaymNetting(Netting)
  if (Netting == "X")
    return "б/д";
  else
    return "д";
  end;
END;

PRIVATE MACRO PrintHeader
  ARRAY TmpName;
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
 [                                                                              ];
 [                                                                              ];
   strsplit({Name_Bank}, TmpName, 50);
 [           ################################################## ########## #####] (TmpName(0), date:f, time:5:t);
   if (valType(TmpName(1)) == V_STRING)   
 [           ##################################################                 ] (TmpName(1));
   end;
 [направляет Подтверждение № ________________   ################  от  ##########] (tick.rec.DealCodePS, date:f);
  strsplit({Name_Bank}, TmpName, 50);
 [в          ##################################################                 ] (PartyName(tick.rec.PartyID));
   if (valType(TmpName(1)) == V_STRING)   
 [           ##################################################                 ] (TmpName(1));
   end;

END;

PRIVATE MACRO PrintDeal(OriginalDealID)
  var i = 0;
  ARRAY TmpComment;  
  
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
 [Сделка № ################ заключена ##########         канал связи: ##########] (tick.rec.DealCode, tick.rec.DealDate:f, NameAlg(ALG_MM_LINK_CHANNEL, tick.rec.LinkChannel));
 [                                                           трейдер: ##########] (tick.rec.TraderID);
 [Мы ########## ####### в ##################### ### на ### дн.     с  ##########] (Direction(), Product(), leg.rec.Principal:21:2, Currency(leg.rec.PFI), leg.rec.Duration:3:0:t, leg.rec.Start:f);
 [                                                                 по ##########] (leg.rec.Maturity:f);
 [под ##################################################                        ] (Rate():l);
 [                                                                              ];
 if ((OriginalDealID != 0) and (not PumpOrDeal(OriginalDealID))) 
 [Пролонгируем сделку № ################ от ##########         Неттинг #########] (OrDealCode(), OrDealDate():f, OrDealNetting());
 end;
 strsplit(tick.rec.Comment, TmpComment, 78, 62, 3);
 [Особые условия: ##############################################################] (TmpComment(i));
 while (valType(TmpComment(i)) == V_STRING)
 [##############################################################################](TmpComment(i));
   i = i + 1;
 end;
END;

PRIVATE MACRO PrintPayments
  var payment = TBfile("pmpaym"),
      Об = TArray, i = 0, /*обязательства*/
      Тр = TArray, j = 0; /*требования*/
  macro filter
    return true;
  end;

  macro definePaym(paym)
    if (paym.rec.Payer == 0) /*плательщик - наш банк - исходящий*/
      Об(i) = TrecHandler("pmpaym");
      copy(Об(i), paym);
      i = i + 1;
    else  
      Тр(j) = TrecHandler("pmpaym");
      copy(Тр(j), paym);
      j = j + 1;
    end;
  end;

  macro printPaym(pm, dirr, DebetCredit)
 [########## ######## ##################### ### (#                           ###](pm.rec.ValueDate:f, dirr, pm.rec.Amount:21:2, Currency(pm.rec.PayFIID), PurposeName(pm.rec.Purpose), PaymNetting(pm.rec.Netting));
    ARRAY TmpName;
    ARRAY TmpBankName;
    ARRAY TmpCorrName;
    var i = 0,
      pmprop = TBfile("pmprop"),
      pmrmprop = TBfile("pmrmprop"),
      cs = TBfile("corschem");
    pmrmprop.KeyNum = 0;
    pmrmprop.rec.PaymentID = pm.rec.PaymentID;
    if (not GetEQ(pmrmprop))
      pmrmprop.rec.PayerName = "                   ";
      pmrmprop.rec.ReceiverName = "                   ";
      pmrmprop.rec.PayerBankName = "                   ";
      pmrmprop.rec.ReceiverBankName = "                   ";
      pmrmprop.rec.PayerCorrBankName = "                   ";
      pmrmprop.rec.ReceiverCorrBankName = "                   ";
      pmrmprop.rec.PayerINN = "                   ";
      pmrmprop.rec.ReceiverINN = "                   ";
    end;
      strsplit(pmrmprop.rec.ReceiverName, TmpName, 20, 20, 1);
      strsplit(pmrmprop.rec.ReceiverBankName, TmpBankName, 20, 19, 1);
      strsplit(pmrmprop.rec.ReceiverCorrBankName, TmpCorrName, 20, 19, 1);

 [в пользу #################### ########### ############# ИНН ##################](TmpName(i), DL_GetCodeKindName( pm.rec.ReceiverCodeKind ), pm.rec.ReceiverCode, pmrmprop.rec.ReceiverINN);
    i = i + 1;
    while (valType(TmpName(i)) == V_STRING)
 [         ####################                                                 ](TmpName(i));
      i = i + 1;
    end;
     i = 0;
    pmprop.KeyNum = 0;
    pmprop.rec.PaymentID = pm.rec.PaymentID;
    pmprop.rec.DebetCredit = 1;
    if (pmprop.GetEQ() and (pmprop.rec.Group == PAYMENTS_GROUP_EXTERNAL))
 [на счет #################### в банке ################### ########### #########](pm.rec.ReceiverAccount, TmpBankName(i), DL_GetCodeKindName(pmprop.rec.CodeKind), pmprop.rec.BankCode);
      i = i + 1;
      while (valType(TmpBankName(i)) == V_STRING)
 [                                     ####################                     ](TmpBankName(i));
        i = i + 1;
      end;
      
      i = 0;
 [корсчет #################### в банке ################### ########### #########](pmprop.rec.CorrAcc, TmpCorrName(i), DL_GetCodeKindName(pmprop.rec.CorrCodeKind), pmprop.rec.CorrCode);
      i = i + 1;
      while (valType(TmpCorrName(i)) == V_STRING)
 [                                     ####################                     ](TmpCorrName(i));
        i = i + 1;
      end;
    else /*кредит - внутреннее свойство - значит это платеж к нам в банк*/
      i = 0;
 [на счет #################### в банке ################### ########### #########](pm.rec.ReceiverAccount, TmpBankName(i), DL_GetCodeKindName(pm.rec.ReceiverCodeKind), pm.rec.ReceiverCode);
      i = i + 1;
      while (valType(TmpBankName(i)) == V_STRING)
 [                                     ####################                     ](TmpBankName(i));
        i = i + 1;
      end;
      /*можно попробовать найти дебет - восстановить корсчет через корсхему*/
      pmprop.KeyNum = 0;
      pmprop.rec.PaymentID = pm.rec.PaymentID;
      pmprop.rec.DebetCredit = 0;
      if (pmprop.GetEQ())
        cs.KeyNum = 0;
        cs.rec.Number = pmprop.rec.Corschem;
        cs.rec.FI_Kind = 1;
        cs.rec.FIID = pm.rec.PayFIID;
        if (cs.GetEQ())
          asize(TmpCorrName, 0);
          i = 0;
          strsplit(PartyName(cs.rec.CorrID), TmpCorrName, 20, 19);

 [корсчет #################### в банке ################### ########### #########](cs.rec.CorAccount, TmpCorrName(i), DL_GetCodeKindName(PTCK_BIC), PartyCode(cs.rec.CorrID, PTCK_BIC));
          i = i + 1;
          while (valType(TmpCorrName(i)) == V_STRING)
 [                                     ####################                     ](TmpCorrName(i));
            i = i + 1;
          end;
        end;

      end;
    end;
  end;
/*Для каждого платежа - печать */
  payment.KeyNum = 1;
  payment.rec.DocKind = tick.rec.BofficeKind;
  payment.rec.DocumentID = tick.rec.DealID;
  payment.rec.Purpose = 0;
  payment.rec.SubPurpose = 0;
  if (GetGE(payment) and limiter(tick.rec, payment.rec))
    if (filter)
      definePaym(payment);
    end;
    while (Next(payment) and limiter(tick.rec, payment.rec))
      if (filter)
        definePaym(payment);
      end;
    end;
  end;
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
 [------------------ ДВИЖЕНИЕ СРЕДСТВ и ПЛАТЕЖНЫЕ ИНСТРУКЦИИ -------------------];
  if (Об.size > 0)
    i = 0;
 [ОБЯЗАТЕЛЬСТВА                                                                 ];
    while (i < Об.size)
      printPaym(Об(i), "платим", 1);
      i = i + 1;
    end;
  end;
  if (Тр.size > 0)
    j = 0;
 [ТРЕБОВАНИЯ                                                                    ];
    while (j < Тр.size)
      printPaym(Тр(j), "получаем", 0);
      j = j + 1;
    end;
  end;

 [------------------------------------------------------------------------------];
END;

PRIVATE MACRO PrintFooter
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
 [Трейдер                _________________  ##############################      ] (TraderFIO());
 [                           (Подпись)      Ф.И.О.                              ];
 [                                                                              ];
 [Начальник БО           _________________  ##############################      ](GetBossBO());
 [                           (Подпись)      Ф.И.О.                              ];
 [                                                                              ];
 [Председатель Правления _________________  ##############################      ](GetBoss():30:t);
 [                           (Подпись)      Ф.И.О.                              ];
 [                                                                              ];
END;

MACRO CreateFileName
    var hour,min,sec, Path,
        txtRegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";

    if(MM_SetRegistryPath(txtRegistryPath, Path))
       return "";
    end;

    // получить полный путь
    Path = MMCMT_processPath(Path) + "\\";
    TimeSplit (Time,hour,min,sec);
    return String(Path,{oper},hour,min,sec,".txt");
END;

MACRO PrintConfirmation (DealID, OriginalDealID, OutFile, first)
  file out() txt;
  var oldOutFile = "";
  if (OutFile != "")
    oldOutFile = SetOutput(OutFile, TRUE);
  else
    OutFile = SetOutput(NULL, TRUE);
    SetOutput(OutFile, FALSE);
  end;
  if (OriginalDealID == null)
    OriginalDealID = 0;
  end;
  tick.KeyNum = 0;
  tick.rec.DealID = DealID;
  if( not GetEq(tick) )
    mm_error( "Не найдена сделка ", DealID );
    return 1;
  end;

  leg.KeyNum = 0;
  leg.rec.LegKind = LEG_KIND_DL_TICK;
  leg.rec.DealID = DealID;
  leg.rec.LegID = 1;
  if( not GetEq(leg) )
    mm_error( "Не найден транш сделки ", tick.rec.DealCode );
    return 1;
  end;
  if (first == false)
    println("\f");
  end;
  PrintHeader();
  PrintDeal(OriginalDealID);
  PrintPayments();
  PrintFooter();
  if (oldOutFile != "")
    SetOutput(oldOutFile, TRUE);
  else
    SetOutput(OutFile);
  end;

  if (not isOprMultiExec)
    SetOutput(CreateFileName(), FALSE);
    open(out, OutFile);
    ViewFile(out);
  end;
  
END;

MACRO ViewTheConfirmation(OutFile)
  file out() txt;
  SetOutput(CreateFileName(), FALSE);
  open(out, OutFile);
  ViewFile(out);
END;

