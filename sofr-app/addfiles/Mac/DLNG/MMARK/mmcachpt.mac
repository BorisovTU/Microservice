/*
    Кэшированный контрагент
*/
IMPORT PTInter;

CLASS CachedParty (PartyID)
    PRIVATE VAR 
        party = null, /*Здесь будет контрагент*/
        self = null, /*Здесь будет наш банк*/
        bank = null, /*Здесь будет банк*/
        IsBank = null;

    PRIVATE MACRO IsValidPt (pt,PartyID,refreshed)
        VAR newpt,ret;

        if (PartyID==null)
            return false;
        end;

        if (pt==null)
            pt = Trechandler ("party.dbt");
            newpt = true;
        end;

        if (pt.rec.PartyID!=PartyID)
            if (ПолучитьСубъекта( PartyID, pt )==0)
                refreshed = true;
                ret = true;
            else
                ret = false;
                refreshed = false;
            end;
        else
            refreshed = false;
            ret = true;
        end;

        SetParm (3,refreshed);

        if (newpt)
            SetParm (1,pt);
        end;

        return ret;
    END;

    /*Проверить правильность*/
    MACRO IsValid (PartyID)        
        VAR refreshed,
            ret = IsValidPt(party,PartyID,refreshed);

        if (refreshed)
            IsBank = null;
        end;

        return ret;
    END;
    
    /*Проверить правильность*/
    PRIVATE MACRO IsValidSelf (PartyID)
        return IsValidPt(self,PartyID);
    END;

    /*Проверить правильность банка, выполнять, 
      только если party правильный и банк - _резидент_!
    */
    PRIVATE MACRO IsValidBank (PartyID)
        if (IsBank==null)
            IsBank = ВидСубъекта( PartyID, PTK_BANK );
        end;

        if (IsBank) /*Подкачиваем банк*/
            if (bank==null)
                bank = TBfile ("bankdprt.dbt");
            end;

            if (bank.rec.PartyID != PartyID)
                bank.rec.PartyID = PartyID;

                if (bank.GetEQ)
                    return true;
                end;
            else
                return true;
            end;
        end;

        return false;
    END;

    MACRO IsResident ( PartyID )
        if (IsValid(PartyID))
            if( party.rec.NotResident == "X" )
                return false; /*нерезидент*/
            end;
        end;

        return true;
    END;

    /*Проверить, что PartyID - банк-нерезидент
    */
    MACRO IsBank_NonResident( PartyID )
        VAR partyown = Tbfile("partyown", "R", 0);

        if (not IsResident(PartyID))
            partyown.Clear();
            partyown.rec.PartyID = PartyID;
            partyown.rec.PartyKind = 2; // KIND_BANK
            if(partyown.GetEQ())
               return true; // банк-нерезидент
            end;
        end;

        return false;
    END;


    MACRO GetKindKDRKR (PartyID)
      if (IsValid(PartyID))
        if(IsResident(PartyID))
          if (IsValidBank(PartyID))
            return 2; /*Кредитная организация - резидент*/
          end;
        elif(IsBank_NonResident(PartyID))
          return 3; /*Банк - нерезидент*/
        end;
      end;
      return -1;
    END;
    MACRO GetKind ( PartyID )
        if( IsValid (PartyID) )

            if(IsResident(PartyID))
                if( IsValidBank (PartyID) )
                  if(IsBankType( PartyID, PT_KIND_PAYM_CASH_CENTRE        ) OR
                     IsBankType( PartyID, PT_KIND_FIELDOFFICE_CENTRALBANK ))
                    return 1; /*ЦБ РФ*/
                  else
                    return 2; /*Кредитная организация - резидент*/
                  end;
                end;
            elif(IsBank_NonResident(PartyID))
                return 3; /*Банк - нерезидент*/
            end;
        end;

        return -1;
    END;


    IsValid (PartyID);
END;
