/*
$Name:        mmconscosts_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Учет затрат"
*/

IMPORT PaymInter, OprInter, PTInter, SfInter, FIInter, MmarkInter, mmlib, mmlibr, spserv, mmcnst_j, mmcarry, mmcateg, "globals.mac", mmservop;

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0,
    ExecDate;

private VAR sfcom  = TRecHandler("sfcomiss.dbt");

RECORD settacc(settacc);

PRIVATE MACRO НужноУчитыватьЗатраты(DocID, DocKind, DealDate, DocParty)
    var PartyCode, sel, res, ContrID = 0;
    PartyCode = ПолучитьКодСубъекта(DocParty, PTCK_CONTR);

    if (DocKind == DL_IBCDOC)
        sel = RSDCommand("select t_id from dsfcontr_dbt where t_object = ? and t_dateconc <= ?");
        sel.addParam("", RSDBP_IN, PartyCode);
        sel.addParam("", RSDBP_IN, DealDate);  
        sel.execute();
        res  = TRsbDataSet(sel);
        if (res.MoveNext())
            ContrID = res.ID;
        else
            return false;
        end;
    end;
    sel = RSDCommand("select 1 from ddlcomis_dbt where t_dockind = ? and t_docid = ?");
    sel.addParam("", RSDBP_IN, DocKind);
    sel.addParam("", RSDBP_IN, DocID);    
    sel.execute();
    res  = TRsbDataSet(sel);
    if (res.MoveNext())
        return true;
    end;
/*
    sel = RSDCommand("select 1 from dsfconcom_dbt where t_sfplanid = ?");
    sel.addParam("", RSDBP_IN, ContrID);
    sel.execute();
    res  = TRsbDataSet(sel);
    if (res.MoveNext())
        return true;
    end;  
*/
    return false;
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/

MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    record deal( dl_tick );
    record sfcontr(sfcontr);

    ID_Operation = IDOperation;
    ID_Step      = IDStep;

    VAR RsbData, at, Payment, doc_pd, sfssi, DefaultCodeKind, Нз, NOTEId, query, AsCategory = 0, OprServDoc;
    VAR DocID, DocPFI, DocOper = 0, DocParty, DocType = 0, DealDate, setaccid, SettaccFound = false, FileSettAcc = TBFIle("settacc.dbt"),
    cat_expenses, cat_expcalcs, FIRole;

    OprServDoc = GetOprServDoc();
    ExecDate = OprServDoc.rec.CommDate;
    if (not isServOpMode())
        ExecDate = {curdate};
    end;

    // учет прочих доходов пока не поддерживается
    if (OprServDoc.rec.ComType == 2)
        return 0;
    end;

    if (DocKind == DL_IBCDOC)
        SetBuff( deal, Document );
        DocID    = deal.DealID;
        DocParty = deal.PaymAgent;
        DealDate = deal.DealDate;
        DocOper  = deal.Oper;
        DocType  = deal.DealType;

        doc_pd   = MMFirstDoc(deal.BofficeKind, deal.DealID, true);
        DocPFI   = doc_pd.GetLeg().PFI;

        cat_expenses = get_expenses(deal.DealType);
        cat_expcalcs = get_expcalcs(deal.DealType);
    else
        SetBuff( sfcontr, Document );
        DocID    = sfcontr.ID;
        DocPFI   = sfcontr.FIID;

        doc_pd   = MMFirstDoc(DL_SFCONTR, sfcontr.ID, false);
        DocID    = sfcontr.ID;

        cat_expenses = tdr_expenses;
        cat_expcalcs = tdr_expcalcs;
    end; 

    if (not MM_CheckLic())
        return 1;
    end;
    
    if (not НужноУчитыватьЗатраты(DocID, DocKind, DealDate, DocParty))
        return 0;
    end;

    query = "select t_sum, t_id, t_feetype, t_comnumber, t_planpaydate, t_contract from ddlcomis_dbt " +
                          "where t_dockind = " + DocKind + 
                          " and t_docid = " + DocID +
                          " and t_factpaydate = " + GetSQLDate(date(0,0,0)) +
                          " and t_id not in (select t_subpurpose from dpmpaym_dbt where t_purpose = 83)";

    if (OprServDoc.rec.ContractID > 0)
        query = query + " and t_contract = " + OprServDoc.rec.ContractID;
    end;
    if (OprServDoc.rec.ComNumber > 0)
        query = query + " and t_comnumber = " + OprServDoc.rec.ComNumber + " and t_feetype = 6";
    end;

    RsbData = TRsbDataSet(query);

    while (RsbData.MoveNext)
        if( not SfGetComiss( RsbData.FeeType, RsbData.ComNumber, sfcom ) )
            mm_error("Не найдена комиссия по операции");
            return 1;
        end;     
        if (sfcom.rec.ReceiverID != {ourbank})
            FIRole = 10000 + RsbData.Id;
            DocParty = sfcom.rec.ReceiverID;
            at = MM_Carry;
            at.SumReceiver = RsbData.Sum;
            ConvSumCross(at.SumReceiver, at.SumReceiver, ExecDate, sfcom.rec.FIID_Comm, DocPFI);
            at.Chapter      = 1;
            at.date_carry   = ExecDate;
            at.PrimaryDoc   = doc_pd;
            at.FIIDPayer    = DocPFI;
            at.FIIDReceiver = DocPFI;
            at.FIID         = DocPFI;

            if (DocKind == DL_IBCDOC)
                // получить категорию оценки
                GetMainObjAttr(NULL, OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 5, AsCategory, NULL, NULL, ExecDate);
                // если не оцениваемые по СС ПУ
                if (AsCategory != 3)
                    at.CatPayer = cat_expenses;
                    at.FIRolePayer = FIRole;
                    at.CatReceiver = cat_expcalcs;
                    at.Ground = "Учет затрат по договору, оцениваемому по АС или СС ПСД";
                else
                    if (isSale(GetOperationGroup(doc_pd.GetTick().DealType)))
                        at.CatPayer = tdr_comP(doc_pd.GetTick().TypeDoc);
                        at.CatReceiver = cat_expcalcs;

                    else
                        at.CatPayer = tdr_comexpP(doc_pd.GetTick().TypeDoc);
                        at.CatReceiver = cat_expcalcs;
                    end;
                    at.FIRoleReceiver = FIRole;
                    at.Ground = "Учет затрат по договору, оцениваемому по СС ПУ";   
                end;                
                Нз = ПолучитьЗначениеПримечания(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 14/*MN_UNALLOCATED*/, ExecDate);
                MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 14, ExecDate, money(Нз + RsbData.Sum));
            else
                at.CatPayer = cat_expenses;
                at.CatReceiver = cat_expcalcs;
                at.Ground = "Учет затрат по договору обслуживания";
            end;

            if (not at.carry())
                mm_error("Ошибка при формировании проводки");
                return 1;
            end;

            // формирование платежа
            Payment = RsbPayment();
            Payment.DocKind              = DocKind;
            Payment.DocumentID           = DocID;
            Payment.Purpose              = PM_PURP_COSTPM;
            Payment.SubPurpose           = RsbData.ID;
            Payment.OrderFIID            =
            Payment.BaseFIID             = 
            Payment.PayerFIID            = 
            Payment.ReceiverFIID         = 
            Payment.FuturePayerFIID      = 
            Payment.FutureReceiverFIID   = DocPFI;
            Payment.OrderAmount          =
            Payment.PayerAmount          = 
            Payment.ReceiverAmount       = 
            Payment.BaseAmount           = 
            Payment.FutureBaseAmount     = at.SumReceiver;
            Payment.ActuateFutureAmounts(TBA_AMOUNT);
            Payment.IsPlanPaym           = "X";
            Payment.Date                 = 
            Payment.PayDate              = 
            Payment.OutTransferDate      =
            Payment.ValueDate            = RsbData.PlanPayDate;
            Payment.ClientDate           = ExecDate;
            Payment.Number               = String(Payment.SubPurpose); 
            Payment.Oper                 = DocOper;
            Payment.Origin               = PAYMENT_OR_AUTO;
            Payment.PaymStatus           = PM_READIED;
            if ((DocKind == DL_IBCDOC) and (deal.Netting == 3))
                Payment.Netting = "X";
            else
                Payment.Netting = "";
            end;

            if (DocPFI == NATCUR)
                DefaultCodeKind = PTCK_BIC;
            else
                DefaultCodeKind = PTCK_SWIFT;
            end;

            var FindAccount = "";
            doc_pd.IsActAccount(cat_expcalcs, FindAccount, true, FIRole, NULL, ExecDate, NULL);
            if (FindAccount == "")
                doc_pd.ActualAccount(cat_expcalcs, FindAccount, true, FIRole, NULL, ExecDate, NULL);
            end;

            Payment.SetPayerPI(PAYMENTS_GROUP_UNDEF,
                    {OurBank},
                    DefaultCodeKind,
                    "",
                    "",
                    "",
                    DocPFI,
                    1,
                    FindAccount,
                    {OurBank},
                    "",
                    "",
                    DefaultCodeKind,
                    ""
                    );
                    
            SettaccFound = false;
            setaccid = GetContrSSISettAcc(DocPFI, RsbData.FeeType, RsbData.ComNumber);
            if (setaccid)
                FileSettAcc.rec.SettAccID = setaccid;
                if (FileSettAcc.GetEQ)
                    SettaccFound = true;
                    copy(settacc, FileSettAcc);
                end;
            end;

            if (not SettaccFound)
                if (not FindSETTACC_PMAUTO(DocParty, FIKIND_CURRENCY, DocPFI, PTSK_MM, DocType, PM_PURP_COSTPM, 0, settacc));
                elif (not FindSETTACC_PMAUTO(DocParty, FIKIND_CURRENCY, DocPFI, PTSK_MM, DocType, 0, 0, settacc));
                elif (not FindSETTACC_PMAUTO(DocParty, FIKIND_CURRENCY, DocPFI, PTSK_MM, 0, PM_PURP_COSTPM, 0, settacc));
                elif (not FindSETTACC_PMAUTO(DocParty, FIKIND_CURRENCY, DocPFI, PTSK_MM, 0, 0, 0, settacc));
                end;
            end;

            Payment.SetReceiverPI(PAYMENTS_GROUP_UNDEF,
                                settacc.BankID,
                                settacc.BankCodeKind,
                                settacc.BankCode,
                                settacc.BankName,
                                settacc.CorrAcc,
                                settacc.FIID,
                                settacc.Chapter,
                                settacc.Account,
                                settacc.PartyID,
                                settacc.RecName,
                                settacc.INN,
                                settacc.CodeKind,
                                settacc.Code
                                );

            Payment.Actuate();

         end;

    end;


    return 0;
END;

NameMacroFile = "mmconscosts_j.mac";
