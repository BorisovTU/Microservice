/*
$Name: mmhedg.mac       
$Module: МБК
$Description: Сервисная операция: Амортизация корректировок хеджирования
*/
/*--------------------------------------------------------------------------------------*/
/*  Имя файла        : mmhedg.mac                                                       */
/*                                                                                      */
/*  Описание         : Сервисная операция: Амортизация корректировок хеджирования       */
/*                                                                                      */
/*  Программист      : Врубель В.Л.                                                     */
/*                                                                                      */
/*  Создан           : 17.12.2021                                                       */
/*--------------------------------------------------------------------------------------*/
IMPORT RsbDataSet, mmservop, globals, mmcateg, mmlb_j, mmcnst_j;
/****************************************************************************************/

/*Печать ошибок**************************************************************************/
MACRO PrintReportError(_ErrorArray)
    VAR 
        idx = 0,
        cntError = _ErrorArray.Size;

    AddSvOpReportLine("┌────────────┬────────────────────┬──────┬─────────────────────────────────────────────────────────────────────────────────────────┐");
    AddSvOpReportLine("│    Код     │    Номер сделки    │  №   │               Сообщение                                                                 │");
    AddSvOpReportLine("│  операции  │                    │ошибки│                                                                                         │");
    AddSvOpReportLine("├────────────┼────────────────────┼──────┼─────────────────────────────────────────────────────────────────────────────────────────┤");

    while (idx < cntError)
        AddSvOpReportLine("│" +
                           string(_ErrorArray[idx].OpCode:12)  + "│" + 
                           string(_ErrorArray[idx].ObjCode:20) + "│" +  
                           string(_ErrorArray[idx].NumErr:6)   + "│" +  
                           string(_ErrorArray[idx].StrErr:89)  + "│"  );
        idx = idx + 1;
    end;

    AddSvOpReportLine("└────────────┴────────────────────┴──────┴─────────────────────────────────────────────────────────────────────────────────────────┘");

    return true;
END;
/*Печать результатов работы**************************************************************/
MACRO PrintReport()
    VAR DataSet  = NULL,
         QueryStr = "", 
         flag     = true,
         deal_pd,
         mainrestacc = "",
         coracc = "",
         coraccrest_after  = $0,
         coraccrest_before = $0;      

    QueryStr = "select tick.t_DealID, tick.t_DealCode, tick.t_PartyID, party.t_shortname, acctrn.t_Sum_Receiver, acctrn.t_account_payer, acctrn.t_account_receiver from ddl_tick_dbt tick " + 
               "join doproper_dbt oproper on tick.t_DealID = oproper.t_DocumentID " +
               "join doprstep_dbt oprstep on oprstep.t_ID_Operation = oproper.t_ID_Operation " +
               "join dparty_dbt party on tick.t_PartyID = party.t_PartyID " +
               "join doprdocs_dbt oprdocs on oprdocs.t_ID_Operation = oprstep.t_ID_Operation and oprdocs.t_ID_Step = oprstep.t_ID_Step " +
               "join dacctrn_dbt acctrn on acctrn.t_AcctrnID = oprdocs.t_AcctrnID "
               "where oprstep.t_ServDocKind = 221 and oprstep.t_Symbol = 'Ж' and oprstep.t_ServDocID = " + GetOprServDoc().rec.DocumentID +
               " and oprdocs.t_DocKind = 1"; 

    DataSet = TRsbDataSet(QueryStr);

    if (DataSet.MoveNext())    
        AddSvOpReportLine("  ");
        AddSvOpReportLine("  ");                                                         
        AddSvOpReportLine("  Номер операции  " + String(GetOprServDoc().rec.CommCode:21:l) + "           Дата расчета:  " + String(Date(GetOprServDoc().rec.CommDate):10:c));
        AddSvOpReportLine("  ");
        AddSvOpReportLine("  Результаты операции амортизации корректировок хеджирования                                                     ");
        AddSvOpReportLine("┌──────────────────────┬────────────────────┬─────────────────────────┬─────────────────────────┬─────────────────────────┬─────────────────────────┬─────────────────────────┬────────────────────────┬────────────────────────┐");
        AddSvOpReportLine("│       № Сделки       │     Контрагент     │    Счет ОД по сделке    │  Счет корректировки     │      Счет по ДТ         │      Счет по КТ         │   Сумма амортизации     │    Остаток на счете    │    Остаток на счете    │"); 
        AddSvOpReportLine("│                      │                    │                         │      хеджирования       │                         │                         │     корректировки       │    корректировки до    │   корректировки после  │");
        AddSvOpReportLine("│                      │                    │                         │                         │                         │                         │                         │        операции        │        операции        │");
        AddSvOpReportLine("│                      │                    │                         │                         │                         │                         │                         │                        │                        │");
        
        while (flag)
            deal_pd = MMFirstDoc(DL_IBCDOC, DataSet.DealID, false);
            deal_pd.FindNumberAccount(tdr_rest(deal_pd.GetTick().FlagTypeDeal), mainrestacc, true, 0, deal_pd.GetLeg().PFI);

            var QueryStr2 = "select accdoc.t_Account, mccateg.t_Code, accdoc.t_FiRole from dmcaccdoc_dbt accdoc inner join dmccateg_dbt mccateg on accdoc.t_catid = mccateg.t_id " +
                            " where mccateg.t_code in ('-Корр, РС_Хедж', '+Корр, РС_Хедж', '-Корр, ПС_Хедж', '+Корр, ПС_Хедж') " + 
                            " and t_Account in ('" + DataSet.Account_Payer + "', '" + DataSet.Account_Receiver + "')";

            var DataSet2 = TRsbDataSet(QueryStr2);
            if (DataSet2.MoveNext())
                coracc = DataSet2.Account;
            end;
            
            coraccrest_after = deal_pd.GetRestAccount(DataSet2.Code, GetOprServDoc().rec.CommDate, NATCUR, 1, DataSet2.FiRole, true);
            if (coracc == DataSet.Account_Payer)
                coraccrest_before = coraccrest_after + DataSet.Sum_Receiver;
            else
                coraccrest_before = coraccrest_after - DataSet.Sum_Receiver;
            end;
            
            AddSvOpReportLine("├──────────────────────┼────────────────────┼─────────────────────────┼─────────────────────────┼─────────────────────────┼─────────────────────────┼─────────────────────────┼────────────────────────┼────────────────────────┤");
            AddSvOpReportLine("│" + 
                               String(DataSet.DealCode:22:r)        + "│" +
                               String(DataSet.ShortName:20:l:z)     + "│" +
                               String(mainrestacc:25:z)             + "│" +
                               String(coracc:25:z)                  + "│" +
                               String(DataSet.Account_Payer:25:z)   + "│" +
                               String(DataSet.Account_Receiver:25:z)+ "│" +
                               String(DataSet.Sum_Receiver:25)      + "│" +
                               String(coraccrest_before:24)         + "│" +
                               String(coraccrest_after:24)          + "│" );

           flag = DataSet.MoveNext();
       end;
        AddSvOpReportLine("└──────────────────────┴────────────────────┴─────────────────────────┴─────────────────────────┴─────────────────────────┴─────────────────────────┴─────────────────────────┴────────────────────────┴────────────────────────┘");
    else
        AddSvOpReportLine("Нет обработанных сделок операцией № " + GetOprServDoc().rec.CommCode);
    end;

    return true;
END;
/****************************************************************************************/
