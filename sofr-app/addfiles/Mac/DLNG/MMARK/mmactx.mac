/*************************************************************************
         Автоматизированная банковская система RS-Bank v5.1           
                 Copyright (c) R-Style Software Lab 2000
                                                                      
  Имя файла    :  mmacc.mac
  Описание     :  Работа с ActiveX
  Программист  :  Леонов И.Е.                                     
  Создан       :  04.10.2000
                  Вынес сюда общие функции из макросов печати подтверждений.
                  Могут использоваться для печати в Word чего угодно.
************************************************************************/

/*IMPORT OprInter, rslx, osfile, rsexts;*/
IMPORT OprInter, rslx, mmlib, mmscmt;

/* Инициализация */
MACRO pcmt_Init()
    var MM_WordApp = ActiveX( "Word.Application", null, TRUE );
    MM_WordApp.Visible = FALSE;
END;

/* Открыть документ по шаблону. Вернет открытый документ */
MACRO OpenWordDocumentByDot( DotFileName )
    var Path = MM_GetDotFileDir(DotFileName); 
    var FullDotFileName = MMCMT_processPath( Path ) + "\\" + DotFileName;
    var WordDoc;
    var MM_WordApp = ActiveX( "Word.Application", null, TRUE );
   
    /* Проверить, можно ли найти шаблон документа по указанному пути */
    if( Path == "" )
       return null;
    end;
   
    WordDoc = MM_WordApp.Documents.Add( FullDotFileName, FALSE );
   
    if( valtype( WordDoc ) == V_UNDEF )
       msgbox( "Ошибка открытия файла-шаблона ", FullDotFileName );
       MM_WordApp.Quit;
       return null;
    end;
    return WordDoc;
end;

/* Записать документ в RTF формате */
MACRO SaveRTFandCloseDoc( Document, RTF_FileName )
    var filename, RTF_PATH,
        txtRegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";

    if(MM_SetRegistryPath(txtRegistryPath, RTF_PATH))
      return "";
    end;

    filename = MMCMT_processPath( RTF_PATH ) + "\\" + RTF_FileName;

    Document.SaveAs( filename, 6 );
    Document.Close;
    return filename;
end;


/* Завершение работы */
MACRO pcmt_Done( p_filename )

var MM_WordApp, RTF_PATH, dot_filename,
    txtRegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";

    if( p_filename == "" ) /* pcmt_Do отработал с ошибкой */
       /*  msgbox вряд ли нужен, сообщения выдает pcmt_Do */
       return FALSE;
    end;
   
    /* пакетный выпуск */
    if( isOprMultiExec() and (МассоваяПечатьПодтверждений == true) )
       MMCMT_WordFiles[ ASize(MMCMT_WordFiles) ] = p_filename;
   /*    MMCMT_WordFilesPtr = MMCMT_WordFilesPtr + 1;*/
       return TRUE;
    end;
   
    MM_WordApp = ActiveX( "Word.Application", null, TRUE );
   
    if (IsStandAlone) /*Двухзвенка*/    
       MM_WordApp.Visible = TRUE;
       MM_WordApp.Documents.Add( p_filename, FALSE );
    else /*Трехзвенка*/
       MM_WordApp.Quit;
       if (not CopyFile (p_filename,"$txtfile\\tmprep.rtf"))
           MsgBox ("Ошибка при передаче файла на терминал");
           return FALSE;
       else
           if(MM_SetRegistryPath(txtRegistryPath, RTF_PATH) == 0)
              // получит полный путь
              RTF_PATH = MMCMT_processPath(RTF_PATH);
              dot_filename = RTF_PATH + "\\tmprep.rtf";
              CallRemoteRsl ("mmshrp.mac" ,"ShowReport", dot_filename);
           end;
       end;
    end;
   
    return TRUE;
END;

