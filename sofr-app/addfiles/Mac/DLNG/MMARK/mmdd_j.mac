/*
$Name:        mmdd_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Учет отсроченных разниц"
*/
IMPORT mmlibr, mmlib, "dlmisc.mac", mmcnst_j, "mmcateg.mac", mmcarry, carrydoc;

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0;

CONST PERC_ACCRUAL_SYMB  = "н",
      INCOMING_PAYM_SYMB = "i",
      OUTGOING_PAYM_SYMB = "g",
      CARRY_OVER_SYMB    = "е";

PRIVATE MACRO GetPrevStepSymb
    VAR Res = TRsbDataSet("SELECT t_symbol " +
                          "FROM doprstep_dbt " +
                          "WHERE t_id_operation = " + ID_Operation + " AND t_id_step IN " +
                          "(SELECT t_previous_step FROM doprstep_dbt " + 
                          " WHERE t_id_operation = " + ID_Operation + 
                          " AND t_id_step = " + ID_Step + ")");
    if (Res.MoveNext)
        return Res.Symbol;
    end;
    return "";
END;
// имеются ли проводки по доначислению процентов
PRIVATE MACRO DealHasPACarries(DealID)
    VAR doc  = TRecHandler( "acctrn.dbt" );
    // получить шаги, в которых могли быть созданы проводки по доначислению процентов
    var steps = TRsbDataSet("SELECT doprstep_dbt.t_id_operation, t_id_step FROM doprstep_dbt " + 
    "INNER JOIN doproper_dbt ON doproper_dbt.t_id_operation = doprstep_dbt.t_id_operation " +
    "WHERE t_documentid = " + DealID + " AND doprstep_dbt.t_dockind = 102 AND t_isexecute = 'X' AND t_symbol IN ('н', 'i', 'g')");

    while (steps.MoveNext)
        ClearRecord(doc);
        while( GetDocsByOperStep( doc, steps.ID_Operation, steps.ID_Step ) )
            if (Index(doc.rec.Ground, "Начисление процентов"))
                return true;
            end;
        end;
    end;
    return false;
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    VAR PrevStepSymb, cmd, OPk, deal_pd, carry, blocksymb = "", UnrecDefDiff = $0, tdr_adj = "", tdr_adj_acc, stat;
    
    record deal( dl_tick );
    SetBuff( deal, Document );

    ID_Operation = IDOperation;
    ID_Step      = IDStep;
	
	// урегулирование парных счетов
	if (IsSALE(GetOperationGroup(deal.DealType)))
		tdr_adj = tdr_adjplR;
	else
		tdr_adj = tdr_adjatR;
	end;
	deal_pd = MMFirstDoc(DL_IBCDOC, deal.DealID, true);
	deal_pd.FindNumberAccount(tdr_adj, tdr_adj_acc, true, NULL, NATCUR);
	RegPairedAccounts(1, {curdate}, tdr_adj_acc, NULL, 2); 

    // данный шаг уже выполнялся
    
    PrevStepSymb = GetPrevStepSymb();
    if (PrevStepSymb != PERC_ACCRUAL_SYMB)
        if (not DealHasPACarries(deal.DealID))
            return 0;
        end;
    end;

    // 2 Вызов процедуры <Расчет суммы отсроченной разницы для учета в ОФР> из справочника <Процедуры МСФО>;
    // Если вызов произведен из цепочки шагов сделки привлечения ?Исполнение исходящих платежей ОД? или из цепочки шагов сделки размещения ?Исполнение входящих платежей ОД?:
    blocksymb = GetBlockSymb(ID_Operation, ID_Step);
    if (((blocksymb == "G") and IsBUY(GetOperationGroup(deal.DealType))) or 
        ((blocksymb == "I") and IsSALE(GetOperationGroup(deal.DealType))))
        OPk = MM_CalcDDA(DL_IBCDOC, Deal.DealID, {curdate}, 10/*CN_CONTEXT_PM_PRINC_EXEC*/, ID_Operation, ID_Step);
    else
        OPk = MM_CalcDDA(DL_IBCDOC, Deal.DealID, {curdate}, 0);
    end;
    
    if ((OPk == -1) or (OPk == 0))
        return 0;
    end;

    deal_pd = MMFirstDoc(DL_IBCDOC, deal.DealID, true);
    UnrecDefDiff = ПолучитьЗначениеПримечания(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 5/*MN_UNRECORDED*/, {curdate});
    if (UnrecDefDiff > 0)
        UnrecDefDiff = UnrecDefDiff - OPk;
    else
        UnrecDefDiff = UnrecDefDiff + OPk;
    end;
    MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 5, {curdate}, UnrecDefDiff);

    ConvSum( OPk, OPk, {curdate}, deal_pd.GetLeg().PFI, NATCUR);
    carry = MM_Carry;
    carry.FIIDPayer        = NATCUR;
    carry.FIIDReceiver     = NATCUR;
    carry.Chapter          = 1;
    carry.date_carry       = {curdate};
    carry.PrimaryDoc       = deal_pd;  
    carry.SumReceiver      = OPk;

    if (UnrecDefDiff > 0)
        if (isSale(Deal.DealGroup))
            carry.CatPayer    = tdr_plcorR(deal.TypeDoc);
            carry.CatReceiver = tdr_plodR(deal.TypeDoc);
        else
            carry.CatPayer    = tdr_atodP(deal.TypeDoc);
            carry.CatReceiver = tdr_atcorR(deal.TypeDoc);
        end;
    else
        if (isSale(Deal.DealGroup))
            carry.CatPayer    = tdr_plodP(deal.TypeDoc);
            carry.CatReceiver = tdr_plcorR(deal.TypeDoc);
        else
            carry.CatPayer    = tdr_atcorP(deal.TypeDoc);
            carry.CatReceiver = tdr_atodR(deal.TypeDoc);
        end;
    end;
    
    if (not carry.carry())
        return 1;
    end;
    return 0;
END;

NameMacroFile = "mmdd_j.mac";
