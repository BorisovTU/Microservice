/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : mmpm_s.mac
  Programmer  : Сабитов Р.Р.
  Description : Обработка платежа
  Comment     : 
└───────────────────────────────────────────────────────────────────────────*/
Import MmarkInter,globals,OprInter,InsCarryDoc,mmlibr,mmcnst_r;

MACRO ClosePayments (tick,leg,dat)
    FILE pm ("pmpaym") key 1;
    var processed = 0;
    macro filter
        return (pm.PaymStatus!=PM_FINISHED) and (pm.ValueDate==dat)
    end;

    macro processPaym (paym)
        if (not InsertPaymentStat(pm.PaymentID,PM_FINISHED))
            return false;
        end;

        if (pm.Purpose==PM_PURP_PERCENT)
            if (not InsertPcPay( MM_GetAutoPerc (leg.DealID,leg.LegID, PC_IBC_CON),
                                 pm.ValueDate,dat,pm.Amount))
                return false;
            end;
        end;

        return true;
    end;

    pm.DocKind = tick.BofficeKind;
    pm.DocumentID = tick.DealID;
    pm.Purpose = 0;
    pm.SubPurpose = 0;

    if (GetGE(pm) and limiter(tick,pm))
        if (filter)
            if (not processPaym(pm))
                return false;
            end;
            processed = processed +1;
        end;
        while (Next(pm) and limiter(tick,pm))
            if (filter)
                if (not processPaym(pm))
                    return false;
                end;
                processed = processed +1;
            end;
        end;
    end;

    if (processed==0)
        mm_error ("Не обработан ни один платеж");
        return false;
    end;

    return true;
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document)
    record tick (dl_tick);
    FILE leg (dl_leg);
    var dat;

    GetOprDate (Дата_ОбработкаПлатежа, dat);

    if (dat > {curdate})
        MsgBox ("Преждевременное выполнение шага");
        return 1;
    end;

    SetBuff (tick,Document);
 
    leg.LegKind = LEG_KIND_DL_TICK;
    leg.DealID = tick.DealID;
    leg.LegID = 1;
    if (not GetEq(leg))
        mm_error ("Не найден транш сделки");
        return 1;
    end;

    if (not ClosePayments(tick,leg,dat))
        mm_error ("Ошибка при закрытии платежей");
        return 1;
    end;

    return 0;
END;

NameMacroFile = "mmpm_s.mac";
