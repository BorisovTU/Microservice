/*
$Name:        mmmodfinres_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Отражение фин. результата от модификации"
*/

IMPORT mmlib, mmlibr, mmcarry;

RECORD mmhistmod("mmhistmod.dbt");

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0,
    ExecDate;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    record deal( dl_tick );
    SetBuff( deal, Document );

    ID_Operation = IDOperation;
    ID_Step      = IDStep;
    ExecDate     = {curdate};

    var FinRes = $0, AS, GBV, at = MM_Carry, deal_pd, AsCategory = 0;

    if (IsSale(deal.DealGroup))
        FinRes = MM_CalcFinResFA(deal.BOfficeKind, deal.DealID, ExecDate, 0);
        GBV = MM_CalcGBV(deal.BOfficeKind, deal.DealID, ExecDate, 0);
        MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 8/*MN_GROSS*/, ExecDate, GBV);
    else
        FinRes = MM_CalcFinResFO(deal.BOfficeKind, deal.DealID, ExecDate, 0);
    end;
    AS = MM_CalcAS(deal.BOfficeKind, deal.DealID, ExecDate, 0);
    MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 7/*MN_AMORTIZED*/, ExecDate, AS);
    GetMainObjAttr(NULL, OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 5, AsCategory, NULL, NULL, ExecDate);
    IF ((AsCategory == 1) or (AsCategory == 2))
        MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 10, ExecDate, AS);
    END;
    if (FinRes == 0)
        return 0;
    end;
    if (mmhistmod.FinRef == "")
        return 0;
    end;

    deal_pd = MMFirstDoc(deal.BofficeKind, deal.DealID, true);
    ConvSum(FinRes, FinRes, ExecDate, deal_pd.GetLeg().pfi, NATCUR);

    at = MM_Carry;
    at.FIIDPayer        =
    at.FIIDReceiver     = NATCUR;
    at.Chapter          = 1;
    at.date_carry       = ExecDate;
    at.PrimaryDoc       = deal_pd;  

    if (FinRes > 0)
        if (IsSale(deal.DealGroup))
            at.CatPayer    = tdr_adjplP;
            at.CatReceiver = tdr_plodR(deal.TypeDoc);
            at.Ground      = "Корректировка, увеличивающая стоимость сделки размещения";
        else
            at.CatPayer    = tdr_atodP(deal.TypeDoc);
            at.CatReceiver = tdr_adjatR;
            at.Ground      = "Корректировка, увеличивающая стоимость сделки привлечения";
        end;
    else
        if (IsSale(deal.DealGroup))
            at.CatPayer    = tdr_plodP(deal.TypeDoc);
            at.CatReceiver = tdr_adjplR;
            at.Ground      = "Корректировка, уменьшающая стоимость сделки размещения";
        else
            at.CatPayer    = tdr_adjatP;
            at.CatReceiver = tdr_atodR(deal.TypeDoc);
            at.Ground      = "Корректировка, уменьшающая стоимость сделки привлечения";
        end;
    end;
    at.SumReceiver = abs(FinRes);
    if (not at.carry())
        return 1;
    end;

    return 0;
END;

NameMacroFile = "mmmodfinres_j.mac";
