/*
$Name:        mmtcf_j.mac
$Module:      МБ Кредиты
$Description: Востребование суммы долга
*/
IMPORT mmcarry, mmlibr, mmlb_j;

VAR DealMaturity = date(0, 0, 0);

//Выполнение шага
PRIVATE MACRO ExecuteStep(Buffer, ticket)
  record deal(dl_tick);

  var
      OD = "", OD_new = "", СчетД = "", СчетК = "",
      FirstDoc = NULL, at = MM_Carry, СуммаОД = 0,
      newPaym = NULL, paym = NULL;

    SetBuff(deal, ticket);

    // получим счет по ОД
    FirstDoc = MMFirstDoc(DL_IBCDOC, Deal.DealID, true);
    if (not СчетСделки(tdr_rest(FirstDoc.GetTick().FlagTypeDeal), FirstDoc, OD, {curdate}, NULL, NULL,
                       FirstDoc.GetLeg().PFI, FirstDoc.GetFIRoleByCategory(tdr_rest(FirstDoc.GetTick().FlagTypeDeal), FirstDoc.GetLeg().PFI))) 
        mm_error("Ошибка при получении счета по ОД");
        return 1; 
    end; 


    // получим новый счет по ОД
    FirstDoc.SetParametr(MC_TYPE_PARAMETR_FINDATE, DealMaturity);
    FirstDoc.ActualCheckPeriod(tdr_rest(FirstDoc.GetTick().FlagTypeDeal), OD_new, NULL, FirstDoc.GetFIRoleByCategory(tdr_rest(FirstDoc.GetTick().FlagTypeDeal), FirstDoc.GetLeg().PFI),
                                NULL, {curdate}, FirstDoc.GetLeg().PFI);
    if (OD_new == "") 
        mm_error("Ошибка при получении счета по ОД");
        return 1; 
    end; 

    ПолучитьОстатокСчета(FirstDoc, NULL, OD, 1, СуммаОД, {curdate}, FirstDoc.GetLeg().PFI);

    if (OD != OD_new)
        at.date_carry   = {curdate};
        at.Chapter      = 1;
        at.FIID         = FirstDoc.GetLeg().PFI;
        at.Sum          = СуммаОД;
        at.Ground       = "Перенос суммы основного долга в связи с восстребованием";

        if(IsSALE( GetOperationGroup(FirstDoc.GetTick().DealType))) /* сделка размещения */
            at.AccountPayer       = OD_new;
            at.AccountReceiver    = OD;
        else // сделка привлечения
            at.AccountPayer     = OD;
            at.AccountReceiver  = OD_new;
        end;

        if(not at.carry())
            mm_error( "Ошибка при формировании проводки по переносу суммы основного долга в связи с восстребованием" );
            return 1;
        end;
    end;

    // генерируем платеж по ОД
    // найдем платеж по предоставлению средств - на основе его будем формировать платеж по ОД
    paym    = RsbPayment(DL_IBCDOC, deal.DealID, PM_PURP_PRINC, 0);

    newPaym = RsbPayment();
    newPaym.DocKind              = DL_IBCDOC;
    newPaym.DocumentID           = deal.DealID;
    newPaym.Purpose              = PM_PURP_PRINC_RET;
    newPaym.SubPurpose           = 0;
    newPaym.PaymentKind          = "Э";
    newPaym.Number               = 0;
    newPaym.Date                 =
    newPaym.PayDate              =
    newPaym.ValueDate            = DealMaturity;
    newPaym.BaseFIID             = 
    newPaym.PayerFIID            = 
    newPaym.ReceiverFIID         = 
    newPaym.FuturePayerFIID      = 
    newPaym.FutureReceiverFIID   = FirstDoc.GetLeg().PFI;
    newPaym.BaseAmount           = 
    newPaym.FutureBaseAmount     = 
    newPaym.PayerAmount          = 
    newPaym.ReceiverAmount       = СуммаОД;
    newPaym.ActuateFutureAmounts(TBA_AMOUNT);  
    newPaym.IsPlanPaym           = "X";
    newPaym.Ground               = "Погашение основного долга по сделке №" + deal.DealCode + " заключена " + String(deal.DealDate:z:f);
    newPaym.PaymStatus           = PM_READIED;

    if(IsSALE( GetOperationGroup(FirstDoc.GetTick().DealType))) /* сделка размещения */
        СчетД = paym.ReceiverAccount;
        СчетК = OD_new;
    else // сделка привлечения
        СчетД = OD_new;
        СчетК = paym.PayerAccount;
    end;

    newPaym.SetPayerPI(PAYMENTS_GROUP_UNDEF,
                         paym.ReceiverBankID,
                         paym.ReceiverBankCodeKind,
                         paym.ReceiverBankCode,
                         paym.ReceiverBankName,
                         paym.ReceiverBankCorrAcc,
                         FirstDoc.GetLeg().PFI,
                         1,
                         СчетД,
                         paym.Receiver,
                         paym.ReceiverName,
                         paym.ReceiverINN,
                         paym.ReceiverCodeKind,
                         ""
                         );

    newPaym.SetReceiverPI(PAYMENTS_GROUP_UNDEF,
                            paym.PayerBankID,
                            paym.PayerBankCodeKind,
                            paym.PayerBankCode,
                            paym.PayerBankName,
                            paym.PayerBankCorrAcc,
                            FirstDoc.GetLeg().PFI,
                            1,
                            СчетК,
                            paym.Payer,
                            paym.PayerName,
                            paym.PayerINN,
                            paym.PayerCodeKind,
                            ""
                            );

    if (newPaym.Payer == {OurBank})
        newPaym.ReceiverBankCorrCodeKind = paym.PayerBankCorrCodeKind;
        newPaym.ReceiverBankCorrCode     = paym.PayerBankCorrCode;
        newPaym.ReceiverBankCorrName     = paym.PayerBankCorrName;
        newPaym.ReceiverBankCorrAcc      = Paym.PayerBankCorrAcc;
    else
        newPaym.PayerBankCorrCodeKind    = paym.ReceiverBankCorrCodeKind;
        newPaym.PayerBankCorrCode        = paym.ReceiverBankCorrCode;
        newPaym.PayerBankCorrName        = paym.ReceiverBankCorrName;
        newPaym.PayerBankCorrAcc         = Paym.ReceiverBankCorrAcc;
    end;

    return 0;
END;
NameMacroFile = "mmtcf_j.mac"; 
