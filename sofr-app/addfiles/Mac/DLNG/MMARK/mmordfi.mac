/*
$Name:         mmordfi.mac
$Module:       МБК
$Description:  +Залог по выданному МБК / формирование распоряжения в СВО
*/

Import OprInter, DealsInter, VSInter;

const DIVISION_MM = 6000;       /* Сектор МБК */
/*      DL_VEKSELPAWNORDER = 112, /* распоряжение по залогу */
      DL_MMPAWN = 126,          /* договора залога МБК */
      ENTERING = 1,             /* документы входящие */
      ALLFININSTR = -1;
*/

/* Идентификатор референса "Номера распоряжений МБК"
*/
PRIVATE CONST REF_MBK_ID = 58;

MACRO FormingInstruction( Order, SysType ) /* договор залога МБК */
var OrderOfInstruction  = TBFile("dl_order"), /* договор распоряжения по залогу */
    Spg1 = TBFile("spground"), /* распоряжение по залогу */
    Spg2 = TBFile("spground"), /* основание договора залога МБК*/
    VSOrdLink = TArray,
    secur = TBFile("dl_secur"),
    cert = TBFile("dl_cert"),
    vsb = TBFile("vsbanner"),
    NmbOurVSBNR, /* кол-во собственных векселей */
    retstat = 0,
    stat,
    i = 0,
    s = "";

    Spg2.KeyNum = 6;
    Spg2.rec.SourceDocKind = DL_MMPAWN;
    Spg2.rec.SourceDocID = Order.ContractID;
    Spg2.rec.Direction = ENTERING;
    Spg2.rec.Division = DIVISION_MM;
    Spg2.rec.Department = Order.Department;
    if( NOT Spg2.GetEQ )
        msgbox("Не найдено основание договора залога МБК.");
        return 1;
    end;

    copy( Spg1, Spg2 );
    Spg1.rec.Kind = 2;
    Spg1.rec.AltXld = Spg2.rec.Xld;

    copy(OrderOfInstruction, Order);
    OrderOfInstruction.rec.ContractKind = Spg1.rec.Kind;
    OrderOfInstruction.rec.Systypes = SysType;
    OrderOfInstruction.rec.DocKind = DL_VEKSELPAWNORDER;
    OrderOfInstruction.rec.Kind_Operation = 0;

    /* заполним буфер связок договор-вексель */
    NmbOurVSBNR = i;
    secur.KeyNum = 0;
    secur.rec.ContractKind = DL_MMPAWN;
    secur.rec.ContractID = Order.ContractID;
    secur.rec.FIID = -1;
    stat = secur.GetGT;
    while( stat )
        if( (secur.rec.ContractKind != DL_MMPAWN) OR
            (secur.rec.ContractID != Order.ContractID) )
            stat = 0;
        else
        if( secur.rec.SecurityProperties )
            cert.KeyNum = 0;
            cert.rec.ContractKind = DL_MMPAWN;
            cert.rec.ContractID  = Order.ContractID;
            cert.rec.FIID = secur.rec.FIID;
            cert.rec.SecurityProperties = secur.rec.SecurityProperties;
            if( cert.GetGE )
              if( (cert.rec.ContractKind == DL_MMPAWN) AND
                  (cert.rec.ContractID   == Order.ContractID) AND
                  (cert.rec.FIID         == secur.rec.FIID) AND
                  (cert.rec.SecurityProperties == secur.rec.SecurityProperties) )
                vsb.KeyNum = 4;
                vsb.rec.BCFormKind = 1; /* VSBANNER_FORMKIND_SIMPLE */
                vsb.rec.BCSeries = cert.rec.Series;
                vsb.rec.BCNumber = cert.rec.NumberFirst;
                stat = vsb.GetGE;
                if( NOT stat )
                    retstat = 2;
                else
                    while( stat )
                        if( ( vsb.rec.BCFormKind == 1 ) AND
                            ( vsb.rec.Issuer == {OurBank} ) AND
                            ( cert.rec.NumberLast >= vsb.rec.BCNumber ) AND
                            ( cert.rec.Series == vsb.rec.BCSeries ) )
                            VSOrdLink(i) = TRecHandler("vsordlnk");
                            if( VSOrdLink(i) )
                                VSOrdLink(i).rec.BCID = vsb.rec.BCID;
                                VSOrdLink(i).rec.LinkKind = VSORDLNK_K_PAWN;
                                i = i + 1;
                                NmbOurVSBNR = i;
                            else
                                retstat = 2;
                            end
                        end;
                        stat = vsb.next;
                    end /* while */
                end /* if */
              end /* if( (cert.rec.ContractKind == DL_MMPAWN) AND */
            end /* cert->GetEq */
        end; /* secur.rec.SecurityProperties */
        stat = secur.next;
        end; /* (secur.rec.ContractKind != DL_MMPAWN) OR
            (secur.rec.ContractID != Order.ContractID) */
    end;

    if( NmbOurVSBNR > 0 )
        if(GenerateReference(REF_MBK_ID, s) != 0)
            msgbox("Ошибка при генерации номера |распоряжения о залоге");
        else
            OrderOfInstruction.rec.OrderNumber = s;
            Spg1.rec.Xld = Spg1.rec.AltXld = s;
        end;
        VS_InsertDL_ORDER( OrderOfInstruction, VSOrdLink, Spg1, Spg2 );
    end;

    return retstat;
END;

