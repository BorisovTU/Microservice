/*
$Name: mmcl_j.mac
$Module: МБК
$Description: Макрос выполнение шага "Корректировка резерва"
*/
IMPORT mmlibr, mmcarry, mmlb_j, mpclb, mmadd_j, mmpaymentcls, RsbFormsInter, mmclfnc,dl_lib;

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0;

PRIVATE VAR
    CorDebtLimit; // заполняется из закрытого кода

CONST
    KLa  = 0,
    KLb  = 1,
    KLAa = 2,
    KLAb = 3,
    KLBa = 4,
    KLbb = 5;

CONST 
    TYPE_DEBTLIM = 1,
    TYPE_ISSUANCELIM = 0;

PRIVATE MACRO GetAccSum(Deal)
    var ODSum = TRsbDataSet("select t_dealid from ddl_tick_dbt where t_parentid = " + Deal.DealID);

    var sum = 0;
    while(ODSum.MoveNext)
        var deal_pd = MMFirstDoc (DL_IBCDOC, ODSum.t_DealID, true);
        var balance;
        if (isSale(GetOperationGroup(Deal.DealType)))
        // размещение
            // Треб. с н.с.
            balance = 0.0;
            ПолучитьОстатокСчета(deal_pd, tdr_exprestR, "", 1, balance, {curdate}, deal_pd.GetLeg().PFI);
            ConvSum(balance, balance, {curdate}, deal_pd.GetLeg().PFI, Deal.LimitCur);
            sum = sum + balance;
        else
            // Обяз. с н.с.
            balance = 0.0;
            ПолучитьОстатокСчета(deal_pd, tdr_exprestP, "", 1, balance, {curdate}, deal_pd.GetLeg().PFI);
            ConvSum(balance, balance, {curdate}, deal_pd.GetLeg().PFI, Deal.LimitCur);
            sum = sum + balance;
        end;
        // ОД
        balance = 0.0;
        ПолучитьОстатокСчета(deal_pd, tdr_mainrest, "", 1, balance, {curdate}, deal_pd.GetLeg().PFI);
        ConvSum(balance, balance, {curdate}, deal_pd.GetLeg().PFI, Deal.LimitCur);
        sum = sum + balance;
    end;

    return sum;
END;

PRIVATE MACRO CheckNewLimit(Deal, NewLimit, isIssuanceLimit)
    if (isIssuanceLimit)
        if (Deal.IssuanceLimit == NewLimit)
            msgbox("Лимит выдачи не изменен");
            return 1;
        elif (NewLimit < GetСLTrnSum(deal.DealID, PM_PURP_PRINC))
            msgbox("Лимит выдачи не может быть меньше уже выданной суммы по договору");
            return 1;
        end;
    else
        if (Deal.DebtLimit == NewLimit)
            msgbox("Лимит задолженности не изменен");
            return 1;
        elif (NewLimit < GetAccSum(deal))
            msgbox("Лимит задолженности не может быть меньше суммы основного долга: " + GetAccSum(deal));
            return 1;
        end; 
    end;
    return 0;
END;

//класс панели параметров отчета
PRIVATE CLASS(TRsbPanel) CReportPanelParm(_deal)
    PRIVATE VAR
        fld_Sum           = NULL, // сумма лимита
        fld_DebtLimit     = NULL, // признак "Лимит выдачи"
        fld_IssuanceLimit = NULL, // признак "Лимит задолженности"
        deal              = NULL; // ссылка на сделку

    VAR
        repPath = "",
        repFileName = "";

    MACRO repFilePath()
        return this.repPath + this.repFileName;
    END;

    MACRO isDebtLimit()
        return fld_DebtLimit.Checked;
    END;
    MACRO Set_DebtLimit(newval: bool)
        fld_DebtLimit.Checked = newval;
    END;
    
    MACRO isIssuanceLimit()
        return fld_IssuanceLimit.Checked;
    END;
    MACRO Set_IssuanceLimit(newval: bool)
        fld_IssuanceLimit.Checked = newval;
    END;
  
    MACRO Sum()
        return fld_Sum.Value;
    END;
    MACRO Set_Sum(newval)
        fld_Sum.Value = newval;
    END;
    
    MACRO eventHandler(EV)
    
        if (EV.keyCode == 316)
            if (CheckNewLimit(deal, Sum(), isIssuanceLimit))
            else
                close(-EV.keyCode);
            end;

        end;
    END;
    
    PRIVATE MACRO InitDefValue(deal)
        if ((deal.IssuanceLimit > 0.0) and (deal.DebtLimit == 0.0))
            Set_Sum(deal.IssuanceLimit);
        elif ((deal.IssuanceLimit) == 0.0 and (deal.DebtLimit > 0.0))
            Set_Sum(deal.DebtLimit);
        else 
            Set_Sum(min(deal.IssuanceLimit, deal.DebtLimit));
        end;
        if (CorDebtLimit != 0)
            Set_DebtLimit("X");
            Set_IssuanceLimit("");
        else
            Set_DebtLimit("");
            Set_IssuanceLimit("X");
        end;
    END;

    //constructor
    InitTRsbPanel();
    caption = "Корректировка лимита";
    setPosition(50, 15);
    setSize(41, 4);
    status = "~F2~ Продолжить ~Esc~ Выход";
    deal = _deal;

    // инициализация полей панели
    fld_Sum            = TRsbEditField();
    fld_Sum.name       = "fld_Sum";
    fld_Sum.dataType   = 25; //FT_NUMERIC
    fld_Sum.valuePrecision = 2;
    fld_Sum.textLength (19);
    fld_Sum.setPosition(18, 1);
    fld_Sum.setSize(21, 1);
    addControl(fld_Sum);
    addLabel(TRsbLabel(2, 1, "Новая сумма лимита"));

    fld_DebtLimit           = TRsbRadioButton(1);
    fld_DebtLimit.name      = "fld_DebtLimit";
    fld_DebtLimit.dataType  = 12; //FT_CHR
    fld_DebtLimit.setPosition(2, 2);
    addControl(fld_DebtLimit);
    addLabel(TRsbLabel(4, 2, "Лимит задолженности"));
    
    fld_IssuanceLimit           = TRsbRadioButton(1);
    fld_IssuanceLimit.name      = "fld_IssuanceLimit";
    fld_IssuanceLimit.dataType  = 12; //FT_CHR
    fld_IssuanceLimit.setPosition(2, 3);
    addControl(fld_IssuanceLimit);
    addLabel(TRsbLabel(4, 3, "Лимит выдачи"));

    addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));

    InitDefValue(deal);
END;

PRIVATE MACRO CreateTrn(deal_pd, Scor, trnType)

    if (trnType == KLa)
        if (isSale(GetOperationGroup(deal_pd.GetTick().DealType)))
        // размещение
            trnType = KLBa;
        else
            trnType = KLAa;
        end;
    elif (trnType == KLb)
        if (isSale(GetOperationGroup(deal_pd.GetTick().DealType)))
        // размещение
            trnType = KLBb;
        else
            trnType = KLAb;
        end;
    end;

    var carry          = MM_Carry;

    if (deal_pd.GetTick().numberpack == 990 )
       Scor=0;
    end;

    carry.SumReceiver  = Scor;
    carry.Chapter      = 3;
    carry.date_carry   = {curdate};
    carry.PrimaryDoc   = deal_pd;
    carry.FIIDPayer    = deal_pd.GetTick().LimitCur;
    carry.FIIDReceiver = deal_pd.GetTick().LimitCur;

    if (trnType == KLAa)
        carry.CatPayer       = tdr_oblR;
        carry.CatReceiver    = tdr_corespacc_vb;
        carry.FiRoleReceiver = FIROLE_CORACC_ACTIVE;
        carry.Ground         = "Увеличение лимита по договору КЛ, привлечение";
    elif (trnType == KLAb)
        carry.CatPayer       = tdr_corespacc_vb;
        carry.FiRolePayer    = FIROLE_CORACC_ACTIVE;
        carry.CatReceiver    = tdr_oblR;
        carry.Ground         = "Уменьшение лимита по договору КЛ, привлечение";
    elif (trnType == KLBa)
        carry.CatPayer       = tdr_corespacc_vb;
        carry.FiRolePayer    = FIROLE_CORACC_PASSIVE;
        carry.CatReceiver    = tdr_oblP;
        carry.Ground         = "Увеличение лимита по договору КЛ, размещение";
    elif (trnType == KLBb)
        carry.CatPayer       = tdr_oblP;
        carry.CatReceiver    = tdr_corespacc_vb;
        carry.FiRoleReceiver = FIROLE_CORACC_PASSIVE;
        carry.Ground         = "Уменьшение лимита по договору КЛ, размещение";
    end;

    carry.Ground         = MM_ground(deal_pd.GetTick().Dealid,512);

    return carry.carry();
END;

PRIVATE MACRO MakeTrn(deal_pd, NewSum, isIssuanceLimit)

    var Sl = 0.0, Scor = 0.0, trnType, Slnew;
    if (isSale(GetOperationGroup(deal_pd.GetTick().DealType)))
    // размещение
        ПолучитьОстатокСчета(deal_pd, tdr_oblP, "", 3, Sl, {curdate}, deal_pd.GetTick().LimitCur);
    else
        ПолучитьОстатокСчета(deal_pd, tdr_oblR, "", 3, Sl, {curdate}, deal_pd.GetTick().LimitCur);
    end;
    if ((deal_pd.GetTick().IssuanceLimit > 0.0) and (deal_pd.GetTick().DebtLimit == 0.0))
        if (newSum > deal_pd.GetTick().IssuanceLimit)
            Scor = newSum - deal_pd.GetTick().IssuanceLimit;
            trnType = KLa;
        else
            Scor = deal_pd.GetTick().IssuanceLimit - newSum;
            trnType = KLb;
        end;

    elif ((deal_pd.GetTick().IssuanceLimit == 0.0) and (deal_pd.GetTick().DebtLimit > 0.0))
        if (newSum > deal_pd.GetTick().DebtLimit)
            Scor = newSum - deal_pd.GetTick().DebtLimit;
            trnType = KLa;
        else
            Scor = deal_pd.GetTick().DebtLimit - newSum;
            trnType = KLb;
        end;

    elif ((deal_pd.GetTick().IssuanceLimit > 0.0) and (deal_pd.GetTick().DebtLimit > 0.0))
        if (isIssuanceLimit)     // задан лимит выдачи
            if (newSum >= deal_pd.GetTick().IssuanceLimit)
                Slnew = deal_pd.GetTick().IssuanceLimit;
            else
                Slnew = newSum;
            end;
        else
            if (newSum >= deal_pd.GetTick().DebtLimit)
                Slnew = deal_pd.GetTick().DebtLimit;
            else
                Slnew = newSum;
            end;
        end;

        if (Slnew > Sl)
            Scor = Slnew - Sl;
            trnType = KLa;
        elif (Slnew < Sl)
            Scor = Sl - Slnew;
            trnType = KLb;
        else 
            return true;
        end;

    end;

    return CreateTrn(deal_pd, Scor, trnType);

END;

// Выполнение шага
PRIVATE MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    record deal( dl_tick );

    SetBuff( deal, Document );

    ID_Operation = IDOperation;
    ID_Step      = IDStep;

    if (CheckExistence(LEG_LegKind_DealID_LegID, LEG_KIND_DL_TICK, deal.DealID, 1))
        mm_error( "Не найден транш сделки ", deal.DealCode );
        return 1;
    end;
    var deal_pd = MMFirstDoc (deal.BOfficeKind, deal.DealID, true);

    var stat = 0;
    var panel = CReportPanelParm(deal);
    if (panel.run() != -316)
        return 1;
    end;

    if (MakeTrn(deal_pd, panel.Sum(), panel.isIssuanceLimit()))
        if (panel.isIssuanceLimit)
            deal.IssuanceLimit = panel.Sum();
        else
            deal.DebtLimit     = panel.Sum();
        end;
        copy (Buffer, deal);

        return 0;
    end;

    return 1;

END;

NameMacroFile = "mmcl_j.mac";


MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */  


  RECORD deal(dl_tick);

  private var rs0,que;
  SetBuff(deal, FirstDoc );


  if ( CommitOrRollback == 1 )

         if (deal.numberpack == 990 )

           	  if(GetTrue(true, "Обнулить номер пачки ?"))
            que="update ddl_tick_dbt set  t_numberpack=0  where t_dealid="+deal.dealid;
            rs0 = LnGetRecordset(que);
                  end;
    

         end;


  end;

END;
