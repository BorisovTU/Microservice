/*
$Name:        mmcntrog_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Исполнение исходящих платежей"
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : mmog_j.mac
  Programmer  : Куперин М.В.
  Comment     : 
└───────────────────────────────────────────────────────────────────────────*/
IMPORT mmlibr, mmcarry, mmlb_j, mpclb, mmadd_j, mmpaymentcls, mmclfnc, mmpenalt, mmservop, MmarkInter;

/* Инициализируются бэк-офисом */
PRIVATE VAR PlanIDs = TArray,       /*Массив идентификаторов планируемых платежей*/
             KvitAmounts = TArray,   /*Массив сумм к исполнению*/
             ExecDate;               /*Дата исполнения*/

PRIVATE VAR PlanPayms = TArray,     /*Массив обрабатываемых платежей*/
             contr_pd = NULL;

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0;

PRIVATE MACRO Send(idx)
    var
        paym    = PlanPayms[idx],
        newPaym = NULL, MM_PaymObj = NULL,
        pml = TRecHandler("pmlink.dbt"),
        pm = NULL, at = NULL;

    if ((paym.FuturePayerAmount == paym.FuturePayerAmount)and
    		 (money(round(paym.FuturePayerAmount,2)) == money(round(KvitAmounts[idx],2)) )
        )  
        pm = paym;
    else
        MM_PaymObj = MM_RsbPayment(MMPAYM_CREATEBYPAYMOBJ, paym, paym.SubPurpose, KvitAmounts[idx], ExecDate);
        newPaym = MM_PaymObj.Payment;

        pml.rec.Amount = KvitAmounts[idx];
        pml.rec.FIID   = newPaym.BaseFIID;
        pml.rec.IPSign = "-";
        pml.rec.PPSign = "";
        Paym.LinkPayment(newPaym, PMLINK_KIND_KVITING, pml);

        newPaym.Actuate();
        pm = newPaym;
    end;

    pm.ValueDate = ExecDate;
    if(pm.ReceiverGroup == PAYMENTS_GROUP_INTERNAL)
        at = MM_Carry;
        at.date_carry       = ExecDate;
        at.PrimaryDoc       = contr_pd;
        at.FIID             = contr_pd.GetContr().FIID;
        at.AccountPayer     = pm.FuturePayerAccount;
        at.AccountReceiver  = pm.FutureReceiverAccount;
        at.Sum              = KvitAmounts[idx];
        at.Chapter          = 1;
        at.Ground           = "Погашение процентных обязательств без нарушения срока";
        at.PaymentObj       = pm;

        if (not at.carry())
            return false; 
        end;

        pm.PaymStatus = PM_FINISHED;
        pm.isFactPaym = "X";
    elif (pm.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL)
            pm.PaymStatus = PM_READY_TO_SEND;
            pm.isFactPaym = "X";
        
    else
        return false;
    end;		

    return true;
END;

PRIVATE MACRO PreparePayment(contr_pd)
    VAR OprServDoc, query, RsbData, i = 0;
    OprServDoc = GetOprServDoc();
    ExecDate = OprServDoc.rec.CommDate;
    if (OprServDoc.rec.DocKind != 212)
        return;
    end;
    // действия для шага "Оплата затрат"

    query = "select t_paymentid, t_amount, ddlcomis_dbt.t_id from dpmpaym_dbt" + 
            " inner join ddlcomis_dbt on ddlcomis_dbt.t_id = dpmpaym_dbt.t_subpurpose" +
            " where dpmpaym_dbt.t_dockind = " + DL_SFCONTR + 
            " and dpmpaym_dbt.t_documentid = " + contr_pd.GetContr().ID + " and t_purpose = 83 and t_paymstatus = 1000";

    if (OprServDoc.rec.ComNumber > 0)
        query = query + " and t_comnumber = " + OprServDoc.rec.ComNumber;
    end; 
    if (OprServDoc.rec.ContractID > 0)
        query = query + " and ddlcomis_dbt.t_contract = " + OprServDoc.rec.ContractID;
    end;  

    RsbData = TRsbDataSet(query);
    while (RsbData.MoveNext)
        MM_ChangeComEndDate(RsbData.ID, OprServDoc.rec.CommDate);
        PlanIDs[i] = RsbData.PaymentID;
        KvitAmounts[i] = RsbData.Amount;
        i = i + 1;
    end;

END;

// Выполнение шага
PRIVATE MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    var
        idx = 0;
    record sfcontr( sfcontr );

    SetBuff( sfcontr, Document );

    ID_Operation = IDOperation;
    ID_Step      = IDStep;

    contr_pd = MMFirstDoc (DL_SFCONTR, sfcontr.ID, false);
    // шаг был запущен из сервисной операции
    if (isServOpMode())
        PreparePayment(contr_pd);
    end;

    if (PlanIDs.size == 0)
        mm_error ("Нет платежей");
        return 1;
    end;

    while (idx < PlanIDs.size)
        PlanPayms[idx] = RsbPayment(PlanIDs[idx]);

        if (PlanPayms[idx].PaymentID == 0)
            mm_error ("Не найден платеж");
            return 1;
        end;

        if (PlanPayms[idx].ValueDate != ExecDate)
            if( not GetTrue( true, "Планируемая дата платежа "+
                                    "|не равна дате исполнения."+
                                    "|Исполнить платеж ?" )) 
                return 1;
            end;
        end;
		
        idx = idx+1;
    end;

    idx = 0;
    while (idx < PlanPayms.size)
        if (not Send(idx))
            return 1;
        end;
        idx = idx + 1;
    end;

    return 0;
END;

NameMacroFile = "mmcntrog_j.mac";
