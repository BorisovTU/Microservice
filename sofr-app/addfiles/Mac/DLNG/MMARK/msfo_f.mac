/*
$Name:        msfo_f.mac
$Module:      МБ Кредиты
$Description: 2.7.2   Отражение затрат, не оплаченных банком на 01.01.2019
*/
import mmlibr, MoCommon, mctplprm, OprInter, SfInter, mmcnst_j, mmlibr, mmlb_j, PaymInter, mmcarry, msfo_mmupgrdlib;

PRIVATE VAR Currency, FirstDoc = NULL, ExecDate;

PRIVATE VAR MCAccDoc = TBFile("mcaccdoc.dbt", "W", 0),
            dlcomis  = TBFile("dlcomis.dbt", "W", 0);

RECORD settacc(settacc);

PRIVATE MACRO Проводка(deal_pd, Sum, AccountDebt)
    VAR at;
    at = MM_Carry;
    at.date_carry       = ExecDate;
    at.PrimaryDoc       = deal_pd;
    at.FIIDPayer        = 
    at.FIIDReceiver     = deal_pd.GetLeg().PFI;
    at.Chapter          = 1;
    at.Ground           = "Перенос по МСФО-9";
    at.Sum              = Sum;
    at.AccountPayer     = AccountDebt;
    at.CatReceiver      = tdr_expcalcs;

    return at.carry();
END;

PRIVATE MACRO СформироватьПлатеж(deal_pd, DealComID, Sum, PlanPayDate)
    VAR Payment, DefaultCodeKind;
    // формирование платежа
    Payment = RsbPayment();
    Payment.DocKind              = deal_pd.GetTick().BOfficeKind;
    Payment.DocumentID           = deal_pd.GetTick().DealID;
    Payment.Purpose              = PM_PURP_COSTPM;
    Payment.SubPurpose           = DealComID;
    Payment.OrderFIID            =
    Payment.BaseFIID             = 
    Payment.PayerFIID            = 
    Payment.ReceiverFIID         = 
    Payment.FuturePayerFIID      = 
    Payment.FutureReceiverFIID   = deal_pd.GetLeg().PFI;
    Payment.OrderAmount          =
    Payment.PayerAmount          = 
    Payment.ReceiverAmount       = 
    Payment.BaseAmount           = 
    Payment.FutureBaseAmount     = Sum;
    Payment.ActuateFutureAmounts(TBA_AMOUNT);
    Payment.IsPlanPaym           = "X";
    Payment.Date                 = 
    Payment.PayDate              = 
    Payment.OutTransferDate      =
    Payment.ValueDate            = PlanPayDate;
    Payment.ClientDate           = ExecDate;
    Payment.Number               = String(Payment.SubPurpose); 
    Payment.Oper                 = deal_pd.GetTick().Oper;
    Payment.Origin               = PAYMENT_OR_AUTO;
    Payment.PaymStatus           = PM_READIED;
    if ((Payment.DocKind == DL_IBCDOC) and (deal_pd.GetTick().Netting == 3))
        Payment.Netting = "X";
    else
        Payment.Netting = "";
    end;

    if (deal_pd.GetLeg().PFI == NATCUR)
        DefaultCodeKind = PTCK_BIC;
    else
        DefaultCodeKind = PTCK_SWIFT;
    end;

    var FindAccount = "";
    deal_pd.IsActAccount(tdr_expcalcs, FindAccount, true, deal_pd.GetFIRoleByCategory(tdr_expcalcs, deal_pd.GetLeg().PFI), NULL, ExecDate, NULL);
    if (FindAccount == "")
        deal_pd.ActualAccount(tdr_expcalcs, FindAccount, true, deal_pd.GetFIRoleByCategory(tdr_expcalcs, deal_pd.GetLeg().PFI), NULL, ExecDate, NULL);
    end;

    Payment.SetPayerPI(PAYMENTS_GROUP_UNDEF,
            {OurBank},
            DefaultCodeKind,
            "",
            "",
            "",
            deal_pd.GetLeg().PFI,
            1,
            FindAccount,
            {OurBank},
            "",
            "",
            DefaultCodeKind,
            ""
            );

    if (not FindSETTACC_PMAUTO(deal_pd.GetTick().PaymAgent, FIKIND_CURRENCY, deal_pd.GetLeg().PFI, PTSK_CURRDL, deal_pd.GetTick().BOfficeKind, PM_PURP_COSTPM, 0, settacc));
    elif (not FindSETTACC_PMAUTO(deal_pd.GetTick().PaymAgent, FIKIND_CURRENCY, deal_pd.GetLeg().PFI, PTSK_CURRDL, deal_pd.GetTick().BOfficeKind, 0, 0, settacc));
    elif (not FindSETTACC_PMAUTO(deal_pd.GetTick().PaymAgent, FIKIND_CURRENCY, deal_pd.GetLeg().PFI, PTSK_CURRDL, 0, PM_PURP_COSTPM, 0, settacc));
    elif (not FindSETTACC_PMAUTO(deal_pd.GetTick().PaymAgent, FIKIND_CURRENCY, deal_pd.GetLeg().PFI, PTSK_CURRDL, 0, 0, 0, settacc));
    end;

    Payment.SetReceiverPI(PAYMENTS_GROUP_UNDEF,
                        settacc.BankID,
                        settacc.BankCodeKind,
                        settacc.BankCode,
                        settacc.BankName,
                        settacc.CorrAcc,
                        settacc.FIID,
                        settacc.Chapter,
                        settacc.Account,
                        settacc.PartyID,
                        settacc.RecName,
                        settacc.INN,
                        settacc.CodeKind,
                        settacc.Code
                        );

    Payment.Actuate();
END;

MACRO ExecuteStep(Buffer, Document)
   record deal(dl_tick);
   SetBuff(deal, Document);

   ExecDate = {curdate}; // Как дату передать ?

   VAR deal_pd, cmd = RsdCommand(RslDefCon, "SELECT * FROM DMSFO_UPGRADE_DBT WHERE T_DEALID = ?");
   cmd.AddParam("p1", RSDBP_IN); cmd.Value("p1") = deal.DealID;
   cmd.Execute();
   VAR rs = RsdRecordSet(cmd);
   IF (rs.MoveNext())
      VAR ContrID    = rs.Value("T_SUM1");   // ID договора обслуживания
      VAR Cur        = rs.Value("T_SUM2");   // Валюта
      VAR Sum        = rs.Value("T_SUM3");   // Сумма произведенных затрат, относящихся к периоду с 01.01.2019
      VAR ComType    = rs.Value("T_SUM4");   // ID вида комиссии из ПЗО
      VAR Account    = rs.Value("T_TEXT1");  // Номер счета
      deal_pd = MMFirstDoc (deal.BOfficeKind, deal.DealID, true);

      InsertMCAccDoc(MCAccDoc, Account, deal.BOfficeKind, deal.DealID, deal_pd.GetLeg().PFI, ExecDate);
      if (not Проводка(deal_pd, Sum, Account))
         return 1;
      end;
      if (not InsertDLCOMIS(dlcomis, deal.BOfficeKind, deal.DealID, ContrID, ComType, Sum, 0, ExecDate, date(0,0,0)))
         return 1;
      end;
      СформироватьПлатеж(deal_pd, dlcomis.rec.ID, Sum, ExecDate);
   END;
      
   RETURN 0;
END;

NameMacroFile = "msfo_d.mac";