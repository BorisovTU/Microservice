/*
$Name:        mmservop.mac
$Module:      МБ Кредиты
$Description: Сервисные операции
*/
IMPORT BankInter;
/****************************************************************************************/
/* Документ сервисной операции */
PRIVATE VAR OprServDoc = NULL;

/*Работает сервисная операция*/
PRIVATE VAR isServOp = false;

/*Файл отчетов: Отчет по ошибкам, Результаты операции */
PRIVATE VAR SvOpReportFile = NULL;

/*Регистрация ошибок во время выполнения сервисной перации*/
PRIVATE VAR
    SvOpErrorArray = TArray;

/****************************************************************************************/
/*Инициализация при запуске сервисной операции*******************************************/
PRIVATE MACRO SvOpInit(_OprServDoc)
    record comm( dl_comm);
    OprServDoc = TRecHandler("dl_comm.dbt");

    SetBuff(comm, _OprServDoc);
    copy(OprServDoc, comm);

    SvOpErrorArray.size = 0;

    isServOp = true;
END;

PRIVATE MACRO GetMacroName()
    if (OprServDoc.rec.DocKind == 163)
        return "mmrsrv.mac";
    elif (OprServDoc.rec.DocKind == 184)
        return "mmrevaltcc.mac";
    elif (OprServDoc.rec.DocKind == 206)
        return "mmsecrsrv.mac";
    elif ((OprServDoc.rec.DocKind == 211) or (OprServDoc.rec.DocKind == 212) or (OprServDoc.rec.DocKind == 213))
        return "mmacccom.mac";
    elif (OprServDoc.rec.DocKind == 218)
        return "mmchrate.mac";
    elif (OprServDoc.rec.DocKind == 219)
        return "mmsprc.mac";
    elif (OprServDoc.rec.DocKind == 221)
        return "mmhedg.mac";
    else
        return "";
    end;
end;

/*Проверка - работаем в режиме сераисной операции или нет********************************/
MACRO isServOpMode()
    return isServOp;
END;
/*Получить документ сервисной операции при условии, что она запущена ********************/
MACRO GetOprServDoc()
    VAR retVal = NULL;

    if (isServOp)
        retVal = OprServDoc;
    else
        retVal = TRecHandler("dl_comm.dbt");;
    end;

    return retVal;
END;
/*Регистрация ошибок при выполнении сервисной операции***********************************/
PRIVATE CLASS SvOpError(_OpCode, _ObjCode, _NumErr, _StrErr)
    var
        OpCode  = _OpCode, 
        ObjCode = _ObjCode, 
        NumErr  = _NumErr, 
        StrErr  = _StrErr; 
END;

/*Регистрация ошибок при выполнении сервисной операции***********************************/
CLASS ProcObj(_OpCode, _ObjID, _ObjN, _NewVal, _OldVal, _ErrCode)
    var
        OpCode  = _OpCode, 
        ObjID   = _ObjID,
        ObjN    = _ObjN, 
        NewVal  = _NewVal, 
        OldVal  = _OldVal,
        ErrCode = _ErrCode;
END;

MACRO SvOpSayError(_ObjCode, _NumErr, _StrErr)
    if (isServOp)
        if ((_StrErr == NULL)or(_StrErr == "")) 
            _StrErr = "Ошибка при выполнении шага."
        end;

        SvOpErrorArray[SvOpErrorArray.size] = SvOpError(OprServDoc.rec.CommCode, _ObjCode, _NumErr, _StrErr);
    end;
END;

/*Добавить строку в отчет. Срабатывает, когда формируются отчеты*************************/
MACRO AddSvOpReportLine(_Str)
    VAR 
        retVal = false;

    if (SvOpReportFile != NULL)
        insert(SvOpReportFile, _Str);
        retVal = true;
    end;

    return retVal;
END;
/*Вывод отчета об ошибках*****************************************************************/
PRIVATE MACRO PrintReportError_def()
    VAR
        idx = 0, cntError = SvOpErrorArray.Size;

    AddSvOpReportLine("┌──────────────────────────────┬───────┬──────────────────────────────────────────────────────────────────────────────────┐");
    AddSvOpReportLine("│         Номер сделки         │№Ошибки│                               Сообщение                                          │");
    AddSvOpReportLine("├──────────────────────────────┼───────┼──────────────────────────────────────────────────────────────────────────────────┤");

    while( idx < cntError )
        AddSvOpReportLine("│" + 
                          string(SvOpErrorArray(idx).ObjCode:30) + "│" +  
                          string(SvOpErrorArray(idx).NumErr:7)   + "│" +  
                          string(SvOpErrorArray(idx).StrErr:82)  + "│"  );
        idx = idx + 1;
    end;

    AddSvOpReportLine("└──────────────────────────────┴───────┴──────────────────────────────────────────────────────────────────────────────────┘");

    return true;
END;

PRIVATE MACRO SvOpPrintReportError()
    VAR
        RepMacro       = "",
        showRep        = true,
        SvOpReportName = "mmservop";

    file SvOpRepErrorFile() txt write;

    if (SvOpErrorArray.Size > 0)
        open(SvOpRepErrorFile, GetTxtFileName(SvOpReportName));

        SvOpReportFile = SvOpRepErrorFile;

        RepMacro = GetMacroName();

        if (RepMacro != "")
            showRep = ExecMacroFile(RepMacro, "PrintReportError", SvOpErrorArray);
        else
            showRep = PrintReportError_def();
        end;

        close(SvOpRepErrorFile);
    else
        showRep = false;
    end;

    if (not showRep)
        exit(1);
    else
        ViewFile(SvOpRepErrorFile);
        exit(1);
    end;
END;
/*Вывод отчета о результатах работы сервисной операции***********************************/
PRIVATE MACRO SvOpPrintReport(_ServDoc)
    VAR
        RepMacro = "",
        showRep = true,
        SvOpReportName = "mmservop";

    file SvOpRepFile() txt write;

    if (isServOp)
        ;//серв. операция уже нициализирована
    elif (ValType(_ServDoc) != V_UNDEF) // надо инициализировать
        SvOpInit(_ServDoc);
    else
        showRep = false;
    end;

    if (showRep)
        open(SvOpRepFile, GetTxtFileName(SvOpReportName));
        SvOpReportFile = SvOpRepFile;
        RepMacro = GetMacroName();
        showRep = ExecMacroFile(RepMacro, "PrintReport");
        close(SvOpRepFile);
        SvOpReportFile = NULL;
    end;

    if (not showRep)
        ;
    else
        ViewFile(SvOpRepFile);
    end;

    isServOp = false;

    exit(1);
END;
/*Фильтр отбора сделок для сервисной операции********************************************/
PRIVATE MACRO SvOpFilter(_OprServDoc, _DealID)       
    VAR
        FltrMacro = GetMacroName(),
        retVal = true;

    retVal = ExecMacroFile(FltrMacro, "OpFilter", _OprServDoc, _DealID);

    return retVal;
END;
