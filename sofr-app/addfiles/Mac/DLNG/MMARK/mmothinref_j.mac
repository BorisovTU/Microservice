/*
$Name:        mmothinref_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Отражение прочих доходов"
*/

IMPORT mmlibr, mmlib, mmservop, mmcarry, PTInter, MmarkInter, SfInter;

PRIVATE CONST
    MM_OTHINREF_ERR_GETCLSCOM = 11,
    MM_OTHINREF_ERR_CALCCOST  = 12,
    MM_OTHINREF_ERR_UNCOSTS   = 13;

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0;

/* Массивы с информацией о исполняемых платежах для квитовки */
/* Инициализируются бэк-офисом */
PRIVATE VAR
    PlanIDs       = TArray,
    FactIDs       = TArray,
    KvitAmounts   = TArray,
    PlanValueDate = TArray,
    ExecDate;

PRIVATE VAR mmcomiss  = TBfile("mmcomiss.dbt");
PRIVATE VAR sfcomiss  = TBfile("sfcomiss.dbt");

PRIVATE MACRO GetNDSRateIfСharged(dlcomis, ndsdate)
    var nds_rate:double = 0;
    if (sfcomiss.rec.PayNDS == 2) // облагается
        GetNDSRateByDate(nds_rate, BILNDSRATE_BASE, ndsdate);
        return nds_rate;
    else
        return 0;
    end;
END;

PRIVATE MACRO CreateTrn(Sum, Type, deal_pd, ComisId)
    if (Sum == 0)
        return true;
    end;
    VAR at, FIRole = 10000 + ComisId;
    at = MM_Carry;
    at.date_carry       = ExecDate;
    at.PrimaryDoc       = deal_pd;
    at.FIIDPayer        = deal_pd.GetLeg().PFI;
    at.FIIDReceiver     = NATCUR;
    at.Chapter          = 1;
    at.SumPayer         = Sum;
    at.CatPayer         = get_othinc(deal_pd.GetTick().DealType, ExecDate);
    at.FIRolePayer      = FIRole;

    if (Type == 1)
        at.CatReceiver = tdr_plcomR(deal_pd.GetTick().TypeDoc);
        at.Ground      = "Отражение процентных доходов по комиссиям.";
    elif (Type == 2)
        at.CatReceiver = tdr_plodR(deal_pd.GetTick().TypeDoc);
        at.Ground      = "Отражение операционных доходов по комиссиям единовременно.";
    elif (Type == 3)
        at.CatReceiver = tdr_plodR(deal_pd.GetTick().TypeDoc);
        at.Ground      = "Отражение операционных доходов по комиссиям.";
    elif (Type == 4)
        at.CatReceiver = tdr_plcomR(deal_pd.GetTick().TypeDoc);
        at.Ground      = "Отражение процентных доходов по комиссиям единовременно.";
    elif (Type == 5)
        at.CatReceiver = tdr_nds;
        at.Ground      = "Отражение НДС уплаченного по комиссиям.";
    end;
    return at.carry();
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    record deal( dl_tick );
    SetBuff( deal, Document );

    ID_Operation = IDOperation;
    ID_Step      = IDStep;
    var OprServDoc, deal_pd, query, RsbData, Сз = $0, ндс = 0.0, Сндс = $0, stat, NoteKind = 0, НзПц, НзОп, Нз;
    // шаг учет комиссий выполнен
    if (not StepIsExecute(deal.DealID, deal.BofficeKind, "ш"))
        return 0;
    end;

    OprServDoc = GetOprServDoc();
    if (PlanIDs.size == 0)
        ExecDate = OprServDoc.rec.CommDate;
        if (not isServOpMode())
            ExecDate = {curdate};
        end;
    end;

    deal_pd = MMFirstDoc (DL_IBCDOC, deal.DealID, true);

    НзПц = ПолучитьЗначениеПримечания(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 12, ExecDate);
    НзОп = ПолучитьЗначениеПримечания(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 13, ExecDate);

    query = "select * from ddlcomis_dbt " +
                          "where t_dockind = " + DL_IBCDOC + 
                          " and t_docid = " + deal.DealID;

    if (OprServDoc.rec.ContractID > 0)
        query = query + " and t_contract = " + OprServDoc.rec.ContractID;
    end;
    if (OprServDoc.rec.ComNumber > 0)
        query = query + " and t_comnumber = " + OprServDoc.rec.ComNumber + " and t_feetype = 6";
    end;

    RsbData = TRsbDataSet(query);
    while (RsbData.MoveNext)
        mmcomiss.rec.FeeType = RsbData.FeeType;
        mmcomiss.rec.Number  = RsbData.ComNumber;
        if (not mmcomiss.GetEQ)
            SvOpSayError(deal.DealCode, MM_OTHINREF_ERR_GETCLSCOM, "Не удалось получить запись из справочника комиссий МБК");
            return 1;
        end;
        if( not SfGetComiss( RsbData.FeeType, RsbData.ComNumber, sfcomiss ) )
            SvOpSayError(deal.DealCode, MM_OTHINREF_ERR_GETCLSCOM, "Не удалось получить комиссию");
            return 1;
        end; 
        if (sfcomiss.rec.ReceiverID == {HeadBankID})
        if (mmcomiss.rec.Type == 1) // процентный
            NoteKind = 12; //MN_RETAINED
        elif (mmcomiss.rec.Type == 2) // операционный
            NoteKind = 13; //MN_OPERATIONAL
        end;

        ндс = GetNDSRateIfСharged(RsbData, ExecDate);
        // единовременно или принято решение не размещать/привлекать денежные средства
        if (mmcomiss.rec.RecognitProc == 1) 
            Нз = RsbData.Sum;
            ConvSumCross(Нз, Нз, ExecDate, sfcomiss.rec.FIID_Comm, deal_pd.GetLeg().PFI);
            Сндс = Нз/(100+ндс)*ндс;
            Нз   = Нз - Сндс;
            if ((mmcomiss.rec.Type == 2))
                stat = CreateTrn(Нз, 2, deal_pd, RsbData.ID);
                НзОп = НзОп - Сндс - Нз;
            elif (mmcomiss.rec.Type == 1) // процентный
                stat = CreateTrn(Нз, 4, deal_pd, RsbData.ID);
                НзПц = НзПц - Сндс - Нз;
            end;
            if (stat)
                stat = CreateTrn(Сндс, 5, deal_pd, RsbData.ID);
            end;
            if (not stat)
                return 1;
            end;
            
        elif (mmcomiss.rec.RecognitProc == 2) // равномерно
            if (mmcomiss.rec.Type == 1) // процентный
                Сз = MM_CalcOthInt(deal.BofficeKind, deal.DealID, ExecDate, 0);
            elif (mmcomiss.rec.Type == 2) // операционный
                Сз = MM_CalcOthOpInt(deal.BofficeKind, deal.DealID, ExecDate, 0);
            end;

            ConvSumCross(Сз, Сз, ExecDate, sfcomiss.rec.FIID_Comm, deal_pd.GetLeg().PFI);
            if ((Сз == -1) or (Сз == 0))
                if (not isServOpMode())
                   return 0;
                end;
                SvOpSayError(deal.DealCode, MM_OTHINREF_ERR_CALCCOST, "Нет комиссий (прочих доходов) для отражения в ОФР");
                return 1;
            end;
            Сндс = Сз/(100+ндс)*ндс;  
            Сз   = Сз - Сндс;
            if (mmcomiss.rec.Type == 1) // процентный
                if ((PlanValueDate.size > 0) and (PlanValueDate[0] > ExecDate))
                    stat = CreateTrn(Сз, 3, deal_pd, RsbData.ID);
                else
                    stat = CreateTrn(Сз, 1, deal_pd, RsbData.ID);
                end;
                НзПц = НзПц - Сз - Сндс;
            elif (mmcomiss.rec.Type == 2) // операционный
                stat = CreateTrn(Сз, 3, deal_pd, RsbData.ID);
                НзОп = НзОп - Сз - Сндс;
            end;
            if (stat)
                stat = CreateTrn(Сндс, 5, deal_pd, RsbData.ID);
            end;
            if (not stat)
                return 1;
            end;
        end;
		end;
    end;

    MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 12, ExecDate, money(НзПц));
    MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 13, ExecDate, money(НзОп));

    return 0;
END;

NameMacroFile = "mmothinref_j.mac";
