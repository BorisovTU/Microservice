
import RSForms, RsbObjFactory, RsbDataSet, DealsInter, dlreport;

/*{{DESIGNER_HEADERS()*/
/*DESIGNER_HEADERS()}}*/


/* Параметры отчета */
PRIVATE VAR
rp_DDirection      = "",            /* Направление сделки */
rp_FI              = 0,             /* Валюта */
rp_ExcludeFI       = FALSE,         /* Признак исключения заданной валюты */
rp_MDirection      = "",            /* Направление платежа */
rp_PartyCtgID      = 0,             /* Категория контрагентов (ID) */
rp_PartyCtgAttrID  = 0,             /* ID выбранного элемента в категории контрагента */
rp_PortfKindID     = 0,             /* Тип портфеля (ID) */
rp_PortfKindAttrID = 0,             /* ID выбранного элемента в типах портфелей */
rp_VD              = date( 0,0,0 ); /* Дата валютирования */


PRIVATE MACRO getObjAttrName( ObjectType, GroupID, AttrID )
   var objattr;
   
   if((ObjectType == 0) or (GroupID == 0) or (AttrID == 0))
      return "Все";
   end;
   
   objattr = TRsbDataSet("select t_Name from dobjattr_dbt " +
                            "where t_ObjectType = " + ObjectType +
                             " and t_AttrID = " + GroupID +
                             " and t_GroupID = " + AttrID);
   if( objattr.MoveNext )
      return objattr.Name;
   end;

   return "Все";
END;


PRIVATE MACRO to_date( _date:date )            
   if( _date != Date(0,0,0) )
      return "TO_DATE( '" + string(_date) + "', 'DD.MM.YYYY' )"; 
   else
      return "TO_DATE( '" + "1.1.1" + "', 'DD.MM.YYYY' )"; 
   end;
end;


/*{{DESIGNER_CLASS_RSL_THEADER()*/
class (TForm)THeader(owner:object, name:string)
/*DESIGNER_CLASS_RSL()}}*/

/*{{DESIGNER_CONSTRBASE_RSL_THEADER()*/
   InitTForm(owner,name);
/*DESIGNER_CONSTRBASE_RSL()}}*/
/*{{DESIGNER_VAL_RSL_THEADER(PaymDate,FIID,Category,Briefcase)*/
   var PaymDate = TControl(this, "PaymDate");
   var FIID = TControl(this, "FIID");
   var Category = TControl(this, "Category");
   var Briefcase = TControl(this, "Briefcase");
/*DESIGNER_VAL_RSL()}}*/

/*{{DESIGNER_CMD_SET_RSL_THEADER()*/
/*DESIGNER_CMD_SET_RSL()}}*/

/*{{DESIGNER_HANDLERS_ADD_RSL_THEADER()*/
/*DESIGNER_HANDLERS_ADD_RSL()}}*/

/*{{DESIGNER_HANDLERS_RSL_THEADER()*/
/*DESIGNER_HANDLERS_RSL()}}*/

end;


/*{{DESIGNER_CLASS_RSL_TMMRPRORL()*/
class (TReport)TMMRPRORL(group:TReportsGroup)
/*DESIGNER_CLASS_RSL()}}*/

/*{{DESIGNER_CONSTRBASE_RSL_TMMRPRORL()*/
   InitTReport();
/*DESIGNER_CONSTRBASE_RSL()}}*/
   setTemplate("deals2.lbr", "MMRPRORL");

/*{{DESIGNER_VAL_RSL_TMMRPRORL(Header,ReportTable)*/
   var Header = THeader(this, "Header");
   var ReportTable = TControl(this, "ReportTable");
/*DESIGNER_VAL_RSL()}}*/

/*{{DESIGNER_CMD_SET_RSL_TMMRPRORL()*/
/*DESIGNER_CMD_SET_RSL()}}*/

/*{{DESIGNER_HANDLERS_ADD_RSL_TMMRPRORL(OnGetContents,OnLoad)*/
   addHandler(4, R2M(this, "OnGetContents"));
   addHandler(-2147385343, R2M(this, "OnLoad"));
/*DESIGNER_HANDLERS_ADD_RSL()}}*/

   Create(group);

   var m_RmmDeal = RsbObjectFactory.GetObject("RmmPaym");

   macro FillTable()
      var l_row, l_tbl = TReportTable(),
          filter_str = "",
          buying  = " and SubStr(o.t_systypes, 2, 1) = 'B' ", // привлечение
          selling = " and SubStr(o.t_systypes, 2, 1) = 'S' "; // размещение

      PutElement(ReportTable, l_tbl);
      // Фильтр по ФИ
      if(rp_FI != -1)
         filter_str = "FIID = " + rp_FI;
      end;
      // Фильтр по дате сделки
      if(rp_VD > date(0,0,0))
         if(filter_str != "") filter_str =  filter_str + " and "; end;
         filter_str =  filter_str + "ValueDate = '" + rp_VD + "'";
      end;
      
      // Фильтр по направлению сделки
      if(rp_DDirection == 1) // привлечение 
         if(filter_str != "") filter_str =  filter_str + " and "; end;
         filter_str =  filter_str + "Direction = 'B'";
      elif(rp_DDirection == 2) // размещение 
         if(filter_str != "") filter_str =  filter_str + " and "; end;
         filter_str =  filter_str + "Direction = 'S'";
      end;

      // Фильтр по направлению платежа
      if(rp_MDirection == 1) // требования 
         if(filter_str != "") filter_str =  filter_str + " and "; end;
         filter_str =  filter_str + "IsCom = 'NULL'";
      elif(rp_MDirection == 2) // обязательства 
         if(filter_str != "") filter_str =  filter_str + " and "; end;
         filter_str =  filter_str + "IsCom != 'NULL'";
      end;

      // портфель
      if((rp_PortfKindID != 0) and (rp_PortfKindAttrID != 0))
         filter_str = filter_str + " AND BCaseID = " + rp_PortfKindAttrID;
      end;
      // категория к.агента
      if((rp_PartyCtgID != 0) and (rp_PartyCtgAttrID != 0))
         filter_str = filter_str + " AND ContragentCatID = " + rp_PartyCtgAttrID;
      end;

      if(filter_str != "")
         m_RmmDeal.SetFilter( filter_str );
      end;
      m_RmmDeal.AddSortFld("DealCode");
      m_RmmDeal.AddSortFld("Purpose");
      m_RmmDeal.ApplyFilter();
      l_row = l_tbl.AddLine( "RowGroup" );
      l_row.Line("Row").BindItem.DataSource = m_RmmDeal;
      expandrowset(l_row.Line("Row"));

   end;

   macro PutAllElements()
/*{{DESIGNER_PUT_ELEMENT_RSL_TMMRPRORL(Header,ReportTable)*/
      PutElement(Header);
      FillTable();
/*DESIGNER_PUT_ELEMENT_RSL()}}*/
   end;

/*{{DESIGNER_HANDLERS_RSL_TMMRPRORL(OnGetContents,OnLoad)*/
   macro OnGetContents (Sender: Object)
      if(rp_VD > date(0,0,0))
         Header.PaymDate.Text  = rp_VD;
      end;
      Header.Briefcase.Text = getObjAttrName( OBJTYPE_MMARKDEAL, rp_PortfKindID, rp_PortfKindAttrID );
      Header.FIID.Text      = DL_getCCode( rp_FI );
      Header.Category.Text  = getObjAttrName( OBJTYPE_PARTY, rp_PartyCtgID, rp_PartyCtgAttrID );

      PutAllElements();
   end;

   macro OnLoad (Sender: Object, pbCancel: @Bool)
      ReportTable.DoInit();
   end;

/*DESIGNER_HANDLERS_RSL()}}*/

end;


PRIVATE MACRO AddPortfel(sqlfrom:@string, sqlwhere:@string)
   if((rp_PortfKindID == 0) or (rp_PortfKindAttrID == 0))
      return;
   end;

   sqlfrom = sqlfrom + ", ddl_tick_dbt tick1, dobjatcor_dbt acor1 ";
   sqlwhere = sqlwhere + " and acor1.t_Object = tick1.t_DealID " + 
                         " and acor1.t_AttrID = " + rp_PortfKindAttrID +
                         " and acor1.t_GroupID = " + rp_PortfKindID +
                         " and acor1.t_ObjectType = " + OBJTYPE_MMARKDEAL +
                         " and acor1.t_ValidFromDate <= tick1.t_DealDate" +
                         " and acor1.t_ValidToDate   >= tick1.t_DealDate";
END;

PRIVATE MACRO AddContrCateg(sqlfrom:@string, sqlwhere:@string)
   if((rp_PartyCtgID == 0) or (rp_PartyCtgAttrID == 0))
      return;
   end;

   sqlfrom = sqlfrom + ", ddl_tick_dbt tick2, dobjatcor_dbt acor2, dparty_dbt party2 ";
   sqlwhere = sqlwhere + " party2.t_PartyID = tick2.t_PartyID " +
                     " and acor2.t_Object = party2.t_PartyID " +
                     " and acor2.t_ObjectType = " + OBJTYPE_PARTY +
                     " and acor2.t_AttrID = " + rp_PartyCtgAttrID +
                     " and acor2.t_GroupID = " + rp_PartyCtgID +
                     " and acor2.t_ValidFromDate <= tick2.t_DealDate" +
                     " and acor2.t_ValidToDate   >= tick2.t_DealDate";
END;

MACRO PrintBody(grp, p_valuedate:date, p_fiid)
var MMRPRORL,
    DataSet, ok = false,
    sqltext  = "select distinct t_ValueDate ",
    sqlfrom  = " from dpmpaym_dbt paym ",
    sqlwhere = " where paym.t_DocKind = 102 ";

    // дата сделки
    if(p_valuedate > date(0,0,0))
       sqlwhere = sqlwhere + " AND paym.t_ValueDate = " + to_date(p_valuedate);
    end;
    // ФИ
    if(p_fiid > -1)
       sqlwhere = sqlwhere + " AND paym.t_fiid = " + p_fiid;
    end;
    // портфель
    AddPortfel(@sqlfrom, @sqlwhere);
    // категория контрагента
    AddContrCateg(@sqlfrom, @sqlwhere);

    DataSet = TRsbDataSet(sqltext + sqlfrom + sqlwhere);
    if(DataSet.MoveNext)
       MMRPRORL = TMMRPRORL(grp);
       ok = true;
    end;
    
    return ok;
END;


/* Печать отчета */
MACRO printReportRSF(p_VD,  /* параметры для печати (всегда определены) */
                   p_DDirection,  p_fiid, p_ExcludeFI, p_MDirection, /* параметры для расчета, м.б. неопред. */
                   p_PartyCtgID,  p_PartyCtgAttrID,
                   p_PortfKindID, p_PortfKindAttrID )
var grp = TReportsGroup();

   /* Считать переданные параметры в глобальные переменные.
      Если какие-либо параметры не были заданы, они должны быть == 0 */
   rp_VD              = p_VD;
   rp_DDirection      = p_DDirection;
   rp_FI              = p_fiid;
   rp_ExcludeFI       = p_ExcludeFI;
   rp_MDirection      = p_MDirection;
   rp_PartyCtgID      = p_PartyCtgID;
   rp_PartyCtgAttrID  = p_PartyCtgAttrID;
   rp_PortfKindID     = p_PortfKindID;
   rp_PortfKindAttrID = p_PortfKindAttrID;

   if(PrintBody(grp, p_VD, p_fiid) == true)
      grp.Print( TRUE );
   else
      msgbox( "Нет данных, удовлетворяющих заданной фильтрации." );
   end;

END;
