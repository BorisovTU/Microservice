/*
$Name:        mmog_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Исполнение исходящих платежей"
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : mmog_j.mac
  Programmer  : Куперин М.В.
  Comment     : 
└───────────────────────────────────────────────────────────────────────────*/
IMPORT mmlibr, mmcarry, mmlb_j, mpclb, mmadd_j, mmpaymentcls, mmclfnc, mmpenalt, mmservop, MmarkInter;

/* Инициализируются бэк-офисом */
PRIVATE VAR PlanIDs = TArray,       /*Массив идентификаторов планируемых платежей*/
             KvitAmounts = TArray,   /*Массив сумм к исполнению*/
             ExecDate;               /*Дата исполнения*/

PRIVATE VAR PlanPayms = TArray,     /*Массив обрабатываемых платежей*/
             deal_pd = NULL;

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0;

PRIVATE VAR NewPaySum = -1,
            OldPaySum = -1;

PRIVATE MACRO ПолучитьСуммуУплаченныхСверхНачисленных(PcDate)
    var 
        СуммаНачисленых = TRsbDataSet("select NVL(Sum(t_SumCalc), 0) as SumAdd from DPRCCNTSCHED_DBT where " +
                                      "T_CONTRACTID = " + MM_GetPercContractID(PC_IBC_CON, deal_pd.GetTick().DealCode) + " and T_SCHEDULEKIND = 2 and T_SPECIALTYPE = 0 " +
                                      "and T_DATE <= TO_DATE('" + PcDate + "') "),
        СуммаУплаченных = TRsbDataSet("select NVL(sum(pml.t_Amount), 0) as SumPay " +
                                      "from dpmlink_dbt pml, dpmpaym_dbt pm " +
                                      "where (pml.t_initialpayment = pm.t_PaymentID) " +
                                      "and(pml.t_InitialPayment = pml.t_PurposePayment) " +
                                      "and(pm.t_DocKind = " + DL_IBCDOC + ") and(pm.t_Purpose = " + PM_PURP_PERCENT + ") " +
                                      "and(pm.t_DocumentID = " + deal_pd.GetTick().DealID + ")and(pm.t_ValueDate <= TO_DATE('" + PcDate + "'))"),
        СуммаСверхНачисленных = 0;



    СуммаНачисленых.MoveNext;
    СуммаУплаченных.MoveNext;

    СуммаСверхНачисленных = СуммаУплаченных.SumPay - СуммаНачисленых.SumAdd;

    if (СуммаСверхНачисленных < 0)
        СуммаСверхНачисленных = 0;
    end;
	  
    return СуммаСверхНачисленных;
END;

PRIVATE MACRO InsertDocsPercent(idx, pm)

    var
        СчетПроцентов, СуммаНачисленныхПроцентов,
        УплачНач, УплачСверхНач, СуммаУплаченных,
        at, PcAdd = NULL, flag, stat, claim_cat,
        party, НалогСтавка, НалогНеНач;

    СчетПроцентов = MM_GetPercContractID(PC_IBC_CON, deal_pd.GetTick().DealCode);

    if (isSale(GetOperationGroup(deal_pd.GetTick().DealType)))
        claim_cat = tdr_perc3P;
    else
        claim_cat = tdr_claimP;
    end;
    СуммаНачисленныхПроцентов = deal_pd.GetRestAccount(claim_cat, ExecDate, deal_pd.GetLeg().PFI, 1, NULL);
    СуммаУплаченных = KvitAmounts[idx];

    if (СуммаУплаченных > СуммаНачисленныхПроцентов)
        mm_error("Сумма платежа превышает сумму начисленных процентов: " + СуммаНачисленныхПроцентов);
        return false;
    end;
    /*
    if (УплачНач == 0)
        mm_error ("Проценты не начислены");
        return false;
    end;*/

    //сделка размещения, отрицательная ставка
    if (deal_pd.GetTick().NegativeRate == "X")

        at = MM_Carry;
        at.date_carry       = ExecDate;
        at.PrimaryDoc       = deal_pd;
        at.FIIDPayer        = deal_pd.GetLeg().PFI;
        at.FIIDReceiver     = deal_pd.GetLeg().PFI;
        at.CatPayer         = tdr_perc3P;
        at.AccountReceiver  = pm.FutureReceiverAccount;
        at.Sum              = СуммаУплаченных;
        at.Chapter          = 1;
        at.Ground           = "Погашение процентных обязательств без нарушения срока";
        at.PaymentObj       = pm;
        
        if (not at.carry())
            return false; 
        end;

    // сделка привлечения
    else
        at = MM_Carry;
        at.date_carry       = ExecDate;
        at.PrimaryDoc       = deal_pd;
        at.FIID             = deal_pd.GetLeg().PFI;
        at.CatPayer         = tdr_claimP;
        at.AccountReceiver  = pm.FutureReceiverAccount;
        at.Sum              = СуммаУплаченных;
        at.Chapter          = 1;
        at.Ground           = "Погашение процентных обязательств без нарушения срока";
        at.PaymentObj       = pm;

        if (not at.carry())
            return false; 
        end;
    end;

//    if (PrcSetCntSchedFactSumByID(GetCntSchedID(СчетПроцентов, 3, PlanPayms[idx].ValueDate), money(СуммаУплаченных), ExecDate, date(0,0,0)) != 0)
//        mm_error("Ошибка при оплате процентов");
//        return false;
//    end;   

    return true;
END;

PRIVATE MACRO InsertCLDocsPrinc(idx, pm)
    VAR Sum = KvitAmounts(idx);
    if (deal_pd.GetTick().ParentID <= 0)
        return true;
    end;
    var cl_pd = MMFirstDoc(DL_IBCDOC, deal_pd.GetTick().ParentID, true);
    if (cl_pd.GetTick().BOfficeKind == DL_IBCDOC)
        return true;
    end;
    // проводка не формируются, если был выполнен шаг списания лимита
    if (LimitWasWrittenOff(cl_pd.GetTick().DealID))
        return true;
    end;

    VAR carry = MM_Carry, Account = "";
    carry.FIIDPayer    = cl_pd.GetTick().LimitCur;
    carry.FIIDReceiver = cl_pd.GetTick().LimitCur;
    carry.Chapter      = 3;
    carry.date_carry   = {curdate};
    carry.PrimaryDoc   = cl_pd;  
    carry.PaymentObj   = pm;

    if (isSale(GetOperationGroup(deal_pd.GetTick().DealType)))
    // размещение
        var LimitSum;
        ПолучитьОстатокСчета(cl_pd, tdr_oblP, "", 3, LimitSum, {curdate}, cl_pd.GetTick().LimitCur);
        if (cl_pd.GetTick().LimitCur != deal_pd.GetLeg().PFI)
            ConvSumCross(Sum, Sum, {curdate}, deal_pd.GetLeg().PFI, cl_pd.GetTick().LimitCur);
        end;
        if (LimitSum < Sum)
            mm_error("Сумма платежа превышает сумму лимита по договору кредитной линии");
            return false;
        end;

        Account = MC_FindAndOpenCommonAccByFD(tdr_corespacc_vb, deal_pd, {curdate}, 0, MC_OPENACC_CREATE, NULL,cl_pd.GetTick().LimitCur, 
                                             NULL, NULL, NULL, FIROLE_CORACC_PASSIVE, NULL, NULL, NULL, NULL, NULL, false, false);
        carry.SumReceiver      = Sum;
        carry.CatPayer         = tdr_oblP;
        carry.AccountReceiver  = Account;
        //carry.CatReceiver      = tdr_corespacc_vb;
        //carry.FiRoleReceiver   = FIROLE_CORACC_PASSIVE;
        carry.Ground           = "Уменьшение лимита по договору КЛ при выдаче, размещение";
    else
    // привлечение
        var Srestor;
        if ((cl_pd.GetTick().IssuanceLimit >= 0.0) and (cl_pd.GetTick().DebtLimit == 0.0))
            return true;
        elif ((cl_pd.GetTick().IssuanceLimit == 0.0) and (cl_pd.GetTick().DebtLimit > 0.0))
            if (cl_pd.GetTick().LimitCur != deal_pd.GetLeg().PFI)
                ConvSumCross(Srestor, Sum, {curdate}, deal_pd.GetLeg().PFI, cl_pd.GetTick().LimitCur);
            end;   
        elif ((cl_pd.GetTick().IssuanceLimit > 0.0) and (cl_pd.GetTick().DebtLimit > 0.0))
            var UnusedIssuanceLimit = GetCLUnusedIssuanceLimit(cl_pd.GetTick().DealID, cl_pd.GetTick().IssuanceLimit);
            var UnusedDebtLimit     = GetCLUnusedDebtLimit(cl_pd.GetTick().DealID, cl_pd.GetTick().DebtLimit);
            var Sl, Slnew;
            if (UnusedIssuanceLimit < UnusedDebtLimit)
                Sl = UnusedIssuanceLimit;
            else
                Sl = UnusedDebtLimit;
            end;
            ConvSum(Sum, Sum, {curdate}, deal_pd.GetLeg().PFI, cl_pd.GetTick().LimitCur);
            var UnusedDebtLimitAfterOP = UnusedDebtLimit + Sum;
            if (UnusedIssuanceLimit < UnusedDebtLimitAfterOP)
                Slnew = UnusedIssuanceLimit;
            else
                Slnew = UnusedDebtLimitAfterOP;
            end;
            Srestor = Slnew - Sl;
            
        end;

        Account = MC_FindAndOpenCommonAccByFD(tdr_corespacc_vb, deal_pd, {curdate}, 0, MC_OPENACC_CREATE, NULL,cl_pd.GetTick().LimitCur, 
                                             NULL, NULL, NULL, FIROLE_CORACC_PASSIVE, NULL, NULL, NULL, NULL, NULL, false, false);

        carry.SumReceiver      = Srestor;
        carry.CatPayer         = tdr_oblR;
        carry.AccountReceiver  = Account;
        //carry.FiRoleReceiver   = FIROLE_CORACC_ACTIVE;
        //carry.CatReceiver      = tdr_corespacc_vb;
        carry.Ground           = "Восстановление лимита по договору КЛ при погашении, привлечение";
    end;
    //carry.SumReceiver = Sum;

    return carry.carry();
END;

PRIVATE MACRO GetAdjRest()
    VAR Остаток = 0, Счет = "";
    
    ПолучитьОстатокСчетаУчетЗнака(deal_pd, tdr_adjatP, Счет, 1, Остаток, ExecDate, NATCUR);
    if (Остаток == 0)
        Счет = "";
        ПолучитьОстатокСчетаУчетЗнака(deal_pd, tdr_adjatR, Счет, 1, Остаток, ExecDate, NATCUR);
    end;
    return Остаток;
END;

PRIVATE MACRO InsertAdjDocsPrinc(idx, pm)
    VAR stat = true, КОРост, Sk, Сод = 0, Счет = "", at, NatcurSum = KvitAmounts[idx];
    if ((pm.Purpose != PM_PURP_PRINC_RET) or (IsGraphPaym(deal_pd.GetTick().DealID, DL_IBCDOC, PM_PURP_PRINC_RET)))
        return stat;
    end;

    КОРост = GetAdjRest();
    stat = ПолучитьОстатокСчета(deal_pd, tdr_rest(deal_pd.GetTick().FlagTypeDeal), Счет, 1, Сод, ExecDate, deal_pd.GetLeg().PFI);
    ConvSum(Сод,       Сод,       ExecDate, deal_pd.GetLeg().PFI, NATCUR);
    ConvSum(NatcurSum, NatcurSum, ExecDate, deal_pd.GetLeg().PFI, NATCUR);
    if (stat and (Сод > 0))
        at = MM_Carry;
        at.FIIDPayer        = 
        at.FIIDReceiver     = NATCUR;
        at.date_carry       = ExecDate;
        at.PrimaryDoc       = deal_pd;
        at.PaymentObj       = pm;
        at.Chapter          = 1;
        Sk = (abs(КОРост)*NatcurSum)/Сод;
        Sk = round(Sk, 2);
        at.Sum              = Sk;
        if (КОРост > 0)
            at.CatPayer    = tdr_adjatR;
            at.CatReceiver = tdr_atodR(deal_pd.GetTick().TypeDoc);
            at.Ground      = "Списание корректировки, уменьшающей стоимость сделки привлечения МБК";
        else
            at.CatPayer    = tdr_atodP(deal_pd.GetTick().TypeDoc);
            at.CatReceiver = tdr_adjatP;
            at.Ground      = "Списание корректировки, увеличивающей стои-мость сделки привлечения МБК";
        end;
        stat = at.carry();
    end;
    return stat;
END;

PRIVATE MACRO InsertDocsPenalty (idx, pm)
  VAR НачШтраф, ДатаВвода, tdr_penny_cat, stat, at, Account = "", Sum;
  GetRegistryValue("COMMON\\МСФО\\ДАТА_ВВОДА",
    V_STRING, ДатаВвода, stat);
  if (stat)
    mm_error("Ошибка при работе с реестром");
    return false;
  end;
  ДатаВвода = date (ДатаВвода);
  if (pm.Purpose == PM_PURP_PENALTY_PRINC)
    tdr_penny_cat = tdr_penny_ODP;
  else
    tdr_penny_cat = tdr_penny_percP;
  end;

  if ((ДатаВвода <= ExecDate) or ((ДатаВвода > ExecDate) 
        and СчетСделки(tdr_penny_cat, deal_pd.GetTick().DealID, Account, ExecDate, NULL, true)))

    ПолучитьОстатокСчета(deal_pd, tdr_penny_cat, NULL, 1, НачШтраф, ExecDate, deal_pd.GetLeg().PFI);
    if (KvitAmounts[idx] > НачШтраф)
      // начисление неустоек
      InsertDocs(deal_pd, pm.Purpose, (KvitAmounts[idx]-НачШтраф));
    end;

    pm.PaymStatus = PM_READY_TO_SEND;
    pm.isFactPaym = "X";
  else 
    at = MM_Carry;
    at.date_carry       = ExecDate;
    at.PrimaryDoc       = deal_pd;
    at.FIIDPayer        = NATCUR;
    at.FIIDReceiver     = deal_pd.GetLeg().PFI;
    at.CatPayer         = tdr_pennyP;
    at.AccountReceiver  = pm.FutureReceiverAccount;
    at.Sum              = KvitAmounts[idx];
    at.Chapter          = 1;
    at.Ground           = "Погашение штрафов за просроченный основной долг";
    at.PaymentObj       = pm;

    if (not at.carry())
        return false; 
    end;
    pm.PaymStatus = PM_FINISHED;
  end;

  return true;
END;

PRIVATE MACRO Send(idx)
    var
        paym    = PlanPayms[idx],
        newPaym = NULL, MM_PaymObj = NULL,
        pml = TRecHandler("pmlink.dbt"),
        pm = NULL, at = NULL;

    if ((paym.FuturePayerAmount == paym.FuturePayerAmount)and
    		 (money(round(paym.FuturePayerAmount,2)) == money(round(KvitAmounts[idx],2)) )
        )  
        pm = paym;
    else
        MM_PaymObj = MM_RsbPayment(MMPAYM_CREATEBYPAYMOBJ, paym, paym.SubPurpose, KvitAmounts[idx], ExecDate);
        newPaym = MM_PaymObj.Payment;

        pml.rec.Amount = KvitAmounts[idx];
        pml.rec.FIID   = newPaym.BaseFIID;
        pml.rec.IPSign = "-";
        pml.rec.PPSign = "";
        Paym.LinkPayment(newPaym, PMLINK_KIND_KVITING, pml);

        newPaym.Actuate();
        pm = newPaym;
    end;

    pm.ValueDate = ExecDate;
    if ((pm.Purpose == PM_PURP_PRINC) and (IsCLAIMSACQ(GetOperationGroup(deal_pd.GetTick().DealType))))
        var Account = MC_FindAndOpenCommonAccByFD(tdr_corespacc_vb, deal_pd, ExecDate, 0, MC_OPENACC_CREATE, NULL,deal_pd.GetLeg().PFI, 
                                             NULL, NULL, NULL, FIROLE_CORACC_ACTIVE, NULL, NULL, NULL, NULL, NULL, false, false);
        at = MM_Carry;
        at.date_carry       = ExecDate;
        at.PrimaryDoc       = deal_pd;
        at.FIIDPayer        = 
        at.FIIDReceiver     = deal_pd.GetLeg().PFI;
        at.CatPayer         = tdr_nomcost;
        at.AccountReceiver  = Account;
        at.SumReceiver      = deal_pd.GetLeg().Principal;
        at.Chapter          = 3;
        at.Ground           = "Отражение номинальной стоимости приобретенных прав требования";
        at.PaymentObj       = pm;

        if (not at.carry())
            return false; 
        end;
        
        at = MM_Carry;
        at.date_carry       = ExecDate;
        at.PrimaryDoc       = deal_pd;
        at.FIIDPayer        = 
        at.FIIDReceiver     = deal_pd.GetLeg().PFI;
        at.CatPayer         = tdr_calcs_cmR;
        at.AccountReceiver  = pm.FutureReceiverAccount;
        at.SumReceiver      = NewPaySum;
        at.Chapter          = 1;
        at.Ground           = "Оплата приобретенных прав требования";
        at.PaymentObj       = pm;

        if (not at.carry())
            return false; 
        end;

        if (NewPaySum > OldPaySum)
            at = MM_Carry;
            at.date_carry       = ExecDate;
            at.PrimaryDoc       = deal_pd;
            at.FIIDPayer        = 
            at.FIIDReceiver     = deal_pd.GetLeg().PFI;
            at.CatPayer         = tdr_prize_cm;
            at.CatReceiver      = tdr_calcs_cmR;
            at.SumReceiver      = NewPaySum - OldPaySum;
            at.Chapter          = 1;
            at.Ground           = "Учет премии по приобретенным правам требования";
            at.PaymentObj       = pm;

            if (not at.carry())
                return false; 
            end;

            at = MM_Carry;
            at.date_carry       = ExecDate;
            at.PrimaryDoc       = deal_pd;
            at.FIIDPayer        = 
            at.FIIDReceiver     = deal_pd.GetLeg().PFI;
            at.CatPayer         = tdr_rights;
            at.CatReceiver      = tdr_calcs_cmR;
            at.SumReceiver      = deal_pd.GetLeg().Principal;
            at.Chapter          = 1;
            at.Ground           = "Учет приобретенных прав требования по сделке с премией";
            at.PaymentObj       = pm;

            if (not at.carry())
                return false; 
            end;
        elif (NewPaySum < OldPaySum)
            at = MM_Carry;
            at.date_carry       = ExecDate;
            at.PrimaryDoc       = deal_pd;
            at.FIIDPayer        = 
            at.FIIDReceiver     = deal_pd.GetLeg().PFI;
            at.CatPayer         = tdr_rights;
            at.CatReceiver      = tdr_calcs_cmR;
            at.SumReceiver      = NewPaySum;
            at.Chapter          = 1;
            at.Ground           = "Учет приобретенных прав требования по сделке с дисконтом";
            at.PaymentObj       = pm;

            if (not at.carry())
                return false; 
            end;

        end;
        pm.PaymStatus = PM_FINISHED;
    elif (pm.Purpose == PM_PURP_PERCENT)
      if (not InsertDocsPercent (idx, pm))
            return false;
        end;

        pm.PaymStatus = PM_FINISHED;
//        pm.isFactPaym = "X";
    elif(pm.ReceiverGroup == PAYMENTS_GROUP_INTERNAL)
        at = MM_Carry;
        at.date_carry       = ExecDate;
        at.PrimaryDoc       = deal_pd;
        at.FIID             = deal_pd.GetLeg().PFI;
        at.AccountPayer     = pm.FuturePayerAccount;
        at.AccountReceiver  = pm.FutureReceiverAccount;
        at.Sum              = KvitAmounts[idx];
        at.Chapter          = 1;
        at.Ground           = "Погашение процентных обязательств без нарушения срока";
        at.PaymentObj       = pm;

        if (not at.carry())
            return false; 
        end;

        pm.PaymStatus = PM_FINISHED;
        pm.isFactPaym = "X";
    elif (pm.ReceiverGroup == PAYMENTS_GROUP_EXTERNAL)
        if ((pm.Purpose == PM_PURP_PENALTY_PRINC) or (pm.Purpose == PM_PURP_PENALTY_PERC))
            return InsertDocsPenalty (idx, pm);
        else
            pm.PaymStatus = PM_READY_TO_SEND;
            pm.isFactPaym = "X";
        end;

        // исполнение/погашение ОД
        if ((pm.Purpose == PM_PURP_PRINC) or (pm.Purpose == PM_PURP_PRINC_RET))
            if (not InsertAdjDocsPrinc(idx, pm))
                return false;
            end;
            if (not InsertCLDocsPrinc(idx, pm))
                return false;
            end;
        // просроченный основной долг
        elif (pm.Purpose == PM_PURP_OVD_PRINC)
            if (not InsertCLDocsPrinc(idx, pm))
                return false;
            end;
        end;
        
    else
        return false;
    end;		

    return true;
END;

PRIVATE MACRO PreparePayment(deal_pd)
    VAR OprServDoc, query, RsbData, i = 0;
    OprServDoc = GetOprServDoc();
    ExecDate = OprServDoc.rec.CommDate;
    if (OprServDoc.rec.DocKind != 212)
        return;
    end;
    // действия для шага "Оплата затрат"
    
    query = "select t_paymentid, t_amount, ddlcomis_dbt.t_id from dpmpaym_dbt" + 
            " inner join ddlcomis_dbt on ddlcomis_dbt.t_id = dpmpaym_dbt.t_subpurpose" +
            " where dpmpaym_dbt.t_dockind = " + deal_pd.GetTick().BofficeKind + 
            " and dpmpaym_dbt.t_documentid = " + deal_pd.GetTick().DealID + " and t_purpose = 83 and t_paymstatus = 1000";

    if (OprServDoc.rec.ComNumber > 0)
        query = query + " and t_comnumber = " + OprServDoc.rec.ComNumber;
    end; 
    if (OprServDoc.rec.ContractID > 0)
        query = query + " and ddlcomis_dbt.t_contract = " + OprServDoc.rec.ContractID;
    end;  

    RsbData = TRsbDataSet(query);
    while (RsbData.MoveNext)
        MM_ChangeComEndDate(RsbData.ID, OprServDoc.rec.CommDate);
        PlanIDs[i] = RsbData.PaymentID;
        KvitAmounts[i] = RsbData.Amount;
        i = i + 1;
    end;

END;

// Выполнение шага
PRIVATE MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    var
        idx = 0;
    record deal( dl_tick );

    SetBuff( deal, Document );

    ID_Operation = IDOperation;
    ID_Step      = IDStep;

    deal_pd = MMFirstDoc (DL_IBCDOC, deal.DealID, true);
    // шаг был запущен из сервисной операции
    if (isServOpMode())
        PreparePayment(deal_pd);
    end;

    if (PlanIDs.size == 0)
        mm_error ("Нет платежей");
        return 1;
    end;

    if (CheckExistence(LEG_LegKind_DealID_LegID, LEG_KIND_DL_TICK, deal.DealID, 1))
        mm_error( "Не найден транш сделки ", deal.DealCode );
        return 1;
    end;


    while (idx < PlanIDs.size)
        PlanPayms[idx] = RsbPayment(PlanIDs[idx]);

        if (PlanPayms[idx].PaymentID == 0)
            mm_error ("Не найден платеж");
            return 1;
        end;

        if (PlanPayms[idx].ValueDate != ExecDate)
            if( not GetTrue( true, "Планируемая дата платежа "+
                                    "|не равна дате исполнения."+
                                    "|Исполнить платеж ?" )) 
                return 1;
            end;
        end;

        // МБК, приобретение прав требования
        IF (IsCLAIMSACQ(GetOperationGroup(deal.DealType)) and (PlanPayms[idx].Purpose == PM_PURP_PRINC))
            var FinInstr = TBFile("fininstr.dbt");
            FinInstr.rec.FIID = deal_pd.GetLeg().PFI;
            FinInstr.GetEQ();
            IF(not GetTrue( true, "Стоимость приобретения " + deal_pd.GetLeg().Principal + " " + FinInstr.rec.CCY + "?")) 
                if (not GetMoney(NewPaySum, string("Сумма платежа по оплате приобретенных прав требования, " + FinInstr.rec.CCY)))
                    RETURN 1;
                END;
            END;
        END;

        OldPaySum = PlanPayms[idx].PayerAmount;
        if (NewPaySum != -1)
            KvitAmounts[idx]              = 
            PlanPayms[idx].OrderAmount    = 
            PlanPayms[idx].PayerAmount    = 
            PlanPayms[idx].ReceiverAmount = 
            PlanPayms[idx].BaseAmount     = NewPaySum;
        end;
		
        idx = idx+1;
    end;

    idx = 0;
    while (idx < PlanPayms.size)
        if (not Send(idx))
            return 1;
        end;
        idx = idx + 1;
    end;

    return 0;
END;

NameMacroFile = "mmog_j.mac";
