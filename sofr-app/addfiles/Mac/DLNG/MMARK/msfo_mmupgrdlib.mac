/*
$Name:        msfo_mmupgrdlib.mac
$Module:      МБ Кредиты
$Description: Параметры апгрейдера МСФО
*/
import mmcnst_j, mmcateg, mmlb_j, sfcommon, SCPeriodComm;

/* Счет корреспонденции для формирования переходных проводок */
CONST ПЕРЕХОДНЫЙ_ФИН_РЕЗ = 1;
/*
Значения: 
1 ? 706, финансовый результат текущего года; 
2 ? 10801, нераспределенная прибыль; 
3 ? 10901, непокрытый убыток; 
4 ? 10801 или 10901 в зависимости от остатка; 
5 ? 10801 всегда по Кт, 10901 - все-гда по Дт.
*/

/* Определение варианта формирования резерва по сделкам, затронутым переносом и формированием новых остатков */
CONST ПЕРЕХОДНЫЕ_РЕЗЕРВЫ = 1;
/*
Значения: 
1 ? досоздание резерва;
2 ? полное восстановление и новое создание резервов.
*/

MACRO ПолучитьКатСчетаКорреспонденции(tdr_default, isDebt, deal_pd, OpDate)
    if (ПЕРЕХОДНЫЙ_ФИН_РЕЗ == 1)
        return tdr_default;
    elif (ПЕРЕХОДНЫЙ_ФИН_РЕЗ == 2)
        return tdr_unapbalance;
    elif (ПЕРЕХОДНЫЙ_ФИН_РЕЗ == 3)
        return tdr_uncoverloss;
    elif (ПЕРЕХОДНЫЙ_ФИН_РЕЗ == 4)
        var rest = $0;
        ПолучитьОстатокСчета(deal_pd, tdr_unapbalance, NULL, 1, rest, OpDate, deal_pd.GetLeg().PFI);
        if (rest != $0)
            return tdr_unapbalance;
        end;
        return tdr_uncoverloss;
    elif (ПЕРЕХОДНЫЙ_ФИН_РЕЗ == 5)
        if (isDebt)
        return tdr_uncoverloss;
        end;
        return tdr_unapbalance;
    end;
END;

MACRO InsertMCAccDoc(MCAccDoc, Account, DocKind, DocID, Cur, AccDate)
    VAR cmd = RsdCommand(RslDefCon, "SELECT 1 FROM dmcaccdoc_dbt WHERE T_ACCOUNT = ? AND T_DOCKIND = ? AND T_DOCID = ?");
    cmd.AddParam("p1", RSDBP_IN); cmd.Value("p1") = Account;
    cmd.AddParam("p2", RSDBP_IN); cmd.Value("p2") = DocKind;
    cmd.AddParam("p3", RSDBP_IN); cmd.Value("p3") = DocID;
    cmd.Execute();
    VAR rs = RsdRecordSet(cmd);
    // уже есть
    IF (rs.MoveNext())
        return true;
    end;

    VAR mccateg = TBFile("mccateg.dbt", "R", 1);
    mccateg.rec.LevelType = 1;

    MCAccDoc.Clear();
    MCAccDoc.rec.ID            = 0;
    MCAccDoc.rec.IsCommon      = "";
    MCAccDoc.rec.DocKind       = DocKind;
    MCAccDoc.rec.DocID         = DocID;
    MCAccDoc.rec.Currency      = Cur;
    MCAccDoc.rec.CatID         = 0;
    MCAccDoc.rec.CatNum        = 0;
    MCAccDoc.rec.Chapter       = 1;
    MCAccDoc.rec.Account       = Account;
    MCAccDoc.rec.TemplNum      = 0;
    MCAccDoc.rec.GroupNum      = 0;
    MCAccDoc.rec.PeriodID      = 0;
    MCAccDoc.rec.ActivateDate  = AccDate;
    MCAccDoc.rec.IsUsable      = "X";
    if (SubStr( Account, 1, 5 ) == "47423")
        MCAccDoc.rec.Kind_Account  = "А";
        mccateg.rec.Code = "+Расчеты";
    else
        MCAccDoc.rec.Kind_Account  = "П";
        mccateg.rec.Code = "-Расчеты";
    end;

    if (mccateg.GetEQ())
        MCAccDoc.rec.CatID  = mccateg.rec.ID;
        MCAccDoc.rec.CatNum = mccateg.rec.Number;        
    end;

    MCAccDoc.rec.FiRole        = FIROLE_UNDEF;
    MCAccDoc.rec.DepartmentID  = {OperDprt};
    MCAccDoc.rec.Branch        = 
    MCAccDoc.rec.MCBranch      = {OperDprtNode};
    MCAccDoc.rec.FIID          =
    MCAccDoc.rec.Owner         =
    MCAccDoc.rec.Place         =
    MCAccDoc.rec.Issuer        =
    MCAccDoc.rec.Centr         = 
    MCAccDoc.rec.CentrOffice   =
    MCAccDoc.rec.ClientContrID =
    MCAccDoc.rec.BankContrID   =
    MCAccDoc.rec.MarketPlaceID =
    MCAccDoc.rec.MarketPlaceOfficeID =
    MCAccDoc.rec.IndexDate     = 
    MCAccDoc.rec.Contractor    = 
    MCAccDoc.rec.CurrencyEQ    = -1;

    return MCAccDoc.Insert();
END;

MACRO InsertDLCOMIS(dlcomis, DocKind, DocID, ContrID, ComNum, Sum, NDS, PlanPayDate, FactPayDate)
    VAR cmd, rs, receiverID = 0;
    cmd = RsdCommand(RslDefCon, "SELECT 1 FROM ddlcomis_dbt WHERE T_DOCKIND = ? AND T_DOCID = ?");
    cmd.AddParam("p1", RSDBP_IN); cmd.Value("p1") = DocKind;
    cmd.AddParam("p2", RSDBP_IN); cmd.Value("p2") = DocID;
    cmd.Execute();
    rs = RsdRecordSet(cmd);
    // уже есть
    IF (rs.MoveNext())
        return true;
    end;

    VAR SfComis = TBFile("sfcomiss.dbt");
    SfComis.rec.FeeType = SF_FEE_TYPE_ONCE;
    SfComis.rec.Number  = ComNum;
    if (SfComis.GetEQ())
        receiverID = SfComis.rec.ReceiverID;
    end;

    dlcomis.Clear();
    dlcomis.rec.DocKind     = DocKind;
    dlcomis.rec.DocID       = DocID;
    dlcomis.rec.Contract    = ContrID;
    dlcomis.rec.FeeType     = SF_FEE_TYPE_ONCE;
    dlcomis.rec.ComNumber   = ComNum;
    dlcomis.rec.Sum         = Sum; // + NDS
    dlcomis.rec.NDS         = NDS;
    dlcomis.rec.Date        =
    dlcomis.rec.PlanPayDate = PlanPayDate;
    dlcomis.rec.FactPayDate = FactPayDate;
    dlcomis.rec.InputMedium = GetInputMedium(INPUTMEDIUM_USE);
    dlcomis.rec.ReceiverId  = receiverID;

    return dlcomis.Insert();
END;
