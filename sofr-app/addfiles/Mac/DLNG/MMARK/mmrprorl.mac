/*
$Name:        mmrprorl.mac
$Module:      МБ Кредиты
$Description: Печать отчета "Движение средств - сделки денежного рынка (МБК)"
*/

/* ───────────────────────────────────────────────────────────────────────────┐
  RS-Bank                                        R-Style Software Lab Ltd
  Файл подсистемы "МБК"

  Печать отчета "Движение средств - сделки денежного рынка (МБК)".

CREATED : 25.02.00 LA
MODIFICATIONS: 25.06.2007 Куперин Максим. Добавлен вывод отчета в Excel.
NOTES:

└─────────────────────────────────────────────────────────────────────────── */

import "or_tpl_h.mac", BankInter, PaymInter, CTInter, PTInter, OprInter, DealsInter, FIInter, globals, dlreport;

FILE dl_tick  ( dl_tick  );
FILE pmpaym   ( pmpaym   )  key 11; /* ValueDate */
FILE dl_leg   ( dl_leg   );
FILE fininstr ( fininstr );
FILE party    ( party    );

FILE wrk      ( mmrprstr ) normal; /* Временный файл для сохранения данных */

/* ------------------------------------------------------------------------- */
/* GLOBALS */

/* Параметры отчета */
VAR
rp_DDirection      = "",            /* Направление сделки */
rp_FI              = 0,             /* Валюта */
rp_ExcludeFI       = FALSE,         /* Признак исключения заданной валюты */
rp_MDirection      = "",            /* Направление платежа */
rp_PartyCtgID      = 0,             /* Категория контрагентов (ID) */
rp_PartyCtgAttrID  = 0,             /* ID выбранного элемента в категории контрагента */
rp_PortfKindID     = 0,             /* Тип портфеля (ID) */
rp_PortfKindAttrID = 0,             /* ID выбранного элемента в типах портфелей */
rp_VD              = date( 0,0,0 ); /* Дата валютирования */

/* Название временного файла, где будут сохранены данные */
var wrk_fileName = GetWorkFileName("mmrprorl");

/* Переменные для сохранения итоговых величин */
var
g_req = 0, /* Итого - требования */
g_com = 0; /* Итого - обязательства */

/* Номер предыдущей сделки */
var g_prevXID = "";

var Rep:CTemplateXLS = CTemplateXLS();
var flagXLS=false;
var lineadd=0; //флаг добавления новой строки в шаблон
var Line;



/* ------------------------------------------------------------------------- */
/* Macros */

/* Отконвертить Duration в дни */
MACRO conv2days( p_duration, p_pitch )
 if( p_pitch == 1 ) /* дни */
  return int( p_duration );
 else /* на этот случай пока заглушка */
  msgbox( "Ошибка!| Невозможно вычислить %-й период (срок) в днях:|Единица измерения срока - не дни.|(Полагаем срок равным 0)" );
  return 0;
 end;
END;

/* Получить сокращенное наименование контрагента - для печати */
MACRO getPartyShortName( p_id )
 party.PartyID = p_id;

 if( not getEQ( party ) )
  return "";
 end;
 return party.ShortName;
END;

/* Проверить выполнение всех условий фильтра, записать полученные
   направления сделки и платежа в рабочий файл */
MACRO data_fits

var l_dealGroup, l_res;

 /* Вид платежа */
 if( pmpaym.DocKind != 102 )
  return FALSE;
 end;

 dl_tick.DealID = pmpaym.DocumentID;
 if( not getEQ( dl_tick ) )
  return FALSE;
 end;

 dl_leg.LegKind = LEG_KIND_DL_TICK;
 dl_leg.DealID = pmpaym.DocumentID; /* просто проверим, что транш есть */
 dl_leg.LegID  = /*pmpaym.LegID*/1;
 if( not getEQ( dl_leg ) )
  return FALSE;
 end;

 l_dealGroup = getOperationGroup( dl_tick.DealType );

 if( isSale( l_dealGroup ) )
  wrk.DDirection = "X"; /* размещение */
 else
  wrk.DDirection = " "; /* привлечение */
 end;

 if( pmpaym.Payer=={OurBank} )
  wrk.MDirection = "X"; /* обязательство */
 else
  wrk.MDirection = " "; /* требование */
 end;

 /* Направление сделки */
 if( ( valType( rp_DDirection )  == V_BOOL ) and /* TRUE-размещение */
     ( ( wrk.DDirection == "X" ) != rp_DDirection ) )
  return FALSE;
 end;

 /* Направление платежа */
 if( ( valType( rp_MDirection )  == V_BOOL ) and /* TRUE-обязательство */
     ( ( wrk.MDirection == "X" ) != rp_MDirection ) )
  return FALSE;
 end;

 /* Валюта */
 if( ( valType( rp_FI )       == V_INTEGER ) and
     ( ( pmpaym.FIID == rp_FI ) == rp_ExcludeFI ) )
  return FALSE;
 end;

 /* Контрагент */
 if( ( valType( rp_PartyCtgID )     == V_INTEGER ) and
     ( valType( rp_PartyCtgAttrID ) == V_INTEGER ) )

  party.PartyID = dl_tick.PartyID;
  if( ( not getEQ( party ) ) or
      ( not CheckObjAttrPresence( l_res, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ), rp_PartyCtgID, rp_PartyCtgAttrID ) ) or
      ( not l_res ) )
   return FALSE;
  end;
 end;

 /* Портфель */
 if( ( valType( rp_PortfKindID )     == V_INTEGER ) and
     ( valType( rp_PortfKindAttrID ) == V_INTEGER ) and
     ( ( not CheckObjAttrPresence( l_res, OBJTYPE_MMARKDEAL, UniID( dl_tick, OBJTYPE_MMARKDEAL ), rp_PortfKindID, rp_PortfKindAttrID ) ) or
       ( not l_res ) ) )
  return FALSE;
 end;

 /* Все Ок */
 return TRUE;
END;

/* Собрать и записать данные */
MACRO prepareData

var l_next;
 var count = 0;

 pmpaym.ValueDate = rp_VD;
 pmpaym.DocKind = 102;
 l_next = getEQ( pmpaym );

 while( ( l_next ) and
        ( pmpaym.ValueDate == rp_VD ) )

  if( data_fits ) /* данные удовлетворяют заданным параметрам */
   /*
   wrk.DDrirection и wrk.MDirection уже проставлены
   */
   wrk.FIID      = dl_leg.PFI;
   wrk.Party     = dl_tick.PartyID;
   wrk.XID       = trim( dl_tick.DealCode );
   wrk.Principal = abs( dl_leg.Principal );
   wrk.Start     = dl_leg.Start;
   wrk.Maturity  = dl_leg.Maturity;
   wrk.Duration  = conv2days( dl_leg.Duration, dl_leg.Pitch );
   if (ПолучитьСтрокуЗначенияКурса(dl_leg.Price,dl_leg.Point,wrk.Rate)!=0)
     wrk.Rate      = dl_leg.Price;
   end;
/*   wrk.Rate      = dl_leg.Price;*/
   wrk.Symbol    = pmpaym.Purpose;
   wrk.Volume    = pmpaym.Amount;
   wrk.DRollover = dl_tick.Flag1;  /* признак пролонгации */
   wrk.Netting   = pmpaym.Netting; /* признак неттинга */

   /* Эти поля пока не используются для печати */
   wrk.Interest  = abs( dl_leg.Cost );
   wrk.VD        = rp_VD;

   insert( wrk );
  end;

  l_next = next( pmpaym );
  UseProgress(count = count + 1);
 end;
END;

/* Печать заголовка отчета */
MACRO printHeader( p_briefcase, p_category, p_FIID )
VAR strhead = "БЭК-ОФИС БАНКА " + {Name_Bank};
if (not flagXLS)
[  ];
[  ];
[                  ######################################################             ](strhead:c);
[  ];
[  ];
[        портфель            категория контрагентов     валюта      дата валютирования  ];
[┌──────────────────────┐   ┌──────────────────────┐   ┌──────┐   ┌────────────────────┐];
[│######################│   │######################│   │##### │   │####################│]( p_briefcase:c, p_category:c, DL_getCCode( p_FIID ):r, rp_VD:m:c );
[└──────────────────────┘   └──────────────────────┘   └──────┘   └────────────────────┘];
[  ];
[  ];
[┌──────────────────────┬───────────────────────────────┬───┬────────────────────┬──────────┬──────────┬──────┬──────────┬────────┬────────────────┬────────────────┬──┐];
[│      Контрагент      │            Сделка №           │Р/П│       Объем        │     с    │    по    │  дн  │ Ставка % │Средства│   Требования   │  Обязательства │  │];
else
Rep.SetValue_NameCell("RepHead",strhead);
Rep.SetValue_NameCell("Briefcase", p_briefcase );
Rep.SetValue_NameCell("Category", p_category );
Rep.SetValue_NameCell("Carry", DL_getCCode( p_FIID ) );
Rep.SetValue_NameCell("VDate", rp_VD:m );

end;
END;

/* Печать данных текущей записи из временного файла */
MACRO printLine

var l_partyShortName, l_XID, 
    l_principal, l_start, 
    l_maturity, l_duration, 
    l_rate, l_DDirection, 
    l_symbol, l_req, l_com, l_marks;


 if( wrk.XID != g_prevXID ) /* печатать ли данные по сделке */
  l_partyShortName = getPartyShortName( wrk.Party );
  l_XID            = wrk.XID;
  l_principal      = wrk.Principal;
  l_start          = wrk.Start;
  l_maturity       = wrk.Maturity;
  l_duration       = wrk.Duration;
  l_rate           = wrk.Rate;

  if( wrk.DDirection == "X" ) /* размещение */
   l_DDirection = "Р";
  else                        /* привлечение */
   l_DDirection = "П";
  end;
 else /* сделка та же - skip */
  l_partyShortName = "";
  l_XID            = "";
  l_principal      = "";
  l_start          = "";
  l_maturity       = "";
  l_duration       = "";
  l_rate           = "";
  l_DDirection     = "";
 end;

 if  ( wrk.Symbol == PM_PURP_PRINC )
  l_Symbol = " ОД";
 elif( wrk.Symbol == PM_PURP_PRINC_RET )
  l_Symbol = "ПОД";
 elif( wrk.Symbol == PM_PURP_PERCENT )
  l_Symbol = " % ";
 else
  l_Symbol = "";
 end;

 if( wrk.MDirection == "X" ) /* обязательства */
  l_req = "";
  l_com = string( wrk.Volume );
  g_com = g_com + wrk.Volume;
 else                        /* требования */
  l_req = string( wrk.Volume );
  l_com = "";
  g_req = g_req + wrk.Volume;
 end;

 l_marks = "";
 if( wrk.DRollover == "X" ) /* признак пролонгации */
  l_marks = l_marks + "П";
 end;

 if( wrk.Netting == "X" )   /* признак неттинга */
  l_marks = l_marks + "Н";
 end;

if (not flagXLS)
[├──────────────────────┼───────────────────────────────┼───┼────────────────────┼──────────┼──────────┼──────┼──────────┼────────┼────────────────┼────────────────┼──┤];
[│######################│###############################│###│####################│##########│##########│######│##########│########│################│################│##│]
( l_partyShortName:l, l_XID:r, l_DDirection:c, l_principal:r, l_Start:c, l_Maturity:c, l_Duration:r, l_Rate:r, l_Symbol:r, l_req:r, l_com:r, l_marks:r );
else
	if (lineadd!=0)
		Line.AddStr();
	end;
	lineadd=1;

	Rep.SetValue_NameCell("T_A1", l_partyShortName );  
	Rep.SetValue_NameCell("T_A2", l_XID );  
	Rep.SetValue_NameCell("T_A3", l_DDirection );  
	Rep.SetValue_NameCell("T_A4", l_principal );  
	Rep.SetValue_NameCell("T_A5", l_Start );  
	Rep.SetValue_NameCell("T_A6", l_Maturity );  
	Rep.SetValue_NameCell("T_A7", l_Duration );  
	Rep.SetValue_NameCell("T_A8", l_Rate );  
	Rep.SetValue_NameCell("T_A9", l_Symbol );  
	Rep.SetValue_NameCell("T_A10", l_req );  
	Rep.SetValue_NameCell("T_A11", l_com );  
	Rep.SetValue_NameCell("T_A12", l_marks );  

end;

 g_prevXID = wrk.XID;
END;

/* Печать итоговых строк таблицы */
MACRO printFooter
if (not flagXLS)
[└──────────────────────┴───────────────────────────────┴───┴────────────────────┴──────────┴──────────┴──────┴──────────┴────────┼────────────────┼────────────────┼──┘];
[                                                                                                                           Итого │################│################│   ]
( g_req:r, g_com:r );
[                                                                                                                                 └────────────────┴────────────────┘   ];
[  ];
[  ];
else
	Rep.SetValue_NameCell("T_B1", g_req );  
	Rep.SetValue_NameCell("T_B2", g_com );  
end;	
 g_req = 0; /* Итого - требования */
 g_com = 0; /* Итого - обязательства */
END;

/* Если значение == 0, установить в NULL */
MACRO setZeroToNull( p_value )
 if( p_value == 0 )
  return null;
 end;
 return p_value;
END;

/* Печать отчета */
MACRO printReport( p_briefcase, p_category, p_VD,  /* параметры для печати (всегда определены) */
                   p_DDirection,  p_FI, p_ExcludeFI, p_MDirection, /* параметры для расчета, м.б. неопред. */
                   p_PartyCtgID,  p_PartyCtgAttrID,
                   p_PortfKindID, p_PortfKindAttrID )

var l_FIID;

 /* Считать переданные параметры в глобальные переменные.
    Если какие-либо параметры не были заданы, они должны быть == 0 */
 rp_VD              = p_VD;
 rp_DDirection      = p_DDirection;
 rp_FI              = p_FI;
 rp_ExcludeFI       = p_ExcludeFI;
 rp_MDirection      = p_MDirection;
 rp_PartyCtgID      = setZeroToNull( p_PartyCtgID );
 rp_PartyCtgAttrID  = setZeroToNull( p_PartyCtgAttrID );
 rp_PortfKindID     = setZeroToNull( p_PortfKindID );
 rp_PortfKindAttrID = setZeroToNull( p_PortfKindAttrID );

 /* Обработать полученные параметры (привести к нужному типу) */
 if( rp_FI == -1 )
  rp_FI = null;
 end;

 if( rp_DDirection == 0 )
  rp_DDirection = null;
 else
  rp_DDirection = ( rp_DDirection == 2 );/* размещение */
 end;

 if( rp_MDirection == 0 )
  rp_MDirection = null;
 else
  rp_MDirection = ( rp_MDirection == 2 );/* обязательства */
 end;

 /* Получить данные для печати */
 InitProgress( -1, "Идет формирование отчета", "Движение средств - сделки денежного рынка МБК" );
 prepareData;
 RemProgress();

 /* Печать отчета */
 rewind( wrk );
 if( not next( wrk ) ) /* Рабочий файл пуст - заканчиваем работу */
  println( "Нет данных (платежей и/или сделок), \nудовлетворяющих заданным параметрам отчета." );
  close( wrk );
  delfile( wrk_filename );
  viewfile( setoutput( null ) );
  return TRUE;
 end;
 
 InitProgress( -1, "Идет печать отчета", "Движение средств - сделки денежного рынка МБК" );
 var count = 0;
 l_FIID = wrk.FIID;
 printHeader( p_briefcase, p_category, l_FIID );

 rewind( wrk );
 g_prevXID = "";
 while( next( wrk ) )
  if( l_FIID != wrk.FIID )
   printFooter;

   l_FIID = wrk.FIID;
   printHeader( p_briefcase, p_category, l_FIID );
  end;

  printLine;
  UseProgress(count = count + 1);
 end;

 printFooter;
 RemProgress();

 /* Окончание работы */
 close( wrk );
 delfile( wrk_filename );
 viewfile( setoutput( null ) );
 return TRUE; /* ? М.б. вывод печатной формы на экран */
END;

var l_FIID;
var BodyLine;

macro print_start(p_briefcase, p_category)
   //открываем шаблон
   Rep.OpenTemplate("Report_mmmovlist.xls",false);
   // регистрируем нашу таблицу
   BodyLine=Rep.RegisterTable("BodyLine");
   // очищаем ее от данных
   BodyLine.ClearCurRow(); 
   //печатаем заголовок
   l_FIID = wrk.FIID;
   printHeader( p_briefcase, p_category, l_FIID );
   Line=BodyLine;   
end;

var Tial,tial_adr,tial_adr_ob,bl_adr,eBodyLine,a1,b1,str;

macro print_end( )
   printFooter;
   
	 rep.CopyAllSheetInTotalBook(null,
										false,
										"Head",
										0,

										"Отчет");

	BodyLine.AddStr();

	 bl_adr = BodyLine.GetTableRange();


									
	rep.CopyAllSheetInTotalBook(null,
										false,
										bl_adr,
										0,
										"Отчет");
	//получаем адрес блока итого
	 Tial=Rep.RegisterTable("Tial");	
	 tial_adr = Tial.GetTableRange();
	 tial_adr_ob = CAddress(tial_adr);
	//получаем конец диапазона
	 eBodyLine = BodyLine.eRowTable;
	if (eBodyLine < BodyLine.bRowTable)
	 eBodyLine = BodyLine.bRowTable;
	end;
	//Высситываем откуда копировать Итого
	 a1 = eBodyLine + 1;
	 b1 = a1 + tial_adr_ob.Get_eRow() - tial_adr_ob.Get_bRow() ;
	//формируем диапазоно откуда копировать
	 str = _SplitCoord(a1,tial_adr_ob.Get_bCol()) +":"+  _SplitCoord(b1, tial_adr_ob.Get_eCol());

									
	rep.CopyAllSheetInTotalBook(null,
										false,
										str,
										0,
										"Отчет");
	// Закрытие шаблона
	rep.Close();  
end;

/***********************************/
//XLS
MACRO printReportXLS( p_briefcase, p_category, p_VD,  /* параметры для печати (всегда определены) */
                   p_DDirection,  p_FI, p_ExcludeFI, p_MDirection, /* параметры для расчета, м.б. неопред. */
                   p_PartyCtgID,  p_PartyCtgAttrID,
                   p_PortfKindID, p_PortfKindAttrID )


 /* Считать переданные параметры в глобальные переменные.
    Если какие-либо параметры не были заданы, они должны быть == 0 */
 rp_VD              = p_VD;
 rp_DDirection      = p_DDirection;
 rp_FI              = p_FI;
 rp_ExcludeFI       = p_ExcludeFI;
 rp_MDirection      = p_MDirection;
 rp_PartyCtgID      = setZeroToNull( p_PartyCtgID );
 rp_PartyCtgAttrID  = setZeroToNull( p_PartyCtgAttrID );
 rp_PortfKindID     = setZeroToNull( p_PortfKindID );
 rp_PortfKindAttrID = setZeroToNull( p_PortfKindAttrID );

 /* Обработать полученные параметры (привести к нужному типу) */
 if( rp_FI == -1 )
  rp_FI = null;
 end;

 if( rp_DDirection == 0 )
  rp_DDirection = null;
 else
  rp_DDirection = ( rp_DDirection == 2 );/* размещение */
 end;

 if( rp_MDirection == 0 )
  rp_MDirection = null;
 else
  rp_MDirection = ( rp_MDirection == 2 );/* обязательства */
 end;

 /* Получить данные для печати */
 InitProgress( -1, "Идет формирование отчета", "Движение средств - сделки денежного рынка МБК" );
 prepareData;
 RemProgress();

 /* Печать отчета */
 rewind( wrk );
 if( not next( wrk ) ) /* Рабочий файл пуст - заканчиваем работу */
  MsgBox("Нет данных (платежей и/или сделок), \nудовлетворяющих заданным параметрам отчета.");
  close( wrk );
  delfile( wrk_filename );
  return TRUE;
 end;

 InitProgress( -1, "Идет печать отчета", "Движение средств - сделки денежного рынка МБК" );
 var count = 0;
 flagXLS=true;
 Rep.CreateTotalBook();

 print_start(p_briefcase, p_category);

 rewind( wrk );
 g_prevXID = "";


 while( next( wrk ) )

  if( l_FIID != wrk.FIID )
  
     print_end();
   
     print_start(p_briefcase, p_category);  

  end;
  printLine;
  UseProgress(count = count + 1);
 end;

 print_end();


 /* Окончание работы */
 close( wrk );
 delfile( wrk_filename );


Rep.SaveTotalBook("ДВИЖЕНИЕ СРЕДСТВ - СДЕЛКИ ДЕНЕЖНОГО РЫНКА (МБК)", true);
 flagXLS=false;
 return TRUE; /* ? М.б. вывод печатной формы на экран */
end;                   


/* --------------------------- ENTRY POINT --------------------------------- */

/* Создать и открыть временный файл для записи собранных данных */
if( not create( wrk, wrk_filename ) )
 msgbox( "Невозможно создать рабочий файл ", wrk_filename );
 exit(1);
end;

if( not open( wrk, wrk_filename ) )
 msgbox( "Невозможно открыть рабочий файл ", wrk_filename );
 exit(1);
end;

/* EoF */
