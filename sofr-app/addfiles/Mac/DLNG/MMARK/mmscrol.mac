/*
$Name: mmscrol.mac
$Module: МБК
$Description: Макрос применяется для инициализации и проверки параметров
              сделки в скроллинге сделок
*/


/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.0                                          R-Style Software Lab

  File Name   : mmscrol.mac
  Created     : 15.04.99
  Programmer  : Сабитов Р.Р.
  Description : Макрос применяется для инициализации и проверки параметров
                сделки в скроллинге сделок

  Comment     : ДОБАВЛЯЙТЕ К ГЛОБАЛЬНЫМ ПЕРЕМЕННЫМ ПРЕФИКС mmscrol_

└───────────────────────────────────────────────────────────────────────────*/
Import globals,BankInter,PaymInter,DealsInter, MmarkInter, PcRateInter,dealInfo,
       mmcateg, dlref, mmlib, mmrestlist, mctplprm, mmlibr,funk;
import "UpdateStatusDeal.mac";  // Интеграция: сервис выгрузки статуса сделки
import oralib, likepy;
import "dvswiftMT320.mac"; // Формирование МТ320 для RS-Payments

RECORD МакетСделки (dl_sbmd);
RECORD СтарыйМакетСделки (dl_sbmd);   /*Используется только при редактировании!! В остальных случаях в переменной будет мусор*/

class GenAgrRep()
  var
      NumDeal = "",
      Result  = "",
      Comment = "";      
end;

PRIVATE VAR TickToGenAgrRep = TArray;

const RTERR_NOSUCHRATE = 9052, /*Не найдено ставки с такими параметрами*/
      RT_PMPURP_OD = 1, /*Предоставление средсв (ОД)*/
      RT_PURPOSE_ANALIZE = 1,
      МБКр = 1,
      МБКн = 2,
      МБДр = 3,
      МБДн = 4;

PRIVATE FILE mmscrol_Party ("party");

/* Вид периода для генерации графика оплат */
PRIVATE const  PC_PAYGRH_WORK_DAY     = 1,   /* рабочий день     */
       PC_PAYGRH_CALEND_DAY   = 2,   /* календарный день */
       PC_PAYGRH_WORK_MONTH   = 3,   /* рабочий месяц    */
       PC_PAYGRH_CALEND_MONTH = 4;   /* календарный месяц*/

PRIVATE const ALLPARTY = -1;

/* Макрос выбора макета. На момент его вызова, по сути, известен только
   вид операции - МакетСделки.DealType. Функция вызывается сразу после
   выбора вида операции. Должна вернуть ID макета или 0, если макет не выбран.
*/

  private macro SelectScroll(Query, Header, x, y /*...*/)
      var rs     = RsdRecordset(Query, NULL, RSDVAL_STATIC),
          Column = TArray(),
          i, ColumnValue, ColumnName, NumColumns = 0;

    macro EvProc(rs, cmd, id, key)
        if ((cmd == DLG_KEY)and(key == 13))
            if (rs.RecCount > 0)
                return CM_SELECT;
            end;
        end;
        return CM_DEFAULT;
    end;
    
    macro AddColumn(ar,ind, fld, head)
        ar.value( ind * 6 + 0 ) = fld;
        ar.value( ind * 6 + 1 ) = head;
        ar.value( ind * 6 + 2 ) = NULL;
        ar.value( ind * 6 + 3 ) = 2;
        ar.value( ind * 6 + 4 ) = NULL;
        ar.value( ind * 6 + 5 ) = 0;
        NumColumns = NumColumns + 1;
    end;

      i = 4;
      while (GetParm(i, ColumnValue)and(GetParm(i+1, ColumnName)))              
         AddColumn( Column, NumColumns, ColumnValue, ColumnName );
         i = i + 2;
      end;   
  
      if (RunScroll(rs, NumColumns, Column, NULL, @EvProc, Header, "Esc Выход F4 Поиск Enter Выбор", true, x, y, NULL, 25))
          return rs;
      end;

      return NULL;
  end;

MACRO РассчитатьСуммуНалога(Doc)
    record tick(dl_tick);
    SetBuff(tick, Doc);
    var TaxAmount = $0, Res = $0;

    if((isBuy(GetOperationGroup(tick.DealType)) and (tick.NegativeRate == "X")) or
       (isSale(GetOperationGroup(tick.DealType)) and (tick.NegativeRate != "X")))
        TaxAmount = GetTaxBank(tick);
    else
        TaxAmount = GetTaxContr(tick);
    end;
    Res = МакетСделки.Cost * TaxAmount/100;
    return Res;
END;
  
MACRO ПолучитьУсловиеРасчета(Doc)
  record tick(dl_tick);
  var CalcGroup = 0, rs = NULL;

    SetBuff(tick, Doc);

    if (isSale(GetOperationGroup(tick.DealType)))
        CalcGroup      = 100;
    else
        CalcGroup      = 101;
    end;

     rs = SelectScroll("select calc.* from dprccalc_dbt calc, dprcgrcalc_dbt grp where " +
                        "(calc.t_isuser is null or calc.t_isuser <> 'X') " +
                        "and calc.t_groupcalcid = grp.t_groupcalcid " +
                        "and grp.t_number =  " + CalcGroup +
                        " order by calc.t_number",
                        "Условия  расчета", 0, 0,
                        "t_number",    "Номер",
                        "t_shortname", "Название", 
                        "t_opendate",  "Открыто",  
                        "t_closedate", "Закрыто");

    if (rs != NULL)
        return Int(rs.value("t_number"));
    end;

    return 0;
END;

MACRO ВыбратьМакет ()
   return 0;
END;

macro СформироватьНомерСделки()
  var
    retVal = 0;

    if (DL_GenRefByTypeDoc(OBJTYPE_MMARKDEAL, МакетСделки.DealCode))
        if (МакетСделки.DealCodeTS == "")
            МакетСделки.DealCodeTS = substr(МакетСделки.DealCode,3);
        end;
        retVal = 0;
    else
        msgbox("Ошибка при генерации номера сделки");
        retVal = 1;
    end;

    return retVal;
end;

macro ОткатитьНомерСделки()
    DL_RestoreRefByTypeDoc(OBJTYPE_MMARKDEAL, МакетСделки.DealCode);
    return 0;
end;

/*В этом макросе можно изменять "МакетСделки"*/
MACRO Новая_Сделка ()
    if (isSale(GetOperationGroup(МакетСделки.DealType)))
        МакетСделки.IbcConCalc      = 100;
        МакетСделки.IbcConExpCalc   = 100;
        МакетСделки.IbcConExpPcCalc = 100;
    else
        МакетСделки.IbcConCalc      = 101; 
        МакетСделки.IbcConExpCalc   = 101;
        МакетСделки.IbcConExpPcCalc = 101;
    end;

//    if ((МакетСделки.DealType==2310) or (МакетСделки.DealType==2311))
//        /*  Режимы неттинга
//            1 - Нет
//            2 - Частичный
//            3 - Полный
//        */
//        МакетСделки.Netting = 3;
//    end;

    return 0;
END;

/*  Режимы:
    DL_MACOP_INSERT         Ввод новой сделки
    DL_MACOP_DELETE         Удаление сделки
    DL_MACOP_UPDATE         Обновление сделки
    DL_MACOP_CREATEOPER     Создание операции
    DL_MACOP_DELETEOPER     Удаление операции
*/
PRIVATE MACRO Проверить_Сделку (Режим);
      var LastRefer;
    /*В этой макросе изменение "МакетСделки" не имеет смысла,
      изменения не будут учтены*/
     if ( Режим == DL_MACOP_INSERT ) 
         if( МакетСделки.DealCode == "" ) 
             if( GetTrue( true, "Номер сделки не указан.|Сформировать все номера автоматически?") ) 
                 СформироватьНомерСделки();
             else
                 return 1;
             end    
         end     
    end;         
    if ((Режим==DL_MACOP_INSERT) or (Режим==DL_MACOP_UPDATE) )

	    if (МакетСделки.PartyID=={OurBank})
            msgbox ("Наш Банк не может быть контрагентом");
            return 9;
        end;
		
        if ((МакетСделки.PartyID==ALLPARTY))
            msgbox ("Не задан контрагент");
            return 9;
        end;

        if ((МакетСделки.PaymAgentID==ALLPARTY))
            msgbox ("Не задан платежный агент");
            return 9;
        end;

        if ((МакетСделки.Srok=="X") and (МакетСделки.PlanMaturity==date(0,0,0)))
            msgbox ("Не задана планируемая дата погашения");
            return 9;
        end;


        if ((МакетСделки.DealType==2310) or (МакетСделки.DealType==2311))
            mmscrol_Party.PartyID = МакетСделки.PartyID;
            if (GetEQ(mmscrol_Party))
                if (mmscrol_Party.Superior!={OurBank})
                    msgbox ("Контрагент должен быть отделением");
                    return 9;
                end;
            else
                msgbox ("Не найден контрагент");
                return 9;
            end;
        end;
    end;

    if (Режим==DL_MACOP_CREATEOPER)
        if (МакетСделки.Start<МакетСделки.DealDate)
            msgbox ("Дата заключения не может быть|меньше даты валютирования");
            return 1;
        end;
    
//        if (МакетСделки.Principal==0)
//            msgbox ("Не задан объем сделки");
//            return 1;
//        end;
//
//        if (МакетСделки.Price==0)
//            msgbox ("Не задана процентная ставка");
//            return 1;
 //       end;
//
//        if (МакетСделки.Cost==0)
//            msgbox ("Не задана сумма процентов");
//            return 1;
//        end;
//
        if( МакетСделки.TraderID == ALLPARTY )
           MsgBox("Не указан дилер.");
           return 1;
       end;
    end;

    if (Режим==DL_MACOP_UPDATE)
        /*
            При редактировании производим проверку важности внесенных изменений   

            Константы важности внесенных изменений:
            CHANG_NOTIMPORTANT  - изменения не влекут за собой последствий
                                  (сохранение без отката операции)
            CHANG_IMPORTANT     - изменения влекут за собой последствия
                                  (такие изменения возможны только в списке
                                  отложенных сделок)
            CHANG_NOTKEEP       - не сохранять изменения

            Если возвращаемое значение  > 0, то оно интерпретируется как номер
            поля с ошибочным параметром

            Если возвращаемое значение  = 0, то cчитается, что проверка прошла
            успешно и сохранение изменений можно производить. Полный аналог
            CHANG_NOTIMPORTANT.
            
            Сделка с изменениями, определенными как CHANG_IMPORTANT может
            быть сохранена только в отложенных

            !!! Если пользователь произвел изменения, определяемые системой
                (не макросом) как CHANG_IMPORTANT, возвращаемые макросом
                значения CHANG_NOTIMPORTANT или 0 игнорируются
        */

        if ((СтарыйМакетСделки.PartyID!=МакетСделки.PartyID) or
            (СтарыйМакетСделки.Principal!=МакетСделки.Principal) or
            (СтарыйМакетСделки.Maturity!=МакетСделки.Maturity) or
            (СтарыйМакетСделки.DealCode!=МакетСделки.DealCode) or
            (СтарыйМакетСделки.Start!=МакетСделки.Start) or
            (СтарыйМакетСделки.PFI!=МакетСделки.PFI))
            return CHANG_IMPORTANT;
        end;

        return CHANG_NOTIMPORTANT;
    end;

    return 0;
END;

/*  Режимы:
    MM_MACOP_CORRECTINS     Корректировка при вводе
    MM_MACOP_CORRECTUPD     Корректировка при редактировании
    MM_MACOP_BEGINUPD       Перед редактированием
*/

MACRO Сервис_Сделки (Режим)
    return 0;
END;

/*
    true - Изменять платеж
    false - Не изменять платеж
*/
MACRO КорректировкаПлатежей (tick_p, paym_p,debet_p,credit_p, rm_p)
  RECORD tick (dl_tick);
  RECORD mmscrol_paym (pmpaym);
  RECORD mmscrol_debet (pmprop);
  RECORD mmscrol_credit (pmprop);
  RECORD mmscrol_rm (pmrmprop);
    var RefID = 0;
    var Ref = "";

    SetBuff (tick,tick_p);
    SetBuff (mmscrol_paym,paym_p);
    SetBuff (mmscrol_debet,debet_p);
    SetBuff (mmscrol_credit,credit_p);
    SetBuff (mmscrol_rm,rm_p);
    GetReferenceIDByType(OBJTYPE_MMARKDEAL, 2, refID);
    GenerateReference(refID, Ref);
    mmscrol_rm.Number = tick.DealCode + "/" + Ref;
    return true;
END;

//Информация по последней (с самым большим ИД) ставке оценочного резерва по сделке
private macro GetOrInfo (dealid, rate, date, id)
  var sql = ExecSqlSelect("select t_id, t_date, t_percloss from dmmhstimp_dbt where t_dealid = " + dealid + " order by t_id desc");
  if (sql.MoveNext() )
    SetParm(1, sql.value("t_percloss") );
    SetParm(2, sql.value("t_date") );
    SetParm(3, sql.value("t_id") );
  end;
End;

//Обновление ставки оценочного резерва
private macro UpdateRateOR (id:integer, rate:money)
  return (ExecSql("update dmmhstimp_dbt set t_percloss = " + rate + " where t_id = " + id) != null);
onerror()
  return false;
End;

PRIVATE MACRO ФункцияПользователя ()

  if ({oper} == 129)   // отключим Контролёру МБК функцию пользователя  (Щербинин, до появления системного решения от R-Style)
    return 0;
  end;

    /*В этой ф-ии можно изменять "МакетСделки", изменения будут учтены*/
    private var que,rs09, dealid, dealstatus, dealdate, isenable,dealtype,newdealcode,dealcode,price,ssu,dealdate2,principal,ii9,basi,rs91,rs92;
    private Var items = Tarray (), ch;
    private Var mass = Tarray (), jk;

    array mas1,mas2;
    mas1(0)="ACT/ACT          ";
    mas1(1)="360/30           ";
    mas1(2)="360/ACT          ";
    mas1(3)="365/ACT          ";

    mas2(0)=4;
    mas2(1)=1;
    mas2(2)=2;
    mas2(3)=8;

 /*
 if (  МакетСделки.dealtype == 12306  )

    items [0] = "График платежей               ";
    items [1] = "Параметры приобретения требований";

    if ( (ch = menu (items, NULL, "Выберите действие")) < 0 )
        return 0;
    end;

    que = "select t_dealid, t_dealstatus, t_dealdate from ddl_tick_dbt  where t_bofficekind=102 and t_dealcode='"+МакетСделки.dealcode+"'";
    rs09 = LnGetRecordset(que);
    if(rs09 != null)
            if (rs09.moveNext) 
              dealid     = rs09.value(0);
              dealstatus = rs09.value(1);
              dealdate   = rs09.value(2);
            end;
    end;

    if   ( ch == 0 )  
            execmacrofile("dl_graf.mac", "prog1", dealid) ;

    elif ( ch == 1 )      
            if (dealstatus  == 0)
               isenable = true;
            else
               isenable = false;
            end;
            execmacrofile("dl_demands.mac", "MakeCDemands", dealid, dealdate, isenable) ;
    end;

 else
*/    
//SVE 29.03.19 Пользователю необходимы были все пункты меню для вида операции 12306, конкретно изменение номера сделки
    items [items.size] = "График платежей";
    items [items.size] = "СПИ по платежам";
    items [items.size] = "Кредитная линия";
    items [items.size] = "Ставка возмещения";
    items [items.size] = "История изменений по сделке";
    items [items.size] = "Печать 115ФЗ по сделке";
    items [items.size] = "Печать 115ФЗ за дату";
    items [items.size] = "Cнятие признака пролонгации";
    items [items.size] = "Установка признака пролонгации";
    items [items.size] = "Разблокировка сделки в Кондор";
    items [items.size] = "Признак - без mt320";
    items [items.size] = "Изменение номера сделки";
    items [items.size] = "Счета по сделке";
    items [items.size] = "Актуализация начисленных процентов после доср. погашения";
    items [items.size] = "Обновление счетов в платежах";
    if (МакетСделки.dealtype == 12306)
       items [items.size] = "Параметры приобретения требований";
       items [items.size] = "Закрытие без балансовых проводок";

    end;

    if ( InList(МакетСделки.dealtype, 12305, 12306, 12335, 32305) )
      items [items.size] = "Изменить значение ставки оценочного резерва";
    end;

    if ( InList(МакетСделки.dealtype, 12300, 12305, 32300, 32305) )
      items [items.size] = "Формирование MT320";
      items [items.size] = "Формирование MT320 (дубликат)";
      items [items.size] = "Формирование MT320 (изменения)";
    end;
    
    if ( (ch = menu (items, NULL, "Выберите действие")) < 0 )
        return 0;
    end;

    que = "select a.t_dealid, a.t_dealstatus, a.t_dealdate,a.t_dealtype, a.t_dealcode, b.t_maturity, b.t_principal from ddl_tick_dbt  a, ddl_leg_dbt b  where a.t_bofficekind=102 and a.t_dealcode='"+МакетСделки.dealcode+"'  and b.t_dealid=a.t_dealid ";
    rs09 = LnGetRecordset(que);
    if(rs09 != null)

            if (rs09.moveNext) 
              dealid     = rs09.value(0);
              dealstatus = rs09.value(1);
              dealdate   = rs09.value(2);
              dealtype   = rs09.value(3);
              dealcode   = rs09.value(4);
              dealdate2   = rs09.value(5);
              principal   = rs09.value(6);

            end;
    end;

    if   ( items[ch] == "График платежей" )  

       if ((dealtype == 12315) or (dealtype == 12310))
        que = "update ddl_tick_dbt  set t_ISTRADEFINANCE='X' where t_DealID = " + DealID;
        rs09 = LnGetRecordset(que);
       end;
            execmacrofile("dl_graf.mac", "prog1", dealid) ;

    elif ( items[ch] == "СПИ по платежам" )      
            execmacrofile("dl_mt.mac", "Kontr320",dealid) ;
    // elif ( items[ch] == "Портфель" )      
    //        execmacrofile("dl_postfin.mac", "MakeCPortfolio", dealid) ;

    elif ( items[ch] == "Кредитная линия" )      
             execmacrofile("dl_prol.mac", "KL_SDEL", МакетСделки.DealCode) ;

    elif ( items[ch] == "Ставка возмещения" )  
          // execmacrofile("dl_npaym.mac", "redpaym", dealid,{curdate}) ;

          //  execmacrofile("dl_postfin.mac", "ReimRate", dealid) ;
          // execmacrofile("dl_prol.mac", "NEW_PROL3",dealid) ;
          if (dealtype  == 12310 )
           // newdealcode=dealcode;
           // getstring(newdealcode,"Введите номер сделки:",29);
           // que="update ddl_tick_dbt set t_dealcodets='"+newdealcode+"'  where t_dealid="+ DealID;
           // rs09 = LnGetRecordSet(que);
  
           newdealcode=0.00;
           getdouble(newdealcode,"Введите сумму маржи:");
            que="update ddl_leg_dbt set t_tablepercent ='X',t_typepercent=1,t_caption=2626, t_correct="+newdealcode+",t_price=0.0,t_cost=0.0   where t_dealid="+ DealID;
            rs09 = LnGetRecordSet(que);
          end;

    elif ( items[ch] == "История изменений по сделке" )     


      mass[0] = "История пролонгации по сделке ";
      mass[1] = "История по процентной ставке  ";
      mass[2] = "История по платежам           ";
      if ( (jk = menu (mass, NULL, "Выберите действие")) < 0 )
          return 0;
      end;
      if   ( jk == 0 )  
          execmacrofile("dl_prol.mac", "HIST_PROL",МакетСделки.DealCode) ;
      elif   ( jk == 1 ) 
          execmacrofile("mm_ps.mac", "prog_ps",dealid,0) ;
      elif   ( jk == 2 ) 
          execmacrofile("dl_prol.mac", "HIST_PL",dealid) ;
      end;

    elif ( items[ch] == "Печать 115ФЗ по сделке" )     
          if (MsgBoxEx("Показывать отчет?",MB_YES+MB_NO,IND_YES) == IND_YES)
            execmacrofile("dl_print.mac", "Print_115fz", dealid, false, 0) ;
          else
            execmacrofile("dl_print.mac", "Print_115fz", dealid, true, (-1)) ;
          end;
    elif ( items[ch] == "Печать 115ФЗ за дату" )     
            execmacrofile("dl_print.mac", "Print_115fz_AllbyDate", dealid, true) ;
    elif ( items[ch] == "Установка признака пролонгации" ) 
          execmacrofile("dl_prol.mac", "NEW_PROL",dealid) ;
    elif ( items[ch] == "Cнятие признака пролонгации" ) 
          execmacrofile("dl_prol.mac", "NEW_PROL2",dealid) ;
    elif ( items[ch] == "Признак - без mt320" ) 
        que = "update ddl_tick_dbt  set T_CONFTPID=0,T_NUMBERPACK=99  where t_DealID = " + DealID;
        rs09 = LnGetRecordset(que);

    elif ( items[ch] == "Закрытие без балансовых проводок" ) 
        que = "update ddl_tick_dbt  set T_CONFTPID=0,T_NUMBERPACK=390  where t_DealID = " + DealID;
        rs09 = LnGetRecordset(que);

    elif ( items[ch] == "Изменение номера сделки" ) 
        newdealcode=dealcode;
        getstring(newdealcode,"Введите номер сделки:",29);
        que="update ddl_tick_dbt set t_dealcode='"+newdealcode+"'  where t_dealid="+ DealID;
        rs09 = LnGetRecordSet(que);
        que="update dprccontract_dbt set t_objectid='"+newdealcode+"'  where t_ObjectType=103 and t_objectid='"+dealcode+"'";
        rs09 = LnGetRecordSet(que);

    elif ( items[ch] == "Счета по сделке" ) 
          execmacrofile("dl_prol.mac", "ACC_SDEL",dealid) ;

    elif ( items[ch] == "Актуализация начисленных процентов после доср. погашения" ) 
          execmacrofile("mm_updperc.mac", "MM_UpdPercPm",dealid) ;
          execmacrofile("mm_updperc.mac", "MM_UpdPrcSched",dealid) ;

    elif ( items[ch] == "Разблокировка сделки в Кондор")// Разблокировка в К+
       //интеграция: выгрузка статуса сделки при помещении в отложенные
       UpdateStatusDeal(CreateObjectForExport( МакетСделки, EXPORT_DEAL_ROLLED_BACK, "MBK" ), true);
    
    elif (items[ch] == "Параметры приобретения требований")
/*
       if (dealstatus  == 0)
          isenable = true;
       else
          isenable = false;
       end;
*/
       execmacrofile("dl_demands.mac", "MakeCDemands", dealid, dealdate, true);

    elif (items[ch] == "Изменить значение ставки оценочного резерва")
       var NewRateOR = $0;
       var OldRateOR = $0;
       var DateOR:Date = Date();
       var IDOR = 0; //ID ставки оценочного резерва

       GetOrInfo(dealid, OldRateOR, DateOR, IDOR);

       if (IDOR == 0) //ставка не найдена
         MsgBox("Для выбранной сделки ставка не заполнена");
         return 0;
       end;

       NewRateOR = OldRateOR;
       if (GetMoney(NewRateOR) )
         if ( MsgBoxEx("Вы действительно желаете изменить значение ставки оценочного резерва на дату " + DateOR + "?|Текущая ставка = " + OldRateOR + "; новая ставка = " + NewRateOR, MB_NO+MB_YES, IND_NO) == IND_YES)
          if (UpdateRateOR(IDOR, NewRateOR) ) 
            MsgBox("Ставка изменена!");
            return 0;
          end;
         end;
       end;
    
    elif (items[ch] == "Формирование MT320")
       DV_MsgSWIFT_MT320FromMenu(dealid, "");
    elif (items[ch] == "Формирование MT320 (дубликат)")
       DV_MsgSWIFT_MT320FromMenu(dealid, "DUPL");
    elif (items[ch] == "Формирование MT320 (изменения)")
       DV_MsgSWIFT_MT320FromMenu(dealid, "AMND");
    elif (items[ch] == "Обновление счетов в платежах")
		que = "select t_paymentid from dpmpaym_dbt where t_dockind=102 and t_documentid = " + dealid + " and t_purpose = 9 and t_paymstatus != " + PM_PREPARING + " and t_paymstatus != " + PM_READIED;
		rs09 = LnGetRecordset(que);
		if(rs09 != null)
            if (rs09.moveNext) 
				msgbox("Было выполнено предоставление средств. Изменение счетов невозможно");
				return 0;
            end;
		end;
	
        var FirstDoc = MMFirstDoc(DL_IBCDOC, dealid, true);
        execmacrofile("mmactacc.mac", "UpdatePayments", FirstDoc, false);
        MsgBox("Счета обновлены!");
    end;

// end;
    return 0;
END;

MACRO ОпределитьПродукт (partyID, TypeDoc)
  var party = TBfile("party");
  if (partyID == 0)
    partyID = {OurBank};
  end;
  party.KeyNum = 0;
  party.rec.PartyID = partyID;
  if (GetEQ(party))
    if (party.rec.NotResident == "X") /*нерезидент*/
      if(Index(TypeDoc, "L") != 0) /*кредит*/
        return МБКн;
      else
        return МБДн;
      end;
    else /*резидент*/
      if(Index(TypeDoc, "L") != 0) /*кредит*/
        return МБКр;
      else
        return МБДр;
      end;
    end;
  end;
  
  return 0;
END;

MACRO ОпределитьНаправление(DealType)
  if (isSale(GetOperationGroup(DealType)))
    return 2;
  else
    return 1;
  end;
END;
/*Определяет кат. качества сделки. Если сделку вводим, то 1, если редактируем, то ищет.*/
MACRO ОпределитьККС(Сделка)
var QC, tick = TBfile("dl_tick");

  tick.KeyNum = 1;
  tick.rec.BofficeKind = DL_IBCDOC;
  tick.rec.DealCode = Сделка.DealCode;
  if (GetEQ(tick))
    QC = ПолучитьКатегориюКачества( tick.rec.DealID, {curdate} );
    if(QC > 0)
       return QC;
    end;
  end;

  return 1; // Пусть в данном случае по-умолчанию всегда будет 1-ая ККС
END;

MACRO НайтиПараметры(Сделка)

var ZeroDate = Date(0, 0, 0);

  if (Сделка.PFI == -1)
    msgbox("Не задана валюта");
    return 1;
  elif (Сделка.Basis == 0)
    msgbox("Не задан базис");
    return 1;
  elif (Сделка.DealType == 0)
    msgbox("Не задан тип сделки");
    return 1;
  elif (Сделка.Start == ZeroDate)
    msgbox("Не задана дата начала");
    return 1;
  elif (Сделка.Maturity == ZeroDate)
    msgbox("Не задана дата окончания");
    return 1;
  elif (Сделка.Principal == 0)
    msgbox("Не задан объем сделки");
    return 1;
  elif (Сделка.PartyID == -1)
    msgbox("Не задан контрагент по сделке");
    return 1;
  elif (Сделка.TypeDoc == "")
    msgbox("Не задан системный тип сделки");
    return 1;
  else
    return 0;
  end;
    

END;
/*Макрос определения значения ставки по таблице процентных ставок*/
MACRO ПодставитьСтавку()
  var stat = 0;
  record rate("mmrt");
  msgbox("!");

  if (НайтиПараметры(МакетСделки))
    return 1;
  end;
  stat = ПолучитьСтавку(МакетСделки.PFI,
                        ОпределитьПродукт(МакетСделки.PartyID, МакетСделки.TypeDoc),
                        RT_PMPURP_OD,
                        {OurBank},
                        {OurBank},
                        RT_PURPOSE_ANALIZE,
                        MM_GRPT_CB,
                        ОпределитьККС(МакетСделки),
                        МакетСделки.Basis,
                        ОпределитьНаправление(МакетСделки.DealType),
                        {curdate},
                        {curdate},
                        МакетСделки.Start,
                        МакетСделки.Maturity,
                        МакетСделки.Principal,
                        rate);
  if (stat == 0)
    МакетСделки.Price = rate.Value;
    МакетСделки.Point = rate.Pitch;
    МакетСделки.Caption = rate.RateKind;
  elif (stat == RTERR_NOSUCHRATE)
    msgbox("Не найдено ставки с такими параметрами");
  else
    msgbox("Ошибка при определении ставки");
  end;
  return stat;
END;

macro MakeCommonRef(Rate, DealType, PartyID, CommonRef)
var BIC_A, BIC_B, error, СделкаПривлечения = true;
var Bank1,Place1,RefCode,Bank2,Place2,
    LastNonZero,i,symb;
    if( isSale( getOperationGroup( DealType ) ) )
        СделкаПривлечения = FALSE;  /* Это сделка размещения */
    end;
     
    if(СделкаПривлечения)
        BIC_A = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
        if( error ) 
            BIC_A = ПолучитьКодСубъекта( {OurBank}, PTCK_BIC, error );
            if ( error ) return 0; end;
        end;
        BIC_B = ПолучитьКодСубъекта( PartyID, PTCK_SWIFT, error );
        if( error ) 
            BIC_B = ПолучитьКодСубъекта( PartyID, PTCK_BIC, error );
            if ( error ) return 0; end;
        end;
    else
        BIC_B = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
        if( error ) 
            BIC_B = ПолучитьКодСубъекта( {OurBank}, PTCK_BIC, error );
            if ( error ) return 0; end;
        end;
        BIC_A = ПолучитьКодСубъекта( PartyID, PTCK_SWIFT, error );
        if( error ) 
            BIC_A = ПолучитьКодСубъекта( PartyID, PTCK_BIC, error );
            if ( error ) return 0; end;
        end;
    end;
     
    if (StrLen(BIC_A) < 8) return 0; 
    end;
    if (StrLen(BIC_B) < 8) return 0;
    end;
       
    if (BIC_A > BIC_B)
    Bank1 = SubStr(BIC_B,1,4);
        Place1 = SubStr(BIC_B,7,2);
    Bank2 = SubStr(BIC_A,1,4);
        Place2 = SubStr(BIC_A,7,2);
    else
        Bank1 = SubStr(BIC_A,1,4);
        Place1 = SubStr(BIC_A,7,2);
    Bank2 = SubStr(BIC_B,1,4);
        Place2 = SubStr(BIC_B,7,2);
    end;
       
    LastNonZero = 0;
    Rate = string(int(Rate));	 //SVE просто с double функции со строками не работают
    for (i, 1, StrLen(Rate))
        symb = SubStr(Rate,i,1);
        if ((symb != "0") and (symb != ",") and (symb != "."))
       	    LastNonZero = i;
        end;
    end;
       
    if (LastNonZero >=4) 
       	RefCode = SubStr(Rate,LastNonZero-3,4);
    else 
       	RefCode = SubStr(Rate,1,LastNonZero);
        while (StrLen(RefCode)<4)
       		RefCode = "0" + RefCode;
        end;
    end;
       
    CommonRef = Bank1+Place1+RefCode+Bank2+Place2; 
       
    SetParm(3, CommonRef);

  return 0;
END;

PRIVATE MACRO Привязать_к_ГС(DealID, GenAgrID)
    var
        cmd = "", rs = NULL, rsG = NULL, rsB = NULL, rep = NULL, DealCode = "", retVal = 0, rsP = NULL, partyErr = 0;

    if (GenAgrID > 0)   // привязка
        rs = TRsbDataSet("select * from ddl_tick_dbt tick, ddl_leg_dbt leg where" +
                          " leg.t_DealID = tick.t_DealID and tick.t_DealID = " + String(DealID));

         rsG = TRsbDataSet("select * from ddl_genagr_dbt gagr where" +
                          " gagr.t_GenAgrID = " + String(GenAgrID));

        rsB = TRsbDataSet("select * from ddl_gabasis_dbt basis where" +
                          " basis.t_GenAgrID = " + String(GenAgrID));

        if (not rs.MoveNext)
            rep = GenAgrRep;
            rep.NumDeal = "";
            rep.Result  = "не привязана";
            rep.Comment = "Сделка с ID = " + String(DealID) + " не найдена.";
        else
            DealCode = rs.DealCode;
        end;

        if (not rsG.MoveNext)
            rep = GenAgrRep;
            rep.NumDeal = "";
            rep.Result  = "не привязана";
            rep.Comment = "ГС с ID = " + String(GenAgrID) + " не найдено.";
        end;

        if (rs.GenAgrID != -1)
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязана";
            rep.Comment = "Сделка с ID = " + String(DealID) + " уже привязана к другому ГС.";
        end;

        if (date(rs.Start) < date(rsG.Start))
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязана";
            rep.Comment = "Дата начала сделки меньше даты начала ГС.";
        end;

        if ((date(rs.Start) + Int(rs.Duration)) > (date(rsG.Start) + Int(rsG.Duration)))
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязана";
            rep.Comment = "Дата окончания сделки больше даты окончания ГС.";
        end;

        var BasisError = false;
        while (rsB.MoveNext)
            var deal_pd = MMFirstDoc(DL_IBCDOC, DealID, true);
            if ((rsB.PFI == deal_pd.GetLeg().PFI) and (rsB.Basis != deal_pd.GetLeg().Basis))
                BasisError = true;
                break;
            end;
        end;
        if (BasisError)
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязана";
            rep.Comment = "Базис расчета по сделке не соответствует базису расчета по ГС";
        end;

        if (rs.PartyID != rsG.PartyID)
            if (rsG.PartyGroup)
                var found = false;
                rsP = TRsbDataSet("select * from ddl_partylist_dbt plist "
                                  "inner join ddl_partygroup_dbt pgroup on plist.t_GroupID = pgroup.t_ID" + 
                                  " where pgroup.t_IsOpen = 'X' AND plist.t_GroupID = " + String(rsG.PartyGroup));
                while (rsP.MoveNext() and (not found))
                    if (rsP.PartyID == rs.PartyID)
                        found = true;
                    end;
                end;
                if (not found)
                    partyErr = 1;
                end;
            else
                partyErr = 1;
            end;
            
            if (partyErr)
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязана";
                rep.Comment = "Контрагент по сделке не совпадает с контрагентом по ГС или отсутствует в группе контрагентов по ГС.";
        end;
        end;

        if ((rs.DealStatus == DL_READIED)and
            (rsG.Status != 10 ))
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязана";
            rep.Comment = "Открытую сделку можно привязывать только к открытому ГС.";
        end;
    elif (GenAgrID < 0)// отвязка
        rs = TRsbDataSet("select * from ddl_tick_dbt tick where" +
                          " tick.t_DealID = " + String(DealID));

        if (rs.MoveNext)
            DealCode = rs.DealCode;

            if (rs.DealStatus == DL_CLOSED)
                rep = GenAgrRep;
                rep.NumDeal = rs.DealCode;
                rep.Result  = "не отвязана";
                rep.Comment = "Нельзя отвязать закрытую сделку от ГС.";
            end;
        else
            rep = GenAgrRep;
            rep.NumDeal = "";
            rep.Result  = "не отвязана";
            rep.Comment = "Сделка с ID = " + String(DealID) + " не найдена.";
        end;

        if (rs.GenAgrID == -1)
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не отвязана";
            rep.Comment = "Сделка с ID = " + String(DealID) + " не привязана к ГС.";
        end;

        rs = TRsbDataSet("select * from ddl_genagr_dbt gagr where" +
                          " gagr.t_GenAgrID = " + String(GenAgrID));

        if (rs.MoveNext)
            if (rs.Status == 20)
                rep = GenAgrRep;
                rep.NumDeal = rs.DealCode;
                rep.Result  = "не отвязана";
                rep.Comment = "Нельза отвязать сделку от закрытого ГС.";
            end;
        else
            // все равно отвяжем, если ГС не нашли
        end;
    else
        return 0;
    end;

    if (rep == NULL)
        cmd = "update ddl_tick_dbt tick set tick.t_GenAgrID = " + String(GenAgrID) + 
               " where tick.t_DealID = " + String(DealID);

        RsdCommand(cmd).Execute;

        rep = GenAgrRep;
        rep.NumDeal = DealCode;
        if (GenAgrID > 0)
            rep.Result  = "привязана";
            rep.Comment = "Сделка привязана к ГС.";
        else
            rep.Result  = "отвязана";
            rep.Comment = "Сделка отвязана от ГС.";
        end;
    else
        retVal = 1;
    end;

   TickToGenAgrRep[TickToGenAgrRep.size] = rep;

    return retVal;
END;

PRIVATE MACRO Отчет_Привязки_к_ГС(GenAgrID)
    var
        ReportFile = NULL, rs = NULL, idx = 0;

    file RepFile() txt write;

    if (TickToGenAgrRep.size == 0)
        ;//ничего не делаем
    elif (TickToGenAgrRep.size == 1)
        msgbox(TickToGenAgrRep[0].Comment);
    else
        open(RepFile, GetTxtFileName("mmgenagr"));

        if (GenAgrID > 0)// привязка
            rs = TRsbDataSet("select * from ddl_genagrview_dbt gagr where" +
                              " gagr.t_GenAgrID = " + String(GenAgrID));
            rs.MoveNext;

            insert(RepFile, "Протокол привязки сделок к ГС " + rs.Code + ", контрагент " + rs.Party);
        else // отвязка
            insert(RepFile, "Протокол отвязки сделок от ГС");
        end;

        insert(RepFile, "---------------------T----------------T--------------------------------------------------------------------------------┐");
        insert(RepFile, "│    Номер сделки    │   Результат    │                              Сообщение                                         │");
        insert(RepFile, "│                    │   процедуры    │                                                                                │");
        insert(RepFile, "+--------------------+----------------+--------------------------------------------------------------------------------+");




        while (idx < TickToGenAgrRep.size)
            insert(RepFile, "│" + 
                               String(TickToGenAgrRep[idx].NumDeal:20)  + "│" +  
                               String(TickToGenAgrRep[idx].Result:16)   + "│" +  
                               String(TickToGenAgrRep[idx].Comment:80)  + "│"  );
    
            idx = idx + 1;
        end;

        insert(RepFile, "L--------------------+----------------+---------------------------------------------------------------------------------");

        close(RepFile);
        ViewFile(RepFile);
    end;

    TickToGenAgrRep.size = 0;

    return 0;
END;

MACRO Вставить_Оцен_При_Перв_Призн(DealID)
    var
        cmd = "", rep = NULL, DealCode = "", deal_pd, oproper, stat;

    deal_pd = MMFirstDoc(DL_IBCDOC, DealID, true);
    oproper = TBFile("oproper", "R", 1);
    oproper.rec.DocKind    = deal_pd.GetTick().BOfficeKind;
    oproper.rec.DocumentID = UniID(deal_pd.tick, OBJTYPE_MMARKDEAL);
    if (oproper.GetEQ())
        rep = GenAgrRep;
        rep.NumDeal = deal_pd.GetTick().DealCode;

        var СумПред = deal_pd.GetRestAccount(tdr_mainrest, {curdate}, deal_pd.GetLeg().PFI, 1);
        IF (StepIsExecute(DealID, DL_IBCDOC, "Ц"))
            rep.Result  = "не выполнена";
            rep.Comment = "Оценка при первоначальном признании по сделке уже выполнялась.";
        ELIF ((СумПред == 0) and ((isSale(GetOperationGroup(deal_pd.GetTick().DealType)) and (not StepIsExecute(DealID, DL_IBCDOC, "G"))) or 
            (isBuy(GetOperationGroup(deal_pd.GetTick().DealType)) and (not StepIsExecute(DealID, DL_IBCDOC, "I")))))
            rep.Result  = "не выполнена";
            rep.Comment = "Первоначального признания по сделке еще не было, оценка не может быть произведена.";
        ELSE
            stat = Opr_InsertAndExecuteBranch(oproper.rec.ID_Operation, "А", "I", 0, NULL);
            IF (not stat)
                rep.Result  = "выполнена";
                rep.Comment = "Оценка при первоначальном признании выполнена.";
            ELSE
            	  rep.Result  = "не выполнена";
                rep.Comment = "Ошибка при выполнении оценки при первоначальном признании.";    
            END;            
        END;

        TickToGenAgrRep[TickToGenAgrRep.size] = rep;
    end;
    return 0;
END;

MACRO Отчет_Оцен_При_Перв_Призн()
    var
        ReportFile = NULL, rs = NULL, idx = 0;

    file RepFile() txt write;

    if (TickToGenAgrRep.size == 0)
        ;//ничего не делаем
    elif (TickToGenAgrRep.size == 1)
        msgbox(TickToGenAgrRep[0].Comment);
    else
        open(RepFile, GetTxtFileName("mmgenagr"));
        insert(RepFile, "Протокол выполнения операции");
        insert(RepFile, "---------------------T----------------T-----------------------------------------------------------------------------------┐");
        insert(RepFile, "│    Номер сделки    │   Результат    │                              Сообщение                                            │");
        insert(RepFile, "│                    │   процедуры    │                                                                                   │");
        insert(RepFile, "+--------------------+----------------+-----------------------------------------------------------------------------------+");

        while (idx < TickToGenAgrRep.size)
            insert(RepFile, "│" + 
                               String(TickToGenAgrRep[idx].NumDeal:20)  + "│" +  
                               String(TickToGenAgrRep[idx].Result:16)   + "│" +  
                               String(TickToGenAgrRep[idx].Comment:83)  + "│"  );
    
            idx = idx + 1;
        end;

        insert(RepFile, "L--------------------+----------------+------------------------------------------------------------------------------------");

        close(RepFile);
        ViewFile(RepFile);
    end;

    TickToGenAgrRep.size = 0;

    return 0;
END;