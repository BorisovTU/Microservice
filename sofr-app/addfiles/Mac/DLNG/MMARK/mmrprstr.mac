/*
$Name: mmrprstr.mac
$Module: МБ Кредиты
$Description: Печать отчета "Реестр сделок денежного рынка (МБК)" (RDTTMM010).

CREATED : 24.01.00 LA

MODIFICATIONS: 26.06.07 MBK Отчет переделан под Windows Reports с 
               использованием шаблонов Excel

NOTES:

*/
import BankInter, OprInter, DealsInter, FIInter, dlreport, RsbDataSet;
import "or_tpl_h.mac";

FILE party( party );
FILE dl_leg( dl_leg );
FILE fininstr( fininstr );

FILE wrk( mmrprstr ) normal; /* Временный файл для сохранения данных */

FILE pers( person );

var ObjTempl:CTemplateXLS = CTemplateXLS(); 
var Table,CurrName,SubItog2,SubItog3:object = NULL;
var lineadd=0;
var CurrNameCounter=0;

/* ------------------------------------------------------------------------- */

/* Параметры настройки отчета */
VAR
_weightingR = "V",           /* Способ взвешивания при усреднении ставки */
_weightingD = "V",           /* Способ взвешивания при усреднении срока */
_header     = TRUE,          /* ?Печатать заголовок отчета */
_statistics = TRUE,          /* ?Печатать статистику */
_aggregates = TRUE,          /* ?Печатать агрегаты */
_signatures = TRUE,          /* ?Печатать подписи */
_roll       = TRUE;          /* ?Печатать реестр */

/* GLOBALS */

/* Название временного файла, где будут сохранены данные */
VAR wrk_fileName = GetWorkFileName("mmrprstr");

/* Массивы для сохранения значений рассчитанных агрегатов
   (Индекс 0 будет соответствовать значению по размещению,
           1            -          значению по привлечению) */

array l_volm_1_1, /* Объем всего */
      l_volm_1_2, /* Максимальный объем */
      l_percnt_2, /* Проценты */
      l_rate_3_1, /* Средневзвешенная по объему ставка */
      l_rate_3_2, /* Средневзвешенная по объему и сроку ставка */
      l_rate_3_3, /* Минимальная ставка */
      l_rate_3_4, /* Максимальная ставка */
      l_term_4_1, /* Средневзвешенный по объему срок */
      l_term_4_2, /* Средневзвешенный по объему и ставке срок */
      l_dealnm_5; /* Количество сделок */

/* ------------------------------------------------------------------------- */
/* Процедура получает на входе запись по сделке и после обработки записывает
   полученные данные во временный файл (вызов из C) */
MACRO get_data( p_pointer )
 RECORD l_dealrec( dl_tick );

 var dlreslnk;

 setbuff( l_dealrec, p_pointer );
 clearrecord( wrk );
 wrk.Party      = l_dealrec.PartyID;
 wrk.XID        = l_dealrec.DealCode;
 wrk.DRollover  = l_dealrec.Flag1;

 if( isSale( getOperationGroup( l_dealrec.DealType ) ) )
  wrk.DDirection = "X"; /* размещение */
 else
  wrk.DDirection = " "; /* привлечение */
 end;

 /* Получить данные по траншу сделки */
 dl_leg.LegKind = LEG_KIND_DL_TICK;
 dl_leg.DealID = l_dealrec.DealID;
 dl_leg.LegID  = 1; /* Скрытый генеральный транш, по идее должно быть -1 */

 if( getEQ( dl_leg ) )
  wrk.FIID     = dl_leg.PFI;
  wrk.Principal = abs( dl_leg.Principal );
  wrk.Start     = dl_leg.Start;
  wrk.Maturity  = dl_leg.Maturity;
  wrk.Duration  = dl_leg.Duration;
  if (ПолучитьСтрокуЗначенияКурса(dl_leg.Price,dl_leg.Point,wrk.Rate)!=0)
    wrk.Rate      = dl_leg.Price;
  end;

/*  wrk.Rate      = dl_leg.Price;*/
  wrk.Interest  = abs( dl_leg.Cost );

   /* Получить данные по dlreslnk */
  dlreslnk = TRsbDataSet("select t_QualityCategory, t_ID " +
                          "from ddlreslnk_dbt " + 
                          "where t_PARENTID = " + l_dealrec.DealID +
                           " and t_TYPE = " + DL_RSRV_MMDEALS +                         
                          " order by t_ID DESC");//Упорядочиваем по убыванию, чтобы потом сразу взять запись с наибольшим ID
 
  if ( dlreslnk.MoveNext() )    
    wrk.QualityCategory = dlreslnk.QualityCategory;
  end;

  insert( wrk );
 end;
END;


/* ------------------------------------------------------------------------- */
/* Получить сокращенное наименование контрагента - для печати */
MACRO getPartyShortName( p_id )
 party.PartyID = p_id;

 if( not getEQ( party ) )
  return "";
 end;
 return party.ShortName;
END;


/* Печать заголовка отчета */
MACRO printHeaderExcel( p_briefcase, p_category, p_FIID, p_dealdate )
 if( not _header )
  return;
 end;
  var strhead = "БЭК-ОФИС БАНКА " +  {Name_Bank};
  ObjTempl.SetValue_NameCell("HeadStr", strhead);
  ObjTempl.SetValue_NameCell("TH_01" , p_briefcase);
  ObjTempl.SetValue_NameCell("TH_02" , p_category);
  ObjTempl.SetValue_NameCell("TH_03" , DL_getCCode( p_FIID ));
  ObjTempl.SetValue_NameCell("TH_04" , p_dealdate);
  
END;



/* Печать данных по текущей сделке (запись из временного файла) */
MACRO printTableLineExcel
 if( not _roll ) /* таблицу реестра не печатать */
  return;
 end;

 if (lineadd!=0)
   Table.AddStr();
 end;
 lineadd=1;

var
 l_dealR  = "",
 l_dealP  = "",
 l_income = "",
 l_outlay = "";

 if( wrk.DDirection == "X" )
  l_dealR  = string( wrk.Principal );
  l_income = string( wrk.Interest );
 else
  l_dealP  = string( wrk.Principal );
  l_outlay = string( wrk.Interest );
 end;

 if( _aggregates )
Table.SetValueCell("T_A1" , getPartyShortName( wrk.Party ));
Table.SetValueCell("T_A2" , wrk.XID);
Table.SetValueCell("T_A3" , l_dealR);
Table.SetValueCell("T_A4" , l_dealP);
Table.SetValueCell("T_A5" , wrk.Start);
Table.SetValueCell("T_A6" , wrk.Maturity);
Table.SetValueCell("T_A7" , wrk.Duration);
Table.SetValueCell("T_A8" , wrk.Rate);
Table.SetValueCell("T_A9" , l_income);
Table.SetValueCell("T_A10" , l_outlay);
Table.SetValueCell("T_A11" , wrk.QualityCategory);
Table.SetValueCell("T_A12" , wrk.DRollover:c);
 else
Table.SetValueCell("T_A1" , getPartyShortName( wrk.Party ));
Table.SetValueCell("T_A2" , wrk.XID);
Table.SetValueCell("T_A3" , l_dealR);
Table.SetValueCell("T_A4" , l_dealP);
Table.SetValueCell("T_A5" , wrk.Start);
Table.SetValueCell("T_A6" , wrk.Maturity);
Table.SetValueCell("T_A7" , wrk.Duration);
Table.SetValueCell("T_A8" , wrk.Rate);
Table.SetValueCell("T_A9" , "");
Table.SetValueCell("T_A10" , "");
Table.SetValueCell("T_A11" , wrk.QualityCategory);
Table.SetValueCell("T_A12" , wrk.DRollover:c);
 end;
 
END;



/* Печать итоговых строк таблицы */
MACRO printTableFooterExcel

var l_D, l_R;

 if( not _roll ) /* таблицу реестра не печатать */
  return;
 end;


 if( _weightingD == "V" )
  l_D = l_term_4_1;
 else
  l_D = l_term_4_2;
 end;

 if( _weightingR == "V" )
  l_R = l_rate_3_1;
 else
  l_R = l_rate_3_2;
 end;

  ObjTempl.SetValue_NameCell("TSI_1" , l_volm_1_1(0));
  ObjTempl.SetValue_NameCell("TSI_2" , l_D(0));
  ObjTempl.SetValue_NameCell("TSI_3" , l_R(0));
  ObjTempl.SetValue_NameCell("TSI_4" , l_percnt_2(0));
  ObjTempl.SetValue_NameCell("TSI_5" , l_volm_1_1(1));
  ObjTempl.SetValue_NameCell("TSI_6" , l_D(1));
  ObjTempl.SetValue_NameCell("TSI_7" , l_R(1));
  ObjTempl.SetValue_NameCell("TSI_8" , l_percnt_2(1));
END;




/* Печать статистики */
MACRO printStatisticsExcel( p_FIID )
 if( not _statistics )
  return;
 end;

if( ( not _header ) and
    ( not _aggregates ) and
    ( not _roll ) ) /* Если печатается только статистика */
  ObjTempl.SetValue_NameCell("T_S21" ,  DL_getCCode( p_FIID ));
end;

ObjTempl.SetValue_NameCell("T_S1" , l_volm_1_1(0));
ObjTempl.SetValue_NameCell("T_S2" , l_volm_1_1(1));
ObjTempl.SetValue_NameCell("T_S3" , l_volm_1_2(0));
ObjTempl.SetValue_NameCell("T_S4" , l_volm_1_2(1));
ObjTempl.SetValue_NameCell("T_S5" , l_percnt_2(0));
ObjTempl.SetValue_NameCell("T_S6" , l_percnt_2(1));
ObjTempl.SetValue_NameCell("T_S7" , l_rate_3_1(0));
ObjTempl.SetValue_NameCell("T_S8" , l_rate_3_1(1));
ObjTempl.SetValue_NameCell("T_S9" , l_rate_3_2(0));
ObjTempl.SetValue_NameCell("T_S10" , l_rate_3_2(1));
ObjTempl.SetValue_NameCell("T_S11" , l_rate_3_3(0));
ObjTempl.SetValue_NameCell("T_S12" , l_rate_3_3(1));
ObjTempl.SetValue_NameCell("T_S13" , l_rate_3_4(0));
ObjTempl.SetValue_NameCell("T_S14" , l_rate_3_4(1));
ObjTempl.SetValue_NameCell("T_S15" , l_term_4_1(0));
ObjTempl.SetValue_NameCell("T_S16" , l_term_4_1(1));
ObjTempl.SetValue_NameCell("T_S17" , l_term_4_2(0));
ObjTempl.SetValue_NameCell("T_S18" , l_term_4_2(1));
ObjTempl.SetValue_NameCell("T_S19" , l_dealnm_5(0));
ObjTempl.SetValue_NameCell("T_S20" , l_dealnm_5(1));
END;



/* Печать шаблона для подписей */
MACRO printSignaturesExcel
 if( not _signatures )
  return;
 end;

ObjTempl.SetValue_NameCell("T_Director" , {FIO_Boss});
ObjTempl.SetValue_NameCell("T_Oper" , {Name_Oper});

END;

/* Инициализировать массивы для записи значений агрегатов */
MACRO initArrays
 l_volm_1_1(0) = l_volm_1_1(1) = l_volm_1_2(0) = l_volm_1_2(1) = 0;
 l_percnt_2(0) = l_percnt_2(1) = l_rate_3_1(0) = l_rate_3_1(1) = 0;
 l_rate_3_2(0) = l_rate_3_2(1) = 0;
 l_rate_3_3(0) = l_rate_3_3(1) = null;
 l_rate_3_4(0) = l_rate_3_4(1) = l_term_4_1(0) = l_term_4_1(1) = 0;
 l_term_4_2(0) = l_term_4_2(1) = l_dealnm_5(0) = l_dealnm_5(1) = 0;
END;




/* Обработать текущую запись в рабочем файле */
MACRO processWrkRec

var l_ind;

 printTableLineExcel;

 if( wrk.DDirection == "X" ) /* размещение */
  l_ind = 0; /* Индекс в массивах для размещения */
 else
  l_ind = 1; /* Индекс в массивах для привлечения */
 end;

 /* Рассчитать агрегаты */
 if( ( not _aggregates ) and
     ( not _statistics ) )
  return;
 end;

 l_volm_1_1(l_ind) = l_volm_1_1(l_ind) + wrk.Principal;
 if( wrk.Principal > l_volm_1_2(l_ind) )
  l_volm_1_2(l_ind) = wrk.Principal;
 end;
 l_percnt_2(l_ind)  = l_percnt_2(l_ind) + wrk.Interest;
 l_rate_3_1(l_ind)  = l_rate_3_1(l_ind) + wrk.Rate * wrk.Principal;
 l_rate_3_2(l_ind)  = l_rate_3_2(l_ind) +
                      wrk.Rate * wrk.Principal * wrk.Duration;

 if( ( valType( l_rate_3_3(l_ind) ) == V_UNDEF ) or
     ( wrk.Rate < l_rate_3_3(l_ind) ) ) /* минимум */
  l_rate_3_3(l_ind) = wrk.Rate;
 end;
 if( wrk.Rate > l_rate_3_4(l_ind) ) /* максимум */
  l_rate_3_4(l_ind) = wrk.Rate;
 end;
 l_term_4_1(l_ind)  = l_term_4_1(l_ind) + wrk.Principal * wrk.Duration;
 l_dealnm_5(l_ind)  = l_dealnm_5(l_ind) + 1;
END;




/* Заключительные операции по отчету */
MACRO completeReport( p_FIID, i )

 /* Окончательный расчет агрегатов */
var l_ind = 0, bl_adr, list_name = "Лист_" + i, default_list = "Лист1";

 if( ( not _aggregates ) and
     ( not _statistics ) ) /* Если не надо считать */
  l_ind = 2;
 end;

 while( l_ind <= 1 )
  if( valType( l_rate_3_3(l_ind) ) == V_UNDEF )
   l_rate_3_3(l_ind) = 0;
  end;

  if( l_rate_3_1(l_ind) == 0 )
   l_term_4_2(l_ind) = 0;
  else
   l_term_4_2(l_ind) = l_rate_3_2(l_ind) / l_rate_3_1(l_ind);
  end;

  if( l_term_4_1(l_ind) == 0 )
   l_rate_3_2(l_ind) = 0;
  else
   l_rate_3_2(l_ind) = l_rate_3_2(l_ind) / l_term_4_1(l_ind);
  end;

  if( l_volm_1_1(l_ind) == 0 )
   l_rate_3_1(l_ind) = 0;
   l_term_4_1(l_ind) = 0;
  else
   l_rate_3_1(l_ind) = l_rate_3_1(l_ind) / l_volm_1_1(l_ind);
   l_term_4_1(l_ind) = l_term_4_1(l_ind) / l_volm_1_1(l_ind);
  end;

  l_ind = l_ind+1;
 end;

 /* Печать итоговых строк таблицы */

 if (_header)
    ObjTempl.CopyAllSheetInTotalBook(default_list,
                                     false,
                                     "T_Header",
                                     0,
                                     list_name);
 end;

 printTableFooterExcel;
 if (_roll)

    Table.AddStr();
    bl_adr = Table.GetTableRange();
						
    ObjTempl.CopyAllSheetInTotalBook(default_list,
                                     false,
                                     bl_adr,
                                     0,
                                     list_name);
    lineadd=0;

    ObjTempl.CopyAllSheetInTotalBook(default_list,
                                     false,
                                     "T_itog",
                                     0,
                                     list_name);
 end;

 /* Печать статистики */

 printStatisticsExcel( p_FIID );
 if (_statistics)
    ObjTempl.CopyAllSheetInTotalBook(default_list,
                                     false,
                                     "T_Stat",
                                     0,
                                     list_name);
 end;

 /* Печать шаблона для подписей */

 printSignaturesExcel;
 if (_signatures)
    ObjTempl.CopyAllSheetInTotalBook(default_list,
                                     false,
                                     "T_Sign",
                                     0,
                                     list_name);
  end;

  ObjTempl.Close();
END;

macro print_start(p_briefcase, p_category, l_FIID, p_dealdate)
   //открываем шаблон
   ObjTempl.OpenTemplate("MbkDealsReestr.xls",false);
   if (_roll)
     // регистрируем нашу таблицу
     Table=ObjTempl.RegisterTable("T_LineToCopy");
     // очищаем ее от данных
     Table.ClearCurRow();  
   end;
   //печатаем заголовок
   printHeaderExcel( p_briefcase, p_category, l_FIID, p_dealdate );  
end;


/* Печать отчета. Параметры передаются из фильтра и панели настройки */
MACRO printReport( p_briefcase, p_category, null, /* значения, установленные в фильтре */

                   p_dealdate,   p_weightingR, p_weightingD, p_header, /* значения, установленные в панели настройки отчета */
                   p_statistics, p_aggregates, p_signatures, p_roll )

var l_FIID;

 /* Установить параметры отчета по переданным значениям */
 _weightingR = p_weightingR;
 _weightingD = p_weightingD;
 _header     = p_header;
 _statistics = p_statistics;
 _aggregates = p_aggregates;
 _signatures = p_signatures;
 _roll       = p_roll;

 /* Предварительная обработка параметров для печати */
 if( trim( p_briefcase ) == "" )
  p_briefcase = "Все";
 end;

 if( trim( p_category ) == "" )
  p_category = "Все";
 end;

 /* Распечатать отчет */
 rewind( wrk );
 if( not next( wrk ) ) /* Если рабочий файл пуст, не будем париться :) */
  println( "Нет данных, удовлетворяющих заданной фильтрации." );
  close( wrk );
  delfile( wrk_filename );
  viewfile( setoutput( null ) );
  return TRUE;
 end;
 
 ObjTempl.CreateTotalBook();

 l_FIID = wrk.FIID;
 print_start(p_briefcase, p_category, l_FIID, p_dealdate);

 initArrays;

 rewind( wrk );
 var i = 1;
 while( next( wrk ) )
  if( l_FIID != wrk.FIID )
   completeReport( l_FIID, i );
   i = i + 1;

   l_FIID = wrk.FIID;
   print_start(p_briefcase, p_category, l_FIID, p_dealdate);
   initArrays;
  end;

  processWrkRec;
 end;

 completeReport( l_FIID, i );

 /* Окончание работы */
 ObjTempl.SaveTotalBook("Registry of transactions MBK", true);

 close( wrk );
 delfile( wrk_filename );
 setoutput( null );
 viewfile( wrk );
 exit(1);
 return true; /* Вывод печатной формы на экран (exit(0)), если не надо - return FALSE (exit(1)) */
END;

/* --------------------------- ENTRY POINT --------------------------------- */
/* Создать и открыть временный файл для записи собранных данных */
if( not create( wrk, wrk_filename ) )
 msgbox( "Невозможно создать рабочий файл ", wrk_filename );
 exit(1);
end;

if( not open( wrk, wrk_filename ) )
 msgbox( "Невозможно открыть рабочий файл ", wrk_filename );
 exit(1);
end;

/* EoF */
