/*
$Name:        mmdf_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Оценка при первоначальном признании"
*/
IMPORT MMarkInter, mmlibr, mmlib, "dlmisc.mac", mmcnst_j, "mmcateg.mac", mmcarry, mmlb_j;

PRIVATE MACRO Проводка(deal_pd, Sum, CatPayer, CatReceiver, Ground)
   VAR carry = MM_Carry;
   carry.FIIDPayer        = NATCUR;
   carry.FIIDReceiver     = NATCUR;
   carry.Chapter          = 1;
   carry.date_carry       = {curdate};
   carry.PrimaryDoc       = deal_pd;  
   carry.CatPayer         = CatPayer;
   carry.CatReceiver      = CatReceiver;
   carry.Ground           = Ground;
   carry.SumReceiver      = Sum;
   RETURN carry.carry();
END;

PRIVATE MACRO ОбработкаСделки(deal_pd)

   var СумПред = $0; 
   var НомСтоимость = $0;
   var Премия = $0;
   var MarketTest;
   var DataType, AsCategory;
   IF (IsCLAIMSACQ(GetOperationGroup(deal_pd.GetTick().DealType)))
      СумПред      = deal_pd.GetRestAccount(get_rights({curdate}),  {curdate}, deal_pd.GetLeg().PFI, 1);
      НомСтоимость = deal_pd.GetRestAccount(tdr_nomcost, {curdate}, deal_pd.GetLeg().PFI, 3);
      Премия       = deal_pd.GetRestAccount(get_prize_cm({curdate}), {curdate}, deal_pd.GetLeg().PFI, 1);
      IF (СумПред < НомСтоимость)
         MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal_pd.tick, OBJTYPE_MMARKDEAL), 17/*MN_DISCOUNT*/, {curdate}, Money(НомСтоимость - СумПред));
      ELIF (Премия > 0)
         MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal_pd.tick, OBJTYPE_MMARKDEAL), 16/*MN_PRIZE*/, {curdate}, Money(Премия));
      END;
   ELSE
      СумПред = deal_pd.GetRestAccount(tdr_rest(deal_pd.GetTick().FlagTypeDeal), {curdate}, deal_pd.GetLeg().PFI, 1);
   END;
   
   // получить категорию оценки
   GetMainObjAttr(NULL, OBJTYPE_MMARKDEAL, UniID(deal_pd.tick, OBJTYPE_MMARKDEAL), 5, AsCategory, NULL, NULL, {curdate});
   IF ((AsCategory == 1))
      MarketTest = 0;
   ELSE
      MarketTest = MM_GetMarketTest(deal_pd.GetTick().BofficeKind, deal_pd.GetTick().DealID, {curdate}, 0);
   END;
   
   IF (MarketTest == -1)
      mm_error("Ошибка при получении значения Тест на рыночность");
      RETURN 1;
   ELIF (MarketTest != 0)
      MM_ChangeMarketTest(deal_pd.GetTick().BofficeKind, deal_pd.GetTick().DealID, {curdate}, 2);
      var SS = MM_CalcSS(deal_pd.GetTick().BofficeKind, deal_pd.GetTick().DealID, {curdate}, 0);
      IF (SS == -1)
         mm_error("Ошибка при расчете Справедливой стоимости");
         RETURN 1;
      END;
      MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal_pd.tick, OBJTYPE_MMARKDEAL), 6/*MN_FAIR*/, {curdate}, Money(SS));

      DataType = GetAttrID(OBJTYPE_MMARKDEAL, deal_pd.GetTick().DealID, 8/*MM_DATA*/, {curdate});
      IF (DataType == 1)
         var Sum = Money(abs(ss - СумПред)), CatPayer, CatReceiver, Ground;
         IF (Sum > 0)
            IF (isSale(deal_pd.GetTick().DealGroup))
               IF (IsCLAIMSACQ(GetOperationGroup(deal_pd.GetTick().DealType)))
                  IF (ss > СумПред)
                     CatPayer    = get_adjcmP({curdate});
                     CatReceiver = tdr_plodR(deal_pd.GetTick().TypeDoc);
                     Ground      = "Корректировка, увеличивающая стоимость сделки приобретения прав требования";
                  ELIF (ss < СумПред)
                     CatPayer    = tdr_plodP(deal_pd.GetTick().TypeDoc);
                     CatReceiver = get_adjcmR({curdate});
                     Ground      = "Корректировка, уменьшающая стоимость сделки приобретения прав требования";
                  END;
               ELSE
                  IF (ss > СумПред)
                     CatPayer    = tdr_adjplR;
                     CatReceiver = tdr_plodR(deal_pd.GetTick().TypeDoc);
                     Ground      = "Корректировка, увеличивающая стоимость сделки размещения";
                  ELIF (ss < СумПред)
                     CatPayer    = tdr_plodP(deal_pd.GetTick().TypeDoc);
                     CatReceiver = tdr_adjplP;
                     Ground      = "Корректировка, уменьшающая стоимость сделки размещения";
                  END;
               END;
            ELSE
               IF (ss > СумПред)
                  CatPayer         = tdr_atodP(deal_pd.GetTick().TypeDoc);
                  CatReceiver      = tdr_adjatR;
                  Ground           = "Корректировка, увеличивающая стоимость сделки привлечения";
               ELIF (ss < СумПред)
                  CatPayer         = tdr_adjatP;
                  CatReceiver      = tdr_atodR(deal_pd.GetTick().TypeDoc);
                  Ground           = "Корректировка, уменьшающая стоимость сделки привлечения";
               END;
            END;

            ConvSum(Sum, Sum, {curdate}, deal_pd.GetLeg().PFI, NATCUR);
            IF (not Проводка(deal_pd, Money(Sum), CatPayer, CatReceiver, Ground))
               RETURN 1;
            END;
         END;
      ELSE
          MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal_pd.tick, OBJTYPE_MMARKDEAL), 5/*MN_UNRECORDED*/, {curdate}, Money(SS - СумПред));
      END;
   ELSE
      MM_ChangeMarketTest(deal_pd.GetTick().BofficeKind, deal_pd.GetTick().DealID, {curdate}, 1);
      MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal_pd.tick, OBJTYPE_MMARKDEAL), 6/*MN_FAIR*/, {curdate}, Money(СумПред));
   END;
   RETURN 0;
END;

PRIVATE MACRO ОбработкаКЛ(deal_pd)
   var СумПред = deal_pd.GetRestAccount(tdr_oblP, {curdate}, deal_pd.GetTick().LimitCur, 3);
   var MarketTest;
   var DataType, AsCategory;
   // получить категорию оценки
   GetMainObjAttr(NULL, OBJTYPE_MMARKDEAL, UniID(deal_pd.tick, OBJTYPE_MMARKDEAL), 5, AsCategory, NULL, NULL, {curdate});
   IF ((AsCategory == 1))
      MarketTest = 0;
   ELSE
      MarketTest = MM_GetMarketTest(deal_pd.GetTick().BofficeKind, deal_pd.GetTick().DealID, {curdate}, 0);
   END;
   
   IF (MarketTest == -1)
      mm_error("Ошибка при получении значения Тест на рыночность");
      RETURN 1;
   ELIF (MarketTest != 0)
      MM_ChangeMarketTest(deal_pd.GetTick().BofficeKind, deal_pd.GetTick().DealID, {curdate}, 2);

      var SS = MM_CalcSS(deal_pd.GetTick().BofficeKind, deal_pd.GetTick().DealID, {curdate}, 0);
      IF (SS == -1)
         mm_error("Ошибка при расчете Справедливой стоимости");
         RETURN 1;
      END;
      MM_ChangeNoteText(OBJTYPE_CREDITLN, UniID(deal_pd.tick, OBJTYPE_CREDITLN), 6/*KL_FAIR*/, {curdate}, Money(SS));

      DataType = GetAttrID(OBJTYPE_CREDITLN, deal_pd.GetTick().DealID, 8/*MM_DATA*/, {curdate});
      IF (DataType == 1)
         var Sum = Money(abs(СумПред - ss)), Ground;
         IF (ss < СумПред)
            Ground      = "Корректировка, уменьшающая стоимость договора КЛ (размещение)";
            ConvSum(Sum, Sum, {curdate}, deal_pd.GetTick().LimitCur, NATCUR);
            IF (not Проводка(deal_pd, Money(Sum), tdr_marginP, tdr_oblfp, Ground))
               RETURN 1;
            END;
            // сохранить значение в параметре договора КЛ ?Нераспределенная корректировка стоимости? tick.rec.UndistrCostAdj = Sum;
            MM_ChangeNoteText(OBJTYPE_CREDITLN, UniID(deal_pd.tick, OBJTYPE_CREDITLN), 15/*KL_UNCOSTADJ*/, {curdate}, Money(SS));
         END;
      ELSE
         MM_ChangeNoteText(OBJTYPE_CREDITLN, UniID(deal_pd.tick, OBJTYPE_CREDITLN), 5/*KL_UNRECORDED*/, {curdate}, Money(SS - СумПред));
      END;
   ELSE
      MM_ChangeMarketTest(deal_pd.GetTick().BofficeKind, deal_pd.GetTick().DealID, {curdate}, 1);
      MM_ChangeNoteText(OBJTYPE_CREDITLN, UniID(deal_pd.tick, OBJTYPE_CREDITLN), 6/*KL_FAIR*/, {curdate}, Money(СумПред));
   END;

   RETURN 0;
END;

PRIVATE MACRO CheckPaymStatusByDealID(DealID)
    var sel = RSDCommand("select * from dpmpaym_dbt where t_dockind = 102 and t_documentid = ? and t_purpose = ? and t_paymstatus = ?");
    sel.addParam("", RSDBP_IN, DealID);
    sel.addParam("", RSDBP_IN, PM_PURP_PRINC);      // Назначение платежа "Предоставление средств"
    sel.addParam("", RSDBP_IN, PM_READY_TO_SEND);   // Статус платежа "Готов к отправке"
    sel.execute();
    var payms = TRsbDataSet(sel);
    
    if (payms.MoveNext())
        return false;
    end;
    
    return true;
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
   record deal( dl_tick );
   SetBuff( deal, Document );
   IF (NOT MM_CheckLic())
      RETURN 1;
   END;

   var deal_pd = MMFirstDoc(Deal.BofficeKind, Deal.DealID, true);
   var EPS = MM_CalcEPS(Deal.BofficeKind, Deal.DealID, {curdate}, 0);
   IF (EPS == -1)
      mm_error("Ошибка при расчете ЭПС");
      RETURN 1;
   END;
   MM_ChangeEPS(Deal.BofficeKind, deal.DealID, {curdate}, EPS);

   var RPS = MM_CalcRPS(Deal.BofficeKind, Deal.DealID, {curdate}, 0);
   IF (RPS == -1)
      mm_error("Ошибка при расчете РПС");
      RETURN 1;
   END;
   MM_ChangeRPS(Deal.BofficeKind, deal.DealID, {curdate}, RPS);

   IF (Deal.BOfficeKind == DL_IBCDOC)
      if (CheckPaymStatusByDealID(Deal.DealID)) 
          return ОбработкаСделки(deal_pd);
      else
		  mm_error("Ошибка при выполнении операции");
		  return 1;
      end;
   ELIF ((Deal.BOfficeKind == DL_CREDITLN)and(isSale(Deal.DealGroup)))
      RETURN ОбработкаКЛ(deal_pd);
   ELSE
      mm_error("Ошибка при выполнении операции");
   END;

   RETURN 0;
END;

NameMacroFile = "mmdf_j.mac";