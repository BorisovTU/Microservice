/*
$Name:        msfo_step5.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "2.6  Учет корректировок на <нерыночность> на 01.01.2019"
*/
import mmlibr, MoCommon, mctplprm, RsbDataSet, RSD, mmcnst_j, mmlibr, PTInter, FIInter, globals, or_exl_h;

PRIVATE CONST FileExcel = "D:\\RS-Bank\\RS-BankV6.20.031.052.22\\WorkFile\\2.6.xlsx"; 
PRIVATE CONST NullCell = "Undefined";

var cdaoms = CDAOMSExcel(FileExcel, true);
VAR Sheet  = cdaoms.Book.worksheets.Item(1);
VAR cmd;
cmd = RsdCommand(RslDefCon, "DELETE FROM DMSFO_UPGRADE_DBT");
cmd.Execute();

VAR I = 4;
WHILE (1)
   VAR DealID = Sheet.Range("B" + I).Value;
   IF ((ValType(DealID) == V_UNDEF) OR (DealID == NullCell))
      BREAK;
   END;

   VAR SumCor= Sheet.Range("J" + I).Value; // Корректировка стоимости по МСФО (+ доходы, - расходы) 
   VAR Neo   = Sheet.Range("K" + I).Value; // Неучтенная отсроченная разница (+ положительная, - отрицательная) 
   VAR Amo   = Sheet.Range("L" + I).Value; // Амортизированная стоимость МСФО на 01.01.2019 
   VAR EPS   = Sheet.Range("M" + I).Value; // Эффективная процентная ставка в % годовых  на 01.01.2019
   VAR Cost  = Sheet.Range("N" + I).Value; // Справедливая стоимость на ДПП
   VAR Mark  = Sheet.Range("O" + I).Value; // Рыночная процентная ставка в % годовых на ДПП

   PrintLn(DealID,":",EPS,":",Cost,":",SumCor,":",Neo); 

   cmd = RsdCommand(RslDefCon,
   "INSERT INTO DMSFO_UPGRADE_DBT (T_DEALID, T_SUM1, T_SUM2, T_SUM3, T_SUM4, T_SUM5, T_SUM6) VALUES (?,?,?,?,?,?,?) ");
   cmd.AddParam("p1", RSDBP_IN); cmd.Value("p1") = int(DealID);
   cmd.AddParam("p2", RSDBP_IN); cmd.Value("p2") = money(SumCor);
   cmd.AddParam("p3", RSDBP_IN); cmd.Value("p3") = money(Neo);
   cmd.AddParam("p4", RSDBP_IN); cmd.Value("p4") = money(Amo);
   cmd.AddParam("p5", RSDBP_IN); cmd.Value("p5") = money(EPS);
   cmd.AddParam("p6", RSDBP_IN); cmd.Value("p6") = money(Cost);
   cmd.AddParam("p7", RSDBP_IN); cmd.Value("p7") = money(Mark);

   cmd.Execute();

   I = I + 1;
END;

cdaoms.Close();

VAR rs = RsdRecordset(
"SELECT d1.T_DEALID, d1.T_BOFFICEKIND, d1.T_DEALTYPE, d1.T_FLAGTYPEDEAL, d1.T_PARTYID, d1.T_DEALSTATUS, d2.T_DURATION, D3.T_ID_OPERATION "+
 " FROM DDL_TICK_DBT d1  "+
 " JOIN DDL_LEG_DBT  d2 ON (D1.T_DEALID     = D2.T_DEALID) "+
 " JOIN DOPROPER_DBT d3 ON (D3.T_DOCUMENTID = LPAD(d1.T_DEALID, 34, '0') AND D3.T_KIND_OPERATION = D1.T_DEALTYPE AND D3.T_DOCKIND = D1.T_BOFFICEKIND)  "+
" WHERE d1.T_BOFFICEKIND IN (102, 208) AND d1.T_DEALSTATUS = 10 AND D2.T_LEGKIND = 0 AND D2.T_LEGID = 1");
WHILE (rs.moveNext())
   Print("Сделка: ", rs.Value("T_DEALID"));
   PrintLn(":"+MM_DealExecuteStep(rs.Value("T_DEALID"), "d"));
END;