/*
$Name:        mmactacc1.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Актуализация счетов"
*/
/*-------------------------------------------------------------------------
  Имя файла        : mmactacc.mac

  Описание         : макрос выполнения шага "Актуализация счетов"

  Программист      : Куперин М.В.

  Создан           : 16.06.2008
-------------------------------------------------------------------------*/
IMPORT mmcateg, mccatacf, mmlib, globals, mmcnst_j, PaymInter, mmlibr, mctplprm;

VAR Категории = TArray,
     Роли      = TArray,
     FirstDoc = NULL;

private RECORD deal(dl_tick);

PRIVATE MACRO ПолучитьТекушийНомерСчет(indx, Дата, Счет)
  var FindAccount = "";

  FirstDoc.IsActAccount(Категории[indx], FindAccount, true, Роли[indx], NULL, Дата, NULL);


  SetParm( 2, FindAccount );

  if (FindAccount == "") 
      return false;
  end;
  
  return true;
END;


PRIVATE MACRO АктуализироватьСчет(indx, Дата, Счет)
  var FindAccount = "";

  FirstDoc.ActualAccount(Категории[indx], FindAccount, NULL, Роли[indx], NULL, Дата, NULL);

  SetParm( 2, FindAccount );

  if (FindAccount == "")
      return false;
  end;
  
  return true;
END;

PRIVATE MACRO ЗаполнитьМассивы(Категория, Роль)
      if (Роль == NULL)
          Роль = FirstDoc.GetFIRoleByCategory(Категория, FirstDoc.GetLeg().PFI)
      end;

      Категории[Категории.size] = Категория; 
      Роли[Роли.size]           = Роль;  
END;

PRIVATE MACRO FillArray(DealType)
  if (isSale(GetOperationGroup(DealType)))
/*------------------------Категории для сделок размещения------------------------------------*/
      ЗаполнитьМассивы(tdr_mainrest);
      ЗаполнитьМассивы(tdr_claimR);
      ЗаполнитьМассивы(tdr_percR(FirstDoc.GetTick().TypeDoc));
      ЗаполнитьМассивы(tdr_pennyR);
      ЗаполнитьМассивы("СЧЕТ_603");

      if (MC_GetKindSecurCredit(FirstDoc.GetTick().PartyID) != 1)
          ЗаполнитьМассивы(tdr_rez_mainrest);
          ЗаполнитьМассивы(tdr_rez_exprest);
          ЗаполнитьМассивы(tdr_RzrvP(FirstDoc.GetTick().TypeDoc), FIROLE_RVPS);
          ЗаполнитьМассивы(tdr_RzrvM(FirstDoc.GetTick().TypeDoc), FIROLE_RVPS);
          ЗаполнитьМассивы(tdr_exprestR);
          ЗаполнитьМассивы(tdr_exppercR);
          ЗаполнитьМассивы(tdr_perc_vb);
          ЗаполнитьМассивы(tdr_expperc_vb);
          ЗаполнитьМассивы("Задолж.% по спис ОД (ВБ)");
          ЗаполнитьМассивы("Задолж. По ОД спис (ВБ)");
          ЗаполнитьМассивы(tdr_rez_perc);
          ЗаполнитьМассивы(tdr_rez_expperc);
      end;
  else
/*------------------------Категории для сделок привлечения------------------------------------*/
      ЗаполнитьМассивы(tdr_mainrest);
      ЗаполнитьМассивы(tdr_exprestP);
      ЗаполнитьМассивы(tdr_exppercP);
      ЗаполнитьМассивы(tdr_claimP);
      ЗаполнитьМассивы(tdr_percP(FirstDoc.GetTick().TypeDoc));
      ЗаполнитьМассивы(tdr_pennyP);
      ЗаполнитьМассивы("СЧЕТ_603");
  end;
END;

PRIVATE MACRO UpdateAccInPayment(deal, Account, CatCode)
  var paym       = NULL,
      countpayms = 0,
      i          = 0;

  macro GetCountPercenPayms(DocKind, DocID, Purpose)
    var rs = RsdRecordset("select count(*) as cnt from dpmpaym_dbt where (t_dockind ="+DocKind+")and(t_documentid = "+DocID+")and(t_purpose = "+Purpose+")");
	rs.moveNext;
    return rs.value("cnt");
  end;

  if (isSale(GetOperationGroup(deal.GetTick().DealType))) /*для размещения*/
    if (CatCode == tdr_mainrest)
        paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC, 0);
        if (paym.PaymentID > 0)
             paym.SetPayerPI (PAYMENTS_GROUP_UNDEF, 
                              paym.PayerBankID,
                              paym.PayerBankCodeKind,
                              paym.PayerBankCode,
                              paym.PayerBankName,
                              paym.PayerBankCorrAcc,
                              deal.GetLeg().PFI,
                              1,
                              Account,
                              paym.Payer,
                              paym.PayerName,
                              paym.PayerINN,
                              paym.PayerCodeKind,
                              "");

            paym.FuturePayerAccount = Account;
        else
            mm_error("Не найдены платежи");
            return 2;
        end;

        paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC_RET, 0);
        if (paym.PaymentID > 0)
            paym.SetReceiverPI (PAYMENTS_GROUP_UNDEF, 
                                paym.ReceiverBankID,
                                paym.ReceiverBankCodeKind,
                                paym.ReceiverBankCode,
                                paym.ReceiverBankName,
                                paym.ReceiverBankCorrAcc,
                                deal.GetLeg().PFI,
                                1,
                                Account,
                                paym.Receiver,
                                paym.ReceiverName,
                                paym.ReceiverINN,
                                paym.ReceiverCodeKind,
                                "");

            paym.FutureReceiverAccount = Account;
        end;
    elif(CatCode == tdr_claimR)
		countpayms = GetCountPercenPayms(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PERCENT);
        while (i < countpayms)
            paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PERCENT, i+1);
            if (paym.PaymentID > 0)
                paym.SetReceiverPI (PAYMENTS_GROUP_UNDEF, 
                                    paym.ReceiverBankID,
                                    paym.ReceiverBankCodeKind,
                                    paym.ReceiverBankCode,
                                    paym.ReceiverBankName,
                                    paym.ReceiverBankCorrAcc,
                                    deal.GetLeg().PFI,
                                    1,
                                    Account,
                                    paym.Receiver,
                                    paym.ReceiverName,
                                    paym.ReceiverINN,
                                    paym.ReceiverCodeKind,
                                    "");

                paym.FutureReceiverAccount = Account;
            end;
			i = i + 1;
        end;  
    end;
  else
    if (CatCode == tdr_mainrest)
        paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC, 0);

        if (paym.PaymentID > 0)
            paym.SetReceiverPI (PAYMENTS_GROUP_UNDEF, 
                                paym.ReceiverBankID,
                                paym.ReceiverBankCodeKind,
                                paym.ReceiverBankCode,
                                paym.ReceiverBankName,
                                paym.ReceiverBankCorrAcc,
                                deal.GetLeg().PFI,
                                1,
                                Account,
                                paym.Receiver,
                                paym.ReceiverName,
                                paym.ReceiverINN,
                                paym.ReceiverCodeKind,
                                "");

            paym.FutureReceiverAccount = Account;
        else
            mm_error("Не найдены платежи");
            return 2;
        end;

        paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC_RET, 0);
        if (paym.PaymentID > 0)
            paym.SetPayerPI (PAYMENTS_GROUP_UNDEF, 
                             paym.PayerBankID,
                             paym.PayerBankCodeKind,
                             paym.PayerBankCode,
                             paym.PayerBankName,
                             paym.PayerBankCorrAcc,
                             deal.GetLeg().PFI,
                             1,
                             Account,
                             paym.Payer,
                             paym.PayerName,
                             paym.PayerINN,
                             paym.PayerCodeKind,
                             "");

            paym.FuturePayerAccount = Account;
        end;
    elif(CatCode == tdr_claimP)
        countpayms = GetCountPercenPayms(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PERCENT);
        while (i < countpayms)
            paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PERCENT, i+1);
            if (paym.PaymentID > 0)
                paym.SetPayerPI (PAYMENTS_GROUP_UNDEF, 
                                 paym.PayerBankID,
                                 paym.PayerBankCodeKind,
                                 paym.PayerBankCode,
                                 paym.PayerBankName,
                                 paym.PayerBankCorrAcc,
                                 deal.GetLeg().PFI,
                                 1,
                                 Account,
                                 paym.Payer,
                                 paym.PayerName,
                                 paym.PayerINN,
                                 paym.PayerCodeKind,
                                 "");

                paym.FuturePayerAccount = Account;
            end;
			i = i + 1;
        end;  
    end;
  end;
  return 0;
END;

MACRO ExecuteStep(Buffer, Document)
  var ExecDate = date(0, 0, 0),
      Account  = "",
      i        = 0, stat = 0;

  SetBuff( deal, Document );
  return 0;

  FirstDoc = MMFirstDoc(DL_IBCDOC, deal.DealID, true);
  
  if (CheckExistence(LEG_LegKind_DealID_LegID, LEG_KIND_DL_TICK, deal.DealID, 1))
      mm_error( "Не найден транш сделки ", deal.DealCode );
      return 1;
  end;
  
  FillArray(deal.DealType); 

  ExecDate = deal.DealDate;
  if (ExecDate > {curdate})
    ExecDate = {curdate};
  end;

  if (Категории.size > Роли.size)
      Роли.size = Категории.size;
  end;

  while (ValType(Категории(i)) == V_STRING)
      if (ПолучитьТекушийНомерСчет(i, ExecDate, Account) OR АктуализироватьСчет(i, ExecDate, Account))
          ;
      else
          mm_error("Не могу актуализировать счет для категории " + Категории(i));
          return 1;
      end;

      if ((Категории(i) == tdr_mainrest)OR 
          (Категории(i) == tdr_claimR) OR
          (Категории(i) == tdr_claimP))

          stat = UpdateAccInPayment(FirstDoc, Account, Категории(i));
          if (stat == 1)
              mm_error("Ошибка при обновлении платежей"); 
              return 1;
          elif (stat == 2)
              return 1;
          end;    
      end;

      i = i + 1;
  end;

  return 0;
END;
NameMacroFile = "mmactacc.mac";
