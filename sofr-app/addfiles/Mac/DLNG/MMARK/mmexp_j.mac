
/*
$Name:        mmexp_j.mac
$Module:      МБ Кредиты
$Description: Вынос на просрочку
*/
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : mmexp_j.mac
  Programmer  : Kuperin M. V.
  Description : Вынос на просрочку
  Comment     : 
└───────────────────────────────────────────────────────────────────────────*/
import mmcnst_j, mmcarry, mmlibr, mpclb, mmadd_j, mmpaymentcls, mctplprm, mmlb_j;

/* Инициализируются бэк-офисом */
VAR
ExpAmount,  /* Сумма выноса на просрочку */
PMPurpose,  /* Назначение выносимого на просрочку платежа */
PmID; 		/* ID выносимого на просрочку платежа */

/*Глобальные переменные*/
record deal ( dl_tick );
file   leg ( dl_leg );

private var ValueDate; // плановая дата платежа, выносимого на просрочку.
private var PaymentAmount;
private var FirstDoc;
private var ContrIsBankrupt = false;

const ПросрочкаОД        = 0,
      ПросрочкаПроцентов = 1,
      ПросрочкаКомиссий  = 2,
      РезервПоОД         = 3,
      РезервПоПроцентам  = 4,
      РезервПоКомиссиям  = 5,
      КорОД              = 6,
      КорПроценты        = 7,
      КорКомиссии        = 8;

/* Возвращает дату планового платежа (из объекта платежа - для шагов)*/
PRIVATE MACRO MM_GetPM_ValueDate(DocKind, DocID, Purpose, ValueDate:@date)
var Payment,
    DataSet = TRsbDataSet("SELECT MIN(pm.t_PaymentID) as PaymentID FROM dpmpaym_dbt pm " +
        " WHERE pm.t_DocKind = " + DocKind + " and pm.t_DocumentID = " + DocID +
            " and pm.t_Purpose = " + Purpose + " and pm.t_IsPlanPaym = 'X' and pm.t_PaymentID = "+PmID);
    if(DataSet.MoveNext())
       Payment = RsbPayment(DataSet.PaymentID);
       ValueDate = Payment.ValueDate; // нашли свой платеж
       PaymentAmount = Payment.FuturePayerAmount; 
       return true;
    end;

    return false;
END;
   
CLASS MM_EXP_CARRY
/*Переменные для проводок*/
var ДатаПроводки,
    ФИПлательщика,
    ФИПолучателя,
    КатегорияДебет,
    КатегорияКредит,
    РольФИПлательщика,
    РольФИПолучателя,
    СуммаПроводки,
    СуммаПолучателя,
    Глава,
    Основание;
END;

CLASS ПроводкиПоПросрочке(Режим)
  var ВыносНаПросрочку      = MM_EXP_CARRY,
      ЧтоВыносимНаПросрочку = Режим;

  macro Check()
      if (ValType(ЧтоВыносимНаПросрочку) == V_UNDEF)
          return false;
      end;
	  
      return true;
  end;
  
  macro Выполнить()
    var  bt = MM_Carry;

    if (not Check())
        return false;
    end;

    bt.date_carry       = ВыносНаПросрочку.ДатаПроводки;
    bt.PrimaryDoc       = MMFirstDoc(DL_IBCDOC, deal.DealID, true);
    bt.FIIDPayer        = ВыносНаПросрочку.ФИПлательщика;
    bt.FIIDReceiver     = ВыносНаПросрочку.ФИПолучателя;
    bt.CatPayer         = ВыносНаПросрочку.КатегорияДебет;
    bt.CatReceiver      = ВыносНаПросрочку.КатегорияКредит;
    bt.FiRolePayer      = ВыносНаПросрочку.РольФИПлательщика;
    bt.FiRoleReceiver   = ВыносНаПросрочку.РольФИПолучателя;
    bt.SumPayer         = ВыносНаПросрочку.СуммаПроводки;
    bt.SumReceiver      = ВыносНаПросрочку.СуммаПолучателя;
    bt.Chapter          = ВыносНаПросрочку.Глава;
    bt.Ground           = ВыносНаПросрочку.Основание;

    if (not bt.carry())
           return false; 
    end;

    MM_ByDefault(ВыносНаПросрочку.СуммаПроводки, ВыносНаПросрочку.СуммаПолучателя);
//    if (ЧтоВыносимНаПросрочку == ПросрочкаПроцентов)	
//        if (PrcSetCntSchedFactSumByID(GetCntSchedID(MM_GetPercContractID(PC_IBC_CON, (String(deal.DealID) +" " + "1")), 1, ВыносНаПросрочку.ДатаПроводки), money(ВыносНаПросрочку.СуммаПроводки), ВыносНаПросрочку.ДатаПроводки, date(0,0,0)) != 0)
//            mm_error("Ошибка при оплате процентов");
//            return false;
//        end;   
//    end;
	
    return true;
  end;
END;

/* Вынос на просрочку %% */
macro InsertDocPerc( IsSaleFlag )
   var КатДебет, КатКредит,
       СчетДебет, Сумма, СуммаПроцентов = $0, СуммаРезерва = $0, СуммаКор = $0, СуммаКРПрП = $0, res, ExpAmountNATCUR;

   var Проводки = ПроводкиПоПросрочке(ПросрочкаПроцентов); 
   var ПроводкиРезерв = ПроводкиПоПросрочке(РезервПоПроцентам); 
   var ПроводкиКор1   = ПроводкиПоПросрочке(КорПроценты); 
   var ПроводкиКор2   = ПроводкиПоПросрочке(КорПроценты); 


   if ((IsSaleFlag)and(MC_GetKindSecurCredit(FirstDoc.GetTick().PartyID ) == 1))
       return true;
   end;

   if( IsSaleFlag ) /* Сделка размещения */

        if (ExpAmount > 0)
            Проводки.ВыносНаПросрочку.ДатаПроводки      = ValueDate;
            Проводки.ВыносНаПросрочку.ФИПлательщика     = 
            Проводки.ВыносНаПросрочку.ФИПолучателя      = leg.PFI;
            if (ContrIsBankrupt)
                Проводки.ВыносНаПросрочку.ФИПлательщика = NATCUR;
            end;

            Проводки.ВыносНаПросрочку.КатегорияДебет    = tdr_exppercR;
            Проводки.ВыносНаПросрочку.КатегорияКредит   = tdr_claimR;
            Проводки.ВыносНаПросрочку.СуммаПолучателя   = ExpAmount;
            Проводки.ВыносНаПросрочку.Глава             = 1;
            Проводки.ВыносНаПросрочку.Основание         = "Вынос процентов на просрочку";
        end;
        ПолучитьОстатокСчета(FirstDoc, tdr_claimR, NULL, 1, СуммаПроцентов, ValueDate, leg.PFI);
        ConvSum( СуммаПроцентов, СуммаПроцентов, ValueDate, leg.PFI, NATCUR);
        ConvSum( ExpAmountNATCUR, ExpAmount, ValueDate, leg.PFI, NATCUR);
        ПолучитьОстатокСчета(FirstDoc, tdr_rez_perc, NULL, 1, СуммаРезерва, ValueDate, NATCUR);
        if (СуммаРезерва > 0)
            ПроводкиРезерв.ВыносНаПросрочку.ДатаПроводки      = ValueDate;
            ПроводкиРезерв.ВыносНаПросрочку.ФИПлательщика     = 
            ПроводкиРезерв.ВыносНаПросрочку.ФИПолучателя      = NATCUR;
            ПроводкиРезерв.ВыносНаПросрочку.КатегорияДебет    = tdr_rez_perc;
            ПроводкиРезерв.ВыносНаПросрочку.КатегорияКредит   = tdr_rez_expperc;
            ПроводкиРезерв.ВыносНаПросрочку.СуммаПроводки     = (СуммаРезерва*ExpAmountNATCUR)/СуммаПроцентов;
            ПроводкиРезерв.ВыносНаПросрочку.Глава             = 1;
            ПроводкиРезерв.ВыносНаПросрочку.Основание         = "Перенос суммы ранее созданного резерва по %% на соотвествующий счет учета";            
        end;

        ПолучитьОстатокСчетаУчетЗнака(FirstDoc, tdr_rezadjprcR, NULL, 1, СуммаКор, ValueDate, NATCUR);
        if (СуммаКор == 0)
            ПолучитьОстатокСчетаУчетЗнака(FirstDoc, tdr_rezadjprcP, NULL, 1, СуммаКор, ValueDate, NATCUR);
        end;
        if (СуммаПроцентов > 0)
            СуммаКРПрП = (СуммаКор*ExpAmount)/СуммаПроцентов;
        else
            СуммаКРПрП = 0;
        end;

        ПроводкиКор1.ВыносНаПросрочку.ДатаПроводки  = ПроводкиКор2.ВыносНаПросрочку.ДатаПроводки  = ValueDate;
        ПроводкиКор1.ВыносНаПросрочку.ФИПлательщика = ПроводкиКор2.ВыносНаПросрочку.ФИПлательщика =
        ПроводкиКор1.ВыносНаПросрочку.ФИПолучателя  = ПроводкиКор2.ВыносНаПросрочку.ФИПолучателя  = NATCUR;
        ПроводкиКор1.ВыносНаПросрочку.СуммаПроводки = ПроводкиКор2.ВыносНаПросрочку.СуммаПроводки = abs(СуммаКРПрП);
        ПроводкиКор1.ВыносНаПросрочку.Глава         = ПроводкиКор2.ВыносНаПросрочку.Глава         = 1;
        ПроводкиКор1.ВыносНаПросрочку.Основание     = ПроводкиКор2.ВыносНаПросрочку.Основание     = 
        "Перенос суммы ранее сформированной корректировки резерва по процентам на соотвествующий счет учета"; 

        if (СуммаКРПрП > 0)
            ПроводкиКор1.ВыносНаПросрочку.КатегорияДебет  = tdr_rezadjprcP;
            ПроводкиКор1.ВыносНаПросрочку.КатегорияКредит = tdr_rezadjR(FirstDoc.GetTick().TypeDoc);

            ПроводкиКор2.ВыносНаПросрочку.КатегорияДебет  = tdr_rezadjP(FirstDoc.GetTick().TypeDoc);
            ПроводкиКор2.ВыносНаПросрочку.КатегорияКредит = tdr_rezadjexpprcR;
        else
            ПроводкиКор1.ВыносНаПросрочку.КатегорияДебет  = tdr_rezadjexpprcP;
            ПроводкиКор1.ВыносНаПросрочку.КатегорияКредит = tdr_rezadjR(FirstDoc.GetTick().TypeDoc);

            ПроводкиКор2.ВыносНаПросрочку.КатегорияДебет  = tdr_rezadjP(FirstDoc.GetTick().TypeDoc);
            ПроводкиКор2.ВыносНаПросрочку.КатегорияКредит = tdr_rezadjprcR;
        end;

        res = (Проводки.Выполнить()) and (ПроводкиРезерв.Выполнить()) and (ПроводкиКор1.Выполнить()) and (ПроводкиКор2.Выполнить());
    else
        if (ExpAmount > 0)
            Проводки.ВыносНаПросрочку.ДатаПроводки      = ValueDate;
            Проводки.ВыносНаПросрочку.ФИПлательщика     =
            Проводки.ВыносНаПросрочку.ФИПолучателя      = leg.PFI;
            Проводки.ВыносНаПросрочку.КатегорияДебет    = tdr_claimP;
            Проводки.ВыносНаПросрочку.КатегорияКредит   = tdr_exppercP;
            Проводки.ВыносНаПросрочку.СуммаПроводки     = ExpAmount;
            Проводки.ВыносНаПросрочку.Глава             = 1;
            Проводки.ВыносНаПросрочку.Основание         = "Вынос процентов на просрочку";
        end;
        res = Проводки.Выполнить();
    end;

    return res;
end;


PRIVATE MACRO GetComisID()
    var DataSet = TRsbDataSet("SELECT t_SubPurpose FROM dpmpaym_dbt " +
        " WHERE t_PaymentID = "+PmID);
    if(not DataSet.MoveNext())
        return 0;
    end;
    return DataSet.SubPurpose;
END;

MACRO InsertDocComis(IsSaleFlag)
   var КатДебет, КатКредит,
       СчетДебет, Сумма, СуммаРезерва = $0, СуммаКор = $0, СуммаКРПрК = $0, СуммаК = $0,
       res, IDКомиссии, ТипКомиссии, inccalcs, othinc, DataSet, FIRole;

    if (ExpAmount == 0)
        return false;
    end;

    var Проводки = ПроводкиПоПросрочке(ПросрочкаКомиссий); 
    var ПроводкиРезерв = ПроводкиПоПросрочке(РезервПоКомиссиям); 
    var ПроводкиКор1   = ПроводкиПоПросрочке(КорКомиссии); 
    var ПроводкиКор2   = ПроводкиПоПросрочке(КорКомиссии); 


    IDКомиссии = GetComisID();
    DataSet = TRsbDataSet("SELECT t_type FROM dmmcomiss_dbt " +
        " INNER JOIN ddlcomis_dbt ON ddlcomis_dbt.t_feetype = dmmcomiss_dbt.t_feetype AND ddlcomis_dbt.t_comnumber = dmmcomiss_dbt.t_number " +
        " WHERE ddlcomis_dbt.t_ID = "+IDКомиссии);
    if(not DataSet.MoveNext())
        return false;
    end;
    ТипКомиссии = DataSet.Type;
    FIRole = 10000 + IDКомиссии;

    //РольФИПолучателя
    Проводки.ВыносНаПросрочку.ДатаПроводки      = ValueDate;
    Проводки.ВыносНаПросрочку.ФИПлательщика     = 
    Проводки.ВыносНаПросрочку.ФИПолучателя      = leg.PFI;
    if (ContrIsBankrupt and IsSaleFlag)
      Проводки.ВыносНаПросрочку.ФИПлательщика = NATCUR;
    end;
    Проводки.ВыносНаПросрочку.СуммаПолучателя   = ExpAmount;
    Проводки.ВыносНаПросрочку.Глава             = 1;
    Проводки.ВыносНаПросрочку.РольФИПлательщика = 
    Проводки.ВыносНаПросрочку.РольФИПолучателя  = FIRole;
    Проводки.ВыносНаПросрочку.КатегорияКредит   = tdr_inccalcs;
    if (ТипКомиссии == 1) // процентная
        Проводки.ВыносНаПросрочку.КатегорияДебет    = tdr_pccomreq;
        Проводки.ВыносНаПросрочку.Основание         = "Вынос процентной комиссии на просрочку";
    else
        Проводки.ВыносНаПросрочку.КатегорияДебет    = tdr_opcomreq;
        Проводки.ВыносНаПросрочку.Основание         = "Вынос операционной комиссии на просрочку";
    end;
    
    inccalcs = abs(FirstDoc.GetRestAccount(tdr_inccalcs, ValueDate, leg.PFI, 1, FIRole));
    othinc   = abs(FirstDoc.GetRestAccount(tdr_othinc,   ValueDate, leg.PFI, 1, FIRole));
    СуммаК   = abs(inccalcs - othinc);
    ConvSum( СуммаК, СуммаК, ValueDate, leg.PFI, NATCUR);

    if (СуммаК == 0)
        return Проводки.Выполнить();
    end;

    СуммаРезерва = FirstDoc.GetRestAccount(tdr_rezcom, ValueDate, NATCUR, 1, FIRole);

    if (СуммаРезерва > 0)
        ПроводкиРезерв.ВыносНаПросрочку.ДатаПроводки      = ValueDate;
        ПроводкиРезерв.ВыносНаПросрочку.ФИПлательщика     = 
        ПроводкиРезерв.ВыносНаПросрочку.ФИПолучателя      = NATCUR;
        ПроводкиРезерв.ВыносНаПросрочку.СуммаПроводки     = (СуммаРезерва*ExpAmount)/СуммаК;
        ПроводкиРезерв.ВыносНаПросрочку.Глава             = 1;
        ПроводкиРезерв.ВыносНаПросрочку.РольФИПлательщика = 
        ПроводкиРезерв.ВыносНаПросрочку.РольФИПолучателя  = FIRole;
        ПроводкиРезерв.ВыносНаПросрочку.КатегорияДебет    = tdr_rezcom;
        if (ТипКомиссии == 1) // процентная
            ПроводкиРезерв.ВыносНаПросрочку.КатегорияКредит   = tdr_rezexppccom;
            ПроводкиРезерв.ВыносНаПросрочку.Основание         = "Перенос суммы ранее созданного резерва по процентной комиссии на соотвествующий счет учета";   
        else
            ПроводкиРезерв.ВыносНаПросрочку.КатегорияКредит   = tdr_rezexpopcom;
            ПроводкиРезерв.ВыносНаПросрочку.Основание         = "Перенос суммы ранее созданного резерва по операционной комиссии на соотвествующий счет учета"; 
        end;
    end;
    СуммаКор = FirstDoc.GetRestAccount(tdr_rezadjcomR, ValueDate, NATCUR, 1, FIRole);
    if (СуммаКор == 0)
        СуммаКор = FirstDoc.GetRestAccount(tdr_rezadjcomP, ValueDate, NATCUR, 1, FIRole);
    end;
    СуммаКРПрК = (СуммаКор*ExpAmount)/СуммаК;

    ПроводкиКор1.ВыносНаПросрочку.ДатаПроводки  = ПроводкиКор2.ВыносНаПросрочку.ДатаПроводки  = ValueDate;
    ПроводкиКор1.ВыносНаПросрочку.ФИПлательщика = ПроводкиКор2.ВыносНаПросрочку.ФИПлательщика =
    ПроводкиКор1.ВыносНаПросрочку.ФИПолучателя  = ПроводкиКор2.ВыносНаПросрочку.ФИПолучателя  = NATCUR;
    ПроводкиКор1.ВыносНаПросрочку.СуммаПроводки = ПроводкиКор2.ВыносНаПросрочку.СуммаПроводки = abs(СуммаКРПрК);
    ПроводкиКор1.ВыносНаПросрочку.Глава         = ПроводкиКор2.ВыносНаПросрочку.Глава         = 1;

    if (ТипКомиссии == 1) // процентная
        ПроводкиКор1.ВыносНаПросрочку.Основание = ПроводкиКор2.ВыносНаПросрочку.Основание = 
        "Перенос суммы ранее сформированной корректировки резерва по процентной комиссии на соотвествующий счет учета"; 
        if (СуммаКРПрК > 0)
            ПроводкиКор1.ВыносНаПросрочку.КатегорияДебет    = tdr_rezadjcomP;
            ПроводкиКор1.ВыносНаПросрочку.РольФИПлательщика = FIRole;
            ПроводкиКор1.ВыносНаПросрочку.КатегорияКредит   = tdr_rezadjcostR;

            ПроводкиКор2.ВыносНаПросрочку.КатегорияДебет    = tdr_rezadjcostP;
            ПроводкиКор2.ВыносНаПросрочку.КатегорияКредит   = tdr_rezadjpccomR;
            ПроводкиКор2.ВыносНаПросрочку.РольФИПолучателя  = FIRole;
        else
            ПроводкиКор1.ВыносНаПросрочку.КатегорияДебет    = tdr_rezadjpccomP;
            ПроводкиКор1.ВыносНаПросрочку.РольФИПлательщика = FIRole;
            ПроводкиКор1.ВыносНаПросрочку.КатегорияКредит   = tdr_rezadjcostR;

            ПроводкиКор2.ВыносНаПросрочку.КатегорияДебет    = tdr_rezadjcostP;
            ПроводкиКор2.ВыносНаПросрочку.КатегорияКредит   = tdr_rezadjcomR;
            ПроводкиКор2.ВыносНаПросрочку.РольФИПолучателя  = FIRole;
        end;
    else
        ПроводкиКор1.ВыносНаПросрочку.Основание = ПроводкиКор2.ВыносНаПросрочку.Основание = 
        "Перенос суммы ранее сформированной корректировки резерва по операционной комиссии на соотвествующий счет учета"; 
        if (СуммаКРПрК > 0)
            ПроводкиКор1.ВыносНаПросрочку.КатегорияДебет    = tdr_rezadjcomP;
            ПроводкиКор1.ВыносНаПросрочку.РольФИПлательщика = FIRole;
            ПроводкиКор1.ВыносНаПросрочку.КатегорияКредит   = tdr_rezadjcostR;

            ПроводкиКор2.ВыносНаПросрочку.КатегорияДебет    = tdr_rezadjcostP;
            ПроводкиКор2.ВыносНаПросрочку.КатегорияКредит   = tdr_rezadjopcomR;
            ПроводкиКор2.ВыносНаПросрочку.РольФИПолучателя  = FIRole;
        else
            ПроводкиКор1.ВыносНаПросрочку.КатегорияДебет    = tdr_rezadjopcomP;
            ПроводкиКор1.ВыносНаПросрочку.РольФИПлательщика = FIRole;
            ПроводкиКор1.ВыносНаПросрочку.КатегорияКредит   = tdr_rezadjcostR;

            ПроводкиКор2.ВыносНаПросрочку.КатегорияДебет    = tdr_rezadjcostP;
            ПроводкиКор2.ВыносНаПросрочку.КатегорияКредит   = tdr_rezadjcomR;
            ПроводкиКор2.ВыносНаПросрочку.РольФИПолучателя  = FIRole;
        end;
    end;

    res = (Проводки.Выполнить()) and (ПроводкиРезерв.Выполнить()) and (ПроводкиКор1.Выполнить()) and (ПроводкиКор2.Выполнить());

    return res;
END;

/* Вынос на просрочку ОД */
macro InsertDocPrinc( IsSaleFlag)
  var Кат1, Кат2, tdr_rez_main, Счет, Сумма, СуммаОД = $0, Сод1 = $0, Сод2 = $0, СуммаРезерва = $0, СуммаКор = $0, СуммаКРПр = $0, res, ПросрочПТ, 
      Сном = FirstDoc.GetLeg().Principal, Соном,
      Проводки = ПроводкиПоПросрочке(ПросрочкаОД),
      ПроводкиРезерв = ПроводкиПоПросрочке(РезервПоОД),
      ПроводкиКор1   = ПроводкиПоПросрочке(КорОД),
      ПроводкиКор2   = ПроводкиПоПросрочке(КорОД);

  if ((IsSaleFlag)and(MC_GetKindSecurCredit(FirstDoc.GetTick().PartyID ) == 1))
      return true;
  end;
  if (IsCLAIMSACQ(GetOperationGroup(FirstDoc.GetTick().DealType)))
    Кат2 = get_rights(ValueDate);
    tdr_rez_main = tdr_rmainrest_cm;

    ПолучитьОстатокСчета(FirstDoc, get_rights(ValueDate), NULL, 1, Сод1, ValueDate, leg.PFI);
    ПолучитьОстатокСчета(FirstDoc, tdr_exprestR, NULL, 1, Сод2, ValueDate, leg.PFI);

    Соном = FirstDoc.GetRestAccount(tdr_nomcost, ValueDate, leg.PFI, 3);

    СуммаОД = Сод1 + Сод2;
    ПросрочПТ = (СуммаОД*ExpAmount)/Соном;
  end;

  if( IsSaleFlag ) /* Сделка размещения */
      Кат1 = tdr_exprestR;
      if (not IsCLAIMSACQ(GetOperationGroup(FirstDoc.GetTick().DealType)))
        if (FirstDoc.GetTick().dealtype == 12335)
           Кат2 = tdr_mainrest_tf;
        else
           Кат2 = tdr_rest(FirstDoc.GetTick().FlagTypeDeal);
        end;
        tdr_rez_main = tdr_rez_mainrest;
        ПолучитьОстатокСчета(FirstDoc, Кат2, NULL, 1, СуммаОД, ValueDate, leg.PFI);
        ConvSum( СуммаОД, СуммаОД, ValueDate, leg.PFI, NATCUR);
      end;
      ПолучитьОстатокСчета(FirstDoc, tdr_rez_main, NULL, 1, СуммаРезерва, ValueDate, NATCUR);

      if (СуммаРезерва > 0)
        ПроводкиРезерв.ВыносНаПросрочку.ДатаПроводки      = ValueDate;
        ПроводкиРезерв.ВыносНаПросрочку.ФИПлательщика     = 
        ПроводкиРезерв.ВыносНаПросрочку.ФИПолучателя      = NATCUR;
        ПроводкиРезерв.ВыносНаПросрочку.Глава             = 1;
        if (not IsCLAIMSACQ(GetOperationGroup(FirstDoc.GetTick().DealType)))
            ПроводкиРезерв.ВыносНаПросрочку.КатегорияДебет    = tdr_rez_main;
            ПроводкиРезерв.ВыносНаПросрочку.КатегорияКредит   = tdr_rez_exprest;
            ПроводкиРезерв.ВыносНаПросрочку.СуммаПроводки     = (СуммаРезерва*ExpAmount)/СуммаОД;
            ПроводкиРезерв.ВыносНаПросрочку.Основание         = "Перенос суммы ранее созданного резерва по ОД на соотвествующий счет учета"; 
        else
            ПроводкиРезерв.ВыносНаПросрочку.КатегорияДебет    = tdr_rmainrest_cm;
            ПроводкиРезерв.ВыносНаПросрочку.КатегорияКредит   = tdr_rez_exprest;
            ПроводкиРезерв.ВыносНаПросрочку.СуммаПроводки     = (СуммаРезерва*ПросрочПТ)/СуммаОД;
            ПроводкиРезерв.ВыносНаПросрочку.Основание         = "Перенос суммы ранее созданного резерва по приобретенным правам требования на соотвествующий счет учета"; 
        end;           
      end;
      
      if (not IsCLAIMSACQ(GetOperationGroup(FirstDoc.GetTick().DealType)))
        ПолучитьОстатокСчетаУчетЗнака(FirstDoc, tdr_rezadjodR, NULL, 1, СуммаКор, ValueDate, NATCUR);
        if (СуммаКор == 0)
            ПолучитьОстатокСчетаУчетЗнака(FirstDoc, tdr_rezadjodP, NULL, 1, СуммаКор, ValueDate, NATCUR);
        end;
      else
        ПолучитьОстатокСчетаУчетЗнака(FirstDoc, get_rezadjod_cmR(ValueDate), NULL, 1, СуммаКор, ValueDate, NATCUR);
        if (СуммаКор == 0)
            ПолучитьОстатокСчетаУчетЗнака(FirstDoc, get_rezadjod_cmP(ValueDate), NULL, 1, СуммаКор, ValueDate, NATCUR);
        end;
      end;

      ПроводкиКор1.ВыносНаПросрочку.ДатаПроводки  = ПроводкиКор2.ВыносНаПросрочку.ДатаПроводки  = ValueDate;
      ПроводкиКор1.ВыносНаПросрочку.ФИПлательщика = ПроводкиКор2.ВыносНаПросрочку.ФИПлательщика =
      ПроводкиКор1.ВыносНаПросрочку.ФИПолучателя  = ПроводкиКор2.ВыносНаПросрочку.ФИПолучателя  = NATCUR;
      ПроводкиКор1.ВыносНаПросрочку.Глава         = ПроводкиКор2.ВыносНаПросрочку.Глава         = 1;
      if (not IsCLAIMSACQ(GetOperationGroup(FirstDoc.GetTick().DealType)))
        СуммаКРПр = (СуммаКор*ExpAmount)/СуммаОД;
        ПроводкиКор1.ВыносНаПросрочку.СуммаПроводки = ПроводкиКор2.ВыносНаПросрочку.СуммаПроводки = abs(СуммаКРПр);
        ПроводкиКор1.ВыносНаПросрочку.Основание     = ПроводкиКор2.ВыносНаПросрочку.Основание     = 
        "Перенос суммы ранее сформированной корректировки резерва по ОД на соотвествующий счет учета"; 
      else
        СуммаКРПр = (СуммаКор*ПросрочПТ)/СуммаОД;
        ПроводкиКор1.ВыносНаПросрочку.СуммаПроводки = ПроводкиКор2.ВыносНаПросрочку.СуммаПроводки = abs(СуммаКРПр);
        ПроводкиКор1.ВыносНаПросрочку.Основание     = ПроводкиКор2.ВыносНаПросрочку.Основание     = 
        "Перенос суммы ранее сформированной корректировки резерва по приобретенным правам требования на соотвествующий счет учета"; 
      end;

      if (СуммаКРПр > 0)
        if (not IsCLAIMSACQ(GetOperationGroup(FirstDoc.GetTick().DealType)))
            ПроводкиКор1.ВыносНаПросрочку.КатегорияДебет  = tdr_rezadjodP;
        else
            ПроводкиКор1.ВыносНаПросрочку.КатегорияДебет  = get_rezadjod_cmP(ValueDate);
        end;
        ПроводкиКор1.ВыносНаПросрочку.КатегорияКредит = tdr_rezadjR(FirstDoc.GetTick().TypeDoc);

        ПроводкиКор2.ВыносНаПросрочку.КатегорияДебет  = tdr_rezadjP(FirstDoc.GetTick().TypeDoc);
        ПроводкиКор2.ВыносНаПросрочку.КатегорияКредит = tdr_rezadjexpodR;
      else
        ПроводкиКор1.ВыносНаПросрочку.КатегорияДебет  = tdr_rezadjexpodP;
        ПроводкиКор1.ВыносНаПросрочку.КатегорияКредит = tdr_rezadjR(FirstDoc.GetTick().TypeDoc);

        ПроводкиКор2.ВыносНаПросрочку.КатегорияДебет  = tdr_rezadjP(FirstDoc.GetTick().TypeDoc);
        if (not IsCLAIMSACQ(GetOperationGroup(FirstDoc.GetTick().DealType)))
            ПроводкиКор2.ВыносНаПросрочку.КатегорияКредит = tdr_rezadjodR;
        else
            ПроводкиКор2.ВыносНаПросрочку.КатегорияКредит = get_rezadjod_cmR(ValueDate);
        end;
      end;
  else
      Кат1 = tdr_rest(FirstDoc.GetTick().FlagTypeDeal);
      Кат2 = tdr_exprestP;
  end;

  Проводки.ВыносНаПросрочку.ФИПлательщика     =
  Проводки.ВыносНаПросрочку.ФИПолучателя      = leg.PFI;
  if (ContrIsBankrupt and IsSaleFlag)
      Проводки.ВыносНаПросрочку.ФИПлательщика = NATCUR;
  end;
  Проводки.ВыносНаПросрочку.КатегорияДебет    = Кат1;
  Проводки.ВыносНаПросрочку.КатегорияКредит   = Кат2;
  Проводки.ВыносНаПросрочку.ДатаПроводки      = ValueDate;
  Проводки.ВыносНаПросрочку.Глава             = 1;
  if (not IsCLAIMSACQ(GetOperationGroup(FirstDoc.GetTick().DealType)))
    Проводки.ВыносНаПросрочку.СуммаПолучателя   = ExpAmount;
    Проводки.ВыносНаПросрочку.Основание         = "Вынос ОД на просрочку";
  else
    Проводки.ВыносНаПросрочку.СуммаПолучателя   = ПросрочПТ;
    Проводки.ВыносНаПросрочку.Основание         = "Вынос приобретенных прав требования на просрочку";
  end;

  if( IsSaleFlag ) /* Сделка размещения */
      res = (Проводки.Выполнить()) and (ПроводкиРезерв.Выполнить()) and (ПроводкиКор1.Выполнить()) and (ПроводкиКор2.Выполнить());
  else
      res = Проводки.Выполнить();
  end;

  return res;
end;
private macro CreatePaym(DealID, purp, IsSaleFlag)
    var stat = 0, ExpPaym = NULL, PmPaym = NULL, Purpose = NULL,
        pml = TRecHandler("pmlink.dbt"), Ground = "", Счет = "", MMExpPaym, BFIID, ExpAmountB = $0;

    if (ContrIsBankrupt)
        BFIID = NATCUR;
    else
        BFIID = leg.PFI;
    end;
    ConvSum( ExpAmountB, ExpAmount, {curdate}, leg.PFI, BFIID);

    PmPaym = RsbPayment(PmID);
    if (purp == PM_PURP_PRINC_RET)
        Purpose = PM_PURP_OVD_PRINC;
        Ground = "Просроченный ОД по сделке";

        if ((IsSaleFlag)and(MC_GetKindSecurCredit(FirstDoc.GetTick().PartyID ) == 1))
            if (PmPaym.Payer == {OurBank})
                Счет = PmPaym.FuturePayerAccount;
            else
                Счет = PmPaym.FutureReceiverAccount;
            end;
        else
            if (PmPaym.Payer == {OurBank})
                FirstDoc.FindNumberAccount("Обяз. с н.с.", Счет, NULL, NULL, leg.PFI);
            else
                if (ContrIsBankrupt)
                    FirstDoc.FindNumberAccount("Треб. с н.с.", Счет, NULL, NULL, NATCUR);
                else
                    FirstDoc.FindNumberAccount("Треб. с н.с.", Счет, NULL, NULL, leg.PFI);
                end;
            end;
        end;
    elif (purp == PM_PURP_PERCENT)
        Purpose = PM_PURP_OVD_PERC;
        Ground = "Просроченные проценты по сделке";
    
        if ((IsSaleFlag)and(MC_GetKindSecurCredit(FirstDoc.GetTick().PartyID ) == 1))
            if (PmPaym.Payer == {OurBank})
                Счет = PmPaym.FuturePayerAccount;
            else
                Счет = PmPaym.FutureReceiverAccount;
            end;
        else
            if (PmPaym.Payer == {OurBank})
                FirstDoc.FindNumberAccount("-% с н.с.", Счет, NULL, NULL, leg.PFI);
            else
                if (ContrIsBankrupt)
                    FirstDoc.FindNumberAccount("+% с н.с.", Счет, NULL, NULL, NATCUR);
                else
                    FirstDoc.FindNumberAccount("+% с н.с.", Счет, NULL, NULL, leg.PFI);
                end;
            end;
        end;
    elif (purp == PM_PURP_COMRECEIVING)
        Purpose = PM_PURP_OVD_COMRECEIVING;
        Ground = "Просроченные комиссии по сделке";

        var ТипКомиссии = 2, Кат;
        var DataSet = TRsbDataSet("SELECT t_type FROM dmmcomiss_dbt " +
        " INNER JOIN ddlcomis_dbt ON ddlcomis_dbt.t_feetype = dmmcomiss_dbt.t_feetype AND ddlcomis_dbt.t_comnumber = dmmcomiss_dbt.t_number " +
        " WHERE ddlcomis_dbt.t_ID = "+GetComisID());
        if(DataSet.MoveNext())
            ТипКомиссии = DataSet.Type;
        end;
        if (ТипКомиссии == 1) // процентная
            Кат = "Треб. по ПрК с н.с.";
        else
            Кат = "Треб. по ОпК с н.с.";
        end;

        FirstDoc.FindNumberAccount(Кат, Счет, true, 10000 + GetComisID(), BFIID);
    end;

    ExpPaym = RsbPayment(DL_IBCDOC, DealID, Purpose, 0);
    if ((ExpPaym.PaymentID <= 0)or(ExpPaym.PaymStatus == PM_FINISHED))
        MMExpPaym = MM_RsbPayment(MMPAYM_CREATEBYPAYMOBJ, PmPaym, 0, ExpAmountB, ValueDate);
        ExpPaym = MMExpPaym.Payment;
        MMExpPaym.Payment.SubPurpose = PmPaym.SubPurpose;
        MMExpPaym.Payment.Ground = Ground;
        MMExpPaym.Payment.Purpose = Purpose;
        MMExpPaym.Payment.PaymStatus = PM_READIED;

        ExpPaym.PayerAmount = ExpAmount;
        ExpPaym.BaseFIID = 
        ExpPaym.OrderFIID = BFIID;
    else
        ExpPaym.OrderAmount          = ExpPaym.OrderAmount      + ExpAmountB;
        ExpPaym.PayerAmount          = ExpPaym.PayerAmount      + ExpAmount;
        ExpPaym.ReceiverAmount       = ExpPaym.ReceiverAmount   + ExpAmountB;
        ExpPaym.BaseAmount           = ExpPaym.BaseAmount       + ExpAmountB;
        //ExpPaym.FutureBaseAmount     = ExpPaym.FutureBaseAmount + ExpAmountB;
        ExpPaym.ActuateFutureAmounts(TBA_AMOUNT);
        ExpPaym.Actuate();
    end;

    pml.rec.Amount = ExpAmount;
    pml.rec.FIID   = leg.PFI;
    pml.rec.IPSign = "-";
    pml.rec.PPSign = "";
    PmPaym.LinkPayment(ExpPaym, PMLINK_KIND_OVERDUE, pml);

    if (ExpPaym.Payer == {OurBank})
        ExpPaym.SetPayerPI(PAYMENTS_GROUP_UNDEF,
                             ExpPaym.PayerBankID,
                             ExpPaym.PayerBankCodeKind,
                             ExpPaym.PayerBankCode,
                             ExpPaym.PayerBankName,
                             ExpPaym.PayerBankCorrAcc,
                             ExpPaym.BaseFIID,
                             1,
                             Счет,
                             ExpPaym.Payer,
                             ExpPaym.PayerName,
                             ExpPaym.PayerINN,
                             ExpPaym.PayerCodeKind,
                             ""
                             );
    else
        ExpPaym.SetReceiverPI(PAYMENTS_GROUP_UNDEF,
                                ExpPaym.ReceiverBankID,
                                ExpPaym.ReceiverBankCodeKind,
                                ExpPaym.ReceiverBankCode,
                                ExpPaym.ReceiverBankName,
                                ExpPaym.ReceiverBankCorrAcc,
                                BFIID,
                                1,
                                Счет,
                                ExpPaym.Receiver,
                                ExpPaym.ReceiverName,
                                ExpPaym.ReceiverINN,
                                ExpPaym.ReceiverCodeKind,
                                ""
                                );
    end;

    return stat;
end;
/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep( Buffer, Document, DocKind, ID_Operation, ID_Step)
    var
        Categ = "",
        BankruptStatus = 0, rvstat = 0;

    SetBuff( deal, Document );

    FirstDoc = MMFirstDoc(DL_IBCDOC, deal.DealID, true);

    leg.LegKind = LEG_KIND_DL_TICK;
    leg.DealID = deal.DealID;
    leg.LegID = 1;
    if( not GetEq(leg) )
        mm_error( "Не найден транш сделки ", deal.DealCode );
        return 1;
    end;

    if(not MM_GetPM_ValueDate(deal.BofficeKind, deal.DealID, PMPurpose, @ValueDate))
        mm_error("Не найден платеж с назначением ", PMPurpose );
        return 1;
    end;
    rvstat = GetPartyBankruptStatus(deal.PartyID, {curdate}, @BankruptStatus);
    if((rvstat == 0) AND (BankruptStatus != 10))
      ContrIsBankrupt = true;
    else
      ContrIsBankrupt = false;
    end;

    if( PMPurpose == PM_PURP_PRINC_RET ) /* Возврат ОД */
        if(( not InsertDocPrinc( isSale( GetOperationGroup( deal.DealType ) ) ) )or
            (CreatePaym(deal.DealID, PM_PURP_PRINC_RET, isSale( GetOperationGroup( deal.DealType ) ))))
            return 1;
        end;   
    elif( PMPurpose == PM_PURP_PERCENT ) /* %% */
        if(( not InsertDocPerc( isSale( GetOperationGroup( deal.DealType ) ) ))or
            (CreatePaym(deal.DealID, PM_PURP_PERCENT, isSale( GetOperationGroup( deal.DealType ) ))))
            return 1;
        end;   
    elif( PMPurpose == PM_PURP_COMRECEIVING ) /* прочие доходы */
        if(( not InsertDocComis( isSale( GetOperationGroup( deal.DealType ) ) ))or
            (CreatePaym(deal.DealID, PM_PURP_COMRECEIVING, isSale( GetOperationGroup( deal.DealType ) ))))
            return 1;
        end;   
    else
        mm_error( "Необрабатываемое назначение платежа ", PMPurpose );
        return 1;
    end;
  
    if(MM_IsDSF(FirstDoc.GetTick().DealType))
        Categ = tdr_pennyR;
    else
        Categ = tdr_pennyP;
    end;

    MC_FindAndOpenAccountEx(Categ, FirstDoc, ValueDate, 0, MC_OPENACC_CREATE);

    return 0;
END;

NameMacroFile = "mmexp_j.mac";
