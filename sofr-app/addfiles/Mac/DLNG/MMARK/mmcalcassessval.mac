 /*
 $Name: mmcalcassessval.mac
 $Module: МБ Кредиты
 $Description: макрос расчета оценочной стоимости предмета залога 
 */

IMPORT BankInter, CTInter, RsbDataSet, OprInter, FIInter;
/****************************************************************************************/
PRIVATE VAR RepData = NULL;

PRIVATE CONST
    ERR_NOERROR       = 0,
    ERR_BADREGESTRY   = 1,
    ERR_NORATEKIND    = 2,
    ERR_NOSECRATEKIND = 3,
    ERR_NORATEVALUE   = 4,
    ERR_NOKOEF        = 5,

    SET_CHAR   = "X";

PRIVATE CLASS CRepData
  var
      SecCode       = "",
      SecName       = "",
      RateName      = "",
      RateFI        = "",
      OrderFI       = "",
      SecFIID       = -1,
      RateFIID      = -1,
      OrderFIID     = -1,
      CountSec      = 0,
      RateKind      = 0,
      RateValue     = 0,
      Cost1Sec      = Money(0),
      CostALLSec    = Money(0),
      Factor        = 0,
      OrderSignDate = date(0, 0, 0),
      RateSinceDate = date(0, 0, 0),
      IsRelative = "",
      FaceValue = Money(0);

  var
     Error = ERR_NOERROR;
END;
/****************************************************************************************/
PRIVATE MACRO GetErrorStr(numError)
    var retVal = "";
    if (numError == ERR_NOERROR)
        retVal = "";
    elif (numError == ERR_BADREGESTRY)
        retVal = "Не установлен вид курса 'Рыночная цена (ломбардный кредит)'";
    elif (numError == ERR_NORATEKIND)
        retVal = "Не найден установленный вид курса 'Рыночная цена (ломбардный кредит)'";
    elif (numError == ERR_NOSECRATEKIND)
        retVal = "Для ценной бумаги не задан вид курса";
    elif (numError == ERR_NORATEVALUE)
        retVal = "Нет значения для основного курса";
    elif (numError == ERR_NOKOEF)
        retVal = "Для ценной бумаги не задано поправочного коэффициента";
    else
        retVal = "";
    end;

    return retVal;
END;
/****************************************************************************************/
PRIVATE MACRO PrintReport()
    if (RepData == NULL)
        [НЕТ ДАННЫХ ДЛЯ ОТЧЕТА];
        return ;
    else
        [  ];
        [                   Протокол расчета оценочной стоимости предмета залога                  ];
        [  ];
    end;

  if (RepData.Error == ERR_NOERROR)
      [ Код ценной бумаги: #################################################################    ](String(RepData.SecCode + " " + RepData.SecName):l);
      [ Количество ценных бумаг: ##########                                                     ](RepData.CountSec:l);
      [ Дата соглашения: ##########                                                             ](Date(RepData.OrderSignDate):f:c);
      [ Вид курса Рыночная стоимость (ломбардный кредит): ##################################    ](String(RepData.RateKind + " " + RepData.RateName):l);
      [ Значение курса: #############################                                           ](String(RepData.RateValue + " номинала (" + RepData.RateFI + ") за 1"):l);
      [ Рыночная цена в валюте оценки: #####################################################    ](String(RepData.Cost1Sec + " " + RepData.OrderFI):l);
      [ Поправочный коэффициент: ##########                                                     ](RepData.Factor:l);
      [ Оценочная стоимость предмета залога в валюте оценки: ###############################    ](String(RepData.CostALLSec + " " + RepData.OrderFI):l);
  else
      [ ####################################################################################    ](GetErrorStr(RepData.Error):l);
  end;
END;
///****************************************************************************************/
MACRO CalcAssesedValue(Order, Secur)
    var stat = 0, ВидКурса,
        QueryStr, rsbQuery, rsbFaceValue,
        ДатаПримечания = date(0,0,0);

    record sec(dl_secur);
    record ord(dl_order);
    record RFi(fininstr);
    record RAvr(avoiriss);

    SetBuff(sec, Secur);
    SetBuff(ord, Order);

    RepData = CRepData();

    GetRegistryValue("MMARK\\ОБЩИЕ\\ВИДКУРСА_ЛОМБАРДНЫЙ_КРЕДИТ",
                        V_INTEGER, RepData.RateKind,stat);

    if ((stat)or(RepData.RateKind == 0))
        RepData.Error = ERR_BADREGESTRY;
        PrintReport();
        return 0;
    end;

    QueryStr = "select ratetype.t_typename from dratetype_dbt ratetype where ratetype.t_type = " + RepData.RateKind;
    rsbQuery = TRsbDataSet(QueryStr);
    if (not rsbQuery.MoveNext)
        RepData.Error = ERR_NORATEKIND;
        PrintReport();
        return 0;
    else
        RepData.RateName = rsbQuery.TypeName;
    end;

    RepData.OrderSignDate = date(ord.SignDate);
    RepData.OrderFIID     = ord.FIID;
    RepData.CountSec      = sec.Quantity;
    RepData.SecFIID       = sec.FIID;

    QueryStr = "select fininstr.t_fi_code, fininstr.t_name as finame "
                "from dfininstr_dbt fininstr "
                "where (fininstr.t_fiid = " + sec.FIID + ")";

    rsbQuery = TRsbDataSet(QueryStr);
    rsbQuery.MoveNext;
    RepData.SecCode = rsbQuery.FI_Code;
    RepData.SecName = rsbQuery.FIName;

    QueryStr = "select fininstrord.t_name as ordfiname "
                "from dfininstr_dbt fininstrord  "
                "where (fininstrord.t_fiid = " + ord.FIID + ")";

    rsbQuery = TRsbDataSet(QueryStr);
    rsbQuery.MoveNext;
    RepData.OrderFI = rsbQuery.OrdFIName;

    QueryStr = "select * from "
                "("
                "select ratedef.t_rateid, ratedef.t_fiid, ratedef.t_otherfi, ratedef.t_sincedate, "
                "ratedef.t_rate/power(10, ratedef.t_point) as t_rate, fininstr.t_fi_code as otherficode, "
                "ratedef.t_isrelative "
                "from dratedef_dbt ratedef, dfininstr_dbt fininstr "
                "where "
                "((ratedef.t_fiid = " + RepData.SecFIID + ")or(ratedef.t_otherfi = " + RepData.SecFIID + "))"
                "and(ratedef.t_type = " + RepData.RateKind + ")"
                "and(ratedef.t_sincedate <= '" + date(RepData.OrderSignDate) + "')"
                "and((fininstr.t_fiid = ratedef.t_fiid)or(fininstr.t_fiid = ratedef.t_otherfi))"
                "and(fininstr.t_fi_kind = " + FIKIND_CURRENCY + ") "
                "UNION ALL "
                "select ratedef.t_rateid, ratedef.t_fiid, ratedef.t_otherfi, ratehist.t_sincedate, "
                "ratehist.t_rate/power(10, ratehist.t_point), fininstr.t_fi_code, ratedef.t_isrelative "
                "from dratehist_dbt ratehist,dratedef_dbt ratedef, dfininstr_dbt fininstr "
                "where "
                "(ratehist.t_rateid = ratedef.t_rateid)"
                "and((ratedef.t_fiid = " + RepData.SecFIID + ")or(ratedef.t_otherfi = " + RepData.SecFIID + "))"
                "and(ratedef.t_type = " + RepData.RateKind + ")"
                "and(ratedef.t_sincedate <= '" + date(RepData.OrderSignDate) + "')"
                "and((fininstr.t_fiid = ratedef.t_fiid)or(fininstr.t_fiid = ratedef.t_otherfi))"
                "and(fininstr.t_fi_kind = " + FIKIND_CURRENCY + ")"
                ") "
                "order by t_sincedate desc";

    rsbQuery = TRsbDataSet(QueryStr);
    if (not rsbQuery.MoveNext)
        RepData.Error = ERR_NOSECRATEKIND;
        PrintReport();
        return 0;
    end;

    RepData.RateSinceDate = date(rsbQuery.SinceDate);

    if (RepData.SecFIID == rsbQuery.FIID)
        RepData.RateFIID = rsbQuery.OtherFI;
    else
        RepData.RateFIID = rsbQuery.FIID;
    end;

    RepData.RateFI        = rsbQuery.OtherFICode;
    RepData.RateValue     = rsbQuery.Rate;
    RepData.IsRelative = rsbQuery.IsRelative;

    rsbFaceValue = TRsbDataSet("select t_facevalue from dfininstr_dbt where t_fiid =" + RepData.SecFIID);
    if (rsbFaceValue.MoveNext)
        RepData.FaceValue = rsbFaceValue.FaceValue;
    end;

    if ((rsbQuery.MoveNext)and(date(rsbQuery.SinceDate) == RepData.RateSinceDate))
        rsbQuery = TRsbDataSet("select * from (" + QueryStr + ") where t_otherfi = " + RepData.OrderFIID);
        if (not rsbQuery.MoveNext)
            rsbQuery = TRsbDataSet("select * from (" + QueryStr + ") secrate, dfininstr_dbt fininstr where (fininstr.t_fiid = secrate.t_fiid)and(fininstr.t_facevaluefi = secrate.t_otherfi)");
            if (rsbQuery.MoveNext)
                RepData.RateSinceDate = date(rsbQuery.SinceDate);
                RepData.RateFIID      = rsbQuery.OtherFI;
                RepData.RateFI        = rsbQuery.OtherFICode;
                RepData.RateValue     = rsbQuery.Rate;
            end;
        else
            RepData.RateSinceDate = date(rsbQuery.SinceDate);
            RepData.RateFIID      = rsbQuery.OtherFI;
            RepData.RateFI        = rsbQuery.OtherFICode;
            RepData.RateValue     = rsbQuery.Rate;
        end;
    end;

    if (RepData.IsRelative == SET_CHAR)
        RepData.RateValue = RepData.RateValue/100 * RepData.FaceValue;
    end;
				
    if (RepData.RateFIID == RepData.OrderFIID)
        RepData.Cost1Sec = Money(RepData.RateValue);
    else
        if (ConvSum(RepData.Cost1Sec, Money(RepData.RateValue), RepData.OrderSignDate, RepData.RateFIID, RepData.OrderFIID))
            RepData.Error = ERR_NORATEVALUE;
            PrintReport();
            return 0;
        end;
    end;

    ПолучитьФинИн(RepData.SecFIID, RFi, RAvr);
    RepData.Factor = readNoteForObject(OBJTYPE_AVOIRISS, UniID(RAvr, OBJTYPE_AVOIRISS), 13, date(RepData.OrderSignDate), ДатаПримечания);

    if ((RepData.Factor == NULL)or
        ((RepData.Factor == 0)and(ДатаПримечания == date(0,0,0))))
        RepData.Error = ERR_NOKOEF;
        PrintReport();
        return 0;
    elif (RepData.Factor == 0)
        RepData.Factor = 1;
    end;

    RepData.CostALLSec = RepData.Cost1Sec * RepData.CountSec * RepData.Factor;

    PrintReport();

    return RepData.CostALLSec;
END;
