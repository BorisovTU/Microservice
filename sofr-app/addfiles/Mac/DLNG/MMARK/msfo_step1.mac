/*
$Name:        msfo_step1.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "2.1 Установка параметров МСФО в сделках"
*/
/*-------------------------------------------------------------------------
  Имя файла        : msfo_step1.mac

  Описание         : макрос выполнения шага "2.1 Установка параметров МСФО в сделках"

  Программист      : Терещенко Ф.С.

  Создан           : 23.11.2018
-------------------------------------------------------------------------*/

import mmlibr, MoCommon, mctplprm;

VAR count = 0;

VAR rs_cnt = RsdRecordset("SELECT COUNT(1) " + 
                           " FROM DDL_TICK_DBT d1 JOIN DDL_LEG_DBT  d2 ON (D1.T_DEALID = D2.T_DEALID) " +
                          " WHERE d1.T_BOFFICEKIND IN (102, 208) AND d1.T_DEALSTATUS IN (0, 10) ORDER BY d1.T_DEALID");
rs_cnt.moveNext(); count = rs_cnt.Value(0, NULL, V_INTEGER);
InitProgress(count, "Сделок обработано...", "Установка параметров сделки");
count = 0;

VAR rs = RsdRecordset("SELECT d1.T_DEALID, d1.T_BOFFICEKIND, d1.T_DEALTYPE, d1.T_FLAGTYPEDEAL, d1.T_PARTYID, d2.T_DURATION " + 
                      " FROM DDL_TICK_DBT d1 JOIN DDL_LEG_DBT  d2 ON (D1.T_DEALID = D2.T_DEALID) " +
                " WHERE d1.T_BOFFICEKIND IN (102, 208) AND d1.T_DEALSTATUS IN (0, 10) ORDER BY d1.T_DEALID");

WHILE (rs.moveNext())
   VAR DealID  = rs.Value("T_DEALID");

   // "Банк России"
   VAR BankRussia = FALSE;
   IF (MC_GetKindSecurCredit( rs.Value("T_PARTYID") ) == 1)
      BankRussia = TRUE;
   END;

   VAR ObjType = 0;
   IF (rs.Value("T_BOFFICEKIND") == 102)
      ObjType = OBJTYPE_MMARKDEAL;
   ELSE
      ObjType = OBJTYPE_CREDITLN;
   END;

   PrintLn("Сделка: " + DealID + "/" + ObjType);

   // Условия 1 - "Индивидуальные", 2 - "Стандартные"
   VAR GroupID = 3;
   VAR AttrID  = 1;

   IF (BankRussia == TRUE)
      AttrID  = 2;
   ELSE
      AttrID  = 1;
   END;
   ConnectObjAttr(NULL, ObjType, L_Z(DealID, 34), GroupID, AttrID, NULL, "", {curdate});

   // 4 Бизнес-модель
   // 1 ─ Удержание до погашения
   // 2 ─ Удержание до погашения и продажи
   // 3 ─ Другая бизнес-модель
   GroupID = 4;
   AttrID  = 1;
   ConnectObjAttr(NULL, ObjType, L_Z(DealID, 34), GroupID, AttrID, NULL, "", {curdate});

   // 5 Категория оценки
   // 1 ─ Оцениваемые по АС (линейный метод)
   // 2 ─ Оцениваемые по АС (метод ЭПС)
   // 3 ─ Оцениваемые по СС ПУ
   // 4 ─ Оцениваемые по СС ПСД
   GroupID = 5;
   IF ((rs.Value("T_DURATION") < 365) AND ((rs.Value("T_FLAGTYPEDEAL") != 1) AND rs.Value("T_FLAGTYPEDEAL") != 3))
      AttrID = 1;   
   ELSE
      AttrID = 2;
   END;
   ConnectObjAttr(NULL, ObjType, L_Z(DealID, 34), GroupID, AttrID, NULL, "", {curdate});

   // 6 Тест на рыночность
   // 1 ─ Пройден
   // 2 ─ Не пройден
   GroupID = 6;
   IF ((rs.Value("T_DURATION") <= 180) OR (BankRussia == TRUE) OR (rs.Value("T_FLAGTYPEDEAL") == 1))
      AttrID = 1;
   ELSE
      AttrID = 2;
   END;
   ConnectObjAttr(NULL, ObjType, L_Z(DealID, 34), GroupID, AttrID, NULL, "", {curdate});

   // 7 Уровень иерархии справедливой стоимости
   // 1 ─ 1
   // 2 ─ 2
   // 3 - 3
   GroupID = 7;
   AttrID  = 2;
   ConnectObjAttr(NULL, ObjType, L_Z(DealID, 34), GroupID, AttrID, NULL, "", {curdate});

   // 8 Тип данных
   // 1 ─ Наблюдаемые
   // 2 ─ Ненаблюдаемые
   GroupID = 8;
   AttrID  = 1;
   ConnectObjAttr(NULL, ObjType, L_Z(DealID, 34), GroupID, AttrID, NULL, "", {curdate});

   // 9 Условия, влияющие на результат SPPI-теста, признак
   // 1 - Да
   // 2 - Нет
   GroupID = 9;
   AttrID  = 2;
   ConnectObjAttr(NULL, ObjType, L_Z(DealID, 34), GroupID, AttrID, NULL, "", {curdate});

   // 10 - SPPI-тест
   // 1 ─ Пройден
   // 2 ─ Не пройден
   VAR DealType = 0;
   IF (rs.Value("T_DEALTYPE") == 2300)
      DealType = 1; // Привлечение
   ELIF  (rs.Value("T_DEALTYPE") == 2305)
      DealType = 2; // Размещение
   END;

   IF (DealType == 2)
      GroupID = 10;
      AttrID  = 1;
      ConnectObjAttr(NULL, ObjType, L_Z(DealID, 34), GroupID, AttrID, NULL, "", {curdate});
   ELSE
      DisconnectObjAttr(ObjType, L_Z(DealID, 34), GroupID);
   END;

   // Регистрация БП
   MM_BankProduct(DealID);

   UseProgress (count = count + 1);
END;
RemProgress (count);