/*
$Name:        mmadjint_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Корректировка процентного  дохода по кредитно-обесцененным ФА"
*/

IMPORT mmlibr, mmcarry;

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0,
    ExecDate;

PRIVATE MACRO Установлена3СтадияОбесц(deal)
    VAR
        RsbReslnk = TRsbDataSet("select 1 from dmmhstimp_dbt where t_dealid = " + deal.DealID + " and t_impgrade = 2" +
                                 " and t_date in (select max(t_date) from dmmhstimp_dbt where t_dealid = " + deal.DealID + 
                                 " and t_impgrade = 2)");
    // по сделке установлена 3 стадия обесценения
    if (RsbReslnk.MoveNext()) 
        return true;
    end;
    return false;
END;

PRIVATE MACRO БылаУстановлена12СтадияОбесц(deal)
    VAR
        RsbReslnk = TRsbDataSet("select 1 from dmmhstimp_dbt where t_dealid = " + deal.DealID + " and (t_impgrade = 0 or t_impgrade = 1)" +
                                 " and t_date <= (select min(t_valuedate) from dpmpaym_dbt where t_dockind = " + deal.BofficeKind  + 
                                 " and t_documentid = " + deal.DealID + " and t_purpose = 10 and t_paymstatus = 32000)");

    if (RsbReslnk.MoveNext()) 
        return true;
    end;
    return false;
END;

PRIVATE MACRO КорректироватьПроцетныйДоход(deal)
    VAR flag, stat = 0, AsCategory = 0;

    GetRegistryValue("MMARK\\МСФО_ПРОЦ_ДОХ_ПО_КРЕД_ОБЕСЦЕН",
        V_BOOL, flag, stat);
    if (not flag)
        return false;
    end;
    // привлечение
    if (IsBUY(GetOperationGroup (deal.DealType)))
        return false;
    end;
    GetMainObjAttr(NULL, OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 5, AsCategory, NULL, NULL, ExecDate);
    if ((AsCategory != 1) and (AsCategory != 2))
        return false;
    end;
    if (not Установлена3СтадияОбесц(deal))
        return false;
    end;
    if (not БылаУстановлена12СтадияОбесц(deal))
        return false;
    end;
    
    return true;
END;


/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    record deal( dl_tick );
    SetBuff( deal, Document );

    if (not КорректироватьПроцетныйДоход(deal))
        return 0;
    end;

    ID_Operation = IDOperation;
    ID_Step      = IDStep;
    ExecDate     = {curdate};

    VAR at, Sk, deal_pd;
    Sk = MM_CalcAdjInt(DL_IBCDOC, deal.DealID, ExecDate, 0);
    if ((Sk == -1) or (Sk == 0))
        return 0;
    end;
    Sk = abs(Sk);

    deal_pd = MMFirstDoc(DL_IBCDOC, deal.DealID, true);
    ConvSum( Sk, Sk, ExecDate, deal_pd.GetLeg().PFI, NATCUR);

    at = MM_Carry;
    at.FIIDPayer        = 
    at.FIIDReceiver     = NATCUR;
    at.date_carry       = ExecDate;
    at.PrimaryDoc       = deal_pd;
    at.Sum              = Sk;
    at.Chapter          = 1;
    at.CatPayer         = tdr_plcorP(deal_pd.GetTick().TypeDoc);
    at.CatReceiver      = tdr_rezadjR(deal_pd.GetTick().TypeDoc);
    at.Ground           = "Отражение корректировки процентного дохода кредитно-обесцененного ФА";
    if (not at.carry())
        return 1;
    end;

    return 0;
END;

NameMacroFile = "mmadjint_j.mac";
