/*
$Name:        mmacccom_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Учет комиссий"
*/

IMPORT PaymInter, OprInter, PTInter, SfInter, FIInter, MmarkInter, mmlib, mmlibr, spserv, mmcnst_j, mmcarry, mmcateg, "globals.mac", mmservop;

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0,
    ExecDate;

PRIVATE CONST
    MM_COSTREF_ERR_GETCLSCOM = 11,
    MM_COSTREF_ERR_CALCCOST  = 12,
    MM_COSTREF_ERR_UNCOSTS   = 13;

private VAR sfcom  = TRecHandler("sfcomiss.dbt");

RECORD settacc(settacc);

PRIVATE MACRO НужноУчитыватьЗатраты(DocID, DocKind)
    var sel, res;
    sel = RSDCommand("select 1 from ddlcomis_dbt where t_dockind = ? and t_docid = ?");
    sel.addParam("", RSDBP_IN, DocKind);
    sel.addParam("", RSDBP_IN, DocID);    
    sel.execute();
    res  = TRsbDataSet(sel);
    if (res.MoveNext())
        return true;
    end;

    return false;
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/

MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    record deal( dl_tick );
    record sfcontr(sfcontr);

    ID_Operation = IDOperation;
    ID_Step      = IDStep;

    VAR RsbData, at, Payment, doc_pd, DefaultCodeKind, НзПц, НзОп, query, OprServDoc, mmcomiss, NoteKind = 0, categ, FIRole, account;
    VAR DocID, DocPFI, DocOper, DocParty, DocType, DealDate, setaccid, SettaccFound = false, FileSettAcc = TBFIle("settacc.dbt"),
    cat_inccalcs, cat_othinc;

    OprServDoc = GetOprServDoc();
    ExecDate = OprServDoc.rec.CommDate;
    if (not isServOpMode())
        ExecDate = {curdate};
    end;

    // учет затрат
    if (OprServDoc.rec.ComType == 1)
        return 0;
    end;

    if (DocKind == DL_IBCDOC)
        SetBuff( deal, Document );
        doc_pd   = MMFirstDoc(deal.BofficeKind, deal.DealID, true);
        DocID    = deal.DealID;
        DocPFI   = doc_pd.GetLeg().PFI;
        DocOper  = deal.Oper;
        DocType  = deal.DealType;
        DocParty = deal.PartyID;
        DealDate = deal.DealDate;

        НзПц = ПолучитьЗначениеПримечания(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 12, ExecDate);
        НзОп = ПолучитьЗначениеПримечания(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 13, ExecDate);

        cat_inccalcs = get_inccalcs(deal.DealType, ExecDate);
        cat_othinc   = get_othinc(deal.DealType, ExecDate);
    else
        SetBuff( sfcontr, Document );
        doc_pd   = MMFirstDoc(DL_SFCONTR, sfcontr.ID, false);
        DocID    = sfcontr.ID;
        DocPFI   = sfcontr.FIID;
        DocParty = sfcontr.PartyID;

        cat_inccalcs = tdr_inccalcs;
        cat_othinc   = tdr_othinc;
    end;

    if (not MM_CheckLic())
        return 1;
    end;
    
    if (not НужноУчитыватьЗатраты(DocID, DocKind))
        return 0;
    end;

    query = "select t_sum, t_id, t_feetype, t_comnumber, t_planpaydate, t_contract from ddlcomis_dbt " +
                          "where t_dockind = " + DocKind + 
                          " and t_docid = " + DocID +
                          " and t_factpaydate = " + GetSQLDate(date(0,0,0)) +
                          " and t_id not in (select t_subpurpose from dpmpaym_dbt where t_purpose = 87)";

    if (OprServDoc.rec.ContractID > 0)
        query = query + " and t_contract = " + OprServDoc.rec.ContractID;
    end;
    if (OprServDoc.rec.ComNumber > 0)
        query = query + " and t_comnumber = " + OprServDoc.rec.ComNumber + " and t_feetype = 6";
    end;


    RsbData = TRsbDataSet(query);

    while (RsbData.MoveNext)
        if( not SfGetComiss( RsbData.FeeType, RsbData.ComNumber, sfcom ) )
            mm_error("Не найдена комиссия по операции");
            return 1;
        end;     
        if (DocKind == DL_IBCDOC)
            sfcontr.ID = RsbData.Contract;
            if (GetEQ(sfcontr))
                DocParty = sfcontr.PartyID;
            end;
        end;
        
        if (sfcom.rec.ReceiverID == {ourbank})
            FIRole = 10000 + RsbData.Id;
            at = MM_Carry;
            at.Chapter      = 1;
            at.date_carry   = ExecDate;
            at.PrimaryDoc   = doc_pd;
            at.FIIDPayer    = DocPFI;
            at.FIIDReceiver = DocPFI;

            mmcomiss  = TBfile("mmcomiss.dbt");
            mmcomiss.rec.FeeType = RsbData.FeeType;
            mmcomiss.rec.Number  = RsbData.ComNumber;
            if (not mmcomiss.GetEQ)
                SvOpSayError(deal.DealCode, MM_COSTREF_ERR_GETCLSCOM, "Не удалось получить запись из справочника комиссий МБК");
                return 1;
            end;

            if (DocKind == DL_IBCDOC)
                if (isSale(GetOperationGroup(deal.DealType)))
                    ConvSumCross(at.SumReceiver, RsbData.Sum, ExecDate, sfcom.rec.FIID_Comm, DocPFI);
                    at.CatPayer    = cat_inccalcs;
                    at.CatReceiver = cat_othinc;
                    at.FIRolePayer =
                    at.FIRoleReceiver = FIRole;
                    at.Ground = "Учет прочих доходов по сделке";
                else
                    at.CatPayer = tdr_comreq;
                    if (mmcomiss.rec.Type == 1) // процентный
                        at.CatReceiver = tdr_atcomR(deal.TypeDoc);
                    elif (mmcomiss.rec.Type == 2) // операционный
                        at.CatReceiver = tdr_atodR(deal.TypeDoc);
                    end;
                    at.FIIDReceiver = NATCUR;
                    ConvSumCross(at.SumPayer, RsbData.Sum, ExecDate, sfcom.rec.FIID_Comm, DocPFI);

                    at.FIRolePayer  = FIRole;
                    at.Ground = "Начисление комиссии по сделке привлечения";
                end;

                if (mmcomiss.rec.Type == 1) // процентный
                    НзПц = НзПц + at.SumReceiver;
                elif (mmcomiss.rec.Type == 2) // операционный
                    НзОп = НзОп + at.SumReceiver;
                end;
                
            else
                VAR mccateg  = TBfile("mccateg.dbt");
                ConvSumCross(at.SumReceiver, RsbData.Sum, ExecDate, sfcom.rec.FIID_Comm, DocPFI);
                mccateg.rec.ID = mmcomiss.rec.CatID;
                if (not mccateg.GetEQ())
                    return 1;
                end;
                at.CatPayer    = tdr_comreq;
                at.CatReceiver = mccateg.rec.code;
                at.FIRolePayer = FIRole;
                at.Ground = "Начисление комиссии по договору обслуживания";
            end;

            if (not at.carry())
                mm_error("Ошибка при формировании проводки");
                return 1;
            end;

            // формирование платежа
            Payment = RsbPayment();
            Payment.DocKind              = DocKind;
            Payment.DocumentID           = DocID;
            Payment.Purpose              = PM_PURP_COMRECEIVING;
            Payment.SubPurpose           = RsbData.ID;
            Payment.OrderFIID            =
            Payment.BaseFIID             = 
            Payment.PayerFIID            = 
            Payment.ReceiverFIID         = 
            Payment.FuturePayerFIID      = 
            Payment.FutureReceiverFIID   = DocPFI;
            Payment.OrderAmount          =
            Payment.PayerAmount          = 
            Payment.ReceiverAmount       = 
            Payment.BaseAmount           = 
            Payment.FutureBaseAmount     = at.SumReceiver;
            Payment.ActuateFutureAmounts(TBA_AMOUNT);
            Payment.IsPlanPaym           = "X";
            Payment.Date                 = 
            Payment.PayDate              = 
            Payment.OutTransferDate      =
            Payment.ValueDate            = RsbData.PlanPayDate;
            Payment.ClientDate           = ExecDate;
            Payment.Number               = String(Payment.SubPurpose); 
            Payment.Oper                 = DocOper;
            Payment.Origin               = PAYMENT_OR_AUTO;
            Payment.PaymStatus           = PM_READIED;
            if ((DocKind == DL_IBCDOC) and (deal.Netting == 3))
                Payment.Netting = "X";
            else
                Payment.Netting = "";
            end;

            if (DocPFI == NATCUR)
                DefaultCodeKind = PTCK_BIC;
            else
                DefaultCodeKind = PTCK_SWIFT;
            end;

            if ((DocKind == DL_SFCONTR) or ((DocKind == DL_IBCDOC) and (isBuy(GetOperationGroup(deal.DealType)))))
                categ = tdr_comreq;
            else
                categ = cat_inccalcs;
            end;

            var FindAccount = "";
            doc_pd.IsActAccount(categ, FindAccount, true, FIRole, NULL, ExecDate, NULL);
            if (FindAccount == "")
                doc_pd.ActualAccount(categ, FindAccount, true, FIRole, NULL, ExecDate, NULL);
            end;

            Payment.SetReceiverPI(PAYMENTS_GROUP_UNDEF,
                    {OurBank},
                    DefaultCodeKind,
                    "",
                    "",
                    "",
                    DocPFI,
                    1,
                    FindAccount,
                    {OurBank},
                    "",
                    "",
                    DefaultCodeKind,
                    ""
                    );

            SettaccFound = false;
            setaccid = GetContrSSISettAccContr(RsbData.Contract, DocPFI, RsbData.FeeType, RsbData.ComNumber);
            if (setaccid)
                FileSettAcc.rec.SettAccID = setaccid;
                if (FileSettAcc.GetEQ)
                    SettaccFound = true;
                    copy(settacc, FileSettAcc);
                end;
            end;

            if (not SettaccFound)
                if (not FindSETTACC_PMAUTO(DocParty, FIKIND_CURRENCY, DocPFI, PTSK_MM, DocType, PM_PURP_COMRECEIVING, 0, settacc));
                elif (not FindSETTACC_PMAUTO(DocParty, FIKIND_CURRENCY, DocPFI, PTSK_MM, DocType, 0, 0, settacc));
                elif (not FindSETTACC_PMAUTO(DocParty, FIKIND_CURRENCY, DocPFI, PTSK_MM, 0, PM_PURP_COMRECEIVING, 0, settacc));
                elif (not FindSETTACC_PMAUTO(DocParty, FIKIND_CURRENCY, DocPFI, PTSK_MM, 0, 0, 0, settacc));
                end;
            end;

            Payment.SetPayerPI(PAYMENTS_GROUP_UNDEF,
                                settacc.BankID,
                                settacc.BankCodeKind,
                                settacc.BankCode,
                                settacc.BankName,
                                settacc.CorrAcc,
                                settacc.FIID,
                                settacc.Chapter,
                                settacc.Account,
                                settacc.PartyID,
                                settacc.RecName,
                                settacc.INN,
                                settacc.CodeKind,
                                settacc.Code
                                );

            Payment.Actuate();

         end;
    end;

    if (DocKind == DL_IBCDOC)
        MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 12, ExecDate, money(НзПц));
        MM_ChangeNoteText(OBJTYPE_MMARKDEAL, UniID(deal, OBJTYPE_MMARKDEAL), 13, ExecDate, money(НзОп));
    end;


    return 0;
END;

NameMacroFile = "mmacccom_j.mac";
