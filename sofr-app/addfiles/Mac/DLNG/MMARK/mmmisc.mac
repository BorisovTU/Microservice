/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : mmmisc.mac
  Programmer  : Велигжанин А.В.
  Comment     : Функции общего употребления
                библиотеки МБК
  Функции     :
                CountSymb
                IIF
                InList
                StrAfter
                StrBefore
                StrCut
                ПоУмолчанию
└───────────────────────────────────────────────────────────────────────────*/

IMPORT BankInter, globals, likepy;

const
  Left = 1, Right = 2;


/* Установка переменной, если она не определена
*/
MACRO ПоУмолчанию(переменная, значение)
  if (ValType(переменная) == V_UNDEF)
      setparm(0,значение);
  end;
end;


/*  Возвращает кол-во символов 'c'
    в строке 's'
    Предполагается, что символы
    идут подряд.
    Например: s=="01234ФФФФФ1234",c=="Ф"
    Результат: 5
*/
macro CountSymb( s, c )
var l = 0,
    p = Index(s,c);

    if(p != 0)
       l = 1;
       while( SubStr(s, p + l, 1) == c )
         l = l + 1;
       end;
    end;
    return l;
end;


/* Обрезает символы 'c' в строке 's'
   k=0-обрезает слева и справа(по умолчанию)
     1-только слева
     2-только справа
   Например: s="00123004500",c='0'
   Результат     "1230045"        (k=0)
                 "123004500"      (k=Left)
               "001230045"        (k=Right)
*/
MACRO MM_StrCut( s, c, k )
var n, p, l, Ok, i;

   if(ValType(c)!=V_STRING)
     c=" ";
   end;
   if((ValType(k)!=V_INTEGER) or (not InList(k,0,Left,Right)))
     k=0;
   end;
   n=strlen(s);
   p=1;
   l=n;
   Ok=true;
   i=1;
   if(k!=Right)
     /* Первый проход по строке
        с начала до конца
        определение позиции 'p'
     */
     while(Ok and (i<=n))
       if(SubStr(s,i,1)==c)
         if(Ok)
           p=p+1;
           l=l-1;
          end;
       else
         Ok=false;
       end;
       i=i+1;
     end;
   end;
   if(k!=Left)
     /* Второй проход по строке
        с конца в начало
     */
     i=n;
     Ok=true;
     while(Ok and (i>=1))
       if(SubStr(s,i,1)==c)
         if(Ok)
           l=l-1;
          end;
       else
         Ok=false;
       end;
       i=i-1;
     end;
   end;
   return SubStr(s,p,l);
END;


/* Аналог условной функции.
   Возвращает 'value_true',
   когда условие 'condition' выполняется(ИСТИНА)
   или 'value_false'.
*/
PRIVATE MACRO IIF(condition,value_true,value_false)
    if (condition)
        return value_true;
    else
        return value_false;
    end;
END;


/* Возвращает строку 's'
   до символа 'c'
   Например: s="abcdefg.xyz",c='.'
   Результат   "abcdefg"
*/
MACRO StrBefore( s, c )
var p=Index(s, c);
    return IIF(p==0,s,SubStr(s,1,p-1));
END;


/* Возвращает строку 's'
   после символа 'c'
   Например: s="abcdefg.xyz",c='.'
   Результат   "xyz"
*/
MACRO StrAfter( s, c )
var p=Index(s, c);
    return IIF(p==0,"",SubStr(s,p+1));
END;


/* Возвращает дату 'd'
   в виде строки для отчета
*/
MACRO DateStr( d )
var s;
   if (d==Date(0,0,0))
       DateSplit ({curdate},null,null,s);
       s = "\"___\"__________ "+ s + " г.";
   else
       s = String(d:m);
   end;
   return s;
END;


/* дополнение строки слева нулями до нужной длины,
   если длина строки больше нужной, строка обрезается слева
*/
MACRO StrTrimLeft( num, len )
VAR
   s=trim(string(num)),
   l=strlen(s);

   if(l==len)
      /*возвращаем без изменений*/;
   elif (l>len)
      /*обрезаем*/
      s=SubStr(s,l-len+1,len);
   else
      /*дополняем*/
      s=mkstr("0",len-l)+s;
   end;

   return s;
END;


