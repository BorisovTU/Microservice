/*
$Name:        msfo_b.mac
$Module:      МБ Кредиты
$Description: Проводки шага "Перенос процентов по сделкам размещения с внебаланса на баланс"
*/

/*-------------------------------------------------------------------------
  Имя файла        : msfo_b.mac

  Описание         : Проводки шага "Перенос процентов по сделкам размещения с внебаланса на баланс"

  Программист      : 

  Создан           : 16.11.2018
-------------------------------------------------------------------------*/
IMPORT OprInter, mmcnst_j, mmlibr, mmlb_j, mmcarry;

PRIVATE VAR Currency, // Валюта сделки (leg.PFI)
            FirstDoc = NULL;

PRIVATE MACRO Проводка(
   КатегорияДебет, СчДебет, КатегорияКредит, СчКредит,
   Глава, Сумма, Основание, ExecDate,
   ВалДебет, ВалКредит)

   var at = MM_Carry;

   MM_ByDefault(ВалДебет,  Currency);
   MM_ByDefault(ВалКредит, Currency);

   at.date_carry         = ExecDate;
   at.PrimaryDoc         = FirstDoc;
   at.AccountPayer       = СчДебет;
   at.AccountReceiver    = СчКредит;
   at.CatPayer           = КатегорияДебет;
   at.CatReceiver        = КатегорияКредит;
   at.FIIDPayer          = ВалДебет;
   at.FIIDReceiver       = ВалКредит;
   
   IF (at.CatPayer == tdr_corespacc_vb)
      at.FIIDPayer       = NATCUR;
      at.FiRolePayer     = FIROLE_CORACC_ACTIVE;
   END;
   
   IF (at.CatReceiver == tdr_corespacc_vb)
      at.FIIDReceiver    = NATCUR;
      at.FiRoleReceiver  = FIROLE_CORACC_ACTIVE;
   END;
   
   IF (ВалДебет == Currency)
      at.SumPayer        = Сумма;
   ELIF (ВалКредит == Currency)
      at.SumReceiver     = Сумма;
   ELSE
      RETURN 1;
   END;

   at.Chapter            = Глава;
   at.Ground             = Основание;

   IF (at.carry())
      SetParm(1, at.AccountPayer);
      SetParm(3, at.AccountReceiver);
      RETURN 0;
   ELSE
      RETURN 1;
   END;
END;

MACRO ExecuteStep(Buffer, Document)
var stat          = 0,
    ExecDate      = NULL,
    Account       = "",
    ТребСрочВб    = NULL,
    ТребПросрочВб = NULL,
    error         = "";

   record deal(dl_tick);

   SetBuff(deal, Document);

   FirstDoc = MMFirstDoc(DL_IBCDOC, deal.DealID, true);

   ExecDate = {curdate};

   IF (not IsSALE(deal.DealGroup))
      RETURN 0;
   END;
   
   Currency = FirstDoc.GetLeg().PFI;
   
   ТребСрочВб    = FirstDoc.GetRestAccount(tdr_perc_vb,    ExecDate, Currency, 3, NULL); // Задолж.% (внебаланс)
   ТребПросрочВб = FirstDoc.GetRestAccount(tdr_expperc_vb, ExecDate, Currency, 3, NULL); // Задолж.%с н.с (внебаланс)
   
   //ТребСрочВб    = FirstDoc.GetRestAccount(tdr_claimR,     ExecDate, Currency, 3, NULL); // +% к погашению
   
   IF (ТребСрочВб > 0)
      // Если не открыт счет по категории учета tdr_claimR - открыть
      FirstDoc.OpenAccount(tdr_perc_vb, Account, false, NULL, NULL, ExecDate, Currency);
   
      IF ( Проводка(tdr_corespacc_vb, "", // ВнебалСчетКорресп
                   tdr_perc_vb, "",      // Задолж.% (внебаланс)
                   3, ТребСрочВб,
                   "Перенос по МСФО-9", 
                   ExecDate, NATCUR, Currency))
          RETURN 1;
      END; 
      
      IF ( Проводка(tdr_claimR, "",              // +% к погашению
                   tdr_percR(deal.TypeDoc), "", // тут должен быть "Переходный фин. рез"
                   1, ТребСрочВб,
                   "Перенос по МСФО-9", 
                   ExecDate, NATCUR, Currency))
          RETURN 1;
      END;
   END;
   
   IF (ТребПросрочВб > 0)
      // Если не открыт счет по категории учета tdr_exppercR - открыть
      FirstDoc.OpenAccount(tdr_expperc_vb, Account, false, NULL, NULL, ExecDate, Currency);
      
      IF ( Проводка(tdr_corespacc_vb, "", // ВнебалСчетКорресп
                   tdr_expperc_vb, "",   // Задолж.%с н.с (внебаланс)
                   3, ТребПросрочВб,
                   "Перенос по МСФО-9", 
                   ExecDate, NATCUR, Currency))
         RETURN 1;
      END;

      IF ( Проводка(tdr_claimR, "",              // +% к погашению
                   tdr_percR(deal.TypeDoc), "", // тут должен быть "Переходный фин. рез"
                   1, ТребПросрочВб,
                   "Перенос по МСФО-9", 
                   ExecDate, NATCUR, Currency))
         RETURN 1;
      END;
      
      IF ( Проводка(tdr_exppercR, "", // +% с н.с.
                   tdr_claimR, "",   // +% к погашению
                   1, ТребПросрочВб,
                   "Перенос по МСФО-9", 
                   ExecDate, NATCUR, Currency))
         RETURN 1;
      END;
   END;
   
   // Закрыть счета tdr_perc_vb, tdr_expperc_vb
   if (FirstDoc.IsExistAccount(tdr_perc_vb, NULL, true, NULL, NULL, ExecDate, Currency))
        FirstDoc.CloseAccount(tdr_perc_vb,    ExecDate, NULL, Currency, true);
   end;
   if (FirstDoc.IsExistAccount(tdr_expperc_vb, NULL, true, NULL, NULL, ExecDate, Currency))
        FirstDoc.CloseAccount(tdr_expperc_vb, ExecDate, NULL, Currency, true);
   end;

   // Запуск проверки полноты и корректности
      // Сумма оборотов по Кт tdr_perc_vb и tdr_expperc_vb == сумме оборотов по Дт tdr_claimR
      // Сумма оборотов по Кт tdr_expperc_vb == сумме оборотов по Дт tdr_exppercR
      // Сумма остатков по счетам tdr_perc_vb и tdr_expperc_vb == 0
   
   // Вывести ошибки в протокол
    
   RETURN 0;
END;

NameMacroFile = "msfo3_MMTRANSFER_J.mac";