/*
$Name:        mmuca_j.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Списание корректировки стоимости КЛ"
*/

IMPORT mmlibr, mmlib, "dlmisc.mac", mmcnst_j, "mmcateg.mac", mmcarry;

// Стандартные процедуры
PRIVATE CONST CorrValDutyStdProc  = "MMARKMSFO.CalcCorrValDuty";

PRIVATE VAR
    CorrValDutyProcName;  // Процедура для расчета ЭПС по сделкам размещения

PRIVATE VAR
    ID_Operation = 0,
    ID_Step      = 0;

PRIVATE MACRO ReadSettings
    VAR stat = 0, flag, userproc;
    GetRegistryValue("MMARK\\ПРОЦЕДУРЫ_МСФО\\ISCORRVALDUTYSTDPROC", V_BOOL, flag, stat);  
    if ((not stat) and (flag == UNSET_CHAR))
        GetRegistryValue("MMARK\\ПРОЦЕДУРЫ_МСФО\\CORRVALDUTYUSERPROC", V_STRING, CorrValDutyProcName, stat);
    else
        CorrValDutyProcName = CorrValDutyStdProc;
    end;
    return stat;
END;

PRIVATE MACRO CalcCorrValDuty(Deal)

    VAR deal_pd = MMFirstDoc(Deal.BOfficeKind, Deal.DealID, true);
    VAR ExecDate = {curdate};
    VAR cmd;
    VAR KLk;
    // Вызов процедуры <Расчет суммы корректировки обязательства по выдаче денежных средств для учета в ОФР>
    cmd = RsdCommand(RslDefCon, "begin ? := " + CorrValDutyProcName + "(?, ?, ?); end;");
    cmd.addParam("retval", RSDBP_RETVAL, V_NUMERIC);
    cmd.addParam("objn",   RSDBP_IN);  cmd.value("objn")   = Deal.DealID;
    cmd.addParam("opn",    RSDBP_IN);  cmd.value("opn")    = Deal.UndistrCostAdj;
    cmd.addParam("opdate", RSDBP_IN);  cmd.value("opdate") = ExecDate;	
    cmd.execute();
    KLk = cmd.value ("retval", V_NUMERIC);
    
    if (KLk == 0)
        msgbox("Нет суммы нераспределенной корректировки стоимости КЛ");
        return false;
    end;
	
    VAR carry = MM_Carry;
    carry.FIIDPayer        = NATCUR;
    carry.FIIDReceiver     = NATCUR;
    carry.Chapter          = 1;
    carry.date_carry       = ExecDate;
    carry.PrimaryDoc       = deal_pd;  

    carry.CatPayer         = tdr_marginP;
    carry.CatReceiver      = tdr_marginR;
    carry.Ground           = "Корректировка, увеличивающая стоимость сделки размещения";

    ConvSum(carry.SumReceiver, KLk, {curdate}, Deal.LimitCur, NATCUR);
    round(carry.SumReceiver, 2);
    if (not carry.carry())
        return false;
    end;

    Deal.UndistrCostAdj = Deal.UndistrCostAdj - KLk;
    return true;
END;

/*----------------------------------------------------------------------------
    Выполнение шага
----------------------------------------------------------------------------*/
MACRO ExecuteStep(Buffer, Document, DocKind, IDOperation, IDStep)
    record deal( dl_tick );
    SetBuff( deal, Document );

    ID_Operation = IDOperation;
    ID_Step      = IDStep;

    if (ReadSettings())
        mm_error("Ошибка при работе с реестром");
        return 1;
    end;

    if (not CalcCorrValDuty(deal))
        return 1;
    end;

    copy (Buffer, deal);

    return 0;
END;

NameMacroFile = "mmuca_j.mac";
