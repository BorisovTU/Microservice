/* ───────────────────────────────────────────────────────────────────────────┐
  RS-Bank                                        R-Style Software Lab Ltd
  Файл подсистемы "МБК"

  Печать отчета "Лист неттинга по рынку МБК" (RASO010U).

CREATED : 20.01.00 LA
MODIFICATIONS:
NOTES:

└─────────────────────────────────────────────────────────────────────────── */
/*                             Настройка                                     */

VAR ПоказыватьОтложенные = FALSE; /* ?Показывать отложенные сделки */

/* ------------------------------------------------------------------------- */
import BankInter, PaymInter, CTInter, PTInter, OprInter, DealsInter, dlreport;

FILE deal( dl_tick );
FILE movements( pmpaym )  key 10; /* DocKind + ValueDate */

FILE fininstr( fininstr );
FILE party( party );

FILE wrk( mmrpnett )      normal; /* Временный файл для сохранения данных */

FILE pers( person );

/* ------------------------------------------------------------------------- */
/* GLOBALS */

VAR wrk_fileName = GetWorkFileName("mmrpnett");

/* Параметры, переданные в printReport из панели настройки */
VAR
rep_groupID = 0,             /* ID категории контрагента */
rep_attrID  = 0,             /* ID выбранного элемента в категории контрагента */
rep_date    = date( 0,0,0 ), /* Дата валютирования */
rep_FI      = 0,             /* FIID валюты, по которой выпускается отчет */
rep_exclude = FALSE;         /* Признак исключения валюты */

/* ------------------------------------------------------------------------- */
MACRO getNameOper
 pers.Oper = {oper};

 if( getEQ( pers ) )
  return pers.Name;
 end;

 return "";
END;

/* Получить сокращенное наименование контрагента - для печати */
MACRO getPartyShortName( p_id )
 party.PartyID = p_id;

 if( not getEQ( party ) )
  return "";
 end;
 return party.ShortName;
END;

/* ......................................................................... */

/* Фильтр по сделкам */
MACRO deal_is_correct

var  l_res;

 deal.DealID = movements.DocumentID;
 if( not getEQ( deal ) )
  return FALSE;
 end;

 if( ( deal.DealStatus == DL_CLOSED ) or /* закрытая сделка */
     ( ( not ПоказыватьОтложенные ) and
       ( deal.DealStatus != DL_READIED ) ) ) /* ?отложенные сделки */

  return FALSE;
 end;

 if( rep_attrID == 0 ) /* категория контрагента не задана */
  return TRUE;
 end;
       
 party.PartyID = deal.PartyID;

/* ( deal.Product == p_product ) */
 return ( ( getEQ( party ) ) and
          ( CheckObjAttrPresence( l_res, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ), rep_groupID, rep_attrID ) ) and
          ( l_res ) ); /* Принадлежит ли контрагент к заданной категории */
END;

/* Формирование выборки и запись в рабочий файл */
MACRO getReportData

var l_cont,
    l_DDir, l_MDir,
    l_rg;

 /* Создать и открыть рабочий файл для записи */
 if( not create( wrk , wrk_filename ) )
  msgbox( "Невозможно создать рабочий файл ", wrk_filename );
  exit(1);
 end;

 if( not open( wrk, wrk_filename ) )
  msgbox( "Невозможно открыть рабочий файл ", wrk_filename );
  exit(1);
 end;

 /* Выбрать и записать данные */
 movements.DocKind   = 102; /* сделки МБК */
 movements.ValueDate = rep_date;

 l_cont = getGE( movements );

 while( ( l_cont ) and
        ( movements.DocKind   == 102 ) and
        ( movements.ValueDate == rep_date ) )

  if( ( trim( movements.Netting ) != "" ) and
      ( ( rep_FI == -1 ) or
        ( ( movements.FIID == rep_FI ) != rep_exclude ) ) and
      ( movements.Purpose != PM_PURP_PERCENT ) and
      ( deal_is_correct ) )

   l_DDir = " "; /* Символьные обозначения направлений сделки и платежа */
   l_MDir = " "; /* соответственно */

   l_gr = getOperationGroup( deal.DealType );

   if( isSale( l_gr ) )
    l_DDir = "X";
   end;

   if( movements.Payer=={OurBank} )
    l_MDir = "X";
   end;

   wrk.FIID         = movements.FIID;
   wrk.Counterparty = deal.PartyID;
   wrk.DDirection   = l_DDir;
   wrk.MDirection   = l_MDir;
   wrk.Volume       = abs( movements.Amount );

   insert( wrk );
  end;

  l_cont = next( movements );
 end;
END;

/* ......................................................................... */

/* Конвертация в STRING, если нулевое значение, то "" */
MACRO str( p_value )
 if( p_value <= 0 )
  return "";
 end;
 return string( p_value );
END;

/* Печать заголовка отчета для заданной валюты */
MACRO printHeader( p_catgname, p_FIID )
[                                                                                                                        ];
[                                                                                                                        ];
[                                                                                                                        ];
[                                                                                       ];
[                              В БУХГАЛТЕРИЮ МОБ СБ РФ                                  ];
[                                                                                       ];
[                                                                                       ];
[                                                                                       ];
[                      СВОДНОЕ РАСПОРЯЖЕНИЕ ПО ИТОГАМ НЕТТИНГА                          ];
[                                                                                       ];
[                                                                                       ];
[      тип сделок            категория контрагентов     дата валютирования       валюта ];
[┌──────────────────────┐   ┌──────────────────────┐   ┌──────────────────┐     ┌──────┐];
[│######################│   │######################│   │##################│     │######│]( "Все":c, p_catgname:c, rep_date:c, DL_getCCode( p_FIID ):c );
[└──────────────────────┘   └──────────────────────┘   └──────────────────┘     └──────┘];
[                                                                                                                        ];
[                                                                                                                        ];
[┌──────────────────────┬─────────────────────────────────────────┬─────────────────────────────────────────┬─────────────────────────────────────────┐];
[│      Контрагент      │               Привлечение               │               Размещение                │                  Итого                  │];
[│                      ├────────────────────┬────────────────────┼────────────────────┬────────────────────┼────────────────────┬────────────────────┤];
[│                      │     Требования     │    Обязательства   │     Требования     │    Обязательства   │     Требования     │    Обязательства   │];
END;

MACRO printFooter( p_rec )
[└──────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤];
[                 Всего │####################│####################│####################│####################│####################│####################│]
( str( p_rec(0) ):r, str( p_rec(1) ):r, str( p_rec(2) ):r, str( p_rec(3) ):r, str( p_rec(4) ):r, str( p_rec(5) ):r );
[                       └────────────────────┴────────────────────┴────────────────────┴────────────────────┼────────────────────┼────────────────────┤];
[                                                                                              Баланс нетто │####################│####################│]
( str( p_rec(4) - p_rec(5) ):r, str( p_rec(5) - p_rec(4) ):r );
[                                                                                                           └────────────────────┴────────────────────┘];
[                                                                                                                        ];
[                                                                                                                        ];
[  Руководитель                                                       _______________________   #                        ]( {FIO_Boss}:l );
[                                                                             подпись           Ф. И. О.                 ];
[                                                                                                                        ];
[  Начальник отдела обеспечения сделок Управления ресурсов МОБ СБ РФ  _______________________                            ];
[                                                                             подпись           Ф. И. О.                 ];
[                                                                                                                        ];
[  Исполнитель                                                        _______________________   #                        ]( getNameOper:l );
[                                                                             подпись           Ф. И. О.                 ];
[                                                                                                                        ];
[                                 ┌───────────────────────────────┐                                                      ];
[  Отметка бухгалтерии о приёме   │                               │                                                      ];
[                                 └───────────────────────────────┘   _______________________                            ];
[                                  дата и время передачи документа            подпись           Ф. И. О.                 ];
[                                                                                                                        ];
[                                                                                                                        ];
[                                                                                                                        ];
[                                                                                                                        ];
END;

/* ------------------- Печать отчета = Точка входа ------------------------- */
MACRO printReport( p_catgname, p_product, p_groupID, p_attrID, p_VD, p_FIID, p_exclude,
                   p_paymFilter )
 array l_rec,   /* массив для записи значений по текущему контрагенту */
       sum_rec; /* массив для суммирования значений по всем контрагентам */

var l_party, l_currency;

 /* Название категории для печати */
 if( trim( p_catgname ) == "" )
  p_catgname = "Все";
 end;

 /* Установить значения глобальных переменных равными принятым параметрам */
 rep_groupID = p_groupID; /* ID категории контрагента */
 rep_attrID  = p_attrID;  /* ID выбранного элемента в категории контрагента */
 rep_date    = p_VD;      /* Дата валютирования */
 rep_FI      = p_FIID;    /* FIID валюты, по которой выпускается отчет */
 rep_exclude = p_exclude; /* Признак исключения валюты */

 /* Далее */
                        
 l_party    = 0; /* текущий контрагент */
 l_currency = 0; /* текущая валюта */
 sum_rec(0) = sum_rec(1) = sum_rec(2) = sum_rec(3) = sum_rec(4) = sum_rec(5) = 0;
 l_rec(0)   = l_rec(1)   = l_rec(2)   = l_rec(3)   = 0;

 MACRO printPartyData
[├──────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤];
[│######################│####################│####################│####################│####################│####################│####################│]
( getPartyShortName( l_party ), str( l_rec(0) ):r, str( l_rec(1) ):r, str( l_rec(2) ):r, str( l_rec(3) ):r, str( l_rec(0) + l_rec(2) - l_rec(1) - l_rec(3) ):r, str( l_rec(1) + l_rec(3) - l_rec(0) - l_rec(2) ):r );

  l_party = wrk.Counterparty;

  var i = 0, l_ind;

  while( i <= 3 )
   sum_rec(i) = sum_rec(i) + l_rec(i);

   i = i+1;
  end;
  sum_rec(4) = sum_rec(4) + max( 0, l_rec(0) + l_rec(2) - l_rec(1) - l_rec(3) );
  sum_rec(5) = sum_rec(5) + max( 0, l_rec(1) + l_rec(3) - l_rec(0) - l_rec(2) );
  l_rec(0)   = l_rec(1)   = l_rec(2)   = l_rec(3)   = 0;
 END;

 /* Получить данные */
 getReportData;

 /* Считать и распечатать */
 rewind( wrk );
 if( not next( wrk ) ) /* Рабочий файл пуст - заканчиваем работу */
  println( "Нет данных (платежей и/или сделок), \nудовлетворяющих заданным параметрам отчета." );
  close( wrk );
  delfile( wrk_filename );
  return TRUE;
 end;

 l_party    = wrk.Counterparty; /* текущий контрагент */
 l_currency = wrk.FIID;
 printHeader( p_catgname, wrk.FIID );

 rewind( wrk );
 while( next( wrk ) )

  /* Если конрагент или валюта сменились, распечатать накопленные данные */
  if( ( wrk.Counterparty != l_party ) or
      ( wrk.FIID         != l_currency ) )
   printPartyData;
  end;

  /* Если валюта сменилась, печатаем */
  if( wrk.FIID != l_currency )
   printFooter( sum_rec );
   l_currency = wrk.FIID;
   printHeader( p_catgname, wrk.FIID );
   sum_rec(0) = sum_rec(1) = sum_rec(2) = sum_rec(3) = sum_rec(4) = sum_rec(5) = 0;
  end;

  /* Записать значения по текущей записи */
  l_ind = 0; /* индекс, по которому будет записано считанное значение */

  if( wrk.DDirection == "X" ) /* продажа */
   l_ind = 2;
  end;

  if( wrk.MDirection == "X" ) /* обязательство */
   l_ind = l_ind + 1;
  end;

  l_rec(l_ind) = l_rec(l_ind) + wrk.Volume;
 end;

 printPartyData;
 printFooter( sum_rec );

 close( wrk );
 delfile( wrk_filename );
 return TRUE; /* Вывод печатной формы на экран */
END;

/* EoF */