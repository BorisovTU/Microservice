IMPORT BankInter, MMarkInter, mmordnum, mmlib;

RECORD ДоговорЗалога(dl_order);
RECORD СтарыйДоговорЗалога (dl_order);

MACRO ФункцияПользователя_ДоговорЗалога()
    msgbox(String("Выполняется макрофункция с именем",
                  "|ФункцияПользователя,",
                  "|которая определена в файле",
                  "|mmscz.mac",
                  "|договор залога"));
    return 0;
END;

/*
    В функции проверки макросом используются следующие константы

    MM_MAC_INIT,         // инициализация нового договора
    MM_MAC_GETNUMBER,    // генерация номера
    MM_MAC_USER,         // зверская функция
    MM_MAC_INSERT,       // проверки перед вставкой
    MM_MAC_UPDATE,       // проверки перед обновлением
    MM_MAC_DELETE,       // проверки перед удалением
    MM_MAC_COMPLETE,     // сообщаем в макрос, что транзакция прошла успешно
    MM_MAC_NOTCOMPLETE,  // а тут о том, что где-то ошибка в ней
    MM_MAC_CREATEOP,     // проверки перед созданием операции
    MM_MAC_DELETEOP,     // проверки перед переводом в отложенные
    MM_MAC_MASSACTION    // ругнемся в макрос о начале или окончании массового режима
*/
MACRO Проверить_ДоговорЗалога (Режим)
    var stat = 0;

    if (Режим == MM_MAC_INIT)    
        // можем менять макет сделки, изменения будут учтены
        // возвращаемое значение не влияет
    elif (Режим == MM_MAC_GETNUMBER)    
        // можем менять макет сделки, изменения будут учтены
        stat = MM_GetOrderNumber(ДоговорЗалога);
    elif (Режим == MM_MAC_USER)    
        // изменения макета не будут учтены
        // возвращаемое значение не влияет
        stat = ФункцияПользователя_ДоговорЗалога();
    elif (Режим == MM_MAC_INSERT)    
        // 0 - проверка пошла успешно. не 0 - ошибка (ругаемся сами)
        if (ДоговорЗалога.Expiry == date(0,0,0))
            msgbox("Дата окончания договора некорректна");
            return 1;
        elif (ДоговорЗалога.Expiry < ДоговорЗалога.CreateDate)
            msgbox("Дата окончания меньше даты начала");
            return 1;
        elif (ДоговорЗалога.SignDate == date(0,0,0)) 
            msgbox("Не задана дата подписания договора");
            return 1;
        end;

        stat = ПроверитьНомерДоговора(ДоговорЗалога);

        if (stat == 106)
            msgbox("Договор с таким номером уже введен");
        elif (stat == 101)
             if( GetTrue( true, "Номер договора не указан.|Сформировать все номера автоматически?") ) 
                 stat = ExecMacro2(@Проверить_ДоговорЗалога, MM_MAC_GETNUMBER); 
             end;
        end;
    elif (Режим == MM_MAC_UPDATE)    
        // 0 - проверка пошла успешно. не 0 - ошибка (ругаемся сами)
        if (ДоговорЗалога.Expiry == date(0,0,0))
            msgbox("Дата окончания договра некорректна");
            return 1;
        elif (ДоговорЗалога.Expiry < ДоговорЗалога.CreateDate)
            msgbox("Дата окончания меньше даты начала");
            return 1;
        elif (ДоговорЗалога.SignDate == date(0,0,0)) 
            msgbox("Не задана дата подписания договора");
            return 1;
        elif (ДоговорЗалога.OrderNumber != СтарыйДоговорЗалога.OrderNumber)
            stat = ПроверитьНомерДоговора(ДоговорЗалога);

            if (stat == 106)
                msgbox("Договор с таким номером уже введен");
            end;
        end;
    elif (Режим == MM_MAC_DELETE)    
        // 0 - проверка пошла успешно. не 0 - ошибка (ругаемся сами)
    elif (Режим == MM_MAC_COMPLETE)    
        // сообщение придет, когда мы в транзакции вставки или редактирования
        // 0 - завершение транзакции. не 0 - откат
    elif (Режим == MM_MAC_NOTCOMPLETE)    
        // сообщение придет, когда мы в транзакции вставки или редактирования
        // возвращаемое значение не влияет
    elif (Режим == MM_MAC_CREATEOP)    
        // 0 - проверка пошла успешно. не 0 - ошибка (ругаемся сами)
    elif (Режим == MM_MAC_DELETEOP)    
        // 0 - проверка пошла успешно. не 0 - ошибка (ругаемся сами)
    elif (Режим == MM_MAC_MASSACTION)    
        // 1 раз приходит при вызове групповых действий над договорами
        // 2 раз - при завершении групповых действий
    end;

    return stat;
END;
