/*
$Name: mmprconf320.mac
$Module: МБК
$Description: Печать подверждения сделки МБК
*/

/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v5.1
                 Copyright (c) R-Style Software Lab

  Имя файла        : mprconf320.mac

  Библиотека       : 

  Описание         : Печать подверждения сделки МБК в формате MT320

  Программист      : Vrubel Vyacheslav (VVL)

  Создан           : 11.02.2019
-------------------------------------------------------------------------*/
import mmlibr, mmlib, swgm320;

PRIVATE VAR tick = TBfile("dl_tick"),
            leg = TBfile("dl_leg"),
            genagr = TBfile("dl_genagr"),
            ExecDate;

PRIVATE FILE bicdir( ptbicdir ) key 0;
PRIVATE FILE party( party ) key 0;

const PRINT_SHIFT = "                     ";

class PMInfo
  var
    CodeKind = 0, 
    Code = "", 
    Name = "", 
    BankCodeKind = 0, 
    BankCode = "", 
    BankName = "", 
    Account = "", 
    CorrCodeKind = 0, 
    CorrCode = "", 
    CorrName = "", 
    CorrAccount = "";
end;

PRIVATE MACRO GetSubjectName(PartyID)
  var dparty = TBFile("party");
  if (PartyID == 1)
    PartyID = {OurBank};
  end;
  dparty.rec.PartyID = PartyID;
  if( dparty.GetEQ() )
    return dparty.rec.Shortname;
  end;
  return "";
END;

/* Записать поле */
PRIVATE MACRO ЗаписатьБанк( BlockName, PartyID, CodeKind, CodeValue, BankName, CorrAccount, isCorr, SubjID )
  var field_value = "", ID, BankCode, error, INN = "";

  /* Только по наименованию пока банк не заполняем */
  if( (PartyID != 0) OR ((CodeKind != 0) AND (CodeValue != "")) )
    if( (CodeKind != 0) AND (CodeValue != "") )
      ID = ПолучитьКодСубъекта( CodeValue, CodeKind, error );
      if( error ) mm_error( "|Не найден банк c кодом: " + CodeValue + " и видом кода: " + CodeKind ); return 1; end;
    else
      ID = PartyID;
    end;

    if (ID == 1)
      ID = {OurBank};
    end;

    /* Ищем для субъекта SWIFT-код. Если не нашли - отваливаем,   */
    /* так как настоятельно рекомендуется использовать именно его */
    if (leg.rec.PFI == NATCUR)
      BankCode = ПолучитьКодСубъекта( ID, PTCK_BIC, error ); CodeKind = PTCK_BIC;
      if( error ) mm_error( "|Не найден BIC-код для банка с кодом: "+ CodeValue + " и видом кода: " + CodeKind ); return 1; end;
    else
      BankCode = ПолучитьКодСубъекта( ID, PTCK_SWIFT, error ); CodeKind = PTCK_SWIFT;
      if( error ) BankCode = ПолучитьКодСубъекта( ID, PTCK_BIC, error ); CodeKind = PTCK_BIC; end;
      if( error ) mm_error( "|Не найден BIC-код для банка с кодом: "+ CodeValue + " и видом кода: " + CodeKind ); return 1; end;
    end;
    
    if (BankCode != "")
      if (CodeKind == PTCK_BIC)
        BankCode = "    " + "BIC " + BankCode;
      else
        BankCode = "    " + "SWIFT " + BankCode;
      end;
    end;

    if ((ValType(isCorr) == V_UNDEF) or (not isCorr))
      if( CorrAccount != "" )
        field_value = field_value + SYMB_SLASH + CorrAccount + SYMB_ENDL;
      else
        field_value = field_value + "ACC YOUR INSTRUCTION" + SYMB_ENDL;
      end;
      field_value = field_value + BankCode;
      SetParm(7, ID);
    else
      if( CorrAccount != "" )
        field_value = field_value + "CA " + CorrAccount + SYMB_ENDL;
      end;
      INN = ПолучитьКодСубъекта( SubjID, PTCK_INN, error );
      if (not error)
        field_value = field_value + "    " + "INN " + INN;
      end;
    end;
    if (ValType(BlockName) != V_UNDEF)
      [#############################################] (BlockName + ":");
    end;
    [    ####################################################] (field_value);
  end;

  return 0;
end;

/* Записать поле */
PRIVATE MACRO ЗаписатьБанкВалюта( BlockName, PartyID, CodeKind, CodeValue, BankName, CorrAccount )
  var field_value = "", ID, BankCode, error, INN = "", SubjectName = "";

  /* Только по наименованию пока банк не заполняем */
  if( (PartyID != 0) OR ((CodeKind != 0) AND (CodeValue != "")) )
    if( (CodeKind != 0) AND (CodeValue != "") )
      ID = ПолучитьКодСубъекта( CodeValue, CodeKind, error );
      if( error ) mm_error( "|Не найден банк c кодом: " + CodeValue + " и видом кода: " + CodeKind ); return 1; end;
    else
      ID = PartyID;
    end;

    if (ID == 1)
      ID = {OurBank};
    end;

    /* Ищем для субъекта SWIFT-код. Если не нашли - отваливаем,   */
    /* так как настоятельно рекомендуется использовать именно его */
    BankCode = ПолучитьКодСубъекта( ID, PTCK_SWIFT, error ); CodeKind = PTCK_SWIFT;
    if( error ) BankCode = ПолучитьКодСубъекта( ID, PTCK_BIC, error ); CodeKind = PTCK_BIC; end;
    if( error ) mm_error( "|Не найден BIC-код для банка с кодом: "+ CodeValue + " и видом кода: " + CodeKind ); return 1; end;
    
    if (BankCode != "")
      if (CodeKind == PTCK_BIC)
        BankCode = "    " + "BIC " + BankCode;
      else
        BankCode = "    " + BankCode;
      end;
    end;

    if( CorrAccount != "" )
      field_value = field_value + SYMB_SLASH + CorrAccount + SYMB_ENDL;
    end;
    field_value = field_value + BankCode;

    SubjectName = GetSubjectName(ID);
    if (SubjectName != "")
      field_value = field_value + SYMB_ENDL + "    " + SubjectName;
    end;

    if (ValType(BlockName) != V_UNDEF)
      [#############################################] (BlockName + ":");
    end;
    [    ##############################################################################################################] (field_value);
  end;

  return 0;
end;

/* Получить курс в формате SWIFT */
PRIVATE MACRO FormatRate( Rate )
  var SWIFTRate = String(Rate);

  if( index( SWIFTRate, "." ) )
    SWIFTRate = GetSWIFTAmount( Rate );
  end;
  
  /* Обрезаем все нули справа */
  while( SubStr( SWIFTRate, StrLen(SWIFTRate), 1 ) == "0" )    
    SWIFTRate = substr( SWIFTRate, 1, StrLen(SWIFTRate)-1 );
  end;
  
  return SWIFTRate;
END;

PRIVATE MACRO PrintDeal()
  VAR BIC_A, BIC_B, field_value, error, Rate, PriceStr, IntPart, FactPart, Letter = "", PartyID_A, PartyID_B, TypeEvent;

  if (isBuy(tick.rec.DealGroup))
    BIC_A = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
    if( error ) BIC_A = ПолучитьКодСубъекта( {OurBank}, PTCK_BIC, error ); end;
    if( error ) mm_error( "|Не найден BIC-код банка нашего банка" ); return false; end;
    BIC_B = ПолучитьКодСубъекта( tick.rec.PaymAgent, PTCK_SWIFT, error );
    if( error ) BIC_B = ПолучитьКодСубъекта( tick.rec.PaymAgent, PTCK_BIC, error ); end;    
    if( error ) mm_error( "|Не найден BIC-код банка-контрагента по сделке" ); return false; end;
    PartyID_A = {OurBank};
    PartyID_B = tick.rec.PaymAgent;
  else
    BIC_B = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
    if( error ) BIC_B = ПолучитьКодСубъекта( {OurBank}, PTCK_BIC, error ); end;
    if( error ) mm_error( "|Не найден BIC-код банка нашего банка" ); return false; end;
    BIC_A = ПолучитьКодСубъекта( tick.rec.PaymAgent, PTCK_SWIFT, error );
    if( error ) BIC_A = ПолучитьКодСубъекта( tick.rec.PaymAgent, PTCK_BIC, error ); end;    
    if( error ) mm_error( "|Не найден BIC-код банка-контрагента по сделке" ); return false; end;    
    PartyID_B = {OurBank};
    PartyID_A = tick.rec.PaymAgent;
  end;

  /* 17R Party's A Role */
  if (isBuy(tick.rec.DealGroup))
    field_value = PARTYS_ROLE_BORROWER; 
  else
    field_value = PARTYS_ROLE_LENDER;   
  end;

  if(isSale(tick.rec.DealGroup))
    Letter = NEGATIVE_SYMB;
  end;

  /* Interest Rate - записываем значение с учетом того, что ставка хранится в двух полях */
  PriceStr = SubStr( String(leg.rec.Price), 1, Index( String(leg.rec.Price), "." ) - 1 );
  if( leg.rec.Point < StrLen(PriceStr) )
    IntPart = SubStr( PriceStr, 1, StrLen(PriceStr) - leg.rec.Point );
  else
    IntPart = "0";
  end;
  if( (leg.rec.Point + 1) > StrLen(PriceStr) )
    FactPart = MkStr( "0", leg.rec.Point - StrLen(PriceStr)) + PriceStr;
  else
    FactPart = SubStr( PriceStr, StrLen(PriceStr)-leg.rec.Point + 1 );
  end;
  Rate = FormatRate( String(IntPart, ",", FactPart) ); 

  if (StepIsExecute(tick.rec.DealID, DL_IBCDOC, "с")) 
    TypeEvent = TYPE_EVENT_ROLL;
  elif (StepIsExecute(tick.rec.DealID, DL_IBCDOC, "О"))
    TypeEvent = TYPE_EVENT_MATU;
  else
    TypeEvent = TYPE_EVENT_CONF;
  end;

  [15A:                                         ];
  [20 :#######################                  ] (string(tick.rec.DealID));
  [22A:#######################                  ] (TYPE_OPERATION_NEWT);
  [22B:##########                               ] (TypeEvent);
  [22C:#######################                  ] (tick.rec.DealCodePS);
  [21N:#######################                  ] (tick.rec.DealCode);
  [82A:#######################                  ] (BIC_A);
  [87A:#######################                  ] (BIC_B);
  if (tick.rec.GenAgrID > 0)
    [77D:#######################                  ] (genagr.rec.Code);
  end;
  [15B:                                         ];
  [17R:#                                        ] (field_value);
  [30T:########                                 ] (GetSWIFTDateFull(tick.rec.DealDate));
  [30V:########                                 ] (GetSWIFTDateFull(leg.rec.Start));
  [30P:########                                 ] (GetSWIFTDateFull(leg.rec.Maturity));
  [32B:#######################                  ] (ПолучитьКодВалютыЛог(leg.rec.PFI) + GetSWIFTAmount(leg.rec.Principal));
  [30X:########                                 ] (GetSWIFTDateFull(GetPercDate(tick.rec.DealID)));
  [34E:#######################                  ] (Letter + ПолучитьКодВалютыЛог(leg.rec.PFI) + GetSWIFTAmount(leg.rec.Cost));
  [37G:##########                               ] (Rate);

  /* 14D Day Count Fraction */
  if( leg.rec.Basis == RS_BASIS_30360 )   /* 360 дней в году, 30 в месяце */
    field_value = SWIFT_BASIS_360_360;
  elif(leg.rec.Basis == RS_BASIS_ACTACT)  /* в году и в месяце по календарю */
    field_value = SWIFT_BASIS_ACT_365;
  elif(leg.rec.Basis == RS_BASIS_ACT360)  /* 360 дней в году, в месяце по календ. */
    field_value = SWIFT_BASIS_ACT_360;
  elif(leg.rec.Basis == RS_BASIS_ACT360)  /* в году и в месяце по календарю без учета високосности */
    field_value = SWIFT_BASIS_ACT_360;
  else                                   /* 360 дней в году, 31 в месяце */
    field_value = SWIFT_BASIS_360_360;
  end;

  [14D:########                     ] (field_value);
END;

PRIVATE MACRO FillPI(Purpose, PMInfoObj)
    var pmrmprop = TBFile("pmrmprop");
    var pmprop   = TBFile("pmprop");
    var partcode = TBFile("partcode");
    var corschem = TBFile("corschem", "R", 1);
    var rs = TRsbDataSet("select * from dpmpaym_dbt where " +
                     "t_DocKind = 102 and t_DocumentID = " + tick.rec.DealID +
                     " and t_Purpose = " + Purpose);

    if (not rs.MoveNext())
      mm_error( "Не найден платеж с назначением ", Purpose );
      return 1;
    end;
    PMInfoObj.CodeKind = rs.ReceiverCodeKind;
    PMInfoObj.Code = rs.ReceiverCode;
    PMInfoObj.Account = rs.ReceiverAccount;

    pmrmprop.rec.PaymentID = rs.PaymentID;
    if (pmrmprop.GetEQ())
      PMInfoObj.Name = pmrmprop.rec.ReceiverName;
      PMInfoObj.BankName = pmrmprop.rec.ReceiverBankName;
      PMInfoObj.CorrName = pmrmprop.rec.ReceiverCorrBankName;

      pmprop.rec.PaymentID = rs.PaymentID;
      pmprop.rec.DebetCredit = 1;
      if (pmprop.GetEQ())
        if (pmprop.rec.Group == PAYMENTS_GROUP_EXTERNAL)
          PMInfoObj.BankCodeKind = pmprop.rec.CodeKind;
          PMInfoObj.BankCode     = pmprop.rec.BankCode;
          PMInfoObj.CorrCodeKind = pmprop.rec.CorrCodeKind;
          PMInfoObj.CorrCode     = pmprop.rec.CorrCode;
          PMInfoObj.CorrAccount  = pmprop.rec.CorrAcc;
        else
          PMInfoObj.BankCodeKind = PTCK_BIC;
          partcode.rec.PartyID = {ourbank};
          partcode.rec.CodeKind = PMInfoObj.BankCodeKind;
          if (partcode.GetEQ())
            PMInfoObj.BankCode = partcode.rec.Code;

            pmprop.rec.DebetCredit = 0;
            if (not pmprop.GetEQ())
              return false;
            end;
            corschem.rec.FI_Kind = 1;
            corschem.rec.FIID = pmprop.rec.PayFIID;
            corschem.rec.Number = pmprop.rec.Corschem;
            if (not corschem.GetEQ())
              SetParm(1, PMInfoObj);
              return false;
            end;
            PMInfoObj.CorrCodeKind = PTCK_BIC;
            partcode.rec.PartyID = corschem.rec.CorrID;
            partcode.rec.CodeKind = PMInfoObj.CorrCodeKind;
            if (not partcode.getEQ())
              return false;
            end;
            PMInfoObj.CorrCode = partcode.rec.Code;
            PMInfoObj.CorrAccount = corschem.rec.CorAccount;

          else 
            return false;
          end;
        end;
      else
        return false;
      end;
    else
      return false;
    end;

    SetParm(1, PMInfoObj);
    return true;
END;

PRIVATE MACRO PrintAcc(PMInfoObj, PartyID)
  var SubjID = 0;
  // сделка пролонгирована
  if (StepIsExecute(tick.rec.DealID, DL_IBCDOC, "с"))
    if (leg.rec.PFI == NATCUR)
      [57D:ROLL                         ];
    else
      [57A:ROLL                         ];
    end;
    return;
  end;

  if (leg.rec.PFI == NATCUR)
    ЗаписатьБанк( "57D", PartyID, PMInfoObj.BankCodeKind, PMInfoObj.BankCode, 
      PMInfoObj.BankName, PMInfoObj.Account, false, SubjID );
    ЗаписатьБанк( NULL, 0, PMInfoObj.CorrCodeKind, PMInfoObj.CorrCode, 
      PMInfoObj.CorrName, PMInfoObj.CorrAccount, true, SubjID);
  else
    ЗаписатьБанкВалюта( "57A", PartyID, PMInfoObj.BankCodeKind, PMInfoObj.BankCode, 
      PMInfoObj.BankName, PMInfoObj.Account);
  end;
    
END;

PRIVATE MACRO PrintPayments()
  var PaymAgent, PrincRetPMInfo = PMinfo, PrincPMInfo = PMInfo, PercentPMInfo = PMInfo;

  FillPI(PM_PURP_PRINC_RET, PrincRetPMInfo);
  FillPI(PM_PURP_PRINC,     PrincPMInfo);

  [15C:                             ];
  if (isBuy(tick.rec.DealGroup))
    PaymAgent = tick.rec.PaymAgent;
  else
    PaymAgent = {OurBank};
  end;
  PrintAcc(PrincRetPMInfo, PaymAgent);

  [15D:                             ];
  if (isBuy(tick.rec.DealGroup))
    PaymAgent = {OurBank};
  else
    PaymAgent = tick.rec.PaymAgent;
  end;
  PrintAcc(PrincPMInfo, PaymAgent);

  /* Платежные инструкции по оплате процентов */
  FillPI(PM_PURP_PERCENT, PercentPMInfo);
  if (isBuy(tick.rec.DealGroup))
    if(((PercentPMInfo.CodeKind     !=  0) AND (PercentPMInfo.CodeKind     != PrincRetPMInfo.CodeKind     )) OR
       ((PercentPMInfo.Code         != "") AND (PercentPMInfo.Code         != PrincRetPMInfo.Code         )) OR
       ((PercentPMInfo.BankCodeKind !=  0) AND (PercentPMInfo.BankCodeKind != PrincRetPMInfo.BankCodeKind )) OR
       ((PercentPMInfo.BankCode     != "") AND (PercentPMInfo.BankCode     != PrincRetPMInfo.BankCode     )) OR
       ((PercentPMInfo.Account      != "") AND (PercentPMInfo.Account      != PrincRetPMInfo.Account      )) OR
       ((PercentPMInfo.CorrCodeKind !=  0) AND (PercentPMInfo.CorrCodeKind != PrincRetPMInfo.CorrCodeKind )) OR
       ((PercentPMInfo.CorrCode     != "") AND (PercentPMInfo.CorrCode     != PrincRetPMInfo.CorrCode     )) OR
       ((PercentPMInfo.CorrAccount  != "") AND (PercentPMInfo.CorrAccount  != PrincRetPMInfo.CorrAccount  )) )

      [15E:                             ];
      PrintAcc(PercentPMInfo, tick.rec.PaymAgent);
    end;
  else
    if(((PercentPMInfo.CodeKind     !=  0) AND (PercentPMInfo.CodeKind     != PrincPMInfo.CodeKind     )) OR
       ((PercentPMInfo.Code         != "") AND (PercentPMInfo.Code         != PrincPMInfo.Code         )) OR
       ((PercentPMInfo.BankCodeKind !=  0) AND (PercentPMInfo.BankCodeKind != PrincPMInfo.BankCodeKind )) OR
       ((PercentPMInfo.BankCode     != "") AND (PercentPMInfo.BankCode     != PrincPMInfo.BankCode     )) OR
       ((PercentPMInfo.Account      != "") AND (PercentPMInfo.Account      != PrincPMInfo.Account      )) OR
       ((PercentPMInfo.CorrCodeKind !=  0) AND (PercentPMInfo.CorrCodeKind != PrincPMInfo.CorrCodeKind )) OR
       ((PercentPMInfo.CorrCode     != "") AND (PercentPMInfo.CorrCode     != PrincPMInfo.CorrCode     )) OR
       ((PercentPMInfo.CorrAccount  != "") AND (PercentPMInfo.CorrAccount  != PrincPMInfo.CorrAccount  )) )

      [15F:                             ];
      PrintAcc(PercentPMInfo, {OurBank});
    end;
  end;
END;

PRIVATE MACRO PrintTax()
  var genagr, taxRate = 0.0, taxAmount = $0, deal_pd;
  deal_pd = MMFirstDoc(tick.rec.BOfficeKind, tick.rec.DealID, true);
  if (isSale(tick.rec.DealGroup))
    taxRate = GetTaxBank(deal_pd.GetTick());
  else
    taxRate = GetTaxContr(deal_pd.GetTick());
  end;
  
  if (TaxRate > 0)
    taxAmount = round(leg.rec.Cost * (taxRate/100), 2);

    [15G:                                         ];
    [37L:##########                               ] (FormatRate(taxRate));
    [33B:##########                               ] (ПолучитьКодВалютыЛог(leg.rec.PFI) + GetSWIFTAmount(leg.rec.Cost - taxAmount));
    [33E:##########                               ] (ПолучитьКодВалютыЛог(leg.rec.PFI) + GetSWIFTAmount(taxAmount));
  end;
END;

MACRO CreateFileName
    var hour,min,sec, Path,
        txtRegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";

    if(MM_SetRegistryPath(txtRegistryPath, Path))
       return "";
    end;

    // получить полный путь
    Path = MMCMT_processPath(Path) + "\\";
    TimeSplit (Time,hour,min,sec);
    return String(Path,{oper},hour,min,sec,".txt");
END;

MACRO PrintConfirmation (DealID, OriginalDealID, OutFile, first)
  ExecDate = {curdate};
  file out() txt;
  var oldOutFile = "";
  if (OutFile != "")
    oldOutFile = SetOutput(OutFile, TRUE);
  else
    OutFile = SetOutput(NULL, TRUE);
    SetOutput(OutFile, FALSE);
  end;
  if (OriginalDealID == null)
    OriginalDealID = 0;
  end;
  tick.KeyNum = 0;
  tick.rec.DealID = DealID;
  if( not GetEq(tick) )
    mm_error( "Не найдена сделка ", DealID );
    return 1;
  end;

  leg.KeyNum = 0;
  leg.rec.LegKind = LEG_KIND_DL_TICK;
  leg.rec.DealID = DealID;
  leg.rec.LegID = 1;
  if( not GetEq(leg) )
    mm_error( "Не найден транш сделки ", tick.rec.DealCode );
    return 1;
  end;

  if (tick.rec.GenAgrID > 0)
    genagr.KeyNum = 0;
    genagr.rec.GenAgrID = tick.rec.GenAgrID;
    if( not GetEq(genagr) )
      mm_error( "Не найдено генеральное соглашение ", tick.rec.DealCode );
      return 1;
    end;
  end;

  if (first == false)
    println("\f");
  end;
  PrintDeal();
  PrintPayments();
  PrintTax();
  if (oldOutFile != "")
    SetOutput(oldOutFile, TRUE);
  else
    SetOutput(OutFile);
  end;

  if (not isOprMultiExec)
    SetOutput(CreateFileName(), FALSE);
    open(out, OutFile);
    ViewFile(out);
  end;
END;

MACRO ViewTheConfirmation(OutFile)
  file out() txt;
  SetOutput(CreateFileName(), FALSE);
  open(out, OutFile);
  ViewFile(out);
END;
