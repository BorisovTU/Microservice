/*
$Name:        mmactacc.mac
$Module:      МБ Кредиты
$Description: макрос выполнения шага "Актуализация счетов"
*/
/*-------------------------------------------------------------------------
  Имя файла        : mmactacc.mac

  Описание         : макрос выполнения шага "Актуализация счетов"

  Программист      : Куперин М.В.

  Создан           : 16.06.2008
-------------------------------------------------------------------------*/
IMPORT mmcateg, mccatacf, mmlib, globals, mmcnst_j, PaymInter, mmlibr, mctplprm, RsbFormsInter, opr_depo, SfInter;

VAR Категории = TArray,
     Роли      = TArray,
     FirstDoc = NULL;

private RECORD deal(dl_tick);

//класс панели параметров отчета
PRIVATE CLASS(TRsbPanel) CCedentSelect(_deal)
    PRIVATE VAR
        fld_Cedent        = NULL, // Цедент
        cedent            = NULL,
        deal              = NULL; // ссылка на сделку

    MACRO Get_Cedent()
        return cedent;
    END;
    MACRO Set_Cedent(newval)
        cedent = newval;
        fld_Cedent.Value = GetPartyName( cedent, false );
    END;
    
    MACRO eventHandler(EV)
        if (EV.keyCode == 316/*K_F2*/)
            close(-EV.keyCode);
        elif (EV.keyCode == 317/*K_F3*/)
            var Party = TRecHandler("party.dbt"), SelCodeKind = 1;
            if( ListPT( Party, SelCodeKind, "", PTLIST_CONTR, PTCK_CONTR) == true )
                cedent = Party.rec.PartyID;
                fld_Cedent.Value = Party.rec.ShortName;
            end;
        end;
    END;
    
    PRIVATE MACRO InitDefValue(deal)
        Set_Cedent(deal.PaymAgent);
    END;

    //constructor
    InitTRsbPanel();
    //caption = "Выбор Цедента";
    setPosition(40, 15);
    setSize(45, 4);
    status = "~F2~ Продолжить ~Esc~ Выход";
    deal = _deal;

    // инициализация полей панели
    fld_Cedent            = TRsbEditField();
    fld_Cedent.name       = "fld_Cedent";
    fld_Cedent.dataType   = 7; //FT_STRING    
    fld_Cedent.editable   = false;
    fld_Cedent.textLength = 61;
    fld_Cedent.setPosition(2, 2);
    fld_Cedent.setSize(42, 1);
    addControl(fld_Cedent);
    addLabel(TRsbLabel(2, 1, "Выберите Цедента из списка субъектов:"));

    addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));

    InitDefValue(deal);
END;


PRIVATE MACRO UpdateSPIForCLAIMSACQ(deal, Cedent)
  var paym       = NULL;

    RECORD settacc(settacc);

    if (not FindSETTACC_PMAUTO(Cedent, FIKIND_CURRENCY, deal.GetLeg().PFI, PTSK_CURRDL, deal.GetTick().DealType, PM_PURP_PRINC, 0, settacc));
    elif (not FindSETTACC_PMAUTO(Cedent, FIKIND_CURRENCY, deal.GetLeg().PFI, PTSK_CURRDL, deal.GetTick().DealType, 0, 0, settacc));
    elif (not FindSETTACC_PMAUTO(Cedent, FIKIND_CURRENCY, deal.GetLeg().PFI, PTSK_CURRDL, 0, PM_PURP_PRINC, 0, settacc));
    elif (not FindSETTACC_PMAUTO(Cedent, FIKIND_CURRENCY, deal.GetLeg().PFI, PTSK_CURRDL, 0, 0, 0, settacc));
    end;

    paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC, 0);
    if (paym.PaymentID > 0)
        paym.SetReceiverPI (PAYMENTS_GROUP_UNDEF,
                            settacc.BankID,
                            settacc.BankCodeKind,
                            settacc.BankCode,
                            settacc.BankName,
                            settacc.CorrAcc,
                            settacc.FIID,
                            settacc.Chapter,
                            settacc.Account,
                            settacc.PartyID,
                            settacc.RecName,
                            settacc.INN,
                            settacc.CodeKind,
                            settacc.Code
                            );

        paym.FutureReceiverAccount = settacc.Account;
    else
        mm_error("Не найдены платежи");
        return 2;
    end;

END;

PRIVATE MACRO ПолучитьТекушийНомерСчет(indx, Дата, Счет, fiid)
  var FindAccount = "";
  if (deal.dealtype != 12335)
     FirstDoc.IsActAccount(Категории[indx], FindAccount, true, Роли[indx], NULL, Дата, NULL);
  else
     FirstDoc.IsActAccount(Категории[indx], FindAccount, true, Роли[indx], NULL, Дата, fiid);
  end;

  if ((FindAccount != "") and ((Категории[indx] == tdr_mainrest) or (Категории[indx] == tdr_mainrest_tf)))
    FirstDoc.ActualCheckPeriod(Категории[indx], FindAccount, NULL, Роли[indx], NULL, Дата, fiid);
  end;
  SetParm( 2, FindAccount );

  if (FindAccount == "") 
      return false;
  end;
  
  return true;
END;


PRIVATE MACRO АктуализироватьСчет(indx, Дата, Счет, fiid)
  var FindAccount = "";

  FirstDoc.ActualAccount(Категории[indx], FindAccount, NULL, Роли[indx], NULL, Дата, fiid);

  SetParm( 2, FindAccount );

  if (FindAccount == "")
      return false;
  end;
  
  return true;
END;

PRIVATE MACRO ЗаполнитьМассивы(Категория, Роль)
      if (Роль == NULL)
          Роль = FirstDoc.GetFIRoleByCategory(Категория, FirstDoc.GetLeg().PFI)
      end;

      Категории[Категории.size] = Категория; 
      Роли[Роли.size]           = Роль;  
END;

PRIVATE MACRO FillArray(Deal)
// Категории для сделок МБК
  if (Deal.BofficeKind == DL_IBCDOC) 
    if (isSale(GetOperationGroup(Deal.DealType)))
/*------------------------Категории для сделок размещения------------------------------------*/
      if  ((deal.dealtype == 12308 ) or  (deal.dealtype == 2308 ))
        ЗаполнитьМассивы(tdr_rights);
        ЗаполнитьМассивы(tdr_nomcost);
      elif (deal.dealtype == 12335)
        ЗаполнитьМассивы(tdr_mainrest_tf);
      else
        ЗаполнитьМассивы(tdr_mainrest);
      end;
      ЗаполнитьМассивы(tdr_claimR);
/*
      ЗаполнитьМассивы(tdr_percR(FirstDoc.GetTick().TypeDoc));
      ЗаполнитьМассивы(tdr_pennyR);
      ЗаполнитьМассивы("СЧЕТ_603");

      if (MC_GetKindSecurCredit(FirstDoc.GetTick().PartyID) != 1)
            if (IsCLAIMSACQ(GetOperationGroup(Deal.DealType)))
                ЗаполнитьМассивы(get_rmainrest_cm({curdate}));
            else
          ЗаполнитьМассивы(tdr_rez_mainrest);
          ЗаполнитьМассивы(tdr_rez_exprest);
            end;
          ЗаполнитьМассивы(tdr_RzrvP(FirstDoc.GetTick().TypeDoc), FIROLE_RVPS);
          ЗаполнитьМассивы(tdr_RzrvM(FirstDoc.GetTick().TypeDoc), FIROLE_RVPS);
          ЗаполнитьМассивы(tdr_RzrvP(FirstDoc.GetTick().TypeDoc), FIROLE_RVP);
          ЗаполнитьМассивы(tdr_RzrvM(FirstDoc.GetTick().TypeDoc), FIROLE_RVP);
          ЗаполнитьМассивы(tdr_exprestR);
          ЗаполнитьМассивы(tdr_exppercR);
          ЗаполнитьМассивы(tdr_perc_vb);
          ЗаполнитьМассивы(tdr_expperc_vb);
          ЗаполнитьМассивы("Задолж.% по спис ОД (ВБ)");
          ЗаполнитьМассивы("Задолж. По ОД спис (ВБ)");
          ЗаполнитьМассивы(tdr_rez_perc);
          ЗаполнитьМассивы(tdr_rez_expperc);
          ЗаполнитьМассивы(tdr_rez_penny);
          ЗаполнитьМассивы(tdr_revrezP, FIROLE_RVP);
          ЗаполнитьМассивы(tdr_revrezR, FIROLE_RVP);
          ЗаполнитьМассивы(tdr_assesrez);
      end;
*/
  else
/*------------------------Категории для сделок привлечения------------------------------------*/
      if (deal.dealtype == 12310)
         ЗаполнитьМассивы(tdr_mainrest_tf);
      else
      ЗаполнитьМассивы(tdr_mainrest);
      end;
      ЗаполнитьМассивы(tdr_claimP);
   /*
      ЗаполнитьМассивы(tdr_exprestP);
      ЗаполнитьМассивы(tdr_exppercP);
      ЗаполнитьМассивы(tdr_percP(FirstDoc.GetTick().TypeDoc));
      ЗаполнитьМассивы(tdr_pennyP);
      ЗаполнитьМассивы("СЧЕТ_603");
   */
  end;
// Категории для кредитных линий МБК
  elif (Deal.BofficeKind == DL_CREDITLN) 
    if (isSale(GetOperationGroup(Deal.DealType)))
        ЗаполнитьМассивы(tdr_oblP);
        ЗаполнитьМассивы(tdr_rezerve7);
        ЗаполнитьМассивы(tdr_rezerve8);
        ЗаполнитьМассивы(tdr_rezerve9);
    else
        ЗаполнитьМассивы(tdr_oblR);
   end;
  end;
END;

PRIVATE MACRO UpdateAccInPayment(deal, Account, CatCode, isStep)
  var paym       = NULL,
      countpayms = 0,
      i          = 0;

  macro GetCountPercenPayms(DocKind, DocID, Purpose)
    var rs = RsdRecordset("select count(*) as cnt from dpmpaym_dbt where (t_dockind ="+DocKind+")and(t_documentid = "+DocID+")and(t_purpose = "+Purpose+")");
	rs.moveNext;
    return rs.value("cnt");
  end;

  macro HasGraphPayms(DocKind, DocID)
    var rs = RsdRecordset("select count(*) as cnt from dpmpaym_dbt where (t_dockind ="+DocKind+")and(t_documentid = "+DocID+")and(t_subpurpose > 1000)");
	if (rs.moveNext and (rs.value("cnt") > 0))
        return true;
    end;
    return false;
  end;

  var SubPurp = 0;
  if (HasGraphPayms(deal.GetTick().BofficeKind, deal.GetTick().DealID))
        SubPurp = 1000;
  end;

  if (isSale(GetOperationGroup(deal.GetTick().DealType))) /*для размещения*/
    if ((CatCode == tdr_mainrest) or (CatCode == tdr_mainrest_tf)) 
        paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC, 0);
        if (paym.PaymentID > 0)
            if ((paym.PaymStatus == PM_PREPARING) OR (paym.PaymStatus == PM_READIED)) 
              paym.SetPayerPI (PAYMENTS_GROUP_UNDEF, 
                                paym.PayerBankID,
                                paym.PayerBankCodeKind,
                                paym.PayerBankCode,
                                paym.PayerBankName,
                                paym.PayerBankCorrAcc,
                                deal.GetLeg().PFI,
                                1,
                                Account,
                                paym.Payer,
                                paym.PayerName,
                                paym.PayerINN,
                                paym.PayerCodeKind,
                                "");

              paym.FuturePayerAccount = Account;
              if (not isStep)
                paym.Update();
              end;
            end;
        else
            mm_error("Не найдены платежи");
            return 2;
        end;

    	countpayms = GetCountPercenPayms(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC_RET);
        if (SubPurp == 1000)
            SubPurp = 1001;
        end;
        while (i < countpayms)
            paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC_RET, SubPurp+i);
            if (paym.PaymentID > 0)
              if ((paym.PaymStatus == PM_PREPARING) OR (paym.PaymStatus == PM_READIED)) 
                paym.SetReceiverPI (PAYMENTS_GROUP_UNDEF, 
                                    paym.ReceiverBankID,
                                    paym.ReceiverBankCodeKind,
                                    paym.ReceiverBankCode,
                                    paym.ReceiverBankName,
                                    paym.ReceiverBankCorrAcc,
                                    deal.GetLeg().PFI,
                                    1,
                                    Account,
                                    paym.Receiver,
                                    paym.ReceiverName,
                                    paym.ReceiverINN,
                                    paym.ReceiverCodeKind,
                                    "");

                paym.FutureReceiverAccount = Account;
                if (not isStep)
                    paym.Update();
                end;
              end;
           end;
           i = i + 1;
        end; 
    elif(CatCode == tdr_claimR)
		countpayms = GetCountPercenPayms(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PERCENT);
        while (i < countpayms)
            paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PERCENT, SubPurp+i+1);
            if (paym.PaymentID > 0)
                if ((paym.PaymStatus == PM_PREPARING) OR (paym.PaymStatus == PM_READIED)) 
                  paym.SetReceiverPI (PAYMENTS_GROUP_UNDEF, 
                                      paym.ReceiverBankID,
                                      paym.ReceiverBankCodeKind,
                                      paym.ReceiverBankCode,
                                      paym.ReceiverBankName,
                                      paym.ReceiverBankCorrAcc,
                                      deal.GetLeg().PFI,
                                      1,
                                      Account,
                                      paym.Receiver,
                                      paym.ReceiverName,
                                      paym.ReceiverINN,
                                      paym.ReceiverCodeKind,
                                      "");

                  paym.FutureReceiverAccount = Account;
                  if (not isStep)
                    paym.Update();
                  end;
                end;
            end;
	    i = i + 1;
        end;

    end;
  else
    if ((CatCode == tdr_mainrest) or (CatCode == tdr_mainrest_tf))
        paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC, 0);
        if (paym.PaymentID > 0)
            if ((paym.PaymStatus == PM_PREPARING) OR (paym.PaymStatus == PM_READIED)) 
              paym.SetReceiverPI (PAYMENTS_GROUP_UNDEF, 
                                  paym.ReceiverBankID,
                                  paym.ReceiverBankCodeKind,
                                  paym.ReceiverBankCode,
                                  paym.ReceiverBankName,
                                  paym.ReceiverBankCorrAcc,
                                  deal.GetLeg().PFI,
                                  1,
                                  Account,
                                  paym.Receiver,
                                  paym.ReceiverName,
                                  paym.ReceiverINN,
                                  paym.ReceiverCodeKind,
                                  "");

              paym.FutureReceiverAccount = Account;
              if (not isStep)
                paym.Update();
              end;
            end;
        else
            mm_error("Не найдены платежи");
            return 2;
        end;

    	countpayms = GetCountPercenPayms(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC_RET);
        if (SubPurp == 1000)
            SubPurp = 1001;
        end;
        while (i < countpayms)
            paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PRINC_RET, SubPurp+i);
        if (paym.PaymentID > 0)
            if ((paym.PaymStatus == PM_PREPARING) OR (paym.PaymStatus == PM_READIED)) 
              paym.SetPayerPI (PAYMENTS_GROUP_UNDEF, 
                              paym.PayerBankID,
                              paym.PayerBankCodeKind,
                              paym.PayerBankCode,
                              paym.PayerBankName,
                              paym.PayerBankCorrAcc,
                              deal.GetLeg().PFI,
                              1,
                              Account,
                              paym.Payer,
                              paym.PayerName,
                              paym.PayerINN,
                              paym.PayerCodeKind,
                              "");

              paym.FuturePayerAccount = Account;
              if (not isStep)
                paym.Update();
              end;
            end;
        end;
            i = i + 1;
        end;
    elif(CatCode == tdr_claimP)
        countpayms = GetCountPercenPayms(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PERCENT);
        while (i < countpayms)
            paym = RsbPayment(deal.GetTick().BofficeKind, deal.GetTick().DealID, PM_PURP_PERCENT, SubPurp+i+1);
            if (paym.PaymentID > 0)
              if ((paym.PaymStatus == PM_PREPARING) OR (paym.PaymStatus == PM_READIED)) 
                paym.SetPayerPI (PAYMENTS_GROUP_UNDEF, 
                                paym.PayerBankID,
                                paym.PayerBankCodeKind,
                                paym.PayerBankCode,
                                paym.PayerBankName,
                                paym.PayerBankCorrAcc,
                                deal.GetLeg().PFI,
                                1,
                                Account,
                                paym.Payer,
                                paym.PayerName,
                                paym.PayerINN,
                                paym.PayerCodeKind,
                                "");

                paym.FuturePayerAccount = Account;
                if (not isStep)
                  paym.Update();
                end;
              end;
            end;
			      i = i + 1;
        end;
    /* MAA: DEF-20493 Для сделок привлечения, ТФ не было актуализации */
    end;
  end;
  return 0;
END;

MACRO UpdatePayments(deal_pd, isStep)
  var ExecDate = date(0, 0, 0),
      Account  = "",
      stat = 0;

  FirstDoc = deal_pd;
  FillArray(FirstDoc.GetTick()); 

  ExecDate = FirstDoc.GetTick().DealDate;
  if (ExecDate > {curdate})
    ExecDate = {curdate};
  end;

  if (Категории.size > Роли.size)
      Роли.size = Категории.size;
  end;
  var i = 0;
  while (ValType(Категории(i)) == V_STRING)
      if (ПолучитьТекушийНомерСчет(i, ExecDate, Account, FirstDoc.GetLeg().pfi) OR АктуализироватьСчет(i, ExecDate, Account, FirstDoc.GetLeg().pfi))
          ;
      else
          mm_error("Не могу актуализировать счет для категории " + Категории(i));
          return 1;
      end;

      if ((Категории(i) == tdr_mainrest)OR 
          (Категории(i) == tdr_claimR) OR
          (Категории(i) == tdr_claimP) OR
          (Категории(i) == tdr_calcs_cmR) OR
          (Категории(i) == get_rights({curdate})) OR
          (Категории(i) == tdr_retirement) OR
	  (Категории(i) == tdr_mainrest_tf))  //SVE в сделках ТФ размещения не заполняется счет 47410* поэтому надо скорректировать платежи после нахождения счета

          //if ((FirstDoc.GetLeg().PFI == 0) or (deal.dealtype ==12335))   // SVE 537396 23.12.2021 при переводе всех исходящих платежей на единый макрос mmog_j.mac необходимо актуализировать все сделки для корректного исполнения платежей
             stat = UpdateAccInPayment(FirstDoc, Account, Категории(i), isStep);
             if (stat == 1)
                mm_error("Ошибка при обновлении платежей"); 
                return 1;
             elif (stat == 2)
                return 1;
             end;
          //end;
      end;

      i = i + 1;
  end;

  return 0;
END;

MACRO ExecuteStep(Buffer, Document)
  SetBuff( deal, Document );

  FirstDoc = MMFirstDoc(deal.BOfficeKind, deal.DealID, true);
  
  if (CheckExistence(LEG_LegKind_DealID_LegID, LEG_KIND_DL_TICK, deal.DealID, 1))
      mm_error( "Не найден транш сделки ", deal.DealCode );
      return 1;
  end;

  if (IsCLAIMSACQ(GetOperationGroup(deal.DealType)))
    var panel = CCedentSelect(deal);
    if (panel.run() != -316)
        return 1;
    end;
    UpdateSPIForCLAIMSACQ(FirstDoc, panel.Get_Cedent());
  end;
  
  return UpdatePayments(FirstDoc, true);
END;

NameMacroFile = "mmactacc.mac";
