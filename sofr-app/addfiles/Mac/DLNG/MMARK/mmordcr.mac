/*
$Name: mmordcr.mac
$Module: МБК
$Description: Проводки договоров залога МБК
*/
//*******************************************************************************//
//*  Модуль           : МБК                                                     *//
//*  Имя файла        : mmordcr.mac                                             *//	
//*  Описание         : Проводки договоров залога МБК                           *//
//*  Программист      : Куперин М.В. //02.07.2009                               *//
//*******************************************************************************//
///////////////////////////////////////////////////////////////////////////////////
IMPORT mmordfd, mmcarry, globals, mmlibr;

var                                
    SetInPledge         = 0, // Передача в залог
    GetFromPledge       = 1, // Возврат из залога
    GetInPledge         = 2, // Прием в залог
    SetFromPledge       = 3, // Возврат из залога
    // производные типы проводок
    EditPledgeP         = 4, // Изменение состава ПЗ для принятого залога
    EditPledgeR         = 5, // Изменение состава ПЗ для переданного залога
    LeadToTCCSetInledge = 6, // Приводить стоимость ПЗ к ТСС для переданного залога (только ц/б)
    LeadToTCCGetInledge = 7, // Приводить стоимость ПЗ к ТСС для принятого залога (только ц/б)
    RevalTCCSetInledge  = 8, // Переоценка по ТСС для переданного залога (только ц/б)
    RevalTCCGetInledge  = 9; // Переоценка по ТСС для принятого залога (только ц/б)

////////////////////////////////////////////////////////////////////////////////////////
// базовый класс для проводок по учету обеспечения
// реализует 4 основные проводки
// здесь же: определяются КУ для разных типов ДО и основания проводок
private class MM_PledgeCarryBase()
  var
      CarryMode    = NULL,
      RealMode     = NULL,
      CarrySum     = NULL,
      CarrySumFIID = NULL,
      PrimaryDoc   = NULL,
      Order        = NULL,
      FIRole       = NULL,
      CarryDate    = {curdate};

  private var
      prm_Ground    = NULL,
      prm_CatNameOP = "",
      prm_CatNameOM = "ВнебалСчетКорресп",
      prm_CatNameVP = "",
      prm_CatNameVM = "ВнебалСчетКорресп";

  private macro SetGround()
      if (RealMode == SetInPledge)
          prm_Ground = "Передача обеспечения";
      elif (RealMode == GetFromPledge)
          prm_Ground = "Возврат обеспечения";
      elif (RealMode == GetInPledge)
          prm_Ground = "Прием обеспечения";
      elif (RealMode == SetFromPledge)
          prm_Ground = "Возврат обеспечения";
      elif (RealMode == EditPledgeP)
          prm_Ground = "Корректировка обеспечения";
      elif (RealMode == EditPledgeR)
          prm_Ground = "Корректировка обеспечения";
      elif (RealMode == LeadToTCCSetInledge)
          prm_Ground = "Корректировка обеспечения";
      elif (RealMode == LeadToTCCGetInledge)
          prm_Ground = "Корректировка обеспечения";
      elif (RealMode == RevalTCCSetInledge)
          prm_Ground = "Переоценка по ТСС обеспечения";
      elif (RealMode == RevalTCCGetInledge)
          prm_Ground = "Переоценка по ТСС обеспечения";
      else
          prm_Ground = "";
          return;
      end;
	  
      if (Order.ContractKind == 107)
          prm_Ground = prm_Ground + " по Договору залога ";
      elif (Order.ContractKind == 111)
          prm_Ground = prm_Ground + " по Соглашению о залоге ";
      else
          return;
      end;
      prm_Ground = prm_Ground + "№ " + Order.OrderNumber + " от " +String(Date(Order.SignDate):f);
  end;

  private macro SetCateg()
      if (Order.SysTypes == "Ц")
          prm_CatNameOP = "+Обесп. кр., ц/б";
          prm_CatNameVP = "-Обесп. кр., ц/б";
      elif (Order.SysTypes == "Г")
          prm_CatNameOP = "+Обесп. кр., г/п";
          prm_CatNameVP = "-Обесп. кр., г/п";
      elif (Order.SysTypes == "Н")
          prm_CatNameOP = "+Обесп. кр., имущ.";
          prm_CatNameVP = "-Обесп. кр., имущ.";
      elif (Order.SysTypes == "К")
          prm_CatNameOP = "+Обесп. кр., имущ.";
          prm_CatNameVP = "-Обесп. кр., имущ.";
      end;
  end;

  macro RunCarry()
    var 
        at = MM_Carry;

      if (ValType(RealMode) == V_UNDEF)
          RealMode = CarryMode;
      end;

      SetCateg();
      SetGround();

      if (CarryMode == SetInPledge)
        at.CatPayer       = prm_CatNameVP;
        at.CatReceiver    = prm_CatNameVM;
        at.FiRolePayer    = FIRole;
        at.FiRoleReceiver = FIROLE_CORACC_ACTIVE;
        at.FIIDPayer      = Int(CarrySumFIID);
        at.FIIDReceiver   = NATCUR;
        //at.FIIDReceiver   = Int(CarrySumFIID);;

        at.SumPayer       = Money(abs(CarrySum));
      elif(CarryMode == GetFromPledge)
        at.CatPayer       = prm_CatNameVM;
        at.CatReceiver    = prm_CatNameVP;
        at.FiRolePayer    = FIROLE_CORACC_ACTIVE;
        at.FiRoleReceiver = FIRole;
        at.FIIDPayer      = NATCUR;
        //at.FIIDPayer      = Int(CarrySumFIID);

        at.FIIDReceiver   = Int(CarrySumFIID);
        at.SumReceiver    = Money(abs(CarrySum));
      elif(CarryMode == GetInPledge)
        at.CatPayer       = prm_CatNameOM;
        at.CatReceiver    = prm_CatNameOP;
        at.FiRolePayer    = FIROLE_CORACC_PASSIVE;
        at.FiRoleReceiver = FIRole;
        at.FIIDPayer      = NATCUR;
        //at.FIIDPayer      = Int(CarrySumFIID);

        at.FIIDReceiver   = Int(CarrySumFIID);
        at.SumReceiver    = Money(abs(CarrySum));
      elif(CarryMode == SetFromPledge )
        at.CatPayer       = prm_CatNameOP;
        at.CatReceiver    = prm_CatNameOM;
        at.FiRolePayer    = FIRole;
        at.FiRoleReceiver = FIROLE_CORACC_PASSIVE;
        at.FIIDPayer      = Int(CarrySumFIID);
        at.FIIDReceiver   = NATCUR;
        //at.FIIDReceiver   = Int(CarrySumFIID);

        at.SumPayer       = Money(abs(CarrySum));
      else
          return 1;
      end;

      at.Ground     = prm_Ground;
      at.Chapter    = 3;
      at.date_carry = CarryDate;
      at.PrimaryDoc = PrimaryDoc;

      if (not at.carry())
          return 1;
      end;

      return 0;
  end;
end;

////////////////////////////////////////////////////////////////////////////////////////
// класс, реализующий алгоритмы формирования проводок при учете по договору
class MM_OrdPledgeExecCarry(_mode, _ord, _dealID, _dateCarry)
  private var
      prm_mode   = NULL,
      prm_order  = NULL,
      prm_dealID    = NULL,
      prm_dateCarry = NULL;
      

  private var
      prm_FINominal = NULL; 

  private macro GetTCCSum(_Sum)
    var
        rsbQuery = NULL, QueryStr = "", stat = 0,
        SumTCC = 0, SumTCCcursec = 0, TypeCurs = 0, SumNominalcursec = 0;

    record ratedef ("ratedef");

      GetRegistryValue("MMARK\\ОБЩИЕ\\ВИДКУРСА_СПРАВЕДЛИВАЯ_СТОИМОСТЬ",
                       V_INTEGER, TypeCurs, stat);

      if (not stat)
          if ((prm_dealID == 0)or(prm_dealID == -1))
              QueryStr = "select * from ddl_secur_dbt sec where (sec.t_contractkind = " + DL_MMPAWN + ")and "
          		        "(sec.t_contractid = " + prm_order.ContractID + ")";
          else
              QueryStr = "select * from ddl_secur_dbt sec where (sec.t_contractkind = " + DL_IBCDOC + ")and "
          		        "(sec.t_contractid = " + prm_dealID + ")and(sec.t_generalcontract = " + prm_order.ContractID + ")";
          end;

          rsbQuery = TRsbDataSet(QueryStr);
          while ((rsbQuery.MoveNext)and(not stat))
              SumTCCcursec = 0;

              if (rsbQuery.quotation == 0)
                  stat = ПолучитьКурс(ratedef, prm_FINominal, rsbQuery.fiid, TypeCurs);
                  if (not stat)
                      stat = ПолучитьЗначениеКурса(ratedef, prm_dateCarry);
                  end;
              else
                  ПолучитьКурс(ratedef, rsbQuery.quotation);
                  ПолучитьЗначениеКурса(ratedef, prm_dateCarry);
              end;
      
              if (not stat)
                 if ((ratedef.fiid == rsbQuery.fiid)and(ratedef.otherfi == prm_FINominal))
                      SumTCCcursec = (rsbQuery.quantity / (ratedef.rate / pow(10, ratedef.point)));
                  elif ((ratedef.fiid == prm_FINominal)and(ratedef.otherfi == rsbQuery.fiid))
                      SumTCCcursec = (rsbQuery.quantity * (ratedef.rate / pow(10, ratedef.point)));
                  else
                      stat = 1;
                  end;
              end;
      
              SumTCC = SumTCC + SumTCCcursec;
          end;
      end;

      SetParm(1, SumTCC);

      return stat;
  end;

  macro ExecCarry()
    var
        Carry = MM_PledgeCarryBase(),
        leadtoTCC = NULL, 
        rsbQuery = NULL, QueryStr = "",
        stat = 0, SumTCC = 0, mny,
        StartCarry = true, УчетПравТребования;

      if (prm_order.Priznak == "")
          return 0;
      end;


      Carry.PrimaryDoc = MMOrderFD(prm_order.DocKind, prm_order.ContractID);
      prm_FINominal    = Carry.PrimaryDoc.GetParametr(MC_TYPE_PARAMETR_CURRENCY);
      Carry.CarryDate  = prm_dateCarry;

      if (prm_order.SysTypes == "Ц")
          GetRegistryValue("MMARK\\ОБЩИЕ\\ПРИВ_ОЦЕНОЧНУЮ_СТОИМОСТЬ_К_ТСС",
                              V_BOOL, leadtoTCC, stat);
          if (stat)
              return 1;
          end;
      end;
	  
	  if (prm_order.SysTypes == "К")
          GetRegistryValue("MMARK\\ОБЩИЕ\\ОБЕСПЕЧЕНИЕ\\УЧЕТ ПРАВ ТРЕБОВАНИЯ", V_INTEGER, УчетПравТребования, stat);
          if (stat)
              return 1;
          end;
      end;
      Carry.RealMode     = prm_mode;
      Carry.CarrySumFIID = prm_FINominal;
      Carry.Order        = prm_order;
	
      if ((prm_mode == GetInPledge)or
            (prm_mode == SetInPledge))

          if (prm_dealID <= 0)
              QueryStr = "select nvl(sum(sec.t_Nominal), 0) as sumNominal, nvl(sum(sec.t_crdsum), 0) as crdSum, sec.t_crdfiid crdfiid from ddl_secur_dbt sec where ";
              QueryStr = QueryStr + "(sec.t_contractkind = " + DL_MMPAWN + ")and(sec.t_contractid = " + prm_order.ContractID + ")";
          else
              QueryStr = "select nvl(sum(sec.t_Nominal), 0) as sumNominal, nvl(sum(sec.t_crdsum), 0) as crdSum, sec.t_crdfiid crdfiid from ddl_secur_dbt sec where ";
              QueryStr = QueryStr + "(sec.t_contractkind = " + DL_IBCDOC + ")and(sec.t_contractid = " + prm_dealID + ")and(sec.t_generalcontract = " + prm_order.ContractID + ")";
          end;
		  QueryStr = QueryStr + " group by t_nominal, t_crdsum, t_crdfiid ";
		  
          Carry.CarryMode = prm_mode;
          rsbQuery = TRsbDataSet(QueryStr);
          while (rsbQuery.MoveNext)
              SumTCC = rsbQuery.sumNominal;
			  
              if (УчетПравТребования == 2)
                 Carry.CarrySum     = rsbQuery.crdSum;
                 Carry.CarrySumFIID = rsbQuery.crdfiid;
              else
                 Carry.CarrySum     = SumTCC;
                 Carry.CarrySumFIID = prm_FINominal;
              end;
			  
              if (StartCarry)
                 if (Carry.RunCarry())
                    return 1;
                  end;

                  if (prm_dealID < 0)
                    prm_dealID = 0;
                  end;

                  MM_ChangeNominalPawn(prm_dealID, prm_order.ContractID, 0, Money(abs(SumTCC)), prm_dateCarry);
              end;
          end;
		  
      elif ((prm_mode == GetFromPledge)or
            (prm_mode == SetFromPledge))
			
          if (prm_dealID <= 0)
              QueryStr = "select nvl(sum(sec.t_Nominal), 0) as sumNominal, nvl(sum(sec.t_crdsum), 0) as crdSum, sec.t_crdfiid crdfiid from ddl_secur_dbt sec where ";
              QueryStr = QueryStr + "(sec.t_contractkind = " + DL_MMPAWN + ")and(sec.t_contractid = " + prm_order.ContractID + ")";
          else
              QueryStr = "select nvl(sum(sec.t_Nominal), 0) as sumNominal, nvl(sum(sec.t_crdsum), 0) as crdSum, sec.t_crdfiid crdfiid from ddl_secur_dbt sec where ";
              QueryStr = QueryStr + "(sec.t_contractkind = " + DL_IBCDOC + ")and(sec.t_contractid = " + prm_dealID + ")and(sec.t_generalcontract = " + prm_order.ContractID + ")";
          end;
          QueryStr = QueryStr + " group by t_nominal, t_crdsum, t_crdfiid ";
		  
          Carry.CarryMode = prm_mode;
          rsbQuery = TRsbDataSet(QueryStr);
          while (rsbQuery.MoveNext)
              SumTCC = rsbQuery.sumNominal;
			  
              if (УчетПравТребования == 2)
			     Carry.CarrySum     = rsbQuery.crdSum;
			     Carry.CarrySumFIID = rsbQuery.crdfiid;
		      else
			     Carry.CarrySum     = SumTCC;
                 Carry.CarrySumFIID = prm_FINominal;
		      end;
			  
              if (StartCarry)
                 if (Carry.RunCarry())
                    return 1;
                  end;

                  if (prm_dealID < 0)
                    prm_dealID = 0;
                  end;

                  MM_ChangeNominalPawn(prm_dealID, prm_order.ContractID, 0, Money(abs(SumTCC)), prm_dateCarry);
              end;
		  
          end;
		  

          SumTCC = 0;
		  
      elif ((prm_mode == EditPledgeP)or(prm_mode == EditPledgeR))
          if (not leadtoTCC)
              QueryStr = "select nvl(sum(t_Nominal), 0) as sumNominal from ddl_secur_dbt where ";
              QueryStr = QueryStr + "(t_contractkind = " + DL_MMPAWN + ")and(t_contractid = " + prm_order.ContractID + ")";

              rsbQuery = TRsbDataSet(QueryStr);
              if (rsbQuery.MoveNext)
                  SumTCC = rsbQuery.sumNominal;
             end;
          else
              if (GetTCCSum(SumTCC))
                  mm_error("Невозможно сформировать проводки.");
                  return 1;
              end;
          end;

          if (SumTCC == 0)
              QueryStr = "    select * from ddl_secur_hist_dbt sech "
                          "    where "
                          "    (sech.t_contractkind = " + DL_MMPAWN + ") "
                          "    and(sech.t_contractid = " + prm_order.ContractID + ") "
                          "    order by sech.t_id_step ";

              rsbQuery = TRsbDataSet(QueryStr);
              rsbQuery.MoveNext;

              FI_GetNominal(rsbQuery.FIID, mny, prm_FINominal)
          end;

          Carry.CarrySum = SumTCC - prm_order.tcc;

          if (Carry.CarrySum == 0)
              return 0;
          end;

          if ((Carry.CarrySum < 0)and(prm_mode == EditPledgeP))
              Carry.CarryMode = SetFromPledge;
          elif ((Carry.CarrySum < 0)and(prm_mode == EditPledgeR))
              Carry.CarryMode = GetFromPledge;
          elif ((Carry.CarrySum > 0)and(prm_mode == EditPledgeP))
              Carry.CarryMode = GetInPledge;
          elif ((Carry.CarrySum > 0)and(prm_mode == EditPledgeR))
              Carry.CarryMode = SetInPledge;
          end;
		  
          if (StartCarry)
             if (Carry.RunCarry())
                return 1;
             end;

             if (prm_dealID < 0)
                prm_dealID = 0;
             end;

             MM_ChangeNominalPawn(prm_dealID, prm_order.ContractID, 0, Money(abs(SumTCC)), prm_dateCarry);
          end; 
      elif ((prm_mode == LeadToTCCSetInledge)or
            (prm_mode == LeadToTCCGetInledge))
          if (leadtoTCC)
              if (prm_dealID <= 0)
                  QueryStr = "select nvl(sum(t_Nominal), 0) as sumNominal from ddl_secur_dbt where ";
                  QueryStr = QueryStr + "(t_contractkind = " + DL_MMPAWN + ")and(t_contractid = " + prm_order.ContractID + ")";
              else
                  QueryStr = "select nvl(sum(t_Nominal), 0) as sumNominal from ddl_secur_dbt where ";
                  QueryStr = QueryStr + "(t_contractkind = " + DL_IBCDOC + ")and(t_contractid = " + prm_dealID + ")and(t_generalcontract = " + prm_order.ContractID + ")";
              end;
      
              rsbQuery = TRsbDataSet(QueryStr);
              if (rsbQuery.MoveNext)
                  Carry.CarrySum = rsbQuery.sumNominal;
              end;

              if (GetTCCSum(SumTCC))
                  mm_error("Невозможно сформировать проводки по ТСС, т.к. в залоге есть ценные бумаги, для которых не установлен курс ТСС");
                  return 1;
              end;

              Carry.CarrySum = SumTCC - abs(Carry.CarrySum);

              if (Carry.CarrySum == 0)
                  return 0;
              end;

              if (prm_mode == LeadToTCCSetInledge)
                  if (Carry.CarrySum < 0)
                      Carry.CarryMode = GetFromPledge
                  else
                      Carry.CarryMode = SetInPledge;
                  end;
              elif(prm_mode == LeadToTCCGetInledge)
                  if (Carry.CarrySum < 0)
                      Carry.CarryMode = SetFromPledge;
                  else
                      Carry.CarryMode = GetInPledge;
                  end;
              end;
          else
              StartCarry = false;
          end;
		  
		  if (StartCarry)
             if (Carry.RunCarry())
                return 1;
             end;

             if (prm_dealID < 0)
                prm_dealID = 0;
             end;

             MM_ChangeNominalPawn(prm_dealID, prm_order.ContractID, 0, Money(abs(SumTCC)), prm_dateCarry);
          end;
		  
      elif ((prm_mode == RevalTCCSetInledge) or
            (prm_mode == RevalTCCGetInledge))
          if (prm_dealID <= 0)
              QueryStr = "select t_tcc as sumTCC from ddl_order_dbt where ";
              QueryStr = QueryStr + "(t_contractid = " + prm_order.ContractID + ")";
          else
              QueryStr = "select t_tcc as sumTCC from ddl_grnt_dbt where ";
              QueryStr = QueryStr + "(t_contractid = " + prm_order.ContractID + ")and(t_dealid = " + prm_dealID + ")";
          end;

          rsbQuery = TRsbDataSet(QueryStr);
          if (rsbQuery.MoveNext)
              Carry.CarrySum = rsbQuery.sumTCC;
          end;

          if (GetTCCSum(SumTCC))
              mm_error("Невозможно сформировать проводки по ТСС, т.к. в залоге есть ценные бумаги, для которых не установлен курс ТСС");
              return 1;
          end;

          Carry.CarrySum = SumTCC - abs(Carry.CarrySum);

          if (prm_mode == RevalTCCSetInledge)
              if (Carry.CarrySum < 0)
                  Carry.CarryMode = GetFromPledge
              else
                  Carry.CarryMode = SetInPledge;
              end;
          elif(prm_mode == RevalTCCGetInledge)
              if (Carry.CarrySum < 0)
                  Carry.CarryMode = SetFromPledge;
              else
                  Carry.CarryMode = GetInPledge;
              end;
          end;

          if (Carry.CarrySum == 0)
              return 1;
          end;
		  
          if (StartCarry)
             if (Carry.RunCarry())
                return 1;
             end;

             if (prm_dealID < 0)
                prm_dealID = 0;
             end;

             MM_ChangeNominalPawn(prm_dealID, prm_order.ContractID, 0, Money(abs(SumTCC)), prm_dateCarry);
          end;
		  
      end;

      return 0;
  end;

  private macro ClassConstructor(_mode, _ord, _dealID, _dateCarry)
      prm_mode   = _mode;
      prm_order  = _ord;
      prm_dealID = _dealID;

      if (ValType(_dateCarry) != V_DATE)
          prm_dateCarry = {curdate};
      else
          prm_dateCarry = _dateCarry;
      end;

      prm_FINominal = NULL; 
  end;

  ClassConstructor(_mode, _ord, _dealID, _dateCarry);
end;

////////////////////////////////////////////////////////////////////////////////////////
// класс, реализующий алгоритмы формирования проводок при учете по предметам залога
class MM_OrdPledgeSecurExecCarry(_mode, _ord, _dealID, _dateCarry)
  private var
      prm_mode   = NULL,
      prm_order  = NULL,
      prm_dealID    = NULL,
      prm_dateCarry = NULL;
      
  private macro GetTCCSum(_Secur, _Sum)
    var
        stat = 0, SumTCCcursec = 0, TypeCurs = 0, SumNominalcursec = 0;

    record ratedef ("ratedef");


    if (_Secur.quotation == 0)
        GetRegistryValue("MMARK\\ОБЩИЕ\\ВИДКУРСА_СПРАВЕДЛИВАЯ_СТОИМОСТЬ",
                     V_INTEGER, TypeCurs, stat);
        if (stat)
            stat = 1;
        else
            stat = ПолучитьКурс(ratedef, _Secur.faceFI, _Secur.fiid, TypeCurs);
            if (not stat)
                stat = ПолучитьЗначениеКурса(ratedef, prm_dateCarry);
            end;
        end;
    else
        ПолучитьКурс(ratedef, _Secur.quotation);
        ПолучитьЗначениеКурса(ratedef, prm_dateCarry);
    end;


    if (not stat)
        if ((ratedef.fiid == _Secur.fiid)and(ratedef.otherfi == _Secur.faceFI))
            SumTCCcursec = (_Secur.quantity / (ratedef.rate / pow(10, ratedef.point)));
        elif ((ratedef.fiid == _Secur.faceFI)and(ratedef.otherfi == _Secur.fiid))
            SumTCCcursec = (_Secur.quantity * (ratedef.rate / pow(10, ratedef.point)));
        else
            stat = 1;
        end;
    end;

    SetParm(2, SumTCCcursec);

    return stat;
  end;

  macro ExecCarry()
    var
        PrimaryDoc = NULL, leadtoTCC = NULL, 
        Carry = TArray(), idx = 0,
        rsbQuery = NULL, QueryStr = "",
        stat = 0, SumTCC = 0, StartCarry = true;

      if (prm_order.Priznak == "")
          return 0;
      end;

      PrimaryDoc = MMOrderFD(prm_order.DocKind, prm_order.ContractID);
  
      if (prm_order.SysTypes == "Ц")
          GetRegistryValue("MMARK\\ОБЩИЕ\\ПРИВ_ОЦЕНОЧНУЮ_СТОИМОСТЬ_К_ТСС",
                              V_BOOL, leadtoTCC, stat);
          if (stat)
              return 1;
          end;
      end;

      if ((prm_mode == GetInPledge)or
            (prm_mode == SetInPledge))
          if (prm_dealID <= 0)
              QueryStr = "select secur.t_numsecinorder, secur.t_Nominal as sumNominal, decode('" + prm_order.SysTypes + "', 'Ц', fininstr.t_facevaluefi, fininstr.t_fiid) as faceFI from ddl_secur_dbt secur, dfininstr_dbt fininstr where ";
              QueryStr = QueryStr + "(secur.t_contractkind = " + DL_MMPAWN + ")and(secur.t_contractid = " + prm_order.ContractID + ")and(fininstr.t_fiid = secur.t_fiid)";
          else
              QueryStr = "select secur.t_numsecinorder, secur.t_Nominal as sumNominal, decode('" + prm_order.SysTypes + "', 'Ц', fininstr.t_facevaluefi, fininstr.t_fiid) as faceFI from ddl_secur_dbt secur, dfininstr_dbt fininstr where ";
              QueryStr = QueryStr + "(secur.t_contractkind = " + DL_IBCDOC + ")and(secur.t_contractid = " + prm_dealID + ")and(secur.t_generalcontract = " + prm_order.ContractID + ")and(fininstr.t_fiid = secur.t_fiid)";
          end;

          rsbQuery = TRsbDataSet(QueryStr);
          while (rsbQuery.MoveNext)
              idx = Carry.size;
              Carry[idx] = MM_PledgeCarryBase();
              SumTCC = rsbQuery.sumNominal;
              Carry[idx].CarryDate  = prm_dateCarry;

              PrimaryDoc.SetSecur(rsbQuery.NumSecInOrder);

              Carry[idx].CarryMode    = prm_mode;
              Carry[idx].RealMode     = prm_mode;
              Carry[idx].Order        = prm_order;
              Carry[idx].CarrySumFIID = Int(rsbQuery.faceFI);
              Carry[idx].PrimaryDoc   = PrimaryDoc;
              Carry[idx].FIRole       = rsbQuery.NumSecInOrder;
              Carry[idx].CarrySum     = SumTCC;
              Carry[idx].CarrySumFIID = Int(rsbQuery.faceFI);

              if (prm_dealID == -1)
                  prm_dealID = 0;
              end;

              MM_ChangeNominalPawn(prm_dealID, prm_order.ContractID,  rsbQuery.NumSecInOrder, Money(abs(SumTCC)), prm_dateCarry);
          end;
      elif ((prm_mode == GetFromPledge)or
            (prm_mode == SetFromPledge))
          if (prm_dealID <= -1)
              QueryStr = "select secur.t_numsecinorder, secur.t_tcc as sumTCC, decode('" + prm_order.SysTypes + "', 'Ц', fininstr.t_facevaluefi, fininstr.t_fiid) as faceFI from ddl_secur_dbt secur, dfininstr_dbt fininstr where ";
              QueryStr = QueryStr + "(secur.t_contractkind = " + DL_MMPAWN + ")and(secur.t_contractid = " + prm_order.ContractID + ")and(fininstr.t_fiid = secur.t_fiid)";
          else
              QueryStr = "select secur.t_numsecinorder, secur.t_tcc as sumTCC, decode('" + prm_order.SysTypes + "', 'Ц', fininstr.t_facevaluefi, fininstr.t_fiid) as faceFI from ddl_secur_dbt secur, dfininstr_dbt fininstr where ";
              QueryStr = QueryStr + "(secur.t_contractkind = " + DL_IBCDOC + ")and(secur.t_contractid = " + prm_dealID + ")and(secur.t_generalcontract = " +prm_order.ContractID + ")and(fininstr.t_fiid = secur.t_fiid)";
          end;

          rsbQuery = TRsbDataSet(QueryStr);

          while (rsbQuery.MoveNext)
              idx = Carry.size;
              Carry[idx] = MM_PledgeCarryBase();
              SumTCC = rsbQuery.sumTCC;
              Carry[idx].CarryDate  = prm_dateCarry;

              PrimaryDoc.SetSecur(rsbQuery.NumSecInOrder);

              Carry[idx].CarryMode    = prm_mode;
              Carry[idx].RealMode     = prm_mode;
              Carry[idx].Order        = prm_order;
              Carry[idx].CarrySumFIID = Int(rsbQuery.faceFI);
              Carry[idx].PrimaryDoc   = PrimaryDoc;
              Carry[idx].FIRole       = rsbQuery.NumSecInOrder;
              Carry[idx].CarrySum     = SumTCC;
              Carry[idx].CarrySumFIID = Int(rsbQuery.faceFI);

              SumTCC = 0;

              if (prm_dealID == -1)
                  prm_dealID = 0;
              end;

              MM_ChangeNominalPawn(prm_dealID, prm_order.ContractID,  rsbQuery.NumSecInOrder, Money(abs(SumTCC)), prm_dateCarry);
          end;
      elif ((prm_mode == EditPledgeP)or(prm_mode == EditPledgeR))
          QueryStr = 
                     "select distinct 0, 0, sec.*, sec.t_Nominal as sumNominal, 0 as t_SumTCC, "
                     "decode('" + prm_order.SysTypes + "', 'Ц', fininstr.t_facevaluefi, fininstr.t_fiid) as faceFI "
                     "from ddl_secur_dbt sec, dfininstr_dbt fininstr "
                     "where "
                     "(sec.t_contractkind = 126) "
                     "and(sec.t_contractid = " + prm_order.ContractID + ") "
                     "and(fininstr.t_fiid = sec.t_fiid) "
                     "and(0 = ( "
                     "          select NVL(max(sech.t_id_step), 0) from ddl_secur_hist_dbt sech "
                     "          where  "
                     "          (sech.t_contractkind = sec.t_contractkind) "
                     "          and(sech.t_contractid = sec.t_contractid) "
                     "          and(sech.t_fiid = sec.t_fiid) "
                     "          and(sech.t_securityproperties = sec.t_securityproperties) "
                     "         )) "
                     "UNION ALL "
                     "select distinct sech.*, 0 as sumNominal, sech.t_tcc as t_SumTCC, "
                     "decode('" + prm_order.SysTypes + "', 'Ц', fininstr.t_facevaluefi, fininstr.t_fiid) as faceFI from "
                     "( "
                     "    select * from ddl_secur_hist_dbt sech "
                     "    where "
                     "    (sech.t_contractkind = 126) "
                     "    and(sech.t_contractid = " + prm_order.ContractID + ") "
                     "    order by sech.t_id_step "
                     ")sech, "
                     "( "
                     "  select * from "
                     "  ( "
                     "    select oprop.t_documentid, step.t_id_operation, step.t_id_step from doproper_dbt oprop, doprstep_dbt step where "
                     "    (oprop.t_dockind = 126)and(oprop.t_documentid = " + prm_order.ContractID + ") "
                     "    and(step.t_id_operation = oprop.t_id_operation) "
                     "    and(step.t_symbol = 'Р')and(step.t_isexecute = 'X') "
                     "    order by step.t_id_step desc "
                     "  ) "
                     "  where rownum = 1 "
                     ") opr, "
                     "dfininstr_dbt fininstr "
                     "where  "
                     "(rownum = 1) "
                     "and(fininstr.t_fiid = sech.t_fiid) "
                     "and(opr.t_documentid = sech.t_contractid) "
                     "and(opr.t_id_operation = sech.t_id_operation)"
                     "and(opr.t_id_step = sech.t_id_step)"
                     "and(not exists( "
                     "               select * from ddl_secur_dbt sec "
                     "               where  "
                     "               (sech.t_contractkind = sec.t_contractkind) "
                     "               and(sech.t_contractid = sec.t_contractid) "
                     "               and(sech.t_fiid = sec.t_fiid) "
                     "               and(sech.t_securityproperties = sec.t_securityproperties))) "
                     "UNION ALL "
                     "select distinct 0, 0, sec.*, sec.t_Nominal as sumNominal, sec.t_tcc as t_SumTCC, "
                     "decode('" + prm_order.SysTypes + "', 'Ц', fininstr.t_facevaluefi, fininstr.t_fiid) as faceFI "
                     "from ddl_secur_dbt sec, ddl_secur_hist_dbt sech, dfininstr_dbt fininstr "
                     "where  "
                     "(sec.t_contractkind = sech.t_contractkind) "
                     "and(sec.t_contractid = sech.t_contractid) "
                     "and(sec.t_fiid = sech.t_fiid) "
                     "and(sec.t_securityproperties = sech.t_securityproperties) "
                     "and((sec.t_nominal <> sech.t_nominal)or(sec.t_cost <> sech.t_cost)or(sec.t_quantity <> sech.t_quantity)) "
                     "and(sec.t_contractkind = 126) "
                     "and(sec.t_contractid = " + prm_order.ContractID + ") "
                     "and(fininstr.t_fiid = sec.t_fiid) ";
  
          rsbQuery = TRsbDataSet(QueryStr);
          while (rsbQuery.MoveNext)
              idx = Carry.size;

              Carry[idx] = MM_PledgeCarryBase();

              if (not leadtoTCC)
                  SumTCC = rsbQuery.sumNominal;
              else
                  if (rsbQuery.sumNominal == 0)
                      SumTCC = rsbQuery.sumNominal;
                  elif (GetTCCSum(rsbQuery, SumTCC))
                      mm_error("Невозможно сформировать проводки по ТСС, т.к. в залоге есть ценные бумаги, для которых не установлен курс ТСС");
                      return 1;
                  end;
              end;

              Carry[idx].CarryDate  = prm_dateCarry;
              Carry[idx].CarrySum = SumTCC - rsbQuery.sumTCC;

              if (Carry[idx].CarrySum == 0)
                  Carry.size = Carry.size - 1;
                  continue;
              end;

              if ((Carry[idx].CarrySum < 0)and(prm_mode == EditPledgeP))
                  Carry[idx].CarryMode = SetFromPledge;
              elif ((Carry[idx].CarrySum < 0)and(prm_mode == EditPledgeR))
                  Carry[idx].CarryMode = GetFromPledge;
              elif ((Carry[idx].CarrySum > 0)and(prm_mode == EditPledgeP))
                  Carry[idx].CarryMode = GetInPledge;
              elif ((Carry[idx].CarrySum > 0)and(prm_mode == EditPledgeR))
                  Carry[idx].CarryMode = SetInPledge;
              end;

              PrimaryDoc.SetSecur(rsbQuery.NumSecInOrder);

              Carry[idx].RealMode     = prm_mode;
              Carry[idx].Order        = prm_order;
              Carry[idx].CarrySumFIID = Int(rsbQuery.faceFI);
              Carry[idx].PrimaryDoc   = PrimaryDoc;
              Carry[idx].FIRole       = rsbQuery.NumSecInOrder;
		   
              if (prm_dealID == -1)
                  prm_dealID = 0;
              end;

              MM_ChangeNominalPawn(prm_dealID, prm_order.ContractID,  rsbQuery.NumSecInOrder, Money(abs(SumTCC)), prm_dateCarry);
          end;
      elif ((prm_mode == LeadToTCCSetInledge)or
            (prm_mode == LeadToTCCGetInledge))
            if (leadtoTCC)
                if (prm_dealID <= 0)
                    QueryStr = "select sec.*, fininstr.t_facevaluefi as faceFI from ddl_secur_dbt sec, dfininstr_dbt fininstr where (sec.t_contractkind = " + DL_MMPAWN + ")and "
                		        "(sec.t_contractid = " + prm_order.ContractID + ")and(fininstr.t_fiid = sec.t_fiid)";
                else
                    QueryStr = "select sec.*, fininstr.t_facevaluefi as faceFI from ddl_secur_dbt sec, dfininstr_dbt fininstr where (sec.t_contractkind = " + DL_IBCDOC + ")and "
                		        "(sec.t_contractid = " + prm_dealID + ")and(sec.t_generalcontract = " + prm_order.ContractID + ")and(fininstr.t_fiid = sec.t_fiid)";
                end;

                rsbQuery = TRsbDataSet(QueryStr);
                while (rsbQuery.MoveNext)
                   idx = Carry.size;
                   Carry[idx] = MM_PledgeCarryBase();

                   Carry[idx].CarryDate  = prm_dateCarry;

                   PrimaryDoc.SetSecur(rsbQuery.NumSecInOrder);

                   Carry[idx].CarrySum = rsbQuery.Nominal;

                   if (GetTCCSum(rsbQuery, SumTCC))
                       mm_error("Невозможно сформировать проводки по ТСС, т.к. в залоге есть ценные бумаги, для которых не установлен курс ТСС");
                       return 1;
                   end;

                   Carry[idx].CarrySum = SumTCC - abs(Carry[idx].CarrySum);
                   Carry[idx].RealMode = prm_mode;
                   Carry[idx].Order    = prm_order;
                   Carry[idx].FIRole       = rsbQuery.NumSecInOrder;
                   Carry[idx].CarrySumFIID = Int(rsbQuery.faceFI);
                   Carry[idx].PrimaryDoc   = PrimaryDoc;     
                   Carry[idx].CarrySumFIID = Int(rsbQuery.faceFI);

                   if (prm_mode == LeadToTCCSetInledge)
                       if (Carry[idx].CarrySum < 0)
                           Carry[idx].CarryMode = GetFromPledge
                       else
                           Carry[idx].CarryMode = SetInPledge;
                       end;
                   elif(prm_mode == LeadToTCCGetInledge)
                       if (Carry[idx].CarrySum < 0)
                           Carry[idx].CarryMode = SetFromPledge;
                       else
                           Carry[idx].CarryMode = GetInPledge;
                       end;
                   end;

                    if (prm_dealID == -1)
                        prm_dealID = 0;
                    end;

                    MM_ChangeNominalPawn(prm_dealID, prm_order.ContractID,  rsbQuery.NumSecInOrder, Money(abs(SumTCC)), prm_dateCarry);
                end;
            else
                StartCarry = false;
            end;
      elif ((prm_mode == RevalTCCSetInledge) or
            (prm_mode == RevalTCCGetInledge))
          if (prm_dealID <= 0)
              QueryStr = "select sec.*, fininstr.t_facevaluefi as faceFI from ddl_secur_dbt sec, dfininstr_dbt fininstr where (sec.t_contractkind = " + DL_MMPAWN + ")and "
                         "(sec.t_contractid = " + prm_order.ContractID + ")and(fininstr.t_fiid = sec.t_fiid)";
          else
              QueryStr = "select sec.*, fininstr.t_facevaluefi as faceFI from ddl_secur_dbt sec, dfininstr_dbt fininstr where (sec.t_contractkind = " + DL_IBCDOC + ")and "
                         "(sec.t_contractid = " + prm_dealID + ")and(sec.t_generalcontract = " + prm_order.ContractID + ")and(fininstr.t_fiid = sec.t_fiid)";
          end;

          rsbQuery = TRsbDataSet(QueryStr);
          while (rsbQuery.MoveNext)
              idx = Carry.size;
              Carry[idx] = MM_PledgeCarryBase();

              Carry[idx].CarryDate  = prm_dateCarry;

              PrimaryDoc.SetSecur(rsbQuery.NumSecInOrder);

              Carry[idx].CarrySum = rsbQuery.TCC;
    
              if (GetTCCSum(rsbQuery, SumTCC))
                  mm_error("Невозможно сформировать проводки по ТСС, т.к. в залоге есть ценные бумаги, для которых не установлен курс ТСС");
                  return 1;
              end;

              Carry[idx].CarrySum = SumTCC - abs(Carry[idx].CarrySum);
              Carry[idx].RealMode = prm_mode;
              Carry[idx].Order    = prm_order;
              Carry[idx].FIRole       = rsbQuery.NumSecInOrder;
              Carry[idx].CarrySumFIID = Int(rsbQuery.faceFI);
              Carry[idx].PrimaryDoc   = PrimaryDoc;     
              Carry[idx].CarrySumFIID = Int(rsbQuery.faceFI);

              if (prm_mode == RevalTCCSetInledge)
                  if (Carry[idx].CarrySum < 0)
                     Carry[idx].CarryMode = GetFromPledge
                  else
                      Carry[idx].CarryMode = SetInPledge;
                  end;
              elif(prm_mode == RevalTCCGetInledge)
                  if (Carry[idx].CarrySum < 0)
                      Carry[idx].CarryMode = SetFromPledge;
                  else
                      Carry[idx].CarryMode = GetInPledge;
                  end;
              end;

              if (Carry[idx].CarrySum == 0)
                  Carry.size = Carry.size - 1;
                  continue;
              end;

              if (prm_dealID == -1)
                  prm_dealID = 0;
              end;

              MM_ChangeNominalPawn(prm_dealID, prm_order.ContractID,  rsbQuery.NumSecInOrder, Money(abs(SumTCC)), prm_dateCarry);
          end;
      end;

      if (StartCarry)
          idx = 0;

          if (Carry.size == 0) 
              return 1;
          end;

          while (idx < Carry.size)
              if (Carry[idx].RunCarry())
                  return 1;
             end;

             idx = idx + 1;
          end;
      end;

      return 0;
  end;

  private macro ClassConstructor(_mode, _ord, _dealID, _dateCarry)
      prm_mode   = _mode;
      prm_order  = _ord;
      prm_dealID = _dealID;

      if (ValType(_dateCarry) != V_DATE)
          prm_dateCarry = {curdate};
      else
          prm_dateCarry = _dateCarry;
      end;
  end;

  ClassConstructor(_mode, _ord, _dealID, _dateCarry);
end;
