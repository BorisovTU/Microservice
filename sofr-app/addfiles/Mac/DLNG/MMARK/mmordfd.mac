/*
$Name:             mmordfd.mac                                             
$Module:           МБ Кредиты
$Description:      Класс документов "Договор залога МБК"
*/
IMPORT FIInter, CTInter, OprInter, DealsInter, MmarkInter, globals;

/* Класс документов "Договора МБК"
*/
CLASS MMOrderFD( DocKind_Order, DocId )
    VAR
        ParmA = TArray,
//        FIRoleBArray = TArray,
        ord = TBfile("dl_order.dbt");
    VAR
        /*Основные свойства*/
        Error = 0, kind, id;

    /* Возвращает строку 's'
       после символа 'c' 
       Например: s="abcdefg.xyz",c='.'
       Результат   "xyz"
    */
    PRIVATE MACRO StrAfter( s, c )
    var
       p=Index(s, c);
       if(p == 0)
         return "";
       else
         return SubStr(s, p + 1);
       end;
    END;

    /* Формирование номера документа 'n'
       Возвращается правая часть номера, начиная со второго символа
       справа, до первого вхождения символа '/'.
       Если последний символ - не цифра, то она обрезается.
       Например: n="1/2/3в"  
       Результат:      "3"
    */
    PRIVATE MACRO MakeNumber(n)
    var s = String(n), c, l;
        
        if(s == "")
           return s;
        end;

        l = strlen(s);
        c = SubStr(s, l, 1);        /* последний символ*/
        if( (c < "0") or (c > "9") )     /* если не цифра */
          s = SubStr(s, 1, l - 1);       /* обрежем */
        end;

        while( Index(s, "/") != 0 )
          s = StrAfter(s, "/");  /* вырезаем начало строки до / */
        end;

        return s;
    END;

    PRIVATE MACRO GetFIIDDoc
    var fiid = ALLFININSTR, prevfiid = ALLFININSTR, SecProp, money,
        leg = TBFile("dl_leg.dbt"),
            secur =TBFile("dl_secur.dbt"),
        stat;

        secur.KeyNum = 0;
        secur.rec.ContractKind = DL_MMPAWN;
        secur.rec.ContractID = ord.rec.ContractID;
        secur.rec.FIID = ALLFININSTR;
        if( secur.GetGT )
                if (ord.rec.Systypes == "Ц")
                    SecProp = secur.rec.SecurityProperties;
                    if( NOT SecProp ) 
                        if( FI_GetNominal(secur.rec.FIID, money, fiid) )
                            fiid = ALLFININSTR;
                        end
                    else
                        leg.rewind;
                        leg.KeyNum = 0;
                        leg.rec.LegKind = 1;
                        leg.rec.DealID = SecProp;
                        leg.rec.LegID = 0;
                        if( leg.GetEQ )
                            fiid = leg.rec.PFI;
                        end;
                    end;
                else
                    fiid = secur.rec.fiid;
                end;
        end;

    return fiid;
    END;

    PRIVATE MACRO InitParmArray
        var FIID;

        ParmA[MC_TYPE_PARAMETR_CONTRACTOR] = ord.rec.Contractor;
        ParmA[MC_TYPE_PARAMETR_DOCKIND] = ord.rec.DocKind;
        ParmA[MC_TYPE_PARAMETR_DOCID] = ord.rec.ContractID;
        ParmA[MC_TYPE_PARAMETR_OWNER] = ord.rec.Contractor;
        ParmA[MC_TYPE_PARAMETR_PARTY] = ord.rec.Contractor;
        ParmA[MC_TYPE_PARAMETR_PLACE] = 0;
        ParmA[MC_TYPE_PARAMETR_BEGDATE] = ord.rec.SignDate;
        ParmA[MC_TYPE_PARAMETR_FINDATE] = date (0,0,0);
        ParmA[MC_TYPE_PARAMETR_VALDATE] = ord.rec.SignDate;
        
        FIID = GetFIIDDoc;
        ParmA[MC_TYPE_PARAMETR_FIID] = FIID;
        ParmA[MC_TYPE_PARAMETR_PAYCURRENCY] = FIID;
        ParmA[MC_TYPE_PARAMETR_CURRENCY] = FIID;

        ParmA[MC_TYPE_PARAMETR_NUMBER] = MakeNumber(ord.rec.OrderNumber);
        ParmA[MC_TYPE_PARAMETR_SENIORNUMBER] = "";
        ParmA[MC_TYPE_PARAMETR_DEPARTMENT] = ord.rec.Department;
//        FIRoleBArray[0] = FIROLE_FIDOC;
//        FIRoleBArray[1] = FIROLE_CORACC_PASSIVE;
//        FIRoleBArray[2] = FIROLE_CORACC_ACTIVE;
    END;

    /*Получить параметры*/
    MACRO GetParametr (ParmKind, OperDate, CatCode, FIRole)
        VAR 
            Parametr = ParmA[ParmKind];

        if( ValType(OperDate) != V_DATE )
            OperDate = {curdate};
        end;
        
        if( Parametr == null ) 
            Parametr = -1;
        end;

        return Parametr;
    END;

    MACRO GetParametrTemplate
    ( 
        ObjectID,       /* параметр - номер справочника */
        Classificator,  /* классификатор - номер классификатора, если он задан */
        OperDate,
        FIRole
    )

        if (ObjectID == OBJTYPE_SIDEBALANCE)          /*1000 - справочник сторон баланса*/
            if (Classificator == LLCLASS_SIDEBALANCE_CORACCKIND)       // 1622 - Вид корреспондирующего счета

               if(FIRole == FIROLE_CORACC_PASSIVE)
                  return 2; // Пассивный
               elif(FIRole == FIROLE_CORACC_ACTIVE)
                  return 1; // Активный
               end;

            end;
        end;

        return -1;
    END;

    MACRO GetBasisFIRole(FIRole)
      var i = 0;
      if (FIRole == FIROLE_UNDEF)
        return FIROLE_FIDOC;
      end;
//      while (i < FIRoleBArray.Size)
//        if (FIRoleBArray[i] == FIRole)
          return FIRole;
//        end;
//        i = i + 1;
//      end;
//      Error = 1;
//      return FIROLE_UNDEF;
    END;

//    MACRO GetFIRoleBArray()
//      return FIRoleBArray;
//    END;

    /*Макрос корректировки открываемого счета. Account, accblnc, ORScheme можно менять*/
    MACRO CorrectAccount(account, accblnc, ORScheme, categ, templ, accdoc,  OperDate)
      return true;
    END;

    MACRO SetSecur(_SecurNum)
        ParmA[MC_TYPE_PARAMETR_NUMBER] = MakeNumber(ord.rec.OrderNumber) + String(_SecurNum);
	END;

/* body */
    if ((ValType(DocKind_Order)==V_INTEGER) AND (ValType(DocId)==V_INTEGER))
        /*Конструктор из DocKind, DocID*/
        ord.KeyNum = 0;
        ord.rec.ContractID = DocId;
        if (not ord.GetEQ)
            RunError ("не найден договор с идентификатором " + DocId);
        end;

    else
        /*Конструктор из структур и дополнительных параметров*/
        if (not copy(ord, DocKind_Order))
            RunError ("неверный вызов конструктора");
        end;

    end;

    InitParmArray ();

    Kind = ParmA[MC_TYPE_PARAMETR_DOCKIND];
    Id = ParmA[MC_TYPE_PARAMETR_DOCID];
END;

