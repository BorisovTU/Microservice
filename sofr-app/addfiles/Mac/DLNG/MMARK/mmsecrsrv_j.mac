/*
$Name:        mmsecrsrv_j.mac
$Module:      МБ Кредиты
$Description: аг: Формирование резерва  
*/
/*--------------------------------------------------------------------------------------*/
/*  Имя файла        : mmsecrsrv_j.mac                                                  */
/*                                                                                      */
/*  Описание         : Шаг: Формирование резерва                                        */
/*                                                                                      */
/*  Программист      : Куперин М.В.                                                     */
/*                                                                                      */
/*  Создан           : 25.06.2013                                                       */
/*--------------------------------------------------------------------------------------*/
IMPORT FIInter, mmservop, mmordfd, mmcarry, mmlb_j, mmcnst_j, mmlibr, mctplprm;
/****************************************************************************************/
PRIVATE CONST
    MM_RSRV_ERR_NOPARM       = 11,
    MM_RSRV_ERR_BADCARRY     = 102;
/****************************************************************************************/
PRIVATE CLASS MM_SECRSRVPARM
  var
      Order_fd     = NULL,
      OprServDoc   = NULL,
      ПР           = 0,
      Rnew         = 0,
      Rold         = 0,
      RBase         = 0;
END;
/****************************************************************************************/
PRIVATE MACRO InsertDlResLnk(Parm)
    VAR
        reslnk = TBfile("dlreslnk.dbt");

    reslnk.clear();

    reslnk.rec.ParentID          = Parm.Order_fd.ord.rec.ContractID; // Порождающий документ
    reslnk.rec.ChildID           = Parm.OprServDoc.rec.DocumentID;   // Порожденный документ
    reslnk.rec.Type              = 4;                                // Тип связи
    reslnk.rec.LnkDate           = Parm.OprServDoc.rec.CommDate;     // Дата
    reslnk.rec.QualityCategory   = 0;                                // Категория качества
    reslnk.rec.ReservePercent    = Parm.ПР;                          // Процент резерва
    reslnk.rec.SecurityAmount    = Parm.RBase;                       // Сумма базы расчета
    reslnk.rec.SecurityCurrency  = 0;                                 // Валюта обеспечения 
    reslnk.rec.ReserveKind       = 1;                                // Вид резерва
    reslnk.rec.ReserveDate       = Parm.OprServDoc.rec.CommDate;     // Дата расчета резерва
    reslnk.rec.Flag1             = Parm.OprServDoc.rec.Flag1;        // Валютная корректировка
    reslnk.rec.IsManual          = 0;                                // Признак пользовательской корректировки
    reslnk.rec.RiseQC            = 0;                                // Повышение ККС
    reslnk.rec.ReserveSubKind    = 0;                                //
    reslnk.rec.TypeBase          = 1;                                // Тип базы расчета
    reslnk.rec.ReserveAmount     = Parm.Rnew;                       // Сумма резерва по ОД

    MM_InsertDLRESLNK(reslnk);
END;
/****************************************************************************************/
PRIVATE MACRO Проводка(Parm)
    VAR
        Delta = 0, Основание = "", 
        СписаниеРезерва = false,
        КатДебет, КатКредит,
        ФИРольДебет, ФИРольКредит,
        at = MM_Carry;

    Delta = Parm.Rnew - Parm.Rold;
    if ((Parm.Rnew == 0)and(Parm.Rold > 0))
        Основание = "Списание резерва";
    elif ((Parm.Rnew > 0)and(Parm.Rold == 0))
        Основание = "Создание резерва";
    elif (Delta > 0)
        Основание = "Увеличение резерва";
    elif (Delta < 0)
        Основание = "Уменьшение резерва";
    else
        return 1;
    end;

    if (Delta > 0)
        КатДебет     = tdr_rezerve6;
        КатКредит    = "Р, ц/б в обеспеч.";
        ФИРольДебет  = FIROLE_RVP;
        ФИРольКредит = FIROLE_FIDOC;
    else
        КатДебет     = "Р, ц/б в обеспеч.";
        КатКредит    = tdr_rezerve5;
        ФИРольДебет  = FIROLE_FIDOC;
        ФИРольКредит = FIROLE_RVP;
    end;

    at.date_carry       = Parm.OprServDoc.rec.CommDate;
    at.PrimaryDoc       = Parm.Order_fd;
    at.FIIDPayer        = 
    at.FIIDReceiver     = NATCUR;
    at.CatPayer         = КатДебет;
    at.CatReceiver      = КатКредит;
    at.FiRolePayer      = ФИРольДебет;
    at.FiRoleReceiver   = ФИРольКредит;
    at.SumPayer         = 
    at.SumReceiver      = abs(Delta);
    at.Chapter          = 1;
    at.Ground           = Основание;

    if (not at.carry())
        return 1; 
    end;	

    return 0;
END;

PRIVATE MACRO ПроводкиШага(Parm)
    VAR stat = 0;

    if (Parm.Rnew != Parm.Rold)
        stat = Проводка(Parm);
        if (not stat)
            InsertDlResLnk(Parm);
        end;
    else
        return 1;
    end;

    if (stat)
        SvOpSayError(Parm.Order_fd.ord.rec.OrderNumber, MM_RSRV_ERR_BADCARRY, "Ошибка при формировании проводок");
        return 1;
    end;

    return 0;
END;
/****************************************************************************************/
PRIVATE MACRO РанееСозданныйРезерв(Parm, LastDate)
    VAR
        ResLnk = TRsbDataSet("select t_reserveamount, t_reservedate " + 
                             "from ddlreslnk_dbt " +
                             "where t_parentid = " + Parm.Order_fd.ord.rec.ContractID +
                             " and t_reservedate <= " + "'" + Date(Parm.OprServDoc.rec.CommDate) + "'" +
                             " and t_typebase = 1 "
                             " and t_ismanual = '0' " +
                             " order by T_RESERVEDATE DESC, T_ID DESC");

    if (ResLnk.MoveNext())
        if (ValType(LastDate) != V_UNDEF)
            LastDate = date(ResLnk.ReserveDate);
            SetParm(2, LastDate);
        end;

        return ResLnk.ReserveAmount;
    end;

    return 0;
END;    

PRIVATE MACRO ОпределитьРанееСозданныйРезерв(Parm)
    Parm.Rold = РанееСозданныйРезерв(Parm);
    SetParm(0, Parm);
END;

PRIVATE MACRO GetTCCSum(ContractID, Kind_Operation, Flag1, OperDate)
    VAR
        DataSet = NULL;

    if (not IsCONVERT_RECEIPT(GetOperationGroup(Kind_Operation)))
        if (Flag1 == "X")
            DataSet = TRsbDataSet("select RSI_RSB_FIINSTR.convsum( "
                                  "                                ord.t_tcc, "
                                  "                                ( "
                                  "                                  select t_facevaluefi from "
                                  "                                  ( "
                                  "                                    select fininstr.t_facevaluefi from ddl_secur_dbt sec, dfininstr_dbt fininstr "
                                  "                                    where (sec.t_contractkind = " + DL_MMPAWN + ")and(sec.t_contractid = " + ContractID + ") "
                                  "                                    and(fininstr.t_fiid = sec.t_fiid) "
                                  "                                  ) where rownum = 1 "
                                  "                                ), "
                                  "                                0, "
                                  "                                TO_DATE('" + Date(OperDate) + "', 'dd.mm.yyyy'), "
                                  "                                0 "
                                  "                               ) as sumTCC "
                                  "from ddl_order_dbt ord where ord.t_contractid = " + ContractID);
        else
            DataSet = TRsbDataSet("select SUM(RSI_RSB_FIINSTR.convsum( "
                                  "                                    grnt.t_tcc, "
                                  "                                    secfi.t_facevaluefi, "
                                  "                                    0, "
                                  "                                    TO_DATE('" + Date(OperDate) + "', 'dd.mm.yyyy'), "
                                  "                                    0 "
                                  "                                   ) "
                                  "               ) as sumTCC "
                                  "from ddl_grnt_dbt grnt, ddl_tick_dbt tick, "
                                  "( "
                                  "  select distinct sec.t_contractid, fininstr.t_facevaluefi from ddl_secur_dbt sec, dfininstr_dbt fininstr "
                                  "  where (sec.t_contractkind = " + DL_IBCDOC + ")and(sec.t_generalcontract = " + ContractID + ") "
                                  "  and(fininstr.t_fiid = sec.t_fiid) "
                                  ") secfi "
                                  "where grnt.t_contractid = " + ContractID + " and tick.t_DealID = grnt.t_DealID "
                                  "and secfi.t_contractid =  tick.t_DealID "
                                  "and MMARK_UTL.IsMMARK(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0 "
                                  "and MMARK_UTL.IsFull(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0 "
                                  "and MMARK_UTL.IsSALE(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0");
        end;
    else
        if (Flag1 == "X")
            DataSet = TRsbDataSet("select SUM(RSI_RSB_FIINSTR.convsum( "
                                  "                                    sec.t_tcc, "
                                  "                                    fininstr.t_facevaluefi, "
                                  "                                    0, "
                                  "                                    TO_DATE('" + Date(OperDate) + "', 'dd.mm.yyyy'), "
                                  "                                    0 "
                                  "                                   ) "
                                  ") as sumTCC "
                                  "from ddl_secur_dbt sec, dfininstr_dbt fininstr "
                                  "where (sec.t_contractkind = " + DL_MMPAWN + ")and(sec.t_contractid = " + ContractID + ") "
                                  "and(fininstr.t_fiid = sec.t_fiid)");
        else
            DataSet = TRsbDataSet("select SUM(RSI_RSB_FIINSTR.convsum( "
                                  "                                    sec.t_tcc, "
                                  "                                    fininstr.t_facevaluefi, "
                                  "                                    0, "
                                  "                                    TO_DATE('" + Date(OperDate) + "', 'dd.mm.yyyy'), "
                                  "                                    0 "
                                  "                                   ) "
                                  "           ) as sumTCC "
                                  "from ddl_secur_dbt sec, dfininstr_dbt fininstr, "
                                  "( "
                                  "  select tick.t_DealID from ddl_grnt_dbt grnt, ddl_tick_dbt tick "
                                  "  where grnt.t_contractid = " + ContractID + " and tick.t_DealID = grnt.t_DealID "
                                  "  and MMARK_UTL.IsMMARK(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0 "
                                  "  and MMARK_UTL.IsFull(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0 "
                                  "  and MMARK_UTL.IsSALE(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0"
                                  ") deals "
                                  "where sec.t_contractkind = " + DL_IBCDOC + " and sec.t_contractid = deals.t_dealid "
                                  "and sec.t_generalcontract = " + ContractID + " and fininstr.t_fiid = sec.t_fiid");
        end;
    end;

    if (DataSet.MoveNext())
        return DataSet.sumTCC;
    end;

    return 0;
END;

PRIVATE MACRO GetSumUnsafeDepoSecur(ContractID, Kind_Operation, Flag1, OperDate)
    VAR
        DataSet = NULL,
        Querystr = "";

    if (Flag1 == "X")
        Querystr = "select SUM(RSI_RSB_FIINSTR.convsum( ";
        if (not IsCONVERT_RECEIPT(GetOperationGroup(Kind_Operation)))
            Querystr = Querystr +
                    "                                     (ord.t_tcc * sec.t_cost / ord.t_cost) , ";
        else
            Querystr = Querystr +
                    "                                    sec.t_tcc, ";
        end;
        Querystr = Querystr +
                    "                                    fininstr.t_facevaluefi, "
                    "                                    0, "
                    "                                    TO_DATE('" + Date(OperDate) + "', 'dd.mm.yyyy'), "
                    "                                    0 "
                    "                                   ) "
                    ") as sumSecur "
                    "from ddl_secur_dbt sec, dfininstr_dbt fininstr, ddl_order_dbt ord, "
                    "( "
                    "  select TO_NUMBER(ltrim(t_object, '0')) as t_object, MAX(t_validfromdate)  from dobjatcor_dbt where "
                    "  t_objecttype = 3 and t_groupid = 52 "
                    "  and t_general = 'X' "
                    "  AND t_attrid = 1 "
                    "  and TO_DATE('" + Date(OperDate) + "', 'dd.mm.yyyy') BETWEEN T_VALIDFROMDATE AND T_VALIDTODATE "
                    "  group by  t_object "
                    ") ctg "
                    "where (sec.t_contractkind = " + DL_MMPAWN + ")and(sec.t_contractid = ord.t_ContractID) "
                    "and(fininstr.t_fiid = sec.t_fiid)and(ord.t_ContractID = " + ContractID + ") "
                    "and (sec.t_depositorydepo = ctg.t_object or sec.t_depositoryverify = ctg.t_object)";
    else
        Querystr = "select SUM(RSI_RSB_FIINSTR.convsum( ";
        if (not IsCONVERT_RECEIPT(GetOperationGroup(Kind_Operation)))
            Querystr = Querystr +
                    "                                   (deals.t_tcc * sec.t_cost / deals.t_amount), ";
        else
            Querystr = Querystr +
                    "                                    sec.t_tcc, ";
        end;
            Querystr = Querystr +
                    "                                    fininstr.t_facevaluefi, "
                    "                                    0, "
                    "                                    TO_DATE('" + Date(OperDate) + "', 'dd.mm.yyyy'), "
                    "                                    0 "
                    "                                   ) "
                    "           ) as sumSecur "
                    "from ddl_secur_dbt sec, dfininstr_dbt fininstr, "
                    "( "
                    "  select tick.t_DealID, grnt.t_Amount, grnt.t_tcc from ddl_grnt_dbt grnt, ddl_tick_dbt tick "
                    "  where grnt.t_contractid = " + ContractID + " and tick.t_DealID = grnt.t_DealID "
                    "  and MMARK_UTL.IsMMARK(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0 "
                    "  and MMARK_UTL.IsFull(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0 "
                    "  and MMARK_UTL.IsSALE(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0"
                    ") deals, "
                    "( "
                    "  select TO_NUMBER(ltrim(t_object, '0')) as t_object, MAX(t_validfromdate)  from dobjatcor_dbt where "
                    "  t_objecttype = 3 and t_groupid = 52 "
                    "  and t_general = 'X' "
                    "  AND t_attrid = 1 "
                    "  and TO_DATE('" + Date(OperDate) + "', 'dd.mm.yyyy') BETWEEN T_VALIDFROMDATE AND T_VALIDTODATE "
                    "  group by  t_object "
                    ") ctg "
                    "where sec.t_contractkind = " + DL_IBCDOC + " and sec.t_contractid = deals.t_dealid "
                    "and sec.t_generalcontract = " + ContractID + " and fininstr.t_fiid = sec.t_fiid "
                    "and (sec.t_depositorydepo = ctg.t_object or sec.t_depositoryverify = ctg.t_object)";
    end;

    DataSet = TRsbDataSet(Querystr);
    if (DataSet.MoveNext())
        return DataSet.sumSecur;
    end;

    return 0;
END;

PRIVATE MACRO РасчетНовойСуммыРезерва(Parm)
    VAR
        idx = 0,
        deal_pd = NULL,
        DataSet = NULL,
        Srez = 0, Sost = 0,
        Sdeal = 0, Scp = 0, ElRB = 0;

    Parm.RBase = GetTCCSum(Parm.Order_fd.ord.rec.ContractID, Parm.Order_fd.ord.rec.Kind_Operation, Parm.Order_fd.ord.rec.Flag1, Parm.OprServDoc.rec.CommDate);

    Srez = GetSumUnsafeDepoSecur(Parm.Order_fd.ord.rec.ContractID, Parm.Order_fd.ord.rec.Kind_Operation, Parm.Order_fd.ord.rec.Flag1, Parm.OprServDoc.rec.CommDate);

    DataSet = TRsbDataSet("select ord.* "
                          " from ddl_grnt_dbt grnt, ddl_order_dbt ord, "
                          "        ( "
                          "          select distinct tick.t_DealID "
                          "          from ddl_grnt_dbt grnt, ddl_tick_dbt tick, ddl_leg_dbt leg "
                          "          where "
                          "          grnt.t_ContractID = " + Parm.Order_fd.ord.rec.ContractID +
                          "          and tick.t_DealID = grnt.t_DealID "
                          "          and MMARK_UTL.IsMMARK(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0 "
                          "          and MMARK_UTL.IsFull(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0 "
                          "          and MMARK_UTL.IsSALE(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0 "
                          "          and leg.t_DealID = tick.t_DealID and leg.t_LegID = 1 and leg.t_LegKind = 0 "
                          "        ) deals "
                          " where "
                          " grnt.t_DealID = deals.t_DealID and ord.t_ContractID = grnt.t_ContractID");
    while (DataSet.MoveNext())
        Sost = Sost + GetTCCSum(DataSet.ContractID, DataSet.Kind_Operation, DataSet.Flag1, Parm.OprServDoc.rec.CommDate);
    end;

    DataSet = TRsbDataSet("select distinct tick.t_DealID, leg.t_PFI "
                          " from ddl_grnt_dbt grnt, ddl_tick_dbt tick, ddl_leg_dbt leg "
                          " where "
                          " grnt.t_ContractID = " +Parm.Order_fd.ord.rec.ContractID +
                          " and tick.t_DealID = grnt.t_DealID "
                          " and MMARK_UTL.IsMMARK(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0 "
                          " and MMARK_UTL.IsFull(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0 "
                          " and MMARK_UTL.IsSALE(MMARK_UTL.GetOperationGroup(tick.t_DealType)) > 0 "
                          " and leg.t_DealID = tick.t_DealID and leg.t_LegID = 1 and leg.t_LegKind = 0");
    while (DataSet.MoveNext())
        deal_pd = MMFirstDoc(DL_IBCDOC, DataSet.DealID);
        Sdeal = 0;
        ПолучитьОстатокСчета(deal_pd, tdr_rest(deal_pd.GetTick().FlagTypeDeal), NULL, 1, Sdeal, Parm.OprServDoc.rec.CommDate, DataSet.PFI);
        ConvSum(Sdeal, Sdeal, Parm.OprServDoc.rec.CommDate, DataSet.PFI, NATCUR);
        Scp = Scp + Sdeal;
        Sdeal = 0;
        ПолучитьОстатокСчета(deal_pd, tdr_exprestR, NULL, 1, Sdeal, Parm.OprServDoc.rec.CommDate, DataSet.PFI);
        ConvSum(Sdeal, Sdeal, Parm.OprServDoc.rec.CommDate, DataSet.PFI, NATCUR);
        Scp = Scp + Sdeal;
    end;

    if (Sost != 0)
      ElRB = Min((Scp / Sost), 1) * Srez;
    else
      ElRB = Srez;
    end;
    Parm.Rnew = money(Round(ElRB * (Parm.ПР/100), 2));

    SetParm(0, Parm);

    return 0;
END;

PRIVATE MACRO MM_CalcSecReserve(Parm)
    VAR 
        stat = 0,
        DataSet = NULL;

    GetRegistryValue("MMARK\\DEFAULT_RESERVE\\ПРОЦЕНТ_РЕЗЕРВА_ЦБ_ВОБЕСПЕЧЕНИИ", V_DOUBLE, Parm.ПР, stat);
    if(stat)
        SvOpSayError(Parm.Order_fd.ord.rec.OrderNumber, MM_RSRV_ERR_NOPARM, "Не заданы параметры резервирования");
        return 1;
    end;

    ОпределитьРанееСозданныйРезерв(Parm);

    if (РасчетНовойСуммыРезерва(Parm) != 0)
        return 1;
    end;

    SetParm(0, Parm);

    return 0;
END;
/****************************************************************************************/
MACRO ExecuteStep(Buffer, Document)
    VAR 
        stat  = 0,
        Parm  = MM_SECRSRVPARM,
        Error = "";

    record order("dl_order");

    SetBuff(order, Document);
    DL_PrepareCarryBuffer (Buffer);

    Parm.OprServDoc = GetOprServDoc();
    Parm.Order_fd   = MMOrderFD(order.DocKind, order.ContractID);

    stat = MM_CalcSecReserve(Parm);

    if (stat == 0)
        stat = ПроводкиШага(Parm);
    end;

    return stat;
END;
/****************************************************************************************/
NameMacroFile = "mmsecrsrv_j.mac";
