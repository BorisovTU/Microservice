/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : dlprxytg.mac
  Programmer  : Азарёнок Ю.Г.
  Операция    : Тэг для представителей сторон договора
└───────────────────────────────────────────────────────────────────────────*/
import "padej.mac";

var  ProxyMan = null;  /* экземпляр класса ПредстСторонДог */

/*  Формулировка сведений о доверенности
*/
PRIVATE MACRO MandateCaption(spg)
var name       = LLVALname(OBJTYPE_KINDDOC, spg.rec.Kind),
    AltXld     = spg.rec.AltXld,
    SignedDate = spg.rec.SignedDate,
    Comment    = spg.rec.Comment;

    if(name=="") 
      name = "   ";      /*не задано*/;
    end;

    if(AltXld != "") 
      AltXld = String(" № ",AltXld); 
    end;

    if(SignedDate != date(0,0,0))
      SignedDate = String(" от ", SignedDate:m);
    else
      SignedDate = "";
    end;

    if (Comment != "")
      Comment = String(", выдан ", Comment);
    end;

 return String(Name, AltXld, SignedDate, Comment);
END;

/*  Формулировка сведений о первом лице
*/
PRIVATE MACRO ProxyTotal(proxy, spg)
var
   AgentPost=proxy.rec.AgentPost,
   AgentName=proxy.rec.AgentName,
   mandCap=MandateCaption(spg),
   ret="";
   
   if(AgentName=="") 
      /*не задано*/;
   elif(mandCap=="")
      /*не задано*/;
   else
    ret=String("в лице ", AgentPost," ",AgentName,
               ", действующего на основании ", MandCap);
   end;

   return ret;
END;


/* Вспомогательный класс для хранения информации по одному Представителю
*/
CLASS Представитель ( proxy, spg, flag )
var 
     Предст = TRecHandler ("dl_proxy"),  
     РегДок =  TRecHandler ("spground"),
     default = false,
     Предъявитель = false,    /* Сформирован Предъявитель ? */
     Получатель = false;      /* Сформирован Получатель ? */

  copy( Предст, proxy);
  copy( РегДок, spg);

  if ( flag )
   default = true; /* Представитель по умолчанию (первый попавшийся) */
  end;

  if(proxy.rec.ProxyRole == DL_PROXY_RECIPIENT)
    Предъявитель = true;
  elif (proxy.rec.ProxyRole == DL_PROXY_PAYEE)
    Получатель = true;
  end;

END;


/* Представители сторон договора */
CLASS ПредстСторонДог ( )
 var  МассивПредстБанка   = TArray, /* Массив информации по */
      МассивПредстКлиента = TArray;

 МассивПредстБанка.size = 0;
 МассивПредстКлиента.size = 0;

  /*  Поиск регистрирующего документа по идентификатору
  */
  MACRO НайтиSpg(spg, ID)
     var 
        ret, otype=ValType(spg);
     if(otype==V_STRUC)
        spg.SPgroundID=ID;
        ret=GetEQ(spg);
     elif(otype==V_GENOBJ)
        spg.KeyNum=0;
        spg.rec.SPgroundID=ID;
        ret=spg.GetEQ;
     else
        ret=false;
     end;
     return ret;
  END;

  MACRO ЗаполниМассивыПредставитей(DocKind, ContractID)
  PRIVATE var proxy = TBfile ("dl_proxy"),
              spg   = TBfile ("spground"),
              flag1  = true, /* Первый попавшийся представитель клиента */
              flag2  = true, /* Первый попавшийся представитель банка */
              Ok, i;

      proxy.KeyNum            = 0;
      proxy.rec.DocKind       = DocKind;
      proxy.rec.ContractID    = ContractID;
      proxy.rec.ContractParty = 0;
      proxy.rec.ProxyRole     = 0;

      Ok=proxy.GetGE;

      i=0;
      while(Ok)
         Ok=((proxy.rec.ContractID==ContractID) and (proxy.rec.DocKind == DocKind));
         if(Ok)
            if(not НайтиSpg(spg, proxy.rec.ProxyID))
              return Ошибка("Не найден документ представителя");
            elif (proxy.rec.ContractParty   == DLPROXY_OURBANK)  
              МассивПредстБанка[МассивПредстБанка.size] = Представитель(proxy, spg, flag2);
              flag2 = false; /* т.е. все следующие уже не первые попавшиеся */
            elif (proxy.rec.ContractParty == DLPROXY_CONTRACTOR)
              МассивПредстКлиента[МассивПредстКлиента.size] = Представитель(proxy, spg, flag1);
              flag1 = false; /* т.е. все следующие уже не первые попавшиеся */
            end;
            i=i+1;
            Ok=proxy.next;
         end;
      end;
      return 0;
  END;

  MACRO НайдиПредстПоУмолч( Сторона )
   var i=0, arr = null, размер = 0, found = false;

     if (Сторона == DLPROXY_OURBANK)
       arr = МассивПредстБанка;
       размер = МассивПредстБанка.size;
     elif(Сторона == DLPROXY_CONTRACTOR)
       arr = МассивПредстКлиента;
       размер = МассивПредстКлиента.size;
     end;

      while (( not found) and (i < размер))
        if ((arr[i].Предст.rec.ContractParty == Сторона) and (arr[i].default == true))
          found = true;
          return i;
        end;
        if ( not found)
         i=i+1;
        end;
      end;
     return -1;
  END;

END;

// Документ основание полномочий в падеже
private macro GetMandateCaptionAlt(mandate)
  if ((ValType(mandate) != V_STRING) or (StrLen(mandate) == 0))
    return "";
  end;
  
  var unusedPartIndex = Index(mandate, ", выдан ");
  if (unusedPartIndex > 0)
    mandate = SubStr(mandate, 1, unusedPartIndex - 1);
  end;
  
  if (Index(mandate, "Устав") == 1)
    return StrSubst(mandate, "Устав", "устава");
  elif (Index(mandate, "Доверенность") == 1)
    return StrSubst(mandate, "Доверенность", "доверенности");
  elif (Index(mandate, "Договор") == 1)
    return StrSubst(mandate, "Договор", "договора");
  elif (Index(mandate, "ый договор") > 0)
    return StrLwr(StrSubst(mandate, "ый договор", "ого договора"));
  elif (Index(mandate, "Поручение") == 1)
    return StrSubst(mandate, "Поручение", "поручения");
  end;
  
  return mandate;
end;

/*  Шаблон для представителя.
    Если flag = true, роль брать из role
*/
PRIVATE MACRO ШаблонДляПредставителя ( proxy, spg, flag, role)
var parm,
  mCaption="",
  pTotal= "", 
  prefix ="";

  if (spg != null)
    pTotal = ProxyTotal(proxy,spg);
    mCaption=MandateCaption(spg);
  end;

  if (proxy.rec.ContractParty   == DLPROXY_OURBANK)
   prefix="Self"
  elif (proxy.rec.ContractParty == DLPROXY_CONTRACTOR)
   prefix ="Counterparty";
  end;

  if ( (GetParm(2,parm) AND (ValType(parm)!=V_BOOL)) AND
       (GetParm(3,parm) AND (ValType(parm)!=V_INTEGER) ) )
   role = proxy.rec.ProxyRole;
  end;

  if (prefix != "")
    var proxyNameAlt = Падеж(proxy.rec.AgentName, 2);
    println(String("~", prefix, "ProxyName", role ));
    println(proxy.rec.AgentName);
    
    println(String("~", prefix, "ProxyNameAlt", role ));
    println(proxyNameAlt);

    println(String("~", prefix, "ProxyNameShort", role ));
    println(ФамилияИИнициалы(proxy.rec.AgentName));
    
    println(String("~", prefix, "ProxyNameShortAlt", role ));
    println(ФамилияИИнициалы(proxyNameAlt));

    println(String("~", prefix, "ProxyAuthority", role ));
    println(proxy.rec.AgentPost);
    
    println(String("~", prefix, "ProxyAuthorityAlt", role ));
    println(StrLwr(ПадежП(proxy.rec.AgentPost, 2)));

    if(mCaption != "")
       println(String("~", prefix, "ProxyMandateCaption", role ));
       println(mCaption);
       
       println(String("~", prefix, "ProxyMandateCaptionA", role ));
       println(GetMandateCaptionAlt(mCaption));
    end;

    if((role == DL_PROXY_BOSS) and (pTotal != ""))
       [~ProxyTotal](role);
       println(pTotal);
    end;
  end;

  return 0;
END;

/*  Тэг для представителей
*/
MACRO ProxyTag (DocKind, ContractID, Contractor, PrintAnyRole)
var stat=0,i=0, 
    pt   = TRecHandler ("party"),
    buff = TRecHandler ("dl_proxy"),  
    ПолучательБанка     = false,
    ПредъявительБанка   = false,

    ПредъявительКлиента = false,
    ПолучательКлиента   = false ;

    if (ValType(PrintAnyRole) == V_UNDEF )
      PrintAnyRole = false;
    end;

    ProxyMan= ПредстСторонДог();

    stat = ProxyMan.ЗаполниМассивыПредставитей(DocKind, ContractID);

    while((stat == 0) and (i < ProxyMan.МассивПредстБанка.size))
      if(ProxyMan.МассивПредстБанка[i].Предст.rec.ProxyRole == DL_PROXY_PAYEE)
       ПредъявительБанка = true; /* Предъявитель векселя найден */
      end;
      if(ProxyMan.МассивПредстБанка[i].Предст.rec.ProxyRole == DL_PROXY_RECIPIENT)
       ПолучательБанка = true;/* Получатель векселя найден */
      end;

      ШаблонДляПредставителя (ProxyMan.МассивПредстБанка[i].Предст, 
                              ProxyMan.МассивПредстБанка[i].РегДок,
                              IIF(PrintAnyRole, true, NULL),
                              IIF(PrintAnyRole, DL_PROXY_BOSS, NULL));
      i = i + 1;
    end;
    if ((stat == 0) and (not ПредъявительБанка))
      /* найдем индекс представителя по умолчанию */
      i=ProxyMan.НайдиПредстПоУмолч( DLPROXY_OURBANK);
      if (i != -1)
        ПредъявительБанка = true; /* т.е. считаем умолчательного представ. Предъявителем */
        ШаблонДляПредставителя ( ProxyMan.МассивПредстБанка[i].Предст, 
                                 ProxyMan.МассивПредстБанка[i].РегДок, 
                                 true,
                                 DL_PROXY_PAYEE  );
      end;
    end;
    if ((stat == 0) and (not ПолучательБанка))
      /* найдем индекс представителя по умолчанию */
      i=ProxyMan.НайдиПредстПоУмолч( DLPROXY_OURBANK);
      if (i != -1)
        ПолучательБанка = true; /* т.е. считаем умолчательного представ. Получателем */
        ШаблонДляПредставителя ( ProxyMan.МассивПредстБанка[i].Предст, 
                                 ProxyMan.МассивПредстБанка[i].РегДок, 
                                 true,
                                 DL_PROXY_RECIPIENT  );
      end;
    end;
 

   
    i = 0;
    while((stat == 0) and (i < ProxyMan.МассивПредстКлиента.size))
      if(ProxyMan.МассивПредстКлиента[i].Предст.rec.ProxyRole == DL_PROXY_RECIPIENT)
       ПолучательКлиента = true;/* Получатель векселя найден */
      end;
      if(ProxyMan.МассивПредстКлиента[i].Предст.rec.ProxyRole == DL_PROXY_PAYEE)
       ПредъявительКлиента = true;/* Предъявитель векселя найден */
      end;

      ШаблонДляПредставителя ( ProxyMan.МассивПредстКлиента[i].Предст, 
                               ProxyMan.МассивПредстКлиента[i].РегДок);
      i = i + 1;
    end;

    if ((stat == 0) and (not ПредъявительКлиента))
      i = ProxyMan.НайдиПредстПоУмолч( DLPROXY_CONTRACTOR );
      if (i != -1)
        ПредъявительКлиента = true; /* т.е. считаем умолчательного представ. Предъявителем Клиента */
        ШаблонДляПредставителя ( ProxyMan.МассивПредстКлиента[i].Предст, 
                                 ProxyMan.МассивПредстКлиента[i].РегДок,
                                 true,
                                 DL_PROXY_RECIPIENT
                               );
      else /* А представителя по умолчанию тоже нет :( */
          /* Проверка является ли представитель физ.лицом */
          if (( ПолучитьСубъекта (Contractor, pt)) OR
              ( pt.rec.LegalForm != PTLEGF_PERSN )) /* не физ.лицо*/
            ; /* для контрагена юр.лица ничего не выводим */
          elif( pt.rec.LegalForm == PTLEGF_PERSN )
            buff.rec.AgentName     = pt.rec.Name;
            buff.rec.ContractParty = DLPROXY_CONTRACTOR;
            buff.rec.ProxyRole     = DL_PROXY_RECIPIENT;
            ШаблонДляПредставителя ( buff );
          end;
      end;
    end;
    if ((stat == 0) and (not ПолучательКлиента))
      i = ProxyMan.НайдиПредстПоУмолч( DLPROXY_CONTRACTOR );
      if (i != -1)
        ПолучательКлиента = true; /* т.е. считаем умолчательного представ. Получателем Клиента */
        ШаблонДляПредставителя ( ProxyMan.МассивПредстКлиента[i].Предст, 
                                 ProxyMan.МассивПредстКлиента[i].РегДок,
                                 true,
                                 DL_PROXY_PAYEE
                              );
      else /* А представителя по умолчанию тоже нет :( */
          /* Проверка является ли представитель физ.лицом */
          if (( ПолучитьСубъекта ( Contractor, pt)) OR
              ( pt.rec.LegalForm != PTLEGF_PERSN )) /* не физ.лицо*/
            ; /* для контрагена юр.лица ничего не выводим */
          elif( pt.rec.LegalForm == PTLEGF_PERSN )
            buff.rec.AgentName     = pt.rec.Name;
            buff.rec.ContractParty = DLPROXY_CONTRACTOR;
            buff.rec.ProxyRole     = DL_PROXY_PAYEE;
            ШаблонДляПредставителя ( buff );
          end;

      end;
    end;

     return (stat == 0);
END;
