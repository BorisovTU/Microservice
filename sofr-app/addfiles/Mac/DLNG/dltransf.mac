/*
$Name:         dltransf.mac
$Module:       Ядро Securities
$Description:  Ф-и для переноса по срокам
*/

IMPORT BankInter, CTInter, Календарь, RsbDataSet;

CONST TRANSFERMODE_PARM = "COMMON\\КАТЕГОРИИ СРЕДСТВ\\ПЕРЕНОС ПО СРОКАМ В НЕРАБ. ДНИ",
      TRANSFERMODE_NO       = 0, /*Без переноса по срокам*/
      TRANSFERMODE_LASTDAY  = 1, /*В последний раб. день перед выходными*/
      TRANSFERMODE_FIRSTDAY = 2; /*В первый раб. день после выходных*/

CONST TRANSFER_CALENDAR = 10003;

MACRO GetTransferMode()
   var TransferMode, ErrCode;

   GetRegistryValue( TRANSFERMODE_PARM, V_INTEGER, TransferMode, ErrCode );
   if( ErrCode != 0 )
      MsgBox( "Ошибка при чтении настройки \"" + TRANSFERMODE_PARM + "\".|Используется значение \"Без переноса\".");
      TransferMode = 0;
   end;

   return TransferMode;
END;

/*Получить дату определения срочности для переноса по срокам
  Period    - mcperiod, соответствующий срочному счету
  OprDate   - Дата вполнения шага (актуализации счетов "Форвард"). 
              Для первой части (или сделок без обр. части) - дата сделки, для второй - дата учета 
              первой части, либо дата изменения условий сделки, если она больше
  DateBegin - Базовая дата (параметр MC_TYPE_PARAMETR_FINDATE для роли FIROLE_FIREQ/для роли FIROLE_FICOM )*/ 
MACRO GetTransferActionDate( Period:VARIANT, OprDate:DATE, DateBegin:DATE )
   /* дата переноса по срокам, это дата окончания текущего периода срочности по счету */
   return ( OprDate + (DateBegin - MC_CalcPeriodBound(OprDate, Period, false)) );
END;

/*Получить дату актуализации переноса по срокам. На эту дату планируются шаги, выполняться 
  проводки и открываться счета для переноса по срокам
  ActionDate - дата из GetTransferActionDate */
MACRO GetTransferActivateDate( Period:VARIANT, ActionDate:DATE )
   var  ActivateDate,
        Mode = GetTransferMode(),
        CalendarID = TRANSFER_CALENDAR;

   if( IsWorkdayTransfer( ActionDate, CalendarID ) == 0 ) /*Дата переноса попала на выходной*/
      if( (Mode == TRANSFERMODE_NO) OR (Mode == TRANSFERMODE_FIRSTDAY) )
         ActivateDate = GetDateAfterWorkDays( ActionDate, 0, CalendarID );
      else
         ActivateDate = GetDateAfterWorkDays( ActionDate, -1, CalendarID );
      end;
   else 
      ActivateDate = ActionDate;
   end;

   if( ActivateDate != ActionDate )
      if( (Period.LowBound <= 1) OR (Period.Number == 1) ) /* очередной период срочности - до востребования (с нулевым сроком)*/ 
         if( Mode == TRANSFERMODE_NO )
            ActivateDate = Date( 0, 0, 0 ); /*Перенос по срокам не нужен*/
         end;
      end;
   end;

   return ActivateDate;
END;
