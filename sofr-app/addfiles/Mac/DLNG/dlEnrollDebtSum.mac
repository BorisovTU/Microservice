/**                                                                                                             
 @file  dlEnrollDebtSum.mac                                                                                           
 @brief Разовый макрос начального решения по переносу на просрочку не оплаченных комиссий "БрокерФикс" и "ИнвестСоветник", по которым прошел срок плановой оплаты                                                                         
                                                                                                                
 #tag                                                                                                          
 - code_type:GUI 
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.09.24 |Жиц М.В.       |BOSS-2234           |Создание макроса начального решения                                                                                                                                                 
*/ 

import sfcommon, "DebtSumOp_Common.mac";

/**
 @brief Выставление признака выноса комиссии на просрочку
 @param[in]  DebtSourceID  идентификатор источника задолженности (ID комиссии) 
*/                    
PRIVATE MACRO SetDebtOverdue(DebtSourceID:integer)
  var cmd;
  cmd = RSDCommand(" UPDATE dsfdef_dbt " +
                   "    SET t_IsDebtOverdue = 'X' " +
                   "  WHERE t_FeeType = " + SF_FEE_TYPE_PERIOD +                                                                                
                   "    AND t_ID = ? "
                  );
  cmd.addParam("", RSDBP_IN, DebtSourceID); 

  cmd.execute();
END;

/**
 @brief Отражение задолженности по начисленным ранее комиссиям "БрокерФикс" или "ИнвестСоветник" после истечения срока платежа
 @param[in]  N               порядковый номер записи (используется в номере проводки) 
 @param[in]  CarryDate       дата проводки 
 @param[in]  CommCode        наименование вида комиссии 
 @param[in]  DebtSourceID    идентификатор источника задолженности (ID комиссии)
 @param[in]  SfContrID       идентификатор субдоговора 
 @param[in]  DatePeriodBegin дата начала действия комиссии 
 @param[in]  DatePeriodEnd   дата окончания действия комиссии 
 @param[in]  DebtSum         сумма комиссии
 @param[in]  DebtCurr        валюта комиссии
 @param[out] ErrText         текст ошибки 
 @return статус выполнения процедуры
*/
PRIVATE MACRO EnrollDebtSumByFixOrInvestCom(N:integer, CarryDate:date, CommissName:string, DebtSourceID:integer, SfContrID:integer, DatePeriodBegin:date, DatePeriodEnd:date, DebtSum:money, DebtCurr:integer, ErrText:@string):integer
  var FD = NULL;
  record DebtAcc(account);
  record ComAcc(account);
  var Ground:string = "";

  ErrText = "";

  FD = DL_SubContrFD(SfContrID);

  if(not OpenDebtAccount(FD, CarryDate, DebtCurr, DebtAcc))
    ErrText = "Ошибка при открытии счета по категории \"Треб. с н.с. брок\" в валюте FIID = " + GetFinInstrCcy(DebtCurr);
    return 1;            
  end;
  
  if(not FD.OpenAccount("+РасчетыКомисс1", NULL, true, ComAcc, CarryDate, DebtCurr))
    ErrText = "Ошибка: не открыт счет категории \"+РасчетыКомисс1\" в валюте FIID = " + GetFinInstrCcy(DebtCurr);
    return 1;
  end;

  RslDefCon.BeginTrans();

  if(CommissName == COMMISS_INVEST_ADVISER_N)
    Ground = "Перенос на счет просроченной задолженности комиссии за услугу по инвест консультированию ";
    if(GetDay(DatePeriodBegin) == GetDay(DatePeriodEnd))
      Ground = Ground + "за " + string(GetDay(DatePeriodEnd));
    else
      Ground = Ground + "c " + string(GetDay(DatePeriodBegin)) + " по " + string(GetDay(DatePeriodEnd));
    end;
    Ground = Ground + " " + string(GetNameMonthRP(DatePeriodEnd)) + " " + string(GetYear(DatePeriodEnd)) + 
                        " по брок.дог. " + FD.GetMainContr().rec.Number + " от " + FD.GetMainContr().rec.DateBegin + ", клиент - " + FD.GetParty().rec.ShortName + ", EKK - " + GetEKKDBO(SfContrID);
  else //БРОКЕРФИКС
    Ground = "Перенос на счет просроченной задолженности ежемесячной комиссии по договору " + FD.GetMainContr().rec.Number + " за " + string(GetNameMonth(DatePeriodEnd)) + " " + string(GetYear(DatePeriodEnd));
  end;
  
  CreateCarryByExpired(FD, DebtAcc, ComAcc,
                       CarryDate, 1, DebtCurr, DebtSum,
                       Ground, {Oper}, "2234"+string(N), false);

  SetDebtOverdue(DebtSourceID);
  
  RslDefCon.CommitTrans();

  return 0;

onError(erru) 
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  if(IsEqClass("TrslError", erru))
    ErrText = erru.Message;
    if(IsEqClass("TDbError", erru.err))
      ErrText = ErrText+":|"+erru.err.Stat+"-"+erru.err.Oper+"-"+erru.err.Table+"-"+erru.err.Message;
    end;
  elif(not ErrText)
    ErrText = "Неизвестная ошибка выполнения при переносе на просрочку комиссии "+CommissName+" за "+string(DatePeriodEnd:f);
  end;

  return 1;
END;

/**
 @brief Начальное решение по переносу на просрочку не оплаченных комиссий "БрокерФикс" и "ИнвестСоветник", по которым прошел срок плановой оплаты                                                                         
*/  
MACRO DlEnrollDebtSum()
  var cmd, DataSet;
  var dt, tm;
  var cnt:integer, i:integer = 0, err:integer = 0;
  var LogFileName:string = "", errmsg:string = "";
  FILE LogOutFile() txt write; /*файл вывода для протокола*/
  var CarryDate:date = {curdate};

  if(GetDate(CarryDate, "Дата формирования проводок по переносу на просрочку:"))

    cmd = DL_RSDCommand("SELECT sfdef.t_ID, sfdef.t_FIID_Sum, sfdef.t_Sum, sfdef.t_DatePeriodBegin, sfdef.t_DatePeriodEnd, com.t_Code, sfdef.t_SfContrID, sf.t_Number " +  
                        "  FROM dsfdef_dbt sfdef, dsfcomiss_dbt com, dsfcontr_dbt sf " +
                        " WHERE com.t_Code IN ('БрокерФикс', 'ИнвестСоветник') " +                                                                            
                        "   AND sfdef.t_FeeType = " + SF_FEE_TYPE_PERIOD +                                                                                
                        "   AND sfdef.t_CommNumber = com.t_Number " +                                                                    
                        "   AND sfdef.t_Status = 10 " +                                                                                 
                        "   AND sfdef.t_PlanPayDate < ? " + 
                        "   AND sfdef.t_IsDebtOverdue <> CHR(88) " +
                        "   AND sf.t_ID = sfdef.t_SfContrID "
                       );
    cmd.addParam(CarryDate);

    cnt = cmd.GetCount();
    if(cnt > 0)
      dt = StrSubSt(string(Date()),".","");
      tm = StrSubst(StrSubSt(string(time()),":","")," ","0");
      //Создание файла протокола  
      LogFileName = GetTxtFileName("dlEnrollDebtSum_"+dt+"_"+tm);
      if(not Open(LogOutFile, LogFileName))
        MsgBox("Ошибка при создании файла протокола "+LogFileName);
      end;
      SetOutput(LogFileName); 
  
      InitProgress(cnt, "Перенос комиссий \"БрокерФикс\" и \"ИнвестСоветник\" на просрочку", "Перенос комиссий на просрочку");
  
      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        if(EnrollDebtSumByFixOrInvestCom(i+1, CarryDate, DataSet.Code, DataSet.ID, DataSet.SfContrID, DataSet.DatePeriodBegin, DataSet.DatePeriodEnd, DataSet.Sum, DataSet.FIID_Sum, @errmsg) != 0)
          println(DataSet.Number + ": " + errmsg);
          err = err + 1;
        end;
  
        UseProgress(i = i + 1);
      end; 
      RemProgress();
  
      println("Обработано комиссий: "+i+". Из них с ошибкой: "+err);
  
      SetOutput(NULL, true);
      close(LogOutFile);
      ViewFile(LogOutFile);
    else
      MsgBox("Задолженности по комиссиям к переносу на просрочку не найдены");
    end;
  end;
END;

DlEnrollDebtSum();
Exit(1);