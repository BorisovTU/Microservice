/*
$Name:         AccNoteFor501.mac
$Module:       БО ЦБ
$Description:  Установка примечаний на счета по сделкам РЕПО для Ф501
*/

import RsbDataSet;
import sp_class;


private var account = TRecHandler("account.dbt");

private macro получитьДатуИзмененияПогашения(rqID)
  var ДатаИзменения :date = date(0,0,0);

  var cmdChange, dataSetChange, queryChange;
  queryChange =" WITH hist "
              +"      AS (SELECT rqbc.t_ChangeDate, "
              +"                 rqbc.t_PlanDate, "
              +"                 rq.T_ID, "
              +"                 rqbc.t_instance, "
              +"                 NVL ( "
              +"                   LAG (rqbc.t_ChangeDate, 1) "
              +"                     OVER (ORDER BY rqbc.t_instance DESC), "
              +"                   rq.t_ChangeDate) "
              +"                   AS chdate, "
              +"                 NVL ( "
              +"                   LAG (rqbc.t_PlanDate, 1) "
              +"                     OVER (ORDER BY rqbc.t_instance DESC), "
              +"                   rq.t_plandate) "
              +"                   AS PlDate "
              +"            FROM DDLRQBC_DBT rqbc, ddlrq_dbt rq "
              +"           WHERE rqbc.T_RQID = rq.t_ID AND rq.t_ID = ?) "
              +"   SELECT * "
              +"     FROM hist "
              +"    WHERE t_PlanDate <> PlDate "
              +" ORDER BY t_instance DESC ";

    cmdChange = DL_RSDCommand(queryChange);
    
    cmdChange.addParam(rqID);
    dataSetChange = cmdChange.execute();
    if(dataSetChange.moveNext())
      ДатаИзменения = date (dataSetChange.chdate);
    end;
    return ДатаИзменения;
    
end;

                                                        
private macro получитьДатуИзмененияСтавки(dealID, Rate)
  var ДатаИзменения :date = date(0,0,0);
  
  var cmdChange, dataSetChange, queryChange;
  queryChange =" WITH hist "
              +"        AS (SELECT sptk.t_OldChangeDate, "
              +"                   sptk.t_Oldincomerate, "
              +"                   TICK.T_DEALID, "
              +"                   tick.t_dealdate, "
              +"                   tick.t_ChangeDate, "
              +"                   t_oldinstance, "
              +"                   NVL ( "
              +"                     LAG (t_OldChangeDate, 1) OVER (ORDER BY t_oldinstance DESC), "
              +"                     tick.t_ChangeDate) "
              +"                     AS chdate, "
              +"                   NVL ( "
              +"                     LAG (t_Oldincomerate, 1) OVER (ORDER BY t_oldinstance DESC), ? ) "
              +"                     AS chRate "
              +"              FROM dsptkchng_dbt sptk, ddl_tick_dbt tick "
              +"             WHERE SPTK.T_DEALID = tick.t_dealID AND tick.t_dealID = ?) "
              +"     SELECT * "
              +"       FROM hist "
              +"      WHERE t_Oldincomerate <> chRate "
              +"   ORDER BY t_oldinstance DESC ";
    cmdChange = DL_RSDCommand(queryChange);

    cmdChange.addParam(Rate);
    cmdChange.addParam(dealID);
    dataSetChange = cmdChange.execute();
    if(dataSetChange.moveNext())
      ДатаИзменения = date (dataSetChange.chdate);
    end;
    return ДатаИзменения;

end;

private macro получитьСредневзвешеннуюПроцСтавку(Accountid, CSAID, DateBeg, DateEnd, WeightedAverage:@variant)
  var SummV = $0.0;
  var SummVP = $0.0;
  var cmd, Dataset, query;
  var dvcsash = TRecHandler("dvcsash.dbt");
  var ExistResult = true;

  query =  " SELECT csash.* FROM ddvcsash_dbt csash"
          +"  WHERE csash.t_csaid = ? "
          +"    AND csash.t_accountmainid = ?"
          +"    AND csash.t_dmain >= ?"
          +"    AND csash.t_dmain <= ?";

  cmd = DL_RSDCommand(query);

  cmd.addParam(CSAID);
  cmd.addParam(Accountid);
  cmd.addParam(DateBeg);
  cmd.addParam(DateEnd);
  Dataset = cmd.execute();

  while (Dataset.moveNext())
    dataSet.GetRecord().CopyTo(dvcsash.rec);
    SummV = SummV + abs(dvcsash.rec.SumAccR);
    SummVP = SummVP + abs(dvcsash.rec.SumAccR) * dvcsash.rec.rate;
  end;

  if (SummV == 0)
    ExistResult = false;
  else
    WeightedAverage = SummVP / SummV;
  end;

  return ExistResult;
end;

Macro ActualAccNoteFor501(DateBeg, DateEnd)

    var cmd, dataSet, query;
    var ObjectID, NoteDate, NoteRate;
    var ДатаУстановкиСтавки :date;
    var ДатаЗаключения :date;
    query = "  SELECT DISTINCT ac.*, rq.t_plandate Plandate, rq.t_plandate Factdate, rq.t_ID rqID, rq.t_state rqState, leg.t_incomerate Rate, tick.t_dealstatus, tick.t_dealdate, tick.t_dealID"
           +"  FROM dmcaccdoc_dbt doc, "
           +"       dmctempl_dbt templ, "
           +"       ddl_leg_dbt leg, "
           +"       ddl_tick_dbt tick, "
           +"       ddlrq_dbt rq, "
           +"       daccount_dbt ac "
           +" WHERE     doc.t_dockind = 176 "
           +"       AND doc.t_docid = leg.t_id "
           +"       AND leg.t_dealid = tick.t_dealid "
           +"       AND rsb_secur.DealIsRepo (tick.t_DealID) = 1 "           //для сделок РЕПО
           +"       AND (   (    tick.t_dealstatus = 10 "                    //открыта в отчетный период
           +"                AND tick.t_dealdate <= ? ) "
           +"            OR (    tick.t_dealstatus = 20 "                    //закрыта в отчетный период
           +"                AND tick.t_closedate BETWEEN ? AND ? )) "
           +"       AND NOT EXISTS "
           +"                 (SELECT 1 "
           +"                    FROM dpartyown_dbt Pto "
           +"                   WHERE     Pto.T_PARTYKIND = 58 "
           +"                         AND tick.T_PARTYID = Pto.T_PARTYID) "  //контрагент не является Центральныйм контрагентом
           +"       AND rq.t_docID = tick.t_DealID "
           +"       AND rq.t_docKind = 101 "                                 //БО ЦБ
           +"       AND rq.t_dealpart = 2 "                                  //по второй части
           +"       AND rq.t_type =2 "                                       //оплата
           +"       AND t_catnum IN (602, 603) "                             //"+ОД"/"-ОД"
           +"       AND templ.t_catid = doc.t_catid "
           +"       AND templ.t_number = doc.t_templnum "
           +"       AND templ.t_value1 IN (2, 3) "                           //банк-резидент /банк-нерезидент
           +"       AND ac.t_account = doc.t_account "
           ;

    cmd = DL_RSDCommand(query);

    cmd.addParam(DateEnd);
    cmd.addParam(DateBeg);
    cmd.addParam(DateEnd);
    dataSet = cmd.execute();
    While(dataSet.moveNext())
      dataSet.GetRecord().CopyTo(account.rec);    //Копирует значения полей в любой RSL-объект. При несовпадении набора полей будут скопированы поля с совпадающими именами.
      ObjectID = UniID(account, OBJTYPE_ACCOUNT);
      var ДатаУстановкиПогашения :date = dataSet.dealdate;
      ДатаУстановкиСтавки = dataSet.dealdate;
      ДатаЗаключения = dataSet.dealdate;
      NoteDate = date(readNoteForObject( OBJTYPE_ACCOUNT,ObjectID, 16, DateEnd));     //дата погашения
      if( (NoteDate == null) or (NoteDate == ZeroDate) )
         addNoteForObject( OBJTYPE_ACCOUNT, ObjectID, 16, date(dataSet.plandate), ДатаЗаключения );
      elif ( NoteDate != date(dataSet.plandate) )
        ДатаУстановкиПогашения = получитьДатуИзмененияПогашения( dataSet.rqID );
        if( (dataSet.dealstatus == DL_READIED) and (dataSet.rqState != DLRQ_STATE_OVERDUE) )
          if(ДатаУстановкиПогашения != date(0,0,0) )
             addNoteForObject( OBJTYPE_ACCOUNT, ObjectID, 16, date(dataSet.plandate), ДатаУстановкиПогашения );
          end;
        elif(dataSet.dealstatus == DL_CLOSED)
          if(ДатаУстановкиПогашения != date(0,0,0) )
            addNoteForObject( OBJTYPE_ACCOUNT, ObjectID, 16, date(dataSet.factdate), ДатаУстановкиПогашения );
          end;
        end;
      end;

      NoteRate = double(readNoteForObject( OBJTYPE_ACCOUNT,ObjectID, 19, DateEnd));     //ставка
      if( (NoteRate == null) or(NoteRate == $0.0) )
         addNoteForObject( OBJTYPE_ACCOUNT, ObjectID, 19, dataSet.rate, ДатаЗаключения );
      elif(NoteRate != (dataSet.rate))
         ДатаУстановкиСтавки = получитьДатуИзмененияСтавки(dataSet.dealID, dataSet.rate);
         if( ДатаУстановкиСтавки != date(0,0,0) )
           addNoteForObject( OBJTYPE_ACCOUNT, ObjectID, 19, dataSet.rate, ДатаУстановкиСтавки );
         end;
      end;
    end;

    //CSA
    query =   " SELECT ac.*, csa.t_csaid, dvcsash.t_dmain "
              +" FROM  ddvcsa_dbt csa, "
              +"       doproper_dbt oproper, "
              +"       doprkoper_dbt oprkoper, "
              +"       dpartyown_dbt pto, "
              +"       (SELECT doc.t_dockind, doc.t_docid, doc.t_catid, doc.t_templnum, doc.t_account FROM dmcaccdoc_dbt doc, dmccateg_dbt categ "
              +"         WHERE     (categ.t_Code = '-МС' OR categ.t_Code = '+МС')  "
              +"               AND categ.t_id = doc.t_catid GROUP BY doc.t_account, doc.t_templnum, doc.t_catid, doc.t_dockind, doc.t_docid) accdoc,  "
              +"       ddvcsash_dbt dvcsash,  "
              +"       daccount_dbt ac "
              +"       WHERE      oprkoper.t_dockind = 4626  "
              +"              AND OPROPER.t_kind_operation = oprkoper.t_kind_operation  "
              +"              AND OPROPER.t_dockind = oprkoper.t_dockind   "
              +"              AND TO_NUMBER (OPROPER.T_DOCUMENTID) = csa.t_csaid  "
              +"              AND (       OPROPER.t_completed = CHR (88)  "
              +"                      AND OPROPER.T_END_DATE > ?  "
              +"                   OR     OPROPER.t_completed = CHR (0)  "
              +"                      AND OPROPER.T_START_DATE <= ? ) "
              +"              AND pto.t_partyid = csa.t_partyid  "
              +"              AND pto.t_partykind = 2  "
              +"              AND accdoc.t_dockind = OPROPER.t_dockind  "
              +"              AND accdoc.t_docid = csa.t_csaid "
              +"              AND EXISTS  "
              +"                    (SELECT templ.* FROM dmctempl_dbt templ, dmccateg_dbt categ, dllclass_dbt clas "
              +"                      WHERE     templ.t_catid = accdoc.t_catid  "
              +"                            AND templ.t_number = accdoc.t_templnum  "
              +"                            AND categ.t_id = templ.t_catid  "
              +"                            AND clas.t_shortname = 'КатегКонтрОД'  "
              +"                            AND (CASE  "
              +"                                  WHEN categ.t_class1 = clas.t_classificator THEN templ.t_value1 "
              +"                                  WHEN categ.t_class2 = clas.t_classificator THEN templ.t_value2 "
              +"                                  WHEN categ.t_class3 = clas.t_classificator THEN templ.t_value3 "
              +"                                  WHEN categ.t_class4 = clas.t_classificator THEN templ.t_value4 "
              +"                                  WHEN categ.t_class5 = clas.t_classificator THEN templ.t_value5 "
              +"                                  WHEN categ.t_class6 = clas.t_classificator THEN templ.t_value6 "
              +"                                  WHEN categ.t_class7 = clas.t_classificator THEN templ.t_value7 "
              +"                                  WHEN categ.t_class8 = clas.t_classificator THEN templ.t_value8 "
              +"                                  ELSE -1 END) IN (2, 3)) "
              +"              AND ac.t_account = accdoc.t_account  "
              +"              AND DVCSASH.T_SHID =  "
              +"                        (SELECT MAX (csash.t_SHID) "
              +"                                KEEP (DENSE_RANK FIRST ORDER BY csash.t_dmain DESC) "
              +"                                AS shid "
              +"                           FROM ddvcsash_dbt csash "
              +"                          WHERE     csash.t_csaid = csa.t_csaid "
              +"                                AND csash.t_accountmainid = ac.t_accountid "
              +"                                AND csash.t_dmain <= ? )";

    cmd = DL_RSDCommand(query);

    cmd.addParam(DateBeg);
    cmd.addParam(DateEnd);
    cmd.addParam(DateEnd);
    dataSet = cmd.execute();

    while(dataSet.moveNext())
      dataSet.GetRecord().CopyTo(account.rec);    //Копирует значения полей в любой RSL-объект. При несовпадении набора полей будут скопированы поля с совпадающими именами.
      ObjectID = UniID(account, OBJTYPE_ACCOUNT);
      ДатаУстановкиСтавки = dataSet.dmain;
      var rate:double = $0.0;
      var ExistProc = получитьСредневзвешеннуюПроцСтавку(account.rec.accountid, dataSet.csaid, DateBeg, DateEnd, @rate);

      var old_flag = SetDialogFlag(0);
      if( ExistProc )
        if( RemoveNoteForObject( OBJTYPE_ACCOUNT,ObjectID, 19) )
          if( ДатаУстановкиСтавки != date(0,0,0) )
            addNoteForObject( OBJTYPE_ACCOUNT, ObjectID, 19, rate, ДатаУстановкиСтавки );
          end;
        end;
      end;
      SetDialogFlag(old_flag);
    end;
End;