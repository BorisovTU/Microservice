import "dl_calendtls.mac";
import RtRegEx;

PRIVATE FILE FileCalend() txt 1024;

//тратить время на функцию по поиску 
//ид субъекта биржи в разовой функции показалось странным, когда всего 2 биржи
private const MMVBPAIR = DL_KeyPair(2,"ММВБ");
private const SPBIDPAIR = DL_KeyPair(151337,"ПАО СПБ");

private CONST CHECK_DATE_REGEX = "(0?[1-9]|[12][0-9]|3[01])[\\.](0?[1-9]|1[012])[\\.]((19|20)\\d\\d)";


private const MARKET_PARAM_NAME = "биржа";
private const DAYTYPE_PARAM_NAME = "календарь";
private const NUMBER_PARAM_NAME = "номер";
private const OBJECT_PARAM_NAME = "объект";
private const OBJECTTYPE_PARAM_NAME = "тип объекта";
private const NAME_PARAM_NAME = "наименование";
private const SUBSYS_PARAM_NAME = "подсистема";

private const BALANCE_SYM = "б";
private const BANK_SYM = "+";

private macro GetCalendarHex(number:integer, year:integer)
  var cmd = DL_RSDCommand(
     " SELECT RAWTOHEX(T_CALENDAYS) as t_calend "
    +"   FROM DCALENDAR_DBT "
    +"  WHERE T_YEAR = ? AND T_ID = 1 "
    );
  cmd.AddParam(year);
  cmd.AddParam(number);

  var ds = cmd.Execute();
  if (ds.MoveNext())
    return SQL_ConvTypeStr(ds.calend);
  end;

  return null;
end;

private macro GetPairValueOrDefault(pairArray:TArray, idx: string, default:string)
  var findIdx = DL_FindKeyPairInArr( pairArray, idx);
  if (findIdx >= 0)
    return pairArray[findIdx].Value.Value; //экзепляр класса CalendPrmS
  end;
  return default;
end;

private macro GetCurrencyFIID(IsoCode:string)
  var cmd = DL_RSDCommand(
     " SELECT t_fiid "
    +"   FROM DFININSTR_DBT "
    +"  WHERE T_ISO_NUMBER = ? AND ROWNUM = 1 "
    );
  cmd.AddParam(IsoCode);

  var ds = cmd.Execute();
  if (ds.MoveNext())
    return SQL_ConvTypeInteger(ds.fiid);
  end;

  return -1;
end;

class CalendPrmS(_ParamKindCode: string, _Value:string, _TextValue: string)
  var ParamKindCode = _ParamKindCode;
  var Value = _Value;
  var TextValue = _TextValue;
end;

class CalendS ()
  var Name = NULL;
  var Number = -1;
  var IdentProgram = -1;

  var CalendParams = TArray();
  var CalendHexs = TArray();

  private macro MakeHexOfDay(_ParamValue:string)
    var ParamValue = StrLwr(Trim(_ParamValue));
    return 
      IIF(Index(ParamValue,BALANCE_SYM) > 0, "01", "00") +
      IIF(Index(ParamValue,BANK_SYM) > 0, "01", "00") +
      "00";
  end;

  macro SetDate(_date:date, _ParamValue:string)
    var y,m,d;
    var tempHex = null;
    DateSplit(_date,d,m,y);
    var yearIdx = DL_FindKeyPairInArr(CalendHexs,y);
    if (not (yearIdx >= 0))
      yearIdx = CalendHexs.size;
      if (Number > 0)
        tempHex = GetCalendarHex(Number, y);
      end;
      CalendHexs[yearIdx] = IIF(tempHex == null,
        DL_KeyPair(y,MkStr("0", ((date(1,1,y+1)-date(1,1,y)) * 6))), // заполняем нулями в количестве дней в году * 6 (3 байта), если такого календаря нет
        tempHex
      );
    end;
    var dayNum = _date - date(1,1,y) + 1;
    var dataPos = (dayNum - 1) * 3 * 2;
    CalendHexs[yearIdx].Value =
      SubStr(CalendHexs[yearIdx].Value, 1, dataPos) + 
      MakeHexOfDay(_ParamValue) +
      SubStr(CalendHexs[yearIdx].Value, dataPos + 1 + 6);
  end;

  macro AddParam(_ParamName:string, _ParamValue:string)
    var ParamName = Trim(StrLwr(_ParamName));
    var ParamIdxName = "";
    var ParamValue = Trim(StrLwr(_ParamValue));
    var SaveValue = NULL;
    if (ParamName == MARKET_PARAM_NAME)
      ParamIdxName = "Market";
      if (ParamValue == "ммвб")
        SaveValue = CalendPrmS(ParamIdxName, MMVBPAIR.Key, MMVBPAIR.Value);
      elif (ParamValue == "спб")
        SaveValue = CalendPrmS(ParamIdxName, SPBIDPAIR.Key, SPBIDPAIR.Value);
      end;
    elif (ParamName == DAYTYPE_PARAM_NAME)
      ParamIdxName = "DayType";
      if (ParamValue == "торговый")
        SaveValue = CalendPrmS(ParamIdxName, string(DL_CALLNK_MRKTDAY_TRADE), "Торговые");
      elif (ParamValue == "расчетный")
        SaveValue = CalendPrmS(ParamIdxName, string(DL_CALLNK_MRKTDAY_SETTL), "Расчетные");
      elif (ParamValue == "без расчетов")
        SaveValue = CalendPrmS(ParamIdxName, string(DL_CALLNK_OUTMRKTDAY_NOSETTL), "Без расчетов");
      end;
    elif (ParamName == OBJECTTYPE_PARAM_NAME)
      ParamIdxName = "ObjectType";
      if (ParamValue == "биржевой")
        SaveValue = CalendPrmS(ParamIdxName, string(DL_CALLNK_MARKET), "Биржевая");
      elif (ParamValue == "внебиржевой")
        SaveValue = CalendPrmS(ParamIdxName, string(DL_CALLNK_OUTMARKET), "Внебиржевая");
      elif (ParamValue == "сервисный")
        SaveValue = CalendPrmS(ParamIdxName, string(DL_CALLNK_SERVOP), "Сервисная");
      end;
    elif (ParamName == OBJECT_PARAM_NAME)
      ParamIdxName = "Object";
      SaveValue = CalendPrmS(ParamIdxName, Trim(_ParamValue), Trim(_ParamValue));
    elif (ParamName == NAME_PARAM_NAME)
      Name = Trim(_ParamValue);
    elif (ParamName == NUMBER_PARAM_NAME)
      Number = Int(Trim(_ParamValue));
    elif (ParamName == SUBSYS_PARAM_NAME)
      if ((_ParamValue == "фиссико") or (_ParamValue == "158"))
        IdentProgram = 158;
      elif ((_ParamValue == "боцб") or (_ParamValue == "83"))
        IdentProgram = 83;
      end;
    end;
    if ((ParamIdxName != "") and (SaveValue != null))
      var paramIdx = DL_FindKeyPairInArr( CalendParams, ParamIdxName);
      paramIdx = IIF(paramIdx < 0, CalendParams.size, paramIdx);
      CalendParams[paramIdx] = DL_KeyPair(ParamIdxName, SaveValue);
    end;
  end;
end;

macro ExecuteParse()
  var FilePath = "";
  if (not SelectFile ( FilePath, "*.csv", "Выберите файл настроек календарей", 0, false ))
    return;
  end;

  if( open( FileCalend, FilePath ) != true )
    MsgBox("Ошибка открытия файла календарей по пути \"" + FilePath + "\"");
    return;
  end;
  Rewind(FileCalend);
  SetDelim(";");

  var calendArr = TArray();

  var dateRegex = TRegEx( CHECK_DATE_REGEX );

  var i = 0;
  var j = 0;

  while(Next(FileCalend) and (ValType(FileCalend(0)) == V_STRING) and (Trim(FileCalend(0)) != ""))
    var prmName = Trim(FileCalend(0));
    var isHeader = not dateRegex.match(prmName); //проверим первое поле, есть ли там дата, если нет, то мы в заголовке
    i = 0;
    while (((i=i+1) > 0) and (ValType(FileCalend(i)) == V_STRING) and (Trim(FileCalend(i)) != ""))
      var paramVal = Trim(FileCalend(i));
      if (isHeader)
        if (ValType(calendArr[i-1]) == V_UNDEF)
          calendArr[i-1] = CalendS;
        end;
        calendArr[i-1].AddParam(prmName, paramVal);
      else //если это дата, то наполняем
        if (ValType(calendArr[i-1]) != V_UNDEF)
          calendArr[i-1].SetDate(date(prmName),paramVal);
        end;
      end;
    end;
  end;

  var calCount = 0;

  i = -1;
  while ((i=i+1) < calendArr.size)
    if (ValType(calendArr[i]) != V_UNDEF)
      if (calendArr[i].Number <= 0)
        println("Календарь из колонки с номером " + (i+2) + " не имеет идентификатора календаря, поэтому был пропущен.");
        continue;
      end;
      if (calendArr[i].Name == NULL)
        println("Календарь из колонки с номером " + (i+2) + " не имеет наименование календаря, поэтому был пропущен.");
        continue;
      end;
      if (calendArr[i].IdentProgram <= 0)
        println("Календарь из колонки с номером " + (i+2) + " не имеет подсистемы, поэтому был пропущен.");
        continue;
      end;
      if (calendArr[i].CalendHexs.size <= 0)
        println("Календарь из колонки с номером " + (i+2) + " не имеет заполненной сетки календарей, поэтому был пропущен.");
        continue;
      end;

      SQL_Execute("DELETE FROM DCALKIND_DBT WHERE T_ID = " + calendArr[i].Number);
      SQL_Execute(
        " INSERT INTO DCALKIND_DBT (T_ID, "
       +"                           T_NAME, "
       +"                           T_BALANCE, "
       +"                           T_RETAILINSATURDAY, "
       +"                           T_RETAILINSUNDAY, "
       +"                           T_RETAILINHOLYDAY, "
       +"                           T_PARENTID) "
       +"      VALUES ("+calendArr[i].Number+", "
       +"              '"+calendArr[i].Name+"', "
       +"              1, "
       +"              CHR (0), "
       +"              CHR (0), "
       +"              CHR (0), "
       +"              0)"
      );

      calCount = calCount + 1;
      j=-1;
      while ((j=j+1) < calendArr[i].CalendHexs.size)
        var calendHex = calendArr[i].CalendHexs[j];
        var daysInHear = date(1,1,calendHex.key+1)-date(1,1,calendHex.key);

        SQL_Execute("DELETE FROM DCALENDAR_DBT WHERE T_ID = " + calendArr[i].Number + " AND T_YEAR = " + calendHex.key);
        SQL_Execute(
          " INSERT INTO DCALENDAR_DBT (T_ID, "
         +"                            T_YEAR, "
         +"                            T_DAYSINYEAR, "
         +"                            T_CALENDAYS) "
         +"      VALUES ("+calendArr[i].Number+", "
         +"              "+calendHex.key+", "
         +"              "+daysInHear+", "
         +"              HEXTORAW('"+calendHex.Value+"')) "
        );
      end;

      SQL_Execute("DELETE FROM DDLCALENDLNK_DBT WHERE T_CALKINDID = "+calendArr[i].Number);
      var cmd = RsdCommand("INSERT INTO DDLCALENDLNK_DBT (T_IDENTPROGRAM, T_CALKINDID, T_OBJTYPE, T_OBJNAME, T_MARKETID, T_MARKETDAYTYPE) "
                          +" VALUES ("+calendArr[i].IdentProgram+","+calendArr[i].Number+
                          +","+GetPairValueOrDefault(calendArr[i].CalendParams,"ObjectType","0")
                          +","+GetPairValueOrDefault(calendArr[i].CalendParams,"Object","CHR(0)")
                          +","+GetPairValueOrDefault(calendArr[i].CalendParams,"Market","0")
                          +","+GetPairValueOrDefault(calendArr[i].CalendParams,"DayType","0")
                          + ")");
      cmd.execute();
    end;
  end;

  MsgBox("Завершено. Загружено календарей: "+calCount);

end;

ExecuteParse();