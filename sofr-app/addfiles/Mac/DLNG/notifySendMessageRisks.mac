/**                                                                                                             
 @file notifySendMessageRisks.mac                                                                                          
 @brief "Ежегодное уведомление клиентов о рисках использования Банком в своих интересах ценных бумаг Клиента" 
                                                                                                                                                                                                                                                                                                                            
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.01.22 |Шестаков Д.В.  |BOSS-3774           |Создание макроса  для отправки ежегодного уведомления о рисках использования Банком в своих интересах ценных бумаг клиента.                                                                                                                                                                                                                                                                                       
*/ 

import "u_common_email_utils.mac";
import oralib, likepy, cb_sql, globals, secinter, dlcontrfunc;

 
//?? переотправка ошибочных договор, каждый день будет повторятся, и писаться в сообщения договора


CONST MAIL_TMPL_NAME = "SendNotificationEmail.dot";  //Файл-шаблон с текстом уведомления
//Тема и тело письма в случае сбоя при отправке
CONST STOP_SENDING_SUBJECT = "Внимание: Отправка уведомлений клиентам о рисках использования Банком бумаг клиента в своих интересах  была приостановлена из-за технической ошибки";
CONST STOP_SENDING_TEXT = "Отправка уведомлений клиентам о рисках использования Банком бумаг клиента в своих интересах  была приостановлена из-за технической ошибки";
//Тема и тело письма при восстановлении отправки
CONST RESTRORED_SENDING_SUBJECT = "Внимание: Работоспособность сервиса восстановлена. Отправка Уведомлений клиентам, по которым ранее была отправка приостановлена по технической ошибке, продолжена.";
CONST RESTRORED_SENDING_TEXT = "Работоспособность сервиса восстановлена. Отправка Уведомлений клиентам, по которым ранее была отправка приостановлена по технической ошибке, продолжена.";
//Адрес почты на который отправялется сообщение о сбое/восстановлении отправки
CONST MAIL_WARNING = "frontinvest@rshb.ru";
CONST PARAM_DIR_TEMPLS = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR";
CONST CURRENT_EMAIL_SEND = "COMMON\\EMAIL\\MAIL_USERNAME";
//Рубильник для автоматической рассылки, по умолчанию ВЫКЛ в 114 релизе
CONST PARAM_BOSS_3774 = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\УВЕДОМЛЕНИЯ КЛИЕНТУ\\BOSS-3774";
//Текст ошибки, если у клиента нет актуальной почты
CONST TEXT_NO_EMAIL    = "У клиента отсутствует электронная почта для рассылки";
//Тема почтового сообщения для рассылки уведомлений
CONST SEND_SUBJECT_EMAIL = "Информационное уведомление о рисках использования Банком в своих интересах ценных бумаг Клиента";

CONST MAIN_TEXT_EMAIL = string("<!DOCTYPE html>",
                      "<html>",
                      "<head>",
                      "<meta charset=""UTF-8"">",
                      "<meta http-equiv=""Content-Type"" content=""text/html; charset=UTF-8"" />",
                      "<title></title>",
                      "<style>",
                      "  p {",
                      "    text-indent: 20px;",
                      "  }",
                      "  </style>",
                      "</head>",
                      "<body>",
                      "Уважаемый клиент, <br><br>",
                      "\n",
                      "\n<p><b>настоящим письмом информируем Вас </b> о важности ознакомления с документами, описывающими основные риски при инвестициях на финансовом рынке, до совершения сделок с финансовыми инструментами.</p>",
                      "\n",
                      "\n<p>АО \x22Россельхозбанк\x22 ежегодно уведомляет своих клиентов, о том, что все основные риски в рамках брокерского обслуживания описаны в Приложениях 12.1 -12.4 к <a href='https://rshb.ru/api/v1/storage/4a12bca0-9265-4796-b105-e28ad1697025/attachment'>Регламенту оказания брокерских услуг АО \x22Россельхозбанк\x22 № 15-Р</a>, с которыми можно ознакомиться на официальном сайте Банка по адресу www.rshb.ru в разделе <a href='https://www.rshb.ru/natural/brokerage/open-account'>\x22Брокерское обслуживание\x22</a> / <a href='https://www.rshb.ru/natural/info/tarif'>Тарифы и документы</a> / <a href='https://www.rshb.ru/natural/info/tarif?fiB9QBM4I9Lf0KIlGfrtn=__Д__о__к__у__м__е__н__т__ы'>Документы.</a> </p>",
                      "\n",
                      "\n<p>Обращаем Ваше внимание на <a href='https://www.rshb.ru/api/v1/storage/104ca0b9-1c10-4c51-ab89-9442a6a4ef61/attachment'>Приложение 12.5</a> к Регламенту <b>\x22Уведомление (декларация) о рисках использования Банком в своих интересах ценных бумаг Клиента\x22</b>, а именно: </p>",
                      "\n",
                      "\n<p>1. Риск возникновения у клиента убытков вследствие неисполнения, несвоевременного либо неполного исполнения (включая неплатежеспособность или несостоятельность контрагента) другой стороной по сделке, совершаемой за счет самого Банка или других клиентов Банка, своих обязательств. Указанный риск может выражаться, в том числе в задержке возврата Банком клиенту ценных бумаг или несвоевременном исполнении Банком распоряжений клиента или неисполнении/ненадлежащем исполнении обязательств клиента.</p> ",
                      "\n",
                      "\n<p>2. Риск не включения клиента в список лиц, осуществляющих права по ценным бумагам (имеющих право на участие в общем собрании владельцев ценных бумаг, имеющих преимущественное право приобретения ценных бумаг и др.) в период использования ценных бумаг клиента в интересах Банка. </p>",
                      "\n",
                      "\n<p>АО \x22Россельхозбанк\x22 уведомляет Вас о том, что Вы вправе подать Банку заявление об отказе от предоставления Банку права использования в своих интересах ценных бумаг клиента путем внесения изменения в пункт 5 Заявления (Приложения 2.1, 2.3, 2.4 к Регламенту), в пункт 4 Заявления (Приложение 2.2. к Регламенту) и предоставления его подписанного клиентом оригинала (на бумажном носителе) в дополнительный офис Банка.</p>",
                      "\n",
                      "\n<p>В случае подачи в Банк заявления об отказе от предоставления Банку права использования в своих интересах ценных бумаг клиента, Вам будет отказано в предоставлении доступа к совершению сделок, указанных в п. 2.4.9 Регламента.</p>",
                      "\n",
                      "\n<p>Мы рекомендуем Вам внимательно рассмотреть вопрос о том, являются ли риски, возникающие при использовании ценных бумаг клиента для исполнения или обеспечения исполнения обязательств по сделкам, совершаемым за счет самого Банка или других клиентов Банка, приемлемыми для Вас.</p>",
                      "\n",
                      "\n<p>Данное уведомление носит информационный характер и не имеет своей целью заставить Вас заявить запрет использования Банком ценных бумаг клиента в интересах Банка, а призвана помочь Вам оценить указанные риски и ответственно подойти к решению указанного вопроса.</p> ",
                      "\n",
                      "\n<p>Банк гарантирует клиенту исполнение его поручений за счет указанных денежных средств и (или) ценных бумаг либо их возврат по требованию клиента в сроки, предусмотренные законодательными и иными нормативными актами, регулирующими брокерскую деятельность, и Соглашением.</p>",
                      "\n",
                      "\nПодробнее изучить <a href='https://www.rshb.ru/api/v1/storage/104ca0b9-1c10-4c51-ab89-9442a6a4ef61/attachment'>Приложение 12.5</a> <br><br>",
                      "\n",
                      "\nДанное письмо отправлено автоматически и не требует Вашего ответа. <br><br>",
                      "\n",
                      "\nС уважением,<br>",
                      "\nАО \x22Россельхозбанк\x22",
                      "</body>",
                      "</html>");

/*
@brief Получить значение из параметра по пути
@param[in] RegistryPath Путь до параметра
@param[out] errorStatus  Возвращает текст ошибки
@param[out] errorText Возвращает статус ошибки. True - есть ошибка, False - ошибки нет
@return Значение параметра
*/
private macro GetStringRegValue(RegistryPath:String, errorStatus:@Bool, errorText:@String)
   var str = "";
   var stat = 0;

   GetRegistryValue(RegistryPath, V_STRING, str, stat);
   if(stat)
      errorStatus = True;
      errorText = "Не найдена настройка в реестре банка: " + RegistryPath;
   elif(str == "")
      errorStatus = True;
      errorText =  "Не задано значение настройки: " + RegistryPath;
   end;

   return str;
end;


/*
@brief Инсерт данных в таблицу DDLCONTRMSG_DBT
@param[in] Dlcontrid  ID договора
@param[in] recipientEmail  Получатель письма
@param[in] senderEmail  Отправитель письма
@param[in] textEmail  Текст, который содержится в письме
@param[in] sendMesState  Статус отправки, успешно обработалось почтовым шлюзом?
@param[out] msgContrId  Id созданного сообщения ДБО в тестовом формате, дополненное нулями слева до 34 символов
@param[out] errorStatus  Возвращает статус ошибки. True - есть ошибка, False - ошибки нет
@param[out] errorText Возвращает текст ошибки
*/
private macro insertDlContrMsg(Dlcontrid, recipientEmail, senderEmail, textEmail, status, msgContrId:@String, errorStatus:@Bool, errorText:@String )

  var sql            :String,
      cmd            :Object;

  sql = "declare l_errMsg varchar2(512);  l_errRes number; l_msgId varchar2(64); begin" 
                + "  rsb_dbo_notifications_risk.insertDlContrMsg(p_Dlcontrid      => :Dlcontrid  "
                + "                                             ,p_recipientEmail => :recipientEmail  "
                + "                                             ,p_senderEmail    => :senderEmail "
                + "                                             ,p_textEmail      => :textEmail "
                + "                                             ,p_sendMesState   => :sendMesState "
                + "                                             ,p_out_msgId      => l_msgId "
                + "                                             ,p_out_errMsg     => l_errMsg "
                + "                                             ,p_out_errRes     => l_errRes ) ;"
                + "   ?:= l_msgId; "
                + "   ?:= substr(l_errMsg,0,73); "  
                + "   ?:= l_errRes; "   
                + " end;";  

  cmd = RSDCommand(sql);
  cmd.AddParam("Dlcontrid",      RSDBP_IN, Dlcontrid);
  cmd.AddParam("recipientEmail", RSDBP_IN, recipientEmail);
  cmd.AddParam("senderEmail",    RSDBP_IN, senderEmail);
  cmd.AddParam("textEmail",      RSDBP_IN, textEmail);
  cmd.AddParam("sendMesState",   RSDBP_IN, status);
  cmd.addParam("l_msgId",        RSDBP_OUT, V_STRING);
  cmd.addParam("l_errMsg",       RSDBP_OUT, V_STRING);
  cmd.addParam("l_errRes",       RSDBP_OUT, V_INTEGER);
  cmd.Execute(); 

  msgContrId  = cmd.value("l_msgId");
  errorText   = cmd.value("l_errMsg");
  errorStatus = IIF(cmd.value("l_errRes"), true, false);
  
end;

/*
@brief Инсерт данных в таблицу DNOTETEXT_DBT
@param[in] dlcontrID  ID договора
@param[in] recipientEmail  Получатель письма
@param[in] senderEmail  Отправитель письма
@param[out] errorStatus  Возвращает статус ошибки. True - есть ошибка, False - ошибки нет
@param[out] errorText Возвращает текст ошибки
*/
private macro insertNoteText(dlcontrID, recipientEmail, senderEmail, errorStatus:@Bool, errorText:@String)

  var sql            :String,
      cmd            :Object;

  sql = "declare l_errMsg varchar2(512);  l_errRes number; begin" 
                + "  rsb_dbo_notifications_risk.insertNoteText(p_dlcontrID      => :dlcontrID  "
                + "                                           ,p_recipientEmail => :recipientEmail "
                + "                                           ,p_senderEmail    => :senderEmail "
                + "                                           ,p_out_errMsg     => l_errMsg "
                + "                                           ,p_out_errRes     => l_errRes ) ;"
                + "   ?:= substr(l_errMsg,0,73); "  
                + "   ?:= l_errRes; "   
                + " end;";  

  cmd = RSDCommand(sql);
  cmd.AddParam("dlcontrID", RSDBP_IN, dlcontrID);
  cmd.AddParam("recipientEmail", RSDBP_IN, recipientEmail);
  cmd.AddParam("senderEmail", RSDBP_IN, senderEmail);
  cmd.addParam("l_errMsg", RSDBP_OUT, V_STRING);
  cmd.addParam("l_errRes",  RSDBP_OUT, V_INTEGER);
  cmd.Execute(); 

  errorText   = cmd.value("l_errMsg");
  errorStatus =  IIF(cmd.value("l_errRes"), true, false);
end;


/*
@brief Update данных в таблице DNOTETEXT_DBT, закрытие пред. примечания, перед открытием нового
@param[in] noteTextID  ID примечания
@param[in] Dlcontrid  ID договора
@param[in] recipientEmail  Получатель письма
@param[in] senderEmail  Отправитель письма
@param[out] errorStatus  Возвращает статус ошибки. True - есть ошибка, False - ошибки нет
@param[out] errorText Возвращает текст ошибки
*/  
private macro updateNoteText(noteTextID, dlcontrID, recipientEmail, senderEmail, errorStatus:@Bool, errorText:@String)

  var sql            :String,
      cmd            :Object;

  sql = "declare l_errMsg varchar2(512);  l_errRes number; begin" 
                + "  rsb_dbo_notifications_risk.updateNoteText(p_noteTextID     => :noteTextID "
                + "                                           ,p_dlcontrID      => :dlcontrID  "
                + "                                           ,p_recipientEmail => :recipientEmail "
                + "                                           ,p_senderEmail    => :senderEmail  "
                + "                                           ,p_out_errMsg     => l_errMsg "
                + "                                           ,p_out_errRes     => l_errRes ) ;"
                + "   ?:= substr(l_errMsg,0,73); "  
                + "   ?:= l_errRes; "   
                + " end;";  

  cmd = RSDCommand(sql);
  cmd.AddParam("noteTextID",     RSDBP_IN, noteTextID);
  cmd.AddParam("dlcontrID",      RSDBP_IN, dlcontrID);
  cmd.AddParam("recipientEmail", RSDBP_IN, recipientEmail);
  cmd.AddParam("senderEmail",    RSDBP_IN, senderEmail);
  cmd.addParam("l_errMsg",       RSDBP_OUT, V_STRING);
  cmd.addParam("l_errRes",       RSDBP_OUT, V_INTEGER);
  cmd.Execute(); 

  errorText   = cmd.value("l_errMsg");
  errorStatus = IIF(cmd.value("l_errRes"), true, false);
end;


/*
@brief Процедура логгирования ошибок в ходе отправки уведомлений 
@param[in] dlcontrid  ID договора
@param[in] head  Тема письма
@param[in] recipientemail  Получатель письма
@param[in] senderemail  Отправитель письма
@param[in] errorText  Текст ошибки
*/
private macro insertNotifyLog(Dlcontrid, head, recipientemail, senderemail, errorText)

  var sql            :String,
      cmd            :Object;

   sql = " begin "
                + "  rsb_dbo_notifications_risk.insertNotifyLog(p_head           => :head  "
                + "                                            ,p_recipientemail => :recipientemail   "
                + "                                            ,p_senderemail    => :senderemail "
                + "                                            ,p_errorText      => :errorText "
                + "                                            ,p_dlcontrid      => :dlcontrid ) ;"
                + " end;"; 

  cmd = RSDCommand(sql);
  cmd.AddParam("head",           RSDBP_IN, head);
  cmd.AddParam("recipientemail", RSDBP_IN, recipientemail);
  cmd.AddParam("senderemail",    RSDBP_IN, senderemail);
  cmd.AddParam("errorText",      RSDBP_IN, errorText);
  cmd.AddParam("dlcontrid",      RSDBP_IN, dlcontrid);
  cmd.Execute(); 
end;


/*
@brief Отправка уведомления через почтовый шлюз
@param[in] subjectEmail Тема письма
@param[in] textEmail Тело письма
@param[in] recipientEmail Email адрес получателя
@param[in] senderEmail Email адрес отправителя
@param[out] errorText Возвращает текст ошибки
@param[out] errorStatus Возвращает статус ошибки. True - есть ошибка, False - ошибки нет
*/
private macro submitEmail(subjectEmail, textEmail, recipientEmail, senderEmail, errorStatus:@Bool, errorText:@String)

  var emailProcessor = c_email_proc_env();
  emailProcessor.m_set_msg_head(subjectEmail);
  emailProcessor.m_add_row_to_msg_text(textEmail);
  emailProcessor.m_add_email_to_list(recipientEmail);

  //сохранение в DEMAIL_NOTIFY_DBT
  emailProcessor.m_save_to_submit_synch(true);
  emailProcessor.m_submit_email_synch();

  ErrorStatus = emailProcessor.m_get_error_status();

  if(ErrorStatus)
    ErrorText =  "Сообщение сформировано, но не отправлено: " + emailProcessor.get_service_msg();
  end;
  
end;

/*
@brief Отправка уведомления через почтовый шлюз для frontInvest в случае сбоев, или возобновления работы
@param[in] p_isFailure Флаг повторного сбоя
@param[out] errorStatus  Возвращает статус ошибки. True - есть ошибка, False - ошибки нет
@param[out] errorText Возвращает текст ошибки
*/
private macro submitEmailFrontInvest(p_isFailure, ErrorStatus:@Bool, ErrorText:@String)

  if(p_isFailure)
    submitEmail(STOP_SENDING_SUBJECT,
                STOP_SENDING_TEXT, 
                MAIL_WARNING, 
                NULL, 
                @ErrorStatus, 
                @ErrorText);
  else
    submitEmail(RESTRORED_SENDING_SUBJECT,
                RESTRORED_SENDING_TEXT, 
                MAIL_WARNING, 
                NULL, 
                @ErrorStatus, 
                @ErrorText);
  end;

end;

/*
@brief Парсинг файла-шаблона с текстом для уведолмения
@param[out] FileTemplsPath Путь до файла-шаблона с текстом уведомления
@param[out] errorStatus  Возвращает статус ошибки. True - есть ошибка, False - ошибки нет
@param[out] errorText Возвращает текст ошибки
@return Возвращает текст в html-формате
*/
private macro ParsingFileTempls(FileTemplsPath:@String, ErrorStatus:@Bool, ErrorText:@String)

    var docFileTempls = "..\\Templs\\Dlng";
    var tmpFullPath = "..\\txtFile";
    var tmpFileName = "tempFile_" + string(UserNumber) + ".htm";
    var DirName_Template = "";
    var TempStr;
    var htm="";
    ErrorStatus = False;

    /**
    @brief содержимое временного "*.htm"-файла считывается в переменную
    @return Статус парсинга из временного файла. true - нет ошибки, false - есть ошибка
    */
    private macro read_tmp()
      File f() txt; 
      Open(f, tmpFullPath, "rsansi");
      while (Next(f,1024))
        htm = htm + f.str;
      end;
      Close(f);
      return true;
    OnError (_err)
      return false;
    end; 

    DirName_Template = GetStringRegValue(PARAM_DIR_TEMPLS, @ErrorStatus, @ErrorText);
    
    if(ErrorStatus)
      return htm;
    end;

    TempStr = FindPath(toANSI(MAIL_TMPL_NAME), DirName_Template);

    if( not TempStr )
      ErrorText = "Сообщение не сформировано: Не найден шаблон уведомления";
      ErrorStatus = True;
      return htm;
    end;

    docFileTempls= DL_GetFullPath(docFileTempls);

    docFileTempls = docFileTempls + MAIL_TMPL_NAME;

    FileTemplsPath = docFileTempls;

    return MAIN_TEXT_EMAIL;

end;


/**
@brief  Отправка уведомления о рисках использования Банком в своих интересах ценных бумаг клиента.
@param[in] dlcontrid  ID договора
@param[out] errorText Возвращает статус ошибки. True - есть ошибка, False - ошибки нет
*/ 
MACRO SendMessage(DlContrId, ErrorText:@String)
    var sql            :String, 
        cmd            :Object, 
        DataSet        :Object,
        textEmail      :String,
        senderEmail    :String = "",
        ErrorStatus    :Bool,
        cnt            :Integer = 0,
        i              :Integer = 0,
        isSendMesg     :Bool = False,
        fileTemplsPath :String,
        msgContrId     :String,
        ReFailure      :Bool = False; //был ли прежде уже сбой
      
    senderEmail = GetStringRegValue(CURRENT_EMAIL_SEND, @ErrorStatus, @ErrorText);

    //Текст почтового сообщения
    textEmail = ParsingFileTempls(@fileTemplsPath,@ErrorStatus,@ErrorText);

    //Проваеряем корректность парс файла-шаблона
    if(ErrorStatus)
      insertNotifyLog(0, 
                      SEND_SUBJECT_EMAIL,
                      " ",
                      senderEmail, 
                      ErrorText);
      return False;
          
    end;
    
    if(ValType(DlContrId) != V_INTEGER)
    //Если не назначен договор, отбираем по всем
        DlContrId = 0;
    end;

    InitProgress(-1, "Отбор ДБО для отправки ежегодных уведомлений", "Отбор ДБО для отправки");
    
    sql = " begin "
        + "  rsb_dbo_notifications_risk.collectDboNotifications(:dlContrId);  "
        + " end; "; 

    cmd = RSDCommand(sql);
    cmd.AddParam("dlContrId", RSDBP_IN, DlContrId);
    cmd.Execute(); 

    cmd = null; sql = null;

    sql = " SELECT * FROM DSOFRSENDMSGEMAIL_TMP WHERE T_MSGSTATUS IN (1,3) ";
    cmd = DL_RSDCommand();
    cnt = cmd.GetCount(sql);
    RemProgress();

    if(cnt > 0)
      InitProgress(cnt, "Выполнение мероприятий по отправке ежегодных уведомлений", "Отправка уведомлений");
      DataSet = cmd.Execute(sql);

      while(DataSet.moveNext())

        if(DataSet.recipientEmail != "NO")

          /* msgStatus:
            1. Примечания нет, с даты открытия прошло больше 11 месяцев,можно отправлять
            2. Примечания нет, с даты открытия прошло меньше 11 месяцев, не отправлять
            3. Примечание есть, прошло 11 месяцев,можно отправлять
            4. Примечание есть, но не прошло время для отправки, не отправлять
          */
          if((DataSet.msgStatus == 1) or (DataSet.msgStatus == 3))

            submitEmail(SEND_SUBJECT_EMAIL,
                        textEmail, 
                        DataSet.recipientEmail, 
                        DataSet.senderEmail, 
                        @ErrorStatus, 
                        @ErrorText);
            

            if(ErrorStatus)
              ErrorText = ErrorText;

              insertNotifyLog(DataSet.Dlcontrid, 
                          STOP_SENDING_SUBJECT,
                          DataSet.recipientEmail,
                          senderEmail, 
                          ErrorText);

              //отправляем сообщение фронтИнвесту, если прежде не было сбоя
              if(not ReFailure)
                submitEmailFrontInvest(True, @ErrorStatus, @ErrorText);
              end;

              ReFailure = True;
            else
              //если прежде был сбой возращаем флаг в первоначальное положение
              if(ReFailure)
                ReFailure = False;
                submitEmailFrontInvest(ReFailure, @ErrorStatus, @ErrorText);
              end;            

              insertDlContrMsg( DataSet.Dlcontrid, 
                                DataSet.recipientEmail, 
                                senderEmail, 
                                textEmail,
                                IIF(ErrorStatus,0,1),
                                @msgContrId,
                                @ErrorStatus, 
                                @ErrorText);

              if(not ErrorStatus)

                if (not LoadImageObj(fileTemplsPath, 208, msgContrId, 1))

                  insertNotifyLog(DataSet.Dlcontrid, 
                                  SEND_SUBJECT_EMAIL,
                                  DataSet.recipientEmail,
                                  senderEmail, 
                                  "К записи о сообщении не удалось прикрепить файл сообщения");
                end;


                if(DataSet.msgStatus == 3 and (ValType(DataSet.noteTextID) == V_INTEGER))
                  updateNoteText(DataSet.noteTextID,
                                DataSet.Dlcontrid, 
                                DataSet.recipientEmail, 
                                senderEmail, 
                                @ErrorStatus, 
                                @ErrorText);
                end;
                if(not ErrorStatus)
                
                  insertNoteText(DataSet.Dlcontrid, 
                                DataSet.recipientEmail,
                                senderEmail,
                                @ErrorStatus, 
                                @ErrorText);
                end;
              end;

              isSendMesg = True;

            end;

          end;
        else 

          ErrorStatus = True;
          //сделать записть в логи о том, что не нашлось актуального емейла
          insertNotifyLog(DataSet.Dlcontrid, 
                          SEND_SUBJECT_EMAIL,
                          DataSet.recipientEmail,
                          senderEmail, 
                          TEXT_NO_EMAIL);
          
          //Также пишем в сообщения договора неудачную отправку
          insertDlContrMsg( DataSet.Dlcontrid, 
                            DataSet.recipientEmail, 
                            senderEmail, 
                            textEmail,
                            IIF(ErrorStatus,0,1),
                            @msgContrId,
                            @ErrorStatus, 
                            @ErrorText);
        end;
        UseProgress(i = i + 1);
      end;
      RemProgress();
    end;

    
    if((not ErrorStatus) and isSendMesg)
      return True;
    else 
      if((ErrorText == "") or (ValType(ErrorText) == V_UNDEF))
        ErrorText = "Договор не удовлетворяет требованиям выгрузки";
      end;
      return False;
    end;
    

onerror(er)
    RemProgress();
    ErrorText =  "onError: " + er.message + " (модуль " + er.module + ", строка " + er.line + ")";

    return False;
end;

/**
@brief  Автоматическая Отправка уведомления о рисках использования Банком в своих интересах ценных бумаг клиента.
  DSS_FUNC_DBT.T_NAME = "Отправка ежегодных уведомлений клиентов о рисках использования цб"
*/ 
macro AutoSendMessage()
  var ErrorStatus     :Integer,
      VALUE_BOSS_3774 :Bool = False;

  GetRegistryValue(PARAM_BOSS_3774, V_BOOL, VALUE_BOSS_3774, ErrorStatus);

  if(not VALUE_BOSS_3774 or ErrorStatus)
    return;
  else 
    SendMessage(0);
  end;


end;