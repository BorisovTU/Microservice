/*
$Name:             dlaccreg_form.mac
$Module:           Ядро Securities
$Description:      Отчет "Регистр внутреннего учета денежных средсв и расчетов по сделкам и операциям с ценными бумагами" - форма ввода данных
*/
import "DlRepForm_h.mac", "dlmisc.mac";

// Номера полей в форме отчета
CONST
      PNFLD_INACC_REG_BEGINDATE           = 0,   //A1
      PNFLD_INACC_REG_FINISHDATE          = 1,   //A2 
      PNFLD_INACC_REG_DIVIDE_BY_DATES     = 2,   //L1   
      PNFLD_INACC_REG_IS_CLIENT_ACCOUNTS  = 3,   //L2
      PNFLD_INACC_REG_SUBJECT             = 4,   //A3
      PNFLD_INACC_REG_ISFORM              = 5,   //L3
      PNFLD_INACC_REG_FORMSUB             = 6,   //A4
      PNFLD_INACC_REG_ISRESIDENT          = 7,   //L4
      PNFLD_INACC_REG_RESIDENTSUB         = 8,   //A5
      PNFLD_INACC_REG_ISRISKLEVEL         = 9,   //L5
      PNFLD_INACC_REG_RISKLEVELSUB        = 10,  //A6
      PNFLD_INACC_REG_ISLOADFROMFILE      = 11,  //L6
      PNFLD_INACC_REG_FILEPATH            = 12,  //A7
      PNFLD_INACC_REG_FILECONTENTTYPE     = 13,  //A8
      PNFLD_INACC_REG_IS_BANK_ACCOUNTS    = 14,  //L7
      PNFLD_INACC_REG_WITH_APOSTROFS      = 15,  //L8
      PNFLD_INACC_REG_TO_EXCEL            = 16;  //L9

CLASS DlAccountsRegFormData()
  VAR 
  BeginDate,
  EndDate,
  DivideByDates,
  IsClientsAccounts,
  IsForm,       
  FormSub,
  IsResident,
  ResidentSub,
  IsRiskLevel,
  RiskLevelSub,
  IsLoadFromFile,
  FilePath,
  FileContentType,
  IsBankAccounts,
  IsUseOpostrofs,
  IsExportToExcel;
    
// Значение по умолчанию
  BeginDate         = {curdate};
  EndDate           = {curdate};
  DivideByDates     = "";
  IsClientsAccounts = "X";
  IsForm            = 0;
  FormSub           = 0;
  IsResident        = 0;
  ResidentSub       = 0;
  IsRiskLevel       = 0;
  RiskLevelSub      = 0;
  IsLoadFromFile    = "";
  FilePath          = "";
  FileContentType   = 0;
  IsBankAccounts    = "";
  IsUseOpostrofs    = "X";
  IsExportToExcel   = "X";

END;

VAR DlAccountsFormData = DlAccountsRegFormData();

PRIVATE CONST
  H_PNFLD_BEGINDATE = 100,
  H_PNFLD_ENDDATE   = 101,
  H_CLIENT_NAME     = 102,
  H_ISFORM          = 103,
  H_FORMSUB         = 104,
  H_ISRESIDENT      = 105,
  H_RESIDENTSUB     = 106, 
  H_ISRISKLEVEL     = 107,
  H_RISKLEVELSUB    = 108,
  H_ISLOADFROMFILE  = 109,  
  H_FILEPATH        = 110,  
  H_FILECONTENTTYPE = 111;

PRIVATE CONST SelCodeKind = 1;

PRIVATE CONST PARTY_ATTR_GROUP_RISKLEVEL = 95;

CONST RESIDENT_TYPE_RESIDENT    = 1,
      RESIDENT_TYPE_NOTRESIDENT = 2;

CONST FILE_CONTENT_TYPE_SOFR = 1,
      FILE_CONTENT_TYPE_CFT  = 2;

CLASS ClientChooseScroll()
  var Columns = TArray();
  var rs = null;
  var NumColumns = 0;

  /* атрибуты полей */
  macro AddColumn( ind, fld, head, width )
    Columns.value( ind * 6 + 0 ) = fld;  // fieldName
    Columns.value( ind * 6 + 1 ) = head; // header 
    Columns.value( ind * 6 + 2 ) = width; // width
    Columns.value( ind * 6 + 3 ) = 2;    // fldType (2 = FBT)
    Columns.value( ind * 6 + 4 ) = null; // decPoint
    Columns.value( ind * 6 + 5 ) = 0;    // reserv
    NumColumns = NumColumns + 1;
  end;

  macro BuildSQL()
    return 
      "SELECT T_CLIENTID, T_CLIENTCODE, T_CLIENTNAME "
     +"  FROM DDLACCREGCLNT_TMP  ";
  end;

  macro BuildRS()
    rs = execSQLselect(BuildSQL(), TArray(), true, RSDVAL_CLIENT, RSDVAL_STATIC);
    return rs;
  end;

  macro EvProc(recset, cmd, id, key)
    /*Обработчик*/
    if(cmd == DLG_KEY)
      /*Esc*/
      if(key == 27)
        return CM_CANCEL;
      elif (key == 322)
        rs.delete();
        if ( rs.reccount )
          goToScroll (recset);
          updateScroll (rs, 5);
        else
          return CM_SELECT;
        end;
      elif (key == 323)
        var partcode  = TBFile("partcode.dbt", "R", 0);
        var PartyF    = TBFile("party.dbt", "R", 0);
        var Party     = TRecHandler("party.dbt");
        var PartyIDArray = TArray();
        var PartyNameArray = TArray();
        var whereCond = " t.T_PARTYID NOT IN (SELECT T_CLIENTID FROM DDLACCREGCLNT_TMP) ";

        if(ListPText( Party, Party.rec.PartyID, whereCond, NULL, NULL, PartyIDArray))
          if((PartyIDArray.Size() == 0) and (Party.rec.PartyID > 0))
            PartyIDArray[0] = Party.rec.PartyID;
          end;
          
          var i = 0;
          while(i < PartyIDArray.Size())
            PartyF.Clear();
            PartyF.rec.PartyID  = PartyIDArray[i];
            
            partcode.Clear();
            partcode.rec.PartyID  = PartyIDArray[i];
            partcode.rec.CodeKind = SelCodeKind;

            if(partcode.GetEQ() and PartyF.GetEQ())
              rs.addNew();
              rs.value("T_CLIENTID") = PartyF.rec.PartyID;
              rs.value("T_CLIENTCODE") = partcode.rec.Code;
              rs.value("T_CLIENTNAME") = PartyF.rec.ShortName;
              rs.update();
              goToScroll(rs);
              updateScroll(rs, 5);
            end;
            i = i + 1;
          end;
        end;
      end;
    end;
  end;

  macro ShowScroll()
    return RunScroll(BuildRS(), NumColumns, Columns, "DLACCREGCLNTC", R2M(this,"EvProc"), "Клиенты к формированию отчета", 
                 "~Esc~ Выход ~F8~ Удалить ~F9~ Добавить", true, -1, -1);
  end;

  execSql("delete from DDLACCREGCLNT_TMP");

  AddColumn(0, "t_clientid",   "ID клиента",   7, 10);
  AddColumn(1, "t_clientcode", "Код клиента", 20, 64);
  AddColumn(2, "t_clientname", "Имя клиента", 30, 20);
END;

PRIVATE MACRO GetClientNameField()
  var query =
    " SELECT CASE "
   +"           WHEN count1.t_cnt > 1 THEN 'Несколько: ' || count1.t_cnt "
   +"           ELSE NVL ( (SELECT T_CLIENTNAME "
   +"                         FROM DDLACCREGCLNT_TMP "
   +"                        WHERE ROWNUM = 1), "
   +"                     'Все') "
   +"        END "
   +"           AS T_CLIENTNAME "
   +"   FROM (SELECT COUNT (*) AS t_cnt FROM DDLACCREGCLNT_TMP) count1 ";

  var rs = ExecSQLSelect(query);
  if(rs.MoveNext())
    return SQL_ConvTypeStr(rs.value(0));
  end;
  return "";
end;

PRIVATE MACRO FldProc_Client(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if(Cmd == DLG_INIT)
    if(FldRealValue == "")
      execSql("delete from DDLACCREGCLNT_TMP");
      FldShowValue = "";
    else
      FldShowValue = GetClientNameField();
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
    execSql("delete from DDLACCREGCLNT_TMP");
    FldShowValue = "Все";
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    pThis.m_ClientScroll.ShowScroll();
    FldShowValue = GetClientNameField();
  end;

  if(FldShowValue == "Все")
    EnableFields(pThis.Data(), PNFLD_INACC_REG_ISFORM);
    EnableFields(pThis.Data(), PNFLD_INACC_REG_ISRESIDENT);
    EnableFields(pThis.Data(), PNFLD_INACC_REG_ISRISKLEVEL);
  else
    pThis.SetLinkedValue(H_ISFORM, 0);
    DisableFields(pThis.Data(), PNFLD_INACC_REG_ISFORM);
    pThis.SetLinkedValue(H_ISRESIDENT, 0);
    DisableFields(pThis.Data(), PNFLD_INACC_REG_ISRESIDENT);
    pThis.SetLinkedValue(H_ISRISKLEVEL, 0);
    DisableFields(pThis.Data(), PNFLD_INACC_REG_ISRISKLEVEL);
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_FormSub(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    if(FldRealValue == 0)
      FldShowValue = "";
    else
      FldShowValue = DL_GetNameAlg(3400, FldRealValue);
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    DL_ListNA(3400, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), NULL);
  end;
  return CM_DEFAULT;        
END;

PRIVATE MACRO FldProc_ResidentSub(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  MACRO Name(Id:INTEGER )
    if(Id == RESIDENT_TYPE_RESIDENT)
      return "Резидент";
    elif(Id == RESIDENT_TYPE_NOTRESIDENT)
      return "Нерезидент";
    end;
    return "";
  END;

  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    FldShowValue = Name(FldRealValue);
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3)) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    DL_ListString(null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                  Name(RESIDENT_TYPE_RESIDENT),    RESIDENT_TYPE_RESIDENT,
                  Name(RESIDENT_TYPE_NOTRESIDENT), RESIDENT_TYPE_NOTRESIDENT
                 );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_RiskLevelSub(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  record ObjAttr("objattr.dbt");

  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    if(FldRealValue == 0)
      FldShowValue = "";
    else
      DL_GetObjAttrName(OBJTYPE_PARTY, PARTY_ATTR_GROUP_RISKLEVEL, FldRealValue, @FldShowValue);
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3)) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    if(ListObjAttr(OBJTYPE_PARTY, PARTY_ATTR_GROUP_RISKLEVEL, ObjAttr))
      FldRealValue = ObjAttr.AttrID;
      FldShowValue = ObjAttr.FullName;
    end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_IsLoadFromFile(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if(Cmd == DLG_INIT)
    FldShowValue = FldRealValue;
  elif(Cmd == DLG_CHANGEVALUE)
    FldRealValue = FldShowValue;
  elif(Cmd == DLG_KEY)
    if(Key == KEY_SPACE) 
      if(FldRealValue == UNSET_CHAR)
        FldShowValue = FldRealValue = SET_CHAR;
      else
        FldShowValue = FldRealValue = UNSET_CHAR;
      end;
    end;
  end;

  if(FldShowValue == SET_CHAR)
    EnableFields(pThis.Data(), PNFLD_INACC_REG_FILEPATH);
    EnableFields(pThis.Data(), PNFLD_INACC_REG_FILECONTENTTYPE);
    pThis.SetLinkedValue(H_CLIENT_NAME, "");
    DisableFields(pThis.Data(), PNFLD_INACC_REG_SUBJECT);
  else
    pThis.SetLinkedValue(H_FILEPATH, "");
    DisableFields(pThis.Data(), PNFLD_INACC_REG_FILEPATH);
    pThis.SetLinkedValue(H_FILECONTENTTYPE, 0);
    DisableFields(pThis.Data(), PNFLD_INACC_REG_FILECONTENTTYPE);
    pThis.SetLinkedValue(H_CLIENT_NAME, "Все");
    EnableFields(pThis.Data(), PNFLD_INACC_REG_SUBJECT);
  end;
END;

PRIVATE MACRO FldProc_FileContentType(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  MACRO Name(Id:INTEGER )
    if(Id == FILE_CONTENT_TYPE_SOFR)
      return "ID СОФР";
    elif(Id == FILE_CONTENT_TYPE_CFT)
      return "ID ЦФТ";
    end;
    return "";
  END;

  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    FldShowValue = Name(FldRealValue);
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3)) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    DL_ListString(null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                  Name(FILE_CONTENT_TYPE_SOFR), FILE_CONTENT_TYPE_SOFR,
                  Name(FILE_CONTENT_TYPE_CFT),  FILE_CONTENT_TYPE_CFT
                 );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_FilePath( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    FldShowValue = FldRealValue = "";
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3)) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    var choice = 1;
    var isTerm = false;
    var filePath = "";
    if(not isStandAlone())
      choice = DL_Menu(makeArray("Терминал", "Сервер"), "Где ищем файл?");
    end;
    if(choice < 0)
      return CM_DEFAULT;
    end;
    if(choice == 0)
      isTerm = true;
    end;
    if(not SelectFile(FilePath, "*.*", "Выберите файл", 0, isTerm))
      return CM_DEFAULT;
    end;
    FldShowValue = FldRealValue = (IIF(isTerm, "$", "") + filePath);
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_IsDifficultChoice(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, pos:INTEGER, FieldName:INTEGER):INTEGER
  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    if(FldRealValue == 0)
      FldShowValue = "";
      pThis.SetLinkedValue(FieldName, 0);
      DisableFields(pThis.Data(), pos);
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
    if(FldRealValue == 0)
      FldShowValue = "=";
      FldRealValue = 1;
      EnableFields(pThis.Data(), pos);
    elif(FldRealValue == 1)
      FldShowValue = "!";
      FldRealValue = 2;
    else
      FldShowValue = "";
      FldRealValue = 0;
      DisableFields(pThis.Data(), pos);
    end;
  end;
  return CM_DEFAULT;        
END;

PRIVATE MACRO FldProc_IsForm(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  FldProc_IsDifficultChoice(pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_INACC_REG_FORMSUB, H_FORMSUB);
END;

PRIVATE MACRO FldProc_IsResident(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  FldProc_IsDifficultChoice(pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_INACC_REG_RESIDENTSUB, H_RESIDENTSUB);
END;

PRIVATE MACRO FldProc_IsRiskLevel(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  FldProc_IsDifficultChoice(pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_INACC_REG_RISKLEVELSUB, H_RISKLEVELSUB);
END;

PRIVATE MACRO FldProc_BeginDate(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    FldShowValue = FldRealValue;
  elif(Cmd == DLG_CHANGEVALUE) 
    FldRealValue = FldShowValue;
    if((FldRealValue != null) AND (pThis.GetLinkedValue(H_PNFLD_ENDDATE) != null))
      if(FldRealValue > pThis.GetLinkedValue(H_PNFLD_ENDDATE))
        MsgBox("Дата окончания не может быть меньше даты начала периода.");
        return CM_IGNORE;
      end;
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_EndDate(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    FldShowValue = FldRealValue;
  elif(Cmd == DLG_CHANGEVALUE) 
    FldRealValue = FldShowValue;
    if((FldRealValue != NULL) AND (pThis.GetLinkedValue(H_PNFLD_BEGINDATE) != null))
      if(FldRealValue < pThis.GetLinkedValue(H_PNFLD_BEGINDATE))
        MsgBox( "Дата окончания не может быть меньше даты начала периода.");
        return CM_IGNORE;
      end;
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

// Инициализация
CLASS (DL_CPanel) DL_AccountsReg_Panel()
  var m_ClientScroll = ClientChooseScroll();

  PRIVATE MACRO Init()
    AddFieldProc(0, PNFLD_INACC_REG_BEGINDATE,          H_PNFLD_BEGINDATE,   @FldProc_BeginDate,        DlAccountsFormData.BeginDate);
    AddFieldProc(0, PNFLD_INACC_REG_FINISHDATE,         H_PNFLD_ENDDATE,     @FldProc_EndDate,          DlAccountsFormData.EndDate);
    AddFieldProc(0, PNFLD_INACC_REG_DIVIDE_BY_DATES,    DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,      DlAccountsFormData.DivideByDates);
                                                                            
    AddFieldProc(0, PNFLD_INACC_REG_IS_CLIENT_ACCOUNTS, DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,      DlAccountsFormData.IsClientsAccounts);
                                                                            
    AddFieldProc(0, PNFLD_INACC_REG_SUBJECT,            H_CLIENT_NAME,       @FldProc_Client            );
    AddFieldProc(0, PNFLD_INACC_REG_ISFORM,             H_ISFORM,            @FldProc_IsForm,           DlAccountsFormData.IsForm);
    AddFieldProc(0, PNFLD_INACC_REG_FORMSUB,            H_FORMSUB,           @FldProc_FormSub,          DlAccountsFormData.FormSub, 35, 10);
    AddFieldProc(0, PNFLD_INACC_REG_ISRESIDENT,         H_ISRESIDENT,        @FldProc_IsResident,       DlAccountsFormData.IsResident);
    AddFieldProc(0, PNFLD_INACC_REG_RESIDENTSUB,        H_RESIDENTSUB,       @FldProc_ResidentSub,      DlAccountsFormData.ResidentSub, 35, 11);
    AddFieldProc(0, PNFLD_INACC_REG_ISRISKLEVEL,        H_ISRISKLEVEL,       @FldProc_IsRiskLevel,      DlAccountsFormData.IsRiskLevel);
    AddFieldProc(0, PNFLD_INACC_REG_RISKLEVELSUB,       H_RISKLEVELSUB,      @FldProc_RiskLevelSub,     DlAccountsFormData.RiskLevelSub);
                                                                            
    AddFieldProc(0, PNFLD_INACC_REG_ISLOADFROMFILE,     H_ISLOADFROMFILE,    @FldProc_IsLoadFromFile,   DlAccountsFormData.IsLoadFromFile);
    AddFieldProc(0, PNFLD_INACC_REG_FILEPATH,           H_FILEPATH,          @FldProc_FilePath,         DlAccountsFormData.FilePath);
    AddFieldProc(0, PNFLD_INACC_REG_FILECONTENTTYPE,    H_FILECONTENTTYPE,   @FldProc_FileContentType,  DlAccountsFormData.FileContentType, 35, 15);
                                                                              
    AddFieldProc(0, PNFLD_INACC_REG_IS_BANK_ACCOUNTS,   DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,      DlAccountsFormData.IsBankAccounts);
                                                                            
    AddFieldProc(0, PNFLD_INACC_REG_WITH_APOSTROFS,     DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,      DlAccountsFormData.IsUseOpostrofs);
    AddFieldProc(0, PNFLD_INACC_REG_TO_EXCEL,           DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,      DlAccountsFormData.IsExportToExcel);
  END;

  PRIVATE MACRO Check():BOOL
    if((GetFieldValue(PNFLD_INACC_REG_IS_CLIENT_ACCOUNTS) != SET_CHAR) and (GetFieldValue(PNFLD_INACC_REG_IS_BANK_ACCOUNTS) != SET_CHAR))
      msgbox("Не выбран ни один из режимов запуска");
      return false;
    end;

    if((GetFieldValue(PNFLD_INACC_REG_ISLOADFROMFILE) == SET_CHAR) and (Trim(GetFieldValue(PNFLD_INACC_REG_FILEPATH)) == ""))
      msgbox("Не задан к путь файлу загрузки списка клиентов");
      return false;
    end;

    if((GetFieldValue(PNFLD_INACC_REG_ISLOADFROMFILE) == SET_CHAR) and (GetFieldValue(PNFLD_INACC_REG_FILECONTENTTYPE) == 0))
      msgbox("Не выбран тип ID клиентов в файле");
      return false;
    end;

    return true;
  END;
  
  initDL_CPanel("dlrep.lbr", "DLACCREP"); 
END;
