/*
$Name:        dlgenagr30.mac
$Module:      Дилинг
$Description: Генеральные соглашения. Шаг "Пролонгация ГС"
*/
IMPORT RSD, globals, Календарь; 

PRIVATE VAR
    PanelPrm = NULL;

/****************************************************************************************/
PRIVATE CLASS PanelParm
  private const // поля панели параметров отчета
    FLD_OPERDATE    = 0,
    FLD_NEWENDDATE     = 1,
    FLD_LAST           = 2;

  var
      OperDate = {curdate}, NewEndDate = {curdate};

  private macro InitDefaultValue(fldnum)
      if (fldnum == FLD_OPERDATE)
          OperDate = {curdate};
      elif (fldnum == FLD_NEWENDDATE)
          NewEndDate = {curdate} + 1;
      else
          return;
      end;
  end;

  private macro Clear(fldnum)
      if (fldnum == FLD_OPERDATE)
          OperDate = date(0,0,0);
      elif (fldnum == FLD_NEWENDDATE)
          NewEndDate = date(0,0,0);
      else
          return;
      end;

      InitDefaultValue(fldnum);
  end;

  private macro ProcessPn(Pn, Cmd, ID, Key)
      var Parm = NULL;

      if(Cmd == DLG_INIT)
          Pn(FLD_OPERDATE)    = OperDate;
          Pn(FLD_NEWENDDATE)  = NewEndDate;
          UpdateFields(Pn);
      elif((Cmd == DLG_KEY) and (Key == 317))
          Pn(ID) = GetDateByCalendar(Pn(ID)); 
          UpdateFields(Pn);
      elif((Cmd == DLG_KEY) and ((Key == 316)or(Key == 323)or(Key == 13))) // запуск 
          if ((Key == 13)and(ID != (FLD_LAST - 1))) // если нажали Enter в первом поле панели, то обрабатывем это как переход в другое поле
              return CM_DEFAULT;
          end;

          return CM_SAVE;
      elif(Cmd == DLG_SAVE)
//      if(Cmd == DLG_INIT)
          OperDate    = Pn(FLD_OPERDATE);
          NewEndDate = Pn(FLD_NEWENDDATE);
      end;

      return CM_DEFAULT;
  end;

  macro ClearPanel()
      Clear(FLD_OPERDATE);
      Clear(FLD_NEWENDDATE);
  end;

  macro Run()
      macro ProcessPanel(Pn, Cmd, ID, Key)
          return ProcessPn(Pn, Cmd, ID, Key);
      end;

      record Panel("DLGAGP", "Deals.lbr") dialog;
      return RunDialog(Panel, @ProcessPanel);
  end;

//Constructor
  ClearPanel();
END;

macro GetNewDate(OPERDATE:@date,NEWENDDATE:@date)
    PanelPrm = PanelParm;
    if(PanelPrm.Run())
        OPERDATE=PanelPrm.OPERDATE;
        NEWENDDATE=PanelPrm.NewEndDate;
    end;

end;


macro ExecuteStep(Buffer, Document, DocKind, ID_Operation, ID_Step)
    var  cmd = "";

    record fd(dl_genagr); 
    setbuff(fd, Document);

    PanelPrm = PanelParm;
    if (PanelPrm.Run())
        if (PanelPrm.NewEndDate > (fd.Start + fd.Duration))
            cmd = "update ddl_genagr_dbt t set t.t_Duration = " + Int(PanelPrm.NewEndDate - fd.Start) + 
                  ", t.t_date_end = to_date('" + date(PanelPrm.NewEndDate) + "', 'dd.mm.yyyy')" +
                  ", t.t_oper = " + int({oper}) +
                  " where (t.t_genagrid = " + fd.GenAgrID + ")";
            RsdCommand(cmd).Execute;
            
            cmd = "update ddlgrdeal_dbt t set T.T_PLANDATE=to_date('"+date(PanelPrm.NewEndDate)+"','dd.mm.yyyy')"+
                  "where t.T_DOCID="+fd.genagrid+" and t.T_DOCKIND="+fd.dockind+" and T.T_TEMPLNUM=51";
            RsdCommand(cmd).Execute;
            
            //VAR СОжиданием=((fd.initiator==2)and(fd.method_reg==2)and(fd.METHOD_WAYTWOWAY==1)):bool; 
            var graf;
            if((fd.initiator==2)and(fd.method_reg==2)and(fd.METHOD_WAYTWOWAY==1))
                graf=49;
            else
                graf=47;
            end;
            
            cmd = "insert into ddlgrdeal_dbt (t_dockind,t_docid,t_templnum,t_plandate) "+
                  "values("+fd.dockind+","+fd.genagrid+","+
                  graf+",to_date('"+date(PanelPrm.OPERDATE)+"','dd.mm.yyyy'))";
            RsdCommand(cmd).Execute;
            
        else
            msgbox("Новая дата окончания ГС не может быть равна или меньше, чем существующая.");
            return 1;
        end;
    else
        return 1;
    end;

    return 0;
end;

MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */  

  var cmd;
  record fd(dl_genagr); 

  if (errTrn) 
    return 1;
  end; 

  setbuff(fd, FirstDoc);

  if (CommitOrRollback == 1)
      cmd = "insert into ddl_genagrprol_dbt " 
             "(select " + ID_Operation + ", " + ID_Step + ", " + fd.GenAgrID + ", " + fd.Duration + " "
             "from ddl_genagr_dbt t where  "
             "(t.t_genagrid = " + fd.GenAgrID + "))";
      RsdCommand(cmd).Execute;
  else
      cmd = "update ddl_genagr_dbt set t_duration = " 
             "(select t.T_OLDDURATION "  
             " from ddl_genagrprol_dbt t where  "
             "t_id_operation = " + ID_Operation + " and t_id_step = " + ID_Step + " and t_genagrid = " + fd.GenAgrID + ")";
      RsdCommand(cmd).execute;
  
      RsdCommand("delete from ddl_genagrprol_dbt where t_id_operation = " + ID_Operation + " and t_id_step = " + ID_Step + " and t_genagrid = " + fd.GenAgrID).execute;
  end;

  return 0;
END;

