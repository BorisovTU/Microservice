/*
$Name:         ws_md_ReutSettings.mac
$Module:       Монитор дилера
$Description:  Настройки для работы с котировками от Рейтерс
*/

import RsbDataSet, secinter;
import  XmlRpcInter, ServiceAction, FIInter, BankInter, globals, lib_str;

 CONST AUTHORIZATION = 1;
 CONST QUOTELIST = 2;

 CONST ASK_IDX = 0;
 CONST BID_IDX = 1;
 CONST DATE_IDX = 2;
 CONST TIME_IDX = 3;
 CONST CURPAIR_IDX = 4;

 CONST TOKEN_EXPIRED_ERR = "Token expired.";
 CONST END_RICs_SYMBOL = "="; /*Символ, которым завершается запрос для валютной пары в Рейтерс*/

/*Данные в валютных парах и кодах ISO*/
CLASS ConvertData
  var ISO_CODE:string;       // значение поля код ISO в справочниках валют RS-Bank
  var REUTERS_CODE: string;   // значение, передающееся в рейтерс
END;

/*Настройки для работы с терминалом RUTERS
  параметры аутенификации:
           _username
           _AppId
           _password
  Исполняемый макрос и процедура (запускаются из планировщика в ws_md_StartShed.mac
           ProcMacroName
           ExecFunName
  Параметры xml запроса к веб-сервису рейтерс для получения котировок инструментом QuoteList:
           _adress, Authorization_url, Authorization_method, GetQuoteList_url, GetQuoteList_method
*/
CLASS REUTERS_SETTINGS()

  PRIVATE CONST _username = "";
  PRIVATE CONST _AppId = "";
  PRIVATE CONST _password = "";

  /*Макрос, запускающий обратку данных*/
  PRIVATE CONST  ProcMacroName = "ws_md_GetRates.mac";
  /*Функция, запускающая обработку*/
  PRIVATE CONST  ExecFunName = "GetRates";

  PRIVATE CONST _adress = "http://www.w3.org/2005/08/addressing";

  PRIVATE CONST  Authorization_url = "https://api.rkd.reuters.com/api/TokenManagement/TokenManagement.svc/Anonymous";
  PRIVATE CONST  Authorization_method = "http://www.reuters.com/ns/2006/05/01/webservices/rkd/TokenManagement_1/CreateServiceToken_1";

  PRIVATE CONST  GetQuoteList_url = "https://api.rkd.reuters.com/api/QuoteLists/QuoteLists.svc";
  PRIVATE CONST  GetQuoteList_method = "http://www.reuters.com/ns/2006/05/01/webservices/rkd/QuoteLists_1/GetSimpleData_1";


  // Соответствие кода ISO и идентификторов полей в рейтерс
  private var CurrencyPair = TArray(); 

  private macro FillCurrencyPairArray()
    var conv_data = ConvertData();

    conv_data.ISO_CODE = "USD";  // значение поля код ISO в справочниках валют RS-Bank
    conv_data.REUTERS_CODE = ""; // значение, передающееся в рейтерс
    CurrencyPair[CurrencyPair.Size] = conv_data;

    conv_data = ConvertData();
    conv_data.ISO_CODE = "EUR";
    conv_data.REUTERS_CODE = "EUR";
    CurrencyPair[CurrencyPair.Size] = conv_data;
    
    conv_data = ConvertData();
    conv_data.ISO_CODE = "RUR";
    conv_data.REUTERS_CODE = "RUB";
    CurrencyPair[CurrencyPair.Size] = conv_data;

    conv_data = ConvertData();
    conv_data.ISO_CODE = "KZT";
    conv_data.REUTERS_CODE = "KZT";
    CurrencyPair[CurrencyPair.Size] = conv_data;
  end;
  
  // Возвращаемые значения о котрировках рейтрес.
  private var FIDs = TArray(); 

  private macro FillFIDsArray()
    FIDs[ASK_IDX] = "ASK";
    FIDs[BID_IDX] = "BID";
    FIDs[DATE_IDX] = "ACTIV_DATE";
    FIDs[TIME_IDX] = "QUOTIM";
  end;

  macro AppId()
    return _AppId;
  end;

  macro Password()
    return _password;
  end;

  macro UserName()
    return _username; 
  end;

  macro Adressing()
    return _adress;
  end;

  macro GetUrl( Action )
    var url = "";
    if (Action == AUTHORIZATION)
      url = Authorization_url; 
    elif (Action == QUOTELIST)
      url = GetQuoteList_url;
    end;
    return url;
  end;

  macro GetMethod( Action )
    var method = "";
    if (Action == AUTHORIZATION)
      method = Authorization_method;
    elif (Action == QUOTELIST)
      method = GetQuoteList_method;
    end;
    return method;
  end;

  macro GetCurrencyCode(code_iso)
    var i = 0;
 /* чаще всего они совпадают, но могут быть исключения,
    проверим, есть ли специальные заданные значения. */
    var reut_code = code_iso;

    while (i < CurrencyPair.Size)     
      if(CurrencyPair[i].ISO_CODE == code_iso)
        reut_code = CurrencyPair[i].REUTERS_CODE;
        break;
      end;
      i = i + 1;
    end;
    return reut_code;
  end;

  macro GetFids()
    return FIDs;
  end;

  macro GetFid(i)
    if(i < FIDs.size)
      return FIDs[i];
    else
      return "";
    end;
  end;

  macro GetProcName()
    return ExecFunName;
  end;

  macro GetMacroName()
    return ProcMacroName;
  end;


/* Конструктор*/
  FillCurrencyPairArray();
  FillFIDsArray();
END;