/*
$Name:             DlNotExec_Report.mac
$Module:           Ядро Securities
$Description:      Отчет Информация о неисполненных сделках (ф. 702)
*/
import "DlNotExec_Form.mac", "mctplprm.mac", "dlquery.mac", "dldlngfun.mac", "spCache.mac", "dv_categ.mac", "dv_car.mac", "secinter.mac";

PRIVATE RECORD party("party.dbt");
PRIVATE VAR DlNotExec_Form;

PRIVATE CLASS NotExecRQ(pDealPart:Integer,pKind:integer,pSubKind:integer,pPlandate:Date,pActiveCode:string,pActiveType:string,pAmount:money)
  var DealPart, PlanDate, ActiveCode, ActiveType, Amount, Kind, SubKind;

  DealPart   = pDealPart;
  PlanDate   = pPlandate;
  ActiveCode = pActiveCode;
  ActiveType = pActiveType;
  Amount     = pAmount;
  Kind       = pKind;
  SubKind    = pSubKind;

  private macro _condition1_2(): bool
    if( ((DealPart == 1) and (Kind == DLRQ_KIND_COMMIT)) OR
             ((DealPart == 2) and (Kind == DLRQ_KIND_REQUEST)) )
      return true;
    else 
      return false;
    end;
  end;
  
  macro ActiveCode1_2() : string
    if ( _condition1_2() )
      return ActiveCode;
    else 
      return "";
    end;
  end;

  macro ActiveCode2_1() : string
    if ( _condition1_2() )
      return "";
    else 
      return ActiveCode;
    end;
  end;

  macro ActiveType1_2() : string
    if ( _condition1_2() )
      return ActiveType;
    else 
      return "";
    end;
  end;

    macro ActiveType2_1() : string
    if ( _condition1_2() )
      return "";
    else 
      return ActiveType;
    end;
  end;

  macro Com1()
    if ( _condition1_2() )
      return Amount;
    else 
      return null;
    end;
  end;

  macro Com2()
    if ( _condition1_2() )
      return null;
    else 
      return Amount;
    end;
  end;

END;
                            
PRIVATE CLASS (TArray) NotExecRQ_Array()

  /* Очистить массив. */
  macro ClearArray()
     this.Size = 0;
  end;

END;

PRIVATE CLASS DV_NotExecStr(pDealPart:integer,pPlanDate:Date,
                            pCom1:money,pActiveCode1:string,pActiveType1:string,
                            pCom2:money,pActiveCode2:string,pActiveType2: string)
  var DealPart,PlanDate,Com1,ActiveCode1,ActiveType1,Com2,ActiveCode2,ActiveType2;

  DealPart    = pDealPart;
  PlanDate    = pPlandate;
  Com1        = pCom1;
  ActiveCode1 = pActiveCode1;
  ActiveType1 = pActiveType1; // 1
  Com2        = pCom2;
  ActiveCode2 = pActiveCode2;
  ActiveType2 = pActiveType2;  //2 

END;

PRIVATE CLASS (TArray) DV_NotExecStr_Array()

  /* Очистить массив. */
  macro ClearArray()
     this.Size = 0;
  end;

  macro Add(DealPart,Kind,PlanDate,Com,ActiveCode,ActiveType)
     var
        Counter = 0, i = 0;
     var IsExistElem = false;

     while( (Counter < this.Size) and (IsExistElem == false) )
        if( (this.(Counter).PlanDate == PlanDate) 
           and (this.(Counter).DealPart == DealPart) 
           and (((Kind == 1) and ((this.(Counter).ActiveCode1==null) or (this.(Counter).ActiveCode1 == ActiveCode))) or ((Kind == 2) and ((this.(Counter).ActiveCode2==null) or (this.(Counter).ActiveCode2 == ActiveCode)))) 
          )
          IsExistElem = true;
        else
          Counter = Counter + 1;
        end;
     end;

     if( IsExistElem and (Counter < this.Size) )
        if( Kind == 1 )
           if( this.(Counter).ActiveCode1 == null )
              this.(Counter).Com1 = Com;
              this.(Counter).ActiveCode1 = ActiveCode;
              this.(Counter).ActiveType1 = ActiveType;
           else
              this.(Counter).Com1 = this.(Counter).Com1 + Com;
           end;
        else
           if( this.(Counter).ActiveCode2 == null )
              this.(Counter).Com2 = Com;
              this.(Counter).ActiveCode2 = ActiveCode;
              this.(Counter).ActiveType2 = ActiveType;
           else
              this.(Counter).Com2 = this.(Counter).Com2 + Com;
           end;
        end;
     else
        if( Kind == 1 )
           this.(this.Size) = DV_NotExecStr(DealPart,PlanDate,Com,ActiveCode,ActiveType,null,null,null);
        else
           this.(this.Size) = DV_NotExecStr(DealPart,PlanDate,null,null,null,Com,ActiveCode,ActiveType);
        end;
     end;
  end;

END;

PRIVATE MACRO GetPartyCode(PartyID:integer,KindParty:@integer,IsRes:@bool,LegalForm:@integer):string
  var PartyCode = "";

  KindParty = MC_GetKindParty( PartyID, IsRes, null, LegalForm );

  if ((LegalForm == PTLEGF_INST) or (LegalForm == null))
     if( (KindParty == 1) AND (IsRes == true) )/*Для кредитных организаций-резидентов*/
        PartyCode = ПолучитьКодСубъекта(PartyID, PTCK_BANKREGNUM);
     elif( IsRes == true )/*Для некредитных организаций-резидентов*/ 
        PartyCode = ПолучитьКодСубъекта(PartyID, PTCK_OGRN);
        if( PartyCode == "" )
           PartyCode = "#"+DL_getPartyFullName(PartyID);
        end;
     else/*Для организаций-нерезидентов*/
        PartyCode = ПолучитьКодСубъекта(PartyID, PTCK_SWIFT);
        if( PartyCode == "" )
           PartyCode = "#"+DL_getPartyFullName(PartyID);
        end;
     end;
  else
     PartyCode = "";
  end;
  return PartyCode;
END;

PRIVATE MACRO GetPartyType(PartyID:integer,KindParty:integer,IsRes:bool,LegalForm:integer) : string
  var PartyType = "";

  if ((LegalForm == PTLEGF_INST) or (LegalForm == null))
     if( (KindParty == 1) AND (IsRes == true) )/*Для кредитных организаций-резидентов*/
        PartyType = "1";
     elif( IsRes == true )/*Для некредитных организаций-резидентов*/ 
        PartyType = "2";
        if( PartyType == "" )
           PartyType = "#"+DL_getPartyFullName(PartyID);
        end;
     else/*Для организаций-нерезидентов*/
        PartyType = "3";  
        if( PartyType == "" )
           PartyType = "#"+DL_getPartyFullName(PartyID);
        end;
     end;
  elif ( (LegalForm != null) and (LegalForm == PTLEGF_PERSN)  )
     PartyType = "4";
  end;
  return PartyType;
END;

private MACRO  GetActiveType(FI_KIND:integer) : string
     var vActiveType = "";

     if( FI_KIND == FIKIND_CURRENCY )
        vActiveType = "1";
     elif( FI_KIND == FIKIND_AVOIRISS )
        vActiveType = "2";
     elif( FI_KIND == FIKIND_METAL )
        vActiveType = "3";
     else
        vActiveType = "4";
     end;

     return vActiveType;
END; 

PRIVATE MACRO GetSecondPartType(PartyID:integer,KindParty:integer,IsRes:bool,LegalForm:integer) : string
  if ( PartyID < 0 )
    return "5";
  end;

  var PartyType = "";

  if ((LegalForm == PTLEGF_INST) or (LegalForm == null))
     if( (KindParty == 1) AND (IsRes == true) )/*Для кредитных организаций-резидентов*/
        PartyType = "1";
     elif( IsRes == true )/*Для некредитных организаций-резидентов*/ 
        PartyType = "2";
        if( PartyType == "" )
           PartyType = "#"+DL_getPartyFullName(PartyID);
        end;
     else/*Для организаций-нерезидентов*/
        PartyType = "3";  
        if( PartyType == "" )
           PartyType = "#"+DL_getPartyFullName(PartyID);
        end;
     end;
  elif ( (LegalForm != null) and (LegalForm == PTLEGF_PERSN)  )
     PartyType = "4";
  end;
  return PartyType;
END;

PRIVATE CLASS TContractorData()
  private var m_PartyID:integer=-1,m_PartyCode:string, m_PartyType:string,
              m_SecondPartType:string,
              m_KindParty:integer,m_IsRes:bool,m_LegalForm:integer,m_LEICode2:string,m_NR:string,m_OrgKind:string;

  macro Init()
     m_PartyCode = "";
     m_PartyType = "";
     m_SecondPartType = "";
     m_KindParty = 0;
     m_IsRes     = false;
     m_LegalForm = 0;
     m_LEICode2  = "";
     m_NR        = "";
     m_OrgKind = "";
  end;

  macro GetPartyInfo(PartyID:integer)

     if( PartyID <= 0 )
        m_PartyID = -1;
        Init();
        m_SecondPartType = "5";
     elif( m_PartyID != PartyID )
        Init();
        m_PartyID = PartyID;
        m_PartyCode = GetPartyCode( m_PartyID, @m_KindParty, @m_IsRes, @m_LegalForm );
        m_PartyType      = GetPartyType( m_PartyID, m_KindParty, m_IsRes, m_LegalForm );
        m_SecondPartType = GetSecondPartType( m_PartyID, m_KindParty, m_IsRes, m_LegalForm );

        if(m_IsRes)
           m_NR = "R";
        else
           m_NR = "N";
        end;

        m_LEICode2 = ПолучитьКодСубъекта(m_PartyID, PTCK_LEI);
        /*определяем m_OrgKind*/
        m_OrgKind = "";
        if( m_LegalForm == PTLEGF_PERSN )
           m_OrgKind = "P";
        else
           var OwnForm = "", OrgForm = "";

           if( ПолучитьСубъекта( m_PartyID, party ) == 0 )
              getMainObjAttr( null, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ),
                              1, /*Форма собственности*/
                              null, null, OwnForm );
              getMainObjAttr( null, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ),
                              57, /*<Организационно-правовые формы (НРД)>*/
                              null, null, OrgForm );
              if( m_KindParty == 1 )/*для кредитных организаций*/
                 m_OrgKind = "CR";
              elif( m_KindParty == 2 )/*для некредитных финансовых организаций*/
                 m_OrgKind = "NCR";
              elif( m_KindParty == 7 )/*по принадлежности <Центральным банком>*/
                 m_OrgKind = "CBRF";
              elif( ВидСубъекта( m_PartyID, PTK_VEB ) == true )/*принадлежность <Внешэкономбанком>*/
                 m_OrgKind = "964";
              elif( ВидСубъекта( m_PartyID, PTK_PENSFOND ) == true )/*принадлежность <Пенсионным фондом>*/
                 m_OrgKind = "PF";
              elif( ВидСубъекта( m_PartyID, PTK_NONFINORG ) == true )/*принадлежность <Нефинансовой организацией*/
                 m_OrgKind = "NF";
              elif( (m_KindParty == 3) or ((ВидСубъекта( m_PartyID, PTK_FEDJUR) == true) and m_IsRes) )/*принадлежность <Органом гос. власти> или <Федеральным органом исп.власти>*/
                 m_OrgKind = "FB";
              elif( (m_KindParty == 4) or ((ВидСубъекта( m_PartyID, PTK_LOCALGOVERNINGINST) == true) and m_IsRes) )/*принадлежность <Органом гос. власти субъекта> или <Органом местной власти> или <Местным органом исполнит. Власти>*/
                 m_OrgKind = "RB";
              elif( OwnForm == "3" )/*категория <:302-п> = Государственные внебюджетные фонды*/
                 m_OrgKind = "NB";
              elif( OwnForm == "4" )/*категория <:302-П> = Внебюджетные фонды субъектов РФ и местных органов власти*/
                 m_OrgKind = "SB";
              elif( (OrgForm == "5") or (OrgForm == "15") or (OrgForm == "20") )/*категория <Организационно-правовые формы (НРД)>= Государственная корпорация или Муниципальное учреждение или Унитарное предприятие*/
                 m_OrgKind = "GC";
              end;
           end;
        end;
     end;
  end;

  MACRO КонтрагентЗадан():bool
     return (m_PartyID > 0);
  END;

  macro PartyCode():string
     return m_PartyCode;
  end;

  macro SecondPartType() : string
      return m_SecondPartType;
  end;

  macro PartyType(): string
     return m_PartyType;
  end;

  macro NR():string
     return m_NR;
  end;

  macro LEICode2():string
     return m_LEICode2;
  end;

  macro OrgKind():string
     return m_OrgKind;
  end;

END;

PRIVATE CLASS SP_NotExec_Report( Report:OBJECT )
   VAR m_Report = Report;

   PRIVATE VAR m_Deal, m_ContrClientID, m_Contractor = TContractorData(),
               m_ClientCode1, m_ClientType1, m_ClientType2, m_SecondPartType, m_LEIClientCode1, m_Code2, m_ClientCode2, m_LEIClientCode2, 
               m_ActiveCode1_2, m_ActiveType1_2, m_Com1, m_ActiveCode2_1, m_ActiveType2_1, m_Com2, m_CalcDate1, m_CalcDate2,
               m_StartComCode1, m_StartCom1, m_StartComCode2, m_StartCom2, m_PlanDate;

   PRIVATE VAR m_NotExecRQ = NotExecRQ_Array();
   PRIVATE VAR m_CacheFinInstr  = TX_CacheFinInstr();

   MACRO Netting():string
      return "";/*Для сделок БОЦБ не заполняется*/
   END;

   MACRO Rep():string
      return "";/*Для сделок БОЦБ не заполняется*/
   END;

   MACRO Com1()
      return m_Report.ВыводЧерезФормулу(m_Com1,5);
   END;

   MACRO Com2()
      return m_Report.ВыводЧерезФормулу(m_Com2,5);
   END;

   MACRO ActiveCode1_2():string
      return m_ActiveCode1_2;
   END;

   MACRO ActiveType1_2() : string
      return m_ActiveType1_2;
   END;

   MACRO ActiveCode2_1():string
      return m_ActiveCode2_1;
   END;

    MACRO ActiveType2_1() : string
      return m_ActiveType2_1;
   END;  

   MACRO DealCode():string
      return m_DEAL.DealCode;
   END;

   MACRO DealDate():DATE
      return SQL_ConvTypeDate(m_DEAL.DealDate);
   END;

   MACRO CalcDate1():DATE
      return m_CalcDate1;
   END;

   MACRO CalcDate2():DATE
      return m_CalcDate2;
   END;

   MACRO ClientCode1():string
      return m_ClientCode1;
   END;

   MACRO ClientType1() : string
      return m_ClientType1;
   END;

   MACRO ClientType2() : string
      return m_ClientType2;
   END;

   MACRO SecondPartType() : string
      return m_SecondPartType;
   END;

   /*Код <LEI> клиента, в интересах которого совершена сделка.
     Для собственных сделок поле не заполняется.*/
   MACRO LEIClientCode1():string
      return m_LEIClientCode1;
   END;

   /*Код контрагента по сделке.
     Определение вида выводимого кода см. в примечании 2.
     Если в параметрах биржевой  сделки поле КОНТРАГЕНТ не заполнено, то в А09 поле выводится полное наименование биржи.
   */
   MACRO Code2():string
      return m_Code2;
   END;

   /*Код <LEI> контрагента по сделке.
     Для биржевых сделок, у которых не заполнено поле КОНТРАГЕНТ, не заполняется
   */
   MACRO LEICode2():string
      return m_Contractor.LEICode2();
   END;

   /*Код клиента второй стороны по сделке*/
   MACRO ClientCode2():string
      return m_ClientCode2;
   END;

   MACRO LEIClientCode2():string
      return m_LEIClientCode2;
   END;

   MACRO OrgKind():string
      return m_Contractor.OrgKind();
   END;

   MACRO NR():string
      return m_Contractor.NR();
   END;

   MACRO ПолучитьДанныеСторонПоСделке()
      /*получаем данные контрагента по сделке*/
      m_Contractor.GetPartyInfo(m_Deal.PartyID);
      if( (not m_Contractor.КонтрагентЗадан()) and (m_Deal.MARKETID > 0) )
         m_Code2 = DL_getPartyFullName(m_Deal.MARKETID);
         m_SecondPartType = "5";
      else
         m_Code2 = m_Contractor.PartyCode();
         m_SecondPartType = m_Contractor.SecondPartType();
      end;
      /*получаем данные клиента по сделке*/
      m_ClientCode1 = "";

      m_ClientType1 = IIF( (m_Deal.ClientID < 0) or (m_Deal.ClientID == {OurBank}),  "0", "" );
    
      m_LEIClientCode1 = "";
      if( m_Deal.ClientID > 0 )
         /*Для собственных сделок поле не заполняется*/
         var Client1 = TContractorData();
         Client1.GetPartyInfo(m_Deal.ClientID);
         m_ClientCode1 = Client1.PartyCode();
         m_ClientType1 = Client1.PartyType();
         /*Код <LEI> клиента, в интересах которого совершена сделка.
           Для собственных сделок поле не заполняется.*/
         m_LEIClientCode1 = ПолучитьКодСубъекта(m_Deal.ClientID, PTCK_LEI);
      end;

      /*получаем данные для клиента-контрагента*/
      m_ClientCode2 = "";
      m_ClientType2 = "";
      m_LEIClientCode2 = "";
      m_ContrClientID = -1;

      if( m_Contractor.КонтрагентЗадан() )
         var m_tick = TRecHandler( "dl_tick" );
         m_tick.rec.DealID = m_Deal.DealID;
         if( GetLinkedObject( 1, OBJTYPE_SECDEAL, UniID( m_tick, OBJTYPE_SECDEAL ), OBJTYPE_PARTY, party) == 0 )
            if( ПолучитьСубъекта(party.PartyID, party) == 0 )
               m_ContrClientID = party.PartyID;
            else
               m_Report.AddErrMsg( "Не могу получить субъекта (PartyID = " + string(party.PartyID) + ")." );
            end;
         end;

          m_ClientType2 = IIF( m_ContrClientID < 0,  "0", "" );

         if( m_ContrClientID > 0 )
            var Client2 = TContractorData();
            Client2.GetPartyInfo(m_ContrClientID);
            m_ClientCode2 = Client2.PartyCode();
            m_ClientType2 = Client2.PartyType();
            m_LEIClientCode2 = ПолучитьКодСубъекта(m_ContrClientID, PTCK_LEI);
         end;
      end;

      if ( (m_ContrClientID < 0) and (m_ClientType2 == "") )
        m_ClientType2 = "0";
      end;
   END;

   MACRO Risk():string
      if( m_Deal.ClientID > 0 )
         return "С";
      end;
   END;

   MACRO Place():string
      if( m_Deal.MARKETID > 0 )
         return "1";
      end;
      return "2";
   END;

   MACRO OperKind():string
      return "REPO";
   END;

   MACRO StartComCode1():string
      return m_StartComCode1;
   END;

   MACRO StartComCode2():string
      return m_StartComCode2;
   END;

   MACRO StartCom1()
      return m_Report.ВыводЧерезФормулу(m_StartCom1,5);
   END;

   MACRO StartCom1_m() : money
      return m_StartCom1;
   END;

   MACRO StartCom2()
      return m_Report.ВыводЧерезФормулу(m_StartCom2,5);
   END;

   MACRO StartCom2_m() : money
      return m_StartCom2;
   END;

   MACRO GetMinMaxPlanRQ(DealPart:integer,Kind:integer,IsMax:bool,pFIID:@integer,pAmount:@money):date
     var vPlanDate = Date(0,0,0);

     pFIID = -1;
     pAmount = $0;                  

     var Select = " select h.t_PlanDate, "
                 +"        case when rq.t_SubKind = ? then RQ.T_FIID "
                 +"             else fi.t_FaceValueFI end T_FIID, "
                 +"        case when rq.t_SubKind = ? then H.T_AMOUNT "
                 +"             else Rsb_Secur.SC_ConvSumTypeRep(h.T_Amount, rq.T_FIID, fi.t_FaceValueFI, fi.t_FaceValueFI, ?, ?) end T_AMOUNT "
                 +"   from v_rqhist h, ddlrq_dbt rq, dfininstr_dbt fi "
                 +"  where rq.T_DOCID = ? "
                 +"    and rq.T_DOCKIND = ?"
                 +IIF(Kind != null, " and rq.t_Kind = ? ", "")
                 +"    and rq.T_DealPart = ? "
                 +"    and rq.t_Type in(?,?,?,?)"
                 +"    and H.T_RQID = RQ.T_ID"
                 +"    and fi.T_FIID = RQ.T_FIID";

     if( IsMax )
        Select = Select + " ORDER BY h.t_PlanDate desc, rq.t_Type";
     else
        Select = Select + " ORDER BY h.t_PlanDate, rq.t_Type ";
     end;

     var cmd = DL_RSDCommand(Select);

     cmd.addParam( DLRQ_SUBKIND_CURRENCY );
     cmd.addParam( DLRQ_SUBKIND_CURRENCY );
     cmd.addParam( m_Report.TSS_CourseKind() );
     cmd.addParam( m_Report.EndDate() );
     cmd.addParam( m_Deal.DealID );
     cmd.addParam( m_Deal.BofficeKind );
     if( Kind != null )
        cmd.addParam( Kind );
     end;
     cmd.addParam( DealPart );
     cmd.addParam( DLRQ_TYPE_PAYMENT );
     cmd.addParam( DLRQ_TYPE_AVANCE );
     cmd.addParam( DLRQ_TYPE_DEPOSIT );
     cmd.addParam( DLRQ_TYPE_DELIVERY );
     
     var DataSet = cmd.Execute();
     if( DataSet.MoveNext() )
        vPlanDate = SQL_ConvTypeDate(DataSet.PlanDate);
        pFIID     = SQL_ConvTypeInteger(DataSet.FIID);
        pAmount   = SQL_ConvTypeSum(DataSet.AMOUNT)/1000;
     end;

     return vPlanDate;
   END;

   MACRO PlanDate():Date
      return m_PlanDate;
   END;

   MACRO PrintLineByDeal()
      var vFIID = - 1, vAmount = $0, i = 0;

      ПолучитьДанныеСторонПоСделке();

      m_CalcDate1 = this.GetMinMaxPlanRQ(1,null,false);
      m_CalcDate2 = this.GetMinMaxPlanRQ(2,null,true);

      // обязательство первой стороны
      this.GetMinMaxPlanRQ(1,DLRQ_KIND_COMMIT,false,@vFIID,@vAmount);
      m_StartComCode1 = m_CacheFinInstr.GetISO( vFIID ).m_Number;
      m_StartCom1     = vAmount;

      // обязательство второй стороны
      this.GetMinMaxPlanRQ(1,DLRQ_KIND_REQUEST,false,@vFIID,@vAmount);
      m_StartComCode2 = m_CacheFinInstr.GetISO( vFIID ).m_Number;
      m_StartCom2     = vAmount;

      while( i < m_NotExecRQ.Size() )
         // обязательство первой стороны
         if( ((m_NotExecRQ[i].DealPart == 1) and (m_NotExecRQ[i].Kind == DLRQ_KIND_COMMIT)) OR
             ((m_NotExecRQ[i].DealPart == 2) and (m_NotExecRQ[i].Kind == DLRQ_KIND_REQUEST)) )
            m_Com1          = m_NotExecRQ[i].Amount;
            m_ActiveCode1_2 = m_NotExecRQ[i].ActiveCode;
            m_ActiveType1_2 = m_NotExecRQ[i].ActiveType;
            m_Com2          = null;
            m_ActiveCode2_1 = "";
            m_ActiveType2_1 = "";

         // обязательство второй стороны
         else
            m_Com1          = null;
            m_ActiveCode1_2 = "";
            m_ActiveType1_2 = "";
            m_Com2          = m_NotExecRQ[i].Amount;
            m_ActiveCode2_1 = m_NotExecRQ[i].ActiveCode;
            m_ActiveType2_1 = m_NotExecRQ[i].ActiveType;
         end;

         m_PlanDate = m_NotExecRQ[i].PlanDate;

         m_Report.PrintTableLine(this);

         i = i + 1;
      end;

      m_Report.PrintTableLineKliko(this, m_NotExecRQ);

   END;

   /*Проверим, что сделка полностью исполнена в отчетном периоде*/
   MACRO СделкаИсполненаПолностью()
      var IsDealExec = false;
      var cmd = DL_RSDCommand(" select nvl(max(rq.t_FactDate),to_date('01.01.0001', 'DD.MM.YYYY')) FactDate "
                             +"   from ddlrq_dbt rq " 
                             +"  where rq.T_DOCID = ? "                                                                                                   
                             +"    and rq.T_DOCKIND = ? "
                             +"    and ( select count(1) from ddlrq_dbt rq1 "
                             +"           where rq.T_DOCID = rq1.T_DOCID and rq1.T_DOCKIND = rq.T_DOCKIND "
                             +"             and (rq1.t_FactDate = to_date('01.01.0001', 'DD.MM.YYYY') or rq1.t_FactDate is NULL) ) = 0 "
                             );

      cmd.addParam( m_Deal.DealID );
      cmd.addParam( m_Deal.BofficeKind );

      var DataSet = cmd.Execute();

      if( DataSet.MoveNext() )
         if( (SQL_ConvTypeDate(DataSet.FactDate) >= m_Report.BegDate()) and (SQL_ConvTypeDate(DataSet.FactDate) <= m_Report.EndDate()) )
           IsDealExec = true;
         end;
      end;
      return IsDealExec;
   END;

   /*отбираем все неисполненные ТО на дату или исполненные в отчетный период с проверкой сумм отобранных неисполненных ТО*/
   MACRO CheckDeal()
      var IsExists = false;                   

      m_NotExecRQ.ClearArray();
      var Select= " select t.*, max(case when t.NotExecTotalAmountOnEndDate > 0 then t.NotExecTotalAmountOnEndDate else t.NotExecTotalAmount end) over() NotExecTotalAmount_Max, "
                 +"        max(t.NotExecTotalAmountOnEndDate) over() NotExecOnEndDate_Max "
                 +"   from ( select t.DealPart, t.PlanDate, t.ActiveCode, t.Kind, t.SubKind, "
                 +"                 t.FI_KIND, "
                 +"                 sum(t.Amount) NotExecTotalAmount, "
                 +"                 sum(case when t.FactDate != to_date('01.01.0001','DD.MM.YYYY') and t.FactDate <= ? then 0 else t.Amount end) NotExecTotalAmountOnEndDate "
                 +"            from ( select rq.t_FIID FIID, h.T_PlanDate PlanDate, rq.T_DealPart DealPart, rq.t_Kind Kind, rq.t_SubKind SubKind, "
                 +"                          fin.t_FI_KIND FI_KIND, "
                 +"                          case when rq.t_SubKind = ? then RSB_FIInstr.ConvSum(h.T_Amount, rq.T_FIID, ?, ?) "
                 +"                               else Rsb_Secur.SC_ConvSumTypeRep(h.T_Amount, rq.T_FIID, NVL((select fin.t_FaceValueFI from dfininstr_dbt fin where fin.t_fiid = rq.T_FIID),-1), ?, ?, ?) end Amount, " 
                 +"                          nvl((select HIST.T_FactDate "
                 +"                                 from v_rqhist hist "
                 +"                                where HIST.T_RQID = RQ.T_ID "
                 +"                                  and HIST.T_STATE = ? "
                 +"                              ),to_date('01.01.0001','DD.MM.YYYY')) FactDate, "
                 +"                          case when rq.t_SubKind = ? "
                 +"                               then (select fin.t_ISO_Number from dfininstr_dbt fin where fin.t_fiid = rq.T_FIID) "
                 +"                               else NVL((Select ATTR.T_NAME "                              
                 +"                                           From dobjatcor_dbt AtCor, dobjattr_dbt Attr"         
                 +"                                          Where AtCor.t_ObjectType = "+OBJTYPE_AVOIRISS                       
                 +"                                            And AtCor.t_GroupID    = "+OBJGROUP_AVRTYPE405                       
                 +"                                            And AtCor.t_Object     = LPAD(rq.t_FIID, 10, '0' ) "
                 +"                                            And Attr.t_AttrID      = AtCor.t_AttrID "           
                 +"                                            And Attr.t_ObjectType  = AtCor.t_ObjectType "       
                 +"                                            And Attr.t_GroupID     = AtCor.t_GroupID "          
                 +"                                        ),chr(1)) "
                 +"                                end ActiveCode "
                 +"                     from v_rqhist h, ddlrq_dbt rq, "  
                 +"                                      dfininstr_dbt fin"                                                                                           
                 +"                    where rq.T_DOCID = ? "                                                                                                   
                 +"                      and rq.T_DOCKIND = ? "                                                                                                   
                 +"                      and H.T_RQID = RQ.T_ID "                                                                                                   
                 +"                      and h.t_Instance = (select MAX(hist.t_Instance) "                                                                            
                 +"                                            from v_rqhist hist "                                                                                   
                 +"                                           where hist.t_rqID = RQ.T_ID "                                                                          
                 +"                                             and hist.T_PlanDate <= ? "
                 +"                                             and hist.T_STATE = ? "                                                                                
                 +"                                         ) "
                 +"                      and fin.t_FIID = rq.t_FIID"
                 +"                    order by rq.T_DealPart, h.T_PlanDate, ActiveCode "
                 +"                 ) t "
                 +"          group by t.DealPart, t.PlanDate, t.Kind, t.SubKind, t.ActiveCode, t.FI_KIND "
                 +"          order by t.DealPart, t.PlanDate, t.Kind desc, t.SubKind, t.ActiveCode "
                 +"       ) t ";

      var cmd = DL_RSDCommand(Select);

      cmd.addParam( m_Report.EndDate() );

      cmd.addParam( DLRQ_SUBKIND_CURRENCY );
      cmd.addParam( m_Deal.PayFIID );
      cmd.addParam( m_Report.EndDate() );

      cmd.addParam( m_Deal.PayFIID );
      cmd.addParam( m_Report.TSS_CourseKind() );
      cmd.addParam( m_Report.EndDate() );

      cmd.addParam( DLRQ_STATE_EXEC );
      cmd.addParam( DLRQ_SUBKIND_CURRENCY );

      cmd.addParam( m_Deal.DealID );
      cmd.addParam( m_Deal.BofficeKind );

      cmd.addParam( m_Report.EndDate() );
      cmd.addParam( DLRQ_STATE_OVERDUE );

      var DataSet = cmd.Execute();

      while( DataSet.MoveNext() )

         if( not IsExists )
            if( (СделкаИсполненаПолностью() or (SQL_ConvTypeSum(DataSet.NotExecOnEndDate_Max) > 0)) AND 
                ( (((SQL_ConvTypeSum(m_Deal.TOTALCOST1) >= 500000000) and (m_Deal.CFI == NATCUR)) or ((SQL_ConvTypeSum(m_Deal.TOTALCOST1) >= 5000000) and (m_Deal.CFI != NATCUR))) OR
                  (((SQL_ConvTypeSum(m_Deal.TOTALCOST2) >= 500000000) and (m_Deal.CFI == NATCUR)) or ((SQL_ConvTypeSum(m_Deal.TOTALCOST2) >= 5000000) and (m_Deal.CFI != NATCUR))) OR
                  (((SQL_ConvTypeSum(DataSet.NotExecOnEndDate_Max) >= 100000000) and (m_Deal.PayFIID == NATCUR)) or ((SQL_ConvTypeSum(DataSet.NotExecOnEndDate_Max) >= 1000000) and (m_Deal.PayFIID != NATCUR))) OR
                  ( (((SQL_ConvTypeSum(DataSet.NotExecTotalAmount_Max) >= 100000000) and (m_Deal.PayFIID == NATCUR)) or ((SQL_ConvTypeSum(DataSet.NotExecTotalAmount_Max) >= 1000000) and (m_Deal.PayFIID != NATCUR))) and
                    СделкаИсполненаПолностью()
                  )
                )
              )
               IsExists = true;
            else
               break;//выходим - сделка не подходит
            end;
         end;

         //собираем в массив
         m_NotExecRQ[m_NotExecRQ.size] = NotExecRQ(SQL_ConvTypeInteger(DataSet.DealPart),SQL_ConvTypeInteger(DataSet.Kind),SQL_ConvTypeInteger(DataSet.SubKind),SQL_ConvTypeDate(DataSet.Plandate),
                                                   SQL_ConvTypeStr(DataSet.ActiveCode), 
                                                   GetActiveType( Int(SQL_ConvTypeStr(DataSet.FI_KIND)) ),
                                                   SQL_ConvTypeSum(DataSet.NotExecTotalAmountOnEndDate)/1000); 
      end;

      return IsExists;
   END;

   MACRO ExecuteReport()
     var NRec = 0, ProgressIndicator = TProgressBar();
     var Select =  " select deal.*, LEG1.T_CFI, LEG1.t_PayFIID, LEG1.T_TOTALCOST TOTALCOST1, LEG2.T_TOTALCOST TOTALCOST2 "                                                                   
                  +"   from (select TICK.*,  "                                                               
                  +"                (case when TICK.T_PARTYID <=0  and TICK.T_MARKETID > 0 then TICK.T_MARKETID else TICK.T_PARTYID end) T_CONTRACTORID, "                                         
                  +"                RSB_SECUR.IsBuy(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) t_IsBuy "                                  
                  +"           from ddl_tick_dbt tick "
                  +"          where RSB_SECUR.IsRepo(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1 ) deal, "
                  +"        ddl_leg_dbt leg1, ddl_leg_dbt leg2 "                                                                                                                       
                  +"  where deal.T_CONTRACTORID != ? "
                  +"    and deal.T_CONTRACTORID not in (select T_PARTYID "
                  +"                                      from DDP_DEP_DBT "
                  +"                                      where T_FMREGIM <> 0) "
                  +"    and not exists (select PARTYOWN.T_PARTYKIND  "                                                                                                                       
                  +"                      from dpartyown_dbt partyown "                                                                                                                      
                  +"                     where PARTYOWN.T_PARTYID = deal.T_CONTRACTORID "                                                                                                          
                  +"                       and PARTYOWN.T_PARTYKIND = 58) "                                                                                                                     
                  +"    and LEG1.T_DEALID = deal.T_DEALID "                                                                                                                                  
                  +"    and LEG1.T_LEGKIND = 0 "                                                                                                                                             
                  +"    and LEG1.T_LEGID = 0 "                                                                                                                                               
                  +"    and LEG2.T_DEALID = deal.T_DEALID "                                                                                                                                  
                  +"    and LEG2.T_LEGKIND = 2 "                                                                                                                                            
                  +"    and LEG2.T_LEGID = 0 "                                                                                                                                               
                  +"    and exists(select H.T_RQID "                                                                                                                                           
                  +"                 from v_rqhist h, ddlrq_dbt rq "                                                                                             
                  +"                where rq.T_DOCID = deal.T_DEALID "                                                                                                   
                  +"                  and rq.T_DOCKIND = deal.T_BofficeKind "                                                                                                   
                  +"                  and H.T_RQID = RQ.T_ID "                                                                                                   
                  +"                  and h.T_PlanDate <= ? "
                  +"                  and H.T_STATE = ? "                                                                                
                  +"              ) " 
                  +" order by deal.T_DEALCODE";
                                                                                                                                           
     var cmd = DL_RSDCommand(Select);

     cmd.addParam( m_Report.MMVB_ID() );
     cmd.addParam( m_Report.EndDate() );
     cmd.addParam( DLRQ_STATE_OVERDUE );

     NRec = cmd.GetCount();
     if( NRec > 0 )
        ProgressIndicator.init(NRec, "Отбор сделок подсистемы \"БОЦБ\" для отчета", "Отбор сделок подсистемы \"БОЦБ\" для отчета");
        m_Deal = cmd.Execute();
        while( m_Deal.MoveNext() )
           if( CheckDeal() )
              PrintLineByDeal();
           end;
           ProgressIndicator.Use;
        end;
        ProgressIndicator.Remove;
     end;
   END;

END;

PRIVATE CLASS DV_NotExec_Report( Report:OBJECT )
   VAR m_Report = Report;

   PRIVATE VAR m_Deal, m_ContrClientID, m_Contractor = TContractorData(),
               m_ClientCode1, m_ClientType1, m_ClientType2, m_SecondPartType, m_LEIClientCode1, m_Code2, m_ClientCode2, m_LEIClientCode2, 
               m_ActiveCode1_2, m_ActiveType1_2, m_Com1, m_ActiveCode2_1, m_ActiveType2_1, m_Com2, m_CalcDate1, m_CalcDate2,
               m_StartComCode1, m_StartCom1, m_StartComCode2, m_StartCom2, m_PlanDate, m_Netting, m_CBR_CourseKind, m_FIID_840 = -1;

   PRIVATE VAR m_tick = TRecHandler( "dvndeal" );

   PRIVATE VAR m_CacheFinInstr = TX_CacheFinInstr();

   PRIVATE VAR DealErrMsg = TArray();

   MACRO PlanDate():Date
      return m_PlanDate;
   END;

   MACRO Netting():string
      return m_Netting;
   END;

   MACRO Rep():string
      return "";/*Для сделок БОЦБ не заполняется*/
   END;

   MACRO Com1()
      return m_Report.ВыводЧерезФормулу(m_Com1,5);
   END;

   MACRO Com2()
      return m_Report.ВыводЧерезФормулу(m_Com2,5);
   END;

   MACRO ActiveCode1_2():string
      return m_ActiveCode1_2;
   END;

   MACRO ActiveType1_2() : string
      return m_ActiveType1_2
   END;

   MACRO ActiveCode2_1():string
      return m_ActiveCode2_1;
   END;

   MACRO ActiveType2_1() : string
      return m_ActiveType2_1;
   END;

   MACRO DealCode():string
      return m_DEAL.Code;
   END;

   MACRO DealDate():DATE
      return SQL_ConvTypeDate(m_DEAL.Date);
   END;

   MACRO CalcDate1():DATE
      return m_CalcDate1;
   END;

   MACRO CalcDate2():DATE                  
      return m_CalcDate2;
   END;

   MACRO ClientCode1():string
      return m_ClientCode1;
   END;

   MACRO ClientType1() : string
      return m_ClientType1;
   END;

   MACRO ClientType2() : string
      return m_ClientType2;
   END;

   MACRO SecondPartType() : string
      return m_SecondPartType;
   END;

   /*Код <LEI> клиента, в интересах которого совершена сделка.
     Для собственных сделок поле не заполняется.*/
   MACRO LEIClientCode1():string
      return m_LEIClientCode1;
   END;

   /*Код контрагента по сделке.
     Определение вида выводимого кода см. в примечании 2.
     Если в параметрах биржевой  сделки поле КОНТРАГЕНТ не заполнено, то в А09 поле выводится полное наименование биржи.
   */
   MACRO Code2():string
      return m_Code2;
   END;

   /*Код <LEI> контрагента по сделке.
     Для биржевых сделок, у которых не заполнено поле КОНТРАГЕНТ, не заполняется
   */
   MACRO LEICode2():string
      return m_Contractor.LEICode2();
   END;

   /*Код клиента второй стороны по сделке*/
   MACRO ClientCode2():string
      return m_ClientCode2;
   END;

   MACRO LEIClientCode2():string
      return m_LEIClientCode2;
   END;

   MACRO OrgKind():string
      return m_Contractor.OrgKind();
   END;

   MACRO NR():string
      return m_Contractor.NR();
   END;

   MACRO Risk():string
      if( m_Deal.Client > 0 )
         return "С";
      end;
   END;

   MACRO Place():string
      return "2";
   END;

   MACRO OperKind():string
      var vOperKind = "";

      if( m_Deal.DVKIND == DV_FORWARD )
         vOperKind = "FORWARD";
      elif( m_Deal.DVKIND == DV_OPTION )
         vOperKind = "OPTION";
      elif( m_Deal.DVKIND == DV_PCTSWAP )
         vOperKind = "IRS";
      elif( (m_Deal.DVKIND == DV_CURSWAP) or (m_Deal.DVKIND == DV_CURSWAP_FX) )
         if( m_Deal.BA_Kind == FIKIND_METAL )/*БА которых = драг.металл*/
            vOperKind = "SWAPM";
         else
            vOperKind = "FXSWAP";
         end;
      end;

      return vOperKind;
   END;

   MACRO StartComCode1():string
      return m_StartComCode1;
   END;

   MACRO StartComCode2():string
      return m_StartComCode2;
   END;

   MACRO StartCom1()
      return m_Report.ВыводЧерезФормулу(m_StartCom1,5);
   END;

   MACRO StartCom1_m() : money
      return m_StartCom1;
   END;

   MACRO StartCom2()
      return m_Report.ВыводЧерезФормулу(m_StartCom2,5);
   END;

   MACRO StartCom2_m() : money
      return m_StartCom2;
   END;

   MACRO GetAvrCode405(FIID:integer)
     var AvrCode405 = "";
     var Select = " Select ATTR.T_NAME ActiveCode "                              
                 +"   From dobjatcor_dbt AtCor, dobjattr_dbt Attr"         
                 +"  Where AtCor.t_ObjectType = "+OBJTYPE_AVOIRISS                       
                 +"    And AtCor.t_GroupID    = "+OBJGROUP_AVRTYPE405                       
                 +"    And AtCor.t_Object     = LPAD(?, 10, '0' ) "
                 +"    And Attr.t_AttrID      = AtCor.t_AttrID "           
                 +"    And Attr.t_ObjectType  = AtCor.t_ObjectType "       
                 +"    And Attr.t_GroupID     = AtCor.t_GroupID ";          
     
     var cmd = DL_RSDCommand(Select);
     
     cmd.addParam( FIID );

     var DataSet = cmd.Execute();
     if( DataSet.MoveNext() )
        AvrCode405 = SQL_ConvTypeStr(DataSet.ActiveCode);
     end;
     if( AvrCode405 == "" )
        DealErrMsg[DealErrMsg.Size()]="Для ц/б с FIID = "+string(SQL_ConvTypeInteger(FIID))+" не задано значение категории \"Код инструмента для формы 405\"";
     end;

     return AvrCode405;
   END;

   MACRO GetMetalCode(p_FIID_Name:string)
      var MetalCode = "", v_FIID_Name = StrUpr(p_FIID_Name);

      if( v_FIID_Name == "ЗОЛОТО" )
         MetalCode = "A98";
      elif( v_FIID_Name == "СЕРЕБРО" )
         MetalCode = "A99";
      elif( v_FIID_Name == "ПЛАТИНА" )
         MetalCode = "A76";
      elif( v_FIID_Name == "ПАЛЛАДИЙ" )
         MetalCode = "A33";
      elif( v_FIID_Name == "ИРИДИЙ" )
         MetalCode = "A101";
      elif( v_FIID_Name == "РОДИЙ" )
         MetalCode = "A30";
      elif( v_FIID_Name == "РУТЕНИЙ" )
         MetalCode = "A102";
      elif( v_FIID_Name == "ОСМИЙ" )
         MetalCode = "A103";
      else
         MetalCode = "000";
      end;

      return MetalCode;
   END;

   MACRO GetActiveCode(FI_KIND:integer,FIID:integer,FI_NAME:string)
      var vActiveCode = "";
 
      if( FI_KIND == FIKIND_CURRENCY )
         vActiveCode = m_CacheFinInstr.GetISO( FIID ).m_Number;
      elif( FI_KIND == FIKIND_METAL )
         vActiveCode = GetMetalCode(FI_NAME);
      elif( FI_KIND == FIKIND_AVOIRISS )
         vActiveCode = GetAvrCode405(FIID);
      else
         vActiveCode = "#"+FI_NAME;
      end;

      return vActiveCode;
   END;

   MACRO WriteErrMsgByDeal()
      var i = 0;
      if(DealErrMsg.size() > 0 )
         while(i < DealErrMsg.size())
            m_report.AddErrMsg(DealErrMsg[i]);
            i = i + 1;
         end;
      end;
   END;

   MACRO ПолучитьДанныеСторонПоСделке()
      /*получаем данные контрагента по сделке*/     
      m_Contractor.GetPartyInfo(m_Deal.Contractor);
      m_Code2 = m_Contractor.PartyCode();

      if( (not m_Contractor.КонтрагентЗадан()) and (m_Deal.MARKETID > 0) )
        m_SecondPartType = "5";
      else
        m_SecondPartType = m_Contractor.SecondPartType();
      end;

      /*получаем данные клиента по сделке*/
      m_ClientCode1 = "";
      m_ClientType1    = IIF( (m_Deal.Client < 0) or (m_Deal.Client == {OurBank}),  "0", "" );

      if( m_Deal.Client > 0 )
         /*Для собственных сделок поле не заполняется*/
         var Client1 = TContractorData();
         Client1.GetPartyInfo(m_Deal.Client);
         m_ClientCode1 = Client1.PartyCode();
         m_ClientType1 = Client1.PartyType();
         /*Код <LEI> клиента, в интересах которого совершена сделка.
           Для собственных сделок поле не заполняется.*/
         m_LEIClientCode1 = ПолучитьКодСубъекта(m_Deal.Client, PTCK_LEI);
      end;
      /*получаем данные для клиента-контрагента*/
      m_ClientCode2 = "";
      m_ClientType2 = "";
      m_ContrClientID = -1;

      if( GetLinkedObject( 1, OBJTYPE_OUTOPER_DV, UniID(m_tick, OBJTYPE_OUTOPER_DV), OBJTYPE_PARTY, party) == 0 )
         if( ПолучитьСубъекта(party.PartyID, party) == 0 )
            m_ContrClientID = party.PartyID;
         else
            DealErrMsg[DealErrMsg.Size]= "Не могу получить субъекта (PartyID = " + string(party.PartyID) + ").";
         end;
      end;

      m_ClientType2 = IIF( m_ContrClientID < 0,  "0", "" );

      if( m_ContrClientID > 0 )
         var Client2 = TContractorData();
         Client2.GetPartyInfo(m_ContrClientID);
         m_ClientCode2 = Client2.PartyCode();
         m_ClientType2 = Client2.PartyType();
         m_LEIClientCode2 = ПолучитьКодСубъекта(m_ContrClientID, PTCK_LEI);
      end;
   END;

   /*Проверим, что сделка полностью исполнена в отчетном периоде*/
   MACRO ДатаИсполненияПоследнегоПлатежа()
      var ExecDate = date(0,0,0);

      var cmd = DL_RSDCommand( "select max(NVL(( select H1.T_DATE "
                              +"                   from dpmhist_dbt h1 "
                              +"                  where h1.t_PaymentID = PM.T_PAYMENTID "             
                              +"                    and h1.t_AutoKey = (select max(h2.t_AutoKey) "
                              +"                                          from dpmhist_dbt h2 "
                              +"                                         where h2.t_PaymentID = h1.t_PaymentID "
                              +"                                       )"
                        /*      +"                    and (h1.t_StatusIDTo >= 3000 or h1.t_StatusIDTo = 150) " */  
                              +"                    and not (h1.t_StatusIDTo >= 3000 or h1.t_StatusIDTo = 150) " 
                              +"                ), PM.T_VALUEDATE)) ExecDate "
                              +"  from dpmpaym_dbt pm "              
                              +" where PM.T_DOCKIND = ? "
                              +"   and PM.T_DOCUMENTID = ? "
                              +"   and PM.T_PURPOSE in(1,2,3,4,6,70,73)"
                             );
      cmd.addParam( DL_DVNDEAL );
      cmd.addParam( m_Deal.ID );

      var DataSet = cmd.Execute();

      if( DataSet.MoveNext() )
         ExecDate = SQL_ConvTypeDate(DataSet.ExecDate);
      end;

      return ExecDate;                                                           
   END;

   MACRO GetCalcDateByPart(DealPart:integer)

      var CalcDate = Date(0,0,0);

      var cmd = DL_RSDCommand();

      var Query = "select PM.T_VALUEDATE "
                 +"  from dpmpaym_dbt pm, dfininstr_dbt FI "              
                 +" where PM.T_DOCKIND = ? "
                 +"   and PM.T_DOCUMENTID = ? ";

      if( DealPart == 1 )
         Query = Query   
                    +"   and PM.T_PURPOSE in(1,2)";
      else
         Query = Query   
                    +"   and PM.T_PURPOSE in(3,4)";
      end;

      Query = Query   
                 +"   and FI.T_FIID = PM.T_FIID "
                 +"   and FI.T_FI_KIND = ? "
                 +" order by PM.T_VALUEDATE "+iif(DealPart==1,"asc","desc");

      cmd.addParam( DL_DVNDEAL );
      cmd.addParam( m_Deal.ID );
      cmd.addParam( FIKIND_CURRENCY );

      var DataSet = cmd.Execute( Query );

      if( DataSet.MoveNext() )
         CalcDate = SQL_ConvTypeDate(DataSet.VALUEDATE);
      end;

      return CalcDate;
   END;

   MACRO MaxPaymDateByPart(DealPart:integer)

      var CalcDate = Date(0,0,0);

      var cmd = DL_RSDCommand();

      var Query = "select PM.T_VALUEDATE "
                 +"  from dpmpaym_dbt pm, dfininstr_dbt FI "              
                 +" where PM.T_DOCKIND = ? "
                 +"   and PM.T_DOCUMENTID = ? ";

      if( DealPart == 1 )
         Query = Query   
                    +"   and PM.T_PURPOSE in(1,2)";
      else
         Query = Query   
                    +"   and PM.T_PURPOSE in(3,4)";
      end;

      Query = Query   
                 +"   and FI.T_FIID = PM.T_FIID "
                 +"   and FI.T_FI_KIND = ? "
                 +" order by PM.T_VALUEDATE desc";

      cmd.addParam( DL_DVNDEAL );
      cmd.addParam( m_Deal.ID );
      cmd.addParam( FIKIND_CURRENCY );

      var DataSet = cmd.Execute( Query );

      if( DataSet.MoveNext() )
         CalcDate = SQL_ConvTypeDate(DataSet.VALUEDATE);
      end;

      return CalcDate;
   END;
   
   MACRO GetStartCom(Kind:integer,StartCom:@variant,StartComCode:@variant)

      StartCom     = null;
      StartComCode = "";
                                             
      var cmd = DL_RSDCommand("select case FI.T_FI_KIND "
                              +"       when 2 "
                              +"       then NVL((select f.t_ISO_NUMBER from dfininstr_dbt f where f.t_FIID = FI.T_FACEVALUEFI ),chr(1)) "
                              +"       when 6 "
                              +"       then '840' "
                              +"       else FI.t_ISO_Number "
                              +"       end t_ISO_Number,    "                                                                                                                                                                          
                              +"       case FI.T_FI_KIND "
                              +"       when 2 "
                              +"       then Rsb_Secur.SC_ConvSumTypeRep(PM.T_Amount, FI.T_FIID, FI.T_FACEVALUEFI, FI.T_FACEVALUEFI, ?, ?) "
                              +"       when 6 "
                              +"       then Rsb_Secur.SC_ConvSumTypeRep(PM.T_Amount, FI.T_FIID, ?, ?, ?, ?) "
                              +"       else PM.T_Amount "
                              +"       end t_Amount    "
                              +"  from dpmpaym_dbt pm, dfininstr_dbt FI "              
                              +" where PM.T_DOCKIND = ? "         
                              +"   and PM.T_DOCUMENTID = ? "         
                              +"   and PM.T_PURPOSE in(1,2)"
                              +"   and PM.T_PAYER " +iif(Kind==1," = ? "," != ? ")                                                                                                                   
                              +"   and FI.T_FIID = PM.T_FIID "
                              +" order by PM.T_VALUEDATE "
                             );

      cmd.addParam( m_Report.TSS_CourseKind() );
      cmd.addParam( m_Report.EndDate() );

      cmd.addParam( NATCUR );
      cmd.addParam( m_FIID_840 );
      cmd.addParam( m_CBR_CourseKind );
      cmd.addParam( m_Report.EndDate() );

      cmd.addParam( DL_DVNDEAL );
      cmd.addParam( m_Deal.ID );
      cmd.addParam( iif(m_Deal.Client>0,m_Deal.Client,{OurBank}) );

      var DataSet = cmd.Execute();
      if( DataSet.MoveNext() )
         StartCom     = SQL_ConvTypeSum(DataSet.Amount)/1000;
         StartComCode = SQL_ConvTypeStr(DataSet.ISO_Number);
      end;

   END;

   MACRO ПроверитьСуммуТО(FIID:integer,Amount:money)
      return ( (Amount != null) and (((FIID == NATCUR) and (Amount >= 100000000)) or ((FIID != NATCUR) and (Amount >= 1000000))) );
   end;

   MACRO ПроверитьСуммуСделки(FIID:integer,Amount:money)
      return ( ((FIID == NATCUR) and (Amount >= 500000000)) or ((FIID != NATCUR) and (Amount >= 5000000)) );
   end;

   /*проверка и печать по поставочным сделкам: форвардам, опционам и валютным свопам, свопам*/
   MACRO CheckAndPrint_FOCS_STATE()
      var IsDealExec = false, DealExecDate = Date(0,0,0), v_ActiveCode = "";
      var СделкаИсполненаВОтчетномПериоде = false;
      var СделкаНеИсполнена = false, СделкаПодходит = false;
      var i = 0;
      var ВалютаРасчётов = IIF( m_Deal.AccFIID1 < 0, m_Deal.PriceFIID1, m_Deal.AccFIID1 );
      var NotExecStrArr = DV_NotExecStr_Array();
      var MaxPaymDate1 = date(0,0,0);

      /*Проверим, что сделка полностью исполнена в отчетном периоде*/
      MACRO СделкаИсполненаПолностью()
         var vIsDealExec = false, Query = "";
    
         DealExecDate = date(0,0,0);
    
         Query =  " select count(PM.T_PAYMENTID) NRec "
                 +"   from dpmpaym_dbt pm "
                 +" where PM.T_DOCKIND = ? "                                 
                 +" and PM.T_DOCUMENTID = ? "                                
                 +" and PM.T_PURPOSE in(1,2,3,4,6,70) "                          
                 +" and (PM.T_PAYMSTATUS < 3000 and PM.T_PAYMSTATUS != 150) ";
    
         var cmd = DL_RSDCommand(Query);

         cmd.addParam( DL_DVNDEAL );
         cmd.addParam( m_Deal.ID );

         var QuerySet = cmd.Execute();
    
         if( QuerySet.MoveNext() and (SQL_ConvTypeInteger(QuerySet.NRec) == 0) )
            /*значит исполнена и ищем дату исполнения последнего платежа*/
            vIsDealExec = true;
            DealExecDate = ДатаИсполненияПоследнегоПлатежа();
         end;
    
         return vIsDealExec;
      END;

      macro GetCommonInfoByDeal()

         ПолучитьДанныеСторонПоСделке();
         GetStartCom(1,@m_StartCom1,@m_StartComCode1);
         GetStartCom(2,@m_StartCom2,@m_StartComCode2);
         m_CalcDate1 = GetCalcDateByPart(1);
         if( (m_Deal.DVKIND == DV_CURSWAP) or (m_Deal.DVKIND == DV_CURSWAP_FX) )
            m_CalcDate2 = GetCalcDateByPart(2);
         else
            m_CalcDate2 = m_CalcDate1;
         end;
      end;

      /*сначала проверим дату исполнения сделки*/
      IsDealExec = СделкаИсполненаПолностью();

      СделкаИсполненаВОтчетномПериоде = (IsDealExec and (DealExecDate >= m_Report.BegDate()) and (DealExecDate <= m_Report.EndDate()));
      СделкаНеИсполнена = ( ((not IsDealExec) or ( DealExecDate > m_Report.EndDate() )) and 
                            (not( (m_Deal.DVKIND != DV_CURSWAP) and (m_Deal.DVKIND != DV_CURSWAP_FX) and (m_Deal.ExecDate == date(0,0,0)) ))
                          );

      m_StartComCode1 = "";
      m_StartCom1     = null;
      m_StartComCode2 = "";
      m_StartCom2     = null;
      m_Netting       = null;

      if( СделкаИсполненаВОтчетномПериоде or СделкаНеИсполнена )

         MaxPaymDate1 = MaxPaymDateByPart(1);

         СделкаПодходит = (ПроверитьСуммуСделки(SQL_ConvTypeInteger(m_Deal.PriceFIID1),SQL_ConvTypeSum(m_Deal.Cost1)) or
                           (((m_Deal.DVKIND == DV_CURSWAP) or (m_Deal.DVKIND == DV_CURSWAP_FX)) and ПроверитьСуммуСделки(SQL_ConvTypeInteger(m_Deal.PriceFIID2),SQL_ConvTypeSum(m_Deal.Cost2)))
                          );

         var cmd = DL_RSDCommand();

         var Query =  " select sum(t.t_Amount) t_Amount, t.t_DealPart, t.t_kind, t.T_VALUEDATE, t.T_FI_KIND, t.t_fiid, t.t_FI_NAME "                                                                                                    
                     +"   from ( select PM.T_FIID, PM.T_VALUEDATE, FI.T_FI_KIND, FI.t_Name t_FI_NAME, "
                     +"                 case when PM.T_VALUEDATE > ? or PM.T_PURPOSE in(3,4) then 2 else 1 end T_DealPart, "                                                                                                                    
                     +"                 case when PM.T_PAYER = ? then 1 else 2 end T_Kind, "
                     +"                 case FI.T_FI_KIND "
                     +"                 when 2            "                                                                                                                                                    
                     +"                 then Rsb_Secur.SC_ConvSumTypeRep(PM.T_Amount, FI.T_FIID, FI.T_FACEVALUEFI, ?, ?, ?) "
                     +"                 when 6            "                                                                                                                                                     
                     +"                 then Rsb_Secur.SC_ConvSumTypeRep(PM.T_Amount, FI.T_FIID, ?, ?, ?, ?) "
                     +"                 else RSB_FIInstr.ConvSum(PM.T_Amount, FI.T_FIID, ?, ?)  "                                                                                                   
                     +"                  end t_Amount,    "
                     +"                 case when (PM.T_PAYMSTATUS >= 3000 or PM.T_PAYMSTATUS = 150) "                                                                                       
                     +"                 then (select nvl(max(H2.T_DATE),PM.T_VALUEDATE)  "                                                                                       
                     +"                         from dpmhist_dbt h2                                          "                                                                                           
                     +"                        where h2.t_PaymentID = PM.T_PAYMENTID                         "                                                                                           
                     +"                          and h2.t_StatusIDTo = PM.T_PAYMSTATUS "                                                                                           
                     +"                      )                                  "                                                                                                     
                     +"                 else to_date('01.01.0001','DD.MM.YYYY') "                                                                                       
                     +"                  end t_ExecDate                  "                                                                                     
                     +"            from dpmpaym_dbt pm, dfininstr_dbt FI "                                                                                                      
                     +"           where PM.T_DOCKIND = ?                 "                                                                                                       
                     +"             and PM.T_DOCUMENTID = ?              "                                                                                                         
                     +"             and PM.T_PURPOSE in(1,2,3,4,6,70) ";

         cmd.addParam( MaxPaymDate1 );
         cmd.addParam( iif(m_Deal.Client>0,m_Deal.Client,{OurBank}) );

         cmd.addParam( ВалютаРасчётов );
         cmd.addParam( m_Report.TSS_CourseKind() );
         cmd.addParam( m_Report.EndDate() );

         cmd.addParam( NATCUR );
         cmd.addParam( ВалютаРасчётов );
         cmd.addParam( m_CBR_CourseKind );
         cmd.addParam( m_Report.EndDate() );

         cmd.addParam( ВалютаРасчётов );
         cmd.addParam( m_Report.EndDate() );
         
         cmd.addParam( DL_DVNDEAL );
         cmd.addParam( m_Deal.ID );

         if( СделкаИсполненаВОтчетномПериоде )
            Query =  Query
                        +"          and PM.T_VALUEDATE < ? ";                                                                                                         
            cmd.addParam( m_Report.BegDate() );
         else
            Query =  Query
                        +"          and PM.T_VALUEDATE <= ? ";                                                                                                         
            cmd.addParam( m_Report.EndDate() );
         end;

         Query =  Query
                     +"             and FI.T_FIID = PM.T_FIID "                                                                                                     
                     +"          order by PM.T_VALUEDATE) t ";

         if( СделкаНеИсполнена )
            Query =  Query
                     +"  where (t.t_ExecDate = to_date('01.01.0001','DD.MM.YYYY') or t.t_ExecDate > ?) ";                                                                  
            cmd.addParam( m_Report.EndDate() );
         else/*СделкаИсполненаВОтчетномПериоде*/
            Query =  Query
                     +"  where t.t_ExecDate > t.T_VALUEDATE "                                                                  
                     +"    and ( ( t.t_ExecDate >= to_date('15.'||to_char(extract(month from ( t.T_VALUEDATE)))||'.'||to_char(extract(year from (t.T_VALUEDATE))),'DD.MM.YYYY') "
                     +"           and t.T_VALUEDATE <= to_date('14.'||to_char(extract(month from ( t.T_VALUEDATE)))||'.'||to_char(extract(year from (t.T_VALUEDATE))),'DD.MM.YYYY') "
                     +"           and t.T_VALUEDATE >= to_date('01.'||to_char(extract(month from ( t.T_VALUEDATE)))||'.'||to_char(extract(year from (t.T_VALUEDATE))),'DD.MM.YYYY') "
                     +"          ) or "
                     +"          ( t.t_ExecDate > LAST_DAY(t.T_VALUEDATE) "
                     +"           and t.T_VALUEDATE >= to_date('15.'||to_char(extract(month from ( t.T_VALUEDATE)))||'.'||to_char(extract(year from (t.T_VALUEDATE))),'DD.MM.YYYY') "
                     +"           and t.T_VALUEDATE <= LAST_DAY(t.T_VALUEDATE) "
                     +"          )"
                     +"        ) ";                                                                  
         end;

         Query =  Query
                     +" group by t.T_DealPart, t.T_VALUEDATE, t.t_kind, t.T_FI_KIND, t.t_fiid, t.t_FI_NAME "                                                                                                    
                     +" order by t.T_DealPart, t.T_VALUEDATE, t.t_kind, t.T_FI_KIND, t.t_fiid";

         NotExecStrArr.ClearArray();

         if( cmd.GetCount(Query) )
            
            var DataSet = cmd.Execute(Query);
            while( DataSet.MoveNext() )

               if( not СделкаПодходит )
                 if( ПроверитьСуммуТО(SQL_ConvTypeInteger(ВалютаРасчётов),SQL_ConvTypeSum(DataSet.Amount)) )
                    СделкаПодходит = true;
                 end;
               end;

               var v_ActiveType = "";
               if(СделкаИсполненаВОтчетномПериоде)
                  v_ActiveCode = "";
               else
                  v_ActiveCode = GetActiveCode(DataSet.FI_KIND,DataSet.FIID,DataSet.FI_NAME);
                  v_ActiveType = GetActiveType(DataSet.FI_KIND);
               end;

               NotExecStrArr.Add(SQL_ConvTypeInteger(DataSet.DealPart),
                                 SQL_ConvTypeInteger(DataSet.Kind),
                                 SQL_ConvTypeDate(DataSet.VALUEDATE),
                                 iif(СделкаИсполненаВОтчетномПериоде,$0,SQL_ConvTypeSum(DataSet.Amount)/1000),
                                 v_ActiveCode,
                                 v_ActiveType
                                );
            end;

            if(СделкаПодходит)
               if(NotExecStrArr.Size() > 0)
                  GetCommonInfoByDeal();
                  WriteErrMsgByDeal();
                  i = 0;
                  while( i < NotExecStrArr.Size() )
                     m_PlanDate      = NotExecStrArr[i].PlanDate;
                     m_Com1          = NotExecStrArr[i].Com1;  
                     m_ActiveCode1_2 = NotExecStrArr[i].ActiveCode1;
                     m_ActiveType1_2 = NotExecStrArr[i].ActiveType1;
                     m_Com2          = NotExecStrArr[i].Com2;  
                     m_ActiveCode2_1 = NotExecStrArr[i].ActiveCode2;
                     m_ActiveType2_1 = NotExecStrArr[i].ActiveType2;
                     m_Report.PrintTableLine(this);
                     i = i + 1;
                  end;

                  m_Report.PrintTableLineKliko(this, NotExecStrArr);

               end;
            end;

         end;

      end;

   END;

   /*проверка и печать по расчетным сделкам: форвардам, опционам и валютным свопам, и поставочным Опционам на драг металл и артикул и поставоч Форвардам на артикул*/
   MACRO CheckAndPrint_FOCS_CALC()
      var IsDealExec = false, DealExecDate = Date(0,0,0), v_ActiveCode = "";
      var СделкаИсполненаВОтчетномПериоде = false;
      var СделкаНеИсполнена = false, СделкаПодходит = false;
      var ВалютаРасчётов = m_Deal.PriceFIID1;
      var i = 0;
      var NotExecStrArr = DV_NotExecStr_Array();
      var MaxPaymDate1 = date(0,0,0);
      var MarginSum = m_Deal.Cost2 - m_Deal.Cost1;

      /*Проверим, что сделка полностью исполнена в отчетном периоде*/
      MACRO СделкаИсполненаПолностью()
         var vIsDealExec = false, Query = "", vCount = 0;
    
         DealExecDate = date(0,0,0);
    
         Query =  " SELECT step.t_Fact_Date "                                               
                 +"   FROM doprstep_dbt step, doproper_dbt oper "                           
                 +"  WHERE oper.t_DocKind      = ? AND "                                    
                 +"        oper.t_DocumentID   = ? AND "                                    
                 +"        step.t_ID_Operation = oper.t_ID_Operation AND "                  
                 +"        (step.t_Symbol = 'и' or step.t_Symbol = 'И' or step.t_Symbol = 'b') AND "               
                 +"        step.t_IsExecute    = 'X' "                                        
                 +" order by step.t_Fact_Date";                                               
    
         var cmd = DL_RSDCommand(Query);

         cmd.addParam( DL_DVNDEAL );
         cmd.addParam( UniID(m_tick, OBJTYPE_OUTOPER_DV) );

         var QuerySet = cmd.Execute();

         while( QuerySet.MoveNext())
            if( vIsDealExec )                                                                   
               DealExecDate = SQL_ConvTypeDate(QuerySet.Fact_Date);
            else                                                                           
               DealExecDate = SQL_ConvTypeDate(QuerySet.Fact_Date);
            end;                                                                           
            vIsDealExec = true;
            vCount = vCount + 1;
         end;

         if( (m_Deal.DVKIND == DV_CURSWAP) and (vCount <= 1) )
            DealExecDate = date(0,0,0);
            vIsDealExec = false;
         end;

         return vIsDealExec;
      END;

      MACRO GetStartCom_Calc(Kind:integer,StartCom:@variant,StartComCode:@variant)
     
         StartCom     = null;
         StartComCode = "";
                                          
         var cmd = DL_RSDCommand( "select FI.t_ISO_Number, PM.T_Amount "
                                 +"  from dpmpaym_dbt pm, dfininstr_dbt FI "              
                                 +" where PM.T_DOCKIND = ? "         
                                 +"   and PM.T_DOCUMENTID = ? "         
                                 +"   and PM.T_PURPOSE in(6,70) "
                                 +"   and PM.T_PAYER " +iif(Kind==1," = ? "," != ? ")                                                                                                                   
                                 +"   and FI.T_FIID = PM.T_FIID "
                                 +" order by PM.T_VALUEDATE, PM.T_PaymentID "
                                );
     
         cmd.addParam( DL_DVNDEAL );
         cmd.addParam( m_Deal.ID );
         cmd.addParam( iif(m_Deal.Client>0,m_Deal.Client,{OurBank}) );
     
         var DataSet = cmd.Execute();
         if( DataSet.MoveNext() )
            StartCom     = SQL_ConvTypeSum(DataSet.Amount)/1000;
            StartComCode = SQL_ConvTypeStr(DataSet.ISO_Number);
         end;
     
      END;

      MACRO GetFactMarginCom_Calc(Kind:integer)
         var FactCom = $0;
                                          
         var cmd = DL_RSDCommand( "select nvl(sum(RSB_FIInstr.ConvSum(PM.T_Amount, PM.T_FIID, ?, ?)),0) t_Amount "
                                 +"  from dpmpaym_dbt pm, dfininstr_dbt FI "              
                                 +" where PM.T_DOCKIND = ? "         
                                 +"   and PM.T_DOCUMENTID = ? "         
                                 +"   and PM.T_PURPOSE = 70 "
                                 +"   and PM.T_PAYER " +iif(Kind==1," = ? "," != ? ")                                                                                                                   
                                 +"   and FI.T_FIID = PM.T_FIID "
                                 +"   and (PM.T_PAYMSTATUS >= 3000 or PM.T_PAYMSTATUS = 150)"
                                 +"   and PM.T_VALUEDATE <= ? "                                                                                                         
                                 +"   and (select nvl(max(H2.T_DATE),PM.T_VALUEDATE)  "                                                                                       
                                 +"          from dpmhist_dbt h2                                          "                                                                                           
                                 +"         where h2.t_PaymentID = PM.T_PAYMENTID                         "                                                                                           
                                 +"           and h2.t_StatusIDTo = PM.T_PAYMSTATUS                       "                                                                                           
                                 +"       ) != to_date('01.01.0001','DD.MM.YYYY')                         "                                                                                                     
                                 +"   and (select nvl(max(H2.T_DATE),PM.T_VALUEDATE)  "                                                                                       
                                 +"          from dpmhist_dbt h2                                          "                                                                                           
                                 +"         where h2.t_PaymentID = PM.T_PAYMENTID                         "                                                                                           
                                 +"           and h2.t_StatusIDTo = PM.T_PAYMSTATUS                       "                                                                                           
                                 +"       ) <= ?                                                          "                                                                                                     
                                );
     
         cmd.addParam( ВалютаРасчётов );
         cmd.addParam( m_Report.EndDate() );
         cmd.addParam( DL_DVNDEAL );
         cmd.addParam( m_Deal.ID );
         cmd.addParam( iif(m_Deal.Client>0,m_Deal.Client,{OurBank}) );
         cmd.addParam( m_Report.EndDate() );
         cmd.addParam( m_Report.EndDate() );
     
         var DataSet = cmd.Execute();
         if( DataSet.MoveNext() )
            FactCom = SQL_ConvTypeSum(DataSet.Amount)/1000;
         end;

         return FactCom;
      END;

      macro GetCommonInfoByDeal()

         ПолучитьДанныеСторонПоСделке();

         GetStartCom_Calc(1,@m_StartCom1,@m_StartComCode1);
         GetStartCom_Calc(2,@m_StartCom2,@m_StartComCode2);

         if( (m_Deal.DVKIND == DV_CURSWAP) and (m_StartCom1 == null) and (m_StartCom2 == null) )

            if( MarginSum < 0 )
               m_StartCom1 = abs(MarginSum)/1000;
               m_StartComCode1 = GetActiveCode(FIKIND_CURRENCY,ВалютаРасчётов);
            end;


            if( MarginSum > 0 )
               m_StartCom2 = abs(MarginSum)/1000;
               m_StartComCode2 = GetActiveCode(FIKIND_CURRENCY,ВалютаРасчётов);
            end;

         end;

         if( m_StartCom1 == null )
            m_StartCom1 = $0;
         end;

         if( m_StartCom2 == null )
            m_StartCom2 = $0;
         end;

         m_CalcDate1 = SQL_ConvTypeDate(m_Deal.ExecDate);
         if( m_Deal.DVKIND == DV_CURSWAP )
            m_CalcDate2 = max(m_Deal.SuplDate_2,m_Deal.PayDate_2);
         else
            m_CalcDate2 = m_CalcDate1;
         end;

      end;

      /*сначала проверим дату исполнения сделки*/
      IsDealExec = СделкаИсполненаПолностью();

      СделкаИсполненаВОтчетномПериоде = (IsDealExec and (DealExecDate >= m_Report.BegDate()) and (DealExecDate <= m_Report.EndDate()));
      СделкаНеИсполнена = (((not IsDealExec) or ( DealExecDate > m_Report.EndDate() )) and (not((m_Deal.DVKIND != DV_CURSWAP) and (m_Deal.ExecDate == date(0,0,0)))));

      m_StartComCode1 = "";
      m_StartCom1     = null;
      m_StartComCode2 = "";
      m_StartCom2     = null;
      m_Netting       = null;

      if( СделкаИсполненаВОтчетномПериоде or СделкаНеИсполнена )

         MaxPaymDate1 = max(SQL_ConvTypeDate(m_Deal.SuplDate_1),SQL_ConvTypeDate(m_Deal.PayDate_1));

         var cmd = DL_RSDCommand();
         var purpose = "";
         if (m_Deal.Bonus > 0)
            purpose = "                      and PM.T_PURPOSE in(6,70)                                                ";
         end;
         /*По факту для форвардов и свопов здесь будет отобрано не более одного просроченного платежа по марже. 
           А для опциона кроме просроченного платежа по марже может присутствовать еще и просроченный платеж по премии
          */
         var Query =  "          select sum(t.t_Amount) t_Amount, t.t_kind, t.T_VALUEDATE, t.t_fiid, SUM (t.t_AmountNett) t_AmountNett, t.t_IsNett "                                                                                                    
                     +"            from ( select PM.T_FIID, PM.T_VALUEDATE, "                                                                                                                                                                                                                                                                                                                                                
                     +"                          case when ( CASE WHEN NVL (PM1.T_Amount, 0) <> 0 THEN PM1.T_PAYER ELSE PM.T_PAYER END ) = ? then 1 else 2 end T_Kind, "                                                                                                                    
                     +"                          RSB_FIInstr.ConvSum( CASE WHEN NVL (CASE WHEN ? = 0 THEN PM1.T_Amount ELSE 0 END, 0) <> 0 THEN  0 ELSE PM.T_Amount END, PM.T_FIID, ?, ?) t_Amount,    " 
                     +"                          CASE WHEN ? = 0 THEN NVL (PM1.T_Amount, 0) ELSE 0 END  t_AmountNett, "                                                                                                                                                                        
                     +"                          CASE WHEN NVL(PM1.T_Amount, 0) <> 0 THEN 1 ELSE 0 END  t_IsNett,     "                                                                                                                                                                        
                     +"                          case when (PM.T_PAYMSTATUS >= 3000 or PM.T_PAYMSTATUS = 150)         "                                                                                       
                     +"                          then (select nvl(max(H2.T_DATE),PM.T_VALUEDATE)  "                                                                                       
                     +"                                  from dpmhist_dbt h2                                          "                                                                                           
                     +"                                 where h2.t_PaymentID = PM.T_PAYMENTID                         "                                                                                           
                     +"                                   and h2.t_StatusIDTo = PM.T_PAYMSTATUS                       "                                                                                           
                     +"                               )                                                               "                                                                                                     
                     +"                          else to_date('01.01.0001','DD.MM.YYYY')                              "                                                                                       
                     +"                           end t_ExecDate                                                      "                                                                                     
                     +"                     from dpmpaym_dbt pm, dpmlink_dbt lnk, dpmpaym_dbt pm1                     "                                                                                                      
                     +"                    where PM.T_DOCKIND = ?                                                     "                                                                                                       
                     +"                      and PM.T_DOCUMENTID = ?                                                  "
                     +"                      AND lnk.t_initialpayment(+) = PM.T_PAYMENTID                             "
                     +"                      AND pm1.t_paymentID(+) = lnk.t_purposepayment                            "                                                                            
                     +"                      AND pm1.T_PAYMSTATUS(+) = 0                                              "                                                                            
                     +purpose;

         cmd.addParam( iif(m_Deal.Client>0,m_Deal.Client,{OurBank}) );
         cmd.addParam( m_Deal.ExecType );
         cmd.addParam( ВалютаРасчётов );
         cmd.addParam( m_Report.EndDate() );
         cmd.addParam( m_Deal.ExecType );

         cmd.addParam( DL_DVNDEAL );
         cmd.addParam( m_Deal.ID );
                                                                                                         
         if( СделкаИсполненаВОтчетномПериоде )
            Query =  Query
                        +"                      and PM.T_VALUEDATE < ? ";                                                                                                         
            cmd.addParam( m_Report.BegDate() );
         else
            Query =  Query
                        +"                      and PM.T_VALUEDATE <= ? ";                                                                                                         
            cmd.addParam( m_Report.EndDate() );
         end;

         Query =  Query
                     +"                     order by PM.T_VALUEDATE) t ";

         if( СделкаНеИсполнена )
            Query =  Query
                     +"           where (t.t_ExecDate = to_date('01.01.0001','DD.MM.YYYY') or t.t_ExecDate > ?) ";                                                                  
            cmd.addParam( m_Report.EndDate() );
         else/*СделкаИсполненаВОтчетномПериоде*/
            Query =  Query
                     +"  where t.t_ExecDate > t.T_VALUEDATE "                                                                  
                     +"    and ( ( t.t_ExecDate >= to_date('15.'||to_char(extract(month from ( t.T_VALUEDATE)))||'.'||to_char(extract(year from (t.T_VALUEDATE))),'DD.MM.YYYY') "
                     +"           and t.T_VALUEDATE <= to_date('14.'||to_char(extract(month from ( t.T_VALUEDATE)))||'.'||to_char(extract(year from (t.T_VALUEDATE))),'DD.MM.YYYY') "
                     +"           and t.T_VALUEDATE >= to_date('01.'||to_char(extract(month from ( t.T_VALUEDATE)))||'.'||to_char(extract(year from (t.T_VALUEDATE))),'DD.MM.YYYY') "
                     +"          ) or "
                     +"          ( t.t_ExecDate > LAST_DAY(t.T_VALUEDATE) "
                     +"           and t.T_VALUEDATE >= to_date('15.'||to_char(extract(month from ( t.T_VALUEDATE)))||'.'||to_char(extract(year from (t.T_VALUEDATE))),'DD.MM.YYYY') "
                     +"           and t.T_VALUEDATE <= LAST_DAY(t.T_VALUEDATE) "
                     +"          )"
                     +"        ) ";                                                                  
         end;

         Query =  Query
                     +" group by t.T_VALUEDATE, t.t_kind, t.t_fiid, t.t_IsNett"                                                                                                    
                     +" order by t.T_VALUEDATE, t.t_kind, t.t_fiid, t.t_IsNett";

         NotExecStrArr.ClearArray();

         if( cmd.GetCount(Query) > 0 )

            var DataSet = cmd.Execute(Query);

            while( DataSet.MoveNext() )
               if( (not СделкаПодходит) AND (SQL_ConvTypeSum(DataSet.AmountNett) > 0))
                 if( ПроверитьСуммуТО( SQL_ConvTypeInteger(DataSet.FIID) ,SQL_ConvTypeSum(DataSet.AmountNett)) )
                    СделкаПодходит = true;
                 end;
               end;
               if( not СделкаПодходит )
                 if( ПроверитьСуммуТО(ВалютаРасчётов,SQL_ConvTypeSum(DataSet.Amount)) )
                    СделкаПодходит = true;
                 end;
               end;

               var v_ActiveType = "";
               if(СделкаИсполненаВОтчетномПериоде)
                  v_ActiveCode = "";
               else
                  v_ActiveCode = GetActiveCode(FIKIND_CURRENCY,DataSet.FIID);
                  v_ActiveType = GetActiveType(FIKIND_CURRENCY);
               end;

               NotExecStrArr.Add(0,
                                 SQL_ConvTypeInteger(DataSet.Kind),
                                 SQL_ConvTypeDate(DataSet.VALUEDATE),
                                 iif(СделкаИсполненаВОтчетномПериоде,$0, IIF( SQL_ConvTypeSum(DataSet.AmountNett) > 0,SQL_ConvTypeSum(DataSet.AmountNett)/1000 , SQL_ConvTypeSum(DataSet.Amount)/1000)),
                                 v_ActiveCode,
                                 v_ActiveType
                                );
            end;

            if(СделкаПодходит and ( (m_Deal.DVKIND != DV_CURSWAP) or ((m_Deal.DVKIND == DV_CURSWAP) AND (SQL_ConvTypeInteger(DataSet.IsNett) != 0) ) ) )
               if(NotExecStrArr.Size() > 0)
                  GetCommonInfoByDeal();
                  WriteErrMsgByDeal();
                  i = 0;
                  while( i < NotExecStrArr.Size() )
                     m_PlanDate      = NotExecStrArr[i].PlanDate;
                     m_Com1          = NotExecStrArr[i].Com1;  
                     m_ActiveCode1_2 = NotExecStrArr[i].ActiveCode1;
                     m_ActiveType1_2 = NotExecStrArr[i].ActiveType1;
                     m_Com2          = NotExecStrArr[i].Com2;  
                     m_ActiveCode2_1 = NotExecStrArr[i].ActiveCode2;
                     m_ActiveType2_1 = NotExecStrArr[i].ActiveType2;
                     m_Netting       = IIF((SQL_ConvTypeInteger(DataSet.IsNett) != 0), "N", "")  ;
                     m_Report.PrintTableLine(this);
                     i = i + 1;
                  end;

                  m_Report.PrintTableLineKliko(this, NotExecStrArr);

               end;
            end;

         end;

         if( m_Deal.DVKIND == DV_CURSWAP )
            /*Если по расчетному Валютному СВОПу нет платежей по вар.марже, а по остальным условиям отбора сделка попадает в отчет, 
              то по сделке рассчитывается <Расчётная маржа>, как разность между суммами контрактива по 2 и 1 части
            */
            m_Com1          = null;
            m_ActiveCode1_2 = "";
            m_ActiveType1_2 = "";
            m_Com2          = null;
            m_ActiveCode2_1 = "";
            m_ActiveType2_1 = "";

            GetCommonInfoByDeal();
            m_PlanDate = m_CalcDate2;
                      
            if( ПроверитьСуммуТО(ВалютаРасчётов,abs(MarginSum)) and
                ( (NotExecStrArr.Size() > 0) or
                  (СделкаИсполненаВОтчетномПериоде and (m_CalcDate2 < DealExecDate) and (m_CalcDate2 < m_Report.BegDate())) or 
                  (СделкаНеИсполнена and (m_CalcDate2 <= m_Report.EndDate()) )
                )
              )
               var FactMargin = $0;
               if( MarginSum < 0 )
                  if( СделкаНеИсполнена )
                     m_Com1 = abs(MarginSum)/1000;
                     m_ActiveCode1_2 = GetActiveCode(FIKIND_CURRENCY,ВалютаРасчётов);
                     m_ActiveType1_2 = GetActiveType(FIKIND_CURRENCY);
                  else
                     m_Com1 = $0;  
                  end;
               end;

               if( СделкаНеИсполнена )

                  if( (NotExecStrArr.Size() > 0) and (NotExecStrArr[0].Com1 != null) and (NotExecStrArr[0].Com1 > 0) and ( (m_Com1==null) or (NotExecStrArr[0].Com1 > m_Com1) ) )
                     m_PlanDate      = NotExecStrArr[i].PlanDate;
                     m_Com1          = NotExecStrArr[i].Com1;
                     m_ActiveCode1_2 = GetActiveCode(FIKIND_CURRENCY,ВалютаРасчётов);
                     m_ActiveType1_2 = GetActiveType(FIKIND_CURRENCY);
                  elif( m_Com1 !=null )
                     FactMargin = GetFactMarginCom_Calc(1);
                     if(FactMargin > $0) 
                        if(FactMargin <= m_Com1 )
                           m_Com1 = m_Com1 - FactMargin;
                        else
                           m_Com1          = null;
                           m_ActiveCode1_2 = "";
                           m_ActiveType1_2 = "";
                        end;
                     end;
                  end;

               end;
               
               if( MarginSum > 0 )
                  if( СделкаНеИсполнена )
                     m_Com2 = abs(MarginSum)/1000;
                     m_ActiveCode2_1 = GetActiveCode(FIKIND_CURRENCY,ВалютаРасчётов);
                  else
                     m_Com2 = $0;  
                  end;
               end;

               if( СделкаНеИсполнена )

                  if( (NotExecStrArr.Size() > 0) and (NotExecStrArr[0].Com2 != null) and (NotExecStrArr[0].Com2 > 0) and ( (m_Com2==null) or (NotExecStrArr[0].Com2 > m_Com2) ) )
                     m_PlanDate      = NotExecStrArr[i].PlanDate;
                     m_Com2 = NotExecStrArr[i].Com2;
                     m_ActiveCode2_1 = GetActiveCode(FIKIND_CURRENCY,ВалютаРасчётов);
                  elif( m_Com2 !=null )
                     FactMargin = GetFactMarginCom_Calc(2);
                     if(FactMargin > $0) 
                        if(FactMargin <= m_Com2 )
                           m_Com2 = m_Com2 - FactMargin;
                        else
                           m_Com2          = null;
                           m_ActiveCode2_1 = "";
                        end;
                     end;
                  end;

               end;

               if( not((m_Com1 == null) and (m_Com2 == null)) )
                  WriteErrMsgByDeal();
                  m_Report.PrintTableLine(this);
                  m_Report.PrintTableLineKliko(this, null);
               end;

            end;

         end;

      end;

   END;

   /*проверка и печать по %свопу (и поставочному и расчетному)*/
   MACRO CheckAndPrintDataByPCTSWAP()
     var IsDealExec = false, DealExecDate = Date(0,0,0), NRec = 0;
     var СделкаИсполненаВОтчетномПериоде = false, СделкаНеИсполнена = false;
     var СделкаПодходит = false;
     var NotExecStrArr = DV_NotExecStr_Array();
     var i = 0;

     /*Проверим, что сделка полностью исполнена в отчетном периоде*/
     MACRO СделкаИсполненаПолностью()
        var vIsDealExec = false, Query = "";
    
        DealExecDate = date(0,0,0);
    
        Query =  "select count(DV.T_ID) NRec "
                +"  from DV_DVNPMGR dv "
                +" where dv.t_Type     = 3 "
                +"   and dv.t_DealID   = "+m_Deal.ID 
                +"   and DV.T_STATUS   = 1";  

        var Querycmd = DL_RSDCommand(Query);
    
        var QuerySet = Querycmd.Execute();
    
        if( QuerySet.MoveNext() and (SQL_ConvTypeInteger(QuerySet.NRec) == 0) )
           /*значит исполнена и ищем дату исполнения последнего платежа*/
           vIsDealExec = true;
           DealExecDate = ДатаИсполненияПоследнегоПлатежа();
        end;
    
        return vIsDealExec;
     END;

     MACRO GetCalcDate1()
       var CalcDate1 = "";
       var Query = " select min(nvl(DV.T_PAYDATE, to_date('01.01.0001', 'DD.MM.YYYY'))) PAYDATE "+
                   "   from DV_DVNPMGR dv "+                                                                                                            
                   "  where dv.t_DealID = ? ";                                                                                                                        
    
        var QueryCmd = DL_RSDCommand(Query);
        QueryCmd.addParam( m_Deal.ID );
    
        var QuerySet = QueryCmd.Execute();
        if( QuerySet.MoveNext() and ( SQL_ConvTypeDate(QuerySet.PAYDATE) != date(0,0,0) ) )
           CalcDate1 = SQL_ConvTypeDate(QuerySet.PAYDATE);
        end;
        return CalcDate1;
     END;
    
     MACRO GetCalcDate2()
       var CalcDate2 = "";
       var Query = " select max(nvl(DV.T_PAYDATE, to_date('01.01.0001', 'DD.MM.YYYY'))) PAYDATE "+
                   "   from DV_DVNPMGR dv "+                                                                                                            
                   "  where dv.t_DealID = ? ";                                                                                                                        
    
        var QueryCmd = DL_RSDCommand(Query);
        QueryCmd.addParam( m_Deal.ID );
    
        var QuerySet = QueryCmd.Execute();
        if( QuerySet.MoveNext() and ( SQL_ConvTypeDate(QuerySet.PAYDATE) != date(0,0,0) ) )
           CalcDate2 = SQL_ConvTypeDate(QuerySet.PAYDATE);
        end;
        return CalcDate2;
     END;

     MACRO GetStartCom_State(Side:integer)

       var Query =  " select DV.T_PAYDATE, dv.t_AMOUNT, (select fi.t_ISO_Number from dfininstr_dbt fi where fi.t_fi_kind = 1 and fi.t_fi_code = dv.t_PAYMENTCURRENCY) ISO_Code "
                   +"   from DV_DVNPMGR dv "                                                                                                            
                   +"  where dv.t_DealID = ? "
                   +"    and DV.T_SIDE  = ? "
                   +" order by DV.T_PAYDATE";                                                                                                                        
    
       var QueryCmd = DL_RSDCommand(Query);
       QueryCmd.addParam( m_Deal.ID );
       QueryCmd.addParam( Side );
    
       var QuerySet = QueryCmd.Execute();
       if( QuerySet.MoveNext() )
          if( Side == ALG_DV_PMGR_SIDE_LIABILITY )
             m_StartCom1     = QuerySet.Amount/1000;
             m_StartComCode1 = QuerySet.ISO_Code;
          else
             m_StartCom2     = QuerySet.Amount/1000;
             m_StartComCode2 = QuerySet.ISO_Code;
          end;
       end;
    
     END;

     MACRO GetStartCom_Calc(Side:integer)

       var Query =  " select DV.T_PAYDATE, dv.t_AMOUNT, (select fi.t_ISO_Number from dfininstr_dbt fi where fi.t_fi_kind = 1 and fi.t_fi_code = dv.t_PAYMENTCURRENCY) ISO_Code "
                   +"   from DV_DVNPMGR dv "                                                                                                            
                   +"  where dv.t_DealID = ? "
                   +" order by DV.T_PAYDATE";                                                                                                                        
    
       var QueryCmd = DL_RSDCommand(Query);
       QueryCmd.addParam( m_Deal.ID );

       var QuerySet = QueryCmd.Execute();
       if( QuerySet.MoveNext() )
          if( Side == ALG_DV_PMGR_SIDE_LIABILITY )
             if( QuerySet.Amount < 0 )
                m_StartCom1     = abs(QuerySet.Amount)/1000;
                m_StartComCode1 = QuerySet.ISO_Code;
             end;
          else
             if( QuerySet.Amount > 0 )
               m_StartCom2     = abs(QuerySet.Amount)/1000;
               m_StartComCode2 = QuerySet.ISO_Code;
             end;
          end;
       end;
    
     END;

     MACRO GetStartCom(Side:integer)
        if( m_Deal.ExecType == DVSETTLEMET_STATE )
           GetStartCom_State(Side);
        else
           GetStartCom_Calc(Side);
        end;
     END;

     MACRO GetNotExecComFromPmGr()
       var Select =  " select nvl(sum(t.t_AMOUNT),0) t_AMOUNT, t.T_PAYDATE, t.T_SIDE, t.t_FIID "
                    +"   from (select sum(t.t_AMOUNT) t_AMOUNT, t.T_PAYDATE, t.T_SIDE, t.t_FIID "                                              
                    +"           from (select abs(dv.t_AMOUNT) t_Amount, DV.T_PAYDATE, "
                    +iif(m_Deal.ExecType == DVSETTLEMET_STATE," DV.T_SIDE "," (case when DV.T_AMOUNT > 0 then 2 else 1 end) ")+" T_SIDE,"
                    +"                        (select fi.t_FIID from dfininstr_dbt fi where fi.t_fi_kind = 1 and fi.t_fi_code = dv.t_PAYMENTCURRENCY) t_FIID "                                                                       
                    +"                   from DV_DVNPMGR dv "                                                                                                            
                    +"                  where dv.t_DealID = ? "                                                                                                 
                    +"                    and DV.T_STATUS = 1 "
                    +"                    and DV.T_PAYDATE <= ? "
                    +"                    and exists( select * " 
                    +"                                  from dmccateg_dbt mccat, dmcaccdoc_dbt accdoc "
                    +"                                 where (accdoc.t_Account = dv.t_DemandAccount or accdoc.t_Account = dv.t_LiabilityAccount) "
                    +"                                   and accdoc.t_DocKind = ? "
                    +"                                   and mccat.t_ID = accdoc.t_CatID "
                    +"                                   and mccat.t_Code in(?,?) "
                    +"                                   and NVL (rsb_account.restall (accdoc.t_Account, accdoc.t_Chapter, accdoc.t_Currency, ?), 0) <> 0) "
                    +"                 order by DV.T_PAYDATE, DV.T_PAYMENTCURRENCY) t "
                    +"         group by t.T_PAYDATE, t.T_SIDE, t.t_FIID "                                                                                                                        
                    +"          union all "
                    +"         select sum(t.t_Amount) t_Amount, t.T_PAYDATE, t.T_SIDE, t.t_fiid "                                                                                                    
                    +"           from ( select PM.T_FIID, PM.T_VALUEDATE T_PAYDATE, "                                                                                                                                                                                                                                                                                                                                                
                    +"                         case when PM.T_PAYER = ? then 1 else 2 end T_SIDE, "                                                                                                                    
                    +"                         PM.T_Amount t_Amount,    "                                                                                                                                                                          
                    +"                         case when (PM.T_PAYMSTATUS >= 3000 or PM.T_PAYMSTATUS = 150)         "                                                                                       
                    +"                         then (select nvl(max(H2.T_DATE),to_date('01.01.0001','DD.MM.YYYY'))  "                                                                                       
                    +"                                 from dpmhist_dbt h2                                          "                                                                                           
                    +"                                where h2.t_PaymentID = PM.T_PAYMENTID                         "                                                                                           
                    +"                                  and h2.t_StatusIDTo = PM.T_PAYMSTATUS                       "                                                                                           
                    +"                              )                                                               "                                                                                                     
                    +"                         else to_date('01.01.0001','DD.MM.YYYY')                              "                                                                                       
                    +"                          end t_ExecDate                                                      "                                                                                     
                    +"                    from dpmpaym_dbt pm                                                       "                                                                                                      
                    +"                   where PM.T_DOCKIND = ?                                                     "                                                                                                       
                    +"                     and PM.T_DOCUMENTID = ?                                                  "                                                                                                         
                    +"                     and PM.T_PURPOSE = 70                                                    "
                    +"                     and PM.T_VALUEDATE <= ? "                                                                                                         
                    +"                 ) t "
                    +"                   where (t.t_ExecDate = to_date('01.01.0001','DD.MM.YYYY') or t.t_ExecDate > ?) "                                                                  
                    +"         group by t.T_PAYDATE, t.T_SIDE, t.t_FIID "
                    +"       ) t"
                    +" group by t.T_PAYDATE, t.T_SIDE, t.t_FIID "                                                             
                    +" order by t.T_PAYDATE, t.T_SIDE, t.t_FIID";

        var QueryCmd = DL_RSDCommand(Select);

        QueryCmd.addParam( m_Deal.ID );
        QueryCmd.addParam( m_Report.EndDate() );

        Querycmd.addParam( DL_DVNDEAL );
        Querycmd.addParam( "Треб. с н.с." );
        Querycmd.addParam( "Обяз. с н.с." );
        Querycmd.addParam( m_Report.BegDate() - 1 );

        QueryCmd.addParam( iif(m_Deal.Client>0,m_Deal.Client,{OurBank}) );

        QueryCmd.addParam( DL_DVNDEAL );
        QueryCmd.addParam( m_Deal.ID );

        QueryCmd.addParam( m_Report.EndDate() );
        QueryCmd.addParam( m_Report.EndDate() );
    
        var QuerySet = QueryCmd.Execute();

        while( QuerySet.MoveNext() )

           if( not СделкаПодходит )
             if( ПроверитьСуммуТО(SQL_ConvTypeInteger(QuerySet.FIID),SQL_ConvTypeSum(QuerySet.Amount)) )
                СделкаПодходит = true;
             end;
           end;

           NotExecStrArr.Add(0,
                             SQL_ConvTypeInteger(QuerySet.SIDE),
                             SQL_ConvTypeDate(QuerySet.PAYDATE),
                             SQL_ConvTypeSum(QuerySet.Amount)/1000,
                             GetActiveCode(FIKIND_CURRENCY,QuerySet.FIID),
                             GetActiveType(FIKIND_CURRENCY)
                            );

        end;
     END;

     MACRO GetNotExecComFromPmHist()

        var Query =  " select t.T_VALUEDATE, NVL(sum(t.t_Amount),0) t_Amount, t.t_FIID, t.T_SIDE "
                    +"   from ( select PM.T_VALUEDATE, PM.t_OrderAmount t_Amount, PM.T_OrderFIID t_FIID, "
                    +"                 case when PM.T_PAYER = ? then 1 else 2 end T_SIDE "                                                                                                                    
                    +"            from dpmhist_dbt h1, dpmpaym_dbt pm "
                    +"           where PM.T_DOCKIND = ? "
                    +"             and PM.T_DOCUMENTID = ? "
                    +"             and PM.T_PURPOSE in(1,2,3,4,70,73) "
                    +"             and h1.t_PaymentID = PM.T_PAYMENTID "             
                    +"             and h1.t_AutoKey = (select max(h2.t_AutoKey) "
                    +"                                   from dpmhist_dbt h2 "
                    +"                                  where h2.t_PaymentID = PM.T_PAYMENTID "
                    +"                                    and (h2.t_StatusIDTo >= 3000 or h2.t_StatusIDTo = 150)"
                    +"                                ) "
                    +"             and exists( select * " 
                    +"                           from dmccateg_dbt mccat, dmcaccdoc_dbt accdoc "
                    +"                          where (accdoc.t_Account = PM.t_PayerAccount or accdoc.t_Account = PM.t_ReceiverAccount) "
                    +"                            and accdoc.t_DocKind = ? "
                    +"                            and mccat.t_ID = accdoc.t_CatID "
                    +"                            and mccat.t_Code in(?,?) "
                    +"                            and NVL (rsb_account.restall (accdoc.t_Account, accdoc.t_Chapter, accdoc.t_Currency, ?), 0) <> 0) "
                    +"             and H1.T_DATE > PM.T_VALUEDATE "
                    +"             and PM.T_VALUEDATE <= ? "
                    +"             and ( ( H1.T_DATE >= to_date('15.'||to_char(extract(month from ( PM.T_VALUEDATE)))||'.'||to_char(extract(year from (PM.T_VALUEDATE))),'DD.MM.YYYY') "
                    +"                    and PM.T_VALUEDATE <= to_date('14.'||to_char(extract(month from ( PM.T_VALUEDATE)))||'.'||to_char(extract(year from (PM.T_VALUEDATE))),'DD.MM.YYYY') "
                    +"                    and PM.T_VALUEDATE >= to_date('01.'||to_char(extract(month from ( PM.T_VALUEDATE)))||'.'||to_char(extract(year from (PM.T_VALUEDATE))),'DD.MM.YYYY') "
                    +"                   ) or "
                    +"                   ( H1.T_DATE > LAST_DAY(PM.T_VALUEDATE) "
                    +"                    and PM.T_VALUEDATE >= to_date('15.'||to_char(extract(month from ( PM.T_VALUEDATE)))||'.'||to_char(extract(year from (PM.T_VALUEDATE))),'DD.MM.YYYY') "
                    +"                    and PM.T_VALUEDATE <= LAST_DAY(PM.T_VALUEDATE) "
                    +"                   )"
                    +"                 ) "                                                                  
                    +"        ) t "
                    +"  group by t.T_VALUEDATE, t.T_SIDE, t.t_FIID "
                    +"  order by t.T_VALUEDATE, t.T_SIDE, t.t_FIID ";
    
        var QueryCmd = DL_RSDCommand(Query);

        QueryCmd.addParam( iif(m_Deal.Client>0,m_Deal.Client,{OurBank}) );
        QueryCmd.addParam( DL_DVNDEAL );
        QueryCmd.addParam( m_Deal.ID );

        Querycmd.addParam( DL_DVNDEAL );
        Querycmd.addParam( "Треб. с н.с." );
        Querycmd.addParam( "Обяз. с н.с." );
        Querycmd.addParam( m_Report.BegDate() - 1 );

        QueryCmd.addParam( m_Report.EndDate() );
    
        var QuerySet = QueryCmd.Execute();

        while( QuerySet.MoveNext() )

           if( not СделкаПодходит )
             if( ПроверитьСуммуТО(SQL_ConvTypeInteger(QuerySet.FIID),SQL_ConvTypeSum(QuerySet.Amount)) )
                СделкаПодходит = true;
             end;
           end;

           NotExecStrArr.Add(0,
                             SQL_ConvTypeInteger(QuerySet.SIDE),
                             SQL_ConvTypeDate(QuerySet.VALUEDATE),
                             $0,
                             ""
                            );

        end;

     END;

     m_StartCom1     = null;
     m_StartComCode1 = "";
     m_StartCom2     = null;
     m_StartComCode2 = "";
     m_Netting       = null;

     NotExecStrArr.ClearArray();

     СделкаПодходит = (ПроверитьСуммуСделки(m_Deal.FIID1,m_Deal.Amount1) or ПроверитьСуммуСделки(m_Deal.FIID2,m_Deal.Amount2));
                                                   
     /*сначала проверим дату исполнения сделки*/
     IsDealExec = СделкаИсполненаПолностью();
     
     СделкаИсполненаВОтчетномПериоде = (IsDealExec and (DealExecDate >= m_Report.BegDate()) and (DealExecDate <= m_Report.EndDate()));
     СделкаНеИсполнена = (not IsDealExec) or ( DealExecDate > m_Report.EndDate() );

     if( СделкаНеИсполнена )

        GetNotExecComFromPmGr();

        if(СделкаПодходит and (NotExecStrArr.Size() > 0))
           ПолучитьДанныеСторонПоСделке();
           
           m_CalcDate1 = GetCalcDate1();
           m_CalcDate2 = GetCalcDate2();
           
           GetStartCom(ALG_DV_PMGR_SIDE_LIABILITY);
           GetStartCom(ALG_DV_PMGR_SIDE_DEMAND);

           WriteErrMsgByDeal();

           i = 0;
           while( i < NotExecStrArr.Size() )
              m_PlanDate      = NotExecStrArr[i].PlanDate;
              m_Com1          = NotExecStrArr[i].Com1;  
              m_ActiveCode1_2 = NotExecStrArr[i].ActiveCode1;
              m_ActiveType1_2 = NotExecStrArr[i].ActiveType1;
              m_Com2          = NotExecStrArr[i].Com2;  
              m_ActiveCode2_1 = NotExecStrArr[i].ActiveCode2;
              m_ActiveType2_1 = NotExecStrArr[i].ActiveType2;
              m_Report.PrintTableLine(this);
              i = i + 1;
           end;
           m_Report.PrintTableLineKliko(this, NotExecStrArr);
        end;
       
     elif( СделкаИсполненаВОтчетномПериоде )
        /*отберем те, что были не исполнены в дату платежа*/
        GetNotExecComFromPmHist();

        if(СделкаПодходит and (NotExecStrArr.Size() > 0))
           ПолучитьДанныеСторонПоСделке();

           m_CalcDate1 = GetCalcDate1();
           m_CalcDate2 = GetCalcDate2();
           
           GetStartCom(ALG_DV_PMGR_SIDE_LIABILITY);
           GetStartCom(ALG_DV_PMGR_SIDE_DEMAND);

           WriteErrMsgByDeal();

           i = 0;
           while( i < NotExecStrArr.Size() )
              m_PlanDate      = NotExecStrArr[i].PlanDate;
              m_Com1          = NotExecStrArr[i].Com1;  
              m_ActiveCode1_2 = NotExecStrArr[i].ActiveCode1;
              m_ActiveType1_2 = NotExecStrArr[i].ActiveType1;
              m_Com2          = NotExecStrArr[i].Com2;  
              m_ActiveCode2_1 = NotExecStrArr[i].ActiveCode2;
              m_ActiveCode2_1 = NotExecStrArr[i].ActiveType2;
              m_Report.PrintTableLine(this);
              i = i + 1;
           end;
           m_Report.PrintTableLineKliko(this, NotExecStrArr);
        end;

     end;

   END;

   MACRO CheckAndPrintDataByDeal()

      DealErrMsg.Size=0;

      if( (m_Deal.DVKIND == DV_FORWARD) or (m_Deal.DVKIND == DV_OPTION) or (m_Deal.DVKIND == DV_CURSWAP) or (m_Deal.DVKind == DV_CURSWAP_FX) )
         if( (m_Deal.ExecType == DVSETTLEMET_CALC) or
             ((m_Deal.ExecType == DVSETTLEMET_STATE) and (((m_Deal.DVKIND == DV_FORWARD) and (m_Deal.BA_Kind == FIKIND_ARTICLE)) 
                                                           or ((m_Deal.DVKIND == DV_OPTION) and ((m_Deal.BA_Kind == FIKIND_ARTICLE)or(m_Deal.BA_Kind == FIKIND_METAL)))
                                                         ) 
             )
           )
            CheckAndPrint_FOCS_CALC();
         else
            CheckAndPrint_FOCS_STATE();
         end;
      elif( m_Deal.DVKIND == DV_PCTSWAP )
         CheckAndPrintDataByPCTSWAP();
      end;

   END;

   MACRO ExecuteReport()
     var fin = TRecHandler("fininstr.dbt");
     var NRec = 0, ProgressIndicator = TProgressBar();
     var Select =  " select DVDeal.*, fin.t_fi_kind BA_Kind, "
                  +"        nfi_1.t_ExecType ExecType, "
                  +"        (case when DVDeal.T_FORVARD = 'X' then nfi_2.t_ExecDate else nfi_1.t_ExecDate end) ExecDate, fin.t_name BA_Name, "
                  +"        nfi_1.t_Amount Amount1, nfi_1.t_FIID FIID1, nfi_2.t_Amount Amount2, nfi_2.t_FIID FIID2, "
                  +"        nfi_1.t_PriceFIID PriceFIID1, nfi_2.t_PriceFIID PriceFIID2, nfi_1.t_AccFIID AccFIID1, nfi_2.t_AccFIID AccFIID2, "
                  +"        nfi_1.t_Cost Cost1, nfi_2.t_Cost Cost2, "
                  +"        nfi_1.t_SuplDate SuplDate_1, nfi_2.t_SuplDate SuplDate_2, nfi_1.t_PayDate PayDate_1, nfi_2.t_PayDate PayDate_2 "
                  +"   from ddvndeal_dbt DVDeal, dfininstr_dbt fin, ddvnfi_dbt nfi_1, ddvnfi_dbt nfi_2 "                                                                                                                       
                  +"  where DVDeal.T_DVKIND in( " + DV_FORWARD + "," + DV_OPTION + "," + DV_CURSWAP + "," + DV_PCTSWAP + "," + DV_CURSWAP_FX + ") "
                  +"    and DVDeal.T_IsPFI = 'X' "                                                                                                                                          
                  +"    and DVDeal.T_Date <= ? "                                                                                                                                          
                  +"    and DVDeal.T_State > 0 "
                  +"    and DVDeal.T_Contractor != ? "                                                                                                                                          
                  +"    and DVDeal.T_CONTRACTOR not in (select T_PARTYID "
                  +"                                      from DDP_DEP_DBT "
                  +"                                     where T_FMREGIM <> 0) "
                  +"    and not exists (select PARTYOWN.T_PARTYKIND  "                                                                                                                       
                  +"                      from dpartyown_dbt partyown "                                                                                                                      
                  +"                     where PARTYOWN.T_PARTYID = DVDeal.T_Contractor "                                                                                                          
                  +"                       and PARTYOWN.T_PARTYKIND = 58) "                                                                                                                     
                  +"    and DVDeal.T_Contractor not in (select T_PARTYID "
                  +"                                      from DDP_DEP_DBT) "
                  +"    and exists (SELECT 1 "                                               
                  +"                  FROM doprstep_dbt step, doproper_dbt oper "                           
                  +"                 WHERE oper.t_DocKind      = ? AND "                                    
                  +"                       oper.t_DocumentID   = DVDeal.T_ID AND "                                    
                  +"                       step.t_ID_Operation = oper.t_ID_Operation AND "                  
                  +"                       step.t_Symbol       = 'Ф' AND "              
                  +"                       step.t_IsExecute    = 'X') "                                        
                  +"    and DVDeal.T_IsTrust = chr(0) "
                  +"    and nfi_1.T_DealID = DVDeal.T_ID and DVDeal.t_state = 1 "/*PNV 510636*/
                  +"    and nfi_1.T_TYPE = case when DVDeal.T_FORVARD = 'X' then 1 else 0 end " /*актив по форварду или по сделке (Для обеих частях СВОП поля T_FIID равны)*/
                  +"    and fin.t_fiid = nfi_1.T_fiid "/*здесь берем базовый актив*/
                  +"    and nfi_2.T_DealID = DVDeal.T_ID "
                  +"    and nfi_2.T_TYPE = case when DVDeal.T_DVKIND in( " + DV_CURSWAP + "," + DV_PCTSWAP + "," + DV_CURSWAP_FX + " ) then 2 else 0 end "
                  +" order by DVDeal.T_CODE";

     var cmd = DL_RSDCommand(Select);
                                                                                
     cmd.addParam( m_Report.EndDate() );
     cmd.addParam( m_Report.MMVB_ID() );
     cmd.addParam( DL_DVNDEAL );

     NRec = cmd.GetCount();
     if( NRec > 0 )
        m_CBR_CourseKind = ПИ_ПолучитьВидКурса(DV_RATETYPE_CBR);
        if( ПолучитьФинИн("840", fin, null, null, null, FICK_ISONUMBER) == 0 )
           m_FIID_840 = fin.rec.FIID;
        else
           m_Report.AddErrMsg("Не найден финансовый инструмент по коду ISO: \"840\"");
        end;
        ProgressIndicator.init(NRec, "Отбор сделок подсистемы \"ФИССиКО\" для отчета", "Отбор сделок подсистемы \"ФИССиКО\" для отчета");
        m_Deal = cmd.Execute();
        while( m_Deal.MoveNext() )
           m_tick.rec.ID = m_Deal.ID;
           CheckAndPrintDataByDeal();
           ProgressIndicator.Use;
        end;
        ProgressIndicator.Remove;
     end;
   END;

END;

PRIVATE CLASS (DL_CReportTemplate) DlNotExec_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )

   PRIVATE VAR m_BegDate, m_EndDate, m_BO, m_DV, m_Numb, m_NumbKliko, m_PrevBegDate, m_PrevEndDate, m_MMVB = -1, m_IsExistsData = false, m_TSS_CourseKind, m_InKliko=false;
   PRIVATE VAR ErrorMesage = DL_TUniqStrCollector();
   PRIVATE var  Kliko_F702 = null;//TStreamDoc; 
   PRIVATE var save_fname;  // Щербинин 05.06.2019      

   // ------------ kliko ------------
    private MACRO Kliko_InsertInTxtFile( str: string ) : bool
     Kliko_F702.WriteLine(str);
     return true;
    END;

    private MACRO Kliko_TxtFileName( RepDate: Date ) : string
      var day, month, year;
      datesplit( RepDate,   day, month, year );
      year = subStr( string(year), strlen(string(year))-1 );

      return GetTxtFileName( "dep"+string({NumDprt}) +"oper"+ string({oper})+ "_" + string(day:2:o)+string(month:2:o)+string(year:2:o)+"_F702" );
    END;

    private MACRO Kliko_CloseTxtFile( bShowFile: bool ) 
      Kliko_InsertInTxtFile( "<OED_TR>" );
      var fname = Kliko_F702.Name;
      Kliko_F702 = null;// Write : flush the data into the file
      var TermDir = "$txtfile\\"; // взял из or_set_h.mac

      //CopyFile (fname, "$C:\\SOFR\\EXPORT\\"+save_fname, True, "Копирование на терминал");     // Щербинин            
      CopyFile (fname, TermDir+save_fname, True, "Копирование на терминал");     // Щербинин 05.06.2019      
      if ( bShowFile )
        ViewFile( fname );
      end;
    END; 

    private MACRO Kliko_TxtFileExt(fileName: string) : string // !!! ПЕределать этот...
      return SubStr(filename,1,Index(filename,"2.") + 1) + "dat";
    END;

    private MACRO Kliko_OpenTxtFile( RepDate: Date ) : bool
      var errCode, errText = "";
      var fname = Kliko_TxtFileExt( Kliko_TxtFileName(RepDate) );
      save_fname = substr (fname,Index(fname,"dep")-1 );  // Щербинин 05.06.2019      
      fname  = "..\\txtfile" + substr (fname,Index(fname,"dep")-1 );/*CHVA*/
      Kliko_F702 = TStreamDoc( fname, "W", "rsoem"  );
      if ( not Kliko_F702  )
        errCode = status(errText);
        RunError( "Ошибка при открытии файла для экспорта в kliko|"+fName+"|(" + errcode + ") " + errtext );
        return false;
      end;
      return true;
    END;

    private MACRO Kliko_Fld( Val:VARIANT ):STRING
    VAR ValStr = "";

    if( Val != NULL ) 
      VAR vt = ValType(Val);
      if( vt == V_DATE )
         if( Val == DATE(0,0,0) )
            ValStr = "0";
         else
            ValStr = string(Val:f);
         end;
      elif( vt == V_STRING )
         ValStr = StrSubst( Val, "\"", "''" );
                     ValStr = StrSubst( ValStr, "-", "0" );
         //if (ValStr == "")
         //    ValStr = "0";
         //end;                  
      elif( (vt == V_DOUBLE) OR (vt == V_MONEY) )
        if( Val != 0.00)
          ValStr = trim(string(Val:18:3));
        else
          ValStr = "0.000";             
        end;
      else
         ValStr = string(Val);
      end;

    else
      ValStr="";
    end;

    return "\""+ValStr+"\"";
  END;

  private MACRO PrintLineKliko( A1, A2, A3, A4, A5, A6, ClientType1, A7, A8, SeconPartType, A9, A10, ClientType2, A11, A12, A13, A14, A15, A16, A17, A18, A19, A20, A21, A22, A23, A24, A25, 
                               subStr_Arr)
    
    var res =  Kliko_InsertInTxtFile(  
                                  Kliko_Fld( A1  )          +","+
                                  Kliko_Fld( A2  )          +","+
                                  Kliko_Fld( A3  )          +","+
                                  Kliko_Fld( A4  )          +","+ 
                                  Kliko_Fld( A5  )          +","+
                                  Kliko_Fld( A6  )          +","+
                                  Kliko_Fld( ClientType1 )  +","+
                                  Kliko_Fld( A7 )           +","+
                                  Kliko_Fld( A8  )          +","+
                                  Kliko_Fld( SeconPartType )+","+
                                  Kliko_Fld( A9  )          +","+
                                  Kliko_Fld( A10 ) +","+
                                  Kliko_Fld( ClientType2 ) +","+
                                  Kliko_Fld( A11 ) +","+
                                  Kliko_Fld( A12 ) +","+
                                  Kliko_Fld( A13 ) +","+
                                  Kliko_Fld( A14 ) +","+
                                  Kliko_Fld( A15 ) +","+
                                  Kliko_Fld( A16 ) +","+
                                  Kliko_Fld( A17 ) +","+
                                  Kliko_Fld( A18 ) +","+
                                  Kliko_Fld( A19 ) +","+
                                  Kliko_Fld( A20 ) +","+
                                  Kliko_Fld( A21 ) +","+
                                  Kliko_Fld( A22 ) +","+
                                  Kliko_Fld( A23 ) +","+
                                  Kliko_Fld( A24 ) +","+
                                  Kliko_Fld( A25 ) +","+
                                  "\"\",\"\",\"\",\"\",\"\",\"S\",\"1\",\"1\",\"\""
                                );    

    if ( not res ) return res; end;
   
    var leftMsg = "\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",\"\",";
    var rightMsg = ",\"P\",\"1\",\"0\",\"1\"";
    var item;
    var msg = "";
    var i = 0; 
    if ( IsEqClass(StrUpr("NotExecRQ_Array"), subStr_Arr)  )
      for (item, subStr_Arr)
        i = i + 1;
        msg =  Kliko_Fld(i) +","+ Kliko_Fld(item.ActiveType1_2()) +","+  Kliko_Fld(item.ActiveCode1_2())
                            +","+ Kliko_Fld(item.ActiveType2_1()) +","+  Kliko_Fld(item.ActiveCode2_1())
                            ;
        res = Kliko_InsertInTxtFile( leftMsg + msg + rightMsg);
        if (not res)  break;    end;
      end;

    elif ( IsEqClass(StrUpr("DV_NotExecStr_Array"), subStr_Arr) )
      for (item, subStr_Arr)
        i = i + 1;
          msg =  Kliko_Fld(i) +","+ Kliko_Fld(item.ActiveType1) +","+  Kliko_Fld(item.ActiveCode1)
                            +","+ Kliko_Fld(item.ActiveType2) +","+  Kliko_Fld(item.ActiveCode2)
                            ;
        res = Kliko_InsertInTxtFile( leftMsg + msg + rightMsg);
        if (not res)  break;    end; 
      end;
    end;
      
     return res;          
  END;
   // ------------end kliko ---------


   MACRO AddErrMsg(ErrMsg:string)
      ErrorMesage.AddString(ErrMsg);
   END;

   MACRO BegDate():date
       return m_BegDate;
   END;

   MACRO EndDate():date
       return m_EndDate;
   END;

   MACRO PrevBegDate():date
       return m_PrevBegDate;
   END;

   MACRO PrevEndDate():date
       return m_PrevEndDate;
   END;

   MACRO MMVB_ID():integer
       return m_MMVB;
   END;

   MACRO TSS_CourseKind():integer
       return m_TSS_CourseKind;
   END;

   /*только ексель*/
   PRIVATE MACRO PrintMode():INTEGER
      return DL_OUTREPORT_EXCEL;
   END;

   PRIVATE MACRO BeginReport( NumCopy:INTEGER )
      var Month = int(DlNotExec_Form.GetFieldValue(PNFLD_DlNotExec_MONTH)), 
          Year  = int(DlNotExec_Form.GetFieldValue(PNFLD_DlNotExec_YEAR));

      ErrorMesage.ClearArray();

      if( DlNotExec_Form.GetFieldValue(PNFLD_DlNotExec_PERIOD) == 0 )/*1-я половина*/
         m_BegDate = date(1,Month,Year);
         m_EndDate = DlNotExec_Form.GetFieldValue(PNFLD_DlNotExec_REPDATE)-1;
         if( Month == 1 )
            m_PrevBegDate = date(14,12,Year-1);
         else
            m_PrevBegDate = date(14,Month-1,Year);
         end;
      else
         m_BegDate = date(15,Month,Year);
         m_EndDate = DlNotExec_Form.GetFieldValue(PNFLD_DlNotExec_REPDATE)-1;
         m_PrevBegDate = date(1,Month,Year);
      end;

      m_PrevEndDate = m_BegDate - 1;

      m_BO = DlNotExec_Form.GetFieldValue(PNFLD_DlNotExec_BO);
      m_DV = DlNotExec_Form.GetFieldValue(PNFLD_DlNotExec_DV);

      m_Numb = 0;

      m_MMVB = GetCodeParty("ММВБ",PTCK_CONTR);
      if( m_MMVB == "" )
         m_MMVB = GetCodeParty("MICEX",24);
         if( m_MMVB == "" )
            ErrorMesage.AddString("Не найден ID биржи \"ММВБ\"");
            m_MMVB = -1;
         end;
      end;
      
      m_TSS_CourseKind = ПолучитьВидКурса(SP_RATETYPE_RightCost);
      
      m_InKliko = IIF( DlNotExec_Form.GetFieldValue(PNFLD_DlNotExec_TO_KLIKO) == SET_CHAR, true, false );
      if ( m_InKliko and (not Kliko_OpenTxtFile(m_EndDate)) )
         m_InKliko = false;
      elif (Kliko_F702)
        m_NumbKliko = 0;
        Kliko_InsertInTxtFile(string("<F702>"));
      end;
      
   END;

   MACRO RegisterTable()

      SetActiveSheet( "Отчет" );
      PrintFormatString( "",
                         "N1_H0", this.H0(),
                         "N1_H1", this.H1(),
                         "N1_H2", this.H2()
                       );

      RegisterTable( "N1_MainTable", "",
                     "N1_Numb",          
                     "N1_DealCode",      
                     "N1_DealDate",      
                     "N1_CalcDate1",     
                     "N1_CalcDate2",     
                     "N1_PlanDate",      
                     "N1_ClientCode1",   
                     "N1_LEIClientCode1",
                     "N1_Code2",         
                     "N1_LEICode2",      
                     "N1_ClientCode2",   
                     "N1_LEIClientCode2",
                     "N1_OrgKind",     
                     "N1_NR",            
                     "N1_StartComCode1", 
                     "N1_StartCom1",     
                     "N1_StartComCode2", 
                     "N1_StartCom2",     
                     "N1_Com1",          
                     "N1_Com2",          
                     "N1_OperKind",      
                     "N1_Place",         
                     "N1_Risk",          
                     "N1_Rep",           
                     "N1_Netting",       
                     "N1_ActiveCode1_2", 
                     "N1_ActiveCode2_1"  
                   );
   END;
 
   MACRO PrintTableLine(Report:OBJECT)

      if( m_IsExistsData == false )
         this.RegisterTable();
         m_IsExistsData = true;
      end;

      m_Numb = m_Numb + 1;

      PrintTableLine(
                     "N1_Numb",            m_Numb,                    null,
                     "N1_DealCode",        Report.DealCode(),         null,
                     "N1_DealDate",        Report.DealDate(),         null,
                     "N1_CalcDate1",       Report.CalcDate1(),        null,
                     "N1_CalcDate2",       Report.CalcDate2(),        null,
                     "N1_PlanDate",        Report.PlanDate(),         null,
                     "N1_ClientCode1",     Report.ClientCode1(),      null, 
                     "N1_LEIClientCode1",  Report.LEIClientCode1(),   null,
                     "N1_Code2",           Report.Code2(),            null,
                     "N1_LEICode2",        Report.LEICode2(),         null,
                     "N1_ClientCode2",     Report.ClientCode2(),      null,
                     "N1_LEIClientCode2",  Report.LEIClientCode2(),   null,
                     "N1_OrgKind",         Report.OrgKind(),          null,  
                     "N1_NR",              Report.NR(),               null,
                     "N1_StartComCode1",   Report.StartComCode1(),    null,
                     "N1_StartCom1",       Report.StartCom1(),        null,
                     "N1_StartComCode2",   Report.StartComCode2(),    null,
                     "N1_StartCom2",       Report.StartCom2(),        null,
                     "N1_Com1",            Report.Com1(),             null,
                     "N1_Com2",            Report.Com2(),             null,
                     "N1_OperKind",        Report.OperKind(),         null,
                     "N1_Place",           Report.Place(),            null,
                     "N1_Risk",            Report.Risk(),             null,
                     "N1_Rep",             Report.Rep(),              null,
                     "N1_Netting",         Report.Netting(),          null,
                     "N1_ActiveCode1_2",   Report.ActiveCode1_2(),    null,
                     "N1_ActiveCode2_1",   Report.ActiveCode2_1(),    null
                    );
   END;                         

   MACRO PrintTableLineKliko(Report: OBJECT, subStr_Arr: OBJECT)
   
    if( not m_InKliko ) return;  end;

    var planDate = Report.Plandate();
    var com1: money  = 0.0;
    var com2: money  = 0.0;
    var item;

    if ( IsEqClass(StrUpr("NotExecRQ_Array"), subStr_Arr) )
      for (item, subStr_Arr)
          com1 = com1 + IIF(item.Com1()!=null, item.Com1(), 0 );
          com2 = com2 + IIF(item.Com2()!=null, item.Com2(), 0 );
      end;

      if ( planDate > Date(item.PlanDate) )
        planDate = item.PlanDate;
      end;

    elif ( IsEqClass(StrUpr("DV_NotExecStr_Array"), subStr_Arr) )
      for (item, subStr_Arr)
        com1 = com1 + IIF(item.Com1!=null, item.Com1, 0);
        com2 = com2 + IIF(item.Com2!=null, item.Com2, 0);
        if ( planDate > Date(item.PlanDate) )
          planDate = item.PlanDate;
        end;
      end;

    else
      planDate = Report.Plandate();
      com1 = Report.Com1();
      com2 = Report.Com2();
    end;
  
    m_NumbKliko = m_NumbKliko + 1;
    PrintLineKliko(
                    m_NumbKliko,              // 1
                    Report.DealCode(),        // 2
                    Report.DealDate(),        // 3
                    Report.CalcDate1(),       // 4
                    Report.CalcDate2(),       // 5
                    planDate,                 // 6
                    Report.ClientType1(),     // ClientType1
                    Report.ClientCode1(),     // 7
                    Report.LEIClientCode1(),  // 8
                    Report.SecondPartType(),  // SecondPartType
                    Report.Code2(),           // 9
                    Report.LEICode2(),        // 10
                    Report.ClientType2(),     // ClientType2           
                    Report.ClientCode2(),     // 11
                    Report.LEIClientCode2(),  // 12
                    Report.OrgKind(),         // 13
                    Report.NR(),              // 14
                    Report.StartComCode1(),   // 15 
                    Report.StartCom1_m(),       // 16    19
                    Report.StartComCode2(),   // 17             
                    Report.StartCom2_m(),       // 18     21
                    com1,                     // 19     
                    com2,                     // 20
                    Report.OperKind(),        // 21  
                    Report.Place(),           // 22    
                    Report.Risk(),            // 23 
                    Report.Rep(),             // 24   
                    Report.Netting(),         // 25
                    //...  подстроки - массив NotExecRQ_Array или DV_NotExecStr_Array
                    subStr_Arr
                  );
   END;                       

   MACRO H0():string
      return "за период с "+DL_DateToStr(m_BegDate)+" по "+DL_DateToStr(m_EndDate);
   END;

   MACRO H1():string
      return {Name_Bank};
   END;

   MACRO H2():string
      return {Real_Addr};
   END;

   MACRO Create()
    if ( m_InKliko )
      Dl_CallInsertStat (DL_OUTREPORT_KLIKO);
    end; 

      if( m_BO )
         SP_NotExec_Report(this).ExecuteReport();/*по сделкам БОЦБ*/
      end;
      if( m_DV )
         DV_NotExec_Report(this).ExecuteReport();/*по сделкам ФИССиКО*/
      end;
   END;

   MACRO EndReport()
      if( m_IsExistsData )
         EndTable();
         AddStandardFooter( "N1_" );

         var i = 0;
         while (i < ErrorMesage.Size)
            msgbox(ErrorMesage[i]);
            i = i + 1;
         end;

         if ( m_InKliko and (Kliko_F702 != NULL) )
           Kliko_CloseTxtFile(true);
         end;

      else
         this.RegisterTable();
         AddStandardFooter( "N1_" );
         //msgbox("Нет данных для формирования отчета.");
      end;
   END;

   initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );  
END;

macro GetAuditMsg(panel:DL_CPanel):String
  var msg:string = "";
  var month_part:string = "";
  var period:string = "";
  var rep_date:string = "";
  var deals1:string = "";
  var deals2:string = "";
  
  month_part = IIF(panel.GetFieldValue(PNFLD_DlNotExec_PERIOD) == 0,
                   "1-я половина месяца",
                   "2-я половина месяца");

  period = PeriodName_GetMonthsAndQuart(
                panel.GetFieldValue(PNFLD_DlNotExec_MONTH),
                panel.GetFieldValue(PNFLD_DlNotExec_YEAR));

  rep_date = panel.GetFieldValue(PNFLD_DlNotExec_REPDATE);

  deals1 = IIF(panel.GetFieldValue(PNFLD_DlNotExec_BO) == "X",
               "Модуля \"Бэк-офис ценных бумаг\"\n",
               "");

  deals2 = IIF(panel.GetFieldValue(PNFLD_DlNotExec_DV) == "X",
               "Модуля \"ФИСС и КО\"\n",
               "");

  msg = 
  "Отчет:              Информация о неисполненных сделках (ф. 702)\n\n" +
  "Отчетный период:    " + period + ", " + month_part + "\n" +
  "Отчетная дата:      " + rep_date + "\n" +
  "По сделкам:\n"  +
  deals1 + deals2 +
  "\n----------------------------------------\n";

  return msg;
end;

MACRO RunReport()

  var stat = true;

  while(stat)
     DlNotExec_Form = DlNotExec_Panel();
   
     stat = DlNotExec_Form.Run();
   
     // Проверяем можем ли мы делать отчет по предоставленным данным
     if (not stat)
        break;
     end;
   
     if( stat )
       var DlNotExec = DlNotExec_Report( null, "Report_DlNotExec.xls" );      
       DlNotExec.Run(null, GetAuditMsg(DlNotExec_Form));
       Dl_CallInsertStat( DL_OUTREPORT_EXCEL );
     end;
  end;

END;

RunReport();
exit(1);  