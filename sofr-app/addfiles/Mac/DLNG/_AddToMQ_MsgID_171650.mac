/*
$Name:        _AddToMQ_MsgID_171650.mac
$Module:      Ядро Securities
$Description: Добавление в очередь отправки сообщения DDLCONTRMSG_DBT->T_ID = 171650 (номер ДБО = "63/27-18121273")
*/
import "dlcontrmsg_mmvb.mac";

PRIVATE CONST DLCONTRMSG_ID = 171650;
PRIVATE CONST DLCONTR_CODE = "63/27-18121273";


// Обязательные параметры при значении настройки: "SECUR\ОНЛАЙН РЕГИСТРАЦИЯ КЛ НА БИРЖЕ" = TRUE для подключение к очереди bank_to_moex:
// (Важно! Обязательно должны быть настроены необходимые war-файлы, RabbitMQ и прочее ПО (#287404))
// для подключение к очереди bank_to_moex:
private const host     = "SGO-AP608";
private const vhost    = "SOFR";
private const user     = "admin";
private const password = "";  //SVI убран пароль
private const port     = 5672;
private const queue    = "bank_to_moex";
/////////////////////////////////////


private macro GetSfContrRH( RecFromSelect:TRecHandler, SfContrID ) :BOOL
  var Query = "SELECT * FROM DSFCONTR_DBT WHERE T_ID = " + SfContrID;
  return DL_GetRecHandler(RecFromSelect, Query);
end;

private macro GetDlContrRH( RecFromSelect:TRecHandler, DlContrID ) :BOOL
  var Query = "SELECT * FROM DDLCONTR_DBT WHERE T_DLCONTRID = " + DlContrID;
  return DL_GetRecHandler(RecFromSelect, Query);
end;

private macro GetDlContrMsgXml(DlContrMsgID, stat:@Integer)
  stat = 0;
  var filePathName = "", xmlData = "";
  var queryImg = " SELECT t_imageid, t_filename " +
                   " FROM dimgdata_dbt " +
                  " WHERE t_imagetype = 1 " +
                    " AND t_objecttype = " + OBJTYPE_BSCMSG_DL +
                    " AND t_objectid = LPAD("+DlContrMsgID+", 34, '0') " +
                    " AND t_closedate = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
                  " ORDER BY t_IsMain desc ";

  var ds = TRsbDataSet(queryImg);
  if(ds.MoveNext())
    println("В dimgdata_dbt найдена запись с t_imageid = "+Int(ds.ImageID)+" и именем файла "+ds.filename);

    if(WEB_ShowImageByID(Int(ds.ImageID), filePathName))
      println("С помощью WEB_ShowImageByID() сохранен файл по пути = "+filePathName);
    
      var xmlReader = Dl_XmlReader();
      if(xmlReader.Load_file(filePathName))
        println("Файл загружен в xmlReader()");

        xmlData = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>" +
                  xmlReader.GetXml().DocumentElement.xml;
                  
        println("Получено тело xml сообщения для отправки: "+xmlData);
      else
        stat = 1;
        println("Не удалось загрузить файл в xmlReader()");
      end;

      if(not RemoveFile(filePathName))
        println("Не удалось удалить временный файл \"" + filePathName + "\"");
      else
        println("Удален временный файл \"" + filePathName + "\"");
      end;
    else
      stat = 1;
      println("С помощью WEB_ShowImageByID() НЕ удалось сохранить файл ");
    end;
  else
    stat = 1;
    println("В dimgdata_dbt НЕ найдена запись для DlContrMsgID = "+DlContrMsgID);
  end;

  return xmlData;
onError(er)
  if(er.Code != 0)
    stat = er.Code;
  else
    stat = 1;
  end;
  println("Произошла непредвиденная ошибка при получении xml сообщения в ф-и GetDlContrMsgXml().\n"+stat+"; "+er.Message);
  return "";
end;

private macro CreateMQClient(MQClient:@variant)
  MQClient = AMQPMOEXClient(host, vhost, user, password, port, queue);
  return 0;
onError(er)
  return 1;
end;

private macro AddMesToMQ(MQClient:@variant, partyID, dlContrID, dlContrMsgID, dataXML:@string)
  MQClient.publish(string(partyID), string(dlContrID), string(dlContrMsgID), dataXML);
  return 0;
onError(er)
  return 1;
end;

private macro ДобавитьСообщВОчередьОтправки(MQClient:@variant, PartyID, dlContrMsg, dataXML:@string)
  var stat = 0, statAddToMQ = 0;
  var sendMesState = DL_IIF(dlContrMsg.rec.SendMesState == 0, DLSENDMESSTATE_INQUEUE, DLSENDMESSTATE_REPEATINQUEUE);
  var transactionStarted:bool = false;

  RslDefCon.BeginTrans();
  transactionStarted = true;

  var DlContrMsgToUpd = TBFile("dlcontrmsg.dbt", "W", 0);
  DlContrMsgToUpd.rec.ID = dlContrMsg.rec.ID;

  if(DlContrMsgToUpd.GetEQ() and
     (DlContrMsgToUpd.rec.Version == dlContrMsg.rec.Version) and 
     (DlContrMsgToUpd.rec.SendMesState != DLSENDMESSTATE_SUCCESS))

    DlContrMsgToUpd.rec.SendMesState = sendMesState;
    if(not DlContrMsgToUpd.Update())
      stat = 1;
    end;
    if(not DlContrMsgToUpd.GetEQ())
      stat = 1;
    end;
    if(stat == 0)
      if(DlContrMsgToUpd.rec.Version != dlContrMsg.rec.Version+1) // запись уже где-то изменилась
        stat = 1;
      end;
    end;

    if(stat == 0)
      statAddToMQ = AddMesToMQ(@MQClient, PartyID, dlContrMsg.rec.DlContrID, dlContrMsg.rec.ID, @dataXML);
      if(statAddToMQ != 0)
        DlContrMsgToUpd.rec.SendMesState = DLSENDMESSTATE_ER_ADDINQUEUE;

        if(not DlContrMsgToUpd.Update())
          stat = 1;
        end;
      end;
    end;
  end;

  if(stat == 0)
    RslDefCon.CommitTrans();
  else
    RslDefCon.RollbackTrans();
  end;
  transactionStarted = false;

  if((stat == 0) and (statAddToMQ != 0))
    stat = statAddToMQ;
  end;

  return stat;
OnError(er)
  if(transactionStarted)
    RslDefCon.RollbackTrans();
  end;
  if(er.Code != 0)
    stat = er.Code;
  else
    stat = 1;
  end;
  println("Произошла непредвиденная ошибка при работе ф-и ДобавитьСообщВОчередьОтправки().\n"+stat+"; "+er.Message);
  return stat;
end;


private macro AddMsg()
  var stat = 0, dataXML = "", MQClient, sql, cmd, ds,
      dlContr = TRecHandler("dlcontr.dbt"), 
      sfContr = TRecHandler("sfcontr.dbt"),
      dlContrMsg = TRecHandler("dlcontrmsg.dbt");

  sql = "SELECT MSG.* "+
         " FROM DDLCONTRMSG_DBT MSG, DDLCONTR_DBT DL, DSFCONTR_DBT SF "+
        " WHERE MSG.T_ID = ? "+
          " AND MSG.T_ISONLINESENDMES = 'X' "+
          " AND MSG.T_SENDMESSTATE IN ("+DLSENDMESSTATE_ER_ADDINQUEUE+","+DLSENDMESSTATE_ER_SIGN+","+DLSENDMESSTATE_ER_SESSION_TOKEN+","+DLSENDMESSTATE_ER_UNSPECIFIED+","+
                                         DLSENDMESSTATE_ER_PRECHECK+","+DLSENDMESSTATE_ER_CALLREGSERV+","+DLSENDMESSTATE_ER_CALLSTATSERV+")"+
          " AND MSG.T_KIND = "+OBJTYPE_DLCONTRMSG_MARKETREG+
          " AND MSG.T_DLCONTRID = DL.T_DLCONTRID "+
          " AND SF.T_ID = DL.T_SFCONTRID "+
          " AND SF.T_NUMBER = ? ";

  cmd = DL_RSDCommand(sql);
  cmd.AddParam(DLCONTRMSG_ID);
  cmd.AddParam(DLCONTR_CODE);

  ds = cmd.Execute();
  if(ds.moveNext() and (stat == 0))
    println("Сообщение ДБО "+DLCONTRMSG_ID+" для добавления в очередь отправки найдено.");
  
    dlContrMsg.Clear();
    ds.GetRecord().CopyTo(dlContrMsg.rec);

    if(not GetDlContrRH(dlContr, dlContrMsg.rec.DlContrID))
      stat = 1;
      println("Не найден ДБО с ID = "+dlContrMsg.rec.DlContrID);
    end;
    
    if((stat == 0) and (not GetSfContrRH(sfContr, dlContr.rec.SfContrID)))
      stat = 1;
      println("Не найден договор с ID = "+dlContr.rec.SfContrID);
    end;

    if(stat == 0)
      dataXML = GetDlContrMsgXml(dlContrMsg.rec.ID, @stat);
      if((strlen(dataXML) == 0) or (stat != 0))
        stat = 1;
        println("Не удалось получить тело xml сообщения для отправки функцией GetDlContrMsgXml()");

      else
        stat = CreateMQClient(@MQClient);
        if(stat != 0)
          println("Проверьте настройки подключения к очереди RabbitMQ!");
        else
          stat = ДобавитьСообщВОчередьОтправки(@MQClient, sfContr.rec.PartyID, dlContrMsg, @dataXML);
          if(stat != 0)
            println("Ошибка при вызове ДобавитьСообщВОчередьОтправки; stat="+stat);
          end;
        end;
      end;
    end;
  else
    println("Сообщение ДБО "+DLCONTRMSG_ID+" для добавления в очередь отправки НЕ найдено.");
  end;

  if(stat == 0)
    println("\nСообщение ДБО "+DLCONTRMSG_ID+" добавлено в очередь отправки!");
  end;

onError(er)
  if(er.Code != 0)
    stat = er.Code;
  else
    stat = 1;
  end;
  println("Произошла непредвиденная ошибка при работе ф-и AddMsg().\n"+stat+"; "+er.Message);
  return stat;
end;

AddMsg();
