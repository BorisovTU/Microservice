/**                                                                                                             
 @file dlChangeTariffForPervRazmNote.mac                                                                                           
 @brief Разовая процедура изменения тарифа по первичному размещению структурных нот                                                                         
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.12.10 |Жиц М.В.       |BOSS-6378           |Создание макроса процедуры                                                                                                                                                 
*/ 
import  dlcontrfunc;
import "fm_util.mac";
import "or_rep_h.mac";

private var FlagCtrlBrk:bool = false;
private var CtrlBrkErr = 17;

private var TPName_list = MakeArray("Малый и средний бизнес", "Инвестор", "Крупный бизнес", "Трейдер", "Трейдер - СВО (без фиксированной комиссии)");

private var ChangeDate:date = {curdate};
private var CurTPName:string = "";
private var ErrMes:string = "";

/**
 @brief Транзакция создания новых комиссий и изменения существующих
*/	
MACRO ChangeAndAddComissTrn()
  var FlagAbortTRN:bool = false;
  var sql:string;
  var exec;

  InitProgress(-1, "Изменение тарифной сетки комиссий по покупке бумаг РСХБ при первичном размещении и бумаг сторонних эмитентов при первичном размещении", "Корректировка тарифной сетки"); 
  sql =  "DECLARE "
         + "BEGIN "
         + "    rsb_ChangeTariff.UpdateSfTarif; "
         + "END; ";

  exec = DL_RSDCommand(sql);
  exec.ExecuteCMD();
  RemProgress();

  InitProgress(-1, "Изменение алгоритма расчета комиссий по покупке бумаг РСХБ при первичном размещении и бумаг сторонних эмитентов при первичном размещении", "Корректировка алгоритмов расчета"); 
  sql =  "DECLARE "
         + "BEGIN "
         + "    rsb_ChangeTariff.UpdateSfCalCal; "
         + "END; ";

  exec = DL_RSDCommand(sql);
  exec.ExecuteCMD();
  RemProgress();

  InitProgress(-1, "Создание новых комиссий по сделкам покупки при размещении бумаг сторонних эмитентов, являющихся структурными нотами, выплаты по которым зависят от изменения величины процентных ставок", "Заведение новых комиссий"); 
  sql =  "DECLARE "
         + "BEGIN "
         + "    rsb_ChangeTariff.AddComiss; "
         + "END; ";

  exec = DL_RSDCommand(sql);
  exec.ExecuteCMD();
  RemProgress();

OnError(ErObj)
  if(ErObj.Code == CtrlBrkErr)
     FlagCtrlBrk = true;
     AbortTRN;
  else
     if(FlagAbortTRN == false)
        ErrMes = "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message;
        AbortTRN;
     end;
  end;
  RemProgress();
END;

/**
 @brief Транзакция добавления новых комиссий под тарифные планы и исключение старых комиссий из под тарифных планов (в рамках конкретного ТП)
*/	
MACRO ChangeTariffTrn()
  var FlagAbortTRN:bool = false;
  var sql:string;
  var exec;

  sql =  "DECLARE "
         + "BEGIN "
         + "    rsb_ChangeTariff.LinkComissToSf(?, ?); "
         + "END; ";

  exec = DL_RSDCommand(sql);
  exec.AddParam(ChangeDate);
  exec.AddParam(CurTPName);
  exec.ExecuteCMD();

  sql =  "DECLARE "
         + "BEGIN "
         + "    rsb_ChangeTariff.ExclusionComissFromTP(?, ?); "
         + "END; ";

  exec = DL_RSDCommand(sql);
  exec.AddParam(ChangeDate-1); 
  exec.AddParam(CurTPName);
  exec.ExecuteCMD();

OnError(ErObj)
  if(ErObj.Code == CtrlBrkErr)
    FlagCtrlBrk = true;
    AbortTRN;
  else
    if(FlagAbortTRN == false)
      ErrMes = "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message;
      AbortTRN;
    end;
  end;
END;

/**
 @brief Основаная ф-ция изменения тарифа по первичному размещению структурных нот
*/	
MACRO MainChangeTariff()  
  var i:integer = 0;
  if(GetDate(ChangeDate, "Дата начала действия новых комиссий:"))

    if(not ProcessTrn(0, "ChangeAndAddComissTrn"))
      msgbox("При создании новых комиссий по первичному размещению структурных нот возникли ошибки:\n" + ErrMes);
      return;
    end;
  
    InitProgress(TPName_list.size(), "Добавление новых комиссий под тарифные планы и исключение старых комиссий из под тарифных планов", "Настройка тарифных планов"); 
    while(i < TPName_list.size())
      CurTPName = TPName_list[i];
      if(not ProcessTrn(0, "ChangeTariffTrn"))
        msgbox("При изменении тарифа по первичному размещению структурных нот для тарифного плана \""+CurTPName+"\" возникли ошибки:\n" + ErrMes);
        RemProgress();
        return;
      end;
      UseProgress(i = i + 1);
    end;
    RemProgress();
  
    msgbox("Изменение тарифа по первичному размещению структурных нот\n успешно завершено!");
  end;
END; 

MainChangeTariff();
