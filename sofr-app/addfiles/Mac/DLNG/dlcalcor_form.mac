/*
$Name:        dlcalcor_form.mac
$Module:      Ядро Securities
$Description: Расчет ОР и корректировок.
              Форма ввода параметров выпуска отчета.
*/
import "DlRepForm_h.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_DLCALCOR_PERIOD      = 0,
      PNFLD_DLCALCOR_YEAR        = 1,
      PNFLD_DLCALCOR_ENDDATE     = 2,
      PNFLD_DLCALCOR_BYSECUR     = 3,
      PNFLD_DLCALCOR_BYBILL      = 4,
      PNFLD_DLCALCOR_BYMBK       = 5,
      PNFLD_DLCALCOR_BYEMAIL     = 6,
      PNFLD_DLCALCOR_PRINTMETHOD = 7;

private macro GetLastDayInMonth( CurDate:Date )
    var day, mon, year;

    DateSplit(CurDate,day,mon,year);
    if( mon == 12 )
      return Date(1,1,year+1) - 1;
    else
      return Date(1,mon+1,year) - 1;
    end;
end;

private macro GetLastWorkDayInMonth( CurDate:Date )
   var LastWorkDate;

   LastWorkDate = GetLastDayInMonth(CurDate);
   if( not IsWorkDay(LastWorkDate))
     LastWorkDate = GetDateAfterWorkDays(LastWorkDate,-1);
   end;
   return LastWorkDate;
end;

/*Период*/
private macro FldProc_Period( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR EndDate, Year;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        DateSplit( {curdate}, null, FldRealValue, null );
     end;     
     FldShowValue = DL_GetNameAlg( 2260, FldRealValue );

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     EndDate = pThis.GetLinkedValue(DL_PNFLD_ENDDATE);
     if((EndDate == null) or (EndDate == ZeroDate))
        EndDate = {curdate};
     end;

     DateSplit( EndDate, NULL, NULL, Year );
     EndDate = GetLastWorkDayInMonth(Date(1, FldRealValue, Year));
     pThis.SetLinkedValue( DL_PNFLD_ENDDATE, EndDate );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 2260, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;

/*Год*/
private macro FldProc_Year( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var EndDate, Month;

  if( Cmd == DLG_INIT ) /*Инициализация по умолчанию*/
     if( FldRealValue == null )
        DateSplit( {curdate}, null, null, FldRealValue );
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if( (FldRealValue < 1990) OR (FldRealValue > 2990) ) /*максимально исключить опечатки*/
        MsgBox( "Неверное значение в поле \"Год\".");
        return CM_IGNORE;
     end;

     EndDate = pThis.GetLinkedValue(DL_PNFLD_ENDDATE);
     if(EndDate == ZeroDate)
        EndDate = {curdate};
     end;

     DateSplit( EndDate, NULL, Month, NULL );
     EndDate = GetLastWorkDayInMonth(Date(1, Month, FldRealValue));
     pThis.SetLinkedValue( DL_PNFLD_ENDDATE, EndDate );
  end;
  return CM_DEFAULT;
END;

private macro FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Year, Month;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     if( FldRealValue == ZeroDate )
        MsgBox( "Отчетная дата должна быть задана.");
        return CM_IGNORE;
     end;

     if(FldRealValue != ZeroDate)
        DateSplit( FldRealValue, null, Month, Year);
        pThis.SetLinkedValue( DL_PNFLD_YEAR, Year );
        pThis.SetLinkedValue( DL_PNFLD_PERIOD, Month);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  rep_EndDate = FldRealValue;
  return CM_DEFAULT;
END;

/* ***************************************** */
/* Форма ввода параметров отчета             */
/* ***************************************** */
class (DL_CPanel) CDLCalcOR_Panel()

  private macro Init()
     /*только в excel*/
     DisableFields( This.Data(), PNFLD_DLCALCOR_PRINTMETHOD );

     AddFieldProc( 0, PNFLD_DLCALCOR_PERIOD,      DL_PNFLD_PERIOD,      @FldProc_Period,         NULL, 11, 4 );
     AddFieldProc( 0, PNFLD_DLCALCOR_YEAR,        DL_PNFLD_YEAR,        @FldProc_Year            );
     AddFieldProc( 0, PNFLD_DLCALCOR_ENDDATE,     DL_PNFLD_ENDDATE,     @FldProc_EndDate         );
     AddFieldProc( 0, PNFLD_DLCALCOR_BYSECUR,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    SET_CHAR );
     AddFieldProc( 0, PNFLD_DLCALCOR_BYBILL,      DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    SET_CHAR );
     AddFieldProc( 0, PNFLD_DLCALCOR_BYMBK,       DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    SET_CHAR );
     AddFieldProc( 0, PNFLD_DLCALCOR_BYEMAIL,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    SET_CHAR );
     AddFieldProc( 0, PNFLD_DLCALCOR_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 31, 11 );
  end;

  macro Run()
    var stat = Run();
    return stat;
  end;

  private MACRO Check():BOOL
    return true;
  END;

  initDL_CPanel("dlrep.lbr", "dlcalcor");
end;

