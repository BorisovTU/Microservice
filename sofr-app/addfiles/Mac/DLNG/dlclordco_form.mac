/*
$Name:        dlclordco_form.mac
$Module:      Ядро Securities
$Description: Отчет "Поручения клиента на перевод д/с между секторами РЦ биржи" (Общий для БО ЦБ и ПИ)
*/

/*******************************************************************************
 DESCRIPTION  :   Отчет "Поручения клиента на перевод д/с между секторами РЦ ОРЦБ " и 
                        "Поручение на перевод ценных бумаг"
                   - форма ввода данных  Общий для БО и ПИ
 PROGRAMMED BY:   Никольская В.
 CREATION DATE:   27.02.09
*******************************************************************************/
import "DlRepForm_h.mac";
  
CONST
      PNFLD_DLCLORDCO_ClientCode       = 0, 
      PNFLD_DLCLORDCO_ClientName       = 1,

      PNFLD_DLCLORDCO_ISFORM           = 2,       /*                      */
      PNFLD_DLCLORDCO_FORMSUB          = 3,       /* Форма субъекта       */
      PNFLD_DLCLORDCO_ISVID            = 4,       /*                      */
      PNFLD_DLCLORDCO_VIDSUB           = 5,       /* Вид субъекта         */
      PNFLD_DLCLORDCO_NORESIDENT       = 6,       /* Флаг резидентности   */
 
      PNFLD_DLCLORDCO_IsBO             = 7, 
      PNFLD_DLCLORDCO_IsDV             = 8, 
      PNFLD_DLCLORDCO_IsCM             = 9, 

      PNFLD_DLCLORDCO_Contr            = 10,
 
      PNFLD_DLCLORDCO_IncludePlanned   = 11, 

      PNFLD_DLCLORDCO_BegDate          = 12, 
      PNFLD_DLCLORDCO_EndDate          = 13, 

      PNFLD_DLCLORDCO_OperCode         = 14, 
      PNFLD_DLCLORDCO_OperName         = 15,
 
      PNFLD_DLCLORDCO_Apostr           = 16;
/*      PNFLD_DLCLORDCO_IsExportToExel           = 11;*/ 

CLASS DLCLORDCOFormData()
  VAR 
  ClientID,     
  IsForm,
  FormSub,
  IsVid,
  VidSub,
  Noresident,
  IsBO,    
  IsDV,    
  IsCM,    
  Contr,  
  IncludePlanned,        
  BegDate,    
  EndDate,     
  OperCode,
  OperName,
/*  IsExportToExel,*/
  IsUseApp;       

  // Значение по умолчанию

  ClientID = -1;
  IsForm     =   0;
  FormSub    =   0;
  IsVid      =   0;
  VidSub     =   0;
  Noresident =  "";
  IsBO = "X";
  IsDV = "X";
  IsCM = "X";
  Contr = -1;  
  IncludePlanned = "X";        
  BegDate = {curdate};
  EndDate = {curdate};
  OperCode = {Oper};
  OperName = "";
/*  IsExportToExel = "X";*/
  IsUseApp = "X";
  
END;     
      
VAR RepFormData = DLCLORDCOFormData();     

PRIVATE CONST /* Обработчики для нестандарных контролов */
      
      H_CLIENT        = 101, 
      H_CLIENT_NAME   = 102,
      H_ISBO          = 103,
      H_ISDV          = 104,
      H_ISCM          = 105,
      H_CONTR         = 106,
      H_OPERCODE      = 107,        
      H_ISFORM        = 108,
      H_FORMSUB       = 109,
      H_ISVID         = 110,
      H_VIDSUB        = 111; 

PRIVATE CONST SelCodeKind = 1;

PRIVATE CONST BO = 1,
              DV = 15,
              CM = 21;

PRIVATE MACRO DL_FldProc_FormSub( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if(FldRealValue == 0)
    FldShowValue = "";
  end;
  if((Cmd == DLG_KEY) AND (Key == KEY_F3))
     DL_ListNA( 3400, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), NULL );
  end;
  return CM_DEFAULT;        
END;

PRIVATE MACRO DL_FldProc_VidSub( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT )
  if(FldRealValue == 0)
    FldShowValue = "";
  end;
  if((Cmd == DLG_KEY) AND (Key == KEY_F3))

    return DL_ComboBox( "select t_Name2, t_PartyKind from dptkind_dbt ",
                        "t_Name2", "t_PartyKind", 
                        @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY()
                      );

  end; 
END;

PRIVATE MACRO DL_FldProc_IsFormVid( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, pos:INTEGER):INTEGER
  if((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
    if ( FldRealValue == 0)
        FldShowValue = "=";
        FldRealValue = "1";
        EnableFields( pThis.Data(), pos );

    elif(FldRealValue == 1)
        FldShowValue = "!";
        FldRealValue = "2";
    else
        FldShowValue = "";
        FldRealValue = "0";
        DisableFields( pThis.Data(), pos );
    end;
  end;
  return CM_DEFAULT;        
END;

PRIVATE MACRO DL_FldProc_IsFormVid1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     DL_FldProc_IsFormVid( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_DLCLORDCO_FORMSUB);
		if (FldRealValue==0)
			pThis.SetLinkedValue( H_FORMSUB, 0 );
		end;
  end;
END;

PRIVATE MACRO DL_FldProc_IsFormVid2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
     DL_FldProc_IsFormVid( pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_DLCLORDCO_VIDSUB);
	 	 if (FldRealValue==0)
			pThis.SetLinkedValue( H_VIDSUB, 0 );
	   end;
  end;
END;

var Party = TRecHandler("party.dbt");
/*ищем код субъекта*/
PRIVATE MACRO  GetPartCode( PartyID:integer )
  var sqlPartcode, PartCode;

  sqlPartcode = RSDCommand("select t_Code from dpartcode_dbt " +
                           " where t_PartyID = ? " +
                           "   and t_CodeKind = 1");

  sqlPartcode.addParam("", RSDBP_IN, PartyID);
  sqlPartcode.execute();

  PartCode = TRsbDataSet(sqlPartcode);
  if( PartCode.MoveNext() )
     return PartCode.Code;
  end;

  return "";
END;

/*является ли клиетом заданного вида обслуживания*/
PRIVATE MACRO IsClServKind( PartyID:integer, ServiceKind:integer )
  var sqlClient, ClientRNum;

  if( PartyID == -1 )
     return true;
  end;

  sqlClient = RSDCommand("select count(1) RNum from dclient_dbt " +
                         " where t_PartyID = ? " +
                         "   and t_ServiceKind = ? " +
                         "   and t_Department = " +string({OperDprt})+
                         "   and t_Branch = 0" );

  sqlClient.addParam("", RSDBP_IN, PartyID);
  sqlClient.addParam("", RSDBP_IN, ServiceKind);
  sqlClient.execute();

  ClientRNum = TRsbDataSet(sqlClient);
  if( ClientRNum.MoveNext() )
     if( ClientRNum.RNum > 0 )
        return true;
     end;
  end;

  return false;
END;

/*является ли договором заданного вида обслуживания*/
PRIVATE MACRO IsContrServKind( SfContrID:integer, ServiceKind:integer )
  var sqlSfcontr, Sfcontr;

  if( SfContrID == -1 )
     return true;
  end;

  sqlSfcontr = RSDCommand("select count(1) RNum from dsfcontr_dbt " +
                          " where t_ID = ? " +
                          "   and t_ServKind = ?");

  sqlSfcontr.addParam("", RSDBP_IN, SfContrID);
  sqlSfcontr.addParam("", RSDBP_IN, ServiceKind);
  sqlSfcontr.execute();

  Sfcontr = TRsbDataSet(sqlSfcontr);
  if( Sfcontr.MoveNext() )
     if( Sfcontr.RNum > 0 )
        return true;
     end;
  end;

  return false;
END;

/*выставим виды обслуживания для клиентов*/
PRIVATE MACRO SetServKind( pThis:DL_CPanel, PartyID:integer )
   if( PartyID > 0 )
        if( IsClServKind( PartyID, BO ) )
           pThis.SetLinkedValue( H_ISBO, "X" );
        else
           pThis.SetLinkedValue( H_ISBO, null );
        end;

        if( IsClServKind( PartyID, DV ) )
           pThis.SetLinkedValue( H_ISDV, "X" );
        else
           pThis.SetLinkedValue( H_ISDV, null );
        end;

        if( IsClServKind( PartyID, CM ) )
           pThis.SetLinkedValue( H_ISCM, "X" );
        else
           pThis.SetLinkedValue( H_ISCM, null );
        end;

   else/*ВСЕ*/
      pThis.SetLinkedValue( H_ISBO, "X" );
      pThis.SetLinkedValue( H_ISDV, "X" );
      pThis.SetLinkedValue( H_ISCM, "X" );
   end;
END;


PRIVATE MACRO DLUSR_FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientName = "";
  var ServKind = TArray();

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_CLIENT_NAME, "");
        SetServKind( pThis, FldRealValue );
     else
        if( not ПолучитьСубъекта( FldRealValue, Party ) )
            FldShowValue = GetPartCode(FldRealValue);
            pThis.SetLinkedValue( H_CLIENT_NAME, Party.rec.ShortName);
            SetServKind( pThis, FldRealValue );
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_CLIENT_NAME, "" );
     pThis.SetLinkedValue( H_CONTR, null );
     SetServKind( pThis, FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if( pThis.GetFieldValue( PNFLD_DLCLORDCO_IsBO ) == "X" )
        ServKind[ServKind.Size] = BO;
     end;
             
     if( pThis.GetFieldValue( PNFLD_DLCLORDCO_IsDV ) == "X" )
        ServKind[ServKind.Size] = DV;
     end;

     if( pThis.GetFieldValue( PNFLD_DLCLORDCO_IsCM ) == "X" )
        ServKind[ServKind.Size] = CM;
     end;

     if(DL_ListClientByServKind( @ClientName, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ServKind, false, {OperDprt} ))
        pThis.SetLinkedValue( H_CLIENT_NAME, ClientName );
        SetServKind( pThis, FldRealValue );
     end;

  end;
  return CM_DEFAULT;
END;

/*Переключатель*/
PRIVATE MACRO DLUSR_FldProc_BO( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( IsClServKind( pThis.GetFieldValue( PNFLD_DLCLORDCO_ClientCode ), BO ) )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
           if( IsContrServKind(pThis.GetFieldValue( PNFLD_DLCLORDCO_Contr ), BO) )
              pThis.SetLinkedValue( H_CONTR, null );
           end;
        end;
     end;                      
  end;
  return CM_DEFAULT;
END;

/*Переключатель*/
PRIVATE MACRO DLUSR_FldProc_DV( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( IsClServKind( pThis.GetFieldValue( PNFLD_DLCLORDCO_ClientCode ), DV ) )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
           if( IsContrServKind(pThis.GetFieldValue( PNFLD_DLCLORDCO_Contr ), DV) )
              pThis.SetLinkedValue( H_CONTR, null );
           end;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_CM( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( IsClServKind( pThis.GetFieldValue( PNFLD_DLCLORDCO_ClientCode ), CM ) )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
           if( IsContrServKind(pThis.GetFieldValue( PNFLD_DLCLORDCO_Contr ), CM) )
              pThis.SetLinkedValue( H_CONTR, null );
           end;
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_NumContr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientID =  -1;
  var ServKind = TArray();

  ClientID = pThis.GetFieldValue( PNFLD_DLCLORDCO_ClientCode );
  
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )                           
        FldShowValue = STR_ALLVALUE;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) AND (ClientID > 0) )

     if( pThis.GetFieldValue( PNFLD_DLCLORDCO_IsBO ) == "X" )
        ServKind[ServKind.Size] = BO;
     end;
     if( pThis.GetFieldValue( PNFLD_DLCLORDCO_IsDV ) == "X" )
        ServKind[ServKind.Size] = DV;
     end;
     if( pThis.GetFieldValue( PNFLD_DLCLORDCO_IsCM ) == "X" )
        ServKind[ServKind.Size] = CM;
     end;

     DL_ListSfContrByServKind( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ClientID, ServKind ); 
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) DL_ClOrdCentrRep_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_DLCLORDCO_ClientCode,     H_CLIENT,           @DLUSR_FldProc_Client, RepFormData.ClientID);         
     AddFieldProc( 0, PNFLD_DLCLORDCO_ClientName,     H_CLIENT_NAME,      @Default, "");

     AddFieldProc( 0, PNFLD_DLCLORDCO_ISFORM,         H_ISFORM,           @DL_FldProc_IsFormVid1,     RepFormData.IsForm);
     AddFieldProc( 0, PNFLD_DLCLORDCO_FORMSUB,        H_FORMSUB,          @DL_FldProc_FormSub,        RepFormData.FormSub, 38, 10);
     AddFieldProc( 0, PNFLD_DLCLORDCO_ISVID,          H_ISVID,            @DL_FldProc_IsFormVid2,     RepFormData.IsVid);
     AddFieldProc( 0, PNFLD_DLCLORDCO_VIDSUB,         H_VIDSUB,           @DL_FldProc_VidSub,         RepFormData.VidSub , 38, 11);
     AddFieldProc( 0, PNFLD_DLCLORDCO_NORESIDENT,     DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,       RepFormData.Noresident);

     AddFieldProc( 0, PNFLD_DLCLORDCO_IsBO,           H_ISBO,             @DLUSR_FldProc_BO, RepFormData.IsBO );
     AddFieldProc( 0, PNFLD_DLCLORDCO_IsDV,           H_ISDV,             @DLUSR_FldProc_DV, RepFormData.IsDV );
     AddFieldProc( 0, PNFLD_DLCLORDCO_IsCM,           H_ISCM,             @DLUSR_FldProc_CM, RepFormData.IsCM );

     AddFieldProc( 0, PNFLD_DLCLORDCO_Contr,          H_CONTR,            @DLUSR_FldProc_NumContr, RepFormData.Contr);
     AddFieldProc( 0, PNFLD_DLCLORDCO_IncludePlanned, DL_PNFLD_CHECKBOX,@DL_FldProc_CheckBox, RepFormData.IncludePlanned);         

     AddFieldProc( 0, PNFLD_DLCLORDCO_BegDate,        DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate,  RepFormData.BegDate );
     AddFieldProc( 0, PNFLD_DLCLORDCO_EndDate,        DL_PNFLD_ENDDATE,   @DL_FldProc_EndDate,    RepFormData.EndDate );

     AddFieldProc( 0, PNFLD_DLCLORDCO_OperCode,       H_OPERCODE,         @DL_FldProc_OperName, RepFormData.OperCode);         
     AddFieldProc( 0, PNFLD_DLCLORDCO_OperName,       DL_PNFLD_OPERNAME,  @Default, "");         

/*     AddFieldProc( 0, PNFLD_DLCLORDCO_IsExportToExel, DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,   RepFormData.IsExportToExel);*/
     AddFieldProc( 0, PNFLD_DLCLORDCO_Apostr,         DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,   RepFormData.IsUseApp);

	 if(this.GetFieldValue( PNFLD_DLCLORDCO_ISFORM ) == 0)
     DisableFields( This.Data(), PNFLD_DLCLORDCO_FORMSUB ); 
     end;
	 if(this.GetFieldValue( PNFLD_DLCLORDCO_ISVID ) == 0)
     DisableFields( This.Data(), PNFLD_DLCLORDCO_VIDSUB ); 
     end;

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;
  
  initDL_CPanel( "dlrep.lbr", "CLORDCO" ); 
END;

