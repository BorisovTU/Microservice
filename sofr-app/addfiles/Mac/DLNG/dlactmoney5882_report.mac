/**
 @file      dlactmoney5882_report.mac
 @brief     Отчет " Акты сверки наличия денежных средств " - класс отчета

 #menu 
  - БО ЦБ\Внутренний учет\Отчеты\Акты сверки\Акты сверки наличия денежных средств

 # tag
 - code_type:GUI

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |10.03.2025 |Велигжанин А.В.|DEF-84131                                       |Print_NoDiff(), Вывод отчета при отсутствии данных
 |10.03.2025 |Велигжанин А.В.|DEF-83842                                       |Print_Tables(), обработка ошибок
 |06.03.2025 |Велигжанин А.В.|DEF-83842                                       |Другая шапка на листах продолжения
 |28.02.2025 |Велигжанин А.В.|DEF-83387                                       |TABLE_ROW_LIMIT = 55000
 |13.02.2025 |Велигжанин А.В.|BOSS-5882_BOSS-7917                             |CreateData(), формирование данных для отчета
 |12.02.2025 |Велигжанин А.В.|BOSS-5882_BOSS-7906                             |Доработка для вывода на нескольких листах
 |31.01.2025 |Велигжанин А.В.|BOSS-5882_BOSS-7711                             |Реализация по BOSS-5882

*/

import "DlRepForm_h.mac", "globals.mac", "dlmisc.mac";
                                   
PRIVATE CONST TABLE_ROW_LIMIT = 55000;

/**
 @brief 	Класс отчета
 @param[in] 	p_BegDate	дата начала периода
 @param[in] 	p_EndDate	дата завершения периода
 @param[in] 	p_FIID 		ID финансового инструмента
 @param[in] 	p_DiffFlag	если "X", то выводятся только различия, иначе выводится всё
*/
CLASS (DL_CReportTemplate) DL_ActMoney5882_Report(p_BegDate, p_EndDate, p_FIID, p_DiffFlag)

  var
    m_CurrSheetName = "", m_space = 0
    , m_rowLimitCounter: integer = 0 		// счетчик записей в Excel-файле
    , m_SheetCount: integer = 0			// счетчик листов по одной дате
    , m_PrintFlag = false			// выполняется заполнение Excel-листа
    , m_NoData = true				// флаг наличия данных
    , m_RS
    , m_NRecs
    , m_RepID: string = "100"
    , m_FIID: integer = p_FIID
    , m_BegDate:date = p_BegDate
    , m_EndDate:date = p_EndDate
    , m_Restdate: date = date(0,0,0)
    , m_DiffFlag = p_DiffFlag
  ;

  private const TEMPLATE_SHEET_NAME = "Tmpl";

  /**
   @brief 	Задает режим вывода отчета
  */
  PRIVATE MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END; // PrintMode()

  /**
   @brief 	Событие начала формирования отчета
  */
  PRIVATE MACRO BeginReport()
     Dl_CallInsertStat(PrintMode());
     CreateTotalBook();
  END; // BeginReport()

  /**
   @brief 	Очистка данных отчета
  */
  PRIVATE MACRO ClearRep()
     var sql, exec;
      sql =  "BEGIN ACTMONEY_utl.ClearRep(?); END; ";
      
     exec = RSDCommand(sql);
     exec.AddParam( "", RSDBP_IN, m_RepID);
     exec.execute();
  END; // ClearRep()


  /**
   @brief 	Событие завершения формирования отчета
  */
  PRIVATE MACRO EndReport()
     SaveTotalBook();
     ClearRep();
  END; // EndReport()

  /**
   @brief 	Регистратор таблицы
  */
  PRIVATE MACRO Reg_Table()
     VAR x_TableHeader: string = "N1_TableHeader";
     IF(m_SheetCount > 0)
        x_TableHeader = "N1_TableHeader1";
     END;
     CopyAllSheetInTotalBook(TEMPLATE_SHEET_NAME, false, x_TableHeader, m_space, m_CurrSheetName, true);
     RegisterTable( "N1_Table", null,
                    "N1_Client", "N1_SfContr", "N1_DateContr", "N1_InnerAcc", "N1_GbAcc", "N1_InnerRest", "N1_GbRest", "N1_Diff", "N1_Details");
  END; // Reg_Table()

  /**
   @brief 	Шапка отчета
  */
  PRIVATE MACRO Print_MainHeader()
     var MainHeader = "Заголовок отчета";
     // DEF-83842 Доработка для печати шапки страницы, полная шапка только на нулевой вкладке, дальше -- не нужна
     IF(m_SheetCount == 0)
       PrintFormatString( MainHeader, "N1_curdate", string({curdate}) );
       PrintFormatString( MainHeader, "N1_restdate", string(m_Restdate) );
       CopyAllSheetInTotalBook(TEMPLATE_SHEET_NAME, false, "N1_MainHeader", m_space, m_CurrSheetName, true);
     END;
  END; // Print_MainHeader()

  /**
   @brief 	Вывод подвала
  */
  PRIVATE MACRO PrintMainFooter()
    var Footer = "\n\n"; 
  END; // PrintMainFooter()

  /**
   @brief 	Завершение вывода таблицы
  */
  PRIVATE MACRO EndPrintTable()
    if( m_PrintFlag )
       EndTable();
       CopyAllSheetInTotalBook(TEMPLATE_SHEET_NAME, false, GetTableRange(), 0, m_CurrSheetName, true);
       PrintMainFooter();
       m_NoData = false;
    end;
  END; // EndPrintTable()

  /**
   @brief 	Возвращает наименование вкладки
   @param[in] 	p_RestDate	дата расчета остатка
   @param[in] 	p_SheetCount	количество вкладок на дату расчета остатка
  */
  PRIVATE MACRO GetSheetName(p_RestDate, p_SheetCount)
    var dd, mm, yyyy, delim = "-";
    DateSplit(p_RestDate, dd, mm, yyyy);
    RETURN string(dd:o:2) + delim + string(mm:o:2) + delim + string(yyyy:o:4) + IIF(p_SheetCount > 0, "("+p_SheetCount+")", "");
  END; // GetSheetName()

  /**
   @brief 	Вывод строчки отчета
   @param[in] 	p_ClientName	наименование клиента
   @param[in] 	p_SfContr	наименование договора
   @param[in] 	p_DateContr	дата договора
   @param[in] 	p_InnerAcc	счет ВУ
   @param[in] 	p_GbAcc		счет БУ
   @param[in] 	p_InnerRest	остаток по счету ВУ
   @param[in] 	p_GbRest	остаток по счету БУ
   @param[in] 	p_Diff		расхождение между счетами ВУ и БУ
   @param[in] 	p_Details	причина расхождения
   @param[in] 	p_RestDate	дата расчета остатка
  */
  PRIVATE MACRO Print_Line( p_ClientName, p_SfContr, p_DateContr, p_InnerAcc, p_GbAcc, p_InnerRest, p_GbRest, p_Diff, p_Details, p_RestDate )

     // дата еще неизвестна, производим установку даты
     IF(m_RestDate == date(0,0,0))
       m_RestDate = p_RestDate;
       m_rowLimitCounter = 0;
       m_SheetCount = 0;
       m_PrintFlag = false;
     END;

     // если дата изменилась, завершаем вывод текущей таблицы
     IF(p_RestDate != m_RestDate) 
       m_RestDate = p_RestDate;
       m_rowLimitCounter = 0;
       EndPrintTable();
       m_SheetCount = 0;
       m_PrintFlag = false;
     END;

     // если количество строк на вкладке переваливает через границу
     IF((m_PrintFlag == true) and (m_rowLimitCounter >= TABLE_ROW_LIMIT))
       m_rowLimitCounter = 0;
       EndPrintTable();
       m_SheetCount = m_SheetCount + 1;
       m_PrintFlag = false;
     END;

     // если флаг печати сброшен, выводим шапку и регистрируем таблицу
     IF( m_PrintFlag == false )
        m_CurrSheetName = GetSheetName(m_RestDate, m_SheetCount);
        UseNewSheetInTotalBook();
        Print_MainHeader();
        Reg_Table();
        m_PrintFlag = true;
     END;

     // собственно печать строки
     PrintTableLine(                    
       "N1_Client", 		p_ClientName,  null
       , "N1_SfContr", 		p_SfContr,  null
       , "N1_DateContr",  	p_DateContr,  null
       , "N1_InnerAcc", 	p_InnerAcc,  null
       , "N1_GbAcc", 		p_GbAcc,  null
       , "N1_InnerRest", 	p_InnerRest,  null
       , "N1_GbRest", 		p_GbRest,  null
       , "N1_Diff", 		p_Diff,  null
       , "N1_Details", 		p_Details,  null
     );

     m_rowLimitCounter = m_rowLimitCounter + 1;
  END; // Print_Line()


  /**
   @brief 	Шапка при отсутствии данных
  */
  PRIVATE MACRO Print_NoDiff()
     var MainHeader = "Заголовок отчета";
     PrintFormatString( MainHeader, "N1_curdate", string({curdate}) );
     PrintFormatString( MainHeader, "N1_restdate", string(m_Restdate) );
     CopyAllSheetInTotalBook(TEMPLATE_SHEET_NAME, false, "N1_MainHeader", m_space, m_CurrSheetName, true);
     CopyAllSheetInTotalBook(TEMPLATE_SHEET_NAME, false, "N1_NoDiff", m_space, m_CurrSheetName, true);
  END; // Print_NoDiff()


  /**
   @brief 	Вывод таблицы
  */
  PRIVATE MACRO Print_Tables()
    var i = 0;
    if(m_NRecs == 0)
      // DEF-84131 Вывод отчета при отсутствии данных
      var m_Stop = false;
      m_RestDate = m_BegDate;
      while (m_Stop == false)
        m_CurrSheetName = GetSheetName(m_RestDate, 0);
        Print_NoDiff();
        m_RestDate = m_RestDate + 1;
        if(m_RestDate > m_EndDate)
          m_Stop = true;
        end;  
      end;
    else
      InitProgress(m_NRecs, "Тестовый отчет", "Печать данных");
      while( m_RS.MoveNext() )
        i = i + 1;
        Print_Line(
           m_RS.Value("t_ClientName")
           , m_RS.Value("t_SfContr")
           , m_RS.Value("t_DateContr")
           , m_RS.Value("t_InnerAcc")
           , m_RS.Value("t_GbAcc")
           , m_RS.Value("t_InnerRest")
           , m_RS.Value("t_GbRest")
           , m_RS.Value("t_Diff")
           , m_RS.Value("t_Details")
           , m_RS.Value("t_RestDate")
        );
        UseProgress(i);
      end;
      RemProgress();
      EndTable();
      CopyAllSheetInTotalBook(TEMPLATE_SHEET_NAME, false, GetTableRange(), 0, m_CurrSheetName, true);
    end;
  OnError( e )
    // DEF-83842 Обработка ошибки
    RunError("Ошибка при обработке строки "+string(i));
    RemProgress();
    EndTable();
    CopyAllSheetInTotalBook(TEMPLATE_SHEET_NAME, false, GetTableRange(), 0, m_CurrSheetName, true);
  END; // Print_Tables()

  /**
   @brief 	Выборка данных. Заполняет m_RS
  */
  PRIVATE MACRO Get_Data()
    var x_Sql =   
      " SELECT "
      + " p.t_shortname AS t_ClientName, s.t_number AS t_SfContr, to_char(s.t_datebegin, 'dd-mm-yyyy') AS t_DateContr "
      + " , r.t_inneraccount AS t_InnerAcc, r.t_gbaccount AS t_GbAcc, r.t_restdate "
      + " , r.t_innerrest, r.t_gbrest, ABS(r.t_innerrest - r.t_gbrest) AS t_Diff "
      + " , NVL(r.t_details, ' ') AS t_details "
      + " FROM dlactmoney5882_dbt partition (p" + string(m_RepID) + ") r "
      + " INNER JOIN dparty_dbt p ON (p.t_partyid = r.t_partyid) "
      + " INNER JOIN dsfcontr_dbt s ON (s.t_id = r.t_sfcontrid) "
      + " ORDER BY r.t_restdate, r.t_partyid, r.t_sfcontrid "
    ;

    m_NRecs = SQL_GetNRecs(x_Sql);
    var cmd = RSDCommand(x_Sql);

    cmd.Execute(x_Sql);
    m_RS = RsdRecordSet( cmd, rsdval_client, rsdval_static );
    m_RS.Open();
  END; // Get_Data()

  /**
   @brief 	Выборка данных. Заполняет m_RS
  */
  PRIVATE MACRO Print_Data()
    Print_Tables();
  END; // Print_Data()

  /**
   @brief 	Ожидание завершения
  */
  PRIVATE MACRO WaitingProcess( p_Cnt, p_Title )
    var 
      sql, cmd, DS, x_Progress :integer = 0;
     
    Sql = "SELECT count(*) AS cnt from itt_parallel_exec partition (P"+string(m_RepID)+") r where str02 is not null";   
    cmd = RsdCommand(sql);

    InitProgress(p_Cnt, p_Title, p_Title);
    while (x_Progress < p_Cnt)
      RslWait(2000);
      cmd.Execute();
      DS = TRsbDataSet(cmd);
      if(Ds.movenext) 
        x_Progress = int(DS.cnt);
      else
        x_Progress = p_Cnt;
      end;
      UseProgress(x_Progress);
    end;
    RemProgress();
  END; // WaitingProcess()


  /**
   @brief 	Формирование данных для отчета
  */
  PRIVATE MACRO CreateData()
     var sql, exec, x_Cnt: integer;
     InitProgress(-1, "Формирование заданий для отчета-сверки денежных средств по счетам ВУ и БУ " + m_BegDate + " - " + m_EndDate);
      sql =  "DECLARE "
          + "BEGIN "
          + "    ACTMONEY_utl.CreateData(?,?,?,?,?,?);"
          + "END; ";
      
     exec = RSDCommand(sql);
     exec.AddParam( "", RSDBP_IN, m_BegDate);
     exec.AddParam( "", RSDBP_IN, m_EndDate);
     exec.AddParam( "", RSDBP_IN, m_FIID);
     exec.AddParam( "", RSDBP_IN, m_DiffFlag);
     exec.addParam( "p_RepID", RSDBP_OUT, V_STRING, 100);
     exec.addParam( "p_Cnt", RSDBP_OUT, V_INTEGER);
     exec.execute();

     m_RepID = exec.value("p_RepID");
     x_Cnt = exec.value("p_Cnt");
     RemProgress();

     WaitingProcess( x_Cnt, "Формирование данных для отчета-сверки денежных средств по счетам ВУ и БУ " + m_BegDate + " - " + m_EndDate );
  END; // CreateData

  /**
   @brief 	Создание отчета
  */
  PRIVATE MACRO Create()
    SetActiveSheet( TEMPLATE_SHEET_NAME );
    CreateData();
    Get_Data();
    Print_Data();
  END; // Create()

  initDL_CReportTemplate( null, "Report_Dlactmoney5882.xlsx", true, null, null, true, true );

END;  // class DL_ActMoney5882_Report
