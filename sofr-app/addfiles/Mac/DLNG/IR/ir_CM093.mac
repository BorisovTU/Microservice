/*
    $Name: ir_CM093.mac
    $Module:   Репозитарий
    $Description:  Анкета о состоянии обязательств по договорам 
*/

import "ir_CMBase.mac";
/*DAN*/
private macro set_conract_ir(DocKind, DocID, ContractIR)
  var sql,cmd,rs;
  if(ContractIR!="")
    sql = "UPDATE ddl_repozdeal_dbt " +
          "   SET t_contract_ir = ? " +
          " WHERE     t_dockind = ? " +
          "       AND t_docid = ? " ;
    cmd = RSDCommand(sql);
    cmd.addparam("",rsdbp_in,ContractIR);
    cmd.addparam("",rsdbp_in,DocKind);
    cmd.addparam("",rsdbp_in,DocID);
    cmd.execute;
  end;
end;
/*DAN*/

class (CMBase) CM093 ( _isRecon, _conveyerID, _ir_op : object)
  InitCMBase();

  var m_CounterParty = TArray(); /*массив субъектов контагентов*/
  m_isRecon     = _isRecon;
  m_conveyerID  = _conveyerID;
  m_ir_op       = _ir_op;
  m_MessageID   = REPOS_CM093;

  macro addCMAll(ir_generalinf     : TRecHandler,
                 TradeRepository   : TRecHandler,
                 Party1            : TRecHandler,
                 Party2            : TRecHandler,
                 Sender            : TRecHandler,
                 cm                : TRecHandler,
                 Instruments       : TRecHandler,
                 GrDealID          : integer,
                 TemplNum          : integer,
                 DealParmForReport : variant)
    
    if ( m_isRecon )
      ir_generalinf.rec.MessageType = "CM001";
    end;
    arrsize = m_GeneralInfArr.size;
    if( arrsize == 0 ) // для сводного для всей пачки один generalinf   ///// тут определяется настройкой (одна сообщение в СРС или несколько )
      m_GeneralInfArr[arrsize] = TRecHandler("ir_generalinf.dbt");
      copy(m_GeneralInfArr[arrsize], ir_generalinf);
      
      m_RepActionArr[arrsize] = GetReportAction( TemplNum );
      m_RepAccGrDocArr[arrsize] = TRecHandler("dlgrdoc.dbt");
      m_RepAccGrDocArr[arrsize].Clear();
      m_RepAccGrDocArr[arrsize].rec.GrDealID= IIF(m_conveyerID == IR_REPACC_SVODSRS093, 0, GrDealID);   //// поправить IR_REPACC_SVODSRS для нового конвейера
      m_RepAccGrDocArr[arrsize].rec.DocKind = REPOS_GENERALINF;
      m_RepAccGrDocArr[arrsize].rec.ServDocKind = m_ir_op.rec.DocKind;
      m_RepAccGrDocArr[arrsize].rec.ServDocID   = m_ir_op.rec.ID;
      
      arrsize = m_PartyArr.size;
      m_PartyArr[arrsize] = TRecHandler("ir_party.dbt");
      TradeRepository.rec.PartyType = "TradeRepository";
      copy(m_PartyArr[arrsize], TradeRepository);
      
      arrsize = m_PartyArr.size;
      m_PartyArr[arrsize] = TRecHandler("ir_party.dbt");
      Party1.rec.PartyType= "Party1";
      copy(m_PartyArr[arrsize], Party1);
      
      arrsize = m_PartyArr.size;
      m_PartyArr[arrsize] = TRecHandler("ir_party.dbt");
      Party2.rec.PartyType= "Party2";
      copy(m_PartyArr[arrsize], Party2);
      
      arrsize = m_PartyArr.size;
      m_PartyArr[arrsize] = TRecHandler("ir_party.dbt");
      Sender.rec.PartyType= "Sender";
      copy(m_PartyArr[arrsize], Sender);
    else 
      if (ir_generalinf.rec.RStatus == 2) //На случай если по текущему последующему ошибка, то поправим статус в сводном
        if (m_GeneralInfArr.size > 0)
          m_GeneralInfArr[0].rec.RStatus = ir_generalinf.rec.RStatus;
          m_GeneralInfArr[0].rec.RStatusName = ir_generalinf.rec.RStatusName;
        end;
      end;
    end;

    addCM(cm);
    addcmGrDoc(GrDealID);
    addInstrument(Instruments);
    m_RepDealParmArr[arrsize] = DealParmForReport;
  end;

  macro insertAll
    var ir_generalinfCache = RsbSQLInsert("ir_generalinf.dbt");
    var realIDs = TArray();
    var err = 0;
    var i = 0;
    var m_ReconMsgID = 0;
    
    ir_generalinfCache.AddRecordArray(m_GeneralInfArr);
    ir_generalinfCache.AddReturning("t_InternalMessageID", V_INTEGER);
    
    if( not ir_generalinfCache.insert() )
      err = 1;
      RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(m_RepDealParmArr[arrsize].DealID, m_RepDealParmArr[arrsize].DealKind, "", "", "Ошибка при сохранении данных сообщения", "", 0);
    else
      ir_generalinfCache.GetReturning(realIDs);
      i = 0;
      while( i < m_GeneralInfArr.size ) 
        m_GeneralInfArr[i].rec.InternalMessageID = realIDs[i];
        m_RepAccGrDocArr[i].rec.DocID = realIDs[i];
        
        m_PartyArr[i*4].rec.InternalMessageID   = realIDs[i];  ///////// 4??
        m_PartyArr[i*4+1].rec.InternalMessageID = realIDs[i];
        m_PartyArr[i*4+2].rec.InternalMessageID = realIDs[i];
        m_PartyArr[i*4+3].rec.InternalMessageID = realIDs[i];
        
        var l_093 = 0;
        while( l_093 < m_cmArr.size )
            m_cmArr[l_093].rec.InternalMessageID = realIDs[i];
            l_093 = l_093 + 1;
        end;
        
        l_093 = 0;
        while( l_093 < m_instrumentsArr.size )
            m_instrumentsArr[l_093].rec.InternalMessageID = realIDs[i];
            l_093 = l_093 + 1;
        end;
        
        l_093 = 0;
        while( l_093 < m_CounterParty.size )
            m_CounterParty[l_093].rec.InternalMessageID = realIDs[i];
            l_093 = l_093 + 1;
        end;
        
        m_ReconMsgID = realIDs[i];
        
        /* добавить запись в лог о вставке СРС */
        if ( not m_isRecon )
            m_RepDataArr[m_RepDataArr.size] = CIR_RepAccRepParm(
                    m_RepActionArr[i],
                    m_GeneralInfArr[i].rec.DealCode,
                    m_GeneralInfArr[i].rec.MessageType,
                    DL_GetNameAlg(ALG_GENINF_RSTATUS, m_GeneralInfArr[i].rec.RStatus),
                    m_RepDealParmArr[i].DealID, 
                    m_RepDealParmArr[i].DealKind);
        end;
        i = i + 1;
      end;
    end;
    
    insertParty;
    
    if( (not err) and (m_cmArr.size > 0) )
        /* Вставляем cm093 */
        var ir_cmCache  = RsbSQLInsert("ir_" + m_MessageType + ".dbt");
        ir_cmCache.AddRecordArray(m_cmArr);
        ir_cmCache.AddReturning("t_id", V_INTEGER);
       
        if( not ir_cmCache.insert() )
            err = 1;
            RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(m_RepDealParmArr.DealID, m_RepDealParmArr.DealKind, "", "", "Ошибка при сохранении данных экономических параметров", "", 0);
        else
            realIDs.size = 0;
            ir_cmCache.GetReturning(realIDs);
            i = 0;
            var delta093 =  m_cmGrDocArr.size;
            while( i < delta093 )
                m_cmGrDocArr[i].rec.DocID = realIDs[i];
                // вставка   REPOS_GENERALINF для 093(для стороки графика)
                m_cmGrDocArr[i+delta093] = TRecHandler("dlgrdoc.dbt");
                m_cmGrDocArr[i+delta093].Clear();
                m_cmGrDocArr[i+delta093].rec.GrDealID    = m_cmGrDocArr[i].rec.GrDealID;
                m_cmGrDocArr[i+delta093].rec.DocKind     = REPOS_GENERALINF;//вставить константу     ?4858
                m_cmGrDocArr[i+delta093].rec.ServDocKind = m_cmGrDocArr[i].rec.ServDocKind;
                m_cmGrDocArr[i+delta093].rec.ServDocID   = m_cmGrDocArr[i].rec.ServDocID;
                m_cmGrDocArr[i+delta093].rec.DocID       = m_GeneralInfArr[0].rec.InternalMessageID;
                i = i + 1;
            end;
        end;
    end;
    
    insertDlgrdocCache;
    
    return err;
  end;

  macro ЗаполнениеCM (cm093 : @TrecHandler,
                      DataSet,
                      GenAgr,
                      first,
                      error,
                      field,
                      FD,
                      ir_generalinf : @TrecHandler,
                      i,
                      DealParmForReport)
    var iParty = TRecHandler("party");
    var stat = 0;
    var Country;
    if (first)
        if((DataSet.TemplNum == DLGR_TEMPL_CLOSECONTR) or (DataSet.TemplNum == DLGR_TEMPL_CLOSECONTRWTHREQUEST) )
          cm093.rec.TradeOblStatus   = "T";
        elif((DataSet.TemplNum == DLGR_TEMPL_EXECDELAYMSG) or (DataSet.TemplNum == DLGR_TEMPL_EXECDELAYMSGWTHREQUEST) )
          cm093.rec.TradeOblStatus   = "C";
        elif((DataSet.TemplNum == DLGR_TEMPL_EARLYEXECMSG) or (DataSet.TemplNum == DLGR_TEMPL_EARLYEXECMSGWTHREQUEST) )
          cm093.rec.TradeOblStatus   = "TD";
        elif((DataSet.TemplNum == DLGR_TEMPL_EXECHOLDMSG) or (DataSet.TemplNum == DLGR_TEMPL_EXECHOLDMSGWTHREQUEST) )
          cm093.rec.TradeOblStatus   = "P";
        elif((DataSet.TemplNum == DLGR_TEMPL_REJECTIONMSG) or (DataSet.TemplNum == DLGR_TEMPL_REJECTIONMSGWTHREQUEST) )
          cm093.rec.TradeOblStatus   = "D";
        elif((DataSet.TemplNum == DLGR_TEMPL_NETTINGSUSP) or (DataSet.TemplNum == DLGR_TEMPL_NETTINGSUSPWTHREQUEST) )
          cm093.rec.TradeOblStatus   = "SO";
        end;

        cm093.rec.TradeIdent = DataSet.CONTRACT_IR;

//DAN Дополнительный шанс исправить ошибку с пустым репозитарным номером для пользователя
        if( cm093.rec.TradeIdent == "" )
            GetString(cm093.rec.TradeIdent,"На сделке|"+ DataSet.DealCode +"|отсутствует репозитарный номер присвоенный НРД. Введите его вручную:",70);
            set_conract_ir(FD.deal.rec.dockind,FD.deal.rec.id,cm093.rec.TradeIdent); 
        end;
//DAN 



        if( cm093.rec.TradeIdent == "" )
            stat = 1;
            RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, ir_generalinf.rec.MessageType, "Не задан код договора, присвоенный репозитарием", "TradeId", GetReportAction(DataSet.TemplNum));
        end;

        cm093.rec.DealID      = DataSet.DocID;
        cm093.rec.BofficeKind = DataSet.DocKind;
        cm093.rec.TemplNum    = DataSet.TemplNum;
        
    end;
    if((error=="Не задан код договора, присвоенный репозитарием") and
        (field=="TradeId"))
        cm093.rec.TradeIdent = DataSet.CONTRACT_IR;
        if( cm093.rec.TradeIdent == "" )
            stat = 1;
            RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, ir_generalinf.rec.MessageType, "Не задан код договора, присвоенный репозитарием", "TradeId", GetReportAction(DataSet.TemplNum));
        end;
    end;


    return stat;
  end;
end;