/*
$Name:             ir_partyimport.mac
$Module:           Репозитарий
$Description:      Выполнение процедуры импорта участников репозитария
*/
import xls_parser, cb_sql;
import ir_partyimpcheck, ir_partyimpload, ir_partyimprep;

private const CONST_PATHFILES = "РЕПОЗИТАРИЙ\\КАТАЛОГ ИМПОРТА УЧАСТНИКОВ";
private const CONST_ROWNUM = 3;
private const CONST_FILENAME = "directory_repository";
private var FullFileName, FileDate, FileTime;
private var objExcel:CExcel = CExcel();
private file partyimprepo("ir_partyimprepo.dbt") write;

// Убираем в строке лишние пробелы: слева, справа и повторяющиеся
private macro TrimString( str: String, maxlen: Integer )
    str = Trim(str);
    if( str == "-" )
        str = "";
    elif( str != "" )
        var len = 0;
        while( len != strlen(str) )
            len = strlen(str);
            str = strsubst(str, "  ", " ");
        end;
    end;
    if( ValType(maxlen) != V_UNDEF )
        if( maxlen > 0 )
            str = Trim(substr( str, 1, maxlen ));
        end;
    end;
    return str;
end; /* TrimString */

// Конвертация значения - признака типа Y/N
private macro ConvertSign( str: String )
    str = Trim(str);
    if( strlen(str) > 1 )
        str = substr(str, 1, 1);
    end;
    return str;
end; /* ConvertChar */

/* Транзакция сохранения данных из файла */
private macro SaveTrnProc
    file partyimprec("ir_partyimprec.dbt") write;

    partyimprepo.FileName = FullFileName;
    partyimprepo.FileDate = FileDate;
    partyimprepo.FileTime = FileTime;
    partyimprepo.Status = PROCSTATUS_Save;
    Update( partyimprepo );
    
    var i = CONST_ROWNUM;
    var iLastRow = objExcel.Sheet.Cells.SpecialCells(objExcel.xlLastCell).Row; // Последняя непустая строка
    InitProgress(iLastRow - i + 1, "... Обработка файла " + objExcel.Book.Name, "Обработка файла " + objExcel.Book.Name );
    while( i <= iLastRow )
        var RICode = TrimString(objExcel.Sheet.Range("A"+string(i)).text, 12);
        // SCR 270924 Необходимо исключить из процедуры импорта закрузку/сверку данных об участниках - физ. лицах
        if( substr(RICode, 1, 3) != CONST_PERSONRICODE ) // Участник НЕ является физическим лицом
            clearRecord( partyimprec );
            partyimprec.IMPREPID = partyimprepo.id;
            partyimprec.Status = RECSTATUS_None;
            partyimprec.RECID = i - CONST_ROWNUM + 1;
            partyimprec.RICode = RICode;
            partyimprec.FullName =      TrimString(objExcel.Sheet.Range("D"+string(i)).text, 320);
            partyimprec.NoReporting =  ConvertSign(objExcel.Sheet.Range("N"+string(i)).text);
            partyimprec.IsResident =   ConvertSign(objExcel.Sheet.Range("O"+string(i)).text);
            partyimprec.ShortName =     TrimString(objExcel.Sheet.Range("C"+string(i)).text, 320);
            partyimprec.FullNameEng =   TrimString(objExcel.Sheet.Range("F"+string(i)).text, 320);
            partyimprec.IsCentrCount = ConvertSign(objExcel.Sheet.Range("G"+string(i)).text);
            partyimprec.IsClearOrg =   ConvertSign(objExcel.Sheet.Range("H"+string(i)).text);
            partyimprec.INN =           TrimString(objExcel.Sheet.Range("I"+string(i)).text, 20);
            partyimprec.OGRN =          TrimString(objExcel.Sheet.Range("J"+string(i)).text, 15);
            partyimprec.BIC =           TrimString(objExcel.Sheet.Range("K"+string(i)).text, 9);
            partyimprec.SwiftBIC =      TrimString(objExcel.Sheet.Range("L"+string(i)).text, 11);
            partyimprec.LEI =           TrimString(objExcel.Sheet.Range("M"+string(i)).text, 35);
            insert(partyimprec);
        end;
        UseProgress(i);
        i = i + 1;
    end;
    RemProgress();
end; /* SaveTrnProc */

/* Контроль очередности загрузки файлов (по дате/времени, содержащихся в имени файла) */
private macro CheckActualFile( )
    var res = true;
    var cmd = RsdCommand(
        "SELECT t.t_FileDate, t.t_FileTime, t.t_FileName "
          "FROM DIR_PARTYIMPREPO_DBT t WHERE t.t_FileDate = ("
               "SELECT MAX (mm.t_FileDate) FROM DIR_PARTYIMPREPO_DBT mm) "
      "ORDER BY t.t_FileTime DESC");
    cmd.execute();
    var rsd = TRsbDataSet(cmd);
    if( rsd.MoveNext() )
        if( (FileDate < rsd.rec.FileDate) OR ((FileDate == rsd.rec.FileDate) AND (FileTime <= rsd.rec.FileTime)) )
            if( MsgBoxEx("Дата/время загружаемого файла " + FullFileName + 
                         "|равна (или меньше) даты/времени загруженного ранее файла " + rsd.rec.FileName + 
                         ".|Продолжить?", MB_YES + MB_NO, IND_NO) == IND_NO )
                res = false;
            end;
        end;
    end;
    return res;
end; /* CheckActualFile */

/* Извлечение времени/даты из названия файла по формату "directory_repository_YYYYMMDD_HHММ.xls" */
private macro ExtractDateTimeFromFileName( FileName )
    var res = true;
    var position = -1;
    var fdate = Date(0,0,0);
    var ftime = Time(0,0);

    if( strlen(FileName) > 34 )
        position = Index( FileName, "_" );
        if( position > 0 )
            position = Index( FileName, "_", position + 1 );
            if( position <= 0 )
                res = false;
            end;
        else
            res = false;
        end;
    else    
        res = false;
    end;
    
    if( res == true )
        if( StrIsNumber(substr( FileName, position + 1, 8 )) )
            fdate = Date( int(substr( FileName, position + 7, 2 )),
                          int(substr( FileName, position + 5, 2 )),
                          int(substr( FileName, position + 1, 4 )) );
        else
           res = false;
        end;
    end;

    if( res == true )
        if( StrIsNumber(substr( FileName, position + 10, 4 )) )
            ftime = Time( int(substr( FileName, position + 10, 2 )),
                          int(substr( FileName, position + 12, 2 )) );
        else
            res = false;
        end;
    end;
    
    FileDate = fdate;
    FileTime = ftime;
    return res;
end; /* GetDateTimeFromFileName */

/* Проверка состава загруженного файла */
private macro CheckFileFormat
    var A1 = TrimString(objExcel.Sheet.Range("A1").text);
    var B1 = TrimString(objExcel.Sheet.Range("B1").text);
    var C1 = TrimString(objExcel.Sheet.Range("C1").text);
    var D1 = TrimString(objExcel.Sheet.Range("D1").text);
    var E1 = TrimString(objExcel.Sheet.Range("E1").text);
    var F1 = TrimString(objExcel.Sheet.Range("F1").text);
    var G1 = TrimString(objExcel.Sheet.Range("G1").text);
    var H1 = TrimString(objExcel.Sheet.Range("H1").text);
    var I1 = TrimString(objExcel.Sheet.Range("I1").text);
    var J1 = TrimString(objExcel.Sheet.Range("J1").text);
    var K1 = TrimString(objExcel.Sheet.Range("K1").text);
    var L1 = TrimString(objExcel.Sheet.Range("L1").text);
    var M1 = TrimString(objExcel.Sheet.Range("M1").text);
    var N1 = TrimString(objExcel.Sheet.Range("N1").text);
    var O1 = TrimString(objExcel.Sheet.Range("O1").text);
    if( (A1 == "Идентификационный код") and
        (B1 == "Назначение идентификационного кода") and
        (C1 == "Краткое официальное наименование") and 
        (D1 == "Полное официальное наименование") and 
        (E1 == "Краткое официальное наименование на английском") and 
        (F1 == "Полное официальное наименование на английском") and 
        (G1 == "Является центральным контрагентом (Если да - 'Y')") and 
        (H1 == "Является клиринговой организацией (Если да - 'Y')") and 
        (I1 == "ИНН для юр.лиц, резидентов") and 
        (J1 == "ОГРН") and 
        (K1 == "Банковский БИК") and 
        (L1 == "Код Swift BIC") and 
        (M1 == "LEI Международный код идентификации юридического лица") and 
        (N1 == "Признак отказа от предоставления сведений (Если сведения не предоставляются - <Y>. Если сведения предоставляются то <N>)") and 
        (O1 == "Резидент РФ") )
        return true;
    end;
    return false;
end; /* CheckFileFormat */

private macro GetMaskFile( fdate, ftime )
    var MaskFile :string = CONST_FILENAME;
    
    if( partyimprepo.FileDate == Date(0,0,0) )
        MaskFile = MaskFile + "*";
    else
        var day, month, year;
        datesplit( partyimprepo.FileDate, day, month, year );
        MaskFile = MaskFile + "_" + string(year);
        if( month < 10 )
            MaskFile = MaskFile + "0" + string(month);
        else
            MaskFile = MaskFile + string(month);
        end;
        if( day < 10 )
            MaskFile = MaskFile + "0" + string(day);
        else
            MaskFile = MaskFile + string(day);
        end;
        
        if( partyimprepo.FileTime == Time(0,0) )
            MaskFile = MaskFile + "*";
        else
            var hour, mint;
            timeSplit( partyimprepo.FileTime, hour, mint );
            if( hour < 10 )
                MaskFile = MaskFile + "_0" + string(hour);
            else
                MaskFile = MaskFile + "_" + string(hour);
            end;
            if( mint < 10 )
                MaskFile = MaskFile + "0" + string(mint);
            else
                MaskFile = MaskFile + string(mint);
            end;
        end;
    end;

    return MaskFile + ".xls*";
end; /* GetMaskFile */

/* Парсинг файла в указанном каталоге с максимальной датой */
macro ParsingXlsFiles( objExcel:CExcel, PathFiles:string, FilePrefix:string )
    var ListFiles:TDirList = TDirList.Sort();
    if(substr(PathFiles, strlen(PathFiles)) != "\\")
      PathFiles = PathFiles + "\\"; 
    end;

    ListFiles.List( PathFiles + GetMaskFile, "F" ); // Перебираем все файлы в каталоге по маске
    var stat = true;

    if( ListFiles.Count > 0 )
        var f = ListFiles.Count - 1;
        FullFileName = PathFiles + ListFiles.Name(f);
        var errorText = "Ошибка: файл " + FullFileName;
        
        if( not ExtractDateTimeFromFileName( ListFiles.Name(f) ) )
            msgbox(errorText + "|не загружен. Неверный формат имени файла");
            stat = false;
        end;

        if( stat == true )
            stat = CheckActualFile( );
        end;

        if( stat == true )
            if( not objExcel.Open( FullFileName ) )
                msgbox(errorText + "|не удалось открыть. Возможно файл поврежден или находится в режиме \"Защищенного просмотра\"");
                stat = false;
            end;
        end;

        if( stat == true )
            if( not CheckFileFormat )
                msgbox(errorText + "|имеет не поддерживаемую структуру");
                stat = false;
            end;
        end;
        
        if( stat == true )
            if( ProcessTrn( 1, "SaveTrnProc" ) )
                partyimprepo.Status = PROCSTATUS_Save;
            else
                msgbox(errorText + "|не загружен. Ошибка сохранения данных из файла");
                stat = false;
            end;
        end;
        
    else
        msgbox("Файл в каталоге " + PathFiles + " не найден");
        stat = false;
    end;

    return stat;
end; /* ParsingXlsFiles */


macro RunPartyImport( ImpRepId, isFormReport )
    var result = true;
    partyimprepo.id = ImpRepId;
    result = GetEQ( partyimprepo );
    
    if( (result == true) AND (partyimprepo.Status == PROCSTATUS_None) AND
        ((partyimprepo.CheckSign) OR (partyimprepo.LoadSign)) )
        var PathFiles = "";
        var stat;
        GetRegistryValue(CONST_PATHFILES, V_STRING, @PathFiles, stat);

        if( (stat == 0) AND (PathFiles != "") )
            objExcel.CreateApplication();
            result = ParsingXlsFiles(objExcel, PathFiles);
        else
            msgbox("Не задано значение настройки|\"" + CONST_PATHFILES + "\"");
        end;
    end;

    if( (result == true) AND (partyimprepo.Status == PROCSTATUS_Save) AND
        ((partyimprepo.CheckSign) OR (partyimprepo.LoadSign)) )
        if( CheckParty( partyimprepo.Id ) )
            partyimprepo.Status = PROCSTATUS_Check;
        else
            msgbox("Ошибка: файл " + partyimprepo.FileName + "|не обработан. Ошибка при сверке полученных данных");
            result = false;
        end;
    end;

    if( (result == true) AND (partyimprepo.Status == PROCSTATUS_Check) AND (partyimprepo.LoadSign) )
        if( not LoadParty( partyimprepo.Id ) )
            msgbox("Ошибка: файл " + partyimprepo.FileName + "|не обработан. Ошибка при загрузке полученных данных");
            result = false;
        end;
    end;

    if( (result == true) AND (isFormReport) AND
        ((partyimprepo.Status == PROCSTATUS_Check) OR (partyimprepo.Status == PROCSTATUS_Load)) )
        RunReport( partyimprepo.Id );
    end;

    return result;
end;
