/*
$Name:             ir_partyimpcheck.mac
$Module:           Репозитарий
$Description:      Сверка участников репозитария в рамках процедуры импорта
*/
import ir_ptimpdef;
private var PARTYIMPREPID;

/* Получение ИД субъекта по наименованию */
private macro FindPartyIdByName( id, FullName, legalForm )
    var cmdstr, cmd, rsd, st;
    if( legalForm == PTLEGF_INST )
        cmdstr = "SELECT t_partyid FROM dparty_dbt " +
                  "WHERE t_name = ? AND t_legalForm = " + PTLEGF_INST + 
                   " AND t_locked = chr(0)";
        cmd =  RSDCommand(cmdstr);
        cmd.addParam("", RSDBP_IN, FullName);
        cmd.execute();
        rsd = TRsbDataSet(cmd);
        st = rsd.MoveNext();
        if( st == true )
            setparm(0, rsd.rec.PartyId);
        end;
    else
        var name1, name2, name3;
        ExtractFIO( FullName, name1, name2, name3 );
        cmdstr = "SELECT t.t_personid FROM dpersn_dbt t "
                  "INNER JOIN dparty_dbt p ON p.t_partyid = t.t_personid "
                  "WHERE p.t_locked = chr(0) "
                    "AND UPPER(t.t_name1) = ? AND UPPER(t.t_name2) = ? AND UPPER(t.t_name3) = ?";
        cmd =  RSDCommand(cmdstr);
        cmd.addParam("", RSDBP_IN, strupr(name1));
        cmd.addParam("", RSDBP_IN, strupr(name2));
        cmd.addParam("", RSDBP_IN, strupr(name3));
        cmd.execute();
        rsd = TRsbDataSet(cmd);
        st = rsd.MoveNext();
        if( st == true )
            setparm(0, rsd.rec.PersonId);
        end;
    end;
    return st;
end; /* FindPartyIdByName */

/* Получение ИД субъекта по краткому наименованию. 
По SCR274017 псевдоним вида 14 "Краткое наименование участника репозитария" */
private macro FindPartyIdByShortName( id, ShortNname, legalForm )
    var cmdstr = "SELECT t.t_partyid FROM dparty_dbt t "
                  "WHERE t.t_legalForm = ? AND t.t_locked = chr(0) "
                   " AND t.t_partyid IN (SELECT ptn.t_partyid FROM dpartyname_dbt ptn "
                                        " WHERE ptn.t_nametypeid = 14 AND ptn.t_name = ?)";
    var cmd =  RSDCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, legalForm);
    cmd.addParam("", RSDBP_IN, ShortNname);
    cmd.execute();
    var rsd = TRsbDataSet(cmd);
    var st = rsd.MoveNext();
    if( st == true )
        setparm(0, rsd.rec.PartyId);
    end;
    return st;
end; /* FindPartyIdByShortName */

/* Получение ИД субъекта по коду */
private macro FindPartyIdByCode( id, code, codekind, legalForm )
    var cmdstr =
        "SELECT t.t_objectid FROM dobjcode_dbt t "
          "INNER JOIN DPARTY_dbt p ON p.t_partyid = t.t_objectid "
        " WHERE     p.t_legalForm = ?"
              " AND p.t_locked = chr(0)"
              " AND t.t_objecttype = 3" // OBJTYPE_PARTY
              " AND t.t_codekind = ?"
              " AND t.t_code = ?"
              " AND t.t_bankclosedate = TO_DATE('01.01.0001', 'dd.mm.yyyy') "
         " ORDER BY t.t_bankdate DESC";
    var cmd =  RSDCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, legalForm);
    cmd.addParam("", RSDBP_IN, codekind);
    cmd.addParam("", RSDBP_IN, code);
    cmd.execute();
    var rsd = TRsbDataSet(cmd);
    var st = rsd.MoveNext();
    if( st == true )
        setparm(0, rsd.rec.ObjectId);
    end;
    return st;
end; /* FindPartyIdByCode */

/* Определение кода для субъекта и его статуса сверки */
private macro DefineCodeAndStatus( codeValue, checkStatus, newCodeValue, partyid, codekind )
    // Поиск заданного кода среди незакрытых кодов субъекта
    var cmdstr =
        "SELECT 1 FROM dobjcode_dbt t"
        " WHERE     t.t_objecttype = 3" // OBJTYPE_PARTY
              " AND t.t_codekind = ?"
              " AND t.t_objectid = ?"
              " AND t.t_code = ?"
              " AND t.t_bankclosedate = TO_DATE('01.01.0001', 'dd.mm.yyyy')";
    var cmd =  RSDCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, codekind);
    cmd.addParam("", RSDBP_IN, partyid);
    cmd.addParam("", RSDBP_IN, newCodeValue);
    cmd.execute();
    var rsd = TRsbDataSet(cmd);
    if( rsd.MoveNext() )
        codeValue = newCodeValue;
        checkStatus = PARAMSTATUS_Equal;
    else
        // Поиск активного кода субъекта
        cmdstr =
            "SELECT t.t_code FROM dobjcode_dbt t"
            " WHERE     t.t_objecttype = 3" // OBJTYPE_PARTY
                  " AND t.t_codekind = ?"
                  " AND t.t_objectid = ?"
                  " AND t.t_state = 0"
                  " AND t.t_bankdate ="
                         " (SELECT MAX (cd.t_bankdate) FROM dobjcode_dbt cd"
                           " WHERE     cd.t_objecttype = t.t_objecttype"
                                 " AND cd.t_codekind = t.t_codekind"
                                 " AND cd.t_objectid = t.t_objectid"
                                 " AND cd.t_state = 0)";
        cmd =  RSDCommand(cmdstr);
        cmd.addParam("", RSDBP_IN, codekind);
        cmd.addParam("", RSDBP_IN, partyid);
        cmd.execute();
        rsd = TRsbDataSet(cmd);
        if( rsd.MoveNext() )
            codeValue = rsd.rec.Code;
            if( codeValue == newCodeValue )
                checkStatus = PARAMSTATUS_Equal;
            else
                checkStatus = PARAMSTATUS_NotEqual;
            end;
        else
            codeValue = "";
            checkStatus = PARAMSTATUS_NotFound;
        end;
    end;
    setparm(0, codeValue);
    setparm(1, checkStatus);
end; /* DefineCodeAndStatus */

/* Определение наличия роли для субъекта и статуса сверки */
private macro DefineRoleAndStatus( signValue, checkStatus, newSignValue, partyid, kind )
    if( IsExistsPTRole( partyid, kind ) )
        signValue = "Y";
    else
        signValue = "N";
    end;
    if( signValue == newSignValue )
        checkStatus = PARAMSTATUS_Equal;
    elif( newSignValue == "Y" )
        checkStatus = PARAMSTATUS_NotFound;
    end;
    setparm(0, signValue);
    setparm(1, checkStatus);
end; /* DefineRoleAndStatus */

/* Определение краткого наименования (псевдоним 14) субъекта и статуса сверки */
private macro DefineShortNameAndStatus( shortName, checkStatus, newShortName, partyid )
    var cmdstr = "SELECT 1 FROM dpartyname_dbt WHERE t_nametypeid = 14 AND t_partyid = ? AND t_name = ?";
    var cmd =  RSDCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, partyid);
    cmd.addParam("", RSDBP_IN, newShortName);
    cmd.execute();
    var rsd = TRsbDataSet(cmd);
    if( rsd.MoveNext() )
        shortName = newShortName;
        checkStatus = PARAMSTATUS_Equal;
    else
        cmdstr = "SELECT t_name, count(1) over() t_count FROM dpartyname_dbt WHERE t_nametypeid = 14 AND t_partyid = ?";
        cmd =  RSDCommand(cmdstr);
        cmd.addParam("", RSDBP_IN, partyid);
        cmd.execute();
        rsd = TRsbDataSet(cmd);
        shortName = "";
        if( rsd.MoveNext() )
            if( rsd.rec.Count == 1 )
                shortName = rsd.rec.Name;
                if( shortName == newShortName )
                    checkStatus = PARAMSTATUS_Equal;
                else
                    checkStatus = PARAMSTATUS_NotEqual;
                end;
            else
                checkStatus = PARAMSTATUS_NotDefined;
            end;
        else
            checkStatus = PARAMSTATUS_NotFound;
        end;
    end;
    setparm(0, shortName);
    setparm(1, checkStatus);
end; /* DefineShortNameAndStatus */

/* Сохранение успешно найденного идентификатора для участника и обновление статусов */
private macro SavePartyId( recId, imprepId, id, attrName )
    var cmdstr = 
        "UPDATE DIR_PARTYIMPREC_DBT " +
        "SET T_STATUS = " + RECSTATUS_Check + ", t_PartyId = ?, t_" + attrName + "DB = t_" + attrName + 
             ", t_Status" + attrName + " = " + PARAMSTATUS_Ident +
        " WHERE t_RECID = ? AND t_ImpRepID = ?";
    var cmd = RsdCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, id);
    cmd.addParam("", RSDBP_IN, recId);
    cmd.addParam("", RSDBP_IN, imprepId);
    cmd.execute();
end; /* SavePartyId */

/* Определение ИД участников репозитария в справочнике субъектов */
private macro DefinePartyId( rsd )
    var id, legalForm;
    var attrName;
    var statFound = false;

    if( rsd.rec.RICode != "" )
        statFound = FindPartyIdByCode( id, rsd.rec.RICode, PTCK_RIC, legalForm );
        attrName = "RICode";
    end;
    
    if( substr(rsd.rec.RICode, 1, 3) == CONST_PERSONRICODE )// Участник является физическим лицом
        legalForm = PTLEGF_PERSN;

        if( (statFound == false) and (rsd.rec.FullName != "") )
            statFound = FindPartyIdByName( id, rsd.rec.FullName, legalForm );
            attrName = "FullName";
        end;
    else                                                    // Участник НЕ является физическим лицом
        legalForm = PTLEGF_INST;

        if( (statFound == false) and (rsd.rec.LEI != "") )
            statFound = FindPartyIdByCode( id, rsd.rec.LEI, PTCK_LEI, legalForm );
            attrName = "LEI";
        end;
        if( (statFound == false) and (rsd.rec.INN != "") )
            statFound = FindPartyIdByCode( id, rsd.rec.INN, PTCK_INN, legalForm );
            attrName = "INN";
        end;
        if( (statFound == false) and (rsd.rec.BIC != "") )
            statFound = FindPartyIdByCode( id, rsd.rec.BIC, PTCK_BIC, legalForm );
            attrName = "BIC";
        end;
        if( (statFound == false) and (rsd.rec.SwiftBIC != "") )
            statFound = FindPartyIdByCode( id, rsd.rec.SwiftBIC, PTCK_SWIFT, legalForm );
            attrName = "SwiftBIC";
        end;
        if( (statFound == false) and (rsd.rec.ShortName != "") )
            statFound = FindPartyIdByShortName( id, rsd.rec.ShortName, legalForm );
            attrName = "ShortName";
        end;
        if( (statFound == false) and (rsd.rec.FullName != "") )
            statFound = FindPartyIdByName( id, rsd.rec.FullName, legalForm );
            attrName = "FullName";
        end;
    end;

    if( statFound == true )
        SavePartyId( rsd.rec.RECID, rsd.rec.ImpRepID, id, attrName );
    end;
end; /* DefinePartyId */

/* Определение параметров участников репозитария для их дальнейшей сверки */
private macro DefinePartyParam( rsd )
    file PTImpRec("ir_partyimprec.dbt") key 0 write;
    PTImpRec.RecID = rsd.rec.RecID;
    PTImpRec.ImpRepID = PARTYIMPREPID;
    var stat = GetEQ( PTImpRec );

    if( stat == true )
        PTImpRec.PartyNotUpdate = rsd.rec.PartyNotUpdate;
        if( PTImpRec.RICode != "" )
            if( PTImpRec.StatusRICode != PARAMSTATUS_Ident )
                DefineCodeAndStatus( PTImpRec.RICodeDB, PTImpRec.StatusRICode, PTImpRec.RICode, PTImpRec.PartyId, PTCK_RIC );
            end;
            if( not IsExistsPTRole(PTImpRec.PartyId, PTK_REPOSPRT) )
                PTImpRec.Status = RECSTATUS_NotRepo;
            end;
        end;
        if( (PTImpRec.StatusFullName != PARAMSTATUS_Ident) and (PTImpRec.FullName != "") )
            if( PTImpRec.FullName == rsd.rec.PartyName )
                PTImpRec.StatusFullName = PARAMSTATUS_Equal;
            elif( rsd.rec.PartyName == "" )
                PTImpRec.StatusFullName = PARAMSTATUS_NotFound;
            else
                PTImpRec.StatusFullName = PARAMSTATUS_NotEqual;
            end;
            PTImpRec.FullNameDB = rsd.rec.PartyName;
        end;
        
        if( rsd.rec.legalform == PTLEGF_INST ) // Участник НЕ является физическим лицом
            if( (PTImpRec.StatusShortName != PARAMSTATUS_Ident) and (PTImpRec.ShortName != "") )
                DefineShortNameAndStatus( PTImpRec.ShortNameDB, PTImpRec.StatusShortName, PTImpRec.ShortName, PTImpRec.PartyId );
            end;
            if( PTImpRec.FullNameEng != "" )
                if( PTImpRec.FullNameEng == rsd.rec.PartyLatName )
                    PTImpRec.StatusFullNameEng = PARAMSTATUS_Equal;
                elif( rsd.rec.PartyLatName == "" )
                    PTImpRec.StatusFullNameEng = PARAMSTATUS_NotFound;
                else
                    PTImpRec.StatusFullNameEng = PARAMSTATUS_NotEqual;
                end;
                PTImpRec.FullNameEngDB = rsd.rec.PartyLatName;
            end;
            if( PTImpRec.IsResident != "" )
                if( rsd.rec.PartyNotResident )
                    PTImpRec.IsResidentDB = "N";
                else
                    PTImpRec.IsResidentDB = "Y";
                end;
                if( PTImpRec.IsResident == PTImpRec.IsResidentDB )
                    PTImpRec.StatusIsResident = PARAMSTATUS_Equal;
                else
                    PTImpRec.StatusIsResident = PARAMSTATUS_NotEqual;
                end;
            end;
            //NoReporting
            if( PTImpRec.IsCentrCount != "" )
                DefineRoleAndStatus( PTImpRec.IsCentrCountDB, PTImpRec.StatusIsCentrCount, PTImpRec.IsCentrCount, PTImpRec.PartyId, PTK_CENTRALCONTR );
            end;
            if( PTImpRec.IsClearOrg != "" )
                DefineRoleAndStatus( PTImpRec.IsClearOrgDB, PTImpRec.StatusIsClearOrg, PTImpRec.IsClearOrg, PTImpRec.PartyId, 63 );
            end;
            if( PTImpRec.OGRN != "" )
                DefineCodeAndStatus( PTImpRec.OGRNDB, PTImpRec.StatusOGRN, PTImpRec.OGRN, PTImpRec.PartyId, PTCK_OGRN );
            end;
            if( (PTImpRec.StatusINN != PARAMSTATUS_Ident) and (PTImpRec.INN != "") )
                DefineCodeAndStatus( PTImpRec.INNDB, PTImpRec.StatusINN, PTImpRec.INN, PTImpRec.PartyId, PTCK_INN );
            end;
            if( (PTImpRec.StatusBIC != PARAMSTATUS_Ident) and (PTImpRec.BIC != "") )
                DefineCodeAndStatus( PTImpRec.BICDB, PTImpRec.StatusBIC, PTImpRec.BIC, PTImpRec.PartyId, PTCK_BIC );
            end;
            if( (PTImpRec.StatusSwiftBIC != PARAMSTATUS_Ident) and (PTImpRec.SwiftBIC != "") )
                DefineCodeAndStatus( PTImpRec.SwiftBICDB, PTImpRec.StatusSwiftBIC, PTImpRec.SwiftBIC, PTImpRec.PartyId, PTCK_SWIFT );
            end;
            if( (PTImpRec.StatusLEI != PARAMSTATUS_Ident) and (PTImpRec.LEI != "") )
                DefineCodeAndStatus( PTImpRec.LEIDB, PTImpRec.StatusLEI, PTImpRec.LEI, PTImpRec.PartyId, PTCK_LEI );
            end;
        end;

        PTImpRec.RICodeDB = substr(PTImpRec.RICodeDB, 1, 12);
        PTImpRec.ShortNameDB = substr(PTImpRec.ShortNameDB, 1, 320);
        PTImpRec.FullNameDB = substr(PTImpRec.FullNameDB, 1, 320);
        PTImpRec.FullNameEngDB = substr(PTImpRec.FullNameEngDB, 1, 320);
        PTImpRec.INNDB = substr(PTImpRec.INNDB, 1, 20);
        PTImpRec.OGRNDB = substr(PTImpRec.OGRNDB, 1, 15);
        PTImpRec.BICDB = substr(PTImpRec.BICDB, 1, 9);
        PTImpRec.SwiftBICDB = substr(PTImpRec.SwiftBICDB, 1, 11);
        PTImpRec.LEIDB = substr(PTImpRec.LEIDB, 1, 35);
        
        if( (PTImpRec.Status == RECSTATUS_Check) and
            (PTImpRec.StatusRICode != PARAMSTATUS_NotEqual) and (PTImpRec.StatusRICode != PARAMSTATUS_NotFound) and
            (PTImpRec.StatusShortName != PARAMSTATUS_NotEqual) and (PTImpRec.StatusShortName != PARAMSTATUS_NotFound) and
//            (PTImpRec.StatusFullName != PARAMSTATUS_NotEqual) and (PTImpRec.StatusFullName != PARAMSTATUS_NotFound) and
//            (PTImpRec.StatusFullNameEng != PARAMSTATUS_NotEqual) and (PTImpRec.StatusFullNameEng != PARAMSTATUS_NotFound) and
            (PTImpRec.StatusIsResident != PARAMSTATUS_NotEqual) and (PTImpRec.StatusIsResident != PARAMSTATUS_NotFound) and
            (PTImpRec.StatusIsCentrCount != PARAMSTATUS_NotEqual) and (PTImpRec.StatusIsCentrCount != PARAMSTATUS_NotFound) and
            (PTImpRec.StatusIsClearOrg != PARAMSTATUS_NotEqual) and (PTImpRec.StatusIsClearOrg != PARAMSTATUS_NotFound) and
            (PTImpRec.StatusINN != PARAMSTATUS_NotEqual) and (PTImpRec.StatusINN != PARAMSTATUS_NotFound) and
            (PTImpRec.StatusOGRN != PARAMSTATUS_NotEqual) and (PTImpRec.StatusOGRN != PARAMSTATUS_NotFound) and
            (PTImpRec.StatusBIC != PARAMSTATUS_NotEqual) and (PTImpRec.StatusBIC != PARAMSTATUS_NotFound) and
            (PTImpRec.StatusSwiftBIC != PARAMSTATUS_NotEqual) and (PTImpRec.StatusSwiftBIC != PARAMSTATUS_NotFound) and
            (PTImpRec.StatusLEI != PARAMSTATUS_NotEqual) and (PTImpRec.StatusLEI != PARAMSTATUS_NotFound) and
            (PTImpRec.StatusNoReporting != PARAMSTATUS_NotEqual) and (PTImpRec.StatusNoReporting != PARAMSTATUS_NotFound) )
            PTImpRec.Status = RECSTATUS_Equal;
        end;
        stat = Update( PTImpRec );
    end;
    return stat;
end; /* DefinePartyParam */

/* Присвоение статуса "дубликаты" для дублирующихся записей файла */
private macro SetSkipedRec
    // Выбираем дублирующиеся названия участников
    var cmdNames = RsdCommand(
        "SELECT t_fullname "
         " FROM (SELECT COUNT (1) cnt, t_fullname "
                 " FROM DIR_PARTYIMPREC_DBT WHERE t_imprepid = ? "
     " GROUP BY t_fullname) "
        " WHERE cnt > 1");
    cmdNames.addParam("", RSDBP_IN, PARTYIMPREPID);
    cmdNames.execute();
    var rsdNames = TRsbDataSet(cmdNames);
    rsdNames.NullConversion = true;
    while( rsdNames.MoveNext() )
        // По каждой группе находим наиболее удачно определенную запись по ее статусу, считая эту запись главной
        var mainRecCmd = RsdCommand(
            "SELECT T_RecID FROM DIR_PARTYIMPREC_DBT WHERE t_ImpRepID = ? AND t_fullname = ?"
            " ORDER BY T_Status DESC, T_RecID");
        mainRecCmd.addParam("", RSDBP_IN, PARTYIMPREPID);
        mainRecCmd.addParam("", RSDBP_IN, rsdNames.rec.FullName);
        mainRecCmd.execute();
        var mainRecrsd = TRsbDataSet(mainRecCmd);
        mainRecrsd.NullConversion = true;
        if( mainRecrsd.MoveNext() )
            // Остальным записям присваиваем статус "дубликаты", чтобы обрабатывать их иначе при загрузке и выводе отчета
            var cmdupd = RsdCommand(
                "UPDATE DIR_PARTYIMPREC_DBT SET T_STATUS = " + RECSTATUS_Duplicate + 
                " WHERE t_ImpRepID = ? AND t_fullname = ? AND t_recid <> ?");
            cmdupd.addParam("", RSDBP_IN, PARTYIMPREPID);
            cmdupd.addParam("", RSDBP_IN, rsdNames.rec.FullName);
            cmdupd.addParam("", RSDBP_IN, mainRecrsd.rec.RecId);
            cmdupd.execute();
        end;
    end;
end; /* SetSkipedRec */

/* Транзакция сверки участников репозитария */
private macro CheckTrnProc
    var cmdstr = "SELECT * FROM DIR_PARTYIMPREC_DBT WHERE t_ImpRepID = ?";
    var countRecs = getCountRecs( cmdstr, PARTYIMPREPID );
    InitProgress( countRecs, " Поиск участников репозитария в Справочнике субъектов ...",
                  "Поиск участников репозитария в Справочнике субъектов");
    var i = 1;
    var cmd = RsdCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, PARTYIMPREPID);
    cmd.execute();
    var rsd = TRsbDataSet(cmd);
    rsd.NullConversion = true;
    while( rsd.MoveNext() )
        DefinePartyId( rsd );
        UseProgress(i);
        i = i + 1;
    end;
    RemProgress();

    cmdstr = 
        "SELECT t.t_RecId, " +
               "p.t_shortName as PartyShortName, " +
               "p.t_name as PartyName, " +
               "p.t_latname as PartyLatName, " +
               "p.t_notresident as PartyNotResident, " +
               "p.t_legalform, " +
               "p.t_notupdateonimport as PartyNotUpdate " +
          "FROM DIR_PARTYIMPREC_DBT t INNER JOIN DPARTY_dbt p ON p.t_partyid = t.t_partyid " +
         "WHERE t.t_ImpRepID = ? AND t.t_Status = " + RECSTATUS_Check;
    countRecs = getCountRecs( cmdstr, PARTYIMPREPID );
    InitProgress( countRecs, " Сверка параметров участников репозитария ...",
                  "Сверка параметров участников репозитария");
    i = 1;
    cmd = RsdCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, PARTYIMPREPID);
    cmd.execute();
    rsd = TRsbDataSet(cmd);
    rsd.NullConversion = true;
    while( rsd.MoveNext() )
        if( not DefinePartyParam( rsd ) )
            AbortTrn( );
        end;
        UseProgress(i);
        i = i + 1;
    end;
    RemProgress();
    
    SetSkipedRec;

    var cmdupd = RsdCommand("UPDATE DIR_PARTYIMPREPO_DBT SET T_STATUS = " + PROCSTATUS_Check + " WHERE t_ID = ?");
    cmdupd.addParam("", RSDBP_IN, PARTYIMPREPID);
    cmdupd.execute();
end; /* CheckTrnProc */


macro CheckParty( ImpRepId )
    PARTYIMPREPID = ImpRepId;
    return ProcessTrn( 1, "CheckTrnProc" );
end;
