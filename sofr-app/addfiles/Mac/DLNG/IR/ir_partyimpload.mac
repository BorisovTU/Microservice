/*
$Name:             ir_partyimpload.mac
$Module:           Репозитарий
$Description:      Загрузка участников репозитария в рамках процедуры импорта
*/
import ir_ptimpdef;
private file PartyImp("ir_partyimprepo.dbt") key 0 write;
private var PARTYIMPREC_RSD;
private var note;

private macro GetPersnShortName( name1, name2, name3 )
    var shName = name1;
    if( name2 != "" )
        shName = shName + " " + substr(name2,1,1) + ".";
    end;
    if( name3 != "" )
        shName = shName + " " + substr(name3,1,1) + ".";
    end;
    return shName;
end; /* GetPersnShortName */

private macro UpdatePTImpRec( partyid, status )
    var cmdstr = "UPDATE DIR_PARTYIMPREC_DBT SET T_STATUS = ?";
    if( partyid > 0 )
        cmdstr = cmdstr + ", T_PARTYID = ?";
    end;
    if( note != "" )
        cmdstr = cmdstr + ", T_NOTE = ?";
    end;
    cmdstr = cmdstr + " WHERE t_recid = ? AND t_imprepid = ?";
    var cmd = RsdCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, status);
    if( partyid > 0 )
        cmd.addParam("", RSDBP_IN, partyid);
    end;
    if( note != "" )
        cmd.addParam("", RSDBP_IN, note);
    end;
    cmd.addParam("", RSDBP_IN, PARTYIMPREC_RSD.rec.RecId);
    cmd.addParam("", RSDBP_IN, PARTYIMPREC_RSD.rec.ImpRepID);
    cmd.execute();
end; /* UpdatePTImpRec */

private macro InsertPTShortName( Partyid, Shortname )
    var cmdstr = "INSERT INTO dpartyname_dbt (t_name, t_partyid, t_nametypeid) VALUES (?, ?, 14)";
    var cmd = RsdCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, shortname);
    cmd.addParam("", RSDBP_IN, partyid);
    cmd.execute();
end; /* InsertPTShortName */

/* Присвоение краткого наименования субъекту при обновлении данных */
private macro SetPTShortName( IsExistsDiff, Partyid, ParamStatus, Shortname )
    var stat = true;
    if( ((((ParamStatus == PARAMSTATUS_NotEqual) or (ParamStatus == PARAMSTATUS_NotDefined)) and (PartyImp.ReplaceParmSign == "X")) OR 
         (((ParamStatus == PARAMSTATUS_NotFound) or (ParamStatus == PARAMSTATUS_NotDefined)) and (PartyImp.AddParmSign == "X"))) )
        if( ParamStatus == PARAMSTATUS_NotDefined )
            note = "Ошибка определения краткого наименования субъекта";
            stat = false;
        else
            setparm( 0, true );
            if( ParamStatus == PARAMSTATUS_NotEqual )
                var cmdstr = "UPDATE dpartyname_dbt SET t_name = ? WHERE t_nametypeid = 14 AND t_partyid = ?";
                var cmd = RsdCommand(cmdstr);
                cmd.addParam("", RSDBP_IN, shortname);
                cmd.addParam("", RSDBP_IN, partyid);
                cmd.execute();
            elif( ParamStatus == PARAMSTATUS_NotFound )
                InsertPTShortName( Partyid, Shortname );
            end;
        end;
    end;
    return stat;
end; /* SetPTShortName */

/* Присвоение кода субъекту при обновлении данных */
private macro SetPartyCode( IsExistsDiff, PtCodeObj, ParamStatus, CodeKind, Code, CodeName )
    var stat = true;
    if( (((ParamStatus == PARAMSTATUS_NotEqual) and (PartyImp.ReplaceParmSign == "X")) OR 
         ((ParamStatus == PARAMSTATUS_NotFound) and (PartyImp.AddParmSign == "X"))) )
        setparm( 0, true );
        stat = PtCodeObj.SetCode( CodeKind, Code, PartyImp.FileDate );
        if( stat == false )
            note = "Ошибка присвоения субъекту кода " + CodeName;
        end;
    end;
    return stat;
end; /* SetPartyCode */

/* Присвоение роли субъекту при обновлении данных */
private macro SetPTRoleForCode( IsExistsDiff, PartyObj, ParamStatus, PTRole, PTRoleName )
    var stat = true;
    if( (((ParamStatus == PARAMSTATUS_NotEqual) and (PartyImp.ReplaceParmSign == "X")) OR 
         ((ParamStatus == PARAMSTATUS_NotFound) and (PartyImp.AddParmSign == "X"))) )
        setparm( 0, true );
        stat = PartyObj.AddOwn( PTRole );
        if( stat == false )
            note = "Ошибка присвоения субъекту роли \"" + PTRoleName + "\"";
        end;
    end;
    return stat;
end; /* SetPTRoleForCode */

/* Транзакция обновления данных субъекта в справочнике */
private macro UpdatePARTYTrnProc
    var cmd = RsdCommand(
        "SELECT t_RICode, t_StatusRICode FROM DIR_PARTYIMPREC_DBT " +
         "WHERE t_ImpRepID = ? AND t_fullname = ? AND t_status = " + RECSTATUS_Duplicate);
    cmd.addParam("", RSDBP_IN, PARTYIMPREC_RSD.rec.ImpRepID);
    cmd.addParam("", RSDBP_IN, PARTYIMPREC_RSD.rec.FullName);
    cmd.execute();
    var duplRsd = TRsbDataSet(cmd);
    var PartyObj = RsbParty(PARTYIMPREC_RSD.rec.PartyId);
    var PtCodeObj = PartyObj.Code();
    var isExistsDiff = false;
    var stat = true;

    if( (stat == true) and (PARTYIMPREC_RSD.rec.Status == RECSTATUS_NotRepo) and (PartyImp.ReplaceParmSign == "X") )
        isExistsDiff = true;
        stat = PartyObj.AddOwn( PTK_REPOSPRT );
        if( stat == false )
            note = "Ошибка присвоения субъекту роли \"Участник репозитария\"";
        end;
    end;
    /* Обновление RIC из дублирующихся записей */
    while( (stat == true) AND (duplRsd.MoveNext()) )
        stat = SetPartyCode( IsExistsDiff, PtCodeObj, duplRsd.rec.StatusRICode, PTCK_RIC, duplRsd.rec.RICode, "РИК" );
    end;
    if( stat == true ) 
        stat = SetPartyCode( IsExistsDiff, PtCodeObj, PARTYIMPREC_RSD.rec.StatusRICode, PTCK_RIC, PARTYIMPREC_RSD.rec.RICode, "РИК" );
    end;
    
    if( PartyObj.LegalForm == PTLEGF_PERSN )    // Участник является физическим лицом
        if( (stat == true) and
            (PARTYIMPREC_RSD.rec.StatusFullName == PARAMSTATUS_NotEqual) and (PartyImp.ReplaceParmSign == "X") )
            var name1, name2, name3;
            ExtractFIO( PARTYIMPREC_RSD.rec.FullName, name1, name2, name3 );
            PartyObj.LegalForm = PTLEGF_PERSN;
            PartyObj.LastName = name1;
            PartyObj.FirstName = name2;
            PartyObj.Patronymic = name3;
            PartyObj.ShortName = GetPersnShortName( name1, name2, name3 );
            PartyObj.FullName = PARTYIMPREC_RSD.rec.FullName;
            isExistsDiff = true;
        end;
    else                                        // Участник НЕ является физическим лицом
        if( stat == true )
/*            if( (PARTYIMPREC_RSD.rec.StatusFullName == PARAMSTATUS_NotEqual) and (PartyImp.ReplaceParmSign == "X") )
                PartyObj.FullName = PARTYIMPREC_RSD.rec.FullName;
                isExistsDiff = true;
            end;
            if( (PARTYIMPREC_RSD.rec.StatusFullNameEng == PARAMSTATUS_NotEqual) and (PartyImp.ReplaceParmSign == "X") )
                PartyObj.LatName = PARTYIMPREC_RSD.rec.FullNameEng;
                isExistsDiff = true;
            end;*/
            if( (PARTYIMPREC_RSD.rec.StatusIsResident == PARAMSTATUS_NotEqual) and (PartyImp.ReplaceParmSign == "X") )
                if( PARTYIMPREC_RSD.rec.IsResident == "Y" )
                    PartyObj.IsNotResident = "";
                else
                    PartyObj.IsNotResident = "X";
                end;
                isExistsDiff = true;
            end;
            //NoReporting;
        end;
        // Роль "Банк" необходима для добавления кода BIC.
        if( stat == true )
            stat = SetPTRoleForCode( IsExistsDiff, PartyObj, PARTYIMPREC_RSD.rec.StatusBIC, PTK_BANK, "Банк" );
        end;
        // Роль "Участник SWIFT" необходима для добавления кода SWIFT BIC.
        if( stat == true )
            var IsExistsDiffSwiftBIC = false;
            stat = SetPTRoleForCode( IsExistsDiffSwiftBIC, PartyObj, PARTYIMPREC_RSD.rec.StatusSwiftBIC, PTK_SWIFT, "Участник SWIFT" );
            // Вместе с добавлением кода SWIFT BIC необходимо добавлять и принадлежность "Банк",
            // т.к. при формировании сообщений проверяется наличие у субъекта именно этой роли, а не роли "Участник SWIFT".
            if( (stat == true) and (IsExistsDiffSwiftBIC == true) )
                IsExistsDiff = IsExistsDiffSwiftBIC;
                stat = SetPTRoleForCode( IsExistsDiff, PartyObj, PARTYIMPREC_RSD.rec.StatusSwiftBIC, PTK_BANK, "Банк" );
            end;
        end;
        if( stat == true )
            stat = SetPTRoleForCode( IsExistsDiff, PartyObj, PARTYIMPREC_RSD.rec.StatusIsCentrCount, PTK_CENTRALCONTR, "Центральный контрагент" );
        end;
        if( stat == true )
            stat = SetPTRoleForCode( IsExistsDiff, PartyObj, PARTYIMPREC_RSD.rec.StatusIsClearOrg, 63, "Клиринговая организация" );
        end;
        if( stat == true )
            stat = SetPartyCode( IsExistsDiff, PtCodeObj, PARTYIMPREC_RSD.rec.StatusBIC, PTCK_BIC, PARTYIMPREC_RSD.rec.BIC, "БИК" );
        end;
        if( stat == true )
            stat = SetPartyCode( IsExistsDiff, PtCodeObj, PARTYIMPREC_RSD.rec.StatusSwiftBIC, PTCK_SWIFT, PARTYIMPREC_RSD.rec.SwiftBIC, "BIC (SWIFT)" );
        end;
        if( stat == true )
            stat = SetPartyCode( IsExistsDiff, PtCodeObj, PARTYIMPREC_RSD.rec.StatusINN, PTCK_INN, PARTYIMPREC_RSD.rec.INN, "INN" );
        end;
        if( stat == true )
            stat = SetPartyCode( IsExistsDiff, PtCodeObj, PARTYIMPREC_RSD.rec.StatusOGRN, PTCK_OGRN, PARTYIMPREC_RSD.rec.OGRN, "ОГРН" );
        end;
        if( stat == true )
            stat = SetPartyCode( IsExistsDiff, PtCodeObj, PARTYIMPREC_RSD.rec.StatusLEI, PTCK_LEI, PARTYIMPREC_RSD.rec.LEI, "LEI" );
        end;
    end;

    if( (stat == true) AND (isExistsDiff) )
        stat = PartyObj.Update(true);
    end;
    
    if( stat == true )
        stat = SetPTShortName( IsExistsDiff, PARTYIMPREC_RSD.rec.PartyId, PARTYIMPREC_RSD.rec.StatusShortName, PARTYIMPREC_RSD.rec.ShortName );
    end;
    
    if( stat == true )
        if( isExistsDiff )
            if( PARTYIMPREC_RSD.rec.Status == RECSTATUS_NotRepo )
                UpdatePTImpRec( 0, RECSTATUS_RepoUpdated );
            else
                UpdatePTImpRec( 0, RECSTATUS_Updated );
            end;
        end;
    else
        AbortTrn();
    end;
end; /* UpdatePARTYTrnProc */

/* Транзакция добавления нового субъекта в справочник */
private macro InsertPARTYTrnProc
    var cmd = RsdCommand(
        "SELECT t_RICode, t_StatusRICode FROM DIR_PARTYIMPREC_DBT " +
         "WHERE t_ImpRepID = ? AND t_fullname = ? AND t_status = " + RECSTATUS_Duplicate);
    cmd.addParam("", RSDBP_IN, PARTYIMPREC_RSD.rec.ImpRepID);
    cmd.addParam("", RSDBP_IN, PARTYIMPREC_RSD.rec.FullName);
    cmd.execute();
    var duplRsd = TRsbDataSet(cmd);
    var PartyObj = RsbParty();
    var PtCodeObj = PartyObj.Code();
    var stat = true;

    if( substr(PARTYIMPREC_RSD.rec.RICode, 1, 3) == CONST_PERSONRICODE )
        var name1, name2, name3;
        ExtractFIO( PARTYIMPREC_RSD.rec.FullName, name1, name2, name3 );
        PartyObj.LegalForm = PTLEGF_PERSN;
        PartyObj.LastName = name1;
        PartyObj.FirstName = name2;
        PartyObj.Patronymic = name3;
        PartyObj.ShortName = GetPersnShortName( name1, name2, name3 );
        PartyObj.FullName = PARTYIMPREC_RSD.rec.FullName;
        /* Добавление RIC из дублирующихся записей */
        while( (stat == true) AND (duplRsd.MoveNext()) )
            if( duplRsd.rec.RICode != "" )
                stat = PtCodeObj.SetCode( PTCK_RIC, duplRsd.rec.RICode, PartyImp.FileDate );
            end;
        end;
        if( (stat == true) AND (PARTYIMPREC_RSD.rec.RICode != "") )
            stat = PartyObj.AddOwn( PTK_REPOSPRT );
            if( stat == true )
                stat = PtCodeObj.SetCode( PTCK_RIC, PARTYIMPREC_RSD.rec.RICode, PartyImp.FileDate );
            else
                note = "Ошибка присвоения субъекту роли \"Участник репозитария\"";
            end;
        end;
    else
        PartyObj.LegalForm = PTLEGF_INST;
        PartyObj.ShortName = PARTYIMPREC_RSD.rec.ShortName;
        PartyObj.FullName = PARTYIMPREC_RSD.rec.FullName;
        PartyObj.LatName = PARTYIMPREC_RSD.rec.FullNameEng;
        if( PARTYIMPREC_RSD.rec.IsResident != "Y" )
            PartyObj.IsNotResident = "X";
        end;
        /* Добавление RIC из дублирующихся записей */
        while( (stat == true) AND (duplRsd.MoveNext()) )
            if( duplRsd.rec.RICode != "" )
                stat = PtCodeObj.SetCode( PTCK_RIC, duplRsd.rec.RICode, PartyImp.FileDate );
            end;
        end;
        if( (stat == true) AND (PARTYIMPREC_RSD.rec.RICode != "") )
            stat = PartyObj.AddOwn( PTK_REPOSPRT );
            if( stat == true )
                stat = PtCodeObj.SetCode( PTCK_RIC, PARTYIMPREC_RSD.rec.RICode, PartyImp.FileDate );
            else
                note = "Ошибка присвоения субъекту роли \"Участник репозитария\"";
            end;
        end;
        if( (stat == true) AND (PARTYIMPREC_RSD.rec.INN != "") )
            stat = PtCodeObj.SetCode( PTCK_INN, PARTYIMPREC_RSD.rec.INN, PartyImp.FileDate );
        end;
        if( (stat == true) AND (PARTYIMPREC_RSD.rec.OGRN != "") )
            stat = PtCodeObj.SetCode( PTCK_OGRN, PARTYIMPREC_RSD.rec.OGRN, PartyImp.FileDate );
        end;
        if( (stat == true) AND (PARTYIMPREC_RSD.rec.LEI != "") )
            stat = PtCodeObj.SetCode( PTCK_LEI, PARTYIMPREC_RSD.rec.LEI, PartyImp.FileDate );
        end;
        if( (stat == true) and (PARTYIMPREC_RSD.rec.BIC != "") )
            // Роль "Банк" необходима для добавления кода BIC.
            stat = PartyObj.AddOwn( PTK_BANK );
            if( stat == true )
                stat = PtCodeObj.SetCode( PTCK_BIC, PARTYIMPREC_RSD.rec.BIC, PartyImp.FileDate );
            else
                note = "Ошибка присвоения субъекту роли \"Банк\"";
            end;
        end;
        if( (stat == true) and (PARTYIMPREC_RSD.rec.SwiftBIC != "") )
            // Роль "Участник SWIFT" необходима для добавления кода SWIFT BIC.
            stat = PartyObj.AddOwn( PTK_SWIFT );
            if( stat == true )
                stat = PtCodeObj.SetCode( PTCK_SWIFT, PARTYIMPREC_RSD.rec.SwiftBIC, PartyImp.FileDate );
            else
                note = "Ошибка присвоения субъекту роли \"Участник SWIFT\"";
            end;
            // Вместе с добавлением кода SWIFT BIC необходимо добавлять и принадлежность "Банк",
            // т.к. при формировании сообщений проверяется наличие у субъекта именно этой роли, а не роли "Участник SWIFT".
            if( stat == true )
                stat = PartyObj.AddOwn( PTK_BANK );
                if( stat == false )
                    note = "Ошибка присвоения субъекту роли \"Банк\"";
                end;
            end;
        end;
        if( (stat == true) and (PARTYIMPREC_RSD.rec.IsCentrCount == "Y") )
            stat = PartyObj.AddOwn( PTK_CENTRALCONTR );
            if( stat == false )
                note = "Ошибка присвоения субъекту роли \"Центральный контрагент\"";
            end;
        end;
        if( (stat == true) and (PARTYIMPREC_RSD.rec.IsClearOrg == "Y") )
            stat = PartyObj.AddOwn( 63 );
            if( stat == false )
                note = "Ошибка присвоения субъекту роли \"Клиринговая организация\"";
            end;
        end;
        //NoReporting;
    end;

    if( stat == true )
        var refid, retVal = 0, code1 = "";
        retVal = GetReferenceIDByType( 3/*OBJTYPE_PARTY*/, 1, refid);
        if( retVal == 0 )
            retVal = GenerateReference( refid, code1 );
        end;
        if( retVal == 0 )
            stat = PtCodeObj.SetCode( PTCK_CONTR, code1, PartyImp.FileDate );
        else
            stat = false;
        end;
        if( stat == false )
            note = "Ошибка присвоения субъекту кода \"Код\"";
        end;
    end;

    if( stat == true )
        stat = PartyObj.Update(true);
    end;
    
    if( stat == true )
        InsertPTShortName( PartyObj.PartyID, PARTYIMPREC_RSD.rec.ShortName );
    end;

    if( stat == true )
        UpdatePTImpRec( PartyObj.PartyID, RECSTATUS_Inserted );
    else
        AbortTrn();
    end;
end; /* InsertPARTYTrnProc */


macro LoadParty( ImpRepId )
    PartyImp.id = ImpRepId;
    var stat = GetEQ( PartyImp );

    if( (stat == true) and (PartyImp.Status == PROCSTATUS_Check) and (PartyImp.LoadSign == "X") and
        ((PartyImp.AddSign == "X") or (PartyImp.AddParmSign == "X") or (PartyImp.ReplaceParmSign == "X")) )
        var cmdstr = 
            "SELECT * FROM DIR_PARTYIMPREC_DBT " +
            " WHERE t_imprepid = ? AND t_PartyNotUpdate <> 'X' " + //and t_recid = 1" + 
              " AND t_statusFullName <> " + PARAMSTATUS_Ident; // При идентификации по наименованию обновление данных не производим
        if( PartyImp.AddSign == "X" )
            cmdstr = cmdstr + RECSTATUS_None;
            if( (PartyImp.AddParmSign == "X") or (PartyImp.ReplaceParmSign == "X") )
                cmdstr = cmdstr + " AND t_status IN (" + RECSTATUS_None + "," + RECSTATUS_NotRepo + "," + RECSTATUS_Check + ") ";
            else
                cmdstr = cmdstr + " AND t_status = " + RECSTATUS_None;
            end;
        else
            cmdstr = cmdstr + " AND t_status IN (" + RECSTATUS_NotRepo + "," + RECSTATUS_Check + ") ";
        end;
        cmdstr = cmdstr + " ORDER BY t_recid";

        var countRecs = getCountRecs( cmdstr, ImpRepId );
        InitProgress( countRecs, " Загрузка параметров участников репозитария ...",
                      "Загрузка параметров участников репозитария");
        var i = 1;

        var cmd = RsdCommand(cmdstr);
        cmd.addParam("", RSDBP_IN, ImpRepId);
        cmd.execute();
        PARTYIMPREC_RSD = TRsbDataSet(cmd);

        while( PARTYIMPREC_RSD.MoveNext() )
            note = "";

            if( (PARTYIMPREC_RSD.rec.Status == RECSTATUS_Check) OR (PARTYIMPREC_RSD.rec.Status == RECSTATUS_NotRepo) )
                if( ProcessTrn( 1, "UpdatePARTYTrnProc" ) == false )
                    if( PARTYIMPREC_RSD.rec.Status == RECSTATUS_NotRepo )
                        UpdatePTImpRec( 0, RECSTATUS_RUpdError );
                    else
                        UpdatePTImpRec( 0, RECSTATUS_UpdError );
                    end;
                end;

            elif( (PARTYIMPREC_RSD.rec.Status == RECSTATUS_None) and (PartyImp.AddSign == "X") )
                if( ProcessTrn( 1, "InsertPARTYTrnProc" ) == false )
                    UpdatePTImpRec( 0, RECSTATUS_InsError );
                end;
            end;

            UseProgress(i);
            i = i + 1;
        end;

        RemProgress();

        PartyImp.Status = PROCSTATUS_Load;
        stat = Update( PartyImp );
    end;
    return stat;
end;
