/*
Отчет о состоянии сообщений Репозитария. Тело отчёта
ir_rep_msgstate.mac
*/
import or_rep_h, oralib, likepy, Календарь, or_srt_h, BankInter;

private var Rep = CMakeReport(), sqlString;
private var IsSecur,        //Совместная работа БОЦБ + Репозитарий
            IsFISSIKO,      //Совместная работа ФИССиКО + Репозитарий
            DaysToSSMsg,    //Срок отправки сообщений об оценке справ.стоимости
            DaysToFirstMsg; //Срок отправки первичного/корректирующего сообщения

//-----------------------------------------------------------------------------------------
private macro ДобавитьСтроку ()
  Rep.AddStr();
End;

private macro ДобавитьПустуюСтроку ()
  Rep.AddEmptyStr();
End;

//-----------------------------------------------------------------------------------------

private macro collectOutProc ( str )
    sqlString = sqlString + "\n" + trim (str);
End;
//-----------------------------------------------------------------------------------------

private macro CaptureOutput
   sqlString = "";
   SetOutHandler (@collectOutProc);
End;
//-----------------------------------------------------------------------------------------
                     
private macro StopCaptureOutput
    SetOutHandler ();
    return sqlString;
End;
//-----------------------------------------------------------------------------------------

//Последний календарный день в месяце
private macro LastDateInMonth (dt:date):date
  var day, month, year;
  DateSplit(dt, null, month, year);
  if (InList(month, 1, 3, 5, 7, 8, 10, 12))
    day = 31;
  elif (InList(month, 4, 6, 9, 11))
    day = 30;
  elif (month == 2)
    if (mod(year, 4) == 0) 
      day = 29;
    else
      day = 28;
    end;
  end;
  return date(day, month, year);
End;
//-----------------------------------------------------------------------------------------

//Сортировка массива по плановой дате СРС
macro PlanData_Sort (p1, p2):integer
  if  (p1.PlanDate < p2.PlanDate) return -1;
  elif(p1.PlanDate > p2.PlanDate) return  1;
  end;
  return 0;
End;

//-----------------------------------------------------------------------------------------

class PlanData (_OperKind,
                _OperCode,
                _EventKind,
                _MsgKind,
                _EventDate,
                _PlanDate,
                _MsgStatus)
  var OperKind:string  = _OperKind;
  var OperCode:string  = _OperCode;
  var EventKind:string = _EventKind;
  var MsgKind:string   = _MsgKind;
  var EventDate:Date   = _EventDate;
  var PlanDate:Date    = _PlanDate;
  var MsgStatus:string = _MsgStatus;
END;

//-----------------------------------------------------------------------------------------

//todo: Добавить в запрос поиск сообщений по CSA
private macro CollectDataInfo_PlanedSRS (ReportDate):TArray
  var DataArray = TArray();
  var EventDate:Date;
  var PlanMsgDate:Date;

  CaptureOutput();
  [select * from (
  select (select t_name from doprkdoc_dbt where t_dockind = deal.dockind) oprkind,
          deal.dealcode,
          (select t_name from ddlgrtempl_dbt where t_num = grdeal.t_templnum) grtype,
          grdeal.t_plandate grdate,
          case when grdeal.t_templnum = 43 /*Сводный отчет*/ then 'CM084'
               when grdeal.t_templnum = 46 /*Заключение сделки*/ then (select t_messagecode from ddl_repozdeal_dbt where t_dockind = deal.dockind and t_docid = deal.docid and t_reporting = 'X')
               when grdeal.t_templnum = 47 /*Cообщение по изменению условий*/ then case when dockind in (101, 199)
                                                                              then (select t_messagecode from ddl_repozdeal_dbt where t_dockind = deal.dockind and t_docid = deal.docid and t_reporting = 'X')
                                                                              else 'CM010' end || ' (изменение условий)'
               when grdeal.t_templnum = 51 /*Закрытие договора*/ then case when dockind in (101, 199) then 'CM093' else 'CM011' end
               when grdeal.t_templnum = 53 /*Сообщение о просрочке исполнения*/ then 'CM093'
               when grdeal.t_templnum = 55 /*Сообщение о приостановке исполнения*/ then 'CM093'
               when grdeal.t_templnum = 57 /*Сообщение о досрочном исполнении*/ then 'CM093'
               when grdeal.t_templnum = 59 /*Сообщение об отказе от сделки*/ then 'CM093'
               when grdeal.t_templnum = 61 /*Прекращение обязательств неттингом*/ then 'CM093'
               when grdeal.t_templnum = 63 /*Заключение договора*/ then 'CM010'
               when grdeal.t_templnum = 66 /*Заключение договора (корр. сообщ.)*/ then 'CM010 (корректирующее сообщение)'
               when grdeal.t_templnum = 72 /*Заключение сделки (корр. сообщ.)*/ then (select t_messagecode from ddl_repozdeal_dbt where t_dockind = deal.dockind and t_docid = deal.docid and t_reporting = 'X') || ' (корректирующее сообщение)'
               when grdeal.t_templnum = 75 /*Переоценка справедливой стоимости*/ then 'CM094'
               --Нет в справочнике when grdeal.t_templnum = /*Сообщение о выплате маржевых сумм*/
               else ''
          end msgkind
   from (];

  if (IsSecur)
    [select t_dealid docid, t_bofficekind dockind, t_dealcode dealcode from ddl_tick_dbt where t_bofficekind = 101 union all];
  end;

  if (IsFISSIKO)
    [select t_id docid, t_dockind dockind, t_code dealcode from ddvndeal_dbt where t_dockind in (199, 4815) union all];
  end;

  [select t_genagrid docid, t_dockind dockind, t_code dealcode from ddl_genagr_dbt) deal, ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc
   where     exists (select 1 from ddl_repozdeal_dbt where t_dockind = deal.dockind and t_docid = deal.docid and t_reporting = 'X')
         and grdeal.t_dockind = deal.dockind
         and grdeal.t_docid = deal.docid
         and gracc.t_grdealid = grdeal.t_id
         and gracc.t_accnum = 5 --Репозитарный учёт
         and gracc.t_state = 1
         and grdeal.t_plandate <> to_date ('01010001', 'ddmmyyyy') /*DAN*/ ) t where t.msgkind is not null
   order by 4, 5];

  var sql = ExecSqlSelect(StopCaptureOutput (), null);
  while (sql.MoveNext())

    EventDate   = IfThenElse(sql.value("msgkind")=="CM094", LastDateInMonth(sql.value("grdate")), sql.value("grdate"));
    PlanMsgDate = DateAfterWorkDays(EventDate, IfThenElse(sql.value("msgkind")=="CM094", DaysToSSMsg, DaysToFirstMsg));
  
    //В отчёт попадают только те строки графика, предельная дата выполнения которых меньше, чем Дата отчета + Количество рабочих дней из настройки
    if (DateAfterWorkDays(ReportDate, IfThenElse(sql.value("msgkind")=="CM094", DaysToSSMsg, DaysToFirstMsg)) < PlanMsgDate)
      continue;
    end;

    DataArray[DataArray.Size] = PlanData(sql.value("oprkind"),
                                         sql.value("dealcode"),
                                         sql.value("grtype"),
                                         sql.value("msgkind"),
                                         EventDate,
                                         PlanMsgDate
                                         );
  end;

  QSortWithUserMacro(DataArray, "PlanData_Sort");
  return DataArray;
End;

//todo: Добавить в запрос поиск сообщений по CSA
macro CollectDataInfo_PrepareSRS (ReportDate):TArray
  var DataArray = TArray();
  var EventDate:Date;
  var PlanMsgDate:Date;

  CaptureOutput();
  [select t.* from (select (select t_name from doprkdoc_dbt where t_dockind = deal.dockind) oprkind,
          deal.dealcode,
          (select t_name from ddlgrtempl_dbt where t_num = grdeal.t_templnum) grtype,
          grdeal.t_plandate grdate,
          nvl(generalinf.t_rstatusname, ' ') msgstatusname,
          generalinf.t_prdate preparedate,
          case when grdeal.t_templnum = 43 /*Сводный отчет*/ then 'CM084'
               when grdeal.t_templnum = 46 /*Заключение сделки*/ then (select t_messagecode from ddl_repozdeal_dbt where t_dockind = deal.dockind and t_docid = deal.docid and t_reporting = 'X')
               when grdeal.t_templnum = 47 /*Cообщение по изменению условий*/ then case when dockind in (101, 199)
                                                                              then (select t_messagecode from ddl_repozdeal_dbt where t_dockind = deal.dockind and t_docid = deal.docid and t_reporting = 'X')
                                                                              else 'CM010' end || ' (изменение условий)'
               when grdeal.t_templnum = 51 /*Закрытие договора*/ then case when dockind in (101, 199) then 'CM093' else 'CM011' end
               when grdeal.t_templnum = 53 /*Сообщение о просрочке исполнения*/ then 'CM093'
               when grdeal.t_templnum = 55 /*Сообщение о приостановке исполнения*/ then 'CM093'
               when grdeal.t_templnum = 57 /*Сообщение о досрочном исполнении*/ then 'CM093'
               when grdeal.t_templnum = 59 /*Сообщение об отказе от сделки*/ then 'CM093'
               when grdeal.t_templnum = 61 /*Прекращение обязательств неттингом*/ then 'CM093'
               when grdeal.t_templnum = 63 /*Заключение договора*/ then 'CM010'
               when grdeal.t_templnum = 66 /*Заключение договора (корр. сообщ.)*/ then 'CM010 (корректирующее сообщение)'
               when grdeal.t_templnum = 72 /*Заключение сделки (корр. сообщ.)*/ then (select t_messagecode from ddl_repozdeal_dbt where t_dockind = deal.dockind and t_docid = deal.docid and t_reporting = 'X') || ' (корректирующее сообщение)'
               when grdeal.t_templnum = 75 /*Переоценка справедливой стоимости*/ then 'CM094'
               --Нет в справочнике when grdeal.t_templnum = /*Сообщение о выплате маржевых сумм*/
               else ''
          end msgkind
   from (];

  if (IsSecur) 
    [select t_dealid docid, t_bofficekind dockind, t_dealcode dealcode from ddl_tick_dbt where t_bofficekind = 101 union all];
  end;

  if (IsFISSIKO)
    [select t_id docid, t_dockind dockind, t_code dealcode from ddvndeal_dbt where t_dockind in (199, 4815) union all];
  end;

  [select t_genagrid docid, t_dockind dockind, t_code dealcode from ddl_genagr_dbt) deal, ddl_repozdeal_dbt repozdeal,
     ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc,
     ddlgrdoc_dbt grdoc, dir_generalinf_dbt generalinf
     where     deal.docid = repozdeal.t_docid
           and deal.dockind = repozdeal.t_dockind
           and repozdeal.t_reporting = 'X'
           and grdeal.t_dockind = deal.dockind
           and grdeal.t_docid = deal.docid
           and gracc.t_grdealid = grdeal.t_id
           and gracc.t_accnum = 5 /*Репозитарный учет*/
           and grdoc.t_grdealid = grdeal.t_id
           and grdoc.t_dockind = 4853 /*СРС*/
           and grdoc.t_servdockind = 4851 /*Сревисная операция репозитарного учета*/
           and grdoc.t_docid = generalinf.t_internalmessageid
           and generalinf.t_rstatus in (1, 2, 3, 4)
           --and generalinf.t_rstatusname in ('Подготовлено', 'Ошибка подготовки', 'Сгенерировано', 'Ошибка генерации') Нужно ещё Ошибка выгрузки. И нужно ли Ошибка обработки?
           ) t where t.msgkind is not null
   order by 4, 5];

  var sql = ExecSqlSelect(StopCaptureOutput (), null);
  while (sql.MoveNext())

    EventDate   = IfThenElse(sql.value("msgkind")=="CM094", LastDateInMonth(sql.value("grdate")), sql.value("grdate"));
    PlanMsgDate = DateAfterWorkDays(EventDate, IfThenElse(sql.value("msgkind")=="CM094", DaysToSSMsg, DaysToFirstMsg));
  
    //В отчёт попадают только те строки графика, предельная дата выполнения которых меньше, чем Дата отчета + Количество рабочих дней из настройки
    if (DateAfterWorkDays(ReportDate, IfThenElse(sql.value("msgkind")=="CM094", DaysToSSMsg, DaysToFirstMsg)) < PlanMsgDate)
      continue;
    end;

    DataArray[DataArray.Size] = PlanData(sql.value("oprkind"),
                                         sql.value("dealcode"),
                                         sql.value("grtype"),
                                         sql.value("msgkind"),
                                         EventDate,
                                         sql.value("preparedate"),
                                         sql.value("msgstatusname")
                                         );
  end;

  QSortWithUserMacro(DataArray, "PlanData_Sort");
  return DataArray;
End;
//-----------------------------------------------------------------------------------------

macro СостояниеОбработкиСообщений (ReportDate:Date)
  var i = 0;
  var DataArray = CollectDataInfo_PlanedSRS(ReportDate);

  Rep.AddPrintCell("Состояние сообщений Репозитария", 100, 0, "c:ex_FS(b)", REP_ELEM_STR ); ДобавитьСтроку();
  Rep.AddPrintCell("Отчетная дата "+ReportDate, 100, 0, "c:ex_FS(b)", REP_ELEM_STR ); ДобавитьСтроку();
  ДобавитьПустуюСтроку();
  Rep.AddPrintCell("Состояние обработки сообщений", 100, 0, "c:ex_FS(b)", REP_ELEM_STR ); ДобавитьСтроку();

  Rep.AddPrintCell("Запланированные и не подготовленные сообщения", 100, 0, "l:ex_FS(b)", REP_ELEM_STR ); ДобавитьСтроку();

  if (DataArray.Size == 0)
    Rep.AddPrintCell( "Запланированные и не подготовленные сообщения на отчетную дату не найдены", 100, 0, "l:", REP_ELEM_STR ); ДобавитьСтроку();
  else
    Rep.AddPrintCell( "N",                                         5, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Вид операции",                             25, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Код операции",                             10, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Вид отчетного события (действие графика)", 15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Вид сообщения",                            15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Дата отчетного события",                   15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Предельная дата подготовки",               15, 0, "c:", REP_ELEM_TABL );
    ДобавитьСтроку();
  end;

  InitProgress(DataArray.Size, "Формирование отчета", "Запланированные и не подготовленные сообщения");

  while (i < DataArray.Size)
    Rep.AddPrintCell( i+1,                     5, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].OperKind,  25, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].OperCode,  10, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].EventKind, 15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].MsgKind,   15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].EventDate, 15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].PlanDate,  15, 0, "c:", REP_ELEM_TABL );
    ДобавитьСтроку();
    UseProgress(i=i+1);
  end;

  RemProgress();

  if (DataArray.Size() > 0)
    ДобавитьПустуюСтроку();
    ДобавитьПустуюСтроку();
  end;

//------------------------------------------
  i = 0;
  DataArray = CollectDataInfo_PrepareSRS(ReportDate);

  Rep.AddPrintCell("Подготовленные и не выгруженные сообщения", 100, 0, "l:ex_FS(b)", REP_ELEM_STR ); ДобавитьСтроку();

  if (DataArray.Size == 0)
    Rep.AddPrintCell( "Подготовленные и не выгруженные сообщения на отчетную дату не найдены", 100, 0, "l:", REP_ELEM_STR ); ДобавитьСтроку();
  else
    Rep.AddPrintCell( "N",                                         5, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Вид операции",                             25, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Код операции",                             10, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Вид отчетного события (действие графика)", 15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Вид сообщения",                            15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Дата отчетного события",                   15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Дата подготовки",                          15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( "Статус сообщения",                         15, 0, "c:", REP_ELEM_TABL );
    ДобавитьСтроку();
  end;

  InitProgress(DataArray.Size, "Формирование отчета", "Подготовленные и не выгруженные сообщения");

  while (i < DataArray.Size)
    Rep.AddPrintCell( i+1,                      5, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].OperKind,   25, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].OperCode,   10, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].EventKind,  15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].MsgKind,    15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].EventDate,  15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].PlanDate,   15, 0, "c:", REP_ELEM_TABL );
    Rep.AddPrintCell( DataArray[i].MsgStatus,  15, 0, "c:", REP_ELEM_TABL );
    ДобавитьСтроку();
    UseProgress(i=i+1);
  end;

  RemProgress();

onerror()
  RemProgress();
  Rep.AddPrintCell( "Ошибка при формировании отчета", 100, 0, "l:", REP_ELEM_STR );
  ДобавитьСтроку();
End;

//Получить значение настройки
private macro IR_GetRegistryValue (path):variant
  var regPath = "РЕПОЗИТАРИЙ\\"+path;
  var regValue;

  if (not GetRegistryValue( regPath, 0, regValue) )
    MsgBox( "Ошибка при получении значения настройки \"" + RegPath + "\"" );
    regValue = null;
  end;

  return regValue;
End;

/*
todo:
  Искать в настройках:
  - Срок отправки сообщений об оценке справ.стоимости
*/
macro Rep_MsgState (ReportDate:date, ReportState:string, ReportOrders:string)
  var excel = false; //Если, вдруг, понадобится вывод в excel, что очень вероятно
  DaysToSSMsg    = 2;
  DaysToFirstMsg = IR_GetRegistryValue("СРОК ОТПРАВКИ ПЕРВ.КОРР. СБЩ");

  if (not GetRegistryValue( "COMMON\\WORK_MODE\\IR_WORK_WITH_DERIVATIVES", 0, IsFISSIKO) )
    MsgBox( "Ошибка при получении значения настройки \"COMMON\\WORK_MODE\\IR_WORK_WITH_DERIVATIVES\"" );
    IsFISSIKO = false;
  end;

  if (not GetRegistryValue( "COMMON\\WORK_MODE\\IR_WORK_WITH_SECURITIES", 0, IsSecur) )
    MsgBox( "Ошибка при получении значения настройки \"COMMON\\WORK_MODE\\IR_WORK_WITH_SECURITIES\"" );
    IsSecur = false;
  end;

  if (ReportState == "X")
    СостояниеОбработкиСообщений(ReportDate);
  end;

  Rep.PrintRep;

  if (excel) 
    Rep.PrintWinRep();
    Rep.ShowWinRep();
  end;
End;