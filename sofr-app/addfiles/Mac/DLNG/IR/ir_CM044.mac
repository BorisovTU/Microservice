/*
    $Name:  ir_CM044.mac
    $Module:   Репозитарий
    $Description:  Класс для сообщения CM044
*/

import "ir_CMBase.mac";

class (CMBase) CM044 ( _isRecon, _conveyerID, _ir_op : object)
  InitCMBase();
  
  m_isRecon     = _isRecon;
  m_conveyerID  = _conveyerID;
  m_ir_op       = _ir_op;
  m_MessageID   = REPOS_CM044;

  macro addCMAll(ir_generalinf     ,
                 TradeRepository   ,
                 Party1            ,
                 Party2            ,
                 UTIGeneratingParty,
                 Sender            ,
                 cm                ,
                 Instruments       ,
                 GrDealID          ,
                 TemplNum          ,
                 DealParmForReport )
    addGenInf(ir_generalinf, TemplNum, DealParmForReport);
    addRepAccGrDoc(GrDealID);
    addcmGrDoc(GrDealID);
    addParty(TradeRepository, Party1, Party2, UTIGeneratingParty, Sender);
    addCM(cm);
    addInstrument(Instruments);
  end;

  macro insertAll
    var err = 0;
    err = insertGeneralinf;
    if (not err)
      err = insertParty;
    end;
    if (not err)
      err = insertDlgrdocCache;
    end;
    if (not err)
      err = insertCM;
    end;
    if (not err)
      err = insertInstrument;
    end;
    return err;
  end;

  macro ЗаполнениеCM (cm044 : @TrecHandler, 
                      DataSet, 
                      GenAgr,
                      FD,
                      first,
                      field,
                      Instruments : @TrecHandler, 
                      DealParmForReport)
    var stat = 0;
    var NominalValue: Numeric = 0.0;
    var FIIDFrom, FIIDTo;
    const ERROR_NominalValue = "Не задано значение номинала для облигации на дату заключения сделки";

    if (first)
        //Instruments.clear();
        
        if( (FD.IsBuy() and (GenAgr.Side1 == 1)) or
            (FD.IsSale() and (GenAgr.Side1 == 2)) )
            cm044.rec.buyerid  = IR_PARTY_KIND_PARTY1;
            cm044.rec.buyer    = "Party1";
            cm044.rec.sellerid = IR_PARTY_KIND_PARTY2;
            cm044.rec.seller   = "Party2";
        else
            cm044.rec.buyerid  = IR_PARTY_KIND_PARTY2;
            cm044.rec.buyer    = "Party2";
            cm044.rec.sellerid = IR_PARTY_KIND_PARTY1;
            cm044.rec.seller   = "Party1";
        end;
     
        if( FD.Deal.rec.OptionStyle == DVSTYLE_AMERICAN )
            cm044.rec.CommencementDate = FD.ДатаНачала();
            cm044.rec.ExpirationDate = FD.ДатаИстеченияСрокаДействия();
            cm044.rec.OptionStyle = "A";
        else
            cm044.rec.ExpirationDate = FD.ДатаИсполнения();
            cm044.rec.OptionStyle = "E";
        end;

        if( FD.ПоставочныйКонтракт() )
            cm044.rec.SettlementTypeId = 2; // Physical
        else
            cm044.rec.SettlementTypeId = 1; // Cash
        end;

        cm044.rec.OptionEntitlement = FD.nFI_().rec.Amount;
        if( cm044.rec.OptionEntitlement == 0 ) // при отсутствии значения - одна облигация
            cm044.rec.OptionEntitlement = 1;
        end;
   
        NominalValue = GetFaceValue(FD.BaseFI().rec.FIID, FD.ДатаСделки());
        if( NominalValue != 0.0 )
            FIIDFrom = FD.BaseFI().rec.FaceValueFI;
            FIIDTo = FD.nFI_().rec.PriceFIID;
            if( FIIDFrom != FIIDTo ) // если валюта номинала отлична от валюты цены сделки
                // номинал ц/б переводится по состоянию на дату заключения сделки -> в валюту цены по валютному курсу на дату заключения сделки
                if( DV_SmartConvertSum( NominalValue, NominalValue, FD.ДатаСделки(), FIIDFrom, FIIDTo, false ) == false )
                    RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, m_MessageType, 
                                                GetCurrencyConvertErrorMsg(FIIDFrom, FIIDTo, FD.ДатаСделки()), "NominalAmount", GetReportAction(DataSet.TemplNum), 400 + 14);
                    stat=1;
                end;
            end;
        else
            RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, m_MessageType, 
                                        ERROR_NominalValue, "NominalAmount", GetReportAction(DataSet.TemplNum), 400 + 14);
            stat=1;
        end;

        var tmpVal: Numeric = cm044.rec.OptionEntitlement * cm044.rec.NumberOfOptions;   // пришлось добавить временное значение для предотвращения переполнения
        cm044.rec.PremiumPayerId    = cm044.rec.buyerid;
        cm044.rec.PremiumPayer      = cm044.rec.buyer;
        cm044.rec.PremiumReceiverId = cm044.rec.sellerid;
        cm044.rec.PremiumReceiver   = cm044.rec.seller;
        cm044.rec.OptionTypeId      = FD.Deal.rec.OptionType;
        cm044.rec.OptionStyleId     = FD.Deal.rec.OptionStyle;
        cm044.rec.PremiumCurrencyId = FD.Deal.rec.BonusFIID;
        cm044.rec.PremiumAmount     = FD.Deal.rec.Bonus;
        cm044.rec.NumberOfOptions   = 1;
        cm044.rec.NominalCurrencyId = FD.nFI_().rec.PriceFIID;

        cm044.rec.NominalAmount     = NominalValue * tmpVal;  
        cm044.rec.Strike            = FD.nFI_().rec.Price; // Страйк - это цена единицы базового актива, цена исполнения в опционном контракте
        cm044.rec.StrikeCurrencyId  = FD.nFI_().rec.PriceFIID;
        cm044.rec.OptionType        = substr( DL_GetNameAlg( 3423 /*ALG_FI_DVTYPE*/, cm044.rec.OptionTypeId ), 1, 4);
        cm044.rec.SettlementType    = substr( DL_GetNameAlg( 7044 /*ALG_DV_OPTION_PAYKIND*/, cm044.rec.SettlementTypeId ), 1, 8);
        cm044.rec.NominalCurrency   = GetFICode(cm044.rec.NominalCurrencyId, null, FICK_ISOSTRING);
        cm044.rec.PremiumCurrency   = GetFICode(cm044.rec.PremiumCurrencyId, null, FICK_ISOSTRING);
        cm044.rec.StrikeCurrency    = GetFICode(cm044.rec.StrikeCurrencyId, null, FICK_ISOSTRING);

        Instruments.rec.FIID = FD.BaseFI().rec.FIID;
        Instruments.rec.InstrumentCode = GetAvoirissCode(FD.BaseFI(), m_ir_op.rec.Date);
        Instruments.rec.Instrumentname = FD.BaseFI().rec.Name;
    end;
    if( field == "NominalAmount" )
        NominalValue = GetFaceValue(FD.BaseFI().rec.FIID, FD.ДатаСделки());
        if( NominalValue != 0.0 )
            FIIDFrom = FD.BaseFI().rec.FaceValueFI;
            FIIDTo = FD.nFI_().rec.PriceFIID;
            if( FIIDFrom != FIIDTo ) // если валюта номинала отлична от валюты цены сделки
                // номинал ц/б переводится по состоянию на дату заключения сделки -> в валюту цены по валютному курсу на дату заключения сделки
                if( DV_SmartConvertSum( NominalValue, NominalValue, FD.ДатаСделки(), FIIDFrom, FIIDTo, false ) == true )
                    cm044.rec.NominalAmount = tmpVal * NominalValue;
                else
                    RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, m_MessageType, 
                                                GetCurrencyConvertErrorMsg(FIIDFrom, FIIDTo, FD.ДатаСделки()), field, GetReportAction(DataSet.TemplNum), 400 + 14);
                    stat=1;
                end;
            end;
        else
            RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, m_MessageType, 
                                        ERROR_NominalValue, field, GetReportAction(DataSet.TemplNum), 400 + 14);
            stat=1;
        end;
    end;
    return stat;
  end;
end;
