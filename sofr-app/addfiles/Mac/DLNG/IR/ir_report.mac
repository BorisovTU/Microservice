import globals, or_rep_h, oralib, likepy, RSD, Календарь,"dlquery.mac";
import  RsbFormsInter, SPInter, PTInter;

const K_F2 = 316;
const K_F3 = 317;
var months = TArray;
Months[1]="Январь";
Months[2]="Февраль";
Months[3]="Март";
Months[4]="Апрель";
Months[5]="Май";
Months[6]="Июнь";
Months[7]="Июль";
Months[8]="Август";
Months[9]="Сентябрь";
Months[10]="Октябрь";
Months[11]="Ноябрь";
Months[12]="Декабрь";


var Rep, sql, cmd, rs;
var NotReportingFlag = 1;
var  p1Date, p2Date;
var pPortfolioID, pPortfolioName ;

var repSource;
//получение портфеля
//select cls.t_name, VAL.T_NAME, val.t_element from dllclsval_dbt clv, dllclass_dbt cls, dllvalues_dbt val where CLV.T_CLASSIFICATOR = CLS.T_CLASSIFICATOR and clv.t_element = VAL.T_ELEMENT and val.t_list = clv.t_list  and clv.t_classificator = 1649;

CLASS ir_Reporting (_date)
var rn = 1;
var EXCEL_MONEY = "\"# ##0,00\"";
var EXCEL_DATE   ="\"m.d.yyyy\"";
var sheet = 1;
var count = 0;
var repDate = _date;
var rep_str;
var col1_1en = 10,
    col2_len = 12,
    col3_len = 60, 
    col4_len = 10,  
    col5_len = 18,
    col6_len = 18,
    col7_len = 15,  
    col8_len = 12,  
    col9_len = 15;
var color  = 12632256,  
    colorD = 16316664,
    colorL = 15395562;


private macro ДобавитьСтроку()
  Rep.AddStr();rn = rn + 1;
end;

private macro ДобавитьПустуюСтроку()
   Rep.AddEmptyStr();rn = rn + 1;
end;   


private macro Печатьшапки ()

    
    var head_format = "ex_FS():ex_B(tblr):ex_BC(" + color + ")";
    var day, month, year;
    datesplit(repDate, day, month, year);

//    Rep.SetExecute("iBook.Sheets("+sheet+").Columns(8).NumberFormat = "+EXCEL_DATE+";");        
    ДобавитьПустуюСтроку();

    Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").WrapText = false;");
    Rep.SetExecute("iBook.Sheets("+sheet+").Range(\"A"+(rn)+":D"+(rn)+"\").merge;");
    Rep.AddPrintCell("Сделки Форвард  по Валюте. " , 10, null, "l:ex_FS(b):ex_B(tblr)");
    ДобавитьСтроку();
    if (NotReportingFlag)
      rep_Str = "Не помеченные к репортингу и с переоценкой/признанием СС за " + Months[month] + ", " + year;
    else
      rep_Str = "Все c переоценкой/признанием СС за " + Months[month] + ", " + year;
    end;
    Rep.AddPrintCell(rep_Str, 100, null, "l:ex_FS():ex_B(tblr)");
    ДобавитьСтроку();

    ДобавитьПустуюСтроку();
/*1*/   Rep.AddPrintCell ("ID Субъекта",        10,  null, "c:" + head_format);
/*1_1*/ Rep.AddPrintCell ("Код Субъекта",       col2_len,  null, "c:" + head_format);
/*2*/   Rep.AddPrintCell ("Субъект",            col3_len,  null,"c:" + head_format);
/*3*/   Rep.AddPrintCell ("ID сделки",         col4_len,  null, "c:"+ head_format);
/*4*/   Rep.AddPrintCell ("Код сделки",        col5_len,  null, "c:" + head_format);
/*5*/   Rep.AddPrintCell ("Дата сделки",       col6_len,  null,"c:" + head_format); 
/*5*/   Rep.AddPrintCell ("ID ГС",             col7_len,  null,"c:" + head_format); 
/*6*/   Rep.AddPrintCell ("Номер ГС",          col8_len,  null, "c:" + head_format);
/*7*/   Rep.AddPrintCell ("Признак репортинга",col9_len,  null, head_format);

  ДобавитьСтроку;  color = colorD; head_format = "c:ex_FZ(8):ex_FS():ex_B(tblr):ex_BC(" + color + ")"; color = colorL;
/*1*/   Rep.AddPrintCell ("1",  10,  null, head_format);
/*1_1*/ Rep.AddPrintCell ("1.1",col2_len,  null, "c:" + head_format);
/*2*/   Rep.AddPrintCell ("2",  col3_len,  null, head_format);
/*3*/   Rep.AddPrintCell ("3",  col4_len,  null, head_format);
/*4*/   Rep.AddPrintCell ("4",  col5_len,  null, head_format);
/*4*/   Rep.AddPrintCell ("4",  col6_len,  null, head_format);
/*5*/   Rep.AddPrintCell ("5",  col7_len,  null, head_format); 
/*6*/   Rep.AddPrintCell ("6",  col8_len,  null, head_format);
/*7*/   Rep.AddPrintCell ("7",  col9_len,  null, head_format);
Rep.SetExecute("iBook.Sheets("+sheet+").Rows("+rn+").AutoFilter;");
  ДобавитьСтроку;

end;

private macro ПечатьБлокаДанных(_rs);
  var format, Наименование, КодСубъекта ;
  record Субъект(party);
  while(_rs.movenext)
    format = "ex_FS():ex_B(tblr):ex_BC(" + color + ")";
//msgbox("Ok");
    ПолучитьСубъекта(_rs.PartyID, Субъект);

    Rep.AddPrintCell(_rs.PartyID,                                  10, null, "l:"+format); 
    Rep.AddPrintCell(ПолучитьКодСубъекта(_rs.PartyID,1),           col2_len, null, "l:"+format); 
    Rep.AddPrintCell(Субъект.Name + " (" + Субъект.ShortName + ")",col3_len, null, "l:"+format); 
    Rep.AddPrintCell(_rs.ID,                                       col4_len, null, "l:"+format); 
    Rep.AddPrintCell(_rs.Deal_Code,                                col5_len, null, "c:"+format);
    Rep.AddPrintCell(string(_rs.Date),                             col6_len, null, "c:"+format);
    Rep.AddPrintCell(_rs.GenAgrID,                                 col7_len, null, "c:"+format); 
    Rep.AddPrintCell(_rs.GA_Code,                                  col8_len, null, "r:"+format); 
    Rep.AddPrintCell(_rs.Reporting,                                col9_len, null, "r:"+format); 

    ДобавитьСтроку;
    
  end;
end;


macro run()
  Rep = CMakeReport("┌┬┬┬┬┬┬┬┬┬┬┬┐");
  Rep.SetDefaultFontName("Times New Roman");
  Rep.SetDefaultFontSize(10);
  Rep.SetFlagShowGrid(true);

  Печатьшапки();

  sql = null;


SQL = 
"SELECT oo.t_id_operation, " +
"       ga.t_partyid , " +
"       dvn.t_id, " +
"       dvn.t_code t_deal_code, " +
"       to_char(DVN.T_DATE,'dd.mm.yyyy') t_date, " +
"       DVN.T_GENAGRID, " +
"       ga.t_code t_ga_code, " +
"       GA.T_REPORTINg, " +
"       rd.t_docid, " +
"       case when rd.t_docid is not null then chr(88) else chr(0) end t_rep_down " +
"   FROM doprstep_dbt os, " +
"       doproper_dbt oo, " +
"             ddvndeal_dbt dvn " +
"          LEFT JOIN " +
"             ddl_genagr_dbt ga " +
"          ON DVN.T_GENAGRID = GA.T_GENAGRID " +
"       LEFT JOIN " +
"          ddl_repozdeal_dbt rd " +
"       ON dvn.t_id = rd.t_docid, " +
"       ddvnfi_dbt dvfi, " +
"       dfininstr_dbt fi, " +
"       dfikinds_dbt fk  " +
"       left join (select ? repdate, ? NotReporting from dual) parm on 1=1 " +
" WHERE     OS.T_ID_OPERATION = oo.t_id_operation " +
"       AND dvn.t_id = DVFI.T_DEALID " +
"       AND DVFI.T_FIID = FI.T_FIID " +
"       AND FI.T_FI_KIND = FK.T_FI_KIND " +
"       AND fk.t_fi_kind = 1 " +
"       AND LPAD (dvn.T_ID, 34, 0) = oo.t_documentid " +
"       AND os.t_kind_operation = 12690 " +
"       AND ( (os.t_blockid = 20000151) " +
"            OR (os.t_blockid = 20000148 AND os.t_id_step = 35)) " +
"       AND (  extract(month from  os.t_plan_date ) = extract(month from repdate)    " +
"            OR  extract(month from  os.t_fact_date ) = extract(month from repdate)) " +
"       AND  nvl(ga.t_reporting, chr(0)) = case when NotReporting = 1 then chr(0)  " +
"       else   ga.t_reporting end        " +
"       AND DVN.T_GENAGRID = GA.T_GENAGRID " ;

cmd = DL_RSDCommand(sql);
cmd.addParam(repDate);
cmd.addParam(NotReportingFlag);

rs = Cmd.Execute();

ПечатьБлокаДанных(rs);

  Rep.PrintWinRep("Форварды");
  Rep.SetWinRepOutput();
  Rep.ShowWinRep();

end;

END; // CLASS clF405

CLASS (TRsbPanel) Pan
    initTRsbPanel();

    const Delta = 50;

    const K_Enter = 13;
    const K_F3    = 317;
    const K_F2    = 316;

    const CalcMonth_ID = 1;

    var curstr = 1;
    Caption = "Репортинг. Форвард по валюте.";
    Status = "~ESC~Выход ~F2~Выполнить";
    setSize(37,5);
    setPosition(30,10);
    var CalcMonth, dx;
    var NotReporting = 0;
    

    curstr = curstr+1;
    CalcMonth = TRsbEditField(V_DATE);
    CalcMonth.setPosition(16,curstr);
    CalcMonth.setSize(8,1);
    CalcMonth.name = "begincalcdate";
    CalcMonth.value = {curdate};
    addControl(CalcMonth);

    var m_LabelCD:TrsbLabel = TRsbLabel(2,curstr,"Дата отчета:");
    addLabel (m_LabelCD);

    addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "onKeyPressed"));
    
    curstr = curstr+1;
    NotReporting = TRsbCheckbox;
    NotReporting.setPosition(2,curstr);
    NotReporting.name = "NotReporting";
    NotReporting.checked = True;
    NotReporting.setsize(1,1);
    addControl(NotReporting);

    var m_LabelNotReporting:TrsbLabel = TRsbLabel(18,curstr,":Только без признака репортинга." );
    m_LabelNotReporting.setsize(23,1);
    addLabel (m_LabelNotReporting);

    addEventHandler(RSB_EV_CONTROL_CHECKED, R2M(this, "onChecked"));

    macro onKeyPressed(ev)
     if (ev.KeyCode == K_F3)
           if (ev.id == CalcMonth_ID)
            CalcMonth.value = GetDateByCalendar({CurDate});
            this.redraw; 
           end;
        elif (ev.KeyCode == K_Enter)
            this.redraw; 
        elif(ev.keyCode == K_F2)
                
         var myRep = IR_Reporting(CalcMonth.value);
         myRep.run;
         close(0);
        end;
    end; 
    
    macro OnChecked(RsbEvent:Object)
       if (RsbEvent.id == RSB_EV_CONTROL_CHECKED)

          if (RsbEvent.source.name == "NotReporting")
             if (NotReportingFlag == 0)
                NotReportingFlag = 1;
             else
                NotReportingFlag = 0;
             end;
             this.redraw();
          end;
       end;
         
       return true;
    end;

END;

macro exec_ir_rep()
  var myPanel:TRsbPanel = Pan;
  myPanel.run;
  exit(1);
end;
