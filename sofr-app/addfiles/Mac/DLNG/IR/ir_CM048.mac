/*
    $Name:  ir_CM048.mac
    $Module:   Репозитарий
    $Description:  Класс для сообщения CM048
*/

import "ir_CMBase.mac";

class (CMBase) CM048 ( _isRecon, _conveyerID, _ir_op : object)
  InitCMBase();
  
  m_isRecon     = _isRecon;
  m_conveyerID  = _conveyerID;
  m_ir_op       = _ir_op;
  m_MessageID   = REPOS_CM048;

  macro addCMAll(ir_generalinf     ,
                 TradeRepository   ,
                 Party1            ,
                 Party2            ,
                 UTIGeneratingParty,
                 Sender            ,
                 cm                ,
                 Instruments       ,
                 GrDealID          ,
                 TemplNum          ,
                 DealParmForReport )
    addGenInf(ir_generalinf, TemplNum, DealParmForReport);
    addRepAccGrDoc(GrDealID);
    addcmGrDoc(GrDealID);
    addParty(TradeRepository, Party1, Party2, UTIGeneratingParty, Sender);
    addCM(cm);
    addInstrument(Instruments);
  end;

  macro insertAll
    var err = 0;
    err = insertGeneralinf;
    if (not err)
      err = insertParty;
    end;
    if (not err)
      err = insertDlgrdocCache;
    end;
    if (not err)
      err = insertCM;
    end;
    if (not err)
      err = insertInstrument;
    end;
    return err;
  end;


  macro ЗаполнениеCM (cm048 : @TrecHandler, 
                      DataSet, 
                      GenAgr,
                      FD,
                      first,
                      field,
                      Instruments : @TrecHandler, 
                      DealParmForReport)
    //Instruments.clear();

    if( (FD.IsBuy() and (GenAgr.Side1 == 1)) or
        (FD.IsSale() and (GenAgr.Side1 == 2)) )
        cm048.rec.buyerid  = IR_PARTY_KIND_PARTY1;
        cm048.rec.buyer    = "Party1";
        cm048.rec.sellerid = IR_PARTY_KIND_PARTY2;
        cm048.rec.seller   = "Party2";
    else
        cm048.rec.buyerid  = IR_PARTY_KIND_PARTY2;
        cm048.rec.buyer    = "Party2";
        cm048.rec.sellerid = IR_PARTY_KIND_PARTY1;
        cm048.rec.seller   = "Party1";
    end;
 
    if( FD.Deal.rec.OptionStyle == DVSTYLE_AMERICAN )
        cm048.rec.CommencementDate = FD.ДатаНачала();
        cm048.rec.ExpirationDate = FD.ДатаИстеченияСрокаДействия();
        cm048.rec.OptionStyle = "A";
    else
        cm048.rec.ExpirationDate = FD.ДатаИсполнения();
        cm048.rec.OptionStyle = "E";
    end;

    if( FD.ПоставочныйКонтракт() )
        cm048.rec.SettlementTypeId = 2; // Physical
    else
        cm048.rec.SettlementTypeId = 1; // Cash
    end;

    cm048.rec.PremiumPayerId    = cm048.rec.buyerid;
    cm048.rec.PremiumPayer      = cm048.rec.buyer;
    cm048.rec.PremiumReceiverId = cm048.rec.sellerid;
    cm048.rec.PremiumReceiver   = cm048.rec.seller;
    cm048.rec.OptionTypeId      = FD.Deal.rec.OptionType;
    cm048.rec.OptionStyleId     = FD.Deal.rec.OptionStyle;
    cm048.rec.PremiumCurrencyId = FD.Deal.rec.BonusFIID;
    cm048.rec.PremiumAmount     = FD.Deal.rec.Bonus;
    cm048.rec.OptionEntitlement = FD.nFI_().rec.Amount; // Количество БА из сделки
    cm048.rec.NumberOfOptions   = 1;
    cm048.rec.OpenUnit          = cm048.rec.OptionEntitlement * cm048.rec.NumberOfOptions; // Количество ц/б сделки
    cm048.rec.SettlementCurrencyId = FD.ВалютаРасчётов();
    cm048.rec.Strike            = FD.nFI_().rec.Price; // Страйк - это цена единицы базового актива, цена исполнения в опционном контракте
    cm048.rec.StrikeCurrencyId  = FD.nFI_().rec.PriceFIID;
    cm048.rec.OptionType        = substr( DL_GetNameAlg( 3423 /*ALG_FI_DVTYPE*/, cm048.rec.OptionTypeId ), 1, 4 );
    cm048.rec.SettlementType    = substr( DL_GetNameAlg( 7044 /*ALG_DV_OPTION_PAYKIND*/, cm048.rec.SettlementTypeId ), 1, 8 );
    cm048.rec.SettlementCurrency= GetFICode(cm048.rec.SettlementCurrencyId, null, FICK_ISOSTRING);
    cm048.rec.PremiumCurrency   = GetFICode(cm048.rec.PremiumCurrencyId, null, FICK_ISOSTRING);
    cm048.rec.StrikeCurrency    = GetFICode(cm048.rec.StrikeCurrencyId, null, FICK_ISOSTRING);

    Instruments.rec.FIID = FD.BaseFI().rec.FIID;
    Instruments.rec.InstrumentCode = GetAvoirissCode(FD.BaseFI(), m_ir_op.rec.Date);
    Instruments.rec.Instrumentname = FD.BaseFI().rec.Name;

    return 0;
  end;
end;
