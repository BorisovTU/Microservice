/*
    $Name: ir_CM002.mac
    $Module:   Репозитарий
    $Description:  Несогласие с параметрами
*/

import "ir_CMBase.mac";

class (CMBase) CM002 ( _isRecon, _conveyerID, _ir_op : object)
  InitCMBase();

  m_isRecon     = _isRecon;
  m_conveyerID  = _conveyerID;
  m_ir_op       = _ir_op;
  m_MessageID   = REPOS_CM002;

  macro addCMAll(ir_generalinf     ,
                 TradeRepository   ,
                 Party1            ,
                 Party2            ,
                 UTIGeneratingParty,
                 Sender            ,
                 GrDealID          ,
                 TemplNum          ,
                 DealParmForReport )
    addGenInf(ir_generalinf, TemplNum, DealParmForReport);
    addRepAccGrDoc(GrDealID);
    addcmGrDoc(GrDealID);
    addParty(TradeRepository, Party1, Party2, UTIGeneratingParty, Sender);
  end;

  macro insertAll
    var err = 0;
    err = insertGeneralinf;
    if (not err)
      err = insertParty;
    end;
    if (not err)
      err = insertDlgrdocCache;
    end;
    return err;
  end;

  macro ЗаполнениеCM (ir_generalinf: @TRecHandler, ir_op: object, CM001_ID: @integer, RM005_ID: @integer)
    //var ir_op = TBFile("ir_op.dbt");
      var DocID: integer;
      var DocKind: integer;
          
      // находим связанные CM001 и RM005
      CM001_ID = 0;
      RM005_ID = 0;         
      var cmd = DL_RSDCommand();
      var DataSet = cmd.Execute("SELECT T_DocID as DocID, T_DocKind as DocKind FROM ddlgrdeal_dbt WHERE T_ID =" + ir_op.rec.DealID);            
      if ( DataSet.moveNext() )
          DocID = DataSet.DocID;
          DocKind = DataSet.DocKind;
          // GrDealID = in (select t_id from ddlgrdeal_dbt where t_docid=DocID and t_dockind=DocKind)
          var DataCM001 = cmd.Execute("SELECT DISTINCT gif.T_InternalMessageID as MesID, gif.T_ReasonCode ReasonCode, gif.T_ReasonDescription ReasonDesc" +
              " FROM dir_generalinf_dbt gif, ddlgrdoc_dbt doc" +
              " WHERE doc.T_GrDealID IN (SELECT T_ID FROM ddlgrdeal_dbt WHERE t_DocID = " + DocID + " AND T_DocKind = " + DocKind + ")" +
              " AND doc.T_DocKind = 4853 AND gif.T_InternalMessageID = doc.T_DocID AND gif.T_MessageType = 'CM001'");    // СРС
          if( DataCM001.moveNext() )
              CM001_ID = DataCM001.MesID;
          end;    
          var DataRM005 = cmd.Execute("SELECT DISTINCT gif.T_InternalMessageID as MesID, gif.T_TradeID_Party2 TradeID2, gif.T_RStatus RStatus" + 
              " FROM dir_generalinf_dbt gif, ddlgrdoc_dbt doc" +
              " WHERE doc.T_GrDealID IN (SELECT T_ID FROM ddlgrdeal_dbt WHERE t_DocID = " + DocID + " AND T_DocKind = " + DocKind + ")" +
              " AND doc.T_DocKind = 4861 AND gif.T_InternalMessageID = doc.T_DocID AND gif.T_MessageType = 'RM005'"+    // сверка
              " AND gif.T_Rstatus != 8");
          if( DataRM005.moveNext() )
              RM005_ID = DataRM005.MesID;
          end;
          if( (CM001_ID == 0) OR (RM005_ID == 0))
              return 1;
          end;

          ir_generalinf.rec.TradeID_Party2 = DataRM005.TradeID2;

          if( DataRM005.RStatus == RSTATUS_CRITICALDIFF)
              if( DataCM001.ReasonCode == "" )
                  ir_generalinf.rec.ReasonCode = "RCDP004";
                  ir_generalinf.rec.ReasonDescription = GetValueLLName(4029, "RCDP004");
              else
                  ir_generalinf.rec.ReasonCode = DataCM001.ReasonCode;
                  if(DataCM001.ReasonDesc == "")
                      ir_generalinf.rec.ReasonDescription = GetValueLLName(4029, ir_generalinf.rec.ReasonCode);
                  else
                      ir_generalinf.rec.ReasonDescription = DataCM001.ReasonDesc;
                  end;
              end;
          end;
          if((ir_generalinf.rec.ReasonCode == "") or (ir_generalinf.rec.ReasonDescription == "")) 
              return 1;
          end;

          return 0;
      else
          return 1;
      end;
  end;

end;