/*                           
$Name:        ir_repstatusmsg.mac
$Module:      Репозитарий
$Description: Запуск "Отчет 'Состояние сообщений Репозитария'"
*/

import IRInter, BankInter, XmlRpcInter, "dllog_xml.mac", "DlReport_h.mac", "dldlngfun.mac", "spserv.mac";

private var NFooter = "######################################" + "#####################################\n" +
                      "######################################" + "#####################################\n" +
                                                                 "#####################################\n" +
                      "######################################" + "#####################################\n";

private var RequestTemplSQL = DLGR_TEMPL_CLOSECONTRWTHREQUEST   + ", "
                            + DLGR_TEMPL_EXECDELAYMSGWTHREQUEST + ", "
                            + DLGR_TEMPL_MAKEDEALWTHREQUEST     + ", "
                            + DLGR_TEMPL_CHANGEMSGWTHREQUEST    + ", "
                            + DLGR_TEMPL_EXECHOLDMSGWTHREQUEST  + ", "
                            + DLGR_TEMPL_EARLYEXECMSGWTHREQUEST + ", "
                            + DLGR_TEMPL_REJECTIONMSGWTHREQUEST + ", "
                            + DLGR_TEMPL_NETTINGSUSPWTHREQUEST  + ", "
                            + DLGR_TEMPL_MAKECONTRACTWTHREQUEST;

private var DateShipmentMessagesFR = "'РЕПОЗИТАРИЙ\\СРОК ПОДГОТОВКИ СООБЩ.-ОТЧЕТА'";
private var DeadLineDateShipmentMessagesFR = "'РЕПОЗИТАРИЙ\\ПРЕД. СРОК ОТПР. СБЩ.-ОТЧЕТА'";
private var FirstMsgPeriodSettingPath = "'РЕПОЗИТАРИЙ\\СРОК ПОДГОТ.СБЩ.-АНКЕТЫ СДЕЛКИ'";
private var DeadLineFirstMsgPeriodSettingPath = "'РЕПОЗИТАРИЙ\\ПРЕД.СРОК ОТПР.СБЩ.-АНКЕТЫ ДОГ.'";
private var ContractGAMsgPeriodSettingPath = "'РЕПОЗИТАРИЙ\\СРОК ПОДГОТ. СООБЩ.-АНКЕТЫ ГС'";

PRIVATE CLASS LineRepData(_A1, _A2, _A3, _A4, _A5, _A6, _A7, _A8, _A9)
  var A1 = _A1;
  var A2 = _A2;
  var A3 = _A3;
  var A4 = _A4;
  var A5 = _A5;
  var A6 = _A6;
  var A7 = _A7;
  var A8 = _A8;
  var A9 = _A9;
END;

private macro РАБОТА_С_РЕПОЗИТАРИЕМ_ФИССиКО()
    const RegPath = "COMMON\\WORK_MODE\\IR_WORK_WITH_DERIVATIVES";
    var workWithRepository = "", err;
    GetRegistryValue( RegPath, V_BOOL, @workWithRepository, err );
    
    if ( err != 0 )
        MsgBox( "Ошибка при получении значения настройки \"" + RegPath + "\"" );
    else
        if ( workWithRepository == "X" )
            return true;
        else
            return false;
        end;
    end;
end;

private macro РАБОТА_С_РЕПОЗИТАРИЕМ_БОЦБ()
    const RegPath = "COMMON\\WORK_MODE\\IR_WORK_WITH_SECURITIES";
    var workWithRepository = "", err;
    
    GetRegistryValue( RegPath, V_BOOL, @workWithRepository, err );
    
    if ( err != 0 )
        MsgBox( "Ошибка при получении значения настройки \"" + RegPath + "\"" );
    else
        if ( workWithRepository == "X" )
            return true;
        else
            return false;
        end;
    end;
end;

PRIVATE CLASS (DL_CReportTemplate) IR_RepStatusMsg(Mode, RepDate, StatusProcessMsg, UnloadedMsg, ContractWthRequest)
  private var m_PrintMode;
  private var m_Executer = DepoExecuter( {oper} );
  private var SheetName = "Список событий";
  private var m_RepDate;
  private var m_StatusProcessMsg;
  private var m_UnloadedMsg;
  private var m_ContractWthRequest;
  private var SchedAndNotPrepMsg = TArray();
  private var PrepAndNotUnloadMsg = TArray();
  private var RequestContract = TArray();
  private var DealsRequestSQL;

  PRIVATE MACRO PrintMode():INTEGER
     return m_PrintMode;
  END;
  
  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    CreateTotalBook();
  END;

  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;

  PRIVATE MACRO GetSQLRequestDeals()
    DealsRequestSQL =             "WITH deals"
                                + "     AS ( ";
    if( РАБОТА_С_РЕПОЗИТАРИЕМ_ФИССиКО() )
      DealsRequestSQL = DealsRequestSQL 
                                + " (SELECT t_ID AS t_DocID, t_DocKind, t_contractor, t_code"
                                + "             FROM DDVNDEAL_DBT"
                                + "            WHERE t_DocKind IN (" + DL_DVNDEAL +", " + DL_DVDEALT3 + ", " + DL_DVFXDEAL + "))"
                                + "         UNION ALL";
    end;
    if( РАБОТА_С_РЕПОЗИТАРИЕМ_БОЦБ() )
      DealsRequestSQL = DealsRequestSQL 
                                + "         (SELECT t_DealID AS t_DocID,"
                                + "                 t_BofficeKind AS t_DocKind,"
                                + "                 t_partyid AS t_contractor,"
                                + "                 t_dealcode AS t_code"
                                + "            FROM DDL_TICK_DBT"
                                + "           WHERE t_BofficeKind = " + DL_SECURITYDOC +")"
                                + "         UNION ALL";
    end;

      DealsRequestSQL = DealsRequestSQL  
                                + "         (SELECT t_genagrid AS t_docid, t_dockind, t_partyid AS t_contractor, t_code"
                                + "            FROM ddl_genagr_dbt"
                                + "           WHERE t_pseudoga <> 'X' AND t_reporting = 'X')"
                                + "         UNION ALL"
                                + "         (SELECT t_csaid AS t_docid,"
                                + "                 " + DV_CSA + " AS t_dockind,"
                                + "                 t_partyid AS t_contractor,"
                                + "                 t_code"
                                + "            FROM ddvcsa_dbt"
                                + "           WHERE t_reposrep = 'X'))";
  END;

  PRIVATE MACRO GetDataForSchedAndNotPrepMsg()
    var cmd, DataSet, query;
    var i = 1, cnt;
    var progr;

    query = DealsRequestSQL
            + "  SELECT OPRK.T_NAME AS oprname,"
            + "         GRTEMPL.T_NAME AS templname,"
            + "         (CASE"
            + "                    WHEN grdeal.t_templnum = " + DLGR_TEMPL_REVALUATION_FAIRVALUE
            + "                    THEN"
            + "                         'CM094'"
            + "                    WHEN grdeal.t_templnum = " + DLGR_TEMPL_CSA_MARGIN_PAYMENT
            + "                    THEN"
            + "                         'CM092'"
            + "                    WHEN grdeal.t_templnum = " + DLGR_TEMPL_BULKREPORT
            + "                    THEN"
            + "                         'CM084'"
            + "                    WHEN (grdeal.t_templnum = " + DLGR_TEMPL_CLOSECONTR + " AND RD.T_MESSAGECODE = 'CM010') "
            + "                    THEN"
            + "                         'CM011'"
            + "                    WHEN ((not(grdeal.t_templnum < " + DLGR_TEMPL_CLOSECONTR + ")) AND (not(grdeal.t_templnum > " + DLGR_TEMPL_NETTINGSUSPWTHREQUEST + "))) "
            + "                    THEN"
            + "                         'CM093'"
            + "                    ELSE"
            + "                         RD.T_MESSAGECODE"
            + "                 END) as MESSAGECODE,"
            + "         (CASE"
            + "                    WHEN grdeal.t_templnum = " + DLGR_TEMPL_REVALUATION_FAIRVALUE
            + "                    THEN"
            + "                         LAST_DAY (GRDEAL.T_PLANDATE)"
            + "                    ELSE"
            + "                         GRDEAL.T_PLANDATE"
            + "                 END) as PlanDate,"
            + "         deals.t_code,"
            + "         grdeal.t_templnum,"
            + "         (CASE"
            + "                    WHEN grdeal.t_templnum = " + DLGR_TEMPL_REVALUATION_FAIRVALUE
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( LAST_DAY (GRDEAL.T_PLANDATE),"
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DateShipmentMessagesFR + ") )"
            + "                    WHEN grdeal.t_templnum = " + DLGR_TEMPL_CSA_MARGIN_PAYMENT
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DateShipmentMessagesFR + ") )"
            + "                    WHEN (grdeal.t_templnum = " + DLGR_TEMPL_MAKECONTRACT + " OR grdeal.t_templnum = " + DLGR_TEMPL_MAKECONTRACT_CORRECTION + ")"
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + ContractGAMsgPeriodSettingPath + ") )"
            + "                    WHEN (grdeal.t_templnum = " + DLGR_TEMPL_CLOSECONTR + " AND RD.T_MESSAGECODE = 'CM010')"
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + ContractGAMsgPeriodSettingPath + ") )"
            + "                    ELSE"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + FirstMsgPeriodSettingPath + ") )"
            + "                 END) as SetDate,"
            + "         (CASE"
            + "                    WHEN grdeal.t_templnum = " + DLGR_TEMPL_REVALUATION_FAIRVALUE
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( LAST_DAY (GRDEAL.T_PLANDATE), "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DeadLineDateShipmentMessagesFR + ") )"
            + "                    WHEN grdeal.t_templnum = " + DLGR_TEMPL_CSA_MARGIN_PAYMENT
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DeadLineDateShipmentMessagesFR + ") )"
            + "                    WHEN (grdeal.t_templnum = " + DLGR_TEMPL_MAKECONTRACT + " OR grdeal.t_templnum = " + DLGR_TEMPL_MAKECONTRACT_CORRECTION + ")"
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DeadLineFirstMsgPeriodSettingPath + ") )"
            + "                    WHEN (grdeal.t_templnum = " + DLGR_TEMPL_CLOSECONTR + " AND RD.T_MESSAGECODE = 'CM010')"
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE,"
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DeadLineFirstMsgPeriodSettingPath + ") )"
            + "                    ELSE"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DeadLineFirstMsgPeriodSettingPath + ") )"
            + "                 END) as DeadLineDate"
            + "    FROM ddlgrdeal_dbt grdeal,"
            + "         ddlgracc_dbt gracc,"
            + "         deals,"
            + "         doproper_dbt opr,"
            + "         doprkoper_dbt oprk,"
            + "         ddlgrtempl_dbt grtempl,"
            + "         ddl_repozdeal_dbt rd"
            + "   WHERE     grdeal.t_docid = deals.t_docid"
            + "         AND grdeal.t_dockind = deals.t_dockind"
            + "         AND GRACC.T_GRDEALID = GRDEAL.T_ID"
            + "         AND GRACC.T_ACCNUM = " + DLGR_ACCKIND_REPOSITORY
            + "         AND GRACC.T_STATE = " + DLGRACC_STATE_PLAN
            + "         AND GRDEAL.T_PLANDATE <=" 
            + "                (CASE" 
            + "                     WHEN deals.t_DocKind = 4626"
            + "                     THEN" 
            + "                          RSI_RsbCalendar.GetDateAfterWorkDay( ?, " 
            + "                          rsb_common.GetRegIntValue ("
            + "                            " + DateShipmentMessagesFR + ") )" 
            + "                     WHEN GRDEAL.T_TEMPLNUM = " + DLGR_TEMPL_CSA_MARGIN_PAYMENT
            + "                     THEN"
            + "                          RSI_RsbCalendar.GetDateAfterWorkDay( ?, "
            + "                          rsb_common.GetRegIntValue ("
            + "                            " + DateShipmentMessagesFR + ") )" 
            + "                     WHEN GRDEAL.T_TEMPLNUM = " + DLGR_TEMPL_MAKECONTRACT
            + "                     THEN"
            + "                          RSI_RsbCalendar.GetDateAfterWorkDay( ?, "
            + "                          rsb_common.GetRegIntValue ("
            + "                            " + ContractGAMsgPeriodSettingPath + ") )" 
            + "                     WHEN (GRDEAL.T_TEMPLNUM = " + DLGR_TEMPL_CLOSECONTR + " AND RD.T_MESSAGECODE = 'CM010')"
            + "                     THEN"
            + "                          RSI_RsbCalendar.GetDateAfterWorkDay( ?, "
            + "                          rsb_common.GetRegIntValue ("
            + "                            " + ContractGAMsgPeriodSettingPath + ") )" 
            + "                     ELSE" 
            + "                          RSI_RsbCalendar.GetDateAfterWorkDay( ?, " 
            + "                          rsb_common.GetRegIntValue ("
            + "                            " + FirstMsgPeriodSettingPath + ") ) END)"
            + "         AND OPR.T_DOCKIND = deals.t_dockind"
            + "         AND TO_NUMBER (OPR.T_DOCUMENTID) = deals.t_docid"
            + "         AND OPRK.T_KIND_OPERATION = OPR.T_KIND_OPERATION"
            + "         AND OPRK.T_DOCKIND = OPR.T_DOCKIND"
            + "         AND GRTEMPL.T_NUM = GRDEAL.T_TEMPLNUM"
            + "         AND RD.T_DOCID = deals.t_docid"
            + "         AND RD.T_DOCKIND = deals.t_dockind "
            + "         AND GRDEAL.T_TEMPLNUM NOT IN (" + RequestTemplSQL + " ) "
            + "ORDER BY PlanDate, SetDate, MESSAGECODE";
    
    cmd = DL_RSDCommand(query);
    cmd.AddParam(m_RepDate);
    cmd.AddParam(m_RepDate);
    cmd.AddParam(m_RepDate);
    cmd.AddParam(m_RepDate);
    cmd.AddParam(m_RepDate);

    cnt = cmd.GetCount();
    if( cnt > 0 )
      DataSet = cmd.execute();

      progr = 0;
      InitProgress(cnt, "Формирование таблицы \"Запланированные и не подготовленные сообщения\"");
      while( DataSet.MoveNext() )
        var size = SchedAndNotPrepMsg.size;
        var NumbDays;

        if( (DataSet.TemplNum == DLGR_TEMPL_MAKECONTRACT_CORRECTION) OR (DataSet.TemplNum == DLGR_TEMPL_MAKEDEAL_CORRECTION) )
          DataSet.messagecode = DataSet.messagecode + " (корректирующее сообщение)";
        end;

        if( DataSet.TemplNum == DLGR_TEMPL_CHANGEMSG )
          DataSet.messagecode = DataSet.messagecode + " (изменение условий)";
        end;

        SchedAndNotPrepMsg[size] = LineRepData(i, DataSet.oprname, DataSet.code, DataSet.templname, DataSet.messagecode, date(DataSet.plandate), date(DataSet.SetDate), date(DataSet.DeadLineDate));
        i = i + 1;
        UseProgress(progr = progr + 1);
      end;
      RemProgress();
    end;
  END;

  PRIVATE MACRO GetDataForPrepAndNotUnloadMsg()
    var cmd, DataSet, query;
    var i = 1, cnt;
    var progr;

    query = DealsRequestSQL
            + "  SELECT OPRK.T_NAME AS oprname,"
            + "         GRTEMPL.T_NAME AS templname,"
            + "         IR_GENERALINF.T_MESSAGETYPE,"
            + "         (CASE"
            + "                    WHEN IR_GENERALINF.T_MESSAGETYPE = 'CM094'"
            + "                    THEN"
            + "                         LAST_DAY (GRDEAL.T_PLANDATE)"
            + "                    ELSE"
            + "                         GRDEAL.T_PLANDATE"
            + "                 END) as PlanDate,"
            + "         deals.t_code,"
            + "         grdeal.t_templnum,"
            + "         ir_generalinf.t_PrDate,"
            + "         (CASE"
            + "                    WHEN IR_GENERALINF.T_MESSAGETYPE = 'CM094' " 
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( LAST_DAY (GRDEAL.T_PLANDATE), "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DeadLineDateShipmentMessagesFR + ") )"
            + "                    WHEN IR_GENERALINF.T_MESSAGETYPE = 'CM092' " 
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DeadLineDateShipmentMessagesFR + ") )"
            + "                    WHEN IR_GENERALINF.T_MESSAGETYPE = 'CM010' " 
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DeadLineFirstMsgPeriodSettingPath + ") )"
            + "                    WHEN IR_GENERALINF.T_MESSAGETYPE = 'CM011' " 
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DeadLineFirstMsgPeriodSettingPath + ") )"
            + "                    ELSE"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DeadLineFirstMsgPeriodSettingPath + ") )"
            + "                 END) as DeadLineDate, "
            + "         IR_GENERALINF.T_RSTATUSNAME"
            + "    FROM ddlgrdeal_dbt grdeal,"
            + "         ddlgracc_dbt gracc,"
            + "         deals,"
            + "         doproper_dbt opr,"
            + "         doprkoper_dbt oprk,"
            + "         ddlgrtempl_dbt grtempl,"
            + "         ddlgrdoc_dbt grdoc,"
            + "         dir_generalinf_dbt ir_generalinf"
            + "   WHERE     grdeal.t_docid = deals.t_docid"
            + "         AND grdeal.t_dockind = deals.t_dockind"
            + "         AND GRACC.T_GRDEALID = GRDEAL.T_ID"
            + "         AND GRACC.T_ACCNUM = " + DLGR_ACCKIND_REPOSITORY
            + "         AND GRACC.T_STATE = " + DLGRACC_STATE_FACTEXEC
            + "         AND GRDEAL.T_PLANDATE <= ?"
            + "         AND OPR.T_DOCKIND = deals.t_dockind"
            + "         AND TO_NUMBER (OPR.T_DOCUMENTID) = deals.t_docid"
            + "         AND OPRK.T_KIND_OPERATION = OPR.T_KIND_OPERATION"
            + "         AND OPRK.T_DOCKIND = OPR.T_DOCKIND"
            + "         AND GRTEMPL.T_NUM = GRDEAL.T_TEMPLNUM"
            + "         AND GRDOC.T_GRDEALID = GRDEAL.T_ID"
            + "         AND GRDOC.T_DOCKIND = " + REPOS_GENERALINF
            + "         AND GRDOC.T_SERVDOCKIND in (" + REPOS_SERVICEOPERATION + ", " + REPOS_INBOUNDMSG + ", 0)"
            + "         AND IR_GENERALINF.T_INTERNALMESSAGEID = GRDOC.T_DOCID";
    if (m_UnloadedMsg)
      query = query
            + "         AND IR_GENERALINF.T_RSTATUS in (1, 2, 3, 4, 13, 14)";
    else
      query = query
            + "         AND IR_GENERALINF.T_RSTATUS in (1, 2, 3, 4, 14) ";
    end;
    query = query
            + "         AND ? >="
            + "                (CASE"
            + "                    WHEN IR_GENERALINF.T_MESSAGETYPE = 'CM094'"
            + "                    THEN"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( LAST_DAY (GRDEAL.T_PLANDATE), "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + DateShipmentMessagesFR + ") )"
            + "                    ELSE"
            + "                         RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                         rsb_common.GetRegIntValue ("
            + "                            " + FirstMsgPeriodSettingPath + ") )"
            + "                 END)"
            + "ORDER BY PlanDate, IR_GENERALINF.t_PrDate, IR_GENERALINF.T_MESSAGETYPE";

    cmd = DL_RSDCommand(query);
    cmd.AddParam(m_RepDate);
    cmd.AddParam(m_RepDate);

    cnt = cmd.GetCount();
    if( cnt > 0 )
      DataSet = cmd.execute();

      progr = 0;
      InitProgress(cnt, "Формирование таблицы \"Подготовленные и не выгруженные сообщения\"");
      while( DataSet.MoveNext() )
        var size = PrepAndNotUnloadMsg.size;
        var DeadlineDate:date;
        var NumbDays;

        if( (DataSet.TemplNum == DLGR_TEMPL_MAKECONTRACT_CORRECTION) OR (DataSet.TemplNum == DLGR_TEMPL_MAKEDEAL_CORRECTION) )
          DataSet.messagetype = DataSet.messagetype + " (корректирующее сообщение)";
        end;

        if( DataSet.TemplNum == DLGR_TEMPL_CHANGEMSG )
          DataSet.messagetype = DataSet.messagetype + " (изменение условий)";
        end;

        PrepAndNotUnloadMsg[size] = LineRepData(i, DataSet.oprname, DataSet.code, DataSet.templname, DataSet.messagetype, date(DataSet.plandate), date(DataSet.PrDate), date(DataSet.DeadLineDate), DataSet.RStatusName);
        i = i + 1;
        UseProgress(progr = progr + 1);
      end;
      RemProgress();
    end;
  END;

  PRIVATE MACRO GetDataForRequestContract()
    var cmd, DataSet, query;
    var i = 1, cnt;
    var progr;

    query = DealsRequestSQL
            + "  SELECT OPRK.T_NAME AS oprname,"
            + "         GRTEMPL.T_NAME AS templname,"
            + "         deals.t_contractor,"
            + "         GRDEAL.T_PLANDATE,"
            + "         deals.t_code,"
            + "         (  RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE,"
            + "          - rsb_common.GetRegIntValue ("
            + "               'РЕПОЗИТАРИЙ\\СРОК ОЖИД.ЗАПР. ПРИ КОМБ.ПОДТВ.') ) )"
            + "            AS FactDate"
            + "    FROM ddlgrdeal_dbt grdeal,"
            + "         ddlgracc_dbt gracc,"
            + "         deals,"
            + "         doproper_dbt opr,"
            + "         doprkoper_dbt oprk,"
            + "         ddlgrtempl_dbt grtempl"
            + "   WHERE     grdeal.t_docid = deals.t_docid"
            + "         AND grdeal.t_dockind = deals.t_dockind"
            + "         AND GRACC.T_GRDEALID = GRDEAL.T_ID"
            + "         AND GRACC.T_ACCNUM = " + DLGR_ACCKIND_REPOSITORY
            + "         AND GRACC.T_STATE = " + DLGRACC_STATE_PLAN
            + "         AND OPR.T_DOCKIND = deals.t_dockind"
            + "         AND TO_NUMBER (OPR.T_DOCUMENTID) = deals.t_docid"
            + "         AND OPRK.T_KIND_OPERATION = OPR.T_KIND_OPERATION"
            + "         AND OPRK.T_DOCKIND = OPR.T_DOCKIND"
            + "         AND GRTEMPL.T_NUM = GRDEAL.T_TEMPLNUM"
            + "         AND GRDEAL.T_TEMPLNUM IN (" + RequestTemplSQL + " ) "
            + "         AND SYSDATE >= RSI_RsbCalendar.GetDateAfterWorkDay( GRDEAL.T_PLANDATE, "
            + "                rsb_common.GetRegIntValue ("
            + "                   'РЕПОЗИТАРИЙ\\СРОК ОЖИД.ЗАПР. ПРИ КОМБ.ПОДТВ.') ) "
            + "ORDER BY GRDEAL.T_PLANDATE, FactDate";
    
    cmd = DL_RSDCommand(query);
    cmd.AddParam(m_RepDate);

    cnt = cmd.GetCount();
    if( cnt > 0 )
      DataSet = cmd.execute();

      progr = 0;
      InitProgress(cnt, "Формирование таблицы \"Список договоров, по которым ожидается получение запроса на согласование\"");
      while( DataSet.MoveNext() )
        var size = RequestContract.size;
        var ContractorName = ПолучитьКороткоеИмяСубъекта(DataSet.Contractor);

        RequestContract[size] = LineRepData(i, DataSet.oprname, DataSet.code, ContractorName, DataSet.templname, date(DataSet.FactDate), date(DataSet.plandate));
        i = i + 1;
        UseProgress(progr = progr + 1);
      end;
      RemProgress();
    end;
  END;

  PRIVATE MACRO PrintSchedAndNotPrepMsg()
    var i = 0;

    for (var data, SchedAndNotPrepMsg)

        if ( i == 0 )
            CopyAllSheetInTotalBook(SheetName, false, "N1_TableHeader", 1, SheetName);
            RegisterTable( "N1_SchedAndNotPrepEvent", NULL,
                           "N1_N",
                           "N1_KindOperation",
                           "N1_CodeOperation",
                           "N1_TemplNum",
                           "N1_KindMsg",
                           "N1_RepDateEvent",
                           "N1_SetDate",
                           "N1_DateDeadlinePrep"
                         );            
       end;
       PrintTableLine( "N1_N",                data.A1, null,
                       "N1_KindOperation",    data.A2, null,
                       "N1_CodeOperation",    data.A3, null,
                       "N1_TemplNum",         data.A4, null,
                       "N1_KindMsg",          data.A5, null,
                       "N1_RepDateEvent",     data.A6, null,
                       "N1_SetDate",          data.A7, null,
                       "N1_DateDeadlinePrep", data.A8, null
                     );
        i = i + 1;
    end;
    
    EndTable();
    CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);
  END;

  PRIVATE MACRO PrintPrepAndNotUnloadMsg()
    var i = 0;

    for (var data, PrepAndNotUnloadMsg)

        if ( i == 0 )
            CopyAllSheetInTotalBook(SheetName, false, "N2_TableHeader", 1, SheetName);
            RegisterTable( "N2_PrepAndNotUnloadMsg", NULL,
                           "N2_N",
                           "N2_KindOperation",
                           "N2_CodeOperation",
                           "N2_TemplNum",
                           "N2_KindMsg",
                           "N2_RepDateEvent",
                           "N2_DatePrep",
                           "N2_DeadLineDate",
                           "N2_StatusMsg"
                         );            
       end;
       PrintTableLine( "N2_N",                data.A1, null,
                       "N2_KindOperation",    data.A2, null,
                       "N2_CodeOperation",    data.A3, null,
                       "N2_TemplNum",         data.A4, null,
                       "N2_KindMsg",          data.A5, null,
                       "N2_RepDateEvent",     data.A6, null,
                       "N2_DatePrep",         data.A7, null,
                       "N2_DeadLineDate",     data.A8, null,
                       "N2_StatusMsg",        data.A9, null
                     );
        i = i + 1;
    end;
    
    EndTable();
    CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);
  END;

  PRIVATE MACRO PrintRequestContract()
    var i = 0;

    for (var data, RequestContract)

        if ( i == 0 )
            CopyAllSheetInTotalBook(SheetName, false, "N3_TableHeader", 1, SheetName);
            RegisterTable( "N3_RequestContract", NULL,
                           "N3_N",
                           "N3_KindOperation",
                           "N3_CodeOperation",
                           "N3_Contrator",
                           "N3_TemplNum",
                           "N3_RepDateEvent",
                           "N3_DateRequest"
                         );            
       end;
       PrintTableLine( "N1_N",                data.A1, null,
                       "N1_KindOperation",    data.A2, null,
                       "N1_CodeOperation",    data.A3, null,
                       "N3_Contrator",        data.A4, null,
                       "N3_TemplNum",         data.A5, null,
                       "N3_RepDateEvent",     data.A6, null,
                       "N3_DateRequest",      data.A7, null
                     );
        i = i + 1;
    end;
    
    EndTable();
    CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);
  END;

private MACRO AddStandardFooter(repDate: DATE)

    MACRO НачальникОтдела()
        var RS = TRsbDataSet("SELECT party.t_Name " +
                            "  FROM dofficer_dbt officer, dparty_dbt party " +
                            " WHERE officer.t_PartyID  = " + string({OurBank}) + " AND " +
                            "       officer.t_OfficeID = " + string(SelfDivision) + " AND " +
                            "       officer.t_IsFirstOfficePerson = 'X' AND "
                            "       party.t_PartyID = officer.t_PersonID "
                            );

        if( RS.MoveNext() )
          return RS.Name;
        end;
        return "";
    END;

    MACRO Операционист()
        var RS = TRsbDataSet("SELECT person.t_Name " +
                            "  FROM dperson_dbt person " +
                            " WHERE person.t_Oper = " + string({oper})
                            );

        if( RS.MoveNext() )
          return RS.Name;
        end;
        return "";
    END;

    MACRO ДолжностьОперациониста()
        return "Исполнитель";
        var RS = TRsbDataSet("SELECT officer.t_Post " +
                            "  FROM dofficer_dbt officer, dperson_dbt person " +
                            " WHERE officer.t_PartyID  = " + string({OurBank}) + " AND " +
                            "       person.t_Oper = " + string({oper}) + " AND " +
                            "       officer.t_PersonID = person.t_PartyID "
                            );

        if( RS.MoveNext() )
          return RS.Post;
        end;
        return "Ответственный сотрудник";
    END;

    var PostBoss, PostBook;
    if( {Name_Boss} )
        PostBoss = {Name_Boss};
    else
        PostBoss = "Директор банка";
    end;

    if( {Name_Book} )
        PostBook = {Name_Book};
    else
        PostBook = "Главный бухгалтер банка"
    end;
    
    PrintFormatString(NULL,"FooterSpace", "");
    CopyAllSheetInTotalBook( SheetName, false, "FooterSpace", 0, SheetName );
    PrintFormatString(NFooter, 
      "DirectorPost", PostBoss, "DirectorName", {FIO_Boss}, 
      "PostBookPost", PostBook, "PostBookName", {FIO_Book}, 
      "DepartmentName", НачальникОтдела(), 
      "OperPost", ДолжностьОперациониста(), "OperName", Операционист() );

    CopyAllSheetInTotalBook( SheetName, false, "Footer", 0, SheetName );
END;

  PRIVATE MACRO Create()
    var IsFirst = true;

    SetActiveSheet(SheetName);

    PrintFormatString(NULL, "RepDate", m_RepDate);
    CopyAllSheetInTotalBook(SheetName, false, "Header", 0, SheetName);

    GetSQLRequestDeals();

    if (m_StatusProcessMsg)
      CopyAllSheetInTotalBook(SheetName, false, "N1_Headline", 0, SheetName);

      GetDataForSchedAndNotPrepMsg();

      if (SchedAndNotPrepMsg.size == 0)
        PrintFormatString(NULL, "OprStatusSchedAndNotPrepMsg", "Запланированные и не подготовленные сообщения на отчетную дату не найдены." );
        CopyAllSheetInTotalBook(SheetName, false, "OprStatusSchedAndNotPrepMsg", 0, SheetName);
      else
        PrintSchedAndNotPrepMsg();
      end;

      GetDataForPrepAndNotUnloadMsg();

      if (m_UnloadedMsg)
        PrintFormatString(NULL, "N2_HeadTitle", "Подготовленные сообщения" );
      else
        PrintFormatString(NULL, "N2_HeadTitle", "Подготовленные и не выгруженные сообщения");
      end;
      CopyAllSheetInTotalBook(SheetName, false, "N2_HeadLine", 0, SheetName);

      if (PrepAndNotUnloadMsg.size == 0)
        if (m_UnloadedMsg)
          PrintFormatString(NULL, "OprStatusPrepAndNotUnloadMsg", "Подготовленные сообщения на отчетную дату не найдены." );
        else
          PrintFormatString(NULL, "OprStatusPrepAndNotUnloadMsg", "Подготовленные и не выгруженные сообщения на отчетную дату не найдены.");
        end;
        CopyAllSheetInTotalBook(SheetName, false, "OprStatusPrepAndNotUnloadMsg", 0, SheetName);
      else
        PrintPrepAndNotUnloadMsg();
      end;
    end;

    if (m_ContractWthRequest)
      CopyAllSheetInTotalBook(SheetName, false, "N3_Headline", 0, SheetName);

      GetDataForRequestContract();

      if (RequestContract.size == 0)
        PrintFormatString(NULL, "OprStatusRequestContract", "Операции с ожиданием запроса на согласование на отчетную дату не найдены.");
        CopyAllSheetInTotalBook(SheetName, false, "OprStatusRequestContract", 0, SheetName);
      else
        PrintRequestContract();
      end;
    end;
    AddStandardFooter(m_RepDate);
  END;

  m_PrintMode               = Mode;
  m_RepDate                 = RepDate;
  m_StatusProcessMsg        = StatusProcessMsg;
  m_UnloadedMsg             = UnloadedMsg;
  m_ContractWthRequest      = ContractWthRequest;
  SchedAndNotPrepMsg.size   = 0;
  PrepAndNotUnloadMsg.size  = 0;
  RequestContract.size      = 0;

  initDL_CReportTemplate( NULL, "RepStatusMsg.xls");
END;

private MACRO PrintRepStatusMsg(RepDate, StatusProcessMsg, UnloadedMsg, ContractWthRequest)
  IR_RepStatusMsg(DL_OUTREPORT_EXCEL, RepDate, StatusProcessMsg, UnloadedMsg, ContractWthRequest).Run();
  return 0;
END;
exit(1);
