/*
    $Name: ir_CM053.mac
    $Module:   Репозитарий
    $Description:  Анкета договора товарного свопа 
*/

import "ir_CMBase.mac";

class (CMBase) CM053 ( _isRecon, _conveyerID, _ir_op : object)
  InitCMBase();

  m_isRecon     = _isRecon;
  m_conveyerID  = _conveyerID;
  m_ir_op       = _ir_op;
  m_MessageID   = REPOS_CM053;

  macro addCMAll(ir_generalinf     ,
                 TradeRepository   ,
                 Party1            ,
                 Party2            ,
                 UTIGeneratingParty,
                 Sender            ,
                 cm                ,
                 GrDealID          ,
                 TemplNum          ,
                 DealParmForReport )
    addGenInf(ir_generalinf, TemplNum, DealParmForReport);
    addRepAccGrDoc(GrDealID);
    addcmGrDoc(GrDealID);
    addParty(TradeRepository, Party1, Party2, UTIGeneratingParty, Sender);
    addCM(cm);
  end;

  macro insertAll
    var err = 0;
    err = insertGeneralinf;
    if (not err)
      err = insertParty;
    end;
    if (not err)
      err = insertDlgrdocCache;
    end;
    if (not err)
      err = insertCM;
    end;
    return err;
  end;

  macro ЗаполнениеCM (cm : @TrecHandler,
                      DataSet,
                      GenAgr,
                      MessageType,
                      FD,
                      first,
                      field,
                      DealParmForReport)
    var stat=0;
    var FI = TRecHandler( "fininstr.dbt" );
    var Art = TBFile( "article.dbt" );
    var CommAssets = TBFile( "ir_commodityassets.dbt" );
    var CAUnits = TBFile( "ir_commassetsunits.dbt" );
    var Divider = 1; 
    var MeasureOfWeigt;
    if (first)
    var pt = RSBPArty(Dataset.Contractor); 

     if(pt.IsNotResident)
        Divider = 31.10349939;
     end;
      //Общая
      cm.rec.StructureType = IIF((FD.IsBuy() and (GenAgr.Side1 == 1)) or (FD.IsSale() and (GenAgr.Side2 == 1)), 1, 2);
      cm.rec.EffectiveDate = FD.ДатаНачала();
      cm.rec.TerminationDate = max(FD.ДатаПоставки(),FD.ДатаОплаты());
      cm.rec.TerminationDate = max(cm.rec.TerminationDate,FD.nFI_BAOther.rec.PayDate);
      cm.rec.SettlementCurrency = GetFICode(FD.ВалютаЦены(), null, FICK_ISOSTRING);

      //Leg1
      cm.rec.payerPartyRef_Leg1 = IIF(cm.rec.StructureType==1,"Party1","Party2");
      cm.rec.payerPartyRef_Leg1Id = IIF(cm.rec.StructureType==1,IR_PARTY_KIND_PARTY1,IR_PARTY_KIND_PARTY2);
      cm.rec.receiverPartyRef_Leg1 = IIF(cm.rec.StructureType==1,"Party2","Party1");
      cm.rec.receiverPartyRef_Leg1Id = IIF(cm.rec.StructureType==1,IR_PARTY_KIND_PARTY2,IR_PARTY_KIND_PARTY1);
      cm.rec.periodMultiplier_Leg1 = 1;
      cm.rec.period_Leg1 = "T";
      cm.rec.amount_Leg1 = FD.nFI_BaseActiv.rec.Cost;
      cm.rec.amountCurrency_Leg1 = GetFICode(FD.nFI_BaseActiv.rec.PriceFIID, null, FICK_ISOSTRING);
      if(pt.IsNotResident)
         cm.rec.Quantity_Leg1 = Round((FD.nFI_BaseActiv.rec.Cost * Divider / FD.nFI_BaseActiv.rec.Amount),3);
      else
         cm.rec.Quantity_Leg1 = FD.nFI_BaseActiv.rec.Amount ;
      end;
      cm.rec.PaymentDate_Leg1 = FD.nFI_BaseActiv.rec.PayDate;

      //Leg2
      cm.rec.payerPartyRef_Leg2 = IIF(cm.rec.StructureType==2,"Party1","Party2");
      cm.rec.payerPartyRef_Leg2Id = IIF(cm.rec.StructureType==2,IR_PARTY_KIND_PARTY1,IR_PARTY_KIND_PARTY2);
      cm.rec.receiverPartyRef_Leg2 = IIF(cm.rec.StructureType==2,"Party2","Party1");
      cm.rec.receiverPartyRef_Leg2Id = IIF(cm.rec.StructureType==2,IR_PARTY_KIND_PARTY2,IR_PARTY_KIND_PARTY1);
      cm.rec.periodMultiplier_Leg2 = 1;
      cm.rec.period_Leg2 = "T";
      cm.rec.amount_Leg2 = FD.nFI_BAOther.rec.Cost;
      cm.rec.amountCurrency_Leg2 = GetFICode(FD.nFI_BAOther.rec.PriceFIID, null, FICK_ISOSTRING);
      if(pt.IsNotResident)
         cm.rec.Quantity_Leg2 = Round((FD.nFI_BAOther.rec.Cost * Divider / FD.nFI_BAOther.rec.Amount),3);
      else
         cm.rec.Quantity_Leg2 = FD.nFI_BAOther.rec.Amount ;
      end;
      cm.rec.PaymentDate_Leg2 = FD.nFI_BAOther.rec.PayDate;

      if (DataSet.TYPEPRODUCT == 60) //Поставочный
        Art.rec.FIID = FD.nFI_().rec.FIID;
        if (Art.GetEQ())
            if (FD.ВидБазовогоАктива() == FIKIND_METAL)
                cm.rec.instrumentid = "Commodity:Metals:Precious";
            else
                CommAssets.rec.ID = Art.rec.RepozCode;
                if (CommAssets.GetEQ())
                    cm.rec.instrumentid = CommAssets.rec.CommodityAssets;
                else
                    stat = 1;
                    RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, MessageType, "Не задан код для сообщений репозитария у ПИ", "InstrumentId", GetReportAction(DataSet.TemplNum));
                end;
            end;

            CAUnits.rec.ID = Art.rec.RepozMeasureCode;
            if (CAUnits.GetEQ())
                cm.rec.priceunit = CAUnits.rec.CommAssetsUnits;
            else
                stat = 1;
                RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, MessageType, "Не задана единица измерения для сообщений репозитария у ПИ", "PriceUnit", GetReportAction(DataSet.TemplNum));
            end;
             MeasureOfWeigt = cm.rec.priceunit;
             if(pt.IsNotResident)
               MeasureOfWeigt = "ozt"; 
               cm.rec.priceunit = "ozt";
             end;    
        end;
        if(not ПолучитьФинИн( FD.nFI_BaseActiv.rec.FIID, FI))
          cm.rec.commodity = FI.rec.Name;
        end;
        cm.rec.pricecurrency = GetFICode(FD.ВалютаЦены(), null, FICK_ISOSTRING);

        cm.rec.payerPartyReference = IIF(cm.rec.StructureType==2,"Party1","Party2");
        cm.rec.payerPartyReferenceId = IIF(cm.rec.StructureType==2,IR_PARTY_KIND_PARTY1,IR_PARTY_KIND_PARTY2);
        cm.rec.receiverPartyReference = IIF(cm.rec.StructureType==2,"Party2","Party1");
        cm.rec.receiverPartyReferenceId = IIF(cm.rec.StructureType==2,IR_PARTY_KIND_PARTY2,IR_PARTY_KIND_PARTY1);

        cm.rec.deliverydate = FD.ДатаПоставки();
        cm.rec.quantityunit = MeasureOfWeigt;
        cm.rec.quantity = Round(FD.nFI_().rec.Amount / Divider,2);
      end;
    end;
    return 0;
  end;
end;