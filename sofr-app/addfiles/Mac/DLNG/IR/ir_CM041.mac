/*
    $Name: ir_CM041.mac
    $Module:   Репозитарий
    $Description:  Анкета договора репо
*/

import "ir_CMBase.mac";

class (CMBase) CM041 ( _isRecon, _conveyerID, _ir_op : object)
  InitCMBase();

  m_isRecon     = _isRecon;
  m_conveyerID  = _conveyerID;
  m_ir_op       = _ir_op;
  m_MessageID   = REPOS_CM041;

  macro addCMAll(ir_generalinf     ,
                 TradeRepository   ,
                 Party1            ,
                 Party2            ,
                 UTIGeneratingParty,
                 Sender            ,
                 cm                ,
                 Instruments       ,
                 GrDealID          ,
                 TemplNum          ,
                 DealParmForReport )
    addGenInf(ir_generalinf, TemplNum, DealParmForReport);
    addRepAccGrDoc(GrDealID);
    addcmGrDoc(GrDealID);
    addParty(TradeRepository, Party1, Party2, UTIGeneratingParty, Sender);
    addCM(cm);
    addInstrument(Instruments);
  end;

  macro insertAll
    var err = 0;
    err = insertGeneralinf;
    if (not err)
      err = insertParty;
    end;
    if (not err)
      err = insertDlgrdocCache;
    end;
    if (not err)
      err = insertCM;
    end;
    if (not err)
      err = insertInstrument;
    end;
    return err;
  end;

  macro ЗаполнениеCM (cm : @TrecHandler,
                      DataSet,
                      GenAgr,
                      FD,
                      first,
                      field,
                      Instruments : @TrecHandler,
                      DealParmForReport)
    var erFlag:bool = false;
    var bondFaceValue;
    var stat = 0;
    if (first)
        //Instruments.clear();
        
        cm.rec.ratetype = "FX";
        cm.rec.initialrate  = ЧислоCЗаданнойТочностью(FD.dl_leg.rec.IncomeRate / 100, 7);
        cm.rec.daycountcalcID = FD.dl_leg.rec.Basis; //порядки в NameAlg совпадют
        cm.rec.daycountcalc = DL_GetNameAlg( 8118 /*ALG_GENINF_DAYCOUNTCALC*/, cm.rec.daycountcalcID ); 
        
        if( FD.tick.rec.OpenDate == SET_CHAR )
            cm.rec.durationtypeid = 3; /*Open*/
        else
            var DateEnd: Date = DataSet.Date_End;
            var DateStart: Date = DataSet.Date_Start;
            if( DateEnd > GetDateAfterWorkDays(DateStart, 1) )
                cm.rec.durationtypeid = 2; /*Term*/
            else
                cm.rec.durationtypeid = 1; /*Overnight*/
            end;
        end;
        
        cm.rec.durationtype  = DL_GetNameAlg(ALG_GENINF_DURATIONTYPE, cm.rec.durationtypeid);
        cm.rec.haircutvaluer = FD.tick.rec.Discont;
        
        if( (IsBUY(FD.Group) and (GenAgr.Side1 == 1)) or
            (IsSALE(FD.Group) and (GenAgr.Side1 == 2))
            )
            cm.rec.buyerpart1id  = IR_PARTY_KIND_PARTY1;
            cm.rec.buyerpart1    = "Party1";
            cm.rec.sellerpart1id = IR_PARTY_KIND_PARTY2;
            cm.rec.sellerpart1   = "Party2";
        else
            cm.rec.buyerpart1id  = IR_PARTY_KIND_PARTY2;
            cm.rec.buyerpart1    = "Party2";
            cm.rec.sellerpart1id = IR_PARTY_KIND_PARTY1;
            cm.rec.sellerpart1   = "Party1";
        end;
        
        cm.rec.buyerpart2id  = cm.rec.sellerpart1id;
        cm.rec.buyerpart2    = cm.rec.sellerpart1;
        cm.rec.sellerpart2id = cm.rec.buyerpart1id;
        cm.rec.sellerpart2   = cm.rec.buyerpart1;
        
        cm.rec.Part1SettlDate= GetDealPayPlanDate( FD.dl_leg.rec );
        cm.rec.DeliveryDatePart1 = GetDealSetAvPlanDate( FD.dl_leg.rec );
        
        if( FD.dl_leg.rec.Formula == SP_TYPE_CALC_DVP )
            cm.rec.settlmethodpart2id = 1; // DeliveryVersusPayment
            cm.rec.settlmethodpart2 = cm.rec.deliverymethodpart1 = DL_GetNameAlg(ALG_GENINF_SETTLMETHOD, 1);
        else
            cm.rec.settlmethodpart2id = 2; // FreeOfPayment
            cm.rec.settlmethodpart2 = cm.rec.deliverymethodpart1 = DL_GetNameAlg(ALG_GENINF_SETTLMETHOD, 2);
        end;
        
        if( FD.ExistAvance )
            cm.rec.part1amount = FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.Amount + FD.GetRQ(DLRQ_TYPE_AVANCE).rec.Amount;
        else
            cm.rec.part1amount = FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.Amount;
        end;
        cm.rec.part1currid   = cm.rec.currencypart2id = FD.GetPayFIID();
        cm.rec.part1curr = cm.rec.currencypart2 = GetFICode(FD.GetPayFIID(), null, FICK_ISOSTRING);
        cm.rec.part2settldate = FD.GetRQ(DLRQ_TYPE_PAYMENT, 2).rec.PlanDate;
        cm.rec.amountpart2   = FD.GetRQ(DLRQ_TYPE_PAYMENT, 2).rec.Amount;
        if( FD.dl_leg.rec.IncomeRate > 0.0 )
            cm.rec.amountpart2 = cm.rec.amountpart2 + FD.GetRQ(DLRQ_TYPE_INCREPO, 2).rec.Amount;
        elif( FD.dl_leg.rec.IncomeRate < 0.0 )
            cm.rec.amountpart2 = cm.rec.amountpart2 - FD.GetRQ(DLRQ_TYPE_INCREPO, 2).rec.Amount;
        end;
        cm.rec.deliverydatepart2 = FD.GetRQ(DLRQ_TYPE_DELIVERY, 2).rec.PlanDate;
        
        if( FI_IsBond(FD.fininstr) == true )
            bondFaceValue = GetFaceValue(FD.fininstr.rec.FIID, FD.tick.rec.DealDate);
            cm.rec.signbonds = SET_CHAR;
            
            cm.rec.collatcurrid = FD.fininstr.rec.FaceValueFI;
            cm.rec.collatcurr   = GetFICode(cm.rec.collatcurrid, null, FICK_ISOSTRING);
            if(not bondFaceValue)
                RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, m_MessageType, "Не задано значение номинала для индексируемой облигации на дату заключения сделки", "CollatNomAmount", GetReportAction(DataSet.TemplNum), 400 + 31);
                stat=1;
            else
                cm.rec.collatnomamount = bondFaceValue * FD.dl_leg.rec.Principal;
            cm.rec.netprice = (GetRateOnDate(FD.tick.rec.DealDate, FD.fininstr.rec.FIID, FD.fininstr.rec.FaceValueFI, false, ВИД_КУРСА_ЧИСТАЯ_ЦЕНА_ОБЛИГАЦИИ(), erFlag) / bondFaceValue) * 100;
            end;
            if( erFlag == true )
                RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, m_MessageType, "Не задано значение курса вида \"" + IR_GetRateTypeName(ВИД_КУРСА_ЧИСТАЯ_ЦЕНА_ОБЛИГАЦИИ()) + "\" на дату " + date_as_string(1, FD.tick.rec.DealDate) + " для определения чистой цены облигации", "CollatNomAmount", GetReportAction(DataSet.TemplNum), 400 + 33);
                stat=1;
            end;
            cm.rec.accrual = (НКД(FD.fininstr.rec.FIID, 1, FD.tick.rec.DealDate) / FD.fininstr.rec.FaceValue) * 100;
            cm.rec.drtprice = cm.rec.netprice + cm.rec.accrual;
        elif( FI_IsShare(FD.fininstr) OR FI_IsInvestmentShare(FD.fininstr) )
            cm.rec.signbond = SET_CHAR;
            cm.rec.numberofunits = FD.dl_leg.rec.Principal;
            if ( FD.dl_leg.rec.RelativePrice == SET_CHAR )
                var CcyRate = GetRateOnDateCrossDbl(FD.tick.rec.DealDate, FD.fininstr.rec.FaceValueFI, FD.dl_leg.rec.CFI, false);
                cm.rec.unitpriceamount = (FD.fininstr.rec.FaceValue * FD.dl_leg.rec.Price / 100) * CcyRate;
            else
                cm.rec.unitpriceamount = FD.dl_leg.rec.Price;
            end;
            cm.rec.unitpricecurrid = FD.dl_leg.rec.CFI;
            cm.rec.unitpricecurr = GetFICode(cm.rec.unitpricecurrid, null, FICK_ISOSTRING);
        end;
        
        Instruments.rec.FIID   = FD.fininstr.rec.FIID;
        Instruments.rec.InstrumentCode = GetAvoirissCode(FD.fininstr, m_ir_op.rec.Date);
        Instruments.rec.Instrumentname = FD.fininstr.rec.Name;
    end;
    if (field=="CollatNomAmount")
        bondFaceValue = GetFaceValue(FD.fininstr.rec.FIID, FD.tick.rec.DealDate);
        if(not bondFaceValue)
            RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, m_MessageType, "Не задано значение номинала для индексируемой облигации на дату заключения сделки", "CollatNomAmount", GetReportAction(DataSet.TemplNum), 400 + 33);
            stat=1;
        else
            cm.rec.netprice = (GetRateOnDate(FD.tick.rec.DealDate, FD.fininstr.rec.FIID, FD.fininstr.rec.FaceValueFI, false, ВИД_КУРСА_ЧИСТАЯ_ЦЕНА_ОБЛИГАЦИИ(), erFlag) / bondFaceValue) * 100;
        end;
        if( erFlag == true )
            RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, m_MessageType, "Не задано значение курса вида \"" + IR_GetRateTypeName(ВИД_КУРСА_ЧИСТАЯ_ЦЕНА_ОБЛИГАЦИИ()) + "\" на дату " + date_as_string(1, FD.tick.rec.DealDate) + " для определения чистой цены облигации", "NetPrice", GetReportAction(DataSet.TemplNum), 400 + 33);
        end;
    end;
    return stat;
    
  end;
end;