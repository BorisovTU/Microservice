/*
$Name:        repaccsrvopscn.mac
$Module:      Репозитарий
$Description: Массовое выполнение действий над операциями репозитарного учета
*/
IMPORT IRInter, DealsInter;   
 
PRIVATE VAR Oper = TRecHandler("ir_op.dbt");

PRIVATE CONST ERROR_EXEC = 0,
              PRINT_HEADER = 1,
              PRINT_FOOTER = 2;

PRIVATE MACRO PrintHead( Head:STRING )
[
                   #
┌──────────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────┐
│            Код           │  №   │                      Сообщение                                                                    │
│         операции         │ошибки│                                                                                                   │
├──────────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────┤](Head);
END;


PRIVATE MACRO PrintFooter()
[└──────────────────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────┘
];

END;

PRIVATE MACRO PrintLine( ErrStatus:INTEGER, ErrMessage:STRING )
[│##########################│######│###################################################################################################│]
( Oper.rec.CodeOp:w, ErrStatus, ErrMessage:w );
END;

PRIVATE VAR OperByID = TBFile("ir_op.dbt", "R", 0);
PRIVATE MACRO GetCurrentStatus():INTEGER
  OperByID.Clear();
  OperByID.rec.ID = Oper.rec.ID;
  if( OperByID.GetEQ() )
     return OperByID.rec.Status;
  end;
  return -1;
END; 

PRIVATE MACRO ExecuteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  VAR Status;

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом создании операций репозитарного учета." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        Status = GetCurrentStatus();
        if( Status == DL_COMM_CLOSED )
           ErrMessage = "Операция закрыта.";
        elif( Status == DL_COMM_CLOSED_ERROR ) 
           ErrMessage = "Операция закрыта с ошибкой.";
        elif( Status == DL_COMM_PREPARING ) 
           ErrMessage = "Операция не создана.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO DeleteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  VAR Status;

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом удалении операций репозитарного учета." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        Status = GetCurrentStatus();
        if( (Status == DL_COMM_READIED) OR (Status == DL_COMM_CLOSED) OR (Status == DL_COMM_CLOSED_ERROR) )
           ErrMessage = "Операция не удалена.";
        elif( Status == DL_COMM_PREPARING ) 
           ErrMessage = "Операция перемещена в отложенные.";
        else
           ErrMessage = "Операция удалена успешно.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO MoveToPrepOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  VAR Status;

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом перемещении в отложенные операций репозитарного учета." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        Status = GetCurrentStatus();
        if( Status != DL_COMM_PREPARING ) 
           ErrMessage = "Операция не перемещена в отложенные.";
        else
           ErrMessage = "Операция перемещена в отложенные.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

/* Action = 1 - начало/продолжение операции по договору (F2 из скроллинга )
   Action = 2 - перевод в отложенные (Alt+F8 из скроллинга )
   Action = 3 - удаление (F8 из скроллинга )
*/
MACRO Печать_ОтчетСканирования( NumExec:INTEGER, FDoc:VARIANT, ErrStatus:INTEGER, ErrMessage:STRING, Action:INTEGER )

  Oper.SetRecordAddr( FDoc );

  if( Action == 1 )
     ExecuteOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 2 )
     MoveToPrepOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 3 )
     DeleteOperation( NumExec, ErrStatus, ErrMessage );
  end;

END;
