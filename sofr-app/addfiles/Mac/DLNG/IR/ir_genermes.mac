/*
$Name:             ir_genermes.mac
$Module:           Репозитарий
$Description:      Выполнение сервисной операции генерации сообщений
*/
import DealsInter, FIInter, OprInter, IRInter;
import "irstartgenermes.mac", "dlquery.mac", "cb_sql.mac", "dlcnst.inc", "dlntfd.mac","bnk_common.mac";

macro ОтобратьОбъектыДляГС( _taskID, _execID, _conveyerID, _packetSize, _Params )
  var ir_op = TBFile("ir_op.dbt");
  var cnt = 0;
  var query, DataSet;

  if( _Params.ID > 0 )

    ir_op.rec.ID = _Params.ID;
    if( ir_op.GetEQ() )

      query = " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
            + " SELECT "+_taskID+","+_execID+","+_conveyerID+",decode("+_packetSize+", 0, 1, (TRUNC((RowNum + "+_packetSize+" - 1)/"+_packetSize+"))), q.t_ObjectType, q.t_ObjectID "
            + "   FROM (" + GetGenerMesObjectQuery(ir_op) + ") q ";
      query = query + " ORDER BY q.t_ObjectID ";

      SQL_Execute(query, "Отбор объектов", false);

      if( _packetSize == 0 )
        cnt = 1;
      else
        query =   " select NVL(max(t_PackID), 0) as cnt "
                + "   from dcnvdoc_dbt "
                + "  where t_ExecID = " + _execID;

        DataSet = TRsbDataSet(query);
        if( DataSet.moveNext() )
          cnt = int(round(int(DataSet.cnt), 0));
        end;
      end;
    end;
  end;

  return cnt;
end;

private var RepErrArr = TArray(); /*массив ошибок*/
private var CurrentOper = {Oper};

macro GenerMes_msgbox( StrErr )
  RepErrArr[RepErrArr.size] = CIR_GenerMesRepErr("", "", StrErr);
end;

private macro SaveErrForReport_XML( ID:integer, DocKind:integer )

  var RepXML = "";

  if( RepErrArr.Size > 0 )
     RepXML = IR_GENERMES_ERROR_LOG_DATA_XML(DocKind, ID, RepErrArr, LOG_TYPE_ERROR).GetDataXML();
  end;

  if( RepXML != "" )
     var Header = DL_LOG_HEADER_XML(DocKind).GetHeaderXML();

     DL_InsertHeaderLog(ID, DocKind, CurrentOper, Header);
     DL_InsertLogXMLData(ID, DocKind, LOG_TYPE_ERROR, CurrentOper, RepXML);
  end;

end;

private macro _ExecuteGenerMes( _taskID, _execID, _conveyerID, _operationID, _packetID, ir_op )
  var err = 0;
  var query, cmd, DataSet;
  var queryDeal, cmdDeal, DataSetDeal;
  var querySRS, cmdSRS, DataSetSRS;
  var cnt = 0, i = 0;
  var transactionStarted:bool = false;
  var m_RepDataArr = TArray();
  var prevGrDealID = 0;
  var prevSrsID = 0;


  m_RepDataArr.size = 0;

//  cmd = RSDCommand();
  if( (_conveyerID) and (_packetID) )
    query =  "with cnvdoc as (select * from dcnvdoc_dbt where t_ExecID = ? and t_ConvTypeID = ? and t_PackID = ?)";
    cmd.AddParam("execID ", RSDBP_IN,_execID);
    cmd.AddParam("conveyerID ", RSDBP_IN,_conveyerID);
    cmd.AddParam("packetID ", RSDBP_IN,_packetID);
  else
    query =  "with cnvdoc as ("+GetGenerMesObjectQuery(ir_op)+")";
  end;

  query = query + " select generalinf.t_InternalMessageID, grdeal.t_ID as GrID"
/*
                + "        td.t_DocKind, "
                + "        td.t_Code AS DealCode, "
                + "        generalinf.* "
*/
                + "   from cnvdoc, dir_generalinf_dbt generalinf, ddlgrdoc_dbt irgrdoc, ddlgrdeal_dbt grdeal "//, dv_irdeal td "
                + "  where cnvdoc.t_ObjectType = 0 "
                + "    and generalinf.t_InternalMessageID = cnvdoc.t_ObjectID "
                + "    and irgrdoc.t_DocKind = " + REPOS_GENERALINF
                + "    and irgrdoc.t_DocID   = generalinf.t_InternalMessageID "
                + "    and grdeal.t_ID       = irgrdoc.t_GrDealID "
                //+ "    and td.t_DocKind      = grdeal.t_DocKind "
                //+ "    and td.t_DocID        = grdeal.t_DocID "
                + " group by generalinf.t_InternalMessageID, grdeal.t_ID "
                + " order by generalinf.t_InternalMessageID ";

  if( (_conveyerID == 0) and (_packetID == 0) )
//    cnt = cmd.GetCount(query);
    cnt = SQL_GetNRecs(query);
    if(cnt)
      InitProgress( cnt, "Идет выполнение операции...", "Генерация сообщений" );
    end;
  end;


  cmd = RSDCommand(query);
  if( (_conveyerID) and (_packetID) )
    cmd.AddParam("execID ", RSDBP_IN,_execID);
    cmd.AddParam("conveyerID ", RSDBP_IN,_conveyerID);
    cmd.AddParam("packetID ", RSDBP_IN,_packetID);
  end;

  cmd.Execute();

  DataSet = TRsbDataSet(cmd);

//  DataSet = cmd.Execute(query);
  while( DataSet.moveNext() )

    /*Начать транзакцию*/
    RslDefCon.BeginTrans();
    transactionStarted = true;
    var exec;
    var params:TArray = TArray();
    params[0] = SQLParam( "p_DraftID"    , DataSet.InternalMessageID  ); // Уникальный идентификатор поручения в АБС
    params[1] = SQLParam( "p_Oper"       , CurrentOper               ); // Код операциониста, инициирующего генерацию
    params[2] = SQLParam( "p_Department" , {OperDprt}                 ); // Код департамента
    params[3] = SQLParam( "p_Text"       , V_STRING,  RSDBP_OUT, 2000); // Текст результата генерации сообщения

    // Возвращаемые значения:
    // 0 - Инструкция сформирована
    // 1 - По поручению уже есть инструкция
    // 2 - Логическая ошибка при формировании, расшифровку см. в p_Text
    if(prevSrsID != DataSet.t_InternalMessageID )
      var stat:integer = 0;
      var p_Text = "";
    
      // Проверка признака репортинг на сделках собщения
      var reportingQ = "SELECT COUNT (*) NoReporting "
                     + "  FROM dir_generalinf_dbt geninf, "
                     + "       ddl_repozdeal_dbt rdeal, "
                     + "       ddlgrdeal_dbt grdeal, "
                     + "       ddlgrdoc_dbt grdoc "
                     + " WHERE     geninf.t_InternalMessageID = ? "
                     + "       AND grdoc.t_DocID = geninf.t_InternalMessageID "
                     + "       AND grdoc.t_DocKind = " + REPOS_GENERALINF
                     + "       AND grdeal.t_ID = grdoc.t_GrDealID "
                     + "       AND rdeal.t_DocID = grdeal.t_DocID "
                     + "       AND rdeal.t_DocKind = grdeal.t_DocKind "
                     + "       AND rdeal.t_Reporting = CHR (0)";

      var reportingCmd = RSDCommand(reportingQ);
      reportingCmd.AddParam("InternalMessageID", RSDBP_IN, DataSet.InternalMessageID);
      reportingCmd.Execute();
      var reportingRes = TRsbDataSet(reportingCmd);
      
      if ( (reportingRes.moveNext()) and (reportingRes.NoReporting > 0) )
        stat = -1;
      else
        exec = RSDCommand( " UPDATE dir_generalinf_dbt "
                         + "  SET t_CreationDate = TRUNC(SYSDATE), "
                         + "  t_CreationTime = TO_DATE('01.01.0001 ' || TO_CHAR (SYSDATE, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS') "
                         + "  WHERE t_InternalMessageID = ? ");
        exec.addParam("", RSDBP_IN, DataSet.InternalMessageID);
        exec.execute();
        stat = execStoredFunc( "RSP_REPOSITORY.GenMsg4Repo", V_INTEGER, params );
        if(stat != 0)
          RslDefCon.RollbackTrans();    // при ошибке откатим текущую транзакцию
          RslDefCon.BeginTrans();       // и начнем новую, чтобы зафиксировать ошибку
        end;
      end;
    
      //var stat:integer = execStoredFunc( "RSP_REPOSITORY.GenMsg4Repo", V_INTEGER, params );

      if( stat != 0)
        p_Text = params.Value(3).value;
      end;
    
      var RStatus:integer = IIF(stat == 0, 3/*Сгенерировано*/, 4/*Ошибка генерации*/);
      var RStatusName = Bnk_GetNameAlg(ALG_GENINF_RSTATUS, RStatus);
      exec = RSDCommand( " UPDATE dir_generalinf_dbt "
                       + "    SET t_RStatus = ? , "
                       + "        t_RStatusName = ? "
//                       + "        t_CreationDate = " + GetSQLDate(date({DprtCurDate})) + ", "
//                       + "        t_CreationTime = TO_DATE('01.01.0001 ' || TO_CHAR (SYSDATE, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS') "
                       + "  WHERE t_InternalMessageID = ? ");
      exec.addParam("", RSDBP_IN, RStatus);
      exec.addParam("", RSDBP_IN, RStatusName);
      exec.addParam("", RSDBP_IN, DataSet.InternalMessageID);
      exec.execute();
    
      querySRS = " select inf.t_MessageID as MessageID, inf.t_MessageType as MessageType  from dir_generalinf_dbt inf  where inf.t_InternalMessageID = ? "; 
      cmdSRS = RSDCommand(querySRS);
      cmdSRS.AddParam("DocID ",       RSDBP_IN,  DataSet.InternalMessageID);
    
      cmdSRS.Execute();
    
      DataSetSRS = TRsbDataSet(cmdSRS);
    
      if( DataSetSRS.moveNext() )
    
        /* добавить запись в лог о генерации СРС */
        m_RepDataArr[m_RepDataArr.size] = CIR_GenerMesRepParm(DataSetSRS.MessageID,
                                                              DataSetSRS.MessageType,
                                                              DL_GetNameAlg(ALG_GENINF_RSTATUS, RStatus) + IIF(stat == 0,"",": "+p_Text) );
      end;
      prevSrsID = DataSet.t_InternalMessageID;
    end;
    
    if( (not stat) and ( RStatus == 3) )
      exec = RSDCommand( " INSERT INTO ddlgrdoc_dbt (t_ID, t_GRDEALID, T_DOCKIND, T_DOCID, T_SERVDOCKIND, T_SERVDOCID, T_GRPID, T_SOURCETYPE) "
                       + " VALUES (0,?,?,?,?,?,0,0)   " );
      exec.addParam("", RSDBP_IN, DataSet.GrID);
      exec.addParam("", RSDBP_IN, REPOS_GENERALINF);
      exec.addParam("", RSDBP_IN, DataSet.InternalMessageID);
      exec.addParam("", RSDBP_IN, REPOS_MESSAGEGENERATION);
      exec.addParam("", RSDBP_IN, ir_op.rec.Id);
      exec.execute();
    end;
    
    RslDefCon.CommitTrans(); /*Завершить транзакцию*/
    transactionStarted = false;

    if( cnt )
      UseProgress( i = i +1 );
    end;
  end;

  DL_SaveDataForReport_XML(ir_op.rec.ID, ir_op.rec.DocKind, m_RepDataArr, LOG_TYPE_DATA, NULL, NULL, CurrentOper);

  if(cnt)
    RemProgress();
  end;

  return err;

OnError(er)
  if( transactionStarted )
     RslDefCon.RollbackTrans();
  end;

  var j = 0, env = cmd.Connection().Environment();

  var Error = er.message;
  while( j < env.ErrorCount )
    Error = Error + "|" + env.error(j).descr;
    j = j + 1;
  end;

  RepErrArr[RepErrArr.size] = CIR_GenerMesRepErr("", "", er.module+ " "+er.line+" "+Error);
  SaveErrForReport_XML(ir_op.rec.ID, ir_op.rec.DocKind);
end;

macro ExecuteGenerMes( _taskID, _execID, _conveyerID, _operationID, _packetID, _Params )
  var ir_op = TBFile("ir_op.dbt");
  var err = 0;
  RepErrArr.size = 0;

  if (_Params.Oper)
    CurrentOper = _Params.Oper;
  end;

  if( _Params.ID > 0 )
    ReplaceMacro("msgbox", "GenerMes_msgbox");

    ir_op.rec.ID = _Params.ID;
    if( ir_op.GetEQ() )

      if( (_conveyerID) and (_packetID) )
        err = _ExecuteGenerMes(_taskID, _execID, _conveyerID, _operationID, _packetID, ir_op);
      else
        err = _ExecuteGenerMes(0, 0, 0, 0, 0, ir_op);
      end;
    end;

    ReplaceMacro("msgbox", NULL);

    SaveErrForReport_XML(ir_op.rec.ID, ir_op.rec.DocKind);
  end;

  if( (_conveyerID) and (_packetID) )
    /*не зависимо от того, делали что-то или нет - ставим исходящий статус обрабатываемой пачке*/
    var exec = RSDCommand(  " UPDATE dcnvpack_dbt "
                          + " SET t_State = t_State + 1 "
                          + " WHERE t_ExecID = ? "
                          + "   AND t_ConvTypeID = ? "
                          + "   AND t_PackID = ? ");

    exec.addParam( "", RSDBP_IN, _execID );
    exec.addParam( "", RSDBP_IN, _conveyerID );
    exec.addParam( "", RSDBP_IN, _packetID );
    exec.execute();
  end;

  return err;

OnError(er)
  ReplaceMacro( "msgbox", NULL );
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  RepErrArr[RepErrArr.size] = CIR_GenerMesRepErr("", "", er.module+ " "+er.line+" "+er.message);
  SaveErrForReport_XML( ir_op.rec.ID, ir_op.rec.DocKind );
  return 1;

end;
