/*
$Name:             ir_partyimprep.mac
$Module:           Репозитарий
$Description:      Формирование отчета о результатах процедуры импорта участников репозитария
*/
import ir_ptimpdef;

private var PARTYIMPREPO_RSD;
private var PARTYIMPREC_RSD;
private var numrec;

private macro GetCountAllRec: integer
    var cmd = RsdCommand("SELECT COUNT(1) as cnt FROM DIR_PARTYIMPREC_DBT WHERE t_imprepid = ?");
    cmd.addParam("", RSDBP_IN, PARTYIMPREPO_RSD.rec.Id);
    cmd.execute();
    var rsd = TRsbDataSet(cmd);
    rsd.NullConversion = true;
    if( rsd.MoveNext() )
        return rsd.rec.cnt;
    end;        
    return 0;
end;

private macro GetCountParty: integer
    var cmd = RsdCommand("SELECT COUNT(DISTINCT(t_partyid)) as cnt FROM DIR_PARTYIMPREC_DBT WHERE t_imprepid = ? AND t_status <> 0");
    cmd.addParam("", RSDBP_IN, PARTYIMPREPO_RSD.rec.Id);
    cmd.execute();
    var rsd = TRsbDataSet(cmd);
    rsd.NullConversion = true;
    if( rsd.MoveNext() )
        return rsd.rec.cnt;
    end;        
    return 0;
end;

private macro GetCountPartyNotUpdate: integer
    var cmd = RsdCommand("SELECT COUNT(DISTINCT(t_partyid)) as cnt FROM DIR_PARTYIMPREC_DBT WHERE t_imprepid = ? AND t_PartyNotUpdate <> chr(0)");
    cmd.addParam("", RSDBP_IN, PARTYIMPREPO_RSD.rec.Id);
    cmd.execute();
    var rsd = TRsbDataSet(cmd);
    rsd.NullConversion = true;
    if( rsd.MoveNext() )
        return rsd.rec.cnt;
    end;        
    return 0;
end;

private macro GetOperName: string
    var cmd = RsdCommand("SELECT t_name FROM dperson_dbt WHERE t_oper = ?");
    cmd.addParam("", RSDBP_IN, PARTYIMPREPO_RSD.rec.Oper);
    cmd.execute();
    var rsd = TRsbDataSet(cmd);
    rsd.NullConversion = true;
    if( rsd.MoveNext() )
        return rsd.rec.Name;
    end;        
    return "";
end;

private macro GetClientCode( partyid ): string
    var cmdstr =
        "SELECT t.t_code FROM dobjcode_dbt t"
        " WHERE     t.t_objecttype = 3" // OBJTYPE_PARTY
              " AND t.t_codekind = ?"
              " AND t.t_objectid = ?"
              " AND t.t_state = 0"
              " AND t.t_bankdate ="
                     " (SELECT MAX (cd.t_bankdate) FROM dobjcode_dbt cd"
                       " WHERE     cd.t_objecttype = t.t_objecttype"
                             " AND cd.t_codekind = t.t_codekind"
                             " AND cd.t_objectid = t.t_objectid"
                             " AND cd.t_state = 0)";
    var cmd =  RSDCommand(cmdstr);
    cmd.addParam("", RSDBP_IN, 1);
    cmd.addParam("", RSDBP_IN, partyid);
    cmd.execute();
    var rsd = TRsbDataSet(cmd);
    if( rsd.MoveNext() )
        return rsd.rec.Code;
    end;
    return "";
end; /*GetClientCode*/

private macro PrintHeader
    var rezim;
    if( PARTYIMPREPO_RSD.rec.LoadSign == "X" )
        rezim = "Загрузка Справочника участников репозитария";
    else
        rezim = "Сверка Справочника участников репозитария";
    end;
  [ ];
  [ Протокол выполнения процедуры импорта справочника участников репозитария ];
  [────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────];
  [ Код операции       #]( PARTYIMPREPO_RSD.rec.Code );
  [ Дата:   ##########           Время:  ########]( String(PARTYIMPREPO_RSD.rec.Date), String(PARTYIMPREPO_RSD.rec.Time) );
  [ Пользователь:   ##### #]( String(PARTYIMPREPO_RSD.rec.Oper):r, GetOperName );
  [ ];
  [ Имя импортированного файла: #]( PARTYIMPREPO_RSD.rec.FileName );
  [ Версия Справочника участников от ########## #####]( String(PARTYIMPREPO_RSD.rec.FileDate), String(PARTYIMPREPO_RSD.rec.FileTime) );
  [ Режим: #]( rezim );
  [ Всего записей в файле:             #]( GetCountAllRec:l );
  [ Количество участников Репозитария: #]( GetCountParty:l );
  [ Субъекты, у которых установлен флаг "Не обновлять при импорте": #]( GetCountPartyNotUpdate );
  [ ];
  [                                               Результаты обработки];
end; /*PrintHeader*/

private macro PrintHead1
  [ ];
  [    1.   Найденные расхождения];
  [┌─────┬─────┬─────────────────┬──────────┬────────────────┬────────────────┬──────────────┬─────────────┬──────────────────────────┐];
  [│  №  │  №  │  Полное наиме-  │ Код субъ-│  Наименование  │ Значение в фай-│  Значение в  │  Результат  │       Примечание         │];
  [│ п/п │стро-│  нование участ- │  екта в  │  параметра, в  │ ле Справочника │ в справочни- │  обработки  │                          │];
  [│     │ки в │      ника       │ справоч- │ котором найде- │ участников Ре- │ ке субъектов │             │                          │];
  [│     │файле│                 │   нике   │ но расхождение │    позитария   │              │             │                          │];
  [│     │     │                 │ субъектов│                │                │              │             │                          │];
end;

private macro PrintLine1( RecId, clientCode, paramName, newParamValue, paramValue, result, note )
  [├─────┼─────┼─────────────────┼──────────┼────────────────┼────────────────┼──────────────┼─────────────┼──────────────────────────┤];
  [│#####│#####│#################│##########│################│################│##############│#############│##########################│]
  (numrec,
   RecId,
   PARTYIMPREC_RSD.rec.FullName:w,
   clientCode:w,
   paramName:w,
   newParamValue:w,
   paramValue:w,
   result:w,
   note:w);
end;

private macro PrintFloor1
  [└─────┴─────┴─────────────────┴──────────┴────────────────┴────────────────┴──────────────┴─────────────┴──────────────────────────┘];
end;

private macro PrintHead2
  [ ];
  [    2.   Параметры, отсутствующие в справочнике субъектов];
  [┌─────┬─────┬─────────────────┬─────────────┬────────────────┬────────────────┬─────────────┬──────────────────────────┐];
  [│  №  │  №  │  Полное наиме-  │ Код субъекта│  Наименование  │ Значение в фай-│  Результат  │        Примечание        │];
  [│ п/п │стро-│  нование участ- │ в справочни-│ отсутствующего │ ле Справочника │  обработки  │                          │];
  [│     │ки в │      ника       │ ке субъектов│    параметра   │ участников Ре- │             │                          │];
  [│     │файле│                 │             │                │    позитария   │             │                          │];
end;

private macro PrintLine2( RecId, clientCode, paramName, newParamValue, result, note )
  [├─────┼─────┼─────────────────┼─────────────┼────────────────┼────────────────┼─────────────┼──────────────────────────┤];
  [│#####│#####│#################│#############│################│################│#############│##########################│]
  (numrec,
   RecId,
   PARTYIMPREC_RSD.rec.FullName:w,
   clientCode:w,
   paramName:w,
   newParamValue:w,
   result:w,
   note:w);
end;

private macro PrintFloor2
  [└─────┴─────┴─────────────────┴─────────────┴────────────────┴────────────────┴─────────────┴──────────────────────────┘];
end;

private macro PrintHead3
  [ ];
  [    3.   Участники, отсутствующие в Справочнике субъектов];
  [┌─────┬─────┬──────────────────────┬──────────────────┬─────────────────────┬──────────────────────────┐];
  [│  №  │  №  │  Полное наименование │ Репозитарный код │ Результат обработки │        Примечание        │];
  [│ п/п │стро-│       участника      │     участника    │                     │                          │];
  [│     │ки в │                      │                  │                     │                          │];
  [│     │файле│                      │                  │                     │                          │];
end;

private macro PrintLine3
    var note = PARTYIMPREC_RSD.rec.Note;
    var result = "Субъект не добавлен в справочник";
    if( PARTYIMPREPO_RSD.rec.Status == PROCSTATUS_Load )
        if( PARTYIMPREC_RSD.rec.Status == RECSTATUS_Inserted )
            result = "Субъект добавлен в справочник";
        elif( PARTYIMPREPO_RSD.rec.AddSign != "X" )
            note = "Процедура выполнялась в режиме <без добавления новых субъектов>";
        elif( (PARTYIMPREC_RSD.rec.Status == RECSTATUS_InsError) and (note == "") )
            note = "Ошибка загрузки параметров субъекта";
        end;
    elif( PARTYIMPREPO_RSD.rec.LoadSign != "X" )
        note = "Процедура выполнялась в режиме <без загрузки справочника участников>";
    end;

  [├─────┼─────┼──────────────────────┼──────────────────┼─────────────────────┼──────────────────────────┤];
  [│#####│#####│######################│##################│#####################│##########################│]
  (numrec,
   PARTYIMPREC_RSD.rec.RecId,
   PARTYIMPREC_RSD.rec.FullName:w,
   PARTYIMPREC_RSD.rec.RICode:w,
   result:w,
   note:w);
end;

private macro PrintFloor3
  [└─────┴─────┴──────────────────────┴──────────────────┴─────────────────────┴──────────────────────────┘];
  [ ];  
end;

private macro GetUpdatePartyResult( status )
    var result = "";
    if( (PARTYIMPREC_RSD.rec.Status == RECSTATUS_Updated) or (PARTYIMPREC_RSD.rec.Status == RECSTATUS_RepoUpdated) )
        if( status == PARAMSTATUS_NotEqual )
            result = "Параметр изменен";
        else
            result = "Параметр добавлен";
        end;
    else
        if( status == PARAMSTATUS_NotEqual )
            result = "Параметр не изменен";
        else
            result = "Параметр не добавлен";
        end;
    end;
    return result;
end; /* GetUpdatePartyResult */

private macro GetUpdatePartyNote( status )
    var note = PARTYIMPREC_RSD.rec.Note;
    if( PARTYIMPREPO_RSD.rec.Status == PROCSTATUS_Load )
        if( ((PARTYIMPREC_RSD.rec.Status == RECSTATUS_UpdError) or (PARTYIMPREC_RSD.rec.Status == RECSTATUS_RUpdError)) and (note == "") )
            note = "Ошибка загрузки параметров субъекта";
        elif( (PARTYIMPREC_RSD.rec.Status != RECSTATUS_Updated) and (PARTYIMPREC_RSD.rec.Status != RECSTATUS_RepoUpdated) )
            if( status == PARAMSTATUS_NotEqual )
                if( PARTYIMPREPO_RSD.rec.ReplaceParmSign != "X" )
                    note = "Процедура выполнялась в режиме <без замены несовпадающих параметров>";
                elif( PARTYIMPREC_RSD.rec.PartyNotUpdate == "X" )
                    note = "Субъект имеет признак <Не обновлять при импорте>";
                elif( PARTYIMPREC_RSD.rec.StatusFullName == PARAMSTATUS_Ident )
                    if( substr(PARTYIMPREC_RSD.rec.RICode, 1, 3) == CONST_PERSONRICODE )
                        note = "Субъект - физ. лицо найден по полному наименованию и не имеет репоз. кода";
                    else
                        note = "Субъект - юр. лицо найден по полному наименованию и не имеет репоз. кода";
                    end;
                end;
            else
                if( PARTYIMPREPO_RSD.rec.AddParmSign != "X" )
                    note = "Процедура выполнялась в режиме <без добавления отсутствующих параметров>";
                elif( PARTYIMPREC_RSD.rec.PartyNotUpdate == "X" )
                    note = "Субъект имеет признак <Не обновлять при импорте>";
                elif( PARTYIMPREC_RSD.rec.StatusFullName == PARAMSTATUS_Ident )
                    if( substr(PARTYIMPREC_RSD.rec.RICode, 1, 3) == CONST_PERSONRICODE )
                        note = "Субъект - физ. лицо найден по полному наименованию и не имеет репоз. кода";
                    else
                        note = "Субъект - юр. лицо найден по полному наименованию и не имеет репоз. кода";
                    end;
                end;
            end;
        end;
    elif( PARTYIMPREPO_RSD.rec.LoadSign != "X" )
        note = "Процедура выполнялась в режиме <без загрузки справочника участников>";
    end;
    return note;
end; /* GetUpdatePartyNote */

private macro PrintImpRec( status )
    var clientCode = GetClientCode( PARTYIMPREC_RSD.rec.PartyId );
    var result = GetUpdatePartyResult( status );
    var note = GetUpdatePartyNote( status );
    
    if( PARTYIMPREC_RSD.rec.StatusRICode == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "Идентификационный код субъекта, присвоенный Репозитарием",
                        PARTYIMPREC_RSD.rec.RICode, PARTYIMPREC_RSD.rec.RICodeDB, result, note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "Идентификационный код субъекта, присвоенный Репозитарием",
                        PARTYIMPREC_RSD.rec.RICode, result, note);
        end;
        numrec = numrec + 1;
    end;

    if( PARTYIMPREC_RSD.rec.StatusShortName == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "Краткое официальное наименование",
                        PARTYIMPREC_RSD.rec.ShortName, PARTYIMPREC_RSD.rec.ShortNameDB, result, note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "Краткое официальное наименование",
                        PARTYIMPREC_RSD.rec.ShortName, result, note);
        end;
        numrec = numrec + 1;
    end;

    if( PARTYIMPREC_RSD.rec.StatusFullName == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "Полное официальное наименование",
                        PARTYIMPREC_RSD.rec.FullName, PARTYIMPREC_RSD.rec.FullNameDB, "Параметр не изменен", note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "Полное официальное наименование",
                        PARTYIMPREC_RSD.rec.FullName, "Параметр не добавлен", note);
        end;
        numrec = numrec + 1;
    end;
    
    if( PARTYIMPREC_RSD.rec.StatusFullNameEng == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "Полное официальное наименование на англ. языке",
                        PARTYIMPREC_RSD.rec.FullNameEng, PARTYIMPREC_RSD.rec.FullNameEngDB, "Параметр не изменен", note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "Полное официальное наименование на англ. языке",
                        PARTYIMPREC_RSD.rec.FullNameEng, "Параметр не добавлен", note);
        end;
        numrec = numrec + 1;
    end;

    if( PARTYIMPREC_RSD.rec.StatusIsCentrCount == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "Является центральным контрагентом",
                        PARTYIMPREC_RSD.rec.IsCentrCount, PARTYIMPREC_RSD.rec.IsCentrCountDB, result, note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "Является центральным контрагентом",
                        PARTYIMPREC_RSD.rec.IsCentrCount, result, note);
        end;
        numrec = numrec + 1;
    end;

    if( PARTYIMPREC_RSD.rec.StatusIsClearOrg == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "Является клиринговой организацией",
                        PARTYIMPREC_RSD.rec.IsClearOrg, PARTYIMPREC_RSD.rec.IsClearOrgDB, result, note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "Является клиринговой организацией",
                        PARTYIMPREC_RSD.rec.IsClearOrg, result, note);
        end;
        numrec = numrec + 1;
    end;

    if( PARTYIMPREC_RSD.rec.StatusINN == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "ИНН для юр.лиц - резидентов",
                        PARTYIMPREC_RSD.rec.INN, PARTYIMPREC_RSD.rec.INNDB, result, note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "ИНН для юр.лиц - резидентов",
                        PARTYIMPREC_RSD.rec.INN, result, note);
        end;
        numrec = numrec + 1;
    end;

    if( PARTYIMPREC_RSD.rec.StatusOGRN == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "ОГРН",
                        PARTYIMPREC_RSD.rec.OGRN, PARTYIMPREC_RSD.rec.OGRNDB, result, note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "ОГРН",
                        PARTYIMPREC_RSD.rec.OGRN, result, note);
        end;
        numrec = numrec + 1;
    end;

    if( PARTYIMPREC_RSD.rec.StatusBIC == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "Банковский БИК",
                        PARTYIMPREC_RSD.rec.BIC, PARTYIMPREC_RSD.rec.BICDB, result, note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "Банковский БИК",
                        PARTYIMPREC_RSD.rec.BIC, result, note);
        end;
        numrec = numrec + 1;
    end;

    if( PARTYIMPREC_RSD.rec.StatusSwiftBIC == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "Код Swift BIC",
                        PARTYIMPREC_RSD.rec.SwiftBIC, PARTYIMPREC_RSD.rec.SwiftBICDB, result, note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "Код Swift BIC",
                        PARTYIMPREC_RSD.rec.SwiftBIC, result, note);
        end;
        numrec = numrec + 1;
    end;

    if( PARTYIMPREC_RSD.rec.StatusLEI == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "LEI Международный код идентификации юридического лица",
                        PARTYIMPREC_RSD.rec.LEI, PARTYIMPREC_RSD.rec.LEIDB, result, note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "LEI Международный код идентификации юридического лица",
                        PARTYIMPREC_RSD.rec.LEI, result, note);
        end;
        numrec = numrec + 1;
    end;

    if( PARTYIMPREC_RSD.rec.StatusNoReporting == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "Признак отказа от предоставления сведений",
                        PARTYIMPREC_RSD.rec.NoReporting, PARTYIMPREC_RSD.rec.NoReportingDB, result, note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "Признак отказа от предоставления сведений",
                        PARTYIMPREC_RSD.rec.NoReporting, result, note);
        end;
        numrec = numrec + 1;
    end;

    if( PARTYIMPREC_RSD.rec.StatusIsResident == status )
        if( status == PARAMSTATUS_NotEqual )
            PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "Резидент РФ",
                        PARTYIMPREC_RSD.rec.IsResident, PARTYIMPREC_RSD.rec.IsResidentDB, result, note);
        else
            PrintLine2( PARTYIMPREC_RSD.rec.RecId, clientCode, "Резидент РФ",
                        PARTYIMPREC_RSD.rec.IsResident, result, note);
        end;
        numrec = numrec + 1;
    end;
    
    if( (status == PARAMSTATUS_NotEqual) and 
        ((PARTYIMPREC_RSD.rec.Status == RECSTATUS_RepoUpdated) or (PARTYIMPREC_RSD.rec.Status == RECSTATUS_RUpdError)) )
        PrintLine1( PARTYIMPREC_RSD.rec.RecId, clientCode, "Является участником репозитария",
                    "X", "", result, note);
        numrec = numrec + 1;
    end;

    /* RIC участника из дублирующихся записей */
    var cmd = RsdCommand(
        "SELECT t_recid, t_RICode, t_RICodeDB, t_StatusRICode FROM DIR_PARTYIMPREC_DBT " +
         "WHERE t_ImpRepID = ? AND t_fullname = ? AND t_status = " + RECSTATUS_Duplicate);
    cmd.addParam("", RSDBP_IN, PARTYIMPREC_RSD.rec.ImpRepID);
    cmd.addParam("", RSDBP_IN, PARTYIMPREC_RSD.rec.FullName);
    cmd.execute();
    var duplRsd = TRsbDataSet(cmd);    
    while( duplRsd.MoveNext() )
        if( duplRsd.rec.StatusRICode == status )
            if( status == PARAMSTATUS_NotEqual )
                PrintLine1( duplRsd.rec.RecId, clientCode, "Идентификационный код субъекта, присвоенный Репозитарием",
                            duplRsd.rec.RICode, duplRsd.rec.RICodeDB, result, note);
            else
                PrintLine2( duplRsd.rec.RecId, clientCode, "Идентификационный код субъекта, присвоенный Репозитарием",
                            duplRsd.rec.RICode, result, note);
            end;
            numrec = numrec + 1;
        end;
    end;
end; /*PrintImpRec1*/


macro RunReport( ImpRepId )
    var StatusStr = " Формирование протокола для процедуры импорта ...";
    var ProgressHead = "Формирование протокола";
    var i, countRecs;
    var cmdstr;

    var cmd = RsdCommand("SELECT * FROM DIR_PARTYIMPREPO_DBT WHERE t_id = ?");
    cmd.addParam("", RSDBP_IN, ImpRepId);
    cmd.execute();
    
    PARTYIMPREPO_RSD = TRsbDataSet(cmd);
    if( PARTYIMPREPO_RSD.MoveNext() )
        if( (PARTYIMPREPO_RSD.rec.Status == PROCSTATUS_Check) OR (PARTYIMPREPO_RSD.rec.Status == PROCSTATUS_Load) )
            PrintHeader;

            numrec = 1;
            PrintHead1;
            cmdstr = 
                "SELECT * FROM DIR_PARTYIMPREC_DBT " +
                " WHERE t_imprepid = ? "
                  " AND t_status IN (" + RECSTATUS_NotRepo + "," + RECSTATUS_Check + "," + 
                                         RECSTATUS_Updated + "," + RECSTATUS_RepoUpdated + "," +
                                         RECSTATUS_UpdError + "," + RECSTATUS_RUpdError + ") " +
             " ORDER BY t_recid";
            countRecs = getCountRecs( cmdstr, ImpRepId );
            InitProgress( countRecs, StatusStr, ProgressHead + ". Графа 1 \"Найденные расхождения\"");
            cmd = RsdCommand(cmdstr);
            cmd.addParam("", RSDBP_IN, ImpRepId);
            cmd.execute();
            PARTYIMPREC_RSD = TRsbDataSet(cmd);
            i = 1;
            while( PARTYIMPREC_RSD.MoveNext() )
                PrintImpRec( PARAMSTATUS_NotEqual );
                UseProgress(i);
                i = i + 1;
            end;
            PrintFloor1;
            RemProgress();

            numrec = 1;
            PrintHead2;
            InitProgress( countRecs, StatusStr, ProgressHead + ". Графа 2 \"Параметры, отсутствующие в справочнике субъектов\"");
            cmd = RsdCommand(cmdstr);
            cmd.addParam("", RSDBP_IN, ImpRepId);
            cmd.execute();
            PARTYIMPREC_RSD = TRsbDataSet(cmd);
            i = 1;
            while( PARTYIMPREC_RSD.MoveNext() )
                PrintImpRec( PARAMSTATUS_NotFound );
                UseProgress(i);
                i = i + 1;
            end;
            PrintFloor2;
            RemProgress();

            numrec = 1;
            PrintHead3;
            cmdstr = 
                "SELECT * FROM DIR_PARTYIMPREC_DBT " +
                " WHERE t_imprepid = ? AND t_status IN (" + RECSTATUS_None + "," + RECSTATUS_Inserted + "," + RECSTATUS_InsError + ") " +
             " ORDER BY t_recid";       
            countRecs = getCountRecs( cmdstr, ImpRepId );
            InitProgress( countRecs, StatusStr, ProgressHead + ". Графа 3 \"Участники, отсутствующие в Справочнике субъектов\"");
            cmd = RsdCommand(cmdstr);
            cmd.addParam("", RSDBP_IN, ImpRepId);
            cmd.execute();
            PARTYIMPREC_RSD = TRsbDataSet(cmd);
            while( PARTYIMPREC_RSD.MoveNext() )
                PrintLine3;
                UseProgress(numrec);
                numrec = numrec + 1;
            end;
            PrintFloor3;
            RemProgress();
        else
            msgbox("Недостаточно данных для формирования протокола");
        end;
    end;
end;
