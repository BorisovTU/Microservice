/*
    $Name: ir_CM043.mac
    $Module:   Репозитарий
    $Description:  Анкета договора форварда на облигации
*/

import "ir_CMBase.mac";

class (CMBase) CM043 ( _isRecon, _conveyerID, _ir_op : object)
  InitCMBase();
  
  m_isRecon     = _isRecon;
  m_conveyerID  = _conveyerID;
  m_ir_op       = _ir_op;
  m_MessageID   = REPOS_CM043;

  macro addCMAll(ir_generalinf     ,
                 TradeRepository   ,
                 Party1            ,
                 Party2            ,
                 UTIGeneratingParty,
                 Sender            ,
                 cm                ,
                 Instruments       ,
                 GrDealID          ,
                 TemplNum          ,
                 DealParmForReport )
    addGenInf(ir_generalinf, TemplNum, DealParmForReport);
    addRepAccGrDoc(GrDealID);
    addcmGrDoc(GrDealID);
    addParty(TradeRepository, Party1, Party2, UTIGeneratingParty, Sender);
    addCM(cm);
    addInstrument(Instruments);
  end;

  macro insertAll
    var err = 0;
    err = insertGeneralinf;
    if (not err)
      err = insertParty;
    end;
    if (not err)
      err = insertDlgrdocCache;
    end;
    if (not err)
      err = insertCM;
    end;
    if (not err)
      err = insertInstrument;
    end;
    return err;
  end;

  macro ЗаполнениеCM (cm043 : @TrecHandler, 
                      DataSet, 
                      GenAgr,
                      FD,
                      first,
                      field,
                      Instruments : @TrecHandler, 
                      DealParmForReport)
    var erFlag : bool = false;
    var bondFaceValue;
    var stat = 0;
    const ERROR_NominalValue = "Не задано значение номинала для облигации на дату заключения сделки";

    if (first)
        //Instruments.clear();
        if( (FD.IsBuy() and (GenAgr.Side1 == 1)) or
            (FD.IsSale() and (GenAgr.Side1 == 2)) )
            cm043.rec.buyerid  = IR_PARTY_KIND_PARTY1;
            cm043.rec.buyer    = "Party1";
            cm043.rec.sellerid = IR_PARTY_KIND_PARTY2;
            cm043.rec.seller   = "Party2";
        else
            cm043.rec.buyerid  = IR_PARTY_KIND_PARTY2;
            cm043.rec.buyer    = "Party2";
            cm043.rec.sellerid = IR_PARTY_KIND_PARTY1;
            cm043.rec.seller   = "Party1";
        end;
        if (FD.BaseFI().rec.FI_Kind == 2)
          cm043.rec.UnderlyerType = "Bond";
          
          cm043.rec.AmountCurrencyID =    FD.BaseFI().rec.FaceValueFI;
          cm043.rec.OpenUnits = FD.nFI_().rec.Amount;
          var NominalValue = GetFaceValue(FD.BaseFI().rec.FIID, FD.ДатаСделки());
          if( NominalValue != 0.0 )
              var FIIDFrom = FD.BaseFI().rec.FaceValueFI;
              var FIIDTo = FD.nFI_().rec.PriceFIID;
              if( FIIDFrom != FIIDTo ) 
                  if( DV_SmartConvertSum( NominalValue, NominalValue, FD.ДатаСделки(), FIIDFrom, FIIDTo, false ) == false )
                      RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, m_MessageType, 
                                                  GetCurrencyConvertErrorMsg(FIIDFrom, FIIDTo, FD.ДатаСделки()), "NominalAmount", GetReportAction(DataSet.TemplNum), 400 + 33);
                      stat = 1;
                  end;
              end;
          else
              RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(DealParmForReport.DealID, DealParmForReport.DealKind, DataSet.DealCode, m_MessageType, 
                                          ERROR_NominalValue, "NominalAmount", GetReportAction(DataSet.TemplNum), 400 + 33);
              stat = 1;
          end;
          
          cm043.rec.Amount =  cm043.rec.OpenUnits * NominalValue;
        elif (FD.BaseFI().rec.FI_Kind == 3)
          cm043.rec.UnderlyerType = "Index";
          cm043.rec.OpenUnits = 1;
          cm043.rec.AmountCurrencyID = FD.nFI_().rec.PriceFIID;
          cm043.rec.Amount = FD.nFI_BaseActiv.rec.Amount * FD.nFI_().rec.Price;
        end;
    end;

    cm043.rec.AmountCurrency  = GetFICode(cm043.rec.AmountCurrencyID, null, FICK_ISOSTRING);

    Instruments.rec.FIID = FD.BaseFI().rec.FIID;
    Instruments.rec.InstrumentName = FD.BaseFI().rec.Name;
    if( cm043.rec.UnderlyerType == "Bond" )
      Instruments.rec.InstrumentCode = GetAvoirissCode(FD.BaseFI(), m_ir_op.rec.Date);
    else
      Instruments.rec.InstrumentCode = FD.BaseFI().rec.FI_Code;
    end;

    if( FD.ДатаОплаты() != ZeroDate )
      cm043.rec.SettlmentDate = FD.ДатаОплаты();
    else
      cm043.rec.SettlmentDate = FD.ДатаИсполнения();
    end;
    cm043.rec.Price = FD.nFI_().rec.Price;
    cm043.rec.SettlCurrencyID = FD.ВалютаРасчётов();
    cm043.rec.SettlCurrency = GetFICode(cm043.rec.SettlCurrencyID, null, FICK_ISOSTRING);
    cm043.rec.PriceCurrencyID = FD.nFI_().rec.PriceFIID;
    cm043.rec.PriceCurrency = GetFICode(cm043.rec.PriceCurrencyID, null, FICK_ISOSTRING);
    return stat;
  end;
end;