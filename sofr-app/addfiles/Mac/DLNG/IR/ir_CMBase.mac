/*
    $Name: ir_CMBase.mac
    $Module:   Репозитарий
    $Description:  Базовый класс для сообщений репозитария
*/

import "ir_CMfunctions.mac";

class CMBase()
  var m_ir_op = TBFile("ir_op.dbt");
  var m_RepDataArr = TArray();
  var arrsize = 0;
  var m_isRecon;
  var m_conveyerID;
  var m_ReconMsgID = 0;
  var m_MessageType = 0;
  var m_MessageID = 0;

  var m_GeneralInfArr  = TArray(); /*массив СРС для cm*/
  var m_cmArr          = TArray(); /*массив cm*/
  var m_instrumentsArr = TArray(); /*массив instruments для */
  var m_PartyArr       = TArray(); /*массив субъектов для cm*/
  var m_RepAccGrDocArr = TArray(); /*массив привязок СРС к строке графика для cm*/
  var m_cmGrDocArr     = TArray(); /*массив привязок cm к строке графика*/
  var m_SrvDocArr      = TArray();  /*массив привязок строк графика к СОРУ*/
  var m_RepActionArr   = TArray();
  var m_RepDealParmArr = TArray();

  macro SetGrDealID( GrDealID:integer )
    var SrvDocSize = m_SrvDocArr.size;
    //для каждой обрабатываемой строки графика будем создавать запись в dlgrdoc в качестве информации о факте обработки
    m_SrvDocArr[SrvDocSize] = TRecHandler("dlgrdoc.dbt");
    m_SrvDocArr[SrvDocSize].Clear();
    m_SrvDocArr[SrvDocSize].rec.GrDealID= GrDealID;
    m_SrvDocArr[SrvDocSize].rec.DocKind = 0; /*Именно так*/
    m_SrvDocArr[SrvDocSize].rec.DocID   = 0; /*Именно так*/
    m_SrvDocArr[SrvDocSize].rec.ServDocKind = m_ir_op.rec.DocKind;
    m_SrvDocArr[SrvDocSize].rec.ServDocID   = m_ir_op.rec.ID;
  end;

  //Заполнение служебных параметров
  macro addGenInf (ir_generalinf,
                   TemplNum,
                   DealParmForReport)
    if ( m_isRecon )
      ir_generalinf.rec.MessageType = "CM001";
    end;
    arrsize = m_GeneralInfArr.size;
    m_GeneralInfArr[m_GeneralInfArr.size/*arrsize*/] = TRecHandler("ir_generalinf.dbt");
    copy(m_GeneralInfArr[m_GeneralInfArr.size-1/*arrsize*/], ir_generalinf);
    m_RepActionArr[arrsize] = GetReportAction( TemplNum );
    m_RepDealParmArr[arrsize] = DealParmForReport;
  end;

  //Заполнение данных о привязках
  macro addRepAccGrDoc (GrDealID)
   arrsize = m_RepAccGrDocArr.size;
   m_RepAccGrDocArr[arrsize] = TRecHandler("dlgrdoc.dbt");
   m_RepAccGrDocArr[arrsize].Clear();
   m_RepAccGrDocArr[arrsize].rec.GrDealID= IIF(m_conveyerID == IR_REPACC_SVODSRS, 0, GrDealID);
   m_RepAccGrDocArr[arrsize].rec.DocKind = REPOS_GENERALINF;
   m_RepAccGrDocArr[arrsize].rec.ServDocKind = m_ir_op.rec.DocKind;
   m_RepAccGrDocArr[arrsize].rec.ServDocID   = m_ir_op.rec.ID;

  end;

  macro addcmGrDoc(GrDealID)
    arrsize = m_cmGrDocArr.size;
    m_cmGrDocArr[arrsize] = TRecHandler("dlgrdoc.dbt");
    m_cmGrDocArr[arrsize].Clear();
    m_cmGrDocArr[arrsize].rec.GrDealID= GrDealID;
    m_cmGrDocArr[arrsize].rec.DocKind = m_MessageID;
    m_cmGrDocArr[arrsize].rec.ServDocKind = m_ir_op.rec.DocKind;
    m_cmGrDocArr[arrsize].rec.ServDocID   = m_ir_op.rec.ID;
  end;
  
  //Заполнение данных о субъектах
  macro addParty(TradeRepository,
                 Party1,
                 Party2,
                 UTIGeneratingParty,
                 Sender)

    arrsize = m_PartyArr.size;
    m_PartyArr[arrsize] = TRecHandler("ir_party.dbt");
    TradeRepository.rec.PartyType = "TradeRepository";
    copy(m_PartyArr[arrsize], TradeRepository);

    arrsize = m_PartyArr.size;
    m_PartyArr[arrsize] = TRecHandler("ir_party.dbt");
    Party1.rec.PartyType= "Party1";
    copy(m_PartyArr[arrsize], Party1);

    arrsize = m_PartyArr.size;
    m_PartyArr[arrsize] = TRecHandler("ir_party.dbt");
    Party2.rec.PartyType= "Party2";
    copy(m_PartyArr[arrsize], Party2);

    if(UTIGeneratingParty != NULL)
    arrsize = m_PartyArr.size;
    m_PartyArr[arrsize] = TRecHandler("ir_party.dbt");
    UTIGeneratingParty.rec.PartyType= "UTIGeneratingParty";
    copy(m_PartyArr[arrsize], UTIGeneratingParty);
    end;

    arrsize = m_PartyArr.size;
    m_PartyArr[arrsize] = TRecHandler("ir_party.dbt");
    Sender.rec.PartyType= "Sender";
    copy(m_PartyArr[arrsize], Sender);
  end;

  //Заполнение экономических параметров
  macro addCM(cm)
    arrsize = m_cmArr.size;
    m_cmArr[arrsize] = TRecHandler("ir_" + m_MessageType + ".dbt");
    copy(m_cmArr[arrsize], cm);
  end;

  //Заполнение инструмента
  macro addInstrument(Instruments)
    arrsize = m_instrumentsArr.size;
    m_instrumentsArr[arrsize] = TRecHandler("ir_instruments.dbt");
    copy(m_instrumentsArr[arrsize], Instruments);
  end;

  // Вставляем СРС и обновляем привязки к строке графика
  macro insertGeneralinf
    var ir_generalinfCache = RsbSQLInsert("ir_generalinf.dbt");
    var err = 0;
    var realIDs = TArray();

    ir_generalinfCache.AddRecordArray(m_GeneralInfArr);
    ir_generalinfCache.AddReturning("t_InternalMessageID", V_INTEGER);

    if( not ir_generalinfCache.insert() )
        err = 1;
        RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(m_RepDealParmArr[m_RepDealParmArr.size - 1].DealID, m_RepDealParmArr[m_RepDealParmArr.size - 1].DealKind, "", "", "Ошибка при сохранении данных сообщения", "", 0);
    else
        ir_generalinfCache.GetReturning(realIDs);
        var i = 0;
        while( i < m_GeneralInfArr.size )
            m_GeneralInfArr[i].rec.InternalMessageID = realIDs[i];
            m_RepAccGrDocArr[i].rec.DocID = realIDs[i];
            
            m_PartyArr[i*5].rec.InternalMessageID   = realIDs[i];
            m_PartyArr[i*5+1].rec.InternalMessageID = realIDs[i];
            m_PartyArr[i*5+2].rec.InternalMessageID = realIDs[i];
            m_PartyArr[i*5+3].rec.InternalMessageID = realIDs[i];
            m_PartyArr[i*5+4].rec.InternalMessageID = realIDs[i];
            
            if( (not err) and (m_cmArr.size > 0) )
              m_cmArr[i].rec.InternalMessageID = realIDs[i];
            end;
            
            if( (not err) and (m_instrumentsArr.size > 0) )
              m_instrumentsArr[i].rec.InternalMessageID = realIDs[i];
            end;
            
            m_ReconMsgID = realIDs[i];
            
            /* добавить запись в лог о вставке СРС */
            if ( not m_isRecon )
                m_RepDataArr[m_RepDataArr.size] = CIR_RepAccRepParm(
                        m_RepActionArr[i],
                        m_GeneralInfArr[i].rec.DealCode,
                        m_GeneralInfArr[i].rec.MessageType,
                        DL_GetNameAlg(ALG_GENINF_RSTATUS, m_GeneralInfArr[i].rec.RStatus),
                        m_RepDealParmArr[i].DealID, 
                        m_RepDealParmArr[i].DealKind);
            end;
            i = i + 1;
        end;
    end;
    return err;
  end;
  
  // Вставляем субъектов
  macro insertParty
    var ir_partyCache = RsbSQLInsert("ir_party.dbt");
    var err = 0;
    ir_partyCache.AddRecordArray(m_PartyArr);

    if( not ir_partyCache.insert() )
      err = 1;
      RepErrArr[RepErrArr.size] = CIR_RepAccRepErr("", "","", "", "Ошибка при сохранении данных субъектов", "", 0);
    end;
    return err;
  end;

  // Вставляем привязки
  macro insertDlgrdocCache
    var dlgrdocCache = RsbSQLInsert("dlgrdoc.dbt");
    var err = 0;
    if (m_RepAccGrDocArr.size > 0)
      dlgrdocCache.AddRecordArray(m_RepAccGrDocArr);
    end;
    if (m_SrvDocArr.size > 0)
      dlgrdocCache.AddRecordArray(m_SrvDocArr);
    end;
    if (m_cmGrDocArr.size > 0)
      dlgrdocCache.AddRecordArray(m_cmGrDocArr);
    end;
    if( not dlgrdocCache.insert() )
      err = 1;
      RepErrArr[RepErrArr.size] = CIR_RepAccRepErr("","", "", "", "Ошибка при сохранении данных", "", 0);
    end;
    return err;
  end;
  
  // Вставляем экономические параметры
  macro insertCM
    var ir_cmCache  = RsbSQLInsert("ir_" + m_MessageType + ".dbt");
    var err = 0;
    var realIDs = TArray();

    ir_cmCache.AddRecordArray(m_cmArr);
    ir_cmCache.AddReturning("t_id", V_INTEGER);
    
    if( not ir_cmCache.insert() )
        err = 1;
        RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(m_RepDealParmArr[m_RepDealParmArr.size - 1].DealID, m_RepDealParmArr[m_RepDealParmArr.size - 1].DealKind, "Ошибка при сохранении данных экономических параметров", "", 0);
    else
        realIDs.size = 0;
        ir_cmCache.GetReturning(realIDs);
        var i = 0;
        while( i < m_cmGrDocArr.size )
            m_cmGrDocArr[i].rec.DocID = realIDs[i];
            i = i + 1;
        end;
    end;
    return err;
  end;

  // Вставка инструмента
  macro insertInstrument
    var ir_instrumentsCache  = RsbSQLInsert("ir_instruments.dbt");
    var err = 0;
    if( (m_instrumentsArr.size > 0) )
      ir_instrumentsCache.AddRecordArray(m_instrumentsArr);

      if( not ir_instrumentsCache.insert() )
        err = 1;
        RepErrArr[RepErrArr.size] = CIR_RepAccRepErr(m_RepDealParmArr[m_RepDealParmArr.size - 1].DealID, m_RepDealParmArr[m_RepDealParmArr.size - 1].DealKind, "Ошибка при сохранении данных финансовых инструментов", "", 0);
      end;
    end;
    return err;
  end;

  m_GeneralInfArr.size  = 0;
  m_cmArr.size          = 0;
  m_instrumentsArr.size = 0;
  m_PartyArr.size       = 0;
  m_RepAccGrDocArr.size = 0;
  m_cmGrDocArr.size     = 0;
  m_RepActionArr.size   = 0;
  m_RepDealParmArr.size = 0;
end;

class (CMBase) NOMSG ( _ir_op : object)
  InitCMBase();
  m_ir_op = _ir_op;
  macro insertAll
    var err = 0;
    err = insertDlgrdocCache;
    return err;
  end;
end;
