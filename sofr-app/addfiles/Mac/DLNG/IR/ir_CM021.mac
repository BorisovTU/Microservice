/*
    $Name: ir_CM021.mac
    $Module:   Репозитарий
    $Description:  Анкета договора валютного свопа 
*/

import "ir_CMBase.mac";

class (CMBase) CM021 ( _isRecon, _conveyerID, _ir_op : object)
  InitCMBase();

  m_isRecon     = _isRecon;
  m_conveyerID  = _conveyerID;
  m_ir_op       = _ir_op;
  m_MessageID   = REPOS_CM021;

  macro addCMAll(ir_generalinf     ,
                 TradeRepository   ,
                 Party1            ,
                 Party2            ,
                 UTIGeneratingParty,
                 Sender            ,
                 cm                ,
                 GrDealID          ,
                 TemplNum          ,
                 DealParmForReport )
    addGenInf(ir_generalinf, TemplNum, DealParmForReport);
    addRepAccGrDoc(GrDealID);
    addcmGrDoc(GrDealID);
    addParty(TradeRepository, Party1, Party2, UTIGeneratingParty, Sender);
    addCM(cm);
  end;

  macro insertAll
    var err = 0;
    err = insertGeneralinf;
    if (not err)
      err = insertParty;
    end;
    if (not err)
      err = insertDlgrdocCache;
    end;
    if (not err)
      err = insertCM;
    end;
    return err;
  end;

  macro ЗаполнениеCM (cm : @TrecHandler,
                      DataSet,
                      GenAgr,
                      FD)
    var FD2 = DVFirstDocNDeal(DataSet.DocKind, DataSet.DocID, 2);

    if( (FD.IsBuy() and (GenAgr.Side1 == 1)) or
        (FD.IsSale() and (GenAgr.Side1 != 1))
        )
        if ( FD.nFI_().rec.ExecType == 1 )
          cm.rec.AmountCurr1_Part1 = FD.nFI_().rec.Cost;
        else
          cm.rec.AmountCurr1_Part1 = FD.nFI_().rec.Amount * FD.nFI_().rec.Price;
        end;
        if ( FD2.nFI_().rec.ExecType == 1 )
          cm.rec.AmountCurr1_Part2 = FD2.nFI_().rec.Cost;
        else
          cm.rec.AmountCurr1_Part2 = FD2.nFI_().rec.Amount * FD2.nFI_().rec.Price;
        end;
        cm.rec.AmountCurr2_Part1 = FD.nFI_().rec.Amount;
        cm.rec.AmountCurr2_Part2 = FD2.nFI_().rec.Amount;
        cm.rec.Currency1_Part1ID = FD.ВалютаЦены();
        cm.rec.Currency2_Part1ID = FD.BaseFI().rec.FIID;
        cm.rec.Currency1_Part2ID = cm.rec.Currency1_Part1ID;
        cm.rec.Currency2_Part2ID = cm.rec.Currency2_Part1ID;
        cm.rec.TradeCurr_Part1   = "ExchangedCurrency2";
        cm.rec.TradeCurr_Part2   = "ExchangedCurrency2";
        cm.rec.TradeCurr_Part1ID = cm.rec.Currency2_Part1ID;
        cm.rec.TradeCurr_Part2ID = cm.rec.Currency2_Part2ID;
        
        if ( FD.nFI_().rec.IsInverse == "X" )
            cm.rec.QBasis_Part1 = "Currency2PerCurrency1";
        else
            cm.rec.QBasis_Part1 = "Currency1PerCurrency2";
        end;
    else
        cm.rec.AmountCurr1_Part1 = FD.nFI_().rec.Amount;
        cm.rec.AmountCurr1_Part2 = FD2.nFI_().rec.Amount;
        if ( FD.nFI_().rec.ExecType == 1 )
          cm.rec.AmountCurr2_Part1 = FD.nFI_().rec.Cost;
        else
          cm.rec.AmountCurr2_Part1 = FD.nFI_().rec.Amount * FD.nFI_().rec.Price;
        end;
        if ( FD2.nFI_().rec.ExecType == 1 )
            cm.rec.AmountCurr2_Part2 = FD2.nFI_().rec.Cost;
        else
            cm.rec.AmountCurr2_Part2 = FD2.nFI_().rec.Amount * FD2.nFI_().rec.Price;
        end;
        cm.rec.Currency1_Part1ID = FD.BaseFI().rec.FIID;
        cm.rec.Currency2_Part1ID = FD.ВалютаЦены();
        cm.rec.Currency1_Part2ID = cm.rec.Currency1_Part1ID;
        cm.rec.Currency2_Part2ID = cm.rec.Currency2_Part1ID;
        cm.rec.TradeCurr_Part1   = "ExchangedCurrency1";
        cm.rec.TradeCurr_Part2   = "ExchangedCurrency1";
        cm.rec.TradeCurr_Part1ID = cm.rec.Currency1_Part1ID;
        cm.rec.TradeCurr_Part2ID = cm.rec.Currency1_Part2ID;

        if ( FD.nFI_().rec.IsInverse == "X" )
            cm.rec.QBasis_Part1 = "Currency1PerCurrency2";
        else
            cm.rec.QBasis_Part1 = "Currency2PerCurrency1";
        end;
    end;
    
    cm.rec.PayerCurr1_Part1ID   = IR_PARTY_KIND_PARTY1;
    cm.rec.PayerCurr1_Part1     = "Party1";
    cm.rec.ReceiverCurr1_Part1ID = IR_PARTY_KIND_PARTY2;
    cm.rec.ReceiverCurr1_Part1   = "Party2";
    cm.rec.PayerCurr1_Part2ID   = IR_PARTY_KIND_PARTY2;
    cm.rec.PayerCurr1_Part2     = "Party2";
    cm.rec.ReceiverCurr1_Part2ID = IR_PARTY_KIND_PARTY1;
    cm.rec.ReceiverCurr1_Part2   = "Party1";
    cm.rec.PayerCurr2_Part1ID    = cm.rec.ReceiverCurr1_Part1ID;
    cm.rec.PayerCurr2_Part1      = cm.rec.ReceiverCurr1_Part1;
    cm.rec.ReceiverCurr2_Part1ID = cm.rec.PayerCurr1_Part1ID;
    cm.rec.ReceiverCurr2_Part1   = cm.rec.PayerCurr1_Part1;
    cm.rec.PayerCurr2_Part2ID    = cm.rec.ReceiverCurr1_Part2ID;
    cm.rec.PayerCurr2_Part2      = cm.rec.ReceiverCurr1_Part2;
    cm.rec.ReceiverCurr2_Part2ID = cm.rec.PayerCurr1_Part2ID;
    cm.rec.ReceiverCurr2_Part2   = cm.rec.PayerCurr1_Part2;
    cm.rec.Currency1_Part1 = GetFICode(cm.rec.Currency1_Part1ID, null, FICK_ISOSTRING);
    cm.rec.Currency2_Part1 = GetFICode(cm.rec.Currency2_Part1ID, null, FICK_ISOSTRING);
    cm.rec.Currency1_Part2 = GetFICode(cm.rec.Currency1_Part2ID, null, FICK_ISOSTRING);
    cm.rec.Currency2_Part2 = GetFICode(cm.rec.Currency2_Part2ID, null, FICK_ISOSTRING);
    cm.rec.ValDate_Part1   = max(FD.ДатаПоставки(), FD.ДатаОплаты());
    if ( cm.rec.ValDate_Part1 == date(0, 0, 0) )
        cm.rec.ValDate_Part1 = FD.ДатаИсполнения();
    end;
    cm.rec.ValDate_Part2   = max(FD2.ДатаПоставки(), FD2.ДатаОплаты());
    if ( cm.rec.ValDate_Part2 == date(0, 0, 0) )
        cm.rec.ValDate_Part2 = FD2.ДатаИсполнения();
    end;
    cm.rec.Quoted_Curr1_Part1ID = cm.rec.Currency1_Part1ID;
    cm.rec.Quoted_Curr1_Part1   = cm.rec.Currency1_Part1;
    cm.rec.Quoted_Curr2_Part1ID = cm.rec.Currency2_Part1ID;
    cm.rec.Quoted_Curr2_Part1   = cm.rec.Currency2_Part1;
    cm.rec.Quoted_Curr1_Part2ID = cm.rec.Currency1_Part2ID;
    cm.rec.Quoted_Curr1_Part2   = cm.rec.Currency1_Part2;
    cm.rec.Quoted_Curr2_Part2ID = cm.rec.Currency2_Part2ID;
    cm.rec.Quoted_Curr2_Part2   = cm.rec.Currency2_Part2;
    cm.rec.QBasis_Part2         = cm.rec.QBasis_Part1;

    if ( FD.nFI_().rec.Scale == 1 )                                             // Курс задан за единицу актива
        cm.rec.Rate_Part1 = FD.nFI_().rec.Price;                                // <Цена единицы БА> из сделки
    elif ( FD.nFI_().rec.IsInverse == "X" )                                     // Задан признак обратной котировки
        cm.rec.Rate_Part1 = FD.nFI_().rec.Amount * 1.0 / FD.nFI_().rec.Cost;    // количество единиц БА / количество единиц КА
    else
        cm.rec.Rate_Part1 = FD.nFI_().rec.Cost * 1.0 / FD.nFI_().rec.Amount;    // количество единиц КА / количество единиц БА
    end;
    
    if ( FD2.nFI_().rec.Scale == 1 )                                            // Курс задан за единицу актива
        cm.rec.Rate_Part2 = FD2.nFI_().rec.Price;                               // <Цена единицы БА> из сделки
    elif ( FD2.nFI_().rec.IsInverse == "X" )                                    // Задан признак обратной котировки
        cm.rec.Rate_Part2 = FD2.nFI_().rec.Amount * 1.0 / FD2.nFI_().rec.Cost;  // количество единиц БА / количество единиц КА
    else
        cm.rec.Rate_Part2 = FD2.nFI_().rec.Cost * 1.0 / FD2.nFI_().rec.Amount;  // количество единиц КА / количество единиц БА
    end;
    
    if ( FD.nFI_().rec.ExecType != 1 ) // по беспоставочному договору
        cm.rec.SettlCurr_Part1ID = FD.ВалютаРасчётов();
        cm.rec.SettlCurr_Part1 = GetFICode(cm.rec.SettlCurr_Part1ID, null, FICK_ISOSTRING);
    end;
    if ( FD2.nFI_().rec.ExecType != 1 ) // по беспоставочному договору
        cm.rec.SettlCurr_Part2ID = FD2.ВалютаРасчётов();
        cm.rec.SettlCurr_Part2 = GetFICode(cm.rec.SettlCurr_Part2ID, null, FICK_ISOSTRING);
    end;
    return 0;
  end;
end;