/*                           
$Name:        ir_recon.mac
$Module:      Репозитарий
$Description: Процедура сверки
*/

import OprInter, IRInter, globals;
import "irstartrepacc.mac", "dlquery.mac", "cb_sql.mac", "bnk_common.mac";

PRIVATE VAR DifferencesArr = TArray();
PRIVATE VAR ProcessedRMcount = 0;
PRIVATE VAR ReconGenErrorCount = 0;
PRIVATE VAR ReconSuccessCount = 0;
PRIVATE VAR CriticalDiffCount = 0;
PRIVATE VAR NonCriticalDiffCount = 0;

private const DIFFDESC_NONCOMPLETE = "отсутствие значения параметра";
private const DIFFDESC_DIFFERENT = "разные значения параметра";
private const DIFFTYPE_NONCOMPLETE = "Отсутствие значения";
private const DIFFTYPE_VALUE = "Значение";

// пара сверяемых сообщений
PRIVATE CLASS ReconPair()
    var MsgIn:integer = 0;
    var MsgOut:integer = 0;
    var DealID:integer;
    var DocKind:integer;
END;

private macro getGrDealIDRecon(DealID, DocKind, MsgId)
    var GrDealID = 0;
    var query =         " select gdoc.t_grdealid ";
        query = query + "   from ddlgrdeal_dbt gdeal, ";
        query = query + "        ddlgrdoc_dbt gdoc, ";
        query = query + "        dir_generalinf_dbt ginf, ";
        query = query + "        ddlgracc_dbt gracc ";
        query = query + "  where gdoc.t_grdealid = gdeal.t_id ";
        query = query + "    and gdeal.t_docid = ? ";
        query = query + "    and gdeal.t_dockind = ? ";
        query = query + "    and ginf.t_internalmessageid = ? ";
        query = query + "    and ginf.t_internalmessageid = gdoc.t_docid ";
        query = query + "    and gdoc.t_dockind = ? ";
        query = query + "    and gracc.t_grdealid = gdoc.t_grdealid ";
        query = query + "    and gracc.t_accnum = ? ";
        query = query + "    and gracc.t_state = ? ";
        query = query + "    and ginf.t_rstatus = ? ";

    var cmd = DL_RSDCommand();
    cmd.addParam(DealID);
    cmd.addParam(DocKind);
    cmd.addParam(MsgId);
    cmd.addParam(REPOS_RECONCILIATION);
    cmd.addParam(DLGR_ACCKIND_REPOSITORY);
    cmd.addParam(DLGRACC_STATE_PLAN);
    cmd.addParam(RSTATUS_READY_RECON);

    var DataSet = cmd.Execute(query);
    if(DataSet.moveNext)
        GrDealID = DataSet.GrDealID;
    end;

    return GrDealID;
end;

// Общий класс сообщения, все таблицы
CLASS RSM( pInternalMessageID:integer )
    private var loaded = false;
    private var InternalMessageID:integer = pInternalMessageID;
    private var m_GeneralInf:Tbfile;
    private var m_Parties:TArray;
    private var m_Instrument:TRecHandler;
    private var m_cm021:TRecHandler;
    private var m_cm022:TRecHandler;
    private var m_cm023:TRecHandler;
    private var m_cm032:TRecHandler;
    private var m_cm041:TRecHandler;
    private var m_cm042:TRecHandler;
    private var m_cm044:TRecHandler;
    private var m_cm045:TRecHandler;
    private var m_cm046:TRecHandler;
    private var m_cm048:TRecHandler;
    private var m_cm051:TRecHandler;
    private var m_cm093:TArray;
    
    private var updateGenInf = false;
    
    // Получить вид сообщения
    macro GetMessageType()
        var query = "SELECT t_MessageCode MessageType "
                  + "  FROM dir_TypesMessage_dbt "
                  + " WHERE t_XMLProduct = '" + m_GeneralInf.rec.XMLProduct + "' ";
        var cmd = DL_RSDCommand();
        var qResult = cmd.Execute(query);
        qResult.moveNext;
        
        return qResult.MessageType;
    end;
    
    macro GetRStatus()
        return m_GeneralInf.rec.RStatus;
    end;
    
    macro GetInternalID()
        return m_GeneralInf.rec.InternalMessageID;
    end;
    
    macro GetCorrelationID()
        return m_GeneralInf.rec.CorrelationID;
    end;
    
    macro GetInReplyTo()
        return m_GeneralInf.rec.InReplyTo;
    end;
    
    // Получить значение по атрибуту шаблона
    macro GetValue(attribute:string)
        macro GetTableAndField(attribute:string, table:@string, field:@string)
            var point = Index(attribute, ".");
            table = string(attribute);
            StrSet(table, point, 0);
            field = SubStr(attribute, point + 1);
            return 0;
        end;
        
        if ( not loaded )
            return null;
        end;
        
        var table:string = "", field:string = "";
        GetTableAndField(attribute, @table, @field);
        
        if ( (table == "") or (field == "") )
            return null;
        end;
        
        var i, value = null;
        if  (( table == "GeneralInf" ) and m_GeneralInf)
            ExecExp("value = m_GeneralInf.rec." + field);
        elif (( table == "Parties" ) and m_Parties)
            value = TArray();
            i = 0;
            while ( i < m_Parties.size )
                ExecExp("value[i] = m_Parties[i].rec." + field);
                i = i + 1;
            end;
        elif (( table == "Instrument" ) and m_Instrument)
            ExecExp("value = m_Instrument.rec." + field);
        elif (( table == "CM021" ) and m_cm021)
            ExecExp("value = m_cm021.rec." + field);
        elif (( table == "CM022" ) and m_cm022)
            ExecExp("value = m_cm022.rec." + field);
        elif (( table == "CM023" ) and m_cm023)
            ExecExp("value = m_cm023.rec." + field);
        elif (( table == "CM032" ) and m_cm032)
            ExecExp("value = m_cm032.rec." + field);
        elif (( table == "CM041" ) and m_cm041)
            ExecExp("value = m_cm041.rec." + field);
        elif (( table == "CM042" ) and m_cm042)
            ExecExp("value = m_cm042.rec." + field);
        elif (( table == "CM044" ) and m_cm044)
            ExecExp("value = m_cm044.rec." + field);
        elif (( table == "CM045" ) and m_cm045)
            ExecExp("value = m_cm045.rec." + field);
        elif (( table == "CM046" ) and m_cm046)
            ExecExp("value = m_cm046.rec." + field);
        elif (( table == "CM048" ) and m_cm048)
            ExecExp("value = m_cm048.rec." + field);
        elif ( (table == "CM051") and m_cm051)            
            ExecExp("value = m_cm051.rec." + field);
        elif (( table == "CM093" ) and m_cm093)
            value = TArray();
            i = 0;
            while ( i < m_cm093.size )
                ExecExp("value[i] = m_cm093[i].rec." + field);
                i = i + 1;
            end;
        end;
        
        var valueType = ValType(value);
        if   ( valueType == V_STRING )
            if ( value == "")
                value = null;
            end;
        elif ((valueType == V_INTEGER) or (valueType == V_DOUBLE))
            if ( value == 0 )
                value = null;
            end;
        elif ( valueType == V_DATE)
            if ( GetSqlDate(value) == GetSqlDate(date(0,0,0)) )
                value = null;
            end;
        end;
        return value;
        
        OnError
            return null;
    end;
    
    macro GetBankSide():integer
        var side = 0;
        
        var i = 0;
        while ( i < m_Parties.size )
            if ( (m_Parties[i].rec.PartyKind == IR_PARTY_KIND_PARTY1) and 
                 (m_Parties[i].rec.PartyID == {ourbank}) )
                side = 1;
                break;
            else
                side = 2;
                break;
            end;
        end;
        
        return side;
    end;
    
    macro IsFullRecon():bool
        return (m_GeneralInf.rec.ReconType == "FULL");
    end;
    
    macro SetRStatus(newStatus:integer)
        m_GeneralInf.rec.RStatus = newStatus;
        m_GeneralInf.rec.RStatusName = Bnk_GetNameAlg(ALG_GENINF_RSTATUS, newStatus);
        updateGenInf = true;
    end;
    
    macro UpdateGeneralInf()
        if ( updateGenInf )
            m_GeneralInf.Update();
            // transaction will be started outside
            // maybe no transaction hmmm
            // updated here, if flagged
        end;
    end;
    
    macro UpdateGrDealAcc(DealID, DocKind, OperID, OperDocKind, OperDate)
        var GrDealID = getGrDealIDRecon(DealID, DocKind, m_GeneralInf.rec.InternalMessageID);
        var m_SrvDocArr = Tbfile("dlgrdoc", "W", 0);
         
        m_SrvDocArr.Clear();
        m_SrvDocArr.rec.GrDealID= GrDealID;
        m_SrvDocArr.rec.DocKind = 0; /*Именно так*/
        m_SrvDocArr.rec.DocID   = 0; /*Именно так*/
        m_SrvDocArr.rec.ServDocKind = OperDocKind;
        m_SrvDocArr.rec.ServDocID   = OperID;
        m_SrvDocArr.insert();
        m_SrvDocArr.Clear();
        m_SrvDocArr.rec.GrDealID    = GrDealID;
        m_SrvDocArr.rec.DocKind     = REPOS_RECONCILIATION; /*Именно так*/
        m_SrvDocArr.rec.DocID       = m_GeneralInf.rec.InternalMessageID; /*Именно так*/
        m_SrvDocArr.rec.ServDocKind = OperDocKind;
        m_SrvDocArr.rec.ServDocID   = OperID;
        m_SrvDocArr.insert();

        var Select = RsdCommand("begin rsi_dlgr.RSI_UpdateGrDealAccByID(?, ?, ?, ?);\n end;");
        Select.AddParam("pGrDealID",   RSDBP_IN, GrDealID );
        Select.AddParam("pAccNum",     RSDBP_IN, DLGR_ACCKIND_REPOSITORY );
        Select.AddParam("pState",      RSDBP_IN, DLGRACC_STATE_FACTEXEC);
        Select.AddParam("pPlanDate",   RSDBP_IN, OperDate);
        Select.Execute();

    end;

    // Подгрузить все части сообщения
    private macro LoadRSM()
    
        var tbbuf;
        
        m_GeneralInf = Tbfile("ir_generalinf", "W", 0);
        m_GeneralInf.rec.InternalMessageID = InternalMessageID;
        m_GeneralInf.getEQ;
        
        tbbuf = TBFile("ir_party");
        var cmd = DL_RSDCommand();
        var query = "SELECT t_ID ptID "
                  + "  FROM dir_party_dbt "
                  + " WHERE t_InternalMessageID = " + InternalMessageID
                  + " ORDER BY t_PartyKind";
        var ids = cmd.Execute(query);
        while ( ids.moveNext )
            if ( ValType(m_Parties) == V_UNDEF )
                m_Parties = TArray();
            end;
            var partyIdx = m_Parties.size;
            m_Parties[partyIdx] = TRecHandler("ir_party");
            tbbuf.rec.ID = ids.ptID;
            if ( tbbuf.getEQ )
                m_Parties[partyIdx] = TRecHandler("ir_party");
                Copy(m_Parties[partyIdx], tbbuf);
            else
                m_Parties[partyIdx] = null;
            end;
        end;
        
        tbbuf = TBFile("ir_instruments");
        query = "SELECT t_ID FROM dir_instruments_dbt WHERE t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        if ( ids.moveNext )
            tbbuf.rec.ID = ids.ID;
        else
            tbbuf.rec.ID = 0;
        end;
        if ( tbbuf.getEQ )
            m_Instrument = TRecHandler("ir_instruments");
            Copy(m_Instrument, tbbuf);
        else
            m_Instrument = null;
        end;
        
        tbbuf = TBFile("ir_cm021");
        query = "SELECT t_ID from dir_cm021_dbt where t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        if ( ids.moveNext )
            tbbuf.rec.ID = ids.ID;
        else
            tbbuf.rec.ID = 0;
        end;
        if ( tbbuf.getEQ )
            m_cm021 = TRecHandler("ir_cm021");
            Copy(m_cm021, tbbuf);
        else
            m_cm021 = null;
        end;

        tbbuf = TBFile("ir_cm022");
        query = "SELECT t_ID from dir_cm022_dbt where t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        if ( ids.moveNext )
            tbbuf.rec.ID = ids.ID;
        else
            tbbuf.rec.ID = 0;
        end;
        if ( tbbuf.getEQ )
            m_cm022 = TRecHandler("ir_cm022");
            Copy(m_cm022, tbbuf);
        else
            m_cm022 = null;
        end;
        
        tbbuf = TBFile("ir_cm023");
        query = "SELECT t_ID from dir_cm023_dbt where t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        if ( ids.moveNext )
            tbbuf.rec.ID = ids.ID;
        else
            tbbuf.rec.ID = 0;
        end;
        if ( tbbuf.getEQ )
            m_cm023 = TRecHandler("ir_cm023");
            Copy(m_cm023, tbbuf);
        else
            m_cm023 = null;
        end;
        
        tbbuf = TBFile("ir_cm032");
        query = "SELECT t_ID from dir_cm032_dbt where t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        if ( ids.moveNext )
            tbbuf.rec.ID = ids.ID;
        else
            tbbuf.rec.ID = 0;
        end;
        if ( tbbuf.getEQ )
            m_cm032 = TRecHandler("ir_cm032");
            Copy(m_cm032, tbbuf);
        else
            m_cm032 = null;
        end;

        tbbuf = TBFile("ir_cm041");
        query = "SELECT t_ID from dir_cm041_dbt where t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        if ( ids.moveNext )
            tbbuf.rec.ID = ids.ID;
        else
            tbbuf.rec.ID = 0;
        end;
        if ( tbbuf.getEQ )
            m_cm041 = TRecHandler("ir_cm041");
            Copy(m_cm041, tbbuf);
        else
            m_cm041 = null;
        end;

        tbbuf = TBFile("ir_cm042");
        query = "SELECT t_ID from dir_cm042_dbt where t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        if ( ids.moveNext )
            tbbuf.rec.ID = ids.ID;
        else
            tbbuf.rec.ID = 0;
        end;
        if ( tbbuf.getEQ )
            m_cm042 = TRecHandler("ir_cm042");
            Copy(m_cm042, tbbuf);
        else
            m_cm042 = null;
        end;

        tbbuf = TBFile("ir_cm044");
        query = "SELECT t_ID from dir_cm044_dbt where t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        if ( ids.moveNext )
            tbbuf.rec.ID = ids.ID;
        else
            tbbuf.rec.ID = 0;
        end;
        if ( tbbuf.getEQ )
            m_cm044 = TRecHandler("ir_cm044");
            Copy(m_cm044, tbbuf);
        else
            m_cm044 = null;
        end;

        tbbuf = TBFile("ir_cm045");
        query = "SELECT t_ID from dir_cm045_dbt where t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        if ( ids.moveNext )
            tbbuf.rec.ID = ids.ID;
        else
            tbbuf.rec.ID = 0;
        end;
        if ( tbbuf.getEQ )
            m_cm045 = TRecHandler("ir_cm045");
            Copy(m_cm045, tbbuf);
        else
            m_cm045 = null;
        end;

        tbbuf = TBFile("ir_cm046");
        query = "SELECT t_ID from dir_cm046_dbt where t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        if ( ids.moveNext )
            tbbuf.rec.ID = ids.ID;
        else
            tbbuf.rec.ID = 0;
        end;
        if ( tbbuf.getEQ )
            m_cm046 = TRecHandler("ir_cm046");
            Copy(m_cm046, tbbuf);
        else
            m_cm046 = null;
        end;

        tbbuf = TBFile("ir_cm048");
        query = "SELECT t_ID from dir_cm048_dbt where t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        if ( ids.moveNext )
            tbbuf.rec.ID = ids.ID;
        else
            tbbuf.rec.ID = 0;
        end;
        if ( tbbuf.getEQ )
            m_cm048 = TRecHandler("ir_cm048");
            Copy(m_cm048, tbbuf);
        else
            m_cm048 = null;
        end;

        tbbuf = TBFile("ir_cm093");
        cmd = DL_RSDCommand();
        query = "SELECT t_ID cm93ID "
                  + "  FROM dir_cm093_dbt "
                  + " WHERE t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        while ( ids.moveNext )
            if ( ValType(m_cm093) == V_UNDEF )
                m_cm093 = TArray();
            end;
            var cm93Idx = m_Parties.size;
            m_cm093[cm93Idx] = TRecHandler("ir_cm093");
            tbbuf.rec.ID = ids.ptID;
            tbbuf.getEQ;
            Copy(m_Parties[cm93Idx], tbbuf);
        end;

        tbbuf = TBFile("ir_cm051");
        query = "SELECT t_ID from dir_cm051_dbt where t_InternalMessageID = " + InternalMessageID;
        ids = cmd.Execute(query);
        if ( ids.moveNext )
            tbbuf.rec.ID = ids.ID;
        else
            tbbuf.rec.ID = 0;
        end;
        if ( tbbuf.getEQ )
            m_cm051 = TRecHandler("ir_cm051");
            Copy(m_cm051, tbbuf);
        else
            m_cm051 = null;
        end;
        
        loaded = true;
    end;
    
    LoadRSM();
END;

PRIVATE MACRO InsertDifferences()
    var DiffCache = RsbSQLInsert("ir_differences.dbt");
    DiffCache.AddRecordArray(DifferencesArr);
    
    if ( not DiffCache.insert() )
        // error
    else
        DifferencesArr.size = 0;
    end;
END;

PRIVATE MACRO FillDifference( sourceMsg:RSM, DifferenceType, ElementName, BasePath, BaseValue, OtherValue, ReconType, DiffDescr, NeedCheckNONREF )
    var difference = TRecHandler("ir_differences");

    if   ( ReconType == "mfr" )
        difference.rec.DifferenceDescr = "Поле обязательной сверки: " + DiffDescr;
        difference.rec.Degree = RECON_DIFF_CRITICAL;
    elif ( ReconType == "sfr" )
        if ( NeedCheckNONREF == true )
            if ( (OtherValue != "NONREF") and (BaseValue != "NONREF") )
                difference.rec.DifferenceDescr = "Поле специальной сверки: " + DiffDescr;
                difference.rec.Degree = RECON_DIFF_CRITICAL;
            else
                return 0;
            end;
        else
            difference.rec.DifferenceDescr = "Поле специальной сверки: " + DiffDescr;
            difference.rec.Degree = RECON_DIFF_CRITICAL;
        end;
    else // afr
        difference.rec.DifferenceDescr = "Поле дополнительной сверки: " + DiffDescr;
        difference.rec.Degree = RECON_DIFF_NONCRITICAL;
    end;

    difference.rec.InternalMessageID = sourceMsg.GetInternalID();
    difference.rec.DifferenceType = DifferenceType;
    difference.rec.ElementName = ElementName;
    difference.rec.BasePath = BasePath;
    difference.rec.BaseValue = string(BaseValue);
    difference.rec.OtherValue = string(OtherValue);
    DifferencesArr[DifferencesArr.size] = difference;
    return difference.rec.Degree;
END;

PRIVATE MACRO CompareValues(template, msgIn:RSM, msgOut:RSM)
    var diffDegree = 0, maxDegree = 0;
    var bankSide = msgIn.GetBankSide();
    var ReconType = template.TypeReconciliation;

    var bankVal = msgOut.GetValue(template.Attribute),
        contrVal = msgIn.GetValue(template.Attribute);

    var i = 0;

    if ( contrVal != null ) // задно значение во входящем
        if ( bankVal != null ) // задано значение в исходящем
            if ( ValType(contrVal) == V_GENOBJ ) // если там массивы ? возможно нужно уточнять, если все таки не оба значения массивы
                i = 0;
                while ( i < contrVal.size )
                        if ( contrVal[i] != bankVal[i] )
                            diffDegree = FillDifference( msgIn, DIFFTYPE_VALUE, template.Name, template.PathMessage,
                                                        bankVal[i], contrVal[i], template.TypeReconciliation, 
                                                        DIFFDESC_DIFFERENT, true );
                            maxDegree = IIF(diffDegree > maxDegree, diffDegree, maxDegree);
                        end;
                    i = i + 1;
                end;
            else // не массивы
                if ( contrVal != bankVal )
                    diffDegree = FillDifference( msgIn, DIFFTYPE_VALUE, template.Name, template.PathMessage,
                                                 bankVal, contrVal, template.TypeReconciliation,
                                                 DIFFDESC_DIFFERENT, true );
                    maxDegree = IIF(diffDegree > maxDegree, diffDegree, maxDegree);
                end;
            end;
        else // в исходящем не задано
            if ( ValType(contrVal) == V_GENOBJ )
                i = 0;
                while ( i < contrVal.size )
                    diffDegree = FillDifference( msgIn, DIFFTYPE_NONCOMPLETE, template.Name, template.PathMessage,
                                                 "", contrVal[i], template.TypeReconciliation, 
                                                 DIFFDESC_NONCOMPLETE, false );
                    maxDegree = IIF(diffDegree > maxDegree, diffDegree, maxDegree);
                    i = i + 1;
                end;
            else // не массивы
                diffDegree = FillDifference( msgIn, DIFFTYPE_NONCOMPLETE, template.Name, template.PathMessage,
                                             "", contrVal, template.TypeReconciliation, 
                                             DIFFDESC_NONCOMPLETE, false );
                maxDegree = IIF(diffDegree > maxDegree, diffDegree, maxDegree);
            end;
        end;
    else // во входящем не задано
        if ( bankVal != null ) // задано значение в исходящем
            if ( ValType(bankVal) == V_GENOBJ )
                i = 0;
                while ( i < bankVal.size )
                    diffDegree = FillDifference( msgIn, DIFFTYPE_NONCOMPLETE, template.Name, template.PathMessage, 
                                                 bankVal[i], "", template.TypeReconciliation, 
                                                 DIFFDESC_NONCOMPLETE, false );
                    maxDegree = IIF(diffDegree > maxDegree, diffDegree, maxDegree);
                    i = i + 1;
                end;
            else // не массив
                diffDegree = FillDifference( msgIn, DIFFTYPE_NONCOMPLETE, template.Name, template.PathMessage, 
                                             bankVal, "", template.TypeReconciliation, 
                                             DIFFDESC_NONCOMPLETE, false );
                maxDegree = IIF(diffDegree > maxDegree, diffDegree, maxDegree);
            end;
        end;
    end;
    
    return maxDegree;
END;

// Сравнить значения атрибута во входящем и исходящем сообщениях
PRIVATE MACRO ReconcileAttribute(template, msgIn:RSM, msgOut:RSM)
    if ( (not msgOut.IsFullRecon()) and  (template.TypeReconciliation == "afr"))
        return 0; // пропускаем поля дополнительной сверки при сверке по основным полям (GENF)
    else
        return CompareValues(template, msgIn, msgOut);
    end;
END;

PRIVATE MACRO GetReplyAutogenerationMode(generateAutomatically:@variant)
    const regPath = "РЕПОЗИТАРИЙ\\АВТОМАТИЧЕСКАЯ ГЕНЕРАЦИЯ CM001";    
    var err;    
    generateAutomatically = false;  // по умолчанию
    GetRegistryValue( regPath, V_BOOL, @generateAutomatically, err );

    if ( err != 0 )
        MsgBox( "Ошибка при получении значения настройки \"" + RegPath + "\"" );
    // else           // 269432 10.01.2020 Судьяров
    //     if ( generateAutomatically == SET_CHAR )
    //         generateAutomaticallybool = true;
    //     else
    //         generateAutomaticallybool = false;
    //     end;
    end;
    return err;
END;

// Выполнение сверки
MACRO _ExecReconciliation(msgIDin:integer, msgIDout:integer, DealID, DocKind, OperID, OperDocKind, Date)
    var RSM_Out = RSM(msgIDout);        
    if ( RSM_Out.GetRStatus != 12 )
        ReconGenErrorCount = ReconGenErrorCount + 1;
        // delete recon message and grdocs and shit
        return;
    end;
    
    var RSM_In = RSM(msgIDin);

    var query = "SELECT * "
              + "  FROM dir_templatemessage_dbt "
              + " WHERE     t_Type = 'F' "
              + "       AND t_Attribute <> 'N' "
              + "       AND t_TypeReconciliation IN ('sfr', 'mfr', 'afr') "
              + "       AND t_MessageType = ? "
              + "       AND INSTR(LOWER(t_PathMessage), 'clientdetails') = 0 ";
    
    var cmd = DL_RSDCommand();
    cmd.addParam(RSM_In.GetMessageType);
    var TemplateLine = cmd.Execute(query);

    var i = 0;
    var maxDegree = 0, retDegree = 0;
    var in, ou;
    while ( TemplateLine.moveNext )
        retDegree = ReconcileAttribute(TemplateLine, RSM_In, RSM_Out);
        maxDegree = IIF(retDegree > maxDegree, retDegree, maxDegree);
        i = i + 1;
    end;

    var generateAutomatically;
    var stat = GetReplyAutogenerationMode(@generateAutomatically);
    
    if(not stat)
        RslDefCon.BeginTrans();
        RSM_In.UpdateGrDealAcc(DealID, DocKind, OperID, OperDocKind, Date);
        if   ( maxDegree == RECON_DIFF_CRITICAL ) // есть критические
            RSM_In.SetRStatus(10);  // Критические расхожденя сверки
            RSM_Out.SetRStatus(10); // Критические расхожденя сверки
        elif ( maxDegree == RECON_DIFF_NONCRITICAL ) // есть некритические или нет расхождений
            if ( generateAutomatically )
                RSM_In.SetRStatus(7);  // Обработано
                RSM_Out.SetRStatus(1); // Подготовлено
            else
                RSM_In.SetRStatus(11);  // Некритические расхожденя сверки
                RSM_Out.SetRStatus(11); // Некритические расхожденя сверки
            end;
        else // нет расхождений      
             RSM_In.SetRStatus(7);  // Обработано
             RSM_Out.SetRStatus(1); // Подготовлено
        end;
        RSM_In.UpdateGeneralInf();
        RSM_Out.UpdateGeneralInf();
        RslDefCon.CommitTrans()
    end;
    // ошибки в транзакции не произошло, увеличиваем счетчики
    if (maxDegree == RECON_DIFF_CRITICAL ) 
        CriticalDiffCount = CriticalDiffCount + 1;    
    elif ( maxDegree == RECON_DIFF_NONCRITICAL ) // есть некритические 
        NonCriticalDiffCount = NonCriticalDiffCount + 1;    
    else      // нет расхождений        
        ReconSuccessCount = ReconSuccessCount + 1;  
    end;
OnError(er)
  msgbox(er.module+ " "+er.line+" "+er.message);
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
    //InsertDifferences();
END;

PRIVATE MACRO GetReconEvent(graccID, ReconRequestID:@variant, forceConv:@integer, forceTempl:@integer)
    var eventQ = "SELECT gf.t_InternalMessageID,"
               + "       gf.t_Amendment, "
               + "       gf.t_XMLProduct, "
               + "       NVL(exst.t_TradeOblStatus, chr(1)) OblStatus "
               + "  FROM ddlgracc_dbt gcc, "
               + "       ddlgrdeal_dbt gdl, "
               + "       ddlgrdoc_dbt gdc, "
               + "       dir_generalinf_dbt gf, "
               + "       dir_cm093_dbt exst "
               + " WHERE     gcc.t_ID = " + graccID
               + "       AND gdl.t_ID = gcc.t_GrDealID "
               + "       AND gdc.t_GrDealID = gdl.t_ID "
               //+ "       AND gdc.t_ServDocKind = " + REPOS_INBOUNDMSG
               + "       AND gdc.t_DocKind = " + REPOS_RECONCILIATION
               + "       AND gf.t_InternalMessageID = gdc.t_DocID "
               + "       AND gf.t_RStatus = 9 "
               + "       AND exst.t_InternalMessageID(+) = gf.t_InternalMessageID "
               + " ORDER BY gf.t_PrDate DESC";
               
    var eventCmd = DL_RSDCommand();
    var eventData = eventCmd.Execute(eventQ);
    if ( eventData.moveNext )
        ReconRequestID = eventData.InternalMessageID;
    
        if   ( StrLwr(eventData.XMLProduct) == StrLwr("executionStatus") )
            forceConv = IR_REPACC_SVODSRS093;
            
            if   ( eventData.OblStatus == "T" )
                forceTempl = DLGR_TEMPL_CLOSECONTRWTHREQUEST;
            elif ( eventData.OblStatus == "C" )
                forceTempl = DLGR_TEMPL_EXECDELAYMSGWTHREQUEST;
            elif ( eventData.OblStatus == "TD" )
                forceTempl = DLGR_TEMPL_EARLYEXECMSGWTHREQUEST;
            elif ( eventData.OblStatus == "P" )
                forceTempl = DLGR_TEMPL_EXECHOLDMSGWTHREQUEST;
            elif ( eventData.OblStatus == "D" )
                forceTempl = DLGR_TEMPL_REJECTIONMSGWTHREQUEST;
            elif ( eventData.OblStatus == "SO" )
                forceTempl = DLGR_TEMPL_NETTINGSUSPWTHREQUEST;
            end;
        elif ( StrLwr(eventData.XMLProduct) == StrLwr("MasterAgreementTerms") )
            forceConv = IR_REPACC_SRS_GA;
            if ( eventData.Amendment != SET_CHAR )
                forceTempl = DLGR_TEMPL_MAKECONTRACTWTHREQUEST;
            else
                forceTempl = DLGR_TEMPL_CHANGEMSGWTHREQUEST;
            end;
        else
            forceConv = IR_REPACC_SRS;
            if ( eventData.Amendment != SET_CHAR )
                forceTempl = DLGR_TEMPL_MAKEDEALWTHREQUEST;
            else
                forceTempl = DLGR_TEMPL_CHANGEMSGWTHREQUEST;
            end;
        end;
    else
        forceTempl = 0;
    end;
END;

MACRO ExecReconciliation(ir_op_id) 
    var ir_op = TBFile("ir_op.dbt");
    ir_op.rec.ID = ir_op_id.ID;
    ir_op.getEQ;

    var subOp = TRecHandler("ir_op");
    subOp.Clear();
    subOp.rec.ID = ir_op.rec.ID;
    subOp.rec.DocKind = ir_op.rec.DocKind;
    subOp.rec.Date = ir_op.rec.Date;
    subOp.rec.Time = ir_op.rec.Time;
    subOp.rec.Protocol = UNSET_CHAR;
    subOp.rec.Sign_DealTransact = SET_CHAR;
    subOp.rec.Form_Contract = SET_CHAR;
    subOp.rec.First_Contract = SET_CHAR;
    subOp.rec.Change_Contract = SET_CHAR;
    subOp.rec.Change_Obligation = SET_CHAR;
    subOp.rec.Generate_GA = SET_CHAR;
    subOp.rec.Change_GA = SET_CHAR;
    var forceTempl = 0;
    var forceConv = 0;
    var ReconPairs = TArray();
    var msgPair:ReconPair;
    var cmd = DL_RSDCommand();
    var DataSet = cmd.Execute(GetRepAccObjectQueryRecon(ir_op));
    while ( DataSet.moveNext )
        subOp.rec.DocKind_op = DataSet.DocKind;
        subOp.rec.DealID = DataSet.DocID;
    
        msgPair = ReconPair();
        GetReconEvent(DataSet.ObjectID, @msgPair.MsgIn, @forceConv, @forceTempl);
        //forceTempl = 65;
        ExecMacroFile("ir_repacc.mac", "ExecuteRepAccFromRecon", ir_op, subOp, forceConv, forceTempl, @msgPair.MsgOut);
        
        msgPair.DealID = DataSet.DocID;
        msgPair.DocKind = DataSet.DocKind;
        ReconPairs[ReconPairs.size] = msgPair;
        ProcessedRMcount = ProcessedRMcount + 1;
    end;
    // loop recon pairs - exec recon
    var i = 0;
    while ( i < ReconPairs.size )
        _ExecReconciliation(ReconPairs[i].MsgIn, ReconPairs[i].MsgOut, ReconPairs[i].DealID, ReconPairs[i].DocKind, ir_op.rec.ID, ir_op.rec.DocKind, ir_op.rec.Date);
        // чистим старые расхождения
        var delCmd = RSDCommand("DELETE FROM dir_differences_dbt WHERE t_InternalMessageID = ?");  
        delCmd.addParam("", RSDBP_IN, + ReconPairs[i].MsgIn);
        delCmd.Execute();       

        i = i + 1;
    end;
     
    InsertDifferences();
    
    var protocolData = TArray();
    protocolData[protocolData.size] = CIR_RepAccRepParm(RECONCILIATION, "", "Всего обработано сообщений RM005", string(ProcessedRMcount), "", "");
    protocolData[protocolData.size] = CIR_RepAccRepParm(RECONCILIATION, "", "не обработано из-за ошибок генерации сверочного сообщения", string(ReconGenErrorCount), "", "");
    protocolData[protocolData.size] = CIR_RepAccRepParm(RECONCILIATION, "", "сверено успешно", string(ReconSuccessCount), "", "");
    protocolData[protocolData.size] = CIR_RepAccRepParm(RECONCILIATION, "", "с критическими расхождениями сверки", string(CriticalDiffCount), "", "");
    protocolData[protocolData.size] = CIR_RepAccRepParm(RECONCILIATION, "", "с некритическими расхождениями сверки", string(NonCriticalDiffCount), "", "");
    DL_SaveDataForReport_XML(ir_op.rec.ID, ir_op.rec.DocKind, protocolData, LOG_TYPE_DATA);

    return 0;            
END;

MACRO ExecReconFromSRS(ir_op_id): integer 
    var rslObj = CnvDataIROP(ir_op_id);
    ExecReconciliation(rslObj);
    return 0;  // 0, если все ОК
OnError(er)
    return 1;    // 1, если ошибка 
END;