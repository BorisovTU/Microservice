/*
    $Name:  ir_CM045.mac
    $Module:   Репозитарий
    $Description:  Класс для сообщения CM045
*/

import "ir_CMBase.mac";

class (CMBase) CM045 ( _isRecon, _conveyerID, _ir_op : object)
  InitCMBase();
  
  m_isRecon     = _isRecon;
  m_conveyerID  = _conveyerID;
  m_ir_op       = _ir_op;
  m_MessageID   = REPOS_CM045;

  macro addCMAll(ir_generalinf     ,
                 TradeRepository   ,
                 Party1            ,
                 Party2            ,
                 UTIGeneratingParty,
                 Sender            ,
                 cm                ,
                 Instruments       ,
                 GrDealID          ,
                 TemplNum          ,
                 DealParmForReport )
    addGenInf(ir_generalinf, TemplNum, DealParmForReport);
    addRepAccGrDoc(GrDealID);
    addcmGrDoc(GrDealID);
    addParty(TradeRepository, Party1, Party2, UTIGeneratingParty, Sender);
    addCM(cm);
    addInstrument(Instruments);
  end;

  macro insertAll
    var err = 0;
    err = insertGeneralinf;
    if (not err)
      err = insertParty;
    end;
    if (not err)
      err = insertDlgrdocCache;
    end;
    if (not err)
      err = insertCM;
    end;
    if (not err)
      err = insertInstrument;
    end;
    return err;
  end;

  macro ЗаполнениеCM (cm045 : @TrecHandler, 
                      DataSet, 
                      GenAgr,
                      FD,
                      first,
                      field,
                      Instruments : @TrecHandler, 
                      DealParmForReport)
    var stat = 0;

    if (first)
        //Instruments.clear();
        
        if( (FD.IsBuy() and (GenAgr.Side1 == 1)) or
            (FD.IsSale() and (GenAgr.Side1 == 2)) )
            cm045.rec.buyerid  = IR_PARTY_KIND_PARTY1;
            cm045.rec.buyer    = "Party1";
            cm045.rec.sellerid = IR_PARTY_KIND_PARTY2;
            cm045.rec.seller   = "Party2";
        else
            cm045.rec.buyerid  = IR_PARTY_KIND_PARTY2;
            cm045.rec.buyer    = "Party2";
            cm045.rec.sellerid = IR_PARTY_KIND_PARTY1;
            cm045.rec.seller   = "Party1";
        end;
     
        if( FD.Deal.rec.OptionStyle == DVSTYLE_AMERICAN )
            cm045.rec.CommencementDate = FD.ДатаНачала();
            cm045.rec.ExpirationDate = FD.ДатаИстеченияСрокаДействия();
            cm045.rec.OptionStyle = "A";
        else
            cm045.rec.ExpirationDate = FD.ДатаИсполнения();
            cm045.rec.OptionStyle = "E";
        end;

        if( FD.ПоставочныйКонтракт() )
            cm045.rec.SettlementTypeId = 2; // Physical
        else
            cm045.rec.SettlementTypeId = 1; // Cash
        end;

        if( FD.nFI_().rec.StdFIID == -1 )
            cm045.rec.NumberOfOptions = 1;
        else
            cm045.rec.NumberOfOptions = FD.nFI_().rec.ContrAmount;
        end;

        cm045.rec.PremiumPayerId    = cm045.rec.buyerid;
        cm045.rec.PremiumPayer      = cm045.rec.buyer;
        cm045.rec.PremiumReceiverId = cm045.rec.sellerid;
        cm045.rec.PremiumReceiver   = cm045.rec.seller;
        cm045.rec.OptionTypeId      = FD.Deal.rec.OptionType;
        cm045.rec.OptionStyleId     = FD.Deal.rec.OptionStyle;
        cm045.rec.PremiumCurrencyId = FD.Deal.rec.BonusFIID;
        cm045.rec.PremiumAmount     = FD.Deal.rec.Bonus;
        cm045.rec.OptionEntitlement = FD.nFI_().rec.Amount;
        cm045.rec.NominalCurrencyId = FD.nFI_().rec.PriceFIID;
        cm045.rec.NominalAmount     = cm045.rec.OptionEntitlement * cm045.rec.NumberOfOptions * FD.nFI_().rec.Price;
        cm045.rec.Strike            = FD.nFI_().rec.Price; // Страйк - это цена единицы базового актива, цена исполнения в опционном контракте
        cm045.rec.StrikeCurrencyId  = FD.nFI_().rec.PriceFIID;
        cm045.rec.OptionType        = substr( DL_GetNameAlg( 3423 /*ALG_FI_DVTYPE*/, cm045.rec.OptionTypeId ), 1, 4 );
        cm045.rec.SettlementType    = substr( DL_GetNameAlg( 7044 /*ALG_DV_OPTION_PAYKIND*/, cm045.rec.SettlementTypeId ), 1, 8 );
        cm045.rec.NominalCurrency   = GetFICode(cm045.rec.NominalCurrencyId, null, FICK_ISOSTRING);
        cm045.rec.PremiumCurrency   = GetFICode(cm045.rec.PremiumCurrencyId, null, FICK_ISOSTRING);
        cm045.rec.StrikeCurrency    = GetFICode(cm045.rec.StrikeCurrencyId, null, FICK_ISOSTRING);

        Instruments.rec.FIID = FD.BaseFI().rec.FIID;
        Instruments.rec.InstrumentCode = FD.BaseFI().rec.FI_Code;
        Instruments.rec.Instrumentname = FD.BaseFI().rec.Name;
    end;
    return stat;
  end;
end;
