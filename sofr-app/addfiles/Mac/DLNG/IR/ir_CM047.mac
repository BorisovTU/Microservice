/*
    $Name: ir_CM047.mac
    $Module:   Репозитарий
    $Description:  Анкета договора форварда на акции
*/

import "ir_CMBase.mac";

class (CMBase) CM047 ( _isRecon, _conveyerID, _ir_op : object)
  InitCMBase();
  
  m_isRecon     = _isRecon;
  m_conveyerID  = _conveyerID;
  m_ir_op       = _ir_op;
  m_MessageID   = REPOS_CM047;

  macro addCMAll(ir_generalinf     ,
                 TradeRepository   ,
                 Party1            ,
                 Party2            ,
                 UTIGeneratingParty,
                 Sender            ,
                 cm                ,
                 Instruments       ,
                 GrDealID          ,
                 TemplNum          ,
                 DealParmForReport )
    
    addGenInf(ir_generalinf, TemplNum, DealParmForReport);
    addRepAccGrDoc(GrDealID);
    addcmGrDoc(GrDealID);
    addParty(TradeRepository, Party1, Party2, UTIGeneratingParty, Sender);
    addCM(cm);
    addInstrument(Instruments);
  end;

  macro insertAll
    var err = 0;
    
    err = insertGeneralinf;
    if (not err)
      err = insertParty;
    end;
    if (not err)
      err = insertDlgrdocCache;
    end;
    if (not err)
      err = insertCM;
    end;
    if (not err)
      err = insertInstrument;
    end;
    return err;
  end;

  macro ЗаполнениеCM (cm047 : @TrecHandler, 
                      DataSet, 
                      GenAgr,
                      FD,
                      first,
                      field,
                      Instruments : @TrecHandler, 
                      DealParmForReport)
    var erFlag : bool = false;
    var bondFaceValue;
    var stat=0;
    
    if (first)
        //Instruments.clear();
        if( (FD.IsBuy() and (GenAgr.Side1 == 1)) or
            (FD.IsSale() and (GenAgr.Side1 == 2)) )
            cm047.rec.buyerid  = IR_PARTY_KIND_PARTY1;
            cm047.rec.buyer    = "Party1";
            cm047.rec.sellerid = IR_PARTY_KIND_PARTY2;
            cm047.rec.seller   = "Party2";
        else
            cm047.rec.buyerid  = IR_PARTY_KIND_PARTY2;
            cm047.rec.buyer    = "Party2";
            cm047.rec.sellerid = IR_PARTY_KIND_PARTY1;
            cm047.rec.seller   = "Party1";
        end;

        cm047.rec.OpenUnits = FD.nFI_BaseActiv.rec.Amount;
        if( FD.ДатаОплаты() != ZeroDate )
            cm047.rec.SettlmentDate = FD.ДатаОплаты();
        else
            cm047.rec.SettlmentDate = FD.ДатаИсполнения();
        end;
        cm047.rec.SettlCurrencyID = FD.ВалютаРасчётов();
        cm047.rec.SettlCurrency = GetFICode(cm047.rec.SettlCurrencyID, null, FICK_ISOSTRING);
        if( FD.ПоставочныйКонтракт() )
            cm047.rec.SettlementTypeId = 2; // Physical
        else
            cm047.rec.SettlementTypeId = 1; // Cash
        end;
        cm047.rec.SettlementType = substr( DL_GetNameAlg( 7044 /*ALG_DV_OPTION_PAYKIND*/, cm047.rec.SettlementTypeId ), 1, 8);
        cm047.rec.Price = FD.nFI_().rec.Price;
        cm047.rec.PriceCurrencyID = FD.nFI_().rec.PriceFIID;
        cm047.rec.PriceCurrency = GetFICode(cm047.rec.PriceCurrencyID, null, FICK_ISOSTRING);
        
        Instruments.rec.FIID = FD.BaseFI().rec.FIID;
        Instruments.rec.InstrumentName = FD.BaseFI().rec.Name;
        Instruments.rec.InstrumentCode = GetAvoirissCode(FD.BaseFI(), m_ir_op.rec.Date);
    end;

    return stat;
  end;
end;