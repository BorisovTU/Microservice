/*
$Name:        dl_elank.mac
$Module:      Ценные бумаги
$Description: Классы общие для работы с Электронной анкетой ФСФР
*/

/*
Программист      : Кучеров Владимир Николаевич                               
Создан           : 15.04.2013                                                
*/

import "DlRepForm_h.mac", "dlerr_log.mac", "dl_xml.mac";

private var _IsApp;

private const 
   TO_DBL=1,
   TO_INT=2,
   TO_STR=3;




const TableErrors = "┌────────────────────────────────────────────────────────────────────────────────────────────────────┐\n" +
                    "│                                          Ошибки выполнения                                         │\n" +
                    "├────────────────────────────────────────────────────────────────────────────────────────────────────┤";

var Errors = TArray();


class CNodeData(_cod,_value,_act)
var cod   = _cod,
    Value = _value,
    Act   = _act;
end;

class CStockData(_cod,_value,_act)
var cod   = _cod,
    Value = _value,
    Act   = _act;
end;



class(DL_XML) DL_ELANK_XML(repExcel : object)
  var Rep = repExcel;
  var ErrLog = ErrorLoger();
  var m_FileNode;
  var m_RepDate;
  var m_RepCalc;
  var m_CurrSign;
  var TegReport="",TegForm="",TegPack= "", Count = 0, УзелСтрока,УзелОтчет,  TegPartition = "";
  var xid = 0;

  MACRO CreateXML()
    var stat = CreateXML("utf-8");
    
    m_FileNode = null;

    return stat;
  END;

  macro PrintLog(err:object)
    var count = 0;
    while( count < err.Size )

      ErrLog.LogError( err[count] );
      count = count + 1;
    end;

    ErrLog.ShowErrLog(true);
  end;


  MACRO SaveXML()

    if(m_FileNode != null)
      AddMainNode(m_FileNode);

/*      PrintLog();
        RemoveEmpty();*/

      SaveXML();

      ErrLog.LogError("Сформирован файл " + GetFileName() + ".xml");
    end;
  END;

  MACRO СоздатьУзелПакет() 
    /*пространсто имен*/
    SetNameSpace("http://www.it.ru/Schemas/Avior/ПУРЦБ");
    xid = 1;
    return CreateNode(TegPack,
				"xmlns:xserializer","http://www.it.ru/Schemas/Avior/XSerializer",
				"xserializer:id","1",
				"externalId","34d72156-fa69-486c-8d7e-83470d43c580",
				"appVersion","2.16.2",
				"xmlns:xsi","http://www.w3.org/2001/XMLSchema-instance",
				"xsi:schemaLocation","http://www.it.ru/Schemas/Avior/ПУРЦБ ПУРЦБ.xsd"
                                 );
  end;
  macro getXid()
    return (string(xid= xid+1));
  end;

  private macro PeriodName(Num)
    var ret = string(num); //1  2  3  4
    if (num == 2)  //ый ой ий ый
      ret =  ret+"ой";
    elif(num == 3)
      ret =  ret+ "ий";
    else
      ret = ret+ "ый";
    end;  
    return (ret + " квартал");
  end;

  MACRO СоздатьУзелКвартал(Period)
    var УзелКвартал =  CreateNode("av:Квартал", "xserializer:id",getXid());

    УзелКвартал.appendChild(CreateElementValue("av:Код",Period));
    УзелКвартал.appendChild(CreateElementValue("av:Описание",PeriodName(Period)));
    УзелКвартал.appendChild(CreateElementValue("av:Активно","true"));

    return (УзелКвартал);
  End;

  MACRO СоздатьУзелName(Name,vObj)
    var УзелName =  CreateNode(Name, "xserializer:id",getXid());

    УзелName.appendChild(CreateElementValue("av:Код",vObj.cod));
    УзелName.appendChild(CreateElementValue("av:Описание",vObj.value));
    УзелName.appendChild(CreateElementValue("av:Активно",vObj.act));

    return (УзелName);
  End;

  MACRO ДобавитьЗначениеУзлаName(УзелName)
    УзелСтрока.appendChild(УзелName);
  End;


  MACRO СоздатьУзелОтчет()
    УзелОтчет =  CreateNode(TegReport, "xserializer:id",getXid());

    Count = 0;
    return (УзелОтчет);
  end;

  MACRO СоздатьУзелРаздел()
    var УзелРаздел =  CreateNode(TegPartition, "xserializer:id", getXid());

    УзелРаздел.appendChild(СоздатьУзелОтчет());

    return (УзелРаздел);
  end;
  

  MACRO СоздатьУзелФорма(Period, Year)
    var УзелФорма =  CreateNode(TegForm, "xserializer:id",getXid());
    УзелФорма.appendChild(CreateElementValue("av:Год",Year));
    УзелФорма.appendChild(СоздатьУзелКвартал(Period));

    if(TegPartition != "")
       УзелФорма.appendChild(СоздатьУзелРаздел());
    else
       УзелФорма.appendChild(СоздатьУзелОтчет());
  end;

    return (УзелФорма);
  end;


  MACRO ДобавитьЗначениеУзлаСтрокиОтчета(ColName, ColValue)
    УзелСтрока.appendChild(CreateElementValue(ColName,ColValue));
  End;


  MACRO СоздатьУзелСтрокиОтчета(tegName)
    
    Count = Count +1;
    УзелСтрока =  CreateNode(TegReport+"_"+Count);
    ДобавитьЗначениеУзлаСтрокиОтчета("av:Col01", Count);

  end;


  MACRO СохранитьУзелСтрокиОтчета()
    //Получить отчет
    if(УзелОтчет != null)
      УзелОтчет.appendChild(УзелСтрока);
    end;

    УзелСтрока = null;

  End;

  macro CreateInstance(_trep, _tForm, _tPack, Period, Year, _tPartition )
    var stat=0,УзелПакет;

    TegReport = _trep;
    TegForm   = _tForm;
    TegPack   = _tPack;

    if(ValType(_tPartition) != V_UNDEF)
       TegPartition = _tPartition;
    end;

    ErrLog.InitErrLog( Rep, true );

    //При формировании в XML будем отдельно вести протокол в Excel, куда запишем ошибки и имена сформированных файлов
    Rep.GetCurSheet().SheetName = "Сообщения";
    ErrLog.StartErrLog();

    stat = CreateXML();	

    if (stat)
      m_FileNode = СоздатьУзелПакет();
    end;
    m_FileNode.appendChild(СоздатьУзелФорма(Period, Year));
  end;

  macro DisposeInstance()

    SaveXML();

    //Напечатать протокол
    ErrLog.ShowErrLog(true);
    Rep.SetZoomType( ZOOM_TYPE_A4L );// альбомная ориентация
    Rep.PrintWinRep();
    Rep.ShowWinRep();
  end;

end;


class  CVisitorXML(_form,_objXml,_ObjReport)
  private var objXml:Object = _objXml;
  private var ObjReport = _ObjReport;
  const RoundN = 5;   /*точность округления*/


  macro ErrorLog( msg:string )
    objXml.ErrLog.LogError(msg);
  end;

  macro PrintTableLine( /*...*/)
     VAR i = 1, CellName, CellValue, CellPoint, CellFormat, FldNumber, CurFldNumber = 0;

     objXml.СоздатьУзелСтрокиОтчета();

     while( GetParm( i, CellName) AND GetParm( i+1, CellValue) AND GetParm( i+2, CellPoint))

        if ( (CellValue != "") and ((CellValue != " ")) and (CellValue != NULL) and (CellValue != "Undefined") )
          objXml.ДобавитьЗначениеУзлаСтрокиОтчета( CellName, CellValue, CellFormat, CellPoint );
        end;

        CellValue = NULL;
        i = i + 3;
     end;
    
     objXml.СохранитьУзелСтрокиОтчета(); 
  end;

  macro ExecuteReport( Data ) /*перекрыть*/
  end;
end;
                            
CLASS (CMakeReport) CVisitorExcel(_Form,_objReport,_TabHead)
  
  private var TabHead=_TabHead,objReport = _objReport;
  var m_Form=_form;
  const RoundN = 5;   /*точность округления*/


   /*Печать суммы в тыс. руб.*/
  macro PrintSum( Sum )
      if(Sum)
         if( _IsApp == true )  
            return ДЛ_ЧислоCЗаданнойТочностью( double(Sum)/1000, RoundN, "a" );
         else 
            return ДЛ_ЧислоCЗаданнойТочностью( double(Sum)/1000, RoundN );
         end;
      else
         return "-";
      end;    
  end;


  macro PrintAmountSum( Sum, _SumPrecision:@integer )

    /*КОЛ-ВО может быть дробным (паи, ГО)*/
    _SumPrecision = DL_MeanSignAfterPoint(Sum,12);

    if(Sum)
       if( _IsApp )  
          return ДЛ_ЧислоCЗаданнойТочностью( double(Sum), _SumPrecision, "a" );
       else 
          return ДЛ_ЧислоCЗаданнойТочностью( double(Sum), _SumPrecision );
       end;
    else
       return "-";
    end;    
  end;


  macro sAddCell(CellVal, dbl, IsSubHead, frm:string, pnt )

     CellVal=StrSubst(CellVal, "'", "");

     if(string(CellVal))
        if(dbl==TO_DBL)
           if( double(CellVal) == 0.0 )
              if( _IsApp )
                 AddPrintCell(0, 0, 0, "a:c");
              else
                 AddPrintCell(0, 0, 0, "c");
              end;
           else
              AddPrintCell(double(CellVal),   0, pnt, frm);         
           end;
        elif(dbl==TO_STR)
           AddPrintCell(CellVal,           0, 0,   frm);
        end; 
     else
        if(IsSubHead)
           CellVal=" - ";
        else
           CellVal=" ";
        end;

        AddPrintCell(CellVal, 0, 0, frm);
     end;

  end;

  macro PrintTableLine( /*...CellName CellValue CellSub */)
     VAR i = 1, CellName, CellValue, CellIsSub, CellPoint, CellFormat, FldNumber, CurFldNumber = 0;

     var formStr:string;
     var SumPrec = 0;
     
       if( _IsApp )
        formStr="z:a:c";      
       else
        formStr="z:c";
       end;


     while( GetParm( i,   CellName)     AND 
            GetParm( i+1, CellValue)    AND 
            GetParm( i+2, CellIsSub)     
          )

        if (i == 1)
          sAddCell(CellValue,   TO_STR,  false, "c");
        elif (i == 4)
          sAddCell(CellValue,   TO_STR,  CellIsSub, "l");
        else

          if( CellValue AND (ValType(CellValue) != V_STRING) )
             CellValue = PrintAmountSum(CellValue,@SumPrec);
             sAddCell(CellValue,      TO_DBL,  CellIsSub, formStr, SumPrec );
          else
             sAddCell(CellValue,      TO_STR,  CellIsSub, formStr);
          end;
        end;

        SumPrec = 0;
        CellValue = NULL;
        i = i + 3;
     end;

     AddStr();
    
  end;

  macro PrintLog()
  var count = 0;
      AddNewSheetBreak("Ошибки",TableErrors);
      while( count < Errors.Size )

         AddPrintCell( Errors[count] , 0, 0, "l");
         AddStr();

         count = count + 1;
      end;
     AddEmptyStr();
  end;

  macro ErrorLog( msg:string )
    Errors[Errors.Size]  = msg;

  end;


  MACRO ExecuteReport( Data )


  END;

  initCMakeReport(TabHead);
END;


class ADataSource(_form)  /*Подготовка данных и выпуск отчета*/
  var Data:object, m_Form = _form;

  macro ExecuteReport(visitor)
     visitor.ReInitObject(m_form);/*переинитим объект для того, чтобы каждый раз печать была в отдельном отчете (иначе все лепится в один отчет и происходит дублирование вкладок по названиям)*/
     visitor.ExecuteReport( Data );
  end;

  macro Create() /*переркрыть в каждом конкретном класе отчетов*/
  end;

end;


class (DL_CPanel) DL_XML_CPanel( Form, TableHeader:STRING,_fldExcel,_fldXml)
  private var isRuning = false, isNextShow=false;
  private var pnfld_Excel=_fldExcel,pnfld_XML=_fldXml;

  private macro ShowForm(_showform)
    isNextShow = _showform;
    return (isNextShow);
  end;
  
  macro NewRuning()
    isRuning = false;
  end;

  macro IsExcel()
    var retVal = GetFieldValue(pnfld_Excel); /*присвоить из формы*/

    if (retVal)
      ShowForm(retVal);
    end;

    return (retVal);
  end;

  macro IsXML()
    var retVal;

    if( pnfld_XML != NULL )
       retVal = GetFieldValue(pnfld_XML); /*присвоить из формы*/
    else
       retVal = UNSET_CHAR;/*нет поля xml*/
    end;

    if (retVal)
      ShowForm(retVal);
    end;
    return (retVal);
  end;


  macro Run()   //пергруженная работа для XML чтобы не запускало форму несколько раз из под Template

   if (not IsRuning)
     IsRuning = true;
     return (ShowForm(Run()));
   end;
   if (isNextShow)
     ShowForm(false);
     return (true)
   end;
   return (isNextShow);
  end;

  initDL_CPanel( Form, TableHeader );
  NewRuning();
end;


class CApp(_Form:Object,_objDs:Object,_execExcel:Object,_execXML:Object)
 var m_Form    = _Form;
 var objDS     = _objDS; /*ADataSource*/
 var execExcel = _execExcel;
 var execXML   = _execXML;

 macro Run()
   var obj,stat= true;

   while (stat)

     stat = m_Form.Run();
     if (not stat)
       break;
     end; 

     objDS.Create();

     if (m_form.IsExcel() )
       objDS.ExecuteReport(execExcel);
     end;

     if (m_form.IsXML() and (execXML != NULL) )
       objDS.ExecuteReport(execXML);
     end;

     m_Form.NewRuning();
   end;
   return 0; 
 end;

end;

