/*
$Name:        DLRenumberingDealsStart.mac
$Module:      Ядро Securities
$Description: Запуск выполнения операции перенумерации сделок
*/
import DVInter, "dllog_xml.mac", "dlreport.mac", "dldlngfun.mac";

//класс с данными для строки протокола
class RenumberingDealsRepParm( _NewCode:string,
                               _OldCode:string,
                               _ExtCode:string,
                               _Date:date,   
                               _Time:time,
                               _ID:integer,         
                               _Module:string,
                               _KindOper:integer
                             )
  var NewCode  = _NewCode;
  var OldCode  = _OldCode;
  var ExtCode  = _ExtCode;
  var DDate    = _Date;
  var DTime    = _Time;
  var ID       = _ID;
  var Module   = _Module;
  var KindOper = _KindOper;
end;

//класс с данными для списка ошибок в протоколе
private class RenumberingDealsRepErr( _ErrCode:integer, _ErrStr:string )
 var ErrCode  = _ErrCode;
 var ErrStr   = _ErrStr;
end;

/*лог ошибок*/
PRIVATE CLASS (DL_LOG_FROM_XML) SC_ERROR_LOG_FROM_XML( pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool )

  macro GetDataFromXMLByDocKind()
     var ErrorData:RenumberingDealsRepErr;

     if( m_ReportVersion == 1 )

        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRCODE", V_STRING, ErrorData.ErrCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRSTR", V_STRING, ErrorData.ErrStr) == false)
        end;
        
        return ErrorData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

/*лог ошибок СО*/
PRIVATE CLASS (DL_LOG_DATA_XML) SC_ERROR_LOG_DATA_XML( pDocKind:integer, pDocumentID:integer, pDataParms:TArray, pType:integer, pVersion:integer )

  PRIVATE MACRO СоздатьУзелОшибок( i )
     var УзелОшибок = null;

     if( m_Version == 1 )
        УзелОшибок = CreateElement("Property",
                                   "ErrCode",   m_DataParms[i].ErrCode, 
                                   "ErrStr",    m_DataParms[i].ErrStr
                                  );
     else
        УзелОшибок = CreateElement("Property");
     end;
     
     return УзелОшибок;
  END;


  initDL_LOG_DATA_XML(pDocKind, pDocumentID, pDataParms, pType, pVersion);
END;

CLASS (DL_LOG_FROM_XML) OFFBALTR_LOG_FROM_XML( pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool )

  macro GetDataFromXMLByDocKind()

     var DealsData:RenumberingDealsRepParm;

     if( m_ReportVersion == 1 )
     
        if(DL_ReadTagAttribut(m_child_ReportNumber, "NEWCODE", V_STRING, DealsData.NewCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "OLDCODE", V_STRING, DealsData.OldCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "EXTCODE", V_STRING, DealsData.ExtCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "DATE", V_DATE, DealsData.DDate) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "TIME", V_TIME, DealsData.DTime) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ID", V_INTEGER, DealsData.ID) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "MODULE", V_STRING, DealsData.Module) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "KINDOPER", V_INTEGER, DealsData.KindOper) == false)
        end;

        return DealsData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

PRIVATE MACRO GetLogDataXML( DocKind:integer, DocumentID:integer, Type:integer, OnlyLastReport:bool )
   var xml_obj = null;

   if( Type == LOG_TYPE_DATA )
      xml_obj = OFFBALTR_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   elif(Type == LOG_TYPE_ERROR )
      xml_obj = SC_ERROR_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   end;

   return xml_obj;
END;

/*печать отчета по данным xml*/
MACRO PrintRenumberingDealsByXML( comm )
  var ReportLineData_XML:variant;
  var IsExistsDeals = false, IsExistsErr = false;
  var HeadPrinted = false;
  var DealsData:RenumberingDealsRepParm;
  var ErrData:RenumberingDealsRepErr;
  record DlComm("dl_comm");
  var ReportFileName, errtext, errcode;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/
  var stat = 0;

  SetBuff(DlComm, comm);

  ReportFileName = GetTxtFileName( "renumdeals_" + DlComm.CommCode );
  if( not Open( ReportOutFile, ReportFileName ) )
     errcode = status(errtext);
     RunError( "Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext );
  end;
  SetOutput( ReportFileName );

  println("");
  println(" Протокол процедуры перенумерации сделок");
  println(" Дата " + string(DlComm.CommDate));
  
  //таблица сделок
  ReportLineData_XML = GetLogDataXML(DlComm.DocKind, DlComm.DocumentID, LOG_TYPE_DATA, true);
  ReportLineData_XML.SetReportNumber(0);

  while( ReportLineData_XML.Next )

     if(not HeadPrinted)
        println("┌──────────────────────┬──────────────────────┬──────────────────────┬─────────────┬──────────────┬────────────────────┬───────────────────────┬────────────────────────────────────────────────────┐");
        println("│ Сформированный номер │ Старый номер         │ Внешний номер        │ Дата сделки │ Время сделки │ id сделки          │ Модуль                │ Вид сделки                                         │");
        println("├──────────────────────┼──────────────────────┼──────────────────────┼─────────────┼──────────────┼────────────────────┼───────────────────────┼────────────────────────────────────────────────────┤");

        HeadPrinted = true;
     end;

     DealsData = ReportLineData_XML.GetReportLineData();
     if (DealsData.Module == "Производные инструменты")
       DealsData.Module = "ФИССиКО"
     end;
                [│######################│######################│######################│#############│##############│####################│#######################│####################################################│]
                 (DealsData.NewCode, DealsData.OldCode, DealsData.ExtCode, DealsData.DDate, IIF(DealsData.DTime == time(0,0,0), "", DealsData.DTime), DealsData.ID, DealsData.Module, DL_GetKindOperName(DealsData.KindOper));

     IsExistsDeals = true;
  end;

  if(HeadPrinted)
        println("└──────────────────────┴──────────────────────┴──────────────────────┴─────────────┴──────────────┴────────────────────┴───────────────────────┴────────────────────────────────────────────────────┘");
        println("");
  end;

  //таблица ошибок
  ReportLineData_XML = GetLogDataXML(DlComm.DocKind, DlComm.DocumentID, LOG_TYPE_ERROR, true);
  ReportLineData_XML.SetReportNumber(0);
  
  stat = ReportLineData_XML.Next();
  if( (stat) or (IsExistsDeals == false) )

    println("");
    println("┌──────────┬──────────────────────────────────────────────────────────────┐");
    println("│№ ошибки  │Сообщение                                                     │");
    println("├──────────┼──────────────────────────────────────────────────────────────┤");

    HeadPrinted = true;

    if(not stat)
            [│##########│##############################################################│]
            ("", "", "Нет подходящих сделок");
    else
      while( stat )
        ErrData = ReportLineData_XML.GetReportLineData();
            [│##########│##############################################################│]
            (ErrData.ErrCode, ErrData.ErrStr:w);

        IsExistsErr = true;
        stat = ReportLineData_XML.Next();
      end;
    end;

    println("└──────────┴──────────────────────────────────────────────────────────────┘");
    println("");
  else
    println("Операция завершена успешно");
    println("");
  end;

  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );

  return true;
END;

private var RepErrArr = TArray();

private macro SaveErrForReport_XML( DocumentID:integer, DocKind:integer )

  var RepXML = "";

  if( RepErrArr.Size > 0 )
     RepXML = SC_ERROR_LOG_DATA_XML(DocKind, DocumentID, RepErrArr, LOG_TYPE_ERROR).GetDataXML();
  end;

  if( RepXML != "" )
     var Header = DL_LOG_HEADER_XML(DocKind).GetHeaderXML();

     DL_InsertHeaderLog(DocumentID, DocKind, {Oper}, Header);
     DL_InsertLogXMLData( DocumentID, DocKind, LOG_TYPE_ERROR, {Oper}, RepXML);
  end;

end;

macro RenumberingDeals_MsgBox( StrErr )
  RepErrArr[RepErrArr.size] = RenumberingDealsRepErr(1, StrErr);
end;

private macro LZ( num, len )
  var str1, len1;

  str1 = trim(string(num));
  len1 = strlen(str1);

  if( len1 >= len )
     return str1;
  else
     return mkstr("0", len-len1) + str1;
  end;
end;

private macro CreateCode( CommDate:Date, Num:integer ):string
  var day, month, year;
  
  DateSplit(CommDate, day, month, year);

  return LZ(year, 4) + LZ(month, 2) + LZ(day, 2) + "/" + LZ(Num, 6);
end;

private macro UpdateTick( NewCode:string, ID:integer )
   var exec = RSDCommand( " UPDATE ddl_tick_dbt " +
                          "    SET t_DealCode = ? " +
                          "  WHERE t_DealID   = ? " );
   exec.addParam("", RSDBP_IN, substr(NewCode, 1, 30));
   exec.addParam("", RSDBP_IN, ID);
   exec.execute();
end;

private macro UpdateNptxop( NewCode:string, ID:integer )
   var exec = RSDCommand( " UPDATE dnptxop_dbt " +
                          "    SET t_Code = ? " +
                          "  WHERE t_ID   = ? " );
   exec.addParam("", RSDBP_IN, substr(NewCode, 1, 25));
   exec.addParam("", RSDBP_IN, ID);
   exec.execute();
end;

private macro UpdatePmGroundByNptxop( ID:integer, SubKind:integer, NewCode:string )
   var v_pm = Trechandler("pmpaym.dbt");
   var v_dt = Trechandler("pmprop.dbt");
   var v_ct = Trechandler("pmprop.dbt");
   var v_rm = Trechandler("pmrmprop.dbt");
   var Платеж = NULL;

   v_pm.clear();
   if( (FindPayment(null, BAi, 0, DL_WRTMONEY, ID, true, v_pm, v_dt, v_ct, v_rm) == 0) and (v_pm.rec.PaymentID > 0) )
      Платеж = RsbPayment(v_pm.rec.PaymentID);
      Платеж.Ground = DL_GetNameAlg(7334/*ALG_NPTX_WRTKINDS*/, SubKind) + " денежных средств по операции № " + NewCode;
      Платеж.Update();
   end;
end;

private macro UpdateDvdeal( NewCode:string, ID:integer )
   var exec = RSDCommand( " UPDATE ddvdeal_dbt " +
                          "    SET t_Code = ? " +
                          "  WHERE t_ID   = ? " );
   exec.addParam("", RSDBP_IN, substr(NewCode, 1, 30));
   exec.addParam("", RSDBP_IN, ID);
   exec.execute();
end;

private macro UpdateDvndeal( NewCode:string, ID:integer )
   var exec = RSDCommand( " UPDATE ddvndeal_dbt " +
                          "    SET t_Code = ? " +
                          "  WHERE t_ID   = ? " );
   exec.addParam("", RSDBP_IN, substr(NewCode, 1, 30));
   exec.addParam("", RSDBP_IN, ID);
   exec.execute();
end;

private macro UpdatePmGroundByDeal( Kind:integer, ID:integer, NewCode:string )
   var v_pm = Trechandler("pmpaym.dbt");
   var v_dt = Trechandler("pmprop.dbt");
   var v_ct = Trechandler("pmprop.dbt");
   var v_rm = Trechandler("pmrmprop.dbt");
   var Платеж = NULL;

   v_pm.clear();
   if( (FindPayment(null, BAi, 0, Kind, ID, true, v_pm, v_dt, v_ct, v_rm) == 0) and (v_pm.rec.PaymentID > 0) )
      Платеж = RsbPayment(v_pm.rec.PaymentID);
      Платеж.Ground = "Платеж по базовому активу по сделке № " + NewCode;
      Платеж.Update();
   end;

   v_pm.clear();
   if( (FindPayment(null, BRi, 0, Kind, ID, true, v_pm, v_dt, v_ct, v_rm) == 0) and (v_pm.rec.PaymentID > 0) )
      Платеж = RsbPayment(v_pm.rec.PaymentID);
      Платеж.Ground = "Платеж по базовому активу в обратной сделке по сделке № " + NewCode;
      Платеж.Update();
   end;

   v_pm.clear();
   if( (FindPayment(null, CAi, 0, Kind, ID, true, v_pm, v_dt, v_ct, v_rm) == 0) and (v_pm.rec.PaymentID > 0) )
      Платеж = RsbPayment(v_pm.rec.PaymentID);
      Платеж.Ground = "Платеж по контрактиву по сделке № " + NewCode;
      Платеж.Update();
   end;

   v_pm.clear();
   if( (FindPayment(null, CRi, 0, Kind, ID, true, v_pm, v_dt, v_ct, v_rm) == 0) and (v_pm.rec.PaymentID > 0) )
      Платеж = RsbPayment(v_pm.rec.PaymentID);
      Платеж.Ground = "Платеж по контрактиву в обратной сделке по сделке № " + NewCode;
      Платеж.Update();
   end;
end;

private macro UpdateDlcomm( NewCode:string, ID:integer )
   var exec = RSDCommand( " UPDATE ddl_comm_dbt " +
                          "    SET t_CommCode = ? " +
                          "  WHERE t_DocumentID = ? " );
   exec.addParam("", RSDBP_IN, substr(NewCode, 1, 30));
   exec.addParam("", RSDBP_IN, ID);
   exec.execute();
end;

// выполнение
macro StartRenumberingDeals( _DocumentID ):integer
  var err = 0;
  var dl_comm = TBFile("dl_comm.dbt");
  var cmd, DataSet, cmd2, DataSet2, exec;
  var NeedRenumber:bool = false;
  var Num:integer = 0, NewCode:string = "", OldCode:string = "", ExtCode:string = "", ID:integer = 0, Module:string = "", KindOper:integer = 0;
  var RepDataArr = TArray();

  RepDataArr.size = 0;

  dl_comm.rec.DocumentID = _DocumentID;
  if( dl_comm.GetEQ() )

     cmd = RSDCommand( " select t_CalcState " +
                       "   from DDLDATECALC_DBT " +
                       "  where t_Date = ? " );

     cmd.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
     cmd.execute();

     DataSet = TRsbDataSet(cmd);
     if( DataSet.moveNext() )
        if( DataSet.CalcState == 1 )
            NeedRenumber = false;
        elif( DataSet.CalcState == 2 )
            err = 1;
            MsgBox("Перенумерация за дату уже выполняется");
            NeedRenumber = false;
        elif( DataSet.CalcState == 0 )
            NeedRenumber = true;
        end;
     else
        NeedRenumber = false;
     end;
     
     if( NeedRenumber )

        ReplaceMacro("MsgBox", "RenumberingDeals_MsgBox");

        exec = RSDCommand( " UPDATE DDLDATECALC_DBT " +
                           "    SET t_CalcState = 2 " +
                           "  WHERE t_Date = ? " );

        exec.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
        exec.execute();

        // Выполнить перенумерацию ГО и ИН
        cmd = RSDCommand( " select Com.* " +
                          "   from ddl_comm_dbt Com " +
                          "  where Com.t_DocKind in(?, ?) " +
                          "    and Com.t_CommDate   = ? " +
                          "    and Com.t_CommStatus = ? " +
                          " Order By Com.t_DocumentID " );

        cmd.addParam("", RSDBP_IN, DL_ISSUE_UNION);
        cmd.addParam("", RSDBP_IN, DL_CHGAVRNOM);
        cmd.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
        cmd.addParam("", RSDBP_IN, DL_COMM_PREPARING);
        cmd.execute();

        DataSet = TRsbDataSet(cmd);
        while( DataSet.moveNext() )
           Num      = Num + 1;
           NewCode  = CreateCode(dl_comm.rec.CommDate, Num);
           OldCode  = DataSet.CommCode;
           ExtCode  = "";
           ID       = DataSet.DocumentID;
           Module   = "Ценные бумаги";
           KindOper = DataSet.OperationKind;

           cmd2 = RSDCommand( " select t_DocumentID, t_CommStatus " +
                              "   from ddl_comm_dbt " +
                              "  where t_CommCode    = ? " +
                              "    and t_DocumentID != ? " +
                              "    and t_DocKind     = ? "
                            );

           cmd2.addParam("", RSDBP_IN, NewCode);
           cmd2.addParam("", RSDBP_IN, DataSet.DocumentID);
           cmd2.addParam("", RSDBP_IN, DataSet.DocKind);
           cmd2.execute();

           DataSet2 = TRsbDataSet(cmd2);
           if( DataSet2.moveNext() )
              if( DataSet2.CommStatus > DL_COMM_PREPARING )
                 MsgBox("Уже существует операция с кодом <" + NewCode + ">, по которой начато исполнение");
                 continue;
              else
                 UpdateDlcomm(DL_GetGUID(), DataSet2.DocumentID);
              end;
           end;

           UpdateDlcomm(NewCode, DataSet.DocumentID);
           RepDataArr[RepDataArr.size] = RenumberingDealsRepParm(NewCode, OldCode, ExtCode, dl_comm.rec.CommDate, time(0,0,0), ID, Module, KindOper);
        end;

        //Выполнить перенумерацию погашений
        cmd = RSDCommand( " select Tick.* " +
                          "   from ddl_tick_dbt Tick " +
                          "  where Tick.t_BOfficeKind = ? " +
                          "    and Tick.t_DealDate    = ? " +
                          "    and Tick.t_DealStatus  = ? " +
                          " Order By lpad(Tick.t_DealCodeTS, 30, '0'), Tick.t_DealID " );

        cmd.addParam("", RSDBP_IN, DL_RETIREMENT);
        cmd.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
        cmd.addParam("", RSDBP_IN, DL_PREPARING);
        cmd.execute();

        DataSet = TRsbDataSet(cmd);
        while( DataSet.moveNext() )
           Num      = Num + 1;
           NewCode  = CreateCode(dl_comm.rec.CommDate, Num);
           OldCode  = DataSet.DealCode;
           ExtCode  = DataSet.DealCodeTS;
           ID       = DataSet.DealID;
           Module   = "Ценные бумаги";
           KindOper = DataSet.DealType;

           cmd2 = RSDCommand( " select t_DealID, t_DealStatus " +
                              "   from ddl_tick_dbt " +
                              "  where t_DealCode    = ? " +
                              "    and t_DealID     != ? " +
                              "    and t_BOfficeKind = ? "
                            );

           cmd2.addParam("", RSDBP_IN, NewCode);
           cmd2.addParam("", RSDBP_IN, DataSet.DealID);
           cmd2.addParam("", RSDBP_IN, DataSet.BOfficeKind);
           cmd2.execute();

           DataSet2 = TRsbDataSet(cmd2);
           if( DataSet2.moveNext() )
              if( DataSet2.DealStatus > DL_PREPARING )
                 MsgBox("Уже существует операция с кодом <" + NewCode + ">, по которой начато исполнение");
                 continue;
              else
                 UpdateTick(DL_GetGUID(), DataSet2.DealID);
              end;
           end;

           UpdateTick(NewCode, DataSet.DealID);
           RepDataArr[RepDataArr.size] = RenumberingDealsRepParm(NewCode, OldCode, ExtCode, dl_comm.rec.CommDate, time(0,0,0), ID, Module, KindOper);
        end;

        // Выполнить перенумерацию следующих операций:
        //   Сделки покупки и продажи ц/б
        //   Операции конвертации АДР в акции в БО ЦБ
        //   Операции приёма денежных средств от клиентов БО ЦБ и БО ПИ
        //   Операции списания/зачисления ценных бумаг в БО ЦБ
        //   Биржевые и внебиржевые сделки БО ПИ
        //   Передача клиентских ЦБ в пул
        cmd = RSDCommand( " select 'Tick' as Tabl, Tick.t_DealTime as Time, lpad(Tick.t_DealCodeTS, 30, '0') as SortCode, Tick.T_DealCodeTS as ExtCode, Tick.T_DealCode as OldCode, Tick.T_DealID as ID, " +
                          "        'Ценные бумаги' as Module, Tick.T_DealType as KindOper, Tick.t_BOfficeKind as BOfficeKind, 0 as SubKind " +
                          "   from ddl_tick_dbt Tick " +
                          "  where Tick.t_BOfficeKind = ? " +
                          "    and Tick.t_DealDate    = ? " +
                          "    and Tick.t_DealStatus  = ? " +
                          "    and ((Tick.t_ParentID <= 0) or " +
                          "         ((Tick.t_ParentID > 0) and not exists ( select nDeal.* from ddvndeal_dbt nDeal where nDeal.t_ID = Tick.t_ParentID and nDeal.t_ExtCode = Tick.t_DealCodeTS ))" +
                          "        )"
                          " UNION ALL " +
                          " select 'Tick' as Tabl, Leg.t_SupplyTime as Time, lpad(Tick.t_DealCodeTS, 30, '0') as SortCode, Tick.T_DealCodeTS as ExtCode, Tick.T_DealCode as OldCode, Tick.T_DealID as ID, " +
                          "        'Ценные бумаги' as Module, Tick.T_DealType as KindOper, Tick.t_BOfficeKind as BOfficeKind, 0 as SubKind " +
                          "   from ddl_tick_dbt Tick, ddl_leg_dbt Leg " +
                          "  where Tick.T_DealID      = Leg.T_DealID " +
                          "    and Leg.t_LegKind      = 0 " +
                          "    and Tick.t_BOfficeKind = ? " +
                          "    and Tick.t_DealDate    = ? " +
                          "    and Tick.t_DealStatus  = ? " +
                          " UNION ALL " +
                          " select 'nptxop' as Tabl, nptxop.t_Time as Time, lpad(nptxop.t_Code, 30, '0') as SortCode, '' as ExtCode, nptxop.t_Code as OldCode, nptxop.T_ID as ID, " +
                          "        (case when exists ( SELECT dc.t_PartyID " +
                          "                              FROM dsfcontr_dbt dc " +
                          "                             WHERE dc.t_PartyID = nptxop.t_Client " +
                          "                               AND dc.t_ID = nptxop.t_Contract" 
                          "                               AND dc.T_Servkind = ? " +
                          "                          ) then 'Ценные бумаги' else 'ФИССиКО' end) as Module, nptxop.T_Kind_Operation as KindOper, nptxop.t_DocKind as BOfficeKind, nptxop.t_SubKind_Operation as SubKind " +
                          "   from dnptxop_dbt nptxop " +
                          "  where nptxop.t_DocKind  = ? " +
                          "    and nptxop.t_OperDate = ? " +
                          "    and nptxop.t_Status   = ? " +
                          " UNION ALL " +
                          " select 'Tick' as Tabl, Tick.t_DealTime as Time, lpad(Tick.t_DealCode, 30, '0') as SortCode, Tick.T_DealCodeTS as ExtCode, Tick.T_DealCode as OldCode, Tick.T_DealID as ID, " +
                          "        'Ценные бумаги' as Module, Tick.T_DealType as KindOper, Tick.t_BOfficeKind as BOfficeKind, 0 as SubKind " +
                          "   from ddl_tick_dbt Tick " +
                          "  where Tick.t_BOfficeKind = ? " +
                          "    and Tick.t_DealDate    = ? " +
                          "    and Tick.t_DealStatus  = ? " +
                          " UNION ALL " +
                          " select 'dvdeal' as Tabl, Deal.t_Time as Time, lpad(Deal.T_ExtCode, 30, '0') as SortCode, Deal.T_ExtCode as ExtCode, Deal.T_Code as OldCode, Deal.T_ID as ID, " +
                          "        'ФИССиКО' as Module, Deal.t_Kind as KindOper, ? as BOfficeKind, 0 as SubKind " +
                          "   from ddvdeal_dbt Deal " +
                          "  where Deal.t_Date  = ? " +
                          "    and Deal.t_State = ? " +
                          " UNION ALL " +
                          " select 'dvndeal' as Tabl, NDeal.t_Time as Time, lpad(NDeal.T_ExtCode, 30, '0') as SortCode, NDeal.T_ExtCode as ExtCode, NDeal.T_Code as OldCode, NDeal.T_ID as ID, " +
                          "        'ФИССиКО' as Module, NDeal.t_Kind as KindOper, NDeal.t_DVKind as BOfficeKind, 0 as SubKind " +
                          "   from ddvndeal_dbt NDeal " +
                          "  where NDeal.t_Date  = ? " +
                          "    and NDeal.t_DocKind in("+string(DL_DVNDEAL)+","+string(DL_DVDEALT3)+")"+
                          "    and NDeal.t_State = ? " +
                          " UNION ALL " +
                          " select 'dlcomm' as Tabl, comm.t_CommTime as Time, lpad(comm.T_CommCode, 30, '0') as SortCode, '' as ExtCode, comm.T_CommCode as OldCode, comm.T_DocumentID as ID, " +
                          "        'Ценные бумаги' as Module, comm.t_operationkind as KindOper, " + DL_SCINOUTPOOL + " as BOfficeKind, 0 as SubKind " +
                          "   from ddl_comm_dbt comm " +
                          "  where comm.t_CommDate  = ? " +
                          "    and comm.t_CommStatus = ? " +
                          "    and comm.t_dockind = " + DL_SCINOUTPOOL +
                          "    and comm.t_ClientID > 0 " + 
                          " Order By Time, SortCode, ID " );

        cmd.addParam("", RSDBP_IN, DL_SECURITYDOC);
        cmd.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
        cmd.addParam("", RSDBP_IN, DL_PREPARING);
        cmd.addParam("", RSDBP_IN, DL_CONVAVR);
        cmd.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
        cmd.addParam("", RSDBP_IN, DL_PREPARING);
        cmd.addParam("", RSDBP_IN, PTSK_STOCKDL);
        cmd.addParam("", RSDBP_IN, DL_WRTMONEY);
        cmd.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
        cmd.addParam("", RSDBP_IN, DL_TXOP_Prep);
        cmd.addParam("", RSDBP_IN, DL_AVRWRT);
        cmd.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
        cmd.addParam("", RSDBP_IN, DL_PREPARING);
        cmd.addParam("", RSDBP_IN, DL_DVDEAL);
        cmd.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
        cmd.addParam("", RSDBP_IN, DVDEAL_PREP);
        cmd.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
        cmd.addParam("", RSDBP_IN, DVDEAL_PREP);
        cmd.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
        cmd.addParam("", RSDBP_IN, DL_COMM_PREPARING);
        cmd.execute();

        DataSet = TRsbDataSet(cmd);
        while( DataSet.moveNext() )
           Num      = Num + 1;
           NewCode  = CreateCode(dl_comm.rec.CommDate, Num);

          if (DataSet.Tabl == "dlcomm")

            cmd2 = RSDCommand(" select t_DocumentID, t_CommStatus " +
                              "   from ddl_comm_dbt " +
                              "  where t_CommCode    = ? " +
                              "    and t_DocumentID != ? " +
                              "    and t_DocKind     = ? ");

            cmd2.addParam("", RSDBP_IN, NewCode);
            cmd2.addParam("", RSDBP_IN, DataSet.ID);
            cmd2.addParam("", RSDBP_IN, DataSet.BOfficeKind);
            cmd2.execute();

            DataSet2 = TRsbDataSet(cmd2);
            
            if( DataSet2.moveNext() )
              if( DataSet2.DealStatus > DL_COMM_PREPARING )
                MsgBox("Уже существует операция с кодом <" + NewCode + ">, по которой начато исполнение");
                continue;
              else
                UpdateDlcomm(DL_GetGUID(), DataSet2.DocumentID);
              end;
            end;

            UpdateDlcomm(NewCode, DataSet.ID);
            RepDataArr[RepDataArr.size] = RenumberingDealsRepParm(NewCode, DataSet.OldCode, DataSet.ExtCode, dl_comm.rec.CommDate, DataSet.Time, DataSet.ID, DataSet.Module, DataSet.KindOper);

           elif( DataSet.Tabl == "Tick" )

              cmd2 = RSDCommand( " select t_DealID, t_DealStatus " +
                                 "   from ddl_tick_dbt " +
                                 "  where t_DealCode    = ? " +
                                 "    and t_DealID     != ? " +
                                 "    and t_BOfficeKind = ? "
                               );

              cmd2.addParam("", RSDBP_IN, NewCode);
              cmd2.addParam("", RSDBP_IN, DataSet.ID);
              cmd2.addParam("", RSDBP_IN, DataSet.BOfficeKind);
              cmd2.execute();

              DataSet2 = TRsbDataSet(cmd2);
              if( DataSet2.moveNext() )
                 if( DataSet2.DealStatus > DL_PREPARING )
                    MsgBox("Уже существует операция с кодом <" + NewCode + ">, по которой начато исполнение");
                    continue;
                 else
                    UpdateTick(DL_GetGUID(), DataSet2.DealID);
                 end;
              end;

              UpdateTick(NewCode, DataSet.ID);
              RepDataArr[RepDataArr.size] = RenumberingDealsRepParm(NewCode, DataSet.OldCode, DataSet.ExtCode, dl_comm.rec.CommDate, DataSet.Time, DataSet.ID, DataSet.Module, DataSet.KindOper);

           elif( DataSet.Tabl == "nptxop" )

              cmd2 = RSDCommand( " select t_ID, t_Status " +
                                 "   from dnptxop_dbt " +
                                 "  where t_Code    = ? " +
                                 "    and t_ID     != ? " +
                                 "    and t_DocKind = ? "
                               );

              cmd2.addParam("", RSDBP_IN, NewCode);
              cmd2.addParam("", RSDBP_IN, DataSet.ID);
              cmd2.addParam("", RSDBP_IN, DataSet.BOfficeKind);
              cmd2.execute();

              DataSet2 = TRsbDataSet(cmd2);
              if( DataSet2.moveNext() )
                 if( DataSet2.Status > DL_TXOP_Prep )
                    MsgBox("Уже существует операция с кодом <" + NewCode + ">, по которой начато исполнение");
                    continue;
                 else
                    UpdateNptxop(DL_GetGUID(), DataSet2.ID);
                 end;
              end;

              UpdateNptxop(NewCode, DataSet.ID);
              UpdatePmGroundByNptxop(DataSet.ID, DataSet.SubKind, NewCode);
              RepDataArr[RepDataArr.size] = RenumberingDealsRepParm(NewCode, DataSet.OldCode, DataSet.ExtCode, dl_comm.rec.CommDate, DataSet.Time, DataSet.ID, DataSet.Module, DataSet.KindOper);

           elif( DataSet.Tabl == "dvdeal" )

              cmd2 = RSDCommand( " select t_ID, t_State " +
                                 "   from ddvdeal_dbt " +
                                 "  where t_Code  = ? " +
                                 "    and t_ID   != ? "
                               );

              cmd2.addParam("", RSDBP_IN, NewCode);
              cmd2.addParam("", RSDBP_IN, DataSet.ID);
              cmd2.execute();

              DataSet2 = TRsbDataSet(cmd2);
              if( DataSet2.moveNext() )
                 if( DataSet2.State > DVDEAL_PREP )
                    MsgBox("Уже существует операция с кодом <" + NewCode + ">, по которой начато исполнение");
                    continue;
                 else
                    UpdateDvdeal(DL_GetGUID(), DataSet2.ID);
                 end;
              end;

              UpdateDvdeal(NewCode, DataSet.ID);
              RepDataArr[RepDataArr.size] = RenumberingDealsRepParm(NewCode, DataSet.OldCode, DataSet.ExtCode, dl_comm.rec.CommDate, DataSet.Time, DataSet.ID, DataSet.Module, DataSet.KindOper);
           elif( DataSet.Tabl == "dvndeal" )

              cmd2 = RSDCommand( " select t_ID, t_State " +
                                 "   from ddvndeal_dbt " +
                                 "  where t_Code  = ? " +
                                 "    and t_ID   != ? "
                               );

              cmd2.addParam("", RSDBP_IN, NewCode);
              cmd2.addParam("", RSDBP_IN, DataSet.ID);
              cmd2.execute();

              DataSet2 = TRsbDataSet(cmd2);
              if( DataSet2.moveNext() )
                 if( DataSet2.State > DVDEAL_PREP )
                    MsgBox("Уже существует операция с кодом <" + NewCode + ">, по которой начато исполнение");
                    continue;
                 else
                    UpdateDvndeal(DL_GetGUID(), DataSet2.ID);
                 end;
              end;

              UpdateDvndeal(NewCode, DataSet.ID);
              UpdatePmGroundByDeal(DL_DVNDEAL, DataSet.ID, NewCode);
              RepDataArr[RepDataArr.size] = RenumberingDealsRepParm(NewCode, DataSet.OldCode, DataSet.ExtCode, dl_comm.rec.CommDate, DataSet.Time, DataSet.ID, DataSet.Module, DataSet.KindOper);
           end;
        end;

        exec = RSDCommand( " UPDATE DDLDATECALC_DBT " +
                           "    SET t_CalcState = 1 " +
                           "  WHERE t_Date = ? " +
                           "    AND t_CalcState = 2 " );

        exec.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
        exec.execute();

        DL_SaveDataForReport_XML(dl_comm.rec.DocumentID, dl_comm.rec.DocKind, RepDataArr, LOG_TYPE_DATA);
        SaveErrForReport_XML(dl_comm.rec.DocumentID, dl_comm.rec.DocKind);

        ReplaceMacro("MsgBox", NULL);
     end;
  else
     MsgBox("Ошибка поиска сервисной операции");
     err = 1; 
  end;

 return err;

OnError( er )
  if( NeedRenumber )
     exec = RSDCommand( " UPDATE DDLDATECALC_DBT " +
                        "    SET t_CalcState = 0 " +
                        "  WHERE t_Date = ? " +
                        "    AND t_CalcState = 2 " );

     exec.addParam("", RSDBP_IN, dl_comm.rec.CommDate);
     exec.execute();
  end;

  DL_SaveDataForReport_XML(dl_comm.rec.DocumentID, dl_comm.rec.DocKind, RepDataArr, LOG_TYPE_DATA);
  RepErrArr[RepErrArr.size] = RenumberingDealsRepErr(1, er.message);
  SaveErrForReport_XML( dl_comm.rec.DocumentID, dl_comm.rec.DocKind );
  return 1;
end;
