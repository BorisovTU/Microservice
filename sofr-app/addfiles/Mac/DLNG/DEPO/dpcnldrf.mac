/*
$Name:         dpcnldrf.mac
$Module:       Депозитарий
$Description:  Протокол выполнения информационной операции "Отмена ранее зарегистрированного поручения"
*/

import "dpstdrep.mac";

private const SC_SPDRAFT_STATUS_UNREG = -1;
private const SC_SPDRAFT_STATUS_CLOSE = 3;

private class (DL_CReportTemplate) CClntDrftRep(SPLogOprID, BriefCode, Name, OpenDate, ShortName, OwnerCode, UseApp)
    private var m_SPLogOprID = SPLogOprID, m_BriefCode = BriefCode, m_Name = Name, m_OpenDate = OpenDate, m_ShortName = ShortName, m_OwnerCode = OwnerCode;
    private var m_DataExist = true, m_NoDataMsg = "";

    macro CReport_DataExist()
        return m_DataExist;
    end;

    macro CReport_NoDataMsg()
        return MsgBox(m_NoDataMsg);
    end;

    private macro Create(NumCopy)
        var query = "", cmd = null, ds = null;
        var note = "", rej = null;

        query = " SELECT "
                  + " q.* "
                 + " ,MAX(q.t_Accepted) OVER () AS t_ExistAccepted "
                 + " ,MAX(q.t_Accept) OVER () AS t_ExistAccept "
                 + " ,DECODE(SUM(q.t_Rejected) OVER (), q.t_Count, 1, 0) AS t_AllRejected "
                 + " ,MAX(q.t_Cancelled) OVER () AS t_ExistCancelled "
              + " FROM "
                  + " (SELECT "
                       + " spground.t_AltXld "
                      + " ,spground.t_SignedDate "
                      + " ,spground.t_Xld "
                      + " ,spground.t_RegistrDate "
                      + " ,spinfopr.t_FromXid "
                      + " ,spinfopr.t_FromDate "
                      + " ,spinfopr.t_ToXid "
                      + " ,spinfopr.t_ToDate "
                      + " ,COUNT(1) OVER () AS t_Count "
                      + " ,ROWNUM AS t_Number "
                      + " ,NVL2(spdraft.t_SPGroundID, 1, 0) AS t_Exist "
                      + " ,CASE "
                           + " WHEN NVL(spdraft.t_Status, " + SC_SPDRAFT_STATUS_UNREG + ") = " + SC_SPDRAFT_STATUS_CLOSE + " "
                            + " AND NVL(spdraft.t_OperState, -1) = " + DP_INVSTATUSORDER_ACCEPT + " "
                           + " THEN 1 "
                           + " ELSE 0 "
                       + " END AS t_Accepted "
                      + " ,CASE "
                           + " WHEN NVL(spdraft.t_Status, " + SC_SPDRAFT_STATUS_UNREG + ") <> " + SC_SPDRAFT_STATUS_CLOSE + " "
                            + " AND NVL(spdraft.t_OperState, -1) = " + DP_INVSTATUSORDER_ACCEPT + " "
                           + " THEN 1 "
                           + " ELSE 0 "
                       + " END AS t_Accept "
                      + " ,CASE "
                           + " WHEN NVL(spdraft.t_Status, " + SC_SPDRAFT_STATUS_UNREG + ") = " + SC_SPDRAFT_STATUS_CLOSE + " "
                            + " AND NVL(spdraft.t_OperState, -1) = " + DP_INVSTATUSORDER_REJECT + " "
                           + " THEN 1 "
                           + " ELSE 0 "
                       + " END AS t_Rejected "
                      + " ,(SELECT "
                            + " COUNT(1) "
                        + " FROM "
                            + " DUAL "
                        + " WHERE "
                            + " EXISTS (SELECT "
                                        + " 1 "
                                    + " FROM "
                                        + " dsplogopr_dbt splogopr3 "
                                       + " ,dspinfopr_dbt spinfopr3 "
                                    + " WHERE "
                                        + " splogopr3.t_State = " + SC_SPDRAFT_STATUS_CLOSE + " "
                                    + " AND splogopr3.t_OperState = " + DP_INVSTATUSORDER_ACCEPT + " "
                                    + " AND splogopr3.t_AutoKey <> splogopr.t_AutoKey "
                                    + " AND splogopr3.t_RecID = spinfopr3.t_SPInfDocID "
                                    + " AND spinfopr3.t_DocType = spinfopr.t_DocType "
                                    + " AND spinfopr3.t_Desc = spinfopr.t_Desc)) AS t_Cancelled "
                   + " FROM "
                       + " dspdraft_dbt spdraft "
                      + " ,dspground_dbt spground "
                      + " ,dspinfopr_dbt spinfopr "
                      + " ,dsplogopr_dbt splogopr "
                   + " WHERE "
                       + " spdraft.t_SPGroundID(+) = spinfopr.t_Desc "
                   + " AND spground.t_SPGroundID = splogopr.t_Draft "
                   + " AND spinfopr.t_SPInfDocID = splogopr.t_RecID "
                   + " AND splogopr.t_AutoKey = ?) q ";

        cmd = RSDCommand(query);
        cmd.AddParam("", RSDBP_IN, m_SPLogOprID);
        cmd.NullConversion = true;
        cmd.Execute();

        ds = TRsbDataSet(cmd);
        ds.MoveNext();

        if((ds.Exist == 1) and (ds.ExistAccept == 1))
            m_DataExist = false;
            m_NoDataMsg = "Для исполнения информационной операции \"Отмена ранее зарегистрированного поручения\" необходимо зарегистрировать отказ|"
                        + "в исполнении инвентарной операции № " + ds.ToXid + " от " + date_as_string(1, ds.ToDate, false);
        else
            if(ds.Exist == 1)
                if(ds.ExistAccepted == 1)
                    note = "Отменяемое поручение № " + ds.FromXid + " от " + date_as_string(1, ds.FromDate, false) + " исполнено" + IIF(ds.Count == 1, "", "/исполнено частично") + ", "
                         + "отмена поручения № " + ds.FromXid + " от " + date_as_string(1, ds.FromDate, false) + " невозможна";
                    GetRejectNoteDR(m_SPLogOprID, DP_INFOPR_OPERATION, note, rej);
                elif(ds.AllRejected == 1)
                    if(ds.ExistCancelled == 1)
                        note = "Отменяемое поручение № " + ds.FromXid + " от " + date_as_string(1, ds.FromDate, false) + " отменено ранее, "
                             + "отмена поручения № " + ds.FromXid + " от " + date_as_string(1, ds.FromDate, false) + " невозможна";
                        GetRejectNoteDR(m_SPLogOprID, DP_INFOPR_OPERATION, note, rej);
                    else
                        note = "Поручение на отмену ранее зарегистрированного поручения исполнено";
                    end;
                end;
            else
                note = "Не найдена инвентарная операция, созданная на основании поручения " + ds.FromXid + " от " + date_as_string(1, ds.FromDate, false);
            end;

            CreateTotalBook();
            SetActiveSheet("Протокол");

            DP_StdHeaderLO_Ex(null, null, "Протокол операции отмены ранее зарегистрированного поручения", m_SPLogOprID, 0, false, {CurDate}, {CurDate}, 0, null, null, this, "N1_");
            CopyAllSheetInTotalBook(null, false, "N1_TableHeader", 1);

            RegisterTable("N1_MainTable", null,
                          "N1_A1", "N1_A2", "N1_A3", "N1_A4", "N1_A5", "N1_A6", "N1_A7",
                          "N1_A8", "N1_A9", "N1_A10", "N1_A11", "N1_A12", "N1_A13", "N1_A14");

            PrintTableLine("N1_A1", ds.AltXld, null,
                           "N1_A2", Date(ds.SignedDate), null,
                           "N1_A3", ds.Xld, null,
                           "N1_A4", Date(ds.RegistrDate), null,
                           "N1_A5", ds.FromXid, null,
                           "N1_A6", Date(ds.FromDate), null,
                           "N1_A7", ds.ToXid, null,
                           "N1_A8", Date(ds.ToDate), null,
                           "N1_A9", m_BriefCode, null,
                           "N1_A10", m_Name, null,
                           "N1_A11", m_OpenDate, null,
                           "N1_A12", m_ShortName, null,
                           "N1_A13", m_OwnerCode, null,
                           "N1_A14", note, null);

            EndTable();

            CopyAllSheetInTotalBook(null, false, GetTableRange(), 0);

            DP_StdAuthPerson_Ex(null, null, this, "N1_");
            DP_StdDeposInfo_Ex(null, null, 0, this, "N1_");
            DP_StdExecReport_Ex(null, null, NULL, NULL, 0, this, "N1_");

            SaveTotalBook();
        end;
    end;

    macro Run()
        var err = 0;

        Run();

        if(not m_DataExist)
            err = 1;
        end;

        return err;
    end;

    initDL_CReportTemplate(null, "dpcnldrf.xls");
    SetPrintMode(DL_OUTREPORT_EXCEL);
    SetUseApp(UseApp);
end;

macro ClntDrftExecRep(SPLogOprID, BriefCode, Name, OpenDate, ShortName, OwnerCode, UseApp)
    var err = 0;

    err = CClntDrftRep(SPLogOprID, BriefCode, Name, OpenDate, ShortName, OwnerCode, UseApp).Run();

    return err;
end;