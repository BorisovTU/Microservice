// ---------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank v6
//                 Copyright (c) R-Style SoftWare Lab  2009
//
//  File Name    : dpacntopr.mac
//  Description  : "Отчет об операциях по счету депо"
//  Programmer   : Nikonorov Evgeny
//  Created      : 19.05.2009
// ---------------------------------------------------------------------------
import DPInter, SfInter, "or_rep_h.mac", "globals.mac", deposerv;

private var depoacnt;

private var depo_acc;
private var LogOprID;
private var use_ap;

private var  dprt_num,
             beg_date,
             end_date,
             ToExcel;

private var LogoprRecipientName;
private var LogoprInitiatorName;

private const SIDEBALANCE_ACTIVE  = 1, /* Активные счета Депо */
              SIDEBALANCE_PASSIVE = 2; /* Пассивные счета Депо */

private var CountPrintDoc = 0;


private var HeadTable = "┌──────────┬───────┬────────────┬────────────────┬─────────────┬────────────┬───────────────┬────────────┬───────────────┬────────────────┬───────────────┐\n"+
                        "│   Дата   │ Номер │    Тип     │  Наименование  │  Номер гос. │ Количество │   Основание   │   Место    │    Счет по    │    Счет по     │ Номер и дата  │\n"+
                        "│          │       │  операции  │      ц/б       │ регистрации │            │   операции    │  хранения  │ дебету/раздел │ кредиту/раздел │ вх. документа │\n"+
                        "├──────────┼───────┼────────────┼────────────────┼─────────────┼────────────┼───────────────┼────────────┼───────────────┼────────────────┼───────────────┤";


private var tablewidth = Index(HeadTable, "\n");
private var Rep = CMakeReport(HeadTable);
private const RepFontStyleTitel =  "ex_FS()";

private macro GetAppDocs(draftid)
   var query, DataSet, docs = "";
   var Select;

   query =  " select vl.t_name, gr.t_altxld, gr.t_signeddate "
          + "   from dspground_dbt gr, dspgrdoc_dbt grd, dspdraft_dbt dr, dllvalues_dbt vl "
          + "  where dr.t_autokey = ? "
          + "    and grd.t_sourcedocid = dr.t_autokey "
          + "    and grd.t_sourcedockind = dr.t_kind "
          + "    and gr.t_spgroundid = grd.t_spgroundid "
          + "    and gr.t_Direction <> " + EXITING 
          + "    and vl.t_element = gr.t_kind"
          + "    and vl.t_list = " + string(OBJTYPE_KINDDOC);

   Select = RSDCommand( query );

   Select.AddParam("DraftID", RSDBP_IN, draftid );  /*1*/

   Select.Execute();                                                               
                                                                                   
   DataSet = TRSBDataSet(Select);
   while(DataSet.MoveNext())
     docs = docs + string(DataSet.name, " №", DataSet.altxld, " от ", SQL_ConvTypeDate(DataSet.signeddate), "\n");
   end;
   return docs;
end;

private macro GetStorage(draftid)
  var query, DataSet, Select;
  var StorageName = "-";
  
  query =   " select pt.t_Name " 
          + "   from dpmprop_dbt pmprop, dspdrmove_dbt spdrmove, ddepoacnt_dbt acnt, dparty_dbt pt " 
          + "  where spdrmove.t_draftid = ? /*1*/ "
          + "    and pmprop.t_payfiid <> 0 " 
          + "    and pmprop.t_paymentid = spdrmove.t_paymentid " 
          + "    and acnt.t_briefcode = pmprop.t_ourcorracc "
          + "    and pt.t_PartyID = acnt.t_Owner";

  Select = RSDCommand( query );

  Select.AddParam("DraftID", RSDBP_IN, draftid );  /*1*/

  Select.Execute();
  
  DataSet = TRsbDataSet(Select);
  if(DataSet.movenext())
    StorageName = DataSet.Name;
  end;

  return StorageName;
end;

private macro GetAccStr(draftid, isPayer)
  var query, DataSet, Select;
  var AccStr = "";

  query =   " select acnt.t_BriefCode as AcntPartCode, root.t_BriefCode as AcntRootCode "
          + "   from ddepoacnt_dbt acnt, ddepoacnt_dbt root, dspdrmove_dbt mv, dpmpaym_dbt pm "
          + "  where mv.t_DraftID = ? /*1*/ "
          + "    and pm.t_PaymentID = mv.t_PaymentID ";
  if(isPayer)
    query = query + " and acnt.t_AutoKey = pm.t_PayerDpNode ";
  else
    query = query + " and acnt.t_AutoKey = pm.t_ReceiverDpNode ";
  end;

  query = query + " and root.t_AutoKey = acnt.t_Root";      

  Select = RSDCommand( query );

  Select.AddParam("DraftID", RSDBP_IN, draftid );  /*1*/

  Select.Execute();
  
  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())
    AccStr = string(DataSet.AcntRootCode, "/", DataSet.AcntPartCode); 
  end;

  return AccStr;
end;

macro GetLogoprSubjectsName()
  var query, DataSet, Select;
  LogoprRecipientName = "-";
  LogoprInitiatorName = "-";
  
  if( LogOprID )
    query =   " select NVL((case when spgr.t_PartyName = CHR(1) then pt.t_Name else spgr.t_PartyName end), '-') as RecipientName, logopr.t_InitiatorName "
            + "   from dspground_dbt spgr, dparty_dbt pt, dsplogopr_dbt logopr "
            + "  where logopr.t_AutoKey = ? /*1*/ "
            + "    and spgr.t_SourceDocID = logopr.t_AutoKey "
            + "    and spgr.t_SourceDocKind = 830 "
            + "    and spgr.t_Direction = " + EXITING
            + "    and spgr.t_Department = logopr.t_Department "
            + "    and pt.t_PartyID (+)= spgr.t_Party";

    Select = RSDCommand( query );

    Select.AddParam("LogOprID", RSDBP_IN, LogOprID );  /*1*/

    Select.Execute();
    
    DataSet = TRsbDataSet(Select);
    if(DataSet.moveNext())
      LogoprRecipientName = string(DataSet.RecipientName);
      LogoprInitiatorName = string(DataSet.InitiatorName); 
    end;
  end;
end;

private macro GetDepoContr(Code, PartyID);
  var query, DataSet, Select;
  var ContrNumber = "";

  query =  " select t_Number, t_DateBegin from dsfcontr_dbt "
         + "  where t_ObjectType = " + SF_DEPOACC
         + "    and t_ServKind = " + PTSK_DEPOS
         + "    and t_Object = ? /*1*/"
         + "    and t_PartyID = ? /*2*/";
  
  Select = RSDCommand( query );

  Select.AddParam("BriefCode", RSDBP_IN, Code );  /*1*/
  Select.AddParam("PartyID", RSDBP_IN, PartyID );  /*2*/

  Select.Execute();
  
  DataSet = TRsbDataSet(Select);
  
  if (DataSet.movenext())
    ContrNumber =  string(DataSet.Number, " от ", string(SQL_ConvTypeDate(DataSet.Datebegin):f)); /*FIV Добавил дату договора*/
  end;

  return ContrNumber;
end;

//печатать отчет по текущему счету depoacnt
private macro PrintRepAcnt()
  var Progress = CProgressBar;
  var i = 0;
  var query_select = "", query_count = "", query_where = "";
  var Select, Select_Count;
  var draft;
  var cnt = 0;

  //отбираем проводки по счету
  query_select = " select draft.t_AutoKey as DraftID, draft.t_Date, draft.t_oprxid, oprk.t_Name as OprName, fin.t_Name as FIName, "
               + "        avr.t_LSIN, acctrn.t_Sum_Payer as Sum, fin.t_Point, spgr.t_Xld, spgr.t_SignedDate ";

  query_count = " select count(1) as cnt ";
           
  query_where = "   from dspdraft_dbt draft, doprdocs_dbt oprdocs, dacctrn_dbt acctrn, doproper_dbt oproper, "
              + "        dspground_dbt spgr, dfininstr_dbt fin, doprkoper_dbt oprk, davoiriss_dbt avr "
              + "  where (   acctrn.t_Account_Payer    in ( select acc1.t_account from daccount_dbt acc1 where acc1.t_deporoot = ? /*1*/) "
              + "         OR acctrn.t_Account_Receiver in ( select acc2.t_account from daccount_dbt acc2 where acc2.t_deporoot = ? /*2*/) "
              + "        ) "
              + "    and t_date_carry <= ? /*3*/ " 
              + "    and t_date_carry >= ? /*4*/ "
              + "    and acctrn.t_State = 1 "/*фактическая проводка*/
              + "    and oprdocs.t_AccTrnID = acctrn.t_AccTrnID "
              + "    and oprdocs.t_DocKind = 1 "
              + "    and oproper.t_ID_Operation = oprdocs.t_ID_Operation " 
              + "    and draft.t_AutoKey = oproper.t_DocumentID "
              + "    and spgr.t_SPgroundID = draft.t_SPgroundID "
              + "    and fin.t_FIID = acctrn.t_FIID_Payer "
              + "    and avr.t_FIID = fin.t_FIID "
              + "    and oprk.t_Kind_Operation = draft.t_Kind_Operation "
              + "  order by draft.t_Date, draft.t_oprxid";

  Select_Count = RSDCommand( query_count + query_where );

  Select_Count.AddParam("deporoot1", RSDBP_IN, depoacnt.AutoKey );  /*1*/
  Select_Count.AddParam("deporoot2", RSDBP_IN, depoacnt.AutoKey );  /*2*/
  Select_Count.AddParam("end_date", RSDBP_IN, end_date );  /*3*/
  Select_Count.AddParam("beg_date", RSDBP_IN, beg_date );  /*4*/

  Select_Count.Execute();
  draft = TRsbDataSet(Select_Count);
  if(draft.MoveNext())
    cnt = draft.cnt;
  end;

  if(cnt)
    DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Отчет об операциях по счету депо ", LogOprID, 0, false, beg_date, end_date, tablewidth );
    Rep.AddEmptyStr();
    DP_AddPrintCell( Rep, "Инициатор поручения: " + LogoprInitiatorName, tablewidth, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddEmptyStr();
    DP_AddPrintCell( Rep, "Получатель отчета: " + LogoprRecipientName, tablewidth, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddEmptyStr(2);
    DP_AddPrintCell( Rep, "Счет депо: " + depoacnt.BriefCode + "             Тип счета депо: " + depoacnt.AcntTypeName, tablewidth, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddEmptyStr();
    DP_AddPrintCell( Rep, "Депонент: " + IIF(depoacnt.Kind == SIDEBALANCE_PASSIVE, depoacnt.OwnerName, ""), tablewidth, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddEmptyStr();
    DP_AddPrintCell( Rep, "Депозитарный договор: " + IIF(depoacnt.Kind == SIDEBALANCE_PASSIVE, GetDepoContr(depoacnt.Code, depoacnt.Owner), ""), tablewidth, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddEmptyStr();

    Select = RSDCommand( query_select + query_where );

    Select.AddParam("deporoot1", RSDBP_IN, depoacnt.AutoKey );  /*1*/
    Select.AddParam("deporoot2", RSDBP_IN, depoacnt.AutoKey );  /*2*/
    Select.AddParam("end_date", RSDBP_IN, end_date );  /*3*/
    Select.AddParam("beg_date", RSDBP_IN, beg_date );  /*4*/

    Select.Execute();
    draft = TRsbDataSet(Select);
    Progress.Init(cnt, "Просмотр проводок...", "Выпуск отчета" );
    while(draft.MoveNext())
      CountPrintDoc = CountPrintDoc + 1;
      DP_AddPrintCell( Rep, string(SQL_ConvTypeDate(draft.date):f), 0, 0, "c:" + RepFontStyleTitel, false );
      DP_AddPrintCell( Rep, draft.oprxid, 0, 0, "c:" + RepFontStyleTitel, false );
      DP_AddPrintCell( Rep, draft.OprName, 0, 0, "c:" + RepFontStyleTitel, false );
      DP_AddPrintCell( Rep, draft.FIName, 0, 0, "c:" + RepFontStyleTitel, false );
      DP_AddPrintCell( Rep, draft.LSIN, 0, 0, "c:" + RepFontStyleTitel, false );
      DP_AddPrintCell( Rep, draft.sum, 0, draft.point, "c:" + RepFontStyleTitel, use_ap );
      DP_AddPrintCell( Rep, GetAppDocs(draft.draftid), 0, 0, "c:" + RepFontStyleTitel, false );
      DP_AddPrintCell( Rep, GetStorage(draft.draftid), 0, 0, "c:" + RepFontStyleTitel, false );
      DP_AddPrintCell( Rep, GetAccStr(draft.draftid, true), 0, 0, "c:" + RepFontStyleTitel, false );
      DP_AddPrintCell( Rep, GetAccStr(draft.draftid, false), 0, 0, "c:" + RepFontStyleTitel, false );
      DP_AddPrintCell( Rep, "№ "+draft.Xld+" от "+string(SQL_ConvTypeDate(draft.SignedDate):f), 0, 0, "c:" + RepFontStyleTitel, false );
      Rep.AddStr();

      Progress.Use();
    end;

    DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

    Progress.Remove();
  end;
end;

private macro CreateRep(IsEmpty)
    var stat = 1;
    var sQuery = "";
    var continue_cicle, i = 0;
    var records = 0;
    var Progress = CProgressBar;
    
    DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

    sQuery  =   " select acnt.*, dac.t_ShortName as AcntTypeName, pt.t_Name as OwnerName " 
              + "   from ddepoacnt_dbt acnt, ddepoac_dbt dac, dparty_dbt pt"
              + "  where acnt.t_Superior = 0 "
              + "    and acnt.t_OpenDate <= " + GetSQLDate(end_date)
              + "    and dac.t_ID = acnt.t_Type "
              + "    and pt.t_PartyID = acnt.t_Owner ";
    if(dprt_num > 0)
      sQuery = sQuery + " and acnt.t_Department = " + dprt_num;
    end;
    
    if( depo_acc != "" )
      sQuery = sQuery + " and acnt.t_Code = '"+depo_acc+"' "; 
    end;
    
    depoacnt = TRsbDataSet( sQuery, RSDVAL_CLIENT, RSDVAL_STATIC);
    depoacnt.MoveLast();
    records = depoacnt.GetRecCount();
    Close(depoacnt);
    //изначально отбираем все подходщие счета, т.е. головы
    depoacnt = TRsbDataSet( sQuery );

    if(records > 0 )
       
       GetLogoprSubjectsName();

       Progress.Init(records, "Поиск счетов для отчета", "Просмотр счетов ДЕПО");
       while ( depoacnt.MoveNext() )
          i = i + 1;
          PrintRepAcnt();
          Rep.AddEmptyStr();
          i = Progress.Use();
       end;
       Progress.Remove();
    end;

    if ( IsEmpty )
      CountPrintDoc = 1;
      DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Отчет об операциях по счету депо", LogOprID, 0, false, beg_date, end_date, tablewidth );
      Rep.AddEmptyStr();
      DP_AddPrintCell( Rep,  "Нет данных, удовлетворяющих параметрам отчета.", tablewidth, 0, "l:" + RepFontStyleTitel, use_ap, REP_ELEM_STR );
      Rep.AddEmptyStr();
      DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);
    end;

    DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
    DP_EndProtocol();                 //закончить протоколирование

    return CountPrintDoc;
end;

private macro CreateEmptyRep()

  return CreateRep(true);
end;

macro CreateReport(_LogOprID, 
                   _depo_acc,
                   _dprt_num,
                   _beg_date,
                   _end_date,
                   _ToExcel,
                   _use_ap
                  )

   
   LogOprID = _LogOprID; 
   depo_acc = _depo_acc; 
   dprt_num = _dprt_num; 
   beg_date = _beg_date; 
   end_date = _end_date; 
   ToExcel  = _ToExcel;  
   use_ap   = _use_ap;
    
   return DP_StdReportControl( LogOprID, CreateRep(false), @CreateEmptyRep, 0, ToExcel, Rep );
end;