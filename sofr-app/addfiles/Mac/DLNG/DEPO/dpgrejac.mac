/*                                           */ 
/* макрос шага обработки отказа в исполнении по счету*/
/* Nikonorov Evgeny 07-09-06                 */

import InsCarryDoc, OprInter, "dlcarry.inc", opr_depo, "spinfop.mac", "dpsteprep.mac";

/* Буферы ГЛ, заполняемые из операции */
record corpop( dpcorpop );    /*поручение*/
record regstr( dpregstr ); /*реестр*/

file depoacnt("depoacnt");
const DLDOC_INSREGLIN_REJ      = 891;


private macro RegExtGround(rej_prm)
  record reglin( dpreglin );
  var query, Select, Data;
  var ReglineID = 0;
  var stat = 0;

  SetBuff(reglin, rej_prm); // Накладываем структуру с первым полем int32
  ReglineID = reglin.RegisterID; // в нем содержится ReglineID!

  query = " SELECT rl.t_HolderAccID " +
          " FROM ddpreglin_dbt rl " +
          " WHERE rl.t_reglineid = ? /*1*/";

  Select = RSDCommand( query );

  Select.AddParam("ReglineID", RSDBP_IN, ReglineID );  /*1*/

  Select.NullConversion = true;
  Select.Execute();

  Data = TRsbDataSet(Select);

  if( Data.moveNext() )
    // Вставить исходящий документ уведомления об исполнении ГО для обработанного счета депо
    stat = DP_RegExtGroundToReglin(regstr.RegisterID, Data.HolderAccID);
  else
    stat = 1;
  end;

  return stat;
end;


macro ExecuteStep( d, crp ) 
  var stat = 0, rej_prm;
  record corpop( dpcorpop );
  var DataSet;

  SetBuff(corpop, crp);

  rej_prm = GetGlobalParameter(DPRejectParm, true);
  if( rej_prm != NULL )
    stat = RejectDepoDraft(rej_prm, DLDOC_INSREGLIN_REJ);
    if(not stat)
      stat = RegExtGround(rej_prm);
      if(stat)
        msgbox("Ошибка регистрации исходящего документа");
      end;
    end;
  else
    stat = 1;
    msgbox("Не удалось получить параметры отказа от исполнения.|Попробуйте откатить предыдущий шаг.");
  end;

  if(not stat)
    //проверить, есть ли еще что-то на шаг "Отражение операции по счетам"?
    DataSet = TRsbDataSet(GetGlobopQuery(regstr.RegisterID));
    //moveNext делаем 2 раза !!!
    //потому что на данном шаге в реальной базе статусы еще для текущей записи не обновились
    //т.е. она подхватится, но при этом смотрим, есть ли еще что-то кроме неё.
    if((DataSet.moveNext()) and (DataSet.moveNext()))
      if( not InsertOprStatus( GetGlobopOprState(corpop.DocKind), DEPOGLOBOP_STATEVALUE_DOCCARRY) )
        msgbox("Невозможно изменить статус операции");
        stat = 1;
      end;
    else
      GlobolCounterStep30 = 0;
      Progress.Remove();
      if( not InsertOprStatus( GetGlobopOprState(corpop.DocKind), DEPOGLOBOP_STATEVALUE_REPORT) )
         msgbox("Невозможно изменить статус операции");
         stat = 1;
      end;
    end;
  end; 

  return stat;

OnError
  GlobolCounterStep30 = 0;
  Progress.Remove();
end;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

  record corpop_buf(dpcorpop);

  if (errTrn OR (CommitOrRollback==2))
    /* Произошла ошибка или происходит откат */
    return;
  end;

  SetBuff( corpop_buf, FirstDoc );

  PrintGlsendByDoc(corpop_buf.DocumentID, ID_Operation, ID_Step);
                      
  return 1;
END;

