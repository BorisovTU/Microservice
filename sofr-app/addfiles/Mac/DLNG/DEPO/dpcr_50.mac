/*
$Name:         dpcr_50.mac
$Module:       Депозитарий
$Description:  Обслуживание счета депо/Начисление дохода
*/

//!!!С 3149 больше не используется

import InsCarryDoc, OprInter, DpInter, "dppmaccfd.mac", "dlquery.mac", "dl_car.mac";

/* Буферы, передаваемые в макрос */
record OprServDoc( dpcorpop ); /*корпортивная операция*/
record dppmacc( dppmacc );     /*Платежная инструкция получателя*/

macro ExecuteStep( d, dpac )
  var err = 0;
  var FD = null, tr = null;
  var DtAcc = TRecHandler("account.dbt");
  var CtAcc = TRecHandler("account.dbt");

  if(CheckDepoChargeIncome() == false)
    msgbox("Начисление дохода возможно только при включении настройки DEPO\\\\CHARGEINCOME");
    err = 1;
  end;

  if(not err)
    if(({curdate} < OprServDoc.ValueDate) OR ({curdate} > OprServDoc.EndValueDate))
      msgbox("Начисление дохода может быть выполнено только в период выплат");
      err = 1;
    end;
  end;
  
  if((not err) AND (OprServDoc.DontCarryCharge == ""))
    FD = DP_PmaccFD(dppmacc.DocumentID, dppmacc.HolderAccID);

    if(false == FD.OpenAccount( "-Расчеты, Выплаты по ц/б", NULL, NULL, CtAcc, OprServDoc.ValueDate, OprServDoc.Currency ))
      msgbox("Ошибка при открытии счета");
      err = 1;
    end;

    if(not err)

      /*проводку формируем без платежа*/
      tr = RsbAccTransaction();

      if(tr == null)
        msgbox("Ошибка при формировании проводки");
        err = 1;
      end;

      if(not err)
        if(DP_FindAccount(OprServDoc.Account, OprServDoc.Currency, 1, DtAcc) != 0)
          msgbox("Не найден счет");
          err = 1;
        end;

        if(not err)
          tr.Date_Carry = OprServDoc.ValueDate;

          tr.Chapter         = 1;
          tr.Department      = OprServDoc.Department;

          tr.Number_Pack     = 0;
          tr.Numb_Document   = "";
          tr.Ground          = "Начисление дохода";
          tr.FIIDPayer       = OprServDoc.Currency;
          tr.FIIDReceiver    = OprServDoc.Currency;
          tr.SumPayer        = dppmacc.BruttoAmount;
          tr.SumReceiver     = dppmacc.BruttoAmount;
          tr.AccountPayer    = OprServDoc.Account;
          tr.AccountReceiver = CtAcc.rec.Account;

          tr.Shifr_Oper = DL_GetShifrOper(1, DtAcc.rec, CtAcc.rec);

          if( not tr.Carry() )
            msgbox("Ошибка при выполнении проводки");
            err = 1;
          end;
        end;

      end;

    end;

  end;

  if(not err)
    if(not DP_ChangePMACCState(dppmacc.DocumentID, dppmacc.HolderAccID, DPPMAC_STATE_CRED, OprServDoc.ValueDate, true))
      msgbox("Ошибка при изменении статуса выплаты");
      err = 1;
    end;
  end;

  return err;
end;