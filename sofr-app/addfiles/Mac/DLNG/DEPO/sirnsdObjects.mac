/*                           
$Name:             sirnsdObjects.mac
$Module:           Депозитарий
$Description:      Импорт справочников ценных бумаг НРД Объекты                                          
*/
import CTInter, FIInter, PTInter, BankInter;
import dlquery, cb_sql, globals, dprepsec;
import sirnsdReport;
import cbissref, fi, fiwarnts;
import "dlreport.mac";

private const OGRN_START_DATE = date(1, 7, 2002);

private const SIRNSD_AVOIR_TABLE     = "sirnsd_avr.tmp",
              SIRNSD_AVOIR_TABLE_SQL = "dsirnsd_avr_tmp",
              SIRNSD_CODES_TABLE     = "sirnsd_addCodes.tmp",
              SIRNSD_CODES_TABLE_SQL = "dsirnsd_addCodes_tmp",
              SIRNSD_WARNT_TABLE     = "sirnsd_warnt.tmp",
              SIRNSD_WARNT_TABLE_SQL = "dsirnsd_warnt_tmp",
              SIRNSD_ORG_TABLE       = "sirnsd_org.tmp",
              SIRNSD_ORG_TABLE_SQL   = "dsirnsd_org_tmp",
              SIRNSD_LIC_TABLE       = "sirnsd_orgLic.tmp",
              SIRNSD_LIC_TABLE_SQL   = "dsirnsd_orgLic_tmp",
              SIRNSD_FACEVALUE_TABLE     = "sirnsd_facevalue.tmp",
              SIRNSD_FACEVALUE_TABLE_SQL = "dsirnsd_facevalue_tmp",;
private const NAT_CUR_STR = "RUB";

private const ACT_UPDATE = 0,
              ACT_INSERT = 1,
              ACT_MERGE  = 2;

// статусы записей
const STATE_SOURCE    = 0,  // исходная запись, с которой работаем
      STATE_SAVEOLD   = 2,  // сохраненная запись (в скроллинге могли внести изменения - навесить другой объект),
      STATE_PROCESSED = 3,  // обработанная запись
      STATE_REMOVED   = 4,  // удаленная запись в скроллинге
      STATE_LOAD      = -1; // загруженная запись из xml, не обрабатывается

private macro CnvBoolToChr(value:bool):string
   if (value)
      return "X";
   end;
   return "";
end;
/* Удалить символ symb из строки и заменить пробелом, если надо*/
private macro RemoveSymbFromMsg(symb:string, msg:string):string
   var retVal:string = "";
   //var sourceStr:string = msg;
   var startPos:integer = 1;

   while( startPos > 0 ) 
     startPos = StrBrk ( msg, "|" ); 
     var tmpStr:string = Trim ( SubStr(msg,1,startPos-1) );
     if ( StrLen( tmpStr ) )
       retVal = RetVal + tmpStr + " ";
     end;
     msg = SubStr( msg, startPos+1 );
   end;
   retVal = Trim(RetVal + msg);
   return retVal;
end;

private macro GetErrorMsg(Number:integer):string
   var RetVal:string = "";
   
   if (Number > 1) // при 1 нечего искать ошибку!
      var cmd, dataSet;
      cmd = DL_RSDCommand(" SELECT T_CONTENTS FROM DBANK_MSG WHERE T_NUMBER = ? ");
      cmd.addParam(Number);

      dataSet = cmd.execute();

      if (dataSet.MoveNext())
         RetVal = RemoveSymbFromMsg("|", dataSet.Contents);
      end;
   end;
   return RetVal;
end;
// получить значение категории
private macro GetNameCateg(objType, code, grID)
   var cmd, dataSet;
   var name = "";

   if (code != "")
      cmd = DL_RSDCommand("SELECT obj.t_Name"
                        + "  FROM dobjattr_dbt obj"
                        + " WHERE obj.t_ObjectType = ?"
                        + " and obj.t_GroupID = ?" 
                        + " and obj.t_numinlist = ?" );

      cmd.addParam(objType);
      cmd.addParam(grID);
      cmd.addParam(code);

      dataSet = cmd.Execute();

      if (dataSet.moveNext())
         name = dataSet.Name;
      end;
   end;
   return name;
end;

private var prevObjectType = NULL;
private var prevCodeKind   = NULL;
private var prevIsUnique   = NULL;
//проверить, может ли дублироваться код данного вида
private macro CodeKindIsUnique(ObjectType, CodeKind)
  var query, cmd, DataSet;
  var isUnique = true;

  if((prevObjectType == ObjectType) and
     (prevCodeKind == CodeKind)
    )
    return prevIsUnique;
  end;

  query = "select t_Duplicate from dobjkcode_dbt where t_ObjectType = ? and t_CodeKind = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ObjectType);
  cmd.AddParam(CodeKind);

  DataSet = cmd.Execute();
  if((DataSet.moveNext()) and (DataSet.Duplicate == SET_CHAR))
    isUnique = false;
  end;

  prevObjectType = ObjectType;
  prevCodeKind   = CodeKind;
  prevIsUnique   = isUnique;

  return isUnique;
end;

// Получить валюту выплаты дохода из страны субъекта
private macro GetPtCountryFI(PartyID:integer) : integer
   var FIID = -1;
   var cmd, dataSet;

   cmd = DL_RSDCommand(" SELECT fin.T_FIID" 
                      +"   FROM dcountry_dbt cntr, dcur_cntr_dbt cc, disocur_dbt iso, dfininstr_dbt fin,"
                      +"        (SELECT NVL(t_nrcountry, chr(1)) as t_nrcountry"
                      +"           FROM dparty_dbt "
                      +"          WHERE t_partyid = ?) prt"
                      +"  WHERE cntr.T_CODELAT3 = prt.t_nrcountry"
                      +"    AND cc.T_CODENUM=cntr.T_CODENUM3"
                      +"    AND iso.T_NUMBERCODE = cc.T_NUMBERCODE"
                      +"    AND iso.T_ISREVOKED != 'X'"
                      +"    AND fin.T_ISO_NUMBER = iso.T_NUMBERCODE"
                      +"    AND fin.T_ISCLOSED != 'X'"
                      );
   cmd.addParam(PartyID);

   dataSet = cmd.Execute();
   if (dataSet.moveNext())
      FIID = dataSet.FIID;
   end;

   return FIID;
end;

// *************************************
// Дополнительные коды
// *************************************
class TAddCode
   var code_type_mn = "", // Тип кода
       code         = ""; // Значение кода
end;

// *************************************
// Блок купонных выплат
// *************************************
class TCouponData
   var payment_date_calc:date = date(0,0,0), // Дата КД расчетная
       payment_date_fact:date = date(0,0,0), // Дата КД фактическая
       record_date_calc:date  = date(0,0,0), // Дата фиксации расчетная
       interest_rate:numeric  = $0.0,        // Ставка купона в % годовых 
       size:numeric           = $0.0,        // Размер выплаты купонного дохода на 1 ц.б. (в валюте платежа)
       lock_date_calc:date    = date(0,0,0), // Дата блокировки расчетная
       coupon_number:integer  = 0,           // Порядковый номер  купона
       period_from_calc:date  = date(0,0,0); // Дата начала купонного периода расчетная
end;

// *************************************
// Блок частичного погашения
// *************************************
class TPartialData
   var pp_number:integer            = 0,           // Номер частичного досрочного погашения основного долга (для окончательного погашения не заполняется)
       action_type_code:string      = "",          // тип погашения
       payment_size_percent:numeric = $0.0,        // Ставка в % погашения части номинальной стоимости 
       payment_date_calc:date       = date(0,0,0), // Дата КД расчетная
       payment_date_fact:date        = date(0,0,0), // Дата КД фактическая
       record_date_calc:date        = date(0,0,0); // Дата фиксации расчетная
end;

// *************************************
// Блок "Календарь номиналов"
// *************************************
class TFaceValueData
   var facevalue_date:date = date(0,0,0), // Дата номинала
       facevalue:numeric   = $0.0;        // Величина номинальной стоимости (в валюте номинала)  
end;

// *************************************
// Блок виды деятельности по лицензии
// *************************************
class TLicenceData
   var num         = 0,           // Номер лицензии, выданной Банком России 
       activity_id = 0,           // Вид деятельности по лицензии
       dateStart   = date(0,0,0); // Дата выдачи по виду деятельности          
end;

// *************************************
// Организации
// *************************************
class TOrganizationData(isProtocolExt_:bool)
   var cmp_code_nsd:string,         // Код НРД организации
       full_name:string,            // Полное фирменное наименование органи-зации
       short_name:string,           // Краткое фирменное наименование органи-зации
       common_name_full_en:string,  // Транслитерированное наименование 
       full_name_en:string,         // Полное фирменное наименование (ин. яз.)
       inn:string,                  // ИНН
       comp_type_id:integer,        // Организационно-правовая форма
       cmp_code_fcsm:string,        // Код ФСФР. Для кредитных организаций поле не заполняется
       kpp:string,                  // КПП
       credit_cmp:bool,             // Признак кредитной  организации (для российских организаций)
       is_bank_4_non_resident:bool, // Признак Банк / не банк  (для иностранных организаций)
       okpo: string,                // ОКПО
       bik:string,                  // БИК
       state_reg_num:string,        // Номер гоc. регистрации при создании
       state_reg_date:date,         // Дата гос. регистрации при создании
       ogrn:string,                 // ОГРН
       egrul_date:date,             // Дата внесения записи в ЕГРЮЛ
       egrul_organ_name:string,     // Орган, внесший запись в ЕГРЮЛ
       pif_type_mn:string,          // Тип ПИФ
       country:string,              // Страна местонахождения
       fio_head:string,             // ФИО первого лица
       //position_head:string,        // Должность первого лица
       address:string,              // Адрес местонахождения   
       post_address:string,         // Почтовый адрес
       phone:string,                // Телефон
       fax:string,                  // Факс
       e_mail:string;               // Адрес электронной почты
   
   var licData = TArray(5,5);//TLicenceData
   // данные не из НРД
   var partyID:integer;             // ID найденного субъекта
   var inn_kpp:string,
       codeCntrSec:string,
       codeNSDSec:string,
       INNSec:string,
       BICSec:string,
       OGRNSec:string,
       isLinkSec:string;
   var DateEGRULSec:date     = ZeroDate,
       CountrySec:string     = "",
       ShortNameSec:string   = "",
       FullNameSec:string    = "",
       CompTypeSec:string    = "", 
       IsBankSec:bool        = false,
       OKPOSec:string        = "",
       RegNumSec:string      = "",
       RegDateSec:date       = ZeroDate,
       LicKindSec:string     = "",
       LicDateSec:date       = ZeroDate,
       LatNameSec:string     = "",
       FullNameEnSec:string  = "",
       AddressSec:string     = "",
       PostAddressSec:string = "",
       PhoneSec:string       = "",
       FaxSec:string         = "",
       EmailSec:string       = "",
       IsIssuerSec:bool       = false, // эмитент?
       isIndirect:bool       = false; // Флаг, что найдено косвенно (как эмитент выпуска)
   var objPartyUpd:RsbParty; // объект субъекта для обнволения (заполняется в fillFRomTMP, используется также и при Update)
   var isIssuer:bool; // эмитент

   var logger = CMessageSIRNSD(OBJTYPE_PARTY);
   var isProtocolExt:bool = isProtocolExt_;

   // вставка записей во временную таблицу
   var dataOrgCache = RsbSQLInsert(SIRNSD_ORG_TABLE);
   var r_org_tmp = TRecHandler(SIRNSD_ORG_TABLE);
   var dataLicCache = RsbSQLInsert(SIRNSD_LIC_TABLE);
   var r_lic_tmp = TRecHandler(SIRNSD_LIC_TABLE);

   macro clear()
      cmp_code_nsd           = "";
      full_name              = "";
      short_name             = "";
      common_name_full_en    = "";
      full_name_en           = "";
      inn                    = "";
      comp_type_id           = 0;
      cmp_code_fcsm          = "";
      kpp                    = "";
      credit_cmp             = false;
      is_bank_4_non_resident = false;
      okpo                   = "";
      bik                    = "";
      state_reg_num          = "";
      state_reg_date         = ZeroDate;
      ogrn                   = "";
      egrul_date             = ZeroDate;
      egrul_organ_name       = "";
      pif_type_mn            = "";
      country                = "";
      fio_head               = "";
      //position_head          = "";
      address                = "";
      post_address           = "";
      phone                  = "";
      fax                    = "";
      e_mail                 = "";

      licData.size = 0;
      
      partyID     = -1;
      inn_kpp     = "";
      codeCntrSec = "";
      codeNSDSec  = "";
      INNSec      = "";
      BICSec      = "";
      OGRNSec     = "";
      isLinkSec   = "";
      DateEGRULSec   = ZeroDate;
      CountrySec     = "";
      ShortNameSec   = "";
      FullNameSec    = "";
      CompTypeSec    = "";
      IsBankSec      = false;
      OKPOSec        = "";
      RegNumSec      = "";
      RegDateSec     = ZeroDate;
      LicKindSec     = "";
      LicDateSec     = ZeroDate;
      LatNameSec     = "";
      FullNameEnSec  = "";
      AddressSec     = "";
      PostAddressSec = "";
      PhoneSec       = "";
      FaxSec         = "";
      EmailSec       = "";
      IsIssuerSec    = false;
      isIndirect     = false;

      isIssuer = false;
      objPartyUpd = null;
   end;

   // Сфорировать ИНН/КПП
   private macro GetINN_KPP(inn, kpp)
      var inn_kpp = inn;
      if (kpp != "")
         inn_kpp = inn + "/" + kpp;
      end;
      return inn_kpp;
   end;

   // -------------------------------
   // поиск объекта Securities
   // -------------------------------
   macro findObject() : integer
      var cmd, dataSet;
      var query =  "(SELECT 1 as Num, code.t_ObjectID "
                 + "    FROM dobjcode_dbt code"
                 + "  WHERE code.t_ObjectType = " + OBJTYPE_PARTY 
                 + "      AND code.t_CodeKind = " + PTCK_NRD
                 + "      AND code.T_State = 0"
                 + "      AND code.t_Code = ? "
                 + ")";
      cmd = DL_RSDCommand();
 
      // cmp_code_nsd всегда должен быть задан!
      cmd.addParam(cmp_code_nsd);

      // ИНН/КПП
      inn_kpp = GetINN_KPP(inn, kpp);
      if (inn_kpp != "")
         // может быть не задан
         query = query + "UNION ALL ( "
                       + "SELECT 2 as Num, code.t_ObjectID "
                       + "    FROM dobjcode_dbt code"
                       + "  WHERE code.t_ObjectType = " + OBJTYPE_PARTY 
                       + "      AND code.t_CodeKind = " + PTCK_INN
                       + "      AND code.t_Code = ? "
                       + ")";
         cmd.addParam(inn_kpp);
      end;
      // ИНН
      if (inn != "")
         query = query + "UNION ALL ( "
                       + "SELECT 3 as Num, code.t_ObjectID "
                       + "    FROM dobjcode_dbt code"
                       + "  WHERE code.t_ObjectType = " + OBJTYPE_PARTY 
                       + "      AND code.t_CodeKind = " + PTCK_INN
                       + "      AND code.t_Code = ? "
                       + ")";
         cmd.addParam(inn);
      end;
      // БИК
      if (bik != "")
         // может быть не задан
         query = query + "UNION ALL ( "
                       + "SELECT 4 as Num, code.t_ObjectID "
                       + "    FROM dobjcode_dbt code "
                       + "  WHERE code.t_ObjectType = " + OBJTYPE_PARTY 
                       + "      AND code.t_CodeKind = " + PTCK_BIC
                       + "      AND code.t_Code = ? "
                       + ")";
         cmd.addParam(bik);
      end;
      // ОГРН
      if ((ogrn != "") and (egrul_date >= OGRN_START_DATE))
         // может быть не задан
         query = query + "UNION ALL ( "
                       + "SELECT 5 as Num, code.t_ObjectID  "
                       + "    FROM dobjcode_dbt code "
                       + "  WHERE code.t_ObjectType = " + OBJTYPE_PARTY
                       + "      AND code.t_CodeKind = " + PTCK_OGRN
                       + "      AND code.t_Code = ? "
                       + ")";
         cmd.addParam(ogrn);
      end;

      query = query + "ORDER BY Num";

      dataSet = cmd.Execute(query);
      if (dataSet.moveNext())
         partyID = dataSet.ObjectID;
      else
         partyID = -1; // не найдено
      end;
      
      return partyID;

      onError(er)
         logger.error(cmp_code_nsd, cmd.GetErrorString(er));
         return (partyID=-9999);
   end;
   // вставка лицензий в кеш
   private macro addRecordLicToCaches():bool
      var stat = false;
      for (var lic, licData)
         r_lic_tmp.clear();

         r_lic_tmp.rec.code_nsd    = cmp_code_nsd;
         r_lic_tmp.rec.number      = lic.num;
         r_lic_tmp.rec.activity_id = lic.activity_id;
         r_lic_tmp.rec.date_start  = lic.dateStart;
         stat = dataLicCache.AddRecord(r_lic_tmp);
         if (not stat)
            logger.error(cmp_code_nsd, "Ошибка при вставке вида деятельности по лицензии");
         end;
      end;
      return true; // не вставили код - и ладно, самое главное основные данные!
   end;

   // вставка кэша видов дейстельности
   private macro insertLicCache()
      return dataLicCache.insert();
   end;
   
   // Заполнить org_rec данными Securities
   private macro FillRecDataSec(org_rec)
      record r_party ("party.dbt");

      // Заполняем данные по связке с Securities
      org_rec.rec.partyID = partyID;
      if (partyID > -1)
         org_rec.rec.isLinkSec = "X";
         if (ПолучитьСубъекта(partyID, r_party) == 0)
            org_rec.rec.ShortNameSec   = r_party.ShortName;
            org_rec.rec.NotResidentSec = r_party.NotResident;
         end;
      end;

      var cmd, dataSet;

      cmd = DL_RSDCommand("SELECT t_codeKind, t_code FROM dpartcode_dbt WHERE t_partyID = ?");
 
      cmd.addParam(partyID);
      dataSet = cmd.Execute();
      while (dataSet.moveNext)
         if (dataSet.CodeKind == PTCK_CONTR)
            org_rec.rec.codeCntrSec = dataSet.Code;
         elif (dataSet.CodeKind == PTCK_NRD )
            org_rec.rec.codeNSDSec = dataSet.Code;
         elif (dataSet.CodeKind == PTCK_INN )
            org_rec.rec.INNSec = dataSet.Code;
         elif (dataSet.CodeKind == PTCK_BIC )
            org_rec.rec.BICSec = dataSet.Code;
         elif (dataSet.CodeKind == PTCK_OGRN)
            org_rec.rec.OGRNSec = dataSet.Code;
         end;
      end;
   end;

   // добавление организации в массив массовых вставок.
   // ret: true - вставка успешна
   //      false - ошибка
   macro addRecordToCache():bool
      var stat = false;
      r_org_tmp.clear();

      r_org_tmp.rec.code_nsd               = cmp_code_nsd;
      r_org_tmp.rec.full_name              = full_name;
      r_org_tmp.rec.short_name             = short_name;
      r_org_tmp.rec.common_name_full_en    = common_name_full_en;
      r_org_tmp.rec.full_name_en           = full_name_en;
      r_org_tmp.rec.comp_type_id           = comp_type_id;
      r_org_tmp.rec.cmp_code_fcsm          = cmp_code_fcsm;
      r_org_tmp.rec.credit_cmp             = CnvBoolToChr(credit_cmp);
      r_org_tmp.rec.is_bank_4_non_resident = CnvBoolToChr(is_bank_4_non_resident);
      r_org_tmp.rec.okpo                   = okpo;
      r_org_tmp.rec.bik                    = bik;
      r_org_tmp.rec.state_reg_num          = state_reg_num;
      r_org_tmp.rec.state_reg_date         = state_reg_date;
      if (egrul_date >= OGRN_START_DATE)
         r_org_tmp.rec.ogrn                   = ogrn;
         r_org_tmp.rec.egrul_date             = egrul_date;
         r_org_tmp.rec.egrul_organ_name       = egrul_organ_name;
      end;
      r_org_tmp.rec.pif_type_mn            = pif_type_mn;
      r_org_tmp.rec.country                = country;
      r_org_tmp.rec.fio_head               = fio_head;
      r_org_tmp.rec.address                = address;
      r_org_tmp.rec.post_address           = post_address;
      r_org_tmp.rec.phone                  = phone;
      r_org_tmp.rec.fax                    = fax;
      r_org_tmp.rec.e_mail                 = e_mail;
      if (r_org_tmp.rec.country == "RUS")
         r_org_tmp.rec.resident = "X";
      end;
      r_org_tmp.rec.inn_kpp = GetINN_KPP(inn, kpp);

      r_org_tmp.rec.State = STATE_LOAD;

      // Заполняем данные по связке с Securities
      FillRecDataSec(r_org_tmp);

      stat = dataOrgCache.AddRecord(r_org_tmp);
      
      if (stat)
         addRecordLicToCaches();
      end;

      return stat;
   end;

   // Вставка кэша в БД.
   // ret: true - вставка успешна
   //      false - ошибка
   macro insertCache():bool
      var stat = false;

      stat = dataOrgCache.insert();

      // вставка лицензий
      if (stat)
         stat = insertLicCache();
      end;

      return stat;
   end;
   
   macro code_nsd()
      return cmp_code_nsd;
   end;

   // получить организационно-правую форму
   private macro GetCompType():string
      var type = "";
      if (comp_type_id != 0)
         type = String(comp_type_id);
      end;

      return comp_type_id;
   end;
   // получить всевдоним
   private macro GetPseudonim(partyID, typeID):string
      var cmd, dataSet;
      var name = "";
     
      cmd = DL_RSDCommand("SELECT T_NAME FROM DPARTYNAME_DBT " +
                          " WHERE T_PARTYID = ?" +
                          " AND T_NAMETYPEID = ?");
      cmd.addParam(partyID);
      cmd.addParam(typeID);

      dataSet = cmd.Execute();

      if (dataSet.moveNext() and (dataSet.Name != null))
         name = dataSet.Name;
      end;

      return name;
   end;
   // получить строку данных о конктактах (контактенация всех контактов (одного ивда), разделенных ;)
   private macro GetFull(arr:TArray)
      var str = "";
      var i = 0;

      while(i < arr.size)

        if(i == 0)
          str = arr[i];
        else
          str = str + ";" + arr[i];
        end;

        i = i + 1;
      end;
      
      return str;
   end;
   private macro GetRegNumber_DateSec(RegNumSec:@string, RegDateSec:@date)
      var cmd, dataSet;

      cmd = DL_RSDCommand("SELECT t_code, t_bankdate FROM dpartcode_dbt WHERE t_partyID = ? AND t_codeKind = 13");/*PTCK_BANKREGNUM*/
 
      cmd.addParam(partyID);

      dataSet = cmd.Execute();
      if (dataSet.moveNext)
         RegNumSec  = dataSet.Code; 
         RegDateSec = dataSet.BankDate;
      end;
   end;

   private macro CorrectString(str)
      return StrSubst(StrSubst(str, "\r", " "), "\n", " ");
   end;

   // заполнение данными из временного файла
   macro fillFromTMP(org_tmp)
      record party ("party.dbt");
      cmp_code_nsd           = org_tmp.rec.code_nsd;
      full_name              = org_tmp.rec.full_name;
      short_name             = org_tmp.rec.short_name;
      common_name_full_en    = org_tmp.rec.common_name_full_en;
      full_name_en           = org_tmp.rec.full_name_en;
      comp_type_id           = org_tmp.rec.comp_type_id;
      cmp_code_fcsm          = org_tmp.rec.cmp_code_fcsm;
      credit_cmp             = (org_tmp.rec.credit_cmp=="X");
      is_bank_4_non_resident = (org_tmp.rec.is_bank_4_non_resident=="X");
      okpo                   = org_tmp.rec.okpo;
      bik                    = org_tmp.rec.bik;
      state_reg_num          = org_tmp.rec.state_reg_num;
      state_reg_date         = org_tmp.rec.state_reg_date;
      ogrn                   = org_tmp.rec.ogrn;
      egrul_date             = org_tmp.rec.egrul_date;
      egrul_organ_name       = org_tmp.rec.egrul_organ_name;
      pif_type_mn            = org_tmp.rec.pif_type_mn;
      country                = org_tmp.rec.country;
      fio_head               = org_tmp.rec.fio_head;
      address                = org_tmp.rec.address;
      post_address           = org_tmp.rec.post_address;
      phone                  = org_tmp.rec.phone;
      fax                    = org_tmp.rec.fax;
      e_mail                 = org_tmp.rec.e_mail;
      inn_kpp                = org_tmp.rec.inn_kpp;

      // Данные Securities
      partyID     = org_tmp.rec.partyID;
      codeCntrSec = org_tmp.rec.codeCntrSec;
      codeNSDSec  = org_tmp.rec.codeNSDSec;
      INNSec      = org_tmp.rec.INNSec;
      BICSec      = org_tmp.rec.BICSec;
      OGRNSec     = org_tmp.rec.OGRNSec;
      isLinkSec   = org_tmp.rec.isLinkSec;
      if (isLinkSec == "X")
         var address1 = null, address2 = null;
         var PartyContact = null, phone_arr = TArray, fax_arr = TArray, e_mail_arr = TArray;

         objPartyUpd = RsbParty(partyID);

         PartyContact = objPartyUpd.PartyContact();
         while(PartyContact.Next())

           if(PartyContact.ContactKind == CNTK_PHONE)
             phone_arr[phone_arr.size] = CorrectString(PartyContact.Value);
           elif(PartyContact.ContactKind == CNTK_FAX)
             fax_arr[fax_arr.size] = CorrectString(PartyContact.Value);
           elif(PartyContact.ContactKind == CNTK_EMAIL)
             e_mail_arr[e_mail_arr.size] = CorrectString(PartyContact.Value);
           end;
         end;

         CountrySec     = CorrectString(objPartyUpd.NRCountry);
         if (CountrySec == "")
            CountrySec = "RUS";
         end;
         ShortNameSec   = CorrectString(objPartyUpd.ShortName);
         FullNameSec    = CorrectString(objPartyUpd.FullName);

         ПолучитьСубъекта(partyID, party);
         var ObjectCode = UniID(party, OBJTYPE_PARTY);
         GetMainObjAttr(null, OBJTYPE_PARTY, ObjectCode,  57, null, "", CompTypeSec);
         IsBankSec      = objPartyUpd.IsOwned(PTK_BANK);
         OKPOSec        = CorrectString(objPartyUpd.OKPO);
         GetRegNumber_DateSec(@RegNumSec, @RegDateSec);
         RegNumSec = CorrectString(RegNumSec);
         var doc = objPartyUpd.PartyRegDoc(REGPT_FKCB, OBJKDOC_SPEC_DEPO_LICENSE); // орган - ФСФР, вид документа - спецдепозитарная лицензия 
         if (doc.Number != "")
            LicKindSec = "Спецдепозитарная";
            LicDateSec = doc.StartDate;
            doc = null;
         else 
            doc = null;
            doc = objPartyUpd.PartyRegDoc(REGPT_FKCB, OBJKDOC_LICEN_DEPOSITARY); // орган - ФСФР, вид документа - депозитарная
            if (doc.Number != "")
               LicKindSec = "Депозитарная";
               LicDateSec = doc.StartDate;
            end;
         end;
         doc = objPartyUpd.PartyRegDoc(REGPT_TAXINSTITUTE, OBJKDOC_EVIDENCE_GOS_REG);
         DateEGRULSec = doc.StartDate;
         doc = null;
         LatNameSec     = CorrectString(GetPseudonim(objPartyUpd.partyID, 7));
         FullNameEnSec  = CorrectString(GetPseudonim(objPartyUpd.partyID, 3));
         address1       = objPartyUpd.Address(1);
         address2       = objPartyUpd.Address(2);
         AddressSec     = CorrectString(address1.Address);
         PostAddressSec = CorrectString(address2.Address);
         PhoneSec       = GetFull(phone_arr);
         FaxSec         = GetFull(fax_arr);
         EmailSec       = GetFull(e_mail_arr);
         IsIssuerSec    = objPartyUpd.IsOwned(PTK_ISSUER);
         isIndirect     = (org_tmp.rec.Indirect=="X");

         address1       = null;
         address2       = null;
      end;

      // Заполняем лицензии
      licData.size=0;
      var cmd, dataSet;
      var lic = null;

      cmd = DL_RSDCommand("SELECT * FROM dsirnsd_orgLic_tmp WHERE t_code_nsd = ? AND t_activity_id IN(1,2,13,29)");
      cmd.addParam(cmp_code_nsd);

      dataSet = cmd.execute();
      while (dataSet.moveNext())
         lic = TLicenceData();
         lic.num         = dataSet.Number;
         lic.activity_id = dataSet.activity_id;
         lic.dateStart   = dataSet.date_start;
         licData[licData.size] = lic;
      end;
   end;

   private macro GetPartyIDFromChanges(IDChanges:@variant)
      var cmd, dataSet;
      var stat = false;

      cmd = DL_RSDCommand("SELECT T_PARTYID FROM dsirnsd_org_tmp WHERE T_CODE_NSD = ? AND T_STATE = " + STATE_SAVEOLD);
      cmd.addParam(cmp_code_nsd);

      dataSet = cmd.Execute();
      stat = dataSet.moveNext();
      if (stat)
         IDChanges = dataSet.PartyID;
      end;

      return stat;
   end;
   // получить номера телефонов (осн. и дополнительные)
   // (в справ. номеров м.б. много)
   private macro GetArrContact(contact_str, arr:TArray)
      var _index = Index(contact_str, ";");
      var one_contact = "";
      
      arr.size = 0;
        
      if(_index > 0)
        while(_index > 0)
          one_contact = Trim(substr(contact_str, 1, _index - 1));
          arr[arr.size] = one_contact;

          contact_str = Trim(substr(contact_str, _index + 1));
          _index = Index(contact_str, ";");
        end;
      end;

      arr[arr.size] = contact_str;
   end;

   private macro GetErrorInfo(errMsgStart:string):string
      var bankMsg:RSBankMsg = AL_GetErrorInfo();

      if (bankMsg.Stat != 0)
         return errMsgStart + ". " + RemoveSymbFromMsg("|", bankMsg.Message);
      end;
      return errMsgStart;
   end;

   // есть ли отличия для обновления?
   macro IsChangesForUpd() : bool
      
      if (isLinkSec != 0)
         var IDSource = 0;

         // 1. проверим, не изменяли ли мы данную запись (с исходной)?
         if (GetPartyIDFromChanges(IDSource))
            if (IDSource != 0) // Изменили запись.
               return true;
            elif (IDSource == PartyID) // Запись не изменили
               return false; 
            end;
         end;

         var phones_arr = TArray, fax_arr = TArray, e_mail_arr = TArray, phoneCmp = "", faxCmp = "", e_mailCmp = "";
         GetArrContact(phone,  phones_arr); 
         GetArrContact(fax,    fax_arr);
         GetArrContact(e_mail, e_mail_arr);

         phoneCmp  = GetFull(phones_arr);
         faxCmp    = GetFull(fax_arr);
         e_mailCmp = GetFull(e_mail_arr);
         // 2. Сверяем данные
         return ((cmp_code_nsd        != codeNSDSec    ) or
                 (inn_kpp             != INNSec        ) or
                 (ogrn                != OGRNSec       ) or
                 (egrul_date          != DateEGRULSec  ) or
                 (country             != CountrySec    ) or
                 (short_name          != ShortNameSec  ) or
                 (full_name           != FullNameSec   ) or
                 (okpo                != OKPOSec       ) or
                 (bik                 != BICSec        ) or
                 (common_name_full_en != LatNameSec    ) or
                 (full_name_en        != FullNameEnSec ) or
                 (address             != AddressSec    ) or
                 (post_address        != PostAddressSec) or
                 (phoneCmp            != PhoneSec      ) or
                 (faxCmp              != FaxSec        ) or
                 (e_mailCmp           != EmailSec      ) or 
                 (isIssuer and (not IsIssuerSec)       )     // нужен эмитент, а у нас признак эмитента не установлен
                );
      end;
      return false;
   end;
   // есть ли отличия для вставки?
   macro IsChangesForAdd() : bool
      return (isLinkSec=="");
   end;

   // генерация кода субъекта
   private macro GenerateCodeCntr()
      var Ref = "";
      record party("party");
      if (GenerateReference(110, Ref, 0, party) != 0)
         logger.error(cmp_code_nsd, "Ошибка при генерации кода субъекта");
      end;

      return Ref;
   end;
   // установка псевдонима субъекта
   private macro SetPseudonim(partyID, typeID, name)
      var cmd, dataSet;
      var InsCmd = null;
     
      cmd = DL_RSDCommand("SELECT * FROM DPARTYNAME_DBT " +
                          " WHERE T_PARTYID = ?" +
                          " AND T_NAMETYPEID = ?");
      cmd.addParam(partyID);
      cmd.addParam(typeID);

      dataSet = cmd.Execute();

      insCmd = null;
      if (dataSet.moveNext())
         if (dataSet.Name != name)
            insCmd = RSDCommand("UPDATE DPARTYNAME_DBT SET T_NAME = ? WHERE T_PARTYNAMEID = ?");
            insCmd.addParam("", RSDBP_IN, name               );
            insCmd.addParam("", RSDBP_IN, dataSet.PartyNameID);
         end;
      else
         insCmd = RSDCommand("INSERT INTO DPARTYNAME_DBT (t_partyid, t_name, t_nametypeid) values(?, ?, ?)");
         
         insCmd.addParam("", RSDBP_IN, partyID);
         insCmd.addParam("", RSDBP_IN, name   );
         insCmd.addParam("", RSDBP_IN, typeID );
      end;
      if (insCmd != null)
         insCmd.execute();
         return 0;
      else
         return 0;
      end;
      onError(e)
         return 1; 
   end;

   private macro GetValueString(value, str):string
      if (str != "")
         return str;
      end;
      return value;
   end;

   // заполнение данными лицензий
   private macro FillLicences(objParty:@RsbParty)
      var doc = null;

      for (var lic, licData)
         if (lic.activity_id == 13)
            doc = objParty.PartyRegDoc(REGPT_FKCB, OBJKDOC_SPEC_DEPO_LICENSE); // орган - ФСФР, вид документа - спецдепозитарная лицензия
         else // 1,2,29
            doc = objParty.PartyRegDoc(REGPT_FKCB, OBJKDOC_LICEN_DEPOSITARY); // орган - ФСФР, вид документа - ЛИЦДЕП (лицензия на депозитарную деятельность)
         end;
         doc.Number     = lic.Num;
         doc.FinishDate = lic.dateStart;
         doc.StartDate  = lic.dateStart;
      end;
   end;

   // заполнение объекта RsbParty
   macro FillPartySec(objParty:@RsbParty)
      objParty.FullName         = full_name;
      objParty.ShortName        = GetValueString(objParty.ShortName, short_name);
      objParty.OKPO             = GetValueString(objParty.OKPO,      okpo      );
      objParty.NRCountry     = GetValueString(objParty.NRCountry, country);
     
      objParty.Code(PTCK_INN)   = GetValueString(objParty.Code(PTCK_INN), inn_kpp);
      if (objParty.Code(PTCK_CONTR) == "")
         codeCntrSec = GenerateCodeCntr();
         objParty.Code(PTCK_CONTR) = codeCntrSec;
      end;
      objParty.Code(PTCK_BIC)   = GetValueString(objParty.Code(PTCK_BIC),   bik          );
      objParty.Code(PTCK_NRD)   = GetValueString(objParty.Code(PTCK_NRD),   cmp_code_nsd );
      objParty.Code(PTCK_OGRN)  = GetValueString(objParty.Code(PTCK_OGRN),  ogrn         );
      objParty.Code(PTCK_FSFR)  = GetValueString(objParty.Code(PTCK_FSFR),  cmp_code_fcsm);

      if (egrul_date != ZeroDate)
         objParty.PartyRegDoc(REGPT_TAXINSTITUTE, OBJKDOC_EVIDENCE_GOS_REG).StartDate = egrul_date;
      end;

      objParty.Address(1).Address     = GetValueString(objParty.Address(1).Address,     address     );
      
      var phones_arr = TArray, fax_arr = TArray, e_mail_arr = TArray;
      var i = 0, j = 0;
      GetArrContact(phone,  phones_arr); 
      GetArrContact(fax,    fax_arr);
      GetArrContact(e_mail, e_mail_arr);

      var PartyContact = objParty.PartyContact();

      while(PartyContact.Next())
        if(PartyContact.IsMain == false)
          PartyContact.Delete();
        end;
      end;

      while(i < phones_arr.size)

        if(trim(phones_arr[i]) != "")
          if((i == 0) and (PartyContact.GetMain(CNTK_PHONE, PTADDR_LEGAL, CNTT_WORKING) == 0))
            PartyContact.SetValue(phones_arr[i]);
          else
            PartyContact.Add(CNTK_PHONE, PTADDR_LEGAL, CNTT_WORKING, phones_arr[i]); 
          end;
        end;

        i = i + 1;
      end;

      i = 0;
      while(i < fax_arr.size)

        if(trim(fax_arr[i]) != "")
          if((i == 0) and (PartyContact.GetMain(CNTK_FAX, PTADDR_LEGAL, CNTT_WORKING) == 0))
            PartyContact.SetValue(fax_arr[i]);
          else
            PartyContact.Add(CNTK_FAX, PTADDR_LEGAL, CNTT_WORKING, fax_arr[i]); 
          end;
        end;

        i = i + 1;
      end;

      i = 0;
      while(i < e_mail_arr.size)

        if(trim(e_mail_arr[i]) != "")
          if((i == 0) and (PartyContact.GetMain(CNTK_EMAIL, PTADDR_LEGAL, CNTT_WORKING) == 0))
            PartyContact.SetValue(e_mail_arr[i]);
          else
            PartyContact.Add(CNTK_EMAIL, PTADDR_LEGAL, CNTT_WORKING, e_mail_arr[i]); 
          end;
        end;

        i = i + 1;
      end;


      objParty.Address(2).Address       = GetValueString(objParty.Address(2).Address, post_address);
      // Организационно-правовые формы
      if (GetCompType() != "")
         objParty.Category.ConnectAttr(57, null, "", GetCompType(), {curdate});
      end;

      // принадлженость Банк
      if ((credit_cmp == "X") or (is_bank_4_non_resident == "X"))
         if (not objParty.IsOwned(PTK_BANK))
            if (not objParty.AddOwn(PTK_BANK))
               logger.error(cmp_code_nsd, "Ошибка при установке принадлежности \"Банк\"");
            end;
         end;
      end;
      // Установка принадлежности Эмитент
      if (isIssuer)
         if (not objParty.IsOwned(PTK_ISSUER))
            if (not objParty.AddOwn(PTK_ISSUER))
               logger.error(cmp_code_nsd, "Ошибка при установке принадлежности \"Эмитент\"");
            else
               logger.warning(cmp_code_nsd, "Для субъекта установлена принадлежность \"Эмитент\"");
            end;
         end;
      end;
      // Установка Номера кредитной организации (только для Банков)
      if (state_reg_num != "")
         if (objParty.IsOwned(PTK_BANK))
            if ((not objParty.Code.SetCode(PTCK_BANKREGNUM, state_reg_num, state_reg_date))) 
               logger.error(cmp_code_nsd, GetErrorInfo("Ошибка при установке кода \"Рег. номер кред. организации\""));
            end;
         else
            logger.warning(cmp_code_nsd, "Субъект не является кредитной организацией. Рег.номер не установлен");
         end;
      end;

      // Заполнение лицензий
      FillLicences(objParty);
   end;
   // Установить флаг Эмитента
   macro SetIssuer(isIssuer_)
      isIssuer = isIssuer_;
   end;

   // Действия после обновления данных
   macro AfterAction(objParty, action)
      // устанавливаем вручную, ибо в нет установки наименовании на латинице!
      if ((common_name_full_en != "") and SetPseudonim(objParty.partyID, 7, common_name_full_en) != 0)
         logger.error(cmp_code_nsd, "Ошибка при установке псевдонима \"Полное наименование латиницей\"");
      end;
      if ((full_name_en != "") and SetPseudonim(objParty.partyID, 3, full_name_en) != 0)
         logger.error(cmp_code_nsd, "Ошибка при установке псевдонима \"Наименование на иностранном языке\"");
      end;
   end;

   private macro CreateDataForRep(action):TOrgNSDRepData
      var org = null;

      if (ACT_INSERT == action)
         org = TOrgNSDRepData();
      else
         org = TOrgRepData();
      end;
      org.ID          = partyID;
      org.CodeCntr    = codeCntrSec;
      org.CodeNSD     = cmp_code_nsd;
      org.INN_KPP     = inn_kpp;
      org.OGRN        = ogrn;
      org.DateEGRUL   = egrul_date;
      org.Country     = country;
      org.ShortName   = short_name;
      org.FullName    = full_name;
      org.CompType    = GetNameCateg(OBJTYPE_PARTY, GetCompType(), 57);
      org.IsBank      = (credit_cmp or is_bank_4_non_resident);
      org.OKPO        = okpo;
      org.BIC         = bik;
      org.RegNum      = StrUpr(state_reg_num);
      org.RegDate     = state_reg_date;
      org.LatName     = common_name_full_en;
      org.FullNameEn  = full_name_en;
      org.Address     = address;
      org.PostAddress = post_address;

      var phones_arr = TArray, fax_arr = TArray, e_mail_arr = TArray, phoneRep = "", faxRep = "", e_mailRep = "";
      GetArrContact(phone,  phones_arr); 
      GetArrContact(fax,    fax_arr);
      GetArrContact(e_mail, e_mail_arr);

      phoneRep  = GetFull(phones_arr);
      faxRep    = GetFull(fax_arr);
      e_mailRep = GetFull(e_mail_arr);
      
      org.Phone       = phoneRep;
      org.Fax         = faxRep;
      org.Email       = e_mailRep;

      for (var lic, licData)
         if (lic.activity_id == 13)
            org.LicKind = "Спецдепозитарная";
            org.LicDate = lic.dateStart;
            break;
         elif ( (lic.activity_id == 1) or (lic.activity_id == 2) or (lic.activity_id == 29))
            org.LicKind = "Депозитарная";
            org.LicDate = lic.dateStart;
            break;
         end;
      end;
      // Securities
      if ((ACT_UPDATE == action) or (ACT_MERGE == action))
         org.rsCodeNSD     = codeNSDSec;
         org.rsINN_KPP     = INNSec;
         org.rsOGRN        = OGRNSec;
         org.rsDateEGRUL   = DateEGRULSec;
         org.rsCountry     = CountrySec;
         org.rsShortName   = ShortNameSec;
         org.rsFullName    = FullNameSec;
         org.rsCompType    = GetNameCateg(OBJTYPE_PARTY, CompTypeSec, 57);
         org.rsIsBank      = IsBankSec;
         org.rsOKPO        = OKPOSec;
         org.rsBIC         = BICSec;
         org.rsRegNum      = RegNumSec;
         org.rsRegDate     = RegDateSec;
         org.rsLicKind     = LicKindSec;
         org.rsLicDate     = LicDateSec;
         org.rsLatName     = LatNameSec;
         org.rsFullNameEn  = FullNameEnSec;
         org.rsAddress     = AddressSec;
         org.rsPostAddress = PostAddressSec;
         org.rsPhone       = PhoneSec;
         org.rsFax         = FaxSec;
         org.rsEmail       = EmailSec;
         org.isIndirect    = isIndirect;
      end;
      return org;
   end;

   macro Update()
      var stat = true;
      var objParty = objPartyUpd;

      FillPartySec(objParty);

      stat = objParty.Update();

      if (stat)
         AfterAction(objParty, ACT_UPDATE);
         SaveOrgUpdDataForReport(CreateDataForRep(ACT_UPDATE));
      end;

      if(stat)
//         RslDefCon.CommitTrans();
      else
//         RslDefCon.RollbackTrans();
         logger.error(cmp_code_nsd, GetErrorInfo("Ошибка при обновлении субъекта"));
      end;
      objPartyUpd = null;
      return stat;
   end;

   // Установить признак обработанной записи
   macro SetProcessedRec()
      // Обновим статус, дабы ее не трогали больше 
      // (вдруг при вставке бумаги обработается эмитент, который не изменялся, он добавится в протокол, а потом при обработке организации он сможет снова попаст ьв протокол)
      var cmd = RSDCommand("UPDATE dsirnsd_org_tmp SET T_STATE = ? WHERE T_CODE_NSD = ?");
      cmd.addParam("", RSDBP_IN, STATE_PROCESSED);
      cmd.addParam("", RSDBP_IN, cmp_code_nsd);

      cmd.Execute();
      // Для расширенного протокола добавим в протокол (если запись есть в Securities и НЕ обновлялась)
      if ((isProtocolExt) and (isLinkSec != ""))
         SaveOrgUpdDataForReport(CreateDataForRep(ACT_UPDATE));
      end;
   end;

   macro Insert()
      var stat = true;
      var objPartyNew = RsbParty;

      objPartyNew.LegalForm = PTLEGF_INST;
      FillPartySec(objPartyNew);

      stat = objPartyNew.Update(); 
      partyID = objPartyNew.PartyID; // в отчете пригодится

      if (stat)
         AfterAction(objPartyNew, ACT_INSERT);
         SaveOrgDataForReport(CreateDataForRep(ACT_INSERT));
      end;

      if(stat)
//         RslDefCon.CommitTrans();
      else
//         RslDefCon.RollbackTrans();
         logger.error(cmp_code_nsd, GetErrorInfo("Ошибка при создании субъекта"));
      end;
      return stat;
   end;

   // действие над записью (вставить\обновить)
   macro ActionOnRec()
      var OldDialogFlag = SetDialogFlag(0);
      if (isLinkSec == "")
         Insert();
      else
         if ((codeNSDSec != "" ) and (code_nsd != codeNSDSec))
            logger.error(code_nsd,"Отличаются коды НРД в справочнике НРД и RS. Субъект не обновляется");
         else
            Update();
         end;
      end;
      SetDialogFlag(OldDialogFlag);
   end;

   // Действие над записью. Сравнение
   macro ActionOnMerge()
      if (isLinkSec == "X") // только те, которые на обнволение.
         SaveOrgUpdDataForReport(CreateDataForRep(ACT_MERGE));
      end;
   end;
   
   // получить partyID для ЦБ (будет произведено необходимое действие с организацией:обновление или вставка)
   macro GetPartyIdForAvoiriss(codeNSDOrg:string):integer
      var ID = 0;
      if (codeNSDOrg != "")
         var cmd, dataSet;
         var stat = false;

         // 1. Ищем организацию                                                              
         cmd = DL_RSDCommand("SELECT * FROM dsirnsd_org_tmp WHERE T_CODE_NSD = ? AND "+
                                     // загруженная                 исходная                 обработанная                  удаленная
                              " (T_STATE="+STATE_LOAD+" OR T_STATE="+STATE_SOURCE+" OR T_STATE="+STATE_PROCESSED+" OR T_STATE="+STATE_REMOVED+")");
         cmd.addParam(codeNSDOrg);

         dataSet = cmd.Execute();
         stat = dataSet.moveNext();
         if (stat)
            ID = dataSet.PartyID;
            if (dataSet.State != STATE_PROCESSED) // НЕ (обработан и вставлен в БД)
               // обработаем запись

               fillFromTMP(dataSet);
               if (IsChangesForAdd() or IsChangesForUpd())
                  if(IsChangesForUpd())
                    findObject();
                    if(partyID <= 0)
                      ActionOnRec();
                    end;
                  else
                    ActionOnRec();
                  end;

                  ID = partyID;
                  // 3. Обновим статус, дабы ее не трогали больше
                  cmd = RSDCommand("UPDATE dsirnsd_org_tmp SET T_STATE = "+STATE_PROCESSED+", T_PARTYID = ? WHERE T_CODE_NSD = ?");
                  cmd.addParam("", RSDBP_IN, ID);
                  cmd.addParam("", RSDBP_IN, codeNSDOrg);

                  cmd.Execute();
               else
                  // Установим признак обработанной записи
                  SetProcessedRec();
               end;
            end;
         end;
      end;
      return ID;
   end;

   // поиск partyID у выпуска, эмитентов которого является организация
   private macro GetPartyIDIssuerFromAvoir(code_nsd)
      var cmd, dataSet;
      var partyID = 0;
      cmd = DL_RSDCommand("SELECT fin.t_Issuer FROM dsirnsd_avr_tmp avr_tmp, dfininstr_dbt fin "+
                          "  WHERE avr_tmp.T_ISSUER = ? AND avr_tmp.T_ISLINKSEC=CHR(88) AND fin.T_FIID=avr_tmp.T_FIID");
      cmd.addParam(code_nsd);
      dataSet = cmd.execute();
      while (dataSet.moveNext())
         partyID = dataSet.Issuer;
         if ((partyID != -1) and (partyID != 0))
            break;
         end;
      end;
      return partyID;
   end;

   // Обработка нерезидентов (поиск выпусков, в которых организация является эмитентом и установка partyID)
   macro ProcessOnNotResident()
      var f_org = TBFile("sirnsd_org.tmp", "w", 0);
      var cmdOrg, dataSetOrg;
      var cmdUpd;

      cmdOrg = DL_RSDCommand("SELECT T_CODE_NSD FROM dsirnsd_org_tmp WHERE T_COUNTRY!=? AND T_PARTYID=0");
      cmdOrg.addParam("RUS");
      dataSetOrg = cmdOrg.execute();
      while (dataSetOrg.moveNext())
         partyID = GetPartyIDIssuerFromAvoir(dataSetOrg.Code_NSD);
         if (partyID != 0)
            // обновляем partyID
            f_org.clear();
            f_org.rec.code_nsd = dataSetOrg.Code_NSD;
            if (f_org.getEQ())
               FillRecDataSec(f_org);
               f_org.rec.Indirect = "X"; // Флаг, что найдено косвенно (как эмитент выпуска)
               f_org.update();
            end;
         end;
      end;
   end;

   this.clear();
end;

/* Класс с данными для замены MsgBox*/ 
private class DataForMsgBox(codeNSD_:string, logger_:CMessageSIRNSD)
   var codeNSD = codeNSD_,
       logger  = logger_;
end;
private var dataMsgBoxAvoir = null;
macro Avoir_msgbox(msg)
   if (dataMsgBoxAvoir != null)
      dataMsgBoxAvoir.logger.error(dataMsgBoxAvoir.codeNSD, msg);
   end;
end;

// *************************************
// Общий класс данных ц/б
// *************************************
class TAvoirData(KindData_, isProtocolExt_)
   // наиболее часто встречающиеся поля у ц/б
   var code_nsd:string,              // Код НРД
       isin:string,                  // ISIN
       cfi:string,                   // CFI
       sec_form_id:integer,          // Форма выпуска: документарная, бездоку-ментарная
       name_full:string,             // Полное наименование
       issuer:string,                // Код НРД эмитента
       issued_size:numeric,          // Размещенное количество штук
       sec_type_br_code:string,      // Значение категории "Код типа ц. б. по классификации Банка России для формы 711".
       state_reg_number:string,      // № гос. регистрации
       state_reg_date:date,          // Дата выпуска
       sec_state_id:integer,         // Состояние выпуска
       registrar:string,             // Реестродержатель
       micex_list:string,            // Листинг/внесписочные (ФБ ММВБ)
       for_qualified_investors:bool, // Признак ценной бумаги предназначенной для квалифицированных инвесторов
       indexnom:bool;                // Бумага с индексируемым номиналом 
   var addCodes = TArray(5,5); // Дополнительные коды
   // данные не из НРД
   var KindData:integer = KindData_;  // вид ЦБ (акция, облигация, ...)
   var AvoirKind:integer;             // подвид ЦБ (обыкновенная акция, привилигированная акция, .....)
   var codeMMVB:string, 
       codeSPVB:string,
       codeMicexList:string,
       Type711NumList:string,
       name:string;                   // наименование выпуска. Формируется GenerateNameAvr
   // данные Securities
   var FIID:integer;                  // FIID найденной бумаги
   var codeNSDSec:string;
   var isLinkSec:string;              // есть связь с Sec
   var CodeCntrSec:string,
       ISINSec:string,
       CFISec:string,
       IssuerNSDSec:string,
       IssuedSizeSec:Numeric,
       Type711Sec:string,
       NameSec:string,
       LSINSec:string,
       RegDateSec:date,
       RegistrarNSDSec:string,
       MicexListSec:string,
       IsKISec:bool,
       IsCanceledSec:bool,
       FullNameSec:string,
       codeMMVBSec:string,
       codeSPVBSec:string;

   // вставка записей во временную таблицу
   var dataAvrCache   = RsbSQLInsert(SIRNSD_AVOIR_TABLE);
   var r_avr_tmp      = TRecHandler(SIRNSD_AVOIR_TABLE);
   var dataCodesCache = RsbSQLInsert(SIRNSD_CODES_TABLE);
   var r_code_tmp     = TRecHandler(SIRNSD_CODES_TABLE);

   var logger = CMessageSIRNSD(OBJTYPE_AVOIRISS);

   var isProtocolExt = isProtocolExt_; // Расширенный протокол (выводить данные по обдинаковым объектам)

   macro clear()
      code_nsd                = "";
      isin                    = "";
      cfi                     = "";
      sec_form_id             = 0;
      name_full               = "";
      name                    = "";
      issuer                  = "";
      issued_size             = $0.0;
      sec_type_br_code        = "";
      state_reg_number        = "";
      state_reg_date          = ZeroDate;
      sec_state_id            = 0;
      registrar               = "";
      micex_list              = "";
      for_qualified_investors = false;
      indexnom                = false;
      addCodes.size = 0;

      FIID      = -1; // not found
      AvoirKind = 0;
      isLinkSec = "";
      
      codeMMVB      = ""; 
      codeSPVB      = "";
      codeMicexList = "";
      Type711NumList= "";

      // Данные Securities
      codeNSDSec      = "";
      CodeCntrSec     = "";
      NameSec         = "";
      ISINSec         = "";
      CFISec          = "";
      IssuerNSDSec    = "";
      IssuedSizeSec   = $0.0;
      Type711Sec      = "";
      LSINSec         = "";
      RegDateSec      = ZeroDate;
      RegistrarNSDSec = "";
      MicexListSec    = "";
      IsKISec         = false;
      IsCanceledSec   = false;
      FullNameSec     = "";
      codeMMVBSec     = "";   
      codeSPVBSec     = "";    
   end;

   // Получение подвида ЦБ. 
   // По умолчанию возращает вид ЦБ. Если он другой - ПЕРЕГРУЖАТЬ
   macro GetAvoirKind()
      return (AvoirKind=KindData);
   end;

   // Аннулирован? 
   macro isCanceled():bool
      return ((sec_state_id == 4) or (sec_state_id == 5) or (sec_state_id == 6) or (sec_state_id == 10));
   end;
   // Получить доп. код
   private macro GetObjCode(ObjectType, CodeKind, ID) : string
      var cmd, dataSet;
      var code = "";
     
      cmd = DL_RSDCommand("SELECT * FROM DOBJCODE_DBT " +
                          " WHERE T_OBJECTTYPE = ?" +
                          " AND T_OBJECTID = ?"
                          " AND T_CODEKIND = ?"
                          " AND T_STATE = 0");
      cmd.addParam(ObjectType);
      cmd.addParam(ID);
      cmd.addParam(CodeKind);

      dataSet = cmd.Execute();

      if (dataSet.moveNext())
         code = dataSet.Code;
      end;
      return code;
   end;
   // Получить имя алгоритма из namealg
   private macro GetNameAlg(iTypeAlg:integer, iNumberAlg:integer):string
      var name = "";
      var cmd, dataSet;
      cmd = DL_RSDCommand("SELECT t_sznamealg FROM dnamealg_dbt WHERE t_itypealg = ? AND t_inumberalg = ?");

      cmd.addParam(iTypeAlg);
      cmd.addParam(iNumberAlg);

      dataSet = cmd.Execute();
      if (dataSet.moveNext())
         name = dataSet.sznamealg
      end;
      return name;
   end;

   // -------------------------------
   // поиск объекта Securities
   // -------------------------------
   macro findObject() : integer
      // если аннулирован, то не найден!
      if (isCanceled())
         return 0;
      end;

      var cmd, dataSet;

      var query =  " (SELECT 1 as Num, fin.t_FIID, fin.t_AvoirKind "
                 + "     FROM dfininstr_dbt fin, dobjcode_dbt code "
                 + "   WHERE code.t_ObjectType = " + OBJTYPE_FININSTR 
                 + "       AND code.t_CodeKind = " + FICK_NRD
                 + "       AND code.t_State = 0" 
                 + "       AND code.t_Code = ?"                                                    /*1*/
                 + "       AND fin.t_FIID = code.t_ObjectID "
                 + " ) ";
      // code_nsd всегда должен быть!
      cmd = DL_RSDCommand();
      cmd.addParam(code_nsd);

      if (isin != "")
         // может быть не задан
         query = query + " UNION ALL ("
                       + " SELECT 2 as Num, avr.t_FIID, fin.t_AvoirKind "
                       + "     FROM davoiriss_dbt avr, dfininstr_dbt fin "
                       + "   WHERE avr.t_ISIN = ? "                                                      /*3*/
                       + "       AND fin.t_FIID = avr.t_FIID "
                       + "       AND ROWNUM = 1 "
                       + " ) ";

         cmd.addParam(isin);
      end;

      if (state_reg_number != "")
         // может быть не задан
         query = query + " UNION ALL ("
                       + " SELECT 3 as Num, avr.t_FIID, fin.t_AvoirKind "
                       + "     FROM davoiriss_dbt avr, dfininstr_dbt fin"
                       + "   WHERE avr.t_LSIN = ?"                                                       /*5*/
                       + "       AND fin.t_FIID = avr.t_FIID"
                       + "       AND ROWNUM = 1 "
                       + " )";

         cmd.addParam(state_reg_number);
      end;
      
      query = query + "ORDER BY Num";
      dataSet = cmd.Execute(query);
      if (dataSet.moveNext())
         FIID = dataSet.FIID;
         AvoirKind = dataSet.AvoirKind; // Заполняем подвид бумаги значением из справочника Securities
      else
         FIID = -1; // не найдено
      end;
      
      return FIID;

      onError(er)
         msgbox(cmd.GetErrorString(er));
         return (FIID=-9999); // magic number
   end;

   // вставка в кеш дополнительных кодов
   private macro addRecordCodesToCaches():bool
      var stat = false;
      for (var code, addCodes)
         r_code_tmp.clear();
         r_code_tmp.rec.code_nsd     = code_nsd;
         r_code_tmp.rec.code_type_mn = code.code_type_mn;
         r_code_tmp.rec.code         = code.code;
         stat = dataCodesCache.AddRecord(r_code_tmp);
         if (not stat)
            logger.error(code_nsd,"Ошибка при вставке дополнительных кодов в кэш");
         end;
      end;
      return true; // не вставили код - и ладно, самое главное основные данные!
   end;

   // вставка кэша доп. кодов в БД
   private macro insertCodesCache()
      return dataCodesCache.insert();
   end;

   // Генерация наименование выпуска, шаблон ТТТ - вид ц/б. Перегружена для каждого вида
   private macro GenerateNameAvr_TTT():string
      return "";
   end;
   // Генерация наименование выпуска, шаблон ВВВ - номер выпуска. Перегружена для каждого вида
   private macro GenerateNameAvr_BBB():string
      return "";
   end;
   // Генерация наименование выпуска, шаблон EEE - эмитент, краткое наименование эмитента для всех ценных бумаг, кроме еврооблигаций
   private macro GenerateNameAvr_EEE():string
      var ret = "";
      if (issuer != "")
         var cmd, dataSet;
         var stat = false;

         cmd = DL_RSDCommand("SELECT T_SHORT_NAME FROM dsirnsd_org_tmp WHERE T_CODE_NSD = ? AND "+
                                     // загруженная                 исходная                 обработанная                  удаленная
                              " (T_STATE="+STATE_LOAD+" OR T_STATE="+STATE_SOURCE+" OR T_STATE="+STATE_PROCESSED+" OR T_STATE="+STATE_REMOVED+")");
         cmd.addParam(issuer);

         dataSet = cmd.Execute();
         stat = dataSet.moveNext();
         if (stat)
            ret = dataSet.Short_Name;
         end;
      end;
      return ret;
   end;
   // Генерация наименование выпуска. 
   private macro GenerateNameAvr():string
      return Trim(GenerateNameAvr_TTT() + " " + GenerateNameAvr_BBB() + " " + GenerateNameAvr_EEE());
   end;          

   // заполнение record'а общими данными
   private macro fillRecordAvr():bool
      r_avr_tmp.rec.code_nsd                = code_nsd;
      r_avr_tmp.rec.isin                    = isin;
      r_avr_tmp.rec.cfi                     = cfi;
      r_avr_tmp.rec.sec_form_id             = sec_form_id;
      r_avr_tmp.rec.name_full               = name_full;
      r_avr_tmp.rec.issuer                  = issuer;
      r_avr_tmp.rec.issued_size             = issued_size;
      r_avr_tmp.rec.sec_type_br_code        = sec_type_br_code;
      r_avr_tmp.rec.state_reg_number        = state_reg_number;
      r_avr_tmp.rec.state_reg_date          = state_reg_date;
      r_avr_tmp.rec.sec_state_id            = sec_state_id;
      r_avr_tmp.rec.registrar               = registrar;
      r_avr_tmp.rec.micex_list              = micex_list;
      r_avr_tmp.rec.for_qualified_investors = CnvBoolToChr(for_qualified_investors);
      r_avr_tmp.rec.indexnom                = CnvBoolToChr(indexnom);

      r_avr_tmp.rec.State = STATE_LOAD;

      r_avr_tmp.rec.FIID       = FIID;
      r_avr_tmp.rec.KindData   = KindData;
      r_avr_tmp.rec.Avoir_Kind = GetAvoirKind();
      r_avr_tmp.rec.name       = GenerateNameAvr();
      if (FIID > -1)
         r_avr_tmp.rec.isLinkSec = "X";
      end;
      
      if (FIID > -1)
         var query, cmd, dataSet;

         cmd = DL_RSDCommand(query);

         query = "SELECT code.t_codeKind, code.t_code "
               + "  FROM dobjcode_dbt code "
               + " WHERE code.t_ObjectType = " + OBJTYPE_FININSTR 
               + "   AND code.t_State      = 0" 
               + "   AND code.t_ObjectID   = ?";
         
         cmd.addParam(FIID);

         if(CodeKindIsUnique(OBJTYPE_FININSTR, FICK_NRD) == false)
           query = query + " ORDER BY DECODE(code.t_Code, ?, 0, 1) ASC, code.t_AutoKey ASC ";
           cmd.AddParam(code_nsd);
         else
           query = query + " ORDER BY code.t_AutoKey ASC ";
         end;

         dataSet = cmd.Execute(query);
         while (dataSet.moveNext)
            if ((dataSet.CodeKind == FICK_NRD) and (r_avr_tmp.rec.codeNSDSec == ""))
               r_avr_tmp.rec.codeNSDSec = dataSet.Code;
            elif ((dataSet.CodeKind == FICK_CFIC) and (r_avr_tmp.rec.CFISec == "") )
               r_avr_tmp.rec.CFISec = dataSet.Code;
            end;
         end;
      end;

      return true;

      onError(er)
        if (er.Code == 64)
          logger.error(code_nsd, "Некорректный формат числа. Работа с такими числами не поддерживается.");
        else
          logger.error(code_nsd, "Ошибка при обработке ц/б: " + er.Message);
        end;
        return false;
   end;

   // добавление цб в массив массовых вставок. Перегрузить!
   // ret: true - вставка успешна
   //      false - ошибка
   macro addRecordToCache():bool
      return false;
   end;

   // Вставка кэша в БД.
   // ret: true - вставка успешна
   //      false - ошибка
   macro insertCache():bool
      var stat = false;

      stat = dataAvrCache.insert();

      // вставка кодов
      if (stat) // коды есть допольнительные. Если они не вставились, то работаем дальше
         insertCodesCache();
      end;

      return stat;
   end;

   // получить код Sec биржевой классификации
   private macro GetCodeSecMicexList(micexList)
      var code = "";
      if (micexList == "Первый уровень листинга")
         code = "1.1";
      elif (micexList == "Второй уровень листинга")
         code = "1.2";
      elif (micexList == "Третий уровень листинга")
         code = "1.3";
      elif ((micexList == "Включен в список внебиржевого РЕПО") or (micexList == "Включён в список внебиржевого РЕПО"))
         code = "1.4";
      end;
//      if (micexList == "Внесписочные")
//         code = "1.2";
//      elif (micexList == "Лист А 1 уровень")
//         code = "1.1.1";
//      elif (micexList == "Лист А 2 уровень")
//         code = "1.1.2";
//      elif (micexList == "Лист Б")
//         code = "1.1.3";
//      elif (micexList == "Лист В")
//         code = "1.1.4";
//      elif (micexList == "Лист И")
//         code = "1.1.5";
//      elif (micexList == "Включен в список внебиржевого РЕПО")
//         code = "1.3";
//      elif (micexList == "Торгуются")
//         code = "1.4";
//      end;
      return code;
   end;

   /* Получить номер категории */
   private macro GetNumListObjAttr(groupID:integer, name:string):string
      var cmd, dataSet;
      
      cmd = DL_RSDCommand("SELECT obj.t_NumInList"
                        + "  FROM dobjattr_dbt obj"
                        + " WHERE obj.t_ObjectType = 12" // OBJTYPE_AVOIRISS 
                        + " and obj.t_GroupID = ?"
                        + " and obj.t_name = ?");

      cmd.addParam(groupID);
      cmd.addParam(name);

      dataSet = cmd.Execute();

      if (dataSet.moveNext())
        return dataSet.NumInList;
      end;

      return "";
   end;


   // заполнение данными из временного файла
   macro fillFromTMP(avr_tmp)
      code_nsd                = avr_tmp.rec.code_nsd;
      isin                    = avr_tmp.rec.isin;
      cfi                     = avr_tmp.rec.cfi;
      sec_form_id             = avr_tmp.rec.sec_form_id;
      name_full               = avr_tmp.rec.name_full;
      name                    = avr_tmp.rec.name;
      issuer                  = avr_tmp.rec.issuer;
      issued_size             = avr_tmp.rec.issued_size;
      sec_type_br_code        = avr_tmp.rec.sec_type_br_code;
      Type711NumList          = GetNumListObjAttr(OBJGROUP_AVRTYPE711, sec_type_br_code);
      state_reg_number        = avr_tmp.rec.state_reg_number;
      state_reg_date          = avr_tmp.rec.state_reg_date;
      sec_state_id            = avr_tmp.rec.sec_state_id;
      registrar               = avr_tmp.rec.registrar;
      micex_list              = avr_tmp.rec.micex_list;
      codeMicexList           = GetCodeSecMicexList(micex_list);
      for_qualified_investors = (avr_tmp.rec.for_qualified_investors=="X");
      indexnom                = (avr_tmp.rec.indexnom=="X");

      AvoirKind  = avr_tmp.rec.Avoir_Kind;
      FIID       = avr_tmp.rec.FIID;
      codeNSDSec = avr_tmp.rec.codeNSDSec;
      isLinkSec  = avr_tmp.rec.isLinkSec;
      // Заполнение данными Securities, если есть связь
      if ((isLinkSec == "X") and (FIID > -1))
         var cmd, dataSet;
         cmd = DL_RSDCommand("SELECT fin.t_FI_Code, fin.t_Name, fin.t_Issued, fin.t_Issuer, fin.t_Name, avr.t_ISIN, avr.t_LSIN, avr.t_Qty FROM dfininstr_dbt fin, davoiriss_dbt avr WHERE fin.T_FIID = ? AND avr.T_FIID = fin.T_FIID");
         cmd.addParam(FIID);

         dataSet = cmd.Execute();
         if (dataSet.moveNext())
            CodeCntrSec   = dataSet.FI_Code;
            NameSec       = dataSet.Name;
            ISINSec       = dataSet.ISIN;
            LSINSec       = dataSet.LSIN;
            RegDateSec    = dataSet.Issued;
            IssuedSizeSec = dataSet.Qty;
            IsCanceledSec = isCanceled(); //????
            // коду
            IssuerNSDSec = GetObjCode(OBJTYPE_PARTY, PTCK_NRD, dataSet.Issuer); 
            CFISec       = GetObjCode(OBJTYPE_FININSTR, FICK_CFIC, FIID); 
            codeMMVBSec  = GetObjCode(OBJTYPE_FININSTR, FICK_MICEX, FIID);
            codeSPVBSec  = GetObjCode(OBJTYPE_FININSTR, FICK_SPVB, FIID);
            // категории
            GetMainObjAttr(null, OBJTYPE_AVOIRISS, L_Z(FIID, 10), 15, null, "", MicexListSec);
            GetMainObjAttr(null, OBJTYPE_AVOIRISS, L_Z(FIID, 10),  1, null, "", Type711Sec  );

            var attr = 0;
            GetMainObjAttr(null, OBJTYPE_AVOIRISS, L_Z(FIID, 10), 34, attr );
            if (attr == 1)
               IsKISec = true;
            else
               IsKISec = false;
            end;

            // код НСД субъекта (связанный объект), реестродержатель
            var registrPartyID = -1;
            if ((GetLinkedObject(11, OBJTYPE_AVOIRISS, L_Z(FIID, 10), OBJTYPE_PARTY, registrPartyID)==0) and
                (registrPartyID != -1))
               RegistrarNSDSec = GetObjCode(OBJTYPE_PARTY, PTCK_NRD, registrPartyID); 
            end;

            FullNameSec     = readNoteForObject(OBJTYPE_AVOIRISS, L_Z(FIID, 10), 16);
         end;
      end;
   end;

   macro GetFIIDFromChanges(FIIDChanges:@variant)
      var cmd, dataSet;
      var stat = false;

      cmd = DL_RSDCommand("SELECT T_FIID FROM dsirnsd_avr_tmp WHERE T_CODE_NSD = ? AND T_STATE = "+STATE_SAVEOLD);
      cmd.addParam(code_nsd);

      dataSet = cmd.Execute();
      stat = dataSet.moveNext();
      if (stat)
         FIIDChanges = dataSet.FIID;
      end;

      return stat;
   end;


   // есть ли отличия для обновления?
   macro IsChangesForUpd() : bool
      
      if (isLinkSec == "X")
         var FIIDSource = 0;

         // 1. проверим, не изменяли ли мы данную запись (с исходной)?
         if (GetFIIDFromChanges(FIIDSource))
            if (FIIDSource != 0) // Изменили запись.
               return true;
            elif (FIIDSource == FIID) // Запись не изменили
               return false; 
            end;
         end;

         // 2. Сверяем данные
         return ((ISINSec         != isin                   ) or
                 (CFISec          != cfi                    ) or
                 (IssuerNSDSec    != issuer                 ) or
                 (IssuedSizeSec   != issued_size            ) or
                 (Type711Sec      != Type711NumList         ) or
                 (LSINSec         != state_reg_number       ) or
                 (RegDateSec      != state_reg_date         ) or
                 (RegistrarNSDSec != registrar              ) or
                 (MicexListSec    != codeMicexList          ) or
                 (IsKISec         != for_qualified_investors) //or
                 //(IsCanceledSec   != isCanceled()           ) or
                 //(FullNameSec     != name_full              ) 
                 //(codeMMVBSec     != codeMMVB) кодыы заполняеюются при обновлении
                 //(codeSPVBSec     != codeSPVB)  
                )
      end;
      return false;
   end;
   // есть ли отличия для вставки?
   macro IsChangesForAdd() : bool
      return ((FIID==-1) and (not isCanceled()));
   end;

   private macro InitAvoiriss(fin:TRecHandler, iss:TRechandler)
      return FI_InitAvoiriss(GetAvoirKind(), fin, iss);
   end;

   // ID эмитента\реестродержателя
   macro GetPartyID(codeNsdOrg:string, isIssuer:bool):integer
      // isIssuer - true, если вставляем эмитента
      var org = TOrganizationData(isProtocolExt);
      org.SetIssuer(isIssuer);
      return org.GetPartyIdForAvoiriss(codeNSDOrg);
   end;
   // заполнение буферов выпуска общими данными для вставки\обновления
   macro FillCommonAvoirissSec(fin:TRecHandler, iss:TRechandler, action:integer)
      var issuerID = -1;
      // обновим\добавим эмитента
      if (issuer != "")
        issuerID = GetPartyID(issuer, true);
      end;

      if (action == ACT_INSERT)
         fin.rec.Name   = NameSec = name;//GenerateNameAvr();
         fin.rec.Issuer = issuerID;// GetPartyID(issuer, true);
         var fiCode, fiCodeInAcc, codeRefID, accCodeRefID;
         if (FI_AvoirGenerateCode(fin, iss, fiCode, fiCodeInAcc, codeRefID, accCodeRefID)==0)
            fin.rec.Fi_Code       = fiCode;
            fin.rec.CodeInAccount = fiCodeInAcc;
         end;

         if ((sec_form_id == 1) or (sec_form_id == 3)) // Документарная
            fin.rec.Settlement_Code = 1;
         else
            fin.rec.Settlement_Code = 0; // Бездокументарная
         end;
      elif (action == ACT_UPDATE)
        if ((issuer != "") and (issuerID != -1) and (issuerID != fin.rec.Issuer))
          logger.warning(code_nsd, "Указанный в справочнике НРД эмитент ценной бумаги ("+issuer+") не совпадает с эмитентом, "+
                                   "указанным для ценной бумаги в справочнике RS ("+IssuerNSDSec+")");

        end;
      end;
      
      fin.rec.Issued = state_reg_date;
      iss.rec.LSIN = state_reg_number;
      iss.rec.ISIN = isin;
      iss.rec.FIID = FIID;
      iss.rec.Qty  = issued_size;
      iss.rec.IndexNom = CnvBoolToChr(indexnom);

   end;

   // Получение кода Securities по доп. коду НРД.
   // Перегружено для видов выпуска
   private macro GetCodesSec(CodeTypeMN, code):String
      var codeSec = "";
      if ("MICEX_FOND" == CodeTypeMN)
         codeSec = FICK_MICEX; // Код ММВБ (фонд. секция)
         codeMMVB = code; 
      end;
      return codeSec;
   end;

   // вставка доп. кода
   private macro InsertObjCode(CodeKind, ID, code)
      var cmd, dataSet, query;
      var insQuery = "", InsCmd;
      var stat = 0;
      var isUnique = IIF(CodeKindIsUnique(OBJTYPE_FININSTR, CodeKind) == true, "CHR(88)", "CHR(0)");

      // 1. Проверим, а не устанволен ли данный код на другой ЦБ
      if (CodeKind != FICK_CFIC) // CFI может дублироваться
         cmd = DL_RSDCommand("SELECT obj.*, fin.T_FI_Code FROM DOBJCODE_DBT obj, dfininstr_dbt fin " +
                             " WHERE obj.T_OBJECTTYPE = " + OBJTYPE_FININSTR +
                             " AND obj.T_CODEKIND = ?"+
                             " AND obj.T_CODE = ?"+
                             " AND obj.T_STATE = 0" +
                             " AND fin.T_FIID = obj.T_OBJECTID "
                             );
         cmd.addParam(CodeKind);
         cmd.addParam(code);

         DataSet = cmd.Execute();

         if (DataSet.moveNext() and (dataSet.ObjectID != ID))
            logger.error(code_nsd, "Код " + code + " установлен на ц.б. "+dataSet.FI_Code);
            stat = 1;
         end;
      end;
      if (stat == 0)
         cmd = DL_RSDCommand();

         query = "SELECT * FROM DOBJCODE_DBT " +
                 " WHERE T_OBJECTTYPE = " + OBJTYPE_FININSTR +
                 " AND T_OBJECTID = ?" +
                 " AND T_CODEKIND = ?" +
                 " AND T_STATE = 0";
         
         cmd.addParam(ID);
         cmd.addParam(CodeKind);

         if((CodeKind == FICK_NRD) AND (CodeKindIsUnique(OBJTYPE_FININSTR, FICK_NRD) == false)) //Кодов в НРД на ФИ может быть несколько, поэтому проверяем, что нет точно такого же
           query = query + " AND T_CODE = ? ";

           cmd.addParam(trim(code));
         end;

         DataSet = cmd.Execute(query);

         if (DataSet.moveNext())
            if (dataSet.Code != code)
              insQuery = "UPDATE DOBJCODE_DBT SET T_CODE = '" + code + "', T_STATE = 0, T_SYSDATE = TO_DATE(TO_CHAR(CURRENT_DATE, 'DD.MM.YYYY'), 'DD.MM.YYYY'), " +
                         "T_SYSTIME = TO_DATE('01.01.0001' || TO_CHAR(CURRENT_DATE, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS'), " +
                         "T_USERID = " + {oper} + " WHERE T_OBJECTTYPE = "+OBJTYPE_FININSTR+" AND T_CODEKIND = " + CodeKind + " " +
                         "AND T_OBJECTID = " + ID + "AND T_STATE = 0";
           end;
         else
            insQuery = "INSERT INTO DOBJCODE_DBT (T_OBJECTTYPE, T_CODEKIND, T_OBJECTID, T_CODE, T_STATE, T_BANKDATE, T_SYSDATE, T_SYSTIME, T_USERID, T_UNIQUE, T_BANKCLOSEDATE)" +
                       "VALUES ("+OBJTYPE_FININSTR+", " + CodeKind + ", " + ID + ", '" + code + "', 0, " +
                       "TO_DATE('" + {curdate} + "', 'DD.MM.YYYY'), TO_DATE(TO_CHAR(CURRENT_DATE, 'DD.MM.YYYY'), 'DD.MM.YYYY'), " +
                       "TO_DATE('01.01.0001 ' || TO_CHAR(CURRENT_DATE, 'HH24:MI:SS'), 'DD.MM.YYYY HH24:MI:SS'), " + {oper} + ", "+isUnique+", TO_DATE('01.01.0001', 'DD.MM.YYYY'))";
         end;
      end;
      if (insQuery != "")
         insCmd = RSDCommand(insQuery);
         insCmd.execute();
      end;

      return stat;
      onError(e)
         return 1; 
   end;

   private macro L_Z( num, len )
      var str1, len1;
      str1 = trim(string(num));
      len1 = strlen(str1);
      if (len1 >= len) return str1;
      else return mkstr("0", len-len1) + str1;
      end;
   end;
   
   // вставка категорий, примечаний и т.п. (после действия над выпуском)
   macro AfterAction(fin, iss, action)
      if (isCanceled())
         logger.warning(code_nsd,"Данный выпуск аннулирован в НРД");
      end;
      // После обработки основных данных!
      if (InsertObjCode(FICK_NRD, fin.rec.FIID, code_nsd) != 0)
         logger.error(code_nsd, "Ошибка при сохранении кода \"Код НРД\"");
      end;

      if (cfi != "")
         if (InsertObjCode(FICK_CFIC, fin.rec.FIID, cfi) != 0)
            logger.error(code_nsd, "Ошибка при сохранении кода \"Код CFI\"");
         end;
      end;

      // sec_type_br_code - категория Код типа ц.б. для формы 711
      if ((sec_type_br_code != "") and (Type711NumList != Type711Sec))
         if (Type711NumList != "")
            if (not ConnectObjAttr(null, OBJTYPE_AVOIRISS, L_Z(fin.rec.FIID, 10), 1, null, "", Type711NumList))
               logger.error(code_nsd, "Ошибка при сохранении категории \"Код типа ц.б. для формы 711\"");
            end;
         else
            logger.error(code_nsd, "Неизвестный код для формы 711: " + sec_type_br_code);
         end;
      end;

      // micex_list - категория Биржевая классификация 
      if ((micex_list != "") and (codeMicexList != "") and (codeMicexList != MicexListSec))
         if (not ConnectObjAttr( null, OBJTYPE_AVOIRISS, L_Z(fin.rec.FIID, 10), 15, null, "", codeMicexList))
            logger.error(code_nsd, "Ошибка при установке категории \"Биржевая классификация\"");
         end;
      end;

      // for_qualified_investors - категория Принадлежность КИ
      if (for_qualified_investors != IsKISec)
         var attr = 0;
         if (for_qualified_investors)
            attr = 1;
         else
            attr = 2;
         end;
         if (not ConnectObjAttr( null, OBJTYPE_AVOIRISS, L_Z(fin.rec.FIID, 10), 34, attr ))
            logger.error(code_nsd, "Ошибка при установке категории \"Принадлежность КИ\"");
         end;
      end;
      // name_full - примечание
      if ((action == ACT_INSERT) and (name_full != "") and
          (AddNoteForObject(OBJTYPE_AVOIRISS, L_Z(fin.rec.FIID, 10), 16, name_full) != 0))
         logger.error(code_nsd, "Ошибка при установке примечания \"Полное официальное наименование\"");
      end;
      // registrar - связ. объект GetPartyID(registrar);
      if ((registrar != "") and (registrar != RegistrarNSDSec)) // обновляем если есть реестродержатель и отличается
         var partReg = GetPartyID(registrar, false);
         if (partReg != 0) 
            if (SetLinkedObject(11, OBJTYPE_AVOIRISS, iss, OBJTYPE_PARTY, String(partReg), true) != 0)
               logger.error(code_nsd, "Ошибка при установке связанного объекта \"Реестродержатель ц/б\"");
            end;
         end;
      end;
      // Дополнительные коды
      var cmdCodes, dataSetCodes;
      var doc = null;
      var codesSec = "";

      cmdCodes = DL_RSDCommand("SELECT * FROM dsirnsd_addCodes_tmp WHERE t_code_nsd = ?");
      cmdCodes.addParam(code_nsd);

      dataSetCodes = cmdCodes.execute();
      while (dataSetCodes.moveNext())
         codesSec = GetCodesSec(dataSetCodes.code_type_mn, dataSetCodes.code);
         
         if (codesSec != "")
            if (InsertObjCode(codesSec, fin.rec.FIID, dataSetCodes.code) != 0)
               logger.error(code_nsd, "Ошибка при сохранении дополнительного кода " + dataSetCodes.code);
            end;
         end;         
      end;
   end;
   // Действия после сохранения данных в отчет
   private macro AfterSaveDataForReport(fin, iss, dataRep, action)
      return true;
   end;

   // Вид ценной бумаги
   private macro GetAvoirKindName()
      var cmd, dataSet;
      var stat = false;

      cmd = DL_RSDCommand("select t_NAME from davrkinds_dbt where t_FI_Kind = 2 and t_AvoirKind = ? ");
      cmd.addParam(GetAvoirKind());

      dataSet = cmd.Execute();
      if (dataSet.moveNext())
         return dataSet.Name;
      end;

      return "";
      onError(e)
         logger.error(code_nsd, "Ошибка при поиске вида выпуска: " + cmd.GetErrorString(e));
         return "";
   end;

   // Заполнение объекта для отчета общими данными.
   // Перегружена
   private macro FillDataForReport(avr, action)
      var precision    = ЧислоЗнаковПослеЗапятой(issued_size);
      
      avr.ID            = FIID;
      avr.CodeCntr      = CodeCntrSec;
      avr.CodeNSD       = code_nsd;
      avr.ISIN          = isin;
      avr.CFI           = cfi;
      avr.IssuerNSD     = issuer;
      avr.IssuedSize    = ЧислоCЗаданнойТочностью(issued_size,precision);
      avr.Type          = sec_type_br_code;
      avr.KindAvoir     = GetAvoirKindName();
      avr.Name          = NameSec;
      avr.LSIN          = state_reg_number;
      avr.RegDate       = state_reg_date;
      avr.RegistrarNSD  = registrar;
      avr.MicexList     = GetNameCateg(OBJTYPE_AVOIRISS, codeMicexList, 15);
      avr.IsKI          = for_qualified_investors;
      avr.IsCanceled    = isCanceled();
      avr.FullName      = name_full;
      avr.CodeMMVB      = codeMMVB; //!!!
      avr.CodeSPVB      = codeSPVB; //!!!

      if ((action == ACT_UPDATE) or (action == ACT_MERGE))
         var precisionSec = ЧислоЗнаковПослеЗапятой(IssuedSizeSec);

         avr.rsCodeNSD      = codeNSDSec;
         avr.rsISIN         = ISINSec;
         avr.rsCFI          = CFISec;
         avr.rsIssuerNSD    = IssuerNSDSec;
         avr.rsIssuedSize   = ЧислоCЗаданнойТочностью(IssuedSizeSec,precisionSec);
         avr.rsType         = GetNameCateg(OBJTYPE_AVOIRISS, Type711Sec, 1);
         avr.rsLSIN         = LSINSec;
         avr.rsRegDate      = RegDateSec;
         avr.rsRegistrarNSD = RegistrarNSDSec;
         avr.rsMicexList    = GetNameCateg(OBJTYPE_AVOIRISS, MicexListSec, 15);
         avr.rsIsKI         = IsKISec;
         avr.rsIsCanceled   = IsCanceledSec;
         avr.rsFullName     = FullNameSec;
         avr.rscodeMMVB     = codeMMVB;//codeMMVBSec; найти значение кода!
         avr.rscodeSPVB     = codeSPVB;//codeSPVBSec;
      end;
   end;

   // Создание и заполнение объекта для очета
   private macro CreateDataForRep(action):TAvrNSDRepData
      var avr = null;
      if (ACT_INSERT == action)
         avr = TAvrNSDRepData();
      else
         avr = TAvrRepData();
      end;
      FillDataForReport(avr, action);
      return avr;
   end;

   // Установить признак обработанной записи
   macro SetProcessedRec()
      // Обновим статус, дабы ее не трогали больше
      var cmd = RSDCommand("UPDATE dsirnsd_avr_tmp SET T_STATE = ? WHERE T_CODE_NSD = ?");
      cmd.addParam("", RSDBP_IN, STATE_PROCESSED);
      cmd.addParam("", RSDBP_IN, code_nsd);

      cmd.Execute();

      // только если запись не для вставки
      if ((isProtocolExt) and (isLinkSec != ""))
         SaveAvrUpdDataForReport(CreateDataForRep(ACT_UPDATE));
      end;
   end;

   private macro UpdateAvoiriss(fin, iss)
      dataMsgBoxAvoir = DataForMsgBox(code_nsd, logger);
      ReplaceMacro("msgbox", "Avoir_msgbox");
      var stat = FI_UpdateAvoiriss(fin, iss);
      ReplaceMacro("msgbox", NULL);
      return stat;
   end;

   // Обновление ц/б
   macro Update()
      var f_fin = TBFile("fininstr.dbt");
      var f_iss = TBFile("avoiriss.dbt");
      var r_fin = TRecHandler("fininstr.dbt");
      var r_iss = TRecHandler("avoiriss.dbt");
      var stat = 0;

      f_fin.rec.FIID = FIID;
      if (not f_fin.getEQ())
         logger.error(code_nsd, "Ошибка при поиске фин.инструмента FIID="+FIID);
         return 1;
      end;
      f_iss.rec.FIID = FIID;
      if (not f_iss.getEQ())
         logger.error(code_nsd, "Ошибка при поиске ц/б FIID="+FIID);
         return 1;
      end;

      copy(r_fin, f_fin);
      copy(r_iss, f_iss);

//      RslDefCon.BeginTrans();
   
      FillCommonAvoirissSec(r_fin, r_iss, ACT_UPDATE);

      stat = UpdateAvoiriss(r_fin, r_iss);

      // обновление категорий\примечаний\....
      if (stat == 0)
         AfterAction(r_fin, r_iss, ACT_UPDATE);
         var dataRep = CreateDataForRep(ACT_UPDATE);
         SaveAvrUpdDataForReport(dataRep);

         AfterSaveDataForReport(r_fin, r_iss, dataRep, ACT_UPDATE);
      end;

//      if(stat == 0)
//         RslDefCon.CommitTrans();
//      else
//         RslDefCon.RollbackTrans();
      if (stat != 0)
         logger.error(code_nsd, "Ошибка при обновлении ц/б. " + GetErrorMsg(stat));
      end;

      return stat;

      onError(e)
//         RslDefCon.RollbackTrans();
         logger.error(code_nsd, "Ошибка при обновлении ц/б. " + e.message);
         return 1;
   end;
   
   private macro InsertAvoiriss(fin, iss)
      dataMsgBoxAvoir = DataForMsgBox(code_nsd, logger);
      ReplaceMacro("msgbox", "Avoir_msgbox");
      var stat = FI_InsertAvoiriss(fin, iss, null, null, true);
      ReplaceMacro("msgbox", NULL);
      return stat;
   end;
   // Вставка цб
   macro Insert()
      var r_fin = TRecHandler("fininstr.dbt");
      var r_iss = TRecHandler("avoiriss.dbt");
      var stat = 0;

//      RslDefCon.BeginTrans();
      r_fin.rec.FI_Kind   = FIKIND_AVOIRISS;
      r_fin.rec.AvoirKind = GetAvoirKind();

      InitAvoiriss(r_fin, r_iss);
      
      FillCommonAvoirissSec(r_fin, r_iss, ACT_INSERT);
      stat = InsertAvoiriss(r_fin, r_iss);
      // сохраняем данные для послед. использование
      FIID        = r_fin.rec.FIID;
      CodeCntrSec = r_fin.rec.FI_Code;
      // обновление категорий\примечаний\....
      if (stat == 0)
         AfterAction(r_fin, r_iss, ACT_INSERT);
         var dataRep = CreateDataForRep(ACT_INSERT);
         SaveAvrDataForReport(dataRep);
         AfterSaveDataForReport(r_fin, r_iss, dataRep, ACT_INSERT);
      end;

//      if(stat == 0)
//         RslDefCon.CommitTrans();
//      else
//         RslDefCon.RollbackTrans();
      if (stat != 0)
         logger.error(code_nsd, "Ошибка при создании ц/б. " + GetErrorMsg(stat));
      end;
      return stat;
      onError(e)
//         RslDefCon.RollbackTrans();
         logger.error(code_nsd, "Ошибка при создании ц/б. " + e.message);
         return 1;                               
   end;

   macro ActionOnRec()
      var OldDialogFlag = SetDialogFlag(0);
      var cmd;
      var stat = 0;
      var query, ds, sel;
      var FindFIID = -1;
      var Action = IIF(isLinkSec == "", ACT_INSERT, ACT_UPDATE);

      SetGroupModeFI(true); // установим групповой режим, чтобы лишнее не спрашивать
      
      if((isin != "") and (CodeKindIsUnique(OBJTYPE_FININSTR, FICK_NRD) == false))
        query =   " select t_FIID "
                + "   from dsirnsd_avr_tmp "
                + "  where t_State = " + STATE_PROCESSED
                + "    and t_ISIN = ? "
                + "    and t_Code_NSD <> ? "
                + "    and t_FIID > 0 ";

        sel = DL_RSDCommand(query);
        sel.AddParam(isin);
        sel.AddParam(code_nsd);

        ds = sel.Execute();
        if(ds.moveNext())
          FindFIID = ds.FIID;
        end;
      end;

      if(FindFIID > 0)
        var f_fin = TBFile("fininstr.dbt");
        var f_iss = TBFile("avoiriss.dbt");
        
        f_fin.rec.FIID = FindFIID;
        if (not f_fin.getEQ())
           logger.error(code_nsd, "Ошибка при поиске фин.инструмента FIID="+FindFIID);
           stat = 1;
        end;

        if(stat == 0)
          f_iss.rec.FIID = FindFIID;
          if (not f_iss.getEQ())
             logger.error(code_nsd, "Ошибка при поиске ц/б FIID="+FindFIID);
             stat = 1;
          end;
        end;

        if(stat == 0)
          FIID        = f_fin.rec.FIID;
          CodeCntrSec = f_fin.rec.FI_Code;
          NameSec     = f_fin.rec.Name; 

          if (InsertObjCode(FICK_NRD, FindFIID, code_nsd) != 0)
            logger.error(code_nsd, "Ошибка при сохранении кода \"Код НРД\"");
            stat = 1;
          end;
        end;
        
        if(stat == 0)
          var dataRep = CreateDataForRep(Action);
          if(Action == ACT_INSERT)
            SaveAvrDataForReport(dataRep);
          else
            SaveAvrUpdDataForReport(dataRep);
          end;

          AfterSaveDataForReport(f_fin, f_iss, dataRep, Action);
        end;

      else
        if (Action == ACT_INSERT)
           stat = Insert();
        else
           if ((codeNSDSec != "" ) and (code_nsd != codeNSDSec))
              logger.error(code_nsd,"Отличаются коды НРД в справочнике НРД и RS. Выпуск не обновляется");
           else
              stat = Update();
           end;
        end;
      end;

      if (stat == 0)
         // Обновим статус, дабы ее не трогали больше
         cmd = RSDCommand("UPDATE dsirnsd_avr_tmp SET T_STATE = ?, T_FIID = ? WHERE T_CODE_NSD = ?");
         cmd.addParam("", RSDBP_IN, STATE_PROCESSED);
         cmd.addParam("", RSDBP_IN, FIID); // обновим и FIID (для вставки его надо обновлять)
         cmd.addParam("", RSDBP_IN, code_nsd);

         cmd.Execute();
      end;
      SetGroupModeFI(false);
      SetDialogFlag(OldDialogFlag);
   end;
   // Действие над записью. Сравнение
   macro ActionOnMerge()
      var dataRep = CreateDataForRep(ACT_MERGE);
      SaveAvrUpdDataForReport(dataRep);
      AfterSaveDataForReport(null, null, dataRep, ACT_MERGE);
   end;
end;
// *************************************
// Облигации
// *************************************
class (TAvoirData)TBondData(isProtocolExt_)
   var sec_type_id:integer,           // Значение категории "Тип ценной бумаги "
       issue_number:string,           // Порядковый номер выпуска
       start_placement_date:date,     // Дата начала размещения
       end_placement_date:date,       // Дата окончания размещения
       face_value:numeric,            // Номинал
       maturity_value:numeric,        // Текущий номинал
       currency_code:string,          // Валюта номинала (3-х буквенный код) 
       expiry_date_calc:date,         // Дата погашения
       first_coupon_date:date,        // Дата начала начисления первого купона
       coupon_period_base_id:integer, // Метод исчисления купонного дохода
       bond_series:string,            // Серия облигации
       rate_type_id:integer;          // Тип ставки  
   // Данные Securities
   var IssueSec:string,
       StartDateSec:date,
       EndDateSec:date,
       FaceValueSec:numeric,
       MaturityValueSec:numeric,
       FaceValueCurSec:string,
       ExpiryDateSec:date,
       NKDBaseKindSec:integer, 
       SeriesSec:string;

   var couponsData:TArray    = TArray(500,500); // массив купонов
   var partsData:TArray      = TArray(500,500); // массив ЧП
   var facevaluesData:TArray = TArray(500,500); // массив ЧП
   var dataWarntCache        = RsbSQLInsert(SIRNSD_WARNT_TABLE);
   var r_warnt_tmp           = TRecHandler(SIRNSD_WARNT_TABLE);
   var dataFaceValueCache    = RsbSQLInsert(SIRNSD_FACEVALUE_TABLE);
   var r_facevalue_tmp       = TRecHandler(SIRNSD_FACEVALUE_TABLE);
   const TYPE_COUPON = 1, // тип купон
         TYPE_PART   = 2; // тип ЧП


   macro clear()
      clear(); // базовый
      sec_type_id           = 0;
      issue_number          = "";
      start_placement_date  = ZeroDate;
      end_placement_date    = ZeroDate;
      face_value            = $0.0;
      maturity_value        = $0.0;
      currency_code         = "";
      expiry_date_calc      = ZeroDate;
      first_coupon_date     = ZeroDate;
      coupon_period_base_id = 0;
      bond_series           = "";
      rate_type_id          = 0;

      couponsData.size    = 0;
      partsData.size      = 0;
      facevaluesData.size = 0;
      // ДАнные Securities
      IssueSec         = "";
      StartDateSec     = ZeroDate;
      EndDateSec       = ZeroDate;
      FaceValueSec     = $0.0;
      MaturityValueSec = $0.0;
      FaceValueCurSec  = "";
      ExpiryDateSec    = ZeroDate;
      NKDBaseKindSec   = 0;
      SeriesSec        = "";
   end;

   // -------------------------------
   // получение подвида цб
   // М.б. перегружена!
   // -------------------------------
   macro GetAvoirKind()
      if (AvoirKind == 0)
         if (sec_type_br_code == "BON1")
            if (currency_code == NAT_CUR_STR)
               AvoirKind = AVOIRISSKIND_BOND_OFZ;
            else 
               AvoirKind = AVOIRISSKIND_BOND_EVRO;
            end;
         else
            // Ищем эмитента
            var cmd, dataSet;
            var comp_type_id = 0, isBank = "", isBankNonRes = "", countryOrg = "";

            cmd = DL_RSDCommand("SELECT t_comp_type_id, t_is_bank_4_non_resident, t_Credit_cmp, t_country FROM dsirnsd_org_tmp WHERE T_CODE_NSD = ? AND "+
                                       // загруженная                 исходная                 обработанная                  удаленная
                                " (T_STATE="+STATE_LOAD+" OR T_STATE="+STATE_SOURCE+" OR T_STATE="+STATE_PROCESSED+" OR T_STATE="+STATE_REMOVED+")");
            cmd.addParam(issuer);

            dataSet = cmd.Execute();
            if (dataSet.moveNext())
               comp_type_id = dataSet.comp_type_id;
               isBankNonRes = dataSet.is_bank_4_non_resident;
               isBank       = dataSet.t_Credit_cmp;
               countryOrg   = dataSet.country;
            end;

            if ((sec_type_br_code == "BON2") and (currency_code == NAT_CUR_STR))
               if (comp_type_id == 20) // муниципальное учреждение
                  AvoirKind = AVOIRISSKIND_BOND_MUNICIPAL_BOND;
               elif (comp_type_id == 15)//Государственная корпорация
                  AvoirKind = AVOIRISSKIND_BOND_CORPORATE_BOND;
               else //муниципальное предприятие НЕИЗВЕСТЕН КОД!
                  AvoirKind = AVOIRISSKIND_BOND_PARTY_BOND;
               end;
            elif (sec_type_br_code == "BON3" ) 
               AvoirKind = AVOIRISSKIND_BOND_CORPORATE_BOND;
            elif (sec_type_br_code == "BON4") 
               AvoirKind = AVOIRISSKIND_BOND_CORPORATE_BOND;
            elif (sec_type_br_code == "BON5")
               AvoirKind = AVOIRISSKIND_BOND_FOREIGN_FED;
            elif ( (sec_type_br_code == "BON6") or (sec_type_br_code == "BON61") or (sec_type_br_code == "BON7") or (sec_type_br_code == "BON71") )
               AvoirKind = AVOIRISSKIND_BOND_CORPORATE_EVRO;
            end;
         end;
      end;
      return AvoirKind;
   end;

   // вставка в кеш купонов и ЧП
   private macro addRecordWarntToCaches():bool
      var stat = false;

      // Купоны
      for (var coupon, couponsData)
         r_warnt_tmp.clear();
         r_warnt_tmp.rec.code_nsd          = code_nsd;
         r_warnt_tmp.rec.type              = TYPE_COUPON;
         r_warnt_tmp.rec.payment_date_calc = coupon.payment_date_calc;
         r_warnt_tmp.rec.date_fact         = coupon.payment_date_fact;
         r_warnt_tmp.rec.record_date_calc  = coupon.record_date_calc;
         r_warnt_tmp.rec.rate              = coupon.interest_rate;
         r_warnt_tmp.rec.size              = coupon.size;
         r_warnt_tmp.rec.lock_date_calc    = coupon.lock_date_calc;
         r_warnt_tmp.rec.number            = coupon.coupon_number;
         r_warnt_tmp.rec.period_from_calc  = coupon.period_from_calc;

         stat = dataWarntCache.AddRecord(r_warnt_tmp);
         if (not stat)
            logger.error(code_nsd,"Ошибка при вставке купонов в кэш");
         end;
      end;
      // ЧП
      for (var part, partsData)
         r_warnt_tmp.clear();
         r_warnt_tmp.rec.code_nsd          = code_nsd;
         r_warnt_tmp.rec.type              = TYPE_PART;
         r_warnt_tmp.rec.number            = part.pp_number;
         r_warnt_tmp.rec.action_type_code  = part.action_type_code;
         r_warnt_tmp.rec.rate              = part.payment_size_percent;
         r_warnt_tmp.rec.payment_date_calc = part.payment_date_calc;
         r_warnt_tmp.rec.date_fact         = part.payment_date_fact;
         r_warnt_tmp.rec.record_date_calc  = part.record_date_calc;

         stat = dataWarntCache.AddRecord(r_warnt_tmp);
         if (not stat)
            logger.error(code_nsd,"Ошибка при вставке ЧП в кэш");
         end;
      end;

      return true; // не вставили код - и ладно, самое главное основные данные!
   end;
   // вставка кэша купонов и ЧП в БД
   private macro insertWarntCache()
      return dataWarntCache.insert();
   end;

   // вставка кэша номиналов
   private macro insertFaceValueCache()
      return dataFaceValueCache.insert();
   end;

   // добавление цб в массив массовых вставок.
   // ret: true - вставка успешна
   //      false - ошибка
   macro addRecordToCache():bool
      var stat = false;
      r_avr_tmp.clear();
      // 1. заполним основными данными
      stat = fillRecordAvr();

      if (stat)
         // заполним своими
         r_avr_tmp.rec.sec_type_id           = sec_type_id;
         r_avr_tmp.rec.issue_number          = issue_number;
         r_avr_tmp.rec.start_placement_date  = start_placement_date;
         r_avr_tmp.rec.end_placement_date    = end_placement_date;
         r_avr_tmp.rec.face_value            = face_value;
         r_avr_tmp.rec.maturity_value        = maturity_value;
         r_avr_tmp.rec.currency_code         = currency_code;
         r_avr_tmp.rec.expiry_date_calc      = expiry_date_calc;
         r_avr_tmp.rec.first_coupon_date     = first_coupon_date;
         r_avr_tmp.rec.coupon_period_base_id = coupon_period_base_id;
         r_avr_tmp.rec.bond_series           = bond_series;
         r_avr_tmp.rec.rate_type_id          = rate_type_id;

         stat = dataAvrCache.AddRecord(r_avr_tmp);
      end;
      if (stat)
         addRecordCodesToCaches();
      end;
      // вставка купонов и ЧП
      if (stat)
         addRecordWarntToCaches();
      end;

      if(stat)
        //Номиналы
        for (var fv, facevaluesData)
           r_facevalue_tmp.clear();
           r_facevalue_tmp.rec.code_nsd       = code_nsd;
           r_facevalue_tmp.rec.facevalue_date = fv.facevalue_date;
           r_facevalue_tmp.rec.facevalue      = fv.facevalue;

           stat = dataFaceValueCache.AddRecord(r_facevalue_tmp);
           if (not stat)
              logger.error(code_nsd,"Ошибка при вставке номинала в кэш");
           end;
        end;
      end;

      return stat;
   end;

   // Вставка кэша в БД.
   // ret: true - вставка успешна
   //      false - ошибка
   macro insertCache():bool
      var stat = false;

      stat = insertCache();

      if (stat)
         stat = insertWarntCache();
      end;
      
      if (stat)
        stat = insertFaceValueCache();
      end;

      return stat;
   end;

   // Получение кода Securities по доп. коду НРД.
   // Перегружено для видов выпуска
   private macro GetCodesSec(CodeTypeMN, code):String
      var codeSec = GetCodesSec(CodeTypeMN);

      if ("PM00205R0000" == CodeTypeMN)
         codeSec = FICK_SPVB; // Код СПВБ
         codeSPVB = code;
      end;
      return codeSec;
   end;

   macro fillFromTMP(avr_tmp)
      var cmd, dataSet;

      fillFromTMP(avr_tmp);

      sec_type_id           = avr_tmp.rec.sec_type_id;
      sec_form_id           = avr_tmp.rec.sec_form_id;
      issue_number          = avr_tmp.rec.issue_number;
      start_placement_date  = avr_tmp.rec.start_placement_date;

      if(state_reg_date == ZeroDate)
        state_reg_date = start_placement_date;
      end;

      end_placement_date    = avr_tmp.rec.end_placement_date;
      face_value            = avr_tmp.rec.face_value;
      maturity_value        = avr_tmp.rec.maturity_value;
      currency_code         = avr_tmp.rec.currency_code;
      expiry_date_calc      = avr_tmp.rec.expiry_date_calc;
      first_coupon_date     = avr_tmp.rec.first_coupon_date;
      coupon_period_base_id = avr_tmp.rec.coupon_period_base_id;
      bond_series           = avr_tmp.rec.bond_series;
      rate_type_id          = avr_tmp.rec.rate_type_id;

      // Заполнение данными Securities, если есть связь
      if ((isLinkSec == "X") and (FIID > -1))
         cmd = DL_RSDCommand("SELECT fin.t_DrawingDate, fin.t_FaceValue, avr.t_Issue, avr.t_Series, avr.t_BegPlacementDate, avr.t_EndPlacementDate, finCur.t_ccy, avr.t_NKDBase_Kind " +
                             " FROM dfininstr_dbt fin, davoiriss_dbt avr, dfininstr_dbt finCur WHERE fin.T_FIID = ? AND avr.T_FIID = fin.T_FIID AND finCur.t_FIID = fin.t_FaceValueFI");

         cmd.addParam(FIID);

         dataSet = cmd.Execute();
         if (dataSet.moveNext())
            IssueSec         = dataSet.Issue;
            StartDateSec     = dataSet.BegPlacementDate;
            EndDateSec       = dataSet.EndPlacementDate;
            FaceValueSec     = dataSet.FaceValue;
            FaceValueCurSec  = dataSet.CCY;
            ExpiryDateSec    = dataSet.DrawingDate;
            SeriesSec        = dataSet.Series;
            NKDBaseKindSec   = dataSet.NKDBase_Kind;

            MaturityValueSec = maturity_value; //!!!!
         end;
      end;

      // Заполнение массивов купонов и ЧП
      couponsData.size = 0;
      partsData.size   = 0;
      var data = null;
      
      cmd = DL_RSDCommand("SELECT * FROM dsirnsd_warnt_tmp WHERE T_CODE_NSD = ? AND T_TYPE = ? ORDER BY T_NUMBER");

      cmd.addParam(code_nsd);
      cmd.addParam(TYPE_COUPON);
      dataSet = cmd.Execute();
      while (dataSet.moveNext())
         data = TCouponData();
         data.payment_date_calc = dataSet.payment_date_calc;
         data.payment_date_fact = dataSet.date_fact;
         data.record_date_calc  = dataSet.record_date_calc;
         data.interest_rate     = dataSet.rate;
         data.size              = dataSet.size;
         data.lock_date_calc    = dataSet.lock_date_calc;
         data.coupon_number     = dataSet.number;
         data.period_from_calc  = dataSet.period_from_calc;
         couponsData[couponsData.size] = data;
      end;
      cmd = null;
      dataSet = null;

      // ЧП немного по-другому формируем
      //cmd = DL_RSDCommand("SELECT * FROM dsirnsd_warnt_tmp WHERE T_CODE_NSD = ? AND T_TYPE = ? ORDER BY T_NUMBER");
      // сначала формируем по DRAW, потом по REDM/BN, потмо по остальным
      cmd = DL_RSDCommand("SELECT t.*, decode(T_ACTION_TYPE_CODE, 'DRAW', 1, decode(T_ACTION_TYPE_CODE, 'REDM/BN', 2, 3)) as t_SortActionTypeCode"
                         +"  FROM dsirnsd_warnt_tmp t WHERE T_CODE_NSD = ? AND T_TYPE = ?"
                         +" ORDER BY t.T_CODE_NSD, t_SortActionTypeCode,  t.T_NUMBER"
                         );

      cmd.addParam(code_nsd);
      cmd.addParam(TYPE_PART);
      dataSet = cmd.Execute();
      while (dataSet.moveNext())
         data = TPartialData();
         data.pp_number            = dataSet.number;
         data.action_type_code     = DataSet.action_type_code;
         data.payment_size_percent = dataSet.rate;
         data.payment_date_calc    = dataSet.payment_date_calc;
         data.payment_date_fact     = dataSet.date_fact;
         data.record_date_calc     = dataSet.record_date_calc;
         partsData[partsData.size] = data;
      end;
      cmd = null;
      dataSet = null;
      
      // Заполнение массива номиналов
      facevaluesData.size   = 0;

      cmd = DL_RSDCommand("SELECT * FROM dsirnsd_facevalue_tmp WHERE T_CODE_NSD = ? ORDER BY T_FACEVALUE_DATE");

      cmd.addParam(code_nsd);
      dataSet = cmd.Execute();
      while (dataSet.moveNext())
         data = TFaceValueData();
         data.facevalue_date = date(dataSet.facevalue_date);
         data.facevalue      = dataSet.facevalue;
         facevaluesData[facevaluesData.size] = data;
      end;

   end;

   // Еврооблигация?
   private macro IsEuroBond():bool
      return ((AvoirKind == AVOIRISSKIND_BOND_PARTY_EVRO    ) or
              (AvoirKind == AVOIRISSKIND_BOND_MUNICIPAL_EVRO) or
              (AvoirKind == AVOIRISSKIND_BOND_CORPORATE_EVRO)  );
   end;

   // Генерация наименование выпуска. Перегружена для каждого вида
   private macro GenerateNameAvr_TTT():string
      // Еврооблигация - Еврооблигация (любой подвид: федеральная, муниципальная, корпоративная)
      if (IsEuroBond())
         return "Еврооблигация";
      else
         return "обл";
      end;
   end;

   // Генерация наименование выпуска, шаблон ВВВ - номер выпуска. Перегружена для каждого вида
   private macro GenerateNameAvr_BBB():string
      return issue_number;
   end;

   // Генерация наименование выпуска, шаблон EEE - эмитент, краткое наименование эмитента для всех ценных бумаг, кроме еврооблигаций; 
   //                                              для еврооблигаций - полное наименование выпуска из справочника НРД
   private macro GenerateNameAvr_EEE():string
      var ret = "";
      if (IsEuroBond() and (issuer != ""))
         ret = name_full;
      else
         ret = GenerateNameAvr_EEE();
      end;
      return ret;
   end;

   /* Получить базис расчета дохода Securities */
   private macro GetNKDBaseRS():integer
      var nkdBase = 0;

      if (coupon_period_base_id == 1)   //  30/360
         nkdBase = 1;
      elif (coupon_period_base_id == 2) // Act/Act
         nkdBase = 4;
      elif (coupon_period_base_id == 3) //  Act/360
         nkdBase = 2;
      elif (coupon_period_base_id == 4) // Act/365
         nkdBase = 0;
      elif ((coupon_period_base_id == 10) or (coupon_period_base_id == 6)) // Нестандартный и AM
         nkdBase = 6;
      elif (coupon_period_base_id == 5)  // Act/365L
         nkdBase = 7;
      elif (coupon_period_base_id == 7)  // Act/364
         nkdBase = 8;
      elif (coupon_period_base_id == 9)  // 30E/360
         nkdBase = 9;
//    Еще не реализованы!
//      elif (coupon_period_base_id == )
//         nkdBase = ;
      end;
      return nkdBase;
   end;

   // заполнение буферов выпуска общими данными для вставки\обновления
   macro FillCommonAvoirissSec(fin:TRecHandler, iss:TRechandler, action:integer)
      var point = 0;

      FillCommonAvoirissSec(fin, iss, action);

      fin.rec.DrawingDate = expiry_date_calc;

      iss.rec.InCirculationDate = start_placement_date; // Дата введения в обращение
      iss.rec.BegPlacementDate  = start_placement_date;
      iss.rec.EndPlacementDate  = end_placement_date;
      iss.rec.NKDBase_Kind      = GetNKDBaseRS(); // базис расчета
      
      if (action == ACT_INSERT)
         fin.rec.FaceValue = face_value;

         var finFace = TRecHandler("fininstr.dbt");
         if (ПолучитьФинИнПоISO(currency_code, finFace))
            fin.rec.FaceValueFI = finFace.rec.FIID;
         else
            logger.error(code_nsd, "Неизвестная валюта " + currency_code);
         end;
         
         iss.rec.Issue  = issue_number;
         iss.rec.Series = bond_series;

         // Округление НКД: 
         // для еврооблигаций  заполняется значением "Округление платежа по НКД"
         // для остальных видов облигаций заполняется для облигаций значением "Округление одного купона".
         if( IsEuroBond() )
            iss.rec.NKDRound_Kind =  AVOIRISSNKDROUND_SUM;
         else
            iss.rec.NKDRound_Kind = AVOIRISSNKDROUND_COUPON;
         end;
      else
         if ((face_value != $0.0) and (fin.rec.FaceValue != face_value))
            logger.warning(code_nsd, "Необходимо выполнить операцию изменения номинала");
         end;
         if (iss.rec.Issue == "")
            iss.rec.Issue = issue_number;
         elif (iss.rec.Issue != issue_number)
            logger.warning(code_nsd, "Отличаются номера выпусков");
         end;

         if (iss.rec.Series == "")
            iss.rec.Series = bond_series;
         elif (iss.rec.Series != bond_series)
            logger.warning(code_nsd, "Отличаются серии облигаций");
         end;
      end;

      point = ЧислоЗнаковПослеЗапятой(face_value);
      if (strlen(string(point)) > 1) // в дистрибутиве поле в 1 символ, т.е. больше 9 не ввести
         point = 9;
      end;
      fin.rec.Point = point - DEFAULT_FACEVALUE_POINT;
   end;

   // получить дата начисления купонного дохода
   private macro GetFirstDateCoupon(coupon):date
      var firstDate = first_coupon_date; // по умолчанию Дата начала начисления первого купона
      var cmd, dataSet;

      if(coupon.period_from_calc > ZeroDate)
        firstDate = coupon.period_from_calc + 1;
      else
        if (coupon.coupon_number == 1)
           if (firstDate == ZeroDate) // first_coupon_date нулевой!!!
              firstDate = start_placement_date + 1;
           end;
        else
           // ищем дату погашения пред. купона
           cmd = DL_RSDCommand("SELECT t_DrawingDate FROM dfiwarnts_dbt WHERE T_ISPARTIAL = CHR(0) AND T_FIID = ? AND t_DrawingDate < ? ORDER BY t_DrawingDate DESC");

           cmd.addParam(FIID);
           cmd.addParam(coupon.payment_date_calc);

           dataSet = cmd.Execute();
           if (dataSet.moveNext())
              firstDate = Date(dataSet.DrawingDate)+1;
           else
              firstDate = start_placement_date + 1;
              logger.error(code_nsd, "Не определена дата начала начисления купона "+coupon.coupon_number+" (дата выплаты: "+string(date(coupon.payment_date_calc):f)+"). Установлена дата: "+string(date(firstDate):f)+".");
           end;
        end;
      end;
      return firstDate;
   end;

   // Получить TRecHandler купона или ЧП
   private macro GetRecordWarntsSec(IsPartial, FIID, DrawingDate, r_warnts:TRecHandler) : bool
      var f_warnts = TBFile("fiwarnts.dbt", "w", 1);
      var stat = false;

      f_warnts.clear();
      f_warnts.rec.IsPartial   = IsPartial;
      f_warnts.rec.DrawingDate = DrawingDate;
      f_warnts.rec.FIID        = FIID;
      stat = f_warnts.getEQ();
      if (stat)
         copy(r_warnts, f_warnts);
      end;

      return stat;
   end;
   // Создание и заполнение объекта для очета
   private macro CreateCouponPartialDataForRep(isPartial, r_saveWarnt, r_warnts, dataRepAvr, action):TWarntsNSDRepDate
      var rep = null;
      if (ACT_INSERT == action)
         rep = TWarntsNSDRepDate();
      else
         rep = TWarntsRepDate();
         if (r_saveWarnt != null)
            rep.rsFirstDate   = r_saveWarnt.rec.FirstDate;
            rep.rsDrawingDate = r_saveWarnt.rec.DrawingDate;
            rep.rsReestrDate  = r_saveWarnt.rec.ListDate;
            rep.rsRate        = r_saveWarnt.rec.IncomeRate;
            rep.rsVolume      = r_saveWarnt.rec.IncomeVolume;
            rep.rsISClosed    = r_saveWarnt.rec.IsClosed;
            // а вдруг r_warnts, то Номер возьмем из r_saveWarnt
            rep.Number        = r_saveWarnt.rec.Number;
         end;
      end;
      
      rep.CountAvr    = dataRepAvr.count+1;// отсчет с нуля :)
      rep.ID          = dataRepAvr.ID;
      rep.CodeCntr    = dataRepAvr.CodeCntr; 
      rep.CodeNSD     = dataRepAvr.CodeNSD;
      rep.ISIN        = dataRepAvr.ISIN;
      rep.CFI         = dataRepAvr.CFI;
      rep.Issuer      = dataRepAvr.IssuerNSD;
      rep.Type711     = dataRepAvr.Type;
      rep.KindAvoir   = dataRepAvr.KindAvoir;
      rep.Name        = dataRepAvr.Name;
      rep.LSIN        = dataRepAvr.LSIN;
      if (IsPartial == "")
         rep.TypeWarnts  = TYPE_COUPON;
      else
         rep.TypeWarnts  = TYPE_PART;
      end;
      if (r_warnts != null)
         rep.Number      = r_warnts.rec.Number;
         rep.FirstDate   = r_warnts.rec.FirstDate;   
         rep.DrawingDate = r_warnts.rec.DrawingDate; 
         rep.ReestrDate  = r_warnts.rec.ListDate;    
         rep.Rate        = r_warnts.rec.IncomeRate;  
         rep.Volume      = r_warnts.rec.IncomeVolume;
         rep.ISClosed    = r_warnts.rec.IsClosed;    
      end;
      return rep;
   end;

   // Получить признак погашения купона или ЧП 
   private macro GetClosedWarnt(date_fact:date):string
      var ret = "";
      if (date_fact != ZeroDate)
         ret = "X";
      end;
      return ret;
   end;
   // Вывод в протокол купонов\ЧП из базы Sec между датами (prevDrawingDate, drawingDate)
   private macro AddProtocolWarntsSec(isPartial, prevDrawingDate:date, drawingDate:date, dataRepAvr, action)
      var queryIsPartial = "";
      if (isPartial == "")
         queryIsPartial = "CHR(0)";
      else
         queryIsPartial = "CHR(88)";
      end;
      var cmd = DL_RSDCommand("SELECT * FROM dfiwarnts_dbt WHERE T_ISPARTIAL = "+queryIsPartial+" AND T_FIID = ? AND t_DrawingDate > ? AND t_DrawingDate < ?");
      var dataSet;

      cmd.addParam(FIID);
      cmd.addParam(prevDrawingDate);
      cmd.addParam(drawingDate);

      dataSet = cmd.Execute();
      while (dataSet.moveNext())
         SaveWarntsUpdDataForReport(CreateCouponPartialDataForRep(isPartial, dataSet, null, dataRepAvr, action));
      end;
   end;

   // Заполняем рекорд купона для вставки\обновления
   private macro FillRecCoupon(coupon, r_warnts, fin, action)
      var oldIsClosed = r_warnts.rec.IsClosed;
      r_warnts.rec.DrawingDate = coupon.payment_date_calc;
      r_warnts.rec.IsClosed    = GetClosedWarnt(coupon.payment_date_fact);
      r_warnts.rec.FirstDate   = GetFirstDateCoupon(coupon);
      r_warnts.rec.ListDate    = coupon.record_date_calc;

      if ((oldIsClosed == "") and (r_warnts.rec.IsClosed == "X"))
         r_warnts.rec.SPIsClosed = "X";
         r_warnts.rec.TSIsClosed = "X";
      end;

      if (ACT_INSERT == action)
         r_warnts.rec.FIID        = FIID;
         r_warnts.rec.Number      = coupon.coupon_number;
         r_warnts.rec.IsPartial   = "";
         r_warnts.rec.IncomeScale = 1;
         r_warnts.rec.IncomeFI    = fin.rec.FaceValueFI;

         // в справочнике НРД м.б. заданы и проценты и абс. величина. Если есть проценты, то работаем с ними
         if (coupon.interest_rate != $0.0)
            r_warnts.rec.IncomeRate     = coupon.interest_rate;
            r_warnts.rec.RelativeIncome = "X";
            r_warnts.rec.IncomePoint    = DL_MeanSignAfterPoint(coupon.interest_rate, 12);
         elif (coupon.size != $0.0)
            r_warnts.rec.IncomeVolume   = coupon.size;
            r_warnts.rec.RelativeIncome = "";
            r_warnts.rec.IncomePoint    = DL_MeanSignAfterPoint(coupon.size, 12);
         end;

         if( r_warnts.rec.IncomePoint <= 0 )
            r_warnts.rec.IncomePoint    = 2;
         end;
      else
         if (r_warnts.rec.RelativeIncome == "X")
            r_warnts.rec.IncomeRate = coupon.interest_rate;
         else
            r_warnts.rec.IncomeVolume = coupon.size;
         end;
      end;
   end;
   // Отличается ли Купон НРД от купона Securities?
   private macro IsChangesCoupon(coupon:TCouponData, r_warnts:TREcHandler):bool
   //(r_warnts.rec.RelativeIncome == "X")
      return (//(coupon.payment_date_calc                 != r_warnts.rec.DrawingDate ) or
              //(GetClosedWarnt(coupon.payment_date_fact) != r_warnts.rec.IsClosed    ) or
              (coupon.record_date_calc                  != r_warnts.rec.ListDate    ) or
              ((r_warnts.rec.RelativeIncome == "X") and (coupon.interest_rate != r_warnts.rec.IncomeRate  )) or
              ((r_warnts.rec.RelativeIncome == "" ) and (coupon.size          != r_warnts.rec.IncomeVolume))
              //(coupon.coupon_number                     != r_warnts.rec.Number      ) 
             );
   end;
   // Действие над купонами
   macro ActionOnCoupon(fin, iss, dataRepAvr, action)
      var r_warnts = TRecHandler("fiwarnts.dbt");
      var r_saveWarnts = TRecHandler("fiwarnts.dbt");
      var stat = 0;
      var prevDrawingDate = ZeroDate;

      for (var coupon, couponsData)
         r_saveWarnts.clear();
         r_warnts.clear();
         // Для расширенного протокола для Сравнения выводим все купоны, которые есть в Sec
         if ((action==ACT_MERGE) and isProtocolExt)
            AddProtocolWarntsSec("", prevDrawingDate, coupon.payment_date_calc, dataRepAvr, action);
            prevDrawingDate = coupon.payment_date_calc;
         end;
         
         var flagFindCoupon = GetRecordWarntsSec("", FIID, coupon.payment_date_calc, r_warnts);
         if (((action==ACT_UPDATE) or (action==ACT_MERGE)) and (flagFindCoupon))
            if (IsChangesCoupon(coupon, r_warnts) or ((action==ACT_MERGE) and isProtocolExt))
               copy(r_saveWarnts, r_warnts);
               FillRecCoupon(coupon, r_warnts, fin, ACT_UPDATE);
               stat = 0;
               if (action==ACT_UPDATE)
                  stat = FI_UpdateWarnt(r_warnts);
               end;
               if (stat == 0)
                  SaveWarntsUpdDataForReport(CreateCouponPartialDataForRep("", r_saveWarnts, r_warnts, dataRepAvr, action));
               else
                  logger.error(code_nsd, "Ошибка при обновлении данных купона №"+coupon.coupon_number+". "+ GetErrorMsg(stat));
               end;
            end;
         elif (((action==ACT_MERGE) and isProtocolExt and (not flagFindCoupon)))
            FillRecCoupon(coupon, r_warnts, fin, ACT_UPDATE);
            r_warnts.rec.Number = coupon.coupon_number;
            SaveWarntsUpdDataForReport(CreateCouponPartialDataForRep("", null, r_warnts, dataRepAvr, action));
         elif ((action == ACT_INSERT) or (not flagFindCoupon and (action!=ACT_MERGE)))
            // вставляем купон 
            r_warnts.clear();
            FillRecCoupon(coupon, r_warnts, fin, ACT_INSERT);
            stat = FI_InsertWarnt(r_warnts);
            if (stat == 0)
               SaveWarntsDataForReport(CreateCouponPartialDataForRep("", r_saveWarnts, r_warnts, dataRepAvr, ACT_INSERT));
            else
               logger.error(code_nsd, "Ошибка при создании нового купона №"+coupon.coupon_number+". " + GetErrorMsg(stat));
            end;
         end;
      end;
      // Для расширенного протокола для Сравнения выводим все купоны, которые есть в Sec
      if ((action==ACT_MERGE) and isProtocolExt)
         AddProtocolWarntsSec("", prevDrawingDate, Date(31,12,2099), dataRepAvr, action);
      end;
   end;

   // Заполняем рекорд ЧП для вставки\обновления
   private macro FillRecPartial(part, r_warnts, fin, action)
      var oldIsClosed = r_warnts.rec.IsClosed;
      r_warnts.rec.DrawingDate    = part.payment_date_calc;
      r_warnts.rec.IsClosed       = GetClosedWarnt(part.payment_date_fact);
      r_warnts.rec.ListDate       = part.record_date_calc;
      r_warnts.rec.RelativeIncome = "X";
      
      if ((oldIsClosed == "") and (r_warnts.rec.IsClosed == "X"))
         r_warnts.rec.SPIsClosed = "X";
         r_warnts.rec.TSIsClosed = "X";
      end;

      if (ACT_INSERT == action)
         r_warnts.rec.FIID        = FIID;
         r_warnts.rec.Number      = part.pp_number;
         r_warnts.rec.IsPartial   = "X";
         r_warnts.rec.IncomeScale = 1;
         r_warnts.rec.IncomePoint = 4;
         r_warnts.rec.IncomeFI    = fin.rec.FaceValueFI;
         r_warnts.rec.IncomeRate  = part.payment_size_percent;
      else
         if (part.payment_size_percent != $0.0)
            r_warnts.rec.IncomeRate = part.payment_size_percent;
         end;
      end;
   end;

   // Отличается ли ЧП НРД от купона Securities?
   private macro IsChangesPartial(part:TPartialData, r_warnts:TRecHandler):bool
      return (//(part.payment_date_calc                != r_warnts.rec.DrawingDate ) or
              //(GetClosedWarnt(part.payment_date_fact) != r_warnts.rec.IsClosed    ) or
              (part.record_date_calc                 != r_warnts.rec.ListDate    ) or
              (part.payment_size_percent             != r_warnts.rec.IncomeRate  ) 
              //(part.pp_number                        != r_warnts.rec.Number      ) 
             );
   end;
   // Действие над ЧП
   macro ActionOnPartial(fin, iss, dataRepAvr, action)
      var r_warnts = TRecHandler("fiwarnts.dbt");
      var r_saveWarnts = TRecHandler("fiwarnts.dbt");
      var stat = 0;
      var lastNumber = 0; // последний номер ЧП (массив ЧП укомплектован по возрастанию номеров)
      var prevDrawingDate:date = ZeroDate;

      for (var part, partsData)
         // Загружаем только DRAW и REDM/BN
         if ((part.action_type_code == "DRAW") or (part.action_type_code == "REDM/BN"))
            if ((part.action_type_code == "REDM/BN") and (part.payment_size_percent == $100.0))
               continue;
            end;
            if (part.pp_number != 0)
               lastNumber = part.pp_number;
            else
               lastNumber = lastNumber + 1;
               part.pp_number = lastNumber;
            end;

            // Для расширенного протокола для Сравнения выводим все купоны, которые есть в Sec
            if ((action==ACT_MERGE) and isProtocolExt)
               AddProtocolWarntsSec("X", prevDrawingDate, part.payment_date_calc, dataRepAvr, action);
               prevDrawingDate = part.payment_date_calc;
            end;

            var flagFindPart = GetRecordWarntsSec("X", FIID, part.payment_date_calc, r_warnts);
            if (((action==ACT_UPDATE) or (action==ACT_MERGE)) and (flagFindPart))
               if (IsChangesPartial(part, r_warnts) or ((action==ACT_MERGE) and isProtocolExt))
                  copy(r_saveWarnts, r_warnts);

                  FillRecPartial(part, r_warnts, fin, ACT_UPDATE);
                  stat = 0;
                  if (action==ACT_UPDATE)
                     stat = FI_UpdateWarnt(r_warnts);
                  end;
                  if (stat == 0)
                     SaveWarntsUpdDataForReport(CreateCouponPartialDataForRep("X", r_saveWarnts, r_warnts, dataRepAvr, ACT_UPDATE));
                  else
                     logger.error(code_nsd, "Ошибка при обновлении данных ЧП №"+part.pp_number+". " + GetErrorMsg(stat));
                  end;
               end;
            elif (((action==ACT_MERGE) and isProtocolExt and (not flagFindPart)))
               FillRecPartial(part, r_warnts, fin, ACT_UPDATE);
               r_warnts.rec.Number = part.pp_number;
               SaveWarntsUpdDataForReport(CreateCouponPartialDataForRep("X", null, r_warnts, dataRepAvr, action));
            elif ((action == ACT_INSERT) or (not flagFindPart and (action != ACT_MERGE)))
               // вставляем ЧП 
               r_warnts.clear();
               FillRecPartial(part, r_warnts, fin, ACT_INSERT);
               stat = FI_InsertWarnt(r_warnts);
               if (stat == 0)
                  SaveWarntsDataForReport(CreateCouponPartialDataForRep("X", r_saveWarnts, r_warnts, dataRepAvr, ACT_INSERT));
               else
                  logger.error(code_nsd, "Ошибка при создании нового ЧП №"+part.pp_number +". " + GetErrorMsg(stat));
               end;
            end;
         else
            logger.warning(code_nsd,"Сведения о типе погашения " + part.action_type_code + " датой " + String(part.payment_date_calc:f) + " не загружены в сравочник Securities");
         end;
      end;
      // Для расширенного протокола для Сравнения выводим все купоны, которые есть в Sec
      if ((action==ACT_MERGE) and isProtocolExt)
         AddProtocolWarntsSec("X", prevDrawingDate, Date(31,12,2099), dataRepAvr, action);
      end;
   end;
   // вставка категорий, примечаний и т.п. (после действия над выпуском)
   macro AfterAction(fin, iss, action)
      AfterAction(fin, iss, action);

      // sec_type_id - категория
      if ((sec_type_id != 0) and not ConnectObjAttr(null, OBJTYPE_AVOIRISS, L_Z(fin.rec.FIID, 10), 51, null, "", String(sec_type_id)))
         logger.error(code_nsd, "Ошибка при установке категории \"Тип ценной бумаги\"");
      end;

      // rate_type_id - примечание
      if ((action == ACT_INSERT) and (rate_type_id == 3) and
          (AddNoteForObject(OBJTYPE_AVOIRISS, L_Z(fin.rec.FIID, 10), 20, SET_CHAR) != 0))
         logger.error(code_nsd, "Ошибка при установке примечания \"Плавающая процентная ставка\"");
      end;
   end;

   // Получить TRecHandler истории номинала
   private macro GetRecordFaceValueSec(FIID:integer, EndDate:date, r_fv:TRecHandler) : bool
      var f_fv = TBFile("fivlhist.dbt");
      var stat = false;

      f_fv.clear();
      f_fv.rec.FIID    = FIID;
      f_fv.rec.ValKind = FI_CHANGE_NOMINAL;
      f_fv.rec.EndDate = EndDate;
      stat = f_fv.getEQ();
      if (stat)
         copy(r_fv, f_fv);
      end;

      return stat;
   end;

   // Заполняем рекорд истори номинала для вставки\обновления
   private macro FillRecFaceValue(fv, r_fv, fin, action)
      if (ACT_INSERT == action)

         r_fv.rec.FIID     = FIID;
         r_fv.rec.ValKind  = FI_CHANGE_NOMINAL;
         r_fv.rec.EndDate  = date(fv.facevalue_date);
         r_fv.rec.Value    = fv.facevalue;
         r_fv.rec.ID       = 0;
         r_fv.rec.Version  = 0;
         r_fv.rec.IntValue = 0;
      else
         r_fv.rec.EndDate  = date(fv.facevalue_date);
         r_fv.rec.Value    = fv.facevalue;
      end;
   end;

   // Отличается ли номинал НРД от номинала Securities?
   private macro IsChangesFaceValue(fv:TFaceValueData, r_fv:TRecHandler):bool
      return (fv.facevalue != r_fv.rec.Value);
   end;

   // Создание и заполнение объекта для очета
   private macro CreateFaceValueDataForRep(r_saveFV, r_fv, dataRepAvr, action):TWarntsNSDRepDate
      var rep = TFaceValuesNSDRepDate();
      
      rep.CountAvr    = dataRepAvr.count+1;// отсчет с нуля :)
      rep.ID          = dataRepAvr.ID;
      rep.CodeCntr    = dataRepAvr.CodeCntr; 
      rep.CodeNSD     = dataRepAvr.CodeNSD;
      rep.ISIN        = dataRepAvr.ISIN;
      rep.CFI         = dataRepAvr.CFI;
      rep.Issuer      = dataRepAvr.IssuerNSD;
      rep.Type711     = dataRepAvr.Type;
      rep.KindAvoir   = dataRepAvr.KindAvoir;
      rep.Name        = dataRepAvr.Name;
      rep.LSIN        = dataRepAvr.LSIN;
      if (r_fv != null)
         rep.FaceValueDate   = r_fv.rec.EndDate;
         rep.FaceValue       = r_fv.rec.Value;
         rep.FaceValueFiCode = currency_code;
      end;
      return rep;
   end;

   // Вывод в протокол номиналов из базы Sec 
   private macro AddProtocolFaceValuesSec(prevEndDate:date, EndDate:date, dataRepAvr, action)
      var cmd = DL_RSDCommand("SELECT * FROM dfivlhist_dbt WHERE T_FIID = ? AND t_ValKind = ? AND t_EndDate > ? AND t_EndDate < ?");
      var dataSet;

      cmd.addParam(FIID);
      cmd.addParam(FI_CHANGE_NOMINAL);
      cmd.addParam(prevEndDate);
      cmd.addParam(EndDate);

      dataSet = cmd.Execute();
      while (dataSet.moveNext())
         SaveFaceValuesDataForReport(CreateFaceValueDataForRep(dataSet, null, dataRepAvr, action));
      end;
   end;

   // Действие над номиналами
   macro ActionOnFaceValue(fin, iss, dataRepAvr, action)
      var r_fv = TRecHandler("fivlhist.dbt");
      var r_saveFV = TRecHandler("fivlhist.dbt");
      var stat = 0;
      var prevEndDate = ZeroDate;
      
      for (var fv, facevaluesData)
         r_saveFV.clear();
         r_fv.clear();
         // Для расширенного протокола для Сравнения выводим все записи, которые есть в Sec
         if ((action==ACT_MERGE) and isProtocolExt)
            AddProtocolFaceValuesSec(prevEndDate, fv.facevalue_date, dataRepAvr, action);
            prevEndDate = fv.facevalue_date;
         end;
         
         var flagFindFaceValue = GetRecordFaceValueSec(FIID, fv.facevalue_date, r_fv);
         if (((action==ACT_UPDATE) or (action==ACT_MERGE)) and (flagFindFaceValue))
            if (IsChangesFaceValue(fv, r_fv) or ((action==ACT_MERGE) and isProtocolExt))
               copy(r_saveFV, r_fv);
               FillRecFaceValue(fv, r_fv, fin, ACT_UPDATE);
               stat = 0;
               if (action==ACT_UPDATE)
                  stat = FI_UpdateHistory(r_fv);
               end;
               if (stat == 0)
                  SaveFaceValuesDataForReport(CreateFaceValueDataForRep(r_saveFV, r_fv, dataRepAvr, action));
               else
                  logger.error(code_nsd, "Ошибка при обновлении данных в истории номинала за дату "+string(date(fv.facevalue_date):f)+". "+ GetErrorMsg(stat));
               end;
            end;
         elif (((action==ACT_MERGE) and isProtocolExt and (not flagFindFaceValue)))
            FillRecFaceValue(fv, r_fv, fin, ACT_UPDATE);
            SaveFaceValuesDataForReport(CreateFaceValueDataForRep(null, r_fv, dataRepAvr, action));
         elif ((action == ACT_INSERT) or (not flagFindFaceValue and (action!=ACT_MERGE)))
            // вставляем запись в историю номинала 
            r_fv.clear();
            FillRecFaceValue(fv, r_fv, fin, ACT_INSERT);
            stat = FI_InsertHistory(r_fv);
            if (stat == 0)
               SaveFaceValuesDataForReport(CreateFaceValueDataForRep(r_saveFV, r_fv, dataRepAvr, ACT_INSERT));
            else
               logger.error(code_nsd, "Ошибка при создании записи в истории номинала за дату "+string(date(fv.facevalue_date):f)+". " + GetErrorMsg(stat));
            end;
         end;
      end;
      // Для расширенного протокола для Сравнения выводим все купоны, которые есть в Sec
      if ((action==ACT_MERGE) and isProtocolExt)
         AddProtocolFaceValuesSec(prevEndDate, Date(31,12,2099), dataRepAvr, action);
      end;
   end;

   // Действия после сохранения данных в отчет
   private macro AfterSaveDataForReport(fin, iss, dataRep, action)
      ActionOnCoupon(fin, iss, dataRep, action);
      ActionOnPartial(fin, iss, dataRep, action);
      ActionOnFaceValue(fin, iss, dataRep, action);
   end;

   // Заполнение объекта для отчета общими данными.
   // Перегружена
   private macro FillDataForReport(avr, action)
      FillDataForReport(avr, action);

      avr.Issue         = issue_number;
      avr.Series        = bond_series;
      avr.StartDate     = start_placement_date;
      avr.EndDate       = end_placement_date;
      avr.FaceValue     = face_value;
      avr.MaturityValue = maturity_value;
      avr.FaceValueCur  = currency_code;
      avr.ExpiryDate    = expiry_date_calc;
      avr.CouponPer     = GetNameAlg(3108, GetNKDBaseRS());
      if ((action == ACT_UPDATE) or (action == ACT_MERGE))
         if(facevaluesData.size > 0)
            avr.FaceValue = facevaluesData[facevaluesData.size-1].facevalue;
         end;

         avr.rsIssue         = IssueSec;
         avr.rsStartDate     = StartDateSec;
         avr.rsEndDate       = EndDateSec;
         avr.rsMaturityValue = MaturityValueSec;
         avr.rsFaceValue     = FaceValueSec;
         avr.rsFaceValueCur  = FaceValueCurSec;
         avr.rsExpiryDate    = ExpiryDateSec;
         avr.rsSeries        = SeriesSec;
         avr.rsCouponPer     = GetNameAlg(3108, NKDBaseKindSec);
      end;
   end;

   /* Есть ли отличия для обновления купонов? */ 
   private macro IsChangesCouponForUpd():bool
      var r_warnts = TRecHandler("fiwarnts.dbt");

      for (var coupon, couponsData)
         r_warnts.clear();
         var flagFindCoupon = GetRecordWarntsSec("", FIID, coupon.payment_date_calc, r_warnts);
         if (not flagFindCoupon or IsChangesCoupon(coupon, r_warnts))
            return true;
         end;
      end;
      return false;
   end;
   /* Есть ли отличия дл яобновления ЧП? */ 
   private macro IsChangesPartialForUpd():bool
      var r_warnts = TRecHandler("fiwarnts.dbt");
      var lastNumber = 0; // последний номер ЧП (массив ЧП укомплектован по возрастанию номеров)

      for (var part, partsData)
         // только DRAW и REDM/BN
         if ((part.action_type_code == "DRAW") or (part.action_type_code == "REDM/BN"))
            if (part.pp_number != 0)
               lastNumber = part.pp_number;
            else
               lastNumber = lastNumber + 1;
               part.pp_number = lastNumber;
            end;

            r_warnts.clear();
            var flagFindPart = GetRecordWarntsSec("X", FIID, part.payment_date_calc, r_warnts);
            if (not flagFindPart or IsChangesPartial(part, r_warnts))
               return true;
            end;
         end;
      end;
      return false;
   end;
   /* Есть ли отличия для обновления номиналов? */ 
   private macro IsChangesFaceValueForUpd():bool
      var r_fv = TRecHandler("fivlhist.dbt");
      for (var fv, facevaluesData)
         
         var flagFindFV = GetRecordFaceValueSec(FIID, date(fv.facevalue_date), r_fv);
         if (not flagFindFV or IsChangesFaceValue(fv, r_fv))
           return true;
         end;
         
      end;
      return false;
   end;

   // есть ли отличия для обновления?
   macro IsChangesForUpd() : bool
      var ret = false;      
      if (isLinkSec == "X")
         ret = IsChangesForUpd();
         if (not ret)
            ret = ((IssueSec        != issue_number        ) or
                   (StartDateSec    != start_placement_date) or
                   (EndDateSec      != end_placement_date  ) or
                   (FaceValueSec    != face_value          ) or
                   (FaceValueCurSec != currency_code       ) or
                   (ExpiryDateSec   != expiry_date_calc    ) or
                   (SeriesSec       != bond_series         ) or
                   (NKDBaseKindSec  != GetNKDBaseRS()      )
                   //( MaturityValueSec != ) 
                  );
         end;
         // есть отличия в купонах?
         if (not ret)
            ret = IsChangesCouponForUpd();
         end;
         // есть отличия в ЧП?
         if (not ret)
            ret = IsChangesPartialForUpd();
         end;
         // есть отличия в номиналах?
         if (not ret)
            ret = IsChangesFaceValueForUpd();
         end;
      end;
      return ret;
   end;

   initTAvoirData(AVOIRISSKIND_BOND, isProtocolExt_);

   this.clear();
end;
// *************************************
// Акция
// *************************************
class (TAvoirData)TShareData(isProtocolExt_)
   var share_category_id:integer,  // Категория акций
       sec_type_id:integer,        // Тип ценной бумаги.
       issue_number:string,        // Порядковый номер выпуска
       face_value:numeric,         // Номинал
       currency_code:string,       // Валюта номинала
       issue_size_planned:integer; // Объявленное  количество (шт.)  ???????? куда заполнять?????
   // Securities
   var IssueSec:string,
       FaceValueSec:string,
       FaceValueCurSec:string;

   macro clear()
      clear(); // базовый

      share_category_id  = 0;
      sec_type_id        = 0;
      issue_number       = "";
      face_value         = $0.0;
      currency_code      = "";
      issue_size_planned = 0;
      // Securities
      IssueSec        = "";
      FaceValueSec    = 0;
      FaceValueCurSec = 0;
   end;

   // добавление цб в массив массовых вставок.
   // ret: true - вставка успешна
   //      false - ошибка
   macro addRecordToCache():bool
      var stat = false;
      r_avr_tmp.clear();
      // 1. заполним основными данными
      stat = fillRecordAvr();

      if (stat)
         // заполним своими
         //r_avr_tmp.rec.share_category_id  = share_category_id; Не сохраняем, июо надо для определения AvoirKind!
         r_avr_tmp.rec.sec_type_id        = sec_type_id;
         r_avr_tmp.rec.issue_number       = issue_number;
         r_avr_tmp.rec.face_value         = face_value;
         r_avr_tmp.rec.currency_code      = currency_code;
         //r_avr_tmp.rec.issue_size_planned = issue_size_planned;

         stat = dataAvrCache.AddRecord(r_avr_tmp);
      end;
      if (stat)
         addRecordCodesToCaches();
      end;
      
      return stat;
   end;
   // -------------------------------
   // получение подвида цб
   // -------------------------------
   macro GetAvoirKind()
      if (AvoirKind == 0)
         if (share_category_id == 1)
            AvoirKind = AVOIRISSKIND_EQUITY_SHARE;
         elif (share_category_id == 2)
            AvoirKind = AVOIRISSKIND_PREFERENCE_SHARE;
         end;
      end;

      return AvoirKind;
   end;

   // Генерация наименование выпуска. Перегружена для каждого вида
   private macro GenerateNameAvr_TTT():string
      if (AvoirKind == AVOIRISSKIND_EQUITY_SHARE)
         return "Аои";
      elif (AvoirKind == AVOIRISSKIND_PREFERENCE_SHARE)
         return "аоп";
      end;
      return GenerateNameAvr_TTT();
   end;

   // Генерация наименование выпуска, шаблон ВВВ - номер выпуска. Перегружена для каждого вида
   private macro GenerateNameAvr_BBB():string
      return issue_number;
   end;

   // ----------------------------------------
   // Заполнение данными из временной таблицы
   // ----------------------------------------
   macro fillFromTMP(avr_tmp)
      fillFromTMP(avr_tmp);

      sec_type_id   = avr_tmp.rec.sec_type_id;
      issue_number  = avr_tmp.rec.issue_number;
      face_value    = avr_tmp.rec.face_value;
      currency_code = avr_tmp.rec.currency_code;

      // Заполнение данными Securities, если есть связь
      if ((isLinkSec == "X") and (FIID > -1))
         var cmd, dataSet;
         cmd = DL_RSDCommand("SELECT fin.t_FaceValue, avr.t_Issue, finCur.t_ccy FROM dfininstr_dbt fin, davoiriss_dbt avr, dfininstr_dbt finCur " +
                             " WHERE fin.T_FIID = ? AND avr.T_FIID = fin.T_FIID AND finCur.t_FIID = fin.t_FaceValueFI");

         cmd.addParam(FIID);

         dataSet = cmd.Execute();
         if (dataSet.moveNext())
            IssueSec         = dataSet.Issue;
            FaceValueSec     = dataSet.FaceValue;
            FaceValueCurSec  = dataSet.CCY;
         end;
      end;
   end;

   // заполнение буферов выпуска общими данными для вставки\обновления
   macro FillCommonAvoirissSec(fin:TRecHandler, iss:TRechandler, action:integer)
      var point = 0;
      FillCommonAvoirissSec(fin, iss, action);

      if (action == ACT_INSERT)
         fin.rec.FaceValue = face_value;

         var finFace = TRecHandler("fininstr.dbt");
         if (ПолучитьФинИнПоISO(currency_code, finFace))
            fin.rec.FaceValueFI = finFace.rec.FIID;
         else
            logger.error(code_nsd, "Неизвестная валюта " + currency_code);
         end;

         iss.rec.Issue = issue_number;
      else
         if ((face_value != $0.0) and (fin.rec.FaceValue != face_value))
            logger.warning(code_nsd, "Необходимо выполнить операцию изменения номинала");
         end;

         if (iss.rec.Issue == "")
            iss.rec.Issue = issue_number;
         elif (iss.rec.Issue != issue_number)
            logger.warning(code_nsd, "Отличаются номера выпусков");
         end;
      end;

      point = ЧислоЗнаковПослеЗапятой(face_value);
      if (strlen(string(point)) > 1) // в дистрибутиве поле в 1 символ, т.е. больше 9 не ввести
         point = 9;
      end;
      fin.rec.Point = point - DEFAULT_FACEVALUE_POINT;
   end;

   // вставка категорий, примечаний и т.п. (после действия над выпуском)
   macro AfterAction(fin, iss, action)
      AfterAction(fin, iss, action);

      // sec_type_id - категория
      if ((sec_type_id != 0) and not ConnectObjAttr(null, OBJTYPE_AVOIRISS, L_Z(fin.rec.FIID, 10), 51, null, "", String(sec_type_id)))
         logger.error(code_nsd, "Ошибка при установке категории \"Тип ценной бумаги\"");
      end;
   end;

   // Заполнение объекта для отчета общими данными.
   // Перегружена
   private macro FillDataForReport(avr, action)
      FillDataForReport(avr, action);

      avr.Issue         = issue_number;
      avr.FaceValue     = face_value;
      avr.FaceValueCur  = currency_code;
      if ((action == ACT_UPDATE) or (action == ACT_MERGE))
         avr.rsIssue         = IssueSec;
         avr.rsFaceValue     = FaceValueSec;
         avr.rsFaceValueCur  = FaceValueCurSec;
      end;
   end;

   // есть ли отличия для обновления?
   macro IsChangesForUpd() : bool
      var ret = false;      
      if (isLinkSec == "X")
         ret = IsChangesForUpd();
         if (not ret)
            ret = ((IssueSec        != issue_number ) or
                   (FaceValueSec    != face_value   ) or
                   (FaceValueCurSec != currency_code) 
                  );
         end;
      end;
      return ret;
   end;

   initTAvoirData(AVOIRISSKIND_SHARE, isProtocolExt_);
   this.clear();
end;
// *************************************
// Инвестиционные паи
// *************************************
class (TAvoirData)TUnitData(isProtocolExt_)
   var fund:string,                  // Паевой инвестиционный фонд (ПИФ)
       // managing:string,              // Управляющая компания (issuer(базового)=managing)
       fraction:integer;             // Кол-во знаков после запятой
   var FractionSec:integer,
       TypePIFSec:integer,    // тип фонда
       NamePIFSec:string;     // наименование фонда
       //RulesRegXIDSec:string; // Регистрационный номер на закладке "Описание фонда"
   var typePIF:integer, // тип ПИФа (namealg.dbt, 3430)
       namePIF:string;   // наименование ПИФа
   var r_invs = TRecHandler("avrinvst.dbt"); // для Update и Insert
       
   macro clear()
      clear(); // базовый
      fund     = "";
      fraction = 0;

      typePIF = 0;
      namePIF = "";
      // Securities
      FractionSec    = 0;
      TypePIFSec     = 0;
      //RulesRegXIDSec = "";
   end;

   // добавление цб в массив массовых вставок.
   // ret: true - вставка успешна
   //      false - ошибка
   macro addRecordToCache():bool
      var stat = false;
      r_avr_tmp.clear();
      // 1. заполним основными данными
      stat = fillRecordAvr();

      if (stat)
         // заполним своими
         r_avr_tmp.rec.fund     = fund;
         r_avr_tmp.rec.fraction = fraction;

         stat = dataAvrCache.AddRecord(r_avr_tmp);
      end;
      if (stat)
         addRecordCodesToCaches();
      end;
      return stat;
   end;

   // полуить тип фонда
   private macro GetPIF(type:@integer, namePIF:@string) : integer
      var cmd, dataSet;
      cmd = DL_RSDCommand("SELECT t_pif_type_mn, t_short_name FROM dsirnsd_org_tmp WHERE t_Code_nsd = ?");

      cmd.addParam(fund);

      dataSet = cmd.Execute();
      if (dataSet.moveNext())
         namePIF = dataSet.short_name;
         if (dataSet.pif_type_mn == "БПИФ")
            type = 3; // Биржевой
         elif (dataSet.pif_type_mn == "ЗПИФ")
            type = 2; // Закрытый
         elif (dataSet.pif_type_mn == "ИПИФ")
            type = 0; // Индивидуальный
         elif (dataSet.pif_type_mn == "ОПИФ")
            type = 1; // Открытый
         end;
      end;
   end;
   
   
   // ----------------------------------------
   // Заполнение данными из временной таблицы
   // ----------------------------------------
   macro fillFromTMP(avr_tmp)
      fillFromTMP(avr_tmp);

      fund     = avr_tmp.rec.fund;
      fraction = avr_tmp.rec.fraction;
      // данные ПИФ'а
      GetPIF(@typePIF, @namePIF);

      // Заполнение данными Securities, если есть связь
      if ((isLinkSec == "X") and (FIID > -1))
         var cmd, dataSet;
         cmd = DL_RSDCommand("SELECT fin.t_SumPrecision, invst.t_Type, invst.t_Name, invst.t_RulesRegXID "+
                             "  FROM dfininstr_dbt fin, davrinvst_dbt invst WHERE fin.T_FIID = ? AND invst.T_FIID=fin.T_FIID");

         cmd.addParam(FIID);

         dataSet = cmd.Execute();
         if (dataSet.moveNext())
            FractionSec    = dataSet.SumPrecision;
            TypePIFSec     = dataSet.Type; 
            NamePIFSec     = dataSet.Name;
            //RulesRegXIDSec = dataSet.RulesRegXID; В протоколе не юзается, убрал
         end;
      end;
   end;
   // заполнение буферов выпуска общими данными для вставки\обновления
   macro FillCommonAvoirissSec(fin:TRecHandler, iss:TRechandler, action:integer)
      FillCommonAvoirissSec(fin, iss, action);

      fin.rec.SumPrecision = fraction;
      if (action == ACT_INSERT)
         // Валюта выплаты дохода
         fin.rec.FaceValueFI =  GetPtCountryFI(fin.rec.Issuer);      
      end;

      r_invs.rec.Type        = typePIF;
      r_invs.rec.Name        = namePIF;
      r_invs.rec.RulesRegXID = state_reg_number;

      // вставка ПИФа
      GetPartyID(fund, false); 
   end;

   // Заполнение объекта для отчета общими данными.
   // Перегружена
   private macro FillDataForReport(avr, action)
      FillDataForReport(avr, action);

      avr.PIF     = GetNameAlg(3430, typePIF);
      avr.PIFPrec = fraction;
      if ((action == ACT_UPDATE) or (action == ACT_MERGE))
         avr.rsPIF     = GetNameAlg(3430, TypePIFSec);
         avr.rsPIFPrec = FractionSec;
      end;
   end;

   // есть ли отличия для обновления?
   macro IsChangesForUpd() : bool
      var ret = false;      
      if (isLinkSec == "X")
         ret = IsChangesForUpd();
         if (not ret)
            ret = ((FractionSec    != fraction) or
                   (TypePIFSec     != typePIF ) /* В протоколе нет поле наименование фонда, поэтому не сравниваем (ибо сравнили, разницу нашли, а в протоколе все гуд)
                   (NamePIFSec  != namePIF )*/
                   //(RulesRegXIDSec != state_reg_number)
                  );
         end;
      end;
      return ret;
   end;

   private macro UpdateAvoiriss(fin, iss)
      dataMsgBoxAvoir = DataForMsgBox(code_nsd, logger);
      ReplaceMacro("msgbox", "Avoir_msgbox");
      var stat = FI_UpdateAvoiriss(fin, iss, r_invs);
      ReplaceMacro("msgbox", NULL);
      return stat;
   end;
   private macro InitAvoiriss(fin:TRecHandler, iss:TRechandler)
      return FI_InitAvoiriss(GetAvoirKind(), fin, iss, r_invs);
   end;

   private macro InsertAvoiriss(fin, iss)
      dataMsgBoxAvoir = DataForMsgBox(code_nsd, logger);
      ReplaceMacro("msgbox", "Avoir_msgbox");
      var stat = FI_InsertAvoiriss(fin, iss, r_invs, null, true);
      ReplaceMacro("msgbox", NULL);
      return stat;
   end;

   macro Update()
      var f_invs = TBFile("avrinvst.dbt");

      f_invs.rec.FIID = FIID;
      if (not f_invs.getEQ())
         logger.error(code_nsd, "Ошибка при поиске ц/б FIID="+FIID);
         return 1;
      end;

      copy(r_invs, f_invs);

      return Update(); // базовый
   end;
   
   macro Insert()
      r_invs.clear();

      return Insert(); // базовый
   end;
   
   initTAvoirData(AVOIRISSKIND_INVESTMENT_SHARE, isProtocolExt_);
   this.clear();
end;

// *************************************
// Депозитарные расписки
// *************************************
class (TAvoirData)TDepoData(isProtocolExt_)
   var dr_category_mn:string,     // Категория ДР. 
       repres_fi_isin:string,     // ISIN представляемой (базовой) ц.б. (связанный объект "Базовый выпуск для депозитарной расписки")
       issuer_base_sec:string,    // Эмитент базовой (представляемой) ценной бумаги
       ratio_numerator:numeric,   // Количество ДР
       ratio_denominator:double; // Количество представляемых ц.б.
   var RepresFiISINSec:string,
       IssuerBaseNSDSec:string,
       RatioNumeratorSec:integer,
       RatioDenominatorSec:double;
   var parentFIID:integer,        // FIID базового выпуска из dsirnsd_avr_tmp
       parentAvr,
       needActionOnParent:bool;

   macro clear()
      clear(); // базовый

      dr_category_mn    = "";
      repres_fi_isin    = "";
      issuer_base_sec   = "";
      ratio_numerator   = $0.0;
      ratio_denominator = 0.0;
      // Securities
      RepresFiISINSec     = "";
      IssuerBaseNSDSec    = "";
      RatioNumeratorSec   = 0;
      RatioDenominatorSec = 0.0;

      parentFIID = -1;
      parentAvr = null;
      needActionOnParent = false;
   end;

   // добавление цб в массив массовых вставок.
   // ret: true - вставка успешна
   //      false - ошибка
   macro addRecordToCache():bool
      var stat = false;
      r_avr_tmp.clear();
      // 1. заполним основными данными
      stat = fillRecordAvr();

      if (stat)
         // заполним своими
         r_avr_tmp.rec.dr_category_mn    = dr_category_mn;
         r_avr_tmp.rec.repres_fi_isin    = repres_fi_isin;
         r_avr_tmp.rec.issuer_base       = issuer_base_sec;
         r_avr_tmp.rec.issued_size       = ratio_numerator;
         r_avr_tmp.rec.ratio_denominator = ratio_denominator;

         stat = dataAvrCache.AddRecord(r_avr_tmp);
      end;
      if (stat)
         addRecordCodesToCaches();
      end;
      
      return stat;
   end;
   // -------------------------------
   // получение подвида цб
   // -------------------------------
   macro GetAvoirKind()
      if (AvoirKind == 0)
         if ("ADR" == dr_category_mn) // Американская
            AvoirKind = AVOIRISSKIND_AMERICAN_DEPREC;
         elif("EDR" == dr_category_mn) // Европейская
            AvoirKind = AVOIRISSKIND_EUROPEAN_DEPREC;
         elif("GDR" == dr_category_mn) // Глобальная
            AvoirKind = AVOIRISSKIND_GLOBAL_DEPREC;
         elif("RDR" == dr_category_mn) // Российская
            AvoirKind = AVOIRISSKIND_RUSSIAN_DEPREC;
         end;
      end;

      return AvoirKind;
   end;
   // Получение категории ДР по подвиду ЦБ (обратная GetAvoirKind) 
   private macro GetCategoryNSD(AvoirKind)
      var ret = "";

      if (AvoirKind == AVOIRISSKIND_AMERICAN_DEPREC) // Американская
         ret = "ADR";
      elif(AvoirKind == AVOIRISSKIND_EUROPEAN_DEPREC) // Европейская
         ret = "EDR";
      elif(AvoirKind == AVOIRISSKIND_GLOBAL_DEPREC) // Глобальная
         ret = "GDR";
      elif(AvoirKind == AVOIRISSKIND_RUSSIAN_DEPREC) // Российская
         ret = "RDR";
      end;

      return ret;
   end;
   // -------------------------------------
   // Получить ФИ по ISIN для баз.выпуска
   // -------------------------------------
   macro GetParentFI(isin)
      if (isin != "")
         var cmd, dataSet;
         cmd = DL_RSDCommand("SELECT * FROM dsirnsd_avr_tmp WHERE T_ISIN = ? AND T_STATE!="+STATE_SAVEOLD+" ORDER BY T_STATE");

         cmd.addParam(isin);

         dataSet = cmd.Execute();
         if (dataSet.moveNext())
            if ((dataSet.FIID > -1) and (dataSet.State == STATE_PROCESSED))
               parentFIID = dataSet.FIID;
            else
               parentAvr = ExecMacro2("CreateDataAvr", dataSet, isProtocolExt);
               if (parentAvr != null)
                  needActionOnParent = (parentAvr.IsChangesForAdd() or parentAvr.IsChangesForUpd());
                  if (not needActionOnParent)
                     parentFIID = parentAvr.FIID;
                     SetProcessedRec(); // Установим признак обработанной записи
                  end;
               end;
            end;
         else
            parentFIID = -2; // не найден в справочнике
         end;
      end;
   end;

   // ----------------------------------------
   // Заполнение данными из временной таблицы
   // ----------------------------------------
   macro fillFromTMP(avr_tmp)
      fillFromTMP(avr_tmp);

      dr_category_mn    = avr_tmp.rec.dr_category_mn;
      repres_fi_isin    = avr_tmp.rec.repres_fi_isin;
      issuer_base_sec   = avr_tmp.rec.issuer_base;
      ratio_numerator   = avr_tmp.rec.issued_size;
      ratio_denominator = avr_tmp.rec.ratio_denominator;

      // Заполнение данными Securities, если есть связь
      if ((isLinkSec == "X") and (FIID > -1))
         var cmd, dataSet;

         cmd = DL_RSDCommand("SELECT avr.T_QTY, avr.T_NumBaseFI, parentAvr.t_ISIN, ptcode.T_Code "+
                             " FROM davoiriss_dbt avr, dfininstr_dbt fin, davoiriss_dbt parentAvr, dfininstr_dbt parentFin, dpartcode_dbt ptcode "+
                             " WHERE avr.T_FIID = ? AND fin.T_FIID = avr.T_FIID" +
                             "   AND parentAvr.T_FIID = fin.T_PARENTFI AND parentFin.T_FIID = parentAvr.T_FIID " +
                             "   AND ptcode.t_codekind (+)= 23 AND ptcode.t_partyID (+)= parentFin.t_ISSUER"
                            );
         cmd.addParam(FIID);

         dataSet = cmd.Execute();
         if (dataSet.moveNext())
            RatioNumeratorSec   = Int(dataSet.QTY);
            RatioDenominatorSec = dataSet.NumBaseFI;
            // Надо для Сравнить (для обновить или добавить м.б. изменено, например, если изменится ISIN у базового)
            RepresFiISINSec     = dataSet.ISIN;
            IssuerBaseNSDSec    = dataSet.Code;
         end;
      end;
      // базовый выпуск
      GetParentFI(repres_fi_isin);
   end;
   // заполнение буферов выпуска общими данными для вставки\обновления
   macro FillCommonAvoirissSec(fin:TRecHandler, iss:TRechandler, action:integer)
      FillCommonAvoirissSec(fin, iss, action);

      iss.rec.QTY       = Numeric(ratio_numerator);
      iss.rec.NumBaseFI = ratio_denominator;

      if (repres_fi_isin != "")
         if (needActionOnParent and (parentAvr != null))
            parentAvr.ActionOnRec();
            parentFIID = parentAvr.FIID;
         end;
         fin.rec.ParentFI = parentFIID;

         if ((fin.rec.ParentFI != -1) and (fin.rec.ParentFI != 0))
            //fin.rec.ParentFI = parentFI;

            // Проверка, что указанный в справочнике НРД эмитент базовой бумаги совпадает с эмитентом, 
            // указанным для базовой бумаги в нашем справочнике. При несовпадении выводить в протокол сообщение

            // ищем ISIN базовой бумаги и код НРД ее эмитента
            var cmd, dataSet;
            
            cmd = DL_RSDCommand("SELECT avr2.t_ISIN, ptcode.T_Code "+
                                " FROM davoiriss_dbt avr2, dfininstr_dbt fin2, dpartcode_dbt ptcode "+
                                " WHERE avr2.T_FIID = ? AND fin2.T_FIID = avr2.T_FIID" +
                                "   AND ptcode.t_codekind (+)= 23 AND ptcode.t_partyID (+)= fin2.t_ISSUER"
                               );

            cmd.addParam(fin.rec.ParentFI);

            dataSet = cmd.Execute();
            if (dataSet.moveNext())
               RepresFiISINSec  = dataSet.ISIN;
               IssuerBaseNSDSec = dataSet.Code;
            end;

            if ((issuer_base_sec != "") AND (issuer_base_sec != IssuerBaseNSDSec))
               logger.warning(code_nsd, "Указанный в справочнике НРД эмитент базовой бумаги ("+issuer_base_sec+") не совпадает с эмитентом, "+
                                        "указанным для базовой бумаги в справочнике RS ("+IssuerBaseNSDSec+")");
            end;
         else
            logger.warning(code_nsd, "Не найден базовый выпуск депозитарной расписки " + isin + " \"" + name_full + "\" в справочнике RS");
         end;
      end;

      if (action == ACT_INSERT)
         fin.rec.FaceValueFI = GetPtCountryFI(fin.rec.Issuer);

         if(fin.rec.FaceValueFI == -1)
           logger.warning(code_nsd, "Нельзя определить валюту выплаты доходов по ДР с кодом НРД "+code_nsd+": не указана страна эмитента ДР");
         end;

      end;
   end;

   // Заполнение объекта для отчета общими данными.
   // Перегружена
   private macro FillDataForReport(avr, action)
      FillDataForReport(avr, action);

      avr.CategDR       = dr_category_mn;
      avr.BaseISIN      = repres_fi_isin;
      avr.BaseIssuerNSD = issuer_base_sec;
      avr.RatioNum      = ratio_numerator;

      if ((action == ACT_UPDATE) or (action == ACT_MERGE))
         avr.rsCategDR        = GetCategoryNSD(GetAvoirKind());
         avr.rsBaseISIN       = RepresFiISINSec;
         avr.rsBaseIssuerNSD  = IssuerBaseNSDSec;
         avr.rsRatioNum       = RatioNumeratorSec;
      end;
   end;

   // есть ли отличия для обновления?
   macro IsChangesForUpd() : bool
      var ret = false;      
      if (isLinkSec == "X")
         ret = IsChangesForUpd();
         if (not ret)
            ret = (//(RepresFiISINSec     != repres_fi_isin   ) or
                   needActionOnParent                         or
                   (RatioNumeratorSec   != ratio_numerator  ) or
                   (RatioDenominatorSec != ratio_denominator)
                  );
         end;
      end;
      return ret;
   end;

   macro Insert()
      if (repres_fi_isin == "")
         logger.error(code_nsd, "Для депозитарной расписки " + isin + " \"" + name_full + "\" не задан базовый выпуск");
      elif ((repres_fi_isin != "") and (parentFIID == -2)) // Не найден базовый выпуск в справочниках НРД
         logger.error(code_nsd, "Для депозитарной расписки " + isin + " \"" + name_full + "\" не найден базовый выпуск " + repres_fi_isin + " в справочниках НРД");
      else
         return Insert(); // базовый
      end;
      return 1;
   end;
   macro Update()
      if ((repres_fi_isin != "") and (parentFIID == -2)) // Не найден базовый выпуск в справочниках НРД
         logger.error(code_nsd, "Для депозитарной расписки " + isin + " \"" + name_full + "\" не найден базовый выпуск " + repres_fi_isin + " в справочниках НРД");
      else
         return Update(); // базовый
      end;
      return 1;
   end;

   initTAvoirData(AVOIRISSKIND_DEPOSITORY_RECEIPT, isProtocolExt_);
   this.clear();
end;

// ************************************
// Очистить временные таблицы
// ************************************
macro TruncateTablesSIRNSD():bool
   SQL_Truncate(SIRNSD_AVOIR_TABLE_SQL);
   SQL_Truncate(SIRNSD_CODES_TABLE_SQL);
   SQL_Truncate(SIRNSD_WARNT_TABLE_SQL);
   SQL_Truncate(SIRNSD_ORG_TABLE_SQL  );
   SQL_Truncate(SIRNSD_LIC_TABLE_SQL  );
   SQL_Truncate(SIRNSD_FACEVALUE_TABLE_SQL);
   return true;
   
   onError(err)
      var logger = CMessageSIRNSD();
      logger.error("Ошибка при очистке временных таблиц");
      return false;
end;

/* Создание объекта по ЦБ*/
macro CreateDataAvr(avr_tmp, isProtocolExt:bool):TAvoirData
   var data:TAvoirData = null;
   
   if (avr_tmp.kindData == AVOIRISSKIND_BOND)
      data = TBondData(isProtocolExt);
   elif (avr_tmp.kindData == AVOIRISSKIND_SHARE)
      data = TShareData(isProtocolExt);
   elif (avr_tmp.kindData == AVOIRISSKIND_INVESTMENT_SHARE)
      data = TUnitData(isProtocolExt);
   elif (avr_tmp.kindData == AVOIRISSKIND_DEPOSITORY_RECEIPT)
      data = TDepoData(isProtocolExt);
   end;

   if (data != null)
      data.fillFromTMP(avr_tmp);
   end;

   return data;
end;
