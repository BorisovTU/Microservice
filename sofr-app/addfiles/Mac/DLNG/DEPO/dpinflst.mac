/*
$Name:         dpinflst.mac
$Module:       Депозитарий
$Description:  Отчет "Журнал информационных операций"
*/
import SecInter, DPInter, dprepsec, cb_sql;

private record SpLogOpr( splogopr ); 
private file SpOper( spoper ); 
private file spinfopr( spinfopr );
private file spground( spground );
private file Report( spground ) key 6;
private file SpgrReport( spground );
private file GrRep( dpgrrep );
private file typeac( typeac );
private file partition(depoacnt);
private file account("account");
private file fininstr(fininstr);
private file avoir(avoiriss);
private file depoacnt(depoacnt);
private file depoac(depoac);
private file part_depoac(depoac);
private file oprkoper(oprkoper);
private file llvalues(llvalues);
private file invcard("invcard.dbt");

private const ALG_DEPO_OPSTATUS = 3227; /* статус операции (исполнена/отказано в исполнении/не задано) */
private const ALG_DPINFSTATE    = 3218; /* статусы информационных операций*/
private const ALG_DINFOBJ       = 3127; /* виды объектов отчетов информационных операций*/
private const ALG_DPINFDUBLE    = 3219; /* оригинал/дубликат инф. операции*/
private const ALG_DEPO_DRAFTREFKIND = 3252; /* виды журналов номеров регистрации поручения депо */
private const SP_DEPOPER_INFOPER= 830; /*Информационная операция ДЕПО*/
private const TA_STAT_DEL       = 152;
private const SP_INV_OPERATION = 1;
private const SP_ADM_OPERATION = 2;
private const SP_INF_OPERATION = 3;
private const OBJTYPE_ADMOPRTYPE = 1207; /* типы административных операций */

private var NumRecord = 1;
var запуск_из_инф_операций = true;
var _IsApostr;

var HeadTable = "┌───┬─────┬─────┬────────────────┬────────────────┬─────────────────────────────────┬─────────────────────┬────────────────────────────────────┬─────────────────────────────────────┐\n"+
                "│ № │  №  │ КОП │Поручение       │Операция        │ Тип отчета по классификации     │Отчетный период      │Идентификаторы материалов, по кото- │ Отчет об исполнении информационной  │\n"+
                "│п/п│опер.│     │дата приема/вх.№│дата исп./время │ депозитария                     │                     │рым выдается отчет                  │ операции                            │\n"+
                "├───┼─────┼─────┼────────────────┼────────────────┼─────────────────────────────────┼─────────────────────┼────────────────────────────────────┼─────────────────────────────────────┤\n"+
                "│ 1 │  2  │  3  │       4        │        5       │                     6           │           7         │                 8                  │                   9                 │\n"+
                "├───┼─────┼─────┼────────────────┼────────────────┼─────────────────────────────────┼─────────────────────┼────────────────────────────────────┼─────────────────────────────────────┤";
                
var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);

const RepFontStyleTitel =  "ex_FS(b)";

macro InExcel(ToExcel)
   if( ToExcel )
     Rep.SetZoomType( ZOOM_TYPE_A4L );
     Rep.PrintWinRep();
     Rep.ShowWinRep();
   else
     Rep.PrintRep();
   end;
end;

macro InfListPreHeader()
   if( запуск_из_инф_операций == true )
     //InfListPreHeader() вызывается первой из GenInfReport(), файл dpinfdoc.c; поэтому здесь вызывается DP_BeginProtocol()
     DP_BeginProtocol();   //начать протоколирование - подмена MsgBox
     InitProgress( -1, "Формирование отчета...", "Журнал информационных операций" );
   end;
   DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "ЖУРНАЛ ИНФОРМАЦИОННЫХ ОПЕРАЦИЙ", 0, 0, false, null, null, tablewidth );
end;

macro InfListFooter()
  var Excel;
  var err;

  Rep.AddEmptyStr();
  Rep.AddEmptyStr();
  DP_StdAuthPerson_Ex(Rep, RepFontStyleTitel);
  DP_StdDeposInfo_Ex(Rep, RepFontStyleTitel);
  DP_StdExecReport_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);
  if( запуск_из_инф_операций == true )
     RemProgress();

     GetRegistryValue( "DEPO\\DEPOREPTOEXCEL", V_BOOL, Excel, err );
     //InfListFooter() вызывается последней из GenInfReport(), файл dpinfdoc.c; поэтому здесь вызывается DP_EndProtocol()
     if(not err)
        DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
        DP_EndProtocol();                 //закончить протоколирование

        InExcel(Excel);
     else
        MsgBox("Не могу найти настройку выпуска отчетов в Excel");

        DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
        DP_EndProtocol();                 //закончить протоколирование

        InExcel(false);
     end;
  end;
end;

macro InfListFilter( xDraftNum:BOOL,
                       DraftNumBeg:STRING,
                       DraftNumEnd:STRING,
                     xUserLog:BOOL,
                       UserLog:INTEGER,
                     xDraftDate:BOOL,
                        DraftDateBeg:DATE,
                        DraftDateEnd:DATE,
                     xDraftState:BOOL,
                        iState:INTEGER,
                     xRepKind:BOOL,
                        iRepID:INTEGER,
                     xRepNum:BOOL,
                        RepNumBeg:STRING,
                        RepNumEnd:STRING,
                     xObjType:BOOL,
                        iObjType:INTEGER,
                     xObjCode:BOOL,
                        iObjectID:INTEGER,
                        ObjectCode:STRING,
                     xOrig:BOOL,
                        IsDuble:INTEGER,
                     xIsExistsDub:BOOL,
                        IsExistsDuble:INTEGER,
                     xSent:BOOL,
                        WasSent:INTEGER,
                     xOpState:BOOL,
                        iOpState:INTEGER,
                     xOpNum:BOOL,
                        OpNumBeg:STRING,
                        OpNumEnd:STRING,
                     xOpDate:BOOL,
                        OpDateBeg:DATE,
                        OpDateEnd:DATE,
                     xOpRepNum:BOOL,
                        OpRepNumBeg:STRING,
                        OpRepNumEnd:STRING,
                     xDepart:BOOL,
                        Department:INTEGER,
                     xBranch:BOOL,
                        Branch:INTEGER
)
   var ReportParm = "";

   if( (xOpDate == true) OR (xOpNum == true) )
      DP_AddPrintCell( Rep,   "Информационная операция", tablewidth, 0, "l:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
      Rep.AddStr();
      if( xOpDate == true )
         DP_AddPrintCell( Rep,  "     дата исполнения                 ", 37, 0, "l:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
         DP_AddPrintCell( Rep,  OpDateBeg, 10, 0, "l:f:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
         DP_AddPrintCell( Rep,  " - ", 3, 0, "l:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
         DP_AddPrintCell( Rep,  OpDateEnd, 10, 0, "l:f:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
         Rep.AddStr();
      end;
      if( xOpNum == true )
         DP_AddPrintCell( Rep,  "     номер                           " + OpNumBeg + " - " + OpNumEnd, tablewidth, 0, "l:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
         Rep.AddStr();
      end;
   end;

   if( (xDraftNum== true) OR (xUserLog == true) OR (xDraftDate== true) OR (xDraftState == true) OR (xOpState == true) )
      DP_AddPrintCell( Rep,  "Поручение", 100, 0, "l:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
      Rep.AddStr();
      if( xDraftState == true )
         DP_AddPrintCell( Rep,  "     статус                          " + DL_GetNameAlg( ALG_DPINFSTATE, iState), tablewidth, 0, "l:w:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
         Rep.AddStr();
      end;

      if( xOpState == true )
         DP_AddPrintCell( Rep,  "     состояние                       " + DL_GetNameAlg( ALG_DEPO_OPSTATUS, iOpState), tablewidth, 0, "l:w:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
         Rep.AddStr();
      end;

      if( xDraftNum == true )
         DP_AddPrintCell( Rep,  "     входящий номер                  " + DraftNumBeg+" - " +DraftNumEnd, tablewidth, 0, "l:w:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
         Rep.AddStr();
      end;

      if( xUserLog == true )
         DP_AddPrintCell( Rep,  "     по журналу                      " + DL_GetNameAlg( ALG_DEPO_DRAFTREFKIND, UserLog ), tablewidth, 0, "l:w:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
         Rep.AddStr();
      end;

      if( xDraftDate == true ) 
         DP_AddPrintCell( Rep,  "     дата приема                     " + DateToStr(DraftDateBeg, true)+" - " +DateToStr(DraftDateEnd, true), tablewidth, 0, "l:w:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
         Rep.AddStr();
      end;
   end;

   if( (xRepKind == true) OR (xObjType == true) OR (xObjCode == true) )
      DP_AddPrintCell( Rep,  "Отчет", 10, 0, "l:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
      Rep.AddStr();
      if( xRepKind == true )
          ClearRecord( SpOper );
          SpOper.operationCode = iRepID;
          if( GetEQ(SpOper) )
             DP_AddPrintCell( Rep,  "     тип отчета                      " + SpOper.operationName, tablewidth, 0, "l:w:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
             Rep.AddStr();
          end;
      end;

      if( xObjType == true )
         DP_AddPrintCell( Rep,  "     основной объект                 " + DL_GetNameAlg( ALG_DINFOBJ, iObjType), tablewidth, 0, "l:w:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
         Rep.AddStr();
      end;

      if( xObjCode == true )
         DP_AddPrintCell( Rep,  "     код основного объекта           " + ObjectCode, tablewidth, 0, "l:w:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
         Rep.AddStr();
      end;
   end;

   if( (xOrig== true) OR (xSent== true) )
      if( xOrig == true )
         ReportParm = DL_GetNameAlg( ALG_DPINFDUBLE, IsDuble );
      end;
      if( xSent == true )
         if( ReportParm != "" )
            ReportParm = ReportParm + ", ";
         end;
         if( WasSent == 1 )
            ReportParm = ReportParm + " Не отправлен";
         else 
            ReportParm = ReportParm + "Отправлен";
         end;
      end;

      DP_AddPrintCell( Rep,  "Характеристики отчета : " + ReportParm, tablewidth, 0, "l:w:" + RepFontStyleTitel, _IsApostr, REP_ELEM_STR );
      Rep.AddStr();
   end;

   Rep.AddEmptyStr();
end;

/*Формирование строки - "Отчет об исполнении информационной операции"*/
private macro ОтчетОбИсполненииИО()
  var LineReport = "";
  var cmd, DS;

  ClearRecord( GrRep );
  GrRep.SourceDocID   = SpLogOpr.AutoKey;
  GrRep.SourceDocKind = SP_DEPOPER_INFOPER;
  GrRep.Recipient     = SpLogOpr.Initiator;
  if( GetEQ( GrRep ) )
     cmd = DL_RSDCommand("select t_RepSPgroundID from ddprepdoc_dbt where t_GrRepID = ?");
     cmd.AddParam(GrRep.ID);
     DS = cmd.Execute();
     while(DS.moveNext())
       ClearRecord( SpgrReport );
       SpgrReport.SPgroundID = DS.RepSPgroundID;
       if( GetEQ( SpgrReport ) )
          if(LineReport != "")
            LineReport = LineReport + "\n";
          end;
          LineReport = LineReport + "№ " + SpgrReport.Xld + ", составлен " + DateToStr(SpgrReport.SignedDate, true);
          if( SpgrReport.Sent == "X" )
            LineReport = LineReport + ",\nотослан " + GrRep.RecipientName + ",\n" + DateToStr(SpgrReport.RegistrDate, true);
            ClearRecord(typeac);
            typeac.iNumType = TA_STAT_DEL;
            typeac.Type_Account = StrFor(GrRep.DeliveryKind);
            if( GetEQ( typeac ) )
               LineReport = LineReport +  ", " + typeac.Name_Type;
            end;
          end;
       end;
     end;
  end;

  return LineReport;
end;

private macro GetDepoAccInfo( DepoID:INTEGER, da:VARIANT, dt:VARIANT )
   ClearRecord( da );
   da.AutoKey = DepoID;
   if( GetEQ( da ) )
      if( da.Type > 0 )
         ClearRecord( dt );
         dt.ID = da.Type;
         GetEQ( dt );
      end;
   end;
end;

private macro GetAccountByID(AccountID)
  var cmd, DataSet;
  var Account = "";

  cmd = DL_RSDCommand("select t_Account from daccount_dbt where t_AccountID = ?");
  cmd.AddParam(AccountID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    Account = DataSet.Account;
  end;

  return Account;
end;

/*получить строку - Идентификаторы материалов, по которым выдается отчет*/
private macro ИдентификаторыМатериаловОтчета()
  var PartCode = "", objType = "", obj = "", parm = "";
  
  ClearRecord( Report );
  Report.SourceDocKind = SP_DEPOPER_INFOPER;
  Report.SourceDocID   = SpLogOpr.AutoKey;
  Report.Direction     = EXITING;
  Report.Department    = {OperDprt};
  Report.Division      = SelfDivision;
  GetEQ( Report );

  if( spinfopr.PartyID > -1 )  /* субъект */
     PartCode = ПолучитьКодСубъекта( spinfopr.PartyID, PTCK_CONTR );
  end;

  if( spinfopr.DepoAcc > 0 ) /* счет */
     GetDepoAccInfo( spinfopr.DepoAcc, depoacnt, depoac );
  end;

  if( spinfopr.DepoPart > 0 ) /* раздел */
     GetDepoAccInfo( spinfopr.DepoPart, partition, part_depoac );
  end;

  if( spinfopr.FIID > 0 ) /* ФИ */
     ПолучитьФинИн( spinfopr.FIID, fininstr, avoir );  
  end;

  if( spinfopr.Account != "" ) /* л/с */
    ClearRecord(account);
    account.Account = spinfopr.Account;
    account.Chapter = 5;
    if( spinfopr.FIID != -1 )
      account.Code_Currency = spinfopr.FIID;
    else
      account.Code_Currency = 0;
    end;
    GetEQ(account);
  end;

  if( spinfopr.InvCardID ) /* ИК */
    ClearRecord(invcard);
    invcard.InvCardID = spinfopr.InvCardID;
    GetEQ( invcard );
  end;

  obj  = "все";
  parm = "дополнительно:\n";
  if( SpOper.Item == DPINF_UNDEF )
     obj = parm = objType = "";

  elif(SpOper.Item == DPINF_DACCOUNT)
     objType = "обьект: счет депо - "; 
     if( spinfopr.DepoAcc > 0 )
       obj = depoacnt.BriefCode;
       parm = parm + "владелец " + GetPartyName( depoacnt.Owner, false ) + "\n";
     end;

  elif(SpOper.Item == DPINF_DPARTITION)
     objType = "обьект: раздел - "; 
     if( spinfopr.DepoPart > 0 )
       obj = partition.BriefCode;
       parm = parm + "владелец " + GetPartyName( partition.Owner, false ) + "\n";
     end;
     
  elif(SpOper.Item == DPINF_FACCOUNT)
     objType = "объект: л/с - ";
     if( spinfopr.Account != "" )
       obj = spinfopr.Account;
     end;

     parm =  parm +   "владелец             ";
     if( spinfopr.PartyID > -1 )  /* субъект */
        parm = parm + ПолучитьКодСубъекта( spinfopr.PartyID, PTCK_CONTR ) + "\n";
     else
        parm = parm + "все\n";
     end;

     parm =  parm +   "счет депо            ";
     if (spinfopr.DepoAcc > 0)
       parm = parm + depoacnt.BriefCode + "\n";
     else
       parm = parm + "все\n";
     end;

     parm = parm + "раздел счета депо    ";
     if (spinfopr.DepoPart > 0)
       parm = parm + partition.BriefCode + "\n";
     else
       parm = parm + "все\n";
     end;                                        

  elif( SpOper.Item == DPINF_PARTY )
     objType = "объект: субъект - ";
     if( spinfopr.PartyID > -1 )
        obj = PartCode + "  " + GetPartyName( spinfopr.PartyID, true );
     end;

     parm =  parm +   "счет депо            ";
     if (spinfopr.DepoAcc > 0)
       parm = parm + depoacnt.BriefCode + "\n";
     else
       parm = parm + "все\n";
     end;

  elif( SpOper.Item == DPINF_AVOIRISS )
     objType = "объект: ценная бумага - ";
     if( spinfopr.FIID > 0 )
       obj = fininstr.FI_Code + "    " + fininstr.Name;
       parm = parm + 
              "эмитент: " + GetPartyName( fininstr.Issuer, true ) + "\n" + 
              "номинал: " + ЧислоCЗаданнойТочностью(GetFaceValue(fininstr.FIID, spground.RegistrDate), fininstr.Point+2) + "\n" + 
              "дата выпуска: " + string(fininstr.Issued:f);
     end;

  elif( SpOper.Item == DPINF_CERTIF )
     objType = "объект: инвентарная карточка - ";
     if( spinfopr.InvCardID )
        obj = invcard.Number + "    " + GetAccountByID(invcard.HAccountID);
     end;

  elif( SpOper.Item == DPINF_ORDER )
     objType = "объект: поручение - ";
     if( spinfopr.SubType == SP_INF_OPERATION )
        obj = SpOper.OperationName;
     elif( spinfopr.SubType == SP_INV_OPERATION )
        ClearRecord(oprkoper);
        oprkoper.Kind_Operation = spinfopr.SubKind;
        if( GetEQ(oprkoper) )
           obj = oprkoper.ShortName;
        end;
     elif( spinfopr.SubType == SP_ADM_OPERATION )
        ClearRecord(llvalues);
        llvalues.List    = OBJTYPE_ADMOPRTYPE;
        llvalues.Element = spinfopr.SubKind; 
        if( GetEQ(llvalues) )
           obj = llvalues.Name;
        end;
     end;
     obj = obj + " - № " + spground.Xld;

     parm =  parm +   "субъект              ";
     if( spinfopr.PartyID > -1 )  /* субъект */
        parm = parm + ПолучитьКодСубъекта( spinfopr.PartyID, PTCK_CONTR ) + "\n";
     else
        parm = parm + "все\n";
     end;

     parm =  parm +   "счет депо            ";
     if (spinfopr.DepoAcc > 0)
       parm = parm + depoacnt.BriefCode + "\n";
     else
       parm = parm + "все\n";
     end;

     parm = parm + "раздел счета депо    ";
     if (spinfopr.DepoPart > 0)
       parm = parm + partition.BriefCode + "\n";
     else
       parm = parm + "все\n";
     end;                                        

     parm = parm + "лицевой счет         ";
     if (spinfopr.account != "")
       parm = parm + spinfopr.Account + "\n";
     else
       parm = parm + "все\n";
     end;

     parm = parm + "ценная бумага        ";
     if (spinfopr.FIID > 0) 
       parm = parm + fininstr.FI_Code + "  " + fininstr.Name + "\n";        
     else
       parm = parm + "все\n";
     end;

     parm = parm + "Инвентарная карточка ";
     if (spinfopr.InvCardID)
       parm = parm + invcard.Number + "\n";
     else
       parm = parm + "все\n";
     end;    
  end;

  return ("номер отчета: " + Report.Xld + "\n" + objType + obj + "\n" + parm);
end;

macro InfListLine( LogOpr:VARIANT )
  var КОП = "";
  var buf_str = "";

  if( (LogOpr != null) AND (Valtype(LogOpr) == V_MEMADDR) ) /* вызов из списка ИО */
     SetBuff( SpLogOpr, LogOpr ); 
  end;

  ClearRecord( spinfopr );
  spinfopr.spinfdocID = SpLogOpr.recId;
  if( GetEQ(spinfopr) )
     ClearRecord( SpOper );
     SpOper.operationCode = spinfopr.docType;
     if( GetEQ(SpOper) )
        КОП = SpOper.OperCode;
     end;
  end;

  ClearRecord( spground );
  spground.SPgroundID = SpLogOpr.Draft;
  GetEQ( spground );

  DP_AddPrintCell( Rep,  NumRecord, 0, 0, "c:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  SpLogOpr.OprXid, 0, 0, "c:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  КОП, 0, 0, "c:" + RepFontStyleTitel );
  buf_str = string(spground.RegistrDate:f) +" "+ spground.Xld;
  DP_AddPrintCell( Rep,  buf_str, 0, 0, "l:" + RepFontStyleTitel );
  buf_str = string(SpLogOpr.CarryDate:f) +" "+ SPTimeSplit( SpLogOpr.CarryTime );
  DP_AddPrintCell( Rep,  buf_str, 0, 0, "l:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  SpOper.OperationName, 0, 0, "l:w:" + RepFontStyleTitel );
  if(date(spinfopr.FromDate) == date(0,0,0))
    buf_str = DateToStr(spinfopr.ToDate, true);
  else
    buf_str = DateToStr(spinfopr.FromDate, true)+"-"+DateToStr(spinfopr.ToDate, true);
  end;
  DP_AddPrintCell( Rep,  buf_str, 0, 0, "l:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  ИдентификаторыМатериаловОтчета(), 0, 0, "l:w:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  ОтчетОбИсполненииИО(), 0, 0, "l:w:" + RepFontStyleTitel );
  Rep.AddStr();

  if( запуск_из_инф_операций == true )
    UseProgress( NumRecord );
  end;
  NumRecord = NumRecord + 1;
end;

private macro GetSQLStr(
   NumDprt:INTEGER,    /*филиал*/
   BeginDate:DATE,     /*Отчетный период*/
   EndDate:DATE,      
   /*поручение*/
   DraftStatus:INTEGER,/*статус поручения*/
   OpStatus:INTEGER,   /*состояние поручения*/            
   OpXldBegin:STRING,  /*диапозон входящих номеров*/  
   OpXldEnd:STRING,        
   OpStatusBegin:DATE, /*Диапозон исполнения информационной операции.*/
   OpStatusEnd:DATE,   
   /*Отчет*/
   ReportKind:INTEGER, /*Тип отчета*/            
   ObjType:INTEGER,    /*Вид основного объекта. В зависимости от вида, задается либо ID либо код объекта*/
   ObjID:INTEGER,      /*ID основного объекта*/    
   ObjCode:STRING,     /*Код основного объекта*/    
   /*Характеристики отчета*/
   IsSent:BOOL,        /*Отправлен*/
   IsDup:BOOL,         /*Дубликат*/         
   IsNotSend:BOOL,     /*Не отправлен*/         
   IsOrigin:BOOL,      /*Оригрнал*/         
   IsApostr:BOOL       /*Апострофы*/
  )

  var query = " t_Department = "+NumDprt+" and t_RecId in "
              "   ( select t_spinfdocID from dspinfopr_dbt where t_spinfdocID > 0 ";
 
  if( BeginDate != Date(0,0,0)  )
     query = query + " and t_FromDate >= "+GetSQLDate(BeginDate)+" ";
  end;
  
  if( EndDate != Date(0,0,0) )
     query = query + " and t_ToDate <= "+GetSQLDate(EndDate)+" ";
  end;


  if( ObjType != DPINF_UNDEF )
      query = query + " and t_DocType in ( select t_OperationCode from dspoper_dbt where t_Item = "+ObjType+" ) "; 
      query = query + " and t_DocType = " + ReportKind;

         if( ObjType == DPINF_DACCOUNT ) 
           if( ObjID > 0 )
            query = query + " and  t_DepoAcc = " + ObjID;
           end;
         end;

         if( ObjType == DPINF_DPARTITION )
            if( ObjID > 0 )
               query = query + " and t_DepoPart = " + ObjID;
            end;
         end;
         
         if( ObjType == DPINF_FACCOUNT )
            if( ObjCode != "" )
               query = query + " and t_Account = '"+ObjCode+"'";
            end;
         end;

         if( ObjType == DPINF_PARTY )
            if( ObjID > 0 )
               query = query + " and t_PartyID = " + ObjID;
            end;
         end;

         if( ObjType == DPINF_AVOIRISS )
            if( ObjID > 0 )
               query = query + " and t_FIID = " +ObjID;
            end;
         end;

         if( ObjType == DPINF_CERTIF )
            if( ObjCode != "" )
               query = query + " and t_InvCardID IN (select t_InvCardID from dinvcard_dbt where t_Number = '"+ObjCode+"')";
            end;
         end;

         if( ObjType == DPINF_DPARTITION )
            if( ObjCode != "" )
               query = query + " and t_Draft in ( select SPgroundID from dspground_dbt where t_Xld = '"+ObjCode+"' ) ";
            end;
         end;
   end;

     query = query + " ) ";

  if( DraftStatus > 0 )
     query = query + " and t_State = "+DraftStatus+" ";
  end;

  if( OpStatus != 2 )
     query = query + " and t_OperState = "+OpStatus+" ";
  end;

  if( OpStatusBegin != Date(0,0,0) )
     query = query + " and t_CarryDate >= "+GetSQLDate(OpStatusBegin)+" ";
  end; 

  if( OpStatusEnd != Date(0,0,0) )
     query = query + " and t_CarryDate <= "+GetSQLDate(OpStatusEnd)+" ";
  end;

  query = query + " and t_Draft in "
                  "   ( select t_SPgroundID from dspground_dbt where t_SPgroundID > 0 ";
 
  if( OpXldBegin != "" )
     query = query + "     and t_Xld >= '"+OpXldBegin+"' ";
  end;

  if( OpXldEnd != "" )
     query = query + "     and t_Xld <= '"+OpXldEnd+"' ";
  end;
     
     query = query + "   ) ";

  if( (IsDup == true) AND (IsOrigin == false) )
      query = query + " and t_IsDubl = 'X' ";
  end;

  if( (IsOrigin == true) AND (IsDup == false) )
      query = query + " and t_IsDubl != 'X' ";
  end;


  query = query + " and t_AutoKey in "
                  "   ( select t_SourceDocID from dspground_dbt where t_SourceDocKind = "+SP_DEPOPER_INFOPER+" "
                  "     and t_Direction = "+EXITING+" "
                  "     and t_Division = "+SelfDivision+" ";
                  

  if( (IsSent == true) AND (IsNotSend == false) )
     query = query + " and t_Sent = 'X' ";
  end;
  if( (IsNotSend == true) AND (IsSent == false) )
     query = query + " and t_Sent != 'X' ";
  end;

     query = query + "   ) ";

  return query;
end;

macro RunReport(
   NumDprt:INTEGER,    /*филиал*/
   BeginDate:DATE,     /*Отчетный период*/
   EndDate:DATE,      
   /*поручение*/
   DraftStatus:INTEGER,/*статус поручения*/
   OpStatus:INTEGER,   /*состояние поручения*/            
   OpXldBegin:STRING,  /*диапозон входящих номеров*/  
   OpXldEnd:STRING,        
   OpStatusBegin:DATE, /*Диапозон исполнения информационной операции.*/
   OpStatusEnd:DATE,   
   /*Отчет*/
   ReportKind:INTEGER, /*Тип отчета*/            
   ObjType:INTEGER,    /*Вид основного объекта. В зависимости от вида, задается либо ID либо код объекта*/
   ObjID:INTEGER,      /*ID основного объекта*/    
   ObjCode:STRING,     /*Код основного объекта*/    
   /*Характеристики отчета*/
   IsSent:BOOL,        /*Отправлен*/
   IsDup:BOOL,         /*Дубликат*/         
   IsNotSend:BOOL,     /*Не отправлен*/         
   IsOrigin:BOOL,      /*Оригрнал*/         
   ToExcel:BOOL,       /*In Excel*/
   IsApostr:BOOL       /*Апострофы*/         
)
   var  Num = 0;
   var LogOpr = TBFile("splogopr"); 

   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

   запуск_из_инф_операций = false;
   ClearRecord(LogOpr);
   
   var sQuery = GetSQLStr( NumDprt, 
                   BeginDate, EndDate,
                   DraftStatus, 
                   OpStatus,
                   OpXldBegin, OpXldEnd,
                   OpStatusBegin, OpStatusEnd,
                   ReportKind,
                   ObjType,
                   ObjID,
                   ObjCode,
                   IsSent,
                   IsDup,
                   IsNotSend,
                   IsOrigin,
                   IsApostr
                   );

   _IsApostr = IsApostr;
   LogOpr.Clear();
   LogOpr.AddFilter(sQuery);
   InitProgress( nrecords(LogOpr), "Формирование отчета...", "Журнал информационных операций" );
   while( next(LogOpr) )
     Copy( SpLogOpr, LogOpr ); 
        if( NumRecord == 1 )
           InfListPreHeader();
        end;
        InfListLine(null);
   
     UseProgress( Num = Num + 1 );
   end;

   RemProgress();

   if( NumRecord > 1 )
      InfListFooter();

      DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
      DP_EndProtocol();                 //закончить протоколирование

      InExcel(ToExcel);
   else
      if(not DP_ProtocolIsEmpty())  //если протоколо не пуст, то выводим пустой отчет с протоколом
        DP_AddPrintCell(Rep, "Не найдено данных, удовлетворяющих параметрам отчета", 55, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR);
        Rep.AddStr();

        DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
        DP_EndProtocol();                 //закончить протоколирование

        if( ToExcel )
          Rep.PrintWinRep();
          Rep.ShowWinRep();
        else
          Rep.PrintRep();
        end;
      else
        DP_EndProtocol();                 //закончить протоколирование
      end;

      MsgBox("Не найдено данных, удовлетворяющих параметрам отчета");
   end;
   return 0;
end;
