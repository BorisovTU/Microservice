/*
$Name: dpdprep.mac
$Module: Депозитарий
$Description: Отчет "Журнал исходящих документов"
*/
import RsbDataSet;
import FIInter, CurrInter, CTInter, DPInter, secinter, "deposerv.mac";

var NameAlg  = TBFile("namealg");
var llvalues = TBFile("llvalues");
var party    = TBFile("party");
var person   = TBFile("person");

//пустая строка в Oracle
const EMPTY_STRING = 1;

private const  SP_DEPOPER_ADMOPER       = 825;//административная операция депо 
private const  SP_DEPOPER_INFOPER       = 830;//информационная операция депо

private const ALG_DEPO_DRAFTREFKIND = 3252; // виды журналов номеров регистрации поручения депо

private const SPSK_OPEN    = 0;
private const SPSK_NEW     = 4;

private const SPKK_PASSIVE = 2;

private const TA_STAT_DEL = 152;

private var DataSet;

private var HeadTable = "┌─────┬───────┬──────────────┬────────────────────────────┬───────────────┬─────────────────┬────────────────┬─────────────┬────────────────┬────────────┬─────────────┬─────────────────────────┬─────────────────────────┬──────────────┐\n"+
                        "│  №  │  Исх. │  Дата/время  │  Основание предоставления  │   Инициатор   │                 │                │             │   Получатель   │   Способ   │ Дата выдачи │                         │                         │ Сформировано │\n"+
                        "│ п/п │   №   │  составления │           отчета           │   операции    │  Вид документа  │    Депонент    │  Счет депо  │     отчета     │  передачи  │  документа  │        Примечания       │        Исполнитель      │ техническими │\n"+
                        "│     │       │              │                            │               │                 │                │             │                │            │             │                         │                         │  средствами  │\n"+
                        "├─────┼───────┼──────────────┼────────────────────────────┼───────────────┼─────────────────┼────────────────┼─────────────┼────────────────┼────────────┼─────────────┼─────────────────────────┼─────────────────────────┼──────────────┤\n"+
                        "│  1  │   2   │       3      │             4              │       5       │        6        │        7       │      8      │        9       │     10     │     11      │            12           │            13           │      14      │\n"+
                        "├─────┼───────┼──────────────┼────────────────────────────┼───────────────┼─────────────────┼────────────────┼─────────────┼────────────────┼────────────┼─────────────┼─────────────────────────┼─────────────────────────┼──────────────┤";

                
private var tablewidth = Index(HeadTable, "\n");
private var Rep = CMakeReport(HeadTable);
private var _is_ap;
private const RepFontStyleTitel =  "ex_FS(b)";

private macro __NameAlg(Type, Number)
  NameAlg.rec.iTypeAlg = Type;
  NameAlg.rec.iNumberAlg = Number;
  if( GetEQ(NameAlg) )
    return NameAlg.rec.szNameAlg;
  end;
  return "";
end;

macro __SPOPER( Kind )
  file spoper("spoper");
  spoper.OperationCode = Kind; 
  if(getEQ(spoper))
    return spoper.OperationName;
  end;

  return NULL;
end;

macro __LLValues( List, Number)
  llvalues.rec.List = List;
  llvalues.rec.Element = Number;
  if(llvalues.GetEQ)
    return llvalues.rec.Name;
  end;
  return NULL;
end;

macro __PersonName( Oper )
  person.rec.Oper = Oper;
  if(person.GetEQ())
    return person.rec.Name;
  end;
  return "";
end;

macro FillReport( DeliveryKind )
  file typeac("typeac");

  typeac.iNumType = TA_STAT_DEL;
  typeac.Type_Account = DeliveryKind;
  if( getEQ(typeac) )
     return typeac.Name_type; 
  end;

  return NULL;
end;

private macro GetSqlDepoAccByOwner(iDepart, owner, IsClient, iDepoAcc)
   var aDepoAcc;
   if(iDepoAcc != null)
      aDepoAcc = iDepoAcc;
   else
      aDepoAcc = 
      " SELECT depoacnt.t_AutoKey "+
      " FROM ddepoacnt_dbt depoacnt " +
      " WHERE depoacnt.t_Superior = 0 " +
      "   AND depoacnt.t_Department = " + iDepart +
      "   AND depoacnt.t_Kind = " + SPKK_PASSIVE +
      "   AND depoacnt.t_Status IN (" + SPSK_OPEN + "," + SPSK_NEW + ") " +
      "   AND depoacnt.t_Owner " + IIF(owner != null, "=" + owner, IIF(IsClient, "<>", "=") + {OurBank}) +
      "    START WITH depoacnt.t_Superior = 0 " +
      "    CONNECT BY PRIOR depoacnt.t_AutoKey = depoacnt.t_Superior ";
   end;

   return " select acnt.t_AutoKey from ddepoacnt_dbt acnt where acnt.t_Root IN (" + aDepoAcc + ")";
end;

macro DPGetSqlString( iDepart:integer, ExtDocKind:integer,
                    Number_doc_beg:string,
                    Number_doc_end:string,
                    Date_doc_beg:date,
                    Date_doc_end:date,
                    PartyCode:integer,
                    iSentKind:integer,
                    isSent:bool,
                    UserLog:integer,
                    ByRegstr:bool,
                    IsOwnAcc:bool,
                    IsClientAcc:bool,
                    iDeponent:integer,
                    iDepoAcc:integer
                  )

   var sQuery =   " select s.*, "
                + IIF(ByRegstr, " NVL((select pt.t_ShortName from dparty_dbt pt where pt.t_PartyID = (case when lin.t_ReglineID IS NOT NULL then lin.t_HolderID else s.t_Deponent end)), CHR(1)) as DeponentShortName, ",
                                " NVL((select pt.t_ShortName from dparty_dbt pt where pt.t_PartyID = s.t_Deponent), CHR(1)) as DeponentShortName,"
                     )
                + IIF(ByRegstr, " NVL((select acnt.t_Code from ddepoacnt_dbt acnt where acnt.t_AutoKey = (case when lin.t_ReglineID IS NOT NULL then lin.t_HolderAccID else s.t_DepoAcntID end)), CHR(1)) as AcntCode ",
                                " NVL((select acnt.t_Code from ddepoacnt_dbt acnt where acnt.t_AutoKey = s.t_DepoAcntID), CHR(1)) as AcntCode"
                     )
                + "   from dspground_dbt s "
                + IIF(ByRegstr, "   left join ddpreglin_dbt lin on lin.t_RegisterID = s.t_RegisterID and lin.t_Type = " + DP_REGLINETYPE_DEPOACC, "")
                + "  where s.t_BackOffice = 'Z' "
                + "    and s.t_Direction = " + EXITING
                + "    and s.t_Department = " + iDepart
                + "    and s.t_Kind IN (select lc.t_Element from dllclsval_dbt lc where lc.t_Classificator = "+LLCLASS_EXTDEPODOC+")"
                + "    and (   (s.t_SPgroundID IN (select rep.t_RepSPgroundID from ddprepdoc_dbt rep)) "
                + "         OR (s.t_SPgroundID IN (select rep.t_RepSPgroundID from ddpreglinrep_dbt rep)) "
                + "         OR (s.t_SourceDocKind = " + SP_DEPOPER_INFOPER + " ) " //исходящий документ инф. операции
                + "        )"; 

   if( (Date_doc_beg <= {curdate}) and (Date_doc_beg != date(0,0,0)) ) 
     sQuery = sQuery + " and s.t_RegistrDate >= "+GetSQLDate(Date_doc_beg)+" ";
   end;

   if( (Date_doc_end <= {curdate}) and (Date_doc_end != date(0,0,0)))
     sQuery = sQuery + " and s.t_RegistrDate <= "+GetSQLDate(Date_doc_end)+" ";
   end;
   
   if( Number_doc_beg )
     sQuery = sQuery + " and s.t_Xld >= '"+Number_doc_beg+"' ";
   end;

   if( Number_doc_beg )
     sQuery = sQuery + " and s.t_Xld <= '"+Number_doc_end+"' ";
   end;

   if( ExtDocKind > 0 )
     sQuery = sQuery + " and s.t_Kind = " + ExtDocKind;
   end;

   if( PartyCode > -1)
      sQuery = sQuery + " and s.t_Party = "+PartyCode+" ";
   end;

   if( iSentKind > 0)
     sQuery = sQuery + " and s.t_DeliveryKind = "+iSentKind+" ";
   end;

   if( isSent == true )
     sQuery = sQuery + " and s.t_Sent = 'X' ";
   end;      

   if(UserLog)
     sQuery = sQuery + " AND (SELECT si.t_userlog " +
                       "        FROM dspground_dbt si " +
                       "       WHERE si.t_spgroundid = " +
                       "                               (SELECT rp.t_desc " +
                       "                                  FROM ddpgrrep_dbt rp, ddprepdoc_dbt repdoc " +
                       "                                 WHERE repdoc.t_repspgroundid = s.t_spgroundid and rp.t_ID = repdoc.t_GrRepID) " +
                       "          OR (     s.t_sourcedockind = " + SP_DEPOPER_INFOPER + // документ инф. операции
                       "               AND si.t_spgroundid = " +
                       "                                     (SELECT lg.t_draft " +
                       "                                        FROM dsplogopr_dbt lg " +
                       "                                       WHERE lg.t_autokey = s.t_sourcedocid) " +
                       "               AND t_direction = " + ENTERING +
                       "             ) " +
                       "     ) = " + UserLog;
   end;

   if( (iDepoAcc > 0) or
       (iDeponent != UnknownParty) or
       (IsOwnAcc and (not IsClientAcc)) or
       (IsClientAcc and (not IsOwnAcc))
   )
      var aDepoAcc = "";

      if(iDepoAcc > 0)
        aDepoAcc = GetSqlDepoAccByOwner(iDepart, iDeponent, null, iDepoAcc);
      elif( iDeponent != UnknownParty )
        aDepoAcc = GetSqlDepoAccByOwner(iDepart, iDeponent, null, null);
      elif( IsOwnAcc and (not IsClientAcc) )
        aDepoAcc = GetSqlDepoAccByOwner(iDepart, null, false, null);
      elif( IsClientAcc and (not IsOwnAcc) )
        aDepoAcc = GetSqlDepoAccByOwner(iDepart, null, true, null);
      end;

      sQuery = sQuery + " AND (";
      // для инвентарных
      sQuery = sQuery + "     exists("+
                        "            SELECT 1 " +
                        "            FROM dspgrdoc_dbt gd, dspdraft_dbt dr, dspdrmove_dbt spdrmove, dpmpaym_dbt pmpaym " +
                        "            WHERE s.t_SPgroundID = gd.t_SPgroundID " +
                        "              AND gd.t_sourcedockind = dr.t_kind " +
                        "              AND gd.t_sourcedocid = dr.t_autokey " +
                        "              AND spdrmove.t_DraftID = dr.t_autokey " +
                        "              AND spdrmove.t_PaymentID = pmpaym.t_PaymentID " +
                        "              AND dr.t_Department = " + iDepart +
                        "              AND (pmpaym.t_PayerDpNode IN ("+aDepoAcc+") or pmpaym.t_ReceiverDpNode IN ("+aDepoAcc+")) " +
                        "     )";

      // для информационных
      sQuery = sQuery + " or exists("+
                        "            SELECT 1 " +
                        "            FROM dspgrdoc_dbt gd, dsplogopr_dbt t, dspinfopr_dbt spinfopr " +
                        "            WHERE spinfopr.t_SPInfDocID = t.t_recId " +
                        "              AND gd.t_SPgroundID = s.t_SPgroundID " +
                        "              AND gd.t_sourcedockind = " + SP_DEPOPER_INFOPER +
                        "              AND gd.t_sourcedocid = t.t_autokey " +
                        "              AND t.t_Department = " + iDepart +
                        "              AND spinfopr.t_DepoAcc IN ("+aDepoAcc+") " +
                        "     )";

      // для административных
      sQuery = sQuery + " or exists("+
                        "            SELECT 1 " +
                        "            FROM dspgrdoc_dbt gd, ddepoadmo_dbt t " +
                        "            WHERE gd.t_SPgroundID = s.t_SPgroundID " +
                        "              AND gd.t_sourcedockind = " + SP_DEPOPER_ADMOPER +
                        "              AND gd.t_sourcedocid = t.t_AdmoprID " +
                        "              AND t.t_Department = " + iDepart +
                        "              AND t.t_DepoAcc IN ("+aDepoAcc+") " +
                        "     )";

      // для глобальных
      sQuery = sQuery + " or exists("+
                        "            SELECT 1 " +
                        "            FROM dspgrdoc_dbt gd, ddpcorpop_dbt t, ddpregstr_dbt dpregstr, ddpreglin_dbt reglin " +
                        "            WHERE s.t_SPgroundID = gd.t_SPgroundID " +
                        "              AND gd.t_sourcedockind = t.t_DocKind " +
                        "              AND gd.t_sourcedocid = t.t_DocumentID " +
                        "              AND dpregstr.t_DocumentID = t.t_DocumentID " +
                        "              AND t.t_Department = " + iDepart +
                        "              AND reglin.t_RegisterID = dpregstr.t_RegisterID " +
                        "              AND reglin.t_HolderAccID IN ("+aDepoAcc+") " +
                        "     )";


      sQuery = sQuery + " ) ";
      
   end;

   sQuery = sQuery + " order by s.t_SPgroundID";
   
   return sQuery;
end;

macro PrintHeader( ExtDocKind:integer,
                   Number_doc_beg:string,
                   Number_doc_end:string, 
                   Date_doc_beg:date,
                   Date_doc_end:date,
                   PartyCode:integer,
                   SentKind:string, 
                   isSent:bool,
                   UserLog:integer )
   
   var HeaderStr;
   var KindName;
   var Deliv_Name;
   var State;
   HeaderStr = "Ж У Р Н А Л  О Т П Р А В Л Е Н Н Ы Х  О Т Ч Ё Т О В  И  В Ы П И С О К";
   DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, HeaderStr, 0, 0, false, null, null, tablewidth );
   DP_AddPrintCell( Rep,  "Установки фильтра", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
   Rep.AddStr();

   if(UserLog)
     DP_AddPrintCell( Rep,  "по журналу:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  __NameAlg(ALG_DEPO_DRAFTREFKIND, UserLog), tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( Number_doc_beg  and (Number_doc_end) )
     DP_AddPrintCell( Rep,  "номера операций:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  Number_doc_beg+" - "+Number_doc_end, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( Date_doc_beg <= {curdate} )
     DP_AddPrintCell( Rep,  "период составления:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  Date_doc_beg, 12, 0, "l:f" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  " - " + Date_doc_end, 20, 0, "l:f" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( ExtDocKind > 0 )
     KindName = __LLValues(1050, ExtDocKind);
     DP_AddPrintCell( Rep,  "вид документа:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  KindName, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( PartyCode > -1 )
     ПолучитьСубъекта(PartyCode, party);
     DP_AddPrintCell( Rep,  "получатель:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  party.rec.Name, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( SentKind != "" )
     if( (Deliv_Name = FillReport( StrFor(int(SentKind)) )) != NULL )
       DP_AddPrintCell( Rep,  "способ отправки:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
       DP_AddPrintCell( Rep,  Deliv_Name, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
       Rep.AddStr();
     end;
   end;

   if( isSent == true )
     State = "Отправлен";
   else
     State = "Не отправлен";
   end;

     DP_AddPrintCell( Rep,  "состояние:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  State, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
     Rep.AddEmptyStr();
end;

macro PrintStr(count, ИсходящийНомер, ДатаИВремяСоставления,
               ОснованиеДляОтчета, Инициатор, ВидОтчета, 
               Депонент, СчетДепо, ПолучательОтчета,
               СпособОтправки, ДатаИВремяВыдачи, Идентификаторы, Исполнитель, АвтоФорм)

   DP_AddPrintCell( Rep,  count, 0, 0, "c:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  ИсходящийНомер, 0, 0, "c:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  ДатаИВремяСоставления, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  ОснованиеДляОтчета, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  Инициатор, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  ВидОтчета, 0, 0, "l:w" + RepFontStyleTitel );
   
   DP_AddPrintCell( Rep,  Депонент, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  СчетДепо, 0, 0, "l:w" + RepFontStyleTitel );

   DP_AddPrintCell( Rep,  ПолучательОтчета, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  СпособОтправки, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  ДатаИВремяВыдачи, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  Идентификаторы, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  Исполнитель, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  АвтоФорм, 0, 0, "c:" + RepFontStyleTitel );
   Rep.AddStr();
end;

macro GetOsnStr()
   
   var query, cmd, data;
   var data_pr;
   var ДокументыОснования_стр = "";
   var AltXld;
   var stat;
   
   //первичный документ
   query = "select spgr.t_SPgroundID, spgr.t_XLD, spgr.t_AltXld, spgr.t_SignedDate, spgr.t_Kind, spgr.t_SourceDocID, spgr.t_SourceDocKind "
           " from dspground_dbt spgr "
           " where (Exists ( select 1 "
           "                   from ddpgrrep_dbt rep, ddprepdoc_dbt repdoc "
           "                  where rep.t_Desc = spgr.t_SpgroundID "
           "                    and repdoc.t_GrRepID = rep.t_ID "
           "                    and repdoc.t_RepSPgroundID = ? /*1*/)) "
           "UNION "
           "select spgr.t_SPgroundID, spgr.t_XLD, spgr.t_AltXld, spgr.t_SignedDate, spgr.t_Kind, spgr.t_SourceDocID, spgr.t_SourceDocKind "
           "  from dspground_dbt spgr "
           " where (Exists (select 1 "
           "                  from ddpreglinrep_dbt rep, ddpreglin_dbt lin, ddpregstr_dbt reg, ddpcorpop_dbt crp "
           "                 where crp.t_SPgroundID = spgr.t_SPgroundID "
           "                   and reg.t_DocumentID = crp.t_DocumentID "
           "                   and lin.t_RegisterID = reg.t_RegisterID "
           "                   and rep.t_ReglineID  = lin.t_ReglineID "
           "                   and rep.t_RepSPgroundID = ? /*2*/ "
           "               )"
           "       )";
   
   cmd = DL_RSDCommand();
   cmd.AddParam(DataSet.SPgroundID);    /*1*/
   cmd.AddParam(DataSet.SPgroundID);    /*2*/

   if(DataSet.SourceDocKind == SP_DEPOPER_INFOPER)
     query = query + "UNION "
                   + "select spgr.t_SPgroundID, spgr.t_XLD, spgr.t_AltXld, spgr.t_SignedDate, spgr.t_Kind, spgr.t_SourceDocID, spgr.t_SourceDocKind "
                   + "  from dspground_dbt spgr "
                   + " where ( spgr.t_Direction = " + ENTERING + " and Exists ( select 1 from dsplogopr_dbt lg where lg.t_AutoKey = ? /*3*/ and lg.t_Draft = spgr.t_SPgroundID)  )";
     cmd.AddParam(DataSet.SourceDocID);   /*3*/
   end;

   data = cmd.Execute(query);
   if( not data.MoveNext() )
     return "";
   end;
   
   AltXld = data.AltXld ;
   if(CodeFor(AltXld) == EMPTY_STRING)
      AltXld = "б/н";
   end;

   ДокументыОснования_стр = "вх. №"+string(data.XLD) +" "+ __LLValues( 1050, data.Kind )+","
                             + " №"+AltXld+" от "+string(date(data.SignedDate):f)+"\n";
   
   data_pr = data;

   //документы приложения
   query =  "select t_Kind, t_Xld, t_AltXld, t_SignedDate from dspground_dbt"
            " where t_Direction <> "+EXITING+" and t_SpgroundID in "
            " ( select t_SpgroundID from dspgrdoc_dbt where t_SourceDocKind = ? /*1*/ "
            "    and t_SourceDocID = ? /*2*/ ) "; 
   
   cmd = DL_RSDCommand(query);
   cmd.AddParam(data_pr.SourceDocKind);    /*1*/
   cmd.AddParam(data_pr.SourceDocID);      /*2*/

   data = cmd.Execute();

   while(data.MoveNext())
     AltXld = data.AltXld ;
     if(CodeFor(AltXld) == EMPTY_STRING)
        AltXld = "б/н";
     end;
        ДокументыОснования_стр = ДокументыОснования_стр+"вх. №"+string(data.XLD) +" "+__LLValues( 1050, data.Kind )+","
                                 + " №"+AltXld+" от "+string(date(data.SignedDate):f)+"\n";
   end;

  return ДокументыОснования_стр;

end;

macro GetInitiator()
   var InitiatorName = "";
   var RepData, query = "", cmd;
   var DraftData, cmdDraft; 

   cmd = DL_RSDCommand();
   query =   " select * from ("
           + "       select rep.t_SourceDocID, rep.t_SourceDocKind "
           + "         from ddpgrrep_dbt rep, ddprepdoc_dbt repdoc "
           + "        where repdoc.t_RepSPgroundID = ? "
           + "          and rep.t_ID = repdoc.t_GrRepID ";

   cmd.AddParam(DataSet.SPgroundID);

   query = query + "UNION"
                 + " select crp.t_DocumentID, crp.t_DocKind "
                 + "   from ddpreglinrep_dbt rep, ddpreglin_dbt lin, ddpregstr_dbt reg, ddpcorpop_dbt crp "
                 + "  where rep.t_RepSPgroundID = ? "
                 + "    and lin.t_ReglineID = rep.t_ReglineID "
                 + "    and reg.t_RegisterID = lin.t_RegisterID "
                 + "    and crp.t_DocumentID = reg.t_DocumentID ";

   cmd.AddParam(DataSet.SPgroundID);


   if(DataSet.SourceDocKind == SP_DEPOPER_INFOPER)
     query = query + "UNION "
                   + "  select ? as t_SourceDocID, ? as t_SourceDocKind"
                   + "    from dual";

     cmd.AddParam(DataSet.SourceDocID);
     cmd.AddParam(SP_DEPOPER_INFOPER);
   end;

   query = query + ") ";

   RepData = cmd.Execute(query);

   while(RepData.moveNext())
     if( (RepData.SourceDocKind == SP_DEPOPER_BOOKORDER)     or   
       (RepData.SourceDocKind == SP_DEPOPER_TRANSFERORDER) or
       (RepData.SourceDocKind == SP_DEPOPER_ENROLMENT)     or   
       (RepData.SourceDocKind == SP_DEPOPER_RECORDER)      or    
       (RepData.SourceDocKind == SP_DEPOPER_WITHDRORDER) )
       cmdDraft = DL_RSDCommand( " select pt.t_Name "
                               + "   from dspdraft_dbt dr, dparty_dbt pt"
                               + "  where dr.t_AutoKey = ? "
                               + "    and pt.t_PartyID = dr.t_Initiator "
                              );
       
       cmdDraft.AddParam(RepData.SourceDocID);
       DraftData = cmdDraft.Execute();
       if(DraftData.moveNext())
         InitiatorName =  DraftData.Name;
       end;
     elif (   (RepData.SourceDocKind == DP_DEPOPER_CONVERT)       or     
        (RepData.SourceDocKind == DP_DEPOPER_REPAY)         or         
        (RepData.SourceDocKind == DP_DEPOPER_BONEMISS) )
       cmdDraft = DL_RSDCommand( " select pt.t_Name "
                               + "   from ddpcorpop_dbt cr, dparty_dbt pt"
                               + "  where cr.t_DocumentID = ? "
                               + "    and pt.t_PartyID = cr.t_Initiator"
                              );
       
       cmdDraft.AddParam(RepData.SourceDocID);
       DraftData = cmdDraft.Execute();
       if(DraftData.moveNext())
         InitiatorName =  DraftData.Name;
       end;
     elif( RepData.SourceDocKind == SP_DEPOPER_ADMOPER)
       cmdDraft = DL_RSDCommand( " select pt.t_Name"
                               + "   from ddepoadmo_dbt a, dparty_dbt pt"
                               + "  where a.t_AdmoprID = ? "
                               + "    and pt.t_PartyID = a.t_Initiator"
                              );
       
       cmdDraft.AddParam(RepData.SourceDocID);
       DraftData = cmdDraft.Execute();
       if(DraftData.moveNext())
         InitiatorName =  DraftData.Name;
       end;
     elif( RepData.SourceDocKind == SP_DEPOPER_INFOPER ) 
       cmdDraft = DL_RSDCommand( " select pt.t_Name "
                               + "   from dsplogopr_dbt log, dparty_dbt pt"
                               + "  where log.t_AutoKey = ? "
                               + "    and pt.t_PartyID = log.t_Initiator"
                              );
       
       cmdDraft.AddParam(RepData.SourceDocID);
       DraftData = cmdDraft.Execute();
       if(DraftData.moveNext())
         InitiatorName =  DraftData.Name;
       end;

     end; 
   end;

   return InitiatorName;
end;

macro GetKindOpr()
  var ВидОтчета= "";
  ВидОтчета = __LLValues(1050, DataSet.Kind);
  return ВидОтчета;
end;

macro GetParty()
  
   var PartyCode = DataSet.Party;
 
  party.Clear();
  ПолучитьСубъекта(PartyCode, party);

  return party.rec.Name;

end;

macro GetKindSend()
  var sDelivery;

  if( DataSet.DeliveryKind > 0 )
     sDelivery = FillReport( StrFor(DataSet.DeliveryKind) );
  else
     sDelivery = "";
  end;

  return sDelivery;
end;


macro GetSentDate()
  var ДатаИВремяВыдачи = "";
  var datesent:date  = DataSet.SentDate;
 
  file spground("spground");
  spground.SpgroundID = DataSet.SpgroundID;
  if(getEQ(spground))
    if( (datesent != date(0,0,0)) and (spground.Sent == "X"))
      ДатаИВремяВыдачи = string(datesent:f)+" "+string(time(DataSet.SentTime));
    else
      ДатаИВремяВыдачи = "";
    end;
  end;

  return ДатаИВремяВыдачи;
end;

macro GetIdentStr()
  var Ident_str = "";
  var data, query;
  var выборка_счет, запрос_счет_стр;
  var выборка_ФИ, запрос_ФИ_стр;
  var выборка_sp, запрос_sp_стр;
  var OprXid;
  var DateOpen:date, DateClose:date;
  var RepData, cmd;
  var DraftData, cmdDraft;

   
  //информация об операции
  cmd = DL_RSDCommand();
  query =   " select * from ("
          + "       select rep.t_SourceDocID, rep.t_SourceDocKind "
          + "         from ddpgrrep_dbt rep, ddprepdoc_dbt repdoc "
          + "        where repdoc.t_RepSPgroundID = ? "
          + "          and rep.t_ID = repdoc.t_GrRepID ";

  cmd.AddParam(DataSet.SPgroundID);

  query = query + "UNION"
                + " select crp.t_DocumentID, crp.t_DocKind "
                + "   from ddpreglinrep_dbt rep, ddpreglin_dbt lin, ddpregstr_dbt reg, ddpcorpop_dbt crp "
                + "  where rep.t_RepSPgroundID = ? "
                + "    and lin.t_ReglineID = rep.t_ReglineID "
                + "    and reg.t_RegisterID = lin.t_RegisterID "
                + "    and crp.t_DocumentID = reg.t_DocumentID ";

  cmd.AddParam(DataSet.SPgroundID);

  if(DataSet.SourceDocKind == SP_DEPOPER_INFOPER)
    query = query + "UNION "
                  + "  select ? as t_SourceDocID, ? as t_SourceDocKind"
                  + "    from dual";

    cmd.AddParam(DataSet.SourceDocID);
    cmd.AddParam(SP_DEPOPER_INFOPER);
  end;

  query = query + ") ";

  RepData = cmd.Execute(query);

  while(RepData.moveNext())
    if( (RepData.SourceDocKind == SP_DEPOPER_BOOKORDER)     or   
      (RepData.SourceDocKind == SP_DEPOPER_TRANSFERORDER) or
      (RepData.SourceDocKind == SP_DEPOPER_ENROLMENT)     or   
      (RepData.SourceDocKind == SP_DEPOPER_RECORDER)      or    
      (RepData.SourceDocKind == SP_DEPOPER_WITHDRORDER) )
      Ident_str = Ident_str + "Инв. операция:\n";
      cmdDraft = DL_RSDCommand( " select dr.t_OprXid, pm.t_ValueDate, op.t_Name "
                              + "   from dspdraft_dbt dr, dspdrmove_dbt m, dpmpaym_dbt pm, doprkoper_dbt op"
                              + "  where dr.t_AutoKey = ? "
                              + "    and m.t_DraftID = dr.t_AutoKey "
                              + "    and pm.t_PaymentID = m.t_PaymentID"
                              + "    and op.t_Kind_Operation = dr.t_Kind_Operation"
                             );
      cmdDraft.AddParam(RepData.SourceDocID);
      DraftData = cmdDraft.Execute();
      if(DraftData.moveNext())
        Ident_str = Ident_str + "№"+DraftData.OprXid + " от " + date(DraftData.ValueDate) + ", \n"+DraftData.Name;
      end;
    elif (   (RepData.SourceDocKind == DP_DEPOPER_CONVERT)       or     
       (RepData.SourceDocKind == DP_DEPOPER_REPAY)         or         
       (RepData.SourceDocKind == DP_DEPOPER_BONEMISS) )
      Ident_str = Ident_str + "Глоб. операция:\n";
      cmdDraft = DL_RSDCommand( " select cr.t_OprXid, cr.t_OprDate, op.t_Name "
                              + "   from ddpcorpop_dbt cr, doprkoper_dbt op"
                              + "  where cr.t_DocumentID = ? " 
                              + "    and op.t_Kind_Operation = cr.t_Kind_Operation"
                             );
      cmdDraft.AddParam(RepData.SourceDocID);
      DraftData = cmdDraft.Execute();
      if(DraftData.moveNext())
        Ident_str = Ident_str + "№"+DraftData.OprXid + " от " + date(DraftData.OprDate) + ", \n"+DraftData.Name;
      end;
    elif( RepData.SourceDocKind == SP_DEPOPER_ADMOPER)
      Ident_str = Ident_str + "Адм. операция:\n";
      cmdDraft = DL_RSDCommand( " select a.t_OprXid, a.t_OperDate, apr.t_Name "
                              + "   from ddepoadmo_dbt a, ddpadmopr_dbt apr"
                              + "  where a.t_AdmoprID = ? " 
                              + "    and apr.t_Item = a.t_Item"
                              + "    and apr.t_OperKind = a.t_OperKind"
                             );
      cmdDraft.AddParam(RepData.SourceDocID);
      DraftData = cmdDraft.Execute();
      if(DraftData.moveNext())
        Ident_str = Ident_str + "№"+DraftData.OprXid + " от " + date(DraftData.OperDate) + ", \n"+DraftData.Name;
      end;
    elif( RepData.SourceDocKind == SP_DEPOPER_INFOPER ) 
      Ident_str = Ident_str + "Инф. операция:\n";
      cmdDraft = DL_RSDCommand( " select log.t_OprXid, log.t_carryDate, so.t_OperationName "
                              + "   from dsplogopr_dbt log, dspoper_dbt so, dspinfopr_dbt inf"
                              + "  where log.t_AutoKey = ? "
                              + "    and inf.t_SPinfDocID = log.t_recId"
                              + "    and so.t_OperationCode = inf.t_docType"
                             );
      cmdDraft.AddParam(RepData.SourceDocID);
      DraftData = cmdDraft.Execute();
      if(DraftData.moveNext())
        Ident_str = Ident_str + "№"+DraftData.OprXid + " от " + date(DraftData.carryDate) + ", \n"+DraftData.OperationName;
      end;

    end; 
  end;

  return Ident_str;
end;

macro GetExecutor()
  var Executor_str = "";

  if(DataSet.IsMakeAuto == SET_CHAR)
    Executor_str = __LLValues(OBJTYPE_TECHAUTODOC, DataSet.TechAutoDoc);
  else
    Executor_str = __PersonName(DataSet.Receptionist) + " (" + string(DataSet.Receptionist) + ")";
  end;

  return Executor_str;
end;

macro Report()

   var count = 1;
   var ИсходящийНомер;
   var ДатаИВремяСоставления;
   var ОснованиеДляОтчета;
   var Инициатор;
   var ВидОтчета;
   var ПолучательОтчета;
   var СпособОтправки;
   var ДатаИВремяВыдачи;
   var Идентификаторы;
   var Исполнитель;
   var Депонент;
   var СчетДепо;


   while( DataSet.MoveNext() )
     UseProgress(count);
     //исходящий номер
     ИсходящийНомер = DataSet.XLD;
     //Дата и время составления
     ДатаИВремяСоставления = string(date(DataSet.RegistrDate):f)+"\n"+string(time(DataSet.RegistrTime));
     //Основание для отчёта
     ОснованиеДляОтчета = GetOsnStr();
     //Инициатор операции
     Инициатор = GetInitiator();
     //ВидОтчета
     ВидОтчета = GetKindOpr();
     //Получатель отчета
     ПолучательОтчета = GetParty();
     //СпособОтправки
     СпособОтправки = GetKindSend();
     //Дата и время выдачи
     ДатаИВремяВыдачи = GetSentDate();
     //Идентификаторы
     Идентификаторы = GetIdentStr();
     //Исполнитель
     Исполнитель = GetExecutor();

     Депонент = DataSet.DeponentShortName;
     СчетДепо = DataSet.AcntCode;

     PrintStr(count, ИсходящийНомер, ДатаИВремяСоставления,
              ОснованиеДляОтчета, Инициатор, ВидОтчета, Депонент, СчетДепо,
              ПолучательОтчета, СпособОтправки, ДатаИВремяВыдачи, Идентификаторы, Исполнитель, DataSet.IsMakeAuto);
     count = count + 1;
   end;
   RemProgress();

end;


macro GenRep( iDepart:integer,
              ExtDocKind:integer,
              Number_doc_beg:string,
              Number_doc_end:string,
              Date_doc_beg:date,
              Date_doc_end:date,
              PartyCode:integer,
              SentKind:string,
              isSent:bool,
              ToExcel:bool,
              is_ap:bool,
              UserLog:integer,
              ByRegstr:bool,
              IsOwnAcc:bool,
              IsClientAcc:bool,
              iDeponent:integer,
              iDepoAcc:integer )

   var sQuery;
   var Count;
   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

   _is_ap = is_ap;

   sQuery = DPGetSqlString(iDepart, ExtDocKind, Number_doc_beg, Number_doc_end,
                         Date_doc_beg, Date_doc_end, PartyCode, int(SentKind),
                         isSent, UserLog, ByRegstr, IsOwnAcc, IsClientAcc, iDeponent, iDepoAcc);

   var cmd = DL_RSDCommand(sQuery);

   count = cmd.GetCount();

   if( count )
     DataSet = cmd.Execute();

     PrintHeader( ExtDocKind, Number_doc_beg, Number_doc_end, 
                  Date_doc_beg, Date_doc_end, PartyCode,
                  SentKind, isSent, UserLog );
     InitProgress( Count, "Идёт формирование отчёта...", "Отчет \"ЖУРНАЛ ИСХОДЯЩИХ ДОКУМЕНТОВ\"" );
     Report();

     DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

     DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
     DP_EndProtocol();                 //закончить протоколирование

     if( ToExcel )
       Rep.SetZoomType( ZOOM_TYPE_A4L );// альбомная ориентация
       Rep.PrintWinRep();
       Rep.ShowWinRep();
     else
       Rep.PrintRep();
     end;
   else
     DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
     DP_EndProtocol();                 //закончить протоколирование

     MsgBox("Не найдено данных, удовлетворяющих параметрам отчёта.");
   end;

   return 0;
end;

macro PrintReportFromScrol( sQuery:string, ToExcel:bool )
   var Count;
   var HeaderStr;
   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );  
   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

   sQuery =   " select q.*, "
            + "        NVL((select pt.t_ShortName from dparty_dbt pt where pt.t_PartyID = s.t_Deponent), CHR(1)) as DeponentShortName,"
            + "        NVL((select acnt.t_Code from ddepoacnt_dbt acnt where acnt.t_AutoKey = s.t_DepoAcntID), CHR(1)) as AcntCode "
            + "   from ("+sQuery+") q, dspground_dbt s "
            + "  where s.t_SPgroundID = q.t_SPgroundID ";

   var cmd = DL_RSDCommand(sQuery);

   count = cmd.GetCount();

   if( count )
     DataSet = cmd.Execute();

     HeaderStr = "ЖУРНАЛ ИСХОДЯЩИХ ДОКУМЕНТОВ";
     DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, HeaderStr, 0, 0, false, null, null, tablewidth );

     InitProgress( Count, "Идёт формирование отчёта...", "Отчет \"ЖУРНАЛ ИСХОДЯЩИХ ДОКУМЕНТОВ\"" );
     Report();

     DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

     DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
     DP_EndProtocol();                 //закончить протоколирование


     if( ToExcel )
       Rep.SetZoomType( ZOOM_TYPE_A4L );// альбомная ориентация
       Rep.PrintWinRep();
       Rep.ShowWinRep();
     else
       Rep.PrintRep();
     end;

   else
     DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
     DP_EndProtocol();                 //закончить протоколирование

     MsgBox("Не найдено данных, удовлетворяющих параметрам отчёта.");
   end;

   return 0;

end;
