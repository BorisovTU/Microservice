/*
$Name:        dpexpsecur.mac
$Module:      Депозитарий
$Description: кспорт поручений в БОЦБ
*/
import secinter, spserv;

private const DL_PREPARING   = 0,    /* сделка на этапе подготовки */
              WRITEOFFAVOIR  = 2010, /* Списание ценных бумаг */
              ENROLMENTAVOIR = 2011, /* Зачисление ценных бумаг */
              SYS_ANL_AINPACCOUNTING = 2; /* Аналитика "Учет актива в пассиве" */

macro GetCorsch( Account )
  file corschem("corschem") key 3;

  corschem.Account = Account;
  corschem.FIID    = -1;
  corschem.Department = {OperDprt};
  return GetEQ(corschem);
end;

macro GetCorschByNum( Num, corsch:@variant )
  var stat;
  var corschem = TBFile("corschem", "R", 1);

  ClearRecord( corschem );
  corschem.rec.Number  = Num;
  corschem.rec.FI_Kind = FIKIND_AVOIRISS;
  corschem.rec.FIID    = -1;

  stat = corschem.GetEQ();
  if( stat )
    Copy( corsch, corschem );
  end;

  return stat;
end;

/* поиск лицевого счета */
macro FindCustody( Account, FIID, pmpaym, Custody:@variant )
  file account_t("account");
  file dpacc_t  ("depoacnt");
  file depoac_t ("depoac.dbt");

  account_t.Chapter       = 5;
  account_t.Account       = Account;
  account_t.Code_Currency = FIID; 
  if ( ( getEQ(account_t) ) )
    if( account_t.Open_Date <= pmpaym.ValueDate )
      if( FindDEPOACNT( account_t.DepoRoot, dpacc_t ) == 0 )
        if( GetCorsch( dpacc_t.BriefCode ) )
          Custody = dpacc_t.Owner;
          return true;
        else
          return false;
        end;
      end;
    else
      return true; /* этот счет нужно пропусить, т.к. дата не подходит */
    end;
  end;

  return false;
end;

//получить депозитарий по поручению
macro GetCustodyByDepo(spdraft, pmpaym)
  var Custody = -1;
  file depoacnt("depoacnt") key 2;

  //ищем по поручению, если возможно
  if( pmpaym.PayerAccount != "" )
    depoacnt.BriefCode  = pmpaym.PayerAccount;
    depoacnt.Department = spdraft.Department;
    if( (GetEQ(depoacnt)) and (depoacnt.Kind == SIDEBALANCE_ACTIVE) )
      Custody = depoacnt.Owner;
    end;
  end;

  if( (Custody == -1) and (pmpaym.ReceiverAccount != "") )
    ClearRecord(depoacnt);
    depoacnt.BriefCode  = pmpaym.ReceiverAccount;
    depoacnt.Department = spdraft.Department;
    if( (GetEQ(depoacnt)) and (depoacnt.Kind == SIDEBALANCE_ACTIVE) )
      Custody = depoacnt.Owner;
    end;
  end;

  return Custody;

end;


/* определение депозитария */
macro GetCusdody( spdraft, dpacc, pmpaym )
  var AinPEnabled = false,
      Custody_o = -1,
      Custody = -1,
      stat, sub_stat,
      WasFound = false;
  file account("account") key 3;
  file accvanl("accvanl") key 1;
  file accsub("accsub");

  if( GetAinPAccState( AinPEnabled ) or (not AinPEnabled) )
    Custody = GetCustodyByDepo(spdraft, pmpaym);
    return Custody;
  end;
  ClearRecord( account );
  ClearRecord( accvanl );
  ClearRecord( accsub );

  account.DepoAcc = dpacc.AutoKey;

  stat = getEQ(account);

  while( stat )
    if ( ( not index(account.Type_Account, "I") ) and ( account.HaveSubAccounts == "X" ) and 
         ( account.Kind_Account == "П" ) and ( account.Open_Date <= pmpaym.ValueDate ) ) 
      /* перебор субсчетов */
      accvanl.FIID        = account.Code_Currency;
      accvanl.Chapter     = 5;
      accvanl.Account     = account.Account;
      accvanl.AnaliticsID = SYS_ANL_AINPACCOUNTING;
      if( GetEQ(accvanl) )
        accsub.AccAnaliticsID = accvanl.AccAnaliticsID;
        accsub.SubAccountCode = "";
        sub_stat = getGE(accsub);
        while( sub_stat )  
          if( accsub.SpecialType == 0 )
            Custody_o = Custody;
            if( FindCustody( accsub.SubAccountCode, accvanl.FIID, pmpaym, @Custody ) )
              if( (Custody_o != Custody) and (Custody_o != -1) )
                Custody = -1;
                break;
              end;
            end;
          end;

          sub_stat = next(accsub) and ( accsub.AccAnaliticsID == accvanl.AccAnaliticsID );
        end;
      end;
    end;

    stat = next(account) and ( account.DepoAcc == dpacc.AutoKey );
  end;
  
  if( Custody == -1 )
    Custody = GetCustodyByDepo(spdraft, pmpaym);
  end;

  return Custody;
end;

/* проверить вид обслуживания субъекта */
macro CheckOwnerService( PartyID, sfcontr, pmpaym )

  if( ОбслуживаетсяКлиент(PartyID, PTSK_STOCKDL) )
     sfcontr.partyID    = partyID;
     sfcontr.ServKind   = PTSK_STOCKDL;
     sfcontr.objectType = 0; /*???*/
     sfcontr.FIID       = -1; /*???*/
     var IsContinue = GetGE(sfcontr); 
     while( IsContinue )
        if( (sfcontr.partyID == partyID) and 
            (sfcontr.ServKind == PTSK_STOCKDL) and 
            (sfcontr.ContractorID == {OurBank}) and 
            (sfcontr.DateBegin <= pmpaym.ValueDate) and
            ((sfcontr.DateClose >= pmpaym.ValueDate) or (sfcontr.DateClose == Date(0,0,0)))
          )
           return true;
        end;

        IsContinue = Next(sfcontr); 
     end;
  end;

  return false;
end;


/* определить счета Дт и Кт */
macro GetDPACC( spdraft, dpaccDb, sfcontrDb, dpaccKr, sfcontrKr, pmpaym )
  var stat = false;
  var stat2 = false;

  if  ( spdraft.Kind == SP_DEPOPER_TRANSFERORDER )
    stat = not FindDEPOACNT( pmpaym.PayerDpNode, dpaccDb );
    if( (stat) and ( (not CheckOwnerService( dpaccDb.Owner, sfcontrDb, pmpaym ) ) and (dpaccDb.Owner != {OurBank} )) )
      stat = false;
      ClearRecord( dpaccDb );
    end;

  elif( spdraft.Kind == SP_DEPOPER_ENROLMENT )
    stat = not FindDEPOACNT( pmpaym.ReceiverDpNode, dpaccKr );
    if( (stat) and ((not CheckOwnerService( dpaccKr.Owner, sfcontrKr, pmpaym )) and (dpaccKr.Owner != {OurBank} ) ) )
      stat = false;
      ClearRecord( dpaccKr );
    end;

  elif( spdraft.Kind == SP_DEPOPER_BOOKORDER )
    stat = not FindDEPOACNT( pmpaym.PayerDpNode, dpaccDb );
    if( stat and ( dpaccDb.Kind == SIDEBALANCE_PASSIVE ) )
      if( (not CheckOwnerService( dpaccDb.Owner, sfcontrDb, pmpaym )) and (dpaccDb.Owner != {OurBank} ))
        stat = false;
        ClearRecord( dpaccDb );
      end;
    else
      stat = false;
      ClearRecord( dpaccDb );
    end;
    stat2 = not FindDEPOACNT( pmpaym.ReceiverDpNode, dpaccKr );
    if( stat2 and ( dpaccKr.Kind == SIDEBALANCE_PASSIVE ) )
      if( (not CheckOwnerService( dpaccKr.Owner, sfcontrKr, pmpaym )) and (dpaccKr.Owner != {OurBank} ) )
        stat2 = false;
        ClearRecord( dpaccKr );
      end;
    else
      stat2 = false;
      ClearRecord( dpaccKr );
    end;
    if( stat2 )
      stat = true;
    end;
  end;

  return stat;

end;

/* получить НКД сделки */
macro GetCost(FIID, PriceFIID, Amount, ValueDate)
  var sum = $0;
  var fin = TBFile("fininstr.dbt");

  ClearRecord(fin);
  fin.rec.FIID = FIID;
  if( fin.getEQ() )
    if( SmartConvertSum( sum, НКД(fin.rec.GeneralizedFIID, Amount, ValueDate), ValueDate, PriceFIID, fin.rec.FaceValueFI ) != true )
      sum = $0;
    end;    
  end;
 
  return sum;
end;

/* заполнение структуры и параметров и экспорт */
macro InsertSecOrd( spdraft, dpacc, sfcontr, IsDb, pmpaym )
  var stat;
  record BoardPrm("BoardPrm.rec");
  record RateRec(ratedef);
  var depoacnt = TRecHandler("depoacnt.dbt");
  var depoac   = TRecHandler("depoac.dbt");
  var corschem = TRecHandler("corschem");
  var fin = TBFile("fininstr.dbt"),
      avr = TBFile("avoiriss.dbt");

  ClearRecord( BoardPrm );

  if( IsDb )
    BoardPrm.DealType = WRITEOFFAVOIR;
  else
    BoardPrm.DealType = ENROLMENTAVOIR;
  end;

  ClearRecord(fin.rec);
  fin.rec.FIID = pmpaym.FIID;
  if( not fin.getEQ() )
    ClearRecord(fin.rec);
    fin.rec.FIID = -1;
  end;

  if( spdraft.GeneralizedFIID != -1 )
    /*если задан обобщенный выпуск, берем его*/
    BoardPrm.FIID = spdraft.GeneralizedFIID;
  else
    /*иначе берем обобщенный с поставляемого, если есть или сам поставляемый в крайнем случае*/
    if( (fin.rec.FIID > 0) and (fin.rec.IsGeneralized != "X") and (fin.rec.GeneralizedFIID != -1) )
      BoardPrm.FIID = fin.rec.GeneralizedFIID;
    else
      BoardPrm.FIID = pmpaym.FIID;
    end;
  end;

  BoardPrm.DealDate          = pmpaym.ValueDate;
  BoardPrm.Client            = dpacc.Owner;
  BoardPrm.ClientContr       = sfcontr.ID;
  BoardPrm.DeliveringFIID    = pmpaym.FIID;
  BoardPrm.Amount            = pmpaym.Amount;

  if(FI_IsKSU(fin))
    BoardPrm.Price           = fin.rec.FaceValue;
    BoardPrm.Cost            = fin.rec.FaceValue * pmpaym.Amount; 
    BoardPrm.PriceFI         = fin.rec.FaceValueFI;
  else
    BoardPrm.Price           = spdraft.DealAmount / pmpaym.Amount;
    BoardPrm.Cost            = spdraft.DealAmount - GetCost(pmpaym.FIID, spdraft.DealCurrency, pmpaym.Amount, pmpaym.ValueDate);
    BoardPrm.PriceFI         = spdraft.DealCurrency;
  end;

  BoardPrm.NKD               = НКД(spdraft.GeneralizedFIID, pmpaym.Amount, pmpaym.ValueDate);
  BoardPrm.Custody           = -1; /* потом в зависимсти от вида поручения будет уточнение */
  BoardPrm.Department        = pmpaym.Department;
  BoardPrm.Oper              = spdraft.Oper;
  BoardPrm.DealState         = DL_PREPARING;
  BoardPrm.DepoClientAccount = dpacc.BriefCode;
  BoardPrm.DepoClientCustody = {OurBank};

  /*для сделок нашего банка*/
  if(BoardPrm.Client == {OurBank})
    if(FI_IsKSU(fin))
      BoardPrm.Portfolio = KINDPORT_BACK_KSU;
    else
      ClearRecord(avr.rec);
      avr.rec.FIID = fin.rec.FIID;
      if( avr.getEQ() )
        if( ExistNOSS(avr, BoardPrm.DealDate) )
          BoardPrm.Portfolio = KINDPORT_TRADE;
        else
          BoardPrm.Portfolio = KINDPORT_SALE;
        end;
      else
        BoardPrm.Portfolio = KINDPORT_UNDEF;
      end;
    end;
  else
    BoardPrm.Portfolio = KINDPORT_CLIENT;
  end;

  if( spdraft.Kind == SP_DEPOPER_TRANSFERORDER )
    BoardPrm.Contragent           = pmpaym.Receiver;
    if( FindDEPOACNT( pmpaym.ReceiverDpNode, depoacnt ) == 0 )
      BoardPrm.DepoContrAcc = depoacnt.rec.BriefCode;
    end;
    BoardPrm.DepoContrCustody     = pmpaym.ReceiverBankID;
    BoardPrm.DepoContrCorrAcc     = Pm_prop.CorrAcc;
    BoardPrm.DepoContrCorrCustody = Pm_prop.CorrID;
  elif( spdraft.Kind == SP_DEPOPER_ENROLMENT )
    BoardPrm.Contragent           = pmpaym.Payer;
    if( FindDEPOACNT( pmpaym.PayerDpNode, depoacnt ) == 0 )
      BoardPrm.DepoContrAcc = depoacnt.rec.BriefCode;
    end;
    BoardPrm.DepoContrCustody     = pmpaym.PayerBankID;
    BoardPrm.DepoContrCorrAcc     = Pm_prop_deb.CorrAcc;
    BoardPrm.DepoContrCorrCustody = Pm_prop_deb.CorrID;
  else
    BoardPrm.Contragent           = -1;
    BoardPrm.DepoContrAcc         = "";
    BoardPrm.DepoContrCustody     = -1;
    BoardPrm.DepoContrCorrAcc     = "";
    BoardPrm.DepoContrCorrCustody = -1;
  end;

  if( spdraft.Kind == SP_DEPOPER_TRANSFERORDER )
    if( GetCorschByNum( pm_prop.Corschem, @corschem ) )
      if( corschem.rec.IsNostro == "X" )
        BoardPrm.Custody = corschem.rec.CorrID;
      else
        BoardPrm.Custody = GetCusdody( spdraft, dpacc, pmpaym );
      end;
    end;
  elif( spdraft.Kind == SP_DEPOPER_ENROLMENT )
    if( GetCorschByNum( pm_prop_deb.Corschem, @corschem ) and
        (corschem.rec.IsNostro == "X") )
      BoardPrm.Custody = corschem.rec.CorrID;
    end;
  elif( spdraft.Kind == SP_DEPOPER_BOOKORDER )
    var KindOperation = DefineKindOperation( pmpaym.PayerDpNode, pmpaym.ReceiverDpNode );
    if(KindOperation == KINDOPER_ENROLMENT)
      if(FindDEPOACNT( pmpaym.PayerDpNode, depoacnt ) == 0)
        BoardPrm.Custody = depoacnt.rec.Owner;
      end;
    elif(KindOperation == KINDOPER_WRITINGOFF)
      if(FindDEPOACNT( pmpaym.ReceiverDpNode, depoacnt ) == 0)
        BoardPrm.Custody = depoacnt.rec.Owner;
      end; 
    else
      if( dpacc.Kind == SIDEBALANCE_ACTIVE )
        BoardPrm.Custody = dpacc.Owner;
      else
        BoardPrm.Custody = GetCusdody( spdraft, dpacc, pmpaym );
      end;
    end;
  end;

  stat = SP_InsertBoardLot( BoardPrm );

  return stat;
end;


macro ExpToSecur( spdraft, pmpaym )

  var stat;
  file dpaccDb("depoacnt.dbt");
  file sfcontrDb("sfcontr.dbt") key 2;
  file dpaccKr("depoacnt.dbt");
  file sfcontrKr("sfcontr.dbt") key 2;

  if( spdraft.TOBO != "X" )
    return 0;
  end;

  if( spdraft.Oper == 0 )
    spdraft.Oper = {oper}; 
  end;

  if( (spdraft.ToBO == "X") and (not CheckExpSecurAble(spdraft, pmpaym)) )
    DPError.SetErr( "Экспорт в БОЦБ невозможен. Поручение не удовлетворяет необходимым условиям" );
    return 1;
  end;

  ClearRecord(dpaccDb);
  ClearRecord(dpaccKr);

  stat = GetDPACC( spdraft, dpaccDb, sfcontrDb, dpaccKr, sfcontrKr, pmpaym );

  if( stat )
    if( dpaccDb.AutoKey )
      stat = InsertSecOrd( spdraft, dpaccDb, sfcontrDb, true, pmpaym );
    end;
    if( stat and dpaccKr.AutoKey )
      stat = InsertSecOrd( spdraft, dpaccKr, sfcontrKr, false, pmpaym );
    end;
  end;

  if( not stat )
    DPError.SetErr( "Ошибка при экспорте в БОЦБ" );
    return 1;
  end;

  return 0;

end;
