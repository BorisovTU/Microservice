/*
$Name:             dpdbmake.mac
$Module:           Депозитарий 
$Description:      Обработка выписки расчетного депозитария
*/

import secinter, dpstdrep, DPInter, spdobook, "dpsteprep.mac";

/////////////////////////////////////////////////////////////////////
//заполняются из сишника при старте
// Параметры фильтра
var Number:string;    
var FormDate:date;  
var CorrID:integer;    
record Corschem(corschem);  
var NumberPack:integer;
private var ToExcel:bool;   
/////////////////////////////////////////////////////////////////////

private var StrNum = 0; //порядковый номер поручения
private var CntSuc = 0; //успешные поручения

private const SC_SPDRAFT_STATUS_CLOSE = 3;

var HeadSett = "┌─────┬───────┬─────────────┬────────────────┬──────────────────┬────────────────────────────────────┬────────────────┬──────────────────────────┐\n"+
               "│  №  │  Вид  │    Номер    │  Счет (раздел  │    Код ценной    │ Краткое наименование ценной бумаги │   Количество   │   Причина невыполнения   │\n"+
               "│ п/п │       │             │  счета) депо   │      бумаги      │                                    │                │                          │\n"+ 
               "├─────┼───────┼─────────────┼────────────────┼──────────────────┼────────────────────────────────────┼────────────────┼──────────────────────────┤";
                                                                                                                                                                        

var HeadSecur = "┌─────┬───────┬──────────────────┬────────────────────┬────────────────┬──────────────────┬────────────────┬──────────────────────────┐\n"+
                "│  №  │  Вид  │ Номер служебного │      Депонент      │  Счет (раздел  │    Код ценной    │   Количество   │   Причина невыполнения   │\n"+
                "│ п/п │       │ поручения БОЦБ   │                    │  счета) депо   │      бумаги      │                │                          │\n"+ 
                "├─────┼───────┼──────────────────┼────────────────────┼────────────────┼──────────────────┼────────────────┼──────────────────────────┤";

var DpMakeRep = null;
private const RepFontStyle =  "ex_FS(b)";

macro MakeDraftFltr(MakeDraftKind)
  var stat = 0;
 
  if(MakeDraftKind == DP_MAKEDRAFTKIND_SETT) //Обработка выписки расчетного депозитария
    //"Через транзитный счет" должен быть сброшен
    SQL_Execute(   " DELETE FROM doprtemp_tmp ot "
                 + "  WHERE Exists(SELECT 1 "
                 + "                 FROM dspdraft_dbt dr"
                 + "                WHERE dr.t_AutoKey = ot.t_OrderID "
                 + "                  AND dr.t_FromTransitAccount = 'X'"
                 + "              )"
               );
  elif(MakeDraftKind == DP_MAKEDRAFTKIND_SECUR) //Обработка поручений БОЦБ
    //"Через транзитный счет" должен быть установлен
    SQL_Execute(   " DELETE FROM doprtemp_tmp ot "
                 + "  WHERE Exists(SELECT 1 "
                 + "                 FROM dspdraft_dbt dr"
                 + "                WHERE dr.t_AutoKey = ot.t_OrderID "
                 + "                  AND dr.t_FromTransitAccount = CHR(0)"
                 + "              )"
               );
  end;

  return stat;
end;

private macro PrintHead(Head, TableWidth)
  
  file pt(party);
  file obj(objcode);
  var CorrName = "", CorrCode = "";
  var LeftColWidth = 30;

  if(CorrID != -1)
    pt.PartyID = CorrID;
    if(GetEQ(pt))
      CorrName = pt.ShortName;
    end;

    obj.ObjectType = OBJTYPE_PARTY;
    obj.CodeKind   = PTCK_CONTR;
    obj.ObjectID   = CorrID;
    if( GetEQ(obj) )
      CorrCode = obj.Code;
    end;
  end;

  DP_AddPrintCell( DpMakeRep,  Head, TableWidth, 0, "c:" + RepFontStyle, false, REP_ELEM_STR );
  DpMakeRep.AddStr();
  DpMakeRep.AddEmptyStr();
  DpMakeRep.AddStr();

  DP_AddPrintCell( DpMakeRep,  "Порядковый номер:", LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DP_AddPrintCell( DpMakeRep,  Number, TableWidth-LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DpMakeRep.AddStr();

  DP_AddPrintCell( DpMakeRep,  "Дата выписки:", LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DP_AddPrintCell( DpMakeRep,  date(FormDate), TableWidth-LeftColWidth, 0, "l:f:" + RepFontStyle, false, REP_ELEM_STR );
  DpMakeRep.AddStr();

  DP_AddPrintCell( DpMakeRep,  "Корреспондент:", LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DP_AddPrintCell( DpMakeRep,  CorrCode + " " + CorrName, TableWidth-LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DpMakeRep.AddStr();

  DP_AddPrintCell( DpMakeRep,  "Счет Ностро:", LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DP_AddPrintCell( DpMakeRep,  Corschem.Account, TableWidth-LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DpMakeRep.AddStr();

  DP_AddPrintCell( DpMakeRep,  "Пачка:", LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DP_AddPrintCell( DpMakeRep,  NumberPack, TableWidth-LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DpMakeRep.AddStr();

  DpMakeRep.AddEmptyStr();
  DpMakeRep.AddStr();

  DP_AddPrintCell( DpMakeRep,  "Список поручений", TableWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DpMakeRep.AddStr();
end;

private macro PrintFooter(TableWidth)
  var LeftColWidth = 30;

  DpMakeRep.AddEmptyStr();
  DpMakeRep.AddStr();
  
  DP_AddPrintCell( DpMakeRep,  "Всего поручений:", LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DP_AddPrintCell( DpMakeRep,  StrNum, TableWidth-LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DpMakeRep.AddStr();

  DP_AddPrintCell( DpMakeRep,  "Исполнено успешно:", LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DP_AddPrintCell( DpMakeRep,  CntSuc, TableWidth-LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DpMakeRep.AddStr();

  DP_AddPrintCell( DpMakeRep,  "Не исполнено:", LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DP_AddPrintCell( DpMakeRep,  StrNum - CntSuc, TableWidth-LeftColWidth, 0, "l:" + RepFontStyle, false, REP_ELEM_STR );
  DpMakeRep.AddStr();

end;

macro PrepareRepSett()
  var TableWidth = Index(HeadSett,"\n");
  var cmd, query, DataSet;
  var RepFileName = "";
  
  DpMakeRep = CMakeReport(HeadSett);
  PrintHead("Протокол о выполнении процедуры обработки выписки расчетного депозитария", TableWidth);

  query =   "with q as (select ds1.t_OrderID, Max(ds1.t_ID_Step) as t_ID_Step "
          + "             from doprdistaff_tmp ds1 "
          + "            group by ds1.t_OrderID "
          + "          )"
          + " select dr.t_Status, "
          + "        spgr.t_AltXld, "
          + "        pm.t_Amount, "
          + "        kopr.t_ShortName as OprShortName, "
          + "        NVL((select acnt.t_BriefCode from ddepoacnt_dbt acnt where acnt.t_AutoKey = (case when dr.t_Kind = "+SP_DEPOPER_TRANSFERORDER+" then pm.t_PayerDpNode else pm.t_ReceiverDpNode end)), CHR(1)) as BriefCode, "
          + "        fin.t_FI_Code as FICode, fin.t_Name as FIName, "
          + "        ds.t_ErrorMessage "
          + "   from q, doprdistaff_tmp ds, dspdraft_dbt dr, dspground_dbt spgr, dspdrmove_dbt mv, dpmpaym_dbt pm, doprkoper_dbt kopr, dfininstr_dbt fin "
          + "  where ds.t_OrderID = q.t_OrderID "
          + "    and ds.t_ID_Step = q.t_ID_Step "
          + "    and dr.t_AutoKey = ds.t_OrderID "
          + "    and mv.t_DraftID = dr.t_AutoKey "
          + "    and pm.t_PaymentID = mv.t_PaymentID "
          + "    and kopr.t_Kind_Operation = dr.t_Kind_Operation "
          + "    and spgr.t_SPgroundID = dr.t_SPgroundID "
          + "    and fin.t_FIID = pm.t_FIID ";
  
  cmd = DL_RSDCommand(query);

  var cnt = cmd.GetCount();
  if(cnt > 0)

    InitProgress( cnt, "Идет формирование протокола", "Формирование протокола" );

    DataSet = cmd.Execute();

    while(DataSet.moveNext())
      StrNum = StrNum + 1;

      if(DataSet.Status == SC_SPDRAFT_STATUS_CLOSE)
        CntSuc = CntSuc + 1;
      end;

      DP_AddPrintCell( DpMakeRep,  StrNum,               0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.OprShortName, 0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.AltXld,       0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.BriefCode,    0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.FiCode,       0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.FiName,       0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.Amount,       0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.ErrorMessage, 0, 0, "c:" + RepFontStyle, false );
      DpMakeRep.AddStr();

      UseProgress(StrNum);

    end;

    RemProgress();

  end;

  PrintFooter(TableWidth);
  
  if( ToExcel )
    DpMakeRep.SetFileNameWin("dpdbmake_sett");
    DpMakeRep.PrintWinRep();
    DpMakeRep.ShowWinRep();
  else
    RepFileName = GetTxtFileName("dpdbmake_sett");
    if(ExistFile(RepFileName))
      DelFile(RepFileName);
    end;
    DpMakeRep.SetFileNameTxt(RepFileName);
    DpMakeRep.PrintRep();
    ViewFile(RepFileName);
  end;

end;

macro PrepareRepSecur()
  var TableWidth = Index(HeadSett,"\n");
  var cmd, query, DataSet;
  var RepFileName = "";

  DpMakeRep = CMakeReport(HeadSecur);
  PrintHead("Протокол о выполнении процедуры обработки поручений БОЦБ по ОРЦБ", TableWidth);

  query =   "with q as (select ds1.t_OrderID, Max(ds1.t_ID_Step) as t_ID_Step "
          + "             from doprdistaff_tmp ds1 "
          + "            group by ds1.t_OrderID "
          + "          )"
          + " select dr.t_Status, "
          + "        spgr.t_AltXld, "
          + "        pm.t_Amount, "
          + "        kopr.t_ShortName as OprShortName, "
          + "        NVL((select acnt.t_BriefCode from ddepoacnt_dbt acnt where acnt.t_AutoKey = (case when dr.t_Kind = "+SP_DEPOPER_TRANSFERORDER+" then pm.t_PayerDpNode else pm.t_ReceiverDpNode end)), CHR(1)) as BriefCode, "
          + "        NVL((select pt.t_ShortName from ddepoacnt_dbt acnt, dparty_dbt pt where acnt.t_AutoKey = (case when dr.t_Kind = "+SP_DEPOPER_TRANSFERORDER+" then pm.t_PayerDpNode else pm.t_ReceiverDpNode end) and pt.t_PartyID = acnt.t_Owner), CHR(1)) as OwnerName, "
          + "        fin.t_FI_Code as FICode, fin.t_Name as FIName, "
          + "        ds.t_ErrorMessage "
          + "   from q, doprdistaff_tmp ds, dspdraft_dbt dr, dspground_dbt spgr, dspdrmove_dbt mv, dpmpaym_dbt pm, doprkoper_dbt kopr, dfininstr_dbt fin "
          + "  where ds.t_OrderID = q.t_OrderID "
          + "    and ds.t_ID_Step = q.t_ID_Step "
          + "    and dr.t_AutoKey = ds.t_OrderID "
          + "    and mv.t_DraftID = dr.t_AutoKey "
          + "    and pm.t_PaymentID = mv.t_PaymentID "
          + "    and kopr.t_Kind_Operation = dr.t_Kind_Operation "
          + "    and spgr.t_SPgroundID = dr.t_SPgroundID "
          + "    and fin.t_FIID = pm.t_FIID ";
  
  cmd = DL_RSDCommand(query);

  var cnt = cmd.GetCount();
  if(cnt > 0)

    InitProgress( cnt, "Идет формирование протокола", "Формирование протокола" );

    DataSet = cmd.Execute();

    while(DataSet.moveNext())
      StrNum = StrNum + 1;

      if(DataSet.Status == SC_SPDRAFT_STATUS_CLOSE)
        CntSuc = CntSuc + 1;
      end;

      DP_AddPrintCell( DpMakeRep,  StrNum,               0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.OprShortName, 0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.AltXld,       0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.OwnerName,    0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.BriefCode,    0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.FiCode,       0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.Amount,       0, 0, "c:" + RepFontStyle, false );
      DP_AddPrintCell( DpMakeRep,  DataSet.ErrorMessage, 0, 0, "c:" + RepFontStyle, false );


      DpMakeRep.AddStr();

      UseProgress(StrNum);

    end;

    RemProgress();

  end;

  PrintFooter(TableWidth);
  
  if( ToExcel )
    DpMakeRep.SetFileNameWin("dpdbmake_secur");
    DpMakeRep.PrintWinRep();
    DpMakeRep.ShowWinRep();
  else
    RepFileName = GetTxtFileName("dpdbmake_secur");
    if(ExistFile(RepFileName))
      DelFile(RepFileName);
    end;
    DpMakeRep.SetFileNameTxt(RepFileName);
    DpMakeRep.PrintRep();
    ViewFile(RepFileName);
  end;
    
end;


macro MakeDraftRep(MakeDraftKind)
  
  if(MakeDraftKind == DP_MAKEDRAFTKIND_SETT) //Обработка выписки расчетного депозитария
    PrepareRepSett();
  elif(MakeDraftKind == DP_MAKEDRAFTKIND_SECUR) //Обработка поручений БОЦБ
    PrepareRepSecur();
  end; 
end;