/*
$Name: dpadmlst.mac
$Module: Депозитарий
$Description: Отчет "Журнал административных операций"
*/

import DPInter, dprepsec, cb_sql;

private const OBJTYPE_ADMOPRTYPE = 1207, /* типы административных операций депозитария */
              SP_DEPOPER_INFOPER = 830,  /* информационная операция депозитария */
              TA_STAT_DEL        = 152;

/* Константы типов основных объектов АО (namealg 3104) */
private const OT_DEPOACC  = 0, /* счет депо */
              OT_DEPOPART = 1, /* раздел счета депо */
              OT_ACCOUNT  = 2, /* лицевой счет */
              OT_PARTY    = 3, /* субъект */
              OT_FIID     = 4, /* выпуск ценных бумаг */
              OT_CERTIF   = 5; /* ИК */


private var HeadTable = "┌───┬─────┬─────┬────────────────┬─────────────────┬────────────┬────────────────────┬───────────────────────────┬─────────────────────────────┬────────────────────┬────────────────────────────────┐\n"+
                        "│ № │  №  │ КОП │Поручение       │Операция         │Тип         │ Идентификатор      │          Код поля         │        Новое значение       │Старое значение     │      Отчет об исполнении       │\n"+
                        "│п/п│опер.│     │дата приема/вх.№│дата исп./время  │документа   │                    │                           │                             │                    │                                │\n"+
                        "├───┼─────┼─────┼────────────────┼─────────────────┼────────────┼────────────────────┼───────────────────────────┼─────────────────────────────┼────────────────────┼────────────────────────────────┤\n"+
                        "│ 1 │  2  │  3  │       4        │        5        │     6      │          7         │             8             │               9             │         10         │               11               │\n"+
                        "├───┼─────┼─────┼────────────────┼─────────────────┼────────────┼────────────────────┼───────────────────────────┼─────────────────────────────┼────────────────────┼────────────────────────────────┤"; 
                
private var tablewidth = Index(HeadTable, "\n");
private var Rep = CMakeReport(HeadTable,SEP_DEFAULT,100);
private const RepFontStyleTitel =  "ex_FS(b)";

private var HeadFiltr = 0,
            Num = 1;

private var dprt_num,
            beg_date,
            end_date,
            oper_type,
            oper_item,
            oper_kind,
            obj_type,
            obj_PartyID, 
            obj_DepoAcc, 
            obj_DepoPart,
            obj_FIID,    
            obj_Account, 
            obj_CertifID,
            was_built,
            was_sent,
            is_ap;

private var запуск_из_адм_операций = true;
                                                                        
private var sNAMEs = TArray,
            sFROM  = TArray,
            sTO    = TArray;

private var  depoadmo = TBFile("depoadmo");
private file dpadmopr("dpadmopr");
private file dpgrrep("dpgrrep");
private file ground("spground");
private file infground("spground");
private file spground("spground");
private file party("party");
private file partcode("partcode");
private file depoacnt("depoacnt");
private file fininstr("fininstr");
private file acc("account");
private file invcard("invcard");


private macro FindACCOUNT(Account, FIID)
  Clearrecord(acc);
  acc.Account = Account;
  acc.Code_Currency = FIID;
  acc.Chapter = 5;
  
  if( not getEQ(acc) )
    return false;
  end;
  return true;
end;


private macro FindINVCARD(InvCardID)
  Clearrecord(invcard);
  invcard.InvCardID = InvCardID;
  
  if( not getEQ(invcard) )
    return false;
  end;
  return true;
end;


private macro FindFININSTR(FIID)
  Clearrecord(fininstr);
  fininstr.FIID = FIID;
  
  if( not getEQ(fininstr) )
    return false;
  end;
  return true;
end;


private macro FindDEPOACNT(AutoKey)
  Clearrecord(depoacnt);
  depoacnt.AutoKey = AutoKey;
  
  if( not getEQ(depoacnt) )
    return false;
  end;
  return true;
end;


private macro FindPARTCODE(PartyID)
  Clearrecord(partcode);
  partcode.PartyID = PartyID;
  partcode.CodeKind = 1;
  
  if( not getEQ(partcode) )
    return false;
  end;
  return true;
end;


private macro FindPARTY(PartyID)
  Clearrecord(party);
  party.PartyID = PartyID;
  
  if( not getEQ(party) )
    return false;
  end;
  return true;
end;


private macro FindDPADMOPR( Item, OperKind )
  Clearrecord(dpadmopr);
  dpadmopr.Item     = Item;
  dpadmopr.OperKind = OperKind;
  if( not getEQ(dpadmopr) )
    return false;
  end;
  return true;
end;


private macro FindGROUND( SPGroundID )
  Clearrecord(ground);
  ground.SPgroundID = SPGroundID;
  if( not getEQ(ground) )
    return false;
  end;
  return true;
end;


private macro FindREPORT(SourceDocID, SourceDocKind, InitiatorID, _was_built, _was_sent)
  Clearrecord(dpgrrep);
  dpgrrep.SourceDocKind = SourceDocKind;
  dpgrrep.SourceDocID   = SourceDocID;
  dpgrrep.Recipient     = InitiatorID;
  if( not getEQ(dpgrrep) )
    return false;
  end;

  var cmd, DS;

  cmd = DL_RSDCommand("select t_RepSPgroundID from ddprepdoc_dbt where t_GrRepID = ?");
  cmd.AddParam(dpgrrep.ID);
  DS = cmd.Execute();
  if(not DS.moveNext())
    return false;
  end;

  Clearrecord(spground);
  if( _was_built )
    spground.SPgroundID = DS.RepSPgroundID;
    if( not getEQ(spground) )
      return false;
    end;
    if( _was_sent and (spground.Sent != "X") )
      return false;
    end;
  end;
  return true;
end;

private macro GetSQLStrFindReport(_was_built, _was_sent)
  var query;

    
  query = " select rep.t_SourceDocID, rep.t_SourceDocKind, rep.t_Recipient from ddpgrrep_dbt rep, ddprepdoc_dbt repdoc, dspground_dbt extspgr"
          "  where extspgr.t_SPgroundID = repdoc.t_RepSPgroundID and rep.t_ID = repdoc.t_GrRepID";

  if( (not _was_built) and (_was_sent) )
    query = query + " and extspgr.t_Sent = 'X' " ;
  elif( (_was_built) and (not _was_sent) )
    query = query + " and extspgr.t_Sent != 'X ' " ;
  elif( (not _was_built) and (not  _was_sent) )
    query = "";
  end;

  return query;
end;

private macro InExcel(ToExcel)
   if( ToExcel )
     Rep.SetZoomType( ZOOM_TYPE_A4L );
     Rep.PrintWinRep();
     Rep.ShowWinRep();
   else
     Rep.PrintRep();
   end;
end;

/* печать универсальной шапки таблицы */
private macro AdmListPreHeader()
  if(запуск_из_адм_операций == true)
     //AdmListPreHeader() вызывается первой из GenAdmReport(), файл dpdpadm.c; поэтому здесь вызывается DP_BeginProtocol()
     DP_BeginProtocol();   //начать протоколирование - подмена MsgBox
     InitProgress( -1, "Формирование отчета...", "Журнал административных операций" );
  end;
  DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "ЖУРНАЛ АДМИНИСТРАТИВНЫХ ОПЕРАЦИЙ", 0, 0, false, beg_date, end_date, tablewidth );
end;

private macro AdmListFooter()
  var Excel;
  var err;
  
  DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);
  if(запуск_из_адм_операций == true)
    RemProgress();

    GetRegistryValue( "DEPO\\DEPOREPTOEXCEL", V_BOOL, Excel, err );
    //AdmListFooter() вызывается последней из GenAdmReport(), файл dpdpadm.c; поэтому здесь вызывается DP_EndProtocol()
    if(not err)
       DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
       DP_EndProtocol();                 //закончить протоколирование

       InExcel(Excel);
    else
       MsgBox("Не могу найти настройку выпуска отчетов в Excel");

       DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
       DP_EndProtocol();                 //закончить протоколирование
       
       InExcel(false);
    end;
  
  end;
end;

private macro FillReport()
  var Report = "";
  file typeac("typeac");

  Report = "№ " + spground.Xld + ", составлен " + DateToStr(spground.SignedDate, true);
  if( spground.Sent == "X" )
    Report = Report + ",\nотослан " + dpgrrep.RecipientName + ",\n" + DateToStr(spground.RegistrDate, true);
    typeac.iNumType = TA_STAT_DEL;
    typeac.Type_Account = StrFor(dpgrrep.DeliveryKind);
    if( getEQ(typeac) )
      Report = Report +  ", " + typeac.Name_Type;
    end;
  end;

  return Report;
end;


private macro Max3(a, b, c)
  return max(max(a, b), c);
end;


/* Заполнить конец строки заданным символом на заданную длину.
   Возвращает итоговую строку. Если заданная длина <= длины строки,
   возвращается исходная строка.
*/
private macro StrFillEnd( str, symb, len )
  return str + mkstr( symb, len - strlen( str ) );
end;


/* печать строки таблицы */
private macro AdmListLinePrn( 
  dpadmo         /* буфер административной операции */
)
  var Item          = "",
      AdmOprCode    = "",
      RegistrDate   = date(0, 0, 0),
      Xld           = "",
      Identificator = "",
      PartyID       = -1,
      FieldCode     = "",
      NewValue      = "",
      OldValue      = "",
      Report        = "";

  var MaxLen = 0,
      i      = 0;

  /* Разбираем изменения в анкете */
  while( i < sNAMEs.Size )
    if( ( sFROM[i] != sTO[i]) AND (sNAMEs[i] != "") )
      
      MaxLen = Max3(strlen(sNAMEs[i]), strlen(sTO[i]), strlen(sFROM[i]));

      FieldCode = FieldCode + StrFillEnd(sNAMEs[i], " ", MaxLen) + "\n";

      if( sTO[i] != "" )
        NewValue = NewValue + StrFillEnd(sTO[i], " ", MaxLen) + "\n";
      else
        NewValue = NewValue + StrFillEnd("-", " ", MaxLen) + "\n";
      end;

      if( sFROM[i] != "" )
        OldValue = OldValue + StrFillEnd(sFROM[i], " ", MaxLen) + "\n";
      else
        OldValue = OldValue + StrFillEnd("-", " ", MaxLen) + "\n";
      end;

    end;
    i = i + 1;
  end;

  /* Код адм.действия */
  record llvalues( "llvalues.dbt" );
  if(FindDPADMOPR(dpadmo.Item, dpadmo.OperKind ) )
    LL_FindLLVALUES( OBJTYPE_ADMOPRTYPE, dpadmopr.OperType, llvalues );
    AdmOprCode = llvalues.Code;
  end;

  /* Основание */
  if(FindGROUND(dpadmo.Ground))
    RegistrDate = ground.RegistrDate;
    Xld = ground.Xld;
  end;

  /* типы документов и их идентификаторы */
  if( dpadmo.PartyID != -1 )
    Item = "субъект" + "\n";
    PartyID = dpadmo.PartyID;
    if(FindPARTCODE(PartyID))
      Identificator = partcode.Code + "\n";
    end;
  end;
  if( dpadmo.DepoAcc != 0 )
    Item = Item + "счет депо" + "\n";
    if(FindDEPOACNT(dpadmo.DepoAcc))
      Identificator = Identificator + depoacnt.Code + "\n";
    else
      Identificator = Identificator + "\n";
    end;
  end;
  if( dpadmo.DepoPart != 0 )
    Item = Item + "раздел счета депо" + "\n";
    if(FindDEPOACNT(dpadmo.DepoPart))
      Identificator = Identificator + depoacnt.BriefCode + "\n";
    else
      Identificator = Identificator + "\n";
    end;
  end;
  if( dpadmo.FIID != -1 )
    Item = Item + "ценная бумага" + "\n";
    if(FindFININSTR(dpadmo.FIID))
      Identificator = Identificator + fininstr.FI_Code + "\n";
    else
      Identificator = Identificator + "\n";
    end;
  end;
  if( dpadmo.Account != "" )
    Item = Item + "лицевой счет";
    Identificator = Identificator + dpadmo.Account;
  end;
  if( dpadmo.CertifID != 0 )
    Item = Item + "инвентарная карточка" + "\n";
    if(FindINVCARD(dpadmo.CertifID))
      Identificator = Identificator + invcard.Number + "\n";
    else
      Identificator = Identificator + "\n";
    end;
  end;

  if(FindREPORT(dpadmo.AdmoprID, 825 /*SP_DEPOPER_ADMOPER*/, dpadmo.Initiator, true, false))
    Report = FillReport();
  end;

  var str_buf = "";
  Rep.AddPrintCell( Num, 0, 0, "c:" + RepFontStyleTitel );
  Rep.AddPrintCell( dpadmo.OprXid, 0, 0, "c:" + RepFontStyleTitel );
  Rep.AddPrintCell( AdmOprCode, 0, 0, "c:" + RepFontStyleTitel );
  str_buf = string(RegistrDate:f) + " " + Xld;
  Rep.AddPrintCell( str_buf, 0, 0, "r:" + RepFontStyleTitel );
  str_buf = string(dpadmo.OperDate:f) + " " + substr(string(dpadmo.OperTime),1,5);
  Rep.AddPrintCell( str_buf, 0, 0, "r:" + RepFontStyleTitel );
  Rep.AddPrintCell( Item, 0, 0, "l:w:" + RepFontStyleTitel );
  Rep.AddPrintCell( Identificator, 0, 0, "l:w:" + RepFontStyleTitel );
  Rep.AddPrintCell( FieldCode, 0, 0, "l:w:" + RepFontStyleTitel );
  Rep.AddPrintCell( NewValue, 0, 0, "l:w:" + RepFontStyleTitel );
  Rep.AddPrintCell( OldValue, 0, 0, "l:w:" + RepFontStyleTitel );
  Rep.AddPrintCell( Report, 0, 0, "l:w:" + RepFontStyleTitel );
  Rep.AddStr();
  Num = Num + 1;
  if(запуск_из_адм_операций == true)
    UseProgress( Num );
  end;
end;


/* печать строки таблицы */
private macro AdmListLine( 
  dpadmop         /* буфер административной операции */
)

  record dpadmo(depoadmo);

  if( Valtype(dpadmop) == V_MEMADDR ) /* вызов из списка АО */
    SetBuff(dpadmo, dpadmop);
    GetDEPOADMO_Contents(dpadmo, sNAMEs, sFROM, sTO);
    AdmListLinePrn(dpadmo);
  else
    GetDEPOADMO_Contents(dpadmop, sNAMEs, sFROM, sTO);
    AdmListLinePrn(dpadmop.rec);
  end;

end;



private macro AdmListFilter(
  OperDate,     
  OperDate_Beg:date,   
  OperDate_Fin:date,
  AOType,
  AOCode:string,
  AOName:string,
  AAType,
  AACode:string,
  AAName:string,
  xAuto,
  TechAutoDocCode:string,
  Party,
  PartyCode:string,
  PartyName:string,
  DepoAcc,
  DepoAccCode:string,
  DepoAccName:string,
  DepoPart,
  DepoPartCode,
  DepoPartName,
  FI,
  FI_Code:string,
  FI_Name:string,
  Acc,
  Account:string,
  AccountName:string,
  xCert,
  Xid:string,
  xDepart,
  Department:integer,
  xBranch,
  Branch:integer,
  xRegNum,
  RegNumBeg:string,
  RegNumEnd:string,
  xUserLog,
  UserLogName:string,
  xRegDate,
  RegDateBeg:date,
  RegDateEnd:date,
  xRecept,
  Receptionist:integer,
  ReceptName:string
)

  macro FiltrHead()
    if(not HeadFiltr)
      Rep.AddPrintCell( "  Параметры фильтра:", 25, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
      Rep.AddStr();
      HeadFiltr = 1;
    end;
  end;

  if(xRegNum)
    FiltrHead();
    Rep.AddPrintCell( "     вх. №                    ", 31, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddPrintCell( RegNumBeg, 0, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddPrintCell( " - ", 3, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddPrintCell( RegNumEnd, 0, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(xUserLog)
    FiltrHead();
    Rep.AddPrintCell( "     по журналу                " + UserLogName, tablewidth, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(xRegDate)
    FiltrHead();
    Rep.AddPrintCell( "     дата приема               ", 31, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddPrintCell( RegDateBeg, 10, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddPrintCell( " - ", 3, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddPrintCell( RegDateEnd, 10, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(xRecept)
    FiltrHead();
    Rep.AddPrintCell( "     принял                    " + Receptionist + " - " + ReceptName, tablewidth, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(OperDate)
    FiltrHead();
    Rep.AddPrintCell( "     за период                 ", 31, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddPrintCell( OperDate_Beg, 10, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddPrintCell( " - ", 3, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddPrintCell( OperDate_Fin, 10, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(AOType)
    FiltrHead();
    Rep.AddPrintCell( "     тип                       " + AOCode + " - " + AOName, tablewidth, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(AAType)
    FiltrHead();
    Rep.AddPrintCell( "     административное действие " + AACode + " - " + AAName, tablewidth, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(xAuto)
    FiltrHead();
    Rep.AddPrintCell( "     автоформирование " + TechAutoDocCode, tablewidth, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(Party)
    FiltrHead();
    Rep.AddPrintCell( "     субъект                   " + PartyCode + " - " + PartyName, tablewidth, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(DepoAcc)
    FiltrHead();
    Rep.AddPrintCell( "     счет депо                 " + DepoAccCode + " - " + DepoAccName, tablewidth, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(DepoPart)
    FiltrHead();
    Rep.AddPrintCell( "     раздел счет депо          " + DepoPartCode + " - " + DepoPartName, tablewidth, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(FI)
    FiltrHead();
    Rep.AddPrintCell( "     выпуск ц/б                " + FI_Code + " - " + FI_Name, tablewidth, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(Acc)
    Rep.AddPrintCell( "     лицевой счет              " + Account + " - " + AccountName, tablewidth, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
    FiltrHead();
  end;

  if(xCert)
    FiltrHead();
    Rep.AddPrintCell( "     инвентарная карточка      " + Xid, tablewidth, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddStr();
  end;

  if(HeadFiltr)
    Rep.AddEmptyStr();
  end;
end;

private macro GetSQLStr()
  var stat;
  var query = "";
  query = " t_AdmoprID > 0 and t_Department = " + dprt_num;

   /* Если заданы даты, то проверяем, чтобы попадали в диапазон */
  if((beg_date > date(0,0,0)) and (end_date > date(0,0,0)))
    query = query + " and t_OperDate >= " + GetSQLDate(beg_date)
                  + " and t_OperDate <= " + GetSQLDate(end_date);
  end;

  /* адм. действие */
  if( ( oper_item > 0 ) AND ( oper_kind > 0 ) )
    query = query + " and t_Item = " + oper_item
                  + " and t_OperKind = " + oper_kind;
  end;

  if( oper_type > 0 )
    query = query + " and (t_Item, t_OperKind) in "
                    " ( select t_Item, t_OperKind from ddpadmopr_dbt where t_OperType = "+oper_type+" ) ";
  end;

  /*  Проверим, чтобы совпадал объект */
  if( obj_type == OT_PARTY ) /* субъект */
    if( obj_PartyID != -1 )
      query = query + " and t_PartyID = " + obj_PartyID;
    else
      query = query + " and t_PartyID >" + obj_PartyID;
    end;
  elif( obj_type == OT_DEPOACC ) /* счет депо */
    if( obj_DepoAcc > 0 )
      query = query + " and t_DepoAcc = " + obj_DepoAcc;
    else
      query = query + " and t_DepoAcc > 0 ";
    end;
  elif( obj_type == OT_DEPOPART ) /* раздел счета депо */
    if( obj_DepoPart > 0 )
      query = query + " and t_DepoPart = " + obj_DepoPart;
    else 
      query = query + " and t_DepoPart > 0 ";
    end;
  elif( obj_type == OT_FIID ) /* выпуск ценных бумаг */
    if( obj_FIID != -1 )
      query = query + " and t_FIID = " + obj_FIID;
    else
      query = query + " and t_FIID > " + obj_FIID;
    end;
  elif( obj_type == OT_ACCOUNT ) /* лицевой счет */
    if( ((obj_Account != "") AND ( obj_FIID != -1 )) )
      query = query + " and t_Account = '" + obj_Account + "' and t_FIID = " + obj_FIID;
    elif( ((obj_Account == "") AND ( obj_FIID == -1 ))  )
      query = query + " and t_Account  != CHR(0) " + " and t_FIID > " + obj_FIID;
    end;
  elif(obj_type == OT_CERTIF) /*ИК*/
    if( obj_CertifID > 0 )
      query = query + " and t_CertifID = " + obj_CertifID;
    else
      query = query + " and t_CertifID > " + obj_CertifID;
    end;
  end;

  if( (was_built) or (was_sent) )
    query = query + " and (t_AdmoprID, 825, t_Initiator) in ( " + GetSQLStrFindReport( was_built, was_sent) + " ) ";
  end;

  return query;
end;


private macro PrintRepParams()
  var xOperDate = false,
  OperDate_Beg = NULL,
  OperDate_Fin = NULL,
  xAOType = false,
  AOCode = NULL,
  AOName = NULL,
  xAAType = false,
  AACode = NULL,
  AAName = NULL,
  xParty = false,
  PartyCode = NULL,
  PartyName = NULL,
  xDepoAcc = false,
  DepoAccCode = NULL,
  DepoAccName = NULL,
  xDepoPart = false,
  DepoPartCode = NULL,
  DepoPartName = NULL,
  xFI = false,
  FI_Code = NULL,
  FI_Name = NULL,
  xAcc = false,
  Account = NULL,
  AccountName = NULL,
  xCert = false,
  Xid = NULL,
  xDepart = false,
  Department = NULL,
  xBranch = false,
  Branch = NULL;

  record llvalues( "llvalues.dbt" );

  if((oper_type) AND (LL_FindLLVALUES( OBJTYPE_ADMOPRTYPE, oper_type, llvalues)))
    xAOType = true;
    AOCode = llvalues.Code;
    AOName = llvalues.Name;
  end;


  if((oper_item) AND (oper_kind) AND (FindDPADMOPR(oper_item, oper_kind)))
    xAAType = true;
    AACode = dpadmopr.Code;
    AAName = dpadmopr.Name;
  end;

  if((obj_PartyID != -1) AND FindPARTY(obj_PartyID))
    xParty = true;
    PartyName = party.Name;
    if(FindPARTCODE(obj_PartyID))
      PartyCode = partcode.Code;
    end;
  end;

  if((obj_DepoAcc) AND (FindDEPOACNT(obj_DepoAcc)))
    xDepoAcc = true;
    DepoAccCode = depoacnt.BriefCode;
    DepoAccName = depoacnt.Name;
  end;

  if((obj_DepoPart) AND (FindDEPOACNT(obj_DepoPart)))
    xDepoPart = true;
    DepoPartCode = depoacnt.BriefCode;
    DepoPartName = depoacnt.Name;
  end;

  if((obj_FIID != -1) AND (FindFININSTR(obj_FIID)))
    xFI = true;
    FI_Code = fininstr.FI_Code;
    FI_Name = fininstr.Name;
  end;

  if((obj_Account != "") AND (obj_FIID != -1) AND (FindACCOUNT(obj_Account, obj_FIID)))
    xAcc = true;
    Account = acc.Account;
    AccountName = acc.NameAccount;
  end;

  if((obj_CertifID) AND (FindINVCARD(obj_CertifID)))
    xCert = true;
    Xid = invcard.Number;
  end;

  AdmListFilter( xOperDate,   OperDate_Beg, OperDate_Fin,
                   xAOType,         AOCode,       AOName,
                   xAAType,         AACode,       AAName,
                     false,              0,
                    xParty,      PartyCode,    PartyName,
                  xDepoAcc,    DepoAccCode,  DepoAccName,
                 xDepoPart,   DepoPartCode, DepoPartName,
                       xFI,        FI_Code,      FI_Name,
                      xAcc,        Account,  AccountName,
                     xCert,            Xid,
                   xDepart,     Department,
                   xBranch,         Branch);

end;


macro AdmList(
  _dprt_num     : integer,
  _beg_date     : date,
  _end_date     : date, 
  _oper_type    : integer,
  _oper_item    : integer,
  _oper_kind    : integer,
  _obj_type     : integer,
  _obj_PartyID  : integer,
  _obj_DepoAcc  : integer,
  _obj_DepoPart : integer,
  _obj_FIID     : integer,
  _obj_Account  : string,
  _obj_CertifID : integer,
  _was_built    : bool,
  _was_sent     : bool,
  ToExcel       : bool,
  _is_ap        : bool
)
   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

   dprt_num     = _dprt_num;
   beg_date     = _beg_date;
   end_date     = _end_date;
   oper_type    = _oper_type;
   oper_item    = _oper_item;
   oper_kind    = _oper_kind;
   obj_type     = _obj_type;
   obj_PartyID  = _obj_PartyID; 
   obj_DepoAcc  = _obj_DepoAcc; 
   obj_DepoPart = _obj_DepoPart;
   obj_FIID     = _obj_FIID;    
   obj_Account  = _obj_Account; 
   obj_CertifID = _obj_CertifID;
   was_built    = _was_built;
   was_sent     = _was_sent;
   is_ap        = _is_ap;

   запуск_из_адм_операций = false;
   
   ClearRecord(depoadmo);


   var sQuery;
   sQuery = GetSQLStr();
   depoadmo.Clear;
   depoadmo.AddFilter(sQuery);
   InitProgress( nrecords(depoadmo), "Формирование отчета...", "Журнал административных операций" );

   while( next(depoadmo) )
       if( Num == 1 )
         AdmListPreHeader();
         PrintRepParams();
       end;
       UseProgress( Num );
       AdmListLine( depoadmo );
   end;
 
   RemProgress();

   if( Num > 1 )
     DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
     DP_EndProtocol();                 //закончить протоколирование

     AdmListFooter();
     InExcel(ToExcel);
   else
     DP_EndProtocol();                 //закончить протоколирование
     MsgBox("Не найдено данных, удовлетворяющих параметрам отчета");
   end;

   return 0;
end;
