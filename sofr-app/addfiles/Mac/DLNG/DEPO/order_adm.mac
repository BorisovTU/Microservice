/*
$Name:        order_adm.mac
$Module:      Депозитарий
$Description: Отчет об исполнении административной операции
*/

import deposerv, CurrInter, cb_sql, adress, pt_lib;
import RsbDataSet;
import dpAdmoCLOB, order_inv, order_inf;

private VAR sNAME   = TArray;
private VAR sFROM   = TArray;
private VAR sTO     = TArray;
private VAR sIDFROM = TArray;
private VAR sIDTO   = TArray;

private var iCountRep = 0;
private var PrintSubNum = false;

const OBJTYPE_ADMOPRTYPE = 1207;//типы административных операций

private var Table1 = "┌──────────────────────┬─────────────────────┬───────────────────────────────┐\n"+
                     "│тип объекта           │код(идентификатор)   │Комментарий                    │\n"+
                     "├──────────────────────┼─────────────────────┼───────────────────────────────┤";
             
private var Table2 = "┌──────────────┬──────────────────┬────────────────┬──────────────────────┬─────────────────────┐\n"+
                     "│документ      │Идентификатор     │Код поля        │Новое значение        │Старое значение      │\n"+
                     "│(объект)      │                  │                │                      │                     │\n"+
                     "├──────────────┼──────────────────┼────────────────┼──────────────────────┼─────────────────────┤";

private var Table3 = "┌───────────────────────────────────────────────────────────────────────────────────────────────┐\n"+
                     "│                                  СВЕДЕНИЯ ОБ УПОЛНОМОЧЕННОМ ЛИЦЕ                              │\n"+
                     "├────────────────────────┬──────────────────────────────────────────────────────────────────────┤";


var RepAdm = CMakeReport();
const RepFontStyleAdm =  "ex_FS(b)";
const TableWidthAdm = 97;

private var opKind, Department, Party, DepoAcc, DepoPart, DAccount, FIID, InvCardID;
private var _AdmoID=0, _AdmRepRecipientID=-1, _RepNum, _RepDate, _RepTime, _RepOper;

private var isPrint = false;

macro PrintObjects( _party, _depoacc, _depopart, _daccount, _FIID, _InvCardID)

  var partcode = TBFILE("partcode");
  var depoacnt = TBFILE("depoacnt");
  var fininstr = TBFILE("fininstr");
  var invcard  = TBFILE("invcard.dbt");
  var num_undef = 0;
  var depo_acc = _depoacc;
  var depo_part = _depopart;
  var Format, i = 0;
  
  /* проверка - если задан только 1 параметр, то тут ничего не печатать */
  if (_party == -1)
    num_undef = num_undef + 1;
  end;
  if (_depoacc <= 0)
    num_undef = num_undef + 1;
  end;
  if (_depopart <= 0)
    num_undef = num_undef + 1;
  end;
  if (_FIID == -1)
    num_undef = num_undef + 1;
  end;
  if (_daccount == "")
    num_undef = num_undef + 1;
  end;
  if(_InvCardID <= 0)
    num_undef = num_undef + 1;
  end;
  if (num_undef >= 5)
    return;
  end; 
  
  DP_AddPrintCell( RepAdm,  "Объекты операции", 50, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
  RepAdm.AddStr();
  RepAdm.AutoScan( "[\n" + Table1 + "]" );

  if (_party != -1)
    if(i == 0)
       Format ="ex_B(rlb)";
    else
       Format ="";
    end;
    partcode.rec.PartyID = _party;
    partcode.rec.CodeKind = 1;
    if (NOT partcode.GetEQ())
      DP_AddPrintCell( RepAdm,  "Не найден код субъекта с ID = " + _party, 100, 0, "l:" + Format + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();
    end;
    DP_AddPrintCell( RepAdm,  "субъект", 22, 0, "l:" + Format + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm,  partcode.rec.Code, 21, 0, "l:" + Format + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm,  GetPartyName( _party, True), 31, 0, "l:" + Format + RepFontStyleAdm );
    RepAdm.AddStr();
    i = i + 1;
  end;

  if (depo_acc > 0)
    if(i == 0)
       Format ="ex_B(rlb)";
    else
       Format ="";
    end;
    depoacnt.rec.AutoKey = depo_acc;
    if (NOT depoacnt.GetEQ())
      DP_AddPrintCell( RepAdm,  "Не найден счет депо с ID = " + depo_acc, 100, 0, "l:" + Format + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();
    end;
    DP_AddPrintCell( RepAdm,  "счет депо", 22, 0, "l:" + Format + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm,  depoacnt.rec.BriefCode, 21, 0, "l:" + Format + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm,  GetPartyName( depoacnt.rec.Owner, True), 31, 0, "l:" + Format + RepFontStyleAdm );
    RepAdm.AddStr();
    i = i + 1;
  end;

  if (depopart > 0)
    if(i == 0)
       Format ="ex_B(rlb)";
    else
       Format ="";
    end;
    depoacnt.rec.AutoKey = depo_part;
    if (NOT depoacnt.GetEQ())
      DP_AddPrintCell( RepAdm,  "Не найден раздел счета депо с ID = " + depo_part, 100, 0, "l:" + Format + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();
    end;
    DP_AddPrintCell( RepAdm,  "счет депо", 22, 0, "l:" + Format + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm,  depoacnt.rec.BriefCode, 21, 0, "l:" + Format + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm,  GetPartyName( depoacnt.rec.Owner, True), 31, 0, "l:" + Format + RepFontStyleAdm );
    RepAdm.AddStr();
    i = i + 1;
  end;

  if (_daccount != "")
    if(i == 0)
       Format ="ex_B(rlb)";
    else
       Format ="";
    end;
    DP_AddPrintCell( RepAdm,  "лицевой счет", 22, 0, "l:" + Format + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm,  _daccount, 21, 0, "l:" + Format + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm,  "", 31, 0, "l:" + Format + RepFontStyleAdm );
    RepAdm.AddStr();
    i = i + 1;
  end;
    
  if (_FIID != -1)
    if(i == 0)
       Format ="ex_B(rlb)";
    else
       Format ="";
    end;
    fininstr.rec.FIID = _FIID;
    if (NOT fininstr.GetEQ())
      DP_AddPrintCell( RepAdm,  "Не найдена ц/б с ID = " + _FIID, 100, 0, "l:" + Format + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();
    end;
    DP_AddPrintCell( RepAdm,  "ценная бумага", 22, 0, "l:" + Format + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm,  fininstr.rec.FI_Code, 21, 0, "l:" + Format + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm,  fininstr.rec.Name, 31, 0, "l:" + Format + RepFontStyleAdm );
    RepAdm.AddStr();
    i = i + 1;
  end;
  
  if (_InvCardID > 0)
    if(i == 0)
       Format ="ex_B(rlb)";
    else
       Format ="";
    end;

    invcard.rec.InvCardID = _InvCardID;
    if (NOT invcard.GetEQ())
      DP_AddPrintCell( RepAdm,  "Не найдена ИК с ID = " + _InvCardID, 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();
    end;
    DP_AddPrintCell( RepAdm,  "инвентарная карточка", 22, 0, "l:" + Format + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm,  invcard.rec.Number, 21, 0, "l:" + Format + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm,  "", 31, 0, "l:" + Format + RepFontStyleAdm );
    RepAdm.AddStr();
    i = i + 1;
  end;

  RepAdm.AddEmptyStr();
  RepAdm.AddEmptyStr();
end;


macro PrintChanges(_AdmoprID)

  var i = 0, document = "", id = "", isFirst = true, TotalRec = -1;
  file depoadmo ("depoadmo");
  var partcode = TBFILE("partcode");
  var depoacnt = TBFILE("depoacnt");
  var fininstr = TBFILE("fininstr");
  var invcard = TBFILE("invcard.dbt");
  var admop = TBFILE("dpadmopr");
  var Format;

  depoadmo.AdmoprID = _AdmOprID;
  if (GetEQ(depoadmo))
    admop.rec.Item = depoadmo.Item;
    admop.rec.OperKind = depoadmo.OperKind;
    admop.GetEQ();
  end; 
      
  
  GetDEPOADMO_Contents(depoadmo, sName, sFROM, sTO);  

  DP_AddPrintCell( RepAdm,  "Административное действие: " + admop.rec.Code + " ," + admop.rec.Name, 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
  RepAdm.AddStr();
  RepAdm.AutoScan( "[\n" + Table2 + "]" );

  /* проверка поля Item */
  if ((depoadmo.Item == SPDP_CLIENT) OR
      (depoadmo.Item == SPDP_GUARDIAN) OR
      (depoadmo.Item == SPDP_OPERATOR) OR
      (depoadmo.Item == SPDP_MANAGER) OR
      (depoadmo.Item == SPDP_OURDEPOSITARY) OR
      (depoadmo.Item == SPDP_STORAGEPLACE))
    document = "субъект";
    partcode.rec.PartyID = depoadmo.partyID;
    partcode.rec.CodeKind = 1;
    if (NOT partcode.GetEQ())
      DP_AddPrintCell( RepAdm,  "Не найден код субъекта с ID = " + depoadmo.partyID, 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();
    else
      id = partcode.rec.Code;
    end;

  elif (depoadmo.Item == SPDP_AVOIRIS)
    document = "ценная бумага";
    fininstr.rec.FIID = depoadmo.FIID;
    if (NOT fininstr.GetEQ())
      DP_AddPrintCell( RepAdm,  "Не найдена ц/б с ID = " + depoadmo.FIID, 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();
    else
      id = fininstr.rec.FI_Code;
    end;

  elif ((depoadmo.Item == SPDP_DEPO_ACCOUNT) OR
        (depoadmo.Item == SPDP_ACCMANAGER))
    document = "счет депо";
    /* записис про права оператора/распорядителя */
    /*if (depoadmo.Item == SPDP_DEPO_ACCOUNT)
      TotalRec = DP_ADMDACNT_Total;
    else
      TotalRec = DP_DACMNGR_Total;
    end;*/ 
    depoacnt.rec.AutoKey = depoadmo.DepoAcc;
    if (NOT depoacnt.GetEQ())
      DP_AddPrintCell( RepAdm,  "Не найден счет депо с ID = " + depoadmo.DepoAcc, 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();
    else
      id = depoacnt.rec.Code;
    end;      

  elif ((depoadmo.Item == SPDP_PARTMANAGER) OR
        (depoadmo.Item == SPDP_DEPO_PARTITION))
    document = "раздел счета";
    /* записис про права оператора/распорядителя */
    /*if (depoadmo.Item == SPDP_DEPO_ACCOUNT)
      TotalRec = DP_ADMDPART_Total;
    else
      TotalRec = DP_DPTMNGR_Total;
    end;*/
    depoacnt.rec.AutoKey = depoadmo.DepoPart;
    if (NOT depoacnt.GetEQ())
      DP_AddPrintCell( RepAdm,  "Не найден счет депо с ID = " + depoadmo.DepoPart, 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();
    else
      id = depoacnt.rec.Code;
    end;      
   
  elif (depoadmo.Item == SPDP_FACE_ACCOUNT) 
    document = "лицевой счет";
    id = depoadmo.account;

  elif (depoadmo.Item == SPDP_INVCARD) 
    document = "инвентарная карточка";
    invcard.rec.InvCardID = depoadmo.CertifID;
    if (NOT invcard.GetEQ())
      DP_AddPrintCell( RepAdm,  "Не найдена ИК с ID = " + depoadmo.CertifID, 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();
    else
      id = invcard.rec.Number;
    end;
  end;

  while (i < sName.Size) /* 64 - размер массива параметров АО */
    if ( (sName[i] != "") AND ((sFrom[i] != sTo[i]) OR (i == sName.size)) )
      if (isFirst == true)
        Format = "ex_B(rlb)";
        DP_AddPrintCell( RepAdm,  document, 14, 0, "l:" + Format + RepFontStyleAdm );
        DP_AddPrintCell( RepAdm,  id, 18, 0, "l:" + Format + RepFontStyleAdm );
        DP_AddPrintCell( RepAdm,  sName[i], 16, 0, "l:w:" + Format + RepFontStyleAdm );
        DP_AddPrintCell( RepAdm,  sTO[i], 22, 0, "l:w:" + Format + RepFontStyleAdm );
        DP_AddPrintCell( RepAdm,  sFROM[i], 21, 0, "l:w:" + Format + RepFontStyleAdm );
        RepAdm.AddStr();
        isFirst = false;
      else
        DP_AddPrintCell( RepAdm,  "", 14, 0, "l:" + RepFontStyleAdm );
        DP_AddPrintCell( RepAdm,  "", 18, 0, "l:" + RepFontStyleAdm );
        DP_AddPrintCell( RepAdm,  sName[i], 16, 0, "l:w:" + RepFontStyleAdm );
        DP_AddPrintCell( RepAdm,  sTO[i], 22, 0, "l:w:" + RepFontStyleAdm );
        DP_AddPrintCell( RepAdm,  sFROM[i], 21, 0, "l:w:" + RepFontStyleAdm );
        RepAdm.AddStr();
      end;
    end;
    i = i + 1;
  end;
end;


macro GetLLVALUESCode( List, Elem )
  file llvalues("llvalues");

  ClearRecord(llvalues);
  llvalues.List    = List;
  llvalues.Element = Elem;
  if( GetEQ(llvalues) )
    return llvalues.Code;
  end;

  return "-";
end;


private macro GetAddress(party, address_kind)
  var Adress = "-";
  record RecordAdress ( adress );

  ClearRecord(RecordAdress);

  НайтиАдресСубъекта(party.PartyID,address_kind, RecordAdress);

  return RecordAdress.Adress;
end;

private class TAcapData(_partyID:integer, _role:integer)
  var PartyID:integer = _partyID,
      Role:integer    = _role;      
end;

private macro GetAcapData(admOprID:integer, operKind:integer):TAcapData
  var acapData:TAcapData = null;
  var acapEmpty:TArray, acapFull:TArray; // пустой и заполненный массивы УЛ (для отмены new пустой, old заполненный, для назначения наоборот)

  if(admOprID> 0)
    if(LoadClobDataByAdmo(admOprID) == 0)
      // найдем подходящую запись (если назначение, то новый new (нет в old), если отмена, то есть в old, но нет в new)
      if (SPAO_CON_AUTHORITY_ASSIGN == operKind)
        acapEmpty = clobData.oldData.Acap;
        acapFull  = clobData.newData.Acap;
      else
        acapEmpty = clobData.newData.Acap;
        acapFull  = clobData.oldData.Acap;
      end;        
      for (var elem, acapFull)
        if (not clobData.IsElemInArrData(acapEmpty, elem.APID))
          acapData = TAcapData(elem.APartyID, elem.Role);
          break;
        end;
      end;
    end;
  end;
  
  return acapData;
end;


macro PrintOrderAdmForm(_groundID, _admoprID, date_begin, date_end)

  var ground = TBFILE("spground");
  var GroundAdd = TBFILE("spground","r",6);
  var depoadmo = TBFILE("depoadmo");
  var logopr = TBFILE("splogopr");
  var admop = TBFILE("dpadmopr");
  var person = TBFILE("person");
  var depoacnt = TBFILE("depoacnt");
  var cmd, DataSet, stat;

  var Reciever = "-", Основание = "", Opername = "_";
  var ExNum = "         ", ExDate = date(0,0,0), ExTime = time(0,0), ExOper = 0, ExitInfo, AgentID;
  var passp_str = "", ogrn_str = "", Name_Country = "";

  file paper("paprkind");
  file party("party");
  file country("country") key 2;
  record persnidc("persnidc");

  /* найдем получателя отчета */  
  if(_AdmRepRecipientID > 0)
    Reciever = GetPartyName( _AdmRepRecipientID, true );
    ExNum  = _RepNum;
    ExDate = _RepDate;
    ExTime = _RepTime;
    ExOper = _RepOper;
  elif( LOID != 0 )  
     logopr.rec.AutoKey = LOID;
     if( logopr.GetEQ() ) 

       GroundAdd.rec.SourceDocKind = 830;
       GroundAdd.rec.SourceDocID = LOID;
       GroundAdd.rec.Direction = EXITING;
       GroundAdd.rec.Department = Department;
       GroundAdd.rec.Division = SelfDivision;
       if (GroundAdd.GetEQ())
         if(GroundAdd.rec.PartyName == "")
           Reciever = GetPartyName( GroundAdd.rec.Party, true );
         else
           Reciever = GroundAdd.rec.PartyName;
         end;
         ExNum  = GroundAdd.rec.Xld;
         ExDate = GroundAdd.rec.RegistrDate; 
         ExTime = GroundAdd.rec.RegistrTime;
         ExOper = GroundAdd.rec.Receptionist;
       end;
     end;
  end; 

  /* установка позиции файлов для даты,№ поручения #213994 */
  ground.rec.SPgroundID = _GroundID;
  ground.GetEQ();

  cmd = DL_RSDCommand(" select spgr.* from dspgrdoc_dbt gr, dspground_dbt spgr "
                    + "  where gr.t_SourceDocKind = 825 " //SP_DEPOPER_ADMOPER  
                    + "    and gr.t_SourceDocID = ? " 
                    + "    and spgr.t_SPgroundID = gr.t_SPgroundID "
                    + "    and spgr.t_Direction <> " + EXITING);

  cmd.AddParam(_AdmoprID);

  DataSet = cmd.Execute();

  stat = DataSet.moveNext();
  while( stat )
    Основание = Основание + GetLLVALUES(OBJTYPE_KINDDOC,DataSet.Kind) + " №" +
                DataSet.AltXld + " от " + string(date(DataSet.SignedDate):f);
    stat = DataSet.moveNext();
    if( stat )
      Основание = Основание + ", \n";
    end;
  end;
  if(Основание == "")
    Основание = "-";
  end;


  depoadmo.rec.AdmoprID = _AdmoprID;
  depoadmo.GetEQ();

  /* вид операции */
  admop.rec.Item = depoadmo.rec.Item;
  admop.rec.OperKind = depoadmo.rec.OperKind;
  if ( admop.GetEQ() )
    Opername = GetLLVALUESCode( OBJTYPE_ADMOPRTYPE, admop.rec.OperType ) + ", " + GetLLVALUES(OBJTYPE_ADMOPRTYPE, admop.rec.OperType);
  end;

  if (isPrint == false)
    isPrint = true;
  end;

  if( iCountRep > 0 )
    DP_AddPrintCell( RepAdm,  "-------------------------------------------------------------------------------", 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
  end;

  if((depoadmo.rec.OperKind != SPAO_CON_AUTHORITY_ASSIGN) AND (depoadmo.rec.OperKind != SPAO_CON_AUTHORITY_CANCEL))
    DP_StdHeaderLO_Ex(RepAdm, RepFontStyleAdm, "ОТЧЕТ ОБ ИСПОЛНЕНИИ АДМИНИСТРАТИВНОЙ ОПЕРАЦИИ" , LOID, iCountRep+1, PrintSubNum, date_begin, date_end, TableWidthAdm, _RepNum, _RepDate);
    DataPrinted = true;

    DP_AddPrintCell( RepAdm,  "Отправитель отчета", 23, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    DP_AddPrintCell( RepAdm,  GetPartyName( {OurBank}, true ), 77, 0, "l:w:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    DP_AddPrintCell( RepAdm,  "Получатель отчета", 23, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    DP_AddPrintCell( RepAdm,  Reciever,            77, 0, "l:w:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    DP_AddPrintCell( RepAdm,  "Поручение", 23, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    DP_AddPrintCell( RepAdm,  "№" + ground.rec.AltXld + " от " + string(ground.rec.SignedDate:f) + 
                      ", Вх.№" + ground.rec.Xld + ", дата приема " + string(ground.rec.RegistrDate:f), 77, 0, "l:w:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    DP_AddPrintCell( RepAdm,  "Инициатор", 23, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    DP_AddPrintCell( RepAdm,  depoadmo.rec.InitiatorName, 77, 0, "l:w:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    DP_AddPrintCell( RepAdm,  "Депозитарная операция", 23, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    DP_AddPrintCell( RepAdm,  OperName, 77, 0, "l:w:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    DP_AddPrintCell( RepAdm,  "    основание", 23, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    DP_AddPrintCell( RepAdm, Основание, 77, 0, "l:w:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    DP_AddPrintCell( RepAdm,  "                        ", 23, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    DP_AddPrintCell( RepAdm,  GetLLVALUES(1200, depoadmo.rec.Product), 77, 0, "l:w:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    DP_AddPrintCell( RepAdm,  "-------------------------------------------------------------------------------", 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    RepAdm.AddEmptyStr();

    /* таблица объектов АО */
    PrintObjects(depoadmo.rec.PartyID, depoadmo.rec.DepoAcc, depoadmo.rec.DepoPart, 
                 depoadmo.rec.Account, depoadmo.rec.FIID, depoadmo.rec.CertifID);

    PrintChanges(_AdmoprID);

    RepAdm.AddEmptyStr();
    DP_AddPrintCell( RepAdm,  "Обработка поручения                  \"ИСПОЛНЕНО\"", 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    DP_AddPrintCell( RepAdm,  "    запись в журнале операций        " + depoadmo.rec.OprXid, 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    DP_AddPrintCell( RepAdm,  "    дата                             " + string(depoadmo.rec.OperDate:f), 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();

    /* исполнитель */
    person.rec.Oper = depoadmo.rec.Oper;
    person.GetEQ();

    DP_AddPrintCell( RepAdm,  "    исполнитель                      " + person.rec.Name, 100, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    RepAdm.AddEmptyStr();

    DP_StdAuthPerson_Ex(RepAdm, RepFontStyleAdm);
    DP_StdDeposInfo_Ex(RepAdm, RepFontStyleAdm);
    DP_StdExecReport_Ex(RepAdm, RepFontStyleAdm);
  else
    DP_AddPrintCell( RepAdm, GetPartyName( {OurBank}, true ), TableWidthAdm, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    DP_AddPrintCell( RepAdm, mkstr("-", strlen(GetPartyName( {OurBank})), true ), TableWidthAdm, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    RepAdm.AddEmptyStr();

    if( ExDate == Date(0,0,0))
      ExDate = "                    ";
    else
      if(ExTime == Time(00,00,00))
        ExDate = String(ExDate) + "         ";
      else
        ExDate = String(ExDate) + ", " + String(ExTime);
      end;
    end;
    /* исполнитель */
    person.rec.Oper = ExOper;
    if(person.GetEQ())
      ExOper = person.rec.Name;
    else
      ExOper = "";
    end;

    if((trim(ExNum) != "") AND PrintSubNum and (ValType(_RepNum) == V_UNDEF) or (_RepNum == ""))
      ExNum = ExNum + "/" + string(iCountRep+1);
    end;

    ExitInfo = "Исх.№ " + ExNum + " Дата, время " + ExDate + " Отправил " + ExOper;

    DP_AddPrintCell( RepAdm, "Форма RA03 " + mkstr(" ", TableWidthAdm - strlen(ExitInfo) - 16) + ExitInfo, TableWidthAdm, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddEmptyStr();
    RepAdm.AddEmptyStr();
    DataPrinted = true;

    DP_AddPrintCell( RepAdm, "УВЕДОМЛЕНИЕ О РЕГИСТРАЦИИ УПОЛНОМОЧЕННОГО ЛИЦА", TableWidthAdm, 0, "c:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddEmptyStr();

    DP_AddPrintCell( RepAdm, "Счет депо №", 24, 0, "l:" + RepFontStyleAdm );
    depoacnt.rec.AutoKey = depoadmo.rec.DepoAcc;
    if (NOT depoacnt.GetEQ())
      DP_AddPrintCell( RepAdm,  "Не найден счет депо с ID = " + depoadmo.rec.DepoAcc, 70, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();
    end;
    DP_AddPrintCell( RepAdm,  depoacnt.rec.BriefCode, 70, 0, "l:" + RepFontStyleAdm );
    RepAdm.AddStr();
    DP_AddPrintCell( RepAdm, "ДЕПОНЕНТ", 24, 0, "l:" + RepFontStyleAdm );
    DP_AddPrintCell( RepAdm, GetPartyName( depoacnt.rec.Owner, false ), 70, 0, "l:" + RepFontStyleAdm );
    RepAdm.AddStr();
    
    RepAdm.AddEmptyStr();
    DP_AddPrintCell( RepAdm, "Настоящим подтверждается исполнение следующей операции по указанному счету депо Депонента:", TableWidthAdm, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    RepAdm.AddStr();
    RepAdm.AddEmptyStr();

    if(depoadmo.rec.OperKind == SPAO_CON_AUTHORITY_ASSIGN)
      DP_AddPrintCell( RepAdm, "[X] - РЕГИСТРАЦИЯ УПОЛНОМОЧЕННОГО ЛИЦА         [ ] - ОТМЕНА УПОЛНОМОЧЕННОГО ЛИЦА", TableWidthAdm, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    else
      DP_AddPrintCell( RepAdm, "[ ] - РЕГИСТРАЦИЯ УПОЛНОМОЧЕННОГО ЛИЦА         [X] - ОТМЕНА УПОЛНОМОЧЕННОГО ЛИЦА", TableWidthAdm, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
    end;
    RepAdm.AddStr();
    RepAdm.AddEmptyStr();
    
    var acap = GetAcapData(_AdmOprID, depoadmo.rec.OperKind);

    if (acap != null)
      var isCurator   = IIF(DEPOACAPROLE_CURATOR == acap.Role, "X", " ");
      var isOperator  = IIF(DEPOACAPROLE_OPERATOR == acap.Role, "X", " ");
      var isManager   = IIF(DEPOACAPROLE_MANAGER == acap.Role, "X", " ");
      var isMortgagee = IIF(DEPOACAPROLE_MORTGAGEE == acap.Role, "X", " ");

      DP_AddPrintCell( RepAdm, "["+isManager+"] - РАСПОРЯДИТЕЛЬ СЧЕТА ДЕПО     ["+isOperator+"] - ОПЕРАТОР СЧЕТА ДЕПО     ["+isCurator+"] - ПОПЕЧИТЕЛЬ СЧЕТА ДЕПО ", TableWidthAdm, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();
      DP_AddPrintCell( RepAdm, "["+isMortgagee+"] - ЗАЛОГОДЕРЖАТЕЛЬ ", TableWidthAdm, 0, "l:" + RepFontStyleAdm, __is_app, REP_ELEM_STR );
      RepAdm.AddStr();

      RepAdm.AutoScan( "[\n" + Table3 + "]" );
      DP_AddPrintCell( RepAdm, "Ф.И.О./Полное наименование", 24, 0, "l:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, GetPartyName( acap.PartyID, true ), 70, 0, "l:" + RepFontStyleAdm );
      RepAdm.AddStr();

      party.PartyID = acap.PartyID;
      if(GetEQ(party))
        if(party.LegalForm==PTLEGF_PERSN) // физлицо
          GetPersonIdc(acap.PartyID, persnidc);
          paper.PaperKind = persnidc.PaperKind;
          if( getEQ(paper) )
            passp_str = paper.Name + ", "; 
          end;
          passp_str = passp_str + "серия: " + persnidc.PaperSeries + " №: " + persnidc.PaperNumber +
                      " выдан: " + date_as_string(1,persnidc.PaperIssuedDate) +
                      " " + persnidc.PaperIssuer;
          ogrn_str = "";
        else
          passp_str = "";
          ogrn_str = ПолучитьКодСубъекта(acap.PartyID, PTCK_OGRN);
        end;
        if(party.NRCountry != "")
          country.CodeLat3 = party.NRCountry;
          if( getGE(country) AND country.CodeLat3 == party.NRCountry )
            Name_Country = trim(country.Name);
          else
            Name_Country = "";
          end;
        else
          Name_Country = "Россия";
        end;
      else
        MsgBox("Не найден субъект с ID = " + acap.PartyID);
      end;
  
      DP_AddPrintCell( RepAdm, "Паспорт", 24, 0, "l:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, passp_str, 70, 0, "l:" + RepFontStyleAdm );
      RepAdm.AddStr();

      DP_AddPrintCell( RepAdm, "ОГРН/ Рег.номер", 24, 0, "l:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, ogrn_str, 70, 0, "l:" + RepFontStyleAdm );
      RepAdm.AddStr();

      DP_AddPrintCell( RepAdm, "Место регистрации", 24, 0, "l:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, GetAddress(party, PTADDR_LEGAL), 70, 0, "l:" + RepFontStyleAdm );
      RepAdm.AddStr();
      
      DP_AddPrintCell( RepAdm, "Юрисдикция/ Гражданство", 24, 0, "l:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, Name_Country, 70, 0, "l:" + RepFontStyleAdm );
      RepAdm.AddStr();
      
      DP_AddPrintCell( RepAdm, "Место нахождения", 24, 0, "l:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, GetAddress(party, PTADDR_REAL), 70, 0, "l:" + RepFontStyleAdm );
      RepAdm.AddStr();

      DP_AddPrintCell( RepAdm, "Почтовый адрес", 24, 0, "l:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, GetAddress(party, PTADDR_POST), 70, 0, "l:" + RepFontStyleAdm );
      RepAdm.AddStr();

      DP_AddPrintCell( RepAdm, "Дополнительная информация", 24, 0, "l:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, "", 70, 0, "l:" + RepFontStyleAdm );
      RepAdm.AddStr();


      RepAdm.AutoScan( "[\n]" );
      DP_AddPrintCell( RepAdm, "№ поручения", 24, 0, "l:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, ground.rec.Xld, 34, 0, "l:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, "дата поручения", 20, 0, "r:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, string(ground.rec.RegistrDate:f) + " г.", 14, 0, "l:" + RepFontStyleAdm );
      RepAdm.AddStr();
      DP_AddPrintCell( RepAdm, "№ операции", 24, 0, "l:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, depoadmo.rec.OprXid, 34, 0, "l:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, "дата операции", 20, 0, "r:" + RepFontStyleAdm );
      DP_AddPrintCell( RepAdm, string(depoadmo.rec.OperDate:f) + " г.", 14, 0, "l:" + RepFontStyleAdm );
      RepAdm.AddStr();
      RepAdm.AddEmptyStr();

    else
      MsgBox("Не определено уполномоченное лицо");
    end;

    DP_StdAuthPerson_Ex(RepAdm, RepFontStyleAdm);
    DP_StdDeposInfo_Ex(RepAdm, RepFontStyleAdm);
    DP_StdExecReport_Ex(RepAdm, RepFontStyleAdm);
  end;
  RepAdm.AddEmptyStr();
end;

macro GetSQLStr()
  var query = "";
  var invcard = TBFILE("invcard.dbt");

  query =   " select t_Ground, t_AdmoprID from ddepoadmo_dbt where  t_Ground in " 
          + " ( select t_SpgroundID from dspground_dbt where "
          + "     t_Direction    = " + ENTERING
          + " and t_Division     = " + SelfDivision;


  if((date_begin > date(0,0,0)) and (date_end > date(0,0,0)))
    query = query + " and t_RegistrDate >= " + GetSQLDate(date_begin)
                  + " and t_RegistrDate <= " + GetSQLDate(date_end);
  end;
      /* проверка номера поручения */
  if ( xld_end != "" )
    query = query + " and t_Xld <= '" + xld_end + "'";
      end;        
    
  if ( xld_begin != "" )
    query = query + " and t_Xld >= '" + xld_begin + "'";
      end;        
   
  if ( GrDoc > 0 )
    query = query + " and t_Kind = " + GrDoc;
      end;
  
  query = query + " ) ";

  if(_AdmoID > 0)
    query = query + " and t_AdmoprID IN (" + _AdmoID + ") ";
  end;
  
  query = query + " and  (t_Item, t_OperKind) in " 
                  "      (select t_Item, t_OperKind from ddpadmopr_dbt where t_Item > 0";

   /* проверка вида операции */
  if ( opkind > 0 )
      query = query + " and  t_OperType = "+opkind+" ";
  end;

    query = query + " ) ";
 
  if((opstatus_begin > date(0,0,0)) and (opstatus_end > date(0,0,0)))
    query = query + " and  t_OperDate >= "+GetSQLDate(opstatus_begin)+" ";
    query = query + " and  t_OperDate <= "+GetSQLDate(opstatus_end)+" ";
  end;

  if ( Party != -1 )
    query = query + " and  t_PartyID = "+Party+" ";
  end;

  if ( DepoAcc > 0 )
    query = query + " and  t_DepoAcc = "+DepoAcc+" ";
  end;

  /* проверка раздела */
  if ( DepoPart > 0 )
    query = query + " and  t_DepoPart = "+DepoPart+" ";
  end;

  /* проверка л/с */
  if ( Daccount != "" )
    query = query + " and  t_Account = '"+DAccount+"' ";
  end;

  /* проверка вида ц/б */
  if ( FIID != -1 )
    query = query + " and  t_FIID = "+FIID+" ";
  end;

  if ( InvCardID )
    invcard.rec.InvCardID = InvCardID;
    if (NOT invcard.GetEQ())
      msgbox("Не найдена ИК с ID = " + InvCardID);
    else
      query = query + " and  t_CertifID = "+InvCardID+" ";
    end;

  end;

  /* проверка № ОЖД */
  if ((ojd_begin != "") AND (ojd_end != ""))
    query = query + " and  t_OprXid >= '"+ojd_begin+"' ";
    query = query + " and  t_OprXid <= '"+ojd_end+"' ";
  end;

  query = query + " and  t_Department = " + Department;   

  return query;
end;

private macro CreateRep()
  DP_BeginProtocol();  //начать протоколирование - подмена MsgBox
 
  if( (xld_begin != xld_end) or (xld_begin == "") or (xld_end == "") )
    PrintSubNum = true;
  end;
  
  var stat = true, cur_stat = true;
  var query;
  var DEPOADMO;
  var count = 0;
  var Progress = CProgressBar;

  query = GetSQLStr();
 
  DEPOADMO = TRsbDataSet( query, RSDVAL_CLIENT, RSDVAL_STATIC);
  DEPOADMO.MoveLast();
  count = DEPOADMO.GetRecCount();
  DEPOADMO = TRsbDataSet( query );

  Progress.Init(count, "Формирование отчета", "ОТЧЕТ ОБ ИСПОЛНЕНИИ АДМИНИСТРАТИВНОЙ ОПЕРАЦИИ");
  while( DEPOADMO.MoveNext() )

        PrintOrderAdmForm(DEPOADMO.Ground, DEPOADMO.AdmoprID, date_begin, date_end);
        iCountRep = Progress.Use();

  end; /* while */
  Progress.Remove();
 
  if (isPrint == false)
    count = 0;
  end;

  DP_AddProtocol(RepAdm, "protocol");  //добавим протокол к уже сформированному отчету
  DP_EndProtocol();                    //закончить протоколирование

  return count;
end;


macro Order_ADM(_GrDoc:integer,
                _date_begin:date,
                _date_end:date,
                _xld_begin:string,
                _xld_end:string,
                _opkind:integer,
                _opstatus:integer,
                _opstatus_begin:date,
                _opstatus_end:date,
                _ojd_begin:string,
                _ojd_end:string,
                _Department:string,
                _Party:integer,
                _DepoAcc:integer,
                _DepoPart:integer,
                _DAccount:string,
                _FIID:integer,
                _InvCardID:integer,
                _LogOprID:integer,
                _ToExcel:bool,
                _is_app:bool,
                _Rep:@variant,
                AdmoID:integer)

  var count;

  if((ValType(_Rep) != V_UNDEF) and (_Rep != null))
    RepAdm = _Rep;
  end;

  /* эти переменные уже определены в макросе order_inf => их переопределять не надо */
  GrDoc = _GrDoc;
  xld_begin  = _xld_begin;
  xld_end    = _xld_end;
  date_begin = _date_begin;
  date_end = _date_end;
  opstatus_begin = _opstatus_begin;
  opstatus_end   = _opstatus_end;
  ojd_begin = _ojd_begin;
  ojd_end = _ojd_end;
  opstatus = _opstatus;
  opkind = _opkind;
  Department = _Department;
  Party = _Party;
  DepoAcc = _DepoAcc; 
  DepoPart = _DepoPart;
  DAccount = _DAccount;
  FIID = _FIID;
  InvCardID = _InvCardID;
  LOID = _LogOprID;
  __is_app = _is_app;
  ToExcel = _ToExcel;
  _AdmoID = AdmoID;

  count = CreateRep();

  _Rep = RepAdm;

  return count;
end;


macro Order_ADMbyAdmOprID( AdmoID, RecipientID, RepNum, RepDate, RepTime, RepOper, _Rep:@variant)
   
  if((ValType(_Rep) != V_UNDEF) and (_Rep != null))
    RepAdm = _Rep;
  end;

  GrDoc = 0;
  xld_begin  = "";
  xld_end    = "";
  date_begin = date(0,0,0);
  date_end = date(0,0,0);
  opstatus_begin = date(0,0,0);
  opstatus_end   = date(0,0,0);
  ojd_begin = "";
  ojd_end = "";
  opstatus = -1;
  opkind = 0;
  Department = {operdprt};
  Party = -1;
  DepoAcc = 0; 
  DepoPart = 0;
  DAccount = "";
  FIID = -1;
  InvCardID = 0;
  LOID = 0;
  __is_app = true;

  _AdmoID = AdmoID;
  _AdmRepRecipientID = RecipientID;
  _RepNum =  RepNum;
  _RepDate = RepDate;
  _RepTime = RepTime;
  _RepOper = RepOper;
    
  CreateRep();

  _Rep = RepAdm;                                 

end;


macro Order_ADMbyStrAdmoprID(_DisplayRep)
  var err = 0;
  var cmd, DS;

  cmd = DL_RSDCommand(  " SELECT rep.t_SourceDocID, rep.t_Recipient, spgr.t_Xld, spgr.t_RegistrDate, spgr.t_RegistrTime, spgr.t_Receptionist "
                      + "   FROM ddpgrrep_dbt rep, ddprepdoc_dbt repdoc, dspground_dbt spgr "
                      + "  WHERE rep.t_SourceDocKind = 825 "
                      + "    AND rep.t_SourceDocID IN (SELECT admotmp.t_AdmOprID FROM ddepoadmoclob_tmp admotmp)"
                      + "    AND repdoc.t_GrRepID = rep.t_ID "
                      + "    AND spgr.t_SPgroundID = repdoc.t_RepSPgroundID "
                     );

  if(cmd.GetCount() > 0)
    GetRegistryValue("DEPO\\DEPOREPTOEXCEL", V_BOOL, ToExcel, err);
    if( err != 0 )
      ToExcel = false;
    end;

    DS = cmd.Execute();
    while(DS.moveNext())
      //для каждого получателя печатаем отчет
      Order_ADMbyAdmOprID( DS.SourceDocID, DS.Recipient, DS.Xld, date(DS.RegistrDate), time(DS.RegistrTime), DS.Receptionist, RepAdm);
    end;

    if( (ToExcel) and (_DisplayRep == true))
      RepAdm.PrintWinRep();
      RepAdm.ShowWinRep();
    else
      RepAdm.PrintRep();
    end;

  end;
end;
