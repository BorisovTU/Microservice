/*                           
$Name:             sirnsdXML.mac
$Module:           Депозитарий
$Description:      Импорт справочников ценных бумаг НРД
*/
import dl_xml, sirnsdObjects;

private const BONDS_FILENAME  = "bonds.xml",
              SHARES_FILENAME = "shares.xml",
              UNITS_NAMEFILE  = "units.xml",
              DEPOS_NAMEFILE  = "dep_receipts.xml",
              ORGS_FILENAME   = "companies.xml";

// **********************************
// Базовый класс чтения XML
// **********************************
class (DL_XML)CSIRNSD_XML
   
   private macro GetFileName()
      return "";
   end;

   macro load(fileName) : bool
      var stat = true; // загрузки
      var sirnsdPath = "", err=0;
      GetRegistryValue( "SECUR\\SIR-NSD-PATH", V_STRING, sirnsdPath, err);

      if (err != 0)
         msgbox("Ошибка при определении настройки SECUR\\SIR-NSD-PATH");
         sirnsdPath = ".\\Import";
      end;                 
      m_FileName = sirnsdPath + "\\" + fileName;
      m_xml = CreateXMLObj();

      if (m_xml != null)
         stat = m_xml.load(m_FileName);
      else
         stat = false;
      end;
      return stat;
   end;
   // сконвертить в дату (2014-04-03T00:00:00)
   private macro CnvToDate(str:string):date

      var YYYY:integer, MM:integer, DD:integer;

      YYYY = int(substr(str, 1, 4));
      MM   = int(substr(str, 6, 2));
      DD   = int(substr(str, 9, 2));

      return date(DD, MM, YYYY);
   end;
   // Сконвертировать в bool
   private macro CnvToBool(str:string):bool
      return ("true" == StrLwr(str));
   end;

   macro InsertCacheToTable(data:object, nameObject:string):bool
      var stat = true;     
      // вставка данных во временную таблицу
      Message("Вставка " + nameObject + " во временную таблицу");
      stat = data.insertCache();

      if (not stat)
         msgbox("Ошибка при вставке " + nameObject + " во временную таблицу");
      end;
      Message("");
   end;

   initDL_XML;
end;

// **********************************
// Базовый класс для бумаг
// **********************************
class (CSIRNSD_XML)CAvoirSIRNSD_XML(action_)
   var data:TAvoirData;
   var action = action_; // действия над записью

   // получение данных по дополнительным кодам
   macro readCodes(node:object)
      var nodeCode = null;
      var sizeAddCode = 0;
      var nodeName = "";

      for(var i, 0, node.length-1)
         if ("code" == StrLwr(node.item(i).nodeName))
            nodeCode = node.item(i).childNodes;
            sizeAddCode = data.addCodes.size;

            data.addCodes[sizeAddCode] = TAddCode();
            for(var j, 0, nodeCode.length-1)
               nodeName = StrLwr(nodeCode.item(j).nodeName);
               if ("code_type_mn" == nodeName)
                  data.addCodes[sizeAddCode].code_type_mn = nodeCode.item(j).nodeTypedValue;
               elif ("code" == nodeName)
                  data.addCodes[sizeAddCode].code = nodeCode.item(j).nodeTypedValue;
               end;               
            end;
         end;
      end;
   end;
   // чтение основных данных
   macro readNode(node)
      var NodeName = StrLwr(node.nodeName);
      if ("code_nsd" == nodeName)
         data.code_nsd = node.nodeTypedValue;
         return true;
      elif ("isin" == nodeName)
         data.isin = node.nodeTypedValue;
         return true;
      elif ("cfi" == nodeName)
         data.cfi = node.nodeTypedValue;
         return true;
      elif ("sec_type_br_code" == nodeName)
         data.sec_type_br_code = node.nodeTypedValue;
         return true;
      elif ("state_reg_number" == nodeName)
         data.state_reg_number = node.nodeTypedValue;
         if (data.state_reg_number == "не предусмотрен")
            data.state_reg_number = "";
         end;
         return true;
      elif ("state_reg_date" == nodeName)
         data.state_reg_date = CnvToDate(node.nodeTypedValue);
         return true;
      elif ("sec_state_id" == nodeName)
         data.sec_state_id = node.nodeTypedValue;
         return true;
      elif ("registrar" == nodeName)
         data.registrar = node.nodeTypedValue;
         return true;
      elif ("micex_list" == nodeName)
         data.micex_list = node.nodeTypedValue;
      elif ("for_qualified_investors" == nodeName)
         data.for_qualified_investors = CnvToBool(node.nodeTypedValue);
         return true;
      elif ("codes" == nodeName)
         readCodes(node.childNodes);
         return true;
      end;
      return false;
   end;

   initCSIRNSD_XML;
end;

// **********************************
// Чтение файла облигаций
// **********************************
class (CAvoirSIRNSD_XML)CBondsSIRNSD_XML(action_, isCoupon_, isPartial_)
   var isCoupon = isCoupon_, 
       isPartial = isPartial_;

   // чтение блока купонов
   macro readCoupons(node:object)
      var nodeCoupon = null;
      var sizeCoupon = 0;
      var nodeName = "";
      for(var i, 0, node.length-1)
         if ("coupon" == StrLwr(node.item(i).nodeName))
            nodeCoupon = node.item(i).childNodes;
            sizeCoupon = data.couponsData.size;
            
            data.couponsData[sizeCoupon] = TCouponData();
            for(var j, 0, nodeCoupon.length-1)
               nodeName = StrLwr(nodeCoupon.item(j).nodeName);
               
               if ("payment_date_calc" == nodeName)
                  data.couponsData[sizeCoupon].payment_date_calc = CnvToDate(nodeCoupon.item(j).nodeTypedValue);
               elif ("payment_date_fact" == nodeName)
                  data.couponsData[sizeCoupon].payment_date_fact = CnvToDate(nodeCoupon.item(j).nodeTypedValue);
               elif ("record_date_calc" == nodeName)
                  data.couponsData[sizeCoupon].record_date_calc = CnvToDate(nodeCoupon.item(j).nodeTypedValue);
               elif ("interest_rate" == nodeName)
                  data.couponsData[sizeCoupon].interest_rate = nodeCoupon.item(j).nodeTypedValue;
               elif ("size" == nodeName)
                  data.couponsData[sizeCoupon].size = nodeCoupon.item(j).nodeTypedValue;
               elif ("lock_date_calc" == nodeName)
                  data.couponsData[sizeCoupon].lock_date_calc = CnvToDate(nodeCoupon.item(j).nodeTypedValue);
               elif ("coupon_number" == nodeName)
                  data.couponsData[sizeCoupon].coupon_number = nodeCoupon.item(j).nodeTypedValue;
               elif ("period_from_calc" == nodeName)
                  data.couponsData[sizeCoupon].period_from_calc = CnvToDate(nodeCoupon.item(j).nodeTypedValue);
               end;               
            end;
         end;
      end;
   end;
   // чтение блока ЧП
   macro readPartials(node)
      var nodePart = null;
      var sizePart = 0;
      var nodeName = "";

      for(var i, 0, node.length-1)
         if ("repayment" == StrLwr(node.item(i).nodeName))
            nodePart = node.item(i).childNodes;
            sizePart = data.partsData.size;

            data.partsData[sizePart] = TPartialData();
            for(var j, 0, nodePart.length-1)
               nodeName = StrLwr(nodePart.item(j).nodeName);

               if ("pp_number" == nodeName)
                  data.partsData[sizePart].pp_number = nodePart.item(j).nodeTypedValue;
               elif ("action_type_code" == nodeName)
                  data.partsData[sizePart].action_type_code = nodePart.item(j).nodeTypedValue;
               elif ("payment_size_percent" == nodeName)
                  data.partsData[sizePart].payment_size_percent = nodePart.item(j).nodeTypedValue;
               elif ("payment_date_calc" == nodeName)
                  data.partsData[sizePart].payment_date_calc = CnvToDate(nodePart.item(j).nodeTypedValue);
               elif ("payment_date_fact" == nodeName)
                  data.partsData[sizePart].payment_date_fact = CnvToDate(nodePart.item(j).nodeTypedValue);
               elif ("record_date_calc" == nodeName)
                  data.partsData[sizePart].record_date_calc = CnvToDate(nodePart.item(j).nodeTypedValue);
               end;               
            end;
         end;
      end;
   end;
   // чтение блока "Календарь номиналов"
   macro readFaceValues(node)
      var nodeFV = null;
      var sizeFV = 0;
      var nodeName = "";
      
      for(var i, 0, node.length-1)
         if ("pv_item" == StrLwr(node.item(i).nodeName))
            nodeFV = node.item(i).childNodes;
            sizeFV = data.facevaluesData.size;

            data.facevaluesData[sizeFV] = TFaceValueData();
            for(var j, 0, nodeFV.length-1)
               nodeName = StrLwr(nodeFV.item(j).nodeName);

               if ("par_value_date" == nodeName)
                  data.facevaluesData[sizeFV].facevalue_date = CnvToDate(nodeFV.item(j).nodeTypedValue);
               elif ("par_value" == nodeName)
                  data.facevaluesData[sizeFV].facevalue = nodeFV.item(j).nodeTypedValue;
               end;               
            end;
            data.indexnom = true;
         end;
      end;
   end;
   // чтение блока Облигация
   macro readBondNode(node:object)
      var i = 0;
      var nodeName = "";

      for(i, 0, node.length-1)
         nodeName = StrLwr(node.item(i).nodeName);
         if (("coupons" == nodeName) and isCoupon)
            readCoupons(node.item(i).childNodes);
         elif (("repayments" == nodeName) and isPartial)
            readPartials(node.item(i).childNodes);
         elif ("par_values" == nodeName)
            readFaceValues(node.item(i).childNodes);
         elif (not readNode(node.item(i))) // основные данные не прочитаны
            if ("issuer" == nodeName)
               data.issuer = node.item(i).nodeTypedValue;
            elif ("sec_type_id" == nodeName)
               data.sec_type_id = node.item(i).nodeTypedValue;
            elif ("sec_form_id" == nodeName)
               data.sec_form_id = node.item(i).nodeTypedValue;
            elif ("issue_name_full" == nodeName)
               data.name_full = node.item(i).nodeTypedValue;
            elif ("issue_number" == nodeName)
               data.issue_number = node.item(i).nodeTypedValue;
            elif ("start_placement_date" == nodeName)
               data.start_placement_date = CnvToDate(node.item(i).nodeTypedValue);
            elif ("end_placement_date" == nodeName)
               data.end_placement_date = CnvToDate(node.item(i).nodeTypedValue);
            elif ("issued_size" == nodeName)
               data.issued_size = node.item(i).nodeTypedValue;
            elif ("face_value" == nodeName)
               data.face_value = node.item(i).nodeTypedValue;
            elif ("maturity_value" == nodeName)
               data.maturity_value = node.item(i).nodeTypedValue;
            elif ("currency_code" == nodeName)
               data.currency_code = node.item(i).nodeTypedValue;
            elif ("expiry_date_calc" == nodeName)
               data.expiry_date_calc = CnvToDate(node.item(i).nodeTypedValue);
            elif ("first_coupon_date" == nodeName)
               data.first_coupon_date = CnvToDate(node.item(i).nodeTypedValue);
            elif ("coupon_period_base_id" == nodeName)
               data.coupon_period_base_id = node.item(i).nodeTypedValue;
            elif ("bond_series" == nodeName)
               data.bond_series = node.item(i).nodeTypedValue;
            elif ("rate_type_id" == nodeName)
               data.rate_type_id = node.item(i).nodeTypedValue;
            end;
         end;
      end;
   end;
   
   macro readData()
      var node;
      if(this.load(BONDS_FILENAME))
         node = m_xml.DocumentElement;
         InitProgress( node.childNodes.length, "Чтение "+BONDS_FILENAME+" и идентификация облигаций", "Идентификация облигаций" );
         for(var i, 0, node.childNodes.length-1)
            if ("bond" == StrLwr(node.childNodes.item(i).nodename))
               readBondNode(node.childNodes.item(i).childNodes);

               action.IdentObjects(data);

               data.clear();
            end;
            UseProgress(i+1);
         end;
         RemProgress();
         // вставка данных во временную таблицу
         this.InsertCacheToTable(data, "облигаций");
      else
         msgbox("Ошибка при открытии файла " + BONDS_FILENAME);
      end;
   end;

   initCAvoirSIRNSD_XML(action_);
   data = TBondData();
end;

// **********************************
// Чтение файла Акций
// **********************************
class (CAvoirSIRNSD_XML)CSharesSIRNSD_XML(action_)

   // чтение блока Акции
   macro readShareNode(node:object)
      var i = 0;
      var nodeName = "";
      for(i, 0, node.length-1)
         nodeName = StrLwr(node.item(i).nodeName);
         if (not readNode(node.item(i))) // основные данные не прочитаны
            if ("issuer" == nodeName)
               data.issuer = node.item(i).nodeTypedValue;
            elif ("share_category_id" == nodeName)
               data.share_category_id = node.item(i).nodeTypedValue;
            elif ("sec_type_id" == nodeName)
               data.sec_type_id = node.item(i).nodeTypedValue;
            elif ("issue_name_full" == nodeName)
               data.name_full = node.item(i).nodeTypedValue;
            elif ("issue_number" == nodeName)
               data.issue_number = node.item(i).nodeTypedValue;
            elif ("face_value" == nodeName)
               data.face_value = node.item(i).nodeTypedValue;
            elif ("currency_code" == nodeName)
               data.currency_code = node.item(i).nodeTypedValue;
            elif ("issue_size_planned" == nodeName)
               data.issue_size_planned = node.item(i).nodeTypedValue;
            elif ("issued_size" == nodeName)
               data.issued_size = node.item(i).nodeTypedValue;
            end;
         end;
      end;
   end;
   
   macro readData()
      var node;

      if(this.load(SHARES_FILENAME))
         node = m_xml.DocumentElement;

         InitProgress( node.childNodes.length, "Чтение "+SHARES_FILENAME+" и идентификация акций", "Идентификация акций" );
         for(var i, 0, node.childNodes.length-1)
            if ("share" == StrLwr(node.childNodes.item(i).nodename))
               readShareNode(node.childNodes.item(i).childNodes);

               action.IdentObjects(data);

               data.clear();
            end;
            UseProgress(i+1);
         end;
         // вставка данных во временную таблицу
         this.InsertCacheToTable(data, "акций");
         RemProgress();
      else
         msgbox("Ошибка при открытии файла " + SHARES_FILENAME);
      end;
   end;

   initCAvoirSIRNSD_XML(action_);
   data = TShareData();
end;

// **********************************
// Чтение файла Инвестиционные паи
// **********************************
class (CAvoirSIRNSD_XML)CUnitsSIRNSD_XML(action_)

   // чтение блока Пай
   macro readUnitNode(node:object)
      var i = 0;
      var nodeName = "";

      for(i, 0, node.length-1)
         nodeName = StrLwr(node.item(i).nodeName);
         if (not readNode(node.item(i))) // основные данные не прочитаны
            if ("fund" == nodeName)
               data.fund = node.item(i).nodeTypedValue;
            elif ("managing" == nodeName)
               data.issuer = node.item(i).nodeTypedValue;
            elif ("unit_name_full" == nodeName)
               data.name_full = node.item(i).nodeTypedValue;
            elif ("state_reg_number_rule" == nodeName)
               data.state_reg_number = node.item(i).nodeTypedValue;
            elif ("state_reg_date_rule" == nodeName)
               data.state_reg_date = CnvToDate(node.item(i).nodeTypedValue);
            elif ("fraction" == nodeName)
               data.fraction = node.item(i).nodeTypedValue;
//            elif ("" == nodeName)
//               data. = node.item(i).nodeTypedValue;
            end;
         end;
      end;
   end;
   
   macro readData()
      var node;

      if(this.load(UNITS_NAMEFILE))
         node = m_xml.DocumentElement;

         InitProgress( node.childNodes.length, "Чтение "+UNITS_NAMEFILE+" и идентификация инвестиционных паев", "Идентификация инвестиционных паев" );
         for(var i, 0, node.childNodes.length-1)
            if ("unit" == StrLwr(node.childNodes.item(i).nodename))
               readUnitNode(node.childNodes.item(i).childNodes);

               action.IdentObjects(data);

               data.clear();
            end;
            UseProgress(i+1);
         end;
         RemProgress();
         // вставка данных во временную таблицу
         this.InsertCacheToTable(data, "инвестиционных паёв");
      else
         msgbox("Ошибка при открытии файла " + UNITS_NAMEFILE);
      end;
   end;

   initCAvoirSIRNSD_XML(action_);
   data = TUnitData();
end;

// **********************************
// Чтение файла Депозитраные расписки
// **********************************
class (CAvoirSIRNSD_XML)CDeposSIRNSD_XML(action_)

   // чтение блока расписка
   macro readDepReceiptNode(node:object)
      var i = 0;
      var nodeName = "";

      for(i, 0, node.length-1)
         if (not readNode(node.item(i))) // основные данные не прочитаны
            
            nodeName = StrLwr(node.item(i).nodeName);
            if ("dr_issuer" == nodeName)
               data.issuer = node.item(i).nodeTypedValue;
            elif ("dr_category_mn" == nodeName)
               data.dr_category_mn = node.item(i).nodeTypedValue;
            elif ("sec_form_id" == nodeName)
               data.sec_form_id = node.item(i).nodeTypedValue;
            elif ("dr_name_full" == nodeName)
               data.name_full = node.item(i).nodeTypedValue;
            elif ("repres_fi_isin" == nodeName)
               data.repres_fi_isin = node.item(i).nodeTypedValue;
            elif ("issuer_base_sec" == nodeName)
               data.issuer_base_sec = node.item(i).nodeTypedValue;
            elif ("ratio_numerator" == nodeName)
               data.ratio_numerator = node.item(i).nodeTypedValue;
            elif ("ratio_denominator" == nodeName)
               data.ratio_denominator = node.item(i).nodeTypedValue;
            end;
         end;
      end;
   end;
   
   macro readData()
      var node;

      if(this.load(DEPOS_NAMEFILE))
         node = m_xml.DocumentElement;
         InitProgress( node.childNodes.length, "Чтение "+DEPOS_NAMEFILE+" и идентификация депозитарных расписок", "Идентификация депозитарных расписок" );
         for(var i, 0, node.childNodes.length-1)
            if ("dep_receipt" == StrLwr(node.childNodes.item(i).nodename))
               readDepReceiptNode(node.childNodes.item(i).childNodes);

               action.IdentObjects(data);

               data.clear();
            end;
            UseProgress(i+1);
         end;
         RemProgress();
         // вставка данных во временную таблицу
         this.InsertCacheToTable(data, "депозитарных расписок");
      else
         msgbox("Ошибка при открытии файла " + DEPOS_NAMEFILE);
      end;
   end;

   initCAvoirSIRNSD_XML(action_);
   data = TDepoData();
end;

// **********************************
// Чтение файла организаций
// **********************************
class (CSIRNSD_XML)COrganization_XML(action_)
   var data:TOrganizationData = TOrganizationData();
   var action = action_; // действия над записью

   // Сконвертировать CHAR в bool
   private macro CnvCharToBool(str:string):bool
      return ("y" == StrLwr(str));
   end;


   // получение данных по блоку "Виды деятельности по лицензии"
   macro readLicences(node:object)
      var nodeLic = null;
      var sizeLicData = 0;
      var nodeName = "";

      for(var i, 0, node.length-1)
         if ("licence" == StrLwr(node.item(i).nodeName))
            nodeLic = node.item(i).childNodes;
            sizeLicData = data.licData.size;

            data.licData[sizeLicData] = TLicenceData();
            for(var j, 0, nodeLic.length-1)
               nodeName = StrLwr(nodeLic.item(j).nodeName);

               if ("licence_num" == nodeName)
                  data.licData[sizeLicData].num = nodeLic.item(j).nodeTypedValue;
               elif ("licence_activity_id" == nodeName)
                  data.licData[sizeLicData].activity_id = nodeLic.item(j).nodeTypedValue;
               elif ("licence_date" == nodeName)
                  data.licData[sizeLicData].dateStart = CnvToDate(nodeLic.item(j).nodeTypedValue);
               end;               
            end;
         end;
      end;
   end;

   macro readCompanyNode(node:object)
      var i = 0;
      var nodeName = "";
      for(i, 0, node.length-1)
         nodeName = StrLwr(node.item(i).nodeName);
         if ("cmp_code_nsd" == nodeName)
            data.cmp_code_nsd = node.item(i).nodeTypedValue;
         elif ("full_name" == nodeName)
            data.full_name = node.item(i).nodeTypedValue;
         elif ("short_name" == nodeName)
            data.short_name = node.item(i).nodeTypedValue;
         elif ("common_name_full_en" == nodeName)
            data.common_name_full_en = node.item(i).nodeTypedValue;
         elif ("full_name_en" == nodeName)
            data.full_name_en = node.item(i).nodeTypedValue;
         elif ("inn" == nodeName)
            data.inn = node.item(i).nodeTypedValue;
         elif ("comp_type_id" == nodeName)
            data.comp_type_id = node.item(i).nodeTypedValue;
         elif ("cmp_code_fcsm" == nodeName)
            data.cmp_code_fcsm = node.item(i).nodeTypedValue;
         elif ("kpp" == nodeName)
            data.kpp = node.item(i).nodeTypedValue;
         elif ("credit_cmp" == nodeName)
            data.credit_cmp = CnvCharToBool(node.item(i).nodeTypedValue);
         elif ("is_bank_4_non_resident" == nodeName)
            data.is_bank_4_non_resident = CnvCharToBool(node.item(i).nodeTypedValue);
         elif ("okpo" == nodeName)
            data.okpo = node.item(i).nodeTypedValue;
         elif ("bik" == nodeName)
            data.bik = node.item(i).nodeTypedValue;
         elif ("state_reg_num" == nodeName)
            data.state_reg_num = node.item(i).nodeTypedValue;
         elif ("state_reg_date" == nodeName)
            data.state_reg_date = CnvToDate(node.item(i).nodeTypedValue);
         elif ("ogrn" == nodeName)
            data.ogrn = node.item(i).nodeTypedValue;
         elif ("egrul_date" == nodeName)
            data.egrul_date = CnvToDate(node.item(i).nodeTypedValue);
         elif ("egrul_organ_name" == nodeName)
            data.egrul_organ_name = node.item(i).nodeTypedValue;
         elif ("pif_type_mn" == nodeName)
            data.pif_type_mn = node.item(i).nodeTypedValue;
         elif ("country" == nodeName)
            data.country = node.item(i).nodeTypedValue;
         elif ("fio_head" == nodeName)
            data.fio_head = node.item(i).nodeTypedValue;
         elif ("address" == nodeName)
            data.address = node.item(i).nodeTypedValue;
         elif ("post_address" == nodeName)
            data.post_address = node.item(i).nodeTypedValue;
         elif ("phone" == nodeName)
            data.phone = node.item(i).nodeTypedValue;
         elif ("fax" == nodeName)
            data.fax = node.item(i).nodeTypedValue;
         elif ("e_mail" == nodeName)
            data.e_mail = node.item(i).nodeTypedValue;
         elif ("licences" == nodeName)
            readLicences(node.item(i).childNodes);
         end;
      end;
   end;

   macro readData()
      var node;

      if(this.load(ORGS_FILENAME))
         node = m_xml.DocumentElement;
         InitProgress( node.childNodes.length, "Чтение "+ORGS_FILENAME+" и идентификация организаций", "Идентификация организаций" );
         for(var i, 0, node.childNodes.length-1)
            if ("company" == StrLwr(node.childNodes.item(i).nodename))
               readCompanyNode(node.childNodes.item(i).childNodes);

               action.IdentObjects(data);

               data.clear();
            end;
            UseProgress(i+1);
         end;
         RemProgress();
         // вставка данных во временную таблицу
         this.InsertCacheToTable(data, "организаций");
      else
         msgbox("Ошибка при открытии файла " + ORGS_FILENAME);
      end;
   end;

   initCSIRNSD_XML;
end;
