/* -@H------------------------------------------------------------------
*            Автоматизированная банковская система RS-Bank
*                 Библиотека интерпретируемых модулей
*                  Copyright (c) JV R-Style 1993-96
*
*   Description      :  Книжный перевод -- блокировка "На проверке"
*
*----------------------------------------------------------------------*/

import oprinter, SPInter, PaymInter, "dps_com.inc";

file pmpaym("pmpaym") key 10;

/* добавление суммы ПЗО */
macro AddBasObj( DealSum, PaymentID )
   record  rBaseSum( "sfbassum.str" );

   rBaseSum.baseType    = SF_BASETYPE_SUM;
   rBaseSum.baseType2   = SF_BASETYPE_SUM;
   rBaseSum.baseSum     = DealSum;
   rBaseSum.baseSum2    = DealSum;

   rBaseSum.BaseObjectType = OBJTYPE_OPERATIONDEPO;
   rBaseSum.BaseObjectID   = GetSpdraftByPayment(PaymentID);

   if( InsertSumList(rBaseSum) )
      MsgBox("Ошибка при вставке базовой суммы");
      return false;
   end;

   return true;
end;

macro InInterDepo(depoac_buf, ДатаНачала, ДатаКонца)
   record fininstr("fininstr");
   record avoiriss("avoiriss");
   file depoacnt(depoacnt);
   var stat=0, DealSum=$0, RetSum, valRest;
   /*Поручение на книжный перевод эмиссионных ценных бумаг*/
   pmpaym.DocKind     = SP_DEPOPER_BOOKORDER;
   pmpaym.ValueDate   = ДатаНачала;
   pmpaym.Purpose     = PurpBase;
   pmpaym.SubPurpose  = 0;
   stat = GetGE(pmpaym);
   while(  stat and
          (pmpaym.DocKind   == SP_DEPOPER_BOOKORDER) and
          (pmpaym.ValueDate <= ДатаКонца)
        )
      if(pmpaym.PaymStatus == PM_FINISHED)
         if(pmpaym.Purpose == PurpBase)
           depoacnt.AutoKey = pmpaym.PayerDpNode;
           if( (GetEQ(depoacnt)) AND (depoacnt.Kind == 1) /* Счет Активный */
               AND ( (depoacnt.AvoirStatus == BlockCheck) OR (depoacnt.AvoirStatus == BlockOnReregistration) ) )
             depoacnt.AutoKey = pmpaym.ReceiverDpNode;
             if( (GetEQ(depoacnt)) AND (depoacnt.Kind == 1) ) /* Счет Активный */
               if( ПолучитьФинИн( pmpaym.FIID, fininstr, avoiriss ) == 0 )
                 if( pmpaym.IndoorStorage ) /* Если закрытое хранение */
                   if( CalcPaymentAvoirIndoor( depoac_buf, pmpaym, valRest ) )
                     DealSum = DealSum + valRest;
                     AddBasObj( valRest, pmpaym.PaymentID );
                   else
                     return $0; 
                   end;
                 else

                   if( CalcPaymentAvoir( depoac_buf, pmpaym, RetSum ) )
                     if( TranferToRUR(RetSum, pmpaym.ValueDate, fininstr, avoiriss, valRest ) )
                       DealSum = DealSum + valRest;
                       AddBasObj( valRest, pmpaym.PaymentID );
                     else
                       return $0;
                     end;
                   else
                     return $0; 
                   end;
                 end;
               else
                 MsgBox("Не найдена ценная бумага c FIID = ", pmpaym.FIID);
                 return $0;
               end;
             end;
           end;
         end;
      end;
      stat = Next(pmpaym);
   end;

   return DealSum;
end;


macro CalcServiceSum( sfcontr_addr, BeginDate, EndDate )
    record  rContr( sfcontr );
    file depoacnt(depoacnt) key 1;
    var  AinPEnabled;

    if( GetAinPAccState( AinPEnabled ) )
      if( AinPEnabled ) /* Если включен актив в пассиве, эта комиссия может работать */
        SetBuff( rContr, sfcontr_addr );
        depoacnt.Code = rContr.Object; 
        if(not GetEQ(depoacnt))
           MsgBox("Не найден счет депо ", rContr.Object);
           MacroError = 1;
           return $0;
        else
           InInterDepo(depoacnt, BeginDate, EndDate);
        end;
        return; 
      else
        MacroError = 1;
        msgbox("Учет актива в пассиве не велся");
      end;
    else
      MacroError = 1;
    end;
end;
