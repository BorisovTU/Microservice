/*
$Name: spdobook.mac
$Module: Депозитарий
$Description: Инициализация и проверки инвентарного поручения ДЕПО
*/

import BankInter, DPInter, SPInter, DealsInter, Payments, Globals, "dlcnst.inc", OprInter, RsbDataSet, opr_depo, "dlquery.mac", "dpsteprep.mac";

record          Spdraft( spdraft );
record          Spground( spground );
record          Spdrmove( spdrmove );
record          Pmpaym ( pmpaym );
record          Pmprop ( pmprop );
record          Pmrmprop( pmrmprop );

record          OldSpdraft( spdraft );
record          OldSpground( spground );
record          OldSpdrmove( spdrmove );
record          OldPmpaym ( pmpaym );
record          OldPmprop ( pmprop );
record          OldPmrmprop( pmrmprop );

private const SC_SPDRAFT_STATUS_PREP  = 1;  /* отложенное */
private const SC_SPDRAFT_STATUS_OPEN  = 2;  /* открытое   */
private const SC_SPDRAFT_STATUS_CLOSE = 3;  /* закрытое   */

private const DP_INFREPORT_PREPARE = 300; //отчет об исполнении инв. операции
private const DP_INFREPORT_REJECT  = 301; //уведомление об отказе в исполнении

private const REFOBJ_ORD_ENTERING_OP = 2; // Номер входящего документа для операций с ц/б
private const REFOBJ_INFOPR          = 1; // ObjectType = 105; номер депозитарного отчета (ценные бумаги)

const DP_SCAN_FILE_NAME = "dpscandraftid";

/* Операции со счетом */
const OPER_ALL     = 0, /* Любые */
      OPER_RECEIPT = 1, /* Зачисление */
      OPER_WITHDR  = 2; /* Списание */

const OBJTYPE_KIND_BLOCK = 1202;
const OBJTYPE_TRANSACTION = 1200;

macro ВыбратьВидДокумента( Kind : Integer /*вид документа скроллинга*/ )
  var Kind_Operation = 0;

  if( not DP_SelectKindOper(Kind, 
                            SP_DEPOPER_BOOKORDER /*SP_DEPOPER_UNIVOPERATION*/, 
                            SP_DEPOPER_WITHDRORDER, 
                            Kind_Operation,
                            true /*не показывать листалку в случае наличия только одной операции по документу*/) )
    Kind_Operation = -1;
  end;

  /* если вернуть -1, то создание нового документа будет отменено */
  /* если вернуть 0, то будет вызвана листалка выбора вида операции в программе */
  return Kind_Operation;
end;


private macro    Новый_Документ( )
  
  if((Spdraft.Kind == SP_DEPOPER_RECORDER) OR (Spdraft.Kind == SP_DEPOPER_WITHDRORDER))
    Pmpaym.IndoorStorage = 1; // Закрытое хранение
  end;

  return 0;
end;


macro Открытие_документа()

///Функция BlockDobookField( Номер_Поля ) блокирует поле на панели для редактирования
// её единственный параметр Номер_Поля - номер поля на панели, которое нужно заблокировать
// нумерция полей происходит слева-направо сверху-вниз.

// доступны глобальные рекорды
//   Spdraft  
//   Spground
//   Spdrmove
//   Pmpaym   
//   Pmprop   
//   Pmrmprop

   
/* Пример использования
  //Для поручения Зачисления Бездокументарных ц/б, импортированного из БО, заблокировать поле Сумма 

  //1.Проверим, что данное поручение является импортированным
  //Для этого найдем проверим, есть ли среди документов операций документ нашего поручения
  var DocID = 0;
  var oprdocs = TBFile("oprdocs.dbt");

  DocID = L_Z(Spdraft.AutoKey, 10); //дополняем нулями до 10 символов
  oprdocs.rec.DocKind = DLDOC_INSSPDRAFT; //118 - отложенное поручение депо
  oprdocs.rec.DocumentID = DocID;
  oprdocs.rec.Origin = 0;
  if( oprdocs.GetEQ() )
    if( Spdraft.Kind == SP_DEPOPER_ENROLMENT )
      BlockDobookField(22);
    end;
  end;
*/

  return 0;
end;


/*
Получение значения настройки "Контроль содержания транзакций в инвентарных операциях"
*/
macro GetControlTrnOper( ControlTrnOper )
  var err;
  var ControlTrnOperTmp;

  GetRegistryValue( "DEPO\\CONTROLTRNOPER", V_BOOL, ControlTrnOperTmp, err );
  if( NOT err )
    SetParm( 0, ControlTrnOperTmp );
  else
    SetParm( 0, "" );
    msgbox("Ошибка поиска настройки \"Контроль содержания транзакций в инвентарных операциях\"");
  end;

  return err;
end;

/*
  проверить заполненность полей ФИ
*/
private macro CheckFin()
  var stat = true;

  /* если установлен признак экспорта в БОЦБ, то */
  /*  необходимо проверить наличие заданности полей "ценная бумага" и "пост. выпуск" */
  if( Spdraft.ToBO == "X" )
    if( Spdraft.GeneralizedFIID == -1 )
      msgbox("Для поручения, отправляемого в БОЦБ, необходимо указать ценную бумагу");
      stat = false;
    elif( Pmpaym.FIID == -1 )
      msgbox("Для поручения, отправляемого в БОЦБ, необходимо указать поставляемый выпуск");
      stat = false;
    end;
  end;

  return stat;
end;

private macro    Проверить_Документ( Режим )
  var stat = 0, ControlTrnOper;
  var DocID = 0;
  var oprdocs = TBFile("oprdocs.dbt");

/////////////////////////////////////////////////////////////////////////
//Данные поля пока используются только внижеописанном примере   
  Array Text;
  Array Button;
  Button(0) = "Продолжить";
  Button(1) = "Не продолжать";
  Button(2) = "Восстановить";
  var but, ind = 0;
  var fininstr = TBFile("fininstr.dbt");
  var depoacnt = TBFile("depoacnt.dbt");
  var oldFi_Code = "", newFi_Code = "";
  var oldBriefCode = "", newBriefCode = "";
/////////////////////////////////////////////////////////////////////////
      
         if(Режим == 3)  /* РЕДАКТИРОВАНИЕ ДОКУМЕНТА */
           if( not CheckFin() )
             stat = 1;
           end;

         /*При редактировании производим проверку важности внесенных изменений */
         /* Константы важности внесенных изменений:           */
         /* CHANG_NOTIMPORTANT   - изменения неважные (сохранение без отката операции)*/
         /* CHANG_IMPORTANT      - изменения важны (сохранение изменений возможно только при откате) */
         /* CHANG_NOTKEEP        - не сохранять изменения */
         /* Если возвращаемое значение  > 0, то это оно интерпритируется как номер поля с ошибочным параметром*/
         /* Если возвращаемое значение  = 0, то cчитается, что проверка прошла успешно
            и сохранение изменений можно производить без отката операции      */
            if(  (Spdraft.Status != SC_SPDRAFT_STATUS_PREP ) AND
                 ((Pmpaym.Amount != OldPmpaym.Amount) or
                 (Pmpaym.PayerAccount != OldPmpaym.PayerAccount) or
                 (Pmpaym.ReceiverAccount != OldPmpaym.ReceiverAccount) or
                 ((Pmpaym.FIID     != OldPmpaym.FIID) and (OldPmpaym.FIID > 0) ) or
                 ((Pmpaym.PayerDpNode != OldPmpaym.PayerDpNode ) and (OldPmpaym.PayerDpNode > 0)) or
                 ((Pmpaym.ReceiverDpNode != OldPmpaym.ReceiverDpNode ) and (OldPmpaym.ReceiverDpNode > 0)) or
                 (Pmprop.CodeKind != OldPmprop.CodeKind) or
                 (Pmprop.BankCode != OldPmprop.BankCode) or
                 (Pmpaym.IndoorStorage != OldPmpaym.IndoorStorage) or
                 (Pmpaym.PayerDpBlock != OldPmpaym.PayerDpBlock ) or
                 (Pmpaym.ReceiverDpBlock != OldPmpaym.ReceiverDpBlock ) or
                 ((Spdraft.OperState != DP_INVSTATUSORDER_REJECT) AND 
                  (NOT GetControlTrnOper( ControlTrnOper )) and 
                  ( ControlTrnOper ) and 
                  ( Spdraft.Product != OldSpdraft.Product ) 
                 ))
              ) stat = CHANG_IMPORTANT;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/* Данный пример демонстрирует обработку поручения, импортированного из БО, перед сохранением, если произведены важные изменения


            elif( (Spdraft.Status == SC_SPDRAFT_STATUS_PREP)  )
              //для импортированных поручений нужно проверить наличие важных изменений при сохранении поручения.
              //если таковые есть - выдаем предупреждающее сообщение, но сохранять не запрещаем

              //1.Проверим, что данное поручение является импортированным
              //Для этого найдем проверим, есть ли среди документов операций документ нашего поручения
              DocID = L_Z(Spdraft.AutoKey, 10); //дополняем нулями до 10 символов
              oprdocs.rec.DocKind = DLDOC_INSSPDRAFT; //118 - отложенное поручение депо
              oprdocs.rec.DocumentID = DocID;
              oprdocs.rec.Origin = 0;
              if( oprdocs.GetEQ() )
                //нашли! значит это импортированное поручение
                //сравним, были ли произведены важные изменения
                if(((Pmpaym.Amount != OldPmpaym.Amount) or
                    (Pmpaym.PayerAccount != OldPmpaym.PayerAccount) or
                    (Pmpaym.ReceiverAccount != OldPmpaym.ReceiverAccount) or
                    (Pmpaym.FIID     != OldPmpaym.FIID ) or
                    (Pmpaym.PayerDpNode != OldPmpaym.PayerDpNode ) or
                    (Pmpaym.ReceiverDpNode != OldPmpaym.ReceiverDpNode ) or
                    (Pmprop.CodeKind != OldPmprop.CodeKind) or
                    (Pmprop.BankCode != OldPmprop.BankCode) or
                    (Pmpaym.IndoorStorage != OldPmpaym.IndoorStorage) or
                    (Pmpaym.PayerDpBlock != OldPmpaym.PayerDpBlock ) or
                    (Pmpaym.ReceiverDpBlock != OldPmpaym.ReceiverDpBlock ))
                  )
                  Text(ind) = "В поручении, импортированном из другого БО, были произведены важные изменения:|";

                  //построим информативную строку сообщения
                  if( Pmpaym.Amount != OldPmpaym.Amount )
                    Text(ind) = Text(ind) + "количество ц/б изменено с " + string(OldPmpaym.Amount) + " на " + string(Pmpaym.Amount) + "|";
                  end;

                  if( Pmpaym.PayerAccount != OldPmpaym.PayerAccount )
                    Text(ind) = Text(ind) + "счет плательщика изменен с " + string(OldPmpaym.PayerAccount) + " на " + string(Pmpaym.PayerAccount) + "|";
                  end;

                  if( Pmpaym.ReceiverAccount != OldPmpaym.ReceiverAccount )
                    Text(ind) = Text(ind) + "счет получателя изменен с " + string(OldPmpaym.ReceiverAccount) + " на " + string(Pmpaym.ReceiverAccount) + "|";
                  end;

                  if( Pmpaym.FIID != OldPmpaym.FIID )
                    fininstr.rec.FIID = OldPmpaym.FIID;
                    if( fininstr.GetEQ() ) oldFi_Code = fininstr.rec.FI_Code; end;
                    fininstr.rec.FIID = Pmpaym.FIID;
                    if( fininstr.GetEQ() ) newFi_Code = fininstr.rec.FI_Code; end;
                    Text(ind) = Text(ind) + "ценная бумага изменена с " + string(oldFi_Code) + " на " + string(newFi_Code) + "|";
                  end;

                  if( Pmpaym.PayerDpNode != OldPmpaym.PayerDpNode )
                    depoacnt.rec.AutoKey = OldPmpaym.PayerDpNode;
                    if( depoacnt.GetEQ() ) oldBriefCode = depoacnt.rec.BriefCode; end;
                    depoacnt.rec.AutoKey = Pmpaym.PayerDpNode;
                    if( depoacnt.GetEQ() ) newBriefCode = depoacnt.rec.BriefCode; end;
                    Text(ind) = Text(ind) + "счет плательщика в нашем банке изменен с " + string(oldBriefCode) + " на " + string(newBriefCode) + "|";
                  end;

                  if( Pmpaym.ReceiverDpNode != OldPmpaym.ReceiverDpNode )
                    depoacnt.rec.AutoKey = OldPmpaym.ReceiverDpNode;
                    if( depoacnt.GetEQ() ) oldBriefCode = depoacnt.rec.BriefCode; end;
                    depoacnt.rec.AutoKey = Pmpaym.ReceiverDpNode;
                    if( depoacnt.GetEQ() ) newBriefCode = depoacnt.rec.BriefCode; end;
                    Text(ind) = Text(ind) + "счет получателя в нашем банке изменен с " + string(oldBriefCode) + " на " + string(newBriefCode) + "|";
                  end;

                  if( Pmprop.CodeKind != OldPmprop.CodeKind )
                    Text(ind) = Text(ind) + "вид кода изменен с " + string(OldPmprop.CodeKind) + " на " + string(Pmprop.CodeKind) + "|";
                  end;

                  if( Pmprop.BankCode != OldPmprop.BankCode )
                    Text(ind) = Text(ind) + "код банка изменен с " + string(OldPmprop.BankCode) + " на " + string(Pmprop.BankCode) + "|";
                  end;

                  if( Pmpaym.IndoorStorage != OldPmpaym.IndoorStorage )
                    Text(ind) = Text(ind) + "признак закрытого хранения изменен " + "|";
                  end;

                  if( Pmpaym.PayerDpBlock != OldPmpaym.PayerDpBlock )
                    Text(ind) = Text(ind) + "блокировка счета плательщика изменена с " + string(OldPmpaym.PayerDpBlock) + " на " + string(Pmpaym.PayerDpBlock) + "|";
                  end;

                  if( Pmpaym.ReceiverDpBlock != OldPmpaym.ReceiverDpBlock )
                    Text(ind) = Text(ind) + "блокировка счета получателя изменена с " + string(OldPmpaym.ReceiverDpBlock) + " на " + string(Pmpaym.ReceiverDpBlock) + "|";
                  end;

                  but = ConfWin(Text,Button);
                  if( but == 1 ) // отказаться от сохранения, но значения не восстанавливаем, а просто остаемся в панеле
                    stat = CHANG_NOTKEEP;
                  elif( but == 2) // восстановить значения
                    Pmpaym.Amount          = OldPmpaym.Amount;
                    Pmpaym.PayerAccount    = OldPmpaym.PayerAccount;
                    Pmpaym.ReceiverAccount = OldPmpaym.ReceiverAccount;
                    Pmpaym.FIID            = OldPmpaym.FIID;
                    Pmpaym.PayerDpNode     = OldPmpaym.PayerDpNode;
                    Pmpaym.ReceiverDpNode  = OldPmpaym.ReceiverDpNode;
                    Pmprop.CodeKind        = OldPmprop.CodeKind;
                    Pmprop.BankCode        = OldPmprop.BankCode;
                    Pmpaym.IndoorStorage   = OldPmpaym.IndoorStorage;
                    Pmpaym.PayerDpBlock    = OldPmpaym.PayerDpBlock;
                    Pmpaym.ReceiverDpBlock = OldPmpaym.ReceiverDpBlock;
                    stat = CHANG_NOTKEEP;
                  end;
                end;
              end;
*/
            end;

         elif(Режим == 2)  /* ВВОД ДОКУМЕНТА */

           if( not CheckFin() )
             stat = 1;
           end;

         elif(Режим == 1)  /* УДАЛЕНИЕ ДОКУМЕНТА */

         end;

  return stat;
end;


private macro MassCheckAllowedTransaction()
   var cmd;
   var query = "";

   query =
     "UPDATE DOPRTEMP_TMP temp SET temp.t_ErrorStatus = ?, temp.t_ErrorMessage = ? " +
     " WHERE temp.t_ErrorStatus = 0 "+
     "   and temp.t_SkipDocument = 0 " +
     "   and temp.t_DocKind IN ("+SP_DEPOPER_BOOKORDER+","+SP_DEPOPER_TRANSFERORDER+","+SP_DEPOPER_WITHDRORDER+")" +
     "   and Exists(select 1 from dspdraft_dbt dr, dspdrmove_dbt mv, dpmpaym_dbt pm " + 
     "               where dr.t_AutoKey = temp.t_OrderID " + 
     "                 and dr.t_Product > 0 " +
     "                 and dr.t_OperState != " + DP_INVSTATUSORDER_REJECT +
     "                 and mv.t_DraftID = dr.t_AutoKey " +
     "                 and pm.t_PaymentID = mv.t_PaymentID " +
     "                 and not Exists(select 1 " +
     "                                  from ddptrnopr_dbt tr " +
     "                                 where (   (tr.t_AvoirState = pm.t_PayerDpBlock and tr.t_Transaction = dr.t_Product) " +
     "                                        or (tr.t_AvoirState = pm.t_PayerDpBlock and tr.t_Transaction = -1) " +
     "                                        or (tr.t_AvoirState = -1 and tr.t_Transaction = dr.t_Product) " +
     "                                       ) " +
     "                                   and (tr.t_AllowOper = "+OPER_ALL+" or tr.t_AllowOper = "+OPER_WITHDR+")" +
     "                               ) " +
     "             )";

   cmd = RsdCommand(query);
   cmd.addParam( "", RSDBP_IN, 1 );
   cmd.addParam( "", RSDBP_IN, "Недопустимое содержание транзакции при заданной блокировке счета списания." );

   SQL_Execute( cmd, "Проверка поручений: содержание транзакции", true );

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
   query =
     "UPDATE DOPRTEMP_TMP temp SET temp.t_ErrorStatus = ?, temp.t_ErrorMessage = ? " +
     " WHERE temp.t_ErrorStatus = 0 "+
     "   and temp.t_SkipDocument = 0 " +
     "   and temp.t_DocKind IN ("+SP_DEPOPER_BOOKORDER+","+SP_DEPOPER_ENROLMENT+","+SP_DEPOPER_RECORDER+")" +
     "   and Exists(select 1 from dspdraft_dbt dr, dspdrmove_dbt mv, dpmpaym_dbt pm " + 
     "               where dr.t_AutoKey = temp.t_OrderID " + 
     "                 and dr.t_Product > 0 " +
     "                 and dr.t_OperState != " + DP_INVSTATUSORDER_REJECT +
     "                 and mv.t_DraftID = dr.t_AutoKey " +
     "                 and pm.t_PaymentID = mv.t_PaymentID " +
     "                 and not Exists(select 1 " +
     "                                  from ddptrnopr_dbt tr " +
     "                                 where (   (tr.t_AvoirState = pm.t_ReceiverDpBlock and tr.t_Transaction = dr.t_Product) " +
     "                                        or (tr.t_AvoirState = pm.t_ReceiverDpBlock and tr.t_Transaction = -1) " +
     "                                        or (tr.t_AvoirState = -1 and tr.t_Transaction = dr.t_Product) " +
     "                                       ) " +
     "                                   and (tr.t_AllowOper = "+OPER_ALL+" or tr.t_AllowOper = "+OPER_WITHDR+")" +
     "                               ) " +
     "             )";

   cmd = RsdCommand(query);
   cmd.addParam( "", RSDBP_IN, 1 );
   cmd.addParam( "", RSDBP_IN, "Недопустимое содержание транзакции при заданной блокировке счета зачисления." );

   SQL_Execute( cmd, "Проверка поручений: содержание транзакции", true );

   return 0;
end;

macro CheckAllowedTransaction(AvoirState, OperType, IsPayer, Acc)
  FILE dptr("dptrnopr.dbt");
  file llval("llvalues.dbt");
  file depoacnt("depoacnt.dbt");
  var blockName = "", blockCode = "";
  var transactionName = "";
  var BriefCode = "";
  var noneTrn = false;

  dptr.AvoirState  = AvoirState;
  dptr.Transaction = Spdraft.Product;
  if(not GetEQ(dptr))
    dptr.AvoirState = AvoirState;
    dptr.Transaction = -1;
    if(not GetEQ(dptr))
      dptr.AvoirState  = -1;
      dptr.Transaction = Spdraft.Product;
      if(not GetEQ(dptr))
        //наити наименование блокировки
        llval.List = OBJTYPE_KIND_BLOCK;
        llval.Element = AvoirState;
        if( GetEQ(llval) )
          blockName = llval.Name;
          blockCode = llval.Code;
        end;
        //наименование транзакции
        ClearRecord(llval);
        llval.List = OBJTYPE_TRANSACTION;
        llval.Element = Spdraft.Product; 
        if( GetEQ(llval) )
          transactionName = llval.Name;
        end;
        //счет депо
        depoacnt.AutoKey = Acc;
        if( GetEQ(depoacnt))
          BriefCode = depoacnt.BriefCode;
        end;
                                  
        if(IsPayer)
          msgbox("Недопустимое содержание транзакции \""+transactionName+"\" при заданной блокировке \""+blockCode+" - "+blockName+"\" счета списания \""+BriefCode+"\"");
        else
          msgbox("Недопустимое содержание транзакции \""+transactionName+"\" при заданной блокировке \""+blockCode+" - "+blockName+"\" счета зачисления \""+BriefCode+"\"");
        end;
        return 1;
      end;
    end;
  end;

  /* Проверка полученной записи с видом оерации */
  if( (dptr.AllowOper == OPER_ALL) OR (dptr.AllowOper == OperType) )
    /* Все ОК */
    return 0;
  else
    //наити наименование блокировки
    llval.List = OBJTYPE_KIND_BLOCK;
    llval.Element = AvoirState;
    if( GetEQ(llval) )
      blockName = llval.Name;
      blockCode = llval.Code;
    end;
    //наименование транзакции
    ClearRecord(llval);
    llval.List = OBJTYPE_TRANSACTION;
    llval.Element = Spdraft.Product; 
    if( GetEQ(llval) )
      transactionName = llval.Name;    
    end;
    //счет депо
    depoacnt.AutoKey = Acc;
    if( GetEQ(depoacnt))
      BriefCode = depoacnt.BriefCode;
    end;

    if(IsPayer)
      msgbox("Недопустимое содержание транзакции \""+transactionName+"\" при заданной блокировке \""+blockCode+" - "+blockName+"\" счета списания \""+BriefCode+"\"");
    else
      msgbox("Недопустимое содержание транзакции \""+transactionName+"\" при заданной блокировке \""+blockCode+" - "+blockName+"\" счета зачисления \""+BriefCode+"\"");
    end;
    return 1;
  end;
end;

private macro DepoDraftIsSummary(DraftID)
  var cmd, DataSet;

  cmd = DL_RSDCommand(  " select count(1) cnt " 
                      + "   from ddlgrdoc_dbt "
                      + "  where t_DocKind = " + DL_DEPODRAFT
                      + "    and t_DocID = ? "
                      + "    and T_SourceType = " + DLGR_SOURCETYPE_DEPOSUMMARY
                     );

  cmd.AddParam(DraftID);
  DataSet = cmd.Execute();
  if((DataSet.moveNext()) and (DataSet.cnt > 0))
    return true;
  end;

  return false;
end;

macro DP_CheckAmount( dpAmount:@variant, dlAmount:@variant )

  var query, query2;
  var d, d2;
  var stat = 0;

  dpAmount = 0.0;
  dlAmount = 0.0;

  if(DepoDraftIsSummary(Spdraft.AutoKey)) //для сводных не проверяем
    return 0;
  end;

  query2 =   " select NVL(sum(rq.t_Amount), 0) as dlAmount "
           + "   from ddlrq_dbt rq, dspdrprop_dbt sp "
           + "  where rq.t_ID = rsi_dlrq.RSI_GetRQByDepoGrDeal(sp.t_DocumentID, "+Pmpaym.BaseFIID+") " 
           + "    and sp.t_DraftID = " + Spdraft.AutoKey
           + "    and sp.t_DocumentKind = " + DL_DLGRDEAL;

  query =   " select dr.t_Kind_Operation, sum(pm.t_Amount) as dpAmount "
          + "   from (select distinct sp.t_DraftID "
          + "           from dspdrprop_dbt sp "
          + "          where sp.t_DocumentID = (select sp1.t_DocumentID from dspdrprop_dbt sp1 where sp1.t_DraftID = "+Spdraft.AutoKey+" and sp1.t_DocumentKind = "+DL_DLGRDEAL+") "
          + "            and sp.t_DocumentKind = " + DL_DLGRDEAL
          + "        ) d, dspdraft_dbt dr, dpmpaym_dbt pm, dspdrmove_dbt mv"
          + "  where dr.t_AutoKey = d.t_DraftID "
          + "    and mv.t_DraftID = dr.t_AutoKey "
          + "    and pm.t_PaymentID = mv.t_PaymentID "
          + "  group by dr.t_Kind_Operation ";

  d2 = TRsbDataSet(query2);
  if(d2.moveNext())
    dlAmount = d2.dlAmount;
  end;

  if( dlAmount != 0.0)
    d = TRsbDataSet(query);
    if(d.moveNext())
      dpAmount = d.dpAmount;
      if( dpAmount != dlAmount ) 
        stat = 1;
      end;

    end;
  end;

  return stat;
end;

macro DP_CheckDealAmount( dpAmount:@variant, dlAmount:@variant )

  var query, query2;
  var d, d2, securprop;
  var stat = 0;

  dpAmount = 0.0;
  dlAmount = 0.0;

  if(DepoDraftIsSummary(Spdraft.AutoKey)) //для сводных не проверяем
    return 0;
  end;
  
  securprop = TRsbDataSet(  " select sp.*, grdeal.t_DocKind "
                          + "   from dspdrprop_dbt sp, ddlgrdeal_dbt grdeal "
                          + "  where sp.t_DraftID = " + Spdraft.AutoKey
                          + "    and sp.t_DocumentKind = " + DL_DLGRDEAL
                          + "    and grdeal.t_ID = sp.t_DocumentID "
                         );
  if(securprop.moveNext())
    if( (securprop.DocKind == DL_SECURITYDOC) or (securprop.DocKind == DL_RETIREMENT) or (securprop.DocKind == DL_AVRWRT) or (securprop.DocKind == DL_CONVAVR) ) 
       query2 =   " select leg.t_TotalCost as dlAmount "
          + "   from ddlrq_dbt rq, dspdrprop_dbt sp, ddl_leg_dbt leg"
          + "  where rq.t_ID = rsi_dlrq.RSI_GetRQByDepoGrDeal(sp.t_DocumentID, "+Pmpaym.BaseFIID+") " 
          + "    and sp.t_DocumentKind = " + DL_DLGRDEAL
          + "    and sp.t_DraftID = " + Spdraft.AutoKey
          + "    and "
          + "        (    ( rq.t_DealPart = 2 and leg.t_LegKind = " + LEG_KIND_DL_TICK_BACK + " ) "
          + "          or ( rq.t_DealPart != 2 and leg.t_LegKind = " +LEG_KIND_DL_TICK+ " ) "
          + "        ) "
          + "    and leg.t_DealID = rq.t_DocID "
          + "    and leg.t_LegID = 0";
        d2 = TRsbDataSet(query2);
        if(d2.moveNext())                           
          dlAmount = d2.dlAmount;
        end;
    
    elif(securprop.DocKind == DL_NTGSEC) //неттинг
        dlAmount = 0.0;
    end;
  end;
  query =   " select dr.t_Kind_Operation, sum(dr.t_DealAmount) as dpAmount "
          + "   from (select distinct sp.t_DraftID "
          + "           from dspdrprop_dbt sp "
          + "          where sp.t_DocumentID = (select sp1.t_DocumentID from dspdrprop_dbt sp1 where sp1.t_DraftID = "+Spdraft.AutoKey+" and sp1.t_DocumentKind = "+DL_DLGRDEAL+") "
          + "            and sp.t_DocumentKind = " + DL_DLGRDEAL
          + "        ) d, dspdraft_dbt dr, dpmpaym_dbt pm, dspdrmove_dbt mv"
          + "  where dr.t_AutoKey = d.t_DraftID "
          + "    and mv.t_DraftID = dr.t_AutoKey "
          + "    and pm.t_PaymentID = mv.t_PaymentID "
          + "  group by dr.t_Kind_Operation ";
  

  if( dlAmount != 0.0)
    d = TRsbDataSet(query);
    while(d.moveNext())
      dpAmount = d.dpAmount;
      if( dpAmount != dlAmount ) 
        stat = 1;
      end;

    end;
  end;
  
  return stat;
end; 


//общая проверка на совпадение количесва ц/б и стоимости в импортированных поручениях и сделке
macro CheckDealAmountData()
                                                             
  var dpAmount = 0.0, dlAmount = 0.0;
  var stat = 0;

  if(Pmpaym.FIID != -1)
  
    if(DP_CheckAmount( @dpAmount, @dlAmount ))
      if( not GetTrue(true, "Количество ц/б ("+money(dpAmount)+") в импортированных поручениях не соответствуют количеству ("+money(dlAmount)+") в сделке. Продолжить?") )
        stat = 1;
      end;
    end;

    if(not stat)
      if(DP_CheckDealAmount( @dpAmount, @dlAmount ))
        if( not GetTrue(true, "Стоимость ц/б ("+money(dpAmount)+") в импортированных поручениях не соответствует стоимости ц/б ("+money(dlAmount)+") в сделке. Продолжить?") )
          stat = 1;
        end;
      end;
    end;
  end; 
  
  return stat;
end;                                                                              


private macro Проверить_Документ_В_Открытые()
  var stat = 0, ControlTrnOper;
  RECORD depoac( depoacnt );

  macro FindDEPOACNT( Autokey, Buff_DepoAcnt )
    file DepoAcnt( depoacnt );

    DepoAcnt.Autokey = Autokey;
    if( GetEQ(DepoAcnt) )
      Copy( Buff_DepoAcnt, DepoAcnt );
      return 0;
    else
      return 1;
    end;
  end;

  if((Spdraft.OperState != DP_INVSTATUSORDER_REJECT) AND (NOT GetControlTrnOper( ControlTrnOper )) and ( ControlTrnOper ))
    if(Spdraft.Product == 0) /* не задано содержание транзакции - нечего контролировать */
      msgbox("Не задано поле \"Содержание транзакции\"");
      stat = 1;
    end;
    if(NOT stat)
      if( Spdraft.Kind == SP_DEPOPER_BOOKORDER )
        if( FindDEPOACNT(Pmpaym.PayerDpNode, depoac) != 0 )
          msgbox("Не найден раздел счета депо отправителя с Autokey = ", Pmpaym.PayerDpNode );
          return 1;
        else
          if(depoac.Kind == 2)
            stat = CheckAllowedTransaction(Pmpaym.PayerDpBlock, OPER_WITHDR, true, Pmpaym.PayerDpNode);
          end;
        end;

        if(NOT stat)
          if( FindDEPOACNT(Pmpaym.ReceiverDpNode, depoac) != 0 )
            msgbox("Не найден раздел счета депо получателя с Autokey = ", Pmpaym.ReceiverDpNode );
            return 1;
          else
            if(depoac.Kind == 2)
              stat = CheckAllowedTransaction(Pmpaym.ReceiverDpBlock, OPER_RECEIPT, false, Pmpaym.ReceiverDpNode);
            end;
          end;
        end;
      elif( Spdraft.Kind == SP_DEPOPER_TRANSFERORDER)
        stat = CheckAllowedTransaction(Pmpaym.PayerDpBlock, OPER_WITHDR, true, Pmpaym.PayerDpNode);
      elif( Spdraft.Kind == SP_DEPOPER_ENROLMENT)
        stat = CheckAllowedTransaction(Pmpaym.ReceiverDpBlock, OPER_RECEIPT, false, Pmpaym.ReceiverDpNode);
      elif( Spdraft.Kind == SP_DEPOPER_RECORDER)
        stat = CheckAllowedTransaction(Pmpaym.ReceiverDpBlock, OPER_RECEIPT, false, Pmpaym.ReceiverDpNode);
      elif( Spdraft.Kind == SP_DEPOPER_WITHDRORDER)
        stat = CheckAllowedTransaction(Pmpaym.PayerDpBlock, OPER_WITHDR, true, Pmpaym.PayerDpNode);
      end;
    end;

    if(not stat)
      stat = CheckDealAmountData();
    end;
  end;

  return stat;
end;

private macro Проверить_Документ_В_Открытые_Масс()
  var stat = 0, ControlTrnOper;
  var cmd;
  var query = "";

  if((NOT GetControlTrnOper( ControlTrnOper )) and ( ControlTrnOper ))
    query =
      "UPDATE DOPRTEMP_TMP temp SET temp.t_ErrorStatus = ?, temp.t_ErrorMessage = ? " +
      " WHERE temp.t_ErrorStatus = 0 "+
      "   and temp.t_SkipDocument = 0 " +
      "   and Exists(select 1 from dspdraft_dbt dr " + 
      "               where dr.t_AutoKey = temp.t_OrderID " + 
      "                 and dr.t_Product = 0 " +
      "                 and dr.t_OperState != " + DP_INVSTATUSORDER_REJECT +
      "             )";

    cmd = RsdCommand(query);
    cmd.addParam( "", RSDBP_IN, 1 );
    cmd.addParam( "", RSDBP_IN, "Не задано поле \"Содержание транзакции\"" );

    SQL_Execute( cmd, "Проверка поручений: поле \"Содержание транзакции\"", true );


    MassCheckAllowedTransaction();
    
  end;

  return stat;
end;


macro    Проверить_Счет_В_Документе( поле ) /*0-счет плательщика, 1-счет получателя*/
        var новое_значение_счета;

        if(поле) новое_значение_счета = Pmpaym.ReceiverAccount;
        else новое_значение_счета = Pmpaym.PayerAccount;
        end;
        return новое_значение_счета;
end;

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string
  //  Возможные значения ScrolStatus:
  // -1, //незарегистрированные 
  // 0,  //все
  // 1,  //отложенные
  // 2,  //открытые
  // 3   //закрытые

  //  Возможные значения ScrolKind:
  // SP_DEPOPER_BOOKORDER     800, // "Универсальная депозитарная операция"
  // SP_DEPOPER_TRANSFERORDER 805, // "Поручение на междепозитарный перевод эмиссионных ценных бумаг"
  // SP_DEPOPER_ENROLMENT     810, // "Распоряжение на зачисление ц/б"
  // SP_DEPOPER_RECORDER      840, // "Поручение на прием на хранение документарных ц/б"
  // SP_DEPOPER_WITHDRORDER   845, // "Поручение на снятие с хранения документарных ц/б"
  // ALL_KIND_OF_DOC          0,   // Все


  //пример
  //return "/*+FIRST_ROWS LEADING(t) INDEX(t dspdraft_dbt_idx0)*/";

  return DefaultHint;

END;

private macro    Функция_Пользователя( )
        return 0;
end;


//вставить документ-приложение в инв.поручение в нижестоящий филиал
macro DP_InsertExtSpgrdocToSpdraft(GrReportID:integer,   //ИД исходящего документа 
                                   GrParentDrID:integer  //ИД исходящего документа инв. операции в нижестоящем филиале
                                  )
  file GrReport(spground);
  file GrParentDr(spground);
  record party("party");
  record SpgrPrm("spgroundprm.rec");
  var DataSet, query;
  var SPGroundId = 0;
  var stat = 0;

  ClearRecord(GrReport);
  ClearRecord(GrParentDr);

  GrReport.SPgroundID = GrReportID;
  if(GetEQ(GrReport))
    GrParentDr.SPgroundID = GrParentDrID;
    if(GetEQ(GrParentDr))

      query =   " select dr.t_OperState from dspdraft_dbt dr, ddpgrrep_dbt rep, ddprepdoc_dbt repdoc "
              + "  where repdoc.t_RepSPgroundID = " + GrReport.SPgroundID
              + "    and rep.t_ID = repdoc.t_GrRepID "
              + "    and dr.t_Kind = rep.t_SourceDocKind "
              + "    and dr.t_AutoKey = rep.t_SourceDocID ";
      DataSet = TRsbDataSet(query);
      if(DataSet.moveNext())
        
        SpgrPrm.Division           =  GrParentDr.Division;
        SpgrPrm.Direction          =  ENTERING;
        if(DataSet.OperState != DP_INVSTATUSORDER_REJECT)
          SpgrPrm.Kind             =  DP_INFREPORT_PREPARE;
        else
         SpgrPrm.Kind              =  DP_INFREPORT_REJECT;
        end;
        SpgrPrm.Xld                =  "";
        SpgrPrm.RegistrDate        =  date(0,0,0);
        SpgrPrm.RegistrTime        =  time(0);
        SpgrPrm.DocLog             =  GrReport.DocLog;
        SpgrPrm.PartyID            =  {OurBank};
        if( ПолучитьСубъекта(SpgrPrm.PartyID, party) ) 
          SpgrPrm.PartyName = party.Name;
          SpgrPrm.PartyCode = ПолучитьКодСубъекта(SpgrPrm.PartyID, PTCK_CONTR);
        end;
      
        SpgrPrm.AltXld             =  GrReport.Xld;
        SpgrPrm.SignedDate         =  GrReport.RegistrDate;
        SpgrPrm.SignedTime         =  GrReport.RegistrTime;
        SpgrPrm.Proxy              =  GrReport.Proxy;
        SpgrPrm.Receptionist       =  GrParentDr.Receptionist;
        SpgrPrm.DeliveryKind       =  "U";  //автоформирование
        SpgrPrm.BackOffice         =  GrParentDr.BackOffice;
        SpgrPrm.Comment            =  GrReport.Comment;
        SpgrPrm.BeginningDate      =  GrReport.BeginningDate;
        SpgrPrm.TerminateDate      =  GrReport.TerminateDate;
        SpgrPrm.Department         =  GrParentDr.Department;
        SpgrPrm.Branch             =  GrParentDr.Branch;
        SpgrPrm.Parent             =  GrReport.SPgroundID;
        SpgrPrm.PrimaryDocKind     =  GrParentDr.SourceDocKind;
        SpgrPrm.PrimaryDocID       =  GrParentDr.SourceDocID;
        SpgrPrm.Comment            =  GrReport.Comment;


        if( DP_InsertGroundDocument(SPGroundId, SpgrPrm) != 0)  
          stat = 1;
        end;
      else
        stat = 1;
      end;
    else
      stat = 1;
    end;
  else
    stat = 1;
  end;

  return stat;
end;

//экспорт отчета об исполнении в БО ЦБ
private macro CreateSecurDoc( spdraft, GrDealID, GrReportID)
   //подготовим данные для экспорта в БО ЦБ
   file party("party");
   file dl_tick("dl_tick");
   file dl_nett("dl_nett");
   file dl_comm("dl_comm");
   record spground("spgroundprm.rec");
   var RefRep = 0, RefGurDoc = 0, SPGroundId;
   var stat;
   var cmd, DataSet;
   ClearRecord(spground);

   //здесь уже должно быть известно, что сделка импортирована из БО ЦБ

   //определим вид первичного документа и еще раз проверим, что это поручение импортированио из ЦБ
   cmd = DL_RSDCommand(  " select grdeal.t_DocKind, grdeal.t_DocID "
                       + "   from ddlgrdeal_dbt, dspdrprop_dbt prop "
                       + "  where prop.t_DraftID = ? "
                       + "    and prop.t_DocumentID = ? "
                       + "    and prop.t_DocumentKind = " + DL_DLGRDEAL
                       + "    and grdeal.t_Id = prop.t_DocumentID " 
                      );
   cmd.AddParam(spdraft.AutoKey);
   cmd.AddParam(GrDealID);

   DataSet = cmd.Execute();
   if( DataSet.MoveNext())
     if( DataSet.DocKind == DL_NTGSEC )                    
       dl_nett.NettingID = DataSet.DocID;
       if( GetEQ(dl_nett) )
         spground.PrimaryDocID   = dl_nett.NettingID;
         spground.PrimaryDocKind = dl_nett.DocKind;
       else
         //так как известно, что сделка из БО ЦБ, то нужно выдать сообщение об ошибке
         MsgBox("Не найден паспорт сделки БО ЦБ");
         return false;//ошибка
       end;
     elif( DataSet.DocKind == DL_SETTLEMENT )
       dl_comm.DocumentID = DataSet.DocID;
       if( GetEQ(dl_comm) )
         spground.PrimaryDocID   = dl_comm.DocumentID;
         spground.PrimaryDocKind = dl_comm.DocKind;
       else
         //так как известно, что сделка из БО ЦБ, то нужно выдать сообщение об ошибке
         MsgBox("Не найден паспорт сделки БО ЦБ");
         return false;//ошибка
       end;
     else
       dl_tick.DealID = DataSet.DocID;
       if( GetEQ(dl_tick) )
         if(  ( dl_tick.BofficeKind == DL_SECURITYDOC ) or  //это все сделки ЦБ
              ( dl_tick.BofficeKind == DL_RETIREMENT )  or
              ( dl_tick.BofficeKind == DL_AVRWRT )      or
              ( dl_tick.BofficeKind == DL_CONVAVR )     or
              ( dl_tick.BofficeKind == DL_INVESTSHARE )     
           )
           spground.PrimaryDocID   = dl_tick.DealID;
           spground.PrimaryDocKind = dl_tick.BofficeKind;
         else
           return true;//значит не из ЦБ, поэтому выполнение прерывать нельзя
         end;
       else
         //так как известно, что сделка из БО ЦБ, то нужно выдать сообщение об ошибке
         MsgBox("Не найден паспорт сделки БО ЦБ");
         return false;//ошибка
       end;
     end;
   else
     //так как известно, что сделка из БО ЦБ, то нужно выдать сообщение об ошибке
     MsgBox("Невозможно определить параметры поручения депо");
     return false; //ошибка  
   end;

   spground.DocLog = LLCLASS_KINDDOC_SECUR; //журнал документов
   //посмотрим состояние поручения, т.е. его OperState
   if( spdraft.OperState == DP_INVSTATUSORDER_ACCEPT )//исполнено
      spground.Kind = DP_INFREPORT_PREPARE;//отчет об исполнении
   elif( spdraft.OperState == DP_INVSTATUSORDER_REJECT )//отказано в исполнении
      spground.Kind = DP_INFREPORT_REJECT;//уведеомление об отказе
   end;

   spground.Direction = ENTERING; //входящий документ-приложение
   spground.UserLog = SPGRUSERLOG_CLIENT;
   if(spdraft.Initiator == {OurBank})
     spground.UserLog = SPGRUSERLOG_OURENTER;
   end;

   //сгенерим референс для журнала вх/исх документов в БО ЦБ
   if ( GetReferenceIDByType(OBJTYPE_KINDDOC, REFOBJ_ORD_ENTERING_OP, RefGurDoc) )
     MsgBox( "Не найден описатель референса" );
     return false;
   end;
   if ( GenerateReference(RefGurDoc, spground.Xld) )
     MsgBox( "Ошибка при генерации референса по описателю " + string(RefGurDoc) );
     return false;
   end;

   spground.RegistrDate = spground.SignedDate = spdraft.Date;//дата валютирования
   spground.RegistrTime = spground.SignedTime = time(); //берем текущее время

   spground.PartyID = {OurBank}; //контрагент всегда наш банк
   //подкачать наименование контрагента
   if( ПолучитьСубъекта({OurBank}, party) )
     spground.PartyName = party.Name;
   else
     spground.PartyName = "";
   end;
   
   //сгенерим референс отчета об исполнении
   if ( GetReferenceIDByType(OBJTYPE_OPERATIONDEPO, REFOBJ_INFOPR, RefRep) )
     MsgBox( "Не найден описатель референса" );
     return false;
   end;

   if ( GenerateReference(RefRep, spground.AltXld) )
     MsgBox( "Ошибка при генерации референса по описателю " + string(RefRep) );
     return false;
   end;
   
   spground.Proxy          = -1;
   spground.Division       = SelfDivision;
   spground.Receptionist   = spdraft.Oper;
   spground.Copies         = 0;
   spground.Sent           = UNSET_CHAR;//неотправлен
   spground.DeliveryKind   =  CodeFor("N");//по электронной сети
   spground.BackOffice     = "Z";//депозитарий
   spground.Comment        = "";
   spground.DocTemplate    = 0;
   spground.BeginningDate  = date(0,0,0);
   spground.TerminateDate  = date(0,0,0);
   spground.Parent         = GrReportID;
  
   if( DP_InsertGroundDocument(SPGroundId, spground) == 0)  //непосредственно вставка в базу
     stat = true;
   else
     stat = false;
   end;
   if( stat == false )
     //откатим референсы
     RestoreReference(RefGurDoc, spground.Xld);
     RestoreReference(RefRep, spground.AltXld);
   end;

   return stat;
end;


//вставить документ-приложение в инв.поручение в нижестоящий филиал
macro DP_InsertExtSpgrdocToSecur(GrReportID:integer, //ИД исходящего документа инф. операции
                                 GrDraftID:integer   //ИД входящего документа инв. операции
                                )
  file GrDraft(spground);
  file spdraft(spdraft);
  file dl_tick("dl_tick");
  file dl_nett("dl_nett");
  file dl_comm("dl_comm");
  var KindLinkCustody;
  var cmd, DataSet;
  var stat = 0;

  GrDraft.SPgroundID = GrDraftID;
  if(GetEQ(GrDraft))
    spdraft.AutoKey = GrDraft.SourceDocID;
    if(GetEQ(spdraft))
      //если поручение импортировано из БО ЦБ
      // ( если что-то не нашли, то операцию не прерываем )
      //Вставка осуществляется если вид связи БО с депозитанием - двухсторонняя с контролем или двух-сторонняя c контролем и синхронизацией
      GetRegistryValue( "SECUR\\ВИД СВЯЗИ С ДЕПОЗИТАРИЕМ", V_INTEGER, KindLinkCustody, stat );
      if( (not stat) and ((KindLinkCustody == DL_KINDLINKCUSTODY_DOUBWITHCTRL) or (KindLinkCustody == DL_KINDLINKCUSTODY_DOUBWITHSYNCANDCTRL)) ) 
        cmd = DL_RSDCommand(  " select grdeal.t_DocKind, grdeal.t_DocID, grdeal.t_ID as GrDealID"
                            + "   from ddlgrdeal_dbt, dspdrprop_dbt prop "
                            + "  where prop.t_DraftID = ? "
                            + "    and prop.t_DocumentKind = " + DL_DLGRDEAL
                            + "    and grdeal.t_Id = prop.t_DocumentID " 
                           );
        cmd.AddParam(spdraft.AutoKey);

        DataSet = cmd.Execute();
        while(DataSet.moveNext())
          if( DataSet.DocKind == DL_NTGSEC )
            dl_nett.NettingID = DataSet.DocID;
            if( GetEQ(dl_nett) )
              //то нужно добавить документ-приложение для сделки
              if( not CreateSecurDoc(spdraft, DataSet.GrDealID, GrReportID) )
                stat = 1;
              end;
            end;
          elif( DataSet.DocKind == DL_SETTLEMENT )
            dl_comm.DocumentID = DataSet.DocID;
            if( GetEQ(dl_comm) )
              //то нужно добавить документ-приложение для сделки
              if( not CreateSecurDoc(spdraft, DataSet.GrDealID, GrReportID) )
                stat = 1;
              end;
            end;
          else
            dl_tick.DealID = DataSet.DocID;
            if( GetEQ(dl_tick) )
              if(  ( dl_tick.BofficeKind == DL_SECURITYDOC ) or  //это все сделки ЦБ
                   ( dl_tick.BofficeKind == DL_RETIREMENT )  or
                   ( dl_tick.BofficeKind == DL_AVRWRT )      or
                   ( dl_tick.BofficeKind == DL_CONVAVR )     or
                   ( dl_tick.BofficeKind == DL_INVESTSHARE )     
                )
                //то нужно добавить документ-приложение для сделки
                if( not CreateSecurDoc(spdraft, DataSet.GrDealID, GrReportID) )
                  stat = 1;
                end;
              end;
            end;
          end;
        end;
      end;
    end;
  end;

  return stat;
end;


private macro GetMsgLength(DraftID, ProcName,PaymKind)
  var query, Select, Stat;

  query = "declare v_Mes CLOB; begin\n ? := RSP_SECURITY." + ProcName + "(?, ?, v_Mes); ? := DBMS_LOB.GETLENGTH(v_Mes); \n end;";

  Select = RSDCommand( query );

  Select.AddParam("p_Stat", RSDBP_OUT, V_INTEGER );

  Select.AddParam("p_DraftKind", RSDBP_IN, PaymKind );
  Select.AddParam("p_DraftID", RSDBP_IN, DraftID );

  Select.AddParam("p_Len", RSDBP_OUT, V_INTEGER );

  Select.Execute();

  if(NOT SQL_ConvTypeInteger(Select.value("p_Stat")))
    return SQL_ConvTypeInteger(Select.value("p_Len"));
  else
    return 0;
  end;
end;


private macro GetMsgText(DraftID, ProcName, PaymKind)
  var query, Select, Stat, MesLength = GetMsgLength(DraftID, ProcName, PaymKind);

  if(MesLength)
    query = "begin\n ? := RSP_SECURITY." + ProcName + "(?, ?, ?); \n end;";

    Select = RSDCommand( query );

    Select.AddParam("p_Stat", RSDBP_OUT, V_INTEGER );

    Select.AddParam("p_DraftKind", RSDBP_IN, PaymKind );
    Select.AddParam("p_DraftID", RSDBP_IN, DraftID );

    Select.AddParam("p_Mes", RSDBP_OUT, V_STRING, MesLength );

    Select.Execute();

    if(NOT SQL_ConvTypeInteger(Select.value("p_Stat")))
      return SQL_ConvTypeStr(Select.value("p_Mes"));
    else
      return "";
    end;
  end;
  return "";
end;


macro PrintMessage(DraftID, IsConf)
  VAR ReportFileName, ProcName;

  ReportFileName = GetTxtFileName( "message");
  SetOutput( ReportFileName );

  if(IsConf)
    ProcName = "GetConfirmText";
  else
    ProcName = "GetMsgText";
  end;

  print(GetMsgText(DraftID, ProcName,RSPMDK_INVDRAFT));

  SetOutput( NULL, true );
  ViewFile( ReportFileName );
end;

macro PrintMessageUniv(DraftID, IsConf)
  VAR ReportFileName, ProcName, Text = "";
  ReportFileName = GetTxtFileName( "message");
  SetOutput( ReportFileName );

  if(IsConf)
    ProcName = "GetConfirmText";
  else
    ProcName = "GetMsgText";
  end;
    Text = Text + GetMsgText(DraftID, ProcName, 5);
    if(not (Text == ""))
      Text = Text + "\n\n";
    end;
    Text = Text + GetMsgText(DraftID, ProcName, 6);

  print(Text);

  SetOutput( NULL, true );
  ViewFile( ReportFileName );
end;


private macro DP_ClearScanFile()
  var ScanFileName = GetTxtFileName(DP_SCAN_FILE_NAME);

  if(ExistFile(ScanFileName))
    DelFile(ScanFileName);
  end;
end;


private macro DP_PrintMassRepByScanFile()
  var err;
  var ToExcel = false;
  var OldDialogFlag;
  var RepFileName;
  var ScanFileName = GetTxtFileName(DP_SCAN_FILE_NAME);
  file ScanFile() txt;
  var ThisRep = NULL;
  var isPrint = false;

  FILE spdraft_f(spdraft); 

  if(ExistFile(ScanFileName)) 
    if(open(ScanFile, ScanFileName))
      while(Next(ScanFile))
        spdraft_f.AutoKey = Int(ScanFile(0));
        if( GetEQ(spdraft_f) )
          DepoMemOrder(spdraft_f, @ThisRep, false);
          DraftRepPrintStep(spdraft_f.AutoKey, spdraft_f.Kind, @ThisRep, false);
          isPrint = true;
        end;
      end;
    end;
    if(isPrint)
      GetRegistryValue("DEPO\\DEPOREPTOEXCEL", V_BOOL, ToExcel, err);
      
      OldDialogFlag = SetDialogFlag(1);
      if( ToExcel )
        ThisRep.PrintWinRep();
        ThisRep.ShowWinRep();
      else
        RepFileName = GetTxtFileName("dpinfexec");
        ThisRep.SetFileNameTxt(RepFileName);
        ThisRep.PrintRep();
        ViewFile(RepFileName);
      end;
      SetDialogFlag(OldDialogFlag);
    end;
  end;
end;


// Массовая печать отчетов об исполнении Инв. Оп.
macro PrintMassReport( Step )
  if(Step == 0)
    DP_ClearScanFile();
  else
    DP_PrintMassRepByScanFile()
  end;
end;
