/*                           
$Name:             sirnsdActions.mac
$Module:           Депозитарий
$Description:      Импорт справочников ценных бумаг НРД             
                   Объекты действий
*/
import sirnsdReport;
// Действия

// ==============================
// Базовый класс действий
// ==============================
class CAction(parm_)
   var parm = parm_;
   // ------------------------------------
   // вставлять во временный файл?
   // Переопределять!
   // ------------------------------------
   private macro checkBeforeInsertToTMP(ID):bool
      // например, если ID задан и обновление, то true. Если ID не задан и обновление, то false, т.к. объект не идентифицирован в системе
      return false;
   end;

   macro GetQueryForFilter()
      return " 1 = 1 ";
   end;

   // ------------------------------------
   // идентификация объекта
   // ------------------------------------
   macro IdentObjects(data)
      // по умолчанию если объект найден
      var idObject = 0; // ИД не найдено

      idObject = data.findObject();// -1 - не найден, >= 0 - найден, -9999 - ошибка

      if (checkBeforeInsertToTMP(idObject))
         data.addRecordToCache();
      end;
   end;
   // ------------------------------------
   // поиск изменений
   // ------------------------------------
   macro SearchChanges(data):bool
      return false;
   end;
   
   // ------------------------------------
   // Действие над записью (обновить, добавить, вывести в протокол).
   // Перегружено!
   // ------------------------------------
   macro ActionOnData(data)
   end;

   // ------------------------------------
   // Вывод отчета о результате работы
   // ------------------------------------
   macro PrintReport()
      CReportSIRNSD(parm).Run();
   end;
   // ------------------------------------
   // Добавить в протокол, что запись обработана
   // ------------------------------------
   macro AddProtocolProcessedRec(data)
   end;
end;

// ==============================
// Действие Сравнить
// ==============================
class (CAction)CMergeAction(parm_)
   // вставлять во временный файл?
   private macro checkBeforeInsertToTMP(ID):bool
      return (ID>0); // если нет ошибки
   end;

   // поиск изменений
   macro SearchChanges(data):bool
      var stat = false;

      // если оставить data.IsChangesForUpd(), то не будут выводиться в протокол записи с одинаковыми значениями - могут быть вопросы, типа в сравнениии есть, а в отчете нет
      // проверим, что не надо вставлять
      if (not stat)
//         stat = data.IsChangesForUpd();
         stat = not data.IsChangesForAdd();
      end;
      
      return stat;
   end;
   
   // Действие над записью (обновить, добавить, вывести в протокол).
   macro ActionOnData(data)
      data.ActionOnMerge();
   end;
   initCAction(parm_);
end;
// ==============================
// Действие Обновить
// ==============================
class (CAction)CUpdateAction(parm_)
   // вставлять во временный файл?
   private macro checkBeforeInsertToTMP(ID):bool
      //return (ID > 0); // вставляем, если ИД положительный (объект есть в системе)
      return true; // вставляем все записи, отсечем ненужные полем State и SearchChanges
   end;

   macro GetQueryForFilter()
      return " t_IsLinkSec = CHR(88) ";
   end;

   // поиск изменений
   macro SearchChanges(data):bool
      return data.IsChangesForUpd();
   end;
   
   // Действие над записью (обновить, добавить, вывести в протокол).
   // Перегружено!
   macro ActionOnData(data)
      data.ActionOnRec();
   end;
   // ------------------------------------
   // Добавить в протокол, что запись обработана
   // ------------------------------------
   macro AddProtocolProcessedRec(data)
      data.SetProcessedRec();
   end;
   initCAction(parm_);
end;
// ==============================
// Действие Добавить
// ==============================
class (CAction)CAddAction(parm_)
   // вставлять во временный файл?
   private macro checkBeforeInsertToTMP(ID):bool
      //return (ID == 0); // вставляем, если ИД == 0 (объект отсутсвует в системе)
      return true; // вставляем все записи, отсечем ненужные полем State и SearchChanges
   end;
   macro GetQueryForFilter()
      return " t_IsLinkSec = CHR(0) ";
   end;

   // поиск изменений
   macro SearchChanges(data):bool
      return data.IsChangesForAdd();
   end;
   
   // Действие над записью (обновить, добавить, вывести в протокол).
   // Перегружено!
   macro ActionOnData(data)
      data.ActionOnRec();
   end;
   initCAction(parm_);
end;
// ==============================
// Действие Обновить+Добавить
// ==============================
class (CAction)CUpdateAddAction(parm_)
   // вставлять во временный файл?
   private macro checkBeforeInsertToTMP(ID):bool
      //return (ID>=0); // если нет ошибки
      return true; // вставляем все записи, отсечем ненужные полем State и SearchChanges
   end;

   // поиск изменений
   macro SearchChanges(data):bool
      var stat = false;

      if (not stat)
         stat = data.IsChangesForAdd();
      end;

      if (not stat)
         stat = data.IsChangesForUpd();
      end;

      return stat;
   end;
   
   // Действие над записью (обновить, добавить, вывести в протокол).
   // Перегружено!
   macro ActionOnData(data)
      data.ActionOnRec();
   end;
   // ------------------------------------
   // Добавить в протокол, что запись обработана
   // ------------------------------------
   macro AddProtocolProcessedRec(data)
      // Сработает для расшиернного протокола и если есть связь с Sec
      data.SetProcessedRec();
   end;
   initCAction(parm_);
end;
// ========================================
// Действие Распарсить (без идентификации)
// ========================================
class (CAction)CParseAction(parm_)
   // вставлять во временный файл?
   private macro checkBeforeInsertToTMP(ID):bool
      return true;
   end;

   // поиск изменений
   macro SearchChanges(data):bool
      return true;
   end;
   
   initCAction(parm_);
end;