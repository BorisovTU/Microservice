/*
$Name: nsdImpRests.mac
$Module: Депозитарий
$Description: Процедура сверки остатков на корсчетах на основании отчета НРД
*/

import BankInter, PTInter, CTInter, DpInter, rsexts;
import dlquery, DlReport_h;
import "dpimp.mac", secinter, globals, dprepsec, "rsberror.mac";
import "qracctools.mac";

private file reporth() dbf; // в класс не запихнуть
private file hdsttds() dbf;  // в класс не запихнуть
private file sttds() dbf;  // в класс не запихнуть

//создание локальной директории
   macro MoveNSDData()

      var TstPath = "$C:\\tmp\\depo\\nsd";
      var TxtPath = GetCurDir(false);
      var FName = "*.*";

      var tdl = tDirList;
      var td2 = tDirList(TxtPath + "\\NSD\\*.*","f");

        td2.Sort(0);
        var i = 0;
     
//          td2.Remove(TxtPath + "\\NSD\\*.*"); 
          tdl.Copy(TstPath+"\\*.*","", TxtPath + "\\NSD\\*.*",true,true,"Copy");
    end;


// Запись остатка ц/б
private class CNSDRest(DepoPartID, DepoAccNSDCode, DepoPartNSDCode, FIID, NSD_FICODE, LSIN, ISIN, Amount, FIName, SumPrecision, NsdAmount)
  if (ValType(NsdAmount)==V_UNDEF)
    NsdAmount=$0.0;
  end;
  var m_DepoPartID = DepoPartID; // ИД раздела счета депо
  var m_FIID = FIID; // ИД ФИ
  var m_LSIN = LSIN; // № гос. рег.
  var m_ISIN = ISIN; // ISIN
  var m_Amount = Amount; // Количество ц/б

  var m_DepoAccNSDCode = DepoAccNSDCode; // Код счета в НРД
  var m_DepoPartNSDCode = DepoPartNSDCode; // Код раздела в НРД
  var m_NSD_FICODE = NSD_FICODE; // Код ц/б в НРД
  var m_NSD_Amount = NsdAmount; // Остаток в НРД
  var m_NSD_FINAME = FIName;

  var m_SumPrecision = SumPrecision;
end;

// Запись ошибки
private class CNSDProtocolStr(DepoAcc, DepoPart, FI_Code, LSIN, ISIN, FI_Name, Reason)
  var m_DepoAcc  = DepoAcc;
  var m_DepoPart = DepoPart;
  var m_FI_Code  = FI_Code;
  var m_LSIN     = LSIN;
  var m_ISIN     = ISIN;
  var m_FI_Name  = FI_Name;
  var m_Reason   = Reason;
end;

// Протокол процедуры сверки
private class CNSDProtocol(Reg_No:string, End_Date:date)
  var m_ErrArr = TArray();
  var m_TotalCnt = 0;
  var m_Reg_No = Reg_No;
  var m_End_Date = End_Date;
  var m_HeadTable = "┌───┬──────────────┬────────────────────┬─────────────────┬────────────────────┬────────────────────┬────────────────────────────┬────────────────────────────────────────────────────────────┐\n"+
                    "│ № │  Счёт депо   │  Раздел счёта депо │Код ценной бумаги│Номер госрегистрации│       ISIN         │  Краткое наименование ц/б  │                     Количество ц/б в шт.                   │\n"+
                    "│   │              │                    │                 │                    │                    │                            ├────────────────────┬───────────────────┬───────────────────┤\n"+
                    "│   │              │                    │                 │                    │                    │                            │         НРД        │      Система      │      Разница      │\n"+
                    "├───┼──────────────┼────────────────────┼─────────────────┼────────────────────┼────────────────────┼────────────────────────────┼────────────────────┼───────────────────┼───────────────────┤";

  var m_HeadTableErr = "┌───┬──────────────┬────────────────────┬─────────────────┬────────────────────┬────────────────────┬────────────────────────────┬────────────────────────────────────────┐\n"+
                       "│ № │  Счёт депо   │  Раздел счёта депо │Код ценной бумаги│Номер госрегистрации│       ISIN         │  Краткое наименование ц/б  │             Описание ошибки            │\n"+
                       "├───┼──────────────┼────────────────────┼─────────────────┼────────────────────┼────────────────────┼────────────────────────────┼────────────────────────────────────────┤";

  var m_tablewidth = Index(m_HeadTable, "\n");

  var m_Rep = CMakeReport(m_HeadTable);

  const m_RepFontStyleTitel =  "ex_FS(b)";

  macro SetCount(cnt)
    m_TotalCnt = cnt;
  end;

  macro SetError(DepoAcc, DepoPart, FI_Code, LSIN, ISIN, FI_Name, Reason)
    m_ErrArr[m_ErrArr.size] = CNSDProtocolStr(DepoAcc, DepoPart, FI_Code, LSIN, ISIN, FI_Name, Reason);
  end;

  macro GetCntErr()
    return m_ErrArr.size;
  end;

  macro Print(Rests : TArray, MisNum : integer)
    var i = 0, mis = 0;
    var wh = 30;
    var diff = $0;
    var Header = "Протокол сверки остатков ценных бумаг в НРД (";

    if(hdsttds.ON_FINISH_ == "Y")
      Header = Header + "На конец операционного дня";
    else
      Header = Header + "Операционный день НРД не закрыт!";
    end;
    Header = Header + ")";

    DP_AddPrintCell( m_Rep, Header, m_tablewidth-2, 0, "c:w:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();

    DP_AddPrintCell( m_Rep, "Номер отчёта НРД:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, m_Reg_No, m_tablewidth-2-wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    DP_AddPrintCell( m_Rep, "Остатки за дату:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, m_End_Date, m_tablewidth-2-wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    m_Rep.AddEmptyStr();
    m_Rep.AddStr();
    DP_AddPrintCell( m_Rep, "Всего обработано записей:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, m_TotalCnt, m_tablewidth-2-wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    DP_AddPrintCell( m_Rep, "Найдено несоответствий:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, MisNum, m_tablewidth-2-wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    m_Rep.AddEmptyStr();

    if(MisNum)
      DP_AddPrintCell( m_Rep, "Перечень несоответствий:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
      m_Rep.AddStr();

      while(i < Rests.size)
        if(Rests[i].m_Amount != Rests[i].m_NSD_Amount )
          mis = mis + 1;
          DP_AddPrintCell( m_Rep, mis,                        0, 0, "l:" + m_RepFontStyleTitel );
          DP_AddPrintCell( m_Rep, Rests[i].m_DepoAccNSDCode,  0, 0, "l:" + m_RepFontStyleTitel );
          DP_AddPrintCell( m_Rep, Rests[i].m_DepoPartNSDCode, 0, 0, "l:" + m_RepFontStyleTitel );
          DP_AddPrintCell( m_Rep, Rests[i].m_NSD_FICODE,      0, 0, "l:" + m_RepFontStyleTitel );
          DP_AddPrintCell( m_Rep, Rests[i].m_LSIN,            0, 0, "l:" + m_RepFontStyleTitel );
          DP_AddPrintCell( m_Rep, Rests[i].m_ISIN,            0, 0, "l:" + m_RepFontStyleTitel );
          DP_AddPrintCell( m_Rep, Rests[i].m_NSD_FINAME,      0, 0, "l:" + m_RepFontStyleTitel );
          DP_AddPrintCell( m_Rep, double(Rests[i].m_NSD_Amount),      0, DP_GetPrecision(Rests[i].m_NSD_Amount, Rests[i].m_SumPrecision), "r:" + m_RepFontStyleTitel );
          DP_AddPrintCell( m_Rep, double(Rests[i].m_Amount),          0, DP_GetPrecision(Rests[i].m_Amount, Rests[i].m_SumPrecision), "r:" + m_RepFontStyleTitel );

          diff = Rests[i].m_NSD_Amount - Rests[i].m_Amount;
          DP_AddPrintCell( m_Rep, double(diff), 0, DP_GetPrecision(diff, Rests[i].m_SumPrecision), "r:" + m_RepFontStyleTitel );

          m_Rep.AddStr();
        end;

        i = i + 1;
      end;

      m_Rep.AddEmptyStr();
    end;

    if(m_ErrArr.size)
      m_Rep.AddNewSheetBreak("Ошибки процедуры сверки", m_HeadTableErr);

      DP_AddPrintCell( m_Rep, "Ошибки процедуры сверки:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
      m_Rep.AddStr();

      i = 0;
      while(i < m_ErrArr.size)
        DP_AddPrintCell( m_Rep, i+1,                    0, 0, "c:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_DepoAcc,  0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_DepoPart, 0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_FI_Code,  0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_LSIN,     0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_ISIN,     0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_FI_Name,  0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_Reason,   0, 0, "l:" + m_RepFontStyleTitel );

        m_Rep.AddStr();

        i = i + 1;
      end;
    end;

    m_Rep.PrintWinRep();
    m_Rep.ShowWinRep();
  end;
end;

class CNSDImportRests()
  private var path:string = ""; // Путь до папки импорта с завершающим \\
  private var protocol = NULL;
  private var ImpDate, ImpNo;
  private var m_Rests = TArray;
  private var isDepo, isKDU;

  private macro GetStorageAcnt(NSD_AcntCode:string, StorageAcnt)
    var cmd, query, DataSet;

    StorageAcnt.Clear();

    query =   "select root.* "
            + "  from ddepoacnt_dbt root "
            + " where root.t_Kind = 1 /*активный*/"
            + "   and root.t_Department = ? "
            + "   and RTRIM(rsb_struct.getString(rsi_rsb_kernel.GetNote(112, TO_CHAR(root.t_AutoKey), 2, ?)), CHR(0)) = ? ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam({OperDprt});
    cmd.AddParam(date({curdate}));
    cmd.AddParam(NSD_AcntCode);

    DataSet = cmd.Execute();

    if(DataSet.moveNext())
      DataSet.GetRecord().CopyTo( StorageAcnt.rec );
    end;
  end;

  private macro error(errstr)
    protocol.SetError(trim(hdsttds.DEPO_ACC_C),
                      trim(sttds.SECTION_CO),
                      trim(sttds.SECURITY_C),
                      trim(ToOEM(sttds.STATE_REGI,true)),
                      trim(sttds.ISIN_CODE),
                      trim(ToOEM(sttds.SECURITY_S,true)),
                      errstr);
  end;

  private macro full_error(depoacc,depopart,fi_code,lsin,isin,shortName,errstr)
    protocol.SetError(depoacc,
                      depopart,
                      fi_code,
                      lsin,
                      isin,
                      shortName,
                      errstr);
  end;

  private macro text_error(errstr)
    protocol.SetError("",
                      "",
                      "",
                      "",
                      "",
                      "",
                      errstr);
  end;

  private macro getCountRecords(f)
    return NRecords(f);
  end;

  private macro GetFIIDbyCodeNSD(NSD_Code)
    var query, cmd, DataSet;
    var FIID = -1;

    if(NSD_Code != "")
      query =   "select obj.t_ObjectID "
              + "  from dobjcode_dbt obj, davoiriss_dbt av "
              + " where t_ObjectType = " + OBJTYPE_FININSTR
              + "   and obj.t_ObjectID = av.t_fiid"
              + "   and t_CodeKind = 14 /*FICK_NADC*/"
              + "   and T_SPISCLOSED != chr(88)"   //DAN проверка на закрытую бумагу
              + "   and t_Code = ? ";
      cmd = DL_RSDCommand(query);
      cmd.AddParam(NSD_Code);

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        FIID = int(DataSet.ObjectID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIIDbyISIN(ISIN)
    var query, cmd, DataSet;
    var FIID = -1;

    if(ISIN != "")
      query =   " select t_FIID "
              + "   from davoiriss_dbt "
              + "  where t_ISIN = ? "
              + "   and T_SPISCLOSED != chr(88)";  //DAN проверка на закрытую бумагу;

      cmd = DL_RSDCommand(query);
      cmd.AddParam(ISIN);

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        FIID = int(DataSet.FIID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIIDbyLSIN(LSIN)
    var query, cmd, DataSet;
    var FIID = -1;

    if(LSIN != "")
      query =   "select t_FIID "
              + "  from davoiriss_dbt "
              + " where t_LSIN = ? "
              + "   and T_SPISCLOSED != chr(88)";  //DAN проверка на закрытую бумагу;

      cmd = DL_RSDCommand(query);
      cmd.AddParam(LSIN);

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        FIID = int(DataSet.FIID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIID()
    var FIID = -1;

    FIID = GetFIIDbyCodeNSD(string(trim(sttds.SECURITY_C)));
    if(FIID == -1)
      FIID = GetFIIDbyISIN(string(trim(sttds.ISIN_Code)));
      if(FIID == -1)
        FIID = GetFIIDbyLSIN(string(trim(ToOEM(sttds.STATE_REGI,true))));
      end;
    end;

    return FIID;
  end;

  private macro loadRestsQr(NrdPartyId:integer)
    var err = 0;
    var cmd, query, DataSet;

    if(not err)
      query = "select rownum as a1, scqr.T_DEPOACCCODE as a2, scqr.T_DEPOPARTCODE as a3, "+
              "       obj.t_Code as a4, avoir.t_Lsin as a5, avoir.t_Isin as a6, fininstr.t_name as a7, "+
              "       RSB_SECUR.GetQRRestOnDate(scqr.T_QRID, ?) as a8, "+
              "       ROUND(ABS(RSI_RSB_ACCOUNT.RestAC(ac.T_ACCOUNT, ac.T_CODE_CURRENCY, ?, ac.T_CHAPTER, 0))) as a9 "+ 
              "  from DSCQRACC_DBT scqr, DAVOIRISS_DBT avoir, DFININSTR_DBT fininstr, DACCOUNT_DBT ac, DOBJCODE_DBT obj "+
              " where avoir.t_Fiid(+) = scqr.t_Fiid "+
              "   and fininstr.t_Fiid(+) = scqr.t_Fiid "+
              "   and ac.t_AccountId(+) = scqr.t_AccountId "+
              "   and obj.t_AUTOKEY(+) = RSB_FIInstr.FI_GetObjCodeOnDate(scqr.T_FIID, ?, 14, ?)"
              "   and ROUND(ABS(RSI_RSB_ACCOUNT.RestAC(ac.T_ACCOUNT, ac.T_CODE_CURRENCY, ?, ac.T_CHAPTER, 0))) != "+
              "       RSB_SECUR.GetQRRestOnDate(scqr.T_QRID, ?) "+
              "   and scqr.t_StorageId = ?";
      
      query = "select q1.*, (q1.a8-q1.a9) as a10 from ("+query+") q1";

      cmd = DL_RSDCommand(query);
      cmd.AddParam({curdate});
      cmd.AddParam({curdate});
      cmd.AddParam(OBJTYPE_FININSTR);
      cmd.AddParam({curdate});
      cmd.AddParam({curdate});
      cmd.AddParam({curdate});
      cmd.AddParam(NrdPartyId);
      DataSet = cmd.Execute();

      while(DataSet.moveNext())
        m_Rests[m_Rests.size] = CNSDRest(DataSet.a1, DataSet.a2, DataSet.a3, NULL, DataSet.a4, DataSet.a5, DataSet.a6, DataSet.a9, DataSet.a7,6, DataSet.a8); //DataSet.a10, DataSet.a8);
      end;

    end;

    return err;
  end;

  // Загрузить остатки по счету депо из Системы
  private macro loadRestsDepo(NSD_AccountCode, RestDate)
    var err = 0;
    var cmd, query, DataSet;
    var StorageAcnt = TRecHandler("depoacnt.dbt");

    GetStorageAcnt(NSD_AccountCode, StorageAcnt);
    if(StorageAcnt.rec.AutoKey <= 0)
      err = 1;
      text_error("Не найден счет места хранения с кодом " + NSD_AccountCode);
    end;

    if(not err)
      // Выбрать все ненулевые остатки по открытому хранению за заданную дату, находящиеся на счете StorageAcnt, сгруппированные по терминальным разделам и ФИ
      // При этом у разделов должно быть определено примечание "Код в НРД"
      query = " SELECT t_DepoPartID, t_DepoPartNSDCode, t_FIID, t_FI_Name, t_SumPrecision, t_NSD_FICODE, t_LSIN, t_ISIN, ABS(SUM(t_Amount)) t_Amount " +
              " FROM (SELECT acc.t_DepoAcc t_DepoPartID, " +
                           " NVL ( RTRIM (rsb_struct.getString (rsi_rsb_kernel.GetNote( 122, TO_CHAR (acc.t_DepoAcc), 2, ?)), CHR (0)), CHR (1)) t_DepoPartNSDCode, " +
                           " acc.t_Code_Currency t_FIID, " +
                           " fi.t_Name t_FI_Name, " +
                           " fi.t_SumPrecision, " +
                           " NVL (oc.t_Code, CHR (1)) t_NSD_FICODE, " +
                           " avr.t_LSIN, " +
                           " avr.t_ISIN, " +
                           " rsb_account.restac(acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, NULL) t_Amount " +
                      " FROM daccount_dbt acc, dfininstr_dbt fi, davoiriss_dbt avr, dobjcode_dbt oc " +
                     " WHERE acc.t_DepoRoot = ? " +
                       " AND INSTR (acc.t_Type_Account, 'I') = 0 " +
                       " AND rsb_account.restac (acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, NULL) != 0 " +
                       " AND NVL(RTRIM(rsb_struct.getString(rsi_rsb_kernel.GetNote(122, TO_CHAR(acc.t_DepoAcc), 2, ?)), CHR(0)), CHR(1)) != CHR(1) " +
                       " AND fi.T_FIID = acc.t_Code_Currency " +
                       " AND avr.t_FIID = acc.t_Code_Currency " +
                       " AND oc.t_ObjectType(+) = " + OBJTYPE_FININSTR +
                       " AND oc.t_ObjectID(+) = acc.t_Code_Currency " +
                       " AND oc.t_CodeKind(+) = 14) " +
              " GROUP BY (t_DepoPartID, t_DepoPartNSDCode, t_FIID, t_FI_Name, t_SumPrecision, t_NSD_FICODE, t_LSIN, t_ISIN) ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(RestDate);
      cmd.AddParam(RestDate);
      cmd.AddParam(StorageAcnt.rec.AutoKey);
      cmd.AddParam(RestDate);
      cmd.AddParam(RestDate);

      DataSet = cmd.Execute();

      // Эти остатки соберем в список для сопоставления
      while(DataSet.moveNext())
        m_Rests[m_Rests.size] = CNSDRest(DataSet.DepoPartID, trim(string(hdsttds.DEPO_ACC_C)), string(DataSet.DepoPartNSDCode), DataSet.FIID, string(DataSet.NSD_FICODE), string(DataSet.LSIN), string(DataSet.ISIN), money(DataSet.Amount), string(DataSet.FI_Name), DataSet.t_SumPrecision);
      end;

      cmd = null;
      DataSet = null;

      // Выбрать все терминальные разделы с ненулевыми остатками по открытому хранению за заданную дату, находящиеся на счете StorageAcnt
      // При этом у которых не определено примечание "Код в НРД"
      query = " select distinct dpac.t_BriefCode " +
                " from daccount_dbt acc, ddepoacnt_dbt dpac " +
               " where acc.t_DepoRoot = ? " +
                 " and instr(acc.t_Type_Account, 'I') = 0 " +
                 " and rsb_account.restac( acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, null ) != 0 " +
                 " and NVL(RTRIM(rsb_struct.getString(rsi_rsb_kernel.GetNote(122, TO_CHAR(acc.t_DepoAcc), 2, ?)), CHR(0)), CHR(1)) = CHR(1) " +
                 " and dpac.t_AutoKey = acc.t_DepoAcc ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(StorageAcnt.rec.AutoKey);
      cmd.AddParam(RestDate);
      cmd.AddParam(RestDate);

      DataSet = cmd.Execute();

      // Такие разделы не рассматриваем
      while(DataSet.moveNext())
        text_error("Для раздела " + string(DataSet.BriefCode) + " счёта " + StorageAcnt.rec.BriefCode + " не определён код раздела в НРД");
      end;
    end;

    return err;
  end;

  private macro AddNSDRest(DepoPartNSDCode, NSD_FICODE, NSD_LSIN, NSD_ISIN, NSD_Amount, NSD_FINAME)
    var i = 0, found = false, NewRest = null;

    while((i < m_Rests.size) AND (NOT found))
      // Строка с таким ФИ на таком разделе уже есть в списке - дополним ее данными из НРД
      if((m_Rests[i].m_DepoPartNSDCode == DepoPartNSDCode) AND
         (((m_Rests[i].m_NSD_FICODE != "") AND (m_Rests[i].m_NSD_FICODE == NSD_FICODE)) OR
          ((m_Rests[i].m_ISIN != "") AND (m_Rests[i].m_ISIN == NSD_ISIN)) OR
          ((m_Rests[i].m_LSIN != "") AND (m_Rests[i].m_LSIN == NSD_LSIN)))
        )
        found = true;

        m_Rests[i].m_NSD_Amount = m_Rests[i].m_NSD_Amount + NSD_Amount;

        // Дозаполним коды и название ц/б, т.к. в отчете преимущество имеют данные НРД
        m_Rests[i].m_NSD_FICODE = NSD_FICODE;
        m_Rests[i].m_ISIN = NSD_ISIN;
        m_Rests[i].m_LSIN = NSD_LSIN;
        m_Rests[i].m_NSD_FINAME = NSD_FINAME;
      end;

      i = i + 1;
    end;

    // Строки с такой комбинацией раздела и ц/б в списке нет - добавим остаток НРД
    if(NOT found)
      NewRest = CNSDRest(0, trim(string(hdsttds.DEPO_ACC_C)), DepoPartNSDCode, -1, NSD_FICODE, NSD_LSIN, NSD_ISIN, $0.0, NSD_FINAME, AVOIRISS_SUM_PRECISION);
      NewRest.m_NSD_Amount = NSD_Amount;

      m_Rests[m_Rests.size] = NewRest;
    end;
  end;

  private macro FoundCreateQrRest(ScQrAcc:TRecHandler, Rest:money, ErrStr:@string)
    var err=0, bufStr="";
    var ScQrRestTB = TBFile("scqrrest.dbt","w",1);
    ScQrRestTB.rec.QrId = ScQrAcc.rec.QrId;
    ScQrRestTB.rec.Date = {curdate};
    if (ScQrRestTB.GetEQ())
      ScQrRestTB.rec.Rest = Rest;
      if (not ScQrRestTB.Update())
        RunError("Ошибка обновления записи таблицы scqrrest.dbt:|"+status(bufStr)+"-"+bufStr);
      end;
    else
      ScQrRestTB.rec.QrId = ScQrAcc.rec.QrId;
      ScQrRestTB.rec.Date = {curdate};
      ScQrRestTB.rec.Rest = Rest;
      if (not ScQrRestTB.Insert())
        RunError("Ошибка вставки записи в таблицу scqrrest.dbt:|"+status(bufStr)+"-"+bufStr);
      end;
    end;

  OnError(ErrObj)
    if (IsEqClass ("TrslError", ErrObj))
      ErrStr=ErrObj.Message;
      if(IsEqClass ("TDbError", ErrObj.err))
        ErrStr=ErrStr+":|"+ErrObj.err.Stat+"-"+ErrObj.err.Oper+"-"+ErrObj.err.Table+"-"+ErrObj.err.Message;
      end;
    elif (not ErrStr)
      ErrStr="Ошибка выполнения FoundOpenScQr";
    end;
    return 1;
  end;

  private macro FoundOpenScQr(NrdPartyId:integer, DepoAcc:string, DepoPart:string, 
    Fiid:integer, ScQrAcc:TRecHandler, AvoirRec:TRecHandler, FininstRec:TRecHandler, Rest:money, ErrStr:@string)
    var err=0, bufStr="";
    var cmd, DataSet;
    var ScQrAccTB = TBFile("scqracc.dbt","w");
    ScQrAcc.Clear();
    FininstRec.Clear();
    AvoirRec.Clear();
    cmd = DL_RSDCommand(  "select * from dscqracc_dbt qracc"
                               + " where qracc.t_StorageId  = ? "
                               + "   and qracc.t_Fiid  = ? "
                               + "   and qracc.t_DepoAccCode = ? "
                               + "   and qracc.t_DepoPartCode = ? "
                               + "   and rownum = 1"
                               ); 
    cmd.AddParam(NrdPartyId);
    cmd.AddParam(Fiid);
    cmd.AddParam(DepoAcc);
    cmd.AddParam(DepoPart);
    DataSet = cmd.Execute();

    
    if (DataSet.moveNext())
      DataSet.GetRecord().CopyTo(ScQrAcc.rec);
      err=FoundCreateQrRest(ScQrAcc, Rest,@ErrStr);
    else
      ScQrAcc.rec.StorageId = NrdPartyId;
      ScQrAcc.rec.Fiid = Fiid;
      ScQrAcc.rec.DepoAccCode = DepoAcc;
      ScQrAcc.rec.DepoPartCode = DepoPart;
      err = СоздатьЗаписьВРегистреКДУ(ScQrAcc, {curdate}, Rest, @ErrStr);
    end;
    if (not err)
      ПолучитьФинИн(Int(Fiid),FininstRec,AvoirRec);
    end;
    return err;
  OnError(ErrObj)
    if (IsEqClass ("TrslError", ErrObj))
      ErrStr=ErrObj.Message;
      if(IsEqClass ("TDbError", ErrObj.err))
        ErrStr=ErrStr+":|"+ErrObj.err.Stat+"-"+ErrObj.err.Oper+"-"+ErrObj.err.Table+"-"+ErrObj.err.Message;
      end;
    elif (not ErrStr)
      ErrStr="Ошибка выполнения FoundOpenScQr";
    end;
    return 1;
  end;

  // чтение сведений об остатках КДУ
  private macro processSttdsQr(NrdPartyId)
    var err=0;
    var FIID = -1;
    var i = 0;
    var cnt = getCountRecords(sttds);
    var ErrStr="";
    var ScQrAcc = TRecHandler("scqracc.dbt");
    var AvoirRec = TRecHandler("avoiriss.dbt");
    var FininstRec = TRecHandler("fininstr.dbt"); 

    if (not err)

      InitProgress(cnt, "Обрабатывается справочник sttds.dbf", "Импорт сведений об остатках");
      protocol.SetCount(cnt);

      rewind(sttds);

      while(next(sttds))
        FIID = GetFIID();
        if( FIID == -1)
          error("В справочнике ценных бумаг не найден выпуск по указанным в отчёте НРД кодам");
        end;

        if (FIID > 0)
          if (FoundOpenScQr(NrdPartyId, 
            trim(string(hdsttds.DEPO_ACC_C)), 
            trim(sttds.SECTION_CO), 
            FIID, 
            ScQrAcc, 
            AvoirRec,
            FininstRec,
            money(strsubst(sttds.SEQ_AMO, " ", "")),
            @ErrStr))
            error(ErrStr);
          end;
        end;

        i = i + 1;
        UseProgress(i);
      end;

      RemProgress();
    end;
    return err;
  end;

  // чтение сведений об остатках Депозитарий
  private macro processSttdsDepo()
    var err=0;
    var FIID = -1;
    var i = 0;
    var cnt = getCountRecords(sttds);

    if (not err)

      InitProgress(cnt, "Обрабатывается справочник sttds.dbf", "Импорт сведений об остатках");
      protocol.SetCount(cnt);

      rewind(sttds);

      while(next(sttds))
        FIID = GetFIID();
        if( FIID == -1)
          error("В справочнике ценных бумаг не найден выпуск по указанным в отчёте НРД кодам");
        end;

        AddNSDRest(trim(sttds.SECTION_CO),
                  trim(sttds.SECURITY_C),
                  trim(tooem(sttds.STATE_REGI, true)),
                  trim(sttds.ISIN_CODE),
                  money(strsubst(sttds.SEQ_AMO, " ", "")),
                  trim(tooem(sttds.SECURITY_S, true)));
        i = i + 1;
        UseProgress(i);
      end;

      RemProgress();
    end;
    return err;
  end;

  private macro GetMisNum()
    var i = 0, mis = 0;
    while(i < m_Rests.size)
      if(m_Rests[i].m_Amount != m_Rests[i].m_NSD_Amount )
        mis = mis + 1;
      end;
      i = i + 1;
    end;

    return mis;
  end;

  // чтение заголовков отчета и выписки
  private macro processReportH()
    var err = 0;
    var CodeStr = "";
    var NrdPartyId = 0;
    var Depo_Acc = "";

    if(next(reporth))
      if(next(hdsttds))
        ImpDate = date(hdsttds.END_DATE);
        ImpNo   = trim(string(reporth.REG_NO));
        Depo_Acc = trim( string(hdsttds.DEPO_ACC_C));

        protocol = CNSDProtocol(ImpNo, ImpDate);

        if (isDepo)
          loadRestsDepo(trim(string(hdsttds.DEPO_ACC_C)), date(hdsttds.END_DATE)); // Загрузить все остатки на дату, хранящиеся в Системе
          processSttdsDepo(); // Загрузить все остатки из выписки
          err = protocol.GetCntErr();
          protocol.Print(m_Rests, GetMisNum());
        elif (isKDU)
          GetRegistryValue( "SECUR\\NRD_CODE", V_STRING, CodeStr, err );
          if (not err)
            NrdPartyId = ПолучитьКодСубъекта(String(CodeStr), PTCK_CLIENT);
            NrdPartyId = IIF(NrdPartyId,Int(NrdPartyId),0);
          end;
          if (NrdPartyId == 0)
            MsgBox("Не найден субъект из NRD_CODE");
            err=1;
          else

// Очистка остатков на дату сверки
var query_QR = "begin\n RSHB_USER.UpdateSCRest(?,?,?);\n end; ";
var Select_QR = RSDCommand( query_QR );
    Select_QR.AddParam("pStorID", RSDBP_IN, NrdPartyId );
    Select_QR.AddParam("pDepoACC", RSDBP_IN, Depo_Acc );
    Select_QR.AddParam("pDate", RSDBP_IN, ImpDate );

    Select_QR.Execute();

            processSttdsQr(NrdPartyId);
            loadRestsQr(NrdPartyId);
            err = protocol.GetCntErr();
            protocol.Print(m_Rests, GetMisNum());
          end;
        end;
      else
        err = 1;
        msgbox("Отсутствует запись в файле заголовка выписки hdsttds.dbf");
      end;
    else
      err = 1;
      msgbox("Отсутствует запись в файле заголовка отчета reporth.dbf");
    end;

    return err;
  end;

  macro moveFile(path:string, fileName:string, dir:string)
    var pathDone = "";
    var day, month, year;

    

    DateSplit( ImpDate, day, month, year );
    if(day < 10)
      day = string("0"+day);
    end;
    if(month < 10)
      month = string("0"+month);
    end;
//Для РСХБ -перенос файлов на СП
    MoveNSDData();

    pathDone = path + dir+"\\";
    MakeDir(pathDone);
    pathDone = path + dir+"\\" + string(day)+string(month)+string(year)+"\\";
    MakeDir(pathDone);
    pathDone = path + dir+"\\" + string(day)+string(month)+string(year)+"\\"+ImpNo+"\\";
    MakeDir(pathDone);

    if (CopyFile(path + fileName, pathDone + fileName))
      RemoveFile(path + fileName);
    else
      msgbox("Ошибка при перемещении файла " + fileName + " в папку " + pathDone);
    end;
  end;

  // Запуск отчета
  macro Run() : bool
    var err = 0;



    GetRegistryValue("COMMON\\WORK_MODE\\WORK_WITH_DEPOSITARY", V_BOOL, isDepo, err);
    GetRegistryValue("SECUR\\ВЕДЕНИЕ КДУ", V_BOOL, isKDU, err);
    if (err)
      MsgBox("Ошибка получение значений настроек \"WORK_WITH_DEPOSITARY\", \"ВЕДЕНИЕ КДУ\"");
    end;
    if (isDepo and isKDU)
      CreateErrMsg(err=28055);
    elif ((not isDepo) and (not isKDU))
      CreateErrMsg(err=28056);
    end;
    if (not err)
      GetRegistryValue("DEPO\\NSD\\NSD_IS401_PATH", V_STRING, path, err);
      if(path == "")
        msgbox("Не указан путь к сетевой папке для загрузки отчётов с остатками ценных бумаг.|Настройка DEPO\\NSD\\NSD_IS401_PATH");
      else
        path = path + "\\";


    MoveNSDData();

        if(open(reporth, path + "reporth.dbf")) // Заголовок отчета
          if(open(hdsttds, path + "hdsttds.dbf")) // Заголовок выписки
            if(open(sttds, path + "sttds.dbf")) // Сведения об остатках

              err = processReportH();

              close(sttds);
            else
              err = 1;
              msgbox("Невозможно открыть файл " + path+"sttds.dbf");
            end;

            close(hdsttds);
          else
            err = 1;
            msgbox("Невозможно открыть файл " + path+"hdsttds.dbf");
          end;

          close(reporth);

          if(err == 0)
            moveFile(path, "reporth.dbf", "Done");
            moveFile(path, "hdsttds.dbf", "Done");
            moveFile(path, "sttds.dbf", "Done");
            moveFile(path, "sttds_sm.dbf", "Done");
          end;
        else
          err = 1;
          msgbox("Невозможно открыть файл " + path + "reporth.dbf");
        end;
      end;
    end;
    if (err>1)
      DisplayError();
    end;
    return err;
  end;
  
end;

CNSDImportRests().Run();
Exit(1);
