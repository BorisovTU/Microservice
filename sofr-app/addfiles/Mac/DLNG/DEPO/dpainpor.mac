/*
$Name:         dpainpor.mac
$Module:       Депозитарий
$Description:  Макрофункция разнесения остатков по субсчетам аналитики "Учет актива в пассиве"
*/
Import globals;
import RsbDataSet, CurrInter, opr_depo;


File acsub("accsub.dbt") write;
File dpvl("depoatvl.dbt");
var DataAcanl;

const DEPOATTR_ATTNUM_OWN = 7;    /* Номер характеристики "Принадлежность" */

/* Значения характеристики DEPOATTR_ATTNUM_OWN */
const DP_ATTNUM_OWN_ALL = 0;   /* Любые бумаги */
const DP_ATTNUM_OWN_OUR = 1;   /* Собственные бумаги */
const DP_ATTNUM_OWN_DEP = 2;   /* Бумаги сторонних депонентов */


macro FindACCSUB( AccAnaliticsID, SubAccountID )
  var bstat;
  var bftext;
  
  acsub.AccAnaliticsID = AccAnaliticsID;
  acsub.SubAccountID  = SubAccountID;

  if( not GetEQ(acsub) )
    bstat = Status(bftext);
    if((bstat) AND (bstat!=9) AND (bstat!=4))
      println( "Ошибка поиска записи в файле accsub.dbt\n" + bftext );
    else
      bstat = 4;
    end;
  else
    bstat = 0;
  end;

  return bstat;
end;


macro FindDEPOATVL( ID )
  var bstat;
  var bftext;

  dpvl.AttrNum = DEPOATTR_ATTNUM_OWN;
  dpvl.IsType  = "";
  dpvl.ID      = ID;

  if( not GetEQ(dpvl) )
    bstat = Status(bftext);
    if((bstat) AND (bstat!=9) AND (bstat!=4))
      println( "Ошибка поиска записи в файле depoatvl.dbt\n" + bftext );
    else
      bstat = 4;
    end;
  else
    bstat = 0;
  end;

  return bstat;
end;


macro Minimum( A, B )
  if( A < B )
    return A;
  else
    return B;
  end;
end;


macro TransferRest( DataAcc, Ownership )
  var stat = TRUE;
  var bstat;
  var bftext;
  var cycle = TRUE;
  var SubAccountID;
  var SumToCarry;     
  var SumToTransfer = -RestAC( DataAcc.Account, DataAcc.Code_Currency, {curdate}, null, DataAcc.Chapter );/*текущий остаток*/
  var FIID = DataAcc.Code_Currency;
  var query;

  Record accsub_buf("accsub.dbt");
  Record accsubdc_buf("accsubdc.dbt");

  query =   " select vanl.t_Account, vanl.t_AccAnaliticsID, acc.t_Client"
          + " from daccvanl_dbt vanl, daccount_dbt acc"
          + " where vanl.t_AnaliticsID = " + SYS_ANL_AINPACCOUNTING
          + "   and vanl.t_FIID = " + FIID
          + "   and vanl.t_Chapter = " + CHAPT5
          + "   and acc.t_Account = vanl.t_Account "
          + "   and acc.t_Chapter = " + CHAPT5
          + "   and acc.t_Code_Currency = " + FIID;

  DataAcanl = TRsbDataSet(query);
  while( (cycle) AND (stat) AND (DataAcanl.moveNext()))
    if( ( SumToTransfer )  )
      /* Для всех аналитик кредитовые субсчета которых не пустые */
      bstat = FindACCSUB( DataAcanl.AccAnaliticsID, ACCSUB_SPECTYPE_CREDIT );
      if( ( bstat ) AND ( bstat != 4 ) )
        stat = FALSE;
      else
        var Rest = DP_RestSubAcc( acsub.AccAnaliticsID, acsub.SubAccountID, {curdate} );
        if( ( not bstat ) AND ( Rest ) )
          /* И если совпадает принадлежность на счете с аналитикой */
          if( not bstat )
            if( ( Ownership == DP_ATTNUM_OWN_ALL ) OR
                ( ( Ownership == DP_ATTNUM_OWN_OUR ) AND ( DataAcanl.Client == {OurBank} ) ) OR 
                ( ( Ownership == DP_ATTNUM_OWN_DEP ) AND ( DataAcanl.Client != {OurBank} ) )
              ) 
              SumToCarry = Minimum( SumToTransfer, Rest );
              /* Вставим субсчет с кодом текущего активного счета */
              ClearRecord(accsub_buf);
              accsub_buf.AccAnaliticsID = DataAcanl.AccAnaliticsID;
              accsub_buf.SubAccountID   = ACCSUB_SPECTYPE_NONE;
              accsub_buf.SubAccountCode = DataAcc.Account;
              accsub_buf.ParentID       = 0;
              accsub_buf.HaveChild      = "";
              accsub_buf.SpecialType    = ACCSUB_SPECTYPE_NONE;
              accsub_buf.Status         = ACCSUB_STATE_OPENED;
              stat = InsertSubAccount( SYS_ANL_AINPACCOUNTING, DataAcanl.Account, FIID, CHAPT5, "", accsub_buf );
              if(  not stat )
                stat = TRUE;
                /* Найдем его идентификатор */
                SubAccountID = GetSubAccID( SYS_ANL_AINPACCOUNTING, DataAcanl.Account, FIID, CHAPT5, DataAcc.Account );
                if( SubAccountID )
                  /* Выполним проводку с субсчета неразнесенных кредитовых остатков на новый субсчет */
                  ClearRecord(accsubdc_buf);
                  accsubdc_buf.SubCarryID           = 0;
                  accsubdc_buf.AccTrnID             = 0;
                  accsubdc_buf.AccAnaliticsID       = DataAcanl.AccAnaliticsID;
                  accsubdc_buf.SubAccountPayerID    = 0;
                  accsubdc_buf.SubAccountReceiverID = SubAccountID;
                  accsubdc_buf.Date_Carry           = {curdate};
                  accsubdc_buf.Sum                  = SumToCarry;
                  accsubdc_buf.Status               = ACSDC_FACT;
                  accsubdc_buf.DebetCredit          = 1;
                  stat = InsertSubAccDoc( accsubdc_buf );
                  if( not stat )
                    stat = TRUE;
                    SumToTransfer = SumToTransfer - SumToCarry;
                  else
                    println( "Ошибка вставки технической проводки!\n" );
                  end;
                else
                  println( "Ошибка поиска субсчета с кодом " + DataAcc.Account + " для счета " + DataAcanl.Account + "\n" );
                  stat = FALSE;
                end;
              else
                println( "Ошибка вставки субсчета с кодом " + DataAcc.Account + " для счета " + DataAcanl.Account + "\n" );
                stat = FALSE;
              end;
            end;
          else
            if( bstat == 4 )
              println( "Не найдена запись в файле account.dbt\n" );
            end;
            stat = FALSE;
          end;
        end;
      end;
    else
      cycle = FALSE;
    end;
  end;
  if( not stat )
    bstat = Status(bftext);
    if((bstat) AND (bstat!=9) AND (bstat!=4))
      println( "Ошибка поиска записи в файле accvanl.dbt\n" + bftext );
      stat = FALSE;
    else
      stat = TRUE;
    end;
  end;

  return stat;
end;


macro TransferRestsToSubAcc()
  var DataAcc;
  var query;
  var stat = TRUE;
  var bstat;
  var bftext;
  var cycle = TRUE;
  var Ownership;
  var error;
  var NumRec = 0, count = 0; 
  println("Обработаны счета");

  query =   " select acc.t_Chapter, acc.t_Account, acc.t_Kind_Account, acc.t_DepoAcc, acc.t_Type_Account, acc.t_Code_Currency "
          + " from daccount_dbt acc"
          + "  where acc.t_Chapter = " + CHAPT5
          + "    and acc.t_Kind_Account = 'А' "
          + "    and acc.t_DepoAcc > 0 "
          + "    and instr(acc.t_Type_Account, 'I') = 0 "
          + " order by acc.t_Chapter, acc.t_Account, acc.t_Code_Currency ";
        

  DataAcc = TRSBDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  DataAcc.MoveLast();
  NumRec = DataAcc.GetRecCount();
  InitProgress( NumRec, "Разнос остатков по субсчетам...", "Разнос остатков по субсчетам");

  stat = DataAcc.moveFirst();
  while( (stat) )
      /* Определим принадлежность */
      Ownership = DP_ATTNUM_OWN_ALL;
      if( DataAcc.DepoAcc )
        bstat = FindDEPOATVL( DataAcc.DepoAcc );
        if( ( bstat ) AND ( bstat != 4 ) )
          stat = FALSE;
        else
          if( not bstat )
            Ownership = int( dpvl.Value );
          end;
        end;
      end;
      /* Разнесем остаток по подходящим пассивным */
      if( stat )
        stat = TransferRest( DataAcc, Ownership );
        if( stat )
          println(DataAcc.Account);
        end;
      end;
      if( stat )
        stat = DataAcc.moveNext();
      end;
      UseProgress(count = count + 1 );
  end;
  RemProgress();
  if( not stat )
    bstat = Status(bftext);
    if((bstat) AND (bstat!=9) AND (bstat!=4))
      println( "Ошибка поиска записи в файле account.dbt\n" + bftext );
      stat = FALSE;
    else
      stat = TRUE;
    end;
  end;

  if( stat )
    println("Разноска остатков по субсчетам проведена успешно");
  else
    println("Ошибки при выполнении разноски остатков по субсчетам");
  end;

  return stat;
end; 

/* Точка входа в макрос */
TransferRestsToSubAcc();
