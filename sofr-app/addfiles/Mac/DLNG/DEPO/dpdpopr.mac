/*
$Name:         dpdpopr.mac
$Module:       Депозитарий
$Description:  Отчет "Журнал операций депозитария"
*/
import RsbDataSet;
import FIInter, CurrInter, CTInter, DPInter, secinter, "deposerv.mac", "opr_depo.mac";

var NameAlg  = TBFile("namealg");
var llvalues = TBFile("llvalues");
var party    = TBFile("party");

record dacnt(depoacnt);

private const ALG_DEPO_DRAFTREFKIND = 3252; // виды журналов номеров регистрации поручения депо

//Классы операций
const DP_INV_OPERATION = 1;//инвентарная
const DP_ADM_OPERATION = 2;//административная
const DP_INF_OPERATION = 3;//информационная
const DP_CRP_OPERATION = 4;//инвентарная-корпоративная

const  SP_DEPOPER_UNIVOPERATION = 800;//Универсальная депозитарная операция
const  SP_DEPOPER_ADMOPER       = 825;//административная операция депо 
const  SP_DEPOPER_INFOPER       = 830;//информационная операция депо

const  ALG_DEPO_OPCLASS  = 3226; //классы операций депозитария
const  ALG_DEPO_FORMCONV = 3233; //форма конвертации

const TA_STAT_DEL = 152;

const OBJTYPE_ADMOPRTYPE = 1207;//типы административных операций

const EMPTY_STRING = 1;

const SC_SPDRAFT_STATUS_CLOSE = 3;

private const SPSK_OPEN    = 0;
private const SPSK_NEW     = 4;

private const SPKK_PASSIVE = 2;

var DataSet;

var CheckInfState = true;

var iDepart = 0;       
var UserLog = 0;
var IsOwnAcc = false;
var IsClientAcc = false;
var iDeponent = UnknownParty;
var iDepoAcc = "";
var Date_doc_beg:date = date(0,0,0);  
var Date_doc_end:date = date(0,0,0);  
var Number_doc_beg = "";
var Number_doc_end = "";
var OpClass_Inv = true;   
var OpClass_Inf = true;   
var OpClass_Adm = true;   
var OpKind = 0;        
var OpKindInv = 0;        
var OpKindInf = 0;        
var OpKindAdm = 0;        
var ToExcel;       
var is_ap;
private var DetailAcnt = false;
private var FromScrol = false;
private var aDepoAcc = "";

var prCount = 1;

var _inv_str = "",
    _adm_str = "",
    _inf_str = "",
    _crp_str = "";
    
private var CurrSPgroundID      = 0;
private var CurrSourceDocKind   = 0;
private var CurrSourceDocID     = 0;
private var CurrRegistrDate     = date(0,0,0);
private var CurrRegistrTime     = time(0);
private var CurrXLD             = "";
private var CurrKind            = 0;
private var CurrAltXld          = "";
private var CurrSignedDate      = date(0,0,0);
private var CurrOperNameAndCode = "";
private var CurrOprIsMakeAuto   = "";
private var CurrTechAutoDocName = "";
private var CurrIsOprXid2       = ""; // признак второй записи для универсальной операции перевода/перемещения

var HeadTable = "┌─────┬──────────┬────────────────┬────────────────────┬─────────────────────────┬─────────────────┬───────────────────┬─────────────────────────┬─────────────────────────┬──────────────┐\n"+
                "│  №  │     №    │                │                    │        Инициатор,       │                 │      Ценные       │                         │                         │ Сформировано │\n"+
                "│ п/п │ операции │    Поручение   │      Операция      │   основание поручения   │    Счет депо    │      бумаги       │        Примечания       │        Исполнитель      │ техническими │\n"+
                "│     │          │                │                    │                         │                 │                   │                         │                         │  средствами  │\n"+
                "├─────┼──────────┼────────────────┼────────────────────┼─────────────────────────┼─────────────────┼───────────────────┼─────────────────────────┼─────────────────────────┼──────────────┤\n"+
                "│  1  │     2    │        3       │          4         │            5            │        6        │         7         │            8            │            9            │      10      │\n"+
                "├─────┼──────────┼────────────────┼────────────────────┼─────────────────────────┼─────────────────┼───────────────────┼─────────────────────────┼─────────────────────────┼──────────────┤";

var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";
                                           


/*интерфейс в Namealg.dbt*/
macro __NameAlg(Type, Number)
  NameAlg.rec.iTypeAlg = Type;
  NameAlg.rec.iNumberAlg = Number;
  if(NameAlg.GetEQ)
    return NameAlg.rec.szNameAlg;
  end;
  return NULL;
end;

macro __LLValues( List, Number)
  llvalues.rec.List = List;
  llvalues.rec.Element = Number;
  if(llvalues.GetEQ)
    return llvalues.rec.Name;
  end;
  return "";
end;

macro __OPRKOPER( Kind )
  file oprkoper("oprkoper");
  oprkoper.Kind_Operation = Kind; 
  if(getEQ(oprkoper))
    return oprkoper.Name;
  end;

  return NULL;
end;

macro __SPOPER( Kind )
  file spoper("spoper");
  spoper.OperationCode = Kind; 
  if(getEQ(spoper))
    return spoper.OperationName;
  end;

  return NULL;
end;

macro FillReport( DeliveryKind )
  file typeac("typeac");

  typeac.iNumType = TA_STAT_DEL;
  typeac.Type_Account = DeliveryKind;
  if( getEQ(typeac) )
     return typeac.Name_type; 
  end;

  return NULL;
end;

macro IsGlobOp( SourceDocKind )

  if( (SourceDocKind == DP_DEPOPER_CONVERT) or
      (SourceDocKind == DP_DEPOPER_REPAY)   or 
      (SourceDocKind == DP_DEPOPER_BONEMISS) )
      return 1;
  end;

  return 0;
end;

macro PrintHeader()
   var HeaderStr;
   var KindName;
   var OpClassNames = "";
   var CountClass = 0;
   HeaderStr = "Ж У Р Н А Л  Д Е П О З И Т А Р Н Ы Х  О П Е Р А Ц И Й";
     DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, HeaderStr, 0, 0, false, null, null, tablewidth );

     DP_AddPrintCell( Rep,  "Установки фильтра", 30, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     Rep.AddStr();

   if(UserLog)
     DP_AddPrintCell( Rep,  "по журналу:", 30, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  __NameAlg(ALG_DEPO_DRAFTREFKIND, UserLog), tablewidth-30, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;
     
   if( Date_doc_beg <= {curdate} )
     DP_AddPrintCell( Rep,  "период исполнения:", 30, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  Date_doc_beg, 10, 0, "l:f" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  " - " + Date_doc_end, 20, 0, "l:f" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( Number_doc_beg  and (Number_doc_end) )
     DP_AddPrintCell( Rep,  "номера операций:", 30, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );

     DP_AddPrintCell( Rep,  Number_doc_beg+" - "+Number_doc_end, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( (OpClass_Inv == true) or (OpClass_Inf == true) or (OpClass_Adm == true) )
     DP_AddPrintCell( Rep,  "класс операций:", 30, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );

     if( OpClass_Inv == true )
       OpClassNames = "инвентарные";
       CountClass = CountClass + 1;
     end;

     if( OpClass_Inf == true )
       if( CountClass > 0 )
         OpClassNames = OpClassNames + ", ";
       end; 
       OpClassNames = OpClassNames + "информационные";
       CountClass = CountClass + 1;
     end;

     if( OpClass_Adm == true )
       if( CountClass > 0 )
         OpClassNames = OpClassNames + ", ";
       end; 
       OpClassNames = OpClassNames + "административные";
     end;

     DP_AddPrintCell( Rep,  OpClassNames, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;
  
   if(OpKindInv > 0)
     KindName = __OPRKOPER( OpKindInv );
     DP_AddPrintCell( Rep,  "вид операции:", 30, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  KindName, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;
   if(OpKindInf > 0)
     KindName = __SPOPER( OpKindInf );
     DP_AddPrintCell( Rep,  "вид операции:", 30, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  KindName, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;
   if(OpKindAdm > 0)
     KindName = __LLValues( OBJTYPE_ADMOPRTYPE, OpKindAdm );
     DP_AddPrintCell( Rep,  "вид операции:", 30, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  KindName, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   Rep.AddEmptyStr();
end;


macro GetOprParm(OpClass, OprXID, InitiatorName)

   file draft(spdraft);
   file globop(dpcorpop);
   file logopr(splogopr);
   file admo("depoadmo");

   if( OpClass == DP_INV_OPERATION )
     if( IsGlobOp( CurrSourceDocKind ) )
       globop.DocumentID = CurrSourceDocId;
       if(getEQ(globop))
         if( CodeFor(globop.OprXiD) != EMPTY_STRING)
           SetParm( 1, globop.OprXiD );
           SetParm( 2, globop.InitiatorName );
         end;
       end;
     else
       draft.AutoKey = CurrSourceDocId;
       if(getEQ(draft))
         if( CodeFor(draft.OprXiD) != EMPTY_STRING)
           SetParm( 1, IIF(CurrIsOprXid2 == SET_CHAR, draft.OprXiD2, draft.OprXiD) );
           SetParm( 2, draft.InitiatorName );
         end;
       end;
     end;
   elif( OpClass == DP_ADM_OPERATION )
     admo.AdmoprID = CurrSourceDocId;
     if(getEQ(admo))
       if( CodeFor(admo.OprXiD) != EMPTY_STRING)
         SetParm( 1, admo.OprXiD );
         SetParm( 2, admo.InitiatorName );
       end;
     end;
   elif( OpClass == DP_INF_OPERATION )
     logopr.AutoKey = CurrSourceDocId;
     if(getEQ(logopr))
       if( CodeFor(logopr.OprXiD) != EMPTY_STRING)
         SetParm( 1, logopr.OprXiD );
         SetParm( 2, logopr.InitiatorName );
       end;
     end;
   else
     SetParm( 1, "" );
   end;

   return "";
end;

macro GetDocStr()
  var AltXlD = CurrAltXlD;
  var DocStr = "";
  
  DocStr = DocStr + "принято:\n" +string(date(CurrRegistrDate):f)+"\n"+string(time(CurrRegistrTime));
  DocStr = DocStr + "\nвходящий номер:\n" + CurrXLD;
  DocStr = DocStr + "\nвид документа:\n" + __LLValues( 1050, CurrKind );
  if(CodeFor(CurrAltXld) == EMPTY_STRING)
    AltXlD = "б/н";
  end;
  DocStr = DocStr + "\nреквизиты:\n №" + string(AltXld) + " от " + string(date(CurrSignedDate):f); 
  return DocStr;
end;

macro ДатаНачалаВыполненияОперации(OpClass)
   var data, query, cmd;
   var str = "";

   if( OpClass == DP_INV_OPERATION )
      query = "select t_Start_Date, t_Syst_Time from doproper_dbt where t_DocKind = ? and t_DocumentID = ?";
      cmd = DL_RSDCommand(query);
      cmd.AddParam(CurrSourceDocKind);
      cmd.AddParam(string(CurrSourceDocID));
      data = cmd.Execute();
      if( data.moveNext() )
        str = string(date(data.Start_Date):f)+" "+string(time(data.Syst_Time));
      end;
   elif( OpClass == DP_ADM_OPERATION )
      query = "select t_OperDate, t_OperTime"
              " from ddepoadmo_dbt "
              " where t_AdmoprID = ?";
      cmd = DL_RSDCommand(query);
      cmd.AddParam(CurrSourceDocID);
      data = cmd.Execute();
      if( data.moveNext() )
        str = string(date(data.OperDate):f)+" "+string(time(data.OperTime));
      end;
   elif( OpClass == DP_INF_OPERATION )
      query = "select t_CarryDate, t_CarryTime"
              " from dsplogopr_dbt "
              " where t_AutoKey = ? and t_State = " + SC_SPDRAFT_STATUS_CLOSE;
      cmd = DL_RSDCommand(query);
      cmd.AddParam(CurrSourceDocID);
      data = cmd.Execute();
      if( data.moveNext() )
        str = string(date(data.CarryDate):f)+" "+string(time(data.CarryTime));
      end;
   end;

   return str;
end;

macro GetOperDateTime(OpClass, oper_time:@variant)
  var data, cmd, query;
  var dt = date(0,0,0);

  oper_time = time(0,0,0);

  if( OpClass == DP_INV_OPERATION )
     query = "select t_End_Date from doproper_dbt where t_DocKind = ? "
             " and t_DocumentID = ? "
             " and t_Completed = 'X'";
     cmd = DL_RSDCommand(query);
     cmd.AddParam(CurrSourceDocKind);
     cmd.AddParam(string(CurrSourceDocID));
     data = cmd.Execute();
     if( data.moveNext() )
       dt = date(data.End_Date);
     end;
  elif( OpClass == DP_ADM_OPERATION )
     query = "select t_OperDate, t_OperTime"
             " from ddepoadmo_dbt "
             " where t_AdmoprID = ?";
     cmd = DL_RSDCommand(query);
     cmd.AddParam(CurrSourceDocID);
     data = cmd.Execute();
     if( data.moveNext() )
       oper_time = time(data.OperTime);
       dt = date(data.OperDate)
     end;
  elif( OpClass == DP_INF_OPERATION )
     query = "select t_CarryDate, t_CarryTime"
             " from dsplogopr_dbt "
             " where t_AutoKey = ? and t_State = " +SC_SPDRAFT_STATUS_CLOSE;
     cmd = DL_RSDCommand(query);
     cmd.AddParam(CurrSourceDocID);
     data = cmd.Execute();
     if( data.moveNext() )
       oper_time = time(data.CarryTime);
       dt = date(data.CarryDate);
     end;
  end;

  return dt;
end;

macro ДатаОкончанияВыполненияОперации(OpClass)
  var date_oper, time_oper;
  date_oper = GetOperDateTime(OpClass, @time_oper);

  if(date_oper != date(0,0,0))
    if(time_oper != time(0,0,0))
      return string(date_oper:f)+" "+string(time_oper);
    else
      return string(date_oper:f);
    end;
  else
    return "";
  end;
end;

macro ФактическаяДатаВыполненияОперации(OpClass)
  var date_oper, time_oper;
  var data, cmd, query;
  var dt = date(0,0,0);

  date_oper = date(0,0,0);
  time_oper = time(0,0,0);

  if( OpClass == DP_INV_OPERATION )
     if( IsGlobOp( CurrSourceDocKind ) )
       query =   " select t_FactOprDate, t_OprTime"
               + "   from ddpcorpop_dbt "
               + "  where t_DocumentID = ?";
       cmd = DL_RSDCommand(query);
       cmd.AddParam(CurrSourceDocID);
       data = cmd.Execute();
       if( data.moveNext() )
         time_oper = time(data.OprTime);
         date_oper = date(data.FactOprDate);
       end;
     else
       query =   " select t_FactOprDate, t_FactOprTime"
               + "   from dspdraft_dbt "
               + "  where t_AutoKey = ?";
       cmd = DL_RSDCommand(query);
       cmd.AddParam(CurrSourceDocID);
       data = cmd.Execute();
       if( data.moveNext() )
         time_oper = time(data.FactOprTime);
         date_oper = date(data.FactOprDate);
       end;
     end;
  elif( OpClass == DP_ADM_OPERATION )
     query =   " select t_SystDate, t_OperTime"
             + "   from ddepoadmo_dbt "
             + "  where t_AdmoprID = ?";
     cmd = DL_RSDCommand(query);
     cmd.AddParam(CurrSourceDocID);
     data = cmd.Execute();
     if( data.moveNext() )
       time_oper = time(data.OperTime);
       date_oper = date(data.SystDate)
     end;
  elif( OpClass == DP_INF_OPERATION )
     query = "select t_FactOprDate, t_FactOprTime"
             " from dsplogopr_dbt "
             " where t_AutoKey = ? and t_State = " +SC_SPDRAFT_STATUS_CLOSE;
     cmd = DL_RSDCommand(query);
     cmd.AddParam(CurrSourceDocID);
     data = cmd.Execute();
     if( data.moveNext() )
       time_oper = time(data.FactOprTime);
       date_oper = date(data.FactOprDate);
     end;
  end;


  if(date_oper != date(0,0,0))
    if(time_oper != time(0,0,0))
      return string(date_oper:f)+" "+string(time_oper);
    else
      return string(date_oper:f);
    end;
  else
    return "";
  end;
end;

macro GetOpClass_real( SourceDocKind )
  if( (SourceDocKind == SP_DEPOPER_BOOKORDER)     or   
      (SourceDocKind == SP_DEPOPER_TRANSFERORDER) or
      (SourceDocKind == SP_DEPOPER_ENROLMENT)     or   
      (SourceDocKind == SP_DEPOPER_RECORDER)      or    
      (SourceDocKind == SP_DEPOPER_WITHDRORDER) )
     return DP_INV_OPERATION;
   elif (   (SourceDocKind == DP_DEPOPER_CONVERT)       or     
      (SourceDocKind == DP_DEPOPER_REPAY)         or         
      (SourceDocKind == DP_DEPOPER_BONEMISS) )
     return DP_CRP_OPERATION;
   elif( SourceDocKind == SP_DEPOPER_ADMOPER)
     return DP_ADM_OPERATION;
   elif( SourceDocKind == SP_DEPOPER_INFOPER ) 
     return DP_INF_OPERATION;
   end;
  return 0;
end;


private macro ПолучитьВидОперации(OpClass, OpKind)
  
  var query, cmd, data;
  var Kind_Op = "";

            
  if( OpClass == DP_INV_OPERATION)
    if(GetOpClass_real(CurrSourceDocKind) == DP_INV_OPERATION) 
      query =   " select opr.t_Kind_Operation, opr.t_Name"
              + " from doprkoper_dbt opr, dspdraft_dbt draft"
              + " where opr.t_Kind_Operation = draft.t_Kind_Operation "
              + "   and draft.t_Kind = ? " 
              + "   and draft.t_AutoKey = ? " ;
    else // Корпоративные - DP_CRP_OPERATION
      query =   " select opr.t_Kind_Operation, opr.t_Name"
              + " from doprkoper_dbt opr, ddpcorpop_dbt crp "
              + " where opr.t_Kind_Operation = crp.t_Kind_Operation "
              + "   and crp.t_DocKind = ? " 
              + "   and crp.t_DocumentID = ? ";
    end;

    cmd = DL_RSDCommand(query);
    cmd.AddParam(CurrSourceDocKind);
    cmd.AddParam(CurrSourceDocID);
             
    data = cmd.Execute();
    if(data.moveNext())
      Kind_Op = "\""+string(data.Kind_Operation)+"\" "+string(data.Name);
    end;
  elif( OpClass == DP_ADM_OPERATION)
    query = " select llv.t_Code, llv.t_Name " +
       " from dllvalues_dbt llv, ddpadmopr_dbt admopr, ddepoadmo_dbt adm " +
       " where llv.t_List = " + OBJTYPE_ADMOPRTYPE +
       "   and llv.t_Element = admopr.t_Opertype " +
       "   and admopr.t_Item = adm.t_Item " +
       "   and admopr.t_OperKind = adm.t_OperKind " +
       "   and adm.t_AdmoprID = ? ";

    cmd = DL_RSDCommand(query);
    cmd.AddParam(CurrSourceDocID);
             
    data = cmd.Execute();
    if( data.MoveNext())
      Kind_Op = "\""+string(data.Code)+"\" "+string(data.Name);
    end;
  elif( OpClass == DP_INF_OPERATION)
     query = " select t_OperCode, t_OperationName"
             " from dspoper_dbt"
             " where t_OperationCode in "
             " ( select t_DocType from dspinfopr_dbt where t_SpinfdocID in ( select t_RecID from dsplogopr_dbt where t_AutoKey = ?) ) " ;
                    
    cmd = DL_RSDCommand(query);
    cmd.AddParam(CurrSourceDocID);
             
    data = cmd.Execute();
    if( data.MoveNext())
      Kind_Op = "\""+string(data.OperCode)+"\" "+string(data.OperationName);
    end;
  end;

  return Kind_Op;
end;

macro GetOprStr(OpClass, OpKind)
  var OprStr = "";
  var begin_date_time;
  var end_date_time;
  var fact_date_time;
  var kind_op;

  begin_date_time = ДатаНачалаВыполненияОперации(OpClass);
  end_date_time   = ДатаОкончанияВыполненияОперации(OpClass);
  fact_date_time  = ФактическаяДатаВыполненияОперации(OpClass);
  kind_op         = ПолучитьВидОперации(OpClass, OpKind);
  OprStr = OprStr + "начало выполнения:\n"+string(begin_date_time);
  OprStr = OprStr + "\nисполнена:\n"+string(end_date_time);
  OprStr = OprStr + "\nдата факт. исполнения:\n"+string(fact_date_time);
  OprStr = OprStr + "\nкласс:\n"+__NameAlg(ALG_DEPO_OPCLASS, OpClass);
  OprStr = OprStr + "\nвид операции:\n"+kind_op;

  return OprStr;
end;

macro GetInitiator(OpClass)

  var InitiatorName = "";
  var Xid = "";

  GetOprParm(OpClass, Xid, InitiatorName );

  return InitiatorName;
end;

macro GetPartyDocStr(OpClass)
   var Party_Name;
   var PartyDocStr = "";
   var query, data, Select;
   var PartyCode;
   var AltXld;
   var stat = 0;

   PartyDocStr = PartyDocStr +"инициатор:\n"+ GetInitiator(OpClass)+"\nдокументы-приложения:";

   query =  "select spgr.t_Kind, spgr.t_Xld, spgr.t_AltXld, spgr.t_SignedDate "
            "  from dspground_dbt spgr, dspgrdoc_dbt grdoc "
            " where spgr.t_Direction <> ? /*1*/ "
            "   and spgr.t_SpgroundID = grdoc.t_SpgroundID "
            "   and grdoc.t_SourceDocKind = ? /*2*/ "
            "   and grdoc.t_SourceDocID = ? /*3*/ "; 
   
   Select = DL_RSDCommand(query);

   Select.AddParam(EXITING); /*1*/
   Select.AddParam(CurrSourceDocKind);   /*2*/
   Select.AddParam(CurrSourceDocID);        /*3*/

   data = Select.Execute();
   stat = data.moveNext();
   if(not stat)
     PartyDocStr = PartyDocStr+"\n- нет";
   else
     while(stat)
       AltXld = data.AltXld ;
       if(CodeFor(AltXld) == EMPTY_STRING)
         AltXld = "б/н";
       end;
       PartyDocStr = PartyDocStr+"\n- вх. №"+string(data.Xld)+", "+__LLValues( 1050, data.Kind )+" №"
                +string(AltXld)+" от "+string(date(data.SignedDate):f)+"\n";
       stat = data.MoveNext();
     end;
   end;

   return PartyDocStr; 
end;

macro GetAcntStr(OpClass)
  var Acnt_str = "";
  var data, query, Select;
  var KindOper = KINDOPER_NOTDEFINED;
  var DwAcc, DwAccName, DwName;
  var BnAcc, BnAccName, BnName;
  var cPayer, cReceiver, PayPTName, RecPTName;
  var opr_date, opr_time;
  
  if( (OpClass == DP_INV_OPERATION) and (not IsGlobOp(CurrSourceDocKind)) )
    query = " SELECT pm.t_Payer, " +
            "       rm.t_PayerName, " +
            "       pm.t_Receiver, " +
            "       rm.t_ReceiverName, " +
            "       payacc.t_briefcode RealDwAcc, " +
            "       payacc.t_Kind KindDwAcc, " +
            "       recacc.t_briefcode RealBnAcc, " +
            "       recacc.t_Kind KindBnAcc, " +
            "       payacc.t_name RealDwAccName, " +
            "       recacc.t_name RealBnAccName, " +
            "       pm.t_PayerBankID, " +
            "       pm.t_ReceiverBankID, " +
            "       pm.t_PayerAccount, " +
            "       pm.t_ReceiverAccount, " +
            "       pm.t_PayerDpNode, " +
            "       pm.t_ReceiverDpNode, " +
            "       (select t_ShortName from ddepoac_dbt where paypart.t_Type = t_ID) t_PayPartTypeName, " +
            "       (select t_ShortName from ddepoac_dbt where recpart.t_Type = t_ID) t_RecPartTypeName " +
            "  FROM dpmpaym_dbt pm, " +
            "       ddepoacnt_dbt paypart, " +
            "       ddepoacnt_dbt payacc, " +
            "       ddepoacnt_dbt recpart, " +
            "       ddepoacnt_dbt recacc, " +
            "       dpmrmprop_dbt rm " +
            " WHERE     pm.t_dockind = ? /*1*/ " +
            "       AND pm.t_documentid = ? /*2*/ " +
            "       AND rm.t_paymentid = pm.t_paymentid " +
            "       AND paypart.t_AutoKey = pm.t_PayerDPNode " +
            "       AND payacc.t_autokey = paypart.t_root  " +
            "       AND recpart.t_AutoKey = pm.t_ReceiverDPNode " +
            "       AND recacc.t_autokey = recpart.t_root ";

    Select = DL_RSDCommand(query);

    Select.AddParam(CurrSourceDocKind); /*1*/
    Select.AddParam(CurrSourceDocID);   /*2*/

    data = Select.Execute();
    if(data.moveNext())
      if(data.PayerBankID == {OurBank})
        DwAcc = string(data.RealDwAcc);
        DwAccName = string(data.RealDwAccName) + ",\n";
      else
        DwAcc = string(data.PayerAccount);
        DwAccName = "";
      end;

      DwName = string(data.PayerName);
      if(DwName == "" ) //получить наименование плательщика
        opr_date = GetOperDateTime(OpClass, @opr_time);
        cPayer = CDPParty(data.Payer, opr_date);
        DwName = cPayer.ShortName();
      end;

      if(data.ReceiverBankID == {OurBank})
        BnAcc = string(data.RealBnAcc);
        BnAccName = string(data.RealBnAccName) + ",\n";
      else
        BnAcc = string(data.ReceiverAccount);
        BnAccName = "";
      end;

      BnName = string(data.ReceiverName);
      if(BnName == "") //получить наименование получателя
        opr_date = GetOperDateTime(OpClass, @opr_time);
        cReceiver = CDPParty(data.Receiver, opr_date);
        BnName = cReceiver.ShortName();
      end;
      PayPTName = string(data.PayPartTypeName);
      RecPTName = string(data.RecPartTypeName);

      KindOper = DefineKindOperation(data.PayerDpNode, data.ReceiverDpNode);

      if((CurrSourceDocKind == SP_DEPOPER_BOOKORDER) and (KindOper == KINDOPER_TRANSFER))
        if(CurrIsOprXid2 == SET_CHAR)
          Acnt_str = Acnt_str + "с/д получателя:\n" + BnAcc + ",\n" + BnAccName + BnName;
        else
          Acnt_str = Acnt_str + "с/д отправителя:\n" + DwAcc + ",\n" + DwAccName + DwName;
        end;
      elif((CurrSourceDocKind == SP_DEPOPER_BOOKORDER) and (KindOper == KINDOPER_MOVEMENT))
        if(CurrIsOprXid2 == SET_CHAR)
          Acnt_str = Acnt_str + "с/д получателя:\n" + DwAcc + ",\n"  + "Раздел: " + data.PayerAccount + " (<" + PayPTName + ">),\n" +  DwAccName + DwName;
        else
          Acnt_str = Acnt_str + "с/д отправителя:\n" + BnAcc + ",\n" + "Раздел: " + data.ReceiverAccount + " (<" + RecPTName + ">),\n" + BnAccName + BnName;
        end;
      elif(KindOper == KINDOPER_ENROLMENT)
        Acnt_str = Acnt_str + "с/д поручения:\n" + BnAcc + ",\n" + BnAccName + BnName
                          + "\nс/д контрагента:\n" + DwAcc + ",\n" + DwAccName + DwName;
      else
        Acnt_str = Acnt_str + "с/д поручения:\n" + DwAcc + ",\n" + DwAccName + DwName
                          + "\nс/д контрагента:\n" + BnAcc + ",\n" + BnAccName + BnName;
      end;
    end;
  elif(OpClass == DP_INF_OPERATION)
    query = " SELECT "
              + " depoacnt.t_Code "
          + " FROM "
              + " ddepoacnt_dbt depoacnt "
             + " ,dspinfopr_dbt spinfopr "
             + " ,dsplogopr_dbt splogopr "
          + " WHERE "
              + " depoacnt.t_AutoKey = spinfopr.t_DepoAcc "
          + " AND spinfopr.t_SPInfDocID = splogopr.t_RecID "
          + " AND splogopr.t_AutoKey = ? ";

    Select = DL_RSDCommand(query);
    Select.AddParam(CurrSourceDocID);

    data = Select.Execute();
    if(data.moveNext())
      Acnt_str = string(data.Code);
    end;
  end;

  return Acnt_str;
end;

macro GetFinInstStr(OpClass, is_ap)
   var data, query, Select;
   var FinInstr_str = "", ko;

   if( (OpClass == DP_INV_OPERATION) and (not IsGlobOp(CurrSourceDocKind)))
     query = " select avr.t_ISIN, avr.t_LSIN, fin.t_Name, fin.t_SumPrecision, "
             "        pm.t_Amount, pm.t_PayerDpNode, pm.t_ReceiverDpNode "
             "   from davoiriss_dbt avr, dfininstr_dbt fin, dpmpaym_dbt pm "
             "  where pm.t_DocKind = ? /*1*/ "
             "    and pm.t_DocumentID = ? /*2*/ "
             "    and fin.t_FIID = pm.t_FIID "
             "    and avr.t_FIID = fin.t_FIID ";
     
     Select = DL_RSDCommand(query);

     Select.AddParam(CurrSourceDocKind); /*1*/
     Select.AddParam(CurrSourceDocID);   /*2*/

     data = Select.Execute();
     if(data.moveNext())
       ko = DefineKindOperation(data.PayerDpNode, data.ReceiverDpNode);
       if((OpClass == DP_INV_OPERATION) and (CurrSourceDocKind == SP_DEPOPER_BOOKORDER) and ((ko == KINDOPER_TRANSFER) or (ko == KINDOPER_MOVEMENT)))
         FinInstr_str = IIF(CurrIsOprXid2 == SET_CHAR, "Зачислено: ", "Списано: ");
       end;
       FinInstr_str = FinInstr_str + "идентификатор ц/б:\n";
       if( CodeFor(data.LSIN) != EMPTY_STRING )
         FinInstr_str = FinInstr_str + string(data.LSIN);
       end;
       FinInstr_str = FinInstr_str + "\n" + string(data.Name);
       if( CodeFor(data.ISIN) != EMPTY_STRING )
         FinInstr_str = FinInstr_str+"\nISIN "+string(data.ISIN);
       end;
       FinInstr_str = FinInstr_str+"\nколичество:\n";
       FinInstr_str = FinInstr_str+string(ЧислоCЗаданнойТочностью(double(data.Amount), DP_GetPrecision(data.Amount ,data.SumPrecision), IIF(is_ap, "a", "")));
     end;
   elif(IsGlobOp(CurrSourceDocKind))
     //зачисление
     query =   " select pmfi.t_FIID as BaseFIID, crp.t_Currency as CalcFIID, "
             + "        baseavr.t_LSIN as baseLSIN, basefin.t_Name as baseName, baseavr.t_ISIN as baseISIN, "
             + "        calcavr.t_LSIN as calcLSIN, calcfin.t_Name as calcName, calcavr.t_ISIN as calcISIN"
             + "   from ddpcorpop_dbt crp, ddpregstr_dbt reg, ddppmfi_dbt pmfi, "
             + "        dfininstr_dbt basefin, davoiriss_dbt baseavr, dfininstr_dbt calcfin, davoiriss_dbt calcavr"
             + "  where crp.t_DocumentID = ? /*1*/ "
             + "    and reg.t_DocumentID = crp.t_DocumentID "
             + "    and pmfi.t_RegisterID = reg.t_RegisterID "
             + "    and basefin.t_FIID = pmfi.t_FIID "
             + "    and baseavr.t_FIID = basefin.t_FIID "
             + "    and calcfin.t_FIID (+)= crp.t_Currency "
             + "    and calcavr.t_FIID (+)= crp.t_Currency ";

     Select = DL_RSDCommand(query);

     Select.AddParam(CurrSourceDocID); /*1*/

     data = Select.Execute();
     if(data.moveNext())
       FinInstr_str = "идентификатор ц/б (списание):\n";
       FinInstr_str = FinInstr_str + IIF(data.baseLSIN != "", "   "+data.baseLSIN+",\n", "");
       FinInstr_str = FinInstr_str + IIF(data.baseName != "", "   "+data.baseName+",\n", "");
       FinInstr_str = FinInstr_str + IIF(data.baseISIN != "", "   "+data.baseISIN+",\n", "");

       if(data.CalcFIID != ALLFININSTR)
         FinInstr_str = FinInstr_str + "\nидентификатор ц/б (зачисление):\n";
         FinInstr_str = FinInstr_str + IIF(data.calcLSIN != "", "   "+data.calcLSIN+",\n", "");
         FinInstr_str = FinInstr_str + IIF(data.calcName != "", "   "+data.calcName+",\n", "");
         FinInstr_str = FinInstr_str + IIF(data.calcISIN != "", "   "+data.calcISIN+",\n", "");
       end;
     end;
   end;

   return FinInstr_str;
end;

macro GetDopStr(OpClass)
   var data, query, Select, ndata, nSelect, data2, query2, Select2;
   var ИмяСубъекта;
   var str = "";
   var OprXid;
   var stat;
   var LSIN = "-";
   var opr_date, opr_time, cParty, repCnt = 0;
   var oprKind = -1, payer = -1, receiver = -1;

   file avoiriss(avoiriss);
////////////////
///описание операции
   if( (OpClass == DP_INV_OPERATION) and (IsGlobOp(CurrSourceDocKind)))
      query =   " select crp.t_Currency, crp.t_ConvForm, crp.t_ConvNumerator, crp.t_ConvDenominator " 
              + "   from ddpcorpop_dbt crp "
              + "  where crp.t_DocumentID = ? /*1*/ "  
              + "    and crp.t_DocKind = ? /*2*/";

      Select = DL_RSDCommand(query);

      Select.AddParam(CurrSourceDocID);   /*1*/
      Select.AddParam(CurrSourceDocKind); /*2*/

      data = Select.Execute();
      
      if( data.moveNext() )
        if( CurrSourceDocKind == DP_DEPOPER_CONVERT ) 
          avoiriss.FIID = data.Currency;
          if( getEQ(avoiriss) )
            LSIN = avoiriss.LSIN;
            if( CodeFor(LSIN) == EMPTY_STRING )
              LSIN = "-";                           
            end;
          end;
          str = str+"выпуск, в который конвертируются ц/б:\n"+string(LSIN)+"\n";
          str = str+"форма конвертации:\n"+__NameAlg(ALG_DEPO_FORMCONV, data.ConvForm)+"\n"; 
          str = str+"коэффициент конвертации:\n"+string(data.ConvNumerator)+"/"+string(data.ConvDenominator)+"\n";

        elif( CurrSourceDocKind == DP_DEPOPER_REPAY )
          str = "";
        elif( CurrSourceDocKind == DP_DEPOPER_BONEMISS )
          avoiriss.FIID = data.Currency;
          if( getEQ(avoiriss) )
            LSIN = avoiriss.LSIN;
            if( CodeFor(LSIN) == EMPTY_STRING )
              LSIN = "-";                           
            end;
          end;
          str = str+"ц/б, начисленные в качестве дохода:\n"+string(LSIN)+"\n";
          str = str+"коэффициент расчета:\n"+string(data.ConvNumerator)+"/"+string(data.ConvDenominator)+"\n";
        end;
      end;
   elif( OpClass == DP_ADM_OPERATION )
      query =   " select admopr.t_Code, admopr.t_Name, adm.t_PartyID, adm.t_Account, "
              + "        NVL((select acnt.t_BriefCode from ddepoacnt_dbt acnt where acnt.t_Autokey = adm.t_DepoAcc and acnt.t_Root = adm.t_DepoAcc), CHR(1)) as AcntBriefCode, "
              + "        NVL((select part.t_BriefCode from ddepoacnt_dbt part where part.t_Autokey = adm.t_DepoPart), CHR(1)) as PartBriefCode, "
              + "        NVL((select fin.t_FI_Code from dfininstr_dbt fin where fin.t_FIID = adm.t_FIID), CHR(1)) as FI_Code, "
              + "        NVL((select ic.t_Number from dinvcard_dbt ic where ic.t_InvCardID = adm.t_CertifID), CHR(1)) as InvCardNumber "
              + "   from ddpadmopr_dbt admopr, ddepoadmo_dbt adm "
              + "  where admopr.t_Item = adm.t_Item "
              + "    and admopr.t_OperKind = adm.t_OperKind "
              + "    and adm.t_AdmoprId = ? /*1*/ ";

      Select = DL_RSDCommand(query);

      Select.AddParam(CurrSourceDocID);   /*1*/

      data = Select.Execute();
      if( data.moveNext() == 0 )
        stat = 0;
      else
        stat = 1;
      end;
      if( stat )
        str = str+"адм. действие:\n"+string(data.Code)+" - "+string(data.Name);
        str = str + "\nобъекты операции:\n";

        //получить наименование субъекта
        opr_date = GetOperDateTime(OpClass, @opr_time);
        cParty = CDPParty(data.PartyID, opr_date);
        ИмяСубъекта = cParty.Name();
      
        if( ИмяСубъекта != "" )
         str = str + "субъект - "+ИмяСубъекта+"\n";
        end;
     ////счет депо
        if(data.AcntBriefCode != "")
          str = str+"счет депо - "+data.AcntBriefCode+"\n";
          var QueryAccPart = "select depoacnt.t_BriefCode as BriefCode"
                           + " from ddepoacnt_dbt depoacnt, ddpadmobj_dbt admobj, ddepoadmo_dbt depoadmo"
                           + " where DEPOADMO.T_ADMOPRID = ?"
                           + "    and ADMOBJ.T_ADMOPRID = DEPOADMO.T_ADMOPRID"
                           + "    and DEPOACNT.T_AUTOKEY = ADMOBJ.T_DEPOPART";

          var AccPartSelect = DL_RSDCommand(QueryAccPart);
          AccPartSelect.AddParam(CurrSourceDocID);
          var DataAccPart = AccPartSelect.Execute();

          while(DataAccPart.MoveNext())
            str = str + "раздел счета депо - " + DataAccPart.BriefCode + "\n";
          end;
        end;
       
     ///раздел счета
        if(data.PartBriefCode != "")
           str = str+"раздел счета депо - "+data.PartBriefCode+"\n";
        end;
     ///выпуск ц/б
        if(data.FI_Code != "")
          str = str+"выпуск - "+data.FI_Code+"\n";
        end;
     ///лицевой счёт
        if(string(data.Account) != "")
          str = str+"лицевой счет - "+data.Account+"\n";
        end;
        if(data.InvCardNumber != "")
          str = str+"инвентарная карточка - "+data.InvCardNumber+"\n";
        end;
      end;
   elif( OpClass == DP_INF_OPERATION )
      str = str+"отчеты:";
      query =   " select t_Kind, t_XLD, t_Party, t_PartyName "
              + "   from dspground_dbt "
              + "  where t_SourceDocKind = ? /*1*/ " 
              + "    and t_SourceDocID = ? /*2*/ "
              + "    and t_Direction = ? /*3*/ "  
              + "    and t_BackOffice = 'Z'"
              + "    and t_Sent = 'X'";

      Select = DL_RSDCommand(query);

      Select.AddParam(SP_DEPOPER_INFOPER);  /*1*/
      Select.AddParam(CurrSourceDocID);    /*2*/
      Select.AddParam(EXITING);            /*3*/

      data = Select.Execute();

      opr_date = GetOperDateTime(OpClass, @opr_time);

      while(data.moveNext())
        if(string(data.PartyName) == "")
          cParty = CDPParty(data.Party, opr_date);
          ИмяСубъекта = cParty.Name();

          if(ИмяСубъекта != "" )
            ИмяСубъекта = ", " + ИмяСубъекта;
          end;
        else
          ИмяСубъекта = ", " + string(data.PartyName);
        end;

        str = str + "\n Исх. №"+string(data.XLD)+", "
                  +__LLValues( 1050, data.Kind )
                  +string(ИмяСубъекта);
      end;

   end;
///////////////
///отчеты об исполнении
   query =   "select spgr.t_Xld, rep.t_RecipientName, spgr.t_Sent, "
           + "       (select llv.t_Name from dllvalues_dbt llv where llv.t_element = spgr.t_kind and llv.t_list = " + string(OBJTYPE_KINDDOC) + ") as KindName,"
           + "       rep.t_Recipient" 
           + "  from dspground_dbt spgr, ddpgrrep_dbt rep, ddprepdoc_dbt repdoc" 
           + " where spgr.t_SPgroundID = repdoc.t_RepSPgroundID "
           + "   and rep.t_ID = repdoc.t_GrRepID "
           + "   and rep.t_desc = ? /*1*/ "
           + "   and rep.t_SourceDocKind = ? /*2*/ "
           + "   and rep.t_SourceDocID = ? /*3*/"
           + "UNION "
           + "select spgr.t_Xld, NVL((select pt.t_Name from dparty_dbt pt where pt.t_PartyID = rep.t_Recipient), CHR(1)) as t_RecipientName, spgr.t_Sent, "
           + "       (select llv.t_Name from dllvalues_dbt llv where llv.t_element = spgr.t_kind and llv.t_list = " + string(OBJTYPE_KINDDOC) + ") as KindName,"
           + "       rep.t_Recipient" 
           + "  from dspground_dbt spgr, dspground_dbt spgr_in, ddpreglin_dbt lin, ddpreglinrep_dbt rep, ddpregstr_dbt reg, ddpcorpop_dbt crp " 
           + " where spgr_in.t_SpgroundID = ? /*4*/ "
           + "   and crp.t_DocKind        = ? /*5*/ "
           + "   and crp.t_DocumentID     = ? /*6*/ "
           + "   and reg.t_DocumentID     = crp.t_DocumentID "
           + "   and lin.t_RegisterID     = reg.t_RegisterID "
           + "   and rep.t_ReglineID      = lin.t_ReglineID "
           + "   and spgr.t_SPgroundID    = rep.t_RepSPgroundID ";

   nSelect = DL_RSDCommand("select count(1) nRecs from(" + query + ")");
   nSelect.AddParam(CurrSpgroundID);    /*1*/
   nSelect.AddParam(CurrSourceDocKind); /*2*/
   nSelect.AddParam(CurrSourceDocID);   /*3*/
   nSelect.AddParam(CurrSpgroundID);    /*4*/
   nSelect.AddParam(CurrSourceDocKind); /*5*/
   nSelect.AddParam(CurrSourceDocID);   /*6*/
   ndata = nSelect.Execute();

   if( ndata.MoveNext() )
      repCnt = int(sql_ConvTypeInteger(ndata.nRecs));
   end;

   str = str + "\nОтчеты об исполнении:";
   if( repCnt == 0 )
      str = str + "\nнет";
   else
      Select = DL_RSDCommand(query);
      Select.AddParam(CurrSpgroundID);    /*1*/
      Select.AddParam(CurrSourceDocKind); /*2*/
      Select.AddParam(CurrSourceDocID);   /*3*/
      Select.AddParam(CurrSpgroundID);    /*4*/
      Select.AddParam(CurrSourceDocKind); /*5*/
      Select.AddParam(CurrSourceDocID);   /*6*/
      data = Select.Execute();

      if( (OpClass == DP_INV_OPERATION) and (CurrSourceDocKind == SP_DEPOPER_UNIVOPERATION) )
         query2 = " select pm.t_Payer, pm.t_Receiver, "
                  "   rsb_depo.DefineKindOperation(pm.t_PayerDpNode, pm.t_ReceiverDpNode) OprKind "
                  "   from dpmpaym_dbt pm "
                  "  where pm.t_DocKind = ? /*1*/ "
                  "    and pm.t_DocumentID = ? /*2*/ ";

         Select2 = DL_RSDCommand(query2);
         Select2.AddParam(CurrSourceDocKind); /*1*/
         Select2.AddParam(CurrSourceDocID);   /*2*/
         data2 = Select2.Execute();

         if(data2.moveNext())
            oprKind  = data2.OprKind;
            payer    = data2.payer;
            receiver = data2.receiver;
         end;
      end;

      while( data.MoveNext() )
         // для операции перевода
         if( (OpClass == DP_INV_OPERATION) and (CurrSourceDocKind == SP_DEPOPER_UNIVOPERATION) and (oprKind == KINDOPER_TRANSFER) )
            if( repCnt == 1 )
               // в обеих строках по данной операции этот номер отчета
            elif( repCnt == 2 )
               // если один отправителю, второй получателю ц/б (как сейчас делается по умолчанию)
               if( not CurrIsOprXid2 )
                  // Для строки с номером о списании ц/б - номер отчета, предоставленного отправителю бумаг
                  if( payer != data.Recipient )
                     continue;
                  end;
               else
                  // Для строки с номером о зачислении ц/б - номер отчета, предоставленного получателю бумаг
                  if( receiver != data.Recipient )
                     continue;
                  end;
               end;
            elif( repCnt > 2 )
               // Если отчетов много (больше 2: например, 3 шт: отправителю, получателю и НБ/некоему третьему лицу)
               if( not CurrIsOprXid2 )
                  // Для строки с номером о списании ц/б - номер отчета, предоставленного отправителю бумаг +номер третьего отчета
                  if( receiver == data.Recipient )
                     continue;
                  end;
               else
                  // Для строки с номером о зачислении ц/б - номер отчета, предоставленного получателю бумаг+номер третьего отчета
                  if( payer == data.Recipient )
                     continue;
                  end;
               end;
            end;
         end;

         OprXid = data.Xld;
         if( CodeFor(OprXid) == EMPTY_STRING )
           OprXid = "б/н";
         end;

         str = str + "\n - исх №"+string(OprXid);

         /*if( (CurrSourceDocKind == DP_DEPOPER_CONVERT ) or
             (CurrSourceDocKind == DP_DEPOPER_REPAY ) or   
             (CurrSourceDocKind == DP_DEPOPER_BONEMISS ) )
           str = str + ",\n Отчет об исполнении поручения на глобальную операцию";
         else
           str = str + ",\n Отчет об исполнении";
         end;*/

         str = str + ",\n " + data.KindName;

         if(data.RecipientName != EMPTY_STRING)
           str = str + ",\n "+data.RecipientName;
         end;

         if(data.Sent == SET_CHAR)
           str = str + ", отправлен";
         end;

      end;
   end; 
////////////////////
   return str;
end;

macro PrintStr(count, NumOpr, Поручение_стр, Операция_стр,
               Инициатор_стр, Счета_стр, ЦенныеБумаги_стр, Примечания_стр,
               Исполнитель_стр, АвтоФорм)

   DP_AddPrintCell( Rep,  count, 0, 0, "c:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  NumOpr, 0, 0, "c:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  Поручение_стр, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  Операция_стр, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  Инициатор_стр, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  Счета_стр, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  ЦенныеБумаги_стр, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  Примечания_стр, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  Исполнитель_стр, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  АвтоФорм, 0, 0, "c:" + RepFontStyleTitel );
   Rep.AddStr();
end;

macro GetOpClass( SourceDocKind )

  Var opclass = GetOpClass_real(SourceDocKind);

  if( opclass == DP_CRP_OPERATION ) 
    opclass = DP_INV_OPERATION;
  end;

  return opclass;
end;


macro PrintOneStr(count, OpClass)

 var NumOpr;
 var Поручение_стр;
 var Операция_стр;
 var Инициатор_стр;
 var Счета_стр;
 var ЦенныеБумаги_стр;
 var Примечания_стр;
 var Исполнитель_стр;
 var InitiatorName = "";

 
 /*получим номер операции*/
 GetOprParm(OpClass, NumOpr, InitiatorName);
 /*формируем строку в поле "Поручение"*/
 Поручение_стр = GetDocStr();
 /*формируем строку в поле "Операция"*/
 Операция_стр = GetOprStr(OpClass, OpKind);
 /*формируем строку для поля "Инициатор"*/
 Инициатор_стр = GetPartyDocStr(OpClass);
 /*формируем строку для поля "Счета"*/
 Счета_стр = GetAcntStr(OpClass);
 /*формирование строки для поля "Ценные бумаги"*/
 ЦенныеБумаги_стр = GetFinInstStr(OpClass, is_ap);
 /*формирование строки для поля "Примечания"*/
 Примечания_стр = GetDopStr(OpClass);

 Исполнитель_стр = IIF(CurrOprIsMakeAuto == SET_CHAR, CurrTechAutoDocName, CurrOperNameAndCode);

 PrintStr(count, NumOpr, Поручение_стр, Операция_стр,
          Инициатор_стр, Счета_стр, ЦенныеБумаги_стр, Примечания_стр,
          Исполнитель_стр, CurrOprIsMakeAuto);

end;

private macro GetSqlDepoAccByOwner(owner, IsClient, iDepoAcc)
   var aDepoAcc;
   if(iDepoAcc != null)
      aDepoAcc = iDepoAcc;
   else
      aDepoAcc = 
      " SELECT depoacnt.t_AutoKey "+
      " FROM ddepoacnt_dbt depoacnt " +
      " WHERE depoacnt.t_Superior = 0 " +
      "   AND depoacnt.t_Department = " + iDepart +
      "   AND depoacnt.t_Kind = " + SPKK_PASSIVE +
      "   AND depoacnt.t_Status IN (" + SPSK_OPEN + "," + SPSK_NEW + ") " +
      "   AND depoacnt.t_Owner " + IIF(owner != null, "=" + owner, IIF(IsClient, "<>", "=") + {OurBank}) +
      "    START WITH depoacnt.t_Superior = 0 " +
      "    CONNECT BY PRIOR depoacnt.t_AutoKey = depoacnt.t_Superior ";
   end;

   return " select acnt.t_AutoKey from ddepoacnt_dbt acnt where acnt.t_Root IN (" + aDepoAcc + ")";
end;

macro GetInvSQLQuery(IsOprXid2)

  //делаем выборку инвентарных 
  var sQuery =   " select dr.t_OprXid" + IIF(IsOprXid2 == true, "2", "") + " as OprXid, s.*, "
               + " dr.t_AutoKey as CurrSourceDocID, dr.t_Kind as CurrSourceDocKind, "
               + " " + DP_INV_OPERATION + " as OpClass, CHR(0) as DetailAcnt, "
               + " NVL((select TO_CHAR(ps.t_Name||' ('||TO_CHAR(ps.t_Oper)||')') from dperson_dbt ps where ps.t_Oper = dr.t_Oper), CHR(1)) as OperNameAndCode, "
               + " CHR(0) as OprIsMakeAuto, "
               + " CHR(1) as TechAutoDocName, "
               + " CHR(" + IIF(IsOprXid2 == true, "88", "0") + ") as IsOprXid2 "
               + " from dspdraft_dbt dr, dspground_dbt s, dspdrmove_dbt mov1, dpmpaym_dbt paym1"
               + " where s.t_SPgroundID = dr.t_SPgroundID"
               + "   and s.t_backoffice = 'Z'"
               + "   and s.t_Department = " + iDepart 
               + "   and s.t_Direction  = " + ENTERING
               + "   and mov1.t_DraftID = dr.t_AutoKey "
               + "   and paym1.t_PaymentID = mov1.t_PaymentID ";  

  if( IsOprXid2 )
     sQuery = sQuery + " and dr.t_Kind = " + SP_DEPOPER_BOOKORDER
                     + " and rsb_depo.DefineKindOperation(paym1.t_PayerDpNode, paym1.t_ReceiverDpNode) IN (" + KINDOPER_TRANSFER + "," + KINDOPER_MOVEMENT + ") ";
  end;

  if( OpKindInv != 0 )
    sQuery = sQuery + " and s.t_SourceDocKind in ( select t_docKind from doprkoper_dbt where t_kind_operation = "+OpKindInv+" ) ";
  end;

  if( _inv_str != "" )
    sQuery = sQuery + " and dr.t_AutoKey in ( "+_inv_str+" ) ";
  else
    sQuery = sQuery + " and dr.t_Status = "+SC_SPDRAFT_STATUS_CLOSE+" and dr.t_OperState = 0 ";
  end;

  if(UserLog)
    sQuery = sQuery + " and s.t_UserLog = " + UserLog;
  end;

  if( (Date_doc_beg <= {curdate}) and (Date_doc_beg != date(0,0,0)) ) 
    sQuery = sQuery + "and paym1.t_ValueDate >= '"+Date_doc_beg+"' ";
  end;

  if( (Date_doc_end <= {curdate}) and (Date_doc_end != date(0,0,0)))
    sQuery = sQuery + " and paym1.t_ValueDate<= '"+Date_doc_end+"' ";
  end;

  if( Number_doc_beg )
    sQuery = sQuery + " and dr.t_OprXid" + IIF(IsOprXid2 == true, "2", "") + " >= '"+Number_doc_beg+"' ";
  end;

  if( Number_doc_beg )
    sQuery = sQuery + " and dr.t_OprXid" + IIF(IsOprXid2 == true, "2", "") + " <= '"+Number_doc_end+"' ";
  end;

  if( OpKindInv != 0 )
    sQuery = sQuery + " and dr.t_kind_operation = "+OpKindInv; 
  end;

  if( (iDepoAcc > 0) or
      (iDeponent != UnknownParty) or
      (IsOwnAcc and (not IsClientAcc)) or
      (IsClientAcc and (not IsOwnAcc))
  )
    sQuery = sQuery + " and (paym1.t_PayerDpNode IN ("+aDepoAcc+") or paym1.t_ReceiverDpNode IN ("+aDepoAcc+")) ";
  end;

  return sQuery;
end;

macro GetCrpSQLQuery()

  //делаем выборку глобальных операций
  var sQuery =   "select crp.t_OprXid as OprXid, s.*, "
               + " crp.t_DocumentID as CurrSourceDocID, crp.t_DocKind as CurrSourceDocKind, "
               + " " + DP_INV_OPERATION + " as OpClass, '" + IIF( DetailAcnt, "X", "" ) + "' as DetailAcnt, "
               + " NVL((select TO_CHAR(ps.t_Name||' ('||TO_CHAR(ps.t_Oper)||')') from dperson_dbt ps where ps.t_Oper = crp.t_Oper), CHR(1)) as OperNameAndCode, "
               + " CHR(0) as OprIsMakeAuto, "
               + " CHR(1) as TechAutoDocName, "
               + " CHR(0) as IsOprXid2 "
               + " from ddpcorpop_dbt crp, dspground_dbt s "
               + " where crp.t_DocKind != " + SP_DEPOPER_DIVIDEND
               + "   and s.t_SPgroundID = crp.t_SPgroundID ";

  if( OpKindInv != 0 )
    sQuery = sQuery + " and s.t_SourceDocKind in ( select t_docKind from doprkoper_dbt where t_kind_operation = "+OpKindInv+" ) ";
  end;

  if( _crp_str != "" )
    sQuery = sQuery + " and crp.t_DocumentID in ( "+_crp_str+" ) ";
  else
    sQuery = sQuery + " and crp.t_State = "+SC_SPDRAFT_STATUS_CLOSE+" and crp.t_OperState = 0 ";
  end;

  if(UserLog)
    sQuery = sQuery + " and s.t_UserLog = " + UserLog;
  end;

  if( (Date_doc_beg <= {curdate}) and (Date_doc_beg != date(0,0,0)) ) 
    sQuery = sQuery + "and crp.t_ValueDate >= '"+Date_doc_beg+"' ";
  end;

  if( (Date_doc_end <= {curdate}) and (Date_doc_end != date(0,0,0)))
    sQuery = sQuery + " and crp.t_ValueDate <= '"+Date_doc_end+"' ";
  end;

  if( Number_doc_beg )
    sQuery = sQuery + " and crp.t_OprXid >= '"+Number_doc_beg+"' ";
  end;

  if( Number_doc_beg )
    sQuery = sQuery + " and crp.t_OprXid <= '"+Number_doc_end+"' ";
  end;

  if( (iDepoAcc > 0) or
      (iDeponent != UnknownParty) or
      (IsOwnAcc and (not IsClientAcc)) or
      (IsClientAcc and (not IsOwnAcc))
  )
    sQuery = sQuery + " and exists( " +
               " SELECT 1 " +
               "   FROM ddpregstr_dbt dpregstr, ddpreglin_dbt reglin " +
               "  WHERE     dpregstr.t_DocumentID = crp.t_DocumentID " +
               "        AND reglin.t_RegisterID = dpregstr.t_RegisterID " +
               "        AND reglin.t_Type = " + DP_REGLINETYPE_DEPOACC +
               "        AND reglin.t_HolderAccID IN (" + aDepoAcc + ")" +
               " ) ";
  end;  

  return sQuery;
end;

macro GetInfSQLQuery()

  //делаем выборку информационных операций
  var sQuery =   "select logopr.t_OprXid as OprXid, s.*, "
               + " logopr.t_AutoKey as CurrSourceDocID, " + SP_DEPOPER_INFOPER + " as CurrSourceDocKind, "
               + " " + DP_INF_OPERATION + " as OpClass, CHR(0) as DetailAcnt, "
               + " NVL((select TO_CHAR(ps.t_Name||' ('||TO_CHAR(ps.t_Oper)||')') from dperson_dbt ps where ps.t_Oper = logopr.t_Oper), CHR(1)) as OperNameAndCode, "
               + " CHR(0) as OprIsMakeAuto, "
               + " CHR(1) as TechAutoDocName, "
               + " CHR(0) as IsOprXid2 "
               + " from dsplogopr_dbt logopr, dspground_dbt s "
               + " where s.t_SPgroundID = logopr.t_Draft " 
               + "   and s.t_backoffice = 'Z'"
               + "   and s.t_Department = " + iDepart
               + "   and s.t_Direction  = " + ENTERING
               + "   and logopr.t_NotRegOpr = CHR(0)";

  if( OpKindInf > 0 )
     sQuery = sQuery + " and logopr.t_RecID in (select t_SpinfDocID from dspinfopr_dbt where t_doctype = "+OpKindInf+" ) ";
  end;

  //отбираем только исполненные
  if( _inf_str != "" )
    sQuery = sQuery + " and logopr.t_AutoKey in ( "+_inf_str+" ) ";
  else
    sQuery = sQuery + " and logopr.t_State = "+SC_SPDRAFT_STATUS_CLOSE+" and logopr.t_OperState = 0 ";
  end;

  if(UserLog)
    sQuery = sQuery + " and s.t_UserLog = " + UserLog;
  end;

  if( (Date_doc_beg <= {curdate}) and (Date_doc_beg != date(0,0,0)) ) 
    sQuery = sQuery + "and logopr.t_carryDate >= '"+Date_doc_beg+"' ";
  end;

  if( (Date_doc_end <= {curdate}) and (Date_doc_end != date(0,0,0)))
    sQuery = sQuery + " and logopr.t_carryDate <= '"+Date_doc_end+"' ";
  end;

  if( Number_doc_beg )
    sQuery = sQuery + " and logopr.t_OprXid >= '"+Number_doc_beg+"' ";
  end;

  if( Number_doc_beg )
    sQuery = sQuery + " and logopr.t_OprXid <= '"+Number_doc_end+"' ";
  end;

  if( (iDepoAcc > 0) or
      (iDeponent != UnknownParty) or
      (IsOwnAcc and (not IsClientAcc)) or
      (IsClientAcc and (not IsOwnAcc))
  )
    sQuery = sQuery + " and exists( select 1 from dspinfopr_dbt spinfopr where spinfopr.t_SPInfDocID = logopr.t_recId and spinfopr.t_DepoAcc IN ("+aDepoAcc+")) ";
  end;

  return sQuery;
end;

macro GetAdmSQLQuery()

  //делаем выборку административных операций
  var sQuery =   "select adm.t_OprXid as OprXid, s.*, "
               + " adm.t_AdmoprID as CurrSourceDocID, " + SP_DEPOPER_ADMOPER + " as CurrSourceDocKind, "
               + " " + DP_ADM_OPERATION + " as OpClass, CHR(0) as DetailAcnt, "
               + " NVL((select TO_CHAR(ps.t_Name||' ('||TO_CHAR(ps.t_Oper)||')') from dperson_dbt ps where ps.t_Oper = adm.t_Oper), CHR(1)) as OperNameAndCode, "
               + " adm.t_IsMakeAuto as OprIsMakeAuto, "
               + " NVL((select llv.t_Name from dllvalues_dbt llv where llv.t_List = "+OBJTYPE_TECHAUTODOC+" and llv.t_Element = adm.t_TechAutoDoc), CHR(1)) as TechAutoDocName, "
               + " chr(0) as IsOprXid2 "
               + " from ddepoadmo_dbt adm, dspground_dbt s "
               + " where s.t_SPgroundID = adm.t_Ground "
               + "   and s.t_backoffice = 'Z'"
               + "   and s.t_Department = " + iDepart
               + "   and s.t_Direction  = " + ENTERING;

  if(OpKindAdm)
    sQuery = sQuery + " and Exists( select admopr.t_Opertype " +
                                  " from ddpadmopr_dbt admopr " +
                                  " where admopr.t_Item = adm.t_Item " +
                                  "   AND admopr.t_OperKind = adm.t_OperKind " +
                                  "   AND admopr.t_OperType = " + OpKindAdm + " ) ";
  end;

  if( _adm_str != "" )
    sQuery = sQuery + " and adm.t_AdmoprID in ( "+_adm_str+" ) ";
  end;              

  if(UserLog)
    sQuery = sQuery + " and s.t_UserLog = " + UserLog;
  end;

  if( (Date_doc_beg <= {curdate}) and (Date_doc_beg != date(0,0,0)) ) 
    sQuery = sQuery + " and adm.t_OperDate >= '"+Date_doc_beg+"' ";
  end;

  if( (Date_doc_end <= {curdate}) and (Date_doc_end != date(0,0,0)))
    sQuery = sQuery + " and adm.t_OperDate <= '"+Date_doc_end+"' ";
  end;

  if( Number_doc_beg )
    sQuery = sQuery + " and adm.t_OprXid >= '"+Number_doc_beg+"' ";
  end;

  if( Number_doc_beg )
    sQuery = sQuery + " and adm.t_OprXid <= '"+Number_doc_end+"' ";
  end;

  if( (iDepoAcc > 0) or
      (iDeponent != UnknownParty) or
      (IsOwnAcc and (not IsClientAcc)) or
      (IsClientAcc and (not IsOwnAcc))
  )
    sQuery = sQuery + " and adm.t_DepoAcc IN ("+aDepoAcc+") ";
  end;

  return sQuery;
end;

macro PrintDetailAcnt()
  var Select, ds, avr_str, acnt_str;

  if( not DetailAcnt )
    return;
  end;

  //детализация по счетам депо
  Select = DL_RSDCommand(  " select acnt.t_Name, acnt.t_BriefCode, pt.t_ShortName as OwnerShortName, lin.t_HRest, lin.t_BruttoAmount, "
                         + "        avrfrom.t_LSIN as FromLSIN, avrfrom.t_ISIN FromISIN, finfrom.t_Name as FromFinName, lin.t_FIIDfrom, "
                         + "        avrto.t_LSIN as ToLSIN, avrto.t_ISIN ToISIN, finto.t_Name as ToFinName, lin.t_FIIDTo, "
                         + "        finfrom.t_SumPrecision SumPrecisionFrom, finto.t_SumPrecision as SumPrecisionTo "
                         + "   from ddepoacnt_dbt acnt, dparty_dbt pt, dfininstr_dbt finto, davoiriss_dbt avrto, dfininstr_dbt finfrom, davoiriss_dbt avrfrom, "
                         + "        (select sum(rlin.t_HRest) as t_HRest, sum(rlin.t_BruttoAmount) as t_BruttoAmount, rlin.t_HolderAccID, rlin.t_FIIDfrom, rlin.t_FIIDTo "
                         + "           from ddpregstr_dbt reg, ddpreglin_dbt rlin, ddepoacnt_dbt acnt "
                         + "          where reg.t_DocumentID = ? /*1*/ "
                         + "            and rlin.t_RegisterID = reg.t_RegisterID "
                         + "            and rlin.t_Type = " + DP_REGLINETYPE_LACCOUNT
                         + "            and acnt.t_AutoKey = rlin.t_HolderAccID "
                         + "          group by rlin.t_HolderAccID, rlin.t_FIIDfrom, rlin.t_FIIDTo) lin "
                         + "  where acnt.t_AutoKey = lin.t_HolderAccID "
                         + "    and pt.t_PartyID = acnt.t_Owner "
                         + "    and finto.t_FIID (+)= lin.t_FIIDTo "
                         + "    and avrto.t_FIID (+)= lin.t_FIIDTo "
                         + "    and finfrom.t_FIID = lin.t_FIIDFrom "
                         + "    and avrfrom.t_FIID = finfrom.t_FIID "
                        );

  Select.AddParam(CurrSourceDocID);
  ds = Select.Execute();
  while(ds.moveNext())
    avr_str = "Списано " + ЧислоCЗаданнойТочностью(ds.HRest, DP_GetPrecision(ds.HRest, ds.SumPrecisionFrom)) + " шт.\n";
    avr_str = avr_str + IIF(ds.FromLSIN != "",    "   " + ds.FromLSIN+",\n",    "");
    avr_str = avr_str + IIF(ds.FromFinName != "", "   " + ds.FromFinName+",\n", "");
    avr_str = avr_str + IIF(ds.FromISIN != "",    "   " + ds.FromISIN+"\n",     "");

    if(ds.FIIDTo != ALLFININSTR)
      avr_str = avr_str + "\nЗачислено " + ЧислоCЗаданнойТочностью(ds.BruttoAmount, DP_GetPrecision(ds.BruttoAmount, ds.SumPrecisionTo)) + " шт.\n";
      avr_str = avr_str + IIF(ds.ToLSIN != "",    "   " + ds.ToLSIN+",\n",    "");
      avr_str = avr_str + IIF(ds.ToFinName != "", "   " + ds.ToFinName+",\n", "");
      avr_str = avr_str + IIF(ds.ToISIN != "",    "   " + ds.ToISIN+"\n",     "");
    end;

    acnt_str =  "с/д поручения:"
               +"\n   "+ds.BriefCode+","
               +"\n   "+ds.Name+","
               +"\n   "+ds.OwnerShortName;

    DP_AddPrintCell( Rep, "", 0, 0, "c:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, "", 0, 0, "c:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, "", 0, 0, "l:w" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, "", 0, 0, "l:w" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, "", 0, 0, "l:w" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, acnt_str, 0, 0, "l:w" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, avr_str, 0, 0, "l:w" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, "", 0, 0, "l:w" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, "", 0, 0, "l:w" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, "", 0, 0, "c:" + RepFontStyleTitel );
    Rep.AddStr();
  end;
end;

macro Report()

  if( not ( OpClass_Inv or OpClass_Inf or OpClass_Adm ) )
    return;
  end;

  var sQuery = "";

  if( OpClass_Inv )
    if( ( FromScrol == false ) or _inv_str )
      sQuery = sQuery +
          " ( " + GetInvSQLQuery() + " union all " + GetInvSQLQuery(true) + " ) ";
    end;

    if( ( FromScrol == false ) or _crp_str )
      if( sQuery != "" )
        sQuery = sQuery +
          " union all ";
      end;

      sQuery = sQuery +
          " ( " + GetCrpSQLQuery() + " ) ";
    end;
  end;

  if( OpClass_Inf )
    if( ( FromScrol == false ) or _inf_str )
      if( sQuery != "" )
        sQuery = sQuery +
          " union all ";
      end;

      sQuery = sQuery +
          " ( " + GetInfSQLQuery() + " ) ";
    end;
  end;

  if( OpClass_Adm )
    if( ( FromScrol == false ) or _adm_str )
      if( sQuery != "" )
        sQuery = sQuery +
          " union all ";
      end;

    sQuery = sQuery +
          " ( " + GetAdmSQLQuery() + " ) ";
    end;
  end;
    
  sQuery = sQuery +
            " order by 1 asc ";

  DataSet = TRsbDataSet( sQuery );
  while( DataSet.moveNext() )
    
    CurrSPgroundID      = DataSet.SPgroundID;
    CurrSourceDocKind   = DataSet.CurrSourceDocKind;
    CurrSourceDocID     = DataSet.CurrSourceDocID;
    CurrRegistrDate     = DataSet.RegistrDate;
    CurrRegistrTime     = DataSet.RegistrTime;
    CurrXLD             = DataSet.XLD;
    CurrKind            = DataSet.Kind;
    CurrAltXld          = DataSet.AltXld;
    CurrSignedDate      = DataSet.SignedDate;
    CurrOperNameAndCode = DataSet.OperNameAndCode;
    CurrOprIsMakeAuto   = DataSet.OprIsMakeAuto;
    CurrTechAutoDocName = DataSet.TechAutoDocName;
    CurrIsOprXid2       = DataSet.IsOprXid2;

    UseProgress(prCount);

    PrintOneStr(prCount, DataSet.OpClass);

    if( DataSet.DetailAcnt == SET_CHAR)
      PrintDetailAcnt()
    end;

    prCount = prCount + 1;
  end;

end;

macro GenRep( _iDepart:integer,
              _UserLog:integer,
              _IsOwnAcc:bool,
              _IsClientAcc:bool,
              _iDeponent:integer,
              _iDepoAcc:integer,
              _Date_doc_beg:date,
              _Date_doc_end:date,
              _Number_doc_beg:string,
              _Number_doc_end:string,
              _OpClass_Inv:bool,
              _OpClass_Inf:bool,
              _OpClass_Adm:bool,
              _OpKindInv:integer,
              _OpKindInf:integer,
              _OpKindAdm:integer,
              _DetailAcnt:bool,
              _ToExcel:bool,
              _is_ap:bool )

   var sQuery:string;
   iDepart        = _iDepart;
   UserLog        = _UserLog;
   IsOwnAcc       = _IsOwnAcc;
   IsClientAcc    = _IsClientAcc;
   iDeponent      = _iDeponent;
   iDepoAcc       = _iDepoAcc;
   Date_doc_beg   = _Date_doc_beg;
   Date_doc_end   = _Date_doc_end;
   Number_doc_beg = _Number_doc_beg;
   Number_doc_end = _Number_doc_end;
   OpClass_Inv    = _OpClass_Inv;
   OpClass_Inf    = _OpClass_Inf;
   OpClass_Adm    = _OpClass_Adm;
   OpKindInv      = _OpKindInv;
   OpKindInf      = _OpKindInf;
   OpKindAdm      = _OpKindAdm;
   DetailAcnt     = _DetailAcnt;
   ToExcel        = _ToExcel; 
   is_ap          = _is_ap;   

   if(iDepoAcc > 0)
     aDepoAcc = GetSqlDepoAccByOwner(iDeponent, null, iDepoAcc);
   elif( iDeponent != UnknownParty )
     aDepoAcc = GetSqlDepoAccByOwner(iDeponent, null, null);
   elif( IsOwnAcc and (not IsClientAcc) )
     aDepoAcc = GetSqlDepoAccByOwner(null, false, null);
   elif( IsClientAcc and (not IsOwnAcc) )
     aDepoAcc = GetSqlDepoAccByOwner(null, true, null);
   end;

   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) ); 
   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox
    
   PrintHeader();
   InitProgress( -1, "Идёт формирование отчёта...", "Отчет \"ЖУРНАЛ ОПЕРАЦИЙ ДЕПОЗИТАРИЯ\"" );
   

   Report();

   DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

   RemProgress();

   DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету - 
   DP_EndProtocol();                 //закончить протоколирование

   if( prCount > 1 )
     if( ToExcel )
       Rep.SetZoomType( ZOOM_TYPE_A4L );// альбомная ориентация
       Rep.PrintWinRep();
       Rep.ShowWinRep();
     else
       Rep.PrintRep();
     end;
   else
     MsgBox("Не найдено данных, удовлетворяющих параметрам отчёта.");
   end;
   
  return 0;
end;

macro PrintReportFromScrol( inv_str:string, adm_str:string, inf_str:string, crp_str:string, ToExcel: bool )
   var HeaderStr;
   OpKind = 0;
   OpClass_Inv    = true;
   OpClass_Inf    = true;
   OpClass_Adm    = true;
   CheckInfState  = false;
   FromScrol      = true;

   _inv_str = inv_str;
   _adm_str = adm_str;
   _inf_str = inf_str;
   _crp_str = crp_str;
   
   iDepart  = {OperDprt};
  
   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

   HeaderStr = "Ж У Р Н А Л  Д Е П О З И Т А Р Н Ы Х  О П Е Р А Ц И Й";
   DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, HeaderStr, 0, 0, false, null, null, tablewidth );
   InitProgress( -1, "Идёт формирование отчёта...", "Отчет \"ЖУРНАЛ ОПЕРАЦИЙ ДЕПОЗИТАРИЯ\"" );

   Report();

   DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

   RemProgress();

   DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
   DP_EndProtocol();                 //закончить протоколирование

   if( prCount > 1 )
     if( ToExcel )
       Rep.SetZoomType( ZOOM_TYPE_A4L );// альбомная ориентация
       Rep.PrintWinRep();
       Rep.ShowWinRep();
     else
       Rep.PrintRep();
     end;
   else
     MsgBox("Не найдено данных, удовлетворяющих параметрам отчёта.");
   end;

  return 0;

end;
