/*
$Name:         dpagacc.mac
$Module:       Депозитарий
$Description:  Отчет "Агенты счета депо"
*/

import deposerv, "pt_lib.mac";
import RsbDataSet, cb_sql;

var
  rep_date,
  deponent_id,
  depoacc_root_id,
  depoacc_part_id,
  isAuthEnd,  
  dateAuthBeg,
  dateAuthEnd,
  show_deponent,
  show_curator,
  show_cur_auth,
  show_operator,
  show_op_auth,
  show_agent,
  show_ag_auth,
  show_mortgagee,
  show_mort_auth;

const
  DEPOATTR_ATTNUM_OPERATOR = 4,
  физическое_лицо          = 2,
  SIDEBALANCE_PASSIVE      = 2;

var
    IsPrintDepoAcc = false,
    IsPrinted = false,
    IsPrintedAuthHead = false;

var dacnt; //TRsbDataSet по ddepoacnt_dbt

var HeadTable = "┌─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n"+
                "│                                                                                                                         │\n"+
                "├─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤";

var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

/* Печать полномочий */
private macro PrintAcAPAuth(apID:integer, role:integer, rootID:integer)
  var query = "select acapau.t_AuthKind, llv.t_name as t_AuthName, "
            + "       ((CASE acnt.t_AutoKey WHEN acnt.t_Root THEN 'Счет ' ELSE 'Раздел ' END) || acnt.t_BriefCode) as t_NodeName"
            + "  from ddepoacnt_dbt acnt, ddpacapau_dbt acapau, dllvalues_dbt llv"
            + " where acapau.t_APID = ? /*1*/" 
            + "   and acapau.t_AUNodeID = acnt.t_AutoKey"
            + "   and acnt.t_AutoKey = acapau.t_AUNodeID"
            + "   and llv.t_List = 1211" /*OBJTYPE_KINDDOC*/
            + "   and llv.t_element = acapau.t_AuthKind"
            + "   and acnt.t_AutoKey IN (select acnt2.t_AutoKey "
            + "                            from ddepoacnt_dbt acnt2 "
            + "                           where acnt2.t_root = acnt.t_root"
            + "                           start with acnt2.t_AutoKey = ? /*2*/" 
            + "                          connect by acnt2.t_Superior = prior acnt2.t_AutoKey)"
            + " order by acnt.t_BriefCode, acapau.t_AuthKind";

  var cmd = DL_RSDCommand(query);

  cmd.AddParam(apID);   /*1*/
  cmd.AddParam(rootID); /*2*/
  
  var dataSet = cmd.Execute();

  var lastNodeName = "";
  var flag = false;
  var RoleName = "";

  if(role == DEPOACAPROLE_OPERATOR )
    RoleName = "оператора";
  elif(role == DEPOACAPROLE_CURATOR )
    RoleName = "попечителя"; 
  elif(role == DEPOACAPROLE_MANAGER )
    RoleName = "распорядителя"; 
  elif(role == DEPOACAPROLE_MORTGAGEE )
    RoleName = "залогодержателя";
  end;

  while (dataSet.MoveNext())
    if (not flag)
      DP_AddPrintCell( Rep, "----------------------------------------------------------------------------------------------------------------------", tablewidth, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
      Rep.AddStr();
      DP_AddPrintCell( Rep, "                                            Полномочия "+RoleName, tablewidth, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      Rep.AddStr();
      flag = true;
    end;
    if (lastNodeName != dataSet.NodeName)
      DP_AddPrintCell( Rep, dataSet.NodeName + ":", tablewidth, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      Rep.AddStr();
      lastNodeName = dataSet.NodeName;
    end;
    DP_AddPrintCell( Rep, "\t" + dataSet.AuthName, tablewidth, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddStr();
  end;
  if (flag)
    Rep.AddEmptyStr();
  end;
end;

/* Печать инфо о документе-основании */
private macro PrintDocGroundAcap(APartyGround:integer, Role:integer)
  var query;  
  
  var cmd = DL_RSDCommand();

  if(Role == DEPOACAPROLE_MORTGAGEE)
    query = "select llv.t_Name as t_DocName, grnd.t_XLD, grnd.t_RegistrDate, grnd.t_PartyName, grnd.t_TerminateDate"
          + "  from  dspground_dbt grnd, dllvalues_dbt llv"
          + " where grnd.t_SPGroundID = ?"
          + "   and llv.t_List = 1050" /*OBJTYPE_KINDDOC*/
          + "   and llv.t_element = grnd.t_Kind";

    cmd.addParam(APartyGround);
  else
    query = "select llv.t_Name as t_DocName, grnd.t_XLD, grnd.t_RegistrDate, grnd.t_PartyName, grnd.t_TerminateDate"
          + "  from ddpprocur_dbt procur, dspground_dbt grnd, dllvalues_dbt llv"
          + " where procur.t_ProcurID = ? " 
          + "   and grnd.t_SPGroundID = procur.t_SPGroundID"
          + "   and llv.t_List = 1050" /*OBJTYPE_KINDDOC*/
          + "   and llv.t_element = grnd.t_Kind";

    cmd.addParam(APartyGround);
  end;
  
  var dataSet = cmd.Execute(query);

  if (dataSet.moveNext())
      DP_AddPrintCell( Rep,  dataSet.DocName + " №" + dataSet.XLD + " ",                33, 0, "l:"   + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  "выдан " + String(Date(dataSet.RegistrDate)),              18, 0, "l:"   + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  "в " + dataSet.PartyName,                                  42, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  "",                                                         1, 0, "l:"   + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  "действителен до " + String(Date(dataSet.TerminateDate)), 27, 0, "l:"   + RepFontStyleTitel, false, REP_ELEM_STR );
      Rep.AddStr();
  end;
end;

/* Печать уполномоченных лиц */
private macro PrintAcAP(role:integer, rootID:integer, show_auth:bool)
  var stat = false;
  var cParty = null;
  var query =  " select acap.*, na.t_sznamealg as RoleName" 
            +  " from ddpacap_dbt acap, dnamealg_dbt na";
  if(isAuthEnd)
    if(Role == DEPOACAPROLE_MORTGAGEE)
      query = query  + " , dspground_dbt grnd " ; 
    else
      query = query  + " , dspground_dbt grnd , ddpprocur_dbt procur " ; 
    end;
   end;

  query = query + " where acap.t_APNodeID = ? /*1*/ " 
                + "   and acap.t_Role = ? /*2*/"
                + "   and na.t_itypealg = 3266"/*ALG_DEPOACAPROLE*/  
                + "   and na.t_inumberalg = acap.t_role";            
 
  if(isAuthEnd)
    query = query + "   and ((acap.t_ToDate >= ? /*3*/ and acap.t_ToDate <= ? /*4*/) OR (acap.t_ToDate=TO_DATE('01.01.0001','DD.MM.YYYY') and  ? /*3*/ <= grnd.t_TerminateDate and  ? /*4*/ >= grnd.t_TerminateDate ))"; 
    if(Role == DEPOACAPROLE_MORTGAGEE)
      query = query + " and grnd.t_SPGroundID = acap.t_APartyGround ";
    else
      query = query + " and procur.t_ProcurID = acap.t_APartyGround "
                    + " and grnd.t_SPGroundID = procur.t_SPGroundID " ;                                                        
    end;
  else
    query = query + "   and (acap.t_FromDate <= ? /*3*/ and (acap.t_ToDate >= ? /*4*/ OR acap.t_ToDate=TO_DATE('01.01.0001','DD.MM.YYYY')))" ;
  end;

  var cmd = DL_RSDCommand(query);

  cmd.AddParam(rootID);   /*1*/
  cmd.AddParam(role);     /*2*/
  if(isAuthEnd)
    cmd.AddParam(dateAuthBeg); /*3*/
    cmd.AddParam(dateAuthEnd); /*4*/
    cmd.AddParam(dateAuthBeg); /*3*/
    cmd.AddParam(dateAuthEnd); /*4*/
  else
    cmd.AddParam(rep_date); /*3*/
    cmd.AddParam(rep_date); /*4*/
  end;
                                                                                                                                             	
  var dataSet = cmd.Execute();

  while (dataSet.moveNext())
    cParty = CDPParty( dataSet.APArtyID, rep_date );
    DP_AddPrintCell( Rep,  "**********************************************************************************************************************", tablewidth, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddStr();
    DP_AddPrintCell( Rep,  dataSet.RoleName + ":", 20, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( Rep,  "", 23, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( Rep,  cParty.Name() + " ИНН " + cParty.CodeINN(), tablewidth-43, 0, "l:w" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddStr();
    DP_AddPrintCell( Rep, "документ: " + cParty.PaperKindName() + " серия: " + cParty.PaperSeries()  + " №: " + cParty.PaperNumber(), tablewidth, 0, "l:w" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddStr();
    DP_AddPrintCell( Rep, "выдан: " + DateToStr( date( cParty.PaperIssuedDate() ), true ) + " " + cParty.PaperIssuerName(), tablewidth - 27 - 3, 0, "l:w" + RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( Rep, "Юрисдикция   " + cParty.CountryName(), 27, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddStr();

    PrintDocGroundAcap(dataSet.APartyGround, dataSet.Role);
    // Полномочия
    if (show_auth)
      PrintAcAPAuth(dataSet.ApID, dataSet.Role, rootID);
    end;
  end;

end;

/* печать секции депонента */
macro PrintDeponentSection()
  var cParty;
  if ( not IsPrinted )
    DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Агенты счета депо", 0, 0, false, rep_date, rep_date, tablewidth );
    IsPrinted = true;
  end;

  Rep.AddEmptyStr();
  DP_AddPrintCell( Rep,  "----------------------------------------------------------------------------------------------------------------------", tablewidth, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
  Rep.AddStr();
  DP_AddPrintCell( Rep,  "Список агентов счета депо", 26, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
  DP_AddPrintCell( Rep,  dacnt.BriefCode, tablewidth-26, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
  Rep.AddStr();
  DP_AddPrintCell( Rep,  "----------------------------------------------------------------------------------------------------------------------", tablewidth, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
  Rep.AddStr();
  if ( show_deponent )
    cParty = CDPParty( dacnt.owner, rep_date );
    DP_AddPrintCell( Rep,  "Владелец счета депо:", 20, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( Rep,  "", 23, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( Rep,  cParty.Name() + " ИНН " + cParty.CodeINN(), tablewidth-43, 0, "l:w" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddStr();
    DP_AddPrintCell( Rep, "документ: " + cParty.PaperKindName() + " серия: " + cParty.PaperSeries()  + " №: " + cParty.PaperNumber(), tablewidth, 0, "l:w" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddStr();
    DP_AddPrintCell( Rep, "выдан: " + DateToStr( date( cParty.PaperIssuedDate() ), true ) + " " + cParty.PaperIssuerName(), tablewidth - 27 - 3, 0, "l:w" + RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( Rep, "Юрисдикция   " + cParty.CountryName(), 27, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddStr();
  end;
end;

/* поиск собственной характеристики */
macro FindOwnDPATVL( DepoAccID )
  file depoatvl("depoatvl");

  ClearRecord( depoatvl );
  depoatvl.AttrNum = DEPOATTR_ATTNUM_OPERATOR;
  depoatvl.IsType  = "";
  depoatvl.ID      = DepoAccID;
  if ( ( getEQ(depoatvl) ) and ( depoatvl.IsOwn == "X" ) )
     return true;
  end;

  return false;
end;

macro checkDepoPart( dpacnt )
  if((depoacc_part_id != 0) AND ( depoacc_part_id != dpacnt.Autokey ) AND ( depoacc_part_id != dpacnt.Root ))
    return false;
  end;

  return true;
end;

macro PrintReport( ParentID, buf )
  var stat;

  //печатаем данные депонента
   PrintDeponentSection();

   if (show_curator)
     PrintAcAP(DEPOACAPROLE_CURATOR, ParentID, show_cur_auth);
   end;
   if (show_operator)
     PrintAcAP(DEPOACAPROLE_OPERATOR, ParentID, show_op_auth);
   end;
   if (show_agent)
     PrintAcAP(DEPOACAPROLE_MANAGER,  ParentID, show_ag_auth);
   end;
   if (show_mortgagee)
     PrintAcAP(DEPOACAPROLE_MORTGAGEE,  ParentID, show_mort_auth);
   end;
end;

private file dpacnt("depoacnt");

macro CorrectDepoPart( DepoPartID )
  if ( DepoPartID )
    ClearRecord(dpacnt);
    dpacnt.AutoKey = DepoPartID;
    if ( getEQ(dpacnt) and FindOwnDPATVL( dpacnt.AutoKey ) )
      depoacc_part_id = dpacnt.AutoKey;
    else
      CorrectDepoPart( dpacnt.Superior );
    end;
  end;
end;

macro DepoAccAgent(  iDepart          : integer,
                     _rep_date        : date,
                     _deponent_id     : integer,
                     _depoacc_root_id : integer,
                     _depoacc_part_id : integer,
                     _isAuthEnd       : bool,
                     _dateAuthBeg     : date,
                     _dateAuthEnd     : date,
                     _show_deponent   : bool,
                     _show_curator    : bool,
                     _show_cur_auth   : bool,
                     _show_operator   : bool,
                     _show_op_auth    : bool,
                     _show_agent      : bool,
                     _show_ag_auth    : bool,
                     _show_mortgagee  : bool,
                     _show_mort_auth  : bool,
                     ToExcel          : bool
                  )

  var stat,
      NumRec = 0,
      iCount = 0;

  rep_date        = _rep_date;
  deponent_id     = _deponent_id;
  depoacc_root_id = _depoacc_root_id;
  depoacc_part_id = _depoacc_part_id;
  isAuthEnd       = _isAuthEnd;  
  dateAuthBeg     = _dateAuthBeg;
  dateAuthEnd     = _dateAuthEnd;
  show_deponent   = _show_deponent;
  show_curator    = _show_curator;
  show_cur_auth   = _show_cur_auth;
  show_operator   = _show_operator;
  show_op_auth    = _show_op_auth;
  show_agent      = _show_agent;
  show_ag_auth    = _show_ag_auth;
  show_mortgagee  = _show_mortgagee;
  show_mort_auth  = _show_mort_auth;

  var query;
  Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
  DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

  if ( depoacc_part_id )
    CorrectDepoPart( depoacc_part_id );
  end;

  /* подсчет данных */
  Message("Идет сбор данных для отчета...");

  var cmd = DL_RSDCommand();

  query = " select d1.t_BriefCode, d1.t_Owner, d1.t_AutoKey, d1.t_Root from ddepoacnt_dbt d1"
          + " where d1.t_AutoKey in ( ";

  query =  query + " select d2.t_Root from ddepoacnt_dbt d2"
          + " where d2.t_AutoKey > 0 "
          + " and d2.t_Kind = " + SIDEBALANCE_PASSIVE;

  if( depoacc_part_id > 0 )
    query = query + " and d2.t_AutoKey = ? ";
    cmd.AddParam(depoacc_part_id);
  end;

  query = query + " ) ";

  if( depoacc_root_id > 0 )
    query = query + " and d1.t_AutoKey = ? ";
    cmd.AddParam(depoacc_root_id);
  end;

  if( deponent_id != -1 )
    query = query + " and d1.t_Owner = ? ";
    cmd.AddParam(deponent_id);
  end;

  if( iDepart > 0 )
    query = query + " and d1.t_Department = ? ";
    cmd.AddParam(iDepart);
  end;

  NumRec = cmd.GetCount(query);
  if(NumRec > 0)
    InitProgress( NumRec, "Формирование отчета...", "Агенты счета депо");

    query = query + " ORDER BY t_Root";

    dacnt = cmd.Execute(query);
    while( dacnt.MoveNext() )
      IsPrintDepoAcc = false;
      PrintReport( dacnt.Root, dacnt );

      UseProgress( iCount = iCount + 1 );
    end;

    RemProgress();
  end;

  if ( IsPrinted )
    Rep.AddEmptyStr();
    DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

    DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
    DP_EndProtocol();                 //закончить протоколирование

    if( ToExcel )
      Rep.PrintWinRep();
      Rep.ShowWinRep();
    else
      Rep.PrintRep();
    end;
  else
    DP_EndProtocol();                 //закончить протоколирование
    MsgBox("Не найдено данных, удовлетворяющих параметрам отчета");
  end;

  return 0;
end;
