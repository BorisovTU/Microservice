/*
$Name: webcabrs.mac
$Module: Депозитарий
$Description: Выгрузка клиентских остатков для web-кабинета НРД
*/

import DlReport_h, dpstdrep, webcabpt;

private record reestr("dpregstr.dbt");


private macro SmartSetBuff(dest, src)
  if(ValType(src) == V_MEMADDR)
    SetBuff(dest, src);
  else
    Copy(dest, src);
  end;
end;


// Класс WR-отчета для выгрузки остатков в web-кабинет НРД
private class (DL_CReportTemplate) DP_CUploadClientRestsRep( v_reestr, _IsTemp, _ReglineID, _exp_date )
  private var IsTemp = _IsTemp, ReglineID = _ReglineID, exp_date = _exp_date;
  private var fi_arr = TArray(), ddpreglin_name, ddploroown_name;

  SmartSetBuff( reestr, v_reestr );

  ddpreglin_name = IIF(IsTemp, "ddpreglin_tmp", "ddpreglin_dbt");
  ddploroown_name = IIF(IsTemp, "ddploroown_tmp", "ddploroown_dbt");


  PRIVATE MACRO PrintMode()
    return DL_OUTREPORT_EXCEL;
  END;


  PRIVATE MACRO BeginReport()
    SetActiveSheet( "Лист1" );
  END;


  PRIVATE MACRO EndReport()
  END;


  private macro FindFI(FIID)
    var i = 0;
    var fi;

    while(i < fi_arr.Size())
      if(fi_arr[i].FIID() == FIID)
        fi = fi_arr[i];
        break;
      end;
      i = i + 1;
    end;

    return fi;
  end;


  private macro FindorAddFI(FIID)
    var fi = FindFI(FIID);

    if(valType(fi) == V_UNDEF)
      fi = DP_CArchWebUploadFI(FIID, exp_date);
      fi_arr[fi_arr.Size()] = fi;
    end;

    return fi;
  end;


  private macro Create()
    var query, DataSet;
    var cmd, count, i = 0, stat;
    var fi, oldFIID = 0, pt, oldPartyID = 0, Rest = 0.0;

    SetXLSXFormat();

    if(ReglineID) // Выберем строки из списка владельцев ЛОРО счета, ID ц/б возьмем из записей об остатках, только если она на этом счете - единственная
      query = " SELECT lr.t_PartyID t_HolderID, " +
              "        lr.t_Code, " +
              "        (CASE " +
              "           WHEN (SELECT COUNT(1) " +
              "                   FROM " + ddpreglin_name + " fi " +
              "                  WHERE fi.t_RegisterID = ? " +
              "                    AND fi.t_Type = 40 " +
              "                    AND fi.t_HolderAccID = (SELECT t_HolderAccID " +
              "                                              FROM " + ddpreglin_name + 
              "                                             WHERE t_RegLineID = ? )) = 1 " +
              "           THEN (SELECT fi.t_FIIDFROM " +
              "                   FROM " + ddpreglin_name + " fi " +
              "                  WHERE fi.t_RegisterID = ? " +
              "                    AND fi.t_Type = 40 " +
              "                    AND fi.t_HolderAccID = (SELECT t_HolderAccID " +
              "                                              FROM " + ddpreglin_name + 
              "                                             WHERE t_RegLineID = ? )) " +
              "           ELSE -1 " +
              "         END ) t_FIID, " +
              "        lr.t_Amount t_Rest, " +
              "        lr.t_INN, " +
              "        lr.t_PaperKind, " +
              "        lr.t_PaperSeries, " +
              "        lr.t_PaperNumber " +
              "   FROM " + ddploroown_name + " lr " +
              "  WHERE lr.t_RegLineID = ? " +
              "    AND lr.t_PartyID NOT IN (SELECT dp.t_PartyID FROM ddp_dep_dbt dp) " +
              "  ORDER BY t_HolderID, t_Code, t_FIID ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(reestr.RegisterID);
      cmd.AddParam(RegLineID);
      cmd.AddParam(reestr.RegisterID);
      cmd.AddParam(RegLineID);
      cmd.AddParam(RegLineID);
    else // Выберем строки об остатках, сгруппировав по владельцу, счету и ц/б
      query = " select regl.t_HolderID, dpac.t_Code, regl.t_FIIDFrom t_FIID, SUM(regl.t_HRest) t_Rest " +
              " from " + ddpreglin_name + " regl, ddepoacnt_dbt dpac " +
              " where regl.t_RegisterID = ? " +
              "   and regl.t_Type = 60 " +
              "   and regl.t_HolderID NOT IN (SELECT dp.t_PartyID FROM ddp_dep_dbt dp) " +
              "   and dpac.t_AutoKey = regl.t_HolderAccId " +
              " GROUP BY regl.t_HolderID, dpac.t_Code, regl.t_FIIDFrom " +
              " ORDER BY regl.t_HolderID, dpac.t_Code, regl.t_FIIDFrom ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(reestr.RegisterID);
    end;

    count = cmd.GetCount();

    if( count > 0 )
      DataSet = cmd.Execute();

      RegisterTable( "N1_MainTable", NULL,
                     "N1_Acc",
                     "N1_ID",
                     "N1_OGRN",
                     "N1_TXID",
                     "N1_RBIC",
                     "N1_CCPT",
                     "N1_INCR",
                     "N1_BIRT",
                     "N1_FCCP",
                     "N1_LICS",
                     "N1_OTHR",
                     "N1_NSDR",
                     "N1_FIIN",
                     "N1_CORP",
                     "N1_ISIN",
                     "N1_GRN",
                     "N1_Country",
                     "N1_NSD_Code",
                     "N1_Rest"
                  );

      InitProgress( count, "Экспорт остатков клиентов для web-кабинета НРД", "Экспорт остатков клиентов");

      while(DataSet.moveNext())

        if((oldPartyID != DataSet.HolderID) OR (DataSet.HolderID == -1)) // Если субъект сменился или субъекта в строке нет, тогда пересоздавать нужно обязательно
          oldPartyID = DataSet.HolderID;

          pt = NULL;

          pt = DP_CArchWebUploadPT(DataSet.HolderID, exp_date);

          if(ReglineID)
            pt.ActuateData(DataSet.INN, DataSet.PaperKind, DataSet.PaperSeries, DataSet.PaperNumber);
          end;
        elif((oldPartyID == DataSet.HolderID) AND ReglineID) // Субъект не сменился, но выборка по счетам раскрывающим ЛОРО, в каждой строке свои данные, их нужно обновить
          pt.ActuateData(DataSet.INN, DataSet.PaperKind, DataSet.PaperSeries, DataSet.PaperNumber);
        end;

        if(oldFIID != DataSet.FIID)
          oldFIID = DataSet.FIID;
          fi = FindorAddFI(DataSet.FIID);
        end;

        Rest = ЧислоCЗаданнойТочностью(double(DataSet.Rest), DP_GetPrecision(DataSet.Rest, fi.SumPrecision()), "");

        PrintTableLine( "N1_Acc",      DataSet.Code,           null,
                        "N1_ID",       pt.ID(),                null,
                        "N1_OGRN",     pt.OGRN(),              null,
                        "N1_TXID",     pt.INN(),               null,
                        "N1_RBIC",     pt.BIC(),               null,
                        "N1_CCPT",     pt.PassNumber(),        null,  
                        "N1_INCR",     "",                     null, // Сертификат об инкорпорации - не заполняется, нет аналога
                        "N1_BIRT",     pt.BirthNumber(),       null,
                        "N1_FCCP",     pt.InterPassNumber(),   null,
                        "N1_LICS",     "",                     null, // Лицензия - не заполняется, нет аналога
                        "N1_OTHR",     "",                     null, // Иной тип документа - не заполняется, нет аналога
                        "N1_NSDR",     pt.NSDCode(),           null,
                        "N1_FIIN",     "",                     null, // Идентификационный номер иностранного инвестора - не заполняется, нет аналога
                        "N1_CORP",     "",                     null, // Корпоративная идентификация - не заполняется, нет аналога
                        "N1_ISIN",     fi.ISIN(),              null,
                        "N1_GRN",      fi.LSIN(),              null,
                        "N1_Country",  fi.IssuerLat2Country(), null,
                        "N1_NSD_Code", fi.NSDCode(),           null,
                        "N1_Rest",     Rest,                   null
                      );

        UseProgress(i = i + 1);
      end;

      RemProgress();

      EndTable();
    end;
  end;


  initDL_CReportTemplate( NULL, "webcabrs.xls" );

end;


// Точка входа в макрос
macro UploadClientRests( v_reestr,
                 IsTemp   : bool,
                 ReglineID : integer
)
  var RepObj;

  RepObj = DP_CUploadClientRestsRep(v_reestr, IsTemp, ReglineID, {curdate});

  RepObj.Run();

  return 0;
end;

