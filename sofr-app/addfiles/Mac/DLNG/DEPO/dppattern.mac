/*
$Name:         dppattern.mac
$Module:       Депозитарий
$Description:  Функции работы с шаблонами нумерации объектов депозитария
*/

Import DPInter, CTInter, FIInter, cb_sql, "MoCommon.mac"/*, "dlcnst.inc"*/;

var CounterPartition = 0; //счетчик кол-ва разделов для уникальности номера при автооткрытии
var CounterBreifPartition = 0; //счетчик кол-ва разделов для уникальности номера при автооткрытии
var CounterAccount   = 0; //счетчик кол-ва л/с дляуникальности номера при автооткрытии
var PrevFIID = 0;

private const 
  ACTIVE_CHAR = "A", 
  PASSIVE_CHAR = "P", 
  UNDEF_CHAR = "?";

private const 
  SIDEBALANCE_ACTIVE  = 1, 
  SIDEBALANCE_PASSIVE = 2;

private const
  DEPOATTR_ATTNUM_OPERATOR = 4,/* Характеристика "Оператор" */
  DEPOATTR_ATTNUM_STORE = 8;   /* Характеристика "Режим хранения" */


private const
  OBJGROUP_AVRTYPE711 = 1;

private const
  err_str = "Ошибка при формировании номера объекта по шаблону. ";

private const
  A_SYMB = "A", /* Счет депо */
  B_SYMB = "B", /* Балансовый счет */
  C_SYMB = "C", /* Сторона баланса */
  D_SYMB = "D", /* Код филиала */
  E_SYMB = "E", /* Эмитент ц/б */
  F_SYMB = "F", /* Владелец л/с */
  I_SYMB = "I", /* Режим хранения документарных ц/б */
  K_SYMB = "K", /* Контрольный код */
  L_SYMB = "L", /* LSIN */
  M_SYMB = "M", /* ISIN */
  N_SYMB = "N", /* Терминальный раздел */
  O_SYMB = "O", /* Раздел оператора */
  P_SYMB = "P", /* Раздел */
  R_SYMB = "R", /* Оператор раздела */
  T_SYMB = "T", /* Тип счета депо */
  V_SYMB = "V", /* Код ц/б для л/с */
  X_SYMB = "X", /* Символ */
  Y_SYMB = "Y", /* Вид ц/б для формы №0409711 */
  dl_SYMB = "d", /* Номер филиала */
  el_SYMB = "e", /* Порядковый номер л/с */
  tl_SYMB = "t"; /* Тип счета депо по назначению */

private var
  DepoCode = ""; /* Код счета депо или раздела */

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

/* Подобрать подходящий шаблон для объекта */
macro DP_GetPatternForObj(ObjKind, CodeKind, Object, FIID, Indoor, Issuer)
  var PatternID;
  var ObjName = "";

  PatternID = DP_GetSuitablePattern(ObjKind, CodeKind, Object, FIID, Indoor, Issuer);

  if  ( ObjKind == DPPATNUM_OBJECT_DEPOACC )
    ObjName = "счета депо";
  elif( ObjKind == DPPATNUM_OBJECT_DEPOPART )
    ObjName = "раздела счета депо";
  elif( ObjKind == DPPATNUM_OBJECT_FACEACC )
    ObjName = "лицевого счета депо";
  end; 

  if( PatternID == 0 )
    msgbox( err_str + "Не удалось подобрать шаблон для формирования номера " + ObjName);
  end;

  return PatternID;
end;


/* Получить вид ц/б для формы №0409711 */
private macro GetSecurKind0409711(fin, avr)
  var AttrID, q, rsd;

  if( GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(avr, OBJTYPE_AVOIRISS), OBJGROUP_AVRTYPE711, AttrID, NULL, NULL, NULL) )
    q = "SELECT obj.t_Name"
      + "  FROM dobjattr_dbt obj"
      + " WHERE obj.t_ObjectType = " + OBJTYPE_AVOIRISS 
      + " and obj.t_GroupID = " + OBJGROUP_AVRTYPE711 
      + " and obj.t_AttrID = " + AttrID;
    rsd = TRsbDataSet(q);
    if(rsd.MoveNext())
      return rsd.Name;
    else
      return "";
    end;
  else
   return "";
  end;
end;

/* Подсчитать кол-во счетов л/с депо */
private macro GetCountAccount()
  var q, rsd;

  q = " SELECT COUNT(1) iCount"
    + "  FROM daccount_dbt "
    + " WHERE t_Chapter = 5";

  rsd = TRsbDataSet(q);
  if( rsd.MoveNext() )
    return int(rsd.iCount+1)+CounterAccount;
  end;

  return CounterAccount;
end;

/* Найти шаблон, по кот. был сформирован номер для счета или раздела */
private macro GetPatternByDa(ID, Pattern, FIID, Indoor, Issuer)
  var q, rsd;
  var PatNum = "";
  var da = TBFile("depoacnt");
  var CodeKind = DPPATNUM_KINDCODE_UNDEF;

  da.rec.AutoKey = ID;
  if( da.getEQ() )
    if( (Pattern.Codekind == DPPATNUM_KINDCODE_CODE) or
        (Pattern.Codekind == DPPATNUM_KINDCODE_UNDEF) )
      CodeKind = DPPATNUM_KINDCODE_CODE;
      DepoCode = da.rec.Code;
    else
      CodeKind = DPPATNUM_KINDCODE_BRIEFCODE;              
      DepoCode = da.rec.BriefCode;
    end;
    PatNum = DP_ColletPattern( DP_GetPatternForObj(IIF(da.rec.Root==ID,DPPATNUM_OBJECT_DEPOACC,DPPATNUM_OBJECT_DEPOPART), Codekind, da, FIID, Indoor, Issuer) );
    if( PatNum == "" )
      msgbox( err_str + "Не найден шаблон, по которому был сформирован номер счета депо ", da.rec.Code );
    end;
  else
    if( ID )
      msgbox( err_str + "Не найден счет депо с ИД = ", ID );
    end;
  end;

  return PatNum;
end;

/* Подсчитать кол-во счетов депо */
private macro GetCountDepoAcRoot()
  var q, rsd;

  q = " SELECT COUNT(1) iCount"
    + "  FROM ddepoacnt_dbt "
    + " WHERE t_SKind = " + DEPO_ACCOUNT;

  rsd = TRsbDataSet(q);
  if( rsd.MoveNext() )
    return int(rsd.iCount+1);
  end;

  return 0;
end;

/* Подсчитать кол-во разделов у счета */
private macro GetCountDepoAcBySup(RootID, Codekind)
  var q, rsd;

  var Counter = 0;

  if(   Codekind == DPPATNUM_KINDCODE_CODE )
    Counter = CounterPartition;
  elif( Codekind == DPPATNUM_KINDCODE_BRIEFCODE )
    Counter = CounterBreifPartition;
  end;

  q = "     SELECT COUNT(1) iCount"
    + "       FROM ddepoacnt_dbt "
    + "      WHERE t_Root = " + RootID + " and level > 1 "
    + " START WITH t_Superior = 0 "
    + " CONNECT BY PRIOR t_AutoKey = t_Superior ";

  rsd = TRsbDataSet(q);
  if( rsd.MoveNext() )
    return Int(rsd.iCount+1)+Counter;
  end;

  return Counter;
end;

/* Получить подразделение */
private macro GetDepartment(DepID)
  var q, rsd;

  q = "SELECT dp.t_Name "
    + "  FROM ddp_dep_dbt dp "
    + " WHERE dp.t_Code = " + DepID;

  rsd = TRsbDataSet(q);
  if( rsd.MoveNext() )
    return string(rsd.Name);
  end;

  return "";
end;

/* Получить характеристику */
private macro FindDepoatvlVal(AttrNum, IsType, Id)
  file dpvl("depoatvl") key 0;

  ClearRecord(dpvl);
  dpvl.AttrNum = AttrNum;
  dpvl.IsType  = IsType;
  dpvl.ID      = Id;

  if( GetEQ(dpvl) )
    return dpvl.Value;
  end;

  return 0;
end;

/* Получить тип счета по назначению */
private macro FindDepoacByType(Id)
  var ll = TRecHandler("llvalues.dbt");      
  file dpac("depoac");

  ClearRecord(dpac);
  dpac.ID = Id;

  if( GetEQ(dpac) )
    if( LL_FindLLVALUES(OBJTYPE_DEPOKINDTYPE, dpac.Type, ll) == true )
       return ll.rec.Code;
    end;
  end;

  return "";
end;

/* IL 16.11.2000 создать контрольный код ISIN. Стандарт ISO-6166.
"Бумаги ценные.  Международная  система цифровой идентификации ценных бумаг (ISIN)".
Приложение А (нормативное).
 "Формула расчета контрольного разряда по модулю 10 'удвоить-сложить-удвоить'".
 Применимо ко всем кодам,  состоящим из цифр и букв  латинского алфавита.

1. Всем символам латинского алфавита присваиваются числовые значения,начиная с 10:
    A = 10    B = 11   C = 12   D = 13   E = 14   F = 15    G = 16
    H = 17    I = 18   J = 19   K = 20   L = 21   M = 22    N = 23
    O = 24    P = 25   Q = 26   R = 27   S = 28   T = 29    U = 30
    V = 31    W = 32   X = 33   Y = 34   Z = 35

2. Каждому символу кода присваивается коэффициент: 1 или 2. Самому правому
   символу присваивается коэффициент 2, предшествующему 1, затем 2, затем 1 
   и т.д., чередуя 2 и 1 до первого символа.

3. Начиная с самого левого,  числовое значение каждого символа умножается на
   присвоенный ему коэффициент. Полученные числа выписываются в одну цифровую 
   строку слева направо.

4. Все цифры полученной строки суммируются и вычитаются из ближайшего большего 
   или равного числа, оканчивающегося на 0.
 
Пример.
   RU21025RMFS - международное расширение кода 25-го выпуска ГКО.
   1. RU21025RMFS = 27 30 2 1 0 2  5 27 22 15 28
   2. Коэффициенты:  2  1 2 1 2 1  2  1  2  1  2
   3. Умножаем:     54 30 4 1 0 2 10 27 44 15 56
   Выписываем в строку: 543041021027441556
   4. Складываем: 5 + 4 + 3 + 0 + 4 + 1 + 0 + 2 + 1 + 0 + 2 + 7 + 4 + 4 + 1 + 5 + 5 + 6 = 54
      Вычитаем: 60 - 54 = 6
      Контрольный код: 6
   Итоговый код: RU21025RMFS6
*/
private macro GetControlCodISIN( str )
   var ch, decCode, CoeffStr = "",
       Len = StrLen( str );

   str = StrUpr( str );
   while( Len )
      ch = SubStr( str, Len, 1 );
      decCode = CodeFor(ch);
      /* ASCII коды заглавных латинских букв  A ...  Z */
      if( (decCode < CodeFor("A")) or (decCode > CodeFor("Z")) )
         if( (decCode < CodeFor("0")) or (decCode > CodeFor("9")) )
            msgbox( err_str + "В строке для генерации кода должны быть только цифры и латинские буквы" );
            return -1;
         end;
         decCode = Int( ch ); 
      else
         decCode = decCode - CodeFor("A") + 10; 
      end;
      if( Len/2*2 != Len ) decCode = decCode * 2;  end;
      CoeffStr = String(decCode) + CoeffStr;
      Len = Len - 1;
   end;

   decCode = 0;
   Len     = StrLen( CoeffStr );
   while( Len )
      decCode = decCode + Int( SubStr( CoeffStr, Len, 1 ) );
      Len = Len - 1;
   end;

   if( mod(decCode, 10) )
     return  (decCode/10*10+10 - decCode);
   else
     return 0;
   end;
end;

/* Получить тип счета депо */
private macro DepoacGetType(Type)
   file DepoAc( depoac ) key 0;
   var len, strResult, bFind;

   DepoAc.ID = Type;
   if (getEQ(DepoAc));
     len = strlen(DepoAc.CodeInAccount); 
     if(len == 0)
       return "0";
     else 
       strResult = StrSubst(DepoAc.CodeInAccount, " ", "0");           
       if (len == 1)
         return L_Z(strResult, 2);
       else       
         return SubStr(strResult, len-1, 2);
       end;
     end;             
   else
     return "0";
   end;     
end;

/* получить раздел уровня "оператор" */
macro GetOperPart( depoac )
  var OperPartID = 0;
  var err = 0;
  file dpac(depoacnt);
  file dpvl(depoatvl);

  dpvl.AttrNum = DEPOATTR_ATTNUM_OPERATOR; /* Оператор */
  dpvl.IsType  = "";
  dpvl.ID      = depoac.Type;

  if(GetEQ(dpvl))
    if( dpvl.IsOwn == "X" )
      OperPartID = depoac.AutoKey;
    else
      if(depoac.Superior)
        dpac.AutoKey = depoac.Superior;
        err = 0;
        while( ( NOT err ) AND ( err = GetEQ(dpac) ) )
          dpvl.ID = dpac.Type;
          if(GetEQ(dpvl))
            if( dpvl.IsOwn == "X" )
              OperPartID = dpac.AutoKey;
            else
              if( dpac.Superior )
                dpac.AutoKey = dpac.Superior;
              else
                err = 1;
              end;
            end;
          else
            Msgbox( err_str + "Ошибка поиска характеристики \"Оператор\" с ID = ", dpac.Type );
            err = 1;
          end;
        end;
      end;
    end;
  end;

  return OperPartID;
end;


/* Сформировать номер для л/с */
private macro FormNumberBySymbAcc(Pattern, depoac, Sym, PrevNum, FIID, Indoor, Issuer)
  file fin( fininstr );
  file avr( avoiriss );
  var PatNum = "";
  var Number = "";

  if( PrevFIID != FIID )
    CounterAccount = 0;
  end;

  PrevFIID = FIID;

  fin.FIID = FIID;
  if(GetEQ(fin))
    avr.FIID = FIID;
    if( (not GetEQ(avr)) and (fin.FI_KIND != 11))
      msgbox( err_str + "Не найдена ценная бумага с ID = ", FIID );
    end;
  else
    msgbox( err_str + "Не найден финансовый инструмент с ID = ", FIID );
  end;

  if  ( Sym.Symbol == A_SYMB ) /* Счет депо */
    PatNum = GetPatternByDa(depoac.Root, Pattern, FIID, Indoor, Issuer);
    Number = L_Z(SubStr(DepoCode, Index(PatNum,Sym.Symbol), Sym.Length), Sym.Length);
  elif( Sym.Symbol == B_SYMB ) /* Балансовый счет */
    Number = L_Z(depoac.Synt, Sym.Length);
  elif( Sym.Symbol == C_SYMB ) /* Сторона баланса */
    if  ( depoac.Kind == SIDEBALANCE_ACTIVE )
      Number = L_Z(ACTIVE_CHAR, Sym.Length);
    elif( depoac.Kind == SIDEBALANCE_PASSIVE )
      Number = L_Z(PASSIVE_CHAR, Sym.Length);
    else
      Number = L_Z(UNDEF_CHAR, Sym.Length);
    end;
  elif( Sym.Symbol == D_SYMB ) /* Код филиала */
    Number = L_Z(GetDepartment(depoac.Department), Sym.Length);
  elif( Sym.Symbol == E_SYMB ) /* Эмитент ц/б */
    Number = L_Z(ПолучитьКодСубъектаДляСчета(Issuer), Sym.Length);
  elif( Sym.Symbol == F_SYMB ) /* Владелец л/с */
    Number = L_Z(ПолучитьКодСубъектаДляСчета(depoac.Owner), Sym.Length);
  elif( Sym.Symbol == I_SYMB ) /* Режим хранения документарных ц/б */
    Number = L_Z(IIF(Indoor,"1","0"), Sym.Length);
  elif( Sym.Symbol == K_SYMB ) /* Контрольный код */
    Number = L_Z(string(GetControlCodISIN(PrevNum)), Sym.Length);
  elif( (Sym.Symbol == L_SYMB) and (fin.FI_KIND != 11) ) /* LSIN */
    if( avr.LSIN == "" )
      msgbox( err_str + "У ценной бумаги ", fin.Name, " не указан код LSIN" );
    else
      Number = L_Z(avr.LSIN, Sym.Length);
    end;
  elif( (Sym.Symbol == M_SYMB) and (fin.FI_KIND != 11) ) /* ISIN */
    if( avr.LSIN == "" )
      msgbox( err_str + "У ценной бумаги ", fin.Name, " не указан код ISIN" );
    else
      Number = L_Z(avr.ISIN, Sym.Length);
    end;
  elif( Sym.Symbol == N_SYMB ) /* Терминальный раздел */
    PatNum = GetPatternByDa(depoac.AutoKey, Pattern, FIID, Indoor, Issuer);
    Number = L_Z(SubStr(DepoCode, Index(PatNum,Sym.Symbol), Sym.Length), Sym.Length);
  elif( Sym.Symbol == O_SYMB ) /* Раздел оператора */
    PatNum = GetPatternByDa(GetOperPart(depoac), Pattern, FIID, Indoor, Issuer);
    Number = L_Z(SubStr(DepoCode, Index(PatNum,N_SYMB), Sym.Length), Sym.Length);
  elif( Sym.Symbol == P_SYMB ) /* Раздел */
    msgbox( err_str + "Задан неподходящий информационный элемент " + Sym.Symbol + " для лицевого счета депо" );
// #193597 to delete
//  elif( Sym.Symbol == R_SYMB ) /* Оператор раздела */
//    Number = L_Z(ПолучитьКодСубъектаДляСчета(depoac.Operator), Sym.Length);
  elif( Sym.Symbol == T_SYMB ) /* Тип счета депо */
    PatNum = GetPatternByDa(depoac.Root, Pattern, FIID, Indoor, Issuer);
    Number = L_Z(SubStr(DepoCode, Index(PatNum,Sym.Symbol), Sym.Length), Sym.Length);
  elif( Sym.Symbol == V_SYMB ) /* Код ц/б для л/с */
    if( fin.CodeInAccount == "" )
      msgbox( err_str + "У ценной бумаги ", fin.Name, " не указан код для лицевого счета" );
    else
      Number = L_Z(fin.CodeInAccount, Sym.Length);
    end;
  elif( (Sym.Symbol == Y_SYMB) and (fin.FI_KIND != 11)) /* Вид ц/б для формы №0409711 */
    Number = L_Z(string(GetSecurKind0409711(fin,avr)), Sym.Length);
  elif( Sym.Symbol == dl_SYMB ) /* Номер филиала */
    Number = L_Z(string(depoac.Department), Sym.Length);
  elif( Sym.Symbol == el_SYMB ) /* Порядковый номер л/с */
    Number = L_Z(string(GetCountAccount()), Sym.Length);
  elif( Sym.Symbol == tl_SYMB ) /* Тип счета депо по назначению */
    msgbox( err_str + "Задан неподходящий информационный элемент " + Sym.Symbol + " для лицевого счета депо" );
  else
    Number = L_Z("", Sym.Length);
    msgbox( err_str + "Неизвестный информационный элемент" );
  end;

  if( StrLen(Number)-Sym.Length > 0 )
    /* отрезаем слева, если не помещается */
    return SubStr(Number, StrLen(Number)-Sym.Length+1, Sym.Length);
  end;

  return Number;
end;

/* Сформировать номер для счета/раздела депо */
private macro FormNumberBySymbDepoac(Pattern, depoac, Sym, PrevNum)
  var PatNum = "";
  var Number = "";

  if  ( Sym.Symbol == A_SYMB ) /* Счет депо */
    if( depoac.SKind == DEPO_ACCOUNT )
      Number = L_Z(string(GetCountDepoAcRoot()), Sym.Length);
    else  /* для раздела берем соответсвующее значение от счета */
      PatNum = GetPatternByDa(depoac.Root, Pattern);
      Number = L_Z(SubStr(DepoCode, Index(PatNum,Sym.Symbol), Sym.Length), Sym.Length);
    end;
  elif( Sym.Symbol == B_SYMB ) /* Балансовый счет */
    Number = L_Z(depoac.Synt, Sym.Length);
  elif( Sym.Symbol == C_SYMB ) /* Сторона баланса */
    if  ( depoac.Kind == SIDEBALANCE_ACTIVE )
      Number = L_Z(ACTIVE_CHAR, Sym.Length);
    elif( depoac.Kind == SIDEBALANCE_PASSIVE )
      Number = L_Z(PASSIVE_CHAR, Sym.Length);
    else
      Number = L_Z(UNDEF_CHAR, Sym.Length);
    end;
  elif( Sym.Symbol == D_SYMB ) /* Код филиала */
    Number = L_Z(GetDepartment(depoac.Department), Sym.Length);
  elif( Sym.Symbol == E_SYMB ) /* Эмитент ц/б */
    msgbox( err_str + "Задан неподходящий информационный элемент " + Sym.Symbol + " для счета депо" );
  elif( Sym.Symbol == F_SYMB ) /* Владелец л/с */
    msgbox( err_str + "Задан неподходящий информационный элемент " + Sym.Symbol + " для счета депо" );
  elif( Sym.Symbol == I_SYMB ) /* Режим хранения документарных ц/б */
    Number = L_Z(string(FindDepoatvlVal(DEPOATTR_ATTNUM_STORE, "", depoac.AutoKey)), Sym.Length);
  elif( Sym.Symbol == K_SYMB ) /* Контрольный код */
    Number = L_Z(string(GetControlCodISIN(PrevNum)), Sym.Length);
  elif( Sym.Symbol == L_SYMB ) /* LSIN */
    msgbox( err_str + "Задан неподходящий информационный элемент " + Sym.Symbol + " для счета депо" );
  elif( Sym.Symbol == M_SYMB ) /* ISIN */
    msgbox( err_str + "Задан неподходящий информационный элемент " + Sym.Symbol + " для счета депо" );
  elif( Sym.Symbol == N_SYMB ) /* Терминальный раздел */
    Number = L_Z(string(GetCountDepoAcBySup(depoac.Root, Pattern.Codekind)), Sym.Length);
  elif( Sym.Symbol == O_SYMB ) /* Раздел оператора */
    Number = L_Z(string(GetCountDepoAcBySup(depoac.Root, Pattern.Codekind)), Sym.Length);
  elif( Sym.Symbol == P_SYMB ) /* Раздел */
    Number = L_Z(string(GetCountDepoAcBySup(depoac.Root, Pattern.Codekind)), Sym.Length);
  elif( Sym.Symbol == R_SYMB ) /* Оператор раздела */
    msgbox( err_str + "Задан неподходящий информационный элемент " + Sym.Symbol + " для счета депо" );
  elif( Sym.Symbol == T_SYMB ) /* Тип счета депо */
    Number = L_Z(string(DepoacGetType(depoac.Type)), Sym.Length);
  elif( Sym.Symbol == V_SYMB ) /* Код ц/б для л/с */
    msgbox( err_str + "Задан неподходящий информационный элемент " + Sym.Symbol + " для счета депо" );
  elif( Sym.Symbol == Y_SYMB ) /* Вид ц/б для формы №0409711 */
    msgbox( err_str + "Задан неподходящий информационный элемент " + Sym.Symbol + " для счета депо" );
  elif( Sym.Symbol == dl_SYMB ) /* Номер филиала */
    Number = L_Z(string(depoac.Department), Sym.Length);
  elif( Sym.Symbol == el_SYMB ) /* Порядковый номер л/с */
    msgbox( err_str + "Задан неподходящий информационный элемент " + Sym.Symbol + " для счета депо" );
  elif( Sym.Symbol == tl_SYMB ) /* Тип счета депо по назначению */
    Number = L_Z(string(FindDepoacByType(depoac.Type)), Sym.Length);
  else
    Number = L_Z("", Sym.Length);
    msgbox( err_str + "Неизвестный информационный элемент" );
  end;

  if( StrLen(Number)-Sym.Length > 0 )
    /* отрезаем слева, если не помещается */
    return SubStr(Number, StrLen(Number)-Sym.Length+1, Sym.Length);
  end;

  return Number;
end;

private macro FormNumberBySymb(Pattern, Object, Sym, PrevNum, FIID, Indoor, Issuer)
  var Number = "";
  Record da( depoacnt );

  SetBuff( da, Object );

  if  ( Sym.Symbol == X_SYMB )
    /* общие элементы */
    Number = mkstr(Sym.Stuff, Sym.Length);
  else
    /* Индивидуальные для каждого объекта */
    if( (Pattern.Object == DPPATNUM_OBJECT_DEPOACC) or
        (Pattern.Object == DPPATNUM_OBJECT_DEPOPART) )
      Number = Number + FormNumberBySymbDepoac(Pattern, da, Sym, Number+PrevNum);
    elif( Pattern.Object == DPPATNUM_OBJECT_FACEACC )
      Number = Number + FormNumberBySymbAcc(Pattern, da, Sym, Number+PrevNum, FIID, Indoor, Issuer);
    else
      msgbox( err_str + "Неизвестный вид объекта" );
    end;
  end;

  return Number;
end;

/* Сформировать номер объекта по шаблону */
macro DP_FormNumObjByPattern( PatternId, Object, FIID, Indoor, Issuer )
  var Number = "";
  var q, rsd;
  file Pattern("dppatnum");

  Pattern.PatternId = PatternId;
  if( getEQ(Pattern) )
    q = "   SELECT * "
      + "     FROM ddppatsym_dbt "
      + "    WHERE t_PatternID = " + Pattern.PatternID
      + " ORDER BY t_Order ASC ";

    rsd = TRsbDataSet(q);
    while( rsd.MoveNext() )
      Number = Number + FormNumberBySymb(Pattern, Object, rsd.GetRecord(), Number, FIID, Indoor, Issuer);
    end;
  else
    if( PatternId != 0 )
      msgbox( err_str + "Не найден шаблон к ИД = ", PatternId );
    end;
  end;

  return StrUpr(Number);
end;
