/*
$Name:         dprdistr.mac
$Module:       Депозитарий
$Description:  Отчет "Ведомость распределения доходов"
*/

import deposerv, "adress.mac", "pt_lib.mac";
import RsbDataSet;

const физическое_лицо = 2,        //для поля LegalForm из party.dbt
      выплачено    = "Да",
      не_выплачено = "Нет",
      BANKCODE_KOD = 1,
      BANKCODE_BICCBRF = 3,
      BANKCODE_BICISO = 6;

private const PW_OUTPAYMENT   = "O";  // Переводом, внешн.
private const PW_INPAYMENT    = "I";  // Переводом, внутр.
private const PW_CONSOLIDATED = "S";  // Переводом, сводн.

private const ALG_DEPO_PAYMENTTYPE = 3258; //Типы выплат доходов в Депозитарии

var DataCrp;
var sign = "   ";
var app_str = "";

var IsAnyIncomeData = false,
    IsHeadPrinted   = false,
    isPrinted = false;

var iCount_list  = 0,
    iCount_avoir = 0,
    iCount_legal = 0,
    iCount_human = 0,
    iCount_total = 0,
    legal_depolist = TArray,
    human_depolist = TArray,
    TotalAmount = $0,
    _split_group,
    _show_reg,
    _show_paid,
    _show_group_total,
    _count_record,
    _show_list_total;

var Total_legal_Quantity, Total_legal_BruttoAmount, Total_legal_TaxAmount, Total_legal_Amount;
var Total_human_Quantity, Total_human_BruttoAmount, Total_human_TaxAmount, Total_human_Amount;
var Total_avoir_Quantity, Total_avoir_BruttoAmount, Total_avoir_TaxAmount, Total_avoir_Amount;
var Total_list_Quantity     = 0,
    Total_list_BruttoAmount = $0,
    Total_list_TaxAmount    = $0,
    Total_list_Amount       = $0;

var dppmacc = TBfile("dppmacc.dbt");
var dppmacfi = TBfile("dppmacfi.dbt");
file depoacnt("depoacnt") key 0;
file spground("spground") key 6;
file regstr("dpregstr") key 1;

var HeadTable0 = "┌─────────────────────┬───────────────────┬─────────────┬───────────────────┬───────────────────┬────────────────────────┬────────────────┬───────────┬───────────────┬─────────────────────┬────────────────┐\n"+
                 "│      Счет депо      │   Наименование,   │ Юрисдикция, │Платёжные реквизиты│        КПП        │          Адрес         │   Количество   │   Ставка  │   Расчитано   │ Удержано источником │    Начислено   │\n"+
                 "│                     │    регистрация    │     ИНН     │                   │                   │  юридический/почтовый  │  ценных бумаг  │   налога  │               │      начисления     │                │\n"+
                 "├─────────────────────┼───────────────────┼─────────────┼───────────────────┼───────────────────┼────────────────────────┼────────────────┼───────────┼───────────────┼─────────────────────┼────────────────┤\n"+
                 "│          1          │         2         │      3      │         4         │         5         │            6           │        7       │     8     │       9       │         10          │       11       │\n"+
                 "├─────────────────────┴───────────────────┴─────────────┴───────────────────┴───────────────────┴────────────────────────┴────────────────┴───────────┴───────────────┴─────────────────────┴────────────────┤";

var HeadTable = "┌───────┬─────────────────────┬───────────────────┬─────────────┬───────────────────┬───────────────────┬────────────────────────┬────────────────┬───────────┬───────────────┬─────────────────────┬────────────────┐\n"+
                "│ Номер │      Счет депо      │   Наименование,   │ Юрисдикция, │Платёжные реквизиты│        КПП        │          Адрес         │   Количество   │   Ставка  │   Расчитано   │ Удержано источником │    Начислено   │\n"+
                "│  п/п  │                     │    регистрация    │     ИНН     │                   │                   │  юридический/почтовый  │  ценных бумаг  │   налога  │               │      начисления     │                │\n"+
                "├───────┼─────────────────────┼───────────────────┼─────────────┼───────────────────┼───────────────────┼────────────────────────┼────────────────┼───────────┼───────────────┼─────────────────────┼────────────────┤\n"+
                "│   1   │          2          │         3         │      4      │         5         │         6         │            7           │        8       │     9     │      10       │         11          │       12       │\n"+
                "├───────┴─────────────────────┴───────────────────┴─────────────┴───────────────────┴───────────────────┴────────────────────────┴────────────────┴───────────┴───────────────┴─────────────────────┴────────────────┤";

var HeadTable1 = "┌───────────────────────┬───────────────────────────┬────────────┬────────────┬────────────────┬────────────┬────────────┬───────────────────┬────────────┬───────────────┬─────────────────────┬────────────────┐\n"+
                 "│ Номер государственной │      Вид ценных бумаг     │   Номинал  │ Код валюты │ Период выплаты │     Д1     │     Д2     │ Количество ценных │   Ставка   │   Расчитано   │ Удержано источником │    Начислено   │\n"+
                 "│      регистрации      │                           │            │  номинала  │                │            │            │       бумаг       │            │               │      начисления     │                │\n"+
                 "├───────────────────────┼───────────────────────────┼────────────┼────────────┼────────────────┼────────────┼────────────┼───────────────────┼────────────┼───────────────┼─────────────────────┼────────────────┤\n"+
                 "│           1           │             2             │      3     │      4     │       5        │      6     │      7     │         8         │      9     │      10       │         11          │       12       │\n"+
                 "├───────────────────────┼───────────────────────────┼────────────┼────────────┼────────────────┼────────────┼────────────┼───────────────────┼────────────┼───────────────┼─────────────────────┼────────────────┤";

var DivRep = CMakeReport();
const FontStyleTitel =  "ex_FS(b)";
const FontStyleTitel1 =  "ex_FS()";

macro AddStr()
   DivRep.AddStr();
end;

macro PrintCell( Str, Big )
   if( (ValType(Big) != V_UNDEF) and Big )
     DP_AddPrintCell( DivRep, Str, strlen(Str) + 2, 0, "l:" + FontStyleTitel, app_str,REP_ELEM_STR );
   else
     DP_AddPrintCell( DivRep, Str, strlen(Str) + 2, 0, "l:" + FontStyleTitel1, app_str,REP_ELEM_STR );
   end;
end;

macro PrintCellTable( Str, S_Size, Big )
  if( (ValType(Big) != V_UNDEF) and Big )
    DP_AddPrintCell( DivRep, Str, S_Size, DataCrp.Point, "r:" + FontStyleTitel );
  else
    DP_AddPrintCell( DivRep, Str, S_Size, DataCrp.Point, "r:" + FontStyleTitel1 );
  end;
end;

macro PrintCellTableLeft( Str, S_Size, Big )
  if( (ValType(Big) != V_UNDEF) and Big )
    DP_AddPrintCell( DivRep, Str, S_Size, 0, "l:" + FontStyleTitel );
  else
    DP_AddPrintCell( DivRep, Str, S_Size, 0, "l:" + FontStyleTitel1 );
  end;
end;

macro PrintCellTableCenter( Str, S_Size, Big )
  if( (ValType(Big) != V_UNDEF) and Big )
    DP_AddPrintCell( DivRep, Str, S_Size, DataCrp.Point, "c:" + FontStyleTitel );
  else
    DP_AddPrintCell( DivRep, Str, S_Size, DataCrp.Point, "c:" + FontStyleTitel1 );
  end;
end;

macro PrintCellSize( Str, S_Size, Big )
  if( (ValType(Big) != V_UNDEF) and Big )
    DP_AddPrintCell( DivRep, Str, S_Size, 0, "r:" + FontStyleTitel, app_str, REP_ELEM_STR );
  else
    DP_AddPrintCell( DivRep, Str, S_Size, 0, "r:"  + FontStyleTitel1, app_str, REP_ELEM_STR );
  end;
end;

macro PrintCellSizeLeft( Str, S_Size, Big )
  if( (ValType(Big) != V_UNDEF) and Big )
    DP_AddPrintCell( DivRep, Str, S_Size, 0, "l:" + FontStyleTitel, app_str, REP_ELEM_STR );
  else
    DP_AddPrintCell( DivRep, Str, S_Size, 0, "l:"  + FontStyleTitel1, app_str, REP_ELEM_STR );
  end;
end;

macro PrintCellTableSize( Str, S_Size, Right, Format )
  if (ValType(Format) == V_UNDEF )
    Format = "";
  end;
  if( (ValType(Right) != V_UNDEF) and Right )
    DP_AddPrintCell( DivRep, Str, S_Size, DataCrp.Point, "r:" + Format + FontStyleTitel );
  else
    DP_AddPrintCell( DivRep, Str, S_Size, DataCrp.Point, "l:" + Format + FontStyleTitel );
  end;
end;

private macro GetFinCCY(FIID)
  file fin("fininstr");
  var CCY = "";

  fin.FIID = FIID;
  if( GetEQ(fin) )
    CCY = fin.CCY;
  end;

  return CCY;
end;

/*интерфейс в Namealg.dbt*/
private macro __NameAlg(Type, Number)
  var NameAlg = TBfile("namealg");
  NameAlg.rec.iTypeAlg = Type;
  NameAlg.rec.iNumberAlg = Number;
  if(NameAlg.GetEQ)
    return NameAlg.rec.szNameAlg;
  end;
  return "";
end;

macro GetPaymentTypeStr(PaymentType)
  var PaymentTypeName = __NameAlg(ALG_DEPO_PAYMENTTYPE, PaymentType);

  if(PaymentTypeName == "")
    PaymentTypeName = "не установлен";
  end;

  return PaymentTypeName;
end;

macro GetPartyStr( PartyID )
  file party("party");
  file obj("objcode");
  var PartyName = "", PartyCode = "";
  var Str = "";

  party.PartyID = PartyID;
  if( GetEQ(party) )
    PartyName = party.Name;

    obj.ObjectType = 3;
    obj.CodeKind   = 1;
    obj.ObjectID   = party.PartyID;
    if( GetEQ(obj) )
      PartyCode = obj.Code;
    end;
  end;

  if( ( PartyName == "" ) and ( PartyCode == "" ) )
    Str = "не установлен";
  else
    Str = PartyCode + "  " + PartyName;
  end;

  return Str;
end;

macro ПечататьДатыФормированияВедомости()
  var query = "", DataSet = null;
  var BeginDate = ZeroDate, EndDate = ZeroDate;

  query = " SELECT "
            + " NVL(MIN(paym.t_ValueDate), " + GetSQLDate( ZeroDate ) + ") AS BeginDate "
           + " ,NVL(MAX(paym.t_ValueDate), " + GetSQLDate( ZeroDate ) + ") AS EndDate "
        + " FROM "
            + " dpmpaym_dbt paym "
           + " ,ddppmacc_dbt pmacc "
        + " WHERE "
            + " paym.t_DocKind = " + SP_DEPOPER_DIVIDEND + " "
        + " AND paym.t_DocumentID = pmacc.t_DocumentID "
        + " AND paym.t_Purpose IN (" + PM_PURP_SECUR_INCOME + ", " + PM_PURP_RETURNINCOME + ") "
        + " AND paym.t_ReceiverDpNode = pmacc.t_HolderAccID "
        + " AND pmacc.t_DocumentID = " + DataCrp.DocumentID + " ";

  DataSet = TRsbDataSet( query );
  if( DataSet.MoveNext() )
    BeginDate = DataSet.BeginDate;
    EndDate = DataSet.EndDate;
  end;

  DivRep.AddEmptyStr();
  DivRep.AddEmptyStr();

  AddStr();

  if( BeginDate == EndDate )
    if( BeginDate == ZeroDate )
      BeginDate = {curdate};
    end;
    PrintCell("Дата формирования " + date_as_string( 1, BeginDate, false ) );
  else
    PrintCell("Период формирования " + date_as_string( 1, BeginDate, false ) + " - " + date_as_string( 1, EndDate, false ) );
  end;
end;

macro ПечататьПервичныйДокумент()
  var GroundStr = "не определен";
  file spgr("spground");
  file llval("llvalues");

  DivRep.AddEmptyStr();
  DivRep.AddEmptyStr();

  AddStr();
  PrintCell("Первичный документ");

  AddStr();
  spgr.SPgroundID = DataCrp.SPgroundID;
  if( GetEQ(spgr) )
    llval.List = 1050;
    llval.Element = spgr.Kind;
    if( GetEQ(llval) )
      GroundStr = llval.Name;
      GroundStr = GroundStr + " № " + spgr.AltXld;
      GroundStr = GroundStr + " от " + spgr.SignedDate;
      GroundStr = GroundStr + "   № входящий: " + spgr.Xld;
      GroundStr = GroundStr + "  зарегистрировано " + spgr.RegistrDate;
    end;
  end;
  PrintCell( sign + sign + GroundStr );
  AddStr();
end;

macro ПечататьПараметрыЦБ()
  var width = 30;
  var PartyStr = "";

  AddStr();
  PrintCell("Ценные бумаги");

  AddStr();
  PrintCellSizeLeft( sign + sign + "тип выплат", width );
  PrintCell( GetPaymentTypeStr( DataCrp.PaymentType ) );

  AddStr();
  PrintCellSizeLeft( sign + sign + "эмитент ценных бумаг", width );
  PartyStr = GetPartyStr( regstr.Issuer );
  PrintCell( PartyStr );

  AddStr();
  PrintCellSizeLeft( sign + sign + "место хранения/учета", width );
  PartyStr = GetPartyStr( regstr.StoragePlace );
  PrintCell( PartyStr );

  AddStr();
  PrintCellSizeLeft( sign + sign + "список-реестр", width );
  PrintCell( regstr.Number + "   дата сбора " + regstr.Date );
end;

macro ПечататьРасчетыПоВыпускам()
  var DataSet, cmd;
  var query;
  var stat;
  var split_all_cell = 9;
  var i = 0;
  var Format;
  var fiwarnts = TBfile( "fiwarnts.dbt" );
  var WarrantDate = ZeroDate;
  var PayPeriod = "";
  var ObjectID = 0;
  var D1 = "", D2 = "";
  var Rate = "";
  var Nal = 0.0;
  DivRep.AddEmptyStr();
  AddStr();
  PrintCell("Расчеты по выпускам ценных бумаг");

  DivRep.AutoScan( "[\n" + HeadTable1 + "]" );

  //делаем выборку данных
  query = " SELECT "
            + " iss.t_LSIN "
           + " ,avr.t_Name AS FiName "
           + " ,rsi_rsb_fiinstr.FI_GetNominalOnDate(fin.t_FIID, ?, 1) as FaceValue "
           + " ,fin.t_Point AS FiPoint "
           + " ,fin.t_SumPrecision "
           + " ,finF.t_CCY "
           + " ,pmfi.t_FIID "
           + " ,pmfi.t_Quantity "
           + " ,pmfi.t_IncomeValue AS IValue "
           + " ,pmfi.t_Amount "
           + " ,pmfi.t_BruttoAmount AS BAmount "
           + " ,pmfi.t_Point "
          " FROM "
            + " ddppmfi_dbt pmfi "
           + " ,dfininstr_dbt fin "
           + " ,davoiriss_dbt iss "
           + " ,davrkinds_dbt avr "
           + " ,dfininstr_dbt finF "
          " WHERE "
            + " pmfi.t_RegisterID = ? "
        + " AND fin.t_FIID = pmfi.t_FIID "
        + " AND finF.t_FIID = fin.t_FaceValueFI "
        + " AND iss.t_FIID = pmfi.t_FIID "
        + " AND avr.t_AvoirKind = fin.t_AvoirKind "
        + " AND avr.t_Fi_Kind = fin.t_Fi_Kind ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(regstr.Date);
  cmd.AddParam(regstr.RegisterID);

  DataSet = cmd.Execute();
  while( DataSet.moveNext() )
    if( i == 0 )
      Format = "ex_B(rlb)";
    else
      Format = "";
    end;

    if( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
      fiwarnts.Clear();
      fiwarnts.rec.FIID = DataSet.FIID;
      fiwarnts.rec.Number = DataCrp.WarrantNum;
      fiwarnts.GetEQ();
    end;

    DP_AddPrintCell( DivRep, DataSet.LSIN, 23, 0, "l:" + Format + FontStyleTitel1 );
    DP_AddPrintCell( DivRep, DataSet.FiName, 27, 0, "l:" + Format + FontStyleTitel1 );
    DP_AddPrintCell( DivRep, double( DataSet.FaceValue ), 12, DataSet.FiPoint + 2, "r:" + Format + FontStyleTitel1, app_str );
    DP_AddPrintCell( DivRep, DataSet.CCY, 12, 0, "l:" + Format + FontStyleTitel1 );
    if( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
      PayPeriod = fiwarnts.rec.PayPeriod;
    elif( ( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_COUPON )
       or ( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_NOMINAL )
       or ( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_ISSUE ) )
      WarrantDate = DP_GetFIWarntDate( DataSet.FIID, DataCrp.WarrantNum, DataCrp.PaymentType );

      if( ( DataCrp.WarrantNum != "" ) and ( WarrantDate != ZeroDate ) )
        PayPeriod = date_as_string( 1, WarrantDate, false ) + "/" + DataCrp.WarrantNum;
      elif( ( DataCrp.WarrantNum != "" ) and ( WarrantDate == ZeroDate ) )
        PayPeriod = DataCrp.WarrantNum;
      elif( ( DataCrp.WarrantNum == "" ) and ( WarrantDate != ZeroDate ) )
        PayPeriod = date_as_string( 1, WarrantDate, false );
      end;
    end;
    DP_AddPrintCell( DivRep, PayPeriod, 16, 0, "l:" + Format + FontStyleTitel1 );
    if( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
      ObjectID = UniID( fiwarnts, OBJTYPE_DIVIDENDS );
      D1 = ЧислоCЗаданнойТочностью( money( readNoteForObject( OBJTYPE_DIVIDENDS, ObjectID, 1, regstr.Date ) ), DataSet.Point, app_str );
      D2 = ЧислоCЗаданнойТочностью( money( readNoteForObject( OBJTYPE_DIVIDENDS, ObjectID, 2, regstr.Date ) ), DataSet.Point, app_str );
    end;
    DP_AddPrintCell( DivRep, D1, 12, 0, "r:" + Format + FontStyleTitel1 );
    DP_AddPrintCell( DivRep, D2, 12, 0, "r:" + Format + FontStyleTitel1 );
    DP_AddPrintCell( DivRep, DataSet.Quantity, 19, DP_GetPrecision( DataSet.Quantity, DataSet.SumPrecision ), "r:" + Format + FontStyleTitel1 );
    if( ( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS ) and ( fiwarnts.rec.DividendsRate != "" ) )
      Rate = fiwarnts.rec.DividendsRate;
    else
      Rate = ЧислоCЗаданнойТочностью( DataSet.IValue, DataSet.Point, app_str ) + " " + DataSet.CCY;
    end;
    DP_AddPrintCell( DivRep, Rate, 12, DataSet.Point, "r:" + Format + FontStyleTitel1 );
    DP_AddPrintCell( DivRep, DataSet.BAmount, 15, DataSet.Point, "r:" + Format + FontStyleTitel1, app_str );
    Nal = double( DataSet.BAmount ) - double( DataSet.Amount );
    DP_AddPrintCell( DivRep, Nal, 21, DataSet.Point, "r:" + Format + FontStyleTitel1, app_str );
    DP_AddPrintCell( DivRep, DataSet.Amount, 16, DataSet.Point, "r:" + Format + FontStyleTitel1, app_str );
    AddStr();
    Format ="";
    i = i + 1;
  end;
end;

macro GetPartyDocumStr(PartyID)
  var PartyStr = "";
  file pt("party");

  pt.PartyID = PartyID;
  if( GetEQ(pt) )
    PartyStr = pt.Name;
  end;

  return PartyStr;
end;

macro GetUrisdic(PartyID)
  var Urisdic = "";
  var INN = "";
  var pt = TRecHandler("party.dbt");

  if(ПолучитьСубъекта(PartyID, pt) == 0)
    Urisdic = DP_GetCountryByCodeLat3(pt.rec.NrCountry).Name;
    INN = ПолучитьКодСубъекта(PartyID, PTCK_INN);
    if((Urisdic != "") and (INN != ""))
      Urisdic = Urisdic + ", ИНН - ";
    end;
  end;

  return Urisdic + INN;
end;

macro GetPartyAdress(PartyID)
  var adress = "";
  file adr("adress");

  adr.PartyID = PartyID;
  adr.Type = 1;
  if( GetEQ(adr) )
    adress = adr.Adress;
  else
    //иначе ищем почтовый
    ClearRecord(adr);
    adr.PartyID = PartyID;
    adr.Type = 3;
    if( GetEQ(adr) )
      adress = adr.Adress;
    end;
  end;

  return adress;
end;

macro ПечататьВыплатыПоСчетамДляЦБ()
  var query;
  var DataSet;
  var DataMacfi;
  var form_str = "";
  var save_form = 0;
  var i = 1;
  var Nal;
  var ItogSum = 0, ItogBAmount = 0.0, ItogNal = 0.0, ItogAmount = 0.0, MaxItogSumPrec = 0;
  var OneItogSum = 0, OneItogBAmount = 0.0, OneItogNal = 0.0, OneItogAmount = 0.0, MaxOneItogSumPrec = 0;
  var AllItogSum = 0, AllItogBAmount = 0.0, AllItogNal = 0.0, AllItogAmount = 0.0, MaxAllItogSumPrec = 0;
  var stat = false;
  var split_cell = 0;
  var split_all_cell = 0;
  var j;
  var Format;
  var FirstColWidth = 0;
  var TableWidth = 0;
  var TaxProp = "";
  var TaxDate = ZeroDate;
  var INN = "", KPP = "";
  var RecAcc = "";
  var RecBankID = "";
  var RecBankCode = "";
  var RecBankName = "";
  var objcode = TRecHandler("objcode.dbt");

  if( _count_record )
    split_cell = 5;
    split_all_cell = 10;
  else
    split_cell = 4;
    split_all_cell = 9;
  end;

  //делаем выборку ц/б бумаг
  query = " SELECT "
            + " pmfi.t_FIID "
           + " ,iss.t_LSIN "
           + " ,fin.t_SumPrecision "
        + " FROM "
            + " ddppmfi_dbt pmfi "
           + " ,davoiriss_dbt iss "
           + " ,dfininstr_dbt fin "
        + " WHERE "
            + " pmfi.t_RegisterID = " + regstr.RegisterID + " "
        + " AND iss.t_FIID = pmfi.t_FIID "
        + " AND fin.t_FIID = pmfi.t_FIID ";
  DataSet = TRsbDataSet(query);

  DivRep.AddEmptyStr();
  AddStr();
  PrintCell("Выплаты по выпускам ценных бумаг");

  if( _count_record)
    DivRep.AutoScan( "[\n" + HeadTable + "]" );
    FirstColWidth = 128;
    TableWidth = 212;
  else
    DivRep.AutoScan( "[\n" + HeadTable0 + "]" );
    FirstColWidth = 120;
    TableWidth = 204;
  end;
  j = 0;
  Format = "ex_B(rlb)";

  while( DataSet.moveNext() )
    stat = true;
    ItogBAmount = 0.0;
    ItogSum = 0;
    ItogNal     = 0.0;
    ItogAmount  = 0.0;
    MaxItogSumPrec = 0;
    OneItogBAmount = 0.0;
    OneItogSum = 0;
    OneItogNal     = 0.0;
    OneItogAmount  = 0.0;
    MaxOneItogSumPrec = 0;
    save_form = 0;
    i = 1;
    if(j == 0)
      Format = "ex_B(rlb)";
    else
      Format = "";
    end;

    PrintCellTableSize("Выплаты по ценной бумаге " + DataSet.LSIN, TableWidth, false, Format);
    AddStr();
    Format = "";
    //делаем выборку выплат по счетам, группируя по виду субъекта - владельца счета
    query = " SELECT "
              + " macfi.* "
             + " ,pt.t_LegalForm "
             + " ,acnt.t_BriefCode "
             + " ,acnt.t_Name "
             + " ,acnt.t_Owner "
             + " ,pmac.t_TaxRate "
             + " ,macfi.t_BruttoAmount AS BAmount "
             + " ,pmac.t_TaxPrice "
             + " ,pmac.t_TaxPriceFI "
             + " ,pmac.t_PaymentWay "
             + " ,pmrac.t_Account "
             + " ,pmrac.t_BankIC "
             + " ,pmrac.t_BankName "
             + " ,pmrac.t_CorrAcc "
             + " ,pmrac.t_BankCorrID "
             + " ,pmrac.t_BankCorrName "
             + " ,sprg.t_RegistrDate "
          + " FROM "
              + " ddppmacfi_dbt macfi "
             + " ,ddepoacnt_dbt acnt "
             + " ,dparty_dbt pt "
             + " ,ddppmacc_dbt pmac "
             + " ,ddppmrac_dbt pmrac "
             + " ,dspground_dbt sprg "
          + " WHERE "
              + " macfi.t_FIID = " + DataSet.FIID + " "
          + " AND macfi.t_DocumentID = " + DataCrp.DocumentID + " "
          + " AND pmac.t_DocumentID = " + DataCrp.DocumentID + " "
          + " AND pmac.t_HolderAccID = macfi.t_HolderAccID "
          + " AND acnt.t_AutoKey = macfi.t_HolderAccID "
          + " AND pt.t_PartyID = acnt.t_Owner "
          + " AND pmrac.t_DocumentID = pmac.t_DocumentID "
          + " AND pmrac.t_HolderAccID = pmac.t_HolderAccID "
          + " AND sprg.t_SPGroundID = " + DataCrp.SPgroundID + " ";

    if( _show_paid == false)
      query = query +
            " AND pmac.t_State > 1 ";
    end;

    query = query +
          "   ORDER BY pt.t_LegalForm ";

    DataMacfi = TRsbDataSet(query);
    while( DataMacfi.moveNext() )

      if( save_form != DataMacfi.LegalForm )

        if( DataMacfi.LegalForm == 1 )
          form_str = "      Юридические лица";
        else
          form_str = "      Физические лица";
        end;

        if( (save_form != 0) and (save_form == 1) and ( _show_group_total ) )
          //AddStr();
          PrintCellTableSize("Итого по юридическим лицам", FirstColWidth, true);
          PrintCellTableSize(ЧислоCЗаданнойТочностью(ItogSum, DP_GetPrecision(ItogSum, DataSet.SumPrecision), app_str ), 16, true);
          PrintCellTableSize("", 11, true);
          PrintCellTableSize(ЧислоCЗаданнойТочностью(ItogBAmount, DataCrp.Point, app_str ), 15, true);
          PrintCellTableSize(ЧислоCЗаданнойТочностью(ItogNal, DataCrp.Point, app_str ), 21, true);
          PrintCellTableSize(ЧислоCЗаданнойТочностью(ItogAmount, DataCrp.Point, app_str ), 16, true);
          AddStr();
        end;

        if( _split_group )
          PrintCellTableSize(form_str, TableWidth);
          AddStr();
          i = 1;
        end;

        save_form = DataMacfi.LegalForm;
        ItogBAmount = 0.0;
        ItogSum     = 0;
        ItogNal     = 0.0;
        ItogAmount  = 0.0;
        MaxItogSumPrec = 0;
      end;

      //печатать строку в таблице
      //AddStr();
      if( _count_record )
        PrintCellTableCenter(string(i), 7);
      end;
      PrintCellTableLeft(DataMacfi.BriefCode + " " + DataMacfi.Name, 21);
      PrintCellTableLeft(GetPartyDocumStr(DataMacfi.Owner), 19);
      PrintCellTableLeft(GetUrisdic(DataMacfi.Owner), 13);

      TaxProp = "";
      if ((DataMacfi.PaymentWay == PW_INPAYMENT)
       OR (DataMacfi.PaymentWay == PW_OUTPAYMENT)
       OR (DataMacfi.PaymentWay == PW_CONSOLIDATED))    // Банковский перевод

        if (DataMacfi.CorrAcc != "")
          RecAcc = DataMacfi.CorrAcc;
          RecBankID = DataMacfi.BankCorrID;
          RecBankName = DataMacfi.BankCorrName;
        else
          RecAcc = DataMacfi.Account;
          RecBankID = DataMacfi.BankIC;
          RecBankName = DataMacfi.BankName;
        end;

        TaxDate = date(DataMacfi.RegistrDate);
        if (RecAcc != "")
          if (not (FindObjCodeForDate(PTLIST_ALLBANK, BANKCODE_BICCBRF, RecBankID, TaxDate, objcode)))
            RecBankCode = objcode.rec.Code;
          else
            if (not (FindObjCodeForDate(PTLIST_ALLBANK, BANKCODE_BICISO, RecBankID, TaxDate, objcode)))
              RecBankCode = objcode.rec.Code;
            else
              if (not (FindObjCodeForDate(PTLIST_ALLBANK, BANKCODE_KOD, RecBankID, TaxDate, objcode)))
                RecBankCode = objcode.rec.Code;
              end;
            end;
          end;
        end;

        if ((RecBankCode != "") and (RecBankName != ""))
          if (DataMacfi.CorrAcc != "")
            TaxProp = TaxProp + "Через ";
          end;
          TaxProp = TaxProp + RecAcc + " в " + RecBankCode + " " + RecBankName;
        end;

      end;
      PrintCellTableLeft(TaxProp, 19);
      SplitFullINN(GetPartyINN(DataMacfi.Owner, 1), INN, KPP);
      PrintCellTableLeft(KPP, 19);
      PrintCellTableLeft(GetPartyAdress(DataMacfi.Owner), 24);
      PrintCellTableLeft(ЧислоCЗаданнойТочностью(DataMacfi.Quantity, DP_GetPrecision(DataMacfi.Quantity, DataSet.SumPrecision), app_str ), 16);
      ItogSum = ItogSum + DataMacfi.Quantity;
      if(DataSet.SumPrecision > MaxItogSumPrec)
        MaxItogSumPrec = DataSet.SumPrecision;
      end;
      if(DataMacfi.TaxPrice > $0)
        PrintCellTable( string(DataMacfi.TaxPrice) + " " + GetFinCCY(DataMacfi.TaxPriceFI), 11);
      else
        PrintCellTable(string(double(DataMacfi.TaxRate)/double(pow(10, DataCrp.Point))) + "%",11);
      end;
      PrintCellTable(DataMacfi.BAmount,15);
      Nal = double(DataMacfi.BAmount) - double(DataMacfi.Amount);
      ItogNal = ItogNal + Nal;
      ItogBAmount = ItogBAmount + DataMacfi.BAmount;
      ItogAmount = ItogAmount + DataMacfi.Amount;
      PrintCellTable(Nal, 21);
      PrintCellTable(DataMacfi.Amount,16);
      AddStr();

      OneItogBAmount = OneItogBAmount+ DataMacfi.BAmount;
      OneItogSum     = OneItogSum + DataMacfi.Quantity;
      OneItogNal     = OneItogNal + Nal;
      OneItogAmount  = OneItogAmount + DataMacfi.Amount;
      if(DataSet.SumPrecision > MaxOneItogSumPrec)
        MaxOneItogSumPrec = DataSet.SumPrecision;
      end;

      AllItogBAmount = AllItogBAmount+ DataMacfi.BAmount;
      AllItogSum     = AllItogSum + DataMacfi.Quantity;
      AllItogNal     = AllItogNal + Nal;
      AllItogAmount  = AllItogAmount + DataMacfi.Amount;
      if(DataSet.SumPrecision > MaxAllItogSumPrec)
        MaxAllItogSumPrec = DataSet.SumPrecision;
      end;

      i = i + 1;
    end;

    if( (save_form != 0)  and ( _show_group_total ))
      if( save_form == 2)
        PrintCellTableSize("Итого по физическим лицам", FirstColWidth, true);
      else
        PrintCellTableSize("Итого по юридическим лицам", FirstColWidth, true);
      end;
      PrintCellTableSize(ЧислоCЗаданнойТочностью(ItogSum, DP_GetPrecision(ItogSum, MaxItogSumPrec), app_str ), 16, true);
      PrintCellTableSize("", 11, true);
      PrintCellTableSize(ItogBAmount, 15, true);
      PrintCellTableSize(ItogNal, 21, true);
      PrintCellTableSize(ItogAmount, 16, true);
      AddStr();
    end;

    if( save_form != 0 )
      PrintCellTableSize("Итого по ценной бумаге", FirstColWidth, true);
      PrintCellTableSize(ЧислоCЗаданнойТочностью(OneItogSum, DP_GetPrecision(OneItogSum, MaxOneItogSumPrec), app_str ), 16, true);
      PrintCellTableSize("", 11, true);
      PrintCellTableSize(OneItogBAmount, 15, true);
      PrintCellTableSize(OneItogNal, 21, true);
      PrintCellTableSize(OneItogAmount, 16, true);
      AddStr();

    end;
    j = j + 1;
  end;

  if( stat and (_show_list_total) )
    PrintCellTableSize("Общие итоги по списку", FirstColWidth, true, Format);
    PrintCellTableSize(ЧислоCЗаданнойТочностью(AllItogSum, DP_GetPrecision(AllItogSum, MaxAllItogSumPrec), app_str ), 16, true, Format);
    PrintCellTableSize("", 11, true, Format);
    PrintCellTableSize(AllItogBAmount, 15, true, Format);
    PrintCellTableSize(AllItogNal, 21, true, Format);
    PrintCellTableSize(AllItogAmount, 16, true, Format);
    AddStr();
  end;
end;

/* Отчет "Ведомость распределения доходов" */
macro RewardDistribution( NumDprt           : integer,
                          signed_date_beg   : date,
                          signed_date_end   : date,
                          order_AltXid      : string,
                          reg_date_beg      : date,
                          reg_date_end      : date,
                          order_Xid_beg     : string,
                          order_Xid_end     : string,
                          issuer_code_id    : integer,
                          domic_code_id     : integer,
                          split_group       : bool,
                          show_paid         : bool,
                          show_group_total  : bool,
                          count_record      : bool,
                          show_list_total   : bool,
                          toExcel           : bool,
                          is_ap             : bool,
                          PaymentType       : integer
                        )

   var stat, iCount, i;
   var listNOSTRO = TArray;
   var DataSetDPPMFI;
   var DataSetACCCRP;
   var query;
   var TableWidth;

   file dpacccrp("dpacccrp") key 0;

   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

   _split_group = split_group;
   _show_paid   = show_paid;
   _show_group_total = show_group_total;
   _count_record = count_record;
   _show_list_total = show_list_total;

   if( is_ap == true )
     app_str = "a";
   end;
   if (_count_record)
     TableWidth = 214;
   else
     TableWidth = 206;
   end;

   //делаем выборку данных
   query = " SELECT "
             + " crp.* "
            + " ,spgr.t_SPgroundID "
         + " FROM "
             + " ddpcorpop_dbt crp "
            + " ,dspground_dbt spgr "
            + " ,ddpregstr_dbt reg "
         + " WHERE "
             + " crp.t_DocKind = " + SP_DEPOPER_DIVIDEND + " "
         + " AND spgr.t_SPgroundID = crp.t_SPgroundID "
         + " AND reg.t_DocumentID = crp.t_DocumentID ";

   if( NumDprt > 0 )
     query = query +
           " AND crp.t_Department = " + NumDprt + " ";
   end;

   if( ( signed_date_beg != ZeroDate ) or ( signed_date_end != ZeroDate ) )
     query = query +
           " AND spgr.t_SignedDate >= " + GetSQLDate( signed_date_beg ) + " "
         + " AND spgr.t_SignedDate <= " + GetSQLDate( signed_date_end ) + " ";
   end;

   if( order_AltXid != ""  )
     query = query +
           " AND spgr.t_AltXld = '" + order_AltXid + "' ";
   end;

   if( ( reg_date_beg != ZeroDate ) or ( reg_date_end != ZeroDate ) )
     query = query +
           " AND spgr.t_RegistrDate >= " + GetSQLDate( reg_date_beg ) + " "
         + " AND spgr.t_RegistrDate <= " + GetSQLDate( reg_date_end ) + " ";
   end;

   if( ( order_Xid_beg != "" ) and ( order_Xid_end != "" ) )
     query = query +
           " AND spgr.t_Xld >= '" + order_Xid_beg + "' "
         + " AND spgr.t_Xld <= '" + order_Xid_end + "' ";
   end;

   if( issuer_code_id != -1 )
     query = query +
           " AND reg.t_Issuer = " + issuer_code_id + " ";
   end;

   if( domic_code_id != -1 )
     query = query +
           " AND reg.t_StoragePlace = " + domic_code_id + " ";
   end;

   if( PaymentType > 0 )
     query = query +
           " AND crp.t_PaymentType = " + PaymentType + " ";
   end;

   DataCrp = TRsbDataSet(query);
   iCount = 0;
   while( DataCrp.moveNext() )
     //для каждого поручения ищем список-реестр
     ClearRecord(regstr);
     KeyNum(regstr, 1);
     regstr.DocumentID = DataCrp.DocumentID;
     if( GetEQ(regstr) )
       DP_StdHeaderLO_Ex( DivRep, FontStyleTitel, "В Е Д О М О С Т Ь   Р А С П Р Е Д Е Л Е Н И Я   Д О Х О Д О В", 0, 0, true, null, null, TableWidth);
       //печатать даты формирования ведомости
       ПечататьДатыФормированияВедомости();
       //первичный документ
       ПечататьПервичныйДокумент();
       //параметры ц/б
       ПечататьПараметрыЦБ();
       //расчеты по ц/б
       ПечататьРасчетыПоВыпускам();
       //данные по ц/б по счетам
       ПечататьВыплатыПоСчетамДляЦБ();

       DP_StdFooter_Ex(DivRep, FontStyleTitel, NULL, NULL, TableWidth);
     end;
     iCount = iCount + 1;
   end;

   if( iCount == 0 )
     if(not DP_ProtocolIsEmpty())
       DP_AddPrintCell(DivRep, "Нет данных, удовлетворяющих параметрам отчета.", 50, 0, "l:" + FontStyleTitel, false, REP_ELEM_STR);
       DivRep.AddStr();

       DP_AddProtocol(DivRep, "protocol");  //добавим протокол к уже сформированному отчету
       DP_EndProtocol();                 //закончить протоколирование

       if( ToExcel )
         DivRep.PrintWinRep();
         DivRep.ShowWinRep();
       else
         DivRep.PrintRep();
       end;
     else
       DP_EndProtocol();                 //закончить протоколирование
     end;

     MsgBox("Нет данных, удовлетворяющих параметрам отчета.");
     return 1;
   end;

   DP_AddProtocol(DivRep, "protocol");  //добавим протокол к уже сформированному отчету
   DP_EndProtocol();                    //закончить протоколирование

   if( ToExcel )
     DivRep.SetZoomType( ZOOM_TYPE_A4L );
     DivRep.PrintWinRep();
     DivRep.ShowWinRep();
   else
     DivRep.PrintRep();
   end;

   return 0;
end;
