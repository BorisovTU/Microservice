/*
$Name:         dpnotdiv.mac
$Module:       Депозитарий
$Description:  Отчет "Уведомление о начислении доходов"
*/

import deposerv, cb_sql;
import RsbDataSet;

private const ALG_DEPO_PAYMENTTYPE = 3258; //Типы выплат доходов в Депозитарии

var iCount = 0, isPrint = false;

file regstr( "dpregstr" ) key 1;

var DataCrp;
var sign = "   ";
var app_str = "";

var _NumDprt,
    _signed_date_beg,
    _signed_date_end,
    _order_AltXid_beg,
    _order_AltXid_end,
    _reg_date_beg,
    _reg_date_end,
    _order_Xid_beg,
    _order_Xid_end,
    _issuer_code_id,
    _domic_code_id,
    _FIID,
    _showfi,
    _toExcel,
    _is_ap,
    _PaymentType;

var HeadTable = "┌───────────────────────┬───────────────────────────┬────────────┬────────────┬────────────────┬────────────┬────────────┬───────────────────┬────────────┬───────────────┬─────────────────────┬────────────────┐\n"+
                "│ Номер государственной │      Вид ценных бумаг     │   Номинал  │ Код валюты │ Период выплаты │     Д1     │     Д2     │ Количество ценных │   Ставка   │   Расчитано   │ Удержано источником │    Начислено   │\n"+
                "│      регистрации      │                           │            │  номинала  │                │            │            │       бумаг       │            │               │      начисления     │                │\n"+
                "├───────────────────────┼───────────────────────────┼────────────┼────────────┼────────────────┼────────────┼────────────┼───────────────────┼────────────┼───────────────┼─────────────────────┼────────────────┤\n"+
                "│           1           │             2             │      3     │      4     │       5        │      6     │      7     │         8         │      9     │      10       │         11          │       12       │\n"+
                "├───────────────────────┼───────────────────────────┼────────────┼────────────┼────────────────┼────────────┼────────────┼───────────────────┼────────────┼───────────────┼─────────────────────┼────────────────┤";

var tablewidth = Index( HeadTable, "\n" );
var DivRep = CMakeReport( HeadTable );
const FontStyleTitel = "ex_FS(b)";

macro AddStr()
  DivRep.AddStr();
end;

macro PrintCell( Str )
  DP_AddPrintCell( DivRep, Str, strlen( Str ) + 2, 0, "l:" + FontStyleTitel, _is_ap, REP_ELEM_STR );
end;

macro PrintCellTable( Str ,t_point, Right )
  if( ( ValType( Right ) != V_UNDEF ) and Right )
    DP_AddPrintCell( DivRep, Str, 0, t_point, "r:" + FontStyleTitel );
  else
    DP_AddPrintCell( DivRep, Str, 0, t_point, "l:" + FontStyleTitel );
  end;
end;

macro PrintCellSize( Str, S_Size, Right )
  if( ( ValType( Right ) != V_UNDEF ) and Right )
    DP_AddPrintCell( DivRep, Str, S_Size, 0, "r:" + FontStyleTitel, _is_ap, REP_ELEM_STR );
  else
    DP_AddPrintCell( DivRep, Str, S_Size, 0, "l:" + FontStyleTitel, _is_ap, REP_ELEM_STR );
  end;
end;

/*интерфейс в Namealg.dbt*/
private macro __NameAlg( Type, Number )
  var NameAlg = TBfile( "namealg" );
  NameAlg.rec.iTypeAlg = Type;
  NameAlg.rec.iNumberAlg = Number;
  if( NameAlg.GetEQ )
    return NameAlg.rec.szNameAlg;
  end;
  return "";
end;

private macro GetFinCCY( FIID )
  file fin( "fininstr" );
  var CCY = "";

  fin.FIID = FIID;
  if( GetEQ( fin ) )
    CCY = fin.CCY;
  end;

  return CCY;
end;

macro GetPaymentTypeStr( PaymentType )
  var PaymentTypeName = __NameAlg( ALG_DEPO_PAYMENTTYPE, PaymentType );

  if( PaymentTypeName == "" )
    PaymentTypeName = "не установлен";
  end;

  return PaymentTypeName;
end;

macro GetPartyStr( PartyID )
  file party( "party" );
  file obj( "objcode" );
  var PartyName = "", PartyCode = "";
  var Str = "";

  party.PartyID = PartyID;
  if( GetEQ( party ) )
    PartyName = party.Name;

    obj.ObjectType = 3;
    obj.CodeKind = 1;
    obj.ObjectID = party.PartyID;
    if( GetEQ( obj ) )
      PartyCode = obj.Code;
    end;
  end;

  if( ( PartyName == "" ) and ( PartyCode == "" ) )
    Str = "не установлен";
  else
    Str = PartyCode + "  " + PartyName;
  end;

  return Str;
end;

macro GetFIIDStr( FIID )

  file fin( "fininstr" );
  file avoir( "avoiriss" );
  var Str = "не установлен";

  if( FIID != -1 )
    fin.FIID = FIID;
    if( GetEQ( fin ) )
      Str = fin.FI_Code + "  " + fin.Name + "  ";

      avoir.FIID = FIID;
      if( GetEQ( avoir ) )
        Str = Str + "LSIN " + avoir.LSIN;
      end;
    end;
  end;

  return Str;
end;

macro ПечататьПараметрыФильтра()
  var width = 30;
  var PartyStr = "";
  var FIIDstr = "";

  DivRep.AddEmptyStr();

  AddStr();
  PrintCell( sign + "Параметры фильтра" );

  AddStr();
  PrintCellSize( sign + sign + "составлено", width );
  PrintCell( string( _signed_date_beg ) + " - " + string( _signed_date_end ) );

  AddStr();
  PrintCellSize( sign + sign + "№ у отправителя", width );
  if( ( _order_AltXid_beg != "" ) and ( _order_AltXid_end != "" ) )
    PrintCell( _order_AltXid_beg + " - " + _order_AltXid_end );
  else
    PrintCell( "не установлен" );
  end;

  AddStr();
  PrintCellSize( sign + sign + "принято", width );
  PrintCell( string( _reg_date_beg ) + " - " + string( _reg_date_end ) );

  AddStr();
  PrintCellSize( sign + sign + "№ входящий", width );
  if( ( _order_Xid_beg != "" ) and ( _order_Xid_end != "" ) )
    PrintCell( _order_Xid_beg + " - " + _order_Xid_end );
  else
    PrintCell( "не установлен" );
  end;

  AddStr();
  PrintCellSize( sign + sign + "тип выплат", width );
  PrintCell( GetPaymentTypeStr( _PaymentType ) );

  PartyStr = GetPartyStr( _issuer_code_id );
  AddStr();
  PrintCellSize( sign + sign + "эмитент", width );
  PrintCell( PartyStr );

  PartyStr = GetPartyStr( _domic_code_id );
  AddStr();
  PrintCellSize( sign + sign + "место хранения", width );
  PrintCell( PartyStr );

  AddStr();
  PrintCellSize( sign + sign + "ценные бумаги", width );
  FIIDstr = GetFIIDStr( _FIID );
  PrintCell( FIIDstr );
  AddStr();
end;

macro ПечататьДатыФормированияВедомости()
  var query = "", DataSet = null;
  var BeginDate = ZeroDate, EndDate = ZeroDate;

  query = " SELECT "
            + " NVL(MIN(paym.t_ValueDate), " + GetSQLDate( ZeroDate ) + ") AS BeginDate "
           + " ,NVL(MAX(paym.t_ValueDate), " + GetSQLDate( ZeroDate ) + ") AS EndDate "
        + " FROM "
            + " dpmpaym_dbt paym "
           + " ,ddppmacc_dbt pmacc "
        + " WHERE "
            + " paym.t_DocKind = " + SP_DEPOPER_DIVIDEND + " "
        + " AND paym.t_DocumentID = pmacc.t_DocumentID "
        + " AND paym.t_Purpose IN (" + PM_PURP_SECUR_INCOME + ", " + PM_PURP_RETURNINCOME + ") "
        + " AND paym.t_ReceiverDpNode = pmacc.t_HolderAccID "
        + " AND pmacc.t_DocumentID = " + DataCrp.DocumentID + " ";

  DataSet = TRsbDataSet( query );
  if( DataSet.MoveNext() )
    BeginDate = DataSet.BeginDate;
    EndDate = DataSet.EndDate;
  end;

  DivRep.AddEmptyStr();
  DivRep.AddEmptyStr();

  AddStr();

  if( BeginDate == EndDate )
    if( BeginDate == ZeroDate )
      BeginDate = {curdate};
    end;
    PrintCell("Дата формирования " + date_as_string( 1, BeginDate, false ) );
  else
    PrintCell("Период формирования " + date_as_string( 1, BeginDate, false ) + " - " + date_as_string( 1, EndDate, false ) );
  end;
end;

macro ПечататьПервичныйДокумент()
  var GroundStr = "не определен";
  file spgr( "spground" );
  file llval( "llvalues" );

  DivRep.AddEmptyStr();
  DivRep.AddEmptyStr();

  AddStr();
  PrintCell( "Первичный документ" );

  AddStr();
  spgr.SPgroundID = DataCrp.SPgroundID;
  if( GetEQ( spgr ) )
    llval.List = 1050;
    llval.Element = spgr.Kind;
    if( GetEQ( llval ) )
      GroundStr = llval.Name;
      GroundStr = GroundStr + " № " + spgr.AltXld;
      GroundStr = GroundStr + " от " + spgr.SignedDate;
      GroundStr = GroundStr + "   № входящий: " + spgr.Xld;
      GroundStr = GroundStr + "  дата приема " + spgr.RegistrDate;
    end;
  end;
  PrintCell( sign + sign + GroundStr );
  AddStr();
end;

macro ПечататьПараметрыЦБ()
  var width = 30;
  var PartyStr = "";

  AddStr();
  PrintCell( "Ценные бумаги" );

  AddStr();
  PrintCellSize( sign + sign + "тип выплат", width );
  PrintCell( GetPaymentTypeStr( DataCrp.paymenttype ) );

  AddStr();
  PrintCellSize( sign + sign + "эмитент ценных бумаг", width );
  PartyStr = GetPartyStr( regstr.Issuer );
  PrintCell( PartyStr );

  AddStr();
  PrintCellSize( sign + sign + "место хранения/учета", width );
  PartyStr = GetPartyStr( regstr.StoragePlace );
  PrintCell( PartyStr );

  AddStr();
  PrintCellSize( sign + sign + "список-реестр", width );
  PrintCell( regstr.Number + "   дата сбора " + regstr.Date );
end;

macro ПечататьПараметрыРасчета()
  var width = 30;

  AddStr();
  PrintCell( "Параметры расчета" );

  AddStr();
  PrintCell( sign + sign + "рассчитать удержанные налоги" );

  AddStr();
  PrintCell( sign + sign + sign + sign + "физических лиц" );
  PrintCell( sign + sign + sign + sign + sign + sign + sign + sign + "юридических лиц" );

  AddStr();
  PrintCellSize( sign + sign + sign + sign + "  резидентов ", width );
  if( DataCrp.LocalHumanPrice > $0 )
    PrintCellSize( string( DataCrp.LocalHumanPrice ) + " " + GetFinCCY( DataCrp.LocalHumanPriceFI ), width / 3, true );
  else
    PrintCellSize( ЧислоCЗаданнойТочностью( double( DataCrp.LocalHumanRate ) / double( pow( 10, DataCrp.Point ) ), DataCrp.Point, app_str ) + "%", width / 3, true );
  end;
  PrintCellSize( sign + sign + sign + sign + "  резидентов ", width );
  if( DataCrp.LocalLegalPrice > $0 )
    PrintCellSize( string( DataCrp.LocalLegalPrice ) + " " + GetFinCCY( DataCrp.LocalLegalPriceFI ), width / 3, true );
  else
    PrintCellSize( ЧислоCЗаданнойТочностью( double( DataCrp.LocalLegalRate ) / double( pow( 10, DataCrp.Point ) ), DataCrp.Point, app_str ) + "%", width / 3, true );
  end;

  AddStr();
  PrintCellSize( sign + sign + sign + sign + "  нерезидентов ", width );
  if( DataCrp.ForeignHumanPrice > $0 )
    PrintCellSize( string( DataCrp.ForeignHumanPrice ) + " " + GetFinCCY( DataCrp.ForeignHumanPriceFI ), width / 3, true );
  else
    PrintCellSize( ЧислоCЗаданнойТочностью( double( DataCrp.ForeignHumanRate ) / double( pow( 10, DataCrp.Point ) ), DataCrp.Point, app_str ) + "%", width / 3, true );
  end;
  PrintCellSize( sign + sign + sign + sign + "  нерезидентов ", width );
  if( DataCrp.ForeignLegalPrice > $0 )
    PrintCellSize( string( DataCrp.ForeignLegalPrice ) + " " + GetFinCCY( DataCrp.ForeignLegalPriceFI ), width / 3, true );
  else
    PrintCellSize( ЧислоCЗаданнойТочностью( double( DataCrp.ForeignLegalRate ) / double( pow( 10, DataCrp.Point ) ), DataCrp.Point, app_str ) + "%", width / 3, true );
  end;

  AddStr();
  PrintCell( sign + sign + "с точностью " + DataCrp.Point + " знака после запятой" );
end;

macro ПечататьПараметрыНачисления()
  var CCY = GetFinCCY( DataCrp.Currency );

  AddStr();
  PrintCell( "Параметры начисления" );

  AddStr();
  PrintCell( sign + sign + "начисленные доходы " + ЧислоCЗаданнойТочностью( DataCrp.PayAmount, DataCrp.Point, app_str ) + " " + CCY );

  AddStr();
  PrintCell( sign + sign + "зачислить доходы владельцам датой " + date( DataCrp.ValueDate ) );

  AddStr();
  PrintCell( sign + sign + "л/с списания " + DataCrp.Account );
end;

macro ПечататьРасчетыПоВыпускам()
  var DataSet;
  var PayPeriod = "";
  var fiwarnts = TBfile( "fiwarnts.dbt" );
  var WarrantDate = ZeroDate;
  var ObjectID = 0;
  var D1 = "", D2 = "";
  var Rate = "";
  var cmd = DL_RSDCommand();
  var query = " SELECT "
                + " iss.t_LSIN "
               + " ,avr.t_Name AS FiName "
               + " ,rsi_rsb_fiinstr.FI_GetNominalOnDate(fin.t_FIID, ?, 1) as FaceValue "
               + " ,fin.t_Point AS FiPoint "
               + " ,fin.t_SumPrecision "
               + " ,finF.t_CCY "
               + " ,pmfi.t_FIID "
               + " ,pmfi.t_Quantity "
               + " ,pmfi.t_IncomeValue / pmfi.t_Scale AS IValue "
               + " ,pmfi.t_Amount "
               + " ,pmfi.t_BruttoAmount AS BAmount "
               + " ,pmfi.t_Point "
            + " FROM "
                + " ddppmfi_dbt pmfi "
               + " ,dfininstr_dbt fin "
               + " ,davoiriss_dbt iss "
               + " ,davrkinds_dbt avr "
               + " ,dfininstr_dbt finF "
            + " WHERE "
                + " pmfi.t_RegisterID = ? "
            + " AND fin.t_FIID = pmfi.t_FIID "
            + " AND finF.t_FIID = fin.t_FaceValueFI "
            + " AND iss.t_FIID = pmfi.t_FIID "
            + " AND avr.t_AvoirKind = fin.t_AvoirKind "
            + " AND avr.t_Fi_Kind = fin.t_Fi_Kind ";

  cmd.AddParam(regstr.Date);
  cmd.AddParam(regstr.RegisterID);

  if( _FIID != -1 )
    query = query +
              " AND pmfi.t_FIID = ? ";

    cmd.AddParam(_FIID);
  end;

  AddStr();
  PrintCell( "Расчеты по выпускам ценных бумаг" );

  DataSet = cmd.Execute( query );
  while( DataSet.moveNext() )
    if( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
      fiwarnts.Clear();
      fiwarnts.rec.FIID = DataSet.FIID;
      fiwarnts.rec.Number = DataCrp.WarrantNum;
      fiwarnts.GetEQ();
    end;

    AddStr();
    PrintCellTable( DataSet.LSIN, DataSet.Point );
    PrintCellTable( DataSet.FiName, DataSet.Point );
    PrintCellTable( DataSet.FaceValue, DataSet.FiPoint + 2, true );
    PrintCellTable( DataSet.CCY, DataSet.Point );
    if( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
      PayPeriod = fiwarnts.rec.PayPeriod;
    elif( ( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_COUPON )
       or ( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_NOMINAL )
       or ( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_ISSUE ) )
      WarrantDate = DP_GetFIWarntDate( DataSet.FIID, DataCrp.WarrantNum, DataCrp.PaymentType );

      if( ( DataCrp.WarrantNum != "" ) and ( WarrantDate != ZeroDate ) )
        PayPeriod = date_as_string( 1, WarrantDate, false ) + "/" + DataCrp.WarrantNum;
      elif( ( DataCrp.WarrantNum != "" ) and ( WarrantDate == ZeroDate ) )
        PayPeriod = DataCrp.WarrantNum;
      elif( ( DataCrp.WarrantNum == "" ) and ( WarrantDate != ZeroDate ) )
        PayPeriod = date_as_string( 1, WarrantDate, false );
      end;
    end;
    PrintCellTable( PayPeriod );
    if( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
      ObjectID = UniID( fiwarnts, OBJTYPE_DIVIDENDS );
      D1 = ЧислоCЗаданнойТочностью( money( readNoteForObject( OBJTYPE_DIVIDENDS, ObjectID, 1, regstr.Date ) ), DataSet.Point, app_str );
      D2 = ЧислоCЗаданнойТочностью( money( readNoteForObject( OBJTYPE_DIVIDENDS, ObjectID, 2, regstr.Date ) ), DataSet.Point, app_str );
    end;
    PrintCellTable( D1, 0, true );
    PrintCellTable( D2, 0, true );
    PrintCellTable( DataSet.Quantity, DP_GetPrecision( DataSet.Quantity, DataSet.SumPrecision ), true );
    if( ( DataCrp.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS ) and ( fiwarnts.rec.DividendsRate != "" ) )
      Rate = fiwarnts.rec.DividendsRate;
    else
      Rate = ЧислоCЗаданнойТочностью( DataSet.IValue, DataSet.Point, app_str ) + " " + DataSet.CCY;
    end;
    PrintCellTable( Rate, 0, true );
    PrintCellTable( DataSet.BAmount, DataSet.Point, true );
    PrintCellTable( ( double( DataSet.BAmount ) - double( DataSet.Amount ) ), DataSet.Point, true );
    PrintCellTable( DataSet.Amount, DataSet.Point, true );
    AddStr();
  end;
end;

macro NotifyCalcDividend( NumDprt         : integer,
                          signed_date_beg : date,
                          signed_date_end : date,
                          order_AltXid_beg: string,
                          order_AltXid_end: string,
                          reg_date_beg    : date,
                          reg_date_end    : date,
                          order_Xid_beg   : string,
                          order_Xid_end   : string,
                          Register_id     : integer,
                          issuer_code_id  : integer,
                          domic_code_id   : integer,
                          FIID            : integer,
                          showfi          : bool,
                          toExcel         : bool,
                          is_ap           : bool,
                          FromScrol       : bool,
                          PaymentType     : integer
                        )
  Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

  DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

  _NumDprt          = NumDprt;
  _signed_date_beg  = signed_date_beg;
  _signed_date_end  = signed_date_end;
  _order_AltXid_beg = order_AltXid_beg;
  _order_AltXid_end = order_AltXid_end;
  _reg_date_beg     = reg_date_beg;
  _reg_date_end     = reg_date_end;
  _order_Xid_beg    = order_Xid_beg;
  _order_Xid_end    = order_Xid_end;
  _issuer_code_id   = issuer_code_id;
  _domic_code_id    = domic_code_id;
  _FIID             = FIID;
  _showfi           = showfi;
  _toExcel          = toExcel;
  _is_ap            = is_ap;
  _PaymentType      = PaymentType;

  var stat, acc_stat, i = 0;

  if( is_ap == true )
    app_str = "a";
  end;

  //накладываем фильтр
  var query = " SELECT "
                + " crp.* "
               + " ,spgr.t_SPgroundID "
            + " FROM "
                + " ddpcorpop_dbt crp "
               + " ,dspground_dbt spgr "
               + " ,ddpregstr_dbt reg "
            + " WHERE "
                + " crp.t_DocKind = " + SP_DEPOPER_DIVIDEND + " "
            + " AND spgr.t_SPgroundID = crp.t_SPgroundID "
            + " AND reg.t_DocumentID = crp.t_DocumentID ";

  if( NumDprt > 0 )
    query = query +
              " AND crp.t_Department = " + NumDprt + " ";
  end;

  if( Register_id > 0 )
    query = query +
              " AND reg.t_RegisterID = " + Register_id + " ";
  end;

  if( ( signed_date_beg != ZeroDate ) or ( signed_date_end != ZeroDate ) )
    query = query +
              " AND spgr.t_SignedDate >= " + GetSQLDate( signed_date_beg ) + " "
            + " AND spgr.t_SignedDate <= " + GetSQLDate( signed_date_end ) + " ";
  end;

  if( order_AltXid_beg != ""  )
    query = query +
              " AND spgr.t_AltXld >= '" + order_AltXid_beg + "' ";
  end;

  if( order_AltXid_end != "" )
    query = query +
              " AND spgr.t_AltXld <= '" + order_AltXid_end + "' ";
  end;

  if( ( reg_date_beg != ZeroDate ) or ( reg_date_end != ZeroDate ) )
    query = query +
              " AND spgr.t_RegistrDate >= " + GetSQLDate( reg_date_beg ) + " "
            + " AND spgr.t_RegistrDate <= " + GetSQLDate( reg_date_end ) + " ";
  end;

  if( ( order_Xid_beg != "" ) and ( order_Xid_end != "" ) )
    query = query +
              " AND spgr.t_Xld >= '" + order_Xid_beg + "' "
            + " AND spgr.t_Xld <= '" + order_Xid_end + "' ";
  end;

  if( issuer_code_id != -1 )
    query = query +
              " AND reg.t_Issuer = " + issuer_code_id + " ";
  end;

  if( domic_code_id != -1 )
    query = query +
              " AND reg.t_StoragePlace = " + domic_code_id + " ";
  end;

  if( FIID != -1 )
    query = query +
              " AND EXISTS (SELECT "
                            + " mf.t_RegisterID "
                        + " FROM "
                            + " ddppmfi_dbt mf "
                        + " WHERE "
                            + " mf.t_RegisterID = reg.t_RegisterID "
                        + " AND mf.t_FIID = " + FIID + ") ";
  end;

  if( PaymentType > 0 )
    query = query +
              " AND crp.t_PaymentType = " + PaymentType + " ";
  end;

  DataCrp = TRsbDataSet( query );
  while( DataCrp.moveNext() )
    //для каждого поручения ищем список-реестр
    ClearRecord( regstr );
    KeyNum( regstr, 1 );
    regstr.DocumentID = DataCrp.DocumentID;
    if( GetEQ( regstr ) )
      if( i != 0 )
        DivRep.AddEmptyStr();
      end;
      DP_StdHeaderLO_Ex( DivRep, FontStyleTitel, "У В Е Д О М Л Е Н И Е  О  Н А Ч И С Л Е Н И И  Д О Х О Д О В", 0, 0, true, null, null, tablewidth );

      if( i == 0 )
        if( not FromScrol )
          //Параметры фильтра
          ПечататьПараметрыФильтра();
        end;
      end;
      //печатать даты формирования ведомости
      ПечататьДатыФормированияВедомости();
      //первичный документ
      ПечататьПервичныйДокумент();
      //параметры ц/б
      ПечататьПараметрыЦБ();
      //параметры расчета
      ПечататьПараметрыРасчета();
      //Печатать параметры начисления
      ПечататьПараметрыНачисления();
      //Печать расчетов по выпускам
      if( showfi )
        ПечататьРасчетыПоВыпускам();
      end;

      DP_StdFooter_Ex( DivRep, FontStyleTitel, null, null, tablewidth );
    end;

    i = i + 1;
  end;

  if( i == 0 )
    if( not DP_ProtocolIsEmpty() )
      DP_AddPrintCell( DivRep, "Нет данных, удовлетворяющих параметрам отчета.", 50, 0, "l:" + FontStyleTitel, false, REP_ELEM_STR );
      DivRep.AddStr();

      //добавим протокол к уже сформированному отчету
      DP_AddProtocol( DivRep, "protocol" );
      //закончить протоколирование
      DP_EndProtocol();

      if( ToExcel )
        DivRep.PrintWinRep();
        DivRep.ShowWinRep();
      else
        DivRep.PrintRep();
      end;
    else
      //закончить протоколирование
      DP_EndProtocol();
    end;

    MsgBox( "Нет данных, удовлетворяющих параметрам отчета." );
    return 1;
  end;

  //добавим протокол к уже сформированному отчету
  DP_AddProtocol( DivRep, "protocol" );
  //закончить протоколирование
  DP_EndProtocol();

  if( ToExcel )
    DivRep.SetZoomType( ZOOM_TYPE_A4L );
    DivRep.PrintWinRep();
    DivRep.ShowWinRep();
  else
    DivRep.PrintRep();
  end;

  return 0;
end;
