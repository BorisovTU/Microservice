/*
$Name:         nsdImpOutDoc.mac
$Module:       Депозитарий
$Description:  Процедура импорта отправленных в НРД эл. документов
*/

import "nsdImpLib.mac";

private class CNSDProtocol(fileName, date1, time1)
    private var m_FileName = fileName;
    private var m_Date = date1;
    private var m_Time = time1;
    private var m_Reports = TArray(), m_Reports1 = TArray(), m_Reasons = TArray();

    macro AddReport(report)
        m_Reports[m_Reports.Size] = report;
    end;

    macro AddReport1(report, reason)
        m_Reports1[m_Reports1.Size] = report;
        m_Reasons[m_Reasons.Size] = reason;
    end;

    macro AddError(errStr)
        DP_ProtocolAddStr(errStr);
    end;

    macro HasErrors()
        return DP_ProtocolIsEmpty();
    end;

    macro Print()
        var rep = CMakeReport();
        var i = 0;
        var fields = null;
        var widths = TArray();
        var head = CHeader();
        var width = 0;
        const style = "ex_FS(b)";
        const style1 = "ex_B(rlb)";
        var day = 0, month = 0, year = 0;
        var hour = 0, minute = 0, sec = 0;
        var name = "";

        fields = SplitString("№;;№ документа, отправленного в НРД;;Дата документа, отправленного в НРД;;Код операции;;Причина", ";;");
        while(i < fields.Size)
            widths[i] = StrLen(fields[i]);
            head.AddChild(fields[i], widths[i]);
            i = i + 1;
        end;

        width = head.CalcWidthHeader();

        DP_StdHeaderLO_Ex(rep, style, "Протокол процедуры автоматической регистрации исходящих документов, отправленных через СЭД \"ЛУЧ\"", 0, 0, false, m_Date, m_Date, width);

        if((m_Reports.Size == 0) and (m_Reports1.Size == 0))
            DP_AddPrintCell(rep, "Нет отправленных электронных документов к регистрации", width, 0, "l:" + style, false, REP_ELEM_STR);
            rep.AddStr();

            rep.AddEmptyStr();
        else
            head.RemoveChild(4);

            if(m_Reports.Size != 0)
                DP_AddPrintCell(rep, "Перечень зарегистрированных отправленных электронных документов:", width, 0, "l:" + style, false, REP_ELEM_STR);

                Rep.AutoScan("[\n" + head.CreateHeader() + "]");

                i = 0;
                while(i < m_Reports.Size)
                    DP_AddPrintCell(rep, i + 1, widths[0], 0, "l:" + style1);
                    DP_AddPrintCell(rep, m_Reports[i].RepNo, widths[1], 0, "l:" + style1);
                    DP_AddPrintCell(rep, date_as_string(1, m_Reports[i].RepDate, false), widths[2], 0, "l:" + style1);
                    DP_AddPrintCell(rep, m_Reports[i].OpCode, widths[3], 0, "l:" + style1);
                    rep.AddStr();
                    i = i + 1;
                end;
            else
                DP_AddPrintCell(rep, "Успешно зарегистрированые отправленные электронные документы отсутствуют", width, 0, "l:" + style, false, REP_ELEM_STR);
                rep.AddStr();
            end;

            rep.AddEmptyStr();

            head.AddChild(fields[4], widths[4]);

            if(m_Reports1.Size != 0)
                DP_AddPrintCell(rep, "Перечень незарегистрированных отправленных электронных документов:", width, 0, "l:" + style, false, REP_ELEM_STR);

                Rep.AutoScan( "[\n" + head.CreateHeader() + "]" );

                i = 0;
                while(i < m_Reports1.Size)
                    DP_AddPrintCell(rep, i + 1, widths[0], 0, "l:" + style1);
                    DP_AddPrintCell(rep, m_Reports1[i].RepNo, widths[1], 0, "l:" + style1);
                    DP_AddPrintCell(rep, date_as_string(1, m_Reports1[i].RepDate, false), widths[2], 0, "l:" + style1);
                    DP_AddPrintCell(rep, m_Reports1[i].OpCode, widths[3], 0, "l:" + style1);
                    DP_AddPrintCell(rep, m_Reasons[i], widths[4], 0, "l:" + style1);
                    rep.AddStr();

                    i = i + 1;
                end;
            else
                DP_AddPrintCell(rep, "Все отправленные электронные документы успешно зарегистрированы", width, 0, "l:" + style, false, REP_ELEM_STR);
                rep.AddStr();
            end;

            rep.AddEmptyStr();
        end;

        DP_StdFooter_Ex(rep, style, NULL, NULL, width);

        DP_AddProtocol(rep, "Ошибки импорта отчетов НРД");

        DateSplit(m_Date, day, month, year);
        TimeSplit(m_Time, hour, minute, sec);

        name = m_FileName + L_Z(String(day:2), 2) + L_Z(String(month:2), 2) + L_Z(String(year - Int(year / 100) * 100:2), 2)
             + "-" + L_Z(String(hour:2), 2) + "." + L_Z(String(minute:2), 2) + "." + L_Z(String(sec:2), 2);

        if(true)
            rep.SetFileName(name);
            rep.PrintWinRep();
            rep.ShowWinRep();
        else
            name = GetTxtFileName(name);
            SetOutput(name);
            rep.PrintRep();
            SetOutput(NULL, true);
            Close(name);
            ViewFile(name);
        end;
    end;

    macro Destructor()
        DP_EndProtocol();
    end;

    DP_BeginProtocol();
end;

private class (CNSDImportReportsBaseXML) CNSDImportOutDoc()
    private macro getRefType(refType)
        var err = 0;

        if(Index(",10,16,16/1,16/2,16/3,20,20/2,35,35/2,37,36,36/2,36/35,26,26/1,70,94,", "," + m_Report.OpCode + ",") != 0)
            refType = REFOBJ_DPENTDOC_UUJ;
        elif(Index(",68/CAIN,68/CAIC,", "," + m_Report.OpCode + ",") != 0)
            refType = REFOBJ_DPENTDOC_UKD;
        else
            err = -1;
        end;

        if(err == 0)
            SetParm(1, refType);
        end;

        return err;
    end;

    private macro checkAcc()
        var err = 0;
        var tag = "";
        var acc = "";

        if(Index(",10,16,16/1,", "," + m_Report.OpCode + ",") != 0)
            tag = "MF010/dep_acc_c";
        elif(Index(",16/2,16/3,", "," + m_Report.OpCode + ",") != 0)
            tag = "MF170/dep_acc_c";
        elif(Index(",20,20/2,", "," + m_Report.OpCode + ",") != 0)
            tag = "MF020/dep_acc_c";
        elif(Index(",19/0,19/1,", "," + m_Report.OpCode + ",") != 0)
            tag = "MF190/dep_acc_c";
        elif(Index(",35,35/2,37,", "," + m_Report.OpCode + ",") != 0)
            tag = "MF035/dep_acc_c";
        elif(Index(",36,36/2,36/35,", "," + m_Report.OpCode + ",") != 0)
            tag = "MF036/dep_acc_c";
        elif(Index(",26,26/1,", "," + m_Report.OpCode + ",") != 0)
            tag = "MF026/dep_acc_c";
        elif(Index(",70,", "," + m_Report.OpCode + ",") != 0)
            tag = "GF070/depo_acc_c";
        elif(Index(",40,41,42,43,44,", "," + m_Report.OpCode + ",") != 0)
            tag = "IF444/depo_acc_c";
        elif(Index(",93,", "," + m_Report.OpCode + ",") != 0)
            tag = "AF093/depo_acc_c";
        elif(Index(",90,91,", "," + m_Report.OpCode + ",") != 0)
            tag = "AF090/depo_acc_c";
        elif(Index(",94,", "," + m_Report.OpCode + ",") != 0)
            tag = "AF094/depo_acc_c";
        elif(Index(",68/CAIN,68/CAIC,80/3,81/3,", "," + m_Report.OpCode + ",") != 0)
            tag = "SfkpgAcct";
        else
            err = -1;
        end;

        if(err == 0)
            err = getAcc("//" + tag, acc);
        end;

        if(err == 0)
            if(Index(acc, "L") != 2)
                err = -1;
                m_Protocol.AddReport1(m_Report, "Найденый в поле " + tag + " счет депо " + acc + " не удовлетворяет условиям отбора");
            end;
        elif(err == -1)
            err = 0;
        end;

        return err;
    end;

    private macro checkAltXld(kind, msgNumber, msgDate)
        var err = 0;
        var query = "", cmd = null, set = null;

        query = " SELECT "
                  + " spgr.t_Xld "
                 + " ,spgr.t_RegistrDate "
              + " FROM "
                  + " dspground_dbt spgr "
              + " WHERE "
                  + " spgr.t_Kind = ? "
              + " AND spgr.t_MsgNumber = ? "
              + " AND spgr.t_MsgDate = ? ";

        cmd = RsdCommand(query);
        cmd.AddParam("", RSDBP_IN, kind);
        cmd.AddParam("", RSDBP_IN, msgNumber);
        cmd.AddParam("", RSDBP_IN, msgDate);
        cmd.NullConversion = true;
        cmd.Execute();

        set = RsdRecordset(cmd);

        if(set.MoveNext())
            err = -1;
            m_Protocol.AddReport1(m_Report, "Документ с таким регистрационным номером уже зарегистрирован в системе, входящий номер: " + set.Value("t_Xld") + " от " + date_as_string(1, Date(set.Value("t_RegistrDate")), false));
        end;

        return err;
    end;

    private macro insertGroundDoc()
        var err = 0, errStr = "";
        var id = 0;
        var refType = 0, refID = 0;
        var xld = "", direction = EXITING, division = DP_SelfDivision, kind = DOCKIND_INSTRUCTIONTONSD, registrDate = m_Date;
        var spgrPrm = TRecHandler("spgroundprm.rec");

        err = getRefType(refType);

        if(err == 0)
            err = checkAcc();
        end;

        if(err == 0)
            err = checkAltXld(kind, m_Report.RepNo, m_Report.RepDate);
        end;

        if(err == 0)
            err = getRefID(refType, refID);
        end;

        if(err == 0)
            err = generateXld(refID, kind, xld, direction, {OperDprt}, division, registrDate);
        end;

        if(err == 0)
            spgrPrm.Clear();

            spgrPrm.rec.Division        = division;
            spgrPrm.rec.Direction       = direction;
            spgrPrm.rec.Kind            = kind;
            spgrPrm.rec.Xld             = xld;
            spgrPrm.rec.RegistrDate     = registrDate;
            spgrPrm.rec.RegistrTime     = m_Time;
            spgrPrm.rec.DocLog          = LLCLASS_KINDDOC_DOCDEPO;
            spgrPrm.rec.PartyID         = m_Party;
            spgrPrm.rec.PartyCodeKind   = PTCK_CONTR;
            spgrPrm.rec.AltXld          = xld;
            spgrPrm.rec.SignedDate      = registrDate;
            spgrPrm.rec.SignedTime      = m_Time;
            spgrPrm.rec.Proxy           = UnknownParty;
            spgrPrm.rec.ProxyCodeKind   = PTCK_CONTR;
            spgrPrm.rec.DeliveryKind    = CodeFor("R"); // Электронный документ
            spgrPrm.rec.BackOffice      = "Z"; // Депозитарий
            spgrPrm.rec.Comment         = "";
            spgrPrm.rec.Department      = {OperDprt};
            spgrPrm.rec.Branch          = {OperDprtNode};
            spgrPrm.rec.IsMakeAuto      = SET_CHAR;
            spgrPrm.rec.TechAutoDoc     = TECHAUTODOC_NSDIMPOUTDOC;
            spgrPrm.rec.Deponent        = UnknownParty;
            spgrPrm.rec.MsgNumber       = m_Report.RepNo;
            spgrPrm.rec.MsgDate         = m_Report.RepDate;

            if(DP_InsertGroundDocument(id, spgrPrm, true, errStr) != 0)
                err = 1;
                m_Protocol.AddError(errStr);
                m_Protocol.AddReport1(m_Report, errStr);
                restoreXld(refID, xld);
            end;
        end;

        return err;
    end;

    private macro processFile()
        var err = 0;
        var val = "";

        m_XMLFile.GetXML().setProperty("SelectionLanguage", "XPath");

        if(m_XMLFile.GetXML().selectSingleNode("//ORDER_HEADER") != null)
            m_Report.RepNo = m_XMLFile.GetXML().selectSingleNode("//contr_d_id").nodeTypedValue;
            val = m_XMLFile.GetXML().selectSingleNode("//createdate").nodeTypedValue;
            m_Report.RepDate = Date(Int(SubStr(val, 9, 2)), Int(SubStr(val, 6, 2)), Int(SubStr(val, 1, 4)));
            m_Report.OpCode = m_XMLFile.GetXML().selectSingleNode("//order_t_id").nodeTypedValue;
        else
            m_Report.RepNo = m_XMLFile.GetXML().selectSingleNode("//BizMsgIdr").nodeTypedValue;
            val = m_XMLFile.GetXML().selectSingleNode("//CreDt").nodeTypedValue;
            m_Report.RepDate = Date(Int(SubStr(val, 9, 2)), Int(SubStr(val, 6, 2)), Int(SubStr(val, 1, 4)));
            m_Report.OpCode = m_XMLFile.GetXML().selectSingleNode("//BizSvc").nodeTypedValue;
        end;

        if(err == 0)
            err = insertGroundDoc();

            if(err > 0)
                err = 1;
            elif(err == 0)
                m_Protocol.AddReport(m_Report);
            end;
        end;

        return err;
    end;

    macro Run()
        var err = 0;
        var nrdCode = "";

        GetRegistryValue("DEPO\\NSD\\NSD_OUTDOC_PATH", V_STRING, m_Root, err);

        if(m_Root == "")
            err = 1;
            m_Protocol.AddError("Не указан путь к сетевой папке для загрузки отправленных в НРД эл. документов.|Настройка DEPO\\NSD\\NSD_OUTDOC_PATH");
        end;

        if(err == 0)
            m_Root = m_Root + "\\";

            GetRegistryValue("SECUR\\NRD_CODE", V_STRING, nrdCode, err);

            if(nrdCode == "")
                err = 1;
                m_Protocol.AddError("Ошибка при получении значения настройки \"SECUR\\NRD_CODE\"");
            end;
        end;

        if(err == 0)
            m_Party = ПолучитьКодСубъекта(nrdCode, PTCK_CONTR, err);

            if(err != 0)
                m_Protocol.AddError("Не найден субъект с кодом \"" + nrdCode + "\"");
            end;
        end;

        if(err == 0)
            InitProgress(-1, "Идет импорт отправленных в НРД эл. документов...", "Импорт отправленных в НРД эл. документов");

            processDirs(".\\");

            RemProgress();
        end;

        m_Protocol.Print();

        return m_Protocol.HasErrors();
    end;

    initCNSDImportReportsBaseXML;
    m_Protocol = CNSDProtocol("REGREPORTOUT", m_Date, m_Time);
end;

if(IsShedulerRunning or (MsgBoxEx("Вы хотите зарегистрировать направленные в НРД электронные документы датой " + date_as_string(1, {CurDate}, false) + "?", MB_YES + MB_NO, IND_YES) == IND_YES))
    CNSDImportOutDoc.Run();
end;

Exit(1);

