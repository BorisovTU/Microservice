/*
$Name: spdb_12.mac
$Module: Депозитарий
$Description: Книжный перевод эмиссионных ценных бумаг - шаг Проверки поручения
*/

import OprInter, PTInter, InsCarryDoc, PaymInter, dpmassexec;

/* Буферы ПД, заполняемые из операции */
record Pm_paym( pmpaym );    /*основной платеж*/
record Pm_prop_deb( pmprop ); /*св-ва платежа банка-плательщика*/
record Pm_prop( pmprop );    /*св-ва платежа*/
record Pm_rmprop( pmrmprop ); /*платеж по ценным бумагам*/


macro ExecuteStep( d, spdraft )
  var err = 0, ErrStr = "";
  var KindOperation = 0;
  record r_spdraft( spdraft );
  var OprStatus = 0;

  SetBuff( r_spdraft, spdraft );

  KindOperation = DefineKindOperation( Pm_paym.PayerDpNode, Pm_paym.ReceiverDpNode );
  if((KindOperation == KINDOPER_ENROLMENT) OR (KindOperation == KINDOPER_TRANSFER))
    if(true != ПроверитьПолномочиеПоУзлу(Pm_paym.ReceiverDpNode, r_spdraft.Initiator, OBJTYPE_DEPOAUTHORITY_ENROL, Pm_paym.FIID, Pm_paym.ValueDate, r_spdraft.Product, @ErrStr))
      err = RejectDepoDraftYesNo( spdraft, DP_SPDRAFT_OPERATION, ErrStr );
    end;
  end;

  if((not err) and ((KindOperation == KINDOPER_WRITINGOFF) OR (KindOperation == KINDOPER_TRANSFER)))
    if(true != ПроверитьПолномочиеПоУзлу(Pm_paym.PayerDpNode, r_spdraft.Initiator, OBJTYPE_DEPOAUTHORITY_WRITEOFF, Pm_paym.FIID, Pm_paym.ValueDate, r_spdraft.Product, @ErrStr))
      err = RejectDepoDraftYesNo( spdraft, DP_SPDRAFT_OPERATION, ErrStr );
    end;
  end;
  
  if((not err) AND (KindOperation == KINDOPER_MOVEMENT) AND (Pm_paym.IndoorStorage == 0))
    var query, cmd, DataSet;
    var stat = 0;
    
    query =   " select acc.t_DepoAcc "
            + "   from daccsubdc_dbt subdc, daccvanl_dbt anl, daccount_dbt acc "
            + "  where subdc.t_PaymentID = ? "
            + "    and anl.t_AccAnaliticsID = subdc.t_AccAnaliticsID "
            + "    and acc.t_Account = anl.t_Account "
            + "    and acc.t_Chapter = anl.t_Chapter "
            + "    and acc.t_Code_Currency = anl.t_FIID ";


    cmd = DL_RSDCommand(query);
    cmd.AddParam(Pm_paym.PaymentID);

    DataSet = cmd.Execute();
    stat = DataSet.moveNext();
    if(stat)
      while(stat and (not err))
        
        if(true != ПроверитьПолномочиеПоУзлу(DataSet.DepoAcc, r_spdraft.Initiator, OBJTYPE_DEPOAUTHORITY_STORAGECHANGE, Pm_paym.FIID, Pm_paym.ValueDate, r_spdraft.Product, @ErrStr))
          err = RejectDepoDraftYesNo( spdraft, DP_SPDRAFT_OPERATION, ErrStr );
          break;
        end;

        stat = DataSet.moveNext();
      end;
    else
      //если это перемещение, сформированное по операции передачи в пул/возврата из пула, то проверяемый счет можно определить
      var AcntID = ПолучитьПассивныйСчетПоИзменениюПула(r_spdraft.AutoKey);
      if(AcntID > 0)
        if(true != ПроверитьПолномочиеПоУзлу(AcntID, r_spdraft.Initiator, OBJTYPE_DEPOAUTHORITY_STORAGECHANGE, Pm_paym.FIID, Pm_paym.ValueDate, r_spdraft.Product, @ErrStr))
          err = RejectDepoDraftYesNo( spdraft, DP_SPDRAFT_OPERATION, ErrStr );
        end;
      else
        err = RejectDepoDraftYesNo( spdraft, DP_SPDRAFT_OPERATION, "Не выполнена разноска по субсчетам" );
      end;
    end;
  end;

  /////////////
  //Проверки КИ
  if(not err)
    if(KindOperation == KINDOPER_TRANSFER )
      if(Pm_paym.Payer != Pm_paym.Receiver)
        if( (ДП_ПроверитьПринадлежностьКИ(spdraft, Pm_paym.FIID, Pm_paym.Receiver, Pm_paym.ReceiverDPNode)) OR
            (ДП_ПроверитьПринадлежностьКИ(spdraft, Pm_paym.FIID, Pm_paym.Payer, Pm_paym.PayerDPNode))
          )
          err = 1;
        end;
      end;
    elif(KindOperation == KINDOPER_ENROLMENT)
      if(ДП_ПроверитьПринадлежностьКИ(spdraft, Pm_paym.FIID, Pm_paym.Receiver, Pm_paym.ReceiverDPNode))
        err = 1;
      end;
    end;
  end;
  ////////////

  if(not err)
    if(r_spdraft.OperState == DP_INVSTATUSORDER_REJECT)
      if( not InsertOprStatus( DP_OPERSTATUS_UNIVOP_DO, DP_OPERSTATUS_SPDRAFT_DO_CRREP) ) 
         msgbox( "Ошибка при установке статуса <Документооборот> операции" );
         err = 1;
      end;
    end;
  end;

  return err;
end;

MACRO MassExecuteStep()
  var err = 0;
  var query, cmd, DataSet;
  var stat = 0;
  var ErrStr = "";


  if(DP_MassCheckValueDate() != 0)
    return 1;
  end;

  if(DP_MassCheckKI(false, " rsb_depo.DefineKindOperation(pm.t_PayerDpNode, pm.t_ReceiverDpNode) = "+KINDOPER_ENROLMENT ) != 0)
    return 1;
  end;

  if(DP_MassCheckKI(true, " rsb_depo.DefineKindOperation(pm.t_PayerDpNode, pm.t_ReceiverDpNode) = "+KINDOPER_TRANSFER+" AND pm.t_Payer <> pm.t_Receiver ") != 0)
    return 1;
  end;

  if(DP_MassCheckKI(false, " rsb_depo.DefineKindOperation(pm.t_PayerDpNode, pm.t_ReceiverDpNode) = "+KINDOPER_TRANSFER+" AND pm.t_Payer <> pm.t_Receiver ") != 0)
    return 1;
  end;

  query =   " select pm.t_PaymentID, pm.t_PayerDpNode, pm.t_ReceiverDpNode, pm.t_FIID, pm.t_ValueDate, pm.t_ReceiverBankID, "
          + "        dr.t_AutoKey as DraftID, dr.t_Initiator, dr.t_Product, dr.t_FromTransitAccount, pm.t_IndoorStorage, "
          + "        rsb_depo.DefineKindOperation(pm.t_PayerDpNode, pm.t_ReceiverDpNode) as KindOperation "
          + "   from DOPRTEMP_VIEW oprtemp, dspdraft_dbt dr, dspdrmove_dbt mv, dpmpaym_dbt pm"
          + "  where dr.t_AutoKey = oprtemp.t_OrderID "
          + "    and mv.t_DraftID = dr.t_AutoKey "
          + "    and pm.t_PaymentID = mv.t_PaymentID ";

  cmd = DL_RSDCommand(query);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    stat = 0;

    if((DataSet.KindOperation == KINDOPER_ENROLMENT) OR (DataSet.KindOperation == KINDOPER_TRANSFER))
      if(true != ПроверитьПолномочиеПоУзлу(DataSet.ReceiverDpNode, DataSet.Initiator, OBJTYPE_DEPOAUTHORITY_ENROL, DataSet.FIID, date(DataSet.ValueDate), DataSet.Product, @ErrStr))
        stat = 1;
        DP_SetErrorOprTemp( DataSet.PaymentID, stat, ErrStr );
      end;
    end;

    if((not stat) and ((DataSet.KindOperation == KINDOPER_WRITINGOFF) OR (DataSet.KindOperation == KINDOPER_TRANSFER)))
      if(true != ПроверитьПолномочиеПоУзлу(DataSet.PayerDpNode, DataSet.Initiator, OBJTYPE_DEPOAUTHORITY_WRITEOFF, DataSet.FIID, date(DataSet.ValueDate), DataSet.Product, @ErrStr))
        stat = 1;
        DP_SetErrorOprTemp( DataSet.PaymentID, stat, ErrStr );
      end;
    end;
    
    if((not stat) AND (DataSet.KindOperation == KINDOPER_MOVEMENT) AND (DataSet.IndoorStorage == 0))
      var Select, DS;
      
      query =   " select acc.t_DepoAcc "
              + "   from daccsubdc_dbt subdc, daccvanl_dbt anl, daccount_dbt acc "
              + "  where subdc.t_PaymentID = ? "
              + "    and anl.t_AccAnaliticsID = subdc.t_AccAnaliticsID "
              + "    and acc.t_Account = anl.t_Account "
              + "    and acc.t_Chapter = anl.t_Chapter "
              + "    and acc.t_Code_Currency = anl.t_FIID ";


      Select = DL_RSDCommand(query);
      Select.AddParam(DataSet.PaymentID);

      DS = Select.Execute();
      stat = DS.moveNext();
      if(stat)
        while(stat and (not err))
          
          if(true != ПроверитьПолномочиеПоУзлу(DS.DepoAcc, DataSet.Initiator, OBJTYPE_DEPOAUTHORITY_STORAGECHANGE, DataSet.FIID, date(DataSet.ValueDate), DataSet.Product, @ErrStr))
            stat = 1;
            DP_SetErrorOprTemp( DataSet.PaymentID, stat, ErrStr );
            break;
          end;

          stat = DataSet.moveNext();
        end;
      else
        //если это перемещение, сформированное по операции передачи в пул/возврата из пула, то проверяемый счет можно определить
        var AcntID = ПолучитьПассивныйСчетПоИзменениюПула(DataSet.DraftID);
        if(AcntID > 0)
          if(true != ПроверитьПолномочиеПоУзлу(AcntID, DataSet.Initiator, OBJTYPE_DEPOAUTHORITY_STORAGECHANGE, DataSet.FIID, date(DataSet.ValueDate), DataSet.Product, @ErrStr))
            stat = 1;
            DP_SetErrorOprTemp( DataSet.PaymentID, stat, ErrStr );
          end;
        else
          stat = 1;
          DP_SetErrorOprTemp( DataSet.PaymentID, stat, "Не выполнена разноска по субсчетам" );
        end;
      end;
    end;
  end;


  VAR OperStatus = DP_MassOperStatus();

  OperStatus.Insert( DP_OPERSTATUS_UNIVOP_DO, DP_OPERSTATUS_SPDRAFT_DO_CRREP, " dr.t_OperState = " + DP_INVSTATUSORDER_REJECT );

  err = OperStatus.Execute( "Шаг проверки поручений. Установка статуса операций." );

  if(err)
    DP_SetErrorOprTemp(0, err, "Ошибка при пакетном выполнении шага");
  end;

  return err;

OnError(er)
  DP_SetErrorOprTemp(0, 1, er.module+ " "+er.line+" "+er.message);
  return 1;
end;