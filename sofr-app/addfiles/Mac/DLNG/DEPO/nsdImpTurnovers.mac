/*
$Name:         nsdImpTurnovers.mac
$Module:       Депозитарий
$Description:  Процедура сверки оборотов ценных бумаг в НРД
*/

import BankInter, PTInter, CTInter, DpInter, rsexts;
import "dlquery.mac", "DlReport_h.mac";
import "dpimp.mac", "secinter.mac", "globals.mac", "dprepsec.mac";

private const FICK_NADC = 14; // код ФИ в кодировке НДЦ
private const OBJTYPE_DEPOACNT = 112; // Счет депо
private const OBJTYPE_DEPOPART = 122; // Раздел счета депо

private file reporth() dbf; // в класс не запихнуть
private file hdsttds() dbf;  // в класс не запихнуть
private file oprp() dbf;  // в класс не запихнуть

// Запись остатка ц/б
private class CNSDTurnovers(DepoPartID, DepoAccNSDCode, DepoPartNSDCode, FIID, NSD_FICODE, LSIN, ISIN, Amount, FIName, SumPrecision)
  var m_DepoPartID = DepoPartID; // ИД раздела счета депо
  var m_FIID = FIID; // ИД ФИ
  var m_LSIN = LSIN; // № гос. рег.
  var m_ISIN = ISIN; // ISIN
  var m_Amount = Amount; // Количество ц/б

  var m_DepoAccNSDCode = DepoAccNSDCode; // Код счета в НРД
  var m_DepoPartNSDCode = DepoPartNSDCode; // Код раздела в НРД
  var m_NSD_FICODE = NSD_FICODE; // Код ц/б в НРД
  var m_NSD_Amount = $0.0; // Обороты в НРД
  var m_NSD_FINAME = FIName;

  var m_SumPrecision = SumPrecision;
end;

// Запись ошибки
private class CNSDProtocolStr(DepoAcc, DepoPart, FI_Code, LSIN, ISIN, FI_Name, Reason)
  var m_DepoAcc  = DepoAcc;
  var m_DepoPart = DepoPart;
  var m_FI_Code  = FI_Code;
  var m_LSIN     = LSIN;
  var m_ISIN     = ISIN;
  var m_FI_Name  = FI_Name;
  var m_Reason   = Reason;
end;

// Протокол процедуры сверки
private class CNSDProtocol(Reg_No:string, Start_Date:date, End_Date:date)
  var m_ErrArr = TArray();
  var m_TotalCnt = 0;
  var m_Reg_No = Reg_No;
  var m_Start_Date = Start_Date;
  var m_End_Date = End_Date;
  var m_HeadTable = "┌───┬──────────────┬────────────────────┬─────────────────┬────────────────────┬────────────────────┬────────────────────────────┬────────────────────────────────────────────────────────────┐\n"
                  + "│ № │  Счёт депо   │  Раздел счёта депо │Код ценной бумаги│Номер госрегистрации│       ISIN         │  Краткое наименование ц/б  │                     Количество ц/б в шт.                   │\n"
                  + "│   │              │                    │                 │                    │                    │                            ├────────────────────┬───────────────────┬───────────────────┤\n"
                  + "│   │              │                    │                 │                    │                    │                            │         НРД        │      Система      │      Разница      │\n"
                  + "├───┼──────────────┼────────────────────┼─────────────────┼────────────────────┼────────────────────┼────────────────────────────┼────────────────────┼───────────────────┼───────────────────┤";

  var m_HeadTableErr = "┌───┬──────────────┬────────────────────┬─────────────────┬────────────────────┬────────────────────┬────────────────────────────┬────────────────────────────────────────┐\n"
                     + "│ № │  Счёт депо   │  Раздел счёта депо │Код ценной бумаги│Номер госрегистрации│       ISIN         │  Краткое наименование ц/б  │             Описание ошибки            │\n"
                     + "├───┼──────────────┼────────────────────┼─────────────────┼────────────────────┼────────────────────┼────────────────────────────┼────────────────────────────────────────┤";

  var m_tablewidth = Index(m_HeadTable, "\n");

  var m_Rep = CMakeReport(m_HeadTable);

  const m_RepFontStyleTitel =  "ex_FS(b)";

  macro SetCount(cnt)
    m_TotalCnt = cnt;
  end;

  macro SetError(DepoAcc, DepoPart, FI_Code, LSIN, ISIN, FI_Name, Reason)
    m_ErrArr[m_ErrArr.size] = CNSDProtocolStr(DepoAcc, DepoPart, FI_Code, LSIN, ISIN, FI_Name, Reason);
  end;

  macro GetCntErr()
    return m_ErrArr.size;
  end;

  macro Print(MisNum:Integer)
    var i = 0, mis = 0;
    var wh = 30;
    var diff = $0;
    var query, cmd, DataSet, cnt = 0;
    var SumNSDAmount = $0;
    var Header = "Протокол сверки оборотов ценных бумаг в НРД (";

    if(hdsttds.ON_FINISH_ == "Y")
      Header = Header + "На конец операционного дня";
    else
      Header = Header + "Операционный день НРД не закрыт!";
    end;
    Header = Header + ")";

    DP_AddPrintCell( m_Rep, Header, m_tablewidth - 2, 0, "c:w:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    DP_AddPrintCell( m_Rep, "Номер отчёта НРД:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, m_Reg_No, m_tablewidth - 2 - wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    DP_AddPrintCell( m_Rep, "Остатки за период с:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, m_Start_Date, wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, "По:", 5, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, m_End_Date, m_tablewidth - 2 - wh - wh - 5, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    m_Rep.AddEmptyStr();
    m_Rep.AddStr();

    DP_AddPrintCell( m_Rep, "Всего обработано записей:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, m_TotalCnt, m_tablewidth - 2 - wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    DP_AddPrintCell( m_Rep, "Найдено несоответствий:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, MisNum, m_tablewidth - 2 - wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    m_Rep.AddEmptyStr();

    if(MisNum)
      DP_AddPrintCell( m_Rep, "Перечень несоответствий:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
      m_Rep.AddStr();

      query =   " select tmp.*, "
              + "        (select Count(1) "
              + "           from ddpnsdrep_tmp tmp1 "
              + "          where tmp1.t_depoaccnsdcode = tmp.t_depoaccnsdcode "
              + "            and tmp1.t_depopartnsdcode = tmp.t_depopartnsdcode "
              + "            and tmp1.t_isin = tmp.t_isin "
              + "            and tmp1.t_lsin = tmp.t_lsin "
              + "        ) as cnt "
              + "   from ddpnsdrep_tmp tmp "
              + "  where tmp.t_Amount <> (select SUM(tmp2.t_NSDAmount) "
              + "                           from ddpnsdrep_tmp tmp2 "
              + "                          where tmp2.t_depoaccnsdcode = tmp.t_depoaccnsdcode "
              + "                            and tmp2.t_depopartnsdcode = tmp.t_depopartnsdcode "
              + "                            and tmp2.t_isin = tmp.t_isin "
              + "                            and tmp2.t_lsin = tmp.t_lsin "
              + "                        ) "
              + " order by tmp.t_depoaccnsdcode, tmp.t_depopartnsdcode, tmp.t_FIID, tmp.t_isin, tmp.t_lsin, tmp.t_nsd_code ";
            
      cmd = DL_RSDCommand(query);

      DataSet = cmd.Execute(); 

      cnt = 0;
      while(DataSet.moveNext())
        mis = mis + 1;
        DP_AddPrintCell( m_Rep, mis,                       0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, DataSet.DepoAccNSDCode,    0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, DataSet.DepoPartNSDCode,   0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, DataSet.NSD_CODE,          0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, DataSet.LSIN,              0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, DataSet.ISIN,              0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, DataSet.FI_NAME,           0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, double(DataSet.NSDAmount), 0, DP_GetPrecision(DataSet.NSDAmount, DataSet.SumPrecision), "r:" + m_RepFontStyleTitel );

        if(DataSet.cnt > 1)
          DP_AddPrintCell( m_Rep, "",                     0, DP_GetPrecision(DataSet.Amount, DataSet.SumPrecision), "r:" + m_RepFontStyleTitel );  
        else
          DP_AddPrintCell( m_Rep, double(DataSet.Amount), 0, DP_GetPrecision(DataSet.Amount, DataSet.SumPrecision), "r:" + m_RepFontStyleTitel );  
        end;

        if(DataSet.cnt > 1)
          DP_AddPrintCell( m_Rep, "",           0, DP_GetPrecision(diff, DataSet.SumPrecision), "r:" + m_RepFontStyleTitel );
        else
          diff = DataSet.NSDAmount - DataSet.Amount;
          DP_AddPrintCell( m_Rep, double(diff), 0, DP_GetPrecision(diff, DataSet.SumPrecision), "r:" + m_RepFontStyleTitel );
        end;

        cnt = cnt + 1;
        SumNSDAmount = SumNSDAmount + DataSet.NSDAmount;

        if(cnt == DataSet.cnt)
          if(DataSet.cnt > 1)
            diff = SumNSDAmount - DataSet.Amount;

            m_Rep.AddStr();
            DP_AddPrintCell( m_Rep, "ИТОГО",                128, 0, "r:" + m_RepFontStyleTitel );
            DP_AddPrintCell( m_Rep, double(SumNSDAmount),   20,  DP_GetPrecision(DataSet.NSDAmount, DataSet.SumPrecision), "r:" + m_RepFontStyleTitel );
            DP_AddPrintCell( m_Rep, double(DataSet.Amount), 19,  DP_GetPrecision(DataSet.NSDAmount, DataSet.SumPrecision), "r:" + m_RepFontStyleTitel );
            DP_AddPrintCell( m_Rep, double(diff),           19,  DP_GetPrecision(diff, DataSet.SumPrecision), "r:" + m_RepFontStyleTitel );
          end;

          cnt = 0;
          SumNSDAmount = $0;
        end;

        m_Rep.AddStr();
      end;

      m_Rep.AddEmptyStr();
    end;

    if(m_ErrArr.size)
      m_Rep.AddNewSheetBreak("Ошибки процедуры сверки", m_HeadTableErr);

      DP_AddPrintCell( m_Rep, "Ошибки процедуры сверки:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
      m_Rep.AddStr();

      i = 0;
      while(i < m_ErrArr.size)
        DP_AddPrintCell( m_Rep, i+1,                    0, 0, "c:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_DepoAcc,  0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_DepoPart, 0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_FI_Code,  0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_LSIN,     0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_ISIN,     0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_FI_Name,  0, 0, "l:" + m_RepFontStyleTitel );
        DP_AddPrintCell( m_Rep, m_ErrArr[i].m_Reason,   0, 0, "l:" + m_RepFontStyleTitel );

        m_Rep.AddStr();

        i = i + 1;
      end;
    end;

    m_Rep.PrintWinRep();
    m_Rep.ShowWinRep();
  end;
end;

class CNSDImportTurnovers()
  private var m_Path = ""; // Путь до папки импорта с завершающим \\
  private var m_Reg_No, m_End_Date;
  private var m_Protocol = NULL;

  private macro GetStorageAcnt(NSD_AcntCode:string, StorageAcnt)
    var cmd, query, ds;

    StorageAcnt.Clear();

    query = " SELECT "
              + " root.* "
          + " FROM "
              + " ddepoacnt_dbt root "
          + " WHERE "
              + " root.t_Kind = 1 "
          + " AND root.t_Department = ? "
          + " AND RTRIM(RSB_STRUCT.GETSTRING(RSI_RSB_KERNEL.GETNOTE(" + OBJTYPE_DEPOACNT + ", TO_CHAR(root.t_AutoKey), 2, ?)), CHR(0)) = ? ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam({OperDprt});
    cmd.AddParam(date({curdate}));
    cmd.AddParam(NSD_AcntCode);

    ds = cmd.Execute();

    if(ds.moveNext())
      ds.GetRecord().CopyTo(StorageAcnt.rec);
    end;
  end;

  private macro error(errstr)
    m_Protocol.SetError(trim(hdsttds.DEPO_ACC_C),
                        trim(oprp.SECTION_CO),
                        trim(oprp.SECURITY_C),
                        trim(ToOEM(oprp.STATE_REGI,true)),
                        "",
                        trim(ToOEM(oprp.SECURITY_S,true)),
                        errstr);
  end;

  private macro text_error(errstr)
    m_Protocol.SetError("",
                        "",
                        "",
                        "",
                        "",
                        "",
                        errstr);
  end;

  private macro getCountRecords(f)
    var i = 0;
    while (next(f))
      i = i + 1;
      Message("Подсчет количества записей");
    end;
    return i;
  end;

  private macro GetFIIDbyCodeNSD(NSD_Code)
    var query, cmd, ds;
    var FIID = ALLFININSTR;

    if(NSD_Code != "")
      query =   " SELECT "
                  + " t_ObjectID "
              + " FROM "
                  + " dobjcode_dbt "
              + " WHERE "
                  + " t_ObjectType = " + OBJTYPE_FININSTR + " "
              + " AND t_CodeKind = " + FICK_NADC + " "
              + " AND t_Code = ? ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(NSD_Code);

      ds = cmd.Execute();
      if(ds.moveNext())
        FIID = int(ds.ObjectID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIIDbyLSIN(LSIN)
    var query, cmd, ds;
    var FIID = ALLFININSTR;

    if(LSIN != "")
      query = " SELECT "
                + " t_FIID "
            + " FROM "
                + " davoiriss_dbt "
            + " WHERE "
                + " t_LSIN = ? ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(LSIN);

      ds = cmd.Execute();
      if(ds.moveNext())
        FIID = int(ds.FIID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIID()
    var FIID = ALLFININSTR;

    FIID = GetFIIDbyCodeNSD(string(trim(oprp.SECURITY_C)));
    if(FIID == ALLFININSTR)
      FIID = GetFIIDbyLSIN(string(trim(ToOEM(oprp.STATE_REGI, true))));
    end;

    return FIID;
  end;

  // Загрузить обороты по счету депо из Системы
  private macro loadTurnovers(NSD_AccountCode, StartDate, EndDate)
    var err = 0;
    var query, cmd, ds, ins;
    var StorageAcnt = TRecHandler("depoacnt.dbt");

    GetStorageAcnt(NSD_AccountCode, StorageAcnt);

    if(StorageAcnt.rec.AutoKey <= 0)
      err = 1;
      text_error("Не найден счет места хранения с кодом " + NSD_AccountCode);
    end;

    if(not err)
      // Выбрать все ненулевые обороты по открытому хранению за заданный период, находящиеся на счете StorageAcnt, сгруппированные по терминальным разделам и ФИ
      // При этом у разделов должно быть определено примечание "Код в НРД"
      query =  "INSERT INTO DDPNSDREP_TMP (T_ID, T_DEPOACCNSDCODE, T_DEPOPARTNSDCODE, T_FIID, T_NSD_CODE, T_ISIN, T_LSIN, T_FI_NAME, T_SUMPRECISION, T_AMOUNT, T_NSDAMOUNT)" 
               + " SELECT 0 "
               + " ,? "
               + " ,t_DepoPartNSDCode "
               + " ,t_FIID "
               + " ,t_NSD_FICODE "
               + " ,t_ISIN "
               + " ,t_LSIN"
               + " ,t_FI_Name "
               + " ,t_SumPrecision "
               + " ,SUM(t_Amount) "
               + " ,0"
            + " FROM "
                + " (SELECT "
                     + " acc.t_DepoAcc AS t_DepoPartID "
                    + " ,NVL(RTRIM(RSB_STRUCT.GETSTRING(RSI_RSB_KERNEL.GETNOTE(" + OBJTYPE_DEPOPART + ", TO_CHAR(acc.t_DepoAcc), 2, ?)), CHR(0)), CHR(1)) AS t_DepoPartNSDCode "
                    + " ,acc.t_Code_Currency AS t_FIID "
                    + " ,fi.t_Name AS t_FI_Name "
                    + " ,fi.t_SumPrecision "
                    + " ,NVL(oc.t_Code, CHR(1)) AS t_NSD_FICODE "
                    + " ,avr.t_LSIN "
                    + " ,avr.t_ISIN "
                    + " ,-RSB_ACCOUNT.RESTAC(acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, NULL) + RSB_ACCOUNT.RESTAC(acc.t_Account, acc.t_Code_Currency, ? - 1, acc.t_Chapter, NULL) AS t_Amount "
                 + " FROM "
                     + " daccount_dbt acc "
                    + " ,dfininstr_dbt fi "
                    + " ,davoiriss_dbt avr "
                    + " ,dobjcode_dbt oc "
                 + " WHERE "
                     + " acc.t_DepoRoot = ? "
                 + " AND INSTR(acc.t_Type_Account, 'I') = 0 "
                 + " AND RSB_ACCOUNT.RESTAC(acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, NULL) - RSB_ACCOUNT.RESTAC(acc.t_Account, acc.t_Code_Currency, ? - 1, acc.t_Chapter, NULL) <> 0 "
                 + " AND NVL(RTRIM(RSB_STRUCT.GETSTRING(RSI_RSB_KERNEL.GETNOTE(" + OBJTYPE_DEPOPART + ", TO_CHAR(acc.t_DepoAcc), 2, ?)), CHR(0)), CHR(1)) != CHR(1) "
                 + " AND fi.T_FIID = acc.t_Code_Currency "
                 + " AND avr.t_FIID = acc.t_Code_Currency "
                 + " AND oc.t_ObjectType(+) = " + OBJTYPE_FININSTR + " "
                 + " AND oc.t_ObjectID(+) = acc.t_Code_Currency "
                 + " AND oc.t_CodeKind(+) = " + FICK_NRD + ") "
            + " GROUP BY "
                + " t_DepoPartID "
               + " ,t_DepoPartNSDCode "
               + " ,t_FIID "
               + " ,t_FI_Name "
               + " ,t_SumPrecision "
               + " ,t_NSD_FICODE "
               + " ,t_LSIN "
               + " ,t_ISIN ";

      ins = RSDCommand(query);

      ins.AddParam("", RSDBP_IN, trim(string(hdsttds.DEPO_ACC_C)));
      ins.AddParam("", RSDBP_IN, EndDate);
      ins.AddParam("", RSDBP_IN, EndDate);
      ins.AddParam("", RSDBP_IN, StartDate);
      ins.AddParam("", RSDBP_IN, StorageAcnt.rec.AutoKey);
      ins.AddParam("", RSDBP_IN, EndDate);
      ins.AddParam("", RSDBP_IN, StartDate);
      ins.AddParam("", RSDBP_IN, EndDate);
      
      SQL_Execute(ins);

 
      cmd = null;
      ds = null;

      // Выбрать все терминальные разделы с ненулевыми оборотами по открытому хранению за заданный период, находящиеся на счете StorageAcnt
      // При этом у которых не определено примечание "Код в НРД"
      query = " SELECT DISTINCT "
                + " dpac.t_BriefCode "
            + " FROM "
                + " daccount_dbt acc "
               + " ,ddepoacnt_dbt dpac "
            + " WHERE "
                + " acc.t_DepoRoot = ? "
            + " AND INSTR(acc.t_Type_Account, 'I') = 0 "
            + " AND RSB_ACCOUNT.RESTAC(acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, NULL) - RSB_ACCOUNT.RESTAC(acc.t_Account, acc.t_Code_Currency, ? - 1, acc.t_Chapter, NULL) <> 0 "
            + " AND NVL(RTRIM(RSB_STRUCT.GETSTRING(RSI_RSB_KERNEL.GETNOTE(" + OBJTYPE_DEPOPART + ", TO_CHAR(acc.t_DepoAcc), 2, ?)), CHR(0)), CHR(1)) = CHR(1) "
            + " AND dpac.t_AutoKey = acc.t_DepoAcc ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(StorageAcnt.rec.AutoKey);
      cmd.AddParam(EndDate);
      cmd.AddParam(StartDate);
      cmd.AddParam(EndDate);

      ds = cmd.Execute();

      // Такие разделы не рассматриваем
      while(ds.moveNext())
        text_error("Для раздела " + string(ds.BriefCode) + " счёта " + StorageAcnt.rec.BriefCode + " не определён код раздела в НРД");
      end;
    end;
  end;

  private macro AddNSDTurnovers(DepoPartNSDCode, FIID, NSD_FICODE, NSD_LSIN, NSD_Amount, NSD_FINAME)
    var i = 0, found = false, NewRest = null;
    var query, cmd, DataSet;

    if (ValType(NSD_Amount)==V_UNDEF)
      NSD_Amount=$0.0;
    end;

    cmd = DL_RSDCommand();
    
    query =   " select t_ID "
            + "   from ddpnsdrep_tmp "
            + "  where t_DepoPartNSDCode = ? ";

    cmd.AddParam(DepoPartNSDCode);

    if(FIID > 0)
      query = query + " and t_FIID = ? ";
      cmd.AddParam(FIID);
    end;

    if(NSD_FICODE != "")
      query = query + " and t_NSD_Code = ? ";
      cmd.AddParam(NSD_FICODE);
    end;

    if(NSD_LSIN != "")
      query = query + " and t_LSIN = ? ";
      cmd.AddParam(NSD_LSIN);
    end;

    DataSet = cmd.Execute(query);
    if(DataSet.moveNext())
      var upd;

      upd = RSDCommand("UPDATE DDPNSDREP_TMP  " +
                       "   SET T_NSDAMOUNT = T_NSDAMOUNT + ?, " +
                       "       T_NSD_CODE = ?, " +
                       "       T_LSIN = ?, " +
                       "       T_FI_NAME = ? " +
                       " WHERE T_ID = ?  "
                      );

      upd.NullConversion = true;
      upd.addParam("", RSDBP_IN, NSD_Amount);
      upd.addParam("", RSDBP_IN, NSD_FICODE);
      upd.addParam("", RSDBP_IN, NSD_LSIN);
      upd.addParam("", RSDBP_IN, NSD_FINAME);
      upd.addParam("", RSDBP_IN, DataSet.ID);

      SQL_Execute(upd);
    else
      var ins = RSDCommand( "INSERT INTO DDPNSDREP_TMP (T_ID, T_DEPOACCNSDCODE, T_DEPOPARTNSDCODE, T_FIID, T_NSD_CODE, T_ISIN, T_LSIN, T_FI_NAME, T_SUMPRECISION, T_AMOUNT, T_NSDAMOUNT)"
                           +"                   VALUES (0, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?)");

      ins.NullConversion = true;
      ins.addParam("", RSDBP_IN, trim(string(hdsttds.DEPO_ACC_C)));
      ins.addParam("", RSDBP_IN, DepoPartNSDCode);
      ins.addParam("", RSDBP_IN, FIID);
      ins.addParam("", RSDBP_IN, NSD_FICODE);
      ins.addParam("", RSDBP_IN, "");
      ins.addParam("", RSDBP_IN, NSD_LSIN);
      ins.addParam("", RSDBP_IN, NSD_FINAME);
      ins.addParam("", RSDBP_IN, AVOIRISS_SUM_PRECISION);
      ins.addParam("", RSDBP_IN, $0);
      ins.addParam("", RSDBP_IN, NSD_Amount);

      SQL_Execute(ins);
    end;

  end;

  // чтение сведений об оборотах
  private macro processOprp()
    var FIID = ALLFININSTR;
    var i = 0;

    var cnt = getCountRecords(oprp);

    InitProgress(cnt, "Обрабатывается справочник oprp.dbf", "Импорт сведений об оборотах");
    m_Protocol.SetCount(cnt);

    rewind(oprp);

    while(next(oprp))
      FIID = GetFIID();
      if(FIID == ALLFININSTR)
        error("В справочнике ценных бумаг не найден выпуск по указанным в отчёте НРД кодам");
      end;

      AddNSDTurnovers(trim(oprp.SECTION_CO),
                      FIID,
                      trim(oprp.SECURITY_C),
                      trim(tooem(oprp.STATE_REGI, true)),
                      IIF(trim(oprp.ORDERS_TYP) != "", money(strsubst(oprp.BALANCE_TU, " ", "")), $0),
                      trim(tooem(oprp.SECURITY_S, true)));

      i = i + 1;
      UseProgress(i);
    end;

    RemProgress();
  end;

  private macro GetMisNum()
    var query, cmd, mis = 0;

    query =   " select 1 "
            + "   from ddpnsdrep_tmp tmp "
            + "  where tmp.t_Amount <> (select SUM(tmp1.t_NSDAmount) "
            + "                           from ddpnsdrep_tmp tmp1"
            + "                          where tmp1.t_depoaccnsdcode = tmp.t_depoaccnsdcode "
            + "                            and tmp1.t_depopartnsdcode = tmp.t_depopartnsdcode "
            + "                            and tmp1.t_isin = tmp.t_isin "
            + "                            and tmp1.t_lsin = tmp.t_lsin "
            + "                        ) ";

    cmd = DL_RSDCommand(query);
    mis = cmd.GetCount();

    return mis;
  end;

  // чтение заголовков отчета и выписки
  private macro processReportH()
    var err = 0;

    if(next(reporth))
      if(next(hdsttds))
        m_Reg_No = trim(string(reporth.REG_NO));
        m_End_Date = date(hdsttds.END_DATE);
        m_Protocol = CNSDProtocol(m_Reg_No, date(hdsttds.START_DATE), m_End_Date);

        loadTurnovers(trim(string(hdsttds.DEPO_ACC_C)), date(hdsttds.START_DATE), m_End_Date); // Загрузить все сведения об оборотах на дату, хранящиеся в Системе
        processOprp(); // Загрузить все сведения об оборотах из выписки

        err = m_Protocol.GetCntErr();
        m_Protocol.Print(GetMisNum());
      else
        err = 1;
        msgbox("Отсутствует запись в файле заголовка выписки hdsttds.dbf");
      end;
    else
      err = 1;
      msgbox("Отсутствует запись в файле заголовка отчета reporth.dbf");
    end;

    return err;
  end;

  private macro moveFile(fileName:string, dir:string)
    var pathDone = "";
    var day, month, year;

    DateSplit(m_End_Date, day, month, year);
    if(day < 10)
      day = string("0" + day);
    end;
    if(month < 10)
      month = string("0" + month);
    end;

    pathDone = m_Path + dir + "\\";
    MakeDir(pathDone);
    pathDone = m_Path + dir + "\\" + string(day) + string(month) + string(year) + "\\";
    MakeDir(pathDone);
    pathDone = m_Path + dir + "\\" + string(day) + string(month) + string(year) + "\\" + m_Reg_No + "\\";
    MakeDir(pathDone);

    if(CopyFile(m_Path + fileName, pathDone + fileName))
      RemoveFile(m_Path + fileName);
    else
      msgbox("Ошибка при перемещении файла " + fileName + " в папку " + pathDone);
    end;
  end;

  // Запуск отчета
  macro Run():bool
    var err = 0;

    SQL_Execute("DELETE FROM DDPNSDREP_TMP");

    GetRegistryValue("DEPO\\NSD\\NSD_IS411_PATH", V_STRING, m_Path, err);
    if(m_Path == "")
      msgbox("Не указан путь к сетевой папке для загрузки отчётов с оборотами ценных бумаг.|Настройка DEPO\\NSD\\NSD_IS411_PATH");
    else
      m_Path = m_Path + "\\";

      if(open(reporth, m_Path + "reporth.dbf")) // Заголовок отчета
        if(open(hdsttds, m_Path + "hdsttds.dbf")) // Заголовок выписки
          if(open(oprp, m_Path + "oprp.dbf")) // Сведения об остатках
            processReportH();

            close(oprp);
          else
            err = 1;
            msgbox("Невозможно открыть файл " + m_Path + "oprp.dbf");
          end;

          close(hdsttds);
        else
          err = 1;
          msgbox("Невозможно открыть файл " + m_Path + "hdsttds.dbf");
        end;

        close(reporth);

        if(err == 0)
          moveFile("reporth.dbf", "Done");
          moveFile("hdsttds.dbf", "Done");
          moveFile("oprp.dbf", "Done");
        end;
      else
        err = 1;
        msgbox("Невозможно открыть файл " + m_Path + "reporth.dbf");
      end;
    end;

    return err;
  end;
end;

CNSDImportTurnovers.Run();
Exit(1);
