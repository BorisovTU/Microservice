/* -@H------------------------------------------------------------------
*            Автоматизированная банковская система RS-Bank
*                 Библиотека интерпретируемых модулей
*                  Copyright (c) JV R-Style 1993-2011
*                       сверка остатков по выписке
*----------------------------------------------------------------------*/
import DPInter, secinter, dpstdrep;

PRIVATE CONST DEPOATTR_ATTNUM_LOROCODE = 18; // Номер характеристики "Код раздела ЛОРО счета"
PRIVATE CONST DEPOLIST_LOROCODE = 1210; // справочник "Код раздела ЛОРО счета"

PRIVATE CONST ALG_DEPO_REQUESTKIND = 3262; // Вид запроса

PRIVATE CONST REQUESTKIND_BYPARTITIONS = 1, // с разбивкой по разделам
              REQUESTKIND_ONEFI        = 3, // конкретного выпуска ц/б
              REQUESTKIND_ONEPART      = 4, // на конкретном разделе счета депо 
              REQUESTKIND_ONEFIPART    = 5; // на конкретном разделе счета депо конкретного выпуска ц/б

PRIVATE MACRO name_alg(Type, Number)
  var NameAlg = TBFile("namealg");

  NameAlg.rec.iTypeAlg = Type;
  NameAlg.rec.iNumberAlg = Number;
  if(NameAlg.GetEQ())
    return NameAlg.rec.szNameAlg;
  end;

  return "";
END;


PRIVATE CLASS (DL_CReportTemplate) DP_CmpRstRep( _CorrAccID, _RestDate, _MessageID, _ToExcel, _is_app )
  var NeedBreak = false;
  private var CorrAccID = _CorrAccID, RestDate = _RestDate, MessageID = _MessageID, ToExcel = _ToExcel, is_app = _is_app;
  private var MsgObj = RsbDepoInfoMessage(MessageID);

  private var HeadTable = "┌──────────┬───────────────────┬──────────────────┬────────────────────────────────────────────────────────┬────────────────────────────────────────────────────────┐\n"+
                          "│  № п/п   │№ гос. регистрации,│      Краткое     │                         Выписка                        │                     Наш Депозитарий                    │\n"+
                          "│          │       ISIN        │ наименование ц/б │                                                        │                                                        │\n"+
                          "│          │                   │                  ├─────────────────┬─────────────────┬────────────────────┼─────────────────┬─────────────────┬────────────────────┤\n"+
                          "│          │                   │                  │Код ценной бумаги│раздел счета депо│Количество ц/б (шт.)│Код ценной бумаги│раздел счета депо│Количество ц/б (шт.)│\n"+
                          "├──────────┼───────────────────┼──────────────────┼─────────────────┼─────────────────┼────────────────────┼─────────────────┼─────────────────┼────────────────────┤";

  PRIVATE VAR RepHeadStr =
    "НОСТРО-счет (код. наим.) в Депозитарии: ##################################################\n"+
    "Дата сбора остатков:                    ##################################################\n"+
    "\n"+
    "Корсчет по выписке:                     ##################################################\n"+
    "Дата формирования выписки:              ##################################################\n"+
    "Вид запроса по выписке:                 ##################################################\n"+
    "\n"+
    "Всего обработано остатков:              ##################################################\n"+
    "Несоответствий остатков:                ##################################################\n";


  PRIVATE MACRO PrintMode():INTEGER
    if(ToExcel)
      return DL_OUTREPORT_EXCEL;
    else
      return DL_OUTREPORT_STD;
    end;
  END;

  PRIVATE MACRO BeginReport()
    CreateTotalBook();     
    SetActiveSheet( "Сверка" );
  END;

  PRIVATE MACRO EndReport()
    if(not NeedBreak)
      SaveTotalBook();
    else
      m_ObjTemplateXLS.SheetDelete(1);
    end;
  END;

  MACRO CReport_DataExist()
    return not NeedBreak;
  END;

  
  PRIVATE MACRO PrintRests(IsWrong, StartNum:@variant)
    var query, DataSet;
    var Select;
    var firstrun;
    var LSIN_ISIN, ExtrRest, Rest;

    query = " SELECT t_LSIN, t_ISIN, t_FIName, t_ExtrFICode, t_ExtrPartCode, t_ExtrAmount, t_FICode, t_PartCode, t_Amount " + 
            "   FROM ddpcmprst_tmp ";

    if(IsWrong)
      StartNum = 0;
      query = query + " WHERE t_ExtrAmount != t_Amount ";
    else
      query = query + " WHERE t_ExtrAmount = t_Amount ";
    end;

    query = query + " ORDER BY t_FIID, t_ExtrPartCode ";

    Select = RSDCommand( query );
    Select.NullConversion = true;

    Select.Execute();

    DataSet = TRsbDataSet(Select);

    firstrun = true;
    while(DataSet.moveNext())
      if(firstrun)
        if(IsWrong)
          CopyAllSheetInTotalBook( NULL, false, "N1_WrongHeader", 0 );
        else
          CopyAllSheetInTotalBook( NULL, false, "N1_RestHeader", 0 );
        end;

        CopyAllSheetInTotalBook( NULL, false, "N1_TableHeader", 0 );

        RegisterTable( "N1_MainTable", HeadTable,
                       "N1_A1",          
                       "N1_A2",               
                       "N1_A3",                
                       "N1_A4",        
                       "N1_A5",        
                       "N1_A6",
                       "N1_A7",
                       "N1_A8",
                       "N1_A9"         
                    );

        firstrun = false;
      end;

      StartNum = StartNum + 1;

      LSIN_ISIN = string(DataSet.LSIN);

      if(string(DataSet.ISIN) != "")
        if(LSIN_ISIN != "")
          LSIN_ISIN = LSIN_ISIN + ", ";
        end;
        LSIN_ISIN = LSIN_ISIN + string(DataSet.ISIN);
      end;
      
      if( is_app == true )
        ExtrRest = string(ЧислоCЗаданнойТочностью(double(DataSet.ExtrAmount), 0, "a"));
        Rest = string(ЧислоCЗаданнойТочностью(double(DataSet.Amount), 0, "a"));
      else
        ExtrRest = string(ЧислоCЗаданнойТочностью(double(DataSet.ExtrAmount), 0, ""));
        Rest = string(ЧислоCЗаданнойТочностью(double(DataSet.Amount), 0, ""));
      end;

      PrintTableLine( "N1_A1",  StartNum,                     null,
                      "N1_A2",  LSIN_ISIN,                    null,
                      "N1_A3",  string(DataSet.FIName),       null,
                      "N1_A4",  string(DataSet.ExtrFICode),   null,
                      "N1_A5",  string(DataSet.ExtrPartCode), null,
                      "N1_A6",  ExtrRest,                     null,  
                      "N1_A7",  string(DataSet.FICode),       null,
                      "N1_A8",  string(DataSet.PartCode),     null,
                      "N1_A9",  Rest,                         null
                    );
    end;

    if(NOT firstrun)
      EndTable();
      CopyAllSheetInTotalBook( NULL, false, GetTableRange(), 0 );
    end;
  END;

  
  PRIVATE MACRO GetRestNum( IsWrong )
    var query, DataSet;
    var Select;

    query = " SELECT COUNT(1) t_Number " + 
            "   FROM ddpcmprst_tmp  ";

    if(IsWrong)
      query = query + " WHERE t_ExtrAmount != t_Amount ";
    end;


    Select = RSDCommand( query );
    Select.NullConversion = true;

    Select.Execute();

    DataSet = TRsbDataSet(Select);
    if(DataSet.MoveNext())
      return DataSet.Number;
    else
      return 0;
    end;
  END;


  PRIVATE MACRO DefineRequestKind()
    var query, DataSet;
    var Select;
    var RequestKind = 0;

    if(MsgObj.RequestKind)
      RequestKind = MsgObj.RequestKind;
    else
      if(MsgObj.ReportRest) // Если в выписке вообще есть остатки
        if(MsgObj.ReportPart) // Если есть разбивка по разделам
          return REQUESTKIND_BYPARTITIONS; // с разбивкой по разделам
        else
          query = " SELECT COUNT(1) t_FiNum " + 
                  "   FROM ddpmsgfi_dbt  " + 
                  "  WHERE t_MessageID = ? /*1*/ ";

          Select = RSDCommand( query );
          Select.NullConversion = true;

          Select.AddParam("MessageID", RSDBP_IN, MessageID );

          Select.Execute();

          DataSet = TRsbDataSet(Select);
          if(DataSet.MoveNext())
            if((DataSet.FiNum == 1) AND (MsgObj.Partition == "")) // Указан один выпуск, без раздела
              RequestKind = REQUESTKIND_ONEFI; // конкретного выпуска ц/б
            elif((DataSet.FiNum > 1) AND (MsgObj.Partition != "")) // Указан раздел и много выпусков
              RequestKind = REQUESTKIND_ONEPART; // на конкретном разделе счета депо 
            elif((DataSet.FiNum == 1) AND (MsgObj.Partition != "")) // Один выпуск и есть раздел
              RequestKind = REQUESTKIND_ONEFIPART; // на конкретном разделе счета депо конкретного выпуска ц/б
            end;
          end;
        end;
      end;
    end;

    return RequestKind;
  END;


  PRIVATE MACRO MakeRestList(WrongNum:@variant, TotalNum:@variant)
    var RequestKind;

    SQL_Execute( " DELETE FROM ddpcmprst_tmp ", "Очистка временной таблицы", true );

    // Сначала добавим все остатки из выписки
    if(MsgObj.ReportRest) // Если в выписке вообще есть остатки
      if(MsgObj.ReportPart) // Если есть детализация по разделам - добавляем их
        SQL_Execute( " INSERT INTO ddpcmprst_tmp (t_FIID, t_LSIN, t_ISIN, t_FIName, t_ExtrFICode, t_ExtrPartCode, t_ExtrAmount, t_FICode, t_PartCode, t_PartID, t_Amount) " +
                     " SELECT msgfi.t_FIID, msgfi.t_LSIN, msgfi.t_ISIN, msgfi.t_Name, msgfi.t_SecurityCode, msgac.t_Partition, msgac.t_Rest, fi.t_FI_Code, chr(1), 0, 0 " + 
                     " FROM DDPMSGAC_DBT msgac, DDPMSGFI_DBT msgfi, DFININSTR_DBT fi " +
                     " WHERE msgac.t_MessageID = " + MessageID + " AND msgfi.t_MessageID = msgac.t_MessageID AND msgfi.t_FIID = msgac.t_FIID AND fi.t_FIID = msgfi.t_FIID ", 
                     "Добавление остатков по разделам", true );
      else // Иначе - просто ц/б с остатками, если при этом выписка по конкретному разделу - укажем, что остатки по ц/б в пределах только этого раздела
        SQL_Execute( " INSERT INTO ddpcmprst_tmp (t_FIID, t_LSIN, t_ISIN, t_FIName, t_ExtrFICode, t_ExtrPartCode, t_ExtrAmount, t_FICode, t_PartCode, t_PartID, t_Amount) " +
                     " SELECT msgfi.t_FIID, msgfi.t_LSIN, msgfi.t_ISIN, msgfi.t_Name, msgfi.t_SecurityCode, " + GetSQLString(MsgObj.Partition) + ", msgfi.t_Rest, fi.t_FI_Code, chr(1), 0, 0 " + 
                     " FROM DDPMSGFI_DBT msgfi, DFININSTR_DBT fi " + 
                     " WHERE msgfi.t_MessageID = " + MessageID + " AND fi.t_FIID = msgfi.t_FIID ", 
                     "Добавление остатков по ц/б", true );
      end;
    end;

    // Определить ID узлов нашего счета депо
    // Для самого счета депо
    SQL_Execute( " UPDATE ddpcmprst_tmp rst " +
                 "    SET rst.t_PartID = " + CorrAccID +
                 "  WHERE rst.t_ExtrPartCode = CHR(1) ", "Обновление ID счетов депо в списке остатков", true );

    // Подкачать для разделов в нашем балансе
    // Ид
    SQL_Execute( " UPDATE ddpcmprst_tmp rst" +
                 "    SET rst.t_PartID = NVL(( SELECT ac.t_AutoKey " + 
                                              " FROM ddepoacnt_dbt ac, ddepoatvl_dbt atvl, dllvalues_dbt llv " +
                                             " WHERE ac.t_AutoKey = atvl.t_ID " +
                                             "   AND atvl.t_IsType = CHR(0) " +
                                             "   AND atvl.t_AttrNum = " + DEPOATTR_ATTNUM_LOROCODE +
                                             "   AND atvl.t_IsOwn = 'X' " +
                                             "   AND atvl.t_Value = llv.t_Element " +
                                             "   AND llv.t_List = " + DEPOLIST_LOROCODE +
                                             "   AND llv.t_Code = rst.t_ExtrPartCode ), 0 ) " +
                 "  WHERE rst.t_ExtrPartCode != CHR(1) ", "Обновление разделов счетов депо в списке остатков", true );

    // Код
    SQL_Execute( " UPDATE ddpcmprst_tmp rst" +
                 "    SET rst.t_PartCode = NVL(( SELECT ac.t_BriefCode " + 
                                                " FROM ddepoacnt_dbt ac, ddepoatvl_dbt atvl, dllvalues_dbt llv " +
                                               " WHERE ac.t_AutoKey = atvl.t_ID " +
                                               "   AND atvl.t_IsType = CHR(0) " +
                                               "   AND atvl.t_AttrNum = " + DEPOATTR_ATTNUM_LOROCODE +
                                               "   AND atvl.t_IsOwn = 'X' " +
                                               "   AND atvl.t_Value = llv.t_Element " +
                                               "   AND llv.t_List = " + DEPOLIST_LOROCODE +
                                               "   AND llv.t_Code = rst.t_ExtrPartCode), CHR(1) ) " +
                 "  WHERE rst.t_ExtrPartCode != CHR(1) ", "Обновление разделов счетов депо в списке остатков", true );

    // Найти остатки в нашем учете
    SQL_Execute( " UPDATE ddpcmprst_tmp rst" +
                 "    SET rst.t_Amount = NVL(( select ABS(SUM(rsb_account.restac(acc.t_Account, acc.t_Code_Currency, " + GetSQLDate(RestDate) + ", acc.t_Chapter, NULL))) " +
                                              "  from daccount_dbt acc " +
                                              " where acc.t_DepoAcc in (select dpac.t_AutoKey " +
                                                                        " from ddepoacnt_dbt dpac " +
                                                                        " start with dpac.t_autokey = rst.t_PartID " +
                                                                        " connect by prior dpac.t_autokey = dpac.t_Superior) " +
                                              "   and acc.t_code_currency = rst.t_FIID ), 0 ) ", "Обновление остатков", true );

    RequestKind = DefineRequestKind();

    // Добавить остатки, которые есть у нас, но нет у корреспондента
    // Код ц/б и номер раздела у корреспондента для добавленных остатков не заполняются, чтобы было видно, что этих строк в выписке нет
    if((RequestKind == REQUESTKIND_BYPARTITIONS) OR (NOT MsgObj.ReportRest)) // Если с разбивкой по разделам или остатков нет - добавим все остатки по ц/б по каждому терминальному разделу, которых еще нет в списке
      SQL_Execute( " INSERT INTO ddpcmprst_tmp (t_FIID, t_LSIN, t_ISIN, t_FIName, t_ExtrFICode, t_ExtrPartCode, t_ExtrAmount, t_FICode, t_PartCode, t_PartID, t_Amount) " +
                   " SELECT acc.t_code_currency, avr.t_LSIN, avr.t_ISIN, fi.t_name, CHR(1), CHR(1), 0, fi.t_fi_code, ac.t_BriefCode, acc.t_DepoAcc, ABS(SUM(rsb_account.restac(acc.t_Account, acc.t_Code_Currency, " + GetSQLDate(RestDate) + " , acc.t_Chapter, NULL))) " +
                   " FROM daccount_dbt acc, dfininstr_dbt fi, davoiriss_dbt avr, ddepoacnt_dbt ac " +
                   " WHERE acc.t_DepoAcc IN (SELECT ac2.t_AutoKey " +
                                           " FROM ddepoacnt_dbt ac2 " +
                                           " START WITH ac2.t_autokey = " + CorrAccID +
                                           " CONNECT BY PRIOR ac2.t_autokey = ac2.t_Superior) " +
                     " AND NOT EXISTS(SELECT rst.t_FIID FROM ddpcmprst_tmp rst where rst.t_FIID = acc.t_Code_Currency AND acc.t_DepoAcc = rst.t_PartID) " +
                     " AND fi.t_FIID = acc.t_Code_Currency " +
                     " AND avr.t_FIID = fi.t_FIID " +
                     " AND ac.t_AutoKey = acc.t_DepoAcc " +
                   " GROUP BY fi.t_fi_code, avr.t_LSIN, avr.t_ISIN, fi.t_name, acc.t_code_currency, ac.t_BriefCode, acc.t_DepoAcc ",
                   "Добавление остатков", true);

    elif((NOT RequestKind) OR (RequestKind == REQUESTKIND_ONEPART)) // Если по конкретному разделу - добавим все остатки на нем по ц/б, которых нет в списке
      SQL_Execute( " INSERT INTO ddpcmprst_tmp (t_FIID, t_LSIN, t_ISIN, t_FIName, t_ExtrFICode, t_ExtrPartCode, t_ExtrAmount, t_FICode, t_PartCode, t_PartID, t_Amount) " +
                   " SELECT acc.t_code_currency, avr.t_LSIN, avr.t_ISIN, fi.t_name, CHR(1), CHR(1), 0, fi.t_fi_code, CHR(1), 0, ABS(SUM(rsb_account.restac(acc.t_Account, acc.t_Code_Currency, " + GetSQLDate(RestDate) + " , acc.t_Chapter, NULL))) " +
                   " FROM daccount_dbt acc, dfininstr_dbt fi, davoiriss_dbt avr " +
                   " WHERE acc.t_DepoAcc IN (SELECT ac2.t_AutoKey " +
                                           " FROM ddepoacnt_dbt ac2 " +
                                           " START WITH ac2.t_autokey = (select rst2.t_PartID from ddpcmprst_tmp rst2 where rownum = 1) " +
                                           " CONNECT BY PRIOR ac2.t_autokey = ac2.t_Superior) " +
                     " AND NOT EXISTS(SELECT rst.t_FIID FROM ddpcmprst_tmp rst where rst.t_FIID = acc.t_Code_Currency) " +
                     " AND fi.t_FIID = acc.t_Code_Currency " +
                     " AND avr.t_FIID = fi.t_FIID " +
                   " GROUP BY fi.t_fi_code, avr.t_LSIN, avr.t_ISIN, fi.t_name, acc.t_code_currency ",
                   "Добавление остатков", true);
    end;

    // Собрать статистику несовпадающих/всего
    WrongNum = GetRestNum(true);
    TotalNum = GetRestNum(false);
  END;


  PRIVATE MACRO GetCorrAcc()
    var query, DataSet;
    var Select;

    query = " SELECT t_BriefCode, t_Name " + 
            "   FROM ddepoacnt_dbt " + 
            " WHERE t_AutoKey = ? /*1*/ ";


    Select = RSDCommand( query );
    Select.NullConversion = true;

    Select.AddParam("CorrAccID", RSDBP_IN, CorrAccID );

    Select.Execute();

    DataSet = TRsbDataSet(Select);
    if(DataSet.MoveNext())
      return string(DataSet.BriefCode) + " (" + string(DataSet.Name) + ")";
    else
      return "";
    end;
  END;


  PRIVATE MACRO Create()
    var WrongNum = 0, TotalNum = 0;

    // Сформировать временный файл с остатками по выписке и добавить в него остатки по разделам из нашего учета
    MakeRestList(@WrongNum, @TotalNum);

    // Печатаем заголовок со статистикой
    PrintFormatString( RepHeadStr,
                   "N1_DPCorrAccCode", GetCorrAcc(),
                   "N1_DPRestDate", RestDate,
                   "N1_ExtrCorrAccCode", MsgObj.Account,
                   "N1_ExtrRestDate", MsgObj.SettlementDate,
                   "N1_Kind", name_alg(ALG_DEPO_REQUESTKIND, MsgObj.RequestKind),
                   "N1_TotalRests", TotalNum,
                   "N1_WrongRests", WrongNum
                 ); 

    CopyAllSheetInTotalBook( NULL, false, "N1_Head", 0 );

    // Выводим список отличающихся
    PrintRests(true, @WrongNum);

    // Выводим список совпадающих
    PrintRests(false, @WrongNum);

    // Выводим подвал
    DP_StdAuthPerson_Ex(null, null, this, "N1_");
    DP_StdDeposInfo_Ex(null, null, 0, this, "N1_");
    DP_StdExecReport_Ex(null, null, NULL, NULL, 0, this, "N1_");
  END;

  initDL_CReportTemplate( NULL, "dpcmprst.xls" );

END;


macro CompareMsgRests( CorrAccID, 
                     RestDate, 
                    MessageID, 
                      ToExcel, 
                       is_app )
  var stat = 0;
  var RepObj = DP_CmpRstRep(CorrAccID, RestDate, MessageID, ToExcel, is_app);

  RepObj.Run();
  stat = RepObj.NeedBreak;
  if(stat == 1)
    return stat;
  else
    return 0;
  end;
end;
