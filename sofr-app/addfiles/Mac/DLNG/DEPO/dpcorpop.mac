/*
$Name:         dpcorpop.mac
$Module:       Депозитарий
$Description:  Инициализация и проверки корпоративной операции
*/
/*
  Программист      : Chernov Anton
  Создан           : 10-11-02
*/

import BankInter, Globals, CTInter, "dprstrsc.mac", "nptxfun.mac", "nutxfun.mac";

record Dpcorpop   ( dpcorpop );
record OldDpcorpop( dpcorpop );

macro Новый_Документ( )
  return 0;
end;

macro Проверить_Документ( Режим )
  return 0;
end;

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string
  //  Возможные значения ScrolStatus:
  // 1  //отложенные
  // 2  //открытые
  // 3  //закрытые

  //  Возможные значения ScrolKind:
  // 0,   // Всегда

  //пример
  //return "/*+FIRST_ROWS LEADING(t) INDEX(t ddpcorpop_dbt_idx0)*/";

  return DefaultHint;

END;

macro Функция_Пользователя( )
  return 0;
end;

private macro GetClientCategory(
  partyID:Integer
):Integer
  var clientCategory:Integer = null;
  var party = TRecHandler( "party" );

  if( ПолучитьСубъекта( partyID, party ) == 0 )
    clientCategory = party.rec.LegalForm;
  end;

  return clientCategory;
end;

private macro GetClientNotResident(
  partyID:Integer
 ,_date:Date
):Bool

  var notResident:Bool = true;
  var party = TRecHandler( "party" );
  var objAttr = TRecHandler( "objattr" );

  if( ПолучитьСубъекта( partyID, party ) == 0 )
    // Является плательщиком НДФЛ
    if( RsbObjCategories( OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ) ).GetMainAttr( 42, _date, objAttr ) == 0 )
      // Является налоговым резидентом
      if( objAttr.rec.NameObject == "1" )
        notResident = false;
      end;
    elif( party.rec.NRCountry == "RUS" )
      notResident = false;
    end;
  end;

  return notResident;
end;

private macro GetDepoAccType(
  autoKey:Integer
):Integer

  var query:String = "";
  var dataSet = null;
  var accType:Integer = null;

  query =
    " SELECT "
      + " RSB_DEPO.DepoKindType(" + String( autoKey ) + ") AS t_Type "
  + " FROM "
      + " dual ";

  dataSet = TRsbDataSet( query );

  if( dataSet.MoveNext() )
    accType = dataSet.Type;
  end;

  return accType;
end;

macro DP_GetTaxRateFormOpenSource(
  corpopBuf:MemAddr
 ,pmaccBuf:MemAddr
 ,sFIID:Integer
)
  var corpop = TRecHandler( "dpcorpop" );
  var pmacc = TRecHandler( "dppmacc" );
  var koef:Double = 1.0;
  var isTaxRateSet:Bool = false;
  var nuTaxGroup = 0;
  var clientCategory:Integer = null;
  var notResident:Bool = null;
  var party = TRecHandler( "party" );
  var noteTaxRate:Double = 0.0;
  var objAttr = TRecHandler( "objattr" );
  var finInstr = TRecHandler( "fininstr" );
  var accType:Integer = null;

  corpop.SetRecordAddr( corpopBuf );
  pmacc.SetRecordAddr( pmaccBuf );

  koef = Pow( 10, corpop.rec.Point );

  if( pmacc.rec.BeneficiarID == {OurBank} )
    pmacc.rec.TaxRate = 0.0;
    isTaxRateSet = true;
  end;

  if( not isTaxRateSet )
    if( IsInstNeresForNUTX( pmacc.rec.BeneficiarID, corpop.rec.Date ) )
      if( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_COUPON )
        nuTaxGroup = GetTaxGroupNUTX( sFIID, corpop.rec.Date );

        if( ( nuTaxGroup == NUTAXGROUP_15 ) and not FIIfMarketNUTX( sFIID, corpop.rec.Date )
         or ( nuTaxGroup == NUTAXGROUP_15 ) )
          pmacc.rec.TaxRate = koef * GetTaxRateNUTX( INCOME_KIND_PERC, corpop.rec.Date, pmacc.rec.BeneficiarID ) * 100.0;
          isTaxRateSet = true;
        elif( ( nuTaxGroup == NUTAXGROUP_15 ) and FIIfMarketNUTX( sFIID, corpop.rec.Date ) )
          pmacc.rec.TaxRate = koef * GetTaxRateNUTX( INCOME_KIND_PRIVPERC, corpop.rec.Date, pmacc.rec.BeneficiarID ) * 100.0;
          isTaxRateSet = true;
        end;
      elif( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
        pmacc.rec.TaxRate = koef * GetTaxRateNUTX( INCOME_KIND_DIVID, corpop.rec.Date, pmacc.rec.BeneficiarID ) * 100.0;
        isTaxRateSet = true;
      end;
    end;
  end;

  if( not isTaxRateSet )
    clientCategory = GetClientCategory( pmacc.rec.BeneficiarID );
    notResident = GetClientNotResident( pmacc.rec.BeneficiarID, corpop.rec.Date );

    if( ( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_NOMINAL )
     or ( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_ISSUE ) )
      isTaxRateSet = true;
    elif( ( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_COUPON )
       or ( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS ) )

      if( ПолучитьСубъекта( pmacc.rec.BeneficiarID, party ) == 0 )
        noteTaxRate = 0.0;
        if( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_COUPON )
          // Налоговая ставка для купона в депозитарии
          noteTaxRate = ReadNoteForObject( OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ), 56 );
        elif( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
          // Налоговая ставка для дивидендов в депозитарии
          noteTaxRate = ReadNoteForObject( OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY ), 57 );
        end;
        if( noteTaxRate > 0.0 )
          pmacc.rec.TaxRate = koef * noteTaxRate;
          isTaxRateSet = true;
        end;
      end;

      if( ( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_COUPON )
      and ( clientCategory == PTLEGF_PERSN ) )
        if( ПолучитьФинИн( sFIID, finInstr ) == 0 )
          // Группа налогового учета для НДФЛ
          if( RsbObjCategories( OBJTYPE_AVOIRISS, UniID( finInstr, OBJTYPE_AVOIRISS ) ).GetMainAttr( 37, corpop.rec.Date, objAttr ) == 0 )
            // Обл. с ИП (ст. 9%)
            if( not notResident and ( objAttr.rec.NameObject == "30" ) )
              pmacc.rec.TaxRate = koef * 9.0;
              isTaxRateSet = true;
            // Обл. льгот. (ст. 0% по ПКД)
            // Обл. льгот. (ст. 0% по погаш.)
            elif( not notResident and ( ( objAttr.rec.NameObject == "40" ) or ( objAttr.rec.NameObject == "50" ) ) )
              pmacc.rec.TaxRate = IIF(corpop.rec.Date < date(1, 1, 2021), 0.0, 13.0 * koef);
              isTaxRateSet = true;
            // <Группа налогового учета для НДФЛ> = 20, вид ц/б - корпоративная облигация, номинал = рубли,
            // дата начала размещения (из анкеты ц/б) не ранее 01.01.2017, дата погашения (из истории купонов) больше 1 января 2018 года
            // и ц/б является обращающейся на дату погашения купона
            elif( DL_NPTX_IsCorpBondAfter2018( sFIID, corpop.rec.WarrantNum ) )
              if( not notResident )
                pmacc.rec.TaxRate = koef * IIF(corpop.rec.Date < date(1, 1, 2021), 35.0, 13.0);
              else
                pmacc.rec.TaxRate = koef * 30.0;
              end;
              isTaxRateSet = true;
            end;
          end;
        end;
      end;
    end;
  end;

  if( not isTaxRateSet )
    accType = GetDepoAccType( pmacc.rec.HolderAccID );

    if( clientCategory == PTLEGF_PERSN )
      if( notResident )
        if( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_COUPON )
          pmacc.rec.TaxRate = koef * 30.0;
          isTaxRateSet = true;
        elif( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
          pmacc.rec.TaxRate = koef * 15.0;
          isTaxRateSet = true;
        end;
      else
        if( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_COUPON )
          pmacc.rec.TaxRate = koef * 13.0;
          isTaxRateSet = true;
        elif( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
          pmacc.rec.TaxRate = koef * 13.0;
          isTaxRateSet = true;
        end;
      end;
    elif( clientCategory == PTLEGF_INST )
      if( notResident )
        if( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
          pmacc.rec.TaxRate = koef * 15.0;
          isTaxRateSet = true;
        elif( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_COUPON )
          pmacc.rec.TaxRate = koef * 20.0;
          isTaxRateSet = true;
        end;
      else
        if( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
          if( ( accType == OBJTYPE_DEPOKINDTYPE_ISSUER ) // Счёт доверительного управляющего
           or ( accType == OBJTYPE_DEPOKINDTYPE_NOMINALKEEPER ) ) // Счёт номинального держателя
            pmacc.rec.TaxRate = 0.0;
          else
            pmacc.rec.TaxRate = koef * 13.0;
          end;
          isTaxRateSet = true;
        end;
      end;
    else
      if( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
        if( ( accType == OBJTYPE_DEPOKINDTYPE_FOREIGNAUTHKEEPER ) // Иностранного уполномоченного держателя
         or ( accType == OBJTYPE_DEPOKINDTYPE_FOREIGNNOMKEEPER ) // Иностранного номинального держателя
         or ( accType == OBJTYPE_DEPOKINDTYPE_DEPOSITORYPROGRAM ) ) // Депозитраных программ
          pmacc.rec.TaxRate = koef * 15.0;
          isTaxRateSet = true;
        end;
      elif( corpop.rec.PaymentType == DP_CRPPAYMENTTYPE_COUPON )
        if( ( accType == OBJTYPE_DEPOKINDTYPE_FOREIGNAUTHKEEPER ) // Иностранного уполномоченного держателя
         or ( accType == OBJTYPE_DEPOKINDTYPE_FOREIGNNOMKEEPER ) // Иностранного номинального держателя
         or ( accType == OBJTYPE_DEPOKINDTYPE_DEPOSITORYPROGRAM ) ) // Депозитраных программ
          pmacc.rec.TaxRate = koef * 30.0;
          isTaxRateSet = true;
        end;
      end;
    end;
  end;

  // pmacc.rec.TaxPrice = $0; // Сумма налога
  // pmacc.rec.TaxPriceFI = NATCUR; // Валюта суммы налога
end;

macro MakeUserCorrectDPPMRAC( dppmracBuf:Memaddr )
  var dppmrac = TRecHandler( "dppmrac" );

  dppmrac.SetRecordAddr( dppmracBuf );

  // Здесь можно корректировать значения
  //dppmrac.rec.INN = "123456789";
end;
