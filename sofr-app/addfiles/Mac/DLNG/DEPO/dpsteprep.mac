/*
$Name: dpsteprep.mac
$Module: Депозитарий
$Description: Функции печати отчетов на шагах операций
*/

import "order_inv.mac", "dpglrep.mac", "dpprintmemord.mac", "dpglsend.mac";

macro DraftRepPrintStep(DraftID, DraftKind, _Rep:@variant, NeedView:bool, ShowTxtRep:bool)
  var cmd, DS;
  
  var ToExcel = false;
  var err = 0;
  VAR OldDialogFlag;
  var ThisRep;
  var RepFileName;

  if(ValType(ShowTxtRep) == V_UNDEF)
    ShowTxtRep = true;
  end;

  if(ValType(NeedView) == V_UNDEF)
    NeedView = true;
  end;

  if((ValType(_Rep) != V_UNDEF) and (_Rep != null))
    ThisRep = _Rep;
  end;

  cmd = DL_RSDCommand(  " select rep.t_Recipient, spgr.t_Xld, spgr.t_RegistrDate "
                      + "   from ddpgrrep_dbt rep, ddprepdoc_dbt repdoc, dspground_dbt spgr "
                      + "  where rep.t_SourceDocKind = ? "
                      + "    and rep.t_SourceDocID = ? "
                      + "    and repdoc.t_GrRepID = rep.t_ID "
                      + "    and spgr.t_SPgroundID = repdoc.t_RepSPgroundID "
                     );

  cmd.AddParam(DraftKind);
  cmd.AddParam(DraftID);

  if(cmd.GetCount() > 0)
    GetRegistryValue("DEPO\\DEPOREPTOEXCEL", V_BOOL, ToExcel, err);
    if( err != 0 )
      ToExcel = false;
    end;

    DS = cmd.Execute();
    
    while(DS.moveNext())
      //для каждого получателя печатаем отчет
      Order_INVbyDraft(DraftID, DS.Recipient, DS.Xld, date(DS.RegistrDate), @ThisRep);
    end;

    if(NeedView)
      OldDialogFlag = SetDialogFlag(1);

      if( ToExcel )
        ThisRep.PrintWinRep();
        ThisRep.ShowWinRep();
      else
        RepFileName = GetTxtFileName("dpinfexec");
        ThisRep.SetFileNameTxt(RepFileName);
        ThisRep.PrintRep();
        if(ShowTxtRep)
          ViewFile(RepFileName);
        end;
      end;

      SetDialogFlag(OldDialogFlag);
    end;

    _Rep = ThisRep;
  end;

end;


macro GlobopRepPrintStep(DocumentID, DocKind, _Rep:@variant, NeedView:bool)
  var cmd, DS;
  
  var ToExcel = false;
  var err = 0;
  VAR OldDialogFlag;
  var ThisRep;
  var RepFileName;

  if(ValType(NeedView) == V_UNDEF)
    NeedView = true;
  end;

  if((ValType(_Rep) != V_UNDEF) and (_Rep != null))
    ThisRep = _Rep;
  end;

  cmd = DL_RSDCommand(  " select rep.t_Recipient, spgr.t_Xld, spgr.t_RegistrDate "
                      + "   from ddpgrrep_dbt rep, ddprepdoc_dbt repdoc, dspground_dbt spgr "
                      + "  where rep.t_SourceDocKind = ? "
                      + "    and rep.t_SourceDocID = ? "
                      + "    and repdoc.t_GrRepID = rep.t_ID "
                      + "    and spgr.t_SPgroundID = repdoc.t_RepSPgroundID "
                     );

  cmd.AddParam(DocKind);
  cmd.AddParam(DocumentID);

  if(cmd.GetCount() > 0)
    GetRegistryValue("DEPO\\DEPOREPTOEXCEL", V_BOOL, ToExcel, err);
    if( err != 0 )
      ToExcel = false;
    end;

    DS = cmd.Execute();
    
    while(DS.moveNext())
      //для каждого получателя печатаем отчет
      DP_GlobRepByDoc(DocumentID, DS.Recipient, DS.Xld, date(DS.RegistrDate), @ThisRep);
    end;

    if(NeedView)
      OldDialogFlag = SetDialogFlag(1);

      if( ToExcel )
        ThisRep.PrintWinRep();
        ThisRep.ShowWinRep();
      else
        RepFileName = GetTxtFileName("dpginfex");
        ThisRep.SetFileNameTxt(RepFileName);
        ThisRep.PrintRep();
        ViewFile(RepFileName);
      end;

      SetDialogFlag(OldDialogFlag);
    end;

    _Rep = ThisRep;
  end;

end;


macro DepoMemOrder(spdraft, _Rep:@variant, NeedView:bool)
  var ToExcel = false;
  var err = 0;
  VAR OldDialogFlag;
  var ThisRep;
  var RepFileName;

  if(ValType(NeedView) == V_UNDEF)
    NeedView = true;
  end;

  if((ValType(_Rep) != V_UNDEF) and (_Rep != null))
    ThisRep = _Rep;
  end;

  GetRegistryValue("DEPO\\DEPOREPTOEXCEL", V_BOOL, ToExcel, err);
  if( err != 0 )
    ToExcel = false;
  end;

  DepoPrintMemOrder(spdraft, @ThisRep);

  if(NeedView)
    OldDialogFlag = SetDialogFlag(1);

    if( ToExcel )
      ThisRep.PrintWinRep();
      ThisRep.ShowWinRep();
    else
      RepFileName = GetTxtFileName("dpmemorder");
      SetOutPut(RepFileName, false);
      ThisRep.SetFileNameTxt(RepFileName);
      ThisRep.PrintRep();
      ViewFile(RepFileName);
    end;

    SetDialogFlag(OldDialogFlag);
  end;

  _Rep = ThisRep;

end;


macro PrintGlsendByDoc(DocumentID, ID_Operation, ID_Step)
  const DLDOC_REGLINSTATE = 890, DLDOC_INSREGLIN_REJ = 891;
  var query, Select, Data;
  
  query = " SELECT rl.t_HolderAccID " +
          " FROM ddpreglin_dbt rl " +
          " WHERE rl.t_reglineid = "
                                 " (SELECT TO_NUMBER (od.t_documentid) " +
                                    " FROM doprdocs_dbt od " +
                                    " WHERE od.t_id_operation = ? /*1*/ " +
                                    "   AND od.t_id_step = ? /*2*/ " +
                                    "   AND ( od.t_dockind = " + DLDOC_REGLINSTATE + 
                                    "       OR od.t_dockind = " + DLDOC_INSREGLIN_REJ + " ) " +
                                 " ) ";

  Select = RSDCommand( query );

  Select.AddParam("ID_Operation", RSDBP_IN, ID_Operation );  /*1*/
  Select.AddParam("ID_Step", RSDBP_IN, ID_Step );  /*2*/

  Select.NullConversion = true;
  Select.Execute();

  Data = TRsbDataSet(Select);

  if( Data.moveNext() )
    DP_GlsendByDoc( DocumentID, Data.HolderAccID );
  end;
end;
