/*
$Name:         dpstdrep.mac
$Module:       Депозитарий
$Description:  Стандартные функции для отчетов
*/
import "adress.mac", DPInter, FIInter, RsbDataSet, "dldlngfun.mac", "DLReport_h.mac", "dlquery.mac", "dpAdmoCLOB.mac", secinter, "dl_util_xml_obj.mac";

//Получить имя папки текстовых файлов
PRIVATE MACRO GetTxtPath()
  var RegistryPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";
  var Path = "";
  var stat = 0;

  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if(stat)
     msgbox("Не задана настройка в реестре банка:|", RegistryPath);
  end;

  return Path;
END;

PRIVATE MACRO processPath( p_path )
  var l_str, l_p, l_path = "";

  p_path = trim( p_path );

  if( substr( p_path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
   p_path = substr( p_path, 3 );

   l_str  = getCWD(false);
   l_p    = strbrk( l_str, "\\" );
   while( l_p != 0 )
    l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
    l_str  = substr( l_str, l_p + 1 );
    l_p    = strbrk( l_str, "\\" );
   end;
   return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );

  elif( substr( p_path, 1, 1 ) == "." ) /* ссылка на текущий каталог */
   return ( getCWD + substr( p_path, 2 ) );
  end;

  return p_path;
END;

PRIVATE MACRO GetRepFullPath(isRemote:bool)
  var Path = "";

  if(isRemote)
    var txtPath = GetTxtPath();

    if( substr( txtPath, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
      txtPath = substr(txtPath, 3 );
    end;

    if( substr( txtPath, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
      txtPath = substr(txtPath, 2 );
    end;

    Path = GetCurDir(true) + "\\" + txtPath + "\\";
  else
    Path = processPath(GetTxtPath()) + "\\";
  end;

  // получить полный путь
  return Path;
END;

//Получить имя папки со сгенерировнными отчетами
MACRO DP_GetStatementPath(isRemote)
  var RegistryPath = "DEPO\\STATEMENT_PATH";
  var Path = "";
  var stat = 0;

  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if(stat)
     msgbox("Не задана настройка в реестре банка:|", RegistryPath);
  end;

  if(Path == "")
    Path = GetRepFullPath(isRemote);
  else
    Path = GetRepFullPath(isRemote) + Path + "\\";
  end;

  return Path;
END;

private macro ПолучитьИмяСубъекта( PartyID )
   var _rParty     = TRecHandler( "party.dbt" );
   var party_name = "";
   if( PartyID >= 0 )
     if( not ПолучитьСубъекта( PartyID, _rParty ) )
        party_name = _rParty.rec.Name;
     end;
   end;
   return party_name;
end;

private macro IsExcelXmlRep(TplObj)
  var stat = false;
  if(StrUpr( GenClassName( TplObj )) == StrUpr("T_XML_Rep"))
    stat = true;
  end;
  return stat;
end;

private const empty_str = "";

// Получить дробную часть
macro DP_GetMoneyFractPart( value:Money ):Money
  var fractPart:Money = $0;

  if( ValType( value ) == V_MONEY )
    fractPart = value - Round( value, 0, 2 );

    if( fractPart < $0 )
      fractPart = $0 - fractPart;
    end;
  end;

  return fractPart;
end;

// Получить значение допустимого округления
macro DP_GetPermissPrecision( precision:Integer ):Integer
  var corrPrecision:Integer = AVOIRISS_SUM_PRECISION;

  if( ( ValType( precision ) == V_INTEGER )
  and ( precision >= 0 )
  and ( precision <= AVOIRISS_SUM_PRECISION ) )
    corrPrecision = precision;
  end;

  return corrPrecision;
end;

// Получить значение округления
macro DP_GetPrecision( value:Money, precision:Integer ):Integer
  return IIF( DP_GetMoneyFractPart( value ) > $0, DP_GetPermissPrecision( Precision ), 0 );
end;

/* получить наименование (длинное или краткое) субъекта */
macro GetPartyName(partyID, IsFullName)
  var Select, ds;

  if (valType(IsFullName) == V_UNDEF)
    IsFullName = true;
  end;

  if ( partyID != -1 )
    Select = DL_RSDCommand("select t_Name, t_ShortName from dparty_dbt where t_PartyID = ?");
    Select.AddParam(partyID);
    ds = Select.Execute();

    if ( ds.moveNext )
       if( IsFullName )
         return ds.Name;
       else
         return ds.ShortName;
       end;
    end;
  end;

  return "";
end;

/* получить краткое наименование субъекта */
macro GetPartyShortName( partyID )
  return GetPartyName( partyID, false );
end;

/* напечатать разделительную линию */
macro DP_PrintSeparatorLine()
  println("-------------------------------------------------------------------------------");
end;

/* получить номер отчета по шаблону N/XXX,
   где N - номер из spground,
       XXX - порядковый номер отчета */
private macro DP_GetRepotrNum( StrNum, Num, PrintSlash )
  if( PrintSlash == true )
    return StrNum + "/" + string(Num:3:o);
  end;

  return StrNum + string(Num:3:o);
end;

/* Подпись уполномоченного лица */
macro DP_FooterAuthFaceSign()
  var Post = "", Name = "";
  var officer = TBFile("officer", null, 5); /*для поиска начальника отдела*/
  var party  = TBFile("party");

  /* пока будем искать начальника депозитарного отдела */
  officer.Clear;
  officer.rec.PartyID             = {OurBank};
  officer.rec.OfficeID            = SelfDivision;
  officer.rec.IsFirstOfficePerson = SET_CHAR;
  if (officer.GetEQ)
     if( officer.rec.Post )
        Post = officer.rec.Post;
     else
        Post = "Начальник отдела";
     end;

     party.rec.PartyID = officer.rec.PersonID;
     if (party.GetEQ)
        Name = party.rec.Name;
     else
        Name = "";
     end;
  end;

  if( Post == "" )
     Post = "Начальник отдела";
  end;

  [ #]( Post                    +" _____________ "+Name );
  [ #]( MkStr(" ", strlen(Post))+"    подпись" );
end;

macro DP_GetFirstPersonPost()
  var Post = "";
  var officer = TBFile("officer", null, 5);

  officer.Clear;
  officer.rec.PartyID             = {OurBank};
  officer.rec.OfficeID            = SelfDivision;
  officer.rec.IsFirstOfficePerson = SET_CHAR;

  if (officer.GetEQ)
     if( officer.rec.Post )
        Post = officer.rec.Post;
     else
        Post = "Начальник отдела";
     end;

  end;

  return Post;
end;

macro DP_GetFirstPersonName()
  var Post = "";
  var Name = "";
  var officer = TBFile("officer", null, 5);
  var party  = TBFile("party");

  officer.Clear;
  officer.rec.PartyID             = {OurBank};
  officer.rec.OfficeID            = SelfDivision;
  officer.rec.IsFirstOfficePerson = SET_CHAR;

  if (officer.GetEQ)
     if( officer.rec.Post )
        Post = officer.rec.Post;
     else
        Post = "Начальник отдела";
     end;

     party.rec.PartyID = officer.rec.PersonID;
     if (party.GetEQ)
        Name = party.rec.Name;
     else
        Name = "";
     end;
  end;

  return Name;
end;

/* Подпись исполнителя */
macro DP_FooterExecuterSign(OperID)
  var Executer = DepoExecuter(OperID);
  return Executer.Name;
end;

/* Печать списока первых лиц */
macro DP_StdFirstPerson()
  /*Подпись директора банка*/
  FooterMainBossSign();
  println();

  /*Подпись главного бухгалтера*/
  FooterBookSign();
  println();

  /*Подпись "Начальника отдела"*/
  FooterBossSign();
  println();
end;

/* Печать уполномоченного лица */
macro DP_StdAuthPerson()
  /*Подпись директора банка*/
  DP_FooterAuthFaceSign();
  println();
end;

/* Печать даты исполнения и исполнителя */
macro DP_StdExecReport(drawDate, drawTime)
  if (valType(drawDate) == V_UNDEF)
    drawDate = {curdate}; /* date(); */
  end;
  if (valType(drawTime) == V_UNDEF)
    drawTime = time();
  end;

  [ дата/время     ########## ########]( drawDate:l:f, drawTime:l );
  [ исполнитель    #]( DP_FooterExecuterSign({oper}) );
  println();
end;

/* печать верхнего колонтитула */
macro DP_StdUpperHeader()
  var i, str_line = "";
  record pt(party);

  if ( ПолучитьСубъекта ({OurBank},pt) != 0 )
    msgbox("Не могу получить анкету банка");
    exit(1);
  end;

[###############################################################################](pt.Name:c:w);
  i=0;
  while( i < strlen(pt.Name) + 4 )
    str_line = str_line + "-";
    i = i + 1;
  end;
[###############################################################################](str_line:c);
  println();
end;

/* печать названия отчета и отчетного периода */
macro DP_StdRepName( RepName, RepNum, RepBegDate, RepEndDate )
  var NoEndDate = true,
      IsprintRepNum = false;

  var RepName_First = "",
      RepName_Second = "";
  var numberofpos;

  numberofpos = Index(RepName,"|");

  if( numberofpos > 0 )
      RepName_First = SubStr(RepName, 1, numberofpos-1);
      RepName_Second = SubStr(RepName, numberofpos+1);
  else
      Repname_First = RepName;
  end;

  [ #](RepName_First);

  if( numberofpos > 0)
     println();
     print( RepName_Second );
  end;

  if( valType(RepNum) != V_UNDEF )
    if( RepNum != NULL )
      if( RepNum != "" )
        if( numberofpos == 0)
          println();
        end;
        print(" № " + RepNum );
        IsprintRepNum = true;
      end;
    end;
  end;

  if (valType(RepEndDate) == V_UNDEF)
    RepEndDate = {curdate};
    NoEndDate  = false;
  end;

  if (valType(RepBegDate) != V_UNDEF) /* если начальная дата не задана, то вообще ничего из дат печатать не нужно */
    if( ( not IsprintRepNum ) and ( numberofpos == 0 ))
      println();
    end;
    if( ( RepbegDate == RepEndDate ) or ( not NoEndDate ) )
      println(" за " + DateToStr(RepBegDate, true) );
    else
      println(" за период с " + DateToStr(RepBegDate, true) + " по " + DateToStr(RepEndDate, true) );
    end;
    println();
  end;
end;

/* печать названия отчета, номера и отчетного периода */
/* умеет сама добывать номер отчета по LogOprID */
macro DP_StdRepNameLO( RepName, LogOprID, NumRep, PrintRepNum, RepBegDate, RepEndDate )
  var RepNum = "",
      RepDate = ZeroDate;

  file Spground(spground) key 6;

  if( (LogOprID != 0) AND (LogOprID != -1) )
    Spground.SourceDocKind = 830; /*SP_DEPOPER_INFOPER*/
    Spground.SourceDocID   = LogOprID;
    Spground.Direction     = EXITING;
    Spground.Department    = {OperDprt};
    Spground.Division      = SelfDivision;
    if( getEQ(Spground) )
      RepNum  = Spground.Xld;
      RepDate = Spground.RegistrDate;
    end;
  end;

  if( RepNum != "" )
    if( PrintRepNum )
      RepNum = DP_GetRepotrNum(RepNum, NumRep, true);
    end;
    RepNum = RepNum + " от " + DateToStr(RepDate, true);
  end;

  DP_StdRepName( RepName, RepNum, RepBegDate, RepEndDate );
end;

macro DP_GetRegistry( PartyID, ListKind_Ref, DocKindID, partyreg )
  if( not CB_GetRegistry( OBJTYPE_PARTY, PartyID, ListKind_Ref, DocKindID, partyreg, null ) )
    SetParm( 3, partyreg );
    return true;
  end;

  return false;
end;

private macro SetElemAdressStr(AdressStr:@variant, ElemStr:string)
  if(ElemStr != "")
    if(AdressStr != "")
      AdressStr = AdressStr + ", ";
    end;

    AdressStr = AdressStr + ElemStr;
  end;
end;

/* печать стандартной шапки отчетов (применяется в случае отсутствия использования класса Depo_StdRepSection) */
MACRO DP_StdHeader(RepName, RepNum, RepBegDate, RepEndDate)
  println();
  DP_StdUpperHeader();
  DP_StdRepName( RepName, RepNum, RepBegDate, RepEndDate );
  println();
END;

/* печать стандартной шапки отчетов (применяется в случае отсутствия использования класса Depo_StdRepSection) */
MACRO DP_StdHeaderLO(RepName, LogOprID, NumRep, PrintRepNum, RepBegDate, RepEndDate)
  println();
  DP_StdUpperHeader();
  DP_StdRepNameLO( RepName, LogOprID, NumRep, PrintRepNum, RepBegDate, RepEndDate );
  println();
END;

/* печать стандартного подвала отчетов (применяется в случае отсутствия использования класса Depo_StdRepSection) */
MACRO DP_StdFooter(drawDate, drawTime)
  println();
  DP_StdFirstPerson();
  DP_StdExecReport(drawDate, drawTime);
  println();
END;

macro FindSETTACC( PartyID, FI_Kind, FIID, _settacc )
  var stat = false;
  var settacc = TBFile("settacc");

  if ( FI_Kind == FIKIND_AVOIRISS )
    FIID = -1;
  end;

  ClearRecord( settacc );
  settacc.AddFilter( " t_PartyID = " + PartyID
                     + " and t_FIKind = " + FI_Kind
                     + " and t_FIID  = " + FIID );
  if ( settacc.next() )
     stat = true;
  end;

  copy( _settacc, settacc );

  return stat;
end;

/* вывести число с апострофами */
macro setApp( Amount, is_app )
  if( is_app )
    Amount = string(Amount:a);
  end;
  return Amount;
end;

private macro GetArchObjCode(ObjectType, ObjectID, CodeKind, BankDate)
  var query, DataSet;
  var cmd;

  query = " SELECT t_code " +
          "  FROM dobjcode_dbt " +
          " WHERE t_objecttype = ? " +
          "   AND t_objectid = ? " +
          "   AND t_codekind = ? " +
          "   AND t_unique = 'X' " +
          "   AND t_bankdate <= ? " +
          "   AND ? < DECODE ( t_bankclosedate, TO_DATE ('01.01.0001', 'dd.mm.yyyy'), TO_DATE ('31.12.9999', 'dd.mm.yyyy'), t_bankclosedate) ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ObjectType);
  cmd.AddParam(ObjectID);
  cmd.AddParam(CodeKind);
  cmd.AddParam(BankDate);
  cmd.AddParam(BankDate);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    return DataSet.Code;
  else
    return "";
  end;
end;

macro DP_GetArchSecurCode(FIID, CodeKind, BankDate)
  return GetArchObjCode(9, FIID, CodeKind, BankDate);
end;

macro DP_GetArchPartyCode(PartyID, CodeKind, BankDate)
  return GetArchObjCode(3, PartyID, CodeKind, BankDate);
end;

/* получение параметров ц/б на заданную дату из административных операций */
class CDPFinInstr( ID     : integer,
                   ArchDate : date )

   var cFIID, cArchDate;
   var avr  = TrecHandler("avoiriss");
   var fins = TrecHandler("fininstr");
   var dpadm = TBFile( "depoadmo", "R", 1 );
   var avrkind = TBFile( "avrkinds" );
   var party   = TBFile( "party" );

   var AdmOprID = 0;
   var AdmoClobPart = NULL;

   /* получить состояние объекта на дату */
   macro GetAdmo()
     var query, cmd, DataSet;
     var IsTo = false;

     query = "   select t_AdmOprID "
           + "     from ddepoadmo_dbt "
           + "    where t_FIID = ? "
           + "      and t_Item = ? "
           + "      and t_OperDate <= ? "
           + "      and exists (select 1 "
           + "                    from ddepoadmo_dbt "
           + "                   where t_FIID = ? "
           + "                     and t_Item = ? "
           + "                     and t_OperDate > ?) "
           + " order by t_OperDate desc, t_AdmOprID desc ";

     cmd = DL_RSDCommand(query);
     cmd.AddParam(cFIID);
     cmd.AddParam(SPDP_AVOIRIS);
     cmd.AddParam(cArchDate);
     cmd.AddParam(cFIID);
     cmd.AddParam(SPDP_AVOIRIS);
     cmd.AddParam(cArchDate);

     DataSet = cmd.Execute();
     if(DataSet.moveNext())
       AdmOprID = DataSet.AdmOprID;
       IsTo     = true;
     else
       query = "   select t_AdmOprID "
             + "     from ddepoadmo_dbt "
             + "    where t_FIID = ? "
             + "      and t_Item = ? "
             + "      and t_OperDate >= ? "
             + " order by t_OperDate asc, t_AdmOprID asc ";

       cmd = DL_RSDCommand(query);
       cmd.AddParam(cFIID);
       cmd.AddParam(SPDP_AVOIRIS);
       cmd.AddParam(cArchDate);

       DataSet = cmd.Execute();
       if(DataSet.moveNext())
         AdmOprID = DataSet.AdmOprID;
         IsTo     = false;
       end;
     end;

     if(AdmOprID > 0)
       if(LoadClobDataByAdmo(AdmOprID) == 0)
         if(IsTo)
           AdmoClobPart = clobData.newData;
         else
           AdmoClobPart = clobData.oldData;
         end;
       end;
     end;

   end;

   /* получить avoiriss */
   macro getAvr()
     if( avr.rec.FIID == -1 )
       ПолучитьФинИн(cFIID, fins, avr);
       GetAdmo();
     end;
     return avr.rec;
   end;

   /* получить fininstr */
   macro getFins()
     if( fins.rec.FIID == -1 )
       ПолучитьФинИн(cFIID, fins, avr);
       GetAdmo();
     end;
     return fins.rec;
   end;

   macro FIID():integer
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.FIID;
     end;

     return fins.rec.FIID;
   end;

   macro AvoirKind():integer
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.AvoirKind;
     end;

     return fins.rec.AvoirKind;
   end;

   macro AvoirKindName():string
     avrkind.rec.FI_Kind   = FIKIND_AVOIRISS;
     avrkind.rec.AvoirKind = AvoirKind();
     if( avrkind.GetEQ() )
       return avrkind.rec.Name;
     end;

     return "";
   end;

   macro LSIN():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.LSIN;
     end;

     return avr.rec.LSIN;
   end;

   macro ISIN():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.ISIN;
     end;

     return avr.rec.ISIN;
   end;

   macro CFI():string
     return DP_GetArchSecurCode(cFIID, 18 /*FICK_CFIC*/, cArchDate);
   end;

   macro Definition():string
     return fins.rec.Definition;
   end;

   macro Name():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.Name;
     end;

     return fins.rec.Name;
   end;

   macro Issuer():integer
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.Issuer;
     end;

     return fins.rec.Issuer;
   end;

   macro IssuerName():string
     if( ПолучитьСубъекта( Issuer(), party ) == 0 )
       return Party.rec.Name;
     end;

     return "";
   end;

   macro Issued():date
     if(ValType(AdmoClobPart) != V_UNDEF)
       if(ValType(AdmoClobPart.Issued) != V_UNDEF)
         return AdmoClobPart.Issued;
       else
         return ZeroDate;
       end;
     end;

     return fins.rec.Issued;
   end;

   macro FaceValue:money
     if(getAvr().IndexNom == SET_CHAR)
       return GetFaceValue(fins.rec.FIID, cArchDate);
     elif(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.FaceValue;
     end;

     return fins.rec.FaceValue;
   end;

   macro FaceValueFI():integer
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.FaceValueFI;
     end;

     return fins.rec.FaceValueFI;
   end;

   macro FaceValueFICode():string
     return GetFICode(FaceValueFI(), null, FICK_USERCODE);
   end;

   macro Qty():money
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.Qty;
     end;

     return FI_GetQtyOnDate( fins, {curdate} );
   end;

   macro DrawingDate():date
     if(ValType(AdmoClobPart) != V_UNDEF)
       if(ValType(AdmoClobPart.DrawingDate) != V_UNDEF)
         return AdmoClobPart.DrawingDate;
       else
         return ZeroDate;
       end;
     end;

     return fins.rec.DrawingDate;
   end;

   macro IncomeVolume():double
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.IncomeRate;
     end;

     return avr.rec.IncomeRate;
   end;

   macro IsClosed():integer
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.ServiceState;
     end;

     return fins.rec.ServiceState;
   end;

   /* констуктор */
   ClearRecord(avr);
   ClearRecord(fins);
   ClearRecord(dpadm);

   avr.rec.FIID  = -1;
   fins.rec.FIID = -1;

   cFIID = ID;
   if (valType(ArchDate) == V_UNDEF)
     cArchDate = {curdate};
   else
     cArchDate = ArchDate;
   end;

   getFins();

end;

/* получение параметров субъекта на заданную дату из административных операций */
class CDPParty( PtID  : integer,
                ArchDate : date,
                _OnlyPrev : bool )

   var cPartyID, cArchDate;
   var party = TBFile( "party" );
   var persn = TBFile( "persn" );
   var paper = TBFile( "paprkind" );
   var persnidc = TBFile( "persnidc" );
   var adressLeg  = TBFile( "adress" ); // адрес юридический
   var adressPost = TBFile( "adress" ); // адрес почтовый
   var dpadm = TBFile( "depoadmo", "R", 1 );
   var cntr  = TBFile( "country", "R", 2 );
   var ptregion = TBFile( "ptregion" );
   var officer  = TBFile( "officer" );
   var admterr  = TBFile( "admterr" );
   var offpt    = TBFile( "party" );
   var PartyIss = TRecHandler( "party" );
   VAR objrgdoc = TRecHandler( "objrgdoc" );
   var OnlyPrev = false;
   var AdmOprID = 0;

   var currPersNum = 0;
   var Person = null;

   var AdmoClobPart = NULL;

   /* получить состояние объекта на дату */
   private macro GetAdmo()
     var i = 0, Item;
     var query, cmd, DataSet;
     var IsTo = false;

     if( cPartyID == {OurBank} )
       Item = SPDP_OURDEPOSITARY;
     else
       Item = SPDP_CLIENT;
     end;

     query = "   select t_AdmOprID "
           + "     from ddepoadmo_dbt "
           + "    where t_PartyID = ? "
           + "      and t_Item = ? "
           + "      and t_FIID = " + ALLFININSTR + " "
           + "      and t_OperDate <= ? "
           + "      and exists (select 1 "
           + "                    from ddepoadmo_dbt "
           + "                   where t_PartyID = ? "
           + "                     and t_Item = ? "
           + "                     and t_FIID = " + ALLFININSTR + " "
           + "                     and t_OperDate > ?) "
           + " order by t_OperDate desc, t_AdmOprID desc ";

     cmd = DL_RSDCommand(query);
     cmd.AddParam(cPartyID);
     cmd.AddParam(Item);
     cmd.AddParam(cArchDate);
     cmd.AddParam(cPartyID);
     cmd.AddParam(Item);
     cmd.AddParam(cArchDate);

     DataSet = cmd.Execute();
     if(DataSet.moveNext())
       AdmOprID = DataSet.AdmOprID;
       IsTo     = true;
     elif(OnlyPrev == false)
       query = "   select t_AdmOprID "
             + "     from ddepoadmo_dbt "
             + "    where t_PartyID = ? "
             + "      and t_Item = ? "
             + "      and t_FIID = " + ALLFININSTR + " "
             + "      and t_OperDate >= ? "
             + " order by t_OperDate asc, t_AdmOprID asc ";

       cmd = DL_RSDCommand(query);
       cmd.AddParam(cPartyID);
       cmd.AddParam(Item);
       cmd.AddParam(cArchDate);

       DataSet = cmd.Execute();
       if(DataSet.moveNext())
         AdmOprID = DataSet.AdmOprID;
         IsTo     = false;
       end;
     end;

     if(AdmOprID > 0)
       if(LoadClobDataByAdmo(AdmOprID) == 0)
         if(IsTo)
           AdmoClobPart = clobData.newData;
         else
           AdmoClobPart = clobData.oldData;
         end;
       end;
     end;
   end;

   /* получить party */
   private macro getParty()
     if( party.rec.PartyID == -1 )
       GetPartyOnDate(cPartyID, cArchDate, party);
       ClearRecord(persn);
       ClearRecord(objrgdoc);
       if( party.rec.LegalForm == 2 )
         persn.rec.PersonID = party.rec.PartyID;
         persn.GetEQ();

         persnidc.KeyNum = 3;
         persnidc.rec.PersonID = party.rec.PartyID;
         persnidc.rec.IsMain = "X";
         if( persnidc.GetEQ() )
           paper.rec.PaperKind = persnidc.rec.PaperKind;
           paper.GetEQ();
         end;
       else

         var cmd, dataSet, DSRecord;
         cmd = DL_RSDCommand("select * from dobjrgdoc_dbt where t_ObjectType = ? and t_ObjectID = ? and t_ismain = 'X' and t_isclosed = chr(0)");
         cmd.addParam(OBJTYPE_PARTY);
         cmd.addParam(party.rec.PartyID);
         dataSet = cmd.execute();
         if( dataSet.MoveNext())
           DSRecord = dataSet.GetRecord();
           DSRecord.CopyTo( objrgdoc.rec );
         end;
       end;

       adressLeg.rec.PartyID = party.rec.PartyID;
       adressLeg.rec.Type    = PTADDR_LEGAL;
       adressLeg.GetEQ();

       adressPost.rec.PartyID = party.rec.PartyID;
       adressPost.rec.Type    = PTADDR_LEGAL;
       adressPost.GetEQ();

       GetAdmo();
     end;
     return party.rec;
   end;

   private macro GetPaperName(kind:integer):string
     var cmd, dataSet;

     cmd = DL_RSDCommand("SELECT t_Name FROM dpaprkind_dbt WHERE t_PaperKind = ?");
     cmd.addParam(kind);

     dataSet = cmd.execute();

     if (dataSet.moveNext())
       return dataSet.Name;
     end;

     return "";

     onError(er)
       return "";
   end;

   private macro GetRegDocName(kind:integer):string
     var cmd, dataSet;

     cmd = DL_RSDCommand("SELECT t_Name FROM dobjkdoc_dbt WHERE t_ObjectType = ? AND t_RegDocKind = ?");
     cmd.addParam(OBJTYPE_PARTY);
     cmd.addParam(kind);

     dataSet = cmd.execute();

     if (dataSet.moveNext())
       return dataSet.Name;
     end;

     return "";

     onError(er)
       return "";
   end;

   private macro GetAdmTerrName(place:string, LevelSQL:string):string
     var cmd, dataSet;

     cmd = DL_RSDCommand("SELECT t_NamePlace, t_Place FROM dadmterr_dbt WHERE t_Place = ? AND " + LevelSQL);
     cmd.addParam(place);

     dataSet = cmd.execute();

     if (dataSet.moveNext())
       return dataSet.NamePlace;
     end;

     return "";

     onError(er)
       return "";
   end;

   private macro name_alg(Type, Number)
     var cmd, dataSet;
     var nameAlg:string = "";
     cmd = DL_RSDCommand("SELECT t_szNameAlg FROM dnamealg_dbt WHERE t_iTypeAlg = ? AND t_iNumberAlg = ?");
     cmd.AddParam(Type);
     cmd.AddParam(Number);
     DataSet = cmd.Execute();

     if(DataSet.moveNext())
       nameAlg = DataSet.szNameAlg;
     end;
     return nameAlg;
   end;

   macro GetAdmOprID():integer
     return AdmOprID;
   end;

   macro PartyID():integer
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.PartyID;
     end;

     return party.rec.PartyID;
   end;

   macro LegalForm():integer
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.LegalForm;
     end;

     return party.rec.LegalForm;
   end;

   macro ShortName():string
     if((ValType(AdmoClobPart) != V_UNDEF) AND (LegalForm() == PTLEGF_INST))
       return AdmoClobPart.ShortName;
     end;

     return party.rec.ShortName;
   end;

   macro Name():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.Name;
     end;

     return party.rec.Name;
   end;

   macro Born():date
     if(ValType(AdmoClobPart) != V_UNDEF)
       if(ValType(AdmoClobPart.Born) != V_UNDEF)
         return AdmoClobPart.Born;
       else
         return ZeroDate;
       end;
     end;

     return persn.rec.Born;
   end;

   macro BirsPlase():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.BirsPlase;
     end;

     return persn.rec.BirsPlase;
   end;

   macro IsMale():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.IsMale;
     end;

     return persn.rec.IsMale;
   end;

   macro CodeINN():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.CodeINN;
     end;

     return ПолучитьКодСубъекта( cPartyID, PTCK_INN );
   end;

   macro CodeDepos():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.CodeDepos;
     end;

     return ПолучитьКодСубъекта( cPartyID, PTCK_DEPOREGN );
   end;

   macro CodeBank():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.CodeBank;
     end;

     return ПолучитьКодСубъекта( cPartyID, PTCK_BANKLICENSE );
   end;

   macro PaperKind():integer
     if(ValType(AdmoClobPart) != V_UNDEF)
       if((LegalForm() == PTLEGF_INST) and AdmoClobPart.PaperKind == 0 )
         return objrgdoc.rec.RegDocKind;
       end;
       return AdmoClobPart.PaperKind;
     end;
     if(LegalForm() == PTLEGF_PERSN)
       return persnidc.rec.PaperKind;
     end;

     return objrgdoc.rec.RegDocKind;
   end;

   macro PaperKindName():string
     if(LegalForm() == PTLEGF_PERSN)
       return GetPaperName(PaperKind());
     end;

     return GetRegDocName(PaperKind());
   end;

   macro PaperSeries():string
     if((ValType(AdmoClobPart) != V_UNDEF) and AdmoClobPart.PaperSeries != "")
       return AdmoClobPart.PaperSeries;
     end;
     if(LegalForm() == PTLEGF_INST)
       return objrgdoc.rec.Series;
     end;

     return persnidc.rec.PaperSeries;
   end;

   macro PaperNumber():string
     if((ValType(AdmoClobPart) != V_UNDEF) and AdmoClobPart.PaperNumber != "")
       return AdmoClobPart.PaperNumber;
     end;
     if(LegalForm() == PTLEGF_INST)
       return objrgdoc.rec.Number;
     end;

     return persnidc.rec.PaperNumber;
   end;

   macro PaperIssuer():integer
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.PaperIssuerID;
     end;
     if(LegalForm() == PTLEGF_INST)
       return objrgdoc.rec.RegPartyID;
     end;
     return -1;
   end;

   macro PaperIssuerName():string

     if(ValType(AdmoClobPart) != V_UNDEF)
       if(AdmoClobPart.PaperIssuer!="")
         return AdmoClobPart.PaperIssuer;
       end;
     end;

     if(LegalForm() == PTLEGF_INST)
       return ПолучитьИмяСубъекта( objrgdoc.rec.RegPartyID);
     end;
     return persnidc.rec.PaperIssuer;
   end;

   macro PaperIssuedDate():date
     if(ValType(AdmoClobPart) != V_UNDEF)
       if(ValType(AdmoClobPart.PaperIssuedDate) != V_UNDEF)
         return AdmoClobPart.PaperIssuedDate;
       else
         return ZeroDate;
       end;
     end;
     if(LegalForm() == PTLEGF_INST)
       if(objrgdoc.rec.DocDate != ZeroDate)
         return objrgdoc.rec.DocDate;
       else
         return objrgdoc.rec.StartDate;
       end;
     end;
     return persnidc.rec.PaperIssuedDate;
   end;

   macro RegNumber():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.RegNumber;
     end;

     return persnidc.rec.PaperNumber;
   end;

   macro Index():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.Index;
     end;

     return adressLeg.rec.PostIndex;
   end;

   macro Country():string
    if((ValType(AdmoClobPart) != V_UNDEF) and  (AdmoClobPart.Country != "" ))
       return AdmoClobPart.Country;
    end;
    if(party.rec.nrcountry != "")
      return party.rec.nrcountry;
    end;
    return "";
   end;

   macro CountryName():string
     var Lat3 = Country();

     if(Lat3 != "")
       cntr.rec.CodeLat3 = Lat3;
       if( cntr.GetGE() )
         return trim(cntr.rec.Name);
       end;
     end;

     return "";
   end;

   macro Region():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.Region;
     end;

     return adressLeg.rec.Region;
   end;

   macro RegionName():string
     var reg = Region();

     ptregion.rec.CodeRegion = reg;
     if( ptregion.GetEQ() )
       return ptregion.rec.NameRegion;
     end;

     return "";
   end;

   macro CodeProvince():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.CodeProvince;
     end;

     return adressLeg.rec.CodeProvince;
   end;

   macro CodeProvinceName():string
     return GetAdmTerrName(CodeProvince(), "t_ProvinceLevel = 'X'");
   end;

   macro Province():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.Province;
     end;

     return adressLeg.rec.Province;
   end;

   macro CodeDistrict():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.CodeDistrict;
     end;

     return adressLeg.rec.CodeDistrict;
   end;

   macro CodeDistrictName():string
     return GetAdmTerrName(CodeDistrict(), "t_DistrictLevel = 'X'");
   end;

   macro District():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.District;
     end;

     return adressLeg.rec.District;
   end;

   macro CodePlace():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.CodePlace;
     end;

     return adressLeg.rec.CodePlace;
   end;

   macro CodePlaceName():string
     return GetAdmTerrName(CodePlace(), "t_PlaceLevel = 'X'");
   end;

   macro Place():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.Place;
     end;

     return adressLeg.rec.Place;
   end;

   macro CodeStreet():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.CodeStreet;
     end;

     return adressLeg.rec.CodeStreet;
   end;

   macro CodeStreetName():string
     return GetAdmTerrName(CodeStreet(), "t_StreetLevel = 'X'");
   end;

   macro Street():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.Street;
     end;

     return adressLeg.rec.Street;
   end;

   macro NotResident():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.NotResident;
     end;

     return party.rec.NotResident;
   end;

   macro House():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.House;
     end;

     return adressLeg.rec.House;
   end;

   macro NumCorps():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.NumCorps;
     end;

     return adressLeg.rec.NumCorps;
   end;

   macro Flat():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.Flat;
     end;

     return adressLeg.rec.Flat;
   end;

   macro PostIndex():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.PostIndex;
     end;

     return adressPost.rec.PostIndex;
   end;

   macro Adress():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.Address;
     end;

     return adressPost.rec.Adress;
   end;

   private macro GetContact(Contacts:TArray, PartyID:integer, AddressType:integer, ContactField:integer)
     var ContactKind    = 0;
     var IsMain = SET_CHAR;
     var ContactValue = "";
     var i = 0;

     if(ContactField == CNTADR_PHONENUMBER)
       ContactKind = CNTK_PHONE;
     elif(ContactField == CNTADR_PHONENUMBER2)
       ContactKind = CNTK_PHONE;
       IsMain = UNSET_CHAR;
     elif(ContactField == CNTADR_FAX)
       ContactKind = CNTK_FAX;
     elif(ContactField == CNTADR_TELEGRAPH)
       ContactKind = CNTK_TELEGRAPH;
     elif(ContactField == CNTADR_TELEXNUMBER)
       ContactKind = CNTK_TELEX;
     elif(ContactField == CNTADR_E_MAIL)
       ContactKind = CNTK_EMAIL;
     end;

     if(ContactKind)
       while(i < Contacts.size)

          if((Contacts[i].PartyID == PartyID) AND
             (Contacts[i].AddressType == AddressType) AND
             (Contacts[i].ContactKind == ContactKind) AND
             (Contacts[i].IsMain == IsMain)
            )
            ContactValue = Contacts[i].Value;
            break;
          end;

          i = i + 1;
       end;
     end;

     return ContactValue;
   end;

   macro PhoneNumber():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       if((ValType(AdmoClobPart.Contacts) != V_UNDEF) and (AdmoClobPart.Contacts.size > 0))
         return GetContact(AdmoClobPart.Contacts, party.rec.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER);
       else
         return AdmoClobPart.PhoneNumber;
       end;
     end;

     var Value = "";
     FindAdressContactValue(party.rec.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, Value);
     return Value;
   end;

   macro PhoneNumber2():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       if((ValType(AdmoClobPart.Contacts) != V_UNDEF) and (AdmoClobPart.Contacts.size > 0))
         return GetContact(AdmoClobPart.Contacts, party.rec.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER2);
       else
         return AdmoClobPart.PhoneNumber2;
       end;
     end;

     var Value = "";
     FindAdressContactValue(party.rec.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER2, Value);
     return Value;
   end;

   macro Fax():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       if((ValType(AdmoClobPart.Contacts) != V_UNDEF) and (AdmoClobPart.Contacts.size > 0))
         return GetContact(AdmoClobPart.Contacts, party.rec.PartyID, PTADDR_LEGAL, CNTADR_FAX);
       else
         return AdmoClobPart.Fax;
       end;
     end;

     var Value = "";
     FindAdressContactValue(party.rec.PartyID, PTADDR_LEGAL, CNTADR_FAX, Value);
     return Value;
   end;

   macro Telegraph():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       if((ValType(AdmoClobPart.Contacts) != V_UNDEF) and (AdmoClobPart.Contacts.size > 0))
         return GetContact(AdmoClobPart.Contacts, party.rec.PartyID, PTADDR_LEGAL, CNTADR_TELEGRAPH);
       else
         return AdmoClobPart.Telegraph;
       end;
     end;

     var Value = "";
     FindAdressContactValue(party.rec.PartyID, PTADDR_LEGAL, CNTADR_TELEGRAPH, Value);
     return Value;
   end;

   macro TelexNumber():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       if((ValType(AdmoClobPart.Contacts) != V_UNDEF) and (AdmoClobPart.Contacts.size > 0))
         return GetContact(AdmoClobPart.Contacts, party.rec.PartyID, PTADDR_LEGAL, CNTADR_TELEXNUMBER);
       else
         return AdmoClobPart.TelexNumber;
       end;
     end;

     var Value = "";
     FindAdressContactValue(party.rec.PartyID, PTADDR_LEGAL, CNTADR_TELEXNUMBER, Value);
     return Value;
   end;

   macro E_Mail():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       if((ValType(AdmoClobPart.Contacts) != V_UNDEF) and (AdmoClobPart.Contacts.size > 0))
         return GetContact(AdmoClobPart.Contacts, party.rec.PartyID, PTADDR_LEGAL, CNTADR_E_MAIL);
       else
         return AdmoClobPart.E_Mail;
       end;
     end;

     var Value = "";
     FindAdressContactValue(party.rec.PartyID, PTADDR_LEGAL, CNTADR_E_MAIL, Value);
     return Value;
   end;

   macro NRCountry():string
     if(ValType(AdmoClobPart) != V_UNDEF)
       return AdmoClobPart.NRCountry;
     end;

     return party.rec.NRCountry;
   end;

   macro Status():string
     if((ValType(AdmoClobPart) != V_UNDEF) AND (AdmoClobPart.Status != -1))
       return name_alg(3243, IIF(AdmoClobPart.Status == 1, 0, 1));
     end;

     if( party.rec.Locked != "X" )
       return "X";
     end;

     return "";
   end;

   //Получить идентификатор субъекта с признаком первого лица
   macro GetFirstPersonID()
     var i = 0;
     var PersonID = -1;

     if((ValType(AdmoClobPart) != V_UNDEF))
       while(ValType(AdmoClobPart.Persons[i]) != V_UNDEF)

         if(AdmoClobPart.Persons[i].IsFirstPerson == SET_CHAR)
           PersonID = AdmoClobPart.Persons[i].PersonID;
           break;
         end;

         i = i + 1;
       end;
     end;

     return PersonID;
   end;

   macro GetFirstPerson()
     currPersNum = 0;
     return this.GetNextPerson();
   end;

   macro GetNextPerson()
     var stat = false;
     var ArrNum = 0;

     if((ValType(AdmoClobPart) != V_UNDEF) and (ValType(AdmoClobPart.Persons[currPersNum]) != V_UNDEF))
       Person = AdmoClobPart.Persons[currPersNum];
       stat = true;
       currPersNum = currPersNum + 1;
     else
       Person = null;
     end;

     return stat;
   end;
   //вызывать только после GetNextPerson() или GetFirstPerson()
   macro PersonName()
     return Person;
   end;

   /* констуктор */
   ClearRecord(party);
   ClearRecord(persn);
   ClearRecord(paper);
   ClearRecord(persnidc);
   ClearRecord(adressLeg);
   ClearRecord(adressPost);
   ClearRecord(dpadm);
   ClearRecord(objrgdoc);

   party.rec.PartyID  = -1;
   persn.rec.PersonID = -1;
   paper.rec.PaperKind = -1;
   persnidc.rec.PersonID = -1;
   persnidc.rec.PaperKind = -1;
//   persnidc.rec.PaperIssuer = -1;
   adressLeg.rec.PartyID = -1;
   adressPost.rec.PartyID = -1;

   cPartyID = PtID;
   if (valType(ArchDate) == V_UNDEF)
     cArchDate = {curdate};
   else
     cArchDate = ArchDate;
   end;

   if(ValType(_OnlyPrev) != V_UNDEF)
     OnlyPrev = _OnlyPrev;
   end;

   getParty();

end;

/**********************************************************************************************/
/**********************************************************************************************/
/*   НОВЫЕ СТАНДАРТНЫЕ ФУНКЦИИ ДЛЯ ОТЧЕТОВ С ВОЗМОЖНОСТЬЮ ВЫВОДА ДАННЫХ В EXCEL               */
/**********************************************************************************************/
/**********************************************************************************************/

macro GetRepEmptyStr(WidthTable, Str)
  var len = 0;
  var i = 0;
  var retstr = "";
  var width = 0;

  if(ValType(WidthTable) != V_UNDEF)
    width = WidthTable;
  end;

  len = (width - strlen(Str))/2;

  if(len < 0) len = 0; end;

  while(i<len)
    retstr = retstr + " " ;
    i = i + 1;
  end;

  return retstr;
end;

/* печать верхнего колонтитула */
macro DP_StdUpperHeader_Ex(ExRep, FontStyle, WidthTable, TplObj, TplFldPref)
  var i, str_line = "";
  var width;
  var FldPref = "";
  var BankNameHeaderStr = "#######################################################################\n";
  var OurBankFullName = GetPartyName({OurBank}, true);

  if(WidthTable)
     width = WidthTable;
  else
     if(ValType(ExRep) != V_UNDEF)
       width = ExRep.GetHeaderWidth();
     end;
  end;

  if( width == 0 )
    width = 100;
  end;

  if(ValType(TplObj) != V_UNDEF)
    if(IsExcelXmlRep(TplObj))
      TplObj.Set_WrapText(0);

      TplObj.Put_Str(OurBankFullName, "START;R="+(WidthTable-1)+";HA=C;END;");

      i=0;
      while( i < strlen(OurBankFullName) + 4 )
        str_line = str_line + "-";
        i = i + 1;
      end;

      TplObj.Put_Str(str_line, "START;R="+(WidthTable-1)+";HA=C;END;");
      TplObj.Blank_Line();

    else
      if(ValType(TplFldPref) != V_UNDEF)
        FldPref = TplFldPref;
      end;
      TplObj.PrintFormatString( "#\n"+GetRepEmptyStr(WidthTable, OurBankFullName)+BankNameHeaderStr,
                                                   FldPref+"EmptyDataStr", "",
                                                   FldPref+"BankName", OurBankFullName);
      TplObj.CopyAllSheetInTotalBook( NULL, false, FldPref+"BankNameHeader", 0 );
    end;
  else
    ExRep.AddPrintCell(OurBankFullName, width, 0, "c:w:" + FontStyle, REP_ELEM_STR);

    i=0;
    while( i < strlen(OurBankFullName) + 4 )
      str_line = str_line + "-";
      i = i + 1;
    end;

    ExRep.AddStr();
    ExRep.AddPrintCell(str_line, width, 0, "c:" + FontStyle, REP_ELEM_STR);

    ExRep.AddStr();
    ExRep.AddEmptyStr();
  end;
end;

/* печать названия отчета и отчетного периода */
macro DP_StdRepName_Ex( ExRep, FontStyle, RepName, RepNum, RepBegDate, RepEndDate, WidthTable, TplObj, TplFldPref )
  var NoEndDate = true,
      IsprintRepNum = false;

  var FldPref = "";

  var RepName_First = "",
      RepName_Second = RepName;
  var numberofpos, width;

  var RepNumStr = "";
  var DateStr = "";

  var RepNameStr    = GetRepEmptyStr(WidthTable, RepName)+"##############################################################\n";
  var RepNumFldStr  = "##############################################################\n";
  var RepDateFldStr = "##############################################################\n";

  if(WidthTable)
     width = WidthTable;
  else
    if(ValType(TplObj) == V_UNDEF)
      width = ExRep.GetHeaderWidth();
    end;
  end;

  if( width == 0 )
    width = 100;
  end;

  if(ValType(TplFldPref) != V_UNDEF)
    FldPref = TplFldPref;
  end;

  if(ValType(TplObj) == V_UNDEF)
    numberofpos = Index(RepName,"|");
    RepName_Second = RepName;

    while( numberofpos > 0 )
        RepName_First = SubStr(RepName, 1, numberofpos-1);
        RepName_Second = SubStr(RepName, numberofpos+1);
        RepName = RepName_Second;
        numberofpos = Index(RepName,"|");

        ExRep.AddStr();
        ExRep.AddPrintCell(RepName_First, width, 0, "c:w:" + FontStyle, REP_ELEM_STR);
    end;

    ExRep.AddStr();
    ExRep.AddPrintCell(RepName_Second, width, 0, "c:w:" + FontStyle, REP_ELEM_STR);
  else
    if(IsExcelXmlRep(TplObj))
      TplObj.Put_Str(RepName, "START;R="+(WidthTable-1)+";HA=C;END;");
    else
      TplObj.PrintFormatString(RepNameStr, FldPref+"RepName", RepName);
    end;
  end;

  if( valType(RepNum) != V_UNDEF )
    if( RepNum != NULL )
      if( RepNum != "" )
        if(ValType(TplObj) == V_UNDEF)
          if( numberofpos == 0)
            ExRep.AddStr();
            ExRep.AddEmptyStr();
          end;
          ExRep.AddStr();
          ExRep.AddPrintCell(" № " + RepNum, width, 0, "c:w:" + FontStyle, REP_ELEM_STR);
        else
          RepNumStr = " № " + RepNum;
        end;
        IsprintRepNum = true;
      end;
    end;
  end;

  if(ValType(TplObj) != V_UNDEF)
    if(IsExcelXmlRep(TplObj))
      TplObj.Put_Str(RepNumStr, "START;R="+(WidthTable-1)+";HA=C;END;");
    else

      TplObj.PrintFormatString(GetRepEmptyStr(WidthTable, RepNumStr)+RepNumFldStr, FldPref+"InfOprData", RepNumStr);
    end;
  end;

  if (valType(RepEndDate) == V_UNDEF)
    //RepEndDate = {curdate};
    NoEndDate  = false;
  end;

  if (valType(RepBegDate) != V_UNDEF) /* если начальная дата не задана, то вообще ничего из дат печатать не нужно */
    if(ValType(TplObj) == V_UNDEF)
      if( ( not IsprintRepNum ) and ( numberofpos == 0 ))
        ExRep.AddStr();
        ExRep.AddEmptyStr();
      end;
    end;

    if( ( RepbegDate == RepEndDate ) and (RepbegDate != NULL))
      DateStr = " за " + DateToStr(RepBegDate, true);

      if(ValType(TplObj) == V_UNDEF)
        ExRep.AddStr();
        ExRep.AddPrintCell(DateStr, width, 0, "l:" + FontStyle, REP_ELEM_STR);
      else
        if(IsExcelXmlRep(TplObj))
          TplObj.Put_Str(DateStr, "START;R="+(WidthTable-1)+";HA=C;END;");
        else
          TplObj.PrintFormatString(GetRepEmptyStr(WidthTable, DateStr)+RepDateFldStr, FldPref+"RepDate", DateStr);
        end;
      end;
    elif( not NoEndDate )
      DateStr = " по состоянию на " + DateToStr(RepBegDate, true);

      if(ValType(TplObj) == V_UNDEF)
        ExRep.AddStr();
        ExRep.AddPrintCell(DateStr, width, 0, "c:w:" + FontStyle, REP_ELEM_STR);
      else
        if(IsExcelXmlRep(TplObj))
          TplObj.Put_Str(DateStr, "START;R="+(WidthTable-1)+";HA=C;END;");
        else
          TplObj.PrintFormatString(GetRepEmptyStr(WidthTable, DateStr)+RepDateFldStr, FldPref+"RepDate", DateStr);
        end;
      end;
    else
      DateStr = " за период с " + DateToStr(RepBegDate, true) + " по " + DateToStr(RepEndDate, true);
      if(ValType(TplObj) == V_UNDEF)
        ExRep.AddStr();
        ExRep.AddPrintCell(DateStr, width, 0, "c:w:" + FontStyle, REP_ELEM_STR);
      else
        if(IsExcelXmlRep(TplObj))
          TplObj.Put_Str(DateStr, "START;R="+(WidthTable-1)+";HA=C;END;");
        else
          TplObj.PrintFormatString(GetRepEmptyStr(WidthTable, DateStr)+RepDateFldStr, FldPref+"RepDate", DateStr);
        end;
      end;
    end;
    if(ValType(TplObj) == V_UNDEF)
      ExRep.AddStr();
    end;
  end;

  if(ValType(TplObj) != V_UNDEF)
    if(not IsExcelXmlRep(TplObj))
      TplObj.CopyAllSheetInTotalBook( NULL, false, FldPref+"RepHeader", 0 );
    end;
  end;

end;

/* печать названия отчета, номера и отчетного периода */
/* умеет сама добывать номер отчета по LogOprID */
macro DP_StdRepNameLO_Ex( ExRep, FontStyle, RepName, LogOprID, NumRep, PrintRepNum, RepBegDate, RepEndDate, WidthTable, ExtRepNum, ExtRepDate, TplObj, TplFldPref )
  var RepNum = "",
      RepDate = date(0,0,0);

  file Spground(spground) key 6;

  if((ValType(ExtRepNum) != V_UNDEF) and (ExtRepNum != ""))
    RepNum = ExtRepNum;
    if((ValType(ExtRepDate) != V_UNDEF) and (ExtRepDate != ZeroDate))
      RepDate = ExtRepDate;
    end;
  elif( (LogOprID != 0) AND (LogOprID != -1) )
    Spground.SourceDocKind = 830; /*SP_DEPOPER_INFOPER*/
    Spground.SourceDocID   = LogOprID;
    Spground.Direction     = EXITING;
    Spground.Department    = {OperDprt};
    Spground.Division      = SelfDivision;
    if( getEQ(Spground) )
      RepNum  = Spground.Xld;
      RepDate = Spground.RegistrDate;
    end;
  end;

  if( RepNum != "" )
    if( (PrintRepNum) and (ValType(ExtRepNum) == V_UNDEF) or (ExtRepNum == "")) //только если не задан внешний номер (если он задан - значит мы хотим использовать только его)
      RepNum = DP_GetRepotrNum(RepNum, NumRep, true);
    end;
    RepNum = RepNum + " от " + DateToStr(RepDate, true);
  end;

  DP_StdRepName_Ex( ExRep, FontStyle, RepName, RepNum, RepBegDate, RepEndDate, WidthTable, TplObj, TplFldPref );
end;

MACRO DP_StdHeaderLO_Ex(ExRep, FontStyle, RepName, LogOprID, NumRep, PrintRepNum, RepBegDate, RepEndDate, WidthTable, ExtRepNum, ExtRepDate, TplObj, TplFldPref )
  DP_StdUpperHeader_Ex(ExRep, FontStyle, WidthTable, TplObj, TplFldPref);
  if(ValType(TplObj) == V_UNDEF)
    ExRep.AddStr();
    ExRep.AddEmptyStr();
  end;
  DP_StdRepNameLO_Ex( ExRep, FontStyle, RepName, LogOprID, NumRep, PrintRepNum, RepBegDate, RepEndDate, WidthTable, ExtRepNum, ExtRepDate, TplObj, TplFldPref );
  if(ValType(TplObj) == V_UNDEF)
    ExRep.AddStr();
    ExRep.AddEmptyStr();
  elif(IsExcelXmlRep(TplObj))
    TplObj.Blank_Line();
  end;
END;

/* Печать списока первых лиц */
macro DP_StdFirstPerson_Ex(ExRep, FontStyle, WidthTable)
  /*Подпись директора банка*/
  FooterMainBossSign(ExRep, WidthTable, FontStyle);
  ExRep.AddEmptyStr();

  /*Подпись главного бухгалтера*/
  FooterBookSign(ExRep, WidthTable, FontStyle);
  ExRep.AddEmptyStr();

  /*Подпись "Начальника отдела"*/
  FooterBossSign(ExRep, WidthTable, FontStyle);
  ExRep.AddEmptyStr();
end;

/* Печать даты исполнения и исполнителя */
macro DP_StdExecReport_Ex(ExRep, FontStyle, drawDate, drawTime, width, TplObj, TplFldPref)
  var FldPref = "";
  var ExecReportStr = "###########################\n"+
                      "исполнитель  ########################";

  if (valType(drawDate) == V_UNDEF)
    drawDate = {curdate}; /* date(); */
  end;
  if (valType(drawTime) == V_UNDEF)
    drawTime = time();
  end;
  if (valType(width) == V_UNDEF)
    width = 100;
  end;

  if(ValType(TplFldPref) != V_UNDEF)
    FldPref = TplFldPref;
  end;

  if(ValType(TplObj) == V_UNDEF)
    ExRep.AddStr();
    ExRep.AddPrintCell(string(drawDate:f) + "   " + string(drawTime), width, 0, "l" + FontStyle, REP_ELEM_STR);
    ExRep.AddStr();
    ExRep.AddPrintCell("исполнитель    " + DP_FooterExecuterSign({oper}), width, 0, "l" + FontStyle, REP_ELEM_STR);
    ExRep.AddStr();
    ExRep.AddEmptyStr();
  else
    if(IsExcelXmlRep(TplObj))
      TplObj.Set_WrapText(0);

      TplObj.Put_Str(string(drawDate:f) + "   " + string(drawTime), "START;HA=R;END;");

      TplObj.Put_Str("исполнитель",                 "START:HA=L;");
      TplObj.Put_Str(DP_FooterExecuterSign({oper}), "HA=L;END;");

      TplObj.Blank_Line();
    else

      TplObj.PrintFormatString(ExecReportStr, FldPref+"CreateDateTime", string(drawDate:f) + "   " + string(drawTime),
                                   FldPref+"OperName", DP_FooterExecuterSign({oper}));
      TplObj.CopyAllSheetInTotalBook( NULL, false, FldPref+"ExecReport", 0 );
    end;
  end;
end;

macro DP_StdDeposInfo_Pt(ExRep, FontStyle, PartyID, DeposStr, width, TplObj, TplFldPref)
  var partyreg = TRecHandler("objrgdoc");
  record RecordAdress ( adress );
  var PartyFullName = GetPartyName(PartyID, true);
  var AdressStr = "", PhoneNumber = "", Fax = "";
  var indent = 23;
  var FldPref = "";
  var CustodyStr = "#####################\n"+
                    "   наименование       ################################################\n";
  var CustodyData = "   место нахождения   ################################################\n"+
                    "   телефон            ##########################    факс   ##########################\n";

  var CustodyReg =  "   регистрация        ################################################\n";
  var CustodyLic =  "   лицензия           ################################################\n";

  if(ValType(width) == V_UNDEF)
    width = 80;
  end;

  if(ValType(TplFldPref) != V_UNDEF)
    FldPref = TplFldPref;
  end;

  ClearRecord(RecordAdress);
  НайтиЮридическийАдресСубъекта(PartyID,RecordAdress);
  FindAdressContactValue(PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber);
  FindAdressContactValue(PartyID, PTADDR_LEGAL, CNTADR_FAX, Fax);

  if(ValType(TplObj) == V_UNDEF)
    ExRep.AddPrintCell(DeposStr, width, 0, "l" + FontStyle, REP_ELEM_STR);
    ExRep.AddStr();
    ExRep.AddPrintCell("   наименование       " + PartyFullName, width, 0, "l:" + FontStyle, REP_ELEM_STR);
    ExRep.AddStr();
  else
    if(IsExcelXmlRep(TplObj))
      TplObj.Set_WrapText(0);

      TplObj.Put_Str(DeposStr, "START;HA=L;END;");
      TplObj.Put_Str("наименование " , "START;HA=R;");
      TplObj.Put_Str(PartyFullName,             "HA=L;END;");
    else
      TplObj.PrintFormatString(CustodyStr, FldPref+"CustodyType", DeposStr,
                                           FldPref+"CustodyName", PartyFullName);
    end;
  end;

  SetElemAdressStr(@AdressStr, string(RecordAdress.Country));
  SetElemAdressStr(@AdressStr, string(RecordAdress.PostIndex));
  SetElemAdressStr(@AdressStr, string(RecordAdress.Region));
  SetElemAdressStr(@AdressStr, string(RecordAdress.District));
  SetElemAdressStr(@AdressStr, string(RecordAdress.codePlace));
  SetElemAdressStr(@AdressStr, string(RecordAdress.Place));
  SetElemAdressStr(@AdressStr, string(RecordAdress.Street));
  SetElemAdressStr(@AdressStr, string(RecordAdress.House));
  SetElemAdressStr(@AdressStr, string(RecordAdress.NumCorps));
  SetElemAdressStr(@AdressStr, string(RecordAdress.Flat));

  if(ValType(TplObj) == V_UNDEF)
    ExRep.AddPrintCell("   место нахождения   " + AdressStr, width, 0, "l" + FontStyle, REP_ELEM_STR);
    ExRep.AddStr();
    ExRep.AddPrintCell("   телефон            " + PhoneNumber + " факс " + Fax, width, 0, "l:" + FontStyle, REP_ELEM_STR);
    ExRep.AddStr();
  else
    if(IsExcelXmlRep(TplObj))
      TplObj.Set_WrapText(0);

      TplObj.Put_Str("место нахождения ", "START;HA=R;");
      TplObj.Put_Str(AdressStr,                "HA=L;END;");

      TplObj.Put_Str("телефон ",   "START;HA=R;");
      TplObj.Put_Str(PhoneNumber, "HA=L;");
      TplObj.Put_Str("факс ",      "HA=R;");
      TplObj.Put_Str(Fax,         "HA=L;END;");
    else
      TplObj.PrintFormatString(CustodyData, FldPref+"CustodyPlace", AdressStr,
                                            FldPref+"CustodyPhone", PhoneNumber,
                                            FldPref+"CustodyFax",   Fax);
    end;
  end;

  if( DP_GetRegistry( PartyID, REGPT_TAXINSTITUTE, OBJKDOC_EVIDENCE_GOS_REG, partyreg ) )
    if(ValType(TplObj) == V_UNDEF)
      ExRep.AddPrintCell("   регистрация        ", indent, 0, "l:w:" + FontStyle, REP_ELEM_STR);
      ExRep.AddPrintCell("Свидетельство о регистрации №" + partyreg.rec.Number + " от " +
                          DateToStr(partyreg.rec.StartDate, true) + " выдано " + GetPartyName(partyreg.rec.RegPartyID),
                          width-indent, 0, "l:w:" + FontStyle, REP_ELEM_STR);
      ExRep.AddStr();
    else
      if(IsExcelXmlRep(TplObj))
        TplObj.Set_WrapText(0);

        TplObj.Put_Str("регистрация ", "START;HA=R;");
        TplObj.Put_Str("Свидетельство о регистрации №" + partyreg.rec.Number + " от " +
                       DateToStr(partyreg.rec.StartDate, true) + " выдано " + GetPartyName(partyreg.rec.RegPartyID), "HA=L;END;");
      else
        TplObj.PrintFormatString(CustodyReg, FldPref+"CustodyDocReg", "Свидетельство о регистрации №" + partyreg.rec.Number + " от " +
                                 DateToStr(partyreg.rec.StartDate, true) + " выдано " + GetPartyName(partyreg.rec.RegPartyID));
      end;
    end;
  else
    if(ValType(TplObj) == V_UNDEF)
      ExRep.AddPrintCell("   регистрация        ", width, 0, "l" + FontStyle, REP_ELEM_STR);
      ExRep.AddStr();
    else
      if(IsExcelXmlRep(TplObj))
        TplObj.Set_WrapText(0);

        TplObj.Put_Str("регистрация ", "START;HA=R;END;");
      else
        TplObj.PrintFormatString(CustodyReg, FldPref+"CustodyDocReg", "");
      end;
    end;
  end;

  if( DP_GetRegistry( PartyID, REGPT_FKCB, OBJKDOC_LICEN_DEPOSITARY, partyreg ) )
    if(ValType(TplObj) == V_UNDEF)
      ExRep.AddPrintCell("   лицензия           ", indent, 0, "l:w:" + FontStyle, REP_ELEM_STR);

      ExRep.AddPrintCell("№ " + partyreg.rec.Number + " от " + DateToStr(partyreg.rec.StartDate, true) +
                         ", действительна до " + DateToStr(partyreg.rec.FinishDate, true) +
                         " выдана " + GetPartyName(partyreg.rec.RegPartyID), width-indent, 0, "l:w:" + FontStyle, REP_ELEM_STR);
      ExRep.AddStr();
    else
      if(IsExcelXmlRep(TplObj))
        TplObj.Set_WrapText(0);

        TplObj.Put_Str("лицензия ", "START;HA=R;");
        TplObj.Put_Str("№ " + partyreg.rec.Number + " от " + DateToStr(partyreg.rec.StartDate, true) +
                       ", действительна до " + DateToStr(partyreg.rec.FinishDate, true) +
                       " выдана " + GetPartyName(partyreg.rec.RegPartyID), "HA=L;END;");
      else
        TplObj.PrintFormatString(CustodyLic, FldPref+"CustodyLic", "№ " + partyreg.rec.Number + " от " + DateToStr(partyreg.rec.StartDate, true) +
                            ", действительна до " + DateToStr(partyreg.rec.FinishDate, true) +
                            " выдана " + GetPartyName(partyreg.rec.RegPartyID));
      end;
    end;
  else
    if(ValType(TplObj) == V_UNDEF)
      ExRep.AddPrintCell("   лицензия           ", width, 0, "l" + FontStyle, REP_ELEM_STR);
      ExRep.AddStr();
    else
      if(IsExcelXmlRep(TplObj))
        TplObj.Set_WrapText(0);

        TplObj.Put_Str("лицензия ", "START;HA=R;END;");
      else
        TplObj.PrintFormatString(CustodyLic, FldPref+"CustodyLic", "");
      end;
    end;
  end;
  if(ValType(TplObj) == V_UNDEF)
    ExRep.AddEmptyStr();
  else
    if(IsExcelXmlRep(TplObj))
      TplObj.Blank_Line();
    else
      TplObj.CopyAllSheetInTotalBook( NULL, false, FldPref+"CustodyData", 0 );
    end;
  end;
end;

/* Печать сведений о депозитарии (нашем банке)*/
macro DP_StdDeposInfo_Ex(ExRep, FontStyle, width, TplObj, TplFldPref)

  DP_StdDeposInfo_Pt(ExRep, FontStyle, {OurBank}, "Депозитарий", width, TplObj, TplFldPref);

end;

/* Подпись уполномоченного лица */
macro DP_FooterAuthFaceSign_Ex(ExRep, FontStyle, TplObj, TplFldPref)
  var Post = "", Name = "";
  var officer = TBFile("officer", null, 5); /*для поиска начальника отдела*/
  var party  = TBFile("party");
  var FldPref = "";
  var OfficerStr = "\n#########################  __________________  ##########################\n"+
                   "                                 подпись\n";

  if(ValType(TplFldPref) != V_UNDEF)
    FldPref = TplFldPref;
  end;

  /* пока будем искать начальника депозитарного отдела */
  officer.Clear;
  officer.rec.PartyID             = {OurBank};
  officer.rec.OfficeID            = SelfDivision;
  officer.rec.IsFirstOfficePerson = SET_CHAR;
  if (officer.GetEQ)
     if( officer.rec.Post )
        Post = officer.rec.Post;
     else
        Post = "Начальник отдела";
     end;

     party.rec.PartyID = officer.rec.PersonID;
     if (party.GetEQ)
        Name = party.rec.Name;
     else
        Name = "";
     end;
  end;

  if( Post == "" )
     Post = "Начальник отдела";
  end;

  if(ValType(TplObj) == V_UNDEF)
    ExRep.AddPrintCell(" " + Post                    +" _____________ "+Name, 100, 0, "l:" + FontStyle, REP_ELEM_STR);
    ExRep.AddStr();
    ExRep.AddPrintCell(" " + MkStr(" ", strlen(Post))+"    подпись", 100, 0, "l:" + FontStyle, REP_ELEM_STR);
    ExRep.AddStr();
  else
    if(IsExcelXmlRep(TplObj))
      TplObj.Set_WrapText(0);

      TplObj.Put_Str(" " + Post, "START;HA=L;");
      TplObj.Put_Str("_____________", "HA=C;");
      TplObj.Put_Str(Name, "HA=L;END;");

      TplObj.Put_Str("", "START;");
      TplObj.Put_Str("подпись", "HA=C;END;");

      TplObj.Blank_Line();
    else
      TplObj.PrintFormatString(OfficerStr, FldPref+"OfficerPost", Post,
                                           FldPref+"OfficerName", Name);
      TplObj.CopyAllSheetInTotalBook( NULL, false, FldPref+"Officer", 0 );
    end;
  end;
end;

/* Печать уполномоченного лица */
macro DP_StdAuthPerson_Ex(ExRep, FontStyle, TplObj, TplFldPref)
  /*Подпись директора банка*/
  DP_FooterAuthFaceSign_Ex(ExRep, FontStyle, TplObj, TplFldPref);
  if(ValType(TplObj) == V_UNDEF)
    ExRep.AddEmptyStr();
  end;
end;

/* печать стандартного подвала отчетов (применяется в случае отсутствия использования класса Depo_StdRepSection) */
MACRO DP_StdFooter_Ex(ExRep, FontStyle, drawDate, drawTime, WidthTable)
  var DeposData, query;

  ExRep.AddEmptyStr();
  ExRep.AddEmptyStr();
  DP_StdFirstPerson_Ex(ExRep, FontStyle, WidthTable);

  //получим головной депозитарий
  query =   " SELECT t_PartyID FROM ddp_dep_dbt "
          + " WHERE t_NodeType = 1 AND t_Status = 2 AND t_ParentCode = 0 "
          + " START WITH t_Code = " + {OperDprt}
          + " CONNECT BY t_Code = PRIOR t_ParentCode ";
  DeposData = TRsbDataSet(query);

  if( (DeposData.moveNext()) and (DeposData.PartyID != {OurBank}))
    DP_StdDeposInfo_Pt(ExRep, FontStyle, DeposData.PartyID, "Сведения о головном депозитарии:", WidthTable);

    DP_StdDeposInfo_Pt(ExRep, FontStyle, {OurBank}, "Сведения о депозитарии текущего филиала:", WidthTable);
  else
    DP_StdDeposInfo_Ex(ExRep, FontStyle, WidthTable );
  end;

  DP_StdExecReport_Ex(ExRep, FontStyle, drawDate, drawTime, WidthTable);
  ExRep.AddEmptyStr();
END;

//Ф-ция формирования отчетов об исполнении для ИО (вызывать соответственно до отображения на экран)
macro DP_PrintAllLogoprExtRep(Rep:@variant, LogoprID, AddNewSheet:bool)
  var DS, cmd;
  var _Rep = Rep;
  var cnt = 0;

  if(LogoprID <= 0) return end;

  cmd = DL_RSDCommand(  " select rep.t_Recipient, spgr.t_Xld, spgr.t_RegistrDate "
                      + "   from ddpgrrep_dbt rep, ddprepdoc_dbt repdoc, dspground_dbt spgr "
                      + "  where rep.t_SourceDocKind = " + 830 /*SP_DEPOPER_INFOPER*/
                      + "    and rep.t_SourceDocID = ? "
                      + "    and repdoc.t_GrRepID = rep.t_ID "
                      + "    and spgr.t_SPgroundID = repdoc.t_RepSPgroundID "
                     );
  cmd.AddParam(LogoprID);
  cnt = cmd.GetCount();
  if(cnt > 0)
    if(AddNewSheet)
      _Rep.AddNewSheetBreak( "Отчет об исполнении" );
      _Rep.SetHeaderStr("");
    else
      _Rep.GetCurSheet().SetSheetName("Отчет об исполнении");
    end;

    DS = cmd.Execute();

    while(DS.moveNext())
      ExecMacroFile("order_inf.mac", "Order_INFbyLogID", LogoprID, DS.Recipient, DS.Xld, date(DS.RegistrDate), @_Rep);
    end;

    Rep = _Rep;
  end;

  return cnt;
end;

/*Отказ для информационной операции*/
macro DP_StdReportControl( LogoprID,     //идентификатор инф. операции
                           Counter,      //счетчик напечатанных блоков
                           EmptyRepFun,  //обработчик возникновения отказа, но пользователь хочет печатать дальше
                           Parm,         //массив параметров
                           ToExcel,      //выпускать в Excel
                           Rep,          //объект отчета
                           ZoomType,     //ориентация при печате
                           TplObj,       //объект класса выпуска отчета с использованием шаблона
                           TplFldPref,   //префикс для полей в шаблоне
                           TblName,      //имя файла вывода
                           RepObj
                         )

  Array Text;
  Array Button;
  var but, ind = 0;
  var err = 0;
  var rej_prm;
  var stat = 0;
  var RejText;
  file logopr("splogopr");
  const SC_SPDRAFT_STATUS_CLOSE = 3;
  var FldPref = "";
  var EmptyDataStr = "#####################################################################################\n";
  file ReportOutFile() txt write;
  var ReportFileName, errcode, errtext;

  Button(0) = "Прервать выполнение";
  Button(1) = "Продолжить";
  Button(2) = "Регистрировать";

  if(ValType(TplFldPref) != V_UNDEF)
    FldPref = TplFldPref;
  end;

  //1.Если счетчик нулевой - т.е. нет данных для отчета, то варианты на выбор
  if( (LogoprID > 0) and (Counter == 0) )
    RejText = "Нет данных, удовлетворяющих параметрам отчета.";

    logopr.AutoKey = LogoprID;
    if((GetEQ(logopr)) and (logopr.State == SC_SPDRAFT_STATUS_CLOSE ))
      if(ValType(TplObj) == V_UNDEF)
        Rep.AddPrintCell( RejText, 50, 0, "l:", REP_ELEM_STR );
      else
        if(IsExcelXmlRep(TplObj))
          TplObj.Set_WrapText(0);

          TplObj.Put_Str("Нет данных, удовлетворяющих параметрам отчета", "START;END;");
        else
          TplObj.PrintFormatString(EmptyDataStr, FldPref+"EmptyDataStr", "Нет данных, удовлетворяющих параметрам отчета");
          TplObj.CopyAllSheetInTotalBook( NULL, false, FldPref+"EmptyDataStr", 0 );
        end;
      end;
    else
      Text(ind) = RejText + " Регистрировать отказ?";
      but = ConfWin(Text,Button);
      if(but == 0)
        stat = 1;  //Т.е. останавливаем выпуск отчета
        if(ValType(TplObj) != V_UNDEF)
          TplObj.NeedBreak = stat;
        end;
      elif(but == 1)
        if(ValType(TplObj) == V_UNDEF)
          ExecMacro2(EmptyRepFun, Rep, Parm);  //печатать урезанный вариант отчета
        else
          if(RepObj != V_UNDEF)
            RepObj.CreateEmptyRep();
          else
            TplObj.CreateEmptyRep();
          end;
        end;
        stat = 0;
      elif(but == 2)
        stat = GetRejectNoteDR(LogoprID, DP_INFOPR_OPERATION, RejText, rej_prm);
        if(stat == DP_REJECTDRAFT_OK)
          stat = 2; //отказ зарегили = там уже статус поменялся - печатать не надо
        elif(stat == DP_REJECTDRAFT_BTRERR)
          stat = 1;
          msgbox("Ошибка менеджера записей!");
        elif(stat == DP_REJECTDRAFT_WRONG_PARAMS)
          stat = 1;
          msgbox( "Функции формирования отказа обработки поручения переданы неверные параметры" );
        elif(stat)
          stat = 1;
        end;
        if(ValType(TplObj) != V_UNDEF)
          TplObj.NeedBreak = stat;
        end;
      end;
    end;
  elif(Counter == 0)
    if(ValType(TplObj) == V_UNDEF)
      Rep.AddEmptyStr();
      Rep.AddPrintCell( "Нет данных, удовлетворяющих параметрам отчета.", 100, 0, "l:", REP_ELEM_STR );
      Rep.AddEmptyStr();
    else
      if(IsExcelXmlRep(TplObj))
        TplObj.Set_WrapText(0);

        TplObj.Put_Str("Нет данных, удовлетворяющих параметрам отчета", "START;END;");
      else
        TplObj.PrintFormatString( EmptyDataStr, FldPref+"EmptyDataStr", "Нет данных, удовлетворяющих параметрам отчета");
        TplObj.CopyAllSheetInTotalBook( NULL, false, FldPref+"EmptyDataStr", 0 );
      end;
    end;
  end;

  if( stat == 0 )
    var cnt = 0;
    if(LogOprID)
      var AddNewSheet = true;

      if(ValType(TplObj) != V_UNDEF)
        Rep = CMakeReport();  //отчеты об исполнении пока всегда выводятся с помощью обычного WinRep (т.е. без шаблонов)
        AddNewSheet = false;
      end;

      //напечатать отчеты об исполнении
      cnt = DP_PrintAllLogoprExtRep(@Rep, LogOprID, AddNewSheet);
    end;

    if((ValType(Rep) != V_UNDEF) and (Rep != null) and ((ValType(TplObj) == V_UNDEF) OR (cnt > 0)))
      if( ToExcel )
        if (ValType(ZoomType) != V_UNDEF)
          Rep.SetZoomType( ZoomType );
        end;
        Rep.PrintWinRep();
        Rep.ShowWinRep();
      else
        if(ValType(TblName) != V_UNDEF)
          ReportFileName = GetTxtFileName( TblName );
          if( Open( ReportOutFile, ReportFileName ) == false )
             errcode = status( errtext );
             MsgBox( "Ошибка при открытии файла отчета|"+ ReportFileName +"|(" + errcode + ") " + errtext);
             break;
          end;
          SetOutput( ReportFileName );
          Rep.PrintRep();
          SetOutput( NULL, true );
          close( ReportOutFile );
          ViewFile( ReportOutFile );
        else
          Rep.PrintRep();
        end;
      end;
    end;
  end;

  if( stat == 2 )  stat = 0; end;

  return stat;
end;

class DP_AvoirKind(AvoirKind)
  private var m_query = "";
  private var m_counter = 0;
  private var m_DataSet = "";

  macro GetNext()
    if( m_counter == 0 )
      m_DataSet = TRsbDataSet(m_query);
    end;
    m_counter = m_counter + 1;
    return m_DataSet.moveNext();
  end;

  macro GetKind()
    return m_DataSet.AvoirKind;
  end;

  //делаем запрос в конструкторе
  m_query =   " select avr.t_AvoirKind from davrkinds_dbt avr "
            + " where avr.t_FI_Kind = 2 "
            + "   and avr.t_AvoirKind in ( select a.t_AvoirKind from davrkinds_dbt a "
            + "                            start with a.t_fi_kind = 2 AND a.t_avoirkind = " + AvoirKind
            + "                            connect by a.t_fi_kind = prior a.t_fi_kind "
            + "                            and prior a.t_avoirkind = a.t_parent )";

end;

/* Класс, инкапсулирующий ProgressBar. Нужен для того, чтобы вручную
   не управлять счетчиком. */
class CProgressBar
   private var
      IsShowProgress = false,
      Counter        = 0;

   /* Инициализация прогрессбара.
         CountRec       -  размер шкалы
         Mes            -  сообщение в статус строку
         Head           -  сообщение в заголовок окна
         MinPossCount   -  минимальный размер шкалы, для которого
                           прогрессбар будет показан */
   macro Init( CountRec, Mes, Head, MinPossCount )
      IsShowProgress = (MinPossCount == null) or (CountRec >= MinPossCount);
      Counter = 0;

      if( IsShowProgress )
         InitProgress( CountRec, Mes, Head );
      end;
   end;

   /* Увеличить счетчик на 1. */
   macro Use
      if( IsShowProgress )
         Counter = Counter + 1;
         UseProgress( Counter );
      end;

      return Counter;
   end;

   /* Убрать прогрессбар с экрана. */
   macro Remove
      if( IsShowProgress )
         RemProgress;
         IsShowProgress = false;
      end;
   end;

   /* Убрать прогрессбар с экрана автоматически, если не сделали этого сами */
   macro destructor
      Remove;
   end;
end;

class DPRepLog()

  private var replog = TBFile("deporeplog.tmp");
  private var cmd = RSDCommand;
  private var m_query = "";
  private var m_DataSet = null;
  private const IMPLODE_SYMBOL = "|";
  private var m_currstr = "";
  private var m_counter:integer = 0; //кол-во сообщений в логе
  private var m_fullStr:string = ""; //содержит "текущую запись" целиком - вместе с разделителями (если они есть)

  macro Init()
    //очистить временную структуру
    cmd.cmdText = "delete from ddeporeplog_tmp";
    cmd.Execute();
    m_DataSet = null;
    m_counter = 0;
    m_fullStr = "";
  end;

  macro GetImplodeSymbol()
    return IMPLODE_SYMBOL;
  end;

  /* Возвращает кол-во записей добавленных в лог */
  macro GetCount()
    return m_counter;
  end;

  /* Возвращает "текущую запись" целиком - вместе с разделителями (если они есть) */
  macro GetFullStr()
    return m_fullStr;
  end;

  macro AddStr( /*...*/ )

    VAR i = 1, CellVal;
    var s:string = "";

    while( GetParm( i, CellVal) )
      if( s == "" )
        s = string(CellVal);
      else
        s = string(string(s) + string(IMPLODE_SYMBOL) + string(CellVal));
      end;
      i = i + 1;
    end;
    if( s != "")
      cmd.cmdText = "insert into ddeporeplog_tmp (t_AutoKey, t_FormatString) values (0, '"+string(s)+"')";
      cmd.Execute();
      m_counter = m_counter + 1;
    end;
  end;

  macro GetNext(SortType) //тип сортировки записей при выборке (0 - "t_AutoKey desc", 1 - "t_FormatString desc", 2 - "t_FormatString asc")

    m_query = " select * from ddeporeplog_tmp ";
    if((ValType(SortType) != V_UNDEF) and (SortType == 1))
      m_query = m_query + "order by t_FormatString desc";
    elif((ValType(SortType) != V_UNDEF) and (SortType == 2))
      m_query = m_query + "order by t_FormatString asc";
    else
      m_query = m_query + "order by t_AutoKey desc";
    end;

    if( not m_DataSet )
      m_DataSet = TRsbDataSet(m_query);
    end;

    if( m_DataSet.moveNext() )
      m_currstr = m_DataSet.FormatString;
      m_fullStr = m_currstr;
      return 1;
    else
      m_currstr = "";
      m_fullStr = m_currstr;
      return 0;
    end;

  end;

  macro GetNextFld()
    var Val = "";
    var pos = Index ( m_currstr, IMPLODE_SYMBOL );

    if( pos )
      Val = substr(m_currstr, 1, pos-1 );
      m_currstr = substr(m_currstr, pos+1);
    else
      val = m_currstr;
    end;

    return Val;
  end;

end;

//общая функция вывода строки в отчет WinRep
//главное предназначение - вывод чисел в числовом формате
macro DP_AddPrintCell(RepObj, CellVal, width:integer, point:integer, format_str:string, isApp:bool, FlagStr:integer)

  var strapp = "";

  if(ValType(RepObj) == V_UNDEF)
    return 1;
  end;

  if(ValType(CellVal) == V_UNDEF)
    CellVal = "";
  end;

  if(isApp)
    strapp = "a:";
  end;

  if((ValType(CellVal) == V_DOUBLE) OR (ValType(CellVal) == V_MONEY))
    RepObj.AddPrintCell( CellVal, width, point, strapp + format_str, FlagStr);
  else
    RepObj.AddPrintCell( CellVal, width, 0, strapp + format_str, FlagStr);
  end;

  return 0;
end;

macro DP_GetSelfId(DepID)
  var q, rsd;

  q = "SELECT dp.t_PartyID "
    + "  FROM ddp_dep_dbt dp "
    + " WHERE dp.t_Code = " + DepID;

  rsd = TRsbDataSet(q);
  if( rsd.MoveNext() )
    return rsd.PartyID;
  end;

  return {OurBank};
end;

/**НАЧАЛО*************************ФУНКЦИИ ДЛЯ ДОБАВЛЕНИЯ ПРОТОКЛА В ОТЧЕТ**********************/
private var _DP_Protocol = DPRepLog;   //Используется функциями DP_BeginProtocol(), DP_EndProtocol()

/*Описание:
 Макро-функция подмена для MsgBox().
 Используется в макро-функциях DP_BeginProtocol() и DP_EndProtocol()
*/
macro DP_MsgBoxToProtocol(/*...*/)
//В виду особенности работы метода AddStr() класса DPRepLog - этот метод принимает переменное число параметров и
//формирует из них строку с разделителями, приходится дублировать код по формированию строки с разделителями,
//из-за того, что нет возможности передать фактические параметры данной фун-и методу AddStr(),
  var i = 0, CellVal;
  var str:string = "";

  while( GetParm( i, CellVal) )
    if( str == "" )
      str = string(CellVal);
    else
      str = string(str) + string(_DP_Protocol.GetImplodeSymbol()) + string(CellVal);
    end;
    i = i + 1;
  end;

  if(str != "")
    _DP_Protocol.AddStr(str);
  end;
end;

/*Описание:
 Включает режим протоколирования.
 Подменяет функцию MsgBox(), и очищает таблицу для накопления сообщений.
 Используется в паре с DP_EndProtocol().
Замечания:
 Необходимо учитывать, что класс реализующий отчеты с шаблонами-DL_CReportTemplate или его наследники
 т.ж. подменяют MsgBox.
 !!!
 Если используются шаблоны, то BeginProtocol необходимо вызывать в начале метода Create(),
 которая должна перегружаться в классе производном от DL_CReportTemplate.
 Метод Create() косвено вызывается в  методе Run(), класса DL_CReportTemplate, в котором (методе Run()) и производится
 подмена MsgBox() для класса DL_CReportTemplate.
 Вызов функции BeginProtocol() в методе Create() обеспечит перекрытие замены MsgBox() в классе DL_CReportTemplate.
 Так же подмена MsgBox() в классе DL_CReportTemplate, выполняется в методе CReport_Clear() этого класса,
 что необходимо учитывать.
 В ОБЩЕМ: РАСПОЛОЖЕНИЯ ФУНКЦИИ DP_BeginProtocol() ЗАВИСИТ ОТ ОКРУЖЕНИЯ,
 И ДОЛЖНО БЫТЬ ВЫБРАНО ТАК ЧТОБЫ НЕ ДОПУСТИТЬ ДАЛЬНЕЙШИХ ПОДМЕН ФУНКЦИИ MsgBox().
 В КЛАССЕ DL_CReportTemplate ПОДМЕНА MsgBox() ВЫПОЛНЯЕТСЯ В МЕТОДАХ: !!Run(), CReport_Clear()!!
*/
macro DP_BeginProtocol()
  ReplaceMacro("MsgBox", "DP_MsgBoxToProtocol");
  _DP_Protocol.Init();
end;

/*Описание:
 Выключает режим протоколирования.
 Выполняет сброс подмены функцию MsgBox(), и очищает таблицу для накопления сообщений
 Используется в паре с DP_BeginProtocol().
Замечания:
  Если используются шаблоны, то DP_EndProtocol необходимо вызывать в конце метода Create(),
  Подробнее смотри замечания к фун-и  DP_BeginProtocol().
*/
macro DP_EndProtocol()
  ReplaceMacro("MsgBox");
  _DP_Protocol.Init();
end;

/*Описание:
  Добавляет сообщения протокола в отдельную вкладку отчета.
 Параметры:
  Rep - объект отчета, типа CMakeReport, куда будет добавляться протокол
  ProtocolSheetName - имя вкладки на которой будет выведен протокол;
Замечания:
*/
macro DP_AddProtocol(Rep:variant, ProtocolSheetName:string)
  var ProtocolHeader =
                        "┌─────┬────────────────────────────────────────────────────────────────────┐\n"+
                        "│  №  │                                Сообщение                           │\n"+
                        "├─────┼────────────────────────────────────────────────────────────────────┤";
  var Num = 1;
  var msgStr = "";
  const RepFontStyleTitel =  "ex_FS(b)";

  if( ValType(Rep) != V_UNDEF)
    if(IsEqClass ("CMakeReport", Rep) and (_DP_Protocol.GetCount() > 0))  //CMakeReport - без шаблона и есть сообщения
      Rep.AddNewSheetBreak( ProtocolSheetName );
//      Rep.SetFlagPrintFullHeader( false ); //печатать последнюю строку шапки на новой странице
      Rep.SetFlagShowGrid( false );        //установка флага показа разделительной сетки в Excel
      Rep.SetFlagShowZeroValue( false );   //установка флага показа нулевых значений в Excel
      Rep.SetHeaderStr(ProtocolHeader, 1); //инициализация закладки шапкой таблицы (еще есть SetHeader-больше возможностей)

      while(_DP_Protocol.GetNext())
        msgStr = StrSubst(_DP_Protocol.GetFullStr(), _DP_Protocol.GetImplodeSymbol(), " "); //заменяем разделитель на пробелы
        DP_AddPrintCell( Rep, Num, 0, 0, "r:" + RepFontStyleTitel);
        DP_AddPrintCell( Rep, msgStr, 0, 0, "l:w" + RepFontStyleTitel);
        Rep.AddStr();
        Num = Num + 1;
      end;

      if(Rep.GetCountSheet() > 0)
        Rep.SetCurSheet(0);     //После печати протокола делаем активным первый лист
      end;

    end;
  end;
end;

/* Обертки */
macro DP_ProtocolGetNext(SortType)
  return _DP_Protocol.GetNext(SortType);
end;

macro DP_ProtocolGetNextFld()
  return _DP_Protocol.GetNextFld();
end;

macro DP_ProtocolAddStr( /*...*/ )
//Приходится дублировать код по формированию строки с разделителями,
//из-за того, что нет возможности передать фактические параметры данной фун-и методу AddStr(),
  var i = 0, CellVal;
  var str:string = "";

  while( GetParm( i, CellVal) )
    if( str == "" )
      str = string(CellVal);
    else
      str = string(str) + string(_DP_Protocol.GetImplodeSymbol()) + string(CellVal);
    end;
    i = i + 1;
  end;

  if(str != "")
    _DP_Protocol.AddStr(str);
  end;
end;

macro DP_ProtocolIsEmpty()
  return _DP_Protocol.GetCount() == 0;
end;

macro DP_ProtocolGetCount()
  return _DP_Protocol.GetCount();
end;

macro DP_ProtocolClear()
  _DP_Protocol.Init();
end;

/**КОНЕЦ**************************ФУНКЦИИ ДЛЯ ДОБАВЛЕНИЯ ПРОТОКЛА В ОТЧЕТ**********************/

MACRO DP_GetFIWarntDate(FIID, Number, PaymentType)
  var query, Select, DataSet;
  var WarntDate = ZeroDate;
  file fin(fininstr);

  if((FIID != ALLFININSTR)
 and ((((PaymentType == DP_CRPPAYMENTTYPE_COUPON) or (PaymentType == DP_CRPPAYMENTTYPE_NOMINAL)) and (Number != ""))
   or (PaymentType == DP_CRPPAYMENTTYPE_ISSUE)))

    query = " SELECT ";

    if(PaymentType == DP_CRPPAYMENTTYPE_NOMINAL)
      query = query +
                " RSI_RSB_FIInstr.FI_GetPartialDrawingDate(?, ?) AS t_DrawingDate ";
    end;

    if(PaymentType == DP_CRPPAYMENTTYPE_COUPON)
      query = query +
                " RSI_RSB_FIInstr.FI_GetCouponDrawingDate(?, ?) AS t_DrawingDate ";
    end;

    if(PaymentType == DP_CRPPAYMENTTYPE_ISSUE)
      query = query +
                " RSI_RSB_FIInstr.FI_GetNominalDrawingDate(?, ?) AS t_DrawingDate ";
    end;

    query = query +
            " FROM "
              + " dual ";

    Select = DL_RsdCommand(query);

    Select.AddParam(FIID);
    if((PaymentType == DP_CRPPAYMENTTYPE_COUPON) or (PaymentType == DP_CRPPAYMENTTYPE_NOMINAL))
      Select.AddParam(Number);
    end;
    if(PaymentType == DP_CRPPAYMENTTYPE_ISSUE)
      Select.AddParam(FIID);
    end;

    DataSet = Select.Execute();

    if(DataSet.moveNext())
      WarntDate = date(DataSet.DrawingDate);
    end;
  end;

  return WarntDate;
END;

MACRO DP_GetSingleCorpFIID(DocumentID)
  var query, Select, DataSet;
  var FIID = -1;

  query =   " select distinct pmfi.t_FIID "
          + "   from ddpregstr_dbt reg, ddppmfi_dbt pmfi "
          + "  where reg.t_DocumentID = ? "
          + "    and pmfi.t_RegisterID = reg.t_RegisterID";

  Select = DL_RsdCommand(query);
  Select.AddParam(DocumentID);

  DataSet = Select.Execute();
  if(DataSet.moveNext())
    FIID = DataSet.FIID;
    if(DataSet.moveNext())
      FIID = -1;
    end;
  end;

  return FIID;
END;

MACRO DP_GetPmRac(DocumentID, HolderAccID, PmRac)
  var cmd, DataSet;
  var err = 0;

  cmd = DL_RSDCommand(  "select * from ddppmrac_dbt"
                      + " where t_DocumentID = ? "
                      + "   and t_HolderAccID = ? "
                      + "   and t_Account <> CHR(1)"
                     );
  cmd.AddParam(DocumentID);
  cmd.AddParam(HolderAccID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo( PmRac.rec );
  else
    err = 1;
  end;

  return err;
END;

macro DP_GetCountryByCodeLat3( CodeLat3 )
  var Query = "select * from dcountry_dbt where t_CodeLat3 = ? /*1*/";

  var Select = RSDCommand( Query );

  Select.AddParam("CodeLat3", RSDBP_IN, CodeLat3 ); /*1*/
  Select.Execute();

  var DataSet = TRsbDataSet( Select );
  var Country = TBFile( "country", "R", 2 );

  Country.Clear();
  if( DataSet.moveNext() )
    Country.rec.CodeLat3 = CodeLat3;
    Country.rec.CountryID = DataSet.CountryID;
    if( not Country.GetEQ )
      Country.Clear();
    end;
  end;

  return Country.rec;
end;

macro DP_EndUseTechAuto()

  SQL_Execute("TRUNCATE TABLE ddptechauto_tmp", "", false);

end;

macro DP_StartUseTechAuto(TechAutoDoc:integer)

  DP_EndUseTechAuto();

  var sql =   " INSERT INTO ddptechauto_tmp "
            + " ( t_IsMakeAuto, "
            + "   t_TechAutoDoc, "
            + "   t_Initiator, "
            + "   t_SPgroundID "
            + " ) VALUES ("
            + "?, "
            + "?, "
            + "?, "
            + "?  "
            + ") ";

  var cmd = RSDCommand(sql);

  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, SET_CHAR );
  cmd.addParam( "", RSDBP_IN, TechAutoDoc );
  cmd.addParam( "", RSDBP_IN, -1 );
  cmd.addParam( "", RSDBP_IN, 0 );
  cmd.execute();
end;
