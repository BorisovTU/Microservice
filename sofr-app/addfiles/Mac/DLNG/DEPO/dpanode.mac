/*
$Name:         dpanode.mac
$Module:       Депозитарий
$Description:  Печатная форма "Обороты на аналитическом узле"
*/

import dprepsec, CurrInter, "adress.mac", "pt_lib.mac", cb_sql, deposerv;
import RsbDataSet;

var HeadTable = "┌─────────────────────────┬──────────────────┬────────────┬───────────────┬────────────────┬────────────┬────────────┬───────────────┐\n"+
                "│№ л/с                    │ц/б               │Эмитент     │Выпуск         │Вх.ост.         │Кт.         │Дт.         │Исх. ост.      │\n"+
                "├─────────────────────────┼──────────────────┼────────────┼───────────────┼────────────────┼────────────┼────────────┼───────────────┤";
                
var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

/* Глава синтетического учета */
const Depo_chapt =  5;    
/* виды значений объекта - Сторона баланса (1000) */
const SIDEBALANCE_UNDEFINE = 0;  /* не установлен */
const SIDEBALANCE_ACTIVE   = 1;  /* активный      */
const SIDEBALANCE_PASSIVE  = 2;  /* пассивный     */
/*globals*/
private var cOwner;
var  ShowAccDetails;
var  XcludeInvZeros;
var  Gfirst_date ;
var  Glast_date; 
var  AcntHeadPrint= false;      /*шапка счета*/
var  DepopartBriefCode = "", 
     DepoacntBriefCode = "";
var  isPrint = false;

macro FindIssuerName(Issuer, last_date)
  var c_Issuer = CDPParty(Issuer, last_date);

  return c_Issuer.ShortName();
end;

/* получить страну субъекта */
private macro GetPartyICountry( cParty )
  file country("country","rsrfdb.def") key 2;

  if(cParty.NotResident() == "X" )
    country.CodeLat3 = cParty.NRCountry();
    if( getEQ(country) )
      return country.Name;
    else 
      MsgBox("Не найдена страна с кодом = " + country.CodeLat3 ); 
    end
  else
    return "Россия";
  end;

  return "";
end;


macro AcntHeader()
 if(AcntHeadPrint == false)
  /*Шапка*/
  Rep.AddEmptyStr();
  DP_AddPrintCell( Rep,  "Ведомость об оборотах ц/б на лицевых счетах " + DepopartBriefCode+" счета депо "+
                     DepoacntBriefCode+" за период с "+DateToStr(Gfirst_date, true)+
                     " по "+DateToStr(Glast_date, true), tablewidth, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR );
  Rep.AddStr();
  /*Депонент*/
    if(ShowAccDetails) /* показывать сведения о депоненте */
      DP_AddPrintCell( Rep,  "Владелец счета депо: " + cOwner.Name() + "    ИНН " + cOwner.CodeINN(), tablewidth, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR );
      Rep.AddStr();
      if(cOwner.LegalForm() == PTLEGF_PERSN)
        DP_AddPrintCell( Rep,  "документ:   " + cOwner.PaperKindName() + "  серия " + cOwner.PaperSeries() + "  № " + cOwner.PaperNumber(), tablewidth, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR );
        Rep.AddStr();
        DP_AddPrintCell( Rep,  "выдан:   " + DateToStr(date(cOwner.PaperIssuedDate()), true) + "  " + cOwner.PaperIssuerName() + "  Юрисдикция " + GetPartyICountry(cOwner), tablewidth, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR );
        Rep.AddStr();
      end;
    end;
    AcntHeadPrint = true;
  end;
end;

macro InvCardRestANdOverturn2Acc(KindAcc, AccountID, SumPrecision )

  var stat, mstat;
  var IKdebet, IKkredit, IKrest, IKLrest;
  var OwTurn, show;
   
  var Act_A = 1;
  var DataIC, query, cmd;
  var DataCH, cmd1;
  
  if(KindAcc == bal_acc_active) /* Актив */
    Act_A = -1;
  end;

  query =   " select ic.*, ficert.*, rsb_deals.GetFicertPlanDrawingDate(ficert.t_FiCertID) as DrawingDate "
          + "   from dinvcard_dbt ic, dv_ficert_dbt ficert "
          + "  where(   ( ic.t_LAccountID = ? or ic.t_HAccountID = ? )"
          + "        or ( ic.t_InvCardID in ( select t_InvCardID from dinvchng_dbt where t_LAccountID = ? or t_HAccountID = ?) )"
          + "       ) "
          + "    and ficert.t_FiCertID = ic.t_FiCertID ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(AccountID);
  cmd.AddParam(AccountID);
  cmd.AddParam(AccountID);
  cmd.AddParam(AccountID);

  DataIC = cmd.Execute();
  while(DataIC.MoveNext())

    InvCardRestAndOverturnByAcc(DataIC.InvCardID, AccountID, Gfirst_date, Glast_date, @IKrest, @IKdebet, @IKkredit, @IKLrest);
    if ( ( XcludeInvZeros == true ) and ( IKLrest == $0 ) )
      show = false;
    else
      show = true;
    end;

    if( ( IKdebet or IKkredit or IKrest ) and show )

/*К 00000000000001 вып. 01.01.2002 ОАО Роги и ноги   погаш. 01.02.2003                6            1            4               3*/
       DP_AddPrintCell( Rep,"ИК " + DataIC.Number + " вып." + string(date(DataIC.IssueDate):f) + " " +
                        FindIssuerName(DataIC.Issuer, Glast_date) + " погаш." + string(date(DataIC.DrawingDate):f),
                        Rep.GetColWidthTable(0)+Rep.GetColWidthTable(1)+Rep.GetColWidthTable(2)+
                        Rep.GetColWidthTable(3)+3, 0, "l:" + RepFontStyleTitel );
       DP_AddPrintCell( Rep, Act_A*Floor_Money(IKrest), Rep.GetColWidthTable(4), DP_GetPrecision(Floor_Money(IKrest), SumPrecision), "l:" + RepFontStyleTitel );
       DP_AddPrintCell( Rep, Floor_Money(IKkredit), Rep.GetColWidthTable(5), DP_GetPrecision(Floor_Money(IKkredit), SumPrecision), "l:" + RepFontStyleTitel );
       DP_AddPrintCell( Rep, Floor_Money(IKdebet), Rep.GetColWidthTable(6), DP_GetPrecision(Floor_Money(IKdebet), SumPrecision), "l:" + RepFontStyleTitel );
       DP_AddPrintCell( Rep, (Act_A*Floor_Money(IKLrest)), Rep.GetColWidthTable(7), DP_GetPrecision(Floor_Money(IKLrest), SumPrecision), "l:" + RepFontStyleTitel );
       Rep.AddStr();
    end;

  end;
end;

/*Строка отчета по лицевому счету депо*/
macro DepoAccReport(Account /*запись*/, first_date, last_date, ShowInvent)

  record fin("fininstr");
  record avr("avoiriss");
  file  avrkind ("avrkinds");
  
  var RestIn = 0, Debet = 0, Kredit = 0, RestOut = 0;
  var Issuer, IssuerName = "", AvoirKindName = "";
  var LSIN = "";
  var REST;

  //получить ФинИнстр
  var query =   " select fin.t_AvoirKind, fin.t_Name, fin.t_FIID,  RSI_RSB_FIInstr.FI_GetIssuerOnDate( fin.t_FIID, " + GetSQLDate(last_date) + " )   AS Issuer , fin.t_SumPrecision, avr.t_LSIN, avk.t_IsIndividual, avk.t_Name as avoirkindName"
              + " from dfininstr_dbt fin, davoiriss_dbt avr, davrkinds_dbt avk "
              + " where fin.t_FIID = " + Account.Code_Currency
              + "   and avr.t_FIID = " + Account.Code_Currency
              + "   and fin.t_Fi_Kind = 2"
              + "   and avk.t_AvoirKind = fin.t_AvoirKind and avk.t_Fi_Kind = fin.t_Fi_Kind";
  var DataSet = TRsbDataSet( query );

  if( DataSet.MoveNext() )
    AvoirKindName = DataSet.avoirkindName;
    if( CodeFor(DataSet.LSIN) != 1 )//пустая строка в Оракле
      LSIN = DataSet.LSIN;
    end;

    if( not DataSet.IsIndividual )
      IssuerName = FindIssuerName(DataSet.Issuer, last_date);
    end;

    //сразу посчитаем остатки на начало, конец дня и дебет и кредит
    query     =  " select sum(rsb_account.restac( a.t_Account, a.t_Code_Currency, " + GetSQLDate(first_date - 1) + ", a.t_Chapter, null )) as RESTACC, "
               + "        sum(rsb_account.kreditac( a.t_Account, a.t_Chapter, a.t_Code_Currency, " + GetSQLDate(first_date) + ", " + GetSQLDate(last_date) + "  )) as KREDITACC, "
               + "        sum(rsb_account.debetac( a.t_Account, a.t_Chapter, a.t_Code_Currency, " + GetSQLDate(first_date) + ", " + GetSQLDate(last_date) + "  )) as DEBETACC, "
               + "        sum(rsb_account.restac( a.t_Account, a.t_Code_Currency, " + GetSQLDate(last_date) + ", a.t_Chapter, null )) as RESTACCLAST "
                + " from daccount_dbt a"
                + " where a.t_Account       = '"+ Account.Account+"'"
                + "   and a.t_Code_Currency = " + Account.Code_Currency
                + "   and a.t_Chapter       = " + Depo_chapt;

    REST = TRsbDataSet(query);
    if(REST.MoveNext())
      if( ValType(REST.RESTACC ) != V_UNDEF )
        RestIn = REST.RESTACC;
      end;
      if( ValType(REST.KREDITACC ) != V_UNDEF )
        Kredit = REST.KREDITACC;
      end;
      if( ValType(REST.DEBETACC ) != V_UNDEF )
        Debet = REST.DEBETACC;
      end;
      if( ValType(REST.RESTACCLAST ) != V_UNDEF )
        RestOut = REST.RESTACCLAST;
      end;
    end;
    Close(REST);

   
  /*234567890123456789012345 Акция обыкновенная Газпром    1-02-00028-А  100 000 000 000  200 000 000  100 000 000  100 100 000 000]*/
    DP_AddPrintCell( Rep, Account.Account, 0, 0, "l:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, AvoirKindName, 0, 0, "l:w:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, IssuerName, 0, 0, "l:w:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, LSIN, 0, 0, "l:w:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, double(RestIn),0, DP_GetPrecision(RestIn, DataSet.SumPrecision), "r:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, double(Kredit),0, DP_GetPrecision(Kredit, DataSet.SumPrecision), "r:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, double(Debet),0, DP_GetPrecision(Debet, DataSet.SumPrecision), "r:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, double(RestOut),0, DP_GetPrecision(RestOut, DataSet.SumPrecision),"r:" + RepFontStyleTitel );
    Rep.AddStr();
    if(ShowInvent and index(Account.Type_Account, "I") )  /*Закрытое хранение*/
       InvCardRestANdOverturn2Acc(Account.Kind_Account, Account.AccountID, DataSet.SumPrecision);
    end;
  end;
end;

/*Терминальность*/
macro TerminalPart(PartAutoKey)
  var SubPart = TBFile("depoacnt","R",5);
  SubPart.rec.Superior = PartAutoKey;
  return not SubPart.GetEQ;
end;

macro CheckSuperior(depopart_AutoKey, PartRec)
  var SuperiorPart = TBFile("depoacnt");
   
  if(depopart_AutoKey == PartRec.AutoKey)
    return true;
  end;

  if(PartRec.Superior == 0)
    return false;
  end;

  SuperiorPart.rec.AutoKey = PartRec.Superior;
  if(SuperiorPart.GetEQ)
    return CheckSuperior(depopart_AutoKey, SuperiorPart.rec)
  end;

  return false;
end;

/*Проверка*/
macro CheckPart(depopart_AutoKey, DepoPart_rec)
  return ((depopart_AutoKey == 0) or CheckSuperior(depopart_AutoKey, DepoPart_rec));
end;

/*Отчет по разделу (терминальному)  счета депо*/
macro DepoPartReport(depoacnt_rec, 
                     first_date,  last_date,
                     depoleafacc, fininstr_FIID,
                     GroupByPartition, ShowInvent)

  file depopart("depoacnt");

  var stat, HeadPrint = false;
  var DataAccount, query;

  query =   " select t_Type_Account, t_Kind_Account, t_Account, t_Code_Currency, t_AccountID "
          + " from daccount_dbt "
          + " where t_DepoAcc = " + depoacnt_rec.AutoKey;
  if( depoleafacc != "" )
    query = query + " and t_Account = '"+depoleafacc+"' ";
  end;
  if(fininstr_FIID != -1)
    query = query + " and t_Code_Currency = " + fininstr_FIID;
  end;

  DataAccount = TRsbDataSet(query);
  /*По разделам, передаются только терминальные и проверенные */
  while( DataAccount.MoveNext())
    if ( isPrint == false )
      DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Обороты на аналитическом узле", 0, 0, false, first_date, last_date, tablewidth );
      isPrint = true;
    end;

    if(not HeadPrint)
      AcntHeader();
      DP_AddPrintCell( Rep,  " Раздел " + depoacnt_rec.BriefCode, tablewidth, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR );
      Rep.AddStr();
      HeadPrint = true;
    end;
    DepoAccReport(DataAccount, first_date, last_date, ShowInvent);
  end;
end;

macro GetListSQLString(iDepart, _depoacnt_AutoKey, _depopart_AutoKey)
  var query;
  query =   " select * from ddepoacnt_dbt "
          + " where t_Root = " + _depoacnt_AutoKey
          + " and   t_AutoKey not in ( select t_Superior from ddepoacnt_dbt) "
          + " and   t_Superior != 0 ";

  if( iDepart > 0 )
    query = query + " and t_Department = " + iDepart;
  end;

  if( _depopart_AutoKey != 0 )
    //ищем терминальные разделы данного подуровня
     query = query + " start with t_AutoKey = " + _depopart_AutoKey
                   + " connect by prior t_AutoKey = t_Superior"
                   + " order by t_AutoKey";
  end;

  return query;
end;

/*Отчет по отдельному счету депо*/
macro DepoAcntReport(iDepart, depoacnt_AutoKey, depopart_AutoKey,
                     first_date, last_date,
                     depoleafacc, fininstr_FIID,
                     GroupByPartition, ShowInvent)
   
  file DepoPart("depoacnt");
  var stat, HeadPrint = false;
  var DataSet, query;
  var ListLeafAccount, queryListLeaf;
  queryListLeaf = GetListSQLString(iDepart, depoacnt_AutoKey, depopart_AutoKey);  //по сути, это записи depoacnt
  ListLeafAccount = TRsbDataSet(queryListLeaf);

  DepoPart.AutoKey = depoacnt_AutoKey;
  if(GetEQ(DepoPart))
    DepoacntBriefCode = DepoPart.BriefCode;
  end;

  if(depopart_AutoKey > 0) 
    DepoPart.AutoKey = depopart_AutoKey;
    if(GetEQ(DepoPart))
      DepopartBriefCode = " раздела " + DepoPart.BriefCode;
    end;
  end;

  AcntHeadPrint = false; /*новый счет депо*/

  /*СЧЕТА*/
  if(GroupByPartition) /*группируя по терминальным разделам*/
    while(ListLeafAccount.MoveNext())
      DepoPartReport(ListLeafAccount.rec,
                     first_date, last_date,
                     depoleafacc, fininstr_FIID,
                     GroupByPartition, ShowInvent);
    end;
  else
    query =   " select t_Type_Account, t_Kind_Account, t_Account, t_Code_Currency, t_DepoAcc, t_AccountID "
            + " from daccount_dbt "
            + " where t_DepoRoot = " + depoacnt_AutoKey
            + " and t_DepoAcc in ( select t_AutoKey from ddepoacnt_dbt )";
    if( fininstr_FIID != -1 )
      query = query + " and t_Code_Currency = " + fininstr_FIID;
    end;
    if( depoleafacc != "")
      query = query + " and t_Account = '"+ depoleafacc + "' ";
    end;

    DataSet = TRsbDataSet(query);
    while(DataSet.MoveNext())
      DepoPart.AutoKey = DataSet.DepoAcc;
      if(GetEQ(DepoPart)
     and (TerminalPart(DepoPart.AutoKey))
     and (CheckPart(depopart_AutoKey, DepoPart)))
        if ( isPrint == false )
          DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Обороты на аналитическом узле", 0, 0, false, first_date, last_date, tablewidth );
          isPrint = true;
        end;
     
        if(not HeadPrint)
          AcntHeader();
          HeadPrint = true;
        end;
        DepoAccReport(DataSet, first_date, last_date, ShowInvent);
      end;
    end;
  end;
end;

macro PumpOwner( owner_PartyID, last_date )
  cOwner = CDPParty(owner_PartyID, last_date);
end;

macro CorrectToSelfID( partID )
  return partID;
end;

macro CorrectToOurBank( partID )
  return partID;
end;

macro FindOwnerInList( listOwner:TArray, OwnerID ) 
  var cur = 0;

  while( cur < listOwner.size )
    if ( listOwner[cur] == OwnerID )
      return 1;
    end;
    cur = cur + 1;
  end;
end;

macro NodeOverturn (iDepart:integer,
                    first_date:date,
                    last_date:date,
                    acc_kind:integer,
                    owner_PartyID:integer,
                    depoacnt_AutoKey:integer,
                    depopart_AutoKey:integer,
                    depoleafacc:string,
                    fininstr_FIID:integer,
                    _ShowAccDetails:bool,
                    GroupByPartition:bool,
                    ShowInvent:bool,
                    _XcludeInvZeros:bool,
                    ToExcel:bool)

  var Depoacnt, OWNER, query;
  var stat, curr;
  var count = 0;

  Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
  DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

  ShowAccDetails = _ShowAccDetails; /*globals*/
  XcludeInvZeros = _XcludeInvZeros; /*globals*/
  Gfirst_date  = first_date;
  Glast_date   = last_date;

  query =   " select distinct(t_Owner) from ddepoacnt_dbt "
          + " where t_Superior = 0 "
          + " and   t_Kind     =   " + acc_kind;

  if( iDepart > 0 )
    query = query + " and   t_Department = " + iDepart;
  end;

  if ( owner_PartyID != -1 ) /* депонент задан  */
    query = query + " and t_Owner = " + owner_PartyID;
  end;

  if( depoacnt_AutoKey != 0 )
    query = query + " and t_AutoKey = " + depoacnt_AutoKey;
  end;

  if( depoleafacc != "" )
    query = query + " and t_AutoKey in (select t_DepoRoot from daccount_dbt where t_Account = '"+depoleafacc+"' )";
  else
    query = query + " and  ( t_AutoKey in (select t_DepoRoot from daccount_dbt) or t_Root in (select t_DepoRoot from daccount_dbt) )";
  end;

   if( depoacnt_AutoKey > 0 )
     query = query + " and t_Root = " + depoacnt_AutoKey;
   end;


  OWNER = TRsbDataSet( query, RSDVAL_CLIENT, RSDVAL_STATIC);
  OWNER.MoveLast();
  count = OWNER.GetRecCount();
  stat = OWNER.MoveFirst();
  if ( count > 0 )
    InitProgress ( count, "Идет подготовка отчета...", "Обороты на аналитическом узле");
  else
   if(not DP_ProtocolIsEmpty())
      DP_AddPrintCell(Rep, "Не найдено данных, удовлетворяющих параметрам отчета", 55, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR);
      Rep.AddStr();

      DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
      DP_EndProtocol();                 //закончить протоколирование

      if( ToExcel )
        Rep.PrintWinRep();
        Rep.ShowWinRep();
      else
        Rep.PrintRep();
      end;
    else
      DP_EndProtocol();                 //закончить протоколирование
    end;

    MsgBox("Не найдено данных, удовлетворяющих параметрам отчета");
    return 0;
  end;

  depopart_AutoKey = 0;
  curr = 0;
  while( stat )
    /* вызов отчета по всем счетам депонента */
    owner_PartyID = CorrectToOurBank( OWNER.Owner );
    if ( PumpOwner( CorrectToSelfID(OWNER.Owner), last_date ) )
      if(not DP_ProtocolIsEmpty())
        DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
        DP_EndProtocol();                 //закончить протоколирование

        if( ToExcel )
          Rep.PrintWinRep();
          Rep.ShowWinRep();
        else
          Rep.PrintRep();
        end;
      else
        DP_EndProtocol();                 //закончить протоколирование
      end;

      return 1;
    end;

    UseProgress (curr);
    query =   " select t_AutoKey from ddepoacnt_dbt "
            + " where t_Superior = 0 "
            + " and   t_Kind     =   " + acc_kind
            + " and t_Owner = " + OWNER.Owner;

    if( iDepart > 0 )
      query = query + " and   t_Department = " + iDepart;
    end;

    if( depoacnt_AutoKey != 0 )
      query = query + " and t_AutoKey = " + depoacnt_AutoKey;
    end;

    if( depoleafacc != "" )
      query = query + " and t_AutoKey in (select t_DepoRoot from daccount_dbt where t_Account = '"+depoleafacc+"' )";
    else
      query = query + " and (t_AutoKey in ( select t_DepoRoot from daccount_dbt ) or t_Root in (select t_DepoRoot from daccount_dbt))";
    end;

    if( depoacnt_AutoKey > 0 )
      query = query + " and t_Root = " + depoacnt_AutoKey;
    end;


    Depoacnt = TRsbDataSet( query );

    while( Depoacnt.MoveNext())
         
      DepoAcntReport(iDepart, Depoacnt.AutoKey, depopart_AutoKey,
                     first_date, last_date,
                     depoleafacc, fininstr_FIID,
                     GroupByPartition, ShowInvent);
    end;
    curr = curr + 1;
    stat = OWNER.MoveNext();
  end;

  RemProgress();

  if ( isPrint == true )   
    DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

    DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
    DP_EndProtocol();                 //закончить протоколирование

    if( ToExcel )
      Rep.SetZoomType( ZOOM_TYPE_A4L );
      Rep.PrintWinRep();
      Rep.ShowWinRep();
    else
      Rep.PrintRep();
    end;
  else
    if(not DP_ProtocolIsEmpty())
      Rep.ClearAllStr();
      DP_AddPrintCell(Rep, "Не найдено данных, удовлетворяющих параметрам отчета", 55, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR);
      Rep.AddStr();

      DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
      DP_EndProtocol();                 //закончить протоколирование

      if( ToExcel )
        Rep.PrintWinRep();
        Rep.ShowWinRep();
      else
        Rep.PrintRep();
      end;
    else
      DP_EndProtocol();                 //закончить протоколирование
    end;

    MsgBox("Не найдено данных, удовлетворяющих параметрам отчета");
  end;

  return 0;
end;
