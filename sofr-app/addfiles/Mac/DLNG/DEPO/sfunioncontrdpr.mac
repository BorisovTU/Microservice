/*
$Name:        sfunioncontrdpr.mac
$Module:      Депозитарий
$Description: 
*/
import deposerv, DPInter, "dlwreps.mac", "adress.mac";

const NumOpenDay = 3,
      NumAdviceDay = 10,
      NumInformationDay = 3;

const физическое_лицо     = 2,
      юридическое_лицо    = 1;

const DLDOC_SFUNIONCONTR_DEPOS  = 4506; /* вид документа договора обслуживания */

var sfunioncontr :TRecHandler = TRecHandler("sfunioncontr.dbt");

/* проверка данных при печати, пустые значения заменяются прочерками */
private macro CheckData( Data )
   var str;
   if( ValType( Data ) == V_DATE)
     if(Data == Date(0,0,0))
       return str = ("\"___\"" + " _________ " + "20__ ");
     else
       return Data;
     end;
   elif(ValType( Data ) == V_STRING)
     if(Data == "")
       return str = "________________________";
     else
       return Data;
     end;
   else
     if( not Data )
       return str = "________";
     else
       return Data;
     end;
   end;
end;

private macro FinInName(FIID)
   file fininstr(fininstr);
   fininstr.FIID = FIID;
   if(not GetEQ(fininstr))
     runerror("Не найден финансовый инструмент");
   end;
   return fininstr.Name;
end;


private macro NameParty(ID)
  file party(party);
  var Name = "";
  var persn =TBFile("persn");

  party.PartyID = ID;
  if( GetEQ(party) )
    if(party.LegalForm==юридическое_лицо)
       Name = party.Name;
    elif(party.LegalForm==физическое_лицо)
       persn.rec.PersonID = ID;
       if(persn.GetEQ)
         Name = trim(persn.rec.Name1)+" "+trim(persn.rec.Name2)+" "+trim(persn.rec.Name3);
       end;
    end;
  end;
  return Name;
end;

private macro AddressParty(ID)
  file party(party);
  record RecordAdress ( adress );
  var address = "";

  party.PartyID = ID;
  if(not GetEQ(party))
    runerror("Не найден субъект");
  else
    if(НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdress))
      address = RecordAdress.Adress;
    end;
  end;

  return address;
end;


private macro ДСчет( Settacc )
  var BankID = Settacc.BankID;//наименование банка
  var Счет = "";
  if( Settacc.FIID != NATCUR )
    Счет = Счет + ПолучитьКодФинИн(Settacc.FIID) + " (" + FinInName(Settacc.FIID) + ") ";
  end;
  Счет = Счет + " р/с " + Settacc.Account + ", в банке " + NameParty(BankID)  + ", " + 
         AddressParty(BankID) + ", БИК " + ПолучитьКодСубъекта(BankID, PTCK_BIC) + " к/с " + 
         Settacc.CorrAcc + " в " + 
         NameParty(Settacc.BankCorrID) + ", БИК " + ПолучитьКодСубъекта(Settacc.BankCorrID, PTCK_BIC);
  
  return Счет;
end;

                          
/* адреса и реквизиты банка */
private macro BankPropsTag( PartyID, side )
  var stat, str = "";
  var Счет = "";
  var РеквизитыТел;

  file settacc(settacc) key 3;
  file party(party);

  record RecordAdress1 ( adress );
  record RecordAdress2 ( adress );

  if ( ПолучитьСубъекта( PartyID, party ) == 0 )
    ClearRecord(RecordAdress1);
    ClearRecord(RecordAdress2);
    НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdress1);
    НайтиАдресСубъекта(party.PartyID,PTADDR_REAL,RecordAdress2);
    println( "~" + side + "НаимПолное" );
    println( CheckData(party.Name) );
    println( "~" + side + "РеквизитыНаимПолное" );
    println( CheckData(party.Name) );
    println( "~" + side + "РеквизитыИНН" );
    println( CheckData( ПолучитьКодСубъекта(party.PartyID, PTCK_INN) ) );
    println( "~" + side + "РеквизитыАдресЮр" );
    println( CheckData(RecordAdress1.Adress) );
    println( "~" + side + "РеквизитыАдресПочт" );
    println( CheckData(RecordAdress2.Adress) );
  
    РеквизитыТел = CheckData(RecordAdress1.PhoneNumber) + ", " + CheckData(RecordAdress1.PhoneNumber2) + ", " + 
                   CheckData(RecordAdress1.Fax) + ", " + CheckData(RecordAdress1.E_Mail);
    println( "~" + side + "РеквизитыТел" );
    println( РеквизитыТел );
  
  
    println( "~" + side + "РеквизитыБанк" );
  
    ClearRecord( settacc );
    settacc.PartyID = party.PartyID;
    settacc.FIKind  = FIKIND_CURRENCY; 
    settacc.FIID    = 0;
    stat = getGE(settacc);
    while ( stat and ( ( settacc.PartyID == party.PartyID ) and ( settacc.FIKind == FIKIND_CURRENCY ) ) )
      Счет = Счет + ДСчет(settacc);
      stat = Next(settacc);
      if(stat)
        Счет = Счет + "\n";
      end;
    end;
    println( CheckData(Счет) );
  else
    runerror("Не найден субъект с номером = " + partyID );
  end;
  
  
  return true;
end;

 /* формирование тегов для печати */ 
private macro FormTagForPrint(num)

  file dlgrtemp(dlgrtemp);
                                                                                                                                                   
    ClearRecord(dlgrtemp);
    dlgrtemp.DocLog = LLCLASS_KINDDOC_DPTEMPL;
    dlgrtemp.Num    = sfunioncontr.rec.ServKindSub;
    if( (not getEQ(dlgrtemp)) or (dlgrtemp.File_Name == "") or(dlgrtemp.IsClose == "X") )
      //msgbox( "Не задан шаблон для документа-основания договора обслуживания № ", sfunioncontr.rec.Number );
      msgbox("Подготовка данного вида договора не предусмотрена.");
      return false;
    end;

  println(dlgrtemp.File_Name);
  [#.rtf]("ДсД" + string(sfunioncontr.rec.Number) + "_" + string(num)); 

  println();

  /* адреса и реквизиты нашего банка */
  if( not BankPropsTag( sfunioncontr.rec.ContractorID, "Депозитарий" ) )
    return false;
  end;

  /* адреса и реквизиты депонента */
  if( not BankPropsTag( sfunioncontr.rec.PartyID, "Депонент" ) )
    return false;
  end;

  println( "~ДоговорНомер" );
  println( CheckData(sfunioncontr.rec.Number) );
  println( "~ДоговорДатаЗаключения" );
  println( CheckData(sfunioncontr.rec.DateConc) );

  println( "~ДепонентВлицеДолжность" );
  println( CheckData("") );
  println( "~ДепонентВлицеНаимПолное" );
  println( CheckData("") );
  println( "~ДепонентВлицеОснование" );
  println( CheckData("") );
  println( "~ДепозитарийВлицеДолжность" );
  println( CheckData("") );
  println( "~ДепозитарийВлицеНаимПолное" );
  println( CheckData("") );
  println( "~ДепозитарийВлицеОснование" );
  println( CheckData("") );

  println( "~ДоговорОткрытьСДнеПозднее" );
  println( CheckData(NumOpenDay) );
  println( "~ДоговорУведомитьИзмУсловийЗа" );
  println( CheckData(NumAdviceDay) );
  println( "~ДоговорДепозитариюОбИзмененияхНеПозднее" );
  println( CheckData(NumInformationDay) );
  println( "~ДоговорДатаВсилу" );
  println( CheckData(sfunioncontr.rec.DateConc) );
  println( "~ДоговорДатаДо" );
  println( CheckData(sfunioncontr.rec.DateClose) );

  println( "~ДепозитарийРеквизитыПодпись" );
  println( CheckData() );
  println( "~ДепонентРеквизитыПодпись" );
  println( CheckData("") );

  return true;
end;
                    

private macro CreateFileName(num)
   return GetTxtFileName( "tmpout" );
end;

macro PrintDocument(ncopy:integer):bool
  var TempFile;
  var i = 0;

  message("Формирование отчета по договору...");

  while( i < ncopy )
     if ( not FormTagForPrint(i+1) )
       return 1;
     end;
     TempFile = SetOutput( CreateFileName );
     DLWREPS_PrintReportFromTagFile( TempFile );
     SetOutput(NULL, false);
     i = i + 1;
  end;
  
  return 1;
end;