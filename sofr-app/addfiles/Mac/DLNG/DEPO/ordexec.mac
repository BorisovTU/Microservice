/*
$Name:         ordexec.mac
$Module:       Депозитарий
$Description:  Макрос отчета об исполнении поручения
*/

import deposerv, CurrInter, order_inv, order_inf, order_adm, dpglrep;

private const SP_ALL_OPERATION = 0;
private const SP_INV_OPERATION = 1;
private const SP_ADM_OPERATION = 2;
private const SP_INF_OPERATION = 3;

private const SC_SPDRAFT_STATUS_CLOSE = 3;

private const  SP_DEPOPER_ADMOPER = 825, //  Административная операция ДЕПО
               SP_DEPOPER_INFOPER = 830; //  Информационная операция ДЕПО

private const tablewidth = 98;
private var RepExec = CMakeReport();
private const RepFontStyleInv =  "ex_FS(b)";

private var Department : integer,
                 GrDoc : integer,
                 date_begin : date,
                 date_end : date,
                 xld_begin : string,
                 xld_end : string,
                 opclass : integer,
                 opkind : integer,
                 opstatus : integer,
                 opstatus_begin : date,
                 opstatus_end : date,
                 ojdnum_begin : string,
                 ojdnum_end : string,
                 party : integer,
                 depoacc : integer,
                 depopart : integer,
                 daccount : string,
                 FIID : integer,
                 InvCardID : integer,
                 LogoprID: integer,
                 ToExcel:bool,
                 is_app:bool;

private macro CreateRep()
  var CountReport = 0;
  var q, DataSet;
  var Select;
  var Params = TArray, i, n;

  /* проверка диапазонов заданных в панели */
  if (((xld_begin != "") AND (xld_end == "")) OR ((xld_begin == "") AND (xld_end != "")))
    runerror("Не верно задан диапазон номеров поручений");
  end;

  if( ((ojdnum_begin == "") AND (ojdnum_end != "")) OR ((ojdnum_begin != "") AND (ojdnum_end == "")))
      runerror("Не верно задан диапазон номеров ОЖД");
  end;

  q = " select vdp.t_Class OprClass, vdp.t_OprDate, vdp.t_OprXid, vdp.t_DocID " +
      " from dv_dpoperlog vdp, dspground_dbt spgr " +
      " where vdp.t_Status = " + SC_SPDRAFT_STATUS_CLOSE +
      "   AND spgr.t_SPGroundID = vdp.t_Ground ";

  if(GrDoc > 0)
    q = q + " AND spgr.t_Kind = ? ";
    Params(Params.Size) = GrDoc;
  end;

  if(date_begin != date(0,0,0))
    q = q + " AND spgr.t_RegistrDate >= ? ";
    Params(Params.Size) = date_begin;
  end;

  if(date_end != date(0,0,0))
    q = q + " AND spgr.t_RegistrDate <= ? ";
    Params(Params.Size) = date_end;
  end;

  if(xld_begin != "")
    q = q + " AND spgr.t_Xld >= ? ";
    Params(Params.Size) = xld_begin;
  end;

  if(xld_end != "")
    q = q + " AND spgr.t_Xld <= ? ";
    Params(Params.Size) = xld_end;
  end;

  if(opclass != SP_ALL_OPERATION)
    q = q + " AND vdp.t_Class = ? ";
    Params(Params.Size) = opclass;
  end;

  if(opkind > 0)
    q = q + " AND vdp.t_KindOper = ? ";
    Params(Params.Size) = opkind;
  end;

  if(opstatus != 2)
    q = q + " AND vdp.t_OperState = ? ";
    Params(Params.Size) = opstatus;
  end;

  if(opstatus_begin != date(0,0,0))
    q = q + " AND vdp.t_OprDate >= ? ";
    Params(Params.Size) = opstatus_begin;
  end;

  if(opstatus_end != date(0,0,0))
    q = q + " AND vdp.t_OprDate <= ? ";
    Params(Params.Size) = opstatus_end;
  end;

  if(ojdnum_begin != "")
    q = q + " AND vdp.t_OprXid >= ? ";
    Params(Params.Size) = ojdnum_begin;
  end;

  if(ojdnum_end != "")
    q = q + " AND vdp.t_OprXid <= ? ";
    Params(Params.Size) = ojdnum_end;
  end;

  q = q + " order by vdp.t_OprXid ";

  Select = RSDCommand( q );

  i = 0;
  n = Params.Size;
  while(i < n)
    Select.AddParam("", RSDBP_IN, Params(i) );
    i = i + 1;
  end;

  Select.Execute();

  DataSet = TRsbDataSet(Select);

  while(DataSet.MoveNext())
    if(DataSet.OprClass == SP_INV_OPERATION)
      CountReport = CountReport + Order_INV(GrDoc, date_begin, date_end, xld_begin, xld_end, opkind, opstatus,
                                            DataSet.OprDate, DataSet.OprDate, DataSet.OprXid, DataSet.OprXid, Department, party,
                                            depoacc, depopart, daccount, FIID, InvCardID, LogOprID, ToExcel, is_app, true, RepExec, DataSet.DocID);
    elif(DataSet.OprClass == SP_ADM_OPERATION)
      CountReport = CountReport + Order_ADM(GrDoc, date_begin, date_end, xld_begin, xld_end, opkind, opstatus,
                                            DataSet.OprDate, DataSet.OprDate, DataSet.OprXid, DataSet.OprXid, Department, party,
                                            depoacc, depopart, daccount, FIID, InvCardID, LogOprID, ToExcel, is_app, RepExec, DataSet.DocID);
    elif(DataSet.OprClass == SP_INF_OPERATION)
      CountReport = CountReport + Order_INF(GrDoc, date_begin, date_end, xld_begin, xld_end, opkind, opstatus,
                                            DataSet.OprDate, DataSet.OprDate, DataSet.OprXid, DataSet.OprXid, Department, party,
                                            depoacc, depopart, daccount, FIID, InvCardID, LogOprID, ToExcel, is_app, RepExec, DataSet.DocID);
    end;
  end;

  return CountReport;
end;

private macro CreateEmptyRep( Rep, Parm )
  DP_StdHeaderLO_Ex( Rep, RepFontStyleInv, "Отчет об исполнении поручения", LogOprID, 0, false, opstatus_begin, opstatus_end, tablewidth );
  DP_AddPrintCell( Rep,  "Нет данных, удовлетворяющих параметрам отчета.", tablewidth, 0, "l:" + RepFontStyleTitel, is_app, REP_ELEM_STR );
  Rep.AddEmptyStr();
  Rep.AddEmptyStr();
  DP_StdAuthPerson_Ex( Rep, RepFontStyleInv );
  DP_StdDeposInfo_Ex( Rep, RepFontStyleInv, tablewidth );
  DP_StdExecReport_Ex( Rep, RepFontStyleInv, NULL, NULL, tablewidth );
  return 0;
end;

// Вызов из отчета об исполнении операции (АО, ИО, Инв. Оп)
macro OrderExec( _Department : integer,
                 _GrDoc : integer,
                 _date_begin : date,
                 _date_end : date,
                 _xld_begin : string,
                 _xld_end : string,
                 _opclass : integer,
                 _opkind : integer,
                 _opstatus : integer,
                 _opstatus_begin : date,
                 _opstatus_end : date,
                 _ojdnum_begin : string,
                 _ojdnum_end : string,
                 _party : integer,
                 _depoacc : integer,
                 _depopart : integer,
                 _daccount : string,
                 _FIID : integer,
                 _InvCardID : integer,
                 _LogoprID: integer,
                 _ToExcel:bool,
                 _is_app:bool )

  var stat = 0;

  Department     = _Department;
  GrDoc          = _GrDoc;
  date_begin     = _date_begin;
  date_end       = _date_end;
  xld_begin      = _xld_begin;
  xld_end        = _xld_end;
  opclass        = _opclass;
  opkind         = _opkind;
  opstatus       = _opstatus;
  opstatus_begin = _opstatus_begin;
  opstatus_end   = _opstatus_end;
  ojdnum_begin   = _ojdnum_begin;
  ojdnum_end     = _ojdnum_end;
  party          = _party;
  depoacc        = _depoacc;
  depopart       = _depopart;
  daccount       = _daccount;
  FIID           = _FIID;
  InvCardID      = _InvCardID;
  LogoprID       = _LogoprID;
  ToExcel        = _ToExcel;
  is_app         = _is_app;

  Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
  return DP_StdReportControl( LogOprID, CreateRep(), @CreateEmptyRep, 0, ToExcel, RepExec );

end;

// Вызов из получателей отчета об исполнении (все операции, включая ГО)
macro OrderExecByGrrep(DPGrrepID)
  var cmd, DS;
  var ToExcel, err;

  cmd = DL_RSDCommand(  " select rep.t_SourceDocID, rep.t_SourceDocKind, rep.t_Recipient, "
                      + "        spgr.t_Xld, spgr.t_RegistrDate, spgr.t_RegistrTime, spgr.t_Receptionist "
                      + "   from ddpgrrep_dbt rep, ddprepdoc_dbt repdoc, dspground_dbt spgr "
                      + "  where rep.t_ID = ? "
                      + "    and repdoc.t_GrRepID = rep.t_ID "
                      + "    and spgr.t_SPgroundID = repdoc.t_RepSPgroundID "
                     );

  cmd.AddParam(DPGrrepID);

  DS = cmd.Execute();
  while(DS.moveNext())
    if( SP_DEPOPER_ADMOPER == DS.SourceDocKind) // АО
      Order_ADMbyAdmOprID( DS.SourceDocID, DS.Recipient, DS.Xld, date(DS.RegistrDate), time(DS.RegistrTime), DS.Receptionist, @RepExec);
    elif( SP_DEPOPER_INFOPER == DS.SourceDocKind) // Инф Оп
      Order_INFbyLogID( DS.SourceDocID, DS.Recipient, DS.Xld, date(DS.RegistrDate), @RepExec);
    elif( (SP_DEPOPER_BOOKORDER == DS.SourceDocKind) OR
          (SP_DEPOPER_TRANSFERORDER== DS.SourceDocKind) OR
          (SP_DEPOPER_ENROLMENT    == DS.SourceDocKind) OR
          (SP_DEPOPER_RECORDER     == DS.SourceDocKind) OR
          (SP_DEPOPER_WITHDRORDER  == DS.SourceDocKind) ) // Инв оп
      Order_INVbyDraft(DS.SourceDocID, DS.Recipient, DS.Xld, date(DS.RegistrDate), @RepExec);
    else // ГО
      DP_GlobRepByDoc(DS.SourceDocID, DS.Recipient, DS.Xld, date(DS.RegistrDate), @RepExec);
    end;

    GetRegistryValue("DEPO\\DEPOREPTOEXCEL", V_BOOL, ToExcel, err);
    if( err != 0 )
      ToExcel = false;
    end;

    if( ToExcel )
      RepExec.PrintWinRep();
      RepExec.ShowWinRep();
    else
      RepExec.PrintRep();
    end;
  end;
end;
