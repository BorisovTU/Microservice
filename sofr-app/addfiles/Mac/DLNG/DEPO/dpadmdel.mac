/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                          R-Style SoftLab

  File Name   : dpadmdel.mac
  Created     : 05.04.05
  Programmer  : Nikonorov Evgeny
  Description : Печать отчетов удаления административных операций 

└───────────────────────────────────────────────────────────────────────────*/
import dpscanr, BankInter;
import RsbDataSet;

const РежимУдалАдмОп = 0;

macro ОтчетАдмОп( Вызов, firstdoc, Статус, Сообщение, Режим )
     var KindName;
     var err, ValueAdm;

     RECORD admo(depoadmo); 
     FILE depoadmo(depoadmo);
     FILE dpadmopr(dpadmopr);

     setbuff(admo,firstdoc);
               
     if (Вызов==Первый)
       if(Режим == РежимУдалАдмОп)
[             Отчет о групповом удалении административных операций.];
       end;
[+-------+----------------------------------------+----------------------------------------------------------------------------------------+];
[| Вх.№  |          Тип административного         |                 Сообщение                                                              |];
[|       |                действия                |                                                                                        |];
[+-------+----------------------------------------+----------------------------------------------------------------------------------------+];
     elif (Вызов==Ошибка)

        if( (admo.Item) and (admo.OperKind))
          dpadmopr.Item = admo.Item;
          dpadmopr.OperKind = admo.OperKind;
          if( GetEQ(dpadmopr) )
             KindName = dpadmopr.Name;
          else
             KindName = "";
          end;
        end;

        GetRegistryValue( "DEPO\\ALLOWEDITADMOPER", V_BOOL, ValueAdm, err );
        if( (err == 0) and (ValueAdm == false) )//запрещено редактировать адм операции
          Сообщение = "Запрещено редактировать список административных операций. Операция не удалена.";
        end;

        if( (Статус == 0) and (Сообщение == "") ) 
           if(admo.AdmoprID)
              depoadmo.AdmoprID = admo.AdmoprID;
              if( GetEQ(depoadmo) )
                 Сообщение = "Операция не удалена.";
              else
                 Сообщение = "Операция успешно удалена.";
              end;
           end;
        end;

[|#######|########################################|########################################################################################|]
( admo.AdmoprID, KindName:w, Сообщение);
     elif (Вызов==Последний)            
[+-------+----------------------------------------+----------------------------------------------------------------------------------------+];
     end;
end;


/* точка входа в макрос!!! */
MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение,Режим)
     if( Режим == РежимУдалАдмОп ) //собственно другого не может и быть
       ОтчетАдмОп(Вызов, firstdoc, Статус, Сообщение, Режим);
     end;
END;