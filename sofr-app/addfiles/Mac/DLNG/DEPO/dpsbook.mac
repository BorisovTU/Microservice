/* -@H------------------------------------------------------------------
*            Автоматизированная банковская система RS-Bank
*                 Библиотека интерпретируемых модулей
*                  Copyright (c) JV R-Style 1993-96
*
*   Description      :  Книжный перевод -- списание   
*
*----------------------------------------------------------------------*/

import oprinter, SPInter, PaymInter, "dps_com.inc", "dp_util.mac";


file pmpaym("pmpaym") key 10;

macro CalcServiceSum( sfcontr_addr, BeginDate, EndDate )
    var stat;
    var Sum, BnAccount = "", DwAccount = "";

    record  rContr( sfcontr );
    record  rBaseSum( "sfbassum.str" );
    file depoacnt(depoacnt) key 1;
    SetBuff( rContr, sfcontr_addr );

    /*Поручение на книжный перевод эмиссионных ценных бумаг*/
    pmpaym.DocKind     = SP_DEPOPER_BOOKORDER;
    pmpaym.ValueDate   = BeginDate;
    pmpaym.Purpose     = PurpBase;
    pmpaym.SubPurpose  = 0;
    stat = GetGE(pmpaym);
    while(  stat and
           (pmpaym.DocKind   == SP_DEPOPER_BOOKORDER) and
           (pmpaym.ValueDate <= EndDate)
         )
       if(pmpaym.PaymStatus == PM_FINISHED)
          BnAccount = GetHeadAccForAcc(pmpaym.ReceiverDpNode);
          DwAccount = GetHeadAccForAcc(pmpaym.PayerDpNode);
          if( (pmpaym.Purpose == PurpBase) and 
              (DwAccount == rContr.Object ) and  /*счет клиента c которого списаны бумаги*/
              (BnAccount != DwAccount)   /*по разным счетам*/
            )

             Sum = PaymNominal(pmpaym);

             ClearRecord( rBaseSum );
             rBaseSum.baseType    = SF_BASETYPE_SUM;
             rBaseSum.baseType2   = SF_BASETYPE_SUM;
             rBaseSum.baseSum     = Sum;
             rBaseSum.baseSum2    = Sum;

             rBaseSum.BaseObjectType = OBJTYPE_OPERATIONDEPO;
             rBaseSum.BaseObjectID   = GetSpdraftByPayment(pmpaym.PaymentID);

             if( InsertSumList(rBaseSum) )
                stat = FALSE;
                MacroError = 1;
                MsgBox("Ошибка при вставке базовой суммы");
             end;

          end;
       end;
       if( stat )
          stat = Next(pmpaym);
       end;
    end;

    return;
end;
