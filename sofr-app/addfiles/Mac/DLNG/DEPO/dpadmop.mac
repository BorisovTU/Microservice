/*  Описание         : Отчет "Поручение на административную операцию" */
/*  Программист      : Chernov Anton 26-03-2008                       */

import CTInter, DPInter, dprepsec;

var HeadTable = "┌────────────────┬────────────────────┬────────────────────────────────┬────────────────────────────────┬────────────────────────────────┐\n"+
                "│    Документ    │    Номер анкеты    │            Код поля            │        Новое значение          │        Старое значение         │\n"+
                "│    (объект)    │                    │                                │                                │                                │\n"+
                "├────────────────┼────────────────────┼────────────────────────────────┼────────────────────────────────┼────────────────────────────────┤\n"+
                "│       1        │          2         │               3                │               4                │               5                │\n"+
                "├────────────────┼────────────────────┼────────────────────────────────┼────────────────────────────────┼────────────────────────────────┤"; 
                
const RepFontStyleTitel =  "ex_FS(b)";

private const SP_DEPOPER_ADMOPER = 825,
              OBJTYPE_ADMOPRTYPE = 1207;

private record dpadmo("depoadmo");
private record spgrin("spground");

var sNAMEs = TArray,
    sFROM  = TArray,
    sTO    = TArray;

private class RepDataPrm( _depoadmo,
                          _spgrin,
                          _IsMem )
  var Rep;
  var IsPrintedHead = false,
      IsMem = _IsMem,
      IsExcel = false;
  var err;

  SetBuff(dpadmo, _depoadmo);
  SetBuff(spgrin, _spgrin);

  GetRegistryValue("DEPO\\DEPOREPTOEXCEL", V_BOOL, IsExcel, err);
  if( err != 0 )
    IsExcel = false;
  end;

  Rep = CMakeReport(HeadTable);
end;

private macro Max3(a, b, c)
  return max(max(a, b), c);
end;

private macro FindINVCARD(InvCardID)
  file invcard("invcard");

  Clearrecord(invcard);
  invcard.InvCardID = InvCardID;
  
  if( getEQ(invcard) )
    return invcard.Number;
  end;

  return "-";
end;


private macro FindFININSTR(FIID)
  file fininstr("fininstr");

  Clearrecord(fininstr);
  fininstr.FIID = FIID;
  
  if( getEQ(fininstr) )
    return fininstr.FI_Code;
  end;

  return "-";
end;


private macro FindDEPOACNT(AutoKey)
  file depoacnt("depoacnt");

  Clearrecord(depoacnt);
  depoacnt.AutoKey = AutoKey;
  
  if( getEQ(depoacnt) )
    return depoacnt.Code;
  end;

  return "-";
end;


private macro FindPARTCODE(PartyID)
  file partcode("partcode");

  Clearrecord(partcode);
  partcode.PartyID = PartyID;
  partcode.CodeKind = 1;
  
  if( getEQ(partcode) )
    return partcode.Code;
  end;

  return "-";
end;

private macro FindOperName(PartyID)
  file party("person");

  Clearrecord(party);
  party.Oper = PartyID;
  
  if( getEQ(party) )
    return party.Name;
  end;

  return "-";
end;

private macro PrintHeader(RepData)
  var llvalues = TRecHandler("llvalues");
  var sql, rsd, 
      AddDocs = "", 
      AdmType = "",
      IsFirst;
  record spground("spground");

  if( not RepData.IsPrintedHead )
    RepData.IsPrintedHead = true;

    /* добываем документы-приложения */
    if( not RepData.IsMem )
      /*напрямую из БД*/
      sql = " SELECT spgr.t_Xld, "
          +        " spgr.t_RegistrDate, "
          +        " llv.t_Name "
          +   " FROM dspgrdoc_dbt grdoc, "
          +        " dspground_dbt spgr, "
          +        " dllvalues_dbt llv "
          +  " WHERE grdoc.t_SourceDocKind = " + string(SP_DEPOPER_ADMOPER)
          +    " AND grdoc.t_SourceDocID = " + string(dpadmo.AdmoprID)
          +    " AND spgr.t_SPGroundID = grdoc.t_SPGroundID "
          +    " AND llv.t_List = " + string(OBJTYPE_KINDDOC)
          +    " AND llv.t_Element = spgr.t_Kind ";
      rsd = TRsbDataSet(sql);
      while(rsd.MoveNext())
        if( AddDocs != "" )
          AddDocs = AddDocs + ", \n";
        end;

        AddDocs = AddDocs
                + rsd.Name + " "
                + "№" + rsd.Xld
                + " от " + date(rsd.RegistrDate);
      end;
    else
      /*из памяти*/
      IsFirst = true;
      while( ПолучитьПамДокПриложения(IsFirst, SP_DEPOPER_ADMOPER, dpadmo.AdmoprID, true, spground) )
        IsFirst = false;

        if( AddDocs != "" )
          AddDocs = AddDocs + ", \n";
        end;

        if( LL_FindLLVALUES( OBJTYPE_KINDDOC, spground.Kind, llvalues ) )
          ClearRecord(llvalues.rec);
        end;

        AddDocs = AddDocs
                + llvalues.rec.Name + " "
                + "№" + spground.Xld
                + " от " + spground.RegistrDate;
      end;
    end;

    if( AddDocs == "" )
      AddDocs = "-";
    end;

    sql = " SELECT t_Code, "
        +       " t_Name "
        +   " FROM ddpadmopr_dbt "
        +  " WHERE t_OperKind = " + string(dpadmo.OperKind)
        +    " AND t_Item = " + string(dpadmo.Item);
    rsd = TRsbDataSet(sql);
    if( rsd.MoveNext() )
      AdmType = rsd.Code + ", " + rsd.Name;
    end;

    if( LL_FindLLVALUES( OBJTYPE_ADMOPRTYPE, dpadmo.OperKind, llvalues ) )
      ClearRecord(llvalues.rec);
    end;

    DP_StdHeaderLO_Ex( RepData.Rep, RepFontStyleTitel, "ПОРУЧЕНИЕ НА АДМИНИСТРАТИВНУЮ  ОПЕРАЦИЮ", 0, 0, false );
    RepData.Rep.AddEmptyStr();

    RepData.Rep.AddPrintCell( "Номер и дата исходящего ", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddPrintCell( "№" + spgrin.AltXld + " от " + spgrin.SignedDate, 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddStr();

    RepData.Rep.AddPrintCell( "Инициатор", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddPrintCell( dpadmo.InitiatorCode + " " + dpadmo.InitiatorName, 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddStr();

    RepData.Rep.AddPrintCell( "Отправитель поручения", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddPrintCell( spgrin.PartyCode + " " + spgrin.PartyName, 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddStr();

    RepData.Rep.AddPrintCell( "Получатель поручения", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddPrintCell( FindOperName(spgrin.Receptionist), 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddStr();

    RepData.Rep.AddPrintCell( "Номер и дата входящего", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddPrintCell( "№" + spgrin.Xld + " от " + spgrin.RegistrDate, 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddStr();

    RepData.Rep.AddEmptyStr();

    RepData.Rep.AddPrintCell( "Тип операции", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddPrintCell( AdmType, 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddStr();

    RepData.Rep.AddPrintCell( "Основание", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddPrintCell( AddDocs, 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddStr();
    RepData.Rep.AddEmptyStr();

    RepData.Rep.AddPrintCell( "Изменение анкетных данных", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    RepData.Rep.AddStr();
  end;
end;

private macro PrintLine(RepData)
  var iCount = 0,
      MaxLen = 0,
      Item = "",
      Identificator = "",
      FieldCode = "", 
      OldValue = "", 
      NewValue = "";

  PrintHeader(RepData);

  /* Разбираем изменения в анкете */
  while( iCount < sNAMEs.Size )
    if( ( sFROM[iCount] != sTO[iCount]) AND (sNAMEs[iCount] != "") )

      FieldCode = FieldCode + sNAMEs[iCount] + "\n";

      if( sTO[iCount] != "" )
        NewValue = NewValue + sTO[iCount] + "\n";
      else
        NewValue = NewValue + "-\n";
      end;

      if( sFROM[iCount] != "" )
        OldValue = OldValue + sFROM[iCount] + "\n";
      else
        OldValue = OldValue + "-\n";
      end;
    end;
    iCount = iCount + 1;
  end;

  /* типы документов и их идентификаторы */
  if( dpadmo.PartyID > 0 )
    Item = "субъект\n";
    Identificator = FindPARTCODE(dpadmo.PartyID) + "\n";
  end;
  if( dpadmo.DepoAcc > 0 )
    Item = Item + "счет депо\n";
    Identificator = Identificator + 
                    FindDEPOACNT(dpadmo.DepoAcc) + "\n";
  end;
  if( dpadmo.DepoPart > 0 )
    Item = Item + "раздел счета депо\n";
    Identificator = Identificator + 
                    FindDEPOACNT(dpadmo.DepoPart) + "\n";
  end;
  if( dpadmo.FIID > 0 )
    Item = Item + "ценная бумага\n";
    Identificator = Identificator + 
                    FindFININSTR(dpadmo.FIID) + "\n";
  end;
  if( dpadmo.Account != "" )
    Item = Item + "лицевой счет";
    Identificator = Identificator + dpadmo.Account;
  end;
  if( dpadmo.CertifID > 0 )
    Item = Item + "инвентарная карточка\n";
    Identificator = Identificator + 
                    FindINVCARD(dpadmo.CertifID) + "\n";
  end;

  RepData.Rep.AddPrintCell( Item, 0, 0, "l:w" );
  RepData.Rep.AddPrintCell( Identificator, 0, 0, "l:w" );
  RepData.Rep.AddPrintCell( FieldCode, 0, 0, "l:w:" );
  RepData.Rep.AddPrintCell( NewValue, 0, 0, "l:w:" );
  RepData.Rep.AddPrintCell( OldValue, 0, 0, "l:w:" );

  RepData.Rep.AddStr();
end;

private macro PrintReport(RepData)
  if( RepData.IsExcel )
    RepData.Rep.PrintWinRep();
    RepData.Rep.ShowWinRep();
  else
    RepData.Rep.PrintRep();
  end;
end;


macro RunRep( _depoadmo  : memaddr,
              _spgrin    : memaddr,
              _IsMem     : bool
            )
  var RepData = RepDataPrm(_depoadmo, _spgrin, _IsMem);
  var iCount = 0;

  GetDEPOADMO_Contents(dpadmo, sNAMEs, sFROM, sTO);

  PrintLine(RepData);
  DP_StdFooter_Ex(RepData.Rep, RepFontStyleTitel);
  PrintReport(RepData);
end;
