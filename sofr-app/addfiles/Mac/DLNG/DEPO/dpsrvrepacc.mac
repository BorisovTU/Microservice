/*
$Name:        dpsrvrepacc.mac
$Module:      Депозитарий
$Description: Процедура массового формирования выписок о состоянии счета депо
*/
import DPInter, "spinfop.mac";

private var CurrDepoacntID = 0;
private var CurrGUID = "";

macro srvrepacc_msgbox()
  var i = 0, StrErr = "", str = "";

  while(getparm(i,str))
    StrErr = StrErr + string(str);
    str = "";
    i = i + 1;
  end;
  
  var cmd = RsdCommand( " INSERT INTO ddpaccreplog_dbt( t_GUID, t_DEPOACNTID, t_MESSAGE ) " +
                        "                       VALUES(?, ?, ?)" );

  cmd.addParam( "", RSDBP_IN, CurrGUID );
  cmd.addParam( "", RSDBP_IN, CurrDepoacntID );
  cmd.addParam( "", RSDBP_IN, StrErr );

  SQL_Execute( cmd );
end;

PRIVATE MACRO GetError()
  var errstr = GetErrMsg();

  if(strlen(errstr) != 0)
    msgbox(errstr);
  end;
END;

PRIVATE CLASS SrvRepAccParams()
  var GUID = ""; //Идентификатор запуска
  var CreateDate:date = date(0,0,0); //Дата запуска

  var RepFormatKind:integer    = 0;           //Формат отчета
  var RepDate:date             = date(0,0,0); //Дата
  var GroundKind:integer       = 0;           //Вид документа-основания
  var InitiatorIsOurBank:bool  = false;       //Признак использования НашегоБанка в качестве инициатора
  var InitiatorIsDeponent:bool = false;       //Признак использования Депонента в качестве инициатора
  var IssuerID:integer         = -1;          //Идентификатор эмитента
  var AvoirKind:integer        = 0;           //Вид ц/б
  var FIID:integer             = -1;          //Идентификатор ц/б
  var WithAmortization:bool    = false;       //С учётом амортизации номинала
  var ByPartition:bool         = false;       //По разделам
  var ByNullRest:bool          = false;       //Нулевые остатки
  var WithStoragePlace:bool    = false;       //С указанием МХ
  var AutoPrint:bool           = false;       //Автоматическая печать
  var Copies:integer           = 0;           //Количество экземпляров
  var Comment:string           = "";          //Комментарий
  var InitiatorIsMortgagee:bool= false;       //Признак использования Залогодержателя в качестве инициатора
  var showPawn:bool            = false;       //Признак формирования отчета только по разделам по учету ценных бумаг, находящихся в залоге
  var MortgageeID:integer      = -1;          //Идентификатор залогодержателя

END;

PRIVATE MACRO StartOneRepAcc(DepoAcntID:integer, params)
  var acnt = TBFile("depoacnt.dbt");
  var inf = TRecHandler("depoinfo.dbt");
  var err = 0;

  var OldDialogFlag = SetDialogFlag(0);

  CurrDepoAcntID = DepoAcntID;

  acnt.Clear();
  acnt.rec.AutoKey = DepoAcntID;
  if(acnt.GetEQ())

    inf.Clear();
    
    inf.rec.LogOprState     = 1;//SC_SPDRAFT_STATUS_PREP;              // Статус
    inf.rec.LogOprProduct   = 700;//OBJTYPE_TRN_CONT_REPORT;           // Содержание транзакции
    inf.rec.LogOprcarryDate = params.CreateDate;  // Дата операции
    inf.rec.LogOprGrDraftID = 0;          // Первичный документ инф. операции

    inf.rec.LogOprInitiator = acnt.rec.Owner; // Инициатор операции
    if(params.InitiatorIsOurBank)
      inf.rec.LogOprInitiator = {OurBank};
    end;
    if(params.InitiatorIsMortgagee and (params.MortgageeID > 0))
      inf.rec.LogOprInitiator = params.MortgageeID;
    end;

    inf.rec.InfoprdocType        = 20;                   // Вид отчета
    inf.rec.InfoprSubType        = params.RepFormatKind; // Формат отчета
    inf.rec.InfoprParty          = acnt.rec.Owner;       // Субъект
    inf.rec.InfoprDepoAcc        = acnt.rec.Root;        // Счет депо
    inf.rec.InfoprDepoPart       = IIF(acnt.rec.AutoKey == acnt.rec.Root, 0, acnt.rec.AutoKey); // Раздел
    inf.rec.InfoprSubKind        = params.AvoirKind;     // Вид ц/б
    inf.rec.InfoprFIID           = params.FIID;          // Выпуск
    inf.rec.InfoprKind           = acnt.rec.Kind;        // Вид счета
    inf.rec.InfoprApostrophe     = SET_CHAR;             // Апострофы
    inf.rec.InfoprDepartment     = acnt.rec.Department;  // Филиал
    inf.rec.InfoprfromDate       = params.RepDate;       // Начальная дата
    inf.rec.InfoprtoDate         = params.RepDate;       // Конечная дата
    inf.rec.InfoprIssuer         = params.IssuerID;      // Эмитент
    inf.rec.InfoprFlag1          = IIF(params.ByPartition == true, SET_CHAR, UNSET_CHAR);  //По разделам
    inf.rec.InfoprFlag3          = IIF(params.ByPartition == true, UNSET_CHAR, SET_CHAR);  //По счету
    inf.rec.InfoprFlag2          = IIF(params.ByNullRest == true, SET_CHAR, UNSET_CHAR);   //Нулевые остатки
    inf.rec.InfoprFlag5          = IIF(params.WithAmortization == true, SET_CHAR, UNSET_CHAR); //С учетом амортизации номинала
    inf.rec.InfoprFlag6          = IIF(params.WithStoragePlace == true, SET_CHAR, UNSET_CHAR); //С указанием МХ
    inf.rec.InfoprToExcel        = SET_CHAR;             // в Excel
    inf.rec.InfoprStoragePlaceID = -1;                   // депозитарий места хранения
    inf.rec.InfoprIssuerTo       = -1;                   // эмитент расчитываемого выпуска
    inf.rec.InfoprFIIDto         = -1;                   // расчитываемый выпуск 
    inf.rec.InfoprComment        = params.Comment;       // Комментарий
    inf.rec.GrDraftKind          = params.GroundKind;    // Вид документа-основания
    inf.rec.GrDraftRegistrDate   = params.CreateDate;
    inf.rec.GrDraftSignedDate    = params.CreateDate;    // дата
    inf.rec.GrDraftSignedTime    = time();               // время
    inf.rec.GrDraftProxy         = -1;                   // Исполнитель
    inf.rec.GrReportRegistrDate  = params.CreateDate;
    inf.rec.GrReportPartyID      = acnt.rec.Owner;       // Получатель отчета
    inf.rec.GrReportDeliveriKind = CodeFor(acnt.rec.DSDelivery); // Способ доставки отчета
    inf.rec.UserLog              = SPGRUSERLOG_CLIENT;   // Вид журнала
    if(params.InitiatorIsOurBank)                                               
      inf.rec.UserLog            = SPGRUSERLOG_OURENTER;
    end;
    inf.rec.DeponentDoc          = -1;                   // Депонент

    if(params.showPawn)                                  // Признак залога
      inf.rec.ByPawn             = SET_CHAR;       
    else
      inf.rec.ByPawn             = UNSET_CHAR;       
    end;
    inf.rec.MortgageeID          = params.MortgageeID;    // Залогодержатель
    err = CreateInfOperation(inf, true, IIF(params.AutoPrint == true, DP_PRINTMODE_PRINT, DP_PRINTMODE_ONLYSAVE), params.Copies);

    GetError();
  end;

  CurrDepoacntID = 0;

  SetDialogFlag(OldDialogFlag);

  return err;

OnError(er)
    msgbox(er.module+ " "+er.line+" "+er.message);
END;

PRIVATE MACRO GetSrvRepAccObjectQuery()
  var query = "";

  query =   " select 0 as t_ObjectType, t_DepoAcntID as t_ObjectID, ROWNUM as t_GroupNumber, t_DepoAcntID "
          + "   from ddepoacnt_tmp";

  return query;
END;

MACRO ОтобратьОбъектыДляВыписок(_taskID, _execID, _conveyerID, _packetSize, _Params)
  var cnt = 0;
  var query, DataSet;

  query = " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
        + " SELECT "+_taskID+","+_execID+","+_conveyerID+",q.t_GroupNumber, q.t_ObjectType, q.t_ObjectID "
        + "   FROM (" + GetSrvRepAccObjectQuery() + ") q ";
  query = query + " ORDER BY q.t_ObjectID ";

  SQL_Execute(query, "Отбор объектов", false );
  
  if(_packetSize == 0)
    cnt = 1;
  else
    query =   " select NVL(max(t_PackID), 0) as cnt "
            + "   from dcnvdoc_dbt "
            + "  where t_ExecID = " + _execID;
    
    DataSet = TRsbDataSet(query);
    if(DataSet.moveNext())
      cnt = int(round(int(DataSet.cnt), 0));
    end;
  end;

  return cnt;
END;


MACRO ExecuteSrvRepAcc(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params)
  var query, cmd, DataSet;
  var cnt = 0;
  var err = 0;

  ReplaceMacro( "msgbox", "srvrepacc_msgbox" );

  CurrGUID = _Params.GUID;
     
  if((_conveyerID) and (_packetID))
    
    query =  "select t_ObjectID as t_DepoAcntID from dcnvdoc_dbt where t_ExecID = ? and t_ConvTypeID = ? and t_PackID = ?";
    
    cmd = DL_RSDCommand(query);
    
    cmd.AddParam(_execID);
    cmd.AddParam(_conveyerID);
    cmd.AddParam(_packetID);
    
    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      StartOneRepAcc(DataSet.DepoAcntID, _Params);
    end;

  else
    query = "select q.t_DepoAcntID from ("+GetSrvRepAccObjectQuery()+") q ";
    cmd = DL_RSDCommand(query);
    cnt = cmd.GetCount();
     
    if(cnt > 0)
      InitProgress( cnt, "Идет выполнение ...", "Подготовка выписок" );
      
      DataSet = cmd.Execute();
      var i = 0;
      while(DataSet.moveNext())
        
        StartOneRepAcc(DataSet.DepoAcntID, _Params);

        UseProgress(i = i + 1);
      end;

      RemProgress();
    end;
  end; 
  
  if((_conveyerID) and (_packetID))
    /*не зависимо от того, делали что-то или нет - ставим исходящий статус обрабатываемой пачке*/
    var exec = RSDCommand(  " UPDATE dcnvpack_dbt "
                          + " SET t_State = t_State + 1 "
                          + " WHERE t_ExecID = ? "
                          + "   AND t_ConvTypeID = ? "
                          + "   AND t_PackID = ? ");

    exec.addParam( "", RSDBP_IN, _execID );
    exec.addParam( "", RSDBP_IN, _conveyerID );
    exec.addParam( "", RSDBP_IN, _packetID );
    exec.execute();
  end; 

  ReplaceMacro( "msgbox", NULL );

  return err;

OnError(er)
    msgbox(er.module+ " "+er.line+" "+er.message);
END;

PRIVATE MACRO PrintProtocol(params)
  var ReportFileName, FName = "srvrepaccprotocol";
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/
  var cmd, query, DataSet;

  ReportFileName = GetTxtFileName( FName );
  if( Open( ReportOutFile, ReportFileName ) == false )
     MsgBox( "Ошибка при открытии файла отчета|"+ ReportFileName);
     return;
  end;

  SetOutput( ReportFileName );

  [###########################################################################################################################################]
  ("Протокол выполнения процедуры формирования отчетов о состоянии счета депо":c);
  [###########################################################################################################################################]
  (string("за "+DL_DateToStr(date(params.RepDate))):c);
  println("");


  query =   " select NVL(acnt.t_Code, CHR(1)) as AcntCode, "
          + "        NVL(acnt.t_Name, CHR(1)) as AcntName, "
          + "        l.t_Message "
          + "   from ddpaccreplog_dbt l "
          + "        left join ddepoacnt_dbt acnt on acnt.t_AutoKey = l.t_DepoAcntID "
          + "  where l.t_GUID = ? ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(params.GUID);

  if(cmd.GetCount() > 0)

    println("┌────────────────────────┬──────────────────────────────────────────┬─────────────────────────────────────────────────────────────────────┐");
    println("│ Код счета/раздела депо │            Наименование счета            │                               Сообщение                             │");
    println("│                        │                                          │                                                                     │");
    println("├────────────────────────┼──────────────────────────────────────────┼─────────────────────────────────────────────────────────────────────┤");


    DataSet = cmd.Execute();
    var i = 0;
    while(DataSet.moveNext())

      if(i != 0)
        println("├────────────────────────┼──────────────────────────────────────────┼─────────────────────────────────────────────────────────────────────┤");
      end;

      [│########################│##########################################│#####################################################################│]
      (DataSet.AcntCode, DataSet.AcntName:w, DataSet.Message:w);

      i = i + 1;
    end;

    println("└────────────────────────┴──────────────────────────────────────────┴─────────────────────────────────────────────────────────────────────┘");

  else
    println("Процедура выполнена успешно.");
  end;

  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );

END;

macro StartSrvRepAcc(RepFormatKind:integer,    //Формат отчета
                     RepDate:date,             //Дата
                     GroundKind:integer,       //Вид документа-основания
                     InitiatorIsOurBank:bool,  //Признак использования НашегоБанка в качестве инициатора
                     InitiatorIsDeponent:bool, //Признак использования Депонента в качестве инициатора
                     InitiatorIsMortgagee:bool,//Признак использования Залогодержателя в качестве инициатора
                     IssuerID:integer,         //Идентификатор эмитента
                     AvoirKind:integer,        //Вид ц/б
                     FIID:integer,             //Идентификатор ц/б
                     WithAmortization:bool,    //С учётом амортизации номинала
                     ByPartition:bool,         //По разделам
                     ByNullRest:bool,          //Нулевые остатки
                     WithStoragePlace:bool,    //С указанием МХ
                     showPawn:bool,            //Признак формирования отчета только по разделам по учету ценных бумаг, находящихся в залоге
                     MortgageeID:integer,      //Идентификатор залогодержателя
                     AutoPrint:bool,           //Автоматическая печать
                     CreateReport:bool,        //Формировать протокол
                     Copies:integer,           //Количество экземпляров
                     Comment:string            //Комментарий
                    )

  var OperID = 18; //Операция для конвейера - Пакетная операция формирования выписок о состоянии счета депо
  var ConvID = 22; //Вид конвейера Подготовка выписок о состоянии счета депо
  var UseConveyer = SecurUseConveyer(ConvID, OperID);
  var params = SrvRepAccParams();
  var err = 0;

  params.GUID                = DL_GetGUID();
  params.CreateDate          = {curdate};

  params.RepFormatKind       = RepFormatKind;      
  params.RepDate             = RepDate;            
  params.GroundKind          = GroundKind;         
  params.InitiatorIsOurBank  = InitiatorIsOurBank; 
  params.InitiatorIsDeponent = InitiatorIsDeponent;
  params.IssuerID            = IssuerID;           
  params.AvoirKind           = AvoirKind;          
  params.FIID                = FIID;               
  params.WithAmortization    = WithAmortization;  
  params.ByPartition         = ByPartition;        
  params.ByNullRest          = ByNullRest;         
  params.WithStoragePlace    = WithStoragePlace;   
  params.AutoPrint           = AutoPrint;          
  params.Copies              = Copies;             
  params.Comment             = Comment;            
  params.InitiatorIsMortgagee= InitiatorIsMortgagee;          
  params.showPawn            = showPawn;             
  params.MortgageeID         = MortgageeID;            
  
  if(UseConveyer)
    var cnv = RsbConveyerExec(ConvID);

    cnv.AddParamsForConveyer(ConvID, params, 0);
    cnv.AddParamsForOperation(ConvID, OperID, params, "", 0);
    cnv.showPanelMonitoring(1);
    cnv.Run();
  else
    err = ExecuteSrvRepAcc(0, 0, 0, 0, 0, params);
  end;

  if(CreateReport == true)
    PrintProtocol(params);
  end;

  SQL_Execute("DELETE FROM DDPACCREPLOG_DBT WHERE T_GUID = '"+params.GUID+"'", "", false );

  return err;
end;
