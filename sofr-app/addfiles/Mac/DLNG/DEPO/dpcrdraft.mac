/*
$Name:         dpcrdraft.mac
$Module:       Депозитарий
$Description:  Формирование поручения депозитария по электронному сообщению
*/

import OprInter, PTInter, PaymInter, CTInter, SPInter, sp_class, "dpimp.mac", "opr_depo.mac";
import RsbDataSet;

const MSG_TYPES_DEFAULT = "544, 546";
const OPER_CODES_DEFAULT = "14";

/*Переменная DoLogging определяет куда будут выводится сообщения об ошибках функцией
  WriteStrToLog(): true - в лог-файл, false - на экран. По УМОЛЧАНИЮ на экран.
  ИЗМЕНЯЕТСЯ в фун-х BeginLogging(), EndLogging().
  ИСПОЛЬЗУЕТСЯ в фун-и WriteStrToLog()*/
var DoLogging = false;


private const ENROLOPRTRANSBLOCK = "DEPO\\ENROLOPRTRANSBLOCK";
private const ENROLOPRTRANSACC = "DEPO\\ENROLOPRTRANSACC";
private var DPError = CDPError;

record Draft  ("draftprm.rec") ;
record Pm_paym( pmpaym );


macro BeginLogging(fileName:string)
  DoLogging = true; //Сообщения об ошибках выводятся в лог-файл
  fileName = GetTxtFileName(fileName);
  SetOutput (fileName);
  println("Начало обработки. Операционный день: "+ {curdate} + ". Cистемная дата: " + date() + ", "  + time() + "\n" );
end;

macro WriteStrToLog(str:string)
  if(str != "")
    if(DoLogging)
      println(" -"+str);
    else
      MsgBox(str);
    end;
  end;
end;

macro EndLogging()
  println("\nКонец обработки. Операционный день: "+ {curdate} + ". Cистемная дата: " + date() + ", "  + time() + "\n" );
  SetOutput (NULL,TRUE);
  DoLogging = false; //Сообщения об ошибках выводятся на экран
end;

private macro GetPartyKindName(PartyKind)
  var Type;

  FILE llvalues(llvalues) key 0;

  llvalues.List = OBJTYPE_MSGPARTYTP;
  llvalues.Element = PartyKind;

  if( GetEQ(llvalues) )
    return llvalues.Name;
  else
    return "";
  end;
end;

private macro GetMessageParty(msginf, type, kind)
   var msgParty;
   var found = false;
   msginf.RewindMessageParty();

   msgParty = msginf.GetMessageParty();
   while( valtype(msgParty)!=V_UNDEF and (NOT found))
       if ( (msgParty.PartyType==type) and (msgParty.PartyKind==kind) )
          found = true;
          break;
       end;
       msgParty = msginf.GetMessageParty();
   end;
   
   if(NOT found)
     msgParty = null;
   end;
   
   return msgParty;
end;


private macro FillDepoDraft(dpi_buf, msginf)
 FILE dpac( depoacnt );
 FILE spdraft( spdraft );
 var day, month, year;
 
 var msgpat, DepoAccID, ErrCode, DepoBlock;
 var msgStr = "";
 var stat = 0; //по правилам С\С++: 1-ошибка, 0-нет
  
 var DataSet, query;
 var Dep_PartyID = -1;

 spdraft.AutoKey = msginf.DraftID;

 if(not getEQ(spdraft)) //Q
   if(((dpi_buf.MessageType == OBJTYPE_MSGTYPE_MT544) or (dpi_buf.MessageType == OBJTYPE_MSGTYPE_MT546)) and (dpi_buf.State == DPMSGINF_STATE_REG)) //Z 
     if(msginf.ProcessingCode == OPER_CODES_DEFAULT) //Y
       if(dpi_buf.MessageType == OBJTYPE_MSGTYPE_MT544) //X
         Draft.Kind = SP_DEPOPER_ENROLMENT;
         Draft.DwPartyID = UnknownParty;
         Draft.DwAccCode = "";
         Draft.Unblock = OBJTYPE_DEPOACC_BLOCK_STANDART;
         Draft.DwOurCustAgentID = msginf.SenderID;
         Draft.DwCustodyID = UnknownParty;
   
         //найдем счет                                                               
         query =   " select cr.t_Account from dcorschem_dbt cr" 
                   " where cr.t_CorrID = "  + msginf.SenderID +
                   "   and cr.t_CorAccount = '" + msginf.Account + "'";
         DataSet = TRsbDataSet(query);
         if( DataSet.moveNext() )
           Draft.DwOurCorAcc = DataSet.Account;
         else
           stat = 1;
           WriteStrToLog("Не найден корсчет прямого входящего корреспондента");
         end;
         
         if(NOT stat) //S
           Draft.BnPartyID = {OurBank};
           Draft.BnAccTypeDef = "";
           GetRegistryValue( ENROLOPRTRANSACC, V_INTEGER, DepoAccID, stat );
           if(NOT stat)
             dpac.AutoKey = DepoAccID;
             if(GetEQ(dpac))
               Draft.BnAccCode = dpac.BriefCode;
             else
               stat = 1;
               WriteStrToLog("Не найден транзитный счет ДЕПО с ID: ", DepoAccID, ". Необходимо указать транзитный счет зачисления в настройке депозитария");
             end;
           else
             WriteStrToLog("Ошибка поиска настройки \"Транзитный счет по умолчанию\"");
           end;
           
           if(NOT stat)
             GetRegistryValue( ENROLOPRTRANSBLOCK, V_INTEGER, DepoBlock, stat );
             if(NOT stat)
               Draft.Block = DepoBlock;
               Draft.BnCustodyID = {OurBank};
               Draft.BnOurCorAcc = "";
             else 
               WriteStrToLog("Ошибка поиска настройки \"Блокировка транзитного счета\""); 
             end;
           end;
         end; //S

         msgpat = GetMessageParty(msginf, DPMSGPAT_SECURITIES, OBJTYPE_MSGPATYTP_DEAG);
         if(msgpat != null)
           Draft.Initiator = msgpat.PartyID; 
         else
           Draft.Initiator = 0;
           WriteStrToLog("Не найден участник "+GetPartyKindName(OBJTYPE_MSGPATYTP_DEAG)+" электронного сообщения.");
         end;


       elif(dpi_buf.MessageType == OBJTYPE_MSGTYPE_MT546)
         Draft.Kind = SP_DEPOPER_TRANSFERORDER;
         Draft.DwPartyID = {OurBank};
         Draft.DwCustodyID = {OurBank};
         
         GetRegistryValue( ENROLOPRTRANSACC, V_INTEGER, DepoAccID, stat );
         if(NOT stat)
           dpac.AutoKey = DepoAccID;
           if(GetEQ(dpac))
             Draft.DwAccCode = dpac.BriefCode;
           else
             stat = 1;
             WriteStrToLog("Не найден транзитный счет ДЕПО с ID: " + DepoAccID + ". Необходимо указать транзитный счет зачисления в настройке депозитария");
           end;
         else
           WriteStrToLog("Ошибка поиска настройки \"Транзитный счет по умолчанию\"");
         end;
         
         if(NOT stat) //D
           GetRegistryValue( ENROLOPRTRANSBLOCK, V_INTEGER, DepoBlock, stat );
           if(NOT stat)
             Draft.Unblock = DepoBlock;
             Draft.DwOurCustAgentID = UnknownParty;
             Draft.DwOurCorAcc = "";
             Draft.BnPartyID = UnknownParty;
             Draft.BnAccTypeDef = "";
             Draft.BnAccCode = "";
             Draft.Block = OBJTYPE_DEPOACC_BLOCK_STANDART;
             Draft.BnCustodyID = UnknownParty;
             //найдем счет 
             query =   " select cr.t_Account from dcorschem_dbt cr" 
                       " where cr.t_CorrID = " + msginf.SenderID +
                       "   and cr.t_CorAccount = '" + msginf.Account + "'";
             DataSet = TRsbDataSet(query);
             if( DataSet.moveNext() )
               Draft.BnOurCorAcc = DataSet.Account;
             else
               stat = 1;
               WriteStrToLog("Не найден корсчет прямого входящего корреспондента, SenderID: " + 
                              msginf.SenderID + ", Account: " + msginf.Account + ".");
             end;
           else
             WriteStrToLog("Ошибка поиска настройки \"Блокировка транзитного счета\""); 
           end;
         end; //D

         msgpat = GetMessageParty(msginf, DPMSGPAT_SECURITIES, OBJTYPE_MSGPATYTP_REAG);
         if(msgpat != null)
           Draft.Initiator = msgpat.PartyID; 
         else
           Draft.Initiator = 0;
           WriteStrToLog("Не найден участник \""+GetPartyKindName(OBJTYPE_MSGPATYTP_DEAG)+"\" электронного сообщения.");
         end;


       end; //X
       
       if(NOT stat)
         Draft.SPgroundID = dpi_buf.SPgroundID;
         Draft.AltXid = "";
         Draft.SignedDate = date(0,0,0);
         Draft.ParentDocID = 0;
         Draft.UserLog = 0;
         Draft.Xid = "";
         Draft.DocKind = 0;
         Draft.RegisterDate = date(0,0,0);
         Draft.RegisterTime = time(0);
         Draft.DeliveryKind = 0;
         Draft.DwPartyName = "";
         Draft.DwAccTypeDef = "";
         Draft.DwAccPartitionCode = "";
         Draft.DwCustodyName = "";
         Draft.DwCustodyAgentID = UnknownParty;
         Draft.DwCustodyAgentName = "";
         Draft.DwCorAcc = "";
         Draft.BnPartyName = "";
         Draft.BnAccPartitionCode = "";
         Draft.BnCustodyName = "";
         Draft.BnCustodyAgentID = UnknownParty;
         Draft.BnCustodyAgentName = "";
         Draft.BnCorAcc = "";
         
         Draft.FIID = msginf.SecurityID;
         Draft.Amount = msginf.SettledQuantity;
         Draft.ValueDate = msginf.EffectiveSettlementDate;
         Draft.IsDVP = UNSET_CHAR; 
         Draft.DealKind = 0; 
         Draft.Agreed = date(0,0,0);
         Draft.DealXid = "";
         Draft.DealParty = -1;
         Draft.Product = 1;
         Draft.FromTransitAccount = UNSET_CHAR; 
         Draft.IndoorStorage = UNSET_CHAR;
         Draft.Oper = {oper};
        //  Draft.Department = (автоматически по операционисту)
         Draft.SendInstruction = UNSET_CHAR;
         Draft.WaitConfirmation = UNSET_CHAR;
         Draft.UseKvit = UNSET_CHAR;
         Draft.PaymentID = 0;
         Draft.DontCreateInitiatorReport = UNSET_CHAR;
         Draft.CreateDwReport = UNSET_CHAR;
         Draft.CreateBnReport = UNSET_CHAR;
         Draft.DocumentKind = 0;
         Draft.DocumentID = 0;
         Draft.GeneralizedFIID = msginf.SecurityID;
         Draft.DealAmount = msginf.DealAmount;
         Draft.DealCurrency = msginf.DealAmountCurrency;
         Draft.SettlementDate = msginf.SettlementDate;
         Draft.LateDeliveryDate = msginf.LateDeliveryDate;

         DateSplit( {curdate}, day, month, year );
         if(day < 10)
           day = string("0"+day);
         end;
         if(month < 10)
           month = string("0"+month);
         end;
         Draft.NumberPack = int(string(string(day)+string(month)));
       end;
     else
       stat = 1;
       WriteStrToLog("Неверный код (" + msginf.ProcessingCode + ") у корреспондента.");
     end; //Y
   else
     stat = 1;
     //Детализируем причину ошибки
     if(dpi_buf.State != DPMSGINF_STATE_REG)
       WriteStrToLog("Сообщение не зарегистрированно.");
     end;
     if(NOT ((dpi_buf.MessageType == OBJTYPE_MSGTYPE_MT544) or (dpi_buf.MessageType == OBJTYPE_MSGTYPE_MT546)))
       WriteStrToLog("Тип сообщения ("+ dpi_buf.MessageType +") не поддерживается.");
     end;
     WriteStrToLog(msgStr);
   end; //Z
 else
   stat = 1;
   WriteStrToLog("По данному сообщению уже сформировано поручение");
 end; //Q
  
 delete(msgpat);
  
 return stat;
 
end; //macro

macro InsertDepoDraftIntoDepart(dpi, dontShowError:bool, errorStr:@string)
  var stat = 0;
  RECORD dpi_buf( dpmsginf );
  var msginf;

  if(ValType(dpi) == V_MEMADDR) //из С/С++, dpi - (DPMSGINF*)
    SetBuff( dpi_buf, dpi );
  elif(ValType(dpi) == 19)      //из RSL, dpi - Tbfile("dpmsginf")
    Copy(dpi_buf, dpi);
  end;  
  
  msginf = RsbDepoInfoMessage(dpi_buf.MessageID);

  stat = FillDepoDraft(dpi_buf, msginf);
  
  if(NOT stat)
    stat = DP_InsertDeferDraft(Draft, Pm_paym, dontShowError, errorStr ); //возвращ. знач. по правилам С/С++ 
    if(not stat)
      msginf.DraftID = Draft.DraftID;
      msginf.Update();
    end;
  end;
  
  return stat;

end;
