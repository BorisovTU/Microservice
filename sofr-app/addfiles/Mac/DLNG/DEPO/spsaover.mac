/*
$Name: spsaover.mac
$Module: Депозитарий
$Description: отчет "Журнал оборотов лицевого счета депо"
*/
import BankInter, CurrInter, PTInter, SPInter, dprepsec, cb_sql;
import RsbDataSet;


const Depo_chapt =  5;    /* Глава синтетического учета */
const UNDEF_ID   = -1;    /* Значение, обозначающее, что ID не задан */
var DataPrinted = FALSE; /* Были ли распечатаны данные по счетам */
var _LogOprID, isPrint = false;
var _beginDate, _endDate, _daccount;

private var _department,
            _DAcnt, _DPart,
            _acc_client, 
            _depoacc_status,
            _ToExcel,
            _is_ap,
            _onlyPerAccWTrans;

const ALL_ACC = "Все";//все статусы - присваиваем сами 
const CLOSE_ACC = "З";//статус л/с - закрыт (в таблице )
const OPEN_ACC  = ""; //статус л/с - открыт (в таблице )

const ALL_STATUS = -1;//все статусы л/с  (из C )
const OPEN_STATUS = 0; //статус л/с - закрыт (из С )
const CLOSE_STATUS = 1;//статус л/с - открыт (из С )
var Status_ACC;

var HeadTable = "┌─────────────┬───────────────┬───────────────┬───────────────┬───────────────┐\n"+
                "│ Дата        │ Остаток на    │ Оборот по     │ Оборот по     │ Остаток на    │\n"+
                "│ внесения    │ начало        │ дебету        │ кредиту       │ конец         │\n"+
                "│ записи      │ периода       │               │               │ периода       │\n"+
                "├─────────────┼───────────────┼───────────────┼───────────────┼───────────────┤";
var tablewidth = Index(HeadTable, "\n");

var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

var avoiriss = TBFile("avoiriss");     
var fininstr = TBFile("fininstr");
FILE   avrk("avrkinds");


/* Проверить, не равно ли PartyID SelfID */
MACRO checkParty( PartyID )
   return PartyID;
END;

macro TablHeader(Account, Balance, Code_Currency, Close_Date, Open_Close, CountRep, is_ap, endDate)

   var format_str, date_cl;
   var avrkName = "";

   if( is_ap )
     format_str = "l:a";
   else
     format_str = "l";
   end;

   if ( isPrint == false )
     isPrint = true;
   else
     Rep.AddEmptyStr();
     Rep.AddEmptyStr();
   end;

   if( _daccount == "" )
     DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Журнал оборотов лицевого счета депо", _LogOprID, CountRep, true, _beginDate, _endDate, tablewidth );
   else
     DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Журнал оборотов лицевого счета депо", _LogOprID, CountRep, false, _beginDate, _endDate, tablewidth );
   end;
   
   DataPrinted = TRUE;
   ClearRecord(avrk);
   if ( ПолучитьФинИн(Code_Currency, fininstr, avoiriss) != 0 )
     MsgBox("Не могу получить параметры ценной бумаги");
     avrkName = "";
   else
     avrk.FI_Kind = fininstr.rec.FI_Kind;
     avrk.AvoirKind = fininstr.rec.AvoirKind;
     getEQ(avrk);
     avrkName = avrk.Name;
   end;

   if(Open_Close == CLOSE_ACC)
     date_cl = date(Close_Date);
   else 
     date_cl = "";
   end;


   DP_AddPrintCell( Rep, " Лицевой счет "+trim( Account )+"   Синтетический счет " + trim( Balance ), tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep, " Ценная бумага ", tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep, "   наименование        "+ fininstr.rec.Name + IIF(QualifiedSecur(avoiriss, fininstr, _endDate), "", " (НКЦБ)"), tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep, "   № гос.регистрации   "+avoiriss.rec.LSIN, tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep, "   эмитент             "+GetPartyName( FI_GetIssuerOnDate(fininstr, endDate ) , true), tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   if( FI_IsBond(fininstr.rec.FIID) )
     DP_AddPrintCell( Rep,  "   вид: " + avrkName+", №выпуска/транша: " + avoiriss.rec.Issue + "/" + avoiriss.rec.Tranche, tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   else
     DP_AddPrintCell( Rep,  "   вид: " + avrkName, tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   end;
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "   номинал: " + ЧислоCЗаданнойТочностью(GetFaceValue(fininstr.rec.FIID, endDate), fininstr.rec.Point+2, format_str ) + 
                     ", валюта: " + GetFICode(fininstr.rec.FaceValueFI, null, FICK_ISOSTRING), tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "   дата выпуска: " + string(fininstr.rec.Issued:f), tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddEmptyStr();
   DP_AddPrintCell( Rep,  " Дата закрытия "+string(date_cl), tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "                              Ведомость оборотов", tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
end;


macro TableFooter(acc, DebetSum, CreditSum, beginDate, is_ap)
   var RestIn:Money = 0;
   var account, query;
   var t_point = AVOIRISS_SUM_PRECISION;   
   var RestIn_str;
   var Select;

   t_point = fininstr.rec.SumPrecision;
  
   if( ( DebetSum  != 0 ) or ( CreditSum != 0 ) )
      DP_AddPrintCell( Rep,  "Итоги по обороту", 0, 0, "l:" + RepFontStyleTitel);
      DP_AddPrintCell( Rep,  "", 0, 0, "l:" + RepFontStyleTitel);
      DP_AddPrintCell( Rep, DebetSum, 0, DP_GetPrecision(DebetSum, t_point), "r:" + RepFontStyleTitel, is_ap );
      DP_AddPrintCell( Rep, CreditSum, 0, DP_GetPrecision(CreditSum, t_point), "r:" + RepFontStyleTitel, is_ap );
      DP_AddPrintCell( Rep,  "", 0, 0, "l:" + RepFontStyleTitel);
      Rep.AddStr();
   else /* небыло движения по счетам */
      query =   " select sum(rsb_account.restac( t_Account, t_Code_Currency, ?, t_Chapter, null )) as restin "  /*1*/
              + " from daccount_dbt "
              + " where  t_account = ? "/*2*/
              + "   and  t_Chapter = " + Depo_chapt
              + "   and  t_Code_Currency = ? "/*3*/;


      Select = RSDCommand( query  );

      Select.AddParam("BeginDate", RSDBP_IN, beginDate ); /*1*/
      Select.AddParam("Account",   RSDBP_IN, acc.Account );           /*2*/
      Select.AddParam("FIID",  RSDBP_IN, acc.Code_Currency );     /*3*/

      Select.Execute();
                
      account = TRsbDataSet(Select);
      if( account.MoveNext() )
        if( ValType(account.restin ) != V_UNDEF )
          RestIn = Abs(account.restin);
        end;
      end;
      DP_AddPrintCell( Rep,  "", 0, 0, "l:" + RepFontStyleTitel, is_ap);
      DP_AddPrintCell(Rep, RestIn, 0, DP_GetPrecision(RestIn, t_point), "r:" + RepFontStyleTitel, is_ap );
      DP_AddPrintCell( Rep,  "", 0, 0, "l:" + RepFontStyleTitel, is_ap);
      DP_AddPrintCell( Rep,  "", 0, 0, "l:" + RepFontStyleTitel, is_ap);
      DP_AddPrintCell(Rep, RestIn, 0, DP_GetPrecision(RestIn, t_point), "r:" + RepFontStyleTitel, is_ap );
      Rep.AddStr();
      DP_AddPrintCell( Rep,  " За данный период бухгалтерских операций по счету не проводилось.", Rep.GetColWidthTable(0)+Rep.GetColWidthTable(1)+Rep.GetColWidthTable(2)+Rep.GetColWidthTable(3)+Rep.GetColWidthTable(4)+4, 0, "l:" + RepFontStyleTitel, is_ap );
      Rep.AddStr();
   end;
end;


/* Печать оборотов лицевого счета за 1 день */
macro PrintTurnover(Debet, Credit, RestIn, RestOut, TmpReportDate, is_ap)
   var  RestIn_str, Debet_str, Credit_str, RestOut_str;
   var  t_point = AVOIRISS_SUM_PRECISION, format_str;

   t_point = fininstr.rec.SumPrecision;
  
   if((Debet == 0) and (Credit == 0))
      return;
   end;
   
   DP_AddPrintCell( Rep,  TmpReportDate, 0, 0, "l:f" + RepFontStyleTitel);
   DP_AddPrintCell(Rep, RestIn, 0, DP_GetPrecision(RestIn, t_point), "r:" + RepFontStyleTitel, is_ap );
   DP_AddPrintCell(Rep, Debet, 0, DP_GetPrecision(Debet, t_point), "r:" + RepFontStyleTitel, is_ap );
   DP_AddPrintCell(Rep, Credit, 0, DP_GetPrecision(Credit, t_point), "r:" + RepFontStyleTitel, is_ap );
   DP_AddPrintCell(Rep, RestOut, 0, DP_GetPrecision(RestOut, t_point), "r:" + RepFontStyleTitel, is_ap );
   Rep.AddStr();
end;

/*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-*/

macro DAccountReport(acc, beginDate, endDate, CountRep, is_ap, onlyPerAccWTrans)

   var DebetSum  = 0,
       CreditSum = 0;
   var Debet     = 0,
       Credit    = 0,
       RestIn    = 0,
       RestOut   = 0;

   var TmpReportDate = beginDate;
   var stat = 0;
   var Select, Select1;

   var MakePrint = true;

   var account;
      
   //нужно посмотреть сначала в архивах
   var acctrn;
   var acctrnRecCount = 0;
   var acctrnIsEmpty = false;
   //выберем только записи с данным счетом
   var query =   " select distinct(t_Date_Carry) from dacctrn_dbt "
               + " where (    t_account_payer = ? "  /*1*/
               + "         or t_account_receiver = ? ) " /*2*/
               + "   and  t_Chapter = " + Depo_chapt
               + "   and  t_FIID_Payer = ? "  /*3*/
               + "   and  ABS(t_Sum_Payer) > 0"
               + "   and  t_Date_Carry >= ? " /*4*/
               + "   and  t_Date_Carry <= ? " /*5*/
               + "   and  t_result_carry != " + ARHCARRY
               + "   and  t_State = 1 " /*фактическая проводка*/
               + " ORDER BY t_Date_Carry";
      
   Select = RSDCommand( query );

   Select.AddParam("Account_Payer",     RSDBP_IN, acc.Account );     /*1*/
   Select.AddParam("Account_Receiver",  RSDBP_IN, acc.Account );     /*2*/
   Select.AddParam("FIID",              RSDBP_IN, acc.Code_Currency );  /*3*/
   Select.AddParam("BeginDate",         RSDBP_IN, beginDate );       /*4*/
   Select.AddParam("EndDate",           RSDBP_IN, endDate );         /*5*/

   Select.Execute();

   acctrn = TRsbDataSet(Select);
   if (not(acctrn.MoveNext()))
      acctrnIsEmpty = true;

      if (onlyPerAccWTrans)
         MakePrint = false;
      end;
   end;

   if (MakePrint)
      TablHeader(acc.Account, acc.Balance, acc.Code_Currency, acc.Close_Date, acc.Open_Close, CountRep, is_ap, endDate);
   end;

   while(not(acctrnIsEmpty))

      //в одном запросе выгребем сразу все остатки и кредит и дебет
      query =   " select ABS(sum(rsb_account.restac( t_Account, t_Code_Currency, ?, t_Chapter, null ))) as restin, "
              + "        ABS(sum(rsb_account.restac( t_Account, t_Code_Currency, ?, t_Chapter, null ))) as restout, "
              + "        sum(rsb_account.debetac( t_Account, t_Chapter, t_Code_Currency, ?, ?  )) as debet, "
              + "        sum(rsb_account.kreditac( t_Account, t_Chapter, t_Code_Currency, ?, ?  )) as kredit "
              + " from daccount_dbt "
              + " where  t_account = ? "  /*7*/
              + "   and  t_Chapter = " + Depo_chapt
              + "   and  t_Code_Currency = ?"; /*8*/

      Select1 = RSDCommand( query );

      Select1.AddParam("DateCarry1", RSDBP_IN, date(acctrn.Date_Carry) - 1 ); /*1*/
      Select1.AddParam("DateCarry2", RSDBP_IN, date(acctrn.Date_Carry) );     /*2*/
      Select1.AddParam("DateCarry3", RSDBP_IN, date(acctrn.Date_Carry) );     /*3*/
      Select1.AddParam("DateCarry4", RSDBP_IN, date(acctrn.Date_Carry) );     /*4*/
      Select1.AddParam("DateCarry5", RSDBP_IN, date(acctrn.Date_Carry) );     /*5*/
      Select1.AddParam("DateCarry6", RSDBP_IN, date(acctrn.Date_Carry) );     /*6*/

      Select1.AddParam("Account", RSDBP_IN, acc.Account );     /*7*/
      Select1.AddParam("FIID", RSDBP_IN, acc.Code_Currency );     /*8*/

      Select1.Execute();

      account = TRsbDataSet(Select1);
      if( account.MoveNext() )
        if( ValType(account.restin ) != V_UNDEF )
          RestIn = account.restin;
        end;
        if( ValType(account.restout ) != V_UNDEF )
          RestOut = account.restout;
        end;
        if( ValType(account.debet ) != V_UNDEF )
          Debet = account.debet;
        end;
        if( ValType(account.kredit ) != V_UNDEF )
          Credit = account.kredit;
        end;
      end;

      PrintTurnover(Debet, Credit, RestIn, RestOut, date(acctrn.Date_Carry), is_ap);
      DebetSum  = DebetSum  + Debet;                                              
      CreditSum = CreditSum + Credit;

      RestIn = 0;
      RestOut = 0;
      Debet  = 0;  
      Credit = 0;

      if (not(acctrn.MoveNext()))
         acctrnIsEmpty = true;
      end;
   end;

   if (MakePrint)
      TableFooter(acc, DebetSum, CreditSum, beginDate, is_ap);
      DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);
   end;
end; /*DAccountReport*/

private macro CreateRep(IsEmpty)

   var stat;
   var account;
   var query = "";
   var count = 0;
   var i = 0;
   var nrec = 0;
   var Progress = CProgressBar;
   
   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox
   
   if( _depoacc_status == OPEN_STATUS )
      Status_ACC = OPEN_ACC;
   elif( _depoacc_status == CLOSE_STATUS )
      Status_ACC = CLOSE_ACC;
   elif( _depoacc_status == ALL_STATUS )
      Status_ACC = ALL_ACC;
   end;

   query = " select t_Account, t_Balance, t_Code_Currency, t_Close_Date, t_Open_Close from daccount_dbt "
         + " where t_Chapter = " + Depo_chapt
         + " and t_Open_Date <= " + GetSQLDate(_endDate);

   if(_department > 0)
     query = query + " and t_Department = " + _department;
         end;
   if(_acc_client != UNDEF_ID)
     query = query + " and t_Client = " + _acc_client;
   end;
   if(Status_ACC != ALL_ACC)
     if(Status_ACC == CLOSE_ACC)
       query = query + " and t_Open_Close = 'З' ";
     elif( Status_ACC == OPEN_ACC )
       query = query + " and t_Open_Close = CHR(0) ";
      end;
   end;

   if( _DAcnt > 0 )
     query = query + " and t_DepoRoot = " + _DAcnt;
   end;

   if( _DPart > 0 )
     query = query + " and t_DepoAcc = " + _DPart;
   end;

   if( _daccount != "" )
     query = query + " and t_Account = '"+_daccount+"' ";
      end;

   account = TRsbDataSet( query, RSDVAL_CLIENT, RSDVAL_STATIC);
   account.MoveLast();
   nrec = account.GetRecCount();
   stat = account.MoveFirst(); 

   Progress.Init(nrec, "Просмотр лицевых счетов ...", "Выпуск отчета");
   while(stat )
      DAccountReport(account, _beginDate, _endDate, count = count+1, _is_ap, _onlyPerAccWTrans);
      i = Progress.Use();
      stat = account.MoveNext();
   end;
   Progress.Remove();

   if( not DataPrinted )/* нет данных для отчета */
      nrec = 0;
   end;

   if(IsEmpty)
     nrec = 1;
     DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Журнал оборотов лицевого счета депо", _LogOprID, 0, false, _beginDate, _endDate, tablewidth );
     Rep.AddEmptyStr();
     DP_AddPrintCell( Rep,  "Нет данных, удовлетворяющих параметрам отчета.", tablewidth, 0, "l:" + RepFontStyleTitel, true, REP_ELEM_STR );
     Rep.AddEmptyStr();
     DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);
   end;

   DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
   DP_EndProtocol();                 //закончить протоколирование

   return nrec;
end;

private macro CreateEmptyRep()

  return CreateRep(true);
end;

/*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-*/

/*=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-*/

MACRO printReport( LogOprID,
                   department,
                   beginDate, endDate,
                   DAcnt, DPart,
                   daccount,
                   acc_client, 
                   depoacc_status,
                   ToExcel,
                   is_ap,
                   onlyPerAccWTrans )

   _beginDate         = beginDate;
   _endDate           = endDate;
   _LogOprID          = LogOprID;
   _daccount          = daccount;
   _department        = department;
   _DAcnt             = DAcnt;
   _DPart             = DPart;
   _acc_client        = acc_client;
   _depoacc_status    = depoacc_status;
   _ToExcel           = ToExcel;
   _is_ap             = is_ap;
   _onlyPerAccWTrans  = onlyPerAccWTrans;
   Dl_CallInsertStat( IIF(  _ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
   return DP_StdReportControl( _LogOprID, CreateRep(false), @CreateEmptyRep, 0, ToExcel, Rep );
END;
