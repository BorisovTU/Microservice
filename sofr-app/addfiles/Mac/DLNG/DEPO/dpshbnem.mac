/* -@H------------------------------------------------------------------
*            Автоматизированная банковская система RS-Bank
*                 Библиотека интерпретируемых модулей
*                  Copyright (c) JV R-Style 1993-96
*
*   Description      :  Плата за хранение безналичных неэмиссионных ценных бумаг
*
*----------------------------------------------------------------------*/

import FIInter, "dlcnst.inc", "dps_com.inc";

/*фильтр*/
macro AvoirFiltr(FIID)
   return ИндивидуальныйФинИн(FIID);  /*неэмиссионные*/
end;

/* добавление суммы ПЗО */
macro AddBasObj( DealSum, FIID )
    record  rBaseSum( "sfbassum.str" );

    rBaseSum.baseType    = SF_BASETYPE_SUM;
    rBaseSum.baseType2   = SF_BASETYPE_SUM;
    rBaseSum.baseSum     = DealSum;
    rBaseSum.baseSum2    = DealSum;

    rBaseSum.BaseObjectType = OBJTYPE_AVOIRISS;
    rBaseSum.BaseObjectID   = FIID;

    if( InsertSumList(rBaseSum) )
       MsgBox("Ошибка при вставке базовой суммы");
       return false;
    end;

    return true;
end;

/* Получить базовую сумму для расчета периодической комиссии  
  sfcontr_addr - Договор обслуживания
  BeginDate    - Начало периода
  EndDate      - Конец периода*/
macro CalcServiceSum( sfcontr_addr, BeginDate, EndDate )
    file  depoacnt("depoacnt") key 1;
    record  rContr( sfcontr );
    var     DealSum = $0, stat=0, AinPEnabled; 

    if( GetAinPAccState( AinPEnabled ) )
      if( AinPEnabled ) /* Если включен актив в пассиве, эта комиссия может работать */

        SetBuff( rContr, sfcontr_addr );
        depoacnt.Code = rContr.Object; 
        if(not GetEQ(depoacnt))
           MsgBox("Не найден счет депо ", rContr.Object);
           MacroError = 1;
           return $0;
        else
           CalcStoringAvoir(depoacnt.AutoKey, BeginDate, EndDate, @AvoirFiltr, false, @AddBasObj);
        end;
        return ;
      else
        MacroError = 1;
        msgbox("Учет актива в пассиве не велся");
      end;
    else
      MacroError = 1;
    end;
end;
