/*
$Name:         sptr_26.mac
$Module:       Депозитарий
$Description:  Междепозитарный перевод эмиссионных ценных бумаг - подтверждение операции
*/

import OprInter, PTInter, InsCarryDoc, PaymInter, opr_depo;

/* Буферы ПД, заполняемые из операции */
record Pm_paym( pmpaym );     /*основной платеж*/
record Pm_prop_deb( pmprop ); /*св-ва платежа банка-плательщика*/
record Pm_prop( pmprop );     /*св-ва платежа*/
record Pm_rmprop( pmrmprop ); /*платеж по ценным бумагам*/

private const DP_NOTFINDREPORT = 0;
private const DP_INFREPORT_PREPARE = 300; //отчет об исполнении инв. операции
private const DP_INFREPORT_REJECT  = 301; //уведомление об отказе в исполнении

macro ExecuteStep( d, spdraft )
  var err = 0;
  var stat = DP_NOTFINDREPORT;
  var RejectNote = "";
  var SPgroundID = 0;
  var FactValueDate = date(0,0,0), Transport, InstrStatus;

  if( DP_CheckRejectedDepoDraft( spdraft, Pm_paym, err ) ) return err; end;

  record spdraft_buf( spdraft );

  SetBuff( spdraft_buf, spdraft );

  if(spdraft_buf.WaitConfirmation == SET_CHAR)
    err = GetTransport(Transport);

    if(not err)
      if(DP_SpdraftIsBothDepart(spdraft_buf)) // МФР
        stat = ExecMacroFile("opr_depo.mac", "IsReportFromParentDprt", spdraft_buf.AutoKey, SPgroundID, RejectNote);

        if( stat == DP_NOTFINDREPORT )
          MsgBox("Межфилиальное поручение еще не получило подтверждение на выполнение из другого филиала.");
          err = DEPOS_ERROR_FIRST;
        elif( stat == DP_INFREPORT_REJECT )
          //если нашли уведомление об отказе, то регистрируем отказ
          err = DP_ShowSPGRPanelByID( SPgroundID );
          if( not err )
            DPError.SetErr(RejectNote);
            err = 1;
          else
            err = DEPOS_ERROR_FIRST;
          end;
        elif(stat == DP_INFREPORT_PREPARE)
          err = DP_ShowSPGRPanelByID( SPgroundID );
          if( err )
            err = DEPOS_ERROR_FIRST;
          end; 
        end;
      elif(Transport == DPTRANSP_MBR) // МБР
        /* если нет сформированного сообщения, то не подтверждаем */
        if( not ExecMacroFile("opr_depo.mac", "FindDpMessageByDraft", spdraft_buf.AutoKey ) )
          return err;
        end;

        err = DpMessageConfirm(spdraft_buf.AutoKey);
        if( not err ) 
          if( ExecMacroFile("opr_depo.mac", "FindDpMessageKvitByDraft", spdraft_buf.AutoKey) != DPMSGINF_STATE_EXEC )
            if( not gettrue(false, "Сообщение не сквитовано. Продолжить операцию?") )
              err = DEPOS_ERROR_FIRST;
            end;
          end;
        end;
      else // RS-payments
        err = GetConfirmParams(RSPMDK_INVDRAFT, spdraft_buf.AutoKey, NULL, @FactValueDate, @InstrStatus);

        if(NOT err)
          if(InstrStatus == 1)
            DPError.SetErr("В исполнении инструкции вышестоящим депозитарием отказано");
            err = 1;
          elif(InstrStatus > 1) /* Подтверждение еще не пришло, есть только статусы */
            err = 0;
            if( not gettrue(false, "Сообщение не сквитовано. Продолжить операцию?") )
              err = DEPOS_ERROR_FIRST;
            end;
          end;
          if( ExecMacroFile("opr_depo.mac", "GetDealID", spdraft_buf.AutoKey)>0) // если из БОЦБ, то обновим запись в таблице сообщений
            var KindMsg = "";
            DP_ConfirmPaymentsMessage(1, spdraft_buf.AutoKey, KindMsg, {Oper}, {NumDprt}, spdraft_buf.Kind);
          end;
        elif(err == 1) /* если нет сформированного сообщения, то не подтверждаем */
          err = 0;
        else /* Подтверждение еще не пришло */
          err = 0;
          if( not gettrue(false, "Сообщение не сквитовано. Продолжить операцию?") )
            err = DEPOS_ERROR_FIRST;
          end;
        end;
      end;
    end;
  end;

  if(not err)
    err = ExecConfirmSpdraft(spdraft_buf, Pm_paym, FactValueDate);
  end;

  if( err and (err != DEPOS_ERROR_FIRST) )
    err = RejectDepoDraftYesNo( spdraft, DP_SPDRAFT_OPERATION );
  end;

  return err;
end;
