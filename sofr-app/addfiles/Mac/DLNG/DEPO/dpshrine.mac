/* -@H------------------------------------------------------------------
*            Автоматизированная банковская система RS-Bank
*                 Библиотека интерпретируемых модулей
*                  Copyright (c) JV R-Style 1993-96
*
*   Description      :     Приём на открытое хранение документарных ц\б
*----------------------------------------------------------------------*/

import oprinter, SPInter, PaymInter, "dps_com.inc", "dp_util.mac";

file pmpaym("pmpaym") key 10;

macro InInterDepo(DACNTCode, ДатаНачала, ДатаКонца)
   var stat=0, NumCert=0, BnAccount = "";
   record  rBaseSum( "sfbassum.str" );

   /*Приём на открытое хранение документарных ц\б*/
   pmpaym.DocKind     = SP_DEPOPER_RECORDER;   /*прием*/
   pmpaym.ValueDate   = ДатаНачала;
   pmpaym.Purpose     = PurpBase;
   pmpaym.SubPurpose  = 0;
   stat = GetGE(pmpaym);
   while(  stat and
          (pmpaym.DocKind   == SP_DEPOPER_RECORDER) and
          (pmpaym.ValueDate <= ДатаКонца)
        )
      if(pmpaym.PaymStatus == PM_FINISHED)
         BnAccount = GetHeadAccForAcc(pmpaym.ReceiverDpNode);
         if( (pmpaym.Purpose == PurpBase) and 
             (not pmpaym.IndoorStorage) and    /*открытое хранение*/
             (BnAccount == DACNTCode )     /*счет клиента на который приняты сертификаты*/
           )
            NumCert = NumCert + 1; /*считаем поручения*/

            rBaseSum.baseType    = SF_BASETYPE_QUONT;
            rBaseSum.baseType2   = SF_BASETYPE_QUONT;
            rBaseSum.baseQuont   = 1;
            rBaseSum.baseQuont2  = 1;

            rBaseSum.BaseObjectType = OBJTYPE_OPERATIONDEPO;
            rBaseSum.BaseObjectID   = GetSpdraftByPayment(pmpaym.PaymentID);

            if( InsertSumList(rBaseSum) )
               MacroError = 1;
               MsgBox("Ошибка при вставке базовой суммы");
            end;

         end;
      end;
      stat = Next(pmpaym);
   end;
   return NumCert;
end;

macro CalcServiceSum( sfcontr_addr, BeginDate, EndDate )
   record  rContr( sfcontr );
   file depoacnt(depoacnt) key 1;
   SetBuff( rContr, sfcontr_addr );
   InInterDepo(rContr.Object, BeginDate, EndDate);
   return;
end;
