/**
 @file 		mac\dlng\depo\nsdImpRests.mac
 @brief 	Отчет "Сверка остатков с НРД"

 # tag
 - functional_block: Отчетность
 - code_type: Report
 - code_type:GUI

 #menu 
  - БО ЦБ\ КДУ \ Сверка остатков с НРД

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |19.11.2025 |Велигжанин А.В.|DEF-111425                                      |Доработка в CNSDProtocol.Print(), использовать T=M (преобразование в Money)
 |02.07.2025 |Велигжанин А.В.|DEF-92764                                       |Автоматизация отчета для нескольких папок
 |17.06.2025 |Велигжанин А.В.|DEF-92764                                       |Адаптация отчета на POI (через T_Poi_Rep)
*/
import BankInter, PTInter, CTInter, DpInter, rsexts;
import dlquery, DlReport_h;
import "dpimp.mac", secinter, globals, dprepsec, "rsberror.mac";
import "qracctools.mac";
import rsexts;
import "scsrvrepfun.mac", "dl_util_poi_obj.mac"; // DEF-92764


private file reporth() dbf; // в класс не запихнуть
private file hdsttds() dbf;  // в класс не запихнуть
private file sttds() dbf;  // в класс не запихнуть

private var repDepoACC = "";

// DEF-92764
var x_SrcDir: string = "$C:\\tmp\\depo\\nsd";

PRIVATE MACRO VerifyDir( p_Path )
  var x_FoundDir: integer = ExistDir( p_Path );
  IF(x_FoundDir == 0 )
    return true;
  ELIF(x_FoundDir == 1)
    return false;
  ELIF(MakeDir(p_Path))
    return true;
  ELSE
    return false;
  END;
END;

//создание локальной директории
PRIVATE macro MoveNSDData( p_SrcDir, p_DestDir )
  IF(VerifyDir( p_DestDir ))
    var tdl = tDirList;
    var td2 = tDirList(p_DestDir + "\\*.*", "f");
    td2.Sort(0);
    td2.Remove(p_DestDir + "\\*.*"); 
    tdl.Copy(p_SrcDir + "\\*.*", "", p_DestDir + "\\*.*", true, false, "Copy");
  END;
END;

// Запись остатка ц/б
private class CNSDRest(DepoPartID, DepoAccNSDCode, DepoPartNSDCode, FIID, NSD_FICODE, LSIN, ISIN, Amount, FIName, SumPrecision, NsdAmount)
  if (ValType(NsdAmount)==V_UNDEF)
    NsdAmount=$0.0;
  end;
  var m_DepoPartID = DepoPartID; // ИД раздела счета депо
  var m_FIID = FIID; // ИД ФИ
  var m_LSIN = LSIN; // № гос. рег.
  var m_ISIN = ISIN; // ISIN
  var m_Amount = Amount; // Количество ц/б

  var m_DepoAccNSDCode = DepoAccNSDCode; // Код счета в НРД
  var m_DepoPartNSDCode = DepoPartNSDCode; // Код раздела в НРД
  var m_NSD_FICODE = NSD_FICODE; // Код ц/б в НРД
  var m_NSD_Amount = NsdAmount; // Остаток в НРД
  var m_NSD_FINAME = FIName;

  var m_SumPrecision = SumPrecision;
end;

// Запись ошибки
private class CNSDProtocolStr(DepoAcc, DepoPart, FI_Code, LSIN, ISIN, FI_Name, Reason)
  var m_DepoAcc  = DepoAcc;
  var m_DepoPart = DepoPart;
  var m_FI_Code  = FI_Code;
  var m_LSIN     = LSIN;
  var m_ISIN     = ISIN;
  var m_FI_Name  = FI_Name;
  var m_Reason   = Reason;
end;

// Протокол процедуры сверки
private class CNSDProtocol(Reg_No:string, End_Date:date)
  var m_ErrArr = TArray();
  var m_TotalCnt = 0;
  var m_Reg_No = Reg_No;
  var m_End_Date = End_Date;

  // DEF-92764
  macro SetCount(cnt)
    m_TotalCnt = cnt;
  end;

  macro SetError(DepoAcc, DepoPart, FI_Code, LSIN, ISIN, FI_Name, Reason)
    m_ErrArr[m_ErrArr.size] = CNSDProtocolStr(DepoAcc, DepoPart, FI_Code, LSIN, ISIN, FI_Name, Reason);
  end;

  macro GetCntErr()
    return m_ErrArr.size;
  end;

  // DEF-92764
  macro GetHeader()
    var Header = "Протокол сверки остатков ценных бумаг в НРД (";

    if(hdsttds.ON_FINISH_ == "Y")
      Header = Header + "На конец операционного дня";
    else
      Header = Header + "Операционный день НРД не закрыт!";
    end;
    Header = Header + ")";
    return Header; 
  end;

  private macro GetMisQuery()
    return " select tmp.*, "
              + "        (select Count(1) "
              + "           from ddpnsdrep_tmp tmp1 "
              + "          where tmp1.t_depoaccnsdcode = tmp.t_depoaccnsdcode "
              + "            and tmp1.t_depopartnsdcode = tmp.t_depopartnsdcode "
              + "            and tmp1.t_isin = tmp.t_isin "
              + "            and tmp1.t_lsin = tmp.t_lsin "
              + "        ) as cnt "
              + "   from ddpnsdrep_tmp tmp "
              + "  where tmp.t_Amount <> (select SUM(tmp2.t_NSDAmount) "
              + "                           from ddpnsdrep_tmp tmp2 "
              + "                          where tmp2.t_depoaccnsdcode = tmp.t_depoaccnsdcode "
              + "                            and tmp2.t_depopartnsdcode = tmp.t_depopartnsdcode "
              + "                            and tmp2.t_isin = tmp.t_isin "
              + "                            and tmp2.t_lsin = tmp.t_lsin "
              + "                        ) "
              + " order by tmp.t_depoaccnsdcode, tmp.t_depopartnsdcode, tmp.t_FIID, tmp.t_isin, tmp.t_lsin, tmp.t_nsd_code "
    ;
  end;

  macro GetAmount( p_Cnt, p_Amount )
    if(p_Cnt > 1)
      return $0;
    else
      return p_Amount;
    end;
  end;

  macro Print(MisNum : integer)
    var CreateOnTerm = (not isStandAlone());
    var query, cmd, DataSet, cnt = 0, mis = 0, i;
    var SumNSDAmount = $0;
    var Amount = $0;
    var diff = $0;
    PRIVATE VAR m_PoiRep = T_Poi_Rep();

    m_PoiRep.Init("utf8", true);

    m_PoiRep.Set_Column_Width(20, 60, 120, 120, 120, 140, 200, 120, 120, 120);

    m_PoiRep.Set_Landscape();
    m_PoiRep.Set_Indent(0);

    m_PoiRep.Print_Header("Отчет", "Calibri", 9);

    m_PoiRep.Set_WrapText(0);

    m_PoiRep.Put_Str(GetHeader(), "START;HA=C;FB;R=9;END;"); 
    m_PoiRep.Put_Val("Номер отчёта НРД:", "START;FB;R=2;HA=L;");
    m_PoiRep.Put_Val(m_Reg_No, "FB;R=6;HA=L;END;");
    m_PoiRep.Put_Val("Остатки за дату:", "START;FB;R=2;HA=L;");
    m_PoiRep.Put_Val(string(m_End_Date), "FB;R=6;HA=L;END;");
    m_PoiRep.Put_Val(" по счету Депо:  ", "START;FB;R=2;HA=L;");
    m_PoiRep.Put_Val(repDepoACC, "FB;R=6;HA=L;END;");
    m_PoiRep.Blank_Line();
    m_PoiRep.Put_Val(" Всего обработано записей:", "START;FB;R=2;HA=L;");
    m_PoiRep.Put_Val(m_TotalCnt, "FB;R=6;HA=L;END;");
    m_PoiRep.Put_Val(" Найдено несоответствий:", "START;FB;R=2;HA=L;");
    m_PoiRep.Put_Val(MisNum, "FB;R=6;HA=L;END;");

    m_PoiRep.Blank_Line();
    m_PoiRep.Put_Str("Перечень несоответствий:", "START;FB;HA=L;END;"); 
    m_PoiRep.Put_Val("№", "START;HA=C;BOR;FB;D=1;IC=E4DFEC;");
    m_PoiRep.Put_Val("Счёт депо", "HA=C;BOR;FB;D=1;IC=E4DFEC;");
    m_PoiRep.Put_Val("Раздел счёта депо", "HA=C;BOR;FB;D=1;IC=E4DFEC;");
    m_PoiRep.Put_Val("Код ценной бумаги", "HA=C;BOR;FB;D=1;IC=E4DFEC;");
    m_PoiRep.Put_Val("Номер госрегистрации", "HA=C;BOR;FB;D=1;IC=E4DFEC;");
    m_PoiRep.Put_Val("ISIN", "HA=C;BOR;FB;D=1;IC=E4DFEC;");
    m_PoiRep.Put_Val("Краткое наименование ц/б", "HA=C;BOR;FB;D=1;IC=E4DFEC;");
    m_PoiRep.Put_Val("Количество ц/б в шт.", "HA=C;BOR;FB;R=2;IC=E4DFEC;END;");
    m_PoiRep.Put_Val("НРД", "START;I=7;HA=C;BOR;FB;IC=E4DFEC;");
    m_PoiRep.Put_Val("Система", "I=8;HA=C;BOR;FB;IC=E4DFEC;");
    m_PoiRep.Put_Val("Разница", "I=9;HA=C;BOR;FB;IC=E4DFEC;END;");

    if(MisNum)
      query = GetMisQuery();
      cmd = DL_RSDCommand(query);
      DataSet = cmd.Execute(); 

      cnt = 0;
      while(DataSet.moveNext())
          mis = mis + 1;
          m_PoiRep.Put_Val(string(mis), "START;BOR;HA=L;" );
          m_PoiRep.Put_Val(DataSet.DepoAccNSDCode, "BOR;HA=L;" );
          m_PoiRep.Put_Val(DataSet.DepoPartNSDCode, "BOR;HA=L;" );
          m_PoiRep.Put_Val(DataSet.NSD_CODE, "BOR;HA=L;" );
          m_PoiRep.Put_Val(DataSet.LSIN, "BOR;HA=L;" );
          m_PoiRep.Put_Val(DataSet.ISIN, "BOR;HA=L;" );
          m_PoiRep.Put_Val(DataSet.FI_NAME, "BOR;HA=L;" );
          m_PoiRep.Put_Val(DataSet.NSDAmount, "BOR;HA=R;"+"T=M"+string(DP_GetPrecision(DataSet.NSDAmount, DataSet.SumPrecision))+";" );
          Amount = GetAmount(DataSet.cnt, DataSet.Amount);  
          m_PoiRep.Put_Val(Amount, "BOR;HA=R;"+"T=M"+string(DP_GetPrecision(Amount, DataSet.SumPrecision))+";" );
          diff = GetAmount(DataSet.cnt, DataSet.NSDAmount - DataSet.Amount);
          m_PoiRep.Put_Val(diff, "BOR;HA=R;"+"T=M"+string(DP_GetPrecision(diff, DataSet.SumPrecision))+";END;" );
          cnt = cnt + 1;
          SumNSDAmount = SumNSDAmount + DataSet.NSDAmount;

          if(cnt == DataSet.cnt)
            if(DataSet.cnt > 1)
              diff = SumNSDAmount - DataSet.Amount;
              m_PoiRep.Put_Val("ИТОГО", "START;BOR;R=6;HA=R;" );
              m_PoiRep.Put_Val(SumNSDAmount, "BOR;HA=R;"+"T=M"+string(DP_GetPrecision(SumNSDAmount, DataSet.SumPrecision))+";" );
              m_PoiRep.Put_Val(DataSet.Amount, "BOR;HA=R;"+"T=M"+string(DP_GetPrecision(DataSet.Amount, DataSet.SumPrecision))+";" );
              m_PoiRep.Put_Val(diff, "BOR;HA=R;"+"T=M"+string(DP_GetPrecision(diff, DataSet.SumPrecision))+";END;" );
            end;

            cnt = 0;
            SumNSDAmount = $0;
          end;
      end; // while
    end; // if

    if(m_ErrArr.size)
      m_PoiRep.Set_Column_Width(20, 60, 120, 120, 120, 140, 200, 240);
      m_PoiRep.Print_Header("Ошибки процедуры сверки", "Calibri", 9, true);
      m_PoiRep.Put_Str("Ошибки процедуры сверки:", "START;HA=L;FB;R=9;END;"); 
      m_PoiRep.Put_Val("№", "START;HA=C;BOR;FB;IC=E4DFEC;");
      m_PoiRep.Put_Val("Счёт депо", "HA=C;BOR;FB;IC=E4DFEC;");
      m_PoiRep.Put_Val("Раздел счёта депо", "HA=C;BOR;FB;IC=E4DFEC;");
      m_PoiRep.Put_Val("Код ценной бумаги", "HA=C;BOR;FB;IC=E4DFEC;");
      m_PoiRep.Put_Val("Номер госрегистрации", "HA=C;BOR;FB;IC=E4DFEC;");
      m_PoiRep.Put_Val("ISIN", "HA=C;BOR;FB;IC=E4DFEC;");
      m_PoiRep.Put_Val("Краткое наименование ц/б", "HA=C;BOR;FB;IC=E4DFEC;");
      m_PoiRep.Put_Val("Описание ошибки", "HA=C;BOR;FB;IC=E4DFEC;END;");

      i = 0;
      while(i < m_ErrArr.size)
        m_PoiRep.Put_Val(string(i+1), "START;POIH=30;BOR;HA=C;" );
        m_PoiRep.Put_Val(m_ErrArr[i].m_DepoAcc, "BOR;HA=L;" );
        m_PoiRep.Put_Val(m_ErrArr[i].m_DepoPart, "BOR;HA=L;" );
        m_PoiRep.Put_Val(m_ErrArr[i].m_FI_Code, "BOR;HA=L;" );
        m_PoiRep.Put_Val(m_ErrArr[i].m_LSIN, "BOR;HA=L;" );
        m_PoiRep.Put_Val(m_ErrArr[i].m_ISIN, "BOR;HA=L;" );
        m_PoiRep.Put_Val(m_ErrArr[i].m_FI_Name, "BOR;HA=L;" );
        m_PoiRep.Put_Val(m_ErrArr[i].m_Reason, "BOR;HA=L;WT;END;" );
        i = i + 1;
      end; // while
    end; // if 

    var m_CurrentRepName = "nsdImpRest_"+repDepoACC+"_"+string(m_End_Date)+".xlsx";
    var brokFileManager = BrokerRepFileStatusManager(m_CurrentRepName, CreateOnTerm);
    m_PoiRep.Save(brokFileManager.Get_GENERATED_REPORTS_path());
    brokFileManager.ShowFile();
    
  end; // macro Print()

end; // class CNSDProtocol

class CNSDImportRests()
  private var path:string = ""; // Путь до папки импорта с завершающим \\
  private var protocol = NULL;
  private var ImpDate, ImpNo;
  private var isDepo, isKDU;

  private macro GetStorageAcnt(NSD_AcntCode:string, StorageAcnt)
    var cmd, query, DataSet;

    StorageAcnt.Clear();

    query =   "select root.* "
            + "  from ddepoacnt_dbt root "
            + " where root.t_Kind = 1 /*активный*/"
            + "   and root.t_Department = ? "
            + "   and RTRIM(rsb_struct.getString(rsi_rsb_kernel.GetNote(112, TO_CHAR(root.t_AutoKey), 2, ?)), CHR(0)) = ? ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam({OperDprt});
    cmd.AddParam(date({curdate}));
    cmd.AddParam(NSD_AcntCode);

    DataSet = cmd.Execute();

    if(DataSet.moveNext())
      DataSet.GetRecord().CopyTo( StorageAcnt.rec );
    end;
  end;

  private macro error(errstr)
    protocol.SetError(trim(hdsttds.DEPO_ACC_C),
                      trim(sttds.SECTION_CO),
                      trim(sttds.SECURITY_C),
                      trim(ToOEM(sttds.STATE_REGI,true)),
                      trim(sttds.ISIN_CODE),
                      trim(ToOEM(sttds.SECURITY_S,true)),
                      errstr);
  end;

  private macro full_error(depoacc,depopart,fi_code,lsin,isin,shortName,errstr)
    protocol.SetError(depoacc,
                      depopart,
                      fi_code,
                      lsin,
                      isin,
                      shortName,
                      errstr);
  end;

  private macro text_error(errstr)
    protocol.SetError("",
                      "",
                      "",
                      "",
                      "",
                      "",
                      errstr);
  end;

  private macro getCountRecords(f)
    return NRecords(f);
  end;

  private macro GetFIIDbyCodeNSD(NSD_Code)
    var query, cmd, DataSet;
    var FIID = -1;

    if(NSD_Code != "")
      query =   "select obj.t_ObjectID "
              + "  from dobjcode_dbt obj, davoiriss_dbt av "
              + " where t_ObjectType = " + OBJTYPE_FININSTR
              + "   and obj.t_ObjectID = av.t_fiid"
              + "   and t_CodeKind = " + FICK_NRD
              + "   and T_SPISCLOSED != chr(88)"   //DAN проверка на закрытую бумагу
              + "   and t_Code = ? ";
      cmd = DL_RSDCommand(query);
      cmd.AddParam(NSD_Code);

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        FIID = int(DataSet.ObjectID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIIDbyISIN(ISIN)
    var query, cmd, DataSet;
    var FIID = -1;

    if(ISIN != "")
      query =   " select t_FIID "
              + "   from davoiriss_dbt "
              + "  where t_ISIN = ? "
              + "   and T_SPISCLOSED != chr(88)";  //DAN проверка на закрытую бумагу;

      cmd = DL_RSDCommand(query);
      cmd.AddParam(ISIN);

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        FIID = int(DataSet.FIID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIIDbyLSIN(LSIN)
    var query, cmd, DataSet;
    var FIID = -1;

    if(LSIN != "")
      query =   "select t_FIID "
              + "  from davoiriss_dbt "
              + " where t_LSIN = ? "
              + "   and T_SPISCLOSED != chr(88)";  //DAN проверка на закрытую бумагу;

      cmd = DL_RSDCommand(query);
      cmd.AddParam(LSIN);

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        FIID = int(DataSet.FIID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIID()
    var FIID = -1;

    FIID = GetFIIDbyCodeNSD(string(trim(sttds.SECURITY_C)));
    if(FIID == -1)
      FIID = GetFIIDbyISIN(string(trim(sttds.ISIN_Code)));
      if(FIID == -1)
        FIID = GetFIIDbyLSIN(string(trim(ToOEM(sttds.STATE_REGI,true))));
      end;
    end;

    return FIID;
  end;

  private macro loadRestsQr(NrdPartyId:integer)
    var err = 0;
    var ins, query, DataSet;

    if(not err)
      query = " INSERT INTO DDPNSDREP_TMP (T_ID, T_DEPOACCNSDCODE, T_DEPOPARTNSDCODE, T_FIID, T_NSD_CODE, T_ISIN, T_LSIN, T_FI_NAME, T_SUMPRECISION, T_AMOUNT, T_NSDAMOUNT)" +
       "select 0, scqr.T_DEPOACCCODE , scqr.T_DEPOPARTCODE ,  scqr.T_FIID,"+
              "       obj.t_Code, avoir.t_Isin, avoir.t_Lsin , fininstr.t_name , fininstr.t_SumPrecision,"+
//              "       ROUND(ABS(RSI_RSB_ACCOUNT.RestAC(ac.T_ACCOUNT, ac.T_CODE_CURRENCY, ?, ac.T_CHAPTER, 0)))  "+ 
              "       ROUND(-1*(RSI_RSB_ACCOUNT.RestAC(ac.T_ACCOUNT, ac.T_CODE_CURRENCY, ?, ac.T_CHAPTER, 0))),  "+ 
              "       /*RSB_SECUR.GetQRRestOnDate(scqr.T_QRID, ?)*/ 0"+
              "  from DSCQRACC_DBT scqr, DAVOIRISS_DBT avoir, DFININSTR_DBT fininstr, DACCOUNT_DBT ac, DOBJCODE_DBT obj "+
              " where avoir.t_Fiid(+) = scqr.t_Fiid "+
              "   and fininstr.t_Fiid(+) = scqr.t_Fiid "+
              "   and ac.t_AccountId(+) = scqr.t_AccountId "+
              "   AND obj.t_ObjectType(+) = " + OBJTYPE_FININSTR +
              "   AND obj.t_ObjectID(+) = scqr.T_FIID " +
              "   AND obj.t_CodeKind(+) = "+FICK_NRD+
//              "   and ROUND(ABS(RSI_RSB_ACCOUNT.RestAC(ac.T_ACCOUNT, ac.T_CODE_CURRENCY, ?, ac.T_CHAPTER, 0))) != "+
              "   and ROUND(-1*(RSI_RSB_ACCOUNT.RestAC(ac.T_ACCOUNT, ac.T_CODE_CURRENCY, ?, ac.T_CHAPTER, 0))) != "+
              "       RSB_SECUR.GetQRRestOnDate(scqr.T_QRID, ?) "+
              "   and scqr.t_StorageId = ? and scqr.t_depoacccode = ?";
      
      ins = RSDCommand(query);
      ins.AddParam("", RSDBP_IN, {curdate});
      ins.AddParam("", RSDBP_IN, {curdate});
      ins.AddParam("", RSDBP_IN, {curdate});
      ins.AddParam("", RSDBP_IN, NrdPartyId);
      ins.AddParam("", RSDBP_IN,repDepoACC);


      SQL_Execute(ins);
    end;

    return err;
  end;

  // Загрузить остатки по счету депо из Системы
  private macro loadRestsDepo(NSD_AccountCode, RestDate)
    var err = 0;
    var ins, cmd, query, DataSet;
    var StorageAcnt = TRecHandler("depoacnt.dbt");

    GetStorageAcnt(NSD_AccountCode, StorageAcnt);
    if(StorageAcnt.rec.AutoKey <= 0)
      err = 1;
      text_error("Не найден счет места хранения с кодом " + NSD_AccountCode);
    end;

    if(not err)
      // Выбрать все ненулевые остатки по открытому хранению за заданную дату, находящиеся на счете StorageAcnt, сгруппированные по терминальным разделам и ФИ
      // При этом у разделов должно быть определено примечание "Код в НРД"
query = " INSERT INTO DDPNSDREP_TMP (T_ID, T_DEPOACCNSDCODE, T_DEPOPARTNSDCODE, T_FIID, T_NSD_CODE, T_ISIN, T_LSIN, T_FI_NAME, T_SUMPRECISION, T_AMOUNT, T_NSDAMOUNT)" +
        " SELECT 0, ?,  t_DepoPartNSDCode, t_FIID, t_NSD_FICODE, t_ISIN, t_LSIN, t_FI_Name, t_SumPrecision, ABS(SUM(t_Amount)), 0 " +
        " FROM (SELECT acc.t_DepoAcc t_DepoPartID, " +
                           " NVL ( RTRIM (rsb_struct.getString (rsi_rsb_kernel.GetNote( 122, TO_CHAR (acc.t_DepoAcc), 2, ?)), CHR (0)), CHR (1)) t_DepoPartNSDCode, " +
                           " acc.t_Code_Currency t_FIID, " +
                           " fi.t_Name t_FI_Name, " +
                           " fi.t_SumPrecision, " +
                           " NVL (oc.t_Code, CHR (1)) t_NSD_FICODE, " +
                           " avr.t_LSIN, " +
                           " avr.t_ISIN, " +
                           " rsb_account.restac(acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, NULL) t_Amount " +
                      " FROM daccount_dbt acc, dfininstr_dbt fi, davoiriss_dbt avr, dobjcode_dbt oc " +
                     " WHERE acc.t_DepoRoot = ? " +
                       " AND INSTR (acc.t_Type_Account, 'I') = 0 " +
                       " AND rsb_account.restac (acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, NULL) != 0 " +
                       " AND NVL(RTRIM(rsb_struct.getString(rsi_rsb_kernel.GetNote(122, TO_CHAR(acc.t_DepoAcc), 2, ?)), CHR(0)), CHR(1)) != CHR(1) " +
                       " AND fi.T_FIID = acc.t_Code_Currency " +
                       " AND avr.t_FIID = acc.t_Code_Currency " +
                       " AND oc.t_ObjectType(+) = " + OBJTYPE_FININSTR +
                       " AND oc.t_ObjectID(+) = acc.t_Code_Currency " +
                       " AND oc.t_CodeKind(+) = "+FICK_NRD+") " +
                       " AND acc.t_accountid in (select qracc.t_accountid from dscqracc_dbt where qracc.t_storageid = ?)"
              " GROUP BY (t_DepoPartID, t_DepoPartNSDCode, t_FIID, t_FI_Name, t_SumPrecision, t_NSD_FICODE, t_LSIN, t_ISIN) ";

      ins = RSDCommand(query);

      ins.NullConversion = true;
      ins.AddParam("", RSDBP_IN, trim(string(hdsttds.DEPO_ACC_C)));
      ins.AddParam("", RSDBP_IN, RestDate);
      ins.AddParam("", RSDBP_IN, RestDate);
      ins.AddParam("", RSDBP_IN, StorageAcnt.rec.AutoKey);
      ins.AddParam("", RSDBP_IN, RestDate);
      ins.AddParam("", RSDBP_IN, RestDate);
      ins.AddParam("", RSDBP_IN, repDepoAcc);

      SQL_Execute(ins);

      cmd = null;
      DataSet = null;

      // Выбрать все терминальные разделы с ненулевыми остатками по открытому хранению за заданную дату, находящиеся на счете StorageAcnt
      // При этом у которых не определено примечание "Код в НРД"
      query = " select distinct dpac.t_BriefCode " +
                " from daccount_dbt acc, ddepoacnt_dbt dpac " +
               " where acc.t_DepoRoot = ? " +
                 " and instr(acc.t_Type_Account, 'I') = 0 " +
                 " and rsb_account.restac( acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, null ) != 0 " +
                 " and NVL(RTRIM(rsb_struct.getString(rsi_rsb_kernel.GetNote(122, TO_CHAR(acc.t_DepoAcc), 2, ?)), CHR(0)), CHR(1)) = CHR(1) " +
                 " and dpac.t_AutoKey = acc.t_DepoAcc ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(StorageAcnt.rec.AutoKey);
      cmd.AddParam(RestDate);
      cmd.AddParam(RestDate);

      DataSet = cmd.Execute();

      // Такие разделы не рассматриваем
      while(DataSet.moveNext())
        text_error("Для раздела " + string(DataSet.BriefCode) + " счёта " + StorageAcnt.rec.BriefCode + " не определён код раздела в НРД");
      end;
    end;

    return err;
  end;

  private macro AddNSDRest(DepoPartNSDCode, FIID, NSD_FICODE, NSD_LSIN, NSD_ISIN, NSD_Amount, NSD_FINAME)
    var i = 0, found = false, NewRest = null;
    var query, cmd, DataSet;

    if (ValType(NSD_Amount)==V_UNDEF)
      NSD_Amount=$0.0;
    end;

    cmd = DL_RSDCommand();
    
    query =   " select t_ID "
            + "   from ddpnsdrep_tmp "
            + "  where t_DepoPartNSDCode = ? ";

    cmd.AddParam(DepoPartNSDCode);

    if(FIID > 0)
      query = query + " and t_FIID = ? ";
      cmd.AddParam(FIID);
    end;

    if(NSD_FICODE != "")
      query = query + " and t_NSD_Code = ? ";
      cmd.AddParam(NSD_FICODE);
      end;

    if(NSD_ISIN != "")
      query = query + " and t_ISIN = ? ";
      cmd.AddParam(NSD_ISIN);
    end;

    /*if(NSD_LSIN != "")
      query = query + " and t_LSIN = ? ";
      cmd.AddParam(NSD_LSIN);
    end;*/

    DataSet = cmd.Execute(query);
    if(DataSet.moveNext())
      var upd;

      upd = RSDCommand("UPDATE DDPNSDREP_TMP  " +
                       "   SET T_NSDAMOUNT = T_NSDAMOUNT + ?, " +
                       "       T_NSD_CODE = ?, " +
                       "       T_ISIN = ?, " +
                       "       T_LSIN = ?, " +
                       "       T_FI_NAME = ? " +
                       " WHERE T_ID = ?  "
                      );

      upd.NullConversion = true;
      upd.addParam("", RSDBP_IN, NSD_Amount);
      upd.addParam("", RSDBP_IN, NSD_FICODE);
      upd.addParam("", RSDBP_IN, NSD_ISIN);
      upd.addParam("", RSDBP_IN, NSD_LSIN);
      upd.addParam("", RSDBP_IN, NSD_FINAME);
      upd.addParam("", RSDBP_IN, DataSet.ID);

      SQL_Execute(upd);
    else
      var ins = RSDCommand( "INSERT INTO DDPNSDREP_TMP (T_ID, T_DEPOACCNSDCODE, T_DEPOPARTNSDCODE, T_FIID, T_NSD_CODE, T_ISIN, T_LSIN, T_FI_NAME, T_SUMPRECISION, T_AMOUNT, T_NSDAMOUNT)"
                           +"                   VALUES (0, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?)");

      ins.NullConversion = true;
      ins.addParam("", RSDBP_IN, trim(string(hdsttds.DEPO_ACC_C)));
      ins.addParam("", RSDBP_IN, DepoPartNSDCode);
      ins.addParam("", RSDBP_IN, FIID);
      ins.addParam("", RSDBP_IN, NSD_FICODE);
      ins.addParam("", RSDBP_IN, NSD_ISIN);
      ins.addParam("", RSDBP_IN, NSD_LSIN);
      ins.addParam("", RSDBP_IN, NSD_FINAME);
      ins.addParam("", RSDBP_IN, AVOIRISS_SUM_PRECISION);
      ins.addParam("", RSDBP_IN, $0);
      ins.addParam("", RSDBP_IN, NSD_Amount);

      SQL_Execute(ins);
    end;

  end;

  private macro FoundCreateQrRest(ScQrAcc:TRecHandler, Rest:money, ErrStr:@string)
    var err=0, bufStr="";
    var ScQrRestTB = TBFile("scqrrest.dbt","w",1);
    ScQrRestTB.rec.QrId = ScQrAcc.rec.QrId;
    ScQrRestTB.rec.Date = {curdate};
    if (ScQrRestTB.GetEQ())

    if(ScQrRestTB.rec.Rest != Rest)
    end;   
      ScQrRestTB.rec.Rest = Rest;
      if (not ScQrRestTB.Update())
        RunError("Ошибка обновления записи таблицы scqrrest.dbt:|"+status(bufStr)+"-"+bufStr);
      end;
    else
      ScQrRestTB.rec.QrId = ScQrAcc.rec.QrId;
      ScQrRestTB.rec.Date = {curdate};
      ScQrRestTB.rec.Rest = Rest;
      if (not ScQrRestTB.Insert())
        RunError("Ошибка вставки записи в таблицу scqrrest.dbt:|"+status(bufStr)+"-"+bufStr);
      end;
    end;
  OnError(ErrObj)
    if (IsEqClass ("TrslError", ErrObj))
      ErrStr=ErrObj.Message;
      if(IsEqClass ("TDbError", ErrObj.err))
        ErrStr=ErrStr+":|"+ErrObj.err.Stat+"-"+ErrObj.err.Oper+"-"+ErrObj.err.Table+"-"+ErrObj.err.Message;
      end;
    elif (not ErrStr)
      ErrStr="Ошибка выполнения FoundOpenScQr";
    end;
    return 1;
  end;

  private macro FoundOpenScQr(NrdPartyId:integer, DepoAcc:string, DepoPart:string, 
    Fiid:integer, ScQrAcc:TRecHandler, AvoirRec:TRecHandler, FininstRec:TRecHandler, Rest:money, ErrStr:@string)
    var err=0, bufStr="";
    var cmd, DataSet;
    var rest0 = 0; 
    var ScQrAccTB = TBFile("scqracc.dbt","w");
    
    ScQrAcc.Clear();
    FininstRec.Clear();
    AvoirRec.Clear();
    cmd = DL_RSDCommand(  "select * from dscqracc_dbt qracc"
                               + " where qracc.t_StorageId  = ? "
                               + "   and qracc.t_Fiid  = ? "
                               + "   and qracc.t_DepoAccCode = ? "
                               + "   and qracc.t_DepoPartCode = ? "
                               + "   and rownum = 1"
                               ); 
    cmd.AddParam(NrdPartyId);
    cmd.AddParam(Fiid);
    cmd.AddParam(DepoAcc);
    cmd.AddParam(DepoPart);
    DataSet = cmd.Execute();
    if (DataSet.moveNext())
      DataSet.GetRecord().CopyTo(ScQrAcc.rec);
      err=FoundCreateQrRest(ScQrAcc, Rest,@ErrStr);
    else
      ScQrAcc.rec.StorageId = NrdPartyId;
      ScQrAcc.rec.Fiid = Fiid;
      ScQrAcc.rec.DepoAccCode = DepoAcc;
      ScQrAcc.rec.DepoPartCode = DepoPart;
      err = СоздатьЗаписьВРегистреКДУ(ScQrAcc, {curdate}, Rest0, @ErrStr);
    end;
    if (not err)
      ПолучитьФинИн(Int(Fiid),FininstRec,AvoirRec);
    end;
    return err;
  OnError(ErrObj)
    if (IsEqClass ("TrslError", ErrObj))
      ErrStr=ErrObj.Message;
      if(IsEqClass ("TDbError", ErrObj.err))
        ErrStr=ErrStr+":|"+ErrObj.err.Stat+"-"+ErrObj.err.Oper+"-"+ErrObj.err.Table+"-"+ErrObj.err.Message;
      end;
    elif (not ErrStr)
      ErrStr="Ошибка выполнения FoundOpenScQr";
    end;
    return 1;
  end;

  // чтение сведений об остатках в НРД
  private macro processSttdsDepo()
    var err=0;
    var FIID = -1;
    var i = 0;
    var cnt = getCountRecords(sttds);

    if (not err)

      InitProgress(cnt, "Обрабатывается справочник sttds.dbf", "Импорт сведений об остатках");
      protocol.SetCount(cnt);

      rewind(sttds);

      while(next(sttds))
        FIID = GetFIID();
        if( FIID == -1)
          error("В справочнике ценных бумаг не найден выпуск по указанным в отчёте НРД кодам");
        end;

        AddNSDRest(trim(sttds.SECTION_CO),
                  FIID,
                  trim(sttds.SECURITY_C),
                  trim(tooem(sttds.STATE_REGI, true)),
                  trim(sttds.ISIN_CODE),
                  money(strsubst(sttds.SEQ_AMO, " ", "")),
                  trim(tooem(sttds.SECURITY_S, true)));
        i = i + 1;
        UseProgress(i);
       end;   

      RemProgress();
    end;
    return err;
  end;

  // чтение сведений об остатках КДУ
  private macro processSttdsQr(NrdPartyId)
    var err=0;
    var FIID = -1;
    var i = 0;
    var cnt = 0;
    var ErrStr="";
    var ScQrAcc = TRecHandler("scqracc.dbt");
    var AvoirRec = TRecHandler("avoiriss.dbt");
    var FininstRec = TRecHandler("fininstr.dbt"); 
    var query, cmd, DataSet;


    processSttdsDepo(); // Загрузить все остатки из выписки

    query =   " SELECT SUM(T_NSDAMOUNT) AS NSDAMOUNT, T_DEPOACCNSDCODE, T_DEPOPARTNSDCODE, T_FIID, T_ISIN, T_LSIN "
            + "   FROM DDPNSDREP_TMP where t_fiid != -1"
            + "  GROUP BY T_DEPOACCNSDCODE, T_DEPOPARTNSDCODE, T_FIID, T_ISIN, T_LSIN ";
 
    cmd = DL_RSDCOmmand(query);

    cnt = cmd.GetCount();

    if(cnt > 0)

      InitProgress(cnt, "Обрабатывается загруженные данные", "Импорт сведений об остатках");

      DataSet = cmd.Execute();

      while(DataSet.moveNext())
          if (FoundOpenScQr(NrdPartyId, 
          DataSet.DEPOACCNSDCODE, 
          DataSet.DEPOPARTNSDCODE, 
          DataSet.FIID, 
            ScQrAcc, 
            AvoirRec,
            FininstRec,
          DataSet.NSDAmount,
            @ErrStr))

            error(ErrStr);
          end;

        i = i + 1;
        UseProgress(i);
      end;

      RemProgress();
    end;

    return err;
  end;

  private macro GetMisNum()
    var query, cmd, mis = 0;

    query =   " select 1 "
            + "   from ddpnsdrep_tmp tmp "
            + "  where tmp.t_Amount <> (select SUM(tmp1.t_NSDAmount) "
            + "                           from ddpnsdrep_tmp tmp1"
            + "                          where tmp1.t_depoaccnsdcode = tmp.t_depoaccnsdcode "
            + "                            and tmp1.t_depopartnsdcode = tmp.t_depopartnsdcode "
            + "                            and tmp1.t_isin = tmp.t_isin "
            + "                            and tmp1.t_lsin = tmp.t_lsin "
            + "                        ) ";

    cmd = DL_RSDCommand(query);
    mis = cmd.GetCount();

    return mis;
  end;

  // чтение заголовков отчета и выписки
  private macro processReportH()
    var err = 0;
    var CodeStr = "";
    var NrdPartyId = 0;
    var Depo_Acc = "";

    if(next(reporth))
      if(next(hdsttds))
        ImpDate = date(hdsttds.END_DATE);
        ImpNo   = trim(string(reporth.REG_NO));
        Depo_Acc = trim( string(hdsttds.DEPO_ACC_C));
        repDepoAcc = trim( string(hdsttds.DEPO_ACC_C));


        protocol = CNSDProtocol(ImpNo, ImpDate);

        if (isDepo)
          loadRestsDepo(trim(string(hdsttds.DEPO_ACC_C)), date(hdsttds.END_DATE)); // Загрузить все остатки на дату, хранящиеся в Системе
          processSttdsDepo(); // Загрузить все остатки из выписки
          err = protocol.GetCntErr();
          protocol.Print(GetMisNum());
        elif (isKDU)
          GetRegistryValue( "SECUR\\NRD_CODE", V_STRING, CodeStr, err );
          if (not err)
            NrdPartyId = ПолучитьКодСубъекта(String(CodeStr), PTCK_CLIENT);
            NrdPartyId = IIF(NrdPartyId,Int(NrdPartyId),0);
          end;
          if (NrdPartyId == 0)
            MsgBox("Не найден субъект из NRD_CODE");
            err=1;
          else
          // Очистка остатков на дату сверки
          var query_QR = "begin\n RSHB_USER.UpdateSCRest(?,?,?);\n end; ";
          var Select_QR = RSDCommand( query_QR );
          Select_QR.AddParam("pStorID", RSDBP_IN, NrdPartyId );
          Select_QR.AddParam("pDepoACC", RSDBP_IN, Depo_Acc );
          Select_QR.AddParam("pDate", RSDBP_IN, ImpDate );

          Select_QR.Execute();

            loadRestsQr(NrdPartyId);
            processSttdsQr(NrdPartyId);
            err = protocol.GetCntErr();
            protocol.Print(GetMisNum());
          end;
        end;
      else
        err = 1;
        msgbox("Отсутствует запись в файле заголовка выписки hdsttds.dbf");
      end;
    else
      err = 1;
      msgbox("Отсутствует запись в файле заголовка отчета reporth.dbf");
    end;

    return err;
  end;

  macro moveFile(path:string, fileName:string, dir:string)
    var pathDone = "";
    var day, month, year;

    DateSplit( ImpDate, day, month, year );
    if(day < 10)
      day = string("0"+day);
    end;
    if(month < 10)
      month = string("0"+month);
    end;
//Для РСХБ -перенос файлов на СП

    pathDone = path + dir+"\\";
    MakeDir(pathDone);
    pathDone = path + dir+"\\" + string(day)+string(month)+string(year)+"\\";
    MakeDir(pathDone);
    pathDone = path + dir+"\\" + string(day)+string(month)+string(year)+"\\"+ImpNo+"\\";
    MakeDir(pathDone);

    if (CopyFile(path + fileName, pathDone + fileName))
      RemoveFile(path + fileName);
    else
      msgbox("Ошибка при перемещении файла " + fileName + " в папку " + pathDone);
    end;
  end;

  // Запуск отчета
  macro Run( p_SrcDir ) : bool
    var err = 0;
    GetRegistryValue("COMMON\\WORK_MODE\\WORK_WITH_DEPOSITARY", V_BOOL, isDepo, err);
    GetRegistryValue("SECUR\\ВЕДЕНИЕ КДУ", V_BOOL, isKDU, err);
    if (err)
      MsgBox("Ошибка получение значений настроек \"WORK_WITH_DEPOSITARY\", \"ВЕДЕНИЕ КДУ\"");
    end;

    SQL_Execute("DELETE FROM DDPNSDREP_TMP");

    if (isDepo and isKDU)
      CreateErrMsg(err=28055);
    elif ((not isDepo) and (not isKDU))
      CreateErrMsg(err=28056);
    end;
    if (not err)
      GetRegistryValue("DEPO\\NSD\\NSD_IS401_PATH", V_STRING, path, err);
      if(path == "")
        msgbox("Не указан путь к сетевой папке для загрузки отчётов с остатками ценных бумаг.|Настройка DEPO\\NSD\\NSD_IS401_PATH");
      else
        // DEF-92764
        path = getCurDir(false) + "\\nsd\\";
        MoveNSDData( p_SrcDir, path );

        if(open(reporth, path + "reporth.dbf")) // Заголовок отчета
          if(open(hdsttds, path + "hdsttds.dbf")) // Заголовок выписки
            if(open(sttds, path + "sttds.dbf")) // Сведения об остатках

              err = processReportH();

              close(sttds);
            else
              err = 1;
              msgbox("Невозможно открыть файл " + path+"sttds.dbf");
            end;

            close(hdsttds);
          else
            err = 1;
            msgbox("Невозможно открыть файл " + path+"hdsttds.dbf");
          end;

          close(reporth);

          if(err == 0)
            moveFile(path, "reporth.dbf", "Done");
            moveFile(path, "hdsttds.dbf", "Done");
            moveFile(path, "sttds.dbf", "Done");
            moveFile(path, "sttds_sm.dbf", "Done");
          end;
        else
          err = 1;
          msgbox("Невозможно открыть файл " + path + "reporth.dbf");
        end;
      end;
    end;
    if (err>1)
      DisplayError();
    end;
    return err;
  end;
  
end;


 run ("unzip.cmd",null,null,"Press a key");

  /**
   @brief 		Процесс построения отчета для полученной папки. 
                        Сначала файлы с терминала перемещаются на сервер приложений ( в obj\nsd )
                        Затем вызывается отчет.
                        По завершении построения отчета, папка удаляется.
   @param[in]  p_SrcDir		Путь к исходной папке с данными
   @param[in]  p_DirName	Путь к папке текущего отчета
  */
  PRIVATE MACRO ProcessOneRep( p_SrcDir, p_DirName )
     var x_DirRepName = p_SrcDir + "\\" + p_DirName;
     CNSDImportRests().Run( x_DirRepName );
     removeDir( x_DirRepName );
  END;

  /**
   @brief 		Процесс построения отчета. Процедура пробегается по подпапкам, 
                        которые находятся по полученному пути, и вызывает построение отчета
                        для каждой папки                       
   @param[in]  p_SrcDir		Путь к папке с данными
  */
  PRIVATE MACRO processImpRests( p_SrcDir )
    var dl = tDirList(p_SrcDir + "\\*", "fd"), i;
    if(dl.Count > 0)
      dl.Sort(0, true);
      for(i, 2, dl.count-1)
        ProcessOneRep(p_SrcDir, dl.name(i)); 
      end;
    end;
  END;

  // DEF-92764 
  processImpRests( x_SrcDir );
  Exit(1);
