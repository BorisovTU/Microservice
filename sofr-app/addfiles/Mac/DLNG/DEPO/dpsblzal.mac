/* -@H------------------------------------------------------------------
*            Автоматизированная банковская система RS-Bank
*                 Библиотека интерпретируемых модулей
*                  Copyright (c) JV R-Style 1993-96
*
*   Description      :  Книжный пеевод -- блокировка "Залог"
*
*----------------------------------------------------------------------*/

import oprinter, SPInter, PaymInter, "dps_com.inc", "dp_util.mac";

file pmpaym("pmpaym") key 10;

macro InInterDepo(DACNTCode, ДатаНачала, ДатаКонца)
   file depoacnt(depoacnt);
   record  rBaseSum( "sfbassum.str" );
   var stat=0, NumCert=0, BnAccount = "", DwAccount = "";

   /*Поручение на книжный перевод эмиссионных ценных бумаг*/
   pmpaym.DocKind     = SP_DEPOPER_BOOKORDER;
   pmpaym.ValueDate   = ДатаНачала;
   pmpaym.Purpose     = PurpBase;
   pmpaym.SubPurpose  = 0;
   stat = GetGE(pmpaym);
   while(  stat and
          (pmpaym.DocKind   == SP_DEPOPER_BOOKORDER) and
          (pmpaym.ValueDate <= ДатаКонца)
        )
      if(pmpaym.PaymStatus == PM_FINISHED)
         BnAccount = GetHeadAccForAcc(pmpaym.ReceiverDpNode);
         DwAccount = GetHeadAccForAcc(pmpaym.PayerDpNode);
         if( (pmpaym.Purpose == PurpBase) and 
             (BnAccount == DACNTCode ) and  /*счет клиента на который зачиcлены бумаги*/
             (BnAccount == DwAccount)   /*по одному счету*/
           )
            depoacnt.AutoKey = pmpaym.ReceiverDpNode;
            if( GetEQ(depoacnt) )
               if( (depoacnt.AvoirStatus == BlockZalog) or  /*блокировка*/
                   (depoacnt.AvoirStatus == BlockZaklad)    /*блокировка*/
                 )
                  NumCert = NumCert + 1; /*считаем поручения*/

                  rBaseSum.baseType    = SF_BASETYPE_QUONT;
                  rBaseSum.baseType2   = SF_BASETYPE_QUONT;
                  rBaseSum.baseQuont   = 1;
                  rBaseSum.baseQuont2  = 1;

                  rBaseSum.BaseObjectType = OBJTYPE_OPERATIONDEPO;
                  rBaseSum.BaseObjectID   = GetSpdraftByPayment(pmpaym.PaymentID);

                  if( InsertSumList(rBaseSum) )
                     MacroError = 1;
                     MsgBox("Ошибка при вставке базовой суммы");
                  end;
               end;
            end;
         end;
      end;
      stat = Next(pmpaym);
   end;

   return NumCert;
end;

macro CalcServiceSum( sfcontr_addr, BeginDate, EndDate )
    record  rContr( sfcontr );
    file depoacnt(depoacnt) key 1;
    SetBuff( rContr, sfcontr_addr );

    InInterDepo(rContr.Object, BeginDate, EndDate);
    return; 
end;
