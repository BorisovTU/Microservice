/*******************************************************************/
/*         Автоматизированная банковская система RS-Bank           */
/*            Copyright (c) R-Style Software Lab 2003              */
/*                                                                 */
/*  Имя файла        : dpainprp.mac                                */
/*  Описание         : Отчет "Распределение остатков на субсчетах" */
/*  Программист      : Alex V. Mishin                              */    
/*  Создан           : 04-02-03                                    */
/*                                                                 */
/*******************************************************************/

import deposerv, CurrInter;

var ReportDate;
var MismatchsOnly;
var ShowUndistributed;

var FirstAccPrinting = 1;
var FirstFIPrinting = 1;

const CHAPT5 = 5;
const SYS_ANL_AINPACCOUNTING = 2;

var HeadTable = "┌──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n"+
                "│                                                                                                                                  │\n"+
                "├──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤";
                
var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

macro MonToStr(Rest)
  return string((double(Rest)):20:0);
end;


class CPassiveAccount(_FIID, _Account, _Rest)
  var Account;
  var Rest;
  var FIID;

  Account = _Account;
  Rest = _Rest;
  FIID = _FIID;

  macro PrintPassiveAcc()
    file fin("fininstr");
    var t_point = AVOIRISS_SUM_PRECISION;    
    fin.FIID = FIID;
    if( GetEQ(fin))
      t_point = fin.SumPrecision;
    else
      t_point = AVOIRISS_SUM_PRECISION;
    end;

    if(Rest != $0)
    DP_AddPrintCell( Rep, "   в пассиве " + Account, 38, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
    DP_AddPrintCell( Rep, "                                                                      ", 70, 0, "r:" + RepFontStyleTitel,false, REP_ELEM_STR );
    DP_AddPrintCell( Rep, abs(double(Rest)), 20, DP_GetPrecision(Rest, t_point), "r:" + RepFontStyleTitel,false, REP_ELEM_STR );
    Rep.AddStr();
    end;
  end;

end;


class CActiveAccount()
  var Account;
  var FIID;
  var FIName;
  var FIKindName;
  var LSIN;
  var Rest;
  var UndistributedSum;
  var PassiveAccounts = TArray;


  macro GetAllPassive()
    FILE accsub(accsub) key 3;
    FILE accvanl(accvanl);
    var PassiveAcc:CPassiveAccount;
    var stat;

    PassiveAccounts.Size = 0;
    accsub.AnaliticsID = SYS_ANL_AINPACCOUNTING;
    accsub.SubAccountCode = Account;
    
    if(stat = GetGE(accsub))
      while( (stat) AND ((accsub.AnaliticsID == SYS_ANL_AINPACCOUNTING) AND (accsub.SubAccountCode == Account) ) )
        accvanl.AccAnaliticsID = accsub.AccAnaliticsID;
        if(GetEQ(accvanl))
          PassiveAcc = CPassiveAccount(FIID, accvanl.Account, RestSubAcc(accsub.AccAnaliticsID, accsub.SubAccountID, ReportDate));
          PassiveAccounts(PassiveAccounts.Size) = PassiveAcc;
          UndistributedSum = UndistributedSum - PassiveAcc.Rest;
        else
          msgbox("Не найдена аналитика счета с AccAnaliticsID = ", accsub.AccAnaliticsID);
          return false;
        end;
        stat = Next(accsub);
      end;
    end;
    return true;
  end;


  macro Init(_Account, _FIID)
    var err;
    
    RECORD fi(fininstr);
    RECORD avoir(avoiriss);
    FILE avr(avrkinds);
    
    Account = _Account;
    FIID = _FIID;
    
    if( NOT ПолучитьФинИн(FIID, fi, avoir) )
      FIName = fi.Name;
      LSIN = avoir.LSIN;
      
      avr.FI_Kind = fi.FI_Kind;
      avr.AvoirKind = fi.AvoirKind;
      if(GetEQ(avr))
        FIKindName = avr.Name;
      else
        msgbox("Не найден вид ФИ с FI_Kind = ", fi.FI_Kind, "AvoirKind = ", fi.AvoirKind);
        return false;
      end;
    else
      msgbox("Не найден ФИ с FIID = ", FIID);
      return false;
    end;
    
    Rest = abs(RestAC(_Account, _FIID, ReportDate, NULL, CHAPT5));
    UndistributedSum = abs(Rest);

    if( NOT GetAllPassive() )
      return false;
    end;

    return true;
  end;

  macro NeedPrintActiveAcc()
    if( (UndistributedSum ) OR ((Rest ) AND (MismatchsOnly == "")) )
      return true;
    end;
  end;

  macro PrintActiveAcc()
    file fin("fininstr");
    var t_point = AVOIRISS_SUM_PRECISION;
    var i;
    fin.FIID = FIID;
    if( GetEQ(fin))
      t_point = fin.SumPrecision;
    else
      t_point = AVOIRISS_SUM_PRECISION;
    end;

    DP_AddPrintCell( Rep, "л/с " + Account + " ", 30, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
    DP_AddPrintCell( Rep, FIName + " ", 26, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
    DP_AddPrintCell( Rep, FIKindName + " ", 26, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
    DP_AddPrintCell( Rep, LSIN + " ", 26, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
    DP_AddPrintCell( Rep, abs(double(Rest)), 20, DP_GetPrecision(Rest, t_point), "r:" + RepFontStyleTitel,false, REP_ELEM_STR );
    Rep.AddStr();
    i = 0;
    while( i < PassiveAccounts.Size )
      PassiveAccounts(i).PrintPassiveAcc();
      i = i + 1;
    end;

    if( UndistributedSum )    
      DP_AddPrintCell( Rep, "   нет подтверждения на сумму                                                                               ", 108, 0, "l:" + RepFontStyleTitel,true, REP_ELEM_STR );
      DP_AddPrintCell( Rep, abs(double(UndistributedSum)), 20, DP_GetPrecision(UndistributedSum, t_point), "r:" + RepFontStyleTitel,true, REP_ELEM_STR );
      Rep.AddStr();
    end;
  end;
  
end;


class CDepoPartition()
  var BriefCode;
  var ActiveAccounts = TArray;
  
  macro Init(depoacnt)
    var stat;
    var ActiveAcc:CActiveAccount;
    FILE acc("account") key 3;

    BriefCode = depoacnt.BriefCode;
    ActiveAccounts.Size = 0;
    
    acc.DepoAcc = depoacnt.AutoKey;
    if(stat = GetGE(acc))
      while( (stat) AND (acc.DepoAcc == depoacnt.AutoKey) )
        if ( not ИндивидуальныйФинИн(acc.Code_Currency) )
          ActiveAcc = CActiveAccount;
          if(ActiveAcc.Init(acc.Account, acc.Code_Currency))
            ActiveAccounts(ActiveAccounts.Size) = ActiveAcc;
          else
            return false;
          end;
        end;
        stat = Next(acc);
      end;
    end;

    return true;
  end;

  macro NeedPrintPartition()  
    var i;
    
    if( ActiveAccounts.Size == 0 )
      return false;
    else
      i = 0;
      while( i < ActiveAccounts.Size )
        if( ActiveAccounts(i).NeedPrintActiveAcc() )
          return true;
        end;
        i = i + 1;
      end;
    end;
  end;

  macro PrintPartition()
    var i;
    DP_AddPrintCell( Rep, "   Раздел: ", 11, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
    DP_AddPrintCell( Rep, BriefCode, 25, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
    Rep.AddStr();
    i = 0;
    while( i < ActiveAccounts.Size )
      ActiveAccounts(i).PrintActiveAcc();
      i = i + 1;
    end;
  end;
  
end;


class CDepoACNT()
  var BriefCode;
  var TermPartitions = TArray;

  var ContinueModifyDEPOAC;
  
  macro Init(depoacnt)
    
    macro ModifyDEPOAC( FilePtr, MacroFun, Root )
      var SaveKeyNumber;
      var SavePos;
      var stat = TRUE;
      var bstat;
      var bftext;
      var cycle = TRUE;
      
      SaveKeyNumber = KeyNum( FilePtr, 4 );
      SavePos = GetPos(FilePtr);
    
      if( SavePos )
        ContinueModifyDEPOAC = TRUE;
        FilePtr.Root = Root;
        stat = GetGE(FilePtr);
        while( (cycle) AND (stat) AND (ContinueModifyDEPOAC) )
          if( FilePtr.Root == Root )
            stat = ExecMacro2( MacroFun );
            if( (stat) AND (ContinueModifyDEPOAC) )
              stat = Next(FilePtr);
            end;
          else
            cycle = FALSE;
          end;
        end;
        if( not stat )
          bstat = Status(bftext);
          if((bstat) AND (bstat!=9) AND (bstat!=4))
            msgbox("Ошибка поиска записи в файле depoacnt.dbt|"+bftext);
            stat = FALSE;
          else
            stat = TRUE;
          end;
        end;
        if(stat)
          KeyNum( FilePtr, SaveKeyNumber );
          stat = GetDirect(FilePtr, SavePos);
          if(not stat )
            status(bftext);
            msgbox("Ошибка восстановления позиции в файле depoacnt.dbt, SavePos ="+SavePos+"|"+bftext);
          end;
        end;
      else
        status(bftext);
        msgbox( "Ошибка сохранения позиции в файле depoacnt.dbt, ID ="+FilePtr.Autokey+"|"+bftext );
        stat = FALSE;
      end;
      
      return stat;
    end;
    
    
    macro FindChild( FilePtr, Child )
      var SaveKeyNumber;
      var SavePos;
      var stat = TRUE;
      var bstat;
      var bftext;
      var Superior;
    
      SetParm(1, FALSE);
    
      SaveKeyNumber = KeyNum( FilePtr, 5 );
      Superior = FilePtr.AutoKey;
      
      SavePos = GetPos(FilePtr);
      if( SavePos )
        FilePtr.Superior = Superior;
        stat = GetGE(FilePtr);
        if(stat)
          if( FilePtr.Superior == Superior )
            SetParm(1, TRUE);
          end;
        else
          bstat = status(bftext);
          if( (bstat==4) OR (bstat==9) )
            stat = TRUE;
          else
            msgbox( "Ошибка поиска записи в файле depoacnt.dbt, ID ="+FilePtr.AutoKey+"|"+bftext );
          end;
        end;
        if(stat)
          KeyNum( FilePtr, SaveKeyNumber );
          stat = GetDirect(FilePtr, SavePos);
          if(not stat )
            status(bftext);
            msgbox( "Ошибка восстановления позиции в файле depoacnt.dbt, SavePos ="+SavePos+"|"+bftext );
          end;
        end;
      else
        status(bftext);
        msgbox( "Ошибка сохранения позиции в файле depoacnt.dbt, ID ="+FilePtr.AutoKey+"|"+bftext );
        stat = FALSE;
      end;
      
      return stat;
    end;
      
    
    macro GetAllTerminal()
      var HaveChild;
      var DepoPart:CDepoPartition;
      
      if(FindChild(depoacnt, HaveChild))  
        if(NOT HaveChild)
          DepoPart = CDepoPartition;
          if( DepoPart.Init(depoacnt) )
            TermPartitions(TermPartitions.Size) = DepoPart;
            return true;
          else
            return false;
          end;
        else
          return true;
        end;
      else
        return false;
      end;
    end;
    
    
    BriefCode = depoacnt.BriefCode;
    TermPartitions.Size = 0;
    
    return ModifyDEPOAC(depoacnt, @GetAllTerminal, depoacnt.Root );
  end;

  
  macro NeedPrintDepoACNT()
    var i;
    if( TermPartitions.Size == 0 )
      return false;
    else
      i = 0;
      while( i < TermPartitions.Size )
        if( TermPartitions(i).NeedPrintPartition() )
          return true;
        end;
        i = i + 1;
      end;
    end;
  end;


  macro PrintDepoACNTInfo()
    var i;
    
    if( FirstAccPrinting == 1 ) /* Вывод шапки */
      FirstAccPrinting = 0;
      DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "РАСПРЕДЕЛЕНИЕ Ц/Б НА ПАССИВНЫХ СУБСЧЕТАХ", 0, 0, false, ReportDate, ReportDate, tablewidth );
      DP_AddPrintCell( Rep,  "=================================================================================================================================", tablewidth, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
      Rep.AddStr();
    end;

DP_AddPrintCell( Rep,  "Счет депо: " + BriefCode, 36, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
DP_AddPrintCell( Rep,  "                                                                        Количество", 100, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
Rep.AddStr();
DP_AddPrintCell( Rep,  "=================================================================================================================================", tablewidth, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
Rep.AddStr();
    i = 0;
    while( i < TermPartitions.Size )
      TermPartitions(i).PrintPartition();
      i = i + 1;
    end;
DP_AddPrintCell( Rep,  "=================================================================================================================================", tablewidth, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
Rep.AddStr();
  end;

end;


class CUndistribFI
  var FIName;
  var FIKindName;
  var LSIN;
  var FIID;
  var UndistributedSum = 0;
  var PassiveAccounts = TArray;

  PassiveAccounts.Size = 0;

  macro GetPassive( accvanl, SpecialType )
    FILE accsub(accsub) key 1;
    var PassiveAcc:CPassiveAccount;
    
    accsub.AccAnaliticsID = accvanl.AccAnaliticsID;
    accsub.ParentID       = 0;
    accsub.SpecialType    = SpecialType;
    accsub.SubAccountCode = "";
    
    if( (GetGE(accsub)) AND (accsub.AccAnaliticsID = accvanl.AccAnaliticsID)
        AND (accsub.ParentID == 0) AND (accsub.SpecialType == SpecialType) )
        
      PassiveAcc = CPassiveAccount(accvanl.FIID, accvanl.Account, RestSubAcc(accsub.AccAnaliticsID, accsub.SubAccountID, ReportDate));
      PassiveAccounts(PassiveAccounts.Size) = PassiveAcc;
      UndistributedSum = UndistributedSum + abs(PassiveAcc.Rest);
    end;
  end;

  
  macro GetAllPassive(accvanl)
    GetPassive(accvanl, 1);
    GetPassive(accvanl, 2);
  end;
  
  macro Init( accvanl )
    var err;
    
    RECORD fi(fininstr);
    RECORD avoir(avoiriss);
    FILE avr(avrkinds);
    
    if( NOT ПолучитьФинИн(accvanl.FIID, fi, avoir) )
      FIName = fi.Name;
      LSIN = avoir.LSIN;
      FIID = accvanl.FIID;
      avr.FI_Kind = fi.FI_Kind;
      avr.AvoirKind = fi.AvoirKind;
      if(GetEQ(avr))
        FIKindName = avr.Name;
      else
        msgbox("Не найден вид ФИ с FI_Kind = ", fi.FI_Kind, "AvoirKind = ", fi.AvoirKind);
        return false;
      end;
    else
      msgbox("Не найден ФИ с FIID = ", accvanl.FIID);
      return false;
    end;

    UndistributedSum = 0;
    PassiveAccounts.Size = 0;
    
    GetAllPassive(accvanl);
    
    return true;
  end;


  macro PrintUndistrFI()
    var i;
    file fin("fininstr");
    var t_point = AVOIRISS_SUM_PRECISION;
    var __UndistributedSum;

    
    if( UndistributedSum != $0.0 )
      if( FirstAccPrinting == 1 ) /* Вывод шапки */
        FirstAccPrinting = 0;
        DP_AddPrintCell( Rep, "РАСПРЕДЕЛЕНИЕ Ц/Б НА ПАССИВНЫХ СУБСЧЕТАХ ПО СОСТОЯНИЮ НА " + ReportDate, tablewidth, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
        Rep.AddStr();
        Rep.AddEmptyStr();
        Rep.AddEmptyStr();
        DP_AddPrintCell( Rep, "=================================================================================================================================", tablewidth, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
        Rep.AddStr();
      end;
      if(FirstFIPrinting == 1)
        FirstFIPrinting = 0;
DP_AddPrintCell( Rep, "=================================================================================================================================", tablewidth, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
Rep.AddStr();
      end;

      fin.FIID = FIID;
      if( GetEQ(fin))
        t_point = fin.SumPrecision;
      else
        t_point = AVOIRISS_SUM_PRECISION;
      end;
      
      DP_AddPrintCell( Rep, "На субсчетах нераспред. ц/б   ", 30, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
      DP_AddPrintCell( Rep, FIName + " ", 26, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
      DP_AddPrintCell( Rep, FIKindName + " ", 26, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
      DP_AddPrintCell( Rep, LSIN + " ", 26, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR );
      DP_AddPrintCell( Rep, abs(double(UndistributedSum)), 20, DP_GetPrecision(UndistributedSum, t_point), "r:" + RepFontStyleTitel,false, REP_ELEM_STR );
      Rep.AddStr();
      i = 0;
      while( i < PassiveAccounts.Size )
        PassiveAccounts(i).PrintPassiveAcc();
        i = i + 1;
      end;
    end;
    UndistributedSum = 0;
    PassiveAccounts.Size = 0;
  end;
  
end;


/*
РАСПРЕДЕЛЕНИЕ Ц/Б НА ПАССИВНЫХ СУБСЧЕТАХ ПО СОСТОЯНИЮ НА 05.06.2002


=======================================================================================================================
Счет депо:    A0001000                                  Количество
=======================================================================================================================
  Раздел:     A0001100
л/с 1234567890123456789012345 ОАО Российские железные тротуары  Акция             1-02-00135-A     111 999 111 999 111 
    в пассиве: 3216549870321654987032154                                                           111 999 000 000 000 
    в пассиве: 3216549870321654987032156                                                                   111 999 000 
    в пассиве: 3216549870321654987032154                                                                           110 
    в пассиве: нет подтверждения на сумму                                                                            1 
=======================================================================================================================
=======================================================================================================================
На субсчетах нераспред. ц/б   ОАО Российские железные тротуары  Акция             1-02-00135-A     111 999 111 999 111 
    в пассиве: 3216549870321654987032165                                                           111 999 000 000 000 
    в пассиве: 3216549870321654987032156                                                                   111 999 111 
*/

/* Точка входа в макрос */
macro SubAccRestDistribution ( iDepart: integer,
  _ReportDate        : date,
  _MismatchsOnly     : string,
  _ShowUndistributed : string,
  ToExcel            : bool )

  Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

  DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

  var stat;
  var dpac = CDepoACNT;
  var UndistrFI = CUndistribFI;
  
  var oldFIID;

  FILE depoacnt(depoacnt);
  FILE accvanl(accvanl);
  FILE accsub(accsub);

  ReportDate        = _ReportDate;
  MismatchsOnly     = _MismatchsOnly;
  ShowUndistributed = _ShowUndistributed;

  depoacnt.Superior = 0;
  KeyNum(depoacnt, 5); /* Выбираем только _счета_ */
  if( stat = GetGE(depoacnt) )
    while( (stat) AND (depoacnt.Superior == 0))
      if((depoacnt.Kind == 1) and (depoacnt.Department == iDepart)) /* Только активные */
        if( dpac.Init(depoacnt) )
          if( dpac.NeedPrintDepoACNT() )
            dpac.PrintDepoACNTInfo();
          end;
        else

          DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
          DP_EndProtocol();                 //закончить протоколирование
          if( ToExcel )
            Rep.PrintWinRep();
            Rep.ShowWinRep();
          else
            Rep.PrintRep();
          end;

          return 1;
        end;
      end;
      stat = Next(depoacnt);
    end;
  end;

  if( ShowUndistributed == "X" )
    oldFIID = 0;
    accvanl.AnaliticsID = SYS_ANL_AINPACCOUNTING;
    accvanl.FIID = 0;
    accvanl.Chapter = 0;
    accvanl.Account = "";
    KeyNum(accvanl, 2);

    if( stat = GetGE(accvanl) )
      while( (stat) AND (accvanl.AnaliticsID == SYS_ANL_AINPACCOUNTING))
        if(oldFIID != accvanl.FIID )
          if(oldFIID != 0 )
            UndistrFI.PrintUndistrFI();
          end;
          if( NOT UndistrFI.Init(accvanl))

            DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
            DP_EndProtocol();                 //закончить протоколирование
            if( ToExcel )
              Rep.PrintWinRep();
              Rep.ShowWinRep();
            else
              Rep.PrintRep();
            end;

            return 1;
          end;
          oldFIID = accvanl.FIID;
        else
            UndistrFI.GetAllPassive(accvanl);
        end;
        stat = Next(accvanl);
      end;
      UndistrFI.PrintUndistrFI();
    end;
  end;

  if( FirstAccPrinting == 1 )
    if(not DP_ProtocolIsEmpty())   //протокол не пуст
      DP_AddPrintCell(Rep, "Не найдено данных, удовлетворяющих параметрам отчета", 55, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR);
      Rep.AddStr();

      DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
      DP_EndProtocol();                 //закончить протоколирование

      if( ToExcel )
        Rep.PrintWinRep();
        Rep.ShowWinRep();
      else
        Rep.PrintRep();
      end;
    else   //протокол пуст
      DP_EndProtocol();                 //закончить протоколирование
    end;

    MsgBox("Не найдено данных, удовлетворяющих параметрам отчета");
    return 1;
  else
    DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

    DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
    DP_EndProtocol();                 //закончить протоколирование

    if( ToExcel )
      Rep.PrintWinRep();
      Rep.ShowWinRep();
    else
      Rep.PrintRep();
    end;
  end;

  return 0;
end;

