/*
$Name: order_inf.mac
$Module: Депозитарий
$Description: Отчет об исполнении информационной операции
*/

import deposerv, CurrInter, cb_sql;

const OP_REJECT = 1;
const OP_OK = 0; 
const DOC_CLOSE = 3;

private const DP_NOTEKIND_REJECT = 3; /* примечание "причина отказа" */

/* Виды объектов для отчетов (в namealg 3127)*/
const   INF_UNDEF      = 0; /* не задан*/
const   INF_PARTY      = 1; /* субъект*/
const   INF_AVOIRISS   = 2; /* выпуск ц/б*/
const   INF_DACCOUNT   = 3; /* счет депо   */
const   INF_DPARTITION = 4; /* раздел счет депо */
const   INF_FACCOUNT   = 5; /* лицевой счет депо  */
const   INF_CERTIF     = 6; /* инвентарная карточка */
const   INF_ORDER      = 7; /* поручение */

var RepInf = CMakeReport();
const RepFontStyleInf =  "ex_FS(b)";
const TableWidthInf = 100;

var GrDoc, xld_begin, xld_end, date_begin, date_end, opstatus, opstatus_begin, opstatus_end, ojd_begin, ojd_end, LOID, ToExcel; 
/* доп. параметры поручения */
private var opKind, Department, Party, DepoAcc, DepoPart, DAccount, FIID, InvCardID;
private var __is_app;

var LogID=0, InfRepRecipientID=-1,InfRepNum, InfRepDate;

private var isPrint = false, CountRep = 0;

private macro GetLLVALUES( List, Elem )
  file llvalues("llvalues");

  ClearRecord(llvalues);
  llvalues.List    = List;
  llvalues.Element = Elem;
  if( GetEQ(llvalues) )
    return llvalues.Name;
  end;

  return "";
end;


macro GetSQLStrSpgr()
  var query;

  query = " select logopr.t_AutoKey, logopr.t_Draft, logopr.t_recId " +
          " from dsplogopr_dbt logopr, dspground_dbt spgr, dspinfopr_dbt infopr " +
          " where spgr.t_SPgroundID = logopr.t_Draft " +
          "   and infopr.t_SpInfDocID = logopr.t_recId ";

  if(LogID > 0)
    query = query + " and logopr.t_AutoKey = " + LogID;
  else

    query = query + " and spgr.t_Direction = " + ENTERING
            + " and spgr.t_Division  = " + SelfDivision
            + " and spgr.t_Department = " + Department;

    if( GrDoc > 0 )
       query = query + " and spgr.t_Kind = " + GrDoc;
    end;

    if( (xld_begin != "") and (xld_end != "") )
       query = query + " and spgr.t_Xld >= '"+xld_begin+"' ";
       query = query + " and spgr.t_Xld <= '"+xld_end+"' ";
    end;

    query = query + " and logopr.t_State = " + DOC_CLOSE;

    if((opstatus_begin > date(0,0,0)) and (opstatus_end > date(0,0,0)))
      query =  query + " and logopr.t_CarryDate >= "+GetSQLDate(opstatus_begin)+" "
                     + " and logopr.t_CarryDate <= "+GetSQLDate(opstatus_end)+" ";
    end;

    /* проверка номера ОЖД */
    if ( (ojd_begin != "") AND (ojd_end != ""))
        query =  query + " and logopr.t_OprXid  >= '"+ojd_begin+"' ";
        query =  query + " and logopr.t_OprXid  <= '"+ojd_end+"' ";           
    end;
    
    /* проверка статуса операции*/
    if( opstatus != 2 ) /* не задано */
      if ( opstatus == OP_OK )
        query =  query + " and logopr.t_OperState = "+OP_OK+" ";
      elif ( opstatus == OP_REJECT )
        query =  query + " and logopr.t_OperState = "+OP_REJECT+" ";
      end;
    end;

      /* проверка вида отчета */
    if ( opkind > 0 )
      query = query + " and infopr.t_docType = " + opkind;
    end;
    
    /* проверка филиала */
    if ( Department > 0 )
      query = query + " and infopr.t_Department = " + Department;
    end;

     /* проверка субъекта */
    if ( Party != -1 )
      query = query + " and infopr.t_PartyID = " + Party;
    end;

    /* проверка счета */
    if ( DepoAcc > 0 )
      query = query + " and infopr.t_DepoAcc = " + DepoAcc;
    end;

    /* проверка раздела */
    if ( DepoPart > 0 )
      query = query + " and infopr.t_DepoPart = " + DepoPart;
    end;

    /* проверка л/с */
    if ( Daccount != "" )
      query = query + " and infopr.t_Account = '" + DAccount + "'";
    end;

    /* проверка вида ц/б */
    if ( FIID != -1 )
      query = query + " and infopr.t_FIID = " + FIID;
    end;

    /* проверка ИК */
    if ( InvCardID )
      query = query + " and infopr.t_InvCardID = " + InvCardID;
    end;
  end;

  return query;
end;

private macro GetAccountByID(AccountID)
  var cmd, DataSet;
  var Account = "";

  cmd = DL_RSDCommand("select t_Account from daccount_dbt where t_AccountID = ?");
  cmd.AddParam(AccountID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    Account = DataSet.Account;
  end;

  return Account;
end;


macro PrintInfOrderForm(groundID, logoprID, infoprID, PrintSubNum)

  var ground    = TBFILE("spground");
  var logopr    = TBFILE("splogopr");
  var logopr_new= TBFILE("splogopr");
  var infopr    = TBFILE("spinfopr");
  var spoper    = TBFILE("spoper");
  var GroundAdd = TBFILE("spground");
  var partcode  = TBFILE("partcode");
  var fininstr  = TBFILE("fininstr");
  var depoacnt  = TBFILE("depoacnt");
  var account   = TBFILE("account");
  var invcard   = TBFILE("invcard.dbt");
  var person    = TBFILE("person");
  var depoac    = TBFILE("depoac");
  var avoir     = TBFILE("avoiriss");
  var notetext  = TBfile("notetext");

  var code_item = "-", type_item = "-", id_item = "-", Reciever = "-", id_item_ext = "";
  var Основание = "";
  var cmd, DataSet, stat;

  macro GetObject()
    
    var item = spoper.rec.Item;
    var partition = TBFILE("depoacnt");
    var part_depoac = TBFILE("depoac");
    var query;

    /* установка позиции файлов */
    if (infopr.rec.PartyID != -1)  /* субъект */
      if( not ПолучитьКодСубъекта( infopr.rec.PartyID, 1 ) )
        return false;
      end;
    end;

    if (infopr.rec.DepoAcc > 0) /* счет */
      depoacnt.rec.AutoKey = infopr.rec.DepoAcc;
      if (NOT depoacnt.GetEQ())
        return false;
      else
        if (depoacnt.rec.Type != 0)
          depoac.rec.ID = depoacnt.rec.Type;
          if ( NOT depoac.GetEQ() )
            return false;
          end;
        end;
      end;
    end;

    if (infopr.rec.DepoPart > 0) /* раздел */
      partition.rec.AutoKey = infopr.rec.DepoPart;
      if (NOT partition.GetEQ())
        return false;
      else
        if (partition.rec.Type != 0)
          part_depoac.rec.ID = partition.rec.Type;
          if ( NOT part_depoac.GetEQ() )
            return false;
          end;
        end;
      end;
    end;

    if (infopr.rec.FIID != -1) /* ФИ */
      fininstr.rec.FIID = infopr.rec.FIID;
      if (NOT fininstr.GetEQ())
        return false;
      else
        avoir.rec.FIID = fininstr.rec.FIID;
        if (NOT avoir.GetEQ())
          return false;
        end;
      end;
    end;

    if (infopr.rec.Account != "") /* л/с */
      account.rec.Account = infopr.rec.Account;
      account.rec.Chapter = 5;
      if (infopr.rec.FIID != -1)
        account.rec.Code_Currency = infopr.rec.FIID;
      else
        account.rec.Code_Currency = 0;
      end;
      if( NOT account.GetEQ() )
        return false;
      end;
    end;

    if (infopr.rec.InvCardID) /* ИК */
      invcard.rec.InvCardID = infopr.rec.InvCardID;
      if (NOT invcard.GetEQ())
        return false;
      end;
    end;
    

    if (item == INF_UNDEF)
      type_item = "-";
      code_item = "-";
      id_item   = "-";

    elif(item == INF_PARTY)
      type_item = "субъект";
      if (infopr.rec.PartyID != -1)
        code_item = partcode.rec.Code;
        id_item   = partcode.rec.Code + "  " + GetPartyName( infopr.rec.PartyID, true );
      else
        code_item = "-";
        id_item   = "-";
      end;

    elif(item == INF_AVOIRISS)
      type_item = "ценная бумага";
      if (infopr.rec.FIID != -1)
        code_item = fininstr.rec.FI_Code + "    " + fininstr.rec.Name;
        id_item   = "эмитент " + GetPartyName( fininstr.rec.Issuer, true );
        id_item_ext = "номинал: " + ЧислоCЗаданнойТочностью(GetFaceValue(fininstr.rec.FIID, ground.rec.RegistrDate), fininstr.rec.Point+2)+" "+GetFICode( fininstr.rec.FaceValueFI, null, FICK_ISOSTRING ) + "\n";
        id_item_ext = id_item_ext + "   дата выпуска: " + string(fininstr.rec.Issued);
      else
        code_item = "-";
        id_item   = "-";
      end;

    elif(item == INF_DACCOUNT)
      type_item = "счет депо";
      if (infopr.rec.DepoAcc > 0)
        code_item = depoacnt.rec.BriefCode + "  владелец  " + GetPartyName( depoacnt.rec.Owner, true );
        id_item   = depoacnt.rec.Code;
        id_item_ext = depoac.rec.Name;
      else
        code_item = "-";
        id_item   = "-";
      end;

    elif(item == INF_DPARTITION)
      type_item = "раздел счета депо";  
      if (infopr.rec.DepoPart > 0)
        code_item = partition.rec.BriefCode + "  владелец  " + GetPartyName( partition.rec.Owner, true );
        id_item   = partition.rec.Code;
        id_item_ext = part_depoac.rec.Name;
      else
        code_item = "-";
        id_item   = "-";
      end;

    elif(item == INF_FACCOUNT)
      type_item = "лицевой счет";
      if (infopr.rec.Account != "")
        code_item = account.rec.Account + "  владелец " + GetPartyName( account.rec.Client, true );
        id_item   = account.rec.Account;
      else
        code_item = "-";
        id_item   = "-";
      end;
    
    elif(item == INF_CERTIF)
      type_item = "инвентарная карточка";
      if (infopr.rec.InvCardID)
        code_item = invcard.rec.Number + "    " + GetAccountByID(invcard.rec.HAccountID);
        id_item   = invcard.rec.Number;
      else
        code_item = "-";
        id_item   = "-";
      end;

    /* пока такой вид объекта отчета нигде не используется (ну кроме отчета об исполнении
       поручения, который мы не обрабатываем), но он может пригодиться в будующем */
    elif(item == INF_ORDER)
      type_item = spoper.rec.OperationName;
      if (infopr.rec.PartyID != -1)
        id_item = "владелец счета      " + GetPartyName(infopr.rec.PartyID, true);
      end;
      if (infopr.rec.DepoAcc > 0)
        id_item = id_item + "счет депо            " + depoacnt.rec.BriefCode;
      end;
      if (infopr.rec.DepoPart > 0)
        id_item = id_item + "раздел счета депо    " + partition.rec.BriefCode;
      end;                                        
      if (infopr.rec.account != "")
        id_item = id_item + "лицевой счет         " + infopr.rec.Account;
      end;
      if (infopr.rec.FIID != -1) 
        id_item = id_item + "ценная бумага        " + fininstr.rec.FI_Code + "  " + 
                  fininstr.rec.Name;        
      end;
      if (infopr.rec.InvCardID)
        id_item = id_item + "Инвентарная карточка " + invcard.rec.Number;
      end;    
      
      
    end;

    return true;
  end;/* macro GetObject() */

 
  /* найдем получателя текущего отчета (а не того о котором этот отчет) */  
  if(InfRepRecipientID > 0)
    Reciever = GetPartyName( InfRepRecipientID, true );
  elif( LOID != 0 )  
    logopr_new.rec.AutoKey = LOID;
    if( logopr_new.GetEQ() ) 

      GroundAdd.rec.SourceDocKind = 830;
      GroundAdd.rec.SourceDocID = LOID;
      GroundAdd.rec.Direction = EXITING;
      GroundAdd.rec.Division = SelfDivision;
      if (GroundAdd.GetEQ())
        if(GroundAdd.rec.PartyName == "")
          Reciever = GetPartyName( GroundAdd.rec.Party, true );
        else
          Reciever = GroundAdd.rec.PartyName;
        end;
      end;
    end;
  end;

  cmd = DL_RSDCommand(" select spgr.* from dspgrdoc_dbt gr, dspground_dbt spgr "
                    + "  where gr.t_SourceDocKind = 830 " //SP_DEPOPER_INFOPER  
                    + "    and gr.t_SourceDocID = ? " 
                    + "    and spgr.t_SPgroundID = gr.t_SPgroundID "
                    + "    and spgr.t_Direction <> " + EXITING);

  cmd.AddParam(logoprID);

  DataSet = cmd.Execute();

  stat = DataSet.moveNext();
  while( stat )
    Основание = Основание + GetLLVALUES(OBJTYPE_KINDDOC,DataSet.Kind) + " №" +
                DataSet.AltXld + " от " + string(date(DataSet.SignedDate):f);
    stat = DataSet.moveNext();
    if( stat )
      Основание = Основание + ", \n";
    end;
  end;
  if(Основание == "")
    Основание = "-";
  end;


  /* установка позиции файлов */
  ground.rec.SPgroundID = groundID;
  if ( ground.GetEQ() )
    logopr.rec.AutoKey = logoprID;
    if ( logopr.GetEQ() )
      /* причина отказа */
      notetext.rec.ObjectType = OBJTYPE_DEPOINFOPR;
      notetext.rec.NoteKind   = DP_NOTEKIND_REJECT;
      notetext.rec.DocumentID = string(logopr.rec.AutoKey);
      notetext.GetEQ();

      infopr.rec.spinfdocID = infoprID;
      if (infopr.GetEQ())
       spoper.rec.operationCode = infopr.rec.docType;
       spoper.GetEQ(); 
       person.rec.Oper = logopr.rec.oper;
       person.GetEQ();
      end;
    end;
  end;

  if( GetObject() == false )
    return false;
  end;

  if (isPrint == false)
    isPrint = true;
  end;

  
  if( CountRep > 0 )
    DP_AddPrintCell( RepInf,  "-------------------------------------------------------------------------------", 100, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
    RepInf.AddStr();
  end;

  if(logopr.rec.OperState == OP_OK)
    DP_StdHeaderLO_Ex(RepInf, RepFontStyleInf, "ОТЧЕТ ОБ ИСПОЛНЕНИИ ИНФОРМАЦИОННОЙ ОПЕРАЦИИ" , LOID, CountRep+1, PrintSubNum, date_begin, date_end, TableWidthInf, InfRepNUm, InfRepDate );
  else
    DP_StdHeaderLO_Ex(RepInf, RepFontStyleInf, "УВЕДОМЛЕНИЯ ОБ ОТКАЗЕ В ИСПОЛНЕНИИ ИНФОРМАЦИОННОЙ ОПЕРАЦИИ" , LOID, CountRep+1, PrintSubNum, date_begin, date_end, TableWidthInf, InfRepNUm, InfRepDate );
  end;

  DP_AddPrintCell( RepInf,  "Отправитель отчета", 23, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  DP_AddPrintCell( RepInf,  GetPartyName( {OurBank}, true ), 77, 0, "l:w:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "Получатель отчета", 23, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  DP_AddPrintCell( RepInf,  Reciever, 77, 0, "l:w:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "Поручение", 23, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  DP_AddPrintCell( RepInf,  "№" + ground.rec.AltXld + " от " + string(ground.rec.SignedDate:f) + 
                    ", Вх.№" + ground.rec.Xld + ", дата приема " + string(ground.rec.RegistrDate:f), 77, 0, "l:w:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "Инициатор" + logopr.rec.InitiatorName, 23, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  DP_AddPrintCell( RepInf,  logopr.rec.InitiatorName, 77, 0, "l:w:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "Депозитарная операция", 23, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  DP_AddPrintCell( RepInf,  spoper.rec.OperCode + ", информационная операция", 77, 0, "l:w:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "    основание", 23, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  DP_AddPrintCell( RepInf,  Основание, 77, 0, "l:w:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "                        ", 23, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  DP_AddPrintCell( RepInf,  GetLLVALUES(1200, logopr.rec.Product), 77, 0, "l:w:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "-------------------------------------------------------------------------------", 100, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();
  RepInf.AddEmptyStr();
  DP_AddPrintCell( RepInf,  "Объект отчета", 100, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "   тип        " + type_item, 100, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "   код        " + code_item, 100, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();
  RepInf.AddEmptyStr();

  DP_AddPrintCell( RepInf,  "Номер отчета по \"журналу отправленных\" отчетов и выписок", 38, 0, "l:w:" + RepFontStyleInf );
  DP_AddPrintCell( RepInf,  ground.rec.Xld, 40, 0, "l:w:" + RepFontStyleInf );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "Тип отчета по классификации \nдепозитария", 38, 0, "l:w:" + RepFontStyleInf );
  DP_AddPrintCell( RepInf,  spoper.rec.operationName, 40, 0, "l:w:" + RepFontStyleInf );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "Отчетный период", 38, 0, "l:w:" + RepFontStyleInf );
  DP_AddPrintCell( RepInf,  string(infopr.rec.fromDate:f) + " - " + string(infopr.rec.toDate:f), 40, 0, "l:w:" + RepFontStyleInf );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "Идентификаторы материалов, по которым выдается отчет", 38, 0, "l:w:" + RepFontStyleInf );
  DP_AddPrintCell( RepInf,  string(id_item) +" \n"+ string(id_item_ext), 40, 0, "l:w:" + RepFontStyleInf );
  RepInf.AddStr();
  RepInf.AddEmptyStr();

  if (logopr.rec.OperState == OP_OK)
    DP_AddPrintCell( RepInf,  "Обработка поручения                  \"ИСПОЛНЕНО\"", 100, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
    RepInf.AddStr();
    DP_AddPrintCell( RepInf,  "    запись в журнале операций        " + logopr.rec.OprXid, 100, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
    RepInf.AddStr();
  else 
    DP_AddPrintCell( RepInf,  "Обработка поручения        \"ОТКАЗАНО В ИСПОЛНЕНИИ\"", 100, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
    RepInf.AddStr();
    DP_AddPrintCell( RepInf,  "    причина отказа                   " + notetext.rec.Text, 100, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
    RepInf.AddStr();
  end;

  DP_AddPrintCell( RepInf,  "    дата                             " + string(logopr.rec.CarryDate:f), 100, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();
  DP_AddPrintCell( RepInf,  "    исполнитель                      " + person.rec.Name, 100, 0, "l:" + RepFontStyleInf, __is_app, REP_ELEM_STR );
  RepInf.AddStr();

  RepInf.AddEmptyStr();
  RepInf.AddEmptyStr();
  RepInf.AddEmptyStr();
  CountRep = CountRep + 1;

  RepInf.AddEmptyStr();
  RepInf.AddEmptyStr();
  DP_StdAuthPerson_Ex(RepInf, RepFontStyleInf);
  DP_StdDeposInfo_Ex(RepInf, RepFontStyleInf);
  DP_StdExecReport_Ex(RepInf, RepFontStyleInf);
  RepInf.AddEmptyStr();
end;

private macro CreateRep()

  var i=0;
  var cnt = 0;
  var Progress = CProgressBar;

  DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

  /* отсечь отчет об исполнении поручения */
  if (opkind == 28)
    DP_EndProtocol();                    //закончить протоколирование
    println("Нет данных, удовлетворяющих параметрам отчета");
    return 1;
  end;

  var stat = true, cur_stat = true, infopID = false;
  var PrintSubNum = false;
  var select, DataSet;

  if( (xld_begin != xld_end) or (xld_begin == "") or (xld_end == "") )
    PrintSubNum = true;
  end;

  select = DL_RSDCommand(GetSQLStrSpgr());

  Progress.Init(select.GetCount(), "Формирование отчета", "ОТЧЕТ ОБ ИСПОЛНЕНИИ ИНФОРМАЦИОННОЙ ОПЕРАЦИИ");

  DataSet = select.Execute();

  while(DataSet.moveNext())
    PrintInfOrderForm(DataSet.Draft, DataSet.Autokey, DataSet.recId, PrintSubNum);
    cnt = cnt + 1;
    i = Progress.Use();
  end;

  Progress.Remove();

  if( not isPrint)
    cnt = 0;  
  end;

  DP_AddProtocol(RepInf, "protocol");  //добавим протокол к уже сформированному отчету
  DP_EndProtocol();                    //закончить протоколирование

  return cnt;
end;


macro Order_INF(_GrDoc:integer,
                _date_begin:date,
                _date_end:date,
                _xld_begin:string,
                _xld_end:string,
                _opkind:integer,
                _opstatus:integer,
                _opstatus_begin:date,
                _opstatus_end:date,
                _ojd_begin:string,
                _ojd_end:string,
                _Department:integer,
                _Party:integer,
                _DepoAcc:integer,
                _DepoPart:integer,
                _DAccount:string,
                _FIID:integer,
                _InvCardID:integer,
                _LogOprID:integer,
                _ToExcel:bool,
                _is_app:bool,
                _Rep:@variant,
                _LogID:integer)

  var count;
                
  if((ValType(_Rep) != V_UNDEF) and (_Rep != null))
    RepInf = _Rep;
  end;

  GrDoc          =  _GrDoc;
  date_begin = _date_begin;
  date_end = _date_end;
  xld_begin      = _xld_begin;
  xld_end        = _xld_end;
  opstatus_begin = _opstatus_begin;
  opstatus_end   = _opstatus_end;
  ojd_begin = _ojd_begin;
  ojd_end = _ojd_end;
  opstatus = _opstatus;
  opkind = _opkind;
  Department = _Department;
  Party = _Party;
  DepoAcc = _DepoAcc;
  DepoPart = _DepoPart;
  DAccount = _DAccount;
  FIID = _FIID;
  InvCardID = _InvCardID;
  LOID = _LogOprID;
  ToExcel = _ToExcel;
  __is_app = _is_app;
  LogID   = _LogID;

  count = CreateRep();

  _Rep = RepInf;                                 

  return count;
end;            


macro Order_INFbyLogID(_LogID, _Recipient, _RepNum, _RepDate, _Rep:@variant)
   
   if((ValType(_Rep) != V_UNDEF) and (_Rep != null))
     RepInf = _Rep;
   end;

   GrDoc          =  0;
   date_begin     = date(0,0,0);
   date_end       = date(0,0,0);
   xld_begin      = "";
   xld_end        = "";
   opstatus_begin = date(0,0,0);
   opstatus_end   = date(0,0,0);
   ojd_begin      = "";
   ojd_end        = "";
   opstatus       = -1;
   opkind         = 0;
   Department     = {operdprt};
   Party          = -1;
   DepoAcc        = 0;
   DepoPart       = 0;
   DAccount       = "";
   FIID           = -1;
   InvCardID      = 0;
   LOID           = 0;
   ToExcel        = false; //здесь не важно
   __is_app       = true;


   LogID   = _LogID;
   InfRepRecipientID = _Recipient;
   InfRepNum  = _RepNum;
   InfRepDate = _RepDate;
    
   CreateRep();

   _Rep = RepInf;                                 

end;