/*
$Name: dpscan.mac
$Module: Депозитарий
$Description: Печать отчета сканирования
*/
import dpscanr, BankInter, DPInter, SPInter, secinter, spdraftr, or_rep_h, "dpsteprep.mac";
import RsbDataSet;

var MassRep;
var ToExcel;
var is_ap;

PRIVATE Record dlg(MPRINT, "dp_res.lbr")dialog;

PRIVATE CONST K_ESC = 27,
              K_F2  = 316,
              K_SPACE = 32,
              K_MBDOWN = 0,
              NO_KEY = -1;


//Поля диалога
PRIVATE CONST F_TOEXCEL      = 0, 
              F_IS_AP        = 1;

macro ОтчетИнфОп(Вызов,firstdoc,Статус,Сообщение,Режим)
     var Xld = "";

     RECORD splogopr_rec(splogopr);
     FILE splogopr(splogopr); 
     FILE spground(spground);

     setbuff (splogopr_rec,firstdoc);

     if (Вызов==Первый)
       if( Режим == РежимСоздИнфОп )
[             Отчет о групповом выпуске информационных операций];
       elif( Режим == РежимКопияИнфОп )
[             Отчет о групповом выпуске копий информационных операций];
       else
[             Отчет о групповом удалении информационных операций];
       end;
[+-------+-------+-------+----------------------------------------------------------------------------------------+];
[| ID    | №     | №     |                 Сообщение                                                              |];
[| поруч.| поруч.| ошибки|                                                                                        |];
[+-------+-------+-------+----------------------------------------------------------------------------------------+];
     elif (Вызов==Ошибка)
        if(splogopr_rec.AutoKey)
           spground.SPgroundID = splogopr_rec.Draft;
           if( GetEQ(spground) )
             Xld = spground.Xld;
           end;
        end;

        if(Статус == 0) 
           if(splogopr_rec.AutoKey)
              splogopr.AutoKey = splogopr_rec.AutoKey;
              if( GetEQ(splogopr) ) 
                 if( Режим == РежимСоздИнфОп ) 
                    if( splogopr.State == SC_SPDRAFT_STATUS_CLOSE )
                       Сообщение = "Операция выполнена успешно. Поручение закрыто.";
                    elif( splogopr.State == SC_SPDRAFT_STATUS_PREP )
                       Сообщение = "Операция не выполнена.";
                    end;
                 elif( Режим == РежимКопияИнфОп )
                    Сообщение = "Копия операции успешно выполнена.";
                 else
                    if( splogopr.State != SC_SPDRAFT_STATUS_PREP )
                       Сообщение = "Перемещение в отложенные не выполнено.";
                    elif(splogopr.State == SC_SPDRAFT_STATUS_PREP )
                       Сообщение = "Перемещение в отложенные выполнено успешно.";
                    end;
                 end;
              else
                 if( Режим != РежимСоздИнфОп )
                   Сообщение = "Поручение успешно удалено.";
                 end;
              end;
           end;
        end;
[|#######|#######|#######|########################################################################################|]
(splogopr_rec.AutoKey, Xld, Статус, Сообщение:w);

     elif (Вызов==Последний)            
[+-------+-------+-------+----------------------------------------------------------------------------------------+];
     end;
end;

macro ОтчетОпПоруч(Вызов,firstdoc,Статус,Сообщение,Режим)
     var Xld, Division;
     var ToExcel = false;
     
     RECORD spdraft_rec (spdraft); 
     FILE spdraft(spdraft); 
     FILE spground(spground);
     var dpgrrep = TBFile("dpgrrep.dbt");
     var extspgr = TBFile("spground.dbt");
     
     setbuff (spdraft_rec,firstdoc);
     if (Вызов==Первый)
[             Отчет о групповом удалении операций для поручений.];
[+-------+-------+-------+----------------------------------------------------------------------------------------+];
[| ID    | №     | №     |                 Сообщение                                                              |];
[| поруч.| поруч.| ошибки|                                                                                        |];
[+-------+-------+-------+----------------------------------------------------------------------------------------+];
     elif (Вызов==Ошибка)
        if(spdraft_rec.AutoKey)
           spground.SPgroundID = spdraft_rec.SPgroundID;
           if( GetEQ(spground) )
             Xld = spground.Xld;
           else
             Xld = "";
           end;
        end;

        if(Статус == 0)
           if(spdraft_rec.AutoKey)
              spdraft.AutoKey = spdraft_rec.AutoKey;
              if( GetEQ(spdraft) )
                 if( spdraft.Status != SC_SPDRAFT_STATUS_PREP )
                    Сообщение = "Операция не удалена.";
                 elif(spdraft.Status == SC_SPDRAFT_STATUS_PREP )
                    Сообщение = "Операция удалена успешно. Поручение отложено.";
                 end;
              else
                 Сообщение = "Операция и поручение удалены успешно.";
              end;
           end;
        end;
[|#######|#######|#######|########################################################################################|]
(spdraft_rec.AutoKey,Xld,Статус,Сообщение:w);
     elif (Вызов==Последний)
[+-------+-------+-------+----------------------------------------------------------------------------------------+];
     end;
end;


macro ОтчетОпКорпОп( Вызов,firstdoc,Статус,Сообщение, Режим )
     var Xld, err, Division;

     RECORD dpcrp(dpcorpop); 
     FILE dpcorpop(dpcorpop); 
     FILE spground(spground) key 6; 

     setbuff(dpcrp,firstdoc);

     if (Вызов==Первый)
       if(Режим == РежимСоздОпКорпОп)
[             Отчет о групповом создании операций для корпоративных операций.];
       elif(Режим == РежимУдалОпКорпОп)
[             Отчет о групповом удалении операций для корпоративных операций.];
       elif(Режим == РежимПересчВыплКорпОп)
[             Отчет о групповом перерасчете данных по выплатам для корпоративных операций.];
       elif(Режим == РежимУдалВыплКорпОп)
[             Отчет о групповом удалении данных по выплатам для корпоративных операций.];
       end;
[+-------+-------+-------+----------------------------------------------------------------------------------------+];
[| ID    | №     | №     |                 Сообщение                                                              |];
[| опер. | опер. | ошибки|                                                                                        |];
[+-------+-------+-------+----------------------------------------------------------------------------------------+];
     elif (Вызов==Ошибка)

        if(dpcrp.DocumentID)
           spground.SourceDocKind = dpcrp.DocKind;
           spground.SourceDocID = dpcrp.DocumentID;
           spground.Direction = 1;
           spground.Department = {OperDprt};
           GetRegistryValue( "DEPO\\OURDEPOSITOFFICE", V_INTEGER, Division, err );
           spground.Division = Division;

           if( GetEQ(spground) )
             Xld = spground.Xld;
           else
             Xld = "";
           end;
        end;

        if(Статус == 0) 
           if(dpcrp.DocumentID)
              dpcorpop.DocumentID = dpcrp.DocumentID;
              if( GetEQ(dpcorpop) )
                 if(Режим == РежимСоздОпКорпОп)
                    if(dpcorpop.State == SC_SPDRAFT_STATUS_CLOSE )
                       Сообщение = "Операция создана успешно. Корпоративная операция закрыта.";
                    elif(dpcorpop.State == SC_SPDRAFT_STATUS_OPEN )
                       Сообщение = "Операция создана успешно. Корпоративная операция не закрыта.";
                    elif( dpcorpop.State == SC_SPDRAFT_STATUS_PREP )
                       Сообщение = "Операция не создана.";
                    end;
                 elif(Режим == РежимУдалОпКорпОп)
                    if(dpcorpop.State != SC_SPDRAFT_STATUS_PREP )
                       Сообщение = "Операция не удалена.";
                    elif(dpcorpop.State == SC_SPDRAFT_STATUS_PREP )
                       Сообщение = "Операция удалена успешно. Корпоративная операция отложена.";
                    end;
                 elif(Режим == РежимПересчВыплКорпОп)
                    Сообщение = "Данные по выплатам успешно пересчитаны.";
                 elif(Режим == РежимУдалВыплКорпОп)
                    Сообщение = "Данные по выплатам успешно удалены.";
                 end;
              else
                 Сообщение = "Операция и корпоративная операция удалены успешно.";
              end;
           end;
        end;
[|#######|#######|#######|########################################################################################|]
(dpcrp.DocumentID,Xld,Статус,Сообщение:w);
     elif (Вызов==Последний)            
[+-------+-------+-------+----------------------------------------------------------------------------------------+];
     end;
end;


macro ОтчетОпВыпл( Вызов,firstdoc,Статус,Сообщение, Режим )
     var BriefCode, Xld, Division, err;

     RECORD dppmacc_rec(dppmacc); 
     FILE dppmacc(dppmacc); 
     FILE depoacnt(depoacnt);
     FILE spground(spground) key 6; 

     setbuff(dppmacc_rec,firstdoc);

     if (Вызов==Первый)
       if(Режим == РежимСоздОпВыпл)
[             Отчет о групповом создании операций для выплат по корпоративной операции.];
       elif(Режим == РежимУдалОпВыпл)
[             Отчет о групповом удалении операций для выплат по корпоративной операции.];
       end;
[+-------+-------+-------------------------+-------+----------------------------------------------------------------------------------------+];
[| ID    | №     |     Короткий код        | №     |                 Сообщение                                                              |];
[| опер. | опер. |     счета               | ошибки|                                                                                        |];
[+-------+-------+-------------------------+-------+----------------------------------------------------------------------------------------+];
     elif (Вызов==Ошибка)
        if(dppmacc_rec.HolderAccID)
          depoacnt.AutoKey = dppmacc_rec.HolderAccID;
          if( GetEQ(depoacnt) )
            BriefCode = depoacnt.BriefCode;
          else
            BriefCode = "";
          end;
        end;

        if(dppmacc_rec.DocumentID)
           spground.SourceDocKind = 870;
           spground.SourceDocID = dppmacc_rec.DocumentID;
           spground.Direction = 1;
           spground.Department = {OperDprt};
           GetRegistryValue( "DEPO\\OURDEPOSITOFFICE", V_INTEGER, Division, err );
           spground.Division = Division;

           if( GetEQ(spground) )
             Xld = spground.Xld;
           else
             Xld = "";
           end;
        end;

        if(Статус == 0) 
           if(dppmacc_rec.ID )
              dppmacc.HolderAccID = dppmacc_rec.ID;
              if( GetEQ(dppmacc) )
                 if(Режим == РежимСоздОпВыпл)
                    if(dppmacc.State > 1 )
                       Сообщение = "Операция создана успешно. Шаг выплаты доходов выполнен.";
                    else
                       Сообщение = "Операция создана успешно. Шаг выплаты доходов не выполнен.";
                    end;
                 elif(Режим == РежимУдалОпВыпл)
                    if(dppmacc.State <= 1 )
                       Сообщение = "Операция удалена успешно. Шаг выплаты ддоходов удален.";
                    end;
                 end;
              end;
           end;
        end;
[|#######|#######|#########################|#######|########################################################################################|]
(dppmacc_rec.DocumentID,Xld,BriefCode,Статус,Сообщение:w);
     elif (Вызов==Последний)            
[+-------+-------+-------+------------------------------------------------------------------------------------------------------------------+];
     end;
end;

/* создать исходящие сообщения по инвентарным операциям */
macro ОтчетВыгрузкаСообщ( Вызов,firstdoc,Статус,Сообщение, Режим )
  var Num;

  RECORD spdraft(spdraft); 
  FILE spground(spground) key 6;

  setbuff(spdraft,firstdoc);

  if (Вызов==Первый)
    if(Режим == РежимВыгрузкаСообщений)
[             Отчет о групповой выгрузке сообщений.];
    end;
[+----------------+-------+----------------------------------------------------------------------------------------+];
[| №              | №     |                 Сообщение                                                              |];
[| опер.          | ошибки|                                                                                        |];
[+----------------+-------+----------------------------------------------------------------------------------------+];
  elif (Вызов==Ошибка)
    ClearRecord(spground);
    spground.SPgroundID = spdraft.SPgroundID;
    if( GetEQ(spground) ) 
      Num = spground.Xld;
    else
      Num = "";
    end;

    if(Статус == 0) 
      Сообщение = "Сообщение сформировано";
    end;
[|################|#######|########################################################################################|]
(Num,Статус,Сообщение:w);
  elif (Вызов==Последний)            
[+----------------+-------+----------------------------------------------------------------------------------------+];
  end;
end;

/* отправить исходящие сообщения в МБР */
macro ОтчетОтправкаСообщ( Вызов,firstdoc,Статус,Сообщение, Режим )
  var Num;

  RECORD dpmsginf_rec(dpmsginf); 
  FILE dpmsgdoc(dpmsgdoc) key 1; 

  setbuff(dpmsginf_rec,firstdoc);

  if (Вызов==Первый)
    if(Режим == РежимОтправкаСообщ)
[             Отчет о групповой отправке сообщений.];
    end;
[+----------------+-------+----------------------------------------------------------------------------------------+];
[| №              | №     |                 Сообщение                                                              |];
[| опер.          | ошибки|                                                                                        |];
[+----------------+-------+----------------------------------------------------------------------------------------+];
  elif (Вызов==Ошибка)
    ClearRecord(dpmsgdoc);
    dpmsgdoc.MessageID = dpmsginf_rec.MessageID;
    if( dpmsginf_rec.MessageDirection == EXITING )
      dpmsgdoc.DocType = DPMSGDOC_SENDER;
    else
      dpmsgdoc.DocType = DPMSGDOC_RECEIVER;
    end;
    if( GetGE(dpmsgdoc) and 
        (dpmsgdoc.MessageID == dpmsginf_rec.MessageID) and
        ( ( (dpmsgdoc.DocType == DPMSGDOC_SENDER) and (dpmsginf_rec.MessageDirection == EXITING) ) or
          ( (dpmsgdoc.DocType == DPMSGDOC_RECEIVER) and (dpmsginf_rec.MessageDirection == ENTERING) ) ) ) 
      Num = dpmsgdoc.DocumentNumber;
    else
      Num = "";
    end;

    if(Статус == 0) 
      Сообщение = "Сообщение обработано";
    end;
[|################|#######|########################################################################################|]
(Num,Статус,Сообщение:w);
  elif (Вызов==Последний)            
[+----------------+-------+----------------------------------------------------------------------------------------+];
  end;
end;

/* откатить отправку сообщения в МБР */
macro ОтчетОткатСообщ( Вызов,firstdoc,Статус,Сообщение, Режим )
  var Num;

  RECORD dpmsginf_rec(dpmsginf); 
  FILE dpmsgdoc(dpmsgdoc) key 1; 

  setbuff(dpmsginf_rec,firstdoc);

  if (Вызов==Первый)
    if(Режим == РежимОткатитьИнфСообщение)
[             Отчет о групповом  откате отправки сообщений.];
    end;
[+----------------+-------+----------------------------------------------------------------------------------------+];
[| №              | №     |                 Сообщение                                                              |];
[| опер.          | ошибки|                                                                                        |];
[+----------------+-------+----------------------------------------------------------------------------------------+];
  elif (Вызов==Ошибка)
    ClearRecord(dpmsgdoc);
    dpmsgdoc.MessageID = dpmsginf_rec.MessageID;
    if( dpmsginf_rec.MessageDirection == EXITING )
      dpmsgdoc.DocType = DPMSGDOC_SENDER;
    else
      dpmsgdoc.DocType = DPMSGDOC_RECEIVER;
    end;
    if( GetGE(dpmsgdoc) and 
        (dpmsgdoc.MessageID == dpmsginf_rec.MessageID) and
        ( ( (dpmsgdoc.DocType == DPMSGDOC_SENDER) and (dpmsginf_rec.MessageDirection == EXITING) ) or
          ( (dpmsgdoc.DocType == DPMSGDOC_RECEIVER) and (dpmsginf_rec.MessageDirection == ENTERING) ) ) ) 
      Num = dpmsgdoc.DocumentNumber;
    else
      Num = "";
    end;

    if(Статус == 0) 
      Сообщение = "Сообщение обработано";
    end;
[|################|#######|########################################################################################|]
(Num,Статус,Сообщение:w);
  elif (Вызов==Последний)            
[+----------------+-------+----------------------------------------------------------------------------------------+];
  end;
end;

macro ОтчетГлобОп(Вызов,firstdoc,Статус,Сообщение,Режим)
     var Xld, Division, err;
     var dpgrrep = TBFile("dpgrrep.dbt");
     var extspgr = TBFile("spground.dbt");
     var OldDialogFlag;
     var RepFileName;
     var ScanFileName = GetTxtFileName("dpscanglobid");
     file ScanFIle() txt;
     var ThisRep = NULL;
     var isPrint = false;

     RECORD dpcorpop_rec (dpcorpop); 
     FILE dpcorpop(dpcorpop); 
     FILE spground(spground) key 6;

     setbuff (dpcorpop_rec,firstdoc);
     if (Вызов==Первый)
       if(ExistFile(ScanFileName))
         DelFile(ScanFileName);
       end;

       if( Режим == РежимВыполнГлобОп )
[             Отчет о групповом создании операций для поручений.];
       else
[             Отчет о групповом удалении операций для поручений.];
       end;
[+-------+-------+-------+----------------------------------------------------------------------------------------+];
[| ID    | №     | №     |                 Сообщение                                                              |];
[| поруч.| поруч.| ошибки|                                                                                        |];
[+-------+-------+-------+----------------------------------------------------------------------------------------+];
     elif (Вызов==Ошибка)
        if(dpcorpop_rec.DocumentID)
           spground.SourceDocKind = dpcorpop_rec.DocKind;
           spground.SourceDocID   = dpcorpop_rec.DocumentID;
           spground.Direction     = ENTERING;
           spground.Department    = {OperDprt};
           GetRegistryValue( "DEPO\\OURDEPOSITOFFICE", V_INTEGER, Division, err );
           spground.Division = Division;

           if( GetEQ(spground) )
             Xld = spground.Xld;
           else
             Xld = "";
           end;
        end;

        
        if(Статус == 0)
           if(dpcorpop_rec.DocumentID )
              dpcorpop.DocumentID = dpcorpop_rec.DocumentID;
              if( GetEQ(dpcorpop) )
                 if( Режим == РежимВыполнГлобОп )
                    if(dpcorpop.State == SC_SPDRAFT_STATUS_CLOSE )
                       Сообщение = "Операция создана успешно. Поручение закрыто.";
                    elif(dpcorpop.State == SC_SPDRAFT_STATUS_OPEN )
                       Сообщение = "Операция создана успешно. Поручение не закрыто.";
                    elif( dpcorpop.State == SC_SPDRAFT_STATUS_PREP )
                       Сообщение = "Операция не создана.";
                    end;
                 else
                    if( dpcorpop.State != SC_SPDRAFT_STATUS_PREP )
                       Сообщение = "Операция не удалена.";
                    elif(dpcorpop.State == SC_SPDRAFT_STATUS_PREP )
                       Сообщение = "Операция удалена успешно. Поручение отложено.";
                    end;
                 end;
              else
                 if( Режим != РежимВыполнГлобОп )
                   Сообщение = "Операция и поручение удалены успешно.";
                 end;
              end;
           end;
        end;
[|#######|#######|#######|########################################################################################|]
(dpcorpop_rec.DocumentID,Xld,Статус,Сообщение:w);
     elif (Вызов==Последний)            
[+-------+-------+-------+----------------------------------------------------------------------------------------+];

       GetRegistryValue("DEPO\\DEPOREPTOEXCEL", V_BOOL, ToExcel, err);
       if(ExistFile(ScanFileName)) 
         if(open(ScanFIle, ScanFileName))
           while(Next(ScanFIle))
             dpcorpop.DocumentID = Int(ScanFIle(0));
             if( GetEQ(dpcorpop) )
               GlobopRepPrintStep(dpcorpop.DocumentID, dpcorpop.DocKind, @ThisRep, false);
               isPrint = true;
             end;
           end;
         end;
         if(isPrint)
           OldDialogFlag = SetDialogFlag(1);
           if( ToExcel )
             ThisRep.PrintWinRep();
             ThisRep.ShowWinRep();
           else
             RepFileName = GetTxtFileName("dpginfex");
             ThisRep.SetFileNameTxt(RepFileName);
             ThisRep.PrintRep();
             ViewFile(RepFileName);
           end;
           SetDialogFlag(OldDialogFlag);
         end;
       end;
     end;
end;

/* Отчет о регистрации/отмене регистрации */
macro ОтчетРегистрСообщ( Вызов,firstdoc,Статус,Сообщение, Режим )
  var Num;

  RECORD spdraft(spdraft); 
  RECORD msginf(dpmsginf);
  FILE spground(spground) key 6;

  if( Режим == РежимРегистрироватьСообщение )
    setbuff(msginf,firstdoc);
  else
    setbuff(spdraft,firstdoc);
  end;

  if (Вызов==Первый)
    if(Режим == РежимВыполнРегистр)
[             Отчет о групповой регистрации/отмене регистрации поручений];
    elif( Режим == РежимРегистрироватьСообщение )
[             Отчет о групповой регистрации регистрации электронных сообщений];
    end;
[+----------------+-------+----------------------------------------------------------------------------------------+];
[| №              | №     |                 Сообщение                                                              |];
[| опер.          | ошибки|                                                                                        |];
[+----------------+-------+----------------------------------------------------------------------------------------+];
  elif (Вызов==Ошибка)
    ClearRecord(spground);
    if( Режим == РежимРегистрироватьСообщение )
      spground.SPGroundID = msginf.SPGroundID;
    else
      spground.SPGroundID = spdraft.SPGroundID;
    end;
    if( GetEQ(spground) ) 
      Num = spground.Xld;
    else
      Num = "";
    end;

    if(Статус == 0) 
      Сообщение = "Успешно";
    end;
[|################|#######|########################################################################################|]
(Num,Статус,Сообщение:w);
  elif (Вызов==Последний)            
[+----------------+-------+----------------------------------------------------------------------------------------+];
  end;
end;


//Обработка клавиш
PRIVATE MACRO PressKey(pn, CMD, ID, KEY)
var CM;

  if ((KEY == K_ESC) OR (KEY == K_F2))        
    CM = CM_CANCEL;    
  elif (KEY == K_SPACE)
    if(ID == F_TOEXCEL)
    
       if(pn.ToExcel == SET_CHAR)
          pn.ToExcel = UNSET_CHAR;
          ToExcel = false;
       else
          pn.ToExcel = SET_CHAR;
          ToExcel = true;
       end;

    elif(ID == F_IS_AP)
       
       if(pn.is_ap == SET_CHAR)
          pn.is_ap = UNSET_CHAR;
          is_ap = false;
       else
          pn.is_ap = SET_CHAR;
          is_ap = true;
       end;

    end;
       CM = CM_IGNORE;
  end;


  UpdateFields( pn );
  return CM;

END;

//Инициализация панели
PRIVATE MACRO InitPanel(pn, CMD, ID, KEY)
var Excel;
var err;
  
    GetRegistryValue( "DEPO\\DEPOREPTOEXCEL", V_BOOL, Excel, err );
    if(not err)
      
      if(Excel)
        pn.ToExcel = SET_CHAR;
        ToExcel = true;
      else
        pn.ToExcel = UNSET_CHAR;
        ToExcel = false;
      end;

      is_ap = true;
      pn.is_ap = SET_CHAR;

    else
       MsgBox("Не могу найти настройку выпуска отчетов в Excel");
    end;

 UpdateFields( pn );
 return CM_IGNORE;

END;

//обработчик панели
PRIVATE MACRO DP_AccountDlg( pn, CMD, ID, KEY )
var CM;

  if( CMD == DLG_INIT )
      CM = InitPanel( pn, CMD, ID, KEY );
  elif (CMD == DLG_KEY)
      CM = PressKey(pn, CMD, ID, KEY);
  end;

  return CM;
END;


PRIVATE MACRO DP_RunDialog()

  //Запуск панели на выполнение
  if(RunDialog(dlg, @DP_AccountDlg) == false)    
    return 0;
  end;

 return 1; 
END;


macro МассоваяПечать(Вызов,firstdoc,Статус,Сообщение,Режим)

var stat = 0;
var iDepoacnt;
var iAcnt_kind;
var iXld;
var iPayer;
var DataSet, query;
VAR ReportFileName, TblName = "dpscan_rep", errcode, errtext;
FILE ReportOutFile() txt write; /*файл вывода для отчета*/


RECORD spdraft_rec (spdraft);
FILE spdraft(spdraft);

      if(Вызов==Первый)
         DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

         MassRep = CMakeReport();       
         if(DP_RunDialog() != true) 
            return false;
         end;
         

      elif(Вызов==Ошибка)

         setbuff (spdraft_rec,firstdoc);

         query  = " SELECT ac.T_KIND,pm.T_PAYER,pm.T_PAYERDPNODE,gr.T_XLD,gr.t_registrdate " +
                  " FROM ddepoacnt_dbt ac,dpmpaym_dbt pm,dspdrmove_dbt sp,dspdraft_dbt spdr,dspground_dbt gr " +
                  " WHERE " +
                  " sp.T_DRAFTID = spdr.T_AUTOKEY AND " +
                  " sp.T_PAYMENTID = pm.T_PAYMENTID AND " +
                  " pm.T_PAYERDPNODE = ac.T_AUTOKEY AND " +
                  " spdr.T_SPGROUNDID = gr.T_SPGROUNDID AND " +
                  " sp.T_DRAFTID = " + spdraft_rec.Autokey;

         DataSet = TRsbDataSet(query);

         if( DataSet.MoveNext() )
            iAcnt_kind = DataSet.T_KIND;       
            iDepoacnt =  DataSet.T_PAYERDPNODE;
            iXld = DataSet.T_XLD;
            iPayer = DataSet.T_PAYER;

         
         stat = DepoDraftReportMass(spdraft_rec.Department,spdraft_rec.Branch,iXld,
                                      date(DataSet.RegistrDate),date(DataSet.RegistrDate),spdraft_rec.Kind_Operation,iAcnt_kind,
                                      iDepoacnt,iPayer,ToExcel,is_ap,@MassRep, spdraft_rec.AutoKey);

         end;
        
        
      elif(Вызов==Последний)
        
        if(stat==0)

          DP_AddProtocol(MassRep, "protocol");  //добавим протокол к уже сформированному отчету
          DP_EndProtocol();                 //закончить протоколирование

          if(ToExcel)
             MassRep.PrintWinRep();
             MassRep.ShowWinRep();
          else
             //MassRep.SetFileName("out");
             MassRep.PrintRep();
                          
             /*Create();

             ReportFileName = GetTxtFileName( TblName );

             if( Open( ReportOutFile, ReportFileName ) == false )
                errcode = status( errtext );
                MsgBox( "Ошибка при открытии файла отчета|"+ ReportFileName +"|(" + errcode + ") " + errtext);
                break;
             end;

             SetOutput( ReportFileName );
             MassRep.PrintRep();
             SetOutput( NULL, true );
             close( ReportOutFile );
             ViewFile( ReportOutFile );*/    
             
          end;       
          return stat;
        end;

      end;  

end;

/* точка входа в макрос!!! */
MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение,Режим)

     if( Режим == РежимУдалОпПоруч )
       ОтчетОпПоруч(Вызов,firstdoc,Статус,Сообщение,Режим);
     elif( ( Режим == РежимСоздОпКорпОп ) OR 
           ( Режим == РежимУдалОпКорпОп ) OR
           ( Режим == РежимПересчВыплКорпОп ) OR
           ( Режим == РежимУдалВыплКорпОп ) )
       ОтчетОпКорпОп(Вызов,firstdoc,Статус,Сообщение,Режим);
     elif( ( Режим == РежимСоздОпВыпл ) OR ( Режим == РежимУдалОпВыпл ) )
       ОтчетОпВыпл(Вызов,firstdoc,Статус,Сообщение,Режим);
     elif( ( Режим == РежимСоздИнфОп ) OR 
           ( Режим == РежимКопияИнфОп ) OR
           ( Режим == РежимУдалИнфОп ) )
       ОтчетИнфОп(Вызов,firstdoc,Статус,Сообщение,Режим);
     elif( Режим == РежимВыгрузкаСообщений )
       ОтчетВыгрузкаСообщ(Вызов,firstdoc,Статус,Сообщение,Режим);
     elif( Режим == РежимОтправкаСообщ )
       ОтчетОтправкаСообщ(Вызов,firstdoc,Статус,Сообщение,Режим);
     elif( ( Режим == РежимВыполнГлобОп ) OR ( Режим == РежимУдалГлобОп ))
       ОтчетГлобОп(Вызов,firstdoc,Статус,Сообщение,Режим);
     elif( Режим == РежимВыполнРегистр )
       ОтчетРегистрСообщ(Вызов,firstdoc,Статус,Сообщение,Режим);
     elif( Режим == РежимРегистрироватьСообщение )
       ОтчетРегистрСообщ(Вызов,firstdoc,Статус,Сообщение,Режим);
     elif( Режим == РежимОткатитьИнфСообщение )
       ОтчетОткатСообщ(Вызов,firstdoc,Статус,Сообщение,Режим);
     elif( Режим == РежимПечать )
       МассоваяПечать(Вызов,firstdoc,Статус,Сообщение,Режим);
     end;

END;
