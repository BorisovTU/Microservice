/* -@H------------------------------------------------------------------
*            Автоматизированная банковская система RS-Bank
*                 Библиотека интерпретируемых модулей
*                  Copyright (c) JV R-Style 1993-96
*
*   Description      :     Приём на закрытое хранение документарных ц\б
*   VSH 22.04.02     считать нужно сертификаты, а не ИК
*
*----------------------------------------------------------------------*/

import oprinter, SPInter, PaymInter, "dps_com.inc", "dp_util.mac";

file pmpaym("pmpaym") key 10;

/*Колличество сертификатов в платеже*/ 
macro CalcCertNum(PaymentID)
   var cmd;
   var num=0;
   
   cmd = DL_RSDCommand("select 1 from dinvlist_dbt where t_PaymentID = ?");
   cmd.AddParam(PaymentID);

   num = cmd.GetCount();
   return Num;
end;

macro InInterDepo(DACNTCode, ДатаНачала, ДатаКонца)
   var stat=0, NumCert=0, BnAccount = "";
   record  rBaseSum( "sfbassum.str" );

   pmpaym.DocKind     = SP_DEPOPER_RECORDER;
   pmpaym.ValueDate   = ДатаНачала;
   pmpaym.Purpose     = PurpBase;
   pmpaym.SubPurpose  = 0;
   stat = GetGE(pmpaym);
   While(  stat and
          (pmpaym.DocKind   == SP_DEPOPER_RECORDER) and
          (pmpaym.ValueDate <= ДатаКонца)
        )
      if(pmpaym.PaymStatus == PM_FINISHED)
         BnAccount = GetHeadAccForAcc(pmpaym.ReceiverDpNode);
         if( (pmpaym.Purpose == PurpBase) and 
             (pmpaym.IndoorStorage) and 
             (BnAccount == DACNTCode )     /*счет клиента на который приняты сертификаты*/
           )
            NumCert = CalcCertNum(pmpaym.PaymentID);
            if(NumCert) /*Возврат для этой сделки*/
               rBaseSum.baseType    = SF_BASETYPE_QUONT;
               rBaseSum.baseType2   = SF_BASETYPE_QUONT;
               rBaseSum.baseQuont   = NumCert;
               rBaseSum.baseQuont2  = NumCert;

               rBaseSum.BaseObjectType = OBJTYPE_OPERATIONDEPO;
               rBaseSum.BaseObjectID   = GetSpdraftByPayment(pmpaym.PaymentID);

               if( InsertSumList(rBaseSum) )
                  MacroError = 1;
                  MsgBox("Ошибка при вставке базовой суммы");
                  return;
               end;
            end;
         end;
      end;
      stat = Next(pmpaym);
   end;
end;

macro CalcServiceSum( sfcontr_addr, BeginDate, EndDate )
   record  rContr( sfcontr );
   file depoacnt(depoacnt) key 1;
   SetBuff( rContr, sfcontr_addr );
   InInterDepo(rContr.Object, BeginDate, EndDate);
   return;
end;
