/*
$Name:         nsdImpReports.mac
$Module:       Депозитарий
$Description:  Процедура импорта отчетов НРД
*/

import "nsdImpLib.mac";

private const PNFLD_LUCH = 0;
private const PNFLD_WCCA = 1;
private const PNFLD_SWIFT = 2;

private file reporth() dbf;

private var parm = "";
private var form = null;

private class (DL_CPanel) CNSDImportReportsForm()
    private var m_NeedRun = true;

    private macro Init()
        AddFieldProc(0, PNFLD_LUCH, DL_PNFLD_CHECKBOX, @DL_FldProc_CheckBox, "X");
        AddFieldProc(0, PNFLD_WCCA, DL_PNFLD_CHECKBOX, @DL_FldProc_CheckBox, "X");
        AddFieldProc(0, PNFLD_SWIFT, DL_PNFLD_CHECKBOX, @DL_FldProc_CheckBox, "X");
    end;

    private macro Check():Bool
        var Stat = true;

        if((GetFieldValue(PNFLD_LUCH) == "")
       and (GetFieldValue(PNFLD_WCCA) == "")
       and (GetFieldValue(PNFLD_SWIFT) == ""))
            MsgBox("Необходимо выбрать тип загружаемых отчетов");
            Stat = false;
        end;

        if(Stat)
            if(MsgBoxEx("Вы хотите загрузить отчёты НРД датой " + date_as_string(1, {CurDate}, false) + "?", MB_YES + MB_NO, IND_YES) == IND_NO)
                m_NeedRun = false;
            end;
        end;

        return Stat;
    end;

    macro Run(RunKey:Integer):Bool
        return (Run(RunKey) and m_NeedRun);
    end;

    initDL_CPanel("dp_res.lbr", "NSDIMPRP");
end;

private class CNSDProtocol(fileName, date1, time1, header1)
    private var m_FileName = fileName;
    private var m_Date = date1;
    private var m_Time = time1;
    private var m_Header = header1;
    private var m_Reports = TArray(), m_Reports1 = TArray(), m_Reasons = TArray();

    macro AddReport(report)
        m_Reports[m_Reports.Size] = report;
    end;

    macro AddReport1(report, reason)
        m_Reports1[m_Reports1.Size] = report;
        m_Reasons[m_Reasons.Size] = reason;
    end;

    macro AddError(errStr)
        DP_ProtocolAddStr(errStr);
    end;

    macro HasErrors()
        return DP_ProtocolIsEmpty();
    end;

    macro Print()
        var rep = CMakeReport();
        var i = 0;
        var fields = null;
        var widths = TArray();
        var head = CHeader();
        var width = 0;
        const style = "ex_FS(b)";
        const style1 = "ex_B(rlb)";
        var day = 0, month = 0, year = 0;
        var hour = 0, minute = 0, sec = 0;
        var name = "";

        fields = SplitString("№;;№ отчета НРД;;Дата отчета НРД;;Код операции;;Наименование операции;;Причина", ";;");
        while(i < fields.Size)
            widths[i] = StrLen(fields[i]);
            head.AddChild(fields[i], widths[i]);
            i = i + 1;
        end;

        width = head.CalcWidthHeader();

        DP_StdHeaderLO_Ex(rep, style, m_Header, 0, 0, false, m_Date, m_Date, width);

        if((m_Reports.Size == 0) and (m_Reports1.Size == 0))
            DP_AddPrintCell(rep, "Нет отчетов к регистрации", width, 0, "l:" + style, false, REP_ELEM_STR);
            rep.AddStr();

            rep.AddEmptyStr();
        else
            head.RemoveChild(5);

            if(m_Reports.Size != 0)
                DP_AddPrintCell(rep, "Перечень зарегистрированных отчетов:", width, 0, "l:" + style, false, REP_ELEM_STR);

                Rep.AutoScan("[\n" + head.CreateHeader() + "]");

                i = 0;
                while(i < m_Reports.Size)
                    DP_AddPrintCell(rep, i + 1, widths[0], 0, "l:" + style1);
                    DP_AddPrintCell(rep, m_Reports[i].RepNo, widths[1], 0, "l:" + style1);
                    DP_AddPrintCell(rep, date_as_string(1, m_Reports[i].RepDate, false), widths[2], 0, "l:" + style1);
                    DP_AddPrintCell(rep, m_Reports[i].OpCode, widths[3], 0, "l:" + style1);
                    DP_AddPrintCell(rep, m_Reports[i].OpName, widths[4], 0, "l:" + style1);
                    rep.AddStr();
                    i = i + 1;
                end;
            else
                DP_AddPrintCell(rep, "Успешно зарегистрированные отчеты отсутствуют", width, 0, "l:" + style, false, REP_ELEM_STR);
                rep.AddStr();
            end;

            rep.AddEmptyStr();

            head.AddChild(fields[5], widths[5]);

            if(m_Reports1.Size != 0)
                DP_AddPrintCell(rep, "Перечень незарегистрированных отчетов:", width, 0, "l:" + style, false, REP_ELEM_STR);

                Rep.AutoScan( "[\n" + head.CreateHeader() + "]" );

                i = 0;
                while(i < m_Reports1.Size)
                    DP_AddPrintCell(rep, i + 1, widths[0], 0, "l:" + style1);
                    DP_AddPrintCell(rep, m_Reports1[i].RepNo, widths[1], 0, "l:" + style1);
                    DP_AddPrintCell(rep, date_as_string(1, m_Reports1[i].RepDate, false), widths[2], 0, "l:" + style1);
                    DP_AddPrintCell(rep, m_Reports1[i].OpCode, widths[3], 0, "l:" + style1);
                    DP_AddPrintCell(rep, m_Reports1[i].OpName, widths[4], 0, "l:" + style1);
                    DP_AddPrintCell(rep, m_Reasons[i], widths[5], 0, "l:" + style1);
                    rep.AddStr();

                    i = i + 1;
                end;
            else
                DP_AddPrintCell(rep, "Все отчеты успешно зарегистрированы", width, 0, "l:" + style, false, REP_ELEM_STR);
                rep.AddStr();
            end;

            rep.AddEmptyStr();
        end;

        DP_StdFooter_Ex(rep, style, NULL, NULL, width);

        DP_AddProtocol(rep, "Ошибки импорта отчетов НРД");

        DateSplit(m_Date, day, month, year);
        TimeSplit(m_Time, hour, minute, sec);

        name = m_FileName + L_Z(String(day:2), 2) + L_Z(String(month:2), 2) + L_Z(String(year - Int(year / 100) * 100:2), 2)
             + "-" + L_Z(String(hour:2), 2) + "." + L_Z(String(minute:2), 2) + "." + L_Z(String(sec:2), 2);

        if(true)
            rep.SetFileName(name);
            rep.PrintWinRep();
            rep.ShowWinRep();
        else
            name = GetTxtFileName(name);
            SetOutput(name);
            rep.PrintRep();
            SetOutput(NULL, true);
            Close(name);
            ViewFile(name);
        end;
    end;

    macro Destructor()
        DP_EndProtocol();
    end;

    DP_BeginProtocol();
end;

private class (CNSDImportReportsBase) CNSDImportReportsLUCH()
    private macro getKind(kind)
        var err = 0;

        if(Index(",40,41,4C,43,44,", "," + m_Report.OpCode + ",") != 0)
            kind = DOCKIND_REPINFREQ;
        elif((Index(",90,91,16/3,26,26/1,20/2,80/3,81/3,35/2,35/3,36/2,36/3,84,80/1,81/1,80/5,81/5,10/80,10/83,20,70,10,16,16/1,16/2,35,36,36/35,37,80/2,35/1,10/81,", "," + m_Report.OpCode + ",") != 0)
          or (Index(m_Report.OpCode, "82/") == 1))
            kind = DOCKIND_REPEXTRREFREGDEP;
        elif(Index(",10/_,10/MS2,14/4,14/5,18/k,19/0,19/1,19/4,", "," + m_Report.OpCode + ",") != 0)
            kind = DOCKIND_CLEARINGREP;
        elif(Index(",10/50,10/51,10/51R,10/51E,10/51C,10/51A,10/51B,10/51N,10/53,68/CAIC,68/CAIR,", "," + m_Report.OpCode + ",") != 0)
            kind = DOCKIND_REPMESCORPACTN;
        elif(Index(",61,62,63,", "," + m_Report.OpCode + ",") != 0)
            kind = DOCKIND_LISTSECUROWNERS;
        else
            err = -1;
            m_Protocol.AddReport1(m_Report, "Код операции " + m_Report.OpCode + " не удовлетворяет условиям отбора");
        end;

        if(err == 0)
            SetParm(1, kind);
        end;

        return err;
    end;

    private macro getRefType(kind, refType)
        var err = 0;

        if(kind == DOCKIND_REPINFREQ)
            refType = REFOBJ_DPENTDOC_BH;
        elif(kind == DOCKIND_REPEXTRREFREGDEP)
            refType = REFOBJ_DPENTDOC_BH;
        elif(kind == DOCKIND_CLEARINGREP)
            refType = REFOBJ_DPENTDOC_BHK;
        elif(kind == DOCKIND_REPMESCORPACTN)
            refType = REFOBJ_DPENTDOC_BHC;
        elif(kind == DOCKIND_LISTSECUROWNERS)
            refType = REFOBJ_DPENTDOC_3KD;
        else
            err = -1;
        end;

        if(err == 0)
            SetParm(2, refType);
        end;

        return err;
    end;

    private macro getAcc(path, name, field, acc)
        var err = 0;
        var dl = null;
        file file1() dbf;
        var i = -1;

        dl = TDirList(m_Root + path + name, "F");

        if(dl.Count == 1)
            if(Open(file1, m_Root + path + name))
                if(Next(file1))
                    i = FldIndex(file1, field);

                    if(i != -1)
                        acc = Trim(String(file1(i)));

                        if(acc == "")
                            err = -1;
                        end;
                    else
                        err = -1;
                    end;
                else
                    err = -1;
                end;

                Close(file1);
            else
                err = 1;
                m_Protocol.AddError("Невозможно открыть файл " + m_Root + path + name);
            end;
        else
            err = -1;
        end;

        if(err == 0)
            SetParm(4, acc);
        end;

        return err;
    end;

    private macro checkAcc(path)
        var err = 0;
        var name = "", field = "";
        var acc = "";

        if(Index(",40,41,43,", "," + m_Report.OpCode + ",") != 0)
            name = "hdsttds.dbf";
            field = "Depo_acc_c";
            err = getAcc(path, name, field, acc);
        else
            err = -1;
        end;

        if(err == -1)
            if(Index(",43,", "," + m_Report.OpCode + ",") != 0)
                name = "oprest.dbf";
                field = "Depo_acc_c";
                err = getAcc(path, name, field, acc);
            else
                err = -1;
            end;
        end;

        if(err == -1)
            if(Index(",44,", "," + m_Report.OpCode + ",") != 0)
                name = "oprp.dbf";
                field = "Depo_acc_c";
                err = getAcc(path, name, field, acc);
            else
                err = -1;
            end;
        end;

        if(err == -1)
            if(Index(",10,10/80,20,20/2,36,36/2,36/35,80/1,81/1,80/3,81/3,80/5,81/5,68/CAIC,", "," + m_Report.OpCode + ",") != 0)
                name = "htrans.dbf";
                field = "Facc_code";
                err = getAcc(path, name, field, acc);

                if(err == -1)
                    name = "htrans.dbf";
                    field = "Depo_acc_c";
                    err = getAcc(path, name, field, acc);
                end;

                if(err == -1)
                    name = "arest_sc.dbf";
                    field = "Facc_code";
                    err = getAcc(path, name, field, acc);
                end;

                if(err == -1)
                    name = "arest_sc.dbf";
                    field = "Depo_acc_c";
                    err = getAcc(path, name, field, acc);
                end;
            else
                err = -1;
            end;
        end;

        if(err == -1)
            if(Index(",16/2,16/3,", "," + m_Report.OpCode + ",") != 0)
                name = "htrpawn.dbf";
                field = "Facc_code";
                err = getAcc(path, name, field, acc);

                if(err == -1)
                    name = "htrpawn.dbf";
                    field = "Tacc_code";
                    err = getAcc(path, name, field, acc);
                end;
            else
                err = -1;
            end;
        end;

        if(err == -1)
            if(Index(",16,16/1,26,26/1,", "," + m_Report.OpCode + ",") != 0)
                name = "htrans.dbf";
                field = "Facc_code";
                err = getAcc(path, name, field, acc);

                if(err == -1)
                    name = "htrans.dbf";
                    field = "Tacc_code";
                    err = getAcc(path, name, field, acc);
                end;
            else
                err = -1;
            end;
        end;

        if(err == -1)
            if(Index(",35,35/1,35/2,35/3,36,36/2,36/3,36/35,37,68/CAIR,", "," + m_Report.OpCode + ",") != 0)
                name = "hbltrans.dbf";
                field = "Facc_code";
                err = getAcc(path, name, field, acc);
            else
                err = -1;
            end;
        end;

        if(err == -1)
            if(Index(",84,", "," + m_Report.OpCode + ",") != 0)
                name = "prmdp.dbf";
                field = "Depo_acc_c";
                err = getAcc(path, name, field, acc);
            else
                err = -1;
            end;
        end;

        if(err == -1)
            if(Index(",10/_,10/50,10/51,10/51A,10/51B,10/51C,10/51E,10/51N,10/51R,10/53,10/80,10/81,14/4,14/5,18/k,80/1,81/1,80/5,81/5,80/2,", "," + m_Report.OpCode + ",") != 0)
                name = "anyacc.dbf";
                field = "Depo_acc_c";
                err = getAcc(path, name, field, acc);
            else
                err = -1;
            end;
        end;

        if(err == -1)
            if(Index(",19/0,19/1,19/4,", "," + m_Report.OpCode + ",") != 0)
                name = "dealinfo.dbf";
                field = "Depo_acc_c";
                err = getAcc(path, name, field, acc);
            else
                err = -1;
            end;
        end;

        if(err == 0)
            if(Index(acc, "L") != 2)
                err = -1;
                m_Protocol.AddReport1(m_Report, "Найденый в поле " + field + " файла " + m_Root + path + name + " счет депо " + acc + " не удовлетворяет условиям отбора");
            end;
        elif(err == -1)
            err = 0;
        end;

        return err;
    end;

    private macro insertGroundDoc(path)
        var err = 0, errStr = "";
        var id = 0;
        var refType = 0, refID = 0;
        var xld = "", direction = ENTERING, division = DP_SelfDivision, kind = 0, registrDate = m_Date;
        var spgrPrm = TRecHandler("spgroundprm.rec");

        err = getKind(kind);

        if(err == 0)
            err = getRefType(kind, refType);
        end;

        if(err == 0)
            err = checkAcc(path);
        end;

        if(err == 0)
            err = checkAltXld(kind, m_Report.RepNo, m_Report.RepDate);
        end;

        if(err == 0)
            err = getRefID(refType, refID);
        end;

        if(err == 0)
            err = generateXld(refID, kind, xld, direction, {OperDprt}, division, registrDate);
        end;

        if(err == 0)
            spgrPrm.Clear();

            spgrPrm.rec.Division        = division;
            spgrPrm.rec.Direction       = direction;
            spgrPrm.rec.Kind            = kind;
            spgrPrm.rec.Xld             = xld;
            spgrPrm.rec.RegistrDate     = registrDate;
            spgrPrm.rec.RegistrTime     = m_Time;
            spgrPrm.rec.DocLog          = LLCLASS_KINDDOC_DOCDEPO;
            spgrPrm.rec.PartyID         = m_Party;
            spgrPrm.rec.PartyCodeKind   = PTCK_CONTR;
            spgrPrm.rec.AltXld          = m_Report.RepNo;
            spgrPrm.rec.SignedDate      = m_Report.RepDate;
            spgrPrm.rec.SignedTime      = m_Report.RepTime;
            spgrPrm.rec.Proxy           = UnknownParty;
            spgrPrm.rec.ProxyCodeKind   = PTCK_CONTR;
            spgrPrm.rec.DeliveryKind    = CodeFor("R"); // Электронный документ
            spgrPrm.rec.BackOffice      = "Z"; // Депозитарий
            spgrPrm.rec.Comment         = "ОТЧЕТ НКО АО НРД №" + m_Report.RepNo + " от " + date_as_string(1, m_Report.RepDate, false) + ". Операция " + m_Report.OpCode;
            spgrPrm.rec.BeginningDate   = m_Report.RepDate;
            spgrPrm.rec.Department      = {OperDprt};
            spgrPrm.rec.Branch          = {OperDprtNode};
            spgrPrm.rec.UserLog         = SPGRUSERLOG_CLIENT;
            spgrPrm.rec.IsMakeAuto      = SET_CHAR;
            spgrPrm.rec.TechAutoDoc     = TECHAUTODOC_NSDIMPREP;
            spgrPrm.rec.Deponent        = UnknownParty;
            spgrPrm.rec.MsgNumber       = m_Report.RepNo;
            spgrPrm.rec.MsgDate         = m_Report.RepDate;
            spgrPrm.rec.MsgTime         = m_Report.RepTime;

            if(DP_InsertGroundDocument(id, spgrPrm, true, errStr) != 0)
                err = 1;
                m_Protocol.AddError(errStr);
                m_Protocol.AddReport1(m_Report, errStr);
                restoreXld(refID, xld);
            end;
        end;

        return err;
    end;

    private macro processFile(path)
        var err = 0;

        if(not Next(reporth))
            err = 1;
            m_Protocol.AddError("Отсутствует запись в файле " + m_Root + path + "reporth.dbf");
        end;

        if(err == 0)
            m_Report.RepNo = Trim(String(reporth.Reg_No));
            m_Report.RepDate = Date(reporth.Reg_Date);
            m_Report.RepTime = Time(reporth.TReg_Date);
            m_Report.OpCode = ToOEM(Trim(String(reporth.Ord_Type_I)), true);
            m_Report.OpName = ToOEM(Trim(String(reporth.Ord_Type_N)), true);
        end;

        if(err == 0)
            err = insertGroundDoc(path);

            if(err > 0)
                err = 1;
            elif(err == 0)
                m_Protocol.AddReport(m_Report);
            end;
        end;

        return err;
    end;

    private macro processDirs(path)
        var err = 0, err2 = 0;
        var dl = null;
        var i = 0;

        dl = TDirList(m_Root + path, "FD");
        if(dl.Count > 2)
            dl = TDirList(m_Root + path + "*", "D");

            while(i < dl.Count)
                if((dl.Name(i) != ".")
               and (dl.Name(i) != "..")
               and (dl.Name(i) != DONE_DIR)
               and (dl.Name(i) != ERR_DIR))
                    err2 = processDirs(path + dl.Name(i) + "\\");
                    if(err2 >= 0)
                        RemoveDir(m_Root + path + dl.Name(i));
                    end;
                    if(err2 == -1)
                        err = -1;
                    end;
                end;
                i = i + 1;
            end;

            dl = TDirList(m_Root + path + "reporth.dbf", "F");

            if(dl.Count == 1)
                if(Open(reporth, m_Root + path + "reporth.dbf"))
                    err2 = processFile(path);
                    Close(reporth);
                else
                    err2 = 1;
                    m_Protocol.AddError("Невозможно открыть файл " + m_Root + path + "reporth.dbf");
                end;

                if(err2 >= 0)
                    err2 = moveFiles(m_Root + path, getPathToDir(IIF(err2 == 0, DONE_DIR, ERR_DIR)) + path, "*.*");
                end;

                if(err2 == -1)
                    err = -1;
                end;

                UseProgress(m_Count = m_Count + 1);
            end;
        else
            err = -1;
        end;

        return err;
    end;

    macro Run()
        var err = 0;
        var nrdCode = "";

        GetRegistryValue("DEPO\\NSD\\NSD_REPORT_PATH_LUCH", V_STRING, m_Root, err);

        if(m_Root == "")
            err = 1;
            m_Protocol.AddError("Не указан путь к сетевой папке для загрузки отчётов, полученных по ЛУЧу.|Настройка DEPO\\NSD\\NSD_REPORT_PATH_LUCH");
        end;

        if(err == 0)
            m_Root = m_Root + "\\";

            GetRegistryValue("SECUR\\NRD_CODE", V_STRING, nrdCode, err);

            if(nrdCode == "")
                err = 1;
                m_Protocol.AddError("Ошибка при получении значения настройки \"SECUR\\NRD_CODE\"");
            end;
        end;

        if(err == 0)
            m_Party = ПолучитьКодСубъекта(nrdCode, PTCK_CONTR, err);

            if(err != 0)
                m_Protocol.AddError("Не найден субъект с кодом \"" + nrdCode + "\"");
            end;
        end;

        if(err == 0)
            InitProgress(-1, "Идет импорт отчётов, полученных по ЛУЧу...", "Импорт отчётов, полученных по ЛУЧу");

            processDirs(".\\");

            RemProgress();
        end;

        m_Protocol.Print();

        return m_Protocol.HasErrors();
    end;

    initCNSDImportReportsBase;
    m_Protocol = CNSDProtocol("REGREPORTIN_LUCH", m_Date, m_Time, "Протокол процедуры автоматической регистрации входящих документов, полученных через СЭД \"ЛУЧ\"");
end;

private class (CNSDImportReportsBaseXML) CNSDImportReportsWCCA()
    private macro getKind(kind)
        var err = 0;

        if((m_Report.OpCode == "seev.001.001.04")
        or (m_Report.OpCode == "seev.002.001.04")
        or (m_Report.OpCode == "seev.006.001.04")
        or (m_Report.OpCode == "seev.008.001.04")
        or (m_Report.OpCode == "seev.031.001.04")
        or (m_Report.OpCode == "seev.034.001.04")
        or (m_Report.OpCode == "seev.035.001.04")
        or (m_Report.OpCode == "seev.036.001.05")
        or (m_Report.OpCode == "seev.032.001.04")
        or (m_Report.OpCode == "seev.039.001.04")
        or (m_Report.OpCode == "seev.041.001.04")
        or (m_Report.OpCode == "seev.038.001.03")
        or (m_Report.OpCode == "semt.015.001.04")
        or (m_Report.OpCode == "semt.014.001.03"))
            kind = DOCKIND_CORPACTNREQ;
        else
            err = -1;
            m_Protocol.AddReport1(m_Report, "Код операции " + m_Report.OpCode + " не удовлетворяет условиям отбора");
        end;

        if(err == 0)
            SetParm(1, kind);
        end;

        return err;
    end;

    private macro getRefType(kind, refType)
        var err = 0;

        if(kind = DOCKIND_CORPACTNREQ)
            refType = REFOBJ_DPENTDOC_BKD;
        else
            err = -1;
        end;

        if(err == 0)
            SetParm(2, refType);
        end;

        return err;
    end;

    private macro checkAcc()
        var err = 0;
        var tag = "";
        var acc = "";

        tag = "AcctId";
        err = getAcc("//" + tag, acc);

        if(err == -1)
            tag = "SfkpgAcct/Id";
            err = getAcc("//" + tag, acc);
        end;

        if(err == -1)
            tag = "SfkpgAcct";
            err = getAcc("//" + tag, acc);
        end;

        if(err == 0)
            if(Index(acc, "L") != 2)
                err = -1;
                m_Protocol.AddReport1(m_Report, "Найденый в поле " + tag + " счет депо " + acc + " не удовлетворяет условиям отбора");
            end;
        elif(err == -1)
            err = 0;
        end;

        return err;
    end;

    private macro insertGroundDoc()
        var err = 0, errStr = "";
        var id = 0;
        var refType = 0, refID = 0;
        var xld = "", direction = ENTERING, division = DP_SelfDivision, kind = 0, registrDate = m_Date;
        var spgrPrm = TRecHandler("spgroundprm.rec");

        err = getKind(kind);

        if(err == 0)
            err = getRefType(kind, refType);
        end;

        if(err == 0)
            err = checkAcc();
        end;

        if(err == 0)
            err = checkAltXld(kind, m_Report.RepNo, m_Report.RepDate);
        end;

        if(err == 0)
            err = getRefID(refType, refID);
        end;

        if(err == 0)
            err = generateXld(refID, kind, xld, direction, {OperDprt}, division, registrDate);
        end;

        if(err == 0)
            spgrPrm.Clear();

            spgrPrm.rec.Division        = division;
            spgrPrm.rec.Direction       = direction;
            spgrPrm.rec.Kind            = kind;
            spgrPrm.rec.Xld             = xld;
            spgrPrm.rec.RegistrDate     = registrDate;
            spgrPrm.rec.RegistrTime     = m_Time;
            spgrPrm.rec.DocLog          = LLCLASS_KINDDOC_DOCDEPO;
            spgrPrm.rec.PartyID         = m_Party;
            spgrPrm.rec.PartyCodeKind   = PTCK_CONTR;
            spgrPrm.rec.AltXld          = m_Report.RepNo;
            spgrPrm.rec.SignedDate      = m_Report.RepDate;
            spgrPrm.rec.SignedTime      = m_Report.RepTime;
            spgrPrm.rec.Proxy           = UnknownParty;
            spgrPrm.rec.ProxyCodeKind   = PTCK_CONTR;
            spgrPrm.rec.DeliveryKind    = CodeFor("R"); // Электронный документ
            spgrPrm.rec.BackOffice      = "Z"; // Депозитарий
            spgrPrm.rec.Comment         = "ОТЧЕТ НКО АО НРД №" + m_Report.RepNo + " от " + date_as_string(1, m_Report.RepDate, false);
            spgrPrm.rec.BeginningDate   = m_Report.RepDate;
            spgrPrm.rec.Department      = {OperDprt};
            spgrPrm.rec.Branch          = {OperDprtNode};
            spgrPrm.rec.UserLog         = SPGRUSERLOG_CLIENT;
            spgrPrm.rec.IsMakeAuto      = SET_CHAR;
            spgrPrm.rec.TechAutoDoc     = TECHAUTODOC_NSDIMPREP;
            spgrPrm.rec.Deponent        = UnknownParty;
            spgrPrm.rec.MsgNumber       = m_Report.RepNo;
            spgrPrm.rec.MsgDate         = m_Report.RepDate;
            spgrPrm.rec.MsgTime         = m_Report.RepTime;

            if(DP_InsertGroundDocument(id, spgrPrm, true, errStr) != 0)
                err = 1;
                m_Protocol.AddError(errStr);
                m_Protocol.AddReport1(m_Report, errStr);
                restoreXld(refID, xld);
            end;
        end;

        return err;
    end;

    private macro processFile()
        var err = 0;
        var val = "";

        m_XMLFile.GetXML().setProperty("SelectionLanguage", "XPath");

        m_Report.RepNo = m_XMLFile.GetXML().selectSingleNode("//BizMsgIdr").nodeTypedValue;
        val = m_XMLFile.GetXML().selectSingleNode("//CreDt").nodeTypedValue;
        m_Report.RepDate = Date(Int(SubStr(val, 9, 2)), Int(SubStr(val, 6, 2)), Int(SubStr(val, 1, 4)));
        m_Report.RepTime = Time(Int(SubStr(val, 12, 2)), Int(SubStr(val, 15, 2)), Int(SubStr(val, 18, 2)));
        m_Report.OpCode = m_XMLFile.GetXML().selectSingleNode("//MsgDefIdr").nodeTypedValue;
        m_Report.OpName = "Запрос корпоративного действия";

        if(err == 0)
            err = insertGroundDoc();

            if(err > 0)
                err = 1;
            elif(err == 0)
                m_Protocol.AddReport(m_Report);
            end;
        end;

        return err;
    end;

    macro Run()
        var err = 0;
        var nrdCode = "";

        GetRegistryValue("DEPO\\NSD\\NSD_REPORT_PATH_WCCA", V_STRING, m_Root, err);

        if(m_Root == "")
            err = 1;
            m_Protocol.AddError("Не указан путь к сетевой папке для загрузки отчётов, полученных по web-кабинету КД.|Настройка DEPO\\NSD\\NSD_REPORT_PATH_WCCA");
        end;

        if(err == 0)
            m_Root = m_Root + "\\";

            GetRegistryValue("SECUR\\NRD_CODE", V_STRING, nrdCode, err);

            if(nrdCode == "")
                err = 1;
                m_Protocol.AddError("Ошибка при получении значения настройки \"SECUR\\NRD_CODE\"");
            end;
        end;

        if(err == 0)
            m_Party = ПолучитьКодСубъекта(nrdCode, PTCK_CONTR, err);

            if(err != 0)
                m_Protocol.AddError("Не найден субъект с кодом \"" + nrdCode + "\"");
            end;
        end;

        if(err == 0)
            InitProgress(-1, "Идет импорт отчётов, полученных по web-кабинету КД...", "Импорт отчётов, полученных по web-кабинету КД");

            processDirs(".\\");

            RemProgress();
        end;

        m_Protocol.Print();

        return m_Protocol.HasErrors();
    end;

    initCNSDImportReportsBaseXML;
    m_Protocol = CNSDProtocol("REGREPORTIN_WCCA", m_Date, m_Time, "Протокол процедуры автоматической регистрации входящих документов, полученных через web-кабинет КД");
end;

private class CNSDImportReportsSWIFT()
    macro Run()
    end;
end;

if(IsShedulerRunning)
    // -luch:yes -wcca:yes -swift:yes
    if(GetCmdLineParm("luch", parm, V_STRING) and (Trim(StrUpr(parm)) == "YES"))
        CNSDImportReportsLUCH.Run();
    end;
    if(GetCmdLineParm("wcca", parm, V_STRING) and (Trim(StrUpr(parm)) == "YES"))
        CNSDImportReportsWCCA.Run();
    end;
    if(GetCmdLineParm("swift", parm, V_STRING) and (Trim(StrUpr(parm)) == "YES"))
        CNSDImportReportsSWIFT.Run();
    end;
else
    form = CNSDImportReportsForm();
    while(form.Run())
        if(form.GetFieldValue(PNFLD_LUCH) == "X")
            CNSDImportReportsLUCH.Run();
        end;
        if(form.GetFieldValue(PNFLD_WCCA) == "X")
            CNSDImportReportsWCCA.Run();
        end;
        if(form.GetFieldValue(PNFLD_SWIFT) == "X")
            CNSDImportReportsSWIFT.Run();
        end;
    end;
end;

Exit(1);

