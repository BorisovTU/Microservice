/*
$Name:        dpclntreg_form.mac
$Module:      Депозитарий
$Description: Форма отчета "Регистр депонентов"
*/

import "DlRepForm_h.mac", "dlmisc.mac";

const PNFLD_DPCLNTREG_REPORTDATE    = 0;
const PNFLD_DPCLNTREG_CLIENTSTATUS  = 1;
const PNFLD_DPCLNTREG_WITHPARTITION = 2;
const PNFLD_DPCLNTREG_TOEXCEL       = 3;

const PNFLD_CLIENTSTATUS = 100;

const DPCLNTREG_CLIENTSTATUS_ALL       = 0;
const DPCLNTREG_CLIENTSTATUS_SERVED    = 1;
const DPCLNTREG_CLIENTSTATUS_NOTSERVED = 2;

private var DPCLNTREG_CLIENTSTATUS = TArray();
DPCLNTREG_CLIENTSTATUS[DPCLNTREG_CLIENTSTATUS_ALL]       = "Все";
DPCLNTREG_CLIENTSTATUS[DPCLNTREG_CLIENTSTATUS_SERVED]    = "Обслуживается";
DPCLNTREG_CLIENTSTATUS[DPCLNTREG_CLIENTSTATUS_NOTSERVED] = "Не обслуживается";

private macro FldProc_ClientStatus(pThis:DL_CPanel, Cmd:Integer, Key:Integer, FldShowValue:@Variant, FldRealValue:@Variant):Integer
  if(Cmd == DLG_INIT) // Установка значения в поле
    if(FldRealValue == null) // инициализация по умолчанию
      FldRealValue = DPCLNTREG_CLIENTSTATUS_ALL;
    end;

    FldShowValue = DPCLNTREG_CLIENTSTATUS[FldRealValue];
  elif(Cmd == DLG_CHANGEVALUE) // значение поля было изменено
  elif((Cmd == DLG_KEY) and (Key == KEY_F3)) // Вызов листалки и заполнение FldShowValue и FldRealValue
    DL_ListString(null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                  DPCLNTREG_CLIENTSTATUS[DPCLNTREG_CLIENTSTATUS_ALL],       DPCLNTREG_CLIENTSTATUS_ALL,
                  DPCLNTREG_CLIENTSTATUS[DPCLNTREG_CLIENTSTATUS_SERVED],    DPCLNTREG_CLIENTSTATUS_SERVED,
                  DPCLNTREG_CLIENTSTATUS[DPCLNTREG_CLIENTSTATUS_NOTSERVED], DPCLNTREG_CLIENTSTATUS_NOTSERVED);
  elif((Cmd == DLG_KEY) and (Key == KEY_SPACE))
    FldRealValue = DPCLNTREG_CLIENTSTATUS_ALL;
    FldShowValue = DPCLNTREG_CLIENTSTATUS[FldRealValue];
  end;

  return CM_DEFAULT;
end;

class (DL_CPanel) DP_CLNTREG_Panel
  private macro Init()
    var ToExcel = true;
    var err = 0;

    GetRegistryValue("DEPO\\DEPOREPTOEXCEL", V_BOOL, ToExcel, err);
    if(err != 0)
      MsgBox("Не могу найти настройку выпуска отчетов в Excel");
    end;

    AddFieldProc(0, PNFLD_DPCLNTREG_REPORTDATE,    DL_PNFLD_ENDDATE,   @DL_FldProc_EndDate, {curdate});
    AddFieldProc(0, PNFLD_DPCLNTREG_CLIENTSTATUS,  PNFLD_CLIENTSTATUS, @FldProc_ClientStatus, DPCLNTREG_CLIENTSTATUS_SERVED, 22, 6);
    AddFieldProc(0, PNFLD_DPCLNTREG_WITHPARTITION, DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox, UNSET_CHAR);
    AddFieldProc(0, PNFLD_DPCLNTREG_TOEXCEL,       DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox, IIF(ToExcel, SET_CHAR, UNSET_CHAR));
  end;

  initDL_CPanel("dp_res.lbr", "DPCLNREG");
end;
