/*
$Name:         dpcr_100.mac
$Module:       Депозитарий
$Description:  Обслуживание счета депо/Оформление выплаты
*/

//!!!С 3149 больше не используется

import InsCarryDoc, OprInter, "dppmaccfd.mac", "dp_voop.mac";

/* Буферы, передаваемые в макрос */
record OprServDoc( dpcorpop ); /*корпортивная операция*/
record dppmacc( dppmacc );     /*Платежная инструкция получателя*/

PRIVATE MACRO GetPartyINN(PartyID):STRING
  var INN = "";
  var KPP = "";

  SplitFullINN(ПолучитьКодСубъекта(PartyID, PTCK_INN), INN, KPP);

  return INN;
END;

PRIVATE MACRO GetPartyCode(PartyID, CodeKind):STRING
  return ПолучитьКодСубъекта(PartyID, CodeKind);
END;

MACRO CorSchem(PmRac):INTEGER
  var Schem = 0;
  if((PmRac.rec.BankCorrID > 0) and (PmRac.rec.BankCorrID != {OurBank}))
    Schem = ПолучитьКорсхемуПоУмолчанию(PmRac.rec.BankCorrID, PmRac.rec.Currency, 1);
  elif((PmRac.rec.BankIC > 0) and (PmRac.rec.BankIC != {OurBank}))
    Schem = ПолучитьКорсхемуПоУмолчанию(PmRac.rec.BankIC, PmRac.rec.Currency, 1);
  end;

  return Schem;
END;

macro ExecuteStep( d, dpac )
  var err = 0;
  var pmObj = NULL;
  var PmRac = TRecHandler("dppmrac.dbt");
  var PayerAcc = TRecHandler("account.dbt");

  if((dppmacc.PaymentWay == PW_OUTPAYMENT) OR
     (dppmacc.PaymentWay == PW_INPAYMENT) OR
     (dppmacc.PaymentWay == PW_CONSOLIDATED))

    pmObj = RsbPayment(0);

    pmObj.DocKind    = SP_DEPOPER_DIVIDEND;
    pmObj.DocumentID = OprServDoc.DocumentID;

    pmObj.SubPurpose = dppmacc.HolderAccID;

    pmObj.OrderFIID   = OprServDoc.Currency;
    pmObj.OrderAmount = dppmacc.Amount;

    pmObj.BaseFIID   = OprServDoc.Currency;
    pmObj.BaseAmount = dppmacc.Amount;

    pmObj.Number     = string(pmObj.SubPurpose);

    if(not CheckDepoChargeIncome())

      pmObj.ValueDate = OprServDoc.ValueDate;

      err = DP_GetPmRac(OprServDoc.DocumentID, dppmacc.HolderAccID, PmRac);
      if(err)
        msgbox("Не найдена ПИ получателя средств");
      end;

      if(DP_FindAccount(OprServDoc.Account, OprServDoc.Currency, 1, PayerAcc) != 0)
        msgbox("Не найден счет плательщика");
        err = 1;
      end;

      pmObj.Purpose = PM_PURP_SECUR_INCOME;
      pmObj.Ground  = PmRac.rec.Description;
    else
      var FD = DP_PmaccFD(dppmacc.DocumentID, dppmacc.HolderAccID);

      pmObj.ValueDate = {curdate};

      if(false == FD.OpenAccount( "-Расчеты, Выплаты по ц/б", NULL, NULL, PayerAcc, pmObj.ValueDate, OprServDoc.Currency ))
        msgbox("Ошибка при открытии счета");
        err = 1;
      end;

      if(not err)
        var HolderAccID = dppmacc.HolderAccID;

        if({curdate} > OprServDoc.EndValueDate)
          HolderAccID = 0; //Для поиска ПИ эмитента
        end;

        err = DP_GetPmRac(OprServDoc.DocumentID, HolderAccID, PmRac);
        if(err)
          if(HolderAccID == 0)
            msgbox("Не найдена ПИ эмитента для возврата дохода");
          else
            msgbox("Не найдена ПИ получателя средств");
          end;
        end;
      end;

      if(not err)
        pmObj.Purpose = PM_PURP_SECUR_INCOME;
        pmObj.Ground  = PmRac.rec.Description;
        if(HolderAccID == 0)
          pmObj.Purpose = PM_PURP_RETURNINCOME;
          pmObj.Ground  = "Возврат средств";
        end;
      end;
    end;

    if(not err)
      var Payer       = PayerAcc.rec.Client;
      var PayerBankID = {OurBank};

      pmObj.SetPayerPI (PAYMENTS_GROUP_INTERNAL,
                        PayerBankID,
                        PTCK_CONTR,
                        GetPartyCode(PayerBankID, PTCK_CONTR),
                        GetPartyName(PayerBankID),
                        "",
                        PayerAcc.rec.Code_Currency,
                        1,
                        PayerAcc.rec.Account,
                        Payer,
                        GetPartyName(Payer),
                        GetPartyINN(Payer),
                        PTCK_CONTR,
                        GetPartyCode(Payer, PTCK_CONTR),
                        0,
                        0);

      pmObj.SetReceiverPI (PAYMENTS_GROUP_UNDEF,
                           PmRac.rec.BankIC,
                           PmRac.rec.BankCodeKind,
                           PmRac.rec.BankCode,
                           PmRac.rec.BankName,
                           PmRac.rec.CorrAcc,
                           PmRac.rec.Currency,
                           1,
                           PmRac.rec.Account,
                           PmRac.rec.Receiver,
                           PmRac.rec.RecName,
                           PmRac.rec.INN,
                           PTCK_CONTR,
                           GetPartyCode(PmRac.rec.Receiver, PTCK_CONTR),
                           CorSchem(PmRac),
                           0);

      pmObj.Department  = OprServDoc.Department;
      pmObj.PaymStatus  = PM_PREPARING;
      pmObj.Oper        = {oper};
      pmObj.Netting     = UNSET_CHAR;

      if( not DP_SetPaymentGround( pmObj.GetPM_PAYM(), OprServDoc, dppmacc ) )
        err = 1;
      end;
    end;
  end;

  return err;
end;
