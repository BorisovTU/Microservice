/*
$Name: dpcomis.mac
$Module: Депозитарий
$Description: отчет - "Взимаемые комиссии депозитария"
*/
import SecInter, DealsInter, SFInter, dpstdrep, RsbDataSet;

const ALG_SFDEFCOMSTATUS  = 2379; /* состояние удержанной комиссии */
const ALG_SV_SERVICE_TYPE = 2375; /* Тип услуги */

var PrintedHeader = false;

private var oproper  =  TBFile("oproper.dbt");        
private var person   =  TBFile("person");

var HeadTable = "┌────────────────────┬────────────────┬───────────────────────────┬──────────────────────────┬────────────────────┐\n"+
                "│    Код комиссии    │   Дата взим.   │       Сумма комиссии      │         Сумма НДС        │    Тип взимания    │\n"+
                "├────────────────────┼────────────────┼───────────────────────────┼──────────────────────────┼────────────────────┤";
                
var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

/* Структура с данными для отчета*/
class SourceData( _iDepart:integer, _ClientID:integer, _ContractID:integer, 
                  _CommFeeType:integer, _CommNumber:integer, 
                  _dateMin:date, _dateMax:date)
  var 
      iDepart       =  _iDepart,
      ClientID      =  _ClientID,     
      ContractID    =  _ContractID,
      CommFeeType   =  _CommFeeType,
      CommNumber    =  _CommNumber, 
      dateMin       =  _dateMin,      
      dateMax       =  _dateMax;

  var PrintHeadClient = false,   
      PrintHeadContr  = false,   
      ReportRun       = false;

  var beg_operation_id = 0; /*ID первой операции, попадающей в отчетный период*/
end;

macro PrintHeadClient( Data )
  record  rParty( party );
  var     Title = "", ClientCode = "";
  var rPartyShortName = "";

  if ( PrintedHeader == false )
     DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "КОМИССИИ ПО ДОГОВОРУ ОБСЛУЖИВАНИЯ ДЕПОЗИТАРИЯ", 0, 0, false, Data.dateMin, Data.dateMax, tablewidth );
     PrintedHeader = true;
  end;

  ClientCode = ПолучитьКодСубъекта( Data.ClientID, PTCK_CONTR );
  if(  (ClientCode == "") OR
       (ПолучитьСубъекта( Data.ClientID, rParty ) != 0) 
    )
     msgbox( "Ошибка при определении клиента с идентификатором ", Data.ClientID );
     rPartyShortName  = "";
  else 
     rPartyShortName = rParty.ShortName
  end;
  Title = ClientCode + "  " + rPartyShortName;

  Rep.AddEmptyStr();
  DP_AddPrintCell( Rep,   "Депонент: " + Title, tablewidth, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
  Rep.AddStr();
  Data.ReportRun       = true;
  Data.PrintHeadClient = true;
end;

macro PrintHeadContr( Data, sfcontr )
   var  Title;

   if( Data.PrintHeadClient == false )  PrintHeadClient( Data );  end;

   if( Data.PrintHeadContr == false )
      Title = sfcontr.Number + "  от  " + string(sfcontr.DateConc:f) +
              "      Счет депо " + sfcontr.Object;


     DP_AddPrintCell( Rep,  "Договор №  " + Title, Rep.GetColWidthTable(0)+Rep.GetColWidthTable(1)+Rep.GetColWidthTable(2)+Rep.GetColWidthTable(3)+Rep.GetColWidthTable(4)+4, 0, "l:" + RepFontStyleTitel );
     Rep.AddStr();

  end;

  Data.PrintHeadContr = true;
end;

macro PrintSum( Data, Sum )
  if( Sum == 0 ) return "";
/*  elif( Data.apostrSum == true )  return string( Sum:a );*/
  else return Sum;
  end;
end;

macro PrintCommLine( Data, sfcontr, sfcomiss, CommDate, CommStatus, CommSum, NDSSumm )
   PrintHeadContr( Data, sfcontr );

   DP_AddPrintCell( Rep,  sfcomiss.Code, 0, 0, "l:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  CommDate, 0, 0, "l:f:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  PrintSum( Data, CommSum ), 0, 2, "r:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  PrintSum( Data, NDSSumm ), 0, 2, "r:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep,  DL_GetNameAlg( ALG_SV_SERVICE_TYPE, sfcomiss.feeType ), 0, 0, "r:" + RepFontStyleTitel );
   Rep.AddStr();
end;

macro УдержанныеКомиссии( Data, sfcontr, sfconcom )
   file sfdefcom( sfdefcom ) key 1;
   file oprsfcom( oprsfcom ) key 3;
   file sfcomiss( sfcomiss );
   /*file oproper( oproper );*/
   file oprstep( oprstep );
   var  continue_cicle;
   var osFactDate = date(0, 0, 0);

   ClearRecord( sfcomiss );
   sfcomiss.FeeType = sfconcom.FeeType;
   sfcomiss.Number  = sfconcom.CommNumber;
   if( not GetEQ(sfcomiss) )
      MsgBox( "Не найдена комиссия для договора ", sfcontr.Number );
      sfcomiss.Code = "";
      sfcomiss.feeType = 0;
   end;

   /*Единовременные комиссии*/
   ClearRecord( oprsfcom );
   oprsfcom.SfContrID  = sfcontr.Id;
   oprsfcom.FeeType    = sfconcom.FeeType;
   oprsfcom.CommNumber = sfconcom.CommNumber;
   continue_cicle = GetGE( oprsfcom );
   while( continue_cicle AND
          (oprsfcom.SfContrID  == sfcontr.Id ) AND
          (oprsfcom.FeeType    == sfconcom.FeeType ) AND
          (oprsfcom.CommNumber == sfconcom.CommNumber ) )

        if( oprsfcom.Sum != $0 )
          ClearRecord(oprstep);
          oprstep.ID_Operation = oprsfcom.ID_Operation;
          oprstep.ID_Step      = oprsfcom.ID_Step; 

          if( (oprstep.ID_Operation > 0) AND (oprstep.ID_Step > 0) ) /* если комиссия на шаге */
            if( not GetEQ(oprstep) )
              MsgBox( "Не найден шаг операции");
            else
              osFactDate = oprstep.Fact_date;
            end;

            if( (osFactDate != date(0,0,0))  AND
              (osFactDate >= Data.dateMin) AND
              (osFactDate <= Data.dateMax) )
              PrintCommLine( Data, sfcontr, sfcomiss, osFactDate, 1, oprsfcom.Sum, oprsfcom.SumNDS );
            end;

          elif( oprstep.ID_Operation ) /*комиссия навешена непосредственно на операцию*/
            oproper.clear();
            oproper.keynum = 0;
            oproper.rec.ID_Operation = oprsfcom.ID_Operation;
            if( (GetEQ(oproper) == true) AND (oproper.rec.Start_Date >= Data.dateMin) AND (oproper.rec.Start_Date <= Data.dateMax) )
               PrintCommLine( Data, sfcontr, sfcomiss, oproper.rec.Start_Date, 1, oprsfcom.Sum, oprsfcom.SumNDS, oprsfcom.FIID_Sum );
            end;
          end; 
        end;
      continue_cicle = Next(oprsfcom);
   end;

   /*Периодические комиссии*/
   ClearRecord( sfdefcom );
   sfdefcom.ConID      = sfcontr.Id;
   sfdefcom.FeeType    = sfconcom.FeeType;
   sfdefcom.CommNumber = sfconcom.CommNumber;
   sfdefcom.DateFee    = Data.dateMin;
   continue_cicle = GetGE( sfdefcom );
   while( continue_cicle AND
          (sfdefcom.ConID      == sfcontr.Id ) AND
          (sfdefcom.FeeType    == sfconcom.FeeType ) AND
          (sfdefcom.CommNumber == sfconcom.CommNumber ) AND
          (sfdefcom.DateFee    <= Data.dateMax) )
      if( sfdefcom.CommSum != $0 )
         PrintCommLine( Data, sfcontr, sfcomiss, sfdefcom.DateFee, sfdefcom.Status, sfdefcom.CommSum, sfdefcom.NDSSum );
      end;
      continue_cicle = Next(sfdefcom);
   end;
end;

macro КомиссииПоДоговору( Data, sfcontr )
   file sfconcom( sfconcom );
   var  continue_cicle;

   Data.PrintHeadContr = false;
   if( ( (sfcontr.DateClose != date(0,0,0)) AND 
         (sfcontr.DateClose < Data.dateMin) ) OR 
       (sfcontr.DateConc > Data.dateMax) )
      return;
   end;

   if( sfcontr.Department != Data.iDepart )
     return;
   end;

   ClearRecord( sfconcom );
   sfconcom.ObjectID      = sfcontr.Id;

   if( Data.CommFeeType > 0 ) /*Задана конкретная комиссия */
      sfconcom.FeeType    = Data.CommFeeType;
      sfconcom.CommNumber = Data.CommNumber;
      sfconcom.ObjectType = OBJTYPE_SFCONTR;
      if( GetEQ(sfconcom) )
         УдержанныеКомиссии( Data, sfcontr, sfconcom );
      end;
   else /*По всем комиссиям договора */
      continue_cicle = GetGE( sfconcom );
      while( continue_cicle AND (sfconcom.ObjectID == sfcontr.Id ) AND (sfconcom.ObjectType == OBJTYPE_SFCONTR) )
         УдержанныеКомиссии( Data, sfcontr, sfconcom );
         continue_cicle = Next( sfconcom );
      end;
   end;
end;

macro КомиссииПоВсемДоговорамКлиента( Data:SourceData )
  file sfcontr( sfcontr ) key 2;
  var  continue_cicle;

  ClearRecord( sfcontr );
  sfcontr.PartyID  = Data.ClientID;
  sfcontr.ServKind = PTSK_DEPOS;
  sfcontr.FIID     = -1;
  continue_cicle = GetGE( sfcontr );

  while( continue_cicle AND
         (sfcontr.PartyID  == Data.ClientID) AND
         (sfcontr.ServKind == PTSK_DEPOS) )
     КомиссииПоДоговору( Data, sfcontr );
     continue_cicle = Next(sfcontr);
  end;
end;

macro КомиссииПоКлиенту( Data:SourceData )
  file sfcontr( sfcontr );

  Message( "Выполняется отчет \"Комиссии по договору обслуживания депозитария\" " );
  Data.PrintHeadClient = false;

  if( Data.ContractID > 0 )  /* Задан договор */
     ClearRecord( sfcontr );
     sfcontr.Id = Data.ContractID;
     if( GetEQ(sfcontr) )  
        КомиссииПоДоговору( Data, sfcontr );
     else
        msgbox( "Не найден договор обслуживания." );
     end;
  else 
     КомиссииПоВсемДоговорамКлиента( Data );
  end;

end;

macro КомиссииПоВсемКлиентам( Data:SourceData )
  var  Client, query;
  var  NumClient = 0;

  InitProgress( NRecords(Client), "Отчет \"Комиссии по договору обслуживания депозитария\"", 
                "Просмотр клиентов депозитарного обслуживания" );

  query = " select distinct t_PartyID, t_ServiceKind, t_Department  from dclient_dbt where t_ServiceKind = " + PTSK_DEPOS + " and t_Department = " + Data.iDepart;
  if( Data.ClientID > 0 )
    query = query + " and t_PartyID = " + Data.ClientID;
  end;

  Client = TRsbDataSet(query);

  while( Client.moveNext() ) 
     Data.ClientID = Client.PartyID;
     КомиссииПоКлиенту( Data );

     NumClient = NumClient + 1;
     UseProgress( NumClient );
  end;
  RemProgress;
end;

/*Запуск отчета*/
MACRO dpComReport( iDepart:integer, ClientID:integer, ContractID:integer, 
                   CommFeeType:integer, CommNumber:integer, 
                   BeginDate:date, EndDate:date, ToExcel: bool)

  Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
  DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

  var Data = SourceData( iDepart, ClientID, ContractID, CommFeeType, CommNumber, 
                         BeginDate, EndDate);

  /*  получим id первой из операций попадающих в отчетный период              */
  /*  для этого переберем всех операционистов и проверим для каждого операции */ 
  person.Clear();           
  person.Rewind();   
  while( person.Next() )             
     oproper.clear();
     oproper.keynum = 3; /*Init_Oper,Start_Date*/
     oproper.rec.Init_Oper    = person.rec.Oper;
     oproper.rec.Start_Date   = BeginDate;
     if( (oproper.GetGE() == true) AND ((Data.beg_operation_id > oproper.rec.ID_Operation ) OR (Data.beg_operation_id == 0)) )
        Data.beg_operation_id = oproper.rec.ID_Operation;
     end;  
  end;


                         
  if( ClientID >= 0 ) КомиссииПоКлиенту( Data );
  else КомиссииПоВсемКлиентам( Data );
  end;

  if( Data.ReportRun == false )
     if(not DP_ProtocolIsEmpty())
       DP_AddPrintCell(Rep, "Нет начисленных или оплаченных комиссий, соответствующих параметрам отчета.", 80, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR);
       Rep.AddStr();

       DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
       DP_EndProtocol();                 //закончить протоколирование

       if( ToExcel )
         Rep.PrintWinRep();
         Rep.ShowWinRep();
       else
         Rep.PrintRep();
       end;
     else
       DP_EndProtocol();                 //закончить протоколирование
     end;

     MsgBox( "Нет начисленных или оплаченных комиссий, соответствующих параметрам отчета.\n");
  else 
     DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

     DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
     DP_EndProtocol();                 //закончить протоколирование

     if( ToExcel )
       Rep.SetZoomType( ZOOM_TYPE_A4L );
       Rep.PrintWinRep();
       Rep.ShowWinRep();
     else
       Rep.PrintRep();
     end;
  end;
  return 1;

END;             
