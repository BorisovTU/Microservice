/*
$Name: dpinfdoc.mac
$Module: Депозитарий
$Description: Печать формы информационной операции
*/

import dpstdrep, ptinter;

const RepFontStyleTitel =  "ex_FS(b)";

private const spREPACC        = 20, // Выписка о состоянии счета депо
              spDPEXTR        =  3, // Выписка об операциях по счету депо
              spDPACNTOPR     = 48; // Отчет об операциях по счету депо

// Получить код и название счета/раздела счета депо
private macro FindDEPOACNT(AutoKey)
  file depoacnt("depoacnt");

  Clearrecord(depoacnt);
  depoacnt.AutoKey = AutoKey;
  
  if( getEQ(depoacnt) )
    return depoacnt.Code + " - " + depoacnt.Name;
  end;

  return "-";
end;


// Получить имя операциониста
private macro FindOperName(OperID)
  file persn("person");

  Clearrecord(persn);
  persn.Oper = OperID;
  
  if( getEQ(persn) )
    return persn.Name;
  end;

  return "-";
end;


// Напечатать одну строку с выбором
private macro PrintOnePick(Rep, IsPicked, PickName, FromDate, ToDate)
  Rep.AddPrintCell( "[", 1, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddPrintCell( IsPicked, 1, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddPrintCell( "]", 1, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddPrintCell( PickName, 65, 0, "l:", REP_ELEM_STR );

  if(ToDate == null)
    Rep.AddPrintCell( FromDate, 30, 0, "c:" + RepFontStyleTitel, REP_ELEM_STR );
  else
    Rep.AddPrintCell( "с", 2, 0, "c:", REP_ELEM_STR );
    Rep.AddPrintCell( FromDate, 12, 0, "c:" + RepFontStyleTitel, REP_ELEM_STR );
    Rep.AddPrintCell( "по", 2, 0, "c:", REP_ELEM_STR );
    Rep.AddPrintCell( ToDate, 12, 0, "c:" + RepFontStyleTitel, REP_ELEM_STR );
  end;

  Rep.AddStr();
end;

// Получить наименование информационной операции
private macro GetInfOpername(docType)
  file spoper("spoper");

  ClearRecord( spoper );
  spoper.operationCode = docType;
  if( GetEQ(SpOper) )
    return spoper.OperationName;
  end;

  return "-";
end;


// Точка входа в макрос
macro DepoInfReport(indocaddr: memaddr, logopraddr: memaddr, infopraddr: memaddr, outdocaddr: memaddr)
  var Rep = CMakeReport();
  var ToExcel = false;
  var err;
  var ownername = "", IsPicked = "", PickName = "", FromDate = "", ToDate = "";
  var WasPicked = false;

  record indoc("spground");
  record logopr("splogopr");
  record infopr("spinfopr");
  record outdoc("spground");

  record party ("party");

  GetRegistryValue("DEPO\\DEPOREPTOEXCEL", V_BOOL, ToExcel, err);
  if( err != 0 )
    ToExcel = false;
  end;

  SetBuff(indoc, indocaddr); // Первичный документ
  SetBuff(logopr, logopraddr); // Информационная операция
  SetBuff(infopr, infopraddr); // Параметры выпуска отчета
  SetBuff(outdoc, outdocaddr); // Исходящий документ

  DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "ПОРУЧЕНИЕ НА ИНФОРМАЦИОННУЮ ОПЕРАЦИЮ", 0, 0, false );
  Rep.AddEmptyStr();


  Rep.AddPrintCell( "Номер у отправителя", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddPrintCell( "№" + indoc.AltXld + " от " + indoc.SignedDate, 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();

  Rep.AddPrintCell( "Инициатор", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddPrintCell( logopr.InitiatorCode + " " + logopr.InitiatorName, 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();

  Rep.AddPrintCell( "Отправитель поручения", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddPrintCell( indoc.PartyCode + " " + indoc.PartyName, 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();

  Rep.AddPrintCell( "Получатель поручения", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddPrintCell( FindOperName(indoc.Receptionist), 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();

  Rep.AddPrintCell( "Номер входящий", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddPrintCell( "№" + indoc.Xld + " от " + indoc.RegistrDate + " время " + indoc.RegistrTime, 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();

  Rep.AddEmptyStr();

  if( NOT ПолучитьСубъекта(infopr.PartyID, party) )
    ownername = party.ShortName;
  else
    ownername = "-";
  end;
  Rep.AddPrintCell( "Депонент", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddPrintCell( ownername, 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();

  Rep.AddPrintCell( "№ счета депо", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddPrintCell( FindDEPOACNT(infopr.DepoAcc), 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();

  Rep.AddPrintCell( "№ раздела счета депо", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddPrintCell( FindDEPOACNT(infopr.DepoPart), 70, 0, "l:"+ RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();

  Rep.AddEmptyStr();
  Rep.AddEmptyStr();

  Rep.AddPrintCell( "Прошу предоставить (нужное отметить X или V):", 80, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
  Rep.AddStr();

  Rep.AddEmptyStr();

  if(infopr.docType == spREPACC) // Выписка о состоянии счета депо
    WasPicked = true;
    IsPicked = "X";
    FromDate = infopr.FromDate;
  else
    IsPicked = "";
    FromDate = "";
  end;

  PrintOnePick(Rep, IsPicked, "Выписку по счету депо по состоянию на дату", FromDate);
  Rep.AddEmptyStr();

  if(infopr.docType == spDPEXTR) // Выписка об операциях по счету депо
    WasPicked = true;
    IsPicked = "X";
    FromDate = infopr.FromDate;
    ToDate = infopr.ToDate;
  else
    IsPicked = "";
    FromDate = "";
    ToDate   = "";
  end;
  PrintOnePick(Rep, IsPicked, "Выписку об операциях по счету депо за период", FromDate, ToDate);
  Rep.AddEmptyStr();

  if(infopr.docType == spDPACNTOPR) // Отчет об операциях по счету депо
    WasPicked = true;
    IsPicked = "X";
    FromDate = infopr.FromDate;
    ToDate = infopr.ToDate;
  else
    IsPicked = "";
    FromDate = "";
    ToDate   = "";
  end;
  PrintOnePick(Rep, IsPicked, "Отчет о совершенных депозитарных операциях", FromDate, ToDate);
  Rep.AddEmptyStr();

  if(NOT WasPicked) // Что-то другое
    IsPicked = "X";
    PickName = "Другое (" + GetInfOperName(infopr.docType) + ")";
    FromDate = infopr.FromDate;
    ToDate = infopr.ToDate;
  else
    IsPicked = "";
    PickName = "Другое";
    FromDate = "";
    ToDate   = "";
  end;
  PrintOnePick(Rep, IsPicked, PickName, FromDate, ToDate);
  Rep.AddEmptyStr();

  DP_StdFooter_Ex(Rep, RepFontStyleTitel, null, null, 80);

  if( ToExcel )
    Rep.PrintWinRep();
    Rep.ShowWinRep();
  else
    Rep.PrintRep();
  end;
end;
