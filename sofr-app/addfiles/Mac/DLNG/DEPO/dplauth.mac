/*
$Name:         dplauth.mac
$Module:       Депозитарий
$Description:  Отчет "Список доверенностей"
*/
import deposerv, "spacform.mac";

private var HeaderStr = "Список зарегистрированных в депозитарии доверенностей";

private var iCount = 0;
private var iDepart          : integer,
            rep_date         : date,
            signed_date_beg  : date,
            signed_date_end  : date,
            term_date_beg    : date,
            term_date_end    : date,
            show_void_auth   : bool,
            sort_by_termdate : bool;

private var HeadTable = "┌─────────────────────────┬────────────┬───────────────────────┬──────────────┬────────────────────────────────┬───────────────┐\n"+
                        "│Доверитель               │Категория   │№ и дата выдачи        │Действ. до    │Доверенное лицо                 │Статус         │\n"+
                        "├─────────────────────────┼────────────┼───────────────────────┼──────────────┼────────────────────────────────┼───────────────┤";
                
private var tablewidth = Index(HeadTable, "\n");
private var Rep = CMakeReport(HeadTable);
private const RepFontStyleTitel =  "ex_FS(b)";


// Получить статус доверенности
private macro GetProcurState(ProcurData)
  var query, Select, DataSet, i;
  var ProcurState = "";
  var IsOpen = false, IsClose = false;

  if(ProcurData.Cancelled == "X") // явно проставлен признак "отозвана"
    ProcurState = "отозвана";
  elif((date(ProcurData.TerminateDate) != date(0,0,0)) AND (date(ProcurData.TerminateDate) < rep_date)) // дата окончания действия меньше даты отчета
    ProcurState = "недействительна";
  else 
    // определим к каким счетам депо подключена доверенность, 
    // если только к закрытым значит "счет закрыт", 
    // если есть хоть один открытый - "действительна"
    // если нет счетов - пусто
    query = " SELECT DISTINCT t_State " +
            "   FROM (SELECT DISTINCT CASE WHEN (root.t_CloseDate = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
            "                                    OR root.t_CloseDate > ? /*1*/) " +
            "                            THEN " + SPSK_OPEN +
            "                            ELSE " + SPSK_CLOSE +
            "                         END t_State " +
            "   FROM ddpacap_dbt acap, ddepoacnt_dbt root " +
            "  WHERE acap.t_APartyGround = ? /*2*/" + //Procutrid
            "    AND acap.t_FromDate <= ? /*3*/ " +
            "    AND (acap.t_ToDate > ? /*4*/ or acap.t_ToDate = TO_DATE('01.01.0001', 'DD.MM.YYYY')) " +
            "    AND root.t_AutoKey = acap.t_APNodeID " +
            "    AND root.t_OpenDate <= ? /*5*/ " +
            " ) " +
            " ORDER BY t_State";

    Select = RSDCommand( query );
    Select.NullConversion = true;
    
    Select.AddParam("", RSDBP_IN, rep_date );
    Select.AddParam("", RSDBP_IN, ProcurData.ProcurID );
    Select.AddParam("", RSDBP_IN, rep_date );
    Select.AddParam("", RSDBP_IN, rep_date );
    Select.AddParam("", RSDBP_IN, rep_date );

    Select.Execute();

    DataSet = TRsbDataSet(Select);

    while(DataSet.moveNext())
      if(DataSet.State == SPSK_OPEN)
        IsOpen = true;
      else
        IsClose = true;
      end;
    end;
    if(IsOpen)
      ProcurState = "действительна";
    elif(IsClose)
      ProcurState = "счет депо закрыт";
    else // Не связана ни с одним счетом депо, но по датам - действительна
      ProcurState = "действительна";
    end;
  end;

  return ProcurState;
end;

private macro PrintAcAP(ProcurData)
  var query, Select, DataSet, i;
  var lastRoleName = "";
  
  query = "SELECT acap.*, na.t_sznamealg as RoleName" +
          "  FROM ddpacap_dbt acap, dnamealg_dbt na " +
          " WHERE acap.t_APartyGround = ? " +
          "   AND acap.t_FromDate <= ? " +
          "   AND (acap.t_ToDate > ? or acap.t_ToDate = TO_DATE('01.01.0001', 'DD.MM.YYYY')) " +
          "   and na.t_itypealg = 3266" + /*ALG_DEPOACAPROLE*/ 
          "   and na.t_inumberalg = acap.t_role"; 

  Select = RSDCommand( query );
  Select.NullConversion = true;
  Select.AddParam("", RSDBP_IN, ProcurData.ProcurID );
  Select.AddParam("", RSDBP_IN, rep_date );
  Select.AddParam("", RSDBP_IN, rep_date );

  Select.Execute();

  DataSet = TRsbDataSet(Select);

  while (DataSet.moveNext())
    if (lastRoleName != DataSet.RoleName)
      // группируем по ролям
      DP_AddPrintCell( Rep,  ProcurData.TrusterName,  0, 0, "l:w:" + RepFontStyleTitel );
      DP_AddPrintCell( Rep,  DataSet.RoleName,        0, 0, "l:"  + RepFontStyleTitel );
      DP_AddPrintCell( Rep,  ProcurData.AltXld + " " + String(date(ProcurData.SignedDate):f), 0, 0, "l:" + RepFontStyleTitel );
      DP_AddPrintCell( Rep,  Date(ProcurData.TerminateDate),   0, 0, "l:f:" + RepFontStyleTitel );
      DP_AddPrintCell( Rep,  ProcurData.AgentName,       0, 0, "l:w:" + RepFontStyleTitel );
      DP_AddPrintCell( Rep,  GetProcurState(ProcurData), 0, 0, "l:"  + RepFontStyleTitel );
      Rep.AddStr();
      lastRoleName = DataSet.RoleName;
    end;
  end;
  if ( lastRoleName == "" )
    DP_AddPrintCell( Rep,  ProcurData.TrusterName,  0, 0, "l:w:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep,  lastRoleName,        0, 0, "l:"  + RepFontStyleTitel );
    DP_AddPrintCell( Rep,  ProcurData.AltXld + " " + String(date(ProcurData.SignedDate):f), 0, 0, "l:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep,  Date(ProcurData.TerminateDate),   0, 0, "l:f:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep,  ProcurData.AgentName,       0, 0, "l:w:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep,  GetProcurState(ProcurData), 0, 0, "l:"  + RepFontStyleTitel );
    Rep.AddStr();  
  end; 


end;

private macro printReport()
  var query, Select, DataSet, i;
  var Params = TArray;

  query = " SELECT procur.t_ProcurID, " +
          "        procur.t_AgentID t_AgentID, " +
          "        procur.t_SPGroundID t_SPGroundID, " +
          "        spground.t_PartyName     t_TrusterName, " +
          "        spground.t_AltXld        t_AltXld, " +
          "        spground.t_SignedDate    t_SignedDate, " +
          "        spground.t_TerminateDate t_TerminateDate, " +
          "        agent.t_Name             t_AgentName, " +
          "        procur.t_Cancelled       t_Cancelled " +
          "   FROM ddpprocur_dbt procur, dspground_dbt spground, dparty_dbt agent " +
          "  WHERE spground.t_SPGroundID = procur.t_SPGroundID " +
          "    AND spground.t_Department = ? /*1*/ " +
          "    AND spground.t_SignedDate >= ? /*2*/ " +
          "    AND spground.t_SignedDate <= ? /*3*/ " +
          "    AND spground.t_TerminateDate >= ? /*4*/ " +
          "    AND spground.t_TerminateDate <= ? /*5*/ " +
          "    AND agent.t_PartyID = procur.t_AgentID ";
   
  Params(Params.Size) = iDepart;
  Params(Params.Size) = signed_date_beg;
  Params(Params.Size) = signed_date_end;
  Params(Params.Size) = term_date_beg;
  Params(Params.Size) = term_date_end;
   
  if(NOT show_void_auth) // Показать только действительные
    // Не отозванные
    query = query + " AND procur.t_Cancelled = CHR(0) ";
    
    // Дата окончания действия доверенности не меньше даты отчета
    query = query + " AND (spground.t_TerminateDate = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
                    "      OR spground.t_TerminateDate >= ? /*6*/ ) ";

    Params(Params.Size) = rep_date;

    // Существует хотя бы один открытый на дату отчета счет депо (дата открытия меньше или равна, а дата закрытия пустая или больше даты отчета)
    // Либо доверенность вообще не привязана ни к чему
    query = query + " AND ( EXISTS (SELECT 1 t_autokey "
                  + "                FROM ddpacap_dbt acap, ddepoacnt_dbt root "
                  + "                WHERE acap.t_APartyGround = procur.t_ProcurID "
                  + "                  AND acap.t_FromDate <= ? /*7*/ "
                  + "                  AND (acap.t_ToDate > ? /*8*/ or acap.t_ToDate = TO_DATE('01.01.0001', 'DD.MM.YYYY')) " 
                  + "                  AND root.t_AutoKey = acap.t_APNodeID "
                  + "                  AND root.t_OpenDate <= ? /*9*/ "
                  + "                  AND (root.t_CloseDate = TO_DATE('01.01.0001', 'DD.MM.YYYY') OR root.t_CloseDate > ? /*10*/)"
                  + "              ) "
                  + "    OR NOT EXISTS (SELECT 1 "
                  + "           FROM ddpacap_dbt acap "
                  + "         WHERE acap.t_APartyGround = procur.t_ProcurID) "
                  + "     )";
    Params(Params.Size) = rep_date;
    Params(Params.Size) = rep_date;
    Params(Params.Size) = rep_date;
    Params(Params.Size) = rep_date;

/*    query = query + " AND ( EXISTS (SELECT 1 t_autokey " +
                    "               FROM ddepoacnt_dbt ac, ddepoacnt_dbt root " +
                    "              WHERE ac.t_operator = procur.t_AgentID " +
                    "                AND ac.t_operground = procur.t_SPGroundID " +
                    "                AND root.t_AutoKey = ac.t_root " +
                    "                AND root.t_OpenDate <= ? /*7*/ " +
                    "                AND (root.t_CloseDate = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
                    "                     OR root.t_CloseDate > ? /*8*/ ) " +
                    "               UNION " +
                    "               SELECT 1 t_autokey " +
                    "                 FROM ddpagacc_dbt ag, ddepoacnt_dbt ac, ddepoacnt_dbt root " +
                    "                WHERE ag.t_agentid = procur.t_AgentID  " +
                    "                  AND ag.t_ground = procur.t_SPGroundID " +
                    "                  AND ac.t_autokey = ag.t_DepoAccID " +
                    "                  AND root.t_AutoKey = ac.t_root " +
                    "                  AND root.t_OpenDate <=  ? /*9*/ " +
                    "                  AND (root.t_CloseDate = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
                    "                       OR root.t_CloseDate >  ? /*10*/ ) " +
                    "              ) " + 
                    "       OR NOT EXISTS (SELECT 1 t_autokey " +
                    "                          FROM ddepoacnt_dbt ac " +
                    "                         WHERE ac.t_operator = procur.t_AgentID " +
                    "                           AND ac.t_operground = procur.t_SPGroundID " +
                    "                        UNION " +
                    "                        SELECT 1 t_autokey " +
                    "                          FROM ddpagacc_dbt ag " +
                    "                         WHERE ag.t_agentid = procur.t_AgentID  " +
                    "                           AND ag.t_ground = procur.t_SPGroundID " +
                    "                     ) " + 
                    "     ) ";
    Params(Params.Size) = rep_date;
    Params(Params.Size) = rep_date;
    Params(Params.Size) = rep_date;
    Params(Params.Size) = rep_date;*/
  end; 
   
  if(sort_by_termdate)
    query = query + " ORDER BY spground.t_TerminateDate ";
  else
    query = query + " ORDER BY spground.t_SignedDate ";
  end; 

  Select = RSDCommand( query );
  Select.NullConversion = true;

  i = 0;
  while(i < Params.Size)
    Select.AddParam("", RSDBP_IN, Params(i) );
    i = i + 1;
  end;

  Select.Execute();

  DataSet = TRsbDataSet(Select);

  Rep.AddEmptyStr();
  while(DataSet.moveNext())
    PrintAcAP(DataSet);
    iCount = iCount + 1;
  end;

  if(iCount)
    Rep.AddEmptyStr();
  else
    DP_AddPrintCell( Rep,  "Нет данных, удовлетворяющих условиям отчета", 50, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
    Rep.AddEmptyStr;
  end;
end;


// Точка входа в макрос
macro ListAuth( _iDepart          : integer,
                _rep_date         : date,
                _signed_date_beg  : date,
                _signed_date_end  : date,
                _term_date_beg    : date,
                _term_date_end    : date,
                _show_void_auth   : bool,
                _sort_by_termdate : bool,
                ToExcel           : bool
              )
  Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
  DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

  iDepart          = _iDepart;
  rep_date         = _rep_date;
  signed_date_beg  = _signed_date_beg;
  signed_date_end  = _signed_date_end;
  term_date_beg    = _term_date_beg;
  term_date_end    = _term_date_end;
  show_void_auth   = _show_void_auth;
  sort_by_termdate = _sort_by_termdate;

  DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, HeaderStr, -1, NULL, NULL, rep_date, NULL, tablewidth );
  printReport();
  DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

  DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
  DP_EndProtocol();                 //закончить протоколирование

  if( ToExcel )
    Rep.PrintWinRep();
    Rep.ShowWinRep();
  else
    Rep.PrintRep();
  end;

  return 0;
end;
