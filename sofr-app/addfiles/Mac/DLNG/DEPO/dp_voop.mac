/*
$Name:        dp_voop.mac
$Module:      Депозитарий
$Description: Определение кодов валютных операций (справочник voopkind.dbt)
              Занесение кодов в основание платежей и проводок
*/

import "dl_voop.mac", "dlquery.mac", "dpstdrep.mac";

// Определение кодов валютных операций
//
private macro ОпределениеКодаВалютнойОперации( paym, dpcorpop, dppmacc )
  var VO_Code = "";
  var DocumentID = 0;
  var PaymentType = 0;
  var HolderAccID = 0;
  var fi = TRecHandler( "fininstr" );
  var dppmrac = TRecHandler( "dppmrac.dbt" );

  if( ValType( dpcorpop ) == V_GENOBJ )
    DocumentID = dpcorpop.rec.DocumentID;
    PaymentType = dpcorpop.rec.PaymentType;
  else
    DocumentID = dpcorpop.DocumentID;
    PaymentType = dpcorpop.PaymentType;
  end;

  if( ValType( dppmacc ) == V_GENOBJ )
    HolderAccID = dppmacc.rec.HolderAccID;
  else
    HolderAccID = dppmacc.HolderAccID;
  end;

  if( ( ПолучитьФинИн( DP_GetSingleCorpFIID( DocumentID ), fi ) == 0 )
  and not НеэмиссионныйФинИн( fi.rec.FIID )
  and ( DP_GetPmRac( DocumentID, HolderAccID, dppmrac ) == 0 )
  and ( Index( "30601,30606", SubStr( dppmrac.rec.Account, 1, 5 ) ) == 0 ) )
    if( MC_IsResident( dppmrac.rec.Receiver ) == 2 )
      if( MC_IsResident( fi.rec.Issuer ) == 1 )
        if( PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
          VO_Code = "50110";
        else
          VO_Code = "55210";
        end;
      end;
    else
      if( MC_IsResident( fi.rec.Issuer ) == 2 )
        if( PaymentType == DP_CRPPAYMENTTYPE_DIVIDENDS )
          VO_Code = "50210";
        else
          VO_Code = "55310";
        end;
      end;
    end;
  end;

  return VO_Code;
end;

// Занести код ВО в основание платежа и в категорию по платежу
//
macro DP_SetPaymentGround( paym, dpcorpop, dppmacc )
  return DL_VOOP_SetPaymGround( @ОпределениеКодаВалютнойОперации, paym, dpcorpop, dppmacc );
end;

// Просто получить код ВО для платежа, заполнение не делать
// Используется для заполнения основания проводок, которые выполняются без платежей
//
macro DP_GetVO_Code( paym, dpcorpop, dppmacc )
  var VO_Code = "";

  if( ( DL_VOOP_GetCode( @VO_Code, @ОпределениеКодаВалютнойОперации, paym, dpcorpop, dppmacc ) ) and ( VO_Code != "" ) )
    return "{VO" + VO_Code + "}";
  else
    return "";
  end;
end;

// Проверка необходимости формирования кода валютной операции для отчета
//
macro DP_VOOP_CheckNeedCode( dpcorpop, dppmacc )
  var need_code = false;
  var DocumentID = 0;
  var Currency = 0;
  var HolderAccID = 0;
  var dppmrac = TRecHandler( "dppmrac.dbt" );

  if( ValType( dpcorpop ) == V_GENOBJ )
    DocumentID = dpcorpop.rec.DocumentID;
    Currency = dpcorpop.rec.Currency;
  else
    DocumentID = dpcorpop.DocumentID;
    Currency = dpcorpop.Currency;
  end;

  if( ValType( dppmacc ) == V_GENOBJ )
    HolderAccID = dppmacc.rec.HolderAccID;
  else
    HolderAccID = dppmacc.HolderAccID;
  end;

  if( ( DP_GetPmRac( DocumentID, HolderAccID, dppmrac ) == 0 )
  and ( Index( "30601,30606", SubStr( dppmrac.rec.Account, 1, 5 ) ) == 0 ) )
    need_code = ( ( MC_IsResident( dppmrac.rec.Receiver ) == 2 ) or ( Currency != NATCUR ) );
  end;

  return need_code;
end;

// Просто получить код ВО для платежа
// Используется для отчета
//
macro DP_GetVO_CodeForReport( dpcorpop, dppmacc )
  var VO_Code = "99999";

  if( DP_VOOP_CheckNeedCode( dpcorpop, dppmacc ) )
    VO_code = ОпределениеКодаВалютнойОперации( null, dpcorpop, dppmacc );

    if( VO_Code == "" )
      VO_Code = "99999";
    end;
  end;

  return VO_Code;
end;

