/* Макрос работы с банковскими продуктами Депозитария  */
/* Nikonorov Evgeny 01-09-2011 */

import RsbDataSet, DPInter, BankInter;

private const SIDEBALANCE_ACTIVE   = 1; /* активный */
private const SIDEBALANCE_PASSIVE  = 2; /* пассивный */

private const SPSK_OPEN   = 0; //Статус счета - Открыт            
private const SPSK_CLOSE  = 1; //Статус счета - Закрыт

//Найти объект продукта клиента
private macro GetDepoAcntObjPRD(ClientProductID, DepoacntID)
  var ClntProdObjID = 0;
  var DataSet, query;

  query =   " select prdobj.t_ClntProdObjID "
          + "   from ddepoacnt_dbt acnt, dprdclientobj_dbt prdobj "
          + "  where acnt.t_AutoKey = " + DepoacntID
          + "    and prdobj.t_ObjectID = TO_CHAR(acnt.t_AutoKey) "
          + "    and prdobj.t_ClientProductID = " + ClientProductID
          + "    and prdobj.t_ObjectProductKindID = (case when acnt.t_Root = acnt.t_AutoKey then " + PRD_DEPO_CONTRACT_OBJ_DEPOACC + " else " + PRD_DEPO_CONTRACT_OBJ_DEPOPART + " end) ";

  DataSet = TRsbDataSet(query);
  if(DataSet.moveNext())
    ClntProdObjID = DataSet.ClntProdObjID;
  end;

  return ClntProdObjID;
end;

//Создать объект типа счет/раздел депо в продукте клиента
macro AddDepoAcntObjPRD(ClientProductID, DepoacntID)
  var acnt, query;
  var ProductObj = NULL, ClntProdObjID = 0;
  var ErrorMessage = "", err = 0;
  var ChangeBranch = false;

  if((ClientProductID) and (DepoacntID))
    ClntProdObjID = GetDepoAcntObjPRD(ClientProductID, DepoacntID);

    ProductObj = RsbProductObject(ClntProdObjID);
    if(ClntProdObjID)
      if(ProductObj.EndDate != date(0,0,0))
        if(ProductObj.OpenProductObject(ErrorMessage) == false)
          msgbox(ErrorMessage);
          err = 1;
        else
          ChangeBranch = true;
        end;
      end;
    else
      query =   " select acnt.* "
              + "   from ddepoacnt_dbt acnt "
              + "  where acnt.t_AutoKey = " + DepoacntID;

      //дополнительные условия, которые не позволят добавить объект продукта для счета
      query = query + " and acnt.t_Kind = " + SIDEBALANCE_PASSIVE
                    + " and acnt.t_Status <> " + SPSK_CLOSE
                    + " and acnt.t_Owner not in (select dp.t_PartyID from ddp_dep_dbt dp)";

      acnt = TRsbDataSet(query);
      
      if(acnt.moveNext())
        ProductObj.ClientProductID     = ClientProductID;
        ProductObj.ParentID            = GetDepoAcntObjPRD(ClientProductID, acnt.Superior);
        ProductObj.BeginDate           = date(acnt.OpenDate);
        if(acnt.Root == acnt.AutoKey)
          ProductObj.ObjectProductKindID = PRD_DEPO_CONTRACT_OBJ_DEPOACC;
        else
          ProductObj.ObjectProductKindID = PRD_DEPO_CONTRACT_OBJ_DEPOPART;
        end;  
        ProductObj.ObjectID            = string(acnt.AutoKey);
        ProductObj.ObjectNumber        = acnt.Code;

        if(ProductObj.AddProductObject(ErrorMessage) == false)
          msgbox(ErrorMessage);
          err = 1;
        else
          ChangeBranch = true;
        end;
      end;
    end;

    //добавить объекты для всех открытых подуровней
    if(ChangeBranch == true)
      query = "select t_AutoKey from ddepoacnt_dbt where t_Superior = " + DepoacntID + " and t_Status <> " + SPSK_CLOSE;
      acnt = TRsbDataSet(query);
      while((not err) and (acnt.moveNext()))
        err = AddDepoAcntObjPRD(ClientProductID, acnt.AutoKey);
      end;
    end;
  end;

  return err;
end;

macro AddDepoAcntObjPRDbyCode(ClientProductID, DepoacntCode)
  var acnt, query;
  var err = 0;

  if(DepoacntCode != "")
    query =   " select acnt.t_AutoKey "
            + "   from ddepoacnt_dbt acnt "
            + "  where acnt.t_Code = '" + DepoacntCode + "' "; 
    acnt = TRsbDataSet(query);
    if(acnt.moveNext())
      err = AddDepoAcntObjPRD(ClientProductID, acnt.AutoKey);
    end;
  end;

  return err;
end;

//Закрыть объект типа счет/раздел депо в продукте клиента
macro CloseDepoAcntObjPRD(ClientProductID, DepoacntID)
  var ClntProdObjID = 0;
  var ProductObj = NULL;
  var ErrorMessage = "", err = 0;
  var acnt, query;

  if((ClientProductID) and (DepoacntID))
    ClntProdObjID = GetDepoAcntObjPRD(ClientProductID, DepoacntID);
    if(ClntProdObjID)
      ProductObj = RsbProductObject(ClntProdObjID);

      if(ProductObj.CloseProductObject(ErrorMessage) == false)
        msgbox(ErrorMessage);
        err = 1;
      else
        //закрыть все подуровни
        query = "select t_AutoKey from ddepoacnt_dbt where t_Superior = " + DepoacntID;
        acnt = TRsbDataSet(query);
        while((not err) and (acnt.moveNext()))
          err = CloseDepoAcntObjPRD(ClientProductID, acnt.AutoKey);
        end;
      end;
    end;
  end;

  return err;
end;

//изменение счета в договоре
macro ChangeSfDepoAcntObjPRDbyCode(SfContrID, ClientProductID, NewDepoacntCode, OldDepoacntCode)
  var DataSet, acnt, query;
  var err = 0;

  if(OldDepoacntCode != "")
    if(ClientProductID == 0) //бывает такое
      query =   " select prdc.t_ClientProductID "
              + "   from dprdclient_dbt prdc "
              + "  where prdc.t_SfContrID = " + SfContrID;
      DataSet = TRsbDataSet(query);
      if(DataSet.moveNext())
        ClientProductID = DataSet.ClientProductID;
      end;
    end;

    if(ClientProductID)
      //закрыть старые объекты
      query =   " select acnt.t_AutoKey "
            + "   from ddepoacnt_dbt acnt "
            + "  where acnt.t_Code = '" + OldDepoacntCode + "' "; 
      acnt = TRsbDataSet(query);
      if(acnt.moveNext())
        err = CloseDepoAcntObjPRD(ClientProductID, acnt.AutoKey);
      end;

      //открыть новые
      if((not err) and (NewDepoacntCode != ""))
        err = AddDepoAcntObjPRDbyCode(ClientProductID, NewDepoacntCode);
      end;
    end;
  end;

  return err;
end;