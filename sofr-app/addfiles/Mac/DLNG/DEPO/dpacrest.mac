/*
$Name:         dpacrest.mac
$Module:       Депозитарий
$Description:  Печать отчета "Ведомость об остатках ц/б на лицевых счетах".
*/

import DealsInter, dprepsec, dlquery;

private const IRCAG_ROLE_PAWNER = 7, IRCAG_ROLE_DEBTOR = 16;

PRIVATE MACRO GetMortIssuerRole()
  var Role = IRCAG_ROLE_DEBTOR, mortgageIssuerPawner = 0, err;

  GetRegistryValue( "DEPO\\MORTGAGEISSUER", V_INTEGER, mortgageIssuerPawner, err );

  if(mortgageIssuerPawner)
    Role = IRCAG_ROLE_PAWNER;
  end;

  return Role;
END;

macro ExecReport(AcntID:integer)
  var acnt = TBFile("depoacnt.dbt");
  var root = TBFile("depoacnt.dbt");
  var party = TBFile("party.dbt");
  var query, cmd, DataSet, i = 0, cnt = 0;
  var text = "", text_emiss = "";
  var MaxPoint = 0, CurrMaxPoint = 0, CurrSummRest = $0, AllSummRest = $0, Rest = "";
  var stat = 0, PrevIsEmissive = "";
  var StdRep = Depo_StdRepSection("Остатки на аналитическом узле");
  var Role = GetMortIssuerRole();

  acnt.Clear();
  acnt.rec.AutoKey = AcntID;
  if(not acnt.GetEQ())
    msgbox("Счет депо с ID = " + AcntID + " не найден");
    return;
  end;

  root.Clear();
  root.rec.AutoKey = acnt.rec.Root;
  if(not root.GetEQ())
    msgbox("Счет депо с ID = " + root.rec.AutoKey + " не найден");
    return;
  end;

  if( ПолучитьСубъекта(acnt.rec.Owner, party) != 0 )
     MsgBox("Не найден владелец счета депо");
     return 1;
  end;

  if(acnt.rec.AutoKey == acnt.rec.Root)
    text = "Ведомость об остатках ц/б на лицевых счетах счета депо " + acnt.rec.BriefCode;
  else
    text = "Ведомость об остатках ц/б на лицевых счетах раздела "+acnt.rec.BriefCode+" счета депо " + root.rec.BriefCode;
  end;

  StdRep.preHeader();

[################################################################################################]
(text:l);
  
  println();
[Владелец счета депо:];
[     Наименование #]( party.rec.ShortName+" ИНН "+ПолучитьКодСубъекта(acnt.rec.Owner, PTCK_INN) );

  println();
[ЦЕННЫЕ БУМАГИ НА ЛИЦЕВЫХ СЧЕТАХ РАЗДЕЛА:];

  query =   " select fin.t_SumPrecision, fin.t_Name as FIName, iss.t_LSIN, "
          + "        avrk.t_IsEmissive, avrk.t_Name as AvrkName, "
          + "        acc.t_Account, "
          + "        rsb_account.restac( acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, null ) as Rest, "
          + "        NVL((case when RSB_FIInstr.FI_AvrKindsEQ( fin.t_FI_Kind, "+AVOIRISSKIND_MORTGAGE+", fin.t_AvoirKind) = 0 "
          + "                    or (SELECT COUNT(1) FROM dv_ficert_dbt ficert WHERE ficert.t_FIID = fin.t_FIID) <> 1 "
          + "                  then (select pt.t_ShortName "
          + "                          from dparty_dbt pt "
          + "                         where pt.t_PartyID = fin.t_Issuer) "
          + "              else (select ag.t_AgentName "
          + "                      from dv_ficert_dbt ficert, dvsircag_dbt ag "
          + "                      where ficert.t_FIID = fin.t_FIID "
          + "                          and ag.t_AvoirKind = ficert.t_AvoirKind "
          + "                          and ag.t_BCID = ficert.t_CertID "
          + "                          and ag.t_AgentRole = " + Role
          + "                          and ag.t_InputOrder = (select Min(ag1.t_InputOrder) from dvsircag_dbt ag1 where ag1.t_AvoirKind = ag.t_AvoirKind and ag1.t_BCID = ag.t_BCID and ag1.t_AgentRole = ag.t_AgentRole) "
          + "                   ) "
          + "              end), CHR(1)) as IssuerName "
          + "   from daccount_dbt acc, dfininstr_dbt fin, davoiriss_dbt iss, davrkinds_dbt avrk "
          + "  where acc.t_Chapter = 5 "
          + "    and acc.t_DepoRoot > 0 "
          + "    and acc.t_DepoAcc IN (select acnt.t_AutoKey "
          + "                            from ddepoacnt_dbt acnt "
          + "                           start with acnt.t_AutoKey = ? "
          + "                           connect by acnt.t_Superior = prior acnt.t_AutoKey "
          + "                         ) "
          + "    and rsb_account.restac( acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, null ) <> 0 "
          + "    and fin.t_FIID = acc.t_Code_Currency "
          + "    and iss.t_FIID = fin.t_FIID "
          + "    and avrk.t_FI_Kind = fin.t_FI_Kind "
          + "    and avrk.t_AvoirKind = fin.t_AvoirKind "
          + " order by avrk.t_IsEmissive DESC, acc.t_DepoAcc DESC, acc.t_AccountID DESC";

  cmd = DL_RSDCommand(query);

  cmd.AddParam({curdate});
  cmd.AddParam(AcntID);
  cmd.AddParam({curdate});

  cnt = cmd.GetCount();
  if(cnt > 0)

    DataSet = cmd.Execute();

    InitProgress( cnt, "Идет формирование отчета", text );

[+-------------------------+-------------------------+-------------------+--------------+--------------------+-------------------------+];
[|Вид                      |Наименование ц/б         |Эмитент            |№ выпуска     |Количество          |Номер л/с                |];
[+-------------------------+-------------------------+-------------------+--------------+--------------------+-------------------------+];

    stat = DataSet.moveNext();
    while(stat)

      if( DataSet.SumPrecision > MaxPoint)
        MaxPoint     = DataSet.SumPrecision;
        CurrMaxPoint = DataSet.SumPrecision;
      end;

      CurrSummRest = CurrSummRest + ABS(DataSet.Rest);
      AllSummRest = AllSummRest + ABS(DataSet.Rest);

      Rest =  ЧислоCЗаданнойТочностью(double(ABS(DataSet.Rest)), DP_GetPrecision(DataSet.Rest, DataSet.SumPrecision), "");

[|#########################|#########################|###################|##############|####################|#########################|]
(DataSet.AvrkName,          DataSet.FIName,           DataSet.IssuerName:w, DataSet.LSIN,  Rest:r,  DataSet.Account); 

      PrevIsEmissive = DataSet.IsEmissive;

      UseProgress(i = i + 1);

      stat = DataSet.moveNext();
      if((not stat) or (PrevIsEmissive != DataSet.IsEmissive))

        if(PrevIsEmissive == "X")
          text_emiss = "эмиссионных";
        else
          text_emiss = "неэмиссионных";
        end;

        CurrSummRest = ЧислоCЗаданнойТочностью(double(CurrSummRest), DP_GetPrecision(CurrSummRest, CurrMaxPoint), "");

[+-------------------------+-------------------------+-------------------+--------------+--------------------+-------------------------+];
[|Всего #############                                                                   |####################|                         |]
(text_emiss, CurrSummRest:r);

        if(stat)
[+-------------------------+-------------------------+-------------------+--------------+--------------------+-------------------------+];
        end;

        CurrSummRest = $0;
        CurrMaxPoint = 0;
      end;
      
    end;

    RemProgress();

[+-------------------------+-------------------------+-------------------+--------------+--------------------+-------------------------+];
  end;

  println();
  AllSummRest = ЧислоCЗаданнойТочностью(double(AllSummRest), DP_GetPrecision(AllSummRest, MaxPoint), "");
[Итого по разделу     #]( AllSummRest:l );

  StdRep.FooterOriginal();
  StdRep.FooterSignature();

end;