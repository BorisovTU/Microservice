/*
$Name: spaccfrm.mac
$Module: Депозитарий
$Description: отчет "Анкета ( регистрационная карточка ) лицевого счета депо"
*/
import deposerv, DPInter, "spacform.mac";

/* Глава синтетического учета */
/* Вид Альтернативного идентификатора */

/*Счет Депо*/

var _is_app;
private var _LogoprID, _RepDate;

/* Направление документов 
const DIRECT_ANY = 0,  /*любое направление*/
      ENTERING   = 1,  /*документы входящие*/      
const EXITING    = 2;  /*документы исходящие*/    */

/* Типы документов в файле оснований
  SPDK_ALL    = 0,     // Все документы
  SPDK_AGREE  = 1,     // договор
  SPDK_ORDER  = 2,     // распоряжение 
  SPDK_REPORT = 3,     // отчет
  SPDK_DRAFT  = 4,     // Поручение на перевод ц/б */
const SPDK_REPDEPO= 5;     /* Отчёт Анкета счета или раздела Депо*/

var HeadTable = "┌────────────────────────────────────────────────────────────────────────────────────────┐\n"+
                "│                                                                                        │\n"+
                "├────────────────────────────────────────────────────────────────────────────────────────┤";
var tablewidth = Index(HeadTable, "\n");
                
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

macro FinInIssuer(PartyID, RepDate, IsFullName)
   var cIssuer = CDPParty(PartyID, RepDate);
   if(IsFullName)
     return cIssuer.Name();
   else
     return cIssuer.ShortName();
   end;
end;

/*Сертификат на счету, если один*/
class AccInvCardProp(_RepDate)

   var select, invcard, RepDate = _RepDate;

   var Inventory_Number = "",
       Nominal = "",
       NominalFICode = "",
       NominalFIIDName = "",
       IssuerName = "";

   macro FindAllPropetis()
      Inventory_Number = invcard.Number;
      IssuerName       = FinInIssuer(invcard.Issuer, RepDate, true);
      Nominal          = invcard.FaceValue;
      NominalFICode    = ПолучитьКодФинИн(0); /*всегда в рублях*/
      NominalFIIDName  = FinInName(0);        /*всегда в рублях*/
   end;

   macro FindInvCard(FIID, AccountID)
      var stat, CertFlag = false;
      var query = "";

      query =   " select ic.t_Number, vficert.t_Issuer, vficert.t_FaceValue "
              + "   from dinvcard_dbt ic, dv_ficert_dbt vficert "
              + "  where vficert.t_FIID = ? " /*1*/
              + "    and ic.t_FiCertID = vficert.t_FICertID "
              + "    and (ic.t_HAccountID = ? " /*2*/
              + "         or ic.t_LAccountID = ? )"; /*3*/

      Select = RSDCommand( query  );

      Select.AddParam("FIID",       RSDBP_IN, FIID );       /*1*/
      Select.AddParam("HAccountID", RSDBP_IN, AccountID );  /*2*/
      Select.AddParam("LAccountID", RSDBP_IN, AccountID );  /*3*/

      Select.Execute();

      invcard = TRsbDataSet(Select);
      while(invcard.moveNext())
        if(not CertFlag)  /*нужно чтоб сертификат на этом счете был единственным*/
           CertFlag = true;
           FindAllPropetis();
        else
           return false;
        end;
      end;
      return CertFlag;
   end;
end;


macro ОднаАнкетаЛицевогоСчетаДепо(LogOprID, 
                                  account,
                                  RepDate,
                                  iCount,
                                  Deponent,
                                  DepoAcc,
                                  DepoPart,
                                  FIID, 
                                  dacc, 
                                  InvCardID,
                                  is_openacc,
                                  is_closeacc)

   var AccCod, DepoInf, RootAcc;
   var LinkKind, OrderChar, StatementChar;
   var Status, StatusName, StatusMomentSet;
   var AccInvCard = AccInvCardProp(RepDate);
   var LSIN            = "",
       Nominal         = "",
       NominalFICode   = "",
       NominalFIIDName = "",
       IssuerName      = "",
       AvoirType       = "";
   var str = "";
   file fininstr(fininstr); 
   file avoiriss(avoiriss); 
   file avrkinds(avrkinds);
   record _party(party);
   var OpenOpNum="", OpenDate="", OpenTime="", OpenOper="", OpenGround="", OpenDocNum="";
   var ModOpNum="",  ModDate="",  ModTime="",  ModOper="",  ModGround="",  ModDocNum="";
   var Issuer  = -1;

   /*Статус л/с счета Депо*/
   if(ValType(account.NextAdmoOperKind) == V_UNDEF)
     Status = account.Open_Close;
   elif((ValType(account.PrevAdmoOperKind) == V_UNDEF) OR (account.PrevAdmoOperKind == SPAO_CON_REGISTERING))
     if((date(account.Close_Date) == date(0,0,0)) OR (date(account.Close_Date) > RepDate))
       Status = "О";
     elif((date(account.Close_Date) != date(0,0,0)) AND (date(account.Close_Date) <= RepDate))
       Status = "З";
     end;
   elif((ValType(account.PrevAdmoOperKind) != V_UNDEF) AND (account.PrevAdmoOperKind == SPAO_CON_CLOSING))
     Status = "З";
   end;

   if(Status == "З")
     if( is_closeacc == false )
       return 1;
     end;

     StatusMomentSet = date(account.Close_Date);
     if(ValType(account.PrevAdmoOperDate) != V_UNDEF)
       StatusMomentSet = date(account.PrevAdmoOperDate);
     elif(ValType(account.NextAdmoOperKind) == V_UNDEF)
       StatusMomentSet = date(account.Close_Date);
     end;
     StatusName = "Закрыт";
   else 
     if( is_openacc == false )
       return 1;
     end;

     if(RepDate >= date(account.Open_Date))
       Status = "О";

       StatusMomentSet = date(account.Open_Date);
       if(ValType(account.PrevAdmoOperDate) != V_UNDEF)
         StatusMomentSet = date(account.PrevAdmoOperDate);
       elif(ValType(account.NextAdmoOperKind) == V_UNDEF)
         StatusMomentSet = date(account.Open_Date);
       end;
       StatusName = "Открыт";  
     else
       Status = "";
       StatusName = "";
       StatusMomentSet = RepDate;
     end;
   end;
   /* <<<<<<< Ценная бумага */
   fininstr.FIID = account.Code_Currency;
   if(not GetEQ(fininstr))
     return 1;
   end;
   avoiriss.FIID = account.Code_Currency;
   if( (fininstr.FI_Kind != FIKIND_AVOIRISS) or (not GetEQ(avoiriss)) )
     return 1;
   end;
   if(ИндивидуальныйФинИн(fininstr.FIID))

      /*найти сертификат и по нему*/
      LSIN            = "-";
      Nominal         = "-"; 
      NominalFIIDName = "-";
      IssuerName      = "-";
   elif(DP_CheckAvoirKind(fininstr.AvoirKind, AVOIRISSKIND_INVESTMENT_SHARE))//пай
      IssuerName      = FinInIssuer(fininstr.Issuer, RepDate, true);
   else
      LSIN            = avoiriss.LSIN;
      Nominal         = ЧислоCЗаданнойТочностью(GetFaceValue(fininstr.FIID, RepDate), fininstr.Point+2);
      NominalFICode   = ПолучитьКодФинИн(fininstr.FaceValueFI);
      NominalFIIDName = FinInName(fininstr.FaceValueFI);
      if(FI_IsBond(fininstr))
         Issuer = FI_GetIssuerOnDate(fininstr, RepDate );
      else
         Issuer = fininstr.Issuer;
      end;
      IssuerName = FinInIssuer( Issuer, RepDate, false);  
   end;
   
   if(НеэмиссионныйФинИн(fininstr.FIID))
      AvoirType = "Неэмиссионные";
   else
      AvoirType = "Эмиссионные";
   end;

   avrkinds.FI_Kind = fininstr.FI_Kind;
   avrkinds.AvoirKind = fininstr.AvoirKind;
   if(not GetEQ(avrkinds))
     return 1;
   end;
   /* Ценная бумага >>>>>>> */
   errstr1 = errACCdepo;
   DepoInf = DepoInfo(account.DepoAcc, RepDate);
   if(DepoInf == NULL)
     return 1;
   end;
   if(DepoInf.Root != 0)
     RootAcc = DepoInfo(DepoInf.Root, RepDate);
   else
     RootAcc = DepoInfo(account.DepoAcc, RepDate);
   end;
   if(RootAcc == NULL)
     return 1;
   end;
   if( (Deponent != -1) and (DepoInf.Owner != Deponent ) )
     return 1;
   end;
   if( (DepoAcc > 0) and (RootAcc.AutoKey != DepoAcc ) )
     return 1;
   end;

   if(ValType(account.PrevOpenAdmoprID) != V_UNDEF)
      OpenOpNum  = account.PrevOpenOprXID;
      OpenDate   = date(account.PrevOpenOperDate);
      OpenTime   = time(account.PrevOpenOperTime);
      OpenOper   = account.PrevOpenOper;
      OpenGround = account.PrevOpenAdmoGround;
      OpenDocNum = account.PrevOpenAdmoDocNum;
   end;

   if((Status == "З") AND (ValType(account.PrevModAdmoprID) != V_UNDEF) AND ((ValType(account.PrevOpenAdmoprID) == V_UNDEF) OR (account.PrevModAdmoprID > account.PrevOpenAdmoprID)))
      ModOpNum  = account.PrevModOprXID;
      ModDate   = date(account.PrevModOperDate);
      ModTime   = time(account.PrevModOperTime);
      ModOper   = account.PrevModOper;
      ModGround = account.PrevModAdmoGround;
      ModDocNum = account.PrevModAdmoDocNum;
   end;

   if( dacc == "" )
     DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Регистрационная карточка", LogOprID, iCount, true, RepDate, NULL, tablewidth );
   else
     DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Регистрационная карточка", LogOprID, iCount, false, RepDate, NULL, tablewidth );
   end;

   DP_AddPrintCell( Rep,  "  лицевого счёта депо", 23, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  account.Account, tablewidth-23, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "  раздела", 14, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  DepoInf.Code+" ( "+DepoInf.PartitionPath+") "+" наименование " + DepoInf.Name, 
                     tablewidth-14, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "  счёта депо", 14, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  RootAcc.Code+/*(<Account CodeD>),*/" наименование "+ RootAcc.Name, 
                     tablewidth-14, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   str = string("  тип раздела ", DepoInf.Type:l, "   - ", DepoInf.DepoTypeName);
   Rep.AddStr();
   DP_AddPrintCell( Rep,  str, tablewidth, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   str = string("  тип счета   ", RootAcc.Type:l, "   - ", RootAcc.DepoTypeName, " ("+RootAcc.DepoKind, ")");
   DP_AddPrintCell( Rep,  str, tablewidth, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "  блокировка", 14, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  string(RootAcc.AvoirStatus)+" - "+RootAcc.AvoirStatusName, tablewidth-14, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "Синтетический счет депо", 25, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  account.Balance, tablewidth-25, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "Ценные бумаги", 14, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  fininstr.Name, tablewidth-14, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
  if(not ИндивидуальныйФинИн(fininstr.FIID))
   if(DP_CheckAvoirKind(fininstr.AvoirKind, AVOIRISSKIND_INVESTMENT_SHARE)) //пай

     DP_AddPrintCell( Rep,  "  вид", 6, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  avrkinds.Name, tablewidth-6, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
     Rep.AddStr();
     DP_AddPrintCell( Rep,  "  эмитент", 10, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  IssuerName, tablewidth-10, 0, "l:w:"  + RepFontStyleTitel, _is_app, REP_ELEM_STR );//Здесь IssuerName уже определён специально для пая
     Rep.AddStr();
   else
     DP_AddPrintCell( Rep,  "  вид", 6, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  avrkinds.Name+"  рег. № выпуска "+LSIN, tablewidth-6, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
     Rep.AddStr();
     DP_AddPrintCell( Rep,  "  номинал", 10, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  string(setApp(Nominal,_is_app))+"  "+NominalFICode+" ("+NominalFIIDName+")",  tablewidth-10, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
     Rep.AddStr();
     DP_AddPrintCell( Rep,  "  эмитент", 10, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  IssuerName, tablewidth-10, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
     Rep.AddStr();
   end;   
  elif(AccInvCard.FindInvCard(fininstr.FIID, account.AccountID))
     Rep.AddPrintCell( "  вид", 6, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
     Rep.AddPrintCell( avrkinds.Name+"  номер инвентарной карточки "+AccInvCard.Inventory_Number, tablewidth-6, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
     Rep.AddStr();
     Rep.AddPrintCell( "  номинал", 10, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
     Rep.AddPrintCell( string(setApp(AccInvCard.Nominal,_is_app))+"  "+AccInvCard.NominalFICode+" ("+AccInvCard.NominalFIIDName+")", tablewidth-10, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
     Rep.AddStr();
     Rep.AddPrintCell( "  эмитент", 10, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
     Rep.AddPrintCell( AccInvCard.IssuerName, tablewidth-10, 0, "l:w:" + RepFontStyleTitel, REP_ELEM_STR );
     Rep.AddStr();
  else
   DP_AddPrintCell( Rep,  "  вид", 6, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  avrkinds.Name, tablewidth-6, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
  end;
   DP_AddPrintCell( Rep,  "  тип", 6, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  AvoirType, tablewidth-6, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddEmptyStr();
   DP_AddPrintCell( Rep,  "Статус счёта", 13, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  "("+string(StatusMomentSet:f)+") "+Status+" - "+StatusName, tablewidth-13, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "Административные операции", 25, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "                  открыт                                     модифицирован", tablewidth, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep, "  № операции      ", 18, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep, string(OpenOpNum), (tablewidth-18)/2, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep, string(ModOpNum), (tablewidth-18)/2, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "  дата, время     ", 18, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  OpenDate    , 12, 0, "l:f"+ RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  OpenTime    , (tablewidth-18)/2-12, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  ModDate     , 12, 0, "l:f"+ RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  ModTime     , (tablewidth-18)/2-12, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "  исполнитель     ", 18, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  OpenOper    , (tablewidth-18)/2, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  ModOper     , (tablewidth-18)/2, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "  основание       ", 18, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  OpenGround   , (tablewidth-18)/2, 0, "l:w:"+ RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  ModGround    , (tablewidth-18)/2, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "  № документа     ", 18, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  OpenDocNum  , (tablewidth-18)/2, 0, "l:w:"+ RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  ModDocNum   , (tablewidth-18)/2, 0, "l:w:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "  дата истечения"  , 17, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  "____________"  , 12, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
   Rep.AddStr();
   Rep.AddEmptyStr();
   DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);
   Rep.AddEmptyStr();
   return 0;
end;


macro АнкетаЛицевогоСчетаДепо(LogOprID, NumDprt, RepDate, Deponent, DepoAcc, DepoPart, FIID, acc, InvCardID, is_openacc, is_closeacc)

   var stat, iCount = 1;
   var sQuery;
   var count = 0;
   var Progress = CProgressBar;

   var account;

   sQuery =   " select acc.t_AccountID, acc.t_Account, acc.t_Code_Currency, acc.t_DepoAcc, acc.t_DepoRoot, acc.t_Balance, acc.t_Open_Date, acc.t_Close_Date, acc.t_Open_Close, "
            + "        prevadmo.t_OperKind as PrevAdmoOperKind, prevadmo.t_OperDate as PrevAdmoOperDate, nextadmo.t_OperKind as NextAdmoOperKind, "
            + "        prevopenadmo.t_AdmoprID as PrevOpenAdmoprID, prevopenadmo.t_OprXID as PrevOpenOprXID, prevopenadmo.t_OperDate as PrevOpenOperDate, "
            + "        prevopenadmo.t_OperTime as PrevOpenOperTime, prevopenadmo.t_Oper as PrevOpenOper, "
            + "        NVL(prevopenadmospgr.t_Xld, CHR(1)) as PrevOpenAdmoDocNum, "
            + "        (select NVL(llv.t_Name, CHR(1)) from dllvalues_dbt llv where llv.t_List = 1050 and llv.t_Element = prevopenadmospgr.t_Kind) as PrevOpenAdmoGround, "
            + "        prevmodadmo.t_AdmoprID as PrevModAdmoprID, prevmodadmo.t_OprXID as PrevModOprXID, prevmodadmo.t_OperDate as PrevModOperDate, "
            + "        prevmodadmo.t_OperTime as PrevModOperTime, prevmodadmo.t_Oper as PrevModOper, "
            + "        NVL(prevmodadmospgr.t_Xld, CHR(1)) as PrevModAdmoDocNum, "
            + "        (select NVL(llv.t_Name, CHR(1)) from dllvalues_dbt llv where llv.t_List = 1050 and llv.t_Element = prevmodadmospgr.t_Kind) as PrevModAdmoGround "
            + "  from daccount_dbt acc "
            + "               LEFT JOIN ddepoadmo_dbt prevadmo ON prevadmo.t_AdmoprID = rsb_depo.GetObjPrevAdmoprOnDate(0, acc.t_Code_Currency, acc.t_Account, "+GetSQLDate(RepDate)+") "
            + "               LEFT JOIN ddepoadmo_dbt nextadmo ON nextadmo.t_AdmoprID = rsb_depo.GetObjNextAdmoprOnDate(0, acc.t_Code_Currency, acc.t_Account, "+GetSQLDate(RepDate)+") "
            + "               LEFT JOIN ddepoadmo_dbt prevopenadmo ON prevopenadmo.t_AdmoprID = rsb_depo.GetObjPrevAdmoprOnDate(0, acc.t_Code_Currency, acc.t_Account, "+GetSQLDate(RepDate)+", "+SPAO_CON_REGISTERING+") "
            + "               LEFT JOIN dspground_dbt prevopenadmospgr ON prevopenadmospgr.t_SPgroundID = prevopenadmo.t_Ground "
            + "               LEFT JOIN ddepoadmo_dbt prevmodadmo ON prevmodadmo.t_AdmoprID = rsb_depo.GetObjPrevAdmoprOnDate(0, acc.t_Code_Currency, acc.t_Account, "+GetSQLDate(RepDate)+", "+SPAO_CON_CLOSING+") "
            + "               LEFT JOIN dspground_dbt prevmodadmospgr ON prevmodadmospgr.t_SPgroundID = prevmodadmo.t_Ground "
            + " where acc.t_Chapter = 5 and acc.t_Department = " + NumDprt;

   if( ( is_openacc == SET_CHAR ) and ( is_closeacc == UNSET_CHAR ) )
      //ищем АО по л/с с датой исполнения ближайшей большей к дате проверки статуса. Если такая ОА не нашлась, то статус берем из анкеты л/с (т.е. он там актуальный). 
      //Иначе ищем АО по л/с с датой исполнения ближайшей меньшей к дате проверки статуса, ВКЛЮЧАЯ дату проверки статуса (мы всегда в таких проверках смотрим на конец дня).
      //5.1.3. Если АО из нашлась и это АО открытия или такая АО не нашлась
      //5.1.3.1. Если дата закрытия в л/с не задана или она больше даты проверки статуса - то статус л/с - "открыт"
      sQuery = sQuery + " and 1 = (case when (nextadmo.t_AdmoprID IS NULL AND acc.t_Open_Close = CHR(0)) then 1 "   
                      + "               when NOT nextadmo.t_AdmoprID IS NULL AND "
                      + "                    (    (prevadmo.t_AdmoprID IS NULL OR "+SPAO_CON_REGISTERING+" = prevadmo.t_OperKind) "
                      + "                     and ( acc.t_Close_Date = " + GetSQLDate(date(0,0,0)) + " OR acc.t_Close_Date > " + GetSQLDate(RepDate) + ")) " 
                      + "                        then 1 else 0 end ) ";
   elif( ( is_openacc == UNSET_CHAR ) and ( is_closeacc == SET_CHAR ) )
      //ищем АО по л/с с датой исполнения ближайшей большей к дате проверки статуса. Если такая ОА не нашлась, то статус берем из анкеты л/с (т.е. он там актуальный).
      //ищем АО по л/с с датой исполнения ближайшей меньшей к дате проверки статуса, ВКЛЮЧАЯ дату проверки статуса (мы всегда в таких проверках смотрим на конец дня).
      //5.1.3. Если АО из нашлась и это АО открытия или такая АО не нашлась
      //5.1.3.2. Если дата закрытия в л/с задана и она меньше или равна дате проверки статуса - то статус л/с "закрыт"
      //5.1.4. Если АО из п.2. нашлась и это АО закрытия - то статус л/с "закрыт"
      sQuery = sQuery + " and 1 = (case when (nextadmo.t_AdmoprID IS NULL AND acc.t_Open_Close = 'З') then 1 "
                      + "               when NOT nextadmo.t_AdmoprID IS NULL AND "
                      + "                    ( (    (prevadmo.t_AdmoprID IS NULL OR "+SPAO_CON_REGISTERING+" = prevadmo.t_OperKind)"
                      + "                     and acc.t_Close_Date > " + GetSQLDate(date(0,0,0))
                      + "                     and acc.t_Close_Date <= " + GetSQLDate(RepDate) + " ) "
                      + "                   OR "+SPAO_CON_CLOSING+" = prevadmo.t_OperKind )"
                      + "                        then 1 else 0 end ) ";
   end;

   if( Deponent != -1 )
     sQuery = sQuery + " and acc.t_Client = " + Deponent;
   end;

   if( acc != "" )
     sQuery = sQuery + " and acc.t_Account       = '"+acc+"' ";
       end;

   if( FIID != -1 )
      sQuery = sQuery +" and acc.t_Code_Currency = " + FIID;
       end;

   if( DepoAcc > 0 )
     sQuery = sQuery + " and acc.t_DepoRoot = " + DepoAcc;
     end;

   if( DepoPart > 0 )
     sQuery = sQuery + " and acc.t_DepoAcc in (select acnt.t_AutoKey from ddepoacnt_dbt acnt start with acnt.t_AutoKey = " + DepoPart + " connect by acnt.t_Superior = prior acnt.t_AutoKey) ";
     end;

   if( InvCardID )
     sQuery = sQuery + " and ( acc.t_AccountID in ( select t_HAccountID from dinvcard_dbt where t_InvCardID = "+InvCardID+" ) "
                     + "    or acc.t_AccountID in ( select t_LAccountID from dinvcard_dbt where t_InvCardID = "+InvCardID+" ) )"; 
   end;

   account = TRsbDataSet(sQuery);
   Progress.Init( SQL_GetNRecs(sQuery), "Формирование отчета...", "Регистрационная карточка л/с депо" );
   while( account.moveNext() )
     if( ОднаАнкетаЛицевогоСчетаДепо(LogOprID, account.rec, RepDate, iCount, Deponent, DepoAcc, DepoPart, FIID, acc, InvCardID, is_openacc, is_closeacc) == 0 )
       iCount = iCount + 1;
     end;
     count = Progress.Use();
   end;

   Progress.Remove();

   if( iCount > 1 )
     return iCount;
   else
     return 0;
   end;
end;

private macro CreateEmptyRep()
  DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Регистрационная карточка", _LogOprID, 0, false, _RepDate, NULL, tablewidth );
  Rep.AddEmptyStr();
  DP_AddPrintCell( Rep,  "Нет данных, удовлетворяющих параметрам отчета.", tablewidth, 0, "l:" + RepFontStyleTitel, _is_app, REP_ELEM_STR );
  Rep.AddEmptyStr();
  DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);
end;

MACRO account_form( LogOprID:integer,
                    NumDprt    :integer,
                    RepDate    :date,
                    Deponent   :integer,
                    DepoAcc    :integer,
                    DepoPart   :integer,
                    FIID       :integer,
                    acc        :string, /*код (строка) лицевого счета депо;*/                                     
                    InvCardID  :integer,
                    is_openacc :bool,
                    is_closeacc:bool,
                    ToExcel    :bool,
                    is_ap      :bool
                  )
    var stat = 0;               
    _is_app = is_ap;
    _LogoprID = LogoprID;
    _RepDate  = RepDate;
    Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
    DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

    if( not АнкетаЛицевогоСчетаДепо(LogOprID, NumDprt, RepDate, Deponent, DepoAcc, DepoPart, FIID, acc, InvCardID, is_openacc, is_closeacc) != 0)
      stat = 1;
    end;

    DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
    DP_EndProtocol();                 //закончить протоколирование

    return DP_StdReportControl( _LogOprID, (not stat), @CreateEmptyRep, 0, ToExcel, Rep );
end;
