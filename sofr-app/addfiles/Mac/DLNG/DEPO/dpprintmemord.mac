/*
$Name:         dpprintmemord.mac
$Module:       Депозитарий
$Description:  Печать депозитарного мемордера
*/

import dprepsec, dpzakrhr, "pt_lib.mac";

private record fin("fininstr");
private record avr("avoiriss");
const ConditionDepo = 1;
private const OBJTYPE_DEPODRAFT = 104;


var Table1 = "┌──┬───────────────────────┬────────────┬────────┬────────┬────────┬────────┬──────────┬──────────┬───────────────────────┐\n"+
             "│№ │Эмитент                │ИНН         │Серия   │с №     │по №    │К-во ц/б│Выдан     │Погаш.    │Номинал ц/б            │\n"+
             "├──┼───────────────────────┼────────────┼────────┼────────┼────────┼────────┼──────────┼──────────┼───────────────────────┤";

//var Table1 = "┌──────────────────────────────────────────────────────────────────────────────────┐\n"+
//             "│                                                                                  │\n"+
 //            "├──────────────────────────────────────────────────────────────────────────────────┤";
                
var MemOrdRep = CMakeReport(Table1);
const MemOrdFontStyle =  "ex_FS(b)";

private macro GroundKind(List, Kind) 
  var llvl = TBFile("llvalues");
  var Ground;
  llvl.rec.List    = List;
  llvl.rec.Element = Kind;
  if (GetEQ(llvl))
    Ground = llvl.rec.Name;
  else
    Ground = "";
  end;
  return Ground;
end;

private macro OperationName(iOpr_Kind)
   var oprkoper=TBFile("oprkoper");

   oprkoper.rec.Kind_Operation = iOpr_Kind;
   if(GetEQ(oprkoper))
      return oprkoper.rec.Name;
   else
      return "";
   end;
end;

private macro ПечатьОписиСертификатов_Ex(PmPaymRec, is_ap)
  var IK = IK_class(PmPaymRec);
  var stat, n = 0;
  if(stat = IK.First_IK())
    MemOrdRep.AddEmptyStr();
    MemOrdRep.AddPrintCell( "   Опись сертификатов " + IK.FiName, 100, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
    MemOrdRep.AddStr();
    while(stat)
      n = n + 1;
      MemOrdRep.AddPrintCell( n, 2, 0, "l:" + RepFontStyleTitel );
      MemOrdRep.AddPrintCell( IK.IssuerName, 23, 0, "l:" + RepFontStyleTitel );
      MemOrdRep.AddPrintCell( IK.IssuerINN, 12, 0, "l:" + RepFontStyleTitel );
      MemOrdRep.AddPrintCell( IK.Series, 8, 0, "l:" + RepFontStyleTitel );
      MemOrdRep.AddPrintCell( trim(IK.Number), 8, 0, "l:" + RepFontStyleTitel );
      MemOrdRep.AddPrintCell( trim(IK.Number), 8, 0, "l:" + RepFontStyleTitel );
      MemOrdRep.AddPrintCell( setApp(IK.IK_Amount,is_ap), 8, 0, "l:" + RepFontStyleTitel );
      MemOrdRep.AddPrintCell( IK.IssueDate, 10, 0, "l:f:" + RepFontStyleTitel );
      MemOrdRep.AddPrintCell( IK.DrawingDate, 10, 0, "l:f:" + RepFontStyleTitel );
      MemOrdRep.AddPrintCell( setApp(IK.Nominal.Nominal,is_ap)+" "+IK.FaceValueFiCcy(), 23, 0, "l:" + RepFontStyleTitel );
      MemOrdRep.AddStr();
      stat = IK.Next_IK();
    end;
  end;
end;                                    

private macro ПолучитьCчета(PaymentID, PayerAcc, ReceiverAcc, PayDate)
   var document, Select, query;
   
   query =   " select d.t_Account_Payer, d.t_Account_Receiver"
           + "   from dacctrn_dbt d, dpmdocs_dbt p "
           + "  where p.t_PaymentID = ? "
           + "    and d.t_AccTrnID = p.t_AccTrnId ";


   Select = RSDCommand( query );

   Select.AddParam("", RSDBP_IN, PaymentID );         /*1*/

   Select.Execute();

   document = TRsbDataSet(Select);
   if(document.moveNext())
     SetParm(1, document.Account_Payer);
     SetParm(2, document.Account_Receiver);
     return true;
   end;

   return false;
end;

private macro BalAccount(AccNum, FIID)
   var acc = TBFile("account");
   acc.rec.Chapter       = 5;
   acc.rec.Account       = AccNum;
   acc.rec.Code_Currency = FIID;
   if(not GetEQ(acc))
      runerror("Не найден счет ", AccNum);
   end;
   return acc.rec.Balance;
end;

private macro СontractorInfo(PartyID)
   var party = TBFile("party");

   party.rec.PartyID = PartyID;
   if(GetEQ(party))
      return party.rec.ShortName+"  ИНН "+ПолучитьКодСубъекта(PartyID, PTCK_INN);
   else
      return "";
   end;
end;

/*Данные об агенте*/
private macro Agent(PartyID)
  
  file country("country","rsrfdb.def") key 2;
  file paper  ("paprkind") key 0;
  file persn  ("persn");
  file party  ("party");
  record persnidc("persnidc");
  
  var Name_Country;
  var PaperName    = "_________________________";
  var PaperSeries  = "___________";
  var PaperNumber  = "___________";
  var PaperIssDate = "___________";
  var PaperIssuer  = "_________________________";

  ClearRecord(party);
  party.PartyID = PartyID;
  GetEQ(party);

  if(GetPersonIdc(PartyID, persnidc)) 
    PaperSeries = persnidc.PaperSeries;
    PaperNumber = persnidc.PaperNumber;
    PaperIssDate= date_as_string(1,persnidc.PaperIssuedDate);
    PaperIssuer = persnidc.PaperIssuer;
    paper.PaperKind = persnidc.PaperKind;
    if( GetEQ(paper) )
      PaperName = paper.Name; 
    end;
  end;

  MemOrdRep.AddPrintCell( party.Name, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
  MemOrdRep.AddStr();
  MemOrdRep.AddPrintCell( "Документ:  " + PaperName, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
  MemOrdRep.AddStr();
  MemOrdRep.AddPrintCell( "Серия:  " + PaperSeries+"  № "+PaperNumber, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
  MemOrdRep.AddStr();
  MemOrdRep.AddPrintCell( "Выдан:  " + PaperIssDate+"  "+PaperIssuer, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
  MemOrdRep.AddStr();
end;



private macro SetAgentID(Payment, parm, AgentID)   /* var Order   =TBFile("spdraft", "R", 1); */
   var depoacnt = TBFile("depoacnt.dbt", "R", 2);
   
   if(parm==1)
    depoacnt.rec.BriefCode = Payment.PayerAccount;
   elif(parm==2)
    depoacnt.rec.BriefCode = Payment.ReceiverAccount;
   end;

   depoacnt.rec.Department = Payment.Department;
   GetEQ(depoacnt);
// #193597 to delete
//   setparm(2, depoacnt.rec.Operator);
   setparm(2, -1);
end;


macro DepoPrintMemOrder(Order, _Rep:@variant)   /* var Order   =TBFile("spdraft", "R", 1); */

   var spdrmove = TBFile("spdrmove.dbt");
   var pmpaym   = TBFile("pmpaym.dbt");
   var spground = TBFile("spground.dbt");
   
   var OrdKindName ="Мемориальный";
   var TickKindName="мемориальному ордеру"; 
   var AgentID = -1;
   var intAmount;
   var PayerAcc = "", ReceiverAcc = "", PayerBalAcc = "", ReceiverBalAcc = "", Direction = "";
   var Note = "";

   if((ValType(_Rep) != V_UNDEF) and (_Rep != null))
     MemOrdRep = _Rep;
   end;

   /*Квитанция*/
   macro Ticket(PartyID)
      DP_StdHeaderLO_Ex( MemOrdRep, MemOrdFontStyle, "Квитанция к "+TickKindName, 0, 0, false, {curdate} );
/*      StdRep = Depo_StdRepSection("Квитанция к "+TickKindName, spground.rec.Xld, {curdate}); */
      MemOrdRep.AddPrintCell( "Внес:", 10, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
      MemOrdRep.AddStr();

         Agent(PartyID);
      MemOrdRep.AddPrintCell( "Ценные бумаги: " + fin.Name+" рег. № выпуска "+avr.LSIN, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
      MemOrdRep.AddStr();
      intAmount = ЧислоCЗаданнойТочностью(pmpaym.rec.Amount, DP_GetPrecision(pmpaym.rec.Amount, fin.SumPrecision), "");
      MemOrdRep.AddPrintCell( "Количество, штук: " + intAmount, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
      MemOrdRep.AddStr();
      MemOrdRep.AddPrintCell( NumToStr(money(intAmount), "штука", "штуки", "штук", false, DP_GetPrecision(pmpaym.rec.Amount, fin.SumPrecision)), 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
      MemOrdRep.AddStr();
      ПечатьОписиСертификатов_Ex(pmpaym.rec,false);
      MemOrdRep.AddEmptyStr();
      MemOrdRep.AddEmptyStr();
      MemOrdRep.AddPrintCell( "   М.П.", 10, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
      MemOrdRep.AddStr();
      MemOrdRep.AddEmptyStr();
      MemOrdRep.AddEmptyStr();
   end; /*Квитанция*/

   spdrmove.rec.DraftID = Order.AutoKey; 
   if(not spdrmove.GetEQ)
      runerror("Не найден платеж");
   end;
   pmpaym.rec.PaymentID = spdrmove.rec.PaymentID;
   if(not pmpaym.GetEQ)
      runerror("Не найден платеж");
   end;

   spground.rec.SPgroundID = Order.SPgroundID;
   if(not spground.GetEQ())
      runerror("Не найден документ -- основание бухгалтерской операции");
   end;

   if(ПолучитьФинИн(pmpaym.rec.FIID, fin, avr) != 0)
      runerror("Не найден финансовый инструмент сделки");
   end;

   if(ПолучитьCчета(pmpaym.rec.PaymentID, PayerAcc, ReceiverAcc, pmpaym.rec.ValueDate) == true)
      PayerBalAcc = BalAccount(PayerAcc, pmpaym.rec.FIID); 
      ReceiverBalAcc = BalAccount(ReceiverAcc, pmpaym.rec.FIID);
      if(ReceiverBalAcc == "98000") /*хранилище*/
         OrdKindName = "Расходный кассовый";
         TickKindName= "расходному кассовому ордеру";
         Direction = "Выдать:"; 
         SetAgentID(pmpaym.rec, 1, AgentID);

      elif(PayerBalAcc == "98000")  /*хранилище*/
         OrdKindName = "Приходный кассовый";
         TickKindName= "приходному кассовому ордеру";
         Direction = "Принять от:"; 
         SetAgentID(pmpaym.rec, 2, AgentID);
      end;
   end;

   /*ПЕЧАТЬ ОРДЕРА*/
   DP_StdHeaderLO_Ex( MemOrdRep, MemOrdFontStyle, OrdKindName+" ордер", 0, 0, false, {curdate} );
/*   StdRep = Depo_StdRepSection(OrdKindName+" ордер", spground.rec.Xld, {curdate}); */

   MemOrdRep.AddEmptyStr();
   MemOrdRep.AddPrintCell( "Ценные бумаги: " + fin.Name+ " рег. № выпуска " + avr.LSIN, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddStr();
   MemOrdRep.AddEmptyStr();
   intAmount = ЧислоCЗаданнойТочностью(pmpaym.rec.Amount, DP_GetPrecision(pmpaym.rec.Amount, fin.SumPrecision), "");   
   MemOrdRep.AddPrintCell( "Количество, штук: " + intAmount, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddStr();
   MemOrdRep.AddPrintCell( NumToStr(money(intAmount), "штука", "штуки", "штук", false, DP_GetPrecision(pmpaym.rec.Amount, fin.SumPrecision)), 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddStr();
   MemOrdRep.AddEmptyStr();
   MemOrdRep.AddPrintCell( "Отправитель:   " + СontractorInfo(pmpaym.rec.Payer), 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddStr();
   MemOrdRep.AddPrintCell( "Л/С № ", 6, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddPrintCell( PayerAcc, 26, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddPrintCell( "  Б/С " + PayerBalAcc, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddStr();
   MemOrdRep.AddEmptyStr();
   MemOrdRep.AddPrintCell( "Получатель:    " + СontractorInfo(pmpaym.rec.Receiver), 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddStr();
   MemOrdRep.AddPrintCell( "Л/С № ", 6, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddPrintCell( ReceiverAcc, 26, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddPrintCell( "  Б/С " + ReceiverBalAcc, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddStr();
   MemOrdRep.AddEmptyStr();
   MemOrdRep.AddPrintCell( "Тип поручения: " + OperationName(Order.Kind_Operation), 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddStr();
   MemOrdRep.AddPrintCell( "Основание: " + GroundKind(1050, spground.rec.Kind)+" № "+spground.rec.Xld+" от "+Order.Date, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddStr();
   MemOrdRep.AddPrintCell( "Содержание транзакции:  " + GroundKind(1200, Order.Product), 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddStr();

   Note = readNoteForObject( OBJTYPE_DEPODRAFT,
                             string(Order.AutoKey),
                             ConditionDepo);
   if(Note != "")
    MemOrdRep.AddPrintCell( "Примечание: " + Note, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
    MemOrdRep.AddStr();
   end;

   if(AgentID != -1)

      MemOrdRep.AddPrintCell( Direction, 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
      MemOrdRep.AddStr();
      MemOrdRep.AddEmptyStr();
      Agent(AgentID);
   end;
   MemOrdRep.AddEmptyStr();
   MemOrdRep.AddPrintCell( "--------------------------------------------------------------", 100, 0, "l:" + MemOrdFontStyle, REP_ELEM_STR );
   MemOrdRep.AddStr();
   if(AgentID != -1)
      Ticket(AgentID);
   end;
   DP_StdFooter_Ex(MemOrdRep, MemOrdFontStyle);

   _Rep = MemOrdRep;

end;