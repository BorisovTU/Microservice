/*
$Name:         nsdImpDrafts.mac
$Module:       Депозитарий
$Description:  Импорт НРД. Итоги торгов (MS140)
*/
import BankInter, PTInter, CTInter, DpInter, rsexts;
import dlquery, DlReport_h;
import "dpimp.mac", secinter, globals, dprepsec;

private file reporth() dbf; // в класс не запихнуть
private file clirtr() dbf;  // в класс не запихнуть

private const DOCKIND_NSD_REPORT = 396;  // Отчёт НРД

private const OBJTYPE_DEPODRAFT = 104;

private const DEPOATTR_ATTNUM_BLOCK = 6; /* Блокировка ц/б */

private const REFOBJ_OPERATIONDEPO     = 1;     // ObjectType = 109; номер поручения на операцию депо

private const SIDEBALANCE_ACTIVE  = 1, /* Активные счета Депо */
              SIDEBALANCE_PASSIVE = 2; /* Пассивные счета Депо */

private class CNSDProtocolStr(StrNum, DepoAcc, DepoPart, FI_Code, FI_Name, Amount, Reason)
  var m_StrNum   = StrNum;
  var m_DepoAcc  = DepoAcc;
  var m_DepoPart = DepoPart;
  var m_FI_Code  = FI_Code;
  var m_FI_Name  = FI_Name;
  var m_Amount   = Amount;
  var m_Reason   = Reason;
end;

private class CNSDProtocol(Reg_No:string, Reg_Date:date)
  var m_StrArr = TArray();
  var m_TotalCnt = 0;
  var m_SuccessfulCnt = 0;
  var m_Reg_No = Reg_No;
  var m_Reg_Date = Reg_Date;
  var m_HeadTable = "┌───┬────────────┬──────────────┬────────────────────┬─────────────────┬────────────────────────────┬──────────┬────────────────────────────────────────────────────────────────┐\n"+
                    "│ № │Номер строки│Счёт депо     │Раздел счёта депо   │Код ценной бумаги│Краткое наименование ц/б    │Количество│Причина                                                         │\n"+
                    "├───┼────────────┼──────────────┼────────────────────┼─────────────────┼────────────────────────────┼──────────┼────────────────────────────────────────────────────────────────┤";

  var m_tablewidth = Index(m_HeadTable, "\n");
  var m_Rep = CMakeReport(m_HeadTable);
  const m_RepFontStyleTitel =  "ex_FS(b)";

  macro SetCount(cnt)
    m_TotalCnt = cnt;
  end;

  macro IncSuccessful()
    m_SuccessfulCnt = m_SuccessfulCnt + 1;
  end;

  macro SetError(StrNum, DepoAcc, DepoPart, FI_Code, FI_Name, Amount, Reason)
    m_StrArr[m_strarr.size] = CNSDProtocolStr(StrNum, DepoAcc, DepoPart, FI_Code, FI_Name, Amount, Reason);
  end;

  macro GetCntErr()
    return m_StrArr.size;
  end;

  macro Print()
    var i = 0;
    var wh = 30;

    DP_AddPrintCell( m_Rep, "Протокол генерации поручений по итогам торгов", m_tablewidth-2, 0, "c:w:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    DP_AddPrintCell( m_Rep, "Номер комплекта:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, m_Reg_No, m_tablewidth-2-wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    DP_AddPrintCell( m_Rep, "Дата комплекта:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, m_Reg_Date, m_tablewidth-2-wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    m_Rep.AddEmptyStr();
    m_Rep.AddStr();
    DP_AddPrintCell( m_Rep, "Всего записей:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, m_TotalCnt, m_tablewidth-2-wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    DP_AddPrintCell( m_Rep, "Обработано успешно:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, m_SuccessfulCnt, m_tablewidth-2-wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    DP_AddPrintCell( m_Rep, "Ошибки:", wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    DP_AddPrintCell( m_Rep, GetCntErr(), m_tablewidth-2-wh, 0, "l:" + m_RepFontStyleTitel, false, REP_ELEM_STR );
    m_Rep.AddStr();
    m_Rep.AddEmptyStr();
    m_Rep.AddStr();

    while(i < m_strarr.size)

      DP_AddPrintCell( m_Rep, i+1,                    0, 0, "c:" + m_RepFontStyleTitel );
      DP_AddPrintCell( m_Rep, m_StrArr[i].m_StrNum,   0, 0, "l:" + m_RepFontStyleTitel );
      DP_AddPrintCell( m_Rep, m_StrArr[i].m_DepoAcc,  0, 0, "l:" + m_RepFontStyleTitel );
      DP_AddPrintCell( m_Rep, m_StrArr[i].m_DepoPart, 0, 0, "l:" + m_RepFontStyleTitel );
      DP_AddPrintCell( m_Rep, m_StrArr[i].m_FI_Code,  0, 0, "l:" + m_RepFontStyleTitel );
      DP_AddPrintCell( m_Rep, m_StrArr[i].m_FI_Name,  0, 0, "l:" + m_RepFontStyleTitel );
      DP_AddPrintCell( m_Rep, m_StrArr[i].m_Amount,   0, 0, "l:" + m_RepFontStyleTitel );
      DP_AddPrintCell( m_Rep, m_StrArr[i].m_Reason,   0, 0, "l:" + m_RepFontStyleTitel );

      m_Rep.AddStr();

      i = i + 1;
    end;

    m_Rep.PrintWinRep();
    m_Rep.ShowWinRep();

  end;

end;

private macro GetAcntByNSDCode(Kind:integer, NSD_AcntCode:string, NSD_PartCode:string, RecordAcnt)
  var cmd, query, DataSet;

  query =   "select part.* "
          + "  from ddepoacnt_dbt part, ddepoacnt_dbt root "
          + " where part.t_Kind = ? "
          + "   and part.t_Department = ? "
          + "   and root.t_AutoKey = part.t_Root "
          + "   and RTRIM(rsb_struct.getString(rsi_rsb_kernel.GetNote(112, TO_CHAR(root.t_AutoKey), 2, ?)), CHR(0)) = ? "
          + "   and RTRIM(rsb_struct.getString(rsi_rsb_kernel.GetNote(122, TO_CHAR(part.t_AutoKey), 2, ?)), CHR(0)) = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(Kind);
  cmd.AddParam({OperDprt});
  cmd.AddParam(date({curdate}));
  cmd.AddParam(NSD_AcntCode);
  cmd.AddParam(date({curdate}));
  cmd.AddParam(NSD_PartCode);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo( RecordAcnt.rec );
  end;
end;

class CNSDImportDrafts()
  private var path:string = ""; // Путь до папки импорта с завершающим \\
  private var cntPrevDrafts = 0;
  private var EnrolmentOprNum = NULL, EnrolmentOprNum_Client = NULL;
  private var WriteOffOprNum = NULL, WriteOffOprNum_Client = NULL;
  private var protocol = NULL;
  private var ImpDate=date(0,0,0), ImpNo;

  private macro error(errstr)
    protocol.SetError(clirtr.RpClir_ID,
                      trim(clirtr.Depo_Acc_C),
                      trim(clirtr.Section_Co),
                      trim(clirtr.Security_C)+"/"+trim(clirtr.Issue_Code),
                      trim(ToOEM(clirtr.Iss_Short_,true)),
                      trim(clirtr.Seq_Amo),
                      errstr);
  end;

  private macro GetEnrolOprNum(IsOwn:bool, OprKind:@variant)
    var err = 0;

    if(IsOwn)
      if(EnrolmentOprNum == NULL)
        GetRegistryValue("DEPO\\NSD\\NSD_MS140_ENROLL", V_INTEGER, EnrolmentOprNum, err);
        if(err)
          error("Ошибка при получении значения настройки DEPO\\NSD\\NSD_MS140_ENROLL");
          EnrolmentOprNum = NULL;
        end;
      end;

      if(not err)
        OprKind = EnrolmentOprNum;
      end;
    else
      if(EnrolmentOprNum_Client == NULL)
        GetRegistryValue("DEPO\\NSD\\NSD_MS140_ENROLL_CLIENT", V_INTEGER, EnrolmentOprNum_Client, err);
        if(err)
          error("Ошибка при получении значения настройки DEPO\\NSD\\NSD_MS140_ENROLL_CLIENT");
          EnrolmentOprNum_Client = NULL;
        end;
      end;

      if(not err)
        OprKind = EnrolmentOprNum_Client;
      end;
    end;

    return err;
  end;

  private macro GetWriteOffOprNum(IsOwn:bool, OprKind:@variant)
    var err = 0;

    if(IsOwn)
      if(WriteOffOprNum == NULL)
        GetRegistryValue("DEPO\\NSD\\NSD_MS140_WRITEOFF", V_INTEGER, WriteOffOprNum, err);
        if(err)
          error("Ошибка при получении значения настройки DEPO\\NSD\\NSD_MS140_WRITEOFF");
          WriteOffOprNum = NULL;
        end;
      end;

      if(not err)
        OprKind = WriteOffOprNum;
      end;
    else
      if(WriteOffOprNum_Client == NULL)
        GetRegistryValue("DEPO\\NSD\\NSD_MS140_WRITEOFF_CLIENT", V_INTEGER, WriteOffOprNum_Client, err);
        if(err)
          error("Ошибка при получении значения настройки DEPO\\NSD\\NSD_MS140_WRITEOFF_CLIENT");
          WriteOffOprNum_Client = NULL;
        end;
      end;

      if(not err)
        OprKind = WriteOffOprNum_Client;
      end;
    end;

    return err;
  end;

  private macro getCountRecords(f)
    var i = 0;
    while (next(f))
      i = i + 1;
      Message("Подсчет количества записей");
    end;
    return i;
  end;

  private macro FindPrevDraft()
    var cmd, query, DataSet;
    var DraftID = 0;

    query =   " select draft.t_AutoKey"
            + "   from dspground_dbt spgr, dspdraft_dbt draft "
            + "  where spgr.t_SPgroundID = draft.t_SPgroundID "
            + "    and spgr.t_Kind = " + DOCKIND_NSD_REPORT
            + "    and spgr.t_SignedDate = ? "
            + "    and spgr.t_AltXld = ? "
            + "    and RTRIM(rsb_struct.getString(rsi_rsb_kernel.GetNote("+OBJTYPE_DEPODRAFT+", TO_CHAR(draft.t_AutoKey), 5, ?)), CHR(0)) = ? ";

    cmd = DL_RSDCommand(query);
    cmd.AddParam(date(reporth.Reg_Date));
    cmd.AddParam(string(trim(reporth.Reg_No)));
    cmd.AddParam(date({curdate}));
    cmd.AddParam(string(ЧислоCЗаданнойТочностью(clirtr.RpClir_Id,0)));

    DataSet = cmd.Execute();

    if(DataSet.moveNext())
      DraftID = int(DataSet.AutoKey);
    end;

    return DraftID;
  end;

  private macro GetSpgroundID()
    var SPgroundID = 0;
    var cmd, query, DataSet;
    var err = 0;

    query =   " select spgr.t_SPgroundID"
            + "   from dspground_dbt spgr "
            + "  where spgr.t_Kind = " + DOCKIND_NSD_REPORT
            + "    and spgr.t_SignedDate = ? "
            + "    and spgr.t_AltXld = ? ";

    cmd = DL_RSDCommand(query);
    cmd.AddParam(date(reporth.Reg_Date));
    cmd.AddParam(string(trim(reporth.Reg_No)));

    DataSet = cmd.Execute();

    if(DataSet.moveNext())
      SPgroundID = int(DataSet.SPgroundID);
    end;

    return SPgroundID;
  end;

  private macro GetFIIDbyCodeNSD()
    var query, cmd, DataSet;
    var FIID = -1;

    if(string(trim(clirtr.Security_C)) != "")
      query =   "select t_ObjectID "
              + "  from dobjcode_dbt "
              + " where t_ObjectType = " + OBJTYPE_FININSTR
              + "   and t_CodeKind = 14 /*FICK_NADC*/"
              + "   and t_Code = ? ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(string(trim(clirtr.Security_C)));

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        FIID = int(DataSet.ObjectID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIIDbyISIN()
    var query, cmd, DataSet;
    var FIID = -1;

    if(string(trim(clirtr.Issue_Code)) != "")
      query =   "select t_FIID "
              + "  from davoiriss_dbt "
              + " where t_ISIN = ? ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(string(trim(clirtr.Issue_Code)));

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        FIID = int(DataSet.FIID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIIDbyLSIN()
    var query, cmd, DataSet;
    var FIID = -1;

    if(string(trim(clirtr.State_Regi)) != "")
      query =   "select t_FIID "
              + "  from davoiriss_dbt "
              + " where t_LSIN = ? ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(string(trim(clirtr.State_Regi)));

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        FIID = int(DataSet.FIID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIID()
    var FIID = -1;

    FIID = GetFIIDbyCodeNSD();
    if(FIID == -1)
      FIID = GetFIIDbyISIN();
      if(FIID == -1)
        FIID = GetFIIDbyLSIN();
      end;
    end;

    return FIID;
  end;

  private macro GetAcntByID(AcntID, acnt)
    var cmd, DataSet;
    acnt.Clear();

    cmd = DL_RSDCommand("select * from ddepoacnt_dbt where t_AutoKey = ?");
    cmd.AddParam(AcntID);

    DataSet = cmd.Execute();
    if(DataSet.MoveNext())
      DataSet.GetRecord().CopyTo( acnt.rec );
    end;
  end;

  //проверить, собственное ли поручение или клиентское
  private macro IsOwnDraft()
    if(Index(clirtr.Depo_Acc_C, "S") == 2)
      return true;
    end;

    return false;
  end;

  private macro GetBlockByAccID(AutoKey, Block:@variant)
    var query, cmd, DataSet;

    Block = 0;

    //найдем блокировку раздела
    query = " select TO_NUMBER(atvl.t_Value) t_Block " +
            " from ddepoatvl_dbt atvl " +
            " where atvl.t_ID = ? " +
            " and atvl.t_IsType = CHR(0) " +
            " and atvl.t_AttrNum = " + DEPOATTR_ATTNUM_BLOCK;

    cmd = DL_RSDCommand(query);
    cmd.AddParam(AutoKey);

    DataSet = cmd.Execute();
    if(DataSet.MoveNext())
      Block = DataSet.Block;
    end;
  end;

  private macro GetSpgroundByXid(Xid, Direction, Division, Kind, RegistrDate)
    var SPgroundID = 0;
    var cmd, query, DataSet;
    var err = 0;

    query =   " select spgr.t_SPgroundID"
            + "   from dspground_dbt spgr "
            + "  where spgr.t_Xld = ? "
            + "    and spgr.t_Direction = " + Direction
            + "    and spgr.t_Division = " + Division
            + "    and spgr.t_Kind = " + DOCKIND_NSD_REPORT
            + "    and spgr.t_RegistrDate = ? ";

    cmd = DL_RSDCommand(query);
    cmd.AddParam(Xid);
    cmd.AddParam(RegistrDate);

    DataSet = cmd.Execute();

    if(DataSet.moveNext())
      SPgroundID = int(DataSet.SPgroundID);
    end;

    return SPgroundID;
  end;

  private macro GenerateXid( RefDoc, SPground )
    var err = 0, Xid = "", Division = 0, ErrCode = 0;

    GetRegistryValue( "DEPO\\OURDEPOSITOFFICE", V_INTEGER, Division, ErrCode );
    if( ErrCode != 0 )
      error( "Ошибка при чтении настройки \"" + "DEPO\\OURDEPOSITOFFICE" + "\"." );
      err = 1;
    end;

    while( err == 0 )
      if( GenerateReference( RefDoc, Xid, 0, SPground ) )
        error( "Ошибка при генерации референса по описателю " + string( RefDoc ) );
        err = 1;
      end;

      if( ( err == 0 ) and not GetSpgroundByXid( Xid, Division, SPground.Department, SPground.Kind, SPground.RegistrDate ) )
        SPground.Xld = Xid;
        break;
      end;
    end;

    return err;
  end;

  // Если буфер транзитного счета и блокировка ранее были подкачаны - записать их значения в буфер счета получателя
  // В противном случае - получить ID транзитного счета и блокировку из настроек, найти их и записать в буфер транзитного счета и буфер счета получателя
  private macro GetAcntFromRegistry(AcntPath,                 // путь к настройке счета
                                    BlockPath,                // путь к настройке блокировки
                                    Block:@variant,           // блокировка счета получателя
                                    AcntBuf,                  // буфер счета получателя
                                    TransAcntBuf,             // буфер транзитного счета
                                    TransAcntBlock:@variant,  // блокировка транзитного счета
                                    HasTransAcnt:@variant)    // флаг "Буфер транзитного счета заполнен"
    var err = 0, AcntID = 0;

    if(HasTransAcnt)
      Copy(AcntBuf, TransAcntBuf);
      Block = TransAcntBlock;
    else
      GetRegistryValue(AcntPath, V_INTEGER, AcntID, err);
      if(err)
        error("Ошибка при получении значения настройки " + AcntPath);
      else
        GetRegistryValue(BlockPath, V_INTEGER, Block, err);
        if(err)
          error("Ошибка при получении значения настройки " + BlockPath);
        else
          GetAcntByID(AcntID, AcntBuf);
          if(AcntBuf.rec.AutoKey == 0)
            err = 1;
            error("Не найден счет/раздел из настройки " + AcntPath);
          else
            Copy(TransAcntBuf, AcntBuf);
            TransAcntBlock = Block;
            HasTransAcnt = true;
          end;
        end;
      end;
    end;

    return err;
  end;

  // чтение сведений о списании/зачислении ц/б по идентификатору отчета
  private macro processClirtr()
    var err = 0;
    var PrevDraftID = 0;
    var RefDoc = 0, AskForRegister = true;
    RECORD DepoDraftParm("draftprm.rec");
    record SPground("spground");
    record paym("pmpaym");
    var StorageAcnt = TRecHandler("depoacnt.dbt");
    var DeponentAcnt = TRecHandler("depoacnt.dbt");
    var OwnTransAcnt = TRecHandler("depoacnt.dbt");
    var ClientTransAcnt = TRecHandler("depoacnt.dbt");
    var OwnTransAcntBlock = 0, ClientTransAcntBlock = 0;
    var HasOwnTransAcnt = false, HasClientTransAcnt = false;
    var Block = 0, StorageBlock = 0;
    var Amount = $0;
    var i = 0;

    var cnt = getCountRecords(clirtr);

    InitProgress(cnt, "Обрабатывается справочник clirtr.dbf", "Импорт итогов торговой сессии");
    protocol.SetCount(cnt);

    rewind(clirtr);

    while((next(clirtr)) AND (err != 2))
      err = 0;
      Block = 0;
      PrevDraftID = 0;

      if(cntPrevDrafts > 0)
        PrevDraftID = FindPrevDraft();
      end;

      if(PrevDraftID != 0)
        err = 1;
        error("Поручение загружено ранее. Id поручения = "+PrevDraftID);
      else
        //формируем поручение

        ClearRecord(DepoDraftParm);
        ClearRecord(paym);

        StorageAcnt.Clear();
        DeponentAcnt.Clear();

        DepoDraftParm.Oper          = {Oper};
        DepoDraftParm.Department    = {OperDprt};
        DepoDraftParm.DealPartyCode = GetSecurNRDCode();

        Amount = money(StrSubst(clirtr.Seq_Amo, " ", ""));
        DepoDraftParm.Amount = abs(Amount);

        DepoDraftParm.SPgroundID = GetSpgroundID();
        if(DepoDraftParm.SPgroundID == 0) // Будем вводить новый ПД
          ClearRecord(SPground);

          SPground.Kind        = DOCKIND_NSD_REPORT;
          SPground.RegistrDate = {curdate};
          SPground.RegistrTime = time();
          SPground.Party       = UnknownParty;
          SPground.AltXld      = string(trim(reporth.Reg_No));
          SPground.SignedDate  = date(reporth.Reg_Date);
          SPground.Proxy       = UnknownParty;
          SPground.UserLog     = SPGRUSERLOG_OURENTER;
          SPground.Deponent    = UnknownParty;

          //сгенерим референс входящих документов
          if(GetReferenceIDByType(OBJTYPE_OPERATIONDEPO, REFOBJ_OPERATIONDEPO, RefDoc))
            error("Не найден описатель референса");
            err = 1;
          end;

          if(not err)
            err = GenerateXid(RefDoc, SPground);
          end;

          if(not err and AskForRegister)
            if(not GetTRUE(true, "Выполнить регистрацию документа?|Вид документа: \"Отчёт НРД\"|Вид журнала: Входящие депозитария|Вх.Номер: " + SPground.Xld + "|Дата регистрации: " + SPground.RegistrDate + "|Время регистрации: " + SPground.RegistrTime))
              error("Отказ пользователя от регистрации первичного документа");
              err = 2; // Выходим из процесса обработки выписки совсем
            else
              // Согласились регистрировать документ, запомним ответ и больше не спросим
              AskForRegister = false;
            end;
          end;

          if(not err)
            DepoDraftParm.Xid          = SPground.Xld;
            DepoDraftParm.UserLog      = SPground.UserLog;
            DepoDraftParm.DocKind      = SPground.Kind;
            DepoDraftParm.AltXid       = SPground.AltXld;
            DepoDraftParm.SignedDate   = SPground.SignedDate;
            DepoDraftParm.RegisterDate = SPground.RegistrDate;
            DepoDraftParm.RegisterTime = SPground.RegistrTime;
          end;
        end;

        if(not err)
          DepoDraftParm.ValueDate = date(reporth.OperDay);
          DepoDraftParm.Product = 1; /* Купля - продажа */

          DepoDraftParm.FIID = GetFIID();
          if(DepoDraftParm.FIID == -1)
            err = 1;
            error("Ценная бумага не найдена");
          end;
        end;

        if(not err)
          GetAcntByNSDCode(SIDEBALANCE_ACTIVE, string(trim(clirtr.Depo_Acc_C)), string(trim(clirtr.Section_Co)), StorageAcnt);
          if(StorageAcnt.rec.AutoKey <= 0)
            err = 1;
            error("Не найден счет/раздел места хранения");
          else
            GetBlockByAccID(StorageAcnt.rec.AutoKey, @StorageBlock);
          end;
        end;

        if(not err)
          if(IsOwnDraft()) //Собственная
            // Попытаемся найти счет, со структурой зеркальной активному (с теми же кодами в НРД в примечаниях, он должен быть один такой)
            GetAcntByNSDCode(SIDEBALANCE_PASSIVE, string(trim(clirtr.Depo_Acc_C)), string(trim(clirtr.Section_Co)), DeponentAcnt);
            if(DeponentAcnt.rec.AutoKey > 0)
              GetBlockByAccID(DeponentAcnt.rec.AutoKey, @Block);
            else // Счета не нашлось, возьмем стандартный счет зачисления из настроек
              err = GetAcntFromRegistry("DEPO\\NSD\\NSD_OWN_ACC", "DEPO\\NSD\\NSD_OWN_BLOCK", @Block, DeponentAcnt, OwnTransAcnt, @OwnTransAcntBlock, @HasOwnTransAcnt);
            end;
          else //клиентская
            // Стандартный счет зачисления из настроек
            err = GetAcntFromRegistry("DEPO\\ENROLOPRTRANSACC", "DEPO\\ENROLOPRTRANSBLOCK", @Block, DeponentAcnt, ClientTransAcnt, @ClientTransAcntBlock, @HasClientTransAcnt);
          end;

          if(not err)
            if(Amount > 0) //Зачисление
              DepoDraftParm.Kind     = SP_DEPOPER_ENROLMENT;
              err = GetEnrolOprNum(IsOwnDraft(), @DepoDraftParm.OperKind);

              DepoDraftParm.UnBlock     = StorageBlock;
              DepoDraftParm.DwOurCorAcc = StorageAcnt.rec.BriefCode;
              DepoDraftParm.DwCustodyID = StorageAcnt.rec.Owner;

              DepoDraftParm.Block     = Block;
              DepoDraftParm.BnAccCode = DeponentAcnt.rec.BriefCode;
              DepoDraftParm.BnPartyID = DeponentAcnt.rec.Owner;

            elif(Amount < 0) //Списание
              DepoDraftParm.Kind     = SP_DEPOPER_TRANSFERORDER;
              err = GetWriteOffOprNum(IsOwnDraft(), @DepoDraftParm.OperKind);

              DepoDraftParm.Block       = StorageBlock;
              DepoDraftParm.BnOurCorAcc = StorageAcnt.rec.BriefCode;
              DepoDraftParm.BnCustodyID = StorageAcnt.rec.Owner;

              DepoDraftParm.UnBlock   = Block;
              DepoDraftParm.DwAccCode = DeponentAcnt.rec.BriefCode;
              DepoDraftParm.DwPartyID = DeponentAcnt.rec.Owner;

            end;
          end;
        end;

        if(not err)
          RslDefCon.BeginTrans();

          var errorStr = "";
          err = DP_InsertDeferDraft(DepoDraftParm, paym, true, errorStr );
          if(err)
            error(errorStr);
          else
            err = addNoteForObject( OBJTYPE_DEPODRAFT, DepoDraftParm.DraftID, 5, string(ЧислоCЗаданнойТочностью(clirtr.RpClir_Id,0)) );
            if(err)
              error("Ошибка при добавлении примечания к поручению");
            end;
          end;

          if(not err)
            RslDefCon.CommitTrans();
          else
            RslDefCon.RollbackTrans();
          end;
        end;

        // Если собирались вставлять ПД и генерировали номер и не вставили - вернем сгенерированное значение
        if((err) AND (DepoDraftParm.SPgroundID == 0) AND (DepoDraftParm.Xid != "") AND (RefDoc))
          RestoreReference(RefDoc, DepoDraftParm.Xid);
        end;
      end;

      if(not err)
        protocol.IncSuccessful();
      end;

      i = i + 1;
      UseProgress(i);
    end;

    RemProgress();

  end;

  private macro CheckReImport()
    var cmd, query;
    Array Text;
    Array Button;
    Button(0) = "Загрузить недостающие";
    Button(1) = "Отмена";
    var but, ind = 0;

    query =   " select 1"
            + "   from dspground_dbt spgr, dspdraft_dbt draft "
            + "  where spgr.t_SPgroundID = draft.t_SPgroundID "
            + "    and spgr.t_Kind = " + DOCKIND_NSD_REPORT
            + "    and spgr.t_SignedDate = ? "
            + "    and spgr.t_AltXld = ? ";

    cmd = DL_RSDCommand(query);
    cmd.AddParam(date(reporth.Reg_Date));
    cmd.AddParam(string(trim(reporth.Reg_No)));

    cntPrevDrafts = cmd.GetCount();
    if(cntPrevDrafts > 0)
      Text(ind) = "Импорт данного отчёта "+string(trim(reporth.Reg_No))+" выполнялся ранее. Найдено "+cntPrevDrafts+" поручений. Выберите вариант обработки";
      but = ConfWin(Text,Button);
      if(but != 0)
        return false;
      end;
    end;

    return true;
  end;

  // чтение заголовочного файла
  private macro processReportH()
    var err = 0;

    protocol = CNSDProtocol(ImpNo, date(reporth.Reg_Date));
    processClirtr();
    err = protocol.GetCntErr();
    protocol.Print();

    return err;
  end;

  macro moveFile(path:string, fileName:string, dir:string)
    var pathDone = "";
    var day, month, year;

    DateSplit( ImpDate, day, month, year );
    if(day < 10)
      day = string("0"+day);
    end;
    if(month < 10)
      month = string("0"+month);
    end;

    pathDone = path + dir+"\\";
    MakeDir(pathDone);
    pathDone = path + dir+"\\" + string(day)+string(month)+string(year)+"\\";
    MakeDir(pathDone);
    pathDone = path + dir+"\\" + string(day)+string(month)+string(year)+"\\"+ImpNo+"\\";
    MakeDir(pathDone);

    if (CopyFile(path + fileName, pathDone + fileName))
      RemoveFile(path + fileName);
    else
      msgbox("Ошибка при перемещении файла " + fileName + " в папку " + pathDone);
    end;
  end;

  /* Чтение пути справочников */
  macro Run() : bool
    var err = 0;
    var stat = true;
    var need_break = false;

    GetRegistryValue("DEPO\\NSD\\NSD_MS140_PATH", V_STRING, path, err);
    if(path == "")
      msgbox("Не указан путь к сетевой папке для загрузки отчётов с итогами торговой сессии.|Настройка DEPO\\NSD\\NSD_MS140_PATH");
    else
      path = path + "\\";

      if(open(reporth, path+"reporth.dbf"))
        if(open(clirtr, path+"clirtr.dbf"))

          if(next(reporth))

            ImpDate = date(reporth.OperDay);
            ImpNo   = trim(string(reporth.Reg_No));

            if(CheckReImport() == true)

              if(processReportH() != 0)
                stat = false;
              end;
            else
              need_break = true;
            end;
          end;

          close(clirtr);
        else
          err = 1;
          msgbox("Невозможно открыть файл " + path+"clirtr.dbf");
        end;
        close(reporth);

        if((err == 0) and (need_break == false))
          if(stat == false)
            moveFile(path, "reporth.dbf", "Err");
            moveFile(path, "clirtr.dbf", "Err");
          else
            moveFile(path, "reporth.dbf", "Done");
            moveFile(path, "clirtr.dbf", "Done");
          end;
        end;

      else
        err = 1;
        msgbox("Невозможно открыть файл " + path+"reporth.dbf");
      end;
    end;

    return err;
  end;
end;

CNSDImportDrafts().Run();
Exit(1);
