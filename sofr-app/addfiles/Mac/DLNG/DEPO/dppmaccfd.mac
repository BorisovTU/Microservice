/*
$Name:         dppmaccfd.mac
$Module:       Депозитарий
$Description:  Класс ПД для выплат по счету
*/

import InsCarryDoc, DealsInter, "mccatacc.mac", "dlquery.mac", PaymInter, OprInter;

const PW_OUTPAYMENT   = "O";  // Переводом, внешн.
const PW_INPAYMENT    = "I";  // Переводом, внутр.
const PW_CONSOLIDATED = "S";  // Переводом, сводн.

macro CheckDepoChargeIncome():Bool
  var regValue:Bool = false;
  var errCode:Integer = 0;

  GetRegistryValue( "DEPO\\CHARGEINCOME", V_BOOL, regValue, errCode );

  return ( ( errCode == 0 ) and regValue );
end;

macro DP_FindAccount(Account:STRING, Code_Currency:INTEGER, Chapter:INTEGER, acc:TRecHandler)
  var cmd, DataSet;
  var err = 0;

  cmd = DL_RSDCommand(  "select * from daccount_dbt"
                      + " where t_Account = ? "
                      + "   and t_Code_Currency = ? "
                      + "   and t_Chapter = ?"
                     ); 
  cmd.AddParam(Account);
  cmd.AddParam(Code_Currency);
  cmd.AddParam(Chapter);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo( acc.rec );
  else
    err = 1;
  end;

  return err;
end;


CLASS DP_PmaccFD (parm1:VARIANT, parm2:VARIANT)
  PRIVATE VAR pmacc = TBFile("dppmacc.dbt", "R", 1);
  PRIVATE VAR crp   = TBFile("dpcorpop.dbt");
  VAR Kind, ID, Error = 0; //$$$

  MACRO GetBasisFIRole( FIRole:INTEGER, NoMsgErr:BOOL )
    var i = 0;
    
    if (FIRole == FIROLE_UNDEF)
      return FIROLE_BA;
    end;

    return FIRole;
  END;

  MACRO OpenAccount( CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER )
    var BackOutAccount = false, ChangeOpenDate  = false, IsMass;
    var ret = true;

    if( (NoErrMes == null) OR (NoErrMes == false) )
       IsMass = 0;
    else 
       IsMass = 1;
    end;

    MC_GetAccountOpenParms( CatCode, @BackOutAccount, @ChangeOpenDate );

    FindAccount = MC_FindAndOpenAccount (
        CatCode,
        this,
        ActionDate,
        IsMass,
        MC_OPENACC_CREATE,
        AccBuf, FIID, null, NULL, null, FIROLE_BA, NULL, NULL, NULL, NULL, NULL,
        BackOutAccount,
        ChangeOpenDate
    );

    if( FindAccount=="" ) 
      if( IsMass == 0 ) 
         MsgBox("Счет категории \"" + CatCode + "\" неопределен или недоступен в данной операции");
      end;
      ret = false;
    end;

    return ret;
  END;

  /*$$$ Макрос корректировки открываемого счета. Account, accblnc, ORScheme можно менять*/
  MACRO CorrectAccount(account, accblnc, ORScheme, categ, templ, accdoc,  OperDate)
     return true;
  END;

  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )
    var Parm = -1;

    if( (ObjectID == OBJTYPE_KINDSUBJ) and (Classificator == LLCLASS_KINDSUBJ_DEPONENT) )
      var pt = TRecHandler("party.dbt");

      if (ПолучитьСубъекта(pmacc.rec.HolderID, pt) == 0)
        if(pt.rec.NotResident == "") //резидент
          if(pt.rec.LegalForm == 1) //ЮЛ
            Parm = 7;
          else
            Parm = 8;
          end;
        else
          if(pt.rec.LegalForm == 1) //ЮЛ
            Parm = 9;
          else
            Parm = 10;
          end;
        end;
      end;
    elif((ObjectID == OBJTYPE_BACKOFFICE) and (Classificator == LLCLASS_BACKOFFICE))
      Parm = OBJTYPE_BACKOFFICE_DEPO;
    elif((ObjectID == OBJTYPE_KINDDEPOPAY) and (Classificator == LLCLASS_KINDDEPOPAY))
      Parm = crp.rec.PaymentType;
    end;

    return Parm;
  END;

  MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )
    var Parametr = -1;

    if( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
      Parametr = crp.rec.Department;
    elif( ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY )  /*Валюта расчетов*/
      Parametr = crp.rec.Currency;
    end;

    return Parametr;
  END;

  /*конструктор класса */
  MACRO Construct( parm1:variant, parm2:variant )

    pmacc.Clear();
    pmacc.rec.DocumentID  = parm1;
    pmacc.rec.HolderAccID = parm2;

    if(not pmacc.GetEQ())
      RunError( "Не найдена запись о выплате.|Ошибка при создании класса "+GenClassName(this) );
    end;

    crp.Clear();
    crp.rec.DocumentID = pmacc.rec.DocumentID;
    if(not crp.GetEQ())
      RunError( "Не найдена запись об операции.|Ошибка при создании класса "+GenClassName(this) );
    end;

    Kind = crp.rec.DocKind;
    ID   = crp.rec.DocumentID;

  END;

  this.Construct( parm1, parm2 );

END;