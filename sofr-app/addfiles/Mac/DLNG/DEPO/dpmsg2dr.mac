/*--------------------------------------------------------------------------
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : dpcrdraft.mac
  Created     : 19.11.08
  Programmer  : Surzhakov M.A.
  Description : Массовое формирование поручений депозитария по входящим электронным сообщениям
  
---------------------------------------------------------------------------*/
Import "dpcrdraft.mac";


macro CreateDraftsFromMsgs(MsgTypes:String, OperCodes:String)

  var query = "";
  var emsg = null;
  var msgCount = 0;   //Кол-во обработанных сообщений
  var draftCount = 0; //Кол-во сгенерированных поручений
  var  msginf  = Tbfile("dpmsginf");
  RECORD msginf_rec (dpmsginf);
  var errStr = "";

  BeginLogging("em2drlog");  //Сообщения об ошибках будут выводится, фун-й WriteStrToLog(), в файл "em2drlog"
  
  //Если какой либо из параметров пустой - присваиваим ему значение по умолчанию
   if(MsgTypes == "")
    MsgTypes = MSG_TYPES_DEFAULT;
  end;
     
  if(OperCodes == "")
    OperCodes = OPER_CODES_DEFAULT;
  end;
  
  query  =    "select minf.* " 
           + " from ddpmsginf_dbt minf, ddpmsgpat_dbt pt" 
           + " where minf.t_MessageDirection = " + ENTERING     //Входящие
           +   " AND minf.t_State = " + DPMSGINF_STATE_REG      //Зарегестрировано
           +   " AND minf.t_MessageType In(" + MsgTypes + ")"   //Тип сообщения
           +   " AND NOT Exists( select dr.t_AutoKey"           //По сообщению нет сформированны поручений
           +                   " from dspdraft_dbt dr"
           +                   " where dr.t_AutoKey = minf.t_DraftID )"
           +   " AND pt.t_MessageID = minf.t_MessageID"
           +   " AND pt.t_PartyType = " + string(DPMSGPAT_SENDER)             //Реквезиты сообщения у получателя
           +   " AND pt.t_ProcessingCode In('" + OperCodes + "')"; //Код операции у отправителя
  emsg = TRsbDataSet(query);

  while(emsg.MoveNext())
    emsg.GetRecord().CopyTo(msginf.rec);
    if(InsertDepoDraftIntoDepart(msginf, true, @errStr) == 0) //возвращаемое знач. по правилам С
      draftCount = draftCount + 1;
    else
      WriteStrToLog(errStr);
    end;
    
    msgCount = msgCount + 1;
  end;
  
  WriteStrToLog("Обработано сообщений:"     + msgCount + "\n" + 
                  "  Сгенерированно поручений:" + draftCount);
                
  EndLogging(); //Сброс вывода ошибок в файл. 
  
end;