/************************************************************************/
/*-    ОПРЕДЕЛЕНИЕ ПОЛЬЗОВАТЕЛЬСКИХ ХАРАКТЕРИСТИК ПО ДАННЫМ ПОРУЧЕНИЯ  -*/
/*----------------------------------------------------------------------*/
/*-  Programming:   Alex V. Mishin                                     -*/
/*-  Creation: 07.02.2002                                              -*/
/************************************************************************/

Import BankInter, DPInter, PTinter, FIInter, CTInter, "dlcnst.inc", dl_voop;

const UNDEF_VALUE = "-1"; /* неопределеное значение */

/* Признак внутренней ц/б */
const UNDEFINED_FI = -1, /* Не определен */
      EXTERNAL_FI = 0,   /* Внешняя бумага */
      INTERNAL_FI = 1;   /* Внутренняя бумага */

/* Спец. счет эмитента */
const ISS_SPEC_ACC_NONE = 0, /* Нет счета */
      ISS_SPEC_ACC_R1 = 1,   /* Счет Р1 */
      ISS_SPEC_ACC_R2 = 2;   /* Счет Р2 */

/* Признак - эмитент является органом гос. власти РФ */
const ISS_STATE_BODY_UNDEF = -1,
      ISS_STATE_BODY_NONE = 0,
      ISS_STATE_BODY = 1;

/* Признак - эмитент является министерством финансов */
const ISS_MIN_FIN_UNDEF = -1,
      ISS_MIN_FIN_NONE = 0,
      ISS_MIN_FIN = 1;

/* Определяет параметры ФИ */
macro GetFIInfo( FIID, IsNotEmissive, IsInternal, AvoirKind, IssuerSpecialAcc, IsIsuerStateBody, IsIssuerMinFin )
  var NotEmissive, error;

  file partyown(partyown);

  record fin(fininstr);

  NotEmissive = ИндивидуальныйФинИн( FIID, error );
  if( valType(NotEmissive) == V_UNDEF )
    MsgBox("Ошибка определения эмиссионности ФИ с идентификатором ", FIID, " - ошибка ", error);
    return 1;
  end;

  SetParm(1, NotEmissive); /* Неэмиссионная бумага ? */

  if( ПолучитьФинИн(FIID, fin) != 0 )
    MsgBox("Не найден финансовый инструмент с идентификатором ", FIID);
    return 1;
  end;

  SetParm(2, UNDEFINED_FI); /* Внутренняя бумага? */

  if(DL_VOOP_AvoirissIsInternal(FIID))
    SetParm(2, INTERNAL_FI); /* Внутренняя бумага? */
  else
    SetParm(2, EXTERNAL_FI); /* Внутренняя бумага? */
  end;

  /* Вид ценной бумаги */
  SetParm(3, fin.AvoirKind);

  /* Спецсчет эмитета -  в текущей версии (до реализации таких счетов в РКО) -
     возвращаем - не имеет. */
  SetParm(4, ISS_SPEC_ACC_NONE);

  if( fin.Issuer != -1 )
    partyown.PartyID = fin.Issuer;
    partyown.PartyKind = PTK_STATE_BODY; /* Орган власти РФ */
    if(GetEQ(partyown))
      SetParm(5, ISS_STATE_BODY) ;
    else
      SetParm(5, ISS_STATE_BODY_NONE) ;
    end;

    partyown.PartyID = fin.Issuer;
    partyown.PartyKind = 33; /* Министерство финансов */
    if(GetEQ(partyown))
      SetParm(6, ISS_MIN_FIN) ;
    else
      SetParm(6, ISS_MIN_FIN_NONE) ;
    end;
  else
    SetParm(5, ISS_STATE_BODY_UNDEF);
    SetParm(6, ISS_MIN_FIN_UNDEF);
  end;

  return 0;
end;


/*
Процедура определяет значения пользовательских характеристик по данным поручения.

Передаваемые параметры:
AttrNum - номер характеристики
IsPayer - сторона проводки, == 1 - если дебет, 0 - если кредит

draft   - ссылка на первичный документ операции (поручение)
paym    - ссылка на основной платеж
prop_d  - ссылка на св-ва банка плательщика основного платежа
prop    - ссылка на св-ва банка получателя основного платежа
rmprop  - ссылка на св-ва платежа

Возвращаемое значение - строковый вариант значения характеристики,
"-1" - неопределеное значение,
в случае проблем возвращает пустую строку
*/

macro GetUserDEPOATVLValue( AttrNum, IsPayer, draft, paym, prop_d, prop, rmprop )
  var IsNotEmissive, IsInternal, AvoirKind, IssuerSpecialAcc, IsIssuerStateBody, IsIssuerMinFin;

  file dpac(depoacnt);
  file persn(persn);
  record party(party);

  Record spdraft(spdraft);
  Record pmpaym(pmpaym);
  Record pmprop_d(pmprop);
  Record pmprop(pmprop);
  Record pmrmprop(pmrmprop);

  SetBuff(spdraft,  draft);
  SetBuff(pmpaym,   paym);
  SetBuff(pmprop_d, prop_d);
  SetBuff(pmprop,   prop);
  SetBuff(pmrmprop, rmprop);

  if( AttrNum == 14 ) /* Системная характеристика "Вид специального раздела валютного регулирования" */
    /* найдем раздел */
    if( IsPayer )
      dpac.AutoKey = pmpaym.PayerDPNode;
    else
      dpac.AutoKey = pmpaym.ReceiverDPNode;
    end;

    if( NOT GetEQ(dpac) )
      MsgBox("Не найден раздел счета депо с идентификатором ", dpac.AutoKey);
      return UNDEF_VALUE;
    end;
   
    /* 1. Если счет  - активный, то неопределено (только для пассивных) */
    if( dpac.Kind == DP_SIDEBALANCE_ACTIVE )
      return UNDEF_VALUE;
    end;

    if( GetFIInfo( pmpaym.FIID, IsNotEmissive, IsInternal, AvoirKind, IssuerSpecialAcc, IsIssuerStateBody, IsIssuerMinFin ) )
      return UNDEF_VALUE;
    end;

    if( ПолучитьСубъекта(dpac.Owner, party) != 0 )
      MsgBox("Не найден владелец раздела счета депо с идентификатором ", dpac.Owner);
      return UNDEF_VALUE;
    end;

    /* 2. Если ц/б - неэмиссионная, то "Обычный"
       3. Если владелец счета  депо - резидент и ц/б - внутренняя - то Обычный
       4. Если владелец счета  - нерезидент, и ц/б - внешняя - то Обычный */
    if( (IsNotEmissive) OR 
        ((party.NotResident == "") AND (IsInternal == INTERNAL_FI)) OR
        ((party.NotResident == "X") AND (IsInternal == EXTERNAL_FI))
      )
      return String(OBJTYPE_KIND_SPEC_PART_COMMON);
    end;


    if( party.LegalForm == PTLEGF_PERSN )
      persn.PersonID = party.PartyID;
      if(NOT GetEQ(persn))
        MsgBox("Не найдена анкета физического лица с идентификатором ", persn.PersonID);
        return UNDEF_VALUE;
      end;
    else
      ClearRecord(persn);
    end;


    /* 5. Если владелец счета - резидент, и ц/б - внешняя то:
         а. если владелец - физлицо -не инд. предприниматель, то Ф
         б. если владелец - юрлицо или предприниматель, то
     - если эмитент имет счет Р1 в нашем банке -то Р1
     - иначе если эмитент имет счет Р2 в нашем банке -то Р2
     - иначе - Обычный */
    if( (party.NotResident == "") AND (IsInternal == EXTERNAL_FI) )
      if( (party.LegalForm == PTLEGF_PERSN) AND (persn.IsEmployer == "") )
        return String(OBJTYPE_KIND_SPEC_PART_F);
      else
        if( IssuerSpecialAcc == ISS_SPEC_ACC_R1 )
          return String(OBJTYPE_KIND_SPEC_PART_R1);
        elif( IssuerSpecialAcc == ISS_SPEC_ACC_R2 )
          return String(OBJTYPE_KIND_SPEC_PART_R2);
        else
          return String(OBJTYPE_KIND_SPEC_PART_COMMON);
        end;
      end;
    end;


    /* 6. Если владелец счета - нерезидент, и ц/б - внутренняя то:
         а. если ц/б - облигация, и эмитент имеет вид  орган власти РФ или министерство финансов - то С
         б. если ц/б акция или пай - то А
         в. если ц/б - облигация, и эмитент не имеет вид  ни орган власти РФ, ни министерство финансов - то О
         г. иной вид эмиссионной ц/б - Обычный */
    if( (party.NotResident == "X") AND (IsInternal == INTERNAL_FI) )
      if( ((DP_CheckAvoirKind(AvoirKind, AVOIRISSKIND_ORDINARY_BOND)) OR 
           (DP_CheckAvoirKind(AvoirKind, AVOIRISSKIND_COUPON_BOND)) OR 
           (DP_CheckAvoirKind(AvoirKind, AVOIRISSKIND_CONV_BOND))) AND
          ((IsIssuerStateBody == ISS_STATE_BODY) OR 
           (IsIssuerMinFin == ISS_MIN_FIN))
        )
        return String(OBJTYPE_KIND_SPEC_PART_S);
      elif((DP_CheckAvoirKind(AvoirKind, AVOIRISSKIND_EQUITY_SHARE)) OR 
           (DP_CheckAvoirKind(AvoirKind, AVOIRISSKIND_PREFERENCE_SHARE)) OR 
           (DP_CheckAvoirKind(AvoirKind, AVOIRISSKIND_PREFERENCE_CONV_SHARE))
          )
        return String(OBJTYPE_KIND_SPEC_PART_A);
      elif(((DP_CheckAvoirKind(AvoirKind, AVOIRISSKIND_ORDINARY_BOND)) OR 
            (DP_CheckAvoirKind(AvoirKind, AVOIRISSKIND_COUPON_BOND)) OR 
            (DP_CheckAvoirKind(AvoirKind, AVOIRISSKIND_CONV_BOND))) AND
           ((IsIssuerStateBody == ISS_STATE_BODY_NONE) AND 
            (IsIssuerMinFin == ISS_MIN_FIN_NONE))
          )
        return String(OBJTYPE_KIND_SPEC_PART_O);
      else
        return String(OBJTYPE_KIND_SPEC_PART_COMMON);
      end;
    end;
   
    return String(OBJTYPE_KIND_SPEC_PART_COMMON);
  end;


/*
  if( AttrNum == 5000 )
    return ...;
  elif(AttrNum == 5001 )
    return ...;
  else
    msxbox( "Неизвестный номер характеристики" );
    return "";
  end;
*/

  return UNDEF_VALUE;
end;

macro GetUserDEPOATVLValueGL( AttrNum, IsPayer, crp )

  Record corpop(dpcorpop);

  SetBuff(corpop,  crp);


  return UNDEF_VALUE;

end;


