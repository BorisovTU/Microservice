/**************************************************************/
/*         Автоматизированная банковская система RS-Bank      */
/*            Copyright (c) R-Style Software Lab 2005         */
/*                                                            */
/*  Имя файла        : dpmsgkvt.mac                           */
/*  Описание         : Автоквитовка электронных сообщений     */
/*  Программист      : Chernov Anton 06-12-2005               */
/*                                                            */
/**************************************************************/

import DPInter, CTInter, "cb_sql.mac";

private const ENTERING = 1; /* исходящее сообщение */
private const EXITING  = 2; /* входящее сообщение */

private const DEPOMSG_INFKIND_SPDRAFT = 1; // поручение

private macro FindDPMSGTP( MessageType, MessageKind )
  var q, rsd;

  q = " select t_MessageKind from ddpmsgtp_dbt "
    + " where t_MessageType = " + MessageType;

  rsd = TRsbDataSet(q);
  if( rsd.MoveNext() )
    MessageKind = rsd.MessageKind;
    SetParm( 1, MessageKind );
    return true;
  end;

  return false;
end;


// Подзапросы "отбираемые сообщения не приквитованы", "отбираемые сообщения указывают на базовое"
private macro UnkvitSelect(Direction, msg, query:@variant, Params)
  if( Direction == EXITING )
    query = query + " AND NOT (EXISTS ( " +
                    "             SELECT dpmsgkvt.t_amount " +
                    "               FROM ddpmsgkvt_dbt dpmsgkvt " +
                    "              WHERE dpmsgkvt.t_inmessageid = ? /*1*/ " +
                    "                AND dpmsgkvt.t_outmessageid = dpmsginf.t_messageid) " +
                    "         ) " +
                    " AND EXISTS ( " +
                    "        SELECT dpmsglnk.t_msglnkid " +
                    "          FROM ddpmsgdoc_dbt dpmsgdoc, ddpmsglnk_dbt dpmsglnk " +
                    "         WHERE dpmsglnk.t_messageid = dpmsginf.t_messageid " +
                    "           AND dpmsgdoc.t_messageid = ? /*2*/ " +
                    "           AND dpmsgdoc.t_doctype = ? /*3*/ " +
                    "           AND dpmsglnk.t_senderreference = dpmsgdoc.t_documentnumber) ";

    Params(Params.Size) = msg.MessageID; // 1
    Params(Params.Size) = msg.MessageID; // 2
    Params(Params.Size) = DPMSGDOC_RECEIVER; // 3
  else
    query = query + " AND NOT (EXISTS ( " +
                    "             SELECT dpmsgkvt.t_amount " +
                    "               FROM ddpmsgkvt_dbt dpmsgkvt " +
                    "              WHERE dpmsgkvt.t_outmessageid = ? /*1*/ " +
                    "                AND dpmsgkvt.t_inmessageid = dpmsginf.t_messageid) " +
                    "         ) " +
                    " AND EXISTS ( " +
                    "        SELECT dpmsglnk.t_msglnkid " +
                    "          FROM ddpmsgdoc_dbt dpmsgdoc, ddpmsglnk_dbt dpmsglnk " +
                    "         WHERE dpmsglnk.t_messageid = dpmsginf.t_messageid " +
                    "           AND dpmsgdoc.t_messageid = ? /*2*/ " +
                    "           AND dpmsgdoc.t_doctype = ? /*3*/ " +
                    "           AND dpmsglnk.t_receiverreference = dpmsgdoc.t_documentnumber) ";

    Params(Params.Size) = msg.MessageID; // 1
    Params(Params.Size) = msg.MessageID; // 2
    Params(Params.Size) = DPMSGDOC_SENDER; // 3
  end;
end;


// Отобрать все подтверждения по записи сообщения поручения draftmsg, еще не приквитованные к нему, с номером у корреспондента, не больше DocumentNumber
private macro MakeSelect(draftmsg, DocumentNumber, query:@variant, Params)
  var Direction;

  if( draftmsg.MessageDirection == ENTERING )
    Direction = EXITING;
  else
    Direction = ENTERING;
  end;

  query = " SELECT dpmsginf.* " +
          " FROM ddpmsgdoc_dbt alldoc, ddpmsginf_dbt dpmsginf " +
          " WHERE dpmsginf.t_messagedirection = ? /*1*/ " +
          "   AND alldoc.t_messageid = dpmsginf.t_messageid " +
          "   AND alldoc.t_doctype = ? /*2*/ ";

  Params(Params.Size) = Direction; // 1

  if(draftmsg.MessageDirection == ENTERING)
    Params(Params.Size) = DPMSGDOC_RECEIVER; // 2
  else
    Params(Params.Size) = DPMSGDOC_SENDER; // 2
  end;

  if((VALTYPE(DocumentNumber) != V_UNDEF) AND (DocumentNumber != "") )
    query = query + " AND alldoc.t_documentnumber <= ? /*3*/ ";
    Params(Params.Size) = DocumentNumber; // 3
  end;

  UnkvitSelect(Direction, draftmsg, @query, Params);

  query = query + " ORDER BY alldoc.t_documentnumber "; // Сортируем в порядке их отсылки корреспондентом, чтобы начать с самого раннего
end;


// Получить Номер подтверждения/уведомления по журналу корреспондента
private macro GetConfNumber(confmsg, DocumentNumber:@variant)
  var query, DataSet;
  var Select;

  query = " SELECT currdoc.t_DocumentNumber " +
          "  FROM ddpmsgdoc_dbt currdoc " +
          " WHERE currdoc.t_MessageID = ? /*1*/ " +
          "  AND currdoc.t_DocType = ? /*2*/ ";

  Select = RSDCommand( query );
  Select.NullConversion = true;

  Select.AddParam("MessageID", RSDBP_IN, confmsg.MessageID );
  if(confmsg.MessageDirection == ENTERING)
    Select.AddParam("DocType", RSDBP_IN, DPMSGDOC_SENDER );
  else
    Select.AddParam("DocType", RSDBP_IN, DPMSGDOC_RECEIVER );
  end;

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())
    DocumentNumber = DataSet.DocumentNumber;
    return true;
  end;

  return false;
end;


// Получить запись сообщения поручения draftmsg по записи сообщения подтверждения/уведомления confmsg
private macro GetDraftMessage(confmsg, draftmsg)
  var query, DataSet;
  var Select;

  if(confmsg.MessageDirection == ENTERING)
    query = " select msg.* " +
            " from ddpmsginf_dbt msg, ddpmsgdoc_dbt doc, ddpmsglnk_dbt lnk " +
            " where lnk.t_messageid = ? /*1*/ " +
            "   and doc.t_DocumentNumber = lnk.t_ReceiverReference " +
            "   and doc.t_DocType = ? /*2*/ " +
            "   and msg.t_MessageID = doc.t_Messageid ";
  else
    query = " select msg.* " +
            " from ddpmsginf_dbt msg, ddpmsgdoc_dbt doc, ddpmsglnk_dbt lnk " +
            " where lnk.t_messageid = ? /*1*/ " +
            "   and doc.t_DocumentNumber = lnk.t_SenderReference " +
            "   and doc.t_DocType = ? /*2*/ " +
            "   and msg.t_MessageID = doc.t_Messageid ";
  end;

  Select = RSDCommand( query );
  Select.NullConversion = true;

  if(confmsg.MessageDirection == ENTERING)
    Select.AddParam("MessageID", RSDBP_IN, confmsg.MessageID );
    Select.AddParam("DocType", RSDBP_IN, DPMSGDOC_SENDER );
  else
    Select.AddParam("MessageID", RSDBP_IN, confmsg.MessageID );
    Select.AddParam("DocType", RSDBP_IN, DPMSGDOC_RECEIVER );
  end;

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo(draftmsg.rec);
    return true;
  end;

  return false;
end;


/* Приквитовать все подходящие сообщения к указанному */
macro SelectMsgKvt( doc /*исходное сообщение*/ )
  var query, DataSet;
  var Select;
  var MessageKind, DocumentNumber;
  record msg( dpmsginf );
  var addmsg = TRecHandler("dpmsginf");
  var draftmsg = TRecHandler("dpmsginf");
  var Params = TArray, i;
  var stat = 0;

  SetBuff(msg, doc);

  if(NOT FindDPMSGTP(msg.MessageType, MessageKind))
    MsgBox("Не найдет тип сообщения " + msg.MessageType);
    stat = 1;
  end;

  if(NOT stat)
    if(MessageKind == DEPOMSG_INFKIND_SPDRAFT) // было передано сообщение о поручении
      MakeSelect(msg, "", @query, Params);  // Ищем квитуемые по поручению msg
    else
      if(NOT GetConfNumber(msg, @DocumentNumber) ) // Найдем номер подтверждения/уведомления, позднее которого отбирать не будем
        MsgBox("Не найден документ сообщения с ID " + msg.MessageID);
        stat = 1;
      end;
      if(NOT stat)
        if(NOT GetDraftMessage(msg, draftmsg)) // Получим по линку запись о поручении на который показывает сообщение, по которому квитуем
          MsgBox("Не найдено парное сообщение о поручении для сообщения с ID " + msg.MessageID);
          stat = 1;
        end;
        if(NOT stat)
          Copy(msg, draftmsg); // Чтобы в msg всегда было сообщение поручения
          MakeSelect(msg, DocumentNumber, @query, Params); // Ищем квитуемые по поручению msg, но не позднее номера DocumentNumber
        end;
      end;
    end;

    if(NOT stat)
      Select = RSDCommand( query );
      Select.NullConversion = true;

      i = 0;
      while(i < Params.Size)
        Select.AddParam("", RSDBP_IN, Params(i) );
        i = i + 1;
      end;

      Select.Execute();

      DataSet = TRsbDataSet(Select);
      while(DataSet.moveNext() AND (stat == 0))
        DataSet.GetRecord().CopyTo(addmsg.rec);
        stat = DpKvitOneMessage(addmsg, msg); // приквитовываем подтверждение/уведомление addmsg к поручению msg
      end;
    end;
  end;

  return stat;
end;
