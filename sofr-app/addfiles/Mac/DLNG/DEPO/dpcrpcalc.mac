/*
$Name:         dpcrpcalc.mac
$Module:       Депозитарий
$Description:  Отчет "СПРАВКА-РАСЧЕТ ВЫПЛАТ ПО ЦЕННЫМ БУМАГАМ"
*/
/*
Programmer:    Nikonorov Evgeny
Created:       14.10.2010
*/

import deposerv, SFInter, FIInter, CurrInter, cb_sql, "dllog_xml.mac";
import RsbDataSet;

PRIVATE VAR DocumentID, AvoirSet;
private var ToExcel;
private var records;

private const PW_OUTPAYMENT   = "O";  // Переводом, внешн.
private const PW_INPAYMENT    = "I";  // Переводом, внутр.
private const PW_CONSOLIDATED = "S";  // Переводом, сводн.

private const ALG_DEPO_PAYMENTTYPE = 3258; //Типы выплат доходов в Депозитарии

private const SC_SPDRAFT_STATUS_CLOSE = 3;

private var HeadTable = "┌─────┬─────────────────────────────────────┬─────────────────────────────────────────┬─────────────────────┬───────────────────────┬────────────┐\n"+
                        "│  №  │        Наименование /Ф.И.О.         │     № счета, на который зачисляются     │   Сумма к выплате   │     Статус выплаты    │Дата статуса│\n"+
                        "│ п/п │     получателя денежных средств     │            денежные средства            │                     │                       │            │\n"+
                        "├─────┼─────────────────────────────────────┼─────────────────────────────────────────┼─────────────────────┼───────────────────────┼────────────┤";

private var tablewidth = Index(HeadTable, "\n")-2;
private var Rep = CMakeReport(HeadTable);
private const RepFontStyleTitel =  "ex_FS(b):";

private macro  ПолучитьИмяСубъекта( PartyID )
   var _rParty     = TRecHandler( "party.dbt" );
   var party_name = "";
   if( PartyID >= 0 )
     if( not ПолучитьСубъекта( PartyID, _rParty ) )
        party_name = _rParty.rec.Name;
     end;
   end;
   return party_name;
END;

/*лог ошибок*/
PRIVATE CLASS (DL_LOG_FROM_XML) DP_ERROR_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()
     var ErrorData;
     
     if( m_ReportVersion == 1 )

        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRSTR",    V_STRING,   ErrorData) == false)
        end;
        
        return ErrorData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

PRIVATE MACRO GetLogDataXML( DocKind:integer, DocumentID:integer, Type:integer, OnlyLastReport:bool )
   var xml_obj = null;

   if(Type == LOG_TYPE_ERROR )
      xml_obj = DP_ERROR_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   end;

   return xml_obj;
END;

private macro GetTotalSumm(DocumentID:integer, SummBruttoAmount:@money, SummTaxAmount:@money, SummAmount:@money, SummComAmount:@money, SummPayAmount:@money)
  var query, cmd, DataSet;

  SummBruttoAmount = $0;
  SummTaxAmount    = $0;
  SummAmount       = $0;
  SummComAmount    = $0;
  SummPayAmount    = $0;

  query =   " select NVL(SUM(t_BruttoAmount), 0) as SummBruttoAmount, "
          + "        NVL(SUM(t_Amount), 0) as SummAmount, "
          + "        NVL(SUM(t_ComAmount), 0) as SummComAmount, "
          + "        NVL(SUM(t_PayAmount), 0) as SummPayAmount "
          + "   from ddppmacc_dbt "
          + "  where t_DocumentID = ? ";

  cmd = DL_RSdCommand(query);
  cmd.AddParam(DocumentID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    SummBruttoAmount = DataSet.SummBruttoAmount;
    SummTaxAmount    = DataSet.SummBruttoAmount - DataSet.SummAmount;
    SummAmount       = DataSet.SummAmount;
    SummComAmount    = DataSet.SummComAmount;
    SummPayAmount    = DataSet.SummPayAmount;
  end;
end;

private MACRO PrintRepAvoir(Executed)
  file pmrac("dppmrac");
  record RecordAdress ( adress );
  var crp = TRecHandler("dpcorpop.dbt");
  var query, Select, DataSet;
  var stat = 0;
  var i = 1;
  var SheetName = AvoirSet.FI_Code;
  var HolderAcc = "";
  var TotalSumm = $0;
  var WarrantNum = "", WarrantDate, WarrantStr = "", SingleFIID = -1;
  var width = int(tablewidth/4);
  var SummBruttoAmount = $0, SummTaxAmount = $0, SummAmount = $0, SummComAmount = $0, SummPayAmount = $0;
  var Comment = "";

  GetTotalSumm(DocumentID, @SummBruttoAmount, @SummTaxAmount, @SummAmount, @SummComAmount, @SummPayAmount);


  crp.Clear();
  crp.rec.DocumentID = DocumentID;
  Comment = ReadNoteForObject( OBJTYPE_CORPOP, UniID(crp, OBJTYPE_CORPOP), NOTEKIND_DEPOCORPOP_REASONFORADJUST );
  
  query =   " select acfi.t_Amount, pmac.t_PaymentWay, pmac.t_HolderID, pmac.t_HolderAccID, NVL(na.t_szNameAlg, chr(1)) t_State, "
          + "        pmac.t_StateDate, pmac.t_Message as Msg"
          + "   from ddppmacfi_dbt acfi, ddppmacc_dbt pmac, dnamealg_dbt na "
          + "  where acfi.t_DocumentID = ? /*1*/ "
          + "    and acfi.t_FIID = ? /*2*/ "
          + "    and pmac.t_DocumentID = acfi.t_DocumentID "
          + "    and pmac.t_HolderAccID = acfi.t_HolderAccID "
          + "    and na.t_iTypeAlg(+) = 7525"
          + "    and na.t_iNumberAlg(+) = pmac.t_State";

  Select = DL_RsdCommand(query);

  Select.AddParam(DocumentID );  /*1*/
  Select.AddParam(AvoirSet.FIID );  /*2*/

  DataSet = Select.Execute();

  stat = DataSet.moveNext();
  if(stat)
    if(Rep.GetCurSheet().SheetName == "Отчет")
      Rep.GetCurSheet().SetSheetName(SheetName);
    else
      Rep.AddNewSheetBreak( SheetName, HeadTable );
    end;

    DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "СПРАВКА-РАСЧЕТ ВЫПЛАТ ПО ЦЕННЫМ БУМАГАМ", 0, 0, false, null, null, tablewidth );

    DP_AddPrintCell(Rep, string(date(AvoirSet.CrpDate):f)+" г.", tablewidth, 0, "c:" + RepFontStyleTitel,false, REP_ELEM_STR);
    Rep.AddEmptyStr(2);
    Rep.AddStr();

    if(NOT Executed)
      DP_AddPrintCell(Rep, "отчет сформирован по незавершенной операции", tablewidth, 0, "c:" + RepFontStyleTitel + "ex_FC("+COLOR_RED+")",false, REP_ELEM_STR);
      Rep.AddEmptyStr(2);
      Rep.AddStr();
    end;

    //параметры
    
    DP_AddPrintCell(Rep, "1. Тип выплаты:", width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, AvoirSet.PaymentTypeName, width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, "9. Итоговые суммы расчетной части:", width, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, "", width+1, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    Rep.AddStr();


    DP_AddPrintCell(Rep, "", width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, "", width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, "9.1 Всего ц/б:", width, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, ЧислоCЗаданнойТочностью(double(AvoirSet.TotalAmount), DP_GetPrecision(AvoirSet.TotalAmount, AvoirSet.SumPrecision), ""), width+1, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    Rep.AddStr();


    DP_AddPrintCell(Rep, "2. Вид ценной бумаги:", width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, AvoirSet.AvrkName, width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, "9.2 Чистая сумма денежных средств:", width, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, SummBruttoAmount, width+1, 2, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    Rep.AddStr();


    DP_AddPrintCell(Rep, "3. Код ценной бумаги:", width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, AvoirSet.FI_Code, width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, "9.3 Всего налогов:", width, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, SummTaxAmount, width+1, 2, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    Rep.AddStr();


    DP_AddPrintCell(Rep, "4. Эмитент ценной бумаги:", width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, AvoirSet.IssuerName, width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, "9.4 Сумма начисленных денежных средств:", width, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, SummAmount, width+1, 2, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    Rep.AddStr();


    DP_AddPrintCell(Rep, "5. Дата и номер списка:", width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, string(date(AvoirSet.RegDate):f) + " " + AvoirSet.RegNumber, width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, "9.5 Сумма комиссии депозитария:", width, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, SummComAmount, width+1, 2, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    Rep.AddStr();


    if(records == 1)
      SingleFIID = AvoirSet.FIID;
    end;

    WarrantNum  = AvoirSet.WarrantNum;
    WarrantDate = DP_GetFIWarntDate(SingleFIID, WarrantNum, AvoirSet.PaymentType);
    WarrantStr  = WarrantNum;

    if(WarrantDate == date(0,0,0))
      WarrantDate = "";
    else
      WarrantDate = string(WarrantDate:f);
      if(WarrantStr == "")
        WarrantStr  = WarrantDate;
      else  
        WarrantStr  = WarrantDate + "/" + WarrantStr;
      end;
    end;

    DP_AddPrintCell(Rep, "6. Дата и номер купона:", width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, WarrantStr, width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, "9.6 Сумма, подлежащая выдаче владельцам:", width, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, SummPayAmount, width+1, 2, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    Rep.AddStr();

    DP_AddPrintCell(Rep, "7. Ставка выплаты:", width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, string(AvoirSet.IncomeValue) + " " + AvoirSet.IncomeCcy, width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, "9.7 Комментарий:", width, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, Comment, width+1, 0, "l:w:" + RepFontStyleTitel,false, REP_ELEM_STR);
    Rep.AddStr();

    DP_AddPrintCell(Rep, "8. Валюта перечисления (код):", width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, AvoirSet.CrpCcy, width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, "", width, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, "", width+1, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    Rep.AddStr();

    Rep.AddEmptyStr(1);
    Rep.AddStr();
        
    while(stat)

      HolderAcc = "";
       
      if((DataSet.PaymentWay == PW_INPAYMENT)
      OR (DataSet.PaymentWay == PW_OUTPAYMENT)
      OR (DataSet.PaymentWay == PW_CONSOLIDATED))
        ClearRecord(pmrac);
        pmrac.DocumentID = DocumentID;
        pmrac.HolderAccID = DataSet.HolderAccID;
        if(GetEQ(pmrac))
          if(pmrac.BankIC == {OurBank})
            HolderAcc = pmrac.Account;
          else
            HolderAcc = " р/с " + pmrac.Account + ", в банке " + ПолучитьИмяСубъекта(pmrac.BankIC);
            
            if(НайтиЮридическийАдресСубъекта(pmrac.BankIC,RecordAdress))
              HolderAcc = HolderAcc + ", " + RecordAdress.Adress;
            end;
              
            HolderAcc = HolderAcc + ", БИК " + ПолучитьКодСубъекта(pmrac.BankIC, PTCK_BIC);
            
            if(pmrac.CorrAcc != "") 
              HolderAcc = HolderAcc + " к/с " + pmrac.CorrAcc + " в " + 
                          ПолучитьИмяСубъекта(pmrac.BankCorrID) + ", БИК " + ПолучитьКодСубъекта(pmrac.BankCorrID, PTCK_BIC);
            end;
          end;
        end;
      end;

      DP_AddPrintCell(Rep, i, 0, 0, "c:" + RepFontStyleTitel,true);
      DP_AddPrintCell(Rep, ПолучитьИмяСубъекта( DataSet.HolderID ), 0, 0, "l:" + RepFontStyleTitel,true);
      DP_AddPrintCell(Rep, HolderAcc, 0, 0, "l:" + RepFontStyleTitel,true);
      DP_AddPrintCell(Rep, money(DataSet.Amount), 0, 2, "r:" + RepFontStyleTitel,true);
      DP_AddPrintCell(Rep, string(DataSet.State), 0, 0, "c:" + RepFontStyleTitel,true);
      DP_AddPrintCell(Rep, date(DataSet.StateDate), 0, 0, "c:" + RepFontStyleTitel,true);

      DP_AddPrintCell(Rep, string(DataSet.Msg), 0, 0, "l:" + RepFontStyleTitel+"ex_B(l)",false, REP_ELEM_STR);
      Rep.AddStr();

      TotalSumm = TotalSumm + money(DataSet.Amount);

      i = i + 1;
      stat = DataSet.moveNext();
    end;

    Rep.AddEmptyStr(1);
    Rep.AddStr();

    DP_AddPrintCell(Rep, "№ счета, с которого списываются денежные средства (итог)", Rep.GetColWidthTable(0)+Rep.GetColWidthTable(1)+2, 0, "w:" + RepFontStyleTitel+"ex_B(lbt)",false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, AvoirSet.CrpAccount, Rep.GetColWidthTable(2)+1, 0, "l:" + RepFontStyleTitel+"ex_B(lbt)",false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, money(TotalSumm), Rep.GetColWidthTable(3)+1, 2, "r:" + RepFontStyleTitel+"ex_B(rlbt)",true, REP_ELEM_STR);
    Rep.AddStr();

    Rep.AddEmptyStr(1);
    Rep.AddStr();

    DP_AddPrintCell(Rep, "Итого (прописью) ", 20, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
    DP_AddPrintCell(Rep, CurToStrAlt(TotalSumm, null, null, int(AvoirSet.CrpISO_Number), 2 ), tablewidth-20, 0, "l:" + RepFontStyleTitel+"ex_FS(u)",false, REP_ELEM_STR);
    Rep.AddStr();


    //Сообщения об ошибках при выполнении операции, не относящиеся к ваплатам
    var ReportLineData_XML:variant;
    var ErrData = "";


    ReportLineData_XML = GetLogDataXML(SP_DEPOPER_DIVIDEND, DocumentID, LOG_TYPE_ERROR, false);
    ReportLineData_XML.SetReportNumber(0);
    
    stat = ReportLineData_XML.Next();
    if(stat)
      Rep.AddEmptyStr(1);
      Rep.AddStr();

      DP_AddPrintCell(Rep, "Сообщения", tablewidth, 0, "l:" + RepFontStyleTitel,false, REP_ELEM_STR);
      Rep.AddStr();

      while(stat)
        ErrData = ReportLineData_XML.GetReportLineData();

        DP_AddPrintCell(Rep, ErrData, tablewidth, 0, "l:" + RepFontStyleTitel+"ex_B(lrtb)",false, REP_ELEM_STR);
        Rep.AddStr();

        stat = ReportLineData_XML.Next();
      end;
    end;

    DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

  end;                                                                  
END;


private macro IsExecuted()
  var query, Select, DataSet;
  var Executed = false;

  query = " select op.t_State "
        + " from ddpcorpop_dbt op "
        + " where op.t_DocumentID = ? /*1*/ ";

  Select = DL_RsdCommand(query);

  Select.AddParam(DocumentID );  /*1*/

  DataSet = Select.Execute();
  
  if(DataSet.moveNext())
    if(DataSet.State == SC_SPDRAFT_STATUS_CLOSE)
      Executed = true;
    end;
  end;

  return Executed;
end;


private MACRO CreateRep()
    var query, cmd;
    var Progress = CProgressBar;
    var Executed = IsExecuted();

    //выбрать все ц/б, участвующие в операции начисления дохода
    query =   " select pmfi.*, reg.t_Date as RegDate, reg.t_Number as RegNumber, crp.t_Date as CrpDate, fin.t_FI_Code, ptype.t_szNameAlg as PaymentTypeName, "
            + "        avrk.t_Name as AvrkName, issuer.t_Name as IssuerName, crp.t_WarrantNum, crp.t_PaymentType, fin.t_SumPrecision, "
            + "        pmfi.t_IncomeValue, pmficurr.t_Ccy as IncomeCcy, pmficurr.t_Ccy as CrpCcy, crp.t_Account as CrpAccount, crpcurr.t_ISO_Number as CrpISO_Number, " 
            + "        NVL((select sum(rl.t_HRest) "
            + "               from ddpreglin_dbt rl "
            + "              where rl.t_RegisterID = reg.t_RegisterID "
            + "                and rl.t_Type = " + DP_REGLINETYPE_LACCOUNT
            + "                and rl.t_FIIDFrom = fin.t_FIID "
            + "            ), 0) as TotalAmount "
            + "   from ddpcorpop_dbt crp, ddpregstr_dbt reg, ddppmfi_dbt pmfi, "
            + "        dfininstr_dbt fin, dnamealg_dbt ptype, davrkinds_dbt avrk, dparty_dbt issuer, "
            + "        dfininstr_dbt pmficurr, dfininstr_dbt crpcurr"
            + "  where crp.t_DocumentID = ? " 
            + "    and reg.t_DocumentID = crp.t_DocumentID "
            + "    and pmfi.t_RegisterID = reg.t_RegisterID "
            + "    and fin.t_FIID = pmfi.t_FIID "
            + "    and ptype.t_iTypeAlg = " + ALG_DEPO_PAYMENTTYPE
            + "    and ptype.t_iNumberAlg = crp.t_PaymentType "
            + "    and avrk.t_FI_Kind = fin.t_FI_Kind "
            + "    and avrk.t_AvoirKind = fin.t_AvoirKind "
            + "    and issuer.t_PartyID = RSI_RSB_FIInstr.FI_GetIssuerOnDate( fin.t_FIID, reg.t_Date  )" 
            + "    and pmficurr.t_FIID = pmfi.t_Currency"
            + "    and crpcurr.t_FIID = crp.t_Currency";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(DocumentID);

    records = cmd.GetCount();
    if(records)
      Progress.Init(records, "Просмотр выпусков ц/б", "Просмотр выпусков ц/б, участвующих в операции");
      AvoirSet = cmd.Execute();
      while(AvoirSet.moveNext())
        //для каждой ц/б печатаем свой отчет
        PrintRepAvoir(Executed);
        Progress.Use();
      end;
      Progress.Remove();
    end;

    return records;
  END;

macro CorpopCalculationRep(_DocumentID)
  var err;
  
  GetRegistryValue("DEPO\\DEPOREPTOEXCEL", V_BOOL, ToExcel, err);
  if( err != 0 )
    ToExcel = false;
  end;

  DocumentID = _DocumentID;
  
  CreateRep();

  if( ToExcel )
    Rep.PrintWinRep();
    Rep.ShowWinRep();
  else
    Rep.PrintRep();
  end;

  
  return 0;
end;
