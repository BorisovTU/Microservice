/*
$Name:         dprstrsc.mac
$Module:       Депозитарий
$Description:  Макрос скроллинга списка-реестра
*/
/*
Chernov Anton 27-04-2007
*/

import Globals, DPInter, SecInter, SfInter, "cb_sql.mac", "dlquery.mac", "opr_depo.mac";

var DpAcList = TArray,
    AvoirList = TArray;

record DpRegstr( dpregstr );
record OldDpRegstr( dpregstr );

//Глобализмы для формирования реестра
private record dpreg("dpregstr.dbt");
private record dpcorpop("dpcorpop.dbt");

private record dpacccrp("DPACCCRP.dbt");
private record subst_dppmfi("DPPMFI.dbt");
private record dppmfi("DPPMFI.dbt");


// Класс для заполнения списка депозитарных расписок
class DRElem(_BaseFI, _DRFI, _NumBaseInDR)
  var BaseFI, DRFI, NumBaseInDR;
 
  BaseFI = _BaseFI;
  DRFI = _DRFI;
  NumBaseInDR = _NumBaseInDR;
end;

private var DRList = TArray;


private macro SmartSetBuff(dest, src, src_subst)
  if(ValType(src) == V_MEMADDR)
    if(ValType(src_subst) != V_UNDEF )
      SetBuff(src_subst, src);
      Copy(dest, src_subst);
    else
      SetBuff(dest, src);
    end;
  else
    Copy(dest, src);
  end;
end;

macro Новый_Документ_Реестр()
  return 0;
end;


macro Проверить_Документ_Реестр( Режим )
  var stat = 0;

  if(Режим == 3)  /* РЕДАКТИРОВАНИЕ ДОКУМЕНТА */

  elif(Режим == 2)  /* ВВОД ДОКУМЕНТА */

  elif(Режим == 1)  /* УДАЛЕНИЕ ДОКУМЕНТА */

  end;

  return stat;
end;

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string
  //  Возможные значения ScrolStatus:
  // 0,  //всегда       
  
  // Возможные значения ScrolKind:
  // 0,   // Всегда

  //пример
  //return "/*+FIRST_ROWS LEADING(t) INDEX(t ddpregstr_dbt_idx0)*/";

  return DefaultHint;

END;

macro Функция_Пользователя_Реестр()
  return 0;
end;

/////////////////////////////////////////////////////////////////////////
//Формирование списка-реестра
/////////////////////////////////////////////////////////////////////////



//корректировка одной строки реестра
private macro CorrectOneReglin(IsTemp,    //работаем с временными файлами
                               LAccount,  //л/с места хранения                      
                               HAccount,  //л/с владельца 
                               Summ,      //сумма со знаком
                               FIID       //финансовый инструмент  
                              )

  var query, Select, DataSet;
  var sql_sel="", sql_from="", sql_where="";
  var NewSum = 0.0;
  var ReglinTable  = "ddpreglin_dbt";
  var ConvDenominator = 0, ConvNumerator = 0;
  var iCount = 0;
  var fi = Tbfile("fininstr");
  var SumPrecision = AVOIRISS_SUM_PRECISION;

  if(dpcorpop.ConvDenominator != "") 
    ConvDenominator = dpcorpop.ConvDenominator;
  end;
  
  if(dpcorpop.ConvNumerator != "")
    ConvNumerator = dpcorpop.ConvNumerator;
  end;

  if(IsTemp)
    ReglinTable  = "ddpreglin_tmp";
  end;

  //1. Ищем нужную строку реестра
  query =
    " SELECT "
      + " rlin.t_HRest "
     + " ,rlin.t_ReglineID "
     + " ,rlin.t_FIIDTo ";

  if( IsTemp )
    query = query +
       " ,rlin.t_Action ";
  end;

  query = query +
    " FROM "
        + ReglinTable + " rlin "
  + " WHERE "
      + " rlin.t_RegisterID = ? /*1*/ "
  + " AND rlin.t_LAccount = ? /*2*/ "
  + " AND rlin.t_HAccount = ? /*3*/ "
  + " AND rlin.t_FIIDFrom = ? /*4*/ "
  + " AND rlin.t_Type = " + DP_REGLINETYPE_LACCOUNT + " ";

  if( IsTemp )
    query = query +
    " AND rlin.t_Action <> " + string( 3 ); // REGLIN_DELETE
  end;

  Select = RSDCommand( query );

  Select.AddParam("RegisterID", RSDBP_IN, dpreg.RegisterID );  /*1*/
  Select.AddParam("LAccount",   RSDBP_IN, LAccount   );  /*2*/
  Select.AddParam("HAccount",   RSDBP_IN, HAccount   );  /*3*/
  Select.AddParam("FIIDFrom",   RSDBP_IN, FIID       );  /*4*/

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())
    //нашли строку - корректируем

    NewSum = DataSet.HRest + Summ;

    if(NewSum == 0.0)
      //Удалить строку
      SQL_Execute("DELETE FROM "+ReglinTable+" WHERE t_ReglineID = " + DataSet.t_ReglineID, "Удаление строки списка-реестра", false );
    else
      //Обновить строку
      query =   " UPDATE "+ReglinTable
              + "    SET t_HRest = "+NewSum+", ";

      if( dpcorpop.DocKind != 870 /*SP_DEPOPER_DIVIDEND*/ )
        SumPrecision = AVOIRISS_SUM_PRECISION;
        if( DataSet.FIIDTo != ALLFININSTR )
          fi.Clear();
          fi.rec.FIID = DataSet.FIIDTo;
          if( fi.GetEQ() )
            SumPrecision = fi.rec.SumPrecision;
          end;
        end;
        query = query + " t_BruttoAmount = rsb_depo.GetReglinBruttoAmount("+NewSum+", " + ConvDenominator + ", " + ConvNumerator + ", " + SumPrecision + ") "; 
      else
        iCount = 0;
        while( iCount < AvoirList.Size )
          SmartSetBuff( dppmfi, AvoirList[iCount], subst_dppmfi );
          if(FIID == dppmfi.FIID)
            query = query + " t_BruttoAmount = rsb_depo.GetReglinBruttoAmount("+(dppmfi.IncomeValue*NewSum)+", " + ConvDenominator + ", " + ConvNumerator + ", " + 12 + ") ";
            break;
          end;
          iCount = iCount + 1;
        end;
      end;

      if( ( IsTemp ) and ( DataSet.Action != 1 ) ) // REGLIN_INSERT
        query = query + " ,t_Action = " + String( 2 ) + " "; // REGLIN_UPDATE
      end;

      query = query + "  WHERE t_ReglineID = " + DataSet.t_ReglineID;

      SQL_Execute( query, "Обновление строки списка-реестра", false );
    end;
  else
    //ещё нет такой строки - нужно добавить

    NewSum = Summ;

    sql_sel =   dpreg.RegisterID + ","       //t_RegisterID
              + " accP.t_DepoRoot, "            //t_HolderAccID
              + FIID + ","                      //t_FIIDfrom 
              + "'"+HAccount+ "',"              //t_HAccount 
              + NewSum + ","                    //t_HRest    
              + "'"+LAccount+"',";               //t_LAccount 
    if( dpcorpop.DocKind != 870 /*SP_DEPOPER_DIVIDEND*/ )
      SumPrecision = AVOIRISS_SUM_PRECISION;
      if( dpcorpop.Currency != ALLFININSTR )
        fi.Clear();
        fi.FIID = dpcorpop.Currency; //t_FIIDto
        if( fi.GetEQ() )
          SumPrecision = fi.SumPrecision;
        end;
      end;
      sql_sel = sql_sel + dpcorpop.Currency + ", "; //t_FIIDto
      sql_sel = sql_sel + " rsb_depo.GetReglinBruttoAmount("+NewSum+", " + ConvDenominator + ", " + ConvNumerator + ", " + SumPrecision + "), "; //t_BruttoAmount
    else
      sql_sel = sql_sel + "-1, ";                   //t_FIIDto
      iCount = 0;
      while( iCount < AvoirList.Size )
        SmartSetBuff( dppmfi, AvoirList[iCount], subst_dppmfi );
        if(FIID == dppmfi.FIID)
          sql_sel = sql_sel + " rsb_depo.GetReglinBruttoAmount("+(dppmfi.IncomeValue*NewSum)+", " + ConvDenominator + ", " + ConvNumerator + ", " + 12 + "), "; //t_BruttoAmount
          break;
        end;
        iCount = iCount + 1;
      end;
    end;

    sql_sel =   sql_sel 
              + " 0, " //t_ReglineID
              + " 0, " //t_State
              + " 0, " //t_Parent (0 - так как пока не известен)
              + " accP.t_Client, "  //t_HolderID
              + " accP.t_DepoAcc, " //t_HolderPartID
              + DP_REGLINETYPE_LACCOUNT + ", "  //t_Type всегда тип "л/с места хранения"
              + FIID; // t_FIIDDR

    sql_from = " daccount_dbt accP ";

    sql_where =   "       accP.t_Account = '" + HAccount + "'"
                + "   and accP.t_Code_Currency = " + FIID
                + "   and accP.t_Chapter = " + CHAPT5;

    if( IsTemp )
      query = "INSERT INTO ddpreglin_tmp ( t_RegisterID, "
            +                            " t_HolderAccID, "
            +                            " t_FIIDfrom, "
            +                            " t_HAccount, "
            +                            " t_HRest, "
            +                            " t_LAccount, "
            +                            " t_FIIDto, "
            +                            " t_BruttoAmount, "
            +                            " t_ReglineID, "
            +                            " t_State, "
            +                            " t_Parent, "
            +                            " t_HolderID, "
            +                            " t_HolderPartID, "
            +                            " t_Type, "
            +                            " t_FIIDDR, "
            /*далее идут системные поля. Их заполнение не менять*/
            +                            " T_RegisterID_From, "
            +                            " T_HolderAccID_From, "
            +                            " T_FIIDFrom_From, "
            +                            " T_HAccount_From, "
            +                            " T_HRest_From, "
            +                            " T_LAccount_From, "
            +                            " T_FIIDTo_From, "
            +                            " T_BruttoAmount_From, "
            +                            " T_ReglineID_From, "
            +                            " T_State_From, "
            +                            " t_Parent_From, "
            +                            " t_HolderID_From, "
            +                            " t_HolderPartID_From, "
            +                            " t_Type_From, "
            +                            " t_FIIDDR_From, "
            +                            " T_Action) "

            + "SELECT " + sql_sel + ",0,0,-1,CHR(1),0,CHR(1),-1,0,-1,0,0,-1,0,"+DP_REGLINETYPE_LACCOUNT+",-1,1 FROM " + sql_from + " WHERE " + sql_where;
    else
      query = "INSERT INTO ddpreglin_dbt ( t_RegisterID, "
            +                            " t_HolderAccID, "
            +                            " t_FIIDfrom, "
            +                            " t_HAccount, "
            +                            " t_HRest, "
            +                            " t_LAccount, "
            +                            " t_FIIDto, "
            +                            " t_BruttoAmount, "
            +                            " t_ReglineID, "
            +                            " t_State, "
            +                            " t_Parent, "
            +                            " t_HolderID, "
            +                            " t_HolderPartID, "
            +                            " t_Type, "
            +                            " t_FIIDDR) "

            + "SELECT " + sql_sel + " FROM " + sql_from + " WHERE " + sql_where;
    end;

    SQL_Execute( query, "Добавление строки списка-реестра", false );
  end;
end;

//корректировка записи в реестре по субпроводкам одного платежа
private macro CorrectReglinByPaym(IsTemp, PaymentID)
  var query, Select, DataSet;

  query =   " select accsubpayer.t_SpecialType STypePayer, accsubrec.t_SpecialType STypeRec, "
          + "        accsubpayer.t_SubAccountCode PSubAccountCode, accsubrec.t_SubAccountCode RSubAccountCode, "
          + "        vanlpayer.t_Account PVanlAccount, vanlrec.t_Account RVanlAccount, "
          + "        dc.t_Sum, vanlpayer.t_FIID"
          + "   from daccsubdc_dbt dc, daccsub_dbt accsubpayer, daccsub_dbt accsubrec, daccvanl_dbt vanlpayer, daccvanl_dbt vanlrec"
          + "  where dc.t_PaymentID = ? /*1*/"
          + "    and accsubpayer.t_AccAnaliticsID = dc.t_AccAnaliticsID "
          + "    and accsubpayer.t_SubAccountID = dc.t_SubAccountPayerID "
          + "    and vanlpayer.t_AccAnaliticsID = accsubpayer.t_AccAnaliticsID "
          + "    and vanlpayer.t_AnaliticsID = accsubpayer.t_AnaliticsID "
          + "    and accsubrec.t_AccAnaliticsID = dc.t_AccAnaliticsID "
          + "    and accsubrec.t_SubAccountID = dc.t_SubAccountReceiverID "
          + "    and vanlrec.t_AccAnaliticsID = accsubrec.t_AccAnaliticsID "
          + "    and vanlrec.t_AnaliticsID = accsubrec.t_AnaliticsID ";
  
  Select = RSDCommand( query );

  Select.AddParam("PaymentID", RSDBP_IN, PaymentID );  /*1*/

  Select.Execute();

  DataSet = TRsbDataSet(Select);

  while(DataSet.moveNext())

    //всегда отрабатываем два варианта
    //т.к., например, для перемещения субсчет плательщика и субсчет получателя всегда настоящие активные счета
    //в остальных случаях один из субсчетов будет default

    if(DataSet.STypePayer == ACCSUB_SPECTYPE_NONE)
      //плательщик - настоящий активный счет
      CorrectOneReglin(IsTemp, DataSet.PSubAccountCode, DataSet.PVanlAccount, -DataSet.t_Sum, DataSet.FIID);
    end;
                                                                                
    if(DataSet.STypeRec == ACCSUB_SPECTYPE_NONE)
      //получатель - настоящий активный счет
      CorrectOneReglin(IsTemp, DataSet.RSubAccountCode, DataSet.RVanlAccount, DataSet.t_Sum, DataSet.FIID);
    end;

  end;

end;


//корректировка записи в реестре по субпроводкам глобальных операций
private macro CorrectReglinByGO(IsTemp)
  var query, Select, DataSet, iCount;

  query =   " SELECT accsubpayer.t_SpecialType STypePayer, accsubrec.t_SpecialType STypeRec, "
          + "        accsubpayer.t_SubAccountCode PSubAccountCode, accsubrec.t_SubAccountCode RSubAccountCode, "
          + "        vanlpayer.t_Account PVanlAccount, vanlrec.t_Account RVanlAccount, "
          + "        dc.t_Sum, vanlpayer.t_FIID "
          + "   FROM ddpcorpop_dbt crp, doproper_dbt op, doprdocs_dbt od, dacctrn_dbt ad, daccsubdc_dbt dc, daccsub_dbt accsubpayer, daccsub_dbt accsubrec, daccvanl_dbt vanlpayer, daccvanl_dbt vanlrec "
          + "  WHERE accsubpayer.t_AccAnaliticsID = dc.t_AccAnaliticsID "
          + "    AND accsubpayer.t_SubAccountID = dc.t_SubAccountPayerID "
          + "    AND vanlpayer.t_AccAnaliticsID = accsubpayer.t_AccAnaliticsID "
          + "    AND vanlpayer.t_AnaliticsID = accsubpayer.t_AnaliticsID "
          + "    AND accsubrec.t_AccAnaliticsID = dc.t_AccAnaliticsID "
          + "    AND accsubrec.t_SubAccountID = dc.t_SubAccountReceiverID "
          + "    AND vanlrec.t_AccAnaliticsID = accsubrec.t_AccAnaliticsID "
          + "    AND vanlrec.t_AnaliticsID = accsubrec.t_AnaliticsID "

          + "    AND crp.t_DocKind IN ( " + DP_DEPOPER_CONVERT + ", " + DP_DEPOPER_REPAY + ", " + DP_DEPOPER_BONEMISS + " ) "
          + "    AND crp.t_ValueDate > " + GetSQLDate(dpreg.Date)
          + "    AND crp.t_DateAtRegistrar <= " + GetSQLDate(dpreg.Date)
          + "    AND op.t_DocKind = crp.t_DocKind "
          + "    AND op.t_DocumentID = crp.t_DocumentID "
          + "    AND od.t_ID_Operation = op.t_ID_Operation "
          + "    AND od.t_DocKind = 1 "
          + "    AND ad.t_AccTrnID = od.t_AccTrnID ";

  ///////////////Наличие ц/б в списке ц/б СР
  iCount = 0;
  if(AvoirList.Size > 0)
    query = query + " AND ad.t_FIID_Payer IN (";
    while( iCount < AvoirList.Size )
      SmartSetBuff( dppmfi, AvoirList[iCount], subst_dppmfi );
      if( iCount > 0 )
        query = query + ", ";
      end;                                                                 
      query = query + dppmfi.FIID;
      iCount = iCount + 1;
    end;
    query = query + ")";
  else
    query = query + " AND ad.t_FIID_Payer = 0 ";
  end;

  query = query + " AND dc.t_AccTrnID = ad.t_AccTrnID "
                + " AND EXISTS(SELECT 1 from daccsub_dbt accsub "
                + "            WHERE accsub.t_AccAnaliticsID = dc.t_AccAnaliticsID "
                + "              AND (accsub.t_SubAccountID = dc.t_SubAccountPayerID or accsub.t_SubAccountID = dc.t_SubAccountReceiverID) "
                + "              AND EXISTS( SELECT 1 from daccount_dbt acc "
                + "                          WHERE acc.t_Account = accsub.t_SubAccountCode "
                + "                            AND acc.t_Code_Currency = ad.t_FIID_Payer "
                + "                            AND acc.t_Chapter = " + CHAPT5

    ///////////////Наличие л/с на счетах, указанных в с/р

                + "                            AND acc.t_DepoAcc IN (SELECT acnt.t_AutoKey "    
                + "                                                    FROM ddepoacnt_dbt acnt ";
  iCount = 0;
  if(DpAcList.Size > 0)
    query = query + "                                                 START WITH acnt.t_AutoKey IN ( ";
    while( iCount < DpAcList.Size )
      SmartSetBuff( dpacccrp, DpAcList[iCount] );
      if( iCount > 0 )
        query = query + ", ";
      end;
      query = query + dpacccrp.DepoAccID;
      iCount = iCount + 1;
    end;

    query = query + " ) ";
  else
    //если счетов нет
    query = query + "                                                 START WITH acnt.t_AutoKey = 0 ";
  end;

  query = query + "                                                  CONNECT BY PRIOR acnt.t_AutoKey = acnt.t_Superior )";
  ///////////////

  query = query + "                         ) "
            + "          ) " 
            + " ORDER BY crp.t_ValueDate ASC ";

  Select = RSDCommand( query );

  Select.Execute();

  DataSet = TRsbDataSet(Select);

  while(DataSet.moveNext())

    //всегда отрабатываем два варианта
    //т.к., например, для перемещения субсчет плательщика и субсчет получателя всегда настоящие активные счета
    //в остальных случаях один из субсчетов будет default

    if(DataSet.STypePayer == ACCSUB_SPECTYPE_NONE)
      //плательщик - настоящий активный счет
      CorrectOneReglin(IsTemp, DataSet.PSubAccountCode, DataSet.PVanlAccount, -DataSet.t_Sum, DataSet.FIID);
    end;
                                                                                
    if(DataSet.STypeRec == ACCSUB_SPECTYPE_NONE)
      //получатель - настоящий активный счет
      CorrectOneReglin(IsTemp, DataSet.RSubAccountCode, DataSet.RVanlAccount, DataSet.t_Sum, DataSet.FIID);
    end;

  end;
end;


private macro SetBaseBuffbyID(FIID)
  var iCount = 0, found = false;

  while( (iCount < AvoirList.Size) AND NOT found)
    SmartSetBuff( dppmfi, AvoirList[iCount], subst_dppmfi );
    if( dppmfi.FIID == FIID )
      found = true;
    end;
    iCount = ICount + 1;
  end;
end;


private macro AddDRtoAvoirList()
  var iCount = 0, realFIsize = AvoirList.Size, DataSet, query;
  const OBJGROUP_BASEISSUE = 10;
  var Select = DL_RSDCommand();
  var iDRs = 0;
  var dppmfi_rec;

  if(dpreg.ByDR == SET_CHAR) // Дополнять реестр остатками по депозитарным распискам, в основе которых лежат ц/б СР
    query = " SELECT TO_NUMBER (lnk.t_attrid) BaseFI, " +
            "        TO_NUMBER (lnk.t_ObjectID) DRFI, " +
            "        UTL_RAW.CAST_TO_VARCHAR2 (note.t_text) NumBaseInDR " +
            "   FROM dobjlink_dbt lnk, dnotetext_dbt note " +
            "  WHERE lnk.t_objecttype = " + OBJTYPE_AVOIRISS +
            "    AND lnk.t_groupid = " + OBJGROUP_BASEISSUE +
            "    AND lnk.t_attrtype = " + OBJTYPE_AVOIRISS +
            "    AND lnk.t_attrid IN ( ";

    while( iCount < AvoirList.Size )
      SmartSetBuff( dppmfi, AvoirList[iCount], subst_dppmfi );
      if( iCount > 0 )
        query = query + ", ";
      end;
      query = query + "LPAD("+dppmfi.FIID+", 10, 0)";
      iCount = iCount + 1;
    end;
    query = query + " ) " +
            " AND note.t_NoteKind = " + NOTEKIND_AVOIR_DEPORECAMOUNT +
            " AND note.t_ObjectType = lnk.t_objecttype " +
            " AND note.t_DocumentID = lnk.t_ObjectID ";

    DataSet = Select.Execute(query);
    while(DataSet.moveNext()) // Дополняем список базовых ФИ депозитарными расписками. При этом параметры ФИ заимствуем у оригинала
      if(int(string(DataSet.NumBaseInDR)) > 0)

        dppmfi_rec = TRecHandler("DPPMFI.dbt");
        SetBaseBuffbyID(DataSet.BaseFI);
        Copy(dppmfi_rec, dppmfi);
        dppmfi_rec.rec.FIID = DataSet.DRFI;

        AvoirList[AvoirList.Size] = dppmfi_rec;

        DRList[iDRs] = DRElem(int(DataSet.BaseFI), int(DataSet.DRFI), int(string(DataSet.NumBaseInDR))); // Сохраняем связь базового ФИ, ДР и количества базовых ФИ в ДР
        iDRs = iDRs + 1;
      end;
    end;
  end;
end;


// Превратим остатки по ДР в остатки по базовым ФИ
// 1. Домножим количество остатков на л/с на количество базового ФИ в ДР
// 2. Изменим FIIDfrom с ДР на FIID базового ФИ (л/с и связанные с ними счета депо останутся от ДР!)
// 3. Для всех остатков по базовому ФИ очистим FIIDDR
private macro ChangeDRtoBaseFI(IsTemp)
  var iCount = 0;
  var sql, tsql;

  if(IsTemp)
    tsql = "ddpreglin_tmp ";
  else
    tsql = "ddpreglin_dbt ";
  end;

  // 1 и 2
  while( iCount < DRList.Size )
    sql = " UPDATE " + tsql + 
          " SET t_FIIDfrom = " + DRList[iCount].BaseFI + ", " +
          "     t_HRest = t_HRest * " + DRList[iCount].NumBaseInDR + ", " +
          "     t_BruttoAmount = t_BruttoAmount * " + DRList[iCount].NumBaseInDR +
          " WHERE t_FIIDfrom = " + DRList[iCount].DRFI;

    SQL_Execute( sql, "Коррекция остатков по ДР", true );

    iCount = iCount + 1;
  end;

  // 3
  sql = " UPDATE " + tsql + 
        " SET t_FIIDDR = -1 " +
        " WHERE t_FIIDfrom = t_FIIDDR ";

  SQL_Execute( sql, "Коррекция ссылок на ДР", true );

end;

private macro CorrectByPawn(IsTemp)
  var table, query = "";

  if(IsTemp)
    table = "ddpreglin_tmp ";                                                                 
  else
    table = "ddpreglin_dbt ";
  end;

  query =   " UPDATE "+table+" lin "
          + " SET lin.t_HolderID = NVL((SELECT acap.t_APartyID "
          + "                             FROM ddpacap_dbt acap "
          + "                            WHERE acap.t_APID = (SELECT MAX(ap.t_APID) KEEP (DENSE_RANK FIRST ORDER BY ap.t_FromDate DESC, ap.t_APID DESC) "
          + "                                                   FROM ddpacap_dbt ap "
          + "                                                  WHERE ap.t_ApNodeID = lin.t_HolderAccID "
          + "                                                    AND ap.t_Role = " + DEPOACAPROLE_MORTGAGEE
          + "                                                    AND EXISTS (SELECT 1 "
          + "                                                                  FROM ddpacapau_dbt au "
          + "                                                                 WHERE au.t_APID = ap.t_APID "
          + "                                                                   AND au.t_AuNodeID = lin.t_HolderPartID"
          + "                                                               )"
          + "                                                )"
          + "                          ), lin.t_HolderID)"
          + " WHERE EXISTS(SELECT 1 "
          + "                FROM ddepoacnt_dbt part "
          + "               WHERE part.t_AutoKey = lin.t_HolderPartID "
          + "                 AND (   part.t_PawnRightMeeting = " + DP_PAWNRIGHTOWNER_MORTGAGEE
          + "                      or part.t_PawnRightIncome = " + DP_PAWNRIGHTOWNER_MORTGAGEE
          + "                      or part.t_PawnRightGlobal = " + DP_PAWNRIGHTOWNER_MORTGAGEE
          + "                     )"
          + "             )";

  SQL_Execute( query, "Учёт залога", true );

end;


/* создать строки реестра */
macro DP_CreateReestr(IsTemp, dpregstr_buf, corpop_buf, NotCreateTree)
  var Result = true, iCount = 0;
  var sql, sql_sel, sql_from, sql_where, rsd, DraftPaym;
  var Dprt = {OperDprt};
  var ConvDenominator = 0, ConvNumerator = 0;
  var fi = Tbfile("fininstr");
  var SumPrecision = AVOIRISS_SUM_PRECISION;

  var command;

  SmartSetBuff( dpreg, dpregstr_buf );
  SmartSetBuff( dpcorpop, corpop_buf );

  if( dpreg.Department )
    Dprt = dpreg.Department;
  end;

  if(dpcorpop.ConvDenominator != "") 
    ConvDenominator = dpcorpop.ConvDenominator;
  end;
  
  if(dpcorpop.ConvNumerator != "")
    ConvNumerator = dpcorpop.ConvNumerator;
  end;

  /*блок FROM*/
  sql_from = "daccount_dbt acc, "
           + "daccsub_dbt accsub, ";

  sql_from = sql_from + "daccsubrd_dbt accsubrd, ";

  sql_from = sql_from
           + "daccvanl_dbt accvanl, "
           + "ddepoacnt_dbt dpac, "
           + "ddepoacnt_dbt dproot, "
           + " ( SELECT acct.t_account, acct.t_Code_Currency "
           +     " FROM daccount_dbt acct, ddepoacnt_dbt dpac1, ddepoacnt_dbt dppart2 ";

  iCount = 0;

  if(AvoirList.Size > 0)
    AddDRtoAvoirList();
    sql_from = sql_from +
              " WHERE acct.t_code_currency IN (";
    while( iCount < AvoirList.Size )
      SmartSetBuff( dppmfi, AvoirList[iCount], subst_dppmfi );
      if( iCount > 0 )
        sql_from = sql_from + ", ";
      end;
      sql_from = sql_from + dppmfi.FIID;
      iCount = iCount + 1;
    end;
    sql_from = sql_from + ")"
  else
    sql_from = sql_from +
              " WHERE acct.t_code_currency IN (0)";
  end;

  sql_from = sql_from +
                  " AND acct.t_department = " + Dprt
           +      " AND acct.t_chapter = " + CHAPT5
           +      " AND acct.t_depoacc = dpac1.t_autokey "
           +      " AND acct.t_deporoot = dppart2.t_autokey "
           +      " AND rsb_account.restac (acct.t_account, "
           +                               "acct.t_code_currency, "
           +                               GetSQLDate(dpreg.Date) + ","
           +                               "acct.t_chapter, "
           +                               "null "
           +                              ") <> 0 "
           /* отбираем все терминальные счета */
           +      " AND acct.t_depoacc IN ( SELECT q2_tree.t_autokey "
           +                                " FROM ddepoacnt_dbt q2_tree "
           +                               " WHERE (NOT EXISTS ( SELECT q1_tree.t_superior "
           +                                                     " FROM ddepoacnt_dbt q1_tree "
           +                                                    " WHERE q1_tree.t_superior = q2_tree.t_autokey) "
           +                                                 " ) ";

  iCount = 0;
  if(DpAcList.Size > 0)
    sql_from = sql_from
             + " START WITH q2_tree.t_autokey IN (";
    while( iCount < DpAcList.Size )
      SmartSetBuff( dpacccrp, DpAcList[iCount] );
      if( iCount > 0 )
        sql_from = sql_from + ", ";
      end;
      sql_from = sql_from + dpacccrp.DepoAccID;
      iCount = iCount + 1;
    end;

    sql_from = sql_from + ")";
  else
    //если счетов нет
    sql_from = sql_from
           + " START WITH q2_tree.t_autokey IN (0)";
  end;

  sql_from = sql_from
           +                             " CONNECT BY PRIOR q2_tree.t_autokey =  q2_tree.t_superior) "
           + " ) acc_a";  /* acc_a - выборка активных л/с */

  /*блок WHERE*/
  /* выборка пассивных счетов по активным */
  sql_where = "acc.t_department = " + Dprt
            + " AND acc.t_chapter = " + CHAPT5
            + " AND acc.t_code_currency = acc_a.t_Code_Currency ";

  sql_where = sql_where
            + " AND accsub.t_accanaliticsid = accsubrd.t_accanaliticsid "
            + " AND accsub.t_subaccountid = accsubrd.t_subaccountid ";

  sql_where = sql_where
            + " AND accsub.t_subaccountcode = acc_a.t_account "
            + " AND accvanl.t_accanaliticsid = accsub.t_accanaliticsid "
            + " AND accvanl.t_chapter = " + CHAPT5
            + " AND accvanl.t_fiid = acc_a.t_Code_Currency "
            + " AND accvanl.t_account = acc.t_account "
            + " AND dpac.t_autokey = acc.t_depoacc "
            + " AND dproot.t_autokey = acc.t_deporoot ";

  sql_where = sql_where 
            +  " AND accsubrd.t_date_carry IN ( SELECT NVL (MAX (ad.t_date_carry), " + GetSQLDate(dpreg.Date) + ") "
            +                                  " FROM daccsubrd_dbt ad "
            +                                 " WHERE ad.t_accanaliticsid = accsub.t_accanaliticsid "
            +                                   " AND ad.t_subaccountid = accsub.t_subaccountid "
            +                                   " AND ad.t_date_carry <= " + GetSQLDate(dpreg.Date) + ") "
            + " AND accsubrd.t_rest <> 0 ";

  /*блок SELECT*/
  sql_sel = dpreg.RegisterID + ", " /*RegisterID*/
          + " dproot.t_AutoKey, "      /*HolderAccID*/
          + " acc.t_Code_Currency, "   /*FIIDfrom*/
          + " acc.t_Account, ";        /*HAccount*/

  sql_sel = sql_sel + " accsubrd.t_Rest, "; /*HRest*/

  sql_sel = sql_sel
          + " acc_a.t_Account, "; /*LAccount*/

  if( dpcorpop.DocKind != 870 /*SP_DEPOPER_DIVIDEND*/ )
    SumPrecision = AVOIRISS_SUM_PRECISION;
    if( dpcorpop.Currency != ALLFININSTR )
      fi.Clear();
      fi.rec.FIID = dpcorpop.Currency; /*FIIDto*/
      if( fi.GetEQ() )
        SumPrecision = fi.rec.SumPrecision;
      end;
    end;
    sql_sel = sql_sel
            + dpcorpop.Currency + ", "; /*FIIDto*/
    sql_sel = sql_sel + " rsb_depo.GetReglinBruttoAmount(accsubrd.t_Rest, " + ConvDenominator + ", " + ConvNumerator + ", " + SumPrecision + "), "; /*BruttoAmount*/
  else
    sql_sel = sql_sel
            + " -1, "; /*FIIDto*/

    if( AvoirList.Size > 0 )
      iCount = 0;
      sql_sel = sql_sel +
                " (CASE WHEN acc.t_Code_Currency = ";
      while( iCount < AvoirList.Size )
        SmartSetBuff( dppmfi, AvoirList[iCount], subst_dppmfi );
        if( iCount > 0 )
          sql_sel = sql_sel + " WHEN acc.t_Code_Currency = ";
        end;
        sql_sel = sql_sel + dppmfi.FIID + " THEN " + dppmfi.IncomeValue + "*accsubrd.t_Rest ";
        iCount = iCount + 1;
      end;
      sql_sel = sql_sel + " ELSE 0 END), "; /*BruttoAmount*/
    else
      sql_sel = sql_sel + " 0, ";
    end;
  end;


  sql_sel = sql_sel
          + " 0, "  /*ReglineID*/
          + " 0, "; /*State*/

  sql_sel = sql_sel
          + " 0, " //Parent (0 - так как пока не известен)
          + " dproot.t_Owner, "  //HolderID
          + " acc.t_DepoAcc, " //HolderPartID
          + DP_REGLINETYPE_LACCOUNT + ", "  //всегда тип "л/с места хранения"
          + "acc.t_Code_Currency"; // t_FIIDDR

  /*добавление строчек списка-реестра*/
  if( IsTemp )
    sql = "INSERT INTO ddpreglin_tmp ( t_RegisterID, "
        +                            " t_HolderAccID, "
        +                            " t_FIIDfrom, "
        +                            " t_HAccount, "
        +                            " t_HRest, "
        +                            " t_LAccount, "
        +                            " t_FIIDto, "
        +                            " t_BruttoAmount, "
        +                            " t_ReglineID, "
        +                            " t_State, "
        +                            " t_Parent, "
        +                            " t_HolderID, "
        +                            " t_HolderPartID, "
        +                            " t_Type, "
        +                            " t_FIIDDR, "
        /*далее идут ситсемные поля. Их заполнение не менять*/
        +                            " T_RegisterID_From, "
        +                            " T_HolderAccID_From, "
        +                            " T_FIIDFrom_From, "
        +                            " T_HAccount_From, "
        +                            " T_HRest_From, "
        +                            " T_LAccount_From, "
        +                            " T_FIIDTo_From, "
        +                            " T_BruttoAmount_From, "
        +                            " T_ReglineID_From, "
        +                            " T_State_From, "
        +                            " t_Parent_From, "
        +                            " t_HolderID_From, "
        +                            " t_HolderPartID_From, "
        +                            " t_Type_From, "
        +                            " t_FIIDDR_From, "
        +                            " T_Action) "

        + "SELECT " + sql_sel + ",0,0,-1,CHR(1),0,CHR(1),-1,0,-1,0,0,-1,0,"+DP_REGLINETYPE_LACCOUNT+",-1,1 FROM " + sql_from + " WHERE " + sql_where;
  else
    sql = "INSERT INTO ddpreglin_dbt ( t_RegisterID, "
        +                            " t_HolderAccID, "
        +                            " t_FIIDfrom, "
        +                            " t_HAccount, "
        +                            " t_HRest, "
        +                            " t_LAccount, "
        +                            " t_FIIDto, "
        +                            " t_BruttoAmount, "
        +                            " t_ReglineID, "
        +                            " t_State, "
        +                            " t_Parent, "
        +                            " t_HolderID, "
        +                            " t_HolderPartID, "
        +                            " t_Type, "
        +                            " t_FIIDDR) "

        + "SELECT " + sql_sel + " FROM " + sql_from + " WHERE " + sql_where;
  end;

  /*println( sql );*/

  SQL_Execute( sql, "Формирование списка-реестра", true );

  if(dpreg.ByRegistrar == SET_CHAR) //построение реестра по дате включения в реестр
    //корректировка остатков
    
    sql =   " select pm.t_PayerDpNode, pm.t_ReceiverDpNode, pm.t_PaymentID "
          + "   from dspdraft_dbt draft, dspdrmove_dbt drmove, dpmpaym_dbt pm "
          + "  where drmove.t_DraftID = draft.t_AutoKey "
          + "    and pm.t_PaymentID = drmove.t_PaymentID "
          + "    and pm.t_ValueDate > " + GetSQLDate(dpreg.Date)
          + "    and draft.t_DateAtRegistrar <= " + GetSQLDate(dpreg.Date)
          + "    and pm.t_PaymStatus = " + PM_FINISHED //поручение исполнено
          + "    and Exists( select 1 from daccsubdc_dbt subdc, daccsub_dbt accsub "
          + "                 where subdc.t_PaymentID = pm.t_PaymentID "
          + "                   and accsub.t_AccAnaliticsID = subdc.t_AccAnaliticsID "
          + "                   and (accsub.t_SubAccountID = subdc.t_SubAccountPayerID or accsub.t_SubAccountID = subdc.t_SubAccountReceiverID) "
          + "                   and Exists( select 1 from daccount_dbt acc "
          + "                                where acc.t_Account = accsub.t_SubAccountCode "
          + "                                  and acc.t_Code_Currency = pm.t_FIID "
          + "                                  and acc.t_Chapter = " + CHAPT5;

    ///////////////Наличие л/с на счетах, указанных в с/р
    sql = sql + "                              and acc.t_DepoAcc in (select acnt.t_AutoKey "
              + "                                                      from ddepoacnt_dbt acnt ";

    iCount = 0;
    if(DpAcList.Size > 0)
      sql = sql + "                                                   start with acnt.t_AutoKey IN (";
      while( iCount < DpAcList.Size )
        SmartSetBuff( dpacccrp, DpAcList[iCount] );
        if( iCount > 0 )
          sql = sql + ", ";
        end;
        sql = sql + dpacccrp.DepoAccID;
        iCount = iCount + 1;
      end;

      sql = sql + ")";
    else
      //если счетов нет
      sql = sql + "                                                   start with acnt.t_AutoKey IN (0)";
    end;

    sql = sql + "                                                     connect by prior acnt.t_AutoKey = acnt.t_Superior )";
    ///////////////

    sql = sql + "                         )"
              + "          ) ";

    ///////////////Наличие ц/б платежа в списке ц/б с/р
    iCount = 0;
    if(AvoirList.Size > 0)
      sql = sql + " and pm.t_FIID in (";
      while( iCount < AvoirList.Size )
        SmartSetBuff( dppmfi, AvoirList[iCount], subst_dppmfi );
        if( iCount > 0 )
          sql = sql + ", ";
        end;                                                                 
        sql = sql + dppmfi.FIID;
        iCount = iCount + 1;
      end;
      sql = sql + ")";
    else
      sql = sql + " and pm.t_FIID in (0)";
    end;
    ///////////////

    sql = sql + " order by pm.t_ValueDate asc";    
    
    DraftPaym = TRsbDataSet(sql);

    while(DraftPaym.moveNext())
      CorrectReglinByPaym(IsTemp, DraftPaym.PaymentID);
    end;

    CorrectReglinByGO(IsTemp);
  end;

  if(dpreg.ByDR == SET_CHAR)
    ChangeDRtoBaseFI(IsTemp);
  end;

  if(dpreg.ByPawn == SET_CHAR)
    CorrectByPawn(IsTemp);
  end;

  if(NotCreateTree == false)
    //строим дерево реестра
    if( IsTemp )
      SQL_Execute("begin rsb_depo.CreateReestrTree_TMP( " + dpreg.RegisterID + "); end;", "Формирование дерева реестра" );
    else
      SQL_Execute("begin rsb_depo.CreateReestrTree_Real( " + dpreg.RegisterID + "); end;", "Формирование дерева реестра" );
    end;
  end;


  return Result;
end; /*DP_CreateReestr*/

// Корректировка по внутрибанковским РЕПО
macro DP_CorrectReglinByREPO(
  isTemp:Bool // По временной таблице
 ,corpopBuf:Memaddr
 ,regstrBuf:Memaddr
):Integer

  var stat = 0;
  var singleCorpFIID:Integer = ALLFININSTR;
  var fiwarnts = TBFile( "fiwarnts" );
  var warrantDate:Date = Date( 0, 0, 0 );
  var dealsQuery:String = "";
  var draftsQuery:String = "";
  var dpacCount:Integer = 0;
  var depoAccIDEnum:String = "";
  var avoirCount:Integer = 0;
  var fiidEnum:String = "";
  var paymsQuery:String = "";
  var reglinsQuery:String = "";
  var rsdCmd = null;
  var dataSet = null;
  var repoCorrected:Bool = false;

  SmartSetBuff( dpcorpop, corpopBuf );
  SmartSetBuff( dpreg, regstrBuf );

  if( AvoirList.Size == 1 )
    SmartSetBuff( dppmfi, AvoirList[0], subst_dppmfi );
    singleCorpFIID = dppmfi.FIID;

    if( dpcorpop.WarrantNum != "" )
      fiwarnts.Clear();

      fiwarnts.rec.IsPartial = "";
      fiwarnts.rec.FIID = singleCorpFIID;
      fiwarnts.rec.Number = dpcorpop.WarrantNum;

      if( fiwarnts.GetEQ() )
        warrantDate = fiwarnts.rec.DrawingDate;
      end;
    end;
  end;

  dealsQuery =
    " SELECT /*+ LEADING( tick ) INDEX( tick DDL_TICK_DBT_IDX4 ) */ "
      + " tick.t_DealID "
     + " ,tick.t_BOfficeKind "
  + " FROM "
      + " ddl_tick_dbt tick "
     + " ,dfininstr_dbt fininstr "
  + " WHERE "
      + " tick.t_BOfficeKind = " + String( DL_SECURITYDOC ) + " "
  + " AND fininstr.t_FIID = tick.t_PFI "

  + " AND ( tick.t_DealStatus = " + String( DL_READIED ) + " "
     + " OR tick.t_DealStatus = " + String( DL_CLOSED ) + " ) "

  + " AND Rsb_Secur.IsRepo( Rsb_Secur.get_OperationGroup( Rsb_Secur.get_OperSysTypes( tick.t_DealType, tick.t_BOfficeKind ) ) ) > 0 "
  + " AND Rsb_Secur.IsOutExchange( Rsb_Secur.get_OperationGroup( Rsb_Secur.get_OperSysTypes( tick.t_DealType, tick.t_BOfficeKind ) ) ) > 0 "
  + " AND Rsb_Secur.IsBasket( Rsb_Secur.get_OperationGroup( Rsb_Secur.get_OperSysTypes( tick.t_DealType, tick.t_BOfficeKind ) ) ) = 0 "

  + " AND ( tick.t_ClientID > 0 "
     + " OR tick.t_IsPartyClient = CHR( 88 ) "
    + " AND tick.t_PartyID > 0 ) "

  + " AND ( RSB_FIInstr.FI_AvrKindsEQ( " + String( FIKIND_AVOIRISS ) + ", " + String( AVOIRISSKIND_SHARE ) + ", fininstr.t_AvoirKind ) > 0 "
     + " OR RSB_FIInstr.FI_AvrKindsEQ( " + String( FIKIND_AVOIRISS ) + ", " + String( AVOIRISSKIND_DEPOSITORY_RECEIPT ) + ", fininstr.t_AvoirKind ) > 0 ) "

  + " AND EXISTS ( SELECT "
                   + " 1 "
               + " FROM "
                   + " ddlrq_dbt dlrq "
               + " WHERE "
                   + " dlrq.t_DocKind = tick.t_BOfficeKind "
               + " AND dlrq.t_DocID = tick.t_DealID "
               + " AND dlrq.t_DealPart = 1 "
               + " AND dlrq.t_Type = " + String( DLRQ_TYPE_DELIVERY ) + " "
               + " AND dlrq.t_State = " + String( DLRQ_STATE_EXEC ) + " "
               + " AND dlrq.t_FactDate < " + GetSQLDate( dpreg.Date ) + " ) "

  + " AND Rsb_Secur.GetDealSupplyDate2( tick.t_DealID, " + String( LEG_KIND_DL_TICK_BACK ) + " ) >= " + GetSQLDate( warrantDate ) + " ";

  draftsQuery =
    " SELECT "
      + " spdrprop.t_DraftID "
     + " ,deals.* "
  + " FROM "
      + " dspdrprop_dbt spdrprop "
     + " ,( " + dealsQuery + " ) deals "
  + " WHERE "
      + " ( ( spdrprop.t_DocumentKind = deals.t_BOfficeKind "
      + " AND spdrprop.t_DocumentID = deals.t_DealID "
       + " OR EXISTS ( SELECT "
                       + " 1 "
                   + " FROM "
                       + " dpmpaym_dbt sc_paym "
                   + " WHERE "
                       + " sc_paym.t_PaymentID = spdrprop.t_PaymentID "
                   + " AND sc_paym.t_DocKind = deals.t_BOfficeKind "
                   + " AND sc_paym.t_DocumentID = deals.t_DealID ) "
      + " AND spdrprop.t_DocumentKind = 0 "
      + " AND spdrprop.t_DocumentID = 0 ) "

     + " OR ( EXISTS ( SELECT "
                       + " 1 "
                   + " FROM "
                       + " ddlgrdeal_dbt grdeal "
                   + " WHERE "
                       + " grdeal.t_DocKind = deals.t_BOfficeKind "
                   + " AND grdeal.t_DocID = deals.t_DealID "
                   + " AND grdeal.t_ID = spdrprop.t_DocumentID ) "
       + " OR EXISTS ( SELECT "
                       + " 1 "
                   + " FROM "
                       + " ddlgrdoc_dbt grdoc "
                   + " WHERE "
                       + " grdoc.t_ServDocKind = deals.t_BOfficeKind "
                   + " AND grdoc.t_ServDocID = deals.t_DealID "
                   + " AND grdoc.t_DocKind = " + String( DL_DEPODRAFT ) + " "
                   + " AND grdoc.t_DocID = spdrprop.t_DraftID "
                   + " AND grdoc.t_GrDealID = spdrprop.t_DocumentID ) ) "
    + " AND spdrprop.t_DocumentKind = " + String( DL_DLGRDEAL ) + " "
    + " AND spdrprop.t_DocumentID = spdrprop.t_PaymentID ) ";

  depoAccIDEnum = "";
  if( DpAcList.Size > 0 )
    dpacCount = 0;
    while( dpacCount < DpAcList.Size )
      SmartSetBuff( dpacccrp, DpAcList[dpacCount] );

      if( depoAccIDEnum != "" )
        depoAccIDEnum = depoAccIDEnum + ", ";
      end;

      depoAccIDEnum = depoAccIDEnum + dpacccrp.DepoAccID;
      dpacCount = dpacCount + 1;
    end;
  else
    depoAccIDEnum = " 0 ";
  end;

  fiidEnum = "";
  if( AvoirList.Size > 0 )
    avoirCount = 0;
    while( avoirCount < AvoirList.Size )
      SmartSetBuff( dppmfi, AvoirList[avoirCount], subst_dppmfi );
      if( fiidEnum != "" )
        fiidEnum = fiidEnum + ", ";
      end;
      fiidEnum = fiidEnum + dppmfi.FIID;
      avoirCount = avoirCount + 1;
    end;
  else
    fiidEnum = " 0 ";
  end;

  paymsQuery =
    " SELECT "
      + " pmpaym.t_PaymentID  "
     + " ,drafts.* "
  + " FROM "
      + " dspdraft_dbt draft "
     + " ,dspdrmove_dbt spdrmove "
     + " ,dpmpaym_dbt pmpaym "
     + " ,ddepoacnt_dbt dw "
     + " ,ddepoacnt_dbt bn "
     + " ,( " + draftsQuery + " ) drafts "
  + " WHERE "
      + " draft.t_AutoKey = drafts.t_DraftID "
  + " AND spdrmove.t_DraftID = draft.t_AutoKey "
  + " AND pmpaym.t_PaymentID = spdrmove.t_PaymentID "
  + " AND dw.t_AutoKey(+) = pmpaym.t_PayerDpNode "
  + " AND bn.t_AutoKey(+) = pmpaym.t_ReceiverDpNode "

  + " AND ( ( draft.t_Kind = " + String( SP_DEPOPER_BOOKORDER ) + " " // SP_DEPOPER_UNIVOPERATION
      + " AND dw.t_Kind = " + String( SIDEBALANCE_PASSIVE ) + " "
      + " AND bn.t_Kind = " + String( SIDEBALANCE_PASSIVE ) + " ) "
     + " OR draft.t_Kind = " + String( SP_DEPOPER_TRANSFERORDER ) + " "
     + " OR draft.t_Kind = " + String( SP_DEPOPER_ENROLMENT ) + " ) "

  + " AND pmpaym.t_PaymStatus = " + String( PM_FINISHED ) + " "

  + " AND EXISTS ( SELECT "
                   + " 1 "
               + " FROM "
                   + " daccsubdc_dbt subdc "
                  + " ,daccsub_dbt accsub "
               + " WHERE "
                   + " subdc.t_PaymentID = pmpaym.t_PaymentID "
               + " AND accsub.t_AccAnaliticsID = subdc.t_AccAnaliticsID "
               + " AND ( accsub.t_SubAccountID = subdc.t_SubAccountPayerID "
                  + " OR accsub.t_SubAccountID = subdc.t_SubAccountReceiverID ) "
               + " AND EXISTS ( SELECT "
                                + " 1 "
                            + " FROM "
                                + " daccount_dbt acc "
                            + " WHERE "
                                + " acc.t_Account = accsub.t_SubAccountCode "
                            + " AND acc.t_Code_Currency = pmpaym.t_FIID "
                            + " AND acc.t_Chapter = " + String( CHAPT5 ) + " "
                            + " AND acc.t_DepoAcc IN ( SELECT "
                                                       + " acnt.t_AutoKey "
                                                   + " FROM "
                                                       + " ddepoacnt_dbt acnt "
                                                   + " START WITH "
                                                       + " acnt.t_AutoKey IN ( " + depoAccIDEnum + " ) "
                                                   + " CONNECT BY PRIOR "
                                                       + " acnt.t_AutoKey = acnt.t_Superior ) ) ) "

  + " AND pmpaym.t_FIID IN ( " + fiidEnum + " ) ";

  reglinsQuery =
    " SELECT "
      + " accsubpayer.t_SpecialType t_STypePayer "
     + " ,accsubrec.t_SpecialType t_STypeRec "
     + " ,accsubpayer.t_SubAccountCode t_PSubAccountCode "
     + " ,accsubrec.t_SubAccountCode t_RSubAccountCode "
     + " ,vanlpayer.t_Account t_PVanlAccount "
     + " ,vanlrec.t_Account t_RVanlAccount "
     + " ,dc.t_Sum "
     + " ,vanlpayer.t_FIID "
     + " ,payms.* "
  + " FROM "
      + " daccsubdc_dbt dc "
     + " ,daccsub_dbt accsubpayer "
     + " ,daccsub_dbt accsubrec "
     + " ,daccvanl_dbt vanlpayer "
     + " ,daccvanl_dbt vanlrec "
     + " ,( " + paymsQuery + " ) payms "
  + " WHERE "
      + " dc.t_PaymentID = payms.t_PaymentID "
  + " AND accsubpayer.t_AccAnaliticsID = dc.t_AccAnaliticsID "
  + " AND accsubpayer.t_SubAccountID = dc.t_SubAccountPayerID "
  + " AND vanlpayer.t_AccAnaliticsID = accsubpayer.t_AccAnaliticsID "
  + " AND vanlpayer.t_AnaliticsID = accsubpayer.t_AnaliticsID "
  + " AND accsubrec.t_AccAnaliticsID = dc.t_AccAnaliticsID "
  + " AND accsubrec.t_SubAccountID = dc.t_SubAccountReceiverID "
  + " AND vanlrec.t_AccAnaliticsID = accsubrec.t_AccAnaliticsID "
  + " AND vanlrec.t_AnaliticsID = accsubrec.t_AnaliticsID ";

  rsdCmd = RSDCommand( reglinsQuery );

  rsdCmd.NullConversion = true;
  rsdCmd.Execute();

  dataSet = TRsbDataSet( rsdCmd );

  while( ( dataSet != null ) and ( dataSet.MoveNext() ) )
    if( SQL_ConvTypeInteger( dataSet.STypePayer ) == ACCSUB_SPECTYPE_NONE )
      CorrectOneReglin( isTemp
                       ,SQL_ConvTypeStr( dataSet.PSubAccountCode )
                       ,SQL_ConvTypeStr( dataSet.PVanlAccount )
                       ,SQL_ConvTypeSum( dataSet.Sum )
                       ,SQL_ConvTypeInteger( dataSet.FIID ) );
      repoCorrected = true;
    end;
                                                                                
    if( SQL_ConvTypeInteger( dataSet.STypeRec ) == ACCSUB_SPECTYPE_NONE )
      CorrectOneReglin( isTemp
                       ,SQL_ConvTypeStr( dataSet.RSubAccountCode )
                       ,SQL_ConvTypeStr( dataSet.RVanlAccount )
                       ,$0 - SQL_ConvTypeSum( dataSet.Sum )
                       ,SQL_ConvTypeInteger( dataSet.FIID ) );
      repoCorrected = true;
    end;
  end;

  if( repoCorrected )
    if( isTemp )
      SQL_Execute( " BEGIN Rsb_Depo.CreateReestrTree_TMP( " + dpreg.RegisterID + " ); END; ", "Формирование дерева реестра" );
    else
      SQL_Execute( " BEGIN Rsb_Depo.CreateReestrTree_Real( " + dpreg.RegisterID + " ); END; ", "Формирование дерева реестра" );
    end;
  else
    stat = 1;
  end;

  return stat;
end; // Корректировка по внутрибанковским РЕПО
