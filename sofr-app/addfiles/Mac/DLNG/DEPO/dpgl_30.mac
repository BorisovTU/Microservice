/*
$Name: dpgl_30.mac
$Module: Депозитарий
$Description: Глобальные операции депозитария - Отражение операции по счетам
*/
import OprInter, InsCarryDoc, DPInter, dpgl_opr, opr_depo, RsbDataSet, "dpsteprep.mac";

/* Буферы ГЛ, заполняемые из операции */
record corpop( dpcorpop );    /*поручение*/
record regstr( dpregstr ); /*реестр*/

class CarryAcnt
  var LAccount; 
  var HAccount; 
end;

macro ExecuteStep( d, crp )
  var carryDate, AinPEnabled, accTrn, IssueAccount = "", EmissAccount = "", err = 0;
  var NewAcc;
  var ground = "Глобальная операция над выпусками ц/б";
  record corpop_buf(dpcorpop);
  var account = TBFile("account");
  var reglin = TBFile("dpreglin");
  var dppmfi = TBFile("dppmfi");
  var acccrp = TBFile("dpacccrp");
  var dpacntStor = TBFile("depoacnt"); //счет места хранения
  record AddParm("dpreglin");
  var acnt = TARRAY;
  var ArrayAcnt = Tarray();
  var i = 0;
  var ConvFIID, NewFIID;
  var StorAcc;
  var transsumm = 0;
  var query, hold_query, DepoAccData, DataSet;
  var stat = 0;
  var CmdText = "", RSDcmd = null;
  var CountAcc:integer = 0;
  var CAcnt;

  SetBuff( Doc, d ); /* документ проводки */
  SetBuff( corpop_buf, corpop );

  if (corpop.ValueDate<{curdate})
      carryDate = corpop.ValueDate;
  else
      carryDate = {curdate};
  end;

  //конвертируемый выпуск
  dppmfi.AddFilter(" t_RegisterID = " + regstr.RegisterID );
  if( not dppmfi.Next() ) // запись должна быть единственной - это неоспоримый факт
    err = 1;
  else
    ConvFIID = dppmfi.rec.FIID;
  end;
  dppmfi.DropFilter();

  //зачисляемый выпуск ( его может и не быть )
  if( ( not err ) and ( corpop.DocKind > 0 ) and ( corpop.DocKind != DP_DEPOPER_REPAY ) )
    NewFIID = corpop.Currency;
  end;

  if( GlobolCounterStep30 == 0 )
    CountAcc = GetCountAccGlobop(regstr.RegisterID);
    Progress.Init(CountAcc, "Выполняется операция...", "Выполняется операция");
  end;  

  //работаем в разрезе счетов ДЕПО

  //делаем выборку всех подходящих "голов" счетов депо
  DepoAccData = TRsbDataSet(GetGlobopQuery(regstr.RegisterID));
  if( DepoAccData.moveNext() )

    hold_query =   " select distinct lin.t_HolderAccID from ddpreglin_dbt lin, ddepoacnt_dbt acnt "
                 + "  where lin.t_RegisterID = " + regstr.RegisterID
                 + "    and lin.t_Type = " + DP_REGLINETYPE_LACCOUNT
                 + "    and acnt.t_AutoKey = lin.t_HolderAccID "
                 + "    and acnt.t_Root = " + DepoAccData.Root;     
    
    //счет места хранения (должен быть всегда) - единственный
    if( not err )
      acccrp.AddFilter(" t_RegisterID = " + regstr.RegisterID );
      if( acccrp.next() )  //должна быть единственн
        dpacntStor.rec.AutoKey = acccrp.rec.DepoAccID;
        if( not dpacntStor.GetEQ() )
          err = 1;
        end;
      else
        err = 1;
      end;
      acccrp.DropFilter();
    end;

    if( not err )
      //открытие л/с
      if(corpop.TransitAcc == "X")
        if( corpop.PayOffAccID > 0 )
          //счет эмитента
          IssueAccount = InputAccountByDepoAcc( corpop.PayOffPartID, DP_OPRACC_ISSUE, crp, DP_GLOBOP_OPERATION, err );
          if( IssueAccount == "" ) /* Была ошибка */
            if(err == DP_GETACC_ESC) /* прерывание пользователя */
              Progress.Remove();
              GlobolCounterStep30 = 0;
              return err;
            end;
            err = 1;
          end;
        end;
        if( (corpop.EmissiveAccID > 0) and (not err ))
          //эмиссионный счет
          EmissAccount = InputAccountByDepoAcc( corpop.EmissivePartID, DP_OPRACC_EMISS, crp, DP_GLOBOP_OPERATION, err );
          if( EmissAccount == "" ) /* Была ошибка */
            if(err == DP_GETACC_ESC) /* прерывание пользователя */
              Progress.Remove();
              GlobolCounterStep30 = 0;
              return err;
            end;
            err = 1;
          end;
        end;
      end;
    end;

    //открыть новые л/с
    //пассивные
    if( (not err) and (corpop.DocKind != DP_DEPOPER_REPAY))
      reglin.Clear();
      reglin.AddFilter( " t_RegisterID = " + regstr.RegisterID  + " and t_Type = " + DP_REGLINETYPE_LACCOUNT + " and t_HolderAccID in ( " + hold_query + " )" );
      while( (reglin.Next())  and (not err))
        CAcnt = CarryAcnt();
        account.Clear();
        account.AddFilter(" t_Chapter = 5 and t_Account = '"+reglin.rec.HAccount+"' and t_Code_Currency = " + reglin.rec.FIIDfrom);
        if( account.next() )
          NewAcc = InputAccountByDepoAcc( account.rec.DepoAcc, DP_OPRACC_OWNER, crp, DP_GLOBOP_OPERATION, err, reglin );  
          if( NewAcc == "" ) /* Была ошибка */
            if(err == DP_GETACC_ESC) /* прерывание пользователя */
              Progress.Remove();
              GlobolCounterStep30 = 0;
              return err;
            end;
            err = 1;
          else
            CAcnt.HAccount = NewAcc;
          end;
        end;
        account.DropFilter();

        if(not err)
          account.Clear();
          account.AddFilter(" t_Chapter = 5 and t_Account = '"+reglin.rec.LAccount+"' and t_Code_Currency = " + reglin.rec.FIIDfrom);
          if( account.next() )
            StorAcc = InputAccountByDepoAcc( account.rec.DepoAcc, DP_OPRACC_STOR, crp, DP_GLOBOP_OPERATION, err, reglin );  
            StorDepoAccStep30 = StorAcc;
            if( StorAcc == "" ) /* Была ошибка */
              if(err == DP_GETACC_ESC) /* прерывание пользователя */
                Progress.Remove();
                GlobolCounterStep30 = 0;
                return err;
              end;
              err = 1;
            else
              CAcnt.LAccount = StorAcc;
            end;
          end;
          account.DropFilter();
          
          ArrayAcnt[ArrayAcnt.Size] = CAcnt;
        end;
      end;
      reglin.DropFilter();
    end;   
    
    if( not err )
      //1.списание или перевод с пассивных л/с из реестра
      if( corpop.DocKind != DP_DEPOPER_BONEMISS )  //для операции "получение доходов" - отсутствуют
        if( (corpop.TransitAcc == "X") and (corpop.PayOffAccID > 0) )
             //1. Перевод с пасива на пассив
             reglin.Clear();
             reglin.AddFilter("t_RegisterID = " + regstr.RegisterID + " and t_Type = " + DP_REGLINETYPE_LACCOUNT + " and t_HolderAccID in ( " + hold_query + " )");
             i = 0;
             while( reglin.Next() )                                                                                  

               if( (NOT err) AND ( DocCarry( true,                    /* счета проводки - валюта (false - валюта с покрытием)*/                
                                             5,                       /*Глава*/
                                             ConvFIID,                /*Валюта*/
                                             reglin.rec.HAccount,     /*Плательшик*/
                                             IssueAccount,            /*Получатель*/
                                             reglin.rec.HRest,        /*сколько*/
                                             CarryDate,               /*дата*/
                                             0,                       /*Результат проводки*/
                                             ground,                  /*Основание*/
                                             0,
                                             0,
                                             accTrn,
                                             18                       /*Шифр операции*/
                                           )
                                 )
                 )
                 DPError.SetErr( "Ошибка при вставке проводки" );
                 err = 1;
               end;
               
               //Обработаем проводки по субсчетам учетаАктива в Пассиве 
               if( NOT err )
                 if( GetAinPAccState( AinPEnabled ) != 0 )
                   err = 1;
                 else
                   if( AinPEnabled ) /* и включен режим учета Актива в Пассиве */
                     err = ExecuteSubAccTransfer( false, accTrn.AccTrnID, reglin.rec.HAccount, IssueAccount, ConvFIID, reglin.rec.HRest, carryDate, 0 );
                     transsumm = transsumm + reglin.rec.HRest;
                   end;
                 end;
               end;
             end;
             reglin.DropFilter();
         elif(  (corpop.TransitAcc == "") or ((corpop.TransitAcc == "X") and (corpop.EmissiveAccID > 0)) )
           //2. Списание
           reglin.AddFilter("t_RegisterID = " + regstr.RegisterID + " and t_Type = " + DP_REGLINETYPE_LACCOUNT + " and t_HolderAccID in ( " + hold_query + " )");
           i = 0;
           while( reglin.Next() ) 

             if( (NOT err) AND ( DocCarry( true,                    /* счета проводки - валюта (false - валюта с покрытием)*/                
                                           5,                       /*Глава*/
                                           ConvFIID,                /*Валюта*/
                                           reglin.rec.HAccount,     /*Плательшик*/
                                           reglin.rec.LAccount,     /*Получатель*/
                                           reglin.rec.HRest,        /*сколько*/
                                           CarryDate,               /*дата*/
                                           0,                       /*Результат проводки*/
                                           ground,                  /*Основание*/
                                           0,
                                           0,
                                           accTrn,
                                           18                       /*Шифр операции*/
                                         )
                               )
               )
               DPError.SetErr( "Ошибка при вставке проводки" );
               err = 1;
             end;

             //Обработаем проводки по субсчетам учета Актива в Пассиве 
             if( NOT err )
               if( GetAinPAccState( AinPEnabled ) != 0 )
                 err = 1;
               else
                 if( AinPEnabled ) /* и включен режим учета Актива в Пассиве */
                   err = ExecuteSubAccWritingOFF( false, accTrn.AccTrnID, reglin.rec.HAccount, reglin.rec.LAccount, ConvFIID, reglin.rec.HRest, carryDate, 0 );
                 end;
               end;
             end;
           end;
           reglin.DropFilter();
         end;
       end;
     end;

     if( (not err) and (corpop.DocKind != DP_DEPOPER_REPAY))
       //3. Перевод с эмиссионного счета (если он есть) или зачисление с актива
       //перевод
       if( corpop.EmissiveAccID > 0 )
         i = 0;
         reglin.Clear();
         reglin.AddFilter("t_RegisterID = " + regstr.RegisterID + " and t_Type = " + DP_REGLINETYPE_LACCOUNT + " and t_HolderAccID in ( " + hold_query + " )");
         while( reglin.next())
           if( (NOT err) AND ( DocCarry( true,                    /* счета проводки - валюта (false - валюта с покрытием)*/                
                                         5,                       /*Глава*/
                                         NewFIID,                 /*Валюта*/
                                         EmissAccount,            /*Плательшик*/
                                         ArrayAcnt[i].HAccount,   /*Получатель*/
                                         reglin.rec.BruttoAmount, /*сколько*/
                                         CarryDate,               /*дата*/
                                         0,                       /*Результат проводки*/
                                         ground,
                                         0,
                                         0,
                                         accTrn,
                                         18                       /*Шифр операции*/
                                       )
                               )
               )
               DPError.SetErr( "Ошибка при вставке проводки" );
               err = 1;
           end;

           //Обработаем проводки по субсчетам учета Актива в Пассиве 
           if( NOT err )
             if( GetAinPAccState( AinPEnabled ) != 0 )
               err = 1;
             else
               if( AinPEnabled ) /* и включен режим учета Актива в Пассиве */
                 err = ExecuteSubAccTransfer( false, accTrn.AccTrnID, EmissAccount, ArrayAcnt[i].HAccount, NewFIID, reglin.rec.BruttoAmount, carryDate, 0 );
               end;
             end;
           end;
           i = i + 1;
         end;
         reglin.DropFilter();
       else
        //зачисление
          i = 0;
          reglin.Clear();
          reglin.AddFilter("t_RegisterID = " + regstr.RegisterID + " and t_Type = " + DP_REGLINETYPE_LACCOUNT + " and t_HolderAccID in ( " + hold_query + " )");
          while( (reglin.next) and (not err) )
            if( (NOT err) AND ( DocCarry( true,                    /* счета проводки - валюта (false - валюта с покрытием)*/                
                                          5,                       /*Глава*/
                                          NewFIID,                 /*Валюта*/
                                          ArrayAcnt[i].LAccount,   /*Плательшик*/
                                          ArrayAcnt[i].HAccount,   /*Получатель*/
                                          reglin.rec.BruttoAmount, /*сколько*/
                                          CarryDate,               /*дата*/
                                          0,                       /*Результат проводки*/
                                          ground,                  /*Основание*/
                                          0,
                                          0,
                                          accTrn,
                                          18                       /*Шифр операции*/
                                        )
                               )
              )
              DPError.SetErr( "Ошибка при вставке проводки" );
              err = 1;
            end;

            //Обработаем проводки по субсчетам учета Актива в Пассиве 
            if( NOT err )
              if( GetAinPAccState( AinPEnabled ) != 0 )
                err = 1;
              else
                if( AinPEnabled ) /* и включен режим учета Актива в Пассиве */
                  err = ExecuteSubAccEnrolment( false, accTrn.AccTrnID, ArrayAcnt[i].LAccount, ArrayAcnt[i].HAccount, NewFIID, reglin.rec.BruttoAmount, carryDate, 0 );            
                end;
              end;
            end;
            i = i + 1;
          end;
          reglin.DropFilter();
      end;
    end;
    GlobolCounterStep30 = Progress.Use();

    if( err )
      //если возникла ошибка на шаге, то вставляем блок с отказом
      err = RejectGlobopAcc(regstr.RegisterID, DepoAccData.Root );
      if( err )
        GlobolCounterStep30 = 0;
        Progress.Remove();
      end;
    else 
      err = DP_SetReglinStateForAcc(regstr.RegisterID, DepoAccData.Root, DPREGLINSTATE_CLOSE );
      if(not err)
        // Вставить исходящий документ уведомления об исполнении ГО для обработанного счета депо
        err = DP_RegExtGroundToReglin(regstr.RegisterID, DepoAccData.Root);
      end;
      if( not err )
        //Есть ли еще что-то на следующий такой же шаг?
        if( not DepoAccData.moveNext() )
          Progress.Remove();
          GlobolCounterStep30 = 0;
          if( not InsertOprStatus( GetGlobopOprState(corpop.DocKind), DEPOGLOBOP_STATEVALUE_REPORT) )
            msgbox("Невозможно изменить статус операции");
            err = 1;
          end;
        else
          if( not InsertOprStatus( GetGlobopOprState(corpop.DocKind), DEPOGLOBOP_STATEVALUE_DOCCARRY) )
             msgbox("Невозможно изменить статус операции");
             err = 1;
          end;
        end;
      end;
    end;
  else
    GlobolCounterStep30 = 0;
    Progress.Remove();
    if( not InsertOprStatus( GetGlobopOprState(corpop.DocKind), DEPOGLOBOP_STATEVALUE_REPORT) )
       msgbox("Невозможно изменить статус операции");
       err = 1;
    end;
  end;

  return err;

OnError(er)
  GlobolCounterStep30 = 0;
  Progress.Remove();
end;


/* Макрос постобработки 
*/
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

  record corpop_buf(dpcorpop);

  if (errTrn OR (CommitOrRollback==2))
    GlobolCounterStep30 = 0;
    Progress.Remove();
    /* Произошла ошибка или происходит откат */
    return;
  end;

  SetBuff( corpop_buf, FirstDoc );

  PrintGlsendByDoc(corpop_buf.DocumentID, ID_Operation, ID_Step);
                      
  return 1;
END;

