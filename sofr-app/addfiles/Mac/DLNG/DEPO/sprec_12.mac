/*
$Name: sprec_12.mac
$Module: Депозитарий
$Description: "Прием документарных ц/б на хранение", "Снятие документарных ц/б с хранения" - шаг Проверки поручения
*/
import OprInter, PTInter, InsCarryDoc, PaymInter, dpmassexec;

/* Буферы ПД, заполняемые из операции */
record Pm_paym( pmpaym );    /*основной платеж*/
record Pm_prop_deb( pmprop ); /*св-ва платежа банка-плательщика*/
record Pm_prop( pmprop );    /*св-ва платежа*/
record Pm_rmprop( pmrmprop ); /*платеж по ценным бумагам*/


macro ExecuteStep( d, spdraft )
  var err = 0, ErrStr = "";
  var KindOperation = 0;
  record r_spdraft( spdraft );
  var acnt = TBFile("depoacnt");
  var Type = 0;
  var OprStatus = 0, StatusKind = 0; 

  SetBuff( r_spdraft, spdraft );              

  if(r_spdraft.Kind == SP_DEPOPER_RECORDER)
    if(true != ПроверитьПолномочиеПоУзлу(Pm_paym.ReceiverDpNode, r_spdraft.Initiator, OBJTYPE_DEPOAUTHORITY_ENROL, Pm_paym.FIID, Pm_paym.ValueDate, r_spdraft.Product, @ErrStr))
      err = RejectDepoDraftYesNo( spdraft, DP_SPDRAFT_OPERATION, ErrStr );
    end;
  else
    if(true != ПроверитьПолномочиеПоУзлу(Pm_paym.PayerDpNode, r_spdraft.Initiator, OBJTYPE_DEPOAUTHORITY_WRITEOFF, Pm_paym.FIID, Pm_paym.ValueDate, r_spdraft.Product, @ErrStr))
      err = RejectDepoDraftYesNo( spdraft, DP_SPDRAFT_OPERATION, ErrStr );
    end;
  end;
  
  if(not err)
    if(r_spdraft.Kind == SP_DEPOPER_RECORDER)
      StatusKind = DP_OPERSTATUS_RECORD_DO;
    else
      StatusKind = DP_OPERSTATUS_WRITEOFF_DO;
    end;

    OprStatus = DP_OPERSTATUS_SPDRAFT_DO_DEPOPART;
    if(r_spdraft.OperState == DP_INVSTATUSORDER_REJECT)
      OprStatus = DP_OPERSTATUS_SPDRAFT_DO_CRREP;
    elif(r_spdraft.Kind == SP_DEPOPER_RECORDER)
      //экспертизу планируем только если активный счет (место хранения) имеет значение "Типа счета по назначению"  равное  "Хранилище" 
      //и депонентом активного счета является Наш банк
      acnt.rec.AutoKey = Pm_paym.PayerDpNode;
      if(acnt.GetEQ()) 
        if(acnt.rec.Owner == {OurBank})
          if(not ПолучитьТипСчетаПоНазначению(acnt.rec.Root, Type))
            if( Type == OBJTYPE_DEPOKINDTYPE_PLACESAVE )
              OprStatus = DP_OPERSTATUS_SPDRAFT_DO_EXAM;
            end;
          end;
        end;
      end;
    end;
    
    if( not InsertOprStatus( StatusKind, OprStatus) ) 
       msgbox( "Ошибка при установке статуса <Документооборот> операции" );
       err = 1;
    end;
  end;

  return err;
end;

MACRO MassExecuteStep()
  var err = 0;

  if(DP_MassCheckKI(true, " dr.t_Kind = "+SP_DEPOPER_WITHDRORDER ) != 0)
    return 1;
  end;

  if(DP_MassCheckKI(false, " dr.t_Kind = "+SP_DEPOPER_RECORDER ) != 0)
    return 1;
  end;

  VAR OperStatus = DP_MassOperStatus();

  OperStatus.Insert( DP_OPERSTATUS_RECORD_DO,   DP_OPERSTATUS_SPDRAFT_DO_CRREP, " dr.t_Kind = "+SP_DEPOPER_RECORDER+" and dr.t_OperState = " + DP_INVSTATUSORDER_REJECT );
  OperStatus.Insert( DP_OPERSTATUS_WRITEOFF_DO, DP_OPERSTATUS_SPDRAFT_DO_CRREP, " dr.t_Kind = "+SP_DEPOPER_WITHDRORDER+" and dr.t_OperState = " + DP_INVSTATUSORDER_REJECT );


  OperStatus.Insert( DP_OPERSTATUS_RECORD_DO,   DP_OPERSTATUS_SPDRAFT_DO_EXAM,  " dr.t_Kind = "+SP_DEPOPER_RECORDER
                                                                              + " and Exists(select 1 "
                                                                              + "              from ddepoacnt_dbt acnt "
                                                                              + "             where acnt.t_AutoKey = pm.t_PayerDpNode "
                                                                              + "               and acnt.t_Owner = "+{OurBank}
                                                                              + "               and rsb_depo.depokindtype(acnt.t_AutoKey) = " + OBJTYPE_DEPOKINDTYPE_PLACESAVE
                                                                              + "            )");


  err = OperStatus.Execute( "Шаг открытия счетов. Установка статуса операций." );

  if(err)
    DP_SetErrorOprTemp(0, err, "Ошибка при пакетном выполнении шага");
  end;

  return err;

OnError(er)
  DP_SetErrorOprTemp(0, 1, er.module+ " "+er.line+" "+er.message);
  return 1;
end;