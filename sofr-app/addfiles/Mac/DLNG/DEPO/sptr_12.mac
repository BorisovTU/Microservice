/*
$Name: sptr_12.mac
$Module: Депозитарий
$Description: Междепозитарный перевод эмиссионных ценных бумаг - шаг Проверки поручения
*/
import OprInter, PTInter, InsCarryDoc, PaymInter, dpmassexec;

/* Буферы ПД, заполняемые из операции */
record Pm_paym( pmpaym );    /*основной платеж*/
record Pm_prop_deb( pmprop ); /*св-ва платежа банка-плательщика*/
record Pm_prop( pmprop );    /*св-ва платежа*/
record Pm_rmprop( pmrmprop ); /*платеж по ценным бумагам*/


macro ExecuteStep( d, spdraft )
  record r_spdraft( spdraft );
  var OprStatus = 0;
  var err = 0, ErrStr = "";

  SetBuff( r_spdraft, spdraft );

  if(true != ПроверитьПолномочиеПоУзлу(Pm_paym.PayerDpNode, r_spdraft.Initiator, OBJTYPE_DEPOAUTHORITY_WRITEOFF, Pm_paym.FIID, Pm_paym.ValueDate, r_spdraft.Product, @ErrStr))
    err = RejectDepoDraftYesNo( spdraft, DP_SPDRAFT_OPERATION, ErrStr );
  end;

  if(not err)
    if((r_spdraft.FromTransitAccount != "X") and (Pm_paym.ReceiverBankID == {OurBank}))
      if(true != ПроверитьПолномочиеПоУзлу(Pm_paym.ReceiverDpNode, r_spdraft.Initiator, OBJTYPE_DEPOAUTHORITY_ENROL, Pm_paym.FIID, Pm_paym.ValueDate, r_spdraft.Product, @ErrStr))
        err = RejectDepoDraftYesNo( spdraft, DP_SPDRAFT_OPERATION, ErrStr );
      end;
    end;
  end;

  ///////////////
  //Проверки КИ
  if(not err)
    if(ДП_ПроверитьПринадлежностьКИ(spdraft, Pm_paym.FIID, Pm_paym.Receiver, Pm_paym.ReceiverDPNode))
      err = 1;
    end;
  end;
  ///////////////

  if(not err)
    if( (r_spdraft.OperState == DP_INVSTATUSORDER_REJECT) OR (r_spdraft.OperState == DP_INVSTATUSORDER_MAKEKVIT))
      if( not InsertOprStatus( DP_OPERSTATUS_TRANSF_DO, DP_OPERSTATUS_SPDRAFT_DO_CRREP) ) 
        msgbox( "Ошибка при установке статуса <Документооборот> операции" );
        err = 1;
      end;
    end;
  end;

  return err;
end;


MACRO MassExecuteStep()
  var err = 0;
  var query, cmd, DataSet;
  var stat = 0;
  var ErrStr = "";

  if(DP_MassCheckKI(false) != 0)
    return 1;
  end;

  query =   " select pm.t_PaymentID, pm.t_PayerDpNode, pm.t_ReceiverDpNode, pm.t_FIID, pm.t_ValueDate, pm.t_ReceiverBankID, "
          + "        dr.t_Initiator, dr.t_Product, dr.t_FromTransitAccount "
          + "   from DOPRTEMP_VIEW oprtemp, dspdraft_dbt dr, dspdrmove_dbt mv, dpmpaym_dbt pm"
          + "  where dr.t_AutoKey = oprtemp.t_OrderID "
          + "    and mv.t_DraftID = dr.t_AutoKey "
          + "    and pm.t_PaymentID = mv.t_PaymentID ";

  cmd = DL_RSDCommand(query);

  
  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    stat = 0;

    if(true != ПроверитьПолномочиеПоУзлу(DataSet.PayerDpNode, DataSet.Initiator, OBJTYPE_DEPOAUTHORITY_WRITEOFF, DataSet.FIID, date(DataSet.ValueDate), DataSet.Product, @ErrStr))
      stat = 1;
      DP_SetErrorOprTemp( DataSet.PaymentID, stat, ErrStr );
    end;

    if(not stat)
      if((DataSet.FromTransitAccount != "X") and (DataSet.ReceiverBankID == {OurBank}))
        if(true != ПроверитьПолномочиеПоУзлу(DataSet.ReceiverDpNode, DataSet.Initiator, OBJTYPE_DEPOAUTHORITY_ENROL, DataSet.FIID, date(DataSet.ValueDate), DataSet.Product, @ErrStr))
          stat = 1;
          DP_SetErrorOprTemp( DataSet.PaymentID, stat, ErrStr );
        end;
      end;
    end;
  end;


  VAR OperStatus = DP_MassOperStatus();

  OperStatus.Insert( DP_OPERSTATUS_TRANSF_DO, DP_OPERSTATUS_SPDRAFT_DO_CRREP, " dr.t_OperState IN (" + DP_INVSTATUSORDER_REJECT+","+DP_INVSTATUSORDER_MAKEKVIT+")" );

  err = OperStatus.Execute( "Шаг проверки поручений. Установка статуса операций." );

  if(err)
    DP_SetErrorOprTemp(0, err, "Ошибка при пакетном выполнении шага");
  end;

  return err;

OnError(er)
  DP_SetErrorOprTemp(0, 1, er.module+ " "+er.line+" "+er.message);
  return 1;
end;