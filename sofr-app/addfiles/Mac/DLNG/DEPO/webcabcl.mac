/*
$Name: webcabcl.mac
$Module: Депозитарий
$Description: Выгрузка клиентов для web-кабинета НРД
*/

import CTInter, DlReport_h, dpstdrep, webcabpt;

record reestr("dpregstr.dbt");


private macro SmartSetBuff(dest, src)
  if(ValType(src) == V_MEMADDR)
    SetBuff(dest, src);
  else
    Copy(dest, src);
  end;
end;


private class DP_CAcc(_ND, _IND, _OWN, _DU, _DP, _Other, _CommIntV, _CommIntD, _CommIntI)
  var ND       = _ND,
      IND      = _IND,
      OWN      = _OWN,
      DU       = _DU,
      DP       = _DP,
      Other    = _Other,
      CommIntV = _CommIntV,
      CommIntD = _CommIntD,
      CommIntI = _CommIntI;
end;


// Класс WR-отчета для выгрузки клиентов в web-кабинет НРД
private class (DL_CReportTemplate) DP_CUploadClientsRep( _exp_date, v_reestr, _IsTemp )
  private var IsRegister, IsTemp = _IsTemp, exp_date = _exp_date;
  private var acc_arr = TArray(), ddpreglin_name, ddploroown_name;

  if(valType(v_reestr) == V_UNDEF)
    IsRegister = false;
  else
    IsRegister = true;

    SmartSetBuff( reestr, v_reestr );

    ddpreglin_name = IIF(IsTemp, "ddpreglin_tmp", "ddpreglin_dbt");
    ddploroown_name = IIF(IsTemp, "ddploroown_tmp", "ddploroown_dbt");
  end;


  PRIVATE MACRO PrintMode()
    return DL_OUTREPORT_EXCEL;
  END;


  PRIVATE MACRO BeginReport()
    SetActiveSheet( "Импортировать данные о клиентах" );
  END;


  PRIVATE MACRO EndReport()
  END;


  private macro AddLine(CodeAcc, TypeAcc)
    var i = 0, GotPlace = false;

    while((i < acc_arr.Size) AND (NOT GotPlace) )
      if(TypeAcc == OBJTYPE_DEPOKINDTYPE_NOMINALKEEPER) // Номинального держателя
        if(acc_arr[i].ND == "")
          acc_arr[i].ND = CodeAcc;
          GotPlace = true;
        end;
      elif(TypeAcc == OBJTYPE_DEPOKINDTYPE_FOREIGNNOMKEEPER) // Иностранного номинального держателя
        if(acc_arr[i].IND == "")
          acc_arr[i].IND = CodeAcc;
          GotPlace = true;
        end;
      elif(TypeAcc == OBJTYPE_DEPOKINDTYPE_DEPONENT) // Владельца 
        if(acc_arr[i].OWN == "")
          acc_arr[i].OWN = CodeAcc;
          GotPlace = true;
        end;
      elif(TypeAcc == OBJTYPE_DEPOKINDTYPE_ISSUER) // Счет доверительного управляющего
        if(acc_arr[i].DU == "")
          acc_arr[i].DU = CodeAcc;
          GotPlace = true;
        end;
      elif(TypeAcc == OBJTYPE_DEPOKINDTYPE_DEPOSITORYPROGRAM) // Депозитарных программ
        if(acc_arr[i].DP == "")
          acc_arr[i].DP = CodeAcc;
          GotPlace = true;
        end;
      elif(TypeAcc == OBJTYPE_DEPOKINDTYPE_ALLOTHER) // Прочие счета
        if(acc_arr[i].Other == "")
          acc_arr[i].Other = CodeAcc;
          GotPlace = true;
        end;
      end;

      i = i + 1
    end;

    if(NOT GotPlace)
      if(TypeAcc == OBJTYPE_DEPOKINDTYPE_NOMINALKEEPER) // Номинального держателя
        acc_arr[acc_arr.Size] = DP_CAcc(CodeAcc, "", "", "", "", "", "", "", "");
      elif(TypeAcc == OBJTYPE_DEPOKINDTYPE_FOREIGNNOMKEEPER) // Иностранного номинального держателя
        acc_arr[acc_arr.Size] = DP_CAcc("", CodeAcc, "", "", "", "", "", "", "");
      elif(TypeAcc == OBJTYPE_DEPOKINDTYPE_DEPONENT) // Владельца 
        acc_arr[acc_arr.Size] = DP_CAcc("", "", CodeAcc, "", "", "", "", "", "");
      elif(TypeAcc == OBJTYPE_DEPOKINDTYPE_ISSUER) // Счет доверительного управляющего
        acc_arr[acc_arr.Size] = DP_CAcc("", "", "", CodeAcc, "", "", "", "", "");
      elif(TypeAcc == OBJTYPE_DEPOKINDTYPE_DEPOSITORYPROGRAM) // Депозитарных программ
        acc_arr[acc_arr.Size] = DP_CAcc("", "", "", "", CodeAcc, "", "", "", "");
      elif(TypeAcc == OBJTYPE_DEPOKINDTYPE_ALLOTHER) // Прочие счета
        acc_arr[acc_arr.Size] = DP_CAcc("", "", "", "", "", CodeAcc, "", "", "");
      end;
    end;
  end;


  private macro FlushLines(pt)
    var i = 0;

    while(i < acc_arr.Size)
      PrintTableLine( "N1_ID",       pt.ID(),             null,
                      "N1_Name",     pt.Name(),           null,
                      "N1_Address",  pt.LegalAdress(),    null,
                      "N1_Country",  pt.Country2Lat(),    null,
                      "N1_Agent",    pt.AgentName(),      null,
                      "N1_NSD_Code", pt.AgentNSDCode(),   null,
                      "N1_Status",   "A",                 null,
                      "N1_Number",   pt.DocNumber(),      null,
                      "N1_Type",     pt.DocType(),        null,
                      "N1_Main",     pt.DocIsMain(),      null,
                      "N1_ND",       acc_arr[i].ND,       null,
                      "N1_IND",      acc_arr[i].IND,      null,
                      "N1_Owner",    acc_arr[i].OWN,      null,
                      "N1_DU",       acc_arr[i].DU,       null,
                      "N1_DP",       acc_arr[i].DP,       null,
                      "N1_Other",    acc_arr[i].Other,    null,
                      "N1_CommIntV", acc_arr[i].CommIntV, null,
                      "N1_CommIntD", acc_arr[i].CommIntD, null,
                      "N1_CommIntI", acc_arr[i].CommIntI, null
                    );
      i = i + 1;
    end;

    acc_arr.Size = 0;
  end;


  private macro Create()
    var query, DataSet;
    var cmd, count, i = 0, stat;
    var oldPartyID = 0, pt, oldPartyName;

    SetXLSXFormat();

    if(IsRegister) // Выберем строки о счетах из списка-реестра: сначала все счета нашего баланса, затем - владельцев ЛОРО счетов
      query = " SELECT 0 t_IsLORO, " +
              "        0 t_LOROOwner, " +
              "        regl.t_HolderID, " +
              "        dpac.t_Code, " +
              "        tp.t_Type, " +
              "        pt.t_Name, " +
              "        CHR(1) t_LegalAdress, " +
              "        CHR(1) t_Nationality, " +
              "        CHR(1) t_INN, " +
              "        0 t_PaperKind, " +
              "        CHR(1) t_PaperSeries, " +
              "        CHR(1) t_PaperNumber " +
              "   FROM " + ddpreglin_name + " regl, ddepoacnt_dbt dpac, ddepoac_dbt tp, dparty_dbt pt " +
              "  WHERE regl.t_RegisterID = ? " +
              "    AND regl.t_Type = 20 " +
              "    AND regl.t_HolderID NOT IN (SELECT dp.t_PartyID FROM ddp_dep_dbt dp) " +
              "    AND dpac.t_AutoKey = regl.t_HolderAccId " +
              "    AND tp.t_ID = dpac.t_Type " +
              "    AND pt.t_PartyID = dpac.t_Owner " +
              " UNION " +
              " SELECT 1 t_IsLORO, " +
              "        regl.t_HolderID t_LOROOwner, "
              "        lr.t_PartyID t_HolderID, " +
              "        lr.t_Code, " +
              "        lr.t_AccType t_Type, " +
              "        DECODE(lr.t_LegalForm, 1, lr.t_FullName || ' ' || lr.t_ShortName || ' ' || lr.t_SecondName, lr.t_FullName) t_Name, " +
              "        lr.t_LegalIndex || ' ' || lr.t_LegalAdress t_LegalAdress, "
              "        lr.t_Nationality, " +
              "        lr.t_INN, " +
              "        lr.t_PaperKind, " +
              "        lr.t_PaperSeries, " +
              "        lr.t_PaperNumber " +
              "   FROM " + ddploroown_name + " lr, " + ddpreglin_name + " regl " +
              "  WHERE lr.t_RegLineID = regl.t_RegLineID " +
              "    AND lr.t_PartyID NOT IN (SELECT dp.t_PartyID FROM ddp_dep_dbt dp) " +
              "    AND regl.t_RegisterID = ?  " +
              "    AND regl.t_Type = 20 " +
              " ORDER BY t_IsLORO, t_HolderID DESC, t_Code ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(reestr.RegisterID);
      cmd.AddParam(reestr.RegisterID);
    else // Выберем все открытые на дату выгрузки пассивные счета клиентов
      query = " SELECT 0 t_IsLORO, " +
              "        0 t_LOROOwner, " +
              "        dpac.t_Owner t_HolderID, " +
              "        dpac.t_Code, " +
              "        tp.t_Type, " +
              "        pt.t_Name, " +
              "        CHR(1) t_LegalAdress, " +
              "        CHR(1) t_Nationality, " +
              "        CHR(1) t_INN, " +
              "        0 t_PaperKind, " +
              "        CHR(1) t_PaperSeries, " +
              "        CHR(1) t_PaperNumber " +
              "   FROM ddepoacnt_dbt dpac, ddepoac_dbt tp, dparty_dbt pt " +
              "  WHERE dpac.t_Kind = 2 " +
              "    AND dpac.t_Superior = 0 " +
              "    AND dpac.t_OpenDate <= ? " +
              "    AND rsb_depo.depostatus(dpac.t_AutoKey, ? ) = 0 " +
              "    AND dpac.t_Owner NOT IN (SELECT dp.t_PartyID FROM ddp_dep_dbt dp) " +
              "    AND tp.t_ID = dpac.t_Type " +
              "    AND pt.t_PartyID = dpac.t_Owner " +
              " ORDER BY t_IsLORO, t_HolderID DESC, t_Code ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(exp_date);
      cmd.AddParam(exp_date);
    end;

    count = cmd.GetCount();

    if( count > 0 )
      DataSet = cmd.Execute();

      RegisterTable( "N1_MainTable", NULL,
                     "N1_ID",
                     "N1_Name",
                     "N1_Address",
                     "N1_Country",
                     "N1_Agent",
                     "N1_NSD_Code",
                     "N1_Status",
                     "N1_Number",
                     "N1_Type",
                     "N1_Main",
                     "N1_ND",
                     "N1_IND",
                     "N1_Owner",
                     "N1_DU",
                     "N1_DP",
                     "N1_Other",
                     "N1_CommIntV",
                     "N1_CommIntD",
                     "N1_CommIntI"
                  );

      InitProgress( count, "Экспорт клиентов для web-кабинета НРД", "Экспорт клиентов");

      while(DataSet.moveNext())
        if((oldPartyID != DataSet.HolderID) OR (DataSet.HolderID == -1))
          FlushLines(pt);
 
          oldPartyID = DataSet.HolderID;
          oldPartyName = Dataset.Name;

          pt = NULL;

          pt = DP_CArchWebUploadPT(DataSet.HolderID, exp_date, DataSet.LOROOwner);

          if(DataSet.IsLORO)
            pt.ActuateData(DataSet.INN, DataSet.PaperKind, DataSet.PaperSeries, DataSet.PaperNumber, DataSet.Name, DataSet.LegalAdress, DataSet.Nationality );
          end;
        elif((oldPartyID == DataSet.HolderID) AND DataSet.IsLORO) // Субъект не сменился, но идут записи по счетам раскрывающим ЛОРО, в каждой строке могут быть свои данные, их нужно обновить
          pt.ActuateData(DataSet.INN, DataSet.PaperKind, DataSet.PaperSeries, DataSet.PaperNumber, DataSet.Name, DataSet.LegalAdress, DataSet.Nationality);
        end;

        AddLine(DataSet.Code, DataSet.Type);

        UseProgress(i = i + 1);
      end;

      FlushLines(pt);

      RemProgress();

      EndTable();
    end;
  end;


  initDL_CReportTemplate( NULL, "webcabcl.xls" );

end;


// Точка входа в макрос
macro UploadReestrClients( v_reestr,    
                           IsTemp   : bool
)
  var RepObj;

  RepObj = DP_CUploadClientsRep({curdate}, v_reestr, IsTemp);

  RepObj.Run();

  return 0;
end;


macro UploadClients()
  var RepObj;

  RepObj = DP_CUploadClientsRep({curdate});

  RepObj.Run();

  return 0;
end;
