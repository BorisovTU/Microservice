/*
$Name:         dpindepo.mac
$Module:       Депозитарий
$Description:  Отчет "Журнал входящих документов"
*/
import RsbDataSet;
import FIInter, CurrInter, CTInter, DPInter, secinter, deposerv;
import "spserv.mac";

var NameAlg  = TBFile("namealg");
var llvalues = TBFile("llvalues");
var party    = TBFile("party");

const  SP_DEPOPER_ADMOPER       = 825;//административная операция депо 
const  SP_DEPOPER_INFOPER       = 830;//информационная операция депо

//типы документов (переданные как параметры отчета)
const  DP_DEPO_TYPE_ALLIN   = 0; //все документы
const  DP_DEPO_TYPE_FIRST   = 1; //первичные
const  DP_DEPO_TYPE_DOCP    = 2; //документы-приложения

//виды операций депозитария
const DP_INVENT_OPERATION    = 0;//инвентарные
const DP_ADM_OPERATION       = 1;//административные 
const DP_INFORM_OPERATION    = 2;//информационные

//виды объектов для notetext
const OBJECT_INVENT = 104;
const OBJECT_INFORM = 105;

//номер алгоритма со статусами
const ALG_DEPO_TYPE_DOC = 3241;
//номер алгоритма с видами журналов
const ALG_DEPO_DRAFTREFKIND = 3252;

private const SPSK_OPEN    = 0;
private const SPSK_NEW     = 4;

private const SPKK_PASSIVE = 2;

//пустая строка в Oracle
const EMPTY_STRING = 1;

const TA_STAT_DEL = 152;
                                                                                                          
var HeadTable = "┌─────┬─────────┬─────────────┬────────────┬─────────────────────┬───────────────────────────┬─────────────────────────────┬─────────────┬─────────────┬─────────────┬──────────────────┬─────────────┬────────────┐\n"+
                "│  №  │ Входящий│             │ Дата/время │                     │                           │            Депонент         │    Номер    │    Дата     │             │      Способ      │             │            │\n"+
                "│ п/п │  номер  │ По журналу  │   приема   │         Тип         │       Вид документа       ├──────────────┬──────────────┤  документа  │ составления │ Отправитель │     доставки     │   Доставил  │   Принял   │\n"+
                "│     │         │             │            │                     │                           │    Код       │     ФИО/     │             │             │             │                  │             │            │\n"+
                "│     │         │             │            │                     │                           │              │ Наименование │             │             │             │                  │             │            │\n"+
                "├─────┼─────────┼─────────────┼────────────┼─────────────────────┼───────────────────────────┼──────────────┼──────────────┼─────────────┼─────────────┼─────────────┼──────────────────┼─────────────┼────────────┤\n"+
                "│  1  │    2    │      3      │      4     │          5          │            6              │      7       │       8      │      9      │      10     │      11     │        12        │      13     │     14     │\n"+
                "├─────┼─────────┼─────────────┼────────────┼─────────────────────┼───────────────────────────┼──────────────┼──────────────┼─────────────┼─────────────┼─────────────┼──────────────────┼─────────────┼────────────┤";
                
                
var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";


var DataSet;
var GUserLog;
var _is_ap;
private var aDepoAcc = "";

/*интерфейс в Namealg.dbt*/
macro __NameAlg(Type, Number)
  NameAlg.rec.iTypeAlg = Type;
  NameAlg.rec.iNumberAlg = Number;
  if(NameAlg.GetEQ)
    return NameAlg.rec.szNameAlg;
  end;
  return NULL;
end;

/*интерфейс в Namealg.dbt*/
macro __LLValues( List, Number)
  llvalues.rec.List = List;
  llvalues.rec.Element = Number;
  if(llvalues.GetEQ)
    return llvalues.rec.Name;
  end;
  return NULL;
end;

macro FillReport( DeliveryKind )
  file typeac("typeac");

  typeac.iNumType = TA_STAT_DEL;
  typeac.Type_Account = DeliveryKind;
  if( getEQ(typeac) )
     return typeac.Name_type; 
  end;

  return NULL;
end;

macro Person( Oper )
  file person("person");
  person.Oper = Oper;
  if( getEQ(person) )
     return person.Name; 
  end;

  return NULL;
end;

macro KindOperation(SourceDocKind)
   
   if(    (SourceDocKind == SP_DEPOPER_BOOKORDER)
       or (SourceDocKind == SP_DEPOPER_TRANSFERORDER)
       or (SourceDocKind == SP_DEPOPER_ENROLMENT)
       or (SourceDocKind == SP_DEPOPER_RECORDER)
       or (SourceDocKind == SP_DEPOPER_WITHDRORDER) )
       
       return DP_INVENT_OPERATION;

   elif(SourceDocKind == SP_DEPOPER_ADMOPER)
       return DP_ADM_OPERATION;
   elif(SourceDocKind == SP_DEPOPER_INFOPER)
       return DP_INFORM_OPERATION;
   end;

   return -1;
end;

private macro IsFirstDoc(SPgroundID)
  var IsFirstDoc = UNSET_CHAR;
  var cmd = DL_RSDCommand("select rsb_depo.GetSpgroundIsFirstDoc(?) as IsFirstDoc from dual");
  cmd.AddParam(SPgroundID);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    IsFirstDoc = DataSet.IsFirstDoc;
  end;

  return IsFirstDoc;
end;

private macro IsGrDoc(SPgroundID)
  var IsGrDoc = UNSET_CHAR;
  var cmd = DL_RSDCommand("select rsb_depo.GetSpgroundIsGrDoc(?) as IsGrDoc from dual");
  cmd.AddParam(SPgroundID);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    IsGrDoc = DataSet.IsGrDoc;
  end;

  return IsGrDoc;
end;

// номер основного (родительского) документа для субъектов множественной регистрации
PRIVATE MACRO  GetXldDeponent(parent:integer)

var sQuery = "Select sp.t_XLD XLD from dspground_dbt sp where sp.t_spgroundID = " + parent ;
var DataSet1 = TRsbDataSet( sQuery);
if(DataSet1.MoveNext())
 return DataSet1.XLD;
end;

END;

private macro GetSqlOwnDepoAcc(iDepart, iDepoAcc)
   var aDepoAcc;
   if(iDepoAcc != null)
      aDepoAcc = iDepoAcc;
   else
      aDepoAcc = 
      " SELECT depoacnt.t_AutoKey "+
      " FROM ddepoacnt_dbt depoacnt " +
      " WHERE depoacnt.t_Superior = 0 " +
      "   AND depoacnt.t_Department = " + iDepart +
      "   AND depoacnt.t_Kind = " + SPKK_PASSIVE +
      "   AND depoacnt.t_Status IN (" + SPSK_OPEN + "," + SPSK_NEW + ") " +
      "   AND depoacnt.t_Owner = " + {OurBank} +
      "    START WITH depoacnt.t_Superior = 0 " +
      "    CONNECT BY PRIOR depoacnt.t_AutoKey = depoacnt.t_Superior ";
   end;

   return " select acnt.t_AutoKey from ddepoacnt_dbt acnt where acnt.t_Root IN (" + aDepoAcc + ")";
end;

macro DPGetSqlString( iDepart:integer, iType:integer,
                    Kind_doc_Code:integer,
                    Sender_doc_Code:integer,
                    IsOwnAcc:bool,
                    iDepoAcc:integer,
                    Number_doc_beg:string,
                    Number_doc_end:string,
                    Date_doc_beg:date,
                    Date_doc_end:date,
                    iKind_send_doc:integer,
                    iPostman:integer,
                    Get_man_Code:integer, 
                    UserLog:integer
                    )

   var sQuery = " SELECT spgr.t_SourceDocID, spgr.t_SourceDocKind, spgr.t_References, " +
                "        spgr.t_Kind, spgr.t_AltXlD, spgr.t_Party, spgr.t_PartyName, spgr.t_DeliveryKind, " +
                "        spgr.t_Receptionist, spgr.t_RegistrDate, spgr.t_RegistrTime, spgr.t_SPgroundID, " +
                "        spgr.t_SignedDate, spgr.t_XlD, spgr.t_UserLog, spgr.t_Proxy, spgr.t_deponent, spgr.t_subjectID, spgr.t_parent " +
                "   FROM dspground_dbt spgr " +
                "  WHERE spgr.t_backoffice = 'Z' " +
                "    and spgr.t_doclog = " + LLCLASS_KINDDOC_DOCDEPO +
                "    and spgr.t_direction = " + ENTERING +
                "    and spgr.t_Department = " + iDepart;

   //сразу выберем документы по заданному типу
   if(iType == DP_DEPO_TYPE_ALLIN )
      sQuery = sQuery + " and spgr.t_Direction = " + ENTERING;
   elif(iType == DP_DEPO_TYPE_FIRST)
      sQuery = sQuery + " and rsb_depo.GetSpgroundIsFirstDoc (spgr.t_SPgroundID) = 'X'";
   elif(iType == DP_DEPO_TYPE_DOCP)
      sQuery = sQuery + " and rsb_depo.GetSpgroundIsGrDoc (spgr.t_SPgroundID) = 'X'";
   end;

   if( Sender_doc_Code > 0)
      sQuery = sQuery + " and spgr.t_party = "+Sender_doc_Code+" ";
   end;
  
   if( (iDepoAcc > 0) or IsOwnAcc)
      if(iDepoAcc > 0)
         aDepoAcc = GetSqlOwnDepoAcc(iDepart, iDepoAcc);
      else
         aDepoAcc = GetSqlOwnDepoAcc(iDepart, NULL);
      end;

      sQuery = sQuery + " AND (";
      // для инвентарных (документ-приложение)
      sQuery = sQuery + "     exists("+
                        "            SELECT 1 " +
                        "            FROM dspgrdoc_dbt gd, dspdraft_dbt dr, dspdrmove_dbt spdrmove, dpmpaym_dbt pmpaym " +
                        "            WHERE gd.t_SPgroundID = spgr.t_spgroundid" +
                        "              AND gd.t_sourcedockind = dr.t_kind " +
                        "              AND gd.t_sourcedocid = dr.t_autokey " +
                        "              AND spdrmove.t_DraftID = dr.t_autokey " +
                        "              AND spdrmove.t_PaymentID = pmpaym.t_PaymentID " +
                        "              AND dr.t_Department = " + iDepart +
                        "              AND (pmpaym.t_PayerDpNode IN ("+aDepoAcc+") or pmpaym.t_ReceiverDpNode IN ("+aDepoAcc+")) " +
                        "     )";
      // для инвентарных (первичный)
      sQuery = sQuery + "  or exists("+
                        "            SELECT 1 " +
                        "            FROM dspdraft_dbt t, dpmpaym_dbt pmpaym, dspdrmove_dbt spdrmove" +
                        "            WHERE spdrmove.t_DraftID = t.t_AutoKey " +
                        "              AND spdrmove.t_PaymentID = pmpaym.t_PaymentID " +
                        "              AND spgr.t_SPgroundID = t.t_SPGroundID " +
                        "              AND t.t_Department = " + iDepart +
                        "              AND (pmpaym.t_PayerDpNode IN ("+aDepoAcc+") or pmpaym.t_ReceiverDpNode IN ("+aDepoAcc+")) " +
                        "     )";

      // для информационных (документ-приложение)
      sQuery = sQuery + "  or exists("+
                        "            SELECT 1 " +
                        "            FROM dspgrdoc_dbt gd, dsplogopr_dbt t, dspinfopr_dbt spinfopr " +
                        "            WHERE spinfopr.t_SPInfDocID = t.t_recId " +
                        "              AND gd.t_SPgroundID = spgr.t_SPgroundID " +
                        "              AND gd.t_sourcedockind = " + SP_DEPOPER_INFOPER +
                        "              AND gd.t_sourcedocid = t.t_autokey " +
                        "              AND t.t_Department = " + iDepart +
                        "              AND spinfopr.t_DepoAcc IN ("+aDepoAcc+") " +
                        "     )";
      // для информационных (первичный)
      sQuery = sQuery + " or exists("+
                        "            SELECT 1 " +
                        "            FROM dsplogopr_dbt t, dspinfopr_dbt spinfopr " +
                        "            WHERE spinfopr.t_SPInfDocID = t.t_recId " +
                        "              AND spgr.t_SPgroundID = t.t_Draft " +
                        "              AND t.t_Department = " + iDepart +
                        "              AND spinfopr.t_DepoAcc IN ("+aDepoAcc+") " +
                        "     )";

      // для административных (документ-приложение)
      sQuery = sQuery + " or exists("+
                        "            SELECT 1 " +
                        "            FROM dspgrdoc_dbt gd, ddepoadmo_dbt t " +
                        "            WHERE gd.t_SPgroundID = spgr.t_SPgroundID " +
                        "              AND gd.t_sourcedockind = " + SP_DEPOPER_ADMOPER +
                        "              AND gd.t_sourcedocid = t.t_AdmoprID " +
                        "              AND t.t_Department = " + iDepart +
                        "              AND t.t_DepoAcc IN ("+aDepoAcc+") " +
                        "     )";
      // для административных (первичный)
      sQuery = sQuery + "  or exists("+
                        "            SELECT 1 " +
                        "            FROM ddepoadmo_dbt t " +
                        "            WHERE spgr.t_SPgroundID = t.t_Ground " +
                        "              AND t.t_Department = " + iDepart +
                        "              AND t.t_DepoAcc IN ("+aDepoAcc+") " +
                        "     )";

      // для глобальных, расчеты выплат по ц/б (документ-приложение)
      sQuery = sQuery + " or exists("+
                        "            SELECT 1 " +
                        "            FROM dspgrdoc_dbt gd, ddpcorpop_dbt t, ddpregstr_dbt dpregstr, ddpreglin_dbt reglin " +
                        "            WHERE spgr.t_SPgroundID = gd.t_SPgroundID " +
                        "              AND gd.t_sourcedockind = t.t_DocKind " +
                        "              AND gd.t_sourcedocid = t.t_DocumentID " +
                        "              AND dpregstr.t_DocumentID = t.t_DocumentID " +
                        "              AND t.t_Department = " + iDepart +
                        "              AND reglin.t_RegisterID = dpregstr.t_RegisterID " +
                        "              AND reglin.t_HolderAccID IN ("+aDepoAcc+") " +
                        "     )";
      // для глобальных, расчеты выплат по ц/б (первичный)
      sQuery = sQuery + " or exists("+
                        "            SELECT 1 " +
                        "            FROM ddpcorpop_dbt t, ddpregstr_dbt dpregstr, ddpreglin_dbt reglin " +
                        "            WHERE spgr.t_SPgroundID = t.t_SPgroundID " +
                        "              AND dpregstr.t_DocumentID = t.t_DocumentID " +
                        "              AND t.t_Department = " + iDepart +
                        "              AND reglin.t_RegisterID = dpregstr.t_RegisterID " +
                        "              AND reglin.t_HolderAccID IN ("+aDepoAcc+") " +
                        "     )";
      sQuery = sQuery + " ) ";
   end;

   if( Kind_doc_Code != 0 )
     sQuery = sQuery + " and spgr.t_kind = "+Kind_doc_Code+" ";
   end;

   if( Date_doc_beg <= {curdate} ) 
     sQuery = sQuery + " and spgr.t_registrdate >= "+GetSQLDate(Date_doc_beg)+" ";
   end;

   if( Date_doc_end <= {curdate} )
     sQuery = sQuery + " and spgr.t_registrdate <= "+GetSQLDate(Date_doc_end)+" ";
   end;
   
   if( Number_doc_beg )
     sQuery = sQuery + " and spgr.t_xld >= '"+Number_doc_beg+"' ";
   end;

   if( Number_doc_beg )
     sQuery = sQuery + " and spgr.t_xld <= '"+Number_doc_end+"' ";
   end;

   if( iKind_send_doc > 0)
     sQuery = sQuery + " and spgr.t_DeliveryKind = "+iKind_send_doc+" ";
   end;

   if( iPostman > 0)
     sQuery = sQuery + " and spgr.t_Proxy = "+iPostman+" ";
   end;

   if( Get_man_Code > 0)
     sQuery = sQuery + " and spgr.t_Receptionist = "+Get_man_Code+" ";
   end;
   
   if( Userlog )
     sQuery = sQuery + " and spgr.t_UserLog = "+UserLog+" ";
   end;
   
   sQuery = sQuery + " order by spgr.t_SpgroundID";
   
   return sQuery;
end;

macro PrintHeader(  iStatus:integer,
                    Kind_doc_Code:integer,
                    Sender_doc_Code:integer,
                    Number_doc_beg:string,
                    Number_doc_end:string,
                    UserLog:integer,
                    Date_doc_beg:date,
                    Date_doc_end:date,
                    sKind_send_doc:string,
                    iPostman:integer,
                    Get_man_Code:integer )
   var HeaderStr;
   var Doc_Name;
   var Status_str;
   var Deliv_Name;
   var Pers_Name;
   var cParty;

   HeaderStr = "Ж У Р Н А Л  В Х О Д Я Щ И Х  Д О К У М Е Н Т О В";
   DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, HeaderStr, 0, 0, false, null, null, tablewidth );

   DP_AddPrintCell( Rep, "Установки фильтра", 30, 0, "l:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
   Rep.AddStr();
   
   if( Number_doc_beg  and (Number_doc_end) )
     DP_AddPrintCell( Rep, "диапазон входящих номеров:", 30, 0, "l:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );

     DP_AddPrintCell( Rep,  Number_doc_beg+" - "+Number_doc_end, tablewidth-30, 0, "l:w:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if(UserLog)
     DP_AddPrintCell( Rep, "по журналу:", 30, 0, "l:" + RepFontStyleTitel, _is_ap,REP_ELEM_STR );
     DP_AddPrintCell( Rep, __NameAlg( ALG_DEPO_DRAFTREFKIND, DataSet.UserLog ), tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap,REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( Date_doc_beg <= {curdate} )
     DP_AddPrintCell( Rep, "период приёма:", 30, 0, "l:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep, Date_doc_beg, 10, 0, "l:f" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep, " - " + Date_doc_end, 20, 0, "l:f" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( iStatus > 0 )
     Status_str = __NameAlg( ALG_DEPO_TYPE_DOC, iStatus );
     DP_AddPrintCell( Rep, "тип документа:", 30, 0, "l:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep, Status_str, tablewidth-30, 0, "l:w:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( Kind_doc_Code > 0 )
     Doc_Name = __LLValues( 1050, Kind_doc_Code );
     DP_AddPrintCell( Rep, "вид документа:", 30, 0, "l:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep, Kind_doc_Code, 4, 0, "l:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep, "  " + Doc_Name, tablewidth-34, 0, "l:w:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( Sender_doc_Code > 0 )
     cParty = CDPParty(DataSet.Party, Date_doc_end);
     DP_AddPrintCell( Rep, "отправитель:", 30, 0, "l:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep, cParty.Name(), tablewidth-30, 0, "l:w:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( sKind_send_doc != "" )
     if( (Deliv_Name = FillReport( StrFor(int(sKind_send_doc)) )) != NULL )
       DP_AddPrintCell( Rep,  "способ доставки:", 30, 0, "l:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
       DP_AddPrintCell( Rep, Deliv_Name, tablewidth-30, 0, "l:w:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
       Rep.AddStr();
     end;
   end;

   if( iPostman >= 0 )
     ПолучитьСубъекта(iPostman, party);
     DP_AddPrintCell( Rep, "доставил:", 30, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
     DP_AddPrintCell( Rep, party.rec.Name, tablewidth-30, 0, "l:w:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( Get_man_Code > 0 )
     Pers_Name = Person( Get_man_Code );
     DP_AddPrintCell( Rep,  "принял:", 30, 0, "l:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep, "" + Get_man_Code, 6, 0, "l:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  " " + Pers_Name, tablewidth-36, 0, "l:w:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;
   Rep.AddEmptyStr();
end;

macro PrintStr(count, XLD, UserLogName,date_str,
               op_type, sKind, deponentID, deponent, ALTXld, SignedDate, 
               Party_Name, sDelivery, sProxyShortName, sOper)

   DP_AddPrintCell( Rep, count, 0, 0, "c:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, XLD, 0, 0, "c:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, UserLogName, 0, 0, "l:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, date_str, 0, 0, "l:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, op_type, 0, 0, "c:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, sKind, 0, 0, "l:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, deponentID, 0, 0, "l:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, deponent, 0, 0, "l:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, ALTXld, 0, 0, "l:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, SignedDate, 0, 0, "l:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, Party_Name, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, sDelivery, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, sProxyShortName, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, sOper, 0, 0, "l:w" + RepFontStyleTitel );
   Rep.AddStr();
end;


macro Report(Date_doc_end)
   
   var count = 1;
   var stat = 1;
   var op_Type;
   var Initiator;
   var sKind;
   var Party_Name = "";
   var sDelivery;
   var sOper;
   var date_str;
   var AltXlD;
   var SignedDate;
   var UserLogName;
   var doc_date, cParty;
   var sProxyShortName;
   record party(party);
   var Deponent = "";
   var DeponentID;
   var Xld;
   
   while( stat )
     UseProgress(count);

     //Определяем тип документа
     if(IsFirstDoc(DataSet.SPgroundID))
       //op_type = "п";
       op_type = "Первичный";
     elif(IsGrDoc(DataSet.SPgroundID))
       //op_type = "дп";
       op_type = "Документ-приложение";
     else
       op_type = "";
     end;

     //Находим название документа
     if( DataSet.Kind >= 0 )
       sKind = __LLValues( 1050, DataSet.Kind );
     else
       sKind = "";
     end;

     //Номер
     if( CodeFor(DataSet.AltXLD) == EMPTY_STRING )
       AltXlD = "";
     else
       AltXlD = DataSet.AltXLD;
     end;
  
     //Наименование отправителя
     if(string(DataSet.PartyName) == "")
       if(date(DataSet.RegistrDate) != date(0,0,0))
         doc_date = date(DataSet.RegistrDate);
       elif(date(DataSet.SignedDate) != date(0,0,0))
         doc_date = date(DataSet.SignedDate);
       else
         doc_date = Date_doc_end;
       end;
       cParty = CDPParty(DataSet.Party, doc_date);
       Party_Name = cParty.Name();
     else
       Party_Name = string(DataSet.PartyName);
     end;

     //название способа доставки
     if(DataSet.DeliveryKind > 0 )
       sDelivery = FillReport( StrFor(DataSet.DeliveryKind) );
     else
       sDelivery = "";
     end;
     //наименование опера
     if(DataSet.Receptionist > 0)
       sOper = Person( DataSet.Receptionist );
     else
       sOper = "";
     end;
     //Дата и время
      date_str = (string(date(DataSet.RegistrDate):f)+" "+string(time(DataSet.RegistrTime)));
     //Дата составления
     if(date(DataSet.SignedDate) != date(0,0,0))
       SignedDate = string(date(DataSet.SignedDate):f);
     else
       SignedDate = "";
     end;

     //номер входящего документа (основного)
     if(DataSet.Parent > 0)
       Xld = GetXldDeponent(DataSet.Parent);     // номер основного (родительского) документа для субъектов множественной регистрации
     else
       Xld = DataSet.XLD;
     end;

     //депонент
     if( DataSet.deponent > 0 )   // если есть депонент
       deponentID = ПолучитьКодСубъекта(DataSet.deponent,1);
       deponent = ПолучитьКороткоеИмяСубъекта( DataSet.deponent);
     else
       if(DataSet.subjectID > 0)  // если нет депонента, но есть субъект множественной регистрации
         deponentID = ПолучитьКодСубъекта(DataSet.subjectID,1);
         deponent = ПолучитьКороткоеИмяСубъекта( DataSet.subjectID);
       else
         deponentID = "";
         deponent = "" ;
       end;
     end;

     UserLogName = __NameAlg( ALG_DEPO_DRAFTREFKIND, DataSet.UserLog );

     sProxyShortName = "";
     if( (DataSet.Proxy > 0) and (ПолучитьСубъекта(DataSet.Proxy, party) == 0) )
       sProxyShortName = party.ShortName;
     end;

     PrintStr(count, Xld, UserLogName, date_str,
              op_type, sKind, deponentID, deponent, AltXlD, SignedDate, 
              Party_Name, sDelivery, sProxyShortName, sOper);
     count = count + 1;
     stat = DataSet.MoveNext();    
   end;

   RemProgress();
   return 0;
end;


macro GenRep( iDepart:integer,
              iType:integer,
              Kind_doc_Code:integer,
              Sender_doc_Code:integer,
              IsOwnAcc:bool,
              iDepoAcc:integer,
              Number_doc_beg:string,
              Number_doc_end:string,
              Date_doc_beg:date,
              Date_doc_end:date,
              sKind_send_doc:string,
              iPostman:integer,
              Get_man_Code:integer,
              ToExcel:bool,
              is_ap:bool,
              UserLog:integer
)
   var sQuery:string;
   var iSenderCode:integer;
   var count = 0; //количество записей выборки
   var refer = 0;
   _is_ap = is_ap;
   iSenderCode = Sender_doc_Code;
   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

   GUserLog = UserLog;

   //Формируем строку SQL запроса к spground.dbt
   sQuery = DPGetSqlString( iDepart, iType,
                          Kind_doc_Code, iSenderCode, 
                          IsOwnAcc, iDepoAcc,
                          Number_doc_beg, Number_doc_end, Date_doc_beg, Date_doc_end,
                          int(sKind_send_doc), iPostman, Get_man_Code, UserLog );

   DataSet = TRsbDataSet( sQuery, RSDVAL_CLIENT, RSDVAL_STATIC);//получаем выборку из spground.dbt

   DataSet.MoveLast();  //нужно, чтобы определить размер кеша
   count = DataSet.GetRecCount();//работает только с RSDVAL_STATIC!!!
   DataSet.MoveFirst();

   if( count )
       PrintHeader( iType, Kind_doc_Code, Sender_doc_Code,
                    Number_doc_beg, Number_doc_end, UserLog, Date_doc_beg,
                    Date_doc_end, sKind_send_doc, iPostman, Get_man_Code );

       InitProgress( count, "Идёт формирование отчёта...", "Отчет \"ЖУРНАЛ ВХОДЯЩИХ ДОКУМЕНТОВ\"" );

       Report(Date_doc_end);
       
       DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

       DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
       DP_EndProtocol();                 //закончить протоколирование

       if( ToExcel )
         Rep.SetZoomType( ZOOM_TYPE_A4L );// альбомная ориентация
         Rep.PrintWinRep();
         Rep.ShowWinRep();
       else
         Rep.PrintRep();
       end;

   else
      DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
      DP_EndProtocol();                 //закончить протоколирование

      MsgBox("Не найдено данных, удовлетворяющих параметрам отчёта.");
   end;

  return 0;
end;

macro PrintReportFromScrol( sQuery:string, ToExcel:bool )
   var count = 0; //количество записей выборки
   var refer = 0;
   var HeaderStr;
   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

   DataSet = TRsbDataSet( sQuery, RSDVAL_CLIENT, RSDVAL_STATIC);//получаем выборку из spground.dbt

   DataSet.MoveLast();  //нужно, чтобы определить размер кеша
   count = DataSet.GetRecCount();//работает только с RSDVAL_STATIC!!!
   DataSet.MoveFirst();

   if( count )
      HeaderStr = "Ж У Р Н А Л  В Х О Д Я Щ И Х  Д О К У М Е Н Т О В";
      DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, HeaderStr, 0, 0, false, null, null, tablewidth );
      InitProgress( count, "Идёт формирование отчёта...", "Отчет \"ЖУРНАЛ ВХОДЯЩИХ ДОКУМЕНТОВ\"" );
      Report();
      DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

      DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
      DP_EndProtocol();                 //закончить протоколирование

      if( ToExcel )
        Rep.SetZoomType( ZOOM_TYPE_A4L );// альбомная ориентация
        Rep.PrintWinRep();
        Rep.ShowWinRep();
      else
        Rep.PrintRep();
      end;

   else
      DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
      DP_EndProtocol();                 //закончить протоколирование

      MsgBox("Не найдено данных, удовлетворяющих параметрам отчёта.");
   end;

  return 0;
end;

