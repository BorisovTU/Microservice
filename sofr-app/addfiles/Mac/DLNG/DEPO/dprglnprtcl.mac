/*
$Name:         dprglnprtcl.mac
$Module:       Депозитарий
$Description:  Макрос протокола процедуры формирования списка-реестра
*/

import SfInter, "opr_depo.mac";

private const DPRGLNPRTCLPNNF_FICODE = 0;
private const DPRGLNPRTCLPNNF_FINAME = 1;

private class ( DL_CPanel ) CReglinProtocolPanel()
  private macro Init()
    AddFieldProc( 0, DPRGLNPRTCLPNNF_FICODE, DL_PNFLD_FI, @DL_FldProc_FI_ISO_Num, NATCUR, 1, 1 );
    AddFieldProc( 0, DPRGLNPRTCLPNNF_FINAME, DL_PNFLD_FI_NAME, @DL_FldProc_FI_Name, NATCUR, 1, 1 );
  end;

  initDL_CPanel( "dp_res.lbr", "SRGLB2PP" );
end;

private class ( RsdRecordset ) CSettAccSet( settAccId:Integer )
  private var query = "", cmd = null;

  query = " SELECT "
            + " settacc.* "
        + " FROM "
            + " dsettacc_dbt settacc "
        + " WHERE "
            + " settacc.t_SettAccID = ? ";

  cmd = RsdCommand( query );
  cmd.AddParam( "", RSDBP_IN, settAccId );
  cmd.NullConversion = true;
  cmd.Execute();

  initRsdRecordset( cmd );
end;

private class ( RsdRecordset ) CAccountSet( chapter:Integer, account:String, codeCurrency:Integer )
  private var query = "", cmd = null;

  query = " SELECT "
            + " account.* "
        + " FROM "
            + " daccount_dbt account "
        + " WHERE "
            + " account.t_Chapter = ? "
        + " AND account.t_Account = ? "
        + " AND account.t_Code_Currency = ? ";

  cmd = RsdCommand( query );
  cmd.AddParam( "", RSDBP_IN, chapter );
  cmd.AddParam( "", RSDBP_IN, account );
  cmd.AddParam( "", RSDBP_IN, codeCurrency );
  cmd.NullConversion = true;
  cmd.Execute();

  initRsdRecordset( cmd );
end;

private class ( DL_CReportTemplate ) CReglinProtocol( isTemp:Bool, registerID:Integer, issuer:Integer, currency:Integer, toExcel:Bool )
  private var m_IsTemp:Bool = isTemp, m_RegisterID:Integer = registerID, m_Issuer:Integer = issuer, m_Currency:Integer = currency;
  private var m_DataExist:Bool = false;

  macro Create( NumCopy )
    var err = 0;
    var query = "", cmd = null, set = null;
    var settAccID = 0;
    var set2 = null;
    var set3 = null;
    var note = "", note2 = "";

    query = " SELECT "
              + " q.* "
             + " ,CASE "
                  + " WHEN q.t_TaxResident = 1 "
                   + " AND q.t_NotResident = 'X' "
                    + " OR q.t_TaxResident <> 1 "
                   + " AND q.t_NotResident <> 'X' "
                  + " THEN 1 "
                  + " ELSE 0 "
              + " END AS t_Error2 "
             + " ,COUNT(1) OVER () AS t_Count "
             + " ,ROWNUM AS t_Number "
          + " FROM "
              + " (SELECT "
                   + " dpreglin.t_HolderAccID "
                  + " ,depoacnt.t_Code "
                  + " ,NVL2(sfcontr.t_ID, 1, 0) AS t_DOExist "
                  + " ,NVL2(sfunioncontr.t_UnionContrID, 1, 0) AS t_SDOExist "
                  + " ,NVL(sfunioncontr.t_Number, CHR(1)) AS t_SDONumber "
                  + " ,NVL(sfunioncontr.t_DateConc, TO_DATE('01.01.0001', 'DD.MM.YYYY')) AS t_DateConc "
                  + " ,NVL2(sfcontr2.t_ID, 1, 0) AS t_DBOExist "
                  + " ,NVL(sfcontr2.t_Number, CHR(1)) AS t_DBONumber "
                  + " ,NVL(sfcontr2.t_DateBegin, TO_DATE('01.01.0001', 'DD.MM.YYYY')) AS t_DateBegin "
                  + " ,NVL(sfcontr2.t_DateClose, TO_DATE('01.01.0001', 'DD.MM.YYYY')) AS t_DateClose "
                  + " ,NVL(dlcontr.t_IIS, CHR(0)) AS t_IIS "
                  + " ,NVL(dlcontr.t_IISCloseByTransf, CHR(0)) AS t_IISCloseByTransf "
                  + " ,NVL(dlcontr.t_IISEnrolBan, CHR(0)) AS t_IISEnrolBan "
                  + " ,CASE "
                       + " WHEN party.t_NotResident = 'X' "
                        + " AND party.t_NRCountry = 'RUS' "
                         + " OR party.t_NotResident <> 'X' "
                        + " AND party.t_NRCountry <> 'RUS' "
                       + " THEN 1 "
                       + " ELSE 0 "
                   + " END AS t_Error1 "
                  + " ,party.t_NotResident "
                  + " ,party.t_NRCountry "
                  + " ,Rsb_Secur.GetMainObjAttr(" + OBJTYPE_PARTY + " "
                                           + " ,LPAD(dpreglin.t_HolderID, 10, '0') "
                                           + " ,42 "
                                           + " ,?) AS t_TaxResident "
                  + " ,party.t_Name "
                  + " ,Rsi_RsbParty.GetPartyCode(dpreglin.t_HolderID, " + PTCK_CONTR + ") AS t_PartyCode "
               + " FROM "
                   + " dparty_dbt party "
                  + " ,dsfcontr_dbt sfcontr2 "
                  + " ,ddlcontr_dbt dlcontr "
                  + " ,dsfunioncontr_dbt sfunioncontr "
                  + " ,dsfcontr_dbt sfcontr "
                  + " ,ddepoacnt_dbt depoacnt "
                  + " ," + IIF( m_IsTemp, "ddpreglin_tmp", "ddpreglin_dbt" ) + " dpreglin "
               + " WHERE "
                   + " party.t_PartyID = dpreglin.t_HolderID "
               + " AND sfcontr2.t_ID(+) = dlcontr.t_SfContrID "
               + " AND dlcontr.t_UniDepoContrID(+) = sfunioncontr.t_UnionContrID "
               + " AND sfunioncontr.t_UnionContrID(+) = sfcontr.t_UnionContrID "
               + " AND sfcontr.t_ServKind(+) = " + PTSK_DEPOS + " "
               + " AND sfcontr.t_ObjectType(+) = " + SF_DEPOACC + " "
               + " AND sfcontr.t_Object(+) = depoacnt.t_Code "
               + " AND depoacnt.t_AutoKey = dpreglin.t_HolderAccID "
               + " AND dpreglin.t_RegisterID = ? "
               + " AND dpreglin.t_Type = " + DP_REGLINETYPE_LACCOUNT + ") q ";

    cmd = RsdCommand( query );
    cmd.AddParam( "", RSDBP_IN, {CurDate} );
    cmd.AddParam( "", RSDBP_IN, m_RegisterID );
    cmd.NullConversion = true;
    cmd.Execute();

    set = RsdRecordset( cmd );
    while( set.MoveNext() )
      if( set.Value( "t_Number" ) == 1 )
        InitProgress( set.Value( "t_Count" ), "Идёт формирование протокола...", "Протокол процедуры формирования списка-реестра" );

        CreateTotalBook();
        SetActiveSheet( "Протокол" );

        DP_StdHeaderLO_Ex( null, null, "Протокол процедуры формирования списка-реестра", 0, 0, false, {CurDate}, {CurDate}, 0, null, null, this, "N1_" );
        CopyAllSheetInTotalBook( null, false, "N1_TableHeader", 1 );
      end;

      UseProgress( set.Value( "t_Number" ) );

      if( ( set.Value( "t_DOExist" ) == 0 ) or ( set.Value( "t_SDOExist" ) == 0 ) )
        note = "Для счета депо " + set.Value( "t_Code" ) + " не определен сводный договор обслуживания";
      else
        if( set.Value( "t_DBOExist" ) == 0 )
          if( ( DP_CrpFindSettAcc( m_IsTemp, {CurDate}, m_RegisterID, set.Value( "t_HolderAccID" ), m_Issuer, m_Currency, false, settAccID ) != 0 )
           or ( ( set2 = CSettAccSet( settAccID ) ) == null ) or not set2.MoveNext()
           or ( ( set3 = CAccountSet( set2.Value( "t_Chapter" ), set2.Value( "t_Account" ), set2.Value( "t_FIID" ) ) ) == null ) or not set3.MoveNext() )
            note = "Отсутствует информация о платежных инструкциях по выплате доходов по ц/б";
          else
            if( ( set2.Value( "t_Chapter" ) == {OurBank} )
            and ( set3.Value( "t_Open_Close" ) == "З" ) and ( set3.Value( "t_Close_Date" ) == {CurDate} ) )
              note = "Лицевой счет " + set2.Value( "t_Account" ) + ", указанный для перечисления выплаты, закрыт. Дата закрытия лицевого счета " + set3.Value( "t_Close_Date" );
            end;
          end;
        else
          if( Date( set.Value( "t_DateClose" ) ) == {CurDate} )
            if( set.Value( "t_IIS" ) == SET_CHAR )
              if( set.Value( "t_IISCloseByTransf" ) == SET_CHAR )
                note = "Договор ИИС № " + set.Value( "t_DBONumber" ) + " закрыт с переводом активов";
              else
                note = "Договор ИИС № " + set.Value( "t_DBONumber" ) + " закрыт без перевода активов";
              end;
            else
              note = "Договор на брокерское обслуживание № " + set.Value( "t_DBONumber" ) + " закрыт";
            end;
          elif( Date( set.Value( "t_DateClose" ) ) == ZeroDate )
            if( set.Value( "t_IISEnrolBan" ) == SET_CHAR )
              if( ( DP_CrpFindSettAcc( m_IsTemp, {CurDate}, m_RegisterID, set.Value( "t_HolderAccID" ), m_Issuer, m_Currency, false, settAccID ) != 0 )
               or ( ( set2 = CSettAccSet( settAccID ) ) == null ) or not set2.MoveNext()
               or ( ( set3 = CAccountSet( set2.Value( "t_Chapter" ), set2.Value( "t_Account" ), set2.Value( "t_FIID" ) ) ) == null ) or not set3.MoveNext() )
                note = "Отсутствует информация о платежных инструкциях по выплате доходов по ц/б";
              else
                if( ( set2.Value( "t_Chapter" ) == {OurBank} )
                and ( set3.Value( "t_Open_Close" ) == "З" ) and ( set3.Value( "t_Close_Date" ) == {CurDate} ) )
                  note = "Лицевой счет " + set2.Value( "t_Account" ) + ", указанный для перечисления выплаты, закрыт. Дата закрытия лицевого счета " + set3.Value( "t_Close_Date" );
                end;
              end;
            else
              if( ( DP_CrpFindSettAcc( m_IsTemp, {CurDate}, m_RegisterID, set.Value( "t_HolderAccID" ), m_Issuer, m_Currency, true, settAccID ) != 0 )
               or ( ( set2 = CSettAccSet( settAccID ) ) == null ) or not set2.MoveNext()
               or ( ( set3 = CAccountSet( set2.Value( "t_Chapter" ), set2.Value( "t_Account" ), set2.Value( "t_FIID" ) ) ) == null ) or not set3.MoveNext() )
                note = "Не найден счет в " + ПолучитьКодФинИн( m_Currency, err, FICK_ISONUMBER ) + " для перечисления полученной выплаты";
              else
                if( ( set3.Value( "t_Open_Close" ) == "З" ) and ( set3.Value( "t_Close_Date" ) == {CurDate} ) )
                  note = "Лицевой счет " + set2.Value( "t_Account" ) + ", указанный для перечисления выплаты, закрыт. Дата закрытия лицевого счета " + set3.Value( "t_Close_Date" );
                end;
              end;
            end;
          end;
        end;
      end;

      if( set.Value( "t_NRCountry" ) == "" )
        note2 = "Поле \"страна\" не заполнено, требуется указать код страны депонента";
      else
        if( set.Value( "t_Error1" ) == 1 )
          note2 = "Обнаружены расхождения в юридическом статусе в анкете депонента";
        else
          if( set.Value( "t_Error2" ) == 1 )
            note2 = "Обнаружено несоответствие юридического и налогового статусов депонента";
          end;
        end;
      end;

      if( not m_DataExist and ( ( note != "" ) or ( note2 != "" ) ) )
        RegisterTable( "N1_MainTable",
                       "┌──────────────┬────────────┬───────────┬──────────┬───────────┬──────────┬──────────┬─────────────┐\n"
                     + "│ Наименование │ Код анкеты │ Номер СДО │   Дата   │ Номер ДБО │   Дата   │   Дата   │ Комментарий │\n"
                     + "│   депонента  │  депонента │           │ открытия │           │ открытия │ закрытия │             │\n"
                     + "│              │            │           │   СДО    │           │   ДБО    │   ДБО    │             │\n"
                     + "├──────────────┼────────────┼───────────┼──────────┼───────────┼──────────┼──────────┼─────────────┤",
                       "N1_A1", "N1_A2", "N1_A3", "N1_A4", "N1_A5", "N1_A6", "N1_A7", "N1_A8" );
      end;

      if( note != "" )
        m_DataExist = true;

        PrintTableLine( "N1_A1", set.Value( "t_Name" ), null,
                        "N1_A2", set.Value( "t_PartyCode" ), null,
                        "N1_A3", set.Value( "t_SDONumber" ), null,
                        "N1_A4", Date( set.Value( "t_DateConc" ) ), null,
                        "N1_A5", set.Value( "t_DBONumber" ), null,
                        "N1_A6", Date( set.Value( "t_DateBegin" ) ), null,
                        "N1_A7", Date( set.Value( "t_DateClose" ) ), null,
                        "N1_A8", note, null );

        note = "";
      end;

      if( note2 != "" )
        m_DataExist = true;

        PrintTableLine( "N1_A1", set.Value( "t_Name" ), null,
                        "N1_A2", set.Value( "t_PartyCode" ), null,
                        "N1_A3", set.Value( "t_SDONumber" ), null,
                        "N1_A4", Date( set.Value( "t_DateConc" ) ), null,
                        "N1_A5", set.Value( "t_DBONumber" ), null,
                        "N1_A6", Date( set.Value( "t_DateBegin" ) ), null,
                        "N1_A7", Date( set.Value( "t_DateClose" ) ), null,
                        "N1_A8", note2, null );
        note2 = "";
      end;

      if( set.Value( "t_Number" ) == set.Value( "t_Count" ) )
        if( m_DataExist )
          EndTable();
          CopyAllSheetInTotalBook( null, false, GetTableRange(), 0 );
        else
          PrintFormatString( "#", "N1_EmptyDataStr", "Депонентов, у которых отсутствуют счета для выплаты дохода, выявлены расхождения по юридическому и налоговому статусу, не обнаружено" );
          CopyAllSheetInTotalBook( null, false, "N1_EmptyDataStr", 1 );
        end;

        DP_StdAuthPerson_Ex( null, null, this, "N1_" );
        DP_StdDeposInfo_Ex( null, null, 0, this, "N1_" );
        DP_StdExecReport_Ex( null, null, NULL, NULL, 0, this, "N1_" );

        SaveTotalBook();

        RemProgress();
      end;
    end;
  end;

  initDL_CReportTemplate( null, "dprglnprtcl.xls" );
  SetPrintMode( IIF( toExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
end;

private macro SmartSetBuff( dest, src )
  if( ValType( src ) == V_MEMADDR )
    SetBuff( dest, src );
  else
    Copy( dest, src );
  end;
end;

macro DP_ReglinProtocol( isTemp:Bool, _dpregstr:Variant, _dpcorpop:Variant )
  var dpcorpop = TRecHandler( "dpcorpop.dbt" ), dpregstr = TRecHandler( "dpregstr.dbt" );
  var err:Integer = 0;
  var pan = null;
  var currency:Integer = ALLFININSTR;

  SmartSetBuff( dpregstr, _dpregstr );
  SmartSetBuff( dpcorpop, _dpcorpop );

  if( ( dpcorpop.rec.DocumentID != 0 )
  and ( ( currency = dpcorpop.rec.Currency ) != ALLFININSTR )
   or ( ( pan = CReglinProtocolPanel() ) != null )
    and pan.Run()
    and ( ( currency = pan.GetFieldValue( DPRGLNPRTCLPNNF_FICODE ) ) != ALLFININSTR ) )
    CReglinProtocol( isTemp, dpregstr.rec.RegisterID, dpregstr.rec.Issuer, currency, true ).Run();
  end;

  return 0;
end;
