/* -@H------------------------------------------------------------------
*            Автоматизированная банковская система RS-Bank
*                 Библиотека интерпретируемых модулей
*                  Copyright (c) JV R-Style 1993-96
*
*   Description      :  междепозитарный перевод -- зачисление 
*
*----------------------------------------------------------------------*/

import oprinter, SPInter, PaymInter, "dps_com.inc", "dp_util.mac";


file pmpaym("pmpaym") key 10;

macro CalcServiceSum( sfcontr_addr, BeginDate, EndDate )
    var stat;
    var Sum, BnAccount = "";

    record  rContr( sfcontr );
    record  rBaseSum( "sfbassum.str" );
    file depoacnt(depoacnt) key 1;
    SetBuff( rContr, sfcontr_addr );

    /*Перебираем все поручение на междепозитарный перевод -- зачисление*/
    pmpaym.DocKind     = SP_DEPOPER_ENROLMENT;
    pmpaym.ValueDate   = BeginDate;
    pmpaym.Purpose     = PurpBase;
    pmpaym.SubPurpose  = 0;
    stat = GetGE(pmpaym);
    while(  stat and
           (pmpaym.DocKind   == SP_DEPOPER_ENROLMENT) and
           (pmpaym.ValueDate <= EndDate)
         )
       if(pmpaym.PaymStatus == PM_FINISHED)
          BnAccount = GetHeadAccForAcc(pmpaym.ReceiverDpNode);
          if( (pmpaym.Purpose == PurpBase) and 
              (BnAccount == rContr.Object ) /*счет клиента на который зачислены бумаги*/
            )
             Sum = PaymNominal(pmpaym);

             ClearRecord( rBaseSum );
             rBaseSum.baseType    = SF_BASETYPE_SUM;
             rBaseSum.baseType2   = SF_BASETYPE_SUM;
             rBaseSum.baseSum     = Sum;
             rBaseSum.baseSum2    = Sum;

             rBaseSum.BaseObjectType = OBJTYPE_OPERATIONDEPO;
             rBaseSum.BaseObjectID   = GetSpdraftByPayment(pmpaym.PaymentID);

             if( InsertSumList(rBaseSum) )
                stat = FALSE;
                MacroError = 1;
                MsgBox("Ошибка при вставке базовой суммы");
             end;
          end;
       end;
       if( stat )
          stat = Next(pmpaym);
       end;
    end;

    return;
end;
