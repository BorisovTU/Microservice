/*
$Name: importParty.mac
$Module: Депозитарий
$Description: Процедура обновления справочника субъектов по данным ЦБ РФ
*/
import BankInter, PTInter, CTInter, rsexts;
import dlquery, DlReport_h, dlmisc;

private file f() dbf; // в класс не запихнуть

private class TIssuerReportData(name_, fullName_, INN_)
  var name     = name_,
      fullName = fullName_,
      inn      = inn_;
end;

private class TLicenseReportData(name_, fullName_, INN_, license_, startEndDate_ )
  var name         = name_,
      fullName     = fullName_,
      inn          = inn_,
      license      = license_,
      startEndDate = startEndDate_;
end;

private class TDeposReportData(name_, OGRN_, INN_)
  var name     = name_,
      OGRN     = OGRN_,
      inn      = inn_;
end;

private class TMessagesData(nameProc_, INN_, msg_ )
  var nameProc:string = "",
      inn:string      = "",
      msg:string      = ""; // удаляем символ |

  // Удалить символ symb из строки и заменить пробелом, если надо
  private macro RemoveSymbFromMsg(symb:string, msg:string):string
     var retVal:string = "";
     //var sourceStr:string = msg;
     var startPos:integer = 1;

     while( startPos > 0 )
       startPos = StrBrk ( msg, "|" );
       var tmpStr:string = Trim ( SubStr(msg,1,startPos-1) );
       if ( StrLen( tmpStr ) )
         retVal = RetVal + tmpStr + " ";
       end;
       msg = SubStr( msg, startPos+1 );
     end;
     retVal = Trim(RetVal + msg);
     return retVal;
  end;

  private macro Constructor(nameProc_, INN_, msg_)
    nameProc = nameProc_;
    inn      = inn_;
    msg      = RemoveSymbFromMsg("|", msg_); // удаляем символ |
  end;

  Constructor(nameProc_, INN_, msg_);
end;

private class CChangeDialogFlag()
  var OldDialogFlag = SetDialogFlag(0);
  macro Destructor()
    SetDialogFlag(OldDialogFlag);
  end;
end;

class CImport(report_)
  var path:string = ""; // Путь до папки импорта с завершающим \\
  var nameProc:string = ""; // наименование процесса
  var partyID = 0;
  var rep = report_;

  private macro GetErrorInfo(errMsgStart:string):string
   var bankMsg:RSBankMsg = AL_GetErrorInfo();

   if (bankMsg.Stat != 0)
     return errMsgStart + ". " + bankMsg.Message;
   end;
   return errMsgStart;
  end;

  /* Установить псевдоним */
  macro setPseudonim(typeID, name)
    var cmd;

    cmd = RSDCommand("DELETE FROM dpartyname_dbt WHERE t_PartyID = ? AND t_NameTypeID = ?");
    cmd.addParam("", RSDBP_IN, partyID);
    cmd.addParam("", RSDBP_IN, typeID );
    cmd.execute();

    cmd = RSDCommand("INSERT INTO dpartyname_dbt (t_PartyID, t_Name, t_NameTypeID) values(?, ?, ?)");
    cmd.addParam("", RSDBP_IN, partyID);
    cmd.addParam("", RSDBP_IN, name   );
    cmd.addParam("", RSDBP_IN, typeID );
    cmd.execute();

    onError(er)
      rep.arrMsg[rep.arrMsg.size] = TMessagesData(nameProc, f.INN, "Ошибка при обновлении псевдонима субъекта. " + cmd.GetErrorString(er));
  end;

  /* Проверка записи */
  macro checkRec() : bool
    var cmd, dataSet;

    cmd = DL_RSDCommand("SELECT code.t_PartyID "
                      + "  FROM dpartcode_dbt code "
                      + " WHERE  code.t_CodeKind = " + PTCK_INN
                      + "   AND (code.t_Code = ? OR code.t_Code LIKE ?)"
                       );
    cmd.addParam(Trim(f.INN));
    cmd.addParam(Trim(f.INN)+"/%");

    dataSet = cmd.Execute();
    if (dataSet.moveNext())
      partyID = dataSet.PartyID;
    else
      partyID = 0;
    end;

    return (partyID != 0);

    onError(er)
      rep.arrMsg[rep.arrMsg.size] = TMessagesData(nameProc, f.INN, "Ошибка при поиске субъекта по ИНН. " + cmd.GetErrorString(er));
      return false;
  end;

  /* Действие над записью */
  macro processRec() : integer
    return 0;
  end;

  /* Опредлеление количества записей в файле справочнике */
  private macro getCountRecords()
    var i = 0;
    while (next(f))
      i = i + 1;
      Message("Подсчет количества записей");
    end;
    return i;
  end;

  /* Чтение файла */
  macro processFile(fileName:string)
    var i = 0, count = 0;

    open(f, path+fileName);

    count = getCountRecords();
    InitProgress(count, "Обрабатывается справочник " + fileName, nameProc);
    while (next(f))
      var dialogFlag = CChangeDialogFlag();
      if (checkRec())
        processRec();
      end;
      dialogFlag = null;

      i = i + 1;
      UseProgress(i);
    end;
    RemProgress();

    close(f);
  end;

  /* Перенос файла в папку Done */
  macro moveDone(fileName:string)
    var pathDone = path + "Done\\";
    MakeDir(pathDone);

    if (CopyFile(path + fileName, pathDone + fileName))
      RemoveFile(path + fileName);
    else
      msgbox("Ошибка при перемещении файла " + fileName + " в папку " + pathDone);
    end;
  end;

  /* Поиск файлов и обработка */
  macro process(mask:string)
    var dir = TDirList(path+"\\"+mask, "F");
    var i = 0;

    if (dir.Count != 0)
      while (i < dir.Count)
        processFile(dir.name(i));
        // Переносим файл в папку Done
        moveDone(dir.name(i));
        i = i + 1;
      end;
    else
      return -1; // Пустая папка
    end;

    return 0;
  end;
end;

/****************************************************/
/*       Обновление наименования эмитентов          */
/****************************************************/
class (CImport)CImportINN(report_)
  /* Действие над записью */
  macro processRec() : integer
    var INNData = TIssuerReportData(Trim(f.NAIM), Trim(f.PNAIM), Trim(f.INN));
    var INN = "", KPP = "", KPPCBR = Trim(f.OPF);

    setPseudonim(PTKN_BANKNAME,      INNData.FullName);
    setPseudonim(PTKN_SHORTBANKNAME, INNData.Name    );

    SplitFullINN(ПолучитьКодСубъекта(partyID, PTCK_INN), INN, KPP);
    if(KPP != KPPCBR)
      rep.arrMsg[rep.arrMsg.size] = TMessagesData(nameProc, INNData.INN, "Error: КПП не совпадают: В справочнике Securities - " + IIF(KPP != "", KPP, "не указан") + ", в справочнике ЦБ - " + IIF(KPPCBR != "", KPPCBR, "не указан"));
    end;

    rep.arrINNData[rep.arrINNData.size] = INNData;

    onError(er)
      rep.arrMsg[rep.arrMsg.size] = TMessagesData(nameProc, INNData.INN, er.Message);
  end;

  /* Чтение пути справочников ИНН */
  macro getPath() : bool
    var err = 0;
    GetRegistryValue("DEPO\\INNCB_IMPORT", V_STRING, path, err);
    path = path + "\\";
    return (err==0);
  end;

  macro process()
    return process("INN*.dbf");
  end;

  initCImport(report_);
  nameProc = "Обновление эмитентов";
end;
/****************************************************/
/*              Обновление лицензий                 */
/****************************************************/
class (CImport)CImportLIZ(report_)
  var arrFinisheRep = TArray();
  private macro isDepositary(TIP:string):bool
    return ((StrLwr(TIP) == "депозитарий") or (StrLwr(TIP) == "депозитарная"));
  end;
  private macro isRegistrator(TIP:string):bool
    return (StrLwr(TIP) == "регистраторы");
  end;
  private macro isSpecDepositary(TIP:string):bool
    return ((StrLwr(TIP) == "спец.депозитарий") or (StrLwr(TIP) == "специализированный депозитарий") or (StrLwr(TIP) == "спец. депозитарий"));
  end;

  /* Депозитарная лицензия?*/
  private macro getKindDoc(TIP:string, kindDoc:@integer, kindCode:@integer):integer
    if (isDepositary(TIP))
      kindDoc = OBJKDOC_LICEN_DEPOSITARY;
      kindCode = PTCK_DEPOREGN;
    elif (isRegistrator(TIP))
      kindDoc = OBJKDOC_LICEN_DEPOSITARY;
      kindCode = PTCK_DEPOREGN;
    elif (isSpecDepositary(TIP))
      kindDoc = OBJKDOC_SPEC_DEPO_LICENSE;
      kindCode = PTCK_SPECDEPLICENSE;
    end;
  end;

  /* Действие над записью */
  macro processRec() : integer
    var objParty = RsbParty(partyID);
    var doc = null;
    var LIZ = Trim(f.LIZ),
        TIP = Trim(f.TIP),
        SROKD = Trim(f.SROKD);
    var kindDoc = 0,
        kindCode = 0;
    var isChanged = false,
        isFinished = false;

    getKindDoc(TIP, @kindDoc, @kindCode);

    doc = objParty.PartyRegDoc(REGPT_FKCB, kindDoc);

    var codeChanges = (objParty.Code.GetCode(kindCode) != LIZ),
        regDocChanges = codeChanges;// Рег номер документа берется из кода! (LIZ != doc.RegNumber);
    // закрываем код и лицензию, если есть
    if ((objParty.Code.GetCode(kindCode) != "") and codeChanges)
      objParty.Code.CloseCode(kindCode, Date(f.DATAV)-1);
    end;
    if ((doc.FinishDate == Date(0,0,0)) and (doc.IsEmpty == false) and (regDocChanges))
      doc.FinishDate = Date(f.DATAV)-1;
      doc.IsClosed = true;
      isChanged = true;
    end;
    // Открываем новый
    if (codeChanges)
      objParty.Code.SetCode(kindCode, LIZ, Date(f.DATAV));
    end;
    // Лицензия
    if (regDocChanges)
      if (doc.IsEmpty)
        isChanged = true;
        //doc.RegNumber = LIZ;// Номер лицензии вставляется как доп. код
        doc.StartDate = Date(f.DATAV);
        doc.DocDate   = Date(f.DATAV);
      else
        if (doc.Add(REGPT_FKCB, kindDoc, Date(f.DATAV)))
          doc.StartDate = Date(f.DATAV);
          isChanged = true;
        else
          rep.arrMsg[rep.arrMsg.size] = TMessagesData(nameProc, f.INN, GetErrorInfo("Ошибка при созданиии нового регистрационного документа"));
        end;
      end;
    end;

    // для депозитарной еще и 17 код используется
    if ((isRegistrator(TIP) or isDepositary(TIP)))
      if ((objParty.Code(PTCK_ORCBLICENSE) != "") and (objParty.Code(PTCK_ORCBLICENSE) != LIZ))
        objParty.Code.CloseCode(PTCK_ORCBLICENSE, Date(f.DATAV)-1);
      end;
      objParty.Code.SetCode(PTCK_ORCBLICENSE, LIZ, Date(f.DATAV));
    end;

    if (objParty.IsChanged or isChanged)
      rep.arrLIZData[rep.arrLIZData.size] = TLicenseReportData(Trim(f.NAIM), Trim(f.NAIMP), Trim(f.INN), LIZ, Date(f.DATAV));
      if (not objParty.Update())
        rep.arrMsg[rep.arrMsg.size] = TMessagesData(nameProc, f.INN, GetErrorInfo("Ошибка при обновлении субъекта"));
      end;
    end;
    // Аннулирование!
    isChanged = false;
    if (Index(StrLwr(SROKD), "аннулиров") != 0) // обрезанная проверка, ибо есть запись со строкой "Аннулирова на 18.12.2012"
      var ind = StrLen(SROKD) - 9;
      // закрываем код и лицензию
      var finishDate = Date(SubStr(SROKD, ind));
      // код
      if (objParty.Code.GetCode(kindCode) != "")
        isChanged = true;
        objParty.Code.CloseCode(kindCode, finishDate);
      end;
      // лицензия
      if (not doc.IsEmpty)
        isChanged = true;
        doc.FinishDate = finishDate;
        doc.IsClosed = true;
      end;
      // для депозитарной еще и 17 код используется
      if ((isRegistrator(TIP) or isDepositary(TIP)) and (objParty.Code(PTCK_ORCBLICENSE) != ""))
        objParty.Code.CloseCode(PTCK_ORCBLICENSE, finishDate);
        isChanged = true;
      end;

      if (isChanged)
        rep.arrFinLIZData[rep.arrFinLIZData.size] = TLicenseReportData(Trim(f.NAIM), Trim(f.NAIMP), Trim(f.INN), LIZ, finishDate);
        if (not objParty.Update())
          rep.arrMsg[rep.arrMsg.size] = TMessagesData(nameProc, f.INN, GetErrorInfo("Ошибка при обновлении субъекта"));
        end;
      end;
    end;

    onError(er)
      rep.arrMsg[rep.arrMsg.size] = TMessagesData(nameProc, f.INN, er.Message);
  end;

  /* Чтение пути справочников */
  macro getPath() : bool
    var err = 0;
    GetRegistryValue("DEPO\\LIZCB_IMPORT", V_STRING, path, err);
    path = path + "\\";
    return (err==0);
  end;

  macro process()
    return process("LIZ*.dbf");
  end;

  initCImport(report_);
  nameProc = "Обновление лицензий";
end;

/****************************************************/
/*   Обновление наименования и ОГРН депозитариев    */
/****************************************************/
class (CImport)CImportSDEP(report_)
  var CurrFileDate;
  var partyown = TBFile("partyown");

  private macro CheckPartyKind( PartyID, PartyKind )
    var isPartyKind = false;

    partyown.rec.PartyID   = PartyID;
    partyown.rec.PartyKind = PartyKind;
    if(GetEQ(partyown))
      isPartyKind = true;
    end;

    return isPartyKind;
  end;

  /* Действие над записью */
  macro processRec() : integer
    var objParty = null;
    var doc = null;
    var OGRNCode;
    var isChanged = false;

    if((f.CE_DATE >= CurrFileDate) AND (CheckPartyKind( partyID, PTK_DEPOSITORY )) ) // Если конец действия строки в файле старше даты составления файла или это не депозитарий, то не обновляем
      // Сначала обновляем псевдонимы субъекта
      setPseudonim(PTKN_BANKNAME, Trim(f.NAME));

      // Обновляем ОГРН
      objParty = RsbParty(partyID);

      doc = objParty.PartyRegDoc(REGPT_TAXINSTITUTE, OBJKDOC_EVIDENCE_GOS_REG);

      OGRNCode = objParty.Code.GetCode(PTCK_OGRN);

      if( OGRNCode == "") // ОГРН нет, добавляем
        objParty.Code.SetCode(PTCK_OGRN, Trim(f.OGRN), {curdate});

        if(doc.IsEmpty)
          isChanged = true;
          doc.StartDate = {curdate};
          doc.DocDate   = {curdate};
          doc.IsMain = true;
        else
          if(doc.Add(REGPT_TAXINSTITUTE, OBJKDOC_EVIDENCE_GOS_REG, {curdate}))
            doc.StartDate = {curdate};
            doc.IsMain = true;
            isChanged = true;
          else
            rep.arrMsg[rep.arrMsg.size] = TMessagesData(nameProc, f.INN, GetErrorInfo("Ошибка при созданиии нового регистрационного документа"));
          end;
        end;
      elif(OGRNCode != Trim(f.OGRN)) // ОГРН указан и он не совпадает - это ошибка
        rep.arrMsg[rep.arrMsg.size] = TMessagesData(nameProc, f.INN, "В справочнике Securities указан ОГРН: " + OGRNCode + ", в справочнике ЦБ: " + Trim(f.OGRN));
      else // ОГРН указан и совпадает - ничего не делаем
        ;
      end;

      if(isChanged)
        if (not objParty.Update())
          rep.arrMsg[rep.arrMsg.size] = TMessagesData(nameProc, f.INN, GetErrorInfo("Ошибка при обновлении субъекта"));
        end;
      end;

      rep.arrDEPOSData[rep.arrDEPOSData.size] = TDeposReportData(Trim(f.NAME), Trim(f.OGRN), Trim(f.INN));
    end;

    onError(er)
      rep.arrMsg[rep.arrMsg.size] = TMessagesData(nameProc, f.INN, er.Message);
  end;

  /* Чтение пути справочников ИНН */
  macro getPath() : bool
    var err = 0;
    GetRegistryValue("DEPO\\SDEPO_IMPORT", V_STRING, path, err);
    path = path + "\\";
    return (err==0);
  end;

  macro process()
    return process("SDEP*.dbf");
  end;

  macro processFile(fileName:string)
    CurrFileDate = Date("01"+(substr(fileName, 5, 4))); // Дату начала действия данных файла считаем с 1 числа месяца и года в имени файла

    return processFile(fileName);
  end;

  initCImport(report_);
  nameProc = "Обновление депозитариев";
end;

class (DL_CReportTemplate) CReportImportParty()
  var arrINNData:TArray    = TArray(),
      arrLIZData:TArray    = TArray(),
      arrFinLIZData:TArray = TArray(),
      arrDEPOSData:TArray    = TArray(),
      arrMsg:TArray        = TArray();

  private macro PrintMode():integer
    return DL_OUTREPORT_EXCEL;
  end;
  private macro BeginReport( NumCopy:INTEGER )
    CreateTotalBook();
  end;

  private macro EndReport()
    SaveTotalBook();
  end;

  private macro Footer(SheetName, prefix)
    PrintFormatString(NULL,
                     prefix+"ExecutorName:c", {Name_Oper},
                     prefix+"Sign_ExecDate:l", "    ___________________      " + String({curdate})
                     );
    CopyAllSheetInTotalBook( SheetName, false, prefix+"Footer", 0, SheetName );
  end;

  macro INN()
    const SheetName = "Импорт эмитентов";
    var i = 0;

    if (arrINNData.size != 0)
      InitProgress(arrINNData.size, "Формирование листа \""+SheetName+"\"", SheetName);

      for (var data, arrINNData)
        if(i == 0)
          SetActiveSheet( SheetName );

          UseNewSheetInTotalBook();

          CopyAllSheetInTotalBook( SheetName, false, "N1_Header", 0, SheetName );

          CopyAllSheetInTotalBook( SheetName, false, "N1_TableHeader", 0, SheetName );
          RegisterTable( "N1_TableRec", "",
                         "N1_Name",
                         "N1_FullName",
                         "N1_INN"
                       );
        end;

        i = i + 1;
        PrintTableLine( "N1_Name",     data.Name,     null,
                        "N1_FullName", data.FullName, null,
                        "N1_INN",      data.INN,      null
                      );

        UseProgress(i);
      end;

      RemProgress();
      EndTable();
      CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );

      Footer(SheetName, "N1_");
    end;
  end;

  macro LIZ()
    const SheetName = "Импорт лицензий";
    var i = 0;

    if (arrLIZData.size != 0)
      InitProgress(arrLIZData.size, "Формирование листа \""+SheetName+"\"", SheetName);

      for (var data, arrLIZData)
        if(i == 0)
          SetActiveSheet( SheetName );

          UseNewSheetInTotalBook();

          CopyAllSheetInTotalBook( SheetName, false, "N2_Header", 0, SheetName );

          CopyAllSheetInTotalBook( SheetName, false, "N2_TableHeader", 0, SheetName );
          RegisterTable( "N2_TableRec", "",
                         "N2_Name",
                         "N2_FullName",
                         "N2_INN",
                         "N2_License",
                         "N2_StartDate"
                       );
        end;

        i = i + 1;
        PrintTableLine( "N2_Name",      data.Name,         null,
                        "N2_FullName",  data.FullName,     null,
                        "N2_INN",       data.INN,          null,
                        "N2_License",   data.License,      null,
                        "N2_StartDate", data.StartEndDate, null
                      );

        UseProgress(i);
      end;

      RemProgress();
      EndTable();
      CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );

      Footer(SheetName, "N2_");
    end;
  end;

  macro FinisheLIZ()
    const SheetName = "Аннулированные лицензии";
    var i = 0;

    if (arrFinLIZData.size != 0)
      InitProgress(arrFinLIZData.size, "Формирование листа \""+SheetName+"\"", SheetName);

      for (var data, arrFinLIZData)
        if(i == 0)
          SetActiveSheet( SheetName );

          UseNewSheetInTotalBook();

          CopyAllSheetInTotalBook( SheetName, false, "N3_Header", 0, SheetName );

          CopyAllSheetInTotalBook( SheetName, false, "N3_TableHeader", 0, SheetName );
          RegisterTable( "N3_TableRec", "",
                         "N3_Name",
                         "N3_FullName",
                         "N3_INN",
                         "N3_License",
                         "N3_EndDate"
                       );
        end;

        i = i + 1;
        PrintTableLine( "N3_Name",      data.Name,         null,
                        "N3_FullName",  data.FullName,     null,
                        "N3_INN",       data.INN,          null,
                        "N3_License",   data.License,      null,
                        "N3_StartDate", data.StartEndDate, null
                      );

        UseProgress(i);
      end;

      RemProgress();
      EndTable();
      CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );

      Footer(SheetName, "N3_");
    end;
  end;

  macro DEPOS()
    const SheetName = "Импорт депозитариев";
    var i = 0;

    if (arrDEPOSData.size != 0)
      InitProgress(arrDEPOSData.size, "Формирование листа \""+SheetName+"\"", SheetName);

      for (var data, arrDEPOSData)
        if(i == 0)
          SetActiveSheet( SheetName );

          UseNewSheetInTotalBook();

          CopyAllSheetInTotalBook( SheetName, false, "N4_Header", 0, SheetName );

          CopyAllSheetInTotalBook( SheetName, false, "N4_TableHeader", 0, SheetName );
          RegisterTable( "N4_TableRec", "",
                         "N4_Name",
                         "N4_OGRN",
                         "N4_INN"
                       );
        end;

        i = i + 1;
        PrintTableLine( "N4_Name",     data.Name,     null,
                        "N4_OGRN",     data.OGRN,     null,
                        "N4_INN",      data.INN,      null
                      );

        UseProgress(i);
      end;

      RemProgress();
      EndTable();
      CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );

      Footer(SheetName, "N4_");
    end;
  end;

  private macro Messages()
    const SheetName = "Сообщения";
    var i = 0;

    if (arrMsg.size != 0)
      InitProgress(arrMsg.size, "Формирование листа \""+SheetName+"\"", SheetName);

      for (var data, arrMsg)
        if(i == 0)
          SetActiveSheet( SheetName );

          UseNewSheetInTotalBook();

          CopyAllSheetInTotalBook( SheetName, false, "MSG_TableHeader", 0, SheetName );
          RegisterTable( "MSG_TableRec", "",
                         "MSG_TypeProc",
                         "MSG_INN",
                         "MSG_Message"
                       );
        end;

        i = i + 1;
        PrintTableLine( "MSG_TypeProc", data.nameProc, null,
                        "MSG_INN",      data.INN,      null,
                        "MSG_Message",  data.Msg,      null
                      );

        UseProgress(i);
      end;
      RemProgress();
      EndTable();
      CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );
    end;
  end;

  private macro Create()
    INN();
    LIZ();
    FinisheLIZ();
    DEPOS();
    Messages();
  end;

  macro isEmpty()
    return ((arrINNData.size == 0) and (arrLIZData.size == 0) and (arrFinLIZData.size == 0) and (arrDEPOSData.size == 0) and (arrMsg.size == 0));
  end;

  initDL_CReportTemplate( NULL, "Report_ImportParty.xls" );
end;

/* Вход */
macro ImportParty(impIssuer:bool, impLicense:bool, impDepositary:bool)
  var impINN = null;
  var impLIZ = null;
  var impSDEP = null;
  var report = CReportImportParty();
  var statINN = -1, statLIZ = -1, statSDEP = -1;

  if (impIssuer)
    impINN = CImportINN(report);
    if (not impINN.getPath())
      msgbox("Ошибка при определении настройки DEPO\\INNCB_IMPORT. Справочник не загружается");
      impINN = null;
    end;
  end;

  if (impLicense)
    impLIZ = CImportLIZ(report);
    if (not impLIZ.getPath())
      msgbox("Ошибка при определении настройки DEPO\\LIZCB_IMPORT. Справочник не загружается");
      impLIZ = null;
    end;
  end;

  if (impDepositary)
    impSDEP = CImportSDEP(report);
    if (not impSDEP.getPath())
      msgbox("Ошибка при определении настройки DEPO\\SDEPO_IMPORT. Справочник не загружается");
      impSDEP = null;
    end;
  end;

  if (impINN != null)
    statINN = impINN.process();
  end;

  if (impLIZ != null)
    statLIZ = impLIZ.process();
  end;

  if (impSDEP != null)
    statSDEP = impSDEP.process();
  end;

  if ((statINN == -1) and (statLIZ == -1) and (statSDEP == -1))
    msgbox("Папки импорта пусты.");
  end;

  if (not report.isEmpty())
    report.Run();
  end;
end;
