/*
$Name:        dpcldepo.mac
$Module:      Депозитарий
$Description: Печать отчета "Журнал принятых поручений"
*/

import RsbDataSet;
import FIInter, CurrInter, CTInter, DPInter, secinter, deposerv;
import "spserv.mac";

var NameAlg  = TBFile("namealg");
var llvalues = TBFile("llvalues");
var party    = TBFile("party");

const  SP_DEPOPER_ADMOPER       = 825;//административная операция депо
const  SP_DEPOPER_INFOPER       = 830;//информационная операция депо

//статусы поручений (переданные как параметры отчета)
const  DP_DEPO_STATUS_ALL      = 0; //все
const  DP_DEPO_STATUS_REG      = 1; //зарегистрированные
const  DP_DEPO_STATUS_PREP     = 2; //отложенные
const  DP_DEPO_STATUS_OPEN     = 3; //открытые
const  DP_DEPO_STATUS_READY    = 4; //исполненные
const  DP_DEPO_STATUS_REJECT   = 5; //отказано в исполнении

//статусы
const SC_SPDRAFT_STATUS_PREP  = 1;
const SC_SPDRAFT_STATUS_OPEN  = 2;
const SC_SPDRAFT_STATUS_CLOSE = 3;

const SC_SPDRAFT_STATUS_READY  = 0; //исполнено
const SC_SPDRAFT_STATUS_REJECT = 1; //отказано в исполнении

//виды операций депозитария
const DP_INVENT_OPERATION    = 0;//инвентарные
const DP_ADM_OPERATION       = 1;//административные
const DP_INFORM_OPERATION    = 2;//информационные
const DP_GLOB_OPERATION      = 3;//глобальные

//виды объектов для notetext
const OBJECT_INVENT = 104;
const OBJECT_INFORM = 105;
const OBJECT_GLOBOP = 111;

//номер алгоритма со статусами
const ALG_DEPO_STATUS_DOC = 3240;
//номер алгоритма с видами журналов
const ALG_DEPO_DRAFTREFKIND = 3252;

//пустая строка в Oracle
const EMPTY_STRING = 1;

const TA_STAT_DEL = 152;

var DataSet;
var _is_ap;

var HeadTable = "┌─────┬──────────┬──────────────┬────────────┬──────────┬───────────────────────────┬─────────────────────────────┬───────────────────────┬──────────────────┬─────────────────────┬────────────────────────────────┐\n"+
                "│  №  │ Входящий │              │ Дата/время │  Способ  │                           │            Депонент         │ Инициатор/Отправитель │      Статус      │    № операции,      │           Примечания.          │\n"+
                "│ п/п │  номер   │  По журналу  │   приема   │ доставки │    Реквизиты документа    ├──────────────┬──────────────┤                       │     документа    │       дата          │     (документы-приложения,     │\n"+
                "│     │          │              │            │          │                           │    Код       │     ФИО/     │                       │                  │    исполнения       │  причина отказа в исполнении)  │\n"+
                "│     │          │              │            │          │                           │              │ Наименование │                       │                  │                     │                                │\n"+
                "├─────┼──────────┼──────────────┼────────────┼──────────┼───────────────────────────┼──────────────┼──────────────┼───────────────────────┼──────────────────┼─────────────────────┼────────────────────────────────┤\n"+
                "│  1  │    2     │       3      │      4     │    5     │            6              │      7       │       8      │           9           │         10       │          11         │                12              │\n"+
                "├─────┼──────────┼──────────────┼────────────┼──────────┼───────────────────────────┼──────────────┼──────────────┼───────────────────────┼──────────────────┼─────────────────────┼────────────────────────────────┤";

var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

/*интерфейс в Namealg.dbt*/
macro __NameAlg(Type, Number)
  NameAlg.rec.iTypeAlg = Type;
  NameAlg.rec.iNumberAlg = Number;
  if(NameAlg.GetEQ)
    return NameAlg.rec.szNameAlg;
  end;
  return NULL;
end;

macro __LLValues( List, Number)
  llvalues.rec.List = List;
  llvalues.rec.Element = Number;
  if(llvalues.GetEQ)
    return llvalues.rec.Name;
  end;
  return NULL;
end;

macro FillReport( DeliveryKind )
  file typeac("typeac");

  typeac.iNumType = TA_STAT_DEL;
  typeac.Type_Account = DeliveryKind;
  if( getEQ(typeac) )
     return typeac.Name_type;
  end;

  return NULL;
end;

macro Person( Oper )
  file person("person");
  person.Oper = Oper;
  if( getEQ(person) )
     return person.Name;
  end;

  return NULL;
end;

macro KindOperation(SourceDocKind)

   if(    (SourceDocKind == SP_DEPOPER_BOOKORDER)
       or (SourceDocKind == SP_DEPOPER_TRANSFERORDER)
       or (SourceDocKind == SP_DEPOPER_ENROLMENT)
       or (SourceDocKind == SP_DEPOPER_RECORDER)
       or (SourceDocKind == SP_DEPOPER_WITHDRORDER) )

       return DP_INVENT_OPERATION;

   elif(   (SourceDocKind == DP_DEPOPER_CONVERT)
        or (SourceDocKind == DP_DEPOPER_REPAY)
        or (SourceDocKind == DP_DEPOPER_BONEMISS))

       return DP_GLOB_OPERATION;
   elif(SourceDocKind == SP_DEPOPER_ADMOPER)
       return DP_ADM_OPERATION;
   elif(SourceDocKind == SP_DEPOPER_INFOPER)
       return DP_INFORM_OPERATION;
   end;

   return -1;
end;

macro DPGetSqlString( iDepart:integer, Kind_doc_Code:integer,
                    Sender_doc_Code:integer,
                    Number_doc_beg:string,
                    Number_doc_end:string,
                    UserLog:integer,
                    Date_doc_beg:date,
                    Date_doc_end:date,
                    iKind_send_doc:integer,
                    iPostman:integer,
                    Get_man_Code:integer
                    )

   var sQuery = " SELECT "
                  + " spgr.* "
              + " FROM "
                  + " dspground_dbt spgr "
                 + " ,dv_spgrounddata grdata "
              + " WHERE "
                  + " spgr.t_BackOffice = 'Z' "
              + " AND spgr.t_Department = " + iDepart + " "
              + " AND spgr.t_Direction = " + ENTERING + " "
              + " AND grdata.t_SpGroundID = spgr.t_SpGroundID "
              + " AND grdata.t_IsFirstDoc = 'X' ";

   if( Sender_doc_Code > -1)
      sQuery = sQuery +
                " AND spgr.t_Party = " + Sender_doc_Code + " ";
   end;

   if( Kind_doc_Code != 0 )
     sQuery = sQuery +
                " AND spgr.t_Kind = " + Kind_doc_Code + " ";
   end;

   if( Date_doc_beg <= {curdate} )
     sQuery = sQuery +
                " AND spgr.t_RegistrDate >= " + GetSQLDate( Date_doc_beg ) + " ";
   end;

   if( Date_doc_end <= {curdate} )
     sQuery = sQuery +
                " AND spgr.t_RegistrDate <= " + GetSQLDate( Date_doc_end ) + " ";
   end;

   if( Number_doc_beg )
     sQuery = sQuery +
                " AND spgr.t_XLD >= " + GetSQLString( Number_doc_beg ) + " ";
   end;

   if( Number_doc_beg )
     sQuery = sQuery +
                " AND spgr.t_XLD <= " + GetSQLString( Number_doc_end ) + " ";
   end;

   if( iKind_send_doc > 0)
     sQuery = sQuery +
                " AND spgr.t_DeliveryKind = " + iKind_send_doc + " ";
   end;

   if( iPostman > -1 )
     sQuery = sQuery +
                " AND spgr.t_Proxy = " + iPostman + " ";
   end;

   if( Get_man_Code > 0)
     sQuery = sQuery +
                " AND spgr.t_Receptionist = " + Get_man_Code + " ";
   end;

   if( UserLog )
     sQuery = sQuery +
                " AND spgr.t_UserLog = " + UserLog + " ";
   end;

   sQuery = sQuery +
                " ORDER BY "
                  + " spgr.t_SpGroundID ";

   return sQuery;
end;

macro PrintHeader(  iStatus:integer,
                    Kind_doc_Code:integer,
                    Sender_doc_Code:integer,
                    Number_doc_beg:string,
                    Number_doc_end:string,
                    UserLog:integer,
                    Date_doc_beg:date,
                    Date_doc_end:date,
                    sKind_send_doc:string,
                    iPostman:integer,
                    Get_man_Code:integer )
   var HeaderStr;
   var Doc_Name;
   var Status_str;
   var Deliv_Name;
   var Pers_Name;
   HeaderStr = "Ж У Р Н А Л  П Р И Н Я Т Ы Х  П О Р У Ч Е Н И Й";
   DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, HeaderStr, 0, 0, false, null, null, tablewidth );
   DP_AddPrintCell( Rep,  "Установки фильтра", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
   Rep.AddStr();
   if( Number_doc_beg  and (Number_doc_end) )
     DP_AddPrintCell( Rep,  "диапазон входящих номеров:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  Number_doc_beg+" - "+Number_doc_end, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if(UserLog)
     DP_AddPrintCell( Rep,  "по журналу:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  __NameAlg( ALG_DEPO_DRAFTREFKIND, DataSet.UserLog ), tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( Date_doc_beg <= {curdate} )
     DP_AddPrintCell( Rep,  "период приёма:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  Date_doc_beg, 10, 0, "l:f" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  " - " + Date_doc_end, 20, 0, "l:f" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( iStatus > 0 )
     Status_str = __NameAlg( ALG_DEPO_STATUS_DOC, iStatus );
     DP_AddPrintCell( Rep,  "статус документа:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  Status_str, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( Kind_doc_Code > 0 )
     Doc_Name = __LLValues( 1050, Kind_doc_Code );
     DP_AddPrintCell( Rep,  "вид документа:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  Kind_doc_Code, 4, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  "  " + Doc_Name, tablewidth-34, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( Sender_doc_Code > -1 )
     ПолучитьСубъекта(Sender_doc_Code, party);
     DP_AddPrintCell( Rep,  "отправитель:", 30, 0, "l:" + RepFontStyleTitel,_is_ap,  REP_ELEM_STR );
     DP_AddPrintCell( Rep,  party.rec.Name, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( sKind_send_doc != "" )
     if( (Deliv_Name = FillReport(  StrFor(int(sKind_send_doc)) )) != NULL )
       DP_AddPrintCell( Rep,  "способ доставки:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
       DP_AddPrintCell( Rep,  Deliv_Name, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
       Rep.AddStr();
     end;
   end;

   if( iPostman > -1 )
     ПолучитьСубъекта(iPostman, party);
     DP_AddPrintCell( Rep,  "доставил:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  party.rec.Name, tablewidth-30, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;

   if( Get_man_Code > 0 )
     Pers_Name = Person( Get_man_Code );
     DP_AddPrintCell( Rep,  "принял:", 30, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  "" + Get_man_Code, 6, 0, "l:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  " " + Pers_Name, tablewidth-36, 0, "l:w:" + RepFontStyleTitel, _is_ap, REP_ELEM_STR );
     Rep.AddStr();
   end;
   Rep.AddEmptyStr();
end;

macro PrintStr( count, XiD, UserLog, RegistrDate, RegistrTime,
                DeliveryKind, Kind, AltXLD, doc_date:date,
                Party_Code, op_status, SourceDocID, SourceDocKind, deponent_ID, subject_ID )
   file splog("splogopr");
   file draft("spdraft");
   file admo("depoadmo");
   file spground("spground");
   var notetext = TBFile("notetext");
   var sDelivery;
   var sKind;
   var Party_Name;
   var Status_str;
   var date_time;
   var doc_str;
   var miniQuery, miniData = 0;
   var numbers_str = "";
   var dp_str = "";
   var sKind_spgr;
   var ObjType;
   var text;
   var stat;
   var UserLogName = "";
   var deponentID = "";
   var deponent = "";

   if( DeliveryKind > 0 )
     sDelivery = FillReport( StrFor(DeliveryKind) );
   else
     sDelivery = "";
   end;
//строка в колонку №5
   if( Kind >= 0 )
     sKind = __LLValues( 1050, Kind );
   else
     sKind = "";
   end;

   if( CodeFor(AltXLD) == EMPTY_STRING )
     AltXLD = "";
   end;

   doc_str = string(sKind)+" №"+string(AltXLD)+" от "+string(doc_date:f);
//////
   if( Party_Code > -1 )
     if( ПолучитьСубъекта(Party_Code, party) == 0 )
       Party_Name = party.rec.Name;
     end;
   else
     Party_Name = "";
   end;

   date_time = (string(date(RegistrDate):f)+" "+string(time(RegistrTime)));

   //статус тут будет всегда, потому что задается вручную
   Status_str = __NameAlg( ALG_DEPO_STATUS_DOC, op_status );

//формирование строки в колонку №8
   if( SourceDocID > 0 )

       //инвентарные
       if( KindOperation(DataSet.SourceDocKind) == DP_INVENT_OPERATION )
         miniQuery =   " select dr.t_OprXid, pm.t_ValueDate "
                     + "   from dspdraft_dbt dr, dspdrmove_dbt dm, dpmpaym_dbt pm"
                     + "  where dr.t_SPgroundID = " + DataSet.SPgroundID
                     + "    and dm.t_DraftID = dr.t_AutoKey "
                     + "    and pm.t_PaymentID = dm.t_PaymentID ";
         miniData = TRsbDataSet(miniQuery);
         while(miniData.MoveNext())
           if( miniData.OprXID != "" )
             numbers_str = numbers_str+"№"+miniData.OprXID+" от "+string(date(miniData.ValueDate):f)+"\n";
           end;
         end;
       elif(KindOperation(DataSet.SourceDocKind) == DP_GLOB_OPERATION)
         miniQuery =   " select t_OprXid, t_OprDate "
                     + "   from ddpcorpop_dbt"
                     + "  where t_SPgroundID = " + DataSet.SPgroundID
                     + "    and t_DocKind in (" + DP_DEPOPER_CONVERT + ", " + DP_DEPOPER_REPAY + ", " + DP_DEPOPER_BONEMISS + ") ";
         miniData = TRsbDataSet(miniQuery);
         while(miniData.MoveNext())
           if( miniData.OprXID != "" )
             numbers_str = numbers_str+"№"+miniData.OprXID+" от "+string(date(miniData.OprDate):f)+"\n";
           end;
         end;
       end;

       //Формируем строку SQL запроса
       //Ищем административные операции
       miniQuery = " select t_OprXid, t_OperDate"
                   " from ddepoadmo_dbt"
                   " where t_Ground = "+DataSet.SPgroundID+"";

       miniData = TRsbDataSet(miniQuery);//получаем выборку из spground.dbt
       while(miniData.MoveNext())
         if( CodeFor(miniData.OprXID) != EMPTY_STRING )
           numbers_str = numbers_str+"№"+miniData.OprXID+" от "+string(date(miniData.OperDate):f)+"\n";
         end;
       end;

      //Формируем строку SQL запроса
      //Ищем информационные операции
       miniQuery = " select t_OprXid, t_CarryDate"
                   " from dsplogopr_dbt"
                   " where t_Draft = "+DataSet.SPgroundID+"";

       miniData = TRsbDataSet(miniQuery);//получаем выборку из spground.dbt
       while(miniData.MoveNext())
         if( CodeFor(miniData.OprXID) != EMPTY_STRING )
           numbers_str = numbers_str+"№"+miniData.OprXID+" от "+string(date(miniData.CarryDate):f)+"\n";
         end;
       end;
   end;

//формирование строки в колонку №9
   if( SourceDocID > 0 )
     miniQuery =  " select spgr.t_Kind, spgr.t_Xld, spgr.t_AltXld, spgr.t_SignedDate "
                + "   from dspgrdoc_dbt grdoc, dspground_dbt spgr "
                + "  where grdoc.t_SourceDocKind = "+SourceDocKind
                + "    and grdoc.t_SourceDocID = "+SourceDocID
                + "    and spgr.t_SPgroundID = grdoc.t_SPgroundID "
                + "    and spgr.t_Direction <> " + EXITING
                + " order by grdoc.t_SpgroundID";

     miniData = TRsbDataSet(miniQuery);

     while(miniData.MoveNext())
       if( miniData.Kind >= 0 )
        sKind_spgr = __LLValues( 1050, miniData.Kind );
       else
        sKind_spgr = "";
       end;
       dp_str = dp_str+"вх. №"+string(miniData.Xld)+" "+sKind_spgr+" №"
                +string(miniData.AltXld)+" от "+string(date(miniData.SignedDate):f)+"\n";
     end;

     if( op_status == DP_DEPO_STATUS_REJECT )
        if( KindOperation(DataSet.SourceDocKind) == DP_INVENT_OPERATION )
           ObjType = OBJECT_INVENT;
        elif(KindOperation(DataSet.SourceDocKind) == DP_GLOB_OPERATION)
           ObjType = OBJECT_GLOBOP;
        elif(KindOperation(DataSet.SourceDocKind) == DP_INFORM_OPERATION)
           ObjType = OBJECT_INFORM;
        end;
        if( ObjType == OBJECT_GLOBOP )
          text = readNoteForObject( ObjType, string(SourceDocID), 2);
        else
          text = readNoteForObject( ObjType, string(SourceDocID), 3);
        end;

        if( text  )
          dp_str = dp_str+"Причина отказа: "+text;
        end;
     end;
   end;

   UserLogName = __NameAlg( ALG_DEPO_DRAFTREFKIND, UserLog );

   if( deponent_ID > 0 )
     deponentID = ПолучитьКодСубъекта(deponent_ID,1);
     deponent = ПолучитьКороткоеИмяСубъекта(deponent_ID);
   else
     if(subject_ID > 0)
       deponentID = ПолучитьКодСубъекта(subject_ID,1);
       deponent = ПолучитьКороткоеИмяСубъекта(subject_ID);
     else
       deponentID = "";
       deponent = "" ;
     end;
   end;

//печатаем строку
   DP_AddPrintCell( Rep, count, 0, 0, "c:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, XiD, 0, 0, "c:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, UserLogName, 0, 0, "c:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, date_time, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, sDelivery, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, doc_str, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, deponentID, 0, 0, "l:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, deponent, 0, 0, "l:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, Party_Name, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, Status_str, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, numbers_str, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, dp_str, 0, 0, "l:w" + RepFontStyleTitel );
   Rep.AddStr();
end;

macro Report(value_Data:integer,
             iStatus:integer,
             Kind_doc_Code:integer,
             Sender_doc_Code:integer,
             Number_doc_beg:string,
             Number_doc_end:string,
             UserLog:integer,
             Date_doc_beg:date,
             Date_doc_end:date,
             sKind_send_doc:string,
             iPostman:integer,
             Get_man_Code:integer)

   var count = 1;
   var stat = 1;
   var spdraft  = TBFile( "spdraft",  "R", 0 );
   var globop   = TBFile( "dpcorpop", "R", 0 );
   var depoadmo = TBFile( "depoadmo", "R", 0 );
   var splogopr = TBFile( "splogopr", "R", 0 );
   var op_status;
   var Initiator;
   var HeaderIsPrint = false;

   InitProgress( value_Data, "Идёт формирование отчёта...", "Отчет \"ЖУРНАЛ ПРИНЯТЫХ ПОРУЧЕНИЙ\"" );
   while( stat )
     UseProgress(count);
     if( DataSet.SourceDocID == 0 and ( DataSet.SourceDocKind == 0 )
       and (DataSet.References = 0 ) )
       if(iStatus == DP_DEPO_STATUS_ALL or (iStatus == DP_DEPO_STATUS_REG))
         op_status = DP_DEPO_STATUS_REG;
         if( not HeaderIsPrint )
            PrintHeader(iStatus, Kind_doc_Code, Sender_doc_Code, Number_doc_beg,
                        Number_doc_end, UserLog, Date_doc_beg, Date_doc_end,
                        sKind_send_doc, iPostman, Get_man_Code);
            HeaderIsPrint = true;
         end;
         PrintStr( count, DataSet.XlD, DataSet.UserLog, DataSet.RegistrDate, DataSet.RegistrTime,
                   DataSet.DeliveryKind, DataSet.Kind, DataSet.AltXLD, DataSet.SignedDate, DataSet.Party, op_status, DataSet.SourceDocID, DataSet.SourceDocKind, DataSet.deponent, DataSet.subjectID );
         count = count+1;
       end;
    //Обработка инвентарных операций
     elif( KindOperation(DataSet.SourceDocKind) == DP_INVENT_OPERATION )

            spdraft.rec.AutoKey = DataSet.SourceDocID;
            if( GetEQ(spdraft) )
              if(   iStatus == DP_DEPO_STATUS_ALL
                 or ( (iStatus == DP_DEPO_STATUS_PREP) and ( spdraft.rec.Status == SC_SPDRAFT_STATUS_PREP ) )
                 or ( (iStatus == DP_DEPO_STATUS_OPEN) and ( spdraft.rec.Status == SC_SPDRAFT_STATUS_OPEN ) )
                 or ( (iStatus == DP_DEPO_STATUS_READY) and ( spdraft.rec.Status == SC_SPDRAFT_STATUS_CLOSE ) )
                 or ( (iStatus == DP_DEPO_STATUS_REJECT) and ( spdraft.rec.OperState == SC_SPDRAFT_STATUS_REJECT ) )
                )
                  op_status = iStatus;
                  if( iStatus == DP_DEPO_STATUS_ALL)
                    op_status = spdraft.rec.Status+1;
                    if( spdraft.rec.OperState == SC_SPDRAFT_STATUS_REJECT )
                      op_status = DP_DEPO_STATUS_REJECT;
                    end;
                  end;
                  if( not HeaderIsPrint )
                      PrintHeader(iStatus, Kind_doc_Code, Sender_doc_Code, Number_doc_beg,
                                  Number_doc_end, UserLog, Date_doc_beg, Date_doc_end,
                                  sKind_send_doc, iPostman, Get_man_Code);
                      HeaderIsPrint = true;
                  end;

                  Initiator = spdraft.rec.Initiator;
                PrintStr( count, DataSet.XlD, DataSet.UserLog, DataSet.RegistrDate, DataSet.RegistrTime,
                          DataSet.DeliveryKind, DataSet.Kind, DataSet.AltXLD,
                          DataSet.SignedDate, Initiator, op_Status,
                          DataSet.SourceDocID, DataSet.SourceDocKind, DataSet.deponent, DataSet.subjectID );
                count = count+1;
              end;
            end;
     //Обработка глобальной операции
     elif( KindOperation(DataSet.SourceDocKind) == DP_GLOB_OPERATION )
       globop.rec.DocumentID = DataSet.SourceDocID;
       if( GetEQ(globop) )
          if(   iStatus == DP_DEPO_STATUS_ALL
             or ( (iStatus == DP_DEPO_STATUS_PREP) and ( globop.rec.State == SC_SPDRAFT_STATUS_PREP ) )
             or ( (iStatus == DP_DEPO_STATUS_OPEN) and ( globop.rec.State == SC_SPDRAFT_STATUS_OPEN ) )
             or ( (iStatus == DP_DEPO_STATUS_READY) and ( globop.rec.State == SC_SPDRAFT_STATUS_CLOSE ) )
             or ( (iStatus == DP_DEPO_STATUS_REJECT) and ( globop.rec.OperState == SC_SPDRAFT_STATUS_REJECT ) )
            )
              op_status = iStatus;
              if( iStatus == DP_DEPO_STATUS_ALL)
                op_status = globop.rec.State+1;
                if( globop.rec.OperState == SC_SPDRAFT_STATUS_REJECT )
                  op_status = DP_DEPO_STATUS_REJECT;
                end;
              end;
              if( not HeaderIsPrint )
                  PrintHeader(iStatus, Kind_doc_Code, Sender_doc_Code, Number_doc_beg,
                              Number_doc_end, UserLog, Date_doc_beg, Date_doc_end,
                              sKind_send_doc, iPostman, Get_man_Code);
                  HeaderIsPrint = true;
              end;

              Initiator = globop.rec.Initiator;
            PrintStr( count, DataSet.XlD, DataSet.UserLog, DataSet.RegistrDate, DataSet.RegistrTime,
                      DataSet.DeliveryKind, DataSet.Kind, DataSet.AltXLD,
                      DataSet.SignedDate, Initiator, op_Status,
                      DataSet.SourceDocID, DataSet.SourceDocKind, DataSet.deponent, DataSet.subjectID );
            count = count+1;
          end;
       end;

     //Обработка административной операций
     elif( KindOperation(DataSet.SourceDocKind) == DP_ADM_OPERATION )
       depoadmo.rec.AdmoprID = DataSet.SourceDocID;
       if( GetEQ(depoadmo) )
         if(    iStatus == DP_DEPO_STATUS_ALL
            or (iStatus == DP_DEPO_STATUS_READY))
            op_status = DP_DEPO_STATUS_READY;
            if( not HeaderIsPrint )
               PrintHeader(iStatus, Kind_doc_Code, Sender_doc_Code, Number_doc_beg,
                           Number_doc_end, UserLog, Date_doc_beg, Date_doc_end,
                           sKind_send_doc, iPostman, Get_man_Code);
               HeaderIsPrint = true;
            end;

            Initiator = depoadmo.rec.Initiator;
            PrintStr( count, DataSet.XlD, DataSet.UserLog, DataSet.RegistrDate, DataSet.RegistrTime,
                      DataSet.DeliveryKind, DataSet.Kind, DataSet.AltXLD,
                      DataSet.SignedDate, Initiator, op_Status,
                      DataSet.SourceDocID, DataSet.SourceDocKind, DataSet.deponent, DataSet.subjectID );
            count = count+1;
         end;
       end;
    //Обработка информационной операций
    elif(KindOperation(DataSet.SourceDocKind) == DP_INFORM_OPERATION)
       splogopr.rec.AutoKey = DataSet.SourceDocID;
       if( GetEQ(splogopr) )
         if(   iStatus == DP_DEPO_STATUS_ALL
            or ( (iStatus == DP_DEPO_STATUS_PREP) and ( splogopr.rec.State == SC_SPDRAFT_STATUS_PREP ) )
            or ( (iStatus == DP_DEPO_STATUS_READY) and ( splogopr.rec.State == SC_SPDRAFT_STATUS_CLOSE ) )
            or ( (iStatus == DP_DEPO_STATUS_REJECT) and ( splogopr.rec.OperState == SC_SPDRAFT_STATUS_REJECT ) ) )

            op_status = iStatus;
            if( iStatus == DP_DEPO_STATUS_ALL)
                op_status = splogopr.rec.State+1;
                if( splogopr.rec.OperState == SC_SPDRAFT_STATUS_REJECT )
                   op_status = DP_DEPO_STATUS_REJECT;
                end;
            end;

            if( not HeaderIsPrint )
               PrintHeader(iStatus, Kind_doc_Code, Sender_doc_Code, Number_doc_beg,
                           Number_doc_end, UserLog, Date_doc_beg, Date_doc_end,
                           sKind_send_doc, iPostman, Get_man_Code);
               HeaderIsPrint = true;
            end;

            Initiator = splogopr.rec.Initiator;
            PrintStr( count, DataSet.XlD, DataSet.UserLog, DataSet.RegistrDate, DataSet.RegistrTime,
                      DataSet.DeliveryKind, DataSet.Kind, DataSet.AltXLD,
                      DataSet.SignedDate, Initiator, op_Status,
                      DataSet.SourceDocID, DataSet.SourceDocKind, DataSet.deponent, DataSet.subjectID );
            count = count+1;
         end;
       end;
    end;
    stat = DataSet.MoveNext();
  end;
  if( HeaderIsPrint )
     DP_StdFooter_Ex(Rep, RepFontStyleTitel);
  end;

  RemProgress();
  return HeaderIsPrint;
end;

macro GenRep( iDepart:integer,
              iStatus:integer,
              Kind_doc_Code:integer,
              Sender_doc_Code:integer,
              Number_doc_beg:string,
              Number_doc_end:string,
              UserLog:integer,
              Date_doc_beg:date,
              Date_doc_end:date,
              sKind_send_doc:string,
              iPostman:integer,
              Get_man_Code:integer,
              ToExcel:bool,
              is_ap:bool
)
   var sQuery:string;
   var iSenderCode:integer;
   var count = 0; //количество записей выборки
   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

   _is_ap = is_ap;
   iSenderCode = Sender_doc_Code;
   //Формируем строку SQL запроса к spground.dbt
   sQuery = DPGetSqlString( iDepart, Kind_doc_Code, iSenderCode, Number_doc_beg,
                          Number_doc_end, UserLog, Date_doc_beg, Date_doc_end,
                          int(sKind_send_doc), iPostman, Get_man_Code );

   DataSet = TRsbDataSet( sQuery, RSDVAL_CLIENT, RSDVAL_STATIC);//получаем выборку из spground.dbt

   DataSet.MoveLast();  //нужно, чтобы определить размер кеша
   count = DataSet.GetRecCount();//работает только с RSDVAL_STATIC!!!
   DataSet.MoveFirst();

   if( count )

     if(not  Report(count, iStatus, Kind_doc_Code, Sender_doc_Code, Number_doc_beg,
             Number_doc_end, UserLog, Date_doc_beg, Date_doc_end,
             sKind_send_doc, iPostman, Get_man_Code) )

        DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
        DP_EndProtocol();                 //закончить протоколирование

        MsgBox("Не найдено данных, удовлетворяющих параметрам отчёта.");
     else
        DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
        DP_EndProtocol();                 //закончить протоколирование

       if( ToExcel )
         Rep.SetZoomType( ZOOM_TYPE_A4L );// альбомная ориентация
         Rep.PrintWinRep();
         Rep.ShowWinRep();
       else
         Rep.PrintRep();
       end;
     end;
   else
      DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
      DP_EndProtocol();                 //закончить протоколирование

      MsgBox("Не найдено данных, удовлетворяющих параметрам отчёта.");
   end;

  return 0;
end;
