/*
$Name:         dpexpdep.mac
$Module:       Депозитарий
$Description:  Выгрузка списка депонентов в ПО "Луч"
*/
import FIInter, secinter, FMInter, "dlmisc.mac", "dl_xml.mac", "dlquery.mac", "pt_lib.mac";

private const TPTCORSCHEM_STATE_OPEN = 0;
private const Chapt5 = 5; /*Глава л/с ДЕПО*/

private var Log = ErrorLoger();
private var Rep;

private class TDPExpDepParam(piNumDprt:Integer, pRestDate:Date, pRegistrAcc:String, piIssuer:Integer, psIssuer:String, piAvrKinds:Integer, piFI:Integer, pIncomeAcc:String, pRefAc:String)
   var iNumDprt:Integer;  // Филиал
   var RestDate:Date;     // Остатки на дату
   var RegistrAcc:String; // Счет депо у регистратора
   var iIssuer:Integer;   // Эмитент
   var sIssuer:String;    // Код эмитента
   var iAvrKinds:Integer; // Вид ценной бумаги
   var iFI:Integer;       // ФИ
   var IncomeAcc:String;  // Счет для отражения доходов
   var RefAc:String;      // Референс корпоративного действия

   macro SetData(piNumDprt:Integer, pRestDate:Date, pRegistrAcc:String, piIssuer:Integer, psIssuer:String, piAvrKinds:Integer, piFI:Integer, pIncomeAcc:String, pRefAc:String)
      iNumDprt = piNumDprt;     if(piNumDprt == null) iNumDprt = UnknownParty; end;
      RestDate = pRestDate;     if(RestDate == null) RestDate = Date(0,0,0); end;
      RegistrAcc = pRegistrAcc; if(RegistrAcc == null) RegistrAcc = ""; end;
      iIssuer = piIssuer;       if(iIssuer == null) iIssuer = UnknownParty; end;
      sIssuer = psIssuer;       if(sIssuer == null) sIssuer = ""; end;
      iAvrKinds = piAvrKinds;   if(iAvrKinds == null) iAvrKinds = FIKIND_ALLAVOIRISS; end;
      iFI = piFI;               if(iFI == null) iFI = ALLFININSTR; end;
      IncomeAcc = pIncomeAcc;   if(IncomeAcc == null) IncomeAcc = ""; end;
      RefAc = pRefAc;           if(RefAc == null) RefAc = ""; end;
   end;

   private macro Constructor(piNumDprt:Integer, pRestDate:Date, pRegistrAcc:String, piIssuer:Integer, psIssuer:String, piAvrKinds:Integer, piFI:Integer, pIncomeAcc:String, pRefAc:String)
      SetData(piNumDprt, pRestDate, pRegistrAcc, piIssuer, psIssuer, piAvrKinds, piFI, pIncomeAcc, pRefAc);
   end;

   Constructor(piNumDprt, pRestDate, pRegistrAcc, piIssuer, psIssuer, piAvrKinds, piFI, pIncomeAcc, pRefAc);
end;

private var ExpDepPrm = TDPExpDepParam();

/*листалка для поля "Счет депо у регистратора"*/
macro DP_FldProc_RegistrAcc(IsSelect:Bool, selectVal:String)
   var Choice; isSelect = false; selectVal = "";
   Choice = DL_Scroll(" select csh.t_CorAccount, " +
                      "       (select t_ShortName from dparty_dbt where t_PartyID = csh.t_corrid) t_PartyName " +
                      " from dcorschem_dbt csh " +
                      " where csh.t_fi_kind = " + FIKIND_AVOIRISS +
                      "   and csh.t_state = "   + TPTCORSCHEM_STATE_OPEN +
                      " order by t_PartyName ",
                      "Счет депо у регистратора", 0, 0,
                      "t_CorAccount", "Счет", "t_PartyName", "Контрагент"
                     );
   if( Choice != NULL )
      isSelect = true;
      selectVal = Choice.Value("t_CorAccount");
   end;

   SetParm(0, isSelect);
   SetParm(1, selectVal);

   return 0;
end;

private macro GetFormatDate(_date)
   var y, mon, d;

   datesplit(_date, d, mon, y);
   return string(L_Z(y, 4)) + "-" + string(L_Z(mon,2)) + "-" + string(L_Z(d,2));
end;

private macro CorrectValue(val:@variant, type)
   if(type == V_STRING)
      if((val == null) or (val == ""))
         val = "UKWN";
      end;
   elif(type == V_DATE)
      if((val == null) or (val == Date(0,0,0)))
         val = GetFormatDate(Date(01,01,1900));
      else
         val = GetFormatDate(val);
      end;
   end;

   return val;
end;

private macro GetCountryCode(CodeLat3:string)
  var ds, cmd;
  var countryCode = "";

  cmd = DL_RSDCommand("select t_CodeLat2 from dcountry_dbt where t_CodeLat3 = ?");
  cmd.AddParam(CodeLat3);

  ds = cmd.Execute();
  if( ds.moveNext() )
     countryCode = ds.CodeLat2;
  end;

  return countryCode;
end;

private macro ClearSymbols(str)
   if((str != null) and (strlen(str) > 0))
      str = StrSubst(str, "\\", "");
      str = StrSubst(str, "/", "");
      str = StrSubst(str, ":", "");
      str = StrSubst(str, "*", "");
      str = StrSubst(str, "?", "");
      str = StrSubst(str, "\"", "");
      str = StrSubst(str, "<", "");
      str = StrSubst(str, ">", "");
      str = StrSubst(str, "|", "");
   end;

   return str;
end;

private class (DL_XML) TDpExpDep_XML()
   private var m_MainNode, m_headerNode;
   private var m_IssuerShortName;

   private macro GetFileName()
      var y, mon, d, h, m, s;

      if(m_FileName == NULL)
         var IssuerShortName = ClearSymbols(m_IssuerShortName);

         datesplit(ExpDepPrm.RestDate, d, mon, y);
         m_FileName = string(L_Z(d,2)) + string(L_Z(mon,2)) + string(L_Z(y, 4)) + "_";
         m_FileName = m_FileName + ExpDepPrm.RegistrAcc + "_";
         m_FileName = m_FileName + IssuerShortName + "_";
         DateSplit (Date(),d,mon,y);
         TimeSplit(Time(),h,m,s);
         m_FileName = m_FileName + string(L_Z(d,2)) + string(L_Z(mon,2)) + string(L_Z(y, 4)) + string(L_Z(h,2)) + string(L_Z(m,2)) + string(L_Z(s, 2));
      end;

      return m_FileName;
   end;

   private macro CreateXML()
     var stat = CreateXML();
     m_MainNode = null;
     return stat;
   end;

   macro SaveXML()
     if(m_MainNode != null)
       AddMainNode(m_MainNode);
       //RemoveEmpty();
       SaveXML();
       Log.LogError("Сформирован файл " + GetFileName()+".xml");
     end;
   end;

   private macro CreateNode_REGISTER_OF_SHAREHOLDERS()
      return CreateNode("REGISTER_OF_SHAREHOLDERS");
   end;

   private macro CreateElementValue_doc_num(val)
      return CreateElementValue("doc_num", val);
   end;

   private macro CreateNode_doc_date(val)
      var node = CreateNode("doc_date");
      node.appendChild(CreateElementValue("date", val));
      return node;
   end;

   private macro CreateNode_out_doc_date(val)
      var node = CreateNode("out_doc_date");
      node.appendChild(CreateElementValue("date", val));
      return node;
   end;

   private macro CreateNode_in_reg_date(val)
      var node = CreateNode("in_reg_date");
      node.appendChild(CreateElementValue("date", val));
      return node;
   end;

   private macro CreateNode_link(out_doc_num, in_doc_num, out_doc_date, in_reg_date)
      var node = CreateNode("link");
      node.appendChild(CreateElementValue("out_doc_num", out_doc_num));
      node.appendChild(CreateElementValue("in_doc_num", in_doc_num));
      node.appendChild(CreateNode_out_doc_date(out_doc_date));
      node.appendChild(CreateNode_in_reg_date(in_reg_date));
      return node;
   end;

   macro Add_Header(name)
      CreateXML();

      m_IssuerShortName = name;

      m_MainNode = CreateNode_REGISTER_OF_SHAREHOLDERS();
      m_MainNode.appendChild(CreateElementValue("version", 3));

      m_headerNode = CreateNode("header");
      m_headerNode.appendChild(CreateElementValue_doc_num(1));
      m_headerNode.appendChild(CreateNode_doc_date(GetFormatDate(date())));
      m_headerNode.appendChild(CreateNode_link("NONREF", "NONREF", GetFormatDate(date(01,01,1900)), GetFormatDate(date(01,01,1900))));
      m_MainNode.appendChild(m_headerNode);
   end;

   private macro CreateNode_issuer(name, val, name_val)
      var node = CreateNode("issuer");
      var id = CreateNode("id");
      id.appendChild(CreateElementValue("id", val));
      id.appendChild(CreateElementValue("issuer", name));
      node.appendChild(id);
      node.appendChild(CreateElementValue("name", name_val));
      return node;
   end;

   macro Add1_Issuer(name, val, name_val)
      m_MainNode.appendChild(CreateNode_issuer(name, val, name_val));
   end;

   private macro CreateNode_id_t(node_name, val, choice, choice_val)
      var node = CreateNode(node_name);
      CorrectValue(@val, V_STRING);
      node.appendChild(CreateElementValue("id", val));
      if( choice != null )
         CorrectValue(@choice_val, V_STRING);
         if(choice == "issuer")
            node.appendChild(CreateElementValue("issuer", choice_val));
         elif(choice == "scheme_name")
            node.appendChild(CreateElementValue("scheme_name", choice_val));
         end;
      end;
      return node;
   end;

   private macro CreateNode_account_dtls_t(name, account_id_val, choice, choice_val, account_type_val)
      var node = CreateNode(name);
      if(account_id_val != null)
         node.appendChild(CreateNode_id_t("account_id", account_id_val, choice, choice_val));
      end;
      CorrectValue(@account_type_val, V_STRING);
      node.appendChild(CreateElementValue("account_type", account_type_val));
      return node;
   end;

   macro Add2_account_dtls(val, type)
      m_MainNode.appendChild(CreateNode_account_dtls_t("account_dtls", val, NULL, NULL, type));
   end;

   private macro CreateNode_address_partad_ndc_t(name, country, index, address)
      var node = CreateNode(name);
      node.appendChild(CreateElementValue("country", country));
      if(index != null)
         node.appendChild(CreateElementValue("index", index));
      end;
      node.appendChild(CreateElementValue("address", address));
      return node;
   end;

   private macro CreateNode_address_ndc_t(name, choice_name, choice_val1, choice_val2, choice_val3)
      var node = CreateNode(name);

      CorrectValue(@choice_val1, V_STRING);
      CorrectValue(@choice_val2, V_STRING);
      CorrectValue(@choice_val3, V_STRING);

      if(choice_name == "partad")
         node.appendChild(CreateNode_address_partad_ndc_t("partad", choice_val1, choice_val2, choice_val3));
      elif(choice_name == "kladr")
         node.appendChild(CreateElementValue("kladr", choice_val1));
      elif(choice_name == "plain")
         node.appendChild(CreateElementValue("plain", choice_val1));
      end;
      return node;
   end;

   private macro CreateElementValue_individual_or_entity(val)
      return CreateElementValue("individual_or_entity", val);
   end;

   private macro CreateElementValue_entity_reg_doc_type_code(val)
      return CreateElementValue("entity_reg_doc_type_code", val);
   end;

   private macro CreateNode_reg_doc_type(val1)
      var node = CreateNode("reg_doc_type");
      node.appendChild(CreateElementValue_entity_reg_doc_type_code(val1));
      return node;
   end;

   private macro CreateNode_entity_reg_dtls_t(name, reg_doc_type_val1, reg_num, date_of_incorporation, reg_org)
      var node = CreateNode(name);

      CorrectValue(@reg_num, V_STRING);
      CorrectValue(@date_of_incorporation, V_DATE);
      CorrectValue(@reg_org, V_STRING);

      node.appendChild(CreateNode_reg_doc_type(reg_doc_type_val1));
      node.appendChild(CreateElementValue("reg_num", reg_num));
      node.appendChild(CreateElementValue("date_of_incorporation", date_of_incorporation));
      node.appendChild(CreateElementValue("reg_org", reg_org));
      return node;
   end;

   private macro CreateNode_individual_document_type_t(name, individual_document_type_code)
      var node = CreateNode(name);
      node.appendChild(CreateElementValue("individual_document_type_code", individual_document_type_code));
      return node;
   end;

   private macro CreateNode_individual_document_t(name, doc_type_val1, doc_num, doc_date, org)
      var node = CreateNode(name);
      node.appendChild(CreateNode_individual_document_type_t("doc_type", doc_type_val1));
      node.appendChild(CreateElementValue("doc_num", doc_num));
      node.appendChild(CreateElementValue("doc_date", doc_date));
      node.appendChild(CreateElementValue("org", org));
      return node;
   end;

   private macro CreateNode_party_dtls_t(name,
      id_val1,id_choice1,id_choice_val1,
      name2,
      choice_val15,choice_val25,choice_val35,
      individual_or_entity6,
      choice_name7,  reg_doc_type_val1,reg_num,date_of_incorporation,reg_org,  doc_type_val1,doc_num,doc_date,org
   )
      var node = CreateNode(name);
      if(id_val1 != null)
         node.appendChild(CreateNode_id_t("id", id_val1, id_choice1, id_choice_val1));
      end;
      CorrectValue(@name2, V_STRING);
      node.appendChild(CreateElementValue("name", name2));
      node.appendChild(CreateNode_address_ndc_t("address", "partad", choice_val15, choice_val25, choice_val35));
      node.appendChild(CreateElementValue_individual_or_entity(individual_or_entity6));
      if(choice_name7 == "entity_reg_dtls")
         node.appendChild(CreateNode_entity_reg_dtls_t("entity_reg_dtls", reg_doc_type_val1, reg_num, date_of_incorporation, reg_org));
      elif(choice_name7 == "individual_document")
         node.appendChild(CreateNode_individual_document_t("individual_document", doc_type_val1, doc_num, doc_date, org));
      end;
      return node;
   end;

   private macro GetDepoCodeType(account, code:@variant, type:@variant)
      var query, cmd, ds;

      query = " SELECT depoacnt.t_BriefCode, llvalues.t_Code " +
              "   FROM daccount_dbt account, ddepoacnt_dbt depoacnt, ddepoac_dbt depoac, dllvalues_dbt llvalues " +
              "  WHERE account.t_Account = ? " +
              "    AND account.t_Chapter = ? " +
              "    AND depoacnt.t_AutoKey = account.t_DepoRoot " +
              "    AND depoac.t_ID = depoacnt.t_Type " +
              "    AND llvalues.t_List = " + OBJTYPE_DEPOKINDTYPE + " " +
              "    AND llvalues.t_Element = depoac.t_Type ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(account);
      cmd.AddParam(Chapt5);

      ds = cmd.Execute();
      if(ds.moveNext())
         code = ds.BriefCode;

         if(ds.Code == "01")   // счет владельца
            type = "01";
         elif(ds.Code == "02") // счет номинального держателя
            type = "02";
         elif(ds.Code == "03") // счет  доверительного управляющего
            type = "03";
         elif(ds.Code == "04") // счет залогодержателя
            type = "04";
         elif(ds.Code == "06") // эмиссионный счет
            type = "05";
         elif(ds.Code == "05") // казначейский счет эмитента
            type = "06";
         elif(ds.Code == "07") // счет ц/б неустановленных лиц
            type = "07";
         elif(ds.Code == "13") // депозитарный счет депо И депонент при этом является судом или нотариусом
            type = "19";
         elif(ds.Code == "11") // счет иностранного номинального держателя
            type = "20";
         elif(ds.Code == "12") // счет иностранного уполномоченного держателя
            type = "21";
         elif(ds.Code == "10") // счет депозитарных программ
            type = "22";
         end;
      end;
   end;

   private macro Add_party_dtls_ndc(node_parent, node_name, partyID, id)
      var party = TRecHandler( "party" );
      var partyAdress = TRecHandler("adress");
      var partyRegInfo = TRecHandler("objrgdoc");
      var partyRegInfo_phys = TRecHandler("persnidc");

      var LEI = ПолучитьКодСубъекта({OurBank}, PTCK_LEI);
      var party_dtls_t__name;
      var country, index, address;
      var individual_or_entity;
      var entity_reg_doc_type_code, reg_num = "", date_of_incorporation, reg_org;
      var individual_document_type_code, doc_num, doc_date, org;

      if(ПолучитьСубъекта(partyID, party) == 0)
         party_dtls_t__name = party.rec.Name;
         if(party.rec.LegalForm == legal_form_phys)
            individual_or_entity = "INDV";
   	
            if(party.rec.NotResident)
               individual_document_type_code = "FCCP";
            else
               individual_document_type_code = "CCPT";
            end;

            if( GetPersonIdc(party.rec.PartyID, partyRegInfo_phys) )
               doc_num = partyRegInfo_phys.rec.PaperNumber;
               doc_date = partyRegInfo_phys.rec.PaperIssuedDate;
               org = partyRegInfo_phys.rec.PaperIssuer;
            end;
         else
            individual_or_entity = "LEGL";

            if(party.rec.NotResident)
               entity_reg_doc_type_code = "INCR";
            else
               entity_reg_doc_type_code = "OGRN";
            end;

            if(ПолучитьДанныеРегистрации( null, reg_num, party.rec.PartyID, partyRegInfo ))
               date_of_incorporation = partyRegInfo.rec.DocDate;
               var RegParty = TRecHandler( "party" );
               if(ПолучитьСубъекта(partyRegInfo.rec.RegPartyID, RegParty) == 0)
                  reg_org = RegParty.rec.Name;
               end;
            end;
         end;

         if( НайтиЮридическийАдресСубъекта(party.rec.PartyID, partyAdress) or НайтиАдресСубъекта(party.rec.PartyID, PTADDR_REAL, partyAdress))
            country = GetCountryCode(partyAdress.rec.Country);
            index = partyAdress.rec.PostIndex;
            address = partyAdress.rec.Adress;
         end;
      end;

      node_parent.appendChild(
         CreateNode_party_dtls_t(node_name,
            id,"issuer",LEI,
            party_dtls_t__name,
            country,index,address,
            individual_or_entity,
            IIF(party.rec.LegalForm == legal_form_jur, "entity_reg_dtls", "individual_document"),
            entity_reg_doc_type_code,reg_num,date_of_incorporation,reg_org,
            individual_document_type_code,CorrectValue(doc_num, V_STRING),
            CorrectValue(doc_date, V_DATE),CorrectValue(org, V_STRING)
         )
      );
   end;

   macro Add3_account_holder()
      Add_party_dtls_ndc(m_MainNode, "account_holder", {OurBank}, ПолучитьКодСубъекта({OurBank}, PTCK_NRD));
   end;

   macro Add4_corporate_action_code(val)
      m_MainNode.appendChild(CreateElementValue("corporate_action_code", val));
   end;

   macro Add5_corporate_action_reference(val)
      CorrectValue(@val, V_STRING);
      m_MainNode.appendChild(CreateElementValue("corporate_action_reference", val));
   end;

   macro Add6_corporate_action_reference_NDC(val)
      CorrectValue(@val, V_STRING);
      m_MainNode.appendChild(CreateElementValue("corporate_action_reference_NDC", val));
   end;

   macro Add7_record_date(val)
      CorrectValue(@val, V_DATE);
      m_MainNode.appendChild(CreateElementValue("record_date", val));
   end;

   macro Add8_message_function(val)
      m_MainNode.appendChild(CreateElementValue("message_function", val));
   end;

   macro Add9_information_indicator(val)
      m_MainNode.appendChild(CreateElementValue("information_indicator", val));
   end;

   private macro Get11_3_1_shareholder_id(val)
      return CreateNode_id_t("shareholder_id", val)
   end;

   private macro Get11_3_2_account_dtls(val, type)
      return CreateNode_account_dtls_t("account_dtls", val, NULL, NULL, type);
   end;

   private macro Add11_3_3_1_shareholder_dtls(node_Parent, Client, DepoCode)
      Add_party_dtls_ndc(node_Parent, "shareholder_dtls", Client.rec.PartyID, DepoCode);
   end;

   private macro GetKPP(ClientID, KPP:@variant)
      var INN = "";
      SplitFullINN(ПолучитьКодСубъекта(ClientID, PTCK_INN), INN, KPP);
   end;

   private macro ПолучитьКодыОКВЭД( PartyID, pDate, Codes:TArray )
      var Select, Pt, str = "", NRecs = 0;

      if( PartyID != -1 )
         Select = DL_RSDCommand(
                  " Select Attr.t_NumInList " +
                  "   From dobjatcor_dbt AtCor, dobjattr_dbt Attr " +
                  "  Where AtCor.t_ObjectType = 3 "  + /*OBJTYPE_PARTY*/
                  "    And AtCor.t_GroupID    = 64 " +
                  "    And AtCor.t_Object     = LPAD( ?, 10, '0' ) " +
                  "    And AtCor.t_ValidFromDate  = ( SELECT MAX(t.T_ValidFromDate) " +
                  "                                     FROM DOBJATCOR_DBT t " +
                  "                                    WHERE t.T_ObjectType = AtCor.T_ObjectType " +
                  "                                      AND t.T_GroupID    = AtCor.T_GroupID    " +
                  "                                      AND t.t_Object     = AtCor.t_Object     " +
                  "                                      AND t.T_ValidFromDate <= ?              " +
                  "                                      AND t.t_General    = 'X'                " +
                  "                                 ) " +
                  "    And AtCor.t_ValidToDate >= ?                                              " +
                  "    And Attr.t_AttrID      = AtCor.t_AttrID " +
                  "    And Attr.t_ObjectType  = AtCor.t_ObjectType " +
                  "    And Attr.t_GroupID     = AtCor.t_GroupID " +
                  "    And AtCor.t_General    = 'X' " +
                  "  Order By Attr.t_NumInList "
                            );

         Select.addParam(PartyID);
         Select.addParam(pDate);
         Select.addParam(pDate);
         Select.execute();

         Pt = Select.execute();

         if( Pt.MoveNext() )
           if( Codes != NULL )
              Codes[Codes.Size] = Pt.NumInList;
           end;
           str = str + Pt.NumInList;
         end;
      end;

      return str;
   end;

   private macro Get11_3_3_11_postal_name(Client)
      var node = CreateNode("postal_name");
	
      // 11.3.3.11.1
	  var name = Client.rec.Name;
	  CorrectValue(@name, V_STRING);
      node.appendChild(CreateElementValue("name", name));

      // 11.3.3.11.2
      var country = "", index = "", address = "", partyAdress = TRecHandler("adress");
      if(НайтиПочтовыйАдресСубъекта(Client.rec.PartyID, partyAdress) or НайтиЮридическийАдресСубъекта(Client.rec.PartyID, partyAdress) or НайтиАдресСубъекта(Client.rec.PartyID, PTADDR_REAL, partyAdress))
         country = GetCountryCode(partyAdress.rec.Country);
         index = partyAdress.rec.PostIndex;
         address = partyAdress.rec.Adress;
      end;
	  CorrectValue(@country, V_STRING);
	  CorrectValue(@index, V_STRING);
	  CorrectValue(@address, V_STRING);
      node.appendChild(CreateNode_address_ndc_t("address", "partad", country, index, address));

      return node;
   end;

   private macro Get11_3_3_shareholder_info(Client, DepoCode)
      file persn("persn.dbt");

      var node_shareholder_info = CreateNode("shareholder_info");

      // 11.3.3.1
      Add11_3_3_1_shareholder_dtls(node_shareholder_info, Client, DepoCode);

      // 11.3.3.3
      var inn = ПолучитьКодСубъекта(Client.rec.PartyID, PTCK_INN );
      CorrectValue(@inn, V_STRING);
      var slashInd = Index(inn, "/");
      var innNotKPP = IIF(slashInd > 0, SubStr(inn, 1, slashInd - 1), inn);
      if(inn != "UKWN")
         node_shareholder_info.appendChild(CreateElementValue("inn", innNotKPP));
      end;

      if(Client.rec.LegalForm == legal_form_jur)
         // 11.3.3.6
         var kpp = ""; GetKPP(Client.rec.PartyID, @kpp);
         CorrectValue(@kpp, V_STRING);
         if(kpp != "UKWN")
            node_shareholder_info.appendChild(CreateElementValue("kpp", kpp));
         end;

         // 11.3.3.7
         var okpo = Client.rec.okpo;
         CorrectValue(@okpo, V_STRING);
         if(okpo != "UKWN")
            node_shareholder_info.appendChild(CreateElementValue("okpo", okpo));
         end;

         // 11.3.3.8
         var okved = ПолучитьКодыОКВЭД(Client.rec.PartyID, ExpDepPrm.RestDate);
         CorrectValue(@okved, V_STRING);
         if(okved != "UKWN")
            node_shareholder_info.appendChild(CreateElementValue("okved", okved));
         end;
      elif(Client.rec.LegalForm == legal_form_phys)
         var birthday = "", nationality = "";
         ClearRecord( persn );
         persn.PersonID = Client.rec.PartyID;
         if(GetEQ( persn ))
            birthday = persn.Born;
         end;
         CorrectValue(@birthday, V_DATE);

         // 11.3.3.9
         node_shareholder_info.appendChild(CreateElementValue("birthday", birthday));
		
         // 11.3.3.10
         nationality = GetCountryCode(Client.rec.NrCountry);
         CorrectValue(@nationality, V_STRING);
         node_shareholder_info.appendChild(CreateElementValue("nationality", nationality));
      end;

      // 11.3.3.11
      node_shareholder_info.appendChild(Get11_3_3_11_postal_name(Client));

      return node_shareholder_info;
   end;

   private macro CreateNode_phone_t(name, phone_num, phone_type)
      var node = CreateNode(name);
      node.appendChild(CreateElementValue("phone_num", phone_num));
      if(phone_type != null)
         node.appendChild(CreateElementValue("phone_type", phone_type));
      end;
      return node;
   end;

   private macro CreateNode_party_contacts_t(name, phone_num_1,phone_type_1, e_mail_2)
      var node = CreateNode(name);
      node.appendChild(CreateNode_phone_t("phone_or_fax", phone_num_1, phone_type_1));
      if(e_mail_2 != null)
         node.appendChild(CreateElementValue("e_mail", e_mail_2));
      end;
      return node;
   end;

   private macro Add11_3_4_shareholder_contacts(node_parent, Client)
      var phone_or_fax = "", _phone = "";
      var e_mail = "", _e_mail = "";
      if((FindAdressContactValue(Client.rec.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, _phone) == 0) or (FindAdressContactValue(Client.rec.PartyID, PTADDR_REAL, CNTADR_PHONENUMBER, _phone) == 0))
         phone_or_fax = _phone;
      end;
      CorrectValue(@phone_or_fax, V_STRING);
      if((FindAdressContactValue(Client.rec.PartyID, PTADDR_LEGAL, CNTADR_E_MAIL, _e_mail) == 0) or (FindAdressContactValue(Client.rec.PartyID, PTADDR_REAL, CNTADR_E_MAIL, _e_mail) == 0))
         e_mail = _e_mail;
      end;
      CorrectValue(@e_mail, V_STRING);
      if(phone_or_fax != "UKWN")
         node_parent.appendChild(CreateNode_party_contacts_t("shareholder_contacts", phone_or_fax,NULL, e_mail));
      end;
   end;

   private macro CreateNode_tax_category_ndc_t(name, tax_status_code, tax_exempt_indicator)
      var node = CreateNode(name);
      node.appendChild(CreateElementValue("tax_status_code", tax_status_code));
      node.appendChild(CreateElementValue("tax_exempt_indicator", tax_exempt_indicator));

      return node;
   end;

   private macro CreateNode_cash_rub_dtls_ndc_t(name, account, bank_name, bank_city, ruic, bank_corr)
      var node = CreateNode(name);
      node.appendChild(CreateNode_id_t("account", account));
      node.appendChild(CreateElementValue("bank_name", bank_name));
      node.appendChild(CreateElementValue("bank_city", bank_city));
      node.appendChild(CreateElementValue("ruic", ruic));
      if(bank_corr != null)
         node.appendChild(CreateNode_id_t("bank_corr", bank_corr));
      end;
      return node;
   end;

   private macro CreateNode_bank_prop_rub_ndc_t(name, pay_name, inn, account, bank_name, bank_city, ruic, bank_corr)
      var node = CreateNode(name);
      node.appendChild(CreateElementValue("pay_name", pay_name));
      if(inn != null)
         node.appendChild(CreateElementValue("inn", inn));
      end;
      node.appendChild(CreateNode_cash_rub_dtls_ndc_t("cash_rub_dtls", account, bank_name, bank_city, ruic, bank_corr));
      return node;
   end;

   private macro Get11_3_6_bank_prop_rub_ndc()
      file bankdprt("bankdprt.dbt");
      var node = CreateNode("bank_prop_rub");
      var pay_name = "", inn = "";
      var account = IIF(ExpDepPrm.IncomeAcc != "", ExpDepPrm.IncomeAcc, "00000000000000000000");
      var bank_name = "", bank_city = "", ruic = "", bank_corr = "";
      var partyAdress = TRecHandler("adress");
      var party = TRecHandler( "party" );

      if(ПолучитьСубъекта({OurBank}, party) == 0)
         pay_name = party.rec.name;
         inn = ПолучитьКодСубъекта({OurBank}, PTCK_INN);
         bank_name = party.rec.name;

         if(НайтиЮридическийАдресСубъекта({OurBank}, partyAdress))
            bank_city = partyAdress.rec.District;
         end;
         ruic = ПолучитьКодСубъекта({OurBank}, PTCK_BIC);

         ClearRecord( bankdprt );
         bankdprt.PartyID = {OurBank};
         if(GetEQ( bankdprt ))
            bank_corr = bankdprt.CorAcc;
         end;
      end;
      CorrectValue(@pay_name, V_STRING);
      CorrectValue(@inn, V_STRING);
      CorrectValue(@bank_name, V_STRING);
      CorrectValue(@bank_city, V_STRING);
      CorrectValue(@ruic, V_STRING);
      CorrectValue(@bank_corr, V_STRING);
      var slashInd = Index(inn, "/");
      var innNotKPP = IIF(slashInd > 0, SubStr(inn, 1, slashInd - 1), inn);

      return CreateNode_bank_prop_rub_ndc_t("bank_prop_rub", pay_name, innNotKPP, account, bank_name, bank_city, ruic, bank_corr);//);
   end;

   private macro GetSecurityClassification(AvoirKind)
      var security_classification = "";
      if(AvoirKind == AVOIRISSKIND_BOND)
         security_classification = "BOND";
      elif(AvoirKind == AVOIRISSKIND_SHARE)
         security_classification = "SHAR";
      elif(AvoirKind == AVOIRISSKIND_INVESTMENT_SHARE)
         security_classification = "MFUN";
      elif(AvoirKind == AVOIRISSKIND_RUSSIAN_DEPREC)
         security_classification = "RDRP";
      elif(AvoirKind == AVOIRISSKIND_HYPOTHECARY_CERT)
         security_classification = "MPPC";
      end;
      return security_classification;
   end;

   private macro CreateNode_security_info_ndc_t(name, security_classification, state_reg_num)
      var node = CreateNode(name);
      node.appendChild(CreateElementValue("security_classification", security_classification));
      node.appendChild(CreateElementValue("state_reg_num", state_reg_num));
      return node;
   end;

   private macro CreateNode_quantity_in_unit_t(name, units)
      var node = CreateNode(name);
      node.appendChild(CreateElementValue("units", units));
      return node;
   end;

   private macro CreateNode_block_type_ndc_t(name, block_type, narrative)
      var node = CreateNode(name);
      node.appendChild(CreateElementValue("block_type_code", block_type));
      node.appendChild(CreateElementValue("narrative", narrative));
      return node;
   end;

   private macro CreateNode_blocked(quantity_units, block_type, narrative)
      var node = CreateNode("blocked");
      node.appendChild(CreateNode_quantity_in_unit_t("quantity", quantity_units));
      node.appendChild(CreateNode_block_type_ndc_t("block_type", block_type, narrative));
      return node;
   end;

   private macro Get11_3_8_security_balances(account, Client, FIID, total, AvoirKind, LSIN)
      var nodeBalances = CreateNode("security_balances");
      var node = CreateNode("security_balance");

      var security_classification = GetSecurityClassification(AvoirKind);
      CorrectValue(@security_classification, V_STRING);
      CorrectValue(@LSIN, V_STRING);
      node.appendChild(CreateNode_security_info_ndc_t("security", security_classification, LSIN));
      node.appendChild(CreateNode_quantity_in_unit_t("total", total));

      var query, cmd, ds;
      query = " SELECT t.t_rest_sa " +
              " from DDPEXPDEP_TMP t " +
              " where t.t_Account = ? and t.t_Client = ? and t.t_FIID = ? " +
              "   and t.t_AttrNum = 6 and t.t_AttrVal <> 0 ";
      cmd = DL_RSDCommand(query);

      cmd.AddParam(account);
      cmd.AddParam(Client.rec.PartyID);
      cmd.AddParam(FIID);

      ds = cmd.Execute();
      while(ds.moveNext())
         node.appendChild(CreateNode_blocked(ds.rest_sa, "OTHR", "Прочее обременение"));
      end;

      nodeBalances.appendChild(node);

      return nodeBalances;
   end;

   private macro Get11_3_shareholder(account, Client)
      var node_shareholder = CreateNode("shareholder");

      var DepoCode, DepoType;
      GetDepoCodeType(account, @DepoCode, @DepoType);

      // 11.3.1
      node_shareholder.appendChild(Get11_3_1_shareholder_id(DepoCode));

      // 11.3.2
      node_shareholder.appendChild(Get11_3_2_account_dtls(DepoCode, DepoType));

      // 11.3.3
      node_shareholder.appendChild(Get11_3_3_shareholder_info(Client, DepoCode));

      // 11.3.4
      Add11_3_4_shareholder_contacts(node_shareholder, Client);

      // 11.3.5
      var tax_category, tax_exempt_indicator;
      if(Client.rec.LegalForm == legal_form_jur)
         if(Client.rec.NotResident == SET_CHAR)
            tax_category = "2";
         else
            tax_category = "1";
         end;
      else
         if(Client.rec.NotResident == SET_CHAR)
            tax_category = "6";
         else
            tax_category = "5";
         end;
      end;
      if(DL_GetMainObjAttr(Client, PARTY_ATTR_GROUP_PREF_TAXATION, OBJTYPE_PARTY) == 1)
        tax_exempt_indicator = "Yes";
      else
        tax_exempt_indicator = "No";
      end;
      node_shareholder.appendChild(CreateNode_tax_category_ndc_t("tax_category", tax_category, tax_exempt_indicator));

      // 11.3.6
      node_shareholder.appendChild(Get11_3_6_bank_prop_rub_ndc());

      // 11.3.8
      var query, cmd, ds;
      query = "   SELECT t.t_FIID, NVL(SUM(t.t_Rest_SA), 0) AS t_Rest, " +
              "          (SELECT RSB_FIInstr.FI_AvrKindsGetRoot(fininstr.t_FI_Kind, fininstr.t_AvoirKind) " +
              "             FROM dfininstr_dbt fininstr " +
              "            WHERE fininstr.t_FIID = t.t_FIID) AS t_AvoirKind, " +
              "          (SELECT avoiriss.t_LSIN " +
              "             FROM davoiriss_dbt avoiriss " +
              "            WHERE avoiriss.t_FIID = t.t_FIID) AS t_LSIN " +
              "     FROM ddpexpdep_tmp t " +
              "    WHERE t.t_Account = ? " +
              "      AND t.t_Client = ? " +
              " GROUP BY t.t_FIID ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(account);
      cmd.AddParam(Client.rec.PartyID);

      ds = cmd.Execute();
      while(ds.moveNext())
         node_shareholder.appendChild(Get11_3_8_security_balances(account, Client, ds.FIID, ds.Rest, ds.AvoirKind, ds.LSIN));
      end;

      return node_shareholder;
   end;

   macro Add11_register_list(IssuerID, Client)
      var node_register_list = CreateNode("register_list");

      // 11.1
      node_register_list.appendChild(CreateElementValue("register_list_category", "ADD"));

      // 11.3
      var query, cmd, ds;
      query = " SELECT DISTINCT t_Account FROM DDPEXPDEP_TMP WHERE t_Issuer = ? and t_Client = ? ORDER BY t_Account ";
      cmd = DL_RSDCommand(query);

      cmd.AddParam(IssuerID);
      cmd.AddParam(Client.rec.PartyID);

      ds = cmd.Execute();
      while(ds.moveNext())
         node_register_list.appendChild(Get11_3_shareholder(ds.Account, Client));
      end;
	
      m_MainNode.appendChild(node_register_list);
   end;
END;

private macro LoadDataInTMP()
   var query = "";
   var header = "Отбор данных";

   SQL_Truncate( "DDPEXPDEP_TMP" );

   query =
     " DECLARE \n"
   + " TYPE expdep_t IS TABLE OF DDPEXPDEP_TMP%ROWTYPE; \n"
   + "    g_expdep_ins expdep_t := expdep_t(); \n"
   + "    expdep DDPEXPDEP_TMP%rowtype; \n"
   + " BEGIN \n"
   + " FOR one_rec IN ("
   + " SELECT fin.t_Issuer, "
   + "        vanl.t_FIID, "
   + "        vanl.t_Account, "
   + "        acnt.t_Client, "
   + "        rsb_account.restsa (accsub.t_AccAnaliticsID, accsub.t_SubAccountID, " + GetSQLDate(ExpDepPrm.RestDate) + ", 1) t_rest_sa, "
   + "        attr.t_AttrNum, "
   + "        attr.t_Value "
   + "   FROM daccsub_dbt accsub, "
   + "        daccvanl_dbt vanl, "
   + "        daccattr_dbt attr, "
   + "        dfininstr_dbt fin, "
   + "        daccount_dbt acnt "
   + "  WHERE     accsub.t_SubAccountCode IN "
   + "               (SELECT acc.t_Account "
   + "                  FROM dcorschem_dbt cr, ddepoacnt_dbt dacc, daccount_dbt acc "
   + "                 WHERE     cr.t_CorAccount = '" + ExpDepPrm.RegistrAcc + "'"
   + "                       AND cr.t_IsNostro = 'X' "
   + "                       AND cr.t_Department = " + ExpDepPrm.iNumDprt
   + "                       AND cr.t_FIID = -1 "
   + "                       AND dacc.t_BriefCode = cr.t_Account "
   + "                       AND dacc.t_Department = cr.t_Department "
   + "                       AND acc.t_DepoRoot = dacc.t_AutoKey "
   + "                       AND acc.t_Chapter = " + Chapt5 + ") "
   + "        AND accsub.t_AccAnaliticsID = vanl.t_AccAnaliticsID "
//   + "        AND accsub.t_SpecialType = 0 "
//   + "        AND vanl.t_AnaliticsID = 2 "  // SYS_ANL_AINPACCOUNTING
   + "        AND attr.t_Account = vanl.t_Account "
   + "        AND attr.t_Chapter = vanl.t_Chapter "
   + "        AND attr.t_FIID = vanl.t_FIID "
   + "        AND vanl.t_Chapter = " + Chapt5
   + "        AND fin.t_FIID = vanl.t_FIID "
   + "        AND acnt.t_Chapter = vanl.t_Chapter "
   + "        AND acnt.t_Account = vanl.t_Account "
   + "        AND acnt.t_Code_Currency = vanl.t_FIID "
   + "        AND acnt.t_Open_Date <= " + GetSQLDate(ExpDepPrm.RestDate)
   + IIF (ExpDepPrm.iIssuer != UnknownParty, " AND fin.t_Issuer = " + ExpDepPrm.iIssuer + " ", "")
   + IIF (ExpDepPrm.iAvrKinds != FIKIND_ALLAVOIRISS, " AND RSB_FIInstr.FI_AvrKindsEQ( " + String( FIKIND_AVOIRISS ) + ", " + ExpDepPrm.iAvrKinds + ", fin.t_AvoirKind ) = 1 ", "")
   + IIF (ExpDepPrm.iFI != ALLFININSTR, " AND fin.t_FIID = " + ExpDepPrm.iFI + " ", "")
   + "        AND rsb_account.restsa (accsub.t_AccAnaliticsID, accsub.t_SubAccountID, " + GetSQLDate(ExpDepPrm.RestDate) + ", 1) <> 0 "
   + " ORDER BY fin.t_Issuer, vanl.t_FIID "
   + ") \n"
   + "      LOOP \n"
   + "        expdep.t_Issuer         := one_rec.t_Issuer; \n"
   + "        expdep.t_FIID           := one_rec.t_FIID; \n"
   + "        expdep.t_Account        := one_rec.t_Account; \n"
   + "        expdep.t_Client         := one_rec.t_Client; \n"
   + "        expdep.t_Rest_SA        := one_rec.t_Rest_SA; \n"
   + "        expdep.t_AttrNum        := one_rec.t_AttrNum; \n"
   + "        expdep.t_AttrVal        := one_rec.t_Value; \n"
   + "        g_expdep_ins.extend; \n"
   + "        g_expdep_ins(g_expdep_ins.LAST) := expdep; \n"
   + "      END LOOP; \n"
   + "      IF g_expdep_ins IS NOT EMPTY THEN \n"
   + "          FORALL i IN g_expdep_ins.FIRST .. g_expdep_ins.LAST \n"
   + "               INSERT INTO DDPEXPDEP_TMP \n"
   + "                    VALUES g_expdep_ins(i); \n"
   + "          g_expdep_ins.delete; \n"
   + "      END IF; \n"
   + " END; \n";

   InitProgress( 1, header, header );
   SQL_Execute(query, header, false);
   UseProgress(1);
   RemProgress();
end;

private macro Fill_1_Issuer(xml_file, ShortName, val1)

   CorrectValue(@ShortName, V_STRING);
   CorrectValue(@val1, V_STRING);

   xml_file.Add1_Issuer(CorrectValue("", V_STRING), val1, ShortName);
end;

private macro _ExpDepList()
   var stat = 0, query, cmd, dataSet, i = 0, cnt = 0;
   var xml_file, prevIssuerID = UnknownParty, fileSaved = false, isFill_parts_1_9 = false;
   var fi = TRecHandler("fininstr"), avoir = TRecHandler("avoiriss"), issuer = TRecHandler("party"), client = TRecHandler("party");

   Rep = CMakeReport();
   Log.InitErrLog( Rep, true );
   Log.StartErrLog();

   LoadDataInTMP();

   query = " SELECT DISTINCT t_Issuer, t_Client FROM DDPEXPDEP_TMP ORDER BY t_Issuer, t_Client ";
   cmd = DL_RSDCommand(query);
   cnt = cmd.GetCount();
   if(cnt > 0)
      dataSet = cmd.Execute();
      InitProgress( cnt, "Обработка данных", "Обработка данных..." );

      // REGISTER_OF_SHAREHOLDERS
      while(DataSet.moveNext())
         if((prevIssuerID != UnknownParty) and (prevIssuerID != DataSet.Issuer) and (xml_file != null))
            xml_file.SaveXML();
            xml_file = null;
            fileSaved = true;
         end;

         if(xml_file == null)
            ПолучитьСубъекта(dataSet.Issuer, issuer);
            xml_file = TDpExpDep_XML();
            xml_file.Add_Header(issuer.rec.ShortName);
            fileSaved = false;
            isFill_parts_1_9 = false;
         end;

         if(not isFill_parts_1_9)
            // 1. issuer
            Fill_1_Issuer(xml_file, issuer.rec.ShortName, ExpDepPrm.sIssuer);

            // 2. account_dtls
            xml_file.Add2_account_dtls(ExpDepPrm.RegistrAcc, "01");

            // 3. account_holder
            xml_file.Add3_account_holder();

            // 4. corporate_action_code
            xml_file.Add4_corporate_action_code("DSCL");

            // 5. corporate_action_reference
            xml_file.Add5_corporate_action_reference(ExpDepPrm.RefAc);

            // 6. corporate_action_reference_NDC
            xml_file.Add6_corporate_action_reference_NDC(ExpDepPrm.RefAc);

            // 7. record_date
            xml_file.Add7_record_date(ExpDepPrm.RestDate);

            // 8. message_function
            xml_file.Add8_message_function("NEWM");

            // 9. information_indicator
            xml_file.Add9_information_indicator("OWNL");

            isFill_parts_1_9 = true;
         end;

         // 11. register_list
         ПолучитьСубъекта(dataSet.Client, client);
         xml_file.Add11_register_list(dataSet.Issuer, client);

         prevIssuerID = DataSet.Issuer;

         UseProgress(i = i + 1);
      end;

      if((not fileSaved) and (xml_file != null))
         xml_file.SaveXML();
         xml_file = null;
         fileSaved = true;
      end;
      RemProgress();
   else
      Log.LogError("Нет данных");
   end;

   Log.FinishErrLog();
    //Напечатать протокол
   Log.ShowErrLog(true);
   Rep.SetZoomType( ZOOM_TYPE_A4L );// альбомная ориентация
   Rep.PrintWinRep();
   Rep.ShowWinRep();

   return stat;
end;

macro DP_ExpDepList(
   iNumDprt:Integer
  ,RestDate:Date
  ,RegistrAcc:String
  ,iIssuer:Integer
  ,sIssuer:String
  ,iAvrKinds:Integer
  ,iFI:Integer
  ,IncomeAcc:String
  ,RefAc:String
)
   InitError();

   ExpDepPrm.SetData(iNumDprt, RestDate, RegistrAcc, iIssuer, sIssuer, iAvrKinds, iFI, IncomeAcc, RefAc);

   return _ExpDepList();
end;
