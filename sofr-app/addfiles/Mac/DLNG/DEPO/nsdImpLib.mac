/*
$Name:         nsdImpLib.mac
$Module:       Депозитарий
$Description:  ...
*/

import "dlmisc.mac", "dpimp.mac", "dpstdrep.mac";

const DOCKIND_REPINFREQ        = 411; // Отчеты по информационному запросу
const DOCKIND_REPEXTRREFREGDEP = 412; // Отчет/выписка/справка от регистратора/депозитария
const DOCKIND_CLEARINGREP      = 413; // Отчет о клиринге
const DOCKIND_REPMESCORPACTN   = 414; // Отчет/сообщение о корпоративном действии
const DOCKIND_LISTSECUROWNERS  = 415; // Список владельцев ц/б
const DOCKIND_CORPACTNREQ      = 416; // Запрос корпоративного действия
const DOCKIND_INSTRUCTIONTONSD = 417; // Инструкция/поручение в НРД

const REFOBJ_DPENTDOC_BH  = 9;  // Номер вх. документа ВН, полученного через СЭД ЛУЧ
const REFOBJ_DPENTDOC_BHK = 10; // Номер вх. документа ВНК, полученного через СЭД ЛУЧ
const REFOBJ_DPENTDOC_BHC = 11; // Номер вх. документа ВНС, полученного через СЭД ЛУЧ
const REFOBJ_DPENTDOC_3KD = 12; // Номер вх. документа ЗКД, полученного через СЭД ЛУЧ
const REFOBJ_DPENTDOC_BKD = 13; // Номер вх. документа ВКД, полученного через web-кабинет КД
const REFOBJ_DPENTDOC_SWI = 14; // Номер вх. документа SWI, полученного через SWIFT
const REFOBJ_DPENTDOC_UUJ = 15; // Номер исх. документа ИИЛ, отправленного в НРД
const REFOBJ_DPENTDOC_UKD = 16; // Номер исх. документа ИКД, отправленного в НРД

const DONE_DIR = "Done";
const ERR_DIR = "Err";

class CNSDReport()
    var RepNo = "";
    var RepDate = ZeroDate;
    var RepTime = ZeroTime;
    var OpCode = "";
    var OpName = "";
end;

class CNSDImportReportsBase()
    private var m_Root = "";
    private var m_Party = UnknownParty;
    private var m_Date = {CurDate}, m_Time = Time();
    private var m_Protocol = null;
    private var m_Report = CNSDReport();
    private var m_Count = 0;

    private macro checkAltXld(kind, altXld, signedDate)
        var err = 0;
        var query = "", cmd = null, set = null;

        query = " SELECT "
                  + " spgr.t_Xld "
                 + " ,spgr.t_RegistrDate "
              + " FROM "
                  + " dspground_dbt spgr "
              + " WHERE "
                  + " spgr.t_Kind = ? "
              + " AND spgr.t_AltXld = ? "
              + " AND spgr.t_SignedDate = ? ";

        cmd = RsdCommand(query);
        cmd.AddParam("", RSDBP_IN, kind);
        cmd.AddParam("", RSDBP_IN, altXld);
        cmd.AddParam("", RSDBP_IN, signedDate);
        cmd.NullConversion = true;
        cmd.Execute();

        set = RsdRecordset(cmd);

        if(set.MoveNext())
            err = -1;
            m_Protocol.AddReport1(m_Report, "Документ с таким регистрационным номером уже зарегистрирован в системе, входящий номер: " + set.Value("t_Xld") + " от " + date_as_string(1, Date(set.Value("t_RegistrDate")), false));
        end;

        return err;
    end;

    private macro getRefID(refType, refID)
        var err = 0, errStr = "";

        err = GetReferenceIDByType(OBJTYPE_KINDDOC, refType, refID);

        if(err == 0)
            SetParm(2, refID);
        else
            errStr = "Не найден описатель референса";
            m_Protocol.AddError(errStr);
            m_Protocol.AddReport1(m_Report, errStr);
        end;

        return err;
    end;

    private macro checkXld(kind, xld, direction, department, division, registrDate)
        var err = 0;
        var query = "", cmd = null, set = null;

        query = " SELECT "
                  + " 1 "
              + " FROM "
                  + " DUAL "
              + " WHERE "
                  + " EXISTS (SELECT "
                              + " 1 "
                          + " FROM "
                              + " dspground_dbt spgr "
                          + " WHERE "
                              + " spgr.t_Kind = ? "
                          + " AND spgr.t_Xld = ? "
                          + " AND spgr.t_Direction = ? "
                          + " AND spgr.t_Department = ? "
                          + " AND spgr.t_Division = ? "
                          + " AND spgr.t_RegistrDate = ?) ";


        cmd = RsdCommand(query);
        cmd.AddParam("", RSDBP_IN, kind);
        cmd.AddParam("", RSDBP_IN, xld);
        cmd.AddParam("", RSDBP_IN, direction);
        cmd.AddParam("", RSDBP_IN, department);
        cmd.AddParam("", RSDBP_IN, division);
        cmd.AddParam("", RSDBP_IN, registrDate);
        cmd.NullConversion = true;
        cmd.Execute();

        set = RsdRecordset(cmd);

        if(set.MoveNext())
            err = 1;
        end;

        return err;
    end;

    private macro generateXld(refID, kind, xld, direction, department, division, registrDate)
        var err = 0, errStr = "";

        while(err == 0)
            if(GenerateReference(refID, xld))
                err = 1;
                errStr = "Ошибка при генерации референса по описателю " + refID;
                m_Protocol.AddError(errStr);
                m_Protocol.AddReport1(m_Report, errStr);
            end;

            if(err == 0)
                if(checkXld(kind, xld, direction, department, division, registrDate) == 0)
                    SetParm(3, xld);
                    break;
                end;
            end;
        end;

        return err;
    end;

    private macro restoreXld(refID, xld)
        RestoreReference(refID, xld);
    end;

    private macro getPathToDir(dir)
        var path = "";
        var day = 0, month = 0, year = 0;
        var hour = 0, minute = 0, sec = 0;

        path = m_Root + dir + "\\";

        DateSplit(IIF(dir == DONE_DIR, m_Report.RepDate, m_Date), day, month, year);
        path = path + L_Z(String(day:2), 2) + L_Z(String(month:2), 2) + L_Z(String(year:4), 4) + "\\";

        if(dir == DONE_DIR)
            path = path + m_Report.RepNo + "\\";
        end;

        return path;
    end;

    private macro moveFiles(pathFrom, pathTo, mask)
        var err = 0;
        var dirs = SplitString(pathTo, "\\");
        var i = 0;
        var path = "";
        var dl = TDirList(pathFrom + mask, "F");

        while(i < dirs.Size)
            path = path + dirs[i] + "\\";
            if((dirs[i] != ".")
           and (dirs[i] != ".."))
                MakeDir(path);
            end;
            i = i + 1;
        end;

        i = 0;
        while(i < dl.Count)
            if(CopyFile(pathFrom + dl.Name(i), pathTo + dl.Name(i)))
                RemoveFile(pathFrom + dl.Name(i));
            else
                err = -1;
                m_Protocol.AddError("Ошибка при перемещении файла " + dl.Name(i) + " в папку " + pathTo);
            end;

            i = i + 1;
        end;

        return err;
    end;
end;

private class (DL_XML) CXMLFileWCCA()
    macro Load(name)
        var err = 0;

        m_xml = CreateXMLObj();
        m_FileName = name;

        if((m_xml == null) or not m_xml.load(name))
            err = 1;
        end;

        return err;
    end;

    macro GetXML()
        return m_xml;
    end;

    initDL_XML;
end;

class (CNSDImportReportsBase) CNSDImportReportsBaseXML()
    private var m_XMLFile = CXMLFileWCCA();

    private macro getAcc(tag, acc)
        var err = 0;
        var node = null;

        node = m_XMLFile.GetXML().selectSingleNode(tag);
        if(node != null)
            acc = node.nodeTypedValue;

            if(acc != "")
                acc = SubStr(acc, 1, 12);
            else
                err = -1;
            end;
        else
            err = -1;
        end;

        if(err == 0)
            SetParm(2, acc);
        end;

        return err;
    end;

    private macro processFile()
        var err = 1;

        return err;
    end;

    private macro processDirs(path)
        var err = 0;
        var dl = null;
        var i = 0;

        dl = TDirList(m_Root + path + "*.xml", "F");

        while(i < dl.Count)
            if(m_XMLFile.Load(m_Root + path + dl.Name(i)) == 0)
                err = processFile();
            else
                err = -1;
                m_Protocol.AddError("Невозможно открыть файл " + m_Root + path + dl.Name(i));
            end;

            if(err >= 0)
                err = moveFiles(m_Root + path, getPathToDir(IIF(err == 0, DONE_DIR, ERR_DIR)) + path, dl.Name(i));
            end;

            UseProgress(m_Count = m_Count + 1);

            i = i + 1;
        end;
    end;

    initCNSDImportReportsBase;
end;
