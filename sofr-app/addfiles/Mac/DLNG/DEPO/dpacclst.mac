/*
$Name:         dpacclst.mac
$Module:       Депозитарий
$Description:  Печать отчета "Справочник лицевых счетов депо".
*/

import BankInter, CurrInter, PTInter, SPInter, dprepsec;

const Depo_chapt =  5;    /* Глава синтетического учета */

const ACCKINDALL   = 0,
      ACCKINDACTIV = 1,
      ACCKINDPASS  = 2;

const ACCOPEN  = 1,
      ACCCLOSE = 2,
      ACCALL   = 3;

const ANYBALLANS = "";

var query = "";

var isPrint = false; /* Хоть что-нибудь напечаталось */

var HeadTable = "┌────────────────────────────┬───────────┬───────────┬───────────────────────┬──────────┬────────┐\n"+
                "│  № л/с                     │Счет депо  │Раздел     │Владелец               │ДПД       │Статус  │\n"+
                "├────────────────────────────┼───────────┼───────────┼───────────────────────┼──────────┼────────┤";

var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

macro CreateHeadline(iAccKind:integer,
                       BlncAc:string,
                      iStatus:integer)
   /*[ вида   пассивный   на балансовом  98040 со статусом открытый];*/
   var str = "";

   if(iAccKind == ACCKINDACTIV)
      str = "вида активный   ";
   elif(iAccKind == ACCKINDPASS)
      str = "вида пассивный   ";
   else
      str = "любого вида   ";
   end;

   if(BlncAc == "")
      str = str + "на любом балансовом   ";
   else
      str = str + "на балансовом " + BlncAc + "   ";
   end;

   if( iStatus == ACCOPEN)
      str = str + "со статусом открытый";
   elif( iStatus == ACCCLOSE)
      str = str + "со статусом закрытый";
   else
      str = str + "с любым статусом ";
   end;

   return str;
end;

macro AccSt(Open_Close)
   if  (Open_Close == UNSET_CHAR)
      return "Открытый";
   else
      return "Закрытый";
   end;
end;

macro AccountList( iDepart:integer,
                   rep_date:date,
                   reportXld:string,
                   iAccKind:integer,
                   BlncAc:string,
                   iStatus:integer,
                   ToExcel:bool )

     var account;
     var stat = 0;
     var SaveBalance = BlncAc, count = 0, i= 0;
     var cClient;
     var LastCarryDate = Date( 0, 0, 0 );

     Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
     DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

     query = " select "
             + " acc.t_Open_Close "
            + " ,acc.t_Code_Currency "
            + " ,acc.t_Account "
            + " ,acc.t_Balance "
            + " ,acc.t_Client "
            + " ,acc.t_Chapter "
            + " ,acc.t_DepoAcc "
            + " ,acc.t_DepoRoot "
            + " ,acnt1.t_BriefCode as BCode1 "
            + " ,acnt2.t_BriefCode as BCode2 "
           + " from "
             + " daccount_dbt acc "
            + " ,ddepoacnt_dbt acnt1 "
            + " ,ddepoacnt_dbt acnt2 "
           + " where "
             + " acc.t_Chapter = " + Depo_Chapt
         + " and acc.t_Department = " + iDepart
         + " and acnt1.t_AutoKey = acc.t_DepoRoot "
         + " and acnt2.t_AutoKey = acc.t_DepoAcc ";

     if(BlncAc != ANYBALLANS )
       query = query + " and acc.t_Balance = " + BlncAc;
     end;

     if( iStatus == ACCOPEN )
       query = query + " and acc.t_Open_Close = CHR(0) ";
     elif( iStatus == ACCCLOSE )
       query = query + " and acc.t_Open_Close = 'З' ";
     end;

     if(iAccKind == ACCKINDACTIV)
       query = query + " and acc.t_Kind_Account = 'А' ";
     elif(iAccKind == ACCKINDPASS)
       query = query + " and acc.t_Kind_Account = 'П' ";
     end;

     query = query + " order by acc.t_Balance, acc.t_Code_Currency, acc.t_Account ";

     account = TRsbDataSet(query,  RSDVAL_CLIENT, RSDVAL_STATIC );
     account.MoveLast();
     count = account.GetRecCount();

     if( count > 0 )
       InitProgress ( count, "Поиск счетов для отчета", "Справочник лицевых счетов депо");
       stat = account.moveFirst();
       while ( stat )

             if ( isPrint == false )
                 DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Список лицевых счетов главы \"Д\" ", 0, 0, false, rep_date, null, tablewidth );
                 DP_AddPrintCell( Rep, "  " + CreateHeadline(iAccKind, BlncAc, iStatus), tablewidth, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
                 Rep.AddStr();
               if(BlncAc != ANYBALLANS)
                  Rep.AddEmptyStr();
                  DP_AddPrintCell( Rep, "    Б/С: " + BlncAc, tablewidth, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
                  Rep.AddStr();
               end;
             isPrint = true;
             end;

             if(account.Balance != SaveBalance)
                SaveBalance = account.Balance;

                    Rep.AddEmptyStr();
                    DP_AddPrintCell( Rep, "    Б/С: " + account.Balance, tablewidth, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
                    Rep.AddStr();
             end;

             cClient = CDPParty( account.Client, rep_date );
             GetLastCarryDate( account.Chapter, account.Code_Currency, account.Account, LastCarryDate, rep_date );
             /*[1234567890123456765412346  P1234       P1032101    Ваня Иванов          05.06.2001  Открытый];*/
             DP_AddPrintCell( Rep, account.Account, 0, 0, "l:" + RepFontStyleTitel );
             DP_AddPrintCell( Rep, account.BCode1, 0, 0, "l:" + RepFontStyleTitel );
             DP_AddPrintCell( Rep, account.BCode2, 0, 0, "l:" + RepFontStyleTitel );
             DP_AddPrintCell( Rep, cClient.Name(), 0, 0, "l:w:" + RepFontStyleTitel );
             DP_AddPrintCell( Rep, LastCarryDate, 0, 0, "l:f:" + RepFontStyleTitel );
             DP_AddPrintCell( Rep, AccSt(account.Open_Close), 0, 0, "l:" + RepFontStyleTitel );
             Rep.AddStr();
             UseProgress (i = i + 1);
             stat = account.moveNext();
       end;

       RemProgress();
       DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

       DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
       DP_EndProtocol();                 //закончить протоколирование

       if( ToExcel )
         Rep.PrintWinRep();
         Rep.ShowWinRep();
       else
         Rep.PrintRep();
       end;
     else
       if(not DP_ProtocolIsEmpty())
         DP_AddPrintCell(Rep, "Не найдено ни одного лицевого счета ДЕПО, удовлетворяющего заданным параметрам.", 85, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR);
         Rep.AddStr();

         DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
         DP_EndProtocol();                 //закончить протоколирование

       if( ToExcel )
         Rep.PrintWinRep();
         Rep.ShowWinRep();
       else
         Rep.PrintRep();
       end;
     else
         DP_EndProtocol();                 //закончить протоколирование
       end;

       MsgBox("Не найдено ни одного лицевого счета ДЕПО, удовлетворяющего заданным параметрам.");
     end;
  return 0;
end;
