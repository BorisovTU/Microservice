/*
$Name:             dpressrvopstart.mac
$Module:           Депозитарий 
$Description:      Выполнение сервисной операции расчета резерва по комиссиям Депозитария
*/

import InsCarryDoc, DealsInter, dpstdrep, "mccatacc.mac", "dlquery.mac", PaymInter, OprInter, "dlcarry.inc", "dllog_xml.mac";

private var RepErrArr = TArray(); //массив ошибок

/*ЛОГ*/
/*класс с данными для списка ошибок в протоколе*/
class CDP_ResSrvRepErr(_ObjTypeName:string, _ObjCode:string, _StatusName:string, _CalcDate:string, _DefaultDays:string, _ErrStr:string)
 var ObjTypeName  = _ObjTypeName;
 var ObjCode      = _ObjCode;
 var StatusName   = _StatusName;
 var CalcDate     = _CalcDate;
 var DefaultDays  = _DefaultDays;
 var ErrStr       = _ErrStr;
end;

PRIVATE VAR CurrResObjData = CDP_ResSrvRepErr("", "", "", "", "", ""); //данные текущего обрабатываемого объекта

PRIVATE MACRO SetCurrResObjData(ObjTypeName:string, ObjCode:string, StatusName:string, CalcDate:date, DefaultDays:integer)
  CurrResObjData = CDP_ResSrvRepErr(ObjTypeName, 
                                    ObjCode, 
                                    StatusName, 
                                    IIF(CalcDate == date(0,0,0), "", string(CalcDate:f)), 
                                    string(DefaultDays), 
                                    "");
END;

PRIVATE MACRO ClearCurrResObjData()
  SetCurrResObjData("", "", "", date(0,0,0), 0);
END;


macro DepoRes_msgbox()
  var i = 0, StrErr = "", str = "";

  while(getparm(i,str))
    StrErr = StrErr + string(str);
    str = "";
    i = i + 1;
  end;

  RepErrArr[RepErrArr.size] = CDP_ResSrvRepErr(CurrResObjData.ObjTypeName, 
                                               CurrResObjData.ObjCode, 
                                               CurrResObjData.StatusName, 
                                               CurrResObjData.CalcDate, 
                                               CurrResObjData.DefaultDays, 
                                               StrErr);
end;


/*лог ошибок*/
PRIVATE CLASS (DL_LOG_FROM_XML) DP_ERROR_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()
     var ErrorData:CDP_ResSrvRepErr;
     
     if( m_ReportVersion == 1 )

        if(DL_ReadTagAttribut(m_child_ReportNumber, "OBJTYPENAME", V_STRING,  ErrorData.ObjTypeName) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "OBJCODE",     V_STRING,  ErrorData.ObjCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "STATUSNAME",  V_STRING,  ErrorData.StatusName) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CALCDATE",    V_STRING,  ErrorData.CalcDate) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "DEFAULTDAYS", V_STRING,  ErrorData.DefaultDays) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRSTR",      V_STRING,  ErrorData.ErrStr) == false)
        end;
        
        return ErrorData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

/*лог ошибок СО*/
CLASS (DL_LOG_DATA_XML) DP_DEPO_ERROR_LOG_DATA_XML(pDocKind:integer, pDocumentID:integer, pDataParms:TArray, pType:integer, pVersion:integer)

  PRIVATE MACRO СоздатьУзелОшибок(i)
     var УзелОшибок = null;

     if( m_Version == 1 )
        УзелОшибок = CreateElement("Property",
                                   "ObjTypeName", m_DataParms[i].ObjTypeName, 
                                   "ObjCode",     m_DataParms[i].ObjCode, 
                                   "StatusName",  m_DataParms[i].StatusName,
                                   "CalcDate",    m_DataParms[i].CalcDate,
                                   "DefaultDays", m_DataParms[i].DefaultDays,
                                   "ErrStr",      m_DataParms[i].ErrStr
                                  );
     else
        УзелОшибок = CreateElement("Property");
     end;
     
     return УзелОшибок;
  END;


  initDL_LOG_DATA_XML(pDocKind, pDocumentID, pDataParms, pType, pVersion);

END;

PRIVATE MACRO GetLogDataXML( DocKind:integer, DocumentID:integer, Type:integer, OnlyLastReport:bool )
   var xml_obj = null;

   if(Type == LOG_TYPE_ERROR )
      xml_obj = DP_ERROR_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   end;

   return xml_obj;
END;

macro SaveDepoResErrForReport_XML( RepErrArr, DocumentID:integer, DocKind:integer )

  var RepXML = "";
  
  if( RepErrArr.Size > 0 )
     RepXML = DP_DEPO_ERROR_LOG_DATA_XML(DocKind,DocumentID,RepErrArr,LOG_TYPE_ERROR).GetDataXML();
  end;

  if( RepXML != "" )
     var Header = DL_LOG_HEADER_XML(DocKind).GetHeaderXML();

     DL_InsertHeaderLog(DocumentID, DocKind, {Oper}, Header);
     DL_InsertLogXMLData( DocumentID, DocKind, LOG_TYPE_ERROR, {Oper}, RepXML);
  end;

  RepErrArr.size = 0;
end;

///////////////////////////////////////////////////////////////////////////////////////////////////////
PRIVATE CLASS (DL_CReportTemplate) DP_DepoResSrvOp_Report(dl_comm)
  PRIVATE VAR m_comm = TRecHandler("dl_comm");

  PRIVATE MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;
  
  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    CreateTotalBook();
  END;

  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;


  PRIVATE MACRO Клиенты()
    var cmd, query, DataSet;
    var cmd1, query1, ds;
    var TplSheetName = "Клиент";
    var CurrSheetName = "";
    var TypeStr = "";
    var PrevBaseAccountID = 0, PrevClientID = -1;
    var stat = 0;
    var i = 0, cnt = 0;
    var TotalReserveSum = 0.0;

    cmd = DL_RSDCommand();

    query =   " select base.t_ClientID, base.t_ID, "
            + "        pt.t_Name as ClientName, "
            + "        NVL((select code.t_Code from dobjcode_dbt code where code.t_ObjectType = "+OBJTYPE_PARTY+" and code.t_CodeKind = "+PTCK_CONTR+" and code.T_State = 0 and code.t_ObjectID = base.t_ClientID), CHR(1)) as ClientCode, "
            + "        base.t_BaseAccountID, base.t_OldSum, base.t_CorrectSum, base.t_NewSum, "
            + "        (select acc.t_Account from daccount_dbt acc where acc.t_AccountID = base.t_BaseAccountID) as BaseAccount, "
            + "        (select acc.t_Account from daccount_dbt acc where acc.t_AccountID = base.t_ReserveAccountID) as ReserveAccount "
            + "   from ddpresbase_dbt base, dparty_dbt pt "
            + "  where base.t_ServDocKind = ? "
            + "    and base.t_ServDocID = ? "
            + "    and pt.t_PartyID = base.t_ClientID "
            + "  order by base.t_ClientID ASC, base.t_BaseAccountID ASC ";

    cmd.AddParam(m_comm.rec.DocKind);
    cmd.AddParam(m_comm.rec.DocumentID);
    
    cnt = cmd.GetCount(query);
    if(cnt > 0)

      InitProgress( cnt, "Идет выполнение ...", "Формирование протокола" );

      SetActiveSheet(TplSheetName);

      DataSet = cmd.Execute(query);
      while(DataSet.MoveNext())

        if(PrevClientID != DataSet.ClientID)
          CurrSheetName = DataSet.ClientCode;

          UseNewSheetInTotalBook();
        end;

        if(PrevBaseAccountID != DataSet.BaseAccountID)
          PrintFormatString(NULL,
                           "N1_A1",    m_comm.rec.CommCode,
                           "N1_A2",    string(m_comm.rec.CommDate:f),
                           "N1_A3",    DataSet.CLientName,
                           "N1_A5_1",  DataSet.BaseAccount,
                           "N1_A5_2",  DataSet.ReserveAccount,
                           "N1_R1",    DataSet.OldSum,
                           "N1_Delta", DataSet.CorrectSum,
                           "N1_R2",    DataSet.NewSum
                           );   
          CopyAllSheetInTotalBook(TplSheetName, false, "N1_RepHeader", 0, CurrSheetName);
        end;

        PrevClientID      = DataSet.ClientID;
        PrevBaseAccountID = DataSet.BaseAccountID;

        
        //ТО
        query1 =   " select NVL(inv.t_DocNumber, CHR(1)) as t_Number, "
                 + "        NVL((select stv.t_Name from doprstval_dbt stv where stv.t_StatusKindID = "+SFINV_OPRPAYSTATUS+" and stv.t_NumValue = obj.t_Status), CHR(1)) as t_StatusName, "
                 + "        NVL((select llv.t_Name from dllvalues_dbt llv where llv.t_List = "+OBJTYPE_COMINVMETHOD+" and llv.t_Element = obj.t_InvMethod), CHR(1)) as t_MethodName, "
                 + "        obj.t_Date, obj.t_DefaultDays, obj.t_ReservePrc, obj.t_BaseSum, obj.t_ReserveSum "
                 + "   from ddpresobj_dbt obj "
                 + "        left join dsfinv_dbt inv on inv.t_InvoiceID = obj.t_InvoiceID "
                 + "  where obj.t_BaseID = ? "
                 + "    and obj.t_InvoiceID > 0 ";

        cmd1 = DL_RSDCommand(query1);
        cmd1.AddParam(DataSet.ID);

        cnt = cmd1.GetCount();
        if(cnt > 0)
          TotalReserveSum = 0.0;

          CopyAllSheetInTotalBook(TplSheetName, false, "N1_TableHeader1", 1, CurrSheetName);
          RegisterTable( "N1_MainTable1",   NULL,
                         "N1_N",  
                         "N1_T1",  
                         "N1_T2",    
                         "N1_T3",    
                         "N1_T4", 
                         "N1_T5",    
                         "N1_T6",
                         "N1_T7"
                       );

          ds = cmd1.Execute();
          while(ds.moveNext())
            PrintTableLine( "N1_N",  ds.Number,      null,
                            "N1_T1", ds.StatusName,  null,
                            "N1_T2", ds.MethodName,  null,
                            "N1_T3", date(ds.Date),  null,
                            "N1_T4", ds.DefaultDays, 0,
                            "N1_T5", ds.ReservePrc,  2,
                            "N1_T6", ds.BaseSum,     2,
                            "N1_T7", ds.ReserveSum,  2
                          );

            TotalReserveSum = TotalReserveSum + ds.ReserveSum;
          end;

          //Итоговая строка
          merge("N1_N", "N1_T6");
          PrintTableLine("N1_N",  "Итого по требованиям на оплату:", null,
                         "N1_T1", null,  null,
                         "N1_T2", null,  null,
                         "N1_T3", null,  null,
                         "N1_T4", null,  null,
                         "N1_T5", null,  null,
                         "N1_T6", null,  null,
                         "N1_T7", TotalReserveSum, 2
                        );

          EndTable();
          CopyAllSheetInTotalBook(TplSheetName, false, GetTableRange(), 0, CurrSheetName );
        end;

        //Комиссии
        query1 =   " select NVL((select cm.t_Name from dsfcomiss_dbt cm where cm.t_FeeType = sfdef.t_FeeType and cm.t_Number = sfdef.t_CommNumber), CHR(1)) as t_CommName, "
                 + "        NVL((select llv.t_Name from dllvalues_dbt llv where llv.t_List = "+OBJTYPE_COMPAYSTATUS+" and llv.t_Element = obj.t_Status), CHR(1)) as t_StatusName, "
                 + "        obj.t_Date, obj.t_DefaultDays, obj.t_ReservePrc, obj.t_BaseSum, obj.t_ReserveSum "
                 + "   from ddpresobj_dbt obj "
                 + "        left join dsfdef_dbt sfdef on sfdef.t_ID = obj.t_SfDefID "
                 + "  where obj.t_BaseID = ? "
                 + "    and obj.t_SfDefID > 0 ";

        cmd1 = DL_RSDCommand(query1);
        cmd1.AddParam(DataSet.ID);

        cnt = cmd1.GetCount();
        if(cnt > 0)
          TotalReserveSum = 0.0;

          CopyAllSheetInTotalBook(TplSheetName, false, "N1_TableHeader2", 1, CurrSheetName);
          RegisterTable( "N1_MainTable2",   NULL,
                         "N1_K1",  
                         "N1_K2",    
                         "N1_K3",    
                         "N1_K4", 
                         "N1_K5",    
                         "N1_K6",
                         "N1_K7"
                       );

          ds = cmd1.Execute();
          while(ds.moveNext())
            PrintTableLine( "N1_K1", ds.CommName,    null,
                            "N1_K2", ds.StatusName,  null,
                            "N1_K3", date(ds.Date),  null,
                            "N1_K4", ds.DefaultDays, 0,
                            "N1_K5", ds.ReservePrc,  2,
                            "N1_K6", ds.BaseSum,     2,
                            "N1_K7", ds.ReserveSum,  2
                          );

            TotalReserveSum = TotalReserveSum + ds.ReserveSum;
          end;

          //Итоговая строка
          merge("N1_K1", "N1_K6");
          PrintTableLine("N1_K1", "Итого по комиссиям:", null,
                         "N1_K2", null,  null,
                         "N1_K3", null,  null,
                         "N1_K4", null,  null,
                         "N1_K5", null,  null,
                         "N1_K6", null,  null,
                         "N1_T7", TotalReserveSum, 2
                        );

          EndTable();
          CopyAllSheetInTotalBook(TplSheetName, false, GetTableRange(), 0, CurrSheetName );
        end;

        UseProgress(i = i + 1);
      end;

      RemProgress();
    end;

  END;

  PRIVATE MACRO Сообщения()
    var ReportLineData_XML:variant;
    var ErrData:CDP_ResSrvRepErr;
    var query, cmd, DataSet;
    var cntobj = 0;
    var stat = 0;
    var SheetName = "Сообщения";

    SetActiveSheet( SheetName );

    UseNewSheetInTotalBook();
    
    cmd = DL_RSDCommand("select 1 from ddpresbase_dbt where t_ServDocKind = ? and t_ServDocID = ?");
    cmd.AddParam(m_comm.rec.DocKind);
    cmd.AddParam(m_comm.rec.DocumentID);
    cntobj = cmd.GetCount();

    ReportLineData_XML = GetLogDataXML(m_comm.rec.DocKind, m_comm.rec.DocumentID, LOG_TYPE_ERROR, false);
    ReportLineData_XML.SetReportNumber(0);
    
    stat = ReportLineData_XML.Next();
    if((stat) or (cntobj == 0))
      m_ObjTemplateXLS.SetSheetColor( XlColorIndex_RED );

      CopyAllSheetInTotalBook( SheetName, false, "N2_TableHeader", 0, SheetName );
      RegisterTable( "N2_MainTable",   NULL,
                     "N2_M1",   
                     "N2_M2",
                     "N2_M3",
                     "N2_M4",
                     "N2_M5",
                     "N2_M6" 
                   );

      if(not stat)
        PrintTableLine( "N2_M1:c",    "",                            null,
                        "N2_M2:c",    "",                            null,
                        "N2_M3:c",    "",                            null,
                        "N2_M4:c",    "",                            null,
                        "N2_M5:c",    "",                            null,
                        "N2_M6:l",    "Нет подходящих ТО и комиссий", null
                      );
      else
        while(stat)
          ErrData = ReportLineData_XML.GetReportLineData();

          PrintTableLine( "N2_M1:c",   ErrData.ObjTypeName, null,
                          "N2_M2:l:w", ErrData.ObjCode,     null,
                          "N2_M3:l:w", ErrData.StatusName,  null,
                          "N2_M4:c",   ErrData.CalcDate,    null,
                          "N2_M5:r",   ErrData.DefaultDays, null,
                          "N2_M6:l:w", ErrData.ErrStr,      null
                        );

          stat = ReportLineData_XML.Next();
        end;
      end;

      EndTable();
      CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );
    else
      PrintFormatString(NULL,
                       "N2_OprStateMsg", "Операция завершена успешно");
      CopyAllSheetInTotalBook( SheetName, false, "N2_OprStateMsg", 0, SheetName );
    end; 

  END; 

  PRIVATE MACRO Create()
    Клиенты();
    Сообщения();
  END;

  m_comm = dl_comm;

  initDL_CReportTemplate( NULL, "Report_DepoResSrvOp.xls");

END;


MACRO PrintReport(DocumentID:integer)
  var rep;
  var dl_comm = TBfile("dl_comm.dbt");
  
  dl_comm.rec.DocumentID = DocumentID;
  if(not dl_comm.GetEQ())
    msgbox("Ошибка поиска операции");
    return;
  end;

  rep = DP_DepoResSrvOp_Report(dl_comm);
  rep.Run();
END;


///////////////////////////////////////////////////////////////////////////////////////////////////////
private macro GetAccount(AccountID:STRING, acc:TRecHandler)
  var cmd, DataSet;
  var err = 0;

  cmd = DL_RSDCommand(  "select * from daccount_dbt"
                      + " where t_AccountID = ? "
                     ); 
  cmd.AddParam(AccountID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo( acc.rec );
  else
    err = 1;
  end;

  return err;
end;


class DP_AccResFD(AccountID:integer)
  private var acc = TRecHandler("account.dbt");
  private var FIRoleBArray = TArray;

  var Error = 0, Kind, ID;

  MACRO CorrectAccount(account, accblnc, ORScheme, categ, templ, accdoc,  OperDate)
     return true;
  END;

  macro GetParametr( ParmKind, OperDate, CatCode, FIRole )
    var Parametr = -1;

    if( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
      Parametr = acc.rec.Branch;
    end;

    return Parametr;
  end;

  macro GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole )
    var Parametr = -1;
    
    if( Classificator == LLCLASS_RSRVLOSS_BY_ACC )
      Parametr = 13; //Требования по прочим операциям
    elif( Classificator == LLCLASS_KIND_OF_ASSETS_RESERV )
      Parametr = OBJTYPE_ACCOUNT;
    end;

    return Parametr;
  end;

  macro GetBasisFIRole(FIRole)
    return FIROLE_UNDEF;
  end;

  macro GetFIRoleBArray()
    return FIRoleBArray;
  end;

  MACRO OpenAccount( CatCode:STRING, FindAccount:STRING, NoErrMes:BOOL, AccBuf:VARIANT, ActionDate:DATE, FIID:INTEGER )
    var BackOutAccount = false, ChangeOpenDate  = false, IsMass;
    var ret = true;

    if( (NoErrMes == null) OR (NoErrMes == false) )
       IsMass = 0;
    else 
       IsMass = 1;
    end;
       
    MC_GetAccountOpenParms( CatCode, @BackOutAccount, @ChangeOpenDate );

    FindAccount = MC_FindAndOpenAccount (
        CatCode,
        this,
        ActionDate,
        IsMass,
        MC_OPENACC_CREATE,
        AccBuf, FIID, null, NULL, null, FIROLE_BA, NULL, NULL, NULL, NULL, NULL,
        BackOutAccount,
        ChangeOpenDate
    );

    if( FindAccount=="" ) 
      if( IsMass == 0 ) 
         MsgBox("Счет категории \"" + CatCode + "\" неопределен или недоступен в данной операции");
      end;
      ret = false;
    end;

    return ret;
  END;

  MACRO Construct( AccountID:integer )
    
    if(GetAccount(AccountID, acc) != 0)
      RunError( "Не найден лицевой счет.|Ошибка при создании класса "+GenClassName(this) );
    end;

    Kind = DLDOC_ACCOUNT;
    Id   = AccountID;

    FIRoleBArray[0] = 0;

  END;

  this.Construct( AccountID );
end;

//Получить процент резерва по количеству дней просрочки
PRIVATE MACRO GetPrcResByDefaultDays(DefaultDays:integer)
  var prc = -1;

  if(DefaultDays == 0)
    prc = 0;
  elif((DefaultDays >= 1) and (DefaultDays <= 5))
    prc = 3;
  elif((DefaultDays >= 6) and (DefaultDays <= 10))
    prc = 19;
  elif((DefaultDays >= 11) and (DefaultDays <= 30))
    prc = 49;
  elif(DefaultDays >= 31)
    prc = 100;
  end;

  return prc;
END;

//Выполнить расчет и начисление резерва по одному счету
PRIVATE MACRO CalcDepoCommReserveByAcc(dl_comm, AccountID:integer)
  var cmd, query, DataSet;
  var prc, sz;
  var SumsArr = TArray();
  var BaseAcc = TRecHandler("account.dbt");
  var ReservAcc = TRecHandler("account.dbt");
  var RSAcc = TRecHandler("account.dbt");
  var RSCatCode = "";
  var delta = 0;
  var err = 0;
  var accTrn;
  var i = 0;
  var Rnew = 0.0, Rold = 0.0;
  var fd;
  var resbase = TRecHandler("dpresbase.dbt");
  var resbaseCache = RsbSQLInsert("dpresbase.dbt");
  var resobjArr = TArray();
  var resobjCache = RsbSQLInsert("dpresobj.dbt");
  var realIDs = TArray();
  var ResBaseID = 0;

  if(GetAccount(AccountID, BaseAcc) != 0)
    msgbox("Ошибка при поиске счета");
    return 1;
  end;

  cmd = DL_RSDCommand();

  query =   " select RSB_FIInstr.ConvSum(q.t_NotPaidSum, q.t_PayFIID, "+NATCUR+", ?) as NotPaidSumRub, "
          + "        q.t_DefaultDays, "
          + "        q.t_InvoiceID, "
          + "        q.t_InvMethod, "
          + "        q.t_Date, "
          + "        q.t_Number, "
          + "        q.t_StatusKind, "
          + "        q.t_StatusName, "
          + "        q.t_SfDefID, "
          + "        q.t_CntLastOpr"
          + "   from (select (inv.t_TotalAmount - inv.t_PaidAmount) as t_NotPaidSum, "
          + "                inv.t_PayFIID, "
          + "                (? - (inv.t_InvoiceDate + inv.t_InvoiceDuration) + 1) as t_DefaultDays, "
          + "                inv.t_InvoiceID, "
          + "                inv.t_InvMethod, "
          + "                inv.t_InvoiceDate as t_Date, "
          + "                inv.t_DocNumber as t_Number, "
          + "                inv.t_InvoiceStatus as t_StatusKind, "
          + "                NVL((select stv.t_Name from doprstval_dbt stv where stv.t_StatusKindID = "+SFINV_OPRPAYSTATUS+" and stv.t_NumValue = inv.t_InvoiceStatus), CHR(1)) as t_StatusName, "
          + "                0 as t_SfDefID, "
          + "                (select count(1) "
          + "                   from ddl_comm_dbt cm, ddpresbase_dbt base, ddpresobj_dbt obj "
          + "                  where cm.t_DocKind = " + DP_CALCCOMMRESERV
          + "                    and cm.t_DocumentID <> ? "
          + "                    and cm.t_CommDate > ? "
          + "                    and base.t_ServDocKind = cm.t_DocKind " 
          + "                    and base.t_ServDocID = cm.t_DocumentID "
          + "                    and obj.t_BaseID = base.t_ID "
          + "                    and obj.t_InvoiceID = inv.t_InvoiceID "
          + "                    and rownum = 1 "
          + "                ) as t_CntLastOpr "
          + "           from daccount_dbt acc, dsfinv_dbt inv "
          + "          where acc.t_AccountID = ? " 
          + "            and inv.t_BeneAccount = acc.t_Account "
          + "            and inv.t_ServiceKind = " + PTSK_DEPOS
          + "            and inv.t_IsIncluded = 'X' " 
          + "            and inv.t_InvoiceStatus <> " + SFINV_STATUS_PAYED
          + "         UNION ALL "
          + "         select (sfdef.t_Sum - "
          + "                NVL((select acctrn.t_Sum_Receiver "
          + "                       from dsfdocs_dbt sfd, dacctrn_dbt acctrn "
          + "                      where sfd.t_ObjectType = " + OBJTYPE_SFDEFCOM
          + "                        and sfd.t_ObjectID = TO_CHAR(sfdef.t_ID) "
          + "                        and sfd.t_LinkKind = " + SFDOCS_LINKKIND_COMISSPAY
          + "                        and acctrn.t_AccTrnID = sfd.t_AccTrnID "
          + "                        and acctrn.t_AccountID_Receiver = ? "
          + "                    ), 0)"
          + "                ) as t_NotPaidSum, "
          + "                sfdef.t_FIID_Sum as t_PayFIID, "
          + "                (? - sfdef.t_DateFee + 1) as t_DefaultDays, "
          + "                0 as t_InvoiceID, "
          + "                0 as t_InvMethod, "
          + "                sfdef.t_DateFee as t_Date, "
          + "                NVL((select cm.t_Name from dsfcomiss_dbt cm where cm.t_FeeType = sfdef.t_FeeType and cm.t_Number = sfdef.t_CommNumber), CHR(1)) as t_Number, "
          + "                sfdef.t_Status as t_StatusKind, "
          + "                NVL((select llv.t_Name from dllvalues_dbt llv where llv.t_List = "+OBJTYPE_COMPAYSTATUS+" and llv.t_Element = sfdef.t_Status), CHR(1)) as t_StatusName, "
          + "                sfdef.t_ID as t_SfDefID, "
          + "                (select count(1) "
          + "                   from ddl_comm_dbt cm, ddpresbase_dbt base, ddpresobj_dbt obj "
          + "                  where cm.t_DocKind = " + DP_CALCCOMMRESERV
          + "                    and cm.t_DocumentID <> ? "
          + "                    and cm.t_CommDate > ? "
          + "                    and base.t_ServDocKind = cm.t_DocKind " 
          + "                    and base.t_ServDocID = cm.t_DocumentID "
          + "                    and obj.t_BaseID = base.t_ID "
          + "                    and obj.t_SfDefID = sfdef.t_ID "
          + "                    and rownum = 1 "
          + "                ) as t_CntLastOpr "
          + "           from dsfdef_dbt sfdef "
          + "          where sfdef.t_InvoiceID = 0 "
          + "            and sfdef.t_Status <> " + SFDEFCOM_STATUS_PAYED
          + "            and Exists(select 1 "
          + "                         from dsfdocs_dbt sfd, dacctrn_dbt acctrn "
          + "                        where sfd.t_ObjectType = " + OBJTYPE_SFDEFCOM
          + "                          and sfd.t_ObjectID = TO_CHAR(sfdef.t_ID) "
          + "                          and sfd.t_LinkKind = " + SFDOCS_LINKKIND_COMISSACCRUE
          + "                          and acctrn.t_AccTrnID = sfd.t_AccTrnID "
          + "                          and (acctrn.t_AccountID_Payer = ? or acctrn.t_AccountID_Receiver = ?) "
          + "                      ) "
          + "        ) q "
          + "  where q.t_NotPaidSum > 0 "
          + "    and q.t_DefaultDays > 0 ";

  cmd.AddParam(dl_comm.rec.CommDate);

  cmd.AddParam(dl_comm.rec.CommDate);
  cmd.AddParam(dl_comm.rec.DocumentID);
  cmd.AddParam(dl_comm.rec.CommDate);
  cmd.AddParam(AccountID);
  
  cmd.AddParam(AccountID);
  cmd.AddParam(dl_comm.rec.CommDate);
  cmd.AddParam(dl_comm.rec.DocumentID);
  cmd.AddParam(dl_comm.rec.CommDate);
  cmd.AddParam(AccountID);
  cmd.AddParam(AccountID);
  
  DataSet = cmd.Execute(query);
   
  while(DataSet.moveNext())
    SetCurrResObjData(IIF(DataSet.InvoiceID > 0, "ТО", "Комиссия"), 
                      DataSet.Number, 
                      DataSet.StatusName, 
                      date(DataSet.Date), 
                      DataSet.DefaultDays);

    if(DataSet.CntLastOpr > 0)
      msgbox("По элементу расчетной базы резерв рассчитан за более позднюю дату");
      err = 1;
      //сразу не прерываемся, а выводим сообщения для всех проблемных ТО/комиссий
    elif(err == 0)

      prc = GetPrcResByDefaultDays(int(DataSet.DefaultDays));

      if(prc < 0)
        msgbox("Не определен процент резерва для периода просрочки");
      else
        sz = resobjArr.size;

        resobjArr[sz] = TRecHandler("dpresobj.dbt");

        resobjArr[sz].Clear();

        resobjArr[sz].rec.ID          = 0;
        resobjArr[sz].rec.BaseID      = 0;
        resobjArr[sz].rec.InvoiceID   = DataSet.InvoiceID;
        resobjArr[sz].rec.InvMethod   = DataSet.InvMethod;
        resobjArr[sz].rec.SfDefID     = DataSet.SfDefID;
        resobjArr[sz].rec.Date        = date(DataSet.Date);
        resobjArr[sz].rec.Status      = int(DataSet.StatusKind);
        resobjArr[sz].rec.DefaultDays = DataSet.DefaultDays;
        resobjArr[sz].rec.ReservePrc  = prc;
        resobjArr[sz].rec.BaseSum     = DataSet.NotPaidSumRub;
        resobjArr[sz].rec.ReserveSum = round((DataSet.NotPaidSumRub * prc / 100.0), 2);

        Rnew = Rnew + resobjArr[sz].rec.ReserveSum;
      end;
    end;
  end;

  ClearCurrResObjData();

  if(not err)
    fd = DP_AccResFD(AccountID);

    if(false == FD.OpenAccount( "Резерв, л/с", NULL, NULL, ReservAcc, dl_comm.rec.CommDate, NATCUR ))
      msgbox("Ошибка при открытии счета по категории Резерв, л/с");
      err = 1;
    end;

    if(not err)
      Rold = abs(DL_GetRestAccount(ReservAcc.rec, dl_comm.rec.CommDate));

      delta = Rnew - Rold;

      if(delta != 0.0)

        RSCatCode = IIF(delta > 0, "-РС, д/с", "+РС, д/с");

        if(false == FD.OpenAccount( RSCatCode, NULL, NULL, RSAcc, dl_comm.rec.CommDate, NATCUR ))
          msgbox("Ошибка при открытии счета по категории " + RSCatCode);
          err = 1;
        end;
        
        if(not err)
          RslDefCon.BeginTrans();

          if(DocCarry( null,                                                       
                       1,                                                           
                       NATCUR,                                                      
                       IIF(delta > 0, RSAcc.rec.Account, ReservAcc.rec.Account),    
                       IIF(delta > 0, ReservAcc.rec.Account, RSAcc.rec.Account),    
                       abs(delta),                                                  
                       dl_comm.rec.CommDate,   
                       0,                      
                       IIF(delta > 0, "Создание/увеличение резерва", "Уменьшение резерва"),                
                       0,
                       0,
                       accTrn,
                       "09"                       
                     ))
            msgbox(GetErrMsg());
            err = 1;
          end;
          
          if(not err)
            resbase.Clear();

            resbase.rec.ID               = 0;
            resbase.rec.ServDocKind      = dl_comm.rec.DocKind;
            resbase.rec.ServDocID        = dl_comm.rec.DocumentID;
            resbase.rec.ClientID         = BaseAcc.rec.Client;
            resbase.rec.BaseAccountID    = AccountID;
            resbase.rec.ReserveAccountID = ReservAcc.rec.AccountID;
            resbase.rec.OldSum           = Rold;
            resbase.rec.CorrectSum       = delta;
            resbase.rec.NewSum           = Rnew;
            resbase.rec.AccTrnID         = accTrn.AccTrnID;

            resbaseCache.AddRecord(resbase);
            resbaseCache.AddReturning( "t_id", V_INTEGER );
            
            if(not resbaseCache.insert())
              err = 1;
            else
              resbaseCache.GetReturning(realIDs);
              ResBaseID = realIDs[0];
            end;

            if(not err)
              i = 0;
              while(i < resobjArr.size)
                resobjArr[i].rec.BaseID = ResBaseID;
                i = i + 1;
              end;

              resobjCache.AddRecordArray(resobjArr);
              if(not resobjCache.insert())
                err = 1;
              end;
            end;
          end;

          if(not err)
            RslDefCon.CommitTrans();
          else
            RslDefCon.RollbackTrans();
          end;
        end;
      end;
    end;
  end;

  return err;

OnError(er)
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  msgbox(er.module+ " "+er.line+" "+er.message);

  return 1;
END;

//класс с данными для конвейера
class DepoResCnvData(_DocumentID)
  var DocumentID = _DocumentID;
end;

PRIVATE MACRO GetSrvDepoResObjectQuery(dl_comm)
  var query = "";

  query =   " select DISTINCT acc.t_AccountID "
          + "   from dmcaccdoc_dbt doc, "
          + "        (select c.* "
          + "           from dmccateg_dbt c "
          + "          where c.t_LevelType = 1 "
          + "            and c.t_Code IN ('+Расчеты', '+Расчеты,НВПИ') "
          + "        ) cat, "
          + "        dmctempl_dbt templ, daccount_dbt acc "
          + "  where doc.t_CatID = cat.t_ID "
          + "    and doc.t_ActivateDate <= " + GetSQLDate(dl_comm.rec.CommDate)
          + "    and templ.t_CatID = doc.t_CatID "
          + "    and templ.t_Number = doc.t_templNum "
          + "    and 1 = (case when cat.t_Class1 = "+LLCLASS_BACKOFFICE+" and templ.t_Value1 = "+OBJTYPE_BACKOFFICE_DEPO+" then 1 "
          + "                  when cat.t_Class2 = "+LLCLASS_BACKOFFICE+" and templ.t_Value2 = "+OBJTYPE_BACKOFFICE_DEPO+" then 1 "
          + "                  when cat.t_Class3 = "+LLCLASS_BACKOFFICE+" and templ.t_Value3 = "+OBJTYPE_BACKOFFICE_DEPO+" then 1 "
          + "                  when cat.t_Class4 = "+LLCLASS_BACKOFFICE+" and templ.t_Value4 = "+OBJTYPE_BACKOFFICE_DEPO+" then 1 "
          + "                  when cat.t_Class5 = "+LLCLASS_BACKOFFICE+" and templ.t_Value5 = "+OBJTYPE_BACKOFFICE_DEPO+" then 1 "
          + "                  when cat.t_Class6 = "+LLCLASS_BACKOFFICE+" and templ.t_Value6 = "+OBJTYPE_BACKOFFICE_DEPO+" then 1 "
          + "                  when cat.t_Class7 = "+LLCLASS_BACKOFFICE+" and templ.t_Value7 = "+OBJTYPE_BACKOFFICE_DEPO+" then 1 "
          + "                  when cat.t_Class8 = "+LLCLASS_BACKOFFICE+" and templ.t_Value8 = "+OBJTYPE_BACKOFFICE_DEPO+" then 1 "
          + "                else 0 end) "
          + "    and acc.t_Account = doc.t_Account "
          + "    and acc.t_Chapter = doc.t_Chapter "
          + "    and acc.t_Code_Currency = doc.t_Currency ";

  if(dl_comm.rec.ClientID > 0)
    query = query + " and acc.t_Client = " + dl_comm.rec.ClientID;
  end;

  query =   " select 0 as t_ObjectType, t_AccountID as t_ObjectID, ROWNUM as t_GroupNumber, t_AccountID "
          + " from ("+query+") ";

  return query;
END;

MACRO ОтобратьОбъектыДляДепРез(_taskID, _execID, _conveyerID, _packetSize, _Params)
  var dl_comm = TBFile("dl_comm.dbt");
  var cnt = 0;
  var query, DataSet;

  if(_Params.DocumentID > 0)
  
  dl_comm.rec.DocumentID = _Params.DocumentID;
    if(dl_comm.GetEQ())

      query = " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
            + " SELECT "+_taskID+","+_execID+","+_conveyerID+",q.t_GroupNumber, q.t_ObjectType, q.t_ObjectID "
            + "   FROM (" + GetSrvDepoResObjectQuery(dl_comm) + ") q ";
      query = query + " ORDER BY q.t_ObjectID ";

      SQL_Execute(query, "Отбор объектов", false );
      
      if(_packetSize == 0)
        cnt = 1;
      else
        query =   " select NVL(max(t_PackID), 0) as cnt "
                + "   from dcnvdoc_dbt "
                + "  where t_ExecID = " + _execID;
        
        DataSet = TRsbDataSet(query);
        if(DataSet.moveNext())
          cnt = int(round(int(DataSet.cnt), 0));
        end;
      end;
    end;
  end;

  return cnt;
END;

MACRO ExecuteSrvDepoRes(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params)
  var dl_comm = TBFile("dl_comm.dbt");
  var query, cmd, DataSet;
  var cnt = 0;
  var err = 0;

  if(_Params.DocumentID > 0)
    
    dl_comm.rec.DocumentID = _Params.DocumentID;
    if(dl_comm.GetEQ())

      RepErrArr.size = 0;
      ReplaceMacro( "msgbox", "DepoRes_msgbox" );

      if((_conveyerID) and (_packetID))
        
        query =  "select t_ObjectID as t_AccountID from dcnvdoc_dbt where t_ExecID = ? and t_ConvTypeID = ? and t_PackID = ?";
        
        cmd = DL_RSDCommand(query);
        
        cmd.AddParam(_execID);
        cmd.AddParam(_conveyerID);
        cmd.AddParam(_packetID);
        
        DataSet = cmd.Execute();
        if(DataSet.moveNext())
          CalcDepoCommReserveByAcc(dl_comm, DataSet.AccountID);
        end;

      else
        query = "select q.t_AccountID from ("+GetSrvDepoResObjectQuery(dl_comm)+") q ";
        cmd = DL_RSDCommand(query);
        cnt = cmd.GetCount();
         
        if(cnt > 0)
          InitProgress( cnt, "Идет выполнение ...", "Формирование резервов" );
          
          DataSet = cmd.Execute();
          var i = 0;
          while(DataSet.moveNext())
            
            CalcDepoCommReserveByAcc(dl_comm, DataSet.AccountID);

            UseProgress(i = i + 1);
          end;

          RemProgress();
        end;
      end; 

      ReplaceMacro( "msgbox", NULL );
      SaveDepoResErrForReport_XML( RepErrArr, dl_comm.rec.DocumentID, dl_comm.rec.DocKind );
    end;
  end;

  if(_packetID)
    /* не зависимо от того, делали что-то или нет - ставим исходящий статус обрабатываемой пачке */
    var exec = RSDCommand(  " UPDATE dcnvpack_dbt "
                          + " SET t_State = t_State + 1 "
                          + " WHERE t_ExecID = ? "
                          + "   AND t_ConvTypeID = ? "
                          + "   AND t_PackID = ? ");

    exec.addParam( "", RSDBP_IN, _execID );
    exec.addParam( "", RSDBP_IN, _conveyerID );
    exec.addParam( "", RSDBP_IN, _packetID );
    exec.execute();
  end;

  return err;

OnError(er)
  msgbox(er.module+ " "+er.line+" "+er.message);
END;

PRIVATE MACRO ErrorFromCnvpack( cnv )
  var Cmd, Query, DataSet, err = 0;
  Query = " select t_Error, t_ErrMsg " +
          "   from dcnvpack_dbt " +
          "  where t_ExecID = ? "  + 
          " group by t_Error, t_ErrMsg" ;
 
  Cmd = DL_RSDCommand( Query );      

  Cmd.AddParam(cnv.ExecID);
  
  DataSet = Cmd.Execute();

  while( DataSet.MoveNext() )
    if( DataSet.ErrMsg != "" )      
      msgbox(DataSet.ErrMsg);
      err = err + 1;
    end; 
  end;
  return err;
END;

PRIVATE MACRO _CalcDepoCommReserve(dl_comm)
  var OperID = 19; //Операция для конвейера - Пакетная операция формирования резервов по депозитарным комиссиям
  var ConvID = 23; //Вид конвейера Формирование резервов по депозитарным комиссиям
  var UseConveyer = SecurUseConveyer(ConvID, OperID);
  var params = DepoResCnvData(dl_comm.rec.DocumentID);
  var query, cmd, DataSet;
  var err = 0;

  if(UseConveyer)
    var cnv = RsbConveyerExec(ConvID);

    cnv.AddParamsForConveyer(ConvID, params, 0);
    cnv.AddParamsForOperation(ConvID, OperID, params, "", 0);
    cnv.showPanelMonitoring(1);
    cnv.Run();
    
    if( ErrorFromCnvpack( cnv ) > 0 )     
      err = 1;
    end;
  else
    err = ExecuteSrvDepoRes(0, 0, 0, 0, 0, params);
  end;

  return err;
end;


//Точка входа. Выполнить расчет и начисление резерва
MACRO CalcDepoCommReserve(DocumentID:integer)
  var dl_comm = TBFile("dl_comm.dbt");
  var err = 0;

  dl_comm.rec.DocumentID = DocumentID;
  if(not dl_comm.GetEQ())
    msgbox("Ошибка поиска операции");
    return 1;
  end;

  RepErrArr.size = 0; //массив ошибок
  ReplaceMacro( "msgbox", "DepoRes_msgbox" );

  err = _CalcDepoCommReserve(dl_comm);

  ReplaceMacro( "msgbox", NULL );

  SaveDepoResErrForReport_XML(RepErrArr, dl_comm.rec.DocumentID, dl_comm.rec.DocKind);

  return err;
END;