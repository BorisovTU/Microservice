/*
$Name: dpclosemas.mac
$Module: Депозитарий
$Description: Массовое закрытие счетов депо
*/

Import rsd;
import DPInter, "secinter.mac", "dpstdrep.mac", "fi.mac", "ws_validation.mac", "spserv.mac";
import RsbDataSet;

private const CHAPT5 = 5; //глава л/с депо
var HeadTable = "┌─────────────────────────┬─────────────────┬─────────────────┬─────────────────┬───────────────┬─────────────────────────┬────────────────────────┐\n"+
                "│     Номер счёта депо    │  Наименование   │  Наименование   │   Код анкеты    │ Дата открытия │       Номер раздела     │ Дата открытия раздела  │\n"+
                "│                         │  счета депо     │   Депонента     │    депонента    │     счета     │        счета депо       │     счета депо         │\n"+
                "├─────────────────────────┼─────────────────┼─────────────────┼─────────────────┼───────────────┼─────────────────────────┼────────────────────────┤";

var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
var RepHead = CMakeReport("                                                      ");
var   HeaderStr = "Закрытие выбранных счетов депо (по поручению Депонентов)";
const RepFontStyleTitel =  "ex_FS(b)";
var  _is_ap;
var HeadDate = "за <"+{Curdate}+">";
DP_StdHeaderLO_Ex( RepHead, RepFontStyleTitel, HeaderStr, 0, 0, false, null, null, tablewidth );
DP_AddPrintCell( RepHead, headDate, 140, 0, "r:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
   RepHead.AddStr();

var HeadTable1 = "┌─────────────────────────┬─────────────────┬─────────────────┬─────────────────┬───────────────────────┬──────────────────────────────────────────┐\n"+
                 "│     Номер счёта депо    │  Наименование   │  Наименование   │   Код анкеты    │      Номер раздела    │                Причина                   │\n"+
                 "│                         │  счета депо     │   Депонента     │    депонента    │       счета депо      │                                          │\n"+
                 "├─────────────────────────┼─────────────────┼─────────────────┼─────────────────┼───────────────────────┼──────────────────────────────────────────┤";

var tablewidth1 = Index(HeadTable1, "\n");
var Rep1 = CMakeReport(HeadTable1);
const RepFontStyleTitel1 =  "ex_FS(b)";

VAR CountErr = 0, CountMes = 0;

macro PrintStr(CodeDA, NameDA, NameDep, CodeDep,
               OpenDateDA, CodeDP, OpenDateDP)

   DP_AddPrintCell( Rep, CodeDA, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, NameDA, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, NameDep, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, CodeDep, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, OpenDateDA, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, CodeDP, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, OpenDateDP, 0, 0, "r:w" + RepFontStyleTitel );
   Rep.AddStr();
end;
	
macro PrintStr1(CodeDA1, NameDA1, NameDep1, CodeDep1,
               CodeDP1, Mes)

   DP_AddPrintCell( Rep1, CodeDA1, 0, 0, "l:w" + RepFontStyleTitel1 );
   DP_AddPrintCell( Rep1, NameDA1, 0, 0, "l:w" + RepFontStyleTitel1 );
   DP_AddPrintCell( Rep1, NameDep1, 0, 0, "l:w" + RepFontStyleTitel1 );
   DP_AddPrintCell( Rep1, CodeDep1, 0, 0, "l:w" + RepFontStyleTitel1 );
   DP_AddPrintCell( Rep1, CodeDP1, 0, 0, "l:w" + RepFontStyleTitel1 );
   DP_AddPrintCell( Rep1, Mes, 0, 0, "l:w" + RepFontStyleTitel1 );
   Rep1.AddStr();
end;


// вывод в протокол инф. о закрытых счета/ разделах депо
PRIVATE MACRO PrintRepRoot(dpacc,OpClose)
  var query, Select, DataSet;
  var i = 0;
  var  CodeDA = "", NameDA = "", NameDep = "", CodeDep = "" , OpenDateDA, CodeDP = "", OpenDateDP , Mes = "";
  CodeDA = dpacc.rec.Code;
  NameDA = dpacc.rec.Name;
  CodeDep = ПолучитьКодСубъекта(dpacc.rec.Owner,1);
  NameDep = GetPartyName(dpacc.rec.Owner, PTKN_FULLNAME);
  OpenDateDA = dpacc.rec.OpenDate;

  query = " SELECT acnt.* "
        + " FROM ddepoacnt_dbt acnt"
        + " START WITH t_autokey = ? " 
        + " CONNECT BY PRIOR t_autokey = t_superior " 
        + " ORDER BY acnt.t_autokey ";

  Select = RSDCommand( query );

  Select.AddParam("Autokey",  RSDBP_IN, dpacc.rec.autokey  );  /*0*/
  Select.Execute();

  DataSet = TRsbDataSet(Select);
  while(DataSet.moveNext())
    if(not(DataSet.Autokey==DataSet.Root))
      CodeDP = DataSet.Code;
      OpenDateDP = date(DataSet.OpenDate);	
    end;
    if(OpClose[i]==0)
      PrintStr(CodeDA, NameDA, NameDep, CodeDep, OpenDateDA, CodeDP, OpenDateDP);
    end;
    i = i+1;
  end;
END;

PRIVATE MACRO  CheckAdmoprForDepoAcc(dpacc)
  Var quer, Sel, DataS, Dpar = -1, DparTemp = -1, count = 0 ;
  var  CodeDA1 = "", NameDA1 = "", NameDep1 = "", CodeDep1 = "" , CodeDP1 = "" , Mes = "";

  quer = " select admo.t_DepoPart as DepoPart, acnt.t_code as Code "
       + " from ddepoadmo_dbt admo, ddepoacnt_dbt acnt "
       + " Where admo.t_operdate > ? "
       + " and admo.t_depoacc = ? "
       + " and acnt.t_Autokey(+) = admo.t_DepoPart "        
       + " order by admo.t_depopart";
  Sel = RSDCommand( quer );
  Sel.AddParam("OperDate", RSDBP_IN, {CurDate}        );       /*0*/
  Sel.AddParam("DEPOACC",  RSDBP_IN, dpacc.rec.autokey  );  /*1*/
  Sel.Execute();

  DataS = TRsbDataSet(Sel);
  while(DataS.moveNext())
    Dpar = DataS.DepoPart;
    if(Dpar != DparTemp)
      CodeDA1 = dpacc.rec.Code;
      NameDA1 = dpacc.rec.Name;
      CodeDep1 = ПолучитьКодСубъекта(dpacc.rec.Owner,1);
      NameDep1 = ПолучитьКороткоеИмяСубъекта( dpacc.rec.Owner);
      if(DataS.DepoPart >0)
        CodeDP1 = DataS.Code ;
      end;
      if(Dpar==0)
        Mes = GetErrorMsg(15093);
      else
        Mes = GetErrorMsg(15092);
      end;
      DparTemp = Dpar;
      PrintStr1(CodeDA1, NameDA1, NameDep1, CodeDep1, CodeDP1, Mes);
    end;
    count = count +1;
  end;
  return count;
END;

// Получить по счету самую позднюю дату проводки, выполненной позднее текущей опердаты, если таковой нет - возвращает текущую дату, чтобы не возникало ошибки
private macro GetFinalDate(acc)
  var query, Select, DataSet;
  var Final_Date = {curdate};

  query =   " SELECT NVL(MAX(t_Date_Carry), TO_DATE('01.01.0001', 'DD.MM.YYYY')) t_Final_Date " +
            "   FROM dacctrn_dbt " +
            "  WHERE ((t_Account_Payer = ? AND t_FIID_Payer = ?) OR (t_Account_Receiver = ? AND t_FIID_Receiver = ?)) " +
            "    AND t_Chapter = ? " +
            "    AND t_State = ? " +
            "    AND t_Date_Carry > ? ";

  Select = RSDCommand( query );

  Select.AddParam("Account_Payer",  RSDBP_IN, acc.rec.Account );
  Select.AddParam("FIID_Payer",  RSDBP_IN, acc.rec.Code_Currency );
  Select.AddParam("Account_Receiver",  RSDBP_IN, acc.rec.Account );
  Select.AddParam("FIID_Receiver",  RSDBP_IN, acc.rec.Code_Currency );
  Select.AddParam("Chapter",  RSDBP_IN, acc.rec.Chapter );
  Select.AddParam("State",  RSDBP_IN, TRN_STATE_FACT );
  Select.AddParam("Date_Carry",  RSDBP_IN, {curdate} );
  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if((DataSet.moveNext()) AND (date(DataSet.Final_Date) != date(0,0,0)) )
    Final_Date = date(DataSet.Final_Date);
  end;

  return Final_Date;
end;

// заполнение массива 
PRIVATE MACRO FillOpClose(dpacc,OpClose:@TArray)
  var query, Select, DataSet;
  var i = 0;
  query = " SELECT acnt.* "
        + " FROM ddepoacnt_dbt acnt"
//        + " WHERE acnt.t_root = ? "               
        + " START WITH t_autokey = ? " 
        + " CONNECT BY PRIOR t_autokey = t_superior " 
        + " ORDER BY acnt.t_autokey ";
  Select = RSDCommand( query );

  Select.AddParam("Autokey",  RSDBP_IN, dpacc.rec.autokey  );  /*0*/
  Select.Execute();

  DataSet = TRsbDataSet(Select);
  while(DataSet.moveNext())
    OpClose[i] = DataSet.Status;
    i = i+1;
  end;
END;

private macro GetFinalDateDEPO(autokey, brief)
  var acc = TRecHandler("account");
  var count = 0 ;
  var query, Select, DataSet;
    query =   " select acc.* "
            + "   from daccount_dbt acc "
            + "  where acc.t_Chapter = " + CHAPT5         //по 5-й главе
            + " and acc.t_DEPOACC  IN  (SELECT t_AutoKey FROM ddepoacnt_dbt "
            + " START WITH t_autokey = ? CONNECT BY PRIOR t_autokey = t_superior )" ;

   Select = RSDCommand( query );

   Select.AddParam("Autokey", RSDBP_IN, autokey );  

   Select.Execute();                                                               

      DataSet = TRsbDataSet(Select);

      while( DataSet.moveNext() )
        DataSet.GetRecord().CopyTo(acc.rec);
          if(GetFinalDate(acc) > {curdate}) //проверяем поле "дата последней проводки" из анкеты лицевого счёта
            count = count + 1;         
          end;
      end; 
  return count;  
end;

MACRO PrintRep()
  var query1, query2, query3, DataSet;
  var CntPrep = 0, CntOpen = 0;
  var mesState = "", mesOpKind = "", mess = "";
  var OpClose = TArray;   //массив открытых/закрытых разделов счета депо
  var Type = 0;

  var dpacc = TRecHandler("depoacnt");
  var  CodeDA = "", NameDA = "", NameDep = "", CodeDep = "" , OpenDateDA ={Curdate}, CodeDP = "", OpenDateDP = {CurDate} , Mes = "";

    query1 =   " select  "
            + "        (select count(1) from dspdraft_dbt draft, dspdrmove_dbt mv, dpmpaym_dbt pm, ddepoacnt_tmp acnt "
            + "          where draft.t_Status = 1"
            + "            and draft.t_Date <= " + GetSQLDate({curdate})
            + "            and mv.t_DraftID = draft.T_AUTOKEY "
            + "            and pm.T_PaymentID = mv.t_PaymentID "
            + "            and (acnt.t_DEPOACNTID = pm.T_PAYERDPNODE or acnt.t_DEPOACNTID = pm.T_RECEIVERDPNODE )    "
            + "        ) as CntDraftPrep "
            + "       , (select count(1) from dspdraft_dbt draft, dspdrmove_dbt mv, dpmpaym_dbt pm, ddepoacnt_tmp acnt "
            + "          where draft.t_Status = 2"
            + "            and draft.t_Date <= " + GetSQLDate({curdate})
            + "            and mv.t_DraftID = draft.T_AUTOKEY "
            + "            and pm.T_PaymentID = mv.t_PaymentID   "
            + "            and (acnt.t_DEPOACNTID = pm.T_PAYERDPNODE or acnt.t_DEPOACNTID = pm.T_RECEIVERDPNODE )    "
            + "        ) as CntDraftOpen"
            + "   from dual ";
    DataSet = TRsbDataSet(query1);
    if((DataSet.moveNext()) and ((DataSet.CntDraftPrep > 0) OR (DataSet.CntDraftOpen > 0) ))
      CntPrep  = DataSet.CntDraftPrep ;
      CntOpen  = DataSet.CntDraftOpen ;

      if((CntPrep > 0) and (CntOpen > 0))
        mesState = "отложенные и открытые";
      elif(CntPrep > 0)
        mesState = "отложенные";
      else
        mesState = "открытые";
      end;

      mess = "Найдены "+mesState +" инвентарные  операции, которые могут изменить остатки по лицевым счетам за "+string({curdate}:f)+". Запустить процедуру закрытия выбранных счетов депо (по поручению Депонентов)?";
      if(false == GetTrue(false, mess))
        return; 
      end;
     

    end;
    CntPrep = CntOpen = 0;

    query2 =   " select  "
            + "        (select count(1) from ddpcorpop_dbt crp , ddpregstr_dbt str, ddpreglin_dbt lin, ddepoacnt_tmp  "
            + "          where crp.t_DocKind in ( "+DP_DEPOPER_CONVERT+", "+DP_DEPOPER_REPAY+", "+DP_DEPOPER_BONEMISS+" ) "
            + "            and crp.t_State = 1 "
            + "            and crp.t_ValueDate <= " + GetSQLDate({curdate})
            + "            and str.t_DOCUMENTID  = crp.t_DOCUMENTID  "
            + "            and str.t_REGISTERID = lin.t_REGISTERID "
            + "            and ddepoacnt_tmp.t_DEPOACNTID = lin.t_HOLDERACCID  "
            + "        ) as CntGlobPrep, "
            + "        (select count(1) from ddpcorpop_dbt crp , ddpregstr_dbt str, ddpreglin_dbt lin, ddepoacnt_tmp "
            + "          where crp.t_DocKind in ( "+DP_DEPOPER_CONVERT+", "+DP_DEPOPER_REPAY+", "+DP_DEPOPER_BONEMISS+" ) "
            + "            and crp.t_State = 2 "
            + "            and crp.t_ValueDate <= " + GetSQLDate({curdate})
            + "            and str.t_DOCUMENTID  = crp.t_DOCUMENTID  "
            + "            and str.t_REGISTERID = lin.t_REGISTERID "
            + "            and ddepoacnt_tmp.t_DEPOACNTID = lin.t_HOLDERACCID  "
            + "        ) as CntGlobOpen "
            + "   from dual ";
    DataSet = TRsbDataSet(query2);
    if((DataSet.moveNext()) and ((DataSet.CntGlobPrep > 0) OR (DataSet.CntGlobOpen > 0) ))
      CntPrep  = DataSet.CntGlobPrep ;
      CntOpen  = DataSet.CntGlobOpen ;

      if((CntPrep > 0) and (CntOpen > 0))
        mesState = "отложенные и открытые";
      elif(CntPrep > 0)
        mesState = "отложенные";
      else
        mesState = "открытые";
      end;

      mess = "Найдены "+mesState +" глобальные операции, которые могут изменить остатки по лицевым счетам за "+string({curdate}:f)+". Запустить процедуру закрытия выбранных счетов депо (по поручению Депонентов)?";
      if(false == GetTrue(false, mess))
        return; 
      end;
    end;

    VAR stat = 0 ;
    query3 = "SELECT  acnt.* FROM  ddepoacnt_dbt acnt, ddepoacnt_tmp acnt2 WHERE  acnt.t_superior NOT IN (SELECT acnt2.t_depoacntid FROM ddepoacnt_tmp acnt2) and ACNT.T_AUTOKEY = acnt2.t_DEPOACNTID ORDER by acnt.t_BriefCode";
    DataSet = TRsbDataSet(query3);
    while(DataSet.moveNext())
       OpClose.size = 0;
       DataSet.GetRecord().CopyTo(dpacc.rec);
       CodeDA = dpacc.rec.Code;
       NameDA = dpacc.rec.Name;
       CodeDep = ПолучитьКодСубъекта(dpacc.rec.Owner,1);
       NameDep = ПолучитьКороткоеИмяСубъекта( dpacc.rec.Owner);
       OpenDateDA = dpacc.rec.OpenDate;
       ПолучитьТипСчетаПоНазначению(dpacc.rec.Root, Type);

       if(DataSet.kind !=2 )
         Mes = GetErrorMsg(15091);
         PrintStr1(CodeDA, NameDA, NameDep, CodeDep, CodeDP, Mes);
         CountErr = CountErr +1;
       elif((Type!=1)and(Type!=2)and(Type!=3)and(Type!=11)and(Type!=12))
         Mes = GetErrorMsg(15091);
         PrintStr1(CodeDA, NameDA, NameDep, CodeDep, CodeDP, Mes);
         CountErr = CountErr +1;
       elif(CheckAdmoprForDepoAcc(dpacc))
         CountErr = CountErr +1;
       elif(GetFinalDateDEPO(DataSet.autokey,DataSet.BriefCode))
         Mes = GetErrorMsg(15094);
         PrintStr1(CodeDA, NameDA, NameDep, CodeDep, CodeDP, Mes);
         CountErr = CountErr +1;
       else
           FillOpClose(dpacc,@OpClose);
         stat =  DP_CloseMAS(dpacc.rec.autokey);
         if(stat == 0)
           PrintRepRoot(dpacc,OpClose);
           CountMes = CountMes +1;
           OpClose.size = 0;
         elif(stat > 0)
           Mes = GetErrorMsg(stat);
           CodeDP = dpacc.rec.Code;
           PrintStr1(CodeDA, NameDA, NameDep, CodeDep, CodeDP, Mes);
           CountErr = CountErr +1;
         end;
       end;

    end;

      //печать протокола
    if((CountMes+CountErr))
        RepHead.PrintRep();

      if(CountMes)
         [Перечень закрытых счетов и разделов счета депо:];
        if( CountErr == 0)
          DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);               // стандартный подвал отчета, добавляем если нет таблицы с ошибками
        end;
        Rep.PrintRep();
      else
       [   Счета депо не закрывались];
      end;
  
      if(CountErr)
        [Перечень незакрытых  счетов:];
        DP_StdFooter_Ex(Rep1, RepFontStyleTitel1, NULL, NULL, tablewidth1);              // добавляем стандартный подвал отчета
        Rep1.PrintRep();
      end;

    else    //если нет никаких таблиц

      DP_AddPrintCell( RepHead, "Счета депо не закрывались", 0, 0, "r:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
      RepHead.AddStr();
      DP_StdFooter_Ex(RepHead, RepFontStyleTitel, NULL, NULL, tablewidth);               
      RepHead.PrintRep();

    end;
    SQL_Execute("DELETE FROM ddepoacnt_tmp");  


end;
