/*
$Name:        dpavrfrm.mac
$Module:      Депозитарий
$Description: Отчет "Анкета выпуска ценной бумаги"
*/

import FIInter, CurrInter, CTInter, dprepsec, DPInter, BankInter, dpstdrep, "globals.mac";

const NameAlg_ФормаВыпуска = 2830;
const AVOIRISSTATE_NOSERV  = 0,  // Не обслуживается
      AVOIRISSTATE_INCLUDE = 10, // Подключена
      AVOIRISSTATE_ONSERV  = 20, // На обслуживании
      AVOIRISSTATE_BLOCK   = 30;  // Блокирован

private const EMPTY_STR = "---";

file party("party");
file links("objatcor") key 2;
file vals("objattr") key 0;
file coupons("fiwarnts");
file avrk("avrkinds");
file NameAlg("namealg");
file fi_ISO ("fininstr") key 0;

record fin("fininstr");
record avr("avoiriss");
record RateDef("ratedef");/*праметры курса*/

var HeadTable = "┌────────────┬──────────────────┬────────────────────┬───────────┐\n"+
                "│Купоны      │Дата погашения    │Ставка дохода       │Доход      │\n"+
                "├────────────┼──────────────────┼────────────────────┼───────────┤";

var tablewidth = 78;
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

private macro GetAOParam(RepDate, FIID, NumDprt, OprKind, OprDate:@variant, OprXid:@variant)
  var query, DataSet;
  var Select;

  query = " select t_OperDate, t_OprXid " +
          "   from ddepoadmo_dbt " +
          "  where t_FIID = ? /*1*/ " +
          "    and t_Department = ? /*2*/ " +
          "    and t_Item = " + SPDP_AVOIRIS + " " +
          "    and t_OperKind = ? /*3*/ " +
          "    and t_OperDate <= ? /*4*/ " +
          "    and exists (select 1 " +
          "                  from ddepoadmo_dbt " +
          "                 where t_FIID = ? /*5*/ " +
          "                   and t_Department = ? /*6*/ " +
          "                   and t_Item = " + SPDP_AVOIRIS + " " +
          "                   and t_OperKind = ? /*7*/ " +
          "                   and t_OperDate > ? /*8*/ ) ";

  Select = RSDCommand( query );

  Select.AddParam("", RSDBP_IN, FIID);    /*1*/
  Select.AddParam("", RSDBP_IN, NumDprt); /*2*/
  Select.AddParam("", RSDBP_IN, OprKind); /*3*/
  Select.AddParam("", RSDBP_IN, RepDate); /*4*/
  Select.AddParam("", RSDBP_IN, FIID);    /*5*/
  Select.AddParam("", RSDBP_IN, NumDprt); /*6*/
  Select.AddParam("", RSDBP_IN, OprKind); /*7*/
  Select.AddParam("", RSDBP_IN, RepDate); /*8*/

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())
    OprDate = date(DataSet.OperDate);
    OprXid = string(DataSet.OprXid);
  end;

end;

private macro GetServiceStateInfo(
  RepDate,
  FIID,
  NumDprt,
  SrvStartDate:@variant,
  SrvStartOpNum:@variant,
  SrvEndDate:@variant,
  SrvEndOpNum:@variant
  )
  var query, DataSet;
  var Select;

  SrvStartDate  = EMPTY_STR;
  SrvStartOpNum = EMPTY_STR;
  SrvEndDate    = EMPTY_STR;
  SrvEndOpNum   = EMPTY_STR;

  /* Получим параметры обслуживания из АО, если они есть */
  GetAOParam(RepDate, FIID, NumDprt, SPAO_CON_REGISTERING, @SrvStartDate, @SrvStartOpNum);
  GetAOParam(RepDate, FIID, NumDprt, SPAO_CON_CLOSING, @SrvEndDate, @SrvEndOpNum);

  if((SrvStartDate == EMPTY_STR) OR (SrvEndDate == EMPTY_STR)) /* Если не вышло попытаемся уточнить */
    /* Получить даты постановки и снятия с обслуживания из параметров обслуживания */
    query = " select t_ServiceStartDate as SrvStartDate, t_ServiceState as SrvState, t_ServiceStateDate as SrvStateDate " +
            "   from davoirsrv_dbt " +
            "  where t_FIID = ? /*1*/ " +
            "    and t_Department = ? /*2*/ ";

    Select = RSDCommand( query );

    Select.AddParam("", RSDBP_IN, FIID );    /*1*/
    Select.AddParam("", RSDBP_IN, NumDprt ); /*2*/

    Select.Execute();

    DataSet = TRsbDataSet(Select);
    if(DataSet.moveNext())
      if((SrvStartDate == EMPTY_STR)
          AND (date(DataSet.SrvStartDate) <= RepDate)
        )
        SrvStartDate = date(DataSet.SrvStartDate);
      end;
      if((SrvEndDate == EMPTY_STR)
          AND (DataSet.SrvState == AVOIRISSTATE_NOSERV)
          AND (date(DataSet.SrvStateDate) != ZeroDate)
          AND (date(DataSet.SrvStateDate) <= RepDate)
        )
        SrvEndDate = date(DataSet.SrvStateDate);
      end;
    end;
  end;
end;

class Купон(_FiID:integer, _Num:string, _P_date:date, _Percent:double, _Value:double, _Point:integer );
  var fiID, num:string, real_num;
  var p_date, percent, value, point;

    fiID = _FiID;
    num = _Num;
    real_num = int (_Num);
    p_date = _P_date;
    percent = _Percent;
    value = _Value;
    point = _Point;

    /* убирает лишние 0*/
    macro SetPoint()
      value = trim( ЧислоCЗаданнойТочностью( value, point, "") );
    end;
end;

/* сравнивалка для qsort */
macro СравнитьКупоны( el1 : Купон, el2 : Купон )
  if ( el1.real_num < el2.real_num )
    return -1;
  elif ( el1.real_num == el2.real_num )
    return 0;
  else
    return 1;
  end;
end;

/* Печать массива купонов, отсортированных по номеру */
macro ПечатьКупонов( НаборКупонов )
    var i = 0;
    Rep.AddEmptyStr();
    while( i < НаборКупонов.Size )
      if ( НаборКупонов[i].percent != 0 )
        DP_AddPrintCell( Rep,  НаборКупонов[i].real_num, 0, 0, "l:" + RepFontStyleTitel );
        DP_AddPrintCell( Rep,  НаборКупонов[i].p_date, 0, 0, "l:f:" + RepFontStyleTitel );
        DP_AddPrintCell( Rep,  string(НаборКупонов[i].percent)+"%", 0, 0, "l:" + RepFontStyleTitel );
        DP_AddPrintCell( Rep,  EMPTY_STR, 0, 0, "l:" + RepFontStyleTitel );
        Rep.AddStr();
      else
        DP_AddPrintCell( Rep,  НаборКупонов[i].real_num, 0, 0, "l:" + RepFontStyleTitel );
        DP_AddPrintCell( Rep,  НаборКупонов[i].p_date, 0, 0, "l:f:" + RepFontStyleTitel );
        DP_AddPrintCell( Rep,  EMPTY_STR, 0, 0, "l:" + RepFontStyleTitel );
        DP_AddPrintCell( Rep,  НаборКупонов[i].value, 0, 0, "l:" + RepFontStyleTitel );
        Rep.AddStr();
      end;
      i = i + 1;
    end;

end;

/*Внутренний интерфейс в Namealg.dbt*/
macro __NameAlg(Type, Number)
  NameAlg.iTypeAlg = Type;
  NameAlg.iNumberAlg = Number;
  if( GetEQ(NameAlg) )
    return NameAlg.szNameAlg;
  end;
  return " Не определен ";
end;

/*ISO код валюты*/
macro GetISOCode( valFIID )
   fi_ISO.FIID = valFIID;
   if ( GetEQ(fi_ISO) )
      return int(fi_ISO.ISO_Number);
   else
      return 0;
   end;
end;

macro CorrectFINominal(FaceValue, Scale, Point)
/*   return money(FaceValue / pow(10, Point) / Scale);*/
   return FaceValue / Scale;
end;

macro CreateAvoirForm(  LogOprID:integer,  /* ИД информационной операции */
                        NumDprt:integer,   /* Филиал */
                        FIID:integer,      /* FIID */
                        RepDate:date,      /* Дата отчета */
                        ToExcel:bool,      /* В Excel */
                        is_ap:bool)

   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

   var AvoirRate = "не задана",              /*КУРС*/
       AvoirRating = "не задан"/*РЕЙТИНГ ФИН. ИНСТРУМЕНТА*/,
       AvoirKodT = "не задан", /*КОД ТИПА ЦЕННОЙ БУМАГИ*/
       AvrKindName,
       Settlement_Name,
       fincursID,
       curs;
   var NomFaceValFi, NominalStr, NominalCur;
   var stat;
   var ISIN,
       LSIN,
       IssuerCode, /*ЭМИТЕНТ*/
       IssuerName,
       IssuerDimension,
       IssuedDate,
       DrawingDate,
       SrvStartDate,
       SrvStartOpNum,
       SrvEndDate,
       SrvEndOpNum,
       Nominal;    /*НОМИНАЛ*/
   var AttrID;
   var НаборКупонов = TArray;
   var point : double; /* точность для купонов*/
   var value;
   var i = 0;
   var cFin = CDPFinInstr( FIID, RepDate );
   var cPat;

   file fins(fininstr);

   /*ЗАПИСИ ФИНИНСТРУМЕНТА И ЦБ*/
   if(ПолучитьФинИн (FIID, fin, avr) != 0)
      MsgBox("Финансовый инструмент не найден");
      ClearRecord(fin);
      ClearRecord(avr);
      fin.Scale = 1;  //чтобы не допустить деления на ноль
   end;

   /*ВИД ЦБ*/
   avrk.FI_Kind = fin.FI_Kind;
   avrk.AvoirKind = cFin.AvoirKind();
   if (GetEQ (avrk))
      AvrKindName = avrk.Name;
   else
      MsgBox("Не найден тип ЦБ ", fin.Name);
      AvrKindName = "";
   end;
   Settlement_Name = __NameAlg(NameAlg_ФормаВыпуска, fin.Settlement_Code);

   /*печатаем все, что есть в анкете*/
   ISIN = cFin.ISIN();
   ISIN = IIF(ISIN!="", ISIN, EMPTY_STR);
   LSIN = cFin.LSIN();
   LSIN = IIF(LSIN!="", LSIN, EMPTY_STR);
   /*ЭМИТЕНТ*/
   cPat = CDPParty( cFin.Issuer(), RepDate );
   IssuerCode = ПолучитьКодСубъекта( cFin.Issuer(), 1 );
   IssuerCode = IIF(IssuerCode!="", IssuerCode, EMPTY_STR);
   IssuerName = cPat.Name();
   IssuerName = IIF(IssuerName!="", IssuerName, EMPTY_STR);
   IssuerDimension = Floor_Money(double(cFin.Qty()));
   IssuerDimension = IIF(IssuerDimension!=0, IssuerDimension, EMPTY_STR);
   IssuedDate  = cFin.Issued();
   IssuedDate  = IIF(IssuedDate!=ZeroDate, IssuedDate, EMPTY_STR);
   DrawingDate = cFin.DrawingDate();
   DrawingDate = IIF(DrawingDate!=ZeroDate, DrawingDate, EMPTY_STR);

   GetServiceStateInfo( RepDate, FIID, NumDprt, @SrvStartDate, @SrvStartOpNum, @SrvEndDate, @SrvEndOpNum);

   /*НОМИНАЛ*/
   if( RepDate == {curdate} )
     Nominal = GetFaceValue(fin.FIID, RepDate);
     NominalStr = CurToStrAlt(ЧислоCЗаданнойТочностью(Nominal, fin.Point+2), null, null, GetISOCode(fin.FaceValueFI), fin.Point+2 );
     NominalCur = fi_ISO.Ccy;
   else
     Nominal = cFin.FaceValue();
     NominalStr = CurToStrAlt(ЧислоCЗаданнойТочностью(Nominal, fin.Point+2), null, null, GetISOCode(cFin.FaceValueFI()), fin.Point+2 );
     NominalCur = fi_ISO.Ccy;
   end;
   NominalStr = IIF(money(Nominal)!=$0, NominalStr, EMPTY_STR);
   Nominal = IIF(money(Nominal)!=$0, Nominal, EMPTY_STR);
   NominalCur = IIF(NominalCur!="", NominalCur, EMPTY_STR);

   /*КУРС*/
   curs = 0;
   /*возьмем курс к валюте номинала, затем полученную сумму переведем в рубли */
   if ( ПолучитьКурс( RateDef, fin.FaceValueFI, FIID ) == 0 )
     if ( fin.FaceValueFI != NATCUR ) /* переводим в рубли */
        curs = RateDef.Rate / pow( 10, RateDef.Point );
        fincursID = RateDef.FIID;
        if ( ПолучитьКурс( RateDef, NATCUR, fincursID ) != 0 )
           if ( not ПолучитьФинИн(fincursID, fins) )
             MsgBox("Отсутствует курс " + fins.Ccy + " / RUR ");
           end;
           curs = 0;
        end;
     else /* это рубли */
        curs = 1;
     end;
   /*к валюте не получилочь, попробуем сразу в рубли */
   elif ( ПолучитьКурс( RateDef, NATCUR, FIID ) == 0 )
        curs = 1;
   end;
   /* ничего не получилось, выдадим ошибку
   if ( curs == 0 )
      MsgBox("Основной курс ценной бумаги \"" + fin.Name + "\" не задан");
   end; */

   AvoirRate = string( RateDef.Rate / pow( 10, RateDef.Point ) * curs );
   if( money(AvoirRate) == $0 )
     AvoirRate = EMPTY_STR;
   else
     AvoirRate = SubStr(AvoirRate, 1, StrBrk(AvoirRate, ".") + RateDef.Point);
     if(RateDef.IsRelative == SET_CHAR)
        AvoirRate = setApp(money(AvoirRate),is_ap)+" % от номинала";
     else
        AvoirRate = setApp(money(AvoirRate),is_ap)+" руб.";
     end;
   end;
   /*РЕЙТИНГ ФИН. ИНСТРУМЕНТА*/
   if( GetMainObjAttr(null, OBJTYPE_AVOIRISS, UniID(avr, OBJTYPE_AVOIRISS), 3, AttrID, null, null) )
      vals.ObjectType = OBJTYPE_AVOIRISS;
      vals.GroupID = 3;
      vals.AttrID = AttrID;
      if ( getEQ(vals) )
         AvoirRating = vals.Name;
      end;
   end;
   /*КОД ТИПА ЦЕННОЙ БУМАГИ*/
   if( GetMainObjAttr(null, OBJTYPE_AVOIRISS, UniID(avr, OBJTYPE_AVOIRISS), 1, AttrID, null, null) )
      vals.ObjectType = OBJTYPE_AVOIRISS;
      vals.GroupID = 1;
      vals.AttrID = AttrID;
      if ( getEQ(vals) )
         AvoirKodT = vals.Name;
      end;
   end;
   /******************-ВЫВОД-НА-ПЕЧАТЬ-***********************/
   DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Анкета выпуска ценных бумаг", LogOprID, 0, false, RepDate, NULL, tablewidth );
   Rep.AddEmptyStr();
   DP_AddPrintCell( Rep,   "Код ценной бумаги " + fin.FI_Code, tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,   "Наименование " + cFin.Name(), tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,   "Вид ценной бумаги " + AvrKindName, tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,   "Номер государственной регистрации " + LSIN, tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,   "Международный код  " + ISIN, tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,   "Эмитент " + IssuerCode + " - " + IssuerName, tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
   Rep.AddStr();
  if (is_ap)
    DP_AddPrintCell( Rep,   "Форма выпуска  "+Settlement_Name+"  Объем выпуска  "+string(IssuerDimension:a:0:0)+" шт.", tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
    Rep.AddStr();
  else
    DP_AddPrintCell( Rep,   "Форма выпуска  "+Settlement_Name+"  Объем выпуска  "+string(IssuerDimension:z:0:0)+" шт.", tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
    Rep.AddStr();
  end;

  DP_AddPrintCell( Rep,  "Дата выпуска ", 13, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
  DP_AddPrintCell( Rep,  IssuedDate, 11, 0, "l:f" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
  DP_AddPrintCell( Rep,  "   Дата погашения  ", 19, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
  DP_AddPrintCell( Rep,  DrawingDate, 11, 0, "l:f" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
  Rep.AddStr();
  if (is_ap)
    DP_AddPrintCell( Rep,   "Номинал            " + ЧислоCЗаданнойТочностью(Nominal, fin.Point+2, "a") + " " + NominalCur + " (" + NominalStr + ")", tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
    Rep.AddStr();
    DP_AddPrintCell( Rep,   "Средневзвешенная цена       ", 28, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
    DP_AddPrintCell( Rep,   AvoirRate, tablewidth-28, 0, "l:w:a" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
    Rep.AddStr();
  else
    DP_AddPrintCell( Rep,   "Номинал            " + ЧислоCЗаданнойТочностью(Nominal, fin.Point+2) + " " + NominalCur + " (" + NominalStr + ")", tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
    Rep.AddStr();
    DP_AddPrintCell( Rep,   "Средневзвешенная цена       ", 28, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
    DP_AddPrintCell( Rep,   AvoirRate, tablewidth-28, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
    Rep.AddStr();
  end;

  DP_AddPrintCell( Rep,   "Код типа ценной бумаги  " + AvoirKodT, tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
  Rep.AddStr();
  DP_AddPrintCell( Rep,   "Рейтинг финансового инструмента   " + AvoirRating, tablewidth, 0, "l:w:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
  Rep.AddStr();

  DP_AddPrintCell( Rep,   "Дата принятия выпуска ценных бумаг на обслуживание " + SrvStartDate, tablewidth, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
  Rep.AddStr();
  DP_AddPrintCell( Rep,   "Номер операции принятия выпуска ценных бумаг на обслуживание " + SrvStartOpNum, tablewidth, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
  Rep.AddStr();
  DP_AddPrintCell( Rep,   "Дата снятия выпуска ценных бумаг с обслуживания " + SrvEndDate, tablewidth, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
  Rep.AddStr();
  DP_AddPrintCell( Rep,   "Номер операции снятия выпуска ценных бумаг с обслуживания " + SrvEndOpNum, tablewidth, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );

  Rep.AddEmptyStr(2);

  if( DP_CheckAvoirKind( cFin.AvoirKind(), AVOIRISSKIND_COUPON_BOND ) )
      coupons.FIID = FIID;
      coupons.Number = "";
      stat = getGE(coupons);
      if( stat and (coupons.FIID == FIID) )
        while( stat and (coupons.FIID == FIID) )
          point = double (pow (10, coupons.IncomePoint ));
          value = coupons.IncomeVolume/coupons.IncomeScale; /*возможно надо делить на масштаб*/
          value = value/point;
          НаборКупонов[НаборКупонов.size] = Купон( coupons.FIID, coupons.Number, coupons.DrawingDate,
                                                   coupons.IncomeRate, value, coupons.IncomePoint );
          stat = Next(coupons);
         end;
      end;
      /* уберем лишние 0*/
      while ( i < НаборКупонов.Size )
        НаборКупонов[i].SetPoint;
        i = i + 1;
      end;
      qsort(НаборКупонов, "СравнитьКупоны", 1);
      ПечатьКупонов( НаборКупонов );
      Rep.AddEmptyStr(5);
   end;

   DP_StdDeposInfo_Ex(Rep, RepFontStyleTitel, tablewidth);
   Rep.AddEmptyStr();
   DP_StdExecReport_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

   DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
   DP_EndProtocol();                 //закончить протоколирование

   return  DP_StdReportControl( LogoprID, 1, 0, 0, ToExcel, Rep );

end;

macro avoiris_form(  LogOprID:integer,  /* ИД информационной операции */
                     NumDprt:integer,   /* Филиал */
                     FIID:integer,      /* FIID */
                     RepDate:date,      /* Дата отчета */
                     ToExcel:bool,
                     IsApp:bool)        /* с апострофами */

  if(FIID == -1)
     MsgBox("Необходимо задать ценную бумагу");
     return 1;
  end;

  return CreateAvoirForm(LogOprID, NumDprt, FIID, RepDate, ToExcel, IsApp);
end;
