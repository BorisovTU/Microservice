/*
$Name: webcabvt.mac
$Module: Депозитарий
$Description: Выгрузка инструкций по голосованию для web-кабинета НРД
*/

import DlReport_h, dpstdrep, webcabpt;

private record reestr("dpregstr.dbt");

private macro SmartSetBuff(dest, src)
  if(ValType(src) == V_MEMADDR)
    SetBuff(dest, src);
  else
    Copy(dest, src);
  end;
end;

// Класс WR-отчета для выгрузки остатков в web-кабинет НРД
private class (DL_CReportTemplate) DP_CUploadClientVotesRep( v_reestr, _IsTemp )
  private var IsTemp = _IsTemp, exp_date = date(0,0,0);
  private var fi_arr = TArray();
  private var ddpregvote_name, ddpregagenda_name, ddpregcml_name;

  SmartSetBuff( reestr, v_reestr );

  exp_date = reestr.Date;

  ddpregvote_name   = IIF(IsTemp, "ddpregvote_tmp",   "ddpregvote_dbt");
  ddpregagenda_name = IIF(IsTemp, "ddpregagenda_tmp", "ddpregagenda_dbt");
  ddpregcml_name    = IIF(IsTemp, "ddpregcml_tmp",    "ddpregcml_dbt");

  PRIVATE MACRO PrintMode()
    return DL_OUTREPORT_EXCEL;
  END;


  PRIVATE MACRO BeginReport()
    CreateTotalBook();
    SetActiveSheet( "Отчет" );
  END;


  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;

  private macro FindFI(FIID)
    var i = 0;
    var fi;

    while(i < fi_arr.Size())
      if(fi_arr[i].FIID() == FIID)
        fi = fi_arr[i];
        break;
      end;
      i = i + 1;
    end;

    return fi;
  end;


  private macro FindorAddFI(FIID)
    var fi = FindFI(FIID);

    if(valType(fi) == V_UNDEF)
      fi = DP_CArchWebUploadFI(FIID, exp_date);
      fi_arr[fi_arr.Size()] = fi;
    end;

    return fi;
  end;


  private macro Create()
    var query, DataSet;
    var cmd, count, i = 0, stat;
    var fi, oldFIID = 0, pt, oldPartyID = 0;
    var CandNum = "", oldVoteID = 0;
    var NumVotes_Yes = 0.0, NumVotes_No = 0.0, NumVotes_Abstention = 0.0;
    
    SetXLSXFormat();

    query =   " select vt.t_HolderID, vt.t_FIID, vt.t_VoteID, vt.t_VoteResult, vt.t_VotingByProxy, "
            + "        vt.t_NumVotes, vt.t_NumVotes_Yes, vt.t_NumVotes_No, vt.t_NumVotes_Abstention,"
            + "        quest.t_Number as QuestNumber, solve.t_Number as SolveNumber, solve.t_Type, "
            + "        avr.t_ISIN, acnt.t_Code,  "
            + "        cast(NVL(cml.t_NumVotes_Yes, 0) as NUMBER(32,12)) as cmlNumVotes_Yes"
            + "   from "+ddpregvote_name+" vt, "+ddpregagenda_name+" quest, "+ddpregagenda_name+" solve, "
            + "        "+ddpregcml_name+" cml, "
            + "        ddepoacnt_dbt acnt, dfininstr_dbt fin, davoiriss_dbt avr"
            + "  where vt.t_RegisterID = ? "
            + "    and solve.t_AgendaID = vt.t_SolveID "
            + "    and quest.t_AgendaID = solve.t_ParentID "
            + "    and fin.t_FIID = vt.t_FIID "
            + "    and avr.t_FIID = fin.t_FIID "
            + "    and acnt.t_AutoKey = vt.t_HolderAccID "
            + "    and cml.t_VoteID (+)= vt.t_VoteID ";

    if(IsTemp)
      query = query + " and vt.t_Action != 3 ";//REGLIN_DELETE
    end;

    query = query + "order by vt.t_HolderID, vt.t_HolderAccID, vt.t_FIID, quest.t_Number, solve.t_Number, cml.t_CandidadID";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(reestr.RegisterID);

    count = cmd.GetCount();

    if( count == 0 )
      PrintFormatString( NULL, "N1_NoDataMsg", "Нет данных.");
      CopyAllSheetInTotalBook( NULL, false, "N1_NoDataMsg", 0 );
    else
      DataSet = cmd.Execute();

      CopyAllSheetInTotalBook( NULL, false, "N1_TableHeader", 0 );

      RegisterTable( "N1_MainTable", NULL,
                     "N1_QuestNum",    
                     "N1_SolveNum",
                     "N1_CandNum",
                     "N1_Acc",
                     "N1_OGRN",
                     "N1_TXID",
                     "N1_RBIC",
                     "N1_CCPT",
                     "N1_INCR",
                     "N1_BIRT",
                     "N1_FCCP",
                     "N1_LICS",
                     "N1_OTHR",
                     "N1_NSDR",
                     "N1_FIIN",
                     "N1_CORP",
                     "N1_ISIN",
                     "N1_GRN",
                     "N1_Country",
                     "N1_NRD_Code",
                     "N1_Yes",
                     "N1_No",
                     "N1_Abstention"
                  );


      InitProgress( count, "Экспорт инструкций по голосованию для web-кабинета НРД", "Экспорт инструкций по голосованию");

      while(DataSet.moveNext())

        NumVotes_Yes        = 0.0; 
        NumVotes_No         = 0.0; 
        NumVotes_Abstention = 0.0;

        if((oldPartyID != DataSet.HolderID) OR (DataSet.HolderID == -1)) // Если субъект сменился или субъекта в строке нет, тогда пересоздавать нужно обязательно
          oldPartyID = DataSet.HolderID;

          pt = NULL;

          pt = DP_CArchWebUploadPT(DataSet.HolderID, exp_date);
        end;

        if(oldFIID != DataSet.FIID)
          oldFIID = DataSet.FIID;
          fi = FindorAddFI(DataSet.FIID);
        end;

        if(DataSet.Type == DP_REGAGENDA_TYPE_CUMULATIVE) //Кумулятивное
          if(oldVoteID != DataSet.VoteID)
            CandNum = 0;
            oldVoteID = DataSet.VoteID;
          end;

          CandNum = CandNum + 1;

          NumVotes_Yes        = DataSet.cmlNumVotes_Yes;

        else //Простое
          CandNum = "";

          if(DataSet.VotingByProxy == SET_CHAR)
            NumVotes_Yes        = DataSet.NumVotes_Yes;
            NumVotes_No         = DataSet.NumVotes_No;
            NumVotes_Abstention = DataSet.NumVotes_Abstention;
          else
            if(DataSet.VoteResult == DP_REGVOTE_RESULT_YES)
              NumVotes_Yes       = DataSet.NumVotes;
            elif(DataSet.VoteResult == DP_REGVOTE_RESULT_NO)
              NumVotes_No        = DataSet.NumVotes;
            elif(DataSet.VoteResult == DP_REGVOTE_RESULT_ABSTENTION)
              NumVotes_Abstention= DataSet.NumVotes;
            end;
          end;
        end;
        
        NumVotes_Yes        = ЧислоCЗаданнойТочностью(double(NumVotes_Yes),        DP_GetPrecision(NumVotes_Yes, fi.SumPrecision()), "");
        NumVotes_No         = ЧислоCЗаданнойТочностью(double(NumVotes_No),         DP_GetPrecision(NumVotes_No, fi.SumPrecision()), "");
        NumVotes_Abstention = ЧислоCЗаданнойТочностью(double(NumVotes_Abstention), DP_GetPrecision(NumVotes_Abstention, fi.SumPrecision()), "");
        
        PrintTableLine("N1_QuestNum",   DataSet.QuestNumber,     null,
                       "N1_SolveNum",   DataSet.SolveNumber,     null,
                       "N1_CandNum",    CandNum,                 null,
                       "N1_Acc",        DataSet.Code,            null,
                       "N1_OGRN",       pt.OGRN(),               null,
                       "N1_TXID",       pt.INN(),                null,
                       "N1_RBIC",       pt.BIC(),                null,
                       "N1_CCPT",       pt.PassNumber(),         null,
                       "N1_INCR",       "",                      null,
                       "N1_BIRT",       pt.BirthNumber(),        null,
                       "N1_FCCP",       pt.InterPassNumber(),    null,
                       "N1_LICS",       "",                      null,
                       "N1_OTHR",       "",                      null,
                       "N1_NSDR",       pt.NSDCode(),            null,
                       "N1_FIIN",       "",                      null,
                       "N1_CORP",       "",                      null,
                       "N1_ISIN",       fi.ISIN(),               null,
                       "N1_GRN",        fi.LSIN(),               null,
                       "N1_Country",    fi.IssuerLat2Country(),  null, 
                       "N1_NRD_Code",   fi.NSDCode(),            null,
                       "N1_Yes",        NumVotes_Yes,            null,
                       "N1_No",         NumVotes_No,             null,
                       "N1_Abstention", NumVotes_Abstention,     null
                      );

        UseProgress(i = i + 1);
      end;

      RemProgress();

      EndTable();
      CopyAllSheetInTotalBook( NULL, false, GetTableRange(), 0 );
    end;
  end;

  initDL_CReportTemplate( NULL, "webcabvt.xls" );
end;


// Точка входа в макрос
macro UploadVotes( v_reestr, IsTemp:bool ) 
  var RepObj;
  
  RepObj = DP_CUploadClientVotesRep(v_reestr, IsTemp);

  RepObj.Run();

  return 0;
end;
