/*
$Name: webcabpt.mac
$Module: Депозитарий
$Description: Выгрузка для web-кабинета НРД. Вспомогательные классы и функции.
*/

import "dpstdrep.mac";

record RecordAdress ( adress );


PRIVATE MACRO GetPartyINN(INN_KPP):STRING
  var INN = "";
  var KPP = "";

  SplitFullINN(INN_KPP, INN, KPP);

  return INN;
END;


macro GetLat2Country(Lat3, Name:@variant)
  var country = TBFile("country.dbt");
  var Lat2 = "";

  Name = "";

  country.AddFilter(" t_CodeLat3 = '" + Lat3 + "' ");
  if( country.next() )
    Lat2 = country.rec.CodeLat2;
    if(Name != NULL)
      Name = country.rec.Name;
    end;
  end;
  country.DropFilter();

  return Lat2;
end;


// Класс архивной анкеты ц/б с дополнительными атрибутами для web-кабинета НРД
class DP_CArchWebUploadFI(_FIID, _RepDate)
  var FIID = _FIID, RepDate = _RepDate;
  private var m_fi, m_iss, m_IssCountryCode, m_NSDCode;

  if(_FIID != -1)
    m_fi = CDPFinInstr(_FIID, _RepDate);

    m_iss = CDPParty(m_fi.Issuer(), m_fi.cArchDate);
  end;

  // ************************ Атрибуты ***********************

  macro LSIN()
    if(FIID != -1)
      return m_fi.LSIN();
    else
      return "";
    end;
  end;

  macro ISIN()
    if(FIID != -1)
      return m_fi.ISIN();
    else
      return "";
    end;
  end;

  macro IssuerLat2Country()
    if(valType(m_IssCountryCode) == V_UNDEF)
      if(FIID != -1)
        m_IssCountryCode = GetLat2Country(m_iss.NRCountry());
      else
        m_IssCountryCode = "";
      end;
    end;
    return m_IssCountryCode;
  end;

  macro NSDCode()
    if(valType(m_NSDCode) == V_UNDEF)
      if(FIID != -1)
        m_NSDCode = DP_GetArchSecurCode(FIID, 14 /*FICK_NADC*/, RepDate);
      else
        m_NSDCode = "";
      end;
    end;
    return m_NSDCode;
  end;

  macro SumPrecision()
    if(FIID != -1)
      return m_fi.fins.rec.SumPrecision;
    else
      return 0;
    end;
  end;

end;


// Класс архивной анкеты субъекта с дополнительными атрибутами для web-кабинета НРД
// Если определен PartyID, то анкета восстанавливается по данным АО Депозитария, если они имеются по данному субъекту
// Переданные данные из остатков по ЛОРО-счету также используются, как наиболее актуальные, накладываясь поверх
class DP_CArchWebUploadPT(_PartyID, _RepDate, _LOROOwner )
  var PartyID = _PartyID, RepDate = _RepDate, LOROOwner = _LOROOwner;
  private var m_Party, m_OGRN, m_INN, m_BIC, m_PassNumber, m_BirthNumber, m_InterPassNumber, m_PassIsMain = false, m_BirthIsMain = false, m_InterPassIsMain = false,
              m_OthrNumber, m_NSDCode, m_Name, m_LegalAdress, m_Nationality, m_AgentName, m_AgentNSDCode,
              m_DocNumber, m_DocType, m_DocIsMain;

  if(_PartyID != -1)
    m_Party = CDPParty(_PartyID, _RepDate);
  end;

  macro ActuateData(_INN, _PaperKind, _PaperSeries, _PaperNumber, _Name, _LegalAdress, _Nationality)
    if(valType(_INN) != V_UNDEF)
      if(_INN != "")
        m_INN = GetPartyINN(_INN);
      end;
    end;

    m_PassIsMain = false;
    m_BirthIsMain = false;
    m_InterPassIsMain = false;

    if((valType(_PaperKind) != V_UNDEF) AND (valType(_PaperSeries) != V_UNDEF) AND (valType(_PaperNumber) != V_UNDEF))
      if((_PaperSeries != "") OR (_PaperNumber != ""))
        if(_PaperKind == 0) // Паспорт гражданина РФ
          m_PassNumber = trim(_PaperSeries + " " + _PaperNumber);
          m_PassIsMain = true;
        elif(_PaperKind == 4) // Свидетельство о рождении
          m_BirthNumber = trim(_PaperSeries + " " + _PaperNumber);
          m_BirthIsMain = true;
        elif(_PaperKind == 6) // Загранпаспорт гражданина РФ
          m_InterPassNumber = trim(_PaperSeries + " " + _PaperNumber);
          m_InterPassIsMain = true;
        else
          m_OthrNumber = trim(_PaperSeries + " " + _PaperNumber);
        end;
      end;
    end;

    if(valType(_Name) != V_UNDEF)
      if(_Name != "")
        m_Name = _Name;
      end;
    end;

    if(valType(_LegalAdress) != V_UNDEF)
      if(_LegalAdress != "")
        m_LegalAdress = _LegalAdress;
      end;
    end;

    if(valType(_Nationality) != V_UNDEF)
      if(_Nationality != "")
        m_Nationality = _Nationality;
      end;
    end;

  end;


  // ************************ Атрибуты ***********************

  macro ID()
    if(PartyID != -1)
      return PartyID;
    else
      return "";
    end;
  end;

  macro Name()
    if(valType(m_Name) == V_UNDEF)
      if(PartyID != -1)
        m_Name = m_Party.Name();
      else
        m_Name = "";
      end;
    end;
    return m_Name;
  end;


  private macro AddElement( stringBuffer, newElement )
    var result:string; 
    if( strlen(stringBuffer) == 0 ) 
      result = newElement;
    else 
      result = stringBuffer + ", " + newElement;
    end;
    return result;
  end;


  macro LegalAdress()
    var Kladr = false, status_ = -1, Result, CountryName;

    if(valType(m_LegalAdress) == V_UNDEF)
      if(PartyID != -1)

        // Соберем юридический (для физлица == регистрации) адрес из полей АО
        adress.PostIndex = m_Party.Index();
        adress.Country = m_Party.Country();
        adress.CodeRegion = m_Party.Region();
        adress.Region = m_Party.RegionName();
        adress.CodeProvince = m_Party.CodeProvince();
        adress.Province = m_Party.Province();
        adress.CodeDistrict = m_Party.CodeDistrict();
        adress.District = m_Party.District();
        adress.CodePlace = m_Party.CodePlace();
        adress.Place = m_Party.Place();
        adress.CodeStreet = m_Party.CodeStreet();
        adress.Street = m_Party.Street();
        adress.House = m_Party.House();
        adress.NumCorps = m_Party.NumCorps();
        adress.Flat = m_Party.Flat();

        /* 2. Почтовый индекс */
        if( adress.PostIndex != "" ) 
           Result = AddElement( Result, adress.PostIndex ); 
        end;

        /* 3. Название государства */
        if(( adress.Country != "" ) and ( adress.Country != {ResidentCountryCode} ))
           GetLat2Country(adress.Country, @CountryName);
           Result = AddElement( Result, CountryName ); 
        end;

        /* 4. название региона */
        if(( status_ != 2 ) and ( status_ != 3 ) and ( status_ != 5 ) and ( adress.Region != "" ) and ( adress.CodeRegion != "" )) 
          if(( adress.CodeRegion == "г." ) or ( adress.CodeRegion == "г" ))
            Result = AddElement( Result, adress.CodeRegion + " " + adress.Region ); 
          else
            Result = AddElement( Result, adress.Region + " " + adress.CodeRegion ); 
          end;
        end;

        /* 5. название района */
        if(( status_ != 1 ) and ( status_ != 3 ) and ( status_ != 4 ) and ( status_ != 5 ) and ( adress.Province != "" ) and ( adress.CodeProvince != "" )) 
          if(( adress.CodeProvince == "г." ) or ( adress.CodeProvince == "г" ))
            Result = AddElement( Result, adress.CodeProvince + " " + adress.Province ); 
          else
            Result = AddElement( Result, adress.Province + " " + adress.CodeProvince ); 
          end;
        end;

        /* 6. название города */
        if(( adress.District != "" ) and ( adress.CodeDistrict != "" )) 
          if(( adress.CodeDistrict == "г." ) or ( adress.CodeDistrict == "г" ))
            Result = AddElement( Result, adress.CodeDistrict + " " + adress.District ); 
          else
            Result = AddElement( Result, adress.District + " " + adress.CodeDistrict ); 
          end;
        end;

        /* 7. название населенного пункта */
        if(( adress.Place != "" ) and ( adress.CodePlace != "" )) 
          if(( adress.CodePlace == "г." ) or ( adress.CodePlace == "г" ))
            Result = AddElement( Result, adress.CodePlace + " " + adress.Place ); 
          else
            Result = AddElement( Result, adress.Place + " " + adress.CodePlace ); 
          end;
        end;

        /* 8. Название улицы */
        if(( adress.Street != "" ) and ( adress.CodeStreet != "" )) 
          Result = AddElement( Result, adress.CodeStreet + " " + adress.Street ); 
        end;

        /* 9. Дом, квартира и тд */
        if( adress.House != "" ) 
          Result = AddElement( Result, "д. " + adress.House ); 
        end;
        if( adress.NumCorps != "" ) 
          Result = AddElement( Result, "корп. " + adress.NumCorps ); 
        end;
        if( adress.Flat != "" ) 
          Result = AddElement( Result, "кв. " + adress.Flat ); 
        end;

        m_LegalAdress = Result;
      else
        m_LegalAdress = "";
      end;
    end;
    return m_LegalAdress;
  end;

  macro Country2Lat()
    if(valType(m_Nationality) == V_UNDEF)
      if(PartyID != -1)
        m_Nationality = GetLat2Country(m_Party.NRCountry());
      else
        m_Nationality = "";
      end;
    end;
    return m_Nationality;
  end;

  macro AgentName()
    if(valType(m_AgentName) == V_UNDEF)
      if(LOROOwner)
        m_AgentName = CDPParty(LOROOwner, RepDate).Name();
      else
        m_AgentName = Name();
      end;
    end;
    return m_AgentName;
  end;

  macro AgentNSDCode()
    if(valType(m_AgentNSDCode) == V_UNDEF)
      if(LOROOwner)
        m_AgentNSDCode = DP_GetArchPartyCode(LOROOwner, 23 /*PTCK_NRD*/, RepDate);
      else
        m_AgentNSDCode = DP_GetArchPartyCode({OurBank}, 23 /*PTCK_NRD*/, RepDate);
      end;
    end;
    return m_AgentNSDCode;
  end;

  macro OGRN()
    if(valType(m_OGRN) == V_UNDEF)
      if(PartyID != -1)
        m_OGRN = DP_GetArchPartyCode(PartyID, 27 /*PTCK_OGRN*/, RepDate);
      else
        m_OGRN = "";
      end;
    end;
    return m_OGRN;
  end;

  macro INN()
    if(valType(m_INN) == V_UNDEF)
      if(PartyID != -1)
        m_INN = GetPartyINN(m_Party.CodeINN());
      else
        m_INN = "";
      end;
    end;
    return m_INN;
  end;

  macro BIC()
    if(valType(m_BIC) == V_UNDEF)
      if(PartyID != -1)
        m_BIC = DP_GetArchPartyCode(PartyID, 3 /*PTCK_BIC*/, RepDate);
      else
        m_BIC = "";
      end;
    end;
    return m_BIC;
  end;

  macro PassNumber()
    if(valType(m_PassNumber) == V_UNDEF)
      if((PartyID != -1) AND (m_Party.PaperKind() == 0)) // Паспорт гражданина РФ
        m_PassNumber = trim(m_Party.PaperSeries() + " " + m_Party.PaperNumber());
      else
        m_PassNumber = "";
      end;
    end;
    return m_PassNumber;
  end;

  macro BirthNumber()
    if(valType(m_BirthNumber) == V_UNDEF)
      if((PartyID != -1) AND (m_Party.PaperKind() == 4)) // Свидетельство о рождении
        m_BirthNumber = trim(m_Party.PaperSeries() + " " + m_Party.PaperNumber());
      else
        m_BirthNumber = "";
      end;
    end;
    return m_BirthNumber;
  end;

  macro InterPassNumber()
    if(valType(m_InterPassNumber) == V_UNDEF)
      if((PartyID != -1) AND (m_Party.PaperKind() == 6)) // Загранпаспорт гражданина РФ
        m_InterPassNumber = trim(m_Party.PaperSeries() + " " + m_Party.PaperNumber());
      else
        m_InterPassNumber = "";
      end;
    end;
    return m_InterPassNumber;
  end;

  macro NSDCode()
    if(valType(m_NSDCode) == V_UNDEF)
      if(PartyID != -1)
        m_NSDCode = DP_GetArchPartyCode(PartyID, 23 /*PTCK_NRD*/, RepDate);
      else
        m_NSDCode = "";
      end;
    end;
    return m_NSDCode;
  end;

  // Определить документ клиента
  private macro DefineDoc()
    var PaperKind;
    if(m_PassIsMain AND (valType(m_PassNumber) != V_UNDEF)) // Первыми обрабатываем данные, которые могли получить только из остатков по ЛОРО, они имеют наибольший приоритет
      m_DocNumber = m_PassNumber;
      m_DocType = "CCPT";
      m_DocIsMain = "Y";
    elif(m_BirthIsMain AND (valType(m_BirthNumber) != V_UNDEF))
      m_DocNumber = m_BirthNumber;
      m_DocType = "BIRT";
      m_DocIsMain = "Y";
    elif(m_InterPassIsMain AND (valType(m_InterPassNumber) != V_UNDEF))
      m_DocNumber = m_InterPassNumber;
      m_DocType = "FCCP";
      m_DocIsMain = "Y";
    elif((valType(m_PassNumber) != V_UNDEF) AND (m_PassNumber != ""))
      m_DocNumber = m_PassNumber;
      m_DocType = "CCPT";
      m_DocIsMain = "N";
    elif((valType(m_InterPassNumber) != V_UNDEF) AND (m_InterPassNumber != ""))
      m_DocNumber = m_InterPassNumber;
      m_DocType = "FCCP";
      m_DocIsMain = "N";
    elif((valType(m_BirthNumber) != V_UNDEF) AND (m_BirthNumber != ""))
      m_DocNumber = m_BirthNumber;
      m_DocType = "BIRT";
      m_DocIsMain = "N";
    elif((valType(m_INN) != V_UNDEF) AND (m_INN != ""))
      m_DocNumber = m_INN;
      m_DocType = "TXID";
      m_DocIsMain = "N";
    elif((valType(m_OthrNumber) != V_UNDEF) AND (m_OthrNumber != ""))
      m_DocNumber = m_OthrNumber;
      m_DocType = "OTHR";
      m_DocIsMain = "N";
    elif(PartyID != -1) // Ни документа ни ИНН не было, зато есть ID, 
      // Получим главный документ из архивной анкеты
      m_DocNumber = trim(m_Party.PaperSeries() + " " + m_Party.PaperNumber());
      if(m_DocNumber != "" ) // Документ имеется
        m_DocIsMain = "Y"; // Он главный
        PaperKind = m_Party.PaperKind();
        if(PaperKind == 0 ) // Паспорт гражданина РФ
          m_DocType = "CCPT";
        elif(m_Party.PaperKind() == 4) // Свидетельство о рождении
          m_DocType = "BIRT";
        elif(m_Party.PaperKind() == 6) // Загранпаспорт гражданина РФ
          m_DocType = "FCCP";
        else // Все равно берем как "иной", ведь он главный и указан в анкете
          m_DocType = "OTHR";
        end;
      else // Документа нет, будем пытаться получить коды

        m_DocNumber = INN();
        if(m_DocNumber != "")
          m_DocType = "TXID";
          m_DocIsMain = "N";
        end;

        if(m_DocNumber == "")
          m_DocNumber = OGRN();
          if(m_DocNumber != "")
            m_DocType = "OGRN";
            m_DocIsMain = "N";
          end;
        end;

        if(m_DocNumber == "")
          m_DocNumber = BIC();
          if(m_DocNumber != "")
            m_DocType = "RBIC";
            m_DocIsMain = "N";
          end;
        end;

        if(m_DocNumber == "")
          m_DocNumber = NSDCode();
          if(m_DocNumber != "")
            m_DocType = "NSDR";
            m_DocIsMain = "N";
          end;
        end;

      end;
    else
      m_DocNumber = "";
      m_DocType = "";
      m_DocIsMain = "";
    end;

  end;

  macro DocNumber()
    if(valType(m_DocNumber) == V_UNDEF)
      DefineDoc()
    end;
    return m_DocNumber;
  end;

  macro DocType()
    if(valType(m_DocType) == V_UNDEF)
      DefineDoc()
    end;
    return m_DocType;
  end;

  macro DocIsMain()
    if(valType(m_DocIsMain) == V_UNDEF)
      DefineDoc()
    end;
    return m_DocIsMain;
  end;

end;
