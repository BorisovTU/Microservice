/*
$Name: dpclosedpacc.mac
$Module: Депозитарий
$Description: Массовое закрытие счетов депо в административном порядке
*/

Import rsd;
import DPInter, "secinter.mac", "dpstdrep.mac", "fi.mac", "ws_validation.mac", "spserv.mac";
import RsbDataSet;
import FIInter, CurrInter, CTInter, secinter, deposerv;

private const CHAPT5 = 5; //глава л/с депо

var HeadTable = "┌─────────────────────────┬─────────────────┬─────────────────┬─────────────────┬───────────────┬─────────────────────────┬────────────────────────┐\n"+
                "│     Номер счёта депо    │  Наименование   │  Наименование   │   Код анкеты    │ Дата открытия │       Номер раздела     │ Дата открытия раздела  │\n"+
                "│                         │  счета депо     │   Депонента     │    депонента    │     счета     │        счета депо       │     счета депо         │\n"+
                "├─────────────────────────┼─────────────────┼─────────────────┼─────────────────┼───────────────┼─────────────────────────┼────────────────────────┤";

var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
var RepHead = CMakeReport("заголовок");
var   HeaderStr = "Протокол процедуры закрытия счетов депо в административном порядке";
const RepFontStyleTitel =  "ex_FS(b)";
var  _is_ap;
var HeadDate = "за <"+{Curdate}+">";
DP_StdHeaderLO_Ex( RepHead, RepFontStyleTitel, HeaderStr, 0, 0, false, null, null, tablewidth );
DP_AddPrintCell( RepHead, headDate, 140, 0, "r:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
   RepHead.AddStr();

var HeadTable1 = "┌─────────────────────────┬─────────────────┬─────────────────┬─────────────────┬───────────────────────┬──────────────────────────────────────────┐\n"+
                 "│     Номер счёта депо    │  Наименование   │  Наименование   │   Код анкеты    │      Номер раздела    │                Причина                   │\n"+
                 "│                         │  счета депо     │   Депонента     │    депонента    │       счета депо      │                                          │\n"+
                 "├─────────────────────────┼─────────────────┼─────────────────┼─────────────────┼───────────────────────┼──────────────────────────────────────────┤";

var tablewidth1 = Index(HeadTable1, "\n");
var Rep1 = CMakeReport(HeadTable1);
const RepFontStyleTitel1 =  "ex_FS(b)";

VAR CountErr = 0, CountMes = 0;

macro PrintStr(CodeDA, NameDA, NameDep, CodeDep,
               OpenDateDA, CodeDP, OpenDateDP)

   DP_AddPrintCell( Rep, CodeDA, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, NameDA, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, NameDep, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, CodeDep, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, OpenDateDA, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, CodeDP, 0, 0, "l:w" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, OpenDateDP, 0, 0, "r:w" + RepFontStyleTitel );
   Rep.AddStr();
end;
	
macro PrintStr1(CodeDA1, NameDA1, NameDep1, CodeDep1,
               CodeDP1, Mes)

   DP_AddPrintCell( Rep1, CodeDA1, 0, 0, "l:w" + RepFontStyleTitel1 );
   DP_AddPrintCell( Rep1, NameDA1, 0, 0, "l:w" + RepFontStyleTitel1 );
   DP_AddPrintCell( Rep1, NameDep1, 0, 0, "l:w" + RepFontStyleTitel1 );
   DP_AddPrintCell( Rep1, CodeDep1, 0, 0, "l:w" + RepFontStyleTitel1 );
   DP_AddPrintCell( Rep1, CodeDP1, 0, 0, "l:w" + RepFontStyleTitel1 );
   DP_AddPrintCell( Rep1, Mes, 0, 0, "l:w" + RepFontStyleTitel1 );
   Rep1.AddStr();
end;

// проверка счета депо на открытые лицевые счета
PRIVATE MACRO IsCloseAcc(dpacc)

  var query, Select, DataSet;
  var closed = 0;
  var  CodeDA = "", NameDA = "", NameDep = "", CodeDep = "" , OpenDateDA ={Curdate}, CodeDP = "", OpenDateDP = {CurDate} , Mes = "";
  CodeDA = dpacc.rec.Code;
  NameDA = dpacc.rec.Name;
  CodeDep = ПолучитьКодСубъекта(dpacc.rec.Owner,1);
  NameDep = GetPartyName(dpacc.rec.Owner, PTKN_FULLNAME);
  Mes = "Необходимо запустить закрытие нулевых лицевых счетов";

  query = " SELECT acnt.t_code, count(ACNT.t_autokey) "
        + " FROM daccount_dbt acc, ddepoacnt_dbt acnt "
        + " WHERE acc.t_chapter = 5 "
        + " AND acc.t_open_close = chr(0) "
        + " AND acc.t_deporoot = ? "
        + " AND acnt.t_autokey = acc.t_depoacc " 
        + " GROUP BY acnt.t_code ";
  Select = RSDCommand( query );
  Select.AddParam("Deporoot",  RSDBP_IN, dpacc.rec.Autokey );
  Select.Execute();
  DataSet = TRsbDataSet(Select);
  while(DataSet.moveNext())            //CodeDep??
    closed = closed +1 ;
    CodeDP = DataSet.Code;
    PrintStr1(CodeDA, NameDA, NameDep, CodeDep, CodeDP, Mes);

  end;

return closed;
END;

// заполнение массива 
PRIVATE MACRO FillOpClose(dpacc,OpClose:@TArray)
  var query, Select, DataSet;
  var i = 0;
  query = " SELECT acnt.* "
        + " FROM ddepoacnt_dbt acnt"
        + " WHERE acnt.t_root = ? "               
        + " ORDER BY acnt.t_autokey ";
  Select = RSDCommand( query );

  Select.AddParam("Root",  RSDBP_IN, dpacc.rec.autokey  );  /*0*/
  Select.Execute();

  DataSet = TRsbDataSet(Select);
  while(DataSet.moveNext())
    OpClose[i] = DataSet.Status;
    i = i+1;
  end;


END;

// вывод в протокол инф. о закрытых счета/ разделах депо
PRIVATE MACRO PrintRepRoot(dpacc,OpClose)
  var query, Select, DataSet;
  var i = 0;
  var  CodeDA = "", NameDA = "", NameDep = "", CodeDep = "" , OpenDateDA, CodeDP = "", OpenDateDP , Mes = "";
  CodeDA = dpacc.rec.Code;
  NameDA = dpacc.rec.Name;
  CodeDep = ПолучитьКодСубъекта(dpacc.rec.Owner,1);
  NameDep = GetPartyName(dpacc.rec.Owner, PTKN_FULLNAME);
  OpenDateDA = dpacc.rec.OpenDate;

  query = " SELECT acnt.* "
        + " FROM ddepoacnt_dbt acnt"
        + " WHERE acnt.t_root = ? "               
        + " ORDER BY acnt.t_autokey ";

  Select = RSDCommand( query );

  Select.AddParam("Root",  RSDBP_IN, dpacc.rec.autokey  );  /*0*/
  Select.Execute();

  DataSet = TRsbDataSet(Select);
  while(DataSet.moveNext())
    if(not(DataSet.Autokey==DataSet.Root))
      CodeDP = DataSet.Code;
      OpenDateDP = date(DataSet.OpenDate);	
    end;
    if(OpClose[i]==0)
      PrintStr(CodeDA, NameDA, NameDep, CodeDep, OpenDateDA, CodeDP, OpenDateDP);
    end;
    i = i+1;
  end;

END;

PRIVATE MACRO  CheckAdmoprForDepoAcc(dpacc)
  Var quer, Sel, DataS, Dpar = -1, DparTemp = -1, count = 0 ;
  var  CodeDA1 = "", NameDA1 = "", NameDep1 = "", CodeDep1 = "" , CodeDP1 = "", Mes = "";

  quer = " select admo.t_DepoPart as DepoPart, acnt.t_code as Code "
       + " from ddepoadmo_dbt admo, ddepoacnt_dbt acnt "
       + " Where admo.t_operdate > ? "
       + " and admo.t_depoacc = ? "
       + " and acnt.t_Autokey(+) = admo.t_DepoPart "        
       + " order by admo.t_depopart";
  Sel = RSDCommand( quer );
  Sel.AddParam("OperDate", RSDBP_IN, {CurDate}        );       /*0*/
  Sel.AddParam("DEPOACC",  RSDBP_IN, dpacc.rec.autokey  );  /*1*/
  Sel.Execute();

  DataS = TRsbDataSet(Sel);
  while(DataS.moveNext())
    Dpar = DataS.DepoPart;
    if(Dpar != DparTemp)
      CodeDA1 = dpacc.rec.Code;
      NameDA1 = dpacc.rec.Name;
      CodeDep1 = ПолучитьКодСубъекта(dpacc.rec.Owner,1);
      NameDep1 = ПолучитьКороткоеИмяСубъекта( dpacc.rec.Owner);
      if(DataS.DepoPart >0)
        CodeDP1 = DataS.Code ;
      end;
      if(Dpar==0)
        mes = "Для счета депо существуют административные операции за более поздние даты. Счет не закрыт";
      else
        mes = "Для раздела счета депо существуют административные операции за более поздние даты. Счет не закрыт";
      end;
      DparTemp = Dpar;                                                                            	
      PrintStr1(CodeDA1, NameDA1, NameDep1, CodeDep1, CodeDP1, Mes);
    end;
    count = count +1;
  end;
  return count;
END;


PRIVATE MACRO  GetFinDate(dpacc, closed:@integer)    // Определяется дата последней операции на каждом счете лицевом, отбирается лицевой счет, на котором последняя дата совершенной операции была наиболее поздней.
  var query, Select, DataSet, clos = 0;
  var FinDate = {CurDate} -730 ;  // если нет проводок на счетах  вернем

//дата последней операции на счете депо       
  query = " SELECT (SELECT NVL (MAX (t_date_carry), TO_DATE ('01.01.0001', 'DD.MM.YYYY'))"    //01.01.0001 - если проводки есть, но дата	 нулевая
                       + " FROM dacctrn_dbt "
                       + " WHERE (( t_account_payer = acc.t_account AND t_fiid_payer = acc.t_code_currency)"
                       + " OR (    t_account_receiver = acc.t_account AND t_fiid_receiver = acc.t_code_currency)"           
                       + " /*AND t_date_carry > '30.12.2017'*/)) t_Final_Date, "
        + " acc.* "
        + " FROM daccount_dbt acc "
        + " WHERE t_chapter = 5 "
        + " AND acc.t_deporoot = ?";
  Select = RSDCommand( query );

  Select.AddParam("Deporoot",  RSDBP_IN, dpacc.rec.Autokey );

  Select.Execute();

  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())                        //если есть запись 
    if(date(DataSet.Final_Date) != date(0,0,0)) 
      FinDate = date(DataSet.Final_Date);       //если есть дата проводки- берем ее
    end;                                        //если дата проводки нулевая - берем дату на два года ранее текущей
  end;

 //ищем открытые лицевые счета счета
  query = " SELECT (SELECT NVL (MAX (t_date_carry), TO_DATE ('01.01.0001', 'DD.MM.YYYY'))"    //01.01.0001 - если проводки есть, но дата	 нулевая
                       + " FROM dacctrn_dbt "
                       + " WHERE (( t_account_payer = acc.t_account AND t_fiid_payer = acc.t_code_currency)"
                       + " OR (    t_account_receiver = acc.t_account AND t_fiid_receiver = acc.t_code_currency)"           
                       + " /*AND t_date_carry > '30.12.2017'*/)) t_Final_Date, "
        + " acc.* "
        + " FROM daccount_dbt acc "
        + " WHERE t_chapter = 5 "
        + " AND acc.t_open_close = chr(0) "
        + " AND acc.t_deporoot = ?";
  Select = RSDCommand( query );
  Select.AddParam("Deporoot",  RSDBP_IN, dpacc.rec.Autokey );
  Select.Execute();
  DataSet = TRsbDataSet(Select);
  if(DataSet.moveNext())
    closed = 1 ;
  end;
  return FinDate;

END;	
	
MACRO  CloseDPAcc()

  var query, Select, DataSet;
  var CDate = {curdate};
  var count = 0;
  var N = 0;
  var err = 0;
  var dt;
  var closed;   //статус лицевого счета 
  var acc = TRecHandler("account");
  var dpacc = TRecHandler("depoacnt");
  var Day = DaysInYearByBasis(BASIS_ACT_ACT, {CurDate}, false);
  var TestDate = CDate - Day;
  TestDate = GetDateAfterWorkDays(TestDate, -N);      //Дата минус N рабочих дней
  var mess = "";
  var stat = 0;
  var OpClose = TArray;   //массив открытых/закрытых разделов счета депо

  var  CodeDA = "", NameDA = "", NameDep = "", CodeDep = "" , OpenDateDA ={Curdate}, CodeDP = "", OpenDateDP = {CurDate} , Mes = "";


  if( (GetDialogFlag()) and (not IsShedulerRunning()) )

    mess = "Вы действительно хотите закрыть счета депо с нулевыми остатками?";
    if(false == GetTrue(false, mess))
      return; //1    Если выбран ответ "Нет" - работа процедуры завершается без формирования протокола
    end;

    GetRegistryValue("DEPO\\ZERO_CLOSEACC_N", V_INTEGER, N, err);
    if( err != 0 )
      msgbox("Ошибка поиска значения настройки \"Срок уведомления Депонента о расторжении Депозитарного договора\"");
      return;
    end;
  
  end;

  DP_StartUseTechAuto(TECHAUTODOC_CLOSEDPACC);
  
  query = " SELECT acnt.* "
        + " FROM ddepoacnt_dbt acnt"
        + " WHERE acnt.t_autokey = acnt.t_root "             //корневой
        + " AND acnt.t_kind = 2 "                            //пассивные
        + " AND acnt.t_opendate < " + GetSQLDate(TestDate)   //период с даты открытия 365/366 + N дней 2.1
        + " AND acnt.t_status = 0 "                          //открыт
        + " AND rsb_depo.depokindtype( acnt.t_AutoKey ) IN (1,2,3,11,12 ) "               //нужные типы пассивных счетов
        + " AND 0 = (select count(acc.t_accountid) "         //нулевые остатки на всех (на закрытых они нулевые)
                  + " from daccount_dbt acc " 
                  + " where acc.t_Chapter =  "+ CHAPT5
                  + " and acc.t_Open_Date <= " + GetSQLDate(CDate)    //открыт ранее текущей даты
                  + " and 0 <> rsb_account.restall(acc.T_ACCOUNT,acc.T_CHAPTER,acc.T_CODE_CURRENCY,"+ GetSQLDate(CDate) +")"  
                  + " and ACC.T_DEPOroot = acnt.t_autokey) "
         + " ORDER BY acnt.t_autokey";
  
  
  count = SQL_GetNRecs(query);
  
  Dl_CallInsertStat( DL_OUTREPORT_STD  );
  
  if( count > 0 )
    InitProgress ( count, "Идет обработка счетов депо...", "Процедура закрытия счетов депо");
    DataSet = TRsbDataSet(query);
    while( DataSet.moveNext() )
  
      OpClose.size = 0;
      closed = 0;   //статус лицевого счета 
      DataSet.GetRecord().CopyTo(dpacc.rec);
      CodeDA = dpacc.rec.Code;
      NameDA = dpacc.rec.Name;
      CodeDep = ПолучитьКодСубъекта(dpacc.rec.Owner,1);
      NameDep = ПолучитьКороткоеИмяСубъекта( dpacc.rec.Owner);
      OpenDateDA = dpacc.rec.OpenDate;
      if((dt=GetFinDate(dpacc,@closed))>=TestDate)  //пропускаем, т.к. меньше срока
      else
        if(IsCloseAcc(dpacc))        //        больше срока, проверяем на закрытие лицевого счета
          CountErr = CountErr +1;
        else
          if((CheckAdmoprForDepoAcc(dpacc)))
            CountErr = CountErr +1;
          else
            FillOpClose(dpacc,@OpClose);
            stat = DP_CloseMasAdm(dpacc.rec.autokey);    //закрытие счета депо
            if(stat == 0)
              PrintRepRoot(dpacc,OpClose);
              CountMes = CountMes +1;
              OpClose.size = 0;
            elif(stat > 0)
              Mes = GetErrorMsg(stat);
              PrintStr1(CodeDA, NameDA, NameDep, CodeDep, CodeDP, Mes);
              CountErr = CountErr +1;
            end;
          end;
        end;
      end;
    end;
    //печать протокола
    if((CountMes+CountErr))
        RepHead.PrintRep();

      if(CountMes)
         [Перечень закрытых счетов и разделов счета депо:];
        if( CountErr == 0)
          DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);               // стандартный подвал отчета, добавляем если нет таблицы с ошибками
        end;
        Rep.PrintRep();
      else
       [Счета депо не закрывались];
      end;
  
      if(CountErr)
        [Перечень незакрытых  счетов:];
        DP_StdFooter_Ex(Rep1, RepFontStyleTitel1, NULL, NULL, tablewidth1);              // добавляем стандартный подвал отчета
        Rep1.PrintRep();
      end;

    else    //если нет никаких таблиц

      DP_AddPrintCell( RepHead, "Счета депо не закрывались", 0, 0, "r:" + RepFontStyleTitel,_is_ap, REP_ELEM_STR );
      RepHead.AddStr();
      DP_StdFooter_Ex(RepHead, RepFontStyleTitel, NULL, NULL, tablewidth);               
      RepHead.PrintRep();

    end;
  
  end;

  DP_EndUseTechAuto();
 
END;

CloseDPAcc()
