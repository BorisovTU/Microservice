/*                           
$Name:             spdpacpr.mac
$Module:           Депозитарий
$Description:      Операционный журнал раздела счета депо
*/
import spacform, DPInter, "adress.mac", "pt_lib.mac", cb_sql;
import RsbDataSet;


var TableAdm = "┌──────────────────────┬──────────────────────────────────┬──────────────────────────────────┐\n"+
               "│ ПАРАМЕТР             │ Значение до операции             │ Значение после операции          │\n"+
               "├──────────────────────┼──────────────────────────────────┼──────────────────────────────────┤";


var TableInf = "┌──────────────────┬─────────────────────────────────────┬─────────────────────────────┬───────────────────────┬───────────────┬────────────────┐\n"+
               "│ Инф. документ  № │ составил                            │ получатель                  │ доставка              │ поручение №   │ принято        │\n"+
               "├──────────────────┼─────────────────────────────────────┼─────────────────────────────┼───────────────────────┼───────────────┼────────────────┤";

var Rep = CMakeReport();
const RepFontStyleTitel =  "ex_FS(b)";
var WidthTable = Index(TableInf, "\n");

const SC_SPDRAFT_STATUS_CLOSE = 3;
const ALG_ITEM_KIND = 3104;   /* объект, по которому осущ. операция*/
const UNDEF_ID = -1;

var ДСчет_arr = TARRAY;

var disp_subpart;
var printDeponentName;  
var printDeponentIURISD;
var printOperatorName;
var printOperatorIURISD;
var printMortgageeName;
var printMortgageeIURISD;


var NameAlg = TBFile("namealg");
var llvl    = TBFile("llvalues");
var personF = TBFile("person");

var icount = 0, count = 0, isCount = false;

/*для class AdmOperations*/
record depoacnt(depoacnt); 

/*интерфейс в Namealg.dbt*/
macro __NameAlg(Type, Number)
  NameAlg.rec.iTypeAlg = Type;
  NameAlg.rec.iNumberAlg = Number;
  if(NameAlg.GetEQ)
    return NameAlg.rec.szNameAlg;
  end;
  return UnSetName;
end;

macro OperName(OperVal)
   personF.rec.oper=OperVal;
   if (personF.GetEQ)
      return personF.rec.Name;
   end;
   return UnSetName;
end;

/* получить страну субъекта */
private macro GetPartyICountry( cParty )
  var country = cParty.NRCountry();
  var cmd, dataSet;

  if(country != "")
    cmd = DL_RSDCommand("SELECT t_Name FROM dcountry_dbt WHERE t_CodeLat3 = ?");
    cmd.addParam(country);

    dataSet = cmd.execute();

    if (dataSet.moveNext()) 
      return dataSet.Name;
    end;
  else
    if(cParty.NotResident() != "" )
      return "Россия";
    end;
  end;

  return "";
end;


/*счет у кореспондента*/
CLASS CorespAccount(BriefCode, __Kind)
   var Kind = __Kind;

   var corschem = TBFile("corschem", "R", 3);
   corschem.rec.Account == BriefCode; 
   corschem.rec.FIID    == ALLFININSTR;
   corschem.rec.Department = {OperDprt};
   if(not GetEQ(corschem)) 
      return NULL; 
   end;

   macro CorType()
      if(corschem.IsBase == SET_CHAR) 
         if(Kind == активный)
           return "Лоро базовый";
         else
           return "Ностро базовый";
         end;
      else
         if(Kind == активный)
           return "Лоро расчетный";
         else
           return "Ностро расчетный";
         end;
      end;
   end;
END;

CLASS AdmOperations(__FromDate, __UptoDate, __DepoAccAK, __iDeponent, __AdmopRegDATEbegin, __AdmopRegDATEend, __DepoOpenDate, __AdmopXldMask)
   var FromDate = __FromDate,
       UptoDate = __UptoDate,
       DepoAccAK= __DepoAccAK,
       iDeponent= __iDeponent,
       AdmopRegDATEbegin = __AdmopRegDATEbegin, 
       AdmopRegDATEend   = __AdmopRegDATEend,
       DepoOpenDate      = __DepoOpenDate, 
       AdmopXldMask      = __AdmopXldMask;

   var AdmOpIDs = TArray;

   var sNAMEs = TArray;
   var sFROM  = TArray;
   var sTO    = TArray;
   var iContString = 0;

   var depoadmo = TBFile("depoadmo", "R", 1); 
   var DataSetAdmo, queryADM;
   var GGround = TBFile("spground");
   var GReport = TBFile("spground");
   var dpgrrep  = TBFile("dpgrrep");
   var splogopr = TBFile("splogopr");

   macro FindGround()
      GGround.rec.SPgroundID = depoadmo.rec.Ground;
      if(GGround.GetEQ)
        return GGround.rec.Xld;
      end;
      return "";
   end;

   macro FindReport()

      var DataSet, query, cmd;
      query  =   " select spgr.t_Xld from dspground_dbt spgr, ddpgrrep_dbt rep, ddprepdoc_dbt repdoc "
               + " where spgr.t_SpgroundID = repdoc.t_RepSPgroundID "
               + "   and rep.t_ID = repdoc.t_GrRepID "
               + "   and rep.t_Desc = ? " 
               + "   and rep.t_Recipient = ? ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(depoadmo.rec.Ground);
      cmd.AddParam(depoadmo.rec.Initiator);

      DataSet = cmd.Execute();
      if(  DataSet.MoveNext() )
        return DataSet.Xld;
      end;

      return "";
   end;

   macro CheckAdmop(Ground)
      FindGround(Ground);
      if((AdmopXldMask != "") and CompareStrWithMasks(AdmopXldMask, GGround.rec.Xld)) 
         return false; 
      end;
    return true;
   end;

   macro cmpAdmOpID( key, elem )
     if  ( key > elem) return +1;
     elif( key < elem) return -1;
     end;
     return 0;
   end;
   /*Сбор подходящих административных операций*/
   macro ColectAdmOperations()
      var Item, Root, Part, continue_cicle;
      file dpacnt(depoacnt);

      dpacnt.AutoKey = DepoAccAK;
      if( not GetEQ(dpacnt) )
        return 0;
      end;  

      AdmOpIDs.size = 0;
      /*Административные операции счета депо*/
      if( dpacnt.Root == DepoAccAK )
        Item = SPDP_DEPO_ACCOUNT;
        Root = DepoAccAK;
        Part = 0;
      else
        Item = SPDP_DEPO_PARTITION;
        Root = dpacnt.Root;
        Part = DepoAccAK;
      end;
      queryADM =   " select t_AdmoprID, t_Ground from ddepoadmo_dbt "
                 + " where  t_PartyID  = " + iDeponent
                 + " and    t_Item     = " + Item
                 + " and    t_DepoAcc  = " + Root
                 + " and    t_FIID     = " + ALLFININSTR
                 + " and    t_OperDate >=" + GetSQLDate(FromDate)
                 + " and    t_OperDate <=" + GetSQLDate(UptoDate)
                 + " and    t_Ground in ( select t_SpgroundID from dspground_dbt " 
                 + "                      where  t_RegistrDate >= " + GetSQLDate(AdmopRegDATEbegin)
                 + "                      and    t_RegistrDate <= " + GetSQLDate(AdmopRegDATEend) 
                 + "                    ) ";
      if( Part > 0 )
        queryADM = queryADM + " and    t_DepoPart = " + Part;
      end;

    
      DataSetAdmo = TRSBDataSet(queryADM);

      while( DataSetAdmo.MoveNext() )
         if(CheckAdmop(DataSetAdmo.Ground))
            AdmOpIDs[AdmOpIDs.size] = DataSetAdmo.AdmoprID;
         end;
      end;
      CLOSE(DataSetAdmo);
      /*Административные операции владельца счета*/
      if( iDeponent == {OurBank} )
        Item = SPDP_OURDEPOSITARY;
      else
        Item = SPDP_CLIENT;
      end;

      queryADM =   " select t_AdmoprID, t_Ground from ddepoadmo_dbt "
                 + " where t_Item      = " + Item
                 + " and   t_PartyID   = " + iDeponent
                 + " and   t_OperDate <= " + GetSQlDate(UptoDate)
                 + " and   t_Ground in ( select t_SpgroundID from dspground_dbt " 
                 + "                     where  t_RegistrDate >= " + GetSQLDate(AdmopRegDATEbegin)
                 + "                     and    t_RegistrDate <= " + GetSQLDate(AdmopRegDATEend) 
                 + "                    ) "
                 + " ORDER BY t_AdmoprID ";

      DataSetAdmo = TRSBDataSet(queryADM);

      while( DataSetAdmo.MoveNext() )
         if(CheckAdmop(DataSetAdmo.Ground))
            AdmOpIDs[AdmOpIDs.size] = DataSetAdmo.AdmoprID;
         end;
      end;
      return AdmOpIDs.size;
   end;

   macro First()
     if(not ColectAdmOperations())
        return false;
     end;
     depoadmo.AddFilter("t_AdmoprID = " + AdmOpIDs[AdmOpIDs.Size-1]);
     if(depoadmo.next())
        FindGround();
        FindReport();
        AdmOpIDs.Size = AdmOpIDs.Size-1;
        return true;
     end;
     return false;
   end;

   macro Next()
     if(AdmOpIDs.Size)
        depoadmo.AddFilter( "t_AdmoprID = " + AdmOpIDs[AdmOpIDs.Size-1] );
        if(AdmOpIDs.Size and depoadmo.next())
           FindGround();
           FindReport();
           AdmOpIDs.Size = AdmOpIDs.Size-1;
           return true;
        end;
     end;
     return false;
   end;

   macro AdmRec()
      return depoadmo.rec;
   end;

   macro FirstContentString()
      iContString = 0;
      GetDEPOADMO_Contents(depoadmo, sNAMEs, sFROM, sTO);
      while((iContString < sNAMEs.size) and
            not (sNAMEs[iContString] and (sFROM[iContString] or sTO[iContString]) and
                                         (sFROM[iContString] != sTO[iContString])
                )
           )
         iContString = iContString + 1;
      end;
      return iContString < sNAMEs.size;
   end;
   
   macro NextContentString()
      iContString = iContString + 1;
      while((iContString < sNAMEs.size) and
            not (sNAMEs[iContString] and (sFROM[iContString] or sTO[iContString]) and 
                                         (sFROM[iContString] != sTO[iContString])
                ) 
           )
         iContString = iContString + 1;
      end;
      return iContString < sNAMEs.size;
   end;

   macro CSName()
      return sNAMEs[iContString];
   end;
   macro CSFrom()
      return sFROM[iContString];
   end;
   macro CSTo()
      return sTO[iContString];
   end;


   macro ActionCaption()
      return __NameAlg(3105, depoadmo.rec.OperKind);
   end;

   macro ActionDone()
      return depoadmo.rec.OperDate;
   end;

   macro ActionExecuter()
      return OperName(depoadmo.rec.Oper);
   end;

   macro Item()
      return __NameAlg(3104, depoadmo.rec.Item); /*"счёт депо"*/
   end;

END;

CLASS InfoOperations(__FromDate, __UptoDate, __DepoAccAK)
   var FromDate = __FromDate,
       UptoDate = __UptoDate,
       DepoAccAK= __DepoAccAK;
   var continue_cicle;
   var DataSetLogopr; var queryLog;
   var spinfopr = TBFile("spinfopr");
   var spground = TBFile("spground");
   var spgrRep  = TBFile("spground", "R", 6);

   macro Clear()
     /* splogopr тут не чистить */
     ClearRecord(spinfopr.rec);
     ClearRecord(spground.rec);
     ClearRecord(spgrRep.rec);
   end;

   macro First()
     Clear();
     queryLog =   " select t_Draft, t_AutoKey, t_recId, t_Oper from dsplogopr_dbt "
                + " where t_State = 3 "
                + " and   t_CarryDate >= " + GetSQLDate(FromDate)
                + " and   t_CarryDate <= " + GetSQlDate(UptoDate)
                + " and   t_recID in ( select t_spinfdocID from dspinfopr_dbt "
                + "                    where  t_DepoAcc  = " + DepoAccAK
                + "                       or  t_DepoPart = " + DepoAccAK
                + "                  )"
                + " and   t_AutoKey in ( select t_SourceDocID from dspground_dbt "
                + "                      where  t_SourceDocKind = 830 "
                + "                        and  t_Direction = " + EXITING
                + "                        and  t_Division  = " + SelfDivision
                + "                    ) ";
     DataSetLogopr = TRSbDataSet(queryLog);
     while( DataSetLogopr.MoveNext() )
        spinfopr.rec.spinfdocID = DataSetLogopr.recId;
        if( spinfopr.getEQ() and ((spinfopr.rec.DepoAcc == DepoAccAK) or (spinfopr.rec.DepoPart == DepoAccAK)) )
             spground.rec.SPgroundID   = DataSetLogopr.Draft;
             spgrRep.rec.SourceDocKind = 830;
             spgrRep.rec.SourceDocID   = DataSetLogopr.AutoKey;
             spgrRep.rec.Direction     = EXITING;
             spgrRep.rec.Department    = {OperDprt};
             spgrRep.rec.Division      = SelfDivision;

             if(not spground.GetEQ)
                ClearRecord(spground.rec);
             end;
             if( spgrRep.GetEQ )
                return true;
             end;
          end;
        end;
     return false;
   end;

   macro Next()
     Clear();
     while(DataSetLogopr.MoveNext())
        spinfopr.rec.spinfdocID = DataSetLogopr.recId;
        if( spinfopr.getEQ() and ((spinfopr.rec.DepoAcc == DepoAccAK) or (spinfopr.rec.DepoPart == DepoAccAK)) )
             spground.rec.SPgroundID   = DataSetLogopr.Draft;
             spgrRep.rec.SourceDocKind = 830;
             spgrRep.rec.SourceDocID   = DataSetLogopr.AutoKey;
             spgrRep.rec.Direction     = EXITING;
             spgrRep.rec.Department    = {OperDprt};
             spgrRep.rec.Division      = SelfDivision;
             if(not spground.GetEQ)
                ClearRecord(spground.rec);
             end;
             if( spgrRep.GetEQ )
                return true;
             end;
          end;
        end;
     return false;
   end;

  macro Oper()
     return DataSetLogopr.oper;
  end;

  macro Rec()
    return spground.rec;
  end;

  macro RepRec()
    return spgrRep.rec;
  end;

END;

CLASS ExecuterInfo(OperVal)
   var Name, Post; 
   var person = TBFile("person");
   var TypeAc = TBFile("typeac"); 
  /*Исполнитель (опер)*/
   /*Имя*/
   person.rec.oper=OperVal;
   if (person.GetEQ)
      Name = person.rec.Name;
   else
      Name = UnSetName;
   end;
   /*Должность*/
   TypeAc.rec.iNumType = 13;
   TypeAc.rec.Type_Account = person.rec.cTypePerson;
   if(TypeAc.GetEQ)
     Post = TypeAc.rec.Name_Type;
   else
     Post = "отв. работник депозитария";
   end;
END;

macro GetSettaccOwnerStr(DeponentID)
   var Settacc;// = TBFile("settacc");
   var query;
   var Счет = "";

   var FIID;
   var NominalFICode;
   var NominalFIIDName;
   var BankCorrID;
   var BankCorrBIC = "";
   var BankCorrName = "";
   var CorrAcc; //Корсчет
   var BankID;//наименование банка
   var BankBIC = "";
   var BankName = "";
   var Account;//счет
      
   macro AddressParty(ID)
     record RecordAdress ( adress );
     var PARTY;
     var address = "-";

     query = "select t_PartyID from dparty_dbt";
     PARTY = TRsbDataSet(query);
     if( PARTY.MoveNext() )
       if(НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdress))
         address = RecordAdress.Adress;
       end;
     else
       MsgBox("Не найден депозитарий");
     end;

     return address;
   end;
   ДСчет_arr.Size = 0;
    //найдем СПИ, привязанные к депоненту
   query =   " select distinct sett.t_FIID, sett.t_BankCorrID, sett.t_BankCorrName, sett.t_CorrAcc, sett.t_BankID, sett.t_BankName, sett.t_Account "
           + " from dsettacc_dbt sett, dpmautoac_dbt pm"
           + " where sett.t_FIKind = " + FIKIND_CURRENCY
           + "   and sett.t_PartyID = " + DeponentID
           + "   and sett.t_FIID > -1"
           + "   and sett.t_SettAccID = pm.t_SettAccID";
   Settacc = TRsbDataSet(query);
   while( Settacc.MoveNext() )
     FIID = Settacc.FIID;
     BankCorrID = Settacc.BankCorrID;
     BankCorrName = string(Settacc.BankCorrName);
     CorrAcc = string(Settacc.CorrAcc);
     BankID  = Settacc.BankID;
     BankName = string(Settacc.BankName);
     Account = string(Settacc.Account);
     
     if( CorrAcc == "")
       CorrAcc = "-";
     end;
     NominalFICode   = ПолучитьКодФинИн(FIID);
     NominalFIIDName = FinInName(FIID);

     BankCorrBIC = ПолучитьКодСубъекта(BankCorrID, PTCK_BIC);
     BankBIC     = ПолучитьКодСубъекта(BankID, PTCK_BIC);

     if(BankCorrName == "")
       BankCorrName = NameParty(BankCorrID);
     end;

     if(BankName == "")
       BankName = NameParty(BankID);
     end;

     Счет = NominalFICode+" ("+NominalFIIDName+")";
     Счет = " р/с " + Account + "  "+Счет + ", в банке " + BankName + ", " + 
            AddressParty(BankID) + ", БИК " + BankBIC + " к/с " + CorrAcc + " в " + 
            BankCorrName + ", БИК " + BankCorrBIC;
     ДСчет_arr[ДСчет_arr.size] = Счет;
   end;
   if( not ДСчет_arr.size )
     ДСчет_arr[ДСчет_arr.size] = "-";
   end;

end;
/* Получить УЛ по счету\разделу, у которого есть полномочия*/
class CDepoAcntAcap
  var dpParty:CDPParty = null;
  
  // Фильтр 
  private var role     = 0,           // роль УЛ
              nodeID   = -1,          // узел раздела
              dateFrom = Date(0,0,0), // Дата От
              dateTo   = Date(0,0,0); // Дата До
  private var cmd, dataSet = null;

  /* Установить фильтр */
  macro SetFilter(nodeID_:integer, role_:integer, dateFrom_:Date, dateTo_:date)
    role     = role_;
    nodeID   = nodeID_;
    dateFrom = dateFrom_;
    dateTo   = dateTo_;

    dataSet = null;
    dpParty = null;
  end;
  /* Получить первое значение*/
  macro GetFirst():bool
    var stat = false;
    var query = " select acap.t_apartyid "
              + "   from ddpacap_dbt acap, ddepoacnt_dbt dacnt "
              + "  where dacnt.t_AutoKey = ? /*1*/" 
              + "    and acap.t_APNodeID = dacnt.t_Root"
              + "    and acap.t_Role = ? /*2*/" 
              + "    and ( (acap.t_FromDate<=? /*3*/ and (acap.t_ToDate>=? /*4*/ OR acap.t_ToDate=TO_DATE('01.01.0001','DD.MM.YYYY')))"
              + "         or (acap.t_ToDate>=? /*5*/ and acap.t_ToDate<=? /*6*/))"
              + "    and exists (select acapau.t_APID "
              + "                  from ddpacapau_dbt acapau "
              + "                 where acapau.t_APID = acap.t_APID "
              + "                   and acapau.t_AUNodeID IN (select acnt.t_AutoKey "
              + "                                               from ddepoacnt_dbt acnt "
              + "                                              start with acnt.t_AutoKey = ? /*7*/" 
              + "                                              connect by acnt.t_AutoKey = prior acnt.t_Superior "
              + "                                            ) )";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(nodeID);   /*1*/
    cmd.AddParam(role);     /*2*/
    cmd.AddParam(dateTo);   /*3*/
    cmd.AddParam(dateTo);   /*4*/
    cmd.AddParam(dateFrom); /*5*/
    cmd.AddParam(dateTo);   /*6*/
    cmd.AddParam(nodeID);   /*7*/

    dataSet = cmd.Execute();

    stat = dataSet.moveNext();
    if (stat)
      dpParty = CDPParty(dataSet.APartyID, dateTo);
    end;
    return stat;
  end;  
  /* Следующее значение */
  macro GetNext():bool
    var stat = dataSet.moveNext();

    if (stat)
      dpParty = CDPParty(dataSet.APartyID, dateTo);
    end;
    return stat;
  end;
end;

/*****************************************************************************/
macro ВыпискаОперЖурналаСчетаДепо(AdmopXldMask, FromDate, UptoDate, DepoAccAK, iDepopart,
                                  iDeponent, AdmopRegDATEbegin, AdmopRegDATEend)
   var i = 1, continue_cicle, n=0, t = 0;
   var Format, j = 0;
   var reporter, Receptionist;
   var PapProp, CorespAcc;
   var DepoAcc, query;
   var DepoPart;
   var AdmOprtn;
   var InfoOprtn;
   var stat = 0;
   var isADM = false, isINF = false;
   var ADM_OP, INF_OP;
   var cDeponent;

   DepoAcc = DepoInfo(DepoAccAK, UptoDate);

   if ( isCount == true )
      count = count + 1;
      return 0;
   end;
   if ( iCount == 0 )
      DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Операционный журнал раздела счета ДЕПО", 0, 0, false, FromDate, UptoDate, WidthTable );
   end;

   /* Возможно нужен цикл по подразделам */
   DepoPart = DepoInfo(iDepopart, UptoDate);
   /* возможно в вызывающей функции */

   //проверим, есть ли хоть одна АО
   query =   " select count( t_AdmoprID ) as count from ddepoadmo_dbt "
           + " where t_OperDate >= " + GetSQLDate(AdmopRegDATEbegin)
           + "   and t_OperDate <= " + GetSQLDate(AdmopRegDATEend)
           + "   and ( t_DepoAcc = "+DepoPart.AutoKey+" or t_DepoPart = "+DepoPart.AutoKey+")";
             
   ADM_OP = TRsbDataSet(query);
   if( (ADM_OP.MoveNext()) and (ADM_OP.count > 0) )
      isADM = true;
      AdmOprtn = AdmOperations(FromDate, UptoDate, iDepopart, iDeponent, AdmopRegDATEbegin, AdmopRegDATEend, DepoAcc.OpenDate, AdmopXldMask);
   end;
   CLOSE(ADM_OP);

   //проверим, есть ли хоть одна инф. оп. в закрытых
   query =   " select count( t_SPinfdocID ) as count from dspinfopr_dbt "
           + " where t_State = " + SC_SPDRAFT_STATUS_CLOSE
           + "   and t_toOperDate >= "+GetSQLDate(FromDate)
           + "   and t_toOperDate <= "+GetSQLDate(UptoDate)
           + "   and ( t_DepoAcc = "+DepoPart.AutoKey+" or t_DepoPart = "+DepoPart.AutoKey+")";
 
   INF_OP = TRsbDataSet(query);
   if( (INF_OP.MoveNext()) and (INF_OP.count > 0) )
      isINF = true;
      InfoOprtn = InfoOperations(FromDate, UptoDate, iDepopart);
   end;
   CLOSE(INF_OP);

   /* есть ли хоть 1 АО */
   if ( isADM )
      isADM = AdmOprtn.First()
   end;
   /* есть ли хоть 1 ИО */
   if ( isINF )
      isINF = InfoOprtn.First()
   end;

   DP_AddPrintCell( Rep,  "     из операционного журнала счёта депо ", 41, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  DepoAcc.Code, 26, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  " по разделу ", 12, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
   DP_AddPrintCell( Rep,  DepoPart.DepoRec.BriefCode, 50, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "     статус счета:   " + DepoAcc.StatusName, WidthTable, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "     статус раздела: " + DepoPart.StatusName, WidthTable, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "     вышестоящие разделы (снизу вверх): " + DepoPart.PartitionPath, WidthTable, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
   Rep.AddStr();
   DP_AddPrintCell( Rep,  "-------------------------------------------------------------------------------------------", WidthTable, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
   Rep.AddStr();
   if(printDeponentName)
      DP_AddPrintCell( Rep,  " Владелец счета депо:", WidthTable, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      Rep.AddStr();
      DP_AddPrintCell( Rep,  "     ", 5, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  "Наименование", 12, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  "  ", 2, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  DepoPart.DeponentName + "   ИНН  " + DepoPart.DeponentINN, 43, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
      Rep.AddStr();

      cDeponent = CDPParty( DepoPart.DeponentID, UptoDate );
      if(printDeponentIURISD)
         if(cDeponent.PaperKind() >= 0)
            DP_AddPrintCell( Rep,  "     документ:   " + cDeponent.PaperKindName() + " серия: " + cDeponent.PaperSeries() + " №: " + cDeponent.PaperNumber(), WidthTable, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
            Rep.AddStr();
            DP_AddPrintCell( Rep,  "     выдан:   " + date_as_string(1, date(cDeponent.PaperIssuedDate())) + " " + cDeponent.PaperIssuerName(), WidthTable, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
            Rep.AddStr();
         end;
         DP_AddPrintCell( Rep,  "     Юрисдикция  " + DepoPart.Nationality, WidthTable, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
         Rep.AddStr();
      end;
   end;
   if(printOperatorName)
      var operators = CDepoAcntAcap();
      operators.SetFilter(iDepopart, /*1*/DEPOACAPROLE_OPERATOR, FromDate, UptoDate);

      stat = operators.GetFirst();
      if (stat)
         DP_AddPrintCell( Rep,  " Оператор:", 50, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      end;
      while (stat)
         Rep.AddStr();
         DP_AddPrintCell( Rep,  "     " + "Наименование  " + operators.dpParty.Name() + "   ИНН  " + operators.dpParty.CodeINN(), WidthTable, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
         Rep.AddStr();

         if(printOperatorIURISD)
            if(operators.dpParty.PaperKind() >= 0)
               DP_AddPrintCell( Rep,  "     документ:   " + operators.dpParty.PaperKindName() + " серия: " + operators.dpParty.PaperSeries() + " №: " + operators.dpParty.PaperNumber(), WidthTable, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
               Rep.AddStr();
               DP_AddPrintCell( Rep,  "     выдан:   " + date_as_string(1, date(operators.dpParty.PaperIssuedDate())) + " " + operators.dpParty.PaperIssuerName(), WidthTable, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
               Rep.AddStr();
            end;
         DP_AddPrintCell( Rep,  "     Юрисдикция  " + GetPartyICountry(operators.dpParty), WidthTable, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
         Rep.AddStr();
         end;
         
         stat = operators.GetNext();
      end;
   end;

   if(printMortgageeName)
      var mortgagees = CDepoAcntAcap();
      mortgagees.SetFilter(iDepopart, /*1*/DEPOACAPROLE_MORTGAGEE, FromDate, UptoDate);

      stat = mortgagees.GetFirst();
      if (stat)
         DP_AddPrintCell( Rep,  " Залогодержатель:", 50, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      end;
      while (stat)
         Rep.AddStr();
         DP_AddPrintCell( Rep,  "     " + "Наименование  " + mortgagees.dpParty.Name() + "   ИНН  " + mortgagees.dpParty.CodeINN(), WidthTable, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
         Rep.AddStr();

         if(printMortgageeIURISD)
            if(mortgagees.dpParty.PaperKind() >= 0)
               DP_AddPrintCell( Rep,  "     документ:   " + mortgagees.dpParty.PaperKindName() + " серия: " + mortgagees.dpParty.PaperSeries() + " №: " + mortgagees.dpParty.PaperNumber(), WidthTable, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
               Rep.AddStr();
               DP_AddPrintCell( Rep,  "     выдан:   " + date_as_string(1, date(mortgagees.dpParty.PaperIssuedDate())) + " " + mortgagees.dpParty.PaperIssuerName(), WidthTable, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
               Rep.AddStr();
            end;
            DP_AddPrintCell( Rep,  "     Юрисдикция  " + GetPartyICountry(mortgagees.dpParty), WidthTable, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
            Rep.AddStr();
         end;
         
         stat = mortgagees.GetNext();
      end;
   end;

   //все денежные счета оператора раздела счета депо
   GetSettaccOwnerStr(DepoAcc.Owner);
   if( ДСчет_arr.size )
     DP_AddPrintCell( Rep,  " ", 1, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  "Денежный счет для выплаты доходов:", 34, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  "     ", 5, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
     DP_AddPrintCell( Rep,  ДСчет_arr[0], 42, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
     Rep.AddStr();
     t = 1;
     while( t < ДСчет_arr.size )
        DP_AddPrintCell( Rep,  "                                        ", 40, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
        DP_AddPrintCell( Rep,  ДСчет_arr[t], 42, 0, "l:w:" + RepFontStyleTitel, false, REP_ELEM_STR );
        Rep.AddStr();
       t =t + 1;
     end;
   end;

   DP_AddPrintCell( Rep,  "-------------------------------------------------------------------------------------------", WidthTable, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
   Rep.AddStr();

   if(isADM)
      DP_AddPrintCell( Rep,  "     АДМИНИСТРАТИВНЫЕ ОПЕРАЦИИ", 50, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      Rep.AddStr();
      continue_cicle = true;
   end;
   while(continue_cicle)
      DP_AddPrintCell( Rep,  "     № ", 7, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  i, 3, 0, "r:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  " от    ", 7, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  AdmOprtn.ActionDone, 10, 0, "l:f:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  " поручение №  ", 14, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  AdmOprtn.GGround.rec.Xld, 21, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  " от ", 4, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  AdmOprtn.GGround.rec.RegistrDate, 10, 0, "l:f:" + RepFontStyleTitel, false, REP_ELEM_STR );
      Rep.AddStr();
      DP_AddPrintCell( Rep,  "     Содержание операции: " + AdmOprtn.ActionCaption, WidthTable, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      Rep.AddStr();
      DP_AddPrintCell( Rep,  "     Исполнил: ", 15, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  AdmOprtn.ActionExecuter, 25, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  " ", 1, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      DP_AddPrintCell( Rep,  AdmOprtn.AdmRec.SystDate, 10, 0, "l:f:" + RepFontStyleTitel, false, REP_ELEM_STR );
      Rep.AddStr();
      stat = AdmOprtn.FirstContentString();
      if(stat) 
         DP_AddPrintCell( Rep,  "     ИЗМЕНЕНИЯ АНКЕТЫ В РЕЗУЛЬТАТЕ ОПЕРАЦИИ:", 50, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
         Rep.AddStr();
         Rep.AutoScan( "[\n" + TableAdm + "]" );
         j = 0;
         while(stat)
           if(j == 0)
             Format ="ex_B(rlb)";
           else
             Format ="";
           end;
           DP_AddPrintCell( Rep,  AdmOprtn.CSName, 22, 0, "l:" + Format + RepFontStyleTitel );
           DP_AddPrintCell( Rep,  AdmOprtn.CSFrom, 34, 0, "l:w:" + Format + RepFontStyleTitel );
           DP_AddPrintCell( Rep,  AdmOprtn.CSTo, 34, 0, "l:w:" + Format + RepFontStyleTitel );
           Rep.AddStr();
           stat = AdmOprtn.NextContentString();
           j = j + 1;
         end;
      end;
      Rep.AddEmptyStr();
      continue_cicle = AdmOprtn.Next();
      i = i + 1;
   end;

   if(isINF)
      DP_AddPrintCell( Rep,  "     ИНФОРМАЦИОННЫЕ ОПЕРАЦИИ", 50, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
      Rep.AddStr();
      continue_cicle = true;
      Rep.AutoScan( "[\n" + TableInf + "]" );
      j = 0;
      while(continue_cicle)
         if(j == 0)
           Format ="ex_B(rlb)";
         else
           Format ="";
         end;
         reporter     = ExecuterInfo(InfoOprtn.oper);
         Receptionist = ExecuterInfo(InfoOprtn.RepRec.Receptionist);
         DP_AddPrintCell( Rep,  InfoOprtn.RepRec.Xld, 18, 0, "l:w:" + Format + RepFontStyleTitel );
         DP_AddPrintCell( Rep,  reporter.Name + " " + string(InfoOprtn.RepRec.RegistrDate:f), 37, 0, "l:w:" + Format + RepFontStyleTitel );
         DP_AddPrintCell( Rep,  Receptionist.Name, 29, 0, "l:w:" + Format + RepFontStyleTitel );
         DP_AddPrintCell( Rep,  string(InfoOprtn.RepRec.SignedDate:f) + "   " + "", 23, 0, "l:" + Format + RepFontStyleTitel );
         DP_AddPrintCell( Rep,  InfoOprtn.Rec.Xld, 15, 0, "l:w:" + Format + RepFontStyleTitel );
         DP_AddPrintCell( Rep,  InfoOprtn.Rec.RegistrDate, 16, 0, "l:f:" + Format + RepFontStyleTitel );
         Rep.AddStr();
         continue_cicle=InfoOprtn.Next;
         j = j + 1;
      end;
   end;
   Rep.AddEmptyStr();
   Rep.AddEmptyStr();
   Rep.AddEmptyStr();
   UseProgress ( icount = icount + 1 ); 
   return 0;
end;

macro AcnDepoPartJournal ( iDepart:integer,
                     iDepoAcc : integer,
                     iDepopart : integer,      /* раздел счета депо */
                     disp_part : bool,         /* все подразделы */
                     iDeponent : integer,
                     AdmopRegDATEbegin : date,
                     AdmopRegDATEend : date, 
                     BeginDate : date,                    
                     EndDate : date,                      
                     AdmopXldMask : string,               
                     pDeponentName : bool,                
                     pDeponentIURISD : bool,              
                     pOperatorName : bool,                
                     pOperatorIURISD : bool,
                     pMortgageeName : bool,                
                     pMortgageeIURISD : bool,
                     ToExcel : bool
                     )                     

   var stat = 0;

   disp_subpart        = disp_part;
   printDeponentName   = pDeponentName;
   printDeponentIURISD = pDeponentIURISD;
   printOperatorName   = pOperatorName;
   printOperatorIURISD = pOperatorIURISD;
   printMortgageeName   = pMortgageeName;
   printMortgageeIURISD = pMortgageeIURISD;

   var DataSet;
   var sQuery = ""; var cnt = 0; var nrec = 0;

   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

   if(EndDate == date(0,0,0)) EndDate = date(31,12,9999); end;
   if(AdmopRegDATEend == date(0,0,0)) AdmopRegDATEend = date(31,12,9999); end;

   sQuery = " select t_AutoKey, t_Root, t_Owner from ddepoacnt_dbt where t_Superior != 0 ";
   sQuery = sQuery + " and t_AutoKey != t_Root and t_Department = " + iDepart;
   if( iDeponent != UNDEF_ID )
     sQuery = sQuery + " and t_Owner = " + iDeponent; 
   end;
   if( (iDepoAcc > 0) and ( iDepopart <= 0) )
     sQuery = sQuery + " and t_Root = " + iDepoAcc;
     sQuery = sQuery + " and t_AutoKey != " + iDepoAcc;
   elif( iDepopart > 0)
     sQuery = sQuery + " and t_AutoKey = " + iDepopart;
   end;

   sQuery = sQuery + " ORDER BY t_AutoKey";

   DataSet = TRsbDataSet( sQuery, RSDVAL_CLIENT, RSDVAL_STATIC);
   DataSet.MoveLast();
   nrec = DataSet.GetRecCount();
   DataSet = TRSBDataSet(sQuery);
   InitProgress (nrec, "Формирование отчета", "Операционный журнал раздела счета ДЕПО");
   while( DataSet.MoveNext() )
       if( not ВыпискаОперЖурналаСчетаДепо(AdmopXldMask, BeginDate, EndDate, DataSet.Root,
                                           DataSet.AutoKey, DataSet.Owner, 
                                           AdmopRegDATEbegin, AdmopRegDATEend) )

          count = count + 1;
       end;
       UseProgress(cnt = cnt+1);
   end;
   
   RemProgress();
   if ( count > 0 )
      DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, WidthTable);

      DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
      DP_EndProtocol();                         //закончить протоколирование

      if( ToExcel )
        Rep.PrintWinRep();
        Rep.ShowWinRep();
        exit(1);
      else
        Rep.PrintRep();
      end;
   else
     if(not DP_ProtocolIsEmpty())
       DP_AddPrintCell(Rep, "Не найдено счетов ДЕПО, удовлетворяющих параметрам отчета", 60, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR);
       Rep.AddStr();

       DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
       DP_EndProtocol();                 //закончить протоколирование

       if( ToExcel )
         Rep.PrintWinRep();
         Rep.ShowWinRep();
         exit(1);
       else
         Rep.PrintRep();
       end;
     else
       DP_EndProtocol();                 //закончить протоколирование
     end;

      MsgBox("Не найдено счетов ДЕПО, удовлетворяющих параметрам отчета");
   end;

   return stat;
end;