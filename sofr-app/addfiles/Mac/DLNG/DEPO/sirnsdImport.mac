/*
$Name:             sirnsdImport.mac
$Module:           Депозитарий
$Description:      Импорт справочников ценных бумаг НРД. Шаги
*/

import sirnsdActions, sirnsdObjects, sirnsdXML;

// Режим запуска
private const SIRNSD_MERGE      = 0, // сравнить
              SIRNSD_UPDATE     = 1, // обновить
              SIRNSD_ADD        = 2, // добавить
              SIRNSD_UPDATE_ADD = 3; // обновить+добавить
private const ALL = "Все";

// Параметры запуска процедуры
private class TParmExecute(act_, isAvoir_, isCoupon_, isPartial_, isOrg_, Type711_, isProtocolExt_)
   var act:integer        = act_,
       isAvoir:bool       = isAvoir_,
       isCoupon:bool      = isCoupon_,
       isPartial:bool     = isPartial_,
       isOrg:bool         = isOrg_,
       isProtocolExt:bool = isProtocolExt_,
       Type711:string     = Type711_,
       startTime:time     = time(); // время запуска процедуры
end;

private var action:CAction = null; // объект действий
private var parm:TParmExecute = null;

/* Фильтрация загруженных ц.б. по коду 711*/
private macro AvoirFilterType711(type711, action)
   var query = "UPDATE dsirnsd_avr_tmp SET t_State = " + STATE_SOURCE + " WHERE " + action.GetQueryForFilter();

   if (type711 != ALL)
      query = query + " AND t_Sec_Type_BR_Code = ?";
   end;

   var cmd = RSDCommand(query);

   if (type711 != ALL)
      cmd.addParam("", RSDBP_IN, type711);
   end;
   cmd.Execute();
end;

/* Фильтрация загруженных организаций по действию*/
private macro OrgFilter(action)
   var query = "UPDATE dsirnsd_org_tmp SET t_State = " + STATE_SOURCE + " WHERE " + action.GetQueryForFilter();

   if(IsEqClass("CUpdateAction", action))
     query = query + " AND t_PartyID IN (SELECT t_PartyID FROM dpartyown_dbt WHERE t_PartyKind = " + PTK_ISSUER + ") ";
   elif(IsEqClass("CUpdateAddAction", action))
     query = query + " AND (t_IsLinkSec = CHR(0) OR t_IsLinkSec = CHR(88) AND t_PartyID IN (SELECT t_PartyID FROM dpartyown_dbt WHERE t_PartyKind = " + PTK_ISSUER + ")) "
   end;

   var cmd = RSDCommand(query);
   cmd.Execute();
end;
/* Код 711 по облигации?*/
private macro IsBondType711(sType711)
  return (("BON1" == sType711) or ("BON2" == sType711) or ("BON3" == sType711) or ("BON4" == sType711) or 
          ("BON5" == sType711) or ("BON6" == sType711) or ("BON61" == sType711) or ("BON7" == sType711) or ("BON71" == sType711));
end;
/* Код 711 по акции?*/
private macro IsShareType711(sType711)
  return (("SHS1" == sType711) or ("SHS2" == sType711) or ("SHS3" == sType711) or ("SHS4" == sType711) or
          ("SHS5" == sType711) or ("SHS51" == sType711) or ("SHS6" == sType711) or ("SHS61" == sType711));
end;
/* Код 711 по паю?*/
private macro IsUnitType711(sType711)
  return (("SHS7" == sType711) or ("SHS71" == sType711) or ("SHS8" == sType711) );
end;
/* Код 711 по депозитарной расписке?*/
private macro IsDepType711(sType711)
  return (("DR" == sType711));
end;
/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;
// -----------------------------------
// Разбор xml-файлов
// -----------------------------------
macro SIRNSD_Step_ParseXML(iAction, bAvoir, bCoupon, bPartial, bOrg, sType711, bProtocolExt) : bool
   var stat = true;

   parm = TParmExecute(iAction, bAvoir, bCoupon, bPartial, bOrg, sType711, bProtocolExt);

   if (iAction == SIRNSD_MERGE)
      action = CMergeAction(parm);
   elif (iAction == SIRNSD_UPDATE)
      action = CUpdateAction(parm);
   elif (iAction == SIRNSD_ADD)
      action = CAddAction(parm);
   elif (iAction == SIRNSD_UPDATE_ADD)
      action = CUpdateAddAction(parm);
   else
      msgbox("Неизвестный режим запуска");
      return false;
   end;

   TruncateTablesSIRNSD();

   if ((parm.Type711 != "Все") and not IsBondType711(parm.Type711) and not IsShareType711(parm.Type711) and
       not IsUnitType711(parm.Type711) and not IsDepType711(parm.Type711))
      var strMsg = "Выбран вид ц/б, по которому нет данных в справочниках НРД.";
      if (parm.isOrg)
         if (not GetTrue(false, strMsg+"|Обрабатывать эмитентов?"))
            return false;
         else
            parm.isAvoir = false;
         end;
      else
         msgbox(strMsg);
         return false;
      end;
   end;
   
   // 2. Загружаем справочники во временные файлы
   if (parm.isAvoir)
      if (iAction != SIRNSD_MERGE) 
         COrganization_XML(CUpdateAddAction(parm)).readData(); // Организации грузим полностью
      elif ((iAction == SIRNSD_MERGE) and parm.isOrg) // для сравнения организации грузим по действию и по флажку
         COrganization_XML(action).readData();
      else
         COrganization_XML(CParseAction(parm)).readData(); // просто грузим, без идентификации
      end;
      // если выбрана ДР, то остальные бумаги подкачиваем по действию Parse
      if ((ALL == sType711) or IsBondType711(parm.Type711) or IsDepType711(parm.Type711))
         CBondsSIRNSD_XML(IIF(IsDepType711(parm.Type711),CParseAction(parm),action), parm.isCoupon, parm.isPartial).readData();
      end;
      if ((ALL == sType711) or IsShareType711(parm.Type711) or IsDepType711(parm.Type711))
         CSharesSIRNSD_XML(IIF(IsDepType711(parm.Type711),CParseAction(parm),action)).readData();
      end;
      if ((ALL == sType711) or IsUnitType711(parm.Type711) or IsDepType711(parm.Type711))
         CUnitsSIRNSD_XML(IIF(IsDepType711(parm.Type711),CParseAction(parm),action)).readData();
      end;
      if ((ALL == sType711) or IsDepType711(parm.Type711))
         CDeposSIRNSD_XML(action).readData();
      end;
      // обновим статус на 0 те цб, код 711 который совпадает с указанным в панели
      AvoirFilterType711(parm.Type711, action);
      // постобработка нерезидентов (поиск partyID у выпусков, у которых организации являются эмитентами)
      TOrganizationData().ProcessOnNotResident();
   elif (parm.isOrg)
      COrganization_XML(action).readData();
   end;

   if ((iAction != SIRNSD_MERGE) and parm.isOrg)
      // Если работаем с организацями, то установим статус исходный для конкрентного действия (чтобы для обновления не было записей для вставки)
      OrgFilter(action);
   end;

   return stat;
end;

// -----------------------------------
// Действие с записями
// -----------------------------------
macro SIRNSD_Step_Action()
   var count = 0, i = 0;
   // Сохраним время запуска процедуры для протокола
   parm.startTime = time();
   // 1. Выпуски
   if (parm.isAvoir)
      var cmdAvr, dataSetAvr;
      var dataAvr:TAvoirData = null;

      cmdAvr = DL_RSDCommand("SELECT * FROM dsirnsd_avr_tmp WHERE T_State = 0");

      count = cmdAvr.GetCount();
      if (count > 0)
         InitProgress(count, "Обработка выпусков", "Обработка выпусков");   

         // 1. пробегаемся по выпускам НЕ депоз. расписки
         cmdAvr = null;
         cmdAvr = DL_RSDCommand("SELECT * FROM dsirnsd_avr_tmp WHERE T_State = 0 AND T_KINDDATA != ?");
         cmdAvr.addParam(AVOIRISSKIND_DEPOSITORY_RECEIPT);         

         dataSetAvr = cmdAvr.Execute();
         while(dataSetAvr.moveNext())
            if (dataSetAvr.State == 0) // проверим на статус, обо при обработке цб может получиться такое, что статус след. бумаги поменялся на 3, а в массиве он будет 0)
               dataAvr = CreateDataAvr(dataSetAvr, parm.isProtocolExt);
               if (dataAvr != null)
                  if (action.SearchChanges(dataAvr))
                     action.ActionOnData(dataAvr);
                  else
                     action.AddProtocolProcessedRec(dataAvr);
                  end;
               end;
            end;
            i = i + 1;
            UseProgress(i);
         end;
         // 2. а теперь депоз. расписки, ибо у них есть ссылки на выпуски, которые надо было загрузить сначала
         cmdAvr = null;
         cmdAvr = DL_RSDCommand("SELECT * FROM dsirnsd_avr_tmp WHERE T_State = 0 AND T_KINDDATA = ?");
         cmdAvr.addParam(AVOIRISSKIND_DEPOSITORY_RECEIPT);
         
         dataSetAvr = null;   
         dataSetAvr = cmdAvr.Execute();
         while(dataSetAvr.moveNext())
            if (dataSetAvr.State == 0) // проверим на статус, обо при обработке цб может получиться такое, что статус след. бумаги поменялся на 3, а в массиве он будет 0)
               dataAvr = CreateDataAvr(dataSetAvr, parm.isProtocolExt);
               if (dataAvr != null)
                  if (action.SearchChanges(dataAvr))
                     action.ActionOnData(dataAvr);
                  end;
               else
                  action.AddProtocolProcessedRec(dataAvr);
               end;
            end;
            i = i + 1;
            UseProgress(i);
         end;
         RemProgress();
      end;
   end;
   // 2. Организации
   if (parm.isOrg)
      var cmdOrg, dataSetOrg;
      var dataOrg:TOrganizationData;

      cmdOrg = DL_RSDCommand("SELECT * FROM dsirnsd_org_tmp WHERE T_State = 0");

      count = cmdOrg.GetCount();
      if (count > 0)
         i = 0;

         dataSetOrg = cmdOrg.Execute();
         InitProgress(count, "Обработка организаций", "Обработка организаций");
         while(dataSetOrg.moveNext())
            dataOrg = TOrganizationData(parm.isProtocolExt);

            dataOrg.fillFromTMP(dataSetOrg);
            if (action.SearchChanges(dataOrg))
               action.ActionOnData(dataOrg);
            else
               action.AddProtocolProcessedRec(dataOrg);
            end;
            i = i + 1;
            UseProgress(i);
         end;
         RemProgress();
      end;
   end;

   action.PrintReport(parm);
   exit(1);
end;

macro Печать_ОтчетСканирования (Вызов, doc, Статус, Сообщение, Действие)
   if (Вызов == 2)
      exit(1);
   end;
end;