/**                                                                                                             
 @file dlcontrsc.mac                                                                                           
 @brief Макрос скроллинга ДБО: проверки при выполнении действий, функция пользователя и т.д.
                                                                                                                
 #tag                                                                                                          
 - functional_block:API_для обработки ошибок                                                                             
 - code_type:GUI 
 - code_type:API                                                                                                                                                                                               
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.02.27 |Шишкин Е.В.    |BOSS-2350           |Создание настройки для включения/отключения отправки уведомления клиенту после закрытие ДБО1                                                                                                                                                 
 |2024.01.29 |Жиц М.В.       |DEF-60527           |BOSS-1802. Приведение к ТЗ текста и формата панели в предупреждении при закрытии договора, 
 |           |               |                    |исправление логики вывода сообщений, добавление шапки макроса и комментариев              
 |2024.01.19 |Шишкин Е.В.    |BOSS-1802           |Автоматическое уведомление клиентов о закрытии договора БО/ИИС                                                                                                                                                 
 |?          |?              |?                   |Создание макроса                                                                                                                                                 
*/ 

import BankInter;
import DealsInter;
import bnk_common;
import "CCloseAccounts.mac";
import "OpenAccountsByDlContr.mac";
import "dlcontrfunc.mac";
/* BIQ-7089 вставка события закрытия перенесена в func_lib */
//import "UpdateBrokContractRequestMsg.mac"; /* интеграция, оптравка запроса на закрытие в Бисквит */ /* kva: CloseContract */
import "func_lib.mac";
import "u_common_email_utils.mac";
import "UpdateDossierDepositoryInfoReq.mac";  // интеграция, отправка запроса на обновление досье в АСОА
import "dbo_change2edp.mac";
import "vtb_createenroll.mac";
import "dbo_message_close.mac";

/* Уже заполненные структуры, все изменения отразятся в базах */
private var Документ         = TRecHandler( "dlcontr" );
private var СтарыйДокумент   = TRecHandler( "dlcontr" ); /* заполняется при редактировании */
private file attr("objattr.dbt") key 0;


private var SfContr      = TRecHandler( "sfcontr" );
private var OldSfContr   = TRecHandler( "sfcontr" ); /* заполняется при редактировании */

PRIVATE CONST OBJTYPE_DLCONTRMSG_NPTXDUEINFO     = 505,   // Значение справочника "Вид сообщения ДБО"(1143)->"Сообщение клиенту о недоплате/переплате НДФЛ"
              OBJTYPE_DLCONTRMSG_NPTXDUEINFO_BCK = 99505; // Значение справочника "Вид сообщения ДБО"(1143)->"Сообщение клиенту о недоплате/переплате НДФЛ_отмена"
//boss-1803
private const CHOICE_BUTTON_CONTINUE = 0;    //Да
private const CHOICE_BUTTON_CANCEL = 1;     //Нет
private const BUTTON_CONTINUE = " Продолжить ";   
private const BUTTON_CANCEL = " Отмена ";    
private const TEXT_INITIATIVE_CLIENT = " Внимание! Закрытие договора происходит по инициативе клиента. Если инициатором закрытия договора является Банк, то необходимо заполнить категорию ""Расторжение договора БО по инциативе Банка""" ;
private const TEXT_INITIATIVE_BANK = " Вы уверены, что закрытие договора БО\\ИИС происходит по инициативе Банка?" ;

/**
 @brief Обработчик сообщения пользователю о закрытии договора
 @param[in] Text        текст самого сообщения 
 @param[in] buttons_yes текст кнопки подтверждения 
 @param[in] buttons_no  текст кнопки отказа 
 @return объект формы выбора
*/
private macro MsgCloseContract(Text,buttons_yes,buttons_no)
  Array Buttons;
  Array Description;
  Description(0) = Text;

  Buttons(CHOICE_BUTTON_CONTINUE)  = buttons_yes;
  Buttons(CHOICE_BUTTON_CANCEL)   = buttons_no;
  return ConfWin(Description,Buttons);
end;

/**
 @brief Макрофункция инициализации нового актива 
        необходимо заполнить структуру "Документ"
 @return 0 - ошибок не было
 @return 1 - была ошибка
*/
private macro Новый_Документ()
   Документ.rec.USESUBACC = StrFor(88);
   return 0;
end;

/**
 @brief Поиск счета с заданными параметрами
 @param[in] currency идентификатор валюты счета 
 @param[in] contrid  идентификатор субдоговора 
 @return параметры счета из таблицы dmcaccdoc_Dbt, если он найден
*/
private macro FindAcc(currency, contrid)
   var Dt = TRsbDataSet("select * from dmcaccdoc_Dbt where t_catnum = 201 and t_clientcontrid = " + contrid + " and t_currency = " + currency);
   return Dt.movenext();
end;

/**
 @brief Открытие счетов по субдоговорам ДБО, заданного в структуре "Документ"
*/
private macro OpenAccs()
   var rs_opacc = TRsbDataSet("select mp.t_sfcontrid, sf.t_servkind, sf.t_servkindsub from ddlcontrmp_dbt mp, dsfcontr_dbt sf where mp.t_sfcontrid = sf.t_id and t_dlcontrid = "  + Документ.rec.dlcontrid);
    while (rs_opacc.movenext())
       /*для люого вида обслуживания открываем рублевый счет*/
       if (not FindAcc(NATCUR, rs_opacc.sfcontrid))
          OpenAllAccounts(rs_opacc.sfcontrid, NATCUR, SfContr.rec.DateBegin);
       end;
       if (rs_opacc.servkind == 21) /*для валютного еще четыре обязатальных*/
          if (not FindAcc(7, rs_opacc.sfcontrid))
             OpenAllAccounts(rs_opacc.sfcontrid, 7,  SfContr.rec.DateBegin);
          end;
          if (not FindAcc(8, rs_opacc.sfcontrid))
             OpenAllAccounts(rs_opacc.sfcontrid, 8,  SfContr.rec.DateBegin);
          end;
          if (not FindAcc(19, rs_opacc.sfcontrid))
             OpenAllAccounts(rs_opacc.sfcontrid, 19, SfContr.rec.DateBegin);
          end;
          if (not FindAcc(20, rs_opacc.sfcontrid))
             OpenAllAccounts(rs_opacc.sfcontrid, 20, SfContr.rec.DateBegin);
          end;
       end;
    end;
end;

/**
 @brief Вставка счетов по списку валют
 @param[in] IsInsert      признак создания нового ДБО 
 @param[in] isLegalEntity признак юридического лица 
 @return 1 - была ошибка
*/
private macro InsertAccByCurList(IsInsert:bool, isLegalEntity)
  if ((StrFor(GetIdentProgram()) == "S") or (StrFor(GetIdentProgram()) == "Ю"))
    var res, err, RegistryPath ;
    GetRegistryValue( "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\РУБИЛЬНИК AVT_BOSS-131", V_BOOL, res, err );  
    if(res)
      RegistryPath = IIF(isLegalEntity, "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\СПИСОК ВАЛЮТ ДЛЯ ОТКР.СЧЕТОВ ЮЛ","РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\СПИСОК_ВАЛЮТ");
    else 
      RegistryPath = IIF(isLegalEntity, "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\СПИСОК_ВАЛЮТ_ЮР_ЛИЦ","РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\СПИСОК_ВАЛЮТ");
    end;  
    var er = 0;
    var AllCurStr = "";
    var cmd1;
    var NeedAddAcc = IsInsert; //Если вставка, то счета добавляем всегда

    if(IsInsert == false)
      //Если редактирование ДБО, то счета открываем в случае добавления площадки или валюты
      var query, cmd;

      query =   " select 1 from dual "
              + "   where Exists(select 1 "
              + "                  from ddlcontrmp_tmp "
              + "                 where t_DateClose = " + GetSQLDate(date(0,0,0))
              + "                   and t_Action in (1, 2) "
              + "                   and t_MPID = 0 "
              + "               ) "
              + "      or Exists(select 1 "
              + "                  from ddlcontracc_tmp "
              + "                 where t_AccountID = 0 "
              + "                   and t_FIID >= 0 "
              + "               )";

      cmd = DL_RSDCommand(query);

      if(cmd.GetCount > 0)
        NeedAddAcc = true;
      end;
    end;

    if(NeedAddAcc == true)
      GetRegistryValue(RegistryPath, V_STRING, AllCurStr, er);
      if(er) 
        msgbox("Ошибка при получении значения настройки:|", RegistryPath);
        return 1;
      end;

      if(AllCurStr != "")
        var select = "";
        
        select =   " SELECT 0,0,t.t_dlcontrid,fin.t_FIID, 0, 0, t.t_id, (CASE WHEN t.t_MPID > 0 THEN GREATEST(t.t_datebegin, "+GetSQLDate({curdate})+") ELSE t.t_datebegin END), 1 " 
                 + "   FROM  ddlcontrmp_tmp t, dfininstr_dbt fin, " 
                 + "         (SELECT REGEXP_SUBSTR ('"+AllCurStr+"', '[^,]+', 1,  LEVEL) CCY "
                 + "           FROM DUAL "
                 + "          CONNECT BY INSTR ('"+AllCurStr+"',  ',', 1, LEVEL - 1) > 0) q "
                 + "  WHERE t.t_ParentID <> 0 "
                 + "    AND t.t_DateClose = " + GetSQLDate(date(0,0,0))
                 + "    AND fin.t_FI_Kind = " + FIKIND_CURRENCY
                 + "    AND UPPER (fin.t_CCY) = TRIM(UPPER (q.CCY)) "
                 + "    AND NOT EXISTS (SELECT 1 FROM ddlcontracc_tmp a WHERE a.t_mpid = t.t_id AND a.t_fiid = fin.t_FIID) ";

        //Рублевые счета добавляем всем площадкам
        cmd1 = RsdCommand(" begin update ddlcontrmp_tmp set t_autoopenacc = chr(88); \n "  +
                          " Insert into DDLCONTRACC_TMP a "+select+" AND t.t_servkind in (1,15,21) AND fin.t_FIID = "+NATCUR+"; \n"
                          " end; \n" 
                         );

        cmd1.execute();

        if ((ДатаВключенияЕДП() != "") and (ДатаВключенияЕДП() <= sfcontr.rec.datebegin) and (not isLegalEntity))
          cmd1 = RsdCommand(" Insert into DDLCONTRACC_TMP a "+select+" AND t.t_subkind = 8 and t.t_servkind <> 15 AND fin.t_FIID != "+NATCUR+"; \n");
        else
          cmd1 = RsdCommand(" Insert into DDLCONTRACC_TMP a "+select+" AND t.t_servkind = 21 AND fin.t_FIID != "+NATCUR+"; \n");
        end;

        cmd1.execute();

        cmd1 = RsdCommand(  " UPDATE ddlcontrmp_tmp t "
                          + "    SET t.t_Action = 2 "
                          + "  WHERE t.t_Action = 0 "
                          + "    AND t.t_MPID > 0 "
                          + "    AND EXISTS(SELECT 1 "
                          + "                 FROM DDLCONTRACC_TMP a "
                          + "                WHERE a.t_MPID = t.t_ID "
                          + "                  AND a.t_Action > 0 "
                          + "                  AND a.t_AccountID = 0 "
                          + "              )"
                         );

        cmd1.execute();

      end;
    end;
  end;
end;

/**
 @brief Проверка выгрузки в систему
 @param[in] _partyid    идентификатор субъекта 
 @param[in] _systemname наименование системы: константы QUIK или BROKER 
 @return true - выгрузка была
 @return false - выгрузки не было или возникли ошибки в процессе определения
*/
macro CheckUnloadExtSystem(_partyid, _systemname)
   var sql_str, cmd, rs;
   var category_num = 0;
   var ObjID = String(_partyid:o:10);
   var ObjType = OBJTYPE_PARTY;
   var ObjCat;
   var res = false;
   private file attr("objattr.dbt") key 0;

   if (_systemname == "QUIK")
      category_num = 171;
   elif (_systemname == "BROKER")
      category_num = 170;
   else  // нечего передавать неизвестные системы
   end;

   ObjCat = RsbObjCategories(ObjType, ObjID);
   ClearRecord(attr);
   var cc = ObjCat.GetMainAttr(category_num, date(), attr); 
   if ( (ValType(attr.attrid) != V_UNDEF) ) // вообще неважно, какое значение, категория установлена - выгрузка была
      if (attr.attrid > 0)
         res = true;
      end;
   end;

   return res;
onError
   return false;
end;

/**
 @brief Найти тип субъекта (ЮЛ/ФЛ) по ID договора обслуживания
 @param[in] SfContrID ID договора обслуживания
 @return Тип субъекта (dParty_dbt t_legalForm)
*/
private macro GetLegalformBySfContrID(SfContrID: Integer): Integer
  var rs = TRsbDataSet(" SELECT (SELECT t_legalForm FROM dParty_Dbt WHERE t_partyID = t.t_partyID) AS legalform  FROM dSfContr_dbt t WHERE t_id = " + SfContrID);
  if (rs.MoveNext())
    return rs.legalform;
  end;
  
  return -1;
end;

private macro CheckInfBrok()
  var query, cmd, DataSet;

  if((Документ.rec.IIS == SET_CHAR) and (Документ.rec.IISTransfer == SET_CHAR))
    query =   " select 1 as cnt "
            + "   from ddlcontrinfbrok_tmp "
            + "  where t_DlContrID = ? "
            + "    and t_Type IN (1,2) "
            + "    and (t_NumAndDateRef = CHR(1) or t_ListCount = 0) " ;

    cmd = DL_RSDCommand(query);

    cmd.AddParam(Документ.rec.DlContrID);
    if(cmd.GetCount() > 0)
      msgbox("Заполните пустые поля на вкладке <Сведения о др. брокерах>");
      return 1;
    end;
  end;

  return 0;
end;

/**
 @brief Макрофункция проверки ДБО при вводе, редактировании, закрытии
 @param[in] Режим    вид выполняемого действия 
 @return 0 - ошибок не было
 @return 1 - была ошибка
 @return -1 - прерывание дальнейшей обработки без вывода сообщения о проваленной проверке из закрытого кода
*/
private macro Проверить_Документ( Режим:Integer )
    var spb_dt, spb_mp;
    var Client = TRecHandler("party.dbt");
    if( Режим == DLCONTR_MACROACTION_INSERT ) //ввод нового ДБО
        //здесь должны быть проверки при вводе нового ДБО
     /*ZY По требованию Федотова отправка уведомления производится только после удачной выгрузки, поэтому эта часть переносится в интеграционную часть в макрос ws_synch_SOFR.mac*/
     /*  var rs_c1 = TRsbDataSet("  select t_sfcontrid, (select t_shortname from dparty_dbt where t_partyid = "+SfContr.rec.partyid+") name from ddlcontrmp_tmp where t_servkind = 1 " );
       if (rs_c1.movenext() )    // фондовый рынок
          var v_email = c_email_proc_env();

          var emailText = "Информируем  о заключении брокерского соглашения ("+
                                        SfContr.rec.Number+" от "+              
                                        SfContr.rec.DateBegin+")_"+                  
                                        rs_c1.name;
          v_email.m_set_msg_head(emailText);
          v_email.m_add_row_to_msg_text(emailText);
          v_email.m_save_to_submit_grp(CONTRACT_EMAIL_GRP, true);
          v_email.m_submit_email_synch();
       end;*/
      
       /*  Вставляет записи в темповую табличку по всем видам рынков по рублям и по валютному по пяти валютам. В момент сохранения их подхватит дистрибутив и откроет счета с помощью */
       /*    dlcontrfunc.mac/macro ОткрытьБрокерскийСчет*/
       InsertAccByCurList(true, СубъектЮЛ_ИП(SfContr.rec.partyid));


 //      OpenAccs();
       if (not УчетДоговоровНаСПБ({curdate}) and (SPB_ID() > 0))
          spb_mp = Trsbdataset("select * from ddlcontrmp_tmp where t_marketid = " + SPB_ID());
          if (spb_mp.movenext())
             spb_dt = ДатаВключенияСПБ();
             if (spb_dt == "")
                MsgBox("Учет договоров на бирже " + ПолучитьКодСубъекта(SPB_ID(), 1) + " выключен");
             else
                MsgBox("Учет договоров на бирже " + ПолучитьКодСубъекта(SPB_ID(), 1) + " действует с " + string(spb_dt):f);
             end;
             return 1;
          end;
       end;


       //Для ЮЛ проверка на наличие заполненной категории 'Сегмент корпоративного бизнеса'
       ПолучитьСубъекта(SfContr.rec.partyid, Client);
       If(Client.rec.legalform == 1)
          If(not CheckClientCateg(Client))
             MsgBox("Для клиента " + Client.rec.name + " не заполнена категория 'Сегмент корпоративного бизнеса'. Договор брокерского обслуживания не может быть сохранен.");
             return 1;
          end;
       end;

       if(CheckInfBrok() != 0)
         return 1;
       end;

    elif( Режим == DLCONTR_MACROACTION_EDIT ) //редактирование ДБО
        //здесь должны быть проверки при редактировании ДБО
       if (SfContr.rec.datebegin != OldSfContr.rec.datebegin)
          var cmd = RsdCommand(" update ddlobjcode_dbt set t_bankdate = to_date('01010001','ddmmyyyy') where t_objecttype = 207 and t_codekind = 1 and t_objectid = " + Документ.rec.dlcontrid);
          cmd.execute;
       end;

       /*  Вставляет записи в темповую табличку по всем видам рынков по рублям и по валютному по пяти валютам. В момент сохранения их подхватит дистрибутив и откроет счета с помощью */
       /*    dlcontrfunc.mac/macro ОткрытьБрокерскийСчет*/
       InsertAccByCurList(false, СубъектЮЛ_ИП(SfContr.rec.partyid));

      // if (GetTrue(false, "Открыть счета по субдоговорам?"))
      //    OpenAccs();
      // end;

       if (not УчетДоговоровНаСПБ({curdate}) and (SPB_ID() > 0))
          spb_mp = Trsbdataset("select * from ddlcontrmp_tmp where t_marketid = " + SPB_ID());
          if (spb_mp.movenext())
             spb_dt = ДатаВключенияСПБ();
             if (spb_dt == "")
                MsgBox("Учет договоров на бирже " + ПолучитьКодСубъекта(SPB_ID(), 1) + " выключен");
             else
                MsgBox("Учет договоров на бирже " + ПолучитьКодСубъекта(SPB_ID(), 1) + " действует с " + string(spb_dt):f);
             end;
          end;
       end;

       /*bpv для макроса закрытия счетов */
       /* dl_CloseAccInDlContr.mac вызывается как из списка счетов по Ctrl+A, так и при закрытии площадки */
       /* но понять откуда прошел вызов нельзя, поэтому используем глобальный параметр*/

       if(CheckInfBrok() != 0)
         return 1;
       end;

    elif( Режим == DLCONTR_MACROACTION_CLOSE ) //закрытие ДБО
         //здесь должны быть проверки при закрытии ДБО

       var err, log, item, StrLog = "",ObjCat,cc;
       var ObjType = 207, ObjID = String(Документ.rec.dlcontrid:o:34), category_num = 198;
       var ca = CCloseContrAccounts(Документ.rec.dlcontrid);
       var notification:integer = 0;

       //DEF-48068 Передача данных по закрытию последнего договора в РСХБ Брокер
       // задания 5025 могут успеть обработаться до того, как закрытие завершится, поэтому вынесены в Режим = AFTER_CLOSE

       /*bpv для макроса закрытия счетов */
       /* dl_CloseAccInDlContr.mac вызывается как из списка счетов по Ctrl+A, так и при закрытии площадки */
       /* но понять откуда прошел вызов нельзя, поэтому используем глобальный параметр*/
       SetGlobalParameter("DLCONTR_MACROACTION_CLOSE", "X");
                                     
       ObjCat = RsbObjCategories(ObjType, ObjID);
       ClearRecord(attr);
       cc = ObjCat.GetMainAttr(category_num, date(), attr); //Получаем значение категории "Расторжение договора БО по инициативе Банка"
       if((ValType(attr.attrid) != V_UNDEF) and (attr.attrid == 1)) //категория заполнена значением "Да"
          if(not GetTrue(true, TEXT_INITIATIVE_BANK))
             return -1;
          end;
       else
          if(MsgCloseContract(TEXT_INITIATIVE_CLIENT,BUTTON_CONTINUE,BUTTON_CANCEL) == CHOICE_BUTTON_CANCEL)
             return -1;
          end;
       end;
       
       if ( ca.CloseAcc(@err, @log) > 0 )
          for ( item, err )
             if ( item.rest != 0 )
                StrLog = StrLog + item.err + " " + item.acc + "   " + item.rest + "\n";
             else
                StrLog = StrLog + item.acc + "   " + item.err + "\n";
             end;
          end;

          MsgBox("Невозможно закрыть счета по договору \n\n" + StrLog);
          SetGlobalParameter("DLCONTR_MACROACTION_CLOSE", null);
          return 1;
       elif (log.size > 0)
          for ( item, log )
             StrLog = StrLog + item.acc + "   " + item.err + "\n";
          end;
          MsgBox(StrLog);
       end;

       // задания 5100 так же могут обрабататься до того, как договор закрыт, выявлено в DEF-48068

       // отправка письма в Депозитарий
       // проверим есть ли в закрываемом договоре фондовый дилинг
       GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\УВЕДОМЛЕНИЯ КЛИЕНТУ\\ДОБ.ОД В СК.КОПИЮ ПРИ ЗАКР.ДБО", V_INTEGER, notification);
       if(notification == 0)
          var rs_c = TRsbDataSet(" select (select t_shortname from dparty_dbt where t_partyid = t.t_partyid) name from dsfcontr_dbt t " +
                                 " where t_id in ( select t_sfcontrid from ddlcontrmp_dbt where t_dlcontrid = " +Документ.rec.dlcontrid+ ") " + 
                                 " and t_servkind = 1 " );
          if (rs_c.movenext() )    // фондовый рынок
             BegAction();
             var rs_sf = TRsbDataSet( " select t_number, t_datebegin from dsfcontr_dbt where t_id = " +  Документ.rec.sfcontrid);
             if (rs_sf.movenext())
                var partyname_short = rs_c.name;
                var v_email_processor = c_email_proc_env();
                var v_email_head =  "Информируем о закрытии брокерского соглашения ("+
                                 rs_sf.Number +" от "+              
                                 SQL_ConvTypeDate(rs_sf.DateBegin)+")_"+                  
                                 partyname_short;                                  
                var v_email_body = v_email_head;
                v_email_processor.m_set_msg_head(v_email_head);
                v_email_processor.m_add_row_to_msg_text(v_email_body);
                v_email_processor.m_save_to_submit_grp(CONTRACT_EMAIL_GRP, true);
                v_email_processor.m_submit_email_synch(); 
             else
                msgbox("Ошибка: не удалось отправить уведомление Депозитарию о закрытии договора");
             end;
             EndAction;
          end;
       end;
       //return 1;

    elif( Режим == DLCONTR_MACROACTION_AFTER_CLOSE ) //после закрытия ДБО
        //здесь должны быть проверки после закрытия ДБО

       //DEF-48068 Передача данных по закрытию последнего договора в РСХБ Брокер
       // задания 5025 (и 5100 - закрытие в АБС) могут успеть обработаться до того, как закрытие завершится, поэтому вынесены в Режим = AFTER_CLOSE
                                          
       // DEF-30604 выгружаем закрытие всего договора, вынесено в отдельную функцию
       // функция определяет выгрузку не по коду, а по категории, как и должно быть
       // при закрытии структура SfContr.rec не заполнена
       var ismessage = 0,er;
       var RegPath = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\УВЕДОМЛЕНИЯ КЛИЕНТУ\\ОТПР. УВЕД. О ЗАКРЫТИЕ ДБО";

       var SqlParty = ExecSqlSelect("SELECT t_partyid FROM dsfcontr_dbt  WHERE t_id = :sfid", makeArray(SqlParam("sfid", Документ.rec.sfcontrid)));
       sqlParty.movenext();
       var party_close = sqlParty.value(0);

       if (CheckUnloadExtSystem(party_close, "QUIK"))
          execMacrofile("global_utils_intgr.mac","m_make_event_to_process", 5025, party_close, 1);
       end;

       if (CheckUnloadExtSystem(party_close, "BROKER"))
          execMacrofile("global_utils_intgr.mac","m_make_event_to_process", 5026, party_close, 3);
       end;

       InsEvent_UpdateContract(Документ.rec.dlcontrid, 3); /*BIQ-7089 Сервис не только для закрытия, доп. параметр*/

       GetRegistryValue(RegPath, V_INTEGER, ismessage, er);

       if(ismessage == 1)
         var mess = dboMessageClose(Документ.rec.dlcontrid).CreateMessage(false,"","");
       end;

       /*bpv сбросим глобальный параметр, который ставили при старте закрытия ДБО*/
       SetGlobalParameter("DLCONTR_MACROACTION_CLOSE", null);
    end;

    if (Режим == DLCONTR_MACROACTION_EDIT)
       if (not CheckDlContrAccCollision(Документ.rec.dlcontrid, ДоговорЕДП(Документ.rec.dlcontrid), NULL, SfContr.rec.servkindsub))
          return 1;
       end;
    elif ( Режим == DLCONTR_MACROACTION_INSERT )
       if (not CheckDlContrAccCollision(NULL, РежимРаботыЕДП({curdate}), NULL, SfContr.rec.servkindsub))
          return 1;
       end;
    end;

    if(Режим == DLCONTR_MACROACTION_CHECK_WITHOUT_CHANGES)
      if(CheckInfBrok() != 0)
         return 1;
      end;
    end;

    return 0;
end;

  Var dlcid;
/**
 @brief Функция пользователя. Вызывается по CTRL+Z из скроллинга ДБО 
 @return 0 - ошибок не было
 @return 1 - была ошибка
*/
private macro Функция_Пользователя()
   private var v_result = true;
   var err_unload; // kva: есть подозрение, что лучше перенести сюда (было в одном из elif) 
   private var v_choice;
   private var v_acc_trn_menu = TArray;
   dlcid =  Документ.rec.dlcontrid;
   
   /// константы меню.  gtv: при необходимости добавить/поменять порядок, изменение только в одном месте
   private const 
      C_UPLOAD_DBO             = 0,  ///< Выгрузка ДБО в Диасофт
      C_SEND_MESSAGE_CLIENT    = 1,  ///< Выполнить отправку сообщения клиенту
      C_RECREATE_MESSAGE_IIA   = 2,  ///< Пересоздать сообщение об открытии ИИС
      C_RECREATE_MESSAGE_MOEX  = 3,  ///< Пересоздать сообщение о регистрации договора на МБ
      C_RECREATE_MESSAGE_MP_MOEX=4,  ///< Сформировать сообщение о площадках договора на МБ
      C_CREATE_MESSAGE_SPB     = 5,  ///< Создать сообщение о бронировании договора на СПБ
      C_CONTRACTNUMBER_BY_CODE = 6,  ///< Номер договора по коду на бирже
      C_ACCOUNT_LIST           = 7,  ///< Список счетов
      C_UPLOAD_CLOSE           = 8,  ///< Выгрузить закрытие
      C_UPLOAD_MESSAGE_ASOA    = 9,  ///< Выгрузить уведомление в АСОА
      C_MOVE_TO_EDP            = 10, ///< Перевести договор в режим ЕДП
      C_RECREATE_FATCA         = 11, ///< Повторно сформировать сообщение FATCA
      C_FORM_ENROLLMENT_VTB    = 12, ///< Сформировать зачисления ВТБ
      C_UPDATE_MSGKIND_SENDDUEINFO = 13,       ///< Перевод сообщения о недоплате/переплате НДФЛ в отменённые
      C_SEND_MESSAGE_CLIENT_QI_REJECTION = 14, ///< Сформировать уведомление об отказе в признании лица КИ
      C_SEND_NOTIFICATION_BASE = 15, ///< Отправка " Ежегодное уведомление"Базовый стандарт"
      // Должны быть последними :
      C_SEND_UL_NOTIFICATION_DBOSB = 16,       ///< Отправить в ДБО СБ уведомление о приеме ЮЛ на БО
      C_DELETE_CONTRACT        = 17  ///< Удалить договор
      ;

   v_acc_trn_menu.value(C_UPLOAD_DBO) = "Выгрузка ДБО в Диасофт";
   v_acc_trn_menu.value(C_SEND_MESSAGE_CLIENT) = "Выполнить отправку сообщения клиенту";
   v_acc_trn_menu.value(C_RECREATE_MESSAGE_IIA) = "Пересоздать сообщение об открытии ИИС";
   v_acc_trn_menu.value(C_RECREATE_MESSAGE_MOEX) = "Пересоздать сообщение о регистрации договора на МБ"; // def-32182
   v_acc_trn_menu.value(C_RECREATE_MESSAGE_MP_MOEX) = "Сформировать сообщение о площадках договора на МБ"; // DEF-36640 
   v_acc_trn_menu.value(C_CREATE_MESSAGE_SPB) = "Создать сообщение о бронировании договора на СПБ"; // def-32182
   v_acc_trn_menu.value(C_CONTRACTNUMBER_BY_CODE) = "Номер договора по коду на бирже";
   v_acc_trn_menu.value(C_ACCOUNT_LIST) = "Список счетов";
   v_acc_trn_menu.value(C_UPLOAD_CLOSE) = "Выгрузить закрытие";
   v_acc_trn_menu.value(C_UPLOAD_MESSAGE_ASOA) = "Выгрузить уведомление в АСОА";
   v_acc_trn_menu.value(C_MOVE_TO_EDP) = "Перевести договор в режим ЕДП";
   v_acc_trn_menu.value(C_RECREATE_FATCA) = "Повторно сформировать сообщение FATCA";
   v_acc_trn_menu.value(C_UPDATE_MSGKIND_SENDDUEINFO) = "Перевод сообщения о недоплате/переплате НДФЛ в отменённые"; // BIQ-13598
   v_acc_trn_menu.value(C_FORM_ENROLLMENT_VTB) = "Сформировать зачисления ВТБ";
   v_acc_trn_menu.value(C_SEND_MESSAGE_CLIENT_QI_REJECTION) = "Сформировать уведомление об отказе в признании лица КИ"; // BOSS-194
   v_acc_trn_menu.value(C_SEND_NOTIFICATION_BASE) = "Отправка Ежегодное уведомление 'Базовый стандарт'"; // BOSS-3775

   /**
   sfcontr пустой ( 
   kva: НЕ ЗАКРЫВАТЬ DATASET - ИСПОЛЬЗУЕТСЯ ДАЛЕЕ!!! 
   */
   var Dt1 = TRsbDataSet(" select t_dateclose from dsfcontr_dbt where t_id = " + Документ.rec.sfcontrid);
   if (Dt1.movenext)
      if (SQL_ConvTypeDate(Dt1.dateclose) != date(0,0,0))
         v_acc_trn_menu.value(C_DELETE_CONTRACT) = "Удалить договор";
      elif (GetLegalformBySfContrID(Документ.rec.sfContrID) == PTLEGF_INST)
         v_acc_trn_menu.value(C_SEND_UL_NOTIFICATION_DBOSB) = "Отправить в ДБО СБ уведомление о приеме ЮЛ на БО"; // BOSS-5842
      end;
   end;
   var res;
   var marketid_moex;

   //При передаче имени окна, теряется последний пункт
   v_choice = menu(v_acc_trn_menu, "Выберите действие"); //, "Выберите действие");
   if(v_choice == C_UPLOAD_DBO)
//biq-7840
      var path = true; // по умолчанию через kafka
      GetRegistryValue("РСХБ\\ИНТЕГРАЦИЯ\\ДЕПОЗИТАРИЙ_KAFKA", V_BOOL, path);
      if(path == true)
         var sql_it, cmd_it;
         
         sql_it = "BEGIN                       "+
               "   IT_DIASOFT.SendBrokerContractDepo("+dlcid+"); "+
               "END;  ";
          
         cmd_it = RSDCommand(sql_it);
         cmd_it.execute();
         
         cmd_it.close(); cmd_it = NULL; sql_it = NULL;
      else
     // BIQ-7382 вынесено в отдельный макрос, чтобы была свобода действий
     ExecMacroFile("ContrUnloadDepo.mac", "ContrUnloadDepo", dlcid);
      end;
   elif (v_choice == C_SEND_MESSAGE_CLIENT)
      ExecMacroFile("dbo.mac");
   elif (v_choice == C_RECREATE_MESSAGE_IIA )
      ExecMacroFile("recreatDlcontrmsg.mac","recreateIISOpenMessage", Документ); 
   elif (v_choice == C_RECREATE_MESSAGE_MOEX ) // def-32182
      marketid_moex = ПолучитьБиржуПоВидуКодаДБО(DLCCK_USC); 
      ExecMacroFile("recreatDlcontrmsg.mac","recreatDlcontrMessage", Документ, OBJTYPE_DLCONTRMSG_MARKETREG, marketid_moex); 
   elif (v_choice == C_RECREATE_MESSAGE_MP_MOEX ) // def-36640
      marketid_moex = ПолучитьБиржуПоВидуКодаДБО(DLCCK_USC); 
      ExecMacroFile("recreatDlcontrmsg.mac","recreatDlcontrMessage", Документ, OBJTYPE_DLCONTRMSG_MPREG, marketid_moex); 
   elif (v_choice == C_CREATE_MESSAGE_SPB ) // def-32182
      var marketid_spb = ПолучитьБиржуПоВидуКодаДБО(DLCCK_SPB_BIDCODE); 
      ExecMacroFile("recreatDlcontrmsg.mac","recreatDlcontrMessage", Документ, OBJTYPE_DLCONTRMSG_BIDCODE, marketid_spb); 
   elif (v_choice == C_CONTRACTNUMBER_BY_CODE )
      var shortcode = "";
      if (GetString(shortcode, "Краткий код",64))
         var dt = TRsbDataSet(" select t_number from dsfcontr_dbt where t_id in (select t_sfcontrid from ddlcontr_dbt where t_dlcontrid in (select t_dlcontrid from ddlcontrmp_dbt where t_mpcode = '"+trim(shortcode)+"'))");
         if (dt.movenext())
            shortcode = dt.number;
            GetString(shortcode, "Номер договора",64);
         else
            MsgBox("Не найден");
         end;
      end;
   elif (v_choice == C_ACCOUNT_LIST )
      OpenAccountsByDlContr(Документ.rec.dlcontrid);
   elif (v_choice == C_UPLOAD_CLOSE )
      /// kva: ИСПОЛЬЗУЕМ DATASET, ОПРЕДЕЛЕННЫЙ ВЫШЕ
      if (SQL_ConvTypeDate(Dt1.dateclose) != date(0,0,0))
         v_choice = C_UPLOAD_CLOSE;
      else
         if(gettrue(true, "Договор не закрыт.\nВсе равно сформировать событие на выгрузку?"))
            v_choice = C_UPLOAD_CLOSE;
         else
            v_choice = -1;
         end;
      end;
      if(v_choice == C_UPLOAD_CLOSE)
         InsEvent_UpdateContract(Документ.rec.dlcontrid, 3); // BIQ-7089 Сервис не только для закрытия, доп. параметр
         execMacrofile("ws_check_job.mac","ws_Init_job_EventCloseContract_single", Документ.rec.dlcontrid); // kva: CloseContract
      end;
   elif (v_choice == C_UPLOAD_MESSAGE_ASOA )
      res = execMacrofile("UploadDocASOA.mac","PushUploadDoc", true, Документ.rec.dlcontrid);
   elif (v_choice == C_MOVE_TO_EDP )
      if (not РежимРаботыЕДП({curdate}))
         MsgBox("Учёт Единой денежной позиции выключен");
      else
         var err_edp="";
         Dt1 = TRsbDataSet(" select (select t_legalform from dparty_Dbt where t_partyid = t.t_partyid) legalform  from dsfcontr_dbt t where t_id = " + Документ.rec.sfcontrid);
         if (Dt1.movenext)
            if ((Dt1.LegalForm == 2) or (ВедениеЕДП_ЮЛ())) 
               if (ДоговорЕДП(Документ.rec.dlcontrid))
                  MsgBox("На договоре уже установлен признак ЕДП");
               else
                  BegAction(null, "Выполняется перевод");
                  if ( not Change2edpTrn(Документ.rec.dlcontrid, @err_edp))
                     EndAction();
                     msgbox("Перевод выполнен");
                  else
                     EndAction();
                     msgbox("Ошибка перевода на ЕДП|"+err_edp);
                  end;
               end;
            else
               MsgBox("Учёт Единой денежной позиции по юр.лицам выключен");
            end;
         else
            MsgBox("Не удалось получить плательщика по договору");
         end;
      end;
   elif (v_choice == C_RECREATE_FATCA )
      var msgdt = TRsbDataSet("select t.t_id from ddlcontrmsg_dbt t where t.t_kind in (10) and t.t_sendmesstate = 305 and t.t_dlcontrid = " + Документ.rec.dlcontrid + " and not exists (select 1 from ddlcontrmsg_dbt t1 where t1.t_dlcontrid = t.t_dlcontrid and t1.t_kind in (10) and t1.t_sendmesstate in (300,302) ) ");  
      if(msgdt.movenext)
         ExecMacroFile("dlcontrmsg.mac","DlRepeatSendMes", msgdt.ID);
         msgbox("Выполнено. \n Внимание! Необходимо повторно сформировать сообщение Fatca(add) для всех ДБО клиента (БО и ИИС) если их больше одного.");
      else
         msgbox("Повторное формирование Fatca(add) не возможно.\n Повторное формирование не возможно при наличии для ДБО  неподтвержденных сообщений Fatca(add) или если такие сообщения еще не формировались.");
      end;
   elif (v_choice == C_FORM_ENROLLMENT_VTB )
      var log_choice10 = "";
      VTB_CreateBoardByDlcontr(Документ.rec.dlcontrid, @log_choice10);
      msgbox(log_choice10);
   elif (v_choice == C_UPDATE_MSGKIND_SENDDUEINFO)
      var cmd, rs;
      cmd = RsdCommand("SELECT t_Id FROM ddlcontrmsg_dbt WHERE t_DlContrId = ? AND t_Kind = ?");
      cmd.AddParam("", RSDBP_IN, Документ.rec.DlContrId);
      cmd.AddParam("", RSDBP_IN, OBJTYPE_DLCONTRMSG_NPTXDUEINFO);
      rs = RsdRecordset(cmd);

      if (rs.MoveNext())
         execSQL("UPDATE ddlcontrmsg_dbt SET t_Kind = " + OBJTYPE_DLCONTRMSG_NPTXDUEINFO_BCK + " WHERE t_Id = " + rs.value("t_Id"));
         msgbox("Сообщение вида 505 успешно переведено в отменённые");
      else
         cmd = RsdCommand("SELECT t_Number FROM dsfcontr_dbt WHERE t_Id = ?");
         cmd.AddParam("", RSDBP_IN, Документ.rec.SfContrId);

         rs = RsdRecordset(cmd);
         rs.MoveNext();
         msgbox("Для договора " + rs.Value("t_Number") + " отсутствует сообщение вида 505");
      end;
   elif (v_choice == C_SEND_MESSAGE_CLIENT_QI_REJECTION)
      ExecMacroFile("dbo_qi_Rejection.mac","QIRejectionNotificationUI", Документ.rec.DlContrId);
   elif (v_choice == C_DELETE_CONTRACT ) ///< bpv Удалить договор. совсем удалить. ВООБЩЕ СОВСЕМ. Логи бросаются в таблицы с префиксом DELDBO_% 
      var del_err;
      if (GetTrue(false, "Вы точно хотите окончательно удалить договор?"))
         if (ExecMacroFile("deletecontr","deleteDBO", Документ.rec.dlcontrid, @del_err))
            msgbox("Договор успешно удален.");
         else
            msgbox("Не удалось удалить договор: " + del_err);
         end;
      end;
   elif (v_choice == C_SEND_NOTIFICATION_BASE )
      var send_status;
      if (ExecMacroFile("notifySendMessageRisks","SendMessage", Документ.rec.dlcontrid, @send_status))
        msgbox("Уведомление успешно отправлено.");
      else
        msgbox("Уведомление не отправлено: " + send_status);
      end;
   elif (v_choice == C_SEND_UL_NOTIFICATION_DBOSB)
      ExecMacroFile("SendBrokerageAgreementTermsReq.mac","ReSendNotification", Документ.rec.DlContrId);
   else
      msgbox("Выбор не подтвержден");
   end;
  
   return 0;
end;

/**
 @brief Функция пользователя. Вызывается по CTRL+Z из списка видов обслуживания ДБО
 @param[in] dlcontrmptmp    рекорд записи ddlcontrmp_tmp  
 @return 0 - ошибок не было
 @return 1 - была ошибка
*/
private macro Функция_Пользователя_Субдоговор(dlcontrmptmp)
    MsgBox( "Выполняется макрофункция \"dlcontrsc.mac\\ Функция_Пользователя_Субдоговор\"." );
    return 0;
end;


private class DL_CDlContrObservedRep(_ScrollSQLQuery:string, ID_Operation:integer, ID_Step:integer, ExecDate:date, IsRollBack:bool)
  private var m_ScrollSQLQuery = _ScrollSQLQuery;
  private var m_ID_Operation   = IIF(ID_Operation== NULL, 0,         ID_Operation);
  private var m_ID_Step        = IIF(ID_Step     == NULL, 0,         ID_Step);
  private var m_ExecDate       = IIF(ExecDate    == NULL, {curdate}, ExecDate);
  private var m_IsRollBack     = IIF(IsRollBack  == NULL, false,     IsRollBack);

  private macro CollectData()
    var query, cmd;

    SQL_Execute("DELETE FROM DDLCONTROBS_TMP");

    if(m_IsRollBack == false)
      //отказ от функции, в пользу одного большого запроса
      query =
       " INSERT INTO DDLCONTROBS_TMP (T_DLCONTRID, T_DATE, T_TAXBASE, T_TAXBASEKIND, T_DELIVERED) "
      +" SELECT Q1.T_DLCONTRID, ?, Q1.TAXBASE, Q1.TAXBASEKIND, CHR(0) "
      +"   FROM (  SELECT /*+ ordered cardinality(dlc 100) */ dlc.T_DLCONTRID, "
      +"                  NVL (SUM (nobj.t_Sum0), 0) AS TAXBASE, "
      +"                  TBK.TAXBASEKIND "
      +"             FROM ddlcontr_dbt dlc, "
      +"                  dsfcontr_dbt sf, "
      +"                  (SELECT 1 AS TAXBASEKIND FROM DUAL "
      +"                   UNION "
      +"                   SELECT 2 AS TAXBASEKIND FROM DUAL "
      +"                   UNION "
      +"                   SELECT 5 AS TAXBASEKIND FROM DUAL) TBK, "
      +"                  dnptxobj_dbt nobj, "
      +"                  dnptxkind_dbt txk "
      +"            WHERE sf.t_ID = dlc.t_SfContrID " ;
      if (not (m_ID_Operation > 0))
      //+"                  AND ( (? > 0) "
      //+"                       OR (RSB_SECUR. "
      //+"                            NeedDlContrMark ( "
      //+"                              dlc.T_DLCONTRID, ?) <> 0)) "
         query = query +" AND dlc.T_DLCONTRID in (select T_DLCONTRID from table(Rsb_Secur.SelectNeedDlContrMark(?)))"
      end ;
      query = query +"    AND nobj.t_Client(+) = sf.t_PartyID "
      +"                  AND nobj.t_Kind(+) IN "
      +"                      ("+TXOBJ_BASEG1+", "
      +"                       "+TXOBJ_BASEG2+", "
      +"                       "+TXOBJ_BASEG3+", "
      +"                       "+TXOBJ_BASEG4+", "
      +"                       "+TXOBJ_BASEG5+", "
      +"                       "+TXOBJ_BASEG6+", "
      +"                       "+TXOBJ_BASEG7+", "
      +"                       "+TXOBJ_BASEG8+", "
      +"                       "+TXOBJ_BASEG9+") "
      +"                  AND nobj.t_Level(+) = 7 "
      +"                  AND nobj.t_Date(+) >= TRUNC (?, 'year') "
      +"                  AND nobj.t_Date(+) <= ? "
      +"                  "+ m_ScrollSQLQuery
      +"                  AND 1 = "
      +"                         (CASE "
      +"                             WHEN dlc.t_IIS = CHR (88) AND nobj.t_Kind(+) = "+TXOBJ_BASEG5+" "
      +"                             THEN "
      +"                                1 "
      +"                             WHEN dlc.t_IIS <> CHR (88) "
      +"                                  AND nobj.t_Kind(+) IN "
      +"                                         ("+TXOBJ_BASEG1+", "
      +"                                          "+TXOBJ_BASEG2+", "
      +"                                          "+TXOBJ_BASEG3+", "
      +"                                          "+TXOBJ_BASEG4+", "
      +"                                          "+TXOBJ_BASEG6+", "
      +"                                          "+TXOBJ_BASEG7+", "
      +"                                          "+TXOBJ_BASEG8+", "
      +"                                          "+TXOBJ_BASEG9+") "
      +"                             THEN "
      +"                                1 "
      +"                             ELSE "
      +"                                0 "
      +"                          END) "
      +"                  AND txk.t_Element(+) = nobj.t_Kind "
      +"                  AND ( (nobj.t_Sum0 IS NULL) "
      +"                       OR (TBK.TAXBASEKIND = txk.t_TaxBaseKind)) "
      +"                  AND txk.t_TaxBaseKind(+) = TBK.TAXBASEKIND "
      +"                  AND ( (DLC.T_IIS = CHR (88) AND TAXBASEKIND = 5) "
      +"                       OR (DLC.T_IIS <> CHR (88) AND TAXBASEKIND <> 5)) "
      +"         GROUP BY T_DLCONTRID, TAXBASEKIND) q1 "
              //Если НОБ не 0 и она изменилась (отработало условие выше), то пропускаем
              //Если НОБ == 0, и ранее выгрузок не было, то не пропускаем
              //Если НОБ == 0, а в предыдущей выгрузке другое значение (отработало условие выше), то пропускаем.
      +"  WHERE ( (q1.TAXBASE <> 0) "
      +"         OR (Q1.TAXBASE = 0 "
      +"             AND EXISTS "
      +"                    (SELECT 1 "
      +"                       FROM DDLCONTROBS_DBT OBS "
      +"                      WHERE OBS.T_DLCONTRID = Q1.T_DLCONTRID "
      +"                            AND OBS.T_TAXBASEKIND = Q1.TAXBASEKIND "
      +"                            AND OBS.T_DATE <= ?))) "
      //Только, если НОБ изменилась по сравнению с предыдущей выгрузкой (если она вообще была)
      +"        AND NOT EXISTS "
      +"                   (SELECT 1 "
      +"                      FROM DDLCONTROBS_DBT OBS "
      +"                     WHERE OBS.T_DLCONTRID = Q1.T_DLCONTRID "
      +"                           AND OBS.T_TAXBASEKIND = Q1.TAXBASEKIND "
      +"                           AND OBS.T_DATE = "
      +"                                  (SELECT NVL ( "
      +"                                             MAX (OBS1.T_DATE), "
      +"                                             TO_DATE ('01-01-0001', "
      +"                                                      'DD-MM-YYYY')) "
      +"                                     FROM DDLCONTROBS_DBT OBS1 "
      +"                                    WHERE OBS1.T_DLCONTRID = OBS.T_DLCONTRID "
      +"                                          AND OBS1.T_TAXBASEKIND = "
      +"                                                 OBS.T_TAXBASEKIND "
      +"                                          AND OBS1.T_DATE <= ?) "
      +"                           AND OBS.T_TAXBASE = Q1.TAXBASE) ";

      cmd = RSDCommand(query);

      cmd.addParam( "", RSDBP_IN, m_ExecDate );
      if ( not ( m_ID_Operation > 0))

      //cmd.addParam( "", RSDBP_IN, m_ID_Operation );
        cmd.addParam( "", RSDBP_IN, m_ExecDate );
      end ;
      cmd.addParam( "", RSDBP_IN, m_ExecDate );
      cmd.addParam( "", RSDBP_IN, m_ExecDate );
      cmd.addParam( "", RSDBP_IN, m_ExecDate );
      cmd.addParam( "", RSDBP_IN, m_ExecDate );

      SQL_Execute( cmd );
    end;
  end;

  private macro WriteDataToDB()
    var query, cmd;
    var OutTrans = RslDefCon.IsInTrans;
    
    if(OutTrans == false)
      RslDefCon.BeginTrans();
    end;

    if((m_IsRollBack == false) and (m_ID_Operation != 0) and (m_ID_Step != 0))
      query =  " INSERT INTO DNPTXOBS_DBT (T_ID_OPERATION, T_ID_STEP, T_DLCONTRID, T_DATE, T_TAXBASE, T_TAXBASEKIND, T_PREVTAXBASE, T_INSERT)"
             + " SELECT ?, ?, TMP.T_DLCONTRID, TMP.T_DATE, TMP.T_TAXBASE, TMP.T_TAXBASEKIND, 0, 'X' "
             + "   FROM DDLCONTROBS_TMP TMP "
             + "  WHERE NOT EXISTS(SELECT 1 "
             + "                     FROM DDLCONTROBS_DBT D "
             + "                    WHERE D.T_DLCONTRID = TMP.T_DLCONTRID "
             + "                      AND D.T_TAXBASEKIND = TMP.T_TAXBASEKIND "
             + "                      AND D.T_DATE = TMP.T_DATE)";


      cmd = RSDCommand(query);

      cmd.addParam( "", RSDBP_IN, m_ID_Operation );
      cmd.addParam( "", RSDBP_IN, m_ID_Step );

      SQL_Execute( cmd );

      query =  " INSERT INTO DNPTXOBS_DBT (T_ID_OPERATION, T_ID_STEP, T_DLCONTRID, T_DATE, T_TAXBASE, T_TAXBASEKIND, T_PREVTAXBASE, T_INSERT)"
             + " SELECT ?, ?, TMP.T_DLCONTRID, TMP.T_DATE, TMP.T_TAXBASE, TMP.T_TAXBASEKIND, "
             + "        (SELECT D.T_TAXBASE " 
             + "           FROM DDLCONTROBS_DBT D "
             + "          WHERE D.T_DLCONTRID = TMP.T_DLCONTRID "
             + "            AND D.T_TAXBASEKIND = TMP.T_TAXBASEKIND "
             + "            AND D.T_DATE = TMP.T_DATE"
             + "        ), "
             + "        CHR(0) "
             + "   FROM DDLCONTROBS_TMP TMP "
             + "  WHERE EXISTS(SELECT 1 "
             + "                 FROM DDLCONTROBS_DBT D "
             + "                WHERE D.T_DLCONTRID = TMP.T_DLCONTRID "
             + "                  AND D.T_TAXBASEKIND = TMP.T_TAXBASEKIND "
             + "                  AND D.T_DATE = TMP.T_DATE)";


      cmd = RSDCommand(query);

      cmd.addParam( "", RSDBP_IN, m_ID_Operation );
      cmd.addParam( "", RSDBP_IN, m_ID_Step );

      SQL_Execute( cmd );

    elif((m_IsRollBack == true) and (m_ID_Operation != 0) and (m_ID_Step != 0))

      query =   "DELETE FROM DDLCONTROBS_DBT OBS "
              + " WHERE (OBS.T_DLCONTRID, OBS.T_TAXBASEKIND, OBS.T_DATE) IN (SELECT N.T_DLCONTRID, N.T_TAXBASEKIND, N.T_DATE "
              + "                                                              FROM DNPTXOBS_DBT N "
              + "                                                             WHERE N.T_ID_OPERATION = ? "
              + "                                                               AND N.T_ID_STEP      = ? "
              + "                                                               AND N.T_INSERT       = 'X') ";

      cmd = RSDCommand(query);

      cmd.addParam( "", RSDBP_IN, m_ID_Operation );
      cmd.addParam( "", RSDBP_IN, m_ID_Step );

      SQL_Execute( cmd );

      query =   "UPDATE DDLCONTROBS_DBT OBS "
              + "   SET OBS.T_TAXBASE = (SELECT N.T_PREVTAXBASE "
              + "                          FROM DNPTXOBS_DBT N "
              + "                         WHERE N.T_ID_OPERATION = ? "
              + "                           AND N.T_ID_STEP      = ? "
              + "                           AND N.T_INSERT       = CHR(0) "
              + "                           AND N.T_DLCONTRID    = OBS.T_DLCONTRID "
              + "                           AND N.T_DATE         = OBS.T_DATE "
              + "                           AND N.T_TAXBASEKIND  = OBS.T_TAXBASEKIND "
              + "                       ) "
              + " WHERE (OBS.T_DLCONTRID, OBS.T_TAXBASEKIND, OBS.T_DATE) IN (SELECT N.T_DLCONTRID, N.T_TAXBASEKIND, N.T_DATE "
              + "                                                              FROM DNPTXOBS_DBT N "
              + "                                                             WHERE N.T_ID_OPERATION = ? "
              + "                                                               AND N.T_ID_STEP      = ? "
              + "                                                               AND N.T_INSERT       = CHR(0)) ";

      cmd = RSDCommand(query);

      cmd.addParam( "", RSDBP_IN, m_ID_Operation );
      cmd.addParam( "", RSDBP_IN, m_ID_Step );
      cmd.addParam( "", RSDBP_IN, m_ID_Operation );
      cmd.addParam( "", RSDBP_IN, m_ID_Step );

      SQL_Execute( cmd );

      query = "DELETE FROM DNPTXOBS_DBT N WHERE N.T_ID_OPERATION = ? AND N.T_ID_STEP = ? ";

      cmd = RSDCommand(query);

      cmd.addParam( "", RSDBP_IN, m_ID_Operation );
      cmd.addParam( "", RSDBP_IN, m_ID_Step );

      SQL_Execute( cmd );
    end;

    if(m_IsRollBack != true)
      SQL_Execute(  " UPDATE DDLCONTROBS_DBT D "
                  + "    SET D.T_TAXBASE = (SELECT TMP1.T_TAXBASE FROM DDLCONTROBS_TMP TMP1 WHERE TMP1.T_DLCONTRID = D.T_DLCONTRID AND TMP1.T_TAXBASEKIND = D.T_TAXBASEKIND AND TMP1.T_DATE = D.T_DATE) "
                  + "  WHERE (D.T_DLCONTRID, D.T_TAXBASEKIND, D.T_DATE) IN (SELECT TMP.T_DLCONTRID, TMP.T_TAXBASEKIND, TMP.T_DATE FROM DDLCONTROBS_TMP TMP)");

      SQL_Execute(  " INSERT INTO DDLCONTROBS_DBT (T_DLCONTRID, T_DATE, T_TAXBASE, T_TAXBASEKIND, T_DELIVERED)"
                  + " SELECT TMP.T_DLCONTRID, TMP.T_DATE, TMP.T_TAXBASE, TMP.T_TAXBASEKIND, TMP.T_DELIVERED "
                  + "   FROM DDLCONTROBS_TMP TMP "
                  + "  WHERE NOT EXISTS(SELECT 1 "
                  + "                     FROM DDLCONTROBS_DBT D "
                  + "                    WHERE D.T_DLCONTRID = TMP.T_DLCONTRID "
                  + "                      AND D.T_TAXBASEKIND = TMP.T_TAXBASEKIND "
                  + "                      AND D.T_DATE = TMP.T_DATE)");

      SQL_Execute("DELETE FROM DDLCONTROBS_TMP");
    end;

    if(OutTrans == false)
      RslDefCon.CommitTrans();
    end;
  end;

  private macro PrintReport(set : variant, count : integer)
    var printStr = "";
    var delim = ";";
    var TaxBaseKindName = "";
    var LastChangeDate = date(0,0,0);

    var p_SFNumber, p_datebegin, p_dateclose, p_DepoAcntCode;

    macro getSFData(PartyID, isIIS, sfnumber:@string, datebegin:@string, dateclose:@string, DepoAcntCode:@string);

      SFNumber  = "";
      datebegin = "";
      dateclose = "";
      DepoAcntCode = "";

/*      var query = String("select t_number, is_close ",
                         "    from (select sfc.t_number, ",
                         "           case when t_dateclose = to_date('01010001', 'ddmmyyyy') then 0 ",
                         "             else 1 end as is_close  ",
                         "      from dsfcontr_dbt sfc, ddlcontr_dbt dlc ",
                         "     where sfc.t_id = dlc.t_sfcontrid ",
                         "       and sfc.t_partyid = ? ",
                         "       and dlc.t_iis ",iif(isIIS == set_char,"=","!=")," 'X' ",
                         "       and sfc.t_servkind = 0) ",
                         "   order by is_close");*/

      var query =        "  select t_number, is_close, t_datebegin, t_dateclose, DepoAcntCode                                       \n "+
                         "    from (select sfc.t_number,                                                                            \n "+
                         "                 case when t_dateclose = to_date('01010001', 'ddmmyyyy') then 0 else 1 end as is_close,   \n "+
                         "                 sfc.t_datebegin,                                                                         \n "+
                         "                 sfc.t_dateclose,                                                                         \n "+
                         "                 NVL(replace(rsb_struct.getstring(rsi_rsb_kernel.getNote(207, lpad(dlc.T_DLCONTRID,34, '0'), 101,sysdate)),chr(0),''),chr(1)) as DepoAcntCode \n "+
                         "            from dsfcontr_dbt sfc, ddlcontr_dbt dlc     \n "+
                         "           where sfc.t_id = dlc.t_sfcontrid             \n "+
                         "             and sfc.t_partyid = ?                                     \n "+
                         "             and dlc.t_iis " + iif(isIIS == set_char,"=","!=") + " 'X' \n "+
                         "             and sfc.t_servkind = 0)                                   \n "+
                         "   order by is_close";

      var cmd = DL_RSDCommand(query);
          cmd.AddParam(PartyID);
      var rs = Cmd.Execute( );
      if(rs.movenext)
        SFNumber  = rs.number;
        datebegin = string(date(rs.datebegin):f);
        dateclose = string(date(rs.dateclose):f);
        DepoAcntCode = rs.DepoAcntCode;
      end;
    end;

    while(set.moveNext())

      if(set.TaxBaseKind == 1)
        TaxBaseKindName = "ДИВИД";
      elif(set.TaxBaseKind == 2)
        TaxBaseKindName = "БРОК";
      elif(set.TaxBaseKind == 5)
        TaxBaseKindName = "ИИС";
      end;

      LastChangeDate = date(set.LastChangeDate);
      if((set.TaxBase == 0) and (LastChangeDate == date(0,0,0)))
        //Если НОБ стала нулевой, а дата по объектам НДР не определилась (например, удалили все объеты НДР), то ставим текущую дату
        LastChangeDate = m_ExecDate;
      end;

      getSFData(set.PartyID, set.IIS, @p_sfnumber, @p_datebegin, @p_dateclose, @p_DepoAcntCode);

      printStr =           set.PartyName                              //ФИО клиента
                 + delim + StrSubst(set.PartyShortName, ". ", ".")    //ФИО клиента - краткое
                 + delim + p_DepoAcntCode                             //Счет Депо
                 + delim + StrSubst(string(set.TaxBase:a), "'", ",")  //Величина НОБ, руб.
                 + delim + TaxBaseKindName                            //Вид НОБ
                 + delim + string(date(LastChangeDate):f)         //Дата последнего изменения НОБ
                 + delim + set.CFTcode                                //ID клиента в ЦФТ
                 + delim + IIF(set.IIS == SET_CHAR, "Да", "")         //Признак ИИС
                 + delim + p_sfnumber                                 //Номер ДБО
                 + delim + p_datebegin                                //Дата открытия
                 + delim + p_dateclose                                //Дата закрытия
                 + delim + ""                                         //ID НОБ, пока не заполняем
                 ;

      println(toAnsi(printStr,true));
    end;

  end;

  private macro Create()
    var query, cmd, set, count = 0;
    
    if((m_IsRollBack == true) and (m_ID_Operation != 0) and (m_ID_Step != 0))

      query =   " select n.t_TaxBase, n.t_TaxBaseKind, dlc.t_IIS, sf.t_Number, sf.t_DateBegin, sf.t_DateClose, sf.t_PartyID, "
              + "        NVL((select party.t_Name from dparty_dbt party where party.t_PartyID = sf.t_PartyID), chr(1)) as PartyName, "
              + "        NVL((select party.t_ShortName from dparty_dbt party where party.t_PartyID = sf.t_PartyID), chr(1)) as PartyShortName, "
              + "        NVL ((select t_code from dobjcode_dbt where T_OBJECTID = sf.t_PartyID and T_OBJECTTYPE = 3 and T_CODEKIND =101 and t_bankclosedate = to_date('01010001','ddmmyyyy')),chr(1)) as CFTCode, " 
              + "        NVL((select dpsf.t_Object "
              + "               from dsfcontr_dbt dpsf "
              + "              where dpsf.t_UnionContrID = dlc.t_UniDepoContrID"
              + "                and ROWNUM = 1"
              + "            ), chr(1)) as DepoAcntCode, "
              + "        NVL((select MAX(n1.t_Date) "
              + "               from dnptxobs_dbt n1 "
              + "              where n1.t_ID_Operation = n.t_ID_Operation "
              + "                and n1.t_ID_Step = n.t_ID_Step "
              + "            ), " + GetSQLDate(date(0, 0, 0)) + ") LastChangeDate "
              + "   from dnptxobs_dbt n, ddlcontr_dbt dlc, dsfcontr_dbt sf "
              + "  where n.t_ID_Operation = ? "
              + "    and n.t_ID_Step = ? "
              + "    and dlc.t_DlContrID = n.t_DlContrID "
              + "    and sf.t_ID = dlc.t_SfContrID ";

      cmd   = DL_RSDCommand(query);

      cmd.AddParam(m_ID_Operation);
      cmd.AddParam(m_ID_Step);

    else

      query = " select obs.t_TaxBase, obs.t_TaxBaseKind, dlc.t_IIS, sf.t_Number, sf.t_DateBegin, sf.t_DateClose, sf.t_PartyID, "
            + "        NVL((select party.t_Name from dparty_dbt party where party.t_PartyID = sf.t_PartyID), chr(1)) as PartyName, "
            + "        NVL((select party.t_ShortName from dparty_dbt party where party.t_PartyID = sf.t_PartyID), chr(1)) as PartyShortName, "
            + "        NVL ((select t_code from dobjcode_dbt where T_OBJECTID = sf.t_PartyID and T_OBJECTTYPE = 3 and T_CODEKIND =101 and t_bankclosedate = to_date('01010001','ddmmyyyy')),chr(1)) as CFTCode, " 
            + "        NVL((select dpsf.t_Object "
            + "               from dsfcontr_dbt dpsf "
            + "              where dpsf.t_UnionContrID = dlc.t_UniDepoContrID"
            + "                and ROWNUM = 1"
            + "            ), chr(1)) as DepoAcntCode, "
            + "        NVL((select MAX(obj.t_Date) "
            + "               from dnptxobj_dbt obj, dnptxkind_dbt txk  "
            + "              where obj.t_Client = sf.t_PartyID "
            + "                and obj.t_Kind IN ("+TXOBJ_BASEG1+","+TXOBJ_BASEG2+","+TXOBJ_BASEG5+") "
            + "                and obj.t_Date <= ? " 
            + "                and obj.t_Date >= TRUNC(?, 'year') "
            + "                and txk.t_Element = obj.t_Kind "
            + "                and txk.t_TaxBaseKind = obs.t_TaxBaseKind "
            + "            ), " + GetSQLDate(date(0, 0, 0)) + ") LastChangeDate "    
            + "    from ddlcontrobs_tmp obs, ddlcontr_dbt dlc, dsfcontr_dbt sf "
            + "   where dlc.t_DlContrID = obs.t_DlContrID "
            + "     and sf.t_ID = dlc.t_SfContrID ";

      cmd   = DL_RSDCommand(query);

      cmd.AddParam(m_ExecDate);
      cmd.AddParam(m_ExecDate);
    end;

    count = cmd.GetCount();

    if(count > 0)
      set = cmd.Execute();
      PrintReport(set, count);
    end;
  end;

  macro MakeReportFileName() : string
    var day, mon, year;
    DateSplit({curdate}, day, mon, year);

    var hour, min, sec;
    TimeSplit(Time(), hour, min, sec);

    var filename = IIF(m_IsRollBack==true,"Откат","Отчет")+"_НОБ_СОФР_"
                 + string(year:4:o) + string(mon:2:o) + string(day:2:o) + "_"
                 + string(hour:2:o) + string(min:2:o) + string(sec:2:o);

    return MergeFile(Bnk_GetTxtFileDir(), filename, "txt");
  end;

  macro Run()
    FILE output_file() txt write;
    var  filename = MakeReportFileName();

    if (not Open(output_file, filename, "rsansi"))
      msgbox("Ошибка при открытии файла отчета|" + filename);
      break;
    end;
    SetOutput(filename);

    CollectData();
    Create();
    WriteDataToDB();

    SetOutput(null, true);
    Close(output_file);
  end;
end;

//Печать ДБО с превышением НОБ
//ScrollSQLStr - строка с sql-запросом (начиная с FROM) скроллинга ДБО с учетом всех наложенных фильтров
macro ПечатьДоговоровСПревышениемНОБ(ScrollSQLStr:string)
  DL_CDlContrObservedRep(ScrollSQLStr).Run();
  // BIQ-7335 создание события для отправки файла

  var v_result;
  v_result = ExecMacroFile("NOB_check_job.mac","ws_check_job_NOB_out_run"); 

  return v_result;

OnError(er)
  if(RslDefCon.IsInTrans)
     RslDefCon.RollbackTrans();
     return 1;
  end;
end;


macro ВыгрузитьНОБ(QueryStr:string, ID_Operation:integer, ID_Step:integer, ExecDate:date, IsRollBack:bool )
  DL_CDlContrObservedRep(QueryStr, ID_Operation, ID_Step, ExecDate, IsRollBack).Run();

OnError(er)
  if(RslDefCon.IsInTrans)
     RslDefCon.RollbackTrans();
  end;
end;