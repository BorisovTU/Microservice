/*
$Name:        dl_xml.mac
$Module:      Ядро Securities
$Description: Класс для формирования XML-документов
*/
import BankInter;
import "or_tools.mac";

CLASS DL_XML()
  private var m_xml, m_FileName = NULL;
  private var NameSpace = "";

  PRIVATE MACRO GetFileName() //необходимо переопределять
    m_FileName = "dl_xml";

    return m_FileName;
  END;

  PRIVATE MACRO CreateXMLObject_Locale( StrVer:string )
    return ActiveX(StrVer, null, true);

  onError(err)
    return NULL;
  END;

  PRIVATE MACRO CreateXMLObject_Server( StrVer:string )
    var axServer:object = NULL;

    if( (axServer = CreateObject("rsax", "TRsAxServer", "RsAxServer", true)) )
       return axServer.CreateComObject(StrVer, true);
    else
       return NULL;
    end;

    return NULL;

  onError(err)
    return CreateXMLObject_Locale(StrVer);
  END;

  PRIVATE MACRO CreateXMLObject( StrVer:string )

    if( isStandalone() )
       return CreateXMLObject_Locale(StrVer);
    else
       return CreateXMLObject_Server(StrVer);
    end;

  onError(err)
    return NULL;
  END;

  ///////////////////////////////////////////////////
  PRIVATE MACRO CreateXML6Object()

    return CreateXMLObject("Msxml2.DOMDocument.6.0");

  onError(err)
    return NULL;
  END;

  ///////////////////////////////////////////////////
  PRIVATE MACRO CreateXML5Object(xml:object)

    return CreateXMLObject("Msxml2.DOMDocument.5.0");

  onError
    return NULL;
  END;

  ///////////////////////////////////////////////////
  PRIVATE MACRO CreateXML4Object(xml:object)

    return CreateXMLObject("Msxml2.DOMDocument.4.0");

  onError
    return NULL;
  END;

  ///////////////////////////////////////////////////
  PRIVATE MACRO CreateXML2Object(xml:object)

    return CreateXMLObject("Msxml2.DOMDocument");

  onError
    return NULL;
  END;

  ///////////////////////////////////////////////////
  PRIVATE MACRO CreateXML1Object(xml:object)

    return CreateXMLObject("Msxml.DOMDocument");

  onError
    return NULL;
  END;

  ///////////////////////////////////////////////////
  // создать объект MSXML
  ///////////////////////////////////////////////////
  PRIVATE MACRO CreateXMLObj()
    var obj:object = NULL;

    if( (obj = CreateXML6Object()) != NULL )
       return obj;
    end;
    if( (obj = CreateXML5Object()) != NULL )
       return obj;
    end;
    if( (obj = CreateXML4Object()) != NULL )
       return obj;
    end;
    if( (obj = CreateXML2Object()) != NULL )
       return obj;
    end;
    if( (obj = CreateXML1Object()) != NULL )
       return obj;
    end;

    return null;
  END;

  PRIVATE MACRO CreateAttr(AttrName:string, AttrVal:variant)
    var Attr = m_xml.createAttribute(AttrName);
    Attr.value = AttrVal;

    return Attr;
  END;

  PRIVATE MACRO CreateElement(ElemName /*...*/)
    var i = 2;
    var AttrName = "", AttrVal = "";
    var Elem = m_xml.createElement( ElemName);

    while(getparm(i,AttrName))
      getparm(i+1,AttrVal);
      /*на тот случай, чтобы не потерять знаки после запятой*/
      var Type = ValType(AttrVal);
      if( (Type == V_DOUBLE) OR (Type == V_DOUBLEL) OR
          (Type == V_MONEY ) OR (Type == V_MONEYL ) ) 
         Elem.setAttributeNode(CreateAttr(AttrName, WR_ЧислоCЗаданнойТочностью(AttrVal,16)));
      else
         Elem.setAttributeNode(CreateAttr(AttrName, AttrVal));
      end;
      i = i + 2;
    end;

    return Elem;
  END;

  PRIVATE MACRO CreateNode(NodeName /*...*/)
    var i = 2;
    var AttrName = "", AttrVal = "";
    var Node = m_xml.createNode(1, NodeName, NameSpace);

    while(getparm(i,AttrName))
      getparm(i+1,AttrVal);
      
      Node.setAttributeNode(CreateAttr(AttrName, AttrVal));

      i = i + 2;
    end;

    return Node;
  END;

  PRIVATE MACRO CreateElementValue(ElemName,ElemVal /**/)
    var i = 3;
    var AttrName = "", AttrVal = "", Elem;
    Elem = m_xml.createElement(ElemName);
    Elem.appendChild(m_xml.createTextNode(ElemVal));

    while(getparm(i,AttrName))
      getparm(i+1,AttrVal);
      
      Elem.setAttributeNode(CreateAttr(AttrName, AttrVal));

      i = i + 2;
    end;

    return Elem;
  END;

  PRIVATE MACRO RemoveEmpty(elem)
    var i = 0;
    var j = 0;
    var ChildNode, NodeAttr;
    
    if(ValType(elem) == V_UNDEF)
      elem = m_xml;
    end;

    if((ValType(elem.childnodes) != V_UNDEF) and (elem.childnodes.length != 0))
      while(i < elem.childnodes.length)
        ChildNode = elem.childnodes(i);
        
        RemoveEmpty(ChildNode);

        if((ValType(ChildNode.attributes) != V_UNDEF) and (ChildNode.attributes.length > 0))
          j = 0;
          while(j < ChildNode.attributes.length)
            
            NodeAttr = ChildNode.attributes(j);

            if((ValType(NodeAttr.NodeValue)) == V_UNDEF or (NodeAttr.NodeValue == ""))
              ChildNode.RemoveAttributeNode(NodeAttr);
              j = j - 1; //т.к. после удаления атрибуты элемента сдвигаются
            end;

            // Для пустых полей, которые надо оставить
            // используется 3 символа '_'
            if (NodeAttr.NodeValue == "___") 
              NodeAttr.NodeValue = "";
            end;

            j = j + 1;
          end;
        end;

        if(    ((ValType(ChildNode.attributes) == V_UNDEF) or (ChildNode.attributes.length == 0))
           and ((ValType(ChildNode.childnodes) == V_UNDEF) or (ChildNode.childnodes.length == 0))
          )
          // Для пустых узлов, которые надо оставить
          // используется 3 символа '_' в начале имени узла
          if (SubStr(ChildNode.nodeName, 1, 3) != "___")
            elem.RemoveChild(ChildNode);
            i = i - 1; //т.к. после удаления узлы в элементе сдвигаются 
          else
            var realName = SubStr(ChildNode.nodeName, 4);
            elem.removeChild(ChildNode);
            var renamedNode = CreateNode(realName);
            elem.appendChild(renamedNode);
          end;
        end;

        i = i + 1;
      end;
    end;

  END;

  PRIVATE MACRO AddMainNode(Node)
    m_xml.appendChild(Node);
  END;

  PRIVATE MACRO CreateXML(strEncoding)
    m_FileName = null;
    if (strEncoding == null)
      strEncoding =  "windows-1251"; // "utf-8"
    end;

    m_xml = CreateXMLObj();
    if(m_xml != null)
      m_xml.appendChild(m_xml.createProcessingInstruction("xml", "version='1.0' encoding='"+strEncoding+"'" ));
    end;

    return (m_xml != null);
  END;

  PRIVATE MACRO SaveXML()
    var NameFile = GetFileName(); 
    var FileNameLen = strlen(NameFile), DirFileLen = 0;
          
    NameFile = GetTxtFileName(NameFile); 
    
    DirFileLen = strlen(NameFile);
    NameFile = substr(NameFile, 1, Index(NameFile, ".", DirFileLen - FileNameLen)-1);

    m_xml.save(NameFile+".xml");
  END;

  macro SetNameSpace (_name)
    NameSpace = _name;
  end;

  NameSpace = "";
END;
