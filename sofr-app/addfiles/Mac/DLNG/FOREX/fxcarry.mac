/*
$Name: fxcarry.mac
$Module: FOREX
$Description: получение счета, проводки
*/

import OprInter, CTInter, PTInter, DealsInter, InsCarryDoc, fxlb, mccatacf;

RECORD FX_Doc(acctrn);
RECORD FX_mc(acctrn);
private RECORD opstat(operstat);
private RECORD opstep(oprstep);

/*поверка даты выполнения шага*/
MACRO CorrectValueDate ()
    var i = 0, Date;
     
    if( not GetOperationState( opstat ) )
        msgbox("Не могу получить информацию об операции");
        return false;
    end;

    if (opstat.ID_Step!=0)
        /*Шаг еще не вставлен, происходит вставка цепочки вместе с шагом*/
        if( NOT StepInfoEx(null, opstep ) )
            msgbox("Не могу получить информациию о шаге операции");
            return false;
        end;
    end;

    while (GetParm(i, Date))
        if (opstat.ID_Step==0) 
            if((Date != NULL) and ({curdate} < Date))
                Date = {curdate}; 
            end;    
        else
            if( {curdate} > opstep.Plan_Date)
                Date = opstep.Plan_Date;
            else     
                Date = {curdate};
            end;
        end;

        SetParm (i, Date);
        i = i + 1;
    end;

    return true;
END;

/*Получить счет для документа*/
MACRO FX_GetDocAccount(CatCode,     /*категория (код)*/
                       fd,          /*документ*/
                       Account, /*здесь будет номер счета*/
                       OpenMode,    /*(MC_OPENACC_ACT, MC_OPENACC_CREATE, MC_OPENACC_RECREATE,
                                       MC_OPENACC_CHECKPERIOD, MC_OPENACC_CHECKEXIST*/
                       ActionDate,  /*дата нахождения, открытия*/
                       Currency,    /*валюта счета*/
                       IsMass,      /*режим массовой работы*/
                       ORScheme,    /*схема переоценки*/
                       AccBuf,      /*буфер для найденного/открытого счета (Account)*/
                       FIRole,      /* роль финансового инструмента */
                       ActivateDate,/* дата начала действия счета */
                       PairAccMode);
  var ResultAccount = "";

  if ((ActionDate == NULL) or (ActionDate == {curdate}))
     if (not CorrectValueDate(ActionDate))
        return false;
     end;
  end;

  if (ValType(OpenMode) != V_INTEGER)
    OpenMode = MC_OPENACC_CREATE;
  end;
  if ((ValType(IsMass) != V_INTEGER) AND (ValType(IsMass) != V_BOOL))
    IsMass = 0;
  end;
  if (ValType(IsMass) == V_BOOL)
    if (IsMass)
      IsMass = 1;
    else
      IsMass = 0;
    end;
  end;
  if (ValType(PairAccMode) != V_INTEGER)
    PairAccMode = MC_PAIRACCMODE_AUTO;
  end;

  
  ResultAccount = MC_FindAndOpenAccountEx(CatCode,
                                        fd,
                                        ActionDate,
                                        IsMass,
                                        OpenMode,
                                        AccBuf,
                                        Currency,
                                        ORScheme,
                                        PairAccMode,
                                        NULL,
                                        FIRole,
                                        null, null, null, null,
                                        ActivateDate            /* дата начала действия счета */
                                       );

  if (ValType(Account) != V_UNDEF)
    Account = ResultAccount;
  end;
  if (ResultAccount == "")
    return false;
  end;
  SetParm(2, ResultAccount);
  return true;
END;

/*Получить корсчет из корсхемы*/
MACRO FX_GetCorrAccount(fd, FIID, Purpose, isDebet, Account)
  var pmpaym = TBFile("pmpaym", "R", 1),
      pmprop = TBFile("pmprop", "R", 0),
      ok = false;

  pmpaym.rec.DocKind = fd.tick.rec.BofficeKind;
  pmpaym.rec.DocumentID = fd.tick.rec.DealID;
  pmpaym.rec.Purpose = Purpose;
  pmpaym.rec.SubPurpose = 0;
  pmpaym.AddFilter("t_DocKind = "+fd.tick.rec.BofficeKind+
              " AND t_DocumentID = "+ fd.tick.rec.DealID+
              " AND t_Purpose = "+Purpose);
  if (pmpaym.GetGE)
    pmprop.rec.PaymentID = pmpaym.rec.PaymentID;
    pmprop.rec.DebetCredit = isDebet;
    if (pmprop.GetEQ)
      ok = true;
    end;
  end;
  pmpaym.DropFilter();
  if(ok == false)
     return false;
  end;
  Account = GetCorAcc(FIID, pmprop.rec.Corschem, CORS_ACC_ACCOUNT);
  if (Account == "")
    return false;
  else
    SetParm(4, Account);
    return true;
  end;
END;

MACRO FX_GetRequirementAccount(fd, TranModeReq, AccountD, AccountC, CurrencyDate)
  var stat = 0, CurCom, CurReq,
      Purpose, Paym, DocKind, DocumentID;

  AccountD = "";
  AccountC = "";

  fd.SetCurrency(CurCom, CurReq);

  if (IsMOVING(fd.tick.rec.DealGroup))
     DocKind    = fd.tick.rec.BofficeKind;
     DocumentID = fd.tick.rec.DealID;
     Purpose    = FX_GetPaymentPurpose(0, fd.leg.rec.LegID, FX_IsSale(fd.tick.rec.TypeDoc));
     Paym       = RsbPayment(DocKind, DocumentID, Purpose, 0);
  
     AccountD = paym.FuturePayerAccount;

     FX_GetDocAccount("+Расчеты", fd, AccountC, MC_OPENACC_CREATE, CurrencyDate, CurReq, NULL, NULL, NULL, FIROLE_FICOM, NULL, MC_PAIRACCMODE_MANUAL);
  else
     if (not fd.PartyIsMarket)
        if (bAND(TranModeReq, FX_TRAN_BEFOREPAYMENT))
           FX_GetDocAccount("+Расчеты", fd, AccountD, MC_OPENACC_CREATE, CurrencyDate, CurReq, NULL, NULL, NULL, FIROLE_FIREQ);
        else
           DocKind    = fd.tick.rec.BofficeKind;
           DocumentID = fd.tick.rec.DealID;
           Purpose    = FX_GetPaymentPurpose(0, fd.leg.rec.LegID, FX_IsSale(fd.tick.rec.TypeDoc));
           Paym       = RsbPayment(DocKind, DocumentID, Purpose, 0);
   
           AccountD = paym.FuturePayerAccount;
        end;
     elif (fd.PartyIsMarket)
         FX_GetDocAccount("+Биржа", fd, AccountD, MC_OPENACC_CREATE, CurrencyDate, CurReq, NULL, NULL, NULL, FIROLE_FIREQ);
     end;

     FX_GetDocAccount("+ОТО", fd, AccountC, MC_OPENACC_CREATE, CurrencyDate, CurReq, NULL, NULL, NULL, FIROLE_FIREQ);
  end;

  SetParm(2, AccountD);
  SetParm(3, AccountC);

  return true;
END;

MACRO FX_GetCommitmentAccount(fd, TranModeCom, AccountD, AccountC, CurrencyDate)
  var stat = 0, CurCom, CurReq,
      Purpose, Paym, DocKind, DocumentID;

  AccountD = "";
  AccountC = "";

  fd.SetCurrency(CurCom, CurReq);

  if (IsMOVING(fd.tick.rec.DealGroup))
     DocKind    = fd.tick.rec.BofficeKind;
     DocumentID = fd.tick.rec.DealID;
     Purpose    = FX_GetPaymentPurpose(1, fd.leg.rec.LegID, FX_IsSale(fd.tick.rec.TypeDoc));
     Paym       = RsbPayment(DocKind, DocumentID, Purpose, 0);
  
     AccountC = paym.FutureReceiverAccount;

     FX_GetDocAccount("-Расчеты", fd, AccountD, MC_OPENACC_CREATE, CurrencyDate, CurReq, NULL, NULL, NULL, FIROLE_FIREQ, NULL, MC_PAIRACCMODE_MANUAL);
  else
     if (not fd.PartyIsMarket)
        if (bAND(TranModeCom, FX_TRAN_BEFOREPAYMENT))
           FX_GetDocAccount("-Расчеты", fd, AccountC, MC_OPENACC_CREATE, CurrencyDate, CurCom, NULL, NULL, NULL, FIROLE_FICOM);
        else
           DocKind    = fd.tick.rec.BofficeKind;
           DocumentID = fd.tick.rec.DealID;
           Purpose    = FX_GetPaymentPurpose(1, fd.leg.rec.LegID, FX_IsSale(fd.tick.rec.TypeDoc));
           Paym       = RsbPayment(DocKind, DocumentID, Purpose, 0);
   
           AccountC = paym.FutureReceiverAccount;
        end;
      elif (fd.PartyIsMarket)
         FX_GetDocAccount("-Биржа", fd, AccountC, MC_OPENACC_CREATE, CurrencyDate, CurCom, NULL, NULL, NULL, FIROLE_FICOM);
      end;

      FX_GetDocAccount("-ОТО", fd, AccountD, MC_OPENACC_CREATE, CurrencyDate, CurCom, NULL, NULL, NULL, FIROLE_FICOM);
  end;

  SetParm(2, AccountD);
  SetParm(3, AccountC);

  return true;
END;

PRIVATE MACRO FX_MakeBookpass( 
                СчетДебет,              /* 0 */
                CуммаДебет,             /* 1 */
                КодВалютыДебет,         /* 2 */
                СчетКредит,             /* 3 */
                СуммаКредит,            /* 4 */
                КодВалютыКредит,        /* 5 */
                Назначение,             /* 6 */
                PaymentID,              /* 7 */
                Method,                 /* 8 */
                Глава,                  /* 9 */
                ДатаВалютирования,      /* 10 */
                ДатаВыполнения,         /* 11 */
                ДатаКурса,              /* 12 */
                Результат,              /* 13 */
                SumEquivalentCarry )    /* 14 */
 var tr;
 var PaymentObj = null;

 if (ValType(Method) == V_UNDEF)         Method = PMMET_ID;     end;
 if (ValType(ДатаВалютирования) == V_UNDEF) ДатаВалютирования = {curdate};     end;
 if (ValType(ДатаВыполнения) == V_UNDEF) ДатаВыполнения = ДатаВалютирования;     end;
 if (ValType(Назначение) == V_UNDEF)     Назначение = "";     end;
 if (ValType(Результат) == V_UNDEF)      Результат = 1;     end;

 /* ------ Выполнить проводку ----------------------------------------------*/
 if( (Method != PMMET_ID_TMP_OP) AND ((PaymentID != NULL) AND (PaymentID > 0)) )//???
    /*проводку формируем по платежу*/
    PaymentObj = RSBPayment( PaymentID );
    tr = PaymentObj.MakeTransaction();
 else
    /*проводку формируем без платежа*/
    tr = RsbAccTransaction();
 end;

 if( tr == NULL )
    MsgBox("Ошибка при создании проводки по платежу");
    return 1;
 end;

 tr.Chapter         = Глава;
 tr.Date_Carry      = ДатаВыполнения;
 tr.Date_Rate    = ДатаКурса;
 if( PaymentObj != null )
    tr.Number_Pack  = PaymentObj.NumberPack;
 end;
 tr.Numb_Document   = "";
 tr.ResultCarry     = Результат;
 tr.Kind_Oper       = "";//???
 tr.Ground          = Назначение;
 tr.Department      = {OperDprt};
 tr.FIIDPayer       = КодВалютыДебет;
 tr.FIIDReceiver    = КодВалютыКредит;
 tr.SumPayer        = CуммаДебет;
 tr.SumReceiver     = СуммаКредит;
 if (ValType(SumEquivalentCarry) == V_MONEY)
     tr.SumEquivalentCarry = SumEquivalentCarry;
 end;
 tr.AccountPayer    = СчетДебет;
 tr.AccountReceiver = СчетКредит;
 tr.Shifr_Oper      = "";

 if( not tr.Carry() )
    MsgBox("Ошибка при выполнении проводки");
    return 1;
 end;
 /*-------------------------------------------------------------------------*/  

 return 0;
END;

MACRO FX_Carry(Chapter,
               FIID,
               Debet,
               Credit,
               Amount,
               ValDate,
               CarryResult,
               Ground,
               PaymentID,
               Method)

  if (not CorrectValueDate(ValDate))
    return false;
  end;

  if (ValType(Method) == V_UNDEF)
    Method = PMMET_ID;
  end;

  if (ValType(ValDate) == V_UNDEF)     ValDate = {curdate}; end;
  if (ValType(CarryResult) == V_UNDEF) CarryResult = 1;     end;
  if (ValType(Ground) == V_UNDEF)      Ground = "";         end;

  return (0 == FX_MakeBookpass(Debet,   Amount,    FIID,
                               Credit,  Amount,    FIID, 
                               Ground,  PaymentID, Method, 
                               Chapter, ValDate,   ValDate, 
                               ValDate, CarryResult));
  return true;
END;

MACRO FX_MultyCarry(FIID_Debet, Acc_Debet, Amount_Debet, 
                    FIID_Credit, Acc_Credit, Amount_Credit,
                    Chapter, AccountPlus, AccountMinus,
                    ExecDate, ValDate, Ground, RateDate, SumEquivalentCarry)

  if (not CorrectValueDate(ValDate, ExecDate))
    return false;
  end;

  return (0 == FX_MakeBookpass(Acc_Debet,   Amount_Debet,  FIID_Debet,
                               Acc_Credit,  Amount_Credit, FIID_Credit, 
                               Ground, 0, 1, Chapter, ValDate, ExecDate, RateDate, NULL, SumEquivalentCarry));
END;
