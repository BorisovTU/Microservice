/*
$Name:        fxcomiss.mac
$Module:      Конверсионные операции
$Description: Комиссии КО
*/
IMPORT fxcarry, fxlb, OprInter, sfInter;

private class StructComiss()
  var 
      Sum      = 0,
      SumNDS   = 0,
      FIID_Sum = NATCUR;
end;

MACRO ПолучитьСуммуКомиссий()
  var Comiss = TArray;

  RECORD opstat(operstat);
  RECORD OprCom( oprsfcom );

    ClearRecord(opstat);
    ClearRecord(OprCom);

    if (not GetOperationState( opstat))
        fx_error("Не могу получить информацию об операции");
        return 0;
    end;

    while(SfGetOperCommission(opstat.ID_Operation, -1, OprCom))
        Comiss[Comiss.size] = StructComiss();
        Comiss[Comiss.size - 1].Sum      = OprCom.Sum;
        Comiss[Comiss.size - 1].SumNDS   = OprCom.SumNDS;
        Comiss[Comiss.size - 1].FIID_Sum = OprCom.FIID_Sum;
      end;

    return Comiss;
END;

macro Get_Comiss(fd)
    if (fd.PartyIsMarket)
        return String(17);
    else
        return String(20);
    end;

    return 1;
end;

PRIVATE MACRO SmartConvertSum(sumTo, sumFrom, sinceDate, fiidFrom, fiidTo):BOOL
  VAR NewSum = $0, SumConv = $0;
  if( fiidFrom == fiidTo )  NewSum = sumFrom;
  elif( ConvSum( NewSum, sumFrom, sinceDate, fiidFrom, fiidTo ) != 0 )
    if( ConvSumCross( NewSum, sumFrom, sinceDate, fiidFrom, fiidTo ) != true ) 
       return false;
    end;
  end;
  SetParm( 0, NewSum );
  return true;
END;

MACRO FX_Comiss(fd)
  var
      Sum,
      SumDebet, SumCredit, 
      Debet, Credit,
      ComCur, ReqCur,
      CurDebet, CurCredit,
      RateDate;

    fd.SetCurrency(ComCur, ReqCur);
    SumDebet = SumCredit = Sum = fd.leg.rec.Principal - fd.leg.rec.Cost;

    RateDate = {curdate};
    CorrectValueDate(RateDate);

    if (Sum == 0)
      return true;
    elif (((Sum > 0)and(fd.tick.rec.TypeDoc == "B"))or
          ((Sum < 0)and(fd.tick.rec.TypeDoc == "S")))
        SumDebet  = Sum;
        SmartConvertSum(SumCredit, Sum, RateDate, ReqCur, NATCUR);
        CurDebet  = ReqCur;
        CurCredit = NATCUR;

        if ((not FX_GetDocAccount("+Расчеты", fd, Debet, NULL, NULL, CurDebet, NULL, NULL, NULL, FIROLE_FIREQ, NULL, MC_PAIRACCMODE_MANUAL)) OR
            (not FX_GetDocAccount("+КВ, РКО", fd, Credit, NULL, NULL, CurCredit, NULL, NULL, NULL, NULL)))
            return false;
        end;

    elif (((Sum < 0)and(fd.tick.rec.TypeDoc == "B"))or
          ((Sum > 0)and(fd.tick.rec.TypeDoc == "S")))
        SumCredit = Sum;
        SmartConvertSum(SumDebet, Sum, RateDate, ComCur, NATCUR);
        CurDebet  = NATCUR;
        CurCredit = ComCur;

        if ((not FX_GetDocAccount("-КВ, РКО", fd, Debet, NULL, NULL, CurDebet, NULL, NULL, NULL, NULL)) OR
            (not FX_GetDocAccount("-Расчеты", fd, Credit, NULL, NULL, CurCredit, NULL, NULL, NULL, FIROLE_FICOM, NULL, MC_PAIRACCMODE_MANUAL)))
            return false;
        end;
    end;

    if (not FX_MultyCarry(CurDebet, Debet,  abs(SumDebet),
                          CurCredit, Credit, abs(SumCredit),
                          1, "", "", {curdate}, {curdate},
                          "Комиссия по банкнотной сделке по балансу", RateDate))
        fx_error("Не могу выполнить проводку \"Комиссия по банкнотной сделке\"");
        return false;
    end;

    return true;
END;

macro FX_MarketComiss(fd)
  var Comiss =     ПолучитьСуммуКомиссий(),
      Sum, SumNDS, Cur,
      Debet, Credit,
      DebetNDS, CreditNDS;

// т.к. в дистрибутиве только одна комиссия, которая работает через механизм комиссий,
// к тому же еще и в нац. валюте, то делаем без излишеств.
// если появяться другие комиссии, то доработаем механизм проводок
    if (Comiss.size > 0)
        Sum    = Comiss[0].Sum;
        SumNDS = Comiss[0].SumNDS;
        Cur    = Comiss[0].FIID_Sum;

        if (Sum > 0)
            if ((not FX_GetDocAccount("-КВ, к/о", fd, Debet, NULL, NULL, Cur, NULL, NULL, NULL, NULL)) OR
                (not FX_GetDocAccount("-Биржа", fd, Credit, NULL, NULL, Cur, NULL, NULL, NULL, NULL)))
                return false;
            end;
            if ((not FX_Carry(1, Cur, Debet, Credit, abs(Sum), {curdate}, 0, "Оплата биржевой комиссии", 0)))
                fx_error("Не могу выполнить проводки по оплате биржевой комиссии");
                return 1;
            end;
        end;

        if (SumNDS > 0)
            if ((not FX_GetDocAccount("+НДС", fd, DebetNDS, NULL, NULL, Cur, NULL, NULL, NULL, NULL)) OR
                (not FX_GetDocAccount("-Биржа", fd, CreditNDS, NULL, NULL, Cur, NULL, NULL, NULL, NULL)))
                return 1;
            end;

            if ((not FX_Carry(1, Cur, DebetNDS, CreditNDS, abs(SumNDS), {curdate}, 0, "Оплата НДС по биржевой комиссии", 0)))
                fx_error("Не могу выполнить проводки по оплате биржевой комиссии");
                return 1;
            end;
        end;
    end;

    return 0;
end;
