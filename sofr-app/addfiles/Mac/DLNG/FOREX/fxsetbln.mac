/*
$Name: fxsetbln.mac
$Module: FOREX
$Description: Постановка сделки на внебалансовый учет, снятие, постановка на балансовый учет
*/

IMPORT InsCarryDoc, RsbFormsInter, "fxcarry.mac", "fxlb.mac", "dlcarry.inc";

PRIVATE MACRO GetKursDate(fd)
    return fd.leg.rec.Start;
END;

MACRO FX_SetNonBalance(fd)
  var CurComm, SumComm,
      CurReq, SumReq,
      Sum1 = 0, Sum2 = 0,
      Debet1, Credit1,
      Debet2, Credit2, OperDate;

    /*поверка даты выполнения шага*/
    if( not CorrectValueDate(OperDate) )
        return false;
    end;

    fd.SetSums(SumComm, SumReq);
    fd.SetCurrency(CurComm, CurReq);

    if (fd.DealType == DL_SPOTDEAL)
        if (
            (not FX_GetDocAccount("+Форвард, прочие", fd, Debet1,  NULL, NULL, CurReq,  NULL, NULL, NULL, FIROLE_FIREQ))          OR
            (not FX_GetDocAccount("СчКорреспГлаваГ",  fd, Credit1, NULL, NULL, NatCur,  NULL, NULL, NULL, FIROLE_CORACC_ACTIVE))  OR
            (not FX_GetDocAccount("СчКорреспГлаваГ",  fd, Debet2,  NULL, NULL, NatCur,  NULL, NULL, NULL, FIROLE_CORACC_PASSIVE)) OR
            (not FX_GetDocAccount("-Форвард, прочие", fd, Credit2, NULL, NULL, CurComm, NULL, NULL, NULL, FIROLE_FICOM))
           )
            return false;
        end;

        if (
            (ConvSum( Sum1, SumReq, fd.GetOperDate(FX_OPERDATE_DEAL), CurReq, NatCur) > 0)  OR
            (ConvSum( Sum2, SumComm, fd.GetOperDate(FX_OPERDATE_DEAL), CurComm, NatCur) > 0)
           )
            fx_error("Не могу выполнить проводку \"Учет сделки Spot по внебалансу\"");
            return false;
        end;

        if (
            (not FX_MultyCarry(CurReq, Debet1, SumReq,
                                NatCur, Credit1, Sum1,
                                4, NULL, NULL,
                                OperDate, OperDate,
                                "Учет сделки Spot по внебалансу", GetKursDate(fd))) OR
            (not FX_MultyCarry(NatCur, Debet2, Sum2,
                                CurComm, Credit2, SumComm,
                                4, NULL, NULL,
                                OperDate, OperDate,
                                "Учет сделки Spot по внебалансу", GetKursDate(fd)))
           )
            fx_error("Не могу выполнить проводку \"Учет сделки Spot по внебалансу\"");
            return false;
        end;
    elif (fd.DealType == DL_FORWARDDEAL)
        if (
            (not FX_GetDocAccount("+Форвард, дрейф",  fd, Debet1,  NULL, NULL, CurReq,  NULL, NULL, NULL, fd.GetFIRoleByCategory("+Форвард, дрейф", CurReq, FIROLE_FIREQ)))          OR
            (not FX_GetDocAccount("СчКорреспГлаваГ",  fd, Credit1, NULL, NULL, NatCur,  NULL, NULL, NULL, FIROLE_CORACC_ACTIVE))  OR
            (not FX_GetDocAccount("СчКорреспГлаваГ",  fd, Debet2,  NULL, NULL, NatCur,  NULL, NULL, NULL, FIROLE_CORACC_PASSIVE)) OR
            (not FX_GetDocAccount("-Форвард, дрейф",  fd, Credit2, NULL, NULL, CurComm, NULL, NULL, NULL, fd.GetFIRoleByCategory("-Форвард, дрейф", CurComm, FIROLE_FICOM)))
           )
            return false;
        end;

        if (
            (ConvSum( Sum1, SumReq, fd.GetOperDate(FX_OPERDATE_DEAL), CurReq, NatCur) > 0)  OR
            (ConvSum( Sum2, SumComm, fd.GetOperDate(FX_OPERDATE_DEAL), CurComm, NatCur) > 0)
           )
            fx_error("Не могу выполнить проводку \"Учет сделки Forward по внебалансу\"");
            return false;
        end;

        if (
            (not FX_MultyCarry(CurReq, Debet1, SumReq,
                                NatCur, Credit1, Sum1,
                                4, NULL, NULL,
                                OperDate, OperDate,
                                "Учет сделки Forward по внебалансу", GetKursDate(fd))) OR
            (not FX_MultyCarry(NatCur, Debet2, Sum2,
                                CurComm, Credit2, SumComm,
                                4, NULL, NULL,
                                OperDate, OperDate,
                                "Учет сделки Forward по внебалансу", GetKursDate(fd)))
           )
            fx_error("Не могу выполнить проводку \"Учет сделки Forward по внебалансу\"");
            return false;
        end;
    end;

    return true;
END;

MACRO FX_UnsetNonBalance(fd)
  var CurComm, SumComm,
      CurReq, SumReq,
      Sum1 = 0, Sum2 = 0,
      Debet1, Credit1,
      Debet2, Credit2, OperDate;

    /*поверка даты выполнения шага*/
    if( not CorrectValueDate(OperDate) )
        return false;
    end;

    fd.SetSums(SumComm, SumReq);
    fd.SetCurrency(CurComm, CurReq);

    if (fd.DealType == DL_SPOTDEAL)
        if (
            (not FX_GetDocAccount("СчКорреспГлаваГ",  fd, Debet1,   NULL, NULL, NatCur,  NULL, NULL, NULL, FIROLE_CORACC_ACTIVE))  OR
            (not FX_GetDocAccount("+Форвард, прочие", fd, Credit1,  NULL, NULL, CurReq,  NULL, NULL, NULL, FIROLE_FIREQ))          OR
            (not FX_GetDocAccount("-Форвард, прочие", fd, Debet2,   NULL, NULL, CurComm, NULL, NULL, NULL, FIROLE_FICOM))          OR
            (not FX_GetDocAccount("СчКорреспГлаваГ",  fd, Credit2,  NULL, NULL, NatCur,  NULL, NULL, NULL, FIROLE_CORACC_PASSIVE))
           )
            return false;
        end;

        if (
            (ConvSum( Sum1, SumReq, OperDate, CurReq, NatCur) > 0)  OR
            (ConvSum( Sum2, SumComm, OperDate, CurComm, NatCur) > 0)
           )
            fx_error("Не могу выполнить проводку \"Прекращение учета сделки Spot по внебалансу\"");
            return false;
        end;

        if (
            (not FX_MultyCarry(NatCur, Debet1, Sum1,
                                CurReq, Credit1, SumReq,
                                4, NULL, NULL,
                                OperDate, OperDate,
                                "Прекращение учета сделки Spot по внебалансу", GetKursDate(fd), Money(0))) OR
            (not FX_MultyCarry(CurComm, Debet2, SumComm,
                                NatCur, Credit2, Sum2,
                                4, NULL, NULL,
                                OperDate, OperDate,
                                "Прекращение учета сделки Spot по внебалансу", GetKursDate(fd), Money(0)))
           )
            fx_error("Не могу выполнить проводку \"Прекращение учета сделки Spot по внебалансу\"");
            return false;
        end;
    elif (fd.DealType == DL_FORWARDDEAL)
        if (
            (not FX_GetDocAccount("СчКорреспГлаваГ",  fd, Debet1,  NULL, NULL, NatCur,  NULL, NULL, NULL, FIROLE_CORACC_ACTIVE))  OR
            (not FX_GetDocAccount("+Форвард, дрейф",  fd, Credit1, NULL, NULL, CurReq,  NULL, NULL, NULL, fd.GetFIRoleByCategory("+Форвард, дрейф", CurReq, FIROLE_FIREQ)))  OR
            (not FX_GetDocAccount("-Форвард, дрейф",  fd, Debet2,  NULL, NULL, CurComm, NULL, NULL, NULL, fd.GetFIRoleByCategory("-Форвард, дрейф", CurComm, FIROLE_FICOM))) OR
            (not FX_GetDocAccount("СчКорреспГлаваГ",  fd, Credit2, NULL, NULL, NatCur,  NULL, NULL, NULL, FIROLE_CORACC_PASSIVE))
           )
            return false;
        end;

        if (
            (ConvSum( Sum1, SumReq, OperDate, CurReq, NatCur) > 0)  OR
            (ConvSum( Sum2, SumComm, OperDate, CurComm, NatCur) > 0)
           )
            fx_error("Не могу выполнить проводку \"Прекращение учета сделки Forward по внебалансу\"");
            return false;
        end;

        if (
            (not FX_MultyCarry(NatCur, Debet1, Sum1,
                                CurReq, Credit1, SumReq,
                                4, NULL, NULL,
                                OperDate, OperDate,
                                "Прекращение учета сделки Forward по внебалансу", GetKursDate(fd), Money(0))) OR
            (not FX_MultyCarry(CurComm, Debet2, SumComm,
                                NatCur, Credit2, Sum2,
                                4, NULL, NULL,
                                OperDate, OperDate,
                                "Прекращение учета сделки Forward по внебалансу", GetKursDate(fd), Money(0)))
           )
            fx_error("Не могу выполнить проводку \"Прекращение учета сделки Forward по внебалансу\"");
            return false;
        end;
    end;

    return true;
END;

private macro ПолучитьСправедливуюСтоимость(tick, stepDate)
   var objVal = 0, ObjectID = UniID(tick, OBJTYPE_CONVDEAL);
   var SSS_Sum = 0;

   // только для сделок исполнения срочных контрактов - категория на сделке "Исполнение поставочных срочных контрактов" = "Да")
   if ((GetMainObjAttr(NULL, OBJTYPE_CONVDEAL, ObjectID, 2, NULL, NULL, objVal) AND // "Исполнение поставочных срочных контрактов"
       (objVal == 1))) // Да
      var Query = RSDCommand( "select NVL(rsb_struct.getMoney(rsi_rsb_kernel.GetNote(?, ?, ?, ?)), 0) as SSS_SUM from dual" );

      Query.addParam( "", RSDBP_IN, OBJTYPE_CONVDEAL);
      Query.addParam( "", RSDBP_IN, ObjectID        );
      Query.addParam( "", RSDBP_IN, 1                );
      Query.addParam( "", RSDBP_IN, stepDate         );
      Query.execute();

      var DataSet = TRsbDataSet(Query);
      DataSet.moveNext();
      SSS_Sum = DataSet.SSS_Sum;
   end;

   return Money(SSS_Sum);
end;

private class (TRsbPanel) TFX_SetBalancePanel()
  var
      ComboBox = NULL;

  macro eventHandler(EV)
      if (EV.keyCode == 323)
          if (ComboBox.currentChoice > 0)
              close(-EV.keyCode);
          end;
      end;
  end;

//constructor
  InitTRsbPanel();

   caption = "Сторона сделки";
   setPosition(10, 5);
   setSize(30, 4);
   status = "~F3~ Выбрать ~F9~ Сохранить  ~Esc~ Выход";


  addLabel(TRsbLabel(2, 2, "Сторона сделки"));
  ComboBox = TRsbComboBox;
  ComboBox.name       = "TRCB";
  ComboBox.dataType   = 7;
  ComboBox.textLength = 10;
  ComboBox.setPosition(15, 2);
  ComboBox.setSize    (10, 1);
  ComboBox.addChoice(1, "Дебет");
  ComboBox.addChoice(2, "Кредит");
  addControl(ComboBox);

  addEventHandler(RSB_EV_KEY_PRESSED, r2m(this, "eventHandler"));
end;

MACRO FX_SetBalance(fd)
  var CurComm, SumComm,
      CurReq = 0, SumReq = 0, 
      SSS_Sum = 0,
      Acc1 = "", Acc2 = "",
      Purpose, paym;

  // Актуализируем платеж по требованиям здесь, в постановке на баланс,
  // а не на шаге "Исполнение требований", т.к. вместо исполнения шага 
  // может быть квитовка платежа по оплате треб.,
  // и к моменту квитовки уже должны быть заполнены суммы в платеже.
  Purpose = FX_GetPaymentPurpose(0, fd.leg.rec.LegID, FX_IsSale(fd.tick.rec.TypeDoc));
  paym = RsbPayment( fd.tick.rec.BofficeKind, fd.tick.rec.DealID, Purpose, 0 );
  if ( paym.PaymentID == 0 )
    fx_error("Не найден плановый платеж по оплате требования");
    return false;
  end;
  paym.Actuate();

  fd.SetSums(SumComm, SumReq);
  fd.SetCurrency(CurComm, CurReq);

  if (IsMOVING(fd.tick.rec.DealGroup))
      if ((not FX_GetDocAccount("+Расчеты", fd, Acc1, NULL, NULL, CurReq,  NULL, NULL, NULL, FIROLE_FIREQ, NULL, MC_PAIRACCMODE_MANUAL)) OR
          (not FX_GetDocAccount("-Расчеты", fd, Acc2, NULL, NULL, CurComm, NULL, NULL, NULL, FIROLE_FICOM, NULL, MC_PAIRACCMODE_MANUAL)))
        return false;
      end;
    
      if (not FX_Carry(1, CurComm, Acc1, Acc2, min(SumComm, SumReq), {curdate}, 0, 
                         "Учет банкнотной сделки по балансу", 0))
        fx_error("Не могу выполнить проводку \"Учет банкнотной сделки по балансу\"");
        return false;
      end;
  else
      if ((not FX_GetDocAccount("+ОТО", fd, Acc1, NULL, NULL, CurReq, NULL, NULL, NULL, FIROLE_FIREQ)) OR
          (not FX_GetDocAccount("-ОТО", fd, Acc2, NULL, NULL, CurComm, NULL, NULL, NULL, FIROLE_FICOM)))
        return false;
      end;

      SSS_Sum = ПолучитьСправедливуюСтоимость(fd.tick, {curdate});
      if (SSS_Sum > 0)
          var win = TFX_SetBalancePanel();
          if (win.run() != -323)
              return false; 
          else
              if (win.ComboBox.currentChoice == 1)
                  if (not FX_MultyCarry(CurReq, Acc1, max((SumReq - SSS_Sum), 0),
                                        CurComm, Acc2, SumComm,
                                        1, NULL, NULL,
                                        {curdate}, {curdate},
                                        "Учет сделки по балансу"))
                    fx_error("Не могу выполнить проводку \"Учет сделки по балансу\"");
                    return false;
                  end;
              else
                  if (not FX_MultyCarry(CurReq, Acc1, SumReq,
                                        CurComm, Acc2, max((SumComm - SSS_Sum), 0),
                                        1, NULL, NULL,
                                        {curdate}, {curdate},
                                        "Учет сделки по балансу"))
                    fx_error("Не могу выполнить проводку \"Учет сделки по балансу\"");
                    return false;
                  end;
              end;
          end;
      else
          if (not FX_MultyCarry(CurReq, Acc1, SumReq,
                                CurComm, Acc2, SumComm,
                                1, NULL, NULL,
                                {curdate}, {curdate},
                                "Учет сделки по балансу"))
            fx_error("Не могу выполнить проводку \"Учет сделки по балансу\"");
            return false;
          end;
      end;
  end;
  
  return true;  
END;
