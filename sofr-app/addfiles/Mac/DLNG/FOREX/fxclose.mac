//*******************************************************************************//
//*  Модуль           : FOREX                                                   *//
//*  Имя файла        : fxclose.mac                                             *// 
//*  Описание         : Закрытие сделки                                         *//
//*  Программист      : Куперин М.В. //13.08.2009                               *//
//*******************************************************************************//
import PaymInter, fxlb, CurrInter, globals;

macro FX_CloseDeal(fd)
  var 
    stat  = 0,    flag     = "", 
    paym = NULL, account = "", 
    rest = 0, retVal = true, QueryStr = "";

  paym = TRsbDataSet("select t_purpose from dpmpaym_dbt where "
                      "(t_dockind = " + fd.tick.rec.BofficeKind + ")and "
                      "(t_documentid = " + fd.tick.rec.DealID + ")and "
                      "(t_paymstatus < " + PM_READY_TO_SEND + ")and "
                      "(t_paymstatus != " + PM_CLOSED_W_M_MOVEMENT + ")");

  if(paym.MoveNext)
      if (FX_IsPaymentCommitment(FX_IsSale(fd.tick.rec.TypeDoc), paym.Purpose))
          fx_error("Обязательства по сделке не исполнены");
      else
          fx_error("Требования по сделке не исполнены");
      end;

      retVal = false;
  else
      GetRegistryValue("КОНВЕРСИОННЫЕ ОПЕРАЦИИ\\ЗАКРЫВАТЬ_СЧЕТА_ПО_СДЕЛКЕ",
                        V_BOOL, flag, stat);

      if (stat)
          fx_error("Ошибка при работе с реестром");
          retVal = false;
      else
          if (flag)
              QueryStr = "select categ.t_code, accdoc.t_account, accdoc.t_currency, "
                          "accdoc.t_chapter, accdoc.t_firole from "
                          "dmcaccdoc_dbt accdoc, dmccateg_dbt categ where "
                          "(accdoc.t_dockind = 175)and "
                          "(accdoc.t_docid = " + fd.leg.rec.ID + ")and "
                          "(accdoc.t_catid = categ.t_id)and "
                          "(categ.t_updatemode = 0) and"
                          " Exists(select * from daccount_dbt acc where acc.t_chapter = accdoc.t_chapter and acc.t_account = accdoc.t_account and acc.t_code_currency = accdoc.t_currency and acc.t_close_date = '01.01.0001')";
              account = TRsbDataSet(QueryStr);

              while(account.MoveNext)
                      rest = Money(0);

                      rest = abs(RestA(account.account, {curdate}, NULL, account.chapter, account.currency)) +
                              abs(RestA(account.account, date(31,12,9999), NULL, account.chapter, account.currency));

                      if (rest > 0)
                          fx_error("Не нулевой остаток на счете " + account.account);
                          retVal = false;
                          break;
                      elif (MC_FindAndCloseAccount (account.code, fd, {curdate}, account.firole, account.currency, MC_ACC_CLOSE_INDIVIDUAL))
                          fx_error("Невозможно закрть счет " + account.account);
                          retVal = false;
                          break;
                      else
                          retVal = true;
                      end;
              end;
          else
              retVal = true;
          end;
      end;
  end;

  return retVal;
end;

FX_NameMacroFile = "fxclose.mac";
