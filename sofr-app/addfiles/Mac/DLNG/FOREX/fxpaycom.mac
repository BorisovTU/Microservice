/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v5.1
                 Copyright (c) R-Style Software Lab

  Имя файла        : fxpaycom.mac

  Библиотека       : FOREX

  Описание         : Оплата обязательств

  Программист      : Kalashnikov Michael (KMV)

  Создан           : 7.12.2001
-------------------------------------------------------------------------*/
import DealsInter, OprInter, PaymInter, fxlb, fxcarry, fxstep;

MACRO FX_PayCommitment(fd)
  var CurComm, SumComm, CurReq, SumReq, Purpose,
      TranModeCom = 0, KvitMode = 0, TypeOrder = "",
      stat = 0, InegrtMode = false, paym;

    if (fd.leg.rec.DVP)
        Purpose = FX_GetPaymentPurpose(0, fd.leg.rec.LegID, FX_IsSale(fd.tick.rec.TypeDoc));
        paym = RsbPayment( fd.tick.rec.BofficeKind, fd.tick.rec.DealID, Purpose, 0);

        if ((paym.PaymStatus == PM_PREPARING)or
            (paym.PaymStatus == PM_READIED))

            fx_error("Сначала должны быть исполнены требования");
            return false;
        end;
    end;

    GetRegistryValue("КОНВЕРСИОННЫЕ ОПЕРАЦИИ\\ИСПОЛЬЗОВАНИЕ ТС\\ИСПОЛНЕНИЕ ОБЯЗАТЕЛЬСТВ",
                        V_INTEGER, TranModeCom, stat);
    if (not stat)
        GetRegistryValue("КОНВЕРСИОННЫЕ ОПЕРАЦИИ\\РЕЖИМ КВИТОВКИ",
                            V_INTEGER, KvitMode, stat);
    end;
    if (not stat)
        GetRegistryValue("COMMON\\WORK_MODE\\INTEGRATED", 
                            V_BOOL, InegrtMode, stat);
    end;
    if (stat)
        fx_error("Ошибка при получении значений реестра");
        return false;
    end;

    fd.SetCurrency(CurComm, CurReq);
    fd.SetSums(SumComm, SumReq);

    // Актуализация платежа по оплате обязательств
    Purpose = FX_GetPaymentPurpose(1, fd.leg.rec.LegID, FX_IsSale(fd.tick.rec.TypeDoc));
    paym = RsbPayment( fd.tick.rec.BofficeKind, fd.tick.rec.DealID, Purpose, 0);
    if ( paym.PaymentID == 0 )
        fx_error("Не найден плановый платеж по оплате обязательства");
        return false;
    end;
    paym.Actuate();

    fd.SetGround(FALSE, paym);

    if (paym.PaymStatus == PM_CLOSED_W_M_MOVEMENT) //Произошел неттинг
        return true;
    end;

    if (CheckTYPEACCOUNT(paym.PayerAccount, "А"))
        TypeOrder = "p";
    elif (CheckTYPEACCOUNT(paym.ReceiverAccount, "А"))
        TypeOrder = "r";
    else
        TypeOrder = "";      
    end;

    if ((InegrtMode)and(TypeOrder != ""))
        if (FX_CreateCashOrder(paym, TypeOrder))
            paym.PaymStatus = PM_READY_TO_SEND;
        else
            fx_error("Не могу сформировать кассовый ордер");
            return false;
        end;
    else
        if ((TranModeCom !=FX_TRAN_BEFOREPAYMENT) and (TranModeCom !=FX_TRAN_NOTUSE))
            fx_error("Указанный режим исполнения обязательств не реализован");
            return false;
        end;
    
        if (bAND(KvitMode, FX_KVIT_COM))
            fx_error("Режим квитовки обязательств не реализован");
            return false;
        end;

        if ((paym.IsExternal())and
              (not fd.PartyIsMarket)and(TranModeCom == FX_TRAN_NOTUSE))
            paym.PaymStatus = PM_READY_TO_SEND;
            paym.IsFactPaym = "X";
        else
            if (not FX_Carry(1, CurComm, paym.FuturePayerAccount, paym.FutureReceiverAccount,
                             SumComm, {curdate}, 0, 
                             paym.Ground, paym.PaymentID))
                fx_error("Не могу выполнить проводку \"Исполнение обязательства по сделке\"");
                return false;
            end;

            paym.PaymStatus = PM_FINISHED;
            paym.IsFactPaym = "X";
        end;  
    end;
 
    return true;
END;
FX_NameMacroFile = "fxpaycom.mac";
