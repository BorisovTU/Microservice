/************************************************************************/
/*         Автоматизированная банковская система RS-Bank v5.0		*/
/*                 Copyright (c) R-Style Software Lab 1998		*/
/*									*/
/*  Модуль           : Сделки						*/
/*									*/
/*  Имя файла        : fxprint.mac					*/
/*									*/
/*  Описание         : Печать паспорта сделки				*/
/*									*/
/*  Программист      : Антонов А.А.					*/
/*									*/
/*  Создан           : 17.11.98						*/
/*									*/
/************************************************************************/
IMPORT PaymInter, DealsInter, FIInter, OprInter, fxlb, dlreport;

FILE DL_TICK("dl_tick");
FILE PMDLV("pmdlvk");
FILE PERSON("person");
FILE PARTCODE("partcode");
FILE PARTY("party");
FILE RG_APP("rg_app","rsgate.def");
FILE DL_LEG("dl_leg");

RECORD PaymBAi("pmpaym");
RECORD PaymCAi("pmpaym");
RECORD PaymBRi("pmpaym");
RECORD PaymCRi("pmpaym");
RECORD PaymINi("pmpaym");
RECORD PaymPRi("pmpaym");

RECORD Leg("dl_leg");
RECORD RevLeg("dl_leg");

ARRAY  Payment;

Payment(BAi) = PaymBAi;
Payment(CAi) = PaymCAi;
Payment(BRi) = PaymBRi;
Payment(CRi) = PaymCRi;
Payment(INi) = PaymINi;
Payment(PRi) = PaymPRi;

ClearRecord(Payment(BAi));
ClearRecord(Payment(CAi));
ClearRecord(Payment(BRi));
ClearRecord(Payment(CRi));
ClearRecord(Payment(INi));
ClearRecord(Payment(PRi));
ClearRecord(Leg);
ClearRecord(RevLeg);

CONST FW_RATEi = 1,
      BW_RATEi = 2,
      PR_RATEi = 3;

/*****************************************************************/
/* Функция преобразования курса в строку со сдвигом точки        */
/*****************************************************************/
MACRO ПолучитьСтрокуКурса(Курс, Точность)

VAR СтрокаКурса = String(Курс);
VAR Ip  = Index(СтрокаКурса, ".");
VAR Len = StrLen(СтрокаКурса);

  /* Убираем точку */
  СтрокаКурса = SubStr(СтрокаКурса, 1, Ip - 1) + SubStr(СтрокаКурса, Ip + 1, Len - Ip);

  /* Устанавливаем точку */
  Len = StrLen(СтрокаКурса);
  While (Len <= Точность)
    СтрокаКурса = "0" + СтрокаКурса;
    Len = StrLen(СтрокаКурса);
    end;

  СтрокаКурса = SubStr(СтрокаКурса, 1, Len - Точность) + "." + SubStr(СтрокаКурса, Len - Точность + 1, Точность);

  return СтрокаКурса;
END;


/************************************************************************/
/* Получить платеж сделки						*/
/************************************************************************/
MACRO GetPayment(BofficeKind, DealID, Purpose, PaymentBuff, FICode, DeliverySymbol, DeliveryName)

  if (FindPayment(0, Purpose, 0, BofficeKind, DealID, TRUE, PaymentBuff))
    MsgBox(String("Не найден платеж сделки с назначением ", Purpose));
    return;
    end;

  SetParm(4, ПолучитьКодФинИн(PaymentBuff.FIID));

  if (PaymentBuff.DeliveryKind)
    PMDLV.DeliveryKind = PaymentBuff.DeliveryKind;
    if (GetEQ(PMDLV))
      SetParm(5, "X");
      SetParm(6, PMDLV.ShortName);
      else
      MsgBox(String("Не найден способ доставки банкнот ", PaymentBuff.DeliveryKind));
      end;
    end;

END;

PRIVATE MACRO Rate(legpar)
  return legpar.Price/(pow(10, legpar.Point));
END;

/************************************************************************/
/* Печать паспорта сделки						*/
/************************************************************************/
MACRO PrintTicket(DealID)

  VAR Title		= "",
      TradeSystem	= "",
      Operation		= "",
      OperationRev	= "",
      CallPut		= "",
      PartyCode		= "",
      PartyName		= "",
      BrokerCode	= "",
      BrokerName	= "",
      ClientCode	= "",
      ClientName	= "",
      TraderCode    = "",
      TraderName	= "",
      Origin		= "",
      CurUp		= "",
      CurDn		= "";

  ARRAY FICode, DeliveryName, DeliverySymbol, RateValue, Comment;

  VAR i;
  
  /* Чистим переменные */
  i = 1;
  While (i < SYSPAYMENTS)
    FICode(i) = DeliveryName(i) = DeliverySymbol(i) = "";
    i = i + 1;
    end;

  /* Ищем сделку */
  DL_TICK.DealID = DealID;
  if (not GetEQ(DL_TICK))
    MsgBox(String("Не найдена сделка с DealID = ", DealID));
    return;
    end;

  DL_LEG.LegKind = LEG_KIND_DL_TICK;
  DL_LEG.DealID = DealID;
  DL_LEG.LegID = DL_LEGID_BASE;
  if (not GetEQ(DL_LEG))
    MsgBox(String("Не найден транш сделки с DealID = ", DealID));
    return;
    end;

  Copy(Leg, DL_LEG);
  

  /* Если сделка не конверсионная, то выход */
  if (DL_TICK.BofficeKind != DL_FOREXDOC)
    MsgBox(String("Сделка не является конверсионной"));
    return;
    end;

  /* Определение заголовка */
  if (IsCONVERS(DL_TICK.DealGroup))	Title = " Конверсионная операция ";
    elif (IsSWAP(DL_TICK.DealGroup))	Title = " Конверсионная сделка \"SWAP\" ";
    elif (IsOPTION(DL_TICK.DealGroup))	Title = " Конверсионный внебиржевой опцион ";
    elif (IsFUTURES(DL_TICK.DealGroup))	Title = " Конверсионная индексная сделка ";
    elif (IsMOVING(DL_TICK.DealGroup))	Title = " Одновалютная банкнотная сделка ";
    else
    MsgBox(String("Неизвестный тип сделки ", DL_TICK.DealGroup));
    return;
    end;

  if (FX_IsSale(DL_TICK.TypeDoc))
    Operation    = "ПРОДАЖА";
    else
    Operation    = "ПОКУПКА";
    end;

  if (IsSWAP(DL_TICK.DealGroup))
    if (FX_IsSale(DL_TICK.TypeDoc))
      OperationRev = "ПОКУПКА";
      else
      OperationRev = "ПРОДАЖА";
      end;
    end;

  if (IsOPTION(DL_TICK.DealGroup))
    if (IsPUT(DL_TICK.DealGroup))
      CallPut = "PUT";
      else
      CallPut = "CALL";
      end;
    end;

  /* Поиск торговой системы */
  TradeSystem = DL_GetCodeKindName( DL_TICK.TradeSystem );
  if (TradeSystem == "")
     MsgBox(String("Не найдена торговая система"));
  end;

  /* Поиск контрагента */
  if (DL_TICK.PartyID != /*ALLPARTY*/-1)
    PARTY.PartyID = DL_TICK.PartyID;
    if (GetEQ(PARTY))
      PartyName = PARTY.ShortName;
      PARTCODE.PartyID  = DL_TICK.PartyID;
      PARTCODE.CodeKind = DL_TICK.TradeSystem;
      if (GetEQ(PARTCODE))
        PartyCode = PARTCODE.Code;
        end;
      else
      MsgBox(String("Не найден контрагент ", DL_TICK.PartyID));
      end;
    end;
      
  /* Поиск брокера */
  if (DL_TICK.BrokerID != /*ALLPARTY*/-1)
    PARTY.PartyID = DL_TICK.BrokerID;
    if (GetEQ(PARTY))
      BrokerName = PARTY.ShortName;
      PARTCODE.PartyID  = DL_TICK.BrokerID;
      PARTCODE.CodeKind = DL_TICK.TradeSystem;
      if (GetEQ(PARTCODE))
        BrokerCode = PARTCODE.Code;
        end;
      else
      MsgBox(String("Не найден брокер", DL_TICK.BrokerID));
      end;
    end;
      
  /* Поиск клиента */
  if (DL_TICK.ClientID != /*ALLPARTY*/-1)
    PARTY.PartyID = DL_TICK.ClientID;
    if (GetEQ(PARTY))
      ClientName = PARTY.ShortName;
      PARTCODE.PartyID  = DL_TICK.ClientID;
      PARTCODE.CodeKind = DL_TICK.TradeSystem;
      if (GetEQ(PARTCODE))
        ClientCode = PARTCODE.Code;
        end;
      else
      MsgBox(String("Не найден клиент ", DL_TICK.ClientID));
      end;
    end;

  /* Поиск дилера */
  if (DL_TICK.TraderID != /*ALLPARTY*/-1)
    PARTY.PartyID = DL_TICK.TraderID;
    if (GetEQ(PARTY))
      TraderName = PARTY.ShortName;
      PARTCODE.PartyID  = DL_TICK.TraderID;
      PARTCODE.CodeKind = DL_TICK.TradeSystem;
      if (GetEQ(PARTCODE))
        TraderCode = PARTCODE.Code;
      end;
    else
      MsgBox(String("Не найден дилер ", DL_TICK.TraderID));
    end;
  end;

  /* Чтение платежей */
  GetPayment(DL_TICK.BofficeKind, DL_TICK.DealID, BAi, Payment(BAi), FICode(BAi), DeliverySymbol(BAi), DeliveryName(BAi));
  GetPayment(DL_TICK.BofficeKind, DL_TICK.DealID, CAi, Payment(CAi), FICode(CAi), DeliverySymbol(CAi), DeliveryName(CAi));

  if (IsSWAP(DL_TICK.DealGroup))
    GetPayment(DL_TICK.BofficeKind, DL_TICK.DealID, BRi, Payment(BRi), FICode(BRi), DeliverySymbol(BRi), DeliveryName(BRi));
    GetPayment(DL_TICK.BofficeKind, DL_TICK.DealID, CRi, Payment(CRi), FICode(CRi), DeliverySymbol(CRi), DeliveryName(CRi));
    DL_LEG.LegKind = LEG_KIND_DL_TICK;
    DL_LEG.DealID = DealID;
    DL_LEG.LegID = DL_LEGID_REV;
    if (not GetEQ(DL_LEG))
      MsgBox(String("Не найден транш сделки с DealID = ", DealID));
      return;
      end;

    Copy(RevLeg, DL_LEG);
    
    end;

  if (IsOPTION(DL_TICK.DealGroup))
    GetPayment(DL_TICK.BofficeKind, DL_TICK.DealID, PRi, Payment(PRi), FICode(PRi), DeliverySymbol(PRi), DeliveryName(PRi));
    end;

  /* Поиск происхождения */
  if ((DL_TICK.originID == 0) or (DL_TICK.originID == 1))
    Origin = String("Внутренняя");
    else 
    RG_APP.ApplicationID = DL_TICK.OriginID;
    if (GetEQ(RG_APP))
      Origin = RG_APP.ApplicationName;
      else
      MsgBox(String("Не найдено происхожение сделки ", DL_TICK.OriginID));
      end;
    end;


  /* Определение курса */
  RateValue(FW_RATEi) = ПолучитьСтрокуКурса(ConvSumRate(Payment(BAi).Amount, Payment(CAi).Amount, DL_TICK.Scale, DL_TICK.Points, DL_TICK.RevRate), DL_TICK.Points);

  if (IsSWAP(DL_TICK.DealGroup))
    RateValue(BW_RATEi) = ПолучитьСтрокуКурса(ConvSumRate(Payment(BRi).Amount, Payment(CRi).Amount, DL_TICK.Scale, DL_TICK.Points, DL_TICK.RevRate), DL_TICK.Points);
    end;

  if (IsOPTION(DL_TICK.DealGroup))
    RateValue(PR_RATEi) = ПолучитьСтрокуКурса(ConvSumRate(Payment(BAi).Amount, Payment(PRi).Amount, DL_TICK.Scale, DL_TICK.Points, DL_TICK.RevRate), DL_TICK.Points);
    end;

  /* Определяем размерность курса */
  if (not DL_TICK.RevRate)
    CurUp = FICode(BAi);
    CurDn = FICode(CAi);
  else
    CurUp = FICode(CAi);
    CurDn = FICode(BAi);
  end;

  Comment(1) = SubStr(DL_TICK.Comment,  1, 64);
  Comment(2) = SubStr(DL_TICK.Comment, 65, 64);

  /* Рисуем тикет */

[╔══════════════════════════════════════#═══════════════════════════════════════╗](Title:c);
[║                                                                              ║];
[║ Торг.система #################### N в бэк-офисе    ######################### ║](TradeSystem, DL_TICK.DealCode);
[║ Дата сделки  ##########           N в торг.системе ######################### ║](DL_TICK.DealDate:f, DL_TICK.DealCodeTS);
[║                                                                              ║];

  if (IsCONVERS(DL_TICK.DealGroup) or IsFUTURES(DL_TICK.DealGroup))

[║──Операция─Актив─────────────Сумма──────────Банкноты───────────Валютирование──║];
[║  Ид. N ####################                                                  ║](Leg.LegNumber);
[║  #######  #####   ####################     [#] ###############  ##########   ║](Operation, FICode(BAi), Payment(BAi).Amount:0:2:a, DeliverySymbol(BAi), DeliveryName(BAi), Payment(BAi).ValueDate:f);
[║                                                                              ║];
[║           #####   ####################     [#] ###############  ##########   ║](           FICode(CAi), Payment(CAi).Amount:0:2:a, DeliverySymbol(CAi), DeliveryName(CAi), Payment(CAi).ValueDate:f);
[║                                                                              ║];
[║                                                                              ║];
[║  Курс ########## ###/###  Масшт. ######  Точн. #  Обр.кот.[#]                ║](Rate(Leg):r, CurUp, CurDn, Leg.Scale, Leg.Point, DL_TICK.RevRate);
[║                                                                              ║];
[║                                                                              ║];

    elif (IsSWAP(DL_TICK.DealGroup))

[║──Операция─Актив─────────────Сумма──────────Банкноты───────────Валютирование──║];
[║  Ид. N ####################                                                  ║](Leg.LegNumber);
[║  #######  #####   ####################     [#] ###############  ##########   ║](Operation, FICode(BAi), Payment(BAi).Amount:0:2:a, DeliverySymbol(BAi), DeliveryName(BAi), Payment(BAi).ValueDate:f);
[║           #####   ####################     [#] ###############  ##########   ║](           FICode(CAi), Payment(CAi).Amount:0:2:a, DeliverySymbol(CAi), DeliveryName(CAi), Payment(CAi).ValueDate:f);
[║  Курс ########## ###/###  Масшт. ######  Точн. #  Обр.кот.[#]                ║](Rate(Leg):r, CurUp, CurDn, Leg.Scale, Leg.Point, DL_TICK.RevRate);
[║                                                                              ║];
[║──Операция─Актив─────────────Сумма──────────Банкноты───────────Валютирование──║];
[║  Ид. N ####################                                                  ║](RevLeg.LegNumber);
[║  #######  #####   ####################     [#] ###############  ##########   ║](OperationRev, FICode(BRi), Payment(BRi).Amount:0:2:a, DeliverySymbol(BRi), DeliveryName(BRi), Payment(BRi).ValueDate:f);
[║           #####   ####################     [#] ###############  ##########   ║](              FICode(CRi), Payment(CRi).Amount:0:2:a, DeliverySymbol(CRi), DeliveryName(CRi), Payment(CRi).ValueDate:f);
[║  Курс ########## ###/###  Масшт. ######  Точн. #  Обр.кот.[#]                ║](Rate(RevLeg):r, CurUp, CurDn, RevLeg.Scale, RevLeg.Point, DL_TICK.RevRate);
[║                                                                              ║];

    elif (IsOPTION(DL_TICK.DealGroup))

[║──Вид опциона─Актив───────────Сумма─────────Банкноты───────────Валютирование──║];
[║  #######     #####   ####################  [#] ###############  ##########   ║](CallPut, FICode(BAi), Payment(BAi).Amount:0:2:a, DeliverySymbol(BAi), DeliveryName(BAi), Payment(BAi).ValueDate:f);
[║              #####   ####################  [#] ###############  ##########   ║](         FICode(CAi), Payment(CAi).Amount:0:2:a, DeliverySymbol(CAi), DeliveryName(CAi), Payment(CAi).ValueDate:f);
[║  Курс ########## ###/###  Масшт. ######  Точн. #  Обр.кот.[#]                ║](RateValue(FW_RATEi):r, CurUp, CurDn, DL_TICK.Scale, DL_TICK.Points, DL_TICK.RevRate);
[║                                                                              ║];
[║──Операция────Актив───────────Премия───────────────────────────Валютирование──║];
[║  #######     #####   ####################  [#] ###############  ##########   ║](Operation, FICode(PRi), Payment(PRi).Amount:0:2:a, DeliverySymbol(PRi), DeliveryName(PRi), Payment(PRi).ValueDate:f);
[║  Курс ########## ###/###  Масшт. ######  Точн. #  Обр.кот.[#]                ║](RateValue(PR_RATEi):r, CurUp, CurDn, DL_TICK.Scale, DL_TICK.Points, DL_TICK.RevRate);
[║                                                                              ║];
[║                                                                              ║];

    end;

[║──────────────────────────────────────────────────────────────────────────────║];
[║ Контрагент ########## ########################### Сист.тип  ################ ║](PartyCode, PartyName, DL_TICK.TypeDoc);
[║ Брокер     ########## ########################### Польз.тип ################ ║](BrokerCode, BrokerName, DL_TICK.UserTypeDoc);
[║ Клиент     ########## ########################### Поручение ####             ║](ClientCode, ClientName, DL_TICK.IndocID);
[║ Дилер      ########## ########################### Пачка     ####             ║](TraderCode, TraderName, DL_TICK.NumberPack);
[║ Филиал     ####                           Происхождение #################### ║](DL_TICK.Department, Origin);
[║ Комментарий ################################################################ ║](Comment(1));
[║             ################################################################ ║](Comment(2));
[╚══════════════════════════════════════════════════════════════════════════════╝];

END;
