 /*
 $Name: fxcateg.mac 
 $Module: Конверсионные операции
 $Description: Процедуры получения параметров документов при работе с категориями учета в БО КО 
 */
IMPORT  BankInter, DealsInter, CTInter, PTInter, OprInter, Календарь, fxlb, globals, mccatacf;

CONST
    MC_TYPE_USPARAMETR_ISMARKET   = 1,
    MC_TYPE_USPARAMETR_ISCLIENT   = 2,
    MC_TYPE_USPARAMETR_ISRESIDENT = 3,
    MC_TYPE_USPARAMETR_DEALTYPE   = 4,
    MC_TYPE_USPARAMETR_ISSWAP     = 5;

/*********************************************************************************/
/* базовый класс - первичные документы для работы с категориями учета            */ 
/*********************************************************************************/
CLASS FXFirstDoc(parm1, parm2, parm3)
    var
        tick   = TBfile("dl_tick.dbt"),
        leg    = TBfile("dl_leg.dbt"),
        genagr = TBFile("dl_genagr.dbt"),
        payment = TRecHandler("pmpaym");;

    private var
        CategU  = "",
        withGA  = false;

    private var 
        ParmA        = TArray(), /* массив значений параметров 
                                    Доступ только через GetParametr*/
        ParmB        = TArray(), /* массив значений пользовательских параметров 
                                    Доступ только через GetParametr*/
        FIRoleBArray = TArray(); /* массив базисных ролей ФИ */

    var
       Error = 0,       /* Номер ошибки. Используется для анализа ошибок в ф-ях категорий*/
       Kind  = 0,       /* вид первичного документа*/
       ID    = 0;       /* ID первичного документа*/

/////////////////////////////////////////////////////////////////////////////////
    MACRO GetTick()
        return tick.rec;
    END;

    MACRO GetLeg()
        return leg.rec;
    END;

    MACRO GetGenAgr()
        return genagr.rec;
    END;

  PRIVATE MACRO IsPartyResident(PartyID)
    var party = TBFile("party");
    party.KeyNum = 0;
    party.rec.PartyID = PartyID;
    if (GetEQ(party))
      if (party.rec.NotResident)
        return false;
      else
        return true;
      end;
    end;
  END;

  /*настроить обязательства и требования
    Валюта обязательств, сумма обязательств, валюта требований, сумма требований*/
    MACRO SetSums(Comm, Req)
      if (FX_IsSale(tick.rec.TypeDoc))
        if (not leg.rec.LegID)
          Comm = leg.rec.Principal;
          Req = leg.rec.Cost;
        else
          Req = leg.rec.Principal;
          Comm = leg.rec.Cost;
        end;
      else
        if (not leg.rec.LegID)
          Comm = leg.rec.Cost;
          Req = leg.rec.Principal;
        else
          Req = leg.rec.Cost;
          Comm = leg.rec.Principal;
        end;
      end;
      SetParm(1, Comm);
      SetParm(2, Req);
    END;
  
    MACRO SetCurrency(CurComm, CurReq)
        if (FX_IsSale(tick.rec.TypeDoc))
            if (not leg.rec.LegID)
                CurComm = leg.rec.PFI;
                CurReq = leg.rec.CFI;
            else
                CurReq = leg.rec.PFI;
                CurComm = leg.rec.CFI;
            end;
        else
            if (not leg.rec.LegID)
                CurComm = leg.rec.CFI;
                CurReq = leg.rec.PFI;
            else
                CurReq = leg.rec.CFI;
                CurComm = leg.rec.PFI;
            end;
        end;

        SetParm(1, CurComm);
        SetParm(2, CurReq);
    END;

    MACRO ReInit(_withGenAgr)
        ParmA[MC_TYPE_PARAMETR_PLACE]                = {OurBank};
        ParmA[MC_TYPE_PARAMETR_ISSUER]               = -1;
        ParmA[MC_TYPE_PARAMETR_CENTR]                = -1;
        ParmA[MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE]  = 0;

        if (Kind == DL_FXLEG) // режим по сделке
            ParmA[MC_TYPE_PARAMETR_DOCKIND]     = DL_FXLEG;
            ParmA[MC_TYPE_PARAMETR_DOCID]       = leg.rec.ID;
            ParmA[MC_TYPE_PARAMETR_CURRENCY]    = ALLFININSTR;
            ParmA[MC_TYPE_PARAMETR_PAYCURRENCY] = leg.rec.CFI;
            ParmA[MC_TYPE_PARAMETR_NUMBER]      = leg.rec.LegNumber;
            ParmA[MC_TYPE_PARAMETR_CONTRACTOR]  =
            ParmA[MC_TYPE_PARAMETR_OWNER]       =
            ParmA[MC_TYPE_PARAMETR_PARTY]       = tick.rec.PartyID;
            ParmA[MC_TYPE_PARAMETR_CLIENT]      = tick.rec.ClientID;
            ParmA[MC_TYPE_PARAMETR_BEGDATE]     = leg.rec.Start;
            ParmA[MC_TYPE_PARAMETR_VALDATE]     = leg.rec.Maturity;
            ParmA[MC_TYPE_PARAMETR_DEPARTMENT]  = tick.rec.Department;

            if (ВидСубъекта(tick.rec.PartyID, PTK_MARKETPLASE) == true)
                ParmA[MC_TYPE_PARAMETR_MARKET_PLACE] = tick.rec.PartyID;
                ParmB[MC_TYPE_USPARAMETR_ISMARKET]  = true;
            else
                ParmB[MC_TYPE_USPARAMETR_ISMARKET]  = false;
            end;

            if (ВидСубъекта(tick.rec.PartyID, PTK_CLIENT) == true)
                ParmB[MC_TYPE_USPARAMETR_ISCLIENT]  = true;
            else
                ParmB[MC_TYPE_USPARAMETR_ISCLIENT]  = false;
            end;

            ParmB[MC_TYPE_USPARAMETR_ISRESIDENT] = IsPartyResident(tick.rec.PartyID);

            if (leg.rec.Maturity <= GetDateAfterWorkDays(leg.rec.Start, 2))
                ParmB[MC_TYPE_USPARAMETR_DEALTYPE] = DL_SPOTDEAL;
            elif (leg.rec.Maturity > GetDateAfterWorkDays(leg.rec.Start, 2))
                ParmB[MC_TYPE_USPARAMETR_DEALTYPE] = DL_FORWARDDEAL;
            end;

            if (_withGenAgr)
                ParmA[MC_TYPE_PARAMETR_CONTRACTOR]  =
                ParmA[MC_TYPE_PARAMETR_OWNER]       =
                ParmA[MC_TYPE_PARAMETR_PARTY]       = genagr.rec.PartyID;
                ParmA[MC_TYPE_PARAMETR_NUMBER]      = genagr.rec.CodeInAccount; // Code
                ParmA[MC_TYPE_PARAMETR_DEPARTMENT]  = genagr.rec.Department;
            end;
        else // режим по ген.соглашению // пользовательские параметры заполняются руками
            ParmA[MC_TYPE_PARAMETR_PAYCURRENCY] =
            ParmA[MC_TYPE_PARAMETR_CURRENCY]    = ALLFININSTR;
            ParmA[MC_TYPE_PARAMETR_BEGDATE]     = genagr.rec.Start;
            ParmA[MC_TYPE_PARAMETR_FINDATE]     = 
            ParmA[MC_TYPE_PARAMETR_VALDATE]     = genagr.rec.Start + Int(genagr.rec.Duration);
            ParmA[MC_TYPE_PARAMETR_CLIENT]      = -1;
            ParmA[MC_TYPE_PARAMETR_CONTRACTOR]  =
            ParmA[MC_TYPE_PARAMETR_OWNER]       =
            ParmA[MC_TYPE_PARAMETR_PARTY]       = genagr.rec.PartyID;
            ParmA[MC_TYPE_PARAMETR_DOCKIND]     = genagr.rec.DocKind;
            ParmA[MC_TYPE_PARAMETR_DOCID]       = genagr.rec.GenAgrID;
            ParmA[MC_TYPE_PARAMETR_NUMBER]      = genagr.rec.CodeInAccount; //.Code;
            ParmA[MC_TYPE_PARAMETR_DEPARTMENT]  = genagr.rec.Department;

            if (ВидСубъекта(genagr.rec.PartyID, PTK_MARKETPLASE) == true)
                ParmA[MC_TYPE_PARAMETR_MARKET_PLACE] = genagr.rec.PartyID;
                ParmB[MC_TYPE_USPARAMETR_ISMARKET]  = true;
            else
                ParmB[MC_TYPE_USPARAMETR_ISMARKET]  = false;
            end;

            if (ВидСубъекта(genagr.rec.PartyID, PTK_CLIENT) == true)
                ParmB[MC_TYPE_USPARAMETR_ISCLIENT]  = true;
            else
                ParmB[MC_TYPE_USPARAMETR_ISCLIENT]  = false;
            end;

            ParmB[MC_TYPE_USPARAMETR_ISRESIDENT] = IsPartyResident(genagr.rec.PartyID);
        end;
    END;

    MACRO GetParametr(_ParmKind, _OperDate, _CatCode, _FIRole, _CurryCurrency)
        var Parametr = ParmA[_ParmKind],
            CommCur, ReqCur;

        if (Parametr == NULL)
            if (_ParmKind == MC_TYPE_PARAMETR_FIID)
                SetCurrency(CommCur, ReqCur);
                if (_FIRole == this.GetFIRoleByCategory(_CatCode, _CurryCurrency, FIROLE_FIREQ))
                    Parametr = ReqCur;
                elif (_FIRole == this.GetFIRoleByCategory(_CatCode, _CurryCurrency, FIROLE_FICOM))
                    Parametr = CommCur;
                elif (_CatCode == "СчКорреспГлаваГ" )
                    if (_FIRole == FIROLE_CORACC_ACTIVE)
                        Parametr = ReqCur;
                    elif (_FIRole == FIROLE_CORACC_PASSIVE )
                        Parametr = CommCur;
                    end;
                else  
                    Parametr = -1;
                end;
            elif (_ParmKind == MC_TYPE_PARAMETR_FINDATE ) /* базовая дата сделки */
                 if (_FIRole == this.GetFIRoleByCategory(_CatCode, _CurryCurrency, FIROLE_FIREQ)) 
                     if( (tick.rec.Flag1 == "X") OR 
                         (tick.rec.Flag2 == "X") )
                         Parametr = leg.rec.Expiry;
                     else
                         Parametr = leg.rec.Maturity;
                     end
                 elif (_FIRole == this.GetFIRoleByCategory(_CatCode, _CurryCurrency, FIROLE_FICOM)) 
                     if( (tick.rec.Flag1 == "X") OR 
                         (tick.rec.Flag2 == "X") )
                         Parametr = leg.rec.Maturity;
                     else
                         Parametr = leg.rec.Expiry;
                     end
                 end	   
            else
              Parametr = -1;
            end;
        end;

        CategU = _CatCode;

        return Parametr;
    END;

    MACRO SetParametr(_ParmKind, _Val)
       ParmA[_ParmKind] = _Val;
    END;

    MACRO GetUsParametr(_ParmKind)
        return ParmB[_ParmKind];
    END;

    MACRO SetUsParametr(_ParmKind, _Val)
       ParmB[_ParmKind] = _Val;
    END;

    /*инициализация массива ролей ФИ, в классах наследниках может переопределяться*/
    MACRO InitFIRoleBArray()
        FIRoleBArray[0] = FIROLE_FIREQ;
        FIRoleBArray[1] = FIROLE_FICOM;
        FIRoleBArray[2] = FIROLE_RVP;
        FIRoleBArray[3] = FIROLE_CORACC_ACTIVE;
        FIRoleBArray[4] = FIROLE_CORACC_PASSIVE;
    END;

    MACRO GetFIRoleBArray()
        return FIRoleBArray;
    END;

    MACRO GetBasisFIRole(_FIRole)
        var i = 0, CommCur, ReqCur;

        if (_FIRole == FIROLE_UNDEF)
            return FIROLE_FIREQ;
        elif (_FIRole > 1000) // счета по ген. соглашениям ведутся в этом диапазоне
            return _FIRole;
        end;

        while (i < FIRoleBArray.Size)
            if (FIRoleBArray[i] == _FIRole)
                return _FIRole;
            end;
            i = i + 1;
        end;

        SetCurrency(CommCur, ReqCur);
        if( _FIRole == FIROLE_BA )
            if( ReqCur == NatCur )
                return FIROLE_FICOM;
            else 
                return FIROLE_FIREQ;
            end    
        end;
        if(_FIRole == FIROLE_CA )
            if( ReqCur == NatCur )
                return FIROLE_FIREQ;
            else 
                return FIROLE_FICOM;
            end    
        end;
        if( _FIRole == FIROLE_INCOME )
            if( ReqCur == NatCur )
                return FIROLE_FICOM;
            else 
                return FIROLE_FIREQ;
            end    
        end;
        if( _FIRole == FIROLE_EXPEND )
            if( CommCur == NatCur )
                return FIROLE_FIREQ;
            else 
                return FIROLE_FICOM;
            end    
        end;
        Error = 1;

        return FIROLE_UNDEF;
    END;

    /*Получить параметры шаблона*/
    MACRO GetParametrTemplate
    ( 
        _ObjectID,       /*   параметр - номер справочника                 */
        _Classificator,  /*   классификатор - номер классификатора, если он задан */
        _OperDate,       /*   дата, на которую надо вернуть характеристики*/
        _FIRole
    )         
        var
            Parametr = -1, /*По умолчанию */
            CurComm = 0, CurReq = 0,
            fininstr = TRecHandler("fininstr.dbt");

        if (ValType(_OperDate) != V_DATE)
            _OperDate = {curdate};
        end;

        if ((_ObjectID == OBJTYPE_SIDEBALANCE) AND (_Classificator == LLCLASS_SIDEBALANCE_CORACCKIND))
            if (CategU == "СчКорреспГлаваГ")
                if(_FIRole == FIROLE_CORACC_ACTIVE)
                    Parametr = 1;/*актив*/
                else
                    Parametr = 2;
                end;
            else
                Parametr = 1;
            end;
        /*Вид ФИ - Вид актива банка*/
        elif (_ObjectID == OBJTYPE_KINDFI ) 

             SetCurrency(CurComm, CurReq);
             if (_Classificator == LLCLASS_KIND_AKT_BANK)
                 Parametr = 1; /*денежные средства*/
             elif (_Classificator == LLCLASS_KIND_AKT_REQ)
                 ПолучитьФинИн(CurReq, fininstr);
                 if (fininstr.rec.FI_Kind == FIKIND_METAL)
                    parametr = 2;
                 else 
                    parametr = 1;
                 end;
             elif (_Classificator == LLCLASS_KIND_AKT_COMM)
                 ПолучитьФинИн(CurComm, fininstr);
                 if (fininstr.rec.FI_Kind == FIKIND_METAL) 
                    parametr = 2;
                 else 
                    parametr = 1;
                 end;
             end
        elif ((_ObjectID == OBJTYPE_RESIDENT) AND (_Classificator == LLCLASS_RESD_CONTRACTOR))
            if (GetUsParametr(MC_TYPE_USPARAMETR_ISRESIDENT))
                Parametr = 1;
            else
                Parametr = 2;
            end;
        elif ((_ObjectID == OBJTYPE_KINDCLCB) AND (_Classificator == LLCLASS_CATEG_CONTR_FOREX))
            if (GetUsParametr(MC_TYPE_USPARAMETR_ISMARKET))
                Parametr = 3;
            elif (GetUsParametr(MC_TYPE_USPARAMETR_ISCLIENT))
                Parametr = 1;
            end;
        elif ((_ObjectID == OBJTYPE_SECTORMARKT) AND (_Classificator == LLCLASS_SECTR_MARKET_BANK_OP))
            Parametr = 2;
        elif ((_ObjectID == OBJTYPE_BACKOFFICE) AND (_Classificator == LLCLASS_BACKOFFICE))
            Parametr = 3;
        elif ((_ObjectID == OBJTYPE_KINDRESERV) AND (_Classificator == LLCLASS_KINDRESERV_CONTR))
            Parametr = 7;
        elif ((_ObjectID == OBJTYPE_PRODUCT) AND (_Classificator == LLCLASS_PRODUCT_FOREX))
            if (GetUsParametr(MC_TYPE_USPARAMETR_ISSWAP) == NULL)
                if (IsSWAP(GetOperationGroup(tick.rec.DealType)))
                    Parametr = 9;
                end;
            else
                Parametr = GetUsParametr(MC_TYPE_USPARAMETR_ISSWAP)
            end;
        elif ((_ObjectID == OBJTYPE_BOOLEAN) AND (_Classificator == LLCLASS_EXCHANGE))
            if (GetUsParametr(MC_TYPE_USPARAMETR_ISMARKET))
                Parametr = 1;
            else
                Parametr = 0;
            end;
        elif ((_ObjectID == OBJTYPE_BOOLEAN) AND (_Classificator == LLCLASS_FORWARD))
            if (ParmB[MC_TYPE_USPARAMETR_DEALTYPE] == DL_FORWARDDEAL)
                Parametr = 1;
            else
                Parametr = 0;
            end;
        elif ((_ObjectID == OBJTYPE_IST_DOH_RASH) AND (_Classificator == LLCLASS_KIND_COURCE_DIF))
            Parametr = 1;
        end;

        return Parametr;
    END;

    /*Макрос корректировки открываемого счета. Account, accblnc, ORScheme можно менять*/
    MACRO CorrectAccount(_account_, _accblnc_, _ORScheme_, categ, templ, accdoc,  OperDate)
        return true;
    END;

    PRIVATE MACRO GetPeriodNumber(_FiRole)
        var
           per = 1, 
           BegDate, FinDate, Delta;

        BegDate = FinDate = GetParametr(MC_TYPE_PARAMETR_BEGDATE);

        if (_FIRole == FIROLE_FIREQ) 
            if( (tick.rec.Flag1 == "X") OR 
                (tick.rec.Flag2 == "X") )
                FinDate = leg.rec.Expiry;
            else
                FinDate = leg.rec.Maturity;
            end;
        elif (_FIRole == FIROLE_FICOM) 
            if( (tick.rec.Flag1 == "X") OR 
                (tick.rec.Flag2 == "X") )
                FinDate = leg.rec.Maturity;
            else
                FinDate = leg.rec.Expiry;
            end;
        end;	   

        Delta = FinDate - BegDate;

        if (Delta == 0)
            per = 1;
        end;

        if (Delta == 1)
            per = 2;
        end;

        if (Delta >= 2)
            per = 3;
        end;

        if (Delta >= 8)
            per = 4;
        end;

        if (Delta >=31)
            per = 5;
        end;

        if (Delta >= 91)
            per = 6;
        end;

        return per;
    END;

    MACRO GetFIRoleByCategory(_Categ, _FIID, _FiRole)
        var
            FiRole = _FiRole, rs = NULL;

        if (
            (
             (_Categ == "+Форвард, дрейф")or(_Categ == "-Форвард, дрейф")
            ) and
            (
             (
              (Kind == DL_FXLEG)      and
              (withGA)                and 
              (tick.rec.GenAgrID > 0) and
              (genagr.rec.AccMode == 2)
             ) or
             (Kind == 4610)
            )
           )
            if (ValType(FiRole) != V_INTEGER)
                FiRole = FIROLE_FIDOC;
            end;

            FiRole = String(FiRole) +
                     String(GetPeriodNumber(FiRole)) + 
                     String(_FIID);

            FiRole = Int(FiRole);
            while (FiRole < 1000) 
                FiRole = FiRole * 10;
            end;
        end;

        return FiRole;
    END;

    /* Получить счет по категории учета
       Вернет false, если ошибка или отказ от ввода счета.
       ! если не установлен NoErrMes выдаст сообщение об ошибке
    */
    PRIVATE MACRO GetAccountByCatCode
    ( 
        _CatCode,     // код категории
        FindAccount_, // здесь вернется номер счета
        /*необязательные параметры*/
        _AccCreate,   // признак верификации\открытия лицевого счета
        _NoErrMes,     // признак молчаливого выполнения - ошибку не выдаем
        _FIRole,      // роль ФИ
        AccBuf_,      // буфер для возврата информации по найденному\открытому счету
        _ActionDate,  // дата
        _FIID         // валюта
    )
       var IsMass;

        FindAccount_  = "";

        if (Error != 0)
            return false;
        end;

        if (((_NoErrMes == null)or(_NoErrMes == false)) and 
            (not isOprMultiExec()) 
           )
            IsMass = 0;
        else /*либо массовый режим, либо принудительно просим не орать ошибки*/
            IsMass = 1;
        end;

        if( ValType(_AccCreate) == V_UNDEF )
            _AccCreate = MC_OPENACC_CREATE;
        end;

        if( _AccCreate != 4 )
            FindAccount_ = MC_FindAndOpenAccountEx(
                                                   _CatCode,
                                                   this,
                                                   _ActionDate,
                                                   IsMass,
                                                   _AccCreate,
                                                   AccBuf_,
                                                   _FIID,
                                                   NULL, NULL, NULL,
                                                   _FIRole
                                                  );
        else
            FindAccount_ = MC_GetAccountNumber(
                                               _CatCode, this,_ActionDate,
                                               IsMass, _FIID, _FIRole
                                              );
        end;

        if (FindAccount_ == "")
            if (IsMass == 0)
                 msgbox("Счет категории \"" + _CatCode + "\" неопределен.");
            end;  

            return false;
        end;

        SetParm(2, FindAccount_);

        return true;
    END;

    /*актуализация счета (без открытия)*/  
    MACRO ActualAccount(_CatCode, FindAccount_, _NoErrMes, _FIRole, AccBuf_, _ActionDate, _FIID)
        var retVal, Account;
        retVal = GetAccountByCatCode(_CatCode, Account, MC_OPENACC_ACT, _NoErrMes, _FIRole, AccBuf_, _ActionDate, _FIID);
        SetParm(2, Account); 
        return retVal;
    END;

    /*Актуализация с проверкой срочности*/  
    MACRO ActualCheckPeriod(_CatCode, FindAccount_, _NoErrMes, _FIRole, AccBuf_, _ActionDate, _FIID)
        var retVal, Account;                                          
        retVal = GetAccountByCatCode(_CatCode, Account, MC_OPENACC_CHECKPERIOD, _NoErrMes, _FIRole, AccBuf_, _ActionDate, _FIID);
        SetParm(2, Account); 
        return retVal;
    END;  

    /*открытие и актуализация счета*/  
    MACRO OpenAccount(_CatCode, FindAccount_, _NoErrMes, _FIRole, AccBuf_, _ActionDate, _FIID)
        var retVal, Account;      
        retVal = GetAccountByCatCode(_CatCode, Account, MC_OPENACC_CREATE, _NoErrMes, _FIRole, AccBuf_, _ActionDate, _FIID);
        SetParm(2, Account); 
        return retVal;
    END;  

    /*переоткрытие и актуализация счета - если счет нашли, его все равно пересоздать*/  
    MACRO ReOpenAccount(_CatCode, FindAccount_, _NoErrMes, _FIRole, AccBuf_, _ActionDate, _FIID)
        var retVal, Account;      
        retVal = GetAccountByCatCode(_CatCode, Account, MC_OPENACC_RECREATE, _NoErrMes, _FIRole, AccBuf_, _ActionDate, _FIID);
        SetParm(2, Account); 
        return retVal;
    END;

    /*аннулирование счета по категории*/
    MACRO CloseAccount(_CatCode, _ActionDate, _FIRole, _FIID, _NoErrMes )
        var retVal;
        retVal = MC_FindAndCloseAccount(_CatCode, this, _ActionDate, _FIRole, _FIID);
        if ((retVal != 0)and((_NoErrMes == null)or(_NoErrMes == false)))
           msgbox("Ошибка при закрытии счета по категории ", _CatCode);
        end;

        if( retVal != 0 )
           return false; 
        else
           return true; 
        end;      
    END;

    /*проверка на существование счета*/  
    MACRO IsExistAccount(_CatCode, FindAccount_, _NoErrMes, _FIRole, AccBuf_, _ActionDate, _FIID)
        var retVal, Account;      
        retVal = GetAccountByCatCode(_CatCode, Account, MC_OPENACC_CHECKEXIST, _NoErrMes, _FIRole, AccBuf_, _ActionDate, _FIID);
        SetParm(2, Account); 
        return retVal;
    END;  

    /*проверка на актуализацию счета*/  
    MACRO IsActAccount(_CatCode, FindAccount_, _NoErrMes, _FIRole, AccBuf_, _ActionDate, _FIID)
        var retVal, Account;      
        retVal = GetAccountByCatCode(_CatCode, Account, MC_OPENACC_CHECKACT, _NoErrMes, _FIRole, AccBuf_, _ActionDate, _FIID);
        SetParm(2, Account); 
        return retVal;
    END;  

    /*получить - найти или сформировать номер счета (без открытия)*/  
    MACRO FindNumberAccount(_CatCode, FindAccount_, _NoErrMes, _FIRole, _FIID)
        var retVal, Account;
        retVal = GetAccountByCatCode(_CatCode, Account, 4, _NoErrMes, _FIRole, NULL, NULL, _FIID);
        SetParm(2, Account); 
        return retVal;
    END;

    MACRO GetRestAccount(_CatCode, _Date, _FIID, _Chapter, _FIRole)
        var 
            retVal = Money(0), Account;

        if (_FIRole == NULL)
            _FIRole = GetFIRoleByCategory(_CatCode, _FIID)
        end;

        if (IsExistAccount(_CatCode, Account, NULL, _FIRole, NULL, _Date, _FIID))
            retVal = RestA(Account, _Date, NULL, _Chapter, _FIID);
        end;

        return Money(abs(retVal));
    END;

    MACRO PartyIsMarket()
        return GetUsParametr(MC_TYPE_USPARAMETR_ISMARKET);
    END;
	
    MACRO PartyIsClient()
        return GetUsParametr(MC_TYPE_USPARAMETR_ISCLIENT);
    END;
	
    MACRO PartyIsResident();
        return GetUsParametr(MC_TYPE_USPARAMETR_ISRESIDENT);
    END;
	
    MACRO DealType()
        return GetUsParametr(MC_TYPE_USPARAMETR_DEALTYPE);
    END;

  private MACRO GetDateStep( RQorCM )
    var purp;
    purp = FX_GetPaymentPurpose( RQorCM, leg.rec.LegID, FX_IsSale(tick.rec.TypeDoc));
    if( FindPayment(0, purp, 0, tick.rec.BofficeKind, tick.rec.DealID, TRUE, payment) )
       MsgBox(String("Не найден платеж сделки с назначением ", purp));
//     return date(0,0,0);
    end;
    return payment.rec.ValueDate;
  END;

  PRIVATE MACRO Get1stPlanPaym( Purpose, payms )
  var 
      Ok, stat = true;
  
      payms.KeyNum = 1;
      payms.rec.DocKind = tick.rec.BofficeKind;
      payms.rec.DocumentID = tick.rec.DealID;
      payms.rec.Purpose = Purpose;
      payms.rec.SubPurpose = 0;
      payms.AddFilter("t_DocKind = "+tick.rec.BofficeKind+
                 " AND t_DocumentID = "+ tick.rec.DealID+
                 " AND t_Purpose = "+Purpose);
      Ok = payms.GetGE;
      while(Ok) 
        if(payms.rec.IsPlanPaym == "X")
           stat = false;
           Ok = false;
        else
           Ok = payms.next; /* ищем дальше */
        end;
  
      end;
      payms.DropFilter();
  
      return stat;
  END;
  
  private macro calcperiod( OperDateID, type, retval, period )
  var stat = 0, base, contr, CurComm, CurReq, purp,
      payms = TBfile("pmpaym.dbt");
  
       retval = date( 0, 0, 0 );
       SetCurrency(CurComm, CurReq);
           if( OperDateID == FX_OPERDATE_PERIOD ) 
               base = BAi;
               contr = CAi;
           elif( OperDateID == FX_OPERDATE_PERIOD_REV )
               base = BRi;
       	       contr = CRi;
           end;
       if( type )
           stat = MC_GetPeriodForCurrAcc("-Форвард, дрейф", 
                                         DL_FXLEG, id, FIROLE_FICOM, CurComm, 
                                         MC_PERIOD_PREV, false, period, 
                                         GetParametr(MC_TYPE_PARAMETR_DEPARTMENT));
           purp = contr;    
       else    
           stat = MC_GetPeriodForCurrAcc("+Форвард, дрейф", 
                                         DL_FXLEG, id, FIROLE_FIREQ, CurReq, 
                                         MC_PERIOD_PREV, false, period, 
                                         GetParametr(MC_TYPE_PARAMETR_DEPARTMENT));
           purp = base;    
       end;    
       if( not stat )
           stat = Get1stPlanPaym( purp, payms );
           if( not stat )
               retval = {curdate} + (payms.rec.ValueDate - MC_CalcPeriodBound( {curdate}, period, false ));
           end;
       end;     
       SetParm( 3, retval );
       return stat;
  end;

  /*вернуть дату очередного переноса сделки по счетам срочности*/
  MACRO GetOperDate(OperDateID, PrevPeriod)
    var stat = 0, mindate, nextdate,
        period = TRecHandler("mcperiod"),
        period1 = TRecHandler("mcperiod");

    if (OperDateID == FX_OPERDATE_DEAL)
      return leg.rec.Start;
    elif( OperDateID == FX_OPERDATE_DEALCLOSE )
      return leg.rec.Expiry;
    elif( (OperDateID == FX_OPERDATE_PERIOD) OR (OperDateID == FX_OPERDATE_PERIOD_REV) )
      if (GetUsParametr(MC_TYPE_USPARAMETR_DEALTYPE) == DL_FORWARDDEAL)
        /*надо искать дату окончания пред. периода срочности*/
        stat = calcperiod( OperDateID, 0, mindate, period ); /* Req */ 
        if( not stat )
            stat = calcperiod( OperDateID, 1, nextdate, period1 ); /* Com */
            if( mindate > nextdate ) 
                mindate = nextdate;
                Copy(period, period1);
            end;    
            if(ValType(PrevPeriod) == V_STRUC) 
               Copy(PrevPeriod, period);
            end;    
        end;
        if( stat )
            Error = stat;
        end;    
        return mindate;
      else
        return leg.rec.Maturity;
      end
    elif( (OperDateID == FX_OPERDATE_VAL) OR
          (OperDateID == FX_OPERDATE_VAL_REV) )
          return leg.rec.Maturity;
    elif( (OperDateID == FX_OPERDATE_VAL_RQ) OR
          (OperDateID == FX_OPERDATE_VAL_RQ_REV) )
          return GetDateStep(0);
    elif( (OperDateID == FX_OPERDATE_VAL_CM) OR
          (OperDateID == FX_OPERDATE_VAL_CM_REV) )
          return GetDateStep(1);
    end;
  END;
  
  /*Заполнить основание платежа*/
  MACRO SetGround( ReQ, paym:RsbPayment )
  VAR Ccy = "";

      paym.Ground = "Погашение ";
      if( ReQ )
          paym.Ground = paym.Ground + "требований";
      else
          paym.Ground = paym.Ground + "обязательств";
      end;  
      paym.Ground = paym.Ground + " по конверсионной сделке (";
      FX_GetFinInstrParams( leg.rec.PFI, Ccy );
      paym.Ground = paym.Ground + Ccy;
      paym.Ground = paym.Ground + "/";
      FX_GetFinInstrParams( leg.rec.CFI, Ccy );
      paym.Ground = paym.Ground + Ccy;
      paym.Ground = paym.Ground + ") от ";
      paym.Ground = paym.Ground + String(paym.ValueDate);
  END;

// Constructor
    if ((ValType(parm1) == V_INTEGER) and
        (ValType(parm2) == V_INTEGER)   and
        (parm1 == DL_FXLEG))
        //Конструктор из DocKind, DocID
        leg.KeyNum = 1;
        leg.rec.ID = parm2;
        if (leg.GetEQ)
          tick.rec.DealID = leg.rec.DealID;
          if (not tick.GetEQ)
            return NULL;
          end;
        else
            return NULL;
        end;

        if (tick.rec.GenAgrID > 0)
            genagr.rec.GenAgrID = tick.rec.GenAgrID;
            if (not genagr.GetEQ)
                return NULL;
            end;
        end;

        Kind = parm1;
        Id   = parm2;
    elif ((ValType(parm1) == V_INTEGER) and
          (ValType(parm2) == V_INTEGER)   and
          (parm1 == 4610)) // Ген. Соглашение КО 
        //Конструктор из DocKind, DocID

         genagr.rec.GenAgrID = parm2;
         if (not genagr.GetEQ)
             return NULL;
         end;

        Kind = parm1;
        Id   = parm2;
    else
        //Конструктор из структур, всегда в режиме сделки
        copy (tick,parm1);
        copy (leg,parm2);

        Kind = DL_FXLEG;
        Id   = leg.rec.ID;
    end;

    if (ValType(parm3) == V_BOOL)
        withGA = parm3;
    else
        withGA = false;
    end;

    ReInit(false);
    InitFIRoleBArray();

    if ((Kind == DL_FXLEG)     and
        (tick.rec.GenAgrID > 0) and
        (genagr.rec.AccMode == 2))
        ReInit(withGA);
    end;
END;
