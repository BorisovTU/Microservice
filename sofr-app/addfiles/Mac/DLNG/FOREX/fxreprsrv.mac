/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style Software Lab

  File Name   : mmreprsrv.mac  
  Description : Отчет о выполнении операции резервирования 
  Comment     : 
└───────────────────────────────────────────────────────────────────────────*/
import RsbDataSet, CTInter, fxlb, mccatacf;


private var OprDate;

PRIVATE MACRO ПолучитьСчет
(   
    CatCode,       /* код категории */
    fd,
    FindAccount ,  /* здесь вернется номер счета */    
    AccDate       /*Дата, в которой искать счет*/     
)
    FindAccount = MC_FindAndOpenAccountEx (
                     CatCode,
                     fd,
                     AccDate,
                     isOprMultiExec,
                     MC_OPENACC_CREATE);

   if (FindAccount=="")
        return false;
   end;

   SetParm( 2, FindAccount );
   return true;
    
END;    

PRIVATE MACRO РассчитатьБазовуюСумму(tick, leg,NomVal:@money, MarketVal:@money, BaseVal:@money)

var
	fi_from = TRecHandler("fininstr.dbt"),
   KursPFI = $1,
   KursCFI = $1;

	MarketVal = $0;
   BaseVal = $0;
   NomVal = $0;

	if(leg.rec.PFI != NATCUR)
   // конвертация
   	if(ConvSum( KursPFI, KursPFI, OprDate, leg.rec.PFI, NATCUR )  != 0 )
      	ПолучитьФинИн(leg.rec.PFI, fi_from);
      	return SayReserveError( tick, 55, string("Не найден курс валюты ", fi_from.rec.ISO_Number,
                                                     " к нац. валюте за ", OprDate) );
      end;
	end;
	  
	if(leg.rec.CFI != NATCUR)
   // конвертация
      if(ConvSum( KursCFI, KursCFI, OprDate, leg.rec.CFI, NATCUR )  != 0 )
       	ПолучитьФинИн(leg.rec.CFI, fi_from);
         return SayReserveError( tick, 55, string("Не найден курс валюты ", fi_from.rec.ISO_Number,
                                                     " к нац. валюте за ", OprDate) );
      end;
	end;	  


if (tick.rec.TypeDoc == "S")
  if ((leg.rec.PFI != NATCUR) and (leg.rec.CFI == NATCUR))
    NomVal = leg.rec.Cost;
    MarketVal = leg.rec.Principal * KursPFI;
	BaseVal = MarketVal - NomVal;
  end;
  if ((leg.rec.PFI == NATCUR) and (leg.rec.CFI != NATCUR))
    NomVal = leg.rec.Cost * KursCFI;
    MarketVal = leg.rec.Principal;
	BaseVal = MarketVal -NomVal;
  end;
  if ((leg.rec.PFI != NATCUR) and (leg.rec.CFI != NATCUR))
    NomVal = leg.rec.Cost * KursCFI;
    MarketVal = leg.rec.Principal * KursPFI;
	BaseVal = MarketVal -NomVal;
  end;
end;

if (tick.rec.TypeDoc == "B")
  if ((leg.rec.PFI != NATCUR) and (leg.rec.CFI == NATCUR))
    NomVal = leg.rec.Cost;
    MarketVal = leg.rec.Principal * KursPFI;
	BaseVal = NomVal -MarketVal;
  end;
  if ((leg.rec.PFI == NATCUR) and (leg.rec.CFI != NATCUR))
    NomVal = leg.rec.Cost* KursCFI;
    MarketVal = leg.rec.Principal;
	BaseVal = NomVal - MarketVal;
  end;
  if ((leg.rec.PFI != NATCUR) and (leg.rec.CFI != NATCUR))
    NomVal = leg.rec.Cost * KursCFI;
    MarketVal = leg.rec.Principal * KursPFI;
	BaseVal = NomVal-MarketVal;
  end;
end;
	if (leg.rec.LegID == 1)
		BaseVal = -BaseVal;	
	end;
	return 0;
END; //РассчитатьБазовуюСумму


MACRO ЛС_УчетаРезерва(tick, leg)

var
   Категория,
   Счет,
   fd = FXFirstDoc(DL_FXLEG,leg.rec.ID, true);     

   Категория = "Резерв, договор";   

   if ( not ПолучитьСчет( Категория,  fd, Счет,  OprDate))
     return;
   else
     return Счет;
   end;

END;    

MACRO OldReservAmount(ДатаРасчетаРезерва, ParentID, ChildID, is_rev_part)
    
   var ResLnk = TRsbDataSet("SELECT t_ReserveAmount,  t_ReserveAmountSwap " +
                            " FROM ddlreslnk_dbt " +
                            " WHERE t_Type = 3 AND t_ParentID = " + ParentID + 
                            " AND t_ChildID <> -1 "+
                            " AND t_ChildID <> " + ChildID +
                            " AND t_ReserveDate <= "+ FX_ToDate(ДатаРасчетаРезерва) +
                            " ORDER BY t_ReserveDate DESC, t_Id DESC ");
   
   if (ResLnk.MoveNext())
	   if (is_rev_part)	
         return ResLnk.ReserveAmountSwap;
	   else 
	  	   return ResLnk.ReserveAmount;
	   end;
   else
      return 0;
   end;

END;    

MACRO СуммаКорректировки(Amount, OldAmount) 

   return (Amount-OldAmount);

END;    

MACRO printHeader(Код_Операции, Дата_Расчета)
 [  ];
 [  ];                                                         
 [  Номер операции  #####################           Дата расчета:  ########## ] (Код_Операции:l, Date(Дата_Расчета):c);
 [  Вид резерва:    Резервы по срочным сделкам с иностранной валютой                          ];
 [  ];
 [  Резервы по сделкам купли/продажи                                                       ];
 [┌────────────────────┬──────────────────┬──────────────────┬─────────────────────┬───────────┬──────────┬──────────────────┬────────────────────┬─────────────────────────┬───────────────────────┐];
 [│      № Сделки      │    Рыночная      │    Контрактная   │     Счет резерва    │ Категория │     %    │    Расчетная     │  Расчетная сумма   │ Ранее созданный резерв, │  Сумма корректировки, │]; 
 [│                    │    стоимость     │    стоимость     │                     │ качества  │  резерва │    база, ###     │  резерва,  ###     │           ###           │           ###         │]({CCYNatCur},{CCYNatCur},{CCYNatCur},{CCYNatCur});

END;   

MACRO printTableLine(Data)
var 
  leg  = TBfile("dl_leg.dbt"),
  leg_for_acc  = TBfile("dl_leg.dbt"), //используем для получения номера счета
  tick = TBfile("dl_tick.dbt"),
  flag = true,
  A3  = 0,
  A4  = 0,
  A10 = 0,
  A11 = 0,
  NomVal = 0,
  MarketVal = 0,
  BaseVal = 0,
  QualityCategory = 0,
  ReservePercent = 0,
  ReserveAmount = 0;

  while (flag)

   tick.rec.DealID = Data.ParentID;
   if (tick.GetEQ)   
      leg.rec.DealID = Data.ParentID;
      leg.rec.LegKind = 0;
      leg.rec.LegID = Data.LegID;
      if (leg.GetEQ)
         // для получения номера счета для СВОП подсовываем всегда первую часть
         leg_for_acc.rec.DealID = Data.ParentID;
         leg_for_acc.rec.LegKind = 0;
         leg_for_acc.rec.LegID = 0;
         leg_for_acc.GetEQ;
         A4 = ЛС_УчетаРезерва(tick, leg_for_acc); // получаем номер счета

         РассчитатьБазовуюСумму(tick, leg,@NomVal,@MarketVal,@BaseVal)
      else
         MsgBox("Не найден транш сделки ",Data.DealCode); 
      end;                               
   else
      MsgBox("Не найдена сделка № ",Data.DealCode);//По идее не должно наступить         
   end;   

	if (Data.LegID == 0)
		QualityCategory = Data.QualityCategory;	
		ReservePercent = Data.ReservePercent;
		ReserveAmount = Data.ReserveAmount;
		A10 = OldReservAmount(Data.ReserveDate, Data.ParentID, Data.DocumentID, false);
	else
		QualityCategory = Data.QualityCategorySwap;	
		ReservePercent = Data.ReservePercentSwap;
		ReserveAmount = Data.ReserveAmountSwap;
		A10 = OldReservAmount(Data.ReserveDate, Data.ParentID, Data.DocumentID, true);
	end;

	A11 = СуммаКорректировки(ReserveAmount, A10);
 if (A11 != 0) 
 [├────────────────────┼──────────────────┼──────────────────┼─────────────────────┼───────────┼──────────┼──────────────────┼────────────────────┼─────────────────────────┼───────────────────────┤];
 [│####################│##################│##################│#####################│###########│##########│##################│####################│#########################│#######################│]
   (Data.DealCode:r, MarketVal, NomVal, A4:z, QualityCategory:z, ReservePercent, abs(BaseVal),  ReserveAmount, A10, A11); 
 end;
   flag = Data.MoveNext();
 end;
END;  

MACRO printTableFooter

 [└────────────────────┴──────────────────┴──────────────────┴─────────────────────┴───────────┴──────────┴──────────────────┴────────────────────┴─────────────────────────┴───────────────────────┘];
END; 

MACRO RsRvPrintReport(commrec)

var   Data,
      query1;      

  RECORD comm( dl_comm );
  SetBuff( comm, commrec );

  OprDate = comm.CommDate;  

  query1 = 
    "select DDl_Comm_dbt.T_COMMCODE, DDl_Comm_dbt.T_DOCUMENTID, " +      
           "DDlResLnk_dbt.T_RESERVEDATE, DDl_Tick_dbt.T_DEALCODE, " +           
           "DParty_dbt.T_SHORTNAME, DDlResLnk_dbt.T_QUALITYCATEGORY, " + 
           "DDlResLnk_dbt.T_QUALITYCATEGORYSWAP, DDlResLnk_dbt.T_RESERVEPERCENTSWAP, " +
           "DDlResLnk_dbt.T_RESERVEPERCENT, DDl_Leg_dbt.T_PRINCIPAL, " +
           "DDlResLnk_dbt.T_RESERVEAMOUNTSWAP, DDlResLnk_dbt.T_RESERVEAMOUNT, " +
           "DDl_Leg_dbt.T_PFI, DDl_Leg_dbt.T_LegID, DDlResLnk_dbt.T_ParentID " +
                        
    "from   DDlResLnk_dbt, Ddl_Comm_dbt, DDl_Tick_dbt, " +
           "DParty_dbt, Ddl_Leg_Dbt " +            
                    
    "where  DDlResLnk_dbt.T_CHILDID = DDl_Comm_dbt.T_DOCUMENTID and " + 
           "DDlResLnk_dbt.T_PARENTID = DDl_Tick_dbt.T_DEALID and " +
           "DDl_Comm_dbt.T_PARTYID = DParty_dbt.T_PARTYID(+) and " + 
           "DDlResLnk_dbt.T_PARENTID = DDl_leg_dbt.T_DEALID and " +
           "DDL_Comm_dbt.T_DOCUMENTID = " + comm.DocumentID +
           " and DdlResLnk_dbt.T_ISMANUAL = '0' and " + 
           "DDl_leg_dbt.T_LEGKIND = 0";

  Data = TRsbDataSet(query1);
  
  if (Data.MoveNext())    
  
    printHeader (comm.CommCode, Data.ReserveDate);  
  
    printTableLine(Data);
    
    printTableFooter();
  else
    println( "Нет обработанных сделок операцией № ", comm.CommCode);
  end;
  
END;    

