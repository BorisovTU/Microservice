/*-------------------------------------------------------------------------
          Автоматизированная банковская система RS-Bank v5.1
                 Copyright (c) R-Style Software Lab

  Имя файла        : fxmove.mac

  Библиотека       : FOREX

  Описание         : Функция шага Перенос сделки по срокам

  Программист      : Kalashnikov Michael (KMV)

  Создан           : 4.12.2001
-------------------------------------------------------------------------*/
IMPORT DealsInter, fxlb, fxcarry, fxtransf, "dlreport.mac";

MACRO FX_MoveDealPeriods(fd)
  var CurComm, SumComm, CurReq, SumReq,
      Sum1 = 0, Sum2 = 0, 
      ВнебСчетУчетаТреб = "",
      ВнебСчетУчетаТребНов = "",
      ВнебСчетУчетаОбяз = "",
      ВнебСчетУчетаОбязНов = "",
      СчКорреспГлаваГ_1 = "", СчКорреспГлаваГ_2 = "",
      StepDate, ActionDate, ActivateDate;

  if (fd.DealType != DL_FORWARDDEAL)
    fx_error("Данная сделка не подлежит переносу по срокам");
    return false;
  elif(not FX_GetTransferActivateDate(fd, ActionDate, ActivateDate)) // Получаем даты
    return true; // ничего делать не надо
  end;

  fd.SetCurrency(CurComm, CurReq);
  fd.SetSums(SumComm, SumReq);

  if ((not FX_GetDocAccount("+Форвард, дрейф", fd, ВнебСчетУчетаТреб, NULL, ActionDate, CurReq, NULL, NULL, NULL, fd.GetFIRoleByCategory("+Форвард, дрейф", CurReq, FIROLE_FIREQ), ActivateDate)) OR
      (not FX_GetDocAccount("-Форвард, дрейф", fd, ВнебСчетУчетаОбяз, NULL, ActionDate, CurComm, NULL, NULL, NULL, fd.GetFIRoleByCategory("-Форвард, дрейф", CurComm, FIROLE_FICOM), ActivateDate)))
    return false;
  end;

  if ((not FX_GetDocAccount("+Форвард, дрейф", fd, ВнебСчетУчетаТребНов, MC_OPENACC_CHECKPERIOD, ActionDate, CurReq, NULL, NULL, NULL, fd.GetFIRoleByCategory("+Форвард, дрейф", CurReq, FIROLE_FIREQ), ActivateDate)) OR
      (not FX_GetDocAccount("-Форвард, дрейф", fd, ВнебСчетУчетаОбязНов, MC_OPENACC_CHECKPERIOD, ActionDate, CurComm, NULL, NULL, NULL, fd.GetFIRoleByCategory("-Форвард, дрейф", CurComm, FIROLE_FICOM), ActivateDate)))
    return false;
  end;
  if ((ВнебСчетУчетаТреб != ВнебСчетУчетаТребНов) or (ВнебСчетУчетаОбяз!=ВнебСчетУчетаОбязНов))
    if (КОРРЕСП_ГЛАВА_Г())
      if (ВнебСчетУчетаТреб != ВнебСчетУчетаТребНов)
        if (not FX_Carry(4, CurReq, ВнебСчетУчетаТребНов, ВнебСчетУчетаТреб,
                          SumReq, ActivateDate, 0, "Перенос требования по сроку", 0))
          fx_error("Не могу выполнить проводку \"Перенос требования по сроку\"");
          return false;
        end;
      end;
      if (ВнебСчетУчетаОбяз!=ВнебСчетУчетаОбязНов)
        if (not FX_Carry(4, CurComm, ВнебСчетУчетаОбяз, ВнебСчетУчетаОбязНов,
                          SumComm, ActivateDate, 0, "Перенос обязательства по сроку", 0))
          fx_error("Не могу выполнить проводку \"Перенос обязательства по сроку\"");
          return false;
        end;
      end;
    else
      if (
          (not FX_GetDocAccount("СчКорреспГлаваГ",  fd, СчКорреспГлаваГ_1, NULL, NULL, NatCur,  NULL, NULL, NULL, FIROLE_CORACC_ACTIVE))  OR
          (not FX_GetDocAccount("СчКорреспГлаваГ",  fd, СчКорреспГлаваГ_2, NULL, NULL, NatCur,  NULL, NULL, NULL, FIROLE_CORACC_PASSIVE))
         )
        return false;
      end;

      if (
          (ConvSum( Sum1, SumReq, ActivateDate, CurReq, NatCur) > 0)  OR
          (ConvSum( Sum2, SumComm, ActivateDate, CurComm, NatCur) > 0)
         )
          fx_error("Не могу выполнить проводку \"Перенос требования по сроку\"");
          return false;
      end;

      if (ВнебСчетУчетаТреб != ВнебСчетУчетаТребНов)
        if (
            (not FX_MultyCarry(CurReq, ВнебСчетУчетаТребНов, SumReq,
                                NatCur, СчКорреспГлаваГ_1, Sum1,
                                4, NULL, NULL,
                                ActivateDate, ActivateDate,
                                "Перенос требования по сроку", ActivateDate)) OR
            (not FX_MultyCarry(NatCur, СчКорреспГлаваГ_1, Sum1,
                                CurReq, ВнебСчетУчетаТреб, SumReq,
                                4, NULL, NULL,
                                ActivateDate, ActivateDate,
                                "Перенос требования по сроку", ActivateDate))
           )
            fx_error("Не могу выполнить проводку \"Перенос требования по сроку\"");
            return false;
        end;
      end;
      if (ВнебСчетУчетаОбяз!=ВнебСчетУчетаОбязНов)
        if (
            (not FX_MultyCarry(CurComm, ВнебСчетУчетаОбяз, SumComm,
                                NatCur, СчКорреспГлаваГ_2, Sum2,
                                4, NULL, NULL,
                                ActivateDate, ActivateDate,
                                "Перенос обязательства по сроку", ActivateDate)) OR
            (not FX_MultyCarry(NatCur, СчКорреспГлаваГ_2, Sum2,
                                CurComm, ВнебСчетУчетаОбязНов, SumComm,
                                4, NULL, NULL,
                                ActivateDate, ActivateDate,
                                "Перенос обязательства по сроку", ActivateDate))
           )
            fx_error("Не могу выполнить проводку \"Перенос обязательства по сроку\"");
            return false;
        end;
      end;
    end;
  else
    fx_error("На текущую дату перенос сделки не требуется");
    return false;
  end;
  return true;
END;

FX_NameMacroFile = "fxmove.mac";
