/*
$Name:             fxrepmov.mac
$Module:           КО, ПИ 
$Description:      Отчет: "ЖУРНАЛ ДВИЖЕНИЯ СРЕДСТВ" для подсистем КО и ПИ
*/
IMPORT RsbDataSet, DealsInter, "or_tpl_h.mac", Globals, dlreport, fxlb, dlsole, "dv_categ.mac";

PRIVATE CONST
    FRX = 12, // КО
    DV = 42,  // ПИ

    ALG_DV_KINDSTATE  = 7009, // статусы сделок\операций с ПИ
    ALG_DV_KINDDVDEAL = 7004, // Покупка \ Продажа \ Исполнение длинных позиций \ Исполнение коротких позиций
    ALG_DV_KINDOP     = 7010; // Вид операции: 1 - "Внебиржевой форвард", 2 - "Внебиржевой опцион"

private var Rep:CTemplateXLS = CTemplateXLS();

private VAR  requestMny, 
             LiabilityMny, 
             allRequest,
             allLiability,
             cur = TArray,
             ManyR = TArray,
             ManyL = TArray,
             IP; // для какой подсистемы выпускать отчет (КО или ПИ)

private macro RepDate(date)
var strdate = string(date:f);
    return strdate;
end;

private macro GetKindOperShortName(Kind_Operation:integer)
   var oprkoper = TRecHandler( "oprkoper.dbt" ),
       Name = "";

   if( DL_GetRecHandler( oprkoper, " SELECT * FROM doprkoper_dbt WHERE t_Kind_Operation = " + string(Kind_Operation) ) )
      Name = oprkoper.rec.ShortName;
   end;

   return Name;
end;

private MACRO InsTeil(CcyCur, Mny, type)
VAR index = 0;
    while( index < cur.size )
        if( cur(index) == CcyCur ) 
            if( type == "R" )        
                ManyR(index) = ManyR(index) + Mny;
            elif( type == "L" )
                ManyL(index) = ManyL(index) + Mny;
            end;
            return 0;
        end;
        index = index + 1;
    end;
    cur(index) = CcyCur;
    if( type == "R" )        
        ManyR(index) = Mny;
        ManyL(index) = $0;
    elif( type == "L" )
        ManyR(index) = $0;
        ManyL(index) = Mny;
    end;
END;

// Выплата обязательства(для ПИ)?
private MACRO DL_IsPaymentCommitment(PaymentID)
  var Payment:RsbPayment;
  Payment = RsbPayment(PaymentID);

  if( Payment.Payer == {OurBank} ) /*обязательство*/
     return true;
  end;

  return false; /*требование*/
END;

/********************************************************************************/
MACRO JournalMRDFxXLS(Ip_, PartyID, dtCurF, dtCurT, FIID,
                      DealType, dtDealF, dtDealT, sign)

  VAR reptime     = 0,
      fio         = "", tmp = NULL,
      index       = 0,
      DealID_prev = 0,
      dlTick_sql  = "",
      dlTick      = NULL,
      Head1       = "";

    InitProgress(-1, "", "");

    IP = Ip_;

    if( IP == FRX ) /*для КО*/
       Head1 = "ЖУРНАЛ ДВИЖЕНИЯ СРЕДСТВ ПО СДЕЛКАМ FOREX ОТ  ";
    elif( IP == DV ) /*для ПИ*/
       Head1 = "ЖУРНАЛ ДВИЖЕНИЯ СРЕДСТВ ";
    end;

    Rep.OpenTemplate("Report_fxmovlist.xls", false);
    Rep.Sheet.Rows.RowHeight = 20;

    var BodyLine = Rep.RegisterTable("BodyLine");
    var TialLine = Rep.RegisterTable("TialLine");

    LiabilityMny = $0;
    RequestMny = $0;

    Rep.SetValue_NameCell("StrHead",  "БЭК-ОФИС БАНКА " + {Name_Bank});    
    Rep.SetValue_NameCell("StrHead1", Head1 + RepDate({curdate})+" г. "+time());    
    Rep.SetValue_NameCell("kindOper", "'" + GetKindOperShortName(DealType));    
    Rep.SetValue_NameCell("Currency", "'" + DL_getCCode(FIID));
    Rep.SetValue_NameCell("CatContr", "' ");
    Rep.SetValue_NameCell("Ccontr",   "'" + DL_getPartyShortName(PartyID));
    Rep.SetValue_NameCell("DateOpen", "'" + string(dtDealF:f, "/", dtDealt:f));
    Rep.SetValue_NameCell("DateCur",  "'" + string(dtCurF:f, "/", dtCurT:f));    

    if( IP == FRX ) /*для КО*/
      dlTick_sql = 
      " SELECT party.t_ShortName ptsh, "
      "   leg.t_LegNumber, "
      "   tick.t_DealDate, "
      "   oprk.t_ShortName oprksh, "
      "   nvl(dealer.t_ShortName, ' ') dsh, "
      "   paym.t_ValueDate, "
      "   fi.t_Ccy, "
      "   paym.t_Amount, "
      "   tick.t_CloseDate, "
      "   tick.t_DealType, "
      "   paym.t_Purpose, "
      "   tick.t_DealID, "
      "   tick.t_TypeDoc, "
      "   paym.t_PaymentID "
      " FROM ddl_tick_dbt tick, "
      "   ddl_leg_dbt leg, "
      "   dparty_dbt party, "
      "   doprkoper_dbt oprk, "
      "   dparty_dbt dealer, "
      "   dpmpaym_dbt paym, "
      "   dfininstr_dbt fi "
      " WHERE     tick.t_DealID = leg.t_DealID "
      "   AND leg.t_LegKind = 0 "
      "   AND oprk.t_Kind_Operation = tick.t_DealType "
      "   AND tick.t_BofficeKind = 100 "
      "   AND tick.t_DealStatus > 0 "
      "   AND party.t_PartyID = tick.t_PartyID "
      "   AND dealer.t_PartyID = tick.t_TraderID "
      "   AND paym.t_DocKind = tick.t_BofficeKind "
      "   AND paym.t_DocumentID = tick.t_DealID "
      "   AND ( ( (t_Purpose IN (1, 2)) AND (leg.t_LegID = 0)) "
      "        OR ( (t_Purpose IN (3, 4)) AND (leg.t_LegID = 0))) "
      "   AND fi.t_FIID = paym.t_FIID ";

      if (PartyID > 0)
        dlTick_sql = dlTick_sql + " and tick.t_PartyID = " + PartyID;
      end;

      if (DealType > 0)
        dlTick_sql = dlTick_sql + " and tick.t_DealType = " + DealType;
      end;

      if (FIID >= 0)
        dlTick_sql = dlTick_sql + " and paym.t_FIID = " + FIID;
      end;

      if (dtDealF != date(0,0,0))
        dlTick_sql = dlTick_sql + " and tick.t_DealDate >= '" + RepDate(dtDealF) + "'";
      end;

      if (dtDealT != date(0,0,0))
        dlTick_sql = dlTick_sql + " and tick.t_DealDate <= '" + RepDate(dtDealT) + "'";
      end;

      if (dtCurF != date(0,0,0))
        dlTick_sql = dlTick_sql + " and paym.t_ValueDate >= '" + RepDate(dtCurF) + "'";
      end;

      if (dtCurT != date(0,0,0))
        dlTick_sql = dlTick_sql + " and paym.t_ValueDate <= '" + RepDate(dtCurT) + "'";
      end;

      dlTick_sql = dlTick_sql + " order by tick.t_DealID, leg.t_LegID, paym.t_PaymentID ";
    elif( IP == DV ) /*для ПИ*/
      dlTick_sql = 
      " ( SELECT 0 t_SortKind, " +
      "   party.t_ShortName ptsh, "
      "   nDeal.t_Code t_LegNumber, "
      "   nDeal.t_Date t_DealDate, "
      "   oprk.t_ShortName oprksh, "
      "   ' ' dsh, "
      "   paym.t_ValueDate, "
      "   fi.t_Ccy, "
      "   paym.t_Amount, "
      "   (CASE WHEN nDeal.t_State = 2 THEN "
      "        RSB_SECUR.RSI_GetOperCloseDate(nDeal.t_DocKind, nDeal.t_Id) "
      "      ELSE "
      "        TO_DATE ('01.01.0001', 'DD.MM.YYYY') "
      "   END) t_CloseDate, "
      "   nDeal.t_Kind t_DealType, "
      "   paym.t_Purpose, "
      "   nDeal.t_Id t_DealID, "
      "   paym.t_PaymentID "
      " FROM ddvndeal_dbt nDeal, "
      "   dparty_dbt party, "
      "   doprkoper_dbt oprk, "
      "   dpmpaym_dbt paym, "
      "   dfininstr_dbt fi, "
      "   DNAMEALG_DBT StateNA, DNAMEALG_DBT TypeNA, DNAMEALG_DBT DVKindNA, DDVNFI_DBT nFI, DDVNFI_DBT nFI_2, DSfContr_DBT ClientContr, DSfContr_DBT AgentContr "
      " WHERE "
      "   oprk.t_Kind_Operation = nDeal.t_Kind "
      "   AND party.t_PartyID = nDeal.t_Contractor "
      "   AND fi.t_FIID = paym.t_FIID "
      "   and(nDeal.t_id = paym.t_documentid) "
      "   and ClientContr.t_ID(+) = NVL(nDeal.t_ClientContr, 0) "
      "   and AgentContr.t_ID(+) = NVL(nDeal.t_AgentContr, 0) "
      "   and StateNA.t_iTypeAlg(+)= "+string(ALG_DV_KINDSTATE)+
      "   and StateNA.t_iNumberAlg(+)= nDeal.t_State "
      "   and TypeNA.t_iTypeAlg(+)= "+string(ALG_DV_KINDDVDEAL)+
      "   and TypeNA.t_iNumberAlg(+)= nDeal.t_Type "
      "   and DVKindNA.t_iTypeAlg(+)= "+string(ALG_DV_KINDOP)+
      "   and DVKindNA.t_iNumberAlg(+)= nDeal.t_DVKind "
      "   and nFI.t_DealID(+)= nDeal.t_ID "
      "   and nFI.t_Type(+)= "+string(DV_NFIType_BaseActiv)+
      "   and nFI_2.t_DealID(+)= nDeal.t_ID "
      "   and nFI_2.t_Type(+)= "+string(DV_NFIType_BaseActiv2)+
      "   and(paym.t_Purpose IN(" + string(BAi) + "," + string(BRi) + "," + string(CAi) + "," + string(CRi) + "," + string(PRi) + "," + string(PM_PURP_COMBROKER)  + "," + string(PM_PURP_COMMARKET) + "," + string(PM_PURP_COMISSUER) + "," +  string(PM_PURP_MARGA) + ","+ string(PM_PURP_TRUST_COMM) + "," + string(PM_PURP_GARANT) + "," + string(PM_PURP_INTERIM) + "))"
      "   and nDeal.t_Client <= 0 "
      "   and paym.t_DocKind = nDeal.t_DocKind "+
      "   and nDeal.t_DocKind != "+DL_DVFXDEAL+
      "   and DECODE (nDeal.t_Forvard, chr(88), -1, fi.t_FI_Kind) = 1 ";

      if (PartyID > 0)
        dlTick_sql = dlTick_sql + " and nDeal.t_Contractor = " + PartyID;
      end;

      if (DealType > 0)
        dlTick_sql = dlTick_sql + " and nDeal.t_Kind = " + DealType;
      end;

      if (FIID >= 0)
        dlTick_sql = dlTick_sql + " and paym.t_FIID = " + FIID;
      end;

      if (dtDealF != date(0,0,0))
        dlTick_sql = dlTick_sql + " and nDeal.t_Date >= '" + RepDate(dtDealF) + "'";
      end;

      if (dtDealT != date(0,0,0))
        dlTick_sql = dlTick_sql + " and nDeal.t_Date <= '" + RepDate(dtDealT) + "'";
      end;

      if (dtCurF != date(0,0,0))
        dlTick_sql = dlTick_sql + " and paym.t_ValueDate >= '" + RepDate(dtCurF) + "'";
      end;

      if (dtCurT != date(0,0,0))
        dlTick_sql = dlTick_sql + " and paym.t_ValueDate <= '" + RepDate(dtCurT) + "'";
      end;

      dlTick_sql = dlTick_sql +
      " ) UNION ALL ( "
      " SELECT 1 t_SortKind, "
      "   party.t_ShortName ptsh, "
      "   tick.t_Code LegNumber, "
      "   tick.t_Date, "
      "   oprk.t_ShortName oprksh, "
      "   nvl(dealer.t_ShortName, ' ') dsh, "
      "   paym.t_ValueDate, "
      "   fi.t_Ccy, "
      "   paym.t_Amount, "
      "   (CASE WHEN tick.t_State = 2 THEN "
      "        RSB_SECUR.RSI_GetOperCloseDate(tick.t_DocKind, tick.t_ID) "
      "      ELSE "
      "        TO_DATE ('01.01.0001', 'DD.MM.YYYY') "
      "   END) t_CloseDate, "
      "   tick.t_Kind t_DealType, "
      "   paym.t_Purpose, "
      "   tick.t_ID t_DealID, "
      "   paym.t_PaymentID "
      " FROM ddvndeal_dbt tick, "
      "   ddvnfi_dbt leg, "
      "   dparty_dbt party, "
      "   doprkoper_dbt oprk, "
      "   dparty_dbt dealer, "
      "   dpmpaym_dbt paym, "
      "   dfininstr_dbt fi "
      " WHERE tick.t_ID = leg.t_DealID "
      "   AND oprk.t_Kind_Operation = tick.t_Kind "
      "   AND tick.t_DocKind = " + DL_DVFXDEAL +
      "   AND tick.t_State > 0 "
      "   AND party.t_PartyID = tick.t_Contractor "
      "   AND dealer.t_PartyID(+) = tick.t_TraderID "
      "   AND paym.t_DocKind = tick.t_DocKind "
      "   AND paym.t_DocumentID = tick.t_ID "
      "   and paym.t_Purpose IN(" + string(BAi) + "," + string(BRi) + "," + string(CAi) + "," + string(CRi) + ")"
      "   AND fi.t_FIID = paym.t_FIID ";

      if (PartyID > 0)
        dlTick_sql = dlTick_sql + " and tick.t_Contractor = " + PartyID;
      end;

      if (DealType > 0)
        dlTick_sql = dlTick_sql + " and tick.t_Kind = " + DealType;
      end;

      if (FIID >= 0)
        dlTick_sql = dlTick_sql + " and paym.t_FIID = " + FIID;
      end;

      if (dtDealF != date(0,0,0))
        dlTick_sql = dlTick_sql + " and tick.t_Date >= '" + RepDate(dtDealF) + "'";
      end;

      if (dtDealT != date(0,0,0))
        dlTick_sql = dlTick_sql + " and tick.t_Date <= '" + RepDate(dtDealT) + "'";
      end;

      if (dtCurF != date(0,0,0))
        dlTick_sql = dlTick_sql + " and paym.t_ValueDate >= '" + RepDate(dtCurF) + "'";
      end;

      if (dtCurT != date(0,0,0))
        dlTick_sql = dlTick_sql + " and paym.t_ValueDate <= '" + RepDate(dtCurT) + "'";
      end;

      dlTick_sql = dlTick_sql + " ) order by t_SortKind, t_DealID, t_PaymentID ";
    end;

    dlTick = TRsbDataSet(dlTick_sql);

    while( dlTick.MoveNext )
        reptime = reptime + 1;
        UseProgress(reptime);

        LiabilityMny = $0;
        RequestMny = $0;

        if( reptime > 1 )
           BodyLine.AddStr();
           if (dlTick.DealID == DealID_prev)
               Rep.Sheet.Range("BodyLine").Borders(8).LineStyle = -4142;
           else
               Rep.Sheet.Range("BodyLine").Borders(8).LineStyle = 1;
               Rep.Sheet.Range("BodyLine").Borders(8).Weight    = 2;
           end;
        end;

        if( IP == FRX ) /*для КО*/
          if (not FX_IsPaymentCommitment(FX_IsSale(dlTick.TypeDoc), dlTick.Purpose))
              RequestMny = dlTick.Amount;
          else
              LiabilityMny = dlTick.Amount;
          end;
        elif( IP == DV ) /*для ПИ*/
          if (not DL_IsPaymentCommitment(dlTick.PaymentID))
              RequestMny = dlTick.Amount;
          else
              LiabilityMny = dlTick.Amount;
          end;
        end;

        Rep.SetValue_NameCell("T_A1", "'" + dlTick.PTSH );  
        Rep.SetValue_NameCell("T_A2", "'" + dlTick.LegNumber );      
        Rep.SetValue_NameCell("T_A3", "'" + RepDate(Date(dlTick.DealDate)));      
        Rep.SetValue_NameCell("T_A4", "'" + dlTick.OPRKSH );      
        Rep.SetValue_NameCell("T_A5", "'" + dlTick.DSH );
        Rep.SetValue_NameCell("T_A6", "'" + RepDate(Date(dlTick.ValueDate)));      
        Rep.SetValue_NameCell("T_A7", "'" + dlTick.Ccy);      
        if (RequestMny != 0)
            Rep.SetValue_NameCell("T_A8", Money(RequestMny) );      
            InsTeil(dlTick.Ccy, RequestMny, "R");
        end;
        if (LiabilityMny != 0)
            InsTeil(dlTick.Ccy, LiabilityMny, "L"); 
            Rep.SetValue_NameCell("T_A9", Money(LiabilityMny) );      
        end;
        Rep.SetValue_NameCell("T_A10", "'" + RepDate(Date(dlTick.CloseDate)));

        DealID_prev = dlTick.DealID;
    end;
    Rep.Sheet.Range("BodyLine").Borders(9).LineStyle = -4119;

    Rep.SetValue_NameCell("T_HEAD_B", "Итого  " );  

    if(sign) 
        Rep.SetValue_NameCell("Boss", "'"+{FIO_Boss} );
        НайтиНачальникаБО(2, @tmp, @fio );
        Rep.SetValue_NameCell("Boss2", "'"+fio ); 
        НайтиИсполнителя( @tmp, @fio );      
        Rep.SetValue_NameCell("Admin", "'"+fio ); 
    else
        Rep.Sheet.Range("Signature").Delete;
    end;   

    while( index < cur.size )
        if( ManyR(index) == $0 )
            allRequest = "";
        else
            allRequest = ManyR(index);
        end;
        if( ManyL(index) == $0 )
            allLiability = "";
        else
            allLiability = ManyL(index);
        end;

        Rep.Sheet.Range("InTial").Borders(8).LineStyle = 1;
        Rep.Sheet.Range("InTial").Borders(8).Weight    = 2;
        if (index > 0)
            TialLine.AddStr();
        end;

        Rep.SetValue_NameCell("T_B1", "'"+cur(index) );  
        Rep.SetValue_NameCell("T_B2", Money(allRequest ));  
        Rep.SetValue_NameCell("T_B3", Money(allLiability ));

        index = index + 1;
    end;
    Rep.Sheet.Range("InTial").Borders(8).LineStyle = 1;
    Rep.Sheet.Range("InTial").Borders(8).Weight    = 2;
    Rep.Sheet.Range("InTial").Borders(9).LineStyle = -4119;

    RemProgress();
    Rep.SaveAsTemplate("", true);
END;
