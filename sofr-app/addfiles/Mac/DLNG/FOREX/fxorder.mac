IMPORT OprInter, Globals, DlSole, DealsInter, BankInter, FXLB, dlreport;

const ALG_MM_NETTING = 2116; 

PRIVATE MACRO PartyName(ID)
  if (ID == 0)
    return {Name_Bank};
  end;
    var party = TBfile("party");
  party.KeyNum = 0;
  party.rec.PartyID = ID;
  if( not GetEq(party) )
    return ID;
  else
    return party.rec.Name;
  end;
END;

MACRO NameAlg(type, num)
    var alg = TBfile("namealg");
  alg.KeyNum = 0;
  alg.rec.iTypeAlg = type;
  alg.rec.iNumberAlg = num;
  if( not GetEq(alg) )
    return "";
  else
    return alg.rec.szNameAlg;
  end;
END;

PRIVATE MACRO Currency(PFI)
var Ccy = "";

  if(FX_GetFinInstrParams( PFI, Ccy ))
     return Ccy;
  end;

  return "___";
END;

MACRO PrintHead
    ARRAY TmpName;
            
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
   strsplit({Name_Bank}, TmpName, 50);
 [           В БУХГАЛТЕРИЮ КБ ##################################################] (TmpName(0));
   if (valType(TmpName(1)) == V_STRING)   
 [                            ##################################################] (TmpName(1));
   end;
 [             РАСПОРЯЖЕНИЕ № ________________             от  ########## ##### ] (date, time:5:t);
 [                                                                              ];
END;

PRIVATE MACRO PrintOF3(tick, Liability, RevDeal)
    private VAR pmpaym = TBfile("pmpaym"),
                pmrmprop = TBfile("pmrmprop"),
                pmprop = TBfile("pmprop"),
                corschem = TBfile("corschem"),
                party = TBfile("party"),
                partcode = TBfile("partcode");
      
    private VAR many, Ccy, ISONumber, FI_Code, summa, rcName, rcCodeKind, rcCode, rcINN,      
                rcAccount, rcBank, bankcodeKind, bankcode, corrAcc, corrBank,
                corrCodeKind, corrCode, nostroAccount, nostroBank, bicCode, purp,isFields = true,
                outnostroAccount, outnostroBank, outbicCode;     
 [Перечислите                                                                   ];
     pmpaym.KeyNum = 1;
     pmpaym.rec.DocKind = tick.BofficeKind;
     pmpaym.rec.DocumentID = tick.DealID;
     pmpaym.rec.Purpose = Fx_GetPaymentPurpose(Liability, RevDeal, FX_IsSale(tick.typedoc));
     if( GetEq(pmpaym) )
        many = pmpaym.rec.Amount;
        FX_GetFinInstrParams( pmpaym.rec.FIID, Ccy, ISONumber, FI_Code );
        summa = CurToStrAlt(many, NULL, NULL, ISONumber);
        rcCodeKind = DL_GetCodeKindName( pmpaym.rec.ReceiverCodeKind );
        rcCode = pmpaym.rec.ReceiverCode;
        rcAccount = pmpaym.rec.ReceiverAccount;
        pmrmprop.KeyNum = 0;
        pmrmprop.rec.PaymentID = pmpaym.rec.PaymentID;
        if( GetEq(pmrmprop) )
            rcName = pmrmprop.rec.ReceiverName;
            rcINN = pmrmprop.rec.ReceiverINN;
            rcBank = pmrmprop.rec.ReceiverBankName;
            corrBank = pmrmprop.rec.ReceiverCorrBankName;
        end;
        pmprop.KeyNum = 0;
        pmprop.rec.PaymentID = pmpaym.rec.PaymentID;
        if( Liability )
            pmprop.rec.DebetCredit = 1;
        else    
            pmprop.rec.DebetCredit = 0;
        end;
		if( GetEq(pmprop) )
            bankcodeKind = DL_GetCodeKindName( pmprop.rec.CodeKind );
            bankCode = pmprop.rec.BankCode;
            corrAcc = pmprop.rec.CorrAcc;
            corrCodeKind = DL_GetCodeKindName( pmprop.rec.CorrCodeKind );
            corrCode = pmprop.rec.CorrCode;
            corschem.KeyNum = 1;
            corschem.rec.Number = pmprop.rec.Corschem;
            corschem.rec.FIID = pmprop.rec.PayFIID;
            corschem.rec.FI_Kind = 1;
            if( GetEq(corschem) )
                if( corschem.rec.IsNostro == "X" )
                    nostroAccount = corschem.rec.Account;
                   	party.KeyNum = 0;
                    party.rec.PartyID = corschem.rec.CorrID;
                    if( GetEq(party))
                        nostroBank = party.rec.ShortName;
                        partcode.KeyNum = 0;
                        partcode.rec.PartyID = party.rec.PartyID;
                        partcode.rec.CodeKind = 3;
                        if( GetEq(partcode) )
                            bicCode = partcode.rec.Code;
			    isFields = false;
                        end;
                    end;
                end;
            end;
        end;
     end;

 [################### ###(###) #################################################](many, Ccy, FI_Code, summa);
 [в пользу                                                                      ];
 [################################################ ### ########## ИНН ##########](rcName, rcCodeKind, rcCode, rcINN);
 [на счет ######################### в банке ##################### ### ##########](rcAccount, rcBank, bankcodeKind, bankcode);
 [корсчет ######################### в банке ##################### ### ##########](corrAcc, corrBank,corrCodeKind, corrCode);
 [Средства поставить                                                            ];
 if (isFields== true) 
 	outnostroAccount=""; 
 	outnostroBank=""; 
 	outbicCode="";
 else
 	outnostroAccount=nostroAccount; 
 	outnostroBank=nostroBank; 
 	outbicCode=bicCode;
 end;
 [со счета ######################### в банке #################### БИК ##########](outnostroAccount, outnostroBank, outbicCode);
END;

PRIVATE MACRO PrintBase(tick, leg)
      var i = 0, reqMany = $0, lblMany = $0, reqCur, lblCur;
      var oprkoper = TBfile("oprkoper");
      ARRAY TmpComment, TmpName;
/*0         1         2         3         4         5         6         7         8*/
 [                                                                              ];
 [──────────────────────────────── Основание ───────────────────────────────────];
 [                                                                              ];
 strsplit(PartyName(tick.PartyID), TmpName, 25, 14);
 [Сделка № ################ заключена ########## ##### Контрагент ##############] (leg.rec.LegNumber, tick.DealDate, tick.DealTime:5:t, TmpName(i));
 i = i + 1;
 while (valType(TmpName(i)) == V_STRING)
 [                                                     #########################] (TmpName(i));
   i = i + 1;
 end;
 i = 0; 
 [договор № ________________ от __________ _____                                ];
 oprkoper.KeyNum = 0;
 oprkoper.rec.Kind_Operation  = tick.DealType;
 if( GetEq(oprkoper))
 [ ###############                                                              ] (oprkoper.rec.ShortName);
 end;
 if( FX_IsSale(tick.typedoc) )
    reqMany = leg.rec.Cost;
    lblMany = leg.rec.Principal;
    reqCur = leg.rec.CFI;
    lblCur = leg.rec.PFI;
 else   
    reqMany = leg.rec.Principal;
    lblMany = leg.rec.Cost;
    reqCur = leg.rec.PFI;
    lblCur = leg.rec.CFI;
 end;
 [Требования:    #####################  ###   ##########                        ] (reqMany:21:2, Currency(reqCur), leg.rec.Start);
 [Обязательства: #####################  ###   ##########                        ] (lblMany:21:2, Currency(lblCur), leg.rec.Start);
 [                                                                              ];
 [Неттинг #########                                                             ] (NameAlg(ALG_MM_NETTING, tick.Netting)); 
 strsplit(tick.Comment, TmpComment, 78, 62, 3);
 [Особые условия: ##############################################################] (TmpComment(i));
 i = i + 1;
 while (valType(TmpComment(i)) == V_STRING)
 [##############################################################################](TmpComment(i));
   i = i + 1;
 end;
END;

PRIVATE MACRO PrintCarries(opID, stID)
    var stat = 0;
    Record Carry("acctrn");

 ClearRecord(Carry);
 stat = GetDocsByOperStep(Carry, opID, stID);
 if (stat)
/*0         1         2         3         4         5         6         7         8*/
 [─────────────────────────── Отражение в бухучете ─────────────────────────────];
 [                                                                              ];
 [Проведите датой ##########                                                    ](Carry.Date_Carry);
 [┌─────────────────────┬───────────────────────────┬──────────────────────────┐];
 [│   Сумма проводки    │       Дебет счета         │      Кредит счета        │];
 [├─────────────────────┼───────────────────────────┼──────────────────────────┤];

 end;
 while (stat)            
 [│#####################│###########################│##########################│](Carry.Sum_Payer:21:2:c, Carry.Account_Payer:c, Carry.Account_Receiver:c);
 stat = GetDocsByOperStep(Carry, opID, stID);
 end;
 [└─────────────────────┴───────────────────────────┴──────────────────────────┘];
END;

MACRO PrintOrder( ID_Operation,  /* Номер экземпляра операции */
                  ID_Step,       /* Номер шага операции */
                  YesPayment,    /* Есть платеж, false - нет */
                  Liability,     /* Обязательство, false - нет */
                  RevDeal,       /* Обратная сделка, false - нет */ 
                  tick)      

    VAR    leg = TBfile("dl_leg");
    VAR    legID = 0, PrintOrderOutFileName, SaveFileName;
    Record Carry("acctrn");    
    File   out() txt;
    
   /* проверим, а есть ли проводки */ 
   ClearRecord(Carry);
   if ( NOT GetDocsByOperStep(Carry, ID_Operation, ID_Step) )
     return;
   end;
   leg.KeyNum = 0;
   leg.rec.LegKind = LEG_KIND_DL_TICK;
  leg.rec.DealID = tick.DealID;
   if( RevDeal )
       legID = 1;
   end;
   leg.rec.LegID = legID;
   if( not GetEq(leg) )
     msgbox( "Не найден транш сделки ", tick.DealCode );
     return 1;
   end;

   PrintOrderOutFileName = GetTxtFileName("fxordr");
   SaveFileName = SetOutput(PrintOrderOutFileName, FALSE);
  
   PrintHead;
   if( Liability AND YesPayment )
       PrintOF3(tick, Liability, RevDeal);
   end;
   PrintBase(tick, leg);
   PrintCarries(ID_Operation, ID_Step);
   ПодошваОтчета(TRUE, TRUE, TRUE, TRUE, 2);

   SetOutput(SaveFileName);
   open(out, PrintOrderOutFileName);
   ViewFile(out);

END;                  

MACRO PrintOrderInBuch( FirstDoc,      /* Указатель на первичный документ */
                        ID_Operation,  /* Номер экземпляра операции */
                        ID_Step,       /* Номер шага операции */
                        YesPayment,    /* Есть платеж, false - нет */
                        Liability,     /* Обязательство, false - нет */
                        RevDeal )      /* Обратная сделка, false - нет */
  Record tick("dl_tick"); 

  setbuff(tick, FirstDoc);
  
  PrintOrder( ID_Operation,ID_Step,YesPayment,Liability,RevDeal,tick ); //Сделано, чтобы работал PrintStep

END;  

