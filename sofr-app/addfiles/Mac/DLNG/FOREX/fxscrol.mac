/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.0                                          R-Style Software Lab
  
  File Name   : fxscrol.mac
  Created     : 15.02.99
  Programmer  : Сабитов Р.Р.
  Description : Макрос применяется для инициализации и проверки параметров
                сделки в скроллинге сделок

  Comment     : Пункт 8000 "Конверсионных операций"

└───────────────────────────────────────────────────────────────────────────*/
import globals,BankInter, OprInter, PTInter, Календарь, fxlb, dlref, DealsInter;

RECORD Сделка(dl_tick);         /*текущая сделка*/
RECORD ЦУ(dl_leg);              /*ценовые условия (dl_leg) прямой сделки*/
RECORD ЦУ_Обр(dl_leg);          /*обратное сделки*/
RECORD Старая_сделка(dl_tick);  /*сохраненное*/
RECORD Старый_ЦУ(dl_leg);
RECORD Старый_ЦУ_Обр(dl_leg);

RECORD mmscrol_paym (pmpaym);
/*
RECORD mmscrol_debet (pmprop);
RECORD mmscrol_credit (pmprop);
*/

class GenAgrRep()
  var
      NumDeal = "",
      Result  = "",
      Comment = "";      
end;

PRIVATE VAR TickToGenAgrRep = TArray;


const   Вставка  = 0,
        Удаление = 1,
        Правка   = 2,
       //SET_CHAR = "X",
       //UNSET_CHAR = "",
       // ALLFININSTR = -1,
       ALLPARTY = -1;

        
MACRO Новая_Сделка ()
  return 0;
END;

MACRO КорректировкаНеттинга(paym_p,debet_p,credit_p)
    SetBuff (mmscrol_paym,paym_p);
/*  SetBuff (mmscrol_debet,debet_p);
    SetBuff (mmscrol_credit,credit_p);
*/
    mmscrol_paym.Netting = "X";

    return mmscrol_paym.Netting;
END;

PRIVATE MACRO CompareLegs(leg1, leg2)
  if ((leg1.PFI != leg2.PFI) OR
      (leg1.Principal != leg2.Principal) OR
      (leg1.CFI != leg2.CFI) OR
      (leg1.Cost != leg2.Cost) OR
      (leg1.Start != leg2.Start) OR
      (leg1.Maturity != leg2.Maturity) OR
      (leg1.Expiry != leg2.Expiry) OR
      (leg1.LegNumber != leg2.LegNumber))
    return 1;
  else
    return 0;
  end;
END;

macro СформироватьНомерСделки()
  var
    retVal = 0;

    if (DL_GenRefByTypeDoc(OBJTYPE_CONVDEAL, Сделка.DealCode))
        Сделка.DealCodeTS = "КО_" + Сделка.DealCode;

        ЦУ.LegNumber = Сделка.DealCode;

        if( ЦУ_Обр.LegID )
            Сделка.DealCode = Сделка.DealCode + "S";
        else
            retVal = 0;
        end;

        if(ЦУ_Обр.LegID and (not DL_GenRefByTypeDoc(OBJTYPE_CONVDEAL, ЦУ_Обр.LegNumber)))
            msgbox("Ошибка при генерации номера сделки");
            retVal = 1;
        else
            retVal = 0;
        end;
    else
        msgbox("Ошибка при генерации номера сделки");
        retVal = 1;
    end;

    return retVal;
end;

macro ОткатитьНомерСделки()
    DL_RestoreRefByTypeDoc(OBJTYPE_CONVDEAL, ЦУ.LegNumber);

    if( ЦУ_Обр.LegID )
        DL_RestoreRefByTypeDoc(OBJTYPE_CONVDEAL, ЦУ_Обр.LegNumber);
    end;

    return 0;
end;

MACRO CheckPN( leg )
	if((leg.Maturity == date(0,0,0)) OR ( leg.Expiry == date(0,0,0)))
        MsgBox("Не указаны даты валютирования сделки.");
        return OBJ_IMPORTANT;
       end;
	if( IsHoliday(leg.Maturity) )
	    MsgBox("Указанная дата (" + leg.Maturity + ") - выходной день.");
	    return OBJ_IMPORTANT;
	end;
	if( IsHoliday(leg.Expiry) )
	    MsgBox("Указанная дата (" + leg.Expiry + ") - выходной день.");
	    return OBJ_IMPORTANT;
	end;
	if( (NOT leg.Principal) OR (NOT leg.Cost) )
	    MsgBox("Не указана сумма сделки.");
	    return OBJ_IMPORTANT;
	end;
        // т.к. ALLFININSTR объявляется 2 раза! 
	//if( (leg.PFI == ALLFININSTR) OR (leg.CFI == ALLFININSTR) )
        if( (leg.PFI == -1) OR (leg.CFI == -1) )
	    MsgBox("Не указана валюта.");
	    return OBJ_IMPORTANT;
	end;
	if(( leg.PFI == leg.CFI )and(not IsMOVING(Сделка.DealGroup)))
	    MsgBox("В сделке одинаковые валюты.");
	    return OBJ_IMPORTANT;
	end;
	if( (Сделка.DealDate > leg.Maturity) OR
	    (Сделка.DealDate > leg.Expiry) )
	    MsgBox("Дата заключения не может быть больше даты валютирования.");
	    return OBJ_IMPORTANT;
	end;

    return 0;
END;

MACRO Check( mode )
VAR dateMin, dateMax, stat;
	if( mode == OBJ_STARTOPR )
       if( Сделка.DealDate == date(0,0,0) )
           MsgBox("Не указана дата заключения сделки.");
           return OBJ_IMPORTANT;
       end;
       if( IsHoliday(Сделка.DealDate) )
           MsgBox("Указанная дата (" + Сделка.DealDate + ") - выходной день.");
           return OBJ_IMPORTANT;
       end;
       if( Сделка.PartyID == ALLPARTY )
           MsgBox("Не указан контрагент по сделке.");
           return OBJ_IMPORTANT;
       end;
       if( (Сделка.BrokerID != ALLPARTY) AND (Сделка.BrokerID == Сделка.PartyID) )
           MsgBox("В сделке совпадают брокер и контрагент.");
           return OBJ_IMPORTANT;
       elif( (Сделка.ClientID != ALLPARTY) AND (Сделка.ClientID == Сделка.PartyID) ) 
           MsgBox("В сделке совпадают клиент и контрагент.");
           return OBJ_IMPORTANT;
       elif( (Сделка.BrokerID != ALLPARTY) AND (Сделка.ClientID != ALLPARTY) AND
             (Сделка.BrokerID == Сделка.ClientID) )
           MsgBox("В сделке одинаковые брокер и клиент.");
           return OBJ_IMPORTANT;
       end;
       if( Сделка.TraderID == ALLPARTY )
           MsgBox("Не указан дилер.");
           return OBJ_IMPORTANT;
       end;
       if( Сделка.ClientID != ALLPARTY )
 	       dateMin = ЦУ.Start;
		   dateMax = ЦУ.Expiry;
           if( ЦУ_Обр.LegID )
               dateMax = ЦУ_Обр.Expiry;
           end;
		   if( NOT ОбслуживаетсяКлиент(Сделка.ClientID, PTSK_CURRDL, dateMin, dateMax) )
               MsgBox("Клиент не состоит на обслуживании в валютном дилинге.");
               return OBJ_IMPORTANT;
		   end;
       end;
       stat = CheckPN( ЦУ );
       if( stat < 0 )
           return stat;
       end;    
       if( ЦУ_Обр.LegID )
           stat = CheckPN( ЦУ_Обр );
           if( stat < 0  )
               return stat;
           end;    
       end;
	end;
	return 0;
END;

MACRO Проверить_Сделку (Режим)
var strbuf, refID, lastRef,isSwapDeal = false;
  if ((Режим == OBJ_DELETE) OR 
      (Режим == OBJ_DELETEOPR))
    return 0;
  elif ( Режим == OBJ_INSNOTOPR )  
       strbuf = "";
       if((Сделка.DealCode == "" ) )
           strbuf = "Номер сделки не указан.|Сформировать все номера автоматически?";
       elif((ЦУ.LegNumber == "" ) OR (ЦУ_Обр.LegID AND (ЦУ_Обр.LegNumber == "") )) 
           strbuf = "Идентификационный номер не указан.|Сформировать все номера автоматически?";
       end;
       if( strbuf != "" ) 
           if( GetTrue( True, strbuf) ) 
             if (not СформироватьНомерСделки())
               return 1;
             end;
           else 
             return 1;
           end
       end; 
  elif ( Режим == OBJ_INSERT )  
  /*ВАЖНО, что в этом случае возвращаем 2 и 3. Это означает, что надо сгенерить ошибку*/
       strbuf = "";

	   if((Сделка.DealCode == "" ) )
           return 2;
       elif((ЦУ.LegNumber == "" ) OR (ЦУ_Обр.LegID AND (ЦУ_Обр.LegNumber == "") )) 
           return 3;
       end;
  elif((Режим == OBJ_UPDATE))
    if (Сделка.DealCode == "")
      return 2;
    end;
    if (Сделка.TypeDoc != Старая_сделка.TypeDoc)
      return OBJ_IMPORTANT;
    end;
    if (CompareLegs(ЦУ, Старый_ЦУ))
      return OBJ_IMPORTANT;
    end;
    if (ЦУ_Обр.LegID AND CompareLegs(ЦУ_Обр, Старый_ЦУ_Обр))
      return OBJ_IMPORTANT;
    end
  end;
	
  if ((Режим == OBJ_INSNOTOPR) or ((Режим == OBJ_COMPLETE)))	
    if (Сделка.PartyID=={OurBank})
        Msgbox ("Наш Банк не может быть контрагентом");
        return OBJ_IMPORTANT;
    end;
  end;	
  /* Проверка правильности заполнения полей */
  return Check( Режим );
END;

MACRO Функция_Пользователя( Режим )
  MsgBox(String("Выполняется макрофункция с именем",
                "|Функция_Пользователя,",
                "|которая определена в файле fxscrol.mac"));
  return 0;
END;

macro MakeCommonRef(/*BIC_A, BIC_B, Rate :string*/)
var error, BIC_A, BIC_B, Rate, IsSaleDeal = false;
var CommonRef,Bank1,Place1,RefCode,Bank2,Place2,
    LastNonZero,i,symb;
  if (isSale( getOperationGroup(Сделка.DealType)))
    IsSaleDeal = true;
  end;

  if(not IsSaleDeal)
  BIC_A = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
  if( error ) return 0; end;
  BIC_B = ПолучитьКодСубъекта( Сделка.PartyID, PTCK_SWIFT, error );
  if( error ) return 0; end;
  else
    BIC_B = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
    if( error ) return 0; end;
    BIC_A = ПолучитьКодСубъекта( Сделка.PartyID, PTCK_SWIFT, error );
    if( error ) return 0; end;    
  end;

  Rate = String(ЦУ.Price);

if (StrLen(BIC_A) < 8) return 0; 
end;
if (StrLen(BIC_B) < 8) return 0;
end;

if (BIC_A > BIC_B)
	Bank1 = SubStr(BIC_B,1,4);
    Place1 = SubStr(BIC_B,7,2);
	Bank2 = SubStr(BIC_A,1,4);
    Place2 = SubStr(BIC_A,7,2);
else
    Bank1 = SubStr(BIC_A,1,4);
    Place1 = SubStr(BIC_A,7,2);
	Bank2 = SubStr(BIC_B,1,4);
    Place2 = SubStr(BIC_B,7,2);
end;

LastNonZero = 0;
for (i, 1, StrLen(Rate))
	symb = SubStr(Rate,i,1);
	if ((symb != "0") and (symb != ",") and (symb != "."))
		LastNonZero = i;
	end;
end;

if (LastNonZero >=4) 
	RefCode = SubStr(Rate,LastNonZero-3,4);
else 
	RefCode = SubStr(Rate,1,LastNonZero);
    while (StrLen(RefCode)<4)
		RefCode = "0" + RefCode;
	end;
end;

CommonRef = Bank1+Place1+RefCode+Bank2+Place2; 

Сделка.DealCodePS = CommonRef;
return 0;

END;

MACRO Привязать_к_ГС(DealID, GenAgrID)
    var
        cmd = "", rs = NULL, rsG = NULL, rep = NULL, DealCode = "", retVal = 0;

    if (GenAgrID > 0)   // привязка
        rs = TRsbDataSet("select * from ddl_tick_dbt tick, ddl_leg_dbt leg where" +
                          " leg.t_DealID = tick.t_DealID and tick.t_DealID = " + String(DealID));

        rsG = TRsbDataSet("select * from ddl_genagr_dbt gagr where" +
                          " gagr.t_GenAgrID = " + String(GenAgrID));

        if (not rs.MoveNext)
            rep = GenAgrRep;
            rep.NumDeal = "";
            rep.Result  = "не привязана";
            rep.Comment = "Сделка с ID = " + String(DealID) + " не найдена.";
        else
            DealCode = rs.DealCode;
        end;

        if (not rsG.MoveNext)
            rep = GenAgrRep;
            rep.NumDeal = "";
            rep.Result  = "не привязана";
            rep.Comment = "ГС с ID = " + String(GenAgrID) + " не найдено.";
        end;

        if (rs.GenAgrID != -1)
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязана";
            rep.Comment = "Сделка с ID = " + String(DealID) + " уже привязана к другому ГС.";
        end;

        if (date(rs.Start) < date(rsG.Start))
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязана";
            rep.Comment = "Дата начала сделки меньше даты начала ГС.";
        end;

        if ((date(rs.Start) + Int(rs.Duration)) > (date(rsG.Start) + Int(rsG.Duration)))
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязана";
            rep.Comment = "Дата окончания сделки больше даты окончания ГС.";
        end;

        if (rs.PartyID != rsG.PartyID)
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязана";
            rep.Comment = "Контрагент по сделке не совпадает с контрагентом по ГС.";
        end;

        if ((rs.DealStatus == DL_READIED)and
            (rsG.Status != 10 ))
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не привязана";
            rep.Comment = "Открытую сделку можно привязывать только к открытому ГС.";
        end;
    elif (GenAgrID < 0)// отвязка
        rs = TRsbDataSet("select * from ddl_tick_dbt tick where" +
                          " tick.t_DealID = " + String(DealID));

        if (rs.MoveNext)
            DealCode = rs.DealCode;

            if (rs.DealStatus == DL_CLOSED)
                rep = GenAgrRep;
                rep.NumDeal = rs.DealCode;
                rep.Result  = "не отвязана";
                rep.Comment = "Нельзя отвязать закрытую сделку от ГС.";
            end;
        else
            rep = GenAgrRep;
            rep.NumDeal = "";
            rep.Result  = "не отвязана";
            rep.Comment = "Сделка с ID = " + String(DealID) + " не найдена.";
        end;

        if (rs.GenAgrID == -1)
            rep = GenAgrRep;
            rep.NumDeal = rs.DealCode;
            rep.Result  = "не отвязана";
            rep.Comment = "Сделка с ID = " + String(DealID) + " не привязана к ГС.";
        end;

        rs = TRsbDataSet("select * from ddl_genagr_dbt gagr where" +
                          " gagr.t_GenAgrID = " + String(GenAgrID));

        if (rs.MoveNext)
            if (rs.Status == 20)
                rep = GenAgrRep;
                rep.NumDeal = rs.DealCode;
                rep.Result  = "не отвязана";
                rep.Comment = "Нельза отвязать сделку от закрытого ГС.";
            end;
        else
            // все равно отвяжем, если ГС не нашли
        end;
    else
        return 0;
    end;

    if (rep == NULL)
        cmd = "update ddl_tick_dbt tick set tick.t_GenAgrID = " + String(GenAgrID) + 
               " where tick.t_DealID = " + String(DealID);

        RsdCommand(cmd).Execute;

        rep = GenAgrRep;
        rep.NumDeal = DealCode;
        if (GenAgrID > 0)
            rep.Result  = "привязана";
            rep.Comment = "Сделка привязана к ГС.";
        else
            rep.Result  = "отвязана";
            rep.Comment = "Сделка отвязана от ГС.";

        end;
    else
        retVal = 1;
    end;

   TickToGenAgrRep[TickToGenAgrRep.size] = rep;

    return retVal;
END;

MACRO Отчет_Привязки_к_ГС(GenAgrID)
    var
        ReportFile = NULL, rs = NULL, idx = 0;

    file RepFile() txt write;

    if (TickToGenAgrRep.size == 0)
        ;//ничего не делаем
    elif (TickToGenAgrRep.size == 1)
        msgbox(TickToGenAgrRep[0].Comment);
    else
        open(RepFile, GetTxtFileName("mmgenagr"));

        if (GenAgrID > 0)// привязка
            rs = TRsbDataSet("select * from ddl_genagrview_dbt gagr where" +
                              " gagr.t_GenAgrID = " + String(GenAgrID));
            rs.MoveNext;

            insert(RepFile, "Протокол привязки сделок к ГС " + rs.Code + ", контрагент " + rs.Party);
        else // отвязка
            insert(RepFile, "Протокол отвязки сделок от ГС");
        end;

        insert(RepFile, "┌────────────────────┬────────────────┬────────────────────────────────────────────────────────────────────────────────┐");
        insert(RepFile, "│    Номер сделки    │   Результат    │                              Сообщение                                         │");
        insert(RepFile, "│                    │   процедуры    │                                                                                │");
        insert(RepFile, "├────────────────────┼────────────────┼────────────────────────────────────────────────────────────────────────────────┤");




        while (idx < TickToGenAgrRep.size)
            insert(RepFile, "│" + 
                               String(TickToGenAgrRep[idx].NumDeal:20)  + "│" +  
                               String(TickToGenAgrRep[idx].Result:16)   + "│" +  
                               String(TickToGenAgrRep[idx].Comment:80)  + "│"  );
    
            idx = idx + 1;
        end;

        insert(RepFile, "└────────────────────┴────────────────┴────────────────────────────────────────────────────────────────────────────────┘");

        close(RepFile);
        ViewFile(RepFile);
    end;

    TickToGenAgrRep.size = 0;

    return 0;
END;

FX_NameMacroFile = "fxscrol.mac";
