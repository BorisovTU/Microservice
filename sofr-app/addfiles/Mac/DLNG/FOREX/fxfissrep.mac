/*--------------------------------------------------------------------------------------*/
/*  Имя файла        : fxfissrep.mac                                                    */
/*                                                                                      */
/*  Описание         : Отчет "Регистр налогового учета доходов (расходов)               */
/*                            по операциям с ФИСС"                                      */
/*                                                                                      */
/*  Программист      : Куперин М.В.                                                     */
/*                                                                                      */
/*  Создан           : 02.03.2011                                                       */
/*--------------------------------------------------------------------------------------*/
IMPORT "globals", Календарь, RSD, RsbDataSet, "or_tpl_h.mac";
///****************************************************************************************/
PRIVATE VAR
    PanelRepParm = NULL;

PRIVATE VAR
    flagNoRate4SomeCurr = false;
/****************************************************************************************/
PRIVATE CLASS ReportParm
  private const // поля панели параметров отчета
    REPFLD_ONPERIOD    = 0,
    REPFLD_PERIODDATE1 = 1,
    REPFLD_PERIODDATE2 = 2,
    REPFLD_ONDATE      = 3,
    REPFLD_DATE        = 4,
    REPFLD_DEALNUM     = 5,
    REPFLD_DEALTYPE    = 6,
    REPFLD_REPFORMAT   = 7,
    REPFLD_LAST        = 8;

  var
      flagPeriod   = "", Date1 = date(0,0,0), Date2 = date(0,0,0),
      flagDate     = "", Date3  = date(0,0,0),
      DealNum      = "", 
      flagDealType = "",
      Format       = 0, path = "";

  private macro PumpFormat(_format, fromMenu)
      ARRAY formatAR;
      formatAR(0) = "Сохранить в xls-файл ";
      formatAR(1) = "Вывести на экран     ";

      if (fromMenu)
          _format = Menu(formatAR);
          if (_format < 0)
              return formatAR(Format);
          else
              Format = _format;
              return formatAR(_format);
          end;
	  else
          return formatAR(_format);
      end;
  end;

  private macro InitDefaultValue(fldnum)
      if (fldnum == REPFLD_ONPERIOD)
          flagPeriod = "";
      elif (fldnum == REPFLD_PERIODDATE1)
          Date1 = {curdate};
      elif (fldnum == REPFLD_PERIODDATE2)
          Date2 = {curdate};
      elif (fldnum == REPFLD_ONDATE)
          flagDate = "";
      elif (fldnum == REPFLD_DATE)
          Date3 = {curdate};
      elif (fldnum == REPFLD_DEALNUM)
          DealNum = "";
      elif (fldnum == REPFLD_DEALTYPE)
          flagDealType = "";
      elif (fldnum == REPFLD_REPFORMAT)
          Format       = 1;
      else
          return;
      end;
  end;

  private macro Clear(fldnum)
      if (fldnum == REPFLD_ONPERIOD)
          flagPeriod = "";
      elif (fldnum == REPFLD_PERIODDATE1)
          Date1 = date(0,0,0);
      elif (fldnum == REPFLD_PERIODDATE2)
          Date2 = date(0,0,0);
      elif (fldnum == REPFLD_ONDATE)
          flagDate = "";
      elif (fldnum == REPFLD_DATE)
          Date3 = date(0,0,0);
      elif (fldnum == REPFLD_DEALNUM)
          DealNum = "";
      elif (fldnum == REPFLD_DEALTYPE)
          flagDealType = "";
      elif (fldnum == REPFLD_REPFORMAT)
          Format = "";
      else
          return;
      end;

      InitDefaultValue(fldnum);
  end;

  private macro PressSpace(Pn, ID)
      if (Pn(ID) == "")
          Pn(ID) = "X";
      elif (Pn(ID) == "X")
          Pn(ID) = "";
      end;
      if (ID == REPFLD_ONPERIOD)
          flagPeriod = Pn(ID);
      elif (ID == REPFLD_ONDATE)
          flagDate = Pn(ID);
      elif (ID == REPFLD_DEALTYPE)
          flagDealType = Pn(ID);
      end;
  end;

  private macro SelectScroll(Query, Header, x, y /*...*/)
      var rs     = RsdRecordset(Query, NULL, RSDVAL_STATIC),
          Column = TArray(),
          i, ColumnValue, ColumnName, NumColumns = 0;

    macro EvProc(rs, cmd, id, key)
        if ((cmd == DLG_KEY)and(key == 13))
            if (rs.RecCount > 0)
                return CM_SELECT;
            end;
        end;
        return CM_DEFAULT;
    end;
    
    macro AddColumn(ar,ind, fld, head)
        ar.value( ind * 6 + 0 ) = fld;
        ar.value( ind * 6 + 1 ) = head;
        ar.value( ind * 6 + 2 ) = NULL;
        ar.value( ind * 6 + 3 ) = 2;
        ar.value( ind * 6 + 4 ) = NULL;
        ar.value( ind * 6 + 5 ) = 0;
        NumColumns = NumColumns + 1;
    end;

      i = 5;
      while (GetParm(i, ColumnValue)and(GetParm(i+1, ColumnName)))              
         AddColumn( Column, NumColumns, ColumnValue, ColumnName );
         i = i + 2;
      end;   
  
      if (RunScroll(rs, NumColumns, Column, NULL, @EvProc, Header, "Esc Выход F4 Поиск Enter Выбор", true, x, y, NULL, 25))
          return rs;
      end;

      return NULL;
  end;

  private macro EnDisFields(Pn, ID)
      if (ID == REPFLD_ONPERIOD)
          if (Pn(ID) == "X")
              if (Pn(REPFLD_ONDATE) == "X")
                  PressSpace(Pn, REPFLD_ONDATE);
              end;
              Clear(REPFLD_DATE);
              DisableFields(Pn, REPFLD_DATE);
              EnableFields(Pn, REPFLD_PERIODDATE1);
              EnableFields(Pn, REPFLD_PERIODDATE2);
          end;
      elif (ID == REPFLD_PERIODDATE1)
          ;
      elif (ID == REPFLD_PERIODDATE2)
          ;
      elif (ID == REPFLD_ONDATE)
          if (Pn(ID) == "X")
              if (Pn(REPFLD_ONPERIOD) == "X")
                  PressSpace(Pn, REPFLD_ONPERIOD);
              end;
              Clear(REPFLD_PERIODDATE1);
              Clear(REPFLD_PERIODDATE2);
              DisableFields(Pn, REPFLD_PERIODDATE1);
              DisableFields(Pn, REPFLD_PERIODDATE2);
              EnableFields(Pn, REPFLD_DATE);
          end;
      elif (ID == REPFLD_DATE)
          ;
      elif (ID == REPFLD_DEALNUM)
          ;
      elif (ID == REPFLD_DEALTYPE)
          ;
      elif (ID == REPFLD_REPFORMAT)
          ;
      end;
  end;

  private macro ProcessPn(Pn, Cmd, ID, Key)
      var Parm = NULL;

        macro CheckPeriod(date1, date2):bool // проверка периода
            if (date1 > date2)
                msgbox("Не корректно задан период дат.");
                return false;
            end;
            return true;
        end;

      if(Cmd == DLG_INIT)
          Pn(REPFLD_ONPERIOD)    = flagPeriod;
          Pn(REPFLD_PERIODDATE1) = Date1;
          Pn(REPFLD_PERIODDATE2) = Date2;
          Pn(REPFLD_ONDATE)      = flagDate;
          Pn(REPFLD_DATE)        = Date3;
          Pn(REPFLD_DEALNUM)     = DealNum;
          Pn(REPFLD_DEALTYPE)    = flagDealType;
          Pn(REPFLD_REPFORMAT)   = PumpFormat(Format, false);
          EnDisFields(Pn, REPFLD_ONPERIOD);
          EnDisFields(Pn, REPFLD_PERIODDATE1);
          EnDisFields(Pn, REPFLD_PERIODDATE2);
          EnDisFields(Pn, REPFLD_ONDATE);
          EnDisFields(Pn, REPFLD_DATE);
          EnDisFields(Pn, REPFLD_DEALNUM);
          EnDisFields(Pn, REPFLD_DEALTYPE);
          EnDisFields(Pn, REPFLD_REPFORMAT);
          UpdateFields(Pn);
      elif((Cmd == DLG_KEY) and (Key == 32))
          if (((ID == REPFLD_ONPERIOD)or
              (ID == REPFLD_ONDATE))and
              (Pn(ID) == ""))
              PressSpace(Pn, ID);
              EnDisFields(Pn, ID);
              UpdateFields(Pn);
          elif (ID == REPFLD_DEALTYPE)
              PressSpace(Pn, ID);
              UpdateFields(Pn);
          end;
      elif((Cmd == DLG_KEY) and (Key == 317))
          if ((ID == REPFLD_PERIODDATE1)or
              (ID == REPFLD_PERIODDATE2)or
              (ID == REPFLD_DATE))
              Pn(ID) = GetDateByCalendar(Pn(ID)); 
          elif (ID == REPFLD_DEALNUM)
              Parm = SelectScroll("select fxdeals.* from dfx_deals_dbt fxdeals, ddl_tick_dbt tick where (tick.t_DealID = fxdeals.t_DealID)and(tick.t_DealStatus in (10, 20))",
                     "Список конверсионных сделок", 10, 0, 
                     "t_dealdate",      "Дата",
                     "t_dealcode",      "Номер",
                     "t_typedeal",      "Тип",
                     "t_partyname",     "Контрагент",
                     "t_currency1",     "Баз. Актив",
                     "t_principal",     "Количество",
                     "t_currency2",     "Контрактив",
                     "t_cost",          "Количество",
                     "t_basedate",      "Валютир.",
                     "t_basedatecontr", "Валютир.ч.2");
              if (Parm != NULL)
                  Pn(REPFLD_DEALNUM) = Parm.value("t_dealcode");
              end;
          elif (ID == REPFLD_REPFORMAT)
               Pn(REPFLD_REPFORMAT) = PumpFormat(Format, true);

               if (Format == 1)
                   path = "";
               else
                  if (not SelectFolder (path, path, "", true))
                      Format = 1;
                      Pn(REPFLD_REPFORMAT) = PumpFormat(Format, false);
                  end;
               end;
          end;
          UpdateFields(Pn);
      elif((Cmd == DLG_KEY) and ((Key == 316)or(Key == 323)or(Key == 13))) // запуск формирования отчета
          if ((Key == 13)and(ID != (REPFLD_LAST - 1))) // если нажали Enter в первом поле панели, то обрабатывем это как переход в другое поле
              return CM_DEFAULT;
          end;

          if ((flagPeriod)and(not CheckPeriod(Pn(REPFLD_PERIODDATE1), Pn(REPFLD_PERIODDATE2))))
              return CM_IGNORE;
          end;

          return CM_SAVE;
      elif(Cmd == DLG_SAVE)
          MakeDir(toAnsi("$" + path));

          flagPeriod   = Pn(REPFLD_ONPERIOD);
          Date1        = Pn(REPFLD_PERIODDATE1);
          Date2        = Pn(REPFLD_PERIODDATE2);
          flagDate     = Pn(REPFLD_ONDATE);
          Date3        = Pn(REPFLD_DATE);
          DealNum      = Pn(REPFLD_DEALNUM);
          flagDealType = Pn(REPFLD_DEALTYPE);
      end;

      return CM_DEFAULT;
  end;

  macro ClearPanel()
      Clear(REPFLD_ONPERIOD);
      Clear(REPFLD_PERIODDATE1);
      Clear(REPFLD_PERIODDATE2);
      Clear(REPFLD_ONDATE);
      Clear(REPFLD_DATE);
      Clear(REPFLD_DEALNUM);
      Clear(REPFLD_DEALTYPE);
      Clear(REPFLD_REPFORMAT);
  end;

  macro Run()
      macro ProcessPanel(Pn, Cmd, ID, Key)
          return ProcessPn(Pn, Cmd, ID, Key);
      end;

      record Panel("FXPNFISS", "Deals.lbr") dialog;
      return RunDialog(Panel, @ProcessPanel);
  end;

//Constructor
  ClearPanel();

  flagDate     = "X";
  Date3  = {curdate};
END;
/****************************************************************************************/
PRIVATE MACRO BodyRep(rs, Rep)
    var RepLine   = Rep.RegisterTable("BodyLine"),
        lineadd   = 0, P = 0;

    Rep.Sheet.Range("BodyLine").ClearContents();

    BegAction(0, "Формирование отчета");
    while (rs.MoveNext())
         // сделки, для которых курсы валют не заданы, в отчет не выводим
         if ((rs.value("t_Principal")    == 0)or
             (rs.value("t_RateBase")     == 0)or
             (rs.value("t_RateContr")    == 0)or
             (rs.value("t_StContr")      == 0)or
             (rs.value("t_StBase")       == 0)or
             (rs.value("t_RateBaseIsp")  == 0)or
             (rs.value("t_RateContrIsp") == 0)or
             (rs.value("t_StContrIsp")   == 0)or
             (rs.value("t_StBaseIsp")    == 0))

             flagNoRate4SomeCurr = true;
             continue;
         end;

         if (lineadd != 0)
            RepLine.AddStr();
         end;

        Rep.SetValue_NameCell("T_B1", rs.value("t_DealCode"));
        Rep.SetValue_NameCell("T_B2", rs.value("t_TypeD"));
        Rep.SetValue_NameCell("T_B3", rs.value("t_PartyName"));
        Rep.SetValue_NameCell("T_B4", rs.value("t_DealDate"));
        Rep.SetValue_NameCell("T_B5", rs.value("t_Maturity"));
        Rep.SetValue_NameCell("T_B6", rs.value("t_BS"));
        Rep.SetValue_NameCell("T_B7", rs.value("t_Currency1"));
        Rep.SetValue_NameCell("T_B8", rs.value("t_Principal"));
        Rep.SetValue_NameCell("T_B9", rs.value("t_Currency2"));
        Rep.SetValue_NameCell("T_B10", rs.value("t_Cost"));
        Rep.SetValue_NameCell("T_B11", rs.value("t_RateBase"));
        Rep.SetValue_NameCell("T_B12", rs.value("t_RateContr"));
        Rep.SetValue_NameCell("T_B13", rs.value("t_StContr"));
        Rep.SetValue_NameCell("T_B14", rs.value("t_StBase"));
        Rep.SetValue_NameCell("T_B15", (rs.value("t_StContr") / rs.value("t_Principal")));
        Rep.SetValue_NameCell("T_B16", "");
        Rep.SetValue_NameCell("T_B17", "");
        Rep.SetValue_NameCell("T_B18", "");

        P = rs.value("t_RateBase") * rs.value("t_P1") / rs.value("t_RateContr");
        Rep.SetValue_NameCell("T_B19", P);

        if (rs.value("t_BS") == "ПОКУПКА")
            Rep.SetValue_NameCell("T_B20", (0.8 *P - rs.value("t_Cost")));
        else
            Rep.SetValue_NameCell("T_B20", (rs.value("t_Cost") - 1.2 * P));
        end;

        Rep.SetValue_NameCell("T_B21", rs.value("t_Ras"));
        Rep.SetValue_NameCell("T_B22", rs.value("t_Nett"));
        Rep.SetValue_NameCell("T_B23", rs.value("t_RateBaseIsp"));
        Rep.SetValue_NameCell("T_B24", rs.value("t_RateContrIsp"));
        Rep.SetValue_NameCell("T_B25", rs.value("t_StContrIsp"));
        Rep.SetValue_NameCell("T_B26", rs.value("t_StBaseIsp"));

        if ((rs.value("t_Ras") == "Поставка")and
            (rs.value("t_BS") == "ПРОДАЖА")and
            (rs.value("t_StContrIsp") > rs.value("t_StBaseIsp")))
            Rep.SetValue_NameCell("T_B27", rs.value("t_StContrIsp") - rs.value("t_StBaseIsp"));
        elif ((rs.value("t_Ras") == "x")and
              (rs.value("t_BS") == "ПРОДАЖА")and
              (rs.value("t_Nett") == "Поставка")and
              (rs.value("t_StContrIsp") > rs.value("t_StBaseIsp")))
            Rep.SetValue_NameCell("T_B27", rs.value("t_StContrIsp") - rs.value("t_StBaseIsp"));
        elif ((rs.value("t_Ras") == "Поставка")and
            (rs.value("t_BS") == "ПОКУПКА")and
            (rs.value("t_StContrIsp") < rs.value("t_StBaseIsp")))
            Rep.SetValue_NameCell("T_B27", rs.value("t_StBaseIsp") - rs.value("t_StContrIsp"));
        elif ((rs.value("t_Ras") == "x")and
              (rs.value("t_BS") == "ПОКУПКА")and
              (rs.value("t_Nett") == "Поставка")and
              (rs.value("t_StContrIsp") < rs.value("t_StBaseIsp")))
            Rep.SetValue_NameCell("T_B27", rs.value("t_StBaseIsp") - rs.value("t_StContrIsp"));
        else
            Rep.SetValue_NameCell("T_B27", "");
        end;

        if ((rs.value("t_Ras") == "Поставка")and
            (rs.value("t_BS") == "ПРОДАЖА")and
            (rs.value("t_StBaseIsp") > rs.value("t_StContrIsp")))
            Rep.SetValue_NameCell("T_B28", rs.value("t_StBaseIsp") - rs.value("t_StContrIsp"));
        elif ((rs.value("t_Ras") == "x")and
              (rs.value("t_BS") == "ПРОДАЖА")and
              (rs.value("t_Nett") == "Поставка")and
            (rs.value("t_StBaseIsp") > rs.value("t_StContrIsp")))
            Rep.SetValue_NameCell("T_B28", rs.value("t_StBaseIsp") - rs.value("t_StContrIsp"));
        elif ((rs.value("t_Ras") == "Поставка")and
            (rs.value("t_BS") == "ПОКУПКА")and
            (rs.value("t_StBaseIsp") < rs.value("t_StContrIsp")))
            Rep.SetValue_NameCell("T_B28", rs.value("t_StContrIsp") - rs.value("t_StBaseIsp"));
        elif ((rs.value("t_Ras") == "x")and
              (rs.value("t_BS") == "ПОКУПКА")and
              (rs.value("t_Nett") == "Поставка")and
            (rs.value("t_StBaseIsp") < rs.value("t_StContrIsp")))
            Rep.SetValue_NameCell("T_B28", rs.value("t_StContrIsp") - rs.value("t_StBaseIsp"));
        else
            Rep.SetValue_NameCell("T_B28", "");
        end;

        if ((rs.value("t_Ras") == "Неттинг")and
            (rs.value("t_BS") == "ПРОДАЖА")and
              (rs.value("t_StContrIsp") > rs.value("t_StBaseIsp")))
            Rep.SetValue_NameCell("T_B29", rs.value("t_StContrIsp") - rs.value("t_StBaseIsp"));
        elif ((rs.value("t_Ras") == "x")and
              (rs.value("t_BS") == "ПРОДАЖА")and
              (rs.value("t_Nett") == "Неттинг")and
              (rs.value("t_StContrIsp") > rs.value("t_StBaseIsp")))
            Rep.SetValue_NameCell("T_B29", rs.value("t_StContrIsp") - rs.value("t_StBaseIsp"));
        elif ((rs.value("t_Ras") == "Неттинг")and
            (rs.value("t_BS") == "ПОКУПКА")and
            (rs.value("t_StContrIsp") < rs.value("t_StBaseIsp")))
            Rep.SetValue_NameCell("T_B29", rs.value("t_StBaseIsp") - rs.value("t_StContrIsp"));
        elif ((rs.value("t_Ras") == "x")and
              (rs.value("t_BS") == "ПОКУПКА")and
              (rs.value("t_Nett") == "Неттинг")and
            (rs.value("t_StContrIsp") < rs.value("t_StBaseIsp")))
            Rep.SetValue_NameCell("T_B29", rs.value("t_StBaseIsp") - rs.value("t_StContrIsp"));
        else
            Rep.SetValue_NameCell("T_B29", "");
        end;

        if ((rs.value("t_Ras") == "Неттинг")and
            (rs.value("t_BS") == "ПРОДАЖА")and
            (rs.value("t_StBaseIsp") > rs.value("t_StContrIsp")))
            Rep.SetValue_NameCell("T_B30", rs.value("t_StBaseIsp") - rs.value("t_StContrIsp"));
        elif ((rs.value("t_Ras") == "x")and
              (rs.value("t_BS") == "ПРОДАЖА")and
              (rs.value("t_Nett") == "Неттинг")and
            (rs.value("t_StBaseIsp") > rs.value("t_StContrIsp")))
            Rep.SetValue_NameCell("T_B30", rs.value("t_StBaseIsp") - rs.value("t_StContrIsp"));
        elif ((rs.value("t_Ras") == "Неттинг")and
              (rs.value("t_BS") == "ПОКУПКА")and
              (rs.value("t_StBaseIsp") < rs.value("t_StContrIsp")))
            Rep.SetValue_NameCell("T_B30", rs.value("t_StContrIsp") - rs.value("t_StBaseIsp"));
        elif ((rs.value("t_Ras") == "x")and
              (rs.value("t_BS") == "ПОКУПКА")and
              (rs.value("t_Nett") == "Неттинг")and
              (rs.value("t_StBaseIsp") < rs.value("t_StContrIsp")))
            Rep.SetValue_NameCell("T_B30", rs.value("t_StContrIsp") - rs.value("t_StBaseIsp"));
        else
            Rep.SetValue_NameCell("T_B30", "");
        end;

        lineadd = lineadd + 1;
    end;

    EndAction();
END;

PRIVATE MACRO PrintReport(Parm, Query)
    var Rep:CTemplateXLS = CTemplateXLS(),
        rs = NULL, LastNameFileTmp = "";   

    Rep.OpenTemplate("Report_fxfiss.xls", false);

    Rep.SetValue_NameCell("T_H1", {Name_Bank});
    if (Parm.flagPeriod)
        Rep.SetValue_NameCell("T_H2", "за период с: " + String(Parm.Date1) + " по:" + String(Parm.Date2));
    else
        Rep.SetValue_NameCell("T_H2", "за дату: " + String(Parm.Date3):f);
    end;

    rs = RsdRecordset(Query);
    BodyRep(rs, Rep);

    if (Parm.Format == 0)
        Parm.path = Parm.path + "\\" + GetExlusiveFileName("xls");  
        LastNameFileTmp = SetOutPut(Parm.path, true);
        LastNameFileTmp = SetOutPut(LastNameFileTmp, true);
        DelFile(Parm.path);
        Rep.SaveAs(Parm.path, WINREP_OUTPUT_EXCEL);
    else
        Rep.SaveAsTemplate(NULL, true);
    end;
END;
///****************************************************************************************/
MACRO PreparePrintReport()
    var Query = "", Query1 = "", Query2 = "", EndDate = NULL;

    PanelRepParm = ReportParm;
    if (PanelRepParm.Run())
        if (PanelRepParm.flagPeriod)
            EndDate = PanelRepParm.Date2;
        else
            EndDate = PanelRepParm.Date3;
        end;

        Query1 = "select fxdeals.t_DealCode, "
                  "case "
                  "  when (bitand(tick.t_DealGroup, 56) = 8) then "
                  "      case "
                  "          when (((leg.t_Maturity - fxdeals.t_DealDate) = 0)and(leg.t_LegID = 0)) then '1 часть swap TOD' "
                  "          when (((leg.t_Maturity - fxdeals.t_DealDate) = 1)and(leg.t_LegID = 0)) then '1 часть swap TOM' "
                  "          when (((leg.t_Maturity - fxdeals.t_DealDate) = 2)and(leg.t_LegID = 0)) then '1 часть swap SPOT' "
                  "          when (((leg.t_Maturity - fxdeals.t_DealDate) > 2)and(leg.t_LegID = 0)) then '1 часть swap FW' "
                  "          when (((leg.t_Maturity - fxdeals.t_DealDate) = 0)and(leg.t_LegID = 1)) then '2 часть swap TOD' "
                  "          when (((leg.t_Maturity - fxdeals.t_DealDate) = 1)and(leg.t_LegID = 1)) then '2 часть swap TOM' "
                  "          when (((leg.t_Maturity - fxdeals.t_DealDate) = 2)and(leg.t_LegID = 1)) then '2 часть swap SPOT' "
                  "          when (((leg.t_Maturity - fxdeals.t_DealDate) > 2)and(leg.t_LegID = 1)) then '2 часть swap FW' "
                  "      end "
                  "  else "
                  "      case "
                  "          when ((leg.t_Maturity - fxdeals.t_DealDate) = 0) then 'TOD' "
                  "          when ((leg.t_Maturity - fxdeals.t_DealDate) = 1) then 'TOM' "
                  "          when ((leg.t_Maturity - fxdeals.t_DealDate) = 2) then 'SPOT' "
                  "          when ((leg.t_Maturity - fxdeals.t_DealDate) > 2) then 'FW' "
                  "          else ' ' "
                  "      end "
                  "end as t_TypeD, "
                  "fxdeals.t_PartyName, fxdeals.t_DealDate, leg.t_Maturity, "
                  "case  "
                  "    when ((tick.t_TypeDoc = 'S')and(leg.t_LegID = 0)) then 'ПРОДАЖА' "
                  "    when ((tick.t_TypeDoc = 'B')and(leg.t_LegID = 0)) then 'ПОКУПКА' "
                  "    when ((tick.t_TypeDoc = 'S')and(leg.t_LegID = 1)) then 'ПОКУПКА' "
                  "    when ((tick.t_TypeDoc = 'B')and(leg.t_LegID = 1)) then 'ПРОДАЖА' "
                  "end as t_BS, "
                  "fxdeals.t_Currency1, leg.t_Principal, "
                  "fxdeals.t_Currency2, leg.t_Cost, ";

         Query2 = 
                  "NVL(RSB_FIINSTR.ConvSum(1, leg.t_PFI, 0, fxdeals.t_DealDate, 0), 0) as t_RateBase, "
                  "NVL(RSB_FIINSTR.ConvSum(1, leg.t_CFI, 0, fxdeals.t_DealDate, 0), 0) as t_RateContr, "
                  "NVL(RSB_FIINSTR.ConvSum(leg.t_Cost, leg.t_CFI, 0, fxdeals.t_DealDate, 1), 0) as t_StContr, "
                  "NVL(RSB_FIINSTR.ConvSum(leg.t_Principal, leg.t_PFI, 0, fxdeals.t_DealDate, 1), 0) as t_StBase, "
                  "'', '','','', "
                  "  ( "
                  "    (1 / (1 + "
                  "          NVL(RSB_FIINSTR.ConvSumType(1, leg.t_PFI, 0, 22, fxdeals.t_DealDate, 1), 0) "
                  "          * "
                  "          decode(leg.t_PFI, 0, (leg.t_Maturity - fxdeals.t_DealDate + 1)/365, (leg.t_Maturity - fxdeals.t_DealDate + 1)/360))) "
                  "    / "
                  "    (1 / (1 + "
                  "          NVL(RSB_FIINSTR.ConvSumType(1, leg.t_CFI, 0, 22, fxdeals.t_DealDate, 1), 0) "
                  "          * "
                  "          decode(leg.t_CFI, 0, (leg.t_Maturity - fxdeals.t_DealDate + 1)/365, (leg.t_Maturity - fxdeals.t_DealDate + 1)/360))) "
                  "  ) as t_P1, "
                  "'', "
                  "case "
                  "    when (tick.t_DealStatus = 20)and(paym.t_PaymStatus = 32000)and(paym.t_ValueDate <= '" + EndDate + "') then 'Поставка' "
                  "    when (tick.t_DealStatus = 20)and(paym.t_PaymStatus <> 32000)and(paym.t_ValueDate <= '" + EndDate + "') then 'Неттинг' "
                  "    when (tick.t_DealStatus <> 20) then 'x' "
                  "end as t_Ras, "
                  "decode(tick.t_Netting, 1, 'Поставка', 'Неттинг') as t_Nett, "
                  "NVL(RSB_FIINSTR.ConvSum(1, leg.t_PFI, 0, leg.t_Maturity, 0), 0) as t_RateBaseIsp, "
                  "NVL(RSB_FIINSTR.ConvSum(1, leg.t_CFI, 0, leg.t_Maturity, 0), 0) as t_RateContrIsp, "
                  "NVL(RSB_FIINSTR.ConvSum(leg.t_Cost, leg.t_CFI, 0, leg.t_Maturity, 1), 0) as t_StContrIsp, "
                  "NVL(RSB_FIINSTR.ConvSum(leg.t_Principal, leg.t_PFI, 0, leg.t_Maturity, 1), 0) as t_StBaseIsp, "
                  "'','','','' "
                  " from dfx_deals_dbt fxdeals, ddl_tick_dbt tick, ddl_leg_dbt leg, "
                  " ( "
                  "    select tick.t_DealID, paym.t_PaymStatus, paym.t_ValueDate, decode(paym.t_Purpose, 1, 0, 1) as t_LegID "
                  "    from ddl_tick_dbt tick, dpmpaym_dbt paym "
                  "    where "
                  "    (paym.t_DocumentID = tick.t_DealID) "
                  "    and(paym.t_DocKind = tick.t_BOfficeKind) "
                  "    and(tick.t_BOfficeKind = 100) "
                  "    and(paym.t_Purpose in (1,3)) "
                  " ) paym "
                  " where "
                  "(tick.t_DealID = fxdeals.t_DealID) "
                  "and(leg.t_DealID = tick.t_DealID) "
                  "and(leg.t_LegKind = 0) "
                  "and(paym.t_DealID = tick.t_DealID) "
                  "and(paym.t_LegID = leg.t_LegID)";

        if (PanelRepParm.flagPeriod)
            Query2 = Query2 +      
                     "and(leg.t_Start <= '" + String(PanelRepParm.Date2) + "') " +
                     "and(leg.t_Maturity >= '" + String(PanelRepParm.Date1) + "')";
        elif (PanelRepParm.flagDate)
            Query2 = Query2 +
                     "and(leg.t_Start <= '" + String(PanelRepParm.Date3) + "') " +
                     "and(leg.t_Maturity >= '" + String(PanelRepParm.Date3) + "')";
        end;

        if (PanelRepParm.DealNum != "")
            Query2 = Query2 +
                     "and(tick.t_DealCode = '" + String(PanelRepParm.DealNum) + "')";
        end;

        if (PanelRepParm.flagDealType)
            Query2 = Query2 +
                     "and((leg.t_Start + 2) < leg.t_Maturity)";
        end;

        Query2 = Query2 +
                  " order by tick.t_DealID, leg.t_LegID";

        PrintReport(PanelRepParm, Query1 + Query2);

        if (flagNoRate4SomeCurr)
            msgbox("Для некоторых валют не задан курс.");
        end;
    end;
END;


PreparePrintReport();
exit(1);
