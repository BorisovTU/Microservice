/*
$Name:        dl_util_xml_obj.mac
$Module:      Ядро Securities
$Description: Утилиты для формирования отчетов в формате XML
*/

//─────────────────────────────────────────────────────────────────────────────┐
// Утилиты для формирования отчетов в формате XML.                             │
// (на основе макроса util_xml.mac)                                            │
//                                                                             │
// Создан:    Шабашов Ю.В., 14.03.2013                                         │
// Изменения:                                                                  │
//─────────────────────────────────────────────────────────────────────────────┘
import "dlquery.mac", "CbRepFun.mac", "dlmisc.mac";

/* Формирование полного кассового символа (с ведущими пробелами) */
macro lpad( str, len )
  var chr;
  if ( not GetParm(2, chr))
     chr = " ";
  end;
  str = Trim( str );
  return MkStr(chr, len - StrLen(str)) + str;
end;

//─────────────────────────────────────────────────────────────────────────────┐
//                               MACRO                                         │
//─────────────────────────────────────────────────────────────────────────────┘
PRIVATE MACRO Nvl()
   private var l_Var, l_Val;
   GetParm(0, l_Var);
   GetParm(1, l_Val);
   SetParm(0, IIF((ValType(l_Var) == V_UNDEF), l_Val, l_Var));
END;  // Nvl() 

Macro NotNull ( val )
    if ( val == NULL ) return false; end;
    if ( ValType (val) == 26 ) return false; end;
    return true;
End;
//-----------------------------------------------------------------------------------------

Macro Sql_String ( str )
    if ( NotNull (str) and (str != StrFor (0)) and (str != StrFor (1)) )
        return str;
    end;
    return "";
End;
//-----------------------------------------------------------------------------------------

Macro Sql_Date ( dt )
    if ( NotNull (dt) and (dt != Date (2,1,1)-1) )
        return Date (dt);
    end;
    return Date (0,0,0);

End;
//-----------------------------------------------------------------------------------------

Macro _SqlTime ( tm )
    if ( NotNull (tm) )
        return Time (tm);
    end;
    return Time (0,0,0);

End;
//-----------------------------------------------------------------------------------------

Macro Sql_Money ( sum )
    if ( NotNull (sum) )
        return Money (sum);
    end;
    return $0;
End;
//-----------------------------------------------------------------------------------------

Macro Sql_Integer ( num )
    if ( NotNull (num) )
        return Int (num);
    end;
    return 0;
End;


//─────────────────────────────────────────────────────────────────────────────┐
//                 Класс для стиля                                             │
//─────────────────────────────────────────────────────────────────────────────┘
CLASS T_XML_Style
   var Name:String, DataType:String, Brl, Brr, Brt, Brb, Ha, Va, FnName, FnSize, FnBld, FnItl, FnUnd;
   var Point, Color, Indent, InteriorColor;
   var WrapText;
   var SignPrefix;
   
   MACRO Init(p_Name, p_DataType, p_Brl, p_Brr, p_Brt, p_Brb, p_Ha, p_Va, p_FnName, p_FnSize, p_FnBld, p_FnItl, p_FnUnd, p_Point, p_Color, p_Indent, p_InteriorColor, p_WrapText, p_SignPrefix)
      Name          = p_Name;
      DataType      = p_DataType;
      Brl           = p_Brl;     
      Brr           = p_Brr;     
      Brt           = p_Brt;     
      Brb           = p_Brb;     
      Ha            = p_Ha;      
      Va            = p_Va;      
      FnName        = p_FnName;  
      FnSize        = p_FnSize;  
      FnBld         = p_FnBld;   
      FnItl         = p_FnItl;   
      FnUnd         = p_FnUnd;
      Point         = p_Point;
      Color         = p_Color;
      Indent        = p_Indent;
      InteriorColor = p_InteriorColor;
      WrapText      = p_WrapText;
      if (p_SignPrefix != null)
        SignPrefix    = p_SignPrefix;
      else
        SignPrefix    = "";
      end;
   END;
   
END;  // T_XML_Style()


//─────────────────────────────────────────────────────────────────────────────┐
//    Класс для условного форматирования                                       │
//─────────────────────────────────────────────────────────────────────────────┘
CLASS T_Format_Condition
   var v_Range, v_Qualif, v_Value, v_Color, v_Pattern; 
   //───────────────────────────────────────────────────────────────────────────
   MACRO Init(l_Range, l_Qualif, l_Value, l_Color, l_Pattern)
      v_Range = l_Range;
      if (l_Qualif == ">")
         v_Qualif = "Greater";
      elif ((l_Qualif == "=") or (l_Qualif == "=="))
         v_Qualif = "Equal";
      elif ((l_Qualif == "!=") or (l_Qualif == "<>"))
         v_Qualif = "NotEqual";
      elif (l_Qualif == "<")
         v_Qualif = "Less";
      elif (l_Qualif == "<=")
         v_Qualif = "LessOrEqual";
      elif (l_Qualif == ">=")
         v_Qualif = "GreaterOrEqual";
      else
         v_Qualif = l_Qualif;
      end;
      v_Value = l_Value;
      v_Color = l_Color;
      Nvl(v_Color, "red");
      v_Pattern = l_Pattern; 
      Nvl(v_Pattern, "white");
   END;
   //───────────────────────────────────────────────────────────────────────────
END;  // T_Format_Condition()  


//─────────────────────────────────────────────────────────────────────────────┐
//                 Основной класс объекта                                      │
//─────────────────────────────────────────────────────────────────────────────┘
CLASS T_Xml_Rep;
   var xml_with:TArray, a_Styles:TArray, a_Rep_Strings;
   var a_Format;
   var a_Header:TArray, a_Data:TArray;
   private var a_PageBreaks:TArray;
   private var l_Delim, g_Filter_Range;
   var a_Style_Name:TArray;
   var Rep_Font_Name:String, Rep_Font_Size:Integer;
   var Row_Count:Integer=0;
   var String_Count:Integer=0;
   var Rep_Type:Integer=0;
   var Indent_Value:Integer=1;
   var WrapText_Value:Integer=0;
   var SplitHorizontal:Integer=0;
   var TopRowBottomPane:Integer=0;
   var l_Orientation=FALSE;
   var l_Zoom:Integer=0;
   var l_PrintScale:Integer=100;
   private var r2;
   private var lf_File_Name: String;
   private var m_UseApostr = false;
   var a_Cell_Names; // Массив с именованными ячейками
   var u_Rep_Sheet_Name:String;  // Наименование листа
   var u_Current_Column:Integer;
   var u_Current_Row:Integer;
   
   //───────────────────────────────────────────────────────────────────────────
   MACRO Set_Indent
      private var l_Indent;
      GetParm(1, l_Indent); Nvl(l_Indent, 1);
      Indent_Value = l_Indent;
   END;  // Set_Indent

   MACRO Set_WrapText
      private var l_WrapText;
      GetParm(1, l_WrapText); Nvl(l_WrapText, 1);
      WrapText_Value = l_WrapText;
   END;  // Set_WrapText

   //───────────────────────────────────────────────────────────────────────────
   MACRO Set_Landscape
      private var l_Val;
      GetParm(1, l_Val); Nvl(l_Val, TRUE);
      l_Orientation = l_Val;
   END;  // Set_Landscape
   //───────────────────────────────────────────────────────────────────────────
   MACRO Set_Zoom
      private var l_Val;
      GetParm(1, l_Val); Nvl(l_Val, 0);
      l_Zoom = l_Val;
   END;  // Set_Zoom
   //───────────────────────────────────────────────────────────────────────────

   MACRO Set_Filter
      private var l_Filter;
      GetParm(1, l_Filter); Nvl(l_Filter, "");
      g_Filter_Range = l_Filter;
   END;  // Set_Filter

   MACRO Set_Name(p_Encode);
      private var sNameReport="", Value_err;
      
      GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, sNameReport, Value_err);
      lf_File_Name = String(sNameReport,  "\\", Trim(StrSubst(String(Date), ".", "")), Trim(StrSubst(String(Time), ":", "")), UserNumber, ".xml");
      
      if (ValType(p_Encode) == V_UNDEF)
         p_Encode = "rsoem";
      end; 
      r2 = TStreamDoc(lf_File_Name, "c", p_Encode);
      lf_File_Name = r2.Name;
   END;
 
   //───────────────────────────────────────────────────────────────────────────
   // Макрос инициализации.
   //───────────────────────────────────────────────────────────────────────────
   MACRO Init(p_Encode, p_UseApostr, p_PrintScale)
      xml_with = TArray(true, 200, 200);
      xml_with.Size = 0;
      a_Styles = TArray(true, 200, 200);
      a_Styles.Size = 0;
      a_Header = TArray(true, 200, 200);
      a_Header.Size = 0;
      a_Data   = TArray(true, 200, 200);
      a_Data.Size = 0;
      a_Style_Name = TArray(true, 200, 200);
      a_Style_Name.Size = 0;
      a_Format = TArray(true, 200, 200);
      a_Format.Size = 0;
      Set_Name(p_Encode);
      m_UseApostr = p_UseApostr;
      a_PageBreaks = TArray(true, 200, 200);
      a_PageBreaks.size = 0;

      l_PrintScale = 100;
      if(ValType(p_PrintScale) != V_UNDEF)
        l_PrintScale = p_PrintScale;
      end;
      a_Cell_Names = TArray(true, 200, 200);
      a_Cell_Names.Size = 0;
      u_Current_Column = 0;
      u_Current_Row = 0;
   END;  // Init;

   //───────────────────────────────────────────────────────────────────────────
   MACRO Add_XML_String(str)
      a_Rep_Strings(a_Rep_Strings.Size) = str;
   END;  // Add_XML_String

   /*Добавить разрыв страницы в указанную строку. Если номер строки не передан, то используется текущая
     Для использования разрывов необходимо указывать параметр p_PrintScale при инициализации объекта ф-цией Init,
     так как в этом случае Excel сам добавляет свою разбивку страниц, и колонки таблиц могу оказаться на разных страницах.
     Чтобы этого не произошло нужно "поиграть" с масштабом, чтобы таблица в ширину полностью помещалась на страницу (если это требуется, конечно).
   */
   MACRO Add_PageBreak(Row_Num)
     var Row = Row_Count;
     
     if(ValType(Row_Num) == V_INTEGER)
       Row = Row_Num;
     end;

     a_PageBreaks[a_PageBreaks.size] = Row;
   END;


//───────────────────────────────────────────────────────────────────────────
   MACRO Push_Names()
      private var i=0;
      IF (a_Cell_Names.Size > 0)
         this.Add_XML_String(" <Names>");
         While (i < a_Cell_Names.Size)
            this.Add_XML_String(a_Cell_Names(i));
            i = i + 1;
         End;
         this.Add_XML_String(" </Names>");
      END;
   END;  // Push_Names;
   //───────────────────────────────────────────────────────────────────────────
   MACRO Push_Styles()
      private var ind:Integer=0, l_Color;

      a_Rep_Strings = a_Styles;
      Rep_Type      = 1;
      
      this.Add_XML_String("  </Style>");

      While(ind < a_Style_Name.Size)
         if   (a_Style_Name(ind).Color == "R")
            l_Color = "#FF0000";
         elif (a_Style_Name(ind).Color == "G")
            l_Color = "#00FF00";
         elif (a_Style_Name(ind).Color == "B")
            l_Color = "#0000FF";
/*ak*/
         elif (a_Style_Name(ind).Color == "W")
            l_Color = "#FFFFFF";
/*~ak*/
         else
            l_Color = "#000000";
         end;
         this.Add_XML_String(String("  <Style ss:ID=\"", a_Style_Name(ind).Name, "\">"));
         this.Add_XML_String(String("   <Alignment ss:Horizontal=\""
                                   ,a_Style_Name(ind).Ha
                                   ,"\" ss:Vertical=\""
                                   ,a_Style_Name(ind).Va
                                   ,"\" ss:Indent=\"", a_Style_Name(ind).Indent, "\" ss:WrapText=\""+a_Style_Name(ind).WrapText+"\"/>")
                                   );
         if ((a_Style_Name(ind).Brl)
         or  (a_Style_Name(ind).Brr)       
         or  (a_Style_Name(ind).Brt)       
         or  (a_Style_Name(ind).Brb)
            )       
            this.Add_XML_String("   <Borders>");
            if (a_Style_Name(ind).Brb)
               this.Add_XML_String("    <Border ss:Position=\"Bottom\" ss:LineStyle=\"Continuous\" ss:Weight=\"1\"/>");
            end;
            if (a_Style_Name(ind).Brl)
               this.Add_XML_String("    <Border ss:Position=\"Left\" ss:LineStyle=\"Continuous\" ss:Weight=\"1\"/>");
            end;
            if (a_Style_Name(ind).Brr)
               this.Add_XML_String("    <Border ss:Position=\"Right\" ss:LineStyle=\"Continuous\" ss:Weight=\"1\"/>");
            end;
            if (a_Style_Name(ind).Brt)
               this.Add_XML_String("    <Border ss:Position=\"Top\" ss:LineStyle=\"Continuous\" ss:Weight=\"1\"/>");
            end;
            this.Add_XML_String("   </Borders>");
         end;
         
         if ((a_Style_Name(ind).FnName != Rep_Font_Name)
         or  (a_Style_Name(ind).FnSize != Rep_Font_Size)
         or  (a_Style_Name(ind).FnItl)
         or  (a_Style_Name(ind).FnBld)
         or  (a_Style_Name(ind).FnUnd)
         or  (l_Color != "#000000")
            )
            this.Add_XML_String(String("   <Font ss:FontName=\""
                                      ,a_Style_Name(ind).FnName
                                      ,"\" x:CharSet=\"204\" x:Family=\"Roman\""));
            this.Add_XML_String(String("    ss:Size=\""
                                      ,a_Style_Name(ind).FnSize
                                      ,"\" ss:Color=\"", l_Color, "\""
                                      ,IIF(a_Style_Name(ind).FnBld, " ss:Bold=\"1\"", "")
                                      ,IIF(a_Style_Name(ind).FnItl, " ss:Italic=\"1\"", "")
                                      ,IIF(a_Style_Name(ind).FnUnd, " ss:Underline=\"Single\"", "")
                                      ,"/>"));
         end;
         
         if (a_Style_Name(ind).InteriorColor != "")
            this.Add_XML_String(String("   <Interior ss:Color=\"#", a_Style_Name(ind).InteriorColor,"\" ss:Pattern=\"Solid\"/>"));
         end;

         if   (a_Style_Name(ind).DataType == "String")
            this.Add_XML_String("   <NumberFormat ss:Format=\"@\"/>");
         elif   (a_Style_Name(ind).DataType == "Account")
            this.Add_XML_String("   <NumberFormat ss:Format=\"##### ### # #### #######\"/>");
         elif   (a_Style_Name(ind).DataType == "Time")
            this.Add_XML_String("   <NumberFormat ss:Format=\"dd/mm/yyyy\ h:mm:ss;@\"/>");
         elif (a_Style_Name(ind).DataType == "Money")
            this.Add_XML_String("   <NumberFormat ");
            this.Add_XML_String(String("    ss:Format=\""+ a_Style_Name(ind).SignPrefix +"#"+IIF(m_UseApostr, ",", "")+"##0"
                                      ,IIF(a_Style_Name(ind).Point > 0, ".", "")
                                      ,MkStr("0", a_Style_Name(ind).Point)
                                      ,";\\-#"+IIF(m_UseApostr, ",", "")+"##0"
                                      ,IIF(a_Style_Name(ind).Point > 0, ".", "")
                                      ,MkStr("0", a_Style_Name(ind).Point)
                                      ,"\"/>"
                                      ));
         elif (a_Style_Name(ind).DataType == "DateTime")
/*ak
            this.Add_XML_String("   <NumberFormat ss:Format=\"Short Date\"/>");*/
            if(a_Style_Name(ind).Point == 1)
              this.Add_XML_String("   <NumberFormat ss:Format=\"dd/mm/yy;@\"/>");
            else
              this.Add_XML_String("   <NumberFormat ss:Format=\"Short Date\"/>");
            end;
/*~ak*/
         elif (a_Style_Name(ind).DataType == "Procent")
            if   (a_Style_Name(ind).Point == 2)
               this.Add_XML_String("   <NumberFormat ss:Format=\"Percent\"/>");
            else
               this.Add_XML_String(String("   <NumberFormat ss:Format=\"0"
                                         ,IIF(a_Style_Name(ind).Point > 0, ".", "")
                                         ,MkStr("0", a_Style_Name(ind).Point)
                                         ,"%\"/>"
                                         ));
            end;
         elif (a_Style_Name(ind).DataType == "Number")
            this.Add_XML_String(String("   <NumberFormat ss:Format=\"0"
                                      ,IIF(a_Style_Name(ind).Point > 0, ".", "")
                                      ,MkStr("0", a_Style_Name(ind).Point)
                                      ,"\"/>"
                                      ));
         end;
         
         this.Add_XML_String("  </Style>");
      
         ind = ind + 1;
      End;
      this.Add_XML_String(" </Styles>");
   END;  // Push_Styles

   MACRO Add_String(p_String)
      r2.WriteLine(p_String);
   END;

   //───────────────────────────────────────────────────────────────────────────
   MACRO Push_Strings()
      private var ind:Integer=0, l_Found=FALSE, tmp, l_Cnt:Integer=0;
      private var l_Str:String;
      this.Push_Styles;
      this.Push_Names;
      While(ind < a_Header.Size)
         this.Add_String(a_Header(ind));
         ind = ind + 1;
      End;
      ind = 0;
      While(ind < a_Styles.Size)
         this.Add_String(a_Styles(ind));
         ind = ind + 1;
      End;
      ind = 0;
      While(ind < a_Data.Size)
         if (NOT l_Found)
            if (Index(a_Data(ind), "ExpandedRowCount") > 0)
               a_Data(ind) = StrSubst(a_Data(ind), "10000000", String(Row_Count));
               l_Found = TRUE;
            end;
         end;
         this.Add_String(a_Data(ind));
         ind = ind + 1;
      End;
   END;  // Push_Strings
   //───────────────────────────────────────────────────────────────────────────
   MACRO Add_Style()
      private var p_Name, p_DataType, p_Brl, p_Brr, p_Brt, p_Brb, p_Ha, p_Va, p_FnName, p_FnSize, p_FnBld, p_FnItl, p_FnUnd, p_Point, p_Color, p_Indent, p_InteriorColor, p_WrapText, p_SignPrefix;
      GetParm(1,  p_DataType); Nvl(p_DataType, "String"); 
      GetParm(2,  p_Brl);      Nvl(p_Brl,      FALSE);
      GetParm(3,  p_Brr);      Nvl(p_Brr,      FALSE);
      GetParm(4,  p_Brt);      Nvl(p_Brt,      FALSE);
      GetParm(5,  p_Brb);      Nvl(p_Brb,      FALSE);
      GetParm(6,  p_Ha);       
      GetParm(7,  p_Va);       Nvl(p_Va,       "Top");
      GetParm(8,  p_FnName);   Nvl(p_FnName,   Rep_Font_Name);
      GetParm(9,  p_FnSize);   Nvl(p_FnSize,   Rep_Font_Size);
      GetParm(10, p_FnBld);    Nvl(p_FnBld,    FALSE);
      GetParm(11, p_FnItl);    Nvl(p_FnItl,    FALSE);
      GetParm(12, p_FnUnd);    Nvl(p_FnUnd,    FALSE);
      if (p_DataType == "String")
         Nvl(p_Ha, "Left");
      elif ((p_DataType == "Procent")
      or    (p_DataType == "Money")
      or    (Substr(p_DataType, 1, 6) == "Number")
           )
         Nvl(p_Ha, "Right");
      else 
         Nvl(p_Ha, "Center");
      end;
      GetParm(13, p_Point);
      if ((p_DataType == "Money")
      or  (p_DataType == "Procent")
         ) 
         Nvl(p_Point, 2);
      else
         Nvl(p_Point, 0);
      end;
      GetParm(14, p_Color);  Nvl(p_Color, "");
      GetParm(15, p_Indent); Nvl(p_Indent, Indent_Value);
      GetParm(16, p_InteriorColor); Nvl(p_InteriorColor, "");
      GetParm(17, p_WrapText); Nvl(p_WrapText, WrapText_Value);
      GetParm(18, p_SignPrefix); Nvl(p_SignPrefix, "");
      
      if   (Substr(p_Va, 1, 1) == "T") p_Va = "Top"; 
      elif (Substr(p_Va, 1, 1) == "C") p_Va = "Center"; 
      elif (Substr(p_Va, 1, 1) == "B") p_Va = "Bottom"; 
      elif (Substr(p_Va, 1, 1) == "J") p_Va = "Justify"; 
      end;
      if   (Substr(p_Ha, 1, 1) == "L") p_Ha = "Left"; 
      elif (Substr(p_Ha, 1, 1) == "C") p_Ha = "Center"; 
      elif (Substr(p_Ha, 1, 1) == "R") p_Ha = "Right"; 
      elif (Substr(p_Ha, 1, 1) == "J") p_Ha = "Justify"; 
      end;
         
      private var ind=0, val:T_XML_Style;
      While (ind < a_Style_Name.Size)
         if ((a_Style_Name(ind).DataType      == p_DataType)
         and (a_Style_Name(ind).Brl           == p_Brl)
         and (a_Style_Name(ind).Brr           == p_Brr)
         and (a_Style_Name(ind).Brt           == p_Brt)
         and (a_Style_Name(ind).Brb           == p_Brb)
         and (a_Style_Name(ind).Ha            == p_Ha)
         and (a_Style_Name(ind).Va            == p_Va)
         and (a_Style_Name(ind).FnName        == p_FnName)
         and (a_Style_Name(ind).FnSize        == p_FnSize)
         and (a_Style_Name(ind).FnBld         == p_FnBld)
         and (a_Style_Name(ind).FnItl         == p_FnItl)
         and (a_Style_Name(ind).FnUnd         == p_FnUnd)
         and (a_Style_Name(ind).Point         == p_Point)
         and (a_Style_Name(ind).Color         == p_Color)
         and (a_Style_Name(ind).Indent        == p_Indent)
         and (a_Style_Name(ind).InteriorColor == p_InteriorColor)
         and (a_Style_Name(ind).WrapText      == p_WrapText)
         and (a_Style_Name(ind).SignPrefix    == p_SignPrefix)
            )         
            Return a_Style_Name(ind).Name;
         end;
         ind = ind + 1;
      End;
      p_Name = String("s_", a_Style_Name.Size);
      val.Init(p_Name, p_DataType, p_Brl, p_Brr, p_Brt, p_Brb, p_Ha, p_Va, p_FnName, p_FnSize, p_FnBld, p_FnItl, p_FnUnd, p_Point, p_Color, p_Indent, p_InteriorColor, p_WrapText, p_SignPrefix);
      a_Style_Name(a_Style_Name.Size) = val;
      Return p_Name;
   END;
   //───────────────────────────────────────────────────────────────────────────
   PRIVATE MACRO Sql_Num(val)
      private var v_Point;
      GetParm(1, v_Point);
      if (ValType(v_Point) == V_UNDEF)
         v_Point = 0;
      end;
      if (NotNull(val))
         if (v_Point == 0)
            Return Int(val);
         else
            Return Double(val);
         end;
      end;
      Return 0;
   END;  // Sql_Num() 
   //───────────────────────────────────────────────────────────────────────────
   MACRO Print_Bottom()
      private var v_New_Sheet, v_Filter_Range, v_Format_Range, ind:Integer=0;
      GetParm(1, v_New_Sheet);    Nvl(v_New_Sheet, FALSE);
      GetParm(2, v_Filter_Range); Nvl(v_Filter_Range, g_Filter_Range);
      this.Add_XML_String("  </Table>");
      this.Add_XML_String("  <WorksheetOptions xmlns=\"urn:schemas-microsoft-com:office:excel\">");
      this.Add_XML_String("   <PageSetup>");
      if (l_Orientation)
         this.Add_XML_String("    <Layout x:Orientation=\"Landscape\"/>");
      end;
      //this.Add_XML_String("    <Header x:Margin=\"0.15\"/>");
      //this.Add_XML_String("    <Footer x:Margin=\"0.15\"/>");
      //this.Add_XML_String("    <PageMargins x:Bottom=\"0.3\" x:Left=\"0.71\" x:Right=\"0.25\" x:Top=\"0.31\"/>");
      this.Add_XML_String("   </PageSetup>");
      //this.Add_XML_String("   <Unsynced/>");

      //Если указаны разывы, то авторастягивание таблиц по ширине страницы не указываем, т.к. в это случае разрывы не сработают.
      if(a_PageBreaks.size == 0)
      this.Add_XML_String("   <FitToPage/>");
      end;

      this.Add_XML_String("   <Print>");
      this.Add_XML_String("   <FitHeight>9999</FitHeight>");
      this.Add_XML_String("   <ValidPrinterInfo/>");
      this.Add_XML_String("   <PaperSizeIndex>9</PaperSizeIndex>");
      this.Add_XML_String("   <Scale>"+l_PrintScale+"</Scale>");
      this.Add_XML_String("   <HorizontalResolution>-1</HorizontalResolution>");
      this.Add_XML_String("   <VerticalResolution>-1</VerticalResolution>");
      this.Add_XML_String("   </Print>");

      this.Add_XML_String("   <Selected/>");
      if (this.SplitHorizontal > 0)
         this.Add_XML_String(String("   <SplitHorizontal>", this.SplitHorizontal, "</SplitHorizontal>"));
         if (this.TopRowBottomPane > 0)
            this.Add_XML_String(String("   <TopRowBottomPane>", this.TopRowBottomPane, "</TopRowBottomPane>"));
         end;
      end;
      
      if (l_Zoom > 0)
         this.Add_XML_String(String("   <Zoom>", l_Zoom, "</Zoom>"));
      end;
      
      this.Add_XML_String("   <Panes>");
      this.Add_XML_String("    <Pane>");
      this.Add_XML_String("     <Number>3</Number>");
      this.Add_XML_String("     <ActiveRow>0</ActiveRow>");
      this.Add_XML_String("     <ActiveCol>0</ActiveCol>");
      this.Add_XML_String("    </Pane>");
      this.Add_XML_String("   </Panes>");
      this.Add_XML_String("   <ProtectObjects>False</ProtectObjects>");
      this.Add_XML_String("   <ProtectScenarios>False</ProtectScenarios>");
      this.Add_XML_String("  </WorksheetOptions>");

      if (a_Format.Size > 0)
         While (ind < a_Format.Size)
            this.Add_XML_String("  <ConditionalFormatting xmlns=\"urn:schemas-microsoft-com:office:excel\">");
            this.Add_XML_String(String("   <Range>", a_Format[ind].v_Range, "</Range>"));
            this.Add_XML_String("   <Condition>");
            this.Add_XML_String(String("    <Qualifier>", a_Format[ind].v_Qualif, "</Qualifier>"));
            this.Add_XML_String(String("    <Value1>", a_Format[ind].v_Value, "</Value1>"));
            this.Add_XML_String(String("    <Format Style='color:", a_Format[ind].v_Color, ";mso-pattern:", a_Format[ind].v_Pattern, "'/>"));
            this.Add_XML_String("   </Condition>");
            this.Add_XML_String("  </ConditionalFormatting>");
            ind = ind + 1;
         End;
         a_Format.Size = 0;
      end;
      
      if (v_Filter_Range != "")
         this.Add_XML_String(String("  <AutoFilter x:Range=\"", v_Filter_Range ,"\""));
         this.Add_XML_String("   xmlns=\"urn:schemas-microsoft-com:office:excel\">");
         this.Add_XML_String("  </AutoFilter>");
         g_Filter_Range = "";
      end;

      if(a_PageBreaks.size > 0)
        var i = 0;

        this.Add_XML_String("  <PageBreaks xmlns=\"urn:schemas-microsoft-com:office:excel\">");
        this.Add_XML_String("   <RowBreaks>");
        while(i < a_PageBreaks.size)
          this.Add_XML_String("    <RowBreak>");
          this.Add_XML_String("     <Row>"+a_PageBreaks[i]+"</Row>");
          this.Add_XML_String("    </RowBreak>");

          i = i + 1;
        end;

        this.Add_XML_String("   </RowBreaks>");
        this.Add_XML_String("  </PageBreaks>");
      end;

      this.Add_XML_String(" </Worksheet>");
      if (NOT v_New_Sheet)
         this.Add_XML_String("</Workbook>");
         this.Push_Strings();
      end;
      
   END;  // Print_Bottom()
   //───────────────────────────────────────────────────────────────────────────
   PRIVATE MACRO Print_Document_Header()
      a_Rep_Strings = a_Header;
      Rep_Type      = 0;
      this.Add_XML_String("<?xml version=\"1.0\"?>");
      this.Add_XML_String("<?mso-application progid=\"Excel.Sheet\"?>");
      this.Add_XML_String("<Workbook xmlns=\"urn:schemas-microsoft-com:office:spreadsheet\"");
      this.Add_XML_String(" xmlns:o=\"urn:schemas-microsoft-com:office:office\"");
      this.Add_XML_String(" xmlns:x=\"urn:schemas-microsoft-com:office:excel\"");
      this.Add_XML_String(" xmlns:ss=\"urn:schemas-microsoft-com:office:spreadsheet\"");
      this.Add_XML_String(" xmlns:html=\"http://www.w3.org/TR/REC-html40\">");
      this.Add_XML_String(" <DocumentProperties xmlns=\"urn:schemas-microsoft-com:office:office\">");
      this.Add_XML_String("  <Author>RSL User</Author>");
      this.Add_XML_String("  <LastAuthor>RSL User</LastAuthor>");
      this.Add_XML_String("  <Created>2012-03-06T04:52:03Z</Created>");
      this.Add_XML_String("  <Version>14.00</Version>");
      this.Add_XML_String(" </DocumentProperties>");
      this.Add_XML_String(" <OfficeDocumentSettings xmlns=\"urn:schemas-microsoft-com:office:office\">");
      this.Add_XML_String("  <AllowPNG/>");
      this.Add_XML_String(" </OfficeDocumentSettings>");
      this.Add_XML_String(" <ExcelWorkbook xmlns=\"urn:schemas-microsoft-com:office:excel\">");
      this.Add_XML_String("  <WindowHeight>12390</WindowHeight>");
      this.Add_XML_String("  <WindowWidth>23955</WindowWidth>");
      this.Add_XML_String("  <WindowTopX>480</WindowTopX>");
      this.Add_XML_String("  <WindowTopY>60</WindowTopY>");
      this.Add_XML_String("  <RefModeR1C1/>");
      this.Add_XML_String("  <ProtectStructure>False</ProtectStructure>");
      this.Add_XML_String("  <ProtectWindows>False</ProtectWindows>");
      this.Add_XML_String(" </ExcelWorkbook>");
      this.Add_XML_String(" <Styles>");
      this.Add_XML_String("  <Style ss:ID=\"Default\" ss:Name=\"Normal\">");
      this.Add_XML_String("   <Alignment ss:Vertical=\"Bottom\"/>");
      this.Add_XML_String("   <Borders/>");
      this.Add_XML_String(String("   <Font ss:FontName=\"", rep_Font_Name, "\" x:CharSet=\"204\" x:Family=\"Swiss\" ss:Size=\"", rep_Font_Size ,"\""));
      this.Add_XML_String("    ss:Color=\"#000000\"/>");
      this.Add_XML_String("   <Interior/>");
      this.Add_XML_String("   <NumberFormat/>");
      this.Add_XML_String("   <Protection/>");
   END;  // Print_Document_Header() 
   //──────────────────────────────────────────────────────────────────────────┐
   //                               MACRO                                      │
   // Печать заголовка XML файла и стилей.                                     │
   // 0 - наименование отчета (листа)                                          │
   // 1 - наименование шрифта                                                  │
   // 2 - размер шрифта                                                        │
   // 3 - FALSE - новый файл, TRUE - новый лист                                │
   // 4 - диапазон для фильтра, если пусто, то без фильтра. Пример "R8C1:R8C12"│
   //──────────────────────────────────────────────────────────────────────────┘
   MACRO Print_Header()
      private var i=0, v_Name, v_New_Sheet=NULL, l_Filter_Range;
      GetParm(1, v_Name);         Nvl(v_Name,      "Лист 666");
      GetParm(2, Rep_Font_Name);  Nvl(Rep_Font_Name, "Calibri");
      GetParm(3, Rep_Font_Size);  Nvl(Rep_Font_Size, 11);
      GetParm(4, v_New_Sheet);    Nvl(v_New_Sheet, FALSE);
      GetParm(5, l_Filter_Range); Nvl(l_Filter_Range, "");
      u_Rep_Sheet_Name = v_Name;
      
      if (NOT v_New_Sheet)
         Print_Document_Header();
      end;

      a_Rep_Strings = a_Data;
      Rep_Type      = 2;
      if (v_New_Sheet)
         this.Print_Bottom(TRUE);
      end;

      g_Filter_Range = l_Filter_Range;
      this.Add_XML_String(String(" <Worksheet ss:Name=\"", v_Name, "\">"));
      this.Add_XML_String(String("  <Table>"));
      // Массив с шириной колонок!
      While (i < xml_with.Size)
         if (xml_with(i) > 0)
            this.Add_XML_String(String("   <Column ss:AutoFitWidth=\"1\" ss:Width=\"", StrSubst(String(xml_with(i)), ",", "."), "\"/>"));
         else
            this.Add_XML_String(String("   <Column ss:Hidden=\"1\" ss:AutoFitWidth=\"0\" ss:Width=\"40\"/>"));
         end;
         i = i + 1;
      End;
      u_Current_Row = 1;
   END;  // Print_Header() 
   //───────────────────────────────────────────────────────────────────────────
   MACRO New_Line()
      private var row_height;
      GetParm(1, row_height);
      if (ValType(row_height) == V_UNDEF)
         this.Add_XML_String(String("   <Row ss:AutoFitHeight=\"1\">"));
      else
         this.Add_XML_String(String("   <Row ss:Height=\"", row_height ,"\">"));
      end;
      Row_Count = Row_Count + 1;
      u_Current_Column = 0;
   END;  // New_Line()  
   //───────────────────────────────────────────────────────────────────────────
   MACRO End_Line()
      this.Add_XML_String("   </Row>");
      u_Current_Row = u_Current_Row + 1;
   END;  // End_Line()  
   //───────────────────────────────────────────────────────────────────────────
   MACRO Table_Header()
      private var i=2, l_Height, v_Str;
      GetParm(1, l_Height);
      this.New_Line(l_Height);
      While(GetParm(i, v_Str))
         this.Put_Val(v_Str, "VA=C;HA=C;BOR");
         i = i + 1;
      End;
      this.End_Line; 
   END;  // Table_Header() 
   //───────────────────────────────────────────────────────────────────────────
   PRIVATE MACRO Value_From_String(str, val)
      private var s:String, l_Pos:Integer, l_Ret:String = "";
      l_Pos = Index(str, val);
      if (l_Pos > 0)
         s  = Substr(str, l_Pos + Strlen(val));
         l_Pos = Index(s, l_Delim);
         if (l_Pos > 0) 
            l_Ret = Substr(s, 1, l_Pos-1);
         else
            l_Ret = Substr(s, 1);
         end;
         SetParm(3, l_Ret);
         Return TRUE;
      end;
      Return FALSE;
   END;  // Value_From_String() 
   //──────────────────────────────────────────────────────────────────────────┐
   //
   //                               MACRO                                      │
   MACRO Add_Cell_Name(p_Name:String, p_Cell:String)
      var p_Sheet_Name:String;
      GetParm(3, p_Sheet_Name);
      Nvl(p_Sheet_Name, u_Rep_Sheet_Name);
      a_Cell_Names(a_Cell_Names.Size) = String("  <NamedRange ss:Name=\"", p_Name, "\" ss:RefersTo=\"='", p_Sheet_Name, "'!", p_Cell, "\"/>");
   END;  // Add_Cell_Name;
   //──────────────────────────────────────────────────────────────────────────┐
   //
   // 1 - значение выводимой переменной                                        │  
   // 2 - строка параметров                                                    │  
   // 3 - если начало строки, то можно указать ее высоту                       │  
   // 4 - разделитель (по-умолчанию ";")                                       │  
   //                                                                          │ 
   // Вывод переменной любого типа с параметрами, описанными в одной строке.   │
   // Ключевые слова:                                                          │ 
   // START - переменная выводится с новой строки                              │ 
   // END   - после вывода переменной вставляется тэг окончания строки         │
   // STEN  - тэг начала и окончания строки, значение выводится в одну строку  │
   // BOR   - рисуется рамка                                                   │
   // BRL   - рисуется граница слева                                           │
   // BRT   - рисуется граница сверху                                          │
   // BRR   - рисуется граница справа                                          │
   // BRB   - рисуется граница внизу                                           │
   // I=X   - данные выводятся в колонке X                                     │ 
   // R=X   - ячейки объединяются, справа будет захвачено еще Х ячеек          │ 
   // D=X   - ячейки объединяются, снизу будет захвачено еще Х ячеек           │ 
   // F=X   - выводится формула, текст в Х                                     │ 
   // WT    - переносить по словам                                             │
   //                                                                          │ 
   // T=XXX - определяется тип переменной. Возможные значения XXX              │ 
   //     D  - дата                                                            │ 
   //     MX - Money                                                           │ 
   //     NX - Number                                                          │ 
   //     PX - Procent                                                         │
   //        (X - кол-во знаков после зяпятой, может отсутствовать, тогда для  │
   //         N X=0, для M и P X=2)                                            │
   //     в остальных случаях - String                                         │ 
   //                                                                          │ 
   // Выравнивание                                                             │ 
   //   P=C - центр по горизонтали и вертикали                                 │ 
   //   HA=X - выравнивание по горизонтали                                     │ 
   //     Значения Х:                                                          │
   //       С - по центру                                                      │ 
   //       L - по левому краю                                                 │ 
   //       R - по правому краю                                                │ 
   //       J - по ширине                                                      │ 
   //   VA=X - выравнивание по вертикали                                       │ 
   //     Значения Х:                                                          │ 
   //       С - по центру                                                      │ 
   //       T - по верхнему краю                                               │ 
   //       B - по нижнему краю                                                │ 
   //   O=X - X-отступ                                                         │
   //                                                                          │
   // Префикс знака                                                            │
   //   + - выводить префикс "+" для неотрицательных значений типа Money       │
   //                                                                          │ 
   // Разделитель ";". Если ключевое слово одно, то разделитель можно не указыв│
   // Если в параметрах присутствует символ "|", то он используется в качестве │
   // разделителя (в формулах иногда используется ";")                         │
   //                                                                          │ 
   // Шрифт.                                                                   │ 
   //                                                                          │ 
   // FN=X - X - наименование шрифта, например "FN=Times New Roman;"           │ 
   // FS=X - X - размер шрифта, например "FS=10;"                              │ 
   // FB   - жирный шрифт                                                      │ 
   // FI   - наклонный шрифт                                                   │ 
   // FU   - подчеркнутый шрифт                                                │ 
   // FC=X - X-цвет шрифта, варианты: R-красный, G-зеленый, B-синий.           │ 
   //     Пример: "FB;FI;FS=12;FC=R;"                                          │ 
   //                                                                          │ 
   // Примеры: "R=2;START;END"                                                 │ 
   //          "T=M;END"                                                       │ 
   //          "R=3;START;T=N2;"                                               │ 
   //──────────────────────────────────────────────────────────────────────────┘
   MACRO Put_Val()
      private var l_Value, l_Index=NULL, l_Point=NULL, l_Formula=NULL, l_Cell_Name=NULL;
      private var l_Right=NULL, l_Down=NULL;
      private var l_Type=NULL, l_String, l_Heig, l_UpString;
      private var l_Name, l_Filter=FALSE;
      private var l_DataType, l_Brl, l_Brr, l_Brt, l_Brb, l_Ha, l_Va, l_FnName, l_FnSize, l_FnBld, l_FnItl, l_FnUnd, l_Color, l_Indent, l_WrapText;
      private var l_str:String=""; 
      private var Day, Month, Year, Hour, Min, Sec, l_Time;
      private var l_InteriorColor;
      private var SignPrefix;
      
      GetParm(1, l_Value);    // Значение переменной.
      GetParm(2, l_String);   // Параметры, описанные в строке.
      Nvl(l_String, "");
      GetParm(3, l_Heig); 
      l_UpString = StrUpr(l_String);
      l_Delim = NULL;
      GetParm(4, l_Delim);
      if (ValType(l_Delim) == V_UNDEF)
         if (Index(l_UpString, "|") > 0)
            l_Delim = "|";
         else
            l_Delim = ";";
         end;
      end;
      l_UpString = StrSubst(l_UpString, "P=C", "HA=C;VA=C");
      l_UpString = StrSubst(l_UpString, "STEN", "START;END;");
      // Если есть ключевое слово, то начинаем новую строку
      if (Index(l_UpString, "START") > 0) this.New_Line(l_Heig); end;
      // Разбор строки.
      Value_From_String(l_UpString, "T=", l_Type);
      Nvl(l_Type, "S");
      if (Index(l_UpString, "+") > 0)
        SignPrefix = "+";
      else 
        SignPrefix = ""; 
      end;

      if (Substr(l_Type, 1, 1) == "M")
         l_DataType = "Money";
         if (StrLen(l_Type) == 1)
            l_Point = 2;
         else
            l_Point = Int(SubStr(l_Type, 2, 1));
         end;
         if(valtype(l_Value) == v_undef)
           l_Value = "";
         else
           l_Value = ExecExp( "String(Sql_Money("+string(double(l_Value)) + "):0:"+string(l_Point)+")");
         end;
      elif (Substr(l_Type, 1, 1) == "A")
         l_DataType = "Account";
         l_Value = String(l_Value);
      elif (Substr(l_Type, 1, 1) == "T")
         l_DataType = "Time";
         l_Value = String(l_Value);
      elif (Substr(l_Type, 1, 1) == "N")
         l_DataType = "Number";
         if (StrLen(l_Type) == 1)
            l_Point = 0;
         else
            l_Point = Int(SubStr(l_Type, 2, 1));
         end;
//         l_Value = String(Sql_Num(l_Value, l_Point):0:l_Point); //bpv string():0:4 не умеет работать с переменными
           l_Value = ExecExp( "String(Sql_Num("+string(l_Value) + ","+l_Point+"):0:"+string(l_Point)+")");
      elif (Substr(l_Type, 1, 1) == "P")
         l_DataType = "Procent";
         if (StrLen(l_Type) == 1)
            l_Point = 2;
         else
            l_Point = Int(SubStr(l_Type, 2, 1));
         end;
         l_Point = l_Point + 2;
//         l_Value = String((Sql_Num(l_Value, l_Point)/100):0:l_Point);
         l_Value = ExecExp("String((Sql_Num("+l_Value+","+ l_Point+")/100):0:"+l_Point+")");
         l_Point = l_Point - 2;
      elif (Substr(l_Type, 1, 1) == "D")
         l_DataType = "DateTime";
         l_Value = Sql_Date(l_Value);
         DateSplit(l_Value, Day, Month, Year);
         DtTmSplit(l_Value, NULL, l_Time);
         TimeSplit(l_Time, Hour, Min, Sec);         
         if (Year > 1)
            l_Value = String(Year, "-", lPad(String(Month), 2, "0"), "-", lPad(String(Day), 2, "0"), "T", lPad(String(Hour), 2, "0"), ":", lPad(String(Min), 2, "0"), ":", lPad(String(Sec), 2, "0"), ".000");
         else
            l_Value = "";
         end;
/*ak
         l_Point = 0;*/
         if (StrLen(l_Type) == 1)
           l_Point = 0
         else
           l_Point = Int(SubStr(l_Type, 2, 1));
         end;
/*~ak*/
      else
         l_DataType = "String";
         l_Value = Sql_String(l_Value);
         l_Value = StrSubst(l_Value, "<", "'");
         l_Value = StrSubst(l_Value, ">", "'");
         l_Point = 0;
      end;

      if ((Substr(l_DataType, 1, 1) == "N")
      or  (Substr(l_DataType, 1, 1) == "M")
      or  (Substr(l_DataType, 1, 1) == "P")
      or  (Substr(l_DataType, 1, 1) == "A")
         )
         l_Type = "Number";
      elif (Substr(l_DataType, 1, 1) == "T")
         l_Type = "DateTime"; 
      else
         l_Type = l_DataType;
      end;

      if (Index(l_UpString, "BOR") > 0)
         l_Brl = TRUE;
         l_Brr = TRUE;
         l_Brt = TRUE;
         l_Brb = TRUE;
      else
         if (Index(l_UpString, "BRL") > 0) l_Brl = TRUE; end;   
         if (Index(l_UpString, "BRR") > 0) l_Brr = TRUE; end;   
         if (Index(l_UpString, "BRT") > 0) l_Brt = TRUE; end;   
         if (Index(l_UpString, "BRB") > 0) l_Brb = TRUE; end;   
      end;   
      Value_From_String(l_String, "FN=", l_FnName);
      if (Value_From_String(l_UpString, "FS=", l_FnSize)) l_FnSize = Int(l_FnSize); end;
      if (Index(l_UpString, "FB") > 0) l_FnBld = TRUE; end;
      if (Index(l_UpString, "FI") > 0) l_FnItl = TRUE; end;
      if (Index(l_UpString, "FU") > 0) l_FnUnd = TRUE; end;
      if (Value_From_String(l_UpString, "I=",   l_Index))  l_Index  = Int(l_Index);  end; Nvl(l_Index, 0);
      if (Value_From_String(l_UpString, "O=",   l_Indent)) l_Indent = Int(l_Indent); end; Nvl(l_Indent, Indent_Value);
      if (Value_From_String(l_UpString, "R=",   l_Right))  l_Right  = Int(l_Right);  end; Nvl(l_Right, 0);
      if (Value_From_String(l_UpString, "D=",   l_Down))   l_Down   = Int(l_Down);   end; Nvl(l_Down, 0);

      // Шабашов. 29.10.2019 добавил обработку имени ячейки
      Value_From_String(l_UpString, "CN=",  l_Cell_Name);  

      Value_From_String(l_String, "F=",   l_Formula);     Nvl(l_Formula, "");
      Value_From_String(l_UpString, "HA=",  l_Ha);  
      Value_From_String(l_UpString, "VA=",  l_Va);  
      Value_From_String(l_UpString, "FC=",  l_Color);
      Value_From_String(l_UpString, "IC=",  l_InteriorColor);  

      if(Index(l_UpString, "WT") > 0)
        l_WrapText = 1;
      end;

      // Находим стиль в таблице стилей (или создаем новый).
      l_Name = Add_Style(l_DataType, l_Brl, l_Brr, l_Brt, l_Brb, l_Ha, l_Va, l_FnName, l_FnSize, l_FnBld, l_FnItl, l_FnUnd, l_Point, l_Color, l_Indent, l_InteriorColor, l_WrapText, SignPrefix);
      
      // В зависимости от типа помещаем данные
      // Если данные есть, то заполняем ячейку по правилам.
      l_str = String(" <Cell"
                    ,IIF(l_Right, String(" ss:MergeAcross=\"", l_Right, "\""), "") 
                    ,IIF(l_Down,  String(" ss:MergeDown=\"",   l_Down,  "\""), "") 
                    ,IIF(l_Index, String(" ss:Index=\"",       l_Index, "\""), "") 
                    ," ss:StyleID=\"", l_Name, "\""
                    ,IIF(l_Formula != "", String(" ss:Formula=\"", l_Formula, "\""), "")
                    );
      if ((Trim(String(l_Value)) == "")
      or  ((l_DataType == "DateTime") and (l_Value == Date(0,0,0)))
         )
         l_str = String(l_str
                       ,"/>"
                       );
      else
         l_str = String(l_str
                       ,">"
                       ,"<Data ss:Type=\"", l_Type, "\">"
                       ,l_Value
                       ,"</Data>"
                       ,"</Cell>"
                       );
      end;
      this.Add_XML_String(l_str);
         
      // Получить текущую колонку
      IF (l_Index > 0)
         u_Current_Column = l_Index;
      ELSE
         u_Current_Column = u_Current_Column + 1;
      END;
      IF (l_Cell_Name != NULL)
         Add_Cell_Name(l_Cell_Name, String("R", u_Current_Row, "C", u_Current_Column));
      END;
         
      // Если есть ключевое слово, то добавляем тэг конца строки
      if (Index(l_UpString, "END") > 0) this.End_Line(); end;
   END;  // Put_Val() 
   //──────────────────────────────────────────────────────────────────────────┐
   //                               MACRO                                      │
   // Вывод переменной типа Money                                              │
   // 1 - значение переменной                                                  │
   // 2 - кол-во цифр после зяпятой (по умолчанию - 2)                         │
   //──────────────────────────────────────────────────────────────────────────┘
   MACRO Put_Mon()
      private var l_Value, l_Point;
      GetParm(1, l_Value);
      GetParm(2, l_Point); Nvl(l_Point, 2);
      this.Put_Val(l_Value, String("T=M", l_Point));
   END;  // Put_Mon()
   //──────────────────────────────────────────────────────────────────────────┐
   //                               MACRO                                      │
   // Вывод переменной типа String                                             │
   // 1 - значение переменной                                                  │
   // 2 - выравнивание                                                         │
   //──────────────────────────────────────────────────────────────────────────┘
   MACRO Put_Str()
      private var l_Value, l_Pos;
      GetParm(1, l_Value);
      GetParm(2, l_Pos);   Nvl(l_Pos, "L");
      if (Strlen(l_Pos) > 1)
         this.Put_Val(l_Value, l_Pos);
      elif (Strlen(l_Pos) == 1)
         this.Put_Val(l_Value, String("HA=", l_Pos));
      else
         this.Put_Val(l_Value);
      end;
   END;  // Put_Str()
   //──────────────────────────────────────────────────────────────────────────┐
   //                               MACRO                                      │
   // Вывод переменной типа Date                                               │
   // 1 - значение переменной                                                  │
   // 2 - строка параметров                                                    │
   //──────────────────────────────────────────────────────────────────────────┘
   MACRO Put_Dat()
      private var l_Value;
      private var l_String;
      GetParm(1, l_Value);
      GetParm(2, l_String); Nvl(l_String, "");
//      if ((Sql_Date(l_Value) == Date(0,0,0))
//      or  (ValType(l_Value)  == V_UNDEF)
//         )
//         this.Put_Str("", l_String);
//      else
         this.Put_Val(l_Value, String(l_String, ";T=D"));
//      end;
   END;  // Put_Dat()
   //──────────────────────────────────────────────────────────────────────────┐
   //                               MACRO                                      │
   // Вывод переменной типа DateTime                                           │
   // 1 - значение переменной                                                  │
   // 2 - строка параметров                                                    │
   //──────────────────────────────────────────────────────────────────────────┘
   MACRO Put_Time()
      private var l_Value;
      private var l_String;
      GetParm(1, l_Value);
      GetParm(2, l_String); Nvl(l_String, "");
      this.Put_Val(l_Value, String(l_String, ";T=T"));
   END;  // Put_Time()
   //──────────────────────────────────────────────────────────────────────────┐
   //                               MACRO                                      │
   // Вывод переменной типа Double/Integer                                     │
   // 1 - значение переменной                                                  │
   // 2 - точность (кол-во знаков после запятой)                               │
   //──────────────────────────────────────────────────────────────────────────┘
   MACRO Put_Num()
      private var l_Value, l_Point;
      GetParm(1, l_Value);
      GetParm(2, l_Point); Nvl(l_Point, 0);
      this.Put_Val(l_Value, String("T=N",l_Point));
   END;  // Put_Num()
   //──────────────────────────────────────────────────────────────────────────┐
   //                               MACRO                                      │
   // Вывод переменной типа Double/Integer со знаком "%"                       │
   // 1 - значение переменной                                                  │
   // 2 - точность (кол-во знаков после запятой)                               │
   //──────────────────────────────────────────────────────────────────────────┘
   MACRO Put_Prc()
      private var l_Value, l_Point;
      GetParm(1, l_Value);
      GetParm(2, l_Point); Nvl(l_Point, 2);
      this.Put_Val(l_Value, String("T=P", l_Point));
   END;  // Put_Prc()
   //───────────────────────────────────────────────────────────────────────────
   MACRO Put_Acc()
      private var l_Value, l_Format;
      GetParm(1, l_Value);
      GetParm(2, l_Format); Nvl(l_Format, "");
      l_Value = StrSubst(Trim(Sql_String(l_Value)), ".", "");
      if (l_Value != "")
         l_Value = Trim(String(l_Value:f));
      end;
      this.Put_Val(l_Value, l_Format);
   END;  // Put_Acc()
   //───────────────────────────────────────────────────────────────────────────
   MACRO Table_Header_Bold()
      private var i=2, l_Height, v_Str;
      GetParm(1, l_Height);
      this.New_Line(l_Height);
      While(GetParm(i, v_Str))
         this.Put_Val(v_Str, "VA=C;HA=C;BOR;");
         i = i + 1;
      End; 
      this.End_Line;
   END;  // Table_Header_Bold() 
   //───────────────────────────────────────────────────────────────────────────
   MACRO Set_Column_Width()
      private var l_Cnt:Integer = 1, l_Value, l_Next=TRUE;
      xml_with.Size = 0;
      While (l_Next)
         l_Value = NULL;
         GetParm(l_Cnt, l_Value);
         if (ValType(l_Value) != V_UNDEF)
            xml_with(l_Cnt-1) = l_Value;
         else
            l_Next = FALSE;
         end;
         l_Cnt = l_Cnt + 1;
      End;
   END;  // Set_Column_Width()
   //───────────────────────────────────────────────────────────────────────────
   MACRO Cifer_Header()
      private var l_Height, l_Cnt:Integer, l_Bold, ind:Integer;
      private var l_Shift;
      GetParm(1, l_Height); Nvl(l_Height, 15);
      GetParm(2, l_Cnt);    Nvl(l_Cnt, aSize(this.xml_with));
      GetParm(3, l_Bold);   Nvl(l_Bold, FALSE);
      GetParm(4, l_Shift);  Nvl(l_Shift, 1);
      For (ind, 1, l_Cnt)
         this.Put_Val(String(ind)
                     ,String(IIF(l_Bold, "FB;", "")
                            ,IIF((ind == 1) and (l_Shift > 1), String("I=", l_Shift, ";"), "")
                            ,"HA=C;VA=C;BOR;"
                            ,IIF(ind == 1,     "START;", "")
                            ,IIF(ind == l_Cnt, "END;",   "")
                            )
                     ,l_Height
                     );
      End; 
   END;  // xml_Cifer_Header()
   //───────────────────────────────────────────────────────────────────────────
   MACRO Blank_Line()
      private var l_Height;
      GetParm(1, l_Height); 
      this.Put_Val("", "STEN;", l_Height);
   END;  // Blank_Line() 
   //───────────────────────────────────────────────────────────────────────────
   MACRO Blank_Cell()
      private var l_Count, l_Format;
      GetParm(1, l_Format); Nvl(l_Format, "");
      GetParm(2, l_Count);  Nvl(l_Count,  1);
      if (l_Count < 1) l_Count = 1; end;
      For (var ind, 1, l_Count)
         this.Put_Val("", l_Format);
      End;
   END;  // Blank_Cell()
   //───────────────────────────────────────────────────────────────────────────
   //  Добавляет условное форматирование для диапазона ячеек.
   //───────────────────────────────────────────────────────────────────────────
   MACRO Add_Format(p_Range, p_Qualif, p_Value, p_Color, p_Pattern)
      var val:T_Format_Condition;
      val.Init(p_Range, p_Qualif, p_Value, p_Color, p_Pattern);
      a_Format[a_Format.Size] = val;
   END;  // Add_Format()
   //───────────────────────────────────────────────────────────────────────────
   
   MACRO Move(p_Name)
      private var v_File;
      private var obBook, ob1, obEx;
      r2 = NULL;
      if (ExistFile(lf_File_Name))
         if (CopyFile(lf_File_Name, p_Name))
           RemoveFile(lf_File_Name);
         end;
         v_File = p_Name;
      else
         MsgBox("Файл не найден!|", lf_File_Name);
      end;
      Return v_File;
   END;

   MACRO Delete()
      r2 = NULL;
      RemoveFile(lf_File_Name);
   END;

   MACRO AddStandardFooter(CntIndentCell:integer)
     if(CntIndentCell == NULL)
       CntIndentCell = 0;
     end;

     MACRO Операционист():string
         var cmd = DL_RSDCommand("SELECT person.t_Name " +
                                 "  FROM dperson_dbt person " +
                                 " WHERE person.t_Oper = ? "
                                );
         cmd.AddParam({oper});

         var RS = cmd.Execute();
         if( RS.MoveNext() )
            return RS.Name;
         end;
         return "";
     END;

     MACRO ДолжностьОперациониста():string
         return "Исполнитель";
     END;

     MACRO ТелефонОперациониста():string
         var PhoneNumber = "";
         var cmd = DL_RSDCommand("SELECT person.t_PartyID "
                                 "  FROM dperson_dbt person " +
                                 " WHERE person.t_Oper = ? "
                                );
         cmd.AddParam({oper});

         var RS = cmd.Execute();
         if( ( RS.MoveNext() ) and ( FindAdressContactValue( RS.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber ) == 0 ) )
            return PhoneNumber;
         end;
         return "";
     END;

     MACRO GetEmptyStr(InStr:string)
       var sz = strlen(InStr);
       var i = 1;
       var OutStr = "";

       while(i < sz)
         OutStr = OutStr + " ";
         i = i + 1;
       end;

       return OutStr;
     END;

     MACRO AddIndentCells()
       var i = 0;

       if(CntIndentCell > 0)
         this.Put_Str("", "START;");
         i = 1;
         while(i < CntIndentCell)
           this.Put_Str("", "");
           i = i + 1;
         end;
       end;
     END;

     var PostBoss, PostBook, PostOper;
     var StartStr = "";

     if( {Name_Boss} )
        PostBoss = {Name_Boss};
     else
        PostBoss = "Директор банка";
     end;

     if( {Name_Book} )
        PostBook = {Name_Book};
     else
        PostBook = "Главный бухгалтер банка"
     end;

     PostOper = ДолжностьОперациониста();

     this.Set_WrapText(0);

     if(CntIndentCell == 0)
       StartStr = "START;"
     end;

     AddIndentCells();
     this.Put_Str(PostBoss,           StartStr+"R=1;P=L;");
     this.Put_Str(" _____________ " , "P=C;");
     this.Put_Str({FIO_Boss},         "P=L;END;");
     AddIndentCells();
     this.Put_Str("",                 StartStr+"R=1;P=L;");
     this.Put_Str("    подпись    " , "P=C;END;");

     AddIndentCells();
     this.Put_Str(PostBook,           StartStr+"R=1;P=L;");
     this.Put_Str(" _____________ " , "P=C;");
     this.Put_Str({FIO_Book},         "P=L;END;");
     AddIndentCells();
     this.Put_Str("",                 StartStr+"R=1;P=L;");
     this.Put_Str("    подпись    " , "P=C;END;");

     AddIndentCells();
     this.Put_Str(PostOper,           StartStr+"R=1;P=L;");
     this.Put_Str(" _____________ " , "P=C;");
     this.Put_Str(Операционист(),         "P=L;END;");
     AddIndentCells();
     this.Put_Str("",                 StartStr+"R=1;P=L;");
     this.Put_Str("    подпись    " , "P=C;END;");

     AddIndentCells();
     this.Put_Str("Телефон: " + ТелефонОперациониста(), StartStr+"P=L;END;");
     this.Blank_Line();
     AddIndentCells();
     this.Put_Str(DL_DateToStr({curdate}),              StartStr+"P=L;END;");
     this.Blank_Line();

   END;

   MACRO ExistDataPrint()
     var exist = true;

     if(ValType(a_Rep_Strings) == V_UNDEF)
       exist = false;
     end;

     return exist;
   END;

END; // T_Xml_Rep
