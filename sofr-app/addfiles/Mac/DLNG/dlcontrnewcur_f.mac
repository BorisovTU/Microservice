/*
$Name:        dlcontrnewcur_f.mac
$Module:      Ядро Securities
$Description: ДБО. Начальное решение для новой валюты. Исполняемая ф-ция через funcobj
*/
import "secinter.mac", "dlquery.mac";
import DealsInter, CTInter;
import dbo_message_main_c;
import rshb_lib;
import func_lib;
import dlcontrfunc;
import "funcobj.mac";

var g_ErrMes = "";

private macro MsgBoxToStr(msg)
  g_ErrMes = msg;
end;

private macro GetAccID(MPID, FIID)
  var query, cmd, DataSet;
  var AccountID = 0;

  query =   "select dlacc.t_AccountID "
          + "  from ddlcontrmp_dbt mp, ddlcontracc_dbt dlacc "
          + " where mp.t_ID           = ? "
          + "   and dlacc.t_DlContrID = mp.t_DlContrID "
          + "   and dlacc.t_MPID      = mp.t_ID "
          + "   and dlacc.t_FIID      = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(MPID);
  cmd.AddParam(FIID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    AccountID = DataSet.AccountID;
  end;

  return AccountID;
end;

macro AddCurToContrExec(MPID, CurFIID, ErrStr:@string)
  var query, cmd, DataSet, cmd1;
  var err = 0;
  var prm = TRecHandler("dlcontraccprm.rec");
  var v_Ob = NULL;
  var RestoreOpenAcc = true;

  query =   " select t_DlContrID, t_AutoOpenAcc "
          + "   from ddlcontrmp_dbt "
          + "  where t_ID = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(MPID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext)

    if(DataSet.AutoOpenAcc == "X")
      RestoreOpenAcc = false;
    else
      cmd1 = DL_RSDCommand("UPDATE ddlcontrmp_dbt SET t_AutoOpenAcc = 'X' WHERE t_ID = ?");
      cmd1.AddParam(MPID);

      cmd1.ExecuteCMD();
    end;

    prm.Clear();

    prm.rec.FIID       = CurFIID;     //Валюта л/с
    prm.rec.AccountID  = 0;           //Идентификатор л/с
    prm.rec.OpenDate   = {curdate};   //Дата откртия счета (дата, с которой открывать счет)

    err = DL_AddAccToDLCONTR(DataSet.DlContrID, prm, ErrStr, MPID);

    if((not err) and (g_ErrMes != ""))
      err = 1;
      ErrStr = g_ErrMes;
    end;

    if(RestoreOpenAcc == true)
      cmd1 = DL_RSDCommand("UPDATE ddlcontrmp_dbt SET t_AutoOpenAcc = CHR(0) WHERE t_ID = ?");
      cmd1.AddParam(MPID);

      cmd1.ExecuteCMD();
    end;
  end;

  return err;

OnError(er)
  
  ErrStr = er.Message;

  return 1;
end;

macro SendMessCurToContrExec(MPID, CurFIID, ErrStr:@string)
  var query, cmd, DataSet, cmd1;
  var cnt = 0, i = 0; 
  var err = 0;
  var v_Ob = NULL;

  query =   " select t_DlContrID, t_AutoOpenAcc "
          + "   from ddlcontrmp_dbt "
          + "  where t_ID = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(MPID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext)
    v_Ob = dboMessage(DataSet.DlContrID);
    if(v_Ob and (v_Ob.statsend != -1))    
      v_Ob.CreateMessage(true, true, true, false, "");
      if(v_Ob.statsend == -1)
        err = 1;
        ErrStr = v_Ob.statMes;
      end;
    else
      if(v_Ob)
        err = 1;
        ErrStr = v_Ob.statMes;
      else
        err = 1;
        ErrStr = "Объект уведомления не создан"; 
        v_Ob = null;
      end;
    end;

    if(v_Ob)
      v_Ob.isLast = true;
      v_Ob.cls_word();
    end;

    if((not err) and (g_ErrMes != ""))
      err = 1;
      ErrStr = g_ErrMes;
    end;

  end;

  return err;

OnError(er)
  
  ErrStr = er.Message;

  return 1;
end;


macro AddCurToContr(ObjectType, ObjectID, ParamStr)
  var CurFIID = -1, MPID = 0;
  var OldDialogFlag = SetDialogFlag(0);
  var ReturnObj = FuncObjResult(FUNCOBJ_RESULT_OK, "", 0);
  var AccountID = 0;
  var err = 0, ErrStr = "";
  var query, cmd, DataSet;
  var State = 0;
  
  MPID    = ObjectID;
  CurFIID = int(substr(ParamStr, 1, index(ParamStr, ";")-1));

  if(CurFIID > 0)
    ReplaceMacro( "MsgBox", "MsgBoxToStr");

    query = "select t_State, t_AccountID from ddlcontrcrf_dbt where t_MPID = ? and t_FIID = ?";
    cmd = DL_RSDCommand(query);

    cmd.AddParam(MPID);
    cmd.AddParam(CurFIID);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      if(DataSet.State == 0)
        var q, cmd1, ds;

        //Проверим, нет ли уже такой валюты на площадке
        q =     " select 1 "
              + "   from ddlcontracc_dbt dlacc "
              + "  where dlacc.t_MPID      = ? "
              + "    and dlacc.t_FIID      = ? ";

        cmd1 = DL_RSDCommand(q);

        cmd1.AddParam(MPID);
        cmd1.AddParam(CurFIID);

        if(cmd1.GetCount() == 0)
          err = AddCurToContrExec(MPID, CurFIID, @ErrStr);
        end;

        if(not err)
          //Найти площадку фондового рынка ММВБ и добавить валюту ей

          q =   " select mp1.t_ID "
              + "   from ddlcontrmp_dbt mp, ddlcontrmp_dbt mp1, dsfcontr_dbt sf "
              + "  where mp.t_ID = ? "
              + "    and mp1.t_DlContrId = mp.t_DlContrID "
              + "    and mp1.t_MarketID = 2 " //ММВБ
              + "    and sf.t_ID = mp1.t_SfContrID "
              + "    and sf.t_ServKind = " + PTSK_STOCKDL
              + "    and sf.t_DateClose = " + GetSQLDate(date(0,0,0))
              + "    AND Not Exists(select 1 "
              + "                     from ddlcontracc_dbt dlacc "
              + "                    where dlacc.t_DlContrID = mp1.t_DlContrID "
              + "                      and dlacc.t_MPID      = mp1.t_ID "
              + "                      and dlacc.t_FIID      = ? "
              + "                  )";

          cmd1 = DL_RSDCommand(q);

          cmd1.AddParam(MPID);
          cmd1.AddParam(CurFIID);
          
          ds = cmd1.Execute();
          if(ds.moveNext())
            err = AddCurToContrExec(ds.ID, CurFIID, @ErrStr);
          end;
        end;

        AccountID = GetAccID(MPID, CurFIID);
        if(AccountID > 0)
          ErrStr = "Счет открыт. "+ErrStr;
        end;

        State = 1;

      elif(DataSet.State == 1)
        AccountID = DataSet.AccountID;
        
        err = SendMessCurToContrExec(MPID, CurFIID, @ErrStr);
        if(not err)
          ErrStr = "Уведомление сформировано и отправлено. "+ErrStr;
        end;

        State = 2;
      end;

      if(err)
        State = State * 10;
      end;
    end;

    cmd = DL_RSDCommand(    " UPDATE DDLCONTRCRF_DBT "
                          + "    SET T_MESSAGE = ?, "
                          + "        T_ACCOUNTID = ?, "
                          + "        T_STATE = ? "
                          + "  WHERE T_MPID = ? "
                          + "    AND T_FIID = ? "
                         );

    cmd.AddParam(ErrStr);
    cmd.AddParam(AccountID);
    cmd.AddParam(State);
    cmd.AddParam(MPID);
    cmd.AddParam(CurFIID);

    cmd.ExecuteCMD();

    if(err)
      //ReturnObj.state = FUNCOBJ_RESULT_ERROR;
      ReturnObj.errorText = ErrStr;
      ReturnObj.errorCode = err;
    end;

    ReplaceMacro( "MsgBox", NULL);
  end;

  SetDialogFlag(OldDialogFlag);

  return ReturnObj;

OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  ReturnObj.state     = FUNCOBJ_RESULT_ERROR;
  ReturnObj.errorText = er.Message;
  ReturnObj.errorCode = er.Code;

  return ReturnObj;
end;