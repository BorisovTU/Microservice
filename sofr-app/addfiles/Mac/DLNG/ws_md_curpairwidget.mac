/*
$Name:             ws_md_curpairwidget.mac
$Module:           Монитор дилера 
$Description:      WEB. Макрос сервисов виджета валютных пар
*/
import "ws_md.mac";
import "ws_common.mac";
import "ws_datafilter.mac";
import "ws_orderbygen.mac";

private const MacroName = "ws_md_curpairwidget.mac";

/* Аналог оператора ?: в си. */
private macro IIF( condition, value_true, value_false )
   if( condition )
      return value_true;
   else 
      return value_false;
   end;
end;

// Класс данных для виджета валютных пар монитора дилера
class CCurPair
   var ID:Integer;                // Идентификатор валютной пары
   var PFI:Integer;               // Идентификатор базовой валюты
   var CFI:Integer;               // Идентификатор котируемой валюты
   var PFI_ISO:String;	          // ISO код базовой валюты
   var CFI_ISO:String;	          // ISO код котируемой валюты
   var PFI_Name:String;	          // Наименование базовой валюты
   var CFI_Name:String;	          // Наименование котируемой валюты
   var CurPairName:String;	      // Наименование валютной пары
end;

// Класс с дополнительными параметрами фильтрации для отбора валютных пар
class CCurPairFilterParm
   var IsShow:Bool;               // Флаг отображения
   var IsLimit:Bool;              // Является валютой ограничения
   var FindID:Bool;               // Поиск по идентификатору валютной пары
   var LikeName:Bool;             // Поиск по наименованию
end;


private macro MdGetCurPairListAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String

  var fp = FilterParser( FilterItems );
  

  return fp.GetFullSQLCondition();
end;

private macro MdGetCurPairListAddWhereFilterParm(
  filterParm//:CCurPairFilterParm
):String
   var strAddWhereFilterParm:string = "";

   if( filterParm != null )
      if( filterParm.IsShow != null )
         strAddWhereFilterParm = strAddWhereFilterParm + " AND RKCURPAIR.t_IsShow = " + IIF(filterParm.IsShow == true, "chr(88)", "chr(0)");
      end;
      
      if( filterParm.IsLimit != null )
         strAddWhereFilterParm = strAddWhereFilterParm + " AND RKCURPAIR.t_IsLimit = " + IIF(filterParm.IsLimit == true, "chr(88)", "chr(0)");
      end;

      if( (filterParm.FindID != null) and (filterParm.FindID > 0) )
         strAddWhereFilterParm = strAddWhereFilterParm + " AND RKCURPAIR.t_ID = " + filterParm.FindID;
      end;
      
      if( (filterParm.LikeName != null) and (filterParm.LikeName != "") )
         strAddWhereFilterParm = strAddWhereFilterParm + " AND (LOWER(fi_pfi.t_CCY || '/' || fi_cfi.t_CCY) LIKE LOWER('%" + filterParm.LikeName + "%')) ";
      end;
   end;
   
   return strAddWhereFilterParm;
end;

// Количество записей виджета валютных пар
macro MdGetCurPairListCount(
   filterParm//:CCurPairFilterParm
  ,filterItems//:TArray of FilterConditionItem
):Integer

   var count:integer = 0;
   var stat:integer = 0;
   var strAddWhere:string = "";
   var strAddWhereFilterParm:string = "";

   InitError();   

   var query =
      " SELECT " +
      "    Count(1) t_cnt " +
      " FROM " +
      "    DRKCURPAIR_DBT RKCURPAIR, " +
      "    DFININSTR_DBT fi_pfi, " +
      "    DFININSTR_DBT fi_cfi " +
      " WHERE " +
      "        RKCURPAIR.T_PFI = fi_pfi.T_FIID " +
      "    AND RKCURPAIR.T_CFI = fi_cfi.T_FIID ";

   strAddWhere = MdGetCurPairListAddWhereCondition( FilterItems );
   if( strAddWhere != "" )
      query = query + " AND " + strAddWhere;
   end;
  
   strAddWhereFilterParm = MdGetCurPairListAddWhereFilterParm( filterParm );
   if( strAddWhereFilterParm != "" )
      query = query + strAddWhereFilterParm;
   end;

   var ds = TRsbDataSet(query);

   if( ds.MoveNext() )
      count = SQL_ConvTypeInteger(ds.cnt);
   end;

   return count;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

   private var sql_query:String =
      " SELECT " +
      "    RKCURPAIR.t_ID, " +
      "    RKCURPAIR.t_PFI, " +
      "    RKCURPAIR.t_CFI, " +
      "    fi_pfi.t_ISO_Number t_PFI_ISO, " +
      "    fi_cfi.t_ISO_Number t_CFI_ISO, " +
      "    fi_pfi.t_Name t_PFI_Name, " +
      "    fi_cfi.t_Name t_CFI_Name, " +
      "    (fi_pfi.t_CCY || '/' || fi_cfi.t_CCY) t_CurPairName " +
      " FROM " +
      "    DRKCURPAIR_DBT RKCURPAIR, " +
      "    DFININSTR_DBT fi_pfi, " +
      "    DFININSTR_DBT fi_cfi " +
      " WHERE " +
      "        RKCURPAIR.T_PFI = fi_pfi.T_FIID " +
      "    AND RKCURPAIR.T_CFI = fi_cfi.T_FIID ";

   private macro SetCurPair(CurPair:CCurPair, ds)
      CurPair.ID = SQL_ConvTypeInteger( ds.ID );
      CurPair.PFI = SQL_ConvTypeInteger( ds.PFI );
      CurPair.CFI = SQL_ConvTypeInteger( ds.CFI );
      CurPair.PFI_ISO = SQL_ConvTypeStr( ds.PFI_ISO );
      CurPair.CFI_ISO = SQL_ConvTypeStr( ds.CFI_ISO );
      CurPair.PFI_Name = SQL_ConvTypeStr( ds.PFI_Name );
      CurPair.CFI_Name = SQL_ConvTypeStr( ds.CFI_Name );
      CurPair.CurPairName = SQL_ConvTypeStr( ds.CurPairName );
   end;

// Получить данные виджета валютных пар
macro MdGetCurPairList (
   filterParm//:CCurPairFilterParm
  ,PageIndex : Integer                         // Номер страницы
  ,PageSize : Integer                          // Размер страницы
  ,filterItems//:TArray of FilterConditionItem // Значения фильтрации
  ,orderItems//:TArray of OrderItem            // Параметры сортировки
)
   var stat:Integer = 0;
   var CurPairList = TArray();
   var CurPair = null;
   var strAddWhere:string = "";
   var strAddWhereFilterParm:string = "";
   var query = sql_query;

   InitError();

   if( PageIndex == null )
      RunError("Не задан обязательный параметр функции: номер страницы ");
   end;
   if( PageSize == null )
      RunError("Не задан обязательный параметр функции: размер страницы ");
   end;

   strAddWhere = MdGetCurPairListAddWhereCondition( FilterItems );
   if( strAddWhere != "" )
      query = query + " AND " + strAddWhere;
   end;
  
   strAddWhereFilterParm = MdGetCurPairListAddWhereFilterParm( filterParm );
   if( strAddWhereFilterParm != "" )
      query = query + strAddWhereFilterParm;
   end;
   
   query = query + " ORDER BY " + SP_GetSortStr( OrderItems, " RKCURPAIR.t_ID " );
   query = SQL_WrapSelect( query, PageIndex, PageSize );

   var ds = TRsbDataSet(query);
   
   while( ds.MoveNext() )
      CurPair = CCurPair();
      SetCurPair(CurPair, ds);
      CurPairList[CurPairList.Size] = CurPair;
   end;

   return CurPairList;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

///////////////////////////////////////////////////////////
// Возвращает список длинны listLen по
// частичному совпадению значения
///////////////////////////////////////////////////////////
macro MdLookupCurPair( filterParm, listLen ):TArray
   var stat:Integer = 0;
   var CurPairList = TArray();
   var CurPair = null;
   var strAddWhere:string = "";
   var strAddWhereFilterParm:string = "";
   var query = sql_query;

   InitError();

   strAddWhereFilterParm = MdGetCurPairListAddWhereFilterParm( filterParm );
   if( strAddWhereFilterParm != "" )
      query = query + strAddWhereFilterParm;
   end;

   if( (listLen != null) AND (listLen != 0) )
      query = query + " AND ROWNUM < " + string(listLen);
   end;

   query = SQL_WrapSelect( query, 0, 0 );

   var ds = TRsbDataSet(query);
   
   while( ds.MoveNext() )
      CurPair = CCurPair();
      SetCurPair(CurPair, ds);
      CurPairList[CurPairList.Size] = CurPair;
   end;

   return CurPairList;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;

