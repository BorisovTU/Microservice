/*
$Name:         dl_report_Over4742_Form.mac
$Module:       ФИССиКО
$Description:  Проверка переоценки по счетам 47421/47424  - форма ввода данных
*/

IMPORT "DlRepForm_h.mac", "dv_categ.mac";

// Номера полей в форме отчета
CONST
    PNFLD_OVER4742_DEPARTCODE    = 0, // код филиала
    PNFLD_OVER4742_DEPARTMENT    = 1, // наименование филиала
    PNFLD_OVER4742_DATE          = 2, // дата начала
    PNFLD_OVER4742_KINDFI        = 3, // вид ФИ
    PNFLD_OVER4742_FICODE        = 4, // код ФИ
    PNFLD_OVER4742_FINAME        = 5, // название ФИ
    PNFLD_OVER4742_ISAPOSTR      = 6, // С апострофами
    PNFLD_OVER4742_EXCEL         = 7; // Выпуск отчета в Excel 

// Обработчики для нестандарных контролов
PRIVATE CONST
   H_FIKind      = 101,
   H_FI          = 102,
   H_FIName      = 103;

CLASS OVER4742RepPanelData()
   var                     
   FIKind        = -1,
   FI            = ALLFININSTR;
END;
VAR OVER4742RepFormData = OVER4742RepPanelData();

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

private MACRO GetFIKindWhereCond(panel)
   var whereCond = "";

   whereCond = FIKIND_CURRENCY + "," + FIKIND_METAL;

   if( whereCond != "")
      whereCond = " AND fk.t_FI_Kind IN( " + whereCond + " ) ";
   end;

   return whereCond;
end;

// Листалка видов ФИ
private MACRO ListFIKind( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, FIKindName:@VARIANT, WhereCond:String ):BOOL
  var select = " SELECT fk.t_FI_Kind FIKind, fk.t_Name NameFIKind " +
               " FROM dFiKinds_dbt fk "  +
               " WHERE 1 = 1 " + IIF(WhereCond != null, WhereCond, "") +
               " ORDER BY fk.t_FI_Kind";
  var Choice = DL_Scroll(select,
                         "Виды финансовых инструментов", X, Y,
                         "FIKind","Номер", "NameFIKind","Название"
                        );
  if( Choice != NULL )
     FldRealValue = Choice.Value("FIKind"); 
     FldShowValue = Choice.Value("NameFIKind");
     FIKindName = Choice.Value("NameFIKind");
  end;

  return (Choice != NULL);
END;

// Обработчик поля виды ФИ
private MACRO OVER4742_FldProc_FIKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
 var FIKindName = "";
 var fikinds  = TBFile( "fikinds.dbt",  "R", 0 );

 if( Cmd == DLG_INIT )
     if( (FldRealValue == null) or (FldRealValue <= 0) )
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        DisableFields( pThis.Data(), PNFLD_OVER4742_FICODE );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     DisableFields( pThis.Data(), PNFLD_OVER4742_FICODE );
     pThis.SetLinkedValue( H_FI, FldRealValue);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if(ListFIKind( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @FIKindName, GetFIKindWhereCond(pThis)) == true)
        pThis.SetLinkedValue( H_FI, ALLFININSTR);
        EnableFields( pThis.Data(), PNFLD_OVER4742_FICODE );
        OVER4742RepFormData.FIKind = FldRealValue;
     elif( FldRealValue == ALLFININSTR )
        DisableFields( pThis.Data(), PNFLD_OVER4742_FICODE );
     end;
  end;

  return CM_DEFAULT;
END;

private MACRO GetFIWhereCond(panel)
   var whereCond = "",
       s, FIKind;

   panel.GetLinkedValue( H_FIKind, @s, @FIKind);   

   if( FIKind != ALLFININSTR )
      whereCond = FIKind;
   end;

   if( whereCond != "")
      whereCond = " AND fi.t_FI_Kind IN( " + whereCond + " ) ";
   end;

   return whereCond;
end;

// Листалка ФИ
private MACRO ListFI( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, FIName:@VARIANT, WhereCond:String ):BOOL
  var select = " SELECT fi.t_FI_Code FICode, fi.t_Name NameFI, fi.t_FIID FI " +
               " FROM dfininstr_dbt fi "  +
               " WHERE fi.t_IsClosed = chr(0) " + IIF(WhereCond != null, WhereCond, "") +
               " ORDER BY fi.t_FI_Kind, fi.t_FI_Code";
  var Choice = DL_Scroll(select,
                         "Список финансовых инструментов", X, Y,
                         "FICode","Код анкеты ФИ", "NameFI","Название", "FI","ID инструмента"
                        );
  if( Choice != NULL )
     FldRealValue = Choice.Value("FI"); 
     FldShowValue = Choice.Value("FICode");
     FIName = Choice.Value("NameFI");
  end;

  return (Choice != NULL);
END;

// Обработчик поля ФИ
private MACRO OVER4742_FldProc_FI( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
 var FIKindName = "";

 if( Cmd == DLG_INIT )
     if( (FldRealValue == null) or (FldRealValue < 0) )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_FIName, "");
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = ALLFININSTR;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_FIName, "");
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     if(ListFI( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @FIKindName, GetFIWhereCond(pThis)) == true)
        OVER4742RepFormData.FI = FldRealValue;
        pThis.SetLinkedValue( H_FIName, FIKindName);
     end;
  end;

  return CM_DEFAULT;
END;

CLASS ( DL_CPanel ) DL_Report_OVER4742_Form()
    PRIVATE MACRO Init()
        AddFieldProc( 0, PNFLD_OVER4742_DEPARTCODE,     DL_PNFLD_DEPARTCODE,    @DL_FldProc_DepartCode,     {OperDprt});
        AddFieldProc( 0, PNFLD_OVER4742_DEPARTMENT,     DL_PNFLD_DEPARTMENT,    @DL_FldProc_Department,     {OperDprt});
        AddFieldProc( 0, PNFLD_OVER4742_DATE,           DL_PNFLD_BEGINDATE,     @DL_FldProc_BeginDate,      {curdate} );
        AddFieldProc( 0, PNFLD_OVER4742_KINDFI,         H_FIKind,               @OVER4742_FldProc_FIKind,   OVER4742RepFormData.FIKind);
        AddFieldProc( 0, PNFLD_OVER4742_FICODE,         H_FI,                   @OVER4742_FldProc_FI,       OVER4742RepFormData.FI);
        AddFieldProc( 0, PNFLD_OVER4742_FINAME,         H_FIName,               @Default,                   "");
        AddFieldProc( 0, PNFLD_OVER4742_ISAPOSTR,       DL_PNFLD_CHECKBOX,      @DL_FldProc_CheckBox,       SET_CHAR);
        AddFieldProc( 0, PNFLD_OVER4742_EXCEL,          DL_PNFLD_CHECKBOX,      @DL_FldProc_CheckBox,       SET_CHAR);
    END;

    initDL_CPanel( "deals.lbr", "REPOVERV" );
END;
