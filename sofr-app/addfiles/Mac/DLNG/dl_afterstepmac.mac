/*
$Name:        dl_afterstepmac.mac
$Module:      Дилинг
$Description: Макрос, запускающийся после выполнения макросов шага
*/
import dl_calendtls;

macro CheckCondition(curAccTrn)
  return (GetIdentProgram() != CodeFor("Ю")) or (curAccTrn.rec.Chapter != 21);
end;

macro AfterStepProc(OprStep)
  var stat = 0;
  var correctPlanDate = NULL;
  var accTrnArr = TArray ();
  var answ = -9999;
  var oprStepBuf = TRecHandler("oprstep.dbt");
  SetBuff(oprStepBuf, OprStep);

  var m_DocKind = 0;
  var m_DocId = 0;
  var m_IdOp = 0;
  var m_IdStep = 0;
  var dateNumb = 0;
  var FD = 0;
  var linkCalendKindId = -1;
  var linkCalendFIID = -1;
  var linkCalendContractor = -1;

  if (DL_CheckHasPlanAccTrn(accTrnArr))

    DL_GetCalendCurOperStepData(m_DocKind, m_IdOp, m_IdStep, m_DocId);
    dateNumb =  DL_GetDateKindByOprID(m_IdOp, m_IdStep);
    FD = DL_CalendGenerateFD(m_DocKind, m_DocId, dateNumb);
    if (DL_Calend_DV_DateDefineIsTransfer(oprStepBuf.rec.DocKind, DL_GetDateKindByOprStep(oprStepBuf)))
      linkCalendKindId = dl_calendar_book;
    end;
    linkCalendKindId = DL_Calend_ForceServOp(linkCalendKindId, oprStepBuf);
    if ((linkCalendKindId < 0) and (FD != NULL))
      linkCalendKindId = FD.ПолучитьКалендСвязанный();
      if (FD.ПолучитьКалендТип() != DL_CALLNK_MARKET)
        linkCalendContractor = FD.ПолучитьКалендКонтрагент();
        linkCalendFIID = FD.ПолучитьКалендВалютуВидаДаты();
      end;
    end;
    if (linkCalendKindId < 0)
      linkCalendKindId = DL_GetCalendByDynParam(NULL, makeArray(DL_KeyPair("Object",DL_GetOperNameByFD(m_DocKind,m_DocId))));
    end;

    var cntr = 0;
    while (cntr < accTrnArr.size)
      if (CheckCondition(accTrnArr[cntr]))
        var curPlanDate = accTrnArr[cntr].rec.Date_Carry;
        if (DL_Calend_NoQuestion(oprStepBuf.rec.DocKind, oprStepBuf.rec.ServDocKind, DL_GetDateKindByOprStep(oprStepBuf)))
          answ = IND_YES;
        end;
        if ((curPlanDate > date(0,0,0)) and (linkCalendKindId >= 0))
          DL_CalendInit(curPlanDate, linkCalendKindId);
          correctPlanDate = DL_GetBalanceDateAfterWorkDayByCalendar(curPlanDate, 0, linkCalendKindId, true, DL_Calend_DefineFIIDForAccTrn(linkCalendFIID, accTrnArr[cntr], oprStepBuf), linkCalendContractor);
          if (correctPlanDate != curPlanDate)
            var text = "Планируемая дата проводок \""+curPlanDate
              +"\" не является балансовой.|Выполнить проводки в ближайшую балансовую дату \""+correctPlanDate+"\"?";
            if (answ == -9999)
              answ = MsgBoxEx ( text, MB_YES+MB_NO+MB_CANCEL, IND_YES, "Вопрос", "Вопрос" );
            end;
            if (answ == IND_YES)
              stat = DL_SetPlanAccTrnCarryDate(accTrnArr[cntr].rec.AccTrnId, correctPlanDate);
            elif (answ == IND_CANCEL)
              return 1365;
            end;
          end;
        end;
      end;
      cntr = cntr + 1;
    end;
  end;

  if ((oprStepBuf.rec.DocKind == DL_DVFXDEAL) or (oprStepBuf.rec.DocKind == DL_DVNDEAL) or (oprStepBuf.rec.DocKind == DL_DVDEALT3))
    DL_BalanceDateEQMinTODateLogic();
  end;

  return stat;
end;