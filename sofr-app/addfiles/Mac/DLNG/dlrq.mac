/*
$Name:        dlrq.mac
$Module:      Ядро Securities
$Description:
*/
import DealsInter, secinter, "spserv.mac", "sp_catac.mac";

//заполнение параметров ТО 
macro FillDealRQ_(tick_buf, leg_buf, dlrq_buf)

  //поля t_DealPart, t_Kind, t_SubKind, t_Type уже заполнены извне, т.к. они являются основополагающими для дальнейшей инициализации
  //также уже заполнены поля t_Amount И t_FIID
  //t_RQAccID также заполняется извне, но уже после данной процедуры
  //p_Leg соответствует той части, которая указана в dlrq_buf.DealPart

  var Group = GetOpGroup( tick_buf );

  if( IsBasket(Group) and IsOUTEXCHANGE(Group) )
    dlrq_buf.Party = tick_buf.PartyID;
  elif(IsEXCHANGE(Group))
    dlrq_buf.Party = tick_buf.MarketID;
  elif(IsBROKER(Group)) 
    dlrq_buf.Party = tick_buf.BrokerID;
  else
    dlrq_buf.Party = tick_buf.PartyID;  
  end;
    
  dlrq_buf.UseNetting = tick_buf.IsNetting;
  dlrq_buf.PlanDate   = SP_GetPlanDateRQ( leg_buf, tick_buf, dlrq_buf.Type, Group);

  if((dlrq_buf.Type == DLRQ_TYPE_AVANCE) or (dlrq_buf.Type == DLRQ_TYPE_DEPOSIT))
    //аванс или задаток
    dlrq_buf.Party    = tick_buf.PartyID;
    dlrq_buf.ChangeDate = tick_buf.DealDate;
  elif(dlrq_buf.Type == DLRQ_TYPE_DELIVERY)
    //поставка ц/б
    if((IsAVRWRTIN(Group)) or (IsAVRWRTOUT(Group)))
       if(tick_buf.BofficeKind == DL_AVRWRTOWN)
         dlrq_buf.Party = {OurBank};
       else
         if(tick_buf.ClientID != -1)
           dlrq_buf.Party = tick_buf.ClientID;
         else
           dlrq_buf.Party = {OurBank};
         end;
       end;
    end;

    if(tick_buf.BofficeKind == DL_RETIREMENT_OWN)
      dlrq_buf.Party = leg_buf.DepositID;
    end;
       
    dlrq_buf.ChangeDate = tick_buf.DealDate;
  elif(dlrq_buf.Type == DLRQ_TYPE_PAYMENT)
     /*оплата*/
     dlrq_buf.ChangeDate = tick_buf.DealDate;
       
     if((IsRET_ISSUE(Group)) or (IsRET_COUPON(Group)) or (IsRET_PARTLY(Group))) //погашение выпуска
       if(tick_buf.BofficeKind == DL_RETIREMENT_OWN)
         dlrq_buf.Party = SP_GetPayAgentOwn(leg_buf.PFI);
       else
         if((tick_buf.Flag1 == UNSET_CHAR) AND (tick_buf.Flag4 == UNSET_CHAR))
           if(tick_buf.BrokerID != -1)
             dlrq_buf.Party = tick_buf.BrokerID;
           else
             dlrq_buf.Party = tick_buf.PartyID;
           end;
         else
           dlrq_buf.Party = tick_buf.PartyID;
         end;
       end;
     end;                                       

  elif(dlrq_buf.Type == DLRQ_TYPE_INCREPO)
     //проценты в РЕПО
     dlrq_buf.ChangeDate = tick_buf.DealDate;

  elif(dlrq_buf.Type == DLRQ_TYPE_PAYINCOME)
     //выплата дохода
     if(tick_buf.BofficeKind == DL_RETIREMENT_OWN)
       dlrq_buf.Party = SP_GetPayAgentOwn(leg_buf.PFI);
     end;

     dlrq_buf.ChangeDate = tick_buf.DealDate;
  elif((dlrq_buf.Type == DLRQ_TYPE_TAX_ULN) or (dlrq_buf.Type == DLRQ_TYPE_TAX_FLN) or (dlrq_buf.Type == DLRQ_TYPE_TAX_FLR) or (dlrq_buf.Type == DLRQ_TYPE_TAX_FLR_ADD))
     //выплата дохода
     if(tick_buf.BofficeKind == DL_RETIREMENT_OWN)
       var party = TRecHandler("party.dbt");
       if(ПолучитьСубъекта({OurBank}, party) == 0 )
         dlrq_buf.Party = party.rec.TaxInstitution;
       end;
     end;
  end;

  return 0;
end;

macro FillDealRQ(tick, leg, dlrq)

  record tick_buf( dl_tick );
  record leg_buf( dl_leg );
  record dlrq_buf( dlrq );


  SetBuff( tick_buf, tick );
  SetBuff( leg_buf, leg );
  SetBuff( dlrq_buf, dlrq );

  return FillDealRQ_(tick_buf, leg_buf, dlrq_buf);
end;

//заполнение параметров ТО 
macro FillNptxopRQ(nptxop, dlrq)

  record nptxop_buf( nptxop );
  record dlrq_buf( dlrq );

  SetBuff( nptxop_buf, nptxop );
  SetBuff( dlrq_buf, dlrq );
  
  //поля t_DealPart, t_Kind, t_SubKind, t_Type уже заполнены извне, т.к. они являются основополагающими для дальнейшей инициализации
  //также уже заполнены поля t_Amount И t_FIID
  //t_RQAccID также заполняется извне, но уже после данной процедуры

  if(dlrq_buf.ID == 0)
    dlrq_buf.Cliring      = UNSET_CHAR;
    dlrq_buf.PlaceID      = nptxop_buf.Place;
    dlrq_buf.State        = DLRQ_STATE_PLAN;
    dlrq_buf.FactDate     = date(0,0,0);
    dlrq_buf.Netting      = UNSET_CHAR;
    dlrq_buf.Instance     = 0;
    dlrq_buf.ChangeDate   = nptxop_buf.OperDate;
    dlrq_buf.ID_Operation = 0;
    dlrq_buf.ID_Step      = 0;
    dlrq_buf.Source       = 0;
    dlrq_buf.Action       = DLRQ_ACTION_CREATE;
    dlrq_buf.SourceObjKind = -1;
    dlrq_buf.SourceObjID   = 0;
  end;
    
  dlrq_buf.Party = nptxop_buf.Client;
  dlrq_buf.PlanDate = nptxop_buf.OperDate;

  dlrq_buf.PlaceID = nptxop_buf.PLACE;

  return 0;
end;


//заполнение параметров ТО 
macro FillNtgRQ(ntg, dlrq)
  record ntg_buf( dl_nett );
  record dlrq_buf( dlrq );

  SetBuff( ntg_buf, ntg );
  SetBuff( dlrq_buf, dlrq );

  //поля t_DealPart, t_Kind, t_SubKind, t_Type уже заполнены извне, т.к. они являются основополагающими для дальнейшей инициализации
  //также уже заполнены поля t_Amount И t_FIID
  //t_RQAccID также заполняется извне, но уже после данной процедуры

  if(dlrq_buf.ID == 0)
    dlrq_buf.Cliring      = UNSET_CHAR;
    dlrq_buf.PlaceID      = -1;
    dlrq_buf.State        = DLRQ_STATE_PLAN;
    dlrq_buf.FactDate     = date(0,0,0);
    dlrq_buf.Netting      = UNSET_CHAR;
    dlrq_buf.Instance     = 0;          
    dlrq_buf.ChangeDate   = ntg_buf.SigningDate;
    dlrq_buf.ID_Operation = 0;
    dlrq_buf.ID_Step      = 0;
    dlrq_buf.Source       = 0;
    dlrq_buf.Action       = DLRQ_ACTION_CREATE;
    dlrq_buf.SourceObjKind = -1;
    dlrq_buf.SourceObjID   = 0;
  end;
    
  dlrq_buf.Party = ntg_buf.Contractor;
  dlrq_buf.PlanDate = ntg_buf.ValueDate;

  return 0;
end;

//заполнение параметров комиссии. Пока только признак существенности затрат.
macro ActuateDealCOMIS(leg, dlcomis)
  var Immaterial = UNSET_CHAR, stat = 0;

  record leg_buf( dl_leg );
  record dlcomis_buf( dlcomis );

  SetBuff( leg_buf, leg );
  SetBuff( dlcomis_buf, dlcomis );

  // Несущественные затраты не бывают по клиентским сделкам, а также РЕПО, зачислениям, списаниям. Проверяется в С.
  if (ПроверятьСущественностьЗатрат() and (leg_buf.PFI > 0) and (leg_buf.TotalCost > 0))
     stat = GetImmaterialFlagByComiss(leg_buf, dlcomis_buf, Immaterial);
  end;

  if (stat == 0)
     dlcomis_buf.Immaterial = Immaterial;
  end;

  return stat;
end;

//заполнение параметров ТО 
macro FillCommissRQ_( tick_buf, leg_buf, comm_buf, dlrq_buf, rqacc_buf )

  dlrq_buf.rec.PlaceID = -1;

  if( rqacc_buf.rec.BANKCORRID > 0 )
     dlrq_buf.rec.PlaceID = rqacc_buf.rec.BANKCORRID;
  elif( rqacc_buf.rec.BANKID > 0 )
     dlrq_buf.rec.PlaceID = rqacc_buf.rec.BANKID;
  end;

  return 0;
end;

macro FillCommissRQ( tick, leg, comm, dlrq, rqacc )

  VAR tick_buf = TRecHandler( "dl_tick" ),
      leg_buf  = TRecHandler( "dl_leg" ),
      comm_buf = TRecHandler( "dlcomis" ),
      dlrq_buf = TRecHandler( "dlrq" ),
      rqacc_buf = TRecHandler( "dlrqacc");

  tick_buf.SetRecordAddr( tick );       
  leg_buf.SetRecordAddr( leg );       
  comm_buf.SetRecordAddr( comm );       
  dlrq_buf.SetRecordAddr( dlrq );       
  if( rqacc != NULL )
     rqacc_buf.SetRecordAddr( rqacc );       
  end;

  return FillCommissRQ_( tick_buf, leg_buf, comm_buf, dlrq_buf, rqacc_buf );
end;

//заполнение места хранения ТО 
macro FillPlaceRQ_(tick_buf, leg_buf, dlrq_buf, rqacc_buf)

  var DLMarket = TRecHandler( "dlmarket.dbt" );

  var Group = GetOpGroup( tick_buf.rec );
  var СделкаБиржевая = (IsEXCHANGE(Group) or ((tick_buf.rec.BofficeKind == DL_RETIREMENT) and (tick_buf.rec.Flag1 == SET_CHAR)));
  var БрокерскаяНЕ_РЕПО = ( (not IsREPO(Group)) and (IsBROKER(Group) or ((tick_buf.rec.BofficeKind == DL_RETIREMENT) and (tick_buf.rec.Flag4 == SET_CHAR))) );

  var БрокерскаяРЕПО = (IsREPO(Group) and (tick_buf.rec.Flag4 == SET_CHAR) and (tick_buf.rec.BrokerID > 0));
  var ЭтоПогашение = (tick_buf.rec.BofficeKind == DL_RETIREMENT);
  var ЭтоПолучениеПаев = (tick_buf.rec.BofficeKind == DL_INVESTSHARE);

  dlrq_buf.rec.PlaceID = -1;

  if( dlrq_buf.rec.SubKind == DLRQ_SUBKIND_CURRENCY )
     if( (not ЭтоПолучениеПаев) and СделкаБиржевая and (not БрокерскаяРЕПО) )
        if( FindDLMARKET(tick_buf.rec.MarketSchemeID,DLMarket) == 0 )
           dlrq_buf.rec.PlaceID = DLMarket.rec.Centr;
        end;
     elif( (not ЭтоПолучениеПаев) and (БрокерскаяНЕ_РЕПО or БрокерскаяРЕПО) )
        if( ЭтоПогашение )
           dlrq_buf.rec.PlaceID = tick_buf.rec.PartyID;
        else
           dlrq_buf.rec.PlaceID = tick_buf.rec.BrokerID;
        end;
     else
        if( rqacc_buf.rec.BANKCORRID > 0 )
           dlrq_buf.rec.PlaceID = rqacc_buf.rec.BANKCORRID;
        elif( rqacc_buf.rec.BANKID > 0 )
           dlrq_buf.rec.PlaceID = rqacc_buf.rec.BANKID;
        else
           dlrq_buf.rec.PlaceID = 0;/*заполнить автоматически (в компенс платежах и комп купона\ЧП)*/
        end;
     end;
  elif( dlrq_buf.rec.SubKind == DLRQ_SUBKIND_AVOIRISS )
     if( ЭтоПогашение or ЭтоПолучениеПаев )
        dlrq_buf.rec.PlaceID = leg_buf.rec.Registrar;
     else
        dlrq_buf.rec.PlaceID = leg_buf.rec.DepositID;
     end;
  end;

  return 0;
end;

macro FillPlaceRQ(tick, leg, dlrq, dlrqacc)

  VAR tick_buf = TRecHandler( "dl_tick" ),
      leg_buf  = TRecHandler( "dl_leg" ),
      dlrq_buf = TRecHandler( "dlrq" ),
      rqacc_buf = TRecHandler("dlrqacc");

  tick_buf.SetRecordAddr( tick );
  leg_buf.SetRecordAddr( leg );
  dlrq_buf.SetRecordAddr( dlrq );
  if( dlrqacc != NULL )
     rqacc_buf.SetRecordAddr( dlrqacc );
  end;

  return FillPlaceRQ_(tick_buf, leg_buf, dlrq_buf, rqacc_buf);
end;
