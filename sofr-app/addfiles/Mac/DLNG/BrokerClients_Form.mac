/*
$Name:        BrokerClients_Form.mac
$Module:      БО ЦБ, ФИССиКО, УВ
$Description: Форма для отчета о клиентах на брокерском обслуживании
*/
import "DlRepForm_h.mac";

CONST PNFLD_CLBOREP_PERIOD_BEGIN = 0,
      PNFLD_CLBOREP_PERIOD_END   = 1;

CONST H_PNFLD_BEGINDATE  = 100,
      H_PNFLD_ENDDATE    = 101;


PRIVATE MACRO FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Month, Year;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      DateSplit({curdate}, NULL, Month, Year);
      DateSplit(Date(1, Month, Year) - 1, NULL, Month, Year);
      FldRealValue = Date(1, Month, Year);
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldShowValue;
    if( (FldRealValue != null) AND (pThis.GetLinkedValue(H_PNFLD_ENDDATE) != null) )
      if( FldRealValue > pThis.GetLinkedValue(H_PNFLD_ENDDATE) )
        MsgBox( "Дата окончания не может быть меньше даты начала периода.");
        return CM_IGNORE;
      end;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Month, Year;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      DateSplit({curdate}, NULL, Month, Year);
      FldRealValue = Date(1, Month, Year) - 1;
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldShowValue;
    if( (FldRealValue != NULL) AND (pThis.GetLinkedValue(H_PNFLD_BEGINDATE) != null) )
      if( FldRealValue < pThis.GetLinkedValue(H_PNFLD_BEGINDATE) )
        MsgBox( "Дата окончания не может быть меньше даты начала периода.");
        return CM_IGNORE;
      end;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) ClBORep_Panel()
  var BeginDate, EndDate;

  PRIVATE MACRO Init()        
    AddFieldProc( 0, PNFLD_CLBOREP_PERIOD_BEGIN, H_PNFLD_BEGINDATE,  @FldProc_BeginDate);
    AddFieldProc( 0, PNFLD_CLBOREP_PERIOD_END,   H_PNFLD_ENDDATE,    @FldProc_EndDate);
  END;

  PRIVATE MACRO GetFieldValue(fldNum:integer):variant
    var showValue:variant, fldValue:variant;

    GetFieldValue(fldNum, @showValue, @fldValue);
    return fldValue;
  END;

  MACRO Run()
    var stat = Run();

    BeginDate = GetFieldValue(PNFLD_CLBOREP_PERIOD_BEGIN);
    EndDate = GetFieldValue(PNFLD_CLBOREP_PERIOD_END);

    return stat;
  END;

  initDL_CPanel("sp_rep.lbr", "CLBOREP");
END;