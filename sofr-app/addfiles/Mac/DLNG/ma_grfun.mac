/*
$Name:        ma_grfun.mac
$Module:      Securities
$Description: Функции для работы с графиками
*/

import BankInter, CTInter;
import "dlquery.mac", "dltransf.mac", "dlcnst.inc", "dldlngconst.mac", "spserv.mac", "sp_class.mac", "dl_grfun.mac", "cb_sql.mac", "sp_grfun.mac";

private var ArrGrDeal    = DL_GrDealData();
private var ArrRepozDeal = RepozDealData();
private var TemplDateArr = TArray( false, 100, 100 );


PRIVATE MACRO isBulkMessage(Code:string)
    var cmd = DL_RSDCommand();
    var query = " SELECT * FROM DIR_TYPESMESSAGE_DBT " +
                "  WHERE T_MESSAGECODE = ? ";
    cmd.AddParam(Code);        
    var DataSet = cmd.Execute(query);
    if( DataSet.moveNext() and ( DataSet.Bulk_Report == "X" ) )
        return true;
    else
        return false;
    end;
END;

PRIVATE MACRO getMessages( BulkMessage:@string,
                           EasyMessage:@string
                         )
    var cmd = DL_RSDCommand(),
      query = " SELECT * " +
              "   FROM DIR_SETUPMESSAGE_DBT STPMSG " +
              " WHERE STPMSG.T_KIND_OPERATION = 1090 " +
              "   AND STPMSG.T_REPORTING = chr(88) ";
        
    var DataSet = cmd.Execute(query);
    BulkMessage = EasyMessage = null;
    var stat = 0;

    while ( DataSet.moveNext() and ( not BulkMessage ) and ( not EasyMessage ) )    
        
        if (isBulkMessage(DataSet.MessageCode))
            if (BulkMessage == null)
                BulkMessage = DataSet.MessageCode;  
            else
                stat = 1;
            end;
        else
            if (EasyMessage == null)
                EasyMessage = DataSet.MessageCode;
            else
                stat = 1;
            end;    
        end;
        
    end;

    if ( (not BulkMessage) and (not EasyMessage) )
        stat = 1;
    end;

    return stat;   
END;

PRIVATE MACRO HasInvalidSymbol( Str )
  var stat = 0, i = 1, ch, ValidSymbols = "QWERTYUIOPASDFGHJKLZXCVBNMqwertyuiopasdfghjklzxcvbnm1234567890";

      while( (not stat) and (i <= strlen(Str)) )
        ch = SubStr( Str, i, 1 );
        if( not Index( ValidSymbols, ch ))
          stat = 1; 
        end;
        i = i + 1;
      end;

  return stat;
end;

private var repozdeal = TRecHandler("dl_repozdeal.dbt");

PRIVATE MACRO RepozBuff(MA:TRecHandler, MessageCode:@string)
                       
    var cmd = DL_RSDCommand();
    
    repozdeal.rec.messagecode         = MessageCode;
    repozdeal.rec.DocKind             = MA.rec.DocKind;
    repozdeal.rec.Reporting           = SET_CHAR;               //  1
    repozdeal.rec.DocID               = MA.rec.GenAgrID;
    repozdeal.rec.Date_Start          = MA.rec.Start;
    repozdeal.rec.Reporter_Bank       = MA.rec.Reporter_Bank;   // 2
    repozdeal.rec.Date_End            = GrDateArray[DLGR_DATEKIND_CLOSEDEAL];
    repozdeal.rec.TypeSecur           = MA.rec.TypeSecur;       // 3
    repozdeal.rec.FormSecur           = MA.rec.FormSecur;       // 4
    repozdeal.rec.Initiator           = MA.rec.Initiator;       // 5
    repozdeal.rec.Method_WayTwoWay    = MA.rec.Method_WayTwoWay;  // 6
    repozdeal.rec.Method_Reg          = MA.rec.Method_Reg;      // 7
    repozdeal.rec.Contract_Contr      = MA.rec.NumContr;        // 8
    repozdeal.rec.Reporter_Contr      = MA.rec.Reporter_Contr;  // 9
    repozdeal.rec.RepozID_Bank        = MA.rec.Bank_RIC;        // 10
    repozdeal.rec.Type_Reconciliation = MA.rec.Type_Sver;       // 11
    repozdeal.rec.Creator_UTI	        = MA.rec.Creator_UTI;     // 12
    repozdeal.rec.UTI_Contract	      = MA.rec.UTI_Code;        // 13
    repozdeal.rec.RelCodeID           = 2;
    
    GetRegistryValue("РЕПОЗИТАРИЙ\\СРОК ОЖИД.ЗАПР. ПРИ КОМБ.ПОДТВ.", V_INTEGER, repozdeal.rec.RequestTimeout, null);
        
    if(repozdeal.rec.RepozID_Bank == "")    // если не задан вручную, ищем есть ли единственое значение по умолчанию
        var BankRIC;
        var cmdRIC = DL_RSDCommand();
        var queryRIC = "SELECT t_code as RIC"
                      +"  FROM (  SELECT OBJ.T_CODE"
                      +"            FROM dobjcode_dbt obj"
                      +"           WHERE     OBJ.T_OBJECTTYPE = 3"
                      +"                 AND OBJ.T_OBJECTID = "+{OurBank}
                      +"                 AND OBJ.T_CODEKIND = 70"
                      +"                 AND OBJ.T_BANKDATE <= " + getsqldate(MA.rec.Date_GenAgr)
                      +"                 AND (   OBJ.T_BANKCLOSEDATE = "
                      +"                            TO_DATE ('01.01.0001', 'dd.mm.yyyy')"
                      +"                      OR OBJ.T_BANKCLOSEDATE > " + getsqldate(MA.rec.Date_GenAgr) + ")"
                      +"        ORDER BY OBJ.T_BANKDATE DESC)"
                      +" WHERE ROWNUM = 1";
        
        var DataSetRIC = cmdRIC.Execute(queryRIC);
        if( DataSetRIC.moveNext() )
          repozdeal.rec.RepozID_Bank = DataSetRIC.RIC;
        end;
    end;
    
    if ( (repozdeal.rec.Initiator == 2) and (repozdeal.rec.Method_WayTwoWay == 1) )
        repozdeal.rec.Status_IR = 11;
    else
        repozdeal.rec.Status_IR = 1;
    end; 
    
    if ( repozdeal.rec.TypeSecur <= 0 )
        repozdeal.rec.TypeSecur = 4;
    end;
    
    if ( repozdeal.rec.FormSecur <= 1 )
        repozdeal.rec.FormSecur = 3;
    end;
    
    repozdeal.rec.Contract = MA.rec.Code;

    var cont;
    var LEI;
    cmd = DL_RSDCommand();
    var query = " SELECT rsi_rsbparty.PT_GetPartyCode(?, 69) AS LEI FROM DUAL" ;
    
    if ( repozdeal.rec.Creator_UTI == 1 )
        cmd.AddParam({OurBank});
        cont = repozdeal.rec.Contract;
    else
        cmd.AddParam(MA.rec.PartyID);
        cont = repozdeal.rec.Contract_Contr;
    end;
    var DataSet = cmd.Execute(query);
    if ( DataSet.moveNext() and cont and (valtype(cont) != 26) and (DataSet.lei != "") and (repozdeal.rec.UTI_contract == ""))
        LEI = DataSet.LEI;
        if (not HasInvalidSymbol(DataSet.lei+cont) )
            repozdeal.rec.UTI_Contract = DataSet.LEI + cont; 
        else
            MsgBoxEx("UTI код содержит недопустимые символы",0);
        end;
    end;
    
    query  =  " SELECT kCode1.T_MAXCODELEN LEI_LEN, kCode2.T_MAXCODELEN RIC_LEN"
        +" FROM DOBJKCODE_DBT kCode1, DOBJKCODE_DBT kCode2"
        +" WHERE KCODE2.T_CODEKIND = 70 AND KCODE1.T_CODEKIND = 69";
    DataSet = cmd.Execute(query);
    if(DataSet.moveNext())  
        if(DataSet.LEI_LEN!=strlen(LEI))
          //  msgboxEx("Длина кода LEI должна быть 20 символов", 0);
        end;
        if(DataSet.RIC_LEN!=strlen(repozdeal.rec.RepozID_Bank))
            msgboxEx("Длина репозитарного идентификационного кода должна быть 12 символов", 0);
        end;        
    end;
    
    ArrRepozDeal.Add(repozdeal);
    
    return 0;
END;

private var grdeal = TRecHandler("dlgrdeal.dbt");
PRIVATE MACRO SetGrDeal( MA:TRecHandler, pTemplNum:integer, pPlanDate, mesID)
    grdeal.Clear();
    
    grdeal.rec.DocKind  = MA.rec.DocKind;
    grdeal.rec.DocID    = MA.rec.GenAgrID;
    grdeal.rec.TemplNum = pTemplNum;
      
    if(ValType(pPlanDate) != V_DATE)
        pPlanDate = date(0,0,0);
        if(ValType(TemplDateArr[pTemplNum]) != V_UNDEF)
            pPlanDate = GrDateArray[TemplDateArr[pTemplNum]];
        else
            var templ = TBFile("dlgrtempl.dbt");
            templ.Clear();
            templ.rec.Num = pTemplNum;                        
            if((templ.GetEQ()) and (ValType(GrDateArray[templ.rec.DateKind]) != V_UNDEF))
                pPlanDate = GrDateArray[templ.rec.DateKind];
                TemplDateArr[pTemplNum] = templ.rec.DateKind;
            end;
        end;
    end;
        
    grdeal.rec.PlanDate = pPlanDate;
    //grdeal.rec.PlanTime = MA.rec.Time;
    //grdeal.rec.FIID     = -1;
    var DealID;   // номер записи - сюда
    if(pTemplNum == 88)   // восстановление из ir_grdoc, левый TemplNum            
        grdeal.rec.TemplNum = IIF(MA.rec.Side2 == MA.rec.Initiator, DLGR_TEMPL_MAKECONTRACTWTHREQUEST, DLGR_TEMPL_MAKECONTRACT); // возвращаем нормальный TemplNum
        // при восстановлении записываем grdeal сразу, используя номер записи для dlgrdoc
        var Op_ID = 0;    // ID операции подготовки сообщения
        var query = "INSERT INTO ddlgrdeal_dbt (T_DocKind, T_DocID, t_TemplNum, t_PlanDate) VALUES (" + grdeal.rec.DocKind +
            ", " + grdeal.rec.DocID + ", " + grdeal.rec.TemplNum + ", TO_DATE('"+ grdeal.rec.PlanDate + "')) RETURNING t_ID INTO :DealID";
        var cmd = RSDCommand(query);      
        cmd.addParam("DealID", RSDBP_OUT, V_INTEGER);
        cmd.execute();
        DealID = cmd.value("DealID");
        var doc = TBFile("dlgrdoc", "W");
        query = "SELECT DISTINCT T_DocID, T_DocKind, T_ServDocID, T_ServDocKind FROM dir_grdoc_dbt WHERE T_DealKind = 4812 AND T_DealID = ?"; 
        var cmd2 = DL_RSDCommand(query);                                                     //  4812 = Генсоглашение ПИ
        cmd2.AddParam(MA.rec.GenAgrID);
        var DataSet = cmd2.Execute();
        var repozOper = IIF(grdeal.rec.TemplNum == DLGR_TEMPL_MAKECONTRACT, REPOS_SERVICEOPERATION, REPOS_INBOUNDMSG);
        while (DataSet.MoveNext())     
            doc.rec.ID = 0;
            doc.rec.GrDealID = DealID;
            doc.rec.DocID = DataSet.DocID;
            doc.rec.DocKind = DataSet.DocKind;
            doc.rec.ServDocID = DataSet.ServDocID;
            doc.rec.ServDocKind = DataSet.ServDocKind;
            doc.Insert();              
            if(DataSet.ServDocKind == repozOper)        
                Op_ID = DataSet.ServDocID;
            end;
        end;
        if(Op_ID == 0)
            MsgBoxEx("Не найдена операция репозитарного учета для связанных сообщений");
            return;
        end;
        // меняем статус в графике на "выполнено"
        SQL_Execute("UPDATE ddlgracc_dbt set T_State=" + DLGRACC_STATE_FACTEXEC + ", T_FactDate=(SELECT T_Date FROM dir_op_dbt WHERE T_ID=" + Op_ID +
            ") , T_Instance=1,  T_ID_Operation=" + Op_ID +" WHERE T_GrDealID=" + DealID + " AND T_AccNum=" + DLGR_ACCKIND_REPOSITORY);
        // убираем старые связи
        SQL_Execute("DELETE FROM dir_grdoc_dbt WHERE t_DealID=" + MA.rec.GenAgrID);
    elif(pTemplNum == 89) // восстановление сверки, левый TemplNum            
        query = "INSERT INTO ddlgrdeal_dbt (T_DocKind, T_DocID, t_TemplNum, t_PlanDate) VALUES (" + grdeal.rec.DocKind +
            ", " + grdeal.rec.DocID + ", " + DLGR_TEMPL_RECONCILIATION + ", TO_DATE('"+ grdeal.rec.PlanDate + "')) RETURNING t_ID INTO :DealID";
        cmd = RSDCommand(query);      
        cmd.addParam("DealID", RSDBP_OUT, V_INTEGER);
        cmd.execute();
        DealID = cmd.value("DealID");
        SQL_Execute("UPDATE ddlgrdoc_dbt SET T_GrDealID = " + DealID + 
            " WHERE T_DocID = " + MesID + " AND T_DocKind = 4861 AND T_ServDocID = 0 AND T_ServDocKind = 0"); 
    else
        ArrGrDeal.Add(grdeal);        
    end;
END;

PRIVATE MACRO isPendingRequestDeal( GenAgr:TRecHandler )
    return (GenAgr.rec.INITIATOR == 2) and (GenAgr.rec.METHOD_WAYTWOWAY == 1);
END;

PRIVATE MACRO getGrPlanDate(pTemplNum, pendingDay)
    var planDate = date(0, 0, 0);
    if(ValType(TemplDateArr[pTemplNum]) == V_UNDEF)
        var templ = TBFile("dlgrtempl.dbt");
        templ.rec.Num = pTemplNum;                        
        if((templ.GetEQ()) and (ValType(GrDateArray[templ.rec.DateKind]) != V_UNDEF))
            planDate = GrDateArray[templ.rec.DateKind];
            TemplDateArr[pTemplNum] = templ.rec.DateKind;
        end;
    end;
  
    if ((ValType(pendingDay) == V_INTEGER) and (pendingDay > 0))
        planDate = GetDateAfterWorkDays(date(planDate), pendingDay);
    end;

    return planDate;
END;

MACRO SetAllGrDealMA( MA:TRecHandler )

    GrDateArray.size = 0;
    //1. Дата заключения
    GrDateArray[DLGR_DATEKIND_DELADATE]  = MA.rec.Date_GenAgr;
    //14. Дата закрытия
    GrDateArray[DLGR_DATEKIND_CLOSEDEAL] = MA.rec.Date_End;
      
    var BulkMessage = null, EasyMessage = null; 
     if( (not getMessages(@BulkMessage, @EasyMessage)) and (MA.rec.Reporting == SET_CHAR) and  (MA.rec.pseudoga == UNSET_CHAR) )
        if( EasyMessage )
            var irdoc = TBFile("ir_grdoc");
            irdoc.rec.DealID = MA.rec.GenAgrID;
            irdoc.rec.DealKind = MA.rec.DocKind;
            var isInitRecurring = irdoc.getEQ;
            var makeCorrectMsg = IND_NO;
            if( isInitRecurring )
                var cmd = DL_RSDCommand();
                var query = " SELECT geninf.t_statuscode, geninf.t_RegistrationDate, geninf.t_RejectDate, geninf.t_CreationDate, geninf.t_TradeDate, "
                        + " geninf.t_PRDATE, geninf.t_internalmessageid "
                        + " from dir_grdoc_dbt ir_grdoc, dir_generalinf_dbt geninf "
                        + " where (ir_grdoc.t_ServDocKind =  4852" /*REPOS_MESSAGEGENERATION*/
                        + " or ir_grdoc.t_ServDocKind = 4869)" /*REPOS_INBOUNDMSG*/
                        + " and ir_grdoc.t_DealKind = " + MA.rec.DocKind
                        + " and ir_grdoc.t_DealID =  " + MA.rec.GenAgrID
                        + " and ir_grdoc.t_DocKind = " + 4853/*REPOS_GENERALINF*/
                        + " and geninf.t_internalmessageid = ir_grdoc.t_DocId ";
                var data = cmd.Execute(query);
                if(data.MoveNext())
                    var ComparisonDate, DeadlineOutMSG;
                    makeCorrectMsg = MsgBoxEx("Запланировать подготовку корректирующего сообщения?", MB_YES+MB_NO, IND_YES);
                        
                    GetRegistryValue("РЕПОЗИТАРИЙ\\ПРЕД.СРОК ОТПР.СБЩ.-АНКЕТЫ ДОГ.", V_INTEGER, DeadlineOutMSG, null);
                    if(data.statuscode == "REGISTERED")
                        ComparisonDate = data.RegistrationDate;
                    elif(data.statuscode == "RJCT")
                        ComparisonDate = data.RejectDate;
                    else
                        ComparisonDate = data.CreationDate;
                    end;                        
                    if(isPendingRequestDeal(MA))   // надо добавить сверку в график
                        SetGrDeal(MA, 88, GetDateAfterWorkDays(date(data.TradeDate), MA.rec.WAITINGPERIOD));  
                        SetGrDeal(MA, 89 /*DLGR_TEMPL_RECONCILIATION*/, date(data.PRDATE), data.InternalMessageID); // сверка
                    else
                        SetGrDeal(MA, 88, date(data.TradeDate));  // восстанавливаем СРС, используем левый TemplNum для обозначения восстановления
                    end;
                    if(makeCorrectMsg == IND_YES)
                        if({curdate} < (date(ComparisonDate) + DeadlineOutMSG))
                            SetGrDeal(MA, DLGR_TEMPL_MAKECONTRACT_CORRECTION, {curdate});
                        else
                            MsgBoxEx("Превышен допустимый срок отправки корректирующего сообщения " + DeadlineOutMSG + " рабочих дня", 0);
                        end;
                    end; 
                else    // data == NULL
                    if( isPendingRequestDeal(MA) )
                        SetGrDeal(MA, DLGR_TEMPL_MAKECONTRACTWTHREQUEST, getGrPlanDate(DLGR_TEMPL_MAKECONTRACTWTHREQUEST, MA.rec.WAITINGPERIOD));
                    else
                        SetGrDeal(MA, DLGR_TEMPL_MAKECONTRACT);
                    end;
                end;
            else
                if( isPendingRequestDeal(MA) )
                    SetGrDeal(MA, DLGR_TEMPL_MAKECONTRACTWTHREQUEST, getGrPlanDate(DLGR_TEMPL_MAKECONTRACTWTHREQUEST, MA.rec.WAITINGPERIOD));
                else
// DAN*  isupport 522493 
                   if ((MsgBoxEx("Вы хотите формировать CM010 ?", MB_YES + MB_NO, IND_YES) == IND_YES ))
                      SetGrDeal(MA, DLGR_TEMPL_MAKECONTRACT);
                   end;
// *DAN
                end;
            end;
            if(MA.rec.UNLIMITEDCONTRACT == UNSET_CHAR)
                SetGrDeal(MA, DLGR_TEMPL_CLOSECONTR);
            end;    
            
            RepozBuff(MA, EasyMessage);
        end;
    end;       
END;

PRIVATE MACRO InsertAllGrDeal()
    ArrGrDeal.Insert();
                          /*  var doc = TBFile("dlgrdoc", "W");;
                            doc.rec.GrDealID = grdeal.rec.ID;
                            doc.rec.DocID = data.InternalMessageID;
                            doc.rec.DocKind = 4853; // REPOS_GENERALINF
                            doc.rec.ServDocID = MA.rec.GenAgrID;
                            doc.rec.ServDocKind = 4851; // REPOS_SERVICEOPERATION  
                            doc.Insert();       */             //  ddlgrdoc_dbt
    ArrRepozDeal.Insert();
END;

MACRO CreateGraphMA( MAID:Integer ): integer
    /*var query = "SELECT t_GenagrID, t_DocKind, t_Date_GenAgr, t_Date_End, t_Method_WayTwoWay, t_Initiator, t_UnlimitedContract "
               +"  FROM ddl_GenAgr_dbt"
               +" WHERE t_GenagrID = " + MAID;
    
    var cmd = DL_RSDCommand(query);
    var DataSet = cmd.Execute();*/
    var vfile = TBFile("dl_genagr.dbt");
    vfile.rec.GenagrID = MAID;
    if ( vfile.getEQ )
        var MA:TRecHandler = TRecHandler("dl_genagr");
        MA.SetRecordAddr(vfile, 0, 0, true);
    /*if ( DataSet.MoveNext() )
        MA.Clear();
        MA.rec.GenAgrID          = DataSet.GenAgrID;
        MA.rec.DocKind           = DataSet.DocKind;
        MA.rec.Date_GenAgr       = DataSet.Date_GenAgr;
        MA.rec.Date_End          = DataSet.Date_End;
        MA.rec.Method_WayTwoWay  = DataSet.Method_WayTwoWay;
        MA.rec.Initiator         = DataSet.Initiator;
        MA.rec.UnlimitedContract = DataSet.UnlimitedContract;
        
        SetAllGrDealMA( MA );
        InsertAllGrDeal();
    end;*/
        SetAllGrDealMA( MA );
        InsertAllGrDeal(); 
    end;
    return 0;  // 0, если все ОК
OnError(er)
  return 1;    // 1, если ошибка 
END;