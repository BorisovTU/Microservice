/**
 @file catEditDisable_check.mac
 @brief Функционал запрета изменения значения категории объекта.
 
 Устанавливается для выбранной группы объектов (dobjgroup_dbt, поле t_macroname)
 
  # tag
 - functional_block:
 - code_type:Event_handler
 - Категории
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |25.01.2024 |Топорков Д.В.|BIQ-16820 BOSS-189 BOSS-1690                              | Создание
 */
import SecInter;

private const BROKERCONTR_ATTR_GROUP_TRADINGRESTRICTIONS = 125;
private const BROKERCONTR_ATTR_GROUP_NOTIFYFORM44 = 126;

var AcceptedActionCategory;

/**
@brief Функция получения текста предупреждения
@param[in] ObjectType Тип объекта
@param[in] GroupID ID группы из таблицы dobjgroup_dbt
@return V_STRING Текст предупреждения
*/
private macro GetAlert(ObjectType: Integer, GroupID: Integer): String
  var alert: String = "Изменение категории запрещено";
  
  if (ObjectType == OBJTYPE_BROKERCONTR_DL)
    if (GroupID == BROKERCONTR_ATTR_GROUP_TRADINGRESTRICTIONS)
      alert = "Изменение значения TRD_RESTR возможно только через панель ДБО";
    end;
    
    if (GroupID == BROKERCONTR_ATTR_GROUP_NOTIFYFORM44)
      alert = "";
    end;
  end;
  
  return alert;
end;

/**
@brief Функция проверки наличия признака "Доверительный управляющий" у клиента, которому принадлежит ДБО
*/
private macro IsContrOfTrustManager(dlContrID: Integer)
  var cmd, dataSet;
  
  cmd = DL_RSDCommand("SELECT 1 FROM dPartyOwn_dbt WHERE t_partyID = (SELECT sf.t_partyID FROM dDlContr_dbt dl, dSfContr_dbt sf WHERE sf.t_id = dl.t_sfContrID AND dl.t_dlContrID = ?) AND t_partyKind = ?");

  cmd.addParam(dlContrID);
  cmd.addParam(PTK_CONFID_MANAGER);
  dataSet = cmd.execute();

  if (dataSet.moveNext())
    return true;
  end;
  
  return false;
  
OnError(err)
  return false;
end;

/**
@brief Функция проверки категории на доступность редактирования
@param[in] ObjectType Тип объекта
@param[in] GroupID ID группы из таблицы dobjgroup_dbt
@param[in] AttrID ID устанавливаемого значения категории объекта
@param[in] ObjectID ID объекта
@return V_BOOL true - редактирование категории возможно, false - запрещено редактирование категории (по умолчанию)
*/
private macro CheckCategoryAcception(ObjectType, GroupID, AttrID, ObjectID): Bool
  var isAcceptable: Bool = false;
  
  if (ObjectType == OBJTYPE_BROKERCONTR_DL)
    if (GroupID == BROKERCONTR_ATTR_GROUP_NOTIFYFORM44)
      var dlContrID: Integer = Int(ObjectID);
      
      if (dlContrID > 0)
        // При значении категории "Да"(1) проверка признака "Доверительный управляющий" клиента
        if ((AttrID != 1) or (not IsContrOfTrustManager(dlContrID)))
          isAcceptable = true;
        end;
      end;
      
    end;
  end;
  
  return isAcceptable;
end;

/**
@brief Функция проверки категории при ручной установке по F2 из списка категорий объекта или из списка установленных атрибутов категории объектов
@param[in] ObjectType Тип объекта
@param[in] GroupID ID группы из таблицы dobjgroup_dbt
@param[in] CodeList Код справочника
@param[in] NumInList Номер в справочнике
@return V_BOOL true в случае успешной проверки
*/
macro CheckAttributeManual(ObjectType: Integer, GroupID: Integer, CodeList: String, NumInList: String): Bool
  MsgBox(GetAlert(ObjectType, GroupID));

  return false;
end;

/**
@brief Функция проверки при установке признака выбором по F9 из списка категорий объекта или из списка установленных атрибутов категории объекта
@param[in] ObjectType Тип объекта
@param[in] GroupID ID группы из таблицы dobjgroup_dbt
@param[in] AttrID ID атрибута
@return V_BOOL true в случае успешной проверки

Отрабатывает после CheckCategorySet
*/
macro CheckAttribute(ObjectType: Integer, GroupID: Integer, AttrID: Integer)
  AcceptedActionCategory = GetGlobalParameter("AcceptedActionCategory", true);
  
  return AcceptedActionCategory; 
end;

/**
@brief Функция проверки при удалении/очистке категории на объекте по F8
@param[out] param1 Результат проверки (?)
@param[in] GroupID ID группы из таблицы dobjgroup_dbt
@param[in] ObjectType Тип объекта
@param[in] ObjectID ID объекта
@param[in] param2 Необходимо дополнить
@param[in] AttrID_new ID атрибута
@param[in] param3 Необходимо дополнить
*/
macro CheckCategoryDelete(param1, GroupID, ObjectType, ObjectID, param2, AttrID_new, param3)
  msgbox("Удаление значения категории невозможно");
  
  setparm(0, 1); // запретим удаление категории установкой параметра 0 
end;

/**
@brief Функция проверки при удалении/очистке категории на объекте
@param[?] param1 Необходимо дополнить
@param[in] GroupID ID группы из таблицы dobjgroup_dbt
@param[in] ObjectType Тип объекта
@param[in] ObjectID ID объекта
@param[in] param2 Необходимо дополнить
@param[in] AttrID_new ID атрибута
*/
macro CheckCategorySet(param1, GroupID, ObjectType, ObjectID, param2, AttrID_new)
  AcceptedActionCategory = CheckCategoryAcception(ObjectType, GroupID, AttrID_new, ObjectID);
  
  if (not AcceptedActionCategory)
    var messageText = GetAlert(ObjectType, GroupID);
  
    if (StrLen(messageText) > 0)
      MsgBox(messageText);
    end;
  end;
  
  SetGlobalParameter("AcceptedActionCategory", AcceptedActionCategory);   

  return true;
end;