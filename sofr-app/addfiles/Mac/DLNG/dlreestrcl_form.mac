/*
$Name:        dlreestrcl_form.mac
$Module:      Ядро Securities
$Description: Форма ввода данных отчета "Реестр клиентов ПУРЦБ"
*/
import "DlRepForm_h.mac", "globals.mac";

CONST PNFLD_REESTRCL_SERVICE_KIND_SD          = 0,       /* Фондовый дилинг */
      PNFLD_REESTRCL_SERVICE_KIND_DV          = 1,       /* Срочные контракты */
      PNFLD_REESTRCL_SERVICE_KIND_CUR         = 2,       /* Валютный рынок */
      PNFLD_REESTRCL_CLIENT_CODE              = 3,       /* Клиент */
      PNFLD_REESTRCL_CLIENT_NAME              = 4,       /* */
      PNFLD_REESTRCL_ISFORM                   = 5,       /* */
      PNFLD_REESTRCL_FORMSUB                  = 6,       /* Форма субъекта */
      PNFLD_REESTRCL_ISVID                    = 7,       /* */
      PNFLD_REESTRCL_VIDSUB                   = 8,       /* Вид субъекта */
      PNFLD_REESTRCL_QIINVESTOR               = 9,      /*Квалифицированный инвестор*/
      PNFLD_REESTRCL_NORESIDENT               = 10,       /* Флаг резидентности */
      PNFLD_REESTRCL_CLOSEORDER               = 11,       /* Включать закрытые договора */
      PNFLD_REESTRCL_BEGINDATE                = 12,      /* Начало периода */
      PNFLD_REESTRCL_ENDDATE                  = 13,      /* Окончание периода */
      PNFLD_REESTRCL_TO_EXEL                  = 14;      /* В Excel */
      

CLASS Dl_ReestrCl_Form_Data()
  VAR 
  ServKind_SD,
  ServKind_DV,
  ServKind_CUR,
  ClientCode,
  ClientName,
  IsForm,
  FormSub,
  IsVid,
  VidSub,
  QiInvestor,
  Noresident,
  CloseOrder,
  BegDate,
  EndDate,
  ToExcel;

  ServKind_SD = "X";
  ServKind_DV = "X";
  ServKind_CUR = "X";
  ClientCode = -1;
  ClientName = -1;
  IsForm     =   0;
  FormSub    =   0;
  IsVid      =   0;
  VidSub     =   0;
  QiInvestor =  "";
  Noresident =  "";
  CloseOrder = "X";
  BegDate = {curdate};
  EndDate = {curdate};
  ToExcel = "X";
END;

VAR DlReestrClRepFormData = Dl_ReestrCl_Form_Data();

PRIVATE CONST
  H_CLIENT_CODE     = 101,
  H_CLIENT_NAME     = 102,
  H_ISFORM          = 103,
  H_FORMSUB         = 104,
  H_ISVID           = 105,
  H_VIDSUB          = 106,
  H_SERVICE_KIND_SD = 107,
  H_QiInvestor      = 108;

/*клиент БОЦБ\УВ\ПИ в заданном филиале*/
PRIVATE MACRO Usr_FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party     = TRecHandler("party.dbt");
  var ClientName = "";
  var ServKind = TArray();
 
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue(H_CLIENT_NAME, "");
     else
        if( not ПолучитьСубъекта(FldRealValue, Party) )
           FldShowValue = ПолучитьКодСубъекта(FldRealValue, PTCK_CONTR);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;

     pThis.SetLinkedValue(H_CLIENT_NAME, "");
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     if( pThis.GetFieldValue(PNFLD_REESTRCL_SERVICE_KIND_SD) == "X" )
        ServKind[ServKind.Size] = PTSK_STOCKDL;
     end;

     if( pThis.GetFieldValue(PNFLD_REESTRCL_SERVICE_KIND_DV) == "X" )
        ServKind[ServKind.Size] = PTSK_DV;
     end;

     if( pThis.GetFieldValue(PNFLD_REESTRCL_SERVICE_KIND_CUR) == "X" )
        ServKind[ServKind.Size] = PTSK_CM;
     end;

     if( DL_ListClientByServKind(@ClientName, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ServKind, false, {OperDprt}) )
        pThis.SetLinkedValue(H_CLIENT_NAME, ClientName);
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_FormSub( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if(FldRealValue == 0)
    FldShowValue = "";
  end;
  if( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA(3400, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), NULL);
  end;
  return CM_DEFAULT;
END;

MACRO CheckBoxSERVICE_KIND_SD( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";

           EnableFields(pThis.Data(), PNFLD_REESTRCL_QIINVESTOR);
        else
           FldShowValue = FldRealValue = "";

           pThis.SetLinkedValue(H_SERVICE_KIND_SD, null);
           pThis.SetLinkedValue(H_QiInvestor, null);
           DisableFields(pThis.Data(), PNFLD_REESTRCL_QIINVESTOR);
        end;
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_VidSub( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT )
  if(FldRealValue == 0)
    FldShowValue = "";
  end;
  if( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    return DL_ComboBox( "select t_Name2, t_PartyKind from dptkind_dbt ",
                        "t_Name2", "t_PartyKind", 
                        @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY()
                      );
  end; 
END;

PRIVATE MACRO DL_FldProc_IsFormVid( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT, pos:INTEGER):INTEGER
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
    if ( FldRealValue == 0)
        FldShowValue = "=";
        FldRealValue = "1";
        EnableFields( pThis.Data(), pos );

    elif(FldRealValue == 1)
        FldShowValue = "!";
        FldRealValue = "2";
    else
        FldShowValue = "";
        FldRealValue = "0";
        DisableFields( pThis.Data(), pos );
    end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_IsFormVid1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     DL_FldProc_IsFormVid(pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_REESTRCL_FORMSUB);
	 	if (FldRealValue==0)
			pThis.SetLinkedValue( H_FORMSUB, 0 );
		end;
  end;
END;

PRIVATE MACRO DL_FldProc_IsFormVid2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     DL_FldProc_IsFormVid(pThis, Cmd, Key, @FldShowValue, @FldRealValue, PNFLD_REESTRCL_VIDSUB);
		if (FldRealValue==0)
			pThis.SetLinkedValue( H_VIDSUB, 0 );
		end;
  end;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;
                    
CLASS (DL_CPanel) DL_ReestrCl_Panel()

  PRIVATE MACRO Init()
     AddFieldProc(0, PNFLD_REESTRCL_SERVICE_KIND_SD, H_SERVICE_KIND_SD,         @CheckBoxSERVICE_KIND_SD,   DlReestrClRepFormData.ServKind_SD);
     AddFieldProc(0, PNFLD_REESTRCL_SERVICE_KIND_DV, DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,   DlReestrClRepFormData.ServKind_DV);
     AddFieldProc(0, PNFLD_REESTRCL_SERVICE_KIND_CUR,DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,   DlReestrClRepFormData.ServKind_CUR);
     AddFieldProc(0, PNFLD_REESTRCL_CLIENT_CODE,     H_CLIENT_CODE,             @Usr_FldProc_Client,    DlReestrClRepFormData.ClientCode);
     AddFieldProc(0, PNFLD_REESTRCL_CLIENT_NAME,     H_CLIENT_NAME,             @Default,               DlReestrClRepFormData.ClientName);
     AddFieldProc(0, PNFLD_REESTRCL_ISFORM,          H_ISFORM,                  @DL_FldProc_IsFormVid1, DlReestrClRepFormData.IsForm);
     AddFieldProc(0, PNFLD_REESTRCL_FORMSUB,         H_FORMSUB,                 @DL_FldProc_FormSub,    DlReestrClRepFormData.FormSub, 29, 9);
     AddFieldProc(0, PNFLD_REESTRCL_ISVID,           H_ISVID,                   @DL_FldProc_IsFormVid2, DlReestrClRepFormData.IsVid);
     AddFieldProc(0, PNFLD_REESTRCL_VIDSUB,          H_VIDSUB,                  @DL_FldProc_VidSub,     DlReestrClRepFormData.VidSub , 29, 10);
     AddFieldProc(0, PNFLD_REESTRCL_QIINVESTOR,      H_QiInvestor,              @DL_FldProc_CheckBox,       DlReestrClRepFormData.QiInvestor);
     AddFieldProc(0, PNFLD_REESTRCL_NORESIDENT,      DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,   DlReestrClRepFormData.Noresident);
     AddFieldProc(0, PNFLD_REESTRCL_CLOSEORDER,      DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,   DlReestrClRepFormData.CloseOrder);
     AddFieldProc(0, PNFLD_REESTRCL_BEGINDATE,       DL_PNFLD_BEGINDATE,        @DL_FldProc_BeginDate,  DlReestrClRepFormData.BegDate);
     AddFieldProc(0, PNFLD_REESTRCL_ENDDATE,         DL_PNFLD_ENDDATE,          @DL_FldProc_EndDate,    DlReestrClRepFormData.EndDate );                 
     AddFieldProc(0, PNFLD_REESTRCL_TO_EXEL,         DL_PNFLD_CHECKBOX,         @DL_FldProc_CheckBox,   DlReestrClRepFormData.ToExcel);

	 if( this.GetFieldValue(PNFLD_REESTRCL_ISFORM) == 0)
     DisableFields(this.Data(), PNFLD_ReestrCl_FORMSUB);
	 end;
	 if( this.GetFieldValue(PNFLD_REESTRCL_ISVID) == 0)
     DisableFields(this.Data(), PNFLD_ReestrCl_VIDSUB);
	 end;
  END;

  PRIVATE MACRO Check():BOOL
     var RetVal:bool = true;

     if( (this.GetFieldValue(PNFLD_REESTRCL_SERVICE_KIND_SD) != SET_CHAR) and
         (this.GetFieldValue(PNFLD_REESTRCL_SERVICE_KIND_DV) != SET_CHAR) and
         (this.GetFieldValue(PNFLD_REESTRCL_SERVICE_KIND_CUR) != SET_CHAR)
       )
        MsgBox("Необходимо выбрать хотя бы один вид обслуживания");
        RetVal = false;
     end;

     return RetVal;
  END;
  
  initDL_CPanel("dlrep.lbr", "REESTRCL");
END;
