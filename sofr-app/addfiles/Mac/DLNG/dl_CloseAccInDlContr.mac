/**
 @file dl_CloseAccInDlContr.mac
 @brief Макрос пользовательских действий перед закрытием брокерских счетов по субдоговорам ДБО.
 
 Функциональность пользовательских действий, вызываемая перед закрытием счетов по субдоговрам ДБО из закрытого кода.
 (Ядро Securities)
 
  # tag
 - functional_block:Счета
 - code_type:Event_handler
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |22.09.2023 |Топорков Д.В.|CCBO-7595 CCBO-7601                                       | Насыщение кода комментариями
 |?          |?            |?                                                         | Создание                   
 */

import "dlcontrfunc.mac", "CCloseAccounts.mac";

/**
@brief Функция пользовательских действий перед закрытием счетов по субдоговорам ДБО
@param[in] Account Закрываемый брокерский субсчет (будет закрыт после выполнения пользовательских действий в закрытом коде)
@param[in] DlContrMP Связанная площадка ДБО
@param[in] CloseDate Значение которое вернет функция при condition = false (тип Variant)
@param[out] NeedCloseAcc Нужно ли закрывать счет (true по умолчанию)
@return 0 Успех
@return !=0 Код ошибки

UPD BPV
NeedCloseAcc - true - закрываем счет, false - счет не закрывается
stat = 0 - коммит транзакции, msgbox не работают. 
stat = 1 - роллбэк транзакции, последний мсжбокс будет выведен в качестве ошибки. Если вызовов было несколько, то может показать предыдущий мсжбокс, поэтому информационные сообщения лучше вообще не показывать  

# При закрытии из скроллинга по Альт+А
  ## 1. по площадке ЕДП. 
      Система Спросит Действительно ли закрыть брокерский счет?
      Если нет, то ничего не произойдет
      Если да
      то Система предпреждает, что счет используется для учет позиции по ЕДП, Закрыть или нет?
      При отказе счет не закроет.
      При согласии счет закроется ( и все связанные 47423 и ВУ,  список закрытых счетов не отобразится и отобразить его возможности нет).  
      Если будет ошибка закрытия, то выдаст сообщение с причиной почему закрыть не может.
  ## 2. По площадке не ЕДП
      Система Спросит Действительно ли закрыть брокерский счет?
      Если нет, то ничего не произойдет
      Если да, то счет закроется ( и все связанные 47423 и ВУ, список закрытых счетов не отобразит и отобразить нет возможности).  
      Если будет ошибка закрытия, то выдаст причину почему   закрыть не может.
# При закрытии площадки ЕДП (при сохранении ДБО после закрытия площадки)
  Система по каждому закрываемому счету предпреждает, что счет используется для учет позиции по ЕДП,   Закрыть или нет?
  При отказе счет и связанные счета закрыт не будет, площадка продолжит закрываться
  При согласии счет закроется ( и все связанные 47423 и ВУ).  Площадка продолжит закрывать. Если при этом будет ошибка закрытия, то выдаст причину почему закрыть не может и площадка закрыта не будет.
# При закрытии площадки не ЕДП (при сохранении ДБО после закрытия площадки)
  Система ничего не говорит. Просто закрывает площадку и счета.
  Если при этом будет ошибка закрытия счета, то выдаст причину почему закрыть не может и площадка закрыта не будет, сохранение остановится.
# При закрытии ДБО
  закрываются все площадки и все счета как и раньше. Старый механизм без изменений.

*/
macro CloseAccInDlContr(Account, DlContrMP, CloseDate, NeedCloseAcc)
   var stat = 0,
       acc_buf = TRecHandler( "account" ),
       mp_buf = TRecHandler( "dlcontrmp" );
   var cmd, rs, msg = "";
   var err, log, item, StrLog = "", ca;

   var DLContr_Close = GetGlobalParameter("DLCONTR_MACROACTION_CLOSE"/*, true*/);///> если вызов из списка счетов по Alt+A, то null, если при закрытии ДБО = "X", устанавливается в dlcontrsc.mac

   acc_buf.SetRecordAddr( Account );
   mp_buf.SetRecordAddr( DlContrMP );
   

   cmd = RsdCommand(" select count (1) from dmcaccdoc_dbt where t_account = ? and t_iscommon = chr(88) and t_chapter = ? and t_currency = ? and t_owner = ? ");
   cmd.addParam("", RSDBP_IN, acc_buf.rec.account);
   cmd.addParam("", RSDBP_IN, acc_buf.rec.chapter);
   cmd.addParam("", RSDBP_IN, acc_buf.rec.code_currency);
   cmd.addParam("", RSDBP_IN, acc_buf.rec.client);
   rs = RsdRecordSet(cmd);
   rs.movenext();

   if (DLContr_Close == "X")///> если действие происходит при закрытии ДБО, а не из списка счетов по Alt+A и не при закрытии площадки, то счета будем закрывать в dlcontrsc.mac
      NeedCloseAcc = false; 
   elif (ДоговорЕДП(0, mp_buf.rec.sfcontrid))   ///> #298821 точно определить площадку по договору ЕДП нет возможности. Но если попалась другая площадка ЕДП, то значит и закрываемая площадка тоже ЕДП 
      if (rs.Value(0) > 1)
         msg = "Закрываемый счет используется для учета Единой денежной позиции и привязан к нескольким площадкам. Продолжить закрытие счета "+acc_buf.rec.account+" и связанных счетов 47423 и ВУ?"
      elif(rs.Value(0) == 1)
         msg = "Закрываемый счет используется для учета Единой денежной позиции. Продолжить закрытие счета "+acc_buf.rec.account+" и связанных счетов 47423 и ВУ?"
      end;
      if (GetTrue(false, msg))
         NeedCloseAcc = true; 
      else
         NeedCloseAcc = false; 
      end;
   end;

   if ( (NeedCloseAcc) and (ValType(DLContr_Close) == V_UNDEF))
      ca = CCloseContrAccounts(0, mp_buf.rec.sfcontrid, acc_buf.rec.code_currency);
      if ( ca.CloseAcc(@err, @log) > 0 )
         for ( item, err )
            if ( item.rest != 0 )
               StrLog = StrLog + item.err + " " + item.acc + "   " + item.rest + "\n";
            else
               StrLog = StrLog + item.acc + "   " + item.err + "\n";
            end;
         end;
         MsgBox("Невозможно закрыть счета по договору \n\n" + StrLog);
         stat = 1;
      elif (log.size > 0)
         for ( item, log )
            StrLog = StrLog + item.acc + "   " + item.err + "\n";
         end;
            //MsgBox(StrLog); ///> если DLContr_Edit == "X", то MsgBox не сработает и при этом "перекричит" ошибку, если она будет, Из списка счетов по Alt+A сработает, но пока мы не можем понять откуда выфполнен вызов
         stat = 0;
      end;
   else
      stat = 0;
   end;

   SetParm(3, NeedCloseAcc);

   return stat;
end;
