/*DEF-43666 SD9401285 Антипов В. М., 63/17-18497873, ЕКК 464351 не зарегистрирован на СПБ Бирже 
   Гераськина Т.В.
   Измененяи в реализации относительно АЗ:
   1. Создается новое событие регистрации клиента вместо изменения старого для сохранений истории
   2. Используется макрос вместо pl/sql процедуры, так как для штатной выгрузки необходимо 
      не только заполнение t_xml, а и наличие записей в dimage_dbt + dimgdata_dbt
   3. Отбор только по открытым площадкам / договорам
*/

import "dlcontrmsg_spb.mac";

private macro GetDlContrMP(rh:TRecHandler, mpID:integer) :bool
  return DL_GetRecHandler(rh,
                          "SELECT * FROM DDLCONTRMP_DBT WHERE T_ID = ? ", mpID);
end;

private macro Run43666
var sql_str, rs;
var err_msg;

// закоментированные строки не нужны для работы, но для визуального просмотра удобнее с ними
sql_str = String(
"SELECT distinct \n",
//"   msg_er.t_id, \n",
"   msg_er.t_dlcontrid, msg_er.t_kind, \n",
"   sfc.t_id as sf_contrid, sfc.t_number, sfc.t_partyid, \n",
"   mp.t_id mpid, mp_sf.t_id mpsf_id \n",
//"   ,(SELECT code.t_code FROM dobjcode_dbt code WHERE code.t_objecttype = 3 AND code.t_objectid = sfc.t_partyid AND code.t_codekind = 1 AND code.t_state = 0) as party_code, \n",
//"   (SELECT p.t_shortname FROM dparty_dbt p WHERE p.t_partyid = sfc.t_partyid) as p_shortname, \n",
//"   val_1.t_name as str_kind, msg_er.t_createdate, msg_er.t_senddate, \n",
//"   msg_er.t_sendmesstate, alg.t_sznamealg as str_state, to_char(msg_er.t_respdata) as t_respdata, msg_er.t_respdate \n",
" FROM ddlcontrmsg_dbt msg_er, \n",
"      ddlcontr_dbt dlc, \n",
"      dsfcontr_dbt sfc, \n",
"      dllvalues_dbt val_1, \n",
"      dnamealg_dbt alg, \n",
"      ddlcontrmp_dbt mp, dsfcontr_dbt mp_sf \n",
" WHERE dlc.t_dlcontrid = msg_er.t_dlcontrid \n",
"   AND sfc.t_id = dlc.t_sfcontrid \n",
"   AND mp.t_dlcontrid = dlc.t_dlcontrid AND mp.t_marketid = 151337 \n",
"   AND mp_sf.t_id = mp.t_sfcontrid \n",
"   and mp_sf.t_servkind = 1 and mp_sf.t_servkindsub = 8 \n",
"   AND msg_er.t_kind in ( 2,7 ) \n", 
"   AND val_1.t_list = 1143 \n",
"   AND alg.t_itypealg = 7535 \n",
"   AND alg.t_inumberalg = msg_er.t_sendmesstate \n",
"   AND val_1.t_element = msg_er.t_kind \n",
"   AND msg_er.t_sendmesstate = 305 /*Ошибка*/ \n",
"   AND instr(to_char(msg_er.t_respdata), 'Недопустимые символы в поле (5)') > 0 \n",
"   AND NOT EXISTS (SELECT * \n",
"                     FROM ddlcontrmsg_dbt msg_ok \n",
"                    WHERE msg_ok.t_dlcontrid = msg_er.t_dlcontrid \n",
"                      AND msg_ok.t_kind = msg_er.t_kind \n",
"                      AND msg_ok.t_marketid = msg_er.t_marketid \n",
"                      AND msg_ok.t_sendmesstate in( 310,300,302) /*Подтверждено биржей или Не отправлено или Ожидает отправки*/) ");
                                                                                                                                   
rs = RSDRecordSet(sql_str);

var dlcontrmsg = Tbfile("dlcontrmsg.dbt", "W", 0);
var dlContr = TRecHandler("dlcontr.dbt"), sfContr = TRecHandler("sfcontr.dbt"), dlContrMp = TRecHandler("dlcontrmp.dbt");
var sfContr_sub = TRecHandler("sfcontr.dbt");
var stat:integer = 0;

var dt = StrSubSt(string(Date()),".","");
var tm = StrSubSt(string(time()),":","");
var InfoLogFileName = GetTxtFileName( "ChangeNotResMsgForSpb"+dt+"_"+StrSubst(tm," ","0") );
SetOutput( InfoLogFileName, true ); 

var cnt = 0;
InitProgress(-1, "Обработка договоров", "Обработка договоров");
while (rs.movenext)
   stat = 0;

   if((DL_GetDlContrRH(dlContr, rs.value("t_dlcontrid")) == false) or
      (DL_GetSfContrRH(sfContr, rs.value("sf_contrid")) == false))
      stat = 1;
      err_msg = rs.value("t_number") + " Не найден договор БО "+rs.value("t_dlcontrid") +" / "+rs.value("sf_contrid");
   end;
   if (sfContr.rec.dateclose != date(0,0,0))
      stat = 1;
      err_msg = rs.value("t_number") + " Договор закрыт "+sfContr.rec.dateclose;
   end;
   
   if (not GetDlContrMp(dlContrMp, rs.value("mpid")))
      stat = 1;
      err_msg = rs.value("t_number") + "Не найдена площадка " + rs.value("mpid");
   end;
  
   if (stat == 0)
      if (DL_GetSfContrRH(sfContr_sub, rs.value("mpsf_id")) == false)
         stat = 1;
         err_msg = rs.value("t_number") + "Не найден субдоговор " + rs.value("mpid");
      end;
   end;
   
   if (sfContr_sub.rec.dateclose != date(0,0,0))
      stat = 1;
      err_msg = rs.value("t_number") + " Субоговор закрыт "+sfContr_sub.rec.dateclose;
   end;
   
   var fullPathName = "";
   var objMsg=TDlMsgSPB();
   stat = objMsg.Stat();
   if (stat == 0)
      dlcontrmsg.clear;
      // Создать запись о сообщении ДБО
      dlcontrmsg.rec.DlContrID = DlContr.rec.DlContrID;
      dlcontrmsg.rec.Kind = rs.value("t_kind")/*OBJTYPE_DLCONTRMSG_MPREG*/;
      dlcontrmsg.rec.CreateDate = date();
      dlcontrmsg.rec.CreateTime = Time();
      dlcontrmsg.rec.IsOnlineSendMes = UNSET_CHAR;
      dlcontrmsg.rec.MarketID = dlContrMp.rec.marketid;
      dlcontrmsg.rec.MpCode = dlContrMp.rec.mpcode;
      if(not dlcontrmsg.insert())
         stat = 1;
         err_msg = rs.value("t_number") + " Не удалось создать запись сообщения на биржу";
      end;
   end;

   if(stat == 0)
      fullPathName = objMsg.СоздатьИндивидуальноеСообщение(DlContr, SfContr, DlContrMp, DlContrMsg);
   end;

   if((stat == 0) and (fullPathName == ""))
      stat = 1;
      err_msg = rs.value("t_number") + " Для клиента \"" + DL_GetPartyShortName(SfContr.rec.PartyID) + "\" не удалось сформировать файл сообщения";
   end;

   if(stat == 0)
      if(not LoadImageObj(fullPathName, OBJTYPE_BSCMSG_DL, UniID(dlcontrmsg, OBJTYPE_BSCMSG_DL), 1));
         stat = 1;
         err_msg = rs.value("t_number") + " К записи о сообщении не удалось прикрепить файл сообщения";
      end;
   end;

   if(stat == 0)
      if(not RemoveFile(fullPathName))
         stat = 1;
         err_msg = rs.value("t_number") + "Не удалось удалить файл \"" + fullPathName + "\"";
      end;
   end;

   if (stat == 0)
      err_msg  = rs.value("t_number") + " OK";
   end;

   println(err_msg);
   cnt = cnt+1;

   UseProgress(cnt);
end;
RemProgress;

if (cnt == 0)
   println("Нет подходящих договоров");
end;

SetOutput( NULL, true ); 
ViewFile( InfoLogFileName );

end;


run43666;
exit(1);

