/*
$Name:             ws_md_tradevolume.mac
$Module:           Монитор дилера 
$Description:      WEB. Макрос сервисов объема торгов
*/
import "ws_md.mac";
import "ws_common.mac";
import "ws_md_convdeal.mac";
import "ws_datafilter.mac";

private const MacroName = "ws_md_tradevolume.mac";

// Класс скроллинга "История торгов"
class CTradeVolume
   var PFI:Integer;               // FIID базовой валюты
   var CFI:Integer;               // FIID валюты
   var BuySum:Money;              // Общая сумма сделок покупки (в разрезе валют)
   var SaleSum:Money;             // Общая сумма сделок продажи (в разрезе валют)
   var Spred:Money;               // Общий спред, вычисляется как Spred = BuySum - SaleSum
   var CurPairName:String;        // Наименование валютной пары
end;


// Получить данные для скроллинга "Объем торгов"
macro MdGetTradeVolume (
   tradeDate : Date // Дата торгов
)
   var stat:Integer = 0;
   var TradeVolumeList = TArray();
   var TradeVolume = null;

   InitError();

   if( tradeDate == null )
      RunError("Не задан обязательный параметр функции: дата торгов ");
   end;

   var query = " SELECT RKCURPAIR.T_PFI t_PFI, -1 t_CFI, " + 
               " (SELECT t_CCY FROM DFININSTR_DBT WHERE t_fiid = RKCURPAIR.T_PFI) t_FI_Name " +
               " FROM DRKCURPAIR_DBT RKCURPAIR " +
               " WHERE RKCURPAIR.T_ISSHOW = CHR(88) " +
               " UNION " + 
               " SELECT  -1 t_PFI, RKCURPAIR.T_CFI t_CFI, " +
               " (SELECT t_CCY FROM DFININSTR_DBT WHERE t_fiid = RKCURPAIR.T_CFI) t_FI_Name " +
               " FROM DRKCURPAIR_DBT RKCURPAIR " +
               " WHERE RKCURPAIR.T_ISSHOW = CHR(88)";

   var ds = TRsbDataSet(query);

   while( ds.MoveNext() )
      TradeVolume = CTradeVolume();

      TradeVolume.PFI = SQL_ConvTypeInteger(ds.PFI);
      TradeVolume.CFI = SQL_ConvTypeInteger(ds.CFI);
      TradeVolume.CurPairName = SQL_ConvTypeStr(ds.FI_Name);
      TradeVolume.BuySum = $0;
      TradeVolume.SaleSum = $0;
      TradeVolume.Spred = $0;
	  
      TradeVolumeList[TradeVolumeList.Size] = TradeVolume;
   end;

   var ConvDealList = TArray();
   var ConvDeal = null;
   // получаем данные о сделках через api
   var pageSize = 0;
   var pageIndex = 0;
   var filterItems = TArray();
   var orderItems = null;
   var val, fc;

   // Подготовим фильтр по дате валютирования
   // В банке пока всегда дата валютирования БА = дате валютирования КА.  Соответственно в эту дату сделка и закрывается
/*
   val = TArray();
   val[val.Size] = tradeDate;

   fc = FilterConditionItem();
   fc.Id = 0;
   fc.Condition = FilterCondition_Equal;
   fc.Value = val;
   fc.IsNot = false;
   fc.ValueType = FilterParameterType_Date;

   filterItems[filterItems.Size] = fc; 
 */
   // Подготовим фильтр по статусу (закрытые)
   val = TArray();
   val[val.Size] = 3; // закрытые

   fc = FilterConditionItem();
   fc.Id = 11;
   fc.Condition = FilterCondition_Equal;
   fc.Value = val;
   fc.IsNot = false;
   fc.ValueType = FilterParameterType_Int;

   filterItems[filterItems.Size] = fc; 
  
   var rs = ExecMacroFile("fx_api.mac", "GetForexDealsRecordset", pageSize, pageIndex, filterItems, orderItems, tradeDate );

   while( rs.MoveNext() )
      ConvDeal = CConvDealList();
	  
      ConvDeal.DealKind = SQL_ConvTypeStr(rs.value("T_DEALKIND")); // покупка/продажа
      ConvDeal.PFI = SQL_ConvTypeInteger(rs.value("T_PFI"));
      ConvDeal.CFI = SQL_ConvTypeInteger(rs.value("T_CFI"));
      ConvDeal.CurPair = SQL_ConvTypeStr(rs.value("T_CURPAIR"));
      ConvDeal.Principal = SQL_ConvTypeSum(rs.value("T_PRINCIPAL"));
      ConvDeal.Cost = SQL_ConvTypeSum(rs.value("T_COST")); 
	  
      ConvDealList[ConvDealList.Size] = ConvDeal;
   end;

   var cnt_tv = 0;
   var cnt_cd = 0;
   
   while( (cnt_tv < TradeVolumeList.Size)
      and (ConvDealList.Size > 0))
      cnt_cd = 0;
      while( cnt_cd < ConvDealList.Size )
         // Базовая валюта
         if( ConvDealList[cnt_cd].PFI == TradeVolumeList[cnt_tv].PFI ) 
            if( ConvDealList[cnt_cd].DealKind == "Покупка" )
               TradeVolumeList[cnt_tv].BuySum = TradeVolumeList[cnt_tv].BuySum + ConvDealList[cnt_cd].Principal;
               TradeVolumeList[cnt_tv].Spred = TradeVolumeList[cnt_tv].Spred + ConvDealList[cnt_cd].Principal;
            else // "Продажа"
               TradeVolumeList[cnt_tv].SaleSum = TradeVolumeList[cnt_tv].SaleSum + ConvDealList[cnt_cd].Principal;
               TradeVolumeList[cnt_tv].Spred = TradeVolumeList[cnt_tv].Spred - ConvDealList[cnt_cd].Principal;				 
            end;
         end;
         // Котируемая валюта
         if( ConvDealList[cnt_cd].CFI == TradeVolumeList[cnt_tv].CFI )
            if( ConvDealList[cnt_cd].DealKind == "Продажа" )
               TradeVolumeList[cnt_tv].BuySum = TradeVolumeList[cnt_tv].BuySum + ConvDealList[cnt_cd].Cost;
               TradeVolumeList[cnt_tv].Spred = TradeVolumeList[cnt_tv].Spred + ConvDealList[cnt_cd].Cost;
            else // "Покупка"
               TradeVolumeList[cnt_tv].SaleSum = TradeVolumeList[cnt_tv].SaleSum + ConvDealList[cnt_cd].Cost;
               TradeVolumeList[cnt_tv].Spred = TradeVolumeList[cnt_tv].Spred - ConvDealList[cnt_cd].Cost;				 
            end;
         end;
	  
         cnt_cd = cnt_cd + 1;
      end;

      cnt_tv = cnt_tv + 1;
   end;

   return TradeVolumeList;
OnError( err )
   stat = -1;
   WSRunError( MacroName, err, stat );
end;
