/*
$Name:        nptxlinksks_rep.mac
$Module:      Ценные бумаги
$Description: Протокол изменений записи настроечной таблицы связи видов НОБ и СНОБ
*/
import OprInter, RsbDataSet, "cb_sql.mac", "scsrvrepfun.mac";

MACRO PrintReportNptxLnkKSCh(LnkID:integer):integer
  var errcode, errtext;
  var ReportFileName;
  var query, cmd, DataSet;
  var FirstTime = time(), LastTime = time();
  var First:bool = true;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/
  var i = 1;

  ReportFileName = GetTxtFileName("dlcontrfrobch");
  if( not Open(ReportOutFile, ReportFileName) )
     errcode = status(errtext);
     RunError("Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext);
  end;
  SetOutput( ReportFileName );

  query =   " select ch.*, p.t_Name as UserName "
          + "   from dnptxlinkskindsnobch_dbt ch, dperson_dbt p "
          + "  where ch.t_LnkID = ? "
          + "    and p.t_Oper = ch.t_User "
          + " order by ch.t_ID ASC ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(LnkID);

  DataSet = cmd.Execute();


  println( "Протокол изменений записи в таблице связи видов НОБ и СНОБ");
  println( "┌───┬────────────────┬──────────────────────┬────────────────────┬────────────────────┐\n" +
           "│ № │ Дата изменения │     Пользователь     │    Прежняя дата    │     Новая дата     │\n" +
           "│   │                │                      │ окончания действия │ окончания действия │\n" +
           "├───┼────────────────┼──────────────────────┼────────────────────┼────────────────────┤");
  while( DataSet.MoveNext() )
     if(i > 1)
       println("├───┼────────────────┼──────────────────────┼────────────────────┼────────────────────┤");

     end;
           
           [│###│################│######################│####################│####################│]
           ( i, 
             string(date(DataSet.Drec):f):c:w, 
             string(string(DataSet.User) + " "+DataSet.UserName):w, 
             string(date(DataSet.EndDateTypeSNOB_Old):f):c,
             string(date(DataSet.EndDateTypeSNOB_New):f):c 
           );     
     
     i = i + 1;
  end;
     
   println("└───┴────────────────┴──────────────────────┴────────────────────┴────────────────────┘");

  SetOutput(NULL, true);
  close(ReportOutFile);
  ViewFile(ReportOutFile);

  return 0;
END;