/*
$Name:        nptxmathinout_rep.mac
$Module:      Ценные бумаги
$Description: Протокол изменений записи таблицы соответствия кодов доходов и вычетов
*/
import OprInter, RsbDataSet, "cb_sql.mac", "scsrvrepfun.mac";

MACRO PrintReportNptxMathInOutCh(MatchID:integer):integer
  var errcode, errtext;
  var ReportFileName;
  var query, cmd, DataSet;
  var FirstTime = time(), LastTime = time();
  var First:bool = true;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/
  var i = 1;

  ReportFileName = GetTxtFileName("nptxmathinoutch");
  if( not Open(ReportOutFile, ReportFileName) )
     errcode = status(errtext);
     RunError("Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext);
  end;
  SetOutput( ReportFileName );

  query =   " select ch.*, p.t_Name as UserName "
          + "   from dnptxmatchinoutch_dbt ch, dperson_dbt p "
          + "  where ch.t_MatchID = ? "
          + "    and p.t_Oper = ch.t_Oper "
          + " order by ch.t_ID ASC ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(MatchID);

  DataSet = cmd.Execute();


  println( "Протокол изменений записи в таблице  соответствия кодов доходов и вычетов");
  println( "┌───┬────────────────┬──────────────────────┬────────────────────┬────────────────────┐\n" +
           "│ № │ Дата изменения │     Пользователь     │    Прежняя дата    │     Новая дата     │\n" +
           "│   │                │                      │ окончания действия │ окончания действия │\n" +
           "│   │                │                      │                    │                    │\n" +
           "├───┼────────────────┼──────────────────────┼────────────────────┼────────────────────┤");
  while( DataSet.MoveNext() )
     if(i > 1)
       println("├───┼────────────────┼──────────────────────┼────────────────────┼────────────────────┤");

     end;
           
           [│###│################│######################│####################│####################│]
           ( i, 
             string(date(DataSet.DateRec):f):c:w, 
             string(string(DataSet.Oper) + " "+DataSet.UserName):w, 
             string(date(DataSet.DateEnd_Old):f):c,
             string(date(DataSet.DateEnd):f):c
           );     
     
     i = i + 1;
  end;
     
   println("└───┴────────────────┴──────────────────────┴────────────────────┴────────────────────┘");

  SetOutput(NULL, true);
  close(ReportOutFile);
  ViewFile(ReportOutFile);

  return 0;
END;