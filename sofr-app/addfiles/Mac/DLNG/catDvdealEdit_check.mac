/**
 @file catDvdealEdit_check.mac
 @brief Функционал проверок при изменении значения категорий на Производных Инструментах.
 
 Устанавливается для выбранной группы объектов (dobjgroup_dbt, поле t_macroname)
 
  # tag
 - functional_block:
 - code_type:Event_handler
 - Категории
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |02.11.2024 |Швецов Я.А.  |BOSS-5183                                                 | Создание
 */
var AcceptedActionCategory;

import SecInter;

/**
@brief Функция проверки того, является ли контракт фьючерсом

@param[in] fiid ID финансового инструмента
@return V_BOOL true - фьючерс, false - не фьючерс
*/
private macro isFutures(fiid: Integer)
  var cmd, dataSet;
  
  cmd = DL_RSDCommand("SELECT 1 FROM dfininstr_dbt WHERE t_fiid = ? and t_fi_kind = ? and t_avoirkind = ? ");

  cmd.addParam(fiid);
  cmd.addParam(FIKIND_DERIVATIVE);
  cmd.addParam(DERIVATIVE_FUTURES);

  dataSet = cmd.execute();

  if (dataSet.moveNext())
    return true;
  end;
  
  return false;
  
OnError(err)
  return false;
end;

/**
@brief Функция проверки категории на доступность редактирования
@param[in] ObjectType Тип объекта
@param[in] GroupID ID группы из таблицы dobjgroup_dbt
@param[in] AttrID ID устанавливаемого значения категории объекта
@param[in] ObjectID ID объекта
@return V_BOOL true - редактирование категории возможно (по умолчанию), false - запрещено редактирование категории
*/
private macro CheckCategoryAcception(ObjectType, GroupID, AttrID, ObjectID, messageText:@string): Bool
  var isAcceptable: Bool = true;
  
  if (ObjectType == OBJTYPE_DV)
    if (GroupID == 2)//Однодневный фьючерс
      var fiid: Integer = Int(ObjectID);
      
      if (not isFutures(fiid))        
        isAcceptable = false;
        messageText = "Категория доступна только для установки на фьючерсах";
      end;      
    end;
  end;
  
  return isAcceptable;
end;

/**
@brief Функция проверки признака при установке категории по F2
@param[in] ObjectType Тип объекта
@param[in] GroupID ID группы из таблицы dobjgroup_dbt
@param[in] AttrID ID атрибута
@return V_BOOL true в случае успешной проверки

Отрабатывает после CheckCategoryManual
*/
macro CheckAttributeManual(ObjectType: Integer, GroupID: Integer, AttrID: Integer)
  AcceptedActionCategory = GetGlobalParameter("AcceptedActionCategory", true); 
 
  return AcceptedActionCategory; 
end;

/**
@brief Функция проверки признака при установке категории выбором по F9 из списка категорий объекта
@param[in] ObjectType Тип объекта
@param[in] GroupID ID группы из таблицы dobjgroup_dbt
@param[in] AttrID ID атрибута
@return V_BOOL true в случае успешной проверки

Отрабатывает после CheckCategorySet
*/
macro CheckAttribute(ObjectType: Integer, GroupID: Integer, AttrID: Integer)
  AcceptedActionCategory = GetGlobalParameter("AcceptedActionCategory", true); 

  return AcceptedActionCategory; 
end;

/**
@brief Функция для проверок возможности ручной установке признака по F2
@param[?] param1 Необходимо дополнить
@param[in] GroupID ID группы из таблицы dobjgroup_dbt
@param[in] ObjectType Тип объекта
@param[in] ObjectID ID объекта
@param[in] param2 Необходимо дополнить
@param[in] AttrID_new ID атрибута
*/
macro CheckCategoryManual(param1, GroupID, ObjectType, ObjectID, param2, AttrID_new)
  var messageText:string = ""; 
  AcceptedActionCategory = CheckCategoryAcception(ObjectType, GroupID, AttrID_new, ObjectID, @messageText);
 
  if (not AcceptedActionCategory)  
    if (StrLen(messageText) > 0)
      MsgBox(messageText);
    end;
  end;
  
  SetGlobalParameter("AcceptedActionCategory", AcceptedActionCategory);   

  return true;
end;

/**
@brief Функция для проверок возможности установки признака выбором по F9 из списка категорий объекта
@param[?] param1 Необходимо дополнить
@param[in] GroupID ID группы из таблицы dobjgroup_dbt
@param[in] ObjectType Тип объекта
@param[in] ObjectID ID объекта
@param[in] param2 Необходимо дополнить
@param[in] AttrID_new ID атрибута
*/
macro CheckCategorySet(param1, GroupID, ObjectType, ObjectID, param2, AttrID_new) 
  return CheckCategoryManual(param1, GroupID, ObjectType, ObjectID, param2, AttrID_new);
end;
