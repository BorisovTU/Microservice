/*
$Name:             DlNotExec_Form.mac
$Module:           Ядро Securities
$Description:      Форма отчета Информация о неисполненных сделках (ф. 702)
*/
import "DlRepForm_h.mac", "dlmisc.mac";

// Номера полей в форме отчета
CONST
      PNFLD_DlNotExec_PERIOD    = 0,  
      PNFLD_DlNotExec_MONTH     = 1,  
      PNFLD_DlNotExec_YEAR      = 2,  
      PNFLD_DlNotExec_REPDATE   = 3,  
      PNFLD_DlNotExec_BO        = 4,  
      PNFLD_DlNotExec_DV        = 5,  
      PNFLD_DlNotExec_TO_EXEL   = 6,
      PNFLD_DlNotExec_TO_KLIKO  = 7;

CLASS DlNotExecForm()
  VAR 
  Period,
  Month,
  Year,
  RepDate,
  BO,
  DV,
  IsExportToExel,
  IsExportToKliko,
  lastParam;

// Значение по умолчанию
  BO = "X";
  DV = "X";
  IsExportToExel = "X";
  IsExportToKliko = "";

END;

VAR DlNotExecFormData = DlNotExecForm();

PRIVATE CONST
  DL_PNFLD_REPDATE = 101,
  DL_PNFLD_BO      = 102,
  DL_PNFLD_DV      = 103;

PRIVATE MACRO DL_FldProc_CheckBox_BO( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;                                                                                       
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        elif( pThis.GetFieldValue( PNFLD_DlNotExec_DV ) == "X" )
           FldShowValue = FldRealValue = "";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_CheckBox_DV( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;                                                                                       
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        elif( pThis.GetFieldValue( PNFLD_DlNotExec_BO ) == "X" )
           FldShowValue = FldRealValue = "";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_CheckBox_Kliko( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;                                                                                       
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        elif( pThis.GetFieldValue( PNFLD_DlNotExec_TO_KLIKO ) == "X" )
           FldShowValue = FldRealValue = "";
        end;
     end;
  end;
  return CM_DEFAULT;
END;

// Месяц
MACRO FldProc_Month( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Day, RepDate = date(0,0,0);
  
  if( Cmd == DLG_INIT ) // Установка значения в поле
     FldShowValue = DL_GetNameAlg( 2260, FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 2260, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  elif (Cmd == DLG_CHANGEVALUE)
     DateSplit( pThis.GetFieldValue(PNFLD_DlNotExec_REPDATE), Day, null, Year );
     if( pThis.GetFieldValue(PNFLD_DlNotExec_PERIOD) == 0 )
        RepDate = date(Day, FldRealValue, Int(Year));
     else
        if( FldRealValue == 12 )
           RepDate = date(Day, 1, Int(Year));
        else
           RepDate = date(Day, FldRealValue+1, Int(Year));
        end;
     end;
     pThis.SetLinkedValue( DL_PNFLD_REPDATE, RepDate );
  end;

  return CM_DEFAULT;
END;

/*Год*/
MACRO FldProc_Year( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Day, Month, Year;

  if( Cmd == DLG_INIT ) /*Инициализация по умолчанию*/
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if( (FldRealValue < 1990) OR (FldRealValue > 2990) ) /*максимально исключить опечатки*/
        MsgBox( "Неверное значение в поле \"Год\".");
        return CM_IGNORE;
     end;
     DateSplit( pThis.GetFieldValue(PNFLD_DlNotExec_REPDATE), Day, Month, Year );
     if( (Day == 1) and (Month==1) )
        Year = int(FldRealValue) + 1;
     else
        Year = int(FldRealValue);
     end;
     pThis.SetLinkedValue( DL_PNFLD_REPDATE, Date( Day, Month, Int(Year)) );
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Period( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, RepDate;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     FldShowValue = DL_GetNameAlg( 8109, FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 8109, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  elif (Cmd == DLG_CHANGEVALUE)
     Month = pThis.GetFieldValue(PNFLD_DlNotExec_MONTH);
     Year = pThis.GetFieldValue(PNFLD_DlNotExec_YEAR);
     if( FldRealValue == 0 )
        RepDate = date(15,Month,Int(Year));
     else
        if( Month == 12 )
           RepDate = date(1,1,Int(Year));
        else
           RepDate = date(1,Month+1,Int(Year));
        end;
     end;
     pThis.SetLinkedValue( DL_PNFLD_REPDATE, RepDate );
  end;
  
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

// Инициализация
CLASS (DL_CPanel) DlNotExec_Panel()

  PRIVATE MACRO Init()

     var Day, Month, Year;

     DateSplit( {curdate}, Day, Month, Year );

     if( Day < 15 )
        DlNotExecFormData.Period = 1;
        if( Month == 1 )
           Month = 12;
           Year = Year - 1;
           DlNotExecFormData.RepDate = date(1,Month,Year);
        else
           Month = Month - 1;
           DlNotExecFormData.RepDate = date(1,Month,Year);
        end;
     else
        DlNotExecFormData.Period = 0;
        DlNotExecFormData.RepDate = date(15,Month,Year);
     end;

     DlNotExecFormData.Month = Month;
     DlNotExecFormData.Year = Year;

     AddFieldProc( 0, PNFLD_DlNotExec_PERIOD,     DL_PNFLD_PERIOD,   @FldProc_Period,         DlNotExecFormData.Period,32,9);
     AddFieldProc( 0, PNFLD_DlNotExec_MONTH,      DL_PNFLD_MONTH,    @FldProc_Month,          DlNotExecFormData.Month,58,9);
     AddFieldProc( 0, PNFLD_DlNotExec_YEAR,       DL_PNFLD_YEAR,     @FldProc_Year,           DlNotExecFormData.Year);
     AddFieldProc( 0, PNFLD_DlNotExec_REPDATE,    DL_PNFLD_REPDATE,  @Default,                DlNotExecFormData.RepDate);
     AddFieldProc( 0, PNFLD_DlNotExec_BO,         DL_PNFLD_BO,       @DL_FldProc_CheckBox_BO, DlNotExecFormData.BO);
     AddFieldProc( 0, PNFLD_DlNotExec_DV,         DL_PNFLD_DV,       @DL_FldProc_CheckBox_DV, DlNotExecFormData.DV);
     AddFieldProc( 0, PNFLD_DlNotExec_TO_EXEL,    DL_PNFLD_EXCEL,    @Default,                DlNotExecFormData.IsExportToExel);
     AddFieldProc( 0, PNFLD_DlNotExec_TO_KLIKO,   DL_PNFLD_KLIKO,    @DL_FldProc_CheckBox_Kliko, DlNotExecFormData.IsExportToKliko );

  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;
  
  initDL_CPanel( "dlrep.lbr", "DLNOTEXE" );
 
END;
