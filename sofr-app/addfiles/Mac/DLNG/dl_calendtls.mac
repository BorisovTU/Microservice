/*
$Name:        dl_calendtls.mac
$Module:      Дилинг
$Description: Вспомогательное по календарям
*/

import OprInter, SPInter, DealsInter, DVInter, dlquery, dlmisc, InsCarryDoc, likepy, oralib;

const DL_CALEND_DOCKINDGRDEAL = -2;

const DL_CALEND_CONST_WRTMONEY = "Операция списания и зачисления денежных средств";

private const CURIDENT = GetIdentProgram();
private const DVIDENT = CodeFor("Ю");
private const SPIDENT = CodeFor("S");

private var firstQuestionBuf = true;
private var ansBuf = 0;
private var isPaymentCarry = false;
private var calendTypeBuf = 0;

private macro CheckForContract(DocKind, KindOp)
  if (DocKind == DL_DVDEAL)
    var oprKoper = TBFile("oprkoper.dbt");
    var fltr = "T_KIND_OPERATION = " +KindOp
      + " AND T_DOCKIND = " + DocKind
      + " AND INSTR(T_SYSTYPES,"+GetSQLChar("E")+") > 0 "
      + " AND (INSTR(T_SYSTYPES,"+GetSQLChar("U")+") > 0 "
      + " OR INSTR(T_SYSTYPES,"+GetSQLChar("O")+") > 0) ";
    oprKoper.AddFilter(fltr);
    return oprKoper.Next();
  end;
  return false;
end;

macro DL_CalendCheckKindOperHasSymb(Symb, DocKind, KindOp)
  var oprKoper = TBFile("oprkoper.dbt");
  var fltr = "T_KIND_OPERATION = " +KindOp
    + " AND T_DOCKIND = " + DocKind
    + " AND INSTR(T_SYSTYPES,"+GetSQLChar(Symb)+") > 0 ";
  oprKoper.AddFilter(fltr);
  return oprKoper.Next();
end;

macro DL_GetDateKindByOprID(IDOperation,IDStep)
  var retDateKindNumb = 0;
  var oprstep = TBFile("oprstep.dbt");
  var oprostep = TBFile("oprostep.dbt");
  var oprkdate = TBFile("oprkdate.dbt");
  var fltr = "T_ID_OPERATION = " +IDOperation
    + " AND T_ID_STEP = " + IDStep;
  oprstep.AddFilter(fltr);
  if (oprstep.Next())
    fltr = "T_BLOCKID = " +oprstep.rec.blockid
      + " AND T_NUMBER_STEP = " + oprstep.rec.number_step;
    oprostep.AddFilter(fltr);
    if (oprostep.Next())
      oprkdate.rec.datekindid = oprostep.rec.datekindid;
      if (oprkdate.GetEQ())
        retDateKindNumb = oprkdate.rec.numberdate;
      end;
    end;
  end;
  return retDateKindNumb;
end;

macro DL_GetDateKindByOprStep(OprStep)
  var oprStepRec = OprStep;
  if(ValType(OprStep) == V_GENOBJ)
    oprStepRec = OprStep.rec;
  end;
  var retDateKindNumb = 0;
  var oprostep = TBFile("oprostep.dbt");
  var oprkdate = TBFile("oprkdate.dbt");
  var fltr = "T_BLOCKID = " +OprStepRec.blockid
    + " AND T_NUMBER_STEP = " + OprStepRec.number_step;
  oprostep.AddFilter(fltr);
  if (oprostep.Next())
    oprkdate.rec.datekindid = oprostep.rec.datekindid;
    if (oprkdate.GetEQ())
      retDateKindNumb = oprkdate.rec.numberdate;
    end;
  end;
  return retDateKindNumb;
end;

macro DL_GetDateKindIdByKindNumb(Numb, DocKind)
  var oprkdate = TBFile("oprkdate.dbt");
  oprkdate.keynum = 1;
  oprkdate.rec.numberdate = Numb;
  oprkdate.rec.dockind = DocKind;
  if (oprkdate.GetEQ())
    return  oprkdate.rec.DateKindId;
  end;
  return null;
end;

macro DL_Calend_SP_DateDefineIsObl(DocKind, DateKind, RqID:@integer, PartNumber:@integer)
  if ((DocKind == DL_AVRWRT) or
      (DocKind == DL_SECUROWN) or
      (DocKind == DL_AVRWRTOWN) or
      (DocKind == DL_SECURITYDOC))
    if (DateKind == DATE_DEALPAY)
      RqID = DLRQ_TYPE_PAYMENT;
      PartNumber = 1;
      return true;
    elif (DateKind == DATE_DEALPAY_2)
      RqID = DLRQ_TYPE_PAYMENT;
      PartNumber = 1;
      return true;
    elif (DateKind == DATE_DEALCOMISS)
      RqID = DLRQ_TYPE_COMISS;
      PartNumber = 1;
      return true;
    elif (DateKind == DATE_DEALAVANCE)
      RqID = DLRQ_TYPE_AVANCE;
      PartNumber = 1;
      return true;
    elif (DateKind == DATE_DEALAVANCE_2)
      RqID = DLRQ_TYPE_AVANCE;
      PartNumber = 2;
      return true;
    end;
  elif (DocKind == DL_CALEND_DOCKINDGRDEAL)
    if (DateKind == DLGR_DATEKIND_PAYMENT2)
      PartNumber = 2;
      RqID = DLRQ_TYPE_PAYMENT;
      return true;
    elif (DateKind == DLGR_DATEKIND_PAYMENT)
      PartNumber = 1;
      RqID = DLRQ_TYPE_PAYMENT;
      return true;
    elif (DateKind == DLGR_DATEKIND_AVANCE)
      PartNumber = 1;
      RqID = DLRQ_TYPE_AVANCE;
      return true;
    elif (DateKind == DLGR_DATEKIND_COMISS)
      PartNumber = 1;
      RqID = DLRQ_TYPE_COMISS;
      return true;
    elif (DateKind == DLGR_DATEKIND_AVANCE2)
      PartNumber = 2;
      RqID = DLRQ_TYPE_AVANCE;
      return true;
    end;
  end;
  return false;
end;

//Макрос определения, является ли вид даты, датой обязательства
//Настроечный макрос
macro DL_Calend_DV_DateDefineIsObl(DocKind, DateKind, PartNumber:@integer) 
  if (DocKind == DL_DVFXDEAL) //Конверсионные сделки
    if (DateKind == DV_FXDEAL_OPRDATE_OBL)
      PartNumber = 1;
      return true;
    end;
    if (DateKind == DV_FXDEAL_OPRDATE_OBL_2)
      PartNumber = 2;
      return true;
    end;
  elif ((DocKind == DL_DVNDEAL) or (DocKind == DL_DVDEALT3)) //Внебиржевые ПИ и Т+3
    if (DateKind == DV_NDEAL_OPRDATE_OBL)
      PartNumber = 1;
      return true;
    end;
    if (DateKind == DV_NDEAL_OPRDATE_OBL_2)
      PartNumber = 2;
      return true;
    end;
  end;
  return false;
end;

//Макрос определения, является ли вид даты, датой требования
//Настроечный макрос
macro DL_Calend_DV_DateDefineIsReq(DocKind, DateKind, PartNumber:@integer) 
  if (DocKind == DL_DVFXDEAL) //Конверсионные сделки
    if (DateKind == DV_FXDEAL_OPRDATE_REQ)
      PartNumber = 1;
      return true;
    end;

    //Сделано исключительно для металлов. Для остальных должна отфутболиваться в DL_Calend_DV_AddCheck
    if (DateKind == DV_FXDEAL_OPRDATE_EXEC) 
      PartNumber = 1;
      return true;
    end;

    if (DateKind == DV_FXDEAL_OPRDATE_REQ_2)
      PartNumber = 2;
      return true;
    end;
  elif ((DocKind == DL_DVNDEAL) or (DocKind == DL_DVDEALT3)) //Внебиржевые ПИ
    if ((DateKind == DV_NDEAL_OPRDATE_REQ) or (DateKind == DV_NDEAL_OPRDATE_EXEC))
      PartNumber = 1;
      return true;
    end;
    if ((DateKind == DV_NDEAL_OPRDATE_REQ_2) or (DateKind == DV_NDEAL_OPRDATE_EXEC_2))
      PartNumber = 2;
      return true;
    end;
  end;
  return false;
end;

//Макрос определения, является ли вид даты, датой переноса по срокам
//Настроечный макрос
macro DL_Calend_DV_DateDefineIsTransfer(DocKind, DateKind) 
  if ((DocKind == DL_DVNDEAL) or (DocKind == DL_DVDEALT3)) //Внебиржевые ПИ и Т+3
    if ((DateKind == DV_NDEAL_OPRDATE_COTERMS) or
      (DateKind == DV_NDEAL_OPRDATE_COTERMS_2) or 
      (DateKind == DV_NDEAL_OPRDATE_COTERMS_IP))
      return true;
    end;
  end;
  return false;
end;

//Макрос определения, требуется ли по документу/виду даты отключать календарные доработки
//Настроечный макрос
macro DL_Calend_DV_DateDefineIsOff(DocKind, DateKind) 
  if ((DocKind == DL_DVNDEAL) or (DocKind == DL_DVDEALT3)) //Внебиржевые ПИ и Т+3
    if ((DateKind == DV_NDEAL_OPRDATE_COTERMS) or
      (DateKind == DV_NDEAL_OPRDATE_COTERMS_2) or 
      (DateKind == DV_NDEAL_OPRDATE_COTERMS_IP))
      return true;
    end;
  end;
  return false;
end;

//Макрос определения, требуется ли по документу/виду даты отключать календарные доработки
//Настроечный макрос
macro DL_Calend_SP_DateDefineIsOff(DocKind, DateKind) 
  if (DocKind == DL_RETIREMENT_OWN)
    if (DateKind == DATE_DEALTRANSFER)
      return true;
    end;
  end;
  return false;
end;

//Макрос определения, требуется ли по документу/виду автоматически подтверждать перенос даты проводок
//Настроечный макрос
macro DL_Calend_NoQuestion(DocKind, ServDocKind, DateKind) 
  if ((ServDocKind == DL_OVERVALUE) or
      (ServDocKind == DL_GET_INCOME) or
      (ServDocKind == SP_ACCEXPREVOEB) or
      (ServDocKind == SP_AMORTHEDGCORR))
    return true;
  end;
  return false;
end;

//Дополнительный макрос определения для уже определённого как ТО
//Настроечный макрос
macro DL_Calend_SP_AddCheck(FD, DateKind)
  if (IsEqClass("SPFirstDoc",FD) and (FD.tick.rec.BofficeKind == DL_SECURITYDOC))
    if (IsREPO(FD.Group) and IsEXCHANGE(FD.Group))
      return false;
    elif (not IsREPO(FD.Group))
      if (IsEXCHANGE(FD.Group) or (IsBROKER(FD.Group) and (FD.tick.rec.MarketId != -1)))
        return false;
      end;
    end;
  end;
  return true;
end;

//Дополнительный макрос определения для уже определённого как ТО
//Настроечный макрос
macro DL_Calend_DV_AddCheck(FD, DateKind)
  if (IsEqClass("DVFirstDocNDeal",FD))
    if ((FD.IsPctSWAP() == false) and (DateKind == DV_NDEAL_OPRDATE_EXEC))
      return false;
    elif (not FD.ПоставочныйКонтракт())
      return false;
    elif ((DateKind == DV_NDEAL_OPRDATE_REQ) and (/*FD.IsOption() or FD.IsForward() or */FD.IsSWAP()))
      return true; //Неактуален после этапа 2 календарей (для требований возвращаем что является ТО, т.е. обычное поведение)
    end;
  elif (IsEqClass("DVFirstDocFXDeal",FD))
    if (DateKind == DV_FXDEAL_OPRDATE_EXEC)
      return false; //Неактуален после этапа 2 календарей (для исполнения возвращаем что не является ТО, т.е. обычное поведение)
    end;
  end;
  return true;
end;

macro DL_Calend_DV_DateDefineIsPayment(DocKind, DateKind, PartNumber:@integer)
  return DL_Calend_DV_DateDefineIsObl(DocKind, DateKind, @PartNumber) or DL_Calend_DV_DateDefineIsReq(DocKind, DateKind, @PartNumber);
end;

macro DL_Calend_PushBlockNumb(BlockNumb)
  var stat = 0;
  if (DL_CheckCallFromStep())

  end;
  return stat;
end;

//Определяет что ранние расчёты работают только для обязательств
//Настроечный макрос
macro DL_Calend_DV_IsEarlyOnlyObl(DateKind, FD)
  return true;
end;


MACRO DL_GetDateWorkDayForPayStep(dateFrom:DATE, calenId:INTEGER, fiid:INTEGER, isObl:BOOL, isEarly:@BOOL, EarlyOnlyForObl, NoSettlCalend)
  var WorkDate = dateFrom;
  isEarly = false;

  if ((ValType(dateFrom) == V_DATE) and (dateFrom > date(1,1,1900)))
    var cmd = RsdCommand( "begin ?:= RSI_DlCalendars.GetDateWorkDayForPayStep(?,?,?,?,?,?,?);\n end; " );
    cmd.addParam("", RSDBP_OUT, V_DATE);
    cmd.addParam("", RSDBP_IN, dateFrom);
    cmd.addParam("", RSDBP_IN, calenId);
    cmd.addParam("", RSDBP_IN, fiid);
    cmd.addParam("", RSDBP_IN, IIF(isObl,1,0));
    cmd.addParam("", RSDBP_OUT, V_INTEGER);
    if (ValType(EarlyOnlyForObl) == V_BOOL)
      cmd.addParam("", RSDBP_IN,IIF(EarlyOnlyForObl,1,0));
    else
      cmd.addParam("", RSDBP_IN,0);
    end;
    if (ValType(NoSettlCalend) == V_INTEGER)
      cmd.addParam("", RSDBP_IN,NoSettlCalend);
    else
      cmd.addParam("", RSDBP_IN,0);
    end;
    cmd.execute();
    WorkDate = SQL_ConvTypeDate(cmd.value(0));
    isEarly = SQL_ConvTypeInteger(cmd.value(5)) == 1;
  end;

  return WorkDate;
OnError(e)
  return false;
END;

macro DL_SetCarryDateWithQuestion(FD, SetDate, NeedQuestion)
  var correctPlanDate;
  var curPlanDate = SetDate;
  var linkCalendKindId = -1;
  if (IsEqClass("SPFirst",FD))
    linkCalendKindId = FD.ПолучитьКалендСвязанный();
  end;
  if (curPlanDate > date(0,0,0))
    correctPlanDate = DL_GetBalanceDateAfterWorkDayByCalendar(curPlanDate, 0, linkCalendKindId);
    if (correctPlanDate != curPlanDate)
      var text = "Планируемая дата проводок \""+curPlanDate
                 +"\" не является балансовой.|Выполнить проводки в ближайшую балансовую дату \""+correctPlanDate+"\"?";
      if (NeedQuestion and firstQuestionBuf)
        ansBuf = MsgBoxEx ( text, MB_YES+MB_NO+MB_CANCEL, IND_YES, "Вопрос", "Вопрос" );
        firstQuestionBuf = false;
      elif (not NeedQuestion)
        ansBuf = IND_YES;
      end;
      if (ansBuf == IND_YES)
        curPlanDate = correctPlanDate;
      elif (ansBuf == IND_CANCEL)
        RunError("Прерывание пользователем");
      end;
    end;
  end;
  return curPlanDate;
end;

macro DL_CalendGenerateFD(DocKind,ID,dateNumb)
  var retFD = NULL;
  var partNumber = 0;
  var rqId = 0;
  var className = "";
  if ( (DocKind == DL_DVNDEAL) or 
       (DocKind == DL_DVDEALT3) or
       (DocKind == DL_DVFXDEAL) or
       (DocKind == DL_DVOPER) or
       (DocKind == DL_DVCURMARKET) or
       (DocKind == DL_SETTLEMENTFCURM))
    className = "DVFirstDocNDeal";
    if (DocKind == DL_DVFXDEAL)
      className = "DVFirstDocFXDeal";
    elif (DocKind == DL_DVOPER)
      className = "DVFirstDocOper";
    elif ((DocKind == DL_DVCURMARKET) or (DocKind == DL_SETTLEMENTFCURM))
      className = "DVFirstDocDLCOMM";
    end;
    InstLoadModule("dv_categ");
    DL_Calend_DV_DateDefineIsPayment(DocKind, dateNumb, @partNumber);
    partNumber = IIF(partNumber != 2,1,2);
    retFD = GenObject ( className, DocKind, ID, partNumber);
  elif (DocKind == DL_SECURITYDOC)
    InstLoadModule("sp_class");
    DL_Calend_SP_DateDefineIsObl(DocKind, dateNumb, @rqId, @partNumber);
    retFD = GenObject ( "SPFirstDoc", IIF(partNumber==2,LEG_KIND_DL_TICK_BACK,DocKind), ID);
  elif (DocKind == DL_NTGDOC)
    InstLoadModule("dlntfd");
    retFD = GenObject ( "DL_NettingDoc", DocKind, ID);
  elif (DocKind == DV_CSA)
    InstLoadModule("dv_class");
    retFD = GenObject ("DVFirstCSA", DocKind, ID);
  end;
  return retFD;
end;

MACRO DL_CalendGetOperNameByKind(kindOper)
  var cmd, DataSet;
  cmd = DL_RSDCommand("select t_Name from doprkoper_dbt where t_Kind_Operation = ? ");
  cmd.AddParam(kindOper);
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    return DataSet.Name;
  end;
END;

MACRO DL_CalendInit(retDate,calendId)
  //Вызываем ядерные функции, чтобы создались календари, если таковые ещё не были заполнены.
  //На всякий случай в пограничных месяцах дополнительно заполняем предыдущий год
  var day,mn,yr;
  if (ValType(retDate) == V_DATE)
    DateSplit ( retDate, day, mn, yr );
    if (mn == 1)
      IsWorkday(date(15,12,yr-1),calendId);
    elif (mn == 12)
      IsWorkday(date(15,01,yr+1),calendId);
    end;
    IsWorkday(retDate,calendId);
  end;
end;

macro DL_Calend_ForceServOp(CalendId, oprStepBuf)
  if ((oprStepBuf.rec.DocKind == DL_DVFXDEAL) and (oprStepBuf.rec.ServDocKind == DL_DVOPER_OVERVALUE))
    return DL_GetCalendByDynParam(158,
          makeArray(
            DL_KeyPair("Object",DL_CALEND_CONST_DVOUTBALOVER),
            DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
          );
  elif ((oprStepBuf.rec.DocKind == DL_DVNDEAL) and (oprStepBuf.rec.ServDocKind == DL_DVOPER_OVERFRVAL))
    return DL_GetCalendByDynParam(158,
          makeArray(
            DL_KeyPair("Object",DL_CALEND_CONST_DVSOFRVAL),
            DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
          );
  elif ((oprStepBuf.rec.DocKind == DL_DVNDEAL) and (oprStepBuf.rec.kind_action == 105))
    return DL_GetCalendByDynParam(158,
          makeArray(
            DL_KeyPair("Object",DL_CALEND_CONST_DVSOFRVAL),
            DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
          );
  elif ((oprStepBuf.rec.DocKind == DLDOC_ISSUE) or (oprStepBuf.rec.DocKind == DL_SECURITYDOC))
    if (oprStepBuf.rec.ServDocKind == DL_OVERVALUE)
      return DL_GetCalendByDynParam(83,
            makeArray(
              DL_KeyPair("Object",DL_CALEND_CONST_DLOVERVALUE),
              DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
            );
    elif (oprStepBuf.rec.ServDocKind == DL_GET_INCOME)
      return DL_GetCalendByDynParam(83,
            makeArray(
              DL_KeyPair("Object",DL_CALEND_CONST_DLGET_INCOME),
              DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
            );      
    elif (oprStepBuf.rec.ServDocKind == SP_ACCEXPREVOEB)
      return DL_GetCalendByDynParam(83,
            makeArray(
              DL_KeyPair("Object",DL_CALEND_CONST_SPACCEXPREVOEB),
              DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
            );
    elif (oprStepBuf.rec.ServDocKind == DL_RESERVEDOC)
      return DL_GetCalendByDynParam(83,
            makeArray(
              DL_KeyPair("Object",DL_CALEND_CONST_DLRESERVEDOC),
              DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
            );
    elif (oprStepBuf.rec.ServDocKind == DL_OVERVALUE_RD)
      return DL_GetCalendByDynParam(83,
            makeArray(
              DL_KeyPair("Object",DL_CALEND_CONST_DLOVERVALUERD),
              DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
            );
    elif (oprStepBuf.rec.ServDocKind == SP_AMORTHEDGCORR)
      return DL_GetCalendByDynParam(83,
            makeArray(
              DL_KeyPair("Object",DL_CALEND_CONST_AMORTHEDGCORR),
              DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
            );
    end;
  elif ((oprStepBuf.rec.DocKind == DL_NTGDOC) or (oprStepBuf.rec.DocKind == DL_NTGSEC))
    return DL_GetCalendByDynParam(NULL,
          makeArray(
            DL_KeyPair("Object",DL_CALEND_CONST_NETTING),
            DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
          );
  elif (oprStepBuf.rec.DocKind == DL_WRTMONEY)
    return DL_GetCalendByDynParam(83,
          makeArray(
            DL_KeyPair("Object",DL_CALEND_CONST_WRTMONEY),
            DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
          );
  elif ((oprStepBuf.rec.DocKind == DL_CALCNDFL) or
        (oprStepBuf.rec.DocKind == DL_CLOSENDFL) or 
        (oprStepBuf.rec.DocKind == DL_HOLDNDFL))
    return DL_GetCalendByDynParam(83,makeArray(DL_KeyPair("ObjectType",DL_CALLNK_SERVOP)));
  elif ((oprStepBuf.rec.DocKind == DL_DVNDEAL) and (oprStepBuf.rec.ServDocKind == DV_SRVOP_AMORT_RHDP))
    return DL_GetCalendByDynParam(158,
          makeArray(
            DL_KeyPair("Object",DL_CALEND_CONST_DVHDGAMORTRHDP),
            DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
          );
  elif ((oprStepBuf.rec.DocKind == DV_CSA) and (oprStepBuf.rec.ServDocKind == DV_CSAPSO))
    return DL_GetCalendByDynParam(158,
          makeArray(
            DL_KeyPair("Object",DL_CalendGetOperNameByKind(2795)),
            DL_KeyPair("ObjectType",DL_CALLNK_SERVOP))
          );
  end;
  return CalendId;
end;

macro DL_Calend_DefineFIIDForAccTrn(curFIID, accTrnBuf, oprStepBuf)
  if (((oprStepBuf.rec.DocKind == DL_NTGDOC) or (oprStepBuf.rec.DocKind == DL_NTGSEC)) and ((StrBrk(oprStepBuf.rec.symbol,"ТИ") > 0) or (oprStepBuf.rec.kind_action == 100)))
    return max(accTrnBuf.rec.fiid_payer,accTrnBuf.rec.fiid_receiver);
  elif (oprStepBuf.rec.DocKind == DV_CSA)
    return max(accTrnBuf.rec.fiid_payer,accTrnBuf.rec.fiid_receiver);
  else
    return curFIID;
  end;
end;

class (ToolsDataAdapter) ChooseScrollAdapterEx()

  InitToolsDataAdapter();

  var curRecord;
  var cols;
  var fields;

  var Header,State;
  var ScrollName;

  private var Query;
  var recset;

  macro FillCurRecord()
    ClearRecord(CurRecord);
    var j=0,ind=0;
    while (j < recset.FldCount)
      ind=FldIndex(CurRecord,recset.fld(j).name);
      if ((ind>=0) and (ValType(recset.Value(j))!=26))
        CurRecord.item(ind)=recset.Value(j);
      end;
      j=j+1;
    end;
  end;

  macro ConstructHeader()
  end;

  macro ConstructState()
  end;

  macro ConstructQuery()
    return true;
  end;

  macro ConstructScrollName() 
    ScrollName="ScrollName";
  end;

  macro getColumnsInfo
    return cols;
  end;
 
  macro getFileName
    return "FileName";
  end;

  macro AddColumn (name, head, width, kind, dec, type, size, decPoint)
    if (cols == null)
      cols = TArray;
    end;

    var cind = cols.size;

    cols.value (cind)        = name;
    cols.value (cind + 1)    = head;
    cols.value (cind + 2)    = width;
    cols.value (cind + 3 )   = kind; 
    cols.value (cind + 4 )   = dec; 
    cols.value (cind + 5 )   = 0;

    if (fields == null)
      fields = TArray;
    end;

    var find = fields.size;

    fields.value (find)     = name;
    fields.value (find + 1) = type;
    fields.value (find + 2) = size;
    fields.value (find + 3) = decPoint;
    fields.value (find + 4) = width;
 end;

  macro prepareColumns()
  end;

  macro calculate()
  end;

  macro moveFirst : bool
    if (recset.moveFirst())
      FillCurRecord();
      calculate();
      return true;
    end;
    return false;
  end;

  macro moveLast : bool
    if (recset.moveLast())
      FillCurRecord();
      calculate();
      return true;
    end;
    return false;
  end;

  macro moveNext : bool
    if (recset.moveNext())
      FillCurRecord();
      calculate();
      return true;
    end;
    return false;
  end;

  macro movePrev : bool
    if (recset.movePrev())
      FillCurRecord();
      calculate();
      return true;
    end;
    return false;
  end;

  macro moveToBookmark (bmk) : bool
    if (recset.move(bmk,BOOKMARK))
      FillCurRecord();
      calculate();
      return true;
    end;
    return false;
  end;

  macro getBookmark ()
    return recset.BookMark;
  end;

  macro RecordInsert: bool
    return true;
  end;

  macro RecordUpdate: bool
    return true;
  end;

  macro RecordDelete: bool
    return true;
  end;

  macro GetRecordset()
    var cmd=RsdCommand(RslDefCon,Query);
    recset=RsdRecordset(cmd,RSDVAL_CLIENT,RSDVAL_STATIC);
    if (not moveFirst())
      return false;
    end;
    return true;
  end;

  macro initAdapter()
    if (not ConstructQuery())
      return;
    end;

    ConstructHeader();
    ConstructState();
    ConstructScrollName();

    prepareColumns();

    curRecord=TRecHandler("SCROLL",fields,false);

    if (GetRecordset())
      setCurrentRecord (curRecord);
    end;
  end;

  macro Handler(rs, cmd, id, key)
    return CM_DEFAULT;
  OnError(e)
    return CM_DEFAULT;
  end;

  initAdapter();

end;

class PrmColumnSettings(_name:string,_sysname:string,_type:integer,_length:integer)
  var Name = _name;
  var SysName = _sysname;
  var Type = _type;
  var Length = _length;
end;

class (ChooseScrollAdapterEx) TChooseValueScrol(_header:string,_columns:tarray,_query:string)
  Header = _header;
  Query = _query;

  private var columns = _columns;

  InitChooseScrollAdapterEx();

  macro ConstructState()
    State="ESC Выход    ENTER Выбор";
  end;

  macro ConstructScrollName() 
    ScrollName="MainScroll";
  end;

  macro prepareColumns()
    var i = 0;
    for (var column, columns)
      if ((column != null) and IsEqClass("PrmColumnSettings",column))
        AddColumn(column.SysName,column.Name,column.Length,0,0,column.Type,column.Length,0);
      end;
    end;
  end;

  macro GetRecordset()
    var cmd=RsdCommand(RslDefCon,Query);
    recset=RsdRecordset(cmd,RSDVAL_CLIENT,RSDVAL_STATIC);
    if (not moveFirst())
      return false;
    end;
    return true;
  end;

  macro Handler(ob, cmd, id, key)
    var edited=false,upd_c,chkrs,confList;
    if(cmd==DLG_KEY)
      if (key==13)
        return CM_SELECT;
      end;
    end;
    return CM_DEFAULT;
  OnError(er)
    msgbox(er.Message);
    return CM_IGNORE;
  end;
end;

//В запросе, все отображаемые колонки должны иметь alias col_НОМЕРСТОЛБЦА (с единицы)
macro GenChooseScrollAndGetValue(_header:string,_columns:tarray,_query:string)
  var chooseScroll = TChooseValueScrol(_header,_columns,_query);
  if (RunScroll(chooseScroll,chooseScroll.cols.size/6,chooseScroll.cols,chooseScroll.ScrollName,R2M(chooseScroll,"Handler"),chooseScroll.Header,chooseScroll.State,true,-1,-1,null,30) == CM_SELECT)
    return chooseScroll.recset;
  end;
  return null;
end;

//преобразователь значений видов рынков со справочников к значениям календарей
macro DL_Calend_MarketPlaceConverter(Type:integer, ValueToConvert:integer)
  if (Type == ALG_DL_MARKETKIND_SETTLE)
    if (ValueToConvert == 1) //Фондовый T0
      return DL_CALLNK_MARKETPLACE_SEC; //Фондовый
    elif (ValueToConvert == 2) //Валютный
      return DL_CALLNK_MARKETPLACE_CUR; //Валютный
    elif (ValueToConvert == 3) //Рынок СПФИ
      return DL_CALLNK_MARKETPLACE_SPFI; //СПФИ
    elif (ValueToConvert == 4) //Срочный
      return DL_CALLNK_MARKETPLACE_DV; //Срочный
    elif (ValueToConvert == 5) //Фондовый T+
      return DL_CALLNK_MARKETPLACE_SEC; //Фондовый
    end;
  end;
  //возвращаем что получили, если преобразование не вышло
  return ValueToConvert;
end;

//Возвращает все календари, у которых есть переданные параметры
//На вход принимается либо 1 параметр массива DL_KeyPair
//Либо сразу перечень параметров DL_KeyPair не обернутых в массив, а просто перечисленные параметрами
macro DL_GetCalendarArrByParam()
  private macro GetAndString(elem)
    return 
      " AND exists (select 1 from DDLCALPARAMLNK_DBT lnk where lnk.T_KNDCODE = '"+elem.Key+"' and lnk.T_VALUE = '"+elem.Value+"' and LNK.T_CALPARAMID = PARM.T_ID) ";
  end;
  var calendArr = TArray ();
  var query = 
     " SELECT DISTINCT parm.T_CALKINDID "
    +"   FROM DDLCALPARAM_DBT parm "
    +"  WHERE 1=1 ";
  var curParm, i:integer = 0;
  GetParm(i, curParm);
  if (IsEqClass( "TArray", curParm ))
    for (var item, curParm)
      query = query + GetAndString(item);
    end;
  else
    while( GetParm(i, curParm) )
      query = query + GetAndString(curParm);
      i = i + 1;
    end;
  end;
  var rs = ExecSQLSelect(query, TArray(), false);
  while (rs.MoveNext())
    calendArr[calendArr.size] = SQL_ConvTypeInteger(rs.value("T_CALKINDID"));
  end;
  return calendArr;
end;

macro DL_GetMinDateOfCalends(FromDate:date, DaysShift:integer, CalendArr:TArray)
  var resultDate = FromDate;
  if ((ValType(CalendArr) == V_GENOBJ) and (IsEqClass( "TArray", CalendArr )) and (CalendArr.size > 0))
    resultDate = date(31,12,2199);
    for (var calendId, CalendArr)
      resultDate = min(GetDateAfterWorkDays(FromDate,DaysShift,calendId), resultDate);
    end;
  else
    resultDate = GetDateAfterWorkDays(FromDate,DaysShift);
  end;

  return resultDate;
end;

private macro GetObjAttrNumInList(objectType: Integer, groupID: Integer, objectID: String):Integer
  var result: Integer = -1;
 
  var sql = RSDCommand(" select oa.* from dobjattr_dbt oa, dobjatcor_dbt oac " +
                       " where oac.t_objecttype = ? " +
                         " and oac.t_groupid = ? " +
                         " and oac.t_general = chr(88) " +
                         " and oac.t_validToDate = to_date('31.12.9999', 'DD.MM.YYYY') " +
                         " and oac.t_object = ? " +
                         " and oa.t_attrid = oac.t_attrid " +
                         " and oa.t_groupid = oac.t_groupid " +
                         " and oa.t_objecttype = oac.t_objecttype ");
 
  sql.AddParam("", RSDBP_IN, objectType);
  sql.AddParam("", RSDBP_IN, groupID);
  sql.AddParam("", RSDBP_IN, objectID);
  
  var dataSet = TRsbDataSet(sql);
 
  if (dataSet.MoveNext())
    result = Int(dataSet.NumInList);
  end;
 
  return result;
end;

macro DL_GetEmitCountryIDFromFiid(Fiid:integer)
  var cmd, DataSet, query = "", Num = -1;
  
  /*DEF-50991 Если на ц/б стоит категория "Вхождение иностранного выпуска в группу инструментов <российские ценные бумаги>" "Да"*/
  /*Всегда выбираем страну 'RUS'*/
  cmd = DL_RSDCommand("select * from dfininstr_dbt where t_fiid = ?");
  cmd.AddParam(Fiid);
  DataSet = cmd.Execute();
  
  if(DataSet.MoveNext())
    if(DataSet.Fi_Kind == FIKIND_AVOIRISS)
      Num = GetObjAttrNumInList(OBJTYPE_AVOIRISS, 130 /*Вхождение иностранного выпуска в группу инструментов <российские ценные бумаги>*/, String(Fiid:o:10));
    end;
  end;
  
  if(Num == 1)
    query = " SELECT T_COUNTRYID " 
           +"   FROM DCOUNTRY_DBT " 
           +"  WHERE T_CODELAT3 = 'RUS'";
  else
    query = " SELECT T_COUNTRYID " 
           +"   FROM DCOUNTRY_DBT " 
           +"  WHERE T_CODELAT3 = " 
           +"           (SELECT CASE " 
           +"                      WHEN TRIM (t_nrcountry) = CHR (1) THEN '---' " 
           +"                      ELSE TRIM (t_nrcountry) " 
           +"                   END " 
           +"                      AS t_nrcountry " 
           +"              FROM dparty_dbt " 
           +"             WHERE t_partyid = (SELECT t_issuer " 
           +"                                  FROM DFININSTR_DBT " 
           +"                                 WHERE t_fiid = ?)) ";
  end;
  
  cmd = DL_RSDCommand(query);
  
  if(Num != 1)
    cmd.AddParam(Fiid);
  end;
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    return SQL_ConvTypeInteger(DataSet.countryId);
  end;
  return -1;
end;

macro DL_GetAvoirKindIDFromFiid(Fiid:integer)
  var cmd, DataSet;
  cmd = DL_RSDCommand(
    " SELECT fin.T_AVOIRKIND"
   +"   FROM dfininstr_dbt fin"
   +"  WHERE fin.t_fiid = ? "
  );
  cmd.AddParam(Fiid);
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    return SQL_ConvTypeInteger(DataSet.AVOIRKIND);
  end;
  return -1;
end;

macro DL_GetMarketIDFromDVScheme(Code:string)
  var cmd, DataSet;
  cmd = DL_RSDCommand("select T_MARKET from ddlmarket_dbt where t_code = ?");
  cmd.AddParam(Code);
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    return SQL_ConvTypeInteger(DataSet.MARKET);
  end;
  return -1;
end;