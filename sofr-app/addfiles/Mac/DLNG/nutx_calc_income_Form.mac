/*
    $Name:        nutx_calc_income_form.mac
    $Module:      Ядро Securities
    $Description: Отчет "Поручения клиента на перевод д/с между секторами РЦ биржи" (Общий для БО ЦБ и ПИ)
*/


import "DlRepForm_h.mac";
import secinter;

var Pid;

CONST
PNFLD_CALCINC_BegDate          = 0, 
PNFLD_CALCINC_EndDate          = 1,

PNFLD_CALCINC_ClientCode       = 2, 
PNFLD_CALCINC_ClientName       = 3,

PNFLD_CALCINC_IsExportToExel   = 4;

CLASS CALCINCFormData()
    VAR 
    BegDate,     
    EndDate,
    ClientCode,
    ClientName,
    IsExportToExel; 
    
    // Значение по умолчанию
   
    BegDate           =   {curdate};
    EndDate           =   {curdate};
    
    ClientCode        =   -1;
    ClientName        =   "";
    
    IsExportToExel    =   SET_CHAR;
    
    
END;     

VAR RepFormData = CALCINCFormData();     

PRIVATE CONST /* Обработчики для нестандарных контролов */

H_PNFLD_BEGINDATE = 101,
H_PNFLD_ENDDATE   = 102,
H_CLIENT          = 103, 
H_CLIENT_NAME     = 104; 


/*листалка клиентов по виду обслуживания*/
PRIVATE MACRO DL_ListClientByServKind( ClientName:@string, FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, 
    ServKind:TArray, IncludeOurBank:bool, Department:integer, EndDate:DATE, BegDate:DATE):BOOL
    VAR Choice, ServCond = "", i = 0, flag = false; 
    VAR Select, IncludeBankCond = "";
    
    if( (ServKind != null) and (ServKind.Size>0) )
        while( i < ServKind.Size )
            if( flag == true )
            ServCond = ServCond + ",";
            else
                flag = true;
            end;
            
            ServCond = ServCond + string(ServKind[i]);
            i = i + 1;
        end;
    else
        ServCond = 0;
    end;
    
    if( not IncludeOurBank )
        IncludeBankCond = " and clt.t_PartyID != " + string({OurBank});
    end;
    
    Select = "select distinct prt.t_Name, prt.t_ShortName, " +
    "       partcode.t_Code, prt.t_PartyID " + 
    "  from dpartcode_dbt partcode, dclient_dbt clt, dparty_dbt prt " +
    " where prt.t_PartyID = partcode.t_PartyID " +
    "   and partcode.t_CodeKind = 1 " +
    "   and prt.t_LegalForm =   "+ legal_form_jur +
    "   and RSB_SECUR.IsTaxResident(prt.t_PartyID, TO_DATE('"+EndDate+"', 'DD-MM-YYYY') ) = 0 "+
    "   and exists(select 1 from dsfcontr_dbt contr where CONTR.T_PARTYID = prt.t_PartyID ) " +
    "   and (( " +
            "   clt.t_ServiceKind in(" + ServCond + ")" +
            IncludeBankCond +
            "   and prt.t_PartyID = clt.t_PartyID " +
            "   and clt.t_Branch    = 0 "+//+
    "        ) or (" + 
            "exists(select 1 from dnutxobj_dbt nptx where nptx.t_client = prt.t_PartyID and nptx.t_date >= TO_DATE('"+ BegDate +"', 'DD-MM-YYYY') and nptx.t_date <= TO_DATE('"+ EndDate +"', 'DD-MM-YYYY'))" +
            "))";

    
    
    
    if( Department != null )
        if( Department > 0 )
            Select = Select + "   and clt.t_Department = "+string(Department);
        end;
    end;
    
    
    Choice = DL_Scroll(Select,"Список клиентов", 0/*X*/,0/* Y*/,"t_Code","Код","t_Name","Наименование" );
    
    if( Choice != NULL )
        FldRealValue = Choice.Value("t_PartyID"); 
        FldShowValue = Choice.Value("t_Code");
        ClientNAme = Choice.Value("t_ShortName");
    end;
    
    return (Choice != NULL);
END;

/*клиент БОЦБ\ПИ\Депоз в заданном филиале*/
PRIVATE MACRO DL_FldProc_Client_BO_Dep( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
    var Party     = TRecHandler("party.dbt");
    var ClientName = "";
    var ServKind = TArray();
    var DepCode = pThis.GetLinkedValue( DL_PNFLD_DEPARTCODE );
    
    
    if( Cmd == DLG_INIT )
        if( FldRealValue == null )
            FldRealValue = -1;
        end;
        if( FldRealValue == -1 )
            FldShowValue = STR_ALLVALUE;
        end;
    elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        
        if( pThis.ExistField(H_CLIENT_NAME) )
            pThis.SetLinkedValue( H_CLIENT_NAME, "" );
        end;
    elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
        ServKind[ServKind.Size] = PTSK_STOCKDL;
        
        
        
        if(DL_ListClientByServKind( @ClientName, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), ServKind, false, DepCode, pThis.GetLinkedValue(H_PNFLD_ENDDATE), pThis.GetLinkedValue(H_PNFLD_BEGINDATE) ))
            if( pThis.ExistField(H_CLIENT_NAME) )
                pThis.SetLinkedValue( H_CLIENT_NAME, ClientName );
            end;
        end;
    end;
    Pid=FldRealValue;
    return CM_DEFAULT;
END;


PRIVATE MACRO FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

    if( Cmd == DLG_INIT ) /*Установка значения в поле*/
        if( FldRealValue == null ) /*инициализация по умолчанию*/
            FldRealValue = {curdate};
        end;
    FldShowValue = FldRealValue;
    elif( Cmd == DLG_CHANGEVALUE ) 
        FldRealValue = FldShowValue;
        if( (FldRealValue != null) AND (pThis.GetLinkedValue(H_PNFLD_ENDDATE) != null) )
            if( FldRealValue > pThis.GetLinkedValue(H_PNFLD_ENDDATE) )
                MsgBox( "Дата окончания не может быть меньше даты начала периода.");
                return CM_IGNORE;
            end;
        end;
    elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
        FldRealValue = GetDateByCalendar(FldRealValue);
        FldShowValue = FldRealValue;
    end;
    
    return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

    if( Cmd == DLG_INIT ) /*Установка значения в поле*/
        if( FldRealValue == null ) /*инициализация по умолчанию*/
            FldRealValue = {curdate};
        end;
    FldShowValue = FldRealValue;
    elif( Cmd == DLG_CHANGEVALUE ) 
        FldRealValue = FldShowValue;
        if( (FldRealValue != NULL) AND (pThis.GetLinkedValue(H_PNFLD_BEGINDATE) != null) )
            if( FldRealValue < pThis.GetLinkedValue(H_PNFLD_BEGINDATE) )
                MsgBox( "Дата окончания не может быть меньше даты начала периода.");
                return CM_IGNORE;
            end;
        end;
    elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
        FldRealValue = GetDateByCalendar(FldRealValue);
        FldShowValue = FldRealValue;
    end;
    
    return CM_DEFAULT;
END;      

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

    if( Cmd == DLG_INIT )
        FldShowValue = FldRealValue;
    elif( Cmd == DLG_CHANGEVALUE ) 
        FldRealValue = FldRealValue;
    end;
    return CM_DEFAULT;
END;

CLASS (DL_CPanel) CALC_INC_Panel()
       
    PRIVATE MACRO Init()

        AddFieldProc( 0, PNFLD_CALCINC_BegDate,        H_PNFLD_BEGINDATE,   @FldProc_BeginDate,           RepFormData.BegDate);
        AddFieldProc( 0, PNFLD_CALCINC_EndDate,        H_PNFLD_ENDDATE,     @FldProc_EndDate,             RepFormData.EndDate);
        
        AddFieldProc( 0, PNFLD_CALCINC_ClientCode,     H_CLIENT,            @DL_FldProc_Client_BO_Dep,    RepFormData.ClientCode); 
        AddFieldProc( 0, PNFLD_CALCINC_ClientName,     H_CLIENT_NAME,       @Default, "");
        
        AddFieldProc( 0, PNFLD_CALCINC_IsExportToExel, DL_PNFLD_CHECKBOX,   @DL_FldProc_CheckBox,         RepFormData.IsExportToExel);
        
        DisableFields( This.Data(), PNFLD_CALCINC_IsExportToExel );
        
    END;
    
    PRIVATE MACRO Check():BOOL
        return true;
    END;
    
    initDL_CPanel( "dlrep.lbr", "CALC_INC" ); 
END;

