/*
$Name:         dlvsacnum.mac
$Module:       Векселя банка
$Description:  Генерация кода векселя в номере счета.
*/

import BankInter, oralib, likepy;

//Идентификатор референса
PRIVATE CONST REF_ID = 92;

//Генерация кода векселя в номере счета.
MACRO DL_VS_GetAccCodeValue()
  var s = "";
  if(GenerateReference(REF_ID, s) != 0)
    s = "";
  end;

  return s;
END;

//Генерация кода векселя в номере счета.
MACRO DL_VS_GetAccCodeNumber( bnr )
  var s = DL_VS_GetAccCodeValue();
  if(s == "")
    msgbox("Ошибка при генерации референса");
  else
    bnr.rec.AccCode = s;
  end;
END;

//Проверяет уникальность кода
private macro UniqueAccCode (bcid, AccCode)
  var sql;
  sql = "select 1 from dvsbanner_dbt where t_bcid <> :bcid and t_acccode = :code";
  sql = ExecSqlSelect(sql, MakeArray(SqlParam("bcid", BCID),
                                     SqlParam("code", AccCode)), false);
  if (sql.MoveNext() )
    return false;
  end;
  return true;
End;

private macro SetAccCode (bcid, AccCode)
  var sql;
  sql = "update dvsbanner_dbt set t_acccode = :code where t_bcid = :bcid";
  sql = ExecSql(sql, MakeArray(SqlParam("code", AccCode), SqlParam("bcid", bcid)), false);
  if (sql == null) 
    return false;
  end;
  return true;
onerror
  return false;
End;

/*
Создаёт код в номере счета, если необходимо.
Затем проверяет его на уникальность, если надо - пересоздаёт. И так, пока не будт уникальный код
*/
macro CheckAndCreateAccCode (BCID, AccCode)

  if (AccCode == "") 
    AccCode = DL_VS_GetAccCodeValue();
  else
    if (UniqueAccCode(BCID, AccCode))
      return true;
    end;
  end;

  while (not UniqueAccCode(BCID, AccCode))
    AccCode = DL_VS_GetAccCodeValue();
  end;

  return SetAccCode(BCID, AccCode);
onerror()
  return false;
End;

macro GetBnrAccCode (BCID, AccCode)

  if (AccCode == "") 
    AccCode = DL_VS_GetAccCodeValue();
  end;

  while (not UniqueAccCode(BCID, AccCode))
    AccCode = DL_VS_GetAccCodeValue();
  end;

  return AccCode;
onerror()
  return 0;
End;