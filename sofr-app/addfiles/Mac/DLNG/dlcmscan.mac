/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.1                                          R-Style Software Lab

  File Name   : cmscan.mac
  Created     : 29.06.99
  Programmer  : Сабитов Р.Р.
  Description : Печать отчета сканирования

└───────────────────────────────────────────────────────────────────────────*/
import DealsInter;   

RECORD firstdoc (dl_comm); 
file   dlcomm(dl_comm); 

const   Ошибка   = 0,
  Первый   = 1,
  Последний= 2;

/*Action == 0 - массовое выполнение 
  Action == 1 - массовое удаление 
  Action == 2 - массовый перевод в отложенные*/
MACRO Печать_ОтчетСканирования (Вызов,doc,Статус,Сообщение, Action )
  var Title;

  setbuff (firstdoc,doc);
  if( Action == NULL ) 
     Action = 0;
  end;

  if (Вызов==Первый)
     if( Action == 0 )  
        Title = "Отчет о групповом создании операций";
     elif( Action == 1 )  
        Title = "Отчет о массовом удалении операций."
     elif( Action == 2 ) 
        Title = "Отчет о массовом переводе операций в отложенные."
     end;

[             #

+-------+-------+------------------------------------------------------------+
|  Код  |№ошибки|                 Сообщение                                  |
|комисc.|       |                                                            |
+-------+-------+------------------------------------------------------------+]
(Title);

  elif (Вызов==Ошибка)
     if (Статус == 0)
        if( Action == 0 )
           ClearRecord(dlcomm); 
           dlcomm.DocumentID = firstdoc.DocumentID;

           if( GetEQ(dlcomm) )
              if( dlcomm.CommStatus == DL_COMM_CLOSED )
                 Сообщение = "Операция создана успешно.";
              elif( dlcomm.CommStatus == DL_COMM_READIED ) 
                 Сообщение = "Действие выполнено частично. Операция перемещена в открытые.";
              elif( dlcomm.CommStatus == DL_COMM_PREPARING ) 
                 Сообщение = "Ошибка при выполнении. Операция не создана.";
              end;
           end;
        elif( Action == 1 )
           Сообщение = "Операция удалена."
        elif( Action == 2 )
           Сообщение = "Операция переведена в отложенные."
        end;
     elif( Сообщение == "" )
        if( Action == 0 )  
           Сообщение = "Ошибка при выполнении операции."
        elif( Action == 1 )  
           Сообщение = "Ошибка при удалении."
        elif( Action == 2 ) 
           Сообщение = "Ошибка при переводе в отложенные."
        end;
     end;
[|#######|#######|############################################################|]
(firstdoc.CommCode,Статус,Сообщение:w);

  elif (Вызов==Последний)         
[+-------+-------+------------------------------------------------------------+];
  end;
END;
