import DealsInter, "DLQICheckOper_form.mac";

CONST DL_QINVTYPE_ERROR_UNDEF = -1, /* Не задана*/
      DL_QINVTYPE_ERROR_0     = 0,  /* Нет ошибки*/
      DL_QINVTYPE_ERROR_1     = 1,  /* Ошибка 1*/
      DL_QINVTYPE_ERROR_2     = 2;  /* Ошибка 2*/

PRIVATE MACRO FindDSCQINV( PartyID:integer, Kind:integer, CheckDate:date ):BOOL

  var RetVal:bool = false;
  var Select, RS;

  Select = RSDCommand( " SELECT hist.*                                                                         " +
                       "   FROM DV_SCQINVHIST hist                                                             " +
                       "  WHERE hist.t_PartyID   = ?                                                           " +
                       "    AND hist.t_Kind      = ?                                                           " +
                       "    AND hist.t_State     = ?                                                           " +
                       "    AND hist.t_BegDate  <= ?                                                           " +
                       "    AND ( hist.t_EndDate = TO_DATE('01.01.0001','DD.MM.YYYY') OR hist.t_EndDate > ? ) " );
  Select.NullConversion = true;
  Select.addParam( "", RSDBP_IN, PartyID                );
  Select.addParam( "", RSDBP_IN, Kind                   );
  Select.addParam( "", RSDBP_IN, DSCQINV_State_INCLUDED );
  Select.addParam( "", RSDBP_IN, CheckDate              );
  Select.addParam( "", RSDBP_IN, CheckDate              );
  Select.Execute();
  
  RS = TRsbDataSet( Select );

  if( RS.MoveNext() )
     RetVal = true;
  end;

  return RetVal;
END;

PRIVATE MACRO FindDSCQINVFI( PartyID:integer, AvoirKind:integer, CheckDate:date ):BOOL

  var RetVal:bool = false;
  var Select, RS;

  Select = RSDCommand( " SELECT dscqinvfi.*                                                                              " +
                       "   FROM DV_SCQINVFIHIST dscqinvfi                                                                " +
                       "  WHERE dscqinvfi.t_PartyID   = ?                                                                " +
                       "    AND dscqinvfi.t_AvoirKind = ?                                                                " +
                       "    AND dscqinvfi.t_State     = ?                                                                " +
                       "    AND dscqinvfi.t_BegDate  <= ?                                                                " +
                       "    AND ( dscqinvfi.t_EndDate = TO_DATE('01.01.0001','DD.MM.YYYY') OR dscqinvfi.t_EndDate > ? ) " );
  Select.NullConversion = true;
  Select.addParam( "", RSDBP_IN, PartyID                );
  Select.addParam( "", RSDBP_IN, AvoirKind              );
  Select.addParam( "", RSDBP_IN, DSCQINVFI_State_ACCESS );
  Select.addParam( "", RSDBP_IN, CheckDate              );
  Select.addParam( "", RSDBP_IN, CheckDate              );
  Select.Execute();
  
  RS = TRsbDataSet( Select );

  if( RS.MoveNext() )
     RetVal = true;
  end;

  return RetVal;
END;

MACRO DL_CheckQIParty(PartyID:integer, AvoirKind:integer, CheckDate:date):integer

   var TypeError = DL_QINVTYPE_ERROR_UNDEF;

   if( FindDSCQINV( PartyID, DSCQINV_Kind_LAW, CheckDate ) == true )
      TypeError = DL_QINVTYPE_ERROR_0;
   end;

   if(TypeError == DL_QINVTYPE_ERROR_UNDEF)
      if( FindDSCQINV( PartyID, DSCQINV_Kind_STATEMENT, CheckDate ) == false )
         TypeError = DL_QINVTYPE_ERROR_1;
      else
         if( FindDSCQINVFI( PartyID, AvoirKind, CheckDate ) == false )
            TypeError = DL_QINVTYPE_ERROR_2;
         else
            TypeError = DL_QINVTYPE_ERROR_0;
         end;
      end;
   end;

   return TypeError;
END;
