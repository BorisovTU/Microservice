/*
$Name:        ntgftagrq.mac
$Module:      Ядро Securities
$Description: Формирование тегов для печати соглашений и подтверждений
*/
import PaymInter, FIInter, PTInter, CTInter, DealsInter, globals, dlwreps, OprInter, "adress.mac", "dlquery.mac";

private file dlnet("dl_nett");

private var FinalRQ;
private var ExistFinalRQ = false;


private var ConfirmHeader = "";               /*Заголовок подтверждения по-умолчанию */

const DOCKIND_BUYSALEREPORT     = 26,     /*договор купли-продажи*/
      DOCKIND_CONTR_FIRSTPART   = 22,     /* договор по первой части*/
      DOCKIND_CONTR_SECONDPART  = 23,     /* договор по второй части*/
      KINDDOC_CONTRBUYSALE = 483,
      NOTEKIND_FINALDEAL = 2, /* вид примечания - номер финальной сделки */
      CODE_BIK = "БИК", /* вид кода БИК */
      COMMITMENT  = "Commitment",     /*обязательство*/
      REQUIREMENT = "Requirement";    /*требование*/


macro CreateFileName()
  return GetTxtFileName( "tmpout" );
end;


PRIVATE MACRO GetValueName( List, Element, Name )
   var ll = TRecHandler("llvalues.dbt");
   if( LL_FindLLVALUES( List, Element, ll ) == true )
      SetParm( 2, ll.rec.Name );
      return ll.rec.Code;
   else
      SetParm( 2, "" );
      return "";
   end;
END;

private macro IIF(condition,value_true,value_false)
    if (condition)
        return value_true;
    else 
        return value_false;
    end;
end;

/* Возвращает строку 's'
   до символа 'c' 
   Например: s="abcdefg.xyz",c='.'
   Результат   "abcdefg" */
private macro StrBefore( s, c )
   var p=Index(s, c);
   return IIF(p==0,s,SubStr(s,1,p-1));
end;

/* Делает из строки вида "Иванов Иван Иваныч" строку "Иванов И.И." */
private macro CutFullName( str )
  var l = strlen(str),
      i = 0,
      res = "";

  res = StrBefore( str, " ") + " "; /*Это будет фамилия*/

  while ( i < l )
    if ( (SubStr(str,i,1) == " ") and (i+1<l) )
      if(SubStr(str,i+1,1) != " ")  /*Проверим, так как ФИО может разделятся не одним пробелом*/
        res = res + SubStr(str,i+1,1) + ".";
      end;
    end;
    i = i + 1;
  end;

  return res;
end;

PRIVATE macro StrTrimLeft( num, len )
var
   s=trim(string(num)),
   l=strlen(s);

   if(l==len)
      /*возвращаем без изменений*/;
   elif (l>len)
      /*обрезаем*/
      s=SubStr(s,l-len+1,len);          
   else
      /*дополняем*/
      s=mkstr("0",len-l)+s;
   end;

   return s;
end;


/* получить цену из dl_leg.dbt */
macro GetDealPrice( dlleg )
   var Price = $0, FaceValue = $0;
   record Fininstr(fininstr);

   if ( ПолучитьФинИн( dlleg.PFI, Fininstr ) == 0 )

     if( FI_IsBond(Fininstr) )     /* Облигация */
       if( dlleg.RelativePrice == "" )      /*Валюта сделки не задана в процентах*/
         Price = money(dlleg.Price/**100*/);
       else
         if( FI_GetNominal( dlleg.PFI, FaceValue ) != 0 )
           FaceValue = $0;
         end; 
         Price = money( FaceValue * dlleg.Price/*/100*/ );
       end;
     elif( FI_IsShare(Fininstr) )  /*   Акция   */
       Price = money(dlleg.Price/**100*/);
     end;
   end;

   return Price;
end;

/* проверка данных при печати, пустые значения заменяются прочерками */
private macro CheckData( Data )
   var str;
   if( ValType( Data ) == V_DATE)
     if(Data == Date(0,0,0))
       return str = ("\"___\"" + " _________ " + "20__ ");
     else
       return Data;
     end;
   elif(ValType( Data ) == V_STRING)
     if(Data == "")
       return str = "________________________";
     else
       return Data;
     end;
   else
     if( not Data )
       return str = "________";
     else
       return Data;
     end;
   end;
end;

private macro FindSETTACC( PartyID, FI_Kind, FIID, _settacc )
  var stat = false;
  file settacc(settacc) key 3;

  if ( FI_Kind == FIKIND_AVOIRISS )
    FIID = -1;
  end;  

  ClearRecord( settacc );
  settacc.PartyID = PartyID;
  settacc.FIKind  = FI_Kind; 
  settacc.FIID    = FIID;
  if ( getGE(settacc) and ( ( settacc.PartyID == PartyID ) and ( settacc.FIKind == FI_Kind ) and ( settacc.FIID == FIID ) ) )
     stat = true;
  end;

  copy( _settacc, settacc );

  return stat;
end;

private macro print_empty()
  println( CheckData("") );
end;

/* перевод числа в строку */
private macro prinln_SumToStr( Sum, FIID )
  record fininstr(fininstr);

  if ( (Sum == 0) or (Sum == $0) ) /* нули в строку не переводим */
    print_empty();
    return;
  end;

  ClearRecord(fininstr);
  if(not ПолучитьФинИн(FIID, fininstr))
    if ( fininstr.FI_Kind == FIKIND_CURRENCY ) /*Валюта*/
      if ( FIID == NATCUR )  
        println( CheckData(RubToStrAlt( Sum, NULL, NULL, fininstr.ISO_Number )) );
      else
        println( CheckData(CurToStrAlt( Sum, NULL, NULL, fininstr.ISO_Number )) );
      end;
    else
      println( CheckData(NumToStr( Sum )) );
    end;
  end;
end;

macro convert_digit( Sum, FIID )
  record fininstr(fininstr);

  ClearRecord(fininstr);
  if(not ПолучитьФинИн(FIID, fininstr))
    if ( fininstr.FI_Kind == FIKIND_CURRENCY ) /*Валюта*/
      return Sum;
    else
      return int( double(Sum)/*/100*/ );
    end;
  end;
  return $0;
end;

/* Создает тэг для копирования строк таблицы */
macro MultipleRow(ТабНо, КолСтрок, BookMark)
  if(КолСтрок > 1)
    [~MultipleRow];
    println(ТабНо);                 /* идентификатор таблицы */
    println(КолСтрок - 1);          /* кол-во недостающих строк */
    if(BookMark != null)
       println(BookMark);
    end;
  end;
end;

/*Удаляем таблицу на которую навешена заданная закладка*/
macro DeleteTable(BookMark);
   if(BookMark != null)
     [~DeleteTable];
     println(BookMark);
   end;
end;

/* формирование тэгов по финансовым инструментам */
macro FinsTag(Date_net: DATE)
  var FaceValue;
  file fininstr(fininstr);
  file avoiriss(avoiriss);
  record Currency(fininstr);
  record party(party);

  ClearRecord(fininstr);
  fininstr.FIID = dlnet.BaseFIID;
  if( GetEQ(fininstr) == false )
    MsgBox( "Не найден финансовый инструмент с парамером FIID = " + dlnet.BaseFIID );
    return false;
  end;

  ClearRecord(avoiriss);
  if ( dlnet.FI_Kind == FIKIND_AVOIRISS )
    avoiriss.FIID = dlnet.BaseFIID;
    if( GetEQ(avoiriss) == false )
      MsgBox( "Не найдена ц/б с парамером FIID = " + dlnet.BaseFIID );
      return false;
    end;
  end;

  
  if( FI_GetNominal( fininstr.FIID, FaceValue ) != 0 )
     FaceValue = $0;
  end; 

  println( "~FaceValue" );
  println( CheckData( FaceValue ) );

  if(ExistFinalRQ == true)
    println( "~Sum_Net_avoir" );
    println( CheckData( money(int(double(FinalRQ.Amount))*FaceValue) ));
    println( "~Sum_Net_avoir_Wr" );
    prinln_SumToStr( money(int(double(FinalRQ.Amount))*FaceValue), fininstr.FaceValueFI );
  else
    println( "~Sum_Net_avoir" );
    println( CheckData( 0 ) );
    println( "~Sum_Net_avoir_Wr" );
    prinln_SumToStr( 0, fininstr.FaceValueFI );
  end;

  if ( ПолучитьФинИн( fininstr.FaceValueFI, Currency ) == 0 )
    println( "~FaceValueWr" );
    prinln_SumToStr( FaceValue, fininstr.FaceValueFI );

    println( "~ParentFI" );
    println( CheckData( Currency.Name ) );
  else
    println( "~Sum_Net_avoir_all_Wr" ); 
    print_empty();
    println( "~FaceValueWr" );
    print_empty();
    println( "~ParentFI" );
    print_empty();
  end;

  println( "~Name_avoir" );
  println( CheckData(fininstr.Name) );

  println( "~ISIN" );
  println( CheckData(avoiriss.ISIN) );

  println( "~LSIN" );
  println( CheckData(avoiriss.LSIN) );

  println( "~Issuer" );
  if ( ПолучитьСубъекта( FI_GetIssuerOnDate( fininstr, Date_net), party ) == 0 )
    println( CheckData(party.Name) );
  else
    print_empty();
  end;

  return true;
end;

/* формирование тэгов по финальному ТО неттинга */
private macro RQTag()
  var PayerName = "", ReceiverName = "";
  var Payer = 0, Receiver = 0;
  var pt = TRecHandler("party");
  record Currency(fininstr);

  if(ExistFinalRQ == true)  /*Если существует финальное ТО, то заполним тэги из него*/
     println( "~Netting" );
     println( CheckData( FinalRq.Amount ) );

     println( "~Netting_Wr" );
     prinln_SumToStr( FinalRq.Amount, FinalRq.FIID );

     println( "~Netting_Avoir" );
     println( CheckData( StrBefore(string(FinalRq.Amount),".")) );

     println( "~Netting_Avoir_Wr" );
     prinln_SumToStr( StrBefore(string(FinalRq.Amount),"."), FinalRq.FIID );
     
     println( "~Fi_netting" );
     if ( ПолучитьФинИн( FinalRq.FIID, Currency ) == 0 )
       println( CheckData(Currency.Name) );
     else
       print_empty();
     end;

     if(FinalRQ.Kind == DLRQ_KIND_REQUEST)
       Payer = FinalRQ.Party;
       Receiver = {OurBank};
     else
       Payer = {OurBank};
       Receiver = FinalRQ.Party;
     end;

     if( ПолучитьСубъекта( Payer, pt ))
       PayerName = pt.rec.ShortName;
     end;

     if( ПолучитьСубъекта( Receiver, pt ))
       ReceiverName = pt.rec.ShortName;
     end;

     println( "~Payer" );
     println( CheckData(PayerName) );

     println( "~Receiver" );
     println( CheckData(ReceiverName) );

     println( "~Date_pay" );
     println( CheckData(date(FinalRq.PlanDate)) );

     println( "~Date_del" );
     println( CheckData(date(FinalRq.PlanDate)) );
  else  
     println( "~Netting" );
     println( CheckData(0) );

     println( "~Netting_Wr" );
     prinln_SumToStr( 0,  dlnet.BaseFIID );

     println( "~Netting_Avoir" );
     println( CheckData(0) );

     println( "~Netting_Avoir_Wr" );
     prinln_SumToStr( 0, 0 );
     
     println( "~Fi_netting" );
     if ( ПолучитьФинИн( dlnet.BaseFIID, Currency ) == 0 )
       println( CheckData(Currency.Name) );
     else
       print_empty();
     end;

     println( "~Payer" );
     print_empty();

     println( "~Receiver" );
     print_empty();

     println( "~Date_pay" );
     println( CheckData(Date(0,0,0)) );

     println( "~Date_del" );
     println( CheckData(Date(0,0,0)) );
  end;
  return true;
end;

/* формирование тэгов по всем участвующим в неттинге сделкам БОЦБ */
private macro AllDealsTag( IsConfirm )
  var  stat, bstat,
       FaceValue = $0,
       FaceValueFI,
       Number_date = "",
       CountReq = 0, /* кол-во требований */
       CountCom = 0, /* кол-во обязательств */
       PayWithoutMovePlus  = 0,
       PayWithoutMoveMinus = 0,
       Netting_All = 0,
       AmountReq = 0,
       AmountCom = 0,
       FaceValueReq = 0,
       FaceValueCom = 0,
       B_RqInAdv = $0,
       S_RqInAdv = $0;

  var  Select, query, DataSet;
  var  rq;
  var  Payer, Receiver;
  file avns_pmpaym(pmpaym) key 1;
  file dlleg(dl_leg);
  record Currency(fininstr);
  record fiwarnts( fiwarnts );

  var IsSecondPart = false;

  macro ДоговорПоСделке()

    var Group = GetOperationGroup(DataSet.DealType);
    var KindDoc, Number = "";
    var Select1, spgr;

    if((IsBACKSALE(Group) == true))
      if(IsSecondPart == false)
        KindDoc = DOCKIND_CONTR_FIRSTPART;
      else
        KindDoc = DOCKIND_CONTR_SECONDPART;
      end;
    else
      KindDoc = DOCKIND_BUYSALEREPORT;
    end;

    Select1 = DL_RSDCommand(  " select spgr.t_Xld, spgr.t_RegistrDate "
                            + "   from dspgrdoc_dbt grdoc, dspground_dbt spgr "
                            + "  where grdoc.t_SourceDocKind = ? "
                            + "    and grdoc.t_SourceDocID = ? "
                            + "    and spgr.t_SPgroundID = grdoc.t_SPgroundID "
                            + "    and spgr.t_Kind = ?"
                           );
    Select1.addParam(DataSet.BofficeKind);
    Select1.addParam(DataSet.DealID);
    Select1.addParam(KindDoc);

    spgr = Select1.execute();

    if( spgr.moveNext() )
      return ("№ " + CheckData(spgr.Xld) + " от " + CheckData(date(spgr.RegistrDate)));
    end;
    return ("№ " + CheckData() + " от " + CheckData());
  end;

  /* сначала подсчитаем все сделки */
  query =  " select rq.t_Kind from dpmlink_dbt lnk, ddlrq_dbt rq"
         + "  where lnk.t_DocKind = ? "
         + "    and lnk.t_DocumentId = ? "
         + "    and lnk.t_LinkKind = ? "
         + "    and rq.t_ID = lnk.t_InitialPayment";

  if(IsConfirm == true)
    if(dlnet.FI_Kind == FIKIND_AVOIRISS)
      query = query + " and rq.t_Type = " + DLRQ_TYPE_DELIVERY
    elif(dlnet.FI_Kind == FIKIND_CURRENCY)
      query = query + " and rq.t_Type IN ("+DLRQ_TYPE_PAYMENT+","+DLRQ_TYPE_AVANCE+","+DLRQ_TYPE_DEPOSIT+") ";
    end;
  end;

  Select = DL_RSDCommand(query);
  Select.addParam(dlnet.DocKind);
  Select.addParam(dlnet.NettingID);
  Select.addParam(PMLINK_KIND_RQNETTING);

  rq = Select.Execute();

  while( rq.moveNext() )
    if ( rq.Kind == DLRQ_KIND_COMMIT )
      CountCom = CountCom + 1;  /*Обязательства*/
    else
      CountReq = CountReq + 1;  /*Требования*/
    end;
  end;

  /*Т.е. реально мы просмотрели все ТО, привязанные к данной сделке
    неттинга. Если ни одного из ТО мы не нашли, то будем ругаться
    и отваливаться*/
  if((CountCom == 0) AND (CountReq == 0))
    Msgbox("Не найдены ТО по сделке.|Необходимо ввести ТО либо обновить сделку");  
    return false;
  end;

  /* размножим строки таблиц до нужного количества или удалим если нет данных*/
  if(CountCom > 0)
    MultipleRow(0, CountCom, COMMITMENT);    /*Обязательство*/
  else
    DeleteTable(COMMITMENT);
  end;

  if(CountReq > 0)
    MultipleRow(0, CountReq, REQUIREMENT);   /*Требование*/
  else
    DeleteTable(REQUIREMENT);
  end;


  CountCom = 0;
  CountReq = 0;

  /* теперь заполним  все тэги сделок */
  query =  " select lnk.t_FIID, lnk.t_Amount, lnk.t_IPSign, "
         + "        rq.t_Party, rq.t_Kind, rq.t_PlanDate, rq.t_DealPart, "
         + "        tick.t_DealDate, tick.t_DealID, tick.t_DealType, tick.t_BofficeKind "
         + "   from dpmlink_dbt lnk, ddlrq_dbt rq, ddl_tick_dbt tick"
         + "  where lnk.t_DocKind = ? "
         + "    and lnk.t_DocumentId = ? "
         + "    and lnk.t_LinkKind = ? "
         + "    and rq.t_ID = lnk.t_InitialPayment "
         + "    and tick.t_DealID = rq.t_DocID ";

  if(IsConfirm == true)
    if(dlnet.FI_Kind == FIKIND_AVOIRISS)
      query = query + " and rq.t_Type = " + DLRQ_TYPE_DELIVERY
    elif(dlnet.FI_Kind == FIKIND_CURRENCY)
      query = query + " and rq.t_Type IN ("+DLRQ_TYPE_PAYMENT+","+DLRQ_TYPE_AVANCE+","+DLRQ_TYPE_DEPOSIT+") ";
    end;
  end;

  Select = DL_RSDCommand(query);
  Select.addParam(dlnet.DocKind);
  Select.addParam(dlnet.NettingID);
  Select.addParam(PMLINK_KIND_RQNETTING);

  DataSet = Select.Execute();

  while( DataSet.moveNext() )

      if( FI_GetNominal( dlnet.BaseFIID, FaceValue, FaceValueFI ) != 0 )
        FaceValue = $0;
      end; 

      if ( DataSet.IPSign == "+" )
        PayWithoutMovePlus = PayWithoutMovePlus + DataSet.Amount;
      elif ( DataSet.IPSign == "-" )
        PayWithoutMoveMinus = PayWithoutMoveMinus + DataSet.Amount;
      end;

      if(DataSet.Kind == DLRQ_KIND_REQUEST)
        Payer = DataSet.Party;
        Receiver = {OurBank};
      else
        Payer = {OurBank};
        Receiver = DataSet.Party;
      end;

    if( Payer == {OurBank} )
      CountCom = CountCom + 1;
      FaceValueCom = FaceValueCom + money(DataSet.Amount*FaceValue/*/100*/); 
      AmountCom = AmountCom + DataSet.Amount;

      println( "~S_Seq_Number" + string(CountCom) );
      println( CheckData(CountCom) );

      println( "~S_date_Deal" + string(CountCom) );
      println( CheckData(date(DataSet.DealDate)) );

      println( "~S_date_pay" + string(CountCom) );
      println( CheckData(date(DataSet.PlanDate)) );

      println( "~S_Fi" + string(CountCom) );
      if ( ПолучитьФинИн( DataSet.FIID, Currency ) == 0 )
        println( CheckData(Currency.Name) );
      else
        print_empty();
      end;

      println( "~S_date_del" + string(CountCom) );
      println( CheckData(date(DataSet.PlanDate)) );

      println( "~S_Amount" + string(CountCom) );
      println( CheckData(convert_digit( DataSet.Amount, DataSet.FIID)));

      println( "~S_Amount_Wr" + string(CountCom) );
      prinln_SumToStr( convert_digit( DataSet.Amount, DataSet.FIID), DataSet.FIID );

      println( "~S_FaceValueAll" + string(CountCom) );
      println( CheckData( money(DataSet.Amount*FaceValue/*/100*/) ));

      println( "~S_FaceValueAllWr" + string(CountCom) );
      prinln_SumToStr( money(DataSet.Amount*FaceValue/*/100*/), FaceValueFI );

      println( "~S_PaymentInAdv" + string(CountCom) );
      S_RqInAdv = $0;
      println( $0 );

    else
      CountReq = CountReq + 1;
      FaceValueReq = FaceValueReq + money(DataSet.Amount*FaceValue/*/100*/);
      AmountReq = AmountReq + DataSet.Amount;

      println( "~B_Seq_Number" + string(CountReq) );
      println( CheckData(CountReq) );
                 
      println( "~B_date_Deal" + string(CountReq) );
      println( CheckData(date(DataSet.DealDate)) );

      println( "~B_date_pay" + string(CountReq) );
      println( CheckData(date(DataSet.PlanDate)) );

      println( "~B_Fi" + string(CountReq) );
      if ( ПолучитьФинИн( DataSet.FIID,  Currency ) == 0 )
        println( CheckData(Currency.Name) );
      else
        print_empty();
      end;

      println( "~B_date_del" + string(CountReq) );
      println( CheckData(date(DataSet.PlanDate)) );

      println( "~B_Amount" + string(CountReq) );
      println( CheckData( convert_digit( DataSet.Amount, DataSet.FIID)) );

      println( "~B_Amount_Wr" + string(CountReq) );
      prinln_SumToStr( convert_digit( DataSet.Amount, DataSet.FIID), DataSet.FIID );

      println( "~B_FaceValueAll" + string(CountReq) );
      println( CheckData( money(DataSet.Amount*FaceValue/*/100*/)) );

      println( "~B_FaceValueAllWr" + string(CountReq) );
      prinln_SumToStr( money(DataSet.Amount*FaceValue/*/100*/), FaceValueFI );

      println( "~B_PaymentInAdv" + string(CountReq) ); 
      B_RqInAdv = $0;
      println( $0 );

    end;


    /* ищем транш сделки */
    ClearRecord(dlleg);
    dlleg.DealID  = DataSet.DealID;
    dlleg.LegID   = 0;
    if( int(DataSet.DealPart) == 1 )
      dlleg.LegKind = LEG_KIND_DL_TICK;
      IsSecondPart = false;
    else
      IsSecondPart = true;
      dlleg.LegKind = LEG_KIND_DL_TICK_BACK;
    end; 
    if ( getEQ(dlleg) )
      ClearRecord(fiwarnts);
      if ( ПолучитьФинИн( dlleg.PFI, null, null, null, fiwarnts ) == 0 )
        if( Payer == {OurBank} )

          println( "~S_Days_NKD" + string(CountCom) );
          if(FI_IsCouponAvoiriss( dlleg.PFI ) == true)
            println( CheckData(date(DataSet.PlanDate) - fiwarnts.FirstDate) );
          else
            print_empty();
          end;

          println( "~S_NKD" + string(CountCom) );                 
          println( CheckData(dlleg.NKD) );

          println( "~S_SummWithoutNKD" + string(CountCom) );
          println( CheckData(dlleg.Cost) );

          println( "~S_SummWithNKD" + string(CountCom) );                 
          println( CheckData(dlleg.TotalCost) );

          println( "~S_DeltaPmAdv" + string(CountCom) );
          println( CheckData( dlleg.TotalCost - S_RqInAdv) );

          println( "~S_Rate" + string(CountCom) );                 
          println( CheckData( GetDealPrice( dlleg ) /*dlleg.Price*/ ) );
        else
          println( "~B_Days_NKD" + string(CountReq) );
          if(FI_IsCouponAvoiriss( dlleg.PFI ) == true)
            println( CheckData(date(DataSet.PlanDate) - fiwarnts.FirstDate) );
          else
            print_empty();
          end;

          println( "~B_NKD" + string(CountReq) );                 
          println( CheckData(dlleg.NKD) );

          println( "~B_SummWithoutNKD" + string(CountReq) );                 
          println( CheckData(dlleg.Cost) );

          println( "~B_SummWithNKD" + string(CountReq) );                 
          println( CheckData(dlleg.TotalCost) );

          println( "~B_DeltaPmAdv" + string(CountReq) );
          println( CheckData(dlleg.TotalCost - B_RqInAdv ) );

          println( "~B_Rate" + string(CountReq) );                 
          println( CheckData( GetDealPrice( dlleg ) /*dlleg.Price*/) );
        end;
      else
        println( "~B_Days_NKD" + string(CountReq) );
        print_empty();
        println( "~B_NKD" + string(CountReq) );
        print_empty();
        println( "~B_SummWithoutNKD" + string(CountReq) );
        print_empty();
        println( "~B_DeltaPmAdv" + string(CountReq) );
        print_empty();
        println( "~B_SummWithNKD" + string(CountReq) );
        print_empty();
        println( "~B_Rate" + string(CountReq) );
        print_empty();

        println( "~S_Days_NKD" + string(CountCom) );
        print_empty();
        println( "~S_NKD" + string(CountCom) );
        print_empty();
        println( "~S_SummWithoutNKD" + string(CountCom) );
        print_empty();
        println( "~S_DeltaPmAdv" + string(CountCom) );
        print_empty();
        println( "~S_SummWithNKD" + string(CountCom) );
        print_empty();
        println( "~S_Rate" + string(CountCom) );
        print_empty();
      end;
    end;

    /* ищем договора по сделке */
    if ( Number_date != "" )
      Number_date = Number_date + ", ";
    end;
    Number_date = Number_date + ДоговорПоСделке();

    if ( Payer == {OurBank} )
      println( "~Number_date_S" + string(CountCom) );
      println( ДоговорПоСделке() );
    else
      println( "~Number_date_B" + string(CountReq) );
      println( ДоговорПоСделке() );
    end;
  end;

  println( "~Number_date" );
  println( CheckData(Number_date) );

  println( "~Netting_All" );
  println( CheckData( money(min(PayWithoutMovePlus, PayWithoutMoveMinus) ) ) );

  println( "~Netting_Avoir_all" );
  println( CheckData( StrBefore(string(min(PayWithoutMovePlus, PayWithoutMoveMinus)),".") ) );

  println( "~Sum_Net_avoir_all" );
  println( CheckData( money(money(min(PayWithoutMovePlus, PayWithoutMoveMinus))*FaceValue/*/100*/) ) );

  println( "~B_Face_All" );
  println( CheckData( AmountReq ) );

  println( "~S_Face_All" );
  println( CheckData( AmountCom ) );

  println( "~B_FaceValueTotal" );
  println( CheckData( FaceValueReq ) );

  println( "~S_FaceValueTotal" );
  println( CheckData( FaceValueCom ) );

  if ( ПолучитьФинИн( dlnet.BaseFIID, Currency ) == 0 )
    println( "~Netting_All_Wr" );
    prinln_SumToStr( min(PayWithoutMovePlus, PayWithoutMoveMinus), dlnet.BaseFIID );

    println( "~Sum_Net_avoir_all_Wr" );
    prinln_SumToStr(  money(money(min(PayWithoutMovePlus, PayWithoutMoveMinus))*FaceValue/*/100*/), FaceValueFI );

    println( "~Netting_Avoir_All_Wr" );
    prinln_SumToStr( convert_digit( min(PayWithoutMovePlus, PayWithoutMoveMinus), dlnet.BaseFIID ), dlnet.BaseFIID);
                     
    println( "~B_Face_All_Wr" );
    prinln_SumToStr( AmountReq,  dlnet.BaseFIID );

    println( "~S_Face_All_Wr" );
    prinln_SumToStr( AmountCom,  dlnet.BaseFIID );

    println( "~B_FaceValueTotal_Wr" );
    prinln_SumToStr( FaceValueReq,  FaceValueFI );

    println( "~S_FaceValueTotal_Wr" );
    prinln_SumToStr( FaceValueCom,  FaceValueFI );
  else
    println( "~Netting_All_Wr" );
    print_empty();
    println( "~B_Face_All_Wr" );
    print_empty();
    println( "~S_Face_All_Wr" );
    print_empty();
    println( "~B_FaceValueTotal_Wr" );
    print_empty();
    println( "~S_FaceValueTotal_Wr" );
    print_empty();
  end;

  println( "~B_Seq_Number_All" );
  println( CheckData(CountReq) );

  println( "~S_Seq_Number_All" );
  println( CheckData(CountCom) );

  return true;
end;

/* формирование тэгов по финальной сделке */
private macro FinalDltickTag()
  var num = readNoteForObject( OBJTYPE_NETTING, StrTrimLeft(dlnet.NettingID,34), NOTEKIND_FINALDEAL),
      stat;

  file spgrd(spgrdoc) key 1;
  file spgr(spground);
  file final_dltick(dl_tick) key 1;

  if ( num != "" )
    ClearRecord(final_dltick);
    final_dltick.BofficeKind = DL_SECURITYDOC;
    final_dltick.DealCode    = num;
    if( GetEQ(final_dltick) == false )
      MsgBox( "Не найдена финальная сделка с номером = " + num );
      return false;
    end;

     ClearRecord(spgrd);
     spgrd.SourceDocKind = final_dltick.BofficeKind;
     spgrd.SourceDocID   = final_dltick.DealID;;

     if ( GetGE(spgrd) )
       ClearRecord(spgr);
       spgr.SPgroundID = spgrd.SPGroundID;
       stat = GetGE(spgr);
       println( "~Contract_N" );
       if ( stat )
         println( "№ " + CheckData(spgr.Xld) );
       else
         print_empty();
       end;

       println( "~Contract_D" );
       if ( stat )
         println( CheckData(spgr.RegistrDate) );
       else
         print_empty();
       end;
     else
       println( "~Contract_N" );
       print_empty();
       println( "~Contract_D" );
       print_empty();
     end;
  else  /*Если номер не задан будем печатать пустые поля*/
     println( "~Contract_N" );
     print_empty();
     println( "~Contract_D" );
     print_empty();
  end;

  return true;
end;

/* формирование тэгов для представителей */
private macro ProxyTag( proxySide )
  var side, stat, Caption = "";
  file proxy(dl_proxy);
  file spgr(spground);

  /* представитель */
  ClearRecord(proxy);
  proxy.DocKind       = DL_NTGDOC;
  proxy.ContractID    = dlnet.NettingID;
  proxy.ContractParty = proxySide;
  if( (GetGE(proxy) == false) or (proxy.DocKind != DL_NTGDOC) or (proxy.ContractID != dlnet.NettingID) or (proxy.ContractParty != proxySide) )
    if ( proxySide == DLPROXY_OURBANK )
      MsgBox( "Не найдены представители банка" );
    else
      MsgBox( "Не найдены представители контрагента" );
    end;
    return false;
  end;

  if ( proxySide == DLPROXY_OURBANK )
    side = "Bank";
  else
    Side = "Count";
  end;

  /* документ-основание */
  ClearRecord(spgr);
  spgr.SPgroundID = proxy.ProxyID;
  println( "~" + side + "_ProxyMandateCaption" );
  if ( getEQ(spgr) )
    GetValueName( OBJTYPE_KINDDOC, spgr.Kind, Caption );
    println( CheckData(Caption) + " № " + CheckData(spgr.AltXld) + " от " + CheckData(spgr.SignedDate) );
  else
    print_empty();
  end;

  println( "~" + side + "_ProxyName" );
  println( CheckData(proxy.AgentName) );

  println( "~" + side + "_ProxyNameShort" );
  println( CheckData(CutFullName(proxy.AgentName)) );

  println( "~" + side + "_ProxyAuthority" );
  println( CheckData(proxy.AgentPost) );

  /* найдем представителя, имеющего вид "1-е лицо" */
  ClearRecord(proxy);
  proxy.DocKind       = DL_NTGDOC;
  proxy.ContractID    = dlnet.NettingID;
  proxy.ContractParty = proxySide;
  proxy.ProxyRole     = DL_PROXY_BOSS;
  println( "~" + side + "_ProxyTotal" );
  if ( GetEQ(proxy) )
    println( CheckData(proxy.AgentPost) + " " + CheckData(proxy.AgentName) + " ");
    ClearRecord(spgr);
    spgr.SPgroundID = proxy.ProxyID;
    if ( getEQ(spgr) )
      GetValueName( OBJTYPE_KINDDOC, spgr.Kind, Caption );
      println( CheckData(Caption) + "№ " + CheckData(spgr.AltXld) + " от " + CheckData(spgr.SignedDate) );
    else
      print_empty();
    end;
  else
    print_empty();
  end;

  return true;
end;

/* адреса и реквизиты банка */
private macro BankPropsTag( PartyID )
  var side, stat, str = "";
  file   country(country,"rsrfdb.def") key 2;
  file   partcode(partcode);
  file   corschem(corschem) key 1;
  record settacc(settacc);
  record party(party);
  record countparty(party);
  record PartyOwn(party);
  record RecordAdressLegal ( adress );
  record RecordAdressReal ( adress );
  var AccountBuf = TRecHandler("account");
  var sa = TRecHandler("settacc");
  var dlrq = tRecHandler("dlrq");
  var dlrqacc = tRecHandler("dlrqacc");
  var PhoneNumber = "", Fax = "", TelexNumber = "";

  if ( ПолучитьСубъекта( PartyID, party ) == 0 )
    if ( PartyID == {OurBank} )
      side = "Bank";
    else
      side = "Count";
    end;

    /* страна нерезидента */
    println( "~Republic_NonRes" );
    if ( (PartyID != {OurBank}) and (party.NotResident == "X" ) )
      ClearRecord(country);
      country.CodeLat3 = party.NRCountry;
      if ( getEQ(country) )
        println( CheckData(country.Name) );
      else
        print_empty();
      end;
    else
      print_empty();
    end;

    ClearRecord(RecordAdressLegal);
    ClearRecord(RecordAdressReal);
    НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdressLegal);
    НайтиАдресСубъекта(party.PartyID,PTADDR_REAL,RecordAdressReal);

    FindAdressContactValue(party.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber);
    FindAdressContactValue(party.PartyID, PTADDR_LEGAL, CNTADR_FAX,         Fax);
    FindAdressContactValue(party.PartyID, PTADDR_LEGAL, CNTADR_TELEXNUMBER, TelexNumber);

    println( "~" + side + "_FullName" );
    println( CheckData(party.Name) );

    println( "~" + side + "_ShortName" );
    println( CheckData(party.ShortName) );

    println( "~" + side + "Name" );
    println( CheckData(party.Name) );

    println( "~" + side + "Address" );
    println( CheckData(RecordAdressLegal.PostIndex + RecordAdressLegal.Adress) );

    println( "~" + side + "AddressP" );
    println( CheckData(RecordAdressReal.PostIndex + RecordAdressReal.Adress) );

    println( "~" + side + "Tel" );
    println( CheckData(PhoneNumber) );

    println( "~" + side + "Fax" );
    println( CheckData(Fax) );

    println( "~" + side + "Telex" );
    println( CheckData(TelexNumber) );
  else
    println( "~" + side + "_FullName" );
    print_empty();
    println( "~" + side + "_ShortName" );
    print_empty();
    println( "~" + side + "Name" );
    print_empty();
    println( "~" + side + "Address" );
    print_empty();
    println( "~" + side + "AddressP" );
    print_empty();
    println( "~" + side + "Tel" );
    print_empty();
    println( "~" + side + "Fax" );
    print_empty();
    println( "~" + side + "Telex" );
    print_empty();
  end;

  println( "~" + side + "Account" );
  str = "";
  if(ExistFinalRQ == true)
    if ( PartyID == {OurBank} ) /* для банка */
      //не понятно, какой счет брать
    else  /* для контрагента */
      ExecMacroFile("spserv.mac", "ПолучитьДенежныйСчетСубъектаПоСделке", FinalRQ.DocKind, FinalRQ.DocID, PartyID, FinalRQ.FIID, AccountBuf);
      str = AccountBuf.rec.Account;
    end;
  end;
  /* если счета нет, попробуем взять из СПИ */
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.Account;
  end;
  println( CheckData( str ) );

  println( "~" + side + "_AccountCur" );
  str = "";
  if(ExistFinalRQ == true)
    if ( PartyID == {OurBank} )
      //не понятно, какой счет брать
    else
      ExecMacroFile("spserv.mac", "ПолучитьДенежныйСчетСубъектаПоСделке", FinalRQ.DocKind, FinalRQ.DocID, PartyID, FinalRQ.FIID, AccountBuf);
      str = AccountBuf.rec.Account;
    end;
  end;
  if ( (str == "") and FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) )
    str = settacc.Account;
  end;
  println( CheckData(str) );

  println( "~" + side + "_BankName" );
  str = "";
  if(ExistFinalRQ == true)
    if ( PartyID == {OurBank} )
      ПолучитьСубъекта( {OurBank}, PartyOwn );
      str = PartyOwn.ShortName;
    else
      ПолучитьСубъекта( ExecMacroFile("spserv.mac", "ПолучитьБанкСубъектаПоСделке", PartyID, FinalRQ.DocKind, FinalRQ.DocID, FinalRQ.FIID, FinalRQ.Type), PartyOwn );
      str = PartyOwn.ShortName;
    end;
  end;
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.BankName;
  end;
  println( CheckData( str ) );

  println( "~" + side + "_BankNameCur" );
  str = "";
  if(ExistFinalRQ == true)
    if ( PartyID == {OurBank} )
      ПолучитьСубъекта( {OurBank}, PartyOwn );
      str = PartyOwn.ShortName;
    else
      ПолучитьСубъекта( ExecMacroFile("spserv.mac", "ПолучитьБанкСубъектаПоСделке", PartyID, FinalRQ.DocKind, FinalRQ.DocID, FinalRQ.FIID, FinalRQ.Type), PartyOwn );
      str = PartyOwn.ShortName;
    end;
  end;
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.BankName;
  end;
  println( CheckData( str ) );

  ClearRecord(partcode);
  partcode.PartyID  = PartyID;
  partcode.CodeKind = PTCK_BIC;
  if ( getEQ(partcode) )
    println( "~" + side + "_BankBIC" );
    if ( PartyID == {OurBank} )
      println( CheckData(partcode.Code) );
    end;

    println( "~" + side + "BIC" );
    println( CheckData(partcode.Code) );
  else
    if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) and ( settacc.BankCodeKind == PTCK_BIC ) )
      println( CheckData(settacc.BankCode) );
    else
      print_empty();
    end;
  end;

  println( "~" + side + "_BankBIC" );
  if ( (ExistFinalRQ == true) and (PartyID != {OurBank}) )
    println( CheckData(ПолучитьКодСубъекта( ExecMacroFile("spserv.mac", "ПолучитьБанкСубъектаПоСделке", PartyID, FinalRQ.DocKind, FinalRQ.DocID, FinalRQ.FIID, FinalRQ.Type), CODE_BIK )) );
  else
    print_empty();
  end;

  ClearRecord(partcode);
  partcode.PartyID  = PartyID;
  partcode.CodeKind = PTCK_INN;
  println( "~" + side + "TaxId" );
  if ( getEQ(partcode) )
    println( CheckData(partcode.Code) );
  else
    print_empty();
  end;

  ClearRecord(partcode);
  partcode.PartyID  = PartyID;
  partcode.CodeKind = PTCK_NDC;
  println( "~" + side + "Identificator" );
  if ( getEQ(partcode) )
    println( CheckData(partcode.Code) );
  else
    print_empty();
  end;

  ClearRecord(partcode);
  partcode.PartyID  = PartyID;
  partcode.CodeKind = PTCK_SWIFT;
  println( "~" + side + "SWIFT" );
  if ( getEQ(partcode) )
    println( CheckData(partcode.Code) );
  else
    if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc )  and ( settacc.BankCodeKind == PTCK_SWIFT ) )
      println( CheckData(settacc.BankCode) );
    else
      print_empty();
    end;
  end;

  ClearRecord(partcode);
  partcode.CodeKind = PTCK_SWIFT;
  println( "~" + side + "_BankSwift" );
  if(ExistFinalRQ == true)
    if ( PartyID != {OurBank} )
      partcode.PartyID = ExecMacroFile("spserv.mac", "ПолучитьБанкСубъектаПоСделке", PartyID, FinalRQ.DocKind, FinalRQ.DocID, FinalRQ.FIID, FinalRQ.Type);
    else
      partcode.PartyID = {OurBank}; 
    end;
    if ( getEQ(partcode) )
      println( CheckData(partcode.Code) );
    else
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) and ( settacc.BankCodeKind == PTCK_SWIFT ) )
        println( CheckData(settacc.BankCode) );
      else
        print_empty();
      end;
    end;
  else
    if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) and ( settacc.BankCodeKind == PTCK_SWIFT ) )
       println( CheckData(settacc.BankCode) );
     else
       print_empty();
     end;
  end;

  FinalRQ.GetRecord().CopyTo( dlrq.rec );
  ExecMacroFile("spserv.mac", "ПолучитПлатежныеРеквизитыПоТО", dlrq, dlrqacc);

  if ( PartyID == {OurBank} )
    ClearRecord(corschem);
    if(ExistFinalRQ == true)
      KeyNum(corschem, 4);
      ClearRecord(corschem);
      corschem.CorrID     = dlrqacc.rec.BankCorrID;
      corschem.CorAccount = dlrqacc.rec.CorrAcc;
      stat = getEQ(corschem);

      KeyNum(corschem, 1);
    else
      stat = false;
    end;
    println( "~" + side + "CorAccNatCur" );
    if ( stat )
      println( CheckData(corschem.Account) );
    else
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) )
        println( CheckData(settacc.CorrAcc) );
      else
        print_empty();
      end;
    end;

    println( "~" + side + "CorAccCur" );
    if ( stat )
      println( CheckData(corschem.Account) );
    else
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) )
        println( CheckData(settacc.CorrAcc) );
      else
        print_empty();
      end;
    end;

    if ( stat )
      ClearRecord(partcode);
      partcode.PartyID  = corschem.CorrID;
      partcode.CodeKind = PTCK_SWIFT;
      println( "~" + side + "CorrBankCodeSWIFT" );
      if ( getEQ(partcode) )
        println( CheckData(partcode.Code) );
      else
        if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) and ( settacc.BankCorrCodeKind == PTCK_SWIFT ) )
          println( CheckData(settacc.BankCorrCode) );
        else
          print_empty();
        end;
      end;

      println( "~" + side + "CorrBankAdd" );
      ClearRecord(RecordAdressLegal);
      if ( НайтиЮридическийАдресСубъекта(corschem.CorrID,RecordAdressLegal) )
        println( CheckData(RecordAdressLegal.PostIndex + RecordAdressLegal.Adress) );
      else
        print_empty();
      end;
    else
      println( "~" + side + "CorrBankCodeSWIFT" );
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) and ( settacc.BankCorrCodeKind == PTCK_SWIFT ) )
        println( CheckData(settacc.BankCorrCode) );
      else
        print_empty();
      end;
      
      println( "~" + side + "CorrBankAdd" );
      ClearRecord(RecordAdressLegal);
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) and 
           (НайтиЮридическийАдресСубъекта(settacc.BankCorrID,RecordAdressLegal)) )
        println( CheckData(RecordAdressLegal.PostIndex + RecordAdressLegal.Adress) );
      else
        print_empty();
      end;
    end;
  else
    println( "~" + side + "CorAccNatCur" );
    if ((ExistFinalRQ == true) AND (dlrqacc.rec.CorrAcc != ""))
      println( CheckData(dlrqacc.rec.CorrAcc) );
    else
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) )
        println( CheckData(settacc.CorrAcc) );
      else
        print_empty();
      end;
    end;

    println( "~" + side + "CorAccCur" );
    if ((ExistFinalRQ == true) AND (dlrqacc.rec.CorrAcc != ""))
      println( CheckData(dlrqacc.rec.CorrAcc) );
    else
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) )
        println( CheckData(settacc.CorrAcc) );
      else
        print_empty();
      end;
    end;

    println( "~" + side + "CorrBankCodeSWIFT" );
    ClearRecord(partcode);
    if(ExistFinalRQ == true)
      partcode.CodeKind = PTCK_SWIFT;
      partcode.PartyID = ExecMacroFile("spserv.mac", "ПолучитьБанкСубъектаПоСделке", PartyID, FinalRQ.DocKind, FinalRQ.DocID, FinalRQ.FIID, FinalRQ.Type);
      if (getEQ(partcode))
        println( CheckData(partcode.Code) );
      else
        print_empty();
      end;
    else
      print_empty();
    end;

    println( "~" + side + "CorrBankAdd" );
    ClearRecord(RecordAdressLegal);
    if ( (ExistFinalRQ == true) AND 
         (НайтиЮридическийАдресСубъекта(partcode.PartyID,RecordAdressLegal)) )
      println( CheckData(RecordAdressLegal.PostIndex + RecordAdressLegal.Adress) );
    else
      print_empty();
    end;
  end;
                                
  println( "~" + side + "CorrBankName" );
  str = "";
  if(ExistFinalRQ == true)
     if ( PartyID == {OurBank} )
       ПолучитьСубъекта( {OurBank}, PartyOwn );
       str = PartyOwn.ShortName;
     else
       str = dlrqacc.rec.BankCorrName;
     end;
  end;
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.BankCorrName;
  end;
  println( CheckData(str) );

  println( "~" + side + "CorrBankNameCur" );
  str = "";
  if(ExistFinalRQ == true)
     if ( PartyID == {OurBank} )
       ПолучитьСубъекта( {OurBank}, PartyOwn );
       str = PartyOwn.ShortName;
     else
       str = dlrqacc.rec.BankCorrName;
     end;
  end;
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.BankCorrName;
  end;
  println( CheckData(str) );

  println( "~" + side + "DepoAcc" );
  str = "";
  if(ExistFinalRQ == true)
     if ( PartyID == {OurBank} )
      //не понятно, какой счет брать
    else
      ExecMacroFile("spserv.mac", "ПолучитьДенежныйСчетСубъектаПоСделке", FinalRQ.DocKind, FinalRQ.DocID, PartyID, FinalRQ.FIID, AccountBuf);
      str = AccountBuf.rec.Account;
    end;
  end;
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.Account;
  end;
  println( CheckData(str) );

  println( "~" + side + "Deposit" );
  str = "";
  if(ExistFinalRQ == true)
     if ( PartyID == {OurBank} )
      ПолучитьСубъекта( {OurBank}, PartyOwn );
      str = PartyOwn.ShortName;
    else
      ПолучитьСубъекта( ExecMacroFile("spserv.mac", "ПолучитьБанкСубъектаПоСделке", PartyID, FinalRQ.DocKind, FinalRQ.DocID, FinalRQ.FIID, FinalRQ.Type), PartyOwn );
      str = PartyOwn.ShortName;
    end;
  end;
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.BankName;
  end;
  println( CheckData(str) );

  /* счет получателя */
  println( "~ReceiverAccount" );
  if(ExistFinalRQ == true)
    println( CheckData(AccountBuf.rec.Account) );
  else
    println( CheckData("") );
  end;

  /* наименование банка получателя платежа */
  println( "~ReceiverBankName" );
  if(ExistFinalRQ == true)
    ПолучитьСубъекта( ExecMacroFile("spserv.mac", "ПолучитьБанкСубъектаПоСделке", PartyID, FinalRQ.DocKind, FinalRQ.DocID, FinalRQ.FIID, FinalRQ.Type), PartyOwn );
    println( CheckData(PartyOwn.ShortName) );
  else
    println( CheckData("") );
  end;

  if(ExistFinalRQ == true)
     KeyNum(corschem, 4);
     ClearRecord(corschem);
     corschem.CorrID     = dlrqacc.rec.BankCorrID;
     corschem.CorAccount = dlrqacc.rec.CorrAcc;
     stat = getEQ(corschem);

     KeyNum(corschem, 1);


     if ( FinalRQ.Kind == DLRQ_KIND_COMMIT )
       /* корсчет получателя */
       println( "~ReceiverCorrAcc" );
       println( CheckData(dlrqacc.rec.CorrAcc) );

       /* БИК корреспондента банка получателя */
       println( "~ReceiverCorrBIC" );
       if ( dlrqacc.rec.BankCorrCodeKind == CODE_BIK )  
         println( CheckData(dlrqacc.rec.CorrAcc) );
       else
         print_empty();
       end;
     else
       if ( stat )
         /* корсчет получателя */
         println( "~ReceiverCorrAcc" );
         println( CheckData(corschem.Account) ); 

         /* БИК корреспондента банка получателя */
         ClearRecord(partcode);
         partcode.CodeKind = PTCK_BIC;
         partcode.PartyID  = corschem.CorrID;
         println( "~ReceiverCorrBIC" );
         if ( getEQ(partcode) )
           println( CheckData(partcode.Code) );
         else
           print_empty();
         end;
       else
         println( "~ReceiverCorrAcc" );
         print_empty();
         println( "~ReceiverCorrBIC" );
         print_empty();
       end;
     end;
  else
     println( "~ReceiverCorrAcc" );
     print_empty();
     println( "~ReceiverCorrBIC" );
     print_empty();
     stat = false;
  end;

  /* ИНН корреспондента банка получателя */
  if ( stat )
    ClearRecord(partcode);
    partcode.CodeKind = PTCK_INN;
    partcode.PartyID  = corschem.CorrID;
    println( "~ReceiverCorrTaxID" );
    if ( getEQ(partcode) )
      println( CheckData(partcode.Code) );
    else
      print_empty();
    end;
  else                                        
    println( "~ReceiverCorrTaxID" );
    print_empty();
  end;

  /**************************** НАШ БАНК ******************************/
   /*Наименование нашего банка короткое*/
   println("~НашБанк");
   ПолучитьСубъекта( {OurBank}, PartyOwn );
   println(CheckData(PartyOwn.Name));

   /*наш банк адрес*/
   println("~НашБанкАдрес");
   ClearRecord(RecordAdressLegal);
   НайтиЮридическийАдресСубъекта(PartyOwn.PartyID,RecordAdressLegal);
   println(CheckData(RecordAdressLegal.PostIndex) + " " + CheckData(RecordAdressLegal.Adress));

  return true;
end;


/* формирование общих тэгов ( наименования, адреса и реквизиты ) */
private macro CommonTag( )
  file proxy(dl_proxy);
  record party(party);
  
  if( not ProxyTag( DLPROXY_OURBANK ) )
    return false;
  end;
  
  if( not ProxyTag( DLPROXY_CONTRACTOR ) )
    return false;
  end;

  /* адреса и реквизиты нашего банка */
  if( not BankPropsTag( {OurBank} ) )
    return false;
  end;

  /* адреса и реквизиты контрагента */  
  if( not BankPropsTag( dlnet.Contractor ) )
    return false;
  end;

  println( "~Header" );
  if ( ПолучитьСубъекта( {OurBank}, party ) == 0 )
     ConfirmHeader = party.Name;
  end;
  println( ConfirmHeader );

  return true;
end;


/* формирование тегов для печати */ 
macro FormTagForPrintRQ(NettingID, SPgroundID, IsConfirm)

  var Select, query;

  file spgr(spground) key 0;
  file Templ(dlgrtemp) key 0;
  var date_net = date(0, 0, 0);

  ClearRecord(dlnet);
  dlnet.NettingID = NettingID;
  if ( not getEQ(dlnet) )
    msgbox( "Не найдена сделка неттинга с параметром NettingID = " + NettingID );
    return false;
  end;

  if ( not IsConfirm )
    ClearRecord(spgr);
    spgr.SPgroundID = SPgroundID;
    if ( not getEQ(spgr) )
      msgbox( "Не найден документ соглашения по сделке с параметром SpgroundID = " + SPgroundID );
      return false;
    end;

    Templ.DocLog = KINDDOC_CONTRBUYSALE;
    Templ.Num    = spgr.DocTemplate;
    if( GetEQ(Templ) == false )
       MsgBox( "Не найден шаблон договора по сделке неттинга" );
       return false;
    end;
  else
    ClearRecord(Templ);
    if ( List_GrTempl( int(KINDDOC_CONTRBUYSALE), 29, Templ ) )
      return true;
    end;
  end;


  println(Templ.File_Name);
  [d#######.rtf](StrTrimLeft(StrBefore(spgr.Xld,"/"),7)); 

  println();

  if ( not IsConfirm )
    println( "~Number" );
    println( spgr.Xld );

    println( "~Date_net" );
    println( spgr.SignedDate );
    date_net = spgr.SignedDate;
  end;

  /*Поищем финальное ТО неттинга*/
  query =   " select rq.*, pt.t_ShortName "
          + "   from ddlrq_dbt rq, dparty_dbt pt " 
          + "  where rq.t_DocKind = ? "
          + "    and rq.t_DocID = ? "
          + "    and pt.t_PartyID = rq.t_Party ";
  Select = DL_RSDCommand(query);
  Select.addParam(dlnet.DocKind);
  Select.addParam(dlnet.NettingID);
  
  FinalRQ = Select.Execute();
  if(FinalRQ.moveNext())
    ExistFinalRQ = true;
  end;


  if ( not RQTag() )
    return false;
  end;

  if ( not AllDealsTag(IsConfirm) )
    return false;
  end;

  if ( not FinsTag(date_net) )
    return false;
  end;

  if ( not FinalDltickTag() )
    return false;
  end;

  if ( not CommonTag() )
    return false;
  end;

  return true;
end;