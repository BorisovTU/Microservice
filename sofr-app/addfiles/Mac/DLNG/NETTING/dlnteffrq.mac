/*
$Name:        dlnteffrq.mac
$Module:      Неттинг
$Description: Общие функции при исполнении ТО
*/
IMPORT DealsInter, FIInter, RSD, dlntlib, dlntfd, dl_car, "dlcarry.inc"; 

PRIVATE MACRO ПроводкаПоКВУ(ВидРО, ntg_fd, rq)
    if (not ПроводкаПоКатегориямВнутреннегоУчета(ВидРО, 
                                                 ntg_fd, 
                                                 NULL, 
                                                 rq.rec.PlanDate, 
                                                 rq.rec.FIID,  
                                                 rq.rec.Amount
                                                )
       )
        return false;
    end;

    return true;
END;

/****************************************************************************************/
PRIVATE CLASS NTG_PARAM
    VAR ntg       = NULL,
        Action    = NULL,
        RqKind    = DLRQ_KIND_UNKNOWN,
        payRQ     = true,
        Error     = NULL,
        ID_Operation = 0,
        ID_Step   = 0,
        Parm      = NULL;
END;

PRIVATE MACRO ИсполнениеТО(rq, Parm)
    VAR ВидРО    = 0,
        ntg_fd  = NULL;

    if (rq.rec.Kind != Parm.RqKind)
        return true;
    end;

    ntg_fd  = DL_NettingDoc(Parm.ntg);

    ntg_fd.SetBaseFIID(rq.rec.FIID);

    if (rq.rec.State == DLRQ_STATE_OVERDUE)
        Parm.Error.Добавить(0, "Ожидается снятие оплаты результирующего ТО с просрочки.");
        return false;
    end;

    if (Parm.payRQ)
        if (not ExecMacro2(Parm.Action, rq, Parm.ntg, ntg_fd, Parm.ID_Operation, Parm.ID_Step))
            return false;
        end;
    else
        rq.rec.State = DLRQ_STATE_EXEC;
        if( DL_ChangeDLRQ(rq, rq.rec.PlanDate, DLRQ_ACTION_UPDATE) )
          return false; 
        end;
        
        return true;
    end;

    if (Parm.Parm.БэкОфис == "S")
        if (rq.rec.Kind == DLRQ_KIND_REQUEST)
            ВидРО = 46001; /*Для клиентских нужно будет добавить 46002*/
        else
            ВидРО = 45001; /*Для клиентских нужно будет добавить 46002*/
        end;

        return ПроводкаПоКВУ(ВидРО, ntg_fd, rq);
    end;

    return true;
END;

PRIVATE MACRO ИсполнениеТОДляВалюты(Параметры, RqKind, ntg, action, error, ID_Operation, ID_Step)
    VAR Parm = NTG_PARAM;

    Parm.ntg      = ntg;
    Parm.Action   = action;
    Parm.RqKind   = RqKind;
    Parm.Error    = error;
    Parm.Parm     = Параметры;
    Parm.ID_Operation  = ID_Operation;
    Parm.ID_Step       = ID_Step;     
    /*
    if (GetTrue(true, "Оформить оплату результирующего ТО?"))
        Parm.payRQ = true;
    else
        Parm.payRQ = false;
    end;
    */
        Parm.payRQ = true;

    if (not ForEachRQRes(ntg.NettingID, @ИсполнениеТО, Parm))
        return false;
    end;

    return true;
END;

PRIVATE MACRO ИсполнениеТОДляЦенныхБумаг(Параметры, RqKind, ntg, error, ID_Operation, ID_Step)
    VAR rq    = NULL,
        ВидРО    = 0;

    if (ЕстьРезТОПоЦеннымБумагам(ntg.NettingID, rq)) 

        if (not ExecMacroFile("dlntdp.mac", "ИмпортироватьПоручениеДепо", ntg, rq, rq.rec.PlanDate))
            return false; 
        end;

        rq.rec.State = DLRQ_STATE_EXEC;

        if( DL_ChangeDLRQ(rq, rq.rec.PlanDate, DLRQ_ACTION_UPDATE) )
          return false; 
        end;

        if (rq.rec.Kind == DLRQ_KIND_REQUEST)
            ВидРО = 77001; /*Для клиентских нужно будет добавить 77002*/
        else  
            ВидРО = 76001; /*Для клиентских нужно будет добавить 77002*/
        end;

        return ПроводкаПоКВУ(ВидРО, DL_NettingDoc(ntg), rq);
    end;

    return true;
END;

/****************************************************************************************/
MACRO ИсполнитьТО(Параметры, RqKind, ntg, action, error, ID_Operation, ID_Step)
    VAR retVal = true;

    if (ntg.FI_Kind == FIKIND_CURRENCY)
        retVal = ИсполнениеТОДляВалюты(Параметры, RqKind, ntg, action, error, ID_Operation, ID_Step);
    elif (ntg.FI_Kind == FIKIND_AVOIRISS)
        retVal = ИсполнениеТОДляЦенныхБумаг(Параметры, RqKind, ntg, error, ID_Operation, ID_Step);
    else
        retVal = false;
    end;

    return retVal;
END;
