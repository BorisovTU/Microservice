/*
$Name:        ntchdate.mac
$Module:      Неттинг
$Description: Шаг изменения сроков исполнения (просрочка/снятие с просрочки)
*/
import DealsInter, "dlcarry.inc", "dlntlib.mac", "dlntfd.mac";

record ChangeDates("spchdate.str"); /*глобальная структура с данными по изменению сроков*/
private record ntg_buff("dl_nett.dbt");
private var FD;

private var ID_Operation,
            ID_Step;     

MACRO ПроверитьРезПлатеж(pmobj, FIID:@Integer )
   if( (FIID != -1) AND (FIID != pmobj.BaseFIID) )
      MsgBoxEX( "Операция должна выполняться только для неттинга по одной валюте.", 0, 0, "Ошибка выполнения" );
      return false;
   end;
   FIID = pmobj.BaseFIID;
   return true;
END;

MACRO ПроверитьРезТО( rq, FIID:@Integer )
   if( (FIID != -1) AND (FIID != rq.rec.FIID) )
      MsgBoxEX( "Операция должна выполняться только для неттинга по одной валюте.", 0, 0, "Ошибка выполнения" );
      return false;
   end;
   FIID = rq.rec.FIID;
   return true;
END;

macro ВыполнитьПросрочку( pmobj )
   var Amount, DocGround, AccDebet, AccCredit;

   FD.SetBaseFIID(pmobj.BaseFIID);

   if( (pmobj.PaymStatus >= PM_READY_TO_SEND) OR 
       (pmobj.PaymStatus == PM_CLOSED_W_M_MOVEMENT) OR
       (pmobj.PaymStatus == PM_OVERDUE)
     )
      MsgBoxEX( "Статус результирующего платежа не подходит для выполнения просрочки.", 0, 0, 
                "Вынос на просрочку", "Ошибка при выполнении операции" );
      return false;
   end;  

   if( PaymIsDemand(pmobj, ntg_buff) == "" )
       /*Обязательства*/
       AccDebet  = pmobj.PayerAccount;  /*"-Форвард, расчеты"; */
       if( not FD.GetAccount( "Обяз. с н.с.", AccCredit, MC_OPENACC_CREATE, /*null*/pmobj.GetPM_PAYM().rec.PayFIID, ntg_buff.ValueDate) )
          return false;
       end;
       pmobj.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                    pmobj.PayerBankID, 
                    pmobj.PayerBankCodeKind, 
                    pmobj.PayerBankCode, 
                    pmobj.PayerBankName,
                    pmobj.PayerBankCorrAcc,
                    pmobj.PayerFIID, 
                    1/*CHAPT1*/, 
                    AccCredit, 
                    pmobj.Payer,                             
                    pmobj.PayerName, 
                    pmobj.PayerINN );


       DocGround = "Перенос на просрочку обязательств";
   else 
       /*Требования*/
       AccCredit  = pmobj.ReceiverAccount; /*"+Форвард, расчеты"; */

       if( not FD.GetAccount( "Треб. с н.с.", AccDebet, MC_OPENACC_CREATE, /*null*/pmobj.GetPM_PAYM().rec.PayFIID, ntg_buff.ValueDate) )
          return false;
       end;
       pmobj.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                    pmobj.ReceiverBankID, 
                    pmobj.ReceiverBankCodeKind, 
                    pmobj.ReceiverBankCode, 
                    pmobj.ReceiverBankName,
                    pmobj.ReceiverBankCorrAcc,
                    pmobj.ReceiverFIID, 
                    1/*CHAPT1*/, 
                    AccDebet, 
                    pmobj.Receiver, 
                    pmobj.ReceiverName, 
                    pmobj.ReceiverINN );

       DocGround = "Перенос на просрочку требований";
   end;

   if( pmobj.BaseFIID != pmobj.GetPM_PAYM().rec.PayFIID )
      if( ConvSum( Amount, pmobj.BaseAmount, pmobj.ValueDate, pmobj.BaseFIID, pmobj.GetPM_PAYM().rec.PayFIID) )
         MsgBoxEX( "Не могу сконвертировать сумму из " + ПолучитьКодФинИн(pmobj.BaseFIID) + " в " + ПолучитьКодФинИн(pmobj.GetPM_PAYM().rec.PayFIID), 0, 0, 
                   "Вынос на просрочку", "Ошибка при выполнении операции" );
         return false;
      end;
   else
      Amount = pmobj.GetPM_PAYM().rec.Amount;
   end;

   if( DocCarry( false, 1, pmobj.GetPM_PAYM().rec.PayFIID, AccDebet, AccCredit, 
                 Amount, pmobj.ValueDate, null, 
                 DocGround, pmobj.PaymentID ) )
       return false;
   end;

   pmobj.PaymStatus = PM_OVERDUE;

   return true;
end;

macro ВыполнитьПросрочкуТО( rq:object )
   var Amount, DocGround, AccDebet, AccCredit;
   var Cat = "";

   FD.SetBaseFIID(rq.rec.FIID);

   if( (rq.rec.State == DLRQ_STATE_EXEC) OR 
       (rq.rec.State == DLRQ_STATE_OVERDUE)
     )
      MsgBoxEX( "Статус результирующего ТО не подходит для выполнения просрочки.", 0, 0, 
                "Вынос на просрочку", "Ошибка при выполнении операции" );
      return false;
   end;  

   if( rq.rec.Kind == DLRQ_KIND_COMMIT )          
       /*Обязательства*/

       if(ntg_buff.OverTransitAccount == "X")
         Cat = "-Расчеты";
       else
         Cat = "-Форвард, расчеты";
       end;

       if( not FD.GetAccount( Cat, AccDebet, MC_OPENACC_CREATE, /*null*/ntg_buff.PayFIID, ntg_buff.ValueDate) )
          return false;
       end;

       if( not FD.GetAccount( "Обяз. с н.с.", AccCredit, MC_OPENACC_CREATE, /*null*/ntg_buff.PayFIID, ntg_buff.ValueDate) )
          return false;
       end;

       DocGround = "Перенос на просрочку обязательств";
   else 
       /*Требования*/
       if(ntg_buff.OverTransitAccount == "X")
         Cat = "+Расчеты";
       else
         Cat = "+Форвард, расчеты";
       end;

       if( not FD.GetAccount( Cat, AccCredit, MC_OPENACC_CREATE, /*null*/ntg_buff.PayFIID, ntg_buff.ValueDate) )
          return false;
       end;

       if( not FD.GetAccount( "Треб. с н.с.", AccDebet, MC_OPENACC_CREATE, /*null*/ntg_buff.PayFIID, ntg_buff.ValueDate) )
          return false;
       end;

       DocGround = "Перенос на просрочку требований";
   end;

   if( rq.rec.FIID != ntg_buff.PayFIID )
      if( ConvSum( Amount, rq.rec.Amount, rq.rec.PlanDate, rq.rec.FIID, ntg_buff.PayFIID) )
         MsgBoxEX( "Не могу сконвертировать сумму из " + ПолучитьКодФинИн(rq.rec.FIID) + " в " + ПолучитьКодФинИн(ntg_buff.PayFIID), 0, 0, 
                   "Вынос на просрочку", "Ошибка при выполнении операции" );
         return false;
      end;
   else
      Amount = rq.rec.Amount;
   end;

   if( DocCarry( false, 1, ntg_buff.PayFIID, AccDebet, AccCredit, 
                 Amount, rq.rec.PlanDate, null, 
                 DocGround, 0 ) )
       return false;
   end;

   rq.rec.State = DLRQ_STATE_OVERDUE;
   if( DL_ChangeDLRQ(rq, ntg_buff.ValueDate, DLRQ_ACTION_UPDATE) )
     return false; 
   end;
   
   return true;
end;

macro СнятьСПросрочки( pmobj )
   var NewDate = {curdate};

   FD.SetBaseFIID(pmobj.BaseFIID);

   if( pmobj.PaymStatus != PM_OVERDUE )
      MsgBoxEX( "Результирующий платеж должен быть просрочен.", 0, 0, "Снятие с просрочки", "Ошибка при выполнении операции" );
      return false;
   end;  

   if( not GetDate( NewDate, "Введите дату снятия платежа с просрочки" ) )
      return false;
   end;
             
   pmobj.ValueDate = NewDate;
   pmobj.PaymStatus = PM_REMOVE_OVERDUE;
   
   pmobj.RecalcPaySums();

   if(pmobj.Actuate())
     return false;
   else
     if(pmobj.ActuateFutureAmounts(TBA_AMOUNT))
       return false;
     end;
   end;

   return true;
end;

macro СнятьСПросрочкиТО( rq )
   var NewDate = {curdate};

   FD.SetBaseFIID(rq.rec.FIID);

   if( rq.rec.State != DLRQ_STATE_OVERDUE )
      MsgBoxEX("Результирующее ТО должно быть просрочено.", 0, 0, "Снятие с просрочки", "Ошибка при выполнении операции");
      return false;
   end;

   if( not GetDate( NewDate, "Введите дату снятия ТО с просрочки" ) )
      return false;
   end;

   rq.rec.State    = DLRQ_STATE_PLAN;
   rq.rec.PlanDate = NewDate;
   if( DL_ChangeDLRQ(rq, NewDate, DLRQ_ACTION_UPDATE) )
     return false; 
   end;

   return true;
end;

PRIVATE MACRO ИзменениеСроковПлатежей( ntg_buff )
  var FIID = -1;

  if( not ForEachPMResPaym(ntg_buff.IdentProgram, ntg_buff.NettingID, @ПроверитьРезПлатеж, @FIID) )
     return 1;
  end;

  if( ChangeDates.Action == 1 ) /*просрочка*/
    if( not ForEachPMResPaym(ntg_buff.IdentProgram, ntg_buff.NettingID, @ВыполнитьПросрочку ) )
       return 1;
    end;

  elif( ChangeDates.Action == 2 ) /*снятие с просрочки*/
    if( not ForEachPMResPaym(ntg_buff.IdentProgram, ntg_buff.NettingID, @СнятьСПросрочки ) )
       return 1;
    end;
  end;

  return 0;
END;

PRIVATE MACRO ИзменениеСроковТО(ntg_buff)
  var FIID = -1;
  
  if( not ForEachRQRes(ntg_buff.NettingID, @ПроверитьРезТО, @FIID) )
     return 1;
  end;
  
  if( ChangeDates.Action == 1 ) /*просрочка*/
    if( not ForEachRQRes(ntg_buff.NettingID, @ВыполнитьПросрочкуТО) )
       return 1;
    end;
  elif( ChangeDates.Action == 2 ) /*снятие с просрочки*/
    if( not ForEachRQRes(ntg_buff.NettingID, @СнятьСПросрочкиТО) )
       return 1;
    end;
  end; 

  return 0;
END;

macro ExecuteStep( doc, FDoc, DocKind, _ID_Operation, _ID_Step )

  SetBuff(ntg_buff, FDoc);

  ID_Operation = _ID_Operation; 
  ID_Step      = _ID_Step;

  /*Должна выполняться только для неттинга по деньгам и по одной валюте*/
  if( ntg_buff.FI_Kind != FIKIND_CURRENCY ) /*валюта*/
     MsgBoxEX("Операция должна выполняться только для неттинга по денежным платежам.", 0, 0, "Ошибка выполнения");
     return 1;
  end;

  DL_PrepareCarryBuffer(doc);

  FD = DL_NettingDoc(ntg_buff);

  if( (ChangeDates.Action == 1) or (ChangeDates.Action == 2) ) /*просрочка или снятие с просрочки*/
     if( ntg_buff.IdentProgram == 14/*IP_SEC*/ )
        return ИзменениеСроковТО(ntg_buff);
     else
        return ИзменениеСроковПлатежей(ntg_buff);
     end;
  elif( ChangeDates.Action == 0 ) 
     MsgBoxEX("Шаг изменения сроков исполнения из списка шагов выполнять запрещено.", 0, 0, "Ошибка выполнения операции");
     return 1;
  else
     MsgBoxEX("Неопределен вид действия для операции изменения сроков.", 0, 0, "Ошибка выполнения операции");
     return 1;
  end;

  return 0;
end;
