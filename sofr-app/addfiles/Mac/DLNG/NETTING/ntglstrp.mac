/*
$Name:             ntglstrp.mac
$Module:           КО, МБК, ПИ, БОЦБ 
$Description:      Отчет "Сессия неттинга"(Excel)- KMB
*/
/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 6.0                                          R-Style SoftLab

  File Name   : ntglstrp.mac
  Created     : 25.06.2007
  Programmer  : Куперин Максим
  Description : печать отчета "Сессия неттинга"(Excel)- KMB
└───────────────────────────────────────────────────────────────────────────*/
IMPORT "or_tpl_h.mac", "dldlngfun.mac"; 
IMPORT "Print_docs_PFI.mac";

//переменные
PRIVATE VAR Лист = TBFile("ntglstrp.dbt", "wc+", 0, GetWorkFileName("ntglstrp")),
            ObjTempl:CTemplateXLS = CTemplateXLS(),
            Table, Counter = 0;
 


// Заполнение отчета данными 
MACRO bodySSN( НомерСделки, Контрагент, Дата, Требования, Обязательства, Валюта, Сделка )
    Лист.Clear;
    Лист.rec.ContrName = Контрагент;
    Лист.rec.DealDate = Дата;
    Лист.rec.ReQ = Требования;
    Лист.rec.Liab = Обязательства;
    Лист.rec.FIID = Валюта;
    Лист.rec.DealCode = НомерСделки;
    Лист.rec.DealName = Сделка;
    if( NOT Лист.Insert )
        RunError("Ошибка при вставке записи во временный файл.");
    end;
END;

//получить код финансового инструмента 
PRIVATE MACRO GetFI_Code( FIID )
  var fininstr = TBFile("fininstr.dbt", "R");

    if( not ПолучитьФинИн( FIID, fininstr ) ) 
        if( fininstr.rec.FI_Kind == FIKIND_AVOIRISS )
            return fininstr.rec.FI_Code;
        else
            return fininstr.rec.CCY;
        end;
    end;

    return "";
END;

// Печать наименования фин инструмента
PRIVATE MACRO PrnCurNameExcel(FIID)
  var CurrAddr;

    if (Counter > 0)
        Table.AddStr();
        Counter = 0;
    end;

    CurrAddr = Table.GetCurAddress("T_LineToCopy");

    Table.AddStr();
    ObjTempl.Sheet.Range(CurrAddr).Merge();
    ObjTempl.Sheet.Range(CurrAddr).RowHeight = 26;

    Table.SetValueCell(CurrAddr , "Код актива: " + (GetFI_Code(FIID)));
END;

// Печать итогов
PRIVATE MACRO PrnBottomExcel(ReQRez, LiabRez, FIID, isEndRep)
  var Itog1, Itog2;

    if (Counter > 0)
        Table.AddStr();
        Itog1 = Table.GetCurAddress("T_A1:T_A3");

        Table.SetValueCell("T_A1", "Всего:");
        Table.SetValueCell("T_A4",  ReQRez  );
        Table.SetValueCell("T_A5",  LiabRez );

        Table.AddStr();
        Itog2 = Table.GetCurAddress("T_A1:T_A3");

        ReQRez = ReQRez - LiabRez;
        if( ReQRez > 0 )
            LiabRez = 0;
        else
            LiabRez = -ReQRez;
            ReQRez = 0;
        end;
       Table.SetValueCell("T_A1" , "Итого ("+GetFI_Code(FIID)+"):");
       Table.SetValueCell("T_A4" , ReQRez  );
       Table.SetValueCell("T_A5" , LiabRez );

        if (not isEndRep)
            Table.AddStr();
            Counter = 0;
        end;

        ObjTempl.Sheet.Range(Itog1).Merge();
        ObjTempl.Sheet.Range(Itog1).RowHeight = 26;
        ObjTempl.Sheet.Range(Itog2).Merge();
        ObjTempl.Sheet.Range(Itog2).RowHeight = 26;
    end;
END;

// Печать строки тела отчета
PRIVATE MACRO PrnStrExcel(market, datevl, req, liab, DealCode, deal)
    if (Counter > 0)
        Table.AddStr();
    end;

    Table.SetValueCell("T_A1" , market );
    Table.SetValueCell("T_A2" , DealCode );
    Table.SetValueCell("T_A3" , datevl );
    Table.SetValueCell("T_A4" , req );
    Table.SetValueCell("T_A5" , liab );
    Table.SetValueCell("T_S8" , deal );

    Counter = Counter + 1;
END;

// Печать шапки отчета
PRIVATE MACRO PrnHeadExcel( DealNmb, SignDate, Contr, ValDate, FiKindName )
    ObjTempl.SetValue_NameCell("TH_01" , "СЕССИЯ НЕТТИНГА № " + DealNmb );
    ObjTempl.SetValue_NameCell("TH_02" ,  Date(SignDate));
    ObjTempl.SetValue_NameCell("TH_03" , "Контрагент: " + Contr);
    ObjTempl.SetValue_NameCell("TH_04" , Date(ValDate));
    ObjTempl.SetValue_NameCell("TH_06" , "Вид актива: " + FiKindName);
END;

// Основная функция формирования отчета
MACRO Session(DealNmb, SignDate, Contr, ValDate, FiKindName)

    /*BIQ-9099, предложим печать формы "Подтверждение неттинга" в MS Word*/
    if( MsgBoxEx( "Открыть форму 'Соглашение о неттинге' в MS Word?", MB_YES+MB_NO, IND_YES ) == IND_YES )
        СоглашениеОНеттинге(DealNmb, SignDate);
        return 1;
    end;    

   var PrevFIID, SumReQ = 0, SumLiaB = 0;
   var idx = 0;

    ObjTempl.OpenTemplate("NetSessRep.xls", false); 
    ObjTempl.Application.DisplayAlerts = false;

    Table = ObjTempl.RegisterTable("T_LineToCopy"); 
    Table.ClearCurRow();

    Лист.ReWind;
    Лист.KeyNum = 4;
    PrevFIID = -1;

    PrnHeadExcel(DealNmb, SignDate, Contr, ValDate, FiKindName);  

    InitProgress(Лист.Nrecords(), "Заполнение шаблона", "Заполнение шаблона" );

    while( Лист.next )
        if( PrevFIID == Лист.rec.FIID)
           PrnStrExcel(Лист.rec.ContrName, Лист.rec.DealDate, Лист.rec.ReQ, Лист.rec.LiaB, Лист.rec.DealCode, Лист.rec.DealName);
            SumReQ = SumReQ + Лист.rec.ReQ;
            SumLiaB = SumLiaB + Лист.rec.LiaB;
        else
            PrnBottomExcel(SumReQ, SumLiaB, PrevFIID, false);

            PrnCurNameExcel(Лист.rec.FIID);
            PrnStrExcel(Лист.rec.ContrName, Лист.rec.DealDate, Лист.rec.ReQ, Лист.rec.LiaB, Лист.rec.DealCode, Лист.rec.DealName);
        
            PrevFIID = Лист.rec.FIID;
            SumReQ = Лист.rec.ReQ;
            SumLiaB = Лист.rec.LiaB;
        end;

        idx = idx + 1;
        Useprogress(idx);
    end;

    if (idx > 0)
        PrnBottomExcel(SumReQ, SumLiaB, PrevFIID, true);
    end;

    RemProgress();
    ObjTempl.SaveAsTemplate("Сессия неттинга", true);
   
    Лист.Clear;
    exit(1);
END;
