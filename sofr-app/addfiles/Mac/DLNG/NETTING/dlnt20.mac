/*
$Name:         dlnt20.mac
$Module:       Неттинг
$Description:  макрос шага "Есть Результирующие платежи?"
*/
IMPORT OprInter, globals, dlntlib, DealsInter;
import "ContrGetNetting.mac";   // интеграция - запрос неттинга по контрагенту в Кондор

PRIVATE CONST
    НетТрОб  = 0,
    ЕстьТр   = 1,
    ЕстьОб   = 2,
    ЕстьТрОб = 3;

PRIVATE VAR ВыборШага = НетТрОб;

PRIVATE MACRO ЕстьТребОбяз( pmobj, ntg )
  VAR Выбор;
  var cmdMes;
 //mda 12.02.19 меняяем позиционирование на пользовательское
  cmdMes = RSDCommand("update dpmprop_dbt "
                          + "set t_corrpostype = 1 "
                          + "where t_paymentid = ? and "
                          + "      t_debetcredit = 1 and "
                          + "      t_corrpostype != 1");
  cmdMes.AddParam("paymentid", RSDBP_IN, pmObj.PaymentID);
  cmdMes.Execute();
  if( (pmobj.Payer == {OurBank}) or (pmobj.Payer == ntg.ClientId) )
    //TEG изврат 12.02.19
     if (substr(pmObj.PayerAccount,6,3)!=substr(pmObj.futureReceiverAccount,6,3))
       cmdMes = RSDCommand( " update dpmpaym_dbt pm set pm.t_payeraccount = (  "+
                            "  select p.t_payeraccount from dpmlink_dbt l,dpmpaym_dbt p where  "+
                            "  l.t_purposepayment=?  "+
                            "  and l.t_initialpayment=p.t_paymentid and p.t_payer=1 and rownum=1 ), " +
                            "  pm.t_futurepayeraccount = (  "+
                            "  select p.t_payeraccount from dpmlink_dbt l,dpmpaym_dbt p where  "+
                            "  l.t_purposepayment=?  "+
                            "  and l.t_initialpayment=p.t_paymentid and p.t_payer=1 and rownum=1 ) where pm.t_paymentid=? " );
       cmdMes.AddParam("paymentid", RSDBP_IN, pmObj.PaymentID);
       cmdMes.AddParam("paymentid2", RSDBP_IN, pmObj.PaymentID);
       cmdMes.AddParam("paymentid3", RSDBP_IN, pmObj.PaymentID);
       cmdMes.Execute();                     
     end; 
     //TEG end изврат
     Выбор = ЕстьОб;
  end;

  if( pmobj.Payer == ntg.Contractor )
     Выбор = ЕстьТр;
  end;

  if( (Выбор == ЕстьТр) and (ВыборШага == ЕстьОб) or
      (Выбор == ЕстьОб) and (ВыборШага == ЕстьТр)
    )
     ВыборШага = ЕстьТрОб;
     return false;
  else
     ВыборШага = Выбор;
  end;
 
  return true;
END;

PRIVATE MACRO ЕстьТО(rq, ntg)
    VAR Выбор;
    
    if(rq.rec.Kind == DLRQ_KIND_REQUEST)
      Выбор = ЕстьТр;
    else
      Выбор = ЕстьОб;
    end;
    
    if ((Выбор == ЕстьТр)and(ВыборШага == ЕстьОб)or
        (Выбор == ЕстьОб)and(ВыборШага == ЕстьТр))
        ВыборШага = ЕстьТрОб;
        return false;
    else
        ВыборШага = Выбор;
    end;

    return true;
END;

MACRO ExecuteCaseStep(OperationKind, StepNumber, Document, DocKind)
    VAR retVal    = "";

    RECORD ntg_buff ("dl_nett");

    SetBuff(ntg_buff, Document);

     // интеграция - запрос неттинга в Кондоре
    var ccf = GetNetting( true, ntg_buff.Contractor, ntg_buff.SigningDate );
    if (ccf == "OK")
      msgbox("Сверка с Кондор+ прошла успешно. Расхождений нет");
    elif (ccf == "IGNORE")
      msgbox("Продолжение выполнения");
    else
      return 1; // возвращаемся назад
    end;

    if( ntg_buff.IdentProgram == 14/*IP_SEC*/ )
      ForEachRQRes(ntg_buff.NettingID, @ЕстьТО, ntg_buff);
    else
      ForEachPMResPaym(ntg_buff.IdentProgram, ntg_buff.NettingID, @ЕстьТребОбяз, ntg_buff);
    end;

    if (ВыборШага == ЕстьТр)
        retVal = "30";
    elif (ВыборШага == ЕстьОб)
        retVal = "40";
    elif (ВыборШага == ЕстьТрОб)
        retVal = "40,30";
    else
        retVal = "";
    end;

    return retVal;
END;
