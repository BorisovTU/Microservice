/*
$Name:        dlntpm.mac
$Module:      Неттинг
$Description: Класc платежа с немного измененной функцией проводки
*/
import PaymInter, FIInter, "globals.mac";

/* Установка переменной, если она не определена*/
PRIVATE MACRO ByDefault(x, value)
  if (ValType(x) == V_UNDEF)
      setparm(0, value);
  end;
end;

PRIVATE MACRO NTG_PmBookpass(pm,
                   BpGround,                          /* основание проводки */
                   CreditAccount, CreditAccountFIID,  /* счет кредита с валютой */
                   MinorAmount, MinorFIID,            /* сумма проводки с валютой */
                   MinorCrossRate,                    /* курс конверсии Minor'а к дебету */
                   MinorCreditCrossRate,              /* курс конверсии Minor'а к кредиту */
                   IsPartOfMain,
                   Date_Carry,
                   Number_Pack,
                   Numb_Document,
                   Result_Carry
                   )
   var
       paymtr, pd;

   paymtr = pm.MakeTransaction(CreditAccount, CreditAccountFIID,
                                 MinorAmount, MinorFIID,
                                 MinorCrossRate, MinorCreditCrossRate);

   if( paymtr == NULL )
     MsgBox("Ошибка при создании проводки по платежу");
     return false;
   end;

   if( ValType(IsPartOfMain) != V_Undef ) paymtr.IsPartOfMain = IsPartOfMain; end;

   ByDefault(Date_Carry,     {curdate});
   ByDefault(Result_Carry,   1);
   ByDefault(BpGround,       pm.Ground);
   ByDefault(CreditAccount,  pm.FutureReceiverAccount);

   if(CreditAccount == "")
     MsgBox("Не задан счет получателя в платеже");
     return false;
   end;

   paymtr.Chapter = 1;
   paymtr.Date_Carry = Date_Carry;
   paymtr.ResultCarry = Result_Carry;
   paymtr.Kind_Oper     = " 1";
   paymtr.Ground       = BpGround;
/*   paymtr.Numb_Document = VS_GetBookpassNum(pm.FuturePayerAccount,
                                            CreditAccount, pm.PayerFIID, 1);*/

   if( not paymtr.Carry )
     MsgBox("Ошибка при актуализации платежа");
     return false;
   end;

   return true;
end;


/* Класс платежа с немного измененной функцией проводки
*/
CLASS (RsbPayment) NtgRsbPayment (p1,p2,p3,p4)
    MACRO Bookpass(BpGround,                          /* основание проводки */
                   CreditAccount, CreditAccountFIID,  /* счет кредита с валютой */
                   MinorAmount, MinorFIID,            /* сумма проводки с валютой */
                   MinorCrossRate,                    /* курс конверсии Minor'а к дебету */
                   MinorCreditCrossRate,              /* курс конверсии Minor'а к кредиту */
                   IsPartOfMain,
                   Date_Carry,
                   Number_Pack,
                   Numb_Document,
                   Result_Carry
                   )

      return NTG_PmBookpass(this,
                   BpGround,                          /* основание проводки */
                   CreditAccount, CreditAccountFIID,  /* счет кредита с валютой */
                   MinorAmount, MinorFIID,            /* сумма проводки с валютой */
                   MinorCrossRate,                    /* курс конверсии Minor'а к дебету */
                   MinorCreditCrossRate,              /* курс конверсии Minor'а к кредиту */
                   IsPartOfMain,
                   Date_Carry,
                   Number_Pack,
                   Numb_Document,
                   Result_Carry);
    end;

    InitRsbPayment(p1,p2,p3,p4);
END;

/* Платеж, включенный в неттинг
   Класс сделан для того, что бы не менять по всем макросам вызовы соотв. свойств в платеже
*/
CLASS (RsbPayment) CNtgPayment( IdentProgram, p1, p2, p3, p4 )

  var m_IdentProgram = IdentProgram;

  MACRO BaseFIID()
    if( (m_IdentProgram == 14/*IP_SEC*/) or (m_IdentProgram == 41/*IP_VEKSELACCOUNTED*/) ) /*операция выполняется из БОЦБ или УВ*/
       return _extObj.OrderFIID;
    end;

    return _extObj.BaseFIID;
  end;

  MACRO BaseAmount()
    if( (m_IdentProgram == 14/*IP_SEC*/) or (m_IdentProgram == 41/*IP_VEKSELACCOUNTED*/) or (m_IdentProgram == 42/*IP_DV*/) ) /*операция выполняется из БОЦБ или УВ или ПИ*/
       return _extObj.OrderAmount;
    end;

    return _extObj.BaseAmount;
  end;

  MACRO ReceiverAmount()
    if( (m_IdentProgram == 14/*IP_SEC*/) or (m_IdentProgram == 41/*IP_VEKSELACCOUNTED*/) ) /*операция выполняется из БОЦБ или УВ*/
       VAR Amount = $0;

       if( ConvSum( Amount, _extObj.OrderAmount, _extObj.ValueDate, _extObj.OrderFIID, _extObj.ReceiverFIID))
           MsgBox( "Не могу сконвертировать сумму из " + ПолучитьКодФинИн(_extObj.OrderFIID) + " в " + ПолучитьКодФинИн(_extObj.ReceiverFIID));
           return $0;
       end;

       return Amount;
    end;

    return _extObj.ReceiverAmount;
  end;

  MACRO PayerAmount()
    if( (m_IdentProgram == 14/*IP_SEC*/) or (m_IdentProgram == 41/*IP_VEKSELACCOUNTED*/) ) /*операция выполняется из БОЦБ или УВ*/
       VAR Amount = $0;

       if( ConvSum( Amount, _extObj.OrderAmount, _extObj.ValueDate, _extObj.OrderFIID, _extObj.PayerFIID))
           MsgBox( "Не могу сконвертировать сумму из " + ПолучитьКодФинИн(_extObj.OrderFIID) + " в " + ПолучитьКодФинИн(_extObj.PayFIID));
           return $0;
       end;

       return Amount;
    end;

    return _extObj.PayerAmount;
  end;

  macro InitPaySums()
    if( (m_IdentProgram == 14/*IP_SEC*/) or (m_IdentProgram == 41/*IP_VEKSELACCOUNTED*/) or (m_IdentProgram == 42/*IP_DV*/) ) /*операция выполняется из БОЦБ или УВ*/
       if( _extObj.ReceiverAmount <= 0 )
          if( ConvSum( _extObj.ReceiverAmount, _extObj.OrderAmount, _extObj.ValueDate, _extObj.OrderFIID, _extObj.ReceiverFIID))
              MsgBox( "Не могу сконвертировать сумму из " + ПолучитьКодФинИн(_extObj.OrderFIID) + " в " + ПолучитьКодФинИн(_extObj.ReceiverFIID));
              return false;
          end;
       end;

       if( _extObj.PayerAmount <= 0 )
          if( ConvSum( _extObj.PayerAmount, _extObj.OrderAmount, _extObj.ValueDate, _extObj.OrderFIID, _extObj.PayerFIID))
              MsgBox( "Не могу сконвертировать сумму из " + ПолучитьКодФинИн(_extObj.OrderFIID) + " в " + ПолучитьКодФинИн(_extObj.PayerFIID));
              return false;
          end;
       end;

       if( _extObj.BaseAmount <= 0 )
          if( ConvSum( _extObj.BaseAmount, _extObj.OrderAmount, _extObj.ValueDate, _extObj.OrderFIID, _extObj.BaseFIID))
              MsgBox( "Не могу сконвертировать сумму из " + ПолучитьКодФинИн(_extObj.OrderFIID) + " в " + ПолучитьКодФинИн(_extObj.BaseFIID));
              return false;
          end;
       end;
    end;
    return true;
  end;

  macro RecalcPaySums()
    if( (m_IdentProgram == 14/*IP_SEC*/) or (m_IdentProgram == 41/*IP_VEKSELACCOUNTED*/) ) /*операция выполняется из БОЦБ или УВ*/
       _extObj.ReceiverAmount = 0;
       _extObj.PayerAmount = 0;
       _extObj.BaseAmount = 0;

       return InitPaySums();
    end;
    return true;
  end;

  InitRsbPayment(p1,p2,p3,p4);
END;
