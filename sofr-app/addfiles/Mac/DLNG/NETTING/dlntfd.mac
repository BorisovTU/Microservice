/*
$Name:         dlntfd.mac
$Module:       Неттинг
$Description:  Класс "Документ неттинга" для категорий учета
*/

Import OprInter, CTInter, FIInter, globals, "dlntlib.mac", "mctplprm.mac",
    mccatacf;

Import "spserv.mac", "dlquery.mac";

private const 
    ntg_buff = TRecHandler ("dl_nett.dbt");

/*Процедура определения валюты платежей*/
PRIVATE MACRO GFP (pml, fiid:@variant)
    var ok = true;
    if (fiid != -1)
        if (fiid!=pml.fiid)
            ok = false; 
        end;
    else
        fiid = pml.fiid;
    end;
    return ok;
END;

/*Класс "Документ неттинга" для категорий учета*/
CLASS DL_NettingDoc( parm1, parm2 )
    PRIVATE VAR
        ntg    = TBFile ("dl_nett"),
        payres = NULL,
        ParmA  = TArray,
        FiRoleBArray = TArray,
        ok = true,
        BaseFIID = -1;
    VAR
        /*Основные свойства*/
        Error = 0, kind, id;

    MACRO DealName():STRING
       return ntg.rec.DealNumber;
    END;

    MACRO dl_nett()
      return ntg;
    END;

    MACRO GetBaseFIID():integer
       var RetVal:integer = ntg.rec.BaseFIID;
       if( BaseFIID != -1 )
          RetVal = BaseFIID;
       end;
       return RetVal;
    END;

    MACRO SetBaseFIID( FIID:integer )
       BaseFIID = FIID;
       ParmA[MC_TYPE_PARAMETR_FIID] = GetBaseFIID();
    END;

    /*Получить валюту платежей, -1 если разная*/
    PRIVATE MACRO GetFIPayments ()
        VAR fiid = -1;
        ForEachPMLink(DL_NTGDOC, ntg.rec.NettingID, @GFP, @fiid);
        return fiid;
    END;

    PRIVATE MACRO GetRQByNtg( rq:TRecHandler )
      var query, Select, DataSet;
      var err = 0;

      rq.Clear();

      query = " SELECT rq.* " +
              "  FROM ddlrq_dbt rq "+ 
              " WHERE RQ.T_DOCID = ? " + 
              "   AND RQ.T_DOCKIND = ? " + 
              "   AND RQ.T_DEALPART = 1 " +
              "   AND RQ.T_TYPE = ? " + 
              "   AND RQ.T_FIID = ? "; 
      Select = Dl_RSDCommand(query);
      Select.AddParam(ntg.rec.NettingID);
      Select.AddParam(ntg.rec.DocKind);
      Select.AddParam(IIF ( (ntg.rec.FI_Kind == FIKIND_CURRENCY),  DLRQ_TYPE_PAYMENT, DLRQ_TYPE_DELIVERY));
      Select.AddParam(GetBaseFIID());

      DataSet = Select.Execute();
      if(DataSet.moveNext())
        DataSet.GetRecord().CopyTo( rq.rec );
      else
        err = 1;
      end;

      return err;
    END;

    PRIVATE MACRO PlaceIdForClientNetting()
      var rq    = TRecHandler("dlrq.dbt");
      var rqacc = TRecHandler("dlrqacc");      
      var placeId = 0;
      
      if( GetRQByNtg(rq) == 0 )
         if( ПолучитПлатежныеРеквизитыПоТО(rq, rqacc) == 0 )
            if( rqacc.rec.BANKCORRID > 0 )
               placeId = rqacc.rec.BANKCORRID;
            else
               placeId = rqacc.rec.BANKID;
            end;
         end;   
      end;

      return placeId;
    END;

    PRIVATE MACRO ClientContrID()
      var SfContrID = ntg.rec.ClientContrID;
      if( (ntg.rec.ClientId > 0) and (SfContrID <= 0) )
         SfContrID = NTG_GetFirstClientContract(IIF(ntg.rec.DocKind == DL_NTGSEC, PTSK_STOCKDL, PTSK_DV), ntg.rec.ClientId, ntg.rec.SigningDate);

         if( SfContrID <= 0 )
            SfContrID = NTG_GetFirstClientContract(IIF(ntg.rec.DocKind == DL_NTGSEC, PTSK_DV, PTSK_STOCKDL), ntg.rec.ClientId, ntg.rec.SigningDate);
         end;
      end;

      return SfContrID;
    END;

    PRIVATE MACRO GetPlace()
      if( ClientContrID() > 0 )
         if( ntg.rec.IdentProgram == 14/*IP_SEC*/ )
            return PlaceIdForClientNetting();
         else
            return {OurBank}; /*???*/
         end;
      else
         return {OurBank}; /* вместо -1 возвращаю наш банк т.к. это нужно во внутреннем учете */
      end;
    END;

    PRIVATE MACRO GetOwner()
      if( ntg.rec.ClientID > -1 )
         return ntg.rec.ClientId;
      else
         return {OurBank};
      end;
    END;

    PRIVATE MACRO InitParmArray
        var tmp;

        ParmA[MC_TYPE_PARAMETR_FIID]         = GetBaseFIID();
        ParmA[MC_TYPE_PARAMETR_PAYCURRENCY]  = ntg.rec.PayFIID;
        ParmA[MC_TYPE_PARAMETR_DOCKIND]      = ntg.rec.DocKind;
        ParmA[MC_TYPE_PARAMETR_DOCID]        = ntg.rec.NettingID;
        ParmA[MC_TYPE_PARAMETR_CONTRACTOR]   = ntg.rec.Contractor;
        ParmA[MC_TYPE_PARAMETR_OWNER]        = GetOwner();
        ParmA[MC_TYPE_PARAMETR_DEPARTMENT]   = ntg.rec.Department;
        ParmA[MC_TYPE_PARAMETR_CONTR_CLIENT] = ClientContrID();
        ParmA[MC_TYPE_PARAMETR_CLIENT]       = ntg.rec.ClientId;

        if( ((ntg.rec.IdentProgram == 12/*IP_FRX*/) or (ntg.rec.IdentProgram == 13/*IP_IBC*/) or (ntg.rec.IdentProgram == 42/*IP_DV*/)) and (ВидСубъекта(ntg.rec.Contractor, PTK_MARKETPLASE) == true) )
           ParmA[MC_TYPE_PARAMETR_MARKET_PLACE] = ntg.rec.Contractor;
        end;
        
           ParmA[MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE]  = 0;
        FIRoleBArray[0] = FIROLE_FIDOC;
    END;

    /*Получить параметры*/
    MACRO GetParametr (ParmKind, OperDate, CatCode, FIRole)
        VAR Parametr = ParmA[ParmKind];

        if (Parametr==null)
          if( (ClientContrID() > 0) and (ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE) )
             Parametr = 0;
          elif( MC_TYPE_PARAMETR_PLACE == ParmKind )
             Parametr = GetPlace();
          else
             Parametr = -1;
          end;
        end;

        return Parametr;
    END;

    MACRO GetParametrTemplate
    ( 
        ObjectID,       /* параметр - номер справочника */
        Classificator,  /* классификатор - номер классификатора, если он задан */
        OperDate,
        FIRole
    )
        var TypeContr, IsRes;

       /*вид бэк-офиса*/
        if( (ObjectID==OBJTYPE_BACKOFFICE) AND (Classificator==LLCLASS_BACKOFFICE) )

           if( ntg.rec.IdentProgram == 13/*IP_IBC*/ )
               return 2; /*МБК*/
           elif( ntg.rec.IdentProgram == 12/*IP_FRX*/ )
               return 3; /*Forex*/
           elif( ntg.rec.IdentProgram == 14/*IP_SEC*/ )
               return 5; /*БО Ц/Б*/            
           elif( ntg.rec.IdentProgram == 41/*IP_VEKSELACCOUNTED*/ )
               return 7; /*БО УВ*/
           elif( ntg.rec.IdentProgram == 42/*IP_DV*/ )
               return 8; /*БО ПИ*/
           end;

         /*вид резерва по договорам с ц/б*/
         elif( ( ObjectID == OBJTYPE_KINDRESERV) AND 
               ( (Classificator == LLCLASS_KINDRESERV_CONTR) OR (Classificator == LLCLASS_KINDRESERV) )
             )
             return KINDRES_EXPREQ; /*РПТ По просроченным требованиям*/

         elif( (ObjectID == OBJTYPE_RESIDENT) AND  (Classificator == LLCLASS_RESD_CONTRACTOR) )      
             return MC_IsResident( this.GetParametr( MC_TYPE_PARAMETR_CONTRACTOR ) );

         /*продукт - другие расходы*/
         elif( (ObjectID ==  OBJTYPE_PRODUCT) AND (Classificator == LLCLASS_PRODUCT_ANRAS) )
         
            return 1000; /*Другие операции*/ 

         elif( (ObjectID == OBJTYPE_OTNSEBEST) AND (Classificator == LLCLASS_OTNSEBEST_SEBEST) )
            return -1;  /*не задано*/

         /*Контрагент-КредОрг?*/
         elif( ( ObjectID == OBJTYPE_BOOLEAN) AND (Classificator == LLCLASS_CONTRCREDIT_ORG))      
            if( MC_GetKindParty( ntg.rec.Contractor, IsRes ) == 1 ) /*банк*/  
               return 1; /*Да*/
            else
               return 0; /*Нет*/
            end;
          /*Вид счета расчетов*/
         elif( (ObjectID == OBJTYPE_KIND_ACCOUNT_CALC) AND (Classificator == LLCLASS_KIND_ACC_IN_FUTURES) )
           return 1;

        /*вид депонента*/
        elif( ObjectID == OBJTYPE_KINDOWNER )
           return 2; /*наш банк*/

        /*принадлежность ц/б*/
        elif( ObjectID == OBJTYPE_SECURITY_OWN )
           return 1; /*свои*/

        /*сектор рынка*/
        elif( ObjectID == OBJTYPE_SECTORMARKT )  
           return 2; /*дилинг*/

        /*вид субъекта - Категория дебитора/кредитора в сделках ц/б */
        elif( (ObjectID == OBJTYPE_KINDSUBJ) AND (Classificator == LLCLASS_KINDSUBJ_DEBETSECUR) )
           return MC_GetKindSecurDebet( this.GetParametr( MC_TYPE_PARAMETR_CONTRACTOR) );

        elif( (ObjectID == OBJTYPE_KINDSUBJ) AND (Classificator == LLCLASS_KINDSUBJ_CREDITSECUR) )
           return MC_GetKindSecurCredit( this.GetParametr( MC_TYPE_PARAMETR_CONTRACTOR) );

        elif( (ObjectID == OBJTYPE_KIND_TO) AND (Classificator == LLCLASS_KIND_TO) )
           if ((FIRole == FIROLE_EMIT_INC_TR) or (FIRole == FIROLE_EMIT_INC_OB))
              return 1;
           else
              return 0;
           end;
        elif( (ObjectID == OBJTYPE_IST_DOH_RASH) AND (Classificator == LLCLASS_KIND_MARG_FUTURES) )
           if( ntg.rec.IdentProgram == 41/*IP_VEKSELACCOUNTED*/ )
             return 6;
           end;
        elif( (ObjectID == OBJTYPE_SERVKIND) AND (Classificator == LLCLASS_SERVKIND_INNER) )
          /*Бэк-офис*/
          if( ntg.rec.IdentProgram == 14/*IP_SEC*/ )
             return 1;
          elif( ntg.rec.IdentProgram == 41/*IP_VEKSELACCOUNTED*/ )
             return 2;
          elif( ntg.rec.IdentProgram == 42/*IP_DV*/ )
             var SfContr = TRecHandler( "sfcontr.dbt" );
             if( (ntg.rec.ClientContrId > 0) and DL_FindSfContrByOrder(ntg.rec.ClientContrId, SfContr) and (SfContr.rec.ServKind == PTSK_CM) )
                return 6;
             else
             return 3;
             end;
          elif( ntg.rec.IdentProgram == 45/*IP_TRUST*/ )
             return 5;
          end;
        elif ( Classificator == 5023 )/*PNV*/
                    return 2; /*Драгоценные металлы*/
       /*PNV*/
        elif( (ObjectID == OBJTYPE_CODE_FI) AND(Classificator == LLCLASS_CODE_FI) )
            var FIID:integer = GetBaseFIID();
            return ExRate_GetParameter_CodeFI(FIID);
        elif( (ObjectID == 1122) AND(Classificator == 181) )
            return 2; /*Драгоценные металлы*/
        elif ( (ObjectID == OBJTYPE_MONEYSOURCE) AND (Classificator == LLCLASS_MONEYSOURCE) )      
            return ALG_SP_MONEY_SOURCE_OWN; 
     /*PNV*/
        end;               

        return -1;
    END;

    MACRO GetBasisFIRole(FIRole)
        VAR i = 0;

        if (FIRole == FIROLE_UNDEF)
            /*Вернем значение по умолчанию*/
            return FIROLE_FIDOC;
        end;

        /*Если роль присутствует в базисе, вернем ее*/
        while (i < FIRoleBArray.Size)
            if (FIRoleBArray[i] == FIRole)
                return FIRole;
            end;
            i = i + 1;
        end;

        Error = 1;
        return FIROLE_UNDEF;
    END;

    MACRO GetFIRoleBArray()
        return FIRoleBArray;
    END;

    MACRO CorrectAccount(account, accblnc, ORScheme, categ, templ, accdoc,  OperDate)
        /*PNV 536514*/
        var v_FI = TRecHandler( "fininstr.dbt" );
        if ((categ.rec.code == "+Расчеты") or (categ.rec.code == "-Расчеты")) 
             if (categ.rec.code == "+Расчеты")
                  Account.rec.NameAccount = "Требования по неттингу ДМ";
             elif (categ.rec.code == "-Расчеты")
                  Account.rec.NameAccount = "Обязательства по неттингу ДМ";
             end;
             if( ПолучитьФинИн( accdoc.rec.currency, v_FI ) == 0 )
                 Account.rec.NameAccount = Account.rec.NameAccount + " (" + v_FI.rec.name + " )";
             end;
             Account.rec.NameAccount = Account.rec.NameAccount + " с " + GetPartyNames(ntg.rec.contractor, 1)[0];
        end;
        /*PNV 536514*/
        return true;
    END;

    MACRO GetAccount
        ( 
            CatCode,       /* код категории */
            /*необязательные параметры*/
            Account,       /* здесь вернется номер счета */
            AccCreate,     /* признак верификации\открытия лицевого счета       */
            Currency,      /* Валюта счета */
            ActionDate,    /* Дата действия*/ 
            AccBuf,         /* буфер для найденного\открытого счета (account.dbt)*/
            NoSayError,
            FIRole,
            PAIRACCMODE   /*TEG не обязательный параметр по умолчанию NULL*/
        )
    var fiid_old = ParmA[MC_TYPE_PARAMETR_FIID];
    var pairacc;
     if (ValType(PAIRACCMODE) == V_UNDEF)
        pairacc=null;
     else
        pairacc=PAIRACCMODE;
     end;   

        if (Currency != ParmA[MC_TYPE_PARAMETR_FIID])
            ParmA[MC_TYPE_PARAMETR_FIID] = Currency;
        end;

        if(AccCreate != 4)
          Account = MC_FindAndOpenAccountEx ( CatCode, this, ActionDate, 0, AccCreate, AccBuf, Currency, NULL, pairacc, NULL, FIRole );

        else
          Account = MC_GetAccountNumber( CatCode,
                                         this,
                                         ActionDate,
                                         0, Currency, FIRole
                                       );
        end;

        if(Account == "")
            if( NoSayError != true )
               msgbox("Ошибка в получении счета:|категория: ", CatCode);
            end;
            return false;
        end;

        if (fiid_old != ParmA[MC_TYPE_PARAMETR_FIID])
            ParmA[MC_TYPE_PARAMETR_FIID] = fiid_old;
        end;

        SetParm( 2, Account );

        return true;
    END;

    /*открытие и актуализация счета*/  
    MACRO OpenAccount( CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts, McAccDoc );
       var ret, acnt;      
       ret = GetAccount( CatCode, acnt, MC_OPENACC_CREATE, FIID, ActionDate, AccBuf, NoErrMes, FIRole );
       setparm( 2, acnt ); 
       return ret;
    END;  

    /*получить - найти или сформировать номер счета (без открытия)*/  
    MACRO FindNumberAccount( CatCode, FindAccount, NoErrMes, FIRole, FIID, Accounts );
       var ret, acnt;
           
       ret = GetAccount( CatCode, acnt, 4, FIID, null, null, NoErrMes, FIRole );
       setparm( 2, acnt ); 
       return ret;
    END;  



    MACRO ПолучитьСчетаДоходовИРасходов( Chapter, СчетДоходов, СчетРасходов ) 
       var КатСчетДоходов, КатСчетРасходов;                                   
       /* Счета доходов\расходов от курсовых разниц*/  
       if( Chapter == 1 )
          КатСчетДоходов  = "+БМаржаП,ср-ва в ин.вал."; 
          КатСчетРасходов = "-БМаржаП,ср-ва в ин.вал.";
       else
          return false;
       end;

       if( (not this.GetAccount( КатСчетДоходов, СчетДоходов, MC_OPENACC_CREATE ) ) OR
           (not this.GetAccount( КатСчетРасходов, СчетРасходов, MC_OPENACC_CREATE) ) 
       )
          return false;
       end;       
       SetParm( 2, СчетДоходов   );
       SetParm( 3, СчетРасходов  );
       return true;
    END;

    MACRO GetDealTypeName()
      var cmd, DataSet;
 
      cmd = DL_RSDCommand("select t_Name from doprkoper_dbt where t_Kind_Operation = ? ");
      cmd.AddParam(ntg.rec.kind_operation);
      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        return DataSet.Name;
      end;
 
      return "";
    END;

    macro ПолучитьКалендВалютуВидаДаты():integer
      var idOpr,idStep,docKind,docId;
      if (not DL_CheckCallFromStep())
        return -2;
      else
        DL_GetCalendCurOperStepData(docKind, idOpr, idStep, docId);
        var oprstep = TBFile("oprstep.dbt");
        var fltr = "T_ID_OPERATION = " +idOpr
           + " AND T_ID_STEP = " + idStep;
        oprstep.AddFilter(fltr);
        if (oprstep.Next() and (oprstep.rec.Kind_Action == 100))
          return GetBaseFIID();
        end;
      end;
      return -1;
    end;
  
    macro ПолучитьКалендКонтрагент()
      return  this.GetParametr( MC_TYPE_PARAMETR_CONTRACTOR);
    end;
  
    macro ПолучитьКалендТип():integer
      return DL_CALLNK_OUTMARKET;
    end;
  
    macro ПолучитьКалендСвязанный():integer
      return DL_GetCalendByDynParam(NULL,makeArray(
         DL_KeyPair("Object",GetDealTypeName()),
         DL_KeyPair("ObjectType",ПолучитьКалендТип())
         )
      );
    end;

    /* Start методы для внутреннего учета */
    macro ВУ_ПолучитьРО( ВидРО )
      if( (ВидРО == 77001) OR (ВидРО == 77002) )
         return 77;
      elif( (ВидРО == 76001) OR (ВидРО == 76002) )
         return 76;
      elif( (ВидРО == 46001) OR (ВидРО == 46002) )
         return 46;
      elif( (ВидРО == 45001) OR (ВидРО == 45002) )
         return 45;
      end;
      return ВидРО;
    end;
    
    macro ВУ_СкорректироватьРесчетнуюОперацию( ВидРО, InnAcc:@VARIANT )
      return 0;
    end;

    macro ВУ_ПолучитьРольФИ( ВидРО, DebCred, ReturnIncomeKind_ )
      var FIRole = FIROLE_FIDOC;

      return FIRole;
    end;

    macro ВУ_ПолучитьКатегориюДебет( ВидРО, ReturnIncomeKind_ )
      var Категория = "";

      if( ВидРО == 77001 )
         Категория = "ЦБ Банка, ВУ";
      elif( ВидРО == 76002 )
         Категория = "ЦБ, Расч. с клиентом, ВУ";
      elif( ВидРО == 76001 )
         Категория = "ЦБ, Прч. счета банка, ВУ";
      elif( ВидРО == 77002 )
         Категория = "ЦБ Клиента, ВУ";
      elif( ВидРО == 46001 )
         Категория = "ДС Банка, ВУ";
      elif( ВидРО == 45002 )
         Категория = "ДС, Расч. с клиентом, ВУ";
      elif( ВидРО == 45001 )
         Категория = "ДС, Прч. счета банка, ВУ";
      elif( ВидРО == 46002 )
         Категория = "ДС Клиента, ВУ";
      end;

      return Категория;
    end;

    macro ВУ_ПолучитьКатегориюКредит( ВидРО, ReturnIncomeKind_ )
      var Категория = "";

      if( ВидРО == 76001 )
         Категория = "ЦБ Банка, ВУ";
      elif( ВидРО == 77002 )
         Категория = "ЦБ, Расч. с клиентом, ВУ";
      elif( ВидРО == 77001 )
         Категория = "ЦБ, Прч. счета банка, ВУ";
      elif( ВидРО == 76002 )
         Категория = "ЦБ Клиента, ВУ";
      elif( ВидРО == 45001 )
         Категория = "ДС Банка, ВУ";
      elif( ВидРО == 46002 )
         Категория = "ДС, Расч. с клиентом, ВУ";
      elif( ВидРО == 46001 )
         Категория = "ДС, Прч. счета банка, ВУ";
      elif( ВидРО == 45002 )
         Категория = "ДС Клиента, ВУ";
      end;

      return Категория;
    end;

    macro ВУ_ОснованиеПроводки( ВидРО )
      var Ground = "";

      if( ntg.rec.IdentProgram == 14/*IP_SEC*/ )
        if( (ВидРО == 77001) OR (ВидРО == 77002) )
           Ground = "Получение итогового ТО неттинга по ц/б по операции №" + ntg.rec.DealNumber;
        elif( (ВидРО == 76001) OR (ВидРО == 76002) )
           Ground = "Оплата итогового ТО неттинга по ц/б по операции №" + ntg.rec.DealNumber;
        elif( (ВидРО == 46001) OR (ВидРО == 46002) )
           Ground = "Получение итогового ТО неттинга по денежным средствам по операции №" + ntg.rec.DealNumber;
        elif( (ВидРО == 45001) OR (ВидРО == 45002) )
           Ground = "Оплата итогового ТО неттинга по денежным средствам по операции №" + ntg.rec.DealNumber;
        end;
      else
        if( (ВидРО == 77001) OR (ВидРО == 77002) )
           Ground = "Получение итогового платежа неттинга по ц/б по операции №" + ntg.rec.DealNumber;
        elif( (ВидРО == 76001) OR (ВидРО == 76002) )
           Ground = "Оплата итогового платежа неттинга по ц/б по операции №" + ntg.rec.DealNumber;
        elif( (ВидРО == 46001) OR (ВидРО == 46002) )
           Ground = "Получение итогового платежа неттинга по денежным средствам по операции №" + ntg.rec.DealNumber;
        elif( (ВидРО == 45001) OR (ВидРО == 45002) )
           Ground = "Оплата итогового платежа неттинга по денежным средствам по операции №" + ntg.rec.DealNumber;
        end;
      end;

      return Ground;
    end;

    private VAR ChapterFIID = TArray();
    macro ВУ_ОпределениеГлавы( FIID:INTEGER ):INTEGER
       VAR v_fininstr;

       if( ChapterFIID[FIID] == NULL )
          v_fininstr = Trechandler( "fininstr.dbt" ); /* фин. инструмент           */

          if( ПолучитьФинИн( FIID, v_fininstr ) != 0 ) 
             this.SetError( "Не найден ФИ при определении главы ВУ (FIID = " + FIID + ")", false );
          else
             if( v_fininstr.rec.FI_Kind == FIKIND_CURRENCY )
                ChapterFIID[FIID] = 21;
             else
                ChapterFIID[FIID] = 22;
             end;
          end;
       end;
       return ChapterFIID[FIID];
    end;
    /* End методы для внутреннего учета */

    /*-------------  Конструктор  ------------------*/

    if ((ValType(parm1)==V_INTEGER) AND (ValType(parm2)==V_INTEGER) AND ((parm1==DL_NTGDOC) OR (parm1==DL_NTGSEC)))
        /*Конструктор из DocKind, DocID*/
        ntg.rec.NettingID = parm2;
        if (not ntg.GetEQ)
            RunError ("не найден документ неттнига с идентификатором " + parm2);
        end;
    elif ((ValType(parm1)==V_INTEGER) AND (ValType(parm2)==24) AND ((parm1==DL_NTGDOC) OR (parm1==DL_NTGSEC)))
        /*Конструктор из DocKind, DocBuf*/
        SetBuff(ntg_buff, parm2);
    elif (copy (ntg,parm1))
        /*Конструктор из структур*/
    else
        RunError ("неверный вызов конструктора");
    end;

    InitParmArray ();
    Kind = ParmA[MC_TYPE_PARAMETR_DOCKIND];
    Id = ParmA[MC_TYPE_PARAMETR_DOCID];
END;
