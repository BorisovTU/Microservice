/*
$Name:         scnt10.mac
$Module:       Неттинг
$Description:  макрос шага "Неттинг"
*/
IMPORT DealsInter, PaymInter, RsbDataSet, dlntlib, dlntfd, "dlcarry.inc", dlntpm, "sp_class.mac","sp_car.mac";

PRIVATE VAR 
    Параметры               = NULL, 
    СписокТребОбяз          = NULL,
    ПлатежиВНеттинге        = TArray,
    ТОВНеттинге             = TArray,
    СуммыПлатежейВНеттинге  = TArray,
    СуммыТОВНеттинге        = TArray,
    ВалютыПлатежейВНеттинге = TArray,
    ВалютыТОВНеттинге       = TArray,
    ИтоговыеТО              = TArray,
    Err                     = NULL;

PRIVATE CLASS ЭлемТрОб(ФИ_п)
  VAR 
      ФИ             = ФИ_п,
      Стр            = $0, /*базовая сумма требования*/
      Соб            = $0, /*бюазовая сумма обязательства*/
      СчетМРасчеты   = "",  /* счет по категории "-Расчеты"  */
      СчетПРасчеты   = "",  /* счет по категории "+Расчеты"  */
      СчетМРасчетыВО = "",  /* счет по категории "-Расчеты" в валюте оплаты (ВО) */
      СчетПРасчетыВО = "";  /* счет по категории "+Расчеты" в валюте оплаты (ВО)   */
END;

PRIVATE CLASS (TArray) КлассСпискаТрОб 
  MACRO ДобавитьЕслиЕщеНет( fiid )
     VAR i = 0;

     while( i < this.size )
        if( this[i].ФИ == fiid )
           return i;
        end;
        i = i + 1;
     end;
     this[this.size] = ЭлемТрОб(fiid);

     return i;
  END;

  MACRO ПолучитьИД( fiid )
     VAR i = 0;
     
     while( i < this.size )
        if( this[i].ФИ == fiid )
           return i;
        end;
        i = i + 1;
     end;
     return NULL;
  END;

  MACRO Получить( fiid )
     VAR id = ПолучитьИД(fiid);
     if( id != NULL )
        return this[id];
     end;
     return NULL;
  END;
END;

PRIVATE CLASS local_ПараметрыИсполнения
  VAR ОтборПлатежейПоУбыванию = false;
END;

PRIVATE MACRO ОпределитьДопПараметры()
    VAR stat = 0, retVal = false;

    Параметры.ПользовательскиеПараметры = local_ПараметрыИсполнения;

    GetRegistryValue("COMMON\\НЕТТИНГ\\Отбор платежей по убыванию", V_BOOL, retVal, stat);
    if (stat)
        retVal = false;
    end;

    Параметры.ПользовательскиеПараметры.ОтборПлатежейПоУбыванию = retVal;
END;

PRIVATE MACRO НайтиРезультирТО(fiid)
  VAR idx = 0, 
      cnt = ИтоговыеТО.size;
  var rq = TRecHandler("dlrq");

  while( idx < cnt )
     rq = ИтоговыеТО[idx];

     if( rq.rec.FIID == fiid )
        return rq;
     end;

     idx = idx + 1;
  end;

  return NULL;
END;

PRIVATE MACRO ДобавитьИтоговоеТО(rq:object)
  var i = ИтоговыеТО.size;
  ИтоговыеТО[i] = TRecHandler("dlrq");
  copy(ИтоговыеТО[i], rq);
  return true;
END;

PRIVATE MACRO ПроверитьИтоговыеТО(ntg)
  VAR idx = 0, cnt = СписокТребОбяз.size;
  var rq = TRecHandler("dlrq");

  if( not ForEachRQRes(ntg.NettingID, @ДобавитьИтоговоеТО, NULL) )
     Err.Добавить(NTG_RQERR_NOFNLRQ);
     return false;
  else
     while( idx < cnt )
        rq = НайтиРезультирТО(СписокТребОбяз[idx].ФИ);

        if( rq )
            /*Частный случай нулевого ТО - проверки не делаем*/
            if( rq.rec.Amount == 0) 
               idx = idx + 1; 
               continue;
            end;

            if( СписокТребОбяз[idx].Стр == СписокТребОбяз[idx].Соб )
               Err.Добавить(NTG_RQERR_CNTFNLRQ, string(СписокТребОбяз[idx].ФИ));
               return false;               
            elif( СписокТребОбяз[idx].Стр < СписокТребОбяз[idx].Соб )
               if( (rq.rec.Kind == DLRQ_KIND_REQUEST) or 
                   (rq.rec.Amount != (СписокТребОбяз[idx].Соб - СписокТребОбяз[idx].Стр))
                 )
                  Err.Добавить(NTG_RQERR_BADFNLRQ, ПолучитьКодФинИн(СписокТребОбяз[idx].ФИ));
                  return false;
               end; 
            else
               if( (rq.rec.Kind == DLRQ_KIND_COMMIT) or 
                   (rq.rec.Amount != (СписокТребОбяз[idx].Стр - СписокТребОбяз[idx].Соб))
                 )
                  Err.Добавить(NTG_RQERR_BADFNLRQ, ПолучитьКодФинИн(СписокТребОбяз[idx].ФИ));
                  return false;
               end; 
            end;
        else
            if( (СписокТребОбяз[idx].Стр - СписокТребОбяз[idx].Соб) != 0 )
               Err.Добавить(NTG_RQERR_NOFNLRQ);
               return false;
            end;
        end;

        idx = idx + 1; 
     end;
  end;

  return true;
END;

PRIVATE MACRO ДобавитьПлатежВалюту( pml, ntg )

  VAR pmobj = NULL, idx = 0, FD = NULL, DocKind, error = 0;

  pmobj = ПлатежиВНеттинге[ПлатежиВНеттинге.size] = CNtgPayment(ntg.IdentProgram, pml.InitialPayment);

  if( pmobj.PaymentID == 0 )
     ПлатежиВНеттинге.size = ПлатежиВНеттинге.size -1;
  else
     error = NGT_ВалидацияПлатежаНеттинга(pmobj, ntg);

     if( error != 0 )
        Err.Добавить(error);
        return false;
     end;

     idx = СписокТребОбяз.ДобавитьЕслиЕщеНет(pml.fiid);

     if( PaymIsDemand(pmobj, ntg) == "" )
        СписокТребОбяз[idx].Соб = СписокТребОбяз[idx].Соб + pmobj.BaseAmount;
     else
        СписокТребОбяз[idx].Стр = СписокТребОбяз[idx].Стр + pmobj.BaseAmount;
     end;

     if( pmobj.PaymStatus != PM_CLOSED_W_M_MOVEMENT )
        pmobj.PaymStatus = PM_CLOSED_W_M_MOVEMENT;
     end;

     if( (pmobj.PayerAmount == $0) or 
         (pmobj.ReceiverAmount == $0)
       )
        pmobj.BaseRate.Actuate(ntg.ValueDate, false);
        pmobj.FactRate.Actuate(ntg.ValueDate, false);

        error = 0;
        error = pmobj.Actuate(); 
        if( error == 545 )
           Err.Добавить(NTG_ERR_BADBASERATE);
           return false;
        elif( error )
           Err.Добавить(NTG_ERR_BADCONVERT);
           return false;
        end;
     end;

     if( not Параметры.НеттингЧерезТранзитныеСчета )
        if( PaymIsDemand(pmobj, ntg) == "" )
           СуммыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]  = pmobj.PayerAmount;
           ВалютыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1] = pmobj.PayerFIID;
        else
           СуммыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]  = pmobj.ReceiverAmount;
           ВалютыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1] = pmobj.ReceiverFIID;
        end;
     end;
  end;

  return true;
END;

PRIVATE MACRO ДобавитьТОВалюту(pml, ntg)
  VAR idx = 0,
      FD = NULL, DocKind, error = 0;
  var rq = TRecHandler("dlrq");

  NGT_GetRQ(pml.InitialPayment, rq);

  if( rq.rec.ID )

     ТОВНеттинге[ТОВНеттинге.size] = rq;

     error = NGT_ВалидацияТОНеттинга(rq, ntg, Параметры);

     if( error != 0 )
        Err.Добавить(error);
        return false;
     end;

     idx = СписокТребОбяз.ДобавитьЕслиЕщеНет(pml.fiid);

     if( rq.rec.Kind == DLRQ_KIND_COMMIT )
        СписокТребОбяз[idx].Соб = СписокТребОбяз[idx].Соб + rq.rec.Amount;
     else
        СписокТребОбяз[idx].Стр = СписокТребОбяз[idx].Стр + rq.rec.Amount;
     end;

     if( (Параметры.БэкОфис == "S")and 
         (rq.rec.DocKind == DL_SECURITYDOC)or(rq.rec.DocKind == DL_RETIREMENT)
       )
        if( rq.rec.State != DLRQ_STATE_EXEC )
           rq.rec.State    = DLRQ_STATE_EXEC;
           rq.rec.FactDate = rq.rec.PlanDate;
        else
           if( rq.rec.DealPart == 2 )
              DocKind = LEG_KIND_DL_TICK_BACK;
           else
              DocKind = LEG_KIND_DL_TICK;
           end;

           FD = SPFirstDoc(DocKind, rq.rec.DocID);

           if( not ExecMacroFile("sp_car.mac", "ОбновитьФлагиЧастиСделки", FD.dl_leg, DL_LEG_NETTING_EXECUTE) )
              Err.Добавить(NTG_RQERR_UPDDLFLAG);
              return false; 
           end;
        end;
     end;

     if( not Параметры.НеттингЧерезТранзитныеСчета )
       СуммыТОВНеттинге[ТОВНеттинге.size - 1]  = rq.rec.Amount;
       ВалютыТОВНеттинге[ТОВНеттинге.size - 1] = rq.rec.FIID;
     end;
  end;

  return true;
END;

PRIVATE MACRO ОпределитьНаборВалютТО( ntg ):bool
  var RetVal:bool = true;

  RetVal = ForEachPMLinkRQ(DL_NTGSEC, ntg.NettingID, @ДобавитьТОВалюту, ntg);

  if( RetVal )
     RetVal = ForEachPMLink(DL_NTGSEC, ntg.NettingID, @ДобавитьПлатежВалюту, ntg);
  end;

  return RetVal;
END;

PRIVATE MACRO НайтиМаксМинСуммуТО( fiid, minsum, maxsum, rqkind, ntg )
  VAR idx = 0, rq = NULL, pmobj = NULL,
      cnt = ТОВНеттинге.size;

  minsum = $0;
  maxsum = $0;

  while( idx < cnt )
     rq = ТОВНеттинге[idx];

     if( (rq.rec.Kind == rqkind) and (rq.rec.FIID == fiid) )
        if( (minsum > rq.rec.Amount) or (minsum == 0) )
           minsum = rq.rec.Amount;
        end;

        if( maxsum < rq.rec.Amount )
           maxsum = rq.rec.Amount;
        end;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = ПлатежиВНеттинге.size;

  while( idx < cnt )
     pmobj = ПлатежиВНеттинге[idx];
     if( (PaymIsDemand(pmobj, ntg) == IIF(rqkind == DLRQ_KIND_REQUEST, "X", "")) and (pmobj.BaseFIID == fiid) )
        if( (minsum > pmobj.BaseAmount) or (minsum == 0) )
           minsum = pmobj.BaseAmount;
        end;

        if( maxsum < pmobj.BaseAmount )
           maxsum = pmobj.BaseAmount;
        end;
     end;

     idx = idx + 1;
  end;

  SetParm(1, minsum);
  SetParm(2, maxsum);
END;

PRIVATE MACRO ПроверитьНеснеттингованныеТО( ntg )

  VAR idx = 0, cnt = СписокТребОбяз.size,
      СуммаРезТО = $0,
      МинСумма, МаксСумма;

  while( idx < cnt )
     СуммаРезТО = СписокТребОбяз[idx].Стр - СписокТребОбяз[idx].Соб;

     if( СуммаРезТО > 0 )
        НайтиМаксМинСуммуТО(СписокТребОбяз[idx].ФИ, МинСумма, МаксСумма, DLRQ_KIND_REQUEST, ntg);
     else
        НайтиМаксМинСуммуТО(СписокТребОбяз[idx].ФИ, МинСумма, МаксСумма, DLRQ_KIND_COMMIT, ntg);
     end;

     СуммаРезТО = abs(СуммаРезТО);

     if( Параметры.ПользовательскиеПараметры.ОтборПлатежейПоУбыванию )
        if( (СуммаРезТО > МинСумма) and (ntg.IncludeOnewayPayment != "X") )
           Err.Добавить(NTG_RQERR_ONEWAYRQ, ПолучитьКодФинИн(СписокТребОбяз[idx].ФИ));
           return false;
        end;
     else
        if( (СуммаРезТО > МаксСумма) and (ntg.IncludeOnewayPayment != "X") )
           Err.Добавить(NTG_RQERR_ONEWAYRQ, ПолучитьКодФинИн(СписокТребОбяз[idx].ФИ));
           return false;
        end;
     end;

     idx = idx + 1; 
  end;

  return true;
END;

PRIVATE MACRO ЗаполнитьУточняющиеЗаписи( rq, ntg, FinalDealNumber )
  VAR idx = 0, cnt = СуммыТОВНеттинге.size,
      DealCode = "",
      rqkind = rq.rec.Kind, dlrq = NULL, pm = NULL;

  while( idx < cnt )
     dlrq = ТОВНеттинге[idx];

     if( (СуммыТОВНеттинге[idx] > 0) and
         (dlrq.rec.Kind == rqkind) and
         (dlrq.rec.FIID == rq.rec.FIID)
       )
        DealCode = NGT_ПолучитьНомерСделки(dlrq.rec.DocID, dlrq.rec.DocKind);

        if( FinalDealNumber == "" )
           FinalDealNumber = DealCode;
        else
           FinalDealNumber = FinalDealNumber + ", " + DealCode;
        end;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = СуммыПлатежейВНеттинге.size;

  while( idx < cnt )
     pm = ПлатежиВНеттинге[idx];

     if( (СуммыПлатежейВНеттинге[idx] > 0) and
         (PaymIsDemand(pm, ntg) == IIF(rqkind == DLRQ_KIND_REQUEST, "X", "")) and
         (pm.BaseFIID == rq.rec.FIID)
       )
        DealCode = NGT_ПолучитьНомерСделки(pm.DocumentID, pm.DocKind);

        if( FinalDealNumber == "" )
           FinalDealNumber = DealCode;
        else
           FinalDealNumber = FinalDealNumber + ", " + DealCode;
        end;
     end;

     idx = idx + 1;
  end;

  SetParm(2, FinalDealNumber);

  return true;
END;

PRIVATE MACRO ИнициализироватьРезультирТО( ntg, ista, FinalDealNumber, ID_Operation, ID_Step )
  VAR idx = 0, rq = NULL,
      cnt = ИтоговыеТО.size;

  FinalDealNumber = "";

  while( idx < cnt )
     rq = ИтоговыеТО[idx];

     rq.rec.State = DLRQ_STATE_PLAN;

     if( DL_ChangeDLRQ(rq, ntg.ValueDate, DLRQ_ACTION_UPDATE) )
        Err.Добавить(NTG_RQERR_UPDRQ);
        return false; 
     end;

     if( (not ista) and (not ЗаполнитьУточняющиеЗаписи(rq, ntg, FinalDealNumber)) )
        return false;
     end;

     idx = idx + 1;
  end;

  SetParm(2, FinalDealNumber);

  return true;
END;

private macro ВставитьДействияПоГрафику(rq, ntg)
    var requestTimeout = 0;
    var ОжиданиеЗапроса = false;
    var repozdeal = TBFile("dl_repozdeal.dbt", "R", 1);
    repozdeal.Clear();
    repozdeal.rec.DocKind = rq.rec.DocKind;
    repozdeal.rec.DocID   = rq.rec.DocID;
//    if( repozdeal.GetEQ() )
    if( GetEQ(repozdeal) )
      if( (repozdeal.rec.INITIATOR == 2) and    (repozdeal.rec.METHOD_WAYTWOWAY == 1))
        ОжиданиеЗапроса = true;
        requestTimeout = repozdeal.rec.requestTimeout;
      end;

      if(repozdeal.rec.reporting == UNSET_CHAR)  //если флаг репортинг в репозитарий не установлен
        return 0;
      end;
    else
      return 0;  // если не нашли репозитарные параметры, то действия не вставляем
    end;

    var SelectClose, DataSetClose, QueryClose;
    var cnt = 0;
    QueryClose = "select * from ddlgrdeal_dbt where t_DocKind = ? and t_DocID = ? and t_TemplNum in(51,52) " ;
    SelectClose = DL_RSDCommand(QueryClose);
    SelectClose.AddParam(rq.rec.DocKind);
    SelectClose.AddParam(rq.rec.DocID);

    cnt = SelectClose.GetCount(QueryClose);     // если не запланировано закрытие договора, то ничего не вставляем
    if( cnt == 0 )
      return 0;
    end;
    var Select, DataSet, Query;
    //DLRQ
    Query = "SELECT * FROM ddlrq_dbt"
          + " WHERE  t_DocKind = ?"
          + " AND t_DocID = ? "
          + " AND t_dealPart = 2 "
          + " AND t_type IN (2, 8) "
          + " AND t_state <>  " + DLRQ_STATE_EXEC       // ищем все неисполненные     
          ;


    Select = DL_RSDCommand(Query);
    Select.AddParam(rq.rec.DocKind);
    Select.AddParam(rq.rec.DocID);
    Select.AddParam(rq.rec.DocKind);
    Select.AddParam(rq.rec.DocID);

    cnt = Select.GetCount(query);
    DataSet = Select.Execute();

    var CloseRQ = TRecHandler( "dlrq" );
    if( cnt == 1 )
      if(DataSet.moveNext())
        DataSet.GetRecord().CopyTo( CloseRQ.rec );
      end;
    else
      return 0;   // Сообщение о неттинге отправляется только в случае прекращения ВСЕХ обязательств по сделке
    end;

    if( CloseRQ.rec.ID == rq.rec.ID )
      var SelectGR, DataSetGR, QueryGR;
      QueryGR =  " SELECT grdeal.t_templnum AS templnum "                   
               + " FROM ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc "        
               + " WHERE     grdeal.t_dockind = ? "                      
                   + "  AND grdeal.t_docid = ? "                        
                   + "  AND grdeal.t_templnum IN (51, 52, 55, 56, 57, 58) "
                   + "  AND gracc.t_grdealid = grdeal.t_id "              
                   + "  AND gracc.t_accnum = 5 "                          
                   + "  AND gracc.t_state = 1 "                           
                   ;

      SelectGR = DL_RSDCommand(QueryGR);
      SelectGR.AddParam(rq.rec.DocKind);
      SelectGR.AddParam(rq.rec.DocID);
      
      cnt = SelectGR.GetCount(queryGR);
      DataSetGR = SelectGR.Execute();
      while(DataSetGR.moveNext())
        if( DL_UpdateGrDealAcc( rq.rec.DocKind, rq.rec.DocID, rq.rec.FIID, DataSetGR.Templnum, DLGR_ACCKIND_REPOSITORY, date(0,0,0), DLGRACC_STATE_NOTNEED ) != 0)
          return 1;
        end;
      end;
      if( ОжиданиеЗапроса and requestTimeout)
        if(DL_InsertGrDeal( rq.rec.DocKind, rq.rec.DocID, DLGR_TEMPL_NETTINGSUSPWTHREQUEST, GetDateAfterWorkDays(ntg.ValueDate, requestTimeout) , time(), rq.rec.FIID) != 0 )
          return 1;
        end;
      else
        if(DL_InsertGrDeal( rq.rec.DocKind, rq.rec.DocID, DLGR_TEMPL_NETTINGSUSP, ntg.ValueDate, time(), rq.rec.FIID) != 0 )
          return false;
        end;
      end;
    end;
end;

private macro ВставитьДействияПоГрафикуВнебиржи(rq, ntg, tick)
  var requestTimeout = 0, BOfficeKind = 0, DealID = 0, FIID = -1;
  var ОжиданиеЗапроса = false;
  if( ПРОВЕРКА_РЕПОЗИТАРНЫХ_ПАРАМЕТРОВ( tick, @ОжиданиеЗапроса, @requestTimeout, @BOfficeKind, @DealID, @FIID) and (IsCloseContr(BOfficeKind, DealID)) )
    var Select, DataSet, Query;
    //DLRQ
    Query = "SELECT * FROM ddlrq_dbt"
          + " WHERE  t_DocKind = ?"
          + " AND t_DocID = ? "
          + " AND t_type IN (2, 8) "
          + " AND t_state <>  " + DLRQ_STATE_EXEC       // ищем все неисполненные
          ;

    Select = DL_RSDCommand(Query);
    Select.AddParam(rq.rec.DocKind);
    Select.AddParam(rq.rec.DocID);

    var cnt = Select.GetCount(query);
    DataSet = Select.Execute();

    var CloseRQ = TRecHandler( "dlrq" );
    if( cnt == 1 )
      if(DataSet.moveNext())
        DataSet.GetRecord().CopyTo( CloseRQ.rec );
      end;
    else
      return 0;   // Сообщение о неттинге отправляется только в случае прекращения ВСЕХ обязательств по сделке
    end;

    if( CloseRQ.rec.ID == rq.rec.ID )
      var SelectGR, DataSetGR, QueryGR;
      QueryGR =  " SELECT grdeal.t_templnum AS templnum "                  
               + " FROM ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc "         
               + " WHERE     grdeal.t_dockind = ? "                        
                   + "  AND grdeal.t_docid = ? "                           
                   + "  AND grdeal.t_templnum IN (51, 52, 55, 56, 57, 58) "
                   + "  AND gracc.t_grdealid = grdeal.t_id "               
                   + "  AND gracc.t_accnum = 5 "                           
                   + "  AND gracc.t_state = 1 "                            
                   ;

      SelectGR = DL_RSDCommand(QueryGR);
      SelectGR.AddParam(BOfficeKind);
      SelectGR.AddParam(DealID);

      cnt = SelectGR.GetCount(queryGR);
      DataSetGR = SelectGR.Execute();
      
      while(DataSetGR.moveNext())
        if( ОжиданиеЗапроса and requestTimeout)
          if(DL_InsertGrDeal( BOfficeKind, DealID, DLGR_TEMPL_NETTINGSUSPWTHREQUEST, GetDateAfterWorkDays(ntg.ValueDate, requestTimeout) , time(), FIID) != 0 )
            return 1;
          end;
          if( DL_UpdateGrDealAcc( BOfficeKind, DealID, FIID, DLGR_TEMPL_CLOSECONTRWTHREQUEST, DLGR_ACCKIND_REPOSITORY, date(0,0,0), DLGRACC_STATE_NOTNEED ) != 0)
            return 1;
          end;
        else
          if(DL_InsertGrDeal( BOfficeKind, DealID, DLGR_TEMPL_NETTINGSUSP, ntg.ValueDate, time(), FIID) != 0 )
            return 1;
          end;
          if( DL_UpdateGrDealAcc( BOfficeKind, DealID, FIID, DLGR_TEMPL_CLOSECONTR, DLGR_ACCKIND_REPOSITORY, date(0,0,0), DLGRACC_STATE_NOTNEED ) != 0)
            return 1;
          end;
        end;
      end;
    end;
  end;
end;

PRIVATE MACRO ОбратотатьТОдляНеттинга( ntg, ID_Operation, ID_Step )
  var idx = 0, cnt = ТОВНеттинге.size;
  var rq = null;

  while( idx < cnt )
    rq = ТОВНеттинге[idx];

    rq.rec.State    = DLRQ_STATE_EXEC;
    rq.rec.FactDate = rq.rec.PlanDate;
    rq.rec.Netting  = SET_CHAR;
    if( DL_ChangeDLRQ(rq, ntg.ValueDate, DLRQ_ACTION_UPDATE) )
       Err.Добавить(NTG_RQERR_UPDRQ);
       return false; 
    end;

    if( ntg.FI_Kind == FIKIND_AVOIRISS )
       if( NGT_ОбновитьГрафикПоТО(rq) != 0 )
          Err.Добавить(NTG_RQERR_GRDEAL);
          return false;
       end;
    end;
//////
    var Tick = TBfile("dl_tick.dbt");
    var Group;
    Tick.Clear();
    Tick.rec.DealID = rq.rec.DocID;
    if( Tick.GetEQ() )
      Group = SP_GetOperationGroup( Tick.rec ); 
    end;

    if( (РАБОТА_С_РЕПОЗИТАРИЕМ() and (rq.rec.Type == DLRQ_TYPE_DELIVERY)) and IsREPO( Group )) // если сделка РЕПО
      if(ВставитьДействияПоГрафику(rq, ntg ) )
        return false;
      end;
    elif ( ((IsOUTEXCHANGE(Group)) or (IsBroker(Group)))  ) // если внебиржевая сделка
      if( ВставитьДействияПоГрафикуВнебиржи(rq, ntg, Tick ) )
        return false;
      end;
    end;
////////
     idx = idx + 1;
  end;

  return true;
END;

MACRO ИсполнитьНеттингТО( ntg_buff, ID_Operation, ID_Step )
  VAR FinalDealNumber = "";

  Err = STEP_10_ERROR;

  Параметры = ПараметрыИсполнения(false, ntg_buff);
  ОпределитьДопПараметры();

  СписокТребОбяз = КлассСпискаТрОб;

  СписокТребОбяз.size          = 0;
  ПлатежиВНеттинге.size        = 0;
  ТОВНеттинге.size             = 0;
  СуммыПлатежейВНеттинге.size  = 0;
  СуммыТОВНеттинге.size        = 0;
  ВалютыПлатежейВНеттинге.size = 0;
  ВалютыТОВНеттинге.size       = 0;
  ИтоговыеТО.size              = 0;

  if( ОпределитьНаборВалютТО(ntg_buff) and
      ПроверитьИтоговыеТО(ntg_buff)
    )
     if (Параметры.НеттингЧерезТранзитныеСчета)
        if( ИнициализироватьРезультирТО(ntg_buff, true, FinalDealNumber, ID_Operation, ID_Step) and
            ОбратотатьТОдляНеттинга(ntg_buff, ID_Operation, ID_Step)
          )
           /*Без ошибок*/
        end;
     else
        if( ПроверитьНеснеттингованныеТО(ntg_buff) and
            ИнициализироватьРезультирТО(ntg_buff, false, FinalDealNumber, ID_Operation, ID_Step) and
            ОбратотатьТОдляНеттинга(ntg_buff, ID_Operation, ID_Step)
          )
           /*Без ошибок*/
        end;
     end;

     if( (FinalDealNumber != "") and (readNoteForObject(OBJTYPE_NETTING, UniID(ntg_buff, OBJTYPE_NETTING), 2, ntg_buff.SigningDate) == "") )
        addNoteForObject(OBJTYPE_NETTING, UniID(ntg_buff, OBJTYPE_NETTING), 2, FinalDealNumber, ntg_buff.SigningDate);
     end;
  end;
  
  if( Err.Вывести() )
     return 1;
  end;

  return 0;
END;

MACRO ExecuteStep(Buffer, Document, DocKind, ID_Operation, ID_Step)
  RECORD ntg_buff ("dl_nett");
  var stat = 0;

  SetBuff(ntg_buff, Document);

  stat = ИсполнитьНеттингТО(ntg_buff, ID_Operation, ID_Step);

  return stat;
END;
