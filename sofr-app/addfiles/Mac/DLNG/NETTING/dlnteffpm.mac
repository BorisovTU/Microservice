/*
$Name:        dlnteffpm.mac
$Module:      Неттинг
$Description: Общие функции при исполнении требований/обязательств
*/
IMPORT PaymInter, FIInter, RSD, dlntlib, dlntfd, dl_car, "dlcarry.inc"; 
/****************************************************************************************/

var ГлобПарам = NULL;

PRIVATE MACRO ПроводкаПоКВУ(ВидРО, ntg_fd, pmobj)
    if (not ПроводкаПоКатегориямВнутреннегоУчета(ВидРО, 
                                                 ntg_fd, 
                                                 NULL, 
                                                 pmobj.ValueDate, 
                                                 pmobj.PayerFIID,  
                                                 pmobj.PayerAmount
                                                )
       )
        return false;
    end;

    return true;
END;

MACRO NTG_GetPlanInAcc(DocKInd:integer, DocID:integer, PaymentID:integer, Дата:date, _CheckCatArr:TArray, inaccArr:@TArray)
  var err = 0;
  var FD = DL_NettingDoc(DocKInd, DocID);
  var pmobj = RsbPayment(PaymentID);
  var paym = TRecHandler("pmpaym.dbt"); 
  var pmisDemand = "";
  var ВидРО = 0;
          
  DL_SetPlanMode(true, _CheckCatArr);

  if(FD != NULL)
    
    Copy(paym, pmobj.GetPM_PAYM());

    pmisDemand = PaymIsDemand(pmobj, FD.dl_nett().rec);

    var Параметры = ПараметрыИсполнения(IIF(pmisDemand == UNSET_CHAR, true, false), FD.dl_nett().rec);

    if(FD.dl_nett().rec.FI_Kind == FIKIND_CURRENCY)
      if(Параметры.БэкОфис == "S")
        if(pmisDemand)
          ВидРО = 46001; /*Для клиентских нужно будет добавить 46002*/
        else
          ВидРО = 45001; /*Для клиентских нужно будет добавить 46002*/
        end;

        if(not ПроводкаПоКВУ(ВидРО, FD, pmobj))
          err = 1;
        end;
      end;
    elif(FD.dl_nett().rec.FI_Kind == FIKIND_AVOIRISS)
      if(pmisDemand)
        ВидРО = 77001; /*Для клиентских нужно будет добавить 77002*/
      else  
        ВидРО = 76001; /*Для клиентских нужно будет добавить 77002*/
      end;

      if(not ПроводкаПоКВУ(ВидРО, FD, pmobj))
        err = 1;
      end;
    end;
  end;

  inaccArr = TArray(DL_GetPlanInAccData());

  DL_SetPlanMode(false);

  return (err == 0);
END;

PRIVATE MACRO ИсполнениеТрОбДляЦенныхБумаг(Параметры, isDemand, ntg, error)
    VAR pmobj    = NULL,
        ВидРО    = 0,
        pmisDemand = "";

    if (ЕстьРезПлатежПоЦеннымБумагам(ntg.IdentProgram, ntg.NettingID, pmobj)) 
        pmisDemand = PaymIsDemand(pmobj, ntg);

        if (not ExecMacroFile("dlntdp.mac", "ИмпортироватьПоручениеДепо", ntg, pmobj, pmisDemand, pmobj.ValueDate))
            return false; 
        end;

        pmobj.PaymStatus = PM_FINISHED;

        if (pmisDemand)
            ВидРО = 77001; /*Для клиентских нужно будет добавить 77002*/
        else  
            ВидРО = 76001; /*Для клиентских нужно будет добавить 77002*/
        end;

        return ПроводкаПоКВУ(ВидРО, DL_NettingDoc(ntg), pmobj);
    end;

    return true;
END;
/****************************************************************************************/
PRIVATE CLASS NTG_PARAM
    VAR ntg      = NULL,
         Action   = NULL,
         isDemand = "",
         payPaym  = true,
         Error    = NULL,
         Parm     = NULL;
END;

PRIVATE MACRO ИсполнитьТрОб( pmobj, Parm )
    VAR isDemand = PaymIsDemand(pmobj, Parm.ntg),
         ВидРО    = 0,
         ntg_fd  = NULL;

    if (isDemand != Parm.isDemand)
        return true;
    end;

    ntg_fd  = DL_NettingDoc(Parm.ntg);

    ntg_fd.SetBaseFIID(pmobj.BaseFIID);

    if (pmobj.PaymStatus == PM_OVERDUE)
        Parm.Error.Добавить(0, "Ожидается снятие оплаты результирующего платежа "+pmobj.PaymentID+" с просрочки.");
        return false;
    end;

    if( (pmobj.PaymStatus != PM_FINISHED) and (pmobj.PaymStatus != PM_CLOSED_W_M_MOVEMENT) )
    pmobj.Actuate();

    if (Parm.payPaym)
        if (not ExecMacro2(Parm.Action, pmobj, Parm.ntg, ntg_fd))
            return false;
        end;
    else
            if( Parm.Parm.ПроводимКвитовку and (Parm.isDemand == SET_CHAR) and (pmobj.ReceiverBankID == {OurBank}) and (pmobj.PayerBankID != {OurBank}))
               if ((pmobj.PaymStatus != PM_FINISHED) and (pmobj.FuturePayerAmount > 0))               
                   if( not GetTrue(true, "Плановый платеж "+pmobj.PaymentID+" не сквитован полностью. Выполнять дальнейшую обработку платежа с выставлением статуса \"Закрыт без движения средств\"?") )/*просили не запрещать*/
                      return false;
                   end;
               end;
            end;
        pmobj.PaymStatus = PM_CLOSED_W_M_MOVEMENT; 
        return true;
    end;
    end;

    if (Parm.Parm.БэкОфис == "S")
        if (isDemand)
            ВидРО = 46001; /*Для клиентских нужно будет добавить 46002*/
        else
            ВидРО = 45001; /*Для клиентских нужно будет добавить 46002*/
        end;

        return ПроводкаПоКВУ(ВидРО, ntg_fd, pmobj);
    end;

    return true;
END;

PRIVATE MACRO ИсполнениеТрОбДляВалюты(Параметры, isDemand, ntg, action, error)
    VAR Parm = NTG_PARAM;
    var but;
    Array Text;
    Array Button;

    Parm.ntg      = ntg;
    Parm.Action   = action;
    Parm.isDemand = isDemand;
    Parm.Error    = error;
    Parm.Parm     = Параметры;

    if(Параметры.БэкОфис == "N")
      Text(0) = "Выполнить результирующие платежи?";

      Button(0) = "Да";
      Button(1) = "Нет";
      Button(2) = "Вынести на просрочку";

      if (IsExistsNotFinishedResultPM(Parm.ntg,Parm.isDemand))
      but = ConfWin(Text,Button);
      else
        but = 1;
      end;

      if(but == 0)
          Parm.payPaym = true;
      elif(but == 1)
          Parm.payPaym = false;
      else
        error.Добавить(0, "Выполните перенос на просрочку из списка операций");
        return false;
      end;
    else
      /*
      if( IsExistsNotFinishedResultPM(Parm.ntg,Parm.isDemand) and GetTrue(true, "Оформить оплату результирующих платежей по "+iif(isDemand,"требованиям","обязательствам")+"?"))
          Parm.payPaym = true;
      else
          Parm.payPaym = false;
      end;
      */
          Parm.payPaym = true;

    end;

    if (not ForEachPMResPaym(ntg.IdentProgram, ntg.NettingID, @ИсполнитьТрОб, Parm))
        return false;
    end;

    return true;
END;
/****************************************************************************************/
MACRO ИсполнитьПлатежи(Параметры, isDemand, ntg, action, error)
    VAR retVal = true;

    ГлобПарам = Параметры;

    if (Параметры.БэкОфис == "S")
        if (not ExecMacroFile ("dlntsp.mac", "Set_VO_Code", ntg))
            error.Добавить(0, "Ошибка инициализации параметров");
            retVal = false;
        end;
    end;

    if (retVal)
        if ((ntg.FI_Kind == FIKIND_CURRENCY) or (ntg.FI_Kind == FIKIND_METAL) or (ntg.FI_Kind == FIKIND_ALLFI))
            retVal = ИсполнениеТрОбДляВалюты(Параметры, isDemand, ntg, action, error);
        elif (ntg.FI_Kind == FIKIND_AVOIRISS)
            retVal = ИсполнениеТрОбДляЦенныхБумаг(Параметры, isDemand, ntg, error);
        else
            retVal = false;
        end;
    end;

    return retVal;
END;
/****************************************************************************************/
