/*
$Name:        scntgacccarry.mac
$Module:      Ядро Securities
$Description: Проводки БУ по неттингу БОЦБ (исполняются в СО БУ)
*/
import "dlntfd.mac", "dlntlib.mac", "spserv.mac", "sp_caracc.mac";

PRIVATE CONST UnknownParty = -1;

PRIVATE MACRO ФормироватьПроводкуБУ(Numb_Document, CarryDate, DebAcc, DebFIID, DebSum, KredAcc, KredFIID, KredSum, Ground)

  Var accTrnData = RsbAccTransactionData;
  
  accTrnData.Chapter         = 1;
  accTrnData.Date_Carry      = CarryDate;
  accTrnData.Numb_Document   = Numb_Document;
  accTrnData.ResultCarry     = 1;
  accTrnData.Ground          = Ground;
  accTrnData.Department      = {OperDprt};
  accTrnData.Oper            = {oper};
  accTrnData.AccountPayer    = DebAcc;
  accTrnData.FIIDPayer       = DebFIID;
  accTrnData.SumPayer        = DebSum;
  accTrnData.AccountReceiver = KredAcc;
  accTrnData.FIIDReceiver    = KredFIID;
  accTrnData.SumReceiver     = KredSum;
  
  ПроводкиБУ.Add(accTrnData, 0);

END;

PRIVATE VAR 
    СписокТребОбяз          = NULL,
    ПлатежиВНеттинге        = TArray,
    ТОВНеттинге             = TArray,
    ДатыПлатежейВНеттинге   = TArray,
    ДатыТОВНеттинге         = TArray,
    СчетаПлатежейВНеттинге  = TArray,
    СчетаТОВНеттинге        = TArray,
    СуммыПлатежейВНеттинге  = TArray,
    СуммыТОВНеттинге        = TArray,
    ВалютыПлатежейВНеттинге = TArray,
    ВалютыТОВНеттинге       = TArray,
    ИтоговыеТО              = TArray;

CLASS NumBuf
   VAR
      НомерТреб   = - 1,
      НомерОбяз   = - 1,
      НомерТребТО = - 1,
      НомерОбязТО = - 1,
      СчетТреб    = "",
      СчетОбяз    = "",
      СчетТребТО  = "",
      СчетОбязТО  = "";
END;

PRIVATE CLASS ЭлемТрОб(ФИ_п)
    VAR 
        ФИ             = ФИ_п,
        Стр            = $0, /*базовая сумма требования*/
        Соб            = $0, /*бюазовая сумма обязательства*/
        СчетМРасчеты   = "",  /* счет по категории "-Расчеты"  */
        СчетПРасчеты   = "",  /* счет по категории "+Расчеты"  */
        СчетМРасчетыВО = "",  /* счет по категории "-Расчеты" в валюте оплаты (ВО) */
        СчетПРасчетыВО = "";  /* счет по категории "+Расчеты" в валюте оплаты (ВО)   */
END;

PRIVATE CLASS (TArray) КлассСпискаТрОб 
  MACRO ДобавитьЕслиЕщеНет( fiid )
     VAR i = 0;

     while( i < this.size )
        if( this[i].ФИ == fiid )
           return i;
        end;
        i = i + 1;
     end;
     this[this.size] = ЭлемТрОб(fiid);

     return i;
  END;

  MACRO ПолучитьИД( fiid )
     VAR i = 0;
     
     while( i < this.size )
        if( this[i].ФИ == fiid )
           return i;
        end;
        i = i + 1;
     end;
     return NULL;
  END;

  MACRO Получить( fiid )
     VAR id = ПолучитьИД(fiid);
     if( id != NULL )
        return this[id];
     end;
     return NULL;
  END;
END;

PRIVATE MACRO ПолучитьДатуОкрытияСчета(Account, ID_RQACC, FIID, AccountTO:@VARIANT)
  var DateOpen = Date(0,0,0);
  var sql, rsd; 

  if( not (Account == ""))

      sql = RSDCommand("   SELECT acc.t_Open_Date OpenDate "
                     + "     FROM daccount_dbt acc "
                     + "    WHERE acc.t_Code_Currency = ? and "
                     + "          acc.t_Chapter = 1 and "
                     + "          acc.t_Account = ? ");

      sql.addParam( "", RSDBP_IN, FIID );
      sql.addParam( "", RSDBP_IN, Account );
      sql.execute();

      rsd = TRsbDataSet(sql);
      if(rsd.MoveNext())
         if( ValType(rsd.OpenDate) != V_UNDEF )
           DateOpen = SQL_ConvTypeDate(rsd.OpenDate) ;
         end;
      end;
  end;
  if(ID_RQACC > 0)

      sql = RSDCommand("   SELECT acc.t_Open_Date OpenDate , acc.t_Account Account"
                     + "     FROM daccount_dbt acc , ddlrqacc_dbt rqacc"
                     + "    WHERE rqacc.t_ID =  ? and "
                     + "          acc.t_Code_Currency = rqacc.t_FIID and "
                     + "          acc.t_Chapter = 1 and "
                     + "          acc.t_Account =  rqacc.t_Account ");

      sql.addParam( "", RSDBP_IN, ID_RQACC );
      sql.execute();

      rsd = TRsbDataSet(sql);
      if(rsd.MoveNext())
         if( ValType(rsd.OpenDate) != V_UNDEF )
           DateOpen  = SQL_ConvTypeDate(rsd.OpenDate) ;
           AccountTO = rsd.Account;
         end;
      end;
  end;

  return DateOpen;
END;


PRIVATE MACRO ДобавитьПлатежВалюту( pml, ntg_fd )

  VAR pmobj = NULL, idx = 0, FD = NULL, DocKind, error = 0;

  pmobj = ПлатежиВНеттинге[ПлатежиВНеттинге.size] = CNtgPayment(ntg_fd.dl_nett.rec.IdentProgram, pml.InitialPayment);

  if( pmobj.PaymentID == 0 )
     ПлатежиВНеттинге.size = ПлатежиВНеттинге.size -1;
  else
     idx = СписокТребОбяз.ДобавитьЕслиЕщеНет(pml.fiid);

     if( PaymIsDemand(pmobj, ntg_fd.dl_nett.rec) == "" )
        СписокТребОбяз[idx].Соб = СписокТребОбяз[idx].Соб + pmobj.BaseAmount;
     else
        СписокТребОбяз[idx].Стр = СписокТребОбяз[idx].Стр + pmobj.BaseAmount;
     end;

     if( ntg_fd.dl_nett.rec.OverTransitAccount == "" )
        if( PaymIsDemand(pmobj, ntg_fd.dl_nett.rec) == "" )
           СуммыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]  = pmobj.PayerAmount;
           ВалютыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1] = pmobj.PayerFIID;
           ДатыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]   = ПолучитьДатуОкрытияСчета(pmobj.PayerAccount, 0, pmobj.PayerFIID, NULL);
           СчетаПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]  = pmobj.PayerAccount;
        else
           СуммыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]  = pmobj.ReceiverAmount;
           ВалютыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1] = pmobj.ReceiverFIID;
           ДатыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]   = ПолучитьДатуОкрытияСчета(pmobj.ReceiverAccount, 0, pmobj.ReceiverFIID, NULL);
           СчетаПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]  = pmobj.ReceiverAccount;
        end;
     end;
  end;

  return true;
END;

PRIVATE MACRO ДобавитьТОВалюту( pml, ntg_fd )
  VAR idx = 0;
  var rq = TRecHandler("dlrq");

  NGT_GetRQ(pml.InitialPayment, rq);

  if( rq.rec.ID )

     ТОВНеттинге[ТОВНеттинге.size] = rq;

     idx = СписокТребОбяз.ДобавитьЕслиЕщеНет(pml.fiid);

     if( rq.rec.Kind == DLRQ_KIND_COMMIT )
        СписокТребОбяз[idx].Соб = СписокТребОбяз[idx].Соб + rq.rec.Amount;
     else
        СписокТребОбяз[idx].Стр = СписокТребОбяз[idx].Стр + rq.rec.Amount;
     end;

     if( ntg_fd.dl_nett.rec.OverTransitAccount == "" )
        var AccountTO = "";
        СуммыТОВНеттинге[ТОВНеттинге.size - 1]  = rq.rec.Amount;
        ВалютыТОВНеттинге[ТОВНеттинге.size - 1] = rq.rec.FIID;
        ДатыТОВНеттинге[ТОВНеттинге.size - 1]   = ПолучитьДатуОкрытияСчета("", rq.rec.RQACCID, -1 , AccountTO) ;
        СчетаТОВНеттинге[ТОВНеттинге.size - 1]  = AccountTO;
     end;

  end;

  return true;
END;

PRIVATE MACRO ОпределитьНаборВалютТО( ntg_fd ):bool
  var RetVal:bool = true;

  RetVal = ForEachPMLinkRQ(DL_NTGSEC, ntg_fd.dl_nett.rec.NettingID, @ДобавитьТОВалюту, ntg_fd);

  if( RetVal )
     RetVal = ForEachPMLink(DL_NTGSEC, ntg_fd.dl_nett.rec.NettingID, @ДобавитьПлатежВалюту, ntg_fd);
  end;

  return RetVal;
END;

PRIVATE MACRO ВыполнитьЗачетТребИОбязТС( ntg_fd )

  VAR idx = 0, СуммаЗачета, СчетМРасчеты, СчетПРасчеты, ВалютаРасчетов,
      cnt = СписокТребОбяз.size;

  while( idx < cnt )
     СуммаЗачета = min(СписокТребОбяз[idx].Стр, СписокТребОбяз[idx].Соб);

     if( (ntg_fd.dl_nett.rec.FI_Kind == FIKIND_CURRENCY) AND (ntg_fd.dl_nett.rec.PayFIID != СписокТребОбяз[idx].ФИ) AND (ntg_fd.dl_nett.rec.PayFIID != -1) )
        /*перевод сумм из ВП в ВО осуществляется по курсу на дату операции неттинга Dпл*/
        if( ConvSum(СуммаЗачета, СуммаЗачета, ntg_fd.dl_nett.rec.ValueDate, СписокТребОбяз[idx].ФИ, ntg_fd.dl_nett.rec.PayFIID) )
           msgbox("Ошибка при конвертации сумм ТО");
           return false;
        end;
        СчетМРасчеты   = СписокТребОбяз[idx].СчетМРасчетыВО;
        СчетПРасчеты   = СписокТребОбяз[idx].СчетПРасчетыВО;
        ВалютаРасчетов = ntg_fd.dl_nett.rec.PayFIID;
     else
        СчетМРасчеты   = СписокТребОбяз[idx].СчетМРасчеты;
        СчетПРасчеты   = СписокТребОбяз[idx].СчетПРасчеты;
        ВалютаРасчетов = СписокТребОбяз[idx].ФИ;
     end;

     if( СуммаЗачета and (СчетПРасчеты != СчетМРасчеты) )
        ФормироватьПроводкуБУ(ntg_fd.dl_nett.rec.DealNumber, ntg_fd.dl_nett.rec.ValueDate, СчетМРасчеты, ВалютаРасчетов, СуммаЗачета, СчетПРасчеты, ВалютаРасчетов, СуммаЗачета, ОпределитьОснованиеПроводки(ntg_fd.dl_nett.rec));
     end;

     idx = idx + 1;
  end;

  return true;
END;

PRIVATE MACRO ПолучитьПоследнююСумму(DocKind, DocID, SumKind, _Date, FIID)
   var Sпред = $0, DataSet, Query, D = Date(0, 0, 0);

   Query = " Select t_Sum, t_Date, t_Currency "
         + "   From ddlsum_dbt  "
         + "  Where t_DocKind = " + string(DocKind)
         + "    and t_DocID   = " + string(DocID)
         + "    and t_Kind    = " + string(SumKind)
         + " Order by t_Date Desc ";
   DataSet = TRsbDataSet(Query);

   if (DataSet.MoveNext())
      Sпред = DataSet.Sum;
      if (_Date != null)
         D = SQL_ConvTypeDate(DataSet.t_Date);
         FIID = DataSet.t_Currency;
      end;
   end;

   if (_Date != null)
      SetParm(3, D);
   end;
   SetParm(4, FIID);

   return Sпред;
END;

private macro ПолучитьДанныеДляОплатыПроцентов(ntg_fd, rq:TRecHandler, Account:@string, SumAcc:@money, CurAcc:@integer, SumPerc:@money, CurPerc:@integer)
   var LegKind = 0;
   var FD = NULL;
   var AccountBuf = TRecHandler("account");
   var Cat = "";
   var IsOverdue = false;// просроченность
   var SumPC_б = $0;

   AccountBuf.Clear();

   if(rq.rec.DealPart == 2)
      LegKind = LEG_KIND_DL_TICK_BACK;
   else
      LegKind = LEG_KIND_DL_TICK;
   end;

   FD = SPFirstDoc(LegKind, rq.rec.DocID);
   IsOverdue = ТОБылоПросрочено(rq);
   CurPerc = CurAcc = FD.GetPayFIID();

   if ( (IsBUY(FD.Group) and (FD.dl_leg.rec.IncomeRate > 0.0)) or
        (IsSALE(FD.Group) and (FD.dl_leg.rec.IncomeRate < 0.0)) )
      
      SumPC_б  = rq.rec.Amount;

      if (IsOverdue) // просрочено
         Cat = "+% с н.с.";
         SumPerc = SumPC_б;
      else
         Cat = "+% к погашению";
         SumPerc = SumPC_б;
      end;
   else // ПРЕПО. Ставка > 0, ОРЕПО. Ставка < 0
      if (IsOverdue == false)
         Cat = "-% к погашению"; 
      else
         Cat = "+% с н.с."; 
      end;
      SumPerc = rq.rec.Amount;
   end;

   if (ConvSum(SumAcc, SumPerc, ntg_fd.dl_nett.rec.ValueDate, CurPerc, CurAcc))
      msgbox("Ошибка при конвертации суммы по оплате процентов");
      return false;
   end;
    
   if( Cat != "" ) 
      if( not FD.OpenAccount( Cat, AccountBuf.rec.Account, false, null, AccountBuf, ntg_fd.dl_nett.rec.SigningDate) )
         return 1;
      end;
   end;

   Account = AccountBuf.rec.Account;
   return 0;
end;

PRIVATE MACRO ПолучитьНашСчетПоТО(ntg_fd, rq:TRecHandler)
  var LegKind;
  var FD = NULL;
  var AccountBuf = TRecHandler("account");

  AccountBuf.Clear();

  if(rq.rec.DealPart == 2)
    LegKind = LEG_KIND_DL_TICK_BACK;
  else
    LegKind = LEG_KIND_DL_TICK;
  end;

  FD = SPFirstDoc(LegKind, rq.rec.DocID);

  if( FD.tick.rec.ClientID != -1 )
     ПолучитьСчетСубъектаПоСделке(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.ClientID, DLRQ_SUBKIND_CURRENCY, FD.GetPayFIID(), AccountBuf);
  else
     if(rq.rec.Type == DLRQ_TYPE_PAYMENT)
       ПолучитьНашСчетТОПоОплате(rq, FD, ntg_fd.dl_nett.rec.SigningDate, AccountBuf, AccountBuf);
     elif((rq.rec.Type == DLRQ_TYPE_AVANCE) OR (rq.rec.Type == DLRQ_TYPE_DEPOSIT))
       ПолучитьНашСчетТОПоАвансуЗадатку(rq, FD, ntg_fd.dl_nett.rec.SigningDate, AccountBuf);
     elif(rq.rec.Type == DLRQ_TYPE_INCREPO)
       ПолучитьНашСчетТОПоПроцентам(rq, FD, ntg_fd.dl_nett.rec.SigningDate, AccountBuf);
     end;
  end;

  return AccountBuf.rec.Account;
END;

PRIVATE MACRO ПереносНаТС_Платежи( ntg_fd )

  VAR idx = 0, cnt = ПлатежиВНеттинге.size, id = 0,
      pmobj, СформироватьПроводку = false,
      СчетДебет, СчетКредит,
      СуммаПроводкиДебет = $0, СуммаПроводкиКредит = $0,
      ВДебет, ВКредит, СуммаВВалютеВО = $0;

  while( idx < cnt )
     pmobj = ПлатежиВНеттинге[idx];

     ntg_fd.SetBaseFIID(pmobj.BaseFIID);

     id = СписокТребОбяз.ПолучитьИД(pmobj.BaseFIID);

     if( PaymIsDemand(pmobj, ntg_fd.dl_nett.rec) == "" )
         СчетДебет = ПолучитьСчетПоПлатежу(pmobj, false);

        /*ВО не совпадает с ВП*/
        if( (ntg_fd.dl_nett.rec.FI_Kind == FIKIND_CURRENCY) AND (pmobj.ReceiverFIID != pmobj.BaseFIID) )
           if( (not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчетыВО, MC_OPENACC_CREATE, pmobj.ReceiverFIID, ntg_fd.dl_nett.rec.SigningDate)) OR
               (not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчетыВО, MC_OPENACC_CREATE, pmobj.ReceiverFIID, ntg_fd.dl_nett.rec.SigningDate))
             )
              msgbox("Ошибка при получении транзитных счетов");
              return false;
           end;

           СписокТребОбяз[id].СчетМРасчеты = СписокТребОбяз[id].СчетМРасчетыВО;
           СписокТребОбяз[id].СчетПРасчеты = СписокТребОбяз[id].СчетПРасчетыВО;

           /*перевод сумм из ВП в ВО осуществляется по курсу на дату операции неттинга Dпл*/
           if( ConvSum(СуммаВВалютеВО, pmobj.BaseAmount, ntg_fd.dl_nett.rec.ValueDate, pmobj.BaseFIID, pmobj.ReceiverFIID) )
              msgbox("Ошибка при конвертации сумм платежа");
              return false;
           end;
           СчетКредит = СписокТребОбяз[id].СчетМРасчетыВО;
           СуммаПроводкиКредит = СуммаПроводкиДебет = СуммаВВалютеВО; 
           ВДебет = ВКредит = pmobj.ReceiverFIID;
        else
           if( СписокТребОбяз[id].СчетМРасчеты == "" )
              if( not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчеты, MC_OPENACC_CREATE, СписокТребОбяз[id].ФИ, ntg_fd.dl_nett.rec.SigningDate) )
                 msgbox("Ошибка при получении транзитных счетов");
                 return false;
              end;
           end;

           СчетКредит = СписокТребОбяз[id].СчетМРасчеты;
           СуммаПроводкиДебет  = pmobj.ReceiverAmount; 
           СуммаПроводкиКредит = pmobj.BaseAmount;
           ВДебет      = pmobj.BaseFIID;
           ВКредит     = pmobj.ReceiverFIID;
        end;
     else
         СчетКредит = ПолучитьСчетПоПлатежу(pmobj, true);

        /*ВО не совпадает с ВП*/
        if( (ntg_fd.dl_nett.rec.FI_Kind == FIKIND_CURRENCY) AND (pmobj.PayerFIID != pmobj.BaseFIID) )
           if( (not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчетыВО, MC_OPENACC_CREATE, pmobj.PayerFIID, ntg_fd.dl_nett.rec.SigningDate)) OR
               (not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчетыВО, MC_OPENACC_CREATE, pmobj.PayerFIID, ntg_fd.dl_nett.rec.SigningDate))
             )
              msgbox("Ошибка при получении транзитных счетов");
              return false;
           end;

           СписокТребОбяз[id].СчетМРасчеты = СписокТребОбяз[id].СчетМРасчетыВО;
           СписокТребОбяз[id].СчетПРасчеты = СписокТребОбяз[id].СчетПРасчетыВО;

           /*перевод сумм из ВП в ВО осуществляется по курсу на дату операции неттинга Dпл*/
           if( ConvSum(СуммаВВалютеВО, pmobj.BaseAmount, ntg_fd.dl_nett.rec.ValueDate, pmobj.BaseFIID, pmobj.PayerFIID) )
              msgbox("Ошибка при конвертации сумм платежа");
              return false;
           end;
           СчетДебет = СписокТребОбяз[id].СчетПРасчетыВО;
           СуммаПроводкиКредит = СуммаПроводкиДебет = СуммаВВалютеВО;
           ВДебет = ВКредит = pmobj.PayerFIID;
        else
           if( СписокТребОбяз[id].СчетПРасчеты == "" )
              if( not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчеты, MC_OPENACC_CREATE, СписокТребОбяз[id].ФИ, ntg_fd.dl_nett.rec.SigningDate))
                 msgbox("Ошибка при получении транзитных счетов");
                 return false;
              end;
           end;

           СчетДебет = СписокТребОбяз[id].СчетПРасчеты;
           СуммаПроводкиДебет  = pmobj.BaseAmount; 
           СуммаПроводкиКредит = pmobj.PayerAmount;
           ВДебет      = pmobj.PayerFIID;
           ВКредит     = pmobj.BaseFIID;
        end;
     end;

     if( СчетДебет == СчетКредит )
        СчетКредит = NULL;
     end;

     СформироватьПроводку = false;

     if( (pmobj.DocKind == DL_IBCDOC) or (pmobj.DocKind == DL_FOREXDOC) )
        if( (pmobj.Purpose == PM_PURP_PRINC) or
            (pmobj.Purpose == PM_PURP_PRINC_RET) or
            (pmobj.Purpose == PM_PURP_OVD_PRINC)or
            (pmobj.Purpose == BAi) or
            (pmobj.Purpose == CAi)
          )
           СформироватьПроводку = true;
        end;
     elif( pmobj.DocKind == DL_VEKSELACCOUNTED )
        if( pmobj.Purpose == CAi )
           СформироватьПроводку = true;
        end;
     elif( (pmobj.DocKind == DL_DVNDEAL) or (pmobj.DocKind == DL_DVDEALT3) or (pmobj.DocKind == DL_DVFXDEAL) )
        СформироватьПроводку = true;
     end;

     if( СчетДебет and СчетКредит and СформироватьПроводку )
        ФормироватьПроводкуБУ(ntg_fd.dl_nett.rec.DealNumber, ntg_fd.dl_nett.rec.ValueDate, СчетДебет, ВДебет, СуммаПроводкиДебет, СчетКредит, ВДебет, СуммаПроводкиДебет, ОпределитьОснованиеПроводки(ntg_fd.dl_nett.rec));
     end;

     idx = idx + 1;
  end;

  return true;
END;

PRIVATE MACRO ПереносНаТС(ntg_fd)
  VAR
      idx = 0, cnt = ТОВНеттинге.size, id = 0,
      СформироватьПроводку = false,
      СчетДебет, СчетКредит,
      СуммаПроводкиДебет = $0, СуммаПроводкиКредит = $0,
      ВДебет, ВКредит, СуммаВВалютеВО = $0;
  var rq = TRecHandler("dlrq");
  var FD = NULL;
  var LegKind;

  while( idx < cnt )
     rq = ТОВНеттинге[idx];

     ntg_fd.SetBaseFIID(rq.rec.FIID);

     if( rq.rec.DealPart == 2 )
        LegKind = LEG_KIND_DL_TICK_BACK;
     else
        LegKind = LEG_KIND_DL_TICK;
     end;

     id = СписокТребОбяз.ПолучитьИД(rq.rec.FIID);

     FD = SPFirstDoc(LegKind, rq.rec.DocID);

     if ((rq.rec.Type == DLRQ_TYPE_AVANCE) or
         (rq.rec.Type == DLRQ_TYPE_DEPOSIT) or
         (rq.rec.Type == DLRQ_TYPE_PAYMENT) or
         (rq.rec.Type == DLRQ_TYPE_INCREPO) )

        if (rq.rec.Kind == DLRQ_KIND_COMMIT)
           if (rq.rec.Type == DLRQ_TYPE_INCREPO)
              if (ПолучитьДанныеДляОплатыПроцентов(ntg_fd, rq, @СчетДебет, @СуммаПроводкиДебет, @ВДебет, @СуммаПроводкиКредит, @ВКредит)==0)

                 if (СписокТребОбяз[id].СчетМРасчеты == "")
                    if (not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчеты, MC_OPENACC_CREATE, СписокТребОбяз[id].ФИ, ntg_fd.dl_nett.rec.SigningDate))
                       msgbox("Ошибка при получении транзитных счетов");
                       return false;
                    end;
                 end;

                 СчетКредит = СписокТребОбяз[id].СчетМРасчеты;
              end;
           else
              СчетДебет = ПолучитьНашСчетПоТО(ntg_fd, rq);

              /*ВО не совпадает с ВП*/
              if( (ntg_fd.dl_nett.rec.FI_Kind == FIKIND_CURRENCY) AND (rq.rec.FIID != FD.GetPayFIID()) )
                 if( (not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчетыВО, MC_OPENACC_CREATE, FD.GetPayFIID(), ntg_fd.dl_nett.rec.SigningDate)) OR
                     (not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчетыВО, MC_OPENACC_CREATE, FD.GetPayFIID(), ntg_fd.dl_nett.rec.SigningDate)))
                    msgbox("Ошибка при получении транзитных счетов");
                    return false;
                 end;

                 СписокТребОбяз[id].СчетМРасчеты = СписокТребОбяз[id].СчетМРасчетыВО;
                 СписокТребОбяз[id].СчетПРасчеты = СписокТребОбяз[id].СчетПРасчетыВО;

                 /*перевод сумм из ВП в ВО осуществляется по курсу на дату операции неттинга Dпл*/
                 if (ConvSum(СуммаВВалютеВО, rq.rec.Amount, ntg_fd.dl_nett.rec.ValueDate, rq.rec.FIID, FD.GetPayFIID()))
                    msgbox("Ошибка при конвертации сумм ТО");
                    return false;
                 end;
                 СчетКредит = СписокТребОбяз[id].СчетМРасчетыВО;
                 СуммаПроводкиКредит = СуммаПроводкиДебет = СуммаВВалютеВО; 
                 ВДебет = ВКредит = FD.GetPayFIID();

              else
                 if (СписокТребОбяз[id].СчетМРасчеты == "")
                    if (not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчеты, MC_OPENACC_CREATE, СписокТребОбяз[id].ФИ, ntg_fd.dl_nett.rec.SigningDate))
                       msgbox("Ошибка при получении транзитных счетов");
                       return false;
                    end;
                 end;

                 СчетКредит = СписокТребОбяз[id].СчетМРасчеты;
                 СуммаПроводкиДебет  = rq.rec.Amount; 
                 СуммаПроводкиКредит = rq.rec.Amount;
                 ВДебет      = FD.GetPayFIID();
                 ВКредит     = FD.GetPayFIID();
              end; 
           end;
           if (СчетДебет == СчетКредит)
               СчетДебет = NULL;
           end;
        else
           if (rq.rec.Type == DLRQ_TYPE_INCREPO)
              if (ПолучитьДанныеДляОплатыПроцентов(ntg_fd, rq, @СчетКредит, @СуммаПроводкиКредит, @ВКредит, @СуммаПроводкиДебет, @ВДебет)==0)
              
                 if (СписокТребОбяз[id].СчетПРасчеты == "")
                     if (not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчеты, MC_OPENACC_CREATE, СписокТребОбяз[id].ФИ, ntg_fd.dl_nett.rec.SigningDate))
                         msgbox("Ошибка при получении транзитных счетов");
                         return false;
                     end;
                 end;

                 СчетДебет = СписокТребОбяз[id].СчетПРасчеты;
              end;
           else
              СчетКредит = ПолучитьНашСчетПоТО(ntg_fd, rq);

              /*ВО не совпадает с ВП*/
              if( (ntg_fd.dl_nett.rec.FI_Kind == FIKIND_CURRENCY) AND (FD.GetPayFIID() != rq.rec.FIID) )
                 if( (not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчетыВО, MC_OPENACC_CREATE, FD.GetPayFIID(), ntg_fd.dl_nett.rec.SigningDate)) OR
                     (not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчетыВО, MC_OPENACC_CREATE, FD.GetPayFIID(), ntg_fd.dl_nett.rec.SigningDate)))
                   msgbox("Ошибка при получении транзитных счетов");
                   return false;
                 end;

                 СписокТребОбяз[id].СчетМРасчеты = СписокТребОбяз[id].СчетМРасчетыВО;
                 СписокТребОбяз[id].СчетПРасчеты = СписокТребОбяз[id].СчетПРасчетыВО;

                 /*перевод сумм из ВП в ВО осуществляется по курсу на дату операции неттинга Dпл*/
                 if (ConvSum(СуммаВВалютеВО, rq.rec.Amount, ntg_fd.dl_nett.rec.ValueDate, rq.rec.FIID, FD.GetPayFIID()))
                     msgbox("Ошибка при конвертации сумм ТО");
                     return false;
                 end;
                 СчетДебет = СписокТребОбяз[id].СчетПРасчетыВО;
                 СуммаПроводкиКредит = СуммаПроводкиДебет = СуммаВВалютеВО; 
                 ВДебет = ВКредит = FD.GetPayFIID();
              else
                 if (СписокТребОбяз[id].СчетПРасчеты == "")
                     if (not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчеты, MC_OPENACC_CREATE, СписокТребОбяз[id].ФИ, ntg_fd.dl_nett.rec.SigningDate))
                         msgbox("Ошибка при получении транзитных счетов");
                         return false;
                     end;
                 end;

                 СчетДебет = СписокТребОбяз[id].СчетПРасчеты;
                 СуммаПроводкиДебет  = rq.rec.Amount; 
                 СуммаПроводкиКредит = rq.rec.Amount;
                 ВДебет      = FD.GetPayFIID();
                 ВКредит     = FD.GetPayFIID();
              end;
           end;
        end;

        if (СчетДебет == СчетКредит)
           СчетКредит = NULL;
        end;

        if (СчетДебет and СчетКредит)
           ФормироватьПроводкуБУ(ntg_fd.dl_nett.rec.DealNumber, ntg_fd.dl_nett.rec.ValueDate, СчетДебет, ВДебет, СуммаПроводкиДебет, СчетКредит, ВДебет, СуммаПроводкиДебет, ОпределитьОснованиеПроводки(ntg_fd.dl_nett.rec));
        end;
     end;

     idx = idx + 1;
  end;

  return true;
END;

PRIVATE MACRO ПолучитьОчереднуюСуммуЗачетаПоДате( Валюта, Дт, Кт, СуммаЗачета, ВалютаЗачета, ntg, NB:@VARIANT )

  VAR idx = 0, cnt = СуммыПлатежейВНеттинге.size,
      Требование = NULL, Обязательство = NULL,
      СумТребование = 0, СумОбязательство = 0,
      pmsum = NULL, pmobj = NULL, rq = NULL,
      ТребованиеIsPM = NULL, ОбязательствоIsPM = NULL, stat = 0, ОтборТОПоУбыванию = false,
      ДатаТреб = Date(0,0,0), ДатаОбяз = Date(0,0,0) ;
  var DateZero = Date(0,0,0), pmdate = NULL;

  GetRegistryValue("COMMON\\НЕТТИНГ\\Отбор платежей по убыванию", V_BOOL, ОтборТОПоУбыванию, stat);
  if( stat )
     ОтборТОПоУбыванию = false;
  end;

  while( idx < cnt )
     pmobj = ПлатежиВНеттинге[idx];

     if( pmobj.BaseFIID == Валюта )
        pmdate = ДатыПлатежейВНеттинге[idx];

        if( (PaymIsDemand(pmobj, ntg.dl_nett.rec) == "X") )
           if( NB.НомерТреб == -1)
              if((NB.СчетТреб == "") or ((NB.СчетТреб == СчетаПлатежейВНеттинге[idx] )))
                 if( (NB.НомерТребТО == -1) and (NB.СчетТребТО == "") )
                    if( ОтборТОПоУбыванию )
                       if( ДатаТреб < pmdate )
                          ДатаТреб       = pmdate;
                          Требование     = idx;
                          ТребованиеIsPM = true;
                       end;
                    else
                       if( ((ДатаТреб > pmdate) and (pmdate != DateZero)) or (ДатаТреб == DateZero) )
                          ДатаТреб       = pmdate;
                          Требование     = idx;
                          ТребованиеIsPM = true;
                       end;
                    end;
                 end;
              end;
           else
             Требование     = NB.НомерТреб;
             ДатаТреб       = ДатыПлатежейВНеттинге[Требование];
             ТребованиеIsPM = true;
           end;
        else
           if( NB.НомерОбяз == -1)
              if((NB.СчетОбяз == "") or ((NB.СчетОбяз == СчетаПлатежейВНеттинге[idx] )))
                 if( (NB.НомерОбязТО == -1) and (NB.СчетОбязТО == ""))
                    if( ОтборТОПоУбыванию )
                       if( ДатаОбяз < pmdate )
                          ДатаОбяз  = pmdate;
                          Обязательство     = idx;
                          ОбязательствоIsPM = true;
                       end;
                    else
                       if( ((ДатаОбяз > pmdate) and (pmdate != DateZero)) or (ДатаОбяз == DateZero) )
                          ДатаОбяз  = pmdate;
                          Обязательство     = idx;
                          ОбязательствоIsPM = true;
                       end;
                    end;
                 end;
              end;
           else
               Обязательство     = NB.НомерОбяз;
               ДатаОбяз          = ДатыПлатежейВНеттинге[Обязательство];
               ОбязательствоIsPM = true;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = СуммыТОВНеттинге.size;

  while( idx < cnt )
     rq = ТОВНеттинге[idx];

     if( rq.rec.FIID == Валюта )
        pmdate = ДатыТОВНеттинге[idx];

        if( rq.rec.Kind == DLRQ_KIND_REQUEST )
           if( NB.НомерТребТО == -1)
              if((NB.СчетТребТО == "") or ((NB.СчетТребТО == СчетаТОВНеттинге[idx] )))
                 if( (NB.НомерТреб == -1) and (NB.СчетТреб == ""))
                    if( ОтборТОПоУбыванию )
                       if( ДатаТреб < pmdate )
                          ДатаТреб  = pmdate;
                          Требование     = idx;
                          ТребованиеIsPM = false;
                       end;
                    else
                       if( ((ДатаТреб > pmdate) and (pmdate != DateZero)) or (ДатаТреб == DateZero) )
                          ДатаТреб       = pmdate;
                          Требование     = idx;
                          ТребованиеIsPM = false;
                       end;
                    end;
                 end;
              end;
           else
             Требование     = NB.НомерТребТО;
             ДатаТреб       = ДатыТОВНеттинге[Требование];
             ТребованиеIsPM = false;
           end;
        else
           if( NB.НомерОбязТО == -1)
              if((NB.СчетОбязТО == "") or ((NB.СчетОбязТО == СчетаТОВНеттинге[idx] )))
                 if( (NB.НомерОбяз == -1) and  (NB.СчетОбяз == ""))
                    if( ОтборТОПоУбыванию )
                       if( ДатаОбяз < pmdate )
                          ДатаОбяз  = pmdate;
                          Обязательство     = idx;
                          ОбязательствоIsPM = false;
                       end;
                    else
                       if( ((ДатаОбяз > pmdate) and (pmdate != DateZero)) or (ДатаОбяз == DateZero) )
                          ДатаОбяз  = pmdate;
                          Обязательство     = idx;
                          ОбязательствоIsPM = false;
                       end;
                    end;
                 end;
              end;
           else
             Обязательство     = NB.НомерОбязТО;
             ДатаОбяз          = ДатыТОВНеттинге[Обязательство];
             ОбязательствоIsPM = false;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  if( (ДатаТреб == DateZero) or (ДатаОбяз == DateZero) )
     return false;
  end;

//  СумТребование    = СуммыПлатежейВНеттинге[Требование];
//  СумОбязательство = СуммыПлатежейВНеттинге[Обязательство];


  if(ТребованиеIsPM)
     NB.НомерТреб = Требование;
     NB.СчетТреб  = СчетаПлатежейВНеттинге[Требование];
     СумТребование    = СуммыПлатежейВНеттинге[Требование];
  else
     NB.НомерТребТО = Требование;
     NB.СчетТребТО  = СчетаТОВНеттинге[Требование];
     СумТребование    = СуммыТОВНеттинге[Требование];
  end;

  if(ОбязательствоIsPM)
     NB.НомерОбяз = Обязательство;
     NB.СчетОбяз  = СчетаПлатежейВНеттинге[Обязательство];
     СумОбязательство = СуммыПлатежейВНеттинге[Обязательство];
  else
     NB.НомерОбязТО = Обязательство;
     NB.СчетОбязТО  = СчетаТОВНеттинге[Обязательство];
     СумОбязательство = СуммыТОВНеттинге[Обязательство];
  end;

  СуммаЗачета = min(СумТребование, СумОбязательство);

  if( СуммаЗачета == 0 )
     return false;
  end;

  if( ТребованиеIsPM )
     ВалютаЗачета = ВалютыПлатежейВНеттинге[Требование];
     СуммыПлатежейВНеттинге[Требование] = СумТребование - СуммаЗачета;
  else
     ВалютаЗачета = ВалютыТОВНеттинге[Требование];
     СуммыТОВНеттинге[Требование] = СумТребование - СуммаЗачета;
  end;

  if( ОбязательствоIsPM )
     СуммыПлатежейВНеттинге[Обязательство] = СумОбязательство - СуммаЗачета;
  else
     СуммыТОВНеттинге[Обязательство] = СумОбязательство - СуммаЗачета;
  end;

  if( СумТребование > СумОбязательство )
     if( ОбязательствоIsPM )
        ПлатежиВНеттинге[Обязательство].PaymStatus = PM_CLOSED_W_M_MOVEMENT;
        ДатыПлатежейВНеттинге[Обязательство] = DateZero;   // обнуляем дату, чтобы учтенный платеж участвовал в дальнейших обработках
        NB.НомерОбяз = - 1;                                // чтобы можно было в следующем заходе отбирать обязательства
     else
        ТОВНеттинге[Обязательство].rec.State = DLRQ_STATE_EXEC;
        ДатыТОВНеттинге[Обязательство] = DateZero;
        NB.НомерОбязТО = - 1;
     end;
  elif( СумТребование < СумОбязательство )
     if( ТребованиеIsPM )
        ПлатежиВНеттинге[Требование].PaymStatus = PM_CLOSED_W_M_MOVEMENT;
        ДатыПлатежейВНеттинге[Требование] = DateZero;
        NB.НомерТреб = - 1;
     else
        ТОВНеттинге[Требование].rec.State = DLRQ_STATE_EXEC;
        ДатыТОВНеттинге[Требование] = DateZero;
        NB.НомерТребТО = - 1;
     end;
  elif( СумТребование == СумОбязательство )
     if( ОбязательствоIsPM )
        ПлатежиВНеттинге[Обязательство].PaymStatus = PM_CLOSED_W_M_MOVEMENT;
        ДатыПлатежейВНеттинге[Обязательство] = DateZero;   
        NB.НомерОбяз = - 1;                                
     else
        ТОВНеттинге[Обязательство].rec.State = DLRQ_STATE_EXEC;
        ДатыТОВНеттинге[Обязательство] = DateZero;
        NB.НомерОбязТО = - 1;
     end;
     if( ТребованиеIsPM )
        ПлатежиВНеттинге[Требование].PaymStatus = PM_CLOSED_W_M_MOVEMENT;
        ДатыПлатежейВНеттинге[Требование] = DateZero;
        NB.НомерТреб = - 1;
     else
        ТОВНеттинге[Требование].rec.State = DLRQ_STATE_EXEC;
        ДатыТОВНеттинге[Требование] = DateZero;
        NB.НомерТребТО = - 1;
     end;
  end;
//////////////
  idx = 0;
  cnt = СуммыПлатежейВНеттинге.size;
  var  СчТреб = false, СчОбяз = false, СчТребТО = false, СчОбязТО = false;
  while( idx < cnt )
     pmobj = ПлатежиВНеттинге[idx];

     if( pmobj.BaseFIID == Валюта )
        pmdate = ДатыПлатежейВНеттинге[idx];

        if( (PaymIsDemand(pmobj, ntg.dl_nett.rec) == "X") )
           if((not NB.СчетТреб == "") and ( ДатыПлатежейВНеттинге[idx] != DateZero) and ( СчетаПлатежейВНеттинге[idx] == NB.СчетТреб))
              СчТреб = true;
           end;
        else
           if((not NB.СчетОбяз == "") and ( ДатыПлатежейВНеттинге[idx] != DateZero) and ( СчетаПлатежейВНеттинге[idx] == NB.СчетОбяз))
              СчОбяз = true;
           end;

        end;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = СуммыТОВНеттинге.size;

  while( idx < cnt )
     rq = ТОВНеттинге[idx];

     if( rq.rec.FIID == Валюта )
        pmdate = ДатыТОВНеттинге[idx];

        if( rq.rec.Kind == DLRQ_KIND_REQUEST )
           if((not NB.СчетТребТО == "") and ( ДатыТОВНеттинге[idx] != DateZero) and ( СчетаТОВНеттинге[idx] == NB.СчетТребТО))
              СчТребТО = true;
           end;
        else
           if((not NB.СчетОбязТО == "") and ( ДатыТОВНеттинге[idx] != DateZero) and ( СчетаТОВНеттинге[idx] == NB.СчетОбязТО))
              СчОбязТО = true;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  if(not СчТреб)
     NB.СчетТреб = "";
  end;
  if(not СчОбяз)
     NB.СчетОбяз = "";
  end;

  if(not СчТребТО)
     NB.СчетТребТО = "";
  end;
  if(not СчОбязТО)
     NB.СчетОбязТО = "";
  end;


////////////
  if( ОбязательствоIsPM )
     SetParm(1, ПолучитьСчетПоПлатежу(ПлатежиВНеттинге[Обязательство], false));
  else
     SetParm(1, ПолучитьНашСчетПоТО(ntg, ТОВНеттинге[Обязательство]));
  end;
  if( ТребованиеIsPM )
     SetParm(2, ПолучитьСчетПоПлатежу(ПлатежиВНеттинге[Требование], true));
  else
     SetParm(2, ПолучитьНашСчетПоТО(ntg, ТОВНеттинге[Требование]));
  end;
  SetParm(3, СуммаЗачета);
  SetParm(4, ВалютаЗачета);

  return true;
END;

PRIVATE MACRO ПолучитьОчереднуюСуммуЗачета( Валюта, Дт, Кт, СуммаЗачета, ВалютаЗачета, ntg_fd )
  VAR idx = 0, cnt = СуммыПлатежейВНеттинге.size,
      Требование = NULL, Обязательство = NULL,
      СумТребование = 0, СумОбязательство = 0,
      pmsum = NULL, rq = NULL, pmobj = NULL, stat = 0, ОтборТОПоУбыванию = false,
      ТребованиеIsPM = NULL, ОбязательствоIsPM = NULL;

  GetRegistryValue("COMMON\\НЕТТИНГ\\Отбор платежей по убыванию", V_BOOL, ОтборТОПоУбыванию, stat);
  if( stat )
     ОтборТОПоУбыванию = false;
  end;

  while( idx < cnt )
     pmobj = ПлатежиВНеттинге[idx];

     if( pmobj.BaseFIID == Валюта )
        pmsum = СуммыПлатежейВНеттинге[idx];
        if( PaymIsDemand(pmobj, ntg_fd.dl_nett.rec) == "X" )
           if( ОтборТОПоУбыванию )
              if( СумТребование < pmsum )
                 СумТребование  = pmsum;
                 Требование     = idx;
                 ТребованиеIsPM = true;
              end;
           else
              if( ((СумТребование > pmsum) and (pmsum != 0)) or (СумТребование == 0) )
                 СумТребование  = pmsum;
                 Требование     = idx;
                 ТребованиеIsPM = true;
              end;
           end;
        else
           if( ОтборТОПоУбыванию )
              if( СумОбязательство < pmsum )
                 СумОбязательство  = pmsum;
                 Обязательство     = idx;
                 ОбязательствоIsPM = true;
              end;
           else
              if( ((СумОбязательство > pmsum) and (pmsum != 0)) or (СумОбязательство == 0) )
                 СумОбязательство  = pmsum;
                 Обязательство     = idx;
                 ОбязательствоIsPM = true;
              end;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = СуммыТОВНеттинге.size;

  while( idx < cnt )
     rq = ТОВНеттинге[idx];

     if( rq.rec.FIID == Валюта )
        pmsum = СуммыТОВНеттинге[idx];

        if( rq.rec.Kind == DLRQ_KIND_REQUEST )
           if( ОтборТОПоУбыванию )
              if( СумТребование < pmsum )
                 СумТребование  = pmsum;
                 Требование     = idx;
                 ТребованиеIsPM = false;
              end;
           else
              if( ((СумТребование > pmsum) and (pmsum != 0)) or (СумТребование == 0) )
                 СумТребование  = pmsum;
                 Требование     = idx;
                 ТребованиеIsPM = false;
              end;
           end;
        else
           if( ОтборТОПоУбыванию )
              if( СумОбязательство < pmsum )
                 СумОбязательство  = pmsum;
                 Обязательство     = idx;
                 ОбязательствоIsPM = false;
              end;
           else
              if( ((СумОбязательство > pmsum) and (pmsum != 0)) or (СумОбязательство == 0) )
                 СумОбязательство  = pmsum;
                 Обязательство     = idx;
                 ОбязательствоIsPM = false;
              end;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  if( (СумТребование == 0) or (СумОбязательство == 0) )
     return false;
  end;

  idx = 0;
  cnt = СуммыПлатежейВНеттинге.size;

  while( idx < cnt )
     pmobj = ПлатежиВНеттинге[idx];

     if( pmobj.BaseFIID == Валюта )
        pmsum = СуммыПлатежейВНеттинге[idx];
        if( PaymIsDemand(pmobj, ntg_fd.dl_nett.rec) == "X" )
           if( (СумТребование > СумОбязательство) and (СумОбязательство == pmsum) )
              СумТребование  = pmsum;
              Требование     = idx;
              ТребованиеIsPM = true;
           end;
        else
           if( (СумТребование < СумОбязательство) and (СумТребование == pmsum) )
              СумОбязательство = pmsum;
              Обязательство    = idx;
              ОбязательствоIsPM = true;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = СуммыТОВНеттинге.size;

  while( idx < cnt )
     rq = ТОВНеттинге[idx];

     if( rq.rec.FIID == Валюта )
        pmsum = СуммыТОВНеттинге[idx];
        if( rq.rec.Kind == DLRQ_KIND_REQUEST )
           if( (СумТребование > СумОбязательство) and (СумОбязательство == pmsum) )
              СумТребование  = pmsum;
              Требование     = idx;
              ТребованиеIsPM = false;
           end;
        else
           if( (СумТребование < СумОбязательство) and (СумТребование == pmsum) )
              СумОбязательство  = pmsum;
              Обязательство     = idx;
              ОбязательствоIsPM = false;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  СуммаЗачета = min(СумТребование, СумОбязательство);

  if( СуммаЗачета == 0 )
     return false;
  end;

  if( ТребованиеIsPM )
     ВалютаЗачета = ВалютыПлатежейВНеттинге[Требование];
     СуммыПлатежейВНеттинге[Требование] = СумТребование - СуммаЗачета;
  else
     ВалютаЗачета = ВалютыТОВНеттинге[Требование];
     СуммыТОВНеттинге[Требование] = СумТребование - СуммаЗачета;
  end;

  if( ОбязательствоIsPM )
     СуммыПлатежейВНеттинге[Обязательство] = СумОбязательство - СуммаЗачета;
  else
     СуммыТОВНеттинге[Обязательство] = СумОбязательство - СуммаЗачета;
  end;

  if( СумТребование > СумОбязательство )
     if( ОбязательствоIsPM )
        ПлатежиВНеттинге[Обязательство].PaymStatus = PM_CLOSED_W_M_MOVEMENT;
     else
        ТОВНеттинге[Обязательство].rec.State = DLRQ_STATE_EXEC;
     end;
  else
     if( ТребованиеIsPM )
        ПлатежиВНеттинге[Требование].PaymStatus = PM_CLOSED_W_M_MOVEMENT;
     else
        ТОВНеттинге[Требование].rec.State = DLRQ_STATE_EXEC;
     end;
  end;

  if( ОбязательствоIsPM )
     SetParm(1, ПолучитьСчетПоПлатежу(ПлатежиВНеттинге[Обязательство], false));
  else
     SetParm(1, ПолучитьНашСчетПоТО(ntg_fd, ТОВНеттинге[Обязательство]));
  end;
  if( ТребованиеIsPM )
     SetParm(2, ПолучитьСчетПоПлатежу(ПлатежиВНеттинге[Требование], true));
  else
     SetParm(2, ПолучитьНашСчетПоТО(ntg_fd, ТОВНеттинге[Требование]));
  end;
  SetParm(3, СуммаЗачета);
  SetParm(4, ВалютаЗачета);

  return true;
END;

PRIVATE MACRO ВыполнитьЗачетТребИОбязНеЧерезТС( ntg_fd )
  VAR idx = 0, cnt = СписокТребОбяз.size, stat = 0, ОтборПоДате = false,
      Дт = "", Кт = "", СуммаЗачета = 0, ВалютаЗачета = -1, NB = NumBuf;
  GetRegistryValue("COMMON\\НЕТТИНГ\\ОТБОР ПЛАТ. ПО ДАТЕ ОТКР. СЧЕТА", V_BOOL, ОтборПоДате, stat);
  if (stat)
      ОтборПоДате = false;
  end;

  if(not ОтборПоДате )

    while( idx < cnt )
        while( ПолучитьОчереднуюСуммуЗачета(СписокТребОбяз[idx].ФИ, Дт, Кт, СуммаЗачета, ВалютаЗачета, ntg_fd) )
           if( Дт != Кт )
              ФормироватьПроводкуБУ(ntg_fd.dl_nett.rec.DealNumber, ntg_fd.dl_nett.rec.ValueDate, Дт, ВалютаЗачета, СуммаЗачета, Кт, ВалютаЗачета, СуммаЗачета, ОпределитьОснованиеПроводки(ntg_fd.dl_nett.rec));
           end;
        end;

      idx = idx + 1;
    end;
  else
    while( idx < cnt )
        while( ПолучитьОчереднуюСуммуЗачетаПоДате(СписокТребОбяз[idx].ФИ, Дт, Кт, СуммаЗачета, ВалютаЗачета, ntg_fd, NB) )
           if( Дт != Кт )
              ФормироватьПроводкуБУ(ntg_fd.dl_nett.rec.DealNumber, ntg_fd.dl_nett.rec.ValueDate, Дт, ВалютаЗачета, СуммаЗачета, Кт, ВалютаЗачета, СуммаЗачета, ОпределитьОснованиеПроводки(ntg_fd.dl_nett.rec));
           end;
        end;

      idx = idx + 1;
    end;
  end;

  return true;
END;

PRIVATE MACRO ИсполнитьНеттингТО( ntg_fd )
  VAR FinalDealNumber = "";

  СписокТребОбяз = КлассСпискаТрОб;

  СписокТребОбяз.size          = 0;
  ПлатежиВНеттинге.size        = 0;
  ТОВНеттинге.size             = 0;
  СуммыПлатежейВНеттинге.size  = 0;
  СуммыТОВНеттинге.size        = 0;
  ВалютыПлатежейВНеттинге.size = 0;
  ВалютыТОВНеттинге.size       = 0;
  ИтоговыеТО.size              = 0;

  if( ОпределитьНаборВалютТО(ntg_fd) )
     if( ntg_fd.dl_nett.rec.OverTransitAccount == "X" )
        if( ПереносНаТС(ntg_fd) and
            ПереносНаТС_Платежи(ntg_fd) and
            ВыполнитьЗачетТребИОбязТС(ntg_fd)
          )
           /*Без ошибок*/
        else
           return 1;
        end;
     elif( ntg_fd.dl_nett.rec.FI_Kind == FIKIND_CURRENCY )
        if( ВыполнитьЗачетТребИОбязНеЧерезТС(ntg_fd) )
           /*Без ошибок*/
        else
           return 1;
        end;
     end;
  end;

  return 0;
END;

PRIVATE MACRO СохранитьСуммуКомиссии( DocKind:INTEGER, DocID:INTEGER, Kind:INTEGER, CommDate:DATE, Sum:MONEY, NDS:MONEY, Currency:INTEGER ):BOOL
  var sql, cmd;
  
  if( (Sum == 0) AND (NDS == 0) )
     return true;
  end;

  sql =  "DECLARE "
       + "BEGIN "
       + "  RSI_DLGR.SetDLSUM(?, ?, ?, ?, ?, ?, ?, ?, ?); "
       + "END; ";
   
  cmd = RSDCommand(sql);

  cmd.addParam( "", RSDBP_IN, DocKind );
  cmd.addParam( "", RSDBP_IN, DocID );
  cmd.addParam( "", RSDBP_IN, Kind );
  cmd.addParam( "", RSDBP_IN, Currency );
  cmd.addParam( "", RSDBP_IN, CommDate );
  cmd.addParam( "", RSDBP_IN, Sum );
  cmd.addParam( "", RSDBP_IN, NDS );
  cmd.addParam( "", RSDBP_IN, 0 );
  cmd.addParam( "", RSDBP_IN, -1 );
  cmd.execute();

  return true;
END;

PRIVATE MACRO СписатьРезерв( ntg_fd, rq )
   VAR Reserv = $0, LastDate, SaveSumma,
       ReservAcc = TRecHandler( "account" ),
       PC        = TRecHandler( "account" );

   if( rq.rec.Type == DLRQ_TYPE_PAYMENT )
      Reserv = ПолучитьПоследнююСумму( DLDOC_PAYMENT, rq.rec.ID, DLSUM_KIND_RESERV_OVERDUE, LastDate );
      if( Reserv != 0 )
          if( ntg_fd.GetAccount("Резерв, договор", null, MC_OPENACC_CHECKEXIST, NATCUR, rq.rec.PlanDate, ReservAcc, true, FIROLE_RESERV_RPT ) == true )
              if( not ntg_fd.GetAccount("+РС, д/с", null, MC_OPENACC_CREATE, null, rq.rec.PlanDate, PC, null, FIROLE_BA) )
                 return false;
              end;

              ФормироватьПроводкуБУ(ntg_fd.dl_nett.rec.DealNumber, rq.rec.PlanDate, 
                                    ReservAcc.rec.Account, NATCUR, Reserv, 
                                    PC.rec.Account, NATCUR, Reserv, 
                                    "Списание резерва по просроченным требованиям.");

              /*После списания создается нулевая сумма того же вида*/
              if( LastDate == rq.rec.PlanDate ) /*Сумма сохраняется нарастающим итогом*/
                 SaveSumma = -Reserv;
              else
                 SaveSumma = $0;
              end;
              if( not СохранитьСуммуКомиссии( DLDOC_PAYMENT, rq.rec.ID, DLSUM_KIND_RESERV_OVERDUE, rq.rec.PlanDate, SaveSumma, $0, NATCUR ) )
                 return false;      
              end; 
          end;
      end;

   end;
END;


PRIVATE MACRO ИсполнитьОбязательство( ntg_fd, rq )
  var stat = 0;
  var DebAccountBuff = TRecHandler("account");
  var CredAccountBuff = TRecHandler("account");
  var Cat = "";
  var Amount_From = $0, Amount_To = $0;

  var НеФормироватьПроводкиПоТО = false;
     
  if((ИнтегрированныйРежим()) and
     (rq.rec.Kind == DLRQ_KIND_COMMIT) and
     (rq.rec.SubKind == DLRQ_SUBKIND_CURRENCY) and
     (rq.rec.State == DLRQ_STATE_EXEC) and
     (rq.rec.RqAccID > 0)
    )

    var rqacc = TRecHandler("dlrqacc.dbt");
    ПолучитПлатежныеРеквизитыПоТО(rq, rqacc);

    if((rqacc.rec.BankID > 0) and (rqacc.rec.BankID > {OurBank}))
      НеФормироватьПроводкиПоТО = true;
    end;
  end;

  if(НеФормироватьПроводкиПоТО == false)
    if(ntg_fd.dl_nett.rec.ClientId == UnknownParty)

      if( ТОБылоПросрочено(rq) )
         Cat = "Обяз. с н.с.";
      else
         if( ntg_fd.dl_nett.rec.OverTransitAccount == "X" )
            Cat = "-Расчеты";
         else
            Cat = "-Форвард, расчеты";
         end;
      end;

      if(not ntg_fd.GetAccount(Cat, DebAccountBuff.rec.Account, MC_OPENACC_CREATE, IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID), rq.rec.PlanDate))
        return false;
      end;
    else
      // тут для клиента нужно найти его счет по ТО
      if(ПолучитьДенежныйСчетСубъектаПоСделке(rq.rec.DocKind, rq.rec.DocID, ntg_fd.dl_nett.rec.ClientId, rq.rec.FIID, DebAccountBuff) != 0)
        return false;
      end;
    end;

     if( rq.rec.FIID != IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID) )
        if( ConvSum( Amount_To, rq.rec.Amount, rq.rec.PlanDate, rq.rec.FIID, IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID)) )
           msgbox("Ошибка при конвертации");
           return false;
        end;
     else
        Amount_From = rq.rec.Amount;
     end;


     if(ПолучитьДенежныйСчетСубъектаПоСделке(rq.rec.DocKind, rq.rec.DocID, rq.rec.Party, rq.rec.FIID, CredAccountBuff) != 0)
       return false;
     end;

     if( rq.rec.FIID != CredAccountBuff.rec.Code_Currency )
        if( ConvSum( Amount_From, rq.rec.Amount, rq.rec.PlanDate, rq.rec.FIID, CredAccountBuff.rec.Code_Currency) )
           msgbox("Ошибка при конвертации");
           return false;
        end;
     else
        Amount_To = rq.rec.Amount;
     end;

     ФормироватьПроводкуБУ(ntg_fd.dl_nett.rec.DealNumber, rq.rec.PlanDate, 
                           DebAccountBuff.rec.Account, IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID), Amount_From, 
                           CredAccountBuff.rec.Account, CredAccountBuff.rec.Code_Currency, Amount_To, 
                           "Исполнение обязательств по операции № "+ntg_fd.dl_nett.rec.DealNumber+" заключена "+ntg_fd.dl_nett.rec.SigningDate);
   end;

   return true;
END;


PRIVATE MACRO ИсполнитьТребование( ntg_fd, rq )
  var stat = 0;
  var DebAccountBuff = TRecHandler("account");
  var CredAccountBuff = TRecHandler("account");
  var Cat = "";
  var Amount_From = $0, Amount_To = $0;


  if(ПолучитьДенежныйСчетСубъектаПоСделке(rq.rec.DocKind, rq.rec.DocID, rq.rec.Party, rq.rec.FIID, DebAccountBuff) != 0)
    return false;
  end;

  if(ntg_fd.dl_nett.rec.ClientId == UnknownParty)

    if( ТОБылоПросрочено(rq) )
       Cat = "Треб. с н.с.";
    else
       if( ntg_fd.dl_nett.rec.OverTransitAccount == "X" )
          Cat = "+Расчеты";
       else
          Cat = "+Форвард, расчеты";
       end;
    end;

    if(not ntg_fd.GetAccount(Cat, CredAccountBuff.rec.Account, MC_OPENACC_CREATE, IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID), rq.rec.PlanDate))
      return false;
    end;
  else
    // тут для клиента нужно найти его счет по ТО
    if(ПолучитьДенежныйСчетСубъектаПоСделке(rq.rec.DocKind, rq.rec.DocID, ntg_fd.dl_nett.rec.ClientId, rq.rec.FIID, CredAccountBuff) != 0)
      return false;
    end;
  end;

  if( rq.rec.FIID != DebAccountBuff.rec.Code_Currency )
     if( ConvSum( Amount_From, rq.rec.Amount, rq.rec.PlanDate, rq.rec.FIID, DebAccountBuff.rec.Code_Currency) )
        msgbox("Ошибка при конвертации");
        return false;
     end;
  else
     Amount_From = rq.rec.Amount;
  end;

  if( rq.rec.FIID != IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID) )
     if( ConvSum( Amount_To, rq.rec.Amount, rq.rec.PlanDate, rq.rec.FIID, IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID)) )
        msgbox("Ошибка при конвертации");
        return false;
     end;
  else
     Amount_To = rq.rec.Amount;
  end;

  ФормироватьПроводкуБУ(ntg_fd.dl_nett.rec.DealNumber, rq.rec.PlanDate, 
                        DebAccountBuff.rec.Account, DebAccountBuff.rec.Code_Currency, Amount_From, 
                        CredAccountBuff.rec.Account, IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID), Amount_To, 
                        "Исполнение требований по операции № "+ntg_fd.dl_nett.rec.DealNumber+" заключена "+ntg_fd.dl_nett.rec.SigningDate);

  return IIF( (ntg_fd.dl_nett.rec.ClientId == UnknownParty), СписатьРезерв(ntg_fd, rq), true );

END;


PRIVATE MACRO ПроводкиПоИтоговымТО( ntg_fd )
  var cmd, query, DataSet;
  var rq = TRecHandler("dlrq.dbt");
  var err = 0;

  cmd = DL_RSDCommand("select * from ddlrq_dbt where t_DocKind = ? and t_DocID = ? ");
  cmd.addParam(ntg_fd.dl_nett.rec.DocKind);
  cmd.addParam(ntg_fd.dl_nett.rec.NettingID);

  DataSet = cmd.Execute();
  while((err == 0) and (DataSet.moveNext()))
    DataSet.GetRecord().CopyTo( rq.rec );

    ntg_fd.SetBaseFIID(rq.rec.FIID);

    if(rq.rec.Kind == DLRQ_KIND_REQUEST)
      if(ИсполнитьТребование(ntg_fd, rq) == false)
        err = 1;
      end;
    else
      if(ИсполнитьОбязательство(ntg_fd, rq) == false)
        err = 1;
      end;
    end;
  end;

  return err;
END;

macro ПроводкиПоОплатеНеттингаБОЦБ( NettingID )
  var err = 0;
  var ntg_fd = DL_NettingDoc(DL_NTGSEC, NettingID);

  if( ntg_fd.dl_nett.rec.ClientId == UnknownParty )
     err = ИсполнитьНеттингТО(ntg_fd);
  end;

  if( not err )
     err = ПроводкиПоИтоговымТО(ntg_fd);
  end;

  return err;
end;

private macro ПроводкиПоПросрочкиТО( rq:object, ntg_fd )
  var Amount, DocGround;
  var Cat = "";
  var DebAccountBuff = TRecHandler("account");
  var CredAccountBuff = TRecHandler("account");

  ntg_fd.SetBaseFIID(rq.rec.FIID);

  if( rq.rec.Kind == DLRQ_KIND_COMMIT )          
     /*Обязательства*/

     if( ntg_fd.dl_nett.rec.OverTransitAccount == "X" )
        Cat = "-Расчеты";
     else
        Cat = "-Форвард, расчеты";
     end;

     if( not ntg_fd.GetAccount(Cat, DebAccountBuff.rec.Account, MC_OPENACC_CREATE, IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID), ntg_fd.dl_nett.rec.ValueDate) )
        return false;
     end;

     if( not ntg_fd.GetAccount("Обяз. с н.с.", CredAccountBuff.rec.Account, MC_OPENACC_CREATE, IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID), ntg_fd.dl_nett.rec.ValueDate) )
        return false;
     end;

     DocGround = "Перенос на просрочку обязательств";
  else 
     /*Требования*/
     if( ntg_fd.dl_nett.rec.OverTransitAccount == "X" )
        Cat = "+Расчеты";
     else
        Cat = "+Форвард, расчеты";
     end;

     if( not ntg_fd.GetAccount(Cat, CredAccountBuff.rec.Account, MC_OPENACC_CREATE, IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID), ntg_fd.dl_nett.rec.ValueDate) )
        return false;
     end;

     if( not ntg_fd.GetAccount("Треб. с н.с.", DebAccountBuff.rec.Account, MC_OPENACC_CREATE, IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID), ntg_fd.dl_nett.rec.ValueDate) )
        return false;
     end;

     DocGround = "Перенос на просрочку требований";
  end;

  if( rq.rec.FIID != IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID) )
     if( ConvSum(Amount, rq.rec.Amount, rq.rec.PlanDate, rq.rec.FIID, IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID)) )
        MsgBoxEX("Не могу сконвертировать сумму из " + ПолучитьКодФинИн(rq.rec.FIID) + " в " + ПолучитьКодФинИн(IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID)), 0, 0, "Вынос на просрочку", "Ошибка при выполнении операции");
        return false;
     end;
  else
     Amount = rq.rec.Amount;
  end;

  ФормироватьПроводкуБУ( ntg_fd.dl_nett.rec.DealNumber, rq.rec.PlanDate,
                         DebAccountBuff.rec.Account,  IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID), Amount,
                         CredAccountBuff.rec.Account, IIF(ntg_fd.dl_nett.rec.PayFIID < 0, rq.rec.FIID, ntg_fd.dl_nett.rec.PayFIID), Amount,
                         DocGround + " по операции № " + ntg_fd.dl_nett.rec.DealNumber + " заключена " + ntg_fd.dl_nett.rec.SigningDate );

  return true;
end;

macro ПроводкиПоПросрочкиНеттингаБОЦБ( NettingID )
  var err = 0;
  var ntg_fd = DL_NettingDoc(DL_NTGSEC, NettingID);

  if( not ForEachRQRes(NettingID, @ПроводкиПоПросрочкиТО, ntg_fd) )
     err = 1;
  end;

  return err;
end;
