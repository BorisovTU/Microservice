/*
$Name: ntswift.mac
$Module: Ценные бумаги
$Description: создание информационного сообщения  599 для неттинга
*/

import OprInter, PTInter, PaymInter, CTInter, SPInter, sp_class, "dpimp.mac" , "spRepFun.mac";
import RsbDataSet;

private var     FD, FD2;
private var  Count;

macro FillPartyShort( PartyID, PartyCodeKind, PartyCode:@variant, PartyName:@variant )
  record party(party);

  if( PartyCode != NULL )
    PartyCode = ПолучитьКодСубъекта(PartyID, PartyCodeKind);
  end;
  if( ПолучитьСубъекта(PartyID, party) == 0 )
    PartyName = party.ShortName;
  else
    PartyName = "";
  end;
end;

private macro  ЗаполнитьСчетРазделДепо(Account,SELLDEPOACC:@variant,SELLDEPOPART:@variant)                   
  var query, DataSet, cmd;
  var queryAcc, DataSetAcc, cmdAcc;
  query = "Select acnt.t_AutoKey AutoKey, acnt.t_Root Root, acnt.t_BriefCode Code From ddepoacnt_dbt acnt Where acnt.t_BriefCode = ?" ;
  cmd = DL_RSDCommand(query);
  cmd.AddParam(Account);
  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    if(DataSet.AutoKey==DataSet.Root)
      SELLDEPOACC = DataSet.Code;
      SELLDEPOPART = "";
    else
      SELLDEPOPART = DataSet.Code;
      queryAcc = "Select acnt.t_BriefCode Code From ddepoacnt_dbt acnt Where acnt.t_AutoKey = ? ";
      cmdAcc = DL_RSDCommand(queryAcc);
      cmdAcc.AddParam(DataSet.Root);
      DataSetAcc = cmdAcc.Execute();
      if( DataSetAcc.moveNext() )
        SELLDEPOACC = DataSetAcc.Code;
      end;
    end;
  end;
end;

class CPMMesDL(NettingID)
  var T_DRAFTID           = NettingID;
  var T_ATTRIBUTE         = "";
  var T_RECEIVERBIC       = "";
  var T_CA                = "";
  var T_TRADTYPE          = "";
  var T_MIC               = "";
  var T_DEALID            = "";
  var T_DEALDATE          = date(0,0,0);
  var T_DEALTIME          = time(0);
  var T_SETTDATE          = date(0,0,0);
  var T_GSETT             = "";

  var T_SETTDATENET       = date(0,0,0);
  var T_DIRNET            = "";
  var T_SUMNET            = $0.0;
  var T_INSNET            = "";
  var T_NANET             = "";
  
  var T_ISSUENAME         = "";
  var T_ISIN              = "";
  var T_CFI               = "";
  var T_MATURITY          = date(0,0,0);
  var T_INTR              = $0.0;
  var T_LSIN              = "";
  var T_ISSUEOTHRCODEKIND = "";
  var T_ISSUEOTHRCODE     = "";
  var T_ISSUENOMINAL      = $0.0;
  var T_ISSUENOMCURRENCY  = "";
  var T_BLOCKED           = "NO";
  var T_NEEDMACH          = "";
  var T_AMOUNT            = $0.0;
  var T_PRICE             = $0.0;
  var T_PRICEPRC          = $0.0;
  var T_DAY               = "";
  var T_SUM               = $0.0;
  var T_SUMCURRENCY       = "";
  var T_COUP              = $0.0;
  var T_COUPCURRENCY      = "";
  var T_SUMNC             = $0.0;
  var T_SUMNCCURRENCY     = "";
  var T_EXCH              = $0.0;
  var T_EXCH1             = $0.0;
  var T_EXCHCUR           = "";
  var T_DIRECTION         = 0;
  var T_DVP               = 0;
  var T_REPO2             = date(0,0,0);
  var T_RATE              = $0.0;
  var T_INTEREST          = $0.0;
  var T_OPERTYPE          = "";
  var T_CLIENT            = "";  
  var T_REPO              = 0;  
  var T_SELLBIC           = "";
  var T_SELLNAME          = "";
  var T_SELLDEPOACC       = "";
  var T_SELLDEPOPART      = "";
  var T_DEAGBIC           = "";
  var T_DEAGNAME          = "";
  var T_DEAGDEPOACC       = "";
  var T_DEAGDEPOPART      = "";
  var T_PSETBIC           = "";
  var T_PSETNAME          = "";
  var T_PSETBIC1          = "";
  var T_PSETNAME1         = "";
  var T_REAGBIC           = "";
  var T_REAGNAME          = "";
  var T_REAGDEPOACC       = "";
  var T_REAGDEPOPART      = "";
  var T_BUYRBIC           = "";
  var T_BUYRNAME          = "";
  var T_BUYRDEPOACC       = "";
  var T_BUYRDEPOPART      = "";
  var T_DEALSUM           = $0.0;
  var T_DEALCURRENCY      = "";
  var T_PAYEBIC           = "";
  var T_PAYENAME          = "";
  var T_PAYECASH          = "";

  var T_PAYEACCWBIC       = "";
  var T_PAYEACCWNAME      = "";
  var T_PAYEACCWCASH      = "";
  var T_PAYEACCWCASHWITH  = "";
  var T_PAYEACCWCASHWITHBIC  = "";
  var T_PAYEACCWCASHWITHINN  = "";

  var T_ACCWBIC           = "";
  var T_ACCWNAME          = "";
  var T_ACCWCASH          = "";

  var T_ACCWCASHWITH      = "";
  var T_ACCWCASHWITHBIC   = "";
  var T_ACCWCASHWITHINN   = "";

  var T_BENMBIC           = "";
  var T_BENMNAME          = "";
  var T_BENMCASH          = "";
  var T_ROWNUM            = 0;
end;


private macro получитьКорСчетБанка(BankID)
  var query, DataSet, cmd;
  var queryAcc, DataSetAcc, cmdAcc;
  query = "Select bank.t_Coracc Coracc From dbankdprt_dbt bank Where bank.t_PartyID = ?" ;
  cmd = DL_RSDCommand(query);
  cmd.AddParam(BankID);
  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    return DataSet.Coracc;
  end;
  return "";

end;
// заполнение данными по каждой сделке
private macro FillPaymentsMessageDataTick( BUFF, PMMsg, tick, Part )

  var stat = 0, KindOper = 0, ReceiverID, PayerID, PayerDepID, ClientID, RecDepositID, PayDepositID;
  var query, cmd, DataSet;
  var ContrPay, ContrCer;  // договора Продавца, Покупателя.
  var rq      = TRecHandler("dlrq.dbt");               // поставка
  var rq_dev2 = TRecHandler("dlrq.dbt");               // поставка 2ч.
  var rqacc   = TRecHandler("dlrqacc");                // продажа
  var rq_pay  = TRecHandler("dlrq.dbt");
  var sa      = TRecHandler("settacc");                // СПИ
  var Banksa  = TRecHandler("settacc");                // СПИ
  var PayRqAcc  = TRecHandler("dlrqacc");              // Платежные реквизиты продавца
  var RecRqAcc  = TRecHandler("dlrqacc");              // Платежные реквизиты покупателя
  var PayRqAccBank  = TRecHandler("dlrqacc");          // Платежные реквизиты банка продавца
  var RecRqAccBank  = TRecHandler("dlrqacc");          // Платежные реквизиты банка покупателя
  record fiwarnts(fiwarnts);
  var  DaysInYear   = 0, DaysInCoupon = 0;

  if (Part == 1)// по первой части
    FD  = SPFirstDoc( tick, false ); 
    FD2  = SPFirstDoc( tick, true ); 
  elif (Part ==2)  //по второй части
    FD  = SPFirstDoc( tick, true ); 
    FD2  = SPFirstDoc( tick, false ); 
  end;
                                                             
  PMMsg.T_DEALID = FD.tick.rec.DealCodeTS;                                                  //Номер сделки
  PMMsg.T_DEALDATE = FD.tick.rec.DealDate;                                                  //Дата сделки 

  if( IsEXCHANGE(FD.Group) == true )
    PMMsg.T_TRADTYPE = "EXCH";
    FillPartyShort(FD.tick.rec.MarketID, PTCK_MIC, @PMMsg.T_MIC, NULL);
  else
    PMMsg.T_TRADTYPE = "OTCO";
  end;
  PMMsg.T_SETTDATE = FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.PlanDate;
  PMMsg.T_ROWNUM = Count;

  // Покупка
  if(IsBUY(FD.Group))                  //покупка    Субъект - контрагент
    if(FD.tick.rec.PartyID == -1)              //продавец
      PayerID ={OurBank};
    else
      PayerID = FD.tick.rec.PartyID;
    end;
    if( FD.tick.rec.ClientID != -1 )                    //покупатель
      ReceiverID = FD.tick.rec.ClientID;
      ClientID   = FD.tick.rec.ClientID;
    else
      ReceiverID = {OurBank};
      ClientID   = {OurBank};
    end;
    PMMsg.T_DIRECTION = 1;
    ContrCer = FD.tick.rec.ClientContrID;  //покупатель является клиентом
    ContrPay = FD.tick.rec.PartyContrID;   //продавец является контрагентом
    RecDepositID = FD.Dl_leg.rec.DepositID;
    FillPartyShort(RecDepositID, PTCK_SWIFT, @PMMsg.T_PSETBIC1, @PMMsg.T_PSETNAME1);      // Код / Краткое наименование депозитария покупателя              
  
    if( IsRepo(FD.Group))    
      PayDepositID = FD2.Dl_leg.rec.DepositID;
      FillPartyShort(PayDepositID, PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME);      // Код / Краткое наименование депозитария продавца              
    end;
  end;

  // Продажа
  if(IsSALE(FD.Group))                  //продажа   НашБанк или Клиент НашегоБанкам 
    if( FD.tick.rec.ClientID != -1 )                    //проверять ли на то, что клиент- наш банк?
      PayerID  = FD.tick.rec.ClientID;
      ClientID = FD.tick.rec.ClientID;
    else
      ClientID = {OurBank};
      PayerID  = {OurBank};
    end;
    if(FD.tick.rec.PartyID == -1)              //покупатель
      ReceiverID ={OurBank};
    else
      ReceiverID = FD.tick.rec.PartyID;
    end;
    PMMsg.T_DIRECTION = 2;
    ContrPay = FD.tick.rec.ClientContrID;  //покупатель является клиентом
    ContrCer = FD.tick.rec.PartyContrID;   //продавец является контрагентом
  
    PayDepositID = FD.Dl_leg.rec.DepositID;
    FillPartyShort(PayDepositID, PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME);      // Код / Краткое наименование депозитария продавца              
    if( IsRepo(FD.Group))    
      RecDepositID = FD2.Dl_leg.rec.DepositID;
      FillPartyShort(RecDepositID, PTCK_SWIFT, @PMMsg.T_PSETBIC1, @PMMsg.T_PSETNAME1);      // Код / Краткое наименование депозитария покупателя              
    end;

  end;
////////////////////////////////////
////////////////////////////////////
  query = " SELECT fi.t_Name t_IssueName, " +
          "        fi.t_DrawingDate,   "
          "        avr.t_ISIN t_ISIN, " +
          "        avr.t_LSIN t_LSIN, " +
          "        CASE " +
          "           WHEN rsi_rsbparty.PT_GetPartyCode (?, 1) = 'НРД' " +
          "           THEN " +
          "              'NSD' " +
          "           ELSE " +
          "              CHR (1) " +
          "        END t_IssueOthrCodeKind, " +
          "        CASE " +
          "           WHEN rsi_rsbparty.PT_GetPartyCode (?, 1) = 'НРД' " +
          "           THEN " +
          "              (SELECT oc.t_Code " +
          "                 FROM DOBJCODE_DBT oc " +
          "                WHERE     oc.t_ObjectType = 9 " +
          "                      AND oc.t_CodeKind = 14 " +
          "                      AND oc.t_ObjectID = fi.t_FIID) " +
          "           ELSE " +
          "              CHR (1) " +
          "        END t_IssueOthrCode, " +
          "        rsi_rsb_fiinstr.FI_GetNominalOnDate (fi.t_FIID, ?, 1) t_IssueNominal, " +
          "        (SELECT fvc.t_Ccy FROM dfininstr_dbt fvc WHERE fvc.t_FIID = fi.t_FaceValueFI) t_IssueNomCurrency " +
          "        from DFININSTR_DBT fi, DAVOIRISS_DBT avr " +
          "        where fi.t_FIID = ? " +
          "        AND avr.t_FIID = fi.t_FIID ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(ReceiverID);
  cmd.AddParam(ReceiverID);
  cmd.AddParam(PMMsg.T_SETTDATE);
  cmd.AddParam(FD.tick.rec.Pfi);

  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    PMMsg.T_ISSUENAME         = DataSet.T_ISSUENAME        ;
    PMMsg.T_ISIN              = DataSet.T_ISIN             ;
    PMMsg.T_LSIN              = DataSet.T_LSIN             ;
    PMMsg.T_ISSUEOTHRCODEKIND = DataSet.T_ISSUEOTHRCODEKIND;
    PMMsg.T_ISSUEOTHRCODE     = DataSet.T_ISSUEOTHRCODE    ;
    PMMsg.T_ISSUENOMINAL      = DataSet.T_ISSUENOMINAL     ;
    PMMsg.T_ISSUENOMCURRENCY  = DataSet.T_ISSUENOMCURRENCY ;
    PMMsg.T_MATURITY          = DataSet.T_DRAWINGDATE      ;

  end;
  PMMsg.T_AMOUNT = FD.DL_LEG.rec.Principal;

  rq_pay = FD.GetRQ(DLRQ_TYPE_PAYMENT);

  PMMsg.T_SUM = rq_pay.rec.Amount;                                                      
  PMMsg.T_SUMCURRENCY = GetFICode(rq_pay.rec.FIID,  null, FICK_ISOSTRING);               // ISO код  для поля Sum

  PMMsg.T_DEALSUM = FD.DL_LEG.rec.Cost;
end;


private macro ClearPaymentsMessageData()
  var stat = 0;

  SQL_Execute(" DELETE FROM DDRAFTSINFO1_TMP ", null, false );

  return stat;

  OnError(err);
    return 1;
end;

private macro InsertPaymentsMessageDataNett(PMMsg)
    var cmd = RSDCommand( " INSERT INTO DDRAFTSINFO1_TMP ( " +
                                      " T_DRAFTID, " +                        //Идентификатор поручения 
                                      " T_ATTRIBUTE, " +                      //Атрибут
                                      " T_RECEIVERBIC, " +                    //Получатель
                                      " T_CA, " +                             //Номер корсчета НашБанк
                                      " T_TRADTYPE, " +                       //Место сделки
                                      " T_MIC, " +                            //Место сделки
                                      " T_DEALID, " +                         //Номер сделки
                                      " T_DEALDATE, " +                       //Дата сделки
                                      " T_DEALTIME, " +                       //время сделки
                                      " T_SETTDATE, " +                       //Дата расчета
                                      " T_GSETT, " +                          //Номер и дата заключения ген.согл.
                                      " T_SETTDATENET, "+
                                      " T_DIRNET, "  +   
                                      " T_SUMNET, "  +  
                                      " T_INSNET, "  +     
                                      " T_NANET, "  +      


///Параметры ценной бумаги
                                      " T_ISSUENAME, " +                      //Наименование ценной бумаги 
                                      " T_ISIN, " +                           //ISIN из справочника ценных бумаг 
                                      " T_CFI, " +                            //CFI-код 
                                      " T_MATURITY, " +                       //Дата погашения 
                                      " T_INTR, " +                           //Текущая ставка купона 
                                      " T_LSIN, " +                           //Код государственной регистрации из справочника ценных бумаг 
                                      " T_ISSUEOTHRCODEKIND, " +              //Вид кода
                                      " T_ISSUEOTHRCODE, " +                  //Код, указанного вида (заполняется, если заполнен атрибут IssueOthrCodeKind)
                                      " T_ISSUENOMINAL, " +                   //Текущий непогашенный номинал
                                      " T_ISSUENOMCURRENCY, " +               //Валюта номинала ценной бумаги 
                                      " T_BLOCKED, " +                        //Признак наличия обременения ценных бумаг 
                                      " T_NEEDMACH, " +                       //Признак необходимости встречного поручения
                                      " T_AMOUNT, " +                         //Количество ц/б
                                      " T_PRICE, " +                          //Цена одной ценной бумаги
                                      " T_PRICEPRC, " +                       //Цена одной ценной бумаги в процентах 
                                      " T_DAY, " +                            //База дней для расчета процентов
                                      " T_SUM, " +                            //Сумма сделки
                                      " T_SUMCURRENCY, " +                    //ISO-код валюты сделки
                                      " T_COUP, " +                           //Сумма НКД
                                      " T_COUPCURRENCY, " +                   //ISO-код валюты НКД
                                      " T_SUMNC, " +                          //Стоимость
                                      " T_SUMNCCURRENCY, " +                  //ISO-код валюты 
                                      " T_EXCH, " +                           //Курс
                                      " T_EXCH1, " +                          //Курс расчетов 2ч.        
                                      " T_EXCHCUR, " +                        //Валюта расчетов
                                      " T_DIRECTION, " +                      //Направление сделки
                                      " T_DVP, " +                            //Признак платежа
                                      " T_REPO2, " +                          //Дата исполнения второй части сделки РЕПО
                                      " T_RATE, " +                           //Ставка РЕПО
                                      " T_INTEREST, "  +                      //Сумма процентов
                                      " T_OPERTYPE, " +                       //условия поставки ц/б / тип расчетной операции
                                      " T_CLIENT, " +                         //Краткое наименование клиента
                                      " T_REPO, "  +                          //Идентификация сделки РЕПО
//Продавец  конечный отправитель ЦБ 
                                      " T_SELLBIC, " +                        //Идентификация стороны 
                                      " T_SELLNAME, " +
                                      " T_SELLDEPOACC, " +                    //Счет депо стороны
                                      " T_SELLDEPOPART, " +
//Депозитарий продавца (отправителя) ценных бумаг
                                      " T_DEAGBIC, " +                        //идентификация стороны
                                      " T_DEAGNAME, " +
                                      " T_DEAGDEPOACC, " +                    //Счет депо стороны
                                      " T_DEAGDEPOPART, " +
                                      " T_PSETBIC, " +                        //идентификация стороны
                                      " T_PSETNAME, " +
                                      " T_PSETBIC1, " +                        //идентификация стороны
                                      " T_PSETNAME1, " +
//Депозитарий покупателя (получателя) ценных бумаг
                                      " T_REAGBIC, " +
                                      " T_REAGNAME, " +
                                      " T_REAGDEPOACC, " +
                                      " T_REAGDEPOPART, " +
//Покупатель  конечный получатель ЦБ 
                                      " T_BUYRBIC, " +
                                      " T_BUYRNAME, " +
                                      " T_BUYRDEPOACC, " +
                                      " T_BUYRDEPOPART, " +
                                      " T_DEALSUM, " +                        //Сумма расчетов
                                      " T_DEALCURRENCY, " +                   //Валюта расчетов
//Плательщик  отправитель денежных средств
                                      " T_PAYEBIC, " +
                                      " T_PAYENAME, " +
                                      " T_PAYECASH, " +
                                      " T_PAYEACCWBIC, " +       
                                      " T_PAYEACCWNAME, " +      
                                      " T_PAYEACCWCASH, " +      
                                      " T_PAYEACCWCASHWITH," +  
                                      " T_PAYEACCWCASHWITHBIC, " +
                                      " T_PAYEACCWCASHWITHINN, " +

//Продавец Ц\Б  конечный получатель Д\С
                                      " T_ACCWBIC, " +
                                      " T_ACCWNAME, " +
                                      " T_ACCWCASH, " +
                                      " T_ACCWCASHWITH, " +   
                                      " T_ACCWCASHWITHBIC, " +
                                      " T_ACCWCASHWITHINN, " +

//Продавец Ц\Б  конечный получатель денежных средств
                                      " T_BENMBIC, " +
                                      " T_BENMNAME, " +
                                      " T_BENMCASH, " +
                                      " T_ROWNUM )" +
                          "        VALUES ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? ) ");

    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DRAFTID           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ATTRIBUTE         );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_RECEIVERBIC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_CA                );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_TRADTYPE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_MIC               );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALID            );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALDATE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALTIME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SETTDATE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_GSETT             );

    cmd.addParam( "", RSDBP_IN, PMMsg.T_SETTDATENET       );       
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DIRNET            );       
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SUMNET            );       
    cmd.addParam( "", RSDBP_IN, PMMsg.T_INSNET            );       
    cmd.addParam( "", RSDBP_IN, PMMsg.T_NANET             );       

    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUENAME         );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISIN              );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_CFI               );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_MATURITY          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_INTR              );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_LSIN              );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUEOTHRCODEKIND );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUEOTHRCODE     );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUENOMINAL      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUENOMCURRENCY  );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BLOCKED           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_NEEDMACH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_AMOUNT            );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PRICE             );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PRICEPRC          );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DAY               );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SUM               );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SUMCURRENCY       );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_COUP              );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_COUPCURRENCY      );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SUMNC             );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SUMNCCURRENCY     );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_EXCH              );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_EXCH1             );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_EXCHCUR           );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DIRECTION         );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DVP               );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REPO2             );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_RATE              );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_INTEREST          );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_OPERTYPE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_CLIENT            );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REPO              );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETBIC1          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETNAME1         );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALSUM           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALCURRENCY      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYEBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYENAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYECASH          );

    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYEACCWBIC       );         
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYEACCWNAME      );         
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYEACCWCASH      );         
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYEACCWCASHWITH  );         
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYEACCWCASHWITHBIC);        
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYEACCWCASHWITHINN);        

    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWCASH          );

    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWCASHWITH      );        
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWCASHWITHBIC   );        
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWCASHWITHINN   );        

    cmd.addParam( "", RSDBP_IN, PMMsg.T_BENMBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BENMNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BENMCASH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ROWNUM            );

    SQL_Execute( cmd, "", false );                              

//  return stat;

  OnError(er)
    return 1;
end;

private macro получитьISIN(FIID)
  var query, DataSet, cmd;
  query = "Select avr.t_ISIN ISIN FROM davoiriss_dbt avr WHERE avr.T_FIID = ? ";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(FIID);
  DataSet = cmd.Execute();
  if (DataSet.moveNext())
    return DataSet.ISIN;
  else
    return "";
  end
end;

 MACRO ПолучитьИтоговоеТО_Неттинг(ID:INTEGER, KIND:INTEGER, DlRq:TRecHandler):BOOL
  var Select, DataSet;                         

  DlRq.Clear();

  Select = DL_RSDCommand("select * from ddlrq_dbt where t_DocID = ? AND t_DocKind = ?");
  Select.AddParam(ID);
  Select.AddParam(KIND);
  DataSet = Select.execute();
  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo( DlRq.rec );
    return true;
  end;  

  return false;
END;


private macro  FillPaymentsMessageData (BUFF, PMMsg)
  var rq = TRecHandler("dlrq.dbt");
  var sa      = TRecHandler("settacc");                // СПИ
  var Banksa  = TRecHandler("settacc");                // СПИ
  var PayRqAcc  = TRecHandler("dlrqacc");              // Платежные реквизиты продавца
  var RecRqAcc  = TRecHandler("dlrqacc");              // Платежные реквизиты покупателя
  var PayRqAccBank  = TRecHandler("dlrqacc");          // Платежные реквизиты банка продавца
  var RecRqAccBank  = TRecHandler("dlrqacc");          // Платежные реквизиты банка покупателя
  var ПолучательЦБ, ОтправительЦБ, ПолучательДС, ОтправительДС;
  var ClientID , ReceiverID;
  if(BUFF.ClientID == -1)                           //клиент
    ClientID = {OurBank};
  else
    ClientID = BUFF.ClientID;
  end;
  ReceiverID = BUFF.Contractor;                     //контрагент
  PMMsg.T_ATTRIBUTE = readNoteForObject(OBJTYPE_NETTING, UniID(BUFF, OBJTYPE_NETTING), 4, BUFF.SigningDate);

  PMMsg.T_SETTDATENET =  BUFF.SigningDate;
  FillPartyShort(ReceiverID, PTCK_SWIFT, @PMMsg.T_RECEIVERBIC, NULL);               //Получатель   // контрагент

//  if (ПолучитьТО(BUFF.NettingID, rq))
//  получаем ТО, записываем в rq
// какую ТО брать???
  if(ПолучитьИтоговоеТО_Неттинг(BUFF.NettingID, BUFF.DocKind, rq))
 
    if(rq.rec.Kind == 0)             // требование- то получатели мы, отправитель- контрагент
      PMMsg.T_DIRNET = "Т"; 
      ПолучательЦБ  = ClientID;
      ПолучательДС  = ClientID;
      ОтправительЦБ = ReceiverID;
      ОтправительДС = ReceiverID;
    elif(rq.rec.Kind == 1)          // обязательство- отправители мы, получатель -контрагент
      PMMsg.T_DIRNET = "О";    
      ПолучательЦБ  = ReceiverID;
      ПолучательДС  = ReceiverID;
      ОтправительЦБ = ClientID;
      ОтправительДС = ClientID;
    end;
  
    if(rq.rec.SubKind == 0)
      PMMsg.T_INSNET = "Д";     
      PMMsg.T_NANET = GetFICode(rq.rec.FIID,  null, FICK_ISOSTRING);     // ISO буквенный код валюты
    elif(rq.rec.SubKind == 1)
      PMMsg.T_INSNET = "Ц";    
      PMMsg.T_NANET = получитьISIN(rq.rec.FIID);
    end;

    if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(BUFF.DocKind, BUFF.NettingID, ПолучательДС, DLRQ_SUBKIND_CURRENCY, BUFF.BaseFIID , DLRQ_TYPE_UNKNOWN, PayRqAcc) != 0) /*нет подходящих платежных реквизитов*/
      if(SP_GetPartySETTACC(ПолучательДС, BUFF.ClientContrId, BUFF.BaseFIID, FIKIND_CURRENCY, 0, sa) == 0)
        FillPartyShort(sa.rec.BankID, PTCK_SWIFT, @PMMsg.T_ACCWBIC, @PMMsg.T_ACCWNAME);      // Код / Краткое наименование депозитария продавца  ЦБ (получателя ДС)             
        PMMsg.T_BENMCASH = sa.rec.Account;
        PMMsg.T_ACCWCASH = sa.rec.CorrAcc;
        if(PMMsg.T_ACCWCASH == "")
          PMMsg.T_ACCWCASH = получитьКорСчетБанка(sa.rec.BankCorrID);
        end;

        if(sa.rec.BankCorrID > 0)
          PMMsg.T_ACCWCASHWITHINN = ПолучитьКодСубъекта(sa.rec.BankCorrID, PTCK_INN);
          PMMsg.T_ACCWCASHWITHBIC = ПолучитьКодСубъекта(sa.rec.BankCorrID, PTCK_SWIFT);
        end;
        PMMsg.T_ACCWCASHWITH = sa.rec.BankCorrName;
      end;
    else
      FillPartyShort(PayRqAcc.rec.BankID, PTCK_SWIFT, @PMMsg.T_ACCWBIC, @PMMsg.T_ACCWNAME);      // Код / Краткое наименование депозитария продавца ЦБ (получателя ДС)    DL_TICK   DL_LEG        
      PMMsg.T_BENMCASH = PayRqAcc.rec.Account;
      PMMsg.T_ACCWCASH = PayRqAcc.rec.CorrAcc;
      if(PMMsg.T_ACCWCASH == "")
        PMMsg.T_ACCWCASH = получитьКорСчетБанка(PayRqAcc.rec.BankCorrID);
      end;

      if(PayRqAcc.rec.BankCorrID > 0)
        PMMsg.T_ACCWCASHWITHINN = ПолучитьКодСубъекта(PayRqAcc.rec.BankCorrID, PTCK_INN);
        PMMsg.T_ACCWCASHWITHBIC = ПолучитьКодСубъекта(PayRqAcc.rec.BankCorrID, PTCK_SWIFT);
      end;
      PMMsg.T_ACCWCASHWITH = PayRqAcc.rec.BankCorrName;

    end;
    // платежные реквизиты покупателя ЦБ // лицевой счет  (отправителя ДС)
    if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(BUFF.DocKind, BUFF.NettingID, ОтправительДС, DLRQ_SUBKIND_CURRENCY, BUFF.BaseFIID , DLRQ_TYPE_UNKNOWN, RecRqAcc) != 0) /*нет подходящих платежных реквизитов*/
      if(SP_GetPartySETTACC(ОтправительДС, BUFF.ClientContrId, BUFF.BaseFIID, FIKIND_AVOIRISS, 0, sa) == 0)
        FillPartyShort(sa.rec.BankID, PTCK_SWIFT, @PMMsg.T_PAYEACCWBIC, @PMMsg.T_PAYEACCWNAME);
        PMMsg.T_PAYECASH = sa.rec.Account;
        PMMsg.T_PAYEACCWCASH = sa.rec.CorrAcc;
        if(PMMsg.T_PAYEACCWCASH == "")
          PMMsg.T_PAYEACCWCASH = получитьКорСчетБанка(sa.rec.BankCorrID);
        end;
        if(sa.rec.BankCorrID > 0)
          PMMsg.T_PAYEACCWCASHWITHINN = ПолучитьКодСубъекта(sa.rec.BankCorrID, PTCK_INN);
          PMMsg.T_PAYEACCWCASHWITHBIC = ПолучитьКодСубъекта(sa.rec.BankCorrID, PTCK_SWIFT);
        end;
        PMMsg.T_PAYEACCWCASHWITH = sa.rec.BankCorrName;
      end;
    else
      FillPartyShort(RecRqAcc.rec.BankID, PTCK_SWIFT, @PMMsg.T_PAYEACCWBIC, @PMMsg.T_PAYEACCWNAME);
      PMMsg.T_PAYECASH = RecRqAcc.rec.Account;
      PMMsg.T_PAYEACCWCASH = RecRqAcc.rec.CorrAcc;
      if(PMMsg.T_PAYEACCWCASH == "")
        PMMsg.T_PAYEACCWCASH = получитьКорСчетБанка(RecRqAcc.rec.BankCorrID);
      end;

      if(RecRqAcc.rec.BankCorrID > 0)
        PMMsg.T_PAYEACCWCASHWITHINN = ПолучитьКодСубъекта(RecRqAcc.rec.BankCorrID, PTCK_INN);
        PMMsg.T_PAYEACCWCASHWITHBIC = ПолучитьКодСубъекта(RecRqAcc.rec.BankCorrID, PTCK_SWIFT);
      end;
      PMMsg.T_PAYEACCWCASHWITH = RecRqAcc.rec.BankCorrName;
    end;
            ////////////////////////////////////
    FillPartyShort(ПолучательЦБ, PTCK_SWIFT, @PMMsg.T_BUYRBIC, @PMMsg.T_BUYRNAME);   // Код / Краткое наименование  покупателя  конечного получателя ЦБ по сделке             
    FillPartyShort(ОтправительЦБ, PTCK_SWIFT, @PMMsg.T_SELLBIC, @PMMsg.T_SELLNAME);      // Код / Краткое наименование  продавца  конечного отправителя ЦБ по сделке             

    FillPartyShort(ОтправительДС, PTCK_SWIFT, @PMMsg.T_PAYEBIC, @PMMsg.T_PAYENAME);   // Код / Краткое наименование  отправителя ДС             
    FillPartyShort(ПолучательДС, PTCK_SWIFT, @PMMsg.T_BENMBIC, @PMMsg.T_BENMNAME);      // Код / Краткое наименование  получателя ДС             

    if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(BUFF.DocKind, BUFF.NettingID, ОтправительЦБ, DLRQ_SUBKIND_AVOIRISS, ALLFININSTR , DLRQ_TYPE_UNKNOWN, PayRqAcc) != 0) /*нет подходящих платежных реквизитов*/
      if(SP_GetPartySETTACC(ОтправительЦБ, 0, ALLFININSTR, FIKIND_AVOIRISS, 0, sa) == 0)
        ЗаполнитьСчетРазделДепо(sa.rec.Account,@PMMsg.T_SELLDEPOACC,@PMMsg.T_SELLDEPOPART);
        FillPartyShort(sa.rec.BankID, PTCK_SWIFT, @PMMsg.T_DEAGBIC, @PMMsg.T_DEAGNAME);      // Код / Краткое наименование депозитария продавца               
          if(SP_GetPartySETTACC(sa.rec.BankID, 0, ALLFININSTR, FIKIND_AVOIRISS, 0, Banksa) == 0)
            ЗаполнитьСчетРазделДепо(Banksa.rec.Account,@PMMsg.T_DEAGDEPOACC,@PMMsg.T_DEAGDEPOPART);
          end;
      end;
    else
      ЗаполнитьСчетРазделДепо(PayRqAcc.rec.Account,@PMMsg.T_SELLDEPOACC,@PMMsg.T_SELLDEPOPART);
      FillPartyShort(PayRqAcc.rec.BankID, PTCK_SWIFT, @PMMsg.T_DEAGBIC, @PMMsg.T_DEAGNAME);      // Код / Краткое наименование депозитария продавца               
      if(SP_GetPartySETTACC(PayRqAcc.rec.BankID, 0, ALLFININSTR, FIKIND_AVOIRISS, 0, Banksa) == 0)
        ЗаполнитьСчетРазделДепо(Banksa.rec.Account,@PMMsg.T_DEAGDEPOACC,@PMMsg.T_DEAGDEPOPART);
      end;
    end;

    if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(BUFF.DocKind, BUFF.NettingID, ПолучательЦБ, DLRQ_SUBKIND_AVOIRISS, ALLFININSTR , DLRQ_TYPE_UNKNOWN, RecRqAcc) != 0) /*нет подходящих платежных реквизитов*/
      if(SP_GetPartySETTACC(ПолучательЦБ, 0, ALLFININSTR, FIKIND_AVOIRISS, 0, sa) == 0)
        ЗаполнитьСчетРазделДепо(RecRqAcc.rec.Account,@PMMsg.T_BUYRDEPOACC,@PMMsg.T_BUYRDEPOPART);
        FillPartyShort(sa.rec.BankID, PTCK_SWIFT, @PMMsg.T_REAGBIC, @PMMsg.T_REAGNAME);      // Код / Краткое наименование депозитария покупателя               
          if(SP_GetPartySETTACC(sa.rec.BankID, 0, ALLFININSTR, FIKIND_AVOIRISS, 0, Banksa) == 0)
            ЗаполнитьСчетРазделДепо(Banksa.rec.Account,@PMMsg.T_REAGDEPOACC,@PMMsg.T_REAGDEPOPART);
          end;
      end;
    
    else
      ЗаполнитьСчетРазделДепо(RecRqAcc.rec.Account,@PMMsg.T_BUYRDEPOACC,@PMMsg.T_BUYRDEPOPART);
      FillPartyShort(RecRqAcc.rec.BankID, PTCK_SWIFT, @PMMsg.T_REAGBIC, @PMMsg.T_REAGNAME);      // Код / Краткое наименование депозитария покупателя               
      if(SP_GetPartySETTACC(RecRqAcc.rec.BankID, 0, ALLFININSTR, FIKIND_AVOIRISS, 0, Banksa) == 0)
        ЗаполнитьСчетРазделДепо(Banksa.rec.Account,@PMMsg.T_REAGDEPOACC,@PMMsg.T_REAGDEPOPART);
      end;
    end;
  
    PMMsg.T_SUMNET = rq.rec.Amount; 
    FillPartyShort(ClientID, PTCK_SWIFT, NULL, @PMMsg.T_CLIENT);                   //  Краткое наименование клиента  ЦБ по сделке             
   
  end;

    
end;


// заполнение данными по каждой сделке
macro ПроверитьОсновнойСпособПолученияСвифт( DocKind, NettingID  )   //DL_NETT
  var query, DataSet, cmd;
  query = "  Select gen.* From ddl_genagr_dbt gen Where gen.t_GenagrID IN("
        +  " SELECT tick.t_GenagrID FROM  ddl_tick_dbt tick WHERE tick.t_DealID IN( SELECT rq.t_DocID FROM ddlrq_dbt rq, dpmlink_dbt link WHERE"
        +  " link.t_DocKind = ?"                   //DocKind
        +  " AND link.t_DocumentID = ? "           //NettingID
        +  " AND link.t_LinkKind = 12 "            //PMLINK_KIND_RQNETTING
        +  " AND link.t_InitialPayment = rq.t_ID "
        +  " AND rq.t_DocKind = 101 )  AND tick.t_BofficeKind = 101"
        +  " UNION SELECT nett.t_GenagrID  FROM ddl_nett_dbt nett WHERE nett.t_NettingID = ?) "            
        +  " AND gen.t_ConfirmMethod = 0 "            
        +  " AND ROWNUM = 1 " ;           

  cmd = RSDCommand(query);
  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, DocKind         );
  cmd.addParam( "", RSDBP_IN, NettingID       );
  cmd.addParam( "", RSDBP_IN, NettingID       );
  cmd.Execute();
  DataSet = TRsbDataSet(cmd);
 
  if( DataSet.moveNext() )
   return 1;
  end;
  file dlnet("dl_nett");
  var AttrID = 0;

  ClearRecord(dlnet);
  dlnet.NettingID = NettingID;
  if ( not getEQ(dlnet) )
    msgbox( "Не найдена сделка неттинга с параметром NettingID = " + NettingID );
    return 1;
  end;

  if( GetMainObjAttr( null, OBJTYPE_NETTING, UniID(dlnet, OBJTYPE_NETTING), 14 , AttrID ) )
    if(AttrID == 1)
    return 1;
    end;
  end;

  return 0;
end;

private macro PreparePaymentsMessageNett(BUFF, KindMsg, Oper, Department, ErrText:@variant)

  var stat = ClearPaymentsMessageData();

  var query, DataSet, cmd;
  var queryD, DataSetD, cmdD;
  var PMMsg = CPMMesDL(BUFF.NettingID);                      // инициализация
  stat = FillPaymentsMessageData( BUFF, PMMsg );         // заполнение данными, одинаковыми для всех
  var Kind = 7;                                            //DraftKind в Payments
  var tick = TRecHandler("dl_tick");
  Count = 0;
  KindMsg = "MT599";

// все сделки БОЦБ по операции неттинга
  query = " SELECT rq.t_DocID DocID,rq.t_DealPart Part FROM ddlrq_dbt rq, dpmlink_dbt link WHERE"
      +  " link.t_DocKind = ?"                   //DocKind
      +  " AND link.t_DocumentID = ? "           //NettingID
      +  " AND link.t_LinkKind = 12 "            //PMLINK_KIND_RQNETTING
      +  " AND link.t_InitialPayment = rq.t_ID "
      +  " AND rq.t_DocKind = 101 " ;           //DL_SECURITYDOC      PMLINK
  cmd = RSDCommand(query);
  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, BUFF.DocKind         );
  cmd.addParam( "", RSDBP_IN, BUFF.NettingID       );
  cmd.Execute();
  DataSet = TRsbDataSet(cmd);
 
  while( DataSet.moveNext() )

    queryD = " SELECT tick.* FROM  ddl_tick_dbt tick WHERE tick.t_DealID = ? "
           + " AND tick.t_BofficeKind = 101" ;           
    cmdD = RSDCommand(queryD);
    cmdD.NullConversion = true;
    cmdD.addParam( "", RSDBP_IN, DataSet.DocID         );
    cmdD.Execute();
    DataSetD = TRsbDataSet(cmdD);
    if( DataSetD.moveNext() )
    
      Count = Count + 1;
      tick.clear();
      DataSetD.GetRecord().CopyTo(tick.rec);
      stat = FillPaymentsMessageDataTick( BUFF, PMMsg, tick, DataSet.Part );         // заполнение данными по каждой сделке
    
      if(NOT stat)
        stat = InsertPaymentsMessageDataNett(PMMsg);
      end;
    end;

  end;
  stat = DL_CreatePaymentsMessageSECNET(Kind, BUFF.NettingID, KindMsg, Oper, Department, BUFF.DocKind ,BUFF.Contractor, 2);     // пока 2- не по второй части Репо
  return stat;
end;

/*
Получение значения настройки "Транспортная система обработки сообщений"
*/
private macro GetTransportBO( Transport )
  var err;
  var TransportTmp;

  GetRegistryValue( "SECUR\\TRANSPORT", V_INTEGER, TransportTmp, err );
  if( NOT err )
    SetParm( 0, TransportTmp );
  else
    SetParm( 0, 0 );
     msgbox( "Ошибка поиска настройки \"Транспортная система обработки сообщений\"" );

  end;

  return err;
end;

/* формирование исходящего инф. сообщения на шаге операции */
macro DL_CreateMsgInfNett( BUFF )
  var err = 0, KindMsg,  Transport, ErrText,  stat;

  err = GetTransportBO(Transport);

  if(NOT err)
    if(Transport == 2)

      PreparePaymentsMessageNett(BUFF, KindMsg, {Oper}, {NumDprt}, @ErrText);
    else 
       msgbox( "Настройка \"Транспортная система обработки сообщений\" не установлена" );
       err = 1;
    end;
  end;
  return err;
end;
