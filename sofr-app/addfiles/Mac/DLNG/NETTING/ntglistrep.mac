/*
$Name:         ntglistrep.mac
$Module:       МБК и КО
$Description:  Отчет "Лист неттинга"
PROGRAMMED BY: Куперин М.В.
CREATION DATE: 25.11.2009
*/
IMPORT FIInter, PTInter, RSD, RsbDataSet, Календарь, globals, "or_tpl_h.mac";
/****************************************************************************************/
PRIVATE VAR
    Filter = NULL;

PRIVATE CONST
    REPFLD_DATE   = 0,
    REPFLD_FILTER = 1,
    REPFLD_LAST   = 2;
/****************************************************************************************/
PRIVATE CLASS FilterParm
  private const
      REPFLD_FLAGFIID1    = 0,
      REPFLD_ISOFIID1     = 1,
      REPFLD_NAMEFIID1    = 2,
      REPFLD_FLAGFIID2    = 3,
      REPFLD_ISOFIID2     = 4,
      REPFLD_NAMEFIID2    = 5,
      REPFLD_FLAGMARKET   = 6,
      REPFLD_MARKET       = 7,
      REPFLD_FLAGDOCKIND  = 8,
      REPFLD_DOCKIND      = 9,
      REPFLD_DOCKINDNAME  = 10,
      REPFLD_FLAGOPKIND   = 11,
      REPFLD_OPKIND       = 12,
      REPFLD_OPKINDNAME   = 13,
      REPFLD_FLAGBASEFIID = 14,
      REPFLD_ISOBASEFIID  = 15,
      REPFLD_NAMEBASEFIID = 16,
      REPFLD_FLAGPAYMFIID = 17,
      REPFLD_ISOPAYMFIID  = 18,
      REPFLD_NAMEPAYMFIID = 19,
      REPFLD_FLAGPMPURP   = 20,
      REPFLD_NAMEPMPURP   = 21,
      REPFLD_FLAGCORSHEM  = 22,
      REPFLD_CORSHEM      = 23,
      REPFLD_FLAGNET      = 24,
      REPFLD_FLAGDLVKIND  = 25,
      REPFLD_DLVKIND      = 26,
      REPFLD_NAMEDLVKIND  = 27,
      REPFLD_FLAGOPER     = 28,
      REPFLD_OPER         = 29,
      REPFLD_NAMEOPER     = 30,
      REPFLD_FLAGDEP      = 31,
      REPFLD_DEP          = 32;

  var
      flagFIID1     = "", flagFIID2     = "", flagMarket    = "", flagDocKind   = "",
      flagOpKind    = "", flagBaseFIID  = "", flagPaymFIID  = "", flagPmPurp    = "",
      flagCorschNmb = "", flagNetting   = "", flagDelivKind = "", flagOper      = "",
      flagDep       = "";
  var
      FIID1     = NULL, FIID2     = NULL, Market    = NULL, DocKind   = NULL,
      OpKind    = NULL, BaseFIID  = NULL, PaymFIID  = NULL, PmPurp    = NULL,
      CorschNmb = NULL, Netting   = NULL, DelivKind = NULL, Oper      = NULL,
      Dep       = NULL, ValueDate = {curdate};

  private macro InitDefaultValue(fldnum)
      if (fldnum == REPFLD_FLAGFIID1)
          FIID1 = NATCUR;
      elif (fldnum == REPFLD_FLAGFIID2)
          FIID2 = NATCUR;
      elif (fldnum == REPFLD_FLAGMARKET)
          if (flagMarket == "X")
          if (StrFor(GetIdentProgram()) == "J")
              Market = 102;
          elif (StrFor(GetIdentProgram()) == "V")
              Market = 100;
          end;
          else
              Market = 0;
          end;
      elif (fldnum == REPFLD_FLAGDOCKIND)
          DocKind = NULL;
      elif (fldnum == REPFLD_FLAGOPKIND)
          OpKind = NULL;
      elif (fldnum == REPFLD_FLAGBASEFIID)
          BaseFIID = NATCUR;
      elif (fldnum == REPFLD_FLAGPAYMFIID)
          PaymFIID = NATCUR;
      elif (fldnum == REPFLD_FLAGPMPURP)
          PmPurp  = NULL;
      elif (fldnum == REPFLD_FLAGCORSHEM)
          CorschNmb = NULL;
      elif (fldnum == REPFLD_FLAGNET)
          Netting = NULL;
      elif (fldnum == REPFLD_FLAGDLVKIND)
          DelivKind = NULL;
      elif (fldnum == REPFLD_FLAGOPER)
          Oper = {oper};
      elif (fldnum == REPFLD_FLAGDEP)
          Dep = {OperDprt};
      else
          return;
      end;
  end;

  private macro CheckedDefault()
          flagMarket = "X";
          InitDefaultValue(REPFLD_FLAGMARKET);
  end;
  
  private macro Clear(fldnum)
      if (fldnum == REPFLD_FLAGFIID1)
          flagFIID1 = "";
      elif (fldnum == REPFLD_FLAGFIID2)
          flagFIID2 = "";
      elif (fldnum == REPFLD_FLAGMARKET)
          flagMarket = "";
      elif (fldnum == REPFLD_FLAGDOCKIND)
          flagDocKind = "";
      elif (fldnum == REPFLD_FLAGOPKIND)
          flagOpKind = "";
      elif (fldnum == REPFLD_FLAGBASEFIID)
          flagBaseFIID = "";
      elif (fldnum == REPFLD_FLAGPAYMFIID)
          flagPaymFIID = "";
      elif (fldnum == REPFLD_FLAGPMPURP)
          flagPmPurp = "";
      elif (fldnum == REPFLD_FLAGCORSHEM)
          flagCorschNmb = "";
      elif (fldnum == REPFLD_FLAGNET)
          flagNetting = "";
      elif (fldnum == REPFLD_FLAGDLVKIND)
          flagDelivKind = "";
      elif (fldnum == REPFLD_FLAGOPER)
          flagOper = "";
      elif (fldnum == REPFLD_FLAGDEP)
          flagDep = "";
      else
          return;
      end;

      InitDefaultValue(fldnum);
  end;

  private macro PressSpace(Pn, ID)
      if (Pn(ID) == "")
          Pn(ID) = "X";
      elif (Pn(ID) == "X")
          Pn(ID) = "";
      end;
      if (ID == REPFLD_FLAGFIID1)
          flagFIID1 = Pn(ID);
      elif (ID == REPFLD_FLAGFIID2)
          flagFIID2 = Pn(ID);
      elif (ID == REPFLD_FLAGMARKET)
          flagMarket = Pn(ID);
      elif (ID == REPFLD_FLAGDOCKIND)
          flagDocKind = Pn(ID);
      elif (ID == REPFLD_FLAGOPKIND)
          flagOpKind = Pn(ID);
      elif (ID == REPFLD_FLAGBASEFIID)
          flagBaseFIID = Pn(ID);
      elif (ID == REPFLD_FLAGPAYMFIID)
          flagPaymFIID = Pn(ID);
      elif (ID == REPFLD_FLAGPMPURP)
          flagPmPurp = Pn(ID);
      elif (ID == REPFLD_FLAGCORSHEM)
          flagCorschNmb = Pn(ID);
      elif (ID == REPFLD_FLAGNET)
          flagNetting = Pn(ID);
      elif (ID == REPFLD_FLAGDLVKIND)
          flagDelivKind = Pn(ID);
      elif (ID == REPFLD_FLAGOPER)
          flagOper = Pn(ID);
      elif (ID == REPFLD_FLAGDEP)
          flagDep = Pn(ID);
      end;
  end;

  private macro SelectScroll(Query, Header, x, y /*...*/)
      var rs     = RsdRecordset(Query, NULL, RSDVAL_STATIC),
          Column = TArray(),
          i, ColumnValue, ColumnName, NumColumns = 0;

    macro EvProc(rs, cmd, id, key)
        if ((cmd == DLG_KEY)and(key == 13))
            if (rs.RecCount > 0)
                return CM_SELECT;
            end;
        end;
        return CM_DEFAULT;
    end;
    
    macro AddColumn(ar,ind, fld, head)
        ar.value( ind * 6 + 0 ) = fld;
        ar.value( ind * 6 + 1 ) = head;
        ar.value( ind * 6 + 2 ) = NULL;
        ar.value( ind * 6 + 3 ) = 2;
        ar.value( ind * 6 + 4 ) = NULL;
        ar.value( ind * 6 + 5 ) = 0;
        NumColumns = NumColumns + 1;
    end;

      i = 5;
      while (GetParm(i, ColumnValue)and(GetParm(i+1, ColumnName)))              
         AddColumn( Column, NumColumns, ColumnValue, ColumnName );
         i = i + 2;
      end;   
  
      if (RunScroll(rs, NumColumns, Column, NULL, @EvProc, Header, "Esc Выход F4 Поиск Enter Выбор", true, x, y, NULL, 25))
          return rs;
      end;

      return NULL;
  end;

  private macro PumpFI(FIID, Pn, fldCode, fldName)
      var 
          FI = TRecHandler("fininstr.dbt");
      if ((FIID != NULL)and(ПолучитьФинИн(FIID, FI) == 0))
          Pn(fldCode) = FI.rec.FI_Code;
          Pn(fldName) = FI.rec.Name;
      else
          Pn(fldCode) = "";
          Pn(fldName) = "";
      end;
  end;

  private macro PumpDepartment(DepID, Pn, fldCode)
      if (DepID != NULL)
          Pn(fldCode) = DepID;
      else
          Pn(fldCode) = "";
      end;
  end;

  private macro PumpOper(OperID, Pn, fldCode, fldName);
      var DataSet;
      if (OperID != NULL)
          DataSet= TRsbDataSet("select t_name from dperson_dbt where t_oper = " + OperID);
          if(DataSet.MoveNext())
              Pn(fldCode) = OperID;
              Pn(fldName) = DataSet.Name;
          end;
      else
          Pn(fldCode) = 0;
          Pn(fldName) = "";
      end;
  end;

  private macro PumpCorschem(SchemID, Pn, fldName)
      var DataSet;
      if (SchemID != NULL)
          DataSet= TRsbDataSet("select t_name from dcorschem_dbt where t_number = " + SchemID);
          if(DataSet.MoveNext())
              Pn(fldName) = DataSet.Name;
          end;
      else
          Pn(fldName) = "";
      end;
  end;

  private macro PumpDlvKind(DelivKindID, Pn, fldCode, fldName);
      var DataSet;
      if (DelivKindID != NULL)
          DataSet= TRsbDataSet("select t_shortname from dpmdlvk_dbt where t_deliverykind = " + DelivKindID);
          if(DataSet.MoveNext())
              Pn(fldCode) = DelivKindID;
              Pn(fldName) = DataSet.ShortName;
          end;
      else
          Pn(fldCode) = 0;
          Pn(fldName) = "";
      end;
  end;

  private macro PumpPurpose(PurpID, Pn, fldName)
      var DataSet;
      if (PurpID != NULL)
          DataSet= TRsbDataSet("select t_shortname from dpmpurp_dbt where t_purpose = " + PurpID);
          if(DataSet.MoveNext())
              Pn(fldName) = DataSet.ShortName;
          end;
      else
          Pn(fldName) = "";
      end;
  end;

  private macro PumpDocKind(DocKindID, Pn, fldCode, fldName);
      var DataSet;
      if (DocKindID != NULL)
          DataSet= TRsbDataSet("select t_name from doprkdoc_dbt where t_dockind = " + DocKindID);
          if(DataSet.MoveNext())
              Pn(fldCode) = DocKindID;
              Pn(fldName) = DataSet.Name;
          end;
      else
          Pn(fldCode) = 0;
          Pn(fldName) = "";
      end;
  end;

  private macro PumpOpKind(OpKindID, Pn, fldCode, fldName);
      var DataSet;
      if (OpKindID != NULL)
          DataSet= TRsbDataSet("select t_name from doprkoper_dbt where t_kind_operation = " + OpKindID);
          if(DataSet.MoveNext())
              Pn(fldCode) = OpKindID;
              Pn(fldName) = DataSet.Name;
          end;
      else
          Pn(fldCode) = 0;
          Pn(fldName) = "";
      end;
  end;

  private macro PumpMarket(MarketID, Pn, fldName)
      if (MarketID == 102)
          Pn(fldName) = "МБК";
      elif (MarketID == 100)
          Pn(fldName) = "КО";
      else
          Pn(fldName) = "";
      end;
  end;

  private macro EnDisFields(Pn, ID)
      if (ID == REPFLD_FLAGFIID1)
          if (Pn(ID) == "X")
              EnableFields(Pn, REPFLD_ISOFIID1);
              EnableFields(Pn, REPFLD_NAMEFIID1);
          else
              Clear(REPFLD_FLAGFIID1);
              DisableFields(Pn, REPFLD_ISOFIID1);
              DisableFields(Pn, REPFLD_NAMEFIID1);
          end;
          PumpFI(FIID1, Pn, REPFLD_ISOFIID1, REPFLD_NAMEFIID1);
      elif (ID == REPFLD_FLAGFIID2)
          if (Pn(ID) == "X")
              EnableFields(Pn, REPFLD_ISOFIID2);
              EnableFields(Pn, REPFLD_NAMEFIID2);
          else
              Clear(REPFLD_FLAGFIID2);
              DisableFields(Pn, REPFLD_ISOFIID2);
              DisableFields(Pn, REPFLD_NAMEFIID2);
          end;
          PumpFI(FIID2, Pn, REPFLD_ISOFIID2, REPFLD_NAMEFIID2);
      elif (ID == REPFLD_FLAGMARKET)
          if (Pn(ID) == "X")
              EnableFields(Pn, REPFLD_MARKET);
          else
              Clear(REPFLD_FLAGMARKET);
              DisableFields(Pn, REPFLD_MARKET);
          end;
          PumpMarket(Market, Pn, REPFLD_MARKET);
      elif (ID == REPFLD_FLAGDOCKIND)
          if (Pn(ID) == "X")
              EnableFields(Pn, REPFLD_DOCKIND);
              EnableFields(Pn, REPFLD_DOCKINDNAME);
          else
              Clear(REPFLD_FLAGDOCKIND);
              DisableFields(Pn, REPFLD_DOCKIND);
              DisableFields(Pn, REPFLD_DOCKINDNAME);
          end;
          PumpDocKind(DocKind, Pn, REPFLD_DOCKIND, REPFLD_DOCKINDNAME);
      elif (ID == REPFLD_FLAGOPKIND)
          if (Pn(ID) == "X")
              EnableFields(Pn, REPFLD_OPKIND);
              EnableFields(Pn, REPFLD_OPKINDNAME);
          else
              Clear(REPFLD_FLAGOPKIND);
              DisableFields(Pn, REPFLD_OPKIND);
              DisableFields(Pn, REPFLD_OPKINDNAME);
          end;
          PumpOpKind(OpKind, Pn, REPFLD_OPKIND, REPFLD_OPKINDNAME);
      elif (ID == REPFLD_FLAGBASEFIID)
          if (Pn(ID) == "X")
              EnableFields(Pn, REPFLD_ISOBASEFIID);
              EnableFields(Pn, REPFLD_NAMEBASEFIID);
          else
              Clear(REPFLD_FLAGBASEFIID);
              DisableFields(Pn, REPFLD_ISOBASEFIID);
              DisableFields(Pn, REPFLD_NAMEBASEFIID);
          end;
          PumpFI(BaseFIID, Pn, REPFLD_ISOBASEFIID, REPFLD_NAMEBASEFIID);
      elif (ID == REPFLD_FLAGPAYMFIID)
          if (Pn(ID) == "X")
              EnableFields(Pn, REPFLD_ISOPAYMFIID);
              EnableFields(Pn, REPFLD_NAMEPAYMFIID);
          else
              Clear(REPFLD_FLAGPAYMFIID);
              DisableFields(Pn, REPFLD_ISOPAYMFIID);
              DisableFields(Pn, REPFLD_NAMEPAYMFIID);
          end;
          PumpFI(PaymFIID, Pn, REPFLD_ISOPAYMFIID, REPFLD_NAMEPAYMFIID);
      elif (ID == REPFLD_FLAGPMPURP)
          if (Pn(ID) == "X")
              EnableFields(Pn, REPFLD_NAMEPMPURP);
          else
              Clear(REPFLD_FLAGPMPURP);
              DisableFields(Pn, REPFLD_NAMEPMPURP);
          end;
          PumpPurpose(PmPurp, Pn, REPFLD_NAMEPMPURP);
      elif (ID == REPFLD_FLAGCORSHEM)
          if (Pn(ID) == "X")
              EnableFields(Pn, REPFLD_CORSHEM);
          else
              Clear(REPFLD_FLAGCORSHEM);
              DisableFields(Pn, REPFLD_CORSHEM);
          end;
          PumpCorschem(CorschNmb, Pn, REPFLD_CORSHEM);
      elif (ID == REPFLD_FLAGNET)
          if (Pn(ID) == "X")
              Netting   = "X";
          else
              Clear(REPFLD_FLAGNET);
          end;
      elif (ID == REPFLD_FLAGDLVKIND)
          if (Pn(ID) == "X")
              EnableFields(Pn, REPFLD_DLVKIND);
              EnableFields(Pn, REPFLD_NAMEDLVKIND);
          else
              Clear(REPFLD_FLAGDLVKIND);
              DisableFields(Pn, REPFLD_DLVKIND);
              DisableFields(Pn, REPFLD_NAMEDLVKIND);
          end;
          PumpDlvKind(DelivKind, Pn, REPFLD_DLVKIND, REPFLD_NAMEDLVKIND);
      elif (ID == REPFLD_FLAGOPER)
          if (Pn(ID) == "X")
              EnableFields(Pn, REPFLD_OPER);
              EnableFields(Pn, REPFLD_NAMEOPER);
          else
              Clear(REPFLD_FLAGOPER);
              DisableFields(Pn, REPFLD_OPER);
              DisableFields(Pn, REPFLD_NAMEOPER);
          end;
          PumpOper(Oper, Pn, REPFLD_OPER, REPFLD_NAMEOPER);
      elif (ID == REPFLD_FLAGDEP)
          if (Pn(ID) == "X")
              EnableFields(Pn, REPFLD_DEP);
          else
              Clear(REPFLD_FLAGDEP);
              DisableFields(Pn, REPFLD_DEP);
          end;
          PumpDepartment(Dep, Pn, REPFLD_DEP);
      end;
  end;

  private macro ProcessFilter(Pn, Cmd, ID, Key)
      var parm = NULL;
      if(Cmd == DLG_INIT)
          Pn(REPFLD_FLAGFIID1)    = flagFIID1;
          Pn(REPFLD_FLAGFIID2)    = flagFIID2;
          Pn(REPFLD_FLAGMARKET)   = flagMarket;
          Pn(REPFLD_FLAGDOCKIND)  = flagDocKind;
          Pn(REPFLD_FLAGOPKIND)   = flagOpKind;
          Pn(REPFLD_FLAGBASEFIID) = flagBaseFIID;
          Pn(REPFLD_FLAGPAYMFIID) = flagPaymFIID;
          Pn(REPFLD_FLAGPMPURP)   = flagPmPurp;
          Pn(REPFLD_FLAGCORSHEM)  = flagCorschNmb;
          Pn(REPFLD_FLAGNET)      = flagNetting;
          Pn(REPFLD_FLAGDLVKIND)  = flagDelivKind;
          Pn(REPFLD_FLAGOPER)     = flagOper;
          Pn(REPFLD_FLAGDEP)      = flagDep;
          EnDisFields(Pn, REPFLD_FLAGFIID1);
          EnDisFields(Pn, REPFLD_FLAGFIID2);
          EnDisFields(Pn, REPFLD_FLAGMARKET);
          EnDisFields(Pn, REPFLD_FLAGDOCKIND);
          EnDisFields(Pn, REPFLD_FLAGOPKIND);
          EnDisFields(Pn, REPFLD_FLAGBASEFIID);
          EnDisFields(Pn, REPFLD_FLAGPAYMFIID);
          EnDisFields(Pn, REPFLD_FLAGPMPURP);
          EnDisFields(Pn, REPFLD_FLAGCORSHEM);
          EnDisFields(Pn, REPFLD_FLAGNET);
          EnDisFields(Pn, REPFLD_FLAGDLVKIND);
          EnDisFields(Pn, REPFLD_FLAGOPER);
          EnDisFields(Pn, REPFLD_FLAGDEP);
          UpdateFields(Pn);
      elif((Cmd == DLG_KEY) and (Key == 32))
          if ((ID == REPFLD_FLAGFIID1)or
              (ID == REPFLD_FLAGFIID2)or
              (ID == REPFLD_FLAGMARKET)or
              (ID == REPFLD_FLAGDOCKIND)or
              (ID == REPFLD_FLAGOPKIND)or
              (ID == REPFLD_FLAGBASEFIID)or
              (ID == REPFLD_FLAGPAYMFIID)or
              (ID == REPFLD_FLAGPMPURP)or
              (ID == REPFLD_FLAGCORSHEM)or
              (ID == REPFLD_FLAGNET)or
              (ID == REPFLD_FLAGDLVKIND)or
              (ID == REPFLD_FLAGOPER)or
              (ID == REPFLD_FLAGDEP))
              PressSpace(Pn, ID);
              EnDisFields(Pn, ID);
              UpdateFields(Pn);
          end;
      elif((Cmd == DLG_KEY) and (Key == 317))
          if (ID == REPFLD_ISOFIID1)
              parm = TRecHandler("fininstr.dbt");
              if (ListFI (FIKIND_CURRENCY, 0, parm))
                  FIID1 = parm.rec.FIID;
                  Pn(REPFLD_ISOFIID1) = parm.rec.FI_Code;
                  Pn(REPFLD_NAMEFIID1) = parm.rec.Name;
              end;
          elif (ID == REPFLD_ISOFIID2)
              parm = TRecHandler("fininstr.dbt");
              if (ListFI (FIKIND_CURRENCY, 0, parm))
                  FIID2 = parm.rec.FIID;
                  Pn(REPFLD_ISOFIID2) = parm.rec.FI_Code;
                  Pn(REPFLD_NAMEFIID2) = parm.rec.Name;
              end;
          elif (ID == REPFLD_MARKET)
              parm = SelectScroll("select cast(t_number as number(5)) as t_number, t_name from (select 102 as t_number, cast('МБК' as varchar(15)) as t_name from dual union select 100, 'КО' from dual)","Виды рынков", 24, 0, "t_number", "Номер", "t_name", "Название");
              if (parm != NULL)
                  Market = parm.value("t_number");
                  Pn(REPFLD_MARKET) = parm.value("t_name");
              end;
          elif (ID == REPFLD_DOCKIND)
              parm = SelectScroll("select t_dockind, t_name from doprkdoc_dbt where t_primary = 'X' order by t_dockind","Виды документов", 10, 0, "t_dockind", "Номер", "t_name", "Название");
              if (parm != NULL)
                  DocKind = parm.value("t_dockind");
                  Pn(REPFLD_DOCKIND) = parm.value("t_dockind");
                  Pn(REPFLD_DOCKINDNAME) = parm.value("t_name");
              end;
          elif (ID == REPFLD_OPKIND)
              parm = SelectScroll("select t_kind_operation, t_name from doprkoper_dbt where t_kind_operation > 0  order by t_kind_operation","Виды операций", 10, 0, "t_kind_operation", "Номер", "t_name", "Название");
              if (parm != NULL)
                  OpKind = parm.value("t_kind_operation");
                  Pn(REPFLD_OPKIND) = parm.value("t_kind_operation");
                  Pn(REPFLD_OPKINDNAME) = parm.value("t_name");
              end;
          elif (ID == REPFLD_ISOBASEFIID)
              parm = TRecHandler("fininstr.dbt");
              if (ListFI (FIKIND_CURRENCY, 0, parm))
                  BaseFIID = parm.rec.FIID;
                  Pn(REPFLD_ISOBASEFIID) = parm.rec.FI_Code;
                  Pn(REPFLD_NAMEBASEFIID) = parm.rec.Name;
              end;
          elif (ID == REPFLD_ISOPAYMFIID)
              parm = TRecHandler("fininstr.dbt");
              if (ListFI (FIKIND_CURRENCY, 0, parm))
                  PaymFIID = parm.rec.FIID;
                  Pn(REPFLD_ISOPAYMFIID) = parm.rec.FI_Code;
                  Pn(REPFLD_NAMEPAYMFIID) = parm.rec.Name;
              end;
          elif (ID == REPFLD_NAMEPMPURP)
              parm = SelectScroll("select t_purpose, t_shortname from dpmpurp_dbt order by t_purpose","Назначения платежей", 10, 0, "t_purpose", "Код", "t_shortname", "Наименование");
              if (parm != NULL)
                  PmPurp = parm.value("t_Purpose");
                  Pn(REPFLD_NAMEPMPURP) = parm.value("t_shortname");
              end;
          elif (ID == REPFLD_CORSHEM)
              parm = TRecHandler("corschem.dbt");
              if (not ListCorschem(parm, "Выбор схемы расчетов", "X"))
                  CorschNmb = parm.rec.Number;
                  Pn(REPFLD_CORSHEM)  = parm.rec.Name;
              end;
          elif (ID == REPFLD_DLVKIND)
              parm = SelectScroll("select t_shortname, t_deliverykind from dpmdlvk_dbt","Способы доставки банкнот", 10, 0, "t_deliverykind", "Код", "t_shortname", "Наименование");
              if (parm != NULL)
                  DelivKind = parm.value("t_DeliveryKind");
                  Pn(REPFLD_DLVKIND) = parm.value("t_DeliveryKind");
                  Pn(REPFLD_NAMEDLVKIND) = parm.value("t_ShortName");
              end;
          elif (ID == REPFLD_OPER)
              parm = TRecHandler("person.dbt");
              if (ListOper(parm))
                  Oper = parm.rec.Oper;
                   Pn(REPFLD_OPER)  = parm.rec.Oper;
                   Pn(REPFLD_NAMEOPER)  = parm.rec.Name;
              end;
          elif (ID == REPFLD_DEP)
              parm = TRecHandler("dp_dep.dbt");
              if (ListDepartment(parm))
                  Dep = parm.rec.Code;
                  Pn(REPFLD_DEP)  = parm.rec.Code;
              end;
          end;
          UpdateFields(Pn);
      elif((Cmd == DLG_KEY) and (Key == 316))
          return CM_SAVE;
      elif(Cmd == DLG_SAVE)
          if (FIID1 != NULL)
              flagFIID1 = Pn(REPFLD_FLAGFIID1);
          else
              Clear(REPFLD_FLAGFIID1);
          end;
          if (FIID2 != NULL)
              flagFIID2 = Pn(REPFLD_FLAGFIID2);
          else
              Clear(REPFLD_FLAGFIID2);
          end;
          if (Market != NULL)
              flagMarket = Pn(REPFLD_FLAGMARKET);
          else
              Clear(REPFLD_FLAGMARKET);
          end;
          if (DocKind != NULL)
              flagDocKind = Pn(REPFLD_FLAGDOCKIND);
          else
              Clear(REPFLD_FLAGDOCKIND);
          end;
          if (OpKind != NULL)
              flagOpKind = Pn(REPFLD_FLAGOPKIND);
          else
              Clear(REPFLD_FLAGOPKIND);
          end;
          if (BaseFIID != NULL)
              flagBaseFIID = Pn(REPFLD_FLAGBASEFIID);
          else
              Clear(REPFLD_FLAGBASEFIID);
          end;
          if (PaymFIID != NULL)
              flagPaymFIID = Pn(REPFLD_FLAGPAYMFIID);
          else
              Clear(REPFLD_FLAGPAYMFIID);
          end;
          if (PmPurp != NULL)
              flagPmPurp = Pn(REPFLD_FLAGPMPURP);
          else
              Clear(REPFLD_FLAGPMPURP);
          end;
          if (CorschNmb != NULL)
              flagCorschNmb = Pn(REPFLD_FLAGCORSHEM);
          else
              Clear(REPFLD_FLAGCORSHEM);
          end;
          if (Netting != NULL)
              flagNetting = Pn(REPFLD_FLAGNET);
          else
              Clear(REPFLD_FLAGNET);
          end;
          if (DelivKind != NULL)
              flagDelivKind = Pn(REPFLD_FLAGDLVKIND);
          else
              Clear(REPFLD_FLAGDLVKIND);
          end;
          if (Oper != NULL)
              flagOper = Pn(REPFLD_FLAGOPER);
          else
              Clear(REPFLD_FLAGOPER);
          end;
          if (Dep != NULL)
              flagDep = Pn(REPFLD_FLAGDEP);
          else
              Clear(REPFLD_FLAGDEP);
          end;
      end;

      return CM_DEFAULT;
  end;

  macro ClearFilter()
      Clear(REPFLD_FLAGFIID1);
      Clear(REPFLD_FLAGFIID2);
      Clear(REPFLD_FLAGMARKET);
      Clear(REPFLD_FLAGDOCKIND);
      Clear(REPFLD_FLAGOPKIND);
      Clear(REPFLD_FLAGBASEFIID);
      Clear(REPFLD_FLAGPAYMFIID);
      Clear(REPFLD_FLAGPMPURP);
      Clear(REPFLD_FLAGCORSHEM);
      Clear(REPFLD_FLAGNET);
      Clear(REPFLD_FLAGDLVKIND);
      Clear(REPFLD_FLAGOPER);
      Clear(REPFLD_FLAGDEP);
  end;

  macro Active()
      var retVal = "";
      if ((flagFIID1     == "X")or(flagFIID2     == "X")or(flagMarket    == "X")or
          (flagDocKind   == "X")or(flagOpKind    == "X")or(flagBaseFIID  == "X")or
          (flagPaymFIID  == "X")or(flagPmPurp    == "X")or(flagCorschNmb == "X")or
          (flagNetting   == "X")or(flagDelivKind == "X")or(flagOper      == "X")or
          (flagDep       == "X"))
          retVal ="X";
      else
          retVal ="";
      end;

      return retVal;
  end;

  macro Run()
      macro ProcessPanel(Pn, Cmd, ID, Key)
          return ProcessFilter(Pn, Cmd, ID, Key);
      end;

      record Panel("NTGFLTR", "Deals.lbr") dialog;
      return RunDialog(Panel, @ProcessPanel);
  end;

//Constructor
  ClearFilter();
  CheckedDefault();
  if ((flagFIID1 == "X")and(FIID1 == NULL))
      Clear(REPFLD_FLAGFIID1);
  end;
  if ((flagFIID2 == "X")and(FIID2 == NULL))
      Clear(REPFLD_FLAGFIID2);
  end;
  if ((flagMarket == "X")and(Market == NULL))
      Clear(REPFLD_FLAGMARKET);
  end;
  if ((flagDocKind == "X")and(DocKind == NULL))
      Clear(REPFLD_FLAGDOCKIND);
  end;
  if ((flagOpKind == "X")and(OpKind == NULL))
      Clear(REPFLD_FLAGOPKIND);
  end;
  if ((flagBaseFIID == "X")and(BaseFIID == NULL))
      Clear(REPFLD_FLAGBASEFIID);
  end;
  if ((flagPaymFIID == "X")and(PaymFIID == NULL))
      Clear(REPFLD_FLAGPAYMFIID);
  end;
  if ((flagPmPurp == "X")and(PmPurp == NULL))
      Clear(REPFLD_FLAGPMPURP);
  end;
  if ((flagCorschNmb == "X")and(CorschNmb == NULL))
      Clear(REPFLD_FLAGCORSHEM);
  end;
  if ((flagNetting == "X")and(Netting == NULL))
      Clear(REPFLD_FLAGNET);
  end;
  if ((flagDelivKind == "X")and(DelivKind == NULL))
      Clear(REPFLD_FLAGDLVKIND);
  end;
  if ((flagOper == "X")and(Oper == NULL))
      Clear(REPFLD_FLAGOPER);
  end;
  if ((flagDep == "X")and(Dep == NULL))
      Clear(REPFLD_FLAGDEP);
  end;
END;
/****************************************************************************************/
PRIVATE MACRO BodyRep(rs, Rep)
    var RepLine   = Rep.RegisterTable("BodyLine"),
        lineadd   = 0,
        separator = false,
        PrevContr = NULL;

    while (rs.MoveNext())
        if (PrevContr != rs.value("t_partyid"))
            separator = true;
        end;

        if (lineadd != 0)
            RepLine.AddStr();	
        end;

        Rep.SetValue_NameCell("T_A1", rs.value("t_shortname"));  
        Rep.SetValue_NameCell("T_A2", rs.value("t_amount1"));  
        Rep.SetValue_NameCell("T_A3", rs.value("t_amount2"));  
        Rep.SetValue_NameCell("T_A4", rs.value("t_amount3"));  
        Rep.SetValue_NameCell("T_A5", rs.value("t_amount4"));  
        Rep.SetValue_NameCell("T_A6", rs.value("t_currency"));  

        separator = false;
        lineadd = lineadd + 1;
        PrevContr = rs.value("t_partyid");
    end;
END;

PRIVATE MACRO FooterRep1(rs, Rep)
    var RezLine1 =Rep.RegisterTable("RezLine1"),
        lineadd  = 0;

    while (rs.MoveNext())
        if (lineadd != 0)	
            RezLine1.AddStr();
        end;

        Rep.SetValue_NameCell("OrderDeal",   rs.value("t_amount1"));   
        Rep.SetValue_NameCell("EngageDeal",  rs.value("t_amount2"));
        Rep.SetValue_NameCell("OrderTotal",  rs.value("t_amount3"));
        Rep.SetValue_NameCell("EngageTotal", rs.value("t_amount4"));
        Rep.SetValue_NameCell("Carry",       rs.value("t_currency"));

        lineadd = lineadd + 1;
    end;
END;

PRIVATE MACRO FooterRep2(rs, Rep)
    var RezLine2 =Rep.RegisterTable("RezLine2"),
        lineadd  = 0, addtonetto = true;

    while (rs.MoveNext())
        if ((rs.value("t_amount5") == 0)and(rs.value("t_amount6") == 0))
            addtonetto = false;
        end;

        if (lineadd != 0)	
             if (addtonetto)
                RezLine2.AddStr();
            end;
        end;

        if (addtonetto)
            Rep.SetValue_NameCell("OrderTotal2",  rs.value("t_amount5"));
            Rep.SetValue_NameCell("EngageTotal2", rs.value("t_amount6"));
            Rep.SetValue_NameCell("Carry2",       rs.value("t_currency"));
        end;

        lineadd = lineadd + 1;
        addtonetto = true;
    end;
END;

PRIVATE MACRO PrintReport(Query)
    var Rep:CTemplateXLS = CTemplateXLS(),
        rs = NULL;   

    Rep.OpenTemplate("Report_ntglist.xls", false);

    Rep.SetValue_NameCell("ValDate", Date(Filter.ValueDate));    
    if (Filter.Market == 102)
        Rep.SetValue_NameCell("Market", "Рынок: " +"МБК");
    elif (Filter.Market == 100)
        Rep.SetValue_NameCell("Market", "Рынок: " +"КО");
    end;

    rs = RsdRecordset(Query);
    BodyRep(rs, Rep);

    rs = RsdRecordset("select sum(t_amount1) as t_amount1, sum(t_amount2) as t_amount2, sum(t_amount3) as t_amount3, sum(t_amount4) as t_amount4,"
                       " t_currency from (" + Query + ") q1 group by t_currency");
    FooterRep1(rs, Rep);
	
    rs = RsdRecordset("select (case when sum(t_amount3) > sum(t_amount4) then (sum(t_amount3) - sum(t_amount4)) else 0 end) as t_amount5, "
                       "(case when sum(t_amount3) < sum(t_amount4) then (sum(t_amount4) - sum(t_amount3)) else 0 end) as t_amount6, "
                       " t_currency from (" + Query + ") q1 group by t_currency");
    FooterRep2(rs, Rep);

    Rep.SaveAsTemplate(NULL, true);
END;
/****************************************************************************************/
PRIVATE MACRO ProcessPanel(Pn, Cmd, ID, Key)
    var Query = "";

    if(Cmd == DLG_INIT)
        Pn(REPFLD_DATE)   = Filter.ValueDate;
        Pn(REPFLD_FILTER) = Filter.Active();
        UpdateFields(Pn);
    elif((Cmd == DLG_KEY) and (Key == 32))
        if (ID == REPFLD_FILTER)
            if (Pn(ID) == "")
                Filter.Run();
                Pn(ID) = Filter.Active();
            elif (Pn(ID) == "X")
                Pn(ID) = "";
                Filter.ClearFilter();
            end;
            UpdateFields(Pn);
        end;
    elif((Cmd == DLG_KEY)and(Key == 317)and(ID == REPFLD_DATE))
        Pn(ID) = GetDateByCalendar(Pn(ID)); 
        UpdateFields(Pn);
    elif((Cmd == DLG_KEY)and(Key == 18)and(ID == REPFLD_DATE))
        Pn(ID) = {curdate}; 
        UpdateFields(Pn);
    elif((Cmd == DLG_KEY)and(Key == 319))
        Filter.Run();
        Pn(REPFLD_FILTER) = Filter.Active();
        UpdateFields(Pn);
    elif((Cmd == DLG_KEY) and ((Key == 316)or(Key == 323)or(Key == 13)))
        if ((Key == 13)and(ID != (REPFLD_LAST - 1)))
            return CM_DEFAULT;
        end;
        return CM_SAVE;
    elif(Cmd == DLG_SAVE)
        Filter.ValueDate = Pn(REPFLD_DATE);
        Query = "select party.t_partyid, party.t_shortname, repdata.t_amount1, repdata.t_amount2, "
                "(case when repdata.t_amount1 > repdata.t_amount2 then (repdata.t_amount1 - repdata.t_amount2) else 0 end) as t_amount3, "
                "(case when repdata.t_amount1 < repdata.t_amount2 then (repdata.t_amount2 - repdata.t_amount1) else 0 end) as t_amount4, "
                "fininstr.t_ccy as t_currency "
                "from dparty_dbt party, dfininstr_dbt fininstr, "
                "( "
                "  select listpaym.t_partyid, listpaym.t_fiid, "
                "  sum(decode(listpaym.t_payer, " + {OurBank} + ", 0, listpaym.t_amount)) as t_amount1, "
                "  sum(decode(listpaym.t_receiver, " + {OurBank} + ", 0, listpaym.t_amount)) as t_amount2 "
                "  from "
                "  ( "
                "    select distinct paym.t_paymentid, party.t_partyid, paym.t_amount, paym.t_fiid, paym.t_payer, paym.t_receiver, "
                "    decode(tick.t_typedoc, 'B',"
                "        case paym.t_purpose"
                "            when 1 then paym.t_payeraccount"
                "            when 2 then paym.t_receiveraccount"
                "            when 3 then paym.t_receiveraccount"
                "            when 4 then paym.t_payeraccount"
                "            else ''"
                "        end, "
                "        case paym.t_purpose"
                "            when 1 then paym.t_receiveraccount"
                "            when 2 then paym.t_payeraccount"
                "            when 3 then paym.t_payeraccount"
                "            when 4 then paym.t_receiveraccount"
                "            else ''"
                "        end"
                "    ) dealacc, paym.t_chapter"
                "    from dpmpaym_dbt paym, dparty_dbt party, ddl_tick_dbt tick, dpmprop_dbt pmprop, ddl_leg_dbt leg "
                "    where "
                "      ((paym.t_payer = party.t_partyid)or(paym.t_receiver = party.t_partyid))"
                "      and(tick.t_dealid = paym.t_documentid)"
                "      and(tick.t_dealstatus <> 20)"
                "      and(tick.t_bofficekind in (100, 102))"
                "      and(pmprop.t_paymentid = paym.t_paymentid)"
                "      and(leg.t_dealid = tick.t_dealid)"
                "      and(leg.t_legkind = 0)"
                "      and(party.t_partyid <> " + {OurBank} + ")"
                "      and(paym.t_valuedate = '" + Date(Filter.ValueDate) + "')";

        if (Filter.flagFIID1 == "X")
            Query = Query + "and((leg.t_cfi = " + Filter.FIID1 + ")or(leg.t_pfi =" + Filter.FIID1 + "))";
        end;
        if (Filter.flagFIID2 == "X")
            Query = Query + "and((leg.t_cfi = " + Filter.FIID2 + ")or(leg.t_pfi =" + Filter.FIID2 + "))";
        end;
        if (Filter.flagMarket == "X")
            Query = Query + "and(tick.t_bofficekind = " + Filter.Market + ")";
        end;
        if (Filter.flagDocKind == "X")
            Query = Query + "and(tick.t_bofficekind = " + Filter.DocKind + ")";
        end;
        if (Filter.flagOpKind == "X")
            Query = Query + "and(tick.t_dealtype = " + Filter.OpKind + ")";
        end;
        if (Filter.flagBaseFIID == "X")
            Query = Query + "and(paym.t_basefiid = " + Filter.BaseFIID + ")";
        end;
        if (Filter.flagPaymFIID == "X")
            Query = Query + "and(((paym.t_receiver = " + {OurBank} + ")and(paym.t_fiid = " + Filter.PaymFIID + "))"
                              "or((paym.t_receiver <> " + {OurBank} + ")and(paym.t_payfiid =  " + Filter.PaymFIID + ")))";
        end;
        if (Filter.flagPmPurp == "X")
            Query = Query + "and(paym.t_purpose = " + Filter.PmPurp + ")";
        end;
        if (Filter.flagCorschNmb == "X")
            Query = Query + "and(pmprop.t_corschem = " + Filter.CorschNmb + ")";
        end;
        if (Filter.flagNetting == "X")
            Query = Query + "and(paym.t_netting = '" + Filter.Netting + "')";
        end;
        if (Filter.flagDelivKind == "X")
            Query = Query + "and(paym.t_deliverykind = " + Filter.DelivKind + ")";
        end;
        if (Filter.flagOper == "X")
            Query = Query + "and(paym.t_oper = " + Filter.Oper + ")";
        end;
        if (Filter.flagDep == "X")
            Query = Query + "and(tick.t_department = " + Filter.Dep + ")";
        end;

        Query = Query +
                "  ) listpaym "
                "  where not exists("
                "  select * from daccount_dbt acnt where acnt.t_account = listpaym.dealacc and acnt.t_chapter = listpaym.t_chapter and acnt.t_code_currency = listpaym.t_fiid and acnt.t_type_account like '%А%' "
                "  )"
                "  group by listpaym.t_partyid, listpaym.t_fiid "
                ") repdata "
                "where "
                "(party.t_partyid = repdata.t_partyid)"
                "and(fininstr.t_fiid = repdata.t_fiid)"
                "and((repdata.t_amount1 > 0)or(repdata.t_amount2 > 0)) "
                "order by repdata.t_partyid, repdata.t_fiid";

        PrintReport(Query);
    end;

    return CM_DEFAULT;
END;
/****************************************************************************************/
MACRO PreparePrintReport()
    record Panel("PNREPNTG", "Deals.lbr") dialog;
    Filter = FilterParm;
    return RunDialog(Panel, @ProcessPanel);
END;

PreparePrintReport();
exit(1);
