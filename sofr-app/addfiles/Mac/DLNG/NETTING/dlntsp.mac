/*
$Name:        dlntsp.mac
$Module:      Ядро Securities
$Description: Часть неттинга, работающая только в Ценных Бумагах
*/
IMPORT BankInter, "sp_class.mac", "sp_voop.mac", "dlntlib.mac", "dlcarry.inc";

PRIVATE var dl_tick     = TBFile("dl_tick.dbt", "R", 1 );
PRIVATE var file_paym   = TBfile ("pmpaym");
PRIVATE var FD; 


PRIVATE MACRO ProcSet_VO_Code( pay, NumPaym:@Integer )
   NumPaym = NumPaym + 1;
   /*проставим в основание платежа код валютной операции, если это надо*/ 
   if( SP_SetPaymentGround( pay.GetPM_PAYM(), FD.pm_avoir.rec.BaseFIID, FD ) == false )
      return false;
   end;   
END;

MACRO Set_VO_Code(ntg)
    var  err = 0, СовместноСДепозитарием = false;
    var  DealCode = "", is_bocb_tick = true, NumPaym = 0;

    if( ntg.FI_Kind != FIKIND_AVOIRISS )

     /*найдем код финальной сделки из участвующих в неттинге*/
     DealCode = readNoteForObject(OBJTYPE_NETTING, UniID(ntg, OBJTYPE_NETTING), 2, ntg.SigningDate); 
     dl_tick.Clear();
     dl_tick.rec.BofficeKind  = DL_SECURITYDOC;
     dl_tick.rec.DealCode     = DealCode;
     if( dl_tick.GetEQ() != true )
        dl_tick.Clear();
        dl_tick.rec.BofficeKind  = DL_RETIREMENT;
        dl_tick.rec.DealCode     = DealCode;
        if( dl_tick.GetEQ() != true )
           is_bocb_tick = false;
        end;
     end;
     if( is_bocb_tick )
        FD = SPFirstDoc(dl_tick);            
        if( ForEachPMResPaym(ntg.IdentProgram, ntg.NettingID, @ProcSet_VO_Code, @NumPaym ) == false )
           return false;
        end;
     end;
    end;   
    return true;
END;

MACRO ИницПарамИсполн(Параметры, ВыполняемОбязательства, ntg)
    Параметры.НеттингЧерезТранзитныеСчета = (ntg.OverTransitAccount == "X");
    return true; 
END;

PRIVATE MACRO ПолучитьПоследнююСумму(DocKind, DocID, SumKind, _Date, FIID)
   var Sпред = $0, DataSet, Query, D = Date(0, 0, 0);

   Query = " Select t_Sum, t_Date, t_Currency "
         + "   From ddlsum_dbt  "
         + "  Where t_DocKind = " + string(DocKind)
         + "    and t_DocID   = " + string(DocID)
         + "    and t_Kind    = " + string(SumKind)
         + " Order by t_Date Desc ";
   DataSet = TRsbDataSet(Query);

   if (DataSet.MoveNext())
      Sпред = DataSet.Sum;
      if (_Date != null)
         D = SQL_ConvTypeDate(DataSet.t_Date);
         FIID = DataSet.t_Currency;
      end;
   end;

   if (_Date != null)
      SetParm(3, D);
   end;
   SetParm(4, FIID);

   return Sпред;
END;

PRIVATE VAR dlsum = TRecHandler( "DLSUM" );
PRIVATE MACRO СохранитьСуммуКомиссии( DocKind:INTEGER, DocID:INTEGER, Kind:INTEGER, CommDate:DATE, Sum:MONEY, NDS:MONEY, Currency:INTEGER ):BOOL
  if( (Sum == 0) AND (NDS == 0) )
     return true;
  end;

  dlsum.Clear();
  dlsum.rec.DocKind  = DocKind;
  dlsum.rec.DocID    = DocID;
  dlsum.rec.Kind     = Kind;
  dlsum.rec.Date     = CommDate;
  dlsum.rec.Sum      = Sum;
  dlsum.rec.NDS      = NDS;
  dlsum.rec.Currency = Currency;

  return OprUpdateDLSUM( dlsum );
END;

PRIVATE MACRO СписатьРезерв(ntg_fd)
   VAR Reserv = $0, LastDate, SaveSumma,
       pay = TBfile ("pmpaym"),
       ReservAcc = TRecHandler( "account" ),
       PC        = TRecHandler( "account" ); 

   if( FindPayment( 0, PM_PURP_NETTING, 0, DL_NTGDOC, ntg_fd.ID, true, pay ) == 0 )
      Reserv = ПолучитьПоследнююСумму( DLDOC_PAYMENT, pay.rec.PaymentID, DLSUM_KIND_RESERV_OVERDUE, LastDate );
      if( Reserv != 0 )
          if( ntg_fd.GetAccount("Резерв, договор", null, MC_OPENACC_CHECKEXIST, NATCUR, pay.rec.ValueDate, ReservAcc, true, FIROLE_RESERV_RPT ) == true )
              if( not ntg_fd.GetAccount("+РС, д/с", null, MC_OPENACC_CREATE, null, pay.rec.ValueDate, PC, null, FIROLE_BA) )
                 return false;
              end;

              if( DocCarry( 0,                 
                  1,                  /*Глава*/
                  NATCUR,           /*Валюта*/
                  ReservAcc.rec.Account,  /*Дебет*/
                  PC.rec.Account, /*Кредит*/
                  Reserv,      /*сколько*/
                  pay.rec.ValueDate,               /*За какое число*/
                  null,               /*CarryResult*/
                  "Списание резерва по просроченным требованиям.",/*Основание*/
                  null ) != 0 
              )         
                 return false;
              end;                         

              /*После списания создается нулевая сумма того же вида*/
              if( LastDate == pay.rec.ValueDate ) /*Сумма сохраняется нарастающим итогом*/
                 SaveSumma = -Reserv;
              else
                 SaveSumma = $0;
              end;
              if( not СохранитьСуммуКомиссии( DLDOC_PAYMENT, pay.rec.PaymentID, DLSUM_KIND_RESERV_OVERDUE, pay.rec.ValueDate, SaveSumma, $0, NATCUR ) )
                 return false;      
              end; 
          end;
      end;

   end;
END;
