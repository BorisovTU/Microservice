/*
$Name: scnt05.mac
$Module: Ценные бумаги
$Description: Шаг 05 Подтверждение сделки
*/

/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank v6                                          R-Style SoftLab

  Programmer  : Nikonorov Evgeny
  Description : Шаг 05 Подтверждение сделки
└───────────────────────────────────────────────────────────────────────────*/

import "ntgftagrq.mac", "ntswift.mac";

PRIVATE RECORD ntg_buff ("dl_nett");
PRIVATE FILE pmlink ("pmlink") key 1;

/* печать подтверждения */
macro PrintConfirmation( NettingID )
  var TempFile;

  message("Идет формирование подтверждения...");

  if ( not FormTagForPrintRQ(NettingID, 0, true) )
    return 1;
  end;

  TempFile = SetOutput( CreateFileName );

  DLWREPS_PrintReportFromTagFile( TempFile );
  SetOutput(NULL, false);

  return 0;
end;

MACRO ExecuteStep(Buffer, Document)
  SetBuff( ntg_buff, Document );

  ClearRecord(pmlink);
  pmlink.DocKind    = ntg_buff.DocKind;
  pmlink.DocumentID = ntg_buff.NettingID;
  if((not GetGE(pmlink)) OR (pmlink.DocKind != ntg_buff.DocKind) OR
     (pmlink.DocumentID != ntg_buff.NettingID))
     msgbox("У сделки отсутствуют ТО.| Воспользуйтесь Ctrl-P.");
     return 1;
  end;

  var method = 0;
  method = ПроверитьОсновнойСпособПолученияСвифт( ntg_buff.DocKind, ntg_buff.NettingID );
  if( method  and ( ntg_buff.BaseFIID < 0) )    // метод подтверждения- Swift
     MsgBox( "Сообщение SWIFT не может быть создано. Необходимо заполнить поле ФИ актива по текущей операции неттинга." );
     return 1;
  end;


  var AttrID = 0;
  var flag = false;
  var stat ;
  if( method )    // метод подтверждения- Swift
    if( ( not DisconnectObjAttr(OBJTYPE_NETTING,  UniID( ntg_buff, OBJTYPE_NETTING), 14) ) OR
        ( not ConnectObjAttr(stat, OBJTYPE_NETTING, UniID( ntg_buff, OBJTYPE_NETTING), 14, 1, null, null, {curdate}) ) OR
        ( stat != 0 ) )
         msgbox( "Ошибка при установке Категории <Формировать SWIFT>" );
         return 1;
      
    end;
  end;

  if( GetMainObjAttr( null, OBJTYPE_NETTING, UniID(ntg_buff, OBJTYPE_NETTING), 14 /*пока 14*/, AttrID ) )
    if(AttrID == 1)
      flag = true;
    end;
  end;
  if(flag)
    DL_CreateMsgInfNett( ntg_buff );
  end;


  return 0;
END;


/* Макрос постобработки */
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,           /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,             /* Указатель на первичный документ */
                ID_Operation,     /* Номер экземпляра операции */
                Num_Step,         /* Номер шага операции(из настроек) */
                Kind_Operation,   /* Вид операции */
                KindDoc,          /* Вид первичного документа */
                KindStep,         /* Вид шага операции */
                ID_Step)          /* Номер шага операции */

  record dl_nett(dl_nett);
  SetBuff(dl_nett, FDoc ); 

  if (errTrn OR (CommitOrRollback == 2)) 
      /* Произошла ошибка или происходит откат */
      return;
  end;

  PrintConfirmation( dl_nett.NettingID );
  
  return 0;
END;


/* Макрос печати */
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */

   file oproper("oproper") ;
   file dl_nett("dl_nett");

   oproper.ID_Operation = ID_Operation;
   if( not GetEQ(oproper) )
      MsgBox("Не найден экземпляр операции");
      return 0;
   end;

   dl_nett.NettingID = int(oproper.DocumentID);
   if( not GetEQ(dl_nett) )
      MsgBox("Не первичный документ <dl_nett.dbt>");
      return 0;
   end;

   PrintConfirmation( dl_nett.NettingID );

   exit(1); /*Для того, чтобы не выводился пустой файл */

END;
