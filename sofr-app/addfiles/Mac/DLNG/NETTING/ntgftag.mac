/*
$Name:        ntgftag.mac
$Module:      Ядро Securities
$Description: Формирование тегов для печати соглашений и подтверждений
*/
import PaymInter, FIInter, PTInter, CTInter, DealsInter, globals, dlwreps, OprInter, "adress.mac";

file dlnet("dl_nett");
file final_pmpaym(pmpaym) key 1;
file final_pmrmprop(pmrmprop);
file final_pmprop(pmprop);
file final_dltick(dl_tick) key 1;

const DOCKIND_BUYSALEREPORT     = 26,     /*договор купли-продажи*/
      DOCKIND_CONTR_FIRSTPART   = 22,     /* договор по первой части*/
      DOCKIND_CONTR_SECONDPART  = 23,     /* договор по второй части*/
      KINDDOC_CONTRBUYSALE = 483,
/*      OBJTYPE_NETTING    = 108,*/ /* сделка неттинга */
      NOTEKIND_FINALDEAL = 2, /* вид примечания - номер финальной сделки */
      CODE_BIK = "БИК", /* вид кода БИК */
      COMMITMENT  = "Commitment",     /*обязательство*/
      REQUIREMENT = "Requirement";    /*требование*/

var ConfirmHeader = "";               /*Заголовок подтверждения по-умолчанию */

private var ExistFinalPayment = false; /*Флаг: существует ли финальный платеж неттинга*/

macro StrTrimLeft( num, len )
var
   s=trim(string(num)),
   l=strlen(s);

   if(l==len)
      /*возвращаем без изменений*/;
   elif (l>len)
      /*обрезаем*/
      s=SubStr(s,l-len+1,len);          
   else
      /*дополняем*/
      s=mkstr("0",len-l)+s;
   end;

   return s;
end;

macro IIF(condition,value_true,value_false)
    if (condition)
        return value_true;
    else 
        return value_false;
    end;
end;

/* Возвращает строку 's'
   до символа 'c' 
   Например: s="abcdefg.xyz",c='.'
   Результат   "abcdefg" */
macro StrBefore( s, c )
   var p=Index(s, c);
   return IIF(p==0,s,SubStr(s,1,p-1));
end;


macro CreateFileName()
   return GetTxtFileName( "tmpout" );
end;

PRIVATE MACRO GetValueName( List, Element, Name )
   var ll = TRecHandler("llvalues.dbt");
   if( LL_FindLLVALUES( List, Element, ll ) == true )
      SetParm( 2, ll.rec.Name );
      return ll.rec.Code;
   else
      SetParm( 2, "" );
      return "";
   end;
END;


/* Делает из строки вида "Иванов Иван Иваныч" строку "Иванов И.И." */
macro CutFullName( str )
  var l = strlen(str),
      i = 0,
      res = "";

  res = StrBefore( str, " ") + " "; /*Это будет фамилия*/

  while ( i < l )
    if ( (SubStr(str,i,1) == " ") and (i+1<l) )
      if(SubStr(str,i+1,1) != " ")  /*Проверим, так как ФИО может разделятся не одним пробелом*/
        res = res + SubStr(str,i+1,1) + ".";
      end;
    end;
    i = i + 1;
  end;

  return res;
end;

/* проверка данных при печати, пустые значения заменяются прочерками */
macro CheckData( Data )
   var str;
   if( ValType( Data ) == V_DATE)
     if(Data == Date(0,0,0))
       return str = ("\"___\"" + " _________ " + "20__ ");
     else
       return Data;
     end;
   elif(ValType( Data ) == V_STRING)
     if(Data == "")
       return str = "________________________";
     else
       return Data;
     end;
   else
     if( not Data )
       return str = "________";
     else
       return Data;
     end;
   end;
end;

macro print_empty()
  println( CheckData("") );
end;


/* перевод числа в строку */
macro prinln_SumToStr( Sum, FIID )
  record fininstr(fininstr);

  if ( (Sum == 0) or (Sum == $0) ) /* нули в строку не переводим */
    print_empty();
    return;
  end;

  ClearRecord(fininstr);
  if(not ПолучитьФинИн(FIID, fininstr))
    if ( fininstr.FI_Kind == FIKIND_CURRENCY ) /*Валюта*/
      if ( FIID == NATCUR )  
        println( CheckData(RubToStrAlt( Sum, NULL, NULL, fininstr.ISO_Number )) );
      else
        println( CheckData(CurToStrAlt( Sum, NULL, NULL, fininstr.ISO_Number )) );
      end;
    else
      println( CheckData(NumToStr( Sum )) );
    end;
  end;
end;

macro convert_digit( Sum, FIID )
  record fininstr(fininstr);

  ClearRecord(fininstr);
  if(not ПолучитьФинИн(FIID, fininstr))
    if ( fininstr.FI_Kind == FIKIND_CURRENCY ) /*Валюта*/
      return Sum;
    else
      return int( double(Sum)/*/100*/ );
    end;
  end;
  return $0;
end;

/* Создает тэг для копирования строк таблицы */
macro MultipleRow(ТабНо, КолСтрок, BookMark)
  if(КолСтрок > 1)
    [~MultipleRow];
    println(ТабНо);                 /* идентификатор таблицы */
    println(КолСтрок - 1);          /* кол-во недостающих строк */
    if(BookMark != null)
       println(BookMark);
    end;
  end;
end;

/*Удаляем таблицу на которую навешена заданная закладка*/
macro DeleteTable(BookMark);
   if(BookMark != null)
     [~DeleteTable];
     println(BookMark);
   end;
end;

/* прверка является ли финальный платеж обятельством */
macro IsObligation( )
  if( final_pmpaym.Payer == {OurBank} ) /* это обязательство */
    return true;
  end;
  return false;
end;

macro FindFinalPayment()
  ClearRecord(final_pmpaym);
  final_pmpaym.DocKind    = dlnet.DocKind;
  final_pmpaym.DocumentID = dlnet.NettingID;
  if( (GetGE(final_pmpaym) == true) AND 
      (final_pmpaym.DocKind == dlnet.DocKind) AND 
      (final_pmpaym.DocumentID == dlnet.NettingID))
    ExistFinalPayment = true;       
  else
    ExistFinalPayment = false;       
  end;
end;

/* формирование тэгов по финальному платежу неттинга */
macro PaymTag()
  record Currency(fininstr);

  if(ExistFinalPayment == true)  /*Если существует финальный платеж, то заполним тэги из него*/
     ClearRecord(final_pmrmprop);
     final_pmrmprop.PaymentID = final_pmpaym.PaymentID;
     if( GetEQ(final_pmrmprop) == false )
       MsgBox( "Не найдены реквизиты платежа по контрактиву с параметром PaymentID = " + final_pmpaym.PaymentID );
       return false;
     end;

     ClearRecord(final_pmprop);
     final_pmprop.PaymentID   = final_pmpaym.PaymentID;
     if ( not IsObligation() )
       final_pmprop.DebetCredit = PRT_Debet;  // дебет
     else
       final_pmprop.DebetCredit = PRT_Credit; // кредит
     end;
     if( GetEQ(final_pmprop) == false )
       MsgBox( "Не найдены свойства платежа с параметром PaymentID = " + final_pmpaym.PaymentID );
       return false;
     end;

     println( "~Netting" );
     println( CheckData( final_pmpaym.BaseAmount ) );

     println( "~Netting_Wr" );
     prinln_SumToStr( final_pmpaym.BaseAmount,  final_pmpaym.BaseFIID );

     println( "~Netting_Avoir" );
     println( CheckData( StrBefore(string(final_pmpaym.BaseAmount),".")) );

     println( "~Netting_Avoir_Wr" );
     prinln_SumToStr( StrBefore(string(final_pmpaym.BaseAmount),"."), final_pmpaym.BaseFIID );
     
     println( "~Fi_netting" );
     if ( ПолучитьФинИн( final_pmpaym.BaseFIID, Currency ) == 0 )
       println( CheckData(Currency.Name) );
     else
       print_empty();
     end;

     println( "~Payer" );
     println( CheckData(final_pmrmprop.PayerName) );

     println( "~Receiver" );
     println( CheckData(final_pmrmprop.ReceiverName) );

     println( "~Date_pay" );
     println( CheckData(final_pmpaym.ValueDate) );

     println( "~Date_del" );
     println( CheckData(final_pmpaym.ValueDate) );
  else  
     println( "~Netting" );
     println( CheckData(0) );

     println( "~Netting_Wr" );
     prinln_SumToStr( 0,  dlnet.BaseFIID );

     println( "~Netting_Avoir" );
     println( CheckData(0) );

     println( "~Netting_Avoir_Wr" );
     prinln_SumToStr( 0, 0 );
     
     println( "~Fi_netting" );
     if ( ПолучитьФинИн( dlnet.BaseFIID, Currency ) == 0 )
       println( CheckData(Currency.Name) );
     else
       print_empty();
     end;

     println( "~Payer" );
     print_empty();

     println( "~Receiver" );
     print_empty();

     println( "~Date_pay" );
     println( CheckData(Date(0,0,0)) );

     println( "~Date_del" );
     println( CheckData(Date(0,0,0)) );
  end;
  return true;
end;

/* проверка платежа */
macro confirm( pmpaym, IsConfirm )
  /* для подтверждений по ц/б рассматриваем платежи только по БА, и БА(обр) */
  if ( ( IsConfirm == true ) and ( dlnet.FI_Kind == FIKIND_AVOIRISS )
       and ( (pmpaym.Purpose == BAi) or (pmpaym.Purpose == BRi) ) 
     )
    return true;
  end;

  /* для подтверждений по валюте рассматриваем платежи только по КА, и КА(обр) */
  if ( ( IsConfirm == true ) and ( dlnet.FI_Kind == FIKIND_CURRENCY )
       and ( (pmpaym.Purpose == CAi) or (pmpaym.Purpose == CRi) or (pmpaym.Purpose == PM_PURP_AVANCE) or (pmpaym.Purpose == PM_PURP_BACK_AVANCE) ) 
     )
    return true;
  end;

  /* для соглашений рассматриваем все платежи */
  if ( IsConfirm == false )
    return true;
  end;

  return false;
end;

/* получить цену из dl_leg.dbt */
macro GetDealPrice( dlleg )
   var Price = $0, FaceValue = $0;
   record Fininstr(fininstr);

   if ( ПолучитьФинИн( dlleg.PFI, Fininstr ) == 0 )

     if( FI_IsBond(Fininstr) )     /* Облигация */
       if( dlleg.RelativePrice == "" )      /*Валюта сделки не задана в процентах*/
         Price = money(dlleg.Price/**100*/);
       else
         if( FI_GetNominal( dlleg.PFI, FaceValue ) != 0 )
           FaceValue = $0;
         end; 
         Price = money( FaceValue * dlleg.Price/*/100*/ );
       end;
     elif( FI_IsShare(Fininstr) )  /*   Акция   */
       Price = money(dlleg.Price/**100*/);
     end;
   end;

   return Price;
end;


/* формирование тэгов по всем участвующим в неттинге сделкам БОЦБ */
macro AllDealsTag( IsConfirm )
  var  stat, bstat,
       FaceValue = $0,
       FaceValueFI,
       Number_date = "",
       CountReq = 0, /* кол-во требований */
       CountCom = 0, /* кол-во обязательств */
       PayWithoutMovePlus  = 0,
       PayWithoutMoveMinus = 0,
       Netting_All = 0,
       AmountReq = 0,
       AmountCom = 0,
       FaceValueReq = 0,
       FaceValueCom = 0,
       B_PaymentInAdv = $0,
       S_PaymentInAdv = $0;

  file pmlink(pmlink) key 1;
  file pmpaym(pmpaym);
  file avns_pmpaym(pmpaym) key 1;
  file pmprop(pmprop);
  file dltick(dl_tick);
  file dlleg(dl_leg);
  file spgrd(spgrdoc) key 1;
  file spgr(spground);
  record Currency(fininstr);
  record fiwarnts( fiwarnts );

  var IsSecondPart = false;

  macro ДоговорПоСделке()

    var Group = GetOperationGroup(dltick.DealType);
    var KindDoc, Number = "";

    if((IsBACKSALE(Group) == true))
      if(IsSecondPart == false)
        KindDoc = DOCKIND_CONTR_FIRSTPART;
      else
        KindDoc = DOCKIND_CONTR_SECONDPART;
      end;
    else
      KindDoc = DOCKIND_BUYSALEREPORT;
    end;

    ClearRecord(spgrd);
    spgrd.SourceDocKind = dltick.BofficeKind;
    spgrd.SourceDocID   = dltick.DealID;
    bstat = getGE(spgrd);
    while( bstat and (spgrd.SourceDocKind == dltick.BofficeKind) and (spgrd.SourceDocID == dltick.DealID) )
      ClearRecord(spgr);
      spgr.SPgroundID = spgrd.SPGroundID;
      if ( getEQ(spgr) )
        if(spgr.Kind == KindDoc)
          return ("№ " + CheckData(spgr.Xld) + " от " + CheckData(spgr.RegistrDate));
        end;
      end;
      bstat = next(spgrd);
    end;
    return ("№ " + CheckData() + " от " + CheckData());
  end;

  /* сначала подсчитаем все сделки */
  ClearRecord(pmlink); 
  pmlink.DocKind    = dlnet.DocKind;
  pmlink.DocumentID = dlnet.NettingID;
  pmlink.LinkKind   = PMLINK_KIND_NETTING;
  stat = GetGE(pmlink);
  while( stat and (pmlink.DocKind == dlnet.DocKind) and (pmlink.DocumentID == dlnet.NettingID) and (pmlink.LinkKind == PMLINK_KIND_NETTING) )
    ClearRecord(pmpaym);
    pmpaym.PaymentID = pmlink.InitialPayment;
    if ( getEQ(pmpaym) and confirm( pmpaym, IsConfirm ) )
      /* ищем сделку */
      ClearRecord(dltick);
      dltick.DealID = pmpaym.DocumentID;
      if ( getEQ(dltick) )
        if ( pmpaym.Payer == {OurBank} )
          CountCom = CountCom + 1;  /*Обязательства*/
        else
          CountReq = CountReq + 1;  /*Требования*/
        end;
      end;
    end;
    stat = next(pmlink);
  end;

  /*Т.е. реально мы просмотрели все платежи, привязанные к данной сделке
    неттинга. Если ни одного из платежей мы не нашли, то будем ругаться
    и отваливаться*/
  if((CountCom == 0) AND (CountReq == 0))
    Msgbox("Не найдены платежи по сделке.|Необходимо ввести платежи либо обновить сделку");  
    return false;
  end;

  /* размножим строки таблиц до нужного количества или удалим если нет данных*/
  if(CountCom > 0)
    MultipleRow(0, CountCom, COMMITMENT);    /*Обязательство*/
  else
    DeleteTable(COMMITMENT);
  end;

  if(CountReq > 0)
    MultipleRow(0, CountReq, REQUIREMENT);   /*Требование*/
  else
    DeleteTable(REQUIREMENT);
  end;


  CountCom = 0;
  CountReq = 0;

  /* теперь заполним  все тэги сделок */
  ClearRecord(pmlink);
  pmlink.DocKind    = dlnet.DocKind;
  pmlink.DocumentID = dlnet.NettingID;
  pmlink.LinkKind   = PMLINK_KIND_NETTING;
  stat = GetGE(pmlink);
  while( stat and (pmlink.DocKind == dlnet.DocKind) and (pmlink.DocumentID == dlnet.NettingID) and (pmlink.LinkKind == PMLINK_KIND_NETTING) )
    ClearRecord(pmpaym);
    pmpaym.PaymentID = pmlink.InitialPayment;
    if ( getEQ(pmpaym) and confirm( pmpaym, IsConfirm ) )

      if( FI_GetNominal( dlnet.BaseFIID, FaceValue, FaceValueFI ) != 0 )
        FaceValue = $0;
      end; 

      if ( pmlink.IPSign == "+" )
        PayWithoutMovePlus = PayWithoutMovePlus + pmlink.Amount;
      elif ( pmlink.IPSign == "-" )
        PayWithoutMoveMinus = PayWithoutMoveMinus + pmlink.Amount;
      end;

      /* ищем сделку */
      ClearRecord(dltick);
      dltick.DealID = pmpaym.DocumentID;
      if ( getEQ(dltick) )

        if ( pmpaym.Payer == {OurBank} )
          CountCom = CountCom + 1;
          FaceValueCom = FaceValueCom + money(pmpaym.BaseAmount*FaceValue/*/100*/); 
          AmountCom = AmountCom + pmpaym.BaseAmount;

          println( "~S_Seq_Number" + string(CountCom) );
          println( CheckData(CountCom) );

          println( "~S_date_Deal" + string(CountCom) );
          println( CheckData(dltick.DealDate) );

          println( "~S_date_pay" + string(CountCom) );
          println( CheckData(pmpaym.ValueDate) );

          println( "~S_Fi" + string(CountCom) );
          if ( ПолучитьФинИн( pmpaym.BaseFIID, Currency ) == 0 )
            println( CheckData(Currency.Name) );
          else
            print_empty();
          end;

          println( "~S_date_del" + string(CountCom) );
          println( CheckData(pmpaym.ValueDate) );

          println( "~S_Amount" + string(CountCom) );
          println( CheckData(convert_digit( pmpaym.BaseAmount, pmpaym.BaseFIID)));

          println( "~S_Amount_Wr" + string(CountCom) );
          prinln_SumToStr( convert_digit( pmpaym.BaseAmount, pmpaym.BaseFIID), pmpaym.BaseFIID );

          println( "~S_FaceValueAll" + string(CountCom) );
          println( CheckData( money(pmpaym.BaseAmount*FaceValue/*/100*/) ));

          println( "~S_FaceValueAllWr" + string(CountCom) );
          prinln_SumToStr( money(pmpaym.BaseAmount*FaceValue/*/100*/), FaceValueFI );
  
          ClearRecord( avns_pmpaym );
          avns_pmpaym.DocKind    = dlnet.DocKind;
          avns_pmpaym.DocumentID = dlnet.NettingID;
          avns_pmpaym.Purpose    = PM_PURP_BACK_AVANCE;
          println( "~S_PaymentInAdv" + string(CountCom) );
          if ( getEQ(avns_pmpaym) )
            S_PaymentInAdv = money(avns_pmpaym.BaseAmount/**100*/);
            println( CheckData(S_PaymentInAdv) );
          else
            S_PaymentInAdv = $0;
            println( $0 );
          end;

        else
          CountReq = CountReq + 1;
          FaceValueReq = FaceValueReq + money(pmpaym.BaseAmount*FaceValue/*/100*/);
          AmountReq = AmountReq + pmpaym.BaseAmount;

          println( "~B_Seq_Number" + string(CountReq) );
          println( CheckData(CountReq) );
                     
          println( "~B_date_Deal" + string(CountReq) );
          println( CheckData(dltick.DealDate) );

          println( "~B_date_pay" + string(CountReq) );
          println( CheckData(pmpaym.ValueDate) );

          println( "~B_Fi" + string(CountReq) );
          if ( ПолучитьФинИн( pmpaym.BaseFIID,  Currency ) == 0 )
            println( CheckData(Currency.Name) );
          else
            print_empty();
          end;

          println( "~B_date_del" + string(CountReq) );
          println( CheckData(pmpaym.ValueDate) );

          println( "~B_Amount" + string(CountReq) );
          println( CheckData( convert_digit( pmpaym.BaseAmount, pmpaym.BaseFIID)) );

          println( "~B_Amount_Wr" + string(CountReq) );
          prinln_SumToStr( convert_digit( pmpaym.BaseAmount, pmpaym.BaseFIID), pmpaym.BaseFIID );

          println( "~B_FaceValueAll" + string(CountReq) );
          println( CheckData( money(pmpaym.BaseAmount*FaceValue/*/100*/)) );

          println( "~B_FaceValueAllWr" + string(CountReq) );
          prinln_SumToStr( money(pmpaym.BaseAmount*FaceValue/*/100*/), FaceValueFI );

          ClearRecord( avns_pmpaym );
          avns_pmpaym.DocKind    = dlnet.DocKind;
          avns_pmpaym.DocumentID = dlnet.NettingID;
          avns_pmpaym.Purpose    = PM_PURP_BACK_AVANCE;
          println( "~B_PaymentInAdv" + string(CountReq) ); 
          if ( getEQ(avns_pmpaym) )
            B_PaymentInAdv = money(avns_pmpaym.BaseAmount/**100*/);
            println( CheckData(B_PaymentInAdv) );
          else
            B_PaymentInAdv = $0;
            println( $0 );
          end;

        end;


        /* ищем транш сделки */
        ClearRecord(dlleg);
        dlleg.DealID  = dltick.DealID;
        dlleg.LegID   = 0;
        if ( (pmpaym.Purpose == BAi) or (pmpaym.Purpose == CAi) or (pmpaym.Purpose == PM_PURP_AVANCE) )
          dlleg.LegKind = LEG_KIND_DL_TICK;
          IsSecondPart = false;
        else
          IsSecondPart = true;
          dlleg.LegKind = LEG_KIND_DL_TICK_BACK;
        end; 
        if ( getEQ(dlleg) )
          ClearRecord(fiwarnts);
          if ( ПолучитьФинИн( dlleg.PFI, null, null, null, fiwarnts ) == 0 )
            if ( pmpaym.Payer == {OurBank} )

              println( "~S_Days_NKD" + string(CountCom) );
              if(FI_IsCouponAvoiriss( dlleg.PFI ) == true)
                println( CheckData(pmpaym.ValueDate - fiwarnts.FirstDate) );
              else
                print_empty();
              end;

              println( "~S_NKD" + string(CountCom) );                 
              println( CheckData(dlleg.NKD) );

              println( "~S_SummWithoutNKD" + string(CountCom) );
              println( CheckData(dlleg.Cost) );

              println( "~S_SummWithNKD" + string(CountCom) );                 
              println( CheckData(dlleg.TotalCost) );

              println( "~S_DeltaPmAdv" + string(CountCom) );
              println( CheckData( dlleg.TotalCost - S_PaymentInAdv) );

              println( "~S_Rate" + string(CountCom) );                 
              println( CheckData( GetDealPrice( dlleg ) /*dlleg.Price*/ ) );
            else
              println( "~B_Days_NKD" + string(CountReq) );
              if(FI_IsCouponAvoiriss( dlleg.PFI ) == true)
                println( CheckData(pmpaym.ValueDate - fiwarnts.FirstDate) );
              else
                print_empty();
              end;

              println( "~B_NKD" + string(CountReq) );                 
              println( CheckData(dlleg.NKD) );

              println( "~B_SummWithoutNKD" + string(CountReq) );                 
              println( CheckData(dlleg.Cost) );

              println( "~B_SummWithNKD" + string(CountReq) );                 
              println( CheckData(dlleg.TotalCost) );

              println( "~B_DeltaPmAdv" + string(CountReq) );
              println( CheckData(dlleg.TotalCost - B_PaymentInAdv ) );

              println( "~B_Rate" + string(CountReq) );                 
              println( CheckData( GetDealPrice( dlleg ) /*dlleg.Price*/) );
            end;
          else
            println( "~B_Days_NKD" + string(CountReq) );
            print_empty();
            println( "~B_NKD" + string(CountReq) );
            print_empty();
            println( "~B_SummWithoutNKD" + string(CountReq) );
            print_empty();
            println( "~B_DeltaPmAdv" + string(CountReq) );
            print_empty();
            println( "~B_SummWithNKD" + string(CountReq) );
            print_empty();
            println( "~B_Rate" + string(CountReq) );
            print_empty();

            println( "~S_Days_NKD" + string(CountCom) );
            print_empty();
            println( "~S_NKD" + string(CountCom) );
            print_empty();
            println( "~S_SummWithoutNKD" + string(CountCom) );
            print_empty();
            println( "~S_DeltaPmAdv" + string(CountCom) );
            print_empty();
            println( "~S_SummWithNKD" + string(CountCom) );
            print_empty();
            println( "~S_Rate" + string(CountCom) );
            print_empty();
          end;
        end;

        /* ищем договора по сделке */
        if ( Number_date != "" )
          Number_date = Number_date + ", ";
        end;
        Number_date = Number_date + ДоговорПоСделке();

        if ( pmpaym.Payer == {OurBank} )
          println( "~Number_date_S" + string(CountCom) );
          println( ДоговорПоСделке() );
        else
          println( "~Number_date_B" + string(CountReq) );
          println( ДоговорПоСделке() );
        end;
      end;
    end;
    stat = next(pmlink);
  end;

  println( "~Number_date" );
  println( CheckData(Number_date) );

  println( "~Netting_All" );
  println( CheckData( money(min(PayWithoutMovePlus, PayWithoutMoveMinus) ) ) );

  println( "~Netting_Avoir_all" );
  println( CheckData( StrBefore(string(min(PayWithoutMovePlus, PayWithoutMoveMinus)),".") ) );

  println( "~Sum_Net_avoir_all" );
  println( CheckData( money(money(min(PayWithoutMovePlus, PayWithoutMoveMinus))*FaceValue/*/100*/) ) );

  println( "~B_Face_All" );
  println( CheckData( AmountReq ) );

  println( "~S_Face_All" );
  println( CheckData( AmountCom ) );

  println( "~B_FaceValueTotal" );
  println( CheckData( FaceValueReq ) );

  println( "~S_FaceValueTotal" );
  println( CheckData( FaceValueCom ) );

  if ( ПолучитьФинИн( dlnet.BaseFIID, Currency ) == 0 )
    println( "~Netting_All_Wr" );
    prinln_SumToStr( min(PayWithoutMovePlus, PayWithoutMoveMinus), dlnet.BaseFIID );

    println( "~Sum_Net_avoir_all_Wr" );
    prinln_SumToStr(  money(money(min(PayWithoutMovePlus, PayWithoutMoveMinus))*FaceValue/*/100*/), FaceValueFI );

    println( "~Netting_Avoir_All_Wr" );
    prinln_SumToStr( convert_digit( min(PayWithoutMovePlus, PayWithoutMoveMinus), dlnet.BaseFIID ), dlnet.BaseFIID);
                     
    println( "~B_Face_All_Wr" );
    prinln_SumToStr( AmountReq,  dlnet.BaseFIID );

    println( "~S_Face_All_Wr" );
    prinln_SumToStr( AmountCom,  dlnet.BaseFIID );

    println( "~B_FaceValueTotal_Wr" );
    prinln_SumToStr( FaceValueReq,  FaceValueFI );

    println( "~S_FaceValueTotal_Wr" );
    prinln_SumToStr( FaceValueCom,  FaceValueFI );
  else
    println( "~Netting_All_Wr" );
    print_empty();
    println( "~B_Face_All_Wr" );
    print_empty();
    println( "~S_Face_All_Wr" );
    print_empty();
    println( "~B_FaceValueTotal_Wr" );
    print_empty();
    println( "~S_FaceValueTotal_Wr" );
    print_empty();
  end;

  println( "~B_Seq_Number_All" );
  println( CheckData(CountReq) );

  println( "~S_Seq_Number_All" );
  println( CheckData(CountCom) );

  return true;
end;


/* формирование тэгов по финансовым инструментам */
macro FinsTag(Date_net: DATE)
  var FaceValue;
  file fininstr(fininstr);
  file avoiriss(avoiriss);
  record Currency(fininstr);
  record party(party);

  ClearRecord(fininstr);
  fininstr.FIID = dlnet.BaseFIID;
  if( GetEQ(fininstr) == false )
    MsgBox( "Не найден финансовый инструмент с парамером FIID = " + dlnet.BaseFIID );
    return false;
  end;

  ClearRecord(avoiriss);
  if ( dlnet.FI_Kind == FIKIND_AVOIRISS )
    avoiriss.FIID = dlnet.BaseFIID;
    if( GetEQ(avoiriss) == false )
      MsgBox( "Не найдена ц/б с парамером FIID = " + dlnet.BaseFIID );
      return false;
    end;
  end;

  
  if( FI_GetNominal( fininstr.FIID, FaceValue ) != 0 )
     FaceValue = $0;
  end; 

  println( "~FaceValue" );
  println( CheckData( FaceValue ) );

  if(ExistFinalPayment == true)
    println( "~Sum_Net_avoir" );
    println( CheckData( money(int(double(final_pmpaym.BaseAmount))*FaceValue) ));
    println( "~Sum_Net_avoir_Wr" );
    prinln_SumToStr( money(int(double(final_pmpaym.BaseAmount))*FaceValue), fininstr.FaceValueFI );
  else
    println( "~Sum_Net_avoir" );
    println( CheckData( 0 ) );
    println( "~Sum_Net_avoir_Wr" );
    prinln_SumToStr( 0, fininstr.FaceValueFI );
  end;

  if ( ПолучитьФинИн( fininstr.FaceValueFI, Currency ) == 0 )
    println( "~FaceValueWr" );
    prinln_SumToStr( FaceValue, fininstr.FaceValueFI );

    println( "~ParentFI" );
    println( CheckData( Currency.Name ) );
  else
    println( "~Sum_Net_avoir_all_Wr" ); 
    print_empty();
    println( "~FaceValueWr" );
    print_empty();
    println( "~ParentFI" );
    print_empty();
  end;

  println( "~Name_avoir" );
  println( CheckData(fininstr.Name) );

  println( "~ISIN" );
  println( CheckData(avoiriss.ISIN) );

  println( "~LSIN" );
  println( CheckData(avoiriss.LSIN) );

  println( "~Issuer" );
  if ( ПолучитьСубъекта( FI_GetIssuerOnDate( fininstr, Date_net), party ) == 0 )
    println( CheckData(party.Name) );
  else
    print_empty();
  end;

  return true;
end;


/* формирование тэгов по финальной сделке */
macro FinalDltickTag()
  var num = readNoteForObject( OBJTYPE_NETTING, StrTrimLeft(dlnet.NettingID,34), NOTEKIND_FINALDEAL),
      stat;

  file spgrd(spgrdoc) key 1;
  file spgr(spground);

  if ( num != "" )
    ClearRecord(final_dltick);
    final_dltick.BofficeKind = DL_SECURITYDOC;
    final_dltick.DealCode    = num;
    if( GetEQ(final_dltick) == false )
      MsgBox( "Не найдена финальная сделка с номером = " + num );
      return false;
    end;

     ClearRecord(spgrd);
     spgrd.SourceDocKind = final_dltick.BofficeKind;
     spgrd.SourceDocID   = final_dltick.DealID;;

     if ( GetGE(spgrd) )
       ClearRecord(spgr);
       spgr.SPgroundID = spgrd.SPGroundID;
       stat = GetGE(spgr);
       println( "~Contract_N" );
       if ( stat )
         println( "№ " + CheckData(spgr.Xld) );
       else
         print_empty();
       end;

       println( "~Contract_D" );
       if ( stat )
         println( CheckData(spgr.RegistrDate) );
       else
         print_empty();
       end;
     else
       println( "~Contract_N" );
       print_empty();
       println( "~Contract_D" );
       print_empty();
     end;
  else  /*Если номер не задан будем печатать пустые поля*/
     println( "~Contract_N" );
     print_empty();
     println( "~Contract_D" );
     print_empty();
  end;

  return true;
end;


/* формирование тэгов для представителей */
macro ProxyTag( proxySide )
  var side, stat, Caption = "";
  file proxy(dl_proxy);
  file spgr(spground);

  /* представитель */
  ClearRecord(proxy);
  proxy.DocKind       = DL_NTGDOC;
  proxy.ContractID    = dlnet.NettingID;
  proxy.ContractParty = proxySide;
  if( (GetGE(proxy) == false) or (proxy.DocKind != DL_NTGDOC) or (proxy.ContractID != dlnet.NettingID) or (proxy.ContractParty != proxySide) )
    if ( proxySide == DLPROXY_OURBANK )
      MsgBox( "Не найдены представители банка" );
    else
      MsgBox( "Не найдены представители контрагента" );
    end;
    return false;
  end;

  if ( proxySide == DLPROXY_OURBANK )
    side = "Bank";
  else
    Side = "Count";
  end;

  /* документ-основание */
  ClearRecord(spgr);
  spgr.SPgroundID = proxy.ProxyID;
  println( "~" + side + "_ProxyMandateCaption" );
  if ( getEQ(spgr) )
    GetValueName( OBJTYPE_KINDDOC, spgr.Kind, Caption );
    println( CheckData(Caption) + " № " + CheckData(spgr.AltXld) + " от " + CheckData(spgr.SignedDate) );
  else
    print_empty();
  end;

  println( "~" + side + "_ProxyName" );
  println( CheckData(proxy.AgentName) );

  println( "~" + side + "_ProxyNameShort" );
  println( CheckData(CutFullName(proxy.AgentName)) );

  println( "~" + side + "_ProxyAuthority" );
  println( CheckData(proxy.AgentPost) );

  /* найдем представителя, имеющего вид "1-е лицо" */
  ClearRecord(proxy);
  proxy.DocKind       = DL_NTGDOC;
  proxy.ContractID    = dlnet.NettingID;
  proxy.ContractParty = proxySide;
  proxy.ProxyRole     = DL_PROXY_BOSS;
  println( "~" + side + "_ProxyTotal" );
  if ( GetEQ(proxy) )
    println( CheckData(proxy.AgentPost) + " " + CheckData(proxy.AgentName) + " ");
    ClearRecord(spgr);
    spgr.SPgroundID = proxy.ProxyID;
    if ( getEQ(spgr) )
      GetValueName( OBJTYPE_KINDDOC, spgr.Kind, Caption );
      println( CheckData(Caption) + "№ " + CheckData(spgr.AltXld) + " от " + CheckData(spgr.SignedDate) );
    else
      print_empty();
    end;
  else
    print_empty();
  end;

  return true;
end;

macro FindSETTACC( PartyID, FI_Kind, FIID, _settacc )
  var stat = false;
  file settacc(settacc) key 3;

  if ( FI_Kind == FIKIND_AVOIRISS )
    FIID = -1;
  end;  

  ClearRecord( settacc );
  settacc.PartyID = PartyID;
  settacc.FIKind  = FI_Kind; 
  settacc.FIID    = FIID;
  if ( getGE(settacc) and ( ( settacc.PartyID == PartyID ) and ( settacc.FIKind == FI_Kind ) and ( settacc.FIID == FIID ) ) )
     stat = true;
  end;

  copy( _settacc, settacc );

  return stat;
end;


/* адреса и реквизиты банка */
macro BankPropsTag( PartyID )
  var side, stat, str = "";
  file   country(country,"rsrfdb.def") key 2;
  file   partcode(partcode);
  file   corschem(corschem) key 1;
  record settacc(settacc);
  record party(party);
  record countparty(party);
  record PartyOwn(party);
  record RecordAdressLegal ( adress );
  record RecordAdressReal ( adress );
  var PhoneNumber = "", Fax = "", TelexNumber = "";

  if ( ПолучитьСубъекта( PartyID, party ) == 0 )
    if ( PartyID == {OurBank} )
      side = "Bank";
    else
      side = "Count";
    end;

    /* страна нерезидента */
    println( "~Republic_NonRes" );
    if ( (PartyID != {OurBank}) and (party.NotResident == "X" ) )
      ClearRecord(country);
      country.CodeLat3 = party.NRCountry;
      if ( getEQ(country) )
        println( CheckData(country.Name) );
      else
        print_empty();
      end;
    else
      print_empty();
    end;

    ClearRecord(RecordAdressLegal);
    ClearRecord(RecordAdressReal);
    НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdressLegal);
    НайтиАдресСубъекта(party.PartyID,PTADDR_REAL,RecordAdressReal);

    FindAdressContactValue(party.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber);
    FindAdressContactValue(party.PartyID, PTADDR_LEGAL, CNTADR_FAX,         Fax);
    FindAdressContactValue(party.PartyID, PTADDR_LEGAL, CNTADR_TELEXNUMBER, TelexNumber);

    println( "~" + side + "_FullName" );
    println( CheckData(party.Name) );

    println( "~" + side + "_ShortName" );
    println( CheckData(party.ShortName) );

    println( "~" + side + "Name" );
    println( CheckData(party.Name) );

    println( "~" + side + "Address" );
    println( CheckData(RecordAdressLegal.PostIndex + RecordAdressLegal.Adress) );

    println( "~" + side + "AddressP" );
    println( CheckData(RecordAdressReal.PostIndex + RecordAdressReal.Adress) );

    println( "~" + side + "Tel" );
    println( CheckData(PhoneNumber) );

    println( "~" + side + "Fax" );
    println( CheckData(Fax) );

    println( "~" + side + "Telex" );
    println( CheckData(TelexNumber) );
  else
    println( "~" + side + "_FullName" );
    print_empty();
    println( "~" + side + "_ShortName" );
    print_empty();
    println( "~" + side + "Name" );
    print_empty();
    println( "~" + side + "Address" );
    print_empty();
    println( "~" + side + "AddressP" );
    print_empty();
    println( "~" + side + "Tel" );
    print_empty();
    println( "~" + side + "Fax" );
    print_empty();
    println( "~" + side + "Telex" );
    print_empty();
  end;

  println( "~" + side + "Account" );
  str = "";
  if(ExistFinalPayment == true)
    if ( PartyID == {OurBank} ) /* для банка */
      if ( IsObligation() )
        str = final_pmpaym.PayerAccount;
      else
        str = final_pmpaym.ReceiverAccount;
      end;
    else  /* для контрагента */
      if ( IsObligation() )
        str = final_pmpaym.ReceiverAccount;
      else
        str = final_pmpaym.PayerAccount;
      end;
    end;
  end;
  /* если счета нет, попробуем взять из СПИ */
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.Account;
  end;
  println( CheckData( str ) );

  println( "~" + side + "_AccountCur" );
  str = "";
  if(ExistFinalPayment == true)
    if ( PartyID == {OurBank} )
      if ( IsObligation() )
        str = final_pmpaym.PayerAccount;
      else
        str = final_pmpaym.ReceiverAccount;
      end;
    else
      if ( IsObligation() )
        str = final_pmpaym.ReceiverAccount;
      else
        str = final_pmpaym.PayerAccount;
      end;
    end;
  end;
  if ( (str == "") and FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) )
    str = settacc.Account;
  end;
  println( CheckData(str) );

  println( "~" + side + "_BankName" );
  str = "";
  if(ExistFinalPayment == true)
    if ( PartyID == {OurBank} )
      if ( IsObligation() )
        str = final_pmrmprop.PayerBankName;
      else
        str = final_pmrmprop.ReceiverBankName;
      end;
    else
      if ( IsObligation() )
        str = final_pmrmprop.ReceiverBankName;
      else
        str = final_pmrmprop.PayerBankName;
      end;
    end;
  end;
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.BankName;
  end;
  println( CheckData( str ) );

  println( "~" + side + "_BankNameCur" );
  str = "";
  if(ExistFinalPayment == true)
    if ( PartyID == {OurBank} )
      if ( IsObligation() )
        str = final_pmrmprop.PayerBankName;
      else
        str = final_pmrmprop.ReceiverBankName;
      end;
    else
      if ( IsObligation() )
        str = final_pmrmprop.ReceiverBankName;
      else
        str = final_pmrmprop.PayerBankName;
      end;
    end;
  end;
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.BankName;
  end;
  println( CheckData( str ) );

  ClearRecord(partcode);
  partcode.PartyID  = PartyID;
  partcode.CodeKind = PTCK_BIC;
  if ( getEQ(partcode) )
    println( "~" + side + "_BankBIC" );
    if ( PartyID == {OurBank} )
      println( CheckData(partcode.Code) );
    end;

    println( "~" + side + "BIC" );
    println( CheckData(partcode.Code) );
  else
    if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) and ( settacc.BankCodeKind == PTCK_BIC ) )
      println( CheckData(settacc.BankCode) );
    else
      print_empty();
    end;
  end;

  println( "~" + side + "_BankBIC" );
  if ( (ExistFinalPayment == true) and (PartyID != {OurBank}) and (final_pmprop.CodeKind == CODE_BIK) )
    println( CheckData(final_pmprop.BankCode) );
  else
    print_empty();
  end;

  ClearRecord(partcode);
  partcode.PartyID  = PartyID;
  partcode.CodeKind = PTCK_INN;
  println( "~" + side + "TaxId" );
  if ( getEQ(partcode) )
    println( CheckData(partcode.Code) );
  else
    print_empty();
  end;

  ClearRecord(partcode);
  partcode.PartyID  = PartyID;
  partcode.CodeKind = PTCK_NDC;
  println( "~" + side + "Identificator" );
  if ( getEQ(partcode) )
    println( CheckData(partcode.Code) );
  else
    print_empty();
  end;

  ClearRecord(partcode);
  partcode.PartyID  = PartyID;
  partcode.CodeKind = PTCK_SWIFT;
  println( "~" + side + "SWIFT" );
  if ( getEQ(partcode) )
    println( CheckData(partcode.Code) );
  else
    if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc )  and ( settacc.BankCodeKind == PTCK_SWIFT ) )
      println( CheckData(settacc.BankCode) );
    else
      print_empty();
    end;
  end;

  ClearRecord(partcode);
  partcode.CodeKind = PTCK_SWIFT;
  println( "~" + side + "_BankSwift" );
  if(ExistFinalPayment == true)
    if ( PartyID != {OurBank} )
      /* SWIFT кода банка контрагента */
      if ( IsObligation() )
        partcode.PartyID = final_pmpaym.ReceiverBankID;
      else
        partcode.PartyID = final_pmpaym.PayerBankID;
      end;
    else
      if ( IsObligation() )
        partcode.PartyID = final_pmpaym.PayerBankID;
      else
        partcode.PartyID = final_pmpaym.ReceiverBankID;
      end;
    end;
    if ( getEQ(partcode) )
      println( CheckData(partcode.Code) );
    else
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) and ( settacc.BankCodeKind == PTCK_SWIFT ) )
        println( CheckData(settacc.BankCode) );
      else
        print_empty();
      end;
    end;
  else
    if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) and ( settacc.BankCodeKind == PTCK_SWIFT ) )
       println( CheckData(settacc.BankCode) );
     else
       print_empty();
     end;
  end;

  if ( PartyID == {OurBank} )
    ClearRecord(corschem);
    if(ExistFinalPayment == true)
      corschem.Number  = final_pmprop.Corschem;
      stat =  getGE(corschem);
    else
      stat = false;
    end;
    println( "~" + side + "CorAccNatCur" );
    if ( stat )
      println( CheckData(corschem.Account) );
    else
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) )
        println( CheckData(settacc.CorrAcc) );
      else
        print_empty();
      end;
    end;

    println( "~" + side + "CorAccCur" );
    if ( stat )
      println( CheckData(corschem.Account) );
    else
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) )
        println( CheckData(settacc.CorrAcc) );
      else
        print_empty();
      end;
    end;

    if ( stat )
      ClearRecord(partcode);
      partcode.PartyID  = corschem.CorrID;
      partcode.CodeKind = PTCK_SWIFT;
      println( "~" + side + "CorrBankCodeSWIFT" );
      if ( getEQ(partcode) )
        println( CheckData(partcode.Code) );
      else
        if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) and ( settacc.BankCorrCodeKind == PTCK_SWIFT ) )
          println( CheckData(settacc.BankCorrCode) );
        else
          print_empty();
        end;
      end;

      println( "~" + side + "CorrBankAdd" );
      ClearRecord(RecordAdressLegal);
      if ( НайтиЮридическийАдресСубъекта(corschem.CorrID,RecordAdressLegal) )
        println( CheckData(RecordAdressLegal.PostIndex + RecordAdressLegal.Adress) );
      else
        print_empty();
      end;
    else
      println( "~" + side + "CorrBankCodeSWIFT" );
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) and ( settacc.BankCorrCodeKind == PTCK_SWIFT ) )
        println( CheckData(settacc.BankCorrCode) );
      else
        print_empty();
      end;
      
      println( "~" + side + "CorrBankAdd" );
      ClearRecord(RecordAdressLegal);
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) and 
           (НайтиЮридическийАдресСубъекта(settacc.BankCorrID,RecordAdressLegal)) )
        println( CheckData(RecordAdressLegal.PostIndex + RecordAdressLegal.Adress) );
      else
        print_empty();
      end;
    end;
  else
    println( "~" + side + "CorAccNatCur" );
    if ((ExistFinalPayment == true) AND (final_pmprop.CorrAcc != ""))
      println( CheckData(final_pmprop.CorrAcc) );
    else
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) )
        println( CheckData(settacc.CorrAcc) );
      else
        print_empty();
      end;
    end;

    println( "~" + side + "CorAccCur" );
    if ((ExistFinalPayment == true) AND (final_pmprop.CorrAcc != ""))
      println( CheckData(final_pmprop.CorrAcc) );
    else
      if ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) )
        println( CheckData(settacc.CorrAcc) );
      else
        print_empty();
      end;
    end;

    println( "~" + side + "CorrBankCodeSWIFT" );
    ClearRecord(partcode);
    if(ExistFinalPayment == true)
      partcode.CodeKind = PTCK_SWIFT;
      if ( IsObligation() )
        partcode.PartyID = final_pmpaym.ReceiverBankID;
      else
        partcode.PartyID = final_pmpaym.PayerBankID;
      end;
      if (getEQ(partcode))
        println( CheckData(partcode.Code) );
      else
        print_empty();
      end;
    else
      print_empty();
    end;

    println( "~" + side + "CorrBankAdd" );
    ClearRecord(RecordAdressLegal);
    if ( (ExistFinalPayment == true) AND 
         (НайтиЮридическийАдресСубъекта(partcode.PartyID,RecordAdressLegal)) )
      println( CheckData(RecordAdressLegal.PostIndex + RecordAdressLegal.Adress) );
    else
      print_empty();
    end;
  end;

  println( "~" + side + "CorrBankName" );
  str = "";
  if(ExistFinalPayment == true)
     if ( PartyID == {OurBank} )
       if ( IsObligation() )
         str = final_pmrmprop.PayerCorrBankName;
       else
         str = final_pmrmprop.ReceiverCorrBankName;
       end;
     else
       if ( IsObligation() )
         str = final_pmrmprop.ReceiverCorrBankName;
       else
         str = final_pmrmprop.PayerCorrBankName;
       end;
     end;
  end;
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.BankCorrName;
  end;
  println( CheckData(str) );

  println( "~" + side + "CorrBankNameCur" );
  str = "";
  if(ExistFinalPayment == true)
     if ( PartyID == {OurBank} )
       if ( IsObligation() )
         str = final_pmrmprop.PayerCorrBankName;
       else
         str = final_pmrmprop.ReceiverCorrBankName;
       end;
     else
       if ( IsObligation() )
         str = final_pmrmprop.ReceiverCorrBankName;
       else
         str = final_pmrmprop.PayerCorrBankName;
       end;
     end;
  end;
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.BankCorrName;
  end;
  println( CheckData(str) );

  println( "~" + side + "DepoAcc" );
  str = "";
  if(ExistFinalPayment == true)
     if ( PartyID == {OurBank} )
       if ( IsObligation() )
         str = final_pmpaym.PayerAccount;
       else
         str = final_pmpaym.ReceiverAccount;
       end;
     else
       if ( IsObligation() )
         str = final_pmpaym.ReceiverAccount;
       else
         str = final_pmpaym.PayerAccount;
       end;
     end;
  end;
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.Account;
  end;
  println( CheckData(str) );

  println( "~" + side + "Deposit" );
  str = "";
  if(ExistFinalPayment == true)
     if ( PartyID == {OurBank} )
       if ( IsObligation() )
         str = final_pmrmprop.PayerBankName;
       else
         str = final_pmrmprop.ReceiverBankName;
       end;
     else
       if ( IsObligation() )
         str = final_pmrmprop.ReceiverBankName;
       else
         str = final_pmrmprop.PayerBankName;
       end;
     end;
  end;
  if ( ( str == "" ) and ( FindSETTACC( PartyID, dlnet.FI_Kind, dlnet.BaseFIID, settacc ) ) )
    str = settacc.BankName;
  end;
  println( CheckData(str) );

  /* счет получателя */
  println( "~ReceiverAccount" );
  if(ExistFinalPayment == true)
    println( CheckData(final_pmpaym.ReceiverAccount) );
  else
    println( CheckData("") );
  end;

  /* наименование банка получателя платежа */
  println( "~ReceiverBankName" );
  if(ExistFinalPayment == true)
    println( CheckData(final_pmrmprop.ReceiverBankName) );
  else
    println( CheckData("") );
  end;

  if(ExistFinalPayment == true)
     ClearRecord(corschem);
     corschem.Number  = final_pmprop.Corschem;
     stat = getGE(corschem);

     if ( IsObligation() )
       /* корсчет получателя */
       println( "~ReceiverCorrAcc" );
       println( CheckData(final_pmprop.CorrAcc) );

       /* БИК корреспондента банка получателя */
       println( "~ReceiverCorrBIC" );
       if ( final_pmprop.CodeKind == CODE_BIK )
         println( CheckData(final_pmprop.CorrAcc) );
       else
         print_empty();
       end;
     else
       if ( stat )
         /* корсчет получателя */
         println( "~ReceiverCorrAcc" );
         println( CheckData(corschem.Account) ); 

         /* БИК корреспондента банка получателя */
         ClearRecord(partcode);
         partcode.CodeKind = PTCK_BIC;
         partcode.PartyID  = corschem.CorrID;
         println( "~ReceiverCorrBIC" );
         if ( getEQ(partcode) )
           println( CheckData(partcode.Code) );
         else
           print_empty();
         end;
       else
         println( "~ReceiverCorrAcc" );
         print_empty();
         println( "~ReceiverCorrBIC" );
         print_empty();
       end;
     end;
  else
     println( "~ReceiverCorrAcc" );
     print_empty();
     println( "~ReceiverCorrBIC" );
     print_empty();
     stat = false;
  end;

  /* ИНН корреспондента банка получателя */
  if ( stat )
    ClearRecord(partcode);
    partcode.CodeKind = PTCK_INN;
    partcode.PartyID  = corschem.CorrID;
    println( "~ReceiverCorrTaxID" );
    if ( getEQ(partcode) )
      println( CheckData(partcode.Code) );
    else
      print_empty();
    end;
  else                                        
    println( "~ReceiverCorrTaxID" );
    print_empty();
  end;

  /**************************** НАШ БАНК ******************************/
   /*Наименование нашего банка короткое*/
   println("~НашБанк");
   ПолучитьСубъекта( {OurBank}, PartyOwn );
   println(CheckData(PartyOwn.Name));

   /*наш банк адрес*/
   println("~НашБанкАдрес");
   ClearRecord(RecordAdressLegal);
   НайтиЮридическийАдресСубъекта(PartyOwn.PartyID,RecordAdressLegal);
   println(CheckData(RecordAdressLegal.PostIndex) + " " + CheckData(RecordAdressLegal.Adress));

  return true;
end;


/* формирование общих тэгов ( наименования, адреса и реквизиты ) */
macro CommonTag( )
  file proxy(dl_proxy);
  record party(party);
  
  if( not ProxyTag( DLPROXY_OURBANK ) )
    return false;
  end;
  
  if( not ProxyTag( DLPROXY_CONTRACTOR ) )
    return false;
  end;

  /* адреса и реквизиты нашего банка */
  if( not BankPropsTag( {OurBank} ) )
    return false;
  end;

  /* адреса и реквизиты контрагента */  
  if( not BankPropsTag( dlnet.Contractor ) )
    return false;
  end;

  println( "~Header" );
  if ( ПолучитьСубъекта( {OurBank}, party ) == 0 )
     ConfirmHeader = party.Name;
  end;
  println( ConfirmHeader );

  return true;
end;


/* формирование тегов для печати */ 
macro FormTagForPrint(NettingID, SPgroundID, IsConfirm)

  file spgrd(spgrdoc) key 0;
  file spgr(spground) key 0;
  file Templ(dlgrtemp) key 0;
  var date_net = date(0, 0, 0);

  ClearRecord(dlnet);
  dlnet.NettingID = NettingID;
  if ( not getEQ(dlnet) )
    msgbox( "Не найдена сделка неттинга с параметром NettingID = " + NettingID );
    return false;
  end;

  if ( not IsConfirm )
    ClearRecord(spgr);
    spgr.SPgroundID = SPgroundID;
    if ( not getEQ(spgr) )
      msgbox( "Не найден документ соглашения по сделке с параметром SpgroundID = " + SPgroundID );
      return false;
    end;

    Templ.DocLog = KINDDOC_CONTRBUYSALE;
    Templ.Num    = spgr.DocTemplate;
    if( GetEQ(Templ) == false )
       MsgBox( "Не найден шаблон договора по сделке неттинга" );
       return false;
    end;
  else
    ClearRecord(Templ);
    if ( List_GrTempl( int(KINDDOC_CONTRBUYSALE), 29, Templ ) )
      return true;
    end;
  end;


  println(Templ.File_Name);
  [d#######.rtf](StrTrimLeft(StrBefore(spgr.Xld,"/"),7)); 

  println();

  if ( not IsConfirm )
    println( "~Number" );
    println( spgr.Xld );

    println( "~Date_net" );
    println( spgr.SignedDate );
    date_net = spgr.SignedDate;
  end;

  /*Поищем финальный платеж неттинга*/
  FindFinalPayment();

  if ( not PaymTag() )
    return false;
  end;

  if ( not AllDealsTag(IsConfirm) )
    return false;
  end;

  if ( not FinsTag(date_net) )
    return false;
  end;

  if ( not FinalDltickTag() )
    return false;
  end;

  if ( not CommonTag() )
    return false;
  end;

  return true;
end;
