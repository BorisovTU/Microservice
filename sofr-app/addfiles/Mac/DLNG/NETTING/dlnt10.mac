/*
$Name:        dlnt10.mac
$Module:      Неттинг
$Description: макрос шага "Неттинг"
*/
IMPORT DealsInter, PaymInter, RsbDataSet, dlntlib, dlntfd, "dlcarry.inc", dlntpm, "sp_class.mac","dv_car.mac";

PRIVATE VAR 
    Параметры               = NULL,
    СписокТребОбяз          = NULL,
    ПлатежиВНеттинге        = TArray,
    ТОВНеттинге             = TArray,
    ДатыПлатежейВНеттинге   = TArray,
    ДатыТОВНеттинге         = TArray,
    СчетаПлатежейВНеттинге  = TArray,
    СчетаТОВНеттинге        = TArray,
    СуммыПлатежейВНеттинге  = TArray,
    СуммыТОВНеттинге        = TArray,
    ВалютыПлатежейВНеттинге = TArray,
    ВалютыТОВНеттинге       = TArray,
    ИтоговыеПлатежи         = TArray,
    Err                     = NULL;

CLASS NumBuf
   VAR
      НомерТреб   = - 1,
      НомерОбяз   = - 1,
      НомерТребТО = - 1,
      НомерОбязТО = - 1,
      СчетТреб    = "",
      СчетОбяз    = "",
      СчетТребТО  = "",
      СчетОбязТО  = "";
END;

CLASS ЭлемТрОб(ФИ_п)
  VAR 
      ФИ             = ФИ_п,
      Стр            = $0, /*базовая сумма требования*/
      Соб            = $0, /*бюазовая сумма обязательства*/
      СчетМРасчеты   = "",  /* счет по категории "-Расчеты"  */
      СчетПРасчеты   = "",  /* счет по категории "+Расчеты"  */
      СчетМРасчетыВО = "",  /* счет по категории "-Расчеты" в валюте оплаты (ВО) */
      СчетПРасчетыВО = "";  /* счет по категории "+Расчеты" в валюте оплаты (ВО)   */
END;

CLASS (TArray) КлассСпискаТрОб 
  MACRO ДобавитьЕслиЕщеНет( fiid )
     VAR i = 0;

     while( i < this.size )
        if( this[i].ФИ == fiid )
           return i;
        end;
        i = i + 1;
     end;
     this[this.size] = ЭлемТрОб(fiid);

     return i;
  END;

  MACRO ПолучитьИД( fiid )
     VAR i = 0;
     
     while( i < this.size )
        if( this[i].ФИ == fiid )
           return i;
        end;
        i = i + 1;
     end;
     return NULL;
  END;

  MACRO Получить( fiid )
     VAR id = ПолучитьИД(fiid);
     if( id != NULL )
        return this[id];
     end;
     return NULL;
  END;
END;

PRIVATE MACRO GetFiKindByID(FIID)
  var FI_KIND = 0;
  record finin(fininstr);
  ClearRecord(finin);

  if( not ПолучитьФинИн( FIID, finin ) ) 
    FI_KIND  = finin.FI_KIND;
  end;

  return FI_KIND;
END;

PRIVATE MACRO ПолучитьДатуОкрытияСчета(Account, ID_RQACC, FIID, AccountTO:@VARIANT)
  var DateOpen = Date(0,0,0);
  var sql, rsd; 

  if( not (Account == ""))

      sql = RSDCommand("   SELECT acc.t_Open_Date OpenDate "
                     + "     FROM daccount_dbt acc "
                     + "    WHERE acc.t_Code_Currency = ? and "
                     + "          acc.t_Chapter = 1 and "
                     + "          acc.t_Account = ? ");

      sql.addParam( "", RSDBP_IN, FIID );
      sql.addParam( "", RSDBP_IN, Account );
      sql.execute();

      rsd = TRsbDataSet(sql);
      if(rsd.MoveNext())
         if( ValType(rsd.OpenDate) != V_UNDEF )
           DateOpen = SQL_ConvTypeDate(rsd.OpenDate) ;
         end;
      end;
  end;
  if(ID_RQACC > 0)

      sql = RSDCommand("   SELECT acc.t_Open_Date OpenDate , acc.t_Account Account"
                     + "     FROM daccount_dbt acc , ddlrqacc_dbt rqacc"
                     + "    WHERE rqacc.t_ID =  ? and "
                     + "          acc.t_Code_Currency = rqacc.t_FIID and "
                     + "          acc.t_Chapter = 1 and "
                     + "          acc.t_Account =  rqacc.t_Account ");

      sql.addParam( "", RSDBP_IN, ID_RQACC );
      sql.execute();

      rsd = TRsbDataSet(sql);
      if(rsd.MoveNext())
         if( ValType(rsd.OpenDate) != V_UNDEF )
           DateOpen  = SQL_ConvTypeDate(rsd.OpenDate) ;
           AccountTO = rsd.Account;
         end;
      end;
  end;

  return DateOpen;
END;

PRIVATE MACRO ВыполнитьЗачетТребИОбязТС( ntg )

  VAR idx = 0, ok = true, СуммаЗачета, СчетМРасчеты, СчетПРасчеты, ВалютаРасчетов,
      cnt = СписокТребОбяз.size;
  //TEG 01.02.19 добавлена пачка для внебиржи
  Var NumberPack = 30;
  //TEG 02.05.19 ролевая модель
  var NumberOper = GetMainOperInGroup(ntg.DocKind);
    if ( NumberOper<= 0)
         NumberOper = {Oper};
    end;
   /**/

  while( ok and (idx < cnt) )
     СуммаЗачета = min(СписокТребОбяз[idx].Стр, СписокТребОбяз[idx].Соб);

     if( (GetFiKindByID( СписокТребОбяз[idx].ФИ) == FIKIND_CURRENCY) AND (ntg.PayFIID != СписокТребОбяз[idx].ФИ) AND (ntg.PayFIID != -1) )
        /*перевод сумм из ВП в ВО осуществляется по курсу на дату операции неттинга Dпл*/
        if( ConvSum(СуммаЗачета, СуммаЗачета, ntg.ValueDate, СписокТребОбяз[idx].ФИ, ntg.PayFIID) )
           Err.Добавить(NTG_ERR_BADCONVERT);
           return false;
        end;
        СчетМРасчеты   = СписокТребОбяз[idx].СчетМРасчетыВО;
        СчетПРасчеты   = СписокТребОбяз[idx].СчетПРасчетыВО;
        ВалютаРасчетов = ntg.PayFIID;
     else
        СчетМРасчеты   = СписокТребОбяз[idx].СчетМРасчеты;
        СчетПРасчеты   = СписокТребОбяз[idx].СчетПРасчеты;
        ВалютаРасчетов = СписокТребОбяз[idx].ФИ;
     end;

     if( СуммаЗачета and (СчетПРасчеты != СчетМРасчеты) )
        ok = (0 == DocCarry( 0,
                             1,
                             ВалютаРасчетов,
                             СчетМРасчеты,
                             СчетПРасчеты,
                             СуммаЗачета,
                             NULL,
                             NULL,
                             ОпределитьОснованиеПроводки(ntg),
                             null,
                             null,
                             null,
                             null,
                             NumberPack,null,null,null,NumberOper));
     end;

     idx = idx + 1;
  end;

  if( not ok )
     Err.Добавить(NTG_ERR_BADCARRYZTO);
  end;

  return ok;
END;

PRIVATE MACRO ПереносНаТС( ntg )

  VAR idx = 0, cnt = ПлатежиВНеттинге.size, id = 0,
      pmobj, СформироватьПроводку = false,
      СчетДебет, СчетКредит,
      СуммаПроводкиДебет = $0, СуммаПроводкиКредит = $0,
      ВДебет, ВКредит, ntg_fd, СуммаВВалютеВО = $0, SumEquivalentCarry = $0;/*\PNV*/
 //TEG 01.02.19 для внебиржи добавим пачку
    //TEG 02.05.19 ролевая модель
    var NumberOper = GetMainOperInGroup(ntg.DocKind);
    var FD;/*PNV 507791*/
    if ( NumberOper<= 0)
         NumberOper = {Oper};
    end;
   /**/

  ntg_fd = DL_NettingDoc(ntg);

  while( idx < cnt )
     SumEquivalentCarry = $0;/*PNV*/
     pmobj = ПлатежиВНеттинге[idx];

     ntg_fd.SetBaseFIID(pmobj.BaseFIID);

     id = СписокТребОбяз.ПолучитьИД(pmobj.BaseFIID);
     
     /*PNV*/
     if( СписокТребОбяз[id].СчетМРасчеты == "" )
         if( not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчеты, MC_OPENACC_CREATE, СписокТребОбяз[id].ФИ, ntg.SigningDate) )
             Err.Добавить(NTG_ERR_BADACCTC);
             return false;
         end;
     end;
     if( СписокТребОбяз[id].СчетПРасчеты == "" )
         if( not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчеты, MC_OPENACC_CREATE, СписокТребОбяз[id].ФИ, ntg.SigningDate))
             Err.Добавить(NTG_ERR_BADACCTC);
             return false;
         end;
     end;
     /*PNV*/

     if( PaymIsDemand(pmobj, ntg) == "" )
         СчетДебет = ПолучитьСчетПоПлатежу(pmobj, false);
       /*PNV 507791*/
         if (СчетДебет == "")
            FD = DVFirstDocNDeal(pmobj.dockind, pmobj.documentid);
        /*    msgbox("Входящая в неттинг сделка " + FD.deal.rec.code + " не поставлена на баланс");
            return false;*//*CHVA*/
         end;
       /*PNV 507791*/

        /*ВО не совпадает с ВП*/
        if( (GetFiKindByID( pmobj.BaseFIID) == FIKIND_CURRENCY) AND (pmobj.ReceiverFIID != pmobj.BaseFIID) )
           if( (not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчетыВО, MC_OPENACC_CREATE, pmobj.ReceiverFIID, ntg.SigningDate)) OR
               (not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчетыВО, MC_OPENACC_CREATE, pmobj.ReceiverFIID, ntg.SigningDate))
             )
              Err.Добавить(NTG_ERR_BADACCTC);
              return false;
           end;

           СписокТребОбяз[id].СчетМРасчеты = СписокТребОбяз[id].СчетМРасчетыВО;
           СписокТребОбяз[id].СчетПРасчеты = СписокТребОбяз[id].СчетПРасчетыВО;

           /*перевод сумм из ВП в ВО осуществляется по курсу на дату операции неттинга Dпл*/
           if( ConvSum(СуммаВВалютеВО, pmobj.BaseAmount, ntg.ValueDate, pmobj.BaseFIID, pmobj.ReceiverFIID) )
              Err.Добавить(NTG_ERR_BADCONVERT);
              return false;
           end;
           СчетКредит = СписокТребОбяз[id].СчетМРасчетыВО;
           СуммаПроводкиКредит = СуммаПроводкиДебет = СуммаВВалютеВО; 
           ВДебет = ВКредит = pmobj.ReceiverFIID;
        else
           if( СписокТребОбяз[id].СчетМРасчеты == "" )
              if( not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчеты, MC_OPENACC_CREATE, СписокТребОбяз[id].ФИ, ntg.SigningDate) )
                 Err.Добавить(NTG_ERR_BADACCTC);
                 return false;
              end;
           end;
           /*PNV*/
           if(СписокТребОбяз[id].СТР > СписокТребОбяз[id].СОБ)
              СчетКредит = СписокТребОбяз[id].СчетПРасчеты;
           else
              СчетКредит = СписокТребОбяз[id].СчетМРасчеты;
           end;

           if( (ВалютаМеталл(pmobj.BaseFIID)) and (СписокТребОбяз[id].СТР == СписокТребОбяз[id].СОБ) )
              if ( (id > 0) and (СписокТребОбяз[id-1].СТР < СписокТребОбяз[id-1].СОБ) )
                   СчетКредит = СписокТребОбяз[id].СчетПРасчеты;
              end;  
           end;

           /*pnv*/
           if(ВалютаМеталл(pmobj.BaseFIID))
              СуммаПроводкиДебет  = pmobj.PayerAmount; 
              ВДебет     = pmobj.PayerFIID;
              СчетДебет = ПолучитьСчетПоПлатежу(pmobj, true);
              if (substr(СчетДебет, 1, 5) != "61213" )
                  if( (not ntg_fd.GetAccount("Выбытие ДМ", СписокТребОбяз[id].СчетМРасчетыВО, MC_OPENACC_CREATE,  NATCUR, ntg.SigningDate, null, null, FIROLE_BA)) OR
                      (not ntg_fd.GetAccount("Выбытие ДМ", СписокТребОбяз[id].СчетПРасчетыВО, MC_OPENACC_CREATE,  NATCUR, ntg.SigningDate, null, null, FIROLE_BA))
                     )
                     Err.Добавить(NTG_ERR_BADACCTC);
                    return false;
                 end;
                 СчетДебет =  СписокТребОбяз[id].СчетПРасчетыВО; 
             end;
             ВДебет = NATCUR;
             if( ConvSum(СуммаПроводкиДебет, pmobj.BaseAmount, ntg.ValueDate, pmobj.BaseFIID, ВДебет) )
                 Err.Добавить(NTG_ERR_BADCONVERT);
                 return false;
             end;
              /*PNV чтобы не было проводки курсовой разницы*/
              if( ConvSum(СуммаВВалютеВО, pmobj.BaseAmount, ntg.ValueDate, pmobj.BaseFIID, NATCUR) )
                  Err.Добавить(NTG_ERR_BADCONVERT);
                  return false;
              end;
              if (pmobj.PayerFIID != NATCUR)  /*PNV 501140*/
                  SumEquivalentCarry = СуммаВВалютеВО;
              else
                  SumEquivalentCarry = СуммаПроводкиДебет; /*PNV 501140*/
              end;
              /*PNV*/
           else
              СуммаПроводкиДебет  = pmobj.ReceiverAmount;  
              ВДебет     = pmobj.ReceiverFIID;
           end;  
           СуммаПроводкиКредит = pmobj.BaseAmount;
           ВКредит    = pmobj.BaseFIID;
           /*pnv*/
        end;

        if( СчетДебет == СчетКредит )
           СчетДебет = NULL;
        end;
     else
         СчетКредит = ПолучитьСчетПоПлатежу(pmobj, true);
       /*PNV 507791*/
         if (СчетКредит == "")
            FD = DVFirstDocNDeal(pmobj.dockind, pmobj.documentid);
       /*     msgbox("Входящая в неттинг сделка " + FD.deal.rec.code + " не поставлена на баланс");
            return false;*/
         end;
       /*PNV 507791*/ 
        /*ВО не совпадает с ВП*/
        if( (GetFiKindByID( pmobj.BaseFIID) == FIKIND_CURRENCY) AND (pmobj.PayerFIID != pmobj.BaseFIID) )
           if( (not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчетыВО, MC_OPENACC_CREATE, pmobj.PayerFIID, ntg.SigningDate)) OR
               (not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчетыВО, MC_OPENACC_CREATE, pmobj.PayerFIID, ntg.SigningDate))
             )
              Err.Добавить(NTG_ERR_BADACCTC);
              return false;
           end;

           СписокТребОбяз[id].СчетМРасчеты = СписокТребОбяз[id].СчетМРасчетыВО;
           СписокТребОбяз[id].СчетПРасчеты = СписокТребОбяз[id].СчетПРасчетыВО;

           /*перевод сумм из ВП в ВО осуществляется по курсу на дату операции неттинга Dпл*/
           if( ConvSum(СуммаВВалютеВО, pmobj.BaseAmount, ntg.ValueDate, pmobj.BaseFIID, pmobj.PayerFIID) )
              Err.Добавить(NTG_ERR_BADCONVERT);
              return false;
           end;
           СчетДебет = СписокТребОбяз[id].СчетПРасчетыВО;
           СуммаПроводкиКредит = СуммаПроводкиДебет = СуммаВВалютеВО;
           ВДебет = ВКредит = pmobj.PayerFIID;
        else
           if( СписокТребОбяз[id].СчетПРасчеты == "" )
              if( not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчеты, MC_OPENACC_CREATE, СписокТребОбяз[id].ФИ, ntg.SigningDate))
                 Err.Добавить(NTG_ERR_BADACCTC);
                 return false;
              end;
           end;
           /*PNV*/
           if(СписокТребОбяз[id].СТР >СписокТребОбяз[id].СОБ)
              СчетДебет = СписокТребОбяз[id].СчетПРасчеты;
           else
              СчетДебет = СписокТребОбяз[id].СчетМРасчеты;
           end;
           if( (ВалютаМеталл(pmobj.BaseFIID)) and (СписокТребОбяз[id].СТР == СписокТребОбяз[id].СОБ) )
              if ( (id > 0) and (СписокТребОбяз[id-1].СТР < СписокТребОбяз[id-1].СОБ) )
                    СчетДебет = СписокТребОбяз[id].СчетПРасчеты;
              end;  
           end;
         /*pnv*/
           if(ВалютаМеталл(pmobj.BaseFIID))
              if ((pmobj.purpose == 1) and (pmobj.dockind == 199) )/*PNV только для СВОП по 1 части, т.к. там проводки через 616*/
                  СуммаПроводкиКредит = pmobj.ReceiverAmount;          
              end;
              ВКредит    = pmobj.ReceiverFIID;
              /*PNV чтобы не было проводки курсовой разницы*/
              if( ConvSum(СуммаВВалютеВО, pmobj.BaseAmount, ntg.ValueDate, pmobj.BaseFIID, NATCUR) )
                  Err.Добавить(NTG_ERR_BADCONVERT);
                  return false;
              end;
              if (pmobj.ReceiverFIID != NATCUR) /*PNV 501140*/
                  SumEquivalentCarry = СуммаВВалютеВО;
              else
                  SumEquivalentCarry = СуммаПроводкиКредит; /*PNV 501140*/
              end;
              /*PNV*/
           else
              СуммаПроводкиКредит = pmobj.PayerAmount;
              ВКредит    = pmobj.PayerFIID;
           end;  
         /*pnv*/
           СуммаПроводкиДебет  = pmobj.BaseAmount;            
           ВДебет     = pmobj.BaseFIID; /*pnv*/
        end;

        if( СчетДебет == СчетКредит )
           СчетКредит = NULL;
        end;
     end;

     СформироватьПроводку = false;

     if( (pmobj.DocKind == DL_IBCDOC) or (pmobj.DocKind == DL_FOREXDOC) )
        if( (pmobj.Purpose == PM_PURP_PRINC) or
            (pmobj.Purpose == PM_PURP_PRINC_RET) or
            (pmobj.Purpose == PM_PURP_OVD_PRINC)or
            (pmobj.Purpose == BAi) or
            (pmobj.Purpose == CAi)
          )
           СформироватьПроводку = true;
        end;
     elif( pmobj.DocKind == DL_VEKSELACCOUNTED )
        if( pmobj.Purpose == CAi )
           СформироватьПроводку = true;
        end;
     elif( (pmobj.DocKind == DL_DVNDEAL) or (pmobj.DocKind == DL_DVDEALT3) or (pmobj.DocKind == DL_DVFXDEAL) )
        СформироватьПроводку = true;
     end;
    
     if( СчетДебет and СчетКредит and СформироватьПроводку )
// KS 29.04.2019 Если на выходной
//        if( DocCarry(false, 1, ВДебет, СчетДебет, СчетКредит, СуммаПроводкиДебет, ntg.ValueDate, NULL, ОпределитьОснованиеПроводки(ntg), pmobj.PaymentID, null, null, null, 30, ВКредит, СуммаПроводкиКредит,  SumEquivalentCarry) != 0 )/*PNV*/
        if( DocCarry(false, 1, ВДебет, СчетДебет, СчетКредит, СуммаПроводкиДебет, GetDateAfterWorkDays(ntg.ValueDate, 0), NULL, ОпределитьОснованиеПроводки(ntg), pmobj.PaymentID, null, null, null, 30, ВКредит, СуммаПроводкиКредит,  SumEquivalentCarry) != 0 )/*PNV*/
           Err.Добавить(NTG_ERR_BADCARRYPTC);
           return false;
        end;
     end;

     idx = idx + 1;
  end;

  return true;
END;

PRIVATE MACRO ПолучитьНашСчетПоТО( ntg, rq:TRecHandler )
  var LegKind;
  var FD = NULL;
  var AccountBuf = TRecHandler("account");

  AccountBuf.Clear();

  if(rq.rec.DealPart == 2)
    LegKind = LEG_KIND_DL_TICK_BACK;
  else
    LegKind = LEG_KIND_DL_TICK;
  end;

  FD = SPFirstDoc(LegKind, rq.rec.DocID);

  if( FD.tick.rec.ClientID != -1 )
     ExecMacroFile("spserv.mac", "ПолучитьСчетСубъектаПоСделке", FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.ClientID, DLRQ_SUBKIND_CURRENCY, FD.GetPayFIID(), AccountBuf);
  else
     if( rq.rec.Type == DLRQ_TYPE_PAYMENT )
        ExecMacroFile("spserv.mac", "ПолучитьНашСчетТОПоОплате", rq, FD, ntg.SigningDate, AccountBuf, AccountBuf);
     elif( (rq.rec.Type == DLRQ_TYPE_AVANCE) OR (rq.rec.Type == DLRQ_TYPE_DEPOSIT) )
        ExecMacroFile("spserv.mac", "ПолучитьНашСчетТОПоАвансуЗадатку", rq, FD, ntg.SigningDate, AccountBuf);
     elif( rq.rec.Type == DLRQ_TYPE_INCREPO )
        ExecMacroFile("spserv.mac", "ПолучитьНашСчетТОПоПроцентам", rq, FD, ntg.SigningDate, AccountBuf);
     end;
  end;

  return AccountBuf.rec.Account;
END;

PRIVATE MACRO ПереносНаТС_ТО( ntg )

  VAR idx = 0, cnt = ТОВНеттинге.size, id = 0,
      СформироватьПроводку = false,
      СчетДебет, СчетКредит,
      СуммаПроводкиДебет = $0, СуммаПроводкиКредит = $0,
      ВДебет, ВКредит, ntg_fd, СуммаВВалютеВО = $0;
   //TEG 02.05.19 ролевая модель
   var NumberOper = GetMainOperInGroup(ntg.DocKind);
   if ( NumberOper<= 0)
         NumberOper = {Oper};
   end;
   /**/

  var rq = TRecHandler("dlrq");
  var FD = NULL;
  var LegKind;

  ntg_fd = DL_NettingDoc(ntg);

  while (idx < cnt)
     rq = ТОВНеттинге[idx];

     ntg_fd.SetBaseFIID(rq.rec.FIID);

     id = СписокТребОбяз.ПолучитьИД(rq.rec.FIID);

     if( rq.rec.DealPart == 2 )
       LegKind = LEG_KIND_DL_TICK_BACK;
     else
       LegKind = LEG_KIND_DL_TICK;
     end;

     FD = SPFirstDoc(LegKind, rq.rec.DocID);

     if( rq.rec.Kind == DLRQ_KIND_COMMIT )
        СчетДебет = ПолучитьНашСчетПоТО(ntg, rq);

        /*ВО не совпадает с ВП*/
       if( (GetFiKindByID( rq.rec.FIID) == FIKIND_CURRENCY) AND (rq.rec.FIID != FD.GetPayFIID()) )
           if( (not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчетыВО, MC_OPENACC_CREATE, FD.GetPayFIID(), ntg.SigningDate)) OR
               (not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчетыВО, MC_OPENACC_CREATE, FD.GetPayFIID(), ntg.SigningDate))
             )
              Err.Добавить(NTG_RQERR_BADACCTC);
              return false;
           end;

           СписокТребОбяз[id].СчетМРасчеты = СписокТребОбяз[id].СчетМРасчетыВО;
           СписокТребОбяз[id].СчетПРасчеты = СписокТребОбяз[id].СчетПРасчетыВО;

           /*перевод сумм из ВП в ВО осуществляется по курсу на дату операции неттинга Dпл*/
           if( ConvSum(СуммаВВалютеВО, rq.rec.Amount, ntg.ValueDate, rq.rec.FIID, FD.GetPayFIID()) )
              Err.Добавить(NTG_RQERR_BADCONVERT);
              return false;
           end;
           СчетКредит = СписокТребОбяз[id].СчетМРасчетыВО;
           СуммаПроводкиКредит = СуммаПроводкиДебет = СуммаВВалютеВО; 
           ВДебет = ВКредит = FD.GetPayFIID();
        else
           if( СписокТребОбяз[id].СчетМРасчеты == "" )
              if( not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчеты, MC_OPENACC_CREATE, СписокТребОбяз[id].ФИ, ntg.SigningDate) )
                 Err.Добавить(NTG_RQERR_BADACCTC);
                 return false;
              end;
           end;

           СчетКредит = СписокТребОбяз[id].СчетМРасчеты;
           СуммаПроводкиДебет  = rq.rec.Amount; 
           СуммаПроводкиКредит = rq.rec.Amount;
           ВДебет      = FD.GetPayFIID();
           ВКредит     = FD.GetPayFIID();
        end; 

        if( СчетДебет == СчетКредит )
           СчетДебет = NULL;
        end;
     else
         СчетКредит = ПолучитьНашСчетПоТО(ntg, rq);

        /*ВО не совпадает с ВП*/
        if( (GetFiKindByID( rq.rec.FIID) == FIKIND_CURRENCY) AND (FD.GetPayFIID() != rq.rec.FIID) )
           if( (not ntg_fd.GetAccount("-Расчеты", СписокТребОбяз[id].СчетМРасчетыВО, MC_OPENACC_CREATE, FD.GetPayFIID(), ntg.SigningDate)) OR
               (not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчетыВО, MC_OPENACC_CREATE, FD.GetPayFIID(), ntg.SigningDate))
             )
              Err.Добавить(NTG_RQERR_BADACCTC);
              return false;
           end;

           СписокТребОбяз[id].СчетМРасчеты = СписокТребОбяз[id].СчетМРасчетыВО;
           СписокТребОбяз[id].СчетПРасчеты = СписокТребОбяз[id].СчетПРасчетыВО;

           /*перевод сумм из ВП в ВО осуществляется по курсу на дату операции неттинга Dпл*/
           if( ConvSum(СуммаВВалютеВО, rq.rec.Amount, ntg.ValueDate, rq.rec.FIID, FD.GetPayFIID()) )
              Err.Добавить(NTG_RQERR_BADCONVERT);
              return false;
           end;
           СчетДебет = СписокТребОбяз[id].СчетПРасчетыВО;
           СуммаПроводкиКредит = СуммаПроводкиДебет = СуммаВВалютеВО;
           ВДебет = ВКредит = FD.GetPayFIID();
        else
           if( СписокТребОбяз[id].СчетПРасчеты == "" )
              if( not ntg_fd.GetAccount("+Расчеты", СписокТребОбяз[id].СчетПРасчеты, MC_OPENACC_CREATE, СписокТребОбяз[id].ФИ, ntg.SigningDate) )
                 Err.Добавить(NTG_RQERR_BADACCTC);
                 return false;
              end;
           end;

           СчетДебет = СписокТребОбяз[id].СчетПРасчеты;
           СуммаПроводкиДебет  = rq.rec.Amount;
           СуммаПроводкиКредит = rq.rec.Amount;
           ВДебет      = FD.GetPayFIID();
           ВКредит     = FD.GetPayFIID();
        end;

        if( СчетДебет == СчетКредит )
           СчетКредит = NULL;
        end;
     end;

     СформироватьПроводку = false;

     if( (rq.rec.Type == DLRQ_TYPE_AVANCE) or
         (rq.rec.Type == DLRQ_TYPE_DEPOSIT) or
         (rq.rec.Type == DLRQ_TYPE_PAYMENT)
       )
        СформироватьПроводку = true;
     end;

     if( СчетДебет and СчетКредит and СформироватьПроводку )

// KS 29.04.2019 Если на выходной
//        if( DocCarry(false, 1, ВДебет, СчетДебет, СчетКредит, СуммаПроводкиДебет, ntg.ValueDate, NULL, ОпределитьОснованиеПроводки(ntg), NULL, null, null, null, 30, ВКредит, СуммаПроводкиКредит) != 0 )/*pnv*/
        if( DocCarry(false, 1, ВДебет, СчетДебет, СчетКредит, СуммаПроводкиДебет, GetDateAfterWorkDays(ntg.ValueDate, 0), NULL, ОпределитьОснованиеПроводки(ntg), NULL, null, null, null, 30, ВКредит, СуммаПроводкиКредит) != 0 )/*pnv*/
           Err.Добавить(NTG_RQERR_BADCARRYPTC);
           return false;
        end;
     end;

     idx = idx + 1;
  end;

  return true;
END;

PRIVATE MACRO НайтиРезультирПлатеж( fiid )
  VAR idx = 0, pmobj = NULL,
      cnt = ИтоговыеПлатежи.size;

  while( idx < cnt )
     pmobj = ИтоговыеПлатежи[idx];

     if( NTG_ActiveFIID(pmobj.GetPM_PAYM()) == fiid )
        return pmobj;
     end;

     idx = idx + 1;
  end;

  return NULL;
END;

PRIVATE MACRO ДобавитьИтоговыйПлатеж( pmobj )
  ИтоговыеПлатежи[ИтоговыеПлатежи.size] = pmobj;
  return true;
END;

PRIVATE MACRO ПроверитьИтоговыеПлатежи( ntg )
  VAR idx = 0, cnt = СписокТребОбяз.size, pmobj = NULL;

  if( not ForEachPMResPaym(ntg.IdentProgram, ntg.NettingID, @ДобавитьИтоговыйПлатеж, NULL) )
     Err.Добавить(NTG_ERR_NOFNLPM);
     return false;
  else
     while( idx < cnt )
        pmobj = НайтиРезультирПлатеж(СписокТребОбяз[idx].ФИ);

        if( pmobj )
           if( СписокТребОбяз[idx].Стр == СписокТребОбяз[idx].Соб )
// оставляем нулевые платежи
//              Err.Добавить(NTG_ERR_CNTFNLPM, string(СписокТребОбяз[idx].ФИ));
//              return false;
           elif( СписокТребОбяз[idx].Стр < СписокТребОбяз[idx].Соб )
              if( (PaymIsDemand(pmobj, ntg) != "") or
                  (pmobj.BaseAmount != (СписокТребОбяз[idx].Соб - СписокТребОбяз[idx].Стр))
                )
                 Err.Добавить(NTG_ERR_BADFNLPM, ПолучитьКодФинИн(СписокТребОбяз[idx].ФИ));
                 return false;
              end; 
           else
              if( (PaymIsDemand(pmobj, ntg) != "X") or 
                  (pmobj.BaseAmount != (СписокТребОбяз[idx].Стр - СписокТребОбяз[idx].Соб))
                )
                 Err.Добавить(NTG_ERR_BADFNLPM, ПолучитьКодФинИн(СписокТребОбяз[idx].ФИ));
                 return false;
              end; 
           end;
        else
           if( (СписокТребОбяз[idx].Стр - СписокТребОбяз[idx].Соб) != 0 )
              Err.Добавить(NTG_ERR_NOFNLPM);
              return false;
           end;
        end;

        idx = idx + 1; 
     end;
  end;

  return true;
END;

PRIVATE MACRO ДобавитьПлатежВалюту(pml, ntg)

  VAR pmobj = NULL, idx = 0, FD = NULL, DocKind, error = 0;

  pmobj = ПлатежиВНеттинге[ПлатежиВНеттинге.size] = CNtgPayment(ntg.IdentProgram, pml.InitialPayment);

  if (Err == null)
      Err = STEP_10_ERROR;
  end; 

  if( pmobj.PaymentID == 0 )
     ПлатежиВНеттинге.size = ПлатежиВНеттинге.size -1;
  else
     error = NGT_ВалидацияПлатежаНеттинга(pmobj, ntg);

     if( error != 0 )          
        Err.Добавить(error);
        return false;
     end;

     idx = СписокТребОбяз.ДобавитьЕслиЕщеНет(pml.fiid);

     if( PaymIsDemand(pmobj, ntg) == "" )
        СписокТребОбяз[idx].Соб = СписокТребОбяз[idx].Соб + pmobj.BaseAmount;
     else
        СписокТребОбяз[idx].Стр = СписокТребОбяз[idx].Стр + pmobj.BaseAmount;
     end;

     if( pmobj.PaymStatus != PM_CLOSED_W_M_MOVEMENT )
        pmobj.PaymStatus = PM_CLOSED_W_M_MOVEMENT;
     end;

     if( (pmobj.PayerAmount == $0) or 
         (pmobj.ReceiverAmount == $0)
       )
        pmobj.BaseRate.Actuate(ntg.ValueDate, false);
        pmobj.FactRate.Actuate(ntg.ValueDate, false);

        error = 0;
        error = pmobj.Actuate(); 
        if( error == 545 )
           Err.Добавить(NTG_ERR_BADBASERATE);
           return false;
        elif( error )
           Err.Добавить(NTG_ERR_BADCONVERT);
           return false;
        end;
     end;

     if( not Параметры.НеттингЧерезТранзитныеСчета )
        if( PaymIsDemand(pmobj, ntg) == "" )
           СуммыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]  = pmobj.PayerAmount;
           ВалютыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1] = pmobj.PayerFIID;
           ДатыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]   = ПолучитьДатуОкрытияСчета(pmobj.PayerAccount, 0, pmobj.PayerFIID, NULL);
           СчетаПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]  = pmobj.PayerAccount;
        else
           СуммыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]  = pmobj.ReceiverAmount;
           ВалютыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1] = pmobj.ReceiverFIID;
           ДатыПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]   = ПолучитьДатуОкрытияСчета(pmobj.ReceiverAccount, 0, pmobj.ReceiverFIID, NULL);
           СчетаПлатежейВНеттинге[ПлатежиВНеттинге.size - 1]  = pmobj.ReceiverAccount;
        end;
     end;
  end;

  return true;
END;

PRIVATE MACRO ДобавитьТОВалюту( pml, ntg )
  VAR idx = 0, FD = NULL, DocKind, error = 0;
  var rq = TRecHandler("dlrq");

  NGT_GetRQ(pml.InitialPayment, rq);

  if( rq.rec.ID )

     ТОВНеттинге[ТОВНеттинге.size] = rq;

     error = NGT_ВалидацияТОНеттинга(rq, ntg, Параметры);

     if( error != 0 )
        Err.Добавить(error);
        return false;
     end;

     idx = СписокТребОбяз.ДобавитьЕслиЕщеНет(pml.fiid);

     if( rq.rec.Kind == DLRQ_KIND_COMMIT )
        СписокТребОбяз[idx].Соб = СписокТребОбяз[idx].Соб + rq.rec.Amount;
     else
        СписокТребОбяз[idx].Стр = СписокТребОбяз[idx].Стр + rq.rec.Amount;
     end;

     if( (rq.rec.DocKind == DL_SECURITYDOC) or (rq.rec.DocKind == DL_RETIREMENT) )
        if( (rq.rec.State != DLRQ_STATE_EXEC) )
           rq.rec.State    = DLRQ_STATE_EXEC;
           rq.rec.FactDate = rq.rec.PlanDate;
        else
           if( rq.rec.DealPart == 2 )
              DocKind = LEG_KIND_DL_TICK_BACK;
           else
              DocKind = LEG_KIND_DL_TICK;
           end; 

           FD = SPFirstDoc(DocKind, rq.rec.DocID);

           if( not ExecMacroFile("sp_car.mac", "ОбновитьФлагиЧастиСделки", FD.dl_leg, DL_LEG_NETTING_EXECUTE) )
              Err.Добавить(NTG_RQERR_UPDDLFLAG);
              return false;
           end;
        end;
     end;

     if( not Параметры.НеттингЧерезТранзитныеСчета )
        var AccountTO = "";
        СуммыТОВНеттинге[ТОВНеттинге.size - 1]  = rq.rec.Amount;
        ВалютыТОВНеттинге[ТОВНеттинге.size - 1] = rq.rec.FIID;
        ДатыТОВНеттинге[ТОВНеттинге.size - 1]   = ПолучитьДатуОкрытияСчета("", rq.rec.RQACCID, -1 , AccountTO) ;
        СчетаТОВНеттинге[ТОВНеттинге.size - 1]  = AccountTO;
     end;
  end;

  return true;
END;

PRIVATE MACRO ОпределитьНаборВалют( ntg ):bool
  var RetVal:bool = true;

  RetVal = ForEachPMLink(DL_NTGDOC, ntg.NettingID, @ДобавитьПлатежВалюту, ntg);

  if( RetVal )
     RetVal = ForEachPMLinkRQ(DL_NTGDOC, ntg.NettingID, @ДобавитьТОВалюту, ntg);
  end;

  return RetVal;
END;

PRIVATE MACRO НайтиМаксМинСуммуПлатежа( fiid, minsum, maxsum, isdemand, ntg )
  VAR idx = 0, pmobj = NULL, rq = NULL,
      cnt = ПлатежиВНеттинге.size;

  minsum = $0;
  maxsum = $0;

  while( idx < cnt )
     pmobj = ПлатежиВНеттинге[idx];
     if( (PaymIsDemand(pmobj, ntg) == isdemand) and (pmobj.BaseFIID == fiid) )
        if( (minsum > pmobj.BaseAmount) or (minsum == 0) )
           minsum = pmobj.BaseAmount;
        end;

        if( maxsum < pmobj.BaseAmount )
           maxsum = pmobj.BaseAmount;
        end;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = ТОВНеттинге.size;

  while (idx < cnt)
     rq = ТОВНеттинге[idx];

     if( (rq.rec.Kind == IIF(isdemand == "X", DLRQ_KIND_REQUEST, DLRQ_KIND_COMMIT)) and (rq.rec.FIID == fiid) )
        if( (minsum > rq.rec.Amount) or (minsum == 0) )
           minsum = rq.rec.Amount;
        end;

        if( maxsum < rq.rec.Amount )
           maxsum = rq.rec.Amount;
        end;
     end;

     idx = idx + 1;
  end;

  SetParm(1, minsum);
  SetParm(2, maxsum);
END;

PRIVATE MACRO ПроверитьНеснеттингованныеПлатежи( ntg )

  VAR idx = 0, cnt = СписокТребОбяз.size,
      СуммаРезПлатежа = $0,
      МинСумма, МаксСумма;

  while( idx < cnt )
     СуммаРезПлатежа = СписокТребОбяз[idx].Стр - СписокТребОбяз[idx].Соб;

     if( СуммаРезПлатежа > 0 )
        НайтиМаксМинСуммуПлатежа(СписокТребОбяз[idx].ФИ, МинСумма, МаксСумма, "X", ntg);
     else
        НайтиМаксМинСуммуПлатежа(СписокТребОбяз[idx].ФИ, МинСумма, МаксСумма, "", ntg);
     end;

     СуммаРезПлатежа = abs(СуммаРезПлатежа);

     if( Параметры.ПользовательскиеПараметры.ОтборПлатежейПоУбыванию )
        if( (СуммаРезПлатежа > МинСумма) and (ntg.IncludeOnewayPayment != "X") )
           Err.Добавить(NTG_ERR_ONEWAYPAYMS, ПолучитьКодФинИн(СписокТребОбяз[idx].ФИ));
           return false;
        end;
     else
        if( (СуммаРезПлатежа > МаксСумма) and (ntg.IncludeOnewayPayment != "X") )
           Err.Добавить(NTG_ERR_ONEWAYPAYMS, ПолучитьКодФинИн(СписокТребОбяз[idx].ФИ));
           return false;
        end;
     end;

     idx = idx + 1; 
  end;

  return true;
END;

PRIVATE MACRO ПолучитьОчереднуюСуммуЗачета( Валюта, Дт, Кт, СуммаЗачета, ВалютаЗачета, ntg )

  VAR idx = 0, cnt = СуммыПлатежейВНеттинге.size,
      Требование = NULL, Обязательство = NULL,
      СумТребование = 0, СумОбязательство = 0,
      pmsum = NULL, pmobj = NULL, rq = NULL,
      ТребованиеIsPM = NULL, ОбязательствоIsPM = NULL;

  while( idx < cnt )
     pmobj = ПлатежиВНеттинге[idx];

     if( pmobj.BaseFIID == Валюта )
        pmsum = СуммыПлатежейВНеттинге[idx];
        if( PaymIsDemand(pmobj, ntg) == "X" )
           if( Параметры.ПользовательскиеПараметры.ОтборПлатежейПоУбыванию )
              if( СумТребование < pmsum )
                 СумТребование  = pmsum;
                 Требование     = idx;
                 ТребованиеIsPM = true;
              end;
           else
              if( ((СумТребование > pmsum) and (pmsum != 0)) or (СумТребование == 0) )
                 СумТребование  = pmsum;
                 Требование     = idx;
                 ТребованиеIsPM = true;
              end;
           end;
        else
           if( Параметры.ПользовательскиеПараметры.ОтборПлатежейПоУбыванию )
              if( СумОбязательство < pmsum )
                 СумОбязательство  = pmsum;
                 Обязательство     = idx;
                 ОбязательствоIsPM = true;
              end;
           else
              if( ((СумОбязательство > pmsum) and (pmsum != 0)) or (СумОбязательство == 0) )
                 СумОбязательство  = pmsum;
                 Обязательство     = idx;
                 ОбязательствоIsPM = true;
              end;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = СуммыТОВНеттинге.size;

  while( idx < cnt )
     rq = ТОВНеттинге[idx];

     if( rq.rec.FIID == Валюта )
        pmsum = СуммыТОВНеттинге[idx];

        if( rq.rec.Kind == DLRQ_KIND_REQUEST )
           if( Параметры.ПользовательскиеПараметры.ОтборПлатежейПоУбыванию )
              if( СумТребование < pmsum )
                 СумТребование  = pmsum;
                 Требование     = idx;
                 ТребованиеIsPM = false;
              end;
           else
              if( ((СумТребование > pmsum) and (pmsum != 0)) or (СумТребование == 0) )
                 СумТребование  = pmsum;
                 Требование     = idx;
                 ТребованиеIsPM = false;
              end;
           end;
        else
           if( Параметры.ПользовательскиеПараметры.ОтборПлатежейПоУбыванию )
              if( СумОбязательство < pmsum )
                 СумОбязательство  = pmsum;
                 Обязательство     = idx;
                 ОбязательствоIsPM = false;
              end;
           else
              if( ((СумОбязательство > pmsum) and (pmsum != 0)) or (СумОбязательство == 0) )
                 СумОбязательство  = pmsum;
                 Обязательство     = idx;
                 ОбязательствоIsPM = false;
              end;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  if( (СумТребование == 0) or (СумОбязательство == 0) )
     return false;
  end;

  idx = 0;
  cnt = СуммыПлатежейВНеттинге.size;

  while( idx < cnt )
     pmobj = ПлатежиВНеттинге[idx];

     if( pmobj.BaseFIID == Валюта )
        pmsum = СуммыПлатежейВНеттинге[idx];
        if( PaymIsDemand(pmobj, ntg) == "X" )
           if( (СумТребование > СумОбязательство) and (СумОбязательство == pmsum) )
              СумТребование  = pmsum;
              Требование     = idx;
              ТребованиеIsPM = true;
           end;
        else
           if( (СумТребование < СумОбязательство) and (СумТребование == pmsum) )
              СумОбязательство = pmsum;
              Обязательство    = idx;
              ОбязательствоIsPM = true;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = СуммыТОВНеттинге.size;

  while( idx < cnt )
     rq = ТОВНеттинге[idx];

     if( rq.rec.FIID == Валюта )
        pmsum = СуммыТОВНеттинге[idx];
        if( rq.rec.Kind == DLRQ_KIND_REQUEST )
           if( (СумТребование > СумОбязательство) and (СумОбязательство == pmsum) )
              СумТребование  = pmsum;
              Требование     = idx;
              ТребованиеIsPM = false;
           end;
        else
           if( (СумТребование < СумОбязательство) and (СумТребование == pmsum) )
              СумОбязательство  = pmsum;
              Обязательство     = idx;
              ОбязательствоIsPM = false;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  СуммаЗачета = min(СумТребование, СумОбязательство);

  if( СуммаЗачета == 0 )
     return false;
  end;

  if( ТребованиеIsPM )
     ВалютаЗачета = ВалютыПлатежейВНеттинге[Требование];
     СуммыПлатежейВНеттинге[Требование] = СумТребование - СуммаЗачета;
  else
     ВалютаЗачета = ВалютыТОВНеттинге[Требование];
     СуммыТОВНеттинге[Требование] = СумТребование - СуммаЗачета;
  end;

  if( ОбязательствоIsPM )
     СуммыПлатежейВНеттинге[Обязательство] = СумОбязательство - СуммаЗачета;
  else
     СуммыТОВНеттинге[Обязательство] = СумОбязательство - СуммаЗачета;
  end;

  if( СумТребование > СумОбязательство )
     if( ОбязательствоIsPM )
        ПлатежиВНеттинге[Обязательство].PaymStatus = PM_CLOSED_W_M_MOVEMENT;
     else
        ТОВНеттинге[Обязательство].rec.State = DLRQ_STATE_EXEC;
     end;
  else
     if( ТребованиеIsPM )
        ПлатежиВНеттинге[Требование].PaymStatus = PM_CLOSED_W_M_MOVEMENT;
     else
        ТОВНеттинге[Требование].rec.State = DLRQ_STATE_EXEC;
     end;
  end;

  if( ОбязательствоIsPM )
     SetParm(1, ПолучитьСчетПоПлатежу(ПлатежиВНеттинге[Обязательство], false));
  else
     SetParm(1, ПолучитьНашСчетПоТО(ntg, ТОВНеттинге[Обязательство]));
  end;
  if( ТребованиеIsPM )
     SetParm(2, ПолучитьСчетПоПлатежу(ПлатежиВНеттинге[Требование], true));
  else
     SetParm(2, ПолучитьНашСчетПоТО(ntg, ТОВНеттинге[Требование]));
  end;
  SetParm(3, СуммаЗачета);
  SetParm(4, ВалютаЗачета);

  return true;
END;
PRIVATE MACRO ПолучитьОчереднуюСуммуЗачетаПоДате( Валюта, Дт, Кт, СуммаЗачета, ВалютаЗачета, ntg, NB:@VARIANT /*НомерТреб:@VARIANT, НомерОбяз:@VARIANT, НомерТребТО:@BOOL, НомерОбязТО:@BOOL */ )

  VAR idx = 0, cnt = СуммыПлатежейВНеттинге.size,
      Требование = NULL, Обязательство = NULL,
      СумТребование = 0, СумОбязательство = 0,
      pmsum = NULL, pmobj = NULL, rq = NULL,
      ТребованиеIsPM = NULL, ОбязательствоIsPM = NULL,
      ДатаТреб = Date(0,0,0), ДатаОбяз = Date(0,0,0) ;
  var DateZero = Date(0,0,0), pmdate = NULL;

  while( idx < cnt )
     pmobj = ПлатежиВНеттинге[idx];

     if( pmobj.BaseFIID == Валюта )
//        pmsum = СуммыПлатежейВНеттинге[idx];
        pmdate = ДатыПлатежейВНеттинге[idx];

        if( (PaymIsDemand(pmobj, ntg) == "X") )
           if( NB.НомерТреб == -1) 
              if((NB.СчетТреб == "") or ((NB.СчетТреб == СчетаПлатежейВНеттинге[idx] )))
                 if( (NB.НомерТребТО == -1) and (NB.СчетТребТО == "") )
                    if( Параметры.ПользовательскиеПараметры.ОтборПлатежейПоУбыванию )
                       if( ДатаТреб < pmdate )
                          ДатаТреб       = pmdate;
                          Требование     = idx;
                          ТребованиеIsPM = true;
                       end;
                    else
                       if( ((ДатаТреб > pmdate) and (pmdate != DateZero)) or (ДатаТреб == DateZero) )
                          ДатаТреб       = pmdate;
                          Требование     = idx;
                          ТребованиеIsPM = true;
                       end;
                    end;
                 end;
              end;
           else
             Требование     = NB.НомерТреб;
             ДатаТреб       = ДатыПлатежейВНеттинге[Требование];
             ТребованиеIsPM = true;
           end;
        else
           if( NB.НомерОбяз == -1 )
              if((NB.СчетОбяз == "") or ((NB.СчетОбяз == СчетаПлатежейВНеттинге[idx] )))
                 if( (NB.НомерОбязТО == -1) and (NB.СчетОбязТО == ""))
                    if( Параметры.ПользовательскиеПараметры.ОтборПлатежейПоУбыванию )
                       if( ДатаОбяз < pmdate )
                          ДатаОбяз  = pmdate;
                          Обязательство     = idx;
                          ОбязательствоIsPM = true;
                       end;
                    else
                       if( ((ДатаОбяз > pmdate) and (pmdate != DateZero)) or (ДатаОбяз == DateZero) )
                          ДатаОбяз  = pmdate;
                          Обязательство     = idx;
                          ОбязательствоIsPM = true;
                       end;
                    end;
                 end;
              end;
           else
               Обязательство     = NB.НомерОбяз;
               ДатаОбяз          = ДатыПлатежейВНеттинге[Обязательство];
               ОбязательствоIsPM = true;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = СуммыТОВНеттинге.size;

  while( idx < cnt )
     rq = ТОВНеттинге[idx];

     if( rq.rec.FIID == Валюта )
//        pmsum = СуммыТОВНеттинге[idx];
        pmdate = ДатыТОВНеттинге[idx];

        if( rq.rec.Kind == DLRQ_KIND_REQUEST )
           if( NB.НомерТребТО == -1)
              if((NB.СчетТребТО == "") or ((NB.СчетТребТО == СчетаТОВНеттинге[idx] )))
                 if( (NB.НомерТреб == -1) and (NB.СчетТреб == ""))
                    if( Параметры.ПользовательскиеПараметры.ОтборПлатежейПоУбыванию )
                       if( ДатаТреб < pmdate )
                          ДатаТреб  = pmdate;
                          Требование     = idx;
                          ТребованиеIsPM = false;
                       end;
                    else
                       if( ((ДатаТреб > pmdate) and (pmdate != DateZero)) or (ДатаТреб == DateZero) )
                          ДатаТреб       = pmdate;
                          Требование     = idx;
                          ТребованиеIsPM = false;
                       end;
                    end;
                 end;
              end;
           else
             Требование     = NB.НомерТребТО;
             ДатаТреб       = ДатыТОВНеттинге[Требование];
             ТребованиеIsPM = false;
           end;
        else
           if( NB.НомерОбязТО == -1)
              if((NB.СчетОбязТО == "") or ((NB.СчетОбязТО == СчетаТОВНеттинге[idx] )) )
                 if( (NB.НомерОбяз == -1) and  (NB.СчетОбяз == ""))
                    if( Параметры.ПользовательскиеПараметры.ОтборПлатежейПоУбыванию )
                       if( ДатаОбяз < pmdate )
                          ДатаОбяз  = pmdate;
                          Обязательство     = idx;
                          ОбязательствоIsPM = false;
                       end;
                    else
                       if( ((ДатаОбяз > pmdate) and (pmdate != DateZero)) or (ДатаОбяз == DateZero) )
                          ДатаОбяз  = pmdate;
                          Обязательство     = idx;
                          ОбязательствоIsPM = false;
                       end;
                    end;
                 end;
              end;
           else
             Обязательство     = NB.НомерОбязТО;
             ДатаОбяз          = ДатыТОВНеттинге[Обязательство];
             ОбязательствоIsPM = false;
           end;
        end;
     end;

     idx = idx + 1;
  end;
/*
  if( (СумТребование == 0) or (СумОбязательство == 0) )
     return false;
  end;
*/
  if( (ДатаТреб == DateZero) or (ДатаОбяз == DateZero) )
     return false;
  end;
//  СумТребование    = СуммыПлатежейВНеттинге[Требование];
//  СумОбязательство = СуммыПлатежейВНеттинге[Обязательство];

  if(ТребованиеIsPM)
     NB.НомерТреб = Требование;
     NB.СчетТреб  = СчетаПлатежейВНеттинге[Требование];
     СумТребование    = СуммыПлатежейВНеттинге[Требование];
  else
     NB.НомерТребТО = Требование;
     NB.СчетТребТО  = СчетаТОВНеттинге[Требование];
     СумТребование    = СуммыТОВНеттинге[Требование];
  end;

  if(ОбязательствоIsPM)
     NB.НомерОбяз = Обязательство;
     NB.СчетОбяз  = СчетаПлатежейВНеттинге[Обязательство];
     СумОбязательство = СуммыПлатежейВНеттинге[Обязательство];
  else
     NB.НомерОбязТО = Обязательство;
     NB.СчетОбязТО  = СчетаТОВНеттинге[Обязательство];
     СумОбязательство = СуммыТОВНеттинге[Обязательство];
  end;

  СуммаЗачета = min(СумТребование, СумОбязательство);

  if( СуммаЗачета == 0 )
     return false;
  end;

  if( ТребованиеIsPM )
     ВалютаЗачета = ВалютыПлатежейВНеттинге[Требование];
     СуммыПлатежейВНеттинге[Требование] = СумТребование - СуммаЗачета;
  else
     ВалютаЗачета = ВалютыТОВНеттинге[Требование];
     СуммыТОВНеттинге[Требование] = СумТребование - СуммаЗачета;
  end;

  if( ОбязательствоIsPM )
     СуммыПлатежейВНеттинге[Обязательство] = СумОбязательство - СуммаЗачета;
  else
     СуммыТОВНеттинге[Обязательство] = СумОбязательство - СуммаЗачета;
  end;

  if( СумТребование > СумОбязательство )
     if( ОбязательствоIsPM )
        ПлатежиВНеттинге[Обязательство].PaymStatus = PM_CLOSED_W_M_MOVEMENT;
        ДатыПлатежейВНеттинге[Обязательство] = DateZero;   // обнуляем дату, чтобы учтенный платеж участвовал в дальнейших обработках
        NB.НомерОбяз = - 1;                                // чтобы можно было в следующем заходе отбирать обязательства
     else
        ТОВНеттинге[Обязательство].rec.State = DLRQ_STATE_EXEC;
        ДатыТОВНеттинге[Обязательство] = DateZero;
        NB.НомерОбязТО = - 1;
     end;
  elif( СумТребование < СумОбязательство )
     if( ТребованиеIsPM )
        ПлатежиВНеттинге[Требование].PaymStatus = PM_CLOSED_W_M_MOVEMENT;
        ДатыПлатежейВНеттинге[Требование] = DateZero;
        NB.НомерТреб = - 1;
     else
        ТОВНеттинге[Требование].rec.State = DLRQ_STATE_EXEC;
        ДатыТОВНеттинге[Требование] = DateZero;
        NB.НомерТребТО = - 1;
     end;
  elif( СумТребование == СумОбязательство )
     if( ОбязательствоIsPM )
        ПлатежиВНеттинге[Обязательство].PaymStatus = PM_CLOSED_W_M_MOVEMENT;
        ДатыПлатежейВНеттинге[Обязательство] = DateZero;   
        NB.НомерОбяз = - 1;                                
     else
        ТОВНеттинге[Обязательство].rec.State = DLRQ_STATE_EXEC;
        ДатыТОВНеттинге[Обязательство] = DateZero;
        NB.НомерОбязТО = - 1;
     end;
     if( ТребованиеIsPM )
        ПлатежиВНеттинге[Требование].PaymStatus = PM_CLOSED_W_M_MOVEMENT;
        ДатыПлатежейВНеттинге[Требование] = DateZero;
        NB.НомерТреб = - 1;
     else
        ТОВНеттинге[Требование].rec.State = DLRQ_STATE_EXEC;
        ДатыТОВНеттинге[Требование] = DateZero;
        NB.НомерТребТО = - 1;
     end;
  end;
//////////////
  idx = 0;
  cnt = СуммыПлатежейВНеттинге.size;
  var  СчТреб = false, СчОбяз = false, СчТребТО = false, СчОбязТО = false;
  while( idx < cnt )
     pmobj = ПлатежиВНеттинге[idx];

     if( pmobj.BaseFIID == Валюта )
        pmdate = ДатыПлатежейВНеттинге[idx];

        if( (PaymIsDemand(pmobj, ntg) == "X") )
           if((not NB.СчетТреб == "") and ( ДатыПлатежейВНеттинге[idx] != DateZero) and ( СчетаПлатежейВНеттинге[idx] == NB.СчетТреб))
              СчТреб = true;
           end;
        else
           if((not NB.СчетОбяз == "") and ( ДатыПлатежейВНеттинге[idx] != DateZero) and ( СчетаПлатежейВНеттинге[idx] == NB.СчетОбяз))
              СчОбяз = true;
           end;

        end;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = СуммыТОВНеттинге.size;

  while( idx < cnt )
     rq = ТОВНеттинге[idx];

     if( rq.rec.FIID == Валюта )
        pmdate = ДатыТОВНеттинге[idx];

        if( rq.rec.Kind == DLRQ_KIND_REQUEST )
           if((not NB.СчетТребТО == "") and ( ДатыТОВНеттинге[idx] != DateZero) and ( СчетаТОВНеттинге[idx] == NB.СчетТребТО))
              СчТребТО = true;
           end;
        else
           if((not NB.СчетОбязТО == "") and ( ДатыТОВНеттинге[idx] != DateZero) and ( СчетаТОВНеттинге[idx] == NB.СчетОбязТО))
              СчОбязТО = true;
           end;
        end;
     end;

     idx = idx + 1;
  end;

  if(not СчТреб)
     NB.СчетТреб = "";
  end;
  if(not СчОбяз)
     NB.СчетОбяз = "";
  end;

  if(not СчТребТО)
     NB.СчетТребТО = "";
  end;
  if(not СчОбязТО)
     NB.СчетОбязТО = "";
  end;


////////////

  if( ОбязательствоIsPM )
     SetParm(1, ПолучитьСчетПоПлатежу(ПлатежиВНеттинге[Обязательство], false));
  else
     SetParm(1, ПолучитьНашСчетПоТО(ntg, ТОВНеттинге[Обязательство]));
  end;
  if( ТребованиеIsPM )
     SetParm(2, ПолучитьСчетПоПлатежу(ПлатежиВНеттинге[Требование], true));
  else
     SetParm(2, ПолучитьНашСчетПоТО(ntg, ТОВНеттинге[Требование]));
  end;
  SetParm(3, СуммаЗачета);
  SetParm(4, ВалютаЗачета);

  return true;
END;

PRIVATE MACRO ВыполнитьЗачетТребИОбязНеЧерезТС( ntg )

  VAR idx = 0, cnt = СписокТребОбяз.size,
      Дт = "", Кт = "", СуммаЗачета = 0, ВалютаЗачета = -1 , NB = NumBuf ;//НомерТреб = -1, НомерОбяз = -1, НомерТребТО = -1, НомерОбязТО = -1;
  //TEG 01.02.19 пока поставим пачку 30 для ВСЕХ
  var NumberPack = 30 ; 
    // KS 29.04.2019 Если дата валютирования попадает на праздник...
  var _ValueDate = ntg.ValueDate;
  if (isHoliday(_ValueDate))
    _ValueDate = GetDateAfterWorkDays(_ValueDate,0);
  end;

  //TEG 02.05.19 ролевая модель
  var NumberOper = GetMainOperInGroup(ntg.DocKind);
   if ( NumberOper<= 0)
        NumberOper = {Oper};
   end;
   /**/

  if(Параметры.ПользовательскиеПараметры.ОтборПлатежейПоДатеОткрытияСчета)
    while( idx < cnt )
       while( ПолучитьОчереднуюСуммуЗачетаПоДате(СписокТребОбяз[idx].ФИ, Дт, Кт, СуммаЗачета, ВалютаЗачета, ntg, NB /* НомерТреб, НомерОбяз, НомерТребТО, НомерОбязТО */) )
          if( (Дт != Кт) and (ntg.ClientId <= 0 ) )
            if( DocCarry( false,
                           1, 
                           ВалютаЗачета,
                           Дт, 
                           Кт, 
                           СуммаЗачета, 
                           _ValueDate, // KS ntg.ValueDate,
                           NULL,
                           ОпределитьОснованиеПроводки(ntg),
                           null,
                           null,
                           null,
                           null,
                           NumberPack,
                           null,
                           null,
                           null,
                           NumberOper 
                         )
               )
                Err.Добавить(NTG_ERR_BADCARRYZTO);
                return false;
             end;
          end;
       end;
    
       idx = idx + 1;
    end;
  else
    while( idx < cnt )
       while( ПолучитьОчереднуюСуммуЗачета(СписокТребОбяз[idx].ФИ, Дт, Кт, СуммаЗачета, ВалютаЗачета, ntg) )
          if( (Дт != Кт) and (ntg.ClientId <= 0 ) )
           /*pnv*/
             if (ВалютаМеталл(СписокТребОбяз[idx].ФИ))
                continue;
             end;
           /*pnv*/
             if( DocCarry( false,
                           1, 
                           ВалютаЗачета,
                           Дт, 
                           Кт, 
                           СуммаЗачета, 
                           _ValueDate, // KS ntg.ValueDate,
                           NULL,
                           ОпределитьОснованиеПроводки(ntg),
                           null,
                           null,
                           null,
                           null,
                           NumberPack,
			   null,
			   null,
			   null,
			   NumberOper
                         )
               )
                Err.Добавить(NTG_ERR_BADCARRYZTO);
                return false;
             end;
          end;
       end;
    
       idx = idx + 1;
    end;
  end;

  return true;
END;

PRIVATE MACRO ПолучитьГлаву( FIKind )
  if( FIKind == FIKIND_AVOIRISS )
     return 5;
  else
     return 1;
  end;
END;

PRIVATE MACRO ПолучитьСчетМаксОстатка( isDemand, ntg, BaseFIID )
  VAR idx = 0, cnt = СуммыПлатежейВНеттинге.size,
      Сумма = 0, id = 0, isPM = true, RetVal = "";

  while( idx < cnt )
     if( (Сумма < СуммыПлатежейВНеттинге[idx]) and (PaymIsDemand(ПлатежиВНеттинге[idx], ntg) == isDemand) and (ПлатежиВНеттинге[idx].BaseFIID == BaseFIID) )
        Сумма = СуммыПлатежейВНеттинге[idx];
        id = idx;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = СуммыТОВНеттинге.size;
  while( idx < cnt )
     if( (Сумма < СуммыТОВНеттинге[idx]) and (ТОВНеттинге[idx].rec.Kind == IIF(isdemand == "X", DLRQ_KIND_REQUEST, DLRQ_KIND_COMMIT)) and ТОВНеттинге[idx].rec.FIID == BaseFIID)
        Сумма = СуммыТОВНеттинге[idx];
        id = idx;
        isPM = false;
     end;

     idx = idx + 1;
  end;

  if( Сумма == 0 )
     return "";
  end;

  if( isPM )
     RetVal = ПолучитьСчетПоПлатежу(ПлатежиВНеттинге[id], isDemand);
  else
     RetVal = ПолучитьНашСчетПоТО(ntg, ТОВНеттинге[id]);
  end;

  return RetVal;
END;

PRIVATE MACRO ЗаполнитьУточняющиеЗаписи( pmobj, ntg, FinalDealNumber )

  VAR idx = 0, cnt = 0,
      pmaddpi = TRecHandler("pmaddpi.dbt"), DealCode = "",
      isDemand = PaymIsDemand(pmobj, ntg), pm = NULL, dlrq = NULL, flag = false;
  var pi = TRecHandler("pmaddpi.dbt");
  VAR IsExist = false, IsNext = 0;

  idx = 0;
  cnt = СуммыТОВНеттинге.size;

  while( idx < cnt )
     dlrq = ТОВНеттинге[idx];

     if( (СуммыТОВНеттинге[idx] > 0)and
         (dlrq.rec.Kind == IIF(isdemand == "X", DLRQ_KIND_REQUEST, DLRQ_KIND_COMMIT)) and
         (dlrq.rec.FIID == pmobj.BaseFIID)
       )
        pmaddpi.Clear();

        if( isDemand == "X" )
           pmaddpi.rec.DebetCredit = PRT_Credit;
        else
           pmaddpi.rec.DebetCredit = PRT_Debet;
        end;

        pmaddpi.rec.Account  = ПолучитьНашСчетПоТО(ntg, dlrq);
        pmaddpi.rec.PMFIID   = /*pmobj.BaseFIID*/ВалютыТОВНеттинге[idx]; /* тут не понятно что должно быть, */
        pmaddpi.rec.FIID     = /*pm.BaseFIID*/ВалютыТОВНеттинге[idx];    /* но если сумма берётся в одной валюте, то логично взять эту же валюту*/
        pmaddpi.rec.Chapter  = ПолучитьГлаву(GetFiKindByID( dlrq.rec.FIID));
        pmaddpi.rec.PMAmount = 
        pmaddpi.rec.Amount   = СуммыТОВНеттинге[idx];
        pmaddpi.rec.Ground   = pmobj.Ground;

        /*по проекту:
          Важное замечание, в списке уточнений не должно присутствовать записей, 
          с дублирующимися номерами счетов. При добавлении записи в список уточнений 
          необходимо найти уже существующую запись и обновить сумму, если запись не найдена, то продолжить вставку.*/
        pi.Clear();
        IsExist = false;
        IsNext = 0;
        if( (pmobj.PIList(pmaddpi.rec.DebetCredit).Size > 0) and (pmobj.PIList(pmaddpi.rec.DebetCredit).First() == 0) )
           while( (not IsExist) and (IsNext == 0) and (pmobj.PIList(pmaddpi.rec.DebetCredit).Current(pi) == 0) )
              IsNext = pmobj.PIList(pmaddpi.rec.DebetCredit).Next;
              if( (pi.rec.PAYMENTID == pmobj.PaymentID) AND
                  (pi.rec.ACCOUNT   == pmaddpi.rec.ACCOUNT) AND
                  (pi.rec.FIID      == pmaddpi.rec.FIID) AND
                  (pi.rec.CHAPTER   == pmaddpi.rec.CHAPTER)
                )
                 IsExist = true;
              end;
           end;
        end;

        if( IsExist )
           pmaddpi.rec.PMAmount = 
           pmaddpi.Rec.Amount   = pmaddpi.Rec.Amount + pi.Rec.Amount;
           /*сначала запись удалим, а затем вставим снова, иначе если апдейтить, 
             то FuturePayerAmount\FutureReceiverAmount не перессчитываются*/
           if( pmobj.PIList(pmaddpi.rec.DebetCredit).Delete(pi) != 0 );
              Err.Добавить(NTG_ERR_UPDPILIST);
              return false;
           end;
           if( pmobj.PIList(pmaddpi.rec.DebetCredit).Insert(pmaddpi) != 0 );
              Err.Добавить(NTG_ERR_INSPILIST);
              return false;
           end;
        else
           if( pmobj.PIList(pmaddpi.rec.DebetCredit).Insert(pmaddpi) != 0 );
              Err.Добавить(NTG_ERR_INSPILIST);
              return false;
           end;
        end;

        DealCode = NGT_ПолучитьНомерСделки(dlrq.rec.DocID, dlrq.rec.DocKind);

        if( FinalDealNumber == "" )
           FinalDealNumber = DealCode;
        else
           FinalDealNumber = FinalDealNumber + ", " + DealCode;
        end;
     end;

     idx = idx + 1;
  end;

  idx = 0;
  cnt = СуммыПлатежейВНеттинге.size;

  while( idx < cnt )
     pm = ПлатежиВНеттинге[idx];

     if( (СуммыПлатежейВНеттинге[idx] > 0) and
         (PaymIsDemand(pm, ntg) == isDemand) and
         (pm.BaseFIID == pmobj.BaseFIID)
       )
        pmaddpi.Clear();

        if( isDemand == "X" )
           pmaddpi.rec.DebetCredit = PRT_Credit;
        else
           pmaddpi.rec.DebetCredit = PRT_Debet;
        end;

        pmaddpi.rec.Account  = ПолучитьСчетПоПлатежу(pm, isDemand);
        pmaddpi.rec.PMFIID   = /*pmobj.BaseFIID*/ВалютыПлатежейВНеттинге[idx]; /* тут не понятно что должно быть, */
        pmaddpi.rec.FIID     = /*pm.BaseFIID*/ВалютыПлатежейВНеттинге[idx];    /* но если сумма берётся в одной валюте, то логично взять эту же валюту*/
        pmaddpi.rec.Chapter  = ПолучитьГлаву(GetFiKindByID( pmobj.BaseFIID));
        pmaddpi.rec.PMAmount = 
        pmaddpi.rec.Amount   = СуммыПлатежейВНеттинге[idx];
        pmaddpi.rec.Ground   = pmobj.Ground;

        /*по проекту:
          Важное замечание, в списке уточнений не должно присутствовать записей, 
          с дублирующимися номерами счетов. При добавлении записи в список уточнений 
          необходимо найти уже существующую запись и обновить сумму, если запись не найдена, то продолжить вставку.*/
        pi.Clear();
        IsExist = false;
        IsNext = 0;
        if( (pmobj.PIList(pmaddpi.rec.DebetCredit).Size > 0) and (pmobj.PIList(pmaddpi.rec.DebetCredit).First() == 0) )
           while( (not IsExist) and (IsNext == 0) and (pmobj.PIList(pmaddpi.rec.DebetCredit).Current(pi) == 0) )
              IsNext = pmobj.PIList(pmaddpi.rec.DebetCredit).Next;
              if( (pi.rec.PAYMENTID == pmobj.PaymentID) AND
                  (pi.rec.ACCOUNT   == pmaddpi.rec.ACCOUNT) AND
                  (pi.rec.FIID      == pmaddpi.rec.FIID) AND
                  (pi.rec.CHAPTER   == pmaddpi.rec.CHAPTER)
                )
                 IsExist = true;
              end;
           end;
        end;

        if( IsExist )
           pmaddpi.rec.PMAmount = 
           pmaddpi.Rec.Amount   = pmaddpi.Rec.Amount + pi.Rec.Amount;
           /*сначала запись удалим, а затем вставим снова, иначе если апдейтить, 
             то FuturePayerAmount\FutureReceiverAmount не перессчитываются*/
           if( pmobj.PIList(pmaddpi.rec.DebetCredit).Delete(pi) != 0 );
              Err.Добавить(NTG_ERR_UPDPILIST);
              return false;
           end;
           if( pmobj.PIList(pmaddpi.rec.DebetCredit).Insert(pmaddpi) != 0 );
              Err.Добавить(NTG_ERR_INSPILIST);
              return false;
           end;
        else
           if( pmobj.PIList(pmaddpi.rec.DebetCredit).Insert(pmaddpi) != 0 );
              Err.Добавить(NTG_ERR_INSPILIST);
              return false;
           end;
        end;

        DealCode = NGT_ПолучитьНомерСделки(pm.DocumentID, pm.DocKind);

        if( FinalDealNumber == "" )
           FinalDealNumber = DealCode;
        else
           FinalDealNumber = FinalDealNumber + ", " + DealCode;
        end;
     end;

     idx = idx + 1;
  end;

  SetParm(2, FinalDealNumber);

  return true;
END;

PRIVATE MACRO ИнициализироватьРезультирПлатеж( ntg, ista, FinalDealNumber )

  VAR idx = 0, pmobj = NULL,
      cnt = ИтоговыеПлатежи.size,
      acc = "";

  FinalDealNumber = "";

  while( idx < cnt )
     pmobj = ИтоговыеПлатежи[idx];

     pmobj.PaymStatus = PM_READIED;

     if( PaymIsDemand(pmobj, ntg) == "" )
        if( ista )
           acc = СписокТребОбяз[СписокТребОбяз.ПолучитьИД(pmobj.BaseFIID)].СчетМРасчеты;
        else
           acc = ПолучитьСчетМаксОстатка("", ntg, pmobj.BaseFIID);
        end;
        pmobj.FUTUREPAYERACCOUNT = ""; /*PNV 498854*/
        pmobj.SetPayerPI( PAYMENTS_GROUP_UNDEF,
                          pmobj.PayerBankID,
                          pmobj.PayerBankCodeKind,
                          pmobj.PayerBankCode,
                          pmobj.PayerBankName,
                          pmobj.PayerBankCorrAcc,
                          pmobj.PayerFIID,
                          ПолучитьГлаву(GetFiKindByID( pmobj.BaseFIID)),
                          acc,
                          pmobj.Payer,
                          pmobj.PayerName,
                          pmobj.PayerINN,
                          pmobj.PayerCodeKind,
                          "" );
     else
        if( ista )
           acc = СписокТребОбяз[СписокТребОбяз.ПолучитьИД(pmobj.BaseFIID)].СчетПРасчеты;
        else
           acc = ПолучитьСчетМаксОстатка("X", ntg, pmobj.BaseFIID);
        end;
        pmobj.FUTURERECEIVERACCOUNT = ""; /*PNV 498854*/
        pmobj.SetReceiverPI( PAYMENTS_GROUP_UNDEF,
                             pmobj.ReceiverBankID,
                             pmobj.ReceiverBankCodeKind,
                             pmobj.ReceiverBankCode,
                             pmobj.ReceiverBankName,
                             pmobj.ReceiverBankCorrAcc,
                             pmobj.ReceiverFIID,
                             ПолучитьГлаву(GetFiKindByID( pmobj.BaseFIID)),
                             acc,
                             pmobj.Receiver,
                             pmobj.ReceiverName,
                             pmobj.ReceiverINN,
                             pmobj.ReceiverCodeKind,
                             "" );
     end;

     if( not pmobj.InitPaySums() )
        Err.Добавить(NTG_ERR_UPDPMSUMS);
        return false; 
     end;

     if( (not ista) and (not ЗаполнитьУточняющиеЗаписи(pmobj, ntg, FinalDealNumber)) )
        return false;
     end;

     idx = idx + 1;
  end;

  SetParm(2, FinalDealNumber);

  return true;
END;

PRIVATE CLASS local_ПараметрыИсполнения
  VAR ОтборПлатежейПоУбыванию = false;
  VAR ОтборПлатежейПоДатеОткрытияСчета = false;
END;

PRIVATE MACRO ОпределитьДопПараметры()
  VAR stat = 0, retVal = false;

  Параметры.ПользовательскиеПараметры = local_ПараметрыИсполнения;

  GetRegistryValue("COMMON\\НЕТТИНГ\\Отбор платежей по убыванию", V_BOOL, retVal, stat);
  if( stat )
     retVal = false;
  end;

  Параметры.ПользовательскиеПараметры.ОтборПлатежейПоУбыванию = retVal;
  GetRegistryValue("COMMON\\НЕТТИНГ\\ОТБОР ПЛАТ. ПО ДАТЕ ОТКР. СЧЕТА", V_BOOL, retVal, stat);
  if (stat)
      retVal = false;
  end;

  Параметры.ПользовательскиеПараметры.ОтборПлатежейПоДатеОткрытияСчета = retVal;

END;

PRIVATE MACRO ОбратотатьТОдляНеттинга( ntg, ID_Operation, ID_Step )
  var idx = 0, cnt = ТОВНеттинге.size;
  var rq = null;

  while( idx < cnt )
     rq = ТОВНеттинге[idx];

     rq.rec.State    = DLRQ_STATE_EXEC;
     rq.rec.FactDate = rq.rec.PlanDate;
     rq.rec.Netting  = SET_CHAR;
     if( DL_ChangeDLRQ(rq, ntg.ValueDate, DLRQ_ACTION_UPDATE) )
        Err.Добавить(NTG_RQERR_UPDRQ);
        return false; 
     end;

     if( GetFiKindByID( rq.rec.FIID) == FIKIND_AVOIRISS )
        if( NGT_ОбновитьГрафикПоТО(rq) != 0 )
           Err.Добавить(NTG_RQERR_GRDEAL);
           return false;
        end;
     end;
    
     idx = idx + 1;
  end;

  return true;
END;

Private macro ВставитьДействие(pmobj, ntg_buff)
    var requestTimeout = 0;
    var ОжиданиеЗапроса = false;
    var repozdeal = TBFile("dl_repozdeal.dbt", "R", 1);
    repozdeal.Clear();
    repozdeal.rec.DocKind = pmobj.DocKind;
    repozdeal.rec.DocID   = pmobj.DocumentID;
//    if( repozdeal.GetEQ() )
    if( GetEQ(repozdeal) )
      if( (repozdeal.rec.INITIATOR == 2) and    (repozdeal.rec.METHOD_WAYTWOWAY == 1))
        ОжиданиеЗапроса = true;
        requestTimeout = repozdeal.rec.requestTimeout;
      end;

      if(repozdeal.rec.reporting == UNSET_CHAR)  //если флаг репортинг в репозитарий не установлен
        return 0;
      end;
    else
      return 0;  // если не нашли репозитарные параметры, то действия не вставляем
    end;

    var SelectClose, DataSetClose, QueryClose;
    var cnt = 0;
    QueryClose = "select * from ddlgrdeal_dbt where t_DocKind = ? and t_DocID = ? and t_TemplNum in(51,52) " ;
    SelectClose = DL_RSDCommand(QueryClose);
    SelectClose.AddParam(pmobj.DocKind);
    SelectClose.AddParam(pmobj.DocumentID);

    cnt = SelectClose.GetCount(QueryClose);     // если не запланировано закрытие договора, то ничего не вставляем
    if( cnt == 0 )
      return 0;
    end;

    var Select, DataSet, Query;
    Query  =  "SELECT * "
           +   "FROM dpmpaym_dbt "
           +   "WHERE     t_DocKind = ? "
              +  " AND t_DocumentID = ? "
              +  " AND t_purpose IN (1, 2) "
              +  " AND t_PaymStatus NOT IN (150, 32000) "
              ;
    Select = DL_RSDCommand(Query);
    Select.AddParam(pmobj.DocKind);
    Select.AddParam(pmobj.DocumentID);
    Select.AddParam(pmobj.DocKind);
    Select.AddParam(pmobj.DocumentID);

    cnt = Select.GetCount(query);
    if( not (cnt == 1) ) // Сообщение о неттинге отправляется только в случае прекращения ВСЕХ обязательств по сделке
      return 0;
    else

      DataSet = Select.Execute();
      if(DataSet.moveNext())
        var ClosePAYM = TRecHandler( "pmpaym" );
        DataSet.GetRecord().CopyTo( ClosePAYM.rec );
        if( ClosePAYM.rec.PaymentID == pmobj.PaymentID )
          var SelectGR, DataSetGR, QueryGR;
          QueryGR =  " SELECT grdeal.t_templnum AS templnum "                   
                   + " FROM ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc "        
                   + " WHERE     grdeal.t_dockind = ? "                      
                       + "  AND grdeal.t_docid = ? "                        
                       + "  AND grdeal.t_templnum IN (51, 52, 55, 56, 57, 58) "
                       + "  AND gracc.t_grdealid = grdeal.t_id "              
                       + "  AND gracc.t_accnum = 5 "                          
                       + "  AND gracc.t_state = 1 "                           
                       ;
        
          SelectGR = DL_RSDCommand(QueryGR);
          SelectGR.AddParam(pmobj.DocKind);
          SelectGR.AddParam(pmobj.DocumentID);
          
          cnt = SelectGR.GetCount(queryGR);
          DataSetGR = SelectGR.Execute();
          while(DataSetGR.moveNext())        
            // ALLFININSTR так как изначально было -1 и не важна валюта
            if( DL_UpdateGrDealAcc( pmobj.DocKind, pmobj.DocumentID, ALLFININSTR, DataSetGR.Templnum, DLGR_ACCKIND_REPOSITORY, date(0,0,0), DLGRACC_STATE_NOTNEED ) != 0)  
              return 1;
            end;
          end;
          if( ОжиданиеЗапроса and requestTimeout)
            if(DL_InsertGrDeal( pmobj.DocKind, pmobj.DocumentID, DLGR_TEMPL_NETTINGSUSPWTHREQUEST, GetDateAfterWorkDays(ntg_buff.ValueDate, requestTimeout) , time(), ALLFININSTR) != 0 )
              return 1;
            end;
          else                                                                                  
            if(DL_InsertGrDeal( pmobj.DocKind, pmobj.DocumentID, DLGR_TEMPL_NETTINGSUSP, ntg_buff.ValueDate, time(), ALLFININSTR) != 0 )
              return false;
            end;
          end;
        end;
      end;

    end;

end;


Private macro ВставитьДействияПоГрафику(ntg_buff)
  VAR idx = 0, pmobj = NULL,
      cnt = ПлатежиВНеттинге.size;
  var err = 0;
  while( idx < cnt )
     pmobj = ПлатежиВНеттинге[idx];
     if((pmobj.purpose == BAi) or (pmobj.purpose == CAi)) //платежи по базовому активу/контрактиву
       if(ВставитьДействие(pmobj,ntg_buff))
         err = err + 1;
       end;
     end;
     idx = idx + 1;
  end;
end;

PRIVATE MACRO ЗакрытьСделкиПлатежи(PaymID)
   var sql, cmd;
      sql = "INSERT INTO dfuncobj_dbt (t_objecttype, t_objectid, t_funcid) "
          +" SELECT paym.t_dockind, paym.t_documentID, 500 "                // 500 funcID в dfunc_dbt
          +"    FROM dpmpaym_dbt paym "
          +"   WHERE t_paymentID IN "
          +"           (SELECT lnk.t_initialPayment "
          +"              FROM dpmlink_dbt lnk, dpmpaym_dbt pm "
          +"             WHERE     lnk.t_purposePayment = pm.t_paymentID "
          +"                   AND pm.t_dockind = 140 "
          +"                   AND pm.t_documentID = ?) "
          ;


      cmd = RSDCommand(sql);
      cmd.addParam("p_PaymID", RSDBP_IN, PaymID);
      cmd.execute();
END;

PRIVATE MACRO ИсполнитьНеттинг( ntg_buff, ID_Operation, ID_Step )
  VAR FinalDealNumber = "";
  
  Err = STEP_10_ERROR;

  Параметры = ПараметрыИсполнения(false, ntg_buff);
  ОпределитьДопПараметры();

  СписокТребОбяз = КлассСпискаТрОб;

  СписокТребОбяз.size          = 0;
  ПлатежиВНеттинге.size        = 0;
  ТОВНеттинге.size             = 0;
  ДатыПлатежейВНеттинге.size   = 0;
  ДатыТОВНеттинге.size         = 0;
  СуммыПлатежейВНеттинге.size  = 0;
  СуммыТОВНеттинге.size        = 0;
  ВалютыПлатежейВНеттинге.size = 0;
  ВалютыТОВНеттинге.size       = 0;
  ИтоговыеПлатежи.size         = 0;
  СчетаПлатежейВНеттинге.size  = 0;
  СчетаТОВНеттинге.size        = 0;

  if( ОпределитьНаборВалют(ntg_buff) and
      ПроверитьИтоговыеПлатежи(ntg_buff)
    )
     if( Параметры.НеттингЧерезТранзитныеСчета )
        if( ПереносНаТС(ntg_buff) and
            ПереносНаТС_ТО(ntg_buff) and
            //ВыполнитьЗачетТребИОбязТС(ntg_buff) and/*pnv*/
            ИнициализироватьРезультирПлатеж(ntg_buff, true, FinalDealNumber) and 
            ОбратотатьТОдляНеттинга(ntg_buff, ID_Operation, ID_Step)
          )
           /*Без ошибок*/
        else /*PNV 507791*/
         if (Err != null)/*pnv*/
              if( Err.Вывести() )
                 return 1;
            end;
        end;/*pnv*/
          return 1;  
        end;
     elif( (ntg_buff.FI_Kind == FIKIND_CURRENCY) or (ntg_buff.FI_Kind == FIKIND_METAL) or (ntg_buff.FI_Kind == FIKIND_ALLFI))
        if( ПроверитьНеснеттингованныеПлатежи(ntg_buff) and
            ВыполнитьЗачетТребИОбязНеЧерезТС(ntg_buff) and
            ИнициализироватьРезультирПлатеж(ntg_buff, false, FinalDealNumber) and
            ОбратотатьТОдляНеттинга(ntg_buff, ID_Operation, ID_Step)
          )
            /*Без ошибок*/
        else /*PNV 507791*/
         if (Err != null)/*pnv*/
              if( Err.Вывести() )
                 return 1;
            end;
        end;/*pnv*/
           return 1;
        end;
     end;
     if( РАБОТА_С_РЕПОЗИТАРИЕМ())
       if(ВставитьДействияПоГрафику(ntg_buff) )
         Err.Добавить(NTG_RQERR_GRDEAL);
         return 1;
       end;
     end;

     if( (FinalDealNumber != "") and (readNoteForObject(OBJTYPE_NETTING, UniID(ntg_buff, OBJTYPE_NETTING), 2, ntg_buff.SigningDate) == "") )
        addNoteForObject(OBJTYPE_NETTING, UniID(ntg_buff, OBJTYPE_NETTING), 2, FinalDealNumber, ntg_buff.SigningDate);
     end;
  end;

  if (Err != null)/*pnv*/
      if( Err.Вывести() )
          return 1;
      end;
  end;/*pnv*/
  return 0;
END; 

MACRO ExecuteStep( Buffer, Document, DocKind, ID_Operation, ID_Step )
  RECORD ntg_buff("dl_nett");
  var stat = 0;

  SetBuff(ntg_buff, Document);

    DL_PrepareCarryBuffer(Buffer);

    stat = ИсполнитьНеттинг(ntg_buff, ID_Operation, ID_Step);

    return stat;
END;

/* Макрос постобработки */
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -откат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */  

  record dl_nett(dl_nett);
  SetBuff(dl_nett, FDoc ); 

  if (errTrn OR (CommitOrRollback == 2)) 
      /* Произошла ошибка или происходит откат */
      return;
  end;
  var stat = 0;
  var ЗакрытиеСделокПОИтогамНеттинга= false;
  GetRegistryValue("COMMON\\НЕТТИНГ\\ЗАКРЫТЬ СДЕЛКИ ПОСЛЕ НЕТТИНГА", V_BOOL, ЗакрытиеСделокПОИтогамНеттинга, stat);
  if (stat)
      ЗакрытиеСделокПОИтогамНеттинга = false;
  end;

  if(ЗакрытиеСделокПОИтогамНеттинга)
    ЗакрытьСделкиПлатежи(dl_nett.nettingid)
  end;

  return 0;
END;