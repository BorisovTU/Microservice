/*
$Name:        dlntlib.mac
$Module:      Неттинг
$Description: Общеиспользуемые ф-ии для неттинга
*/
IMPORT OprInter, PTInter, BankInter, PaymInter, FIInter, RsbDataSet, globals, cb_sql, "dlntpm.mac", "dlquery.mac", DealsInter;

CONST NTG_ERR_BADCONVERT  = 1,
      NTG_ERR_BADBASERATE = 2,
      NTG_ERR_UPDDLFLAG   = 3,
      NTG_ERR_NOFNLPM     = 4,
      NTG_ERR_CNTFNLPM    = 5,
      NTG_ERR_BADFNLPM    = 6,
      NTG_ERR_BADCARRYZTO = 7,
      NTG_ERR_BADCARRYPTC = 8,
      NTG_ERR_BADACCTC    = 9,
      NTG_ERR_PAYMSTAT    = 10,
      NTG_ERR_PAYMCONTR   = 11,
      NTG_ERR_PAYMDATE    = 12,
      NTG_ERR_PAYMBASEFI  = 13,
      NTG_ERR_PAYMPAYFI   = 14,
      NTG_ERR_PAYMPAYFIMN = 15,
      NTG_ERR_ONEWAYPAYMS = 16,
      NTG_ERR_INSPILIST   = 17,
      NTG_ERR_UPDPILIST   = 18,
      NTG_ERR_NOINCPM     = 19,
      NTG_ERR_UPDPMSUMS   = 20,

      NTG_RQERR_BADCONVERT  = 100,
      NTG_RQERR_BADBASERATE = 101,
      NTG_RQERR_UPDDLFLAG   = 102,
      NTG_RQERR_NOFNLRQ     = 103,
      NTG_RQERR_CNTFNLRQ    = 104,
      NTG_RQERR_BADFNLRQ    = 105,
      NTG_RQERR_BADCARRYZTO = 106,
      NTG_RQERR_BADCARRYPTC = 107,
      NTG_RQERR_BADACCTC    = 108,
      NTG_RQERR_STATE       = 109,
      NTG_RQERR_CONTR       = 110,
      NTG_RQERR_DATE        = 111,
      NTG_RQERR_BASEFI      = 112,
      NTG_RQERR_ONEWAYRQ    = 115,
      NTG_RQERR_INSPILIST   = 116,
      NTG_RQERR_UPDPILIST   = 117,
      NTG_RQERR_NOINCRQ     = 118,
      NTG_RQERR_UPDRQ       = 119,
      NTG_RQERR_GRDEAL      = 120;

CLASS NTG_ERROR
    PRIVATE VAR
        Ошибки           = TArray(),
        ПараметрыОшибок  = TArray();

    PRIVATE MACRO ПолучитьСтрокуСОшибкой(errnum, parm)
        return "";
    END;

    /* В вызовах данного метода, через запятую можно добавлять дополнительные параметры.
       ParmCount() считает их количество. Последующая их передача осуществляется через ПараметрыОшибок[idx]
    */
    MACRO Добавить(Номер)
        VAR 
            idx = 1, id = 0,
            cnt = ParmCount();

        id = Ошибки.size;

        Ошибки[id] = Номер;
        ПараметрыОшибок[id] = TArray();

        while (idx < cnt)
            GetParm(idx, ПараметрыОшибок[id][idx]);
            idx = idx + 1;
        end;
    END;

    MACRO Вывести()
        VAR 
            idx = 0, cnt = Ошибки.size;

        while (idx < cnt)
            msgbox(ПолучитьСтрокуСОшибкой(Ошибки[idx], ПараметрыОшибок[idx]));

            idx = idx + 1;
        end;

        Ошибки.size = 0;
        ПараметрыОшибок.size = 0;

        return (idx != 0);
    END;
END;

CLASS (NTG_ERROR)STEP_10_ERROR
    PRIVATE MACRO ПолучитьСтрокуСОшибкой(errnum, parm)
        VAR
            idx = 2,
            cnt = parm.Size,
            str = "";

        if (errnum == NTG_ERR_BADCONVERT)
            str = "Ошибка при конвертации сумм платежа";
        elif (errnum == NTG_ERR_BADBASERATE)
            str = "Не установлен базовый курс";
        elif (errnum == NTG_ERR_UPDDLFLAG)
            str = "Ошибка при обновлении флагов части сделки";
        elif (errnum == NTG_ERR_UPDPMSUMS)
            str = "Ошибка при обновлении сумм в платеже";
        elif (errnum == NTG_ERR_NOFNLPM)
            str = "Ошибка при получении итогового платежа";
        elif (errnum == NTG_ERR_CNTFNLPM)
            str = "Не должно быть итогового платежа с валютой " + parm[idx];
            idx = idx + 1;
        elif (errnum == NTG_ERR_BADFNLPM)
            str = "Неверный итоговый платеж в валюте ";
        elif (errnum == NTG_ERR_BADCARRYZTO)
            str = "Ошибка при зачете требований и обязательств";
        elif (errnum == NTG_ERR_BADCARRYPTC)
            str = "Ошибка при переносе средств на транзитные счета";
        elif (errnum == NTG_ERR_BADACCTC)
            str = "Ошибка при получении транзитных счетов";
        elif (errnum == NTG_ERR_PAYMSTAT)
            str = "Статус платежа не равен \"Готов к обработке\" или \"Досрочная обработка\"| или \"Снят с просрочки\" или \"Снят с пролонгации\"";
        elif (errnum == NTG_ERR_PAYMCONTR)
            str = "Неверные участники взаимозачета в платеже";
        elif (errnum == NTG_ERR_PAYMDATE)
            str = "Дата валютирования платежа не совпадает с датой неттинга";
        elif (errnum == NTG_ERR_PAYMBASEFI)
            str = "Базовая валюта платежа не совпадает с базовой валютой неттинга";
        elif (errnum == NTG_ERR_PAYMPAYFI)
            str = "Валюта оплаты платежа не совпадает с валютой оплаты неттинга";
        elif (errnum == NTG_ERR_PAYMPAYFIMN)
            str = "В мультивалютном неттинге валюта оплаты должна совпадать с валютой платежа";
        elif (errnum == NTG_ERR_ONEWAYPAYMS)
            //str = "Итоговый платеж включает однонаправленные платежи.";
            str = "По финансовому инструменту " + parm[idx] + " существуют однонаправленные платежи. Выполните корректировку списка платежей для неттинга.";
            idx = idx + 1;
        elif (errnum == NTG_ERR_INSPILIST)
            str = "Ошибка при вставке уточняющей записи. Запись сформирована не будет";
        elif (errnum == NTG_ERR_UPDPILIST)
            str = "Ошибка при апдейте уточняющей записи. Запись скорректирована не будет";
        elif (errnum == NTG_ERR_NOINCPM)
            str = "В режиме без использования транзитных счетов| в общем списке платежей для неттинга не должно быть|"
                  " платежей по авансу и компенсационных";

        elif (errnum == NTG_RQERR_BADCONVERT)
            str = "Ошибка при конвертации сумм ТО";
        elif (errnum == NTG_RQERR_BADBASERATE)
            str = "Не установлен базовый курс";
        elif (errnum == NTG_RQERR_UPDDLFLAG)
            str = "Ошибка при обновлении флагов части сделки";
        elif (errnum == NTG_RQERR_UPDRQ)
            str = "Ошибка при обновлении ТО";
        elif (errnum == NTG_RQERR_NOFNLRQ)
            str = "Ошибка при получении итогового ТО";
        elif (errnum == NTG_RQERR_CNTFNLRQ)
            str = "Не должно быть итогового ТО с валютой " + parm[idx];
            idx = idx + 1;
        elif (errnum == NTG_RQERR_BADFNLRQ)
            str = "Неверное итоговое ТО в валюте ";
        elif (errnum == NTG_RQERR_BADCARRYZTO)
            str = "Ошибка при зачете требований и обязательств";
        elif (errnum == NTG_RQERR_BADCARRYPTC)
            str = "Ошибка при переносе средств на транзитные счета";
        elif (errnum == NTG_RQERR_BADACCTC)
            str = "Ошибка при получении транзитных счетов";
        elif (errnum == NTG_RQERR_STATE)
            str = "Статус ТО не равен \"Запланирован\" или \"Отложен\"";
        elif (errnum == NTG_RQERR_CONTR)
            str = "Неверные участники взаимозачета в ТО";
        elif (errnum == NTG_RQERR_DATE)
            str = "Дата валютирования ТО не совпадает с датой неттинга";
        elif (errnum == NTG_RQERR_BASEFI)
            str = "Базовая валюта ТО не совпадает с базовой валютой неттинга";
        elif (errnum == NTG_RQERR_ONEWAYRQ)
            //str = "Итоговый платеж включает однонаправленные ТО.";
            str = "По финансовому инструменту " + parm[idx] + " существуют однонаправленные ТО. Выполните корректировку списка ТО для неттинга.";
            idx = idx + 1;
        elif (errnum == NTG_RQERR_INSPILIST)
            str = "Ошибка при вставке уточняющей записи. Запись сформирована не будет";
        elif (errnum == NTG_RQERR_UPDPILIST)
            str = "Ошибка при апдейте уточняющей записи. Запись скорректирована не будет";
        elif (errnum == NTG_RQERR_NOINCRQ)
            str = "В режиме без использования транзитных счетов| в общем списке ТО для неттинга не должно быть|"
                  " ТО по авансу и компенсационных";
        elif (errnum == NTG_RQERR_GRDEAL)
            str = "Ошибка при обновлении строки графика по сделке";
        else
            str = "Неизвестная ошибка";
        end;

        while(idx < cnt)
            if((ValType(parm[idx]) != V_UNDEF) and (parm[idx] != null))
                str = str + "|" + parm[idx];
            end;
            idx = idx + 1;
        end;

        return str;
    END;

    InitNTG_ERROR();
END;

CLASS ПараметрыИсполнения (ВыполняемОбязательства, ntg)
    PRIVATE VAR ParmInitFile = "", BOforInit = TArray;

    VAR
        ПроводимКвитовку            = false,
        НеттингЧерезТранзитныеСчета = false,
        СквознойНеттинг             = false,
        ПользовательскиеПараметры   = NULL,
        БэкОфис                     = "";

    PRIVATE MACRO ИнициализироватьБО( ВыполняемОбязательства, ntg )
       if( ntg.IdentProgram == 13/*IP_IBC*/ )              /*Выполнение из БО "МБК"*/
          ParmInitFile = "dlntmm.mac";
          БэкОфис = "J";
       elif( ntg.IdentProgram == 12/*IP_FRX*/ )             /*Выполнение из БО Forex*/
          ParmInitFile = "dlntfx.mac";
          БэкОфис = "V";
       elif( ntg.IdentProgram == 14/*IP_SEC*/ )             /*Выполнение из БО Ц/Б*/      
          ParmInitFile = "dlntsp.mac";
          БэкОфис = "S";
       elif( ntg.IdentProgram == 41/*IP_VEKSELACCOUNTED*/ ) /*Выполнение из БО УВ*/
          ParmInitFile = "dlntva.mac";
          БэкОфис = "N";
       elif( ntg.IdentProgram == 42/*IP_DV*/ )              /*Выполнение из БО ФИССиКО*/
          ParmInitFile = "dlntdv.mac";
          БэкОфис = "Ю";
       end;

       if( not ExecMacroFile (ParmInitFile, "ИницПарамИсполн", this, ВыполняемОбязательства, ntg) )
          RunError("Ошибка инициализации параметров");
       end;
    END;

    PRIVATE MACRO ПроверитьСквознойНеттинг( ntg )
        VAR stat = 0, DataSet,
            cntIBC = 0, cntFX = 0, cntDV:integer = 0;

        DataSet = TRsbDataSet("select count(*) as cnt from dpmpaym_dbt paym where (paym.t_paymentid in " +
                              "( " +
                              "     select distinct pml.t_initialpayment from dpmlink_dbt pml where " +
                              "     (pml.t_dockind    = " + DL_NTGDOC + ")and " +
                              "     (pml.t_documentid = " + ntg.NettingID + ")and " +
                              "     (t_linkkind = 1) " +
                              "))and(paym.t_dockind   = " + DL_FOREXDOC + ")");
        DataSet.MoveNext();
        cntFX = DataSet.Cnt;

        DataSet = TRsbDataSet("select count(*) as cnt from dpmpaym_dbt paym where (paym.t_paymentid in " +
                              "( " +
                              "     select distinct pml.t_initialpayment from dpmlink_dbt pml where " +
                              "     (pml.t_dockind    = " + DL_NTGDOC + ")and " +
                              "     (pml.t_documentid = " + ntg.NettingID + ")and " +
                              "     (t_linkkind = 1) " +
                              "))and(paym.t_dockind   = " + DL_IBCDOC + ")");
        DataSet.MoveNext();
        cntIBC = DataSet.Cnt;

        DataSet = TRsbDataSet("select count(*) as cnt from dpmpaym_dbt paym where (paym.t_paymentid in " +
                              "( " +
                              "     select distinct pml.t_initialpayment from dpmlink_dbt pml where " +
                              "     (pml.t_dockind    = " + DL_NTGDOC + ")and " +
                              "     (pml.t_documentid = " + ntg.NettingID + ")and " +
                              "     (t_linkkind = 1) " +
                              "))and(paym.t_dockind   = " + DL_DVNDEAL + ")");
        DataSet.MoveNext();
        cntDV = DataSet.Cnt;

        СквознойНеттинг = false;
        if( ((cntIBC > 0) and (cntFX > 0)) or
            ((cntIBC > 0) and (cntDV > 0)) or
            ((cntDV  > 0) and (cntFX > 0)) )
            СквознойНеттинг = true;
        end;
    END;

    /*Конструктор*/
    ПроверитьСквознойНеттинг(ntg);
    ИнициализироватьБО(ВыполняемОбязательства, ntg);
END;
/****************************************************************************************/
MACRO ОпределитьОснованиеПроводки (ntg)
    var Основание = "Неттинг с ",
        pt = TRecHandler ("party");

    if (0==ПолучитьСубъекта(ntg.Contractor, pt))
        Основание = Основание + pt.rec.ShortName;
    else
        Основание = Основание + "неизвестным контрагентом";
    end;

    return String(Основание, " за ", StrSubSt(String(ntg.ValueDate:f), ".", "/"));
END;
/****************************************************************************************/
MACRO PaymIsDemand( paym, ntg )
   if( (paym.Payer == {OurBank}) or (paym.Payer == ntg.ClientId) )
      return "";
   else
      return "X";
   end;
END;
/****************************************************************************************/
MACRO ПолучитьСчетПоПлатежу(paym, Требование)
   if (Требование)
      return paym.ReceiverAccount;
   else
      return paym.PayerAccount;
   end;
END;
/****************************************************************************************/
PRIVATE MACRO ForEachPMLinkCommon( DocKind, DocumentID, action, data:@variant, linkkind )
   var Select, DataSet,
       result = true,
       pml    = TRecHandler("pmlink");

   Select = DL_RSDCommand( "select * from dpmlink_dbt where "
                           "t_dockind     = ? and "
                           "t_documentid  = ? and "
                           "t_linkkind    = ? "
                         );
   Select.addParam(DocKind);
   Select.addParam(DocumentID);
   Select.addParam(linkkind);

   DataSet = Select.Execute();
   while( (result) and (DataSet.MoveNext()) )
      DataSet.GetRecord().CopyTo(pml.rec);
      result = ExecMacro2(@action, pml.rec, @data);
   end;

   return result;
END;

/****************************************************************************************/
MACRO ForEachPMLink( DocKind, DocumentID, action, data:@variant )
/*
   var Select, DataSet,
       result = true,
       pml    = TRecHandler("pmlink");

   Select = DL_RSDCommand( "select * from dpmlink_dbt where "
                           "t_dockind     = ? and "
                           "t_documentid  = ? and "
                           "t_linkkind    = ? "
                         );
   Select.addParam(DocKind);
   Select.addParam(DocumentID);
   Select.addParam(PMLINK_KIND_NETTING);

   DataSet = Select.Execute();
   while( (result) and (DataSet.MoveNext()) )
      DataSet.GetRecord().CopyTo(pml.rec);
      result = ExecMacro2(@action, pml.rec, @data);
   end;
*/
   return ForEachPMLinkCommon(DocKind, DocumentID, action, @data, PMLINK_KIND_NETTING);
END;

/****************************************************************************************/
MACRO ForEachPMLinkRQ(DocKind, DocumentID, action, data:@variant)
/*
   var Select, DataSet,
       result = true,
       pml    = TRecHandler("pmlink");

   Select = DL_RSDCommand( "select * from dpmlink_dbt where "
                           "t_dockind     = ? and "
                           "t_documentid  = ? and "
                           "t_linkkind    = ? "
                         );
   Select.addParam(DocKind);
   Select.addParam(DocumentID);
   Select.addParam(PMLINK_KIND_RQNETTING);

   DataSet = Select.Execute();
   while( (result) and (DataSet.MoveNext()) )
      DataSet.GetRecord().CopyTo(pml.rec);
      result = ExecMacro2(@action, pml.rec, @data);
   end;
*/
   return ForEachPMLinkCommon(DocKind, DocumentID, action, @data, PMLINK_KIND_RQNETTING);
END;

MACRO IsExistsNotFinishedResultPM( ntg, isDemand )
    VAR result = false,
        cmd  = DL_RSDCommand(),
        Sql = "select count(1) Nrec " +
                             "  from dpmpaym_dbt where " + 
                             " t_DocKind     = ? and " +
                             " t_DocumentID  = ? and " +
                             " t_Purpose    = ? and " +
                             " t_PaymStatus != ? and t_PaymStatus != ? ";

    cmd.AddParam(DL_NTGDOC);
    cmd.AddParam(ntg.NettingID);
    cmd.AddParam(PM_PURP_NETTING);
    cmd.AddParam(PM_FINISHED);
    cmd.AddParam(PM_CLOSED_W_M_MOVEMENT);

    if( isDemand )
      Sql = Sql + " and t_Payer != ? and t_Payer != ? ";
    else
      Sql = Sql + " and (t_Payer = ? or t_Payer = ?)";
    end;

    cmd.AddParam({OurBank});
    cmd.AddParam(ntg.ClientId);

    var DataSet = cmd.Execute(Sql);

    if(DataSet.MoveNext() and (DataSet.Nrec != null) and (DataSet.Nrec > 0))
       result = true;
    end;

    return result;
END;

/****************************************************************************************/
MACRO ForEachPMResPaym( IdentProgram, DocumentID, action, data:@variant )
    VAR rset   = NULL,
        result = true,
        pmobj  = NULL,
        query  = "";
        
    query = "select t_SubPurpose from dpmpaym_dbt where " + 
                        " t_DocKind     = " + DL_NTGDOC  + " and " +
                        " t_DocumentID  = " + DocumentID + " and " +
                        " t_Purpose    = "  + PM_PURP_NETTING +
                        " AND t_Amount > 0" +
                        " order by t_SubPurpose";
    rset = TRsbDataSet(query);

    while ((result)and(rset.MoveNext()))
        pmobj = CNtgPayment(IdentProgram, DL_NTGDOC, DocumentID, PM_PURP_NETTING, rset.SubPurpose);
        result = ExecMacro2(@action, pmobj, @data);
    end;

    return result;
END;

/****************************************************************************************/
MACRO ForEachRQRes(DocumentID, action, data:@variant)
    VAR  result    = true,
         rq        = TRecHandler("dlrq");
    var Select, DataSet;

    Rq.Clear();

    Select = DL_RSDCommand("select * from ddlrq_dbt where t_DocKind = ? and t_DocID = ?");
    Select.addParam(DL_NTGSEC);
    Select.addParam(DocumentID);

    DataSet = Select.Execute();
    while((result) and (DataSet.moveNext()))
      DataSet.GetRecord().CopyTo( rq.rec );
      result = ExecMacro2(@action, rq, @data);
    end;

    return result;
END;
/****************************************************************************************/
PRIVATE MACRO ПроверитьСущПлатежа(pmobj, pay:@variant)
    pay = pmobj;
    return false;
END;

MACRO ЕстьРезПлатежПоЦеннымБумагам( IdentProgram, NettingID, pmobj )
    VAR isFind = false;

    pmobj = NULL;
    ForEachPMResPaym(IdentProgram, NettingID, @ПроверитьСущПлатежа, @pmobj);

    if (pmobj.PaymentID > 0)
        isFind = true;
    end;

    SetParm(1, pmobj);

    return isFind;
END;

PRIVATE MACRO ПроверитьСущТО(rq, pay:@variant)
    pay = rq;
    return false;
END;

MACRO ЕстьРезТОПоЦеннымБумагам(NettingID, rq)
    VAR isFind = false;

    rq = NULL;
    ForEachRQRes(NettingID, @ПроверитьСущТО, @rq);

    if (rq.rec.ID > 0)
        isFind = true;
    end;

    SetParm(1, rq);

    return isFind;
END;

PRIVATE VAR _ИнтегрРежимРаботы = NULL;
MACRO NGT_ИнтегрРежимРаботы():bool
  var ErrCode, Path = "COMMON\\WORK_MODE\\INTEGRATED";

  if( _ИнтегрРежимРаботы == NULL )
     GetRegistryValue(Path, V_BOOL, _ИнтегрРежимРаботы, ErrCode);
     if( ErrCode != 0 )
        MsgBox("Ошибка при получении значения настройки \"" + Path + "\"");
        _ИнтегрРежимРаботы = false;
     end;
  end;

  return _ИнтегрРежимРаботы;
END;

PRIVATE VAR _ДелатьПроводкуПоИсхПлатежам = NULL;
MACRO NGT_ДелатьПроводкуПоИсхПлатежам():bool
  var ErrCode, Path = "COMMON\\WORK_MODE\\ПРОВОДКА ПО ИСХОДЯЩИМ ПЛАТЕЖАМ";

  if( _ДелатьПроводкуПоИсхПлатежам == NULL )
     GetRegistryValue(Path, V_BOOL, _ДелатьПроводкуПоИсхПлатежам, ErrCode);
     if( ErrCode != 0 )
        MsgBox("Ошибка при получении значения настройки \"" + Path + "\"");
        _ДелатьПроводкуПоИсхПлатежам = false;
     end;
  end;

  return _ДелатьПроводкуПоИсхПлатежам;
END;

MACRO NGT_GetRQ( RqID, Rq:TRecHandler )
  var Select, DataSet;

  Rq.Clear();

  Select = DL_RSDCommand("select * from ddlrq_dbt where t_ID = ?");
  Select.addParam(RqID);

  DataSet = Select.Execute();
  if( DataSet.moveNext() )
     DataSet.GetRecord().CopyTo(Rq.rec);
  end;
END;

MACRO NGT_ВалидацияТОНеттинга( rq:object, ntg, Параметры )

  if( (rq.rec.State != DLRQ_STATE_PLAN) and (rq.rec.State != DLRQ_STATE_DELAYED) )
     return NTG_RQERR_STATE;
  end;

  /*для БОЦБ проверяем, что в случае не через транзит сч в список ТО не попали ТО по авансу или компенсационные*/
  if( (ntg.FI_Kind == FIKIND_CURRENCY) AND
      (not Параметры.НеттингЧерезТранзитныеСчета) AND (ntg.ClientId <= 0) AND/*проверяем для собственных*/
      ((rq.rec.Type == DLRQ_TYPE_AVANCE) or
       (rq.rec.Type == DLRQ_TYPE_DEPOSIT) or
       (rq.rec.Type == DLRQ_TYPE_COMPPAYM))
    )
     return NTG_RQERR_NOINCRQ;
  end;

  if( rq.rec.PlanDate != ntg.ValueDate )
     return NTG_RQERR_DATE;
  end;

  if( (ntg.BaseFIID != -1) and (rq.rec.FIID != ntg.BaseFIID) )
     return NTG_RQERR_BASEFI;
  end;

  return 0;
END;

MACRO NGT_ВалидацияПлатежаНеттинга( pmobj, ntg )

  if( (pmobj.PaymStatus != PM_READIED) and (pmobj.PaymStatus != PM_BEFORE_PROCESS) and (pmobj.PaymStatus != PM_REMOVE_OVERDUE) and (pmobj.PaymStatus != PM_REMOVE_PROLONGATED) )
     return NTG_ERR_PAYMSTAT;
  end;

  /*if( not( ((((ntg.ClientId <= 0) and (pmobj.Payer == {OurBank})) or (((ntg.ClientId > 0)) and (pmobj.Payer == ntg.ClientId)))       and (pmobj.PayerBankID    == {OurBank}) and (pmobj.Receiver == ntg.Contractor)) or
           ((((ntg.ClientId <= 0) and (pmobj.Receiver == {OurBank})) or (((ntg.ClientId > 0)) and (pmobj.Receiver == ntg.ClientId))) and (pmobj.ReceiverBankID == {OurBank}) and (pmobj.Payer    == ntg.Contractor)) 
         )
    )
     return NTG_ERR_PAYMCONTR;
  end;*/

  if( pmobj.ValueDate != ntg.ValueDate )
     return NTG_ERR_PAYMDATE;
  end;

  if( (ntg.BaseFIID != -1) and (pmobj.BaseFIID != ntg.BaseFIID) )
     return NTG_ERR_PAYMBASEFI;
  end;

  if( (ntg.PayFIID != -1)and(ntg.FI_Kind == FIKIND_CURRENCY) )
     if( ((pmobj.Payer == {OurBank}) or (pmobj.Payer == ntg.ClientId)) and (pmobj.ReceiverFIID != ntg.PayFIID) )
         return NTG_ERR_PAYMPAYFI;
     elif( ((pmobj.Receiver == {OurBank}) or (pmobj.Receiver == ntg.ClientId)) and (pmobj.PayerFIID != ntg.PayFIID) )
         return NTG_ERR_PAYMPAYFI;
     end;
  end;

  if( (ntg.PayFIID == -1) and (ntg.FI_Kind == FIKIND_CURRENCY) )
     if( ((pmobj.Payer == {OurBank}) or (pmobj.Payer == ntg.ClientId)) and (pmobj.ReceiverFIID != pmobj.BaseFIID) )
        return NTG_ERR_PAYMPAYFIMN;
     elif( ((pmobj.Receiver == {OurBank}) or (pmobj.Receiver == ntg.ClientId)) and (pmobj.PayerFIID != pmobj.BaseFIID) )
        return NTG_ERR_PAYMPAYFIMN;
     end;
  end;

  return 0;      
END;

MACRO NGT_ПолучитьНомерСделки( DealID:integer, DocKind:integer ):string
  VAR Deal = NULL;

  if( (DocKind == DL_DVNDEAL) or (DocKind == DL_DVDEALT3) or (DocKind == DL_DVFXDEAL) )
     Deal = TRsbDataSet("select t_code as dc from ddvndeal_dbt where t_id = " + DealID);
  else
     Deal = TRsbDataSet("select t_dealcode as dc from ddl_tick_dbt where t_dealid = " + DealID);
  end;

  Deal.MoveNext();

  return Deal.dc;
END;


MACRO NGT_ОбновитьГрафикПоТО( rq )
  var DataSet, query, cmd; 
  var err = 0;

  query =   " select grdeal.t_TemplNum, grdeal.t_FIID "
          + "   from ddlgrdeal_dbt grdeal "
          + "  where grdeal.t_DocKind = ? "
          + "    and grdeal.t_DocID = ? "
          + "    and (   (grdeal.t_TemplNum IN ("+DLGR_TEMPL_DEPODRAFT+","+DLGR_TEMPL_DEPODRAFTCONTR+") and ? = 1) "
          + "         or (grdeal.t_TemplNum IN ("+DLGR_TEMPL_DEPODRAFT2+","+DLGR_TEMPL_DEPODRAFTCONTR2+") and ? = 2) "
          + "        ) ";

  cmd = Dl_RSDCommand(query);
  cmd.AddParam(rq.rec.DocKind);
  cmd.AddParam(rq.rec.DocID);
  cmd.AddParam(rq.rec.DealPart);
  cmd.AddParam(rq.rec.DealPart);

  DataSet = cmd.Execute();
  while( (err == 0) and (DataSet.moveNext()) )
     err = DL_UpdateGrDealAcc( rq.rec.DocKind, rq.rec.DocID, DataSet.FIID, DataSet.TemplNum, DLGR_ACCKIND_CUSTODY, date(0,0,0), DLGRACC_STATE_FACTEXEC);
  end;

  return err;
END;
