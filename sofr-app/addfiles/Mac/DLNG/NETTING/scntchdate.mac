/*
$Name:        scntchdate.mac
$Module:      Неттинг
$Description: Шаг изменения сроков исполнения (просрочка/снятие с просрочки)
*/
import DealsInter, "dlcarry.inc", "dlntlib.mac", "dlntfd.mac";

record ChangeDates("spchdate.str"); /*глобальная структура с данными по изменению сроков*/
private record ntg_buff("dl_nett.dbt");
private var FD;

MACRO ПроверитьРезТО(rq, FIID:@Integer )
  if( (FIID != -1) AND (FIID != rq.rec.FIID) )
     MsgBoxEX("Операция должна выполняться только для неттинга по одной валюте.", 0, 0, "Ошибка выполнения");
     return false;
  end;
  FIID = rq.rec.FIID;
  return true;
END;

macro ВыполнитьПросрочкуТО( rq:object )
    
  FD.SetBaseFIID(rq.rec.FIID);
    
  if( (rq.rec.State == DLRQ_STATE_EXEC) OR (rq.rec.State == DLRQ_STATE_OVERDUE) )
     MsgBoxEX("Статус результирующего ТО не подходит для выполнения просрочки.", 0, 0, "Вынос на просрочку", "Ошибка при выполнении операции");
     return false;
  end;

  rq.rec.State = DLRQ_STATE_OVERDUE;
  if( DL_ChangeDLRQ(rq, ntg_buff.ValueDate, DLRQ_ACTION_UPDATE) )
    return false;
  end;

  if( ntg_buff.FI_Kind == FIKIND_CURRENCY )
     if( DL_InsertGrDeal(ntg_buff.DocKind, ntg_buff.NettingID, DLGR_TEMPL_OVERDUEPAY, ntg_buff.ValueDate, time(), ntg_buff.BaseFIID) != 0 )
        return false;
     end;

     if( DL_SetDateGrDeal(ntg_buff.DocKind, ntg_buff.NettingID, DLGR_DATEKIND_PAYMENT, date(31,12,9999)) != 0 )
        return false;
     end;
  elif( ntg_buff.FI_Kind == FIKIND_AVOIRISS )
     if( DL_SetDateGrDeal(ntg_buff.DocKind, ntg_buff.NettingID, DLGR_DATEKIND_DELIVERY, date(31,12,9999)) != 0 )
        return false;
     end;

     if( DL_SetDateGrDeal(ntg_buff.DocKind, ntg_buff.NettingID, DLGR_DATEKIND_DEPO, date(31,12,9999)) != 0 )
        return false;
     end;
  end;
  
  return true;
end;

macro СнятьСПросрочкиТО( rq ) 
  var NewDate = {curdate};

  FD.SetBaseFIID(rq.rec.FIID);

  if( rq.rec.State != DLRQ_STATE_OVERDUE )
     MsgBoxEX("Результирующее ТО должно быть просрочено.", 0, 0, "Снятие с просрочки", "Ошибка при выполнении операции");
     return false;
  end;

  if( not GetDate(NewDate, "Введите дату снятия ТО с просрочки") )
     return false;
  end;

  rq.rec.State    = DLRQ_STATE_PLAN;
  rq.rec.PlanDate = NewDate;
  if( DL_ChangeDLRQ(rq, NewDate, DLRQ_ACTION_UPDATE) )
     return false; 
  end;

  if( ntg_buff.FI_Kind == FIKIND_CURRENCY )
     if( DL_SetDateGrDeal(ntg_buff.DocKind, ntg_buff.NettingID, DLGR_DATEKIND_PAYMENT, NewDate) != 0 )
        return false;
     end;
  elif( ntg_buff.FI_Kind == FIKIND_AVOIRISS )
     if( DL_SetDateGrDeal(ntg_buff.DocKind, ntg_buff.NettingID, DLGR_DATEKIND_DELIVERY, NewDate) != 0 )
        return false;
     end;
     
     if( DL_SetDateGrDeal(ntg_buff.DocKind, ntg_buff.NettingID, DLGR_DATEKIND_DEPO, NewDate) != 0 )
        return false;
     end;
  end;

  return true;
end;

PRIVATE MACRO ИзменениеСроковТО(ntg_buff)
  var FIID = -1;

  if( not ForEachRQRes(ntg_buff.NettingID, @ПроверитьРезТО, @FIID) )
     return 1;
  end;

  if( ChangeDates.Action == 1 ) /*просрочка*/
    if( not ForEachRQRes(ntg_buff.NettingID, @ВыполнитьПросрочкуТО) )
       return 1;
    end;
  elif( ChangeDates.Action == 2 ) /*снятие с просрочки*/
    if( not ForEachRQRes(ntg_buff.NettingID, @СнятьСПросрочкиТО) )
       return 1;
    end;
  end; 

  return 0;
END;

macro ExecuteStep( doc, FDoc, DocKind, _ID_Operation, _ID_Step )

  SetBuff(ntg_buff, FDoc);

  /*Должна выполняться только для неттинга по деньгам и по одной валюте*/
  if( ntg_buff.FI_Kind != FIKIND_CURRENCY ) /*валюта*/
     MsgBoxEX("Операция должна выполняться только для неттинга по денежным платежам.", 0, 0, "Ошибка выполнения");
     return 1;
  end;

  FD = DL_NettingDoc(ntg_buff);

  if( (ChangeDates.Action == 1) or (ChangeDates.Action == 2)) /*просрочка или снятие с просрочки*/
     return ИзменениеСроковТО(ntg_buff);
  elif( ChangeDates.Action == 0 ) 
     MsgBoxEX("Шаг изменения сроков исполнения из списка шагов выполнять запрещено.", 0, 0, "Ошибка выполнения операции");
     return 1;
  else
     MsgBoxEX("Неопределен вид действия для операции изменения сроков.", 0, 0, "Ошибка выполнения операции");
     return 1;
  end;

  return 0;
end;
