/*
$Name:         scnt40.mac
$Module:       Неттинг
$Description:  макрос шага "Исполнение обязательств по сделке"
*/
IMPORT DealsInter, dlntlib;

PRIVATE CONST
    NTG_ERR_SYSERROR = 0,
    NTG_ERR_BADEXECRQ = 1;

PRIVATE VAR Err       = NULL;

PRIVATE CLASS (NTG_ERROR)STEP_ERROR
    PRIVATE MACRO ПолучитьСтрокуСОшибкой(errnum, parm)
        VAR
            idx = 2,
            cnt = parm.Size,
            str = "";

        if (errnum == NTG_ERR_SYSERROR)
            str = parm[2];
        elif (errnum == NTG_ERR_BADEXECRQ)
            str = "Ошибка при исполнении итогового ТО";
        else
            str = "Неизвестная ошибка";
        end;

        while(idx < cnt)
            if(not((idx == 2) and (errnum == NTG_ERR_SYSERROR)))
                if((ValType(parm[idx]) != V_UNDEF) and (parm[idx] != null))
                    str = str + "|" + parm[idx];
                end;
            end;
            idx = idx + 1;
        end;

        return str;
    END;

    InitNTG_ERROR();
END;

PRIVATE MACRO ИсполнитьИтоговыеТО(ntg_buff)
  var err = 0;
  var DataSet, cmd, query;
  var rq = TRecHandler("dlrq.dbt");
  var cntrq = 0;

  query =   " select * "
          + "   from ddlrq_dbt "
          + "  where t_DocKind = ? "
          + "    and t_DocID = ? ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(ntg_buff.DocKind);
  cmd.AddParam(ntg_buff.NettingID);

  DataSet = cmd.Execute();                       
  while((err == 0) and (DataSet.moveNext()))
    /*посчитаем, что есть не нулевые ТО */ 
    if( DataSet.Amount > $0) 
      cntrq = cntrq + 1;
    end; 
    
    DataSet.GetRecord().CopyTo( rq.rec );

    if( rq.rec.State == DLRQ_STATE_OVERDUE )
       MsgBox("Ожидается снятие оплаты результирующего ТО с просрочки.");
       return 1;
    end;

    rq.rec.State = DLRQ_STATE_EXEC;
    rq.rec.FactDate = rq.rec.PlanDate;
    err = DL_ChangeDLRQ(rq, rq.rec.PlanDate, DLRQ_ACTION_UPDATE);
  end;

  if(not err)
    if(ntg_buff.FI_Kind == FIKIND_CURRENCY)
      err = DL_UpdateGrDealAcc( ntg_buff.DocKind, ntg_buff.NettingID, ntg_buff.BaseFIID, DLGR_TEMPL_PAYMENT, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC);

      if((not err) and (cntrq == 0))
        err = DL_UpdateGrDealAcc( ntg_buff.DocKind, ntg_buff.NettingID, ntg_buff.BaseFIID, DLGR_TEMPL_PAYMENT, DLGR_ACCKIND_ACCOUNTING, date(0,0,0), DLGRACC_STATE_NOTNEED);
        if(not err)
          err = DL_UpdateGrDealAcc( ntg_buff.DocKind, ntg_buff.NettingID, ntg_buff.BaseFIID, DLGR_TEMPL_PAYMENT, DLGR_ACCKIND_INNER, date(0,0,0), DLGRACC_STATE_NOTNEED);
        end;
      end;
    elif(ntg_buff.FI_Kind == FIKIND_AVOIRISS)
      if(cntrq != 0)
        err = DL_UpdateGrDealAcc( ntg_buff.DocKind, ntg_buff.NettingID, ntg_buff.BaseFIID, DLGR_TEMPL_DELIVERYNTG, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC);
      end;

      if((not err) and (cntrq == 0))
        err = DL_UpdateGrDealAcc( ntg_buff.DocKind, ntg_buff.NettingID, ntg_buff.BaseFIID, DLGR_TEMPL_DELIVERYNTG, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_NOTNEED);
        if(not err)
          err = DL_UpdateGrDealAcc( ntg_buff.DocKind, ntg_buff.NettingID, ntg_buff.BaseFIID, DLGR_TEMPL_DELIVERYNTG, DLGR_ACCKIND_INNER, date(0,0,0), DLGRACC_STATE_NOTNEED);
        end;
        if(not err)
          err = DL_UpdateGrDealAcc( ntg_buff.DocKind, ntg_buff.NettingID, ntg_buff.BaseFIID, DLGR_TEMPL_DEPODRAFT, DLGR_ACCKIND_CUSTODY, date(0,0,0), DLGRACC_STATE_NOTNEED);
        end;
      end;
    end;
  end;

  return err;

END;

MACRO ExecuteStep(Buffer, Document, DocKind, ID_Operation, ID_Step)
    RECORD ntg_buff("dl_nett");
    Err = STEP_ERROR;

    SetBuff( ntg_buff, Document );

    if(ИсполнитьИтоговыеТО(ntg_buff) != 0)
      Err.Добавить(NTG_ERR_BADEXECRQ);
    end;

    if (Err.Вывести())
        return 1;
    end;

    return 0;
END;
