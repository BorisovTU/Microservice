/*
$Name:        dlnt40.mac
$Module:      Неттинг
$Description: макрос шага "Исполнение обязательств по сделке"
*/
IMPORT PaymInter, dlnteffpm, dlnteffrq;
IMPORT "role_model.mac";//ролевая модель
import "dl_check_mt300.mac"; //Simanov

PRIVATE CONST
    NTG_ERR_SYSERROR = 0,
    NTG_ERR_CHGKURS  = 1,
    NTG_ERR_BADCARRY = 2,
    NTG_ERR_BADCONV  = 3;

PRIVATE VAR Параметры = NULL,
            Err       = NULL;
/*PNV*/
PRIVATE MACRO IsCorschem_LORO (corrid, FIid)

        FILE crs(corschem) key 1;
        crs.Number  = corrid;
        crs.FI_Kind = 1;
        crs.FIid = FIid;
        if( getEQ(crs) )
            if (crs.ISNOSTRO == SET_CHAR)
                return false;
            else
                return true;
            end;
        else
            return false;
        end;
END;

private macro PartyIsNotResident( PartyID )
   var PartyObj:RsbParty = RsbParty( PartyID );
   return PartyObj.IsNotResident;
end;
/*PNV*/

PRIVATE CLASS (NTG_ERROR)STEP_ERROR
    PRIVATE MACRO ПолучитьСтрокуСОшибкой(errnum, parm)
        VAR
            idx = 2,
            cnt = parm.Size,
            str = "";

        if (errnum == NTG_ERR_SYSERROR)
            str = parm[2];
        elif (errnum == NTG_ERR_CHGKURS)
            str = "Ошибка при учете доходов/расходов от изменения курсов";
        elif (errnum == NTG_ERR_BADCARRY)
            str = "Ошибка при исполнении проводок";
        elif (errnum == NTG_ERR_BADCONV)
            str = "Ошибка при конвертации";
        else
            str = "Неизвестная ошибка";
        end;

        while(idx < cnt)
            if(not((idx == 2) and (errnum == NTG_ERR_SYSERROR)))
                if((ValType(parm[idx]) != V_UNDEF) and (parm[idx] != null))
                    str = str + "|" + parm[idx];
                end;
            end;
            idx = idx + 1;
        end;

        return str;
    END;

    InitNTG_ERROR();
END;

PRIVATE MACRO ИсполнениеОбязательствДляВалют(pmobj, ntg, ntg_fd)
  var stat = 0;
  var mes = "";
  //TEG 02.05.19 ролевая модель   
   /* Определяем конечного операциониста для проводки */
  var NumberOper = GetMainOperInGroup(PmObj.DocKind);
   if ( NumberOper<= 0)
        NumberOper = {Oper};
   end;
      if( Параметры.БэкОфис == "Ю" )
           pmobj = ExecMacroFile("dlntdv.mac", "КорректироватьПлатеж", pmobj, ntg);
      end;
      //TEG 12.02.19  BEG
      if (pmobj.BaseFiid == 0)
          if ((pmobj.OutCorschem != -1 ) and (IsCorschem_LORO (pmobj.OutCorschem, pmobj.basefiid)) )
               if (PartyIsNotResident(pmobj.receiver))
                   pmobj.Ground = "TRANSFER OF FUNDS UNDER NETTING FOREX DEAL VALUE DATE " + StrSubSt(String(ntg.valuedate:f), ".", "/");
              else
                   pmobj.Ground = "ПЕРЕВОД СРЕДСТВ ПО НЕТТИНГУ ОТ " + StrSubSt(String(ntg.valuedate:f), ".", "/")+" С " + pmobj.ReceiverName+" НДС НЕ ОБЛАГАЕТСЯ";
                   pmobj.paymentkind = "С";
              end;
         else
              pmobj.Ground = "ПЕРЕВОД СРЕДСТВ ПО НЕТТИНГУ ОТ " + StrSubSt(String(ntg.valuedate:f), ".", "/")+" С " + pmobj.ReceiverName+" НДС НЕ ОБЛАГАЕТСЯ";
              pmobj.paymentkind = "С";
         end;
      else
         pmobj.Ground = "TRANSFER OF FUNDS UNDER NETTING FOREX DEAL VALUE DATE " + StrSubSt(String(ntg.valuedate:f), ".", "/");
      end;

      pmobj.number = pmobj.paymentid;
      pmobj.numberpack = 30;
     /*PNV 505842*/
      var valueDate = pmobj.valueDate;
      if (isHoliday(valueDate))
        pmobj.valueDate = GetDateAfterWorkDays(valueDate, 1);
      end;
     /*PNV 505842*/
      //TEG 02.05.19 ролевая модель   
      PmObj.Oper = NumberOper;

      if( ((pmobj.PayerBankID == {OurBank}) and (pmobj.ReceiverBankID == {OurBank})) OR
         ((Параметры.БэкОфис == "Ю") and (/*(not NGT_ИнтегрРежимРаботы()) or*/ NGT_ДелатьПроводкуПоИсхПлатежам()))  
         )
        if( (pmobj.PIList(PRT_Debet).Size > 0) and (pmobj.PIList(PRT_Credit).Size > 0) )
             stat = pmobj.MakeMultyTransaction();
             if( stat )
                 mes = GetErrMsg();
                 if( strlen(mes) == 0 )
                     InitError();
                     MemoryError(stat);
                     mes = GetErrMsg();
                 end;
                 Err.Добавить(NTG_ERR_BADCARRY,mes);
                 return false;
             end;
        else
           if (Round(pmobj.BaseAmount(), 2) > 0) /*PNV 535466 проводку формируем только если сумма со всеми округлениями будет ненулевая*/
               if( not pmobj.MakeTransaction().Carry() )
                   mes = GetErrMsg();
                   Err.Добавить(NTG_ERR_BADCARRY,mes);
                   return false;
               end;
           end;
        end;
        pmobj.PaymStatus = PM_READY_TO_SEND; 
        pmobj.IsFactPaym = "X"; 
       /*PNV 505842*/
        if (isHoliday(valueDate))
           pmobj.valueDate = GetDateAfterWorkDays(valueDate, 1);/*CHVA 512411*/
        end;
      /*PNV 505842*/
     else
        if( (Параметры.БэкОфис == "V") or (Параметры.СквознойНеттинг) )
             pmobj = ExecMacroFile("dlntfx.mac", "КорректироватьПлатеж", pmobj, ntg);
        end;

        pmobj.PaymStatus = PM_READY_TO_SEND;
        pmobj.IsFactPaym = "X"; 
     end;

  return true;

OnError( e )
  msgbox(e.Message);
  return false;
END;

PRIVATE MACRO ИсполнениеТОДляВалют(rq, ntg, ntg_fd, ID_Operation, ID_Step)
    var stat = 0;
    var mes = "";
    var DebAccountBuff = TRecHandler("account");
    var CredAccountBuff = TRecHandler("account");
    var Cat = "";
    var Amount_From = $0, Amount_To = $0;
   //TEG 02.05.19 ролевая модель
    var NumberOper = GetMainOperInGroup(ntg.DocKind);
    if ( NumberOper<= 0)
         NumberOper = {Oper};
    end;
   /**/

    if(ntg.OverTransitAccount == "X")
      Cat = "+Расчеты";
    else
      Cat = "+Форвард, расчеты";
    end;

    if(not ntg_fd.GetAccount(Cat, DebAccountBuff.rec.Account, MC_OPENACC_CREATE, ntg.PayFIID, rq.rec.PlanDate))
      return false;
    end;
    
    if( rq.rec.FIID != ntg.PayFIID )
       if( ConvSum( Amount_From, rq.rec.Amount, rq.rec.PlanDate, rq.rec.FIID, ntg.PayFIID) )
          Err.Добавить(NTG_ERR_BADCONV);
          return false;
       end;
    else
       Amount_From = rq.rec.Amount;
    end;


    if(ExecMacroFile("spserv.mac", "ПолучитьДенежныйСчетСубъектаПоСделке", rq.rec.DocKind, rq.rec.DocID, rq.rec.Party, rq.rec.FIID, CredAccountBuff) != 0)
      return false;
    end;

    if( rq.rec.FIID != CredAccountBuff.rec.Code_Currency )
       if( ConvSum( Amount_To, rq.rec.Amount, rq.rec.PlanDate, rq.rec.FIID, CredAccountBuff.rec.Code_Currency) )
          Err.Добавить(NTG_ERR_BADCONV);
          return false;
       end;
    else
       Amount_To = rq.rec.Amount;
    end;

    if(DL_MultyCarry (ntg.PayFIID, DebAccountBuff.rec.Account, Amount_From, 
                      CredAccountBuff.rec.Code_Currency, CredAccountBuff.rec.Account, Amount_To, 1,
                      NULL, NULL, rq.rec.PlanDate, rq.rec.PlanDate,
                      "Исполнение обязательств по операции № "+ntg.DealNumber+" заключена "+ntg.SigningDate, 0, null, 30,NumberOper) ) 
        mes = GetErrMsg();
        Err.Добавить(NTG_ERR_BADCARRY,mes);
        return false;
    end;

    rq.rec.State = DLRQ_STATE_EXEC;
    if( DL_ChangeDLRQ(rq, rq.rec.PlanDate, DLRQ_ACTION_UPDATE) )
      return false; 
    end; 

    return true;
END;

MACRO ExecuteStep( Buffer, Document, DocKind, ID_Operation, ID_Step )

  RECORD ntg_buff("dl_nett");
  Err = STEP_ERROR;

  SetBuff(ntg_buff, Document);
  DL_PrepareCarryBuffer(Buffer);

  //Simanov. 13.01.20 - Проверка сквитованных МТ300 для сделок, заключенных по телефону
  if (not DL_nett_check_kvit_mt(ntg_buff) )
    return 1;
  end;

  Параметры = ПараметрыИсполнения(true, ntg_buff);
  if (Параметры.БэкОфис == "S")
    ИсполнитьТО(Параметры, DLRQ_KIND_COMMIT, ntg_buff, @ИсполнениеТОДляВалют, Err, ID_Operation, ID_Step);
  else
     ИсполнитьПлатежи(Параметры, false, ntg_buff, @ИсполнениеОбязательствДляВалют, Err);
  end;

  if( Err.Вывести() )
     return 1;
  end;

  return 0;
END;
