/*******************************************************************************
 DESCRIPTION  :   Отчет "Уведомление о приобретениях ц/б нерезидентами"
 PROGRAMMED BY:   Трафименков Г.М.
 CREATION DATE:   08.02.2005
*******************************************************************************/
import "spRepFn2.mac", "dlmisc.mac";

private const
   StatLine = "Выпуск отчета \"Уведомление о приобретениях ц/б нерезидентами\"";

/* Класс с данными отчета. */
private class (TBaseReportData) TReportData( _BegDate, _EndDate, _PartyID,
                                             _IssuerID, _Apostr )

   var
      BegDate     = _BegDate,       /* Начало отчетного периода. */
      EndDate     = _EndDate,       /* Конец отчетного периода. */
      IssuerID    = _IssuerID,      /* Эмитент, если -1, то все. */
      /* Субъект, если -1, то все. */
      PartyID     = IIF( _PartyID == {OurBank}, {OurBank}, _PartyID );

   /* Конструктор. */
   initTBaseReportData( _Apostr );
end;

var TableHead = "┌──────────────────────┬──────────────────────┬───────────────────────┬───────────────────────┬───────────────────────┬─────────---───-──┬───────────────────┐\n"+
                "│   Полное фирменное   │    Вид, категория,   │    Государственный    │   Наименование (имя)  │   Наименование (имя)  │    Количество    │   Дата внесения   │\n"+
                "│     наименование     │  (тип) ценных бумаг, │ регистрационный номер │        продавца       │       покупателя      │   приобретенных  │  приходной записи │\n"+
                "│ российского эмитента │     номинальная      │    выпуска и дата     │                       │                       │    ценных бумаг  │ по лицевому счету │\n"+
                "│                      │   стоимость одной    │   государственной     │                       │                       │                  │    (счету депо)   │\n"+
                "│                      │    ценной бумаги     │     регистрации       │                       │                       │                  │    приобретателя  │\n"+
                "├──────────────────────┼──────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼──────────────────┼───────────────────┤";

var Rep = CMakeReport(TableHead);
const FontStyleTitel =  "ex_FS(b)";


/* Печать заголовка таблицы. */
private macro PrintTableHeader( Party )
  
       Rep.AddPrintCell("В ФСФР России", Rep.GetHeaderWidth(), 0, "r:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
       Rep.AddPrintCell("О сделках, совершенных с акциями", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
       Rep.AddPrintCell("российских эмитентов", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
       Rep.AddPrintCell("Настоящим  " + Party.Name, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
       Rep.AddPrintCell("уведомляет о совершении следующих сделок с акциями российских эмитентов,", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
       Rep.AddPrintCell("в капитале которых законодательством Российской Федерации установлены", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
       Rep.AddPrintCell("ограничения на долю участия иностранных лиц:", Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
end;

macro PrintLine(  IssuerName:String, AvoirKind1:string, GosN:string, SellerName:string, BuyerName:string,
                  Amount, SetAvDate:string, SumPrecision:integer )

        var precision = 0;
        if(IsHaveFractPart(Amount))
           Precision = SumPrecision;
        end;

        Rep.AddPrintCell(IssuerName,  0, 0, "l");
        Rep.AddPrintCell(AvoirKind1,  0, 0, "l");
        Rep.AddPrintCell(GosN,        0, 0, "l");
        Rep.AddPrintCell(SellerName,  0, 0, "l");
        Rep.AddPrintCell(BuyerName, 0, 0, "c");
        Rep.AddPrintCell(ЧислоCЗаданнойТочностью(Amount, Precision),  0, Precision, "c");/*выводим в числовом формате*/
        Rep.AddPrintCell(SetAvDate,    0, 0, "c");

   Rep.AddStr();
end;

/* Печать подвала таблицы.
      Party       -  субъект
      IsOurClient -  субъект является нашим клиентом */
private macro PrintTableFooter( Party, IsOurClient )
   var
      JurAddress, PostalAddress, PartyFullName, OurFullName;

   /* H2 - Юридический адрес стороны по сделке, для которой формируется
      уведомление */
   JurAddress = GetPartyAddress( Party.PartyID );

   /* H3 - Почтовый адрес стороны по сделке (из анкеты субъекта), для
      которой формируется уведомление */
   PostalAddress = GetPartyAddress( Party.PartyID, false, false, PTADDR_POST );
       Rep.AddPrintCell("Место нахождения: " + JurAddress, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
       Rep.AddPrintCell("Адрес для получения почтовых отправлений: "+ PostalAddress, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();

  
   if( IsOurClient )
      /* H4 -  Полное наименование стороны по сделке, для которой формируется
               уведомление, дополненное слева текстом "По поручению ".
               Заполняется, только если указанная сторона по сделке - наш
               клиент, действующий по договору */
      PartyFullName = Party.Name;

      /* H5 -  Полное наименование нашего банка, дополненное слева текстом
               "уведомление направляется ". Заполняется, только если
               указанная сторона по сделке - наш клиент, действующий по
               договору. */
      OurFullName = GetPartyFullNameByID( {OurBank} );
       Rep.AddPrintCell("По поручению " + PartyFullName, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
       Rep.AddPrintCell("уведомление направляется " + OurFullName, Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();

       end;

   /* F1 - Дата составления отчета (текущая операционная дата) */
       Rep.AddPrintCell(" Дата:  " + DateToStr({CurDate}, true), Rep.GetHeaderWidth(), 0, "l:" + FontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
end;

/* Расчитываем параметры строки таблицы и печатаем ее. 
      ReportData     -  данные отчета
      TempFileRec    -  буфер записи
      CurParty       -  текущий субъект */
private macro CalcAndPrintTableLine( ReportData, TempFileRec, CurParty )

   /* Получить ID покупателя или продавца.
         Deal        -  сделка
         IsBuy       -  флаг, сделка покупки или часть покупки
         IsNeedBuyer -  флаг, нужен покупатель, иначе продавец */
   macro GetBuyerSallerID( Deal, IsBuy, IsNeedBuyer )

      /* Продавец: если сделка - покупка, то контрагент; если сделка
         продажи, но не клиентская, то наш банк, клиентская продажа -
         клиент. */

      /* Покупатель: если сделка - продажа, то котрагент; если сделка
         покупки, но не клиентская, то наш банк, клиентская покупка -
         клиент. */

      if(   (IsBuy and (not IsNeedBuyer))
            or ((not IsBuy) and IsNeedBuyer) )
         return GetContrInDeal( Deal );
      else
         return IIF( IsClientDeal(Deal), Deal.ClientID, {OurBank} );
      end;
   end;

   var
      FinInstr, Avoir, Deal, Leg,
      IssuerName, AvoirKind, FaceValueDbl, FaceValueStr, FaceValFIName,
      LSIN, Issued, SellerName, BuyerName, Amount = 0.0, SetAvDate, IsBack, IsBuy, AvoirKind1, GosN;

   /* Получаем параметры ценной бумаги. */
   FinInstr = GetFinInstr( TempFileRec.FIID, true );
   Avoir    = GetAvoirRec( TempFileRec.FIID, true );
   var fi = TRecHandler( "fininstr" );
   ПолучитьФинИн(TempFileRec.FIID, fi );  /*Нужно для передачи в FI_GetIssuerOnDate*/

   /* Получаем параметры сделки. */
   Deal        = GetDealByID( TempFileRec.DealID, true );
   Leg         = GetDlLegByID( TempFileRec.LegID, true );
   IsBack      = Leg.LegKind == LEG_KIND_DL_TICK_BACK;
   IsBuy       = IsBuyPartDeal( Deal, IsBack );

   /* A1 - Полное наименование эмитента из анкеты субъекта (актуального на дату составления отчета (текущая операционная дата). )*/
   IssuerName = GetPartyFullNameByID( FI_GetIssuerOnDate (fi, {CurDate} ), true ); 

   /* A2 - Полное наименование вида ц/б */
   AvoirKind = GetAvoirKindRec( FinInstr.AvoirKind ).Name;

   /* N2 - Номинал ценной бумаги (для облигаций с амортизацией долга -
      исходный номинал) */
   FaceValueDbl = GetFaceValue( FinInstr.FIID, null, true );
   FaceValueStr = ReportData.NumToStr( FaceValueDbl,
                                       GetFinInstrFaceValPoint(FinInstr) );

   /* A2.1 - Название валюты номинала */
   FaceValFIName = GetFinInstrName( FinInstr.FaceValueFI, true );

   /* A3 - Номер государственной регистрации из анкеты выпуска ц/б */
   LSIN = Avoir.LSIN;

   /* D3 - Дата выпуска из анкеты выпуска ц/б */
   Issued = FinInstr.Issued;

   /* A4 - Краткое наименование продавца по сделке */
   SellerName = GetPartyShortNameByID( GetBuyerSallerID(Deal, IsBuy, false) );

   /* A5 - Краткое наименование покупателя по сделке */
   BuyerName = GetPartyShortNameByID( GetBuyerSallerID(Deal, IsBuy, true) );

   /* N6 - Количество ц/б в сделке */
   Amount = Leg.Principal;

   /* D7 - Фактическая дата поставки по сделке */
   SetAvDate = TempFileRec.SetAvDate;
   AvoirKind1 = AvoirKind + ", " + FaceValueStr + " " + FaceValFIName; 
   GosN = LSIN + ", " + DateToStr(Issued, true);
   PrintLine(IssuerName, AvoirKind1, GosN, SellerName, BuyerName, Amount, DateToStr(SetAvDate, true), FinInstr.SumPrecision);

end;
  

/* Печать отчета. */
private macro PrintReport( ReportData, TempFile )
   var
      Progress       = TProgressBar,
      PrevPartyID    = -1,
      CurParty, IsOurClient;

   Progress.Init( TempFile.NRecords, StatLine, "Печать отчета" );

   TempFile.Rewind;
   while( TempFile.Next )
      /* Если изменился субъект, то печатаем итоги и заголовок новой
         таблицы. */
      if( PrevPartyID != TempFile.rec.PartyID )

         if( PrevPartyID != -1 )
            PrintTableFooter( CurParty, IsOurClient );
         end;

         CurParty    = GetPartyByID( TempFile.rec.PartyID, true );
         IsOurClient = TempFile.rec.IsClient;

         PrintTableHeader( CurParty );
      end;

      /* Печатаем строку таблицы. */
      CalcAndPrintTableLine( ReportData, TempFile.rec, CurParty );

      PrevPartyID = TempFile.rec.PartyID;
      Progress.Use;
   end;

   Progress.Remove;

   if( CurParty != NULL)
      PrintTableFooter( CurParty, IsOurClient );
   else
      Print_NoDataForReport;
   end;
end;

/* Проверяем, есть ли у эмитента ограничение на участие иностранцев в
   капитале. Возвращаем true, если ограничение есть.
      Issuer   -  ID эмитента или буфер (файл) */
private macro CheckIssuer( Issuer )
   /* Проверку делаем на текущую операционную дату. */
   return PT_IsHaveLimitAlienInCap( Issuer );
end;

/* Сбор данных во временный файл. Возвращаем true, если данные отобрали.
      ReportData  -  данные отчета
      TempFile    -  временный файл для сбора данных */
private macro CollectData( ReportData, TempFile )

   /* Сохраняем сделку для отчета.
         PartyID        -  субъект
         IsPartyClient  -  субъект является нашим клиентом */
   macro SaveDealForReport( Deal, Leg, DataSet, PartyID, IsPartyClient )
      /* Если отчет выпускаем по конкретному субъекту, отчет должен печататься только по одной стороне - субъекту*/
      if( ( ReportData.PartyID == -1 ) OR ( (NOT( ReportData.PartyID == -1 )) AND ( ReportData.PartyID == PartyID ) ) )
         TempFile.Clear;
         TempFile.rec.PartyID    = PartyID;
         TempFile.rec.IsClient   = BoolToInt( IsPartyClient );
         TempFile.rec.DealID     = Deal.DealID;
         TempFile.rec.LegID      = Leg.ID;
         TempFile.rec.FIID       = DataSet.FIID;
         TempFile.rec.SetAvDate  = SQL_ConvTypeDate(DataSet.FactDate);
         TempFile.rec.SetAvTime  = Leg.SupplyTime;
         InsertRecTempTBFile( TempFile );
         return true;
      end;
      return false;
   end;

   /* Обрабатываем сделку.
      Получаем параметры сделки и на их основе определяем, надо ли включать
      сделку в отчет, и по какому субъекту. Одна сделка может входить в несколько
      отчетов по разным субъектам.
      Возвращаем true, если включили сделку в отчет. */
   macro ProcessDeal( ReportData, FD, DataSet, IsBuy )
      /* Проверку того, что у клиента
         сделки есть договор обслуживания не делаем, т.к. такая проверка
         делается при вводе сделки. */
      var
         IsSale         = not IsBuy,
         ClientID       = FD.tick.rec.ClientID,
         IsClientDeal   = ClientID != -1,
         ClientNotRes   = IsClientDeal and PartyIsNotResident( ClientID ),
         ContrID        = GetContrInDeal( FD.tick.rec ),
         ContrNotRes    = PartyIsNotResident( ContrID ),
         IsPartyClient  = FD.tick.rec.IsPartyClient == "X",
         WasCollectData = false;

      /* Если: -  сделка покупки;
               -  контрагент
               -  клиент в сделке нерезидент
         То:   включаем сделку в отчет по клиенту. */
      if( IsBuy and IsClientDeal and ClientNotRes )

         WasCollectData = SaveDealForReport( FD.tick.rec, FD.dl_leg.rec, DataSet, ClientID, true );

         /* Если для контрагента указан договор обслуживания, то включаем еще и в отчет по контрагенту */
         if( FD.tick.rec.PartyContrID > 0 )
            WasCollectData = SaveDealForReport( FD.tick.rec, FD.dl_leg.rec, DataSet, ContrID, true );
         end;
      end;

      /* Если: -  сделка продажи;
               -  контрагент нерезидент
               -  сделка собственная
         То:   включаем сделку в отчет по нашему банку. */
      if( IsSale and ContrNotRes and (not IsClientDeal) )

         WasCollectData = SaveDealForReport( FD.tick.rec, FD.dl_leg.rec, DataSet, {OurBank}, false );

         /* Если для контрагента указан договор обслуживания, то включаем еще и в отчет по контрагенту */         
         if ( FD.tick.rec.PartyContrID > 0 )
            WasCollectData = SaveDealForReport( FD.tick.rec, FD.dl_leg.rec, DataSet, ContrID, true );
         end;
      end;

      /* Если: -  сделка продажи;
               -  контрагент нерезидент
               -  сделка клиентская
         То:   включаем сделку в отчет по клиенту. */
      if( IsSale and ContrNotRes and IsClientDeal ) 

         WasCollectData = SaveDealForReport( FD.tick.rec, FD.dl_leg.rec, DataSet, ClientID, true );

         /* Если для контрагента указан договор обслуживания, то включаем еще и в отчет по контрагенту */         
         if ( FD.tick.rec.PartyContrID > 0 )
            WasCollectData = SaveDealForReport( FD.tick.rec, FD.dl_leg.rec, DataSet, ContrID, true );
         end;
      end;

      return WasCollectData;
   end;

   var
      WasCollectData = false,
      Progress       = TProgressBar,
      FD,
      IsBack, IsBuy, Group, select, query, DataSet, count = 0;

      query = DL_RSDCommand();

      /*Сделки РЕПО на корзину не отбираем, пояснения аналитика: "В случае РЕПО с корзиной покупателем всегда является Банк России, 
             а продавцом российский коммерческий банк, исходя из определения сделок РЕПО с корзиной. Сделкам с нерезидентами тут не место."*/
      select = "select tick.t_dealID, rq.t_DealPart, rq.t_FIID, rq.t_Kind, rq.t_FactDate, RSB_FIInstr.FI_GetIssuerOnDate(fi.t_FIID,?) Issuer " +
               "  from ddl_tick_dbt tick, ddlrq_dbt rq, dfininstr_dbt fi "+
               " where rq.t_DocID    = tick.t_DealID "+ 
               "   and rq.t_DocKind  = tick.t_BofficeKind "+ 
               "   and rq.t_Type     = ? "+
               "   and rq.t_FactDate != to_date('01.01.0001', 'DD.MM.YYYY') "+
               "   and rq.t_FactDate <= ? "+
               "   and rq.t_FactDate >= ? "+
               "   and rsb_secur.IsBasket(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) != 1 "+
               "   and fi.t_FIID = rq.t_FIID "+
               "   and RSB_FIInstr.FI_AvrKindsGetRoot(fi.t_FI_Kind,fi.t_AvoirKind) = ? ";

      query.addParam({CurDate});/*эмитент, актуальный на дату <F1> (Дата составления отчета (текущая операционная дата))*/
      query.addParam(DLRQ_TYPE_DELIVERY);
      query.addParam(ReportData.EndDate);
      query.addParam(ReportData.BegDate);
      query.addParam(AVOIRISSKIND_SHARE);

      if( ReportData.IssuerID > 0 )
         select = select +
               "   and RSB_FIInstr.FI_GetIssuerOnDate(fi.t_FIID,?) = ? ";
         query.addParam({CurDate});/*эмитент, актуальный на дату <F1> (Дата составления отчета (текущая операционная дата))*/
         query.addParam(ReportData.IssuerID);
      end;

      count = query.GetCount(select);

      DataSet = query.execute(select);

      Progress.Init( count, StatLine, "Просмотр ТО по сделкам" );

      while( DataSet.MoveNext() )
         /* Проверяем эмитента.
            Если установлен фильтр на эмитента то проверяем подходит
            ли ц/б нам или нет если не подходит то пробрасываем */
         if( CheckIssuer( Int(DataSet.Issuer) ) )

            IsBack  = (DataSet.DealPart == 2);
            FD = SPFirstDoc( IIF(IsBack,LEG_KIND_DL_TICK_BACK,LEG_KIND_DL_TICK), DataSet.DealID );

            if( DataSet.Kind == DLRQ_KIND_REQUEST )
               IsBuy = true;
            else
               IsBuy = false;
            end;

            if( ProcessDeal(Reportdata, FD, DataSet, IsBuy) )
               WasCollectData = true;
            end;

         end;

         Progress.Use;
      end;

      Progress.Remove;

      return WasCollectData;
end;

/* Вызов отчета. */
macro ReportRun(  BegDate, EndDate, PartyID, IssuerID, Apostr, IsExcel )
   var
      ReportData  = TReportData( BegDate, EndDate, PartyID, IssuerID, Apostr, IsExcel ),
      TempFile    = TBFile(   "repadvnr.tmp", "W", 0 );

   Dl_CallInsertStat(IIF(IsExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD));
   ClearGlobalTmp( "drepadvnr_tmp" );

   /* Если в параметрах отчета задан конкретный эмитент, то проверим, что
      по нему есть ограничение. */
   if(   (ReportData.IssuerID != -1)
         and (not CheckIssuer(ReportData.IssuerID)) )
      MsgBox(  "По эмитенту \"" + GetPartyShortNameByID(ReportData.IssuerID)
               + "\" нет ограничения|на участие иностранцев в капитале." );
      exit(1);
   end;

   if( CollectData(ReportData, TempFile) )
      PrintReport( ReportData, TempFile );
      if( IsExcel )
         Rep.PrintWinRep();
         Rep.ShowWinRep();
      else
         Rep.PrintRep();
      end;
   else
      Print_NoDataForReport;
   end;
end;

/*
                                                               В ФСФР России
 О сделках, совершенных с акциями
 российских эмитентов

 Настоящим #
 уведомляет о совершении следующих сделок с акциями российских эмитентов,
 в капитале которых законодательством Российской Федерации установлены
 ограничения на долю участия иностранных лиц:
┌──────────────────────┬──────────────────────┬───────────────────────┬───────────────────────┬───────────────────────┬───────────────┬───────────────────┐
│   Полное фирменное   │    Вид, категория,   │    Государственный    │   Наименование (имя)  │   Наименование (имя)  │   Количество  │   Дата внесения   │
│     наименование     │  (тип) ценных бумаг, │ регистрационный номер │        продавца       │       покупателя      │ приобретенных │  приходной записи │
│ российского эмитента │     номинальная      │    выпуска и дата     │                       │                       │  ценных бумаг │ по лицевому счету │
│                      │   стоимость одной    │   государственной     │                       │                       │               │    (счету депо)   │
│                      │    ценной бумаги     │     регистрации       │                       │                       │               │    приобретателя  │
├──────────────────────┼──────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────┼───────────────────┤
│######################│######################│#######################│#######################│#######################│###############│###################│
├──────────────────────┼──────────────────────┼───────────────────────┼───────────────────────┼───────────────────────┼───────────────┼───────────────────┤
│######################│######################│#######################│#######################│#######################│###############│###################│
└──────────────────────┴──────────────────────┴───────────────────────┴───────────────────────┴───────────────────────┴───────────────┴───────────────────┘
 Место нахождения: #

 Адрес для получения почтовых отправлений: #

 #

 #

 Дата: #

*/
