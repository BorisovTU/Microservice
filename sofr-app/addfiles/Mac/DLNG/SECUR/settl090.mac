/*
$Name:        settl090.mac
$Module:      Ценные бумаги
$Description: Операция Расчеты на ОРЦБ. Шаг - завершение операции
*/

import "sp_carin.mac", "sp_categ.mac", "sp_car.mac";

private class FactSumData ( _Currency:Integer, _InSum:Double, _OutSum:Double)
   
   var Currency         = _Currency,
       InSum            = _InSum,
       OutSum           = _OutSum;

end;

private class PartySumData ( _MarketAcc:string, _Party:integer, _ClientContr:integer, _Currency:integer, _PayedSum:double, _ReceivedSum:double, _DealCode:string, _BankRest:money )
   
   var MarketAcc        = _MarketAcc,
       Party            = _Party,
       ClientContr      = _ClientContr,
       Currency         = _Currency,
       PayedSum         = _PayedSum,
       ReceivedSum      = _ReceivedSum,
       DealCode         = _DealCode,
       BankRest         = _BankRest;


end;

/*по клиентам, по котрым не было сделок за день*/
private class ClientsNoDealData ( _CLIENTCONTR:integer, _Account:string )
   
   var Account     = _Account,
       CLIENTCONTR = _CLIENTCONTR;
end;

private class (TArray) TDataClientsNoDeal

   /* Очистить массив. */
   macro ClearArray
      this.Size = 0;
   end;

   /* Добавить строчку в массив. */
   macro AddLine(CLIENTCONTR,Account)

      var
         Counter = 0;

      while( ( Counter < this.Size ) and 
             not(( this.(Counter).Account == Account ) and (this.(Counter).CLIENTCONTR == CLIENTCONTR)) 
           )
         Counter = Counter + 1;
      end;

      if( this.Size > Counter )
         if( ( this.(Counter).Account == Account ) and (this.(Counter).CLIENTCONTR == CLIENTCONTR) )
         else
             this.(Counter) = ClientsNoDealData( CLIENTCONTR,Account );
         end;
      else
         this.(Counter) = ClientsNoDealData( CLIENTCONTR,Account );
      end;

   end; /*AddLine*/

end;

private class (TArray) TDataCurrency

      class CurrData( _Currency:Integer )
          var Curr  = _Currency;
      end;
      /* Очистить массив. */
      macro ClearArray
         this.Size = 0;
      end;

      /* Добавить строчку в массив. */
      macro AddLine(Currency)

         var
            Counter = 0;

         while( ( Counter < this.Size ) and 
                (this.(Counter).Curr != Currency )
              )
            Counter = Counter + 1;
         end;

         if( this.Size > Counter )
            if( ( this.(Counter).Curr != Currency ) )
               this.(Counter) = CurrData( Currency );
            end;
         else
             this.(Counter) = CurrData( Currency );
         end;

      end; /*AddLine*/
end;

private class (TArray) TDataFactSum

      /* Очистить массив. */
      macro ClearArray
         this.Size = 0;
      end;

      macro MoveInORCB(Kind)
         if((Kind == DLSUM_KIND_DLCOMM_IN_CLIRINGACC_NATCUR) OR (Kind == DLSUM_KIND_DLCOMM_IN_CLIRINGACC_CUR) or 
            (Kind == DLSUM_KIND_DLCOMM_FROM_OTHER_CLIRACC_NATCUR) or (Kind == DLSUM_KIND_DLCOMM_FROM_OTHER_CLIRACC_CUR)
           )
            return true;
         else
            return false;
         end;
      end;

      /* Добавить строчку в массив. */
      macro AddLine(Sum_Currency, Kind, Sum, Currency)

         var
            Counter = 0;

         while( ( Counter < this.Size ) and 
                (this.(Counter).Currency != Sum_Currency )
              )
            Counter = Counter + 1;
         end;

         if( this.Size > Counter )
            if( ( this.(Counter).Currency == Sum_Currency ) AND MoveInORCB(Kind) )
                this.(Counter).InSum = this.(Counter).InSum + Sum;
            elif( ( this.(Counter).Currency == Sum_Currency ) AND (MoveInORCB(Kind) == false) )
                this.(Counter).OutSum = this.(Counter).OutSum + Sum;
            else
                Currency.AddLine(Sum_Currency);
                this.(Counter) = FactSumData( Sum_Currency, IIF(MoveInORCB(Kind),Sum,0), IIF(MoveInORCB(Kind) == false,Sum,0) );
            end;
         else
            Currency.AddLine(Sum_Currency);
            this.(Counter) = FactSumData( Sum_Currency, IIF(MoveInORCB(Kind),Sum,0), IIF(MoveInORCB(Kind) == false,Sum,0) );
         end;

      end; /*AddLine*/
end; 

private macro CompareForSort( key, elem )
   if( key.BankRest > elem.BankRest )           return -1;   end;
   if( key.BankRest < elem.BankRest )           return 1;  end;
   return 0;
end;

private class (TArray) TDataPartySum

      /* Очистить массив. */
   macro ClearArray
      this.Size = 0;
   end;

   macro GetBankAccRest(ClientContr,Curr,CommDate)
      record Acc(account); 
      var FD_Contr, Ribnk = $0;

      ClearRecord( Acc );
      FD_Contr = SPFirstDocContract( DL_STOCKCONTR, ClientContr );
      FD_Contr.SetParametr( MC_TYPE_PARAMETR_FIID, Curr );
      
      if( not FD_Contr.IsExistAccount( "ДС Клиента, ВУ", null, true, null, Acc, CommDate, null, Curr ) )
      end;

      return GetRestAccount( Acc, CommDate );
   end;

   /* Добавить строчку в массив. */
   macro AddLine(Data,Currency,CommDate)

      var
         Counter = 0, DealAсс, Rest = $0;

      if( (IsBUY(GetOperationGroup(Data.DealType))) OR 
          (IsBUY(GetOperationGroup(Data.DealType)) AND IsREPO(GetOperationGroup(Data.DealType)) AND (Data.LegKind == LEG_KIND_DL_TICK)) OR
          (IsSALE(GetOperationGroup(Data.DealType)) AND IsREPO(GetOperationGroup(Data.DealType)) AND (Data.LegKind == LEG_KIND_DL_TICK_BACK)) OR
          (Data.OperType == 9)
        )
          DealAсс = Data.KredAcc;  /*buy*/
      else
          DealAсс = Data.DebAcc;   /*sale*/
      end;


      while( ( Counter < this.Size ) and 
             (this.(Counter).MarketAcc != DealAсс )
           )
         Counter = Counter + 1;
      end;

      if( this.Size > Counter )
         if( ( this.(Counter).MarketAcc == DealAсс ) )
             if( DealAсс == Data.KredAcc)  /*buy*/
                this.(Counter).PayedSum = this.(Counter).PayedSum + Data.Sum;
             else
                this.(Counter).ReceivedSum = this.(Counter).ReceivedSum + Data.Sum;
             end;
         else
             Currency.AddLine(Data.Curr);
             Rest = GetBankAccRest(Data.ContrID, Data.Curr, CommDate);
             this.(Counter) = PartySumData( DealAсс, IIF(Data.ClientID != -1, Data.ClientID, {OurBank}), Data.ContrID, Data.Curr, 
                                               IIF(DealAсс == Data.KredAcc,Data.Sum,0), IIF(DealAсс != Data.KredAcc,Data.Sum,0), Data.DealCode, Rest );
         end;
      else
          Currency.AddLine(Data.Curr);
          /*берем остаток только если он отрицательный, т.к. счет активный*/
          Rest = GetBankAccRest(Data.ContrID, Data.Curr, CommDate);
          this.(Counter) = PartySumData( DealAсс, IIF(Data.ClientID != -1, Data.ClientID, {OurBank}), Data.ContrID, Data.Curr, 
                                         IIF(DealAсс == Data.KredAcc,Data.Sum,0), IIF(DealAсс != Data.KredAcc,Data.Sum,0), Data.DealCode, Rest );
      end;

   end; /*AddLine*/


   /* Сортировка данных в массиве */
   macro Sorting
      qsort( this, "CompareForSort" );
   end;

end;

var FactSum       = TDataFactSum();
var PartySum      = TDataPartySum();
var Currency      = TDataCurrency();
var ClientsNoDeal = TDataClientsNoDeal();

private macro CalcFactMovingSumm(comm)
   var Select, DataSet;

   Select = RSDCommand(" Select * " +
                       "   From ddlsum_dbt " +
                       "  Where t_DocKind = ? " +
                       "    and t_DocID   = ? " +
                       "    and t_Kind IN (?, ?, ?, ?, ?, ?, ?, ?) "
                      ); 

   Select.addParam("", RSDBP_IN, comm.rec.DocKind);
   Select.addParam("", RSDBP_IN, comm.rec.DocumentID);

   Select.addParam("", RSDBP_IN, DLSUM_KIND_DLCOMM_IN_CLIRINGACC_NATCUR );
   Select.addParam("", RSDBP_IN, DLSUM_KIND_DLCOMM_IN_CLIRINGACC_CUR    );
   Select.addParam("", RSDBP_IN, DLSUM_KIND_DLCOMM_OUT_CLIRINGACC_NATCUR);
   Select.addParam("", RSDBP_IN, DLSUM_KIND_DLCOMM_OUT_CLIRINGACC_CUR   );
 
   Select.addParam("", RSDBP_IN, DLSUM_KIND_DLCOMM_FROM_OTHER_CLIRACC_NATCUR );
   Select.addParam("", RSDBP_IN, DLSUM_KIND_DLCOMM_FROM_OTHER_CLIRACC_CUR );
   Select.addParam("", RSDBP_IN, DLSUM_KIND_DLCOMM_TO_OTHER_CLIRACC_NATCUR );
   Select.addParam("", RSDBP_IN, DLSUM_KIND_DLCOMM_TO_OTHER_CLIRACC_CUR );

   Select.execute();

   DataSet = TRsbDataSet(Select);
   while (DataSet.MoveNext())
      FactSum.AddLine( int(DataSet.Currency), int(DataSet.Kind), money(DataSet.Sum), Currency );
   end;
end;
 
private macro GetOwnerSumm(WorkDate, CommDate,MarketSchemeID,PartyID,pCurr:integer)
   var Select, Data, vCurr = -1;

   if( pCurr != null )
      vCurr = pCurr;
   end;
                                                                                                          
   Select = RSDCommand("select linacc.t_KredAcc KredAcc, linacc.t_DebAcc DebAcc, linacc.t_Sum Sum, linacc.t_FIID Curr, " +
                       "       DECODE(linacc.t_documentkind,176,NVL((select leg.t_legkind from ddl_leg_dbt leg " +
                       "                                          where leg.t_ID = linacc.t_documentid),0),0) LegKind, " +
                       "       linacc.t_OperType OperType, tick.t_DealType DealType, tick.t_DealID DealID, " +
                       "       tick.t_DealCode DealCode, tick.t_ClientID ClientID, tick.t_ClientContrID ContrID " +
                       "  from ddlinacc_dbt linacc, ddl_tick_dbt tick " +
                       " where linacc.t_OperType in(20,22,23,9) " +
                       "   and linacc.t_fikind = ? " +
                       "   and linacc.t_chapter = 21 " +
                       "   and linacc.t_date = ? " +
                       "   and linacc.t_documentkind in(?,?,?) " +
                       "   and tick.t_DealID = Rsb_Secur.GetDealID(linacc.t_DocumentKind,linacc.t_DocumentID) " +
                       "   and tick.T_MARKETSCHEMEID = ? " +
                       "   and tick.T_MARKETID = ? " +
                       IIF(vCurr!=-1,"   and linacc.t_FIID = ? ","")+
                       "   and tick.t_ClientID > 0 "
                      );

   Select.addParam("", RSDBP_IN, FIKIND_CURRENCY);
   Select.addParam("", RSDBP_IN, CommDate);
   Select.addParam("", RSDBP_IN, DL_SECURITYDOC);
   Select.addParam("", RSDBP_IN, DL_SECURLEG);
   Select.addParam("", RSDBP_IN, DL_RETIREMENT);
   Select.addParam("", RSDBP_IN, MarketSchemeID);
   Select.addParam("", RSDBP_IN, PartyID);

   if( vCurr != -1 )
      Select.addParam("", RSDBP_IN, vCurr);
   end;

   Select.execute();

   Data = TRsbDataSet(Select);

   while( Data.MoveNext() )
      PartySum.AddLine( Data,Currency,WorkDate);
   end;

end;

/*собираем счета по клиентам, по котoрыим не было сделок за день*/
private macro GetClientsNoDeal(FD:SPFirstDocDLCOMM,pCurr:integer,OnDate:Date)
   var Select, Data, Query;

   Select = DL_RSDCommand();
                                              
   Query = " select ACCDOC.T_CLIENTCONTRID, ACCDOC.T_ACCOUNT, ACCDOC.T_CHAPTER, ACCDOC.T_CURRENCY           "+
           "   from dmcaccdoc_dbt accdoc, dmccateg_dbt categ, dsfcontr_dbt sfcontr                          "+
           "  where ACCDOC.T_CHAPTER             = ?                                                        "+
           "    and ACCDOC.T_CURRENCY            = ?                                                        "+
           "    and ACCDOC.T_CATID               = CATEG.T_ID                                               "+
           "    and CATEG.T_CODE                 = ?                                                        "+  
           "    and CATEG.T_LEVELTYPE            = 1                                                        "+
           "    and ACCDOC.T_Place               = ?                                                        "+
           "    and rsb_account.restall(ACCDOC.T_ACCOUNT,ACCDOC.T_CHAPTER,ACCDOC.T_CURRENCY,?) < 0          "+
           "    and ( select count(0)                                                                       "+
           "            from ddlinacc_dbt dlinacc, dmcaccdoc_dbt accdoc_1                                   "+
           "           where DLINACC.T_DATE                 = ?                                             "+
           "             and (DLINACC.T_DEBACC = accdoc_1.T_ACCOUNT or DLINACC.T_KREDACC = accdoc_1.T_ACCOUNT)  "+
           "             and accdoc_1.T_CHAPTER             = ACCDOC.T_CHAPTER                              "+
           "             and DLINACC.T_CHAPTER              = ACCDOC.T_CHAPTER                              "+
           "             and DLINACC.T_FIID                 = ACCDOC.T_CURRENCY                             "+
           "             and accdoc_1.T_CURRENCY            = ACCDOC.T_CURRENCY                             "+
           "             and accdoc_1.T_CATID               = CATEG.T_ID                                    "+
           "             and ACCDOC_1.T_PLACE               = ACCDOC_1.T_MARKETPLACEID                      "+
           "             and accdoc_1.T_MARKETPLACEID       = ?                                             "+
           "             and accdoc_1.T_MARKETPLACEOFFICEID = ?                                             "+
           "             and ACCDOC_1.T_CLIENTCONTRID       = ACCDOC.T_CLIENTCONTRID                        "+
           "        ) = 0                                                                                   "+
           "    and sfcontr.t_ID = ACCDOC.T_CLIENTCONTRID                                                   "+
           "    and sfcontr.t_servkind = ?                                                                  "+
           "    and sfcontr.t_DateBegin <= ? " +
           "    and (sfcontr.t_DateClose >= ? or sfcontr.t_DateClose = TO_DATE('01.01.0001','DD.MM.YYYY'))  " +
           "  group by ACCDOC.T_CLIENTCONTRID, ACCDOC.T_ACCOUNT, ACCDOC.T_CHAPTER, ACCDOC.T_CURRENCY        "+
           "  order by abs(rsb_account.restall(ACCDOC.T_ACCOUNT,ACCDOC.T_CHAPTER,ACCDOC.T_CURRENCY,?)) desc ";

   Select.addParam( 21);
   Select.addParam( pCurr);
   Select.addParam( "ДС Клиента, ВУ");
   Select.addParam( {OurBank});

   Select.addParam( OnDate);
   Select.addParam( OnDate);

   Select.addParam( FD.comm.rec.TraderID);
   Select.addParam( FD.comm.rec.PartyOfficeID);

   Select.addParam( PTSK_STOCKDL );

   Select.addParam( OnDate);
   Select.addParam( OnDate);

   Select.addParam( OnDate);

   Data = Select.execute(Query);

   while( Data.MoveNext() )
      ClientsNoDeal.AddLine( Data.CLIENTCONTRID,Data.Account );
   end;

end;

/*собираем счета по клиентам в общую очередь
  при этом все равно были операции за день или нет
  сортируем по остатку на счете
*/
private macro GetAllClients(FD:SPFirstDocDLCOMM,pCurr:integer,OnDate:Date)
   var Select, Data, Query;

   Select = DL_RSDCommand();
                                              
   Query = " select ACCDOC.T_CLIENTCONTRID, ACCDOC.T_ACCOUNT, ACCDOC.T_CHAPTER, ACCDOC.T_CURRENCY           "+
           "   from dmcaccdoc_dbt accdoc, dmccateg_dbt categ, dsfcontr_dbt sfcontr                          "+
           "  where ACCDOC.T_CHAPTER               = ?                                                      "+
           "    and ACCDOC.T_CURRENCY              = ?                                                      "+
           "    and ACCDOC.T_CATID                 = CATEG.T_ID                                             "+
           "    and CATEG.T_CODE                   = ?                                                      "+  
           "    and CATEG.T_LEVELTYPE              = 1                                                      "+
           "    and ACCDOC.T_PLACE                 = ACCDOC.T_MARKETPLACEID                                 "+
           "    and accdoc.T_MARKETPLACEID         = ?                                                      "+
           "    and accdoc.T_MARKETPLACEOFFICEID   = ?                                                      "+
           "    and rsb_account.restall(ACCDOC.T_ACCOUNT,ACCDOC.T_CHAPTER,ACCDOC.T_CURRENCY,?) < 0          "+
           "    and ACCDOC.T_CLIENTCONTRID > 0                                                              "+
           "    and sfcontr.t_ID = ACCDOC.T_CLIENTCONTRID                                                   "+
           "    and sfcontr.t_servkind = ?                                                                  "+
           "    and sfcontr.t_DateBegin <= ? " +
           "    and (sfcontr.t_DateClose >= ? or sfcontr.t_DateClose = TO_DATE('01.01.0001','DD.MM.YYYY'))  " +
           "  group by ACCDOC.T_CLIENTCONTRID, ACCDOC.T_ACCOUNT, ACCDOC.T_CHAPTER,ACCDOC.T_CURRENCY         "+
           "  order by abs(rsb_account.restall(ACCDOC.T_ACCOUNT,ACCDOC.T_CHAPTER,ACCDOC.T_CURRENCY,?)) desc ";

   Select.addParam( 21);
   Select.addParam( pCurr);
   Select.addParam( "ДС Клиента, ВУ");

   Select.addParam( FD.comm.rec.TraderID);
   Select.addParam( FD.comm.rec.PartyOfficeID);

   Select.addParam( OnDate);

   Select.addParam( PTSK_STOCKDL );

   Select.addParam( OnDate);
   Select.addParam( OnDate );

   Select.addParam( OnDate);

   Data = Select.execute(Query);

   while( Data.MoveNext() )
      ClientsNoDeal.AddLine( Data.CLIENTCONTRID, Data.Account );
   end;

end;

private macro ПроводкаПоКлиенту( FD, ClientContr, MarketAcc, BankAcc, Currency, Summ, Doc, IsIn:bool )
   var FD_Contr, RoleKind;
   record Acc(account); 

   var DebAcc, KredAcc;

   FD.SetParametr( MC_TYPE_PARAMETR_FIID, Currency );

   FD_Contr = SPFirstDocContract( DL_STOCKCONTR, ClientContr );
   FD_Contr.SetParametr( MC_TYPE_PARAMETR_FIID, Currency );

   if( BankAcc == null )
      ClearRecord( Acc );
      if( not FD_Contr.IsExistAccount( "ДС Клиента, ВУ", null, true, null, Acc, FD.comm.rec.CommDate, null, Currency ) )
         if( not FD_Contr.OpenAccount( "ДС Клиента, ВУ", null, null, null, Acc, FD.comm.rec.CommDate, Currency ) )
            return false;
         end;
      end;
      BankAcc = Acc;
   end;

   if( MarketAcc == null )
      FD_Contr.SetParametr( MC_TYPE_PARAMETR_PLACE, FD.comm.rec.TraderID );
      FD_Contr.SetParametr( MC_TYPE_PARAMETR_PARTY, FD.comm.rec.PartyID );

      FD_Contr.SetParametr( MC_TYPE_PARAMETR_MARKET_PLACE, FD.comm.rec.TraderID );
      FD_Contr.SetParametr( MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE, FD.comm.rec.PartyOfficeID );

      ClearRecord( Acc );
      if( not FD_Contr.IsExistAccount( "ДС Клиента, ВУ", null, true, null, Acc, FD.comm.rec.CommDate, null, Currency ) )
         if( not FD_Contr.OpenAccount( "ДС Клиента, ВУ", null, null, null, Acc, FD.comm.rec.CommDate, Currency ) )
            return false;
         end;
      end;

      MarketAcc = Acc;
   end;

   if( IsIn == true )  /*зачисление на счет Клиента*/
      RoleKind = 2;
      KredAcc = BankAcc;/*здесь буфер счета*/
      DebAcc = MarketAcc;/*здесь буфер счета*/
   else
      RoleKind = 4;
      KredAcc = MarketAcc;/*здесь буфер счета*/
      DebAcc = BankAcc;/*здесь буфер счета*/
   end;
/*   
   if( not ПроводкаПоКатегориямУчета( FD, 
                                      DebAcc, 
                                      KredAcc,
                                      FD.comm.rec.CommDate,
                                      21,
                                      Currency,
                                      Summ,
                                      Doc,
                                      INPCARRY,
                                      " 1",
                                      FD.comm.rec.CommCode,
                                      FD.ВУ_ОснованиеПроводки( RoleKind ),
                                      0,
                                      null, null,
                                      Currency, Currency,
                                      null, null, NULL, null,
                                      true,
                                      RoleKind
                                    ) )
       return false;
    end;
*/
    return true;
end;

private macro ПроводкиВУВводаВыводаДС(FD:SPFirstDocDLCOMM,Doc)
   var i = 0, j = 0, k = 0;
   var Si2, Siofr, Siof = 0, Si1, Riex, D;
   var FactSumSize, PartySumSize, CurrencySize, ClientsNoDealSize, FD_Contr, Ribnk = 0, Si3 = 0;
   var AccBuf = TRecHandler("account");

   FactSum.ClearArray();

   PartySum.ClearArray();
   Currency.ClearArray();

   /* 1.Рассчитать фактические суммы переводов*/
   CalcFactMovingSumm(FD.comm);

   /* 2.  Получить суммы по владельцам */
   GetOwnerSumm(GetDateAfterWorkDays(FD.comm.rec.CommDate, -1, FD.ПолучитьКалендСвязанный() ), FD.comm.rec.CommDate,FD.GetMarketSchemeID(),FD.comm.rec.PartyID);
   /*3.  Выполняем проводки по переводу на биржу*/
   FactSumSize = FactSum.Size;

   PartySumSize = PartySum.Size;
   CurrencySize = Currency.Size;

   PartySum.Sorting;

   while( k < CurrencySize )

      Siof = 0;

      while(i < FactSumSize)

         if( (Currency[k].Curr == FactSum[i].Currency) AND (FactSum[i].InSum != 0) ) 
            Siof = FactSum[i].InSum;
            break;
         end;

         i = i + 1;
      end;

      Siofr = Siof;
      j = 0;

      /*выполняем проводки по клиентам, сначала по тем, по которым были сделки за день*/
      while( j < PartySumSize )
         if( PartySum[j].Currency == Currency[k].Curr )
            if( PartySum[j].PayedSum > PartySum[j].ReceivedSum )
               Si1 = PartySum[j].PayedSum - PartySum[j].ReceivedSum;
               ClearRecord( AccBuf );
               if( GetAccount( 21, Currency[k].Curr, PartySum[j].MarketAcc, AccBuf ) )/*по 21 Chapter*/
                  /*остаток на счете PartySum.MarketAcc на конец предыдущего раб. дня*/
                  /*берем остаток только если он отрицательный, т.к. счет активный*/
                  Riex = max(0,-GetRestAccount( AccBuf.rec, GetDateAfterWorkDays(FD.comm.rec.CommDate, -1, FD.ПолучитьКалендСвязанный()) ));

                  if( Si1 > Riex )/*этого условия не было в ТЗ, но было в коде раньше и оно было верным*/
                     Si2 = Si1 - Riex;

                     Siofr = Siofr - Si1;
                    
                     if( Siofr > 0 )
                    
                        Ribnk = max(0,-PartySum[j].BankRest);
                    
                        Si3 = min(Siofr,Ribnk);
                        Siofr = Siofr - Si3;
                        Si3 = Si3 + Si2;
                        /*Выполнить проводку по  зачислению на счет клиента*/
                        if( not ПроводкаПоКлиенту( FD, PartySum[j].ClientContr, AccBuf, null, Currency[k].Curr, Si3, Doc, true ) )
                           return false;
                        end;
                     else
                        Siofr = 0;
                        /* Выполнить проводку по  зачислению на счет клиента*/
                        if( Si2 > 0 )
                           if( not ПроводкаПоКлиенту( FD, PartySum[j].ClientContr, AccBuf, null, Currency[k].Curr, Si2, Doc, true ) )
                              return false;
                           end;
                        end;
                     end;
                  end;

                  if( Siofr == 0 )
                     break;
                  end;

               end; /* GetAccount */
            end; /* PartySum[j].PayedSum > PartySum[j].ReceivedSum */
         end; /*(PartySum[j].Currency == Currency) AND (PartySum[j].Party != {OurBank})*/
         j = j + 1;
      end; /*while по j*/
      /*вторая очередь клиентов, по которым не было сделок за день*/

      if( Siofr > 0 )
         ClientsNoDeal.ClearArray();
         GetClientsNoDeal(FD,Currency[k].Curr,GetDateAfterWorkDays(FD.comm.rec.CommDate, -1, FD.ПолучитьКалендСвязанный()));
         ClientsNoDealSize = ClientsNoDeal.Size;

         j = 0;

         while(j < ClientsNoDealSize)
            ClearRecord( AccBuf );
            if( GetAccount( 21, Currency[k].Curr, ClientsNoDeal[j].Account, AccBuf, true ) ) 
                  /*берем остаток только если он отрицательный, т.к. счет активный*/
                Ribnk = max(0,-GetRestAccount( AccBuf.rec, GetDateAfterWorkDays(FD.comm.rec.CommDate, -1, FD.ПолучитьКалендСвязанный()) ));
               
                Si3 = min(Siofr,Ribnk);
                Siofr = Siofr - Si3;
                /*Выполнить проводку по  зачислению на счет клиента*/
                if( not ПроводкаПоКлиенту( FD, ClientsNoDeal[j].ClientContr, null, AccBuf, Currency[k].Curr, Si3, Doc, true ) )
                   return false;
                end;
            end;
            if( Siofr == 0 )
               break;
            end;
            j = j + 1;
         end;

      end;

      k = k +1;
   end; /* while */

   /* 4. Выполняем проводки по выводу с биржи*/
   i = j = k = 0;

   while( k < CurrencySize )

      Siof = 0;

      while(i < FactSumSize)

         if( (Currency[k].Curr == FactSum[i].Currency) AND (FactSum[i].OutSum != 0) ) 
            Siof = FactSum[i].OutSum;
            break;
         end;

         i = i + 1;
      end;
                                            
      Siofr = Siof;                         
      j = 0;

      if( Siofr > 0 )
         ClientsNoDeal.ClearArray();
         GetAllClients(FD,Currency[k].Curr,FD.comm.rec.CommDate);
         ClientsNoDealSize = ClientsNoDeal.Size;
       
         /*выполняем проводки по клиентам общей очереди*/
         while( j < ClientsNoDealSize )
            ClearRecord( AccBuf );
            if( GetAccount( 21, Currency[k].Curr, ClientsNoDeal[j].Account, AccBuf ) )/*по 21 Chapter по счету с МХ = БИРЖА*/
                  /*берем остаток только если он отрицательный, т.к. счет активный*/
               Si2 = max(0,-GetRestAccount( AccBuf.rec, FD.comm.rec.CommDate ));
               if( Si2 != 0 )
                  Si3 = min(Siofr,Si2);
                  Siofr = Siofr - Si3;
                  /*Выполнить проводку по  списанию со счета клиента*/
                  if( not ПроводкаПоКлиенту( FD, ClientsNoDeal[j].ClientContr, AccBuf, null, Currency[k].Curr, Si3, Doc, false ) )
                     return false;
                  end;
               end;
            end;
            if( Siofr == 0 )
               break;
            end;
            j = j + 1;
         end;
      end;

      k = k + 1;
   end; /* while */

   return true;
end;

PRIVATE MACRO SayError( RslErrObj )
   MsgBox( RslErrObj.Module + " строка " + RslErrObj.Line + "|" + RslErrObj.Message);
   return 1;
END;

macro ExecuteStep( Doc, adr, DocKind, ID_Operation, ID_Step )

   record comm( dl_comm );
   var    FD;

   if( not ВестиВнутреннийУчет())
      return 0;
   end;

   SetBuff( comm, adr );
   FD = SPFirstDocDLCOMM( comm );

   if( (FD.comm.rec.OperSubKind == ALG_SP_MONEY_SOURCE_CLIENT) and (ВУ_БОЦБПроводкиВводаВыводаДСНаОРЦБ() == 2) )
      if( not ПроводкиВУВводаВыводаДС(FD,Doc) )
         return 1;
      end;
   end;

   return 0;
end;

