/*Загрузка курсов из РТС для БОЦБ, XLS формат*/
IMPORT "SCImpRate.mac";

PRIVATE VAR   FlagCtrlBrk = false;
PRIVATE CONST CtrlBrkErr  = 17;

PRIVATE CONST Код_на_РТС_ФинИн   = 12;
PRIVATE CONST Код_на_РТС_Субъект = 31;

PRIVATE CONST КодРТС = "РТС";

PRIVATE CONST RATE_KIND_MARKET  = 1;  /*курс вида "Рыночная цена"         */        
PRIVATE CONST RATE_KIND_MIN     = 2;  /*курс вида "Минимальная цена"      */
PRIVATE CONST RATE_KIND_MAX     = 3;  /*курс вида "Максимальная цена"     */
PRIVATE CONST RATE_KIND_WA      = 4;  /*курс вида "Средневзвешенная цена" */
PRIVATE CONST RATE_KIND_NKD1SEC = 15; /*курс вида "НКД на одну бумагу"    */
PRIVATE CONST RATE_KIND_VOLUME  = 17; /*курс вида "Объём торгов"          */

PRIVATE CONST MOMENT          = "A",
              ISSUE_TYPE      = "D",
              ISSUE_CLASSIC   = "F",
              PRICE_CURRENCY  = "J",
              MAX_PRICE_AGGR  = "O",
              MIN_PRICE_AGGR  = "P",
              AVERAGE_AGGR    = "W",
              MARKET_PRICE    = "AO";

PRIVATE CONST NullCell = "Undefined";

PRIVATE CLASS (SC_ImportRate) SC_ImportRateRTS()

  PRIVATE CLASS DataRates()
    var moment          :date,
        issue_type      :string,
        issue_classic   :string,
        price_currency  :string,
        max_price_aggr  :double,
        min_price_aggr  :double,
        average_aggr    :double,
        market_price    :double;

    macro Init()
       moment = date(0,0,0);
       issue_classic = issue_type = price_currency = "";
       max_price_aggr = min_price_aggr = average_aggr = market_price = 0.0;
    end;

  end;

  PRIVATE VAR Rates = DataRates();

  MACRO strImpDateRTS():STRING
     var day,month,year;
     DateSplit( ImpDate(), day, month, year );

     year = year - 1900;

     if ( year >= 100 ) 
        year = year - 100; 
     end;

     return LZ(year,2) + LZ(month,2) + LZ(day,2);
  END;

  /* настраиваемый пользователем макрос формирования имен файла котировок */
  MACRO GetDataFileName( datafilepath:@string, fname:@string ):BOOL
     var ErrStr = "";

     datafilepath = GetRegImportDir( "RTS", @ErrStr );

     if( ErrStr != "" )
        MsgBox( ErrStr );
        return false;
     end;

     datafilepath = datafilepath + "QUOTES" + "\\";
     fname = strImpDateRTS() + "_2.xls";
     return true;
  END;

  PRIVATE MACRO RunImp( RateKind:INTEGER, RateValue:DOUBLE ):INTEGER
     VAR IsRelative, IsDominant, imp_date;

     /* Признак: цена задана в процентах от номинала */
     if(StrUpr(Rates.issue_type) == "B")
        IsRelative = true;
     else
        IsRelative = false;
     end;

     if( RateKind == RATE_KIND_MARKET ) /*вид котировки - рыночная цена, значит устанавливаем признак основного курса*/
        IsDominant = true;
     else
        IsDominant = false;
     end;   

     if( Rates.moment != date(0,0,0) )
        imp_date = Rates.moment;
     else
        imp_date = ImpDate();
     end;

     if( ImportOneCourse( Rates.issue_classic,     // Код ФИ (Базовый ФИ)
                          Код_на_РТС_ФинИн,        // Вид кода ФИ (FICK_...)
                          RateKind,                // Вид курса
                          IsRelative,              // Признак относительной котировки
                          IsDominant,              // Признак основного курса
                          false,                   // Признак обратной котировки
                          1,                       // Масштаб курса
                          4,                       // Количество значащих цифр
                          imp_date,                // Дата установки курса
                          Rates.price_currency,    // Код валюты котировки (Котируемый ФИ)
                          Код_на_РТС_ФинИн,        // Вид кода валюты котировки (FICK_...)
                          MarketCode(),            // Код торговой площадки
                          Код_на_РТС_Субъект,      // Вид кода субъекта торговой площадки (PTCK_...)
                          0,                       // Сектор торговой площадки
                          RateValue,               // Значение курса
                          null,                    // Возможность надежного определения справедливой стоимости
                          null                     // Режима торгов
                        ) == false )
        return 1;
     end;     
     return 0;

  OnError(ErObj)

     Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
     else
        RemProgress();
        RunError( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );
     end;
     return ErObj.Code;
  end;

  PRIVATE MACRO GetNameCell(Type:string, i:integer)
     return Type + string(i);
  END;

  PRIVATE MACRO OpenFileXls( xls ):bool
     if( (not xls) OR (not xls.Workbooks) OR (not xls.Workbooks.Open(DataFileName())) )
        return false;
     end;
     return true;

     OnError( er )            
        return false;
  end;

  MACRO LoadFromFile():bool
     var xls:object = null, startAX:object = null,
         Book, Sheet;
 
     if (isStandAlone())
        xls = ActiveX("Excel.Application", null, false);
        if( not xls )
           return Error(string( "Невозможно создать объект Excel.Application" ) );
        end;
     else
        startAX = CreateObject("rsax", "TRsAxServer", "RTSRateImport", isStandalone());
        if( startAX )
           xls = startAX.CreateComObject("Excel.Application");
        end;

        if( (not startAX) OR (not xls) )
           return Error(string( "Невозможно создать объект Excel.Application" ) );
        end;
     end;

     /* откроем файл импорта */
     if( not OpenFileXls( xls ) )
        return Error(string( "Невозможно загрузить xls-документ \"" + DataFileName() + "\""));
     end;

     Book  = xls.Application.Workbooks.Item(1);
     Sheet = Book.Sheets("aggregated");

     var i:integer = 2;
     while(1)

        Rates.Init();

        if(string(Sheet.Range(GetNameCell(MOMENT, i)).value) != NullCell)
           Rates.moment = Sheet.Range(GetNameCell(MOMENT, i)).value;
        end;

        if(string(Sheet.Range(GetNameCell(ISSUE_TYPE, i)).value) != NullCell)
           Rates.issue_type = Sheet.Range(GetNameCell(ISSUE_TYPE, i)).value;
        end;

        if(string(Sheet.Range(GetNameCell(ISSUE_CLASSIC, i)).value) != NullCell)
           Rates.issue_classic = Sheet.Range(GetNameCell(ISSUE_CLASSIC, i)).value;
        end;

        if(string(Sheet.Range(GetNameCell(MAX_PRICE_AGGR, i)).value) != NullCell)
           Rates.max_price_aggr = Sheet.Range(GetNameCell(MAX_PRICE_AGGR, i)).value;
        end;

        if(string(Sheet.Range(GetNameCell(MIN_PRICE_AGGR, i)).value) != NullCell)
           Rates.min_price_aggr = Sheet.Range(GetNameCell(MIN_PRICE_AGGR, i)).value;
        end;

        if(string(Sheet.Range(GetNameCell(AVERAGE_AGGR, i)).value) != NullCell)
           Rates.average_aggr = Sheet.Range(GetNameCell(AVERAGE_AGGR, i)).value;
        end;

        if(string(Sheet.Range(GetNameCell(MARKET_PRICE, i)).value) != NullCell)
           Rates.market_price = Sheet.Range(GetNameCell(MARKET_PRICE, i)).value;
        end;

        if(string(Sheet.Range(GetNameCell(PRICE_CURRENCY, i)).value) != NullCell)
           Rates.price_currency = Sheet.Range(GetNameCell(PRICE_CURRENCY, i)).value;
        end;


        if(((Rates.moment          == date(0,0,0)) and
            (Rates.issue_type      == "")  and
            (Rates.issue_classic   == "")  and
            (Rates.price_currency  == "")  and
            (Rates.max_price_aggr  == 0.0) and
            (Rates.min_price_aggr  == 0.0) and
            (Rates.average_aggr    == 0.0) and
            (Rates.market_price    == 0.0)) or (i > MAXROWSHEET))
           break;
        else
           RunImp( RATE_KIND_MARKET, Rates.market_price   );
           RunImp( RATE_KIND_MIN,    Rates.min_price_aggr );
           RunImp( RATE_KIND_MAX,    Rates.max_price_aggr );
           RunImp( RATE_KIND_WA,     Rates.average_aggr   );
        end;
        i = i + 1;
     end;

     xls.Workbooks.Close();
     xls.Quit();

     return true;

  OnError( RslErrObj )
     RemProgress();RemProgress();
     MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
     RunError;
  END;

  initSC_ImportRate( true, КодРТС );
END;

PRIVATE VAR Rate = SC_ImportRateRTS();
if( Rate != NULL )
   Rate.Run();
end;

Exit(1);
