/*
$Name:        nutxfun.mac
$Module:      Ценные бумаги
$Description: Константы, общие функции НДЮН
*/
IMPORT CurrInter, FIInter, PaymInter, CTInter, SfInter, OprInter, DealsInter, BankInter, secinter, globals, Календарь;
import RsbDataSet, "cb_sql.mac", "Rcbconst.mac", "dldlngfun.mac", spserv;

/*Константы видов объектов НДР, по таблице dnutxkind_dbt*/
const NUTXOBJ_PLUS_25  = 1;  //Доход по имущественным правам
const NUTXOBJ_PLUS_10  = 2;  //Купон по государственным/муниципальным ц/б
const NUTXOBJ_PLUS_11  = 3;  //Купон по прочим облигациям
const NUTXOBJ_PLUS_13  = 4;  //Доход по акциям
const NUTXOBJ_PLUS_27  = 5;  //Доход по паям
const NUTXOBJ_DUETAX   = 6;  //Налог, подлежащий уплате
const NUTXOBJ_DUETAX1  = 7;  //Налог, подлежащий уплате
const NUTXOBJ_PAIDTAX  = 8;  //Удержанный налог
const NUTXOBJ_PLUS_01  = 9;  //Дивиденды
const NUTXOBJ_PLUS_15  = 10; //Доходы ПФИ
const NUTXOBJ_PLUS_26 = 11; //Полученная сумма штрафа по сделке

//Константы направлений
const NUTXOBJ_DIR_ALL = 0; // Не задано
const NUTXOBJ_DIR_IN  = 1; // Доход 
const NUTXOBJ_DIR_OUT = 2; // Расход

//Константы видов лотов
const NUTXLOTS_UNDEF     = 0; // не определено
const NUTXLOTS_BUY       = 1; // Покупка
const NUTXLOTS_SALE      = 2; // Продажа
const NUTXLOTS_REPO      = 3; // Репо прямое
const NUTXLOTS_BACKREPO  = 4; // Репо обратное
const NUTXLOTS_LOANPUT   = 5; // Займ размещение
const NUTXLOTS_LOANGET   = 6; // Займ привлечение

//Константы типов лотов
const NUTXDEAL_UNDEF     = 0; // не определено
const NUTXDEAL_REAL      = 1; // Реальная сделка
const NUTXDEAL_MARKET    = 2; // Виртуальная рыночного типа
const NUTXDEAL_CALC      = 3; // Виртуальная расчетная сделка
const NUTXDEAL_COMP      = 4; // Посткомпенсационный лот
                        
//Константы типов проис хождения лотов
const NUTXLOTORIGIN_DEAL = 0; // Сделка
const NUTXLOTORIGIN_GO   = 1; // ГО: Глобальная конвертация
const NUTXLOTORIGIN_IN   = 2; // ИН: Изменение номинала

//Константы типов связей
const NUTXLNK_UNDEF      = 0; // не определено
const NUTXLNK_DELIVER    = 1; // Поставка
const NUTXLNK_REPO       = 2; // Прямое Репо
const NUTXLNK_SUBSTREPO  = 3; // Подстановка Репо
const NUTXLNK_OPPOS      = 4; // Открытие короткой позиции
const NUTXLNK_CLPOS      = 5; // Закрытие короткой позиции

const NUTXBC_OBJKIND_LOT  = 1; //Лот
const NUTXBC_OBJKIND_LNK  = 2; //Связь
const NUTXBC_OBJKIND_LS   = 3; //Перевешивание связи
const NUTXBC_OBJKIND_TS   = 4; //Состояние лота
const NUTXBC_OBJKIND_OBJ  = 5; //Объект НДР

//Константы видов действий для истории сущностей НДЮН 
const NUTXBC_ACTION_CREATE = 0; //Создание
const NUTXBC_ACTION_UPDATE = 1; //Обновление
const NUTXBC_ACTION_DELETE = 2; //Удаление

//Группы налогового учета ц/б для юрлиц-нерезидентов
const NUTAXGROUP_10 = 1; //необлагаемые ц/б
const NUTAXGROUP_15 = 2; //льгота по 58-ФЗ (15% с купона)
const NUTAXGROUP_20 = 3; //облагаемые облигации
const NUTAXGROUP_40 = 4; //ПАИ
const NUTAXGROUP_50 = 5; //50% активов недвиж. имущ. в РФ
const NUTAXGROUP_60 = 6; //ПФИ
const NUTAXGROUP_70 = 7; // Векселя


//Проверить, является ли субъект нерезидентом для целей учета налогов юрлиц-нерезидентов
macro IsNeresidentForNUTX(PartyID:integer, CheckDate:date)
  var query, cmd, DataSet;
  var IsNeres = false;

  query = "select RSB_SECUR.IsTaxResident(?, ?) as IsResident from dual";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(PartyID);
  cmd.AddParam(CheckDate);

  DataSet = cmd.Execute();

  if((DataSet.moveNext()) and (DataSet.IsResident == 0))
    IsNeres = true;
  end;

  return IsNeres;
end;

//Проверить, является ли субъект юрлицом-нерезидентом для налогов
macro IsInstNeresForNUTX(PartyID:integer, CheckDate:date)
  var pt = TRecHandler("party.dbt");
  var IsInstNeres = false;

  if((ПолучитьСубъекта(PartyID, pt) == 0) and
     (pt.rec.LegalForm == legal_form_jur) AND 
     (IsNeresidentForNUTX(PartyID, CheckDate) == true)
    )
    IsInstNeres = true;
  end;

  return IsInstNeres; 
end;

//Получить группу налогового учета ц/б для юрлиц-нерезидентов
macro GetTaxGroupNUTX(FIID:integer, OnDate:date)
  var query, cmd, DataSet;
  var TaxGroup = 0;

  query = "select RSI_NUTX.GetNUTaxGroup(?, ?) as TaxGroup from dual";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(FIID);
  cmd.AddParam(OnDate);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    TaxGroup = DataSet.TaxGroup;
  end;

  return TaxGroup;
end;

//Проверить, является ли ц/б обращающейся
macro FIIfMarketNUTX(FIID:integer, OnDate:date)
  var fin = Trechandler( "fininstr.dbt" ); /* бумага*/
  var IfMarket = false;
  var NumInList = "";

  fin.Clear();
  fin.rec.FIID = FIID;

  if( GetMainObjAttr(null, OBJTYPE_AVOIRISS, UniID(fin, OBJTYPE_AVOIRISS), OBJGROUP_AVRNPTXCIRCULATE, null, null, NumInList, OnDate) )
    if(NumInList == "1")
      IfMarket = true;
    end;
  end;

  return IfMarket;
end;

//Получить фактического получателя дохода для юрлица-нерезидента
macro GetFactReceiverForNUTX(PartyID:integer) : integer
  var ReceiverID = PartyID;
  var query, cmd, DataSet;

  query = "select RSI_NUTX.GetFactReceiverForNUTX(?) as FactReceiverID from dual";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(PartyID);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    ReceiverID = int(DataSet.FactReceiverID);
  end;

  return ReceiverID;
end;

//Получить ставку налога
macro GetTaxRateNUTX(IncomeKind:integer, OnDate:date, ClientId:integer)
  var query, cmd, DataSet;
  var TaxRate = 0.0;

  if(ClientId == NULL)
    ClientId = 0;
  end;
  
  query = "select RSI_NUTX.GetNUTaxRate(?, ?, ?) as Rate from dual";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(IncomeKind);
  cmd.AddParam(OnDate);
  cmd.AddParam(ClientId);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    TaxRate = DataSet.Rate / 100.0;
  end;

  return TaxRate;
end;

CLASS TInsertTaxObjectNUTX(ID_Operation, ID_Step, DocKind, DocID, GrpID, TaxGroup)
  var m_TaxObjectsArr = TArray();
  var m_ID_Operation = ID_Operation;
  var m_ID_Step      = ID_Step;
  var m_DocKind      = DocKind;
  var m_DocID        = DocID;
  var m_GrpID        = GrpID;
  var m_TaxGroup     = TaxGroup;

  macro Clear()
    m_TaxObjectsArr.size = 0;
  end;

  macro Add(Date, 
            Client,
            Party,
            Direction,
            Level, 
            User,
            Kind,
            Sum,
            Cur,
            Sum0,
            AnaliticKind1,
            Analitic1,
            AnaliticKind2,
            Analitic2,
            AnaliticKind3,
            Analitic3,
            AnaliticKind4,
            Analitic4,
            AnaliticKind5,
            Analitic5,
            AnaliticKind6,
            Analitic6,
            Comment)

    var nutxobj = TRecHandler("nutxobj.dbt");

      nutxobj.Clear();

      nutxobj.rec.OBJID         = 0;
      nutxobj.rec.DATE          = Date;           
      nutxobj.rec.CLIENT        = Client;         
      nutxobj.rec.PARTY         = Party;
      nutxobj.rec.DIRECTION     = Direction;      
      nutxobj.rec.LEVEL         = Level;          
      nutxobj.rec.USER          = User;           
      nutxobj.rec.KIND          = Kind;           
      nutxobj.rec.SUM           = Sum;            
      nutxobj.rec.CUR           = Cur;
      if((Sum0 == NULL) or (Sum0 == 0))
        SmartConvertSum(nutxobj.rec.SUM0, nutxobj.rec.SUM, Date, Cur, NATCUR, false);
      else
        nutxobj.rec.SUM0        = sum0;
      end;  
      nutxobj.rec.ANALITICKIND1 = AnaliticKind1;  
      nutxobj.rec.ANALITIC1     = Analitic1;      
      nutxobj.rec.ANALITICKIND2 = AnaliticKind2;  
      nutxobj.rec.ANALITIC2     = Analitic2;      
      nutxobj.rec.ANALITICKIND3 = AnaliticKind3;  
      nutxobj.rec.ANALITIC3     = Analitic3;      
      nutxobj.rec.ANALITICKIND4 = AnaliticKind4;  
      nutxobj.rec.ANALITIC4     = Analitic4;      
      nutxobj.rec.ANALITICKIND5 = AnaliticKind5;
      nutxobj.rec.ANALITIC5     = Analitic5;      
      nutxobj.rec.ANALITICKIND6 = AnaliticKind6;  
      nutxobj.rec.ANALITIC6     = Analitic6;      
      nutxobj.rec.COMMENT       = Comment;        

      m_TaxObjectsArr[m_TaxObjectsArr.size] = nutxobj;
  end;

  private macro SetOprContext(ID_Operation, ID_Step, DocKind, DocID, GrpID)
    var sql, exec;
    
    sql =  "DECLARE "
         + "BEGIN "
         + "    RSI_NUTX.SetOprContext(?, ?, ?, ?, ?); "
         + "END; ";
     
    exec = RSDCommand(sql);

    exec.addParam( "", RSDBP_IN, ID_Operation );
    exec.addParam( "", RSDBP_IN, ID_Step );
    exec.addParam( "", RSDBP_IN, DocKind );
    exec.addParam( "", RSDBP_IN, DocID );
    exec.addParam( "", RSDBP_IN, GrpID );
    exec.execute();
  end;

  macro Save()
    var nutxobjCache = RsbSQLInsert("nutxobj.dbt");
    
    SetOprContext(m_ID_Operation, m_ID_Step, m_DocKind, m_DocID, m_GrpID);

    nutxobjCache.AddRecordArray(m_TaxObjectsArr);
          
    nutxobjCache.Insert();          

    SetOprContext(0, 0, 0, 0, 0);

    Clear();
  end;

  Clear();

END;

PRIVATE VAR TaxObj = NULL;


private macro СоздатьОбъектыНДРПоПолученномуНКД(NuTxKind, grp, fin, dl_comm, CommDate, Comment)
  var query, cmd, DataSet;
  var ReceiverID = GetFactReceiverForNUTX(grp.rec.Party);
  var err = 0;

  query =   " select /*+ INDEX( lot DNUTXLOT_DBT_IDXA)*/ "
          + "        RSI_RSB_FIInstr.ConvSum(lot.t_NKD, fin.t_FaceValueFI, td.t_PayFI, lot.t_SaleDate) as NKD, lot.t_DocID,  "
          + "        td.t_PayFI "
          + "   from dnutxlot_dbt lot, dv_sctotaldeals td, dfininstr_dbt fin "
          + "  where lot.t_SaleDate = ? "
          + "    and lot.t_Kind     = " + NUTXLOTS_SALE
          + "    and lot.t_Origin   = " + NUTXLOTORIGIN_DEAL
          + "    and lot.t_Client   = ? "
          + "    and lot.t_Contract = ? "
          + "    and lot.t_FIID     = ? "
          + "    and Exists(select 1 "
          + "                 from dnutxbc_dbt nubc "
          + "                where nubc.t_ObjKind = " + NUTXBC_OBJKIND_LOT
          + "                  and nubc.t_ObjID = lot.t_ID "
          + "                  and nubc.t_ID_Operation = ? "
          + "                  and nubc.t_ID_Step = ? "
          + "                  and nubc.t_DocKind = ? "
          + "                  and nubc.t_DocID = ? "
          + "                  and nubc.t_GrpID = ? "
          + "                  and nubc.t_Action = " + NUTXBC_ACTION_CREATE
          + "              )"
          + "    and td.t_DocKind = lot.t_DocKind "
          + "    and td.t_DocID   = lot.t_DocID "
          + "    and fin.t_FIID   = lot.t_FIID ";


  cmd = DL_RSDCommand(query);

  cmd.AddParam(CommDate);
  cmd.AddParam(grp.rec.Party);
  cmd.AddParam(grp.rec.Contract);
  cmd.AddParam(fin.rec.FIID);

  cmd.AddParam(TaxObj.m_ID_Operation);
  cmd.AddParam(TaxObj.m_ID_Step);
  cmd.AddParam(TaxObj.m_DocKind);
  cmd.AddParam(TaxObj.m_DocID);
  cmd.AddParam(grp.rec.ID);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())

    if(DataSet.NKD > 0)
      TaxObj.Add( CommDate,
                  grp.rec.Party,
                  ReceiverID,       
                  NUTXOBJ_DIR_IN,    
                  4,        
                  UNSET_CHAR,         
                  NuTxKind,         
                  DataSet.NKD,          
                  DataSet.PayFI,
                  null,          
                  TXOBJ_KIND1010, //Сделка              
                  DataSet.DocID,                        
                  0,                                    
                  -1,                                   
                  TXOBJ_KIND3010, //Ц/б                 
                  fin.rec.FIID,                         
                  0,                                    
                  -1,                                   
                  TXOBJ_KIND5010, //Налоговая группа    
                  TaxObj.m_TaxGroup,                            
                  TXOBJ_KIND6020, //Договор обслуживания
                  grp.rec.Contract,    
                  Comment          
                );
    end;
  end;

  return err;
end;

private macro СоздатьОбъектыНДРПоСвязям(NuTxKind, grp, fin, CommDate, Comment)
  var query, cmd, DataSet;
  var ReceiverID = GetFactReceiverForNUTX(grp.rec.Party);
  var S = 0;
  var err = 0;  
    
  //Продажи
  query =   " select lnk.t_Amount as QLink, lotS.t_Amount as QS, lotB.t_Amount as QB, tdS.t_PayFI, tdS.t_DocID, "
          + "        NVL((select SUM(RSI_RSB_FIInstr.ConvSum(rq.t_Amount, rq.t_FIID, tdS.t_PayFI, rq.t_FactDate)) "
          + "               from ddlrq_dbt rq "
          + "              where rq.t_DocKind = rqS.t_DocKind "
          + "                and rq.t_DocID = rqS.t_DocID "
          + "                and rq.t_DealPart = rqS.t_DealPart "
          + "                and rq.t_Type IN ("+DLRQ_TYPE_PAYMENT+", "+DLRQ_TYPE_AVANCE+", "+DLRQ_TYPE_DEPOSIT+")"
          + "                and rq.t_FactDate <= ? "
          + "            ), 0) as MainS, "
          + "        NVL((select SUM(RSI_RSB_FIInstr.ConvSum(rq.t_Amount, rq.t_FIID, tdS.t_PayFI, rq.t_FactDate)) "
          + "               from ddlrq_dbt rq "
          + "              where rq.t_DocKind = rqS.t_DocKind "
          + "                and rq.t_DocID = rqS.t_DocID "
          + "                and rq.t_DealPart = rqS.t_DealPart "
          + "                and rq.t_Type = "+DLRQ_TYPE_COMISS
          + "                and rq.t_FactDate <= ? "
          + "            ), 0) as ComS, "
          + "        NVL((select SUM(RSI_RSB_FIInstr.ConvSum(rq.t_Amount, rq.t_FIID, tdS.t_PayFI, rq.t_FactDate)) "
          + "               from ddlrq_dbt rq "
          + "              where rq.t_DocKind = rqB.t_DocKind "
          + "                and rq.t_DocID = rqB.t_DocID "
          + "                and rq.t_DealPart = rqB.t_DealPart "
          + "                and rq.t_Type IN ("+DLRQ_TYPE_PAYMENT+", "+DLRQ_TYPE_AVANCE+", "+DLRQ_TYPE_DEPOSIT+")"
          + "                and rq.t_FactDate <= ? "
          + "            ), 0) as MainB, "
          + "        NVL((select SUM(RSI_RSB_FIInstr.ConvSum(rq.t_Amount, rq.t_FIID, tdS.t_PayFI, rq.t_FactDate)) "
          + "               from ddlrq_dbt rq "
          + "              where rq.t_DocKind = rqB.t_DocKind "
          + "                and rq.t_DocID = rqB.t_DocID "
          + "                and rq.t_DealPart = rqB.t_DealPart "
          + "                and rq.t_Type = "+DLRQ_TYPE_COMISS
          + "                and rq.t_FactDate <= ? "
          + "            ), 0) as ComB "
          + "   from dnutxlnk_dbt lnk, dnutxlot_dbt lotS, dnutxlot_dbt lotB, ddlrq_dbt rqS, ddlrq_dbt rqB, dv_sctotaldeals tdS "
          + "  where lnk.t_Client = ? " 
          + "    and lnk.t_Contract = ? "
          + "    and lnk.t_FIID = ? "
          + "    and lnk.t_Type = " + NUTXLNK_DELIVER
          + "    and Exists(select 1 "
          + "                 from dnutxbc_dbt nubc "
          + "                where nubc.t_ObjKind = " + NUTXBC_OBJKIND_LNK
          + "                  and nubc.t_ObjID = lnk.t_ID "
          + "                  and nubc.t_ID_Operation = ? "
          + "                  and nubc.t_ID_Step = ? "
          + "                  and nubc.t_DocKind = ? "
          + "                  and nubc.t_DocID = ? "
          + "                  and nubc.t_GrpID = ? "
          + "                  and nubc.t_Action = " + NUTXBC_ACTION_CREATE
          + "              ) "
          + "    and lotS.t_ID = lnk.t_SaleID "
          + "    and rqS.t_ID = lotS.t_RQID "
          + "    and tdS.t_DocKind = rqS.t_DocKind "
          + "    and tdS.t_DocID = rqS.t_DocID "
          + "    and lotB.t_ID = lnk.t_BuyID "
          + "    and rqB.t_ID = lotB.t_RQID ";
    

  cmd = DL_RSDCommand(query);

  cmd.AddParam(CommDate);
  cmd.AddParam(CommDate);
  cmd.AddParam(CommDate);
  cmd.AddParam(CommDate);

  cmd.AddParam(grp.rec.Party);
  cmd.AddParam(grp.rec.Contract);
  cmd.AddParam(fin.rec.FIID);

  cmd.AddParam(TaxObj.m_ID_Operation);
  cmd.AddParam(TaxObj.m_ID_Step);
  cmd.AddParam(TaxObj.m_DocKind);
  cmd.AddParam(TaxObj.m_DocID);
  cmd.AddParam(grp.rec.ID);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())

    S = DataSet.QLink * ((DataSet.MainS - DataSet.ComS)/DataSet.QS - (DataSet.MainB - DataSet.ComB)/DataSet.QB);

    if(S > 0)
      TaxObj.Add( CommDate,         
                  grp.rec.Party,
                  ReceiverID,       
                  NUTXOBJ_DIR_IN,    
                  4,        
                  UNSET_CHAR,         
                  NuTxKind,         
                  S,          
                  DataSet.PayFI,          
                  null,
                  TXOBJ_KIND1010, //Сделка              
                  DataSet.DocID,                        
                  0,                                    
                  -1,                                   
                  TXOBJ_KIND3010, //Ц/б                 
                  fin.rec.FIID,                         
                  0,                                    
                  -1,                                   
                  TXOBJ_KIND5010, //Налоговая группа    
                  TaxObj.m_TaxGroup,                            
                  TXOBJ_KIND6020, //Договор обслуживания
                  grp.rec.Contract,    
                  Comment          
                );
    end;

  end;

  //Покупки
  query =   " select lnk.t_Amount as QLink, lotS.t_Amount as QS, lotB.t_Amount as QB, tdB.t_PayFI, tdB.t_DocID, "
          + "        NVL((select SUM(RSI_RSB_FIInstr.ConvSum(rq.t_Amount, rq.t_FIID, tdB.t_PayFI, rq.t_FactDate)) "
          + "               from ddlrq_dbt rq "
          + "              where rq.t_DocKind = rqS.t_DocKind "
          + "                and rq.t_DocID = rqS.t_DocID "
          + "                and rq.t_DealPart = rqS.t_DealPart "
          + "                and rq.t_Type IN ("+DLRQ_TYPE_PAYMENT+", "+DLRQ_TYPE_AVANCE+", "+DLRQ_TYPE_DEPOSIT+")"
          + "                and rq.t_FactDate <= ? "
          + "            ), 0) as MainS, "
          + "        NVL((select SUM(RSI_RSB_FIInstr.ConvSum(rq.t_Amount, rq.t_FIID, tdB.t_PayFI, rq.t_FactDate)) "
          + "               from ddlrq_dbt rq "
          + "              where rq.t_DocKind = rqS.t_DocKind "
          + "                and rq.t_DocID = rqS.t_DocID "
          + "                and rq.t_DealPart = rqS.t_DealPart "
          + "                and rq.t_Type = "+DLRQ_TYPE_COMISS
          + "                and rq.t_FactDate <= ? "
          + "            ), 0) as ComS, "
          + "        NVL((select SUM(RSI_RSB_FIInstr.ConvSum(rq.t_Amount, rq.t_FIID, tdB.t_PayFI, rq.t_FactDate)) "
          + "               from ddlrq_dbt rq "
          + "              where rq.t_DocKind = rqB.t_DocKind "
          + "                and rq.t_DocID = rqB.t_DocID "
          + "                and rq.t_DealPart = rqB.t_DealPart "
          + "                and rq.t_Type IN ("+DLRQ_TYPE_PAYMENT+", "+DLRQ_TYPE_AVANCE+", "+DLRQ_TYPE_DEPOSIT+")"
          + "                and rq.t_FactDate <= ? "
          + "            ), 0) as MainB, "
          + "        NVL((select SUM(RSI_RSB_FIInstr.ConvSum(rq.t_Amount, rq.t_FIID, tdB.t_PayFI, rq.t_FactDate)) "
          + "               from ddlrq_dbt rq "
          + "              where rq.t_DocKind = rqB.t_DocKind "
          + "                and rq.t_DocID = rqB.t_DocID "
          + "                and rq.t_DealPart = rqB.t_DealPart "
          + "                and rq.t_Type = "+DLRQ_TYPE_COMISS
          + "                and rq.t_FactDate <= ? "
          + "            ), 0) as ComB "
          + "   from dnutxlnk_dbt lnk, dnutxlot_dbt lotS, dnutxlot_dbt lotB, ddlrq_dbt rqS, ddlrq_dbt rqB, dv_sctotaldeals tdB "
          + "  where lnk.t_Client = ? " 
          + "    and lnk.t_Contract = ? "
          + "    and lnk.t_FIID = ? "
          + "    and lnk.t_Type = " + NUTXLNK_CLPOS
          + "    and Exists(select 1 "
          + "                 from dnutxbc_dbt nubc "
          + "                where nubc.t_ObjKind = " + NUTXBC_OBJKIND_LNK
          + "                  and nubc.t_ObjID = lnk.t_ID "
          + "                  and nubc.t_ID_Operation = ? "
          + "                  and nubc.t_ID_Step = ? "
          + "                  and nubc.t_DocKind = ? "
          + "                  and nubc.t_DocID = ? "
          + "                  and nubc.t_GrpID = ? "
          + "                  and nubc.t_Action = " + NUTXBC_ACTION_CREATE
          + "              ) "
          + "    and lotS.t_ID = lnk.t_SaleID "
          + "    and rqS.t_ID = lotS.t_RQID "
          + "    and lotB.t_ID = lnk.t_BuyID "
          + "    and rqB.t_ID = lotB.t_RQID "
          + "    and tdB.t_DocKind = rqB.t_DocKind "
          + "    and tdB.t_DocID = rqB.t_DocID ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(CommDate);
  cmd.AddParam(CommDate);
  cmd.AddParam(CommDate);
  cmd.AddParam(CommDate);

  cmd.AddParam(grp.rec.Party);
  cmd.AddParam(grp.rec.Contract);
  cmd.AddParam(fin.rec.FIID);

  cmd.AddParam(TaxObj.m_ID_Operation);
  cmd.AddParam(TaxObj.m_ID_Step);
  cmd.AddParam(TaxObj.m_DocKind);
  cmd.AddParam(TaxObj.m_DocID);
  cmd.AddParam(grp.rec.ID);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())

    S = DataSet.QLink * ((DataSet.MainS - DataSet.ComS)/DataSet.QS - (DataSet.MainB - DataSet.ComB)/DataSet.QB);

    if(S > 0)
      TaxObj.Add( CommDate,         
                  grp.rec.Party,
                  ReceiverID,       
                  NUTXOBJ_DIR_IN,    
                  4,        
                  UNSET_CHAR,         
                  NuTxKind,         
                  S,          
                  DataSet.PayFI,          
                  null,
                  TXOBJ_KIND1010, //Сделка              
                  DataSet.DocID,                        
                  0,                                    
                  -1,                                   
                  TXOBJ_KIND3010, //Ц/б                 
                  fin.rec.FIID,                         
                  0,                                    
                  -1,                                   
                  TXOBJ_KIND5010, //Налоговая группа    
                  TaxObj.m_TaxGroup,                            
                  TXOBJ_KIND6020, //Договор обслуживания
                  grp.rec.Contract,    
                  Comment          
                );
    end;

  end;

  return err;
end;

macro NUTX_BS_CreateObj_Plus_25(grp, CommDate, TaxGroup, dl_comm, fin)
  var pt = TRecHandler("party.dbt");
  var err = 0;

  if(FI_IsBond(fin.rec.FIID) and 
     (ПолучитьСубъекта(FI_GetIssuerOnDate(fin, CommDate), pt) == 0) and 
     (pt.rec.NotResident == UNSET_CHAR)
  )
    err = СоздатьОбъектыНДРПоПолученномуНКД(NUTXOBJ_PLUS_25, grp, fin, dl_comm, CommDate, "Доход по имущественным правам");

  elif((TaxGroup == NUTAXGROUP_40) or (TaxGroup == NUTAXGROUP_50))
    
    err = СоздатьОбъектыНДРПоСвязям(NUTXOBJ_PLUS_25, grp, fin, CommDate, "Доход по имущественным правам");

  end;

  return err;
end;

macro NUTX_BS_CreateObj_Plus_10(grp, CommDate, dl_comm, fin)
  var err = 0;

  err = СоздатьОбъектыНДРПоПолученномуНКД(NUTXOBJ_PLUS_10, grp, fin, dl_comm, CommDate, "Купон по государственным/муниципальным ц/б");

  return err;
end;

macro NUTX_BS_CreateObj_Plus_11(grp, CommDate, dl_comm, fin)
  var err = 0;

  err = СоздатьОбъектыНДРПоПолученномуНКД(NUTXOBJ_PLUS_11, grp, fin, dl_comm, CommDate, "Купон по прочим облигациям");

  return err;
end;

macro NUTX_BS_CreateObj_Plus_13(grp, CommDate, dl_comm, fin)
  var err = 0;

  err = СоздатьОбъектыНДРПоСвязям(NUTXOBJ_PLUS_13, grp, fin, CommDate, "Доход по акциям");

  return err;
end;

macro NUTX_BS_CreateObj_Plus_27(grp, CommDate, dl_comm, fin)
  var err = 0;

  err = СоздатьОбъектыНДРПоСвязям(NUTXOBJ_PLUS_27, grp, fin, CommDate, "Доход по паям");

  return err;
end;

macro NUTX_BS_CreateObj_DueTax(grp, CommDate, Rate, dl_comm, fin)
  var query, cmd, DataSet;
  var ReceiverID = GetFactReceiverForNUTX(grp.rec.Party);
  var err = 0;
  var S = 0;

  if((IsInstNeresForNUTX(ReceiverID, CommDate) == true) and (Rate != 0.0))

    query =   " select NVL(sum(nuobj.t_Sum), 0) as S, nuobj.t_Analitic1, nuobj.t_Cur"
            + "   from dnutxobj_dbt nuobj "
            + "  where nuobj.t_Direction = " + NUTXOBJ_DIR_IN
            + "    and nuobj.t_Date = ? "
            + "    and nuobj.t_Client = ? "
            + "    and nuobj.t_Kind IN ("+NUTXOBJ_PLUS_25+", "+NUTXOBJ_PLUS_10+", "+NUTXOBJ_PLUS_11+", "+NUTXOBJ_PLUS_13+", "+NUTXOBJ_PLUS_27+") "
            + "    and nuobj.t_AnaliticKind1 = " + TXOBJ_KIND1010
            + "    and nuobj.t_AnaliticKind3 = " + TXOBJ_KIND3010
            + "    and nuobj.t_Analitic3 = ? "
            + "    and nuobj.t_AnaliticKind5 = " + TXOBJ_KIND5010
            + "    and nuobj.t_Analitic5 = ? "
            + "    and nuobj.t_AnaliticKind6 = " + TXOBJ_KIND6020
            + "    and nuobj.t_Analitic6 = ? "
            + "    and Exists(select 1 "
            + "                 from dnutxbc_dbt nubc "
            + "                where nubc.t_ObjKind = " + NUTXBC_OBJKIND_OBJ
            + "                  and nubc.t_ObjID = nuobj.t_ObjID "
            + "                  and nubc.t_ID_Operation = ? "
            + "                  and nubc.t_ID_Step = ? "
            + "                  and nubc.t_DocKind = ? "
            + "                  and nubc.t_DocID = ? "
            + "                  and nubc.t_GrpID = ? "
            + "                  and nubc.t_Action = " + NUTXBC_ACTION_CREATE
            + "              ) "
            + " group by nuobj.t_Analitic1, nuobj.t_Cur ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(CommDate);
    cmd.AddParam(ReceiverID);
    cmd.AddParam(fin.rec.FIID);
    cmd.AddParam(TaxObj.m_TaxGroup);
    cmd.AddParam(grp.rec.Contract);

    cmd.AddParam(TaxObj.m_ID_Operation);
    cmd.AddParam(TaxObj.m_ID_Step);
    cmd.AddParam(TaxObj.m_DocKind);
    cmd.AddParam(TaxObj.m_DocID);
    cmd.AddParam(grp.rec.ID);

    DataSet = cmd.Execute();
    while(DataSet.moveNext())

      S = DataSet.S * Rate;

      if(S > 0)
        TaxObj.Add( CommDate,         
                    grp.rec.Party,
                    ReceiverID,       
                    NUTXOBJ_DIR_OUT,    
                    5,        
                    UNSET_CHAR,         
                    NUTXOBJ_DUETAX,         
                    S,          
                    DataSet.Cur,          
                    null,
                    TXOBJ_KIND1010, //Сделка              
                    DataSet.Analitic1,                        
                    0,                                    
                    -1,                                   
                    TXOBJ_KIND3010, //Ц/б                 
                    fin.rec.FIID,                         
                    0,                                    
                    -1,                                   
                    TXOBJ_KIND5010, //Налоговая группа    
                    TaxObj.m_TaxGroup,                            
                    TXOBJ_KIND6020, //Договор обслуживания
                    grp.rec.Contract,    
                    "Налог, подлежащий уплате"          
                  );
      end;

    end;
  end;
  
  return err;
end;

macro NUTX_REPO_CreateObj(grp, CommDate, Rate, dl_comm, fin)
  var query, cmd, DataSet;
  var ReceiverID = GetFactReceiverForNUTX(grp.rec.Party);
  var err = 0;
  var S = 0;

  query =   " select /*+ INDEX( lot DNUTXLOT_DBT_IDXA)*/ "
          + "        (select SUM(RSI_RSB_FIInstr.ConvSum(rqc.t_Amount, rqc.t_FIID, leg2.t_PayFIID, rqc.t_FactDate) * (case when rqc.t_Kind = "+DLRQ_KIND_REQUEST+" then 1 else -1 end)) "
          + "           from ddlrq_dbt rqc "
          + "          where rqc.t_DocKind = rq.t_DocKind "
          + "            and rqc.t_DocID   = rq.t_DocID "
          + "            and rqc.t_SubKind = " + DLRQ_SUBKIND_CURRENCY
          + "            and rqc.t_Type IN ("+DLRQ_TYPE_PAYMENT+", "
                                             +DLRQ_TYPE_INCREPO+", "
                                             +DLRQ_TYPE_AVANCE+", "
                                             +DLRQ_TYPE_DEPOSIT+", "
                                             +DLRQ_TYPE_COMPPAYM+", "
                                             +DLRQ_TYPE_PAYMREPOCOUP+", "
                                             +DLRQ_TYPE_PAYMREPOPART+") "
          + "        ) as Frepo, "
          + "        lot.t_DocID, Rsb_Secur.IsBuy(Opr.oGrp) IsBuy, "
          + "        leg2.t_PayFIID, leg2.t_IncomeRate, tk.t_ClientID, tk.t_PartyID "
          + "   from dnutxlot_dbt lot, ddlrq_dbt rq, ddl_tick_dbt tk, ddl_leg_dbt leg2,  "
          + "        (select t_Kind_Operation, rsb_secur.get_OperationGroup(t_SysTypes) oGrp " 
          + "           from doprkoper_dbt where t_DocKind = "+DL_SECURITYDOC+") Opr " 
          + "  where lot.t_Kind IN ("+NUTXLOTS_REPO+ ", "+NUTXLOTS_BACKREPO+") "
          + "    and lot.t_Origin   = " + NUTXLOTORIGIN_DEAL
          + "    and lot.t_Client   = ? "
          + "    and lot.t_Contract = ? "
          + "    and lot.t_FIID     = ? "
          + "    and lot.t_RetFlag  = 'X' "
          + "    and Exists(select 1 "
          + "                 from dnutxbc_dbt nubc "
          + "                where nubc.t_ObjKind = " + NUTXBC_OBJKIND_LOT
          + "                  and nubc.t_ObjID = lot.t_ID "
          + "                  and nubc.t_ID_Operation = ? "
          + "                  and nubc.t_ID_Step = ? "
          + "                  and nubc.t_DocKind = ? "
          + "                  and nubc.t_DocID = ? "
          + "                  and nubc.t_GrpID = ? "
          + "                  and nubc.t_Action = " + NUTXBC_ACTION_UPDATE
          + "              )"
          + "    and rq.t_ID        = lot.t_RQID "
          + "    and tk.t_BOfficeKind = rq.t_DocKind "
          + "    and tk.t_DealID    = rq.t_DocID "
          + "    and leg2.t_DealID  = tk.t_DealID "
          + "    and leg2.t_LegKind = " + LEG_KIND_DL_TICK_BACK
          + "    and leg2.t_LegID   = 0 "
          + "    and Opr.t_Kind_Operation = tk.t_DealType ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(grp.rec.Party);
  cmd.AddParam(grp.rec.Contract);
  cmd.AddParam(fin.rec.FIID);

  cmd.AddParam(TaxObj.m_ID_Operation);
  cmd.AddParam(TaxObj.m_ID_Step);
  cmd.AddParam(TaxObj.m_DocKind);
  cmd.AddParam(TaxObj.m_DocID);
  cmd.AddParam(grp.rec.ID);
  
  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    
    S = 0;

    if((DataSet.ClientID == grp.rec.Party) AND  
       (((DataSet.IncomeRate  > 0.0) and (DataSet.IsBuy == 1)) OR
        ((DataSet.IncomeRate  < 0.0) and (DataSet.IsBuy == 0)) OR
        ((DataSet.IncomeRate == 0.0) and (DataSet.IsBuy == 1) and (DataSet.FRepo > 0)) OR
        ((DataSet.IncomeRate == 0.0) and (DataSet.IsBuy == 0) and (DataSet.FRepo < 0))
       )
      )
      S = abs(DataSet.FRepo);
    elif((DataSet.PartyID == grp.rec.Party) AND  
       (((DataSet.IncomeRate  < 0.0) and (DataSet.IsBuy == 1)) OR
        ((DataSet.IncomeRate  > 0.0) and (DataSet.IsBuy == 0)) OR
        ((DataSet.IncomeRate == 0.0) and (DataSet.IsBuy == 1) and (DataSet.FRepo < 0)) OR
        ((DataSet.IncomeRate == 0.0) and (DataSet.IsBuy == 0) and (DataSet.FRepo > 0))
       )
      )
      S = abs(DataSet.FRepo);
    end;

    if(S > 0)
      TaxObj.Add( CommDate,         
                  grp.rec.Party,
                  ReceiverID,       
                  NUTXOBJ_DIR_IN,    
                  4,        
                  UNSET_CHAR,         
                  NUTXOBJ_PLUS_11,         
                  S,          
                  DataSet.PayFIID,          
                  null,
                  TXOBJ_KIND1010, //Сделка              
                  DataSet.DocID,                        
                  0,                                    
                  -1,                                   
                  TXOBJ_KIND3010, //Ц/б                 
                  fin.rec.FIID,                         
                  0,                                    
                  -1,                                   
                  TXOBJ_KIND5010, //Налоговая группа    
                  TaxObj.m_TaxGroup,                            
                  0, 
                  -1,    
                  "Проценты по РЕПО"          
                );

      if((IsInstNeresForNUTX(ReceiverID, CommDate) == true) and (Rate > 0))
        TaxObj.Add( CommDate,         
                    grp.rec.Party,
                    ReceiverID,       
                    NUTXOBJ_DIR_OUT,    
                    5,        
                    UNSET_CHAR,         
                    NUTXOBJ_DUETAX,         
                    S*Rate,          
                    DataSet.PayFIID,          
                    null,
                    TXOBJ_KIND1010, //Сделка              
                    DataSet.DocID,                        
                    0,                                    
                    -1,                                   
                    TXOBJ_KIND3010, //Ц/б                 
                    fin.rec.FIID,                         
                    0,                                    
                    -1,                                   
                    TXOBJ_KIND5010, //Налоговая группа    
                    TaxObj.m_TaxGroup,                            
                    0, 
                    -1,    
                    "Налог, подлежащий уплате"          
                  );
      end;
    end;
  end;

  //Учёт купона в РЕПО
  if(not err)
    var pt = TRecHandler("party.dbt");
    var NuTxKind = 0;
    var Comment = "";
     
    if(FI_IsBond(fin.rec.FIID) and 
       (ПолучитьСубъекта(FI_GetIssuerOnDate(fin, CommDate), pt) == 0) and 
       (pt.rec.NotResident == UNSET_CHAR)
      )
      if(  FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_STATE, fin.rec.AvoirKind ) //Государственная
        or FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_MUNICIPAL, fin.rec.AvoirKind ) //Муниципальная
        )
        NuTxKind = NUTXOBJ_PLUS_10;
        Comment  = "Купоны по государственным/муниципальным облигациям";
      elif(FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_CORPORATE, fin.rec.AvoirKind )) //Корпоративная
        NuTxKind = NUTXOBJ_PLUS_11;
        Comment  = "Купоны по прочим российским облигациям";
      end;
    end;

    query =   " select rq.t_Amount, rq.t_FIID, rq.t_DocID, rq.t_TaxRateSell, rq.t_FactReceiverID, tk.t_ClientID "
            + "   from ddlrq_dbt rq, ddl_tick_dbt tk,  "
            + "        (select t_Kind_Operation, rsb_secur.get_OperationGroup(t_SysTypes) oGrp " 
            + "           from doprkoper_dbt where t_DocKind = "+DL_SECURITYDOC+") Opr "
            + "  where rq.t_Type = " + DLRQ_TYPE_PAYMREPOCOUP
            + "    and rq.t_FactDate = ? "
            + "    and tk.t_DealID = rq.t_DocID "
            + "    and Exists(select 1 "
            + "                 from ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc "
            + "                where grdeal.t_DocKind = tk.t_BOfficeKind "
            + "                  and grdeal.t_DocID = tk.t_DealID "
            + "                  and grdeal.t_TemplNum = " + DLGR_TEMPL_COUP
            + "                  and grdeal.t_FIID = ? "
            + "                  and gracc.t_GrDealID = grdeal.T_ID "
            + "                  and gracc.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING 
            + "                  and gracc.t_State = " + DLGRACC_STATE_PLAN
            + "                  and Rsb_Secur.SC_GetRQByGrDeal(grdeal.T_ID) = rq.t_ID"
            + "              ) "
            + "   and tk.t_ClientID = ? and tk.t_ClientContrID = ? "
            + "   and Opr.t_Kind_Operation = tk.t_DealType "
            + "   and Rsb_Secur.IsSale(Opr.oGrp) = 1 ";
            
    cmd = DL_RSDCommand(query);

    cmd.AddParam(CommDate);
    cmd.AddParam(fin.rec.FIID);
    cmd.AddParam(grp.rec.Party);
    cmd.AddParam(grp.rec.Contract);

    if(dl_comm.rec.OperSubKind > 0)
      query = query + " and tk.t_DealType = ? ";
      cmd.AddParam(dl_comm.rec.OperSubKind);
    elif(dl_comm.rec.ContractKind > 0)
      if(dl_comm.rec.ContractKind == SC_ACCOPERTYPE_EXCHANGE)
        query = query + " AND RSB_SECUR.ISEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BOfficeKind))) = 1 ";
      elif(dl_comm.rec.ContractKind == SC_ACCOPERTYPE_OUTEXCHANGE)
        query = query + " AND RSB_SECUR.ISOUTEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BOfficeKind))) = 1 ";
      elif(dl_comm.rec.ContractKind == SC_ACCOPERTYPE_OTHER)
        query = query + " AND RSB_SECUR.ISEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BOfficeKind))) = 0 "
                      + " AND RSB_SECUR.ISOUTEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BOfficeKind))) = 0 ";
      end;
    end;

    DataSet = cmd.Execute();
    while(DataSet.moveNext())

      if(NuTxKind > 0)
        TaxObj.Add( CommDate,         
                    grp.rec.Party,
                    DataSet.FactReceiverID,       
                    NUTXOBJ_DIR_IN,    
                    4,        
                    UNSET_CHAR,         
                    NuTxKind,         
                    DataSet.Amount,          
                    DataSet.FIID,          
                    null,
                    TXOBJ_KIND1010, //Сделка              
                    DataSet.DocID,                        
                    0,                                    
                    -1,                                   
                    TXOBJ_KIND3010, //Ц/б                 
                    fin.rec.FIID,                         
                    0,                                    
                    -1,                                   
                    TXOBJ_KIND5010, //Налоговая группа    
                    TaxObj.m_TaxGroup,                            
                    0, 
                    -1,    
                    Comment          
                  );
      end;

      if(IsInstNeresForNUTX(DataSet.FactReceiverID, CommDate) == true)
        if((TaxObj.m_TaxGroup == NUTAXGROUP_15) or (TaxObj.m_TaxGroup == NUTAXGROUP_20))
          if(DataSet.TaxRateSell)
            TaxObj.Add( CommDate,         
                        grp.rec.Party,
                        DataSet.FactReceiverID,       
                        NUTXOBJ_DIR_OUT,    
                        5,        
                        UNSET_CHAR,         
                        NUTXOBJ_DUETAX1,         
                        DataSet.Amount * DataSet.TaxRateSell / 100.0,          
                        DataSet.FIID,          
                        null,
                        TXOBJ_KIND1010, //Сделка              
                        DataSet.DocID,                        
                        0,                                    
                        -1,                                   
                        TXOBJ_KIND3010, //Ц/б                 
                        fin.rec.FIID,                         
                        0,                                    
                        -1,                                   
                        TXOBJ_KIND5010, //Налоговая группа    
                        TaxObj.m_TaxGroup,                            
                        0, 
                        -1,    
                        "Налог, подлежащий уплате"          
                      );
          end;
        end;
      end;
    end;
  end;

  if(not err)
    TaxObj.Save();
  end;

  return err;
end;

private macro NUTX_CreateObjByGrpFI(dl_comm, grp, fin, CommDate)
  var pt = TRecHandler("party.dbt");
  var err = 0;
  var ЯвлИмущПравами = 0;
  var TaxGroup = 0, Rate = 0.0;
    
  ЯвлИмущПравами = ПолучитьЗначениеКатегории(fin, 59/*Является имущественными правами*/, OBJTYPE_AVOIRISS, CommDate);

  TaxGroup = GetTaxGroupNUTX(fin.rec.FIID, CommDate);

  TaxObj = TInsertTaxObjectNUTX(0, 0, dl_comm.rec.DocKind, dl_comm.rec.DocumentID, grp.rec.ID, TaxGroup);
  
  //Покупки и продажи
  if(TaxGroup != NUTAXGROUP_10)

    if(ЯвлИмущПравами == 1 /*Да*/)
      err = NUTX_BS_CreateObj_Plus_25(grp, CommDate, TaxGroup, dl_comm, fin);
    else
      if(FI_IsBond(fin.rec.FIID) and 
         (ПолучитьСубъекта(FI_GetIssuerOnDate(fin, CommDate), pt) == 0) and 
         (pt.rec.NotResident == UNSET_CHAR)
      )

        if(  FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_STATE, fin.rec.AvoirKind ) //Государственная
          or FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND_MUNICIPAL, fin.rec.AvoirKind ) //Муниципальная
          )
          err = NUTX_BS_CreateObj_Plus_10(grp, CommDate, dl_comm, fin);
        else
          err = NUTX_BS_CreateObj_Plus_11(grp, CommDate, dl_comm, fin);
        end;
      elif((TaxGroup == NUTAXGROUP_50) or (TaxGroup == NUTAXGROUP_40))
        
        if(TaxGroup == NUTAXGROUP_50)
          err = NUTX_BS_CreateObj_Plus_13(grp, CommDate, dl_comm, fin);
        elif(TaxGroup == NUTAXGROUP_40)
          err = NUTX_BS_CreateObj_Plus_27(grp, CommDate, dl_comm, fin);
        end;
      end;
    end; 

    if(not err)
      err = TaxObj.Save();
    end;
    
    if(not err)
      if(FI_IsBond(fin.rec.FIID) and 
         (ПолучитьСубъекта(FI_GetIssuerOnDate(fin, CommDate), pt) == 0) and 
         (pt.rec.NotResident == UNSET_CHAR)
      )
        if(TaxGroup == NUTAXGROUP_20)
          Rate = GetTaxRateNUTX(INCOME_KIND_PERC, CommDate, grp.rec.Party);
        elif(TaxGroup == NUTAXGROUP_15)
          var IfMarket = FIIfMarketNUTX(fin.rec.FIID, CommDate);
          if(IfMarket)
            Rate = GetTaxRateNUTX(INCOME_KIND_PRIVPERC, CommDate, grp.rec.Party);
          else
            Rate = GetTaxRateNUTX(INCOME_KIND_PERC, CommDate, grp.rec.Party);
          end;
        end;
      elif((TaxGroup == NUTAXGROUP_50) or (TaxGroup == NUTAXGROUP_40))
        Rate = GetTaxRateNUTX(INCOME_KIND_OTHER, CommDate, grp.rec.Party);
      end;

      err = NUTX_BS_CreateObj_DueTax(grp, CommDate, Rate, dl_comm, fin);

      if(not err)
        TaxObj.Save();
      end;
    end;
  end;
  
  //РЕПО
  if(not err)
    err = NUTX_REPO_CreateObj(grp, CommDate, GetTaxRateNUTX(INCOME_KIND_OTHER, CommDate, grp.rec.Party), dl_comm, fin);
  end;

  return err;
end;

macro NUTX_CreateObjByGrp(DocKind, DocumentID, GrpID, CommDate)
  var grp = TBFile("pmwrtgrp.dbt");
  var dl_comm = TBFile("dl_comm.dbt");
  var fin = TRecHandler("fininstr.dbt");
  var query, cmd, DataSet;
  var err = 0;
  
  grp.Clear();
  grp.rec.ID = GrpID;

  dl_comm.Clear();
  dl_comm.rec.DocumentID = DocumentID;
  
  if(grp.GetEQ() and dl_comm.GetEQ())
    query =   " select fin.* "
            + "  from dpmwrtgrpfi_dbt grpfi, dfininstr_dbt fin "
            + " where grpfi.t_GrpID = ? "
            + "   and fin.t_FIID = grpfi.t_FIID ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(GrpID);

    DataSet = cmd.Execute();
    
    while((not err) and (DataSet.moveNext()))
      DataSet.GetRecord().CopyTo(fin.rec);

      err = NUTX_CreateObjByGrpFI(dl_comm, grp, fin, CommDate);
    end;
  else
    msgbox("Не найдены параметры для формирования объектов НДР");
    err = 1;
  end;

  return err;
end;

macro NUTX_GetClientDealDueTax1(DealID:integer, PartyID:integer, Sum:@money, Sum0:@money)
  var query, cmd, DataSet;

  Sum  = $0;
  Sum0 = $0;

  query =   " select NVL(SUM(obj.t_Sum), 0) as DueTax, NVL(SUM(obj.t_Sum0), 0) as DueTax0 "
          + "   from dnutxobj_dbt obj"
          + "  where obj.t_Kind = " + NUTXOBJ_DUETAX1
          + "    and obj.t_Client = ? "
          + "    and obj.t_Party = ? "
          + "    and obj.t_AnaliticKind1 = " + TXOBJ_KIND1010
          + "    and obj.t_Analitic1 = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(PartyID);
  cmd.AddParam(GetFactReceiverForNUTX(PartyID));
  cmd.AddParam(DealID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    Sum  = DataSet.DueTax;
    Sum0 = DataSet.DueTax0;
  end;
end;

//Получить сумму налога к удержанию по сделке
macro NUTX_GetClientDealDueTax(DealID:integer, PartyID:integer, Sum:@money, Sum0:@money)
  var query, cmd, DataSet;

  Sum  = $0;
  Sum0 = $0;

  query =   " select NVL(SUM(obj.t_Sum), 0) as DueTax, NVL(SUM(obj.t_Sum0), 0) as DueTax0 "
          + "   from dnutxobj_dbt obj"
          + "  where obj.t_Kind = " + NUTXOBJ_DUETAX
          + "    and obj.t_Client = ? "
          + "    and obj.t_Party = ? "
          + "    and obj.t_AnaliticKind1 = " + TXOBJ_KIND1010
          + "    and obj.t_Analitic1 = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(PartyID);
  cmd.AddParam(GetFactReceiverForNUTX(PartyID));
  cmd.AddParam(DealID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    Sum  = DataSet.DueTax;
    Sum0 = DataSet.DueTax0;
  end;
end;

//Получить сумму налога TaxMain по сделке
macro NUTX_GetClientDealTax(DealID:integer, PartyID:integer, TaxGroupStr:string, TaxMain:@money, TaxMain0:@money)
  var query, cmd, DataSet;
  var FactReceiverID = GetFactReceiverForNUTX(PartyID);
  var pt = TRecHandler("party.dbt");
  var receiver = TRecHandler("party.dbt");

  TaxMain  = $0;
  TaxMain0 = $0;

  if((ПолучитьСубъекта(PartyID, pt) == 0) and
     (pt.rec.LegalForm == legal_form_jur) and 
     (IsNeresidentForNUTX(PartyID, {curdate})) and 
     (ПолучитьСубъекта(FactReceiverID, receiver) == 0) and
     (receiver.rec.LegalForm == legal_form_jur) and 
     (IsNeresidentForNUTX(FactReceiverID, {curdate}))
    )
  
    query =   " select NVL(SUM(obj.t_Sum), 0) as DueTax, NVL(SUM(obj.t_Sum0), 0) as DueTax0 "
            + "   from dnutxobj_dbt obj"
            + "  where obj.t_Kind = "+NUTXOBJ_DUETAX
            + "    and obj.t_Client = ? "
            + "    and obj.t_Party = ? "
            + "    and obj.t_AnaliticKind1 = " + TXOBJ_KIND1010
            + "    and obj.t_Analitic1 = ? "
            + "    and obj.t_AnaliticKind5 = " + TXOBJ_KIND5010
            + "    and obj.t_Analitic5 IN ("+TaxGroupStr+")";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(PartyID);
    cmd.AddParam(FactReceiverID);
    cmd.AddParam(DealID);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      TaxMain  = DataSet.DueTax;
      TaxMain0 = DataSet.DueTax0;
    end;
  end;
end;

//Получить сумму налога уплаченного по сделке
macro NUTX_GetClientDealPaidTax(DealID:integer, PartyID:integer, Sum:@money, Sum0:@money)
  var query, cmd, DataSet;

  Sum  = $0;
  Sum0 = $0;

  query =   " select NVL(SUM(obj.t_Sum), 0) as PaidTax, NVL(SUM(obj.t_Sum0), 0) as PaidTax0 "
          + "   from dnutxobj_dbt obj"
          + "  where obj.t_Kind = "+NUTXOBJ_PAIDTAX
          + "    and obj.t_Client = ? "
          + "    and obj.t_Party = ? "
          + "    and obj.t_AnaliticKind1 = " + TXOBJ_KIND1010
          + "    and obj.t_Analitic1 = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(PartyID);
  cmd.AddParam(GetFactReceiverForNUTX(PartyID));
  cmd.AddParam(DealID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    Sum  = DataSet.PaidTax;
    Sum0 = DataSet.PaidTax0;
  end;
end;

//Получить налог к удержанию при учёте купона
macro NUTX_GetClientDealTaxCoup(DealID:integer, PartyID:integer, dat:date, Sum:@money, Sum0:@money)
  var query, cmd, DataSet;

  Sum  = $0;
  Sum0 = $0;

  query =   " select NVL(SUM(obj.t_Sum), 0) as DueTax, NVL(SUM(obj.t_Sum0), 0) as DueTax0 "
          + "   from dnutxobj_dbt obj"
          + "  where obj.t_Kind = "+NUTXOBJ_DUETAX1
          + "    and obj.t_Client = ? "
          + "    and obj.t_Party = ? "
          + "    and obj.t_Date = ? "
          + "    and obj.t_AnaliticKind1 = " + TXOBJ_KIND1010
          + "    and obj.t_Analitic1 = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(PartyID);
  cmd.AddParam(GetFactReceiverForNUTX(PartyID));
  cmd.AddParam(dat);
  cmd.AddParam(DealID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    Sum  = DataSet.DueTax;
    Sum0 = DataSet.DueTax0;
  end;
end;

macro IsControlOrg(PartyID)
  var ObjAtCorQuery = "select atcor.t_AttrID " +
                    "from dobjatcor_dbt atcor " +
                    "where atcor.t_ObjectType = " + string(OBJTYPE_PARTY) +
                    "  and atcor.t_GroupID = 97" + //Является контролируемой организацией
                    "  and atcor.t_Object = LPAD(" + string(PartyID) + ", 10, '0') " +
                    "  and RSI_NUTX.GetFactReceiverForNUTX(" + string(PartyID) + ") = " + string(PartyID);

  var ObjAtCorSelect = DL_RSDCommand(ObjAtCorQuery);
  var ObjAtCorDS = ObjAtCorSelect.Execute();

  return (ObjAtCorDS.MoveNext() and (ObjAtCorDS.AttrID == 1));
end;