/*
$Name:         dividend_emit_030.mac
$Module:       Ценные бумаги
$Description:  Операция получения дивидендов по РЕПО (от Контрагента)
*/
/*  Шаг Получение платежа от эмитента (30): шаг формирования проводок,   */

import InsCarryDoc, "sp_class.mac", "sp_car.mac", "sp_car2.mac", "spservsql.mac", "SPQICheckOper.mac";

PRIVATE CONST K_ENTER = 13,
              K_ESC = 27,
              K_F2  = 316,
              K_F3  = 317,
              K_F9  = 323;

PRIVATE Record dlg(DIVSUMEM, "sp_res.lbr") dialog;
private var _CurDiv = 0;

private var _leg, _tick;

//Поля диалога
private const F_SUMSTEP = 0,
              F_CURDIV  = 1,
              F_QSTEP   = 2;

private macro getTick(id:Integer, dl): bool
    var tick = TbFile("dl_tick.dbt");
    tick.Clear();
    tick.rec.DealID = id;
    if ( tick.GetEQ() )
        copy( dl, tick );
        return true;
    end; 
    return false;
end;


// конвертирование сумм валют по курсу ЦБ РФ
private macro _convertCurr(outVal:@money, inVal:money, ondate:date, fromCurr, toCurr ): bool
    var stat: bool = true;
    if (fromCurr != toCurr)
        stat = ConvSumDbl( outVal, inVal, onDate, fromCurr, toCurr, true, 7 /*ЦБ РФ*/ );
        SetParm(0, outVal); // передаем по ссылке
    end;
    return stat;
end;  

private MACRO getDL_SUM(docID:Integer, docKind:Integer, onDate:date, sums:@money, qsteps:@money): bool
      var cmd = DL_RSDCommand( "select Nvl(Sum(dl.t_Sum),0) QSums, Nvl(Sum(dl.t_Nds),0) QSteps from ddlsum_dbt dl"
                            + " where dl.t_DocKind = ?"
                            + " and dl.t_DocID = ?"
                            + " and dl.t_Kind = ?" 
                            + " and dl.t_Date >= ? ");  
    cmd.AddParam( docKind );
    cmd.AddParam( docID );
    cmd.AddParam( DLSUM_KIND_BANK_PERIOD );
    cmd.AddParam( onDate );
    var DataSet = cmd.Execute(); 

    sums = 0; qsteps = 0;

    if ( DataSet.moveNext() )
         qsteps = DataSet.QSteps;
         sums   = DataSet.QSums;
         return true;
    end;
    return false;
end;

private macro ListPanel( pn, CMD, ID, KEY )
    if ( F_CURDIV == ID )
        var Fininstr = TRecHandler( "fininstr.dbt" ); 
        if (ListFI(FIKIND_CURRENCY, 0, Fininstr))
            _CurDiv   = Fininstr.rec.FIID;
            pn.CurDiv = Fininstr.rec.Ccy; ;
        end;
    end;
end;

//Обработка клавиш
PRIVATE MACRO PressKey(pn, CMD, ID, KEY)
    var CM = CM_DEFAULT;
    var sums = $0;
    var qsteps = $0;

    if (KEY == K_ESC) 
        CM = CM_CANCEL;

    elif ( K_F3 == KEY )
        ListPanel(pn, CMD, ID, KEY);
        CM = CM_IGNORE;

    elif ( K_ENTER == KEY )
        CM = CM_IGNORE;

    elif ( (K_F2 == KEY) or (K_F9 == KEY) )
        if ( pn.SumStep <= 0 )
            SetFocus(pn, F_SUMSTEP);
            MsgBox("Сумма платежа равна или меньше 0!");
            CM = CM_IGNORE;

        elif ( pn.CurDiv < 0 )
            SetFocus(pn, F_CURDIV);
            MsgBox("Не выбрана валюта!");
            CM = CM_IGNORE;

        elif ( pn.Qstep <= 0 )
            SetFocus(pn, F_QSTEP);
            MsgBox("Количество ц/б равно или меньше 0!");
            CM = CM_IGNORE;

        elif ( getDL_SUM( _tick.DealID, _tick.BOfficeKind, _tick.DealDate, @sums , @qsteps ) ) 
    
            CM = CM_SAVE;     
            if ( qsteps + pn.QStep > _leg.Principal )  // Qstep + текущее значение Qstep > Qdepo 
                SetFocus(pn, F_QSTEP);
                MsgBox("Ввод количества цб или суммы платежа превышают допустимые значения параметров операции");
                return CM_IGNORE;
            end;
            
            var _sumStep = pn.SumStep;
            if ( _CurDiv != _leg.CFI )
                _convertCurr(_sumStep, _sumStep, {CurDate}, _CurDiv, _leg.CFI);
            end;                        
            if ( sums + _sumStep > _leg.Principal*_leg.Price ) //SumStep + текущее значение SumStep > Qdepo x DivOne
                SetFocus(pn, F_SUMSTEP);
                MsgBox("Ввод количества цб или суммы платежа превышают допустимые значения параметров операции");
                CM = CM_IGNORE;
            end;
            
        else
            CM = CM_SAVE;
        end;

    end;

    return CM;
end;

//обработчик панели
PRIVATE MACRO SP_DivSumEmitemt_Process( pn, CMD, ID, KEY )
  var CM = CM_DEFAULT;

  if( CMD == DLG_INIT )
     UpdateFields( pn );
     CM = CM_IGNORE;
  elif (CMD == DLG_KEY)
     CM = PressKey(pn, CMD, ID, KEY);
  end;

  return CM;
END;

private macro ПолучитьЭмитента( FIID:integer ): integer 
    var cmd = RsdCommand( "select fin.t_ISSUER as ISSUER from dfininstr_dbt fin"
                          " where fin.t_FIID = " + FIID );
    var rs = RsdRecordset( cmd );
    cmd.execute();
    if ( rs.moveNext() )
        if ( ValType( rs.value( "ISSUER" ) ) != V_UNDEF )
            return Int( rs.value( "ISSUER" ) );
        end;
    end;

    return -1;
end;

private macro УчетОплатыНалога(FD: SPFirstDoc, bAcc60301:bool, curDiv:integer, sumStep:money, QStep:money):bool
    var СчОплачНалог  = TrecHandler("account.dbt"), 
        СчНачислДивид = TRecHandler("account.dbt"), 
        СчБюджет      = TRecHandler("account.dbt"), 
        СчКор         = TRecHandler("account.dbt");
    var factDate      = {CurDate};
    var leg           = FD.dl_leg.rec;
    var tick          = FD.tick.rec;

    // Э1. УЧЕТ НАЛОГА                                                                
    if ( not FD.OpenAccount("Оплаченный налог", null, false, null, СчОплачНалог, factDate, NATCUR) )
        return false;
    end;

    if ( not FD.OpenAccount( "Начисленные дивиденды", null, false, 
                             IIF(leg.DmaxPay < factDate,FIROLE_DEALS_OVERDUE,null),
                             СчНачислДивид, factDate, curDiv) )
        return false;
    end;

    if ( ПолучитьДенежныйСчетСубъектаПоСделке( tick.BOfficeKind, tick.DealID, 
                                               ПолучитьЭмитента(leg.PFI), 
                                               leg.CFI,//FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.FIID, 
                                               СчКор) != 0)
        Msgbox( "Не заполнены платежные реквизиты субъекта с ID = " +string(ПолучитьЭмитента(leg.PFI)) );
        return false;
    end;

    if ( bAcc60301 ) // Настройка "Использовать сч.60301" = YES                            
        if ( not FD.OpenAccount( "-Бюджет, ф.налоги", null, false, null, СчБюджет, factDate, NATCUR/*curDiv*/) )
            return false;
        end;

    end; // if ( bAcc60301 )

    // QStep * DivOne - SumStep. Но для этого приводим выражение (QStep*DivOne) к валюте суммы SumStep.
    var avoirSum = QStep * leg.Price /*DivOne*/;
    if (leg.CFI /*валюта указанная в панели от Эмитента*/ != curDiv)
        if ( not _convertCurr( avoirSum, avoirSum, factDate, leg.CFI, curDiv ) )
            return false;
        end;
    end; 
    var sum =  avoirSum - sumStep;

    // Э1 УЧЕТ НАЛОГА
    if ( not bAcc60301 )
        if( not ПроводкаПоКатегориямУчета( 
                FD, 
                СчОплачНалог, СчНачислДивид, 
                factDate,                      /* Дата  */
                1,                             /* Глава */
                curDiv,             //NATCUR,  /* Валюта суммы проводки */
                sum,                           /* Сумма проводки*/    
                Doc,                           /* Документ */
                INPCARRY,                      /* Result_Carry */
                " 1",                          /* Kind_Oper */
                tick.DealCode,                 /* Numb_Document */
                "Учет налога",                 /* Ground */
                null,null,null,
                СчОплачНалог.rec.code_currency, СчНачислДивид.rec.code_currency  // валюты счетов: Дт - Кт 
            ) 
        )
            MsgBox( "Ошибка при выполнении проводки 'Учет налога'!" );
            return false;
        end;
    
    else // bAcc60301 == true
        
        if( not ПроводкаПоКатегориямУчета( 
                FD, 
                СчОплачНалог, СчБюджет, 
                factDate,                      /* Дата  */
                1,                             /* Глава */
                curDiv,             //NATCUR,  /* Валюта суммы проводки */
                sum,                           /* Сумма проводки*/    
                Doc,                           /* Документ */
                INPCARRY,                      /* Result_Carry */
                " 1",                          /* Kind_Oper */
                tick.DealCode,                 /* Numb_Document */
                "Учет налога",                 /* Ground */
                null,null,null,
                СчОплачНалог.rec.code_currency, СчБюджет.rec.code_currency  // валюты счетов: Дт - Кт 
            ) 
        )
            MsgBox( "Ошибка при выполнении проводки 'Учет налога'!" );
            return false;
        end; 

        if( not ПроводкаПоКатегориямУчета( 
                FD, 
                СчБюджет, СчНачислДивид,   
                factDate,                      /* Дата  */
                1,                             /* Глава */
                curDiv,             //NATCUR,  /* Валюта суммы проводки */
                sum,                           /* Сумма проводки*/    
                Doc,                           /* Документ */
                INPCARRY,                      /* Result_Carry */
                " 1",                          /* Kind_Oper */
                tick.DealCode,                 /* Numb_Document */
                "Учет налога",                 /* Ground */
                null,null,null,
                 СчБюджет.rec.code_currency, СчНачислДивид.rec.code_currency  // валюты счетов: Дт - Кт 
            ) 
        )
            MsgBox( "Ошибка при выполнении проводки 'Учет налога'!" );
            return false;
        end;    
    end;

    // Э2. УЧЕТ ОПЛАТЫ
    if( not ПроводкаПоКатегориямУчета( 
            FD, 
            СчКор, СчНачислДивид, 
            factDate,                      /* Дата */
            1,                             /* Глава */
            curDiv,             //NATCUR,  /* Валюта суммы проводки */
            sumStep,                       /* Сумма проводки*/    
            Doc,                           /* Документ */
            INPCARRY,                      /* Result_Carry */
            " 1",                          /* Kind_Oper */
            tick.DealCode,                 /* Numb_Document */
            "Учет оплаты",                 /* Ground */
            null,null,null,
            СчКор.rec.code_currency, СчНачислДивид.rec.code_currency  // валюты счетов: Дт - Кт 
        ) 
    )
        MsgBox( "Ошибка при выполнении проводки 'Учет оплаты'!" );
        return false;
    end;

    return true;
end;

// 2.	Выполняются блоки проводок Э и К.
private macro DoOperation(FD: SPFirstDoc, sumStep: money, curDiv: integer, Qstep: money)
    var AccDebet  = TrecHandler( "account.dbt");
    var AccCredit = TrecHandler( "account.dbt");
    var factDate  = {CurDate};
    var leg       = FD.dl_leg.rec;
    var tick      = FD.tick.rec;
    const bAcc60301: bool = Использовать_Сч60301();

    // Выполняются блоки проводок Э и К.
    if ( not УчетОплатыНалога(FD, bAcc60301, curDiv, sumStep, QStep) )
        return false;
    end;

    // К. ПЕРЕНОС ЗАДОЛЖЕННОСТИ ПО КОНТРАГЕНТАМ
    // Есть запись с Дата сбора ре-естра=Dfix и ц/б= SecCode, и "До выплаты эмитентом"=Истина
    if ( leg.Principal > QStep ) // Если сумма QStep == QDepo, тогда не нужно переносить задолженность
    
        var cmd = DL_RSDCommand( "select Sum(dt.t_Qnty) Qcontr, dt.t_PartyID PartyID, dt.t_DealRepo DealRepo  from dcontrduties_dbt dt "
                                + " where dt.t_Dfix = ? and "   // GetSQLDate(leg.Start)
                                + " dt.t_PFI = ? and "
                                + " dt.t_BeforePay = 'X'"
                                + " group by dt.t_DealRepo, dt.t_PartyID" );

        cmd.AddParam( leg.Start );
        cmd.AddParam( tick.PFI );

        var DataSet = cmd.Execute();
        while ( DataSet.moveNext() )

            var sum  = leg.Price /*DivOne_*/ *  DataSet.Qcontr; 

            if ( sum == 0 ) continue;  end;
    
            FD.setCurContrAgent( DataSet.PartyID );  // открываем счет по нужному КА            
            if ( not FD.OpenAccount("+Расчеты", null, false, FIROLE_CUR_CONTRAGENT/*FIROLE_EMIT_INC_TR*/, AccDebet, factDate, leg.CFI) )
                return false;
            end;
            FD.setCurContrAgent( -1 ); 
            if ( not FD.OpenAccount("Начисленные дивиденды", null, false, 
                                    IIF(leg.DmaxPay < factDate,FIROLE_DEALS_OVERDUE,null), 
                                    AccCredit, factDate, leg.CFI) )
                return false;
            end;

            if( not ПроводкаПоКатегориямУчета( 
                        FD, 
                        AccDebet, AccCredit, 
                        factDate,                      /* Дата */
                        1,                             /* Глава */
                        leg.CFI,                       /* Валюта суммы проводки */
                        sum,                           /* Сумма проводки*/    
                        Doc,                           /* Документ */
                        INPCARRY,                      /* Result_Carry */
                        " 1",                          /* Kind_Oper */
                        tick.DealCode,                 /* Numb_Document */
                        "Перенос задолженности по контрагентам",                 /* Ground */
                        null,null,null,
                        AccDebet.rec.code_currency, AccCredit.rec.code_currency  // валюты счетов: Дт - Кт 
                    ) 
                )
                    MsgBox( "Ошибка при выполнении проводки 'Перенос задолженности по контрагентам'!" );
                    return false;
            end;
        end; // while
    end; // if

    return true;
end;

// 3.	Происходит изменение признака "До выплаты эмитентом" в записях об имеющемся остатке в таблице "Долг контрагентов по дивидендам". 
private macro changeBeforePayInContrDuties(pfi: Integer, dfix: Date)
     
   /* var cmd = RSDCommand( "update dcontrduties_dbt dt set dt.t_BeforePay = NULL "
                            + " where dt.t_Dfix = " + GetSQLDate(dfix) 
                            + " and dt.t_PFI = "+ pfi 
                            + " and dt.t_BeforePay = 'X'" ); 
    cmd.Execute(); // update
    */

    return DL_ContrDutiesUnsetBeforePay(pfi, dfix);
end;

macro ExecuteStep( Doc, FDoc )   

    record deal(DL_TICK);
    SetBuff( deal, FDoc ); 

    if((GetDialogFlag() != 0) and (SP_IsShedulerRunning() == false))

        dlg.SumStep = 0;
        dlg.CurDiv = "RUB";
        dlg.Qstep = 0;

        VAR FD = SPFirstDoc( deal );
        var leg = FD.dl_leg.rec;
        var tick = FD.tick.rec;
        _leg = leg; _tick = tick; // для внешних ссылок

        if (RunDialog(dlg, @SP_DivSumEmitemt_Process))            

            // 2.	Выполняются блоки проводок Э и К.
 
            if ( not DoOperation(FD, dlg.SumStep, _curDiv, dlg.Qstep) )
                return 1;
            end;
         
            // 3.	Происходит изменение признака "До выплаты эмитентом" в записях об имеющемся остатке
            //       в таблице "Долг контрагентов по дивидендам". 
            changeBeforePayInContrDuties(tick.PFI, leg.Start);

            var _sumStep = dlg.SumStep; // храним в DL_SUM сумму в валюте CurDiv
            if (  _CurDiv != leg.CFI )
                _convertCurr(_sumStep, _sumStep, {CurDate}, _CurDiv, leg.CFI );
            end;
            if( not СохранитьСуммуКомиссии( tick.BOfficeKind, tick.DealID, DLSUM_KIND_BANK_PERIOD, 
                                        {CurDate}, money(_sumStep), money(dlg.Qstep), $0 ) )
                return 1;
            end; 

            DefineOprDate( 100, {CurDate} );

            return 0;
        else
            return 1;
        end;
    end;

    return 1;
end;
          
