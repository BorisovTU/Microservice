/*
$Name:        IncR_Reg_Form.mac
$Module:      АРНУ
$Description: Отчет "Регистры по начислению дохода по ц/б в РЕПО" (НУ) - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_INCR_REG_PERIOD     = 0,
      PNFLD_INCR_REG_YEAR       = 1,
      PNFLD_INCR_REG_ENDDATE    = 2,
      PNFLD_INCR_REG_TYPEREG    = 3,
      PNFLD_INCR_REG_AVRGROUP   = 4,
      PNFLD_INCR_REG_AVRKIND    = 5,
      PNFLD_INCR_REG_AVRCODE    = 6,
      PNFLD_INCR_REG_AVRNAME    = 7,
      PNFLD_INCR_REG_PRINTMETHOD= 8;

CONST H_PNFLD_SUBKIND = 100, 
      H_PNFLD_AVRGROUP = 101;

/*Типы формы - 0405 - 0408 - для отчета "Регистры по начислению ц/б в РЕПО".*/
CONST INCR_REG_ALL    = 0, /*Все*/
      INCR_REG_0405   = 5, /*0401*/
      INCR_REG_0406   = 6, /*0402*/
      INCR_REG_0407   = 7, /*0403*/
      INCR_REG_0408   = 8; /*0404*/

PRIVATE VAR INCR_REG_NAME = TArray();
INCR_REG_NAME[INCR_REG_ALL ] = "Все";
INCR_REG_NAME[INCR_REG_0405] = "0405 - рублевые процентные облигации";                           
INCR_REG_NAME[INCR_REG_0406] = "0406 - рублевые дисконтные облигации";            
INCR_REG_NAME[INCR_REG_0407] = "0407 - валютные процентные облигации";            
INCR_REG_NAME[INCR_REG_0408] = "0408 - валютные дисконтные облигации";   

macro GetAvoirListAddWhere(RepSubKind)

  var WhereCond = " ";
  WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 ";

  if(RepSubKind == INCR_REG_0405)
    WhereCond = WhereCond + " AND  t_FaceValueFI = " + NATCUR
                          + " AND  Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1"; 
  elif(RepSubKind == INCR_REG_0406)
    WhereCond = WhereCond + " AND  t_FaceValueFI = " + NATCUR
                          + " AND  Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) = 1";
  elif(RepSubKind == INCR_REG_0407)
    WhereCond = WhereCond + " AND  t_FaceValueFI <> " + NATCUR
                          + " AND  Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1";
  elif(RepSubKind == INCR_REG_0408)
    WhereCond = WhereCond + " AND  t_FaceValueFI <> " + NATCUR
                          + " AND  Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) = 1";
  end;

  WhereCond = WhereCond + " AND AV.t_TaxGroup not in (12, 13, 22, 35, 36, 37, 919, 943 ) "; 
      
  return WhereCond;
end;

/*Тип отчета - 0405 - 0408*/
PRIVATE MACRO FldProc_Subkind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = INCR_REG_ALL;
     end;
     FldShowValue = INCR_REG_NAME[FldRealValue];
     
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
  
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    INCR_REG_NAME[INCR_REG_ALL ], INCR_REG_ALL ,
                    INCR_REG_NAME[INCR_REG_0405], INCR_REG_0405,
                    INCR_REG_NAME[INCR_REG_0406], INCR_REG_0406,
                    INCR_REG_NAME[INCR_REG_0407], INCR_REG_0407,
                    INCR_REG_NAME[INCR_REG_0408], INCR_REG_0408 
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = INCR_REG_ALL;
     FldShowValue = INCR_REG_NAME[FldRealValue];
  end;
  return CM_DEFAULT;
END;

/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR RetVal = CM_DEFAULT;
  VAR WhereCond = "1=1", AddTable = "", AvrKind, AvrGroup;
  VAR RepSubKind = 0;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoir    = TRecHandler( "AVOIRISS.dbt" );

  if( not ((Cmd == DLG_KEY) AND (Key == KEY_F3)) )
    DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
   
    AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
    if( (AvrKind == NULL) OR (AvrKind < 0) )
       AvrKind = 0;
    end;

    AddTable  = AddTable  + "davoiriss_dbt AV";
    WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID ";

    RepSubKind = pThis.GetLinkedValue(H_PNFLD_SUBKIND);

    WhereCond = WhereCond + GetAvoirListAddWhere(RepSubKind);

    AvrGroup = pThis.GetLinkedValue(H_PNFLD_AVRGROUP);
    if( (AvrGroup != NULL) AND (AvrGroup > 0) )
       WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrGroup;
    end;

    if( ListAvoiriss( AvrKind, Fininstr, Avoir, null, WhereCond, AddTable ) )
       FldRealValue = Fininstr.rec.FIID;
       FldShowValue = Fininstr.rec.FI_Code;
       pThis.SetLinkedValue( H_PNFLD_AVRGROUP, Avoir.rec.TaxGroup );
    end;

  end;
  return RetVal;
END;

/*Группа налогового учета*/
PRIVATE MACRO FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;
  VAR Avoiriss, AvrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
              + "  and t_Element not in (12, 13, 22, 35, 36, 37, 919, 943 ) order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Name");    
        FldRealValue = Choice.Value("t_Element");
     end;

  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_IncR_Reg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year, BegDate, RepDate;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;

     /*Получим отчетную дату - последнюю дату из отчетного периода.*/
     Period_GetInterval( PrevQuartal + 12, Year, @BegDate, @RepDate );
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,H_PNFLD_AVRGROUP) )
        SetLinkedValue( H_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     if( ВОЗМОЖНОСТЬ_ВЫБОРА_ВИДА_ЦБ() == false)
        DisableFields(This.Data(), PNFLD_INCR_REG_AVRKIND);
     end;

     /*только в excel*/
     DisableFields( This.Data(), PNFLD_INCR_REG_PRINTMETHOD );

     AddFieldProc( 0, PNFLD_INCR_REG_PERIOD,      DL_PNFLD_PERIOD,      @DL_FldProc_Period, PrevQuartal + 12, 20, 3 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_INCR_REG_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_INCR_REG_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate, RepDate );
     AddFieldProc( 0, PNFLD_INCR_REG_TYPEREG,     H_PNFLD_SUBKIND,      @FldProc_Subkind, INCR_REG_ALL, 12, 7 );
     AddFieldProc( 0, PNFLD_INCR_REG_AVRGROUP,    H_PNFLD_AVRGROUP,     @FldProc_AvrGroupFltr );
     AddFieldProc( 0, PNFLD_INCR_REG_AVRKIND,     DL_PNFLD_AVRKIND,     @DL_FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_INCR_REG_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_INCR_REG_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_INCR_REG_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 40, 17 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "REG_INCR" ); 
END;

CLASS ( TxReportForm ) WS_TX_IncR_Reg_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_INCR_REG_PERIOD]       = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_INCR_REG_YEAR]         = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_INCR_REG_ENDDATE]      = TXREPORTFORM_FLD_REPORTDATE;
  ReDefineFieldNum[PNFLD_INCR_REG_TYPEREG]      = TXREPORTFORM_WIDGET_KIND;
  ReDefineFieldNum[PNFLD_INCR_REG_AVRGROUP]     = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_INCR_REG_AVRKIND]      = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_INCR_REG_AVRCODE]      = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_INCR_REG_AVRNAME]      = TXREPORTFORM_FAKEFLD_AVRNAME;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
