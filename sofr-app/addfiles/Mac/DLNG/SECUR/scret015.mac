/* Операция погашения ц/б 
   Актуализация счетов балансового учета. */
import InsCarryDoc, "sp_car.mac", "sp_carrl.mac", "sp_cars.mac";

macro ExecuteStep( doc, FDoc )
  
  record deal(dl_tick);
  var    FD, dat, ContrCateg;

  SetBuff( deal, FDoc );
  FD = SPFirstDoc( deal, false );

  GetOprDate( FD.GetKindDate(DATE_DEALDATE), dat );
  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага запрещено.");
    return 1;
  end;

  if( not ОбновитьЛот( FD.pmwrtsum, PM_WRTSUM_UPDTMODE_CREATE, dat, FD ) )
     msgbox("Ошибка при создании лота.");
     return 1;
  end;     

  if( not ОбновитьСтатусЧастиСделки( FD.dl_leg, DL_LEG_BALANCE ) )
     msgbox("Ошибка при изменении статуса сделки");
     return 1;
  end;

  if( not IsClientDeal(deal) ) /*наша сделка*/
     if( not FD.OpenAccount( "Реализация, ц/б", null, null, null, null, dat ) )
        return 1;
     end;
  end;

  ContrCateg = СчетСписанияСредствЗаПогашение(FD);
  if( (ContrCateg != "Корсчет") AND (ContrCateg != "") )
     if( not FD.OpenAccount( ContrCateg, null, null, null, null, dat ) )
        return 1;
     end;
  end;

  ContrCateg = ПолучитьКатегориюСчетаКонтрагента( FD, true );
  if( (ContrCateg != "Корсчет") AND (ContrCateg != "") AND
      (not FD.OpenAccount( ContrCateg, null, null, null, null, dat) )
    )
     return 1;
  end;

  return 0;
end;

