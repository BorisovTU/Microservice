/*
$Name:        nptxctrl_refsend_form.mac
$Module:      Ценные бумаги
$Description: Формирование справок по НДФЛ - форма ввода данных
*/
import "DlRepForm_h.mac";

CONST PNFLD_NPTXCTRL_REFSEND_CSVFILENAME       = 0,
      PNFLD_NPTXCTRL_REFSEND_CLIENTCODE        = 1,
      PNFLD_NPTXCTRL_REFSEND_CLIENTNAME        = 2,
      PNFLD_NPTXCTRL_REFSEND_TOPUP_DATE        = 3,
      PNFLD_NPTXCTRL_REFSEND_PRINTMETHOD_EXCEL = 4,
      PNFLD_NPTXCTRL_REFSEND_PRINTMETHOD_PDF   = 5,
      PNFLD_NPTXCTRL_REFSEND_SENDMETHOD_EMAIL  = 6;

PRIVATE CONST
  H_CSVFILENAME     = 101,
  H_CLIENTCODE      = 102,
  H_CLIENTNAME      = 103,
  H_TOPUP_DATE      = 104,
  H_EXCEL           = 105,
  H_PDF             = 106,
  H_EMAIL           = 107;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_TopUp_Date( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = {curdate};
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

//Получить имя папки csv-файлов
PRIVATE MACRO GetCSVPath()
  var RegistryPath = "РСХБ\\ДИРЕКТОРИИ\\OUT\\КОНТРОЛЬ_ЗАДОЛЖЕННОСТИ";
  var Path = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if((stat) or (Path == "")) 
    msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  else
    if(substr(Path, strlen(Path)-1, 1) != "\\")
      Path = Path + "\\";
    end;
  end;

  return Path;
END;

/* Обработчик клиента */
private macro FldProc_CSVFIleName( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var saveFldRealValue = FldRealValue;
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == ""))
      if( FldRealValue == null )
         FldRealValue = "";
      end;
      if( FldRealValue == "" )
         FldShowValue = "";
      end;
    else
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    var file_name = "";
    if(SelectFile(file_name, MergeFile(GetCSVPath(), "*.csv*"), NULL , 0, true)) 
      FldRealValue = file_name;
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = "";
     FldShowValue = "";
  end;

  return CM_DEFAULT;
end;

//Листалка клиентов с множественным выбором
private macro SelectClient()

   /*Обработчик скроллинга*/
   macro Client_Scroll(rs, cmd, id, key)
      /*Обработчик*/
      if(cmd == DLG_KEY)
         if(key == 13) /*Enter*/
            return CM_IGNORE;
         elif(key == 27) /*Esc*/
            return CM_CANCEL;
         elif(key == 32) /*Space*/
            if(rs.RecCount > 0)
               rs.edit();
               if(rs.value("t_SelectFlag") == "X")
                  rs.value("t_SelectFlag") = " ";
               else                                                                                         
                  rs.value("t_SelectFlag") = "X";
               end;
               rs.update();
               updatescroll(rs, 5);
               return CM_IGNORE;
            end;
         end;
      end;
   end;

   var query, cmd, rs;
   query =   " SELECT S.t_SelectFlag, S.t_ClientID, S.t_ClientName, S.t_ClientCode "
           + "   FROM DDLSELECTPT_TMP S "
           + "  ORDER BY S.t_SelectFlag DESC, S.t_ClientName";

   cmd = RsdCommand(query);

   rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);

   while(RunScroll(rs, 4, MakeArray("t_SelectFlag", "В", 1, 2, -1, 0,  
                                    "t_ClientCode", "Код", 15, 2, -1, 0,
                                    "t_ClientName", "Наименование клиента", 30, 2, -1, 0,
                                    "t_ClientID",   "ID", 8, 2, -1, 0 
                                   ), 
                  null, "Client_Scroll", "Выбор клиентов", "~Esc~ Выход ~Пробел~ Изменить", false))
      cmd = RsdCommand(query);
      rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
   end;
end;

/* Обработчик клиента */
private macro FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party    = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var query, exec, DataSet;
  var cnt = 0;
  var saveFldRealValue = FldRealValue;
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == -1))
      if( FldRealValue == null )
         FldRealValue = -1;
      end;
      if( FldRealValue == -1 )
         FldShowValue = STR_ALLVALUE;
      end;
    elif(FldRealValue == -2)
      cnt = DL_RSDCommand("SELECT 1 FROM DDLSELECTPT_TMP WHERE T_SELECTFLAG = 'X'").GetCount();
      FldShowValue = "Несколько";
      pThis.SetLinkedValue(H_CLIENTNAME, "Выбрано: "+int(cnt));
    else
      partcode.Clear();
      partcode.rec.PartyID  = FldRealValue;
      partcode.rec.CodeKind = 1;
      if (partcode.GetEQ())
       FldShowValue = partcode.rec.Code;
       if( ПолучитьСубъекта( FldRealValue, Party) == 0 )
         pThis.SetLinkedValue(H_CLIENTNAME, Party.rec.Name);
       end;
      end;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    cnt = DL_RSDCommand("SELECT 1 FROM DDLSELECTPT_TMP WHERE ROWNUM = 1").GetCount();

    if(cnt == 0)
      //Добавить клиентов во временную таблицу
      query =   " INSERT INTO DDLSELECTPT_TMP (T_CLIENTID, T_CLIENTCODE, T_CLIENTNAME, T_SELECTFLAG) "
              + " SELECT DISTINCT CL.T_PARTYID, RSB_SECUR.SC_GetObjCodeOnDate("+OBJTYPE_PARTY+", "+PTCK_CONTR+", CL.T_PARTYID), PT.T_NAME, CHR(0) "
              + "   FROM DPERSN_DBT P, DCLIENT_DBT CL, DPARTY_DBT PT "
              + "  WHERE CL.T_PARTYID = P.T_PERSONID "
              + "    AND CL.T_SERVICEKIND IN ("+PTSK_STOCKDL+","+PTSK_DV+","+PTSK_DEPOS+") "
              + "    AND PT.T_PARTYID = CL.T_PARTYID";

      DL_RSDCommand(query).ExecuteCMD();
    end;
    
    SelectClient();
    
    exec = DL_RSDCommand("SELECT S.*, COUNT(1) OVER() AS CNT FROM DDLSELECTPT_TMP S WHERE S.T_SELECTFLAG = 'X'");
    DataSet = exec.Execute();
    if(DataSet.moveNext())
      if(DataSet.cnt == 0)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue(H_CLIENTNAME, "");
      elif(DataSet.cnt == 1)
        FldRealValue = DataSet.ClientID;
        FldShowValue = DataSet.ClientCode;
        pThis.SetLinkedValue(H_CLIENTNAME, DataSet.ClientName );
      else
        FldRealValue = -2;
        FldShowValue = "Несколько";
        pThis.SetLinkedValue(H_CLIENTNAME, "Выбрано: "+int(DataSet.cnt));
      end;
    else
      FldRealValue = -1;
      FldShowValue = STR_ALLVALUE;
      pThis.SetLinkedValue(H_CLIENTNAME, "");
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     DL_RSDCommand("UPDATE DDLSELECTPT_TMP SET T_SELECTFLAG = CHR(0) WHERE T_SELECTFLAG = 'X'").ExecuteCMD();
     
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue(H_CLIENTNAME, "");
  end;

  return CM_DEFAULT;
end;

PRIVATE MACRO FldProc_CheckBoxEmail( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) 
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if( FldShowValue == SET_CHAR)
        if(pThis.GetFieldValue(PNFLD_NPTXCTRL_REFSEND_TOPUP_DATE) == date(0,0,0))
          msgbox("Необходимо установить срок пополнения брокерского счета");
          FldShowValue = FldRealValue = UNSET_CHAR;
        end;
     end;
  elif( Cmd == DLG_KEY )
     if(Key == KEY_SPACE) 
        if( FldRealValue == UNSET_CHAR )
          if(pThis.GetFieldValue(PNFLD_NPTXCTRL_REFSEND_TOPUP_DATE) == date(0,0,0))
            msgbox("Необходимо установить срок пополнения брокерского счета");
            FldShowValue = FldRealValue = UNSET_CHAR;
          else
            FldShowValue = FldRealValue = SET_CHAR;
          end;
        else
           FldShowValue = FldRealValue = UNSET_CHAR;
        end;
     end;
  end;
END;


CLASS (DL_CPanel) Sp_NPTXCTRL_RefSend_Panel()
  var m_CSVFileName;         
  var m_ClientID;       
  var m_TopUpDate;
  var m_IsExcel;
  var m_IsPDF; 
  var m_IsEmail;    

  PRIVATE MACRO Init()

     AddFieldProc( 0, PNFLD_NPTXCTRL_REFSEND_CSVFILENAME,        H_CSVFILENAME,    @FldProc_CSVFIleName,     "" );
     AddFieldProc( 0, PNFLD_NPTXCTRL_REFSEND_CLIENTCODE,         H_CLIENTCODE,     @FldProc_Client,          -1 );
     AddFieldProc( 0, PNFLD_NPTXCTRL_REFSEND_CLIENTNAME,         H_CLIENTNAME,     @Default,                 "" );
     AddFieldProc( 0, PNFLD_NPTXCTRL_REFSEND_TOPUP_DATE,         H_TOPUP_DATE,     @FldProc_TopUp_Date,      date(0,0,0));
     AddFieldProc( 0, PNFLD_NPTXCTRL_REFSEND_PRINTMETHOD_EXCEL,  H_EXCEL,          @DL_FldProc_CheckBox,     SET_CHAR );     
     AddFieldProc( 0, PNFLD_NPTXCTRL_REFSEND_PRINTMETHOD_PDF,    H_PDF,            @DL_FldProc_CheckBox,     UNSET_CHAR );
     AddFieldProc( 0, PNFLD_NPTXCTRL_REFSEND_SENDMETHOD_EMAIL,   H_EMAIL,          @FldProc_CheckBoxEmail,   UNSET_CHAR );
  END;

  PRIVATE MACRO Check():BOOL
    if(GetFieldValue(PNFLD_NPTXCTRL_REFSEND_CSVFILENAME) == "")
      msgbox("Необходимо указать файл отчета для загрузки");
      return false;
    end;
    
    if((GetFieldValue(PNFLD_NPTXCTRL_REFSEND_PRINTMETHOD_EXCEL) == UNSET_CHAR) AND (GetFieldValue(PNFLD_NPTXCTRL_REFSEND_PRINTMETHOD_PDF) == UNSET_CHAR))
      MsgBox("Необходимо указать способ формирования справки НДФЛ");
      return false;
    end;
    
    if((GetFieldValue(PNFLD_NPTXCTRL_REFSEND_SENDMETHOD_EMAIL) == SET_CHAR) AND (GetFieldValue(PNFLD_NPTXCTRL_REFSEND_TOPUP_DATE) == date(0,0,0)))
      msgbox("Необходимо установить срок пополнения брокерского счета");
      return false;
    end;

    return true;
  END;

  MACRO Run()
    var stat = Run();

    m_CSVFileName     = GetFieldValue(PNFLD_NPTXCTRL_REFSEND_CSVFILENAME);
    m_ClientID        = GetFieldValue(PNFLD_NPTXCTRL_REFSEND_CLIENTCODE);
    m_TopUpDate       = GetFieldValue(PNFLD_NPTXCTRL_REFSEND_TOPUP_DATE);
    m_IsExcel         = GetFieldValue(PNFLD_NPTXCTRL_REFSEND_PRINTMETHOD_EXCEL);
    m_IsPDF           = GetFieldValue(PNFLD_NPTXCTRL_REFSEND_PRINTMETHOD_PDF); 
    m_IsEmail         = GetFieldValue(PNFLD_NPTXCTRL_REFSEND_SENDMETHOD_EMAIL);

    return stat;
  END;

  DL_RSDCommand("TRUNCATE TABLE DDLSELECTPT_TMP").ExecuteCMD();

  initDL_CPanel( "sp_rep.lbr", "nptxrfse" );
END;