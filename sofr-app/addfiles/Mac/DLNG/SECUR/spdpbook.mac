/*----------------------------------------
Переведен на новые счета депо
/*17 May 2000, VSH*/ 
//переделано 28.10.2005 Nikonorov Engeny
------------------------------------------*/
import deposerv;
import RsbDataSet;
var exist = false;
const MAXINT = 2147483647;
const MAXDATE = date (31,12,9999);

/*Настраиваемая пользователем константа
     - глава в которой будет производится поиск счетов брокерских операций*/

var HeadTable = "┌──────┬──────────────┬──────────────────┬───────────────────┬──────────┬──────────┬────────────┐\n"+
                "│ Синт.│     Счет     │  Название счета  │     Тип счета     │   Дата   │   Дата   │    Дата    │\n"+
                "│ счет │              │                  │        депо       │ открытия │ закрытия │ блокировки │\n"+
                "├──────┼──────────────┼──────────────────┼───────────────────┼──────────┼──────────┼────────────┤";
                
var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

/* дата блокирования счета депо */
macro GetBlockDate ( depoacnt )
  var admoper;
  var stat = 1;
  var query;

  if ( depoacnt.Status == 2 )/* блокирован */
    query =   " select t_OperKind, t_OperDate from ddepoadmo_dbt "
            + " where t_PartyID = " + depoacnt.Owner;
    if( depoacnt.SKind == DEPO_ACCOUNT )
      query = query + " and t_Item = " + SPDP_DEPO_ACCOUNT
                    + " and t_DepoAcc = " + depoacnt.AutoKey
                    + " and t_DepoPart = 0 ";
    else
      query = query + " and t_Item = " + SPDP_DEPO_PARTITION
                    + " and t_DepoAcc = " + depoacnt.Root
                    + " and t_DepoPart = " + depoacnt.AutoKey;  
    end;
    query = query + " and t_FIID = " + ALLFININSTR
                  + " and t_Account = CHR(1) ";

    query = query + " order by t_OperDate ";
    admoper = TRsbDataSet(query);

    while ( admoper.moveNext() )
      if ( (SPAO_CON_UNLOCKING   == admoper.OperKind) or 
           (SPAO_CON_CLOSING     == admoper.OperKind) or
           (SPAO_CON_LOCKING     == admoper.OperKind) or
           (SPAO_CON_REGISTERING == admoper.OperKind) )
        return date(admoper.OperDate);
      end;
    end;
  end;
  return "";
end;

macro PrintLine( depoacnt )
  var SyntAcc, Code, Name, TypeAcc, OpenDate, Closedate = "", BlockDate = "";
  //синтетический счет
  SyntAcc  = depoacnt.Synt;
  //код счета
  Code     = depoacnt.Code;
  //название счета
  Name     = depoacnt.Name;
  //Тип счета депо
  TypeAcc  = depoacnt.ShortName;
  //дата открытия
  OpenDate = date(depoacnt.OpenDate);
  //дата закрытия
  if( depoacnt.CloseDate != date(0,0,0))
    CloseDate = date(depoacnt.CloseDate);
  end;
  //дата блокировки
  BlockDate = GetBlockDate(depoacnt);

  DP_AddPrintCell( Rep,  SyntAcc, 0, 0, "c:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  Code, 0, 0, "c:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  Name, 0, 0, "c:w:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  TypeAcc, 0, 0, "c:w:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  OpenDate, 0, 0, "c:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  Closedate, 0, 0, "c:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  BlockDate, 0, 0, "c:" + RepFontStyleTitel );
  Rep.AddStr();
end;


macro depobook( dprt_num : integer, rep_date : date, ToExcel: bool )
  var query;
  var i = 0;
  var depoacnt;
  var count = 0;
  var stat = 0; 
 
  Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );

  DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

  query =   " select acc.t_ShortName, acnt.t_Synt, acnt.t_Code, acnt.t_Name, acnt.t_Type, acnt.t_OpenDate, acnt.t_CloseDate, acnt.t_SKind, acnt.t_Status, acnt.t_Root, acnt.t_AutoKey, acnt.t_Owner "
          + " from ddepoacnt_dbt acnt, ddepoac_dbt acc"
          + " where acnt.t_OpenDate <= " + GetSQLDate(rep_date)
          + "   and acc.t_ID = acnt.t_Type ";

  if( dprt_num != 0 )
    query = query + " and acnt.t_Department = " + dprt_num;
  end;

  query = query + " order by acnt.t_Root, acnt.t_AutoKey, acnt.t_OpenDate ";

  depoacnt = TRsbDataSet(query,  RSDVAL_CLIENT, RSDVAL_STATIC );
  depoacnt.MoveLast();
  count = depoacnt.GetRecCount();
  
  if( count > 0 )
    InitProgress (count, "Поиск счетов для отчета", "Просмотр счетов ДЕПО");
    DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Отчет по справочнику счетов ДЕПО", 0, 0, false, rep_date, NULL, tablewidth );
    stat = depoacnt.moveFirst();
    while( stat )
      PrintLine( depoacnt );
      stat = depoacnt.moveNext();
      UseProgress(i = i+1);
    end;
    RemProgress();
    DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

    DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
    DP_EndProtocol();                 //закончить протоколирование

    if( ToExcel )
      Rep.PrintWinRep();
      Rep.ShowWinRep();
    else
      Rep.PrintRep();
    end;
  else

    if(not DP_ProtocolIsEmpty())
      DP_AddPrintCell(Rep, "Не найдено ни одного счета ДЕПО", 31, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR);
      Rep.AddStr();

      DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
      DP_EndProtocol();                 //закончить протоколирование

      if( ToExcel )
        Rep.PrintWinRep();
        Rep.ShowWinRep();
      else
        Rep.PrintRep();
      end;
    else
      DP_EndProtocol();                 //закончить протоколирование
    end;

    MsgBox("Не найдено ни одного счета ДЕПО");
    return 1;
  end;

  return 0;
end;
