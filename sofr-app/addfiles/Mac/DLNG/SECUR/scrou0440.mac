/*
$Name:        scrou0440.mac
$Module:      Ценные бумаги
$Description: Внебиржевое РЕПО. Шаг: Ожидание квитовки (для ТО по комп. платежу ДС, ТО по учету купона и ТО по учету ЧП)
*/
import InsCarryDoc, "sp_car.mac";

macro ExecuteStep( doc, FDoc)

  record deal(dl_tick);
  var ДатаИсполненияШага:date, FD;
  var Sql:string = "", Query, Data, IsExistsData = false, IdStr = "";

  SetBuff( deal, FDoc );
  FD = SPFirstDoc( deal, true );

  /*пока с аналитиком договорились так: реально может быть одно неисполненное ТО по комп. платежу или купону или ЧП*/
  GetOprDate( DATE_DEALKVIT, ДатаИсполненияШага );

  Sql = " SELECT rq.t_ID      " +
        "   FROM ddlrq_dbt rq " +
        "  WHERE rq.t_DocKind = ? " +
        "    AND rq.t_DocID   = ? " +
        "    AND (rq.t_Type = ? OR rq.t_Type = ? OR rq.t_Type = ?) " +
        "    AND rq.t_State = ? " +
        "    AND rq.t_PlanDate <= ? "; 
  
  Query = RSDCommand(Sql);
  Query.addParam("", RSDBP_IN, DL_SECURITYDOC);
  Query.addParam("", RSDBP_IN, FD.tick.rec.DealID);
  Query.addParam("", RSDBP_IN, DLRQ_TYPE_COMPPAYM);
  Query.addParam("", RSDBP_IN, DLRQ_TYPE_PAYMREPOCOUP);
  Query.addParam("", RSDBP_IN, DLRQ_TYPE_PAYMREPOPART);
  Query.addParam("", RSDBP_IN, DLRQ_STATE_PLAN);
  Query.addParam("", RSDBP_IN, ДатаИсполненияШага);
  Query.execute();
  
  Data = TRsbDataSet(Query);
  while( Data.MoveNext() )
     if( not IsExistsData )
        IsExistsData = true;
     else
        IdStr = IdStr + ", ";
     end;
     IdStr = IdStr + string(Data.ID);
  end;

  if( IdStr != "" )
     MsgBox("По ТО с ID "+string(IdStr)+" ожидается выполнение квитовки");
     return 1;
  end;

  if( not InsertOprStatus(SP_OPERSTATUS_SECURDEALS_KTO/*Квитовка плановых ТО*/, SP_OPERSTATUS_SECURDEALS_KTO_NOTRUN/*Не выполнять*/) )
     MsgBox("Ошибка при установке статуса <Квитовка плановых ТО> для операции.");
     return 1;
  end;

  return 0;

end;
