/*
secur_resending_funcobj.mac

Переотправка задания на закрытие операции списания ц/б ПКО в сервис Funcobj 
*/

import oralib;
import globals;
import "cblogger2.mac";
import "FuncObjController.mac";

Class SecurFuncobjResender() 

    //код соответствующий отказу в проведении операции
    CONST FAILURE_CODE = 8210;
    //код соответствующий успеху в проведении операции 
    CONST SUCCESS_CODE = 8213;

    private var funcObj:FuncObjController = FuncObjController(); //объект для работы с фанкобжем
    private var logger:c_logger = LoggerFactory().NewItLog("secur_resending_funcobj.mac").GetLogger();

    private macro operationIdIsCorrect(_dealID:integer)
        var sql = "select case when operationid is null then 1 else 0 end as operationid_is_correct from pko_writeoff where dealid = :1";
        var cmd = RSDCommand(sql);
        cmd.AddParam(":1", RSDBP_IN, _dealID);
        var result = RSDRecordSet(cmd);

        if(result.moveNext)
            if(result.value("operationid_is_correct") == 0)
                return true;
            else
                return false;    
            end;
        end;
    end;

    private macro clearFailureFlag(_dealID:integer)
        ExecSql("update pko_writeoff set iscanceled = chr(0) where dealid = :dealID", makeArray(SQLParam("dealID", _dealID)));
    end;

    macro resendTask(dealID:integer)
        var sql = " select tick.t_dealstatus, pko.operationid, pko.iscanceled, pko.iscompleted "
                + "  from pko_writeoff pko  "
                + "  join ddl_tick_dbt tick on pko.dealid = tick.t_dealid "
                + "  where pko.dealid = :1 ";
        var cmd = RSDCommand(sql);
        cmd.AddParam(":1", RSDBP_IN, dealID);
        var result = RSDRecordSet(cmd);

        if (result.moveNext)
            //шаг 1) проверка статуса операции - 10 открытая
            if(result.value("t_dealstatus") == 10)
                //шаг 2) проверка наличия операции в буфере pko_writeoff
                if(operationIdIsCorrect(dealID))
                    //шаг 3) проверка iscanceled и iscompleted
                    if((result.value("iscanceled") != "X") and (result.value("iscompleted") != "X"))
                        logger.Info("Операция № "+ dealID +". Отсутствует конечный статус операции от Диасофт (iscanceled-отказ; iscompleted-успех). Переотправка не произведена.");
                        MsgBox("По данной операции ещё не получен ответ о конечном статусе от Диасофт. Переотправка не требуется");
                        return 0;
                    end;

                    if((result.value("iscanceled") == "X") and (result.value("iscompleted") != "X"))
                        clearFailureFlag(dealID); //DEF-113565 очищаем признак отказа для корректного повторного выполнения шага отказа и правильного закрытия операции.
                        funcObj.Save(dealID, FAILURE_CODE);
                        logger.Info("Операция № "+ dealID +" переотправлена в Funcobj с кодом 8210(отказ)");
                        MsgBox("Операция успешно переотправлена");
                        return 0;
                    end;

                    if((result.value("iscanceled") != "X") and (result.value("iscompleted") == "X"))
                        funcObj.Save(dealID, SUCCESS_CODE);
                        logger.Info("Операция № "+ dealID +" переотправлена в Funcobj с кодом 8213(успех)");
                        MsgBox("Операция успешно переотправлена");
                        return 0;
                    end;
                else
                    logger.Info("Операция № "+ dealID +". operationid = " + result.value("operationid") + ". Операция некорректно заведена");
                    MsgBox("Данная операция некорректно заведена, требуется переотправка через штатные механизмы поддержки");
                    return 0;     
                end;
            else
                logger.Info("Операция № "+ dealID +" имеет статус = " + result.value("t_dealstatus") + ". Переотправка не произведена");
                MsgBox("Данная операция находится в корректном статусе. Переотправка не требуется");
                return 0;
            end;

        else
            logger.Error("Операция № "+ dealID +" не найдена!");
            MsgBox("Операция не найдена в БД!");
            return 1;
        end;        

        onError(errObj)
        logger.ErrorClob("SecurFuncobjResender. resendTask. Операция с dealid = " + dealID, GetFullErrMsg(errObj));
        return 1;
    end;

End;