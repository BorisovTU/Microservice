/*                           
$Name:             spCache.mac
$Module:           Ценные бумаги
$Description:      Кэширование объектов
*/
/*******************************************************************************
 DESCRIPTION  :   Кэширование объектов
 PROGRAMMED BY:   Леонов И. Е.
 CREATION DATE:   IL 02.11.06
*******************************************************************************/
IMPORT RsbDataSet, "cb_sql.mac", FIInter, SPInter, "dlreport.mac", "dlquery.mac";

/*Базовый для элементов кэша*/
PRIVATE CLASS CItemCache()
  MACRO IsEqual( Obj:CItemCache ):BOOL
     return false;
  END;

  MACRO Calculate()
  END;
END;

/*Кэш объектов*/
PRIVATE CLASS CCache()
  PRIVATE VAR m_Data = TArray();

  MACRO Clear()
     m_Data.Size = 0;
  END;

  MACRO ItemExist( ObjWithKey:CItemCache ):CItemCache
     VAR i = m_Data.Size-1;

     while( i >= 0 )  
        if( m_Data[i].IsEqual( ObjWithKey ) )
           return m_Data[i];
        end;
        i = i - 1;
     end;
     return NULL;
  END;

  MACRO Add( NewItem:CItemCache ):CItemCache
     NewItem.Calculate();
     m_Data[m_Data.Size] = NewItem;
     return m_Data[m_Data.Size-1];
  END;     

  MACRO Get( ObjWithKey:CItemCache ):CItemCache
     VAR Item = ItemExist( ObjWithKey );
     if( Item == NULL )
        Item = Add( ObjWithKey );
     end;
     return Item;
  END;

END;

/*****************************************************************************************************************************
  Кэширование данных по FIID
*****************************************************************************************************************************/
/*Базовый для элементов кэша*/
PRIVATE CLASS (CItemCache) CItemCacheFIID( FIID:INTEGER, Type:INTEGER )
  VAR m_FIID = FIID, 
      m_Type = Type;

  MACRO IsEqual( Obj:CItemCacheFIID ):BOOL
     return ( (m_FIID == Obj.m_FIID) AND (m_Type == Obj.m_Type) );
  END;

  initCItemCache();
END;

/*Виды элементов кэша (CItemCacheFIID.m_Type)*/
PRIVATE CONST CACHEFININSTR_COUPON_PARM = 1, // CTaxCouponParm
              CACHEFININSTR_NOMINAL     = 2, // CNominal
              CACHEFININSTR_ISO         = 3, // CISONumber
              CACHEFININSTR_NALOGGROUP  = 4, // CNalogGroup
              CACHEFININSTR_FIDEFAULT   = 5, // Дефолтная
              CACHEFININSTR_AVRKINDROOT       = 6, // CAvrKindRoot
              CACHEFININSTR_NOMINAL_ON_ISSUED = 7; // CNominalOnIssued

/* Параметры купонов для налогового учета
 Дк  - дата начала купона, который начинается до <H0.7> включительно, а заканчивается после <H0.7>,
 Дк1 = дата погашения купона, который гасится первым в период между <H0.7> и <H0.8> включительно
 Ск1 = сумма (в валюте номинала) купона, который гасится первым в период между <H0.7> и <H0.8> включительно 
 Дк2 = дата погашения купона, который гасится вторым в период между <H0.7> и <H0.8> включительно
 Ск2 = сумма (в валюте номинала) купона, который гасится вторым в период между <H0.7> и <H0.8> включительно
 Дк3 = дата погашения купона, который гасится третьим в период между <H0.7> и <H0.8> включительно
 Ск3 = сумма (в валюте номинала) купона, который гасится третьим в период между <H0.7> и <H0.8> включительно
 Дк4 = дата погашения купона, который гасится четвертым в период между <H0.7> и <H0.8> включительно
 Ск4 = сумма (в валюте номинала) купона, который гасится четвертым в период между <H0.7> и <H0.8> включи-тельно
*/
CLASS (CItemCacheFIID) CTaxCouponParm( FIID:INTEGER, H07:DATE, H08:DATE )
  VAR m_H07 = H07, m_H08 = H08,
      m_Дк, m_Дк1, m_Дк2, m_Дк3, m_Дк4, m_Ск1, m_Ск2, m_Ск3, m_Ск4;

  initCItemCacheFIID( FIID, CACHEFININSTR_COUPON_PARM );

  MACRO Calculate()
     VAR sql1 = RSDCommand( " SELECT t_FirstDate " +
                                  "   FROM dfiwarnts_dbt " +
                                  "  WHERE t_FIID         =  ? AND " +
                                  "        t_IsPartial   != 'X' AND " +
                                  "        t_FirstDate   <=  ? AND " +
                                  "        t_DrawingDate >=  ? "
                                );

     sql1.addParam( "", RSDBP_IN, m_FIID );
     sql1.addParam( "", RSDBP_IN, m_H07 );
     sql1.addParam( "", RSDBP_IN, m_H07 );
     sql1.execute();

     VAR DataSetK0 = TRsbDataSet(sql1);

     VAR sql2   = RSDCommand( " SELECT t_DrawingDate " +
                                  "   FROM dfiwarnts_dbt " +
                                  "  WHERE t_FIID         =  ? AND " +
                                  "        t_IsPartial   != 'X' AND " +
                                  "        t_DrawingDate >=  ? AND " +
                                  "        t_DrawingDate <=  ? " +
                                  " ORDER BY t_DrawingDate ASC "
                                );

     sql2.addParam( "", RSDBP_IN, m_FIID );
     sql2.addParam( "", RSDBP_IN, m_H07 );
     sql2.addParam( "", RSDBP_IN, m_H08 );
     sql2.execute();

     VAR DataSet   = TRsbDataSet(sql2);
     MACRO CalculateByCoupon( Дк:@DATE, Ск:@MONEY )
        if( DataSet.MoveNext() )
           Дк = SQL_ConvTypeDate( DataSet.t_DrawingDate );
           Ск = СуммаКупона( m_FIID, $1, Дк );
           return true;
        end;
        return false;
     END;

     m_Дк = m_Дк1 = m_Дк2 = m_Дк3 = m_Дк4 = DATE(0,0,0);
     m_Ск1= m_Ск2 = m_Ск3 = m_Ск4 = $0;

     if( DataSetK0.MoveNext() )
        m_Дк = SQL_ConvTypeDate( DataSetK0.t_FirstDate );
     end;

     if( CalculateByCoupon( @m_Дк1, @m_Ск1 ) )
        if( CalculateByCoupon( @m_Дк2, @m_Ск2 ) )
           if( CalculateByCoupon( @m_Дк3, @m_Ск3 ) )
              CalculateByCoupon( @m_Дк4, @m_Ск4 );
           end;
        end;
     end;
  END;

END;

/*Номинал */
CLASS (CItemCacheFIID) CNominal( FIID:INTEGER )
  VAR m_Value = $0;

  initCItemCacheFIID( FIID, CACHEFININSTR_NOMINAL);

  MACRO Calculate()
     FI_GetNominal( m_FIID, m_Value );
  END;
END;

/*Номинал по условиям выпуска (на дату выпуска ц\б)*/
CLASS (CItemCacheFIID) CNominalOnIssued( FIID:INTEGER )
  VAR m_Value = $0;

  initCItemCacheFIID( FIID, CACHEFININSTR_NOMINAL_ON_ISSUED);

  MACRO Calculate()
     VAR FI = TRecHandler( "fininstr.dbt" );

     if( ПолучитьФинИн( m_FIID, FI ) ) 
        MsgBox ("Не найдена ценная бумага (DFININSTR_DBT.t_FIID =", m_FIID ,")" );
        return;
     end;

     if( not FI_GetCurrentNominal(m_FIID, FI.rec.Issued, m_Value) )
        msgbox( "Не могу получить номинал фин. инструмента (FIID = "+ String(m_FIID) + ")" );
        return;
     end;
  END;
END;

/*Налоговая группа*/
CLASS (CItemCacheFIID) CNalogGroup( FIID:INTEGER )
  VAR m_AvrAttrID = 0, m_Number = "", m_FullName = "", m_ShortName = "";

  initCItemCacheFIID( FIID, CACHEFININSTR_NALOGGROUP );

  MACRO Calculate()
     VAR Avoiriss = TRecHandler( "avoiriss.dbt" );

     if( ПолучитьФинИн( m_FIID, null, Avoiriss ) ) 
        MsgBox ("Не найдена ценная бумага (DFININSTR_DBT.t_FIID =", m_FIID ,")" );
        return;
     end;

     if( (m_AvrAttrID = DL_GetMainObjAttr( Avoiriss, 19, OBJTYPE_AVOIRISS )) > 0 )
        DL_GetObjAttrName( OBJTYPE_AVOIRISS, 19, m_AvrAttrID, @m_FullName, @m_ShortName, @m_Number );
     end;
  END;
END;

/*Цифровой код валюты*/
CLASS (CItemCacheFIID) CISONumber( FIID:INTEGER )
  VAR m_Number;

  initCItemCacheFIID( FIID, CACHEFININSTR_ISO );

  MACRO Calculate()
     m_Number = ПолучитьКодФинИн( m_FIID, null, FICK_ISONUMBER );
  END;
END;

/*Дефолтная*/
CLASS (CItemCacheFIID) CFIDefault( FIID:INTEGER, OnDate:Date )
  VAR m_IsDef = false, m_OnDate = OnDate;

  initCItemCacheFIID( FIID, CACHEFININSTR_FIDEFAULT );

  MACRO Calculate()
     VAR Avoiriss = TRecHandler( "avoiriss.dbt" ), 
         ValidFromDate = Date(0,0,0), NumInList = "";

     if( ПолучитьФинИн( m_FIID, null, Avoiriss ) ) 
        MsgBox ("Не найдена ценная бумага (DFININSTR_DBT.t_FIID =", m_FIID ,")" );
        return;
     end;

     if ( (GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(Avoiriss, OBJTYPE_AVOIRISS), OBJGROUP_AVRDEFAULT, null, null, NumInList, m_OnDate, ValidFromDate) == true)
          AND (ValidFromDate < m_OnDate)
          AND (NumInList == "1")
         )
         m_IsDef = true;
     end;
  END;
END;

CLASS (CItemCacheFIID) CAvrKindRoot( FIID:INTEGER )
  VAR m_AvrKindRoot = -1;

  initCItemCacheFIID( FIID, CACHEFININSTR_AVRKINDROOT);

  MACRO Calculate()
     var cmd = DL_RSDCommand( " select RSB_FIInstr.FI_AvrKindsGetRoot(FI.t_FI_Kind,FI.t_AvoirKind) root "
                             +"   from dfininstr_dbt FI "
                             +"  where FI.t_fiid = ?");
     cmd.AddParam(m_FIID);
     var dataSet = cmd.Execute();
                                                                       
     if( dataSet.movenext() )
        m_AvrKindRoot = SQL_ConvTypeInteger(dataSet.root);
     end;
  END;
END;

/*Кэшированный финансовый инструмент*/
CLASS TX_CacheFinInstr()
  PRIVATE VAR m_Cache = CCache();

  /* Получить параметры купонов для налогового учета*/
  MACRO GetCouponParms( FIID:INTEGER, H07:DATE, H08:DATE ):CTaxCouponParm
     return m_Cache.Get( CTaxCouponParm( FIID, H07, H08 ) );
  END;

  /* Получить номинал*/
  MACRO GetNominal( FIID:INTEGER ):CNominal
     return m_Cache.Get( CNominal( FIID ) );
  END;

  MACRO GetNalogGroup( FIID:INTEGER ):CNalogGroup
     return m_Cache.Get( CNalogGroup( FIID ) );
  END;

  MACRO GetISO( FIID:INTEGER ):CISONumber
     return m_Cache.Get( CISONumber( FIID ) );
  END;

  MACRO IsFIDefault( FIID:INTEGER, OnDate:Date ):CFIDefault
    return m_Cache.Get( CFIDefault( FIID, OnDate ) );
  END;

  MACRO GetAvrKindRoot( FIID:INTEGER ):CAvrKindRoot
     return m_Cache.Get( CAvrKindRoot( FIID ) );
  END;

  /* Получить номинал по условиям выпуска*/
  MACRO GetNominalOnIssued( FIID:INTEGER ):CNominalOnIssued
     return m_Cache.Get( CNominalOnIssued( FIID ) );
  END;

  MACRO Clear()
     m_Cache.Clear();
  END;

END;

/*****************************************************************************************************************************
  Кэширование данных по ISO (для валют)
*****************************************************************************************************************************/
/*Базовый для элементов кэша*/
PRIVATE CLASS (CItemCache) CItemCacheISO( ISO:INTEGER )
  VAR m_ISO = ISO;

  MACRO IsEqual( Obj:CItemCacheISO ):BOOL
     return ( m_ISO == Obj.m_ISO );
  END;

  initCItemCache();
END;

/*получить FIID по ISO*/
CLASS (CItemCacheISO) CFIIDbyISO( ISO:INTEGER )
  VAR m_FIID = -1;

  initCItemCacheISO(ISO);

  MACRO Calculate()
     var fin = TRecHandler("fininstr.dbt");
     if( ПолучитьФинИнПоISO(m_ISO, fin) )
        m_FIID = fin.rec.FIID;
     else
        MsgBox("Не найден финансовый инструмент по коду ISO: " + m_ISO);
        return;
     end;
  END;
END;

/*Кэшированный финансовый инструмент по ISO*/
CLASS TX_CacheISO()
  PRIVATE VAR m_Cache = CCache();

  MACRO GetFIID( ISO:STRING ):CFIIDbyISO
     return m_Cache.Get( CFIIDbyISO( ISO ) );
  END;

  MACRO Clear()
     m_Cache.Clear();
  END;

END;
