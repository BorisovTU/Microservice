/*
$Name:        nptxcalc100.mac
$Module:      Ценные бумаги
$Description: Операция расчета НОБ для НДФЛ. Шаг "Расчет НДР 8 уровня"
*/
IMPORT "secinter.mac", "nptxcalcfun.mac", "dlcontrsc.mac", "nptxstbfun.mac";

PRIVATE VAR Oper = TRecHandler( "nptxop.dbt" );

PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )
  VAR NewOper = TRecHandler( "nptxop.dbt" );
  VAR C1=$0, C2=$0, C3=$0, C4=$0, C5=$0, TaxPeriod, Dbeg;
  var err = 0;
  var ClientResident = false;
  var Technical = -1;
  var WhereCondContract = "";

  Oper.SetRecordAddr( FDoc );

  DateSplit(Oper.rec.OperDate, null, null, TaxPeriod);

  var TaxTeriodToLucre = CheckTaxTeriodToLucre(TaxPeriod);

  var pIIS = IIF( (Oper.rec.IIS == SET_CHAR), 1 , 0 );

  err = DL_NPTX_PutMsg( "Расчет НДР 8 уровня", 40 );
  //расчет суммы НОБ
  if(not err)
    Copy( NewOper, Oper );

    ClientResident = false;
    if( DL_GetPartyResident(Oper.rec.Client) == NPTXPARTY_RESIDENT )
      ClientResident = true;
    end;

    if( pIIS == 0 )
       Dbeg = date(1, 1, TaxPeriod);

       if((TaxPeriod >= 2021) and (ClientResident == true))
         if((Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_LUCRE) and (TaxTeriodToLucre == true))
           C1 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASEMATERIAL), Dbeg, Oper.rec.OperDate, null, pIIS);
         else
           C2 = GetRubSumByClient(Oper.rec.Client, TXOBJ_BASEG1+","+TXOBJ_BASEG2+","+TXOBJ_BASEG3+","+TXOBJ_BASEG4+","+TXOBJ_BASEG6+","+TXOBJ_BASEG7+","+TXOBJ_BASEG8+","+TXOBJ_BASEG9, Dbeg, Oper.rec.OperDate, null, pIIS);
           C3 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASESPECIAL), Dbeg, Oper.rec.OperDate, null, pIIS);
           C5 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASE_35), Dbeg, Oper.rec.OperDate, null, pIIS);
         end;
       else
         C1 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASEMATERIAL), Dbeg, Oper.rec.OperDate, null, pIIS);
         C2 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASEGENERAL), Dbeg, Oper.rec.OperDate, null, pIIS);
         C3 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASESPECIAL), Dbeg, Oper.rec.OperDate, null, pIIS);
         C4 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASESPECIAL_DIV), Dbeg, Oper.rec.OperDate, null, pIIS);
         C5 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASE_35), Dbeg, Oper.rec.OperDate, null, pIIS);
       end;
    else
       Dbeg = GetFirstDateIIS(Oper.rec.Client, Oper.rec.Contract);

       if(Oper.rec.Contract > 0)
         WhereCondContract =   "     obj.t_AnaliticKind6 = " + TXOBJ_KIND6020
                             + " and obj.t_Analitic6 IN (select mp.t_SfContrID from ddlcontrmp_dbt mp where mp.t_DlContrID = "+Oper.rec.Contract+") ";
       end;

       if((TaxPeriod >= 2021) and (ClientResident == true))
         C2 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASEG5), Dbeg, Oper.rec.OperDate, null, pIIS, NULL, WhereCondContract);
         C3 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASESPECIAL_IIS), Dbeg, Oper.rec.OperDate, null, pIIS, NULL, WhereCondContract);
       else
         C1 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASEMATERIAL_IIS), Dbeg, Oper.rec.OperDate, null, pIIS);
         C2 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASEGENERAL_IIS), Dbeg, Oper.rec.OperDate, null, pIIS);
         C3 = GetRubSumByClient(Oper.rec.Client, string(TXOBJ_BASESPECIAL_IIS), Dbeg, Oper.rec.OperDate, null, pIIS);
       end;
    end;

    NewOper.rec.TaxBase = C1 + C2 + C3 + C4 + C5;

    if( DL_NPTX_UpdateSumm( NewOper ) != 0 )
      return Error( "Ошибка при обновлении суммы НОБ в dnptxop_dbt" );
    end;

    DL_NPTX_SaveOperLog( Oper.rec.ID, 100 );

    if( not InsertOprStatus(46052, 8)) //Расчет НОБ = Завершен
      return Error( "Ошибка при установке статуса вида \"Расчет НОБ\" операции" );        
    end;

    var StartSTBDate = GetStartSTBDate();
    if((   (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_ENDYEAR) 
        or (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_CLOSE_IIS)
        or ((Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_LUCRE) and (TaxTeriodToLucre == true)) 
        or ((Oper.rec.Recalc == SET_CHAR) and (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_NORMAL))
       ) and
       (РАБОТА_С_ВНЕШНИМ_ХРАНИЛИЩЕМ_СНОБ()) and (StartSTBDate > date(0,0,0)) and 
       (Oper.rec.OperDate >= StartSTBDate) and
       (IsChildOp(Oper.rec.DocKind, Oper.rec.ID) == false)
      )
      var CurrYear = 0;

      datesplit(Oper.rec.OperDate, NULL, NULL, CurrYear);

      Technical = IsTechnical(Oper.rec.DocKind, Oper.rec.ID);
      
      if((Technical != 1) and 
         (    ((Oper.rec.IIS == UNSET_CHAR) and (Oper.rec.Recalc == SET_CHAR)) 
           or ((Oper.rec.IIS == UNSET_CHAR) and (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_ENDYEAR))
           or ((Oper.rec.IIS == UNSET_CHAR) and ((Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_LUCRE) and (TaxTeriodToLucre == true))) 
           or ((Oper.rec.IIS == SET_CHAR) and (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_CLOSE_IIS))
         )
        )
        if(Oper.rec.Recalc == SET_CHAR)
          if( not InsertOprStatus(46058, 1)) //Отмена прежних событий СНОБ = Выполнить
            return Error( "Ошибка при установке статуса вида \"Отмена прежних событий СНОБ\" операции" );        
          end;
        else

          if((TaxPeriod >= НП_ВВОДА_ДОРАБОТОК_BOSS_2981()) and STB_ClientNeedRecalc(Oper.rec.Client, TaxPeriod))
            if( not InsertOprStatus(46057, 1)) //Пересчет СНОБ = Выполнить
              return Error( "Ошибка при установке статуса вида \"Пересчет СНОБ\" операции" );        
            end;
          else
            var НОБ_опер = $0;

            //Проверить необходимость запроса СНОБ
           
            CalcNOBoper(Oper, @НОБ_опер);

            if(DL_InsertNPTXVAL(Oper.rec.DocKind, Oper.rec.ID, NPTXVAL_KIND_TB_Oper, date(0,0,0), time(0), НОБ_опер, 0) != 0)
              return Error( "Ошибка при сохранении значения СНОБ" );
            end;

            if(НОБ_опер == 0)
              if( not InsertOprStatus(46055, 1)) //Закрытие = Выполнить
                return Error( "Ошибка при установке статуса вида \"Закрытие\" операции" );        
              end;
            elif(НОБ_опер > 0)
              if( not InsertOprStatus(46054, 1)) //Запрос СНОБ = Выполнить
                return Error( "Ошибка при установке статуса вида \"Запрос СНОБ\" операции" );        
              end;
            elif(НОБ_опер < 0)
              if( not InsertOprStatus(46056, 1)) //Формирование и передача записи в хран. = Выполнить
                return Error( "Ошибка при установке статуса вида \"Формирование и передача записи в хран.\" операции" );        
              end;
            end;
          end;
        end;
      else
        if( not InsertOprStatus(46055, 1)) //Закрытие = Выполнить
          return Error( "Ошибка при установке статуса вида \"Закрытие\" операции" );        
        end;
      end;
    else
      if( not InsertOprStatus(46055, 1)) //Закрытие = Выполнить
        return Error( "Ошибка при установке статуса вида \"Закрытие\" операции" );        
      end;
    end;
  end;

  return err;
END;

/* Макрос постобработки */
MACRO PostStep (CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,          /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

  var query = "", cmd, DataSet;
  var NeedLoad = false;

  if (errTrn) 
      /* Произошла ошибка или происходит откат */
      return 0;
  end;
  
  Oper.SetRecordAddr( FDoc );

  var IIS = IIF( (Oper.rec.IIS == SET_CHAR), "'X'" , "CHR(0)" );
  
  if(CommitOrRollback != 2) // не откат

    if((Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_ENDYEAR) or
       (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_CLOSE_IIS)
      )
      NeedLoad = true;
    end;

    if((NeedLoad == false) and (Oper.rec.Recalc == SET_CHAR))
      query =   " select 1 "
              + "   from dnptxop_dbt npop "
              + "  where npop.t_DocKind = " + DL_CALCNDFL
              + "    and npop.t_ID != ? "
              + "    and npop.t_Client = ? "
              + "    and npop.t_OperDate <= ? "
              + "    and npop.t_OperDate >= ? "
              + "    and (   npop.t_SubKind_Operation = " + DL_TXBASECALC_OPTYPE_ENDYEAR
              + "         or (npop.t_IIS = ? and npop.t_SubKind_Operation = " + DL_TXBASECALC_OPTYPE_CLOSE_IIS + ") "
              + "         or Exists(select 1 "
              + "                     from doproper_dbt op "
              + "                    where op.t_DocKind = npop.t_DocKind "
              + "                      and op.t_DocumentID = LPAD(npop.t_ID, 34, '0') "
              + "                      and op.t_Parent_ID_Operation > 0 "
              + "                  )"
              + "        ) "
              + "    and rownum = 1 ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(Oper.rec.ID);
      cmd.AddParam(Oper.rec.Client);
      cmd.AddParam(Oper.rec.EndRecalcDate);
      cmd.AddParam(Oper.rec.BegRecalcDate);
      cmd.AddParam(IIS);

      if(cmd.GetCount() > 0)
        NeedLoad = true;
      end;

    end;

    if(NeedLoad == false)
      query =   " select 1 "
              + "   from doproper_dbt op "
              + "  where op.t_ID_Operation = ? "
              + "    and op.t_Parent_ID_Operation > 0 "; 

      cmd = DL_RSDCommand(query);
      cmd.AddParam(ID_Operation);

      if(cmd.GetCount() > 0)
        NeedLoad = true;
      end;
    end;

    if(NeedLoad == true)

      query =   "   AND SF.T_PARTYID = " + Oper.rec.Client
              + "   AND DLC.T_IIS = " + IIS
              + "   AND (SF.T_DATECLOSE = "+GetSQLDate(date(0,0,0))+" OR SF.T_DATECLOSE >= " + GetSQLDate(Oper.rec.OperDate) + ")";

      ВыгрузитьНОБ(query, ID_Operation, ID_Step, Oper.rec.OperDate, false);
    end;
  else //Откат

   query =   " select 1 "
           + "   from dnptxobs_dbt "
           + "  where t_ID_Operation = ? "
           + "    and t_ID_Step = ? "
           + "    and ROWNUM = 1"; 

   cmd = DL_RSDCommand(query);
   cmd.AddParam(ID_Operation);
   cmd.AddParam(ID_Step);

   DataSet = cmd.Execute();

   if(DataSet.moveNext())
     ВыгрузитьНОБ("", ID_Operation, ID_Step, Oper.rec.OperDate, true);
   end;

  end;
  
  return 0;

END;