/**                                                                                                             
 @file InvestRegisterDBO_Report.mac                                                                                           
 @brief Формирование отчета "Реестр договоров по инвестиционному консультированию"  

 #menu 
  - БО ЦБ\Отчеты\РСХБ\Инвест.советник\Реестр договоров по инвестиционному консультированию                                                                                                                                                                                                                          
                                                                                                                
 # tag                                                                                                          
 - functional_block:Отчетность_Внутренняя                                                                             
 - code_type:Report                                                                                                
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.01.31 |Жиц М.В.       |DEF-60756,DEF-60760 |BIQ-14887. Исправление недочётов логирования и вывода ошибок, добавлние комментариев                                                                                                                                                 
 |2023.12.15 |Шишкин Е.В.    |BIQ-14887           |Создание макроса для отчета                                                                                                                                                 
*/ 

import "InvestRegisterDBO_Form.mac", "dldlngfun.mac", "dl_report_log.mac", SFInter;

/**
 @brief Класс отчета Реестр договоров по инвестиционному консультированию
 @param[in] Parm класс параметров отчета
*/                    
PRIVATE CLASS (DL_CReportTemplate) SP_InvestRegisterDBO_Report(Parm:DL_CPanel)
  private var m_Parm = Parm;
  private var m_IsDataExist = false;
 
  var CurLog = DL_ReportLog(Report_InvestRegisterDBO, Date(), Time(), m_Parm.m_repdate);

  /**
   @brief Переопределение функции задания способа печати: всегда в Excel
  */                    
  private macro PrintMode():integer
    return DL_OUTREPORT_EXCEL;
  end;

  /**
   @brief Переопределение функции создания главной страницы
   @param[in] NumCopy номер страницы
  */                    
  private macro BeginReport(NumCopy:integer)
    Dl_CallInsertStat(PrintMode()); //Логирование в ddl_report_stat_dbt 

    SetActiveSheet("Лист1");
  end;

  /**
   @brief Печать шапки отчета
   @param[in] ReportDate дата отчета
  */                    
  private macro PrintReportHead(ReportDate:date)

    PrintFormatString("", "N1_RepDate", DateToStr(ReportDate));
    PrintFormatString("", "N1_SysDate", string(Date():f) + " " + Time());

    RegisterTable( "N1_MainTable", "", 
                   "N1_A1", 
                   "N1_A2", 
                   "N1_A3", 
                   "N1_A4", 
                   "N1_A5", 
                   "N1_A6",
                   "N1_A7", 
                   "N1_A8"
                 );
  end;

  /**
   @brief Печать строки отчета
   @param[in] dataSet структура параметров из запроса в рамках одного подключения советника
  */                    
  private macro PrintLine(dataSet) 
    PrintTableLine( "N1_A1",  dataSet.t_number,                null,
                    "N1_A2",  dataSet.t_ShortName,             null,
                    "N1_A3",  dataSet.t_EKK,                   null,
                    "N1_A4",  date(dataSet.t_ContrBeginDate),  null, 
                    "N1_A5",  date(dataSet.t_ContrEndDate),    null,
                    "N1_A6",  dataSet.t_IsInvestAdviser,       null, 
                    "N1_A7",  date(dataSet.t_InvestBeginDate), null,
                    "N1_A8",  date(dataSet.t_InvestEndDate),   null 
                  );
  end;      

  /**
   @brief Сбор данных и циклическая печать отчета
  */                    
  private macro ExecuteReport()
    var query, cmd, dataSet;
    var cnt = 0, i = 0;

    query = " SELECT sf.t_Number,                                                               "+
            "         pt.t_ShortName,                                                           "+
            "         RSB_SECUR.                                                                "+
            "          SC_GetDlObjCodeOnDate ("+OBJTYPE_BROKERCONTR_DL+",                       "+                                
            "                                 1, /*Единый краткий код*/                         "+
            "                                 dl.t_DlContrID, ?)                                "+
            "                                 t_EKK,                                            "+
            "         sf.t_DateBegin t_ContrBeginDate,                                          "+
            "         CASE                                                                      "+
            "            WHEN sf.t_DateClose > ?                                                "+
            "            THEN                                                                   "+
            "               TO_DATE ('01.01.0001', 'dd.mm.yyyy')                                "+
            "            ELSE                                                                   "+
            "               sf.t_DateClose                                                      "+
            "         END                                                                       "+
            "            t_ContrEndDate,                                                        "+
            "         CASE                                                                      "+
            "            WHEN serv.t_EndDate = TO_DATE ('01.01.0001', 'dd.mm.yyyy')             "+
            "                 OR serv.t_EndDate > ?                                             "+
            "            THEN                                                                   "+
            "               'Да'                                                                "+
            "            ELSE                                                                   "+
            "               'Нет'                                                               "+
            "         END                                                                       "+
            "            t_IsInvestAdviser,                                                     "+
            "         serv.t_BeginDate t_InvestBeginDate,                                       "+
            "         CASE                                                                      "+
            "            WHEN serv.t_EndDate > ?                                                "+
            "            THEN                                                                   "+
            "               TO_DATE ('01.01.0001', 'dd.mm.yyyy')                                "+
            "            ELSE                                                                   "+
            "               serv.t_EndDate                                                      "+
            "         END                                                                       "+
            "            t_InvestEndDate                                                        "+
            "    FROM ddlcontrserv_dbt serv,                                                    "+
            "         ddlcontr_dbt dl,                                                          "+
            "         dsfcontr_dbt sf,                                                          "+
            "         dparty_dbt pt                                                             "+
            "   WHERE     serv.t_BeginDate <= ?                                                 "+
            "         AND serv.t_ServKind = " + DLCONTR_SERVKIND_INVESTADVICE                    +
            "         AND serv.t_DlContrID = dl.t_DlContrID                                     "+
            "         AND dl.t_SfContrID = sf.t_ID                                              "+
            "         AND sf.t_PartyID = pt.t_PartyID                                           "+
            " ORDER BY sf.t_Number, sf.t_DateBegin                                              ";

    cmd = DL_RSDCommand(query);
    cmd.AddParam(m_Parm.m_repdate);
    cmd.AddParam(m_Parm.m_repdate);
    cmd.AddParam(m_Parm.m_repdate);
    cmd.AddParam(m_Parm.m_repdate);
    cmd.AddParam(m_Parm.m_repdate);
    
    cnt = cmd.GetCount();

    if(cnt > 0)
      CurLog.AddLogRow("Поиск договоров брокерского обслуживания с подключенной услугой ""Инвестиционное консультирование"" на отчетную дату "+m_Parm.m_repdate, 
                       "Договоры брокерского обслуживания с подключенной услугой ""Инвестиционное консультирование"" найдены");

      InitProgress(cnt, "Отчет \"Отчет о договорах БО с подключенной услугой Инвестиционное консультирование\"", "Формирование отчета");
      
      dataSet = cmd.Execute();
      PrintReportHead(m_Parm.m_repdate);
      m_IsDataExist = true;

      while(dataSet.MoveNext())
        PrintLine(dataSet);
        i = i + 1;
        UseProgress(i);
      end;

      EndTable();
      RemProgress;
    else
      CurLog.AddLogRow("Поиск договоров брокерского обслуживания с подключенной услугой ""Инвестиционное консультирование"" на отчетную дату "+m_Parm.m_repdate, 
                       "Договоры брокерского обслуживания с подключенной услугой ""Инвестиционное консультирование"" НЕ найдены");
      m_IsDataExist = false;
    end;
    
    CurLog.EndLoggingTable(); 
    
  OnError(e)
    CurLog.ErrorHistoryRec(); 
    msgbox(cmd.GetErrorString(e));
    RemProgress;
  end;

  /**
   @brief Переопределение функции создания отчета
  */                    
  private macro Create()
    CurLog.BeginLogging(); 
    ExecuteReport();
    CurLog.EndLogging(); 
  end;

  /**
   @brief Проверка наличия данных в выборке
   @return true, если есть данные для выпуска отчета
   @return false, если данных нет
  */                    
  macro CReport_DataExist()
    return m_IsDataExist;
  end;

  initDL_CReportTemplate(null, "Report_InvestRegisterDBO.xlsx");  
END;

/**
 @brief Вызов панели и самого формирования отчета "Реестр договоров по инвестиционному консультированию"
*/                    
MACRO ReportRunEx()
  var stat = true;
  var report = null; 

  var param = InvestRegisterDBO_Panel();

  while(stat)
    stat = param.Run();

    if(stat)
      report = SP_InvestRegisterDBO_Report(param);      
      report.Run();
      if(not report.CReport_DataExist())
        msgbox("Нет данных для выпуска отчета");
      end;
    end;
  end;
END;

ReportRunEx();
Exit(1);
