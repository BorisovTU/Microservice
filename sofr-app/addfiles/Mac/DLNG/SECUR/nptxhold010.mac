/*
$Name:        nptxhold010.mac
$Module:      Ценные бумаги
$Description: Операция удержания НДФЛ. Шаг настройки
*/

/* Операция удержания НДФЛ
   Шаг настройки*/
Import InsCarryDoc, DealsInter, OprInter, "dlquery.mac", "nptxstbfun.mac";

PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr );
  MsgBox( ErrStr );
  return 1;
END;

macro executeParamStep( kind_oper, BOf_kind, FDoc )

  VAR Oper = TRecHandler( "nptxop.dbt" );
  var pt = TBFile("party.dbt");
  var cmd, query;
  var Year, EndDate;

  Oper.SetRecordAddr( FDoc );

  DateSplit(Oper.rec.PrevDate, null, null, Year);

  if( (Oper.rec.Method == DL_TYPEGETNALOG_BROKER_ACC) AND (Oper.rec.Account == "") AND (Oper.rec.SubKind_Operation != DL_TXHOLD_OPTYPE_ENDYEAR) AND (Oper.rec.SubKind_Operation != DL_TXHOLD_OPTYPE_LUCRE))          
    MsgBox( "Необходимо задать счет для удержания НДФЛ в форме операции." );
    return 1;
  end;

  if(Oper.rec.Subkind_Operation == DL_TXHOLD_OPTYPE_TAXREF) //возврат налога
    query =   " SELECT 1 "
            + "  FROM dnptxop_dbt "
            + " WHERE T_DocKind = "+DL_RETREQ
            + "   AND T_STATUS IN ("+DLNPTXRETREQ_CHECKED+", "+DLNPTXRETREQ_PARTEXECUTED+") "
            + "   AND t_OperDate <= ? "
            + "   AND T_CLIENT = ? ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(Oper.rec.OperDate);
    cmd.AddParam(Oper.rec.Client);

    if(cmd.GetCount() == 0)
      pt.Clear();

      pt.rec.PartyID = Oper.rec.Client;
      pt.GetEQ();

      if (not GetTRUE(false, "У клиента "+pt.rec.Name+" отсутствует заявление на возврат налога. Продолжить операцию?"))
        return 1;
      end;
    end;
  end;

  EndDate = date(31,12,Year);
  if(EndDate > Oper.rec.OperDate)
    EndDate = Oper.rec.OperDate;
  end;

  var StartSTBDate = GetStartSTBDate();
  if((РАБОТА_С_ВНЕШНИМ_ХРАНИЛИЩЕМ_СНОБ()) and (StartSTBDate > date(0,0,0)) and (EndDate >= StartSTBDate))
    var CurrYear = 0;

    datesplit(EndDate, NULL, NULL, CurrYear);
    if(Oper.rec.Subkind_Operation == DL_TXHOLD_OPTYPE_OUTMONEY)
      if( not InsertOprStatus(46081, 17)) //Расчет НОБ
        return Error( "Ошибка при установке статуса вида \"Пересчет СНОБ\" операции" );        
      end;
    elif(STB_ClientNeedRecalc(Oper.rec.Client, CurrYear))
      if( not InsertOprStatus(46081, 11)) //Пересчет СНОБ
        return Error( "Ошибка при установке статуса вида \"Пересчет СНОБ\" операции" );        
      end;
    elif((Oper.rec.SubKind_Operation != DL_TXHOLD_OPTYPE_ENDYEAR) and (Oper.rec.Subkind_Operation != DL_TXHOLD_OPTYPE_TAXREF) and (NeedNptxNettOp(Oper.rec.Client, CurrYear, Oper.rec.OperDate)))
      if( not InsertOprStatus(46081, 12)) //Зачет удержанного НДФЛ = Выполнить
        return Error( "Ошибка при установке статуса вида \"Зачет удержанного НДФЛ\" операции" );        
      end;
    else
      if( not InsertOprStatus(46081, 9)) //Запрос СНОБ
        return Error( "Ошибка при установке статуса вида \"Запрос СНОБ\" операции" );        
      end;
    end;
  else
    if( not InsertOprStatus(46081, 10)) //Расчет сумм и корректировка
      return Error( "Ошибка при установке статуса вида \"Расчет сумм и корректировка\" операции" );        
    end;
  end;

  return 0;
end;