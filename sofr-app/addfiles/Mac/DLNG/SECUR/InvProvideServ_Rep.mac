/**                                                                                                             
 @file InvProvideServ_Rep.mac                                                                                           
 @brief Отчет о задолженности по комиссиям.  

 #menu 
  - БО ЦБ\Отчеты\РСХБ\Инвест.советник\Отчет по инвестиционному консультированию                                                                                                                                                                                                                          
                                                                                                                
 # tag                                                                                                          
 - functional_block:Отчетность_Клиент                                                                                                                                                       
 - code_type:Report                                                                                                
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.01.09 |Шишкин Е.В.    |BIQ-14887           |Создание макроса для отчета                                                                                                                                                 
 |2024.02.21 |Шишкин Е.В.    |DEF-62156           |Изменение даты оплаты комиссии                                                                                                                                                 
 |2024.02.21 |Шишкин Е.В.    |DEF-62130           |Исправлена дата оплаты комиссии для отключенной услуги  
 |2024.02.22 |Жиц М.В.       |DEF-62182           |Исправлено перетирание лога выпуска отчета по расшифровке                                                                                                                                                                                                                                                                                                
*/ 
 

Import rsd, RsbDataSet;
import "dl_report_log.mac";
import "dlwreps.mac";

CONST ACCOUNTING_CALENDAR = 10003;


/**
 @brief Класс с параметрами введенными в панеле отчета.
*/                    

CLASS InvProvideServParams()
 var  BegDate:date,            // Начальная дата
      EndDate:date,            // Конечная дата
      ClientID:integer   = 0,                   // Клиент
      DlContrID:integer  = 0,                    // ДБО
      WordFormat:bool   = true,                // печатать в word
      ShowFile:bool      = true,                // показать отчет
      quarter:integer; 

END;

/**
 @brief Класс отчета о задолженности по комиссиям.
 @param[in] Params параметры из панели
*/                    

class InvProvideServ_PrintRep(Params)

   var m_Rep_Params = Params;
   const wdFormatPDF = 17;

   private var m_CurLog = DL_ReportLog(Report_InvProvideServ, Date(), Time(), m_rep_params.BegDate, m_rep_params.EndDate); 
   private var IsNtr = isStandalone();

   var SumComiss,
       SumComiss_str,
       FractionalPart,
       ContrNumber,     
       Sysdate,         
       Name,            
       Surname,
       IsMale,  
       BeginDateInvest, 
       EndDateInvest,
       ConcDate;   
   
   private var PATH_OUT;
   private var FileDirForm;
   var Mess = "";
   
   var TemplName = "";

   var Templ_Dir = "";

   var statMes = "";
   var statsend = 0;
                      
   var FolderName = "";
   var FormFilename = "";
   var w_ext = ".docx";

   var NumberSend;
   var FullSumComiss:money = 0;

   private macro GetFullPath( Path )
       var fullpath;
       if( substr( Path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
         Path = substr(Path, 3 );
       end;
       if( substr( Path, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
         Path = substr(Path, 2 );
       end;
       fullpath =   strsubst( GetCurDir(false) , "\\Obj","")  + "\\" + Path ; 
       return fullpath;
   end;;

/**
 @brief Сбор данных 
*/                    

   macro constructor()           
      var sqls, rsdc, rsds, StrErr;
      statMes = "";
      statsend = 0;

      sqls = String( 
                      "SELECT SUM (base.t_comsum) AS comsum,                                           \n"
                      "       contr.t_number,                                                          \n"
                      "       contr.t_concdate,                                                        \n"
                      "       contr.t_begindate,                                                       \n"
                      "       decode(contr.t_enddate,to_date('01.01.0001','dd.mm.yyyy'),:DateEnd1,contr.t_enddate) as enddate    \n"
                      "  FROM DDLCONTRSERV_DBT contr, DDL_BASESUM_DBT base                             \n"
                      " WHERE     base.t_clientid = :Clientid                                          \n"
                      "       AND base.t_dlcontrid = contr.t_dlcontrid                                 \n"
                      "       AND  contr.t_dlcontrid = :DlContrID  -- id  из панели                    \n"
                      "       AND contr.t_ServKind = 1                                                 \n"
                      "       AND base.t_date BETWEEN :DateBegin AND :DateEnd                          \n"
                      "       AND contr.t_begindate < base.t_date                                      \n"
                      "       AND (contr.t_enddate >= base.t_date or contr.t_enddate = to_date('01.01.0001','dd.mm.yyyy'))          \n"
                      "       AND base.t_state = chr(0)                                                \n"
                      "       AND base.t_feetype = 1                                                   \n"
                      " GROUP BY contr.t_number,                                                       \n"
                      "       contr.t_concdate,                                                        \n"
                      "       contr.t_begindate,                                                       \n"
                      "       contr.t_enddate                                                          \n"
                      "       order by contr.t_begindate                                               \n" 
                                        );

      rsdc = rsdcommand(sqls);

      rsdc.AddParam("DateEnd1", RSDBP_IN, m_rep_params.EndDate);                                            
      rsdc.AddParam("Clientid", RSDBP_IN, m_rep_params.ClientID);                                            
      rsdc.AddParam("DlContrID", RSDBP_IN, m_rep_params.DlContrID);                                            
      rsdc.AddParam("DateBegin", RSDBP_IN, m_rep_params.BegDate);                                            
      rsdc.AddParam("DateEnd", RSDBP_IN, m_rep_params.EndDate);                                            

      rsds = rsdrecordset(rsdc, RSDVAL_CLIENT_IF_NEEDED, RSDVAL_FORWARD_ONLY);

      if (rsds.moveNext())
          FullSumComiss   = rsds.value("comsum");
          ContrNumber     = rsds.value("t_number");
          Sysdate         = date();
          BeginDateInvest = date(rsds.value("t_begindate"));
          EndDateInvest   = date(rsds.value("enddate"));
          ConcDate        = date(rsds.value("t_concdate"))
      else
          runerror("Ошибка получения данных по договору","Нет данных для формирования отчета");
      end;        

      sqls = null; rsdc = null; rsds = null;

      sqls = "select  pers.t_name2, pers.t_name3, decode(pers.t_ismale, chr(88),1,0) as ismale from DPERSN_DBT pers where t_personid = :Clienid ";

      rsdc = rsdcommand(sqls);
      rsdc.AddParam("Clientid", RSDBP_IN, m_rep_params.ClientID);                                            

      rsds = rsdrecordset(rsdc, RSDVAL_CLIENT_IF_NEEDED, RSDVAL_FORWARD_ONLY);

      if (rsds.moveNext())
          Name    = rsds.value("t_name2");
          Surname = rsds.value("t_name3");
          IsMale  = rsds.value("ismale");
      else
          runerror("Ошибка получения данных по договору","Нет данных для формирования отчета");
      end;        

      SumComiss = int(double(FullSumComiss));
      rubtostralt(SumComiss,SumComiss_str);

      FractionalPart = int((FullSumComiss - money(SumComiss)) * 100);
      if(FractionalPart == 0)
        FractionalPart = "00";
      end;  

      PATH_OUT = "";

      GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ИНВЕСТ_СОВЕТНИК\\ДИРЕКТОРИЯ_КЛИЕНТСКИЙ_ОТЧЕТ", V_STRING, PATH_OUT, StrErr);

      GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEMPLSDIR", V_STRING, Templ_Dir, StrErr);
      FileDirForm = "..\\txtFile\\";

      FormFilename = "Отчет об оказанных услугах по инвестиционному консультированию к договору " + ContrNumber;
      FormFilename = strSubst(FormFilename,"\"","");
      FormFilename = strSubst(FormFilename,"\\","-");
      FormFilename = strSubst(FormFilename,"/","-");
      FormFilename    = FormFilename + w_ext;

      sqls = null; rsdc = null; rsds = null;
   onerror(e);
      sqls = null; rsdc = null; rsds = null;
      if ((e.Err != null) or (e.Err != nullval))
         statMes = e.Err;
      else 
         statMes = e.Message;
      end;
   end;

/**
 @brief получение полного имени файла из относительного
*/                    

   macro abs_path(p)
      var i = 0, l = 0, pth = "";
      if (substr(p, 1, 1) != ".")
         return p;
      end;        
      if (IsNtr)
         pth = GetCurDir(false);
         while (true)
            i = index(pth, "\\", l + 1);
            if (i == 0)
               break;
            else
               l = i;
            end;
         end;       
         if (l > 0)          
            pth = substr(pth, 1, l - 1);
         end;              
      else
         pth = GetCurDir(true);
      end;
      p = strsubst(p, "..", "");
      return string(pth, p); 
   end;

/**
 @brief коррекция терминального имени для копирования
 @param[in] путь к фалу
 @return скорректированный путь к фалу без $
*/                    

   macro fnam4copy(p)
   if (not isNtr)
      if (substr(p, 1, 1) == "$")
         return p;
      end;
      return string("$", p);
   end;
   return p;
   end;

   macro fill_docx()
      var txtfile = "..\\txtfile\\bmk_file.txt";
      var DATEPAYMCOMISS,StrErr;
      GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ИНВЕСТ_СОВЕТНИК\\РАБ.ДНЕЙ ДО ОПЛАТЫ", V_INTEGER, DATEPAYMCOMISS, StrErr);
	  var hour, minute, sec;
      var day, month, year;
	  DateSplit( Date(), day , month , year );
      TimeSplit( time(), hour, minute, sec  );
      var dateTime = year + "-" + month + "-" + day + " " + hour + ":" + minute + ":" + sec;

      SetOutPut(txtfile, false);
      [InvProvideServ.docx];
      println("Отчет об оказанных услугах по инвестиционному консультированию к договору " + ContrNumber +"  " + dateTime);

      [~Номер_соглашения];
      println(ContrNumber);

      [~дата_соглашения];
      println(ConcDate:f);

      [~дата];
      println(Sysdate:f);

      [~Обращение];
      if(Ismale == 1)
         println("Уважаемый");
      else
         println("Уважаемая");
      end;

      [~Имя];
      println(Name);

      [~Отчество];
      println(SurName);

      [~Номер_соглашения2];
      println(ContrNumber);

      [~дата_соглашения2];
      println(ConcDate:f);

      [~дата_начала];
      if(BeginDateInvest > m_rep_params.begdate)
         println(BeginDateInvest:f);
      else
         println(m_rep_params.begdate:f);
      end;

      [~дата_окончания];
      if(m_rep_params.enddate > EndDateInvest)
         println(EndDateInvest:f);
      else
         println(m_rep_params.enddate:f);
      end;

      [~сумма];
      println(SumComiss);

      [~сумма_прописью];
      println(SumComiss_str);

      [~дробная_часть];
      println(FractionalPart);

      [~конечная_дата];
      println(GetDateAfterWorkDays(EndDateInvest, DATEPAYMCOMISS, ACCOUNTING_CALENDAR):f);

      txtfile = SetOutput(null, false);

      return DLWREPS_PrintReportFromTagFile(txtfile, @Mess);
   end;

/**
 @brief функция формирования текста уведомления
*/                    

   macro Head_Form(p_Templ)
   var count = 0, str = "", i = 0;
   var tpl_nam = "";
   //доставить шаблон в место формирования
   tpl_nam = FindPath(toANSI(p_Templ), Templ_Dir);
   if (strlen(trim(tpl_nam)) == 0)
      runerror("Ошибка поиска шаблона", "Ошибка поиска шаблона " + p_Templ);
   end;

   if (NOT fill_docx())
      str = "Error";
   end;

   return str;
   onerror(e)
      if (e.Err)
         if (IsEqClass ("TRsComErr", e.err))
            count = e.err.Count;
            i = 0;
            while (i < count)
               str = string(str, e.err.Message(i), " (Code = ",e.err.Code(i), ", Level = ",e.err.Level(i), ")", strfor(10));
               i = i + 1
            end;
            str = str + " " + e.Module + " " + e.Line + " " + e.Message;
         elif (valtype(e.err) == v_string)
            str = e.err;
         else
            str = e.Module + " " + e.Line + " " + e.Message;
         end;  
      else
         str = e.Module + " " + e.Line + " " + e.Message;
      end;    
      return str;
   end;

   macro CreateReport()
      var FileFromDOC; 

      macro remove_tmp()
         RemoveFile(FileFromDOC);
      end;

      m_CurLog.BeginLogging(); 
   
      if (strlen(trim(statMes)) > 0)
         m_CurLog.AddLogRow("Поиск задолженностей по комиссиям за отчетный период c " + BeginDateInvest + "по " + EndDateInvest, 
                            "Задолженности по комиссии НЕ найдены"); 
         runerror("Ошибка формирования отчета", "Ошибка формирования отчета: " + statMes);
      else
         m_CurLog.AddLogRow("Поиск задолженностей по комиссиям за отчетный период c " + BeginDateInvest + "по " + EndDateInvest, 
                            "Задолженности по комиссии найдены"); 
      end;

      m_CurLog.EndLoggingTable(); 
      m_CurLog.EndLogging(); 

      TemplName   = "InvProvideServ" + w_ext;
      statmes = Head_Form(TemplName);

      // локальный путь для трехзвенки
      FileFromDOC = fnam4copy(Mess);
     
      if (strlen(trim(statMes)) > 0)
         runerror("Ошибка формирования отчета", "Ошибка формирования отчета: " + statMes);
      end;

      if (PATH_OUT != "") //Если путь до сетевой папки не задан, то и копировать никуда не будем
         FolderName = "\\" + replace(replace(date(),".",""), " ", "0");
         MakeDir(string(PATH_OUT, FolderName));

         // перенос файла
         if (not CopyFile(Mess, PATH_OUT+FolderName+"\\"+FormFilename))
            runerror("Ошибка формирования отчета", "Не удалось скопировать файл " + Mess + " в директорию " + string(PATH_OUT, FolderName));
         end;
      end;

     return 0;

   onerror(e)   
      if ((e.Err != null) and (e.Err != nullval))
         statMes = e.Err;
      else   
         statMes = e.Message;
      end;   

      remove_tmp();  
      m_CurLog.ErrorHistoryRec(); 
      msgbox(statMes);

      return -1;
   end;

   constructor();
end;
  
MACRO CreateRepInvestProvideServ(params)

    var i = 0;

    InitProgress( "Формирование отчета", "Формирование отчета");

    InvProvideServ_PrintRep(Params).CreateReport();

    UseProgress(i = i + 1);

    RemProgress();

END;
