/*                           
$Name:             sc_srvrepbrk.mac
$Module:           Ценные бумаги
$Description:      Выполнение сервисной операции отправки отчета брокера в части формирования отчета
*/
import "scstartsrvrep.mac", "BrokerRepRSHB_Report.mac", "BrokerRepRSHBNew_Report.mac", "brokcurr.mac", "conveyerfun.mac";

private macro GetDlContrID(SfContrID:integer)
  var query, cmd, DataSet;
  var DlContrID = 0;

  if(SfContrID > 0)
    query = "select t_DlContrID from ddlcontr_dbt where t_SfContrID = ? ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(SfContrID);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      DlContrID = DataSet.DlContrID;
    end;
  end;

  return DlContrID;
end;

private macro _ExecuteSrvRepBrk(_taskID, _execID, _conveyerID, _operationID, _packetID, srvrep, DlContrID, no_data : @bool, SignerParty)
  var query, cmd, DataSet;
  var cnt = 0;
  var err = 0, stat = 0;
  var params = NULL;
  var srvrepobjCache = RsbSQLInsert("scsrvrepobj.dbt");
  var srvrepobjCache1 = RsbSQLInsert("scsrvrepobj.dbt");
  var srvrepobj = TRecHandler("scsrvrepobj.dbt");
  var RetParmArr = TArray();
  var i = 0;
  var cm_no_data    = false;
  var stock_no_data = false;

  cmd = DL_RSDCommand();
   
  query =   " select sf.t_PartyID as t_ClientID, dlc.t_SfContrID, "
          + "   CASE WHEN  exists (select 1"
          + "               from dscsrvrepobj_dbt repobj "
          + "              where repobj.t_ServDocID = ? "
          + "                and repobj.t_ClientID = sf.t_PartyID "
          + "                and repobj.t_ContractID = sf.t_ID "
          + "                and repobj.t_Format = " + SC_SRVREPFORMAT_PDF
          + "            ) then 1 else 0 END as ExistsPdfFormat, "
          + "   CASE WHEN  exists(select 1"
          + "               from dscsrvrepobj_dbt repobj "
          + "              where repobj.t_ServDocID = ? "
          + "                and repobj.t_ClientID = sf.t_PartyID "
          + "                and repobj.t_ContractID = sf.t_ID "
          + "                and repobj.t_Format = " + SC_SRVREPFORMAT_EXCEL_XLSX
          + "            ) then 1 else 0 END as ExistsExcelFormat "
          + "   from ddlcontr_dbt dlc, dsfcontr_dbt sf "
          + "  where dlc.t_DlContrID = ? "
          + "    and sf.t_ID = dlc.t_SfContrID ";  
  
  cmd.AddParam(srvrep.rec.ID);
  cmd.AddParam(srvrep.rec.ID);
  cmd.AddParam(DlContrID);

  var isNew = false;
  if (srvrep.rec.DocKind == SP_SRVBROKERREPNEW)
    isNew = true;
  end;

  params = CBrokerRepRSHBParams(isNew);
  
  params.ReportDate    = srvrep.rec.Date;  
  params.PeriodKind    = srvrep.rec.PeriodKind;
  params.BegDate       = srvrep.rec.BegDate; 
  params.EndDate       = srvrep.rec.EndDate; 
  params.SrvRepId      = srvrep.rec.Id; 
  params.IsMarket      = IIF(srvrep.rec.ByExchange    == SET_CHAR, true, false);
  params.IsOutMarket   = IIF(srvrep.rec.ByOutExchange == SET_CHAR, true, false);
  params.IsFiMovement  = IIF(srvrep.rec.ByMoving      == SET_CHAR, true, false);     
  params.IsNotZeroRest = IIF(srvrep.rec.ByNotNullRest == SET_CHAR, true, false);
  params.IsApp         = true;

  params.ShowFile      = false;
  params.InCnv         = IIF(_conveyerID, true, false);
  params.InServOp      = true;
  params.SignerParty   = SignerParty;

 //dan показывать плановый остаток
  params.ShowPlanRest  = True;

  DataSet = cmd.Execute(query);
  stat = DataSet.moveNext();
  if(stat)
    SrvRepCurrData.SetClient(DataSet.ClientID);

    params.ClientID    = DataSet.ClientID;
    params.DlContrID   = DlContrID;
    params.PdfFormat   = IIF(((srvrep.rec.PdfFormat == SET_CHAR) and (DataSet.ExistsPdfFormat == 0)), true, false);  
    params.ExcelFormat = IIF(((srvrep.rec.ExcelFormat == SET_CHAR) and (DataSet.ExistsExcelFormat == 0)), true, false);

    var email = SC_GetClientRepEmail(DataSet.ClientID, DataSet.SfContrID);

    if (srvrep.rec.DocKind == SP_SRVBROKERREPNEW)
      SC_CreateRepBrokerRSHBNew(params);
    elif (srvrep.rec.DocKind == SP_SRVBROKERREP)
      SC_CreateRepBrokerRSHB(params);
    end;

    stock_no_data = params.NoData;

    //Отправка сообщений об остутствующих заявках
    if(params.NoReq)
      while(i < params.ErrMails.Size)
        SC_SendBrkRepErrMail(params.ErrMails[i].Subject, params.ErrMails[i].Body);
        i = i + 1;
      end;
    end;


 
    i = 0;
    if(params.RepData.size > 0)
      while(i < params.RepData.size)

        srvrepobj.Clear();
        srvrepobj.rec.ServDocID  = srvrep.rec.ID; 
        srvrepobj.rec.ClientID   = DataSet.ClientID;
        srvrepobj.rec.CreateDate = params.RepData[i].CreateDate;
        srvrepobj.rec.CreateTime = params.RepData[i].CreateTime;
        srvrepobj.rec.FileName   = params.RepData[i].NameFile;
        srvrepobj.rec.Format     = params.RepData[i].Format;
        srvrepobj.rec.email      = email;

        if(params.RepData[i].DlContrID > 0)
          var dlcontr = TBFile("dlcontr.dbt");

          dlcontr.Clear();
          dlcontr.rec.DlContrID = params.RepData[i].DlContrID;
          if(dlcontr.GetEQ())
            srvrepobj.rec.ContractID = dlcontr.rec.SfContrID;
          end;
        end;

        srvrepobjCache.AddRecord(srvrepobj);

        i = i + 1;
      end;

      srvrepobjCache.Insert();
    end;
  end;
  no_data = no_data or (cm_no_data and stock_no_data);

  err = SignAndSendForAutoSend(srvrep, DataSet.SfContrID, err);

  return err;

OnError(er)

  SrvRepErrArr[SrvRepErrArr.size] = CSC_SrvRepErr(GetLargeErrorMessage(er));
  SaveSrvRepErrForReport_XML(SrvRepErrArr, srvrep.rec.ID, srvrep.rec.DocKind);

  return 1;
end;


macro ExecuteSrvRepBrk(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params)
  var srvrep = TBFile("scsrvrep.dbt");
  var RetParmArr = TArray();
  var query, cmd, DataSet;
  var err = 0, curErr = 0;
  var i = 0;
  var errmsg = "";
  var taskProfiler = null;

  SrvRepErrArr.size = 0;
  ReplaceMacro( "msgbox", "SrvRep_msgbox" );

  /*выбор подписанта*/
  var SignerParty = -1, Choice, SetDialogFlagVal;
  //выбор и проверки только для не конвейера, т.к. в конвейере мы подписанта получаем раньше
  if(not((_conveyerID) and (_packetID)))
    var sql_s = "SELECT p.t_oper, p.t_name, p.t_partyid FROM dperson_Dbt p " +
                " WHERE p.t_partyid <> -1 AND EXISTS " +
                "              (SELECT 1 " +
                "                 FROM dnotetext_dbt n " +
                "                WHERE     n.t_objecttype = 3 " +
                "                      AND n.t_notekind = 107 " +
                "                      AND n.t_documentid = LPAD (p.t_partyid, 10, '0') " + 
                "                      AND ascii(RSB_STRUCT.GETSTRING(n.t_text)) != 0) " +
                "  AND p.t_userClosed <> CHR(88) " +
                "ORDER BY p.t_name ";
  
    var Dt = TRsbDataSet(sql_s);
    if(not Dt.movenext())
       MsgBox("Не найдены доступные для отчета подписанты");
       return false;
    else
       SignerParty = Dt.partyid;
       SetDialogFlagVal = SetDialogFlag(1);
       if ( (SetDialogFlagVal == 1) and (_conveyerID == 0) )
          Choice = DL_Scroll(sql_s, " Выбор подписанта ", 15, 15, "t_oper", "N", "t_Name", "Подписант");
                         
          if( Choice != NULL )
            SignerParty = Choice.Value("t_partyid"); 
          end;
       elif (GetDefaultSigner(@SignerParty, @errmsg) != 0)
          MsgBox(errmsg);
          return false;
       end;
       SetDialogFlag(SetDialogFlagVal);
    end;
  else
    SignerParty = _Params.Signer;
  end;

  if(_Params.ID > 0)

    srvrep.rec.ID = _Params.ID;
    if(srvrep.GetEQ())
      var isStock = FALSE;
      IF (srvrep.rec.ByStock == SET_CHAR)
        isStock = TRUE;
      end;

      if((_conveyerID) and (_packetID))
        var conveyer_no_data = false;
        var DlContrID = 0;

        query = "select t_ObjectID as DlContrID from dcnvdoc_dbt where t_ExecID = ? and t_ConvTypeID = ? and t_PackID = ?";

        cmd = DL_RSDCommand(query);

        cmd.AddParam(_execID);
        cmd.AddParam(_conveyerID);
        cmd.AddParam(_packetID);

        DataSet = cmd.Execute();
        while(DataSet.moveNext())
          taskProfiler = ConvExecProfiler(_taskID, DataSet.DlContrID);
          curErr = _ExecuteSrvRepBrk(_taskID, _execID, _conveyerID, _operationID, _packetID, srvrep, DataSet.DlContrID, @conveyer_no_data, SignerParty);
          taskProfiler = null;
          if ((curErr == 0) and conveyer_no_data) 
            curErr = 1; 
          end;
          err = IIF((curErr or err), 1, 0);
        end;
      else
        ПодготовкаДанныхОтчетаБрокера(srvrep);

        InitProgress(-1,"Вычисление списка клиентов");
        query = "select count(*) over() as rec_cnt, "+
                "       q.t_DlContrID as DlContrID "+ 
                "  from (select t_DlContrID "+
                "          from DSCSRVREPCNTR_DBT "+
                "         where T_SERVDOCID = "+String(srvrep.rec.ID)+
                "       ) q  ";
        cmd = DL_RSDCommand(query);

        var cnt = -1;
        var no_data = false;  
        DataSet = cmd.Execute();
        i = 0;
        while((not err) and (DataSet.moveNext()))
            if(cnt = -1 )
              cnt = int(DataSet.rec_cnt); 
              RemProgress();
              InitProgress( cnt, "Идет выполнение операции...", "Формирование отчетов брокера" );              
             end;
            
            err = _ExecuteSrvRepBrk(0, 0, 0, 0, 0, srvrep, DataSet.DlContrID, @no_data, SignerParty);

            UseProgress(i = i + 1);
        end;
        if(cnt > 0 )
         RemProgress();
         if ((err == 0) and no_data) err = 1; end;
        else
          RemProgress();
          err = 1;
          msgbox("Нет клиентов, удовлетворяющих условиям отбора");
        end;
      end; 

    end;
    
  end;

  ReplaceMacro( "msgbox", NULL );

  SaveSrvRepErrForReport_XML(SrvRepErrArr, srvrep.rec.ID, srvrep.rec.DocKind );

  if((_conveyerID) and (_packetID))
    /*не зависимо от того, делали что-то или нет - ставим исходящий статус обрабатываемой пачке*/
    var exec = RSDCommand(  " UPDATE dcnvpack_dbt "
                          + " SET t_State = t_State + 1 "
                          + " WHERE t_ExecID = ? "
                          + "   AND t_ConvTypeID = ? "
                          + "   AND t_PackID = ? ");

    exec.addParam( "", RSDBP_IN, _execID );
    exec.addParam( "", RSDBP_IN, _conveyerID );
    exec.addParam( "", RSDBP_IN, _packetID );
    exec.execute();
  end; 

  return err;
end;

macro ExecuteSendSrvRepBrk(_taskID, _execID, _conveyerID, _operationID, _packetID, _Params)
  var stat = 0;
  var err = 0;
  var cmd, query, DataSet;
  var srvrep = TBFile("scsrvrep.dbt");

  SrvRepErrArr.size = 0;
  ReplaceMacro( "msgbox", "SrvRep_msgbox" );

  srvrep.rec.ID = _Params.ID;
  if(srvrep.GetEQ())

    if((_conveyerID) and (_packetID))
      query =   " select distinct repobj.T_CONTRACTID, repobj.T_CLIENTID  "
              + "   from dcnvdoc_dbt cnvdoc, DSCSRVREPOBJ_DBT repobj "
              + "  where cnvdoc.t_ExecID = ? "
              + "    and cnvdoc.t_ConvTypeID = ? "
              + "    and cnvdoc.t_PackID = ? "
              + "    and cnvdoc.t_ObjectID = repobj.T_ID ";
      cmd = DL_RSDCommand(query);
      cmd.AddParam(_execID);
      cmd.AddParam(_conveyerID);
      cmd.AddParam(_packetID);
      SrvRepCurrData.SetClient(DataSet.ClientID);
      err = SC_SendOneRepClientContr(srvrep, DataSet.ObjectID);
      if (err)
        stat = err;
      end;
    else
      query = "select distinct T_CONTRACTID, T_CLIENTID from DSCSRVREPOBJ_TMP ";
      cmd = DL_RSDCommand(query);
      var cnt = cmd.GetCount();

      if(cnt > 0)
        InitProgress( cnt, "Идет выполнение операции...", "Отправка отчетов брокера" );
        
        DataSet = cmd.Execute();
        var i = 0;
        while(DataSet.moveNext())
          SrvRepCurrData.SetClient(DataSet.ClientID);
          err = SC_SendOneRepClientContr(srvrep, DataSet.ContractId);
          if (err)
            stat = err;
          end;
          UseProgress(i = i + 1);
        end;

        RemProgress();
      end;
    end; 

  end;
  
  ReplaceMacro( "msgbox", NULL );

  SaveSrvRepErrForReport_XML(SrvRepErrArr, srvrep.rec.ID, srvrep.rec.DocKind );
  
  if((_conveyerID) and (_packetID))
    /*не зависимо от того, делали что-то или нет - ставим исходящий статус обрабатываемой пачке*/
    var exec = RSDCommand(  " UPDATE dcnvpack_dbt "
                          + " SET t_State = t_State + 1, t_Error = ? "
                          + " WHERE t_ExecID = ? "
                          + "   AND t_ConvTypeID = ? "
                          + "   AND t_PackID = ? ");

    exec.addParam( "", RSDBP_IN, IIF(stat != 0, 1, 0) );
    exec.addParam( "", RSDBP_IN, _execID );
    exec.addParam( "", RSDBP_IN, _conveyerID );
    exec.addParam( "", RSDBP_IN, _packetID );
    exec.execute();
  end; 

  return stat;
end;