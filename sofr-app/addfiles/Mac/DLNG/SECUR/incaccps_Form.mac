/*******************************************************************************
 DESCRIPTION  :   Отчет "Доходы в виде начисленных процентов" - форма ввода данных
 PROGRAMMED BY:   Chernov Anton 14-01-2008
*******************************************************************************/

import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета 6-го регистра*/
CONST PNFLD_INCACCPS_REG_DEPCODE         = 0,
      PNFLD_INCACCPS_REG_DEPNAME         = 1,
      PNFLD_INCACCPS_REG_PERIOD          = 2,
      PNFLD_INCACCPS_REG_YEAR            = 3,
      PNFLD_INCACCPS_REG_GOS             = 4,
      PNFLD_INCACCPS_REG_AVRKIND         = 5,
      PNFLD_INCACCPS_REG_NOTGOS          = 6,
      PNFLD_INCACCPS_REG_CIRCINMARKET    = 7,
      PNFLD_INCACCPS_REG_NOTCIRCINMARKET = 8,
      PNFLD_INCACCPS_REG_NOMINRUR        = 9,
      PNFLD_INCACCPS_REG_NOMINCUR        = 10,
      PNFLD_INCACCPS_REG_AVRCODE         = 11,
      PNFLD_INCACCPS_REG_AVRNAME         = 12,
      PNFLD_INCACCPS_REG_BACKREPO        = 13,
      PNFLD_INCACCPS_REG_PRINTMETHOD     = 14;

PRIVATE CONST /* Обработчики для нестандарных контролов */
      H_DEPARTMENT      = 101,
      H_DEPARTMENT_NAME = 102,
      H_GOS_CHECKBOX    = 103,
      H_AVRKIND         = 104,
      H_NOTGOS_CHECKBOX = 105;

CLASS INCACCPSRegFormData()
  VAR Period,
      Year,
      IsGos,
      IsNotGos,
      CirculateInMarket,
      NotCirculateInMarket,
      NominateInRUR,
      NotNominateInRUR,
      IsBackRepo,
      Bond_Kind,
      AvrID;

  /* Значение по умолчанию */
  Period = 0;
  Year = 0;
  IsGos = "X";
  IsNotGos = "X";
  CirculateInMarket = "X";
  NotCirculateInMarket = "X";
  NominateInRUR = "X";
  NotNominateInRUR = "X";
  IsBackRepo = "X";
  AvrID = -1;
  Bond_Kind = 0;
END;

VAR FormData = INCACCPSRegFormData();


PRIVATE MACRO DLUSR_FldProc_Department( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  file  Department( dp_dep );

  if( Cmd == DLG_INIT )
    if( FldRealValue == null )
      FldRealValue = {OperDprt};
    end;
    if( FldRealValue < 0 )
      FldShowValue = STR_ALLVALUE;
      pThis.SetLinkedValue( H_DEPARTMENT_NAME, STR_ALLVALUE );
    else
      ClearRecord( Department );

      Department.Code = FldRealValue;
      if( GetGE(Department) )
        FldShowValue = Department.Name;
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, DL_GetDepartmentNameByCode(FldRealValue));
      else
        FldShowValue = "";
        pThis.SetLinkedValue( H_DEPARTMENT_NAME, "" );
      end;
    end;
  end;
  
  if( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
    FldRealValue = -1;
    FldShowValue = STR_ALLVALUE;

    return pThis.SetLinkedValue( H_DEPARTMENT_NAME, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    if( ListDepartment( Department ) )
       FldRealValue = Department.Code;
       FldShowValue = Department.Name;
       pThis.SetLinkedValue( H_DEPARTMENT_NAME, DL_GetDepartmentNameByCode(FldRealValue)); 
    end;
  end;

  return CM_DEFAULT;
END;


PRIVATE MACRO DLUSR_FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, Avoiriss, Avoirkinds, AddTable = "", WhereCond = "1=1";
  VAR BegDate, EndDate;

  if( Cmd == DLG_INIT )
    if( FldRealValue == null )
      FldRealValue = -1;
    end;
    if( FldRealValue < 0 )
      FldShowValue = STR_ALLVALUE;
      pThis.SetLinkedValue( DL_PNFLD_AVRNAME, "" );
    else
      FldShowValue = ПолучитьКодФинИн(FldRealValue);
    end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    Fininstr = TRecHandler( "fininstr.dbt" );
    Avoiriss = TRecHandler( "avoiriss.dbt" );

    if( FldShowValue == STR_ALLVALUE )
      FldRealValue = -1;
    else
      if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) )
        MsgBox( "Неверное значение поля" );
        return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
      end;

      FldRealValue = Fininstr.rec.FIID;
      FormData.AvrID = FldRealValue;
      pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
    FldRealValue = -1;
    FldShowValue = STR_ALLVALUE;
    FormData.AvrID = FldRealValue;
    pThis.SetLinkedValue( DL_PNFLD_AVRNAME, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    Period_GetInterval( pThis.GetFieldValue(PNFLD_INCACCPS_REG_PERIOD), 
                        pThis.GetFieldValue(PNFLD_INCACCPS_REG_YEAR), 
                        @BegDate, 
                        @EndDate, 
                        true );

    FormData.CirculateInMarket    = pThis.GetFieldValue(PNFLD_INCACCPS_REG_CIRCINMARKET);
    FormData.NotCirculateInMarket = pThis.GetFieldValue(PNFLD_INCACCPS_REG_NOTCIRCINMARKET);
    FormData.NominateInRUR    = pThis.GetFieldValue(PNFLD_INCACCPS_REG_NOMINRUR);
    FormData.NotNominateInRUR = pThis.GetFieldValue(PNFLD_INCACCPS_REG_NOMINCUR);

    if  ( (FormData.CirculateInMarket == "X") and (FormData.NotCirculateInMarket == "X") )
      /* если установлены оба признака, то это значит все ц\б, и условия не накладываем */
    else
      if( (FormData.CirculateInMarket == "X") or (FormData.NotCirculateInMarket == "X") )
        WhereCond = WhereCond  + " AND ";

        if( FormData.NotCirculateInMarket == "X" )
          WhereCond = WhereCond + " NOT ";
        end;

        WhereCond = WhereCond
                  + " Exists(SELECT attr.t_GroupID "
                  +          " FROM dobjatcor_dbt attr "
                  +         " WHERE attr.t_ObjectType = " + string(OBJTYPE_AVOIRISS)
                  +           " AND attr.t_GroupID = " + string(OBJGROUP_CIRCINMARKET)
                  +           " AND attr.t_Object = LPAD(T.t_FIID, 10, '0') "
                  +           " AND attr.t_General = CHR(88) "
                  +           " AND attr.t_ValidToDate >= " + GetSQLDate(EndDate) + " ) ";
      end;
    end;

    if( (FormData.NominateInRUR == "X") and (FormData.NotNominateInRUR == "X") )
      /* если установлены оба признака, то это значит все ц\б, и условия не накладываем */
    else
      if( FormData.NominateInRUR == "X" )
        WhereCond = WhereCond
                  + " AND T.t_FaceValueFI = " + string(NATCUR);
      end;
      if( FormData.NotNominateInRUR == "X" )
        WhereCond = WhereCond
                  + " AND T.t_FaceValueFI <> " + string(NATCUR)
                  + " AND T.t_FaceValueFI <> -1 ";
      end;
    end;
   
    Fininstr = TRecHandler( "fininstr.dbt" );
    if( ListAvoiriss( FormData.Bond_Kind, Fininstr, null, null, WhereCond, AddTable ) )
      FldRealValue = Fininstr.rec.FIID;
      FldShowValue = Fininstr.rec.FI_Code;
      FormData.AvrID = FldRealValue;

      pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
    else
      return CM_IGNORE;
    end; 
  end;

  return CM_DEFAULT;
END;


PRIVATE MACRO DLUSR_FldProc_AvrKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var MenuValues   = TArray,
      ReturnValues = TArray,
      Choice, Query, AvrKindsDataSet;

  if( (Cmd == DLG_INIT) or (Cmd == DLG_CHANGEVALUE) )
    if( FldRealValue == null )
       FldRealValue = 0;
    end;
    if( FldRealValue <= 0 )
       FldShowValue = STR_ALLVALUE;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
    FldRealValue = 0;
    FldShowValue = STR_ALLVALUE;
    FormData.Bond_Kind = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 
    Query = " SELECT T_NAME, T_AVOIRKIND "
          +   " FROM Davrkinds_DBT "
          +  " WHERE T_FI_KIND = " + string(FIKIND_AVOIRISS)
          +    " AND rsb_fiinstr.FI_AvrKindsGetRoot(2, T_AVOIRKIND) = 17 "; /*RSI_RSB_FIInstr.AVOIRKIND_BOND*/

    AvrKindsDataSet = TRSBDataSet(Query);
    while( AvrKindsDataSet.MoveNext() )              
      MenuValues[MenuValues.Size]     = AvrKindsDataSet.rec.Name; 
      ReturnValues[ReturnValues.Size] = AvrKindsDataSet.rec.AVOIRKIND;
    end;   
    Choice = menu( MenuValues, null, "Вид облигаций", pThis.CurrentFldX(), pThis.CurrentFldY() );
    if( Choice >= 0 )
       FldShowValue = MenuValues[Choice]; 
       FldRealValue = ReturnValues[Choice];
       FormData.Bond_Kind = FldRealValue;
    end;
  end;

  return CM_DEFAULT;
END;


PRIVATE MACRO DLUSR_FldProc_GosCheckBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;

    if( pThis.GetFieldValue(PNFLD_INCACCPS_REG_GOS) == "X" )
      EnableFields( pThis.Data(), PNFLD_INCACCPS_REG_AVRKIND );
    else
      FormData.Bond_Kind = 0;
      pThis.SetLinkedValue( H_AVRKIND, FormData.Bond_Kind );
      DisableFields( pThis.Data(), PNFLD_INCACCPS_REG_AVRKIND );
    end;

  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
      else
        FldShowValue = FldRealValue = "";
      end;
      FormData.IsGos    = pThis.GetFieldValue(PNFLD_INCACCPS_REG_GOS);
      FormData.IsNotGos = pThis.GetFieldValue(PNFLD_INCACCPS_REG_NOTGOS);
    end;
  end;

  return CM_DEFAULT;
END;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_TxReg8_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal;

     DateSplit( {curdate}, null, CurMonth, FormData.Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal   = 4;
        FormData.Year = FormData.Year - 1;
     end;

     FormData.Period = PrevQuartal + 12; /*(+12 - для NameAlg.dbt)*/
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,H_AVRKIND) )
        SetLinkedValue( H_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     AddFieldProc( 0, PNFLD_INCACCPS_REG_DEPCODE,         H_DEPARTMENT,         @DLUSR_FldProc_Department ); 
     AddFieldProc( 0, PNFLD_INCACCPS_REG_DEPNAME,         H_DEPARTMENT_NAME,    @Default, "" ); 

     AddFieldProc( 0, PNFLD_INCACCPS_REG_PERIOD,          DL_PNFLD_PERIOD,      @DL_FldProc_Period,      FormData.Period, 17, 8 ); 
     AddFieldProc( 0, PNFLD_INCACCPS_REG_YEAR,            DL_PNFLD_YEAR,        @DL_FldProc_Year,        FormData.Year );
     AddFieldProc( 0, PNFLD_INCACCPS_REG_GOS,             H_GOS_CHECKBOX,       @DLUSR_FldProc_GosCheckBox, FormData.IsGos );
     AddFieldProc( 0, PNFLD_INCACCPS_REG_AVRKIND,         H_AVRKIND,            @DLUSR_FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_INCACCPS_REG_NOTGOS,          H_NOTGOS_CHECKBOX,    @DLUSR_FldProc_GosCheckBox, FormData.IsNotGos );
     AddFieldProc( 0, PNFLD_INCACCPS_REG_CIRCINMARKET,    DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.CirculateInMarket );
     AddFieldProc( 0, PNFLD_INCACCPS_REG_NOTCIRCINMARKET, DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.NotCirculateInMarket );
     AddFieldProc( 0, PNFLD_INCACCPS_REG_NOMINRUR,        DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.NominateInRUR );
     AddFieldProc( 0, PNFLD_INCACCPS_REG_NOMINCUR,        DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.NotNominateInRUR );
     AddFieldProc( 0, PNFLD_INCACCPS_REG_AVRCODE,         DL_PNFLD_AVRCODE,     @DLUSR_FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_INCACCPS_REG_AVRNAME,         DL_PNFLD_AVRNAME,     @Default );
     AddFieldProc( 0, PNFLD_INCACCPS_REG_BACKREPO,        DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox,    FormData.IsBackRepo );
     AddFieldProc( 0, PNFLD_INCACCPS_REG_PRINTMETHOD,     DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 36, 25 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "INCACCPS" );
END;
