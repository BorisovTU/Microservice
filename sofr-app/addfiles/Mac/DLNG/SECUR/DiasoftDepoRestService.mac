//DiasoftDepoRestService.mac
import oralib, "SfContrService.mac", "cblogger2.mac";

class DiasoftDepoRestService
    private var contractService:SfContrService;
    private var logger:c_logger = LoggerFactory().NewItLog("DiasoftDepoRestService").GetLogger();

    macro IsRestsOnDateExists(dt:date):bool
        var query = 
                  "select 1 "
                + "  from ddiasrestdepo_dbt "
                + " where reportdate = :restDate "
                + "   and rownum = 1 ";
        var sql = ExecSQLselectPrmDyn(query, dt);
        return sql.MoveNext();
    end;

    macro GetRestBySubContr(subContrId:integer, isin:string, dt:date):money
        var subContract = contractService.GetSubContrById(subContrId);

        var query = 
              "with depoacc as ( "
            + "    select regexp_substr(acc.t_sectioncode, '^[-a-zA-Z]+') as first_letter, "
            + "           regexp_substr(acc.t_sectioncode, '[^-]+', 1, 3) as middle_number, "
            + "           acc.t_sofraccid, "
            + "           acc.t_contractnumber "
            + "      from ddiasaccdepo_dbt acc "
            + ") "
            + "select rest.value "
            + "  from dsfcontr_dbt c "
            + "  join depoacc acc on acc.t_contractnumber = c.t_number "
            + "  join ddiasrestdepo_dbt rest on rest.accdepoid = acc.t_sofraccid "
            + "  join ddiasisin_dbt i on i.t_id = rest.isin "
            + " where c.t_id = :subContrId "
            + "   and rest.reportdate = :restDate "
            + "   and i.t_isin = :isin "
            + "   and (acc.middle_number != '2727' or rest.value != 0) "
            + "   and sec_cl_blnc_reconc_utl.Check_DepoAcc_TradePlace(p_depoacc_firstletter  => acc.first_letter, "
            + "                                                       p_depoacc_middlenumber => acc.middle_number, "
            + "                                                       p_marketid             => :marketId, "
            + "                                                       p_servkindsub          => :servKindSub) = 1 ";
        var sql = ExecSQLselectPrmDyn(
            query,
            subContract.contrId,
            dt,
            isin,
            subContract.marketId,
            subContract.servKindSub
        );

        logger.Debug(string(subContract));

        if (sql.MoveNext())
            return sql.value("value");
        end;
        
        return null;
    OnError(err)
        logger.ErrorClob(string("GetRestBySubContr. subContrId:", subContrId,
                            ", isin:", isin ,
                            ", dt:", dt),
                        GetFullErrMsg(err));
        return null;
    end;
end;