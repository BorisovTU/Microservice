/*
$Name:        nptxphysiisinfo_form.mac
$Module:      Ценные бумаги
$Description: Форма отчета "Сведения о ФЛ и его ИИС"
*/

import "DlRepForm_h.mac", "spserv.mac";
import SfInter;

const PNFLD_NPTXPIIS_REPORTDATE = 0;
const PNFLD_NPTXPIIS_INVESTORCODE = 1;
const PNFLD_NPTXPIIS_INVESTORNAME = 2;
const PNFLD_NPTXPIIS_CONTRACT = 3;
const PNFLD_NPTXPIIS_DOCUMENTNUMBER = 4;

PRIVATE MACRO Generate_DocumentNumber()
   var RefNum = "";                                               
   if( GenerateNumberByReference( OBJTYPE_PERSN, 1, null, @RefNum) )                 
   end;                                                                                        
   return RefNum;                                                                     
END;

PRIVATE MACRO Generate_Mes(SfContrID:integer)
   var SfContr = TRecHandler("sfcontr.dbt");
   var Query = "", ДоговорНеЗакрыт = false, СчетНеЗакрыт = false, AccNumber = "";
   var DataSet = null, Mes = "";

   if( SfGetContr(SfContrID, SfContr) )

      if( SfContr.rec.DateClose == date(0,0,0) )
         ДоговорНеЗакрыт = true;
      end;

      Query = " select ACC.T_ACCOUNT, ACC.T_CLOSE_DATE "
             +"   from dsfssi_dbt sfssi, dsettacc_dbt settacc, daccount_dbt acc "
             +"  where SFSSI.T_OBJECTTYPE = "+OBJTYPE_SFCONTR              
             +"    and SFSSI.T_OBJECTID = lpad("+SfContrID+",10,'0')     "                 
             +"    and SETTACC.T_SETTACCID = SFSSI.T_SETACCID  "                 
             +"    and ACC.T_ACCOUNT = SETTACC.T_ACCOUNT       "                 
             +"    and ACC.T_CHAPTER = SETTACC.T_CHAPTER       "                 
             +"    and ACC.T_CODE_CURRENCY = SETTACC.T_FIID    "                 
             +" order by acc.t_open_date desc";/*Каждое физическое лицо вправе иметь только один ИИС, но если вдруг много, то возьмем последний по дате открытия*/  
     
      DataSet = TRsbDataSet(Query);
      if( DataSet.MoveNext() and (SQL_ConvTypeDate(DataSet.CLOSE_DATE) == Date(0,0,0)) )
         СчетНеЗакрыт = true;
         AccNumber = SQL_ConvTypeStr(DataSet.Account);
      end;
     
      if( ДоговорНеЗакрыт and СчетНеЗакрыт )
         Mes = "Счет \""+AccNumber+"\" и договор обслуживания \""+SfContr.rec.Number+"\" не закрыты. Сформировать отчет?";
      elif( ДоговорНеЗакрыт )
         Mes = "Договор обслуживания \""+SfContr.rec.Number+"\" не закрыт. Сформировать отчет?";
      elif( СчетНеЗакрыт )
         Mes = "Счет \""+AccNumber+"\" не закрыт. Сформировать отчет?";
      end;

   end;

   return Mes;
END;
                            
private macro GetOneClientContrStock(PartyID, OnDate)
    var Query = "";
    var DataSet = null;
    var ContrID = 0;

    Query = " SELECT "
              + " sfc.t_ID "
          + " FROM "
              + " dsfcontr_dbt sfc "
          + " WHERE "
              + " sfc.t_ServKind = " + PTSK_STOCKDL + " "
          + " AND sfc.t_ContractorID = " + {OurBank} + " "
          + " AND sfc.t_PartyID = " + PartyID + " "
          + " AND NPTO.CheckContrIIS(sfc.t_ID) = 1 "
          + " AND sfc.t_DateBegin <= " + GetSQLDate(OnDate) + " "
          + " AND (sfc.t_DateClose = TO_DATE('01.01.0001','DD.MM.YYYY') "
            + " OR sfc.t_DateClose >= " + GetSQLDate(OnDate) + ") "
          + " order by sfc.t_DateConc desc";

    DataSet = TRsbDataSet(Query);
    if(DataSet.MoveNext())
       ContrID = SQL_ConvTypeInteger(DataSet.ID);
    end;

    return ContrID;
end;

// Код физлица - клиента фондового дилинга
private macro FldProc_Client_STOCKDL_Code(pThis:DL_CPanel, Cmd:Integer, Key:Integer, FldShowValue:@Variant, FldRealValue:@Variant):Integer
    var Party = TRecHandler("party.dbt");
    var AddWhere = "";
    var ContrID = -1;

    if(Cmd == DLG_INIT)
        if(FldRealValue == null)
            FldRealValue = 0;
        end;
        if(FldRealValue <= 0)
            FldShowValue = STR_ALLVALUE;
            pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, STR_ALLVALUE);
            pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, -1);
        else
            FldShowValue = ПолучитьКодСубъекта(FldRealValue, PTCK_CONTR);
            if(not ПолучитьСубъекта(FldRealValue, Party))
                pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName);
            end;
        end;
    elif((Cmd == DLG_KEY) and (Key == KEY_SPACE))
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, STR_ALLVALUE);
        pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, -1);
    elif((Cmd == DLG_KEY) and (Key == KEY_F3))
        var RepDate = Date(pThis.GetFieldValue(PNFLD_NPTXPIIS_REPORTDATE));

        AddWhere = " t.t_LegalForm = 2 "
                 + " and exists( "
                 + "              select 1 "
                 + "              from dclient_dbt cl "
                 + "              where cl.t_PartyID = t.t_PartyID "
                 + "                and cl.t_ServiceKind = " + string(PTSK_STOCKDL)
                 + "                and cl.t_StartDate < " + GetSQLDate(RepDate)
                 + "           ) ";

        if(ListPText(Party, FldRealValue, AddWhere))
            FldRealValue = Party.rec.PartyID;
            FldShowValue = ПолучитьКодСубъекта(FldRealValue, PTCK_CONTR);
            pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName);
            pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, -1);
            // Если ДО - один, то подставляется номер этого ДО
            ContrID = GetOneClientContrStock(Party.rec.PartyID, pThis.GetLinkedValue(DL_PNFLD_ENDDATE));
            if(ContrID > 0)
                pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, ContrID);
            end;
        end;
    end;

    return CM_DEFAULT;
end;

// листалка договоров
private macro ListSfContrByServKind(FldShowValue:@Variant, FldRealValue:@Variant, X:Integer, Y:Integer, PartyID:@Integer):Bool
    var Query = "";
    var RecordSet = null; 

    Query = " SELECT "
              + " sfc.t_ID "
             + " ,sfc.t_DateConc "
             + " ,sfc.t_Name "
             + " ,sfc.t_Number "
             + " ,prt1.t_ShortName AS t_Payer "
             + " ,prt2.t_ShortName AS t_Contractor "
             + " ,sfc.t_PartyID "
          + " FROM "
              + " dsfcontr_dbt sfc "
             + " ,dparty_dbt prt1 "
             + " ,dparty_dbt prt2 "
          + " WHERE "
              + " sfc.t_ServKind = " + PTSK_STOCKDL + " "
          + " AND NPTO.CheckContrIIS(sfc.t_ID) = 1 "
          + " AND prt1.t_PartyID = sfc.t_partyID "
          + " AND prt2.t_PartyID = sfc.t_ContractorID "
          + " AND sfc.T_ContractorID = " + {OurBank} + " "
          + " AND prt1.t_LegalForm = 2 ";

    if(PartyID > 0)
        Query = Query +
            " AND sfc.t_PartyID = " + PartyID + " ";
    end;

    RecordSet = DL_Scroll(Query, "Договора обслуживания", X, Y,
                        "t_Number", "№ договора",
                        "t_Name", "Наименование договора",
                        "t_DateConc", "Дата закл.",
                        "t_Payer","Плательщик",
                        "t_Contractor", "Контрагент");

    if(RecordSet != null)
        FldRealValue = RecordSet.Value("t_ID"); 
        FldShowValue = RecordSet.Value("t_Number");
        PartyID = RecordSet.Value("t_PartyID");
    end;

    return (RecordSet != null);
end;

private macro FldProc_Contract_BO_Dep(pThis:DL_CPanel, Cmd:Integer, Key:Integer, FldShowValue:@Variant, FldRealValue:@Variant):Integer
    var SfContr = TRecHandler("sfcontr.dbt");
    var ClientID = pThis.GetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE);

    if(Cmd == DLG_INIT)
        if(FldRealValue == null)
            FldRealValue = -1;
        end;

        if(FldRealValue <= 0)
            FldShowValue = STR_ALLVALUE;
        else
            if(SfGetContr(FldRealValue, SfContr))
                FldShowValue = SfContr.rec.Number;
            end;
        end;
    elif((Cmd == DLG_KEY) and (Key == KEY_SPACE)) 
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, -1);
    elif((Cmd == DLG_KEY) and (Key == KEY_F3))
        ListSfContrByServKind(@FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @ClientID);
        pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, ClientID);
    end;

    return CM_DEFAULT;
end;

private macro FldProc_DocumentNumber(pThis:DL_CPanel, Cmd:Integer, Key:Integer, FldShowValue:@Variant, FldRealValue:@Variant):Integer
    if(Cmd == DLG_INIT)
//        if(FldRealValue == null)
            FldRealValue = Generate_DocumentNumber();
//        end;
        FldShowValue = FldRealValue;
    elif(Cmd == DLG_CHANGEVALUE)
        FldRealValue = FldShowValue;
    elif(Cmd == DLG_KEY)
        if(Key == KEY_SPACE)
            FldShowValue = FldRealValue = "";
        end;
    end;

    return CM_DEFAULT;
end;

class (DL_CPanel) NpTxPhysIISInfoPanel()

    private macro Init()
        AddFieldProc(0, PNFLD_NPTXPIIS_REPORTDATE, DL_PNFLD_ENDDATE, @DL_FldProc_EndDate);
        AddFieldProc(0, PNFLD_NPTXPIIS_INVESTORCODE, DL_PNFLD_CLIENT_STOCKDL_CODE, @FldProc_Client_STOCKDL_Code);
        AddFieldProc(0, PNFLD_NPTXPIIS_INVESTORNAME, DL_PNFLD_CLIENT_STOCKDL_NAME, @DL_FldProc_Client_STOCKDL_Name);
        AddFieldProc(0, PNFLD_NPTXPIIS_CONTRACT, DL_PNFLD_CONTRACT_OFBU, @FldProc_Contract_BO_Dep);
        AddFieldProc(0, PNFLD_NPTXPIIS_DOCUMENTNUMBER, DL_USERFIELD, @FldProc_DocumentNumber);
    end;

    private macro Check():BOOL
        var Stat = true, Mes = "";
        var Key = 0;

        if(GetFieldValue(PNFLD_NPTXPIIS_INVESTORCODE) <= 0)
            MsgBox("Необходимо заполнить поле \"Инвестор\"");
            Stat = false;
        end;

        if(Stat and (GetFieldValue(PNFLD_NPTXPIIS_CONTRACT) <= 0))
            MsgBox("Необходимо заполнить поле \"Договор обслуживания\"");
            Stat = false;
        end;

        if(Stat)
           Mes = Generate_Mes(GetFieldValue(PNFLD_NPTXPIIS_CONTRACT));
           if( Mes != "" )
              Key = MsgBoxEx(Mes, MB_YES + MB_NO + MB_CANCEL, IND_CANCEL);
              if(Key == IND_NO)
                  Exit(1);
              elif(Key == IND_CANCEL)
                  Stat = false;
              end;
           end;
        end;

        return Stat;
    end;

    initDL_CPanel("sp_rep.lbr", "TXPIISOL");
end;
