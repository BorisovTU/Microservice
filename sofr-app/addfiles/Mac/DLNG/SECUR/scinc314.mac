/*
$Name:        scinc314.mac
$Module:      Ценные бумаги
$Description: Операция начисления ПДД. Шаг - Начисление дохода(расхода), по РЕПО
*/
import RsbDataSet, "sp_car.mac", "scservop.mac";

private record Deal( dl_tick );

private macro SayError(ErrStr:string)
   if((ErrStr!=null)and(ErrStr!=""))
      SC_SaveErrorForReport_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind, ErrStr, 1, ScOprServDoc.CommCode, deal, deal.BofficeKind);
   end;
   InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
   ClearErrorArr();
   return 1;
end;

private macro GetIncRateOnDate(DealId, DocKind, OnDate)
  var cmd, dataSet;
  var rate = $0;
  cmd = DL_RSDCommand( "SELECT T_INCOMERATE FROM DDL_INCRATE_HST_DBT WHERE T_DEALID = ? AND T_DOCKIND = ? AND T_FROMDATE <= ? ORDER BY T_FROMDATE DESC");

  cmd.addParam(DealID);
  cmd.addParam(DocKind);
  cmd.addParam(OnDate); 

  dataSet = cmd.execute();
  if(dataSet.MoveNext())
    rate = dataSet.incomerate;
  end;

  return rate;
end;

macro ExecuteStep( Doc, FirstDoc )
   var FD, FD2, Chapter = 1, R, FiRole = FIROLE_BA, ground, ПроцентыПросрочены = false;
   var CatDebet = "", CatCredit = "";
   var DebFIID, CredFIID;
   var Sпред_вр = $0, Sпред_вц = $0, Sum_CFI = $0, Sum_PayFIID = $0, 
       Sum_PayFIID_save= $0, Sдонач_вц = $0, Sдонач_вр = $0, Sum_CFI_save = $0;
   var dlsum = TRecHandler( "dlsum" );
   var rq_avance1 = TRecHandler("dlrq");
   var rq_money1 = TRecHandler("dlrq");
   var rq_avance2 = TRecHandler("dlrq");
   var rq_money2 = TRecHandler("dlrq");
   var rq_pc2 = TRecHandler("dlrq");
   var AccountReceiver = TRecHandler( "account" );
   var AccountPayer = TRecHandler( "account" );
   var IsExistsDebetAcc = false, IsExistsCreditAcc = false;
   var DebAccNumber = "", CredAccNumber = "", NewIncome = $0;
   var Err = "", ЭтоПРЕПО = false;
   var Select, DataSet, SдоначОР = $0, S_СЗ = $0, SдоначПроцДоЭПС = $0, Sдонач_НацВ = $0; 

   SetBuff( Deal, FirstDoc );

   ScOpReportLineData.Size = 0;

   FD = SPFirstDoc( deal, false );
   FD2 = SPFirstDoc( deal, true );

   ЭтоПРЕПО = (IsREPO(FD.Group) and IsSALE(FD.Group));

   if( FD2.ExistPC() )
      if( FD2.GetRQ(DLRQ_TYPE_INCREPO).rec.State == DLRQ_STATE_OVERDUE )
         ПроцентыПросрочены = true;
      end;
   end;

   if (FD2.dl_leg.rec.IsFlRate == SET_CHAR)
     R = GetIncRateOnDate(FD2.tick.rec.Dealid, FD2.tick.rec.BofficeKind, ScOprServDoc.EndDate);
   else
     R = FD2.dl_leg.rec.IncomeRate;
   end;

   Sпред_вр    = ПолучитьПоследнююСумму(DL_SECURITYDOC, deal.DealID, DLSUM_KIND_SUM_TO_PERCENT);
   Sпред_вц    = ПолучитьПоследнююСумму(DL_SECURITYDOC, deal.DealID, DLSUM_KIND_SUM_TO_PERCENT_CFI);

   if(FD.ExistRqMoney())
     rq_money1 = FD.GetRQ(DLRQ_TYPE_PAYMENT);
   end;

   if(FD.ExistAvance())
     rq_avance1 = FD.GetRQ(DLRQ_TYPE_AVANCE);
   elif(FD.ExistDeposit())
     rq_avance1 = FD.GetRQ(DLRQ_TYPE_DEPOSIT);
   end;

   if(FD2.ExistRqMoney())
     rq_money2 = FD2.GetRQ(DLRQ_TYPE_PAYMENT);
   end;

   if(FD2.ExistAvance())
     rq_avance2 = FD2.GetRQ(DLRQ_TYPE_AVANCE);
   elif(FD2.ExistDeposit())
     rq_avance2 = FD2.GetRQ(DLRQ_TYPE_DEPOSIT);
   end;

   if(FD2.ExistPC())
     rq_pc2 = FD2.GetRQ(DLRQ_TYPE_INCREPO);
   end;

   /*вычисляется S% в ВЦ на дату операции по формуле из ТЗ на операцию начисления */
   Sum_CFI = SP_GetPercentSum( rq_avance1, rq_money1, rq_avance2, rq_money2, rq_pc2, FD.dl_leg, FD2.dl_leg, FD.Group, ScOprServDoc.EndDate, true );

   /*если ВЦ!=ВР выполняется конвертация в ВР*/
   if( SmartConvertSum( Sum_PayFIID, Sum_CFI, ScOprServDoc.EndDate, 
                        FD.dl_leg.rec.CFI, FD.GetPayFIID(), false ) != true )
      return SayError();
   end;    

   Sum_PayFIID_save = Sum_PayFIID;

   /* сумма  в ВР заносится в dlsum  вида 2010 в ВР и 2011 в ВЦ за дату начисления */

   /*Если уже есть сумма за эту дату - нам нужно чтобы в итоге оказалось именно Sum_PayFIID. 
     Уменьшаем то что нужно добавить.*/
   if( FindDLSUM( DL_SECURITYDOC, FD.tick.rec.DealID, DLSUM_KIND_SUM_TO_PERCENT, ScOprServDoc.EndDate, dlsum ) == 0)
      Sum_PayFIID_save = Sum_PayFIID - dlsum.rec.Sum;
   end;

   if( not СохранитьСуммуКомиссии( DL_SECURITYDOC, FD.tick.rec.DealID, DLSUM_KIND_SUM_TO_PERCENT, 
                                   ScOprServDoc.EndDate, Sum_PayFIID_save, $0, FD.GetPayFIID() ) )
      return SayError();
   end; 

   NewIncome = Sum_PayFIID_save;/*для протокола*/

   Sum_CFI_save = Sum_CFI;
   /*Если уже есть сумма за эту дату - нам нужно чтобы в итоге оказалось именно Sum_CFI. 
     Уменьшаем то что нужно добавить.*/
   if( FindDLSUM( DL_SECURITYDOC, FD.tick.rec.DealID, DLSUM_KIND_SUM_TO_PERCENT_CFI, ScOprServDoc.EndDate, dlsum ) == 0)
      Sum_CFI_save = Sum_CFI - dlsum.rec.Sum;
   end;

   if( not СохранитьСуммуКомиссии( DL_SECURITYDOC, FD.tick.rec.DealID, DLSUM_KIND_SUM_TO_PERCENT_CFI, 
                                   ScOprServDoc.EndDate, Sum_CFI_save, $0, FD.dl_leg.rec.CFI ) )
      return SayError();
   end; 

   Sдонач_вр = Sum_PayFIID - Sпред_вр;
   Sдонач_вц = Sum_CFI - Sпред_вц;

   if (Sдонач_вр != $0)
      /*Выполняются проводки по доначислению процентов на сумму SдоначР*/
      Chapter = 1;
      if((((R > 0.0) and (not IsLoan(FD.Group))) and IsSALE(FD.Group)) or
         (((R < 0.0) or (IsLoan(FD.Group))) and IsBUY(FD.Group)) /*Обратное РЕПО или займ привличение*/
        )
//         ground = "Начисление расхода";
         ground = "Начисление процентов по первой части сделки РЕПО без признания № " + UserGetDealCode(FD) + " с ЦБ" + GetFinName(FD.fininstr.rec) + " (контр. "  + UserGetPartyShortName(FD.tick.rec.partyid) + ") от " + FD.tick.rec.dealdate + ", получение дохода было признано определенным";
         CatDebet  = "-%Пкр"; 
         DebFIID   = NATCUR;

         if (ПроцентыПросрочены)
            CatCredit = "-% с н.с.";
         else
            CatCredit = "-% к погашению";
         end;
         CredFIID  = FD.GetPayFIID();
      else
//         ground = "Доначисление процентов в балансе";
         ground = "Начисление процентов по первой части сделки РЕПО без признания № " + UserGetDealCode(FD) + " с ЦБ" + GetFinName(FD.fininstr.rec) + " (контр. "  + UserGetPartyShortName(FD.tick.rec.partyid) + ") от " + FD.tick.rec.dealdate + ", получение дохода было признано определенным";
//Начисление процентов по первой части сделки РЕПО без признания № 2849521748 с ЦБ SBER_ (контр. НКО НКЦ (АО)) от 29/06/2018, получение дохода было признано определенным

         if (ПроцентыПросрочены)
            CatDebet = "+% с н.с.";
         else
            CatDebet = "+% к погашению";
         end;
         DebFIID  = FD.GetPayFIID();

         CatCredit = "+%Пкр";
         CredFIID   = NATCUR;
      end;

      DebAccNumber = CredAccNumber = "";

      if(not ПроводкаПоКатегориямУчета( FD, 
             CatDebet, CatCredit,         /* КатегДеб , КатегКредит*/
             ScOprServDoc.CommDate,         /* Дата */
             Chapter,                     /* Глава */
             FD.GetPayFIID(),             /* Валюта */
             Sдонач_вр,                   /* Сумма  - превышение суммы плано-вых процентов (за срок с До1ф до До2ф) над суммой начисленных процентов*/
             Doc,                         /* Документ */
             INPCARRY,                    /* Result_Carry */
             " 1",                        /* Kind_Oper */
             ScOprServDoc.CommCode,         /* Numb_Document */
             ground,
             null,
             FiRole, FiRole,
             DebFIID, CredFIID,
             null,
             null,
             null,
             null,
             null,
             null,
             null,
             @DebAccNumber,
             @CredAccNumber
            ) 
        )
         return SayError();
      end;

      ScOpReportLineData[ScOpReportLineData.Size] =  SvOpIncome( INCOME_REPO,     
                                                                 -1,       
                                                                 FD.tick.rec.DealCode,     
                                                                 FD2.dl_leg.rec.IncomeRate,   
                                                                 FD2.dl_leg.rec.Point,    
                                                                 IIF(NewIncome>=Sдонач_вр,NewIncome-Sдонач_вр,$0),
                                                                 NewIncome,
                                                                 Sдонач_вр,      
                                                                 GetFinInstrCcy( FD2.GetPayFIID(), true ),   
                                                                 DebAccNumber,    
                                                                 CredAccNumber,    
                                                                 Ground   
                                                               );
   end; 

   if( IsREPO(FD.Group) )
      if( SmartConvertSum( Sдонач_НацВ, Sдонач_вц, ScOprServDoc.EndDate, FD.dl_leg.rec.CFI, NATCUR, false ) != true )
         return SayError();
      end;    

      if( DL_WRTCalcDealParmREPO(ScOprServDoc.CommDate,ScOprServDoc.EndDate,FD.tick.rec.DealID, Sдонач_НацВ) != 0 )
         return SayError(GetErrMsg());
      end;
     
      Select = DL_RSDCommand( "SELECT TMP.T_DEFDIFFADD, TMP.T_WRTOUTLAYADD, (TMP.T_CORRINTTOEIR - LOT.T_CORRINTTOEIR) DeltaCORRINTTOEIR "
                             +"  FROM DPMWRTSUM_TMP TMP, DPMWRTSUM_DBT LOT "
                             +" WHERE LOT.T_SUMID = TMP.T_SUMID"
                            );
     
      DataSet = Select.Execute();
      if(DataSet.moveNext())

         SдоначОР = SQL_ConvTypeSum(DataSet.DEFDIFFADD);
         S_СЗ = SQL_ConvTypeSum(DataSet.WRTOUTLAYADD);
         SдоначПроцДоЭПС = SQL_ConvTypeSum(DataSet.DeltaCORRINTTOEIR); 

         if( SдоначОР != $0 )

            if( SдоначОР > $0 )
               if( ЭтоПРЕПО )
                  CatDebet = "-КСт, ПривлСрАС";
                  CatCredit = "+Дох, ПривлСр";
               else
                  CatDebet = "-КСт, РазмещСрАС";
                  CatCredit = "+Дох, РазмещСр";
               end;
            else
               if( ЭтоПРЕПО )
                  CatDebet = "-Расх, ПривлСр";
                  CatCredit = "+КСт, ПривлСрАС";
               else
                  CatDebet = "-Расх, РазмещСр";
                  CatCredit = "+КСт, РазмещСрАС";
               end;
            end;

            ClearRecord(AccountPayer.rec);
            IsExistsDebetAcc = false;
            if( FD.IsExistAccount( CatDebet, null, true, null, AccountPayer, null, ScOprServDoc.CommDate ) )
               IsExistsDebetAcc = true;
            end;
           
            ClearRecord(AccountReceiver.rec);
            IsExistsCreditAcc = false;
            if( FD.IsExistAccount( CatCredit, null, true, null, AccountReceiver, null, ScOprServDoc.CommDate ) )
               IsExistsCreditAcc = true;
            end;

            if(not ПроводкаПоКатегориямУчета( FD, 
                   iif(IsExistsDebetAcc,AccountPayer,CatDebet), iif(IsExistsCreditAcc,AccountReceiver,CatCredit),         /* КатегДеб , КатегКредит*/
                   ScOprServDoc.CommDate,         /* Дата */
                   Chapter,                     /* Глава */
                   NATCUR,             /* Валюта */
                   ABS(SдоначОР),                   /* Сумма */
                   Doc,                         /* Документ */
                   INPCARRY,                    /* Result_Carry */
                   " 1",                        /* Kind_Oper */
                   ScOprServDoc.CommCode,         /* Numb_Document */
                   "Доначисление отсроченной разницы"
                  ) 
              )
               return SayError();
            end;
         end;

         if( SдоначПроцДоЭПС != $0 )

            if( SдоначПроцДоЭПС < $0 )
               if( ЭтоПРЕПО )
                  CatDebet = "-КСт, ПривлСрАС";
                  CatCredit = "-Корр, ПРепо";
               else
                  CatDebet = "-КСт, РазмещСрАС";
                  CatCredit = "-Корр, ОРепо";
               end;
            else
               if( ЭтоПРЕПО )
                  CatDebet = "+Корр, ПРепо";
                  CatCredit = "+КСт, ПривлСрАС";
               else
                  CatDebet = "+Корр, ОРепо";
                  CatCredit = "+КСт, РазмещСрАС";
               end;
            end;
           
            ClearRecord(AccountPayer.rec);
            IsExistsDebetAcc = false;
            if( FD.IsExistAccount( CatDebet, null, true, null, AccountPayer, null, ScOprServDoc.CommDate ) )
               IsExistsDebetAcc = true;
            end;
           
            ClearRecord(AccountReceiver.rec);
            IsExistsCreditAcc = false;
            if( FD.IsExistAccount( CatCredit, null, true, null, AccountReceiver, null, ScOprServDoc.CommDate ) )
               IsExistsCreditAcc = true;
            end;

            if(not ПроводкаПоКатегориямУчета( FD, 
                   iif(IsExistsDebetAcc,AccountPayer,CatDebet), iif(IsExistsCreditAcc,AccountReceiver,CatCredit),         /* КатегДеб , КатегКредит*/
                   ScOprServDoc.CommDate,         /* Дата */
                   Chapter,                     /* Глава */
                   NATCUR,             /* Валюта */
                   ABS(SдоначПроцДоЭПС),                   /* Сумма */
                   Doc,                         /* Документ */
                   INPCARRY,                    /* Result_Carry */
                   " 1",                        /* Kind_Oper */
                   ScOprServDoc.CommCode,         /* Numb_Document */
                   "Корректировка % до ЭПС"
                  ) 
              )
               return SayError();
            end;
         end;

         if( S_СЗ != $0 )
            if(not ПроводкаПоКатегориямУчета( FD, 
                   iif(ЭтоПРЕПО,"-КВ проц.расход, ц/б","-КВ проц.доход, ц/б"), "Затраты, Репо",         /* КатегДеб , КатегКредит*/
                   ScOprServDoc.CommDate,         /* Дата */
                   Chapter,                     /* Глава */
                   NATCUR,             /* Валюта */
                   abs(S_СЗ),                   /* Сумма */
                   Doc,                         /* Документ */
                   INPCARRY,                    /* Result_Carry */
                   " 1",                        /* Kind_Oper */
                   ScOprServDoc.CommCode,         /* Numb_Document */
                   "Списание существенных затрат"
                  ) 
              )
               return SayError();
            end;
         end;

         if( DL_WRTSetDealParmREPO(ScOprServDoc.CommDate,ScOprServDoc.EndDate,FD.tick.rec.DealID) != 0 )
            return SayError(GetErrMsg());
         end;
      end;
   end;

   return 0;
end;

/* Макрос постобработки */
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
    ClearErrorArr();
    if (errTrn OR (CommitOrRollback == 2)) /* Произошла ошибка или происходит откат */
       if( CommitOrRollback == 2 )
          DL_DeleteDataByStep_XML(ID_Operation, ID_Step);
       end;
       return;
    else
      if(ScOpReportLineData.size > 0)
         DL_SaveDataForReport_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind, ScOpReportLineData, LOG_TYPE_DATA, ID_Operation, ID_Step);
      end;
    end;

    return 0;
END;