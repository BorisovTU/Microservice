/*
$Name:          ws_txlinkscalc.mac
$Module:        АРНУ 
$Description:   Макрос сервисов формы "Пересчет связей для налогового учета"
*/
import "secinter.mac";
import "sctxrun.mac";
import "ws_avoiriss.mac";
import "ws_fiunilist.mac";
import "ws_llvalues.mac";
import "ws_common.mac";
import ws_file;
import rsexts;

// класс CTxLinksCalc для формы "Пересчет связей для налогового учета"
class CTxLinksCalc
  var DateClosed:Date;      // Дата закрытого налогового периода
  var DateLast:Date;        // Дата последнего расчета
  var ButStart:Bool;        // Рассчитать связи по дату
  var DateStart:Date;       // Дата расчета
  var ButStartByFin:Bool;   // Пересчитать связи с даты
  var DateStartByFin:Date;  // Дата начала пересчета
  var ButByFinDateEnd:Bool; // По дату
  var DateEndByFin:Date;    // Дата окончания пересчета
  var TaxGroupList = TArray(); // Список групп налогового учета
  var FIList = TArray();  // Список кодов ценных бумаг
  var ButCloseTX:Bool;      // Закрыть налоговый период
  var DateCloseTX:Date;     // Дата закрытия налогового периода
end;

// класс CTCalcCompleted для возврата флага завершения и файла контейнера 
class CTxCalcCompleted
  var CalcCompleted:Bool;       // Флаг завершения расчёта
  var Container:FileContainer;  // Файл контейнер
  var ErrMess:String;           // Описание ошибки
end;

macro TxLinksCalcRunError( err, stat:integer )
  WSRunError( "ws_txlinkscalc.mac", err, stat );
end;

macro TxFillLinksCalcByRec( TxLinksCalc_rec:TRecHandler ):CTxLinksCalc
  var TxLinksCalc = CTxLinksCalc();

  TxLinksCalc.DateClosed      = TxLinksCalc_rec.rec.DateClosed;
  TxLinksCalc.DateLast        = TxLinksCalc_rec.rec.DateLast;
  TxLinksCalc.ButStart        = (TxLinksCalc_rec.rec.ButStart == SET_CHR);
  TxLinksCalc.DateStart       = TxLinksCalc_rec.rec.DateStart;
  TxLinksCalc.ButStartByFin   = (TxLinksCalc_rec.rec.ButStartByFin == SET_CHR);
  TxLinksCalc.DateStartByFin  = TxLinksCalc_rec.rec.DateStartByFin;
  TxLinksCalc.ButByFinDateEnd = (TxLinksCalc_rec.rec.ButByFinDateEnd == SET_CHR);
  TxLinksCalc.DateEndByFin    = TxLinksCalc_rec.rec.DateEndByFin;
  if( TxLinksCalc_rec.rec.TaxGroup > -1 )
    TxLinksCalc.TaxGroupList[TxLinksCalc.TaxGroupList.Size] = CreateLLValueFromAppServ(2903, TxLinksCalc_rec.rec.TaxGroup);
  end;
  if(TxLinksCalc_rec.rec.FIID > ALLFININSTR)
    TxLinksCalc.FIList[TxLinksCalc.FIList.Size] = GetTWsFinInstrByID(TxLinksCalc_rec.rec.FIID);
  end;
  TxLinksCalc.ButCloseTX      = (TxLinksCalc_rec.rec.ButCloseTX == SET_CHR);
  TxLinksCalc.DateCloseTX     = TxLinksCalc_rec.rec.DateCloseTX;

  return TxLinksCalc;
end;

// Инициализация данных формы "Пересчет связей для налогового учета"
macro TxLinksCalcInit()//:CTxLinksCalc
  var TxLinksCalc_rec = TRecHandler( "TxLinksCalc.rec" );
  var TxLinksCalc = CTxLinksCalc();
  var stat:integer = -1;

  SP_InitLinksCalc( TxLinksCalc_rec );
  TxLinksCalc = TxFillLinksCalcByRec( TxLinksCalc_rec );
  TxLinksCalc.ButStart = true;

  return TxLinksCalc;

OnError( err )
  TxLinksCalcRunError(err, stat);
end;

macro TxProtocolCalcLink(outFileName : String)
  var sql, rsd, result;

  FILE outFile() txt write;

  sql = "SELECT TO_CHAR(T_MESDATE, 'dd.mm.yyyy, hh24:mi:ss') MESDATE, T_TYPE, T_MESSAGE FROM DSCTXMES_DBT WHERE T_ID > 0 ORDER BY T_ID";
  rsd = TRsbDataSet(sql);
  result = "";
  while(rsd.MoveNext())
    result = result + rsd.mesdate + ", " + GetMesType(rsd.t_type) + ": \t" + rsd.t_message + "\n\r"; 
  end;

  outFileName = GetTxtFileName( outFileName );
  if( Open( outFile, outFileName ))
    Insert( outFile, result);
    Close( outFile );
  end;

  return outFileName;

OnError( err )
  return "Сбой вывода протокола";
end;

macro TxGetProgressValue()
  var sql, rsd, result, stat = -1, chp, tmp;

  sql = " SELECT t_Message FROM DSCTXMES_DBT WHERE T_ID = -1 AND T_TYPE = 50 ";
  rsd = TRsbDataSet(sql);

  result = 0;

  if(rsd.MoveNext())
    chp = index(rsd.t_message,":");
    if(chp > 0 )
      tmp = substr(rsd.t_message,chp+1);
      chp = index(tmp,".");
      if(chp > 0 )
        tmp = substr(tmp,1,chp-1);
        result = int(trim(tmp));
      end;
    end;
  end;

  return result;

OnError( err )
  return -1;
end;

macro TxLinksCalcRun(
  FormData //:CTxLinksCalc Параметры пересчета
):CTxCalcCompleted

  var stat:Integer = -1;
  var ErrMess:String = "";
  var CanCalc:Bool = false;  
  var Mode:Integer = TXRUN_MODE_UNDEF;
  var DateStart:Date = Date(0,0,0);
  var DateEnd:Date = Date(0,0,0);
  var TaxGroupCount:Integer = 0;
  var TaxGroupElementList = TArray();
  var FICount:Integer = 0;
  var FIIDList = TArray();
  var AvoirIss = TRecHandler( "avoiriss.dbt" );
  var CalcCompleted = CTxCalcCompleted();

  CalcCompleted.CalcCompleted = false;

  if( FormData.ButStart )
    Mode = TXRUN_MODE_ONDATE;
  else
    if( FormData.ButStartByFin )
      if( FormData.ButByFinDateEnd )
        Mode = TXRUN_MODE_BYDATE;
      else
        Mode = TXRUN_MODE_BYFIID;
      end;
    else
      if( FormData.ButCloseTX )
        Mode = TXRUN_MODE_CLOSE;
      else
        Mode = TXRUN_MODE_UNDEF;
      end;
    end;
  end;

  if( Mode == TXRUN_MODE_ONDATE )
    DateStart = FormData.DateStart;
  elif( Mode == TXRUN_MODE_BYFIID )
    DateStart   = FormData.DateStartByFin;

    if( ( FormData.TaxGroupList != null ) and ( FormData.TaxGroupList.Size > 0 ) )
      TaxGroupCount = 0;
      while( TaxGroupCount < FormData.TaxGroupList.Size )
        if( ( FormData.TaxGroupList[TaxGroupCount] != null )
        and ( FormData.TaxGroupList[TaxGroupCount].Element != null )
        and ( FormData.TaxGroupList[TaxGroupCount].Element > -1 ) )
          TaxGroupElementList[TaxGroupElementList.Size] = FormData.TaxGroupList[TaxGroupCount].Element;
        end;
        TaxGroupCount = TaxGroupCount + 1;
      end;
    end;

    if( ( FormData.FIList != null ) and ( FormData.FIList.Size > 0 ) )
      FICount = 0;
      while( FICount < FormData.FIList.Size )
        if( ( FormData.FIList[FICount] != null )
        and ( FormData.FIList[FICount].Fiid != null )
        and ( FormData.FIList[FICount].Fiid > ALLFININSTR ) )
          FIIDList[FIIDList.Size] = FormData.FIList[FICount].Fiid;
        end;
        FICount = FICount + 1;
      end;
    end;
  elif( Mode == TXRUN_MODE_BYDATE )
    DateStart = FormData.DateStartByFin;
    DateEnd = FormData.DateEndByFin;
  elif( Mode == TXRUN_MODE_CLOSE )
    DateStart = FormData.DateCloseTX;
  end;

  if( TaxGroupElementList.Size == 0 )
    TaxGroupElementList[TaxGroupElementList.Size] = -1;
  end;

  if( FIIDList.Size == 0 )
    FIIDList[FIIDList.Size] = ALLFININSTR;
  end;

// TODO...

// SELECT NVL(t1.t_element,t2.t_taxgroup),
//   NVL (t2.t_FIID, -1)
// FROM
//   (SELECT t_element
//   FROM dllvalues_dbt llv
//   WHERE t_list       = 2903
//   AND llv.t_element IN (2,3)
//   ) t1
// FULL OUTER JOIN
//   (SELECT t_taxgroup,t_FIID FROM davoiriss_dbt WHERE t_FIID IN (6,7,19)
//   ) t2
// ON t1.t_element = t2.t_taxgroup

  TaxGroupCount = 0;
  while( (ErrMess == "") and (TaxGroupCount < TaxGroupElementList.Size) )
    FICount = 0;
    while( (ErrMess == "") and (FICount < FIIDList.Size) )
      CanCalc = false;
      if( ( TaxGroupElementList[TaxGroupCount] > -1 )
      and ( FIIDList[FICount] > ALLFININSTR ) )
        if( ( ПолучитьФинИн( FIIDList[FICount], null, AvoirIss ) == 0 )
        and ( AvoirIss.rec.TaxGroup == TaxGroupElementList[TaxGroupCount] ) )
          CanCalc = true;
        end;
      else
        CanCalc  = true;
      end;

      if( CanCalc )
        if( Begin_RunSCTX( Mode, DateStart, DateEnd, TaxGroupElementList[TaxGroupCount], FIIDList[FICount], @ErrMess ) )
          CalcCompleted.CalcCompleted = RunSCTX( Mode, DateStart, DateEnd, TaxGroupElementList[TaxGroupCount], FIIDList[FICount] );
          End_RunSCTX( Mode, DateStart, DateEnd, TaxGroupElementList[TaxGroupCount], FIIDList[FICount] );
        else
          CalcCompleted.ErrMess = ErrMess;
          CalcCompleted.CalcCompleted = false;
        end;
      end;

      FICount = FICount + 1;
    end;
    TaxGroupCount = TaxGroupCount + 1;
  end;

  if( ErrMess == "" )
    CalcCompleted.Container = CreateLargeFileContainer(ConvertTxt2Html(TxProtocolCalcLink("ws_protocol.txt")), true);
  end;

  return CalcCompleted;

OnError( err )
  TxLinksCalcRunError( err, stat );
end;

macro TxCheckLinksCalc(
  LinksCalc //: CTxLinksCalc
) : TArray
  var stat:integer = -1;
  var ResponseBuilder = WebResponseBuilder();

  if( LinksCalc != null )
    ResponseBuilder.SetUserObject( LinksCalc );

    if( ( LinksCalc.ButStart == false )
    and ( LinksCalc.ButStartByFin == false )
    and ( LinksCalc.ButCloseTX == false ) )
      // Не выбран вид пересчета
      ResponseBuilder.AddErrorEx( 1, MessageSeverity.ValidationError, "Не выбран вид пересчета" );
    end;

    // DateStart
    if( LinksCalc.ButStart == true )
      if( LinksCalc.DateStart == date(0,0,0) )
        // Введите дату
        ResponseBuilder.AddErrorEx( 2, MessageSeverity.ValidationError, "Введите дату" );
      elif( LinksCalc.DateStart <= LinksCalc.DateLast )
        // Дата должна быть больше даты последнего расчета - 8172
        ResponseBuilder.AddErrorEx( 3, MessageSeverity.ValidationError, "Дата должна быть больше даты последнего расчета" );
      elif( LinksCalc.DateStart >= {curdate} )
        // Дата должна быть меньше текущей - 8080
        ResponseBuilder.AddErrorEx( 4, MessageSeverity.ValidationError, "Дата должна быть меньше текущей" );
      end;
    end;

    // DateStartByFin
    if( LinksCalc.ButStartByFin == true )
      if( LinksCalc.DateStartByFin <= LinksCalc.DateClosed )
        // Дата должна быть больше даты закрытия - 8173
        ResponseBuilder.AddErrorEx( 5, MessageSeverity.ValidationError, "Дата должна быть больше даты закрытия" );
      elif( LinksCalc.DateStartByFin > LinksCalc.DateLast )
        // Дата должна быть меньше даты последнего расчета - 8171
        ResponseBuilder.AddErrorEx( 6, MessageSeverity.ValidationError, "Дата должна быть меньше даты последнего расчета" );
      elif( LinksCalc.DateStartByFin >= {curdate} )
        // Дата должна быть меньше текущей - 8080
        ResponseBuilder.AddErrorEx( 7, MessageSeverity.ValidationError, "Дата должна быть меньше текущей" );
      end;
    end;

    // DateEndByFin
    if( LinksCalc.ButByFinDateEnd == true )
      if( LinksCalc.DateEndByFin == date(0,0,0) )
        // Введите дату
        ResponseBuilder.AddErrorEx( 8, MessageSeverity.ValidationError, "Введите дату" );
      elif( LinksCalc.DateEndByFin < LinksCalc.DateStartByFin )
        // Дата должна быть не меньше даты начала расчета - 8190
        ResponseBuilder.AddErrorEx( 9, MessageSeverity.ValidationError, "Дата должна быть не меньше даты начала расчета" );
      elif( LinksCalc.DateEndByFin >= LinksCalc.DateLast )
        // Дата должна быть меньше даты последнего расчета - 8171
        ResponseBuilder.AddErrorEx( 10, MessageSeverity.ValidationError, "Дата должна быть меньше даты последнего расчета" );
      end;
    end;

    // DateCloseTX
    if( LinksCalc.ButCloseTX == true )
      if( LinksCalc.DateCloseTX == date(0,0,0) )
        // Введите дату
        ResponseBuilder.AddErrorEx( 11, MessageSeverity.ValidationError, "Введите дату" );
      elif( LinksCalc.DateCloseTX <= LinksCalc.DateClosed )
        // Дата должна быть больше даты закрытия - 8173
        ResponseBuilder.AddErrorEx( 12, MessageSeverity.ValidationError, "Дата должна быть больше даты закрытия" );
      elif( LinksCalc.DateCloseTX > LinksCalc.DateLast )
        // Дата должна быть меньше даты последнего расчета - 8171
        ResponseBuilder.AddErrorEx( 13, MessageSeverity.ValidationError, "Дата должна быть меньше даты последнего расчета" );
      end;
    end;
  end;

  return ResponseBuilder.Result;

OnError( err )
  TxLinksCalcRunError( err, stat );
end;

macro TxGetFininstrList(
  PageSize:Integer // Размер страницы
 ,PageIndex:Integer // Номер страницы
 ,FltrParm //: TWsFinInstrFltrParm // Параметры фильтрации
) //: TArray of TWsFinInstr

  var stat:Integer = -1;

  return GetFIList( PageSize, PageIndex, FltrParm, " RSB_FIInstr.FI_IsSecurEmissive( fi.t_FIID ) > 0 " );

OnError( err )
  TxLinksCalcRunError( err, stat );
end;
