/*
  Макрос сервиса виджета выбора СПИ
  ws_scsettacc.mac
  Avdaschenko D. V.
  16.05.2013
*/

import "ws_common.mac";
import "secinter.mac";
import "cb_sql.mac";
import RsbDataSet;

// Счета для расчетов с субъектами ( почти SETTACC.DBT )
class CScSettAcc()
  var SettAccID         : Integer;  // Уникальный идентификатор
  var PartyID           : Integer;  // Идентификатор субъекта
  var BankID            : Integer;  // Идент-р банка куда пер-ся средства
  var FIID              : Integer;  // Идентификатор ФИ (валюты)
  var FICode            : String;   // Код ФИ
  var Chapter           : Integer;  // Глава счетов
  var Account           : String;   // Счет куда перечисляются средства
  var INN               : String;   // ИНН бенифициара
  var RecName           : String;   // Название бенифициара
  var BankCodeKind      : Integer;  // Вид кода банка
  var BankCode          : String;   // Код банка
  var BankName          : String;   // Наименование банка
  var BankCorrID        : Integer;  // Идентификатор корреспондента банка
  var BankCorrCodeKind  : Integer;  // Вид кода корреспондента банка
  var BankCorrCode      : String;   // Код корреспондента банка
  var BankCorrName      : String;   // Наименование корреспондента банка
  var CorrAcc           : String;   // Корсчет
  var FIKind            : Integer;  // Вид ФИ
  var BeneficiaryID     : Integer;  // Идентификатор владельца счета
  var CodeKind          : Integer;  // Вид кода получателя
  var Code              : String;   // Код получателя
  var Description       : String;   // Описание СПИ
  var Order             : Integer;  // Порядок сортировки СПИ
  var ShortName         : String;   // Короткое описание СПИ
  var NoAccept          : Bool;     // Без акцепта
  var KZPartyCode       : String;   // Кбе
end;

macro ScSettAccRunError( err, stat : Integer )
  RunError( "Макрос: ws_scsettacc.mac, Строка: " + err.line + " , Сообщение: " + err.message, stat );
end;

macro ScGetSettAccList(
  PartyID : Integer
 ,FIKind  : Integer
 ,FIID    : Integer
)/*: TArray of CScSettAcc*/

  var stat : Integer = -1;
  var ScSettAccList = TArray();

  InitError();

  var Query : String =
    " SELECT sa.*, NVL(fi.t_FI_Code, CHR(1)) FICode "
    + " FROM dsettacc_dbt sa, dfininstr_dbt fi "
   + " WHERE fi.t_FIID(+) = sa.t_FIID ";

  if( ( ValType( PartyID ) == V_INTEGER ) and ( PartyID > 0 ) )
    Query = Query
     + " AND sa.t_PartyID = " + PartyID + " ";
  end;

  if( ( ValType( FIKind ) == V_INTEGER ) and ( FIKind > 0 ) )
    Query = Query
     + " AND sa.t_FIKind = " + FIKind + " ";
  end;

  if( ( ValType( FIID ) == V_INTEGER ) and ( FIID > 0 ) )
    Query = Query
     + " AND sa.t_FIID = " + FIID + " ";
  end;

  //fprintln( "ws_scsettacc_debug", Query );

  var ScSettAcc = CScSettAcc();
  var DataSet = TRsbDataSet( Query );
  while( DataSet.MoveNext() )
    ScSettAcc = CScSettAcc();

    ScSettAcc.SettAccID         = SQL_ConvTypeInteger( DataSet.SettAccID );
    ScSettAcc.PartyID           = SQL_ConvTypeInteger( DataSet.PartyID );
    ScSettAcc.BankID            = SQL_ConvTypeInteger( DataSet.BankID );
    ScSettAcc.FIID              = SQL_ConvTypeInteger( DataSet.FIID );
    ScSettAcc.FICode            = SQL_ConvTypeStr( DataSet.FICode );
    ScSettAcc.Chapter           = SQL_ConvTypeInteger( DataSet.Chapter );
    ScSettAcc.Account           = SQL_ConvTypeStr( DataSet.Account );
    ScSettAcc.INN               = SQL_ConvTypeStr( DataSet.INN );
    ScSettAcc.RecName           = SQL_ConvTypeStr( DataSet.RecName );
    ScSettAcc.BankCodeKind      = SQL_ConvTypeInteger( DataSet.BankCodeKind );
    ScSettAcc.BankCode          = SQL_ConvTypeStr( DataSet.BankCode );
    ScSettAcc.BankName          = SQL_ConvTypeStr( DataSet.BankName );
    ScSettAcc.BankCorrID        = SQL_ConvTypeInteger( DataSet.BankCorrID );
    ScSettAcc.BankCorrCodeKind  = SQL_ConvTypeInteger( DataSet.BankCorrCodeKind );
    ScSettAcc.BankCorrCode      = SQL_ConvTypeStr( DataSet.BankCorrCode );
    ScSettAcc.BankCorrName      = SQL_ConvTypeStr( DataSet.BankCorrName );
    ScSettAcc.CorrAcc           = SQL_ConvTypeStr( DataSet.CorrAcc );
    ScSettAcc.FIKind            = SQL_ConvTypeInteger( DataSet.FIKind );
    ScSettAcc.BeneficiaryID     = SQL_ConvTypeInteger( DataSet.BeneficiaryID );
    ScSettAcc.CodeKind          = SQL_ConvTypeInteger( DataSet.CodeKind );
    ScSettAcc.Code              = SQL_ConvTypeStr( DataSet.Code );
    ScSettAcc.Description       = SQL_ConvTypeStr( DataSet.Description );
    ScSettAcc.Order             = SQL_ConvTypeInteger( DataSet.Order );
    ScSettAcc.ShortName         = SQL_ConvTypeStr( DataSet.ShortName );
    ScSettAcc.NoAccept          = SQL_ConvTypeStr( DataSet.NoAccept ) == SET_CHR;
    ScSettAcc.KZPartyCode       = SQL_ConvTypeStr( DataSet.KZPartyCode );

    ScSettAccList[ScSettAccList.Size] = ScSettAcc;
  end;

  return ScSettAccList;

OnError( err )
  ScSettAccRunError( err, stat );
end;

//var sal = ScGetSettAccList();
