/**
  @file nptxsnobverprocres.mac
  @brief Получения ответа от хранилища СНОБ операции технической сверки СНОБ в планировщике

  #tag
  - functional_block: СНОБ
  - code_type: Operation
  - Сверка
  - Техническая сверка

  #link
  [BIQ-7294. Анализ. Техническая сверка] (https://sdlc.go.rshbank.ru/confluence/pages/viewpage.action?pageId=223940937)
  [Анализ. Техническая сверка. Доработка] (https://sdlc.go.rshbank.ru/confluence/pages/viewpage.action?pageId=276937627)

  #changeLog
  |date      |author       |tasks    |note                                                             |
  |----------|-------------|---------|-----------------------------------------------------------------|
  |04.12.2023|Хромеев Д. С.|BOSS-1489|BOSS-48.9 СОФР. Реализация Технической сверки данных. Разработка.|
  |12.04.2024|Сенников И.В.|BOSS-1489|Доработка технической сверки.                                    |
*/

//IMPORTANT TODO!!
//Если это читается в рамках CodeReview, то можно смело сразу выставлять замечание:
//Почти все алгоритмы этого макроса не имеют смысла существования в виде макроса
//Очень напрашивается переписать всё это на PL/SQL, т.к. асболютно никакого смысла не имеет передавать данные на сторону СП СОФРа.
//Почему сразу не было написано в виде PL/SQL ? Осознание пришло, что весь этот код бессмысленен как макрос, только когда начал его дорабатывать под новые реаилии
//Раньше он был как макрос nptxsnobver020.mac, и работал в качестве шага
//А переписать его уже не хватило времени
//Насчет "почти" - исполнить шаг код на PL/SQL не сможет, но вполне реально вставлять funobj который будет заниматься чисто этим

import BankInter, OprInter, DealsInter, CarryDoc, InsCarryDoc, dlquery;
import RtRegEx, "oralib.mac", "dlmisc.mac", "likepy.mac", "dlutils.mac";

private const SS_RESPONSE_END = 3; /* Успешное завершение всего сервиса */
private const SS_RESPONSE_FATAL = 4; /* Ошибка исполнения, обработка всей цепочки прерывается */

private class c_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
   var ProcessState;
   var ErrorNumber;
   var ErrorText;

   private macro init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText)
      if(ValType(p_ProcessState) != V_UNDEF)
         ProcessState = p_ProcessState;
      end;
      if(ValType(p_ErrorNumber) != V_UNDEF)
         ErrorNumber = p_ErrorNumber;
      end;
      if(ValType(p_ErrorText) != V_UNDEF)
         ErrorText = p_ErrorText;
      end;
   end;

   init_ssRunResult(p_ProcessState, p_ErrorNumber, p_ErrorText);
end;

private macro UpdateMessageStat(messId, status)
  execSQL("update SOFR_IPS_MESSAGE set T_STATUS = ? where T_ID = ?", makeArray(SQLParam("stat", status), SQLParam("mesid", messId)), true);
end;

private macro UpdateTotalBase(tbid, uuid)
  //только по активным записям
  execSQL("update DNPTXTOTALBASE_DBT set T_STORID = ?, T_CONFIRMSTATE = 1 where T_TBID = ? and T_STORSTATE = 1", 
          makeArray(SQLParam("uuid", uuid), SQLParam("tbid", tbid)), true);
end;

private macro GetRetCanceled(BatchId)
  var ds = ExecSQLSelect("select t_returncanceled from dnptxsnobver_dbt where t_id = ?", MakeArray(SQLParam("batch_id", BatchId)), false);
  if (ds.MoveNext())
    return IIF(SQL_ConvTypeStr(ds.value("t_returncanceled")) == SET_CHAR, SET_CHAR, UNSET_CHAR);
  end;
  return UNSET_CHAR;
end;

private class OperCloser()
  private var idBatchArr = TArray();

  private macro GetOperationIdByBatch(batch_id)
    var ds =
      ExecSQLSelect("select t_id_operation from doproper_dbt where t_dockind = 4650 and t_documentid = lpad(:docid, 34, 0)",
                     MakeArray(SQLParam("batch_id", batch_id)), false);
    if (ds.MoveNext())
      return SQL_ConvTypeInteger(ds.value("t_id_operation"));
    end;
    return 0;
  end;

  private macro GetStepExecuteStateIdByBatch(operId, symb)
    var ds =
      ExecSQLSelect("select T_ISEXECUTE from doprstep_dbt where t_id_operation = ? and T_SYMBOL = ?",
                     MakeArray(SQLParam("operId", operId), SQLParam("symb", symb)), false);
    if (ds.MoveNext())
      return SQL_ConvTypeStr(ds.value("T_ISEXECUTE"));
    end;
    return NULL;
  end;

  macro CloseSnobVerOperByBatchId(BatchId)
    //не выполняем закрытие повторно
    if (find(idBatchArr,BatchId) >= 0)
      return;
    end;
    idBatchArr[idBatchArr.size] = BatchId;
    var ID_Operation = GetOperationIdByBatch(BatchId);
    var stat = 0;

    var receiveState = GetStepExecuteStateIdByBatch(ID_Operation, "П");
    if (receiveState == NULL)
      stat = Opr_InsertAndExecuteBranch(ID_Operation, "П", "I", NULL, "З");
    elif (receiveState == "R")
      stat = Opr_ExecuteStep(ID_Operation, "П");
    end;

    if (not stat)
      var closeState = GetStepExecuteStateIdByBatch(ID_Operation, "З");
      if ((closeState != NULL) and (closeState == "R"))
        stat = Opr_ExecuteStep(ID_Operation, "З");
      end;
    end;
    if (stat)
      RunError("Ошибка №"+stat+", Текст: " + GetErrMsg());
    end;
    if (not stat)
      AddDBLogDebug("ExecuteSverProc","Успешно закрыта операция " + ID_Operation);
    end;
  OnError(exc)
    AddDBLogDebug("ExecuteSverProc","Ошибка закрытия операции " + ID_Operation + ", " + GetFullErrorMessage(exc));
  end;
end;

private macro CheckInt(val : string) : integer
  if ( StrIsNumber(val) )
    return int(val);
  end;
  RunError("Wrong number");
end;

private macro ConvStrToTime(val : string) : time
  if (  ( SubStr(val, 3, 1)  != ":" )
     or ( SubStr(val, 6, 1)  != ":" )
     )
    RunError("FormatError");
  end;
  return time (  CheckInt( SubStr(val, 1, 2) )
              ,  CheckInt( SubStr(val, 4, 2) )
              ,  CheckInt( SubStr(val, 7, 2) )
              );
end;

macro ConvStrToDate(val : string) : date
  if (  ( StrLen(val)        != 10    )
     or ( SubStr(val, 5, 1)  != "-"   )
     or ( SubStr(val, 8, 1)  != "-"   )
     )
    RunError("FormatError");
  end;
  return date (  CheckInt( SubStr(val, 9, 2) )
              ,  CheckInt( SubStr(val, 6, 2) )
              ,  CheckInt( SubStr(val, 1, 4) )
              );
end;

private macro GetDateTime(val : string)
  if ( SubStr(val, 11, 1) != " " )
    RunError("FormatError");
  end;
  return Dttm (  ConvStrToDate( SubStr(val, 1, 10) )
              ,  ConvStrToTime( SubStr(val, 12)    )
              );
OnError()
  return Dttm(date(0,0,0),time(0,0,0));
end;


/**
  @brief Класс для парсинга одной CSV строки ответа
  fieldOrder - настройка порядка парсинга
*/
private class OneCsvLineStruct()
  private CONST CSV_PARSE_REGEX = "(?:,|\\n|^)(\"(?:(?:\"\")*[^\"]*)*\"|[^\",\\n]*|(?:\\n|$))";
  private CONST CSV_REGEX_OBJ = RsRegex( CSV_PARSE_REGEX );

  private CONST fieldOrder = TArray();
  fieldOrder[fieldOrder.size] = "rec_number";
  fieldOrder[fieldOrder.size] = "creation_date";
  fieldOrder[fieldOrder.size] = "update_date";
  fieldOrder[fieldOrder.size] = "client_id";
  fieldOrder[fieldOrder.size] = "operation_id";
  fieldOrder[fieldOrder.size] = "event_id";
  fieldOrder[fieldOrder.size] = "record_found";
  fieldOrder[fieldOrder.size] = "record_id";
  fieldOrder[fieldOrder.size] = "record_requested";
  fieldOrder[fieldOrder.size] = "error";
  fieldOrder[fieldOrder.size] = "batch_id";
  fieldOrder[fieldOrder.size] = "record_status";

  var uuid = NULL;
  var creation_date = NULL;
  var update_date = NULL;
  var client_id = NULL;
  var operation_id = NULL;
  var event_id = NULL;
  var record_found = NULL;
  var record_id = NULL;
  var record_requested = NULL;
  var error = NULL;
  var batch_id = NULL;
  var record_status = NULL;
  var rec_number = NULL;

  private macro Clear()
    uuid = 
    creation_date = 
    update_date = 
    client_id = 
    operation_id = 
    event_id = 
    record_found = 
    record_id = 
    record_requested = 
    error = 
    batch_id = 
    rec_number =
    record_status = NULL;
  end;

  macro FillFromStringByRegex(line)
    Clear();
    var i = 0;
    if (CSV_REGEX_OBJ.match(line))
      var flag = CSV_REGEX_OBJ.first();
      while (flag)
        //присвоить текущему объекту значение поля указанному в массиве очередности полей
        GenSetProp(this, fieldOrder[i], CSV_REGEX_OBJ.cap(1));
        flag = CSV_REGEX_OBJ.next();
        i = i + 1;
      end;
    else
      RunError("Строка \""+line+"\" не распознана как CSV");
    end;
    return this;
  end;

  macro ToString()
    var retStr = "";
    if (ValType(rec_number) == V_STRING)
      retStr = retStr + IIF(retStr == "", "", ", ") + "line_numb: "+ rec_number;
    end;
    if (ValType(uuid) == V_STRING)
      retStr = retStr + IIF(retStr == "", "", ", ") + "uuid: "+ uuid;
    end;
    return retStr;
  end;
end;

macro GetStringFromBlobBuf()
  var resStr = "";
  var idx = 1;
  while(true)
    var cmd = RsdCommand(" begin "+
                         " ? := it_rsl_string.get_varchar(?); "+
                         " end;" );
    cmd.addParam("v_result",RSDBP_OUT,V_STRING, 32768);
    cmd.addParam("p_index",RSDBP_IN,idx);
    cmd.execute();

    var bufStr = cmd.value("v_result");
    if ((ValType(bufStr) != V_STRING) or (StrLen(bufStr) == 0))
      break;
    end;
    resStr = resStr + bufStr;
    idx = idx + 1;
  end;
  execSQL("begin it_rsl_string.clear; end;"); // Очистка буфера
  return resStr;
end;

/**
  @brief Получить ServiceID сервиса eprshb_to_sofr_verify_snob_result из таблицы SOFR_IPS_SETTINGS
  @return ServiceID, или -1, если не найдено
*/
MACRO GetServiceID()
   var QueryServiceID = "select t_ID " +
                        "from SOFR_IPS_SETTINGS " +
                        "where LOWER(t_ServiceName) = 'eprshb_to_sofr_verify_snob_result' " +
                        "order by t_ID ";

   var CmdServiceID = DL_RSDCommand(QueryServiceID);
   var DSServiceID = CmdServiceID.Execute();

   if(DSServiceID.MoveNext())
      return DSServiceID.ID;
   end;

   return -1;
END;

MACRO CsvStr(str)
  if (ValType(str) == V_STRING)
    return str;
  end;
  return "";
OnError
  return "";
END;

MACRO CsvStrToInt(str)
   return Int(str);
OnError
   return NULL;
END;

MACRO CsvGetFlagChar(str)
   if(StrLwr(str) == "true")
      return SET_CHAR;
   else
      return UNSET_CHAR;
   end;
OnError
   return UNSET_CHAR;
END;

MACRO CsvGetFlagCharDigit(str)
   if(str == "1")
      return SET_CHAR;
   else
      return UNSET_CHAR;
   end;
OnError
   return UNSET_CHAR;
END;

macro DefineIsChecked(error_, ReturnCanceled, SofrStorState, SnobState, RecRequested, RecFound)
  if ((ValType(error_) == V_STRING) and (Trim(error_) != ""))
    return false; //если есть ошибка, значит точно не прошли сверку
  else
    if (ReturnCanceled != SET_CHAR)
      if ((SofrStorState == 1) and (((ValType(SnobState) != V_STRING) or (Trim(SnobState) == "")) or (SnobState == "0")))
        return false;
      end;
      if ((SofrStorState == 0) and ((ValType(SnobState) == V_STRING) and (SnobState == "1")))
        return false;
      end;
      if ((SofrStorState == 0) and ((ValType(SnobState) != V_STRING) or (Trim(SnobState) == "")))
        return true;
      end;
    elif (ReturnCanceled == SET_CHAR)
      if ((SofrStorState == 1) and (((ValType(SnobState) != V_STRING) or (Trim(SnobState) == "")) or (SnobState == "0")))
        return false;
      end;
      if ((SofrStorState == 0) and (((ValType(SnobState) != V_STRING) or (Trim(SnobState) == "")) or (SnobState == "1")))
        return false;
      end;
    end;
    if ((RecRequested != SET_CHAR) or (RecFound != SET_CHAR))
      return false;
    end;
    return true;
  end;
end;

MACRO ProcessOneCsvLine(csvLineObj, operCloseObj)
  var client_id =    CheckInt(csvLineObj.client_id);
  var operation_id = CheckInt(csvLineObj.operation_id);
  var event_id =     CheckInt(csvLineObj.event_id);
  var batch_id =     CheckInt(csvLineObj.batch_id);

  var querySelect = " select t.*, fd.t_returncanceled " +
                    " from DNPTXSNOBVERTB_DBT t, DNPTXSNOBVER_DBT fd " +
                    " where t.t_ClientID = ? "
                    "   and t.t_Operation_ID = ? " +
                    "   and fd.T_ID = t.t_BatchID " +
                    "   and t.t_Event_ID = ? " +
                    "   and t.t_BatchID = ? ";

  var cmdSelect = DL_RSDCommand(querySelect);
  cmdSelect.AddParam(client_id);
  cmdSelect.AddParam(operation_id);
  cmdSelect.AddParam(event_id);
  cmdSelect.AddParam(batch_id);

  var DSSelect = cmdSelect.Execute();

  if(DSSelect.MoveNext())
    var IsChecked = DefineIsChecked(CsvStr(csvLineObj.error),
                                    SQL_ConvTypeStr(DSSelect.returncanceled),
                                    SQL_ConvTypeInteger(DSSelect.StorState),
                                    csvLineObj.record_status,
                                    CsvGetFlagChar(csvLineObj.record_requested),
                                    CsvGetFlagChar(csvLineObj.Record_Found));

    var queryUpdate = "UPDATE DNPTXSNOBVERTB_DBT " +
                      "SET t_Record_Found = ?, " +
                      "    t_Record_ID = ?, " +
                      "    t_Record_Requested = ?, " +
                      "    t_Error = ?, " +
                      "    t_Record_Status = ?, " +
                      "    t_Checked = ? " +
                      "WHERE t_ClientID = ? " +
                      "  and t_Operation_ID = ? " +
                      "  and t_Event_ID = ? " +
                      "  and t_BatchID = ? ";

    var cmdUpdate = RSDCommand(queryUpdate);
    cmdUpdate.AddParam("", RSDBP_IN, CsvGetFlagChar(csvLineObj.record_found));
    cmdUpdate.AddParam("", RSDBP_IN, CsvStr(csvLineObj.record_id));
    cmdUpdate.AddParam("", RSDBP_IN, CsvGetFlagChar(csvLineObj.record_requested));
    cmdUpdate.AddParam("", RSDBP_IN, CsvStr(csvLineObj.error));
    cmdUpdate.AddParam("", RSDBP_IN, CsvGetFlagCharDigit(csvLineObj.record_status));
    cmdUpdate.AddParam("", RSDBP_IN, IIF(IsChecked,SET_CHAR,UNSET_CHAR));
    cmdUpdate.AddParam("", RSDBP_IN, client_id);
    cmdUpdate.AddParam("", RSDBP_IN, operation_id);
    cmdUpdate.AddParam("", RSDBP_IN, event_id);
    cmdUpdate.AddParam("", RSDBP_IN, batch_id);
    cmdUpdate.NullConversion = true;
    SQL_Execute(cmdUpdate);

    UpdateTotalBase(SQL_ConvTypeInteger(DSSelect.Event_Id),CsvStr(csvLineObj.record_id));
  else
    var insQuery = "INSERT INTO DNPTXSNOBVERTB_DBT "
                  +"(t_BatchID,t_ClientID,t_StartDate,t_EndDate,t_Operation_ID,t_Event_ID,"
                  +"t_Return_Canceled,t_Record_Found,t_Record_ID,t_Record_Requested,t_Error,t_Record_Status,t_checked,t_storstate)"
                  +" values (?,?,?,?,?,?,?,?,?,?,?,?,?,?)";
    execSQL(insQuery, 
            makeArray(
              SQLParam("BatchID", batch_id),
              SQLParam("ClientID", client_id),
              SQLParam("StartDate", date(GetDateTime(csvLineObj.creation_date))),
              SQLParam("EndDate", date(0,0,0)),
              SQLParam("Operation_ID", operation_id),
              SQLParam("Event_ID", event_id),
              SQLParam("Return_Canceled", GetRetCanceled(batch_id)),
              SQLParam("Record_Found", CsvGetFlagChar(csvLineObj.record_found)),
              SQLParam("Record_ID", CsvStr(csvLineObj.record_id)),
              SQLParam("Record_Requested", CsvGetFlagChar(csvLineObj.record_requested)),
              SQLParam("Error", CsvStr(csvLineObj.error)),
              SQLParam("Record_Status", CsvGetFlagCharDigit(csvLineObj.record_status)),
              SQLParam("checked", UNSET_CHAR),
              SQLParam("storstate", 1)
            ),
            true);
  end;
  operCloseObj.CloseSnobVerOperByBatchId(CsvStrToInt(csvLineObj.batch_id));
  return true;
OnError(exc)
  AddDBLogDebug("ExecuteSverProc","Ошибка обработки строки " + csvLineObj.ToString() + ", " + GetFullErrorMessage(exc));
  return false;
END;

MACRO ExecuteSverProc()
  var ServiceID = GetServiceID();
  var operCloseObj = OperCloser();

  var isSuccess = true;

  if(ServiceID > 0)
    var csvStruct = OneCsvLineStruct();
    
    var queryAns = " select T_ID " +
                   " from SOFR_IPS_MESSAGE " +
                   " where t_ServiceID = ? " +
                   "   and t_Direction = 1 " +
                   "   and T_STATUS = 0 " +
                   " order by t_ID ";

    var cmdAns = DL_RSDCommand(queryAns);
    cmdAns.AddParam(ServiceID);

    var DSAns = cmdAns.Execute();
    while(DSAns.MoveNext())
      AddDBLogDebug("ExecuteSverProc","Начало разбора сообщения из таблицы SOFR_IPS_MESSAGE с ID: " + String(DSAns.Id));
       
      var sqlRead = 
         " DECLARE "
        +"    p_message   CLOB; "
        +" BEGIN "
        +"    SELECT T_MESSAGE "
        +"      INTO p_message "
        +"      FROM SOFR_IPS_MESSAGE "
        +"     WHERE T_ID = "+String(DSAns.Id)+"; "
        +"  "
        +"    it_rsl_string.clear (); "
        +"    it_rsl_string.set_clob (p_message); "
        +" END; ";

      SQL_Execute(sqlRead);

      for (var curLine, split(GetStringFromBlobBuf(), "\n"))
        if ((ValType(curLine) == V_STRING) and (Trim(curLine) != ""))
          csvStruct.FillFromStringByRegex(curLine);
          isSuccess = isSuccess and ProcessOneCsvLine(csvStruct, operCloseObj);
        end;
      end;

      var setStat = IIF(isSuccess,1,2);
      UpdateMessageStat(DSAns.Id,setStat);

      AddDBLogDebug("ExecuteSverProc", "Окончание разбора сообщения из таблицы SOFR_IPS_MESSAGE с ID: " + String(DSAns.Id));
    end;

  end;

  return c_ssRunResult(SS_RESPONSE_END);
OnError(exc)
  AddDBLogWithErrorObj("ExecuteSverProc", exc);
  return c_ssRunResult(SS_RESPONSE_FATAL, 1, GetFullErrorMessage(exc));
END;