/*
Name: wrtavr21PKO.mac

Операция: Списание/зачисление ценных бумаг (Списание ценных бумаг ПКО)
Шаг: Проверка лимитов
*/

import "NontradeSecurSendingMessages.mac";
import "cblogger2.mac";
import "PkoWriteOffBuffer.mac";
import oralib;
import "RegvalReader.mac";
import "SfSubcontr.mac";
import "rs_json.mac";

macro ExecuteStep( Doc, ticket, DocKind, OperationID)
    record deal("dl_tick");
    SetBuff(deal, ticket);
    var logBuilder = LoggerFactory().NewItLog("wrtavr21PKO");
    var logger:c_logger = logBuilder.GetLogger();
    logger = logBuilder.WithPrefix(" Операция с ID = " + deal.dealId).GetLogger();
    var buffer = PKOWriteOff(deal.dealId);
    var sfContr = SfSubContr(buffer.GetIdContract());
    var registryPath = "РСХБ\\ИНТЕГРАЦИЯ\\НЕТОРГОВЫЕ ПОРУЧЕНИЯ\\ВЫГРУЗКА В QUIK\\ВЫВОД ЦБ";
    var messageBuilder = NontradeSecurQuikService();
    var messageSender = NontradeSecurKafkaMessageSender();
    var isNeedSendMessage = true;

    if(buffer.GetStep2Reject() == 1)
        logger.Info("Ошибка передачи неторгового поручения в QUIK. Установлен признак отказа.");
        return 0;
    end;
    
    //1 проверка, включена ли соответсвующая настройка
    if(not RegValReader.GetBool(registryPath, false)) 
        logger.Info("Настройка (" + registryPath + ") выключена");
        return 0; 
    end;
    
    //2 проверка что операция не по договору внебиржевого рынка
    if(sfContr.IsExchange())
        logger.Info("Ошибка передачи неторгового поручения в QUIK. Операция идёт по договору внебиржевого рынка.");
        return 0; 
    end;

    //3 проверка если операция пришла со статусом(уже исполнена в диасофт), то не нужна отправка сообщения и проверка лимитов, завершаем работу шага.
    if(buffer.GetPkoStatus() == 7)
        logger.Info("Неторговое поручение по операции уже исполнено в Diasoft, проверка лимитов не требуется.");
        return 0;
    end;

    if(deal.dealDate != Date) 
        logger.Info("Ошибка передачи неторгового поручения в QUIK. Некорректная дата операции.");
        return 0;
    end;

    //4 проверяем отправлялось ли сообщение по операции ранее
    if(messageSender.IsExists(deal.dealId, messageSender.QUIK))
        isNeedSendMessage = MsgBoxEx("Поручение по данной операции уже отправлялось в QUIK ранее. Выполнить повторную отправку?", MB_YES + MB_NO, IND_NO) == IND_YES;
    end;

    if(isNeedSendMessage)
       var guid:string = messageSender.GenerateGUID();
       var messageObject = messageBuilder.CreateWriteOffMessage(deal.dealId, guid);
       var messageBody = "{\"UpdateQuikLimitsNewInstrSecReq\":" + jsn.ClassToStr(messageObject);
       var sendSuccess = messageSender.SaveAndSend(deal.dealId, messageSender.QUIK, guid, messageBody, buffer.GetGUID());
       if(not sendSuccess)
            logger.Error("Ошибка передачи неторгового поручения в QUIK.");  
            return 1;          
       end;
    end;
    
    return 0;

    onError(errObj)
    logger.ErrorClob("Ошибка при обработке операции с ID: " + deal.dealId, GetFullErrMsg(errObj));
    return 1;

end;