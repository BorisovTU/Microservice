/*
$Name:        loan300.mac
$Module:      Ценные бумаги
$Description: Операция займа. Шаг "Начисление процентов"
*/
import "sp_class.mac", "sp_carcm.mac";

private record SpChangeDates("spchdate.str");/*глобальная структура с данными для расчетов*/

macro ExecuteStep( doc, FDoc)

   record deal(dl_tick);
   var    FD, PcAccID, PcBegin, PcEnd,
          pm_avoir_2    = TRecHandler( "pmpaym.dbt" ), /* платеж ценными бумагами по части возврата ц/б*/
          dat = SpChangeDates.FactDate; /*дата подписания договора*/ 

   if( SpChangeDates.Action <= 0 )
      msgbox("Шаг начисления процентов из списка шагов выполнять запрещено."); 
      return 1;
   end; 

   SetBuff( deal, FDoc ); 
   FD = SPFirstDoc( deal );

   /*Дата не меньше факт. даты получения +1*/
   if( dat <= FD.pm_avoir.rec.ValueDate )
      MsgBox( "Дата начисления процентов должна быть больше фактической даты получения ц/б." );
      return 1;
   end;

   /*Если не выполнен перенос на просрочку возврата - не больше даты возврата*/
   if( FindPayment( null, BRi, 0, deal.BOfficeKind, deal.DealID, true, pm_avoir_2 ) != 0 )
      MsgBox( "Не найден платеж по ценным бумагам для части возврата ц/б.");
      return 1;
   end;  

   if( pm_avoir_2.rec.PaymStatus != PM_OVERDUE )
      if( dat > pm_avoir_2.rec.ValueDate )
         MsgBox( "Дата начисления процентов должна быть больше фактической даты получения ц/б." );
         return 1;
      end;
   end;

   PcAccID = SP_GetPercentAccount( deal );
   if( PcAccID <= 0 )
      MsgBox( "Не найден счет процентов для сделки." );
      return 1;
   end;

   /*проверить историю начисления на непрерывность выполненных начислений от даты 
     пред. начисления до заданной даты. Если пропущен очередной период (конец месяца) 
     выдать предупреждение "По сделке пропущено начисление процентов за <месяц> "*/
  // if( not ПроверитьИсториюНачислений( PcAccID, dat, PcBegin, PcEnd) ) 
  //    MsgBox( "Пропущено начисление процентов за период " + string(PcBegin) + " по " + string(PcEnd) );
  //    return 1;
  // end;

   if( SpChangeDates.Summa <= 0 )
      MsgBox( "Нулевая сумма к начислению." );
      return 1;
   end;

   if( not InsertPcAdd( PcAccID, dat, dat, SpChangeDates.Summa ) )
       msgbox( "Ошибка при начислении процентов." );
       return 1;
   end;

   if(not ПроводкаПоКатегориямУчета( FD, 
          "-% начисленные", "-% к погашению",  /* КатегДеб , КатегКредит*/
          dat,                  /* Дата */
          1 ,                   /* Глава */
          FD.dl_leg.rec.CFI,    /* Валюта */
          SpChangeDates.Summa,  /* Сумма */
          Doc,                  /* Документ */
          INPCARRY,             /* Result_Carry */
          " 1",                 /* Kind_Oper */
          FD.tick.rec.DealCode, /* Numb_Document */
          "Начисление процентов" /* Ground */
         ))
       return 1;
   end;

   /*Если старая дата завершения меньше даты начисления, то увеличить первую 
     (такое бывает пр просрочке)*/
   if( FD.DateArray[DATE_DEALEEND] < dat ) /*Переинитить дату завершения сделки*/
      if( not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALEEND), dat ) )
         msgbox("Ошибка при попытке переинициализировать дату завершения операции." );         
         return 1;
      end;
   end;

   return 0;
end;
