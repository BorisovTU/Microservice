/*
$Name:             OpenPosReg_Report.mac
$Module:           АРНУ
$Description:      Отчет "Регистр по открытым позициям" (НУ)
*/

IMPORT "OpenPosReg_Form.mac", "DLReport_h.mac", PtInter, "dlreport.mac", "cb_sql.mac", "sprepfun.mac", "oralib", "ws_txreportform.mac";

PRIVATE CONST H011 = "PP",                                                                                                                                                                                                                                                                                                   
              H01  = "07",                                                                                                                                                                                                                                                                                                   
              H02  = "XXX",                                                                                                                                                                                                                                                                                                  
              H03  = "20",                                                                                                                                                                                                                                                                                                   
              ItogsLineName = "Всего:";

PRIVATE CONST SHEET_0720  = "0720";

PRIVATE VAR   T_Dep:T_Dep_Collector;

PRIVATE VAR   OpenPosReg_Form;
PRIVATE VAR   OpenPosReg_Report;
PRIVATE VAR   WebMenuItem; // Информация о запуске отчета из веб

PRIVATE VAR ReportHeader =
"\n\n##############################  ###########################\n" +
"Приложение № ##.## \n" +
"Аналитический регистр налогового учета № 1-02-#####-###-##-000-##-210302-##-##-70102000000000000000\n" +
"                ┌──────────────────────┬─────────────────────────────────────────────────┐\n" +
"                │                    1 │Наименование налоговой базы                      │\n" +
"                ├──────────────────────┼─────────────────────────────────────────────────┤\n" +
"                │                   02 │Лист декларации                                  │\n" +
"                ├──────────────────────┼─────────────────────────────────────────────────┤\n" +
"                │                ##### │Номер приложения синтетического регистра         │\n" +
"                ├──────────────────────┼─────────────────────────────────────────────────┤\n" +
"                │                  ### │Код строки в синтетическом регистре              │\n" +
"                ├──────────────────────┼─────────────────────────────────────────────────┤\n" +
"                │                   ## │Номер аналитического регистра                    │\n" +
"                ├──────────────────────┼─────────────────────────────────────────────────┤\n" +
"                │                  000 │Код филиала                                      │\n" +
"                ├──────────────────────┼─────────────────────────────────────────────────┤\n" +
"                │                   ## │                                                 │\n" +
"                ├──────────────────────┼─────────────────────────────────────────────────┤\n" +
"                │               210302 │Код структурного подразделения внутри филиала    │\n" +
"                ├──────────────────────┼─────────────────────────────────────────────────┤\n" +
"                │                   ## │Номер месяца (последний в отчетном периоде)      │\n" +
"                ├──────────────────────┼─────────────────────────────────────────────────┤\n" +
"                │                   ## │Две последние цифры года                         │\n" +
"                ├──────────────────────┼─────────────────────────────────────────────────┤\n" +
"                │ 70102000000000000000 │Номер лиц. счета по учету доходов/расходов банка │\n" +
"                └──────────────────────┴─────────────────────────────────────────────────┘\n" +
"#######################################################################################################################################################\n" +
"на ########## \n" +
"Группа налогового учета: #################################################################\n" +
"Вид ц/б:                 #################################################################\n" +
"Выпуск ц/б:              #################################################################\n";
                                                                                                                                                                                                                                                                                                                             
PRIVATE VAR TableHeader =                                                                                                                                                                                                                                                                   
"┌────────────────────┬────────────────────┬────────────────────┬──────────┬──────────┬───────────┬──────┬───────────────────────────────────┬───────────────────────────────┬──────────────────────────────────────────────────────────┬────────────────────┬────────────────────┬────┐\n" +   
"│Код бумаги          │      Выпуск ценной │Номер сделки        │ Дата     │Дата      │Количество │Валюта│Стоимость реализации               │НКД полученный при реализации  │Сделка РЕПО/займа, по которой открыта короткая позиция    │Контрагент          │ГО/филиал           │Нал │\n" +               
"│                    │         бумаги     │ реализации         │закл.     │реализа   │бумаг в    │номина│(Стоимость открытия КП)            │ (НКД на дату открытия КП)     │                                                          │по сделке           │(для сделки         │ого │\n" +               
"│                    │                    │                    │сделки    │ции (дата │открытой   │ла    ├─────────────────┬─────────────────┼─────────────────┬─────────────┼────────────────────┬───┬───────────┬──────────┬──────────┤реализации          │ продажи)           │вая │\n" +               
"│                    │                    │                    │на        │открытия  │на отчетную│      │Сумма в          │ Сумма, руб.     │ Сумма в         │ Сумма, руб. │Номер               │Тип│Кол-во в   │Дата 1    │ Дата 2   │                    │                    │груп│\n" +               
"│                    │                    │                    │реализ.   │короткой  │дату КП,шт.│      │валюте           │                 │ валюте          │             │                    │   │сделке     │ части    │  части   │                    │                    │па  │\n" +
"│                    │                    │                    │ЦБ        │позиции)  │           │      │номинала         │                 │ номинала        │             │                    │   │           │          │          │                    │                    │    │\n" +
"├────────────────────┼────────────────────┼────────────────────┼──────────┼──────────┼───────────┼──────┼─────────────────┼─────────────────┼─────────────────┼─────────────┼────────────────────┼───┼───────────┼──────────┼──────────┼────────────────────┼────────────────────┼────┤\n" +
"│    1               │            2       │       3            │     4    │    5     │      6    │   7  │       8         │       9         │      10         │      11     │     12             │ 13│     14    │     15   │     16   │     Т1             │     Т2             │ Т3 │\n" +
"├────────────────────┼────────────────────┼────────────────────┼──────────┼──────────┼───────────┼──────┼─────────────────┼─────────────────┼─────────────────┼─────────────┼────────────────────┼───┼───────────┼──────────┼──────────┼────────────────────┼────────────────────┼────┤";

/*Отчет "Регистр неттинга"*/
PRIVATE CLASS (DL_CReportTemplate) SP_OpenPosReg_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  PRIVATE VAR m_AvrGroup, m_QueryRest,
              m_Avoiriss = TRecHandler( "avoiriss.dbt" ),
              m_Fininstr = TRecHandler( "fininstr.dbt" );

  PRIVATE VAR m_IsDataExist = false; /*для Web, если нет данных, то ничего не показываем*/

 /* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsDataExist or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END; 

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport()
     VAR Month, Year, INN, KPP,
         AttrID = OpenPosReg_Form.GetFieldValue( PNFLD_OPENPOSREG_AVRGROUP ),
         EndDate = OpenPosReg_Form.GetFieldValue( PNFLD_OPENPOSREG_ENDDATE ),
         GroupName = "",
         AvrKindShowValue = "";

     OpenPosReg_Form.GetFieldValue( PNFLD_OPENPOSREG_AVRGROUP, @GroupName );
     OpenPosReg_Form.GetFieldValue( PNFLD_OPENPOSREG_AVRKIND, @AvrKindShowValue );
     DateSplit( EndDate, null, Month, Year );

     Year = SubStr( string(Year), strlen(string(Year))-1 );
     SplitFullINN( ПолучитьКодСубъекта({OurBank}, PTCK_INN), INN, KPP );

     SetActiveSheet( SHEET_0720 );
     PrintFormatString( ReportHeader,
                        "N1_H0_A:l" , ПолучитьКороткоеИмяСубъекта({OurBank}),  // Банк
                        "N1_H0_B:l" , INN + "/" + KPP,  // Инн/КПП
                        "N1_H0_1"   , H01,  // Приложение №
                        "N1_H0_3"   , H03,
                        "N1_H0_12"  , H011, // Аналитический регистр налогового учета №
                        "N1_H0_2"   , H02,
                        "N1_H0_3"   , H03,
                        "N1_H0_1"   , H01,
                        "N1_H0_4"   , string(Month),
                        "N1_H0_5"   , string(Year),
                        "N1_H0_12:r"  , H011, // Номер приложения синтетического регистра
                        "N1_H0_2"   , H02,  // Код строки в синтетическом регистре
                        "N1_H0_3"   , H03,  // Номер аналитического регистра
                        "N1_H0_1"   , H01,
                        "N1_H0_4"   , string(Month:2:o),  // Номер месяца (последний в отчетном периоде)
                        "N1_H0_5"   , string(Year:2:o),   // Две последние цифры года
                        "N1_H0_6"   , "Реестр коротких позиций, не закрытых  на конец отчетного  (налогового) периода",  // Имя отчета
                        "N1_H0_7"   , EndDate,            // на 
                        "N1_H0_8"   , GroupName,          // Группа налогового учета
                        "N1_H0_9"   , AvrKindShowValue,   // Вид ц/б
                        "N1_H0_10"  , OpenPosReg_Form.GetFieldValue( PNFLD_OPENPOSREG_AVRNAME )    // Выпуск ц/б
                      );

     RegisterTable( "N1_MainTable", TableHeader,
                    "N1_SecCode", "N1_SecName", "N1_CodeS",      "N1_DZS",        "N1_DDS",    "N1_QntyOpen", "N1_VN",
                    "N1_SumSvn",  "N1_SumSrub", "N1_NkdSVN",     "N1_NkdSrub",    "N1_CodeOR", "N1_TypeOR",   "N1_QntyDeal",
                    "N1_DD1OR",   "N1_DD2OR",   "N1_ContS",      "N1_FilS",       "N1_SecType"     
                  );

     SetCellAutoSummaPoint( ItogsLineName, "N1_QntyOpen",6, "N1_SumSrub",2, "N1_NkdSrub",2 );
  END;

  PRIVATE MACRO SetAutoFilter()
     if( ExistsSheet( SHEET_0720 ) )
        m_ObjTemplateXLS.SetAutoFilter("A28:S28", SHEET_0720);
     end;
  END;

  PRIVATE MACRO EndReport()
     EndTable();

     SetSubtotal(28, 26 + MAXROWSHEET,
                 "F23", "I23", "K23"
                );

     AddStandardFooter( "N1_" );

     SetAutoFilter();
  END;

  /*Для инвестиционных паев выводится с точностью, предусмотренной в анкете пая
    Получить самую большую точность, которая будет в колонках суммы для отображения поля "Итого"
  */
  PRIVATE MACRO GetTotalAmountPoint()
     VAR RS = TRsbDataSet( " SELECT   NVL(MAX(fininstr.t_SumPrecision),0) AS SumPrecision " +
                           "   FROM   (" + m_QueryRest + ") QueryRest, dfininstr_dbt fininstr " +
                           "  WHERE   QueryRest.t_FIID = fininstr.t_FIID "

                         );
     if( RS.MoveNext() )
        return RS.SumPrecision;
     end;
     return NULL;
  END;

  PRIVATE MACRO GetTaxLot( Lot:@Object, LotID:INTEGER ):BOOL
     var sql = RSDCommand("SELECT * FROM DSCTXLOT_DBT WHERE t_ID = ? ");
     sql.addParam( "", RSDBP_IN, LotID );
     sql.execute();
     Lot = TRsbDataSet( sql );
     return ( (Lot.MoveNext() == true) AND (Lot.t_ID > 0) );
  END;

  PRIVATE MACRO GetDeal( Deal:TBFile, DealID:INTEGER ):BOOL
     Deal.Clear();
     Deal.rec.DealID = DealID;
     return Deal.GetEQ();
  END;

  /*Получить дату договора купли/продажи
    Это дата документа по сделке. Вид документа - "договор купли продажи" . Если такого документа нет - то дата заключения сделки*/
  PRIVATE MACRO GetDateContract( Deal:TBFile, DealDate:DATE ):DATE
     VAR DocDate = DealDate;

     /* 26 - договор купли продажи*/
     if( (Deal != NULL) AND 
         (ПолучитьДокументПоСделке( Deal.rec.BofficeKind, Deal.rec.DealID, 26, null, DocDate ) == false)
       )
        DocDate = Deal.rec.DealDate;
     end;
     return DocDate;
  END;

  /* Получить наименование филиала из сделки*/
  PRIVATE MACRO GetDepartmentName( Deal:TBFile ):STRING
     VAR DprtName = "";

     if( Deal != NULL ) 
        DprtName = T_Dep.Get_Dep(Deal.rec.Department);
     end;
     return DprtName;
  END;

  /* Получить наименование контрагента сделки*/
  PRIVATE MACRO GetContractorName( Deal:TBFile ):STRING
     if( (Deal != NULL) AND (Deal.rec.PartyID > 0) )                                           
        return ПолучитьКороткоеИмяСубъекта( Deal.rec.PartyID );
     end;
     return "";
  END;

  PRIVATE MACRO IsSale( LotType:INTEGER ):BOOL
     var params:TArray = TArray();
     params[0] = SQLParam( "v_Type", LotType );

     return (execStoredFunc( "Rsb_SCTXC.TXIsSale", V_INTEGER, params ) == 1);
  END;

  PRIVATE MACRO GetDealType( LotType:INTEGER ):STRING

     if( LotType == TXLOTS_BUY )
        return "B";
     elif( LotType == TXLOTS_SALE )
        return "S";
     elif( LotType == TXLOTS_REPO )
        return "РП";
     elif( LotType == TXLOTS_BACKREPO )
        return "РО";
     elif( LotType == TXLOTS_LOANPUT )
        return "ЗП";
     elif( LotType == TXLOTS_LOANGET )
        return "ЗО";
     else
        return "";
     end;
     
  END;

  PRIVATE MACRO GetDealFactPaymDate(DealID , Type, DealPart)
     var ValueDate = date(0,0,0);
     var DataSet;
     var PType;

     var sql = RSDCommand( " SELECT (NVL( (SELECT min(pm.t_FactDate) " +
                           "         FROM ddl_tick_dbt tk, DDLRQ_DBT pm " + 
                           "         WHERE tk.t_DealID = ? " +
                           "           AND pm.t_DocKind    = tk.t_BOfficeKind " +
                           "           AND pm.t_DocID = tk.t_DealID " +
                           "           AND pm.t_FactDate > TO_DATE('01-01-0001','DD-MM-YYYY') " +
                           "           AND pm.t_Type = ? " +
                           "           AND pm.t_DealPart = ? " + 
                           "       ), " +
                           "       (NVL( (SELECT min(pm.t_PlanDate) " +
                           "              FROM ddl_tick_dbt tk, DDLRQ_DBT pm " + 
                           "              WHERE tk.t_DealID = ? " +
                           "                 AND pm.t_DocKind    = tk.t_BOfficeKind " +
                           "                 AND pm.t_DocID = tk.t_DealID " +
                           "                 AND pm.t_FactDate = TO_DATE('01-01-0001','DD-MM-YYYY') "+
                           "                 AND pm.t_Type = ? " +
                           "                 AND pm.t_DealPart = ? " + 

                           "       ), TO_DATE('01-01-0001','DD-MM-YYYY') ) " +
                           "       )" +
                           "     )" +
                           " ) as FactDate FROM dual"
                         );
     
     sql.addParam( "", RSDBP_IN, DealID );
     sql.addParam( "", RSDBP_IN, Type );
     sql.addParam( "", RSDBP_IN, DealPart );
     sql.addParam( "", RSDBP_IN, DealID );
     sql.addParam( "", RSDBP_IN, Type );
     sql.addParam( "", RSDBP_IN, DealPart );
     
     sql.execute();
     
     DataSet = TRsbDataSet( sql );
     if(DataSet.moveNext())
       ValueDate = SQL_ConvTypeDate(DataSet.FactDate);
     end;

    return ValueDate; 
  END;

  PRIVATE MACRO GetDealParms( LotID:INTEGER, DealAmount:@VARIANT, DealType:@STRING, DealCode:@STRING, DealTime:@VARIANT, Date1:@VARIANT, Date2:@VARIANT)
     VAR Lot = NULL;
     VAR dl_leg = TRecHandler( "dl_leg.dbt" );

     DealType   = "";
     DealCode   = "";
     DealAmount = "";
     DealTime   = "";
     Date1      = "";
     Date2      = "";

     if( (LotID != null) AND (LotID > 0) )
        if( GetTaxLot( @Lot, LotID ) == false )
           MsgBox( "Не найден лот (DSCTXLOT_DBT.t_ID = " + string(LotID) + " )"  );
        else
           DealType = GetDealType( Lot.t_Type );
           DealCode   = Lot.t_DealCodeTS;
           DealTime   = Time(Lot.t_DealTime);

           if( FindDL_LEG( LEG_KIND_DL_TICK, Lot.t_DealID, 0, dl_leg ) == 0 )
             DealAmount = dl_leg.rec.Principal;
           end;

           Date1 = SQL_ConvTypeDate(GetDealFactPaymDate(Lot.t_DealID, DLRQ_TYPE_DELIVERY, 1));
           Date2 = SQL_ConvTypeDate(GetDealFactPaymDate(Lot.t_DealID, DLRQ_TYPE_DELIVERY, 2));

           if( Date2 == Date(0,0,0) )
              if( FindDL_LEG( LEG_KIND_DL_TICK_BACK, Lot.t_DealID, 0, dl_leg ) == 0 )
              /*то плановая дата поставки по 2 части сделки */
                 Date2 = GetDealPlanDate( dl_leg.rec, null, false, false );
              end;                  
           end;
        end;
     end;
  END;

  PRIVATE MACRO LegKind( Deal:TBFile ):INTEGER
     VAR Group = GetOpGroup( Deal.rec );
     return GetLegKindByBackFlag( IsBUY(Group) OR IsAVRWRTIN(Group) );
  END;

  PRIVATE MACRO GetNKD( RS, Deal:TBFile, OpenDate:DATE, Ratio:DOUBLE, NKD:@MONEY, NKD_RUR:@MONEY )
     VAR dl_leg, NKDconv = $0;

     NKD     = $0;
     NKD_RUR = $0;

     if( RS.t_VirtualType != TXVDEAL_REAL )
        NKD = НКД( RS.t_FIID, money(RS.Amount), OpenDate );
     else
        if( Deal != null )
           dl_leg = TRecHandler( "dl_leg.dbt" );
           if( FindDL_LEG( LegKind( Deal ), Deal.rec.DealID, 0, dl_leg ) == 0 )
              NKD = dl_leg.rec.NKD;
           end;                  
        end;
     end;

     NKD = NKD*Ratio;
     SmartConvertSum( NKDconv, NKD, OpenDate, m_Fininstr.rec.FaceValueFI, NATCUR, true );
     NKD_RUR = NKDconv;
  END;

  PRIVATE MACRO Create()
     VAR DataQuery, RS, AvrFIID, AvrKind, AddTable = "", AddWhere = "",
        AvrFIIDList = null,
        AvrFIIDCount : Integer = 0,
        AvrFIIDAddWhere : String = "",
        WasSetSecurity : Bool = false,

        AvrKindList = null,
        AvrKindAddWhere : String = "",
        AvrKindCount : Integer = 0,
         
        GNUList = null,
        GNUAddWhere : String = "",
        GNUCount : Integer = 0;
     VAR DealSale, dlleg, CostNominal = $0, CostRUR = $0, OpenDate, SaleAmount = 0,
         SourceDealAmount = 0, SourceDealType = "", SourceDealCode = "", 
         Date1, Date2, NKD_RUR, NKD, AmountPoint = 0, PartyNameSale = "", DepartmentName = "", SourceDealTime;
     VAR EndDate = OpenPosReg_Form.GetFieldValue( PNFLD_OPENPOSREG_ENDDATE );
     VAR ProgressValue = CTxProgressValue();
                   
     T_Dep.ClearArray();
     /*ценные бумаги в соединенных сделках  должны удовлетворять условиям, заданным панели подготовки отчета*/
     if( not m_IsWebService )
        AvrFIIDList = TArray();
        AvrFIIDList[AvrFIIDList.Size] = AvrFIID = OpenPosReg_Form.GetFieldValue( PNFLD_OPENPOSREG_AVRCODE );
     else
        AvrFIIDList = AvrFIID = OpenPosReg_Form.GetFieldValue( PNFLD_OPENPOSREG_AVRCODE );
     end;
     if( ( ValType( AvrFIIDList ) != V_UNDEF ) and ( AvrFIIDList.Size > 0 ) )
        while( AvrFIIDCount < AvrFIIDList.Size )
           if( ( ValType( AvrFIIDList[AvrFIIDCount] ) == V_INTEGER )
           and ( AvrFIIDList[AvrFIIDCount] > 0 ) )
              if( AvrFIIDAddWhere == "" )
                 AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
              else
                 AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
              end;
              AvrFIIDAddWhere = AvrFIIDAddWhere + " TXRest.t_FIID = " + String( AvrFIIDList[AvrFIIDCount] ) + " ";
           end;
           AvrFIIDCount = AvrFIIDCount + 1;
        end;
        if( AvrFIIDAddWhere != "" )
           AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
           WasSetSecurity = true;
        end;
        AddWhere = AddWhere + AvrFIIDAddWhere;
     end;

     /*если не задана бумага*/
     if( not WasSetSecurity )
        if( not m_IsWebService )
           AvrKindList = TArray();
           AvrKindList[AvrKindList.Size] = AvrKind  = OpenPosReg_Form.GetFieldValue( PNFLD_OPENPOSREG_AVRKIND );
        else
           AvrKindList = AvrKind  = OpenPosReg_Form.GetFieldValue( PNFLD_OPENPOSREG_AVRKIND );
        end;
        /*задан вид бумаги*/
        if( ( ValType( AvrKindList ) != V_UNDEF ) and ( AvrKindList.Size > 0 ) )
           while( AvrKindCount < AvrKindList.Size )
              if( ( ValType( AvrKindList[AvrKindCount] ) == V_INTEGER )
              and ( AvrKindList[AvrKindCount] > 0 ) )
                 if( AvrKindAddWhere == "" )
                    AvrKindAddWhere = AvrKindAddWhere + " AND ( ";
                 else
                    AvrKindAddWhere = AvrKindAddWhere + " OR ";
                 end;
                 AvrKindAddWhere = AvrKindAddWhere + " RSB_FIInstr.FI_AvrKindsEQ(2, " + String( AvrKindList[AvrKindCount] ) + ", FinInstr.t_AvoirKind) = 1 ";
              end;
              AvrKindCount = AvrKindCount + 1;
           end;
           if( AvrKindAddWhere != "" )
              AvrKindAddWhere = AvrKindAddWhere + " ) ";
           end;
           AddWhere = AddWhere + AvrKindAddWhere;      
        end;

        if( not m_IsWebService )
           GNUList = TArray();
           GNUList[GNUList.Size] = OpenPosReg_Form.GetFieldValue( PNFLD_OPENPOSREG_AVRGROUP );
        else
           GNUList = OpenPosReg_Form.GetFieldValue( PNFLD_OPENPOSREG_AVRGROUP );
        end;
        /*задана категория бумаги*/
        if( ( ValType( GNUList ) != V_UNDEF ) and ( GNUList.Size > 0 ) )
           while( GNUCount < GNUList.Size )
              if( ( ValType( GNUList[GNUCount] ) == V_INTEGER )
              and ( GNUList[GNUCount] > 0 ) )
               if( GNUAddWhere == "" )
                    GNUAddWhere = GNUAddWhere + " AND ( ";
                 else
                    GNUAddWhere = GNUAddWhere + " OR ";
                 end;
                 GNUAddWhere = GNUAddWhere + " av.t_TaxGroup = " + String( GNUList[GNUCount] ) + " ";
              end;
              GNUCount = GNUCount + 1;
           end;
           if( GNUAddWhere != "" )
              GNUAddWhere = GNUAddWhere + " ) ";
           end;
           AddWhere = AddWhere + GNUAddWhere;
        end;
     end;

     m_QueryRest = " SELECT NVL(TXRest.T_SALEID,0) AS SaleID, NVL(Lot.T_BegLotID,0) AS SourceID, NVL(sum(TXRest.T_AMOUNT),0) AS Amount," +
                 "        FinInstr.t_FIID"+
                 "   FROM DSCTXREST_DBT   TXRest,"+
                 "        DFININSTR_DBT FinInstr, " + 
                 "        DAVOIRISS_DBT av, " + 
                 "        DSCTXLOT_DBT Lot"
                 "  WHERE ( TXRest.t_Type = " + string(TXREST_Open) + " OR " +
                 "          TXRest.t_Type = " + string(TXREST_Closed) +
                 "        ) AND " +
                 "        TXRest.T_AMOUNT > 0 AND " +
                 "        TXRest.t_SALEDATE <= " + GetSQLDate( EndDate ) + " AND " +
                 "        ( TXRest.t_BUYDATE IS NULL  OR " +
                 "          TXRest.t_BUYDATE = " + GetSQLDate( DATE(0,0,0) ) + " OR " + 
                 "          TXRest.t_BUYDATE > " + GetSQLDate( EndDate ) +
                 "        ) AND " +
                 "        FinInstr.t_FIID = TXRest.t_FIID  AND " +
                 "        FinInstr.t_FIID = av.t_FIID AND " +
                 "        Lot.t_ID (+)= TXRest.T_SourceID" +
                          AddWhere +
                 " GROUP BY FinInstr.t_FIID, TXRest.T_SALEID, Lot.T_BegLotID";

     DataQuery = " SELECT   QueryRest.t_FIID, QueryRest.Amount, QueryRest.SourceID AS SourceID, " +
                 "          TXSaleLot.t_ID LotSaleID, TXSaleLot.t_BuyID LotBuyID, TXSaleLot.t_DealCodeTS, TXSaleLot.T_SALEDATE, TXSaleLot.T_DEALID AS DealSaleID, TXSaleLot.T_DEALDATE AS DealSaleDate, TXSaleLot.t_PriceFIID, TXSaleLot.t_Price AS SalePrice, TXSaleLot.t_DealTime, TXSaleLot.t_VirtualType, " +
                            
                            Get_PartyName_Query( "DealSale", "PartyNameSale" ) + " " +/*PartyNameSale*/
                 "   FROM   (" + m_QueryRest + ") QueryRest," +
                 "          DSCTXLOT_DBT  TXSaleLot, DSCTXLOT_DBT currTXSaleLot, DDL_TICK_DBT DealSale " +
                 "  WHERE   currTXSaleLot.t_ID = QueryRest.SALEID AND TXSaleLot.t_ID = currTXSaleLot.t_BegLotID " +
                 "    AND   DealSale.t_DealID = TXSaleLot.T_DEALID "
                 " ORDER BY TXSaleLot.T_SALEDATE, TXSaleLot.t_DealCodeTS";

     ProgressValue.Maximum = SQL_GetNRecs( DataQuery );
     ProgressValue.Current = 0;

     var ProgressIndicator = CreateProgressIndicator(m_IsWebService);

     if( m_IsWebService and (WebMenuItem != null) )
       var header = "Формирование отчета " + WebMenuItem.szNameItem;
       ProgressIndicator.Start(ProgressValue.Maximum, header, header);
     else
       ProgressIndicator.Start(ProgressValue.Maximum, HTML_StSheetName, HTML_StSheetName);
     end;

     SetCurrentLineFont( 8, true, false );
     PrintTableLine( "N1_SecCode",  ItogsLineName, null,
                     "N1_QntyOpen", null, GetTotalAmountPoint() /*только установить точность в поле*/
                   );
     SetCurrentLineFont( 8, false, false );
     PrintTableLine( "N1_SecCode",  "В том числе:", null );

     RS = TRsbDataSet( DataQuery );
     while( RS.MoveNext() )
        PartyNameSale = ""; 
        DepartmentName = "";
        CostNominal = CostRUR = $0;

        if( ПолучитьФинИн( RS.t_FIID, m_Fininstr, m_Avoiriss ) ) 
           MsgBox ("Не найден ФИ остатка (DV_SCTXREST.t_FIID =", RS.t_FIID,")" );
           continue;
        end;

        /* Дата реализации (дата открытия короткой позиции) */
        OpenDate = SQL_ConvTypeDate(RS.T_SaleDate);

        SaleAmount = RS.Amount;

        if( RS.DealSaleID > 0 )
        
           DealSale = TBFile( "dl_tick.dbt", "R", 0 );
           if( GetDeal( DealSale, RS.DealSaleID ) == false )
              MsgBox( "Не найдена сделка продажи по лоту налогового учета DSCTXLOT_DBT.t_DealID = " + string(RS.DealSaleID)  );
              continue;
           end;

           PartyNameSale = RS.PartyNameSale;
           DepartmentName = GetDepartmentName( DealSale );
           if((RS.t_VirtualType != TXVDEAL_REAL) and (SQL_ConvTypeDate(RS.DealSaleDate) < date(1,1,2010)))
             PartyNameSale = "";
             DepartmentName = "";
           end;

           dlleg = TRecHandler( "dl_leg.dbt" );
           if( FindDL_LEG( LegKind( DealSale ), RS.DealSaleID, 0, dlleg ) == 0 )
              SaleAmount = dlleg.rec.Principal;
              CostNominal = dlleg.rec.Cost * RS.Amount / dlleg.rec.Principal;
           end;
        else
           DealSale = null;
        end;

        if( RS.PriceFIID >= 0 )
           SmartConvertSum( CostNominal, RS.SalePrice * RS.Amount, OpenDate, RS.PriceFIID, m_Fininstr.rec.FaceValueFI, true );
           SmartConvertSum( CostRUR, RS.SalePrice * RS.Amount, OpenDate, RS.PriceFIID, NATCUR, true );
        else
           CostNominal = CostRUR = $0;
        end;

        GetDealParms( RS.SourceID, @SourceDealAmount, @SourceDealType, @SourceDealCode, @SourceDealTime, @Date1, @Date2 );
        
        GetNKD( RS, DealSale, OpenDate, RS.Amount/SaleAmount, @NKD, @NKD_RUR );

           AmountPoint = m_Fininstr.rec.SumPrecision;
           var Precision = 0;
           if( IsHaveFractPart(RS.Amount) OR IsHaveFractPart(SourceDealAmount) )
              Precision = AmountPoint;
           end;

        m_IsDataExist = true;
        PrintTableLine( "N1_SecCode:l",   m_Fininstr.rec.FI_Code,                                                               null,        // Код ЦБ
                        "N1_SecName:l",   m_Fininstr.rec.Name,                                                                  null,        // Краткое наименование ценной бумаги
                        "N1_CodeS:l",     RS.t_DealCodeTS,                                                                      null,        // № сделки продажи
                        "N1_DZS:r",       SQL_ConvTypeDate(RS.DealSaleDate),                                                    null,        // Дата заключения сделки на реализацию ЦБ
                        "N1_DDS:r",       OpenDate,                                                                             null,        // Дата реализации (дата открытия короткой позиции)
                        "N1_QntyOpen:r:a",RS.Amount,                                                                            Precision, // Ко-во ЦБ
                        "N1_VN:r" ,       ПолучитьКодФинИн ( m_Fininstr.rec.FaceValueFI, null, FICK_ISONUMBER ),                null,        // Валюта номинала
                        "N1_SumSvn:r" ,   CostNominal,                                                                          null,        // Стоимость, в валюте номинала
                        "N1_SumSrub:r" ,  CostRUR,                                                                              null,        // Стоимость открытия  КП - в рублях
                        "N1_NkdSVN:r" ,   NKD,                                                                                  null,        // НКД на дату открытия КП в валюте
                        "N1_NkdSrub:r" ,  NKD_RUR,                                                                              null,        // НКД на дату открытия КП в рублях
                        "N1_CodeOR:r" ,   SourceDealCode,                                                                       null,        // № РЕПО/займа
                        "N1_TypeOR:r" ,   SourceDealType,                                                                       null,        // Тип РЕПО/займа
                        "N1_QntyDeal:r" , SourceDealAmount,                                                                     Precision,        // Кол-во в РЕПО
                        "N1_DD1OR:r" ,    Date1,                                                                                null,        // Дата 1 части
                        "N1_DD2OR:r" ,    Date2,                                                                                null,        // Дата 2 части
                        "N1_ContS:r" ,    PartyNameSale,                                                                        null,        // Контрагент
                        "N1_FilS:r" ,     DepartmentName,                                                                       null,        // Краткое наименование филиала из сделки продажи
                        "N1_SecType:r" ,  m_Avoiriss.rec.TaxGroup,                                                              null         // Налоговая группа
                      );
        ProgressValue.Current = ProgressValue.Current + 1;
        ProgressIndicator.Update(ProgressValue.Current,ProgressValue.Maximum); // всего 3 парамера Update(index, count, text)
     end;

     ProgressIndicator.Stop();
  END;
  
  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  if( not IsWebService )
    OpenPosReg_Form = SP_OpenPosReg_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      OpenPosReg_Form = WS_TX_OpenPosReg_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = OpenPosReg_Form.Run();
    end;

    if( stat )
      OpenPosReg_Report = SP_OpenPosReg_Report( null, "Report_TxOpenPos.xls", null, IsWebService, ReportHashCode );
      OpenPosReg_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat( OpenPosReg_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
        FullReportFileNames = OpenPosReg_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat( OpenPosReg_Report.PrintMode() );
      end;
    end;
  end;

  return FullReportFileNames;
END;
