/*
$Name:             sctaxlot_Form.mac
$Module:           АРНУ
$Description:      Отчет "Связывание лотов в налоговом учете" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_SCTXLOT_REG_PERIOD      = 0,
      PNFLD_SCTXLOT_REG_YEAR        = 1,
      PNFLD_SCTXLOT_REG_GROWTH      = 2,
      PNFLD_SCTXLOT_REG_BEGDATE     = 3,
      PNFLD_SCTXLOT_REG_ENDDATE     = 4,
      PNFLD_SCTXLOT_REG_AVRKIND     = 5,
      PNFLD_SCTXLOT_REG_AVRCODE     = 6,
      PNFLD_SCTXLOT_REG_AVRNAME     = 7,
      PNFLD_SCTXLOT_REG_PRINTMETHOD = 8;

CLASS SCTAXLOTRegFormData()
  VAR Period,
      Year,
      GrowingItog,
      BegDate,
      EndDate;

  /* Значение по умолчанию */
  Period = 0;
  Year = 0;
  BegDate = date(0,0,0);
  EndDate = date(0,0,0);
END;

VAR FormData = SCTAXLOTRegFormData();


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_TxLots_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal;

     DateSplit( {curdate}, null, CurMonth, FormData.Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal   = 4;
        FormData.Year = FormData.Year - 1;
     end;

     FormData.Period = PrevQuartal + 12; /*(+12 - для NameAlg.dbt)*/
     FormData.GrowingItog = "X";

     Period_GetInterval(FormData.Period, FormData.Year, @FormData.BegDate, @FormData.EndDate, true);

     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;

     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     AddFieldProc( 0, PNFLD_SCTXLOT_REG_PERIOD,      DL_PNFLD_PERIOD,      @DL_FldProc_Period,      FormData.Period, 24, 4 ); 
     AddFieldProc( 0, PNFLD_SCTXLOT_REG_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year,        FormData.Year );
     AddFieldProc( 0, PNFLD_SCTXLOT_REG_GROWTH,      DL_PNFLD_GROWTH,      @DL_FldProc_Growth,      FormData.GrowingItog );
     AddFieldProc( 0, PNFLD_SCTXLOT_REG_BEGDATE,     DL_PNFLD_BEGINDATE,   @DL_FldProc_BeginDate,   FormData.BegDate );
     AddFieldProc( 0, PNFLD_SCTXLOT_REG_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate,     FormData.EndDate );
     AddFieldProc( 0, PNFLD_SCTXLOT_REG_AVRKIND,     DL_PNFLD_AVRKIND,     @DL_FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_SCTXLOT_REG_AVRCODE,     DL_PNFLD_AVRCODE,     @DL_FldProc_AvrCode  );
     AddFieldProc( 0, PNFLD_SCTXLOT_REG_AVRNAME,     DL_PNFLD_AVRNAME,     @Default );
     AddFieldProc( 0, PNFLD_SCTXLOT_REG_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 44, 13 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "SCTAXLOT" );
END;

CLASS ( TxReportForm ) WS_TX_TxLots_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_SCTXLOT_REG_PERIOD]      = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_SCTXLOT_REG_YEAR]        = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_SCTXLOT_REG_GROWTH]      = TXREPORTFORM_FLD_ADDITOG;
  ReDefineFieldNum[PNFLD_SCTXLOT_REG_BEGDATE]     = TXREPORTFORM_FLD_DATEBEGIN;
  ReDefineFieldNum[PNFLD_SCTXLOT_REG_ENDDATE]     = TXREPORTFORM_FLD_DATEEND;
  ReDefineFieldNum[PNFLD_SCTXLOT_REG_AVRKIND]     = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_SCTXLOT_REG_AVRCODE]     = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_SCTXLOT_REG_AVRNAME]     = TXREPORTFORM_FAKEFLD_AVRNAME;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;