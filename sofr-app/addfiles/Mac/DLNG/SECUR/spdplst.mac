/*----------------------------------------
Переведен на новые счета депо
/*17 May 2000, VSH*/ 
------------------------------------------*/
import spReport, SecInter, dpstdrep;

var HeadTable = "┌────────────────────┬────────────────────────┬────────────────┬────────────────┐\n"+
                "│        Счет        │     Название счета     │      Дата      │      Дата      │\n"+
                "│                    │                        │    открытия    │    закрытия    │\n"+
                "├────────────────────┼────────────────────────┼────────────────┼────────────────┤";
                
var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

macro PrintLine( depoacnt )
  var Code, Name, OpenDate, Closedate = "";
  //код счета
  Code     = depoacnt.Code;
  //название счета
  Name     = depoacnt.Name;
  //дата открытия
  OpenDate = date(depoacnt.OpenDate);
  //дата закрытия
  if( depoacnt.CloseDate != date(0,0,0))
    CloseDate = date(depoacnt.CloseDate);
  end;

  DP_AddPrintCell( Rep,  Code, 0, 0, "c:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  Name, 0, 0, "c:w" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  OpenDate, 0, 0, "c:" + RepFontStyleTitel );
  DP_AddPrintCell( Rep,  Closedate, 0, 0, "c:" + RepFontStyleTitel );
  Rep.AddStr();
end;


macro depolist(dprt_num : integer, rep_date : date, ToExcel:bool)
  var depoacnt;
  var query;
  var count = 0;
  var stat = 0;
  var i = 0;

  Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
  DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

  query =   " select t_Code, t_Name, t_OpenDate, t_CloseDate "
          + " from ddepoacnt_dbt "
          + " where t_Superior = 0 "
          + "   and t_OpenDate <= " + GetSQLDate(rep_date);
  if (  dprt_num != 0  )
      query = query + " and t_Department = " + dprt_num;
  end;

  query = query + " order by t_OpenDate ";

  depoacnt = TRsbDataSet(query,  RSDVAL_CLIENT, RSDVAL_STATIC );
  depoacnt.MoveLast();
  count = depoacnt.GetRecCount();

  if( count > 0 )
    InitProgress (count, "Поиск счетов для отчета", "Просмотр счетов ДЕПО");
    DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Список счетов Депо", 0, 0, false, rep_date, null, tablewidth );
    stat = depoacnt.moveFirst();
    while( stat )
      PrintLine( depoacnt );
      stat = depoacnt.moveNext();
      UseProgress(i = i+1);
    end;
    RemProgress();
    DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

    DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
    DP_EndProtocol();                 //закончить протоколирование

    if( ToExcel )
      Rep.PrintWinRep();
      Rep.ShowWinRep();
    else
      Rep.PrintRep();
    end;
  else
    if(not DP_ProtocolIsEmpty())
       DP_AddPrintCell(Rep, "Нет счетов, удовлетворяющих параметрам отчета", 50, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR);
       Rep.AddStr();

       DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
       DP_EndProtocol();                 //закончить протоколирование

      if( ToExcel )
        Rep.PrintWinRep();
        Rep.ShowWinRep();
      else
        Rep.PrintRep();
      end;
    else
       DP_EndProtocol();                 //закончить протоколирование
    end;

    MsgBox ("Нет счетов, удовлетворяющих параметрам отчета");
    return 1;
  end;

  return 0;
end;
