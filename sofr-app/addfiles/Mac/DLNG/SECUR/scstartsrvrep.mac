/*                           
$Name:             scstartsrvrep.mac
$Module:           Ценные бумаги
$Description:      Запуск сервисной операции отправки отчета брокера
*/

Import BankInter, XmlRpcInter, "scservop.mac", "dlreport.mac", "dlQuery.mac", "scsrvrepfun.mac", "gtconst.inc", "conveyerfun.mac";

CONST PACK_COUNT_LIMIT = 10000;

CONST RESTART_CONVERT_COUNT = 3;

var flagBreak = 0;/*CHVA 517260*/

/**
 @brief Аналог оператора ?: в си
 @param[in] condition   условие
 @param[in] value_true  вариант, если условие верное
 @param[in] value_false вариант, если условие неверное
 @return value_true или value_false в зависимости от условия
*/                    
PRIVATE MACRO IIF(condition, value_true, value_false)
  if( condition )
     return value_true;
  else
     return value_false;
  end;
END;

 MACRO GetSQLDateTIME( _date ,stime)
    if ( _date!=date(0,0,0) )
        return string( "TO_DATE( '",(substr(string(_date), 1, 10)+" "+stime),"', 'DD.MM.YYYY hh24:mi:ss' )" );
    else
        return "TO_DATE('01-01-0001', 'DD-MM-YYYY')";
    end;
 end;

private macro GetRecCount(servDocId, isTemp)
  var rs = ExecSQLSelect("select count(*) as t_cnt from DSCSRVREPCNTR_"+IIF(IsTemp,"TMP","DBT")+" where T_SERVDOCID = ?",
                          MakeArray(SQLParam("servDocId", servDocId)));
  if (rs.MoveNext())
    return int(rs.value("t_cnt"));
  end;
  return 0;
end;

private macro GetSrvObjCount(ServDocId, Format, existQuery)
  var rs = ExecSQLSelect("select count(*) as t_cnt from DSCSRVREPOBJ_DBT obj where obj.T_SERVDOCID = ? and obj.t_format = ? " + existQuery,
                         MakeArray(SQLParam("servDoc", ServDocId),SQLParam("format", Format)));
  if (rs.MoveNext())
    return int(rs.value("t_cnt"));
  end;
  return 0;
end;

macro SrvRep_msgbox()
  var i = 0, StrErr = "", str = "";

  while(getparm(i,str))
    StrErr = StrErr + string(str);
    str = "";
    i = i + 1;
  end;
  
  SrvRepErrArr[SrvRepErrArr.size] = CSC_SrvRepErr(StrErr);
end;

/*лог ошибок*/
PRIVATE CLASS (DL_LOG_FROM_XML) SC_ERROR_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()
     var ErrorData:CSC_SrvRepErr;
     
     if( m_ReportVersion == 1 )

        if(DL_ReadTagAttribut(m_child_ReportNumber, "CLIENTID", V_INTEGER, ErrorData.ClientID) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CLIENTCODE", V_STRING, ErrorData.ClientCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CLIENTSHORTNAME", V_STRING, ErrorData.ClientShortName) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRSTR", V_STRING, ErrorData.ErrStr) == false)
        end;
        
        return ErrorData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

/*лог ошибок СО*/
CLASS (DL_LOG_DATA_XML) SC_SRVREP_ERROR_LOG_DATA_XML(pDocKind:integer, pDocumentID:integer, pDataParms:TArray, pType:integer, pVersion:integer)

  PRIVATE MACRO СоздатьУзелОшибок(i)
     var УзелОшибок = null;

     if( m_Version == 1 )
        УзелОшибок = CreateElement("Property",
                                   "ClientID",       m_DataParms[i].ClientID,
                                   "ClientCode",     m_DataParms[i].ClientCode, 
                                   "ClientShortName",m_DataParms[i].ClientShortName, 
                                   "ErrStr",         m_DataParms[i].ErrStr
                                  );
     else
        УзелОшибок = CreateElement("Property");
     end;
     
     return УзелОшибок;
  END;


  initDL_LOG_DATA_XML(pDocKind, pDocumentID, pDataParms, pType, pVersion);

END;

PRIVATE MACRO GetLogDataXML( DocKind:integer, DocumentID:integer, Type:integer, OnlyLastReport:bool )
   var xml_obj = null;

   if(Type == LOG_TYPE_ERROR )
      xml_obj = SC_ERROR_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   end;

   return xml_obj;
END;

macro SaveSrvRepErrForReport_XML( RepErrArr, DocumentID:integer, DocKind:integer )

  var RepXML = "";
  
  if( RepErrArr.Size > 0 )
     RepXML = SC_SRVREP_ERROR_LOG_DATA_XML(DocKind,DocumentID,RepErrArr,LOG_TYPE_ERROR).GetDataXML();
  end;

  if( RepXML != "" )
     var Header = DL_LOG_HEADER_XML(DocKind).GetHeaderXML();

     DL_InsertHeaderLog(DocumentID, DocKind, {Oper}, Header);
     DL_InsertLogXMLData( DocumentID, DocKind, LOG_TYPE_ERROR, {Oper}, RepXML);

     RepErrArr.size = 0;
  end;

end;


MACRO GetSrvRepBrkObjectQuery(srvrep)
  var DepSet = TBFile( "dldepset.dbt", "R", 0 );

  var servKindStr = "";

  var cond = "";
  
  servKindStr = "1, 21, 15";
  cond = "1,21,15";

  //Заранее отберем договора со сделками, заключенными в периоде отчета
  var sql, exec;
  sql =  "DECLARE "
       + "BEGIN "
       + "    RSB_BRKREP_RSHB.GetCntrWithConcDeals(?, ?, ?, ?, ?, ?); "
       + "END; ";   
  exec = DL_RSDCommand(sql);

  exec.addParam(srvrep.rec.BegDate);
  exec.addParam(srvrep.rec.EndDate);
  exec.addParam(srvrep.rec.ClientID);
  exec.addParam(IIF(srvrep.rec.ByExchange == StrFor(88), 1, 0));
  exec.addParam(IIF(srvrep.rec.ByOutExchange == StrFor(88), 1, 0));
  exec.addParam(IIF(srvrep.rec.excludeUK == SET_CHAR, 1, 0));
  exec.ExecuteCMD();

  var query =   " select /*+ leading(p) use_nl(sfdl)*/ dlc.t_DlContrID, sfdl.T_NUMBER,  "
            + " NVL ( "
            + "    (SELECT dlobjcode.t_Code "
            + "       FROM ddlobjcode_dbt dlobjcode "
            + "      WHERE dlobjcode.t_ObjectType = 207 "
            + "            AND dlobjcode.t_CodeKind = 1 "
            + "            AND dlobjcode.t_BankDate <= " + GetSQLDate(srvrep.rec.Date)
            + "            AND (dlobjcode.t_BankCloseDate = "
            + "                    TO_DATE ('01 01 0001', 'DD MM YYYY') "
            + "                 OR dlobjcode.t_BankCloseDate >= "+GetSQLDate(srvrep.rec.Date)+" ) "
            + "            AND dlobjcode.t_ObjectID = dlc.t_DlContrID), "
            + "    CHR (1)) "
            + "    AS t_ekk, "
            + " prt.t_shortname as t_clientname, "
            + " prt.t_partyid as t_clientid "
            + "    from ddlcontr_dbt dlc, dsfcontr_dbt sfdl, dparty_dbt prt " 
/*bpv отсекаем договора без движения или без остатков, кроме УК*/
            + ",  " +
            "  ( " +
            "    select distinct t_partyid, t_dlcontrid from  ( " +
            "        select /*+ ordered */ acc.t_id, dlmp.t_dlcontrid, acc.t_partyid, acc.t_code_currency, acc.t_account, acc.t_chapter, acc.t_department  " ;
   if (srvrep.rec.bynotnullrest == StrFor(88)) /*ненулевые остатки*/
     query =  query + 
            "           ,rsb_account.restall(acc.t_account, acc.t_chapter, acc.t_code_currency, "+GetSQLDate(srvrep.rec.BegDate)+" - 1) r1 " +
            "           ,rsb_account.restall(acc.t_account, acc.t_chapter, acc.t_code_currency, "+GetSQLDate(srvrep.rec.EndDate)+") r2 " ;
   end;
   if (srvrep.rec.bymoving == StrFor(88))  /*движение*/
     query =  query + 
            "           ,rsb_account.kreditac(acc.t_Account, acc.t_Chapter, acc.t_Code_Currency,"+GetSQLDate(srvrep.rec.BegDate)+", "+GetSQLDate(srvrep.rec.EndDate)+", null) k " +
            "           ,rsb_account.debetac(acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, "+GetSQLDate(srvrep.rec.BegDate)+", "+GetSQLDate(srvrep.rec.EndDate)+", null) d " ;
   end;
   query =  query +
            "    from  " +
            "     (  " ;
  if((srvrep.rec.ClientID > 0) /*or (srvrep.rec.ClientContrID > 0)*/)
     query =  query +
            " with contrmp as ( select /*+ materialize*/ distinct dlmp.T_SFCONTRID  from ddlcontrmp_dbt dlmp,ddlcontr_dbt dlc ,dsfcontr_dbt sfdl "+
            "            where dlmp.t_dlcontrid = dlc.t_dlcontrid "+
            "             and sfdl.t_ID = dlc.t_SfContrID "+
            "             and sfdl.t_Department = " + string(srvrep.rec.Department)+
            "             and sfdl.t_DateBegin <= " + GetSQLDate(srvrep.rec.EndDate)+
            "             and (sfdl.t_DateClose = " + GetSQLDate(date(0,0,0)) + " or sfdl.t_DateClose >= " + GetSQLDate(srvrep.rec.BegDate)+ ") ";
     if(srvrep.rec.ClientID > 0)
       query = query + " and sfdl.t_PartyID = " + string(srvrep.rec.ClientID);
     end;
     /*if(srvrep.rec.ClientContrID > 0)
       query = query + " and dlc.t_SfContrID = " + string(srvrep.rec.ClientContrID);
     end;*/
     query =  query +" )"+
            " SELECT    " +
            "       sf.t_id, sf.t_partyid, a.* " +
            "  FROM contrmp,dsettacc_dbt s, dsfssi_dbt ss, dsfcontr_dbt sf, daccount_dbt a  " +
            " WHERE  contrmp.T_SFCONTRID = sf.t_id and s.t_settaccid = ss.t_setaccid  " +
            "       AND ss.t_objecttype = 659  " +
            "       AND ss.t_objectid = lpad(sf.t_id,10,'0')  " +
            "       AND a.t_account = s.t_account  " +
            "       AND a.t_chapter = s.t_chapter  " +
            "       and sf.t_servkind in ( " + cond + ")" +          
            " union  " +
            " SELECT  sf.t_id, sf.t_partyid, a.*  " +
            "  FROM contrmp, dmcaccdoc_dbt mc, dsfcontr_dbt sf, daccount_dbt a  " +
            " WHERE  contrmp.T_SFCONTRID = sf.t_id and (  mc.t_chapter in (21,22) or t_catnum = 5087) " +
            "       AND mc.t_iscommon = CHR (88)  " +
            "       AND sf.t_id = mc.t_clientcontrid  " +
            "       AND a.t_account = mc.t_account  " +
            "       AND a.t_chapter = mc.t_chapter  " +
            "       and sf.t_servkind in ( " + cond + ")" ;          
   elif ((srvrep.rec.bymoving == StrFor(88)) and (srvrep.rec.bynotnullrest != StrFor(88))) /*только движение*/
     query =  query +
            " select /*+ leading (r)*/ sf.t_id " +
            "       ,sf.t_partyid " +
            "       ,a.* " +
            " from DRESTDATE_DBT r " +
            "    join daccount_dbt a  on a.t_accountid = r.t_accountid " +
            "    join dsettacc_dbt s on a.t_account = s.t_account and a.t_chapter = s.t_chapter " +
            "    join dsfssi_dbt ss on (s.t_settaccid = ss.t_setaccid and ss.t_objecttype = 659) " +
            "    join dsfcontr_dbt sf on (to_number(ss.t_objectid) = sf.t_id and  sf.t_servkind in (" + cond + ")) " +
            " where r.t_restdate >= " + GetSQLDate(srvrep.rec.BegDate)+ " and " +
            "      r.t_restdate <= "+ GetSQLDateTIME(srvrep.rec.EndDate,"23:59:59") +
            "  union  " +
            " select /*+ leading (r)*/ sf.t_id " +
            "       ,sf.t_partyid " +
            "       ,a.* " +
            " from DRESTDATE_DBT r " +
            "    join daccount_dbt a  on a.t_accountid = r.t_accountid " +
            "    join dmcaccdoc_dbt mc on (a.t_account = mc.t_account and a.t_chapter = mc.t_chapter and mc.t_iscommon = CHR(88)  and  (mc.t_chapter in (21, 22) or mc.t_catid = 818)) " +
            "    join dsfcontr_dbt sf on (mc.t_clientcontrid = sf.t_id and  sf.t_servkind in (" + cond + ")) " +
            " where r.t_restdate >= " + GetSQLDate(srvrep.rec.BegDate)+ " and " +
            "      r.t_restdate <= "+ GetSQLDateTIME(srvrep.rec.EndDate,"23:59:59");
      if (srvrep.rec.excludeUK != SET_CHAR)/* УК РСХБ*/
        query =   query + 
            "  union  " +
            " SELECT  /*+ leading(sf)*/  " +
            "       sf.t_id, sf.t_partyid, a.* " +
            "  FROM dsettacc_dbt s, dsfssi_dbt ss, dsfcontr_dbt sf, daccount_dbt a  " +
            " WHERE  s.t_settaccid = ss.t_setaccid  " +
            "       AND ss.t_objecttype = 659  " +
            "       AND ss.t_objectid = lpad(sf.t_id,'0',10)  " +
            "       AND a.t_account = s.t_account  " +
            "       AND a.t_chapter = s.t_chapter  " +
            "       and sf.t_partyid  = 114800 " + 
            "       and sf.t_servkind in ( " + cond + ")" +          
            " union  " +
            " SELECT /*+ leading(sf) */ sf.t_id, sf.t_partyid, a.*  " +
            "  FROM dmcaccdoc_dbt mc, dsfcontr_dbt sf, daccount_dbt a  " +
            " WHERE  (mc.t_catid = 818 or mc.t_chapter in (21, 22)) " + //t_catnum = 508 
            "       AND mc.t_iscommon = CHR (88)  " +
            "       AND sf.t_id = mc.t_clientcontrid  " +
            "       AND a.t_account = mc.t_account  " +
            "       AND a.t_chapter = mc.t_chapter  " +
            "       and sf.t_partyid  = 114800 " + 
            "       and sf.t_servkind in ( " + cond + ")" ;         
      end ;
   else
     query =  query +
            " SELECT  /*+ use_hash(sf,ss) full(ss)*/  " +
            "       sf.t_id, sf.t_partyid, a.* " +
            "  FROM dsettacc_dbt s, dsfssi_dbt ss, dsfcontr_dbt sf, daccount_dbt a  " +
            " WHERE     s.t_settaccid = ss.t_setaccid  " +
            "       AND ss.t_objecttype = 659  " +
            "       AND to_number(ss.t_objectid) = sf.t_id  " +
            "       AND a.t_account = s.t_account  " +
            "       AND a.t_chapter = s.t_chapter  " +
            "       and sf.t_servkind in ( " + cond + ")" +          
            " union  " +
            " SELECT /*+ use_hash(sf,a,mc) full(mc)*/ sf.t_id, sf.t_partyid, a.*  " +
            "  FROM dmcaccdoc_dbt mc, dsfcontr_dbt sf, daccount_dbt a  " +
            " WHERE   (  mc.t_chapter in (21,22) or t_catnum = 5087) " +
            "       AND mc.t_iscommon = CHR (88)  " +
            "       AND sf.t_id = mc.t_clientcontrid  " +
            "       AND a.t_account = mc.t_account  " +
            "       AND a.t_chapter = mc.t_chapter  " +
            "       and sf.t_servkind in ( " + cond + ")" ;          
   end;
     query =  query +
            "           ) acc " +
            "       , ddlcontrmp_dbt  dlmp where dlmp.T_SFCONTRID = acc.t_id " ;
   if ((srvrep.rec.bynotnullrest == StrFor(88)) or (srvrep.rec.bymoving == StrFor(88)))
     query =  query +
            "       and ((acc.t_close_date = " + GetSQLDate(date(0,0,0)) + " or acc.t_close_date >= " + GetSQLDate(srvrep.rec.BegDate)+ ") or acc.t_partyid = 114800)" ;
   else
     query =  query +
            "       AND (exists(select 1 from drestdate_dbt r where acc.t_accountid = r.t_accountid) or acc.t_partyid = 114800)  ";
   end;
   query =  query + 
            "    group by acc.t_id, dlmp.t_dlcontrid, acc.t_partyid,acc.t_code_currency, acc.t_account, acc.t_chapter, acc.t_department) " +
            "    where   ";
            if ((srvrep.rec.bynotnullrest == StrFor(88)) and (srvrep.rec.bymoving == StrFor(88)))
               query =   query + " (r1 <> 0 or r2 <> 0 or k <> 0 or d <> 0) ";
            elif (srvrep.rec.bynotnullrest == StrFor(88))
               query =   query + " (r1 <> 0 or r2 <> 0) ";
            elif (srvrep.rec.bymoving == StrFor(88))
               query =   query + "  (k <> 0 or d <> 0) ";
            else
               query =   query + "  (1 = 1) ";
            end;           

            if (srvrep.rec.excludeUK == SET_CHAR)/*Исключить УК РСХБ*/
               query =   query + "    and t_partyid != 114800 ";
            end;

            query =   query + 
              "         UNION "
            + "         SELECT t_PartyID, t_DlContrID FROM dscsrvrepcntrconc_tmp " /*Договора со сделками, заключенными в периоде отчета*/
            + "                      ) p " 
            + "       WHERE     sfdl.t_ID = dlc.t_SfContrID " 
            + "             AND p.t_dlcontrid = dlc.t_dlcontrid "
            + "             and prt.t_partyid = sfdl.t_partyid "
            + "             and sfdl.t_Department = " + string(srvrep.rec.Department)
            + "             and sfdl.t_DateBegin <= " + GetSQLDate(srvrep.rec.EndDate)
            + "             and (sfdl.t_DateClose = " + GetSQLDate(date(0,0,0)) + " or sfdl.t_DateClose >= " + GetSQLDate(srvrep.rec.BegDate)+ ") "
            + "    and (select RSI_RSB_GATE.CheckClientIDByRawRecords(sfdl.t_PartyID, 0, "+GetSQLDate(date(0,0,0))+",'"+GT_ASTS+","+GT_VR_ASTS+"') from dual) = CHR(0) ";

  if(srvrep.rec.AddDisp == SET_CHAR)
    //Отобрать все ДБО, для которых есть операция зачисления ДС в эту же дату, но не было отправки в эту дату после времени операции

    query = query + " and Exists(select 1 "
                  + "              from dnptxop_dbt nptx, dsfcontr_dbt sf, ddlcontrmp_dbt mp "
                  + "             where mp.t_DlContrID = dlc.t_DlContrID "
                  + "               and sf.t_ID = mp.t_SfContrID "
                  + "               and nptx.t_DocKind = " + DL_WRTMONEY
                  + "               and nptx.t_Subkind_Operation = " + DL_NPTXOP_WRTKIND_ENROL
                  + "               and nptx.t_Client = sfdl.t_PartyID "
                  + "               and nptx.t_OperDate = " + GetSQLDate(srvrep.rec.Date)
                  + "               and nptx.t_Contract = sf.t_ID "
                  + "               and not Exists(select 1 "
                  + "                                from dscsrvrepobj_dbt repobj, dscsrvrep_dbt srvrep "
                  + "                               where repobj.t_ClientID = nptx.t_Client "
                  + "                                 and srvrep.t_ID = repobj.t_ServDocID "
                  + "                                 and srvrep.t_Date = nptx.t_OperDate "
                  + "                                 and repobj.t_CreateTime > nptx.t_Time "
                  + "                                 and 1 = (case when srvrep.t_ClientContrID = 0 then 1 "
                  + "                                               when srvrep.t_ClientContrID = dlc.t_SfContrID then 1 "
                  + "                                               else 0 end) "
                  + "                             ) ";

    if(servKindStr != "")
      query = query + "             and sf.t_ServKind IN ("+servKindStr+") ";
    end;

    query = query + "           ) ";
  end;

  //CHVA 518366 добавил  отбор клиентов для  автоматически создаваемых отчетов , т.е. где параметр ВСЕ, по фондовому рынку ,  у которых на договоре есть категория формировать нулевой отчет
   if(/*(srvrep.rec.ByStock=="X") and */(srvrep.rec.ClientID==-1))
           query = query + " UNION" 
                    +" SELECT contrmp.t_dlcontrid, sfdl.T_NUMBER, "
                    + " NVL ( "
                    + "    (SELECT dlobjcode.t_Code "
                    + "       FROM ddlobjcode_dbt dlobjcode "
                    + "      WHERE dlobjcode.t_ObjectType = 207 "
                    + "            AND dlobjcode.t_CodeKind = 1 "
                    + "            AND dlobjcode.t_BankDate <= " + GetSQLDate(srvrep.rec.Date)
                    + "            AND (dlobjcode.t_BankCloseDate = "
                    + "                    TO_DATE ('01 01 0001', 'DD MM YYYY') "
                    + "                 OR dlobjcode.t_BankCloseDate >= "+GetSQLDate(srvrep.rec.Date)+" ) "
                    + "            AND dlobjcode.t_ObjectID = contrmp.t_dlcontrid), "
                    + "    CHR (1)) "
                    + "    AS t_ekk, "
                    + " prt.t_shortname as t_clientname, "
                    + " prt.t_partyid as t_clientid "
                    + "     from dsfcontr_dbt sfcontr, ddlcontrmp_dbt contrmp, dparty_dbt prt, ddlcontr_dbt dlc, dsfcontr_dbt sfdl "
                    +"     where  contrmp.t_sfcontrid = sfcontr.t_id"
                    + "      and sfcontr.t_partyid != 114800 " //убираем УК , чтобы не попадала в отбор по обычным клиентам, так как при формировании отчета по УК  клиент один - 114800 и  отбор договоров с такой категорией  происходит в пакете , по всем же клиентам надо ещё отбирать самих клиентов для формирования
                    + "      and prt.t_partyid = sfdl.t_partyid "
                    + "      and contrmp.t_dlcontrid = dlc.t_dlcontrid "
                    + "      and dlc.t_sfcontrid = sfdl.t_id "
                    +"       and sfcontr.t_servkind = 1"
                   +"        and sfcontr.t_servkindsub = 8"
                   +"        and sfcontr.t_datebegin <  TO_DATE ('25.11.2020', 'DD.MM.YYYY')"
                   +"        and (sfcontr.t_dateclose = to_date ('01.01.0001','dd.mm.yyyy') OR sfcontr.t_dateclose > TO_DATE ('25.11.2020', 'DD.MM.YYYY'))"
                   +"        and contrmp.t_dlcontrid IN"
                   +"                                  (select contr.t_dlcontrid from dobjatcor_dbt att, ddlcontr_dbt contr"
                   +"                                       where lpad (contr.t_dlcontrid, 34, '0') = att.t_object"
                   +"                                              and att.t_objecttype = 207"
                   +"                                              and att.t_groupid = 150"
                   +"                                              and att.t_attrid = 1)" ;
    end;
//CHVA 518366
 

  query =   " select q.t_DlContrID as t_ObjectID, ROWNUM as t_Rownum, q.T_NUMBER, q.T_EKK, q.T_CLIENTNAME, q.t_clientid "
          + "   from ("+query+") q "; 

  return query;
END;

macro ПерераспределитьПачки(srvrepid, IsTemp)
  //для временной таблицы в перераспределении нет смысла
  if (isTemp)
    return;
  end;
  var clntCount = GetRecCount(srvrepid, IsTemp);
  var query = "";
  if (clntCount > PACK_COUNT_LIMIT)
    query = 
      " MERGE INTO DSCSRVREPCNTR_"+IIF(IsTemp,"TMP","DBT")+" c "
     +"      USING (SELECT t_id, "
     +"                    NTILE("+PACK_COUNT_LIMIT+") OVER (ORDER BY T_DLCONTRID) AS t_rownumber "
     +"               FROM DSCSRVREPCNTR_"+IIF(IsTemp,"TMP","DBT")
     +"              WHERE t_servdocid = "+String(srvrepid)+") d "
     +"         ON (c.t_id = d.t_id) "
     +" WHEN MATCHED "
     +" THEN "
     +"    UPDATE SET c.T_GROUPNUMBER = d.t_rownumber, C.T_ROWNUM = d.t_rownumber ";
    var bgSql = BackgroundSQLExecuter(query);
    bgSql.Run("Перераспределение пачек");
    bgSql = null;
  else
    query = 
      " MERGE INTO DSCSRVREPCNTR_"+IIF(IsTemp,"TMP","DBT")+" c "
     +"      USING (SELECT t_id, "
     +"                    ROW_NUMBER () OVER (ORDER BY T_DLCONTRID) AS t_rownumber "
     +"               FROM DSCSRVREPCNTR_"+IIF(IsTemp,"TMP","DBT")
     +"              WHERE t_servdocid = "+String(srvrepid)+") d "
     +"         ON (c.t_id = d.t_id) "
     +" WHEN MATCHED "
     +" THEN "
     +"    UPDATE SET c.T_GROUPNUMBER = d.t_rownumber, C.T_ROWNUM = d.t_rownumber ";
      BegAction(2000, "Перераспределение пачек", false);
      execSQL(query);
      EndAction();
    end;
end;

macro ПодготовкаДанныхОтчетаБрокераБазовое(srvrep, IsTemp, Force)
  var query = "";

  var oldDialog = SetDialogFlag(1);

  //если не было ручного редактирования, перестроим список на всякий случай
  if (Force or (srvrep.rec.WasManClntEdit != SET_CHAR))
    BegAction(2000, "Удаление старых данных", false);
    execSQL( "delete from DSCSRVREPCNTR_"+IIF(IsTemp,"TMP","DBT")+" where t_servdocid = " + String(srvrep.rec.Id));
    EndAction();
  end;

  if ((GetRecCount(srvrep.rec.Id, IsTemp) == 0) or (srvrep.rec.WasManClntEdit != SET_CHAR))
    //заполняем список клиентов по отчёту, только если в списке нет записей, или не было ручного редактирования
    query = " INSERT INTO DSCSRVREPCNTR_"+IIF(IsTemp,"TMP","DBT")+" (T_SERVDOCID, T_DLCONTRID, T_GROUPNUMBER, T_ROWNUM, T_DLCONTRNUMBER, T_CLIENTNAME, T_EKK, T_CLIENTCODE) "
          + " SELECT "+String(srvrep.rec.Id)+", q.t_ObjectID, q.t_Rownum, q.t_Rownum, q.t_number, q.T_CLIENTNAME, q.t_ekk,  "
          "          (select t_code from dobjcode_dbt where t_objecttype = 3 and t_codekind = 1 and t_state = 0 and t_objectid = q.t_clientid) as T_CLIENTCODE "
          + "   FROM (" + GetSrvRepBrkObjectQuery(srvrep) + ") q ";
    if (not IsTemp)
      var bgSql = BackgroundSQLExecuter(query);
      bgSql.Run("Подготовка данных");
      bgSql = null;
    else
      BegAction(2000, "Подготовка данных", false);
      execSQL(query);
      EndAction();
    end;
  end;

  ПерераспределитьПачки(srvrep.rec.Id, IsTemp);

  SetDialogFlag(oldDialog);
end;

macro ПодготовкаДанныхОтчетаБрокера(srvrep)
  ПодготовкаДанныхОтчетаБрокераБазовое(srvrep, false, false);
end;

macro ПодготовкаДанныхОтчетаБрокераВрем(srvrep)
  ПодготовкаДанныхОтчетаБрокераБазовое(srvrep, true, true);
end;


MACRO ОтобратьОбъектыДляОтчБрокера(_taskID, _execID, _conveyerID, _packetSize, _Params)
  var srvrep = TBFile("scsrvrep.dbt");
  var cnt = 0;
  var cmd, query, req, DataSet, bgSql;
  
  if(_Params.ID > 0)
    
    srvrep.rec.ID = _Params.ID;
    if(srvrep.GetEQ())

      query = " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
            + " SELECT ?,?,?,q.t_GroupNumber, q.t_ObjectType, q.t_ObjectID "
            + "   FROM (select t_GroupNumber, t_DlContrID as t_ObjectID, 0 as t_ObjectType from DSCSRVREPCNTR_DBT where t_servdocid = "+String(srvrep.rec.ID)+" ) q ";
      query = query + " ORDER BY q.t_ObjectID ";

      cmd = RSDCommand(query);
      cmd.AddParam("", RSDBP_IN, _taskID);
      cmd.AddParam("", RSDBP_IN, _execID);
      cmd.AddParam("", RSDBP_IN, _conveyerID);

      SQL_Execute(cmd, "Отбор объектов", false );

      if(_packetSize == 0)
        cnt = 1;
      else
        query =   " select NVL(max(t_PackID), 0) as cnt "
                + "   from dcnvdoc_dbt "
                + "  where t_ExecID = " + _execID;
        
        DataSet = TRsbDataSet(query);
        if(DataSet.moveNext())
          cnt = int(round(int(DataSet.cnt), 0));
        end;
      end;
    end;
  end;

  return cnt;
END;

PRIVATE MACRO GetSendSrvRepBrkObjectQuery()
  return  " select 0 as t_ObjectType, tmpobj.t_ID as t_ObjectID, ROWNUM as t_GroupNumber, tmpobj.t_ID as t_RepObjID, tmpobj.t_clientid as t_clientid "
          + "   from dscsrvrepobj_tmp tmpobj" 
          + "  where (   tmpobj.t_Format = " + SC_SRVREPFORMAT_PDF
          + "         or (    tmpobj.t_Format <> " + SC_SRVREPFORMAT_PDF
          + "             and not Exists(select 1 "
          + "                              from dscsrvrepobj_dbt obj "
          + "                             where obj.t_ServDocID = tmpobj.t_ServDocID "
          + "                               and obj.t_ClientID = tmpobj.t_ClientID "
          + "                               and obj.t_Format = " + SC_SRVREPFORMAT_PDF
          + "                           ) "
          + "            ) "
          + "        ) ";
END;

MACRO ОтобратьОбъектыДляОтправОтчБрк(_taskID, _execID, _conveyerID, _packetSize, _Params)
  var srvrep = TBFile("scsrvrep.dbt");
  var cnt = 0;
  var query, DataSet;
  
  if(_Params.ID > 0)
    
    srvrep.rec.ID = _Params.ID;
    if(srvrep.GetEQ())

      query = " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
            + " SELECT "+_taskID+","+_execID+","+_conveyerID+",q.t_GroupNumber, q.t_ObjectType, q.t_ObjectID "
            + "   FROM (" + GetSendSrvRepBrkObjectQuery(srvrep) + ") q ";
      query = query + " ORDER BY q.t_ObjectID ";

      SQL_Execute(query, "Отбор объектов", false );
      
      if(_packetSize == 0)
        cnt = 1;
      else
        query =   " select NVL(max(t_PackID), 0) as cnt "
                + "   from dcnvdoc_dbt "
                + "  where t_ExecID = " + _execID;
        
        DataSet = TRsbDataSet(query);
        if(DataSet.moveNext())
          cnt = int(round(int(DataSet.cnt), 0));
        end;
      end;
    end;
  end;

  return cnt;
END;

MACRO ОтобратьОбъектыДляКонвертОтчБр(_taskID, _execID, _conveyerID, _packetSize, _Params)
  var srvrep = TBFile("scsrvrep.dbt");
  var cnt = 0;
  var objCnt = 0;
  var query, DataSet, existQuery = "";

  private macro GetRepObjExistSql(syn, format)
    return 
      " exists (select 1 "
     +"  from dscsrvrepobj_dbt repobj "
     +" where repobj.t_ServDocID = "+syn+".T_SERVDOCID "
     +"   and repobj.t_ClientID = "+syn+".t_ClientID "
     +"   and repobj.t_ContractID = "+syn+".t_ContractID "
     +"   and repobj.t_Format = " + format + ")";
  end;
  
  if(_Params.ID > 0)
    
    srvrep.rec.ID = _Params.ID;
    if(srvrep.GetEQ())

      if (srvrep.rec.PdfFormat == SET_CHAR)
        existQuery = existQuery + " not " + GetRepObjExistSql("obj", SC_SRVREPFORMAT_PDF);
      end;

      if (srvrep.rec.ExcelFormat == SET_CHAR)
        if (existQuery != "")
          existQuery = existQuery + " or ";
        end;
        existQuery = existQuery + " not " + GetRepObjExistSql("obj", SC_SRVREPFORMAT_EXCEL_XLSX);
      end;

      if (existQuery != "")
        existQuery = " and (" + existQuery + ")";
      end;

      objCnt = GetSrvObjCount(_Params.ID, 3 /*SC_SRVREPFORMAT_EXCEL_XML*/, existQuery);

      query = " INSERT INTO DCNVDOC_DBT(T_TASKID,T_EXECID,T_CONVTYPEID,T_PACKID,T_OBJECTTYPE,T_OBJECTID) "
            + " SELECT "+_taskID+","+_execID+","+_conveyerID+",q2.t_groupnum, 0, srvobj.t_ObjectID "
            + "   FROM (SELECT CASE WHEN q1.t_LEVEL <= 1 THEN 1 ELSE q1.t_low + 1 END "
            + "                   AS t_low, "
            + "                CASE "
            + "                   WHEN q1.t_LEVEL >= LEAST ("+PACK_COUNT_LIMIT+", "+objCnt+") THEN "+objCnt+" "
            + "                   ELSE q1.t_high "
            + "                END "
            + "                   AS t_high, "
            + "                q1.t_level AS t_groupnum "
            + "           FROM (    SELECT LEVEL AS t_LEVEL, "
            + "                            TRUNC (GREATEST ( ("+objCnt+" / "+PACK_COUNT_LIMIT+"), 1) * (LEVEL - 1)) "
            + "                               AS t_low, "
            + "                            TRUNC (GREATEST ( ("+objCnt+" / "+PACK_COUNT_LIMIT+"), 1) * (LEVEL)) "
            + "                               AS t_high "
            + "                       FROM DUAL "
            + "                 CONNECT BY LEVEL <= LEAST ("+PACK_COUNT_LIMIT+", "+objCnt+")) q1) q2, "
            + "        (SELECT obj.T_ID AS t_ObjectID, ROWNUM AS t_GroupNumber "
            + "           FROM DSCSRVREPOBJ_DBT obj "
            + "          WHERE OBJ.T_SERVDOCID = "+_Params.ID+" AND t_format = 3 /*SC_SRVREPFORMAT_EXCEL_XML*/ "+existQuery+" ) srvobj "
            + "  WHERE srvobj.t_GroupNumber BETWEEN q2.t_low AND q2.t_high ";

      SQL_Execute(query, "Отбор объектов", false );
      
      if(_packetSize == 0)
        cnt = 1;
      else
        query =   " select NVL(max(t_PackID), 0) as cnt "
                + "   from dcnvdoc_dbt "
                + "  where t_ExecID = " + _execID;
        
        DataSet = TRsbDataSet(query);
        if(DataSet.moveNext())
          cnt = int(round(int(DataSet.cnt), 0));
        end;
      end;
    end;
  end;

  return cnt;
END;

//Формирование отчетов 
PRIVATE MACRO StartCreateRepBrk(srvrep)/*CHVA 517110*/
  var OperID = 13; //Операция для конвейера - Пакетная операция формирования отчета брокера в БОЦБ
  var ConvID = 17; //Вид конвейера Формирование отчета брокера БО ЦБ
  var UseConveyer = SecurUseConveyer(ConvID, OperID); //UseConveyer = False;

  var err = 0;
  var signer = 0;
  var errmsg = "";

  var rslObj = SrvRepCnvData(srvrep.rec.ID, null, true);

  if(UseConveyer)
    if (GetDefaultSigner(@signer,@errmsg) != 0)
       MsgBox(errmsg);
       return 1;
    end;

    rslObj.Signer = signer;

    var cnv = RsbConveyerExec(ConvID);

    cnv.AddParamsForConveyer(ConvID, rslObj, 0);
    cnv.AddParamsForOperation(ConvID, OperID, rslObj, "", 0);
    cnv.showPanelMonitoring(1);
    ПодготовкаДанныхОтчетаБрокера(srvrep);
    cnv.Run();
    RecalcConvTaskPriority(ConvID);
    
    if( ErrorFromCnvpack( cnv ) > 0 )
      err = 1;
    end;
  else
    err = ExecMacroFile("sc_srvrepbrk.mac", "ExecuteSrvRepBrk", 0, 0, 0, 0, 0, rslObj);
  end;

  return err;
END;

//Подписание отчетов
PRIVATE MACRO StartSignRepBrk(srvrep)
  var err = 0;
  var exec = NULL;
  
  SQL_Execute("DELETE FROM DSCSRVREPOBJ_TMP", "", false );

  exec = RSDCommand(  " INSERT INTO DSCSRVREPOBJ_TMP "
                    + " SELECT * "
                    + "   FROM DSCSRVREPOBJ_DBT "
                    + " WHERE T_SERVDOCID = ? "
                    + "   AND T_ISSIGN = CHR(0) "
                    + "   AND T_ISSEND = CHR(0) "
                    + "   AND T_FORMAT = " + SC_SRVREPFORMAT_PDF);
  exec.addParam( "", RSDBP_IN, srvrep.rec.ID );
  exec.execute();

  err = SC_SignAllRepObjTmp();

  SQL_Execute("DELETE FROM DSCSRVREPOBJ_TMP", "", false );

  return err;

OnError(er)
  msgbox(er.module+ " "+er.line+" "+er.message);
  return 1;
END;

//Отправка отчетов
PRIVATE MACRO StartSendRepBrk(srvrep)
  var err = 0;
  var exec = NULL;
  
  SQL_Execute("DELETE FROM DSCSRVREPOBJ_TMP", "", false );

  exec = RSDCommand(  " INSERT INTO DSCSRVREPOBJ_TMP "
                    + " SELECT * "
                    + "   FROM DSCSRVREPOBJ_DBT "
                    + " WHERE T_SERVDOCID = ? "
                    + "   AND T_ISSEND = CHR(0) "
                    + "   AND T_FORMAT = ? ");
  exec.addParam( "", RSDBP_IN, srvrep.rec.ID );
  exec.addParam( "", RSDBP_IN, IIF((srvrep.rec.PdfFormat == SET_CHAR), SC_SRVREPFORMAT_PDF, SC_SRVREPFORMAT_EXCEL_XLSX) );
  exec.execute();

  err = SendAllRepObjTmp(srvrep);

  SQL_Execute("DELETE FROM DSCSRVREPOBJ_TMP", "", false );

  return err;

OnError(er)
  msgbox(er.module+ " "+er.line+" "+er.message);
  return 1;
END;

//Выполнение сервисной операции
MACRO StartSrvRep(ID:integer)
  var srv = CSrvRepExec();
  var err = 0, stat = 0;
  var clientCount = 0;
  err = srv.Load(ID);
  if(not err)

    SrvRepErrArr.size = 0; //массив ошибок
    ReplaceMacro( "msgbox", "SrvRep_msgbox" );

    if((srv.srvrep.rec.DocKind == SP_SRVBROKERREP) or (srv.srvrep.rec.DocKind == SP_SRVBROKERREPNEW))
      
      //1. Сформировать отчет
      srv.StartAction(SCSRVREP_ACTION_CREATE);
      err = srv.SetStatus(SC_SRVREPSTATUS_EXEC);
      if(not err)
        err = StartCreateRepBrk(srv.srvrep);
      end;
      stat = srv.SetStatus(IIF(err == 0, SC_SRVREPSTATUS_DONE, SC_SRVREPSTATUS_ERROR));
      if((not err) and stat)
        err = stat;
      end;
      clientCount = GetRecCount(ID, false);

      if (srv.srvrep.rec.AutoSendKind == SCSRVREP_AUTOSENDKIND_TOGETHER)
        if (srv.srvrep.rec.AutoSign == SET_CHAR)
          srv.StartAction(SCSRVREP_ACTION_SIGN);
          srv.SetStatus(IIF(((err == 0) and (clientCount > 0)), SC_SRVREPSTATUS_DONE, SC_SRVREPSTATUS_ERROR));
        end;
        srv.StartAction(SCSRVREP_ACTION_SEND);
        srv.SetStatus(IIF(((err == 0) and (clientCount > 0)), SC_SRVREPSTATUS_DONE, SC_SRVREPSTATUS_ERROR));
      end;
       
      if((not err) and (srv.srvrep.rec.AutoSign == SET_CHAR) and (srv.srvrep.rec.AutoSendKind == SCSRVREP_AUTOSENDKIND_SEPARATED))
        //2. Подписание отчетов
        srv.StartAction(SCSRVREP_ACTION_SIGN);
        err = srv.SetStatus(SC_SRVREPSTATUS_EXEC);
        if(not err)
          err = StartSignRepBrk(srv.srvrep);
        end;
        stat = srv.SetStatus(IIF(err == 0, SC_SRVREPSTATUS_DONE, SC_SRVREPSTATUS_ERROR));
        if((not err) and stat)
          err = stat;
        end;
      end;

      if((not err) and (srv.srvrep.rec.AutoSendKind == SCSRVREP_AUTOSENDKIND_SEPARATED))
        //3. Отправка отчетов
        srv.StartAction(SCSRVREP_ACTION_SEND);
        err = srv.SetStatus(SC_SRVREPSTATUS_EXEC);
        if(not err)
          err = StartSendRepBrk(srv.srvrep);
        end;
        stat = srv.SetStatus(IIF(  ((err == 0) and (clientCount > 0)), SC_SRVREPSTATUS_DONE, SC_SRVREPSTATUS_ERROR));/*CHVA 517110*/
        if((not err) and stat)
          err = stat;
        end;
      end;

    end;

    if (clientCount == 0) msgbox("Нет клиентов, удовлетворяющих условиям отбора");end;

    ReplaceMacro( "msgbox", NULL );

    SaveSrvRepErrForReport_XML(SrvRepErrArr, srv.srvrep.rec.ID, srv.srvrep.rec.DocKind);
  end;

  return err;
END;


//Удалить файл отчета при откате/удалении сервисной операции
macro Удалить_Отчеты_Физически()
  var query, cmd, DataSet;
  var FullPath = ""; //Путь до файла на СП
  
  query = "select * from dscsrvrepobj_tmp";
  cmd = DL_RSDCommand(query);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    var managStatus = BROKER_REPORT_FILE_STATUS_SAVED;
    if (DataSet.IsSend == SET_CHAR)
      managStatus = BROKER_REPORT_FILE_STATUS_SENT;
    end;
    if (DataSet.IsSign == SET_CHAR)
      managStatus = BROKER_REPORT_FILE_STATUS_SIGNED;
    end;
    BrokerRepFileStatusManager(DataSet.FileName, false, managStatus).Remove_CURRENT_file();
  end;

  return 0;
end;

//////////////////////////////////////////////////////////////////////////////////////
//ВЫПОЛНЕНИЕ ОТДЕЛЬНЫХ ДЕЙСТВИЙ ИЗ СПИСКА ОТЧЕТОВ ПО СО
//////////////////////////////////////////////////////////////////////////////////////

//Открыть файл отчета для просмотра
macro Открыть_Файл_Отчетa(RepObjID:integer)
  var query, cmd, DataSet;
  var FullPath = ""; //Путь до файла на СП

  query = "select * from dscsrvrepobj_dbt where t_ID = ?";
  cmd = DL_RSDCommand(query);

  cmd.AddParam(RepObjID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    var managStatus = BROKER_REPORT_FILE_STATUS_SAVED;
    if (DataSet.IsSend == SET_CHAR)
      managStatus = BROKER_REPORT_FILE_STATUS_SENT;
    end;
    if (DataSet.IsSign == SET_CHAR)
      managStatus = BROKER_REPORT_FILE_STATUS_SIGNED;
    end;

    BrokerRepFileStatusManager(DataSet.FileName, false, managStatus).ShowFile();
  end;

  return 0;
end;

//Выполнить конвертацию в PDF отчетов (записи для конвертации в dscsrvrepobj_tmp)
private macro _Выполнить_Конвертацию_В_PDF(srvrep)
  var query, cmd, DataSet;
  var cnt = 0;
  var FName = "";
  var wasErr = false;
  var srv = NULL;
  var err = 0, stat = 0;
  var NewStatus;
  
  query = "select * from dscsrvrepobj_tmp";
  cmd = DL_RSDCommand(query);

  cnt = cmd.GetCount();
  if(cnt == 0)
    msgbox("Нет подходящих для конвертации в PDF отчетов");
  else
    srv = CSrvRepExec();
    err = srv.Load(srvrep.rec.ID);
    if(not err)
      srv.StartAction(SCSRVREP_ACTION_CREATE);

      err = srv.SetStatus(SC_SRVREPSTATUS_EXEC);

      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        err = 0;

        var managStatus = BROKER_REPORT_FILE_STATUS_SAVED;
        if (DataSet.IsSend == SET_CHAR)
          managStatus = BROKER_REPORT_FILE_STATUS_SENT;
        end;
        if (DataSet.IsSign == SET_CHAR)
          managStatus = BROKER_REPORT_FILE_STATUS_SIGNED;
        end;

        var brokFileManager = BrokerRepFileStatusManager(DataSet.FileName, false, managStatus);

        var resFileManager = BrokerRepFileStatusManager(brokFileManager.Get_CURRENT_filename_with_no_ext()+".pdf", false);

        err = SC_ConvertToFormat(brokFileManager.Get_CURRENT_path(), resFileManager.Get_GENERATED_REPORTS_path(), SC_SRVREPFORMAT_PDF);
        if(not err)
          if(SC_SrvObjExistsFormat(DataSet.ServDocID, DataSet.ClientID, SC_SRVREPFORMAT_PDF))
              var exec = RSDCommand(  " UPDATE DSCSRVREPOBJ_DBT "
                                    + "    SET T_FILENAME = ?, "
                                    + "        T_ISSIGN = ?, "
                                    + "        T_SIGNDATE = ?, "
                                    + "        T_SIGNTIME = ? "
                                    + "  WHERE T_SERVDOCID = ? "
                                    + "    AND T_CLIENTID = ? "
                                    + "    AND T_FORMAT = ? ");
              
              exec.addParam( "", RSDBP_IN, FName );
              exec.addParam( "", RSDBP_IN, UNSET_CHAR );
              exec.addParam( "", RSDBP_IN, date(0,0,0) );
              exec.addParam( "", RSDBP_IN, time(0) );
              
              exec.addParam( "", RSDBP_IN, DataSet.ServDocID );
              exec.addParam( "", RSDBP_IN, DataSet.ClientID );
              exec.addParam( "", RSDBP_IN, SC_SRVREPFORMAT_PDF );
              exec.execute();

          else
            var srvrepobjCache = RsbSQLInsert("scsrvrepobj.dbt");
            var srvrepobj = TRecHandler("scsrvrepobj.dbt");

            srvrepobj.Clear();

            srvrepobj.rec.ServDocID  = DataSet.ServDocID; 
            srvrepobj.rec.ClientID   = DataSet.ClientID;
            srvrepobj.rec.CreateDate = date();
            srvrepobj.rec.CreateTime = time();
            srvrepobj.rec.FileName   = FName;
            srvrepobj.rec.Format     = SC_SRVREPFORMAT_PDF;
            srvrepobj.rec.email      = SC_GetClientRepEmail(DataSet.ClientID, FName);

            srvrepobjCache.AddRecord(srvrepobj);
            srvrepobjCache.Insert();

          end;

          resFileManager.Move_File(BROKER_REPORT_FILE_STATUS_SAVED);
        else
          wasErr = true;
        end;
      end;
    end;

    if((not err) and wasErr)
      err = 1;
    end;

    if(err)
      NewStatus = SC_SRVREPSTATUS_ERROR;
    else
      NewStatus = SC_SRVREPSTATUS_DONE;
    end;

    stat = srv.SetStatus(NewStatus);
    if((not err) and stat)
      err = stat;
    end;
  end;

  return err;

OnError(er)
  if(ValType(srv) != V_UNDEF)
    srv.SetStatus(SC_SRVREPSTATUS_ERROR);
  end;

  msgbox(er.module+ " "+er.line+" "+er.message);
  return 1;
end;

macro Выполнить_Конвертацию_В_PDF(_srvrep)
  record rec_srvrep("scsrvrep");
  var srvrep = TRecHandler("scsrvrep");
  var err = 0;

  SetBuff(rec_srvrep, _srvrep);
  copy(srvrep, rec_srvrep);

  SrvRepErrArr.size = 0; //массив ошибок
  ReplaceMacro( "msgbox", "SrvRep_msgbox" );

  err = _Выполнить_Конвертацию_В_PDF(srvrep);

  ReplaceMacro( "msgbox", NULL );
  SaveSrvRepErrForReport_XML(SrvRepErrArr, srvrep.rec.ID, srvrep.rec.DocKind);

  if(err)
    msgbox("Действие выполнено с ошибками");
  end;

  return err;
end;

//Выполнить подписание выделенных отчетов (записи  в dscsrvrepobj_tmp)
private macro _Выполнить_Подписание(srvrep)
  var srv = CSrvRepExec();
  var err = 0, stat = 0;
  var NewStatus;
  
  err = srv.Load(srvrep.rec.ID);
  if(not err)
    srv.StartAction(SCSRVREP_ACTION_SIGN);

    err = srv.SetStatus(SC_SRVREPSTATUS_EXEC);
    if(not err)

      err = SC_SignAllRepObjTmp();

      if(err)
        NewStatus = SC_SRVREPSTATUS_ERROR;
      else
        NewStatus = SC_SRVREPSTATUS_DONE;

        //проверить наличие не подписанных отчетов, которые подлежат подписанию
        var cmd, query;

        query =   " select 1 "
                + "   from dscsrvrepobj_dbt obj"
                + "  where obj.t_ServDocID = ? "
                + "    and obj.t_Format = " + SC_SRVREPFORMAT_PDF 
                + "    and obj.t_IsSign = CHR(0) ";

        cmd = DL_RSDCommand(query);

        cmd.AddParam(srvrep.rec.ID);
        if(cmd.GetCount() > 0)
          NewStatus = SC_SRVREPSTATUS_PARTLY;
        end; 
      end;

      stat = srv.SetStatus(NewStatus);
      if((not err) and stat)
        err = stat;
      end;
    end;
  end;

  return err;

OnError(er)
  if(ValType(srv) != V_UNDEF)
    srv.SetStatus(SC_SRVREPSTATUS_ERROR);
  end;

  msgbox(er.module+ " "+er.line+" "+er.message);
  return 1;
end;

macro Выполнить_Подписание(_srvrep)
  record rec_srvrep("scsrvrep");
  var srvrep = TRecHandler("scsrvrep");
  var err = 0;

  SetBuff(rec_srvrep, _srvrep);
  copy(srvrep, rec_srvrep);

  SrvRepErrArr.size = 0; //массив ошибок
  ReplaceMacro( "msgbox", "SrvRep_msgbox" );

  err = _Выполнить_Подписание(srvrep);

  ReplaceMacro( "msgbox", NULL );
  SaveSrvRepErrForReport_XML(SrvRepErrArr, srvrep.rec.ID, srvrep.rec.DocKind);

  if(err)
    msgbox("Действие выполнено с ошибками");
  end;

  return err;
end;

//Выполнить отправку выделенных отчетов (записи  в dscsrvrepobj_tmp)
private macro _Выполнить_Отправку(srvrep)
  var srv = CSrvRepExec();
  var err = 0, stat = 0;
  var NewStatus;
  
  err = srv.Load(srvrep.rec.ID);
  if(not err)
    srv.StartAction(SCSRVREP_ACTION_SEND);

    err = srv.SetStatus(SC_SRVREPSTATUS_EXEC);
    if(not err)
      err = SendAllRepObjTmp(srvrep);

      if(err)
        NewStatus = SC_SRVREPSTATUS_ERROR;
      else
        NewStatus = SC_SRVREPSTATUS_DONE;

        //проверить наличие не отправленных отчетов, которые подлежат отправке
        var cmd, query;

        query =   " select 1 "
                + "   from dscsrvrepobj_dbt obj"
                + "  where obj.t_ServDocID = ? "
                + "    and (   (obj.t_Format = " + SC_SRVREPFORMAT_PDF + " and obj.t_IsSend = CHR(0)) "
                + "         or (    obj.t_Format <> " + SC_SRVREPFORMAT_PDF 
                + "             and obj.t_IsSend = CHR(0) "
                + "             and Not Exists(select 1 "
                + "                              from dscsrvrepobj_dbt obj1 "
                + "                             where obj1.t_ServDocID = obj.t_ServDocID "
                + "                               and obj1.t_ClientID = obj.t_ClientID "
                + "                               and obj1.t_Format = " + SC_SRVREPFORMAT_PDF
                + "                           )"
                + "            ) "
                + "        )";

        cmd = DL_RSDCommand(query);

        cmd.AddParam(srvrep.rec.ID);
        if(cmd.GetCount() > 0)
          NewStatus = SC_SRVREPSTATUS_PARTLY;
        end; 
      end;

      stat = srv.SetStatus(NewStatus);
      if((not err) and stat)
        err = stat;
      end;
    end;
  end;

  return err;

OnError(er)
  if(ValType(srv) != V_UNDEF)
    srv.SetStatus(SC_SRVREPSTATUS_ERROR);
  end;

  msgbox(er.module+ " "+er.line+" "+er.message);
  return 1;
end;


macro Выполнить_Отправку(_srvrep)
  record rec_srvrep("scsrvrep");
  var srvrep = TRecHandler("scsrvrep");
  var err = 0;

  SetBuff(rec_srvrep, _srvrep);
  copy(srvrep, rec_srvrep);

  SrvRepErrArr.size = 0; //массив ошибок
  ReplaceMacro( "msgbox", "SrvRep_msgbox" );

  err = _Выполнить_Отправку(srvrep);

  ReplaceMacro( "msgbox", NULL );
  SaveSrvRepErrForReport_XML(SrvRepErrArr, srvrep.rec.ID, srvrep.rec.DocKind);

  if(err)
    msgbox("Действие выполнено с ошибками");
  end;

  return err;
end;

//////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////


//Напечатать протокол
macro Печатать_Протокол(_srvrep)
  VAR ReportFileName, FName = "srvrepprotocol", errtext;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/
  var stat = 0;
  var ErrData;
  var ReportLineData_XML;
  record rec_srvrep("scsrvrep");
  var srvrep = TRecHandler("scsrvrep");
  
  SetBuff(rec_srvrep, _srvrep);

  copy(srvrep, rec_srvrep);
  
  ReportLineData_XML = GetLogDataXML(srvrep.rec.DocKind, srvrep.rec.ID, LOG_TYPE_ERROR, false);
  ReportLineData_XML.SetReportNumber(0);
  
  ReportFileName = GetTxtFileName( FName );
  if( Open( ReportOutFile, ReportFileName ) == false )
     MsgBox( "Ошибка при открытии файла отчета|"+ ReportFileName +"|" + errtext);
     break;
  end;
  SetOutput( ReportFileName );

  stat = ReportLineData_XML.Next();
  if(stat)

    println("┌──────────────┬──────────────────────────────────────────┬─────────────────────────────────────────────────────────────────────┐");
    println("│ Код клиента  │ Наименование клиента                     │ Сообщение                                                           │");
    println("│              │                                          │                                                                     │");
    println("├──────────────┼──────────────────────────────────────────┼─────────────────────────────────────────────────────────────────────┤");

    while(stat)
      ErrData = ReportLineData_XML.GetReportLineData();
      
            [│##############│##########################################│#####################################################################│]
            (ErrData.ClientCode, ErrData.ClientShortName, ErrData.ErrStr:w);
      

      stat = ReportLineData_XML.Next();
      if(stat)
        println("├──────────────┼──────────────────────────────────────────┼─────────────────────────────────────────────────────────────────────┤");
      else
        println("└──────────────┴──────────────────────────────────────────┴─────────────────────────────────────────────────────────────────────┘");
      end;
    end;
  else
    println("Сообщений об ошибках нет");
  end;

  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );
end;
