/*
$Name:         SPQICheckOper.mac 
$Module:       Ценные бумаги
$Description:  Квалифицированные инвесторы
*/
import "dlqinvfun.mac", "secinter.mac", "sp_class.mac";

PRIVATE VAR DealErrorsMsg    = TArray( false, 2000, 2000 );
PRIVATE VAR DealErrorsOperID = TArray( false, 2000, 2000 );

PRIVATE MACRO СоздатьОбщееСообщение(TypeError:integer, PartyID:integer, ClientID:integer, AvoirKind:integer):string

   var ErrMsg:string = "";
   var PartyName:string = "", ClientName:string = "", AvrKindName:string = "";
   record Party("party");

   if( ПолучитьСубъекта(PartyID, Party) == 0 )
      PartyName = Party.Name;
   end;
   if( ПолучитьСубъекта(ClientID, Party) == 0 )
      ClientName = Party.Name;
   end;

   if(TypeError == DL_QINVTYPE_ERROR_1)
      ErrMsg = PartyName + " и " + ClientName + " не признаны квалифицированными инвесторами.";
   elif(TypeError == DL_QINVTYPE_ERROR_2)
      AvoirKindName( AvoirKind, NULL, AvrKindName );
      ErrMsg = PartyName + " и " + ClientName + " не признаны квалифицированными инвесторами в отношении вида ц\б " + AvrKindName + ".";
   end;

   return ErrMsg;
END;

PRIVATE MACRO СоздатьСообщение(TypeError:integer, PartyID:integer, AvoirKind:integer):string

   var ErrMsg:string = "";
   var PartyName:string = "", AvrKindName:string = "";
   record Party("party");

   if( ПолучитьСубъекта(PartyID, Party) == 0 )
      PartyName = Party.Name;
   end;

   if(TypeError == DL_QINVTYPE_ERROR_1)
      ErrMsg = PartyName + " не признан квалифицированным инвестором.";
   elif(TypeError == DL_QINVTYPE_ERROR_2)
      AvoirKindName( AvoirKind, NULL, AvrKindName );
      ErrMsg = PartyName + " не признан квалифицированным инвестором в отношении вида ц\б " + AvrKindName + ".";
   end;

   return ErrMsg;
END;

// проверить сделку
PRIVATE MACRO CheckDeal( FD:SPFirstDoc, FD_back:SPFirstDoc, IsMassExec:BOOL ):BOOL

   var RetVal:bool = true;

   var PartyID:integer = -1, ClientID:integer = -1;
   var TypeErrorParty = DL_QINVTYPE_ERROR_0, TypeErrorClient = DL_QINVTYPE_ERROR_0;
   var ErrMsg:string = "";
   var QIOperFormData:DL_QIOperData;
   var PartyAvrKind = FD.fininstr.rec.AvoirKind, ClientAvrKind = FD.fininstr.rec.AvoirKind;
   var pt = TRecHandler ("party.dbt");

   if( FD.tick.rec.PartyID != -1 )
      PartyID = FD.tick.rec.PartyID;
   elif( IsEXCHANGE(GetOpGroup(FD.tick.rec)) )
      PartyID = FD.tick.rec.MarketID;
   elif( IsBROKER(GetOpGroup(FD.tick.rec)) )
      PartyID = FD.tick.rec.BrokerID;
   end;

   if( FD.tick.rec.ClientID > 0 )
      ClientID = FD.tick.rec.ClientID;
   end;

   if(PartyID > 0)
      pt.Clear();
      pt.rec.PartyID = PartyID;
      if(DL_GetMainObjAttr( pt, PARTY_ATTR_GROUP_FOREIGNENTITY, OBJTYPE_PARTY ) != 2) //2=Да
         TypeErrorParty = DL_CheckQIParty(PartyID, PartyAvrKind, FD.tick.rec.DealDate);
         if( (TypeErrorParty == DL_QINVTYPE_ERROR_0) AND (FD_back != null) )
            PartyAvrKind = FD_back.fininstr.rec.AvoirKind;
            TypeErrorParty = DL_CheckQIParty(PartyID, PartyAvrKind, FD.tick.rec.DealDate);
         end;
      end;
   end;

   if(ClientID > 0)
      pt.Clear();
      pt.rec.PartyID = ClientID;
      if(DL_GetMainObjAttr( pt, PARTY_ATTR_GROUP_FOREIGNENTITY, OBJTYPE_PARTY ) != 2)
         TypeErrorClient = DL_CheckQIParty(ClientID, ClientAvrKind, FD.tick.rec.DealDate);
         if( (TypeErrorClient == DL_QINVTYPE_ERROR_0) AND (FD_back != null) )
            ClientAvrKind = FD_back.fininstr.rec.AvoirKind;
            TypeErrorClient = DL_CheckQIParty(ClientID, ClientAvrKind, FD.tick.rec.DealDate);
         end;
      end;
   end;

   if((TypeErrorParty != DL_QINVTYPE_ERROR_0) or (TypeErrorClient != DL_QINVTYPE_ERROR_0))
      if( (TypeErrorParty != DL_QINVTYPE_ERROR_0) and (TypeErrorClient != DL_QINVTYPE_ERROR_0) )
         if( (TypeErrorParty == TypeErrorClient) AND (PartyAvrKind == ClientAvrKind) )
            ErrMsg = СоздатьОбщееСообщение(TypeErrorParty, PartyID, ClientID, PartyAvrKind);
         else
            ErrMsg = СоздатьСообщение(TypeErrorParty, PartyID, PartyAvrKind) + "|" + СоздатьСообщение(TypeErrorClient, ClientID, ClientAvrKind);
         end;
      elif( TypeErrorParty != DL_QINVTYPE_ERROR_0 )
         ErrMsg = СоздатьСообщение(TypeErrorParty, PartyID, PartyAvrKind);
      elif( TypeErrorClient != DL_QINVTYPE_ERROR_0 )
         ErrMsg = СоздатьСообщение(TypeErrorClient, ClientID, ClientAvrKind);
      end;

      if( IsMassExec == false )
         var Form = DL_QIOper_Panel(), ContinueFlag:bool = true;
         while(ContinueFlag == true)
            if( MsgBoxEx( ErrMsg + "| Продолжить выполнение?", MB_YES+MB_NO, IND_NO ) == IND_NO )
               RetVal = ContinueFlag = false;
               if(IsOprMultiExec() == true)
                  MsgBox( ErrMsg ); /*что бы нормальное сообщение в логе было*/
               end;
            else
               if(Form.Run())
                  QIOperFormData = Form.ReturnFormFields();
                  addNoteForObject( OBJTYPE_SECDEAL, makeObjectID(OBJTYPE_SECDEAL, NULL, FD.tick), 20/*именно 20, в 2029 - 18*/,
                                    IIF(QIOperFormData.Reason != "Иное", QIOperFormData.Reason, QIOperFormData.OtherReason), FD.tick.rec.DealDate );
                  ContinueFlag = false;
               end;
            end;
         end;
      else
         DealErrorsMsg[DealErrorsMsg.Size]       = ErrMsg;
         RetVal = false;
      end;
   end;

   return RetVal;
END;

// проверить при массовом исполнении сделок
PRIVATE MACRO CheckMassExecute():BOOL
  VAR cmd, DataSet, i = 0;

  InitProgress( -1, "Проверка квалифицированных инвесторов", "Проверка квалифицированных инвесторов" );

  cmd = DL_RSDCommand( " SELECT deal.t_DealID, deal.t_ID_Operation "
                      +"   FROM DV_MKDEAL_MASS_EXEC deal "
                      +"  WHERE Exists ( SELECT AtCor.t_Object "
                      +"                   FROM dobjatcor_dbt AtCor "
                      +"                  WHERE TO_NUMBER(AtCor.t_Object) = deal.t_PFI "
                      +"                    AND AtCor.t_ObjectType        = 12 /*OBJTYPE_AVOIRISS*/ "
                      +"                    AND AtCor.t_GroupID           = 34 /*OBJGROUP_ACCESSORYQI*/ "
                      +"                    AND AtCor.t_AttrID            = 1 /*OBJATTR_ACCESSORYQI_ONLY*/ "
                      +"                    AND AtCor.t_ValidFromDate     <= deal.t_DealDate "
                      +"                    AND (AtCor.t_ValidToDate = TO_DATE('01.01.0001','DD.MM.YYYY') OR AtCor.t_ValidToDate > deal.t_DealDate ) "
                      +"               ) "
                    );

   DataSet = cmd.execute();
   while( DataSet.moveNext() )

      if( CheckDeal( SPFirstDoc(LEG_KIND_DL_TICK, DataSet.t_DealID), null, true ) == false)
         DealErrorsOperID[DealErrorsOperID.Size] = DataSet.t_ID_Operation;
      end;

      UseProgress( i = i + 1 );
   end;

   if( DealErrorsMsg.Size > 0 )

       var upd = RsbSQLInsert("UPDATE doprtemp_tmp SET t_errorstatus = 1, t_errormessage = ? WHERE t_id_operation = ?", 2, DealErrorsMsg.Size );
       upd.AddParam( V_STRING , DealErrorsMsg, 2000 );
       upd.AddParam( V_INTEGER, DealErrorsOperID  );
       if(not upd.Insert())
         return false;
       end;
   end;

   RemProgress();
   return true;
END;

PRIVATE MACRO CheckOneExecute( FD:SPFirstDoc ):BOOL

   var FD_back = null;

   if( FD.Kind == DL_CONVAVR )
      FD_back = SPFirstDoc( FD.tick, true );
   end;

   if( (ПолучитьЗначениеКатегории( FD.avoiriss, OBJGROUP_ACCESSORYQI, OBJTYPE_AVOIRISS, FD.tick.rec.DealDate ) == OBJATTR_ACCESSORYQI_ONLY) OR/*Только для КИ*/
       ( (FD_back != null) AND (ПолучитьЗначениеКатегории( FD_back.avoiriss, OBJGROUP_ACCESSORYQI, OBJTYPE_AVOIRISS, FD_back.tick.rec.DealDate ) == OBJATTR_ACCESSORYQI_ONLY) )
     )
      return CheckDeal( FD, FD_back, false );
   end;

   return true;
END;

MACRO ПроверитьПринадлежностьКИ( FD:SPFirstDoc ):bool

   DealErrorsMsg.Size = 0;
   DealErrorsOperID.Size = 0;

   if( FD == NULL )
     return CheckMassExecute();
   else
     return CheckOneExecute( FD );
   end;
END;
