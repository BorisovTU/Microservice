/*                           
$Name:             sprepcar.mac
$Module:           Ценные бумаги
$Description:      отчеты по списку проводок
*/
/*
 PROGRAMMED BY:   Леонов И. Е.
 CREATION DATE:   22.06.04
*/

import "spserv.mac", SPInter, "sprepfun.mac";

private record rAccount( account );
private file FinInstr( fininstr );

private const DLDOC_SPDOCOFF     = 136; /* Отложенная проводка в сделках с ценными бумагами*/
private const KindActionSPDOCOFF = 130; /* Системный вид шага формирование сводных проводок на основе отложенных в сделках (spdocoff.dbt)*/
private const AKMULTYCARRY:integer = 15;

private var LastFIID = -1;

private macro GetFinInstr( FIID:INTEGER )
   if( LastFIID != FIID )
      ClearRecord( FinInstr );
      FinInstr.FIID = FIID;
      if( not GetEQ(FinInstr) )
         msgbox( "Не найден финансовый инструмент FIID = " + string(FIID) );
         return false;
      end;
      LastFIID = FIID;
   end;
   return true;
end;

/*получить статус проводки*/
private macro GetTxtAccTrnState(State)
   var DataSet, cmd = RSDCommand("select t_statename from dacctrn_state_dbt where t_state = ?");

   cmd.addParam("", RSDBP_IN, State);
   cmd.execute();

   DataSet = TRsbDataSet(cmd);

   if( DataSet.MoveNext() )
      return DataSet.statename;
   end;
   return "";
end;

/*получить статус проводки сводной из spdocoff*/
private macro GetTxtSpDocState(State)
   if( State == SPDOCOFF_STATE_PREPARED )
      return "Отложен";
   elif(State == SPDOCOFF_STATE_EXECUTED)
      return "Выполнен";
   elif(State == SPDOCOFF_STATE_CANCELED)
      return "Аннулирован";
   end;
   return "";
end;

/*Сформировать отчет по списку всех проводок по сделке (включая сводные) */
macro RunReportByDeal( Ticket:VARIANT )
   var query, select, ds;
   record dl_tick( dl_tick );

   SetBuff( dl_tick, Ticket ) ;      

   private macro PrintLine( Numb_Document, Account_Payer, Sum_Payer, FIID_Payer, Account_Receiver, Sum_Receiver, FIID_Receiver, Sum_NatCur, Date_Carry:date, DocStatus )
      var FIID_Payer_Code, FIID_Receiver_Code;

      if( (GetFinInstr( FIID_Payer ) != true) )
         return;
      end;
      FIID_Payer_Code = FinInstr.FI_Code;

      if( (GetFinInstr( FIID_Receiver ) != true) )
         return;
      end;
      FIID_Receiver_Code = FinInstr.FI_Code;

[├────────────────┼─────────────────────────┼────────────────────┼────────────────┼─────────────────────────┼────────────────────┼────────────────┼────────────────────┼──────────┼───────────────┤
 │################│#########################│####################│################│#########################│####################│################│####################│##########│###############│]
      ( Numb_Document:c, Account_Payer:r, Sum_Payer:r, FIID_Payer_Code:r, Account_Receiver:r, Sum_Receiver:r, FIID_Receiver_Code:r, Sum_NatCur:r, Date_Carry:r:f, DocStatus:r);
   end;

[                                                                                                                                                
                                                Список проводок по сделке #
 ┌────────────────┬─────────────────────────┬────────────────────┬────────────────┬─────────────────────────┬────────────────────┬────────────────┬────────────────────┬──────────┬───────────────┐
 │ Номер проводки │    Счет плательщика     │  Сумма  дебета     │  Валюта дебета │    Счет получателя      │   Сумма кредита    │ Валюта кредита │ Сумма в нац. валюте│   Дата   │ Статус        │
](dl_tick.DealCodeTS);                                             

   query =   " select acctrn.* "
           + "   from dacctrn_dbt acctrn, "
           + "        (select /*+LEADING(grdeal) INDEX(grdeal ddlgrdeal_dbt_idx1)*/ grdoc.t_DocID as AccTrnID "
           + "           from ddlgrdeal_dbt grdeal, ddlgrdoc_dbt grdoc "
           + "          where grdeal.t_DocKind = ? "
           + "            and grdeal.t_DocID = ? "
           + "            and grdoc.t_GrDealID = grdeal.t_ID "
           + "            and grdoc.t_DocKind = " + DLDOC_CARRY
           + "         UNION ALL "
           + "         select oprdocs.t_AccTrnID as AccTrnID"
           + "           from doproper_dbt opr, doprdocs_dbt oprdocs "
           + "          where opr.t_DocKind = ? "
           + "            and opr.t_DocumentID = ? "
           + "            and oprdocs.t_ID_Operation = opr.t_ID_Operation "
           + "            and oprdocs.t_DocKind = " + DLDOC_CARRY
           + "        ) q "
           + "  where acctrn.t_AccTrnID = q.AccTrnID "; 

   select = Dl_RSDCommand(query);

   select.AddParam(dl_tick.BOfficeKind);
   select.AddParam(dl_tick.DealID);
   select.AddParam(dl_tick.BOfficeKind);
   select.AddParam(UniID(dl_tick,OBJTYPE_SECDEAL));

   ds = select.Execute();

   while(ds.moveNext())
     PrintLine( ds.Numb_Document, ds.Account_Payer, ds.Sum_Payer, ds.FIID_Payer, ds.Account_Receiver, ds.Sum_Receiver, ds.FIID_Receiver, ds.Sum_NatCur, ds.Date_Carry, GetTxtAccTrnState(ds.State) );
   end;


   /*Сводные проводки*/
   var cmd = DL_RSDCommand("select * from DSPDOCOFF_DBT "+
                           " WHERE t_DealID = ? "+
                           "   AND t_State = ?");

   cmd.AddParam(dl_tick.DealID);
   cmd.AddParam(SPDOCOFF_STATE_EXECUTED);

   var DataSet = cmd.Execute();

   while( DataSet.MoveNext() )
      PrintLine( "Сводная проводка", DataSet.Account_Payer, DataSet.Sum_Payer, DataSet.Currency_Payer, DataSet.Account_Receiver, DataSet.Sum_Receiver, DataSet.Currency_Receiver, "", SQL_ConvTypeDate(DataSet.Date_Carry), GetTxtSpDocState(int(DataSet.State)) );
   end;

[└────────────────┴─────────────────────────┴────────────────────┴────────────────┴─────────────────────────┴────────────────────┴────────────────┴────────────────────┴──────────┴───────────────┘];
                   
   after_44_footer();
end;

/*Входит ли отложенная проводка по сделке (spdocoff) в сводную проводку*/
private macro IsIncludDocOffSubQuery( Code_CurrencyPayer:integer, Code_CurrencyReceiver:integer, Chapter:integer, Account_Payer:string, Account_Receiver:string )
   private macro ChAcc(account) /*Корректировка строки счета для запроса*/
      var account_ret = "";
      if (account == "")
         account_ret = "chr(0)"; 
      else
         account_ret = " '" + string(account) + "' "; 
      end;
      return account_ret;
   end;

   var SubQuery = "", SubQuery1 = "", SubQuery2 = "", SubQuery3 = "", SubQuery4 = "", DtPairAcc = "", CtPairAcc = "";

   /*парные счета документов проводки*/       
   if( GetAccount( Chapter, Code_CurrencyPayer, Account_Payer, rAccount, true ) )
       DtPairAcc = rAccount.PairAccount;
   end;

   if( GetAccount( Chapter, Code_CurrencyReceiver, Account_Receiver, rAccount, true ) )
       CtPairAcc = rAccount.PairAccount;
   end;

   SubQuery1 = 
          " (   t_Account_Payer = " + ChAcc(Account_Payer) +
          " and t_Account_Receiver = " + ChAcc(Account_Receiver) + " ) ";

   SubQuery2 = 
          /*возможно были идентичные проводки по парным счетам*/
          " (  ( t_Account_Payer = " + ChAcc(DtPairAcc) +
          "  and t_Account_Receiver = " + ChAcc(Account_Receiver) + " )" +
          " or ( t_Account_Payer = " + ChAcc(Account_Payer) +
          "  and t_Account_Receiver = " + ChAcc(CtPairAcc) + " )" +
          " or ( t_Account_Payer = " + ChAcc(DtPairAcc) +
          "  and t_Account_Receiver = " + ChAcc(CtPairAcc) + " ) ) ";

   SubQuery4 = " ( " + SubQuery1 + " or (not " + SubQuery1 + " and " + SubQuery2 + " ) ) ";

   /*возможно были противоположные проводки по парным счетам*/
   if( (DtPairAcc != "") AND (CtPairAcc != "") ) /*Оба счета парные*/
      SubQuery3 =  
          " and t_Account_Payer = " + ChAcc(CtPairAcc) +
          " and t_Account_Receiver = " + ChAcc(DtPairAcc);
   elif( DtPairAcc != "" ) /*Счет дебета парный*/
      SubQuery3 =  
          " and t_Account_Payer = " + ChAcc(Account_Receiver) +
          " and t_Account_Receiver = " + ChAcc(DtPairAcc);
   elif( CtPairAcc != "" ) /*Счет кредита парный*/
      SubQuery3 =  
          " and t_Account_Payer = " + ChAcc(CtPairAcc) +
          " and t_Account_Receiver = " + ChAcc(Account_Payer);
   end;                                              

   SubQuery = " and ( " + SubQuery4 + 
         IIF (SubQuery3 == "", "", " or (not " + SubQuery4 + SubQuery3 + " ) ") + " ) ";

   return SubQuery;
end;

/*Сформировать отчет по списку сводных проводок для операции БУ */
private macro RunReportAccounting( dl_comm, _ByGrpID:integer )
   file   dl_tick( dl_tick ) ;
   var    errcode, errtext, TmpFName = GetWorkFileName("docoffrp");
   var    NumRec = 0, ContinueDoc, Summa = $0, FIID, NumLine = 0;
   var    ByGrpID = 0;

   if( (_ByGrpID != null) and (_ByGrpID > 0) )
      ByGrpID = _ByGrpID;
   end;

   MACRO PrintLine( Numb_Document, Account_Payer, Sum_Payer, FIID_Payer, Account_Receiver, Sum_Receiver, FIID_Receiver, Sum_NatCur, Date_Carry, DocStatus )

      var FIID_Payer_Code, FIID_Receiver_Code;

      if( (GetFinInstr( FIID_Payer ) != true) )
         return;
      end;
      FIID_Payer_Code = FinInstr.CCY;

      if( (GetFinInstr( FIID_Receiver ) != true) )
         return;
      end;
      FIID_Receiver_Code = FinInstr.CCY;

      if( NumLine == 0 )
[
                                          СВОДНЫЕ ПРОВОДКИ ПО СДЕЛКАМ НА БИРЖЕ
                                                           #
 ┌────────────────┬─────────────────────────┬────────────────────┬────────────────┬─────────────────────────┬────────────────────┬────────────────┬────────────────────┬──────────┬───────────────┐
 │ Номер проводки │    Счет плательщика     │  Сумма  дебета     │  Валюта дебета │    Счет получателя      │   Сумма кредита    │ Валюта кредита │ Сумма в нац. валюте│   Дата   │ Статус        │
](("Дата операции " + string(dl_comm.CommDate) + "  " + "Код: " + dl_comm.CommCode):c);                                             
      end;

[├────────────────┼─────────────────────────┼────────────────────┼────────────────┼─────────────────────────┼────────────────────┼────────────────┼────────────────────┼──────────┼───────────────┤
 │################│#########################│####################│################│#########################│####################│################│####################│##########│###############│]
      ( Numb_Document:c, Account_Payer:r, Sum_Payer:r, FIID_Payer_Code:r, Account_Receiver:r, Sum_Receiver:r, FIID_Receiver_Code:r, Sum_NatCur:r, Date_Carry:r, DocStatus:r);
      NumLine = NumLine + 1;
   END;

   MACRO PrintDealsByCarryGround(AccTrnData,GroundKind)
      var
         CarryByDeal;

       var cmd = DL_RSDCommand( " select DOCOFF.*, FIP.T_CCY AS FIID_Payer_Code, FIR.T_CCY AS FIID_Receiver_Code "
                                   + "   from DSPDOCOFF_DBT DOCOFF, DFININSTR_DBT FIP, DFININSTR_DBT FIR "
                                   + "  WHERE DOCOFF.t_GrpID      = ? "
                                   + "    and DOCOFF.t_State      = ? " 
                                   + "    and DOCOFF.t_Date_Carry = ? " 
                                   + "    and DOCOFF.t_Chapter    = ? " 
                                   + "    and (DOCOFF.t_Sum_Payer = 0 " 
                                   + "     or DOCOFF.t_Currency_Payer = ? ) " 
                                   + "    and (DOCOFF.t_Sum_Receiver = 0 " 
                                   + "    or DOCOFF.t_Currency_Receiver = ?) " 
                                   +IsIncludDocOffSubQuery( AccTrnData.FIID_Payer, AccTrnData.FIID_Receiver, AccTrnData.Chapter, AccTrnData.Account_Payer, AccTrnData.Account_Receiver ) 
                                   + "    AND FIP.T_FIID = DOCOFF.T_Currency_Payer "
                                   + "    AND FIR.T_FIID = DOCOFF.T_Currency_Receiver "
                                   + "    and DOCOFF.t_GroundKind = ? "
                                   + " order by DOCOFF.t_DealID"
                              );
   
   
      cmd.AddParam(AccTrnData.GRPID);
      cmd.AddParam(SPDOCOFF_STATE_EXECUTED);
      cmd.AddParam(dl_comm.CommDate);
      cmd.AddParam(AccTrnData.Chapter);
      cmd.AddParam(AccTrnData.FIID_Payer);
      cmd.AddParam(AccTrnData.FIID_Receiver);
      cmd.AddParam(GroundKind);
      
      CarryByDeal = cmd.Execute();
      
      while( CarryByDeal.MoveNext() )
         dl_tick.DealId = CarryByDeal.DealID;
         if( GetEQ( dl_tick ) )                                                                                                                                                                                                                          
             PrintLine( dl_tick.DealCodeTS, CarryByDeal.Account_Payer, CarryByDeal.Sum_Payer, CarryByDeal.FIID_Payer_Code, CarryByDeal.Account_Receiver, CarryByDeal.Sum_Receiver, CarryByDeal.FIID_Receiver_Code, "", SQL_ConvTypeDate(CarryByDeal.Date_Carry), GetTxtSpDocState(int(CarryByDeal.State)));
         end;
      end;

   END;

   /*Найти все сделки, проводки которых составили сводную проводку*/
   MACRO PrintDealsByCarry(AccTrnData)
      var
         CarryByGround;

      var cmd = DL_RSDCommand("select sum(t_Sum_Payer) Sum_Payer, sum(t_Sum_Receiver) Sum_Receiver, t_GroundKind from DSPDOCOFF_DBT "+
                              " WHERE t_GrpID      = ? "+
                              "   and t_State      = ? " +
                              "   and t_Date_Carry = ? " +
                              "   and t_Chapter    = ? " +
                              "   and (t_Sum_Payer = 0 " +
                              "   or t_Currency_Payer = ? ) " +
                              "   and (t_Sum_Receiver = 0 " +
                              "   or t_Currency_Receiver = ?) " +
                             IsIncludDocOffSubQuery( AccTrnData.FIID_Payer, AccTrnData.FIID_Receiver, AccTrnData.Chapter, AccTrnData.Account_Payer, AccTrnData.Account_Receiver ) +
                              " group by t_GroundKind"
                             );

      cmd.AddParam(AccTrnData.GRPID);
      cmd.AddParam(SPDOCOFF_STATE_EXECUTED);
      cmd.AddParam(dl_comm.CommDate);
      cmd.AddParam(AccTrnData.Chapter);
      cmd.AddParam(AccTrnData.FIID_Payer);
      cmd.AddParam(AccTrnData.FIID_Receiver);

      CarryByGround = cmd.Execute();

      while( CarryByGround.MoveNext() )
         if( (CarryByGround.Sum_Payer != $0) or (CarryByGround.Sum_Receiver != $0) )

            if( ((AccTrnData.FIID_Receiver == AccTrnData.FIID_Payer) and (AccTrnData.Sum_Payer == CarryByGround.Sum_Payer)) or
                ( (CarryByGround.Sum_Payer == AccTrnData.Sum_Payer) and (CarryByGround.Sum_Receiver == AccTrnData.Sum_Receiver) )
              )
                PrintDealsByCarryGround(AccTrnData,CarryByGround.GroundKind);
            end;

         end;
      end;

   END;

   /*Сводные проводки*/
   ClearGlobalTmp( "ddocoffrp_tmp" );

   message( "Выполнение отчета \"Сводные проводки по сделкам на бирже\" ..." );

   var cmd = DL_RSDCommand("select ACCTRN.*, DLGRDOC.t_GrpID from DDLGRDOC_DBT DLGRDOC, DACCTRN_DBT ACCTRN "+
                           " WHERE DLGRDOC.T_SERVDOCKIND = ? "+
                           "   AND DLGRDOC.T_SERVDOCID = ? "+
                           "   AND DLGRDOC.T_DocKind = ? "+
                           "   AND DLGRDOC.T_GRDEALID = 0 " +
                           "   AND ACCTRN.T_ACCTRNID = DLGRDOC.T_DocID "+
                           IIF(ByGrpID > 0, "   AND DLGRDOC.T_GrpID = "+ByGrpID, "" )/*по горуппе, если задана (для выпусков)*/
                          );

   cmd.AddParam(dl_comm.DocKind);
   cmd.AddParam(dl_comm.DocumentID);
   cmd.AddParam(DLDOC_CARRY);

   NumRec = cmd.GetCount();

   if( NumRec > 0 )
      InitProgress( NumRec, "Выполнение отчета \"Сводные проводки по сделкам на бирже\"", "Просмотр сводных проводок" );
     
      var DataSet = cmd.Execute();
     
      NumRec = 0;
      while( DataSet.MoveNext() )
         PrintLine( "Сводная проводка              ", DataSet.Account_Payer, DataSet.Sum_Payer, DataSet.FIID_Payer, DataSet.Account_Receiver, DataSet.Sum_Receiver, DataSet.FIID_Receiver, DataSet.Sum_NatCur, SQL_ConvTypeDate(DataSet.Date_Carry), GetTxtAccTrnState(int(DataSet.State)) );
         PrintDealsByCarry(DataSet);
         UseProgress( NumRec = NumRec + 1 );
      end;
      RemProgress();                              
   end;

   if( NumLine > 0 )
[└────────────────┴─────────────────────────┴────────────────────┴────────────────┴─────────────────────────┴────────────────────┴────────────────┴────────────────────┴──────────┴───────────────┘];
      after_44_footer();
   else
      println( "Нет выполненных сводных проводок по операции." );
   end;

end;

macro RunReportDocOff( Comm:VARIANT )
   record dl_comm( dl_comm );
   SetBuff( dl_comm, Comm ) ;      

   RunReportAccounting( dl_comm );
end;
  
macro RunReportByGrpID( PmWrtGrp:VARIANT )
   record dl_pmwrtgrp( pmwrtgrp );

   SetBuff( dl_pmwrtgrp, pmwrtgrp ) ;      
   
   var cmd = DL_RSDCommand("select * from DDL_COMM_DBT "+
                           " WHERE T_DocumentID = ? "+
                           "   AND T_DocKind = ? ");

   cmd.AddParam(dl_pmwrtgrp.DocID);
   cmd.AddParam(dl_pmwrtgrp.DocKind);

   var DataSet = cmd.Execute();

   if( DataSet.MoveNext() )
      RunReportAccounting( DataSet.GetRecord(), dl_pmwrtgrp.ID);
   end;

end;                              
