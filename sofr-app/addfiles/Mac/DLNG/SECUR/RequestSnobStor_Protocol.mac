/**
  @file RequestSnobStore_Protocol.mac
  @brief Макрос печати протокола выполнения процедуры запроса в хранилище СНОБ через общесистемную плановую процедуру

  #link
  - [BOSS-187.4_Разработка планировщика запроса к Хранилищу СНОБ] (https://sdlc.go.rshbank.ru/confluence/pages/viewpage.action?pageId=248463674)

  #changeLog
  |date      |author       |tasks    |note                                                                                                          
  |----------|-------------|---------|--------------------------------------------------------------------------------------------------------------
  |31.01.2024|Хромеев Д. С.|BOSS-1819|BOSS-253_Разработка планировщика запроса к Хранилищу СНОБ. Разработка
*/

import "CbReport_h.mac";

/**
  @brief Добавить сообщение на печать в протокол
  @param [in] Msg Текст сообщения
*/
MACRO AddNpTxMes(Msg:string)
   var query, cmd;

   query =   " INSERT INTO DNPTXTBMES_TMP (T_ID, T_TBID, T_DATE, T_TIME, T_TYPE, T_MESSAGE) "
           + "                VALUES (0, 0, ?, ?, ?, ?) ";

   cmd = DL_RSDCommand(query);

   cmd.AddParam(date(0,0,0));
   cmd.AddParam(time(0));
   cmd.AddParam(0);
   cmd.AddParam(Msg);

   cmd.ExecuteCMD();

OnError(er)
   msgbox(er.Message);
END;

/**
  @brief Класс отчета
  @param [in] Header Заголовок протокола
  @param [in] GenSystDate Системная дата
  @param [in] GenSystTime Системное время
*/
PRIVATE CLASS (DL_CReportTemplate) RequestSNOBStorProtocol_Report(Header:String,GenSystDate:DATE, GenSystTime:TIME)
   var m_Header = Header;
   var m_GenSystDate = GenSystDate;
   var m_GenSystTime = GenSystTime;

   macro PrintMode():INTEGER
      return DL_OUTREPORT_EXCEL;
   end;
   macro BeginReport()
      SetIsshowAppl(false); //Всегда не показываем отчет на экран
   end;

   macro Create()
      SetActiveSheet("Протокол");

      PrintFormatString(NULL, "N1_Header",  m_Header,
                              "N1_GenDate", string(m_GenSystDate:f),
                              "N1_GenTime", string(m_GenSystTime));

      RegisterTable("N1_MainTable", NULL, "N1_Num",
                                          "N1_Msg");

      var query = "select t_Message from dnptxtbmes_tmp";
      var cmd = DL_RSDCommand(query);

      var cnt = cmd.GetCount();
      if(cnt > 0)
         InitProgress(cnt, "Печать протокола", "Печать протокола");

         var DS = cmd.Execute();
         var i = 1;
         while(DS.MoveNext())
            PrintTableLine("N1_Num", i, NULL,
                           "N1_Msg", DS.Message, NULL);

            i = i + 1;
         end;

         EndTable();

         RemProgress();
      end;
   end;

   macro EndReport()
   end;

   MACRO Run()
      Run();

      for (var fileName, GetFullReportFileNames())
        return fileName;
      end;

      return "";
   END;

   initDL_CReportTemplate( NULL, "Report_ProtocolRequestSNOBStor.xlsx", true, NULL, NULL, false ); 
END;

/**
  @brief Функция печати протокола
  @param [in] FileNameXLSX Название файла протокола
  @param [in] Header Заголовок протокола
  @param [in] GenSystDate Системная дата
  @param [in] GenSystTime Системное время
*/
MACRO PrintProtocol(FileNameXLSX:String, Header:String, GenSystDate:DATE, GenSystTime:TIME)
   var day, mon, year;
   var hour, min, sec;
   var ProtocolRepObj = RequestSNOBStorProtocol_Report(Header, GenSystDate, GenSystTime);
   var FullProtocolFileNameXLSX = ProtocolRepObj.Run();
   ProtocolRepObj = NULL;
   var FName, FExt;

   if(not isStandAlone())
      FullProtocolFileNameXLSX = "$"+FullProtocolFileNameXLSX;
   end;

   if(FullProtocolFileNameXLSX != "")
      DateSplit(GenSystDate, day, mon, year);
      TimeSplit(GenSystTime, hour, min, sec);
   
      var FromDir = SplitFile(FullProtocolFileNameXLSX, FName, FExt);
      //var NewFileNameXLSX = "Протокол_генерации_процедуры_запроса_к_хранилищу_СНОБ_" + string(day:o:2) + "." + string(mon:o:2) + "." + string(year) + " " + string(hour:o:2) + "-" + string(min:o:2) + "-" + string(sec:o2) + ".xlsx";
      var NewFileNameXLSX = FileNameXLSX + "_" + string(day:o:2) + "." + string(mon:o:2) + "." + string(year) + " " + string(hour:o:2) + "-" + string(min:o:2) + "-" + string(sec:o2) + ".xlsx";
   
      if(not RenameFile(FullProtocolFileNameXLSX, FromDir + NewFileNameXLSX))
         msgbox("Ошибка при переименовании файла протокола");
      end;
   end;
END;