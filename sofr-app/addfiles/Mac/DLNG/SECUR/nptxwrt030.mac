/*
$Name: nptxwrt030.mac
$Module: Ценные бумаги
$Description: Расчёт НОБ для операций списания/зачисления денежных средств
*/

import InsCarryDoc, "sp_categ.mac", "sp_car.mac", RsbDataSet, "nptxstbfun.mac";
private var CurrYear = 0;

private macro IsCalcNOBCloseISS()
   var ErrCode, useCalcNOBEndIIS;

   GetRegistryValue( "SECUR\\НАЛОГОВЫЙ УЧЕТ\\РАСЧЕТ_НОБ_ЗАКРЫТИЕ_ИИС3", V_BOOL, useCalcNOBEndIIS, ErrCode );

   return useCalcNOBEndIIS and (ErrCode == 0);
end;

private macro it_log(text)
  var sql = "begin it_log.log(:1); end;";
  var cmd = RSDCommand(sql);
  cmd.AddParam("",RSDBP_IN,text);
  cmd.execute;
end;

private macro Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr );
  MsgBox( ErrStr );
  return 1;
end;

Private Macro NptxCalcTaxPrevDate( Client, IIS, OperDate)
   var RetVal = ZeroDate;
   var query, DataSet, cmd;
     Query = " SELECT max(T_OPERDATE) prev_date" + 
                "   FROM dnptxop_dbt " +  
                "  WHERE t_DocKind = 4605 " + /*DL_CALCNDFL*/
                "    AND t_Recalc = chr(0) " +
                "    AND t_Client = ? " +
                "    AND t_IIS = ? " +   
                "    AND t_OPERDATE <= ?" +
                "    AND t_STATUS != 0 ";   /*DL_TXOP_Prep*/

   cmd = DL_RSDCommand(query);
   cmd.AddParam(Client);
   cmd.AddParam(IIS);
   cmd.AddParam(OperDate);
   DataSet = cmd.Execute();
   if( DataSet.moveNext() AND  (ValType(DataSet.prev_date) != V_UNDEF))
     RetVal = DataSet.prev_date;
   end;
   return RetVal;
end;

PRIVATE MACRO CheckContr (t_IDcontr):BOOL
var sql, cmd, Set; 

   sql = "select T_SERVKIND from dsfcontr_dbt where t_id = ? ";
   cmd = DL_RSDCommand(sql);
   cmd.AddParam(t_IDcontr);
   Set = cmd.Execute();
   if(Set.movenext())
     if (Set.T_SERVKIND == PTSK_CM)
       return false;
     else
       return true;
     end; 
   else 
     return false;   // несли не нашли- что-то не так в договором
   end;   
END;

private macro CheckCanIISCalcNDFL(nptxop_new, ErrStr:@string)
  var query, cmd, DataSet;
  var err = 0;

  ErrStr = "";

  cmd = DL_RSDCommand(query);

  query =   " select 1 "
          + "   from dnptxop_dbt "
          + "  where t_DocKind = " + cmd.AddParam(nptxop_new.rec.DocKind) 
          + "    and t_Client = " + cmd.AddParam(nptxop_new.rec.Client)
          + "    and t_IIS = 'X'"
          + "    and t_Contract = " + cmd.AddParam(nptxop_new.rec.Contract)
          + "    and t_OperDate > " + cmd.AddParam(nptxop_new.rec.OperDate);

  DataSet = cmd.Execute(query);

  if(DataSet.moveNext())
    err = 1;
    ErrStr = "Есть более поздние операции расчета НОБ";
  end;

  if(not err)
    cmd = DL_RSDCommand(query);

    query =   " select 1 "
            + "   from dnptxop_dbt "
            + "  where t_DocKind = " + cmd.AddParam(nptxop_new.rec.DocKind) 
            + "    and t_Client = " + cmd.AddParam(nptxop_new.rec.Client)
            + "    and t_IIS = 'X'"
            + "    and t_Contract = " + cmd.AddParam(nptxop_new.rec.Contract)
            + "    and t_SubKind_Operation = " + DL_TXBASECALC_OPTYPE_CLOSE_IIS
            + "    and t_Technical = CHR(0)";

    DataSet = cmd.Execute(query);

    if(DataSet.moveNext())
      err = 1;
      ErrStr = "По договору выполнен расчет НОБ с типом \"Закрытие ИИС\"";
    end;
  end;

  if(not err)
    cmd = DL_RSDCommand(query);

    query =   " select 1 "
            + "   from dsfcontr_dbt sf, ddlcontr_dbt dlc "
            + "  where dlc.t_DlContrID = " + cmd.AddParam(nptxop_new.rec.Contract)
            + "    and sf.t_ID = dlc.t_SfContrID " 
            + "    and sf.t_DateClose > " + GetSQLDate(date(0,0,0));

    DataSet = cmd.Execute(query);

    if(DataSet.moveNext())
      err = 1;
      ErrStr = "Договор ИИС является закрытым";
    end;
  end;

  return err;
end;

macro ExecuteStep( kind_oper, FDoc, DocKind, ID_Operation, ID_Step )
  record nptxop("nptxop");
  var err = 0;
  var TaxPeriod = 0;
  var ErrStr = "";

  SetBuff( nptxop, FDoc );

  if( (nptxop.FlagTax == SET_CHAR ) AND CheckContr(nptxop.Contract))
    datesplit(nptxop.OperDate, null, null, TaxPeriod);
    var sfcontr = TBFile("sfcontr.dbt");
    var IsIIS = false;
    var SfContrID = nptxop.Contract;

    if(SfContrID > 0)
      sfcontr.Clear();
      sfcontr.rec.ID = SfContrID;
    
      if(sfcontr.GetEQ())
        if(DL_GetMainObjAttr(sfcontr, OBJGROUP_SFCONTR_IIS, OBJTYPE_SFCONTR) == 1)
          IsIIS = true;
        end;
      end;
    end;
          
    var Query = "select 1 from dnptxop_dbt nptxop, dsfcontr_dbt sfcontr " +
                "where nptxop.t_Status = 2 " +
                "    and nptxop.t_Kind_Operation = 2035 " +
                "    and nptxop.t_Client = ? " +
                "    and sfcontr.t_ID = ?" +
                "    and ((rsi_npto.CheckContrIIS(sfcontr.t_ID) = 1 and nptxop.t_IIS = 'X')" +
                "       or rsi_npto.CheckContrIIS(sfcontr.t_ID) = 0)" +
                "    and (" +
                "            (" +
                "               ? = 'X'" +
                "               and (" +
                "                       (rsi_npto.CheckContrIIS(sfcontr.t_ID) = 1 and nptxop.t_SubKind_Operation = "+DL_TXBASECALC_OPTYPE_CLOSE_IIS+" and nptxop.t_Technical = CHR(0)) " +
                "                    or (rsi_npto.CheckContrIIS(sfcontr.t_ID) = 0 and nptxop.t_SubKind_Operation = "+DL_TXBASECALC_OPTYPE_ENDYEAR+")" +
                "                   )" +
                "            )" +
                "            or t_SubKind_Operation in ("+DL_TXBASECALC_OPTYPE_ENDYEAR+")" +
                "        )" +
                IIF(((nptxop.CloseContr == SET_CHAR) and IsIIS and IsCalcNOBCloseISS()), " AND nptxop.t_Contract = ( SELECT MP.T_DLCONTRID FROM DDLCONTRMP_DBT MP WHERE MP.t_sfcontrid = sfcontr.t_ID ) ", " ") +
                "    and EXTRACT(YEAR from nptxop.t_OperDate) = ? " +
                "    and nptxop.t_Recalc <> 'X'";

    var SqlQuery = RSDCommand(Query);
    SqlQuery.addParam("", RSDBP_IN, nptxop.Client);
    SqlQuery.addParam("", RSDBP_IN, nptxop.Contract);
    SqlQuery.addParam("", RSDBP_IN, nptxop.CloseContr);
    SqlQuery.addParam("", RSDBP_IN, TaxPeriod);
    SqlQuery.Execute();

    var nptxcalcop = TRsbDataSet(SqlQuery);
    if(not nptxcalcop.MoveNext())
      var nptxop_new = TRecHandler( "nptxop" );
      nptxop_new.Clear();

      var RefID;
      GenerateNumberByReference( 132/*OBJTYPE_NPTXCALC*/, 1 /*REFOBJ_NPTXCALC*/, @RefID, @nptxop_new.rec.Code );
    
      nptxop_new.rec.DocKind           = DL_CALCNDFL;
      nptxop_new.rec.OperDate          = nptxop.OperDate;
      if( nptxop.CloseContr == SET_CHAR )
        if((IsIIS) and (nptxop.IIS == UNSET_CHAR)) //Если договор ИИС и в операции НЕ установлен флаг "Перевод активов по ИИС"
          nptxop_new.rec.SubKind_Operation = DL_TXBASECALC_OPTYPE_CLOSE_IIS;  // Подвид операции
        else
          nptxop_new.rec.SubKind_Operation = DL_TXBASECALC_OPTYPE_ENDYEAR;  // Подвид операции
        end;
      else
        nptxop_new.rec.SubKind_Operation = DL_TXBASECALC_OPTYPE_NORMAL;  // Подвид операции
      end;
      nptxop_new.rec.Department        = 1;
      //nptxop_new.rec.Department        = {OperDprt};
      nptxop_new.rec.Oper              = nptxop.Oper;
      nptxop_new.rec.Status            = 0/*DL_TXOP_Prep*/;
      nptxop_new.rec.Recalc            = UNSET_CHAR;
      nptxop_new.rec.FIID              = ALLFININSTR;
      nptxop_new.rec.BegRecalcDate     = ZeroDate;
      nptxop_new.rec.EndRecalcDate     = ZeroDate;
      nptxop_new.rec.Client            = nptxop.Client;
      nptxop_new.rec.IIS               = IIF(IsIIS == true, SET_CHAR, UNSET_CHAR);
      //nptxop_new.rec.FlagTax           = UNSET_CHAR;  
      nptxop_new.rec.FlagTax           = SET_CHAR;/*PNV 523148*/
      
      nptxop_new.rec.Kind_Operation    = 2035;// для 4605
      nptxop_new.rec.PrevDate    = NptxCalcTaxPrevDate( nptxop.Client, IIF(IsIIS == true, SET_CHAR, UNSET_CHAR), nptxop.OperDate);

      if(IsIIS)
        var queryDContr, cmdDcontr, DSDcontr;

        queryDContr = "SELECT MP.T_DLCONTRID FROM DDLCONTRMP_DBT MP " + 
                      " WHERE MP.T_SFCONTRID = ?";

        cmdDcontr = DL_RSDCommand(queryDContr);
        cmdDcontr.addParam(nptxop.Contract);
        DSDcontr = cmdDcontr.Execute();

        if( DSDcontr.MoveNext() )
          nptxop_new.rec.Contract = DSDcontr.DLCONTRID;
        end;
       
        err = CheckCanIISCalcNDFL(nptxop_new, @ErrStr);
        if(err)
          msgbox(ErrStr);
          DL_NPTX_PutMsg(ErrStr);
          it_log(ErrStr);
        end;
      end;

      if( (not err) and (not CreateNptxOpStep(nptxop_new, true)))
        DL_NPTX_PutMsg( string("Ошибка при добавлении расчета НОБ для клиента ",  nptxop.Client, " в операции ДС ",  nptxop.id ) );
        it_log( string("Ошибка при добавлении расчета НОБ для клиента ",  nptxop.Client, " в операции ДС ",  nptxop.id ) );
        err = 1;
      end;

      datesplit({curdate},null,null,CurrYear); //текущий год
      var operyear;
      datesplit(nptxop_new.rec.OperDate,null,null,operyear);  //год сгенерированного НОБ
      if ( (CurrYear!=operyear) and  
           (nptxop_new.rec.Recalc!=SET_CHAR) and
           (nptxop_new.rec.SubKind_Operation!= DL_TXBASECALC_OPTYPE_ENDYEAR)
         )
        DL_NPTX_PutMsg( string("Операция не в текущем налоговом периоде, клиент ",  nptxop.Client, " в операции ДС ",  nptxop.id ) );
        it_log( string("Операция не в текущем налоговом периоде, клиент ",  nptxop.Client, " в операции ДС ",  nptxop.id ) );
        err = 1;
      end;

      if (not err) 
        it_log(string("Добавлен расчет НОБ для клиента ",  nptxop.Client, " в операции ДС ",  nptxop.id ) );
      end;

    end;
  end;

  if(not err)
    var StartSTBDate = GetStartSTBDate();
    if((nptxop.FlagTax == SET_CHAR) and (РАБОТА_С_ВНЕШНИМ_ХРАНИЛИЩЕМ_СНОБ()) and (StartSTBDate > date(0,0,0)) and (nptxop.OperDate >= StartSTBDate))

      datesplit(nptxop.OperDate, NULL, NULL, CurrYear);
      
      if(STB_ClientNeedRecalc(nptxop.Client, CurrYear))
        if( not InsertOprStatus(460712, 1)) //Пересчет СНОБ = Выполнить
          return Error( "Ошибка при установке статуса вида \"Пересчет СНОБ\" операции" );        
        end;
      elif(NeedNptxNettOp(nptxop.Client, CurrYear, nptxop.OperDate))
         if( not InsertOprStatus(460713, 1)) //Зачет удержанного НДФЛ = Выполнить
           return Error( "Ошибка при установке статуса вида \"Зачет удержанного НДФЛ\" операции" );        
         end;
      else
        if( not InsertOprStatus(46076, 1)) //Запрос СНОБ = Выполнить
          return Error( "Ошибка при установке статуса вида \"Запрос СНОБ\" операции" );        
        end;
      end;
    else
      if( not InsertOprStatus(46078, 1)) //Расчет сумм налога и вывода = Выполнить
        return Error( "Ошибка при установке статуса вида \"Расчет сумм налога и вывода\" операции" );        
      end;
    end;
  end;

  return err;
end;

