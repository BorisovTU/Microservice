/*
$Name:        nptxctrl.mac
$Module:      Ценные бумаги
$Description: Отчет "Контроль задолженности"
*/
import "nptxctrl_form_2025.mac","sprepfun.mac", "sqlconv.mac", "CbReport_h.mac", "scsrvrepfun.mac", "xls_parser.mac", "poisimplerepo.mac", "partysql.mac";

PRIVATE CONST CL_STATE_RES    = "резидент",
              CL_STATE_NOTRES = "нерезидент";

PRIVATE const NPTXPARTTAXRATE_RESIDENT    = 0.09; //для резидентов
PRIVATE const NPTXPARTTAXRATE_NOTRESIDENT = 0.15; //для нерезидентов

PRIVATE VAR НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ = 10;
PRIVATE VAR НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ_ИИС = 10;

PRIVATE CLASS (DL_CReportTemplate) Sp_NPTXCTRL_Report(OperDate, Year, Dbeg, Dend, IsCurBrocAcc, IsActiveClient, FromSOFR, FromDEPO, ByChanges, ChBegDate, ChEndDate, IsLoadFromFile, FilePath, FileContentType, ListWithAcc, ProtObj)
  var m_Nrec = 0, m_CurrStrNum = НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ;
  var m_Data, m_Query;
  var m_PaidSpecial, m_PaidSpecial_0;
  var m_Dg, m_Dp, m_Ds, m_Dm;
  var m_Pg, m_Pp, m_Ps, m_Pm;
  var m_OperDate        = OperDate;      
  var m_Year            = Year;          
  var m_Dbeg            = Dbeg;          
  var m_Dend            = Dend;          
  var m_IsCurBrocAcc    = IsCurBrocAcc;  
  var m_IsActiveClient  = IsActiveClient;
  var m_FromSOFR        = FromSOFR;
  var m_FromDEPO        = FromDEPO;
  var m_ByChanges       = ByChanges;
  var m_ChBegDate       = ChBegDate;
  var m_ChEndDate       = ChEndDate;
  var m_IsLoadFromFile  = IsLoadFromFile;
  var m_FilePath        = FilePath;
  var m_FileContentType = FileContentType;
  var m_ListWithAcc     = ListWithAcc;

  var m_simpleRepo = ProtObj;

  var m_show_protocol = false;

  var m_TaxGeneral;
  var m_TaxGeneral15;
  var m_PaidGeneral;
  var m_PaidGeneral15;
  var m_DEPOTaxGeneral;
  var m_DEPOTaxGeneral15;
  var m_DEPOPaidGeneral; 
  var m_DEPOPaidGeneral15;

  var m_printClient;           
  var m_printClientStatus;     
  var m_printRateGeneral;      
  var m_printRateSpecial;      
  var m_printTaxSpecial0;      
  var m_printTaxSpecial;       
  var m_printTaxGeneral;       
  var m_printDEPOTaxGeneral;   
  var m_printTaxGeneral15;     
  var m_printDEPOTaxGeneral15; 
  var m_printCalcSpecial;      
  var m_printCalcGeneral;      
  var m_printDEPOCalcGeneral;  
  var m_printCalcGeneral15;    
  var m_printDEPOCalcGeneral15;
  var m_printPaidSpecial;      
  var m_printPaidGeneral;      
  var m_printDEPOPaidGeneral;  
  var m_printPaidGeneral15;    
  var m_printDEPOPaidGeneral15;
  var m_printSOFRDueTotal;     
  var m_printDEPODueTotal;     
  var m_printDueTotal;         
  var m_printIdSofr;
  var m_printIdCft;
  var m_printIISNumber;

  private macro CheckServIsGood(sofrId)
    var oldDialog = SetDialogFlag(0);
    var query = 
       "  select c2.T_PARTYID " +
       "    from dclient_dbt c2 " +
       "   where c2.T_PARTYID = ? " +
       "     and c2.t_servicekind in("+PTSK_STOCKDL+","+PTSK_DV+","+PTSK_DEPOS+","+PTSK_SVO+") ";
    var cmd = DL_RSDCommand(query);
    cmd.addParam( sofrId );
    var ds = cmd.Execute();
    SetDialogFlag(oldDialog);
    if (ds.moveNext())
      return true;
    end;
    return false;
  OnError(er)
    SetDialogFlag(oldDialog);
    return false;
  end;

  private macro CheckSofrId(sofrId)
    var oldDialog = SetDialogFlag(0);
    var query = 
       " select party.T_PARTYID " +
       "   from dparty_dbt party " +
       "  where party.T_LEGALFORM = " + legal_form_phys +
       "     and party.T_PARTYID = ? ";
    var cmd = DL_RSDCommand(query);
    cmd.addParam( sofrId );
    var ds = cmd.Execute();
    SetDialogFlag(oldDialog);
    if (ds.moveNext())
      return true;
    end;
    return false;
  OnError(er)
    SetDialogFlag(oldDialog);
    return false;
  end;

  private macro PrintProtLine(SofrID, CftID, ProtValue)
    m_show_protocol = true;
    if (SofrID <= 0)
      SofrID = GetSofrIDFromCftId(CftID);
    end;
    if (Trim(CftID) == "")
      CftID = GetCftIDFromSofrID(SofrID);
    end;
    m_simpleRepo.PrintLine(SofrID, CftID, ProtValue);
  end;

  macro GetClientFilterCount()
    var ds = execSQLselect("SELECT COUNT (*) FROM DNPTXCTRLCLNT_TMP", TArray(), true);
    if (ds.MoveNext())
      return SQL_ConvTypeInteger(ds.value(0));
    end;
  end;

  private Macro GetClientFilterQuery(selfAlias)
    if (m_IsLoadFromFile or (GetClientFilterCount() > 0))
      return "IN (SELECT T_CLIENTID FROM DNPTXCTRLCLNT_TMP)";
    else
      return " = " + selfAlias; 
    end;
  end;

  private macro SetDataExists(clientId)
    execSql ("update DNPTXCTRLCLNT_TMP set T_DATAEXISTS = chr(88) where T_CLIENTID = " + clientId);
  end;

  PRIVATE MACRO GetQuery(IsIIS, IsNotIIS, RetParmArr:@TArray)
    /*Отбираются инвестор(инвесторы), которые являются физическими лицами и клиенты с видом обслуживания "Фондовый дилинг", "Срочные контракты" или "Депозитарное обслуживание",
      для которых существуют операции расчета НОБ с датой, входящей в период от начала года по отчетную дату включительно.*/
    var Year = 0;

    if(m_Dend == date(31,12,2022))
      datesplit(m_Dend, null, null, Year);
    end;

    private macro AddRetParm(Parm)
      var retStr = "?";

      if(ValType(RetParmArr) != V_UNDEF)
        RetParmArr[RetParmArr.size] = Parm;
      else
        if(ValType(Parm) == V_DATE)
          retStr = GetSQlDate(Parm);
        else
          retStr = string(Parm);
        end;
      end;

      return retStr;
    end;


    var Query = "with cl as (select party.T_PARTYID, party.T_ShortName " +
                "              from dparty_dbt party " +
                "             where party.T_LEGALFORM = " + legal_form_phys +
                "               and exists ( select c2.T_PARTYID " +
                "                              from dclient_dbt c2 " +
                "                             where c2.T_PARTYID = party.T_PARTYID " +
                "                               and c2.t_servicekind in("+PTSK_STOCKDL+","+PTSK_DV+","+PTSK_DEPOS+","+PTSK_SVO+") " +
                "                          ) ";

    if(m_ByChanges == SET_CHAR)
      Query = Query + 
                             "  and ( exists ( select 1 " 
                           + "                  from dnptxtotalbase_dbt stb " 
                           + "                 where stb.t_ClientID = party.T_PARTYID " 
                           + "                   and stb.t_TaxPeriod = " + AddRetParm(m_Year) 
                           + "                   and stb.t_StorState = " + NPTXTOTALBASE_STORSTATE_ACTIVE 
                           + "                   and stb.t_ConfirmState = " + NPTXTOTALBASE_CONFIRMSTATE_CONFIRMED
                           + "                   and Exists(select 1"
                           + "                                from doprstep_dbt oprstep "
                           + "                               where oprstep.t_ID_Operation = stb.t_ID_Operation "
                           + "                                 and oprstep.t_ID_Step = stb.t_ID_Step "
                           + "                                 and oprstep.t_Syst_Date between "+AddRetParm(date(m_ChBegDate))+" and " + AddRetParm(date(m_ChEndDate))
                           + "                             )"
                           + "              ) " 
                           + "    or exists ( select 1 " 
                           + "                  from dnptxtbext_dbt tbext "
                           + "                 where tbext.t_ClientID = party.T_PARTYID " 
                           + "                   and tbext.t_TaxPeriod = " + AddRetParm(m_Year) 
                           + "                   and tbext.t_LoadDate between "+AddRetParm(date(m_ChBegDate))+" and " + AddRetParm(date(m_ChEndDate))
                           + "                   and tbext.t_IsChanged = 'X' "
                           + "              ) " 
                           + "      ) ";
     end;

     Query = Query + " and party.T_PARTYID " + GetClientFilterQuery("party.T_PARTYID");

     Query = Query +
                "          ) "+
                "select q.client, " +
                "       q.T_ShortName, " +
                "       q.KindIIS, " +
                "       q.t_IISNumber, " +
                "       q.t_contrid, " +
                " (SELECT o.t_Code " +
                "    FROM DOBJCODE_DBT o " +
                "   WHERE o.t_ObjectType = 3 " +
                "     AND o.t_CodeKind = 101 " +
                "     AND o.t_ObjectID = q.client " +
                "     AND o.t_state = 0) as cftid, " +
                "       NVL(( " +
                "              select rsb_struct.getDouble(notetext.t_Text) " +
                "              from dnotetext_dbt notetext " +
                "              where notetext.t_ObjectType = 3 " +
                "                and notetext.t_DocumentID = LPAD(to_char(q.client), 10, '0') " +
                "                and notetext.t_NoteKind = 57 " +
                "           ), 0) as RateSpecial " +
                "from ( ";
    if(IsNotIIS)
          Query = Query + "select cl.T_PARTYID Client, cl.T_ShortName, 0 KindIIS, '' as t_IISNumber, 0 as t_contrid "+ 
                          "  from cl "+               
                          " where  ( " +
                          "          exists ( select 1 " +
                          "                     from dnptxtotalbase_dbt stb " +
                          "                    where stb.t_ClientID = cl.T_PARTYID " +
                          "                      and stb.t_TaxPeriod = " + AddRetParm(m_Year) +
                          "                      and stb.t_StorState = " + NPTXTOTALBASE_STORSTATE_ACTIVE +
                          "                      and stb.t_TaxBaseKind != " + NPTXTOTALBASE_TAXBASEKIND_IIS +
                          "                 ) ";
          // Если не установлен признак "Выводить по изменениям" и есть операции расчета НОБ без признака "Технический расчет"
          if(m_ByChanges == UNSET_CHAR)
            Query = Query +
                          "       or exists ( select 1 " +
                          "                    from dnptxop_dbt op " +
                          "                    where op.t_DocKind = " + DL_CALCNDFL +
                          "                      and op.t_Client = cl.t_PartyID " +
                          "                      and op.t_OperDate between "+AddRetParm(date(m_DBeg))+" and " + AddRetParm(date(m_DEnd)) + 
                          "                      and op.t_IIS <> CHR(88) " +
                          "                      and RSB_SECUR.GetMainObjAttr(132, LPAD(to_char(op.t_ID), 34, '0'), 1, "+AddRetParm(date(m_DBeg))+") = 0 " +
                          "                 ) ";
          end;

          Query = Query +
                          "       or exists ( select 1 " + 
                          "                     from dnptxtbext_dbt tbext "
                          "                    where tbext.t_ClientID = cl.T_PARTYID " + 
                          "                      and tbext.t_TaxPeriod = " + AddRetParm(m_Year) +
                          "                      and tbext.t_TaxBaseKind != " + NPTXTOTALBASE_TAXBASEKIND_IIS + 
                          "                 ) " + 
                          "       or exists ( select 1 " +
                          "                     from dnptxobj_dbt nptxobj " +
                          "                    where nptxobj.t_Client = cl.t_PartyID " +
                          "                      and nptxobj.t_Date between "+AddRetParm(date(m_DBeg))+" and " + AddRetParm(date(m_DEnd)) +
                          "                      and nptxobj.t_Kind in ( " + string(TXOBJ_DIVPAY_SEC_0) + ", " +
                                                                             string(TXOBJ_PAIDSPECIAL) + ", " +
                                                                             string(TXOBJ_PAIDSPECIAL_0) + 
                                                                      ") " +
                          "              ) " +
                          "       or exists ( select 1 " +
                          "                     from dnptxobj_dbt nptxobj " +
                          "                    where nptxobj.t_Client = cl.t_PartyID " +
                          "                      and nptxobj.t_Date between "+AddRetParm(date(m_DBeg))+" and " + AddRetParm(date(m_DEnd)) +
                          "                      and nptxobj.t_Kind in ( " + string(TXOBJ_DIVPAY_SEC) + ", " +
                                                                             string(TXOBJ_PAIDSPECIAL_IIS) +
                                                                      ") " +
                          "                      and nptxobj.t_FromOutSyst = CHR(0) " + 
                          "              ) " +
                          "       ) " +
               IIF(IsIIS, "  union all ", "");
     end;
     if(IsIIS)
          Query = Query + "select cl.T_PARTYID Client, cl.T_ShortName, 1 KindIIS, sfcontr.t_number as t_IISNumber, sfcontr.t_id as t_contrid "+ 
                          "  from cl, ddlcontr_dbt dlcontr, dsfcontr_dbt sfcontr  "+
                          "   WHERE     sfcontr.t_PartyID = cl.T_PARTYID " +
                          "         AND dlcontr.t_SfContrID = sfcontr.t_ID " +
                          "         AND dlcontr.t_IIS = chr(88) " +
                          "         AND SFCONTR.T_DATEBEGIN <= TO_DATE ('31.12."+m_Year+"', 'DD.MM.YYYY') " +
                          "         AND (SFCONTR.T_DATECLOSE >= TO_DATE ('01.01."+m_Year+"', 'DD.MM.YYYY') " +
                          "              OR SFCONTR.T_DATECLOSE = TO_DATE ('01.01.0001', 'DD.MM.YYYY')) " +
                          " AND ( " +
                          "          exists ( select 1 " +
                          "                     from dnptxtotalbase_dbt stb " +
                          "                    where stb.t_ClientID = cl.T_PARTYID " +
                          "                      and stb.t_TaxPeriod = " + AddRetParm(m_Year) +
                          "                      and stb.t_StorState = " + NPTXTOTALBASE_STORSTATE_ACTIVE +
                          "                      and stb.t_TaxBaseKind = " + NPTXTOTALBASE_TAXBASEKIND_IIS +
                          "                 ) ";
          // Если не установлен признак "Выводить по изменениям" и есть операции расчета НОБ без признака "Технический расчет"
          if(m_ByChanges == UNSET_CHAR)
             Query = Query +
                          "       or exists ( select 1 " +
                          "                    from dnptxop_dbt op " +
                          "                    where op.t_DocKind = " + DL_CALCNDFL +
                          "                      and op.t_Client = cl.t_PartyID " +
                          "                      and op.t_OperDate between "+AddRetParm(date(m_DBeg))+" and " + AddRetParm(date(m_DEnd)) +
                          "                      and op.t_IIS = CHR(88) " +
                          "                      and RSB_SECUR.GetMainObjAttr(132, LPAD(to_char(op.t_ID), 34, '0'), 1, "+AddRetParm(date(m_DBeg))+") = 0 " +
                          "                 ) ";
          end;

          Query = Query +
                          "       or exists ( select 1 " + 
                          "                     from dnptxtbext_dbt tbext "
                          "                    where tbext.t_ClientID = cl.T_PARTYID " + 
                          "                      and tbext.t_TaxPeriod = " + AddRetParm(m_Year) + 
                          "                      and tbext.t_TaxBaseKind = " + NPTXTOTALBASE_TAXBASEKIND_IIS +
                          "                 ) " +
                          "       or exists ( " +
                          "                    select 1 " +
                          "                     from dnptxobj_dbt nptxobj, dnptxobdc_dbt obdc, dnptxop_dbt nptxop " +
                          "                    where nptxobj.t_Client = cl.t_PartyID " +
                          "                      and nptxobj.t_Date between "+AddRetParm(date(m_DBeg))+" and " + AddRetParm(date(m_DEnd)) +
                          "                      and nptxobj.t_Kind in ( " + string(TXOBJ_DIVPAY_SEC) + ", " +
                                                                             string(TXOBJ_DIVPAY_SEC_0) + ", " +
                                                                             string(TXOBJ_PAIDGENERAL_15_1) + ", " +
                                                                             string(TXOBJ_PAIDGENERAL_15_1_0) + ", " +
                                                                             string(TXOBJ_PAIDGENERAL) + ", " +
                                                                             string(TXOBJ_PAIDGENERAL_0) + ", " +
                                                                             string(TXOBJ_PAIDMATERIAL) + ", " +
                                                                             string(TXOBJ_PAIDBILL) + ", " +
                                                                             string(TXOBJ_PAIDGENERAL_15_2) + ", " +
                                                                             string(TXOBJ_PAIDGENERAL_15_9) + ", " +
                                                                             string(TXOBJ_PAIDSPECIAL) + ", " +
                                                                             string(TXOBJ_PAIDSPECIAL_0) + ", " +
                                                                             string(TXOBJ_PAIDGENERAL_IIS) + ", " +
                                                                             string(TXOBJ_PAIDGENERAL_15_IIS) + ", " +
                                                                             string(TXOBJ_PAIDSPECIAL_IIS) +
                                                                      ") " +
                          "                       and rsi_npto.CheckObjIIS(nptxobj.t_AnaliticKind6, nptxobj.t_Analitic6) = 1 " +
                          "                       and obdc.t_ObjID = nptxobj.t_ObjID " +
                          "                       and nptxop.t_ID = obdc.t_DocID " +
                          "                       and nptxop.t_DocKind = " + string(DL_CALCNDFL) +
                          "                       and nptxop.t_SubKind_Operation = " + string(DL_TXBASECALC_OPTYPE_CLOSE_IIS) +
                          "                 ) " +
                          "       ) ";
     end;
     Query = Query + "       ) q ";

     Query = Query +
                " order by q.t_ShortName, q.KindIIS DESC";
    return Query;
  END;


  PRIVATE MACRO BeginReport()
    CreateTotalBook();
    SetActiveSheet("Контроль");
    
    //m_Query     = GetQuery();
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO PrintRepHeader()
     PrintFormatString( NULL, "H0_1", m_OperDate,
                              "H0_2", m_Year
                      );
     this.ПечататьПромежуточныеИтоги();
     CopyAllSheetInTotalBook("Контроль", false, "N1_Header", 0, "Контроль");
     CopyAllSheetInTotalBook("Контроль", false, "N1_MainTableHeader", 0, "Контроль");
  END;

  PRIVATE MACRO PrintAccRepHeader(IsIIS)
     var SheetName = IIF(IsIIS, "Счета ИИС", "Счета");
     var prefix = IIF(IsIIS, "N3_", "N2_");
     
     PrintFormatString( NULL, prefix + "H0_1", m_OperDate,
                              prefix + "H0_2", m_Year
                      );
     CopyAllSheetInTotalBook(SheetName, false, prefix + "Header", 0, SheetName);
  END;

  /*Убрано наименование листа из формулы итогов*/
  MACRO GetFormulaSumm()
     GetFormulaSummREG();
  END;

  PRIVATE MACRO RegistrTableCtrl()
     RegisterTable( "MainTable", NULL,
                    "IDSOFR",
                    "IDCFT",
                    "Client",            
                    "IISNumber",
                    "ClientStatus",      
                    "RateGeneral",       
                    "RateSpecial",       
                    "TaxSpecial0",       
                    "TaxSpecial",        
                    "TaxGeneral",        
                    "DEPOTaxGeneral",    
                    "TaxGeneral15",      
                    "DEPOTaxGeneral15",  
                    "CalcSpecial",       
                    "CalcGeneral",       
                    "DEPOCalcGeneral",   
                    "CalcGeneral15",     
                    "DEPOCalcGeneral15",    
                    "PaidSpecial",         
                    "PaidGeneral",           
                    "DEPOPaidGeneral",   
                    "PaidGeneral15",     
                    "DEPOPaidGeneral15", 
                    "SOFRDueTotal",      
                    "DEPODueTotal",      
                    "DueTotal"           
                  );

       SetCellAutoSumma( "Всего", 
                         "TaxSpecial0",       
                         "TaxSpecial",        
                         "TaxGeneral",        
                         "DEPOTaxGeneral",    
                         "TaxGeneral15",      
                         "DEPOTaxGeneral15",  
                         "CalcSpecial",       
                         "CalcGeneral",       
                         "DEPOCalcGeneral",   
                         "CalcGeneral15",     
                         "DEPOCalcGeneral15",    
                         "PaidSpecial",         
                         "PaidGeneral",           
                         "DEPOPaidGeneral",   
                         "PaidGeneral15",     
                         "DEPOPaidGeneral15", 
                         "SOFRDueTotal",      
                         "DEPODueTotal",      
                         "DueTotal");

       PrintTableLine( "IDSOFR", "Всего", null );
       PrintTableLine( "IDSOFR", "В том числе", null );
  END;

  PRIVATE MACRO FSUMIIS(CellName:string)
    return F(FormulaSUM() + "(" + CellName + string(НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ_ИИС) + ":" + CellName + string(m_CurrStrNum) + ")");
  END;

  PRIVATE MACRO FSUBTOTAL(CellName:string)
    return F(FormulaSubtotal() + "(9;" + CellName + string(НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ) + ":" + CellName + string(m_CurrStrNum) + ")");
  END;

  PRIVATE MACRO RegistrTableAcc(IsIIS)
    var prefix = IIF(IsIIS, "N3_", "N2_");
    if(m_IsCurBrocAcc == SET_CHAR)
        RegisterTable(  prefix + "MainTable", NULL,
                        prefix + "ContractNumber",
                        prefix + "ServiceKind",
                        prefix + "ServiceSubKind",
                        prefix + "AccNumber",
                        prefix + "AccCur",
                        prefix + "RestRub"
                     );
    else
        RegisterTable(  prefix + "MainTableCur", NULL,
                        prefix + "ContractNumber1",
                        prefix + "ServiceKind1",
                        prefix + "ServiceSubKind1",
                        prefix + "AccNumber1",
                        prefix + "Rest1"
                     );
    end;
  end;

  MACRO ПечататьПромежуточныеИтоги()
     SetSubtotalEx(7, 10,
                  "H4",
                  "I4",
                  "J4",
                  "K4",
                  "L4",
                  "M4",
                  "N4",
                  "O4",
                  "P4",
                  "Q4",
                  "R4",
                  "S4",
                  "T4",
                  "U4",
                  "V4",
                  "W4",
                  "X4",
                  "Y4",
                  "Z4"
                );
  END;

  PRIVATE MACRO LoadLineDataByClnt()

    m_printClient            = this.Client();            
    m_printClientStatus      = this.ClientStatus();      
    m_printRateGeneral       = this.RateGeneral();       
    m_printRateSpecial       = this.RateSpecial();       
    m_printTaxSpecial0       = this.TaxSpecial0();       
    m_printTaxSpecial        = this.TaxSpecial();        
    m_printTaxGeneral        = this.TaxGeneral();        
    m_printDEPOTaxGeneral    = this.DEPOTaxGeneral();    
    m_printTaxGeneral15      = this.TaxGeneral15();      
    m_printDEPOTaxGeneral15  = this.DEPOTaxGeneral15();  
    m_printCalcSpecial       = this.CalcSpecial();       
    m_printCalcGeneral       = this.CalcGeneral();       
    m_printDEPOCalcGeneral   = this.DEPOCalcGeneral();   
    m_printCalcGeneral15     = this.CalcGeneral15();     
    m_printDEPOCalcGeneral15 = this.DEPOCalcGeneral15(); 
    m_printPaidSpecial       = this.PaidSpecial();       
    m_printPaidGeneral       = this.PaidGeneral();       
    m_printDEPOPaidGeneral   = this.DEPOPaidGeneral();   
    m_printPaidGeneral15     = this.PaidGeneral15();     
    m_printDEPOPaidGeneral15 = this.DEPOPaidGeneral15(); 
    m_printSOFRDueTotal      = this.SOFRDueTotal();      
    m_printDEPODueTotal      = this.DEPODueTotal();      
    m_printDueTotal          = this.DueTotal();          
    m_printIdSofr            = this.GetSofrID();
    m_printIdCft             = this.CftID();
    m_printIISNumber         = this.IISNumber();

  END;


  PRIVATE MACRO PrintLineByClnt()

    PrintTableLine( 
                    "IDSOFR",             m_printIdSofr,            null,
                    "IDCFT",              m_printIdCft,             null,
                    "Client",             m_printClient,            null,
                    "IISNumber",          m_printIISNumber,         null,
                    "ClientStatus",       m_printClientStatus,      null,
                    "RateGeneral",        m_printRateGeneral,       null,//формат ячеек процентный. на 100 домнажать коэффициент нет необходимости
                    "RateSpecial",        m_printRateSpecial,       null,//формат ячеек процентный. на 100 домнажать коэффициент нет необходимости
                    "TaxSpecial0",        m_printTaxSpecial0,       null,
                    "TaxSpecial",         m_printTaxSpecial,        null,
                    "TaxGeneral",         m_printTaxGeneral,        null,
                    "DEPOTaxGeneral",     m_printDEPOTaxGeneral,    null,
                    "TaxGeneral15",       m_printTaxGeneral15,      null,
                    "DEPOTaxGeneral15",   m_printDEPOTaxGeneral15,  null,
                    "CalcSpecial",        m_printCalcSpecial,       null,
                    "CalcGeneral",        m_printCalcGeneral,       null,
                    "DEPOCalcGeneral",    m_printDEPOCalcGeneral,   null,
                    "CalcGeneral15",      m_printCalcGeneral15,     null,
                    "DEPOCalcGeneral15",  m_printDEPOCalcGeneral15, null,
                    "PaidSpecial",        m_printPaidSpecial,       null,
                    "PaidGeneral",        m_printPaidGeneral,       null,
                    "DEPOPaidGeneral",    m_printDEPOPaidGeneral,   null,
                    "PaidGeneral15",      m_printPaidGeneral15,     null,
                    "DEPOPaidGeneral15",  m_printDEPOPaidGeneral15, null,
                    "SOFRDueTotal",       m_printSOFRDueTotal,      null,
                    "DEPODueTotal",       m_printDEPODueTotal,      null,
                    "DueTotal",           m_printDueTotal,          null
                  );
                  
  END;
  
  PRIVATE MACRO PrintLineNoData()
    PrintTableLine(  "Client",               "",       null,
                     "ClientStatus",         "",       null,      
                     "RateGeneral",          "",       null,   
                     "RateSpecial",          "",       null,
                     "TaxSpecial0",          $0,       null,
                     "TaxSpecial",           $0,       null,
                     "TaxGeneral",           $0,       null,
                     "DEPOTaxGeneral",       $0,       null,
                     "TaxGeneral15",         $0,       null,
                     "DEPOTaxGeneral15",     $0,       null,
                     "CalcSpecial",          $0,       null,
                     "CalcGeneral",          $0,       null,
                     "DEPOCalcGeneral",      $0,       null,
                     "CalcGeneral15",        $0,       null,
                     "DEPOCalcGeneral15",    $0,       null,
                     "PaidSpecial",          $0,       null,
                     "PaidGeneral",          $0,       null,
                     "DEPOPaidGeneral",      $0,       null,
                     "PaidGeneral15",        $0,       null,
                     "DEPOPaidGeneral15",    $0,       null,
                     "SOFRDueTotal",         $0,       null,
                     "DEPODueTotal",         $0,       null,
                     "DueTotal",             $0,       null 
                  );

  END;

  PRIVATE MACRO EndReport()
    SaveTotalbook();
    ReplaceAndCalculate("ПРОМЕЖУТОЧНЫЕ,ИТОГИ", "ПРОМЕЖУТОЧНЫЕ.ИТОГИ");
    ReplaceAndCalculate( GetDL_FORMULA(), "=" );
  END;

 PRIVATE MACRO PrintOneBrocAccLine(IsIIS, ContractNumber, ServiceKind, ServiceSubKind, AccNumber, Rest, AccCurr, RestRub)
    var prefix = IIF(IsIIS, "N3_", "N2_");
 
    if(m_IsCurBrocAcc == SET_CHAR)
        PrintTableLine( prefix + "ContractNumber",  ContractNumber,     null,
                        prefix + "ServiceKind",     ServiceKind,        null,
                        prefix + "ServiceSubKind",  ServiceSubKind,     null,
                        prefix + "AccNumber",       AccNumber,          null,
                        prefix + "AccCurr",         AccCurr,            null,
                        prefix + "N2_RestRub",      RestRub,            null
                      );
    else
        PrintTableLine( prefix + "ContractNumber1", ContractNumber,     null,
                        prefix + "ServiceKind1",    ServiceKind,        null,
                        prefix + "ServiceSubKind1", ServiceSubKind,     null,
                        prefix + "AccNumber1",      AccNumber,          null,
                        prefix + "Rest1",           Rest,               null
                      );
    end;
 end;

  PRIVATE MACRO PrintTableHeader(IsIIS)
     var SheetName = IIF(IsIIS, "Счета ИИС", "Счета");
     var prefix = IIF(IsIIS, "N3_", "N2_");
     var FieldName = IIF(m_IsCurBrocAcc, "F", "E");
     var SumFieldName = IIF(m_IsCurBrocAcc, prefix + "Sum", prefix + "Sum1");
     var SumLine = prefix + IIF(m_IsCurBrocAcc, "SumLine", "SumLine1");
     
     PrintFormatString(NULL, IIF(m_IsCurBrocAcc == SET_CHAR, prefix + "Client", prefix + "Client1"),    m_Data.ShortName,
                      IIF(m_IsCurBrocAcc == SET_CHAR, prefix + "DueTotal", prefix + "DueTotal1"),  this.DueTotal()//, null
                    );
     CopyAllSheetInTotalBook(SheetName, false, IIF(m_IsCurBrocAcc == SET_CHAR, prefix + "ClientTable", prefix + "ClientTable1"), 0, SheetName);
     
     PrintFormatString("", SumFieldName, IIF(IsIIS, FSUMIIS(FieldName), FSUBTOTAL(FieldName)));
     CopyAllSheetInTotalBook(SheetName, false, SumLine, 0, SheetName);
     CopyAllSheetInTotalBook(SheetName, false, prefix + IIF(m_IsCurBrocAcc, "TableHeader", "TableHeader1"), 0, SheetName);
     RegistrTableAcc(IsIIS);
  end;

  PRIVATE MACRO GetDataCount(IsIIS)
    var QuerySfSSICount;
    var SfSSICountRSDCommand;
  
    var QueryContrs =  "select dlcontr.*, sfcontr.t_Number as t_Number" +
                       " from ddlcontr_dbt dlcontr, dsfcontr_dbt sfcontr" +
                       " where sfcontr.t_PartyID = ?" +
                       "     and dlcontr.t_SfContrID = sfcontr.t_ID" +
            IIF(IsIIS, "     and dlcontr.t_IIS = 'X'", " and dlcontr.t_IIS <> 'X'");
                       " order by t_Number";
 
     var ContrsRsdCommand = RSDCommand(QueryContrs);
     ContrsRsdCommand.addParam("", RSDBP_IN, m_Data.Client);
     ContrsRsdCommand.execute();
 
     
     var DlContrDS = TRsbDataSet(ContrsRsdCommand);
     var Count = 0;
     var IsFirstLine = true;
     var CountData = 0;

     while(DlContrDS.MoveNext())         
         if(DlContrDS.UseSubAcc == SET_CHAR)
              var QuerySC = "select SubContr.t_ID, ServKind.t_Name as ServKind, ServKSub.t_Name as ServKindSub" +
                           " from dsfcontr_dbt SubContr, ddlcontr_dbt dlcontr, dsfcontr_dbt sfcontr, ddlcontrmp_dbt dlcontrmp, dservkind_dbt ServKind, dservksub_dbt servksub" +
                           " where DLCONTR.T_SFCONTRID = sfcontr.t_ID" +
                           "     and DLCONTRMP.T_DLCONTRID = DLCONTR.T_DLCONTRID" +
                           "     and SubContr.t_ID = DLCONTRMP.T_SFCONTRID" +
                           "     and DLCONTR.T_DLCONTRID = ?" +
                           "     and SubContr.t_DateClose = " + sqlDateToStr(date(0, 0, 0)) +
                           "     and SubContr.t_ServKind in(" + PTSK_STOCKDL + ", " + PTSK_CURRDL + ", " + PTSK_DV + ")" +
                           "     and ServKind.t_ServiseKind = SubContr.t_ServKind" +
                           "     and ServKSub.t_ServiceKind = SubContr.t_ServKind" +
                           "     and ServKSub.t_ServiceKindSub = SubContr.t_ServKindSub";
 
             var SCRsdCommand = RSDCommand(QuerySC);
             SCRsdCommand.addParam("", RSDBP_IN, DlContrDS.DlContrID);
             SCRsdCommand.execute();
     
             var SubContrsDS = TRsbDataSet(SCRsdCommand);
             while(SubContrsDS.MoveNext())
                 QuerySfSSICount = "select count(1) as t_Count" +
                                  " from dsfssi_dbt sfssi, dsettacc_dbt settacc" +
                                  " where SFSSI.T_OBJECTTYPE = " + OBJTYPE_SFCONTR +
                                  "  and SFSSI.T_OBJECTID = lpad(?, 10, '0')" +
          IIF(not m_IsCurBrocAcc, "  and SFSSI.t_FIID = " + string(NATCUR), "") +
                                  "  and SettAcc.t_SettAccID = SfSSI.t_SetAccID order by t_Account";

                 SfSSICountRSDCommand = RSDCommand(QuerySfSSICount);
                 SfSSICountRSDCommand.addParam("", RSDBP_IN, SubContrsDS.ID);
                 SfSSICountRSDCommand.execute();
 
                 var SfSSISubDSCount = TRsBDataSet(SfSSICountRSDCommand);
                 if(SfSSISubDSCount.MoveNext())
                    CountData = CountData + SfSSISubDSCount.Count;
                 end;
             end;
         else
             var QueryDlContrAccCount = "select count(1) as t_Count" +
                                       " from ddlcontracc_dbt dlcontracc, daccount_dbt account, dfininstr_dbt fininstr" +
                                       " where dlcontracc.t_DlContrID = ?" +
                                       "     and dlcontracc.t_MPID = 0 " +
                                       "     and dlcontracc.t_AccountID > 0 " +
                                       "     and account.t_AccountID = dlcontracc.t_AccountID" +
                                       "     and account.t_Close_Date = " + sqlDateToStr(date(0, 0, 0)) +
               IIF(not m_IsCurBrocAcc, "     and account.t_Code_Currency = " + string(NATCUR), "") +
                                       "     and fininstr.t_FI_Kind = " + FIKIND_CURRENCY +
                                       "     and fininstr.t_FIID = account.t_Code_Currency";

             var DlContrAccCountRsdCommand = RSDCommand(QueryDlContrAccCount);
             DlContrAccCountRsdCommand.addParam("", RSDBP_IN, DlContrDS.DlContrID);
             DlContrAccCountRsdCommand.execute();
 
             var DlContrAccCountDS = TRsbDataSet(DlContrAccCountRsdCommand);
             if(DlContrAccCountDS.MoveNext())
                CountData = CountData + DlContrAccCountDS.Count;
             end;
         end;

         Count = Count + 1;
     end;

     if(Count == 0)
         var QuerySfContrs = "select sfcontr.*, servkind.t_Name as t_ServiceKind, servksub.t_Name as t_ServiceKindSub" +
                            " from dsfcontr_dbt sfcontr, dservkind_dbt servkind, dservksub_dbt servksub" +
                            " where t_PartyID = ?" +
                            "   and t_ServKind in (" + PTSK_STOCKDL + ", " + PTSK_DV + ")" +
                 IIF(IsIIS, "   and rsi_npto.CheckContrIIS(sfcontr.t_ID) = 1", " and rsi_npto.CheckContrIIS(sfcontr.t_ID) <> 1") +
                            "   and servkind.t_ServiseKind = sfcontr.t_ServKind" +
                            "   and servksub.t_ServiceKind = sfcontr.t_ServKind" +
                            "   and servksub.t_ServiceKindSub = sfcontr.t_ServKindSub" +
                            " order by sfcontr.t_Number";

         var SfContrRsdCommand = RSDCommand(QuerySfContrs);
         SfContrRsdCommand.addParam("", RSDBP_IN, m_Data.Client);
         SfContrRsdCommand.execute();
         
         var SfContrDS = TRsbDataSet(SfContrRsdCommand);
         while(SfContrDS.MoveNext())
             QuerySfSSICount = "select count(1) as t_Count" +
                              " from dsfssi_dbt sfssi, dsettacc_dbt settacc, daccount_dbt account, dfininstr_dbt fininstr" +
                              " where SFSSI.T_OBJECTTYPE = " + OBJTYPE_SFCONTR +
                              "  and SFSSI.T_OBJECTID = lpad(?, 10, '0')" +
      IIF(not m_IsCurBrocAcc, "  and SFSSI.t_FIID = " + string(NATCUR), "") +
                              "  and settacc.t_SettAccID = SFSSI.t_SetAccID" +
                              "  and account.t_Chapter = settacc.t_Chapter" +
                              "  and account.t_Account = settacc.t_Account" +
                              "  and account.t_Code_Currency = settacc.t_FIID" +
                              "  and fininstr.t_FI_Kind = " + FIKIND_CURRENCY +
                              "  and fininstr.t_FIID = settacc.t_FIID";

             SfSSICountRSDCommand = RSDCommand(QuerySfSSICount);
             SfSSICountRSDCommand.addParam("", RSDBP_IN, SfContrDS.ID);
             SfSSICountRSDCommand.execute();
 
             var SfSSICountDS = TRsBDataSet(SfSSICountRSDCommand);
             if(SfSSICountDS.MoveNext())
                CountData = CountData + SfSSICountDS.Count;
             end;
         end;
     end;

     return CountData;
  end;

  PRIVATE MACRO PrintOneClientTable(IsIIS)
     var SheetName = IIF(IsIIS, "Счета ИИС", "Счета");
     var prefix = IIF(IsIIS, "N3_", "N2_");
     var QuerySfSSI;
     var SfSSIRSDCommand;
     var RestRub = $0.0, Rest = $0.0;
     record AccBuf(account);

     if(IsIIS)
        m_CurrStrNum = int(НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ_ИИС + GetDataCount(IsIIS) - 1);
     else
        m_CurrStrNum = int(НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ + GetDataCount(IsIIS) - 1);
     end;
  
     var QueryContrs = "select dlcontr.*, sfcontr.t_Number as t_Number" +
                       " from ddlcontr_dbt dlcontr, dsfcontr_dbt sfcontr" +
                       " where sfcontr.t_PartyID = ?" +
                       "     and dlcontr.t_SfContrID = sfcontr.t_ID" +
            IIF(IsIIS, "     and dlcontr.t_IIS = 'X'", " and dlcontr.t_IIS <> 'X'") +
                       " order by t_Number";
 
     var ContrsRsdCommand = RSDCommand(QueryContrs);
     ContrsRsdCommand.addParam("", RSDBP_IN, m_Data.Client);
     ContrsRsdCommand.execute();
 
     
     var DlContrDS = TRsbDataSet(ContrsRsdCommand);
     var Count = 0;
     var IsFirstLine = true;
     var CountData = 0;

     while(DlContrDS.MoveNext())         
         if(DlContrDS.UseSubAcc == SET_CHAR)
              var QuerySC = "select SubContr.t_ID, ServKind.t_Name as ServKind, ServKSub.t_Name as ServKindSub" +
                           " from dsfcontr_dbt SubContr, ddlcontr_dbt dlcontr, dsfcontr_dbt sfcontr, ddlcontrmp_dbt dlcontrmp, dservkind_dbt ServKind, dservksub_dbt servksub" +
                           " where DLCONTR.T_SFCONTRID = sfcontr.t_ID" +
                           "     and DLCONTRMP.T_DLCONTRID = DLCONTR.T_DLCONTRID" +
                           "     and SubContr.t_ID = DLCONTRMP.T_SFCONTRID" +
                           "     and DLCONTR.T_DLCONTRID = ?" +
                           "     and SubContr.t_DateClose = " + sqlDateToStr(date(0, 0, 0)) +
                           "     and SubContr.t_ServKind in(" + PTSK_STOCKDL + ", " + PTSK_CURRDL + ", " + PTSK_DV + ")" +
                           "     and ServKind.t_ServiseKind = SubContr.t_ServKind" +
                           "     and ServKSub.t_ServiceKind = SubContr.t_ServKind" +
                           "     and ServKSub.t_ServiceKindSub = SubContr.t_ServKindSub" +
                           "     and ( " +
                           "            not exists( " +
                           "                         select 1 " +
                           "                         from dobjatcor_dbt objatcor " +
                           "                         where objatcor.t_ObjectType = " + string(OBJTYPE_SFCONTR) +
                           "                           and objatcor.t_Object = LPAD(to_char(SubContr.t_ID), 10, '0') " +
                           "                           and objatcor.t_GroupID = 3" +
                           "                           and ( " +
                           "                                  objatcor.t_AttrID = 1 " +
                           "                               ) " +
                           "                      ) " +
                           "            or exists( " +
                           "                        select 1 " +
                           "                        from dnptxop_dbt nptxop, dnptxobj_dbt obj, dnptxobdc_dbt obdc " +
                           "                        where obj.t_AnaliticKind6 = " + string(TXOBJ_KIND6020) +
                           "                          and ( " +
                           "                                 obj.t_Analitic6 = SubContr.t_ID " +
                           "                                 or obj.t_Analitic6 = sfcontr.t_ID " +
                           "                              ) " +
                           "                          and obdc.t_ObjID = obj.t_ObjID " +
                           "                          and nptxop.t_ID = obdc.t_DocID " +
                           "                          and nptxop.t_DocKind = " + string(DL_CALCNDFL) +
                           "                          and nptxop.t_SubKind_Operation = " + string(DL_TXBASECALC_OPTYPE_CLOSE_IIS) +
                           "                          and nptxop.t_Status > 0 " +
                           "                          and nptxop.t_OperDate between ? and ? " +
                           "                     ) " +
                           "         ) ";
 
             var SCRsdCommand = RSDCommand(QuerySC);
             SCRsdCommand.addParam("", RSDBP_IN, DlContrDS.DlContrID);
             SCRsdCommand.addParam("", RSDBP_IN, m_Dbeg);
             SCRsdCommand.addParam("", RSDBP_IN, m_Dend);
             SCRsdCommand.execute();
     
             var SubContrsDS = TRsbDataSet(SCRsdCommand);
             while(SubContrsDS.MoveNext())
                 QuerySfSSI = "select settacc.t_Account as t_Account, settacc.t_Chapter as t_Chapter, settacc.t_FIID as t_FIID" +
                              " from dsfssi_dbt sfssi, dsettacc_dbt settacc" +
                              " where SFSSI.T_OBJECTTYPE = " + OBJTYPE_SFCONTR +
                              "  and SFSSI.T_OBJECTID = lpad(?, 10, '0')" +
      IIF(not m_IsCurBrocAcc, "  and SFSSI.t_FIID = " + string(NATCUR), "") +
                              "  and SettAcc.t_SettAccID = SfSSI.t_SetAccID order by t_Account";

                 SfSSIRSDCommand = RSDCommand(QuerySfSSI);
                 SfSSIRSDCommand.addParam("", RSDBP_IN, SubContrsDS.ID);
                 SfSSIRSDCommand.execute();
 
                 var SfSSISubDS = TRsBDataSet(SfSSIRSDCommand);
                 while(SfSSISubDS.MoveNext())
                    ClearRecord(AccBuf);
                    GetAccount(SfSSISubDS.Chapter, SfSSISubDS.FIID, SfSSISubDS.Account, AccBuf);
                    var fininstr = TRecHandler("fininstr");
                    var Currency = "";
                    if(ПолучитьФинИн(SfSSISubDS.FIID, fininstr) == 0)
                        Currency = fininstr.rec.Ccy;
                    end;
                    if(IsFirstLine)
                        PrintTableHeader(IsIIS);
                        IsFirstLine = false;
                    end;
                    Rest = GetRestAccount(AccBuf, m_OperDate);
                    if(m_IsCurBrocAcc)
                        SmartConvertSum(RestRub, Rest, m_OperDate, SfSSISubDS.FIID, NATCUR);
                    end;
                    PrintOneBrocAccLine(IsIIS, DlContrDS.Number, SubContrsDS.ServKind, SubContrsDS.ServKindSub, SfSSISubDS.Account, Rest, Currency, RestRub);
                    CountData = CountData + 1;
                 end;
             end;
         else
             var QueryDlContrAcc = "select account.*, fininstr.t_Ccy as t_Currency, account.t_Code_Currency as t_FIID" +
                                   " from ddlcontracc_dbt dlcontracc, daccount_dbt account, dfininstr_dbt fininstr" +
                                   " where dlcontracc.t_DlContrID = ?" +
                                   "     and dlcontracc.t_MPID = 0 " + 
                                   "     and dlcontracc.t_AccountID > 0 " +
                                   "     and account.t_AccountID = dlcontracc.t_AccountID" +
                                   "     and account.t_Close_Date = " + sqlDateToStr(date(0, 0, 0)) +
           IIF(not m_IsCurBrocAcc, "     and account.t_Code_Currency = " + string(NATCUR), "") +
                                   "     and fininstr.t_FI_Kind = " + FIKIND_CURRENCY +
                                   "     and fininstr.t_FIID = account.t_Code_Currency" +
                                   " order by t_Account, t_Currency";

             var DlContrAccRsdCommand = RSDCommand(QueryDlContrAcc);
             DlContrAccRsdCommand.addParam("", RSDBP_IN, DlContrDS.DlContrID);
             DlContrAccRsdCommand.execute();
 
             var DlContrAccDS = TRsbDataSet(DlContrAccRsdCommand);
             while(DlContrAccDS.MoveNext())
                if(IsFirstLine)
                    //m_Dbeg = IIF(m_Data.KindIIS == 1, GetFirstDateIIS(m_Data.client), Date(1,1,m_Year));
                    PrintTableHeader(IsIIS);
                    IsFirstLine = false;
                end;
                Rest = GetRestAccount(DlContrAccDS, m_OperDate);
                if(m_IsCurBrocAcc)
                    SmartConvertSum(RestRub, Rest, m_OperDate, DlContrAccDS.FIID, NATCUR);
                end;
                PrintOneBrocAccLine(IsIIS, DLContrDS.Number, "", "", DlContrAccDS.Account, Rest, DlContrAccDS.Currency, RestRub);
                CountData = CountData + 1;
             end;
         end;

         Count = Count + 1;
     end;

     if(Count == 0)
         var QuerySfContrs = "select sfcontr.*, servkind.t_Name as t_ServiceKind, servksub.t_Name as t_ServiceKindSub" +
                            " from dsfcontr_dbt sfcontr, dservkind_dbt servkind, dservksub_dbt servksub" +
                            " where t_PartyID = ?" +
                            "   and t_ServKind in (" + PTSK_STOCKDL + ", " + PTSK_DV + ")" +
                 IIF(IsIIS, "   and rsi_npto.CheckContrIIS(sfcontr.t_ID) = 1", " and rsi_npto.CheckContrIIS(sfcontr.t_ID) <> 1") +
                            "   and servkind.t_ServiseKind = sfcontr.t_ServKind" +
                            "   and servksub.t_ServiceKind = sfcontr.t_ServKind" +
                            "   and servksub.t_ServiceKindSub = sfcontr.t_ServKindSub" +
                            " order by sfcontr.t_Number";

         var SfContrRsdCommand = RSDCommand(QuerySfContrs);
         SfContrRsdCommand.addParam("", RSDBP_IN, m_Data.Client);
         SfContrRsdCommand.execute();
         
         var SfContrDS = TRsbDataSet(SfContrRsdCommand);
         while(SfContrDS.MoveNext())
             QuerySfSSI = "select sfssi.*, account.*, fininstr.t_Ccy as t_Currency, settacc.t_FIID as FIID" +
                          " from dsfssi_dbt sfssi, dsettacc_dbt settacc, daccount_dbt account, dfininstr_dbt fininstr" +
                          " where SFSSI.T_OBJECTTYPE = " + OBJTYPE_SFCONTR +
                          "  and SFSSI.T_OBJECTID = lpad(?, 10, '0')" +
  IIF(not m_IsCurBrocAcc, "  and SFSSI.t_FIID = " + string(NATCUR), "") +
                          "  and settacc.t_SettAccID = SFSSI.t_SetAccID" +
                          "  and account.t_Chapter = settacc.t_Chapter" +
                          "  and account.t_Account = settacc.t_Account" +
                          "  and account.t_Code_Currency = settacc.t_FIID" +
                          "  and fininstr.t_FI_Kind = " + FIKIND_CURRENCY +
                          "  and fininstr.t_FIID = settacc.t_FIID" +
                          " order by account.t_Account";

             SfSSIRSDCommand = RSDCommand(QuerySfSSI);
             SfSSIRSDCommand.addParam("", RSDBP_IN, SfContrDS.ID);
             SfSSIRSDCommand.execute();
 
             var SfSSIDS = TRsBDataSet(SfSSIRSDCommand);
             while(SfSSIDS.MoveNext())
                if(IsFirstLine)
                    PrintTableHeader(IsIIS);
                    IsFirstLine = false;
                end;
                Rest = GetRestAccount(SfSSIDS, m_OperDate);
                if(m_IsCurBrocAcc)
                    SmartConvertSum(RestRub, Rest, m_OperDate, SfSSIDS.FIID, NATCUR);
                end;
                PrintOneBrocAccLine(IsIIS, SfContrDS.Number, SfContrDS.ServiceKind, SfContrDS.ServiceKindSub, SfSSIDS.Account, Rest, SfSSIDS.Currency, RestRub);
                CountData = CountData + 1;
             end;
         end;
     end;

     if(CountData > 0)
         if(IsIIS)
            НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ_ИИС = НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ_ИИС + CountData + 6;
         else
            НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ = НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ + CountData + 6;
         end;

         EndTable();
         CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);
     end;
  end;

  PRIVATE MACRO PrintOneSheet(IsIIS)
    var SheetName = IIF(IsIIS, "Счета ИИС", "Счета");
    var prefix = IIF(IsIIS, "N3_", "N2_");
    var clientNum = 0;
    var RetParmArr = TArray();
    var i = 0;

    НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ_ИИС = 10;
    НОМЕР_НАЧАЛЬНОЙ_СТРОКИ_СУММ = 10;
    
    SetActiveSheet(SheetName);
    m_Query = GetQuery(IsIIS, not IsIIS, RetParmArr);
    var Sql1 = DL_RSDCommand(m_Query);

    if(RetParmArr.size > 0)
      i = 0;
      while(i < RetParmArr.size)
        Sql1.AddParam(RetParmArr[i]);
        i = i + 1;
      end;
    end;

    m_Data = Sql1.execute();

    PrintAccRepHeader(IsIIS);
    while(m_Data.MoveNext())
        m_Dbeg = IIF(m_Data.KindIIS == 1, GetFirstDateIIS(m_Data.client), Date(1,1,m_Year));
        if(this.DueTotal() > 0)
            PrintOneClientTable(IsIIS);
            UseProgress(clientNum = clientNum + 1);
        end;
    end;

    AddStandardFooter(prefix);

    CopyAllSheetInTotalBook(SheetName, false, prefix + "Footer", 0, SheetName);

    RemProgress();
  end;

  MACRO Clear()
     m_PaidSpecial      = null;
     m_PaidSpecial_0    = null;
     m_Dg               = null;
     m_Dp               = null;
     m_Ds               = null;
     m_Dm               = null;
     m_Pg               = null;
     m_Pp               = null;
     m_Ps               = null;
     m_Pm               = null;

     m_TaxGeneral        = null;
     m_TaxGeneral15      = null;
     m_PaidGeneral       = null;
     m_PaidGeneral15     = null;
     m_DEPOTaxGeneral    = null;
     m_DEPOTaxGeneral15  = null;
     m_DEPOPaidGeneral   = null; 
     m_DEPOPaidGeneral15 = null;

  END;

   PRIVATE MACRO ExecuteReport()
    var Num = 0;
    var Query, Sql;
    var RetParmArr = TArray();
    var i = 0;

    m_Query = GetQuery(true, true, RetParmArr);
    Sql = DL_RSDCommand(m_Query);

    if(RetParmArr.size > 0)
      i = 0;
      while(i < RetParmArr.size)
        Sql.AddParam(RetParmArr[i]);
        i = i + 1;
      end;
    end;

    m_Data = Sql.execute();

    InitProgress( 3, "Отчет: Контроль задолженности", "Просмотр НДР по клиентам" );

    InitProgress( m_Nrec, "Отчёт: Контроль задолженности", "Печать вкладки \"Контроль\"" );
    if( m_Nrec )
       PrintRepHeader();
       RegistrTableCtrl();
    end;

    var stat = m_Data.MoveNext();
    while(stat)
       Clear();
       // для ИИС начальную дату приходится переопределять
        m_Dbeg = IIF(m_Data.KindIIS == 1, GetFirstDateIIS(m_Data.client), Date(1,1,m_Year));

        LoadLineDataByClnt();

       if( (m_IsActiveClient == "") or
           ((m_IsActiveClient == "X") and (this.ExistsClntNDR() == true))
         )
          SetDataExists(m_Data.client);
          PrintLineByClnt();
       end;
       UseProgress( Num = Num + 1 );
       stat = m_Data.MoveNext();
    end;

    EndTable(); //В этот момент сработает ф-ция AfterAutoFill

    CopyAllSheetInTotalBook("Контроль", false, GetTableRange(), 0, "Контроль");

    AddStandardFooter();

    CopyAllSheetInTotalBook("Контроль", false, "N1_Footer", 0, "Контроль");

    RemProgress();

    if (m_ListWithAcc)
      UseProgress(1);
      InitProgress(m_Nrec, "Отчет: Контроль задолженности", "Печать вкладки \"Счета\"");
      PrintOneSheet(false);
      RemProgress();
      UseProgress(2);
  
      InitProgress(m_Nrec, "Отчет: Контроль задолженности", "Печать вкладки \"Счета ИИС\"");
      PrintOneSheet(true);
      RemProgress();
      UseProgress(3);
    end;

    RemProgress();
  END;

  PRIVATE MACRO GetDataForRep()
    var NRec, NRecData;
    var RetParmArr = TArray();
    var i = 0;

    m_Nrec = 0;

    m_Query = GetQuery(true, true, RetParmArr);
    NRec = DL_RSDCommand(m_Query);

    if(RetParmArr.size > 0)
      i = 0;
      while(i < RetParmArr.size)
        NRec.AddParam(RetParmArr[i]);
        i = i + 1;
      end;
    end;

    m_NRec = NRec.GetCount();

    return m_Nrec;
  END;

  private macro GetStr(val)
    var Type = ValType( val );

    if( Type == V_STRING )
      return Trim(val);
    elif( ( Type == V_DOUBLE ) or ( Type == V_MONEY ) )
      return string(bigint(val));
    else
      return "";
    end;
  end;

  private macro DelClientFromList(ClientID)
    execSql ("delete from DNPTXCTRLCLNT_TMP where T_CLIENTID = " +ClientID+ " ");
  end;

  private macro CheckClientList()
    var query = 
     " SELECT T_CLIENTID " +
     "    FROM DNPTXCTRLCLNT_TMP ";
    var cmd = DL_RSDCommand(query);
    var ds = cmd.Execute();
    while (ds.MoveNext())
      if (not CheckSofrId(ds.CLIENTID))
        PrintProtLine(ds.CLIENTID, "", "Недопустимая форма субъекта");
       DelClientFromList(ds.CLIENTID);
      elif(not CheckServIsGood(ds.CLIENTID))
        PrintProtLine(ds.CLIENTID, "", "Недопустимый вид обслуживания");
        DelClientFromList(ds.CLIENTID);
      end;
    end;
  end;

  private macro FillErrorsNoData()
    var query = 
     " SELECT T_CLIENTID " +
     "    FROM DNPTXCTRLCLNT_TMP "+
     "  WHERE T_DATAEXISTS is null or T_DATAEXISTS <> CHR(88)";
    var cmd = DL_RSDCommand(query);
    var ds = cmd.Execute();
    while (ds.MoveNext())
      PrintProtLine(ds.CLIENTID, "", "Не подходит под условия отбора");
      DelClientFromList(ds.CLIENTID);
    end;
  end;

  private macro LoadClientListFromFile()
    var objExcel = CExcelPoi();
    var resultFilePath = m_FilePath;
    var fileName, fileExt;
    SplitFile( resultFilePath, fileName, fileExt );
    if (SubStr( resultFilePath, 1, 1 ) == "$")
      var toPath = PathCombine(GetAbsoluteTxtPath(),GetExlusiveFileName(SubStr( fileExt, 2)));
      if( not CopyFile(resultFilePath, toPath) )
        RunError( "Ошибка при копировании файла",  CustomError("Ошибка при копировании файла " + resultFilePath + " в " + toPath ));
      end;
      resultFilePath = toPath;
    end;
    if (not objExcel.open(resultFilePath))
      RunError("Не удалось открыть excel файл", CustomError("Не удалось открыть excel файл по пути " + resultFilePath));
    end;
    execSql ("delete from DNPTXCTRLCLNT_TMP");
    var curRow = 0;
    var iLastRow = objExcel.GetLastRowNum()+1;
    while ((curRow=curRow+1) <= iLastRow)
      var clientIdStr = "";
      var excelValue = GetStr(objExcel.CellValue("A"+string(curRow)));
      var dotIdx = Index(excelValue, ".");
      if (dotIdx > 0)
        excelValue = SubStr(excelValue, 1, dotIdx-1);
      end;
      if (excelValue == "")
        continue;
      end;
      if (m_FileContentType == FILE_CONTENT_TYPE_SOFR)
        clientIdStr = trim(excelValue);
        if(strLen(clientIdStr) > 8)
          PrintProtLine(excelValue, "", "Некорректный идентификатор СОФР");
          continue;
        end;
        if(not CheckSofrIdExists(clientIdStr))
          PrintProtLine(excelValue, "", "Неверный ID");
          continue;
        end;
      elif (m_FileContentType == FILE_CONTENT_TYPE_CFT)
        var sofrId = GetSofrIDFromCftId(trim(excelValue));
        if (sofrId == 0)
          PrintProtLine(0, excelValue, "Указан несуществующий ID");
          continue;
        else
          clientIdStr = trim(string(sofrId));
        end;
      end;
      execSql ("insert into DNPTXCTRLCLNT_TMP (T_CLIENTID) values ("+clientIdStr+")");
    end;
    objExcel = null;
  end;

  PRIVATE MACRO Create()
    //грузим excel
    if (m_IsLoadFromFile)
      LoadClientListFromFile();
    end;

    CheckClientList();

    if( GetDataForRep() )
      ExecuteReport();
    else
    /*если данных вообще нет*/
      PrintRepHeader();
      RegistrTableCtrl();
      PrintLineNoData();
      EndTable();
      CopyAllSheetInTotalBook("Контроль", false, GetTableRange(), 0, "Контроль");
      
      AddStandardFooter();
    end;

    FillErrorsNoData();
  END;

  MACRO GetSofrID():string
     return m_Data.Client;
  END;

  MACRO CftID():string
     return m_Data.cftid + " ";
  END;

  MACRO Client():string
     return m_Data.ShortName;
  END;

  macro IISNumber():string
     return m_Data.IISNumber;
  end;

  MACRO ClientStatus():string
     var v_ClientRec = TRecHandler( "party.dbt" );
     var NumInList = "", v_ClientStatus = "";

     if( not ПолучитьСубъекта(m_Data.Client,v_ClientRec) AND 
         GetMainObjAttr(null, OBJTYPE_PARTY, UniID(v_ClientRec, OBJTYPE_PARTY), 42/*Является плательщиком НДФЛ*/, null, null, NumInList, m_OperDate) )
       if(( NumInList == "1" ) or ( NumInList == "7" ))
          v_ClientStatus = CL_STATE_RES;
       else
          v_ClientStatus = CL_STATE_NOTRES;
       end;
     end;

     if( (v_ClientStatus == "") and (v_ClientRec.rec.PartyID > 0) )
       if( v_ClientRec.rec.NotResident == SET_CHAR )
          v_ClientStatus = CL_STATE_NOTRES; //для нерезидента
       else
          v_ClientStatus = CL_STATE_RES; //для резидента
       end;
     end;

     return v_ClientStatus;
  END;

  MACRO RateGeneral():money
     if( this.ClientStatus() == CL_STATE_RES )
        return NPTXGENTAXRATE_RESIDENT;
     elif( this.ClientStatus() == CL_STATE_NOTRES )
        var _stat = 1;
        var _TaxRate;
        if (m_Dend != NULL)
          _stat = ПолучитьЗначениеПримечанияНаДату(m_Data.Client, PARTY_NOTE_KIND_NPTX_RATE_NOTRES, m_Dend, @_TaxRate);
        end;
        if (_stat != 0)
          _TaxRate = NPTXGENTAXRATE_NOTRESIDENT; //для нерезидента      
        elif (_TaxRate != NULL)
          _TaxRate = _TaxRate / 100;//примечание вернёт целое число процентов. Нам же необходим коэффициент
        end;
     
        return _TaxRate;
     end;
     return 0;
  END;

  MACRO RateSpecial():money
     if( this.ClientStatus() == CL_STATE_RES )
        return NPTXPARTTAXRATE_RESIDENT;
     elif( this.ClientStatus() == CL_STATE_NOTRES )
        if(m_Data.RateSpecial > 0)
           return m_Data.RateSpecial;
        else
           return NPTXPARTTAXRATE_NOTRESIDENT;
        end;
     end;
     return 0;
  END;


  MACRO BaseSpecial():money
     var v_BaseSpecial = $0;
     DL_GetNPTXOBJSumByClient(m_Data.Client,
                              string(TXOBJ_BASESPECIAL),
                              m_Dbeg,
                              m_Dend,
                              null,
                              null,
                              @v_BaseSpecial,
                              null,
                              null,
                              null,
                              IIF(m_Data.KindIIS == 1, " obj.t_Analitic6 = " + m_Data.ContrID, "")
                             );
     return v_BaseSpecial;
  END;

  MACRO BaseSpecialIIS():money
     var v_BaseSpecialIIS = $0;
     DL_GetNPTXOBJSumByClient(m_Data.Client,
                              string(TXOBJ_BASESPECIAL_IIS),
                              m_Dbeg,
                              m_Dend,
                              null,
                              null,
                              @v_BaseSpecialIIS,
                              null,
                              null,
                              null,
                              IIF(m_Data.KindIIS == 1, " obj.t_Analitic6 = " + m_Data.ContrID, "")
                             );
     return v_BaseSpecialIIS;
  END;

  /*НДР с кодом Coup0_Group, Exp_Group, Plus_Sec0 за период с 1 января года <Year> по <OperDate> включительно*/
  MACRO TaxSpecial0():money
     var v_TaxSpecial0 = $0;
     var objstr = string(TXOBJ_COUP0_GROUP)+", "+string(TXOBJ_EXP_GROUP)+", "+string(TXOBJ_PLUS_SEC0)+", "+string(TXOBJ_MAINB_0)+", "+string(TXOBJ_MAINS_0)+", "+string(TXOBJ_COMB_0)
                  +", "+string(TXOBJ_COMS_0)+", "+string(TXOBJ_MAINB_TS_0)+", "+string(TXOBJ_MAINS_TS_0)+", "+string(TXOBJ_COMB_TS_0)+", "+string(TXOBJ_COMS_TS_0);

     DL_GetNPTXOBJSumByClient(m_Data.Client,
                              objstr,
                              m_Dbeg,
                              m_Dend,
                              null,
                              null,
                              @v_TaxSpecial0,
                              null,
                              m_Data.KindIIS,
                              null,
                              IIF(m_Data.KindIIS == 1, " obj.t_Analitic6 = " + m_Data.ContrID, "")
                             );

     return v_TaxSpecial0;
  END;

  PRIVATE MACRO ЗагрузитьЗначенияСОФР()
    var query, cmd, DataSet;

    if(m_FromSOFR == UNSET_CHAR)
      m_TaxGeneral    = $0;
      m_TaxGeneral15  = $0;
      m_PaidGeneral   = $0;
      m_PaidGeneral15 = $0;
    elif(m_TaxGeneral == NULL)
      var Year = 0;
      var TaxBaseKind;

      m_TaxGeneral    = $0;
      m_TaxGeneral15  = $0;
      m_PaidGeneral   = $0; 
      m_PaidGeneral15 = $0;

      if(m_Data.KindIIS == 0)
        TaxBaseKind = NPTXTOTALBASE_TAXBASEKIND_ONB + ", " + NPTXTOTALBASE_TAXBASEKIND_BROK + ", 0";
      else
        TaxBaseKind = NPTXTOTALBASE_TAXBASEKIND_IIS;
      end;

      datesplit(m_Dend, null, null, Year);

      query =   " select t_RateHoldPITax, "
              + "        SUM(t_TaxBaseCurrPay) as SumTaxBaseCurrPay, "
              + "        SUM(t_HoldPITax) as SumHoldPITax "
              + "   from dnptxtotalbase_dbt "
              + "  where t_ClientID = ? "
              + "    and t_TaxPeriod = ? "
              + "    and t_StorState = " + NPTXTOTALBASE_STORSTATE_ACTIVE
              + "    and t_TaxBaseKind IN ("+TaxBaseKind+") "
              + "  group by t_RateHoldPITax";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(m_Data.Client);
      cmd.AddParam(Year);

      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        if((DataSet.RateHoldPITax == int(NPTXGENTAXRATE_RESIDENT*100)) or (DataSet.RateHoldPITax == int(NPTXGENTAXRATE_NOTRESIDENT*100)))
          m_TaxGeneral  = m_TaxGeneral + DataSet.SumTaxBaseCurrPay;
          m_PaidGeneral = m_PaidGeneral + DataSet.SumHoldPITax;
        else
          m_TaxGeneral15  = m_TaxGeneral15 + DataSet.SumTaxBaseCurrPay;
          m_PaidGeneral15 = m_PaidGeneral15 + DataSet.SumHoldPITax;
        end;
      end;
    end;
  END;

  PRIVATE MACRO ЗагрузитьЗначенияДЕПО()
    var query, cmd, DataSet;
    
    if(m_FromDEPO == UNSET_CHAR)
      m_DEPOTaxGeneral    = $0;
      m_DEPOTaxGeneral15  = $0;
      m_DEPOPaidGeneral   = $0; 
      m_DEPOPaidGeneral15 = $0;

    elif(m_DEPOTaxGeneral == NULL)
      var Year = 0;
      var TaxBaseKind;

      m_DEPOTaxGeneral    = $0;
      m_DEPOTaxGeneral15  = $0;
      m_DEPOPaidGeneral   = $0; 
      m_DEPOPaidGeneral15 = $0;

      if(m_Data.KindIIS == 0)
        TaxBaseKind = NPTXTOTALBASE_TAXBASEKIND_ONB + ", " + NPTXTOTALBASE_TAXBASEKIND_BROK + ", 0";
      else
        TaxBaseKind = NPTXTOTALBASE_TAXBASEKIND_IIS;
      end;

      datesplit(m_Dend, null, null, Year);

      query =   " select t_NdflKeepRate, "
              + "        SUM(t_NOBAmount) as SumNOBAmount, "
              + "        SUM(t_NdflKeepAmount) as SumNdflKeepAmount "
              + "   from dnptxtbext_dbt "
              + "  where t_ClientID = ? "
              + "    and t_TaxPeriod = ? "
              + "    and t_TaxBaseKind IN ("+TaxBaseKind+") "
              + "  group by t_NdflKeepRate";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(m_Data.Client);
      cmd.AddParam(Year);

      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        if((DataSet.NdflKeepRate == int(NPTXGENTAXRATE_RESIDENT*100)) or (DataSet.NdflKeepRate == int(NPTXGENTAXRATE_NOTRESIDENT*100)))
          m_DEPOTaxGeneral  = m_DEPOTaxGeneral + DataSet.SumNOBAmount;
          m_DEPOPaidGeneral = m_DEPOPaidGeneral + DataSet.SumNdflKeepAmount;
        else
          m_DEPOTaxGeneral15  = m_DEPOTaxGeneral15 + DataSet.SumNOBAmount;
          m_DEPOPaidGeneral15 = m_DEPOPaidGeneral15 + DataSet.SumNdflKeepAmount;
        end;
      end;
    end;
  END;

  MACRO TaxGeneral():money
    ЗагрузитьЗначенияСОФР();
    return m_TaxGeneral;
  END;

  MACRO TaxGeneral15()
    ЗагрузитьЗначенияСОФР();
    return m_TaxGeneral15;
  END;

  MACRO DEPOTaxGeneral():money
    ЗагрузитьЗначенияДЕПО();
    return m_DEPOTaxGeneral;
  END;

  MACRO DEPOTaxGeneral15()
    ЗагрузитьЗначенияДЕПО();
    return m_DEPOTaxGeneral15;
  END;

  /*Доходы (нарастающим итогом с начала года), облагаемые по ставке 9 (15)%*/
  MACRO TaxSpecial():money
     var RetVal:money = $0;

     if( m_Data.KindIIS == 1 )
        RetVal = BaseSpecialIIS();
     else
        RetVal = BaseSpecial();
     end;

     return RetVal;
  END;

  MACRO PaidSpecial_0()
     if(m_PaidSpecial_0 == null)
        DL_GetNPTXObjSumByClientAndOper(m_Data.Client,
                                        string(TXOBJ_PAIDSPECIAL_0),
                                        m_Dbeg,
                                        m_Dend,
                                        NULL,
                                        NULL,
                                        @m_PaidSpecial_0,
                                        null,
                                        null,
                                        null,
                                        IIF(m_Data.KindIIS == 1, " obj.t_Analitic6 = " + m_Data.ContrID, "")
                                       );
     end;

     return m_PaidSpecial_0;
  END;

  MACRO PaidGeneral_0()
     var v_PaidGeneral_0 = $0;
  
     DL_GetNPTXObjSumByClientAndOper(m_Data.Client,
                                     string(TXOBJ_PAIDGENERAL_0),
                                     m_Dbeg,
                                     m_Dend,
                                     NULL,
                                     NULL,
                                     @v_PaidGeneral_0,
                                     null,
                                     null,
                                     null,
                                     IIF(m_Data.KindIIS == 1, " obj.t_Analitic6 = " + m_Data.ContrID, "")
                                    );

     return v_PaidGeneral_0;
  END;

  /*Рассчитанный налог - по ставке 9 (15)% */
  MACRO CalcSpecial():money
     return ROUND(this.RateSpecial() * TaxSpecial() - PaidSpecial_0(), 0);
  END;

  MACRO Rg()
     return RateGeneral();
  END;

  MACRO Rp()
     if(this.ClientStatus() == CL_STATE_RES)
        return NPTXGENTAXRATE_RESIDENT_15;
     end;

     return 0;
  END;

  MACRO PaidSpecial():money
     var Bs;
     if(m_Ps == NULL)
        Bs = TaxSpecial();
        
        GetTAXCalculatedDue (m_Data.Client, ClientStatus, m_Dbeg, m_Dend, m_Year, m_Data.KindIIS, Rg(), RateSpecial(), Rp, Bs, @m_Dg, @m_Ds, @m_Dm, @m_Dp, @m_Pg, @m_Pm, @m_Pp, @m_Ps, true);
     end;

     return m_Ps;
  END;

  /*<TaxGeneral>*<RateGeneral>*/
  MACRO CalcGeneral():money
     return ROUND(((TaxGeneral()+this.DEPOTaxGeneral())*Rg() - this.DEPOCalcGeneral()),0);
  END;

  MACRO CalcGeneral15():money
     return ROUND(((TaxGeneral15()+this.DEPOTaxGeneral15())*NPTXGENTAXRATE_RESIDENT_15 - this.DEPOCalcGeneral15()),0);
  END;

  MACRO DEPOCalcGeneral():money
     return ROUND(DEPOTaxGeneral()*Rg(),0);
  END;

  MACRO DEPOCalcGeneral15():money
     return ROUND(DEPOTaxGeneral15()*NPTXGENTAXRATE_RESIDENT_15,0);
  END;
  
  MACRO PaidGeneral():money
    ЗагрузитьЗначенияСОФР();
    return m_PaidGeneral;
  END;

  MACRO PaidGeneral15()
    ЗагрузитьЗначенияСОФР();
    return m_PaidGeneral15;
  END;

  MACRO DEPOPaidGeneral():money
    ЗагрузитьЗначенияДЕПО();
    return m_DEPOPaidGeneral;
  END;

  MACRO DEPOPaidGeneral15()
    ЗагрузитьЗначенияДЕПО();
    return m_DEPOPaidGeneral15;
  END;
 
  MACRO SOFRDueTotal():money
     return CalcSpecial() + CalcGeneral() + CalcGeneral15() - PaidSpecial() - PaidGeneral() - PaidGeneral15();
  END;

  MACRO DEPODueTotal():money
     return DEPOCalcGeneral() + DEPOCalcGeneral15() - DEPOPaidGeneral() - DEPOPaidGeneral15();
  END;

  MACRO DueTotal():money
     return SOFRDueTotal() + DEPODueTotal();
  END;

  MACRO ExistsClntNDR():bool
     var exists = false;

     if( (m_printTaxSpecial0 > 0) or
         (m_printTaxSpecial > 0) or
         (m_printTaxGeneral > 0) or
         (m_printTaxGeneral15 > 0) or
         (m_printDEPOTaxGeneral > 0) or
         (m_printDEPOTaxGeneral15 > 0) or
         (m_printCalcSpecial > 0) or
         (m_printCalcGeneral > 0) or
         (m_printCalcGeneral15 > 0) or
         (m_printDEPOCalcGeneral > 0) or
         (m_printDEPOCalcGeneral15 > 0) or
         (m_printPaidSpecial > 0) or
         (m_printPaidGeneral > 0) or
         (m_printPaidGeneral15 > 0) or
         (m_printDEPOPaidGeneral > 0) or
         (m_printDEPOPaidGeneral15 > 0) or
         (m_printSOFRDueTotal != 0) or
         (m_printDEPODueTotal != 0) or
         (m_printDueTotal != 0)
       )
        exists = true;
     end;
  
     return exists;
  END;  

  PRIVATE MACRO CReport_Show( NumCopy:INTEGER )
    var day, mon, year;
    var hour, min, sec;

    DateSplit(date(), day, mon, year);
    TimeSplit(time(), hour, min, sec);
    SetLastTotalBookName("Отчет_Контроль_задолженности_"+string(day:o:2)+string(mon:o:2)+string(year)+"_"+string(hour:o:2)+string(min:o:2));
    CReport_Show(NumCopy);
  end;

  initDL_CReportTemplate( NULL, "Report_nptxctrl_2025.xlsx", true, null, null, true, true ); 
  SetRowFlushCount(2000);  
END;


PRIVATE MACRO ReportRun()
  
  var Form = Sp_NPTXCTRL_Panel();

  while(Form.Run())
    var OperDate        = Form.m_EndDate;
    var Year            = Form.m_Year;
    var Dbeg            = Date(1,1,Year);
    var Dend            = min(OperDate,Date(31,12,Year));
    var IsCurBrocAcc    = Form.m_IsCurBrocAcc;
    var IsActiveClient  = Form.m_IsActiveClient;
    var FromSOFR        = Form.m_FromSOFR;
    var FromDEPO        = Form.m_FromDEPO;
    var ByChanges       = Form.m_ByChanges;
    var ChBegDate       = Form.m_ChBegDate;
    var ChEndDate       = Form.m_ChEndDate;
    var IsLoadFromFile  = Form.m_isLoadFromFile;
    var FilePath        = Form.m_filePath;
    var FileContentType = Form.m_fileContentType;
    var ListWithAcc     = Form.m_listWithAcc;

    var protObj = PoiSimpleRepo("Протокол ошибок при создании Отчета \"Контроль задолженности по НДФЛ\"",
                                 makeArray(
                                   DL_KeyPair("ID СОФР", 15),
                                   DL_KeyPair("ID ЦФТ", 15),
                                   DL_KeyPair("Ошибка", 50)
                                 ),
                                 true);
    var rep = Sp_NPTXCTRL_Report(OperDate, Year, Dbeg, Dend, IsCurBrocAcc, IsActiveClient, FromSOFR, FromDEPO, ByChanges, ChBegDate, ChEndDate, IsLoadFromFile, FilePath, FileContentType, ListWithAcc, protObj);
    rep.Run();
    rep = null;

    if (protObj.IsHasData())
      var d,m,y,hh,mm;
      DateSplit(date(),d,m,y);
      TimeSplit(time(),hh,mm);
      var fileName = "Протокол_Отчет_Контроль_задолженности_" + string(d:o:2, m:o:2, y:o:4) + "_" + string(hh:o:2, mm:o:2) + ".xlsx";
      var path = PathCombine(GetAbsoluteTxtPath(),fileName);
      protObj.SaveAs(path);
      MsgBox("Операция завершена. Создан Протокол ошибок " + path);
    end;
  end;
END;


ReportRun();
exit(1);