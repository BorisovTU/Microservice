/*
$Name:        spgenagr.mac
$Module:      Ценные бумаги
$Description: Макрос массовой привязки ГС к сделкам БО ЦБ.
*/
import "dv_categ.mac";

PRIVATE CLASS GenAgrRep()
  var NumDeal = "",
      Result  = "",
      Comment = "";
END;

PRIVATE VAR DealToGenAgrRep = TArray;

MACRO Привязать_к_ГС(DealID, GenAgrID)
  var cmd = "", rs = NULL, rsG = NULL, rsLeg1 = NULL, rsLeg2 = NULL,
      rep = NULL, DealCode = "", retVal = 0;

  if (GenAgrID > 0) 
    //
    // Привязка
    //
    rs = TRsbDataSet("select * " +
                     "from ddl_tick_dbt tick " +
                     "where tick.t_DealID = " + String(DealID));

    rsG = TRsbDataSet("select * " +
                      "from ddl_genagr_dbt gagr " +
                      "where gagr.t_GenAgrID = " + String(GenAgrID));

    rsLeg1 = TRsbDataSet("select * " +
                         "from ddl_leg_dbt leg " +
                         "where " +
                         "leg.t_LegKind = " + String(LEG_KIND_DL_TICK) + " " +
                         "and leg.t_DealID = " + String(DealID) + " " +
                         "and leg.t_LegID = 0 ");

    rsLeg2 = TRsbDataSet("select * " +
                         "from ddl_leg_dbt leg " +
                         "where " +
                         "leg.t_LegKind = " + String(LEG_KIND_DL_TICK_BACK) + 
                         " " +
                         "and leg.t_DealID = " + String(DealID) + " " +
                         "and leg.t_LegID = 0 ");

    if (not rs.MoveNext)
      rep = GenAgrRep;
      rep.NumDeal = "";
      rep.Result  = "не привязана";
      rep.Comment = "Сделка с ID = " + String(DealID) + " не найдена.";
    else
      DealCode = rs.DealCode;
    end;

    if ((rep == NULL) and (not rsG.MoveNext))
      rep = GenAgrRep;
      rep.NumDeal = "";
      rep.Result  = "не привязана";
      rep.Comment = "ГС с ID = " + String(GenAgrID) + " не найдено.";
    end;

    if ((rep == NULL) and 
        ((rs.DealStatus != 10) and (rs.DealStatus != 0)))
      rep = GenAgrRep;
      rep.NumDeal = rs.DealCode;
      rep.Result  = "не привязана";
      rep.Comment = "Сделка должна быть открыта или отложена.";
    end;

    if ((rep == NULL) and 
        ((rsG.Status != 0) and (rsG.Status != 10)))
      rep = GenAgrRep;
      rep.NumDeal = rs.DealCode;
      rep.Result  = "не привязана";
      rep.Comment = "ГС должно быть открыто или отложено.";
    end;

    if ((rep == NULL) and 
        ((rs.DealStatus == 10) and (rsG.Status != 10)))
      rep = GenAgrRep;
      rep.NumDeal = rs.DealCode;
      rep.Result  = "не привязана";
      rep.Comment = "Открытую сделку можно привязать только к открытому ГС.";
    end;

    if ((rep == NULL) and (rs.GenAgrID > 0))
      rep = GenAgrRep;
      rep.NumDeal = rs.DealCode;
      rep.Result  = "не привязана";
      rep.Comment = "Сделка с ID = " + String(DealID) + " уже привязана к ГС.";
    end;

    if ((rep == NULL) and (date(rs.DealDate) < date(rsG.Start)))
      rep = GenAgrRep;
      rep.NumDeal = rs.DealCode;
      rep.Result  = "не привязана";
      rep.Comment = "Дата сделки меньше даты начала ГС.";
    end;

    if (rsG.Date_End != zerodate)
      if ((rep == NULL) and 
          (date(rs.DealDate) > date(rsG.Date_End)))
        rep = GenAgrRep;
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не привязана";
        rep.Comment = "Дата сделки больше даты окончания ГС.";
      end;
    end;

    if ((rep == NULL) and (rs.PartyID != rsG.PartyID))
      rep = GenAgrRep;
      rep.NumDeal = rs.DealCode;
      rep.Result  = "не привязана";
      rep.Comment = "Контрагент по сделке не совпадает с контрагентом по ГС.";
    end;

    if ((rep == NULL) and (rs.BofficeKind != DL_SECURITYDOC))
      rep = GenAgrRep;
      rep.NumDeal = rs.DealCode;
      rep.Result  = "не привязана";
      rep.Comment = "ФИ сделки не соответствует ГС.";
    end;

    if ((rep == NULL) and
        (not IsREPO(getOperationGroup(rs.DealType))) and (not rsG.Can_Buy_Sale))
      rep = GenAgrRep;
      rep.NumDeal = rs.DealCode;
      rep.Result  = "не привязана";
      rep.Comment = "Тип сделки не соответствует ГС.";
    end;

    if ((rep == NULL) and
        (IsREPO(getOperationGroup(rs.DealType))) and (not rsG.Can_Repo))
      rep = GenAgrRep;
      rep.NumDeal = rs.DealCode;
      rep.Result  = "не привязана";
      rep.Comment = "Тип сделки не соответствует ГС.";
    end;

    if ((rep == NULL) and rsLeg1.MoveNext)
      if (((date(rsLeg1.Start) != zerodate) and 
           ((date(rsG.Start) > date(rsLeg1.Start)) or
            ((date(rsG.Date_End) != zerodate) 
             and (date(rsG.Date_End) < date(rsLeg1.Start))))) or
          ((date(rsLeg1.Maturity) != zerodate) and 
           ((date(rsG.Start) > date(rsLeg1.Maturity)) or
            ((date(rsG.Date_End) != zerodate) 
             and (date(rsG.Date_End) < date(rsLeg1.Maturity))))) or
          ((date(rsLeg1.Expiry) != zerodate) and 
           ((date(rsG.Start) > date(rsLeg1.Expiry)) or
            ((date(rsG.Date_End) != zerodate) 
             and (date(rsG.Date_End) < date(rsLeg1.Expiry))))))
        rep = GenAgrRep;
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не привязана";
        rep.Comment = 
          "Плановая дата расчетов не соответствует периоду действия ГС.";
      end;
      if ((rep == NULL) and (rsLeg1.Formula == 49))
        rep = GenAgrRep;
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не привязана";
        rep.Comment = "Тип расчетов не соответствует генеральному соглашению.";
      end;
    end;

    if ((rep == NULL) and rsLeg2.MoveNext)
      if (((date(rsLeg2.Start) != zerodate) and 
           ((date(rsG.Start) > date(rsLeg2.Start)) or
            ((date(rsG.Date_End) != zerodate) 
             and (date(rsG.Date_End) < date(rsLeg2.Start))))) or
          ((date(rsLeg2.Maturity) != zerodate) and 
           ((date(rsG.Start) > date(rsLeg2.Maturity)) or
            ((date(rsG.Date_End) != zerodate) 
             and (date(rsG.Date_End) < date(rsLeg2.Maturity))))) or
          ((date(rsLeg2.Expiry) != zerodate) and 
           ((date(rsG.Start) > date(rsLeg2.Expiry)) or
            ((date(rsG.Date_End) != zerodate) 
             and (date(rsG.Date_End) < date(rsLeg2.Expiry))))))
        rep = GenAgrRep;
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не привязана";
        rep.Comment = 
          "Плановая дата расчетов не соответствует периоду действия ГС.";
      end;
      if ((rep == NULL) and (rsLeg2.Formula == 49))
        rep = GenAgrRep;
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не привязана";
        rep.Comment = "Тип расчетов не соответствует генеральному соглашению.";
      end;
    end;

    if (rep == NULL)
      if (IsREPO(getOperationGroup(rs.DealType)))
        if (IsBuy(getOperationGroup(rs.DealType)))
          if ((rsLeg1.Formula == 51) or (rsLeg2.Formula == 52))
            rep = GenAgrRep;
          end;
        elif (IsSale(getOperationGroup(rs.DealType)))
          if ((rsLeg1.Formula == 52) or (rsLeg2.Formula == 51))
            rep = GenAgrRep;
          end;
        end;
      else
        if (IsBuy(getOperationGroup(rs.DealType)))
          if (rsLeg1.Formula == 51)
            rep = GenAgrRep;
          end;
        elif (IsSale(getOperationGroup(rs.DealType)))
          if (rsLeg1.Formula == 52)
            rep = GenAgrRep;
          end;
        end;
      end;
      if (rep != NULL)
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не привязана";
        rep.Comment = "Тип расчетов не соответствует генеральному соглашению.";
      end;
    end;

  else
    // 
    // Отвязка
    //
    rs = TRsbDataSet("select * " +
                     "from ddl_tick_dbt tick " +
                     "where tick.t_DealID = " + String(DealID));

    if (rs.MoveNext)
      DealCode = rs.DealCode;
      if (rs.DealStatus == DVDEAL_CLOSE)
        rep = GenAgrRep;
        rep.NumDeal = rs.DealCode;
        rep.Result  = "не отвязана";
        rep.Comment = "Нельзя отвязать закрытую сделку от ГС.";
      end;
    else
      rep = GenAgrRep;
      rep.NumDeal = "";
      rep.Result  = "не отвязана";
      rep.Comment = "Сделка с ID = " + String(DealID) + " не найдена.";
    end;

    if ((rep == NULL) and (rs.GenAgrID <= 0))
      rep = GenAgrRep;
      rep.NumDeal = rs.DealCode;
      rep.Result  = "не отвязана";
      rep.Comment = "Сделка с ID = " + String(DealID) + " не привязана к ГС.";
    end;

    if (rep == NULL)
      rs = TRsbDataSet("select * " +
                       "from ddl_genagr_dbt gagr " +
                       "where gagr.t_GenAgrID = " + String(GenAgrID));

      if (rs.MoveNext)
        if (rs.Status == 20)
          rep = GenAgrRep;
          rep.NumDeal = rs.DealCode;
          rep.Result  = "не отвязана";
          rep.Comment = "Нельза отвязать сделку от закрытого ГС.";
        end;
      else
         // все равно отвяжем, если ГС не нашли
      end;
    end;
  end;

  if (rep == NULL)
    cmd = "update ddl_tick_dbt tick set tick.t_GenAgrID = " + String(GenAgrID) + 
          " where tick.t_DealID = " + String(DealID);

    RsdCommand(cmd).Execute;

    rep = GenAgrRep;
    rep.NumDeal = DealCode;
    if (GenAgrID > 0)
      rep.Result  = "привязана";
      rep.Comment = "Сделка привязана к ГС.";
    else
      rep.Result  = "отвязана";
      rep.Comment = "Сделка отвязана от ГС.";
    end;
  else
    retVal = 1;
  end;

  DealToGenAgrRep[DealToGenAgrRep.size] = rep;

  return retVal;
END;

MACRO Отчет_Привязки_к_ГС(GenAgrID)
  var ReportFile = NULL, rs = NULL, idx = 0;

  file RepFile() txt write;

  if (DealToGenAgrRep.size == 0)
    ;//ничего не делаем
  elif (DealToGenAgrRep.size == 1)
    msgbox(DealToGenAgrRep[0].Comment);
  else
    open(RepFile, GetTxtFileName("spgenagr"));

    if (GenAgrID > 0)// привязка
      rs = TRsbDataSet("select * " +
                       "from ddl_genagrview_dbt gagr " +
                       "where gagr.t_GenAgrID = " + String(GenAgrID));
      rs.MoveNext;

      insert(RepFile, "Протокол привязки сделок к ГС №" + Trim(rs.Code)
                      + String(", контрагент ") + Trim(rs.Party));
    else // отвязка
      insert(RepFile, "Протокол отвязки сделок от ГС");
    end;

    insert(RepFile, "┌────────────────────┬────────────────┬────────────────────────────────────────────────────────────────────────────────┐");
    insert(RepFile, "│    Номер сделки    │   Результат    │                              Сообщение                                         │");
    insert(RepFile, "│                    │   процедуры    │                                                                                │");
    insert(RepFile, "├────────────────────┼────────────────┼────────────────────────────────────────────────────────────────────────────────┤");

    while(idx < DealToGenAgrRep.size)
      insert(RepFile, "│" + String(DealToGenAgrRep[idx].NumDeal:20) + "│" +
                            String(DealToGenAgrRep[idx].Result:16) + "│" +
                            String(DealToGenAgrRep[idx].Comment:80) + "│" );

      idx = idx + 1;
    end;

    insert(RepFile, "└────────────────────┴────────────────┴────────────────────────────────────────────────────────────────────────────────┘");

    close(RepFile);
    ViewFile(RepFile);
  end;

  DealToGenAgrRep.size = 0;

  return 0;
END;
