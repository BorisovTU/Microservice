/**                                                                                                             
 @file  WrtOffDebtSumToCFT.mac                                                                                           
 @brief Операция Инициации процесса списания задолженностей со счетов в ЦФТ                                                                         
                                                                                                                
 #menu 
  - БО ЦБ\Договоры\Договоры брокерского обслуживания\Инициация процесса списания задолженностей со счетов в ЦФТ                                                                         
                                                                                                                
 #tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:GUI 
 - code_type:API                                                                                                                                                                                               
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.10.14 |Жиц М.В.       |AVT_BOSS-714        |Создание макроса операции                                                                                                                                                 
*/ 

import "DebtSumOp_Common.mac", "WrtOffDebtSumToCFT_Form.mac", AutoProcess; 

private var RsltStr = TStrCollector;

/**
 @brief Сохранение и вывод протокола на экран 
 @param[out] errmsg   сообщение об ошибке
*/ 
private macro log2file(errmsg:@string)
  FILE output_file() txt write;
  var filename:string, FullNameFile:string;
  var dt = StrSubSt(string(date()),".","");
  var tm = StrSubst(StrSubSt(string(time()),":","")," ","0");
  var i:integer = 0;

  filename = "Протокол_инициации_списания_задолженности_в_ЦФТ_"+dt+"_"+tm+".txt";
  FullNameFile = GetTxtFileName(filename);

  if(not Open(output_file, FullNameFile))
    errmsg = "Ошибка при открытии файла отчета|" + FullNameFile;
  end;
  SetOutput(FullNameFile);

  while(i < RsltStr.Size)
    println(RsltStr(i));
    i = i + 1;
  end;

  SetOutput(null, true);
  Close(output_file);
  ViewFile(output_file); //Покажем файл отчёта на экран
end;

/**
 @brief Переключение функции вывода println на свою 
*/ 
private macro ResultCaptureStart
  RsltStr.ClearArray; 
  SetOutHandler("ResultCaptureLineArr");
end;

/**
 @brief Функция переопределения вывода println на добавление непустой строки в массив 
 @param[in]  str строка данных
*/ 
macro ResultCaptureLineArr(str)
  if((trim(str) != "") or (str == ""))
    RsltStr.AddString(trim(str));
  end;
end;

/**
 @brief Возвращение функции вывода println в обычный режим работы 
*/ 
private macro ResultCaptureStop
  SetOutHandler();
end;

/**
 @brief Получение даты последнего запуска процедуры из буферной таблицы
 @return дата последнего запуска процедуры  
*/
PRIVATE MACRO GetLastEnterDate():date
  var cmd, DataSet;
  cmd = DL_RSDCommand("SELECT t_EnterDate FROM duserdebttocft_dbt WHERE ROWNUM = 1 ");
  DataSet = cmd.Execute(); 

  if(DataSet.moveNext())
    return DataSet.EnterDate;
  end;
  return date(0,0,0);
END;

/**
 @brief Перенос предоставляемых задолженностей для ЦФТ и данных по оплаченным задолженностям в ЦФТ в историю  
*/
PRIVATE MACRO TransferCFTDataToHist()
  var cmd;
  cmd = RSDCommand("BEGIN RSB_DEBTSUM.TransferCFTDataToHist; END;"); 
  cmd.execute();
END;

/**
 @brief Подготовка данных о задолженности клиентов к списанию со счетов в ЦФТ с заполнением буферной таблицы 
 @param[in]  FormData класс параметров операции
*/
PRIVATE MACRO GetWrtOffDebtDataToCFT(FormData:WrtOffDebtSumToCFTParams)
  var cmd;
  cmd = RSDCommand("BEGIN RSB_DEBTSUM.GetWrtOffDebtDataToCFT(:p_ProcDate, :p_ClientID, :p_DlContrID); END;"); 
  cmd.addParam("", RSDBP_IN, FormData.ProcDate);
  cmd.addParam("", RSDBP_IN, FormData.ClientID);
  cmd.addParam("", RSDBP_IN, FormData.DlContrID);

  cmd.execute();
END;

/**
 @brief Подсчет количества обработанных записей/ошибок/предупреждений
 @param[in]  FormData класс параметров операции 
 @param[out] recs     счетчик записей (отправленных, с учетом группировки по клиентам)
 @param[out] allrecs  счетчик записей (всего добавленных задолженностей к отправке)
 @param[out] errrecs  счетчик ошибок
 @param[out] wrnrecs  счетчик предупреждений
 @param[out] errmsg   сообщение об ошибке 
*/
PRIVATE MACRO GetCountRecs(recs:@integer, allrecs:@integer, errrecs:@integer, wrnrecs:@integer, errmsg:@string)
  var query, cmd, DataSet; 
   
  //Получим кол-во успешно обработанных записей (отправленных, с учетом группировки по клиентам)
  cmd = DL_RSDCommand("SELECT count(1) t_cnt " +
                      "  FROM (SELECT 1 " +
                      "          FROM duserdebttocft_dbt " +
                      "         WHERE t_IsInWork = 'X' " +
                      "         GROUP BY t_DlContrID, t_DebtSumCur) ");   
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    recs = DataSet.cnt;
  end;

  //Получим кол-во успешно обработанных записей (всего добавленных задолженностей к отправке)
  cmd = DL_RSDCommand("SELECT count(1) t_cnt " +
                      "  FROM duserdebttocft_dbt " +
                      " WHERE t_IsInWork = 'X' ");   
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    allrecs = DataSet.cnt;
  end;

  //Получим кол-во ошибок 
  cmd = DL_RSDCommand("SELECT count(1) t_cnt " +
                      "  FROM ddl_debtsumlog_tmp " +
                      " WHERE t_Type = "+ISSUE_ERROR);   
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    errrecs = DataSet.cnt;
  end;

  //Получим кол-во предупреждений
  cmd = DL_RSDCommand("SELECT count(1) t_cnt " +
                      "  FROM ddl_debtsumlog_tmp " +
                      " WHERE t_Type = "+ISSUE_WARNING);   
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    wrnrecs = DataSet.cnt;
  end;

onError(erru)  
  errmsg = "Ошибка при подсчете количества обработанных записей: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
END;

/**
 @brief Формирование протокола операции
 @param[in]  FormData класс параметров операции 
 @param[in]  recs     счетчик записей (отправленных, с учетом группировки по клиентам)
 @param[in]  allrecs  счетчик записей (всего добавленных задолженностей к отправке)
 @param[in]  errrecs  счетчик ошибок
 @param[in]  wrnrecs  счетчик предупреждений
 @param[out] errmsg   сообщение об ошибке 
*/
PRIVATE MACRO FormLog(FormData:WrtOffDebtSumToCFTParams, recs:integer, allrecs:integer, errrecs:integer, wrnrecs:integer, errmsg:@string)
  var cmd, DataSet;
  var NRec:integer = 0, i:integer = 0;

  ResultCaptureStart();
  [Протокол Инициации процесса списания задолженностей со счетов в ЦФТ                                                
   Дата процедуры: ##########                                              
   ДБО: ####################
   Клиент: ############################################################
  ]
  (FormData.ProcDate:f, 
   IIF(FormData.DlContrID > -1, GetDLContrNumber(FormData.DlContrID), "Все"):l,
   IIF(FormData.ClientID > -1, DL_GetPartyShortName(FormData.ClientID), "Все"):l
  );

  println("");
  println("Добавлено задолженностей к отправке: " + allrecs);  
  println("Успешно отправлено: " + recs); 
  println("Ошибок: " + errrecs); 
  println("Предупреждений: " + wrnrecs); 
  println(""); 
 
  //Выводим подробную информацию об ошибках/предупреждениях, если они есть
  if((errrecs > 0) or (wrnrecs > 0))
    [Ошибки и предупреждения:
     ┌─────┬─────────────────────┬───────────────────────────────────────────────────────────────────────────────────────────────┐
     │ Тип │ ДБО                 │ Текст ошибки                                                                                  │
     ├─────┼─────────────────────┼───────────────────────────────────────────────────────────────────────────────────────────────┤
    ];
    
    cmd = DL_RSDCommand("SELECT decode(t_Type, "+ISSUE_ERROR+", 'E', "+ISSUE_WARNING+", 'W', '') t_TypeName, " + 
                        "       sf.t_Number, log.t_Text " +
                        "  FROM ddl_debtsumlog_tmp log, ddlcontr_dbt dl, dsfcontr_dbt sf " +
                        " WHERE sf.t_ID = dl.t_SfContrID " +
                        "   AND dl.t_DlContrID = log.t_DlContrID " +
                        " ORDER BY log.t_Type ");  
    NRec = cmd.GetCount();
 
    i = 1;
    DataSet = cmd.Execute(); 
    while(DataSet.moveNext())
      [│#####│#####################│###############################################################################################│]
      (DataSet.TypeName:c,
       DataSet.Number:l:w,
       DataSet.Text:l:w      
      );

      if(i < NRec)
        println("├─────┼─────────────────────┼───────────────────────────────────────────────────────────────────────────────────────────────┤");
        i = i + 1;
      end;
    end;

    println("└─────┴─────────────────────┴───────────────────────────────────────────────────────────────────────────────────────────────┘");
    println(""); 
  end;
  ResultCaptureStop(); 

  if(GetDialogFlag())
    log2file(@errmsg);
  end; 
END;

/**
 @brief Алгоритм работы операции Инициации процесса списания задолженностей со счетов в ЦФТ 
 @param[in]  FormData класс параметров операции
 @param[in]  result_protocol протокол
 @param[out] errmsg   строка ошибки
 @param[out] recs     счетчик записей
 @param[out] errrecs  счетчик ошибок
 @param[out] wrnrecs  счетчик предупреждений 
*/
MACRO CreateDebtListToCFT(FormData:WrtOffDebtSumToCFTParams, result_protocol:@TStrCollector, errmsg:@string, recs:@integer, errrecs:@integer, wrnrecs:@integer) 
  var lastEnterDate:date = GetLastEnterDate();
  var allrecs:integer = 0, lastRecs:integer = 0, lastAllRecs:integer = 0;

  recs = 0;
  errrecs = 0;
  wrnrecs = 0;
  errmsg = "";

  SQL_Execute("TRUNCATE TABLE DDL_DEBTSUMLOG_TMP"); //Очистим лог

  if(FormData.ProcDate > lastEnterDate)
    TransferCFTDataToHist();
  elif(FormData.ProcDate < lastEnterDate)
    errmsg = "Некорректная дата запуска процедуры. В таблице duserdebttocft_dbt есть данные за более позднюю дату";
  else
    GetCountRecs(@lastRecs, @lastAllRecs, null, null, @errmsg); //Посчитаем кол-во записей до нашего запуска, чтобы потом вычесть их из итогого значения
  end;

  if(errmsg == "")
    InitProgress(-1, "Подготовка данных о задолженности клиентов к списанию со счетов в ЦФТ", "Подготовка данных...");
    GetWrtOffDebtDataToCFT(FormData);
    RemProgress(); 

    InitProgress(-1, "Формирование XML файлов по данным о задолженности клиентов для последующей отправки в ЦФТ", "Отправка данных в ЦФТ...");
    SendDebtListToCFT(-1, -1);
    RemProgress(); 

    InitProgress(-1, "Формирование и печать протокола", "Формирование протокола...");
    GetCountRecs(@recs, @allrecs, @errrecs, @wrnrecs, @errmsg);
    recs = recs - lastRecs; allrecs = allrecs - lastAllRecs;
    FormLog(FormData, recs, allrecs, errrecs, wrnrecs, @errmsg); //Формирование, печать и сохранение протокола
    result_protocol = RsltStr;
    RemProgress();    
  end;

onError(erru)  
  errmsg = "Ошибка в процессе работы процедуры: " + erru.message + " (модуль " + erru.module + ", строка " + erru.line + ")";
  RemProgress(); 
END;