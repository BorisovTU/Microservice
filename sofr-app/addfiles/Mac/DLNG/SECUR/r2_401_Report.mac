/*******************************************************************************
 DESCRIPTION  :   Отчет "Расшифровка 401 Сделки ПФИ с ценными бумагами"
 PROGRAMMED BY:   Сагиян С.Г.
 CREATION DATE:    12.02.2019
*******************************************************************************/
IMPORT "spCache.mac", "r2_401_form.mac", "DLReport_h.mac", PtInter, "dlreport.mac", "cb_sql.mac", "sprepfun.mac", "oralib", "MoCommon.mac","ws_txreportform.mac";;
//import "sccomiss.mac", "spRepFun.mac"

PRIVATE VAR   T_Dep:T_Dep_Collector;

PRIVATE VAR   OD1_Form;
PRIVATE VAR   OD1_Report;
PRIVATE VAR   WebMenuItem; // Информация о запуске отчета из веб

PRIVATE VAR   NFinRez;
PRIVATE VAR   NNDFL;

PRIVATE VAR ReportHeader = " \n"; 
                                                                                                                                                                                                                                                                                                                             
PRIVATE VAR TableHeader = " \n"; 

                                                                                                                                    
PRIVATE CLASS (DL_CReportTemplate) SP_OD1_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  PRIVATE VAR F0_6, F0_9, F0_11; /*суммы строки "Итого"*/
  PRIVATE VAR m_TotalAmountPoint = 0;
  PRIVATE VAR v_account = "";
  var RS, RS1;
  VAR RepDate, BeginDate, EndDate;

  PRIVATE VAR v_BuyCost, v_BuyNKD, v_BuyTotalCost, v_SaleCost, v_SaleNKD, v_SaleTotalCost, v_ComS, v_ComB, v_FinRez;
  PRIVATE VAR v_s_BuyCost, v_s_BuyNKD, v_s_BuyTotalCost, v_s_SaleCost, v_s_SaleNKD, v_s_SaleTotalCost, v_s_ComS, v_s_ComB, v_s_FinRez;
  VAR v_Acc;
  VAR         ProgressValue = CTxProgressValue();

  var    v_oborot_plus = 0;
  var    v_oborot_minus = 0;



  PRIVATE VAR m_IsDataExist = false; /*для Web, если нет данных, то ничего не показываем*/
/* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsDataExist or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END;

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO PrintLine( P1,P2,P3,P4,P5,P6,P7,P8,P9,P10,P11,P12,P13,P14,P15,P16,P17,P18,P19,P20,P21,P22 )

     this.PrintTableLine( 

          /*1 */     "DealCode",      P1  , null, 
          /*2 */     "DealDir",       P2  , null, 
          /*3 */     "DealKind",      P3  , null, 
          /*4 */     "DealType",      P4  , null, 
          /*5 */     "FiKind",        P5  , null, 
          /*6 */     "Contractor",    P6  , null, 
          /*7 */     "ContrCou",      P7  , null, 
          /*8 */     "ContrType",     P8  , null, 
          /*9 */     "Emitent",       P9  , null,
          /*10*/     "ISIN",          P10 , null,
          /*11*/     "DealDate",      P11 , null,
          /*12*/     "DealClose",     P12 , null,
          /*13*/     "ValNom",        P13 , null,
          /*14*/     "Amount",        P14 , null,
          /*15*/     "Price",         P15 , null,
          /*16*/     "Cost",          P16 , null,
          /*17*/     "NKD",           P17 , null,
          /*18*/     "TotalCost",     P18 , null,
          /*19*/     "Bloom",         P19 , null,
          /*20*/     "sprav_pol",     P20 , null,
          /*21*/     "sprav_otr",     P21 , null,
          /*22*/     "Nominal",       P22 , null
       );      

  END;




  PRIVATE MACRO BeginReport()

     VAR AvrKindShowValue = "";

     this.SetActiveSheet( "Лист1" );
     
     VAR cmd1, rs1;


     cmd1 = RSDCommand("select to_char(d,'dd.mm.yyyy') begindate, to_char(d + interval '1' month - interval '1' day, 'dd.mm.yyyy') enddate from  (select  to_date( '01.'|| to_char( ? , 'mm.yyyy'), 'dd.mm.yyyy') d from dual)");
     cmd1.AddParam( "", RSDBP_IN, OD1_Form.GetFieldValue( PNFLD_OD1_DATE ) );	  
     RS1 = TRsbDataSet( cmd1 );
     RS1.MoveNext;
     BeginDate = RS1.BeginDate;
     EndDate = RS1.EndDate;


                               

     this.PrintFormatString( ReportHeader,
                     "H0_1"  , BeginDate + " - " + EndDate 
                   );


     this.RegisterTable( "MainTable", "",

          /*1 */     "DealCode",      
          /*2 */     "DealDir",       
          /*3 */     "DealKind",      
          /*4 */     "DealType",      
          /*5 */     "FiKind",        
          /*6 */     "Contractor",    
          /*7 */     "ContrCou",      
          /*8 */     "ContrType",     
          /*9 */     "Emitent",       
          /*10*/     "ISIN",          
          /*11*/     "DealDate",      
          /*12*/     "DealClose",     
          /*13*/     "ValNom",        
          /*14*/     "Amount",        
          /*15*/     "Price",         
          /*16*/     "Cost",          
          /*17*/     "NKD",           
          /*18*/     "TotalCost",     
          /*19*/     "Bloom",         
          /*20*/     "sprav_pol",     
          /*21*/     "sprav_otr",
          /*22*/     "Nominal"

       );      

  END;

  MACRO ПечататьПромежуточныеИтоги()
/*
     SetSubtotal(12, 11 + MAXROWSHEET,
                 "K8",    
                 "S8"
                 );
*/
  END;

  PRIVATE MACRO EndReport()
     this.EndTable();
     ПечататьПромежуточныеИтоги();
//     this.AddStandardFooter( "" );
  END;


  PRIVATE MACRO DealCode
	return rs.DealCode;
  END;

  PRIVATE MACRO DealDir
     return rs.DealDir;
  END;

  PRIVATE MACRO DealKind
	return "ПФИ";
  END;

  PRIVATE MACRO DealType
	return "внебиржевая";
  END;

  PRIVATE MACRO FiKind
	return "форвард";
  END;

  PRIVATE MACRO Contractor
	return rs.Contractor;
  END;

  PRIVATE MACRO ContrCou
	return rs.ContrCou;
  END;

  PRIVATE MACRO ContrType
     VAR cmd1, rs1;
     cmd1 = RSDCommand("select 1 from dpartyown_dbt where t_partyid=? and t_partykind=2");
     cmd1.AddParam( "", RSDBP_IN, rs.ContrPartyID );	  
     RS1 = TRsbDataSet( cmd1 );
     IF (RS1.MoveNext)
		return "Банк";
     else
		return "ЮЛ";
     end;
  END;

  PRIVATE MACRO Emitent
	return rs.Emitent;
  END;

  PRIVATE MACRO ISIN
	return rs.ISIN;
  END;

  PRIVATE MACRO DealDate
	return rs.DealDate;
  END;

  PRIVATE MACRO DealClose
	return rs.DealClose;
  END;

  PRIVATE MACRO ValNom
	return rs.ValNom;
  END;

  PRIVATE MACRO Amount
	return rs.Amount;
  END;

  PRIVATE MACRO Price
	return rs.Price;
  END;

  PRIVATE MACRO Cost
	return rs.Cost;
  END;

  PRIVATE MACRO NKD
	return rs.NKD;
  END;

  PRIVATE MACRO TotalCost
	return rs.TotalCost;
  END;

/* MAA: iS - 519124 */
  PRIVATE MACRO GetRate(fi, rtype, ddate)
     VAR cmd1, rs1, cmd2, rs2;
     cmd1 = RSDCommand("select t_rate/power(10,t_point) RetVal, t_sincedate RetDate from dratedef_dbt rh where t_otherfi = ? and t_type = ? and rh.t_sincedate <= ? ");

     cmd1.AddParam( "", RSDBP_IN, fi );	  
     cmd1.AddParam( "", RSDBP_IN, rtype );	  
     cmd1.AddParam( "", RSDBP_IN, ddate );	  

     RS1 = TRsbDataSet( cmd1 );
     IF ((RS1.MoveNext) and ( RS1.RetDate <= ddate))
		return rs1.RetVal;
     else
	     cmd2 = RSDCommand("select t_rate/power(10,t_point) RetVal from dratehist_dbt rh where rh.t_rateid in " +
                       " (select t_rateid from dratedef_Dbt where t_otherfi=? and t_type=?) " + 
                       " and  rh.t_sincedate = (select max(l.t_sincedate) from dratehist_dbt l where l.t_rateid = rh.t_rateid and l.t_sincedate <= ?) ");

	     cmd2.AddParam( "", RSDBP_IN, fi );	  
	     cmd2.AddParam( "", RSDBP_IN, rtype );	  
	     cmd2.AddParam( "", RSDBP_IN, ddate );	  
	     RS2 = TRsbDataSet( cmd2 );
	     IF (RS2.MoveNext)
		return rs2.RetVal;        
	     end;
	     return 0;
     end;
       
  END;

/* MAA: iS - 519124 */
  PRIVATE MACRO rtDate
        if (rs.DealClose > EndDate)
          return EndDate;
        else
          return rs.DealClose;
        end;
  END;

/* MAA: iS - 519124 */
  PRIVATE MACRO Bloom
	return GetRate(rs.fiid, 23, rtDate);
  END;

/* MAA: iS - 519124 */
  PRIVATE MACRO Nominal
	var cmd4, rs4;
        cmd4 = RSDCommand("SELECT rsb_fiinstr.FI_GetNominalOnDate(?, ?, 0) nmnl FROM dual");
        cmd4.AddParam( "", RSDBP_IN, rs.fiid);
        cmd4.AddParam( "", RSDBP_IN, rtDate);
        rs4 = TRsbDataSet( cmd4 );
        if (rs4.MoveNext)
          return rs4.nmnl;
        else
          return "";
        end;
  END;



  // Получение счетов категорий +ПФИ(1217) и -ПФИ(1218)
  macro GetAccPFI
	var query3, cmd3, rs3;

      //  обороты по проводкам..
 
      v_oborot_plus = 0;
      v_oborot_minus = 0;

      query3 = 
	"SELECT T.T_ACCOUNT_PAYER, T.T_ACCOUNT_RECEIVER, substr(T.T_ACCOUNT_PAYER,1 ,5) PAYER_BAL2, substr(T.T_ACCOUNT_RECEIVER,1 ,5) RECEIVER_BAL2,  substr(T.T_ACCOUNT_PAYER,1 ,3) PAYER_BAL1, substr(T.T_ACCOUNT_RECEIVER,1 ,3) RECEIVER_BAL1, t.t_fiid_receiver, t.t_sum_natcur summ, t_ground  FROM dacctrn_dbt t " +
	"WHERE ( (t.t_AccTrnID IN  (SELECT docs.t_AccTrnID FROM doprdocs_dbt docs, doproper_dbt opr WHERE     opr.t_DocKind = ? AND opr.t_DocumentID = LPAD (?, 34, chr(48)) AND docs.t_ID_Operation = opr.t_ID_Operation AND docs.t_DocKind = 1) " +
	"OR t.t_AccTrnID IN (SELECT grdoc.t_DocID FROM ddlgrdeal_dbt grdeal, ddlgrdoc_dbt grdoc WHERE   grdeal.t_DocKind = ? AND grdeal.t_DocID = ? AND grdoc.t_GrDealID = grdeal.t_ID AND grdoc.t_DocKind = 1))) " +
	"and ( substr(t_account_payer, 1, 3) = '526' or substr(t_account_receiver, 1, 3) = '526') and t_ground not like '%преми_ %'  and t_date_carry <= ?  order by t_date_carry desc" ;

      cmd3 = RSDCommand(query3);         

      cmd3.AddParam("", RSDBP_IN, int(rs.bo));
      cmd3.AddParam("", RSDBP_IN, int(rs.id));
      cmd3.AddParam("", RSDBP_IN, int(rs.bo));
      cmd3.AddParam("", RSDBP_IN, int(rs.id));
      cmd3.AddParam("", RSDBP_IN, date(EndDate));

      rs3 = TRsbDataSet( cmd3 );
      while (rs3.moveNext())

		if     ( (rs3.payer_bal1 == "526") AND (rs3.receiver_bal1 == "526") )
		       // нейтрализуем проводку урегулирования парных

		elif   ( ( rs3.payer_bal2 == "52602") AND (rs3.receiver_bal1 != "706") )   // Списание отрицательной переоценки при продаже
			v_oborot_plus = 0;
			v_oborot_minus = abs(rs3.summ);
			return;

		elif   ( ( rs3.payer_bal1 != "706" ) AND (rs3.receiver_bal2 == "52602") )   // Списание отрицательной переоценки при продаже
			v_oborot_plus = abs(rs3.summ);
			v_oborot_minus = 0;
			v_oborot_plus = 0;
			v_oborot_minus = abs(rs3.summ);
//msgbox(int(rs.id));
			return;

		elif   ( ( rs3.payer_bal1 != "706" ) AND (rs3.receiver_bal2 == "52601") )   // Списание положительной переоценки при покупке
			v_oborot_plus = abs(rs3.summ);
			v_oborot_minus = 0;
			return;

		elif   ( ( rs3.payer_bal2 == "52601") AND (rs3.receiver_bal1 != "706") )   // Списание отрицательной переоценки при покупке
			v_oborot_plus = abs(rs3.summ);
			v_oborot_minus = 0;
//msgbox(int(rs.id));
			return;
	
		elif   ( rs3.payer_bal2  == "52601" ) 
				v_oborot_plus = v_oborot_plus + abs(rs3.summ);
		elif   ( rs3.receiver_bal2 == "52602" ) 
				v_oborot_minus = v_oborot_minus + abs(rs3.summ);
		elif   ( rs3.payer_bal2	 == "52602" ) 
				v_oborot_minus = v_oborot_minus - abs(rs3.summ);
		elif   ( rs3.receiver_bal2 == "52601" ) 
				v_oborot_plus = v_oborot_plus - abs(rs3.summ);
		end;

      end; 

	//-- урегулирование парных
      if  (v_oborot_minus > v_oborot_plus)
		v_oborot_minus = v_oborot_minus - v_oborot_plus;
		v_oborot_plus = 0;
      else
		v_oborot_plus = v_oborot_plus - v_oborot_minus;
		v_oborot_minus = 0;
      end;
  end;





  PRIVATE MACRO sprav_pol

      return v_oborot_plus;

  END;

  PRIVATE MACRO sprav_otr
	      
      return v_oborot_minus;

  END;



               
  PRIVATE MACRO Create()

     VAR DataQuery, DataQuery1;
     VAR cmd, cmd1;
     VAR DealBuy;
     VAR AddWhere:string = "";

     VAR AvrFIID:integer, AvrKind:integer;       
     VAR QntySold:money = $0, SumBvpr:money = $0;
//   VAR NKD:money = $0;
     VAR BuyDealTime:time, BuyPrice:double = 0.0;
     VAR StrNumbDeal1, StrNumbDeal2;
     VAR IsBlock;
     VAR SUMM_SUM;
     var s1=0,s2=0,s3=0,s4=0,s5=0,s6=0;
            
//     OD1_Form.GetFieldValue( PNFLD_OD1_DATE, @RepDate );

     NFinRez = 0;
     NNDFL = 0;
                   

     DataQuery = "select to_number(coalesce(fis1.t_id,fis2.t_id,tick.t_dealid)) t_id, (case when coalesce(fis1.t_id,fis2.t_id) is null then 101 else 199 end) bo,    RSI_RSBCalendar.getWorkDayCount(tick.t_dealdate, tick.t_closedate) d1, tick.t_closedate-tick.t_dealdate d2,  tick.t_dealid dealid, emit.t_shortname emitent, contr.t_shortname contractor, cou.t_name contrcou, contr.t_partyid 	ContrPartyID, avr.t_ISIN ISIN, to_char(tick.t_dealdate,'dd.mm.yyyy') DealDate, to_char(tick.t_closedate,'dd.mm.yyyy') DealClose, ValNomT.t_iso_number ValNom, "
                 "tick.t_dealcode DealCode, decode(tick.t_dealtype, 3143, 'Покупка', 12183, 'Покупка', 12144, 'Покупка', 12143, 'Покупка', 2162, 'Покупка', 'Продажа') DealDir, decode(tick.t_dealtype, 3143, 1, 12183, 1, 12144, 1, 12143, 1, 2162, 1, 0) IsBuy, leg.t_principal Amount, leg.t_price Price, leg.t_cost Cost, leg.t_nkd NKD, leg.t_totalcost TotalCost, "
                 "fi.t_fiid fiid, 0 Bloom, 0 sprav_pol, 0 sprav_otr   "  // MAA: iS - 519124. Добавлен вывод fiid
                 " from ddl_tick_dbt tick, ddl_leg_dbt leg, dfininstr_dbt fi, davoiriss_dbt avr, dparty_dbt contr, dparty_dbt emit, davrkinds_dbt kinds, dcountry_dbt cou, dfininstr_dbt ValNomT, ddvndeal_dbt fis1, ddvdeal_dbt fis2  "
                 "where tick.t_dealid=leg.t_dealid and tick.t_pfi=fi.t_fiid and fi.t_fiid=avr.t_fiid and tick.t_partyid=contr.t_partyid and fi.t_issuer=emit.t_partyid  "
		 "and tick.t_dealcode = fis1.t_code(+) and tick.t_dealcode = fis2.t_code(+) AND ((fis1.t_id is not null) or (fis2.t_id is not null) or (tick.t_ispfi = chr(88))) " +
                 "and fi.t_avoirkind = kinds.t_avoirkind and contr.t_notresident<>chr(0) and kinds.t_fi_kind in (2,4) and RSI_RSBCalendar.getWorkDayCount(tick.t_dealdate, tick.t_closedate)-1 >= 3 and tick.t_clientid=-1 "
                 "and contr.t_nrcountry = cou.T_CODELAT3 AND ValNomT.t_fiid=FI.T_FACEVALUEFI and tick.t_dealdate <=  " + GetSQLDate( EndDate ) + " and tick.t_closedate >=  " + GetSQLDate( BeginDate ) + " order by dealid"; // Golovkin 14.07.2021 ID : 530613

     ProgressValue.Maximum = SQL_GetNRecs( DataQuery );
     ProgressValue.Current = 0;
     
     var ProgressIndicator = CreateProgressIndicator(m_IsWebService);
     
     if( m_IsWebService and (WebMenuItem != null) )
       var header = "Формирование отчета " + WebMenuItem.szNameItem;
       ProgressIndicator.Start(ProgressValue.Maximum, header, header);
     else
       ProgressIndicator.Start(ProgressValue.Maximum,"\"Расшифровка 401 Сделки ПФИ с ценными бумагами\"" , HTML_StSheetName);
     end;

     RS = TRsbDataSet( DataQuery );
     while( RS.MoveNext() )
          SUMM_SUM = 0;
		  GetAccPFI;

                  s1 = s1 + Amount;
		  s2 = s2 + Cost;
		  s3 = s3 + nkd;
		  s4 = s4 + TotalCost;
		  s5 = s5 + sprav_pol;
		  s6 = s6 + sprav_otr;

	          m_IsDataExist = true;
	          PrintLine( 
	          /*1 */     DealCode,      
	          /*2 */     DealDir,       
	          /*3 */     DealKind,      
	          /*4 */     DealType,      
	          /*5 */     FiKind,        
	          /*6 */     Contractor,    
	          /*7 */     ContrCou,      
	          /*8 */     ContrType,     
	          /*9 */     Emitent,       
	          /*10*/     ISIN,          
	          /*11*/     DealDate,      
	          /*12*/     DealClose,     
	          /*13*/     ValNom,        
	          /*14*/     Amount,        
	          /*15*/     Price,         
	          /*16*/     Cost,          
	          /*17*/     NKD,           
	          /*18*/     TotalCost,     
	          /*19*/     Bloom,         
	          /*20*/     sprav_pol,     
	          /*21*/     sprav_otr,
	          /*22*/     Nominal
		  );		

        ProgressValue.Current = ProgressValue.Current + 1;
        ProgressIndicator.Update(ProgressValue.Current,ProgressValue.Maximum);
     end;
     PrintLine( "","","","","","","","","","","","","",s1,"",s2,s3,s4,"",s5,s6,"");

     ProgressIndicator.Stop();

  END;
  
  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  OD1_Form = DL_OD1_Panel();

  stat = OD1_Form.Run();

  if( stat )
      OD1_Report = SP_OD1_Report( null, "Report_401_2.xls", null, IsWebService, ReportHashCode );
      OD1_Report.Run();

      Dl_CallInsertStat( DL_OUTREPORT_EXCEL );
  end;

  return FullReportFileNames;
END;
