/*
$Name:        BrokerRepRSHB_Form.mac
$Module:      Ценные бумаги
$Description: Отчет брокера РСХБ 
              Форма ввода параметров выпуска отчета.
*/

import "DlRepForm_h.mac", "BrokerRepRSHBNew_Report.mac";

private const PNFLD_BROKERREP_PERIODKIND    = 0,
              PNFLD_BROKERREP_BEGDATE       = 1,
              PNFLD_BROKERREP_ENDDATE       = 2,
              PNFLD_BROKERREP_CLNTCODE      = 3,
              PNFLD_BROKERREP_CLNTNAME      = 4,
              PNFLD_BROKERREP_CONTRACT      = 5,
              PNFLD_BROKERREP_CONTRACT_STATUS = 6,
              PNFLD_BROKERREP_PROCMARKET      = 7,
              PNFLD_BROKERREP_PROCOUTMARKET   = 8,
              PNFLD_BROKERREP_FIMOVEMENT      = 9,
              PNFLD_BROKERREP_NOTZEROREST     = 10,
              PNFLD_BROKERREP_SHOWPLANREST    = 11,
              PNFLD_BROKERREP_SHOWEMPTY       = 12,
              PNFLD_BROKERREP_APOSTROF        = 13,
              PNFLD_BROKERREP_PRINTMETHOD     = 14;

CONST H_PNFLD_PERIODKIND  = 100;
CONST H_PNFLD_CONTRACT    = 101;
CONST H_PNFLD_CONTRACT_STATUS = 102;
CONST H_PNFLD_NOTZEROREST     = 103;

//Методы печати
CONST PRINTMETHOD_PDF       = 1; //PDF
CONST PRINTMETHOD_EXCEL     = 2; //MS Excel
CONST PRINTMETHOD_EXCEL_PDF = 3; //MS Excel и pdf

private macro Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
end;

/* Обработчик клиента */
private macro FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party    = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var saveFldRealValue = FldRealValue;
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == -1))
      FldShowValue = "";
      pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_NAME, FldShowValue);
    else
      partcode.Clear();
      partcode.rec.PartyID  = FldRealValue;
      partcode.rec.CodeKind = 1;
      if (partcode.GetEQ())
       FldShowValue = partcode.rec.Code;
       if( ПолучитьСубъекта( FldRealValue, Party) == 0 )
         pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName );
       end;
      end;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    Party = TRecHandler("party.dbt");
    if( ListPT( Party, 1, "", PTLIST_STOCKDLCLIENT, PTCK_ALL) == true ) 
      FldRealValue = Party.rec.PartyID;
      partcode.Clear();
      partcode.rec.PartyID  = Party.rec.PartyID;
      partcode.rec.CodeKind = 1;
      if (partcode.GetEQ())
        FldShowValue = partcode.rec.Code;
      end;
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName );
    end;
  end;

  if (saveFldRealValue != FldRealValue)
    if (pThis.GetLinkedValue(H_PNFLD_CONTRACT) != 0)
      pThis.SetLinkedValue(H_PNFLD_CONTRACT, 0);
    end;
  end;

  return CM_DEFAULT;
end;

/* Листалка ДБО */
private macro ListDlContr( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, PartyID:@INTEGER, Department:integer, BegDate:date, StatusName:@string ):BOOL
  VAR Choice, i = 0, flag = false; 
  VAR Select;
  Select = "select sfc.t_id, sfc.t_DateBegin, sfc.t_DateClose, sfc.t_Number, prt1.t_ShortName ClientName, prt1.t_PartyID as t_PartyID, sfc.t_Department as t_Department, " + 
               "   (CASE WHEN sfc.t_DateClose = "+GetSQLDate(date(0,0,0))+" or sfc.t_DateClose > "+GetSQLDate(BegDate)+" THEN 'Открыт' ELSE 'Закрыт' END) as t_StatusName, " +
               "   dlc.t_IIS, dlc.t_DlContrID, " +
               "   (CASE WHEN dlc.t_UseSubAcc = 'X' THEN CHR(0) ELSE 'X' END) EDP "
               "  from ddlcontr_dbt dlc, dsfcontr_dbt sfc, dparty_dbt prt1 "+
               " where sfc.t_ID = dlc.t_SfContrID " +
               "   and prt1.t_PartyID = sfc.t_partyID " ;

  if( PartyID > 0 )
    Select = Select + " and sfc.T_PARTYID = " + PartyID;
  end;

  if( (Department != null) and (Department > 0) )
    Select = Select + "   and sfc.t_Department = "+string(Department)+
                      "   and sfc.t_Branch = 0";
  end;

  Choice = DL_Scroll(Select,
                     "Договора брокерского обслуживания", X, Y,
                     "t_Number","№ договора",
                     "ClientName","Клиент",
                     "t_DateBegin", "Дата открытия",
                     "t_DateClose", "Дата закрытия",
                     "t_StatusName","Статус",
                     "t_IIS","ИИС",
                     "EDP", "ЕДП"
                    );

  if( Choice != NULL )
    FldRealValue = Choice.Value("t_DlContrID" ); 
    FldShowValue = Choice.Value("t_Number"    );
    PartyID      = Choice.Value("t_PartyID"   );
    StatusName   = Choice.Value("t_StatusName");
  end;
end;

/* Обработчик договора */
private macro FldProc_Contract_BO_Dep( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientName = "";
  var ClientID = pThis.GetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE );
  var BegDate    = pThis.GetLinkedValue( DL_PNFLD_BEGINDATE );
  var StatusName = "";

  if (Cmd == DLG_INIT)
    if ((FldRealValue == null) or (FldRealValue == 0))
      FldShowValue = "";
      pThis.SetLinkedValue(H_PNFLD_CONTRACT_STATUS, StatusName);
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
    FldRealValue = 0;
    FldShowValue = "";
    pThis.SetLinkedValue(H_PNFLD_CONTRACT_STATUS, StatusName);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    ListDlContr( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @ClientID, {OperDprt}, BegDate, @StatusName );
    pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, ClientID);
    pThis.SetLinkedValue(H_PNFLD_CONTRACT_STATUS, StatusName);
  end;
  return CM_DEFAULT;
end;

private macro Period_GetInterval(PeriodCode, StartDate:date, BegDate:@date, EndDate:@date )
   var Month, Year;
   
   if(PeriodCode == SC_REPPERIODKIND_DAY) //День
     BegDate = EndDate = StartDate;
   elif(PeriodCode == SC_REPPERIODKIND_MONTH) //Месяц  
     DateSplit(StartDate, null, Month, Year );

     BegDate = Date( 1, Month, Year );
     EndDate = DateAfterCalenMonths( BegDate, 1) - 1;
   elif(PeriodCode == SC_REPPERIODKIND_QUARTER) //Квартал
     DateSplit(StartDate, null, Month, Year );
     
     BegDate = Date( 1, (int((Month-1)/3)*3 + 1), Year );
     EndDate = DateAfterCalenMonths( BegDate, 3 ) - 1;
   end;
end;

private macro GetContrStatusName(ContrID:integer, BegDate:date)
  var StatusName = "";

  if(ContrID > 0)
    var query =   " select (CASE WHEN sfc.t_DateClose = "+GetSQLDate(date(0,0,0))+" or sfc.t_DateClose > "+GetSQLDate(BegDate)+" THEN 'Открыт' ELSE 'Закрыт' END) as t_StatusName "
                + "   from dsfcontr_dbt sfc "
                + "  where sfc.t_ID = ? ";
    var Select = DL_RSDCommand(query);

    Select.AddParam(ContrID);

    var DataSet = Select.Execute();
    if(DataSet.moveNext())
      StatusName = DataSet.StatusName;
    end;
  end;

  return StatusName;
end;

/*Вид периода*/
MACRO FldProc_PeriodKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR BeginDate, EndDate, Month, Year, BegD, EndD;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = 1;
     end;     
    
     FldShowValue = DL_GetNameAlg( 3169, FldRealValue );

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     BeginDate = pThis.GetLinkedValue(DL_PNFLD_BEGINDATE);
     
     if (FldRealValue != SC_REPPERIODKIND_PERIOD)
       Period_GetInterval( FldRealValue, BeginDate, @BegD, @EndD );
       pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, BegD );
       pThis.SetLinkedValue( DL_PNFLD_ENDDATE,   EndD );

       pThis.SetLinkedValue(H_PNFLD_CONTRACT_STATUS, GetContrStatusName(pThis.GetLinkedValue(H_PNFLD_CONTRACT), BegD));           
     end;

     EnableFields( pThis.Data(), PNFLD_BROKERREP_ENDDATE );
     if(FldRealValue == SC_REPPERIODKIND_DAY)
       DisableFields( pThis.Data(), PNFLD_BROKERREP_ENDDATE );
     elif(FldRealValue == SC_REPPERIODKIND_QUARTER)
       pThis.SetLinkedValue( H_PNFLD_NOTZEROREST, SET_CHAR );
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 3169, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;

/*Начальная дата выпуска отчета*/
MACRO FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var SaveFldRealValue = FldRealValue;
  var sign = DL_FldProc_BeginDate(pThis, Cmd, Key, @FldShowValue, @FldRealValue);
  
  if(FldRealValue != SaveFldRealValue)
    if(pThis.GetLinkedValue(H_PNFLD_PERIODKIND) == SC_REPPERIODKIND_DAY)
      pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, FldRealValue );
      pThis.SetLinkedValue( DL_PNFLD_ENDDATE, FldRealValue );
    else
      pThis.SetLinkedValue( H_PNFLD_PERIODKIND, SC_REPPERIODKIND_PERIOD );
    end;

    pThis.SetLinkedValue(H_PNFLD_CONTRACT_STATUS, GetContrStatusName(pThis.GetLinkedValue(H_PNFLD_CONTRACT), FldRealValue));           
  end;

  return sign;
END;

/*Конечная дата выпуска отчета*/
MACRO FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var SaveFldRealValue = FldRealValue;
  var sign = DL_FldProc_EndDate(pThis, Cmd, Key, @FldShowValue, @FldRealValue);

  if(FldRealValue != SaveFldRealValue)
    pThis.SetLinkedValue( H_PNFLD_PERIODKIND, SC_REPPERIODKIND_PERIOD );
  end;

  return sign;
END;

/*Способ формирования отчета*/
MACRO FldProc_PrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == PRINTMETHOD_PDF )
        return "pdf - формат";
     elif( Id == PRINTMETHOD_EXCEL )
        return "MS Excel";
     elif( Id == PRINTMETHOD_EXCEL_PDF )
        return "MS Excel и pdf";
     end;
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = PRINTMETHOD_EXCEL_PDF;
     end;
     FldShowValue = Name(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(PRINTMETHOD_PDF),  PRINTMETHOD_PDF,
                    Name(PRINTMETHOD_EXCEL),PRINTMETHOD_EXCEL,
                    Name(PRINTMETHOD_EXCEL_PDF),PRINTMETHOD_EXCEL_PDF
                  );
  end;
  return CM_DEFAULT;
END;


/* ***************************************** */
/* Форма ввода параметров отчета             */
/* ***************************************** */
class (DL_CPanel) СBrokerRepRSHB_Panel()
  var RepParams = CBrokerRepRSHBParams();  // Способ формирования отчета
  
  private macro Init()
    AddFieldProc( 0, PNFLD_BROKERREP_PERIODKIND,      H_PNFLD_PERIODKIND,           @FldProc_PeriodKind,      RepParams.PeriodKind, 26, 9 );
    AddFieldProc( 0, PNFLD_BROKERREP_BEGDATE,         DL_PNFLD_BEGINDATE,           @FldProc_BeginDate,       RepParams.BegDate );
    AddFieldProc( 0, PNFLD_BROKERREP_ENDDATE,         DL_PNFLD_ENDDATE,             @FldProc_EndDate,         RepParams.EndDate );
    AddFieldProc( 0, PNFLD_BROKERREP_CLNTCODE,        DL_PNFLD_CLIENT_STOCKDL_CODE, @FldProc_Client,          RepParams.ClientID);
    AddFieldProc( 0, PNFLD_BROKERREP_CLNTNAME,        DL_PNFLD_CLIENT_STOCKDL_NAME, @Default,                 RepParams.ClientID);
    AddFieldProc( 0, PNFLD_BROKERREP_CONTRACT,        H_PNFLD_CONTRACT,             @FldProc_Contract_BO_Dep, RepParams.DlContrID );
    AddFieldProc( 0, PNFLD_BROKERREP_CONTRACT_STATUS, H_PNFLD_CONTRACT_STATUS,      @Default,                 "" );
    AddFieldProc( 0, PNFLD_BROKERREP_PROCMARKET,      DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,     SET_CHAR);
    AddFieldProc( 0, PNFLD_BROKERREP_PROCOUTMARKET,   DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,     SET_CHAR);
    AddFieldProc( 0, PNFLD_BROKERREP_FIMOVEMENT,      DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,     SET_CHAR);
    AddFieldProc( 0, PNFLD_BROKERREP_NOTZEROREST,     H_PNFLD_NOTZEROREST,          @DL_FldProc_CheckBox,     SET_CHAR);
    AddFieldProc( 0, PNFLD_BROKERREP_SHOWPLANREST,    DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,     SET_CHAR);
    AddFieldProc( 0, PNFLD_BROKERREP_SHOWEMPTY,       DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,     SET_CHAR);
    AddFieldProc( 0, PNFLD_BROKERREP_APOSTROF,        DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,     SET_CHAR);
    AddFieldProc( 0, PNFLD_BROKERREP_PRINTMETHOD,     DL_PNFLD_PRINTMETHOD,         @FldProc_PrintMethod,     PRINTMETHOD_EXCEL_PDF, 28, 25 );

    DisableFields(Data(), PNFLD_BROKERREP_FIMOVEMENT);
    DisableFields(Data(), PNFLD_BROKERREP_NOTZEROREST);
    DisableFields(Data(), PNFLD_BROKERREP_SHOWEMPTY);
  end;

  private macro GetFieldValue(fldNum:integer):variant
    var showValue:variant, fldValue:variant;

    GetFieldValue(fldNum, @showValue, @fldValue);
    return fldValue;
  end;

  macro Run()
    var stat = Run();
    RepParams.PeriodKind   = GetFieldValue(PNFLD_BROKERREP_PERIODKIND);
    RepParams.BegDate      = GetFieldValue(PNFLD_BROKERREP_BEGDATE);
    RepParams.EndDate      = GetFieldValue(PNFLD_BROKERREP_ENDDATE);
    RepParams.ClientID     = GetFieldValue(PNFLD_BROKERREP_CLNTCODE);
    RepParams.DlContrID    = GetFieldValue(PNFLD_BROKERREP_CONTRACT);
    RepParams.IsMarket     = GetFieldValue(PNFLD_BROKERREP_PROCMARKET)    == SET_CHAR;
    RepParams.IsOutMarket  = GetFieldValue(PNFLD_BROKERREP_PROCOUTMARKET) == SET_CHAR;
    RepParams.IsFiMovement = GetFieldValue(PNFLD_BROKERREP_FIMOVEMENT)    == SET_CHAR;
    RepParams.IsNotZeroRest= GetFieldValue(PNFLD_BROKERREP_NOTZEROREST)   == SET_CHAR;
    RepParams.ShowPlanRest = GetFieldValue(PNFLD_BROKERREP_SHOWPLANREST)  == SET_CHAR;
    RepParams.ShowEmpty    = GetFieldValue(PNFLD_BROKERREP_SHOWEMPTY)     == SET_CHAR;
    RepParams.IsApp        = GetFieldValue(PNFLD_BROKERREP_APOSTROF)      == SET_CHAR;

    var PrintMethod        = GetFieldValue(PNFLD_BROKERREP_PRINTMETHOD);
    RepParams.PdfFormat    = ((PrintMethod == PRINTMETHOD_PDF)   or (PrintMethod == PRINTMETHOD_EXCEL_PDF));
    RepParams.ExcelFormat  = ((PrintMethod == PRINTMETHOD_EXCEL) or (PrintMethod == PRINTMETHOD_EXCEL_PDF));

    RepParams.ShowFile     = true;

    return stat;
  end;

  macro GetParams()
    return RepParams;
  end;

  PRIVATE MACRO Check():BOOL
    var ClientId = GetFieldValue(PNFLD_BROKERREP_CLNTCODE);
    if ((ClientId == null) or (ClientId <= 0))
      msgbox("Необходимо задать клиента.");
      return false;
    end;

    if((GetFieldValue(PNFLD_BROKERREP_PROCMARKET) == false) and (GetFieldValue(PNFLD_BROKERREP_PROCOUTMARKET) == false))
      msgbox("Необходимо указать тип сделок для отбора.");
      return false;
    end;

    if((date(GetFieldValue(PNFLD_BROKERREP_BEGDATE)) > date(GetFieldValue(PNFLD_BROKERREP_ENDDATE))))
      MsgBox( "Дата окончания не может быть меньше даты начала периода.");
      return false;
    end;

    return true;
  END;

  initDL_CPanel("sp_rep.lbr", "BRKRRSHB");
end;


macro ReportRun()
  var param = СBrokerRepRSHB_Panel();
  while (param.Run())
    /*выбор подписанта*/
    var SignerParty = -1;
    var sql_s = "SELECT p.t_oper, p.t_name, p.t_partyid FROM dperson_Dbt p " +
                " WHERE p.t_partyid <> -1 AND EXISTS " +
                "              (SELECT 1 " +
                "                 FROM dnotetext_dbt n " +
                "                WHERE     n.t_objecttype = 3 " +
                "                      AND n.t_notekind = 107 " +
                "                      AND n.t_documentid = LPAD (p.t_partyid, 10, '0') " + 
                "                      AND ascii(RSB_STRUCT.GETSTRING(n.t_text)) != 0) " +
                "  AND p.t_userClosed <> CHR(88) " +
                "ORDER BY p.t_name ";

    var Dt = TRsbDataSet(sql_s);
    if(not Dt.movenext())
       MsgBox("Не найдены доступные для отчета подписанты");
       break;
    else
       SignerParty = Dt.partyid;
       var Choice = DL_Scroll(sql_s, " Выбор подписанта ", 15, 15, "t_oper", "N", "t_Name", "Подписант");
       if( Choice != NULL )
          SignerParty = Choice.Value("t_partyid"); 
       end;
    end;
    param.RepParams.SignerParty = SignerParty;

    //Определение даты формирования отчета
    GetDate(param.RepParams.ReportDate, "Дата формирования отчета:");

    SC_CreateRepBrokerRSHBNew(param.GetParams());
  end;
end; 

ReportRun();
exit(1);