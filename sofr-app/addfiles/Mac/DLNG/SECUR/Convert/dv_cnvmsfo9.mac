/*
$Name:        dv_cnvmsfo9.mac
$Module:      ФИССиКО
$Description: Конвертация МСФО9.
*/

import DealsInter, dv_categ, dv_car, dv_period;


/*****************************************************/
/* Дата операционного дня в котором будут            */
/* сформированы проводки, относящиеся к переходу.    */
/* Предположительно <первый рабочий день 2019 года>  */
/*****************************************************/
PRIVATE CONST ДатаРасчета = GetDateAfterWorkDays( date(1, 1, 2019), 0);

/*****************************************************/
/* Определят на какие счета будет относится          */
/* переходящий финансовый результат.                 */
/* Возможные значения:                               */
/*    - 706: фин. результат текущего года            */
/*    - 10801: КУ "Нераспределенная прибыль" /       */
/*      10901: КУ "Непокрытый убыток"                */
/*****************************************************/
PRIVATE CONST БС_706 = 1;
PRIVATE CONST БС_10801_10901 = 2;
PRIVATE CONST СчетДляУчетаФР = БС_706;

PRIVATE VAR MoveDateKindID = -1;
PRIVATE VAR MoveDateKindID_2 = -1;

MACRO conversion_msgbox( message )
    println( message );
END;

PRIVATE MACRO УстановитьПризнакПФИ()

    var cmd = RSDCommand("UPDATE ddvndeal_dbt deal "
                        +"   SET deal.t_IsPFI = chr(88) "                              // устанавливаем признак "ПФИ"
                        +" WHERE   ( (deal.t_DocKind = 4815 AND deal.t_DVkind = 5) "   // для сделок "Покупка/продажа Т+3,
                        +"        OR (deal.t_DocKind = 4813 AND deal.t_DVkind = 6) "   // "Своп",
                        +"        OR deal.t_DVkind = 3 "                               // "Валютный своп (не ПФИ)"
                        +"         ) "
                        +"       AND deal.t_Date < TO_DATE('01.01.2019', 'DD.MM.YYYY')"// заключенных ранее 01.01.2019,
                        +"       AND deal.t_State = 1 ");                              // и открытых на момент перехода
    cmd.Execute();
    
END;

PRIVATE MACRO FormError( StrErr:STRING, err, command:RSDCommand ):STRING
  VAR i = 0;

  StrErr = "!!! " + StrErr;

  if( err != NULL )
     StrErr = StrErr + "\nСтрока: " + string(err.line) + "  " + err.message + "\n";
  end;

  if( command != NULL )
     while( i < command.connection.environment.ErrorCount )
         StrErr = StrErr + command.connection.environment.Error(i).Descr + "\n";
         i = i + 1;
     end;
  end;
  return StrErr;
OnError
  return StrErr;
end;

PRIVATE MACRO GetOprStatus( ID_Operation:INTEGER, StatusKindID:INTEGER )

  VAR RS, cmd = RSDCommand( "Select t_NumValue " +
                            "  From DOPRCURST_DBT " +
                            " Where t_ID_Operation = ? " +
                            "   And t_StatusKindID = ? " );
  cmd.addParam( "", RSDBP_IN, ID_Operation );
  cmd.addParam( "", RSDBP_IN, StatusKindID );
  cmd.execute();
  RS = TRsbDataSet(cmd);
  if( RS.movenext() )
    return SQL_ConvTypeInteger(RS.NumValue);
  end;

  return 0;
END;

PRIVATE MACRO GetDateKindID( DocKind:INTEGER, NumberDate:INTEGER ):INTEGER

  VAR RS, cmd = RSDCommand( "SELECT NVL(t_DateKindID,-1) DateKindID from doprkdate_dbt where t_DocKind = ? and t_NumberDate = ?" );
  cmd.addParam( "", RSDBP_IN, DocKind );
  cmd.addParam( "", RSDBP_IN, NumberDate );
  cmd.execute();
  RS = TRsbDataSet(cmd);
  if( RS.movenext() )
    return SQL_ConvTypeInteger(RS.DateKindID);
  end;
  return -1;
END;

PRIVATE MACRO GetOprDate( ID_Operation:INTEGER, DateKindID:INTEGER )
  VAR cmd, RS;

  cmd = RSDCommand( "Select t_Date "+
                    "  from DOPRDATES_DBT "+
                    " where t_ID_Operation = ? "+
                    "   and t_DateKindID = ? ");

  cmd.addParam( "", RSDBP_IN, ID_Operation );
  cmd.addParam( "", RSDBP_IN, DateKindID );
  cmd.execute();
  RS = TRsbDataSet(cmd);
  if( RS.movenext() )
    return SQL_ConvTypeDate(RS.Date);
  end;
  return Date(0,0,0);
END;

MACRO IsNeedInsertTransfStep( ID_Operation:INTEGER, Symbol:String, Plan_Date:Date )
   var RetVal = true;
   var Query, Data, Sql;

   Sql = " SELECT step.t_Plan_Date, step.t_IsExecute " +
         "   FROM doprstep_dbt step" +
         "  WHERE step.t_ID_Operation = ? AND " +
         "        step.t_Symbol       = to_char(?) " +
         " order by step.t_ID_Step desc";

   Query = RSDCommand( Sql );

   Query.addParam("", RSDBP_IN, ID_Operation );
   Query.addParam("", RSDBP_IN, Symbol );

   Query.execute();

   Data = TRsbDataSet(Query);
   if( Data.MoveNext() )
      if( SQL_ConvTypeDate(Data.Plan_Date) >= Plan_Date )
         RetVal = false;
      end;
   end;

   return RetVal;
END;

PRIVATE MACRO InsertOperStatus( ID_Operation:INTEGER, StatusKindID:INTEGER, NumValue:INTEGER )
  VAR cmd = RSDCommand( "{ CALL RSI_RsbOperation.SetOprStatusValue(?, ?, ?, ?) }" );
  cmd.addParam( "", RSDBP_IN, DL_DVFXDEAL );
  cmd.addParam( "", RSDBP_IN, ID_Operation );
  cmd.addParam( "", RSDBP_IN, StatusKindID );
  cmd.addParam( "", RSDBP_IN, NumValue );
  cmd.execute();
ONERROR(err)
  msgbox( FormError("Ошибка при вставке статуса операции (InsertOperStatus)", err, cmd) );
  return false;
END;

PRIVATE MACRO GetID_Operation( DocKind:INTEGER, DocID:INTEGER ):INTEGER
  VAR RS, cmd = RSDCommand( "   SELECT t_ID_Operation " +
                            "     FROM doproper_dbt " +
                            "    WHERE to_number(t_DocKind) = ? AND " +
                            "          t_DocumentID = ? "
                          );
  cmd.addParam( "", RSDBP_IN, DocKind );
  cmd.addParam( "", RSDBP_IN, DocID );
  cmd.execute();
  RS = TRsbDataSet(cmd);
  if( RS.movenext() )
    return SQL_ConvTypeInteger(RS.ID_Operation);
  end;
  return -1;
END;

PRIVATE MACRO InsertOperDate( ID_Operation:INTEGER, DateKindID:INTEGER, OperDate:Date )
  VAR cmd;

  cmd = RSDCommand( "INSERT INTO DOPRDATES_DBT( "+
                    "    t_ID_Operation,     "+// Идентификатор экземпляра операции
                    "    t_DateKindID,       "+// Номер (вида) даты
                    "    t_Date              "+// Значение даты
                    ") VALUES(?,?,?)");

  cmd.addParam( "", RSDBP_IN, ID_Operation );
  cmd.addParam( "", RSDBP_IN, DateKindID );
  cmd.addParam( "", RSDBP_IN, OperDate );
  cmd.execute();

ONERROR(err)
  msgbox( FormError("Ошибка при вставке даты операции (InsertOperDate)", err, cmd) );
  return false;
END;

PRIVATE MACRO InsertTransfStep( ID_Operation:INTEGER, BlockID:INTEGER )
  VAR RS, cmd = RSDCommand(       
    "DECLARE "
    +"  v_err INTEGER; "
    +"BEGIN "
    +  "  DELETE FROM doprbstep_tmp; "
    +  "  INSERT INTO doprbstep_tmp (t_ID_Operation, t_Previous_Step, t_Kind_Operation) VALUES(?,NVL((select max(STEP.T_ID_STEP)"
    +  "                                                                                                from doprstep_dbt step"              
    +  "                                                                                               where STEP.T_ID_OPERATION = ?"  
    +  "                                                                                                 and STEP.T_ISEXECUTE = 'X'"
    +  "                                                                                                 and STEP.T_ISREMOVESTEP = chr(0)),0),?); "
    +  "  v_err := rsi_rsboperation.BuildBlockStep(?);"
    +  "  UPDATE doprstep_dbt "
    +  "     SET t_IsExecute = 'R' "
    +  "   WHERE t_ID_Operation = ? "
    +  "     AND t_BlockID = ? "
    +  "     AND t_IsExecute = chr(0); "
    +"END;"
                          );

  cmd.addParam( "", RSDBP_IN, ID_Operation );
  cmd.addParam( "", RSDBP_IN, ID_Operation );
  cmd.addParam( "", RSDBP_IN, DL_DVFXDEAL );
  cmd.addParam( "", RSDBP_IN, BlockID );
  cmd.addParam( "", RSDBP_IN, ID_Operation );
  cmd.addParam( "", RSDBP_IN, BlockID );

  cmd.execute();

ONERROR(err)
  msgbox( FormError("Ошибка при вставке даты операции (InsertTransfStep)", err, cmd) );
  return false;
END;

PRIVATE MACRO ПеренестиТОиПереоценки( FD )
    
    record СчетТреб("account");
    record СчетОбяз("account");
    record НовСчетТреб("account");
    record НовСчетОбяз("account");
    record AccPlus("account");
    record AccMinus("account");
    record СчетДеб("account");
    record СчетКред("account");
    record accdoc_plus(mcaccdoc);
    record accdoc_minus(mcaccdoc);
    
    var msg = "", ID_Operation = -1;
    
    if( FD.nFI.rec.OperState != DL_LEG_OUTBAL )
        return 0;
    end;
    
    if( FD.IsExistAccount("+Форвард, прочие", null, true, FIROLE_FIREQ, СчетТреб, null, ДатаРасчета, FD.ВалютаТребования()) )
        var RestReq = abs(RestAC(СчетТреб.Account, СчетТреб.Code_Currency, ДатаРасчета, null, СчетТреб.Chapter));
        if( RestReq != Money(0) )
            if( not FD.OpenAccount("+Форвард, дрейф внебирж", null, true, FIROLE_FIREQ, НовСчетТреб, ДатаРасчета, FD.ВалютаТребования(), null, accdoc_plus) )                
                return 1;
            end;
            
            if( not ПроводкаПоКатегориямУчета( FD, 
                                               НовСчетТреб, СчетТреб,
                                               ДатаРасчета, 4,
                                               FD.ВалютаТребования(), RestReq,
                                               null,
                                               INPCARRY, "1", "",
                                               "Конвертация. Перенос требований по сделке " + FD.DealCode)
              )
                msg = GetErrMsg();
                if(msg != "")
                    msgbox(msg);
                end;
                return 1;
            end;
                                               
            if( CB_CloseAccount(СчетТреб.Chapter, СчетТреб.Code_Currency, СчетТреб.Account, ДатаРасчета, null) != 0 )
                return 1;
            end;
            
            msgbox("   Закрыт счет "+ СчетТреб.Account +" по сделке "+ FD.DealCode);
        end;
    end;
    
    if( FD.IsExistAccount("-Форвард, прочие", null, true, FIROLE_FICOM, СчетОбяз, null, ДатаРасчета, FD.ВалютаОбязательства()) )
        var RestCom = abs(RestAC(СчетОбяз.Account, СчетОбяз.Code_Currency, ДатаРасчета, null, СчетОбяз.Chapter));
        if( RestCom != Money(0) )
            if( not FD.OpenAccount("-Форвард, дрейф внебирж", null, true, FIROLE_FICOM, НовСчетОбяз, ДатаРасчета, FD.ВалютаОбязательства(), null, accdoc_minus) )
                return 1;
            end;
            
            if( not ПроводкаПоКатегориямУчета( FD, 
                                               СчетОбяз, НовСчетОбяз,
                                               ДатаРасчета, 4,
                                               FD.ВалютаОбязательства(), RestCom,
                                               null,
                                               INPCARRY, "1", "",
                                               "Конвертация. Перенос обязательств по сделке " + FD.DealCode)
              )
                msg = GetErrMsg();
                if(msg != "")
                    msgbox(msg);
                end;
                return 1;
            end;
            
            if( CB_CloseAccount(СчетОбяз.Chapter, СчетОбяз.Code_Currency, СчетОбяз.Account, ДатаРасчета, null) != 0 )
                return 1;
            end;
            
            msgbox("   Закрыт счет "+ СчетОбяз.Account +" по сделке "+ FD.DealCode);
        end;
    end;

    var TransfDate:date = date(0,0,0);
    var NeedTransfer = false;
    if( (НовСчетТреб.Account != "") and (НовСчетОбяз.Account != "") )
       ID_Operation = GetID_Operation(FD.Kind,FD.ID);
       if( ПереноситьПоСрокам(FD, ДатаРасчета, @TransfDate, true, accdoc_plus.PeriodID, accdoc_minus.PeriodID, "+Форвард, дрейф внебирж", "-Форвард, дрейф внебирж", FD.ВалютаТребования(), FD.ВалютаОбязательства(), FIROLE_FIREQ, FIROLE_FICOM, FD.ДатаИсполнения()) )
          NeedTransfer = true;
          if( GetOprDate( ID_Operation, IIF(FD.DealPart == 1, MoveDateKindID, MoveDateKindID_2) ) == Date(0,0,0) )
             InsertOperDate( ID_Operation, IIF(FD.DealPart == 1, MoveDateKindID, MoveDateKindID_2), TransfDate );
          end;
       end;
       if( GetOprStatus( ID_Operation, IIF(FD.DealPart == 1, DV_FX_OPERSTATUS_KIND_COTERMS, DV_FX_OPERSTATUS_KIND_COTERMS_2) ) == 0 )
          InsertOperStatus( ID_Operation, IIF(FD.DealPart == 1, DV_FX_OPERSTATUS_KIND_COTERMS, DV_FX_OPERSTATUS_KIND_COTERMS_2), IIF(NeedTransfer, DV_OPERSTATUS_COTERMS_TRANSFER, DV_OPERSTATUS_COTERMS_NOTRANSFER));
       end;
       if( NeedTransfer and IsNeedInsertTransfStep(ID_Operation,IIF(FD.DealPart == 1,"Р","р"),TransfDate) )
          InsertTransfStep( ID_Operation,IIF(FD.DealPart == 1, 274003, 274004));
       end;
    end;
    
    var OvervalRole = IIF(FD.DealPart == 1, FIROLE_FIRST_OVERVAL, FIROLE_SECOND_OVERVAL);
    
    if( FD.IsExistAccount("+Переоц.,поставка ИВ и ДМ", null, true, OvervalRole, AccPlus, MC_PAIRACCMODE_AUTO, ДатаРасчета) )
        var RestAccPlus = abs(RestAC(AccPlus.Account, AccPlus.Code_Currency, ДатаРасчета, null, AccPlus.Chapter));
        if( RestAccPlus != Money(0) )
            if( СчетДляУчетаФР == БС_706 )
                if( not FD.OpenAccount("-Маржа, ИВ и ДМ", null, true, FIROLE_BA, СчетДеб, ДатаРасчета) )
                    return 1;
                end;
            else
                if( not FD.OpenAccount("Непокрытый убыток", null, true, null, СчетДеб, ДатаРасчета) )
                    return 1;
                end;
            end;
            
            if( not ПроводкаПоКатегориямУчета( FD, 
                                               СчетДеб, AccPlus,
                                               ДатаРасчета, 1,
                                               NATCUR, RestAccPlus,
                                               null,
                                               INPCARRY, "1", "",
                                               "Конвертация. Перенос переоценки по сделке " + FD.DealCode)
              )
                msg = GetErrMsg();
                if(msg != "")
                    msgbox(msg);
                end;
                return 1;
            end;
            
            if( CB_CloseAccount(AccPlus.Chapter, AccPlus.Code_Currency, AccPlus.Account, ДатаРасчета, null) != 0 )
                return 1;
            end;
            
            msgbox("   Закрыт счет "+ AccPlus.Account +" по сделке "+ FD.DealCode);
        end;
    end;
    
    if( FD.IsExistAccount("-Переоц.,поставка ИВ и ДМ", null, true, OvervalRole, AccMinus, MC_PAIRACCMODE_AUTO, ДатаРасчета) )
        var RestAccMinus = abs(RestAC(AccMinus.Account, AccMinus.Code_Currency, ДатаРасчета, null, AccMinus.Chapter));
        if( RestAccMinus != Money(0) )
            if( СчетДляУчетаФР == БС_706 )
                if( not FD.OpenAccount("+Маржа, ИВ и ДМ", null, true, FIROLE_BA, СчетКред, ДатаРасчета) )
                    return 1;
                end;
            else
                if( not FD.OpenAccount("Нераспределенная прибыль", null, true, null, СчетКред, ДатаРасчета) )
                    return 1;
                end;
            end;
            
            if( not ПроводкаПоКатегориямУчета( FD, 
                                               AccMinus, СчетКред,
                                               ДатаРасчета, 1,
                                               NATCUR, RestAccMinus,
                                               null,
                                               INPCARRY, "1", "",
                                               "Конвертация. Перенос переоценки по сделке " + FD.DealCode)
              )
                msg = GetErrMsg();
                if(msg != "")
                    msgbox(msg);
                end;
                return 1;
            end;
            
            if( CB_CloseAccount(AccMinus.Chapter, AccMinus.Code_Currency, AccMinus.Account, ДатаРасчета, null) != 0 )
                return 1;
            end;
            
            msgbox("   Закрыт счет "+ AccMinus.Account +" по сделке "+ FD.DealCode);
        end;
    end;
    
    return 0;
    
END;

PRIVATE MACRO ВыполнитьПереносОстатковПоОперациямСвоп()
    
    var error = 0;
    
    MoveDateKindID = GetDateKindID(DL_DVFXDEAL,DV_FXDEAL_OPRDATE_COTERMS);
    if( MoveDateKindID == -1 )
       msgbox(" Для ПД \"Конверсионная сделка ФИССиКО\" не найден вид даты \"Перенос по срокам\"");
       return 1;
    end;

    MoveDateKindID_2 = GetDateKindID(DL_DVFXDEAL,DV_FXDEAL_OPRDATE_COTERMS_2);
    if( MoveDateKindID_2 == -1 )
       msgbox(" Для ПД \"Конверсионная сделка ФИССиКО\" не найден вид даты \"Перенос по срокам 2ч\"");
       return 1;
    end;

    var query = "SELECT deal.t_ID ID "
               +"  FROM ddvndeal_dbt deal "
               +" WHERE     deal.t_DocKind = 4813 "
               +"       AND deal.t_DVkind = 6 "
               +"       AND deal.t_Date < TO_DATE('01.01.2019', 'DD.MM.YYYY')"
               +"       AND deal.t_State = 1";
    
    var cmd = DL_RSDCommand(query);
    
    var count = cmd.GetCount();
    if( count > 0 )
        InitProgress(count, "Обработка сделок \"Своп\"", "Обработка сделок \"Своп\"");
        var DataSet = cmd.Execute();
        var FD1, FD2;
        var i = 0;
        while( (not error) and (DataSet.moveNext()) )
            FD1 = DVFirstDocFXDeal(4813, DataSet.ID, 1);
            error = ПеренестиТОиПереоценки(FD1);
            if( not error )
                FD2 = DVFirstDocFXDeal(4813, DataSet.ID, 2);
                error = ПеренестиТОиПереоценки(FD2);
            end;
            UseProgress(i = i + 1);
        end;
    end;
    
    return error;
    
END;

MACRO StartTransaction( message )
    msgbox(message);
    RslDefCon.BeginTrans();
END;

MACRO EndTransaction( error )
    if( error )
        RslDefCon.RollbackTrans();
        msgbox("При выполнении возникли ошибки!");
    else
        RslDefCon.CommitTrans();
        msgbox("Выполнено успешно");
    end;
END;

MACRO StartConversion()
    var error = 0;
    
    msgbox("Начало конвертации");
    
    if( (СчетДляУчетаФР != БС_706) and (СчетДляУчетаФР != БС_10801_10901) )
        msgbox("Не реализована обработка значения "+СчетДляУчетаФР+" для параметра \"СчетДляУчетаФР\"");
        error = 1;
    end;
    
    if( not error )
        StartTransaction("1. Проставляем признак ПФИ");
        УстановитьПризнакПФИ();
        EndTransaction(error);
    end;
    
    if( not error )
        StartTransaction("2. Обработка сделок \"Своп\"");
        error = ВыполнитьПереносОстатковПоОперациямСвоп();
        EndTransaction(error);
    end;
    
    if(not error)
        msgbox("Конвертация успешно завершена");
    else
        msgbox("При конвертации возникли ошибки.\nПосле устранения причин ошибок повторную конвертацию следует выполнять, начиная с проблемного блока.\nВсе предыдущие успешно выполненные блоки следует закомментировать в макросе.");
    end;
    
OnError( errObj );
    if(RslDefCon.IsInTrans)
        RslDefCon.RollbackTrans();
    end;
    
    if(errObj and errObj.err and errObj.err.environment)
        var j;
        var err_text = "", delim = "";
        j = 0;
        while(j < errObj.err.environment.ErrorCount)
            err_text = err_text + delim  + errObj.err.environment.error(j).descr;
            delim = "\n";
            j = j + 1;
        end;
        
        MsgBox(err_text);
    end;
    
    return 1;
END;

ReplaceMacro( "msgbox", "conversion_msgbox" );

StartConversion();

ReplaceMacro( "msgbox", NULL );