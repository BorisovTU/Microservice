/*
$Name:        209780_upg.mac
$Module:      Ценные бумаги
$Description: 
*/
Import RSD, RsbDataSet, CTInter, FIInter, "cb_sql.mac", "sp_car";

/* определить вид финансового инструмент */
private macro GetFIKind( Currency : integer ) : integer

  var FIKind : integer;
  var rs = RsdRecordset( "SELECT t_FI_Kind FROM dfininstr_dbt WHERE t_FIID = " + string(Currency) );

  FIKind = -1;

  if( rs.moveNext() )

    FIKind = rs.value(0);

  end;

    if( FIKind == FIKIND_CURRENCY ) FIKind = 1;
  elif( FIKind == FIKIND_METAL    ) FIKind = 2;
  end;

  return FIKind;

end;

private class PDocOverEstimate( _Chapter:integer, _Currency:integer/*, _Account : string, _conn_account*/ )

  var Chapter  : integer;
  var Currency : integer;
/*  var Account  : string;

  var conn_account : TRecHandler;
*/
  var error : integer;

  /* корректировка счета */
  macro CorrectAccount( account, accblnc, ORScheme, categ, templ, accdoc, OperDate )
    return true;
  end;

  /* определение FIRole */
  macro GetBasisFIRole( FIRole : integer ) : integer
    return FIROLE_UNDEF;
  end;

  /* определение параметров второго рода */
  macro GetParametr( ParmKind, OperDate, CatCode, FIRole ) : integer
    
    var Parametr = -1;

    if( ParmKind == MC_TYPE_PARAMETR_FIID )
      Parametr = Currency;
    elif( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
/*      Parametr = conn_account.rec.Department;*/
      Parametr = {OperDprt};
    end;

    return Parametr;

  end;

  /* определение параметров первого рода */
  macro GetParametrTemplate( ObjectID, Classificator, OperDate, FIRole ) : integer
    
    var Parametr = -1;

    if( Classificator == LLCLASS_OVERVALUE_ACC )
/*      Parametr = conn_account.rec.ORScheme; /* схема переоценки */*/
    elif( (Classificator == LLCLASS_FI_KIND) or (Classificator == LLCLASS_KIND_AKT_BANK) )
      Parametr = GetFIKind( Currency );
    elif( Classificator == LLCLASS_SIDEBALANCE_CORACCKIND )
/*      Parametr = GetSideBalance( conn_account );*/
    end;

    return Parametr;

  end;

  PRIVATE MACRO CorrectParameters( store:bool ):bool
     return false;
  END;

    /* Получить счет по категории учета
       Вернет false, если ошибка или отказ от ввода счета.
       ! если не установлен NoErrMes выдаст сообщение об ошибке
    */
  PRIVATE MACRO GetAccountByCatCode
  ( 
      CatCode:string     ,  /* код категории */
      FindAccount:string ,  /* здесь вернется номер счета */
      /*необязательные параметры*/
      AccCreate:Integer,     /* признак верификации\открытия лицевого счета       */
      NoErrMes:bool,      /* признак молчаливого выполнения - ошибку не выдаем */
      FIRole:Variant,        /* роль ФИ или массив ролей*/
      AccBuf:Variant,        /* буфер для возврата информации по найденному\открытому счету   */
      PairAcc:Integer,       /* с учетом парности */
      ActionDate:Date,    /* дата */
      FIID:Integer,          /*валюта*/
      Accounts:Variant,      /*массив номеров счетов, соответствующий массиву ролей */  
      RealOpenMode:Integer,  /*фактический режим открытия*/
      McAccDocBuf:Variant,   /* буфер для возврата данных по счету mcaccdoc.dbt */
      ActivateDate:Variant,   /*Дата актуализации счета (может быть не задана)*/
      NumberForAcc:string    /*номер существующего счета, который нужно привязать к документу*/
  )
     var i = 0, loop = true, open_acc = true, _FIRole = null, IsMass;

     if( this.Error != 0 ) return false; end;

     if( ((NoErrMes == null) OR (NoErrMes == false)) AND 
         (not isOprMultiExec()) 
       )
        IsMass = 0;
     else /*либо массовый режим, либо принудительно просим не орать ошибки*/
        IsMass = 1;
     end;

     if( ValType(AccCreate) == V_UNDEF )
        AccCreate = MC_OPENACC_CREATE;
     end;

     while( loop ) /*цикл нужен для перебора ролей, если задан массив ролей*/

        if( (FIRole != null) AND (ValType( FIRole ) == V_GENOBJ) ) /*массив ролей*/  

           if(FIRole[i] != null)            
             loop     = true;                    
             open_acc = true;
             _FIRole  = FIRole[i];
           else
             loop     = false;                    
             open_acc = false;
             _FIRole  = null;
           end;
           i = i + 1;    

        else
           loop       = false; /*перебор не нужен, только одна роль задана*/
           open_acc   = true;
           _FIRole    = FIRole;
        end;
    
        if( open_acc )                    
           CorrectParameters( true );

           if( AccCreate != 4 )
              FindAccount = MC_FindAndOpenCommonAccByFD(
                    CatCode,
                    this,
                    ActionDate,
                    IsMass,
                    AccCreate,
                    AccBuf, FIID, null, PairAcc, null, _FIRole, RealOpenMode, McAccDocBuf, NULL, NULL, ActivateDate//,
                    //BackOutAccount,
                    //ChangeOpenDate
                );
           else
              FindAccount = MC_GetAccountNumber (
                  CatCode,
                  this,
                  ActionDate,
                  IsMass, FIID, _FIRole );
           end;

           CorrectParameters( false );

           if( FindAccount != "" )
              if( (Accounts != null) AND (ValType( Accounts ) == V_GENOBJ) ) /*массив счетов*/  
                 Accounts[i-1] = FindAccount;
              end;
           end;
        end;
     end;

     SetParm( 2, FindAccount );
     if ( FindAccount=="" )
        if( IsMass == 0 )
           MsgBox("Счет категории \"" + CatCode + "\" неопределен или недоступен в данной операции");
        end;  
        return false;
     end;

     return true;
  END;

  /*открытие и актуализация счета*/  
  MACRO OpenAccount( CatCode, FindAccount, NoErrMes, FIRole, AccBuf, ActionDate, FIID, Accounts, McAccDoc, PairAcc, NumberForAcc )
     var ret, acnt;      
     ret = GetAccountByCatCode( CatCode, acnt, MC_OPENACC_CREATE, NoErrMes, FIRole, AccBuf, PairAcc, ActionDate, FIID, Accounts, null, McAccDoc, null, NumberForAcc );
     setparm( 2, acnt ); 
     return ret;
  END;  
  
/* конструктор */  
  error = 0;

  Chapter  = _Chapter ;
  Currency = _Currency;
end;

private class СчетаДляВалюты( FICode_:string, СчетПоложитПереоценки_:string, СчетОтрицатПереоценки_:string )
  var FICode = FICode_,
      СчетПоложитПереоценки = СчетПоложитПереоценки_,
      СчетОтрицатПереоценки = СчетОтрицатПереоценки_;
end;

private macro Upgrade()
  record AccCt( account );
  record CredCatAcc(account);
  record Acc( account );
  var stat:integer = 0;
  var begin_transaction:bool = false;
  var DateUpg:date = date(1,11,2014);
  var FD, Categ:string = "";
  var query, query_, ds, ds_;
  var PrevFIID:integer = ALLFININSTR, FIID;
  var SumCarry:money = $0.0;
  var СчетаПереоценки = TArray();
  var Sum1:money = $0.0, Sum2:money = $0.0;

/* Пример заполнения счетов переоценки:
  СчетаПереоценки[СчетаПереоценки.Size] = СчетаДляВалюты("840", "СчётДоходов70606*27202*", "СчётРасходов70607*17202*");
  СчетаПереоценки[СчетаПереоценки.Size] = СчетаДляВалюты("978", "СчётДоходов70606*27202*", "СчётРасходов70607*17202*");
  ...
*/

  if( СчетаПереоценки.Size == 0 )
     println("Не заданы счета переоценки. Необходимо задать счета в макросе 209780_upg.mac");
     return 1;
  end;

  private macro ПолучитьСчет( Acc_, FIID:integer, ВидСчета:integer ):integer
     var stat = 0;
     var fininstr = TRecHandler("fininstr");

     if( ПолучитьФинИн(FIID, fininstr) == 0 )
        stat = 1;
        var i = 0;
        while( i < СчетаПереоценки.Size() )
           if( СчетаПереоценки[i].FICode == fininstr.rec.FI_Code )
              if( GetAccount(1, FIID, IIF(ВидСчета == 0, СчетаПереоценки[i].СчетПоложитПереоценки, СчетаПереоценки[i].СчетОтрицатПереоценки), Acc_, true) )
                 stat = 0; /*нашли*/
              end;
              break;
           end;
           i = i + 1;
        end;
     else
        stat = 1;
     end;

     return stat;
  end;


  RslDefCon.BeginTrans();
  begin_transaction = true;

  /* Обновление сумм лотов ПКУ */
  query = RSDCommand( " SELECT Lot.t_SumID, fin.t_FaceValueFI " +
                      "   FROM dpmwrtsum_dbt Lot, dfininstr_dbt fin " +
                      "  WHERE Lot.t_Portfolio = ? " +
                      "    AND Lot.t_Amount > 0 " +
                      "    AND fin.t_FIID = Lot.t_FIID " +
                      "    AND fin.t_FaceValueFI != ? " );
  query.NullConversion = true;
  query.addParam("", RSDBP_IN, KINDPORT_CONTR);
  query.addParam("", RSDBP_IN, NATCUR);
  query.execute();

  ds = TRsbDataSet(query);
  while( ds.MoveNext() )
     query_ = RSDCommand( " update dpmwrtsum_dbt Lot " +
                          "    set Lot.t_Cost = RSI_RSB_FIInstr.ConvSum(Lot.t_Cost, ?, ?, Lot.t_Date, 1), " +
                          "        Lot.t_BalanceCost = RSI_RSB_FIInstr.ConvSum(Lot.t_BalanceCost, ?, ?, Lot.t_Date, 1) " +
                          "  where Lot.t_SumID = ? " );
     query_.NullConversion = true;
     query_.addParam("", RSDBP_IN, SQL_ConvTypeInteger(ds.FaceValueFI));
     query_.addParam("", RSDBP_IN, NATCUR);
     query_.addParam("", RSDBP_IN, SQL_ConvTypeInteger(ds.FaceValueFI));
     query_.addParam("", RSDBP_IN, NATCUR);
     query_.addParam("", RSDBP_IN, SQL_ConvTypeInteger(ds.SumID));
     query_.execute();
  end;

  /* Перенос средств с "Наш портфель ц/б" на "Наш портфель ПКУ, ц/б" */
  query = RSDCommand( " SELECT accdoc.t_Account, accdoc.t_Chapter, accdoc.t_Currency, accdoc.t_FIID " +
                      "   FROM dmccateg_dbt categ, dmcaccdoc_dbt accdoc, dmctempl_dbt templ, daccount_dbt ac " +
                      "  WHERE categ.t_Code       = 'Наш портфель ц/б' " +
                      "    AND accdoc.t_CatID     = categ.t_ID" +
                      "    AND templ.t_CatID      = categ.t_ID  " +
                      "    AND templ.t_Number     = accdoc.t_TemplNum " +
                      "    AND (   (categ.t_Param1 = ? and categ.t_Class1 = ? and templ.t_Value1 = ?) " +
                      "         OR (categ.t_Param2 = ? and categ.t_Class2 = ? and templ.t_Value2 = ?) " +
                      "         OR (categ.t_Param3 = ? and categ.t_Class3 = ? and templ.t_Value3 = ?) " +
                      "         OR (categ.t_Param4 = ? and categ.t_Class4 = ? and templ.t_Value4 = ?) " +
                      "         OR (categ.t_Param5 = ? and categ.t_Class5 = ? and templ.t_Value5 = ?) " +
                      "         OR (categ.t_Param6 = ? and categ.t_Class6 = ? and templ.t_Value6 = ?) " +
                      "         OR (categ.t_Param7 = ? and categ.t_Class7 = ? and templ.t_Value7 = ?) " +
                      "         OR (categ.t_Param8 = ? and categ.t_Class8 = ? and templ.t_Value8 = ?) " +
                      "        ) " +
                      "    AND ac.t_Account = accdoc.t_Account " +
                      "    AND ac.t_Chapter = accdoc.t_Chapter " +
                      "    AND ac.t_Code_Currency = accdoc.t_Currency " +
                      "    and accdoc.t_iscommon = 'X'  " +
                      "    AND ac.t_Close_Date = "+GetSQLDate(date(0,0,0)) );
  query.NullConversion = true;
  query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
  query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
  query.addParam("", RSDBP_IN, KINDPORT_CONTR);
  query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
  query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
  query.addParam("", RSDBP_IN, KINDPORT_CONTR);
  query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
  query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
  query.addParam("", RSDBP_IN, KINDPORT_CONTR);
  query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
  query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
  query.addParam("", RSDBP_IN, KINDPORT_CONTR);
  query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
  query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
  query.addParam("", RSDBP_IN, KINDPORT_CONTR);
  query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
  query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
  query.addParam("", RSDBP_IN, KINDPORT_CONTR);
  query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
  query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
  query.addParam("", RSDBP_IN, KINDPORT_CONTR);
  query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
  query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
  query.addParam("", RSDBP_IN, KINDPORT_CONTR);
  query.execute();

  ds = TRsbDataSet(query);
  while( ds.MoveNext() and (stat == 0) )
     FD = SPFirstDocFI(DLDOC_ISSUE, SQL_ConvTypeInteger(ds.FIID), SQL_ConvTypeInteger(ds.FIID), -1, {OurBank});

     if( SQL_ConvTypeInteger(ds.Currency) != NATCUR )
        if( not GetAccount(SQL_ConvTypeInteger(ds.Chapter), SQL_ConvTypeInteger(ds.Currency), SQL_ConvTypeStr(ds.Account), AccCt, true) )
           println( "Не найден лицевой счет " + SQL_ConvTypeStr(ds.Account) );
           stat = 1;
        else
           if( not ПроводкаПоКатегориямУчета( FD,
                                              "Наш портфель ПКУ, ц/б", AccCt, 
                                              DateUpg,                             /* Дата */
                                              1,                                   /* Глава */
                                              SQL_ConvTypeInteger(ds.Currency),    /* Валюта */
                                              Abs(GetRestAccount(AccCt, DateUpg)), /* Сумма */
                                              null,                                /* Документ */
                                              INPCARRY,                            /* Result_Carry */
                                              " 1",                                /* Kind_Oper */
                                              "",                                  /* Numb_Document */
                                              "",                                  /* Ground */
                                              null, null, null,
                                              NATCUR, SQL_ConvTypeInteger(ds.Currency)
                                            )
             )
              println("Ошибка при выполнении проводки");
              stat = 1;
           end;
        end;
     else
        if( not FD.OpenAccount("Наш портфель ПКУ, ц/б", null, true, FIROLE_BAINCONTR, null, DateUpg, NATCUR, null, null, null, SQL_ConvTypeStr(ds.Account)) )
           println("Счёт " + SQL_ConvTypeStr(ds.Account) + " не перепривязан");
        end;
     end;
  end;

  /* Перепривяжем счета переоценки */
  if( stat == 0 )
     if( ПарностьСчетовПереоценки() )
        query = RSDCommand( " SELECT categ.t_Code, accdoc.t_Account, accdoc.t_FIID, " +
                            "        (case when categ.t_Class1 = ? THEN templ.t_Value1 " +
                            "              when categ.t_Class2 = ? THEN templ.t_Value2 " +
                            "              when categ.t_Class3 = ? THEN templ.t_Value3 " +
                            "              when categ.t_Class4 = ? THEN templ.t_Value4 " +
                            "              when categ.t_Class5 = ? THEN templ.t_Value5 " +
                            "              when categ.t_Class6 = ? THEN templ.t_Value6 " +
                            "              when categ.t_Class7 = ? THEN templ.t_Value7 " +
                            "              when categ.t_Class8 = ? THEN templ.t_Value8 " +
                            "              else -1 end) as t_PortfolioID " +
                            "   FROM dmccateg_dbt categ, dmcaccdoc_dbt accdoc, dmctempl_dbt templ, daccount_dbt ac " +
                            "  WHERE categ.t_Code       in('-Переоценка, ц/б', '+Переоценка, ц/б') " +
                            "    AND accdoc.t_CatID     = categ.t_ID" +
                            "    AND templ.t_CatID      = categ.t_ID  " +
                            "    AND templ.t_Number     = accdoc.t_TemplNum " +
                            "    AND (   (categ.t_Param1 = ? and categ.t_Class1 = ? and templ.t_Value1 in(?,?)) " +
                            "         OR (categ.t_Param2 = ? and categ.t_Class2 = ? and templ.t_Value2 in(?,?)) " +
                            "         OR (categ.t_Param3 = ? and categ.t_Class3 = ? and templ.t_Value3 in(?,?)) " +
                            "         OR (categ.t_Param4 = ? and categ.t_Class4 = ? and templ.t_Value4 in(?,?)) " +
                            "         OR (categ.t_Param5 = ? and categ.t_Class5 = ? and templ.t_Value5 in(?,?)) " +
                            "         OR (categ.t_Param6 = ? and categ.t_Class6 = ? and templ.t_Value6 in(?,?)) " +
                            "         OR (categ.t_Param7 = ? and categ.t_Class7 = ? and templ.t_Value7 in(?,?)) " +
                            "         OR (categ.t_Param8 = ? and categ.t_Class8 = ? and templ.t_Value8 in(?,?)) " +
                            "        ) " +
                            "    AND ac.t_Account = accdoc.t_Account " +
                            "    AND ac.t_Chapter = accdoc.t_Chapter " +
                            "    AND ac.t_Code_Currency = accdoc.t_Currency " +
                            "    and accdoc.t_iscommon = 'X'  " +
                            "    AND ac.t_Close_Date = "+GetSQLDate(date(0,0,0)) );
        query.NullConversion = true;
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, KINDPORT_TRADE);
        query.addParam("", RSDBP_IN, KINDPORT_SALE);
        query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, KINDPORT_TRADE);
        query.addParam("", RSDBP_IN, KINDPORT_SALE);
        query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, KINDPORT_TRADE);
        query.addParam("", RSDBP_IN, KINDPORT_SALE);
        query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, KINDPORT_TRADE);
        query.addParam("", RSDBP_IN, KINDPORT_SALE);
        query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, KINDPORT_TRADE);
        query.addParam("", RSDBP_IN, KINDPORT_SALE);
        query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, KINDPORT_TRADE);
        query.addParam("", RSDBP_IN, KINDPORT_SALE);
        query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, KINDPORT_TRADE);
        query.addParam("", RSDBP_IN, KINDPORT_SALE);
        query.addParam("", RSDBP_IN, OBJTYPE_KINDPORT);
        query.addParam("", RSDBP_IN, LLCLASS_KINDPORT);
        query.addParam("", RSDBP_IN, KINDPORT_TRADE);
        query.addParam("", RSDBP_IN, KINDPORT_SALE);
        query.execute();

        ds = TRsbDataSet(query);
        while( ds.MoveNext() and (stat == 0) )
           FD = SPFirstDocFI(DLDOC_ISSUE, SQL_ConvTypeInteger(ds.FIID), SQL_ConvTypeInteger(ds.FIID), -1, {OurBank});
           if( SQL_ConvTypeStr(ds.Code) == "-Переоценка, ц/б" )
              if( SQL_ConvTypeInteger(ds.PortfolioID) == KINDPORT_TRADE )
                 Categ = "-Переоценка, ц/б ССПУ_ЦБ";
              else
                 Categ = "-Переоценка, ц/б СССД_ЦБ";
              end;
           elif( SQL_ConvTypeStr(ds.Code) == "+Переоценка, ц/б" )
              if( SQL_ConvTypeInteger(ds.PortfolioID) == KINDPORT_TRADE )
                 Categ = "+Переоценка, ц/б ССПУ_ЦБ";
              else
                 Categ = "+Переоценка, ц/б СССД_ЦБ";
              end;
           end;

           if( not FD.OpenAccount(Categ, null, true, null, null, DateUpg, NATCUR, null, null, null, SQL_ConvTypeStr(ds.Account)) )
              println("Счёт " + SQL_ConvTypeStr(ds.Account) + " не перепривязан");
           end;
        end;
     end;
  end;

  /* Перепривяжем счета ПО ДK */
  if( stat == 0 )
     if( ПарностьСчетовПереоценки() )
        query = RSDCommand( " SELECT categ.t_Code, accdoc.t_Account, accdoc.t_FIID " +
                            "   FROM dmccateg_dbt categ, dmcaccdoc_dbt accdoc, daccount_dbt ac " +
                            "  WHERE categ.t_Code   in('-ПО ДК', '+ПО ДК') " +
                            "    AND accdoc.t_CatID = categ.t_ID " +
                            "    AND ac.t_Account = accdoc.t_Account " +
                            "    AND ac.t_Chapter = accdoc.t_Chapter " +
                            "    AND ac.t_Code_Currency = accdoc.t_Currency " +
                            "    and accdoc.t_iscommon = 'X'  " +
                            "    AND ac.t_Close_Date = "+GetSQLDate(date(0,0,0)) );
        query.NullConversion = true;
        query.execute();

        ds = TRsbDataSet(query);
        while( ds.MoveNext() and (stat == 0) )
           FD = SPFirstDocFI(DLDOC_ISSUE, SQL_ConvTypeInteger(ds.FIID), SQL_ConvTypeInteger(ds.FIID), -1, {OurBank});
           if( SQL_ConvTypeStr(ds.Code) == "-ПО ДК" )
              Categ = "-ПО ДК 2014";
           elif( SQL_ConvTypeStr(ds.Code) == "+ПО ДК" )
              Categ = "+ПО ДК 2014";
           end;

           if( not FD.OpenAccount(Categ, null, true, null, null, DateUpg, NATCUR, null, null, null, SQL_ConvTypeStr(ds.Account)) )
              println("Счёт " + SQL_ConvTypeStr(ds.Account) + " не перепривязан");
           end;
        end;
     end;
  end;

  if( stat == 0 )
     query = RSDCommand( " SELECT Tick.t_DealID, Rq1.t_ID RQID, Rq1.t_FIID FIID " +
                         "   FROM DDL_TICK_DBT Tick, DDLRQ_DBT Rq1, DDLRQ_DBT Rq2 " +
                         "  WHERE Tick.t_BOfficeKind = ? " +
                         "    AND Tick.t_DealStatus  = ? " +
                         "    AND Tick.t_ClientID    = -1 " +
                         "    AND Rq1.t_DocKind      = Tick.t_BOfficeKind " +
                         "    AND Rq1.t_DocID        = Tick.t_DealID " +
                         "    AND Rq1.t_Type         = ? " +
                         "    AND Rq1.t_DealPart     = 1 " +
                         "    AND Rq1.t_Kind         = ? " +
                         "    AND Rq1.t_State        = ? " +
                         "    AND Rq1.t_FactDate     > TO_DATE('01-01-0001','DD-MM-YYYY') " +
                         "    AND Rq1.t_FactDate     < TO_DATE('01-11-2014','DD-MM-YYYY') " +
                         "    AND Rq2.t_DocKind      = Tick.t_BOfficeKind " +
                         "    AND Rq2.t_DocID        = Tick.t_DealID " +
                         "    AND Rq2.t_Type         = ? " +
                         "    AND Rq2.t_DealPart     = 2 " +
                         "    AND Rq2.t_Kind         = ? " +
                         "    AND Rq2.t_State        != ? " +
                         "    AND Rq2.t_PlanDate     >= TO_DATE('01-11-2014','DD-MM-YYYY') " +
                         " GROUP BY Tick.t_DealID, Rq1.t_ID, Rq1.t_FIID " );
     query.NullConversion = true;
     query.addParam("", RSDBP_IN, DL_SECURITYDOC);
     query.addParam("", RSDBP_IN, DL_READIED);
     query.addParam("", RSDBP_IN, DLRQ_TYPE_DELIVERY);
     query.addParam("", RSDBP_IN, DLRQ_KIND_COMMIT);
     query.addParam("", RSDBP_IN, DLRQ_STATE_EXEC);
     query.addParam("", RSDBP_IN, DLRQ_TYPE_DELIVERY);
     query.addParam("", RSDBP_IN, DLRQ_KIND_REQUEST);
     query.addParam("", RSDBP_IN, DLRQ_STATE_EXEC);
     query.execute();

     ds = TRsbDataSet(query);
     while( ds.MoveNext() and (stat == 0) )
        query_ = RSDCommand( " SELECT grdeal.t_DocID, grdeal.t_FIID " +
                             "   FROM ddlgracc_dbt gracc, ddlgrdeal_dbt grdeal " +
                             "  WHERE grdeal.t_DocKind  = ? " +
                             "    AND grdeal.t_DocID    = ? " +
                             "    AND grdeal.t_TemplNum = ? " +
                             "    AND gracc.t_GrDealID  = grdeal.t_ID " +
                             "    AND gracc.t_AccNum    = ? " +
                             "    AND gracc.t_State     = ? " +
                             "    AND grdeal.t_FIID     = ? " );
        query_.NullConversion = true;
        query_.addParam("", RSDBP_IN, DL_SECURITYDOC);
        query_.addParam("", RSDBP_IN, SQL_ConvTypeInteger(ds.DealID));
        query_.addParam("", RSDBP_IN, DLGR_TEMPL_RECDELIVERY);
        query_.addParam("", RSDBP_IN, DLGR_ACCKIND_ACCOUNTING);
        query_.addParam("", RSDBP_IN, DLGRACC_STATE_FACTEXEC);
        query_.addParam("", RSDBP_IN, SQL_ConvTypeInteger(ds.FIID));

        ds_ = TRsbDataSet(query_);
        while( ds_.MoveNext() and (stat == 0) )
           var cmd = RSDCommand("SELECT wrt.t_SumID " +
                                "  FROM dpmwrtsum_dbt wrt " +
                                " WHERE wrt.t_DocKind  = ? " +
                                "   AND wrt.t_DocID    = ? " +
                                "   AND wrt.t_Buy_Sale = ? " +
                                "   AND wrt.t_FIID     = ? " );
           cmd.NullConversion = true;
           cmd.addParam("", RSDBP_IN, DLDOC_PAYMENT);
           cmd.addParam("", RSDBP_IN, SQL_ConvTypeInteger(ds.RQID));
           cmd.addParam("", RSDBP_IN, PM_WRITEOFF_SUM_SALE);
           cmd.addParam("", RSDBP_IN, SQL_ConvTypeInteger(ds_.FIID));
           cmd.execute();

           var RSDQuery = TRsbDataSet(cmd);
           if( RSDQuery.movenext() )
              var SQLQuery = RSDCommand(" select buylot.t_DealID, buylot.t_Portfolio, lnk.t_BalanceCostBuy " +
                                        "   from dpmwrtlnk_dbt lnk, dpmwrtsum_dbt buylot " +
                                        "  where buylot.t_SumID     = lnk.t_BuyID " +
                                        "    and lnk.t_SaleID       = ? " +
                                        "    and buylot.t_Portfolio = ? ");
              SQLQuery.NullConversion = true;
              SQLQuery.addParam("", RSDBP_IN, SQL_ConvTypeInteger(RSDQuery.SumID));
              SQLQuery.addParam("", RSDBP_IN, KINDPORT_BACK);
              SQLQuery.execute();
             
              var RSD = TRsbDataSet(SQLQuery);
              while( RSD.MoveNext() and (stat == 0) )
                 var CarSumm = SQL_ConvTypeSum(RSD.rec.BalanceCostBuy);
                 if( CarSumm != 0.0 )
                    FD = SPFirstDoc(LEG_KIND_DL_TICK, SQL_ConvTypeInteger(ds.rec.DealID));
                    var FD_buy = SPFirstDoc(LEG_KIND_DL_TICK, SQL_ConvTypeInteger(RSD.rec.DealID));
                    FD.SetCurPFI(SQL_ConvTypeInteger(ds_.FIID));
                    FD_buy.SetCurPFI(SQL_ConvTypeInteger(ds_.FIID));

                    if( FD_buy.IsExistAccount("ВнебалСчетКорресп", null, true, GetFIRoleByPortfolio(RSD.rec.Portfolio), CredCatAcc, null, DateUpg, NATCUR) )
                       if( not ПроводкаПоКатегориямУчета( FD,
                                                          IIF(IsBasket(FD.Group), "Ц/б, ПВО_БПП, Корзина", "Ц/б, ПВО_БПП"), CredCatAcc, /* КатегДеб, КатегКредит */
                                                          DateUpg,                  /* Дата */
                                                          3,                        /* Глава */
                                                          FD.FaceFIID(),            /* Валюта */
                                                          CarSumm,                  /* Сумма */
                                                          null,
                                                          INPCARRY,                 /* Result_Carry */
                                                          " 1",                     /* Kind_Oper */
                                                          "",                       /* Numb_Document */
                                                          "Перевод стоимости ц/б",
                                                          null,
                                                          FIROLE_BA,
                                                          GetFIRoleByPortfolio(RSD.rec.Portfolio),
                                                          FD.FaceFIID(), NATCUR
                                                        )
                         )
                          println("Ошибка при выполнении проводки");
                          stat = 1;
                       end;
                    end;

                 end;
              end;
           else
              println("Не найден лот по первой части сделки с DealID=" + string(SQL_ConvTypeInteger(ds.DealID)) + " и бумагой FIID=" + string(SQL_ConvTypeInteger(ds_.FIID)));
           end;
        end;
     end;
  end;

  /* Проводки по учёту переоценки, относящейся к прошлым отчётным периодам (ранее 2014) */
  if( stat == 0 )
  query = RSDCommand( " SELECT fin.t_FIID, fin.t_FaceValueFI, wrthist.t_Date, wrthist.t_Amount " +
                      "   FROM dfininstr_dbt fin, v_scwrthistex wrthist " +
                      "  WHERE wrthist.t_Portfolio = ? " +
                      "    AND wrthist.t_Date < TO_DATE('01-01-2014','DD-MM-YYYY') " +
                      "    AND wrthist.t_State = ? " +
                      "    AND fin.t_FIID = wrthist.t_FIID " +
                      "    AND fin.t_FI_Kind = ? " +
                      "    AND fin.t_FaceValueFI != ? " +
                      "    AND wrthist.t_Instance = ( SELECT max(hist.t_Instance) " +
                      "                                 FROM v_scwrthistex hist " +
                      "                                WHERE hist.t_SumID = wrthist.t_SumID " +
                      "                                  AND hist.t_ChangeDate < TO_DATE('01-01-2014','DD-MM-YYYY') " +
                      "                             ) " +
                      " ORDER BY fin.t_FIID " );
  query.NullConversion = true;
  query.addParam("", RSDBP_IN, KINDPORT_CONTR);
  query.addParam("", RSDBP_IN, PM_WRTSUM_FORM);
  query.addParam("", RSDBP_IN, FIKIND_AVOIRISS);
  query.addParam("", RSDBP_IN, NATCUR);
  query.addParam("", RSDBP_IN, KINDPORT_CONTR);
  query.execute();

  ds = TRsbDataSet(query);
  while( ds.MoveNext() and (stat == 0) )
     FIID = SQL_ConvTypeInteger(ds.FIID);

     if( (PrevFIID != FIID) and (PrevFIID != ALLFININSTR) and (SumCarry != $0.0) )
        FD = SPFirstDocFI(DLDOC_ISSUE, PrevFIID, PrevFIID, -1, {OurBank});
        var СчетДебетFIID:integer = NATCUR, СчетКредитFIID:integer = NATCUR;
        if( SumCarry > 0 )
           stat  = ПолучитьСчет(Acc, FD.fininstr.rec.FaceValueFI, 0);
        elif( SumCarry < 0 )
           stat = ПолучитьСчет(Acc, FD.fininstr.rec.FaceValueFI, 1);
        end;

        if( stat == 0 )
           if( not ПроводкаПоКатегориямУчета( FD,
                                              IIF(SumCarry > 0, Acc, "Наш портфель ПКУ, ц/б"), IIF(SumCarry > 0, "Наш портфель ПКУ, ц/б", Acc),
                                              DateUpg,                             /* Дата */
                                              1,                                   /* Глава */
                                              NATCUR,                              /* Валюта */
                                              Abs(SumCarry),                       /* Сумма */
                                              null,                                /* Документ */
                                              INPCARRY,                            /* Result_Carry */
                                              " 1",                                /* Kind_Oper */
                                              "",                                  /* Numb_Document */
                                              "",                                  /* Ground */
                                              null, null, null,
                                              IIF(SumCarry > 0, Acc.Code_Currency, NATCUR), IIF(SumCarry > 0, NATCUR, Acc.Code_Currency)
                                            )
             )
              println("Ошибка при выполнении проводки");
              stat = 1;
           end;
        else
           println("Ошибка при получении счета переоценки");
        end;

        SumCarry = $0.0;
     end;

     Sum1 = $0.0; Sum2 = $0.0;
     if( stat == 0 )
        if( not SmartConvertSumDbl(Sum1, 1.0, date(1,1,2014), SQL_ConvTypeInteger(ds.FaceValueFI), NATCUR, false) )
           println("Ошибка при получении курса " + ПолучитьКодФинИн(SQL_ConvTypeInteger(ds.FaceValueFI)) + " к " + ПолучитьКодФинИн(NATCUR) + " на 1.1.2014");
           stat = 1;
        end;
     end;
     if( stat == 0 )
        if( not SmartConvertSumDbl(Sum2, 1.0, SQL_ConvTypeDate(ds.Date), SQL_ConvTypeInteger(ds.FaceValueFI), NATCUR, false) )
           println("Ошибка при получении курса " + ПолучитьКодФинИн(SQL_ConvTypeInteger(ds.FaceValueFI)) + " к " + ПолучитьКодФинИн(NATCUR) + " на " + string(SQL_ConvTypeDate(ds.Date)) );
           stat = 1;
        end;
     end;

     if( stat == 0 )
        SumCarry = SumCarry + (Sum1 - Sum2) * SQL_ConvTypeSum(ds.Amount);
        PrevFIID = FIID;
     end;
  end;
  end;

  /* Проводки по учёту переоценки текущего года (2014) */
  if( stat == 0 )
     var UnrealizedCatNumPlus:string = "", UnrealizedCatNumMinus:string = "";
     query_ = RSDCommand( " SELECT (SELECT categ.t_Code FROM dmccateg_dbt categ WHERE categ.t_LevelType = ? and categ.t_Number = obchaptr.t_UnrealizedCatNumPlus) UnrealizedCatNumPlus , " +
                          "        (SELECT categ.t_Code FROM dmccateg_dbt categ WHERE categ.t_LevelType = ? and categ.t_Number = obchaptr.t_UnrealizedCatNumMinus) UnrealizedCatNumMinus " +
                          "   FROM DOBCHAPTR_DBT obchaptr " +
                          "  WHERE obchaptr.t_Chapter = ? " );

     query_.NullConversion = true;
     query_.addParam("", RSDBP_IN, 1/*MCCATEG_LEVEL_TYPE_CATEGORY*/);
     query_.addParam("", RSDBP_IN, 1/*MCCATEG_LEVEL_TYPE_CATEGORY*/);
     query_.addParam("", RSDBP_IN, 1/*CHAPT1*/);
     query_.execute();

     ds_ = TRsbDataSet(query_);
     if( ds_.MoveNext() )
        UnrealizedCatNumPlus  = SQL_ConvTypeStr(ds_.UnrealizedCatNumPlus);
        UnrealizedCatNumMinus = SQL_ConvTypeStr(ds_.UnrealizedCatNumMinus);
     else
        println("Не найдены КУ нереализованных курсовых разниц");
        stat = 1;
     end;

     if( stat == 0 )
        PrevFIID = ALLFININSTR;
        SumCarry = $0.0;

        query = RSDCommand( " SELECT fin.t_FIID, fin.t_FaceValueFI, wrthist.t_Date, wrthist.t_Amount " +
                            "   FROM dfininstr_dbt fin, v_scwrthistex wrthist " +
                            "  WHERE wrthist.t_Portfolio = ? " +
                            "    AND wrthist.t_Date < TO_DATE('01-11-2014','DD-MM-YYYY') " +
                            "    AND wrthist.t_State = ? " +
                            "    AND fin.t_FIID = wrthist.t_FIID " +
                            "    AND fin.t_FI_Kind = ? " +
                            "    AND fin.t_FaceValueFI != ? " +
                            "    AND wrthist.t_Instance = ( SELECT max(hist.t_Instance) " +
                            "                                 FROM v_scwrthistex hist " +
                            "                                WHERE hist.t_SumID = wrthist.t_SumID " +
                            "                                  AND hist.t_ChangeDate < TO_DATE('01-11-2014','DD-MM-YYYY') " +
                            "                             ) " +
                            " ORDER BY fin.t_FIID " );
        query.NullConversion = true;
        query.addParam("", RSDBP_IN, KINDPORT_CONTR);
        query.addParam("", RSDBP_IN, PM_WRTSUM_FORM);
        query.addParam("", RSDBP_IN, FIKIND_AVOIRISS);
        query.addParam("", RSDBP_IN, NATCUR);
        query.addParam("", RSDBP_IN, KINDPORT_CONTR);
        query.execute();
       
        ds = TRsbDataSet(query);
        while( ds.MoveNext() and (stat == 0) )
           FIID = SQL_ConvTypeInteger(ds.FIID);
       
           if( (PrevFIID != FIID) and (PrevFIID != ALLFININSTR) and (SumCarry != $0.0) )
              FD = SPFirstDocFI(DLDOC_ISSUE, PrevFIID, PrevFIID, -1, {OurBank});
              var FD2 = PDocOverEstimate(1, SQL_ConvTypeInteger(ds.FaceValueFI));
              if( not FD2.OpenAccount(IIF(SumCarry > 0, UnrealizedCatNumPlus, UnrealizedCatNumMinus), null, true, null, Acc, DateUpg, SQL_ConvTypeInteger(ds.FaceValueFI)) )
                 println("Ошибка открытия счета");
              end;

              if( not ПроводкаПоКатегориямУчета( FD,
                                                 IIF(SumCarry > 0, Acc, "Наш портфель ПКУ, ц/б"), IIF(SumCarry > 0, "Наш портфель ПКУ, ц/б", Acc),
                                                 DateUpg,                             /* Дата */
                                                 1,                                   /* Глава */
                                                 NATCUR,                              /* Валюта */
                                                 Abs(SumCarry),                       /* Сумма */
                                                 null,                                /* Документ */
                                                 INPCARRY,                            /* Result_Carry */
                                                 " 1",                                /* Kind_Oper */
                                                 "",                                  /* Numb_Document */
                                                 "",                                  /* Ground */
                                                 null, null, null,
                                                 IIF(SumCarry > 0, SQL_ConvTypeInteger(ds.FaceValueFI), NATCUR), IIF(SumCarry > 0, NATCUR, SQL_ConvTypeInteger(ds.FaceValueFI))
                                               )
                )
                 println("Ошибка при выполнении проводки");
                 stat = 1;
              end;

              SumCarry = $0.0;
           end;
       
           Sum1 = $0.0; Sum2 = $0.0;
           if( stat == 0 )
              if( not SmartConvertSumDbl(Sum1, 1.0, date(1,11,2014), SQL_ConvTypeInteger(ds.FaceValueFI), NATCUR, false) )
                 println("Ошибка при получении курса " + ПолучитьКодФинИн(SQL_ConvTypeInteger(ds.FaceValueFI)) + " к " + ПолучитьКодФинИн(NATCUR) + " на 1.11.2014");
                 stat = 1;
              end;
           end;
           if( stat == 0 )
              if( not SmartConvertSumDbl(Sum2, 1.0, date(1,1,2014), SQL_ConvTypeInteger(ds.FaceValueFI), NATCUR, false) )
                 println("Ошибка при получении курса " + ПолучитьКодФинИн(SQL_ConvTypeInteger(ds.FaceValueFI)) + " к " + ПолучитьКодФинИн(NATCUR) + " на 1.1.2014");
                 stat = 1;
              end;
           end;
       
           if( stat == 0 )
              SumCarry = SumCarry + (Sum1 - Sum2) * SQL_ConvTypeSum(ds.Amount);
              PrevFIID = FIID;
           end;
        end;
     end;
  end;

  if( begin_transaction )
     if( stat != 0 )
        RslDefCon.RollbackTrans();
     else
        begin_transaction = false;
        RslDefCon.CommitTrans();
     end;
  end;

  return stat;

OnError( err )
  if( begin_transaction )
     RslDefCon.RollbackTrans();
     return 1;
  end;
end;

if( Upgrade() != 0 )
   println("Обновление не выполнено!");
else
   println("Обновление выполнено!");
end;
