/*******************************************************************************
 DESCRIPTION  :   Обновление 2027->2028 (302-П)
                  Процедура выгрузки остатков
 PROGRAMMED BY:   Иванов А.И.
 CREATION DATE:   22.11.07
*******************************************************************************/
import BankInter, "DlRepForm_h.mac", "globals.mac", "cb_sql.mac", "secinter.mac", "spserv.mac";
import DealsInter, OprInter, SecInter, RsbDataSet, "spserv.mac", "sp_class.mac", "sprepfun.mac";

/*Номера полей в форме */
CONST PNFLD_NTGREG_DATE         = 0,
      PNFLD_NTGREG_FLAG_1       = 1,
      PNFLD_NTGREG_FLAG_2       = 2,
      PNFLD_NTGREG_FLAG_3       = 3,
      PNFLD_NTGREG_FLAG_31      = 4,
      PNFLD_NTGREG_FLAG_REPORT  = 5;

private macro CreateTables()
  var cmd;
  var Sql = " CREATE TABLE DAVRREST_BUF  " +
            " (  " +
            "    T_DEPARTMENT   NUMBER(5)     DEFAULT -1,  " +
            "    T_FIID         NUMBER(10)    DEFAULT -1,  " +
            "    T_BUYGOAL      NUMBER(5)     DEFAULT -1,  " +
            "    T_PORTFOLIO    NUMBER(5)     DEFAULT -1,  " +
            "    T_AMOUNT       NUMBER(32,12) DEFAULT 0,  " +
            "    T_BALACC       VARCHAR2(26)  DEFAULT chr(1),  " +
            "    T_BALANCE      NUMBER(32,12) DEFAULT 0,  " +
            "    T_OUTACC       VARCHAR2(26)  DEFAULT chr(1),  " +
            "    T_OUTLAY       NUMBER(32,12) DEFAULT 0,  " +
            "    T_NKDACC       VARCHAR2(26)  DEFAULT chr(1),  " +
            "    T_NKD          NUMBER(32,12) DEFAULT 0,  " +
            "    T_COST         NUMBER(32,12) DEFAULT 0  " +
            " );  ";
   cmd = RSDCommand(Sql);
   cmd.Execute();
      Sql = " CREATE INDEX DAVRREST_BUF_IDX0 ON DAVRREST_BUF  " +
            " (T_DEPARTMENT, T_FIID, T_BUYGOAL, T_PORTFOLIO);  ";
   cmd = RSDCommand(Sql);
   cmd.Execute();
      Sql = " CREATE TABLE DDLREPO_BUF  " +
            " (  " +
            "    T_DEALID       NUMBER(10)     DEFAULT -1,  " +
            "    T_OPERKIND     NUMBER(5)      DEFAULT -1,  " +
            "    T_STATE1       NUMBER(5)      DEFAULT -1,  " +
            "    T_STATE2       NUMBER(5)      DEFAULT -1,  " +
            "    T_BITMASK1     NUMBER(10)     DEFAULT 0,  " +
            "    T_BITMASK2     NUMBER(10)     DEFAULT 0,  " +
            "    T_OWN          CHAR(1)        DEFAULT chr(0),  " +
            "    T_ORCB         CHAR(1)        DEFAULT chr(0),  " +
            "    T_OUTACC       VARCHAR2(26)   DEFAULT chr(1),  " +
            "    T_OUTLAY       NUMBER(32,12)  DEFAULT 0,  " +
            "    T_PFDACC1      VARCHAR2(26)   DEFAULT chr(1),  " +
            "    T_PFDSUM1      NUMBER(32,12)  DEFAULT 0,  " +
            "    T_MFDACC1      VARCHAR2(26)   DEFAULT chr(1),  " +
            "    T_MFDSUM1      NUMBER(32,12)  DEFAULT 0,  " +
            "    T_OVR1         NUMBER(32,12)  DEFAULT 0,  " +
            "    T_FMACC1       VARCHAR2(26)   DEFAULT chr(1),  " +
            "    T_PFDACC2      VARCHAR2(26)   DEFAULT chr(1),  " +
            "    T_PFDSUM2      NUMBER(32,12)  DEFAULT 0,  " +
            "    T_MFDACC2      VARCHAR2(26)   DEFAULT chr(1),  " +
            "    T_MFDSUM2      NUMBER(32,12)  DEFAULT 0,  " +
            "    T_OVR2         NUMBER(32,12)  DEFAULT 0,  " +
            "    T_FMACC2       VARCHAR2(26)   DEFAULT chr(1),  " +
            "    T_BALACC       VARCHAR2(26)   DEFAULT chr(1),  " +
            "    T_BALANCE      NUMBER(32,12)  DEFAULT 0,  " +
            "    T_NKDACC       VARCHAR2(26)   DEFAULT chr(1),  " +
            "    T_NKD          NUMBER(32,12)  DEFAULT 0,  " +
            "    T_NOTSALEDCOST NUMBER(32,12)  DEFAULT 0,  " +
            "    T_SALED        NUMBER(32,12)  DEFAULT 0,  " +
            "    T_SALEDCOST    NUMBER(32,12)  DEFAULT 0,  " +
            "    T_FINRES       NUMBER(32,12)  DEFAULT 0   " +
            " );  ";
   cmd = RSDCommand(Sql);
   cmd.Execute();
      Sql = " CREATE UNIQUE INDEX DDLREPO_BUF_IDX0 ON DDLREPO_BUF  " +
            " (T_DEALID);  ";
   cmd = RSDCommand(Sql);
   cmd.Execute();
      Sql = " CREATE INDEX DDLREPO_BUF_IDX1 ON DDLREPO_BUF  " +
            " (T_OWN, T_ORCB);  ";
   cmd = RSDCommand(Sql);
   cmd.Execute();
      Sql = " CREATE TABLE DCOMSUM_BUF  " +
            " (  " +
            "    T_DEALID   NUMBER(10),  " +
            "    T_KIND     NUMBER(5),  " +
            "    T_DATE     DATE,  " +
            "    T_SUM      NUMBER(32,12),  " +
            "    T_NDS      NUMBER(32,12),  " +
            "    T_CUR      NUMBER(10),  " +
            "    T_PAYCURR  NUMBER(10)  " +
            " );  ";
   cmd = RSDCommand(Sql);
   cmd.Execute();
      Sql = " CREATE UNIQUE INDEX DCOMSUM_BUF_IDX0 ON DCOMSUM_BUF  " +
            " (T_DEALID, T_KIND); ";
   cmd = RSDCommand(Sql);
   cmd.Execute();

  OnError(err);
end;



PRIVATE MACRO ПосчитатьЗатратыНаКомиссииПосреднику( FD, dat, _SumComm )

/*Сумма затрат по комиссии вычисляется следующим образом. Берется сумма комиссии с лота.  */
/*Если комиссия через тр. счет, то сумма конвертится в ВН по дате поставки                */
/*иначе:                                                                                  */
/*-  если дата комиссии не равна дате поставки, то сумма сначала конвертится в руб по дате курса конвертации баз. в фикс. из платежа, а затем в ВН по курсу даты поставки*/
/*-  иначе:                                                                               */   
/*1) если ВН равна валюте комиссии, то берется сумма из лота                              */
/*2) иначе сумма  сначала конвертится в руб (по курсу за дату баз. в фикс), а затем в ВН  (по курсу фикс. в нефикс) */

   record paym( pmpaym );   
   var SumComm = FD.pmwrtsum.rec.AgentComm, ConvDate, ConvDate2;

   if( SumComm != $0 )
      if( IsClientDeal(FD.tick.rec) OR FD.КомиссияЧерезТранзитныйСчет() )
         if( SmartConvertSum( SumComm, SumComm, dat, FD.pmwrtsum.rec.FIID_AgentComm, FD.FaceFIID, true ) == false )
            return false;
         end;                            
      else
         if( GetPaymentInfo( FD.tick.rec, GetComPaymPurpose( FD ), paym ) )
            ConvDate = paym.BaseRateDate; /*дата конвертации из базового в фиксированный*/
            ConvDate2= paym.RateDate;     /*дата конвертации из  фиксированного в нефиксированный*/
         else 
            ConvDate = ConvDate2 = dat;
         end; 

         if( FD.DateArray[DATE_DEALCOMISS] != dat )
            /*сначала в рубли по дате курса из баз. в фикс.*/
            if( SmartConvertSum( SumComm, SumComm, ConvDate, FD.pmwrtsum.rec.FIID_AgentComm, NATCUR, true ) == false )
               return false;
            end;                            
            /*затем в валюту номинала на дату поставки*/
            if( SmartConvertSum( SumComm, SumComm, dat, NATCUR, FD.FaceFIID, true ) == false )
               return false;
            end;                                 
         else
            if( FD.FaceFIID != FD.pmwrtsum.rec.FIID_AgentComm )
               /*сначала в рубли по дате курса из баз. в фикс.*/
               if( SmartConvertSum( SumComm, SumComm, ConvDate, FD.pmwrtsum.rec.FIID_AgentComm, NATCUR, true ) == false )
                  return false;
               end;                            
               /*затем в валюту номинала по дате курса из фикс. в нефикс.*/
               if( SmartConvertSum( SumComm, SumComm, ConvDate2, NATCUR, FD.FaceFIID, true ) == false )
                  return false;
               end;                                 
            end;            
         end;      
      end; 
   end;

   SetParm( 2, SumComm );
   return true;
END;

private macro ClearTable( Name )
  var cmd = RSDCommand("drop table " + Name + " cascade constraints ");
   cmd.Execute();
  OnError(err);
end;

private macro ПолучитьСчетПоКатегорииДляЦБ( CatNum, Department, FIID, Portfolio, AccBuf:@TRecHandler )
  var cmd, DS;
   cmd = RsdCommand( " select accdoc.t_Account, accdoc.t_Chapter, accdoc.t_Currency " +
                     "   from dfininstr_dbt FI, ddp_dep_dbt Dep, dmcaccdoc_dbt accdoc, dmctempl_dbt templ " +
                     "  where     FI.t_FI_Kind = 2 " +                     
                     "        and accdoc.t_FIID = ? " +
                     "        and accdoc.t_DepartmentID = ? " +
                     "        and accdoc.t_IsCommon = chr(88) " +
                     "        and accdoc.t_CatNum = ? " +
                     "        and accdoc.t_TemplNum = templ.t_Number " +
                     "        and templ.t_Value1 = ? " +
                     "        and templ.t_CatID = accdoc.t_CatID " +
                     " order by Dep.t_Code, FI.t_FIID, templ.t_Value1 " );

   cmd.addParam( "", RSDBP_IN, FIID );
   cmd.addParam( "", RSDBP_IN, Department );
   cmd.addParam( "", RSDBP_IN, CatNum );
   cmd.addParam( "", RSDBP_IN, Portfolio );
   cmd.execute();

   DS = TRsbDataSet( cmd );

   if( DS.movenext() )
      if( not GetAccount( DS.rec.Chapter, DS.rec.Currency, DS.rec.Account, AccBuf, true ) )
         return false;
      end;
   else
      return false;
   end;

   return true;
end;

PRIVATE MACRO FldProc_Flag3( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR RetVal = DL_FldProc_CheckBox( pThis, Cmd, Key, @FldShowValue, @FldRealValue );

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( FldRealValue == "X" ) 
        EnableFields( pThis.Data(), PNFLD_NTGREG_FLAG_31 );
     else 
        DisableFields( pThis.Data(), PNFLD_NTGREG_FLAG_31 );
        pThis.Data.rec.Flag31 = "";
     end;
  end;
  return RetVal;
END;


PRIVATE CLASS (DL_CPanel) SP_OutAccData_Form()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_NTGREG_DATE,        DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate, GetDateAfterWorkDays( DATE(1,1,2008), 0 ) );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_1,      DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,  "X" );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_2,      DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,  "X" );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_3,      DL_PNFLD_CHECKBOX,  @FldProc_Flag3,  "X" );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_31,     DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,  "" );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_REPORT, DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,  "X" );
  END;

  PRIVATE MACRO Check():BOOL

     return true;
  END;

  initDL_CPanel( "sp_conv.lbr", "poutdta" ); 
END;

PRIVATE VAR ReportHeader =
"\n               Процедура выгрузки данных\n" +
"Дата: ##########\n" +
"Сохранять  суммы проводок по переносу остатков л/с: #\n" +
"Сохранять  данные для переквалификации сделок Репо и ОП в сделки Репо БПП: #\n" +
"Сохранять суммы оплаченных комиссий: #\n" +
"  По открытым сделкам/по открытым и закрытым сделкам: #\n";

PRIVATE VAR TableHeader =                                                                                                                                                                                                                                                                                                                                                                                                                                                                                         
"┌─────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n" +
"│                                       Отчет о выполнении                                                        │\n" +
"├─────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤";

PRIVATE CLASS (DL_CReportTemplate) SP_OutAccData

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_STD;
  END;

  PRIVATE MACRO BeginReport()

     PrintFormatString( ReportHeader,
                        "N1_H0_Date",    FormData.OperDate,  
                        "N1_H0_Flag1:l",  IIF( FormData.Flag1  == "X", "Да", "Нет"),  
                        "N1_H0_Flag2:l", IIF( FormData.Flag2 == "X", "Да", "Нет"),  
                        "N1_H0_Flag3:l",  IIF( FormData.Flag3  == "X", "Да", "Нет"),  
                        "N1_H0_Flag31:l", IIF( FormData.Flag31 == "X", "Да", "Нет")
                      );

     RegisterTable( "N1_MainTable", TableHeader, "N1_Action" );
  END;

  PRIVATE MACRO EndReport()
     EndTable();
  END;

  PRIVATE MACRO Message( Mes:STRING)
     if( FormData.CreateReport == "X" )
           PrintTableLine( "N1_Action", Mes,   null );
     end;
  END;

  macro InsertDCOMSUM( DealID, Kind, ComDate, Sum, NDS, Curr, PayCurr )
    var cmd, i = 0;

     cmd = RsdCommand( "insert into dcomsum_buf ( t_DealID, t_Kind, t_Date, t_Sum, t_NDS, t_Cur, t_PayCurr ) " +
                       "                  values ( ?, ?, ?, ?, ?, ?, ? ) " );
     cmd.NullConversion = true;

     cmd.addParam( "", RSDBP_IN, DealID );
     cmd.addParam( "", RSDBP_IN, Kind );
     cmd.addParam( "", RSDBP_IN, ComDate );
     cmd.addParam( "", RSDBP_IN, Sum );
     cmd.addParam( "", RSDBP_IN, NDS );
     cmd.addParam( "", RSDBP_IN, Curr );
     cmd.addParam( "", RSDBP_IN, PayCurr );

     cmd.execute();

     OnError(err);

     Message( "Строка: " + err.line + " | " + err.message );

  end;

  macro InsertDAVRREST( Department, FIID, BuyGoal, Portfolio, Amount, BalAcc, BalAccRest, OutAcc, OutAccRest, NKDAcc, NKDAccRest, Cost )
    var cmd, i = 0;

     cmd = RsdCommand( "insert into davrrest_buf ( t_Department, t_FIID, t_BuyGoal, t_Portfolio, t_Amount, t_BalAcc, t_Balance, t_OutAcc, t_Outlay, t_NKDAcc, t_NKD, t_Cost ) " +
                       "                  values ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? ) " );
     cmd.NullConversion = true;

     cmd.addParam( "", RSDBP_IN, Department );
     cmd.addParam( "", RSDBP_IN, FIID );
     cmd.addParam( "", RSDBP_IN, BuyGoal );
     cmd.addParam( "", RSDBP_IN, Portfolio );
     cmd.addParam( "", RSDBP_IN, Amount );
     cmd.addParam( "", RSDBP_IN, BalAcc );
     cmd.addParam( "", RSDBP_IN, BalAccRest );
     cmd.addParam( "", RSDBP_IN, OutAcc );
     cmd.addParam( "", RSDBP_IN, OutAccRest );
     cmd.addParam( "", RSDBP_IN, NKDAcc );
     cmd.addParam( "", RSDBP_IN, NKDAccRest );
     cmd.addParam( "", RSDBP_IN, Cost );

     cmd.execute();


     Message( "Выгружен счет: " + BalAcc + " остаток: " + string(BalAccRest) + "\nОстаток стоимости покупки: " + string(Cost) );
          
     OnError(err);

     Message( "!!! Ошибка при сохранении счета: " + BalAcc + " остаток: " + string(BalAccRest) );

  end;

  macro InsertDDLREPO( DealID, OperKind, State1, State2, BitMask1, BitMask2, IsOwn, IsORCB, OutAcc, Outlay, PFDAcc1, PFDSum1,
                               MFDAcc1, MFDSum1, Ovr1, FMAcc1, PFDAcc2, PFDSum2, MFDAcc2, MFDSum2, Ovr2, FMAcc2, BalAcc, Balance, NKDAcc, NKD,
                               NotSaledCost, Saled, SaledCost, FinRes )
    var cmd, i = 0;

     cmd = RsdCommand( "insert into ddlrepo_buf ( t_DealID, t_OperKind, t_State1, t_State2, t_BitMask1, t_BitMask2, t_Own, t_ORCB, t_OutAcc, t_Outlay, " +
                       "                          t_PFDAcc1, t_PFDSum1, t_MFDAcc1, t_MFDSum1, t_Ovr1, t_FMAcc1, t_PFDAcc2, t_PFDSum2, t_MFDAcc2, t_MFDSum2, " +
                       "                          t_Ovr2, t_FMAcc2, t_BalAcc, t_Balance, t_NKDAcc, t_NKD, t_NotSaledCost, t_Saled, t_SaledCost, t_FinRes ) " +
                       "                  values ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? ) " );

     cmd.NullConversion = true;

     cmd.addParam( "", RSDBP_IN, DealID );
     cmd.addParam( "", RSDBP_IN, OperKind );
     cmd.addParam( "", RSDBP_IN, State1 );
     cmd.addParam( "", RSDBP_IN, State2 );
     cmd.addParam( "", RSDBP_IN, BitMask1 );
     cmd.addParam( "", RSDBP_IN, BitMask2 );
     cmd.addParam( "", RSDBP_IN, IsOwn );
     cmd.addParam( "", RSDBP_IN, IsORCB );
     cmd.addParam( "", RSDBP_IN, OutAcc );
     cmd.addParam( "", RSDBP_IN, Outlay );
     cmd.addParam( "", RSDBP_IN, PFDAcc1 );
     cmd.addParam( "", RSDBP_IN, PFDSum1 );
     cmd.addParam( "", RSDBP_IN, MFDAcc1 );
     cmd.addParam( "", RSDBP_IN, MFDSum1 );
     cmd.addParam( "", RSDBP_IN, Ovr1 );
     cmd.addParam( "", RSDBP_IN, FMAcc1 );
     cmd.addParam( "", RSDBP_IN, PFDAcc2 );
     cmd.addParam( "", RSDBP_IN, PFDSum2 );
     cmd.addParam( "", RSDBP_IN, MFDAcc2 );
     cmd.addParam( "", RSDBP_IN, MFDSum2 );
     cmd.addParam( "", RSDBP_IN, Ovr2 );
     cmd.addParam( "", RSDBP_IN, FMAcc2 );
     cmd.addParam( "", RSDBP_IN, BalAcc );
     cmd.addParam( "", RSDBP_IN, Balance );
     cmd.addParam( "", RSDBP_IN, NKDAcc );
     cmd.addParam( "", RSDBP_IN, NKD );
     cmd.addParam( "", RSDBP_IN, NotSaledCost );
     cmd.addParam( "", RSDBP_IN, Saled );
     cmd.addParam( "", RSDBP_IN, SaledCost );
     cmd.addParam( "", RSDBP_IN, FinRes );

     cmd.execute();

     OnError(err);

     Message( "Строка: " + err.line + " | " + err.message );

  end;

  MACRO GetLotData( FIID:INTEGER, Portfolio:INTEGER, BuyGoal:INTEGER, Department:INTEGER, BalAcc:STRING, Amount:@MONEY, Sum:@MONEY )
     VAR RS, cmd = RSDCommand( "SELECT SUM(t_Amount) Amount, SUM(t_sum) LotSum"+
                               "  FROM DPMWRTSUM_DBT "+
                               " WHERE     t_Party = -1 "+
                               "       AND t_FIID  = ? "+
                               "       AND t_Department = ? "+
                               IIF( BuyGoal > 0," AND t_BuyGoal = ? ","")+
                               "       AND t_State >= 40 "+
                               "       AND t_Buy_Sale = 0 "+
                               "       AND t_GroupID IN (SELECT t_GroupID FROM DPMWRTGRP_DBT WHERE t_Party = -1 AND t_Type = ? AND t_Balance = ?) "
                             );
     cmd.addParam( "", RSDBP_IN, FIID );
     cmd.addParam( "", RSDBP_IN, Department );
     if( BuyGoal > 0 )
        cmd.addParam( "", RSDBP_IN, BuyGoal );
     end;
     cmd.addParam( "", RSDBP_IN, Portfolio );
     cmd.addParam( "", RSDBP_IN, BalAcc );
     cmd.execute();

     RS = TRsbDataSet(cmd);
     if( RS.MoveNext() )
        Amount = round(SQL_ConvTypeSum(RS.Amount),2);
        Sum    = round(SQL_ConvTypeSum(RS.LotSum),2);
     else
        Amount = 0;
        Sum    = 0;
     end;
  END;

  macro ВыгрузитьОстатки( dat )
    var cmd, DS, i = 0, Amount, Sum;
    var AccBuf1 = TRecHandler("account"),
        AccBuf2 = TRecHandler("account"),
        AccBuf3 = TRecHandler("account"); 
    var RestAcc1 = $0,
        RestAcc2 = $0,
        RestAcc3 = $0;

     cmd = RsdCommand( " select Dep.t_Code Department, FI.t_FIID FIID, FI.t_FaceValueFI FaceValueFI, templ.t_Value1 BuyGoal, templ.t_Value2 Portfolio, accdoc.t_Account, accdoc.t_Chapter, accdoc.t_Currency " +
                       "   from dfininstr_dbt FI, ddp_dep_dbt Dep, dmcaccdoc_dbt accdoc, dmctempl_dbt templ " +
                       "  where     FI.t_FI_Kind = 2 " +                     
                       "        and accdoc.t_FIID = FI.t_FIID " +
                       "        and accdoc.t_DepartmentID = Dep.t_Code " +
                       "        and accdoc.t_IsCommon = chr(88) " +
                       "        and accdoc.t_CatNum = 231 " +
                       "        and accdoc.t_TemplNum = templ.t_Number " +
//                       "        and templ.t_Value1 in ("+BUYGOAL_RESALE+", "+BUYGOAL_INVEST+") " +
                       "        and templ.t_Value2 in ("+KINDPORT_TRADE+", "+KINDPORT_INVEST+") " +
                       "        and templ.t_CatID = accdoc.t_CatID " +
                       " order by Dep.t_Code, FI.t_FIID, templ.t_Value1, templ.t_Value2 " );
     cmd.execute();
     DS = TRsbDataSet( cmd );
     
     InitProgress( -1, "Выгрузка остатков по счетам", "Выгрузка остатков по счетам" );

     while( DS.movenext() );

        RestAcc1 = 0;
        AccBuf1.Clear();
        AccBuf2.Clear();
        AccBuf3.Clear();

        if( GetAccount( DS.rec.Chapter, DS.rec.Currency, DS.rec.Account, AccBuf1, true ) )
           RestAcc1 = abs(GetRestAccount( AccBuf1.rec, dat ));
        end;

        if( abs(RestAcc1) > $0)
/*
           if( not WRT_TotalCalc( {OurBank}, DS.rec.FIID, DS.rec.Portfolio, -1, -1, dat, DS.rec.BuyGoal, calcwrt, false, PM_WRTOFF_CALC_FORMANDLARGER, null, null, DS.rec.Department ) )
              Message( "Немогу выполнить WRT_TotalCalc" );
           end;
*/
           GetLotData( DS.rec.FIID, DS.rec.Portfolio, IIF(DS.rec.BuyGoal < 0, 0, DS.rec.BuyGoal), DS.rec.Department, AccBuf1.rec.Balance, @Amount, @Sum );

           SmartConvertSum(Sum, Sum, dat, DS.rec.Currency, DS.rec.FaceValueFI, false);

           /* Затраты ц/б */
           if( ПолучитьСчетПоКатегорииДляЦБ( 164, DS.rec.Department, DS.rec.FIID, DS.rec.Portfolio, AccBuf2 ) )
              RestAcc2 = abs(GetRestAccount( AccBuf2.rec, dat ));
           else
              RestAcc2 = $0;
           end;
           /* -НКД */ 
           if( ПолучитьСчетПоКатегорииДляЦБ( 161, DS.rec.Department, DS.rec.FIID, DS.rec.Portfolio, AccBuf3 ) )
              RestAcc3 = abs(GetRestAccount( AccBuf3.rec, dat ));
           else
              RestAcc3 = $0;
           end;

           InsertDAVRREST( DS.rec.Department, DS.rec.FIID, DS.rec.BuyGoal, DS.rec.Portfolio, Amount, AccBuf1.rec.Account, RestAcc1, AccBuf2.rec.Account, RestAcc2, AccBuf3.rec.Account, RestAcc3, Sum );
           UseProgress(i=i+1);
        end;
     end;

     RemProgress();

  end;

  macro ВыгрузитьРЕПО( dat )
    var cmd, DS, i = 0;
    var FD1, FD2;
    var IsOwn, IsORCB, IsOutBal1, IsOutBal2;
    var OutAccBuf  = TRecHandler("account"),
        PFDAccBuf1 = TRecHandler("account"),
        MFDAccBuf1 = TRecHandler("account"),
        FMAccBuf1  = TRecHandler("account"),
        PFDAccBuf2 = TRecHandler("account"),
        MFDAccBuf2 = TRecHandler("account"),
        FMAccBuf2  = TRecHandler("account"),
        BalAccBuf  = TRecHandler("account"),
        NKDAccBuf  = TRecHandler("account");
    var PFDAccCat1 = "",
        MFDAccCat1 = "",
        FMAccCat1  = "",
        FMAccCat2  = "";
    var Outlay = $0,
        PFDSum1 = $0,
        MFDSum1 = $0,
        Ovr1    = $0,
        TotalCost1 = $0,
        PFDSum2 = $0,
        MFDSum2 = $0,
        Ovr2    = $0,
        TotalCost2 = $0,
        Balance = $0,
        NKD     = $0,
        NotSaledCost = $0,
        Saled   = $0,
        SaledCost = $0,
        FinRes  = $0;
    var WriteOffBalanceCost = $0,
        OutLayTmp  = $0,
        OutlayConv = $0,
        AgentSumComm = $0;


     private macro GetWRTLink( DocKind, DocID, PartNum, dat, _Saled:@money, _SaledCost:@money )
       var cmd, DS;

        cmd = RsdCommand( "select NVL(sum(lnk.t_Amount),0) Saled, NVL(sum(lnk.t_BalanceCost),0) SaledCost " +
                          "  from dpmwrtlnk_dbt lnk " +
                          " where     lnk.t_DocKindBuy = ? " +
                          "       and lnk.t_DocIDBuy = ? " + 
                          "       and lnk.t_PartNumBuy = ? " +
                          "       and lnk.t_CreateDate <= ? " +
                          "       and lnk.t_DocKindSale <> 117 ");

        cmd.addParam( "", RSDBP_IN, DocKind );
        cmd.addParam( "", RSDBP_IN, DocID);
        cmd.addParam( "", RSDBP_IN, PartNum );
        cmd.addParam( "", RSDBP_IN, dat );
        cmd.execute();

        DS = TRsbDataSet( cmd );

        if( DS.movenext() )
           _Saled = DS.rec.Saled;
           _SaledCost = DS.rec.SaledCost;
        else
           return false;
        end;

        return true;

        OnError(err);

        Message( "Строка: " + err.line + " | " + err.message );

     end;


     cmd = RsdCommand( "select distinct tick.t_DealID, tick.t_DealType " +
                       "  from ddl_tick_dbt tick " +
                       " where   ( (rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1) " +
                       "       or  (rsb_secur.IsBackSale(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1) )" +
                       "       and tick.t_DealStatus = 10 " );

     cmd.execute();

     DS = TRsbDataSet( cmd );

     InitProgress( -1, "Выгрузка сделок РЕПО", "Выгрузка сделок РЕПО" );

     while( DS.movenext() )

        FD1 = SPFirstDoc( LEG_KIND_DL_TICK,       DS.rec.DealID );
        FD2 = SPFirstDoc( LEG_KIND_DL_TICK_BACK,  DS.rec.DealID );

        TotalCost1 = $0;
        TotalCost2 = $0;

        if( FD1.СделкаОРЦБ() )
           IsORCB = SET_CHAR;
        else
           IsORCB = UNSET_CHAR;
        end;

        if( not IsClientDeal(FD1.tick.rec) )
           IsOwn = SET_CHAR;
        else
           IsOwn = UNSET_CHAR;
        end;

        if( GetLotStateOnDate( FD1.pmwrtsum.rec, dat ) == PM_WRTSUM_OUTBAL )
           IsOutBal1 = true;
        else
           IsOutBal1 = false;
        end;

        if( GetLotStateOnDate( FD2.pmwrtsum.rec, dat ) == PM_WRTSUM_OUTBAL )
           IsOutBal2 = true;
        else
           IsOutBal2 = false;
        end;

        if( IsOwn == SET_CHAR )

          if( FD1.IsExistAccount( "Затраты, договор", null, true, null, OutAccBuf, null, dat ) )
             Outlay = abs(GetRestAccount( OutAccBuf.rec, dat ));
          else
             Outlay = $0;
             OutAccBuf.Clear();
          end;

          if( FD1.ВидСрочностиСделки() == СделкаСрочная )
             PFDAccCat1 = "+Форвард, дрейф";
          
          else
             PFDAccCat1 = "+Форвард, кэш";
          end;

          if( FD1.IsExistAccount( PFDAccCat1, null, true, null, PFDAccBuf1, null, dat ) and IsOutBal1 )
             PFDSum1 = abs(GetRestAccount( PFDAccBuf1.rec, dat ));
          else
             PFDSum1 = $0;
             PFDAccBuf1.Clear();
          end;

          if( FD1.ВидСрочностиСделки() == СделкаСрочная )
             MFDAccCat1 = "-Форвард, дрейф";
          
          else
             MFDAccCat1 = "-Форвард, кэш";
          end;

          if( FD1.IsExistAccount( MFDAccCat1, null, true, null, MFDAccBuf1, null, dat ) and IsOutBal1 )
             MFDSum1 = abs(GetRestAccount( MFDAccBuf1.rec, dat ));
          else
             MFDSum1 = $0;
             MFDAccBuf1.Clear();
          end;

          if( IsOutBal1 )
             if( not SmartConvertSum(TotalCost1, FD1.dl_leg.rec.TotalCost, FD1.tick.rec.DealDate, FD1.GetPayFIID(), FD1.FaceFIID(), false) )
                Message( "Ошибка" );
             end;

             if( FD1.FaceFIID() != FD1.GetPayFIID() )
                if( IsBuy(FD1.Group) )
                   Ovr1 = Abs(PFDSum1) - TotalCost1;
                else
                   Ovr1 = Abs(MFDSum1) - TotalCost1;
                end;
             else
                Ovr1 = MFDSum1 - PFDSum1;
             end;
          else
             Ovr1 = $0;
          end;

          if( ( Abs(Ovr1) > 0 )  and IsOutBal1 )
             if( ( IsBUY(FD1.Group) and ( Ovr1 < 0 ) ) or
                 ( IsSALE(FD1.Group) and ( Ovr1 > 0 ) )
               )
                FMAccCat1 = "+Форвард, маржа";
             else
                FMAccCat1 = "-Форвард, маржа";
             end;

             if( not FD1.IsExistAccount( FMAccCat1, null, true, null, FMAccBuf1, null, dat ) and IsOutBal1 )
                FMAccBuf1.Clear();
             end;
          else
             FMAccBuf1.Clear();
          end;


          if( FD2.IsExistAccount( "+Форвард, дрейф", null, true, null, PFDAccBuf2, null, dat ) and IsOutBal2 )
             PFDSum2 = abs(GetRestAccount( PFDAccBuf2.rec, dat ));
          else
             PFDSum2 = $0;
             PFDAccBuf2.Clear();
          end;

          if( FD2.IsExistAccount( "-Форвард, дрейф", null, true, null, MFDAccBuf2, null, dat ) and IsOutBal2 )
             MFDSum2 = abs(GetRestAccount( MFDAccBuf2.rec, dat ));
          else
             MFDSum2 = $0;
             MFDAccBuf2.Clear();
          end;

          if( IsOutBal2 )
             if( not SmartConvertSum(TotalCost2, FD2.dl_leg.rec.TotalCost, FD2.tick.rec.DealDate, FD2.GetPayFIID(), FD2.FaceFIID(), false) )
                Message( "Ошибка" );
             end;

             if( FD1.FaceFIID() != FD1.GetPayFIID() )
                if( IsSale(FD2.Group) )
                   Ovr2 = Abs(PFDSum2) - TotalCost2;
                else
                   Ovr2 = Abs(MFDSum2) - TotalCost2;
                end;
             else
                Ovr2 = MFDSum2 - PFDSum2;
             end;
          else
             Ovr2 = $0;
          end;

          if( ( Abs(Ovr2) > 0 )  and IsOutBal2 )
             if( ( IsBUY(FD2.Group) and ( Ovr2 > 0 ) ) or
                 ( IsSALE(FD2.Group) and ( Ovr2 < 0 ) )
               )
                FMAccCat2 = "+Форвард, маржа";
             else
                FMAccCat2 = "-Форвард, маржа";
             end;

             if( not FD2.IsExistAccount( FMAccCat2, null, true, null, FMAccBuf2, null, dat ) and IsOutBal2 )
                FMAccBuf2.Clear();
             end;
          else
             FMAccBuf2.Clear();
          end;

        end;

        if( (GetLotStateOnDate( FD1.pmwrtsum.rec, dat ) >= PM_WRTSUM_FORM) and
            (GetLotStateOnDate( FD2.pmwrtsum.rec, dat ) < PM_WRTSUM_FORM)
          )

           if( FD1.IsExistAccount( "Ц/б, договор", null, true, null, BalAccBuf, null, dat ) )
              Balance = abs(GetRestAccount( BalAccBuf.rec, dat ));
           else
              BalAccBuf.Clear();
              Balance = $0;
           end;

           if( FD1.IsExistAccount( "-НКД, договор", null, true, null, NKDAccBuf, null, dat ) )
              NKD = abs(GetRestAccount( NKDAccBuf.rec, dat ));
           else
              NKDAccBuf.Clear();
              NKD = $0;
           end;

           if( not SmartConvertSum(NotSaledCost, FD1.pmwrtsum.rec.BalanceCost, FD1.tick.rec.DealDate, NATCUR, FD1.FaceFIID(), false) )
              Message( "Ошибка" );
           end;

           NotSaledCost = NotSaledCost + FD1.pmwrtsum.rec.NKDAmount;

           if( not GetWRTLink( FD1.pmwrtsum.rec.DocKind, FD1.pmwrtsum.rec.DocID, FD1.pmwrtsum.rec.PartNum, dat, @Saled, @SaledCost ) )
              Saled = $0;
              SaledCost = $0;
           end;

           /*получим результаты списания*/
           if( not WRT_GetFinResult( FD1.pmwrtsum.rec.DocKind, FD1.pmwrtsum.rec.DocID, BUYGOAL_UNDEF, 0, null, null, null, WriteOffBalanceCost, OutLayTmp ) )
              return Message( FD1.tick.rec, "!!! Ошибка при получении результатов списания");
           end;

           if( SmartConvertSum( OutlayConv, OutLayTmp, dat, NATCUR, FD1.FaceFIID ) != true )
              return Message( FD1.tick.rec, "Не могу сконвертировать сумму из ", ПолучитьКодФинИн(FD1.FaceFIID)," в ", ПолучитьКодФинИн(NATCUR));
           end;    

           if( not FD1.УчитватьЗатратыВЦеломПоПортфелю() ) 
              if( ПосчитатьЗатратыНаКомиссииПосреднику( FD1, dat, AgentSumComm ) == false )
                 Message("Немогу посчитать затраты на комиссии посреднику");
              end;                                             
           end;

           FinRes = FD1.pmwrtsum.rec.BalanceCost - OutlayConv - AgentSumComm;

        else
           BalAccBuf.Clear();
           NKDAccBuf.Clear();
           Balance = $0;
           NKD = $0;
           NotSaledCost = $0;
           Saled = $0;
           SaledCost = $0;
           FinRes = $0;
        end;

        InsertDDLREPO( DS.rec.DealID, DS.rec.DealType, 
                       FD1.pmwrtsum.rec.State, FD2.pmwrtsum.rec.State, 
                       FD1.pmwrtsum.rec.BitMask, FD2.pmwrtsum.rec.BitMask,
                       IsOwn, IsORCB,
                       OutAccBuf.rec.Account, Outlay,
                       PFDAccBuf1.rec.Account, PFDSum1,
                       MFDAccBuf1.rec.Account, MFDSum1,
                       Ovr1, FMAccBuf1.rec.Account, 
                       PFDAccBuf2.rec.Account, PFDSum2,
                       MFDAccBuf2.rec.Account, MFDSum2,
                       Ovr2, FMAccBuf2.rec.Account,
                       BalAccBuf.rec.Account, Balance,
                       NKDAccBuf.rec.Account, NKD,
                       NotSaledCost, Saled, SaledCost,
                       FinRes );
         UseProgress(i=i+1);
     end;
     RemProgress();
  end;

  macro ВыгрузитКомиссии( dat, IsClosed )
    var cmd, DS, i = 0;
    var OldDealID,
        Sum, NDS, Kind, Cur;

     cmd = RsdCommand( "select pws.t_AgentComm, pws.t_BankComm, pws.t_BitMask, pws.t_DealID, tick.t_CommDate, " +
                       "       pws.t_AgentCommNDS, pws.t_BankCommNDS, " +
                       "       pws.t_FIID_AgentComm, pws.t_FIID_BankComm " +
                       "  from dpmwrtsum_dbt pws, ddl_tick_dbt tick " +
                       " where    ((( ? = 1 )  and ( tick.t_CloseDate <= ?)) or " + 
                       "           (( ? <> 1 ) and  (( tick.t_CloseDate > ?) or ( tick.t_CloseDate = ?))) )and " + 
                       "       tick.t_DealID = pws.t_DealID " +
                       "order by pws.t_DealID " );

     cmd.addParam( "", RSDBP_IN, iif(IsClosed, 1, 0) );
     cmd.addParam( "", RSDBP_IN, dat );
     cmd.addParam( "", RSDBP_IN, iif(IsClosed, 1, 0) );
     cmd.addParam( "", RSDBP_IN, dat );
     cmd.addParam( "", RSDBP_IN, Date(0,0,0) );
     cmd.execute();

     DS = TRsbDataSet( cmd );

     InitProgress( -1, "Выгрузка комиссий", "Выгрузка комиссий" );
     while( DS.movenext() )

        if( bAND( DS.rec.BitMask, PM_WRTSUM_PAY_BANK_ONCE ) or 
            bAND( DS.rec.BitMask, PM_WRTSUM_ADD_BANK_PERIOD )
          )
           InsertDCOMSUM( DS.rec.DealID, 1, DS.rec.CommDate, DS.rec.BankComm, DS.rec.BankCommNDS, DS.rec.FIID_BankComm, DS.rec.FIID_BankComm );
        end;

        if( bAND( DS.rec.BitMask, PM_WRTSUM_PAY_AGENT_ONCE ) or 
            bAND( DS.rec.BitMask, PM_WRTSUM_PAY_AGENT_PERIOD )
          )
           InsertDCOMSUM( DS.rec.DealID, 2, DS.rec.CommDate, DS.rec.AgentComm, DS.rec.AgentCommNDS, DS.rec.FIID_AgentComm, DS.rec.FIID_AgentComm );
           UseProgress(i=i+1);
        end;

     end;
     RemProgress();

  end;



  PRIVATE MACRO Create()

     ClearTable("davrrest_buf");
     ClearTable("ddlrepo_buf");
     ClearTable("dcomsum_buf");

     CreateTables();

     if( FormData.Flag1 == "X" )
        ВыгрузитьОстатки( FormData.OperDate );
     end;

     if( FormData.Flag2 == "X" )
        ВыгрузитьРЕПО( FormData.OperDate );
     end;

     if( FormData.Flag3 == "X" )
        ВыгрузитКомиссии( FormData.OperDate, (FormData.Flag31 == "X") );
     end;

  END;

  initDL_CReportTemplate( SP_OutAccData_Form(), NULL );
END;

SP_OutAccData().Run();
Exit(1);
