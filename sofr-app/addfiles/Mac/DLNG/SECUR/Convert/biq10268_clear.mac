/** 
 @file  biq10268_clear.mac
 @brief Закрытие субъектов, для которых нет связей в таблицах БД

 Набор функций, позволяющий в рамках единовременного запуска закрыть субъекты, у которых нет связей с таблицами базы данных

 # tag
 - functional_block:Сервисные операции
 - code_type:one_time

 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------------------
 |2024.10.30 |Швецов  Я. А.  |CCBO-10667       | Первоначальная реализация

*/

import spserv;
import or_rep_h, dl_activex;

private var TestMode = true; //Тестовый режим (без фиксации действий в БД)

// Режим работы: закрытие/анализ
private const MODE_CLOSE    = 0;
private const MODE_ANALYSIS = 1;

private var TableHeaderClose = "┌─────────────┬──────────────────────────────┬──────────────────────────────┐\n";
                               "│   PartyID   │     Краткое наименование     │        Комментарий           │\n";
                               "│             │           субъекта           │                              │\n";
                               "├─────────────┼──────────────────────────────┼──────────────────────────────┤";
private var TableHeaderLink  = "┌─────────────┬──────────────────────────────┬──────────────────────────────┬──────────────────────────────┐\n";
                               "│   PartyID   │     Краткое наименование     │    Имя таблицы со связями    │  Описание таблицы со связями │\n";
                               "│             │           субъекта           │                              │                              │\n";
                               "├─────────────┼──────────────────────────────┼──────────────────────────────┼──────────────────────────────┤";
private var Rep;

private var wHead = 60; 
private var wSubHead = 26;

PRIVATE CLASS TProtocolData(_PartyID:integer, _ShortName:string, _TableName:string, _TableDescr:string, _Msg:string)
  var PartyID    = _PartyID;  
  var ShortName  = _ShortName;
  var TableName  = _TableName;
  var TableDescr = _TableDescr;
  var Msg        = _Msg; 
END;

PRIVATE MACRO CloseParty(PartyID:integer, ErrStr:@string)
  var party;
  var query, cmd, DataSet;
  ErrStr = ""; 

  party = RsbParty(PartyID);
  if(not party.Lock(false))
    var skinds = "";
    query = " select skind.t_name from dservkind_dbt skind, dclient_dbt clnt where clnt.t_partyid = ? and clnt.t_servicekind = skind.t_servisekind ";
    cmd = DL_RSDCOmmand(query);
    cmd.AddParam(PartyID);

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      if(skinds == "")
        skinds = DataSet.name;  
      else
        skinds = skinds +", "+DataSet.name;
      end;
    end;
    if(skinds == "")
      ErrStr = "Ошибка при закрытии субъекта. " + GetErrMsg;
      return 1;
    end;
    query = " delete from dptsvdp_dbt where t_partyid = ? ";
    cmd = RSDCommand(query);
    cmd.AddParam("", RSDBP_IN, PartyID);
    cmd.Execute();

    query = " delete from dclient_dbt where t_partyid = ? ";
    cmd = RSDCommand(query);
    cmd.AddParam("", RSDBP_IN, PartyID);
    cmd.Execute();

    ErrStr = "Удалены виды обслуживания: " + skinds;
    if(not party.Lock(false))
      ErrStr = ErrStr+"\nОшибка при закрытии субъекта. " + GetErrMsg;
      return 1;
    else
      ErrStr = ErrStr+"\nСубъект закрыт успешно";
    end;
  end;
  return 0;
OnError(er)
  ErrStr = er.Message;
  return 1;
END;

PRIVATE MACRO AddLinkLog(TableStr:string, DescrStr:string, TableName:@string, TableDescr:@string)
  if(TableName == "")
    TableName = TableStr;
  else
    TableName = TableName + "\n" + TableStr; 
  end;
  if(TableDescr == "")
    TableDescr = DescrStr;
  else
    TableDescr = TableDescr + "\n" + DescrStr; 
  end;
END;

PRIVATE MACRO ExecFindAndCloseParty()
  var query, cmd, DataSet;
  var cnt = 0, i = 0;
  var positionCost = $0.0;
  var LogArr = TArray(15000);
  var LogArrLnk = TArray(15000);
  var UpdateDate = date(0,0,0);
  var SystDate = Date, SystTime = Time;
  var err = 0, ErrStr = "", TableName = "", TableDescr = "";
  var err_cnt = 0;
  var close_cnt = 0;
  var link_cnt = 0;
  
  if(GetTRUE(FALSE, "Выполнить с фиксацией действий в БД?\n"
                   +"(НЕТ - процедура выполнится в тестовом режиме без сохранения изменений в базе; ДА - все успешные изменения будут сохранены в базе)\n"
                   +"Первый запуск рекомендуется выполнить в тестовом режиме и проверить, что процедура выполнится без ошибок."))
    TestMode = false;
  end;

  query =   " select count (*) over() cnt, t.* " +
            "  from( " +
            "       with tick as (select/*+ parallel(8) materialize*/ "+
            "                     distinct t_ClientID, t_PartyID, t_MarketID, t_TraderID, t_BrokerID "+
            "                     from ddl_tick_dbt), "+
            "           mcacc as (select/*+ parallel(8) materialize*/ "+
            "                     distinct T_OWNER, T_ISSUER, T_MARKETPLACEID, T_MARKETPLACEOFFICEID, T_CONTRACTOR "+
            "                     from dmcaccdoc_dbt), "+
            "          nptxop as (select/*+  parallel(8) materialize*/ "+                   
            "                     distinct T_CLIENT, T_PLACE, T_PLACE2, T_MARKETPLACE, T_MARKETPLACE2  "+
            "                     from DNPTXOP_DBT),          "+
            "           Ndeal as (select/*+  parallel(8) materialize*/ "+
            "                     distinct t_Client, t_contractor "+
            "                     from ddvndeal_dbt), "+
            "          Dvdeal as (select/*+  parallel(8) materialize*/ "+
            "                     distinct t_Client, t_contractor "+
            "                     from ddvdeal_dbt) "+
            "       select/*+  parallel(8) */ "+
            "         un.t_PartyID, un.ShortNameOrg, "+
            "         (select count(*) from tick "+
            "          where (t_Clientid = un.t_PartyID or t_Partyid = un.t_PartyID or t_MarketID = un.t_PartyID or t_traderid = un.t_PartyID or t_brokerid = un.t_PartyID) "+
            "                 and rownum < 2) existTick, "+
            "         (select count(*) from mcacc "+
            "          where (T_OWNER = un.t_PartyID or T_ISSUER = un.t_PartyID or T_MARKETPLACEID = un.t_PartyID or T_MARKETPLACEOFFICEID = un.t_PartyID or T_CONTRACTOR = un.t_PartyID) "+
            "                 and rownum < 2) existMcacc, "+
            "         (select count(*) "+
            "          from nptxop where(T_CLIENT = un.t_PartyID or T_PLACE = un.t_PartyID or T_PLACE2 = un.t_PartyID or T_MARKETPLACE = un.t_PartyID or T_MARKETPLACE2 = un.t_PartyID) "+
            "          and rownum < 2) existNptxop, "+
            "         (select count(*) from daccount_dbt where t_Client = un.t_PartyID  and rownum< 2) existAccount, "+
            "         (select count(*) from dsfcontr_dbt where t_partyid = un.t_PartyID and rownum< 2) existSfcontr, "+
            "         (select count(*) from dobjcode_dbt where(t_objecttype = 3 and t_codekind = 8 and t_objectid = un.t_PartyID) and rownum< 2) existObjcode, "+
            "         (select count(*) from Ndeal where(t_Client = un.t_PartyID or t_contractor = un.t_PartyID) and rownum < 2) existNdeal, "+
            "         (select count(*) from Dvdeal where(t_Client = un.t_PartyID or t_contractor = un.t_PartyID) and rownum< 2) existDvdeal, "+
            "         (select count(*) from DDVCSA_DBT where T_PARTYID = un.t_PartyID and rownum < 2) existCSA, "+
            "         (select count(*) from DDL_GENAGR_DBT where T_PARTYID = un.t_PartyID and rownum < 2) existGenagr, "+
            "         (select count(*) from DFININSTR_DBT where T_ISSUER = un.t_PartyID and rownum < 2) existFininstr, "+
            "         (select count(*) from DVSBANNER_DBT where(T_ISSUER = un.t_PartyID or T_HOLDER = un.t_PartyID) and rownum < 2) existVsbanner, "+
            "         (select count(*) from DNPTXPAY_DBT where T_PARTYID = un.t_PartyID and rownum < 2) existNptxpay "+
            "    from biq10268_unload un ) t " +
            "             order by T_PARTYID ";

  cmd = DL_RSDCommand(query);
  InitProgress(1, "Выполнение запроса", "Отбор записей по субъектам");
    
  DataSet = cmd.Execute();

  UseProgress(1);
  RemProgress();
  while(DataSet.moveNext())
    if(cnt == 0)
      cnt = int(DataSet.cnt);
      InitProgress(cnt, "Обработка записей", "Обработка записей по субъектам");
    end;
    ErrStr = ""; TableName = ""; TableDescr = "";
    err = 0;
    if(DataSet.existTick == 1)        
      AddLinkLog("ddl_tick_dbt", "Сделки", @TableName, @TableDescr);
      err = 2;
    end;
    if(DataSet.existMcacc == 1)
      AddLinkLog("dmcaccdoc_dbt", "Категории учёта", @TableName, @TableDescr);
      err = 2;
    end;
    if(DataSet.existNptxop == 1)
      AddLinkLog("dnptxop_dbt", "Списания-зачисления", @TableName, @TableDescr);
      err = 2;
    end;
    if(DataSet.existAccount == 1)
      AddLinkLog("daccount_dbt", "Лицевые счета", @TableName, @TableDescr);
      err = 2;
    end;
    if(DataSet.existSfcontr == 1)
      AddLinkLog("dsfcontr_dbt", "Договоры обслуживания", @TableName, @TableDescr);
      err = 2;
    end;
    if(DataSet.existObjcode == 1)
      AddLinkLog("dobjcode_dbt", "Код субъекта МБ", @TableName, @TableDescr);
      err = 2;
    end;
    if(DataSet.existNdeal == 1)
      AddLinkLog("ddvndeal_dbt", "Сделки ФИСС и КО1", @TableName, @TableDescr);
      err = 2;
    end;
    if(DataSet.existDvdeal == 1)
      AddLinkLog("ddvdeal_dbt", "Сделки ФИСС и КО2", @TableName, @TableDescr);
      err = 2;
    end;
    if(DataSet.existCSA == 1)
      AddLinkLog("ddvcsa_dbt", "Операции с CSA", @TableName, @TableDescr);
      err = 2;
    end;
    if(DataSet.existGenagr == 1)
      AddLinkLog("ddl_genagr_dbt", "Генеральные соглашения", @TableName, @TableDescr);
      err = 2;
    end;
    if(DataSet.existFininstr == 1)
      AddLinkLog("dfininstr_dbt", "Ценные бумаги", @TableName, @TableDescr);
      err = 2;
    end;
    if(DataSet.existVsbanner == 1)
      AddLinkLog("dvsbanner_dbt", "Векселя", @TableName, @TableDescr);
      err = 2;
    end;
    if(DataSet.existNptxpay == 1)
      AddLinkLog("dnptxpay_dbt", "Удержания НДФЛ", @TableName, @TableDescr);
      err = 2;
    end;

    if(err != 2)
      RslDefCon.BeginTrans();

      err = CloseParty(DataSet.PartyID, @ErrStr);
        
      if((TestMode) or (err))
        RslDefCon.RollbackTrans();
      else
        RslDefCon.CommitTrans();
      end;
    end;

    if(err != 2)
      LogArr[LogArr.size()] = TProtocolData(DataSet.PartyID, DataSet.ShortNameOrg, "", "", ErrStr);
    elif(TableName != "")
      LogArrLnk[LogArrLnk.size()] = TProtocolData(DataSet.PartyID, DataSet.ShortNameOrg, TableName, TableDescr, ErrStr);
    end;

    if(err == 2)
      link_cnt = link_cnt + 1;
    elif(err == 1)
      err_cnt = err_cnt + 1;
    elif(err == 0)
      close_cnt = close_cnt + 1;      
    end;

    UseProgress(i = i + 1);
  end;

  if(cnt != 0)
    RemProgress();
  end;
  
  if(LogArr.size != 0)
    InitProgress(LogArr.size(), "Печать протокола", "Печать протокола");
    Rep = CMakeReport();
    Rep.AddPrintCell("Список субъектов, у которых нет связей с таблицами БД", wHead, 0, "c:ex_FS(b)", REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell( "Дата выполнения:", wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell( SystDate:f, wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell( "Время выполнения:", wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell( SystTime, wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell( "Закрыто успешно:", wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell( close_cnt, wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell( "Не закрыто из-за ошибок:", wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell( err_cnt, wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell( "PartyID", 10, 0, "c");
    Rep.AddPrintCell( "Краткое наименование\nсубъекта", 30, 0, "c");
    Rep.AddPrintCell( "Комментарий", 30, 0, "c");
    Rep.AddStr();
    i = 0;
    while(i < LogArr.size())
      Rep.AddPrintCell( LogArr[i].PartyID, 10, 0, "l");
      Rep.AddPrintCell( LogArr[i].ShortName, 30, 0, "l");
      Rep.AddPrintCell( LogArr[i].Msg, 30, 0, "l");
      Rep.AddStr();
      UseProgress(i = i + 1);
    end;
    RemProgress();
  end;

  if(LogArrLnk.size != 0)
    InitProgress(LogArrLnk.size(), "Печать протокола", "Печать протокола");
    if (not Rep)
      Rep = CMakeReport(TableHeaderLink);
    else
      Rep.AddNewSheetBreak( "Субъекты со связями", TableHeaderLink);
    end;
    Rep.AddPrintCell("Список субъектов, у которых есть связи с таблицами БД", wHead, 0, "c:ex_FS(b)", REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell( "Дата выполнения:", wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell( SystDate:f, wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell( "Время выполнения:", wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell( SystTime, wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell( "Записей со связями:", wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddPrintCell( link_cnt, wSubHead, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
    Rep.AddPrintCell( "PartyID", 10, 0, "c");
    Rep.AddPrintCell( "Краткое наименование субъекта", 30, 0, "c");
    Rep.AddPrintCell( "Имя таблицы со связями", 30, 0, "c");
    Rep.AddPrintCell( "Описание таблицы со связями", 30, 0, "c");
    Rep.AddStr();
    i = 0;
    while(i < LogArrLnk.size())
      Rep.AddPrintCell( LogArrLnk[i].PartyID, 10, 0, "l");
      Rep.AddPrintCell( LogArrLnk[i].ShortName, 30, 0, "l");
      Rep.AddPrintCell( LogArrLnk[i].TableName, 30, 0, "l");
      Rep.AddPrintCell( LogArrLnk[i].TableDescr, 30, 0, "l");
      Rep.AddStr();
      UseProgress(i = i + 1);
    end;
    RemProgress();
  end;
  Rep.SetFlagShowGrid(true);

  if (LogArr.size != 0)
    Rep.PrintWinRep("Субъекты без связей");
  elif (LogArrLnk.size != 0)
    Rep.PrintWinRep("Субъекты со связями");
  else
    msgbox("Нет подходящих записей");
    return 0;
  end;
  Rep.ShowWinRep();

OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  msgbox("Ошибка: " + er.Message);
END;

ExecFindAndCloseParty();
