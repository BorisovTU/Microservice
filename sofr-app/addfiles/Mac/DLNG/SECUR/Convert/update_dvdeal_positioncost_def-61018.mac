/** 
 @file  update_dvdeal_positioncost_def-61018.mac
 @brief Обновление суммы операции на сделках срочного рынка

 Набор функций, позволяющий в рамках единовременного запуска обновить сумму по операциям срочного рынка со фьючерсами на бивалютный индекс

 # tag
 - functional_block:Сервисные операции
 - code_type:one_time

 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------------------
 |2023.02.05 |Швецов  Я. А.  |DEF-61018        | Первоначальная реализация

*/

import spserv;

private var TestMode   = true; //Тестовый режим (без фиксации действий в БД)

PRIVATE CLASS TProtocolData(_DealID:integer, _DealCode:string, _DealCost_OLD:money, _DealCost:money, _Msg:string)
  var DealID       = _DealID;  
  var DealCode     = _DealCode;
  var DealCost_OLD = _DealCost_OLD;
  var DealCost     = _DealCost;   
  var Msg          = _Msg; 
END;

PRIVATE MACRO SetNewPositionCost(DealID:integer, _DealCost:money, ErrStr:@string)
  var query, cmd;

  ErrStr = "";

  query =   " UPDATE ddvdeal_dbt "
          + "    SET t_positioncost = ? "
          + "  WHERE t_ID = ? ";


  cmd = DL_RSDCOmmand(query);

  cmd.AddParam(_DealCost);
  cmd.AddParam(DealID);

  cmd.ExecuteCMD();

  return 0;
OnError(er)
  ErrStr = er.Message;
  return 1;
END;

private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

PRIVATE MACRO ExecUpdateSettAcc()
  var query, cmd, DataSet;
  var cnt = 0, i = 0;
  var positionCost = $0.0;
  var LogArr = TArray(10000);
  var UpdateDate = date(0,0,0);
  var SystDate, SystTime;
  var err = 0, ErrStr = "";
  var err_cnt = 0;

  if(GetTRUE(FALSE, "Выполнить с фиксацией действий в БД?\n"
                   +"(НЕТ - процедура выполнится в тестовом режиме без сохранения изменений в базе; ДА - все успешные изменения будут сохранены в базе)\n"
                   +"Первый запуск рекомендуется выполнить в тестовом режиме и проверить, что процедура выполнится без ошибок."))
    TestMode = false;
  end;

  GetSystDateTime(@SystDate, @SystTime);
  GetDate(UpdateDate, "Введите дату исправления сумм сделок");

  query =  "  select deal.t_id, deal.t_code, deal.t_positioncost, deal.t_amount, prm.t_doubleval cost_new  "
         + "    from ddvdeal_dbt deal, dgtrecprm_dbt prm, dgtrecord_dbt rec, dgtcode_dbt code  "
         + "   where deal.t_client > 0 /*Клиентские*/ and deal.t_date_clr = ? "
         + "     and t_FIID in ( "
         + "                     select fin.t_FIID from dfininstr_dbt fin, dfininstr_dbt fin_base, dfideriv_dbt dv "
         + "                      where fin.t_avoirkind = 1/*Фючерс*/ and fin_base.t_fiid = fin.t_facevaluefi and dv.t_fiid = fin.t_fiid  "
         + "                        and fin_base.t_fi_kind = 3 /*FIKIND_INDEX*/ and fin_base.t_settlement_code = 1 /*FI_INDEX_CURRENCY */ "
         + "                        and dv.t_tickfiid != 0  /*На валютную пару*/  "
         + "                   ) "
         + "     and code.t_objectcode = TO_CHAR(deal.t_id) and code.t_objectkind = 86 /*Биржевая операция с ПИ*/ and rec.t_objectid  = code.t_objectid "
         + "     and prm.t_recordid = rec.t_recordid and prm.t_koprmid = 619 /*PRICE_RUR*/ ";
;

  cmd = DL_RSDCommand(query);
  cmd.addParam(UpdateDate);

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "Обработка сделок", "Обработка сделок");
    
    DataSet = cmd.Execute();
    while(DataSet.moveNext())

      err = 0;
      ErrStr = "";
      positionCost = money(round(DataSet.cost_new * DataSet.Amount, 2));

      RslDefCon.BeginTrans();

      err = SetNewPositionCost(DataSet.ID, positionCost, @ErrStr);
        
      if((TestMode) or (err))
        RslDefCon.RollbackTrans();
      else
        RslDefCon.CommitTrans();
      end;
      LogArr[LogArr.size()] = TProtocolData(DataSet.ID, DataSet.Code, DataSet.PositionCost, positionCost, ErrStr);
      if(err)
        err_cnt = err_cnt + 1;
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();

    InitProgress(LogArr.size(), "Печать протокола", "Печать протокола");

             [                                                        Обновление сумм по сделкам за #############
             Дата выполнения:  #############
             Время выполнения: #############
             Обработано сделок:   #############  Из них не обновлены: #########
             ]
             (UpdateDate:f, SystDate:f, SystTime, cnt, err_cnt);

    println("");
    println("┌─────────────┬──────────────────────────────┬──────────────────────────────────┬─────────────────────────────────┐");
    println("│   DealID    │          Код сделки          │      Сумма по операции старая    │      Сумма по операции новая    │");
    println("│             │                              │                                  │                                 │");
    println("├─────────────┼──────────────────────────────┼──────────────────────────────────┼─────────────────────────────────┤");

    i = 0;
    while(i < LogArr.size())

            [│#############│##############################│##################################│#################################│]
            (string(LogArr[i].DealID):c,     
             string(LogArr[i].DealCode):c,   
             string(LogArr[i].DealCost_old):c,     
             string(LogArr[i].DealCost):c,  
             string(LogArr[i].Msg):l:w
            );

      if(i < LogArr.size() - 1)
        println("├─────────────┼──────────────────────────────┼──────────────────────────────────┼─────────────────────────────────┤");
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();

    println("└─────────────┴──────────────────────────────┴──────────────────────────────────┴─────────────────────────────────┘");

  else
    msgbox("Нет подходящих сделок");
  end;

OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  msgbox("Ошибка: " + er.Message);
END;

ExecUpdateSettAcc();
