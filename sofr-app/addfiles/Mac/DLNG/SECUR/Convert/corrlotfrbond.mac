/*
$Name:        corrlotfrbond.mac
$Module:      Ценные бумаги
$Description: Перевод лотов с метода ЭПС на ЛМ для облигаций с плавающей ставкой
*/
import secinter, sp_categ, sp_car;

private var ActDate = {curdate};
private var TestMode = true; //Тестовый режим без фиксации изменений в БД

private var ID_Operation = -8887; //Просто любой идентефикатор операции для фиксации изменений и возможности отката
private var ID_Step      = -1;    //Просто любой идентефикатор шага для фиксации изменений и возможности отката
private var Action       = 9999;  //Просто любой несуществующий вид действия для обновления лотов

private var gSQL = "";

private macro SqlHandler(str)
  gSQL = gSQL + str;
end;

private macro UpdateLots()
  var query, cmd, DataSet;
  var cntExec = 0;

  gSQL = "";

  SetOutHandler(@SqlHandler);

[ DECLARE

    v_cnt NUMBER := 0;

  BEGIN

    FOR one_lot IN (SELECT LOT.T_SUMID, LOT.T_CORRINTTOEIR, LOT.T_CORRINTTOEIRDATE, FI.T_FACEVALUEFI
                      FROM DPMWRTSUM_DBT LOT, DFININSTR_DBT FI, DAVOIRISS_DBT AVR, DDL_TICK_DBT TK
                     WHERE LOT.T_PARTY = -1
                       AND LOT.T_AMOUNT > 0
                       AND LOT.T_AMORTCALCKIND = 2 /*ЭПС*/
                       AND FI.T_FIID = LOT.T_FIID
                       AND FI.T_FI_KIND = 2
                       AND RSI_RSB_FIINSTR.FI_AvrKindsGetRoot(FI.T_FI_KIND, FI.T_AVOIRKIND) = RSI_RSB_Fiinstr.AVOIRKIND_BOND
                       AND AVR.T_FIID = FI.T_FIID
                       AND AVR.T_FLOATINGRATE = 'X'
                       AND TK.T_DEALID = LOT.T_DEALID
                       --AND RSB_SECUR.GetDealMarketTestAttrID(TK.T_BOfficeKind, TK.T_DealID) = 1 /*Тест на рыночность пройден = Да*/
                     ORDER BY LOT.T_SUMID
                   )
    LOOP

      RSB_PMWRTOFF.RSI_WRTSaveLot(one_lot.T_SUMID, ? /*ID_Operation*/, ? /*ID_Step*/, ? /*OperDate*/, ? /*Action*/);

      UPDATE DPMWRTSUM_DBT L
         SET L.T_AMORTCALCKIND      = 1,
             L.T_EFFECTINTERESTRATE = 0,
             L.T_CORRINTTOEIR       = 0,
             L.T_CORRINTTOEIRDATE   = TO_DATE('01.01.0001','DD.MM.YYYY'),
             L.T_BALANCECOST        = L.T_BALANCECOST - ROUND(RSB_FIInstr.ConvSum(one_lot.t_CorrIntToEIR, ? /*NATCUR*/, one_lot.t_FaceValueFI, one_lot.t_CorrIntToEIRDate, 1), 2)
       WHERE L.T_SUMID = one_lot.T_SUMID;

       v_cnt := v_cnt + 1;

    END LOOP;

    ? := v_cnt;

  END;
];


  SetOutHandler(NULL);

  cmd = RSDCommand(gSQL);

  cmd.addParam("", RSDBP_IN, ID_Operation); 
  cmd.addParam("", RSDBP_IN, ID_Step);      
  cmd.addParam("", RSDBP_IN, ActDate);      
  cmd.addParam("", RSDBP_IN, Action);       
  cmd.addParam("", RSDBP_IN, NATCUR);
  cmd.addParam("p_cnt", RSDBP_OUT, V_INTEGER);

  cmd.execute();

  cntExec = SQL_ConvTypeInteger(cmd.value("p_cnt"));

  query =   " SELECT 1 "
          + "   FROM DPMWRTSUM_DBT LOT, DFININSTR_DBT FI, DAVOIRISS_DBT AVR, DDL_TICK_DBT TK "
          + "  WHERE LOT.T_PARTY = -1 "
          + "    AND LOT.T_AMOUNT > 0 "
          + "    AND LOT.T_AMORTCALCKIND = 2 /*ЭПС*/ "
          + "    AND FI.T_FIID = LOT.T_FIID "
          + "    AND FI.T_FI_KIND = 2 "
          + "    AND RSI_RSB_FIINSTR.FI_AvrKindsGetRoot(FI.T_FI_KIND, FI.T_AVOIRKIND) = " + AVOIRISSKIND_BOND 
          + "    AND AVR.T_FIID = FI.T_FIID "
          + "    AND AVR.T_FLOATINGRATE = 'X' "
          + "    AND TK.T_DEALID = LOT.T_DEALID ";

  cmd = DL_RSDCommand(query);

  println("Выполнено обновление лотов. Успешно обработано " + cntExec + " лотов. Осталось " + cmd.GetCount() + " лотов с методом ЭПС.");

  return 0;

OnError(er)
  println("ОШИБКА! Исключение при обновлении данных по лотам!");

  return 1;
end;

PRIVATE MACRO GetCCY(FIID)
  var cmd = DL_RSDCommand("select t_CCY from dfininstr_dbt where t_FIID = ?");
  var ccy = "";

  cmd.AddParam(FIID);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ccy = DataSet.CCY;
  end;

  return ccy;
END;

private macro Execute(FIID)
  var query, cmd, DataSet;
  var FD = NULL;
  var CorrAcc = TRecHandler("account.dbt");
  var Rest = $0;
  var DebAccNumber = "", CredAccNumber = "";
  var err = 0;
  var msg = "";

  if(TestMode == true)
    println("ТЕСТОВЫЙ РЕЖИМ!");
  end;

  println("");
  RslDefCon.BeginTrans();

  err = UpdateLots();

  if(not err)
    query =   " select lot.t_FIID, lot.t_Portfolio "
            + "   from dpmwrtsum_dbt lot "
            + "  where lot.t_Party = -1 "
            + "    and lot.t_Amount > 0 "
            + "    and lot.t_ID_Operation = ? "
            + "    and lot.t_ID_Step = ? "
            + "    and lot.t_Action = ? "
            + "    and lot.t_ChangeDate = ? "
            + "  group by lot.t_FIID, lot.t_Portfolio ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(ID_Operation);
    cmd.AddParam(ID_Step);
    cmd.AddParam(Action);
    cmd.AddParam(ActDate);

    DataSet = cmd.Execute();

    while((not err) and (DataSet.moveNext()))

      FD = SPFirstDocFI(DLDOC_ISSUE, DataSet.FIID, DataSet.FIID, DataSet.Portfolio, {OurBank}, null, null, null);

      if( FD.IsExistAccount("+Корректировка, ц/б", null, true, GetFIRoleByPortfolio(DataSet.Portfolio), CorrAcc, MC_PAIRACCMODE_MANUAL, ActDate) )
        Rest = abs(GetRestAccount(CorrAcc.rec, ActDate));

        if(Rest > 0)
          if(not ПроводкаПоКатегориямУчета(FD, 
                 "-КорЭПС_%, ц/б", CorrAcc,              /* КатегДеб , КатегКредит*/
                 ActDate,                     /* Дата */
                 1,                           /* Глава */
                 CorrAcc.rec.Code_Currency,    /* Валюта */
                 Rest,                        /* Сумма  */
                 NULL,                        /* Документ */
                 INPCARRY,                    /* Result_Carry */
                 " 1",                        /* Kind_Oper */
                 "",                          /* Numb_Document */
                 "Отнесение корректировки на расходы",
                 null, GetFIRoleByPortfolio(DataSet.Portfolio), null,
                 null, null, null, null,
                 null, null, null, null, null,
                 @DebAccNumber, @CredAccNumber
                ) 
            )
              msg = GetErrMsg();
              if(msg != "")
                println("  Ошибка при выполнении проводки. "+msg);
              end;

              err = 1;
          else
            println("    Выполнена проводка "+DebAccNumber+"<->"+CredAccNumber+" на сумму " + money(Rest) + " " + GetCCY(CorrAcc.rec.Code_Currency));
          end;
        end;
      end;

      if( FD.IsExistAccount("-Корректировка, ц/б", null, true, GetFIRoleByPortfolio(DataSet.Portfolio), CorrAcc, MC_PAIRACCMODE_MANUAL, ActDate) )
        Rest = abs(GetRestAccount(CorrAcc.rec, ActDate));

        if(Rest > 0)
          if(not ПроводкаПоКатегориямУчета(FD, 
                 CorrAcc, "+КорЭПС_%, ц/б",   /* КатегДеб , КатегКредит*/
                 ActDate,                     /* Дата */
                 1,                           /* Глава */
                 CorrAcc.rec.Code_Currency,   /* Валюта */
                 Rest,                        /* Сумма  */
                 NULL,                        /* Документ */
                 INPCARRY,                    /* Result_Carry */
                 " 1",                        /* Kind_Oper */
                 "",                          /* Numb_Document */
                 "Отнесение корректировки на доходы",
                 null, null, GetFIRoleByPortfolio(DataSet.Portfolio), 
                 null, null, null, null,
                 null, null, null, null, null,
                 @DebAccNumber, @CredAccNumber
                ) 
            )
              msg = GetErrMsg();
              if(msg != "")
                println("  Ошибка при выполнении проводки. "+msg);
              end;

              err = 1;
          else
            println("    Выполнена проводка "+DebAccNumber+"<->"+CredAccNumber+" на сумму " + money(Rest) + " " + GetCCY(CorrAcc.rec.Code_Currency));
          end;
        end;
      end;
    end;

  end;

  if(err != 0)
    RslDefCon.RollbackTrans();
    println("ВНИМАНИЕ!!! Фиксация изменения не выполнена!");
  elif(TestMode == true)
    RslDefCon.RollbackTrans();
    println("ВНИМАНИЕ!!! Фиксация изменения в БД не производилась!");
  else
    RslDefCon.CommitTrans();
    println("ВНИМАНИЕ!!! Выполнена фиксация изменений в БД!");
  end;


OnError( err );
  println("ОШИБКА!!! Исключение при обработке");
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
    println("ВНИМАНИЕ!!! Фиксация изменения не выполнена!");
  end;

end;


  Execute();
