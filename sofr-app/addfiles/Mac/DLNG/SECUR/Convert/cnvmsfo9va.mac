/*
$Name:        cnvmsfo9va.mac
$Module:      Ценные бумаги
$Description: Конвертация МСФО0 для данных УВ
*/

import valib, MC_lib, vsbnrfd, vacateg, vaacc, vamisc, vaservop, dlvaprds, vspmfutu, va_voop;

private const AccDate  = date(08,01,2019); //Дата учета
private const CalcDate = date(08,01,2019); //Дата расчета

//Счет для учета финансового результата
//Возможные значения: 706, 10801, 10901
private const СчетДляУчетаФР = 706;

//Метод расчета АС для РЕПО
//Возможные значения: 0 - Авто; 1 - ЛМ; 2 - ЭПС
private const МетодРасчетаАСДляРЕПО = 0;
//Вариант учета отсроченной разницы
//Возможные значения: 1 - за весь период; 2 - От даты расчета
private const ОтсроченнаяРазница = 2;

private var currPart = "";

private const UNDEF_CHAR = "?",
              L_OBJTYPE_LEVELESSENTIAL_EPS = 4,
              L_OBJTYPE_LEVELESSENTIAL_AC = 1,
              METHOD_UNDEF = 0,
              METHOD_LINE = 1,
              METHOD_EPS = 2,
              METHOD_RPS = 3;

private macro GetBuffAccount(КодВалюты, Счет, Buffer, Глава )
  var Acc  = TBfile("account.dbt");
  var stat = 0;
  ПоУмолчанию(КодВалюты, 0);
  ПоУмолчанию(Глава, 1);
  if(Счет == "")
    stat = 1;
    msgbox("Не задан счет для поиска" + Счет);
  else
      Acc.Clear();
      Acc.KeyNum = 0;
      Acc.rec.Account = Счет;
      Acc.rec.Chapter = Глава;
      Acc.rec.Code_Currency = КодВалюты;
      if(Acc.GetEQ())
        SetParm(2, Acc);
      else 
        stat = 1;
        msgbox("Ошибка получения буффера счета " + Счет);
      end;
  end;  
  return (stat == 0);
end;

class AccInf(_AccID, _FIID)
  var AccId, FIID;

  AccId = _AccID;
  FIID = _FIID;
end;

private Macro CorrectFlag()
  RslDefCon.BeginTrans();
  var cmd;
  cmd = RsdCommand("UPDATE DDL_LEG_DBT leg "
                  +"   SET T_RELATIVEPRICE = '?' "
                  +" WHERE     (T_RELATIVEPRICE = CHR(0) OR T_RELATIVEPRICE IS NULL) "
                  +"       AND T_LEGID = 0 "
                  +"       AND T_LEGKIND = 1 "
                  +"       AND ( (SELECT COUNT (*) "
                  +"                FROM DFICERT_DBT "
                  +"               WHERE T_CERTID = leg.T_DEALID AND T_AVOIRKIND = 5) > 0)");
  cmd.Execute();
  RslDefCon.CommitTrans();
end;

PRIVATE MACRO ПолучитьОстаток( Сумма, Счет, КодВалюты, Дата, Глава )
    ПоУмолчанию(КодВалюты, 0);
    ПоУмолчанию(Дата, {curdate});
    ПоУмолчанию(Глава, 1);
    Сумма = VS_IIF(КодВалюты,
                RestAC(Счет,КодВалюты,Дата,NULL,Глава),
                RestA(Счет,Дата,NULL,Глава));

    SetParm( 0, Сумма );

    return true;
END;

private macro ОбновитьПараметрыВекселя(_bnr,_leg)
  var ret = true;
  var bnr = TBFile("vsbanner.dbt","W");
  var leg = TBFile("dl_leg.dbt","W",1);
  bnr.rec.BCID = _bnr.rec.BCID;
  leg.rec.ID = _leg.rec.ID;
  if (bnr.GetEQ())
    copy(bnr,_bnr);
    ret = bnr.Update();
  end;
  if ((ret) and (leg.GetEQ()))
    copy(leg,_leg);
    ret = leg.Update();
  end;
  return ret;
end;

Class SetAccountsID
  var SetAccID = Tarray;

  private macro NewAcc(_AccId, _FIID)
    SetAccID[SetAccID.size()] = AccInf(_AccId, _FIID);  
  end;

  private macro Find(_AccID)
    var i = 0;
    while (i < SetAccID.size())
      if(SetAccID[i].AccID == _AccID)
        return i;
      end;
      i = i + 1;
    end;
    return -1;
  end;

  macro AddAcc(_AccId, _FIID)
    var i = Find(_AccId);

    if(i == -1)
      NewAcc(_AccId, _FIID);
    end;
  end;

  macro CloseAllAcc()
    var Acc  = TBfile("account.dbt");
    var msg, err = 0, i = 0;
    var cnt = SetAccID.size();
    var SUMACC;
    InitProgress(cnt, currPart+"Закрытие счетов", currPart+"Закрытие счетов");
    while ((i < SetAccID.size()) AND (not err))
      Acc.rec.AccountId = SetAccID[i].AccID;
      Acc.KeyNum = 1;
      if(Acc.GetEQ())
        if( CB_CloseAccount(Acc.rec.Chapter, Acc.rec.Code_Currency, Acc.rec.Account, AccDate, null) != 0 )
          msg = GetErrMsg();
          msgbox("Ошибка закрытия счета: "+msg+". Счет "+Acc.rec.Account+" не закрыт.");
          err = 1;
        else
          msgbox("   Счет "+Acc.rec.Account+" успешно закрыт! ");
        end;
        UseProgress(i = i + 1);
      else 
        err = 1;
      end;
    end;
    RemProgress();
    return err;
  end;

  macro Clear()
    SetAccID = Tarray;
  end;
end;

private var SetAccountsToClose = SetAccountsID;

macro cnv_msgbox(StrErr)
  println(StrErr);
end;

macro ПолучитьКатегориюФР(Categ:string)
  var CatFR = Categ;

  if(СчетДляУчетаФР == 10801)
    CatFR = "Нераспределенная прибыль";
  elif(СчетДляУчетаФР == 10901)
    CatFR = "Непокрытый убыток";
  end;

  return CatFR;
end;

PRIVATE MACRO GetCCY(FIID)
  var cmd = DL_RSDCommand("select t_CCY from dfininstr_dbt where t_FIID = ?");
  var ccy = "";

  cmd.AddParam(FIID);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ccy = DataSet.CCY;
  end;

  return ccy;
END;

private macro ПолучитьЗначениеКорректировкиНаНерыночность(BCID,EnrolId,Corr:@Numeric,CorrPFI:@Numeric)
  var cmd, DataSet, query;
  query =  "SELECT T_PERC as Corr, T_CURRENCY as CorrPFI"
          +"  FROM DVSINCOME_DBT "
          +" WHERE     T_INCOMETYPE = ? "
          +"       AND T_BCID = ? "
          +"       AND T_ENROLMENT_ID = ? ";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(150);
  cmd.AddParam(BCID);
  cmd.AddParam(EnrolId);
  DataSet = cmd.Execute();
  if (DataSet.MoveNext())
    Corr = DataSet.Corr;
    CorrPFI = DataSet.CorrPFI;
  end;
end;

/* Проводка по категориям учета (в том числе и мультивалютная)
   СчетДебет,СчетКредит - если тип V_STRING то категории, иначе record ACCOUNT*/
MACRO МСФО9_ПроводкаПоКатегориямУчета( FD, СчетДебет, СчетКредит, ДатаВалютирования,
                                 Chapter, FIID, СуммаОперации, docum,
                                 Result_Carry, Kind_Oper,Numb_Document,Ground,
                                 PaymentID, FIRolePayer, FIRoleReceiver,
                                 СчетДебетFIID, СчетКредитFIID, FIID2, СуммаОперации2,
                                 СчетДебетРежим, СчетКредитРежим
                                 )
   var stat:bool = true, CуммаДебет = $0, СуммаКредит  = $0, 
      СчетОВПДебет = "", СчетОВПКредит = "", СчетДоходов = "", СчетРасходов = "";

   var tr, NoCarry:bool = false;
   var PaymentObj = null;

   record AccountPayer(account);
   record AccountReceiver(account);

   macro SayCarryError( error )
     msgbox( error );
     return false;
   end;

   if( (СуммаОперации == null) AND (СуммаОперации2 == null) )  
     return SayCarryError( "В проводке не задана ни одна из сумм." );
   end;

   if (СчетДебетРежим == null)
     СчетДебетРежим = MC_OPENACC_RECREATE;
   end;

   if (СчетКредитРежим == null)
     СчетКредитРежим = MC_OPENACC_RECREATE;
   end;
     
   /*оставить только копейки для первой суммы, если она задана*/
   if( СуммаОперации != null )
     if( Chapter != 5 ) /*количество ц/б может быть дробным*/
        СуммаОперации = money( round( СуммаОперации, 2 ) );    
     end;
     if( СуммаОперации < $0 )  
        return SayCarryError( "Сумма проводки < 0." );
     end;
   else  
    СуммаОперации = $0;
   end;
   /*для второй суммы, если она задана*/
   if( СуммаОперации2 != null )
     if( Chapter != 5 ) /*количество ц/б может быть дробным*/
        СуммаОперации2 = money( round( СуммаОперации2, 2 ) );
     end;
     if( СуммаОперации2 < $0 )  
        return SayCarryError( "Вторая сумма проводки < 0." );
     end;
   else
    СуммаОперации2 = $0;
   end;

   if( (СуммаОперации == $0) AND (СуммаОперации2 == $0) )  
     return true;
   end;

   if( ValType(СчетДебет) == V_STRING ) /*задан через категорию*/
         if(not VS_GetAccountManual(СчетДебет, FD, NULL, СчетДебетРежим, СчетДебетFIID, FIRolePayer, AccountPayer, ДатаВалютирования))
             return SayCarryError( "Ошибка при определении счета по категории "+СчетДебет+" в проводке.");
         end;
   else
     copy( AccountPayer, СчетДебет );
   end;

   if( ValType(СчетКредит) == V_STRING ) /*задан через категорию*/
         if(not VS_GetAccountManual(СчетКредит, FD, NULL, СчетКредитРежим, СчетКредитFIID, FIRoleReceiver, AccountReceiver, ДатаВалютирования))
            return SayCarryError( "Ошибка при определении счета по категории "+СчетКредит+" в проводке." );
         end;
   else
     copy( AccountReceiver, СчетКредит );
   end;

   if( AccountPayer.Code_Currency == AccountReceiver.Code_Currency)

     /*задана только вторая сумма в валюте одного из счетов*/
     if( (FIID == null) AND (FIID2 != null) )
        FIID = FIID2;
     elif( (FIID == null) AND (FIID2 == null) )
        return SayCarryError("Не задана ни одна из валют проводки.");
     end;

     if( (FIID != AccountPayer.Code_Currency) AND
         (FIID != AccountReceiver.Code_Currency) )
        return SayCarryError( "В проводке не совпадают валюты счетов: " 
                               + string(AccountPayer.Code_Currency) + " " + string( AccountReceiver.Code_Currency ) +
                              "| и валюта документа: " + string(FIID) );
     end;

     if( СуммаОперации != $0 )
        CуммаДебет = СуммаКредит = СуммаОперации;
     elif( СуммаОперации2 != $0 )
        CуммаДебет = СуммаКредит = СуммаОперации2;
     end;
     

   else /* валюты разные - мультивалютная проводка */
      
     СуммаКредит = $0;
     CуммаДебет  = $0;
     
     /*задана одна сумма в валюте одного из счетов*/
     if( (FIID != null) AND (FIID2 == null) )
        if( FIID == AccountPayer.Code_Currency )
           CуммаДебет  = СуммаОперации;
        else
           СуммаКредит = СуммаОперации;
        end;
     /*задана только вторая сумма в валюте одного из счетов*/
     elif( (FIID2 != null) AND (FIID == null) )
        if( FIID2 == AccountPayer.Code_Currency )
           CуммаДебет  = СуммаОперации2;
        else
           СуммаКредит = СуммаОперации2;
        end;
     /*заданы обе суммы в валютах счетов по которым выполняем проводку*/
     elif( (FIID2 != null) AND (FIID != null) )                         
       if( FIID == AccountPayer.Code_Currency ) 
          CуммаДебет  = СуммаОперации;
       elif( FIID2 == AccountPayer.Code_Currency ) 
          CуммаДебет  = СуммаОперации2;
       end;

       if( FIID == AccountReceiver.Code_Currency ) 
          СуммаКредит = СуммаОперации;
       elif( FIID2 == AccountReceiver.Code_Currency ) 
          СуммаКредит = СуммаОперации2;
       end;
     else
        return SayCarryError("Не задана ни одна из валют проводки.");
     end;

     if( not CheckMultyCarrySum( AccountPayer.Code_Currency, AccountReceiver.Code_Currency,
                                 CуммаДебет, СуммаКредит, ДатаВалютирования ) )
        return SayCarryError("Ошибка при определении сумм мультивалютной проводки.");
     end;
  
   end;

   if( NoCarry == false )
      /* ------ Выполнить проводку ----------------------------------------------*/
      tr = RsbAccTransaction();
              
      if( tr == NULL )
         return SayCarryError("Ошибка при создании проводки");
      end;

      tr.Chapter         = Chapter;
      tr.Date_Carry      = ДатаВалютирования;
      if( PaymentObj != null )
         tr.Number_Pack  = PaymentObj.NumberPack;
      end;
      tr.Numb_Document   = Numb_Document;
      tr.ResultCarry     = 1;
      tr.Kind_Oper       = Kind_Oper;
      tr.Ground          = "Конвертация. " + Ground;
      tr.Department      = {OperDprt};
      tr.Oper            = {oper};
      tr.FIIDPayer       = AccountPayer.Code_Currency;
      tr.FIIDReceiver    = AccountReceiver.Code_Currency;
      tr.SumPayer        = CуммаДебет;
      tr.SumReceiver     = СуммаКредит;
      tr.AccountPayer    = AccountPayer.Account;
      tr.AccountReceiver = AccountReceiver.Account;
      tr.Shifr_Oper      = "18";

      tr.TypeDocument = "М"; //Перенос по МСФО-9

      if( not tr.Carry() )
         return SayCarryError("Ошибка при выполнении проводки \"" + Ground + "\"");
      else
         msgbox("   Выполнена проводка "+AccountPayer.Account+"<->"+AccountReceiver.Account+" \"" + Ground + "\" на сумму " + СуммаОперации + " " + GetCCY(FIID));
      end;
      /*-------------------------------------------------------------------------*/  

   end;

   if( not stat ) 
      return SayCarryError("Ошибка при вставке документа проводки.");
   end;

   return stat;
END;

private macro GetVABannerQuery()
  var day, month, year;
  DateSplit(CalcDate, day, month, year);
  var query = " select T_BCID as BCID "
            + "   from dvsbanner_dbt bnr "
            + "  where bnr.t_abcstatus = " + VABANNER_STATUS_ACCOUNT
            + "    AND INSTR (bnr.t_bcstate, 'Ч') = 0"
            + "    AND bnr.T_BACKOFFICE = 'N'"
            //+ "    AND bnr.T_REGISTRATIONDATE < to_date('" + day + " " + month + " " + year +"', 'dd mm yyyy')"
            + "    AND ( (SELECT COUNT (*) FROM DFICERT_DBT"
            + "  WHERE T_CERTID = bnr.T_BCID AND T_AVOIRKIND = 5) > 0)";
  return query;
end;

private macro GetVAOverdueBannerQuery()
  var day, month, year;
  DateSplit(CalcDate, day, month, year);
  var query = " select T_BCID as BCID "
            + "   from dvsbanner_dbt bnr "
            + "  where bnr.t_abcstatus = " + VABANNER_STATUS_ACCOUNT
            + "    AND INSTR (bnr.t_bcstate, 'Ч') != 0"
            + "    AND bnr.T_BACKOFFICE = 'N'"
            + "    AND bnr.T_REGISTRATIONDATE <  to_date('" + day + " " + month + " " + year +"', 'dd mm yyyy')"
            + "    AND ( (SELECT COUNT (*) FROM DFICERT_DBT"
            + "  WHERE T_CERTID = bnr.T_BCID AND T_AVOIRKIND = 5) > 0)";
  return query;
end;

private macro GetOverdueRepaymentDeal(BCID)
  var cmd, DataSet, query;
  var tick = TBfile("dl_tick.dbt");

  query =  " SELECT t_DealID as DealID "
        +  "   FROM ddl_tick_dbt tick, dvsbnrbck_dbt bck, doprdocs_dbt oprdocs, doproper_dbt opr"
        +  "   WHERE bck.t_BCID = ?" //BCID
        +  "     AND bck.t_ChangeDate <= ?" //дата коневертирования
        +  "     AND INSTR (bck.t_OLDBcState, 'Ч') = 0"
        +  "     AND INSTR (bck.t_NEWBcState, 'Ч') != 0"
        +  "     AND oprdocs.t_DocKind = 191" //изменение учтенного векселя
        +  "     AND oprdocs.t_DocumentID = LTRIM(TO_CHAR (bck.t_ID, '0000000000'))"
        +  "     AND opr.t_ID_Operation = oprdocs.t_ID_Operation"
        +  "     AND tick.t_BOfficeKind = opr.t_DocKind"
        +  "     AND LPAD( tick.t_DealID, 34,'0' ) = opr.t_DocumentID"
        +  "   ORDER BY bck.t_ChangeDate DESC, bck.t_ID DESC";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(BCID);
  cmd.AddParam(CalcDate);
  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    tick.rec.DealID = DataSet.DealID;
    if(tick.getEQ())
      return tick;
    end;
    return null;
  end;

  return null;
end;

MACRO VA_IsSumSeparation()
  var 
  separate = false;
  if(not VS_GetRegistryValue("ВЕКСЕЛЯ БАНКА\\РЕЖИМ РАБОТЫ\\ПЕРЕНОС \"К ИСП.\" РАЗБИВКА СУММ", V_BOOL, @separate))
    separate = false;
  end;
  return separate;

END;

private macro CreatePaym(tick, leg, Purpose, NumPaym, FIID, Amount, Acc)
  var DocKind, DocID, ВН, newPaym, Voop_Paym, mainPaym, Paym, Debet, Credit, RMProp;
    
  if(ValType(tick) == V_GENOBJ)
    DocID   = tick.rec.DealID;
    DocKind = tick.rec.BofficeKind;
  else
    DocID   = tick.DealID;
    DocKind = tick.BofficeKind;
  end;         
    
  if(ValType(leg) == V_GENOBJ)
    ВН = leg.rec.PFI;
  else
    ВН = leg.PFI;
  end;         
    
  newPaym   = RsbPayment();
  Voop_Paym = TRecHandler("pmpaym.dbt");
  mainPaym  = MyRsbPayment(DocKind, DocID, PM_PURP_PRINC_RET, 0);
  Paym      = mainPaym.GetPM_PAYM();
  Debet     = mainPaym.GetDEBET();
  Credit    = mainPaym.GetCREDIT();
  RMProp    = mainPaym.GetPMRMPROP();
    
  Amount = VA_Convert(Amount, CalcDate, ВН, mainPaym.OrderFIID);

  newPaym.DocKind          = Paym.rec.DocKind;
  newPaym.DocumentID       = Paym.rec.DocumentID;   
  newPaym.Purpose          = Purpose;      
  newPaym.SubPurpose       = NumPaym;   
  newPaym.ValueDate        = Paym.rec.ValueDate;    
  newPaym.OrderAmount      = Amount;//сюда нужную сумму  
  newPaym.BaseFIID         = Paym.rec.BaseFIID;     
  newPaym.ReceiverFIID     = Paym.rec.PayFIID; 
  newPaym.PayerFIID        = Paym.rec.FIID;    
  newPaym.OrderFIID        = Paym.rec.OrderFIID;    
  newPaym.PaymStatus       = PM_OVERDUE;
  newPaym.IsFixPayerAmount = Paym.rec.IsFixAmount;   
  newPaym.CheckTerror      = Paym.rec.CheckTerror;
  newPaym.PayerMesBankID   = Paym.rec.PayerMesBankID;


  newPaym.SetPayerPI(PAYMENTS_GROUP_UNDEF,
                      Paym.rec.PayerBankID,
                      Debet.rec.CodeKind,
                      Debet.rec.BankCode,
                      RMProp.rec.PayerBankName,
                      RMProp.rec.PayerCorrAccNostro,
                      Paym.rec.FIID,
                      Paym.rec.Chapter,
                      Paym.rec.PayerAccount,
                      Paym.rec.Payer,
                      RMProp.rec.PayerName,
                      RMProp.rec.PayerINN,
                      Paym.rec.PayerCodeKind,
                      Paym.rec.PayerCode
                      );

  newPaym.SetReceiverPI(PAYMENTS_GROUP_UNDEF,            // Group  
                        Paym.rec.ReceiverBankID,         // BankID
                        Credit.rec.CodeKind,             // BankCodeKind
                        Credit.rec.BankCode,             // BankCode
                        RMProp.rec.ReceiverBankName,     // BankName
                        "",// CorrAcc
                        FIID,                // AccountFI
                        Paym.rec.Chapter,                // AccountChapter
                        Acc,                             // Account сюда нужный счет
                        Paym.rec.Receiver,               // ClientID
                        RMProp.rec.ReceiverName,         // ClientName,
                        RMProp.rec.ReceiverINN,          // ClientINN,
                        Paym.rec.ReceiverCodeKind,       // ClientCodeKind,
                        Paym.rec.ReceiverCode            // ClientCode
                        );

  VA_SetPaymentGround( newPaym.GetPM_PAYM(), tick );

  return newPaym;
end;

private macro GetVekselName(FD)
  var bnr = FD.GetBnr();
  return "сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber);
end;

private macro GetReserveName(SubKindReserve)
  if(SubKindReserve == VARES_SUBKIND_AVR)
    return " по ц/б";
  elif(SubKindReserve == VARES_SUBKIND_PRC)
    return " по ПДД";
  end;
end;

macro ВыполнитьПереносОстатков()
  var cmd, query, DataSet;
  var FD;
  var cnt = 0, i = 0, j = 0;
  var Sum = $0;
  var err = 0;
  var leg, bnr;
  var msg = "";
  var ВалУч;
  var СчетКредит = TRecHandler("account.dbt");
  var СчетДебет  = TRecHandler("account.dbt");
  var Счет;
  var ReserveFiRole = TArray();
  var ReserveSubKind = TArray();
  var CatFrom, CatTo;
  var НачальныйДисконтВН, НачальныйДисконтВУ;

  ReserveSubKind[0] = VARES_SUBKIND_AVR;
  ReserveSubKind[1] = VARES_SUBKIND_PRC;
  ReserveFiRole[0] = FIROLE_FIDOC;
  ReserveFiRole[1] = FIROLE_PERCENT;

  cmd = DL_RSDCommand(GetVABannerQuery());

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, currPart+"Перенос остатков лицевых счетов, открытых по старым КУ ", currPart+"Перенос остатков лицевых счетов, открытых по старым КУ ");
    
    DataSet = cmd.Execute();
    while((not err) and (DataSet.moveNext()))
      j = 0;
      FD = VSBannerFD(DL_VSBANNER,DataSet.BCID);
      leg = FD.GetLeg();
      bnr = FD.GetBnr();
      ВалУч = FD.ОпределитьВалютуУчета();

      msgbox("Обрабатывается вексель " + GetVekselName(FD));
      
      if(ВексельДисконтный(leg, bnr) )
        CatFrom = "Начисл.ПДД, УВ";
        CatTo = "Начисл.ПДД, УВ";
        if(not VA_GetAccount(CatTo, FD, Счет, MC_OPENACC_CHECKACT, ВалУч, FIROLE_DISCOUNT, null, AccDate))
          msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatTo+"\"");
        elif(not GetBuffAccount(ВалУч, Счет, СчетКредит))
          return 1;
        elif(VA_GetAccount(CatFrom, FD, null, MC_OPENACC_RECREATE, ВалУч, FIROLE_DISCOUNT, СчетДебет, AccDate))
          Sum = ПолучитьСуммуПДД(DataSet.BCID, 0, FIROLE_DISCOUNT, CalcDate) - ПолучитьСуммуПДДНеотнесУнивНаДату(DataSet.BCID, FIROLE_DISCOUNT, CalcDate);
          Sum = VA_Convert(Sum, CalcDate, leg.rec.PFI, ВалУч);
          
          if(Sum != $0)
            if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    СчетДебет, СчетКредит, /* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    1,                           /* Глава */
                    ВалУч,                       /* Валюта */
                    Sum,                         /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    "Дисконт",
                    null,
                    FIROLE_DISCOUNT,
                    FIROLE_DISCOUNT,
                    ВалУч, ВалУч
                    ) 
              )
              msg = GetErrMsg();
              if(msg != "")
                msgbox(msg);
              end;

              return 1;
            end;
          end;
          SetAccountsToClose.AddAcc(СчетКредит.rec.AccountId, ВалУч);

        else
          msgbox("Ошибка открытия счета для векселя" + GetVekselName(FD)+ " с КУ \""+CatFrom+"\"" + "(Д)");
        end;
        НачальныйДисконтВН = УВ_ПолучитьСуммуНачальногоДисконта( bnr, leg, CalcDate );
        НачальныйДисконтВУ = VA_Convert(НачальныйДисконтВН, CalcDate, leg.rec.PFI, ВалУч);
        if( not VA_SaveIncomeSum(VSINCOMETYPE_FIRSTDISC, bnr.rec.BCID, 0/*т.к. на одном шаге - заполнится автоматом*/, date(CalcDate), НачальныйДисконтВН, leg.rec.PFI, НачальныйДисконтВУ, ВалУч) )
          return VA_Err("Не удалось сохранить запись о сумме начального дисконта в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
        end;
      end;

      if(ВексельПроцентный(leg, bnr))
        CatFrom = "Начисл.ПДД, УВ";
        CatTo = "Начисл.ПДД, УВ";
        if(not VA_GetAccount(CatTo, FD, Счет, MC_OPENACC_CHECKACT, ВалУч, FIROLE_PERCENT, null, AccDate))
          msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatTo+"\"");
        elif(not GetBuffAccount(ВалУч, Счет, СчетКредит))
          return 1;
        elif(VA_GetAccount(CatFrom, FD, null, MC_OPENACC_RECREATE, ВалУч, FIROLE_PERCENT, СчетДебет, AccDate))
          Sum = ПолучитьСуммуПДД(DataSet.BCID, 0, FIROLE_PERCENT, CalcDate) - ПолучитьСуммуПДДНеотнесУнивНаДату(DataSet.BCID, FIROLE_PERCENT, CalcDate);
          Sum = VA_Convert(Sum, CalcDate, leg.rec.PFI, ВалУч);

          if(Sum != $0)
            if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    СчетДебет, СчетКредит, /* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    1,                           /* Глава */
                    ВалУч,                       /* Валюта */
                    Sum,                         /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    "Процент",
                    null,
                    FIROLE_PERCENT,
                    FIROLE_PERCENT,
                    ВалУч, ВалУч
                  ) 
              )
              msg = GetErrMsg();
              if(msg != "")
                msgbox(msg);
              end;

              return 1;
            end;
          end;
          SetAccountsToClose.AddAcc(СчетКредит.rec.AccountId, ВалУч);

        else
          msgbox("Ошибка открытия счета для векселя" + GetVekselName(FD)+ " с КУ \""+CatFrom+"\"" + "(П)");
        end;
      end;

      if(НачальнаяПремияПоВекселю(DataSet.BCID, CalcDate))
        CatFrom = "Премия, вексель";
        CatTo = "Премия, вексель";
        if(not VA_GetAccount(CatTo, FD, Счет, MC_OPENACC_CHECKACT, ВалУч, null, null, AccDate))
          msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatTo+"\"");
        elif(not GetBuffAccount(ВалУч, Счет, СчетКредит))
          return 1;
        else
          err = 1;
          Sum = ПолучитьОстатокПремииНаДатуВН(DataSet.BCID, CalcDate);
          Sum = VA_Convert(Sum, CalcDate, leg.rec.PFI, ВалУч);

          if(Sum != $0)
            if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    CatFrom, СчетКредит, /* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    1,                           /* Глава */
                    ВалУч,                       /* Валюта */
                    Sum,                         /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    "Премия",
                    null,
                    null,
                    null,
                    ВалУч, ВалУч
                  ) 
              )
              msg = GetErrMsg();
              if(msg != "")
                msgbox(msg);
              end;

              return 1;
            end;
          end;
          SetAccountsToClose.AddAcc(СчетКредит.rec.AccountId, ВалУч);
        end;
      end;

      CatFrom = "Учтенные векселя";
      CatTo = "Учтенные векселя";
      if(not VA_GetAccount(CatTo, FD, Счет, MC_OPENACC_CHECKACT, ВалУч, null, null, AccDate))
        msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatTo+"\"");
      elif(not GetBuffAccount(ВалУч, Счет, СчетКредит))
        return 1;      
      else
        Sum = БалансоваяСтоимостьВекселяВН(DataSet.BCID, CalcDate);
        Sum = VA_Convert(Sum, CalcDate, leg.rec.PFI, ВалУч);

        if(Sum != $0)
          if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                  CatFrom, СчетКредит, /* КатегДеб , КатегКредит*/
                  AccDate,                     /* Дата */
                  1,                           /* Глава */
                  ВалУч,                       /* Валюта */
                  Sum,                         /* Сумма  */
                  NULL,                        /* Документ */
                  INPCARRY,                    /* Result_Carry */
                  " 1",                        /* Kind_Oper */
                  "",                          /* Numb_Document */
                  "Вложений в векслеь",
                  null,
                  null,
                  null,
                  ВалУч, ВалУч
                ) 
            )
            msg = GetErrMsg();
            if(msg != "")
              msgbox(msg);
            end;

            return 1;
          end;
        end;
        SetAccountsToClose.AddAcc(СчетКредит.rec.AccountId, ВалУч);

      end;

      while(j < ReserveFiRole.size())

        CatFrom = "Резерв, вексель";
        CatTo = "Резерв, вексель";
        FD = VSBannerFD (DL_VSBANNER, DataSet.BCID, DL_VARESERVE, ReserveSubKind[j]);
        if(VA_GetAccount(CatFrom, FD, null, MC_OPENACC_CHECKACT, NATCUR, ReserveFiRole[j], СчетДебет, AccDate))
          ПолучитьОстаток(Sum, СчетДебет.rec.Account, NATCUR, CalcDate);
          Sum = Abs(Sum);

          if(Sum != $0)
            if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    СчетДебет, CatTo, /* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    1,                           /* Глава */
                    NATCUR,                       /* Валюта */
                    Sum,                         /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    "Резерва",
                    null,
                    ReserveFiRole[j],
                    ReserveFiRole[j],
                    NATCUR, NATCUR
                  ) 
              )
              msg = GetErrMsg();
              if(msg != "")
                msgbox(msg);
              end;

              return 1;
            end;

            SetAccountsToClose.AddAcc(СчетДебет.rec.AccountId, NATCUR);
          end;
        else
          msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatFrom+"\"" + GetReserveName(ReserveSubKind[j]));
        end;
        j = j + 1;
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();
  end;

  return err;
END;

macro ВыполнитьПереносНеПолученныхДоходов()
  var cmd, query, DataSet;
  var FD;
  var cnt = 0, i = 0;
  var Sum = $0;
  var SumNat = $0;
  var err = 0;
  var leg;
  var bnr;
  var msg = "";
  var ВалУч;
  var СчетДебет  = TRecHandler("account.dbt");
  var СчетКредит = TRecHandler("account.dbt");
  var CatFrom;
  var CatTo;
  var Счет;

  cmd = DL_RSDCommand(GetVABannerQuery());

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, currPart+"Перенос на баланс неполученного процентного/дисконтного дохода", currPart+"Перенос на баланс неполученного процентного/дисконтного дохода");
    
    DataSet = cmd.Execute();
    while((not err) and (DataSet.moveNext()))

      FD = VSBannerFD(DL_VSBANNER,DataSet.BCID);
      leg = FD.GetLeg();
      bnr = FD.GetBnr();
      ВалУч = FD.ОпределитьВалютуУчета();

      msgbox("Обрабатывается вексель " + GetVekselName(FD));
      
      if(ВексельДисконтный(leg, bnr) )
        CatTo   = ПолучитьКатегориюФР("+%, вексель");
        CatFrom = "Начисл.ПДД, УВ";
        if(not VA_GetAccount(CatFrom, FD, Счет, MC_OPENACC_CHECKACT, ВалУч, FIROLE_DISCOUNT, null, AccDate))
          msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatFrom+"\"");
        elif(not GetBuffAccount(ВалУч, Счет, СчетДебет))
          return 1;
        elif(VA_GetAccount(CatTo, FD, null, MC_OPENACC_CHECKEXIST, NATCUR, null, СчетКредит, AccDate))
          Sum = ПолучитьСуммуПДДНеотнесУнивНаДату(DataSet.BCID, FIROLE_DISCOUNT, CalcDate);
          SumNat = VA_Convert(Sum, CalcDate, leg.rec.PFI, NATCUR);
          Sum = VA_Convert(Sum, CalcDate, leg.rec.PFI, ВалУч);
          
          if(Sum != $0)
            if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    СчетДебет, СчетКредит,/* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    1,                           /* Глава */
                    ВалУч,                       /* Валюта */
                    Sum,                         /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    "Дисконт",
                    null,
                    FIROLE_DISCOUNT,
                    null,
                    NATCUR, SumNat
                  ) 
              )
              msg = GetErrMsg();
              if(msg != "")
                msgbox(msg);
                msgbox("ID Векселя " + DataSet.BCID);
              end;

              return 1;
            end;
          end;

        else
          msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatTo+"\"");
        end;
      end;

      if(ВексельПроцентный(leg, bnr))
        CatTo   = ПолучитьКатегориюФР("+%, вексель");
        CatFrom = "Начисл.ПДД, УВ";
        if(not VA_GetAccount(CatFrom, FD, Счет, MC_OPENACC_CHECKACT, ВалУч, FIROLE_PERCENT, null, AccDate))
            msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatFrom+"\"");
        elif(not GetBuffAccount(ВалУч, Счет, СчетДебет))
          return 1;
        elif(VA_GetAccount(CatTo, FD, null, MC_OPENACC_CHECKEXIST, NATCUR, null, СчетКредит, AccDate))
          Sum = ПолучитьСуммуПДДНеотнесУнивНаДату(DataSet.BCID, FIROLE_PERCENT, CalcDate);
          SumNat = VA_Convert(Sum, CalcDate, leg.rec.PFI, NATCUR);
          Sum = VA_Convert(Sum, CalcDate, leg.rec.PFI, ВалУч);

          if(Sum != $0)
            if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    СчетДебет, СчетКредит, /* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    1,                           /* Глава */
                    ВалУч,                       /* Валюта */
                    Sum,                         /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    "Процент",
                    null,
                    FIROLE_PERCENT,
                    null,
                    ВалУч, NATCUR,
                    NATCUR, SumNat
                  ) 
              )
              msg = GetErrMsg();
              if(msg != "")
                msgbox(msg);
                msgbox("ID Векселя " + DataSet.BCID);
              end;

              return 1;
            end;
          end;

        else
          msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatTo+"\"");
        end;
      end;

      if(ВексельПроцентный(leg, bnr) or ВексельДисконтный(leg, bnr))
        CatTo   = "%Ндоход внебаланс, УВ";
        CatFrom = "ВнебалСчетКорресп";
        if(not VA_GetAccount(CatFrom, FD, null, MC_OPENACC_CHECKEXIST, NATCUR, FIROLE_CORACC_ACTIVE, СчетДебет, AccDate))
          msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatFrom+"\"");
        elif(VA_GetAccount(CatTo, FD, Счет, MC_OPENACC_CHECKACT, ВалУч, null, null, AccDate))
          msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatTo+"\"");
        elif(not GetBuffAccount(ВалУч, Счет, СчетКредит, 3))
          return 1;
        else
          ПолучитьОстаток(Sum, СчетКредит.rec.Account, ВалУч, CalcDate, 3);
          Sum = abs(Sum);
          SumNat = VA_Convert(Sum, CalcDate, ВалУч, NATCUR);

          if(Sum != $0)
            if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    СчетДебет, СчетКредит, /* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    3,                           /* Глава */
                    NATCUR,                       /* Валюта */
                    SumNat,                         /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    "Процент",
                    null,
                    FIROLE_PERCENT,
                    null,
                    NATCUR, ВалУч,
                    ВалУч, Sum
                    ) 
              )
              msg = GetErrMsg();
              if(msg != "")
                msgbox(msg);
              end;

              return 1;
            end;

            SetAccountsToClose.AddAcc(СчетКредит.rec.AccountId, ВалУч);
          end;

        end;
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();
  end;

  return err;
END;

macro ПереносПросроченныхЗадолжностей()
  var cmd, query, DataSet;
  var FD;
  var cnt = 0, i = 0, j = 0;
  var Sum = $0, PaySum = $0;
  var err = 0;
  var bnr, leg;
  var msg = "";
  var ВалУч;
  var СчетДебет  = TRecHandler("account.dbt");
  var СчетКредит = TRecHandler("account.dbt");
  var tick       = TRecHandler("dl_tick.dbt");
  var CatFrom, CatTo;
  var DealID, pm_obj, NRepay;
  var ReserveFiRole = TArray();
  var ReserveSubKind = TArray();
  var Счет;

  ReserveSubKind[0] = VARES_SUBKIND_AVR;
  ReserveSubKind[1] = VARES_SUBKIND_PRC;
  ReserveFiRole[0] = FIROLE_FIDOC;
  ReserveFiRole[1] = FIROLE_PERCENT;

  cmd = DL_RSDCommand(GetVAOverdueBannerQuery());

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, currPart+"Обработка остатков просроченных векселей", currPart+"Обработка остатков просроченных векселей");
    
    DataSet = cmd.Execute();
    while((not err) and (DataSet.moveNext()))

      FD = VSBannerFD(DL_VSBANNER,DataSet.BCID);
      leg = FD.GetLeg();
      bnr = FD.GetBnr();
      ВалУч = FD.ОпределитьВалютуУчета();

      msgbox("Обрабатывается вексель " + GetVekselName(FD));
            
      tick = GetOverdueRepaymentDeal(DataSet.BCID);
      if(tick == null)
        msgbox("Ошибка в получение сделки Погашения для векселя" + GetVekselName(FD));
        return 1;
      end;
      DL_GetVekselNumberInDoc(NRepay, bnr.rec.BCID, VSORDLNK_K_ALL, tick.rec.DealID, tick.rec.BofficeKind);
      
      CatFrom = "Просроч_вексель";
      CatTo = "Учтенные векселя";
      if(not VA_GetAccount(CatTo, FD, Счет, MC_OPENACC_CHECKACT, ВалУч, null, null, AccDate))    
        msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatTo+"\"");
      elif(not GetBuffAccount(ВалУч, Счет, СчетКредит))
        return 1;
      else
        Sum = БалансоваяСтоимостьВекселяВН(DataSet.BCID, CalcDate);
        Sum = VA_Convert(Sum, CalcDate, leg.rec.PFI, ВалУч);

        if(Sum != $0) 
          
          if(VA_GetAccount(CatFrom, FD, null, MC_OPENACC_RECREATE, ВалУч, null, СчетДебет, AccDate))
            if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    СчетДебет, СчетКредит, /* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    1,                           /* Глава */
                    ВалУч,                       /* Валюта */
                    Sum,                         /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    "Вложений в векслеь",
                    null,
                    null,
                    null,
                    ВалУч, ВалУч
                  ) 
              )
              msg = GetErrMsg();
              if(msg != "")
                msgbox(msg);
              end;

              return 1;
            end;

            if(((pm_obj = MyRsbPayment(tick.rec.BofficeKind, tick.rec.DealID, PM_PURP_PRINC_RET, NRepay)) == null)
                  OR (pm_obj.PaymentID <= 0))
                pm_obj = CreatePaym(tick, leg, PM_PURP_PRINC_RET, NRepay, ВалУч, VA_Convert(leg.rec.Principal, CalcDate, leg.rec.PFI, ВалУч), СчетДебет.rec.Account);
            else 
                pm_obj.OrderAmount = VA_Convert(leg.rec.Principal, CalcDate, leg.rec.PFI, pm_obj.OrderFIID);
                pm_obj.SetReceiverAccount( ВалУч, 1, СчетДебет.rec.Account );
            end;
            pm_obj.Update();

          else
            msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatFrom+"\"");
          end;
        end;
        
        if(ВексельДисконтный(leg, bnr) )
          if(VA_GetAccount(CatFrom, FD, null, MC_OPENACC_RECREATE, ВалУч, null, СчетДебет, AccDate))
            Sum = ПолучитьСуммуПДД(DataSet.BCID, 0, FIROLE_DISCOUNT, CalcDate);
            Sum = VA_Convert(Sum, CalcDate, leg.rec.PFI, ВалУч);
            
            if(Sum != $0)
              if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                      СчетДебет, СчетКредит, /* КатегДеб , КатегКредит*/
                      AccDate,                     /* Дата */
                      1,                           /* Глава */
                      ВалУч,                       /* Валюта */
                      Sum,                         /* Сумма  */
                      NULL,                        /* Документ */
                      INPCARRY,                    /* Result_Carry */
                      " 1",                        /* Kind_Oper */
                      "",                          /* Numb_Document */
                      "Перенос просрочной задолжности по Дисконту",
                      null,
                      null,
                      null,
                      ВалУч, ВалУч
                    ) 
                )
                msg = GetErrMsg();
                if(msg != "")
                  msgbox(msg);
                end;

                return 1;
              end;
            end;

          else
            msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatFrom+"\"");
          end;
        end;

        if(ВексельПроцентный(leg, bnr))
          if(VA_IsSumSeparation())

            if(VA_GetAccount(CatFrom, FD, null, MC_OPENACC_RECREATE, ВалУч, FIROLE_PERCENT, СчетДебет, AccDate))
              Sum = ПолучитьСуммуПДД(DataSet.BCID, 0, FIROLE_PERCENT, CalcDate);
              PaySum = Sum;
              Sum = VA_Convert(Sum, CalcDate, leg.rec.PFI, ВалУч);

              if(Sum != $0)
                if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                        СчетДебет, СчетКредит, /* КатегДеб , КатегКредит*/
                        AccDate,                     /* Дата */
                        1,                           /* Глава */
                        ВалУч,                       /* Валюта */
                        Sum,                         /* Сумма  */
                        NULL,                        /* Документ */
                        INPCARRY,                    /* Result_Carry */
                        " 1",                        /* Kind_Oper */
                        "",                          /* Numb_Document */
                          "Перенос просрочной задолжности по Процентам",
                        null,
                        FIROLE_PERCENT,
                        FIROLE_PERCENT,
                        ВалУч, ВалУч
                      ) 
                  )
                  msg = GetErrMsg();
                  if(msg != "")
                    msgbox(msg);
                  end;

                  return 1;
                end;

                if(((pm_obj = MyRsbPayment(tick.rec.BofficeKind, tick.rec.DealID, PM_PURP_PERCENT, NRepay)) == null)
                    OR (pm_obj.PaymentID <= 0))
                  pm_obj = CreatePaym(tick, leg, PM_PURP_PERCENT, NRepay, ВалУч, PaySum, СчетДебет.rec.Account);
                else 
                  pm_obj.OrderAmount = VA_Convert(PaySum, CalcDate, leg.rec.PFI, pm_obj.OrderFIID);
                  pm_obj.SetReceiverAccount( ВалУч, 1, СчетДебет.rec.Account );
                end;
                pm_obj.Update();  
              end;
              
              
            else
              msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatFrom+"\"");
            end;
          else
            if(VA_GetAccount(CatFrom, FD, null, MC_OPENACC_RECREATE, ВалУч, null, СчетДебет, AccDate))
              Sum = ПолучитьСуммуПДД(DataSet.BCID, 0, FIROLE_PERCENT, CalcDate);
              PaySum = Sum;
              Sum = VA_Convert(Sum, CalcDate, leg.rec.PFI, ВалУч);

              if(Sum != $0)
                if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                        СчетДебет, СчетКредит, /* КатегДеб , КатегКредит*/
                        AccDate,                     /* Дата */
                        1,                           /* Глава */
                        ВалУч,                       /* Валюта */
                        Sum,                         /* Сумма  */
                        NULL,                        /* Документ */
                        INPCARRY,                    /* Result_Carry */
                        " 1",                        /* Kind_Oper */
                        "",                          /* Numb_Document */
                          "Перенос просрочной задолжности по Процентам",
                        null,
                        null,
                        FIROLE_PERCENT,
                        ВалУч, ВалУч
                      ) 
                  )
                  msg = GetErrMsg();
                  if(msg != "")
                    msgbox(msg);
                  end;

                  return 1;
                end;

                if(pm_obj == null)
                  pm_obj = CreatePaym(tick, leg, PM_PURP_PRINC_RET, NRepay, ВалУч, PaySum, СчетДебет.rec.Account);
                else 
                  pm_obj.OrderAmount = pm_obj.OrderAmount + VA_Convert(PaySum, CalcDate, leg.rec.PFI, pm_obj.OrderFIID);
                  pm_obj.SetReceiverAccount( ВалУч, 1, СчетДебет.rec.Account );
                end;
                pm_obj.Update();  
              end;
              
            else
              msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatFrom+"\"");
            end;
          end;
        end;

        SetAccountsToClose.AddAcc(СчетКредит.rec.AccountId, ВалУч);
      end;
      
      while(j < ReserveFiRole.size())
        FD = VSBannerFD (DL_VSBANNER, DataSet.BCID, DL_VARESERVE, ReserveSubKind[j]);
        Sum = $0;

        CatTo = "Резерв, вексель";
        CatFrom = ПолучитьКатегориюФР("+Маржа, вексель");
        if(not VA_GetAccount(CatTo, FD, Счет, MC_OPENACC_CHECKACT, NATCUR, ReserveFiRole[j], null, AccDate))
          msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatTo+"\"" + GetReserveName(ReserveSubKind[j]));
        elif(not GetBuffAccount(NATCUR, Счет, СчетДебет))
          return 1;
        elif(VA_GetAccount(CatFrom, FD, null, MC_OPENACC_CHECKEXIST, NATCUR, null, СчетКредит, AccDate))
          ПолучитьОстаток(Sum, СчетДебет.rec.Account, NATCUR, CalcDate);
          Sum = Abs(Sum);

          if(Sum != $0)
            if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    СчетДебет, СчетКредит, /* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    1,                           /* Глава */
                    NATCUR,                       /* Валюта */
                    Sum,                         /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    "Восстанавление резерва на возможные потери",
                    null,
                    ReserveFiRole[j],
                    null,
                    NATCUR, NATCUR
                  ) 
              )
              msg = GetErrMsg();
              if(msg != "")
                msgbox(msg);
              end;

              return 1;
            end;
          end;
          SetAccountsToClose.AddAcc(СчетДебет.rec.AccountId, NATCUR);
        else
          msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatFrom+"\"");
        end;
      
        if(Sum != $0)
          CatTo = ПолучитьКатегориюФР("-Маржа, вексель");
          CatFrom = "РезервПросроч, вексель";
          if(VA_GetAccount(CatTo, FD, null, MC_OPENACC_CHECKEXIST, NATCUR, null, СчетДебет, AccDate))

            if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    СчетДебет, CatFrom, /* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    1,                           /* Глава */
                    NATCUR,                       /* Валюта */
                    Sum,                         /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    "Формирование резерва в размере списанного на возможные потери",
                    null,
                    null,
                    ReserveFiRole[j],
                    NATCUR, NATCUR
                  ) 
              )
              msg = GetErrMsg();
              if(msg != "")
                msgbox(msg);
              end;

              return 1;
            end;
          else
            msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+CatTo+"\"");
          end;
        end;

        j = j + 1;
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();
  end;

  return err;
end;

macro ОбработатьВекселяПоПервоначальномуПризнанию()
  var cmd, query, DataSet;
  var FD;
  var cnt = 0, i = 0;
  var CatCred = "";
  var Sum = $0;
  var msg = "",err;
  var ВалУч;
  var leg, bnr;
  var МинРПС, МаксРПС, ОтклСуществ, ССНацВал, 
      ДнейВГоду, СрокОбращения, НастройкаДОГод,
      НастройкаДОНесущОткл, СуммаКорр, СуммаКоррКНачисл, СуммКоррВУ;
  var ЭПС, АСпоЭПС, АСпоЛМ, ДатаПокупкиВекселя;
  var КатегДеб, КатегКредит, Ground;
  var EnrolId, SourceDataLayer;
  var СПДСВН,СПДСНацВал,ОтсрРазнНацВал,ОтсрРазнВУ,
      ОтсрРазнНацВалКНачисл,FairValue,СуммаДисконта,
      ДатаПослРасчКорр,InterestIncomeAdd,СуммаПроцентов, 
      НачисПроц, СуммаПроцентовВУ, prccontract,
      Корр = NULL, КоррВал = NULL, РасчетСС, DealId,
      pMsg,pCat55,pCat66,PAlgUsed,pEir,pAmortCalcKind,
      НоваяСС, ОстаточнСтоимостьВН, ОстаточнСтоимостьНацВал,
      ОтсрРазнВН, ВалютаДебет, ВалютаКредит, СуммаДебет,
      СуммаКредит, СуммаКоррНацВал, СуммаКоррВУ, pRateId;
  record income("vsincome");
  prccontract = TRecHandler("prccontract");

  cmd = DL_RSDCommand(GetVABannerQuery());

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, currPart+"Обработка векселей по первоначальному признанию", currPart+"Обработка векселей по первоначальному признанию");
    
    DataSet = cmd.Execute();
    while((not err) and (DataSet.moveNext()))

      FD = VSBannerFD(DL_VSBANNER,DataSet.BCID);

      msgbox("Обрабатывается вексель " + GetVekselName(FD));

      ВалУч = FD.ОпределитьВалютуУчета();
      bnr = FD.GetBnr();
      leg = FD.GetLeg();

      EnrolId = VA_GetLastAccountedEnrolmentID(bnr.rec.BCID, CalcDate);
      ДатаПокупкиВекселя = VA_GetLastBalanceDate(bnr.rec.BCID, CalcDate);

      if (not УВ_ПолучитьИдСделки(bnr.rec.BCID, CalcDate, DealId))
        return VA_Err("Не удалось получить DealId для векселя");
      end;

      ОстаточнСтоимостьВН = БалансоваяСтоимостьВекселяВН(bnr.rec.BCID, CalcDate);
      ОстаточнСтоимостьНацВал = VA_Convert(ОстаточнСтоимостьВН, CalcDate, leg.rec.PFI, NATCUR);

      if ((leg.rec.IncomeRate != $0) and (leg.rec.PrincipalDiff!=0) and (leg.rec.RelativePrice != BNR_UNDEF_CHAR)) //Параметры заданы вручную
        MsgBox("По векселю "+GetVekselName(FD)+" основные параметры заданы вручную, расчёт не используется");
        ПолучитьЗначениеКорректировкиНаНерыночность(bnr.rec.BCID,EnrolId,@Корр,@КоррВал);
        ОтсрРазнНацВал =  VA_Convert(Корр, CalcDate, КоррВал, NATCUR );
        ОтсрРазнВУ =  VA_Convert(Корр, CalcDate, КоррВал, ВалУч );
        ОтсрРазнВН =  VA_Convert(Корр, CalcDate, КоррВал, leg.rec.PFI );
        НоваяСС = ОстаточнСтоимостьНацВал + ОтсрРазнНацВал;
        ЭПС = leg.rec.IncomeRate / 100;
      else
        MsgBox("По векселю "+GetVekselName(FD)+" не заданы основные параметры, параметры расчитаются автоматически");
        
        if (not УВ_РасчетСС(bnr.rec.Fiid,bnr.rec.BCID,DealId,ДатаПокупкиВекселя,РасчетСС,pMsg,pRateId,pCat55,pCat66,PAlgUsed,ЭПС,pAmortCalcKind))
          return VA_Err("Ошибка расчета справедливой стоимости: "+pMsg);
        end;
        ОтсрРазнВН = РасчетСС - ОстаточнСтоимостьВН;
        ОтсрРазнВУ = VA_Convert(ОтсрРазнВН, CalcDate, leg.rec.PFI, ВалУч );
        ОтсрРазнНацВал = VA_Convert(ОтсрРазнВН, CalcDate, leg.rec.PFI, NATCUR );
        НоваяСС = VA_Convert(РасчетСС, CalcDate, leg.rec.PFI, NATCUR );
        leg.rec.IncomeRate = ЭПС * 100;
        if ((pCat55 == 1) or (pCat55 == 2))
          leg.rec.PayRegTax = SET_CHAR; //Наблюдаемые данные
        elif (pCat55 == 3)
          leg.rec.PayRegTax = UNSET_CHAR; //Ненаблюдаемые данные
        end;
        if (pAmortCalcKind == METHOD_RPS)
          ОтклСуществ = true;
          leg.rec.PrincipalDiff = METHOD_RPS;
        else
          ОтклСуществ = false;
        end;
        leg.rec.RelativePrice = IIF(ОтклСуществ,BNR_UNSET_CHAR,BNR_SET_CHAR);
      end;

      if (leg.rec.RelativePrice == BNR_UNSET_CHAR) // Тест на рын. = нет
        if (leg.rec.PayRegTax == SET_CHAR) // Наблюдаемые данные = да
          if (ОтсрРазнНацВал != $0)
            if (ОтсрРазнНацВал > $0)
              КатегДеб = "КорСт_Уменьшение, вексель";
              КатегКредит = ПолучитьКатегориюФР("+Маржа, вексель");
              ВалютаДебет = ВалУч;
              ВалютаКредит = NATCUR;
              СуммаДебет = ОтсрРазнВУ;
              СуммаКредит = ОтсрРазнНацВал;
              Ground = String("Отражение корректировки по ц/б " + GetVekselName(FD) + " уменьшающая стоимость");
            else 
              КатегДеб = ПолучитьКатегориюФР("-Маржа, вексель");
              КатегКредит = "КорСт_Увеличение, вексель";
              ВалютаДебет = NATCUR;
              ВалютаКредит = ВалУч;
              СуммаДебет = NATCUR;
              СуммаКредит = ОтсрРазнВУ;
              Ground = String("Отражение корректировки по ц/б " + GetVekselName(FD) + " увеличивающая стоимость");
            end;
            if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    КатегДеб, КатегКредит, /* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    1,                           /* Глава */
                    ВалютаДебет,                 /* Валюта */
                    ABS(СуммаДебет),             /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    Ground,
                    null,
                    NULL,
                    null,
                    ВалютаДебет, ВалютаКредит, ВалютаКредит, ABS(СуммаКредит)
                    ) 
                )
                msg = GetErrMsg();
                if(msg != "")
                    msgbox(msg);
                end;

                return 1;
            end;

          end;
        else //Наблюдаемые данные = нет
          if( not VS_SaveIncomeSum(VSINCOMETYPE_DEFDIF, bnr.rec.BCID, EnrolId, CalcDate, ОтсрРазнНацВал, NATCUR, ОтсрРазнВУ,ВалУч) )
            return VA_Err("Не удалось сохранить запись о отсроченной разнице в истории для ц/б " + GetVekselName(FD));
          end;
          if ((ОтсроченнаяРазница == 2) and (СчетДляУчетаФР == 706))
            ОтсрРазнНацВалКНачисл = ПолучитьОтсроченнуюРазницуНаДату(fd, CalcDate) - ПолучитьОтраженнуюОтсроченнуюРазницу(bnr.rec.BCID, CalcDate, EnrolId);
            if (ОтсрРазнНацВалКНачисл != $0)
              if (ОтсрРазнНацВалКНачисл > $0)
                КатегДеб = "-КорЭПС_%вексель";
                КатегКредит = "+Маржа, вексель";
                Ground = String("Отражение положительной отсроченной разницы по ц/б " + GetVekselName(FD));
              else
                КатегДеб = "-Маржа, вексель";
                КатегКредит = "+КорЭПС_%вексель";
                Ground = String("Отражение отрицательной отсроченной разницы по ц/б " + GetVekselName(FD));
              end;
              if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    КатегДеб, КатегКредит, /* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    1,                           /* Глава */
                    NATCUR,                      /* Валюта */
                    ABS(ОтсрРазнНацВалКНачисл),  /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    Ground,
                    null,
                    NULL,
                    null,
                    NATCUR, NATCUR
                    ) 
                )
                  msg = GetErrMsg();
                  if(msg != "")
                    msgbox(msg);
                  end;
     
                  return 1;
              end;
              ClearRecord(income);
              income.AutoKey        = 0;
              income.BCID           = bnr.rec.BCID;     // ID векселя/сертификата
              income.Currency       = NATCUR;  // Валюта
              income.OperDate       = CalcDate;  // Операционный день, когда выполнялось начисление
              income.Quality        = SET_CHAR;
              income.Enrolment_ID   = EnrolId;
              income.AccCurrency    = ВалУч;
              income.IncomeType     = VSINCOMETYPE_CHARGE;
              income.DefDif         = ОтсрРазнНацВалКНачисл;
              income.AccDefDif      = VS_Convert(ОтсрРазнНацВалКНачисл, CalcDate, NATCUR, ВалУч);
              if( InsertVSINCOME(income) )
                return VA_Err("Не удалось вставить запись в vsincome");
              end;
            end;
          end;
        end;

      end;

      if (НоваяСС > $0)
        var insCmd = RSDCommand( "INSERT INTO DBNRFRVAL_DBT (T_BCID, T_BEGDATE, T_CONTRACTID, T_FAIRVALUE, T_ACCOUNTED)"
                            +"            VALUES  (?, ?, ?, ?, ?)");

        insCmd.NullConversion = true;
        insCmd.addParam( "", RSDBP_IN, bnr.rec.BCID      );
        insCmd.addParam( "", RSDBP_IN, CalcDate );
        insCmd.addParam( "", RSDBP_IN, DealId );
        insCmd.addParam( "", RSDBP_IN, НоваяСС );
        insCmd.addParam( "", RSDBP_IN, SET_CHAR );
        insCmd.execute();
      end;

      ДнейВГоду=DaysInYearByBasis(leg.rec.Basis, ДатаПокупкиВекселя);
      СрокОбращения = VS_GetTermCircDate(bnr,leg);

      GetRegistryValue( "SECUR\\МСФО\\ЭПС ДЛЯ  ДО МЕНЬШЕ ГОДА", V_BOOL, НастройкаДОГод, err );
      if (err != 0)
        return VA_Err("Ошибка получения настройки \"ЭПС для ДО меньше года\"");
      end;
      GetRegistryValue( "SECUR\\МСФО\\ЭПС ДЛЯ ДО С НЕСУЩЕСТВ. ОТКЛ", V_BOOL, НастройкаДОНесущОткл, err );
      if (err != 0)
        return VA_Err("Ошибка получения настройки \"ЭПС для ДО с несущ. откл\"");
      end;
      
      if (leg.rec.PrincipalDiff == METHOD_UNDEF) // Не определён метод расчёта АС
        if (not СущественностьОтклонения(ДатаПокупкиВекселя, L_OBJTYPE_LEVELESSENTIAL_AC, bnr.rec.Portfolioid, 
                                        DealId, NULL, null, DL_VSBANNER, bnr.rec.BCID, ОтклСуществ))
          return VA_Err("Ошибка расчёта отклонения АС");
        end;
        //тест на рыночность = да и (срок "по предъявлении" или обращение <= год) и настройка = нет
        if (((bnr.rec.BCTermFormula == VS_TERMF_ATSIGHT) or (СрокОбращения <= ДнейВГоду)) 
          and (НастройкаДОГод == false)) 
          leg.rec.PrincipalDiff = METHOD_LINE;
        elif (ОтклСуществ == false)
          leg.rec.PrincipalDiff = METHOD_LINE;
        else
          leg.rec.PrincipalDiff = METHOD_EPS;
        end;
      end;

      if (leg.rec.PrincipalDiff == METHOD_EPS)
        ДатаПослРасчКорр = УВ_ПолучитьДатуПоследнегоРасчетаКорр(bnr.rec.BCID, CalcDate);
        СуммаДисконта = УВ_ПолучитьСуммуДисконтаНаДату(fd, CalcDate) - УВ_ПолучитьСуммуДисконтаНаДату(fd, ДатаПослРасчКорр);
        if (not РасчетПроцентовХранЗаПериод(leg.rec.id, ДатаПослРасчКорр, CalcDate, InterestIncomeAdd ))
          InterestIncomeAdd = $0;
        end;
        СуммаКорр = РасчетКорректировкиПроцентаПоЭПСУВ(DealId,bnr.rec.BCID,CalcDate, InterestIncomeAdd, 0, 
                                                      СуммаДисконта, VS_Convert(НоваяСС, CalcDate, NATCUR, leg.rec.PFI), ЭПС);
        СуммаКоррНацВал = VA_Convert( СуммаКорр, CalcDate, leg.rec.PFI, NATCUR );
        СуммаКоррВУ = VA_Convert( СуммаКорр, CalcDate, leg.rec.PFI, ВалУч);
        if (СуммаКоррНацВал != $0)
          if (СуммаКоррНацВал < $0)
            КатегДеб = "-КорЭПС_%вексель";
            КатегКредит = "КорСт_Уменьшение, вексель";
            ВалютаДебет = NATCUR;
            ВалютаКредит = ВалУч;
            СуммаДебет = СуммаКоррНацВал;
            СуммаКредит = СуммаКоррВУ;
          else 
            КатегДеб = "КорСт_Увеличение, вексель";
            КатегКредит = "+КорЭПС_%вексель";
            ВалютаДебет = ВалУч;
            ВалютаКредит = NATCUR;
            СуммаДебет = СуммаКоррВУ;
            СуммаКредит = СуммаКоррНацВал;
          end;
          Ground = String("Корректировка стоимости векселя на разность между суммой процентов по ставке векселя и по ЭПС по ц/б " + GetVekselName(FD));
          if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                КатегДеб, КатегКредит, /* КатегДеб , КатегКредит*/
                AccDate,                     /* Дата */
                1,                           /* Глава */
                ВалютаДебет,                 /* Валюта */
                ABS(СуммаДебет),             /* Сумма  */
                NULL,                        /* Документ */
                INPCARRY,                    /* Result_Carry */
                " 1",                        /* Kind_Oper */
                "",                          /* Numb_Document */
                Ground,
                null,
                NULL,
                null,
                ВалютаДебет, ВалютаКредит, ВалютаКредит, ABS(СуммаКредит)
                ) 
            )
              msg = GetErrMsg();
              if(msg != "")
                msgbox(msg);
              end;

              return 1;
          end;
          if( not VS_SaveIncomeSum(VSINCOMETYPE_EPRPERC, bnr.rec.BCID, EnrolId, ДатаПокупкиВекселя, СуммаКорр, leg.rec.PFI, СуммаКоррВУ, ВалУч) )
            return false;
          end;
        end;

      end;

      if(ВексельПроцентный(leg))
        if(not НайтиСчетПроцВекселя(prccontract, bnr.rec.BCID, null, VS_FINDPC_ACTUAL))
          return VA_Err("Не найден счет процентного векселя");
        elif(not ПолучитьСуммуКНачисл_В(bnr, leg, prccontract.rec.EndDate, @СуммаПроцентов, @НачисПроц))
          return VA_Err("Не удалось получить сумму к начислению");
        else
          СуммаПроцентов = СуммаПроцентов + НачисПроц;
          СуммаПроцентовВУ = VS_Convert(СуммаПроцентов, CalcDate, leg.rec.PFI, fd.ОпределитьВалютуУчета());
          if( not VS_SaveIncomeSum(VSINCOMETYPE_PERCENT, bnr.rec.BCID, 0, date(CalcDate), СуммаПроцентов, leg.rec.PFI, СуммаПроцентовВУ, fd.ОпределитьВалютуУчета()) )
            return VA_Err("Не удалось сохранить запись о сумме процентов в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
          end; 
        end;
      end;

      if (not ОбновитьПараметрыВекселя(bnr,leg))
        return VA_Err("Ошибка обновления параметров векселя");
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();
  end;

  return err;
END;

macro StartTrans(part:string, msg:string)
  msgbox(part+msg);
  RslDefCon.BeginTrans();

  //currIDStep = currIDStep - 1;
  currPart = part;
end;

macro EndTrans(err)
  if(err)
    RslDefCon.RollbackTrans();
    msgbox("При выполнении возникли ошибки!");
  else
    RslDefCon.CommitTrans();
    //RslDefCon.RollbackTrans();
    msgbox("Выполнено успешно");
  end;

  msgbox("");
end;


macro StartExec()
  var err = 0;

  ReplaceMacro( "msgbox", "cnv_msgbox" );

  msgbox("Начало конвертации");


  if((СчетДляУчетаФР != 706) and (СчетДляУчетаФР != 10801) and (СчетДляУчетаФР != 10901))
    msgbox("Не реализована обработка значения "+СчетДляУчетаФР+" для параметра \"СчетДляУчетаФР\"");
    err = 1;
  end;

  if(not err)
    if((ОтсроченнаяРазница != 1) and (ОтсроченнаяРазница != 2))
      msgbox("Не реализована обработка значения "+ОтсроченнаяРазница+" для параметра \"ОтсроченнаяРазница\"");
      err = 1;
    end;
  end;

  CorrectFlag();

  if(not err)

    msgbox("1. Обработка остатков счетов");
    
    StartTrans("  1.1 ", "Перенос остатков лицевых счетов, открытых по старым КУ ");
      err = ВыполнитьПереносОстатков();
      if(not err) 
        err = SetAccountsToClose.CloseAllAcc();
      end;
      SetAccountsToClose.Clear();
    EndTrans(err); 

    StartTrans("  1.2. ", "Перенос на баланс неполученного процентного/дисконтного дохода ");
      if(not err) 
        err = ВыполнитьПереносНеПолученныхДоходов();
      end;
      if(not err) 
        err = SetAccountsToClose.CloseAllAcc();
      end;
      SetAccountsToClose.Clear();
    EndTrans(err); 

    StartTrans("  1.3. ", "Обработка остатков просроченных векселей");
      if(not err) 
        err = ПереносПросроченныхЗадолжностей();
      end;
      if(not err) 
        err = SetAccountsToClose.CloseAllAcc();
      end;
      SetAccountsToClose.Clear();
    EndTrans(err); 

  end; 

   if(not err)
     msgbox("2. Первоначальное признание");
     
     StartTrans("  2.1. ", "Обработка векселей процедурой первоначального признания");
     err = ОбработатьВекселяПоПервоначальномуПризнанию();
     EndTrans(err);  
   end;

  if(not err)
    msgbox("Конвертация успешно завершена");
  else
    msgbox("#################################################################################");
    msgbox("ВНИМАНИЕ!!!");
    msgbox("При конвертации возникли ошибки. \nПосле устранения причин ошибок повторную конвертацию следует выполнять, начиная с проблемного блока. \nВсе предыдущие успешно выполненные блоки следует закомментировать в макросе.");
    msgbox("#################################################################################");
  end;

  ReplaceMacro( "msgbox", NULL );

  OnError( err );
  var j;
  var err_text = "", delim = "";

  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;


  if (IsEqClass ("TrslError", err))
    err_text=err.Message;
    if(IsEqClass ("TDbError", err.err))
      err_text=err_text+":|"+err.err.Stat+"-"+err.err.Oper+"-"+err.err.Table+"-"+err.err.Message;
    elif (isEqClass("TRsdError", err.err))

      j = 0;
      while(j < err.err.environment.ErrorCount)
          err_text = err_text + delim  + err.err.environment.error(j).descr;
          delim = "\n";
          j = j + 1;
      end;
    end;
  end;

  if(err_text == "")
    err_text=err.module+ " "+err.line+" "+err.message;
  end;

  msgbox(err_text);

  return 1;
end;

ReplaceMacro("msgbox", "cnv_msgbox");

StartExec();

ReplaceMacro("msgbox", NULL );