/*
$Name:        update_nameaccount_DEF-35928.mac
$Module:      Ценные бумаги
$Description: Изменение наименований счетов по КУ "+РасчетыКомисс1"
*/

import DealsInter, spserv, sp_class, sp_car, spreserv, dlcontrfunc;

private var TestMode   = true;      //Тестовый режим (без фиксации действий в БД)


PRIVATE MACRO UpdateAccRec(GUID, AccountID, NameAccount)
  var query, cmd;

  query = "UPDATE ddpaccreplog_dbt SET t_Message = ? WHERE T_GUID = ? AND T_DEPOACNTID = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(NameAccount);
  cmd.AddParam(GUID);
  cmd.AddParam(AccountID);

  cmd.ExecuteCMD();
END;

PRIVATE MACRO ClearTable()
  var query = "TRUNCATE TABLE DDPACCREPLOG_DBT";

  var cmd = DL_RSDCommand(query);

  cmd.ExecuteCMD();
END;

PRIVATE MACRO ExecRenameAccounts()
  var query, cmd, DataSet;
  var cnt = 0, i = 0;
  var ClientName = "";
  var DogNum = "", DogDate = date(0,0,0), ЕКК = "", KindAcc = "";
  var NameAccount = "";
  var GUID = SP_GetGUID();

  ClearTable();

  query = "DECLARE \n"
       +  " v_err EXCEPTION; \n"
       +  " PRAGMA EXCEPTION_INIT( v_err, -955 );\n"
       + "BEGIN\n"             
       +  " EXECUTE IMMEDIATE 'CREATE INDEX DDPACCREPLOG_DBT_IDX_USR1 ON DDPACCREPLOG_DBT"
       + " (T_GUID, T_DEPOACNTID)';\n"
       + "EXCEPTION WHEN v_err THEN NULL; \n"
       + "END; \n";
  DL_RSDCommand(query).ExecuteCMD();

  if(GetTRUE(FALSE, "Выполнить с фиксацией действий в БД?\n"
                   +"(НЕТ - процедура выполнится в тестовом режиме без сохранения изменений в базе; ДА - все успешные изменения будут сохранены в базе)\n"
                   +"Первый запуск рекомендуется выполнить в тестовом режиме и проверить, что процедура выполнится без ошибок."))
    TestMode = false;
  end;

  query =   " INSERT INTO DDPACCREPLOG_DBT (T_GUID, T_DEPOACNTID, T_MESSAGE) "
          + " select /*+ INDEX(acc daccount_dbt_idx0)*/ ?, acc.t_AccountID, CHR(1) "
          + "   from (select DISTINCT accdoc.t_Account, accdoc.t_Chapter, accdoc.t_Currency "
          + "           from (select t_ID from dmccateg_dbt where t_LevelType = 1 and t_Code = '+РасчетыКомисс1') cat, dmcaccdoc_dbt accdoc "
          + "          where accdoc.t_CatID = cat.t_ID "  
          + "             and accdoc.t_IsCommon = 'X'"  
          + "             and accdoc.t_Owner <> 1 " 
          + "             and accdoc.t_DisablingDate = " + GetSQLDate(date(0,0,0))
          + "             and Exists(select 1 from dsfcontr_dbt sf where sf.t_ID = accdoc.t_ClientContrID and sf.t_DateClose = "+GetSQLDate(date(0,0,0))+") "
          + "        ) q, daccount_dbt acc"
          + "  where acc.t_Account = q.t_Account "
          + "    and acc.t_Chapter = q.t_Chapter "
          + "    and acc.t_Code_Currency =q.t_Currency  "
          + "    and acc.t_Close_Date = " + GetSQLDate(date(0,0,0)) 
          + "    and (acc.t_NameAccount LIKE 'Сре%' or acc.t_NameAccount LIKE 'Инд%') ";

   cmd = DL_RSDCommand(query);

  cmd.AddParam(GUID);

  InitProgress(-1, "Отбор счетов", "Отбор счетов");
  cnt = cmd.ExecuteCMD();
  RemProgress();

  cmd = DL_RSDCommand("select 1 from ddpaccreplog_dbt");

  cnt = cmd.GetCount();

  if(cnt > 0)

    InitProgress(cnt, "Обработка отобранных счетов", "Обработка отобранных счетов");
    
    query =   " select acc.t_AccountID, "
            + "        NVL((SELECT 1 " 
            + "               FROM dobjatcor_dbt a " 
            + "              WHERE a.t_objecttype = 659 " 
            + "                AND a.t_groupid = 102 " 
            + "                AND a.t_attrid = 1 " 
            + "                AND a.t_object = LPAD (sub_sf.t_id, 10, '0') "
            + "            ), 0) as IsEDP, "
            + "        acc.t_NameAccount, "
            + "        pt.t_LegalForm, pt.t_ShortName, "
            + "        main_sf.t_Number, main_sf.t_DateBegin, mp.t_MarketID, "
            + "        sub_sf.t_ServKind, sub_sf.t_ServKindSub, " 
            + "        RSB_SECUR.SC_GetDlObjCodeOnDate("+OBJTYPE_BROKERCONTR_DL+", "+DLCCK_USC+", dlcontr.t_DlContrID, "+GetSQLDate(date(0,0,0))+") as EKK "
            + "   from ddpaccreplog_dbt dp, daccount_dbt acc, ddlcontrmp_dbt mp, dsfcontr_dbt sub_sf, ddlcontr_dbt dlcontr, dsfcontr_dbt main_sf, dparty_dbt pt "
            + "  where dp.t_GUID = ? "
            + "    and acc.t_AccountID = dp.t_DepoAcntID "
            + "    and mp.t_SfContrID = (select accdoc.t_ClientContrID "
            + "                            from dmcaccdoc_dbt accdoc, dsfcontr_dbt sf "
            + "                           where accdoc.t_Account = acc.t_Account "
            + "                             and accdoc.t_IsCommon = 'X' "
            + "                             and sf.t_ID = accdoc.t_ClientContrID "
            + "                             and sf.t_DateClose = " + GetSQLDate(date(0,0,0))
            + "                             and ROWNUM = 1) "
            + "    and sub_sf.t_ID = mp.t_SfContrID "
            + "    and dlcontr.t_DlContrID = mp.t_DlContrID "
            + "    and main_sf.t_ID = dlcontr.t_SfContrID "
            + "    and pt.t_PartyID = main_sf.t_PartyID ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(GUID);

    DataSet = cmd.Execute();

    while(DataSet.moveNext())
      
      ClientName = DataSet.ShortName;

      DogNum = DataSet.Number;

      DogDate = date(DataSet.DateBegin);

      ЕКК = DataSet.EKK;
       
      if(DataSet.LegalForm == 2)
        if(DataSet.IsEDP > 0)
          KindAcc = "ЕДП";
        elif(DataSet.ServKindSub == 9)
          KindAcc = "внебиржевой рынок";
        end;
      else
        if(DataSet.ServKindSub == 9)
          KindAcc = "внебиржевой рынок";
        else
          if(DataSet.ServKind == 1)
            if(DataSet.MarketID == 2 /*MMVB_ID()*/)
              KindAcc = "ФР ММВБ";
            elif(DataSet.MarketID == 151337 /*SPB_ID()*/)
              KindAcc = "ФР СПБ";
            end;
          elif(DataSet.ServKind == 21)
            KindAcc = "ВР ММВБ";
          elif(DataSet.ServKind == 15)
            KindAcc = "СР ММВБ";
          end;
        end;
      end;

      NameAccount = "Треб. по брокерским операциям. "+ClientName+" Дог. № "+DogNum+" от "+string(DogDate:f)+". ЕКК - "+ЕКК+" ("+KindAcc+")";

      UpdateAccRec(GUID, DataSet.AccountID, NameAccount);

      UseProgress(i = i + 1);
    end;

    RemProgress();

    if(TestMode)
      msgbox("Успешно переименовано "+i+" счетов. Сохранение в БД не выполнялось.");
    else
      query =   " UPDATE daccount_dbt acc "
              + "    SET acc.t_NameAccount = (SELECT dp.t_Message FROM ddpaccreplog_dbt dp WHERE dp.t_GUID = ? and dp.t_DepoacntID = acc.t_AccountID) "
              + "  WHERE Exists(SELECT dp.t_Message FROM ddpaccreplog_dbt dp WHERE dp.t_GUID = ? and dp.t_DepoacntID = acc.t_AccountID and dp.t_Message <> CHR(1)) ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(GUID);
      cmd.AddParam(GUID);

      InitProgress(-1, "Сохранение изменений", "Сохранение изменений");
      cmd.executeCMD();
      RemProgress();

      msgbox("Успешно переименовано "+i+" счетов.");
    end; 

    ClearTable();

    query = "DECLARE \n"
         +  " v_err EXCEPTION; \n"
         +  " PRAGMA EXCEPTION_INIT( v_err, -955 );\n"
         + "BEGIN\n"             
         +  " EXECUTE IMMEDIATE 'DROP INDEX DDPACCREPLOG_DBT_IDX_USR1';\n"
         + "EXCEPTION WHEN v_err THEN NULL; \n"
         + "END; \n";
    DL_RSDCommand(query).ExecuteCMD();

  else
    msgbox("Нет подходящих счетов");
  end;

END;

ExecRenameAccounts();
