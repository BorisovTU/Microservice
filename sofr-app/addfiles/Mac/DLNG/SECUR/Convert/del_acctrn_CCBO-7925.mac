/*
$Name:        del_acctrn_CCBO-7925.mac
$Module:      Ценные бумаги
$Description: Удаление проводок из Excel-файла по CCBO-7925
*/

import rsexts;
import "global_classes.mac";           /* класс ошибок */
import "ws_msg_transform_sec.mac";     /* для обработки ошибок */
import dlmisc;                         /* SplitString*/
import "func_lib";
import "dlquery.mac";

import DealsInter, spserv, sp_class, sp_car, spreserv;

private var TestMode   = true;      //Тестовый режим (без фиксации действий в БД)


private Macro DelAccTrn(namefile)
  var objExcel = CDAOMSExcel();
  var i = 1, cnt = 0;
  var err = 0;
  var FD = NULL;

  if(GetTRUE(FALSE, "Выполнить с фиксацией действий в БД?\n"
                   +"(НЕТ - процедура выполнится в тестовом режиме без сохранения изменений в базе; ДА - все успешные изменения будут сохранены в базе)\n"
                   +"Первый запуск рекомендуется выполнить в тестовом режиме и проверить, что процедура выполняется без ошибок."))
    TestMode = false;
  end;
 
  if(objExcel.CreateApplication())
    if(objExcel.open(namefile))
      objExcel.Book.Sheets(1).Activate();
      
      var curr_row = -1;
      var activeSheet = objExcel.Book.ActiveSheet;
      var objTarget = activeSheet.UsedRange.Find("T_ACCTRNID");

      if(objTarget)
        curr_row = objTarget.row + 1;
      else
        msgbox("Данные в файле не найдены");
        return;
      end;
      
      if(curr_row > -1)
        InitProgress(activeSheet.Rows.CurrentRegion.Rows.Count - curr_row + 1, "Ждите, выполняется удаление проводок...", "Выполняется удаление проводок");

        while(curr_row <= activeSheet.Rows.CurrentRegion.Rows.Count)
          var AccTrnID = trim(string(activeSheet.Cells(int(curr_row), 1).Value));

          if((AccTrnID == "") or (AccTrnID == NULL) or (AccTrnID == "Undefined"))
            break;
          end;

          AccTrnID = int(StrSubSt(AccTrnID," ",""));

          var accTrn = RsbAccTransaction();

          err = accTrn.Find(AccTrnID);
          if(accTrn.Find(AccTrnID) != 0)
            println("Не найдена проводка с идентификатором " + AccTrnID);
          else
            RslDefCon.BeginTrans();
            if(accTrn.Delete() == false)
              println("Ошибка при удалении проводки с идентификатором " + AccTrnID + ". "+GetErrMsg());
              RslDefCon.RollbackTrans();
            else
              println("Удалена проводка с идентификатором " + AccTrnID);

              if(TestMode)
                RslDefCon.RollbackTrans();
              else
                RslDefCon.CommitTrans();
              end;

              cnt = cnt + 1;
            end;
          end;

          UseProgress(curr_row = curr_row + 1);
        end;

        RemProgress();

        msgbox("Удалено "+ trim(string(cnt)) + " проводок.");
      end;
    end;
    objExcel.Book.Close();
  end;
  objExcel = null;
OnError(er)
  Msgbox(er.Message);
  objExcel.Book.Close();
  objExcel = null;
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  msgbox("Ошибка: " + er.Message);
end;

var file_name = "";
if(SelectFile(file_name, MergeFile ("..\\Import", "*.xls*"), "Выбор xls-файла с проводками для удаления")) 
  DelAccTrn(file_name);
end;
