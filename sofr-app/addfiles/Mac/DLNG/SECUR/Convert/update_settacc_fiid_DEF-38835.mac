/*
$Name:        update_settacc_fiid_DEF-38835.mac
$Module:      Ценные бумаги
$Description: Обновление валюты в СПИ с некорректной валютой
*/

import DealsInter, spserv, sp_class, sp_car, spreserv, dlcontrfunc;

private var TestMode   = true;      //Тестовый режим (без фиксации действий в БД)

PRIVATE CLASS TProtocolData(_PartyID:integer, _SettAccID:integer, _Account:string, _FI_OLD:integer, _FI_NEW:integer, _Msg:string)
  var PartyID     = _PartyID;  
  var SettAccID   = _SettAccID;
  var Account     = _Account;  
  var FI_OLD      = _FI_OLD; 
  var FI_NEW      = _FI_NEW;
  var Msg         = _Msg; 
END;

PRIVATE MACRO AddSAToArhTable(Action:integer, SettAccID:integer, ErrStr:@string)
  var query, cmd;

  ErrStr = "";

  query = "INSERT INTO DEF38835_SETTACC_DBT (   T_ACTION "         
                                        + "   , T_SETTACCID "      
                                        + "   , T_PARTYID "        
                                        + "   , T_BANKID "         
                                        + "   , T_FIID "           
                                        + "   , T_CHAPTER "        
                                        + "   , T_ACCOUNT "        
                                        + "   , T_INN "            
                                        + "   , T_RECNAME "        
                                        + "   , T_BANKCODEKIND "   
                                        + "   , T_BANKCODE "       
                                        + "   , T_BANKNAME "       
                                        + "   , T_BANKCORRID "     
                                        + "   , T_BANKCORRCODEKIND "
                                        + "   , T_BANKCORRCODE "   
                                        + "   , T_BANKCORRNAME "   
                                        + "   , T_CORRACC "         
                                        + "   , T_FIKIND "         
                                        + "   , T_BENEFICIARYID "  
                                        + "   , T_CODEKIND "       
                                        + "   , T_CODE "           
                                        + "   , T_DESCRIPTION "    
                                        + "   , T_ORDER "          
                                        + "   , T_SHORTNAME "      
                                        + "   , T_NOACCEPT "       
                                        + "   , T_KZPARTYCODE "    
                                        + "   , T_SPI_IDENT "
                                        + "    ) "
           + " SELECT   ? "
           + "        , T_SETTACCID "      
           + "        , T_PARTYID "
           + "        , T_BANKID "
           + "        , T_FIID "
           + "        , T_CHAPTER "
           + "        , T_ACCOUNT "
           + "        , T_INN "
           + "        , T_RECNAME "
           + "        , T_BANKCODEKIND "
           + "        , T_BANKCODE "
           + "        , T_BANKNAME "
           + "        , T_BANKCORRID "
           + "        , T_BANKCORRCODEKIND "
           + "        , T_BANKCORRCODE "
           + "        , T_BANKCORRNAME "
           + "        , T_CORRACC "
           + "        , T_FIKIND "
           + "        , T_BENEFICIARYID "
           + "        , T_CODEKIND "
           + "        , T_CODE "
           + "        , T_DESCRIPTION "
           + "        , T_ORDER "
           + "        , T_SHORTNAME "
           + "        , T_NOACCEPT "
           + "        , T_KZPARTYCODE "
           + "        , T_SPI_IDENT "
           + "  FROM dsettacc_dbt WHERE t_SettAccID = ? ";


  cmd = DL_RSDCOmmand(query);

  cmd.AddParam(Action   );
  cmd.AddParam(SettAccID);

  cmd.ExecuteCMD();

  return 0;
OnError(er)
  ErrStr = er.Message;
  return 1;
END;

PRIVATE MACRO SetNewFiToSettAcc(SettAccID:integer, FIID_New:integer, ErrStr:@string)
  var query, cmd;

  ErrStr = "";

  query =   " UPDATE DSETTACC_DBT "
          + "    SET T_FIID = ? "
          + "  WHERE t_SettAccID = ? ";


  cmd = DL_RSDCOmmand(query);

  cmd.AddParam(FIID_New );
  cmd.AddParam(SettAccID);

  cmd.ExecuteCMD();

  return 0;
OnError(er)
  ErrStr = er.Message;
  return 1;
END;

PRIVATE MACRO SetNewFiToAccount(Chapter:integer, Account:string, FIID_New:integer, ErrStr:@string)
  var query, cmd;

  ErrStr = "";

  query =   " UPDATE DACCOUNT_DBT "
          + "    SET T_CODE_CURRENCY = ? "
          + "  WHERE t_Chapter = ? "
          + "    AND t_Account = ? ";


  cmd = DL_RSDCOmmand(query);

  cmd.AddParam(FIID_New);
  cmd.AddParam(Chapter);
  cmd.AddParam(Account);

  cmd.ExecuteCMD();

  return 0;
OnError(er)
  ErrStr = er.Message;
  return 1;
END;

private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

PRIVATE MACRO ExecUpdateSettAcc()
  var query, cmd, DataSet;
  var cnt = 0, i = 0;
  var LogArr = TArray(203000);
  var SystDate, SystTime;
  var err = 0, ErrStr = "";
  var err_cnt = 0;

  if(GetTRUE(FALSE, "Выполнить с фиксацией действий в БД?\n"
                   +"(НЕТ - процедура выполнится в тестовом режиме без сохранения изменений в базе; ДА - все успешные изменения будут сохранены в базе)\n"
                   +"Первый запуск рекомендуется выполнить в тестовом режиме и проверить, что процедура выполнится без ошибок."))
    TestMode = false;
  end;


  GetSystDateTime(@SystDate, @SystTime);

  query =   " SELECT * "
          + "   FROM (SELECT sa.t_settaccid, "
          + "                sa.t_partyid, "
          + "                sa.t_fiid, "
          + "                fi.t_fi_code, "
          + "                sa.t_account, sa.t_Chapter, "
          + "                (SELECT fi_s.t_fiid "
          + "                   FROM dfininstr_dbt fi_s "
          + "                  WHERE fi_s.t_fi_kind = 1 "
          + "                    AND fi_s.t_avoirkind = 0 "
          + "                       AND fi_s.t_fi_code = substr(sa.t_account, 6, 3)) as t_fiid_new, "
          + "                  substr(sa.t_account, 6, 3) as t_acc_val_code, "
          + "                NVL((SELECT 1 "
          + "                       FROM dacctrn_dbt acctrn "
          + "                      WHERE acctrn.t_State = 1 "
          + "                        AND (acctrn.t_Account_Payer = sa.t_Account or acctrn.t_Account_Receiver = sa.t_Account) "
          + "                        AND ROWNUM = 1 "  
          + "                    ), 0) as ExistAccTrn, "
          + "                (SELECT /*+ INDEX(acc daccount_dbt_idx0)*/ Count(1) "
          + "                   FROM daccount_dbt acc "
          + "                  WHERE acc.t_Chapter = sa.t_Chapter "
          + "                    AND acc.t_Account = sa.t_Account "
          + "                ) as CntAcc, "
          + "                (SELECT /*+ INDEX(acc daccount_dbt_idx0)*/ acc.t_Code_Currency "
          + "                   FROM daccount_dbt acc "
          + "                  WHERE acc.t_Chapter = sa.t_Chapter "
          + "                    AND acc.t_Account = sa.t_Account "
          + "                    AND ROWNUM = 1"
          + "                ) as AccCode_Currency "
          + "           FROM dsettacc_dbt sa, dfininstr_dbt fi, dparty_dbt p "
          + "          WHERE p.t_partyid = sa.t_partyid "
          + "            AND p.t_legalform = 2 "
          + "            AND fi.t_fiid = sa.t_fiid "
          + "            AND (sa.t_account like '40817%' or sa.t_account like '40820%')"
          + "        ) t "
          + "  WHERE t.t_fi_code != t.t_acc_val_code ";
          

  cmd = DL_RSDCommand(query);

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "Обработка СПИ", "Обработка СПИ");
    
    query = query + "  ORDER BY t.t_Account ASC ";

    DataSet = cmd.Execute(query);
    while(DataSet.moveNext())

      err = 0;
      ErrStr = "";

      if((not err) and (DataSet.CntAcc > 1))
        err = 1;
        ErrStr = "Найдено "+int(DataSet.CntAcc)+" счетов с одинаковым номером. Обновление невозможно";
      end;

      if((not err) and (strlen(DataSet.Account) != 20))
        err = 1;
        ErrStr = "Некорректный счет! Обновление невозможно";
      end;
      
      if((not err) and (DataSet.ExistAccTrn > 0) and (DataSet.AccCode_Currency != DataSet.FIID_New))
        err = 1;
        ErrStr = "По счету были проводки. Обновление невозможно";
      end;

      if(not err)
        RslDefCon.BeginTrans();

        err = AddSaToArhTable(1, DataSet.SettAccID, @ErrStr);
        if(not err)
          err = SetNewFiToSettAcc(DataSet.SettAccID, DataSet.FIID_New, @ErrStr);
          if((not err) and (DataSet.AccCode_Currency != DataSet.FIID_New) and (DataSet.CntAcc == 1))
            err = SetNewFiToAccount(DataSet.Chapter, DataSet.Account, DataSet.FIID_New, @ErrStr);
          end;
        end;
        
        if((TestMode) or (err))
          RslDefCon.RollbackTrans();
        else
          RslDefCon.CommitTrans();
        end;
      end;

      LogArr[LogArr.size()] = TProtocolData(DataSet.PartyID, DataSet.SettAccID, DataSet.Account, DataSet.FIID, DataSet.FIID_New, ErrStr);
      if(err)
        err_cnt = err_cnt + 1;
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();

    InitProgress(LogArr.size(), "Печать протокола", "Печать протокола");

             [                                  Обновление валюты в СПИ
             Дата выполнения:  #############
             Время выполнения: #############
             Обработано СПИ:   #############  Из них не обновлены: #########
             ]
             (SystDate:f, SystTime, cnt, err_cnt);

    println("");
    println("┌─────────────────────┬──────────────────────┬──────────────────────┬────────────────┬────────────────┐");
    println("│       PartyID       │      SettAccID       │        Account       │    FIID_OLD    │    FIID_NEW    │");
    println("│                     │                      │                      │                │                │");
    println("├─────────────────────┼──────────────────────┼──────────────────────┼────────────────┼────────────────┤");

    i = 0;
    while(i < LogArr.size())

            [│#####################│######################│######################│################│################│############################################################################################]
            (string(LogArr[i].PartyID):c,
             string(LogArr[i].SettAccID):c,
             string(LogArr[i].Account):c,
             string(LogArr[i].FI_OLD):c,
             string(LogArr[i].FI_NEW):c,
             string(LogArr[i].Msg):l:w        
            );

      if(i < LogArr.size() - 1)
        println("├─────────────────────┼──────────────────────┼──────────────────────┼────────────────┼────────────────┤");
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();

        println("└─────────────────────┴──────────────────────┴──────────────────────┴────────────────┴────────────────┘");

  else
    msgbox("Нет подходящих СПИ");
  end;

OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  msgbox("Ошибка: " + er.Message);
END;

ExecUpdateSettAcc();
