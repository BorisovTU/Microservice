/*
$Name:        transfsubownacc_11500.mac
$Module:      Ценные бумаги
$Description: Перенос остатков со счетов размещения, премии и дисконта субординированных ОЭБ на новые счета с 01.01.2023
*/

import DealsInter, spserv, sp_class, sp_car, spreserv;

private var TransfDate = {curdate}; //Дата переноса (проводок)
private var TestMode   = true;      //Тестовый режим (без фиксации действий в БД)

macro cnv_msgbox(StrErr)
  println(StrErr);
end;

var mestab_str = "";
macro msgbox_tab(StrErr)
  mestab_str = mestab_str + StrErr + "\n";
end;

PRIVATE MACRO GetCCY(FIID)
  var cmd = DL_RSDCommand("select t_CCY from dfininstr_dbt where t_FIID = ?");
  var ccy = "";

  cmd.AddParam(FIID);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ccy = DataSet.CCY;
  end;

  return ccy;
END;

// ────────────────────────────────
// Установить дату деактивации 
// ────────────────────────────────
private macro setAccDisablingDate(McAccDocID, DisDate)
  var exec = RSDCommand(" UPDATE dmcaccdoc_dbt "+
                        "    SET t_DisablingDate = ?,"+
                        "        t_IsUsable = chr(0) "+
                        "  WHERE t_ID = ?"      );
  
  exec.addParam( "", RSDBP_IN, DisDate );
  exec.addParam( "", RSDBP_IN, McAccDocID );

  exec.execute();

  return 0;
OnError(er)
  return 1;
end;

private macro StartTransf()
  var query, cmd, DataSet;
  var OldAcc = TRecHandler("account.dbt");
  var NewAcc = TRecHandler("account.dbt");
  var CatCode = "", Currency;
  var FD = NULL;
  var err = 0, msg="";
  var i = 0;
  var q, sel, ds;

  if(GetTRUE(FALSE, "Выполнить с фиксацией действий в БД?\n"
                   +"(НЕТ - процедура выполнится в тестовом режиме без сохранения изменений в базе; ДА - все успешные изменения будут сохранены в базе)\n"
                   +"Первый запуск рекомендуется выполнить в тестовом режиме и проверить протокол на наличие ошибок."))
    TestMode = false;
  end;

  if(TestMode == true)
    println("ТЕСТОВЫЙ РЕЖИМ! (без фиксации действий в БД)");
    println("");
  end;

  query =   " with f as (select fin.t_FIID, fin.t_FI_Code "
          + "              from dfininstr_dbt fin, davoiriss_dbt avr "
          + "             where fin.t_FI_Kind = " + FIKIND_AVOIRISS
          + "               and RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_FI_Kind, fin.t_AvoirKind ) = " + AVOIRISSKIND_BOND
          + "               and avr.t_FIID = fin.t_FIID "
          + "               and avr.t_Subordinated = 'X'), "
          + "      cat as (select t_ID, t_Code "
          + "                from dmccateg_dbt "
          + "               where t_LevelType = 1 "
          + "                 and t_Code in ('Размещ. ОЭБ', 'Премия, ОЭБ', 'Дисконт, ОЭБ')"
          + "             ) "
          + " select q.CatCode, q.t_Account, q.t_Currency, q.t_Chapter, q.t_FIID, q.t_FI_Code, "
          + "        abs(rsb_account.restac(q.t_Account, q.t_Currency, ?, q.t_Chapter, null)) as RestAcc "
          + "   from (select DISTINCT mcacc.t_Account, mcacc.t_Chapter, mcacc.t_Currency, cat.t_Code as CatCode, f.t_FIID, f.t_FI_Code "
          + "           from dmcaccdoc_dbt mcacc, cat, f, daccount_dbt acc "
          + "          where mcacc.t_CatID = cat.t_ID "
          + "            and mcacc.t_IsCommon = 'X' "
          + "            and mcacc.t_FIID = f.t_FIID "
          + "            and mcacc.t_DisablingDate = " + GetSQLDate(date(0,0,0))
          + "            and acc.t_Account = mcacc.t_Account "
          + "            and acc.t_Chapter = mcacc.t_Chapter "
          + "            and acc.t_Code_Currency = mcacc.t_Currency "
          + "            and acc.t_Open_Close = CHR(0) "
          + "        ) q "
          + " order by q.t_FIID, q.t_Account";


  cmd = DL_RSDCommand(query);

  cmd.AddParam(TransfDate);

  var cnt = cmd.GetCount();
  if(cnt > 0)
    println("Перенос остатков по счетам субординированных ОЭБ");
    println("");
    println("┌─────┬─────────────────────┬──────────────────────┬──────────────────────┬────────────────┬─────────────────────────────────────────────────────────────────┐");
    println("│     │       Код ц/б       │        Старый        │        Новый         │ Сумма проводки │                            Сообщения                            │");
    println("│     │                     │     лицевой счет     │     лицевой счет     │                │                                                                 │");
    println("├─────┼─────────────────────┼──────────────────────┼──────────────────────┼────────────────┼─────────────────────────────────────────────────────────────────┤");
    println("│  1  │          2          │           3          │           4          │        5       │                                 6                               │");
    println("├─────┼─────────────────────┼──────────────────────┼──────────────────────┼────────────────┼─────────────────────────────────────────────────────────────────┤");
            


    InitProgress(cnt, "Обработка счетов", "Обработка счетов");

    DataSet = cmd.Execute(query);
    while(DataSet.moveNext())

      FD = SPFirstDocFI(DLDOC_ISSUE, int(DataSet.FIID), int(DataSet.FIID), -1, {OurBank}, true, true, null );
      
      err = 0;
      mestab_str = "";

      RslDefCon.BeginTrans();

      if(GetAccount(DataSet.Chapter, DataSet.Currency, DataSet.Account, OldAcc, false ) == 0)
        err = 1;
        msgbox_tab("ОШИБКА!!! Не найден старый счет "+DataSet.rec.Account);
      end;

      if(err == 0)
        var NewCatCode = "";

        if(DataSet.CatCode == "Размещ. ОЭБ")
          NewCatCode = "Размещ., суборд.ОЭБ";
        elif(DataSet.CatCode == "Премия, ОЭБ")
          NewCatCode = "Премия, суборд.ОЭБ";
        elif(DataSet.CatCode == "Дисконт, ОЭБ")
          NewCatCode = "Дисконт, суборд.ОЭБ";
        end;

        if( not FD.OpenAccount(NewCatCode, null, null, FIROLE_AJUSTVALOEB, NewAcc, TransfDate, Dataset.Currency) )
          msg = GetErrMsg();
          msgbox_tab("ОШИБКА!!! Ошибка при открытии счета по КУ \"" + NewCatCode+"\": "+msg+".");
          err = 1;
        else
          msgbox_tab("Открыт новый счет "+NewAcc.rec.Account+" по КУ \"" + NewCatCode+"\"");
        end;

        if(not err)
          if(DataSet.RestAcc > 0)
            if(not ПроводкаПоКатегориямУчета(FD, 
                   OldAcc, NewAcc,              /* КатегДеб , КатегКредит*/
                   TransfDate,                  /* Дата */
                   1,                           /* Глава */
                   OldAcc.rec.Code_Currency,    /* Валюта */
                   money(DataSet.RestAcc),      /* Сумма  */
                   NULL,                        /* Документ */
                   INPCARRY,                    /* Result_Carry */
                   " 1",                        /* Kind_Oper */
                   "",                          /* Numb_Document */
                   "Перенос остатков по собств.суборд.облигациям банка, облиг. "+FD.fininstr.rec.Name+", гос.рег.№ "+FD.avoiriss.rec.LSIN
                  ) 
              )
                msg = GetErrMsg();
                if(msg != "")
                  msgbox_tab("ОШИБКА!!! Ошибка при выполнении проводки. "+msg);
                end;

                err = 1;
            else
              msgbox_tab("Выполнена проводка "+NewAcc.rec.Account+"<->"+OldAcc.rec.Account+" на сумму " + money(DataSet.RestAcc) + " " + GetCCY(OldAcc.rec.Code_Currency));
            end;
          else
            msgbox_tab("На старом счете нулевой остаток. Проводка не требуется.");
          end;
        end;
      end;

      if(err == 0)
        if( CB_CloseAccount(OldAcc.rec.Chapter, OldAcc.rec.Code_Currency, OldAcc.rec.Account, TransfDate, null) != 0 )
           msg = GetErrMsg();
           msgbox_tab("Ошибка закрытия счета: "+msg+". Счет "+OldAcc.rec.Account+" не закрыт.");
           err = 1;
        else
           //Найти и закрыть привязку счета в КУ
           q =   " select accdoc.t_ID "
               + "   from dmcaccdoc_dbt accdoc "
               + "  where accdoc.t_Account = ? "
               + "    and accdoc.t_Chapter = ? "
               + "    and accdoc.t_Currency = ? "
               + "    and accdoc.t_DisablingDate = " + GetSQLDate(date(0,0,0));

           sel = DL_RSDCommand(q);

           sel.AddParam(OldAcc.rec.Account);
           sel.AddParam(OldAcc.rec.Chapter);
           sel.AddParam(OldAcc.rec.Code_Currency);

           ds = sel.Execute();
           while(ds.moveNext())
             if(setAccDisablingDate(ds.ID, TransfDate) != 0)
               msg = GetErrMsg();
               msgbox_tab("ОШИБКА!!!Ошибка закрытия привязки счета: "+msg+". Привязка счета "+OldAcc.rec.Account+" не закрыта.");
             end;
           end;

           msgbox_tab("Счет "+OldAcc.rec.Account+" успешно закрыт!");
        end;
      end;

      if(TestMode)
        RslDefCon.RollbackTrans();
      else
        RslDefCon.CommitTrans();
      end;

      [│#####│#####################│######################│######################│################│#################################################################│]
      (string(i+1):c,
       string(DataSet.FI_Code):c,
       string(DataSet.Account):c,                                                                                 
       string(NewAcc.rec.Account):c,      
       string(money(DataSet.RestAcc)):r,         
       string(mestab_str):l:w        
      );

      UseProgress(i = i + 1);

      if(i < cnt)
        println("├─────┼─────────────────────┼──────────────────────┼──────────────────────┼────────────────┼─────────────────────────────────────────────────────────────────┤");
      end;
    end;

    RemProgress();

    println("└─────┴─────────────────────┴──────────────────────┴──────────────────────┴────────────────┴─────────────────────────────────────────────────────────────────┘");

  else
    println("Нет подходящих счетов");
  end;


OnError( err );
  var j;
  var err_text = "", delim = "";
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  

  if (IsEqClass ("TrslError", err))
    err_text=err.Message;
    if(IsEqClass ("TDbError", err.err))
      err_text=err_text+":|"+err.err.Stat+"-"+err.err.Oper+"-"+err.err.Table+"-"+err.err.Message;
    elif (isEqClass("TRsdError", err.err))

      j = 0;
      while(j < err.err.environment.ErrorCount)
         err_text = err_text + delim  + err.err.environment.error(j).descr;
         delim = "\n";
         j = j + 1;
      end;
    end;
  end;

  if(err_text == "")
    err_text=err.module+ " "+err.line+" "+err.message;
  end;

  println(err_text);

end;

StartTransf();
