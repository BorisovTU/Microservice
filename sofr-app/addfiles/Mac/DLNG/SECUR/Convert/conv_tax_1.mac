/* Процедура конвертации данных НУ. Выгрузка валидных лотов и связей в Excel
   Chernov Anton 05-12-2007
*/

import "DLReport_h.mac", "dlreport.mac", "conv_tax_c.mac";

PRIVATE CLASS (DL_CReportTemplate) CPrintReport()
  PRIVATE VAR m_RS, m_RS_lnk, m_NumRec = 0;

  PRIVATE MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
  END;

  PRIVATE MACRO EndReport()
  END;

  PRIVATE MACRO ИнициализироватьТаблицуЛотов()
    this.SetActiveSheet( "лоты НУ" );

    this.CreateTable( "ScrolName", "H_", "N_", "V_",
                      "Список налоговых лотов",
                      "Код сделки",                        V_STRING,
                      "Внешний код сделки",                V_STRING,
                      "Вид сделки",                        V_STRING,
                      "Дата заключения сделки",            V_DATE,
                      "Время заключения сделки",           V_TIME,
                      "Код выпуска",                       V_STRING,
                      "Наименование выпуска",              V_STRING,
                      "Количество ц/б",                    V_NUMERIC,
                      "Дата исполнения сделки",            V_DATE,
                      "Время исполнения сделки",           V_TIME,
                      "Дата исполнения 2-й части",         V_DATE,
                      "Время исполнения 2-й части",        V_TIME,
                      "Вид лота НУ",                       V_STRING,
                      "ИД лота (внутренний идентификатор)", V_INTEGER
                    );
  END;

  PRIVATE MACRO ИнициализироватьТаблицуСвязей()
    this.SetActiveSheet( "связи НУ" );

    this.CreateTable( "ScrolName1", "HL_", "NL_", "VL_",
                      "Список связей налоговых лотов",
                      "Код покупки",                                    V_STRING,
                      "Внешний код покупки",                            V_STRING,
                      "Дата покупки",                                   V_DATE,
                      "ИД покупки (внутренний идентификатор)",          V_INTEGER,
                      "Код продажи",                                    V_STRING,
                      "Внешний код продажи",                            V_STRING,
                      "Дата продажи",                                   V_DATE,
                      "ИД продажи (внутренний идентификатор)",          V_INTEGER,
                      "Код источника",                                  V_STRING,
                      "Внешний код источника",                          V_STRING,
                      "Дата источника",                                 V_DATE,
                      "ИД источника (внутренний идентификатор)",        V_INTEGER,
                      "Код окончат. продажи",                           V_STRING,
                      "Внешний код окончат. продажи",                   V_STRING,
                      "Дата окончат. продажи",                          V_DATE,
                      "ИД окончат. продажи (внутренний идентификатор)", V_INTEGER,
                      "Код выпуска",                                    V_STRING,
                      "Наименование выпуска",                           V_STRING,
                      "Тип связи",                                      V_STRING,
                      "Количество",                                     V_NUMERIC,
                      "ИД типа связи (внутренний идентификатор)",       V_INTEGER
                    );
  END;

  PRIVATE MACRO ПолучитьПараметрыФИ( FIID : integer, FI_Code:@variant, FI_Name:@variant )
    var rsd, sql, stat = false;

    sql = " SELECT fin.t_FI_Code, fin.t_Name "
        +   " FROM dfininstr_dbt fin "
        +  " WHERE fin.t_FIID = " + FIID;

    rsd = TRsbDataSet(sql);
    if( rsd.MoveNext() )
      FI_Code = string(rsd.FI_Code);
      FI_Name = string(rsd.Name);

      stat = true;
    end;

    return stat;
  END;

  PRIVATE MACRO ПолучитьВидЛота( LotType : integer )
    if(   LotType == TAXLOTS_UNDEF )
      return "Не определено";
    elif( LotType == TAXLOTS_BUY )
      return "Покупка";
    elif( LotType == TAXLOTS_SALE )
      return "Продажа";
    elif( LotType == TAXLOTS_REPO )
      return "РЕПО прямое";
    elif( LotType == TAXLOTS_BACKREPO )
      return "РЕПО обратное";
    end;

    return "";
  END;

  PRIVATE MACRO ПолучитьВидСвязи( LinkType : integer )
    if(   LinkType == TAXLNK_UNDEF )
      return "Не определено";
    elif( LinkType == TAXLNK_BS )
      return "Купля-продажа";
    elif( LinkType == TAXLNK_REPO )
      return "РЕПО прямое";
    elif( LinkType == TAXLNK_OPSPOS )
      return "Открытие короткой позиции";
    elif( LinkType == TAXLNK_CLPOS )
      return "Закрытие короткой позиции";
    elif( LinkType == TAXLNK_BREPO )
      return "Возврат из прямого РЕПО";
    elif( LinkType == TAXLNK_DELIVER )
      return "Поставка";
    elif( LinkType == TAXLNK_DELREPO )
      return "Поставка - прямое РЕПО";
    elif( LinkType == TAXLNK_DELBREP )
      return "Поставка - возврат из прямого РЕПО";
    elif( LinkType == TAXLNK_RETURN2 )
      return "Возврат 2";
    elif( LinkType == TAXLNK_DELRET2 )
      return "Поставка - возврат 2";
    elif( LinkType == TAXLNK_RETSPOS )
      return "Возврат в короткую позицию";
    end;

    return "";
  END;


  PRIVATE MACRO ПечататьСтрокуЛотов( )
     var rsd, sql;
     var FI_Code, FI_Name,
         DealCode, DealCodeTS, DealKind,
         DealDate, DealTime, ValueDate,
         ValueTime, ValueDate2, ValueTime2;

     if( not ПолучитьПараметрыФИ( int(m_RS.FIID), @FI_Code, @FI_Name ) )
       FI_Code = "";
       FI_Name = "";
     end;

     if( string(m_RS.IsVirtual) == "X" )
       DealCode   = string(m_RS.Number);
       DealCodeTS = string(m_RS.Number);
       DealKind   = "Вирт. " + ПолучитьВидЛота( int(m_RS.Type) );
       DealDate   = date(m_RS.Date);
       DealTime   = time(m_RS.Time);
       ValueDate  = date(m_RS.Date);
       ValueTime  = time(m_RS.Time);
       ValueDate2 = date(m_RS.Date2);
       ValueTime2 = time(m_RS.Time2);
     else
       sql = " SELECT tick.t_DealCode, tick.t_DealCodeTS, tick.t_DealDate, tick.t_DealTime, opr.t_Name "
           +   " FROM ddl_tick_dbt tick, "
           +        " doprkoper_dbt opr "
           +  " WHERE tick.t_DealID = " + int(m_RS.t_DealID)
           +    " AND opr.t_Kind_Operation = tick.t_DealType "
           +    " AND opr.t_DocKind = tick.t_BOfficeKind ";

       rsd = TRsbDataSet(sql);
       if( rsd.MoveNext() )
         DealCode   = string(rsd.DealCode);
         DealCodeTS = string(rsd.DealCodeTS);
         DealKind   = string(rsd.Name);
         DealDate   = date(rsd.DealDate);
         DealTime   = time(rsd.t_DealTime);
         ValueDate  = date(m_RS.Date);
         ValueTime  = time(m_RS.Time);
         ValueDate2 = date(m_RS.Date2);
         ValueTime2 = time(m_RS.Time2);
       else
         DealCode   = "";
         DealCodeTS = "";
         DealKind   = "";
         DealDate   = date(0,0,0);
         DealTime   = time(0,0,0,0);
         ValueDate  = date(0,0,0);
         ValueTime  = time(0,0,0,0);
         ValueDate2 = date(0,0,0);
         ValueTime2 = time(0,0,0,0);
       end;
     end;
   
     PrintTableLine( "V_",   DealCode,                            null,
                     "V_1",  DealCodeTS,                          null,
                     "V_2",  DealKind,                            null,
                     "V_3",  DealDate,                            null,
                     "V_4",  DealTime,                            null,
                     "V_5",  FI_Code,                             null,
                     "V_6",  FI_Name,                             null,
                     "V_7",  money(m_RS.newlotamount),            2,
                     "V_8",  ValueDate,                           null,
                     "V_9",  ValueTime,                           null,
                     "V_10", ValueDate2,                          null,
                     "V_11", ValueTime2,                          null,
                     "V_12", ПолучитьВидЛота( int(m_RS.Type) ),   null,
                     "V_13", int(m_RS.ID),                        0
                   );
  END;                                                

  PRIVATE MACRO ПечататьСтрокуСвязей( )
     var rsd, sql;
     var FI_Code, FI_Name,
         BuyCode, BuyCodeTS, BuyDate, SaleCode, SaleCodeTS, SaleDate,
         SourceCode, SourceCodeTS, SourceDate, DestCode, DestCodeTS, DestDate;

     if( not ПолучитьПараметрыФИ( int(m_RS_lnk.FIID), @FI_Code, @FI_Name ) )
       FI_Code = "";
       FI_Name = "";
     end;

     sql = " SELECT buy.t_Number BuyCode, "
         +        " NVL((SELECT tick.t_DealCodeTS "
         +               " FROM ddl_tick_dbt tick "
         +              " WHERE tick.t_DealID = buy.t_DealID), buy.t_Number) BuyCodeTS, "
         +        " buy.t_Date BuyDate, "
         +        " sale.t_Number SaleCode, "
         +        " NVL((SELECT tick.t_DealCodeTS "
         +               " FROM ddl_tick_dbt tick "
         +              " WHERE tick.t_DealID = sale.t_DealID), sale.t_Number) SaleCodeTS, "
         +        " sale.t_Date SaleDate ";

     if( int(m_RS_lnk.t_SourceID) > 0 )
       sql = sql + " , "
           +        " NVL(source.t_Number, CHR(1)) SourceCode, "
           +        " NVL((SELECT tick.t_DealCodeTS "
           +               " FROM ddl_tick_dbt tick "
           +              " WHERE tick.t_DealID = source.t_DealID), NVL(source.t_Number, CHR(1))) SourceCodeTS, "
           +        " NVL(source.t_Date, TO_DATE('01-01-0001','DD-MM-YYYY')) SourceDate ";
     end;

     if( int(m_RS_lnk.t_DestID) > 0 )
       sql = sql + " , "
           +        " NVL(dest.t_Number, CHR(1)) DestCode, "
           +        " NVL((SELECT tick.t_DealCodeTS "
           +               " FROM ddl_tick_dbt tick "
           +              " WHERE tick.t_DealID = dest.t_DealID), NVL(dest.t_Number, CHR(1))) DestCodeTS, "
           +        " NVL(dest.t_Date, TO_DATE('01-01-0001','DD-MM-YYYY')) DestDate "
     end;

     sql = sql
         +   " FROM dsctaxlot_dbt buy, "
         +        " dsctaxlot_dbt sale ";

     if( int(m_RS_lnk.t_SourceID) > 0 )
       sql = sql + " , "
           +        " dsctaxlot_dbt source "
     end;

     if( int(m_RS_lnk.t_DestID) > 0 )
       sql = sql + " , "
           +        " dsctaxlot_dbt dest "
     end;

     sql = sql
         +  " WHERE buy.t_ID = " + int(m_RS_lnk.t_BuyID)
         +    " AND sale.t_ID = " + int(m_RS_lnk.t_SaleID);

     if( int(m_RS_lnk.t_SourceID) > 0 )
       sql = sql
           +    " AND source.t_ID = " + int(m_RS_lnk.t_SourceID)
     end;

     if( int(m_RS_lnk.t_DestID) > 0 )
       sql = sql
           +    " AND dest.t_ID = " + int(m_RS_lnk.t_DestID);
     end;

     rsd = TRsbDataSet(sql);
     if( rsd.MoveNext() )
       BuyCode      = string(rsd.BuyCode);
       BuyCodeTS    = string(rsd.BuyCodeTS);
       BuyDate      = date(rsd.BuyDate);
       SaleCode     = string(rsd.SaleCode);
       SaleCodeTS   = string(rsd.SaleCodeTS);
       SaleDate     = date(rsd.SaleDate);

       if( int(m_RS_lnk.t_SourceID) > 0 )
         SourceCode   = string(rsd.SourceCode);
         SourceCodeTS = string(rsd.SourceCodeTS);
         SourceDate   = date(rsd.SourceDate);
       else
         SourceCode   = "";
         SourceCodeTS = "";
         SourceDate   = date(0,0,0);
       end;

       if( int(m_RS_lnk.t_DestID) > 0 )
         DestCode     = string(rsd.DestCode);
         DestCodeTS   = string(rsd.DestCodeTS);
         DestDate     = date(rsd.DestDate);
       else
         DestCode     = "";
         DestCodeTS   = "";
         DestDate     = date(0,0,0);
       end;
     else
       BuyCode      = "";
       BuyCodeTS    = "";
       SaleCode     = "";
       SaleCodeTS   = "";
       SourceCode   = "";
       SourceCodeTS = "";
       DestCode     = "";
       DestCodeTS   = "";
     end;

     PrintTableLine( "VL_",   BuyCode,                              null,
                     "VL_1",  BuyCodeTS,                            null,
                     "VL_2",  BuyDate,                              null,
                     "VL_3",  int(m_RS_lnk.BuyID),                  0,
                     "VL_4",  SaleCode,                             null,
                     "VL_5",  SaleCodeTS,                           null,
                     "VL_6",  SaleDate,                             null,
                     "VL_7",  int(m_RS_lnk.SaleID),                 0,
                     "VL_8",  SourceCode,                           null,
                     "VL_9",  SourceCodeTS,                         null,
                     "VL_10", SourceDate,                           null,
                     "VL_11", int(m_RS_lnk.SourceID),               0,
                     "VL_12", DestCode,                             null,
                     "VL_13", DestCodeTS,                           null,
                     "VL_14", DestDate,                             null,
                     "VL_15", int(m_RS_lnk.DestID),                 0,
                     "VL_16", FI_Code,                              null,
                     "VL_17", FI_Name,                              null,
                     "VL_18", ПолучитьВидСвязи(int(m_RS_lnk.Type)), null,
                     "VL_19", money(m_RS_lnk.newamountlink),        2,
                     "VL_20", int(m_RS_lnk.Type),                   0
                   );
  END;                                                

  PRIVATE MACRO GetNRecs()
    return -1;
  END;

  PRIVATE MACRO FormLotQuery()
    var sql;

    FormQuery( @sql );

    m_RS = TRsbDataSet(sql);
  END;

  PRIVATE MACRO FormLinkQuery()
    var sql;

    /* Отбор закрытых прямых репо, но последних в цепочке размещений */
    sql = " SELECT pv.t_amount - pv.qlater newamountlink, pv.* "
        +   " FROM (SELECT trepo.t_FIID, ldr.*, "
        +                " NVL ((SELECT SUM(lpv.t_amount) "
        +                        " FROM dsctaxlnk_dbt lpv, "
        +                             " dsctaxlot_dbt tlater "
        +                       " WHERE lpv.t_saleid = tlater.t_id "
        +                         " AND lpv.t_buyid = ldr.t_saleid "
        +                         " AND lpv.t_sourceid = ldr.t_buyid "
        +                         " AND tlater.t_date < " + Date302SQL
        +                         " AND lpv.t_type = " + string(TAXLNK_BREPO) + "), 0) qlater "
        +           " FROM dsctaxlnk_dbt ldr, "
        +                " dsctaxlot_dbt trepo, "
        +                " dsctaxlot_dbt tbuy "
        +          " WHERE ldr.t_type = " + string(TAXLNK_REPO)
        +            " AND trepo.t_id = ldr.t_saleid "
        +            " AND tbuy.t_id = ldr.t_buyid "
        +            " AND tbuy.t_type = " + string(TAXLOTS_BUY)
        +            " AND trepo.t_date2 < " + Date302SQL
        +            " AND trepo.t_date2 > " + ZeroDateSQL + ") pv "
        + "  WHERE pv.t_amount > pv.qlater "
        + " UNION ";

    /* Возвраты из размещений - для покупок- связи */
    sql = sql 
        + "SELECT l.t_amount newamountlink, buy.t_FIID, l.*, 0 "
        +  " FROM dsctaxlnk_dbt l, "
        +       " dsctaxlot_dbt buy, "
        +       " dsctaxlot_dbt sell "
        + " WHERE l.t_type = " + string(TAXLNK_REPO)
        +   " AND buy.t_id = l.t_buyid "
        +   " AND sell.t_id = l.t_saleid "
        +   " AND (sell.t_date2 >= " + Date302SQL + " OR sell.t_date2 = " + ZeroDateSQL + ") "
        +   " AND sell.t_type IN (" + string(TAXLOTS_REPO) + ", " + string(TAXLOTS_BACKREPO) + ") "
        +  "UNION ";


    /* Незакрытые короткие позиции - связи */
    sql = sql 
        + " SELECT poz.t_amount - poz.qclosed newamountlink, poz.* "
        +   " FROM (SELECT brepo.t_FIID, lopen.*, "
        +                " NVL ((SELECT SUM(lcls.t_amount) "
        +                        " FROM dsctaxlnk_dbt lcls, "
        +                             " dsctaxlot_dbt tbuy "
        +                       " WHERE lcls.t_buyid = tbuy.t_id "
        +                         " AND lcls.t_saleid = lopen.t_saleid "
        +                         " AND tbuy.t_date < " + Date302SQL
        +                         " AND lcls.t_type = " + string(TAXLNK_CLPOS) + "), 0) qclosed "
        +           " FROM dsctaxlnk_dbt lopen, "
        +                " dsctaxlot_dbt tsell, "
        +                " dsctaxlot_dbt brepo "
        +          " WHERE lopen.t_type = " + string(TAXLNK_OPSPOS)
        +            " AND tsell.t_id = lopen.t_saleid "
        +            " AND brepo.t_id = lopen.t_buyid "
        +            " AND tsell.t_date < " + Date302SQL
        +            " AND (brepo.t_date2 >= " + Date302SQL + " OR brepo.t_date2 = " + ZeroDateSQL + ")) poz "
        +  " WHERE poz.t_amount > poz.qclosed";

    m_RS_lnk = TRsbDataSet(sql);
  END;

  PRIVATE MACRO Create()
    var TaxRegDate = date(0,0,0),
        TaxRegDate_str = "",
        err;

    GetRegistryValue("SECUR\\DATE_BUILD_TAXREG", V_STRING, TaxRegDate_str, err);
    if( err == 0 )
      TaxRegDate = date(TaxRegDate_str);
    end;
    if( TaxRegDate < date(31,12,2007) )
      msgbox( "До выполненения процедуры необходимо построить связи на 31.12.2007 " );
      return;
    end; 

    InitProgress( GetNRecs(), "Печать таблицы лотов", "Печать таблицы лотов" );

    ИнициализироватьТаблицуЛотов();
    FormLotQuery();

    while( m_RS.MoveNext() )
      ПечататьСтрокуЛотов();

      UseProgress( m_NumRec = m_NumRec + 1 );
    end;

    RemProgress();

    EndTable();

    InitProgress( GetNRecs(), "Печать таблицы связей", "Печать таблицы связей" );
    m_NumRec = 0;

    ИнициализироватьТаблицуСвязей();
    FormLinkQuery();

    while( m_RS_lnk.MoveNext() )
      ПечататьСтрокуСвязей();

      UseProgress( m_NumRec = m_NumRec + 1 );
    end;

    EndTable();

    RemProgress();
  END;

  initDL_CReportTemplate( NULL, "conv_tax.xls" );
END;

CPrintReport().Run();
exit( 1 );
