/** 
 @file  insert_currlotsize_DEF-60727.mac
 @brief Вставка информации о размере лотов для инструментов валютного рынка

 Набор функций, позволяющий в рамках единовременного запуска вставить значения 

 # tag
 - functional_block:Счета
 - code_type:one_time

 # changeLog 
 |date       |author         |tasks            |note                            
 |-----------|---------------|-----------------|--------------------------------------------
 |2023.11.03 |Швецов  Я. А.  |DEF-60727        | Первоначальная реализация
*/

import "dlcontrfunc.mac", or_rep_h, "or_exl_h.mac";

private var TestMode = true; //Тестовый режим (без фиксации действий в БД)

//Колонки xls файла биржи
private const COL_MAIN_BOARD = "B",
              COL_SECCODE    = "N",
              COL_INSTRUMENT = "C",
              COL_LOTAMOUNT  = "D";

/**
 @brief Класс данных для оформления протокола
*/

PRIVATE CLASS TProtocolData(_Main_Board:string, _Seccode:string, _LotAmount:money, _Msg:string)
  var Main_Board = _Main_Board;  
  var Seccode    = _Seccode;
  var LotAmount  = _LotAmount;  
  var Msg        = _Msg;
END;

/**
 @brief Вставка информации по лотам инструментов валютного рынка
 @param[in]  Main_Board    Режим торгов
 @param[in]  Seccode       Код инструмента
 @param[in]  LotAmount     Размер лота
 @param[out] ErrStr        Текст ошибки выполнения функции
 @return                   0 если функция выполнена без ошибок, 1 - если выполнена с ошибками
*/
PRIVATE MACRO InsertCurrLotSize(Main_Board:string, Seccode:string, LotAmount:money, ErrStr:@string)
  var query, cmd, DataSet, LotSizeID;

  ErrStr = "";
 
  query = "SELECT T_ID FROM DASTS_CURRLOTSIZE_DBT WHERE T_MAIN_BOARD = ? AND T_SECCODE = ?";

  cmd = DL_RSDCOmmand(query);
  cmd.AddParam(Main_Board );
  cmd.AddParam(Seccode    );
  DataSet = cmd.Execute();
  DataSet.moveNext();
  if(not DataSet.ID)
    query = "INSERT INTO DASTS_CURRLOTSIZE_DBT (T_MAIN_BOARD, T_SECCODE, T_LOTAMOUNT) VALUES (?,?,?)";

    cmd = DL_RSDCOmmand(query);
    cmd.AddParam(Main_Board );
    cmd.AddParam(Seccode    );
    cmd.AddParam(LotAmount  );
    cmd.ExecuteCMD();
  else
    LotSizeID = DataSet.ID;
    query = "UPDATE DASTS_CURRLOTSIZE_DBT SET T_LOTAMOUNT = ? WHERE T_ID = ?";

    cmd = DL_RSDCOmmand(query);
    cmd.AddParam(LotAmount );
    cmd.AddParam(LotSizeID  );
    cmd.ExecuteCMD();
  end;
  return 0;
OnError(er)
  ErrStr = er.Message;
  return 1;
END;

/**
 @brief Получение системной даты и времени
 @param[out] SystDate Системная дата
 @param[out] SystTime Системное время
*/
private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

/**
 @brief Объект класса для получения значений кода
 @param[in] ActiveBook объект книга
 @param[in] row строка старта поиска
 @param[in] maxrow строка окончания поиска
*/
private class InstrumentToCodeArr(ActiveBook, row, maxrow)
  private var Arr = TArray;
  var _row = row;
  /**
   @brief Класс связки Instrument - Code
   @param[in] pInstrument Значение Instrument
   @param[in] pCode Значение Code
  */
  private class InstrumentCode (pInstrument, pCode)
    var Instrument = "";
    var Code = "";
    Instrument = pInstrument;
    Code = pCode;
  end;
  /**
   @brief Получение Code по Instrument
   @param[in] Instrument 
   @return Значение Code для данного Instrument
  */
  macro getCodeFromInstrument(Instrument)
    var i = 0;
    while (i < Arr.size)
      if (Arr(i).Instrument == Instrument)
        return Arr(i).Code;
      end;
      i = i + 1;
    end;
    return Instrument;
  end;

  while (_row <= maxrow)
    if (valtype( ActiveBook.getCellValue(COL_SECCODE + _row)) != V_UNDEF) 
      var Instrument_tmp = string(ActiveBook.getCellValue(COL_INSTRUMENT + _row));
      var Code_tmp    = string(ActiveBook.getCellValue(COL_SECCODE + _row)); 
      Arr[Arr.size()] = InstrumentCode(Instrument_tmp,Code_tmp);
    end;
    _row = _row + 1;
  end;
end;

/**
 @brief Процедура для копирования файла на сервер приложений
*/
private macro SaveFileToAppServ(filepath:string, FileName):string
  if(not isStandAlone()) // когда мы на терминале, копируем файл на СП     
    var path_appserv = "..\\txtfile\\" + FileName;
      
    if(not CopyFile("$" + filepath, path_appserv))
      msgbox("Ошибка при передаче на сервер приложений файла $" + filepath);
    end;
    filepath = path_appserv;
  end;
    
  return filepath;
end;

/**
 @brief Процедура удаления файла с сервер приложений
*/
private macro DeleteFileToAppServ(FullNameFile)
  if(not isStandAlone()) // когда мы на терминале, удаляем файл на СП
    if (not RemoveFile (FullNameFile))
      println ("Ошибка удаления с сервера приложений файла " + FullNameFile);
    end;
  end;
end;

/**
 @brief Процедура для получения информации о лотах валютного рынка
*/

PRIVATE MACRO ExecUpdateSettAcc()
  var cnt = 0, i = 0, settaccid = 0;
  var LogArr = TArray(203000);
  var SystDate, SystTime;
  var err = 0, ErrStr = "";
  var err_cnt = 0;
  var FullNameFile = "";
  var FileName = "";
  var ExtName = "";
  var frow = 3;
  var row = 1;
  var maxrow = 0;
  var InstrumentToCodeObj;
  var ActiveBook;

  var jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  var rc = jvm.callStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");

  if(GetTRUE(FALSE, "Выполнить с фиксацией действий в БД?\n"
                 +"(НЕТ - процедура выполнится в тестовом режиме без сохранения изменений в базе; ДА - все успешные изменения будут сохранены в базе)\n"
                 +"Первый запуск рекомендуется выполнить в тестовом режиме и проверить, что процедура выполнится без ошибок."))
    TestMode = false;
  end;

  GetSystDateTime(@SystDate, @SystTime);
  if (not getint(maxrow, "Введите номер последней строки с данными"))
    MsgBox("Отказ от загрузки");
    return 0;
  end;
  /*Выбираем файл для обработки*/
  if(not SelectFile(FullNameFile, "*.xls*", "Выберите файл для загрузки", 0, true))
    MsgBox("Отказ от загрузки");
    return 0;
  end;

  //Отделяем путь файла от имени
  splitfile(FullNameFile, FileName, ExtName);
  FileName = FileName + ExtName;

  FullNameFile = SaveFileToAppServ(FullNameFile, FileName);
  /*Открываем файл*/
  BegAction(1, "Обработка файла \"" + FullNameFile + "\". Ждите...");

  ActiveBook = rc.openWorkbook(FullNameFile);
  if(not ActiveBook)
    ActiveBook = null;
    rc = null;
    DeleteFileToAppServ(FullNameFile);
    println("Ошибка открытия файла \"" + FullNameFile + "\"");
    EndAction(1);
    return 1;
  end;

  ActiveBook.setActiveSheet(0);

  /*Ищем начало таблицы*/
  if(valtype(ActiveBook.getCellValue(COL_MAIN_BOARD + frow)) == V_UNDEF)
    MsgBox("Попытка загрузить пустой файл или файл неправильного формата");
    ActiveBook.close();
    ActiveBook = null;
    rc = null;
    DeleteFileToAppServ(FullNameFile);
    return 0;
  end;

  row = frow+1;
  //Проходим по файлу
  InstrumentToCodeObj = InstrumentToCodeArr(ActiveBook, row, maxrow);
  InitProgress(-1);
  i = 0;
  cnt = 0;
  while (row <= maxrow)
    //Проходим только по не-пустым строкам 
    if (valtype(ActiveBook.getCellValue(COL_MAIN_BOARD + row)) != V_UNDEF) 
      var main_board_tmp = string(ActiveBook.getCellValue(COL_MAIN_BOARD + row));
      var seccode_tmp    = string(ActiveBook.getCellValue(COL_SECCODE + row)); 
      var lotamount_tmp  = money(ActiveBook.getCellValue(COL_LOTAMOUNT + row));

      if((seccode_tmp == "Undefined") OR (seccode_tmp == "") )
        seccode_tmp = InstrumentToCodeObj.getCodeFromInstrument(string(ActiveBook.getCellValue(COL_INSTRUMENT + row)));
      end;
      RslDefCon.BeginTrans();
      err = InsertCurrLotSize(main_board_tmp, seccode_tmp, lotamount_tmp, @ErrStr);
      if((TestMode) or (err))
        RslDefCon.RollbackTrans();
      else
        RslDefCon.CommitTrans();
      end;
      LogArr[LogArr.size()] = TProtocolData(main_board_tmp, seccode_tmp, lotamount_tmp, ErrStr);
      cnt = cnt + 1;
    end;
    UseProgress(i);
    i = i + 1;  
    row = row + 1;
  end;
  RemProgress();

  ActiveBook.close();
  ActiveBook = null;
  rc = null;
   
  if (LogArr.size() == 0)
    println("Не найдено информации для загрузки");
  end;
  InitProgress(LogArr.size(), "Печать протокола", "Печать протокола");

           [                                  Вставка информации о размере лотов для инструментов валютного рынка
           Дата выполнения:  #############
           Время выполнения: #############
           Отобрано записей:   #############  Из них не обработано: #########
           ]
           (SystDate:f, SystTime, cnt, err_cnt);

  println("");
  println("┌───────────────────────────────┬────────────────────────────────┬──────────────────────┐");
  println("│        Режим торгов           │        Код инструмента         │       Размер лота    │");
  println("│                               │                                │                      │");
  println("├───────────────────────────────┼────────────────────────────────┼──────────────────────┤");
  i = 0;
  while(i < LogArr.size())
          [│###############################│################################│######################│############################################################################################]
          (string(LogArr[i].Main_Board):c,
           string(LogArr[i].Seccode):c,
           string(LogArr[i].LotAmount):c,
           string(LogArr[i].Msg):l:w        
          );
    if(i < LogArr.size() - 1)
      println("├───────────────────────────────┼────────────────────────────────┼──────────────────────┤");
    end;

    UseProgress(i = i + 1);
  end;

  RemProgress();

      println("└───────────────────────────────┴────────────────────────────────┴──────────────────────┘");

  DeleteFileToAppServ(FullNameFile);

OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  if (valtype(ActiveBook) != V_UNDEF)
    ActiveBook.close();
  end;
  ActiveBook = null;
  rc = null;
  if (FullNameFile)
    DeleteFileToAppServ(FullNameFile);
  end;
  msgbox("Ошибка: " + er.Message);
  EndAction(1);
  return 1;
END;

ExecUpdateSettAcc();