/*
$Name:        cnvmsfo9vs.mac
$Module:      Ценные бумаги
$Description: Конвертация МСФО0 для данных СВ
*/

import vslib,MC_lib,vsbnrfd,vscateg;

private var AccDate  = date(08,01,2019); //Дата учета
private var CalcDate = date(08,01,2019); //Дата расчета

//Счет для учета финансового результата
//Возможные значения: 706, 10801, 10901
private const СчетДляУчетаФР = 706;

//Метод расчета АС для РЕПО
//Возможные значения: 0 - Авто; 1 - ЛМ; 2 - ЭПС
private const МетодРасчетаАСДляРЕПО = 0;


//Вариант учета отсроченной разницы
//Возможные значения: 1 - за весь период; 2 - От даты расчета
private const ОтсроченнаяРазница = 2;

private var currPart = "";

private const UNDEF_CHAR = "?",
              L_OBJTYPE_LEVELESSENTIAL_EPS = 4,
              L_OBJTYPE_LEVELESSENTIAL_AC = 1,
              METHOD_UNDEF = 0,
              METHOD_LINE = 1,
              METHOD_EPS = 2,
              METHOD_RPS = 3;

macro cnv_msgbox(StrErr)
  println(StrErr);
end;

macro ПолучитьКатегориюФР(Categ:string)
  var CatFR = Categ;

  if(СчетДляУчетаФР == 10801)
    CatFR = "Нераспределенная прибыль";
  elif(СчетДляУчетаФР == 10901)
    CatFR = "Непокрытый убыток";
  end;

  return CatFR;
end;

PRIVATE MACRO GetCCY(FIID)
  var cmd = DL_RSDCommand("select t_CCY from dfininstr_dbt where t_FIID = ?");
  var ccy = "";

  cmd.AddParam(FIID);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ccy = DataSet.CCY;
  end;

  return ccy;
END;

private Macro CorrectFlag()
  RslDefCon.BeginTrans();
  var cmd;
  cmd = RsdCommand("UPDATE DDL_LEG_DBT leg "
                  +"   SET T_RELATIVEPRICE = '?' "
                  +" WHERE     (T_RELATIVEPRICE = CHR(0) OR T_RELATIVEPRICE IS NULL) "
                  +"       AND T_LEGID = 0 "
                  +"       AND T_LEGKIND = 1 "
                  +"       AND ( (SELECT COUNT (*) "
                  +"                FROM DFICERT_DBT "
                  +"               WHERE T_CERTID = leg.T_DEALID AND T_AVOIRKIND = 5) > 0)");
  cmd.Execute();
  RslDefCon.CommitTrans();
end;

private macro ПолучитьЗначениеКорректировкиНаНерыночность(BCID,EnrolId,Corr:@Numeric,CorrPFI:@Numeric)
  var cmd, DataSet, query;
  query =  "SELECT T_PERC as Corr, T_CURRENCY as CorrPFI"
          +"  FROM DVSINCOME_DBT "
          +" WHERE     T_INCOMETYPE = ? "
          +"       AND T_BCID = ? "
          +"       AND T_ENROLMENT_ID = ? ";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(150);
  cmd.AddParam(BCID);
  cmd.AddParam(EnrolId);
  DataSet = cmd.Execute();
  if (DataSet.MoveNext())
    Corr = DataSet.Corr;
    CorrPFI = DataSet.CorrPFI;
  end;
end;

private macro ПереводСтатусаЗалога()
  var query = " DECLARE "
             +" BEGIN "
             +"    FOR cur "
             +"       IN (SELECT bck.* "
             +"             FROM dvsbnrbck_dbt bck, dvsbanner_dbt bnr, dficert_dbt ficert "
             +"            WHERE     bnr.T_BCID = bck.t_bcid "
             +"                  AND bnr.t_backoffice = 'Y' "
             +"                  AND (   INSTR (bck.T_OLDBCSTATE, 'З') != 0 "
             +"                       OR INSTR (bck.t_newbcstate, 'З') != 0) "
             +"                  AND ficert.T_CERTID = bnr.T_BCID "
             +"                  AND ficert.t_avoirkind = 5) "
             +"    LOOP "
             +"       IF cur.t_isbcstate = 'R' "
             +"       THEN "
             +"          cur.t_oldabcstatus := 25; "
             +"          cur.t_newabcstatus := 20; "
             +"       ELSE "
             +"          cur.t_oldabcstatus := 20; "
             +"          cur.t_newabcstatus := 25; "
             +"       END IF; "
             +"       cur.t_isbcstate := NULL; "
             +"       cur.t_bcstatus := 'X'; "
             +"       cur.t_id := 0; "
             +"       cur.t_newbcstate := ''; "
             +"       cur.T_OLDBCSTATE := ''; "
             +"       INSERT INTO dvsbnrbck_dbt "
             +"            VALUES (cur.T_ID, "
             +"                    cur.T_BCID, "
             +"                    cur.T_CHANGEDATE, "
             +"                    cur.T_BCSTATUS, "
             +"                    cur.T_ABCSTATUS, "
             +"                    cur.T_OLDABCSTATUS, "
             +"                    cur.T_NEWABCSTATUS, "
             +"                    cur.T_ISBCSTATE, "
             +"                    cur.T_OLDBCSTATE, "
             +"                    cur.T_NEWBCSTATE, "
             +"                    cur.T_REPAYMENTDATE, "
             +"                    cur.T_OLDREPAYMENTDATE, "
             +"                    cur.T_NEWREPAYMENTDATE, "
             +"                    cur.T_BCPRESENTATIONDATE, "
             +"                    cur.T_OLDBCPRESENTATIONDATE, "
             +"                    cur.T_NEWBCPRESENTATIONDATE, "
             +"                    cur.T_PORTFOLIO, "
             +"                    cur.T_OLDPORTFOLIOID, "
             +"                    cur.T_NEWPORTFOLIOID, "
             +"                    cur.T_PRESENTER, "
             +"                    cur.T_OLDPRESENTER, "
             +"                    cur.T_NEWPRESENTER, "
             +"                    cur.T_STORAGEORDERNUM, "
             +"                    cur.T_OLDSTORAGEORDERNUM, "
             +"                    cur.T_NEWSTORAGEORDERNUM, "
             +"                    cur.T_STORAGEORDERDATE, "
             +"                    cur.T_OLDSTORAGEORDERDATE, "
             +"                    cur.T_NEWSTORAGEORDERDATE); "
             +"    END LOOP; "
             +"    UPDATE dvsbanner_dbt bnr "
             +"       SET bnr.t_bcstatus = 25, "
             +"           bnr.t_bcstate = REPLACE (bnr.t_bcstate, 'З', 'Щ') "
             +"     WHERE     bnr.t_backoffice = 'Y' "
             +"           AND INSTR (bnr.T_BCSTATE, 'З') != 0 "
             +"           AND EXISTS "
             +"                  (SELECT ficert.t_ficertid "
             +"                     FROM dficert_dbt ficert "
             +"                    WHERE     ficert.T_CERTID = bnr.T_BCID "
             +"                          AND ficert.t_avoirkind = 5); "
             +" END;";
  var cmd = RsdCommand(query);
  cmd.Execute();
end;

/* Проводка по категориям учета (в том числе и мультивалютная)
   СчетДебет,СчетКредит - если тип V_STRING то категории, иначе record ACCOUNT*/
MACRO МСФО9_ПроводкаПоКатегориямУчета( FD, СчетДебет, СчетКредит, ДатаВалютирования,
                                 Chapter, FIID, СуммаОперации, docum,
                                 Result_Carry, Kind_Oper,Numb_Document,Ground,
                                 PaymentID, FIRolePayer, FIRoleReceiver,
                                 СчетДебетFIID, СчетКредитFIID, FIID2, СуммаОперации2,
                                 СчетДебетРежим, СчетКредитРежим
                                 )
   var stat:bool = true, CуммаДебет = $0, СуммаКредит  = $0, 
      СчетОВПДебет = "", СчетОВПКредит = "", СчетДоходов = "", СчетРасходов = "";

   var tr, NoCarry:bool = false;
   var PaymentObj = null;

   record AccountPayer(account);
   record AccountReceiver(account);

   macro SayCarryError( error )
     msgbox( error );
     return false;
   end;

   if( (СуммаОперации == null) AND (СуммаОперации2 == null) )  
     return SayCarryError( "В проводке не задана ни одна из сумм." );
   end;

   if (СчетДебетРежим == null)
     СчетДебетРежим = MC_OPENACC_RECREATE;
   end;

   if (СчетКредитРежим == null)
     СчетКредитРежим = MC_OPENACC_RECREATE;
   end;
     
   /*оставить только копейки для первой суммы, если она задана*/
   if( СуммаОперации != null )
     if( Chapter != 5 ) /*количество ц/б может быть дробным*/
        СуммаОперации = money( round( СуммаОперации, 2 ) );    
     end;
     if( СуммаОперации < $0 )  
        return SayCarryError( "Сумма проводки < 0." );
     end;
   else  
    СуммаОперации = $0;
   end;
   /*для второй суммы, если она задана*/
   if( СуммаОперации2 != null )
     if( Chapter != 5 ) /*количество ц/б может быть дробным*/
        СуммаОперации2 = money( round( СуммаОперации2, 2 ) );
     end;
     if( СуммаОперации2 < $0 )  
        return SayCarryError( "Вторая сумма проводки < 0." );
     end;
   else
    СуммаОперации2 = $0;
   end;

   if( (СуммаОперации == $0) AND (СуммаОперации2 == $0) )  
     return true;
   end;

   if( ValType(СчетДебет) == V_STRING ) /*задан через категорию*/
         if(not VS_GetAccountManual(СчетДебет, FD, NULL, СчетДебетРежим, СчетДебетFIID, FIRolePayer, AccountPayer, ДатаВалютирования))
             return SayCarryError( "Ошибка при определении счета по категории "+СчетДебет+" в проводке.");
         end;
   else
     copy( AccountPayer, СчетДебет );
   end;

   if( ValType(СчетКредит) == V_STRING ) /*задан через категорию*/
         if(not VS_GetAccountManual(СчетКредит, FD, NULL, СчетКредитРежим, СчетКредитFIID, FIRoleReceiver, AccountReceiver, ДатаВалютирования))
            return SayCarryError( "Ошибка при определении счета по категории "+СчетКредит+" в проводке." );
         end;
   else
     copy( AccountReceiver, СчетКредит );
   end;

   if( AccountPayer.Code_Currency == AccountReceiver.Code_Currency)

     /*задана только вторая сумма в валюте одного из счетов*/
     if( (FIID == null) AND (FIID2 != null) )
        FIID = FIID2;
     elif( (FIID == null) AND (FIID2 == null) )
        return SayCarryError("Не задана ни одна из валют проводки.");
     end;

     if( (FIID != AccountPayer.Code_Currency) AND
         (FIID != AccountReceiver.Code_Currency) )
        return SayCarryError( "В проводке не совпадают валюты счетов: " 
                               + string(AccountPayer.Code_Currency) + " " + string( AccountReceiver.Code_Currency ) +
                              "| и валюта документа: " + string(FIID) );
     end;

     if( СуммаОперации != $0 )
        CуммаДебет = СуммаКредит = СуммаОперации;
     elif( СуммаОперации2 != $0 )
        CуммаДебет = СуммаКредит = СуммаОперации2;
     end;
     

   else /* валюты разные - мультивалютная проводка */
      
     СуммаКредит = $0;
     CуммаДебет  = $0;
     
     /*задана одна сумма в валюте одного из счетов*/
     if( (FIID != null) AND (FIID2 == null) )
        if( FIID == AccountPayer.Code_Currency )
           CуммаДебет  = СуммаОперации;
        else
           СуммаКредит = СуммаОперации;
        end;
     /*задана только вторая сумма в валюте одного из счетов*/
     elif( (FIID2 != null) AND (FIID == null) )
        if( FIID2 == AccountPayer.Code_Currency )
           CуммаДебет  = СуммаОперации2;
        else
           СуммаКредит = СуммаОперации2;
        end;
     /*заданы обе суммы в валютах счетов по которым выполняем проводку*/
     elif( (FIID2 != null) AND (FIID != null) )                         
       if( FIID == AccountPayer.Code_Currency ) 
          CуммаДебет  = СуммаОперации;
       elif( FIID2 == AccountPayer.Code_Currency ) 
          CуммаДебет  = СуммаОперации2;
       end;

       if( FIID == AccountReceiver.Code_Currency ) 
          СуммаКредит = СуммаОперации;
       elif( FIID2 == AccountReceiver.Code_Currency ) 
          СуммаКредит = СуммаОперации2;
       end;
     else
        return SayCarryError("Не задана ни одна из валют проводки.");
     end;

     if( not CheckMultyCarrySum( AccountPayer.Code_Currency, AccountReceiver.Code_Currency,
                                 CуммаДебет, СуммаКредит, ДатаВалютирования ) )
        return SayCarryError("Ошибка при определении сумм мультивалютной проводки.");
     end;
  
   end;

   if( NoCarry == false )
      /* ------ Выполнить проводку ----------------------------------------------*/
      tr = RsbAccTransaction();
              
      if( tr == NULL )
         return SayCarryError("Ошибка при создании проводки");
      end;

      tr.Chapter         = Chapter;
      tr.Date_Carry      = ДатаВалютирования;
      if( PaymentObj != null )
         tr.Number_Pack  = PaymentObj.NumberPack;
      end;
      tr.Numb_Document   = Numb_Document;
      tr.ResultCarry     = 1;
      tr.Kind_Oper       = Kind_Oper;
      tr.Ground          = "Конвертация. " + Ground;
      tr.Department      = {OperDprt};
      tr.Oper            = {oper};
      tr.FIIDPayer       = AccountPayer.Code_Currency;
      tr.FIIDReceiver    = AccountReceiver.Code_Currency;
      tr.SumPayer        = CуммаДебет;
      tr.SumReceiver     = СуммаКредит;
      tr.AccountPayer    = AccountPayer.Account;
      tr.AccountReceiver = AccountReceiver.Account;
      tr.Shifr_Oper      = "18";
      
      tr.TypeDocument = "М"; //Перенос по МСФО-9

      if( not tr.Carry() )
         return SayCarryError("Ошибка при выполнении проводки \"" + Ground + "\"");
      else
         msgbox("   Выполнена проводка "+AccountPayer.Account+"<->"+AccountReceiver.Account+" \"" + Ground + "\" на сумму " + СуммаОперации + " " + GetCCY(FIID));
      end;
      /*-------------------------------------------------------------------------*/  

   end;

   if( not stat ) 
      return SayCarryError("Ошибка при вставке документа проводки.");
   end;

   return stat;
END;

private macro CloseAccount(Acc)
  var err = 0;
  var msg = "";

  if( CB_CloseAccount(Acc.rec.Chapter, Acc.rec.Code_Currency, Acc.rec.Account, AccDate, null) != 0 )
     msg = GetErrMsg();
     msgbox("Ошибка закрытия счета: "+msg+". Счет "+Acc.rec.Account+" не закрыт.");
     err = 1;
  else
     msgbox("   Счет "+Acc.rec.Account+" успешно закрыт!");
  end;

  return err;
end;

private macro GetVSBannerQuery()
  var query = " select bnr.T_BCID as BCID, fi.T_FICERTID as FICERTID "
            + "   from dvsbanner_dbt bnr, dficert_dbt fi "
            + "  where bnr.T_BCID = fi.T_CERTID "
            + "    AND bnr.t_bcstatus = " + VSBANNER_STATUS_SENDED
            + "    AND INSTR (bnr.t_bcstate, 'И') = 0 "
            + "    AND fi.T_AVOIRKIND = " + AVOIRISSKIND_BILL;
  return query;
end;

private macro GetVekselName(FD)
  var bnr = FD.GetBnr();
  return "сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber);
end;

PRIVATE MACRO ОформлениеРасчетаДисконта(ВексПД, ActionDate, Discount, AccDiscount, CalcDiscount, AccCalcDiscount)
  var bnr, EnrolId;
  record income("vsincome");
  bnr = ВексПД.GetBnr();
  EnrolId = VS_GetLastPayedEnrolmentID(bnr.rec.BCID, ActionDate);
  //Начальный дисконт
  income.AutoKey        = 0;
  income.BCID           = bnr.rec.BCID;
  income.Perc           = Discount; 
  income.Disc           = 0.0;
  income.Currency       = ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY);
  income.StartDate      = date(ActionDate);
  income.EndDate        = date(ActionDate);
  income.OperDate       = date(ActionDate);
  income.Quality        = UNSET_CHAR;
  income.AccPerc        = AccDiscount;
  income.AccDisc        = 0.0;
  income.AccCurrency    = ВексПД.ОпределитьВалютуУчета();
  income.IncomeType     = VSINCOMETYPE_FIRSTDISC;
  income.Enrolment_Id   = EnrolId;
  income.Bonus          = 0.0;
  income.AccBonus       = 0.0;
  if( InsertVSINCOME(income) )
    return Ошибка("Не удалось сохранить запись о начислении ПДД в истории");
  end;
  //Списанный дисконт
  ClearRecord(income);
  income.AutoKey        = 0;
  income.BCID           = bnr.rec.BCID;
  income.Perc           = 0.0; 
  income.Disc           = CalcDiscount;
  income.Currency       = ВексПД.GetParametr(MC_TYPE_PARAMETR_CURRENCY);
  income.StartDate      = date(ActionDate);
  income.EndDate        = date(ActionDate);
  income.OperDate       = date(ActionDate);
  income.Quality        = UNSET_CHAR;
  income.AccPerc        = 0.0;
  income.AccDisc        = AccCalcDiscount;
  income.AccCurrency    = ВексПД.ОпределитьВалютуУчета();
  income.IncomeType     = VSINCOMETYPE_CHARGE;
  income.Bonus          = 0.0;
  income.Enrolment_Id   = EnrolId;
  income.AccBonus       = 0.0;
  if( InsertVSINCOME(income) )
    return Ошибка("Не удалось сохранить запись о начислении ПДД в истории");
  end;

  return 0;
END;



macro ВыполнитьПроводкиДисконтаКонв()
  var cmd, query, DataSet;
  var FD;
  var cnt = 0, i = 0, leg, bnr;
  var CatCred = "";
  var Sum = $0;
  var err = 0;
  var msg = "";
  var ВалУч, СПДСВН, ДисконтВУ, КПеречислениюВУ, КПеречислениюВН, ДисконтВН;
  var СчетДисконта = TRecHandler("account.dbt");

  var ourBnr = "Наш вексель";
  var discFrom = "БВыплДиск,Сц/б";
  var discTo = "Дисконт, Свексель";

  cmd = DL_RSDCommand(GetVSBannerQuery());

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, currPart+"Обработка векселей по остаткам дисконта", currPart+"Обработка векселей по остаткам дисконта");
    
    DataSet = cmd.Execute();
    while((not err) and (DataSet.moveNext()))

      FD = VSBannerFD(DL_VSBANNER,DataSet.BCID);
      leg = FD.GetLeg();
      bnr = FD.GetBnr();
      ВалУч = FD.ОпределитьВалютуУчета();
      msgbox("Обрабатывается вексель " + GetVekselName(FD));
      if (ПолучитьСчетВекселя (discFrom, FD, NULL, MC_OPENACC_CHECKEXIST, ВалУч, СчетДисконта, AccDate ))
        ПолучитьОстаток(Sum, СчетДисконта.rec.Account, ВалУч, AccDate);
        Sum = Abs(Sum);
        msgbox("Найден счет "+СчетДисконта.rec.Account+", остаток: " + Sum);
        if (Sum != $0)
          if (not СВ_ПолучитьЦенуПродажиНаДату(leg.rec.id,CalcDate,СПДСВН))
            return Ошибка("Не удалось получить цену продажи векселя");
          else
            ДисконтВН = leg.rec.Principal - СПДСВН;
            ДисконтВУ = VS_Convert(ДисконтВН, CalcDate, leg.rec.PFI, ВалУч);
            КПеречислениюВН = ДисконтВН - VS_Convert(Sum, CalcDate, ВалУч, leg.rec.PFI);
            КПеречислениюВУ = ДисконтВУ - Sum;
          end;
           if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                  ourBnr, СчетДисконта, /* КатегДеб , КатегКредит*/
                  AccDate,                     /* Дата */
                  1,                           /* Глава */
                  ВалУч,                       /* Валюта */
                  Sum,                         /* Сумма  */
                  NULL,                        /* Документ */
                  INPCARRY,                    /* Result_Carry */
                  " 1",                        /* Kind_Oper */
                  "",                          /* Numb_Document */
                  "Перенос остатка дисконта по ц/б "+GetVekselName(FD),
                  null,
                  null,
                  null,
                  ВалУч, ВалУч,
                  null,
                  null,
                  MC_OPENACC_CHECKEXIST
                 ) 
             )
               msg = GetErrMsg();
               if(msg != "")
                 msgbox(msg);
               end;

               return 1;
           end;

           if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                  ourBnr, discTo, /* КатегДеб , КатегКредит*/
                  AccDate,                     /* Дата */
                  1,                           /* Глава */
                  ВалУч,                 /* Валюта */
                  КПеречислениюВУ,               /* Сумма  */
                  NULL,                        /* Документ */
                  INPCARRY,                    /* Result_Carry */
                  " 1",                        /* Kind_Oper */
                  "",                          /* Numb_Document */
                  "Перенос начисленного дисконта по ц/б "+GetVekselName(FD),
                  null,
                  null,
                  null,
                  ВалУч, ВалУч
                 ) 
             )
               msg = GetErrMsg();
               if(msg != "")
                 msgbox(msg);
               end;

               return 1;
           end;

           if (CloseAccount(СчетДисконта) != 0)
             return Ошибка("Ошибка закрытия счета");
           end;

           if (ОформлениеРасчетаДисконта(FD,CalcDate, ДисконтВН, ДисконтВУ, КПеречислениюВН, КПеречислениюВУ) != 0)
             return Ошибка("Ошибка расчета начального дисконта");
           end;
        end;

      else
        //msgbox("Векселеь " + GetVekselName(FD)+ " не имеет открытого счета с КУ \""+discFrom+"\"");
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();
  end;

  return err;
END;

private macro ОбновитьПараметрыВекселя(_bnr,_leg)
  var ret = true;
  var bnr = TBFile("vsbanner.dbt","W");
  var leg = TBFile("dl_leg.dbt","W",1);
  bnr.rec.BCID = _bnr.rec.BCID;
  leg.rec.ID = _leg.rec.ID;
  if (bnr.GetEQ())
    copy(bnr,_bnr);
    ret = bnr.Update();
  end;
  if ((ret) and (leg.GetEQ()))
    copy(leg,_leg);
    ret = leg.Update();
  end;
  return ret;
end;

private macro ВставитьСтатусОбОплате(_bnr)
  var vsbnrbck = TBFile("vsbnrbck.dbt","W");
  var query = "select * "
      +"  from dvsbnrbck_dbt "
      +" where t_newabcstatus = " + VSBANNER_STATUS_FORMED
      +"   and t_oldabcstatus != " + VSBANNER_STATUS_PAYED
      +"   and t_BCID = " + _bnr.rec.BCID;
  var cmd = DL_RSDCommand(query);
  var DataSet = cmd.Execute();
  while (DataSet.moveNext())
    vsbnrbck.rec.ID = DataSet.ID;
    if (not vsbnrbck.GetEQ())
      return false;
    end;
    vsbnrbck.rec.oldabcstatus = VSBANNER_STATUS_PAYED;
    if (not vsbnrbck.Update())
      return false;
    end;
    vsbnrbck.Clear();
    DataSet.GetRecord().CopyTo(vsbnrbck.rec);
    vsbnrbck.rec.Id = 0;
    vsbnrbck.rec.newabcstatus = VSBANNER_STATUS_PAYED;
    if (not vsbnrbck.Insert())
      return false;
    end;
  end;
  return true;
end;

macro ОбработатьВекселяПоПервоначальномуПризнанию()
  var cmd, query, DataSet;
  var FD;
  var cnt = 0, i = 0;
  var CatCred = "";
  var Sum = $0;
  var msg = "",err;
  var ВалУч;
  var leg, bnr;
  var МинРПС, МаксРПС, ОтклСуществ, ССНацВал, 
      ДнейВГоду, СрокОбращения, НастройкаДОГод,
      НастройкаДОНесущОткл, СуммаКорр, СуммаКоррКНачисл, СуммКоррВУ;
  var ЭПС, АСпоЭПС, АСпоЛМ, ДатаВыдачиВекселя;
  var КатегДеб, КатегКредит, Ground;
  var EnrolId, SourceDataLayer;
  var СПДСВН,СПДСНацВал,ОтсрРазнНацВал=$0,ОтсрРазнВУ,
      ОтсрРазнНацВалКНачисл,FairValue,СуммаДисконта,
      ДатаПослРасчКорр,InterestIncomeAdd,СуммаПроцентов, 
      НачисПроц, СуммаПроцентовВУ, prccontract, DealId,
      Корр=NULL,КоррВал=NULL,РасчетСС,pMsg,pCat55,pCat66,
      PAlgUsed,pEir,pAmortCalcKind,ОтсрРазнВН, НоваяСС, pRateId;
  record income("vsincome");
  prccontract = TRecHandler("prccontract");

  cmd = DL_RSDCommand(GetVSBannerQuery());

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, currPart+"Обработка векселей по первоначальному признанию", currPart+"Обработка векселей по первоначальному признанию");
    
    DataSet = cmd.Execute();
    while((not err) and (DataSet.moveNext()))

      FD = VSBannerFD(DL_VSBANNER,DataSet.BCID);

      msgbox("Обрабатывается вексель " + GetVekselName(FD));

      ВалУч = FD.ОпределитьВалютуУчета();
      bnr = FD.GetBnr();
      leg = FD.GetLeg();

      if (not ВставитьСтатусОбОплате(bnr))
        return Ошибка("Не удалось вставить статус об оплате");
      end;

      EnrolId = VS_GetLastPayedEnrolmentID(bnr.rec.BCID);
      ДатаВыдачиВекселя = VS_GetVSBnrLastPayedDate(bnr.rec.BCID, CalcDate);

      if (not СВ_ПолучитьИдСделки(bnr.rec.BCID, CalcDate, DealId))
        return Ошибка("Не удалось получить DealId для векселя");
      end;

      if (not СВ_ПолучитьЦенуПродажиНаДату(leg.rec.id,CalcDate,СПДСВН))
        return Ошибка("Не удалось получить цену продажи векселя");
      end;

      if ((leg.rec.IncomeRate != $0) and (leg.rec.PrincipalDiff!=0) and (leg.rec.RelativePrice != BNR_UNDEF_CHAR)) //Параметры заданы вручную
        MsgBox("По векселю "+GetVekselName(FD)+" основные параметры заданы вручную, расчёт не используется");
        ПолучитьЗначениеКорректировкиНаНерыночность(bnr.rec.BCID,EnrolId,@Корр,@КоррВал);
        ОтсрРазнНацВал =  VS_Convert(Корр, CalcDate, КоррВал, NATCUR );
        ОтсрРазнВУ =  VS_Convert(Корр, CalcDate, КоррВал, ВалУч );
        ОтсрРазнВН =  VS_Convert(Корр, CalcDate, КоррВал, leg.rec.PFI );
        НоваяСС = VS_Convert(СПДСВН, CalcDate, leg.rec.PFI, NATCUR) + ОтсрРазнНацВал;
        ЭПС = leg.rec.IncomeRate / 100;
      else
        MsgBox("По векселю "+GetVekselName(FD)+" не заданы основные параметры, параметры расчитаются автоматически");
        if (not СВ_РасчетСС(bnr.rec.Fiid,bnr.rec.BCID,DealId,ДатаВыдачиВекселя,РасчетСС,pMsg,pRateId,pCat55,pCat66,PAlgUsed,ЭПС,pAmortCalcKind))
          return Ошибка("Ошибка расчета справедливой стоимости: "+pMsg);
        end;
        ОтсрРазнВН = СПДСВН - РасчетСС;
        ОтсрРазнВУ = VS_Convert(ОтсрРазнВН, CalcDate, leg.rec.PFI, ВалУч );
        ОтсрРазнНацВал = VS_Convert(ОтсрРазнВН, CalcDate, leg.rec.PFI, NATCUR );
        НоваяСС = VS_Convert(РасчетСС, CalcDate, leg.rec.PFI, NATCUR);
        leg.rec.IncomeRate = ЭПС * 100;
        if ((pCat55 == 1) or (pCat55 == 2))
          leg.rec.PayRegTax = SET_CHAR; //Наблюдаемые данные
        elif (pCat55 == 3)
          leg.rec.PayRegTax = UNSET_CHAR; //Ненаблюдаемые данные
        end;
        if (pAmortCalcKind == METHOD_RPS)
          ОтклСуществ = true;
          leg.rec.PrincipalDiff = METHOD_RPS;
        else
          ОтклСуществ = false;
        end;
        leg.rec.RelativePrice = IIF(ОтклСуществ,BNR_UNSET_CHAR,BNR_SET_CHAR);
      end;

      if (leg.rec.RelativePrice == BNR_UNSET_CHAR) // Тест на рын. = нет

        if (leg.rec.PayRegTax == SET_CHAR) // Наблюдаемые данные = да

          if (ОтсрРазнНацВал != $0)
            if (ОтсрРазнНацВал > $0)
              КатегДеб = "+Корр, Свексель";
              КатегКредит = ПолучитьКатегориюФР("+Эмиссия, СВ");
              Ground = String("Отражение корректировки по ц/б " + GetVekselName(FD) + " уменьшающая стоимость");
            else 
              КатегДеб = ПолучитьКатегориюФР("-Эмиссия, СВ");
              КатегКредит = "-Корр, Свексель";
              Ground = String("Отражение корректировки по ц/б " + GetVekselName(FD) + " увеличивающая стоимость");
            end;
            if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                  КатегДеб, КатегКредит, /* КатегДеб , КатегКредит*/
                  AccDate,                     /* Дата */
                  1,                           /* Глава */
                  NATCUR,                      /* Валюта */
                  ABS(ОтсрРазнНацВал),         /* Сумма  */
                  NULL,                        /* Документ */
                  INPCARRY,                    /* Result_Carry */
                  " 1",                        /* Kind_Oper */
                  "",                          /* Numb_Document */
                  Ground,
                  null,
                  NULL,
                  null,
                  NATCUR, NATCUR
                  ) 
              )
                msg = GetErrMsg();
                if(msg != "")
                  msgbox(msg);
                end;

                return 1;
            end;

          end;
        else //Наблюдаемые данные = нет
          if( not VS_SaveIncomeSum(VSINCOMETYPE_DEFDIF, bnr.rec.BCID, EnrolId, ДатаВыдачиВекселя, ОтсрРазнНацВал, NATCUR, ОтсрРазнВУ, ВалУч) )
            return Ошибка("Не удалось сохранить запись о отсроченной разнице в истории для ц/б " + GetVekselName(FD));
          end;
          if ((ОтсроченнаяРазница == 2) and (СчетДляУчетаФР == 706))
            ОтсрРазнНацВалКНачисл = РасчитатьСуммуОР( CalcDate, bnr, leg );
            if (ОтсрРазнНацВалКНачисл != $0)
              if (ОтсрРазнНацВалКНачисл > $0)
                КатегДеб = "-Эмиссия, СВ";
                КатегКредит = "+КОР_ПР_ЭПС, СВ";
                Ground = String("Отражение положительной отсроченной разницы по ц/б " + GetVekselName(FD));
              else
                КатегДеб = "-КОР_ПР_ЭПС, СВ";
                КатегКредит = "+Эмиссия, СВ";
                Ground = String("Отражение отрицательной отсроченной разницы по ц/б " + GetVekselName(FD));
              end;
              if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                    КатегДеб, КатегКредит, /* КатегДеб , КатегКредит*/
                    AccDate,                     /* Дата */
                    1,                           /* Глава */
                    NATCUR,                      /* Валюта */
                    ABS(ОтсрРазнНацВалКНачисл),  /* Сумма  */
                    NULL,                        /* Документ */
                    INPCARRY,                    /* Result_Carry */
                    " 1",                        /* Kind_Oper */
                    "",                          /* Numb_Document */
                    Ground,
                    null,
                    NULL,
                    null,
                    NATCUR, NATCUR
                    ) 
                )
                  msg = GetErrMsg();
                  if(msg != "")
                    msgbox(msg);
                  end;
     
                  return 1;
              end;
              ClearRecord(income);
              income.AutoKey        = 0;
              income.BCID           = bnr.rec.BCID;     // ID векселя/сертификата
              income.Currency       = NATCUR;  // Валюта
              income.OperDate       = CalcDate;  // Операционный день, когда выполнялось начисление
              income.Quality        = SET_CHAR;
              income.Enrolment_ID   = EnrolId;
              income.AccCurrency    = ВалУч;
              income.IncomeType     = VSINCOMETYPE_CHARGE;
              income.DefDif         = ОтсрРазнНацВалКНачисл;
              income.AccDefDif      = VS_Convert(ОтсрРазнНацВалКНачисл, CalcDate, NATCUR, ВалУч);
              if( InsertVSINCOME(income) )
                return Ошибка("Не удалось вставить запись в vsincome");
              end;
            end;
          end;
        end;

      end;

      var insCmd = RSDCommand( "INSERT INTO DBNRFRVAL_DBT (T_BCID, T_BEGDATE, T_CONTRACTID, T_FAIRVALUE, T_ACCOUNTED)"
                          +"            VALUES  (?, ?, ?, ?, ?)");

      insCmd.NullConversion = true;
      insCmd.addParam( "", RSDBP_IN, bnr.rec.BCID      );
      insCmd.addParam( "", RSDBP_IN, ДатаВыдачиВекселя );
      insCmd.addParam( "", RSDBP_IN, DealId );
      insCmd.addParam( "", RSDBP_IN, НоваяСС );
      insCmd.addParam( "", RSDBP_IN, UNSET_CHAR );
      insCmd.execute();

      ДнейВГоду=DaysInYearByBasis(leg.rec.Basis, ДатаВыдачиВекселя);
      СрокОбращения = VS_GetTermCircDate(bnr,leg);

      GetRegistryValue( "SECUR\\МСФО\\ЭПС ДЛЯ  ДО МЕНЬШЕ ГОДА", V_BOOL, НастройкаДОГод, err );
      if (err != 0)
        return Ошибка("Ошибка получения настройки \"ЭПС для ДО меньше года\"");
      end;
      GetRegistryValue( "SECUR\\МСФО\\ЭПС ДЛЯ ДО С НЕСУЩЕСТВ. ОТКЛ", V_BOOL, НастройкаДОНесущОткл, err );
      if (err != 0)
        return Ошибка("Ошибка получения настройки \"ЭПС для ДО с несущ. откл\"");
      end;
      
      if (leg.rec.PrincipalDiff == METHOD_UNDEF) // Не определён метод расчёта АС
        if (not СущественностьОтклонения(ДатаВыдачиВекселя, L_OBJTYPE_LEVELESSENTIAL_AC, KINDPORT_OWNB_OTHER_CMPR_INCOME, 
                                        DealId, NULL, null, DL_VSBANNER, bnr.rec.BCID, ОтклСуществ))
          return Ошибка("Ошибка расчёта отклонения АС");
        end;
        //тест на рыночность = да и (срок "по предъявлении" или обращение <= год) и настройка = нет
        if (((bnr.rec.BCTermFormula == VS_TERMF_ATSIGHT) or (СрокОбращения <= ДнейВГоду))
          and (НастройкаДОГод == false)) 
          leg.rec.PrincipalDiff = METHOD_LINE;
        elif (ОтклСуществ == false)
          leg.rec.PrincipalDiff = METHOD_LINE;
        else
          leg.rec.PrincipalDiff = METHOD_EPS;
        end;
      end;

      if (leg.rec.PrincipalDiff == METHOD_EPS)
        FairValue = VS_Convert( НоваяСС, CalcDate, NATCUR, leg.rec.PFI);
        ДатаПослРасчКорр = СВ_ПолучитьДатуПоследнегоРасчетаКорр(bnr.rec.BCID, CalcDate);
        СуммаДисконта = РассчитатьСуммуДисконта( CalcDate, bnr, leg, РассчитатьСуммуДисконта( ДатаПослРасчКорр, bnr, leg, 0 ));
        if (not РасчетПроцентовХранЗаПериод(leg.rec.id, ДатаПослРасчКорр, CalcDate, InterestIncomeAdd ))
          InterestIncomeAdd = $0;
        end;
        СуммаКорр = РасчетКорректировкиПроцентаПоЭПССВ(DealId,bnr.rec.BCID,CalcDate, InterestIncomeAdd, 0, СуммаДисконта, FairValue, ЭПС);
        СуммаКоррКНачисл = VS_Convert( СуммаКорр, CalcDate, leg.rec.PFI, NATCUR );
        СуммКоррВУ = VS_Convert( СуммаКорр, CalcDate, leg.rec.PFI, ВалУч);
        if (СуммаКоррКНачисл != $0)
          if (СуммаКоррКНачисл < $0)
            КатегДеб = "+Корр, Свексель";
            КатегКредит = ПолучитьКатегориюФР("+КОР_ПР_ЭПС, СВ");
            Ground = String("Отражение отрицательной корректировки по ц/б " + GetVekselName(FD));
          else 
            КатегДеб = ПолучитьКатегориюФР("-КОР_ПР_ЭПС, СВ");
            КатегКредит = "-Корр, Свексель";
            Ground = String("Отражение положительной корректировки по ц/б " + GetVekselName(FD));
          end;
          if(not МСФО9_ПроводкаПоКатегориямУчета( FD, 
                КатегДеб, КатегКредит, /* КатегДеб , КатегКредит*/
                AccDate,                     /* Дата */
                1,                           /* Глава */
                NATCUR,                      /* Валюта */
                ABS(СуммаКоррКНачисл),       /* Сумма  */
                NULL,                        /* Документ */
                INPCARRY,                    /* Result_Carry */
                " 1",                        /* Kind_Oper */
                "",                          /* Numb_Document */
                Ground,
                null,
                NULL,
                null,
                NATCUR, NATCUR
                ) 
            )
              msg = GetErrMsg();
              if(msg != "")
                msgbox(msg);
              end;

              return 1;
          end;
          if( not VS_SaveIncomeSum(VSINCOMETYPE_EPRPERC, bnr.rec.BCID, EnrolId, ДатаВыдачиВекселя, СуммаКорр, leg.rec.PFI, СуммКоррВУ, ВалУч) )
            return false;
          end;
        end;
      end;

      if(ВексельПроцентный(leg))
        if(not НайтиСчетПроцВекселя(prccontract, bnr.rec.BCID, null, VS_FINDPC_ACTUAL))
          return Ошибка("Не найден счет процентного векселя");
        elif(not ПолучитьСуммуКНачисл_В(bnr, leg, prccontract.rec.EndDate, @СуммаПроцентов, @НачисПроц))
          return Ошибка("Не удалось получить сумму к начислению");
        else
          СуммаПроцентов = СуммаПроцентов + НачисПроц;
          СуммаПроцентовВУ = VS_Convert(СуммаПроцентов, CalcDate, leg.rec.PFI, fd.ОпределитьВалютуУчета());
          if( not VS_SaveIncomeSum(VSINCOMETYPE_PERCENT, bnr.rec.BCID, 0, date(CalcDate), СуммаПроцентовВУ, leg.rec.PFI, СуммаПроцентовВУ, fd.ОпределитьВалютуУчета()) )
            return Ошибка("Не удалось сохранить запись о сумме процентов в истории для ц/б сер. " + string(bnr.rec.BCSeries) + " N " + trim(bnr.rec.BCNumber));
          end; 
        end;
      end;

      if (not ОбновитьПараметрыВекселя(bnr,leg))
        return Ошибка("Ошибка обновления параметров векселя");
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();
  end;

  return err;
END;

macro StartTrans(part:string, msg:string)
  msgbox(part+msg);
  RslDefCon.BeginTrans();
  currPart = part;
end;

macro EndTrans(err)
  if(err)
    RslDefCon.RollbackTrans();
    msgbox("При выполнении возникли ошибки!");
  else
    RslDefCon.CommitTrans();
    //RslDefCon.RollbackTrans();
    msgbox("Выполнено успешно");
  end;

  msgbox("");
end;


macro StartExec()
  var err = 0;

  ReplaceMacro( "msgbox", "cnv_msgbox" );

  msgbox("Начало конвертации");

  CorrectFlag();

  if((СчетДляУчетаФР != 706) and (СчетДляУчетаФР != 10801) and (СчетДляУчетаФР != 10901))
    msgbox("Не реализована обработка значения "+СчетДляУчетаФР+" для параметра \"СчетДляУчетаФР\"");
    err = 1;
  end;

  if(not err)
    if((ОтсроченнаяРазница != 1) and (ОтсроченнаяРазница != 2))
      msgbox("Не реализована обработка значения "+ОтсроченнаяРазница+" для параметра \"ОтсроченнаяРазница\"");
      err = 1;
    end;
  end;

  if(not err)

    msgbox("1. Обработка остатков по счетам");
   
    StartTrans("  1.1. ", "Обработка остатков начисленного дисконта");
    err = ВыполнитьПроводкиДисконтаКонв();
    EndTrans(err); 

  end; 

  if(not err)

    msgbox("2. Конвертация состояния залога на статус залога");
   
    StartTrans("  2.1. ", "Конвертация состояния залога на статус залога");
    err = ПереводСтатусаЗалога();
    EndTrans(err); 

  end; 


  if(not err)
    msgbox("3. Первоначальное признание");
     
    StartTrans("  3.1. ", "Обработка векселей процедурой первоначального признания");
    err = ОбработатьВекселяПоПервоначальномуПризнанию();
    EndTrans(err);  
  end;

  if(not err)
    msgbox("Конвертация успешно завершена");
  else
    msgbox("#################################################################################");
    msgbox("ВНИМАНИЕ!!!");
    msgbox("При конвертации возникли ошибки. \nПосле устранения причин ошибок повторную конвертацию следует выполнять, начиная с проблемного блока. \nВсе предыдущие успешно выполненные блоки следует закомментировать в макросе.");
    msgbox("#################################################################################");
  end;

  ReplaceMacro( "msgbox", NULL );

OnError( err );
  var j;
  var err_text = "", delim = "";
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  

  if (IsEqClass ("TrslError", err))
    err_text=err.Message;
    if(IsEqClass ("TDbError", err.err))
      err_text=err_text+":|"+err.err.Stat+"-"+err.err.Oper+"-"+err.err.Table+"-"+err.err.Message;
    elif (isEqClass("TRsdError", err.err))

      j = 0;
      while(j < err.err.environment.ErrorCount)
         err_text = err_text + delim  + err.err.environment.error(j).descr;
         delim = "\n";
         j = j + 1;
      end;
    end;
  end;

  if(err_text == "")
    err_text=err.module+ " "+err.line+" "+err.message;
  end;

  msgbox(err_text);

  return 1;
end;

ReplaceMacro("msgbox", "cnv_msgbox");

StartExec();

ReplaceMacro("msgbox", NULL );