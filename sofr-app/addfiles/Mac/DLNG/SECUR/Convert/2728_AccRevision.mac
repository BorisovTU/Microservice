/*******************************************************************************
 DESCRIPTION  :   Обновление 2027->2028 (302-П)
                  Процедура ревизии счетов
 PROGRAMMED BY:   Леонов И. Е.
 CREATION DATE:   22.11.07
*******************************************************************************/
import BankInter, "DlRepForm_h.mac", "globals.mac", "cb_sql.mac", "secinter.mac", "spserv.mac", "sp_categ.mac";

/*Номера полей в форме */
CONST PNFLD_NTGREG_DATE         = 0,
      PNFLD_NTGREG_FLAG_1       = 1,
      PNFLD_NTGREG_FLAG_11      = 2,
      PNFLD_NTGREG_FLAG_2       = 3,
      PNFLD_NTGREG_FLAG_21      = 4,
      PNFLD_NTGREG_FLAG_ProfAcc = 5,
      PNFLD_NTGREG_FLAG_REPORT  = 6;

PRIVATE VAR RecProfAcc = TRecHandler( "account" );

PRIVATE MACRO FormError( StrErr:STRING, err, command:RSDCommand ):STRING
  VAR i = 0;
  StrErr = "!!! " + StrErr + "\nСтрока: " + string(err.line) + "  " + err.message + "\n";
  if( command != NULL )
     while( i < command.connection.environment.ErrorCount )
         StrErr = StrErr + command.connection.environment.Error(i).Descr + "\n";
         i = i + 1;
     end;
  end;
  return StrErr;
OnError
  return StrErr;
end;

PRIVATE MACRO FldProc_Flag1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR RetVal = DL_FldProc_CheckBox( pThis, Cmd, Key, @FldShowValue, @FldRealValue );

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( FldRealValue == "X" ) 
        EnableFields( pThis.Data(), PNFLD_NTGREG_FLAG_11 );
     else 
        DisableFields( pThis.Data(), PNFLD_NTGREG_FLAG_11 );
        pThis.Data.rec.Flag11 = "";
     end;
  end;
  return RetVal;
END;

PRIVATE MACRO FldProc_Flag2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR RetVal = DL_FldProc_CheckBox( pThis, Cmd, Key, @FldShowValue, @FldRealValue );

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( FldRealValue == "X" ) 
        EnableFields( pThis.Data(), PNFLD_NTGREG_FLAG_21 );
     else 
        DisableFields( pThis.Data(), PNFLD_NTGREG_FLAG_21 );
        pThis.Data.rec.Flag21 = "";
     end;
  end;
  return RetVal;
END;

PRIVATE MACRO CheckProfAcc( Acc:STRING ):BOOL
  Acc = trim(Acc);
  if( Acc == "" )
     return false;
  end;

  if( GetAccount( 1, NATCUR, Acc, RecProfAcc, true ) == false )
     return false;
  end;
  return true;
END;

PRIVATE MACRO FldProc_ProfAcc( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     /*Если в поле есть ручной ввод, то заполнить FldRealValue*/
     FldRealValue = FldShowValue;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     if( ListAccount( RecProfAcc, 1, NATCUR, FldRealValue) == true )
        FldRealValue = RecProfAcc.rec.Account;
        FldShowValue = FldRealValue;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO GetDefProfAcc():STRING
  /* по умолчанию ищется открытый лицевой счет с б/с 70302*/
  VAR RS,
      cmd = RSDCommand( "SELECT t_Account "+
                        "  FROM DACCOUNT_DBT "+
                        " WHERE     t_Chapter = ? "+
                        "       AND t_Code_Currency = ? "+
                        "       AND t_Balance = ? "+
                        "       AND ROWNUM = 1 "
                      );
  cmd.addParam( "", RSDBP_IN, 1 );
  cmd.addParam( "", RSDBP_IN, NATCUR );
  cmd.addParam( "", RSDBP_IN, "70302" );
  cmd.execute();

  RS = TRsbDataSet(cmd);
  if( RS.MoveNext() )
     return SQL_ConvTypeStr(RS.Account);
  end;
  return "";
END;

PRIVATE CLASS (DL_CPanel) SP_DealCorrect_Form()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_NTGREG_DATE,        DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate, GetDateAfterWorkDays( DATE(1,1,2008), 0 ) );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_1,      DL_PNFLD_CHECKBOX,  @FldProc_Flag1,  "X" );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_11,     DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,  "" );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_2,      DL_PNFLD_CHECKBOX,  @FldProc_Flag2,  "X" );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_21,     DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,  "" );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_ProfAcc,DL_PNFLD_CHECKBOX,  @FldProc_ProfAcc,   GetDefProfAcc() );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_REPORT, DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,  "X" );
  END;

  PRIVATE MACRO Check():BOOL
     if( CheckProfAcc( Data.rec.ProfAcc ) == false )
        MsgBox( "Неверно задано значение поля \"Счет учета прибыли предшествующих лет\"" );
        return false;
     end;
     return true;
  END;

  initDL_CPanel( "sp_conv.lbr", "previs" ); 
END;

PRIVATE VAR ReportHeader =
"\n               Процедура ревизии счетов\n" +
"Дата: ##########\n" +
"Формировать проводки по переносу остатков вложений : #\n" +
"    Выполнять проводки: #\n" +
"Формировать проводки по переносу остатков по счетам по сделкам РЕПО: #\n"
"    Выполнять проводки: #\n"
"Счет учета прибыли предшествующих лет: #\n"
;

PRIVATE VAR TableHeader =                                                                                                                                                                                                                                                                                                                                                                                                                                                                                         
"┌─────────────────────────────────────────────────────────────────────────────────────────────────────────────────┐\n" +
"│                                       Отчет о выполнении                                                        │\n" +
"├─────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤";

PRIVATE CLASS (DL_CReportTemplate) SP_DealCorrect

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_STD;
  END;

  PRIVATE MACRO BeginReport()

     PrintFormatString( ReportHeader,
                        "N1_H0_Date",    FormData.OperDate,  
                        "N1_H0_Flag1:l",  IIF( FormData.Flag1  == "X", "Да", "Нет"),  
                        "N1_H0_Flag11:l", IIF( FormData.Flag11 == "X", "Да", "Нет"),  
                        "N1_H0_Flag2:l",  IIF( FormData.Flag2  == "X", "Да", "Нет"),  
                        "N1_H0_Flag21:l", IIF( FormData.Flag21 == "X", "Да", "Нет"),  
                        "N1_H0_ProfAcc:l",FormData.ProfAcc
                      );

     RegisterTable( "N1_MainTable", TableHeader, "N1_Action" );
  END;

  PRIVATE MACRO EndReport()
     EndTable();
  END;

  PRIVATE MACRO Message( Mes:STRING)
     if( FormData.CreateReport == "X" )
           PrintTableLine( "N1_Action", Mes,   null );
     end;
  END;

  PRIVATE MACRO RunCarry( Department:INTEGER, Chapter:INTEGER, SumPayer:MONEY, SumReceiver:MONEY, AccountPayer:STRING, AccountReceiver:STRING, FIIDPayer:INTEGER, FIIDReceiver:INTEGER, Ground:STRING, ObjectCode:STRING, Execute:BOOL, SPOD:BOOL )
    VAR tr, OldDialogFlag, SumTmp, SumFiCode;
    if( ( (AccountPayer == NULL) OR 
          (AccountPayer == "")
        ) OR 
        ( (AccountReceiver == NULL) OR 
          (AccountReceiver == "") 
        )
      )
       return true;
    end;

    if( SumReceiver != NULL ) 
       SumReceiver = Round( ABS(SumReceiver), 2 );
    end; 

    if( SumPayer != NULL ) 
       SumPayer = Round( ABS(SumPayer), 2 );
    end; 

    if( ( (SumReceiver == NULL) OR 
          (SumReceiver == 0)
        ) AND 
        ( (SumPayer == NULL) OR 
          (SumPayer == 0) 
        )
      )
       return true;
    end;

    tr = RsbAccTransaction();

    tr.Chapter         = Chapter;
    tr.Date_Carry      = tr.Date_Document = FormData.OperDate;
    tr.Numb_Document   = ObjectCode;
    tr.ResultCarry     = INPCARRY;
    tr.Department      = Department;
    tr.Ground          = Ground;

    tr.FIIDPayer       = FIIDPayer;
    tr.FIIDReceiver    = FIIDReceiver;
    tr.SumPayer        = SumPayer;
    tr.SumReceiver     = SumReceiver;
    tr.AccountPayer    = AccountPayer;
    tr.AccountReceiver = AccountReceiver;
    if( SPOD == true )
      tr.TypeDocument  = "З"; /*СПОД*/
    end;

    if( Execute != true )
       tr.Status_After    = ACCTRN_STATUS_POST;     
    end;

    if( (SumPayer == 0) OR (SumPayer == NULL) )
       SumTmp = SumReceiver;
       SumFiCode = ПолучитьКодФинИн(FiidReceiver);
    else
       SumTmp = SumPayer;
       SumFiCode = ПолучитьКодФинИн(FiidPayer);
    end;

    OldDialogFlag = SetDialogFlag(0);
    if( not tr.Carry() )
       Message("!!! Ошибка при выполнении проводки \"" + Ground + "\"  "+ AccountPayer + "  " + AccountReceiver + "  " + SumTmp + " (" + SumFiCode + ")\n"+ GetErrMsg() );
       InitError();
       SetDialogFlag( OldDialogFlag );
       return false;
    end;
    SetDialogFlag( OldDialogFlag );

    Message( "Проводка \"" +Ground + "\" "+ AccountPayer + "  " + AccountReceiver + "  " + SumTmp + " (" + SumFiCode + ")" );
    return true;
  END;

  PRIVATE MACRO ExistNOSS( FIID:INTEGER, DealDate:DATE ):BOOL
    VAR NumInList = "", ValidFromDate = DATE(0,0,0);

    return (    (GetMainObjAttr( null, OBJTYPE_AVOIRISS, L_Z( FIID, 10 ), 27/*Возможность надежного расчета TCC*/, null, null, NumInList, DealDate, ValidFromDate) == true)
            AND (ValidFromDate <= DealDate)
            AND (NumInList == "1")
           );
  END;

  PRIVATE MACRO OpenAccount( FD, CatCode:STRING, FIRole:INTEGER, OpenDate:DATE, AccBuf:TRecHandler ):BOOL
     if( FD.IsExistAccount( CatCode, NULL, true, FIRole, AccBuf, NULL, OpenDate ) == false )
        if( not FD.OpenAccount( CatCode, null, true, FIRole, AccBuf, OpenDate) )
           Message( "!!! Ошибка при открытии счета по КУ \""+CatCode+"\"\n" + GetErrMsg() );
           return false;
        else
           Message( "Открыт счет по КУ \""+CatCode+"\" - " + AccBuf.rec.Account );
        end;
     end;
     return true;
  END;

  PRIVATE VAR PrevFIID = -1, FD, FIRole;
  PRIVATE VAR AccOurPortf = TRecHandler("account");
  PRIVATE MACRO CreateCarryAvrRest( RS )
     VAR Д, Outlay;

     if( PrevFIID != RS.FIID )
        Message( "ц/б " + ПолучитьКодФинИн(SQL_ConvTypeInteger(RS.FIID)) );
        if( ExistNOSS( SQL_ConvTypeInteger(RS.FIID), SQL_ConvTypeDate(FormData.OperDate) ) == true )
           FIRole = FIROLE_BA_TP;
        else
           FIRole = FIROLE_BA_PPR;
        end;
        PrevFIID = RS.FIID;

        FD = SPFirstDocFI( DLDOC_ISSUE, SQL_ConvTypeInteger(RS.FIID), SQL_ConvTypeInteger(RS.FIID) );
        if( OpenAccount( FD, "Наш портфель ц/б", FIRole, FormData.OperDate, AccOurPortf ) == false )
           return;
        end;
     END;

     if( AccOurPortf.rec.Account != RS.BALACC )
        RunCarry( RS.Department, 
                  1,
                  RS.BALANCE, NULL, 
                  AccOurPortf.rec.Account, RS.BALACC, 
                  AccOurPortf.rec.Code_Currency, NULL, 
                  "Перенос стоимости ц/б", 
                  RS.FI_Code,
                  FormData.Flag11 == "X",  
                  false );
     end;

     RunCarry( RS.Department, 
               1,
               NULL, Outlay,
               AccOurPortf.rec.Account, RS.OUTACC, 
               NULL, NATCUR, 
               "Перенос затрат на покупку", 
               RS.FI_Code,
               FormData.Flag11 == "X",  
               false );

     RunCarry( RS.Department, 
               1,
               RS.NKD, NULL, 
               AccOurPortf.rec.Account, RS.NKDACC, 
               AccOurPortf.rec.Code_Currency, NULL, 
               "Перенос суммы уплаченного НКД", 
               RS.FI_Code,
               FormData.Flag11 == "X",  
               false );

     Д = SQL_ConvTypeSum(RS.COST) - SQL_ConvTypeSum(RS.BALANCE);
     if( Д < 0 )
        RunCarry( RS.Department, 
                  1,
                  NULL, -Д,
                  FormData.ProfAcc, AccOurPortf.rec.Account, 
                  NATCUR, AccOurPortf.rec.Code_Currency, 
                  "Восстановление стоимости покупки на счете вложений в ц/б", 
                  RS.FI_Code,
                  FormData.Flag11 == "X",  
                  true );
     elif( Д > 0 )
        RunCarry( RS.Department, 
                  1,
                  Д, NULL,
                  AccOurPortf.rec.Account, FormData.ProfAcc,
                  AccOurPortf.rec.Code_Currency, NATCUR,
                  "Восстановление стоимости покупки на счете вложений в ц/б", 
                  RS.FI_Code,
                  FormData.Flag11 == "X",  
                  true );
     end;
  ONERROR(err)
    Message( FormError("!!! Ошибка при выполнении проводок (CreateCarryAvrRest)", err) );
    return false;
  END;

  PRIVATE MACRO CreateCarryREPO( RS )
     VAR FD_1, FD_2;
     VAR TotalCostR = $0, TotalCostN = $0;
     VAR AccVnebal, AccCred, AccPlusOD, AccMinusOD, AccPVO, AccBPP, AccPlusR, AccMinusR, Summa = $0;

     /*под прямым  Репо понимается Репо продажа и продажа с обр. выкупом, 
      под обратным Репо -  Репо покупка и покупка с обр. продажей*/
     if( (RS.ORCB == "X") AND (RS.ClientID <= 0 ) )  /*для сделок ОРЦБ собств*/
        Message( "Сделка \"" + RS.DealCode + "\" (ОРЦБ)");

        FD_1 = SPFirstDoc( LEG_KIND_DL_TICK, RS.DealID );
        FD_2 = SPFirstDoc( LEG_KIND_DL_TICK_BACK, RS.DealID );

        if( IsBUY(FD_1.Group) ) /*Обратное Репо ОРЦБ:*/
           RunCarry( {OperDprt}, 
                     1,
                     RS.OUTLAY, NULL,
                     FormData.ProfAcc, RS.OUTACC,
                     NATCUR, NULL,
                     "Списание затрат, ранее учтенных по сделке", 
                     RS.DealCode,
                     FormData.Flag21 == "X",  
                     true );

           if( (FD_1.pmwrtsum.rec.State == PM_WRTSUM_NOTFORM) OR (FD_1.pmwrtsum.rec.State == PM_WRTSUM_SALE_BPP))/*1 часть не исполнена*/

              if( RS.OVR1 < 0 )
                 RunCarry( {OperDprt}, 
                           4,
                           -RS.OVR1, NULL,
                           RS.PFDACC1, RS.FMACC1,
                           FD_1.FaceFIID, NULL,
                           "Учет отрицательной переоценки по ц/б на счете требования", 
                           RS.DealCode,
                           FormData.Flag21 == "X",  
                           false );
              else
                 RunCarry( {OperDprt}, 
                           4,
                           RS.OVR1, NULL,
                           RS.PFDACC1, RS.FMACC1,
                           FD_1.FaceFIID, NULL,
                           "Учет положительной переоценки по ц/б на счете требования", 
                           RS.DealCode,
                           FormData.Flag21 == "X",  
                           false );
              end;

              RunCarry( {OperDprt}, 
                        4,
                        RS.MFDSUM1, RS.PFDSUM1 - RS.OVR1,
                        RS.MFDACC1, RS.PFDACC1,
                        FD_1.GetPayFIID(), FD_1.FaceFIID,
                        "Списание 1 ч. с внебаланса", 
                        RS.DealCode,
                        FormData.Flag21 == "X",  
                        false );

              AccVnebal = TRecHandler( "account" );
              AccCred   = TRecHandler( "account" );

              if( OpenAccount( FD_1, "ВнебалСчетКорресп", NULL, FormData.OperDate, AccVnebal ) == false )
                 return;
              end;
              if( OpenAccount( FD_1, "-Кредит, неисп.", NULL, FormData.OperDate, AccCred ) == false )
                 return;
              end;

              if( SmartConvertSum( TotalCostR, FD_1.dl_leg.rec.TotalCost, FD_1.tick.rec.DealDate, FD_1.dl_leg.rec.CFI, FD_1.GetPayFIID(), false ) == false)
                 Message( "!!! Ошибка при конвертации суммы сделки из " + ПолучитьКодФинИн(FD_1.dl_leg.rec.CFI) + " в " + ПолучитьКодФинИн(FD_1.GetPayFIID()) + " на " + date_as_string(1, FD_1.tick.rec.DealDate) );
                 return;
              end;

              if( SmartConvertSum( TotalCostN, TotalCostR, FD_1.tick.rec.DealDate, FD_1.GetPayFIID(), NATCUR, false ) == false)
                 Message( "!!! Ошибка при конвертации суммы сделки из " + ПолучитьКодФинИн(FD_1.GetPayFIID()) + " в " + ПолучитьКодФинИн(NATCUR) + " на " + date_as_string(1, FD_1.tick.rec.DealDate) );
                 return;
              end;

              RunCarry( {OperDprt}, 
                        3,
                        TotalCostN, TotalCostR,
                        AccVnebal.rec.Account, AccCred.rec.Account,
                        NATCUR, FD_1.GetPayFIID(), 
                        "Учет суммы оплаты на внебалансе", 
                        RS.DealCode,
                        FormData.Flag21 == "X",  
                        false );
           elif( (FD_2.pmwrtsum.rec.State == PM_WRTSUM_NOTFORM) OR (FD_2.pmwrtsum.rec.State == PM_WRTSUM_SALE_BPP) ) /*1 часть исполнена 2-я часть не исполнена*/
              if( RS.OVR2 < 0 )
                 RunCarry( {OperDprt}, 
                           4,
                           -RS.OVR2, NULL,
                           RS.PFDACC2, RS.FMACC2,
                           FD_2.FaceFIID, NULL,
                           "Учет отрицательной переоценки по ц/б на счете обязательств", 
                           RS.DealCode,
                           FormData.Flag21 == "X",  
                           false );
              else
                 RunCarry( {OperDprt}, 
                           4,
                           RS.OVR2, NULL,
                           RS.PFDACC2, RS.FMACC2,
                           FD_2.FaceFIID, NULL,
                           "Учет положительной переоценки по ц/б на счете обязательств", 
                           RS.DealCode,
                           FormData.Flag21 == "X",  
                           false );
              end;

              RunCarry( {OperDprt}, 
                        4,
                        null, RS.PFDSUM2,
                        RS.MFDACC2, RS.PFDACC2,
                        FD_2.FaceFIID, FD_1.GetPayFIID(),
                        "Списание 2 ч. с внебаланса", 
                        RS.DealCode,
                        FormData.Flag21 == "X",  
                        false );

              AccVnebal  = TRecHandler( "account" );
              AccPlusOD  = TRecHandler( "account" );
              AccMinusOD = TRecHandler( "account" );
              AccPVO     = TRecHandler( "account" );
              if( (OpenAccount( FD_2, "+ОД", NULL, FormData.OperDate, AccPlusOD ) == false)  OR
                  (OpenAccount( FD_2, "-ОД", NULL, FormData.OperDate, AccMinusOD ) == false) OR
                  (OpenAccount( FD_2, "Ц/б, ПВО", NULL, FormData.OperDate, AccPVO ) == false) OR
                  (OpenAccount( FD_2, "ВнебалСчетКорресп", NULL, FormData.OperDate, AccVnebal ) == false)
                )
                 return;
              end;

              if( SmartConvertSum( Summa, SQL_ConvTypeSum(RS.BALANCE), FD_1.pmwrtsum.rec.Date, FD_2.FaceFIID, FD_2.GetPayFIID, false ) == false)
                 Message( "!!! Ошибка при конвертации суммы сделки из " + ПолучитьКодФинИн(FD_2.FaceFIID) + " в " + ПолучитьКодФинИн(FD_2.GetPayFIID) + " на " + date_as_string(1, FD_1.pmwrtsum.rec.Date) );
                 return;
              end;

              RunCarry( {OperDprt}, 
                        1,
                        Summa , RS.BALANCE ,
                        AccPlusOD.rec.Account, RS.BALACC,
                        FD_2.GetPayFIID(), FD_2.FaceFIID,
                        "Учет сделки в балансе", 
                        RS.DealCode,
                        FormData.Flag21 == "X",  
                        false );

              if( SmartConvertSum( Summa, SQL_ConvTypeSum(RS.NKD), FD_1.pmwrtsum.rec.Date, FD_2.FaceFIID, FD_2.GetPayFIID, false ) == false)
                 Message( "!!! Ошибка при конвертации суммы НКД из " + ПолучитьКодФинИн(FD_2.FaceFIID) + " в " + ПолучитьКодФинИн(FD_2.GetPayFIID) + " на " + date_as_string(1, FD_1.pmwrtsum.rec.Date) );
                 return;
              end;

              RunCarry( {OperDprt}, 
                        1,
                        Summa , RS.NKD ,
                        AccPlusOD.rec.Account, RS.NKDACC,
                        FD_2.GetPayFIID(), FD_2.FaceFIID,
                        "Учет сделки в балансе", 
                        RS.DealCode,
                        FormData.Flag21 == "X",  
                        false );

              if( SmartConvertSum( Summa, SQL_ConvTypeSum(RS.NOTSALEDCOST), FD_1.pmwrtsum.rec.Date, FD_2.FaceFIID, NATCUR, false ) == false)
                 Message( "!!! Ошибка при конвертации суммы из " + ПолучитьКодФинИн(FD_2.FaceFIID) + " в " + ПолучитьКодФинИн(NATCUR) + " на " + date_as_string(1, FD_1.pmwrtsum.rec.Date) );
                 return;
              end;

              RunCarry( {OperDprt}, 
                        3,
                        Summa , RS.NOTSALEDCOST ,
                        AccVnebal.rec.Account, AccPVO.rec.Account,
                        NATCUR, FD_2.FaceFIID,
                        "Учет сделки в балансе", 
                        RS.DealCode,
                        FormData.Flag21 == "X",  
                        false );

              if( SmartConvertSum( Summa, SQL_ConvTypeSum(RS.SALEDCOST), FD_1.pmwrtsum.rec.Date, FD_2.FaceFIID, FD_2.GetPayFIID, false ) == false)
                 Message( "!!! Ошибка при конвертации суммы из " + ПолучитьКодФинИн(FD_2.FaceFIID) + " в " + ПолучитьКодФинИн(FD_2.GetPayFIID) + " на " + date_as_string(1, FD_1.pmwrtsum.rec.Date) );
                 return;
              end;

              RunCarry( {OperDprt}, 
                        1,
                        Summa , RS.SALEDCOST ,
                        AccPlusOD.rec.Account, AccMinusOD.rec.Account,
                        FD_2.GetPayFIID(), FD_2.FaceFIID,
                        "Учет сделки в балансе", 
                        RS.DealCode,
                        FormData.Flag21 == "X",  
                        false );
           end; 
        else /*   Прямое Репо ОРЦБ*/
           RunCarry( {OperDprt}, 
                     1,
                     RS.OUTLAY, NULL,
                     FormData.ProfAcc, RS.OUTACC,
                     NATCUR, NULL,
                     "Списание затрат, ранее учтенных по сделке", 
                     RS.DealCode,
                     FormData.Flag21 == "X",  
                     true );

           if( (FD_1.pmwrtsum.rec.State == PM_WRTSUM_NOTFORM) OR (FD_1.pmwrtsum.rec.State == PM_WRTSUM_SALE_BPP) )/*1 часть не исполнена*/

              if( RS.OVR1 < 0 )
                 RunCarry( {OperDprt}, 
                           4,
                           -RS.OVR1, NULL,
                           RS.FMACC1 , RS.MFDACC1,
                           FD_1.FaceFIID, NULL,
                           "Учет отрицательной переоценки по ц/б на  счете обязательств", 
                           RS.DealCode,
                           FormData.Flag21 == "X",  
                           false );
              else
                 RunCarry( {OperDprt}, 
                           4,
                           RS.OVR1, NULL,
                           RS.MFDACC1, RS.FMACC1,
                           FD_1.FaceFIID, NULL,
                           "Учет положительной переоценки по ц/б на счете обязательств", 
                           RS.DealCode,
                           FormData.Flag21 == "X",  
                           false );
              end;

              RunCarry( {OperDprt}, 
                        4,
                        RS.MFDSUM1 - RS.OVR1, RS.PFDSUM1,
                        RS.MFDACC1, RS.PFDACC1 ,
                        FD_1.FaceFIID, FD_1.GetPayFIID(),
                        "Списание 1 ч. с внебаланса", 
                        RS.DealCode,
                        FormData.Flag21 == "X",  
                        false );

              AccVnebal = TRecHandler( "account" );
              AccCred   = TRecHandler( "account" );

              if( OpenAccount( FD_1, "ВнебалСчетКорресп", NULL, FormData.OperDate, AccVnebal ) == false )
                 return;
              end;
              if( OpenAccount( FD_1, "+Кредит, неисп.", NULL, FormData.OperDate, AccCred ) == false )
                 return;
              end;

              if( SmartConvertSum( TotalCostR, FD_1.dl_leg.rec.TotalCost, FD_1.tick.rec.DealDate, FD_1.dl_leg.rec.CFI, FD_1.GetPayFIID(), false ) == false)
                 Message( "!!! Ошибка при конвертации суммы сделки из " + ПолучитьКодФинИн(FD_1.dl_leg.rec.CFI) + " в " + ПолучитьКодФинИн(FD_1.GetPayFIID()) + " на " + date_as_string(1, FD_1.tick.rec.DealDate) );
                 return;
              end;

              if( SmartConvertSum( TotalCostN, TotalCostR, FD_1.tick.rec.DealDate, FD_1.GetPayFIID(), NATCUR, false ) == false)
                 Message( "!!! Ошибка при конвертации суммы сделки из " + ПолучитьКодФинИн(FD_1.GetPayFIID()) + " в " + ПолучитьКодФинИн(NATCUR) + " на " + date_as_string(1, FD_1.tick.rec.DealDate) );
                 return;
              end;

              RunCarry( {OperDprt}, 
                        3,
                        TotalCostR, TotalCostN,
                        AccCred.rec.Account, AccVnebal.rec.Account,
                        FD_1.GetPayFIID(), NATCUR,
                        "Учет суммы оплаты на внебалансе", 
                        RS.DealCode,
                        FormData.Flag21 == "X",  
                        false );
           elif( (FD_2.pmwrtsum.rec.State == PM_WRTSUM_NOTFORM) OR (FD_2.pmwrtsum.rec.State == PM_WRTSUM_SALE_BPP) ) /*1 часть исполнена 2-я часть не исполнена*/
              if( RS.OVR2 < 0 )
                 RunCarry( {OperDprt}, 
                           4,
                           -RS.OVR2, NULL,
                           RS.PFDACC2, RS.FMACC2,
                           FD_2.FaceFIID, NULL,
                           "Учет отрицательной переоценки по ц/б на счете требования", 
                           RS.DealCode,
                           FormData.Flag21 == "X",  
                           false );
              else
                 RunCarry( {OperDprt}, 
                           4,
                           RS.OVR2, NULL,
                           RS.FMACC2, RS.PFDACC2,
                           FD_2.FaceFIID, NULL,
                           "Учет положительной переоценки по ц/б на счете требования", 
                           RS.DealCode,
                           FormData.Flag21 == "X",  
                           false );
              end;

              RunCarry( {OperDprt}, 
                        4,
                        NULL, RS.PFDSUM2, 
                        RS.MFDACC2, RS.PFDACC2 ,
                        FD_2.GetPayFIID(), FD_1.FaceFIID,
                        "Списание 2 ч. с внебаланса", 
                        RS.DealCode,
                        FormData.Flag21 == "X",  
                        false );

              AccPlusR  = TRecHandler( "account" );
              AccMinusR = TRecHandler( "account" );
              AccMinusOD= TRecHandler( "account" );
              AccBPP    = TRecHandler( "account" );
              if( (OpenAccount( FD_2, "+Расчеты,ХО", NULL, FormData.OperDate, AccPlusR ) == false)  OR
                  (OpenAccount( FD_2, "-Расчеты,ХО", NULL, FormData.OperDate, AccMinusR ) == false) OR
                  (OpenAccount( FD_2, "-ОД", NULL, FormData.OperDate, AccMinusOD ) == false) OR
                  (OpenAccount( FD_2, "Ц/б, БПП", NULL, FormData.OperDate, AccBPP ) == false) 
                )
                 return;
              end;

              if( SmartConvertSum( TotalCostR, FD_1.dl_leg.rec.TotalCost, FD_1.tick.rec.DealDate, FD_1.dl_leg.rec.CFI, FD_1.GetPayFIID(), false ) == false)
                 Message( "!!! Ошибка при конвертации суммы сделки из " + ПолучитьКодФинИн(FD_1.dl_leg.rec.CFI) + " в " + ПолучитьКодФинИн(FD_1.GetPayFIID()) + " на " + date_as_string(1, FD_1.tick.rec.DealDate) );
                 return;
              end;

              if( SmartConvertSum( TotalCostN, TotalCostR, FD_1.tick.rec.DealDate, FD_1.GetPayFIID(), FD_1.FaceFIID, false ) == false)
                 Message( "!!! Ошибка при конвертации суммы сделки из " + ПолучитьКодФинИн(FD_1.GetPayFIID()) + " в " + ПолучитьКодФинИн(FD_1.FaceFIID) + " на " + date_as_string(1, FD_1.tick.rec.DealDate) );
                 return;
              end;

              RunCarry( {OperDprt}, 
                        1,
                        TotalCostN, TotalCostR,
                        AccBPP.rec.Account, AccMinusOD.rec.Account,
                        FD_1.FaceFIID, FD_1.GetPayFIID(),
                        "Учет обязательств по возврату ден. средств ", 
                        RS.DealCode,
                        FormData.Flag21 == "X",  
                        false );

              if( RS.FINRES > 0 )
                 RunCarry( {OperDprt}, 
                           1,
                           RS.FINRES, NULL,
                           AccPlusR.rec.Account, FormData.ProfAcc,
                           NATCUR, NULL,
                           "Списание фин. результата 1 части с прибыли прошлого года", 
                           RS.DealCode,
                           FormData.Flag21 == "X",  
                           true );
              else
                 RunCarry( {OperDprt}, 
                           1,
                           -RS.FINRES, NULL,
                           FormData.ProfAcc, AccMinusR.rec.Account,
                           NATCUR, NULL,
                           "Списание фин. результата 1 части с прибыли прошлого года", 
                           RS.DealCode,
                           FormData.Flag21 == "X",  
                           true );
              end;


              if( SmartConvertSum( Summa, abs(RS.FINRES), FD_1.pmwrtsum.rec.Date, NATCUR, FD_1.FaceFIID, false ) == false)
                 Message( "!!! Ошибка при конвертации суммы сделки из " + ПолучитьКодФинИн(NATCUR) + " в " + ПолучитьКодФинИн(FD_1.FaceFIID) + " на " + date_as_string(1, FD_1.pmwrtsum.rec.Date) );
                 return;
              end;
              RunCarry( {OperDprt}, 
                        1,
                        Summa, abs(RS.FINRES),
                        AccBPP.rec.Account, AccMinusR.rec.Account,
                        FD_1.FaceFIID, NATCUR,
                        "Учет фин.результата 1 части в стоимости ц/б", 
                        RS.DealCode,
                        FormData.Flag21 == "X",  
                        true );
           end;
        end;
     end;

  ONERROR(err)
    Message( FormError("!!! Ошибка при выполнении проводок (RunCarryAccountREPO)", err) );
    return false;
  END;

  /*Формировать проводки по переносу остатков вложений */
  PRIVATE MACRO RunCarryInvestment()
     VAR Num = 0, DataQuery, DataQueryNum, RS, cmd;

     SetActiveSheet( "1" );
     PrintFormatString( "\n\n#", "N1_H0", "Формирование проводок по остаткам вложений в ц/б" );  

     DataQueryNum = "SELECT T_FIID FROM DAVRREST_BUF";

     DataQuery    = "SELECT Buf.*, FI.t_FI_Code "+
                    "  FROM DAVRREST_BUF Buf, dfininstr_dbt FI "+
                    " WHERE Buf.t_FIID = FI.t_FIID "
                    " ORDER BY Buf.T_FIID";

     InitProgress( SQL_GetNRecs( DataQueryNum ), "Формирование проводок по остаткам вложений в ц/б", "Процедура ревизии счетов" );

     RS = TRsbDataSet( DataQuery );
     while( RS.MoveNext() )
        CreateCarryAvrRest( RS );
        UseProgress( Num = Num + 1 );
     end;
/*!!!
     cmd = RSDCommand( "DROP TABLE DAVRREST_BUF CASCADE CONSTRAINTS" );
     cmd.execute();
*/
     RemProgress();
  END;

  /*Формировать проводки по переносу остатков по счетам по сделкам Репо*/
  PRIVATE MACRO RunCarryAccountREPO()
     VAR Num = 0, DataQuery, DataQueryNum, RS, cmd;

     SetActiveSheet( "2" );
     PrintFormatString( "\n\n#", "N2_H0", "Формирование проводок по сделкам РЕПО" );  

     DataQueryNum = "SELECT T_DealID FROM DDLREPO_BUF";

     DataQuery    = "SELECT Buf.*, Deal.t_DealCode, Deal.t_ClientID "+
                    "  FROM DDLREPO_BUF Buf, ddl_tick_dbt Deal "+
                    " WHERE Deal.t_DealID = Buf.t_DealID " +
                    " ORDER BY Buf.t_DealID";

     InitProgress( SQL_GetNRecs( DataQueryNum ), "Формирование проводок по сделкам РЕПО", "Процедура ревизии счетов" );

     RS = TRsbDataSet( DataQuery );
     while( RS.MoveNext() )
        CreateCarryREPO( RS );
        UseProgress( Num = Num + 1 );
     end;
/*!!!
     cmd = RSDCommand( "DROP TABLE DDLREPO_BUF CASCADE CONSTRAINTS" );
     cmd.execute();
*/
     RemProgress();
  END;

  PRIVATE MACRO Create()

     if( FormData.Flag1 == "X" )
        RunCarryInvestment();
     end;

     if( FormData.Flag2 == "X" )
        RunCarryAccountREPO();
     end;
  END;

  initDL_CReportTemplate( SP_DealCorrect_Form(), NULL );
END;

SP_DealCorrect().Run();
Exit(1);
