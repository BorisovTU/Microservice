/*
Макрос для апгрейда по запросу 139755.
Вставляются платежи по учету купона/ЧП. 
Если на данное действие платеж уже был - то он не вставляется.
*/
import "screpocalc.mac";


private macro ВыполнитьПереборСделок ()
   var Query, count, DataSet, Num = 0,
       FD, Instance, ChangeKind, ChangeDate,
       Query2, select2, DataSet2, Purpose, Sum, msg, _msg,
       Query3, select3, DataSet3;

   Query = " SELECT distinct(t_DealID)           " +
           "   FROM ( SELECT t_DealID            " +
           "            FROM ddl_tick_dbt        " +
           "           WHERE (t_ChangeKind = 5   " +
           "              or  t_ChangeKind = 7)  " +
           "             and t_DealStatus = 10   " +
           "          UNION                      " +
           "          SELECT s.t_DealID            " +
           "            FROM dsptkchng_dbt s, ddl_tick_dbt d " +
           "           WHERE (s.t_OldChangeKind = 5  " +
           "              or  s.t_OldChangeKind = 7) " +
           "             and d.t_DealStatus = 10     " +
           "             and d.t_DealID = s.t_DealID " +
           "        )                                ";

   count = SQL_GetNRecs( Query );
   if (count > 0)
      InitProgress( count, "Просмотр сделок", "Просмотр сделок" );

      DataSet = TRsbDataSet (Query);
      while (DataSet.MoveNext())

         FD = SPFirstDoc( LEG_KIND_DL_TICK_BACK, DataSet.DealID );

         Instance   = FD.tick.rec.Instance;
         ChangeKind = FD.tick.rec.ChangeKind;
         ChangeDate = FD.tick.rec.ChangeDate;

         while (Instance > 0)
            Query2 = " SELECT t_OldChangeKind, t_OldChangeDate, t_Sum " +
                     "   FROM dsptkchng_dbt     " +
                     "  WHERE t_DealID      = ? " +
                     "    AND t_OldInstance = ? ";

            select2 = RSDCommand(Query2);
            select2.addParam( "", RSDBP_IN, FD.tick.rec.DealID );
            select2.addParam( "", RSDBP_IN, Instance-1 );
            select2.execute();

            DataSet2 = TRsbDataSet(select2);
            if( DataSet2.MoveNext() )
               if ((ChangeKind == SPTKCHNG_COUPON) or (ChangeKind == SPTKCHNG_PARTIAL))

                  Sum = SQL_ConvTypeSum(DataSet2.Sum);

                  if (ChangeKind == SPTKCHNG_COUPON)
                     Purpose = PM_PURP_PAY_NKD;
                     _msg = "купона";
                  else
                     Purpose = PM_PURP_RET_PARTLY;
                     _msg = "ЧП";
                  end;

                  Query3 = " SELECT count(1) nRecs " +
                           "   FROM dpmpaym_dbt    " +
                           "  WHERE t_DocKind     = ? " +
                           "    AND t_DocumentID  = ? " +
                           "    AND t_Purpose     = ? " +
                           "    AND t_ValueDate   = ? ";

                  select3 = RSDCommand(Query3);
                  select3.addParam( "", RSDBP_IN, FD.tick.rec.BOfficeKind );
                  select3.addParam( "", RSDBP_IN, FD.tick.rec.DealID );
                  select3.addParam( "", RSDBP_IN, Purpose );
                  select3.addParam( "", RSDBP_IN, ChangeDate );
                  select3.execute();

                  DataSet3 = TRsbDataSet(select3);
                  if( DataSet3.MoveNext() and (DataSet3.nRecs == 0))

                     if( СоздатьПлатежПоКупонуЧП( FD, ChangeDate, null, Sum, Purpose, 
                                                  FD.Fininstr.rec.FaceValueFI, PM_CLOSED_W_M_MOVEMENT ) != 0 )
                        RemProgress();
                        return false;
                     end;

                     msg = "Для сделки с кодом " + FD.tick.rec.DealCode + 
                           " создан платеж по учёту " + _msg + " в дату " + ChangeDate + 
                           " в размере " + string(Sum) + " " + ПолучитьКодФинИн(FD.Fininstr.rec.FaceValueFI, null, FICK_ISOSTRING);
                     [######################################################################################################################################################](msg);
                  else
                     msg = "Для сделки с кодом " + FD.tick.rec.DealCode + 
                           " найден платеж по учёту " + _msg + " в дату " + ChangeDate + 
                           ". Создание платежа не требуется ";
                     [######################################################################################################################################################](msg);

                  end;
               end;

               ChangeKind = DataSet2.OldChangeKind;
               ChangeDate = SQL_ConvTypeDate(DataSet2.OldChangeDate);
            end;
            Instance = Instance - 1;
         end;

         UseProgress( Num = Num + 1 );
      end;

      RemProgress();
   end;

end;

ВыполнитьПереборСделок ();