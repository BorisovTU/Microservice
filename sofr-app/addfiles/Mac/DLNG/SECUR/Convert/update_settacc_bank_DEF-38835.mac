/*
$Name:        update_settacc_bank_DEF-38835.mac
$Module:      Ценные бумаги
$Description: Обновление филиальной принадлежности в СПИ
*/

import DealsInter, spserv, sp_class, sp_car, spreserv, dlcontrfunc;

private var TestMode   = true;      //Тестовый режим (без фиксации действий в БД)

PRIVATE CLASS TProtocolData(_PartyID:integer, _SettAccID:integer, _Account:string, _BankID_OLD:integer, _BankCode_OLD:string, _BankName_OLD:string, _BankID_NEW:integer, _BankCode_NEW:string, _BankName_NEW:string, _Msg:string)
  var PartyID      = _PartyID;  
  var SettAccID    = _SettAccID;
  var Account      = _Account;  
  var BankID_OLD   = _BankID_OLD; 
  var BankCode_OLD = _BankCode_OLD;
  var BankName_OLD = _BankName_OLD;
  var BankID_NEW   = _BankID_NEW; 
  var BankCode_NEW = _BankCode_NEW;
  var BankName_NEW = _BankName_NEW;
  var Msg          = _Msg; 
END;

PRIVATE MACRO AddSAToArhTable(Action:integer, SettAccID:integer, ErrStr:@string)
  var query, cmd;

  ErrStr = "";

  query = "INSERT INTO DEF38835_SETTACC_DBT (   T_ACTION "         
                                        + "   , T_SETTACCID "      
                                        + "   , T_PARTYID "        
                                        + "   , T_BANKID "         
                                        + "   , T_FIID "           
                                        + "   , T_CHAPTER "        
                                        + "   , T_ACCOUNT "        
                                        + "   , T_INN "            
                                        + "   , T_RECNAME "        
                                        + "   , T_BANKCODEKIND "   
                                        + "   , T_BANKCODE "       
                                        + "   , T_BANKNAME "       
                                        + "   , T_BANKCORRID "     
                                        + "   , T_BANKCORRCODEKIND "
                                        + "   , T_BANKCORRCODE "   
                                        + "   , T_BANKCORRNAME "   
                                        + "   , T_CORRACC "         
                                        + "   , T_FIKIND "         
                                        + "   , T_BENEFICIARYID "  
                                        + "   , T_CODEKIND "       
                                        + "   , T_CODE "           
                                        + "   , T_DESCRIPTION "    
                                        + "   , T_ORDER "          
                                        + "   , T_SHORTNAME "      
                                        + "   , T_NOACCEPT "       
                                        + "   , T_KZPARTYCODE "    
                                        + "   , T_SPI_IDENT "
                                        + "    ) "
           + " SELECT   ? "
           + "        , T_SETTACCID "      
           + "        , T_PARTYID "
           + "        , T_BANKID "
           + "        , T_FIID "
           + "        , T_CHAPTER "
           + "        , T_ACCOUNT "
           + "        , T_INN "
           + "        , T_RECNAME "
           + "        , T_BANKCODEKIND "
           + "        , T_BANKCODE "
           + "        , T_BANKNAME "
           + "        , T_BANKCORRID "
           + "        , T_BANKCORRCODEKIND "
           + "        , T_BANKCORRCODE "
           + "        , T_BANKCORRNAME "
           + "        , T_CORRACC "
           + "        , T_FIKIND "
           + "        , T_BENEFICIARYID "
           + "        , T_CODEKIND "
           + "        , T_CODE "
           + "        , T_DESCRIPTION "
           + "        , T_ORDER "
           + "        , T_SHORTNAME "
           + "        , T_NOACCEPT "
           + "        , T_KZPARTYCODE "
           + "        , T_SPI_IDENT "
           + "  FROM dsettacc_dbt WHERE t_SettAccID = ? ";


  cmd = DL_RSDCOmmand(query);

  cmd.AddParam(Action   );
  cmd.AddParam(SettAccID);

  cmd.ExecuteCMD();

  return 0;
OnError(er)
  ErrStr = er.Message;
  return 1;
END;

PRIVATE MACRO SetNewBankToSettAcc(SettAccID:integer, Acc_PartyID:integer, Acc_BankName:string, Acc_BankCode:string, ErrStr:@string)
  var query, cmd;

  ErrStr = "";

  query =   " UPDATE DSETTACC_DBT "
          + "    SET t_BankID = ?, "
          + "        t_BankName = ?, "
          + "        t_BankCode = ? "
          + "  WHERE t_SettAccID = ? ";


  cmd = DL_RSDCOmmand(query);

  cmd.AddParam(Acc_PartyID);
  cmd.AddParam(Acc_BankName);
  cmd.AddParam(Acc_BankCode);
  cmd.AddParam(SettAccID);

  cmd.ExecuteCMD();

  return 0;
OnError(er)
  ErrStr = er.Message;
  return 1;
END;

private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

PRIVATE MACRO ExecUpdateSettAcc()
  var query, cmd, DataSet;
  var cnt = 0, i = 0;
  var LogArr = TArray(10000);
  var SystDate, SystTime;
  var err = 0, ErrStr = "";
  var err_cnt = 0;

  if(GetTRUE(FALSE, "Выполнить с фиксацией действий в БД?\n"
                   +"(НЕТ - процедура выполнится в тестовом режиме без сохранения изменений в базе; ДА - все успешные изменения будут сохранены в базе)\n"
                   +"Первый запуск рекомендуется выполнить в тестовом режиме и проверить, что процедура выполнится без ошибок."))
    TestMode = false;
  end;


  GetSystDateTime(@SystDate, @SystTime);

  query =  "  with acc_m as (select substr(acc.t_account, 10, 2) || '00' as filial_code, "
         + "                        acc.T_SETTACCID, acc.t_partyid, acc.t_bankid, acc.t_fiid, acc.t_account, "
         + "                        acc.t_bankcodekind, acc.t_bankcode, acc.t_bankname "
         + "                   from dsettacc_dbt acc, dparty_dbt p "
         + "                  where acc.t_partyid = p.t_partyid and p.t_legalform = 2 and (acc.t_account like '40817%' or acc.t_account like '40820%')) "
         + "  select acc_m.*, "
         + "         dep.t_partyid as ACC_PartyID, "
         + "         (select p.t_name from dparty_dbt p where p.t_partyid = dep.t_partyid) as Acc_BankName, "
         + "         (select obj.t_code from dobjcode_dbt obj where obj.t_objecttype = 3 and obj.t_codekind = acc_m.t_BankCodeKind and obj.t_objectid = dep.t_partyid and obj.t_state = 0) as ACC_BankCode "
         + "    from acc_m "
         + "         left join ddp_dep_dbt dep on dep.t_name = acc_m.filial_code "
         + "   where acc_m.t_bankid != dep.t_partyid "
         + "     and not ((acc_m.t_bankid = 1657 and dep.t_partyid = 1) "
         + "               or "
         + "             (acc_m.t_bankid = 1 and dep.t_partyid = 1657)) ";

  cmd = DL_RSDCommand(query);

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "Обработка СПИ", "Обработка СПИ");
    
    DataSet = cmd.Execute();
    while(DataSet.moveNext())

      err = 0;
      ErrStr = "";

      if(strlen(DataSet.Account) != 20)
        err = 1;
        ErrStr = "Некорректный счет! Обновление невозможно";
      end;

      if(not err)
        RslDefCon.BeginTrans();

        err = AddSaToArhTable(2, DataSet.SettAccID, @ErrStr);
        if(not err)
          err = SetNewBankToSettAcc(DataSet.SettAccID, DataSet.Acc_PartyID, DataSet.Acc_BankName, DataSet.Acc_BankCode, @ErrStr);
        end;
        
        if((TestMode) or (err))
          RslDefCon.RollbackTrans();
        else
          RslDefCon.CommitTrans();
        end;
      end;

      LogArr[LogArr.size()] = TProtocolData(DataSet.PartyID, DataSet.SettAccID, DataSet.Account, DataSet.BankID, DataSet.BankCode, DataSet.BankName, DataSet.Acc_PartyID, DataSet.Acc_BankCode, DataSet.Acc_BankName, ErrStr);
      if(err)
        err_cnt = err_cnt + 1;
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();

    InitProgress(LogArr.size(), "Печать протокола", "Печать протокола");

             [                                                        Обновление филиальной принадлежности в СПИ
             Дата выполнения:  #############
             Время выполнения: #############
             Обработано СПИ:   #############  Из них не обновлены: #########
             ]
             (SystDate:f, SystTime, cnt, err_cnt);

    println("");
    println("┌─────────────┬─────────────┬──────────────────────┬──────────────┬────────────────┬────────────────────────────────────────┬──────────────┬────────────────┬────────────────────────────────────────┐");
    println("│   PartyID   │  SettAccID  │        Account       │  BANKID_Old  │  BANKCODE_Old  │              BANKNAME_Old              │  BANKID_New  │  BANKCODE_New  │              BANKNAME_New              │");
    println("│             │             │                      │              │                │                                        │              │                │                                        │");
    println("├─────────────┼─────────────┼──────────────────────┼──────────────┼────────────────┼────────────────────────────────────────┼──────────────┼────────────────┼────────────────────────────────────────┤");

    i = 0;
    while(i < LogArr.size())

            [│#############│#############│######################│##############│################│########################################│##############│################│########################################│############################################################################################]
            (string(LogArr[i].PartyID):c,     
             string(LogArr[i].SettAccID):c,   
             string(LogArr[i].Account):c,     
             string(LogArr[i].BankID_OLD):c,  
             string(LogArr[i].BankCode_OLD):c,
             string(LogArr[i].BankName_OLD):l:w,
             string(LogArr[i].BankID_NEW):c,  
             string(LogArr[i].BankCode_NEW):c,
             string(LogArr[i].BankName_NEW):l:w,
             string(LogArr[i].Msg):l:w
            );

      if(i < LogArr.size() - 1)
        println("├─────────────┼─────────────┼──────────────────────┼──────────────┼────────────────┼────────────────────────────────────────┼──────────────┼────────────────┼────────────────────────────────────────┤");
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();

        println("└─────────────┴─────────────┴──────────────────────┴──────────────┴────────────────┴────────────────────────────────────────┴──────────────┴────────────────┴────────────────────────────────────────┘");

  else
    msgbox("Нет подходящих СПИ");
  end;

OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  msgbox("Ошибка: " + er.Message);
END;

ExecUpdateSettAcc();
