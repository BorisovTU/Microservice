/*
$Name:        checkaverage.mac
$Module:      Ценные бумаги
$Description: Проверка корректности осреднения при переходе на средневзвес. Предполагается, что она выполняется сразу после первого осреднения
*/
import secinter;


private macro CheckNullLots(FIID, Portfolio, State, Amount, Sum, Field, Text)
  var cmd, query;

  query =   " select 1 "
          + "   from dpmwrtsum_dbt wrt "
          + "  where wrt.t_FIID = ? "
          + "    and wrt.t_Portfolio = ? "
          + "    and wrt.t_State = ? "
          + "    and wrt.t_Amount > 0 "
          + "    and wrt.t_Party = " + UnknownParty
          + "    and wrt.t_Contract = 0 "
          + "    and wrt."+Field+" = 0 "
          + "    and ?*wrt.t_Amount/? >= 1/100 "
          + "    and rownum = 1 ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(FIID);
  cmd.AddParam(Portfolio);
  cmd.AddParam(State);
  cmd.AddParam(Sum);
  cmd.AddParam(Amount);
  
  println("Проверка отсутствия лотов с "+Text+": " + IIF(((Sum != 0) and (cmd.GetCount() > 0)), "ОШИБКА! Есть лоты с "+Text, "ОК"));

end;

private macro CheckFI(FIID, Portfolio, State)
  var query, cmd, DataSet;
  var Amount = $0;
  var SumCost = $0;             
  var SumBalanceCost = $0;       
  var SumBonus = $0;             
  var SumBegBonus = $0;          
  var SumDiscountIncome = $0;    
  var SumBegDiscount = $0;       
  var SumCorrIntToEIR = $0;      
  var OldSumCost = $0;           
  var OldSumBalanceCost = $0;    
  var OldSumBonus = $0;          
  var OldSumBegBonus = $0;       
  var OldSumDiscountIncome = $0; 
  var OldSumBegDiscount = $0;    
  var OldSumCorrIntToEIR = $0;    
  var fin = TBFile("fininstr.dbt");


  fin.Clear();
  fin.rec.FIID = FIID;
  if(not fin.GetEQ())
    println("Не найдена ц/б с идентификатором " + FIID);
    return;
  end;

  println("Ценная бумага " + fin.rec.FI_Code + ". Портфель " + DL_GetValueName(OBJTYPE_KINDPORT, Portfolio) + " " + IIF(State == PM_WRTSUM_SALE_BPP, "(БПП)", ""));


  //Т.к. проверку запускаем после первого осреднения, то суммы по портфелю ДО осреднения берем из предыдущего состояния лотов
  query =   " select NVL(SUM(wrt.t_Amount),0)         as Amount, "
          + "        NVL(SUM(wrt.t_Cost),0)           as SumCost, "
          + "        NVL(SUM(wrt.t_BalanceCost),0)    as SumBalanceCost, "
          + "        NVL(SUM(wrt.t_Bonus),0)          as SumBonus, "
          + "        NVL(SUM(wrt.t_BegBonus),0)       as SumBegBonus, "
          + "        NVL(SUM(wrt.t_DiscountIncome),0) as SumDiscountIncome, "
          + "        NVL(SUM(wrt.t_BegDiscount),0)    as SumBegDiscount, "
          + "        NVL(SUM(wrt.t_CorrIntToEIR),0)   as SumCorrIntToEIR, "
          + "        NVL(SUM(h.t_Cost),0)             as OldSumCost, "
          + "        NVL(SUM(h.t_BalanceCost),0)      as OldSumBalanceCost, "
          + "        NVL(SUM(h.t_Bonus),0)            as OldSumBonus, "
          + "        NVL(SUM(h.t_BegBonus),0)         as OldSumBegBonus, "
          + "        NVL(SUM(h.t_DiscountIncome),0)   as OldSumDiscountIncome, "
          + "        NVL(SUM(h.t_BegDiscount),0)      as OldSumBegDiscount, "
          + "        NVL(SUM(h.t_CorrIntToEIR),0)     as OldSumCorrIntToEIR "
          + "   from dpmwrtsum_dbt wrt, v_scwrthistex h "
          + "  where wrt.t_FIID = ? "
          + "    and wrt.t_Portfolio = ? "
          + "    and wrt.t_State = ? "
          + "    and wrt.t_Amount > 0 "
          + "    and wrt.t_Party = " + UnknownParty
          + "    and wrt.t_Contract = 0 "
          + "    and h.t_SumID = wrt.t_SumID "
          + "    and h.t_Instance = wrt.t_Instance - 1 ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(FIID);
  cmd.AddParam(Portfolio);
  cmd.AddParam(State);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    Amount               = DataSet.Amount;
    SumCost              = DataSet.SumCost;             
    SumBalanceCost       = DataSet.SumBalanceCost;      
    SumBonus             = DataSet.SumBonus;            
    SumBegBonus          = DataSet.SumBegBonus;         
    SumDiscountIncome    = DataSet.SumDiscountIncome;   
    SumBegDiscount       = DataSet.SumBegDiscount;      
    SumCorrIntToEIR      = DataSet.SumCorrIntToEIR;     
    OldSumCost           = DataSet.OldSumCost;          
    OldSumBalanceCost    = DataSet.OldSumBalanceCost;   
    OldSumBonus          = DataSet.OldSumBonus;         
    OldSumBegBonus       = DataSet.OldSumBegBonus;      
    OldSumDiscountIncome = DataSet.OldSumDiscountIncome;
    OldSumBegDiscount    = DataSet.OldSumBegDiscount;   
    OldSumCorrIntToEIR   = DataSet.OldSumCorrIntToEIR;  
  end;

  println("Чистая стоимость:       " + IIF(SumCost           == OldSumCost,           "ОК", "ОШИБКА! Новое значение = "+SumCost+", прежнее значение = " + OldSumCost));
  println("Балансовая стоимость:   " + IIF(SumBalanceCost    == OldSumBalanceCost,    "ОК", "ОШИБКА! Новое значение = "+SumBalanceCost+", прежнее значение = " + OldSumBalanceCost));
  println("Начальная премия:       " + IIF(SumBegBonus       == OldSumBegBonus,       "ОК", "ОШИБКА! Новое значение = "+SumBegBonus+", прежнее значение = " + OldSumBegBonus));
  println("Списанная премия:       " + IIF(SumBonus          == OldSumBonus,          "ОК", "ОШИБКА! Новое значение = "+SumBonus+", прежнее значение = " + OldSumBonus));
  println("Начальный дисконт:      " + IIF(SumBegDiscount    == OldSumBegDiscount,    "ОК", "ОШИБКА! Новое значение = "+SumBegDiscount+", прежнее значение = " + OldSumBegDiscount));
  println("Начисленный дисконт:    " + IIF(SumDiscountIncome == OldSumDiscountIncome, "ОК", "ОШИБКА! Новое значение = "+SumDiscountIncome+", прежнее значение = " + OldSumDiscountIncome));
  println("Корректировка % до ЭПС: " + IIF(SumCorrIntToEIR   == OldSumCorrIntToEIR,   "ОК", "ОШИБКА! Новое значение = "+SumCorrIntToEIR+", прежнее значение = " + OldSumCorrIntToEIR));



  //Проверка существования лотов с нулевым значением поля и ненулевым значением по портфелю
  CheckNullLots(FIID, Portfolio, State, Amount, SumBegBonus,      "t_BegBonus",      "нулевой начальной премией");
  CheckNullLots(FIID, Portfolio, State, Amount, SumBonus,         "t_Bonus",         "нулевой списанной премией");
  CheckNullLots(FIID, Portfolio, State, Amount, SumBegDiscount,   "t_BegDiscount",   "нулевым начальным дисконтом");
  CheckNullLots(FIID, Portfolio, State, Amount, SumDiscountIncome,"t_DiscountIncome","нулевым начисленным дисконтом");
  CheckNullLots(FIID, Portfolio, State, Amount, SumCorrIntToEIR,  "t_CorrIntToEIR",  "нулевой корректировкой % до ЭПС");


  println();
  println();

end;

private macro Check()
  var query, cmd, DataSet;
  var i = 0;

  query =   " select DISTINCT wrt.t_FIID, wrt.t_Portfolio, wrt.t_State "
          + "   from dpmwrtsum_dbt wrt "
          + "  where wrt.t_Amount > 0 "
          + "    and wrt.t_Party = " + UnknownParty
          + "    and wrt.t_Contract = 0 "
          + "    and wrt.t_Buy_Sale = " + PM_WRITEOFF_SUM_BUY
          + "    and wrt.t_State IN (" + PM_WRTSUM_FORM + ", " + PM_WRTSUM_SALE_BPP + ") "
          + "    and wrt.t_Portfolio IN (" + KINDPORT_SSSD + ", " + KINDPORT_SSPU + " , " + KINDPORT_ASCB + ") "
          + "    and RSB_PMWRTOFF.WRTNeedChargeIncome(wrt.t_FIID) = 1 "
          + " order by wrt.t_FIID, wrt.t_Portfolio, wrt.t_State ";

  cmd = DL_RSDCommand(query);

  var cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "Проверка","Проверка");
    
    DataSet = cmd.Execute();

    while(DataSet.moveNext())
      CheckFI(DataSet.FIID, DataSet.Portfolio, DataSet.State);

      UseProgress(i = i + 1);
    end;

    RemProgress();
  end;

end;

Check();