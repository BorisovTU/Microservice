/*
$Name:        initsolution_createSTB_7294.mac
$Module:      Ценные бумаги
$Description: Начальное решение по созданию событий СНОБ
*/

import DealsInter, spserv, sp_class, sp_car, spreserv, "vs4each.mac", "vsconv.mac", "vsrepay5.mac", "vsnptxspec.mac";

/*CONST NPTXKBK_MAIN = "18210102010011000110";
CONST NPTXKBK_INCR_218 = "18210102180011000110";
CONST NPTXKBK_INCR_224 = "18210102240011000110";*/

macro AggregateByKBK(TxArr)
  var newTxArr = TArray();

  var i = -1;
  while ((i=i+1) < TxArr.size)
    var current = TxArr[i];
    var foundIdx = -1;
    var j = -1;
    while ((j=j+1) < newTxArr.size)
      if (newTxArr[j].КБК == current.КБК)
        foundIdx = j;
        break;
      end;
    end;

    if (foundIdx == -1)
      newTxArr[newTxArr.size] = STB_TaxRateParm(current.Ставка,
                                                current.НОБ,
                                                current.Налог,
                                                current.КБК,
                                                current.ВидНБ,
                                                current.ИИС,
                                                current.Тип,
                                                current.OrigTBID,
                                                current.TaxCalc,
                                                current.AddTaxCalc,
                                                current.Symbol,
                                                current.SpecialTag
                                               );
    else
      newTxArr[foundIdx].НОБ = newTxArr[foundIdx].НОБ + current.НОБ;
      newTxArr[foundIdx].Налог = newTxArr[foundIdx].Налог + current.Налог;
      newTxArr[foundIdx].TaxCalc = newTxArr[foundIdx].TaxCalc + current.TaxCalc;
    end;
  end;

  return newTxArr;
end;

macro GetPaidTaxByKBK(ClientID, ContractId, Year, KBK)
  var minDate = date(1,1,year);
  var maxDate = date(31,12,Year);
  var StrDocKind = string(DL_VEKSELDRAWORDER)+", "+ string(DL_VSBARTERORDER)  +", "+ string(DL_VSINTERCHANGE);
  var SVal, SRub, vCount;
  var arrayKind = Tarray();
  var arrayVal = Tarray();

  arrayKind[0] = TXOBJ_KIND1130;
  arrayVal[0] = ContractId;

  var StrKind = "";
  if (KBK == NPTXKBK_MAIN)
    StrKind = TXOBJ_PAIDBILL;
  elif (KBK == NPTXKBK_INCR_218)
    StrKind = TXOBJ_PAIDGENERAL_15_9+","+TXOBJ_PAIDGENERAL_18_9+","+TXOBJ_PAIDGENERAL_20_9;
  elif (KBK == NPTXKBK_INCR_224)
    StrKind = TXOBJ_PAIDGENERAL_22_9;
  end;

  if (StrKind != "")
    DL_GetNPTXOBJSum(null, null, null, StrKind, MinDate, MaxDate, null, @SVal, @SRub, @vCount, ClientID, arrayKind, arrayVal, null, null, null, null, null,
                      null, null, null, true, StrDocKind, null, null, null, null, null, null);
  end;
  return SRub;
end;

private var TestMode   = true;      //Тестовый режим (без фиксации действий в БД)

private class TClientErr(_ClientCode, _ClientName, _ErrStr)
  var ClientCode = _ClientCode;
  var ClientName = _ClientName;
  var ErrStr     = _ErrStr;     
end;

private var ErrArr = TArray();

PRIVATE MACRO GetPartyName( PartyID:INTEGER ) :STRING
  VAR Party_ForGet = TRecHandler( "party.dbt" );
  if( ПолучитьСубъекта( PartyID, Party_ForGet ) == 0 )
     return Party_ForGet.rec.Name;
  end;
  return "";
end;

private macro GetVSDateTime(dlorder, currIncDate:@date, currIncTime:@time)
  var query, cmd, DataSet;  
  
  currIncDate = dlorder.rec.SignDate;
  currIncTime = time(0);

  query =   " select t_RegistrDate, t_RegistrTime "
          + "   from dspground_dbt "
          + "  where t_SourceDocKind = ? "
          + "    and t_SourceDocID = ? "
          + "    and t_Direction = 1 ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(dlorder.rec.DocKind);
  cmd.AddParam(dlorder.rec.ContractID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    currIncDate = date(DataSet.RegistrDate);
    currIncTime = time(DataSet.RegistrTime);
  end;
end;

private macro CreateSTBByVSClient(ClientId)
  record dlord_rec(dl_order);
  var query, cmd, DataSet;
  var dlorder = TBFile("dl_order.dbt");
  var err = 0, ErrStr = "";
  var FindTaxBaseKind = string(NPTXTOTALBASE_TAXBASEKIND_BROK);
  var currIncDate = date(0,0,0);
  var currIncTime = time(0);
  var Year = 0;

  println("ClientID: " + ClientId);

  RslDefCon.BeginTrans();

  query = 
    " select distinct T_CLIENTID, t_docid, t_incdate "
   +" FROM DNPTXTOTALBASE_DBT "
   +" WHERE t_dockind = 110 "
   +"   AND t_incdate >= :startdate "
   +"   AND t_clientid = :clientid "
   +"   and t_type = 1 "
   +" order by T_CLIENTID, t_incdate, t_docid ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(VsGetStartNewNOBDate());
  cmd.AddParam(ClientId);

  var count = cmd.GetCount();

  if (count > 0)
    InitProgress(count, "Обработка погашений векселей", "Обработка погашений векселей");
    var i = 0;

    DataSet = cmd.Execute();
    while((not err) and (DataSet.moveNext()))
      dlorder.Clear();
      dlorder.rec.ContractID = DataSet.docid;
      dlorder.GetEQ();

      Copy(dlord_rec,dlorder);

      GetVSDateTime(dlorder, @currIncDate, @currIncTime);

      DateSplit(currIncDate, null, null, Year);

      var AmountSNOB = STB_GetSumTaxBaseCurrPay(dlord_rec.Contractor, FindTaxBaseKind, Year, NULL, NULL, currIncDate, currIncTime);

      println("AmountSNOB: " + AmountSNOB);

      var retData = CalcTaxSTB_VekselPRGS_AndGetTaxHold(dlord_rec, AmountSNOB );

      var TxArr = AggregateByKBK(retData.TaxHoldArr);
      var НОБ_опер = retData.НОБ_опер;

      var j = -1;
      while ((j=j+1) < TxArr.size)
        TxArr[j].Налог = GetPaidTaxByKBK(dlord_rec.Contractor, dlord_rec.ContractID, Year, TxArr[j].КБК);

        println("KBK: " + string(TxArr[j].КБК) + " Tax: "+ string(TxArr[j].Налог));
      end;

      if(not err)
        err = STB_ExecuteCreateSTBOnStep(TxArr,
                                         НОБ_опер,
                                         dlord_rec.DocKind, 
                                         dlord_rec.ContractID, 
                                         dlord_rec.Contractor, 
                                         dlord_rec.Signdate, 
                                         false, 
                                         Year, 
                                         0, 
                                         0, 
                                         @ErrStr,
                                         currIncDate,
                                         currIncTime,
                                         AmountSNOB,
                                         NULL,
                                         NULL,
                                         NULL,
                                         NULL,
                                         NULL,
                                         NULL,
                                         NULL,
                                         NULL,
                                         NULL,
                                         "ВЕКС");
      end;
      UseProgress((i=i+1));
    end;
    RemProgress();
  end;

  println();

  

  if((TestMode) or (err))
    RslDefCon.RollbackTrans();
  else
    RslDefCon.CommitTrans();
  end;

  if(err)
    RunError(ErrStr);
  end;

OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  ErrArr[ErrArr.size] = TClientErr(ПолучитьКодСубъекта(ClientId, PTCK_CONTR), GetPartyName(ClientId), GetFullErrorMessage(er));
end;

private macro ExecCreateSTB()
  var query, cmd, DataSet;
  var cnt = 0, i = 0;

  ErrArr.size = 0;
  
  if(GetTRUE(FALSE, "Выполнить с фиксацией действий в БД?\n"
                   +"(НЕТ - процедура выполнится в тестовом режиме без сохранения изменений в базе; ДА - все успешные изменения будут сохранены в базе)\n"
                   +"Первый запуск рекомендуется выполнить в тестовом режиме и проверить в протоколе, что процедура выполняется без ошибок."))
    TestMode = false;
  end;

  if(РАБОТА_С_ВНЕШНИМ_ХРАНИЛИЩЕМ_СНОБ() == false)
    msgbox("Не включена работа с Хранилищем СНОБ");
    return;
  end;

  query = 
    " select distinct t_clientid"
   +" from dnptxtotalbase_dbt "
   +" where t_dockind = 110 "
   +"   and t_incdate >= :startdate "
   +" order by t_clientid";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(VsGetStartNewNOBDate());

  cnt = cmd.GetCount();

  if(cnt > 0)
    InitProgress(cnt, "Обработка клиентов векселей", "Обработка клиентов векселей");

    i = 0;
    DataSet = cmd.Execute();
    while(DataSet.moveNext())

      CreateSTBByVSClient(DataSet.clientid);

      UseProgress(i = i + 1);
    end;

    RemProgress();

    println("Перезаполнение данных Хранилища СНОБ в СОФР по операциям векселей");

    [Обработано операций: ############# Из них: успешно - ###########   с ошибкой - ###########]
    (cnt:l, cnt-ErrArr.size:l, ErrArr.size:l);

    if(ErrArr.size > 0)
      println();
      println("Ошибки обработки");
      println("┌──────────────────────┬────────────────────────────────────────────────┬────────────────────────────────────────────────────────────┐");
      println("│          Код         │                      ФИО                       │                        Сообщение                           │");
      println("│        клиента       │                                                │                                                            │");
      println("├──────────────────────┼────────────────────────────────────────────────┼────────────────────────────────────────────────────────────┤");

      i = 0;

      while(i < ErrArr.size)
        
              [│######################│################################################│############################################################│]
              (string(ErrArr[i].ClientCode):c,   
               string(ErrArr[i].ClientName):l:w,   
               string(ErrArr[i].ErrStr):l:w        
              );

        if(i < ErrArr.size() - 1)
          println("├──────────────────────┼────────────────────────────────────────────────┼────────────────────────────────────────────────────────────┤");
        end;
        
        i = i + 1;
      end;

      println("└──────────────────────┴────────────────────────────────────────────────┴────────────────────────────────────────────────────────────┘");
    end;

  else
    println("Нет операций");    
  end; 

OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  msgbox("Ошибка: " + GetFullErrorMessage(er));
end;

ExecCreateSTB();
