/*
$Name:        load_nptxdefferedobj.mac
$Module:      Ценные бумаги
$Description: Загрузка файла с компенсациями
*/

import rsexts;
import "global_classes.mac";           /* класс ошибок */
import "ws_msg_transform_sec.mac";     /* для обработки ошибок */
import dlmisc;                         /* SplitString*/
import "func_lib";
import "dlquery.mac";
import "scsrvrepfun.mac";

private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

private macro GetClientAndContr(ContrNumber:string, ClientFullName:string, ClientID:@integer, ContrID:@integer)
  var query, cmd, DataSet;

  ClientID = -1;
  ContrID = 0;

  if(ContrNUmber != "")
    query =   " select t_ID, t_PartyID"
            + "   from dsfcontr_dbt "
            + "  where t_Number = ? "
            + " order by t_DateBegin DESC";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(ContrNumber);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      ContrID = DataSet.ID;
      ClientID = DataSet.PartyID;
    else
      println("Не найден договор с номером " + ContrNumber + ". Запись не добавлена");
    end;
  elif(ClientFullName != "")
    query = "select t_PartyID from dparty_dbt where TRIM(LOWER(t_Name)) = TRIM(LOWER(?)) ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(ClientFullName);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      ClientID = DataSet.PartyID;
      if(DataSet.moveNext())
        println("Найдено несколько клиентов с именем " + ClientFullName + ". Запись не добавлена");
        ClientID = -1;
      end;
    else
      println("Не найден клиент с именем " + ClientFullName + ". Запись не добавлена");
    end;
  else
    println("Не заданы номер договора и ФИО клиента. Запись не добавлена");
  end;
end;

private macro ExistsRec(ClientID, ContrID, PlusG_4800, PaidComp, PaidComp_15, DeffDate)   
  var query, cmd;
  var cnt = 0;

  query =   " select 1 "
          + "   from dnptxdefferedobj_dbt "
          + "  where t_PartyID     = ? "
          + "    and t_ContractID  = ? "
          + "    and t_PlusG_4800  = ? "
          + "    and t_PaidComp    = ? "
          + "    and t_PaidComp_15 = ? "
          + "    and t_Date        = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ClientID); 
  cmd.AddParam(ContrID); 
  cmd.AddParam(PlusG_4800); 
  cmd.AddParam(PaidComp); 
  cmd.AddParam(PaidComp_15); 
  cmd.AddParam(DeffDate);

  cnt = cmd.GetCount();

  if(cnt > 0)
    return true;
  end;

  return false;
end;


private Macro LoadDeff(namefile)
  var objExcel = CDAOMSExcel();
  var query, Ins;
  var cnt = 0, load_cnt = 0;

  if(objExcel.CreateApplication())
    if(objExcel.open(namefile))
      objExcel.Book.Sheets(1).Activate();
      
      var curr_row = -1;
      var activeSheet = objExcel.Book.ActiveSheet;
      var objTarget = activeSheet.UsedRange.Find("Договор");

      if(objTarget)
        curr_row = objTarget.row + 1;
      else
        msgbox("Данные в файле не найдены");
        return;
      end;
      
      if(curr_row > -1)
        InitProgress(activeSheet.Rows.CurrentRegion.Rows.Count - curr_row + 1, "Ждите, выполняется загрузка данных ...", "Выполняется загрузка данных");
        
        while(curr_row <= activeSheet.Rows.CurrentRegion.Rows.Count)
          var ContrNumber    = trim(string(activeSheet.Cells(int(curr_row), 1).Value));
          var ClientFullName = trim(string(activeSheet.Cells(int(curr_row), 2).Value));
          var PlusG_4800     = money(activeSheet.Cells(int(curr_row), 3).Value);
          var PaidComp       = money(activeSheet.Cells(int(curr_row), 4).Value);
          var PaidComp_15    = money(activeSheet.Cells(int(curr_row), 5).Value);
          var DeffDate       = date(trim(string(activeSheet.Cells(int(curr_row), 6).Value)));
          var ClientID = -1, ContrID = 0;

          if(ContrNumber == "Undefined")
            ContrNumber = "";
          end;

          GetClientAndContr(ContrNumber, ClientFullName, @ClientID, @ContrID);

          if(ClientID > 0)

            if(ExistsRec(ClientID, ContrID, PlusG_4800, PaidComp, PaidComp_15, DeffDate))
              println("Запись с номером договора = "+ContrNumber+" и ФИО = "+ClientFullName+" и суммой = "+PlusG_4800+" за "+string(DeffDate:f)+" была загружена ранее. Запись не добавлена");
            else                    
              query =   " DECLARE "
                      + "   v_NextID NUMBER := 0; "
                      + " BEGIN "
                      + "   SELECT NVL(MAX(t_ID),0)+1 INTO v_NextID FROM DNPTXDEFFEREDOBJ_DBT; "
                      + "   INSERT INTO DNPTXDEFFEREDOBJ_DBT (T_ID,T_PARTYID,T_PLUSG_4800,T_PAIDCOMP,T_PAIDCOMP_15,T_DATE,T_CONTRACTID,T_ACCOUNT3060,T_ERROR,TRN1,TRN2,TRN3,TRN4) "
                      + "                             VALUES (v_NextID, "//T_ID 
                      + "                                     ?, "       //T_PARTYID
                      + "                                     ?, "       //T_PLUSG_4800
                      + "                                     ?, "       //T_PAIDCOMP
                      + "                                     ?, "       //T_PAIDCOMP_15 
                      + "                                     ?, "       //T_DATE
                      + "                                     ?, "       //T_CONTRACTID
                      + "                                     CHR(1), "  //T_ACCOUNT3060
                      + "                                     CHR(1), "  //T_ERROR
                      + "                                     0, "       //TRN1
                      + "                                     0, "       //TRN2
                      + "                                     0, "       //TRN3
                      + "                                     0 "        //TRN4
                      + "                                    ); "
                      + " END; ";

              Ins = DL_RSDCommand(query);
              Ins.AddParam(ClientID);
              Ins.AddParam(PlusG_4800);
              Ins.AddParam(PaidComp);
              Ins.AddParam(PaidComp_15);
              Ins.AddParam(DeffDate);
              Ins.AddParam(ContrID);

              Ins.ExecuteCMD();

              load_cnt = load_cnt + 1;
            end;
          end;

          cnt = cnt + 1;

          UseProgress(curr_row = curr_row + 1);
        end;

 
        RemProgress();

        msgbox("Загружено "+ load_cnt + " записей из " + cnt);
      end;
    end;
    objExcel.Book.Close();
  end;
  objExcel = null;
OnError(err)

  Msgbox(err.Message);
  objExcel.Book.Close();
  objExcel = null;
  RemProgress();
end;

private Macro LoadDeffPOI(namefile)
  macro FindOnSheet(sheet, str)
    var row, cell, irow = sheet.rowIterator(), icell, data;
    while (irow.hasNext())
      row = irow.next();
      icell = row.cellIterator();
      while (icell.hasNext())
        cell = icell.next();
        if (cell.getCellType().code == 1)  //STRING
          data = cell.getStringCellValue();
          if (data == str)
            return cell;
          end;
        end;
      end;
    end;

    return null;
  end;

  var jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  var rc = jvm.callStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");
  var book = rc.openWorkbook(namefile);
  var query, Ins;
  var cnt = 0, load_cnt = 0;
  
  if (book)
    var curr_row = -1;
    var activeSheet = book.getSheet(0);
    var objTarget = FindOnSheet( activeSheet, "Договор");

    if(objTarget)
      curr_row = objTarget.getRowIndex() + 1;
    else
      msgbox("Данные требуемого формата в файле не найдены");
      return;
    end;

    var row_count = book.getRowCount(activeSheet) - curr_row;

    if(curr_row > -1)
      InitProgress(row_count, "Ждите, выполняется загрузка данных ...", "Выполняется загрузка данных");
        
      while(curr_row <= row_count)
        var ContrNumber    = trim(string(book.getCellValue(book.getCellRangeAddress(int(curr_row), int(curr_row), 0, 0))));
        var ClientFullName = trim(string(book.getCellValue(book.getCellRangeAddress(int(curr_row), int(curr_row), 1, 1))));
        var PlusG_4800     = money(book.getCellValue(book.getCellRangeAddress(int(curr_row), int(curr_row), 2, 2)));
        var PaidComp       = money(book.getCellValue(book.getCellRangeAddress(int(curr_row), int(curr_row), 3, 3)));
        var PaidComp_15    = money(book.getCellValue(book.getCellRangeAddress(int(curr_row), int(curr_row), 4, 4)));
        var DeffDate       = date(book.getCellValue(book.getCellRangeAddress(int(curr_row), int(curr_row), 5, 5)));
        var ClientID = -1, ContrID = 0;

        if(ContrNumber == "Undefined")
          ContrNumber = "";
        end;

        GetClientAndContr(ContrNumber, ClientFullName, @ClientID, @ContrID);

        if(ClientID > 0)

          if(ExistsRec(ClientID, ContrID, PlusG_4800, PaidComp, PaidComp_15, DeffDate))
            println("Запись с номером договора = "+ContrNumber+" и ФИО = "+ClientFullName+" и суммой = "+PlusG_4800+" за "+string(DeffDate:f)+" была загружена ранее. Запись не добавлена");
          else                    
            query =   " DECLARE "
                    + "   v_NextID NUMBER := 0; "
                    + " BEGIN "
                    + "   SELECT NVL(MAX(t_ID),0)+1 INTO v_NextID FROM DNPTXDEFFEREDOBJ_DBT; "
                    + "   INSERT INTO DNPTXDEFFEREDOBJ_DBT (T_ID,T_PARTYID,T_PLUSG_4800,T_PAIDCOMP,T_PAIDCOMP_15,T_DATE,T_CONTRACTID,T_ACCOUNT3060,T_ERROR,TRN1,TRN2,TRN3,TRN4) "
                    + "                             VALUES (v_NextID, "//T_ID 
                    + "                                     ?, "       //T_PARTYID
                    + "                                     ?, "       //T_PLUSG_4800
                    + "                                     ?, "       //T_PAIDCOMP
                    + "                                     ?, "       //T_PAIDCOMP_15 
                    + "                                     ?, "       //T_DATE
                    + "                                     ?, "       //T_CONTRACTID
                    + "                                     CHR(1), "  //T_ACCOUNT3060
                    + "                                     CHR(1), "  //T_ERROR
                    + "                                     0, "       //TRN1
                    + "                                     0, "       //TRN2
                    + "                                     0, "       //TRN3
                    + "                                     0 "        //TRN4
                    + "                                    ); "
                    + " END; ";

            Ins = DL_RSDCommand(query);
            Ins.AddParam(ClientID);
            Ins.AddParam(PlusG_4800);
            Ins.AddParam(PaidComp);
            Ins.AddParam(PaidComp_15);
            Ins.AddParam(DeffDate);
            Ins.AddParam(ContrID);

            Ins.ExecuteCMD();

            load_cnt = load_cnt + 1;
          end;
        end;

        cnt = cnt + 1;

        UseProgress(curr_row = curr_row + 1);
      end;

 
      RemProgress();

      msgbox("Загружено "+ load_cnt + " записей из " + cnt);
    end;

    book.close();
  end;
  rc = null;
OnError(err)

  Msgbox(err.Message);
  book.close();
  book = null;
  rc = null;
  RemProgress();
end;


var file_name = "";
if(SelectFile(file_name, MergeFile ("..\\Import", "*.xls*"), "Выбор Excel-файла с компенсациями")) 
  if (UseExcelApp)
    LoadDeff(file_name);
  else
    LoadDeffPOI(file_name);
  end;
end;
