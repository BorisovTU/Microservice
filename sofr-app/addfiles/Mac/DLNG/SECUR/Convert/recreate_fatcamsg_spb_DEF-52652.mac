/**                                                                                                             
 @file recreate_fatcamsg_spb_DEF-52652.mac                                                                                           
 @brief Переформирование FATCA СПБ для открытых договоров, где FATCA зависла в статусе "Ожидает подтверждения биржи" из-за неправильного формирования сообщения для клинтов с двумя договорами (БО и ИИС)                                                                          
                                                                                                                
 # tag                                                                                                          
 - functional_block:Ценные бумаги                                                                             
 - code_type:API                                                                                                
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2023.10.13 |Жиц М.В.       |DEF-52652           |Реализация макроса переформирования FATCA СПБ                                 
                                                                                                                
*/                                                                                                              

import secinter, "usr_msg_lib.mac";

private const REGPATH_CLIENT_BOOKING = "SECUR\\SPB\\PATH_CLIENT_BOOKING";
private const REGPATH_CLIENT_ONLINE  = "SECUR\\SPB\\PATH_CLIENT_ONLINE";
private const REGPATH_CLIENTS        = "SECUR\\SPB\\PATH_CLIENTS";
private const REGPATH_FATCA          = "SECUR\\SPB\\PATH_FATCA";
private var m_Msg:string = "";

/**
@brief                    Фунция для переопределения msgbox с вывода на экран на запись в переменную
@param[in]    StrErr      текст информационного сообщения
*/
macro save_msgbox(StrErr)
  m_Msg = m_Msg + StrErr + "\n";
end;

/**
@brief                    Получение системной даты и времени
@param[out]   SystDate    системная дата
@param[out]   SystTime    системное время
*/
private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

/**
@brief                    Получение значения настройки СОФР
@param[in]   regPath      название настройки
@return 		  значение настройки
*/
private macro GetStrRegVal(regPath:string):string
  var stat = 0, path = "";
  GetRegistryValue(regPath, V_STRING, path, stat);
  if(stat != 0)
    msgbox("Ошибка при получении значения настройки: " + regPath);
  elif(path == "")
    stat = 1;
    msgbox("Не задано значение настройки: " + regPath);
  end;
  return path;
end;

/**
@brief       Формирование путей для работы с FATCA
@return      статус выполнения: 0 - выполнено без ошибок, иное значение - номер ошибки
*/
private macro Construct():integer
  var stat:integer = 0;
  var path:string = "";

  path = DL_GetFullPath(GetStrRegVal(REGPATH_CLIENT_BOOKING, @stat));
  if((stat == 0) and ((stat = DL_CreateDirIfNotExists(path)) != 0))
    msgbox("Не удалось сформировать путь сохранения файлов: " + path + "\n Убедитесь в правильности настройки СОФР " + REGPATH_CLIENT_BOOKING);
  end;

  if(stat == 0)
    path = DL_GetFullPath(GetStrRegVal(REGPATH_CLIENT_ONLINE, @stat));
  end;
  if((stat == 0) and ((stat = DL_CreateDirIfNotExists(path)) != 0))
    msgbox("Не удалось сформировать путь сохранения файлов: " + path + "\n Убедитесь в правильности настройки СОФР " + REGPATH_CLIENT_ONLINE);
  end;

  if(stat == 0)
    path = DL_GetFullPath(GetStrRegVal(REGPATH_CLIENTS, @stat));
  end;
  if((stat == 0) and ((stat = DL_CreateDirIfNotExists(path)) != 0))
    msgbox("Не удалось сформировать путь сохранения файлов: " + path + "\n Убедитесь в правильности настройки СОФР " + REGPATH_CLIENTS);
  end;

  if(stat == 0)
    path = DL_GetFullPath(GetStrRegVal(REGPATH_FATCA, @stat));
  end;
  if((stat == 0) and ((stat = DL_CreateDirIfNotExists(path)) != 0))
    msgbox("Не удалось сформировать путь сохранения файлов: " + path + "\n Убедитесь в правильности настройки СОФР " + REGPATH_FATCA);
  end;

  return stat;
end;

/**
@brief      Переформирование сообщений FATCA СПБ с выводом протокола
*/
private macro ReCreateFatcaMsgSPB()
  var query, cmd, DataSet, cnt = 0, i = 0;
  var SystDate, SystTime;

  GetSystDateTime(@SystDate, @SystTime);

  if(Construct())
    return;
  end;

  InitProgress(1, "Отбор договоров для пересоздания FATCA СПБ", "Отбор договоров для пересоздания FATCA СПБ");

  query = " SELECT dlcontr.t_dlcontrid, sfcontr.t_number, party.t_shortname, msg.t_createdate "
        + "   FROM ddlcontrmsg_dbt msg, ddlcontr_dbt dlcontr, dsfcontr_dbt sfcontr, dparty_dbt party "
        + "  WHERE msg.t_kind = " + OBJTYPE_DLCONTRMSG_FATCAA
        + "    AND msg.t_sendmesstate = 302 /*Ожидает подтверждения биржи*/ " 
        + "    AND msg.t_dlcontrid = dlcontr.t_dlcontrid "
        + "    AND dlcontr.t_sfcontrid = sfcontr.t_id "
        + "    AND sfcontr.t_dateclose = TO_DATE('01.01.0001','dd.mm.yyyy') " //Только открытые ДБО
        + "    AND party.t_partyid = sfcontr.t_partyid "
        + "    AND NOT EXISTS (SELECT 1 " //Сообщение FATCA не было подтверждено позднее
        + "                      FROM ddlcontrmsg_dbt msg1 "
        + "                     WHERE msg1.t_dlcontrid = msg.t_dlcontrid "
        + "                       AND msg1.t_kind = msg.t_kind "
        + "                       AND msg1.t_sendmesstate = 310 /*Подтверждено биржей*/ "
        + "                       AND   (msg1.t_createdate > msg.t_createdate "
        + "                          OR (msg1.t_createdate = msg.t_createdate AND msg1.t_createtime = msg.t_createtime))) "
        + "    AND EXISTS (SELECT 1 " //У клиента есть второй договор
        + "                  FROM ddlcontr_dbt dlcontr1, dsfcontr_dbt sfcontr1 "
        + "                 WHERE sfcontr1.t_partyid = sfcontr.t_partyid "
        + "                   AND dlcontr1.t_sfcontrid = sfcontr1.t_id "
        + "                   AND dlcontr1.t_dlcontrid != dlcontr.t_dlcontrid) "
        + "    ORDER BY msg.t_createdate ";
  cmd = DL_RSDCommand(query);

  cnt = cmd.GetCount();
  UseProgress(1);
  RemProgress();

  if(cnt > 0)
    InitProgress(cnt, "Пересоздание FATCA СПБ", "Пересоздание FATCA СПБ");

    ReplaceMacro("msgbox", "save_msgbox");

    DataSet = cmd.Execute();

             [                              Договора, по которым выполнено переформирование FATCA СПБ
             Дата выполнения: #############
             Время начала выполнения: #############
             Отобрано записей: #############
             ]
             (SystDate:f, SystTime, cnt);

    println("");
    println("┌──────────────────────────────┬────────────────────────────────┬─────────────────────┐");
    println("│        Номер договора        │             Клиент             │  Дата формирования  │");
    println("│                              │                                │  прошлого сообщения │");
    println("├──────────────────────────────┼────────────────────────────────┼─────────────────────┤");

    i = 0;
    while(DataSet.moveNext())
       m_Msg = "";
       usr_ReCreateMSG(DataSet.DlContrID, 151337, OBJTYPE_DLCONTRMSG_FATCAA); // Собственно, сама процедура переформирования

       [│##############################│################################│#####################│#############################################################################]
       (string(DataSet.Number):c,   
        string(DataSet.ShortName):c,   
        string(date(DataSet.CreateDate)):c,
        string(m_Msg):l:w        
       );

       i = i + 1;

       if(i < cnt)
         println("├──────────────────────────────┼────────────────────────────────┼─────────────────────┤");
       end;

       UseProgress(i);
    end;

    println("└──────────────────────────────┴────────────────────────────────┴─────────────────────┘");

    ReplaceMacro("msgbox", NULL);
    RemProgress();
  else
    println("Договоров для переформирования FATCA СПБ не найдено");
  end;

OnError(er)
  ReplaceMacro("msgbox", NULL);
  msgbox("Ошибка: " + er.Message);
end;

ReCreateFatcaMsgSPB();
