/*
$Name:        reprecalcNDLF.mac
$Module:      Ценные бумаги
$Description: Изенение НОБ в результате пересчета
*/
import secinter, sp_categ, nptxfun, "sprepfun.mac";

private macro PrintClientList(PrefStr:string)
  var query, cmd, DataSet, cnt = 0, i = 0;
  var MesStr = "";
  var sizeH1_1 = 15, sizeH1_2 = 45, sizeH1_3 = 16, sizeH1_4 = 12, sizeH1_5 = 14, sizeH1_6 = 14, sizeH1_7 = 14, sizeH1_8 = 46;
  var sizeHeader = (sizeH1_1+1) + (sizeH1_2+1) + (sizeH1_3+1) + (sizeH1_4+1) + (sizeH1_5+1) + (sizeH1_6+1) + (sizeH1_7+1) + (sizeH1_8+1);

  var Rep = CMakeReport();

  Rep.AddPrintCell("Клиенты, по которым выполнялся пересчет НОБ", sizeHeader, 0, "c:ex_FS(b)", REP_ELEM_STR);
  Rep.AddStr();
  Rep.AddEmptyStr(1, sizeHeader);

  query =   " select op.t_Client, EXTRACT(YEAR FROM op.t_EndRecalcDate) as t_Year, op.t_Code, op.t_Status, pt.t_Name, "
          + "        (select obj.t_code from dobjcode_dbt obj where obj.t_objecttype = 3 and obj.t_codekind = 1 and obj.t_objectid = op.t_Client and obj.t_state = 0) as ClientCode, "
          + "        (select NVL(SUM(obj.t_Sum0),0) from dnptxobdc_dbt odc, dnptxobj_dbt obj where odc.t_DocID = op.t_ID and obj.t_ObjID = odc.t_ObjID and obj.t_Kind = "+TXOBJ_BASEG2+") as NewNOB, "
          + "        (select NVL(SUM(obj.t_Sum0),0) from dnptxobjbc_dbt obj where obj.t_DocID = op.t_ID and obj.t_Kind = "+TXOBJ_BASEG2+") as OldNOB, "
          + "        (select NVL(LISTAGG(TRIM(ms.t_Message), '\n ') WITHIN GROUP (ORDER BY ms.t_ID ASC), CHR(1)) from dnptxmes_dbt ms where ms.t_DocID = op.t_ID and ms.t_Type = 10) as MesStr, "
          + "        NVL((select op1.t_Code "
          + "               from dnptxbc_dbt bc, dnptxbc_dbt bc1, doproper_dbt oproper1, dnptxop_dbt op1  "
          + "              where bc.t_ID_Operation = oproper.t_ID_Operation "
          + "                and bc.t_Action = 0 " //Создание
          + "                and bc1.t_ObjKind = bc.t_ObjKind "
          + "                and bc1.t_ObjID = bc.t_ObjID "
          + "                and bc1.t_Action = 2 " //Удаление
          + "                and oproper1.t_ID_Operation = bc1.t_ID_Operation "
          + "                and op1.t_DocKind = oproper1.t_DocKind "
          + "                and op1.t_ID = TO_NUMBER(oproper1.t_DocumentID) "
          + "                and instr(op1.t_Code, '"+PrefStr+"') > 0 "
          + "                and op1.t_Recalc = 'X' "
          + "                and op1.t_ID <> op.t_ID "
          + "                and ROWNUM = 1 "
          + "            ), CHR(1)) as DelOpDocCode "
          + "   from dnptxop_dbt op, dparty_dbt pt, doproper_dbt oproper "
          + "  where op.t_DocKind = 4605 "
          + "    and op.t_Recalc = 'X' "
          + "    and instr(op.t_Code, '"+PrefStr+"') > 0 "
          + "    and pt.t_PartyID = op.t_Client "
          + "    and oproper.t_DocKind = op.t_DocKind "
          + "    and oproper.t_DocumentID = LPAD(op.t_ID, 34, '0') "
          + " order by op.t_Client, EXTRACT(YEAR FROM op.t_EndRecalcDate) ";

  cmd = DL_RSDCommand(query);

  cnt = cmd.GetCount();

  if(cnt > 0)

    DataSet = cmd.Execute();

    Rep.AddPrintCell("Код клиента", sizeH1_1, 0, "l:ex_FS(b)");
    Rep.AddPrintCell("ФИО", sizeH1_2, 0, "l:w:ex_FS(b)");
    Rep.AddPrintCell("Код операции пересчета", sizeH1_3, 0, "l:ex_FS(b)");
    Rep.AddPrintCell("Налоговый период", sizeH1_4, 0, "l:ex_FS(b)");
    Rep.AddPrintCell("НОБ ДО пересчета", sizeH1_5, 0, "l:ex_FS(b)");
    Rep.AddPrintCell("НОБ ПОСЛЕ пересчета", sizeH1_6, 0, "l:ex_FS(b)");
    Rep.AddPrintCell("Изменение НОБ", sizeH1_7, 0, "l:ex_FS(b)");
    Rep.AddPrintCell("Сообщение", sizeH1_8, 0, "l:ex_FS(b)");
    Rep.AddStr();

    i = 0;
    while(DataSet.moveNext())
       MesStr = "";

       if(DataSet.Status != 2)
         MesStr = "Операция не закрыта!";
       end;

       if(MesStr != "")
         MesStr = MesStr + "\n"+ DataSet.MesStr;
       end;

       if(DataSet.DelOpDocCode != "")
         if(MesStr != "")
           MesStr = MesStr + "\n";
         end;
         MesStr = MesStr + "ВНИМАНИЕ! Расчеты по операции удалены при выполнении более ранней операции с кодом "+DataSet.DelOpDocCode+". Необходимо выполнить пересчет заново, запустив операцию вручную.";
       end;

       Rep.AddPrintCell(string(DataSet.ClientCode), sizeH1_1, 0, "с");
       Rep.AddPrintCell(string(DataSet.Name), sizeH1_2, 0, "l:w");
       Rep.AddPrintCell(string(DataSet.Code), sizeH1_3, 0, "с");
       Rep.AddPrintCell(string(int(DataSet.Year)), sizeH1_4, 0, "с");
       Rep.AddPrintCell(money(DataSet.OldNOB), sizeH1_5, 0, "с");
       Rep.AddPrintCell(money(DataSet.NewNOB), sizeH1_6, 0, "с");
       Rep.AddPrintCell(money(DataSet.NewNOB-DataSet.OldNOB), sizeH1_7, 0, "с");
       Rep.AddPrintCell(MesStr, sizeH1_8, 0, "l:w");
       Rep.AddStr();

       i = i + 1;
    end;

  else
    Rep.AddPrintCell("Клиентов с изменением НОБ не найдено", sizeHeader, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
  end;

  Rep.PrintWinRep();
  Rep.ShowWinRep();

end;

var PrefStr = "";
if((GetString(PrefStr, "Введите префикс операция пересчета НОБ", 10)) and (trim(PrefStr) != ""))
  PrintClientList(PrefStr);
end;

