/*******************************************************************************
 DESCRIPTION  :   Обновление 2027->2028 (302-П)
                  Процедура коррекции сделок
 PROGRAMMED BY:   Леонов И. Е.
 CREATION DATE:   22.11.07
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac", "cb_sql.mac", "secinter.mac", "spserv.mac", "sp_class.mac", "sp_catac.mac", "sp_car2.mac";

/*Номера полей в форме */
CONST PNFLD_NTGREG_DATE        = 0,
      PNFLD_NTGREG_FLAG_1      = 1,
      PNFLD_NTGREG_FLAG_2      = 2,
      PNFLD_NTGREG_FLAG_3      = 3,
      PNFLD_NTGREG_FLAG_REPORT = 4;

PRIVATE VAR FormData;

CONST
   PM_WRTSUM_KIND_U            = 0,   // Не определено
   PM_WRTSUM_KIND_S            = 10,  // Продажа
   PM_WRTSUM_KIND_B            = 20,  // Покупка
   PM_WRTSUM_KIND_FS           = 30,  // Списание лота
   PM_WRTSUM_KIND_FB           = 40,  // Зачисление лота 
   PM_WRTSUM_KIND_DI           = 50,  // Погашение выпуска
   PM_WRTSUM_KIND_DC           = 60,  // Погашение купона
   PM_WRTSUM_KIND_DP           = 70,  // Частичное погашение
   PM_WRTSUM_KIND_MS           = 80,  // Перемещение из портфеля
   PM_WRTSUM_KIND_MB           = 90,  // Перемещение в портфель
   PM_WRTSUM_KIND_DS           = 100, // Списание в конвертации ДР
   PM_WRTSUM_KIND_DB           = 110, // Зачисление в конвертации ДР
   PM_WRTSUM_KIND_GS           = 120, // Списание в глобальной операции
   PM_WRTSUM_KIND_GB           = 130, // Зачисление в глобальной операции
   PM_WRTSUM_KIND_BSB          = 140, // Зачисление в покупке с обр. продажей (1 ч.)
   PM_WRTSUM_KIND_BSS          = 150, // Списание в покупке с обр. продажей (2 ч.)
   PM_WRTSUM_KIND_SBS          = 160, // Списание в продаже с обр. продажей (1 ч.)
   PM_WRTSUM_KIND_SBB          = 170, // Зачисление в продаже с обр. продажей (2 ч.)
   PM_WRTSUM_KIND_RRWAB1       = 180, // RRWAB- Зачисление в Репо обратном БПП (1 ч.)
   PM_WRTSUM_KIND_RRWAS2       = 190, // RRWAS - Списание в Репо обратном БПП (2 ч.)
   PM_WRTSUM_KIND_RRWAS1       = 200, // RRWAS - Списание в Репо прямом БПП (1 ч.)
   PM_WRTSUM_KIND_RRWAB2       = 210, // RRWAB - Зачислениев Репо прямом БПП (2 ч.)
   PM_WRTSUM_KIND_LCB          = 220, // Привлечение займа (1 ч.)
   PM_WRTSUM_KIND_LCS          = 230, // Возврат привлеченного займа (2.ч.)
   PM_WRTSUM_KIND_LPS          = 240, // Размещение займа (1 ч.)
   PM_WRTSUM_KIND_LPB          = 250, // Возврат размещенного займа (2.ч.)
   PM_WRTSUM_KIND_PB           = 260, // Получение паев
   PM_WRTSUM_KIND_RRAB1        = 270, // RRAB - Зачисление в Репо обратном ПП (1 ч.)
   PM_WRTSUM_KIND_RRAS2        = 280, // RRAS - Списание в Репо обратном ПП (2 ч.)
   PM_WRTSUM_KIND_RRAS1        = 290, // RRAS - Списание в Репо прямом ПП (1 ч.)
   PM_WRTSUM_KIND_RRAB2        = 300;  // RRAB - Зачислениев Репо прямом ПП (2 ч.)

/*Новые виды операций РЕПО*/
CONST NEW_DEALTYPE_REPO_BACK_ORCB = 2121, /*Обратное РЕПО ОРЦБ без признания ц/б*/
      NEW_DEALTYPE_REPO_ORCB      = 2126, /*Прямое   РЕПО ОРЦБ без признания ц/б*/
      NEW_DEALTYPE_REPO_BACK      = 2131, /*Обратное РЕПО без признания ц/б*/
      NEW_DEALTYPE_REPO           = 2136; /*Прямое   РЕПО без признания ц/б*/

PRIVATE CLASS (DL_CPanel) SP_DealCorrect_Form()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_NTGREG_DATE,        DL_PNFLD_BEGINDATE, @DL_FldProc_BeginDate, GetDateAfterWorkDays( DATE(1,1,2008), 0 ) );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_1,      DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,  "X" );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_2,      DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,  "X" );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_3,      DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,  "X" );
     AddFieldProc( 0, PNFLD_NTGREG_FLAG_REPORT, DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox,  "X" );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;
                  
  initDL_CPanel( "sp_conv.lbr", "pcorrect" ); 
END;

PRIVATE VAR __DealCode;

PRIVATE CLASS SP_DealCorrect

  MACRO BeginReport()
[ 
              Процедура коррекции сделок
Дата: ##########
Выполнить переквалификацию сделок РЕПО и ОП в сделки Репо БПП: #
Выполнить коррекцию лотов: #
Выполнить формирование сумм по оплаченным комиссиям: #
](FormData.OperDate,  
  IIF( FormData.Flag1 == "X", "Да", "Нет"),  
  IIF( FormData.Flag2 == "X", "Да", "Нет"),  
  IIF( FormData.Flag3 == "X", "Да", "Нет")
 );

     if( FormData.CreateReport == "X" )
[┌─────────────────────────────┬──────────────────────────────────────────────────────────────────────────────────────┐
 │             Сделка          │                                  Отчет о выполнении                                  │];
     end;
  END;

  MACRO PrintMessage( Deal:STRING, Mes:STRING)
     if( FormData.CreateReport == "X" )

[├─────────────────────────────┼──────────────────────────────────────────────────────────────────────────────────────┤
 │#############################│######################################################################################│
](Deal, Mes:w);

     end;
  END;

  PRIVATE MACRO PrintSubHeader( Mes:STRING)
     if( FormData.CreateReport == "X" )
[├─────────────────────────────┴──────────────────────────────────────────────────────────────────────────────────────┤
 │####################################################################################################################│
](Mes:w);

     end;
  END;

  MACRO EndReport()
     if( FormData.CreateReport == "X" )
[└─────────────────────────────┴──────────────────────────────────────────────────────────────────────────────────────┘
];
     end;
  END;

  PRIVATE MACRO ExistNOSS( FIID:INTEGER, DealDate:DATE ):BOOL
    VAR NumInList = "", ValidFromDate = DATE(0,0,0);

    return (    (GetMainObjAttr( null, OBJTYPE_AVOIRISS, L_Z( FIID, 10 ), 27/*Возможность надежного расчета TCC*/, null, null, NumInList, DealDate, ValidFromDate) == true)
            AND (ValidFromDate <= DealDate)
            AND (NumInList == "1")
           );
  END;

  MACRO FormError( StrErr:STRING, err, command:RSDCommand ):STRING
    VAR i = 0;

    StrErr = "!!! " + StrErr;

    if( err != NULL )
       StrErr = StrErr + "\nСтрока: " + string(err.line) + "  " + err.message + "\n";
    end;

    if( command != NULL )
       while( i < command.connection.environment.ErrorCount )
           StrErr = StrErr + command.connection.environment.Error(i).Descr + "\n";
           i = i + 1;
       end;
    end;
    return StrErr;
  OnError
    return StrErr;
  end;

  PRIVATE VAR Party_ForGet = TRecHandler( "party.dbt" );
  MACRO GetPartyName( PartyID:INTEGER, LongName:@STRING ) :STRING

    if( ПолучитьСубъекта( PartyID, Party_ForGet ) == 0 )
       LongName = Party_ForGet.rec.Name;
       return Party_ForGet.rec.ShortName;
    end;
    return "";
  end;

  PRIVATE VAR Payment  = TRecHandler( "pmpaym.dbt" );
  PRIVATE VAR PaymPropDT = TRecHandler( "pmprop.dbt" );
  PRIVATE VAR PaymPropCT = TRecHandler( "pmprop.dbt" );
  private macro СоздатьПлатежПроцентов( FD:SPFirstDoc, dat:DATE, Summa:MONEY )
    VAR PaymentID, StrErr = "", FIID = FD.dl_leg.rec.CFI, MarketAcc, IsSale, 
        PayerName, PayerBankName, ReceiverName, ReceiverBankName,
        pmobj = RsbPayment( 0 );

    /*Дублировать заполнение полей через Payment (TRecHandler) приходится из за отсутствия
      FillPaymentAccounts в классе платежа*/
    Payment.Clear();
    Payment.rec.Purpose    = PMObj.Purpose      = PM_PURP_PERCENT;  
    Payment.rec.SubPurpose = PMObj.SubPurpose   = 0;
    Payment.rec.PaymStatus = PMObj.PaymStatus   = PM_PREPARING;
    Payment.rec.DocKind    = PMObj.DocKind      = FD.tick.rec.BofficeKind;
    Payment.rec.DocumentID = PMObj.DocumentID   = FD.tick.rec.DealID;
    Payment.rec.PayFIID    = FIID;
    Payment.rec.FIID       = FIID;
    Payment.rec.BaseFIID   = PMObj.BaseFIID     = FIID;
    Payment.rec.ValueDate  = PMObj.ValueDate    = dat;
    Payment.rec.Department = PMObj.Department   = FD.tick.rec.Department;
    Payment.rec.BaseAmount = PMObj.BaseAmount   = Summa;
    Payment.rec.IsPlanPaym = PMObj.IsPlanPaym   = "X";
                             PMObj.Ground       = "Платеж процентов по сделке " + FD.tick.rec.DealCode;

    PaymPropDT.Clear();
    PaymPropCT.Clear();
    PaymPropDT.rec.DebetCredit = PRT_Debet;
    PaymPropCT.rec.DebetCredit = PRT_Credit;
    Payment.rec.PayerBankID = Payment.rec.ReceiverBankID = {OurBank};

    if( FillPaymentsByDeal( FD.tick, Payment, PaymPropDT, PaymPropCT, NULL, false, StrErr ) )
       PrintMessage( FD.tick.rec.DealCode, "!!! Ошибка при формировании платежа процентов.|" +  StrErr );
       return 1;
    end;

    FillPaymentAccounts( Payment.rec.Payer, Payment.rec.Receiver, 0, Payment, PaymPropDT, PaymPropCT, StrErr );

    /*Дозаполнить параметры плательщика, если они не заполнилось из СПИ*/
    if( PaymPropDT.rec.CodeKind <= 0 ) 
       PaymPropDT.rec.CodeKind     = PTCK_CONTR;
       PMObj.PayerBankCorrCodeKind = PTCK_CONTR;
    end;

    if( Payment.rec.PayerBankID <= 0 ) 
       Payment.rec.PayerBankID = {OurBank};
       PaymPropDT.rec.BankCode = ПолучитьКодСубъекта( Payment.rec.PayerBankID, PaymPropDT.rec.CodeKind );
    end;

    /*Дозаполнить параметры получателя, если они не заполнилось из СПИ*/
    if( PaymPropCT.rec.CodeKind <= 0 ) 
       PaymPropCT.rec.CodeKind          = PTCK_CONTR;
       PMObj.ReceiverBankCorrCodeKind   = PTCK_CONTR;
    end;

    if( Payment.rec.ReceiverBankID < 0 ) 
       Payment.rec.ReceiverBankID   = {OurBank};
       PaymPropCT.rec.BankCode = ПолучитьКодСубъекта( Payment.rec.ReceiverBankID, PaymPropCT.rec.CodeKind );
    end;

    /*Параметры отправителя/получателя*/       
    GetPartyName( Payment.rec.Payer, @PayerName );
    GetPartyName( Payment.rec.PayerBankID, @PayerBankName );

    GetPartyName( Payment.rec.Receiver, @ReceiverName );
    GetPartyName( Payment.rec.ReceiverBankID, @ReceiverBankName );

    PMObj.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                     Payment.rec.PayerBankID, 
                     PaymPropDT.rec.CodeKind, 
                     PaymPropDT.rec.BankCode, 
                     PayerBankName,
                     "",
                     FIID, 
                     1/*CHAPT2*/, 
                     Payment.rec.PayerAccount, 
                     pmobj.Payer, 
                     PayerName, 
                     ПолучитьКодСубъекта( Payment.rec.Payer, PTCK_INN ) );

    PMObj.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                     Payment.rec.ReceiverBankID, 
                     PaymPropCT.rec.CodeKind, 
                     PaymPropCT.rec.BankCode, 
                     ReceiverBankName,
                     "",
                     FIID, 
                     1/*CHAPT2*/, 
                     Payment.rec.ReceiverAccount, 
                     pmobj.Receiver, 
                     ReceiverName, 
                     ПолучитьКодСубъекта( Payment.rec.Receiver, PTCK_INN ) );

    /*Плательщик*/
    PMObj.PayerFIID             = FIID;
    PMObj.PayerDpNode           = Payment.rec.PayerDpNode;

    PMObj.PayerBankCorrAcc      = PaymPropDT.rec.CorrAcc;
    PMObj.PayerBankCorrCode     = PaymPropDT.rec.CorrCode;
    PMObj.PayerBankCorrCodeKind = PaymPropDT.rec.CorrCodeKind;

    PMObj.PayerInOurBalance     = PaymPropDT.rec.InOurBalance;
    PMObj.PayerOurCorrAcc       = PaymPropDT.rec.OurCorrAcc;
    PMObj.PayerOurCorrID        = PaymPropDT.rec.OurCorrID;

    /*Получатель*/
    PMObj.ReceiverFIID             = FIID;
    PMObj.ReceiverDpNode           = Payment.rec.ReceiverDpNode;

    PMObj.ReceiverBankCorrAcc      = PaymPropCT.rec.CorrAcc;
    PMObj.ReceiverBankCorrCode     = PaymPropCT.rec.CorrCode;
    PMObj.ReceiverBankCorrCodeKind = PaymPropCT.rec.CorrCodeKind;

    PMObj.ReceiverInOurBalance     = PaymPropCT.rec.InOurBalance;
    PMObj.ReceiverOurCorrAcc       = PaymPropCT.rec.OurCorrAcc;
    PMObj.ReceiverOurCorrID        = PaymPropCT.rec.OurCorrID;

    PMObj.Actuate();
    PMObj.Update();

    Payment.rec.PaymentID = PMObj.PaymentID;
    Copy( FD.pmpc, Payment );

    if( FD.АктуализироватьПлатеж( FD.pmpc ) == true )
       ЗаполнитьНашСчетВПлатежеПоПроцентам( FD, dat );

       IsSale = SP_PaymIsSale( FD.pmpc, FD.tick );
       MarketAcc = ПолучитьСчетКонтрагентаВПлатеже( FD, IsSale );
      
       if( MarketAcc != "Корсчет" ) 
          if( ЗаполнитьСчетПлатежа( FD, FD.pmpc, "по процентам",
                                        dat, MarketAcc, null,
                                        IsSale, FD.tick.rec.PartyID) )
             СохранитьСчетаВПлатеже( FD.pmpc );
          end; 
       end;
    end;

    PrintMessage( FD.tick.rec.DealCode, "Создан платеж процентов (PaymentID = "+ Payment.rec.PaymentID + ")" );

    return 0;
  end; 

  PRIVATE MACRO UpdateDeal( RS, NewDealKind:INTEGER, NewBase:INTEGER, FD_1, FD_2 ):BOOL
    VAR cmd, ReturnIncomeKind, SumPC, StrErr;

    /*Установить признак возврата дохода по процентам и способ возврата - выплата для не ОРЦБ, списание для ОРЦБ*/
    if( (NewDealKind == NEW_DEALTYPE_REPO_BACK_ORCB) OR (NewDealKind == NEW_DEALTYPE_REPO_ORCB) )
       ReturnIncomeKind = SP_RETURNINCOME_WRTOFF;
    else
       ReturnIncomeKind = SP_RETURNINCOME_PAYMENT;
    end;

    cmd = RSDCommand( "UPDATE DDL_TICK_DBT SET t_PortfolioID_2 = -1, t_Flag5 = ?, t_ReturnIncomeKind = ? where t_DealID = ?" );
    cmd.addParam( "", RSDBP_IN, "X" );
    cmd.addParam( "", RSDBP_IN, ReturnIncomeKind );
    cmd.addParam( "", RSDBP_IN, RS.DealID );
    cmd.execute();

    SumPC = round( SP_GetPercentSum( FD_1.pm_avance, FD_1.pm_money, FD_2.pm_avance, FD_2.pm_money, FD_1.dl_leg, FD_2.dl_leg, FD_2.Group, FD_2.DateArray[DATE_DEALPAY_PLAN] ), 2);
    cmd = RSDCommand( "UPDATE DDL_LEG_DBT SET t_Base = ?, t_ReturnIncome = ? where (t_LegKind = ? OR t_LegKind = ?) AND t_LegID = 0 AND t_DealID = ? " );
    cmd.addParam( "", RSDBP_IN, IIF(NewBase == NULL, FD_1.dl_leg.rec.Base, NewBase ) );
    cmd.addParam( "", RSDBP_IN, SumPC );
    cmd.addParam( "", RSDBP_IN, LEG_KIND_DL_TICK );
    cmd.addParam( "", RSDBP_IN, LEG_KIND_DL_TICK_BACK );
    cmd.addParam( "", RSDBP_IN, RS.DealID );
    cmd.execute();

    if( SumPC != 0 )
       СоздатьПлатежПроцентов( FD_2, FD_2.DateArray[DATE_DEALPAY_PLAN], SumPC );
    end;

    return true;
  ONERROR(err)
    PrintMessage( RS.DealCode, FormError("Ошибка при изменении сделки (UpdateDeal)", err, cmd) );
    return false;
  END;

  PRIVATE VAR Party   = TRecHandler("party");
  PRIVATE MACRO GetNewBase( RS ):INTEGER
    if(ПолучитьСубъекта( RS.Contractor, Party ) == 0) 
       if( Party.rec.NotResident == SET_CHAR ) /*360, если к/агент - нерезидент*/
          return 1/*SP_KINDBASISCALC_360*/;
       else
          return 0/*SP_KINDBASISCALC_365*/;
       end;
    end;
    return NULL;
  END;

  PRIVATE MACRO InsertOperStatus( DocKind:INTEGER, ID_Operation:INTEGER, StatusKindID:INTEGER, NumValue:INTEGER )

    VAR cmd = RSDCommand( "{ CALL RSI_RsbOperation.SetOprStatusValue(?, ?, ?, ?) }" );
    cmd.addParam( "", RSDBP_IN, DocKind );
    cmd.addParam( "", RSDBP_IN, ID_Operation );
    cmd.addParam( "", RSDBP_IN, StatusKindID );
    cmd.addParam( "", RSDBP_IN, NumValue );
    cmd.execute();
  ONERROR(err)
    PrintMessage( __DealCode, FormError("Ошибка при вставке статуса операции (InsertOperStatus)", err, cmd) );
    return false;
  END;

  MACRO GetDateKindID( DocKind:INTEGER, NumberDate:INTEGER ):INTEGER

    VAR RS, cmd = RSDCommand( "SELECT NVL(t_DateKindID,-1) DateKindID from doprkdate_dbt where t_DocKind = ? and t_NumberDate = ?" );
    cmd.addParam( "", RSDBP_IN, DocKind );
    cmd.addParam( "", RSDBP_IN, NumberDate );
    cmd.execute();
    RS = TRsbDataSet(cmd);
    if( RS.movenext() )
      return RS.DateKindID;
    end;
    return -1;
  END;

  PRIVATE MACRO InsertOperDate( DocKind:INTEGER, ID_Operation:INTEGER, NumberDate:INTEGER, OperDate:Date )
    VAR cmd, DateKindID;

    DateKindID = GetDateKindID( DocKind, NumberDate );
    if( DateKindID >= 0 )
       cmd = RSDCommand( "INSERT INTO DOPRDATES_DBT( "+
                         "    t_ID_Operation,     "+// Идентификатор экземпляра операции
                         "    t_DateKindID,       "+// Номер (вида) даты
                         "    t_Date              "+// Значение даты
                         ") VALUES(?,?,?)");

       cmd.addParam( "", RSDBP_IN, ID_Operation );
       cmd.addParam( "", RSDBP_IN, DateKindID );
       cmd.addParam( "", RSDBP_IN, IIF(OperDate == NULL, Date(0,0,0), OperDate) );
       cmd.execute();
    end;

  ONERROR(err)
    PrintMessage( __DealCode, FormError("Ошибка при вставке даты операции (InsertOperDate)", err, cmd) );
    return false;
  END;

  PRIVATE MACRO PlanOperStep( ID_Operation, Kind_Operation )
    VAR RS, cmd = RSDCommand(       

      "DECLARE "+
      "  v_err INTEGER; "+
      "BEGIN "+
      "  DELETE FROM doprbstep_tmp; "+
      "  INSERT INTO doprbstep_tmp (t_ID_Operation, t_Previous_Step, t_Kind_Operation) VALUES(?,0,?); "+
      "  v_err := rsi_rsboperation.BuildBlockStep;" +
      "END;"
                            );
    cmd.addParam( "", RSDBP_IN, ID_Operation );
    cmd.addParam( "", RSDBP_IN, Kind_Operation );
    cmd.execute();
    return true;

  ONERROR(err)
    PrintMessage( __DealCode, FormError("Ошибка при вставке даты операции (PlanOperStep)", err, cmd) );
    return false;
  END;

  MACRO ExistsDepoDraft( AvrPaymentID:INTEGER ):BOOL

    VAR RS, cmd = RSDCommand( "Select Draft.t_Status DraftStatus " +
                             "  From dspdrprop_dbt Prop, dspdraft_dbt Draft " +
                             " Where Prop.t_PaymentID = ? " +
                             "   And Draft.t_AutoKey = Prop.t_DraftID " );
    cmd.addParam( "", RSDBP_IN, AvrPaymentID );
    cmd.execute();
    RS = TRsbDataSet(cmd);
    if( RS.movenext() )
      return true;
    end;
    return false;
  END;
  
  PRIVATE MACRO GetOprStatus( ID_Operation:INTEGER, StatusKindID:INTEGER )

    VAR RS, cmd = RSDCommand( "Select t_NumValue " +
                              "  From DOPRCURST_DBT " +
                              " Where t_ID_Operation = ? " +
                              "   And t_StatusKindID = ? " );
    cmd.addParam( "", RSDBP_IN, ID_Operation );
    cmd.addParam( "", RSDBP_IN, StatusKindID );
    cmd.execute();
    RS = TRsbDataSet(cmd);
    if( RS.movenext() )
      return RS.NumValue;
    end;

    return -1;
  END;

  PRIVATE MACRO DropWrtSum( DealID:INTEGER, PmPurpose:INTEGER )
    VAR cmd, RS;

    cmd = RSDCommand("  select count(1) NumLot "+
                     "    from dpmwrtsum_dbt "+
                     "   where    t_DocKind = 29 "+
                     "        AND t_DocID   IN ( SELECT t_PaymentID "+
                     "                             FROM dpmpaym_dbt "+
                     "                            WHERE     t_Purpose    = ? "+
                     "                                  AND t_DocumentID = ? "+
                     "                         )"
                    );

    cmd.addParam( "", RSDBP_IN, PmPurpose );
    cmd.addParam( "", RSDBP_IN, DealID );
    cmd.Execute();
    RS = TRsbDataSet(cmd);
    if( RS.movenext() AND (RS.NumLot > 0) )
       PrintMessage( __DealCode, "Удален лот по "+ IIF(PmPurpose==BAi, "первой части", "второй части") +" сделки " + __DealCode );

       cmd = RSDCommand(" delete from dpmwrtsum_dbt "+
                        "   where    t_DocKind = 29 "+
                        "        AND t_DocID   IN ( SELECT t_PaymentID "+
                        "                             FROM dpmpaym_dbt "+
                        "                            WHERE     t_Purpose    = ? "+
                        "                                  AND t_DocumentID = ? "+
                        "                         )"
                       );

       cmd.addParam( "", RSDBP_IN, PmPurpose );
       cmd.addParam( "", RSDBP_IN, DealID );
       cmd.Execute();
    end;
  END;

  PRIVATE VAR Oproper = TBFile( "oproper.dbt", "W", 1 );
  PRIVATE MACRO CreateOperationDeal( RS, NewDealKind:INTEGER, FD_1:SPFirstDoc ):BOOL
    VAR cmd, err, ID_Operation, OperStatus, FD_2;

    Oproper.Clear();
    Oproper.rec.DocKind    = RS.BofficeKind;   
    Oproper.rec.DocumentID = L_Z(RS.DealID, 34);
    if( Oproper.GetEQ() )
       /*если по сделке есть операция, то ничего с ней не делаем*/
       return true;
    end;

    cmd = RSDCommand( "UPDATE DDL_TICK_DBT SET t_DealType = ? where t_DealID = ?" );
    cmd.addParam( "", RSDBP_IN, NewDealKind );
    cmd.addParam( "", RSDBP_IN, RS.DealID );
    cmd.execute();
    PrintMessage( RS.DealCode, "Изменен вид сделки на " + NewDealKind );

    cmd = RSDCommand( 
       "INSERT INTO DOPROPER_DBT( "+
       "    t_Kind_Operation,         "+// Вид операции
       "    t_Init_Oper,              "+// Операционист, инициировавший операцию
       "    t_Start_Date,             "+// Операционная дата инициирования операции
       "    t_Syst_Date,              "+// Системная дата инициирования операции
       "    t_Syst_Time,              "+// Системное время инициирования операции
       "    t_DocKind,                "+// Вид первичного документа
       "    t_DocumentID,             "+// Ссылка на конкретный первичный документ
       "    t_IsNew,                  "+// Операция с вводом первичного документа
       "    t_Completed,              "+// Признак завершенной операции
       "    t_End_Date,               "+// Дата окончания операции
       "    t_Sort,                   "+// Пользовательское поле сортировки
       "    t_BOActions,              "+// Флаг обработки для планировщика
       "    t_BOCode,                 "+// Код бэк-офиса - источника
       "    t_KO_ManualSelection      "+// Признак ручного задания вида операции
       ") VALUES(?,?,?,?,?,?,?,chr(0),?,?,chr(1),?,?,chr(0))");
    cmd.addParam( "", RSDBP_IN, NewDealKind );                   //t_Kind_Operation,   
    cmd.addParam( "", RSDBP_IN, RS.Oper );                       //t_Init_Oper,        
    cmd.addParam( "", RSDBP_IN, SQL_ConvTypeDate(RS.DealDate) ); //t_Start_Date,       
    cmd.addParam( "", RSDBP_IN, Date() ); //t_Syst_Date,        
    cmd.addParam( "", RSDBP_IN, time() ); //t_Syst_Time,        
    cmd.addParam( "", RSDBP_IN, RS.BOfficeKind );                //t_DocKind,          
    cmd.addParam( "", RSDBP_IN, L_Z(RS.DealID, 34) );            //t_DocumentID,       
    cmd.addParam( "", RSDBP_IN, IIF(RS.DealStatus == DL_CLOSED,"X","")  ); //t_Completed,        
    cmd.addParam( "", RSDBP_IN, SQL_ConvTypeDate(RS.CloseDate) ); //t_End_Date,         
    cmd.addParam( "", RSDBP_IN, 0 ); //t_BOActions,        
    cmd.addParam( "", RSDBP_IN, 0 ); //t_BOCode,           
    cmd.execute();

    Oproper.Clear();
    Oproper.rec.DocKind    = RS.BofficeKind;   
    Oproper.rec.DocumentID = L_Z(RS.DealID, 34);
    if( Oproper.GetEQ() )
       ID_Operation = Oproper.rec.ID_Operation;
       PrintMessage( RS.DealCode, "Создана операция вида " + NewDealKind );
    else
       PrintMessage( RS.DealCode, "!!! Ошибка при создании операции (CreateOperationDeal)" );
       return false;
    end;

    FD_2 = SPFirstDoc( LEG_KIND_DL_TICK_BACK, RS.DealID );
    FD_1.ПолучитьДатыСделки( true, false, FD_2 );
    FD_2.ПолучитьДатыСделки( true, false, FD_1 );

    /* Документооборот ДО: Открыт*/
    InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_DO, SP_OPERSTATUS_SECURDEALS_DO_OPEN );

    /* Оплата комиссий КО: Рассчитать, Оплатить, Завершить*/
    if( bAND( RS.BITMASK1, DL_LEG_PAY_BANK_ONCE ) OR
        bAND( RS.BITMASK1, DL_LEG_PAY_AGENT_ONCE ) 
      )
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_KO, SP_OPERSTATUS_SECURDEALS_KO_FINISH /* Завершить*/ );
    elif( bAND( RS.BITMASK1, DL_LEG_ADD_AGENT_ONCE ) OR
          bAND( RS.BITMASK1, DL_LEG_ADD_BANK_ONCE )
        )
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_KO, SP_OPERSTATUS_SECURDEALS_KO_PAY /*Оплатить*/ );
    elif( bAND( RS.BITMASK1, DL_LEG_CALC_BANK_ONCE) OR
          bAND( RS.BITMASK1, DL_LEG_CALC_AGENT_ONCE)
        )
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_KO, SP_OPERSTATUS_SECURDEALS_KO_CALC/* Рассчитать*/ );
    end;

    if( (RS.Flag2 != SET_CHAR) OR 
        ( bAND( RS.BITMASK1, DL_LEG_PREPARE_CONTR) OR 
          bAND( RS.BITMASK2, DL_LEG_PREPARE_CONTR) 
        )
      )/*договор не нужен или уже подписан*/
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_OD, SP_OPERSTATUS_SECURDEALS_OD_FINISH );
    else
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_OD, SP_OPERSTATUS_SECURDEALS_OD_FORM );
    end;

    /*Если есть аванс по 1 ч А1 = "Оплатить", иначе А1 = "Завершить"*/
    if( (FD_1.ExistAvance == true) AND 
        (ПлатежЗакрыт(FD_1.pm_avance.rec) == false)
      )
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_A1, SP_OPERSTATUS_SECURDEALS_A1_PAY);
    else
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_A1, SP_OPERSTATUS_SECURDEALS_A1_FINISH);
    end;

    if( (FD_2.ExistAvance == true) AND 
        (ПлатежЗакрыт(FD_2.pm_avance.rec) == false)
      )
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_A2, SP_OPERSTATUS_SECURDEALS_A2_PAY);
    else
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_A2, SP_OPERSTATUS_SECURDEALS_A2_FINISH);
    end;

    if( НужноПредФормПоручДЕПО(FD_1, err) )
       if( ExistsDepoDraft( FD_1.pm_avoir.rec.PaymentID ) )
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_D1, SP_OPERSTATUS_SECURDEALS_D1_FINISH);
       else
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_D1, SP_OPERSTATUS_SECURDEALS_D1_FORM);
       end;
       InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALINSDEPODRAFT,   FD_1.pm_avoir.rec.ValueDate );
    else
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_D1, SP_OPERSTATUS_SECURDEALS_D1_FINISH);
    end;

    if( НужноПредФормПоручДЕПО(FD_2, err) )
       if( ExistsDepoDraft( FD_2.pm_avoir.rec.PaymentID ) )
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_D2, SP_OPERSTATUS_SECURDEALS_D1_FINISH);
       else
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_D2, SP_OPERSTATUS_SECURDEALS_D1_FORM);
       end;
       InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALINSDEPODRAFT_2, FD_2.pm_avoir.rec.ValueDate );
    else
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_D2, SP_OPERSTATUS_SECURDEALS_D1_FINISH);
    end;

    if( ПлатежЗакрыт( FD_1.pm_avoir.rec ) ) 
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_P1, SP_OPERSTATUS_SECURDEALS_P1_FINISH);
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_I1, SP_OPERSTATUS_SECURDEALS_I1_FINISH);
    else
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_P1, SP_OPERSTATUS_SECURDEALS_P1_SET);
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_I1, SP_OPERSTATUS_SECURDEALS_I1_RUN);
    end;

    if( ПлатежЗакрыт( FD_2.pm_avoir.rec ) ) 
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_P2, SP_OPERSTATUS_SECURDEALS_P2_FINISH);
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_I2, SP_OPERSTATUS_SECURDEALS_I2_FINISH);
    else
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_P2, SP_OPERSTATUS_SECURDEALS_P2_SET);
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_I2, SP_OPERSTATUS_SECURDEALS_I2_RUN);
    end;

    if( ПлатежЗакрыт( FD_1.pm_money.rec ) ) 
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_O1, SP_OPERSTATUS_SECURDEALS_O1_FINISH);
    else
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_O1, SP_OPERSTATUS_SECURDEALS_O1_PAY);
    end;

    if( ПлатежЗакрыт( FD_2.pm_money.rec ) ) 
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_O2, SP_OPERSTATUS_SECURDEALS_O2_FINISH);
    else
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_O2, SP_OPERSTATUS_SECURDEALS_O2_PAY);
    end;

    /*Учет затрат по 1 ч online N2: Выполнить, Завершить        */
    if( FD_1.GetModeWriteOff() == "X" ) /*  "X" - online, "" - offline  */
       if( FD_1.dl_leg.rec.OperState == DL_LEG_FORM ) /*уже был учет себестоимости по 1 ч*/
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_N1, SP_OPERSTATUS_SECURDEALS_N1_FINISH);
       else
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_N1, SP_OPERSTATUS_SECURDEALS_N1_RUN);
       end;
    else /*Учет затрат по 1 ч offline F2: Выполнить, Завершить       */
       if(FD_1.dl_leg.rec.OperState == DL_LEG_FORM) /*уже был учет себестоимости по 1 ч*/
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_F1, SP_OPERSTATUS_SECURDEALS_F1_FINISH);
       else
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_F1, SP_OPERSTATUS_SECURDEALS_F1_RUN);
       end;
    end;

    /*Учет затрат по 2 ч online N2: Выполнить, Завершить        */
    if( FD_2.GetModeWriteOff() == "X" ) /*  "X" - online, "" - offline  */
       if(FD_2.dl_leg.rec.OperState == DL_LEG_FORM) /*уже был учет себестоимости по 2 ч*/
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_N2, SP_OPERSTATUS_SECURDEALS_N2_FINISH);
       else
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_N2, SP_OPERSTATUS_SECURDEALS_N2_RUN);
       end;
    else /*Учет затрат по 2 ч offline F2: Выполнить, Завершить       */
       if(FD_2.dl_leg.rec.OperState == DL_LEG_FORM) /*уже был учет себестоимости по 2 ч*/
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_F2, SP_OPERSTATUS_SECURDEALS_F2_FINISH);
       else
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_F2, SP_OPERSTATUS_SECURDEALS_F2_RUN);
       end;
    end;

    /*Списание себестоимости*/
    if( not IsClientDeal(FD_1.tick.rec) )
       if(FD_2.dl_leg.rec.OperState == DL_LEG_FORM) /*уже было списание себестоимости по 1й части*/
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_C1, SP_OPERSTATUS_SECURDEALS_C1_FINISH);
       else
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_C1, SP_OPERSTATUS_SECURDEALS_C1_WRTOFF);
       end;

       if(FD_2.dl_leg.rec.OperState == DL_LEG_FORM) /*уже было списание себестоимости по 2й части*/
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_C2, SP_OPERSTATUS_SECURDEALS_C2_FINISH);
       else
          InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_C2, SP_OPERSTATUS_SECURDEALS_C2_WRTOFF);
       end;
    else
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_C1, SP_OPERSTATUS_SECURDEALS_C1_FINISH);
       InsertOperStatus( RS.BofficeKind, ID_Operation, SP_OPERSTATUS_SECURDEALS_C2, SP_OPERSTATUS_SECURDEALS_C2_FINISH);
    end;

    InsertOperDate( RS.BofficeKind, ID_Operation, 0/*начало операции*/, FD_1.DateArray[DATE_DEALDATE] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALDATE,        FD_1.DateArray[DATE_DEALDATE] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALPAY,         FD_1.DateArray[DATE_DEALPAY] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALSETAVOIRISS, FD_1.DateArray[DATE_DEALSETAVOIRISS] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALAVANCE,      FD_1.DateArray[DATE_DEALAVANCE] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALDEPOSIT,     FD_1.DateArray[DATE_DEALDEPOSIT] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALCLOSE,       FD_1.DateArray[DATE_DEALCLOSE] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALEEND,        maxDate( maxDate(FD_1.DateArray[DATE_DEALPAY], FD_1.DateArray[DATE_DEALSETAVOIRISS]), FD_1.DateArray[DATE_DEALCOMISS]) );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALBEGINEXEC,   FD_1.DateArray[DATE_DEALBEGINEXEC] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALEXEC,        FD_1.DateArray[DATE_DEALEXEC] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALEXEC_1,      FD_1.DateArray[DATE_DEALEXEC] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALTRANSFER,    FD_1.DateArray[DATE_DEALTRANSFER] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALCOMISS,      FD_1.DateArray[DATE_DEALCOMISS] );

    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALSETAVOIRISS_2,  FD_2.DateArray[DATE_DEALSETAVOIRISS] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALPAY_2,          FD_2.DateArray[DATE_DEALPAY] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALBEGINEXEC_2,    FD_2.DateArray[DATE_DEALBEGINEXEC] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALEEND_2,         maxDate( maxDate(FD_2.DateArray[DATE_DEALPAY], FD_2.DateArray[DATE_DEALSETAVOIRISS]), FD_2.DateArray[DATE_DEALCOMISS]) );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALDEPOSIT_2,      FD_2.DateArray[DATE_DEALDEPOSIT] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALAVANCE_2,       FD_2.DateArray[DATE_DEALAVANCE] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALTRANSFER_2,     FD_2.DateArray[DATE_DEALTRANSFER] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALEXEC_2,         FD_2.DateArray[DATE_DEALEXEC_2] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALBEGIN_2,        FD_2.DateArray[DATE_DEALBEGIN_2] );
    InsertOperDate( RS.BofficeKind, ID_Operation, DATE_DEALOUTBAL_2,       FD_2.DateArray[DATE_DEALOUTBAL_2] );

    PlanOperStep( ID_Operation, NewDealKind );

    if( NewDealKind == NEW_DEALTYPE_REPO_BACK_ORCB ) /*Обратное РЕПО ОРЦБ без признания ц/б*/
       /*лот по 2ч. Исполнение 1 ч*/
       if( GetOprStatus( ID_Operation, SP_OPERSTATUS_SECURDEALS_I1 ) == SP_OPERSTATUS_SECURDEALS_I1_RUN )
          DropWrtSum( RS.DealID, BRi );
       end;
    elif( NewDealKind == NEW_DEALTYPE_REPO_ORCB )/*Прямое РЕПО ОРЦБ без признания ц/б*/
       /*лот по 2ч. Учет себестоимости по 1 ч */
       if( (GetOprStatus( ID_Operation, SP_OPERSTATUS_SECURDEALS_N1 ) == SP_OPERSTATUS_SECURDEALS_N1_RUN) OR 
           (GetOprStatus( ID_Operation, SP_OPERSTATUS_SECURDEALS_F1 ) == SP_OPERSTATUS_SECURDEALS_F1_RUN)
         )
          DropWrtSum( RS.DealID, BRi );
       end;
    elif( NewDealKind == NEW_DEALTYPE_REPO_BACK )/*Обратное РЕПО без признания ц/б*/
       /*лот по 1ч. Постановка на учет */
       if( GetOprStatus( ID_Operation, SP_OPERSTATUS_SECURDEALS_KO ) == SP_OPERSTATUS_SECURDEALS_KO_CALC )
          DropWrtSum( RS.DealID, BAi );
       end;

       /*лот по 2ч. Поставка по 1 ч*/
       if( GetOprStatus( ID_Operation, SP_OPERSTATUS_SECURDEALS_P1 ) == SP_OPERSTATUS_SECURDEALS_P1_SET )
          DropWrtSum( RS.DealID, BRi );
       end;
    elif( NewDealKind == NEW_DEALTYPE_REPO )/*Прямое РЕПО без признания ц/б*/
       /*лот по 1ч. Постановка на учет (польз) */
       if( GetOprStatus( ID_Operation, SP_OPERSTATUS_SECURDEALS_KO ) == SP_OPERSTATUS_SECURDEALS_KO_CALC )
          DropWrtSum( RS.DealID, BAi );
       end;

       /*лот по 2ч. Учет себестоимости по 1 ч */
       if( (GetOprStatus( ID_Operation, SP_OPERSTATUS_SECURDEALS_N1 ) == SP_OPERSTATUS_SECURDEALS_N1_RUN) OR 
           (GetOprStatus( ID_Operation, SP_OPERSTATUS_SECURDEALS_F1 ) == SP_OPERSTATUS_SECURDEALS_F1_RUN)
         )
          DropWrtSum( RS.DealID, BRi );
       end;
    end;
  end;

  PRIVATE MACRO Retraining( RS )
    VAR NewDealKind, NewBase, 
        FD_1 = SPFirstDoc( LEG_KIND_DL_TICK, RS.DealID ),
        FD_2 = SPFirstDoc( LEG_KIND_DL_TICK_BACK, RS.DealID );
 
    if( IsEXCHANGE(FD_1.Group) )
       if( FD_1.СделкаОРЦБ() ) /*Биржевые с установл. признаком ОРЦБ - на соотв. Репо ОРЦБ*/
          if( IsSALE(FD_1.Group) )
             NewDealKind = NEW_DEALTYPE_REPO_ORCB;/*Прямое   РЕПО ОРЦБ без признания ц/б*/
          else
             NewDealKind = NEW_DEALTYPE_REPO_BACK_ORCB; /*Обратное РЕПО ОРЦБ без признания ц/б*/
          end;
       else /*Биржевые без устан. признака ОРЦБ - на. Репо не ОРЦБ*/
          if( IsSALE(FD_1.Group) )
             NewDealKind = NEW_DEALTYPE_REPO; /*Прямое   РЕПО без признания ц/б*/
          else
             NewDealKind = NEW_DEALTYPE_REPO_BACK; /*Обратное РЕПО без признания ц/б*/
          end;

          NewBase = GetNewBase();
       end;
    elif( IsOutEXCHANGE(FD_1.Group) ) /*Внебиржевые- на. Репо не ОРЦБ*/
       if( IsSALE(FD_1.Group) )
          NewDealKind = NEW_DEALTYPE_REPO; /*Прямое   РЕПО без признания ц/б*/
       else
          NewDealKind = NEW_DEALTYPE_REPO_BACK; /*Обратное РЕПО без признания ц/б*/
       end;
    elif( IsBROKER(FD_1.Group) ) /*Брокерские*/
       if(FD_1.tick.rec.MarketID > 0 ) /*в которых указана биржа  - на. Репо ОРЦБ*/
          if( IsSALE(FD_1.Group) )
             NewDealKind = NEW_DEALTYPE_REPO_ORCB;/*Прямое   РЕПО ОРЦБ без признания ц/б*/
          else
             NewDealKind = NEW_DEALTYPE_REPO_BACK_ORCB; /*Обратное РЕПО ОРЦБ без признания ц/б*/
          end;
       else
          if( IsSALE(FD_1.Group) ) /*в которых не указана биржа  - на Репо не ОРЦБ*/
             NewDealKind = NEW_DEALTYPE_REPO; /*Прямое   РЕПО без признания ц/б*/
          else
             NewDealKind = NEW_DEALTYPE_REPO_BACK; /*Обратное РЕПО без признания ц/б*/
          end;
          NewBase = GetNewBase();
       end;
    end;

    if( UpdateDeal( RS, NewDealKind, NewBase, FD_1, FD_2 ) == false )
       return false;
    end;

    if( CreateOperationDeal( RS, NewDealKind, FD_1 ) == false )
       return false;
    end;
  END;

  /*Выполнить переквалификацию сделок Репо и ОП в сделки Репо БПП*/
  PRIVATE MACRO RunRetraining()
     VAR Num = 0, DataQuery, DataQueryNum, RS, cmd;

     PrintSubHeader( "Переквалификация сделок РЕПО и ОП в сделки РЕПО БПП" );  

     DataQueryNum = "SELECT Buf.T_DEALID FROM DDLREPO_BUF Buf, DDL_TICK_DBT Deal WHERE Buf.t_DealID = Deal.t_DealID";
     DataQuery    = "SELECT Buf.*, Deal.t_DealCode, Deal.t_PartyID Contractor, Deal.t_BofficeKind, Deal.t_Oper, Deal.t_DealDate, Deal.t_DealStatus, Deal.t_CloseDate, Deal.t_Flag2 "+
                    "  FROM DDLREPO_BUF Buf, DDL_TICK_DBT Deal "+
                    " WHERE Buf.t_DealID = Deal.t_DealID";

     InitProgress( SQL_GetNRecs( DataQueryNum ), "Переквалификация сделок РЕПО и ОП", "Процедура коррекции сделок" );

     RS = TRsbDataSet( DataQuery );
     while( RS.MoveNext() )
        __DealCode = RS.DealCode;
        Retraining( RS );
        UseProgress( Num = Num + 1 );
     end;

     cmd = RSDCommand(" delete from dpmwrtbc_dbt pwbc " + 
                      " WHERE (SELECT count(1) from dpmwrtsum_dbt where t_SumID = pwbc.t_SumID) = 0"
                     );
     cmd.Execute();

     RemProgress();
  END;

  PRIVATE MACRO UpdatePORTFOLIO( SumID:INTEGER, OldPORTFOLIO:INTEGER, NewPORTFOLIO:INTEGER, DealCode:STRING ):BOOL
    VAR cmd;
    if( OldPORTFOLIO != NewPORTFOLIO )
       cmd = RSDCommand( "UPDATE DPMWRTSUM_DBT SET t_PORTFOLIO = ?, t_GROUPID = ? where t_SumID = ?" );
       cmd.addParam( "", RSDBP_IN, NewPORTFOLIO );
       cmd.addParam( "", RSDBP_IN, NewPORTFOLIO );
       cmd.addParam( "", RSDBP_IN, SumID );
       cmd.execute();

       PrintMessage( DealCode, "Изменен портфель с " + IIF(OldPORTFOLIO == 1, "\"Торговый\"", "\"ц/б для продажи\"") + IIF(NewPORTFOLIO == 1, "\"Торговый\"", "\"ц/б для продажи\"") );
    end;
    return true;
  ONERROR(err)
    PrintMessage( DealCode, FormError("Ошибка при изменении портфеля (UpdatePORTFOLIO)", err, cmd) );
    return false;
  END;

  PRIVATE MACRO SaveLot( SumID:INTEGER, ChangeDate:DATE, Action:INTEGER, DealCode:STRING ):BOOL
    VAR cmd = RSDCommand( "call RSB_PMWRTOFF.WRTSaveLot( ?, 0,0,?,?)" );

    cmd.addParam( "", RSDBP_IN, SumID );
    cmd.addParam( "", RSDBP_IN, ChangeDate );
    cmd.addParam( "", RSDBP_IN, Action );
    cmd.execute();
    return true;
  ONERROR(err)
    PrintMessage( DealCode, FormError("Ошибка при сохранении лота (SaveLot)", err, cmd) );
    return false;
  END;

  PRIVATE MACRO UpdateOUTLAY( RS ):BOOL
    VAR cmd, FaceValue = $0, Cost = $0, BalanceCost = $0, BegDiscount = $0, Outlay = SQL_ConvTypeSum(RS.Outlay);

    if( Outlay != 0 )
       if( RS.FaceValueFI != NATCUR )
          if( SmartConvertSum( Outlay, Outlay, FormData.OperDate, NATCUR, RS.FaceValueFI, false ) == false)
             PrintMessage( RS.DealCode, "Ошибка при конвертации затрат из " + ПолучитьКодФинИн(NATCUR) + " в " + ПолучитьКодФинИн(RS.FaceValueFI) + " на " + date_as_string(1, FormData.OperDate) );
             return false;
          end;
       end;
       Cost        = Outlay+SQL_ConvTypeSum(RS.Cost);
       BalanceCost = Outlay+SQL_ConvTypeSum(RS.BalanceCost);
    end;

    if( RS.Party <= 0 ) /*свои сделки*/
       if( SP_GetNominal( RS.FIID, SQL_ConvTypeDate(RS.BEGDATE), FaceValue ) )
          BegDiscount = FaceValue*SQL_ConvTypeSum(RS.Amount)-(Outlay+SQL_ConvTypeSum(RS.Cost));
       end;
    end;

    if( (Outlay != 0) OR (BegDiscount != 0) )
       if( SaveLot( SQL_ConvTypeInteger(RS.SumID), FormData.OperDate, 4999 /*Коррекция сумм*/, RS.DealCode ) == false )
          return false;
       end;

       if( (Outlay != 0) AND (BegDiscount != 0) )
          cmd = RSDCommand( "UPDATE DPMWRTSUM_DBT SET t_OUTLAY = ?, t_COST = ?, t_BALANCECOST = ?, t_BEGDISCOUNT = ? where t_SumID = ?" );
          cmd.addParam( "", RSDBP_IN, Outlay );
          cmd.addParam( "", RSDBP_IN, Cost );
          cmd.addParam( "", RSDBP_IN, BalanceCost );
          cmd.addParam( "", RSDBP_IN, BegDiscount );
          cmd.addParam( "", RSDBP_IN, RS.SumID );
          cmd.execute();

          PrintMessage( RS.DealCode, "Изменены суммы в лоте:"+ 
                             "\nOUTLAY: "+RS.Outlay+"->"+Outlay+
                             "\nCOST: "+RS.Cost  +"->"+Cost+
                             "\nBALANCECOST: "+RS.BALANCECOST+"->"+BALANCECOST+
                             "\nBEGDISCOUNT: "+RS.BEGDISCOUNT+"->"+BEGDISCOUNT
                 );
       elif( Outlay != 0 )
          cmd = RSDCommand( "UPDATE DPMWRTSUM_DBT SET t_OUTLAY = ?, t_COST = ?, t_BALANCECOST = ? where t_SumID = ?" );
          cmd.addParam( "", RSDBP_IN, Outlay );
          cmd.addParam( "", RSDBP_IN, Cost );
          cmd.addParam( "", RSDBP_IN, BalanceCost );
          cmd.addParam( "", RSDBP_IN, RS.SumID );
          cmd.execute();

          PrintMessage( RS.DealCode, "Изменены суммы в лоте:"+ 
                             "\nOUTLAY: "+RS.Outlay+"->"+Outlay+
                             "\nCOST: "+RS.Cost  +"->"+Cost+
                             "\nBALANCECOST: "+RS.BALANCECOST+"->"+BALANCECOST
                 );
       elif( BegDiscount != 0 )
          cmd = RSDCommand( "UPDATE DPMWRTSUM_DBT SET t_BEGDISCOUNT = ? where t_SumID = ?" );
          cmd.addParam( "", RSDBP_IN, BegDiscount );
          cmd.addParam( "", RSDBP_IN, RS.SumID );
          cmd.execute();

          PrintMessage( RS.DealCode, "Изменены суммы в лоте:"+ 
                             "\nBEGDISCOUNT: "+RS.BEGDISCOUNT+"->"+BEGDISCOUNT
                 );
       end;
    end;

    return true;
  ONERROR(err)
    PrintMessage( RS.DealCode, FormError("Ошибка при учете затрат на лоте (UpdateOUTLAY)", err, cmd) );
    return false;
  END;

  PRIVATE MACRO UpdateOUTLAY_REPO1( RS ):BOOL
    VAR cmd, FaceValue = $0, BegDiscount = $0;

    if( RS.Party <= 0 ) /*свои сделки*/
       if( SP_GetNominal( RS.FIID, SQL_ConvTypeDate(RS.BEGDATE), FaceValue ) )
          BegDiscount = FaceValue*SQL_ConvTypeSum(RS.Amount)-SQL_ConvTypeSum(RS.Cost);
       end;
    end;

    if( (RS.Outlay != 0) OR (BegDiscount != 0) )
       if( SaveLot( SQL_ConvTypeInteger(RS.SumID), FormData.OperDate, 4999 /*Коррекция сумм*/, RS.DealCode ) == false )
          return false;
       end;

       if( (RS.Outlay != 0) AND (BegDiscount != 0) )
          cmd = RSDCommand( "UPDATE DPMWRTSUM_DBT SET t_OUTLAY = ?, t_BEGDISCOUNT = ? where t_SumID = ?" );
          cmd.addParam( "", RSDBP_IN, 0 );
          cmd.addParam( "", RSDBP_IN, BegDiscount );
          cmd.addParam( "", RSDBP_IN, RS.SumID );
          cmd.execute();

          PrintMessage( RS.DealCode, "Изменены суммы в лоте:"+ 
                             "\nOUTLAY: "+RS.Outlay+"->"+0+
                             "\nBEGDISCOUNT: "+RS.BEGDISCOUNT+"->"+BEGDISCOUNT
                 );
       elif( RS.Outlay != 0 )
          cmd = RSDCommand( "UPDATE DPMWRTSUM_DBT SET t_OUTLAY = ? where t_SumID = ?" );
          cmd.addParam( "", RSDBP_IN, 0 );
          cmd.addParam( "", RSDBP_IN, RS.SumID );
          cmd.execute();

          PrintMessage( RS.DealCode, "Изменены суммы в лоте:"+ 
                             "\nOUTLAY: "+RS.Outlay+"->"+0
                 );
       elif( BegDiscount != 0 )
          cmd = RSDCommand( "UPDATE DPMWRTSUM_DBT SET t_BEGDISCOUNT = ? where t_SumID = ?" );
          cmd.addParam( "", RSDBP_IN, BegDiscount );
          cmd.addParam( "", RSDBP_IN, RS.SumID );
          cmd.execute();

          PrintMessage( RS.DealCode, "Изменены суммы в лоте:"+ 
                             "\nBEGDISCOUNT: "+RS.BEGDISCOUNT+"->"+BEGDISCOUNT
                 );
       end;
    end;

    return true;
  ONERROR(err)
    PrintMessage( RS.DealCode, FormError("Ошибка при учете затрат на лоте (UpdateOUTLAY_REPO1)", err, cmd) );
    return false;
  END;

  PRIVATE MACRO UpdateOUTLAY_REPO2( RS ):BOOL
    VAR RSrepo, cmdUpdt, cmd = RSDCommand( "SELECT T_SALED, T_SALEDCOST FROM DDLREPO_BUF WHERE t_DEALID = ?" );
    cmd.addParam( "", RSDBP_IN, RS.t_DealID );
    cmd.execute();
    RSrepo = TRsbDataSet(cmd);
    if( RSrepo.movenext() AND (RSrepo.SALED != 0) )

       if( SaveLot( SQL_ConvTypeInteger(RS.SumID), FormData.OperDate, 4999 /*Коррекция сумм*/, RS.DealCode ) == false )
          return false;
       end;

       cmdUpdt = RSDCommand( "UPDATE DPMWRTSUM_DBT SET T_AMOUNTBD = ?, T_BALANCECOSTBD = ? where t_SumID = ?" );
       cmdUpdt.addParam( "", RSDBP_IN, RSrepo.SALED );
       cmdUpdt.addParam( "", RSDBP_IN, RSrepo.SALEDCOST );
       cmdUpdt.addParam( "", RSDBP_IN, RS.SumID );
       cmdUpdt.execute();

       PrintMessage( RS.DealCode, "Изменены суммы в лоте:"+ 
                          "\nAMOUNTBD: "+RS.AMOUNTBD+"->"+RSrepo.SALED+
                          "\nBALANCECOSTBD: "+RS.BALANCECOSTBD+"->"+RSrepo.SALEDCOST
              );
    end;

    return true;
  ONERROR(err)
    PrintMessage( RS.DealCode, FormError("Ошибка обновлении сумм в лоте (UpdateOUTLAY_REPO2)", err, cmd) );
    return false;
  END;

  PRIVATE MACRO UpdateOUTLAY_REPO3( RS ):BOOL
    VAR cmdUpdt;

    if( RS.OUTLAY != 0 )
       if( SaveLot( SQL_ConvTypeInteger(RS.SumID), FormData.OperDate, 4999 /*Коррекция сумм*/, RS.DealCode ) == false )
          return false;
       end;

       cmdUpdt = RSDCommand( "UPDATE DPMWRTSUM_DBT SET t_OUTLAY = ? where t_SumID = ?" );
       cmdUpdt.addParam( "", RSDBP_IN, $0 );
       cmdUpdt.addParam( "", RSDBP_IN, RS.SumID );
       cmdUpdt.execute();

       PrintMessage( RS.DealCode, "Изменены суммы в лоте:"+ 
                          "\nOUTLAY: "+RS.OUTLAY+"->"+0
              );
    end;

    return true;
  ONERROR(err)
    PrintMessage( RS.DealCode, FormError("Ошибка обновлении сумм в лоте (UpdateOUTLAY_REPO3)", err, cmdUpdt) );
    return false;
  END;

  PRIVATE MACRO GetLotState_ByID( SumID:INTEGER ):INTEGER
    VAR RS, cmd = RSDCommand( "SELECT T_State from dpmwrtsum_dbt WHERE t_SumID = ?" );
    cmd.addParam( "", RSDBP_IN, SumID );
    cmd.execute();
    RS = TRsbDataSet(cmd);
    if( RS.movenext() )
       return SQL_ConvTypeInteger(RS.State);
    end;
    return -1;
  ONERROR(err)
    return -1;
  END;

  PRIVATE MACRO GetLot_ByID( SumID:INTEGER )
    VAR RS, cmd = RSDCommand( "SELECT * from dpmwrtsum_dbt WHERE t_SumID = ?" );
    cmd.addParam( "", RSDBP_IN, SumID );
    cmd.execute();
    RS = TRsbDataSet(cmd);
    if( RS.movenext() )
       return RS;
    end;
    return NULL;
  ONERROR(err)
    return NULL;
  END;

  PRIVATE MACRO GetLotState_ByParent( parent:INTEGER ):INTEGER
    VAR RS, cmd = RSDCommand( "SELECT T_State from dpmwrtsum_dbt WHERE t_parent = ?" );
    cmd.addParam( "", RSDBP_IN, parent );
    cmd.execute();
    RS = TRsbDataSet(cmd);
    if( RS.movenext() )
       return SQL_ConvTypeInteger(RS.State);
    end;
    return -1;
  ONERROR(err)
    return -1;
  END;

  PRIVATE MACRO UpdateOUTLAY_REPO4( RS:OBJECT ):BOOL
    VAR FD_1, PayFIID, FinRes, TotalCost_1, FaceValue = $0, Cost = $0, BalanceCost = $0, BegDiscount = $0,  BegDate, 
        RSrepo, cmdUpdt, cmd = RSDCommand( "SELECT T_FINRES FROM DDLREPO_BUF WHERE t_DEALID = ?" );

    cmd.addParam( "", RSDBP_IN, RS.t_DealID );
    cmd.execute();
    RSrepo = TRsbDataSet(cmd);
    if( RSrepo.movenext() )

       if( SaveLot( SQL_ConvTypeInteger(RS.SumID), FormData.OperDate, 4999 /*Коррекция сумм*/, RS.DealCode ) == false )
          return false;
       end;

       if( RS.Party <= 0 ) /*свои сделки*/
          if( SP_GetNominal( RS.FIID, SQL_ConvTypeDate(RS.BEGDATE), FaceValue ) )
             BegDiscount = FaceValue*SQL_ConvTypeSum(RS.Amount)-SQL_ConvTypeSum(RS.Cost);
          end;
       end;

       FD_1 = SPFirstDoc( LEG_KIND_DL_TICK, RS.DealID );

       BegDate = SQL_ConvTypeDate(FD_1.pmwrtsum.rec.Date); /*дата поставки 1 ч*/
       PayFIID = FD_1.GetPayFIID();
       if( SmartConvertSum( FinRes, RSrepo.FinRes, BegDate, NATCUR, RS.FaceValueFI, false ) == false )
          PrintMessage( RS.DealCode, "Ошибка при конвертации суммы ФР из " + ПолучитьКодФинИн(NATCUR) + " в " + ПолучитьКодФинИн(RS.FaceValueFI) + " на " + date_as_string(1, BegDate) );
          return false;
       end;

       if( SmartConvertSum( TotalCost_1, FD_1.dl_leg.rec.TotalCost, BegDate, FD_1.dl_leg.rec.CFI, PayFIID, false ) == false )
          PrintMessage( RS.DealCode, "Ошибка при конвертации суммы сделки из " + ПолучитьКодФинИн(FD_1.dl_leg.rec.CFI) + " в " + ПолучитьКодФинИн(PayFIID) + " на " + date_as_string(1, BegDate) );
       end;

       if( SmartConvertSum( TotalCost_1, TotalCost_1, BegDate, PayFIID, RS.FaceValueFI, false ) == false )
          PrintMessage( RS.DealCode, "Ошибка при конвертации суммы сделки из " + ПолучитьКодФинИн(PayFIID) + " в " + ПолучитьКодФинИн(RS.FaceValueFI) + " на " + date_as_string(1, BegDate) );
       end;

       BalanceCost = TotalCost_1+FinRes;
       Cost        = BalanceCost - RS.NKDAMOUNT;

       if( BegDiscount != 0 )
          cmdUpdt = RSDCommand( "UPDATE DPMWRTSUM_DBT SET t_State = ?, t_BALANCECOST = ?, t_COST = ?, t_BEGDATE = ?, t_BEGDISCOUNT = ? where t_SumID = ?" );
          cmdUpdt.addParam( "", RSDBP_IN, PM_WRTSUM_SALE_BPP );
          cmdUpdt.addParam( "", RSDBP_IN, BalanceCost );
          cmdUpdt.addParam( "", RSDBP_IN, Cost        );
          cmdUpdt.addParam( "", RSDBP_IN, BegDate     );
          cmdUpdt.addParam( "", RSDBP_IN, BegDiscount );
          cmdUpdt.addParam( "", RSDBP_IN, RS.SumID );
          cmdUpdt.execute();

          PrintMessage( RS.DealCode, "Изменены поля в лоте:"+ 
                             "\nState: "+RS.State+"->"+PM_WRTSUM_SALE_BPP + "  (Продан БПП)" +
                             "\nt_BalanceCost: "+RS.BalanceCost+"->"+BalanceCost + 
                             "\nt_Cost: "+RS.Cost+"->"+Cost + 
                             "\nt_BegDate: "+RS.BegDate+"->"+BegDate + 
                             "\nt_BegDiscount: "+RS.BegDiscount+"->"+BegDiscount
                 );
      else
          cmdUpdt = RSDCommand( "UPDATE DPMWRTSUM_DBT SET t_State = ?, t_BALANCECOST = ?, t_COST = ?, t_BEGDATE = ? where t_SumID = ?" );
          cmdUpdt.addParam( "", RSDBP_IN, PM_WRTSUM_SALE_BPP );
          cmdUpdt.addParam( "", RSDBP_IN, BalanceCost );
          cmdUpdt.addParam( "", RSDBP_IN, Cost        );
          cmdUpdt.addParam( "", RSDBP_IN, BegDate     );
          cmdUpdt.addParam( "", RSDBP_IN, RS.SumID );
          cmdUpdt.execute();

          PrintMessage( RS.DealCode, "Изменены поля в лоте:"+ 
                             "\nState: "+RS.State+"->"+PM_WRTSUM_SALE_BPP + "  (Продан БПП)" +
                             "\nt_BalanceCost: "+RS.BalanceCost+"->"+BalanceCost + 
                             "\nt_Cost: "+RS.Cost+"->"+Cost + 
                             "\nt_BegDate: "+RS.BegDate+"->"+BegDate 
                 );
      end;
    end;
    return true;
  ONERROR(err)
    PrintMessage( RS.DealCode, FormError("Ошибка обновлении сумм в лоте (UpdateOUTLAY_REPO3)", err, cmd) );
    return false;
  END;

  PRIVATE MACRO ConvertWrtSum( RS )
     /*Во всех собств. лотах по обычным покупкам/продажам и Репо прям. 2 ч.:*/
     if( (RS.PORTFOLIO == 1) OR (RS.PORTFOLIO == 2) ) /*ТП или ППР*/
        if( ExistNOSS( SQL_ConvTypeInteger(RS.FIID), SQL_ConvTypeDate(RS.DealDate) ) == true )
            if( UpdatePORTFOLIO( SQL_ConvTypeInteger(RS.SumID), SQL_ConvTypeInteger(RS.PORTFOLIO), 1, RS.DealCode ) == false )
              return;
            end;
        else 
            if( UpdatePORTFOLIO( SQL_ConvTypeInteger(RS.SumID), SQL_ConvTypeInteger(RS.PORTFOLIO), 2, RS.DealCode ) == false )
              return;
            end;
        end;
     end;
     
     /*В поставленных лотах по обычным покупкам и прямого Репо 2 ч., по которым есть ос-таток*/
     if( (RS.State == PM_WRTSUM_FORM /*Поставлен*/) AND 
         (RS.Amount > 0) AND 
         ( (RS.Kind == PM_WRTSUM_KIND_B /*покупки*/) OR
           (RS.Kind == PM_WRTSUM_KIND_FB /*Зачисление лота*/) OR
           (RS.Kind == PM_WRTSUM_KIND_RRWAB2 /*Зачисление в Репо прямом БПП (2 ч.)*/) OR
           (RS.Kind == PM_WRTSUM_KIND_RRAB2  /*Зачисление в Репо прямом ПП (2 ч.)*/) 
         )
       )
        if( UpdateOUTLAY( RS ) == false )
          return;
        end;

     end;

     /*В поставленных лотах Репо обр по 1 ч., если есть остаток и 2 ч не выполнена*/
     if( (RS.State == PM_WRTSUM_FORM /*Поставлен*/) AND 
         (GetLotState_ByParent(RS.SumID) == PM_WRTSUM_NOTFORM) AND /*2 ч не выполнена*/
         ( (RS.Kind == PM_WRTSUM_KIND_RRWAB1 /*Зачисление в Репо обратном БПП (1 ч.)*/) OR
           (RS.Kind == PM_WRTSUM_KIND_RRAB1  /*Зачисление в Репо обратном ПП (1 ч.)*/) 
         )
       )
        if( UpdateOUTLAY_REPO1( RS ) == false )
          return;
        end;
     end;

     /*В непоставленных лотах Репо обр по 2 ч., если поставка по 1 ч выполнена*/
     if( (RS.State == PM_WRTSUM_NOTFORM /*не поставлен*/) AND 
         (RS.Parent > 0) AND
         ( (RS.Kind == PM_WRTSUM_KIND_RRWAS2 /*Списание в Репо обратном БПП (2 ч.)*/) OR
           (RS.Kind == PM_WRTSUM_KIND_RRAS2  /*Списание в Репо обратном ПП (2 ч.)*/)
         ) AND
         (GetLotState_ByID( RS.Parent ) == PM_WRTSUM_FORM ) /*поставка по 1 ч выполнена*/
       )
        if( UpdateOUTLAY_REPO2( RS ) == false )
          return;
        end;
     end;

     /*В поставленных лотах Репо прям. по 1 ч., если 2 ч не выполнена*/
     if( (RS.State == PM_WRTSUM_FORM /*поставлен*/) AND 
         ( (RS.Kind == PM_WRTSUM_KIND_RRWAS1  /*Списание в Репо прямом БПП (1 ч.)*/) OR
           (RS.Kind == PM_WRTSUM_KIND_RRAS1   /*Списание в Репо прямом ПП (1 ч.)*/)
         )AND
         (GetLotState_ByParent(RS.SumID) == PM_WRTSUM_NOTFORM) /*2 ч не выполнена*/
       )
        if( UpdateOUTLAY_REPO3( RS ) == false )
          return;
        end;
     end;

     /* В непоставленных лотах Репо прям. по 2 ч., если поставка по 1 ч выполнена*/
     if( (RS.State == PM_WRTSUM_NOTFORM /*не поставлен*/) AND 
         ( (RS.Kind == PM_WRTSUM_KIND_RRWAB2  /*Зачислениев Репо прямом БПП (2 ч.)*/) OR
           (RS.Kind == PM_WRTSUM_KIND_RRAB2   /*Зачислениев Репо прямом ПП (2 ч.)*/)
         )AND
         (GetLotState_ByID( RS.Parent ) == PM_WRTSUM_FORM) /*поставка по 1 ч выполнена*/
       )
        if( UpdateOUTLAY_REPO4( RS ) == false )
          return;
        end;
     end;
  END;

  /*Выполнить коррекцию лотов*/
  PRIVATE MACRO RunCorrectLots()
     VAR Num = 0, DataQuery, DataQueryNum, RS, cmd;

     PrintSubHeader( "Коррекция лотов" );  

     DataQueryNum = "SELECT T_SumID FROM DPMWRTSUM_DBT";
     DataQuery    = "SELECT wrt.*, Deal.t_DealCode, Deal.t_BofficeKind, Deal.t_DealDate, FI.t_FaceValueFI "+
                    "  FROM DPMWRTSUM_DBT wrt, DDL_TICK_DBT Deal, dfininstr_dbt FI "+
                    " WHERE     wrt.t_DealID  = Deal.t_DealID"+
                    "       AND FI.t_FIID = wrt.t_FIID "
                    " ORDER BY wrt.T_DEALID ";

     InitProgress( SQL_GetNRecs( DataQueryNum ), "Корректировка сумм в лотах", "Процедура коррекции сделок" );

     RS = TRsbDataSet( DataQuery );
     while( RS.MoveNext() )
        __DealCode = RS.DealCode;
        ConvertWrtSum( RS );
        UseProgress( Num = Num + 1 );
     end;

     cmd = RSDCommand( "UPDATE DDL_TICK_DBT tick SET tick.t_PORTFOLIOID = NVL( (SELECT t_PORTFOLIO FROM DPMWRTSUM_DBT WHERE t_SumID = tick.t_DealID ),-1 )" );
     cmd.execute();

     RemProgress();
  END;

  /*Выполнить формирование сумм по оплаченным комиссиям*/
  PRIVATE MACRO RunCreateCommissSumm()
     VAR Num = 0, DataQuery, DataQueryNum, RS, cmd, errtext, errcode,
         dlsum = TBFile( "DLSUM", "W" );

     PrintSubHeader( "Формирование сумм по оплаченным комиссиям" );  

     DataQueryNum = "SELECT T_DEALID FROM DCOMSUM_BUF";
     DataQuery    = "SELECT Buf.*, Deal.t_DealCode, Deal.t_BofficeKind, FI.t_FI_Code "+
                    "  FROM DCOMSUM_BUF Buf, DDL_TICK_DBT Deal, dfininstr_dbt FI "+
                    " WHERE     Buf.t_DealID  = Deal.t_DealID "+
                    "       AND Buf.t_PAYCURR = FI.t_FIID "+
                    " ORDER BY Buf.T_DEALID ";

     InitProgress( SQL_GetNRecs( DataQueryNum ), "Формирование сумм по оплаченным комиссиям", "Процедура коррекции сделок" );

     RS = TRsbDataSet( DataQuery );
     while( RS.MoveNext() )

        __DealCode = RS.DealCode;

        dlsum.Clear();
        dlsum.rec.DocKind  = RS.BofficeKind;
        dlsum.rec.DocID    = RS.DealID;
        dlsum.rec.Date     = SQL_ConvTypeDate(RS.DATE);
        dlsum.rec.Currency = RS.PAYCURR;
        dlsum.rec.Kind     = IIF( RS.Kind == 1, 20, 30 );

        if( (SmartConvertSum( dlsum.rec.Sum, RS.T_SUM, dlsum.rec.Date, RS.CUR, dlsum.rec.Currency, false ) == false) OR
            (SmartConvertSum( dlsum.rec.NDS, RS.T_NDS, dlsum.rec.Date, RS.CUR, dlsum.rec.Currency, false ) == false)
          )
           PrintMessage( RS.DealCode, "Ошибка при конвертации суммы комиссии из " + ПолучитьКодФинИн(RS.CUR) + " в " + ПолучитьКодФинИн(dlsum.rec.Currency) + " на " + date_as_string(1, dlsum.rec.Date) );
        else    
           if( dlsum.Insert() == false )
              errcode = status( errtext );
              PrintMessage( RS.DealCode, "Ошибка при вставке данных во временный файл (" + errcode + ") " + errtext );
           else
              PrintMessage( RS.DealCode, IIF(RS.Kind == 1, "Клиентская периодическая комиссия\n", "Единовременная комиссия посреднику\n") + "  Сумма = " + string(dlsum.rec.Sum) + "\n  НДС = " + dlsum.rec.NDS + "\n  Валюта = " + RS.FI_Code );
           end;
        end;

        UseProgress( Num = Num + 1 );
     end;

     cmd = RSDCommand( "DROP TABLE DCOMSUM_BUF CASCADE CONSTRAINTS" );
     cmd.execute();

     RemProgress();
  OnError
     return;
  END;

  PRIVATE MACRO DisableTrigger( TrName:STRING )
    VAR cmd = RSDCommand("declare "+
                     "  e_no_trigger exception; "+
                     "  pragma exception_init (e_no_trigger, -4080); "+
                     " begin "+
                     "  execute immediate ('alter trigger "+ TrName + " disable'); "
                     " exception "+
                     "  when e_no_trigger then "+
                     "    null; "+
                     " end;");
    cmd.Execute();
  END;

  PRIVATE MACRO EnableTrigger( TrName:STRING )
    VAR cmd = RSDCommand("declare "+
                     "  e_no_trigger exception; "+
                     "  pragma exception_init (e_no_trigger, -4080); "+
                     " begin "+
                     "  execute immediate ('alter trigger "+ TrName + " enable'); "
                     " exception "+
                     "  when e_no_trigger then "+
                     "    null; "+
                     " end;");
    cmd.Execute();
  END;

  MACRO Create()
     if( FormData.Flag1 == "X" )
        DisableTrigger("DPMWRTSUM_DBT_TBD");
        RunRetraining();
        EnableTrigger("DPMWRTSUM_DBT_TBD");
     end;

     if( FormData.Flag2 == "X" )
        RunCorrectLots();
     end;

     if( FormData.Flag3 == "X" )
        RunCreateCommissSumm();
     end;
  END;
END;

VAR Rep,
    RepForm = SP_DealCorrect_Form();

MACRO MsgBoxFromReport( Msg:VARIANT )
   VAR i = 1, CurVal, FormMessage = string(Msg);

   while( GetParm( i, CurVal) )
      FormMessage = FormMessage + string(CurVal);
      i = i + 1;
   end;
   Rep.PrintMessage( IIF(__DealCode == NULL, "", __DealCode), Rep.FormError(FormMessage) );
END;

if( RepForm.Run() )
  FormData = RepForm.Data().rec;
  Rep = SP_DealCorrect();

  ReplaceMacro ( "msgbox", "MsgBoxFromReport" );

  Rep.BeginReport();
  Rep.Create();
  Rep.EndReport();

  ReplaceMacro ( "msgbox", NULL );
end;

OnError
  ReplaceMacro ( "msgbox", NULL );
