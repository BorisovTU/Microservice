/*
$Name:        load_taxbase2022.mac
$Module:      Ценные бумаги
$Description: Загрузка выгруженного из Диасофт файла с НОБ и налогами по окнчанию 2022-го года
*/

import rsexts;
import "global_classes.mac";           /* класс ошибок */
import "ws_msg_transform_sec.mac";     /* для обработки ошибок */
import dlmisc;                         /* SplitString*/
import "func_lib";
import "dlquery.mac";

private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

Private macro GetSavedRows(Year)
  var sql, cmd;
  sql = "select 1 from dnptxdias_dbt where t_Year = ? and rownum = 1";
  cmd = DL_RSDCommand(sql);
  cmd.AddParam(Year);
  return cmd.GetCount();
End;

private Macro DeleteYearRows(Year)
  var sql, cmd;
  sql = "delete from dnptxdias_dbt where t_Year = ?";
  cmd = DL_RSDCommand(sql);
  cmd.AddParam(Year);
  cmd.ExecuteCMD();
end;

private macro StrToMoney(Str:string)
  var m = $0;

  Str = StrSubst(Str, " ", "");
  Str = StrSubst(Str, ",", ".");

  return money(Str);
end;

private Macro LoadTaxBase(namefile)
  var objExcel = CDAOMSExcel();
  var i = 1, cntempty = 0, cntsave = 0;
  var SystDate = date(0,0,0);
  var SystTime = time(0);
  var Month = 0, Year = 2022;
  var nptxdiasCache = RsbSQLInsert("nptxdias.dbt");
  var nptxdias_arr = TArray();

  GetSystDateTime(@SystDate, @SystTime);

 /* DateSplit(SystDate, null, Month Year);

  if(Month == 1)
    //Т.е. процедура разовая (пока только за 2022), но чтобы не прописывать тут конкретный год, попробуем его вычислить
    //Когда буду запускать процедуру - не известно, а в файле этой информации нет
    //Будем считать, что если загружают в январе, то это за предыдущий год. Т.е. ели будут реально загружать в январе 2023, то это будет за 2022.
    //В ТЗ написано, что загружать будут 31.12.2022
    Year = Year - 1;
  end;*/

  if( GetSavedRows(Year) > 0) 
   if(not GetTrue(false, "За " + Year + " год данные уже загружались, желаете обновить?"))
     return;
   end;
   DeleteYearRows(Year);
  end;

  if(objExcel.CreateApplication())
    if(objExcel.open(namefile))
      objExcel.Book.Sheets(1).Activate();
      
      var curr_row = -1;
      var activeSheet = objExcel.Book.ActiveSheet;
      var objTarget = activeSheet.UsedRange.Find("ID CFT");

      if(objTarget)
        curr_row = objTarget.row + 1;
      else
        msgbox("Данные в файле не найдены");
        return;
      end;
      
      if(curr_row > -1)
        InitProgress(activeSheet.Rows.CurrentRegion.Rows.Count - curr_row + 1, "Ждите, выполняется загрузка данных ...", "Выполняется загрузка данных");

        while(curr_row <= activeSheet.Rows.CurrentRegion.Rows.Count)
          var nptxdias = TRecHandler("nptxdias.dbt");

          nptxdias.Clear();
          
          nptxdias.rec.Year     = Year;
          nptxdias.rec.LoadDate = SystDate;
          nptxdias.rec.CFTID    = trim(string(activeSheet.Cells(int(curr_row), 1).Value));
          if(nptxdias.rec.CFTID == "")
            break;
          end;

          nptxdias.rec.FIO      = trim(string(activeSheet.Cells(int(curr_row), 2).Value));

          nptxdias.rec.ClientID = GetPartyID_byPartyCode(PARTY_CODEKIND_ABS, string(nptxdias.rec.CFTID));
          if(nptxdias.rec.ClientID == -1)
             var ErrStr = "Субъект с кодом "+string(nptxdias.rec.CFTID)+" не найден";
             println(ErrStr);
             UseProgress(curr_row = curr_row + 1);
             continue;
          end;
          
          nptxdias.rec.IIS      = "";
          if(StrUpr(trim(string(activeSheet.Cells(int(curr_row), 3).Value))) == "ИИС")
            nptxdias.rec.IIS    = "X";
          end;

          nptxdias.rec.ItoType  = trim(string(activeSheet.Cells(int(curr_row), 4).Value));
          nptxdias.rec.TaxRate  = int(activeSheet.Cells(int(curr_row), 5).Value);
          nptxdias.rec.KBK      = trim(string(activeSheet.Cells(int(curr_row), 6).Value));
          nptxdias.rec.TaxByPay = money(activeSheet.Cells(int(curr_row), 7).Value);
          nptxdias.rec.TaxBase  = money(activeSheet.Cells(int(curr_row), 8).Value);

          nptxdias_arr[nptxdias_arr.size] = nptxdias;

          if(nptxdias_arr.size == 1000)
            nptxdiasCache.AddRecordArray(nptxdias_arr);

            nptxdiasCache.Insert();


            cntsave = cntsave + nptxdias_arr.size;
            nptxdias_arr.size = 0;
            nptxdiasCache = RsbSQLInsert("nptxdias.dbt");
          end;

          UseProgress(curr_row = curr_row + 1);
        end;

        if(nptxdias_arr.size > 0)
          nptxdiasCache.AddRecordArray(nptxdias_arr);

          nptxdiasCache.Insert();

          cntsave = cntsave + nptxdias_arr.size;
          nptxdias_arr.size = 0;
          nptxdiasCache = RsbSQLInsert("nptxdias.dbt");
        end;

        RemProgress();

        msgbox("За " + Year +  " год загружено "+ trim(string(cntsave)) + " записей.");
      end;
    end;
    objExcel.Book.Close();
  end;
  objExcel = null;
OnError(err)
  Msgbox(err.Message);
  objExcel.Book.Close();
  objExcel = null;
end;

var file_name = "";
if(SelectFile(file_name, MergeFile ("..\\Import", "*.xls*"))) 
  LoadTaxBase(file_name);
end;
