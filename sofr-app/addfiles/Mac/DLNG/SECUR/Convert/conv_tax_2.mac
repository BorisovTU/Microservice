/* Процедура конвертации данных НУ. Загрузка лотов и связей в БД
   Chernov Anton 07-12-2007
*/

import "cb_sql", OprInter, PaymInter, SecInter, "conv_tax_c.mac";

PRIVATE CONST 
  txtDelim = ";";

PRIVATE FILE f_lots_txt() txt;
PRIVATE FILE f_lnks_txt() txt;

var f_lots_name, f_lnks_name;
var LotCol = TArray,
    LnkCol = TArray;


PRIVATE MACRO CreateTempTable()
  var sql;

  sql = "DECLARE \n"
      +  " v_err EXCEPTION; \n"
      +  " PRAGMA EXCEPTION_INIT( v_err, -955 );\n"
      + "BEGIN EXECUTE IMMEDIATE 'CREATE TABLE dsctxlot_repl"
      + " ( T_OLDID NUMBER(10), " /* ИД старого лота */
      +   " t_NEWID NUMBER(10) )';\n " /* ИД нового лота */
      + "EXCEPTION WHEN v_err THEN NULL; \n"
      + "END; \n";
  SQL_Execute(sql,"Создание временной таблицы", false);

  sql = "DECLARE \n"
      +  " v_err EXCEPTION; \n"
      +  " PRAGMA EXCEPTION_INIT( v_err, -955 );\n"
      + "BEGIN\n"
      +  " EXECUTE IMMEDIATE 'CREATE INDEX ddsctxlot_repl_idx0 ON dsctxlot_repl"
      + " (T_OLDID)';\n"
      + "EXCEPTION WHEN v_err THEN NULL; \n"
      + "END; \n";
  SQL_Execute(sql,"Создание индексов временной таблицы", false);

  SQL_Execute("TRUNCATE TABLE dsctxlot_repl", "Очистка временной таблицы", false);
END;

PRIVATE MACRO DropTempTable()
  var sql;

  sql = "DECLARE \n"
      +  " v_err EXCEPTION; \n"
      +  " PRAGMA EXCEPTION_INIT( v_err, -942 );\n"
      + "BEGIN EXECUTE IMMEDIATE 'DROP TABLE dsctxlot_repl';"
      + "EXCEPTION WHEN v_err THEN NULL; \n"
      + "END; \n";

  SQL_Execute(sql,"Удаление временной таблицы", false);
END;

PRIVATE MACRO InitRegistry()
  SQL_Execute("call rsb_sctx.GetSettingsTax()", "Чтение настроек НУ", false);
END;


PRIVATE MACRO OpenFiles()
  var stat = true;

  if( not SelectFile(f_lots_name, "*.csv", "Выберите файл лотов") )
    stat = false;
  end;

  if( stat and (not SelectFile(f_lnks_name, "*.csv", "Выберите файл связей")) )
    stat = false;
  end;

  if( not Open(f_lots_txt, f_lots_name,"rsansi") )
    msgbox( "Ошибка открытия файла " + f_lots_name );
    stat = false;
  end;

  if( stat and (not Open(f_lnks_txt, f_lnks_name)) )
    msgbox( "Ошибка открытия файла " + f_lnks_name );
    stat = false;
  end;

  return stat;
END;

/* заполнить массив названий колонок таблицы */
PRIVATE MACRO FillCols( v_fl, ColArray )
  var iCount = 0;

  Rewind(v_fl);

  if( next(v_fl) )
    while( v_fl(iCount) != "" )

      ColArray(ColArray.Size) = v_fl(iCount);
      iCount = iCount + 1;
    end;
  end;
END;

/* получить номер колонки таблицы по ее названию */
PRIVATE MACRO FindColsByName( arr : TArray, Name : string )
  var iCount = 0,
      stat = false;

  while( (iCount < arr.Size) and (not stat) )
    if( arr[iCount] == Name )
      stat = true;
      break;
    end;
    iCount = iCount + 1;
  end;

  if( stat == false )
    iCount = -1;
  end;

  return iCount;
END;

PRIVATE MACRO IsVirtualLot( IdLot : integer )
  var sql, rsd;

  sql = " SELECT t_IsVirtual "
      +   " FROM dsctaxlot_dbt "
      + " WHERE t_ID = " + string(IdLot);

  rsd = TRsbDataSet(sql);
  if( rsd.MoveNext() and (rsd.IsVirtual == "X") )
    return true;
  end;

  return false;
END;

/* вставка лотов НУ */
PRIVATE MACRO InsertLots()
  var cmd, sql, rsd, nRec = 0, iCount = 0, LotID,
      iAmountColNum, iIDColNum, iDealCodeTSColNum, iDealDateColNum;

  iAmountColNum = FindColsByName(LotCol, "Количество ц/б");
  if( iAmountColNum < 0 )
    msgbox( "В файле \"" + f_lots_name + "\" не найдено поле \"Количество ц/б\"");
    exit(1);
  end;

  iIDColNum = FindColsByName(LotCol, "ИД лота (внутренний идентификатор)");
  if( iIDColNum < 0 )
    msgbox( "В файле \"" + f_lots_name + "\" не найдено поле \"ИД лота (внутренний идентификатор)\"");
    exit(1);
  end;

  iDealCodeTSColNum = FindColsByName(LotCol, "Внешний код сделки");
  if( iDealCodeTSColNum < 0 )
    msgbox( "В файле \"" + f_lots_name + "\" не найдено поле \"Внешний код сделки\"");
    exit(1);
  end;

  iDealDateColNum = FindColsByName(LotCol, "Дата заключения сделки");
  if( iDealDateColNum < 0 )
    msgbox( "В файле \"" + f_lots_name + "\" не найдено поле \"Дата заключения сделки\"");
    exit(1);
  end;

  Message( "Выполняется вставка налоговых лотов из файла в базу" );

  Rewind(f_lots_txt);
  while( Next(f_lots_txt) )
    nRec = nRec + 1;
  end;

  InitProgress( nRec-2, "Выполняется обработка налоговых лотов", "Обработка налоговых лотов" );

  Rewind(f_lots_txt);
  Next(f_lots_txt); /*пропускаем шапку таблицы и нумерацию колонок таблицы*/
  Next(f_lots_txt);

  while( Next(f_lots_txt) )
    LotID = strsubst(f_lots_txt(iIDColNum), " ", "");

    sql =  " INSERT INTO dsctxlot_dbt (T_DEALID, "
        +                            "T_FIID, "
        +                            "T_TYPE, "
        +                            "T_VIRTUALTYPE, "
        +                            "T_DEALCODE, "
        +                            "T_DEALCODETS, "
        +                            "T_BUYID, "
        +                            "T_REALID, "
        +                            "T_DEALDATE, "
        +                            "T_DEALTIME, "
        +                            "T_BUYDATE, "
        +                            "T_SALEDATE, "
        +                            "T_RETRDATE, "
        +                            "T_AMOUNT, "
        +                            "T_NETTINGID, "
        +                            "T_PRICE, "
        +                            "T_PRICEFIID, "
        +                            "T_PRICECUR, "
        +                            "T_OLDTYPE, "
        +                            "T_NETTING, "
        +                            "T_SALE, "
        +                            "T_RETFLAG, "
        +                            "T_ISFREE, "
        +                            "T_ORDFORSALE, "
        +                            "T_ORDFORREPO ) "
        +  "SELECT olot.T_DEALID, " /* T_DEALID */
        +         "olot.T_FIID,  " /* T_FIID */
        +         "(CASE WHEN olot.T_TYPE = 0 THEN 0 " /*TAXLOTS_UNDEF*/
        +               "WHEN olot.T_TYPE = 1 THEN 1 " /*TAXLOTS_BUY*/
        +               "WHEN olot.T_TYPE = 2 THEN 2 " /*TAXLOTS_SALE*/
        +               "WHEN olot.T_TYPE = 3 THEN 3 " /*TAXLOTS_REPO*/
        +               "WHEN olot.T_TYPE = 4 THEN 4 " /*TAXLOTS_BACKREPO*/
        +          "ELSE olot.T_TYPE END) t_Type, " /* T_TYPE */
        +         "(CASE WHEN olot.T_ISVIRTUAL = CHR(0) THEN 1 "
        +               "ELSE 3 END) t_VIRTUALTYPE, " /* T_VIRTUALTYPE */
        +         "olot.T_NUMBER T_DEALCODE, "  /* T_DEALCODE */
        +         "? T_DEALCODETS, " /* T_DEALCODETS */
        +         "olot.T_BUYID, " /* T_BUYID */
        +         "0 T_REALID, " /* T_REALID */
        +         "? T_DEALDATE, " /* T_DEALDATE */
        +         "(CASE WHEN olot.T_TYPE IN (1,4) THEN olot.T_TIME " 
        +               "ELSE olot.T_TIME2 END) T_DEALTIME, " /* T_DEALTIME */
        +         "(CASE WHEN olot.T_TYPE IN (1,4) THEN olot.T_DATE " 
        +               "ELSE olot.T_DATE2 END) T_BUYDATE, " /* T_BUYDATE */
        +         "(CASE WHEN olot.T_TYPE IN (2,3) THEN olot.T_DATE " 
        +               "ELSE olot.T_DATE2 END) T_SALEDATE, " /* T_SALEDATE */
        +         "TO_DATE('01.01.0001','DD.MM.YYYY') T_RETRDATE, " /* T_RETRDATE */
        +         "? T_AMOUNT, "; /* T_AMOUNT */

    if( IsVirtualLot(LotID) )
      sql = sql 
          +      "0 T_NETTINGID, "      /* T_NETTINGID */
          +      "0 T_PRICE, "          /* T_PRICE */
          +      "-1 T_PRICEFIID, "     /* T_PRICEFIID */
          +      "CHR(1) T_PRICECUR, "; /* T_PRICECUR */
    else
      sql = sql 
          +      "(CASE WHEN Rsb_Secur.IsRepo(Opr.oGrp) <> 1 AND "
          +                 "Rsb_Secur.IsLoan(Opr.oGrp) <> 1 AND "
          +                 "rsb_sctx.CheckExistsNote(14, " /*NOTEKIND_DEAL_REGULAT*/
          +                                          "DECODE(tick.t_BofficeKind, 117, 117, " /*DL_RETIREMENT*/ /*OBJTYPE_RETIRE*/
          +                                           "130, 117, " /*DL_RETIREMENT_DST*/ /*OBJTYPE_RETIRE*/
          +                                           "101), " /*OBJTYPE_SECDEAL*/
          +                                          "tick.t_DealID) <> 1 "
          +            "THEN (NVL((SELECT t_DocumentID "
          +                         "FROM dpmlink_dbt "
          +                        "WHERE t_InitialPayment = paym.t_PaymentID AND "
          +                              "t_LinkKind = 1 AND " /*PMLINK_KIND_NETTING*/
          +                              "t_DocKind = 140), 0) ) " /*DL_NTGDOC*/
          +       "ELSE 0 END) T_NETTINGID, " /* T_NETTINGID */
          +      "(CASE WHEN leg.t_RelativePrice = CHR(0) OR "
          +                 "leg.t_RelativePrice IS NULL OR "
          +                 "paym.t_DocKind = 117 THEN leg.t_Price " /*DL_RETIREMENT*/
          +       "ELSE rsi_rsb_fiinstr.FI_GetNominalOnDate(leg.t_PFI, paym.t_ValueDate)*leg.t_Price/100 END) T_PRICE, " /* T_PRICE */
          +      "leg.t_CFI T_PRICEFIID, " /* T_PRICEFIID */
          +      "fin.t_CCY T_PRICECUR, "; /* T_PRICECUR */
    end;

    sql = sql 
        +        "0 T_OLDTYPE, " /* T_OLDTYPE */
        +        "olot.T_NETTING, " /* T_NETTING */
        +        "0 T_SALE, " /* T_SALE */
        +        "CHR(0) T_RETFLAG, " /* T_RETFLAG */
        +        "CHR(0) T_ISFREE, " /* T_ISFREE */
        +        "0 T_ORDFORSALE, " /* T_ORDFORSALE */
        +        "0 T_ORDFORREPO " /* T_ORDFORREPO */
        +   "FROM dsctaxlot_dbt olot ";

    if( not IsVirtualLot(LotID) )
      sql = sql + ", "
          +      "ddl_tick_dbt tick, "
          +      "dpmpaym_dbt paym, "
          +      "ddl_leg_dbt leg, "
          +      "dfininstr_dbt fin, "
          +      "(SELECT t_Kind_Operation, t_DocKind, rsb_secur.get_OperationGroup(t_SysTypes) oGrp "
          +         "FROM doprkoper_dbt) Opr ";
    end;

    sql = sql
        +  "WHERE olot.t_ID = ? ";

    if( not IsVirtualLot(LotID) )
      sql = sql 
          + "AND tick.t_DealID = olot.t_DealID "
          + "AND paym.t_DocumentID = tick.t_DealID "
          + "AND paym.t_DocKind = tick.t_BOfficeKind "
          + "AND leg.t_DealID = tick.t_DealID "
          + "AND leg.t_LegID = 0 "
          + "AND leg.t_LegKind = DECODE(paym.t_Purpose, 1, 0, 2) " /*BAi*/
          + "AND fin.t_FIID = leg.t_CFI "
          + "AND Opr.t_Kind_Operation = tick.t_DealType "
          + "AND Opr.t_DocKind = tick.t_BOfficeKind "
          + "AND rsb_sctx.get_lotType(Opr.oGrp, tick.t_DealID, Paym.t_Purpose) = olot.t_Type "
          + "AND (Paym.t_Purpose = 1 OR " /*BAi*/
          +      "(Paym.t_Purpose = 3 AND " /*BRi*/
          +       "not(Rsb_Secur.IsRepo(Opr.oGrp)=1 AND Rsb_Secur.IsBuy(Opr.oGrp)=1) AND "
          +       "not(Rsb_Secur.IsRepo(Opr.oGrp)=1 AND Rsb_Secur.IsSale(Opr.oGrp)=1) AND "
          +       "not(Rsb_Secur.IsRepo(Opr.oGrp)<>1 AND Rsb_Secur.IsBackSale(oGrp)<>1 AND Rsb_Secur.IsSale(Opr.oGrp)=1 AND Rsb_Secur.IsLoan(Opr.oGrp)=1) AND "
          +       "not(Rsb_Secur.IsRepo(Opr.oGrp)<>1 AND Rsb_Secur.IsBackSale(oGrp)<>1 AND Rsb_Secur.IsBuy(Opr.oGrp)=1 AND Rsb_Secur.IsLoan(Opr.oGrp)=1) "
          +      ") "
          +     ") ";
    end;

    cmd = RSDCommand( sql );

    cmd.addParam( "", RSDBP_IN, f_lots_txt(iDealCodeTSColNum));
    cmd.addParam( "", RSDBP_IN, date(f_lots_txt(iDealDateColNum)) );
    cmd.addParam( "", RSDBP_IN, money(strsubst(strsubst(f_lots_txt(iAmountColNum), " ", ""), ",", ".")) );
    cmd.addParam( "", RSDBP_IN, int(strsubst(f_lots_txt(iIDColNum), " ", "")) );

    Message( "Вставка нового налогового лота " + f_lots_txt(iDealCodeTSColNum));

    cmd.Execute();

    /* заполним таблицу соответствий */
    /* т.к. работаем в монопольном режиме, MAX(t_ID) будетм последняя вставленная запись */
    sql = " SELECT MAX(t_ID) t_ID "
        +   " FROM dsctxlot_dbt ";

    rsd = TRsbDataSet(sql);
    if( rsd.MoveNext() and (rsd.ID != 0) ) 
      SQL_Execute(" INSERT INTO dsctxlot_repl(T_OLDID, t_NEWID) VALUES(" + 
                    string(LotID) + ", " + string(int(rsd.ID)) + ")", "Заполнение таблицы соответствий");
    end;

    UseProgress( iCount = iCount + 1 );
  end;

  RemProgress();

  /* определим кол-во вставленных лотов */
  sql = " SELECT COUNT(1) iCount "
      +   " FROM dsctxlot_repl ";
  rsd = TRsbDataSet(sql);
  if( rsd.MoveNext() ) 
    iCount = int(rsd.iCount);
  else
    iCount = 0;
  end;

  println( "Загружено " + iCount + " налоговых лотов" );
  return;
END;

/* проверить на задвоенность новые лоты */
PRIVATE MACRO CheckNewLots()
 var sql, rsd,
     stat = true;

  sql = " SELECT t_oldid "
      +   " FROM dsctxlot_repl "
      +  " GROUP BY t_oldid "
      + " HAVING COUNT(t_oldid) > 1 ";

  rsd = TrsbDataSet(sql);
  while( rsd.MoveNext() )
    println( "Ошибка. Лот с ИД (до конвертирования) dsctaxlot_dbt.t_ID = " + rsd.OldID + " выгрузился более одного раза " );

    stat = false;
  end;

  return stat;
END;

/* вставка связей НУ */
PRIVATE MACRO InsertLnks()
  var cmd, sql, nRec = 0, iCount = 0, LinkType, 
      iBuyIDColNum, iSaleIDColNum, iSourceIDColNum, iDestIDColNum, 
      iTypeIDColNum, iAmountIDColNum;

  iBuyIDColNum = FindColsByName(LnkCol, "ИД покупки (внутренний идентификатор)");
  if( iBuyIDColNum < 0 )
    msgbox( "В файле \"" + f_lnks_name + "\" не найдено поле \"ИД покупки (внутренний идентификатор)\"");
    exit(1);
  end;

  iSaleIDColNum = FindColsByName(LnkCol, "ИД продажи (внутренний идентификатор)");
  if( iSaleIDColNum < 0 )
    msgbox( "В файле \"" + f_lnks_name + "\" не найдено поле \"ИД продажи (внутренний идентификатор)\"");
    exit(1);
  end;

  iSourceIDColNum = FindColsByName(LnkCol, "ИД источника (внутренний идентификатор)");
  if( iSourceIDColNum < 0 )
    msgbox( "В файле \"" + f_lnks_name + "\" не найдено поле \"ИД источника (внутренний идентификатор)\"");
    exit(1);
  end;

  iDestIDColNum = FindColsByName(LnkCol, "ИД окончат. продажи (внутренний идентификатор)");
  if( iDestIDColNum < 0 )
    msgbox( "В файле \"" + f_lnks_name + "\" не найдено поле \"ИД окончат. продажи (внутренний идентификатор)\"");
    exit(1);
  end;

  iTypeIDColNum = FindColsByName(LnkCol, "ИД типа связи (внутренний идентификатор)");
  if( iTypeIDColNum < 0 )
    msgbox( "В файле \"" + f_lnks_name + "\" не найдено поле \"ИД типа связи (внутренний идентификатор)\"");
    exit(1);
  end;

  iAmountIDColNum = FindColsByName(LnkCol, "Количество");
  if( iAmountIDColNum < 0 )
    msgbox( "В файле \"" + f_lnks_name + "\" не найдено поле \"Количество\"");
    exit(1);
  end;

  Message( "Выполняется вставка связей" );

  Rewind(f_lnks_txt);
  while( Next(f_lnks_txt) )
    nRec = nRec + 1;
  end;

  InitProgress( nRec-2, "Выполняется обработка связей", "Обработка связей" );

  Rewind(f_lnks_txt);
  Next(f_lnks_txt); /*пропускаем шапку таблицы и нумерацию колонок таблицы*/
  Next(f_lnks_txt);

  while( Next(f_lnks_txt) )

    LinkType = f_lnks_txt(iTypeIDColNum);

    sql =  " INSERT INTO dsctxlnk_dbt (T_SALEID, "
        +                            " T_BUYID, "
        +                            " T_TYPE, "
        +                            " T_DATE, "
        +                            " T_AMOUNT, "
        +                            " T_SOURCEID, "
        +                            " T_DESTID, "
        +                            " T_LOT1ID, "
        +                            " T_LOT2ID, "
        +                            " T_SHORT, "
        +                            " T_RET, "
        +                            " T_RET2, "
        +                            " T_RETSP, "
        +                            " T_PARENTID, "
        +                            " T_PARENTDATE, "
        +                            " T_BEGDATE, "
        +                            " T_ENDDATE, "
        +                            " T_RETFLAG ) "
        + " SELECT (SELECT t_NEWID repl "
        +           " FROM dsctxlot_repl repl "
        +          " WHERE repl.T_OLDID = ?) T_SALEID, " /* T_SALEID */
        +        " (SELECT t_NEWID repl "
        +           " FROM dsctxlot_repl repl "
        +          " WHERE repl.T_OLDID = ?) T_BUYID, " /* T_BUYID */
        +        " ? T_TYPE, " /* T_TYPE */
        +        " (SELECT lot.T_SALEDATE "
        +           " FROM dsctxlot_repl repl, "
        +                " dsctxlot_dbt lot "
        +           "WHERE repl.T_OLDID = ?"
        +            " AND lot.t_ID = repl.t_NEWID) T_DATE, " /* T_DATE */
        +        "? T_AMOUNT, " /* T_AMOUNT */
        +        " NVL((SELECT t_NEWID repl "
        +               " FROM dsctxlot_repl repl "
        +              " WHERE repl.T_OLDID = ?), 0) T_SOURCEID, " /* T_SOURCEID */
        +        " NVL((SELECT t_NEWID repl "
        +               " FROM dsctxlot_repl repl "
        +              " WHERE repl.T_OLDID = ?), 0) T_DESTID, " /* T_DESTID */
        +        " 0 T_LOT1ID, " /* T_LOT1ID */
        +        " 0 T_LOT2ID, " /* T_LOT2ID */
        +        " 0 T_SHORT, " /* T_SHORT */
        +        " 0 T_RET, " /* T_RET */
        +        " 0 T_RET2, " /* T_RET2 */
        +        " 0 T_RETSP, " /* T_RETSP */
        +        " 0 T_PARENTID, " /* T_PARENTID */
        +        " TO_DATE('01-01-0001', 'DD-MM-YYYY') T_PARENTDATE, " /* T_PARENTDATE */
        +        " TO_DATE('01-01-0001', 'DD-MM-YYYY') T_BEGDATE, " /* T_BEGDATE */
        +        " TO_DATE('01-01-0001', 'DD-MM-YYYY') T_ENDDATE, " /* T_ENDDATE */
        +        " CHR(0) T_RETFLAG " /* T_RETFLAG */
        +   " FROM dual ";

    cmd = RSDCommand( sql );

    cmd.addParam( "", RSDBP_IN, int(strsubst(f_lnks_txt(iSaleIDColNum), " ", "")) );
    cmd.addParam( "", RSDBP_IN, int(strsubst(f_lnks_txt(iBuyIDColNum), " ", "")) );

    if  ( LinkType == TAXLNK_REPO )
      cmd.addParam( "", RSDBP_IN, TXLNK_DELREPO );
    elif( LinkType == TAXLNK_OPSPOS )
      cmd.addParam( "", RSDBP_IN, TXLNK_OPSREPO );
    end;

    cmd.addParam( "", RSDBP_IN, int(strsubst(f_lnks_txt(iSaleIDColNum), " ", "")) );
    cmd.addParam( "", RSDBP_IN, money(strsubst(strsubst(f_lnks_txt(iAmountIDColNum), " ", ""), ",", ".")) );
    cmd.addParam( "", RSDBP_IN, int(strsubst(f_lnks_txt(iSourceIDColNum), " ", "")) );
    cmd.addParam( "", RSDBP_IN, int(strsubst(f_lnks_txt(iDestIDColNum), " ", "")) );

    Message( "Вставка новой связи" );

    cmd.Execute();

    UseProgress( iCount = iCount + 1 );
  end;

  RemProgress();

  println( "Загружено " + iCount + " связей" );
  return;
END;

/* возвращаем лоты приобретения из закрытых в прошлом периоде закрытых сделок РЕПО*/ 
PRIVATE MACRO UpdateLinks()
  var cmd, sql;

  sql = " UPDATE dsctxlot_dbt "
      +    " SET t_RetFlag = CHR(88) "
      +  " WHERE t_Type IN (" + string(TXLOTS_BACKREPO) + ", " + string(TXLOTS_LOANGET) + ") "
      +    " AND t_SaleDate < " + Date302SQL
      +    " AND t_SaleDate > TO_DATE('01-01-0001','DD-MM-YYYY') ";

  cmd = RSDCommand( sql );
  cmd.Execute();

  sql = " UPDATE dsctxlnk_dbt lnk "
      +    " SET lnk.t_RetFlag = CHR(88) "
      +  " WHERE lnk.t_Type IN (" + TXLNK_DELREPO + ", " + 
                                    TXLNK_SUBSTREPO + ", " + 
                                    TXLNK_LOANPUT + ", " + 
                                    TXLNK_SUBSTLOAN + ") "
      +    " AND ( ( Exists(SELECT lot.t_Type "
      +                     " FROM dsctxlot_dbt lot "
      +                    " WHERE lot.t_ID = lnk.t_SaleID "
      +                      " AND lot.t_Type = " + string(TXLOTS_REPO)
      +                      " AND lot.t_BuyDate < " + Date302SQL
      +                      " AND lot.t_BuyDate > TO_DATE('01-01-0001','DD-MM-YYYY') "
      +                      " AND lot.t_BuyDate <> lot.t_SaleDate "
      +        " ) AND "
      +        " lnk.t_Type IN (" + string(TXLNK_DELREPO) + ", " + string(TXLNK_SUBSTREPO) + ") ) OR "
      +          " ( Exists(SELECT lot.t_Type "
      +                     " FROM dsctxlot_dbt lot "
      +                    " WHERE lot.t_ID = lnk.t_SaleID "
      +                      " AND lot.t_Type = " + string(TXLOTS_LOANPUT)
      +                      " AND lot.t_BuyDate < " + Date302SQL
      +                      " AND lot.t_BuyDate > TO_DATE('01-01-0001','DD-MM-YYYY') "
      +                      " AND lot.t_BuyDate <> lot.t_SaleDate "
      +        " ) AND "
      +        " lnk.t_Type IN (" + string(TXLNK_LOANPUT) + ", " + string(TXLNK_SUBSTLOAN) + "))) ";

  cmd = RSDCommand( sql );
  cmd.Execute();
END;


if( not OpenFiles() )
  exit(1);
end;

CreateTempTable();
SetDelim(txtDelim);
FillCols(f_lots_txt, LotCol);
FillCols(f_lnks_txt, LnkCol);
InitRegistry();
InsertLots();
if( CheckNewLots() )
  InsertLnks();
end;
UpdateLinks();
DropTempTable();
