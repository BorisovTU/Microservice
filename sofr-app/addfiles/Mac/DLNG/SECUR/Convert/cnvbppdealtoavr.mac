/*
$Name:        cnvbppdealtoavr.mac
$Module:      Ценные бумаги
$Description: Конвертация при переходе с посделочных счетов БПП на портфельные
*/

import DealsInter, spserv, sp_class, sp_car, spreserv;

private var CnvDate  = {curdate}; //Дата конвертации (проводок)
private var TestMode = true;      //Тестовый режим (без фиксации действий в БД)

macro cnv_msgbox(StrErr)
  println(StrErr);
end;

PRIVATE MACRO GetCCY(FIID)
  var cmd = DL_RSDCommand("select t_CCY from dfininstr_dbt where t_FIID = ?");
  var ccy = "";

  cmd.AddParam(FIID);

  var DataSet = cmd.Execute();
  if(DataSet.moveNext())
    ccy = DataSet.CCY;
  end;

  return ccy;
END;

// ────────────────────────────────
// Установить дату деактивации 
// ────────────────────────────────
private macro setAccDisablingDate(McAccDocID, DisDate)
  var exec = RSDCommand(" UPDATE dmcaccdoc_dbt "+
                        "    SET t_DisablingDate = ?,"+
                        "        t_IsUsable = chr(0) "+
                        "  WHERE t_ID = ?"      );
  
  exec.addParam( "", RSDBP_IN, DisDate );
  exec.addParam( "", RSDBP_IN, McAccDocID );

  exec.execute();

  return 0;
OnError(er)
  return 1;
end;

private macro StartExec(FIID)
  var query, cmd, DataSet;
  var OldAcc = TRecHandler("account.dbt");
  var NewAcc = TRecHandler("account.dbt");
  var CatCode = "", Currency;
  var FD = NULL, FDfin = NULL;
  var err = 0, msg="";
  var AcCurSec = УЧЕТ_ВАЛЮТНЫХ_ДОЛЕВЫХ_ЦБ();
  var SumEquivalentCarry = null;
  var i = 0;
  var q, sel, ds;

  if(TestMode == true)
    msgbox("ТЕСТОВЫЙ РЕЖИМ! (без фиксации действий в БД)");
    msgbox("");
  end;

  if(FIID == NULL)
    FIID = -1;
  end;

  query =   "with cat as (select DISTINCT c.t_Code as CatCode, accdoc.t_Account, accdoc.t_Currency, accdoc.t_Chapter,  "
          + "                    (CASE WHEN c.t_Code IN ('Ц/б, Корзина БПП', 'Уплач. НКД, Корзина БПП', 'Ц/б, Корзина ПКУ БПП') THEN accdoc.t_FIID "
          + "                          WHEN accdoc.t_DocKind = "+DL_SECURLEG+" THEN (select (CASE WHEN rsb_fiinstr.FI_IsBasket(leg.t_PFI) = 0 THEN leg.t_PFI ELSE -1 END) "
          + "                                                                          from ddl_leg_dbt leg "
          + "                                                                         where leg.t_ID = accdoc.t_DocID"
          + "                                                                       ) " 
          + "                          ELSE -1 "
          + "                     END) as FIID, "
          + "                    (select (CASE WHEN c.t_Class1 = "+LLCLASS_KIND_DEAL_DELAY+" AND tpl.t_Value1 = 1 THEN 1 "
          + "                                  WHEN c.t_Class2 = "+LLCLASS_KIND_DEAL_DELAY+" AND tpl.t_Value2 = 1 THEN 1 " 
          + "                                  WHEN c.t_Class3 = "+LLCLASS_KIND_DEAL_DELAY+" AND tpl.t_Value3 = 1 THEN 1 "
          + "                                  WHEN c.t_Class4 = "+LLCLASS_KIND_DEAL_DELAY+" AND tpl.t_Value4 = 1 THEN 1 "
          + "                                  WHEN c.t_Class5 = "+LLCLASS_KIND_DEAL_DELAY+" AND tpl.t_Value5 = 1 THEN 1 "
          + "                                  WHEN c.t_Class6 = "+LLCLASS_KIND_DEAL_DELAY+" AND tpl.t_Value6 = 1 THEN 1 "
          + "                                  WHEN c.t_Class7 = "+LLCLASS_KIND_DEAL_DELAY+" AND tpl.t_Value7 = 1 THEN 1 "
          + "                                  WHEN c.t_Class8 = "+LLCLASS_KIND_DEAL_DELAY+" AND tpl.t_Value8 = 1 THEN 1 "
          + "                                  ELSE 0 "
          + "                             END)"
          + "                       from dmctempl_dbt tpl "
          + "                      where tpl.t_CatID = accdoc.t_CatID "
          + "                        and tpl.t_Number = accdoc.t_TemplNum"
          + "                    ) as IsOverdue, "
          + "                    (select (CASE WHEN c.t_Class1 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value1 "
          + "                                  WHEN c.t_Class2 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value2 " 
          + "                                  WHEN c.t_Class3 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value3 "
          + "                                  WHEN c.t_Class4 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value4 "
          + "                                  WHEN c.t_Class5 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value5 "
          + "                                  WHEN c.t_Class6 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value6 "
          + "                                  WHEN c.t_Class7 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value7 "
          + "                                  WHEN c.t_Class8 = "+LLCLASS_KINDPORT+" THEN tpl.t_Value8 "
          + "                                  ELSE 0 "
          + "                             END)"
          + "                       from dmctempl_dbt tpl "
          + "                      where tpl.t_CatID = accdoc.t_CatID "
          + "                        and tpl.t_Number = accdoc.t_TemplNum"
          + "                    ) as Portfolio, "
          + "                    (CASE WHEN accdoc.t_DocKind = "+DL_SECURLEG+" THEN "
          + "                                 (select tk.t_DealID "
          + "                                    from ddl_leg_dbt leg, ddl_tick_dbt tk "
          + "                                   where leg.t_ID = accdoc.t_DocID "
          + "                                     and tk.t_DealID = leg.t_DealID "
          + "                                 )"
          + "                          ELSE "
          + "                                 (select tk.t_DealID "
          + "                                    from ddl_tick_ens_dbt ens, ddl_tick_dbt tk "
          + "                                   where ens.t_ID = accdoc.t_DocID "
          + "                                     and tk.t_DealID = ens.t_DealID "
          + "                                 )"
          + "                     END ) as DealID "
          + "               from dmcaccdoc_dbt accdoc, dmccateg_dbt c "
          + "              where c.t_LevelType = 1 "
          + "                and c.t_Code IN ('Ц/б, БПП', 'Ц/б, Корзина БПП', 'Уплаченный НКД, БПП', 'Уплач. НКД, Корзина БПП', 'Ц/б, ПКУ БПП', 'Ц/б, Корзина ПКУ БПП')"
          + "                and accdoc.t_CatID = c.t_ID "
          + "                and accdoc.t_DocKind IN (" + DL_SECURLEG + ", "+DL_TICK_ENS_DOC+")"
          + "            ) "
          + "select acc.*, abs(rsb_account.restac(acc.t_Account, acc.t_Code_Currency, ?, acc.t_Chapter, null)) as RestAcc, "
          + "       cat.FIID, cat.IsOverdue, cat.Portfolio, cat.CatCode, cat.DealID "
          + "  from daccount_dbt acc, cat "
          + " where acc.t_Account = cat.t_Account "
          + "   and acc.t_Code_Currency = cat.t_Currency "
          + "   and acc.t_Chapter = cat.t_Chapter "
          + "   and acc.t_Open_Close = CHR(0)";

  
  cmd = DL_RSDCommand();

  cmd.AddParam(CnvDate);
  
  if(FIID > 0 )
    query = query + " and cat.FIID = ? ";
    cmd.AddParam(FIID);
  end;

  var cnt = cmd.GetCount(query);
  if(cnt > 0)
    SP_BeginCreateDeal(DL_SECURITYDOC);

    RslDefCon.BeginTrans();

    InitProgress(cnt, "Обработка счетов", "Обработка счетов");

    DataSet = cmd.Execute(query);
    while(DataSet.moveNext())

      err = 0;

      if(DataSet.RestAcc > 0)
        
        DataSet.GetRecord().CopyTo(OldAcc.rec);

        msgbox("Обрабатывается счет "+OldAcc.rec.Account+" КУ \"" + DataSet.CatCode + "\"");

        if(DataSet.FIID <= 0)
          msgbox("    По счету "+OldAcc.rec.Account+" не определена ц/б для открытия нового счета. Счет обработан не будет.");
          err = 1;
        end;

        if(err == 0)
          SumEquivalentCarry = 0;

          FDfin = SPFirstDocFI(DLDOC_ISSUE, int(DataSet.FIID), int(DataSet.FIID), int(DataSet.Portfolio), {OurBank}, null, null, null);
          
          FD = SPFirstDoc( LEG_KIND_DL_TICK, int(DataSet.DealID) );
          FD.SetCurPFI(int(DataSet.FIID));

          CatCode = "";
          if((DataSet.CatCode == "Ц/б, БПП") or (DataSet.CatCode == "Ц/б, Корзина БПП"))
            CatCode = "Наш портфель ц/б";
            Currency = SP_WRTDetermineAccFI(int(DataSet.FIID));

            if((AcCurSec == 0) and (not FI_IsBond(int(DataSet.FIID))) and (FD.fininstr.rec.FaceValueFI != NATCUR))
              q =   " select NVL(SUM(RSI_RSB_FIInstr.ConvSum(lot.t_Cost, ?, "+NATCUR+", lot.t_BegDate, 1)), 0) as NatCost "
                  + "   from dmcaccdoc_dbt accdoc, ddl_leg_dbt leg, ddl_tick_dbt tk, ddlrq_dbt rq, dpmwrtsum_dbt lot "
                  + "  where accdoc.t_Account = ? "
                  + "    and accdoc.t_Chapter = ? "
                  + "    and accdoc.t_Currency = ? "
                  + "    and accdoc.t_DocKind = " + DL_SECURLEG
                  + "    and leg.t_ID = accdoc.t_DocID "
                  + "    and tk.t_DealID = leg.t_DealID "
                  + "    and rq.t_DocKind = tk.t_BOfficeKind "
                  + "    and rq.t_DocID = tk.t_DealID "
                  + "    and rq.t_FIID = ? "
                  + "    and rq.t_DealPart = 2 "
                  + "    and rq.t_Type = " + DLRQ_TYPE_DELIVERY
                  + "    and lot.t_DocKind = 29 "
                  + "    and lot.t_DocID = rq.t_ID "
                  + "    and lot.t_Kind = " + PM_WRTSUM_KIND_RRWAB2;
                   
              sel = DL_RSDCommand(q);

              sel.AddParam(FD.fininstr.rec.FaceValueFI);
              sel.AddParam(OldAcc.rec.Account);
              sel.AddParam(OldAcc.rec.Chapter);
              sel.AddParam(OldAcc.rec.Code_Currency);
              sel.AddParam(int(DataSet.FIID));

              ds = sel.Execute();
              if(ds.moveNext())
                SumEquivalentCarry = money(ds.NatCost);
              end;

              q =   " select NVL(SUM(RSI_RSB_FIInstr.ConvSum(lot.t_Cost, ?, "+NATCUR+", lot.t_BegDate, 1)), 0) as NatCost "
                  + "   from dmcaccdoc_dbt accdoc, ddl_tick_ens_dbt ens, ddl_tick_dbt tk, ddlrq_dbt rq, dpmwrtsum_dbt lot "
                  + "  where accdoc.t_Account = ? "
                  + "    and accdoc.t_Chapter = ? "
                  + "    and accdoc.t_Currency = ? "
                  + "    and accdoc.t_DocKind = " + DL_TICK_ENS_DOC
                  + "    and ens.t_ID = accdoc.t_DocID "
                  + "    and tk.t_DealID = ens.t_DealID "
                  + "    and rq.t_DocKind = tk.t_BOfficeKind "
                  + "    and rq.t_DocID = tk.t_DealID "
                  + "    and rq.t_FIID = ? "
                  + "    and rq.t_DealPart = 2 "
                  + "    and rq.t_Type = " + DLRQ_TYPE_DELIVERY
                  + "    and lot.t_DocKind = 29 "
                  + "    and lot.t_DocID = rq.t_ID "
                  + "    and lot.t_Kind = " + PM_WRTSUM_KIND_RRWAB2;
                   
              sel = DL_RSDCommand(q);

              sel.AddParam(FD.fininstr.rec.FaceValueFI);
              sel.AddParam(OldAcc.rec.Account);
              sel.AddParam(OldAcc.rec.Chapter);
              sel.AddParam(OldAcc.rec.Code_Currency);
              sel.AddParam(int(DataSet.FIID));

              ds = sel.Execute();
              if(ds.moveNext())
                SumEquivalentCarry = SumEquivalentCarry + money(ds.NatCost);
              end;
            else
              SumEquivalentCarry = money(DataSet.RestAcc);
            end;

          elif((DataSet.CatCode == "Уплаченный НКД, БПП") or (DataSet.CatCode == "Уплач. НКД, Корзина БПП"))
            CatCode = "Уплаченный НКД";
            Currency = FD.fininstr.rec.FaceValueFI;
            SumEquivalentCarry = money(DataSet.RestAcc);
          elif((DataSet.CatCode == "Ц/б, ПКУ БПП") or (DataSet.CatCode == "Ц/б, Корзина ПКУ БПП"))
            CatCode = "Наш портфель ПКУ, ц/б";
            Currency = OldAcc.rec.Code_Currency;
            SumEquivalentCarry = money(DataSet.RestAcc);
          end;

          if( not FD.OpenAccount(CatCode, null, null, GetFIRoleByPortfolio(int(DataSet.Portfolio), null, null, true, IIF(int(DataSet.IsOverdue)==1, true, false) ), NewAcc, CnvDate, Currency) )
            msg = GetErrMsg();
            msgbox("    Ошибка при открытии счета по КУ \"" + CatCode+"\": "+msg+".");
            err = 1;
          else
            msgbox("    Под сделкой №"+FD.tick.rec.DealCode+" открыт новый счет "+NewAcc.rec.Account+" по КУ \"" + CatCode+"\"");
          end;

          if( not FDfin.OpenAccount(CatCode, null, null, GetFIRoleByPortfolio(int(DataSet.Portfolio), null, null, true, IIF(int(DataSet.IsOverdue)==1, true, false) ), NULL, CnvDate, Currency) )
            msg = GetErrMsg();
            msgbox("    Ошибка при привязке к ц/б счета по КУ \"" + CatCode+"\": "+msg+".");
            err = 1;
          else
            msgbox("    К ц/б привязан счет "+NewAcc.rec.Account+" по КУ \"" + CatCode+"\"");
          end;

          if(err == 0)
            if(not ПроводкаПоКатегориямУчета(FD, 
                   NewAcc, OldAcc,              /* КатегДеб , КатегКредит*/
                   CnvDate,                     /* Дата */
                   1,                           /* Глава */
                   OldAcc.rec.Code_Currency,    /* Валюта */
                   money(DataSet.RestAcc),      /* Сумма  */
                   NULL,                        /* Документ */
                   INPCARRY,                    /* Result_Carry */
                   " 1",                        /* Kind_Oper */
                   "",                          /* Numb_Document */
                   "Перенос остатка",
                   null, null, null, null, null,
                   Currency, SumEquivalentCarry
                  ) 
              )
                msg = GetErrMsg();
                if(msg != "")
                  msgbox("  Ошибка при выполнении проводки. "+msg);
                end;

                err = 1;
            else
              msgbox("    Выполнена проводка "+NewAcc.rec.Account+"<->"+OldAcc.rec.Account+" на сумму " + money(DataSet.RestAcc) + " " + GetCCY(OldAcc.rec.Code_Currency));
            end;
          end;

          if(err == 0)
            if( CB_CloseAccount(OldAcc.rec.Chapter, OldAcc.rec.Code_Currency, OldAcc.rec.Account, CnvDate, null) != 0 )
               msg = GetErrMsg();
               msgbox("    Ошибка закрытия счета: "+msg+". Счет "+OldAcc.rec.Account+" не закрыт.");
               if((AcCurSec == 0) and (not FI_IsBond(int(DataSet.FIID))) and (FD.fininstr.rec.FaceValueFI != NATCUR))
                 msgbox("    При необходимости следует скорректировать остаток в национальной валюте.");
               end;

               err = 1;
            else
               //Найти и закрыть привязку счета в КУ
               q =   " select accdoc.t_ID "
                   + "   from dmcaccdoc_dbt accdoc "
                   + "  where accdoc.t_Account = ? "
                   + "    and accdoc.t_Chapter = ? "
                   + "    and accdoc.t_Currency = ? "
                   + "    and accdoc.t_DocKind IN (" + DL_SECURLEG + ", "+DL_TICK_ENS_DOC+") "
                   + "    and accdoc.t_DisablingDate = " + GetSQLDate(date(0,0,0));

               sel = DL_RSDCommand(q);

               sel.AddParam(OldAcc.rec.Account);
               sel.AddParam(OldAcc.rec.Chapter);
               sel.AddParam(OldAcc.rec.Code_Currency);

               ds = sel.Execute();
               while(ds.moveNext())
                 if(setAccDisablingDate(ds.ID, CnvDate) != 0)
                   msg = GetErrMsg();
                   msgbox("    Ошибка закрытия привязкис счета: "+msg+". Привязка счета "+OldAcc.rec.Account+" к сделке №"+FD.tick.rec.DealCode+" не закрыта.");
                 else
                   msgbox("    Привязка счета "+OldAcc.rec.Account+" к сделке №"+FD.tick.rec.DealCode+" успешно закрыта!");
                 end;
               end;

               msgbox("    Счет "+OldAcc.rec.Account+" успешно закрыт!");
            end;
          end;
        end;

        msgbox("");
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();

    if(TestMode)
      RslDefCon.RollbackTrans();
    else
      RslDefCon.CommitTrans();
    end;

    SP_EndCreateDeal();
  else
    msgbox("Нет счетов для конвертации");
  end;

OnError( err );
  var j;
  var err_text = "", delim = "";
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  

  if (IsEqClass ("TrslError", err))
    err_text=err.Message;
    if(IsEqClass ("TDbError", err.err))
      err_text=err_text+":|"+err.err.Stat+"-"+err.err.Oper+"-"+err.err.Table+"-"+err.err.Message;
    elif (isEqClass("TRsdError", err.err))

      j = 0;
      while(j < err.err.environment.ErrorCount)
         err_text = err_text + delim  + err.err.environment.error(j).descr;
         delim = "\n";
         j = j + 1;
      end;
    end;
  end;

  if(err_text == "")
    err_text=err.module+ " "+err.line+" "+err.message;
  end;

  msgbox(err_text);

end;


ReplaceMacro("msgbox", "cnv_msgbox");

StartExec();

ReplaceMacro("msgbox", NULL );
  
