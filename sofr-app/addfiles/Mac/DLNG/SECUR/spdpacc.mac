/*
$Name:         spdpacc.mac
$Module:       Ценные бумаги
$Description:  Отчет "Журнал лицевого счета ДЕПО". 
*/

import oprinter, CurrInter, FIInter, spaccfrm, dp_util, cb_sql;
import RsbDataSet;

const UNDEF_ID   = -1;    /* Значение, обозначающее, что ID не задан */
const DEPOATTR_ATTNUM_ISSUER = 13; /* характеристика "эмитент" */
const LLCLASS_TRNCONTENT_INVOPER = 435;

FILE TmpFileDoc( "acctrn.dbt" ) write;
var  PathWrkDir = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\WORKDIR";
var  NameJrn = "Отчет \"Журнал лицевого счета ДЕПО\".";
var  isPrint = false;
private var  _is_app, _NumDprt, __LogoprID, _BeginDate, _EndDate;

var Дебет_лицевой  = 0;
var Кредит_лицевой = 0;

var Glob_Error = 1;

macro IsDepoDraft( DocKind )

   if(   ( DocKind == SP_DEPOPER_BOOKORDER )
      or ( DocKind == SP_DEPOPER_TRANSFERORDER )
      or ( DocKind == SP_DEPOPER_ENROLMENT )
      or ( DocKind == SP_DEPOPER_RECORDER )
      or ( DocKind == SP_DEPOPER_WITHDRORDER )
     )
     return true;
   end;

   return false;
end;

macro IsDepoGlobop( DocKind )

  if(   ( DocKind == DP_DEPOPER_CONVERT )
     or ( DocKind == DP_DEPOPER_REPAY )
     or ( DocKind == DP_DEPOPER_BONEMISS )
    )
    return true;
  end;

  return false;
end;


var Head_Table = "┌─────┬───────┬─────┬──────────────────┬──────────────────┬─────────────────┬──────────────┬──────────────┬──────────────┬──────────────┬─────────────────────────┬────────────────────────────┐\n"+
                 "│ №   │ №     │ КОП │ Содержание       │ Поручение        │ Операция        │ Остаток      │ Дебет        │ Кредит       │ Остаток      │ Корреспонденция         │  Отчет об исполнении,      │\n"+
                 "│ п/п │ опер. │     │ транзакции       │ дата приема/Вх.№ │ дата исп./время │ на начало    │              │              │ на конец     │                         │  сертификаты               │\n"+
                 "├─────┼───────┼─────┼──────────────────┼──────────────────┼─────────────────┼──────────────┼──────────────┼──────────────┼──────────────┼─────────────────────────┼────────────────────────────┤";

private var tablewidth = Index(Head_Table, "\n");

var DpRep = CMakeReport(Head_Table);
const DpRepFontStyleTitel =  "ex_FS(b)";

class AccTrnSort(_AccTrnAutokey, _AccTrnDate, _AccTrnTime)
  var AccTrnAutokey, AccTrnDate, AccTrnTime;

  AccTrnAutokey = _AccTrnAutokey;
  AccTrnDate = _AccTrnDate;
  AccTrnTime = _AccTrnTime;
end;
var AccTrnSort_Arr = TArray;

class CInvCard(_Issuer, _FaceValue, _Issued, _certif, _NominalFICode, _NominalFIIDName)
  var Issuer          = _Issuer;    /* эмитент */
  var FaceValue       = _FaceValue; /* номинал */
  var Issued          = _Issued;    /* дата выпуска */
  var certif          = _certif;
  var NominalFICode   = _NominalFICode;  
  var NominalFIIDName = _NominalFIIDName;
end;
var CInvCard_Arr = TArray;
  
/* Данные для отчета */
class CData( acc, cur, BDate, EDate, RDate, Deponent, AutoKey )
  var  DepoInf; /* Раздел счета ДЕПО к которому привязан выбранный л/с */
  var  RootAcc; /* самый верхний счет ДЕПО для раздела DepoInf */
  var  НомерПроводки = 0; 
  var  ЧислоПроводок = 0; 
  var  NameFile; /* Имя временного файла */ 

  /* эти данные из панели ввода */
  var  DAccount    = acc;
  var  FIID        = cur;
  var  BeginDate   = BDate;
  var  EndDate     = EDate;
  var  RepDate        = RDate;
  var  iDeponent      = Deponent;
  var  AutoKey_logopr = AutoKey;
end;

/*
macro MakeDocumentID( Doc )
  return ( L_Z(Doc.iApplicationKind, 5) + Doc.ApplicationKey);
end;
*/

/* Создать и заполнить временный файл для отчета.
   Сначала переносятся все (подход. для отчета) аривные документы с сохранением
   их AutoKey, затем записи из document.dbt (в порядке создания документов 
   в document.dbt), при этом их AutoKey становится > чем у перенесенных ранее 
   архивных. (Т.о. сохраняется хронология выполнения проводок)
*/
macro CreateTmpFileDoc( Data )


  var  AccTrn;
  var query;
  file Draft  ( spdraft  ) key 0;
  file Globop ( dpcorpop ) key 0;
  file Ad     ( "acctrn.dbt"  ) key 0;
  file account( "account.dbt"  ) key 0;
  account.Chapter = 5;
  account.Account = Data.DAccount;
  account.Code_Currency = Data.FIID;
  if(not GetEQ(account))
    /*MsgBox("Не найден лицевой счет");*/
    return 1;
  end;
  var OprXid  = "";

  AccTrnSort_Arr.Size = 0;
  Data.ЧислоПроводок = 0;

  var  err, continue_cicle ;

   /* раздел к которому привязан счет */
   Data.DepoInf = DepoInfo(account.DepoAcc, Data.EndDate);
   if( Data.DepoInf == NULL )  return 1; end;

   if(Data.DepoInf.Root != 0)   /* самый верхний счет */
     Data.RootAcc = DepoInfo(Data.DepoInf.Root, Data.EndDate);
   else   /* счет привязан к счету(а не разделу) ДЕПО */
     Data.RootAcc = Data.DepoInf; /*DepoInfo(account.DepoAcc);*/
   end;

   if(Data.RootAcc == NULL)
     return 1;
   end;

  Message( NameJrn + " Создание временного файла ..." );
  if( not Create( TmpFileDoc, Data.NameFile ) or not Open( TmpFileDoc, Data.NameFile ) )
    return FALSE; 
  end;

  query =   RSDCommand(" select ad.t_AccTrnID, OprOper.t_DocKind, OprOper.t_DocumentID, OprStep.t_Syst_time "
          + "   from dacctrn_dbt ad, doprdocs_dbt OprDoc, doproper_dbt OprOper, doprstep_dbt OprStep "
          + "  where ad.t_Chapter = ? "
          + "    and ad.t_Account_Payer = ? "
          + "    and ad.t_FIID_Payer = ? "
          + "    and ad.t_Date_Carry >= ? "
          + "    and ad.t_Date_Carry <= ? "
          + "    and OprDoc.t_DocKind = ? "
          + "    and OprDoc.t_AccTrnID = ad.t_AccTrnID "
          + "    and ad.t_State = 1 "/*фактическая проводка*/
          + "    and OprDoc.t_Origin  = 0 "
          + "    and OprOper.t_ID_Operation = OprDoc.t_ID_Operation "
          + "    and OprStep.t_ID_Operation = OprOper.t_ID_Operation "
          + "    and OprStep.t_ID_step = OprDoc.t_ID_step "
          + " order by ad.t_Date_Carry, OprStep.t_Syst_time ");

  query.addParam( "", RSDBP_IN, synt_chapter );
  query.addParam( "", RSDBP_IN, Data.DAccount );
  query.addParam( "", RSDBP_IN, Data.FIID );
  query.addParam( "", RSDBP_IN, Data.BeginDate );
  query.addParam( "", RSDBP_IN, Data.EndDate );
  query.addParam( "", RSDBP_IN, DLDOC_CARRY );
  query.execute();

  AccTrn = TRsbDataSet(query);
  while( AccTrn.moveNext() ) 
    OprXid  = "";

    ClearRecord(Ad);
    Ad.AccTrnID = AccTrn.AccTrnID;
    GetEQ(Ad);
    Copy( TmpFileDoc, Ad );
    Insert( TmpFileDoc );

    if( IsDepoDraft( AccTrn.DocKind ) )
      /* поручение депо (первичный документ операции)*/
      Draft.AutoKey = int( AccTrn.DocumentID );
    
      if( not GetEQ(Draft) or (Draft.Kind != AccTrn.DocKind) )
        return FALSE; /*первичный документ - не поручение депо*/
      end;
      OprXid  = Draft.OprXid;
    elif( IsDepoGlobop( AccTrn.DocKind ) )
      /* глобальная операция (первичный документ операции)*/
      Globop.DocumentID = int( AccTrn.DocumentID );
    
      if( not GetEQ(Globop) or (Globop.DocKind != AccTrn.DocKind) )
        return FALSE; /*первичный документ - не поручение депо*/
      end;
      OprXid  = Globop.OprXid;
    else
      return false;
    end;

    AccTrnSort_Arr( AccTrnSort_Arr.Size ) = AccTrnSort(TmpFileDoc.AccTrnID, SQL_ConvTypeDate(TmpFileDoc.Date_Carry), SQL_ConvTypeTime(AccTrn.Syst_time)); 
    Data.ЧислоПроводок = Data.ЧислоПроводок + 1;
  end;

  query = RSDCommand(" select ad.t_AcctrnID, OprOper.t_DocKind, OprOper.t_DocumentID, OprStep.t_Syst_time "
          + "   from dacctrn_dbt ad, doprdocs_dbt OprDoc, doproper_dbt OprOper, doprstep_dbt OprStep "
          + "  where ad.t_Chapter = ? "
          + "    and ad.t_Account_Receiver = ? "
          + "    and ad.t_FIID_Receiver = ? "
          + "    and ad.t_Date_Carry >= ? "
          + "    and ad.t_Date_Carry <= ? "
          + "    and ad.t_State = 1 "/*фактическая проводка*/
          + "    and OprDoc.t_DocKind = ? "
          + "    and OprDoc.t_AccTrnID = ad.t_AccTrnID "
          + "    and OprDoc.t_Origin  = 0 "
          + "    and OprOper.t_ID_Operation = OprDoc.t_ID_Operation "
          + "    and OprStep.t_ID_Operation = OprOper.t_ID_Operation "
          + "    and OprStep.t_ID_step = OprDoc.t_ID_step "
          + " order by ad.t_Date_Carry, OprStep.t_Syst_time ");
  query.addParam( "", RSDBP_IN, synt_chapter );
  query.addParam( "", RSDBP_IN, Data.DAccount );
  query.addParam( "", RSDBP_IN, Data.FIID );
  query.addParam( "", RSDBP_IN, Data.BeginDate );
  query.addParam( "", RSDBP_IN, Data.EndDate );
  query.addParam( "", RSDBP_IN, DLDOC_CARRY );
  query.execute();
  AccTrn = TRsbDataSet(query);
  while( AccTrn.moveNext() )
    OprXid  = "";
  
    ClearRecord(Ad);
    Ad.AccTrnID = AccTrn.AccTrnID;
    GetEQ(Ad);
    Copy( TmpFileDoc, Ad );
    Insert( TmpFileDoc );

    if( IsDepoDraft( AccTrn.DocKind ) )
      /* поручение депо (первичный документ операции)*/
      Draft.AutoKey = int( AccTrn.DocumentID );
    
      if( not GetEQ(Draft) or (Draft.Kind != AccTrn.DocKind) )
        return FALSE; /*первичный документ - не поручение депо*/
      end;
      OprXid  = Draft.OprXid;
    elif( IsDepoGlobop( AccTrn.DocKind ) )
      /* глобальная операция (первичный документ операции)*/
      Globop.DocumentID = int( AccTrn.DocumentID );
    
      if( not GetEQ(Globop) or (Globop.DocKind != AccTrn.DocKind) )
        return FALSE; /*первичный документ - не поручение депо*/
      end;
      OprXid  = Globop.OprXid;
    else
      return false;
    end;

    AccTrnSort_Arr( AccTrnSort_Arr.Size ) = AccTrnSort(TmpFileDoc.AccTrnID, SQL_ConvTypeDate(TmpFileDoc.Date_Carry), SQL_ConvTypeTime(AccTrn.Syst_time)); 

    Data.ЧислоПроводок = Data.ЧислоПроводок + 1;
  end;
  
  KeyNum( TmpFileDoc, 0 ); /* По AutoKey */
  
  return TRUE;
end;

/* получить характеристику "эмитент" на счете */
macro FindDEPOATVLISSUER(AutoKey, IssuerID )
   file depoatvl("depoatvl");

   depoatvl.AttrNum = DEPOATTR_ATTNUM_ISSUER;
   depoatvl.IsType  = "";
   depoatvl.ID      = AutoKey;
   if ( getEQ(depoatvl) )
     IssuerID = int(Depoatvl.Value);
     SetParm( 1, IssuerID );
     return true;
   end;

   return false;
end;

/* получить характеристику "эмитент" на л/с */
//macro FindACCTRRTISSUER(account, IssuerID )
//   file accattr("accattr");

//   accattr.Chapter = account.Chapter;
//   accattr.Account = account.Account;
//   accattr.FIID    = account.Code_Currency;
//   accattr.AttrNum = DEPOATTR_ATTNUM_ISSUER;

//   if ( getEQ(accattr) )
//     IssuerID = int(accattr.Value);
//     SetParm( 1, IssuerID );
//     return true;
//   end;

//   return false;
//end;

macro ЗаголовокЖурнала( Data, iCount, IsAllDAcc )
   file account ("account");
   file fininstr(fininstr); 
   file avoiriss(avoiriss); 
   file avrkinds(avrkinds);
   file avoir(avoiriss);
   var depoacnt = TBFile("depoacnt", "R", 1);

   var LSIN            = "",
       Nominal         = "",
       NominalFICode   = "",
       NominalFIIDName = "",
       IssuerName      = "",
       IssuerCode      = "",
       Issued          = "",
       IssuerID        = -1,
       Tranche         = "";
   var FaceValueMoney:moneyl = $0;
   var cFin;
   var cIssuer;

   Message( NameJrn + " Заполнение анкеты счета ДЕПО ..." );
   account.Chapter = 5;
   account.Account = Data.DAccount;
   account.Code_Currency = Data.FIID;
   if(not GetEQ(account))
     /*MsgBox("Не найден лицевой счет");*/
     return 1;
   end;

   cFin = CDPFinInstr(Data.FIID, Data.EndDate);
   fininstr.FIID = Data.FIID;
   if(not GetEQ(fininstr))
     /*MsgBox("Не найден финансовый инструмент");*/
     return 1;
   end;

   avoiriss.FIID = Data.FIID;
   if( (fininstr.FI_Kind != FIKIND_AVOIRISS) or (not GetEQ(avoiriss)) )
     /*MsgBox("Не найдена ценная бумага");*/
     return 1;
   end;

   avrkinds.FI_Kind = fininstr.FI_Kind;
   avrkinds.AvoirKind = fininstr.AvoirKind;
   if(not GetEQ(avrkinds))
     MsgBox("Ненайден вид ценной бумаги");
     return 1;
   end;

   errstr1 = "Не найден счет ДЕПО для заданного лицевого"; /* особенности класса DepoInfo */

   if(ИндивидуальныйФинИн(fininstr.FIID))
      LSIN            = "-";
      Nominal         = "-"; 
      NominalFICode   = "-";
      NominalFIIDName = "-";
      IssuerCode      = "-";
      IssuerName      = "-";
      Issued          = "-";

      if( fininstr.Issuer != -1 ) /* если эмитент задан в анкете, то берем его */
        cIssuer = CDPParty(fininstr.Issuer, Data.EndDate );
        IssuerName      = cIssuer.Name();
        IssuerCode      = ПолучитьКодСубъекта( cIssuer.PARTYID(), PTCK_CONTR );
//!!!        Issued          = cIssuer.PARTYID();
        Issued          = fininstr.Issued;
      elif( FindDEPOATVLISSUER(Data.DepoInf.DepoAcount.rec.AutoKey, IssuerID ) and 
            (IssuerID != -1) ) /* иначе, если задана хар-ка "эмитент" на разделе, то берем ее */
        cIssuer = CDPParty(IssuerID, Data.EndDate );
        IssuerName      = cIssuer.Name();
        IssuerCode      = ПолучитьКодСубъекта( cIssuer.PARTYID(), PTCK_CONTR );
        Issued          = fininstr.Issued;
//      elif( FindACCTRRTISSUER(account, IssuerID ) and 
//            (IssuerID != -1) ) /* иначе, если задана хар-ка "эмитент" на л/с, то берем ее */
//        cIssuer = CDPParty(IssuerID, Data.EndDate );
//        IssuerName      = cIssuer.Name();
//        IssuerCode      = ПолучитьКодСубъекта( cIssuer.PARTYID(), PTCK_CONTR );
//        Issued          = fininstr.Issued;
      end;

      if( Data.EndDate == {curdate} )
        FaceValueMoney = money(GetFaceValue(fininstr.FIID, Data.EndDate));
        if( FaceValueMoney != $0 ) /* если номинал задан в анкете, то берем его */
          Nominal = FaceValueMoney;
          NominalFICode   = ПолучитьКодФинИн(fininstr.FaceValueFI);
          NominalFIIDName = FinInName(fininstr.FaceValueFI);
        end;
      else
        FaceValueMoney = money(cfin.FaceValue());
        if( FaceValueMoney != $0 ) /* если номинал задан в анкете, то берем его */
          Nominal = FaceValueMoney;
          NominalFICode   = cfin.FaceValueFICode();
          NominalFIIDName = FinInName(cfin.FaceValueFI());
        end;
      end;
   elif(DP_CheckAvoirKind(fininstr.AvoirKind, AVOIRISSKIND_INVESTMENT_SHARE))
      LSIN            = avoiriss.LSIN;
      cIssuer = CDPParty(fininstr.Issuer, Data.EndDate );
      IssuerName      = cIssuer.Name();
      IssuerCode      = ПолучитьКодСубъекта(fininstr.Issuer, PTCK_CONTR);
      Issued          = fininstr.Issued;
   else
      LSIN = cFin.LSIN();
      if( Data.EndDate == {curdate} )
        FaceValueMoney = money(GetFaceValue(fininstr.FIID, Data.EndDate));
      else
        FaceValueMoney = money(cfin.FaceValue());
      end;
      Nominal = FaceValueMoney;
      NominalFICode   = cfin.FaceValueFICode();
      NominalFIIDName = FinInName(cfin.FaceValueFI());

      cIssuer = CDPParty(FI_GetIssuerOnDate(fininstr, Data.EndDate ), Data.EndDate );
      IssuerName      = cIssuer.Name();
      IssuerCode      = ПолучитьКодСубъекта( cIssuer.PARTYID(), PTCK_CONTR );
      Issued          = fininstr.Issued;                                             

      if(   (DP_CheckAvoirKind(fininstr.AvoirKind, AVOIRISSKIND_ORDINARY_BOND))
         or (DP_CheckAvoirKind(fininstr.AvoirKind, AVOIRISSKIND_COUPON_BOND))
         or (DP_CheckAvoirKind(fininstr.AvoirKind, AVOIRISSKIND_CONV_BOND))
         or (DP_CheckAvoirKind(fininstr.AvoirKind, AVOIRISSKIND_COUPON_BOND_AD))
         or (DP_CheckAvoirKind(fininstr.AvoirKind, AVOIRISSKIND_ORDINARY_BOND_AD)) )
        avoir.FIID = fininstr.FIID;
        if(GetEQ(avoir))
          if( avoir.Tranche != "" )
            Tranche = " (транш: "+string(avoir.Tranche)+")"; 
          end;
        end; 
      end;
   end;


   if ( isPrint == false )
      isPrint = true;
   end;

   if( iCount > 1 )
   end;

   if( IsAllDAcc )
     DP_StdHeaderLO_Ex( DpRep, DpRepFontStyleTitel, "ОПЕРАЦИОННЫЙ ЖУРНАЛ ЛИЦЕВОГО СЧЁТА ДЕПО|Выписка", Data.AutoKey_logopr, iCount, true, Data.BeginDate, Data.EndDate, tablewidth );
   else
     DP_StdHeaderLO_Ex( DpRep, DpRepFontStyleTitel, "ОПЕРАЦИОННЫЙ ЖУРНАЛ ЛИЦЕВОГО СЧЁТА ДЕПО|Выписка", Data.AutoKey_logopr, 0, false, Data.BeginDate, Data.EndDate, tablewidth );
   end;

   DpRep.AddStr();
   DP_AddPrintCell(DpRep, "Лицевой счет депо", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     код", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  Data.DAccount, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     наименование", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  account.NameAccount, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     дата открытия", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  account.Open_Date, tablewidth-30, 0, "l:f" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "Раздел счета депо", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     код", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  Data.DepoInf.Code + " ( короткий " + Data.DepoInf.DepoAcount.rec.BriefCode + " )", tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     наименование", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  Data.DepoInf.Name, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     вид", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  Data.DepoInf.DepoKind, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     тип", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  Data.DepoInf.Type + " " + Data.DepoInf.DepoTypeName, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     код объемлющего раздела", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  Data.DepoInf.Superior(), tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     блокировка", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  string(Data.DepoInf.AvoirStatus)+" - "+Data.DepoInf.AvoirStatusName, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     балансовый счет", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  account.Balance, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "Cчёт депо", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     код", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  Data.RootAcc.Code + " ( короткий " + Data.RootAcc.DepoAcount.rec.BriefCode + " )", tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     наименование", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  Data.RootAcc.Name, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     владелец", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  Data.RootAcc.DeponentName, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "          ИНН", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  Data.RootAcc.DeponentINN, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "          код анкеты", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  Data.RootAcc.DeponentКод, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "Ценные бумаги", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     наименование", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  cfin.Name() + IIF(QualifiedSecur(cfin.avr, cfin.fins, Data.EndDate), "", " (НКЦБ)"), tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     № гос. регистрации", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  LSIN, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "     эмитент", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  IssuerName, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
   DP_AddPrintCell( DpRep,  "          код анкеты", 30, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DP_AddPrintCell( DpRep,  IssuerCode, tablewidth-30, 0, "l:l" + DpRepFontStyleTitel, true, REP_ELEM_STR );
   DpRep.AddStr();
  var str;
  if(DP_CheckAvoirKind(fininstr.AvoirKind, AVOIRISSKIND_INVESTMENT_SHARE))
    if( _is_app )
      str = string("     ", "вид ", avrkinds.Name, ", № выпуска: ",  avoiriss.Issue, Tranche, ", дата выпуска: ", Issued:f);
    else
      str = string("     ", "вид ", avrkinds.Name, ", № выпуска: ",  avoiriss.Issue, Tranche, ", дата выпуска: ", Issued:f);
    end;
  else
   if(Issued == "-")
    if( _is_app )
      str = string("     ", "вид ", avrkinds.Name, ", № выпуска: ",  avoiriss.Issue, Tranche, ", номинал: ", Nominal:a, ", валюта: ", NominalFICode, " (", NominalFIIDName, ")", ", дата выпуска: ", Issued);
    else
      str = string("     ", "вид ", avrkinds.Name, ", № выпуска: ",  avoiriss.Issue, Tranche, ", номинал: ", Nominal,   ", валюта: ", NominalFICode, " (", NominalFIIDName, ")", ", дата выпуска: ", Issued);
    end;
   else
    if( _is_app )
      str = string("     ", "вид ", avrkinds.Name, ", № выпуска: ",  avoiriss.Issue, Tranche, ", номинал: ", Nominal:a, ", валюта: ", NominalFICode, " (", NominalFIIDName, ")", ", дата выпуска: ", Issued:f);
    else
      str = string("     ", "вид ", avrkinds.Name, ", № выпуска: ",  avoiriss.Issue, Tranche, ", номинал: ", Nominal,   ", валюта: ", NominalFICode, " (", NominalFIIDName, ")", ", дата выпуска: ", Issued:f);
    end;
  end;
  end;
  DP_AddPrintCell( DpRep,  str, tablewidth-30, 0, "l:w:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
  DpRep.AddStr();
  DpRep.AddEmptyStr();

  return 0;
end;

macro Итого(data_FIID)
  file fininstr(fininstr);

if( Дебет_лицевой == 0 )
  Дебет_лицевой = "-";
end;
if( Кредит_лицевой == 0 )
  Кредит_лицевой = "-";
end;
  var Дебет_лицевой_str="-", Кредит_лицевой_str="-";
  var format_str, t_point = AVOIRISS_SUM_PRECISION;

  if(_is_app)
    format_str = "a";
  else
    format_str = "";
  end;
  
  fininstr.FIID = data_FIID;
  if( GetEQ(fininstr))
      t_point =  Fininstr.SumPrecision;
  end;
/*  
  if((Дебет_лицевой != "-") and (abs(Дебет_лицевой) >= 1))
    Дебет_лицевой_str  = ЧислоCЗаданнойТочностью(double(Дебет_лицевой),  t_point, format_str);
  end;
  if((Кредит_лицевой != "-") and (abs(Кредит_лицевой) >= 1))
    Кредит_лицевой_str = ЧислоCЗаданнойТочностью(double(Кредит_лицевой), t_point, format_str);
  end;
*/

  DP_AddPrintCell( DpRep,  "ИТОГО:", DpRep.GetColWidthTable(0)+DpRep.GetColWidthTable(1)+DpRep.GetColWidthTable(2)+DpRep.GetColWidthTable(3)+DpRep.GetColWidthTable(4)+DpRep.GetColWidthTable(5)+5, 0, "r:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  "", DpRep.GetColWidthTable(6), 0, "r:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  Дебет_лицевой, DpRep.GetColWidthTable(7), DP_GetPrecision(Дебет_лицевой, t_point), "r:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  Кредит_лицевой, DpRep.GetColWidthTable(8), DP_GetPrecision(Кредит_лицевой, t_point), "r:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  "", DpRep.GetColWidthTable(9), 0, "r:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  "", DpRep.GetColWidthTable(10), 0, "r:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  "", DpRep.GetColWidthTable(11), 0, "r:" + DpRepFontStyleTitel );
  DpRep.AddStr();
end;


macro ОстатокДоОперации( Data, Doc )
  var Остаток = 0;
  var DocDate, DocAutoKey, OldKey, continue_cicle; 
  var DataSet, query;
  var savepos; 

  DocDate    = Doc.Date_Carry;
  DocAutoKey = Doc.AccTrnID;
  savepos = GetPos( TmpFileDoc );
  query = RSDCommand(" SELECT count(a.t_Account) as count, sum( rsb_account.restac( a.t_Account, a.t_Code_Currency, ?, a.t_Chapter, null )  ) as Rest"
             + " FROM  daccount_dbt a"
             + " WHERE a.t_Code_Currency = ? "
             + " and a.t_Chapter = ? "
             + " and a.t_Account = ? ");
 
  query.addParam( "", RSDBP_IN, DocDate-1 );
  query.addParam( "", RSDBP_IN, Data.FIID );
  query.addParam( "", RSDBP_IN, synt_chapter );
  query.addParam( "", RSDBP_IN, Data.DAccount );
  query.execute();

  DataSet = TRsbDataSet( query );
  if( (DataSet.MoveNext())  and (DataSet.count > 0) )
    Остаток  = DataSet.Rest;
  end;
                                         
//  Остаток = RestAC( Data.DAccount, Data.FIID, DocDate-1, NULL, synt_chapter );

  /* все проводки по счету за дату Doc.Date_Carry до текущей проводки (Doc) */
  OldKey = KeyNum( TmpFileDoc, 1 ); /* Chapter + Date_Carry +State */ 
  ClearRecord(TmpFileDoc);
  TmpFileDoc.Chapter    = synt_chapter;
  TmpFileDoc.Date_Carry = DocDate;
  TmpFileDoc.State      = 1;
  continue_cicle = GetGE( TmpFileDoc );
  while( continue_cicle and (TmpFileDoc.Date_Carry == DocDate) ) 
    if( TmpFileDoc.AccTrnID < DocAutoKey ) /* Проводка была раньше чем Doc */
      if( Doc.Account_Payer == Data.DAccount )  /* Дебет */  
        Остаток = Остаток - TmpFileDoc.Sum_Payer;
      else  /* Кредит */
        Остаток = Остаток + TmpFileDoc.Sum_Receiver;
      end;
    end;
    continue_cicle = next(TmpFileDoc);
  end;

  KeyNum( TmpFileDoc, OldKey ); 
  GetDirect( TmpFileDoc, savepos );
  return Остаток;
end;

macro НапечататьПроводку( Data, Doc )
  var Credit;
  var DataSet;
  var query;

  if( Doc.Account_Receiver == Data.DAccount ) Credit = 1; /*Кредит счета*/
  elif( Doc.Account_Payer  == Data.DAccount ) Credit = 0; /*Дебет счета*/
  else return;
  end;

  file OprDoc ( oprdocs  ) key 6;
  file OprOper( oproper  ) key 0;
  file OprStep( oprstep )  key 0;
  file Draft  ( spdraft  ) key 0;
  file Globop  ( dpcorpop  ) key 0;
  file Drmove   ( spdrmove ) key 0;
  file Ground ( spground ) key 0;
  file Logopr   ( splogopr ) key 0;
  file Typeac   ( typeac   ) key 0;
  file Pmpaym   ( pmpaym   ) key 0;
  file invcard  ( "invcard.dbt"   );
  file Fininstr ( fininstr ) key 0;
  file dpgrrep  (dpgrrep)    key 1;
  file spinfopr (spinfopr)   key 0;
  record fininstr_("fininstr");
  var invlist, cmd;
  var vficert, cmd1;
                                              
  var stat;
  var КОП,Транзакция, Исполнена, Поручение, НачОстаток, Дебет, Кредит, Остаток, Корреспонденция;
  var Xld_Отчета, Data_Отчета, Party_Отчета, Delivery_Отчет = "";
  var ОтчетОбИсполнении = "-";
  var Issuer, FaceValue = 0, FaceValueFI, Issued, NominalFICode, NominalFIIDName;
  var j = 0;
  var SPGroundID = 0, SourceDocKind = 0, SourceDocID = 0, OprXid = 0, Product = 0;
  var cIssuer;

/*  
  OprDoc.DocKind    = DLDOC_CARRY;
  OprDoc.DocumentID = MakeDocumentID( Doc );
  OprDoc.Origin     = 0;
*/
  OprDoc.AccTrnID    = Doc.AccTrnID;

  if( not GetEQ(OprDoc) )
    /*msgbox( "Не найден документ операции." );*/
    return; 
  end;

  OprOper.ID_Operation = OprDoc.ID_Operation;
  if( not GetEQ(OprOper) ) 
    /*msgbox( "Не найдена операция" );  */
    return;
  end;

  if( IsDepoDraft( OprOper.DocKind ) )
    /* поручение депо (первичный документ операции)*/
    Draft.AutoKey = int( OprOper.DocumentID );
    if( not GetEQ(Draft) or (Draft.Kind != OprOper.DocKind) )  
      return; /*первичный документ - не поручение депо*/
    end;

    SPGroundID    = Draft.SPgroundID;
    SourceDocKind = Draft.Kind;
    SourceDocID   = Draft.AutoKey;
    OprXid        = Draft.OprXid;
    Product       = Draft.Product;
  elif( IsDepoGlobop( OprOper.DocKind ) )
    /* Глобальная операция (первичный документ операции)*/
    Globop.DocumentID = int( OprOper.DocumentID );
    if( not GetEQ(Globop) or (Globop.DocKind != OprOper.DocKind) )  
      return; /*первичный документ - не Глобальная операция*/
    end;

    SourceDocKind = Globop.DocKind;
    SourceDocID   = Globop.DocumentID;
    OprXid        = Globop.OprXid;
    Product       = Globop.Product;
  end;

  CInvCard_Arr.Size = 0;
  stat = 1;
  var ОписьСертификатов = "";
  var flag = 0;
  if( IsDepoDraft( OprOper.DocKind ) )
    Drmove.DraftID = Draft.AutoKey;
    if( GetEQ(Drmove) AND Drmove.DraftID==Draft.AutoKey )  
       cmd = DL_RSDCommand("select t_InvCardID from dinvlist_dbt where t_PaymentID = ?");
       cmd.AddParam(Drmove.PaymentID);

       invlist = cmd.Execute();
       Pmpaym.PaymentID = Drmove.PaymentID;
       if( (invlist.moveNext()) AND ((GetEQ(Pmpaym)) AND (Pmpaym.IndoorStorage!=0)) )
          while( stat )  
            invcard.InvCardID = invlist.InvCardID;
            if( GetEQ(invcard) )
              cmd1 = DL_RSDCommand("select * from dv_ficert_dbt where t_FiCertID = ?");
              cmd1.AddParam(invcard.FiCertID);

              vficert = cmd1.Execute();

              if(vficert.moveNext())
                Fininstr.FIID = vficert.FIID;
                if(not GetEQ(Fininstr))
                  /*MsgBox("Не найден финансовый инструмент");*/
                  return 1;
                else
                  Issuer = vficert.Issuer;
    
                  FaceValue   = vficert.FaceValue;
                  FaceValueFI = vficert.FaceValueFI;
                  if( FaceValueFI!=-1 )
                    if (ПолучитьФинИн (FaceValueFI, fininstr_, NULL) != 0)
                      NominalFICode   = "-";
                      NominalFIIDName = "-";
                      /*MsgBox("Не найдена валюта номинала ценной бумаги");*/
                    else
                      NominalFICode   = ПолучитьКодФинИн(fininstr_.FIID); /*всегда в рублях*/
                      NominalFIIDName = FinInName(fininstr_.FIID);        /*всегда в рублях*/
                    end;
                  end;
                  
    
                  Issued = date(vficert.IssueDate);
                end;

                if(flag == 0)
                  ОписьСертификатов = "сертификаты: ";
                  flag = flag + 1;
                end;
                ОписьСертификатов = ОписьСертификатов + vficert.Series + " " + int(vficert.Number);
      
                if(ИндивидуальныйФинИн(vficert.FIID))
                  Fininstr.FIID = Data.FIID;
                  if(not GetEQ(Fininstr))
                    /*MsgBox("Не найден финансовый инструмент");*/
                    return 1;
                  else
                    CInvCard_Arr( CInvCard_Arr.Size ) = CInvCard(Issuer, FaceValue, Issued, ОписьСертификатов, NominalFICode, NominalFIIDName); 
                  end;
                end;
              end;
            end;
            stat = invlist.moveNext();
            if( stat ) ОписьСертификатов = ОписьСертификатов + ", ";
            end;
          end;
       end;
    end;
  end;

  j = 0;
  if(CInvCard_Arr.Size > 0)
    TArray_QSort(CInvCard_Arr, "Issuer", 1, "FaceValue", 1, "Issued", 1);
    ОписьСертификатов = "\nОпись сертификатов:         ";
    while( j < CInvCard_Arr.Size )
      if( CInvCard_Arr(j).Issuer > UNDEF_ID)
         ОписьСертификатов = ОписьСертификатов + "\nэмитент код анкеты - " + ПолучитьКодСубъекта(CInvCard_Arr(j).Issuer, PTCK_CONTR);
  
         cIssuer = CDPParty(CInvCard_Arr(j).Issuer, Doc.Date_Carry );
         ОписьСертификатов = ОписьСертификатов + " " + cIssuer.Name();
      end;                                                    
      if( CInvCard_Arr(j).FaceValue > 0)
         ОписьСертификатов = ОписьСертификатов + "\n  номинал " + setApp(CInvCard_Arr(j).FaceValue,_is_app);
         ОписьСертификатов = ОписьСертификатов + " " + CInvCard_Arr(j).NominalFICode + " (" + CInvCard_Arr(j).NominalFIIDName + ")";
      end;                                                        
      if( CInvCard_Arr(j).Issued > date(0,0,0))
         ОписьСертификатов = ОписьСертификатов + "\n    дата выпуска " + CInvCard_Arr(j).Issued;
      end;                                                        
      ОписьСертификатов = ОписьСертификатов + "\n      " + CInvCard_Arr(j).Certif;
      j = j + 1;                                                
    end;
  else
    if( ОписьСертификатов != "")
      ОписьСертификатов = "\nОпись сертификатов:         " + ОписьСертификатов; 
    end;
  end;
  

  /* Пока в documnt$.dbt не добавят время, бeрем его из шага операции, 
     на котором проводился документ */
  OprStep.ID_Operation = OprOper.ID_Operation;
  OprStep.ID_step      = OprDoc.ID_step;
  if( not GetEQ(OprStep) )
    /*msgbox( "Не найден шаг операции." );   */
    return;
  end;

  /* Документ основание */
  if( SPGroundID )
    KeyNum(Ground, 0);
    Ground.SPGroundID = SPGroundID;
  else
    KeyNum(Ground, 6);
    Ground.SourceDocKind = SourceDocKind;
    Ground.SourceDocID   = SourceDocID;
    Ground.Direction     = ENTERING;
    Ground.Department    = _NumDprt;
    Ground.Division      = SelfDivision;
  end;
  if( not GetEQ(Ground) )              
    /*msgbox( "Не найден входящий документ - основание" );   */
    return;
  end;
  KeyNum(Ground, 0);

  Data.НомерПроводки = Data.НомерПроводки + 1;
  КОП = OprOper.Kind_Operation;
  query = RSDCommand("  select lv.t_name"
          + "    from dllclsval_dbt lc, dllvalues_dbt lv "
          + "   where lc.t_Classificator = " + LLCLASS_TRNCONTENT_INVOPER
          + "     and lv.t_List = lc.t_List "
          + "     and lv.t_Element = ? " );
  query.addParam( "", RSDBP_IN, Product );
  query.execute();
  DataSet = TRsbDataSet(query);
  if(DataSet.MoveNext())
    Транзакция = DataSet.Name;
  else
    Транзакция = "-";
  end;

  Исполнена = string(OprStep.Plan_date:f) + "  " + substr( string(OprStep.Syst_time),1,5);

  Поручение = DateToStr(Ground.RegistrDate, true) + "   " + Ground.Xld;

  НачОстаток = ОстатокДоОперации( Data, Doc );
  
  query = RSDCommand(" select rep.t_RecipientName as PartyName, spgr.t_DeliveryKind, spgr.t_Xld, spgr.t_SignedDate, spgr.t_RegistrDate "
          + "   from ddpgrrep_dbt rep, ddprepdoc_dbt repdoc, dspground_dbt spgr"
          + "  where rep.t_SourceDocKind = ? "
          + "    and rep.t_SourceDocID = ? "
          + "    and repdoc.t_GrRepID = rep.t_ID "
          + "    and spgr.t_SPgroundID = repdoc.t_RepSPgroundID " );
              
  query.addParam( "", RSDBP_IN, SourceDocKind );
  query.addParam( "", RSDBP_IN, SourceDocID );

  query.execute();
  DataSet = TRsbDataSet(query);
  /* перебираем все отчеты об исполении */
  ОтчетОбИсполнении = "";
  while((stat) and (DataSet.moveNext()))

    Typeac.iNumType = TA_STAT_DEL;
    Typeac.Type_Account = StrFor(DataSet.DeliveryKind);
    if( GetEQ(Typeac) )
      Delivery_Отчет = Typeac.Name_Type;
    end;
    ОтчетОбИсполнении = ОтчетОбИсполнении +
                        "№ " + DataSet.Xld + ", составлен " + DateToStr(date(DataSet.SignedDate) ,true) + 
                        ", отослан " + DataSet.PartyName + ",\n" + DateToStr(date(DataSet.RegistrDate), true) + ", " + Delivery_Отчет + "\n";
  end;

  KeyNum(Ground, 0);
  if( ОтчетОбИсполнении == "" )
    ОтчетОбИсполнении = "-";
  end;

  if( Doc.Account_Payer == Data.DAccount )  /* Дебет */  
    Остаток = НачОстаток - Doc.Sum_Payer;
    Дебет_лицевой  = Дебет_лицевой + Doc.Sum_Payer;
    Дебет   = Doc.Sum_Payer;
    Кредит  = "-";
    Корреспонденция = Doc.Account_Receiver;
  else  /* Кредит */
    Остаток = НачОстаток + Doc.Sum_Receiver;
    Кредит_лицевой = Кредит_лицевой + Doc.Sum_Receiver;
    Дебет   = "-";
    Кредит  = Doc.Sum_Receiver;
    Корреспонденция = Doc.Account_Payer;
  end;

  var НачОстаток_str="-", Дебет_str="-",  Кредит_str="-",  Остаток_str="-";
  var format_str, t_point = AVOIRISS_SUM_PRECISION;

  if(_is_app)
    format_str = "a";
  else
    format_str = "";
  end;
  
  Fininstr.FIID = Data.FIID;
  if(GetEQ(Fininstr))
      t_point =  Fininstr.SumPrecision;
  end;

/*
  if((НачОстаток != "-") and (abs(НачОстаток) >= 1) )
    НачОстаток_str = ЧислоCЗаданнойТочностью(double(НачОстаток), t_point, format_str);
  end;
  if((Дебет != "-") and (abs(Дебет) >= 1) )
    Дебет_str      = ЧислоCЗаданнойТочностью(double(Дебет),      t_point, format_str);
  end;
  if((Кредит != "-") and (abs(Кредит) >= 1) )
    Кредит_str     = ЧислоCЗаданнойТочностью(double(Кредит),     t_point, format_str);
  end;
  if((Остаток != "-") and (abs(Остаток) >= 1) )
    Остаток_str    = ЧислоCЗаданнойТочностью(double(Остаток),    t_point, format_str);
  end;
*/

  DP_AddPrintCell( DpRep,  Data.НомерПроводки, 0, 0, "c:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  OprXid, 0, 0, "c:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  КОП, 0, 0, "c:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  Транзакция, 0, 0, "l:c" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  Поручение, 0, 0, "l:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  Исполнена, 0, 0, "l:c" + DpRepFontStyleTitel );

  DP_AddPrintCell( DpRep,  НачОстаток, 0, DP_GetPrecision(НачОстаток, t_point), "r:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  Дебет, 0, DP_GetPrecision(Дебет, t_point), "r:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  Кредит, 0, DP_GetPrecision(Кредит, t_point), "r:" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  Остаток, 0, DP_GetPrecision(Остаток, t_point), "r:" + DpRepFontStyleTitel );

  DP_AddPrintCell( DpRep,  Корреспонденция, 0, 0, "l:c" + DpRepFontStyleTitel );
  DP_AddPrintCell( DpRep,  (ОтчетОбИсполнении + ОписьСертификатов), 0, 0, "l:w" + DpRepFontStyleTitel );
  DpRep.AddStr();
end;

macro БухгалтерскиеОпрации( Data )
  var i = 0;
  var j = 0;

  Дебет_лицевой  = 0;
  Кредит_лицевой = 0;

  ReWind( TmpFileDoc );

  TArray_QSort(AccTrnSort_Arr, "AccTrnDate", 1, "AccTrnTime", 1); 

  while( j < AccTrnSort_Arr.size ) 
    TmpFileDoc.AccTrnID = AccTrnSort_Arr(j).AccTrnAutokey;
    if( GetEQ(TmpFileDoc) )
      НапечататьПроводку( Data, TmpFileDoc );
    end;
    j = j + 1;
  end;
  if( (Дебет_лицевой != 0) OR (Кредит_лицевой != 0) )
    Итого(Data.FIID);
  end;

  DP_StdFooter_Ex(DpRep, DpRepFontStyleTitel, NULL, NULL, tablewidth);
  return 0;
end;


macro CreateReport(InputData, iCount, IsAllDAcc)
   var Error;
   Error = 0;
   if( not CreateTmpFileDoc( InputData ) )
     Error = 1;//RunError( "Ошибка при соэдании временного файла для отчета." );
   end;
   if( (not Error) AND (InputData.ЧислоПроводок) ) 
     Error = ЗаголовокЖурнала( InputData, iCount, IsAllDAcc );
     if( Error )  return Error;  end;
     Error = БухгалтерскиеОпрации( InputData );
     
   end;
   return Error;
end;

private macro CreateEmptyRep()

  DP_StdHeaderLO_Ex( DpRep, DpRepFontStyleTitel, "ОПЕРАЦИОННЫЙ ЖУРНАЛ ЛИЦЕВОГО СЧЁТА ДЕПО|Выписка", __LogoprID, 0, false, _BeginDate, _EndDate, tablewidth );
  DpRep.AddEmptyStr();
  DP_AddPrintCell( DpRep,  "Нет данных, удовлетворяющих параметрам отчета.", tablewidth, 0, "l:" + DpRepFontStyleTitel, true, REP_ELEM_STR );
  DpRep.AddEmptyStr();
  DP_StdFooter_Ex(DpRep, DpRepFontStyleTitel, NULL, NULL, tablewidth);
  return 1;
end;

macro AccDepoJournal( NumDprt,
                      DAccount, 
                      FIID, 
                      iDepoAcc,
                      iDepoPart,
                      BeginDate, 
                      EndDate, 
                      RepDate, 
                      iDeponent,
                      AutoKey_logopr,
                      ToExcel,
                      is_app
                      )
 file Splogopr(splogopr);
 file Spground(spground) key 6;
 var DataSet;

 var InputData = CData( DAccount, FIID, BeginDate, EndDate, RepDate, iDeponent, AutoKey_logopr );
 var Error = 0, iCount = 1;
 var stat, НомерВыписки;
 var Progress = CProgressBar;

 if( InputData == NULL) return 1;  end;
 Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
 DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

 _NumDprt = NumDprt;
 _is_app = is_app;
 __LogoprID = AutoKey_logopr;
 _BeginDate = BeginDate;
 _EndDate   = EndDate;

 var sQuery = "";
 
 if( iDeponent != UNDEF_ID )
   sQuery = " select acc.t_Account, acc.t_Code_Currency "
          + " from daccount_dbt acc "
          + " where acc.t_Chapter = " + synt_chapter
          + "   and acc.t_Department = " + NumDprt;

 else
   sQuery = " select acc.t_Account, acc.t_Code_Currency, party.t_Name "
          + " from daccount_dbt acc, dparty_dbt party "
          + " where acc.t_Chapter = " + synt_chapter
          + "   and acc.t_Department = " + NumDprt
          + "   and party.t_PartyID = acc.t_Client";
 end;

 if( InputData.FIID > 0 )
   sQuery = sQuery + " and acc.t_Code_Currency = " + InputData.FIID;
 end;

 if( iDepoAcc > 0 )
   sQuery = sQuery + " and acc.t_DepoRoot = " + iDepoAcc;
 end;

 if( iDepoPart > 0 )
   sQuery = sQuery + " and acc.t_DepoAcc = " + iDepoPart;
 end;

 if( DAccount != "" )
   sQuery = sQuery + " acc.t_Account = '"+DAccount+"' ";
 end;

 if( iDeponent != UNDEF_ID )
   sQuery = sQuery + " and acc.t_Client = " + iDeponent;
   sQuery = sQuery + " order by t_Code_Currency";
 else 
   sQuery = sQuery + " order by party.t_Name, t_Code_Currency ";
 end;

 GetRegistryValue( PathWrkDir, V_STRING, InputData.NameFile, Error );
 if( Error  ==  7 )
   msgbox( "Не определен ключ " + PathWrkDir );
   return FALSE;
 end;

  InputData.NameFile = InputData.NameFile + "\\document." + string( UserNumber() );


 if( DAccount == "")
   DataSet = TRsbDataSet(sQuery);
   Progress.Init( -1, NameJrn+" Выполнение отчета ...", "Сканирование проводок" );
   while( DataSet.MoveNext() )
     InputData.DAccount = DataSet.Account;
     InputData.FIID = DataSet.Code_Currency;
     Error = CreateReport(InputData, iCount, true);
     if( not Error ) Glob_Error = 0; end;
     iCount = Progress.Use();
   end;
   Progress.Remove();
 else
   DpRep.SetFlagShowZeroValue(true);
   Error = CreateReport(InputData, 0, false);
 end;

 if( not Error ) Glob_Error = 0; end;
 if( ( Glob_Error ) or ( isPrint == false ) )
    Error = 1;
 end;

 Close( TmpFileDoc );
 DelFile( InputData.NameFile );

 DP_AddProtocol(DpRep, "protocol");  //добавим протокол к уже сформированному отчету
 DP_EndProtocol();                 //закончить протоколирование


 return DP_StdReportControl( __LogOprID, ( not Error), @CreateEmptyRep, 0, ToExcel, DpRep, ZOOM_TYPE_A4L );

end;
