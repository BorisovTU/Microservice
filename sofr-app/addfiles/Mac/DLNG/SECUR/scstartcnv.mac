/*                           
$Name:             scstartcnv.mac
$Module:           Ценные бумаги
$Description:      запуск, выполнение конвейера ВУ
*/

//запуск на выполнение сервисной операции внутреннего учета
//формирование протокола сервисной операции внутреннего учета
import BankInter, XmlRpcInter, "scservop.mac", "ws_file.mac";
import sc_closeacc;

//класс с данными для строки протокола
class CSC_InAccRepParm(_DealKindName:string,   
                       _DealCode:string,
                       _InAccKindName:string,      
                       _PayerAccount:string,   
                       _ReceiverAccount:string,
                       _Amount:money,         
                       _FICode:string)

  var Kind            = 0;
  var DealKindName    = _DealKindName;    //Вид операции
  var DealCode        = _DealCode;        //Код операции
  var InAccKindName   = _InAccKindName;   //Вид расчетной операции
  var PayerAccount    = _PayerAccount;    //Счет Дт
  var ReceiverAccount = _ReceiverAccount; //Счет Кт
  var Amount          = _Amount;          //Сумма
  var FICode          = _FICode;          //Валюта
end;

//класс с данными для списка ошибок в протоколе
class CSC_InAccRepErr(_RepNumber:integer, _DealCode:string, _ErrCode:integer, _ErrStr:string)
 var RepNumber = _RepNumber;
 var DealCode  = _DealCode;
 var ErrCode   = _ErrCode;
 var ErrStr    = _ErrStr;
end;
private var RepErrArr = TArray(); //массив ошибок


CLASS (DL_LOG_FROM_XML) INACC_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()

     var CarryData:CSC_InAccRepParm;

     if( m_ReportVersion == 1 )
     
        if(DL_ReadTagAttribut(m_child_ReportNumber, "DEALKINDNAME", V_STRING, CarryData.DealKindName) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "DEALCODE", V_STRING, CarryData.DealCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "INACCKINDNAME", V_STRING, CarryData.InAccKindName) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "PAYERACCOUNT", V_STRING, CarryData.PayerAccount) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "RECEIVERACCOUNT", V_STRING, CarryData.ReceiverAccount) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "AMOUNT", V_MONEY, CarryData.Amount) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "FICODE", V_STRING, CarryData.FICode) == false)
        end;
       
        return CarryData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

/*лог ошибок*/
PRIVATE CLASS (DL_LOG_FROM_XML) SC_ERROR_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()
     var ErrorData:CSC_InAccRepErr;

     if( m_ReportVersion == 1 )

        if(DL_ReadTagAttribut(m_child_ReportNumber, "DEALCODE", V_STRING, ErrorData.DealCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRCODE", V_STRING, ErrorData.ErrCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRSTR", V_STRING, ErrorData.ErrStr) == false)
        end;
        
        return ErrorData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

/*лог ошибок СО*/
CLASS (DL_LOG_DATA_XML) SC_INACC_ERROR_LOG_DATA_XML(pDocKind:integer, pDocumentID:integer, pDataParms:TArray, pType:integer, pVersion:integer)

  PRIVATE MACRO СоздатьУзелОшибок(i)
     var УзелОшибок = null;

     if( m_Version == 1 )
        УзелОшибок = CreateElement("Property",
                                   "DealCode",  m_DataParms[i].DealCode, 
                                   "ErrCode",   m_DataParms[i].ErrCode, 
                                   "ErrStr",    m_DataParms[i].ErrStr
                                  );
     else
        УзелОшибок = CreateElement("Property");
     end;
     
     return УзелОшибок;
  END;


  initDL_LOG_DATA_XML(pDocKind, pDocumentID, pDataParms, pType, pVersion);

END;


PRIVATE MACRO GetLogDataXML( DocKind:integer, DocumentID:integer, Type:integer, OnlyLastReport:bool )
   var xml_obj = null;

   if( Type == LOG_TYPE_DATA )
      xml_obj = INACC_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   elif(Type == LOG_TYPE_ERROR )
      xml_obj = SC_ERROR_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   end;

   return xml_obj;
END;

private macro SaveInAccErrForReport_XML( RepErrArr, DocumentID:integer, DocKind:integer )

  var RepXML = "";

  if( RepErrArr.Size > 0 )
     RepXML = SC_INACC_ERROR_LOG_DATA_XML(DocKind,DocumentID,RepErrArr,LOG_TYPE_ERROR).GetDataXML();
  end;

  if( RepXML != "" )
     var Header = DL_LOG_HEADER_XML(DocKind).GetHeaderXML();

     DL_InsertHeaderLog(DocumentID, DocKind, {Oper}, Header);
     DL_InsertLogXMLData( DocumentID, DocKind, LOG_TYPE_ERROR, {Oper}, RepXML);
  end;

end;



private var N1RepHeaderStr = "                                                          ###########################################################";

private var N1TableHeader = "┌─────────────────────────────┬──────────┬────────────────────────────┬─────────────────────────┬─────────────────────────┬────────────────┬───────────────┐\n"+
                            "│Вид операции                 │№         │Вид расчетной операции      │Счет Дт                  │Счет Кт                  │      Сумма     │      Вал      │\n"+
                            "├─────────────────────────────┼──────────┼────────────────────────────┼─────────────────────────┼─────────────────────────┼────────────────┼───────────────┤";


private var N2RepHeaderStr = "                     #################################################";

private var N2TableHeader = "┌─────────────────────────────┬────────────────────┬────────────────────┬────────────────┬────┐\n"+
                            "│Наименование                 │Счет Дт             │ Счет Кт            │      Сумма     │Вал │\n"+
                            "├─────────────────────────────┼────────────────────┼────────────────────┼────────────────┼────┤";


private var NFooter = "###############################################################\n"+
                      "######################################################################                 ################\n"+
                      "────────────────────────────────────────────────────────────────────── ───────────────   дата\n"+
                      "                                 ФИО                                      подпись";




private var N3OprStateMsg = "########################################################";
private var N3TableHeader1 = "┌──────────┬──────────┬──────────────────────────────────────────────────────────────┐\n"+
                             "│№ операции│№ ошибки  │Сообщение                                                     │\n"+
                             "├──────────┼──────────┼──────────────────────────────────────────────────────────────┤";


private var N3TableHeader2 = "┌──────────┬──────────┬──────────────────────────────────────────────────────────────┐\n"+
                             "│№ договора│№ ошибки  │Сообщение                                                     │\n"+
                             "├──────────┼──────────┼──────────────────────────────────────────────────────────────┤";


private var N5BlockHeader = "########################################################";
private var N5StateMsg = "########################################################";


PRIVATE CLASS (DL_CReportTemplate) SC_InAcc_Report(dl_comm, Mode, IsWebService:BOOL)
  PRIVATE VAR m_PrintMode;
  PRIVATE VAR m_comm = TRecHandler("dl_comm");
  PRIVATE VAR m_PrintedStdHead = false;
  PRIVATE VAR m_IsExistsCarry = false, m_IsExistsCarryComis = false;
  PRIVATE var m_Executer = DepoExecuter( {oper} );

  PRIVATE MACRO PrintMode():INTEGER
     return m_PrintMode;
  END;
  
  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    CreateTotalBook();
  END;

  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;

  PRIVATE MACRO Проводки()
    var ReportLineData_XML:variant;
    var CarryData:CSC_InAccRepParm;
    var SheetName = "Проводки";    
    var HeadPrinted = false;
    
    ////////////////////////////////////////////////////////////////////////////////////////////////////////
    //таблица проводок
    ReportLineData_XML = GetLogDataXML(m_comm.rec.DocKind, m_comm.rec.DocumentID, LOG_TYPE_DATA, false);
    ReportLineData_XML.SetReportNumber(0);
   
    while( ReportLineData_XML.Next )
       m_IsExistsCarry = true;

       if(not HeadPrinted)
          SetActiveSheet( SheetName );

          UseNewSheetInTotalBook();

          PrintFormatString(N1RepHeaderStr,
                           "N1_RepHead", "ВНУТРЕННИЙ УЧЕТ"
                           );
          CopyAllSheetInTotalBook( SheetName, false, "N1_RepHeader", 0, SheetName );

          CopyAllSheetInTotalBook( SheetName, false, "N1_TableHeader", 0, SheetName );
          RegisterTable( "N1_MainTable",   N1TableHeader,
                         "N1_DealType",   
                         "N1_DealCode",  
                         "N1_InAccType", 
                         "N1_DtAccount",
                         "N1_CtAccount",
                         "N1_Sum",       
                         "N1_SumFI"     
                       );

          HeadPrinted = true;
       end;

       CarryData = ReportLineData_XML.GetReportLineData();

       PrintTableLine( "N1_DealType:l",    CarryData.DealKindName,    null,
                       "N1_DealCode:c",    CarryData.DealCode,        null,
                       "N1_InAccType:l:w", CarryData.InAccKindName,   null,
                       "N1_DtAccount",     CarryData.PayerAccount,    null,
                       "N1_CtAccount",     CarryData.ReceiverAccount, null,
                       "N1_Sum:r",         CarryData.Amount,          null,
                       "N1_SumFI:c",       CarryData.FICode,          null
                     );
    end;
    
    if(HeadPrinted)
      EndTable();
      CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );

      PrintFormatString(NFooter,
                       "N1_ExecutorPost", "Составил ( " + m_Executer.Post + " ):",
                       "N1_ExecutorName", string( m_Executer.Name ),
                       "N1_ExecuteDate:l",  m_comm.rec.CommDate:f
                       );
      CopyAllSheetInTotalBook( SheetName, false, "N1_Footer", 1, SheetName );
    end;

  END;

  PRIVATE MACRO ПериодическиеКомиссии()
    var cmd, query, DataSet;
    var prevContrID = 0;
    var SheetName = "ПериодКомиссии";    
    var HeadPrinted = false;
    var stat = 0;

    query =   " select inacc.t_PartyID, inacc.t_PartyContrID, "
            + "        pt.t_ShortName as PartyShortName, contr.t_Number as ContrNumber, "
            + "        comiss.t_Code as ComissCode, fin.t_ISO_Number, "
            + "        acctrn.t_Account_Payer, acctrn.t_Account_Receiver, acctrn.t_Sum_Payer "
            + "   from ddlinacc_dbt inacc, dparty_dbt pt, dsfcontr_dbt contr, dacctrn_dbt acctrn, dsfdefcom_dbt defcom, dsfcomiss_dbt comiss, dfininstr_dbt fin "
            + "  where inacc.t_ServDocKind = ? "
            + "    and inacc.t_ServDocID = ? "
            + "    and inacc.t_DocumentKind = " + DLDOC_SFCONCOM
            + "    and pt.t_PartyID = inacc.t_PartyID "
            + "    and contr.t_ID = inacc.t_PartyContrID "
            + "    and acctrn.t_AccTrnID = inacc.t_AccTrnID "
            + "    and defcom.t_ID = inacc.t_DocumentID "
            + "    and comiss.t_FeeType = defcom.t_FeeType "
            + "    and comiss.t_Number = defcom.t_CommNumber "
            + "    and fin.t_FIID = acctrn.t_FIID_Payer "
            + "  order by inacc.t_PartyID, inacc.t_PartyContrID ";

    cmd = DL_RSDCommand(query);
    cmd.AddParam(m_comm.rec.DocKind);
    cmd.AddParam(m_comm.rec.DocumentID);

    if(cmd.GetCount() > 0)
      SetActiveSheet( SheetName );

      UseNewSheetInTotalBook();

      PrintFormatString(N2RepHeaderStr,
                       "N2_RepHead", "ПРОТОКОЛ ОПЛАТЫ ПЕРИОДИЧЕСКИХ КОМИССИЙ"
                       );
      CopyAllSheetInTotalBook( SheetName, false, "N2_RepHeader", 0, SheetName );

      CopyAllSheetInTotalBook( SheetName, false, "N2_TableHeader", 0, SheetName );
      RegisterTable( "N2_MainTable",   N2TableHeader,
                     "N2_ComisName",   
                     "N2_DtAccount",
                     "N2_CtAccount",
                     "N2_Sum",       
                     "N2_SumFI"     
                   );

      DataSet = cmd.execute();
      stat = DataSet.moveNext();
      while(stat)                   
        m_IsExistsCarryComis = true;

        if(prevContrID != DataSet.PartyContrID)
          if(m_PrintMode == DL_OUTREPORT_EXCEL)
            Merge( "N2_ComisName", "N2_SumFI" );
          end;
          
          PrintTableLine( "N2_ComisName:w", "Клиент: "+DataSet.PartyShortName+"  № договора обслуживания: "+DataSet.ContrNumber,  null);
        end;

        PrintTableLine( "N2_ComisName:c", DataSet.ComissCode,       null,
                        "N2_DtAccount:c", DataSet.Account_Payer,    null,
                        "N2_CtAccount:c", DataSet.Account_Receiver, null,
                        "N2_Sum:r",       DataSet.Sum_Payer,        null,
                        "N2_SumFI:c",     DataSet.ISO_Number,       null
                      );

        prevContrID = DataSet.PartyContrID;

        stat = DataSet.moveNext();
      end;

      EndTable();
      CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );

      PrintFormatString(NFooter,
                       "N2_ExecutorPost", "Составил ( " + m_Executer.Post + " ):",
                       "N2_ExecutorName", string( m_Executer.Name ),
                       "N2_ExecuteDate:l",  m_comm.rec.CommDate:f
                       );
      CopyAllSheetInTotalBook( SheetName, false, "N2_Footer", 0, SheetName );

    end;

  END;

  PRIVATE MACRO Сообщения()
    var SheetName = "Сообщения";
    var IsExistsErr = false;
    var ErrData:CSC_InAccRepErr;
    var ReportLineData_XML:variant;
    var stat = 0;

    SetActiveSheet( SheetName );

    UseNewSheetInTotalBook();

    //1. Ошибки проводок
    ReportLineData_XML = GetLogDataXML(m_comm.rec.DocKind, m_comm.rec.DocumentID, LOG_TYPE_ERROR, false);
    ReportLineData_XML.SetReportNumber(0);
    
    stat = ReportLineData_XML.Next();
    if((stat) or (m_IsExistsCarry == false))
      IsExistsErr = true;

      if(m_PrintMode == DL_OUTREPORT_EXCEL)
        m_ObjTemplateXLS.SetSheetColor( XlColorIndex_RED );
      end;

      CopyAllSheetInTotalBook( SheetName, false, "N3_TableHeader1", 0, SheetName );
      RegisterTable( "N3_MainTable1",   N3TableHeader1,
                     "N3_DealCode",      
                     "N3_DealErrCode",
                     "N3_DealMessage" 
                   );


      if(not stat)
        PrintTableLine( "N3_DealCode:c",    "",                      null,
                        "N3_DealErrCode:c", "",                      null,
                        "N3_DealMessage:l", "Нет подходящих сделок", null
                      );

      else
        while(stat)
          ErrData = ReportLineData_XML.GetReportLineData();
              
          PrintTableLine( "N3_DealCode:c",      ErrData.DealCode, null,
                          "N3_DealErrCode:c",   ErrData.ErrCode,  null,
                          "N3_DealMessage:l:w", ErrData.ErrStr,   null
                        );                                      

          
          stat = ReportLineData_XML.Next();
        end;
      end;

      EndTable();
      CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );
    end; 

    //2. Ошибки комиссий
    if(m_comm.rec.Flag1 == SET_CHAR)
      ReportLineData_XML = GetLogDataXML(m_comm.rec.DocKind, m_comm.rec.DocumentID, LOG_TYPE_ERROR, false);
      ReportLineData_XML.SetReportNumber(1);
      
      stat = ReportLineData_XML.Next();
      if((stat) or (m_IsExistsCarryComis == false))
        IsExistsErr = true;

        if(m_PrintMode == DL_OUTREPORT_EXCEL)
          m_ObjTemplateXLS.SetSheetColor( XlColorIndex_RED );
        end;

        CopyAllSheetInTotalBook( SheetName, false, "N3_TableHeader2", 0, SheetName );
        RegisterTable( "N3_MainTable2",   N3TableHeader2,
                       "N3_ContrCode",      
                       "N3_ContrErrCode",
                       "N3_ContrMessage" 
                     );

        if(not stat)
           PrintTableLine( "N3_ContrCode:c",    "",                        null,
                           "N3_ContrErrCode:c", "",                        null,
                           "N3_ContrMessage:l", "Нет подходящих комиссий", null
                         );

        else
          while(stat)
            ErrData = ReportLineData_XML.GetReportLineData();

            PrintTableLine( "N3_ContrCode:c",    ErrData.DealCode, null,
                            "N3_ContrErrCode:c", ErrData.ErrCode,  null,
                            "N3_ContrMessage:l", ErrData.ErrStr,   null
                          );

            
            stat = ReportLineData_XML.Next();
          end;
        end;

        EndTable();
        CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );
      end;
    end;

    if(IsExistsErr == false)
      PrintFormatString(N3OprStateMsg,
                        "N3_OprStateMsg", "Операция завершена успешно");
      CopyAllSheetInTotalBook( SheetName, false, "N3_OprStateMsg", 0, SheetName );
    end;
  END;

  PRIVATE MACRO ЗакрытиеСчетов()
    var SheetName = "ЗакрытиеСчетов";

    SetActiveSheet( SheetName );

    UseNewSheetInTotalBook();

    // Закрытые счета по выпускам
    PrintFormatString(N5BlockHeader,
                     "N5_BlockHeader", "Закрытие лицевых счетов по выпускам");
    CopyAllSheetInTotalBook( SheetName, false, "N5_BlockHeader", 0, SheetName );

    var cnt_closeaccFI = ReportProtocolCloseAcc_FI(this, m_comm, SheetName);
    if (cnt_closeaccFI != 0)
       EndTable();
       CopyAllSheetInTotalBook( SheetName, false, GetTableRange(), 0, SheetName );

       PrintFormatString(NFooter,
                        "N5_ExecutorPost", "Составил ( " + m_Executer.Post + " ):",
                        "N5_ExecutorName", string( m_Executer.Name ),
                        "N5_ExecuteDate:l",  m_comm.rec.CommDate:f
                        );
       CopyAllSheetInTotalBook( SheetName, false, "N5_Footer", 0, SheetName );
    else
       PrintFormatString(N5StateMsg,
                        "N5_StateMsg", "Нет обработанных счетов");
       CopyAllSheetInTotalBook( SheetName, false, "N5_StateMsg", 0, SheetName );
    end;
  END;

  PRIVATE MACRO Create()
    Проводки();
    if(m_comm.rec.Flag1 == SET_CHAR)
      ПериодическиеКомиссии();
    end;
    ЗакрытиеСчетов();
    Сообщения();
  END;

  m_PrintMode = Mode;
  m_comm = dl_comm;

  initDL_CReportTemplate( NULL, "Report_InAcc.xls", NULL, IsWebService );

END;

private MACRO PrintInAccByXML(comm, Mode)
  record DlComm("dl_comm");
  var reccomm = TRecHandler("dl_comm");
  
  SetBuff(DlComm, comm);

  copy(reccomm, dlcomm);

  SC_InAcc_Report(reccomm, Mode).Run();
  return true;

END;

MACRO WebPrintInAccByXML(comm, Mode)
  var rep, FC;

  rep = SC_InAcc_Report(comm, Mode, true);
  rep.Run();

  if(Mode == DL_OUTREPORT_STD)
     FC = CreateFileContainer(ConvertTxt2Html(rep.GetFullReportFileNames[0]));
  elif(Mode == DL_OUTREPORT_EXCEL)
     FC = CreateFileContainer(rep.GetFullReportFileNames[0]);
  end;

  return FC;
END;

MACRO PrintInAccByXMLToExcel(comm)
  return PrintInAccByXML(comm, DL_OUTREPORT_EXCEL);
END;

MACRO PrintInAccByXMLToSTD(comm)
  return PrintInAccByXML(comm, DL_OUTREPORT_STD);
END;

// Печать отчета по откату оперции
macro PrintRollBackReport(comm)
  VAR ReportFileName, errtext, errcode;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/
  record DlComm("dl_comm");

  SetBuff(DlComm, comm);
  
  ReportFileName = GetTxtFileName( "inacc_" + DlComm.CommCode );
  if( not Open( ReportOutFile, ReportFileName ) )
     errcode = status(errtext);
     RunError( "Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext );
  end;
  SetOutput( ReportFileName );

  var sql =  " select t_warning from DSCREPLOG_TMP group by t_warning order by t_warning ";
     
  var cmd = RSDCommand(sql);
  cmd.execute();

  var DataSet = TRsbDataSet(cmd);
  var i = 1, IsFirstUse = true;;

  while( DataSet.MoveNext() )
     if(IsFirstUse == true )
        println(" Протокол отката операции");
        println("┌──────────┬──────────────────────────────────────────────────────────────┐");
        IsFirstUse = false;
     else
        println("├──────────┼──────────────────────────────────────────────────────────────┤");
     end;

     [│##########│##############################################################│]
     (i:r, DataSet.Warning:w);
     i = i + 1;

  end;

  if( IsFirstUse == false )
     println("└──────────┴──────────────────────────────────────────────────────────────┘");
  end;

  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );

  return true;
end;


PRIVATE CLASS (DL_CReportTemplate) SC_InAccMsg_Report(dl_comm)
  PRIVATE VAR m_comm = TRecHandler("dl_comm");

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  private macro RemoveSymbFromMsg(symb:string, msg:string):string
     var retVal:string = "";
     var startPos:integer = 1;

     while( startPos > 0 ) 
       startPos = StrBrk ( msg, "|" ); 
       var tmpStr:string = Trim ( SubStr(msg,1,startPos-1) );
       if ( StrLen( tmpStr ) )
         retVal = RetVal + tmpStr + " ";
       end;
       msg = SubStr( msg, startPos+1 );
     end;
     retVal = Trim(RetVal + msg);
     return retVal;
  end;

  PRIVATE MACRO Create()

    var ReportLineData_XML:variant;
    var IsExistsErr = false;
    var ErrData:CSC_InAccRepErr;
    var reccomm = TRecHandler("dl_comm");
    VAR errtext, errcode;
    var stat = 0;
    var cntobj = 0, cmd;
    var SheetName = "Сообщения";
    var i = 0;
    

    SetActiveSheet(SheetName);

    RegisterTable( "N1_MainTable", "", "N1_DealCode", "N1_ErrCode", "N1_Message");

    PrintFormatString(NULL,
                      "N1_OprDate", string(m_comm.rec.CommDate:f),
                      "N1_OprCode", m_comm.rec.CommCode);

    cmd = DL_RSDCommand("select 1 from ddlgrdoc_dbt where t_ServDocKind = ? and t_ServDocID = ? and rownum = 1");
    cmd.AddParam(m_comm.rec.DocKind);
    cmd.AddParam(m_comm.rec.DocumentID);
    cntobj = cmd.GetCount();

    ReportLineData_XML = GetLogDataXML(m_comm.rec.DocKind, m_comm.rec.DocumentID, LOG_TYPE_ERROR, false);
    ReportLineData_XML.SetReportNumber(0);
    
    stat = ReportLineData_XML.Next();

    if(stat or (cntobj == 0))
      PrintFormatString("", "N1_OprStateMsg", "");
      
      if(not stat)
        PrintTableLine( "N1_DealCode",  "",                      null,
                        "N1_ErrCode",   "",                      null,
                        "N1_Message",   "Нет подходящих сделок", null
                      );

      else
        InitProgress(-1, "Печать сообщений", "Печать сообщений");
        while(stat)
          ErrData = ReportLineData_XML.GetReportLineData();
          var RetValErrMsg:string = "";
          RetValErrMsg  = RemoveSymbFromMsg("|",ErrData.ErrStr);         
          PrintTableLine( "N1_DealCode",  ErrData.DealCode, null,
                          "N1_ErrCode",   ErrData.ErrCode,  null,
                          "N1_Message:w", RetValErrMsg,     null
                        );

          stat = ReportLineData_XML.Next();

          UseProgress(i = i + 1);
        end;
        RemProgress();
      end;

      EndTable();

    else
      PrintFormatString("", "N1_OprStateMsg", "Операция завершена успешно");
    end;

  END;

  m_comm = dl_comm;

  initDL_CReportTemplate( NULL, "Report_InAccMsg.xls", NULL, false );
END;

MACRO PrintInAccMsg(comm)
    record DlComm("dl_comm");
  var reccomm = TRecHandler("dl_comm");

  SetBuff(DlComm, comm);

  copy(reccomm, dlcomm);

  var Rep = SC_InAccMsg_Report(reccomm);
      Rep.Run(1);
           
  return true; 
END;


private MACRO ErrorFromCnvpack( cnv)
  var Cmd, Query, DataSet, err = 0;
  Query = " select t_Error, t_ErrMsg " +
          " from dcnvpack_dbt " +
          " where t_ExecID =  "  + string(cnv.ExecID) + 
          " group by t_Error, t_ErrMsg" ;
 
  Cmd = RSDCommand( Query );      
  
  Cmd.Execute();
  DataSet = TRsbDataSet(Cmd);

  while( DataSet.MoveNext() )
    if( DataSet.Error != 0 ) 
      err = err + 1;    
     
      if((DataSet.ErrMsg != "") OR (DataSet.Error != 1))
      RepErrArr[RepErrArr.size] = CSC_InAccRepErr(0,"", DataSet.Error, IIF( DataSet.ErrMsg, DataSet.ErrMsg, "" ) );
      end;
    end; 
  end;
  return err;
END;

//Режимы формирования выборки
const INACC_MODE_NO     = 0; //без режима
const INACC_MODE_MAIN   = 1; //режим основной обработки - т.е. без комиссий
const INACC_MODE_PLAN_MAIN     = 2; //Плановые РОВУ
const INACC_MODE_PLAN_MAIN_AVR = 3; //Плановые РОВУ только по ц/б

//выборка данных для конвейера
macro GetInAccObjectQuery(dl_comm, mode, FI, Department, ContrID, RetParmArr:@TArray)
  var query;
  var isPlanMode = IIF(((mode == INACC_MODE_PLAN_MAIN) or (mode == INACC_MODE_PLAN_MAIN_AVR)), true, false);

  if(ValType(mode) == V_UNDEF)
    mode = INACC_MODE_NO;
  end;


  private macro AddRetParm(Parm)
    var retStr = "?";

    if(ValType(RetParmArr) != V_UNDEF)
      RetParmArr[RetParmArr.size] = Parm;
    else
      if(ValType(Parm) == V_DATE)
        retStr = GetSQlDate(Parm);
      else
        retStr = string(Parm);
      end;
    end;

    return retStr;
  end;

  var DeliveryTemplNums = DLGR_TEMPL_DELIVERY+","
                         +DLGR_TEMPL_DELIVERYNTG+","      
                         +DLGR_TEMPL_DELIVERYCONTR+","    
                         +DLGR_TEMPL_COMPDELIVERY+","     
                         +DLGR_TEMPL_COMPDELIVERYCONTR+","
                         +DLGR_TEMPL_DELIVERY2+","        
                         +DLGR_TEMPL_DELIVERYCONTR2;   


  query = "SELECT 0 as t_ObjectType, gracc_inn.t_ID as t_ObjectID, td.t_DealCode as t_DealNumber, "
        + "       (CASE WHEN    grdeal.t_TemplNum IN ("+DeliveryTemplNums+") THEN td.t_PFI "
        + "             ELSE td.t_PayFI END) as t_FI, "
        + "       td.t_Department as t_Department, "
        + "       (CASE WHEN td.t_IsPartyClient = 'X' AND (   grdeal.t_TemplNum = "+DLGR_TEMPL_DELIVERYCONTR
        + "                                                OR grdeal.t_TemplNum = "+DLGR_TEMPL_DELIVERYCONTR2
        + "                                                OR grdeal.t_TemplNum = "+DLGR_TEMPL_COMPDELIVERYCONTR
        + "                                                OR grdeal.t_TemplNum = "+DLGR_TEMPL_PAYCOMCONTR
        + "                                               ) THEN td.t_PartyContrID "
        + "             ELSE td.t_ClientContrID END) as t_ContrID "
        + "           FROM dv_sctotaldeals td, ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc_inn, ddlgracc_dbt gracc_bo "
        + "          WHERE grdeal.t_DocKind = td.t_DocKind "
        + "            AND grdeal.t_DocID = td.t_DocID "
        + "            AND gracc_inn.t_GrDealID = grdeal.t_ID "
        + "            AND gracc_inn.t_AccNum = " + DLGR_ACCKIND_INNER
        + "            AND gracc_inn.t_State = " + DLGRACC_STATE_PLAN
        + "            AND gracc_bo.t_GrDealID = grdeal.t_ID "
        + "            AND gracc_bo.t_AccNum = " + DLGR_ACCKIND_BACKOFFICE
        + "            AND (   td.t_DocKind in (" + DL_NTGSEC + "," + DL_SCINOUTPOOL + ")"
        + "                 OR (    td.t_DocKind not in (" + DL_NTGSEC + "," + DL_SCINOUTPOOL + ")"
        + "                     AND Exists (SELECT 1 FROM ddl_leg_dbt LEG "
        + "                                  WHERE LEG.T_DEALID = td.T_DOCID "
        + "                                    AND LEG.T_LEGKIND = " + LEG_KIND_DL_TICK
        + "                                    AND LEG.T_LEGID = 0 "
        + "                                    AND LEG.T_OPERSTATE " + IIF((isPlanMode == false), " > ", " >= ") + DL_LEG_PREPARING
        + "                                ) "
        + "                    )"
        + "                ) ";

  if(isPlanMode == false)
    query = query + " AND gracc_bo.t_State IN (" + DLGRACC_STATE_NOTNEED + ", "+DLGRACC_STATE_FACTEXEC+") "
                  + " AND grdeal.t_PlanDate = " + AddRetParm(date(dl_comm.rec.CommDate));
  else
    query = query + " AND grdeal.t_PlanDate >= " + AddRetParm(date(dl_comm.rec.BeginDate))
                  + " AND grdeal.t_PlanDate <= " + AddRetParm(date(dl_comm.rec.EndDate));
  end;

  if(mode == INACC_MODE_PLAN_MAIN_AVR)
    query = query + "and grdeal.t_TemplNum IN ("+DeliveryTemplNums+") ";
  end;

  if(dl_comm.rec.Division > 0)
    query = query + " AND td.t_Department = " + AddRetParm(dl_comm.rec.Division);
  end;

  if(dl_comm.rec.OperationKind > 0)
    query = query + "  AND grdeal.t_TemplNum = " + AddRetParm(dl_comm.rec.OperationKind);
  end;

  if(dl_comm.rec.AvoirKind > 0)
    query = query + "  AND 1 = (SELECT RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AddRetParm(dl_comm.rec.AvoirKind)+", avr_fin.t_AvoirKind) "
                  + "            FROM dfininstr_dbt avr_fin "
                  + "           WHERE avr_fin.t_FIID = grdeal.t_FIID)";
  end;

  if(dl_comm.rec.FIID > -1)
    query = query + "  AND (grdeal.t_FIID = " + AddRetParm(dl_comm.rec.FIID) + " OR td.t_CFI = " + AddRetParm(dl_comm.rec.FIID) + ") ";
  end;

  if(dl_comm.rec.ContractKind != SP_SECDEALTYPE_ALL)
    if(dl_comm.rec.ContractKind == SP_SECDEALTYPE_OWN)
      query = query + " AND td.t_ClientID = -1 ";
    elif(dl_comm.rec.ContractKind == SP_SECDEALTYPE_CLNT)
      query = query + " AND td.t_ClientID <> -1 ";
    end;
  end;

  if(dl_comm.rec.OperSubKind > 0)
    query = query + "  AND td.t_Kind_Operation = " + AddRetParm(dl_comm.rec.OperSubKind);
  end;

  if(dl_comm.rec.Deal1 > 0)
    query = query + "  AND td.t_DocID = " + AddRetParm(dl_comm.rec.Deal1);
  end;

  if(dl_comm.rec.ClientID > -1)
    query = query + "  AND td.t_ClientID = " + AddRetParm(dl_comm.rec.ClientID);
  end;

  if(dl_comm.rec.ContractID > 0)
    query = query + "  AND td.t_ClientContrID = " + AddRetParm(dl_comm.rec.ContractID);
  end;

  query =   " select q.*, ROUND(ROWNUM/100, 0)+1 as t_GroupNumber from"  
          + "          (select q1.t_ObjectType, q1.t_ObjectID, q1.t_DealNumber, q1.t_FI, q1.t_Department, q1.t_ContrID "
          + "             from ("+query;

    if(dl_comm.rec.Flag1 == SET_CHAR) /*Клиентские комиссии за обороты*/
      query = query + " UNION ALL "
                    + " SELECT "+OBJTYPE_SFCONTR+" as t_ObjectType, gracc_inn.t_ID as t_ObjectID, contr.t_Number as t_DealNumber, "
                    + "        -1 as t_FI, contr.t_Department as t_Department, contr.t_ID as t_ContrID "
                    + "   FROM ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc_inn, dsfcontr_dbt contr "
                    + "  WHERE grdeal.t_DocKind = " + OBJTYPE_SFCONTR
                    + "    AND grdeal.t_PlanDate = " + AddRetParm(date(dl_comm.rec.CommDate))
                    + "    AND gracc_inn.t_GrDealID = grdeal.t_ID "
                    + "    AND gracc_inn.t_AccNum = " + DLGR_ACCKIND_INNER
                    + "    AND gracc_inn.t_State = " + DLGRACC_STATE_PLAN  
                    + "    AND contr.t_ID = grdeal.t_DocID "
                    + "    AND contr.t_Department = " + AddRetParm(dl_comm.rec.Division);

    if(dl_comm.rec.ClientID > -1)
      query = query + "  AND contr.t_PartyID = " + AddRetParm(dl_comm.rec.ClientID);
    end;

    if(dl_comm.rec.ContractID > 0)
      query = query + "  AND contr.t_ID = " + AddRetParm(dl_comm.rec.ContractID);
    end;

  end;

  query = query + ") q1 order by q1.t_Department, q1.t_ContrID, q1.t_FI) q "
                + " where q.t_ContrID >= 0 "; 

  if(ValType(FI) != V_UNDEF)
    query = query + " and q.t_FI = " + AddRetParm(FI);
    end;

  if(ValType(Department) != V_UNDEF)
    query = query + " and q.t_Department = " + AddRetParm(Department);
  end;

  if(ValType(ContrID) != V_UNDEF)
    query = query + " and q.t_ContrID = " + AddRetParm(ContrID);
  end;
    
  return query;
end;

private macro ПроверитьПеренумерациюСделок( dl_comm:variant ):integer

  var RetVal:integer = 0;

  if( НужнаПеренумерацияСделокЗаДату(dl_comm.rec.CommDate) )
     if( MsgBoxEx("Требуется выполнить перенумерацию сделок в дате " + string(dl_comm.rec.CommDate:f) + ". Продолжить выполнение без перенумерации?", MB_YES+MB_NO, IND_NO) != IND_YES )
        RepErrArr[RepErrArr.size] = CSC_InAccRepErr(0, "", 1, "Требуется выполнить перенумерацию сделок в дате " + string(dl_comm.rec.CommDate:f));
        RetVal = 1;
     end;
  end;

  return RetVal;
end;

macro ПроверитьОперациюВУзаТекущуюДату(dl_comm)
  var query, cmd, DataSet;
  var errStr = "";
  var i = 0;
  var RetParmArr = TArray();
  
  query =   "select t_DealNumber, t_ObjectType "
          + "  from ("+GetInAccObjectQuery(dl_comm, NULL, NULL, NULL, NULL, RetParmArr)+") "
          + " group by t_DealNumber, t_ObjectType ";

  cmd = DL_RSDCommand(query);

  if(RetParmArr.size > 0)
    i = 0;
    while(i < RetParmArr.size)
      cmd.AddParam(RetParmArr[i]);
      i = i + 1;
    end;
  end;

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    errStr = "";

    if(DataSet.ObjectType == OBJTYPE_SFCONTR)
      errStr = "По договору обслуживания "+DataSet.DealNumber+" остались необработанные строки графика за дату " + string(date(dl_comm.rec.CommDate):f);
    else
      errStr = "По сделке "+DataSet.DealNumber+" остались необработанные строки графика за дату " + string(date(dl_comm.rec.CommDate):f);
    end;
    if(errStr != "")
      RepErrArr[RepErrArr.size] = CSC_InAccRepErr(0,DataSet.DealNumber, 1, errStr );
    end;
  end;
end;

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//ЗАПУСК КОНВЕЙЕРА

//класс с данными для конвейера
class CnvDataDLCOMM(_DocumentID, _mode)
  var DocumentID = _DocumentID;
  var mode = _mode;
end;

//запуск конвейера
private macro __StartCnvInAcc( dl_comm:TBFile, IsWebService )
  var rslObj = CnvDataDLCOMM(dl_comm.rec.DocumentID, INACC_MODE_MAIN);
//  var ConvID = 13; //Вид конвейера Внутренний учет БО ЦБ
//  var OperID = 10; //Операция для конвейра - Пакетная операция формирования внутреннего учета по сделкам модуля "Ценные бумаги"

//  var UseConveyer = SecurUseConveyer(ConvID, OperID);
  var UseConveyer = UserSecurUseConveyer();

  var err = 0;

  if( ПроверитьПеренумерациюСделок(dl_comm) != 0 )
     return 0;
  end;

  if(UseConveyer)
    var ConvID = 13; //Вид конвейера Внутренний учет БО ЦБ
    var OperID = 10; //Операция для конвейра - Пакетная операция формирования внутреннего учета по сделкам модуля "Ценные бумаги"
    var cnv = RsbConveyerExec(ConvID);

    cnv.AddParamsForConveyer(ConvID, rslObj, 0);
    cnv.AddParamsForOperation(ConvID, OperID, rslObj, "", 0);
    if( (IsWebService != null) and (IsWebService == true) )
       cnv.showPanelMonitoring(0);
    else
       cnv.showPanelMonitoring(1);
    end;
    cnv.Run();
    
    if( ErrorFromCnvpack( cnv ) > 0 )     
      err = 1;
    end;
  else
    err = ExecMacroFile("sc_inacc.mac", "ExecuteInAcc", 0, 0, 0, 0, 0, rslObj);
  end;

  if( err == 0 )
    //Если СОВУ выполняется последней среди всех СО, обрабатывающих сделки, 
    //то при работе через конвейер довольно редко могут быть ситуации, когда по сделке обработаны все строки гарфика, но она не закрылась
    //Связано это с обработкой в СОВУ в различных потоках Оплаты и Поставки сделки
    //Иногда, если получилась параллельная обработка, то триггер одного потока может ещё не увидеть изменений другого потока, и сделка не закроется
    //Для этого здесь попробуем закрыть все обработанные в СОВУ сделки, которые должны были быть закрыты, но не закрылись.
    err = ВыполнитьЗакрытиеСделокСО(dl_comm.rec.DocKind, dl_comm.rec.DocumentID);
  end;
  
  if( err == 0 )
     ПроверитьОперациюВУзаТекущуюДату(dl_comm);
  end;
  
  return err;

  OnError(er)
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  RepErrArr[RepErrArr.size] = CSC_InAccRepErr(0, 0, 1, er.module+ " "+er.line+" "+er.message );

  return 1;
end;

macro StartCnvInAcc(DocumentID, IsWebService)

  macro getDocumentName(id)
    var query = "select * from doprkdoc_dbt where t_dockind = " + id;
    var cmd = RSDCommand(query);
    var rs = RSDRecordSet(cmd);
    if (rs.movenext)
      return rs.value("t_name");
    else
      return "";
    end; 
  end;

  var stat = 0;
  var dl_comm = TBFile("dl_comm.dbt");  

  dl_comm.rec.DocumentID = DocumentID;
  if(not dl_comm.GetEQ())
    msgbox("Ошибка поиска операции");
    return 1;
  end;   

  //file docfcomm("dl_comm.dbt");
  //var dl_comm_old_rec = TRecHandler ("dl_comm");
  //var dl_comm_new_rec = TRecHandler ("dl_comm");
  //copy(dl_comm_old_rec,dl_comm);  

  RepErrArr.size = 0; //массив ошибок

  //WriteFiscLog(OLstrproc, "Старт сервисной операции Внутренний учет. Имя файла - dl_comm_dbt. ID операции "+dl_comm.rec.documentid+
  //    ". Тип операции ("+dl_comm.rec.dockind+") "+getDocumentName(dl_comm.rec.dockind)+
  //    ". Код операции "+dl_comm.rec.commcode); 
  //WriteFiscLog(OLinsert,docfcomm,dl_comm_old_rec);

  stat = __StartCnvInAcc(dl_comm, IsWebService);

  SaveInAccErrForReport_XML( RepErrArr, dl_comm.rec.DocumentID, dl_comm.rec.DocKind );  

  //WriteFiscLog(OLfinproc, "Завершение сервисной операции Внутренний учет. Имя файла - dl_comm_dbt. ID операции "+dl_comm.rec.documentid+
  //    ". Тип операции ("+dl_comm.rec.dockind+") "+getDocumentName(dl_comm.rec.dockind)+
  //    ". Код операции "+dl_comm.rec.commcode); 
  //copy(dl_comm_new_rec,dl_comm);  
  //WriteFiscLog(OLinsert,docfcomm,dl_comm_new_rec);

  return stat;
end;



