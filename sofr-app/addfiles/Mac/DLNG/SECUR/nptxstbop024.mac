/*
 $Name: nptxstbop024.mac
 $Module: Ценные бумаги
 $Description: Сопровождение событий СНОБ. Шаг "Корректировка. Изменение удержанного налога"
 */

import DealsInter, "nptxstbfun.mac", "nptxcalcfun.mac";

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  var err = 0;
  var OrigSTB = TRecHandler("nptxtotalbase.dbt");
  var STB = TRecHandler("nptxtotalbase.dbt");
  var STBArr = TArray();
  var query, cmd, DataSet;
  var NeedLoopStep = false;
  var ErrStr = "";
  var HoldPITax = $0;
  var ObjKind = 0, ObjKind2 = 0;
  var AnaliticKind1 = 0, AnaliticKind2 = 0, AnaliticKind3 = 0, AnaliticKind4 = 0, AnaliticKind5 = 0, AnaliticKind6 = 0;
  var Analitic1 = -1, Analitic2 = -1, Analitic3 = -1, Analitic4 = -1, Analitic5 = -1, Analitic6 = -1;
  var Sum = 0;

  SetBuff( OrigSTB, FDoc );

  HoldPITax = OrigSTB.rec.HoldPITax;

  if(GetMoney(HoldPITax, "Удержанный налог:", NULL, false, NULL, "Корректировка события СНОБ", "Введите сумму удержанного налога" ) == false )
    return 1;
  end;
  
  copy(STB, OrigSTB);

  OrigSTB.rec.ConfirmState = NPTXTOTALBASE_CONFIRMSTATE_NOTCONFIRMED; 
  OrigSTB.rec.StorState    = NPTXTOTALBASE_STORSTATE_CANCELED;

  err = CheckAndSaveSTB(OrigSTB);
  if(err)
    err = 1;
    msgbox("Ошибка при обновлении записи");
  end;

  if(not err)
    STB.rec.TBID         = 0;
    STB.rec.StorState    = NPTXTOTALBASE_STORSTATE_ACTIVE;
    STB.rec.ConfirmState = NPTXTOTALBASE_CONFIRMSTATE_NOTCONFIRMED; //Не подтверждена
    STB.rec.OrigTBID     = OrigSTB.rec.TBID;
    STB.rec.StorID       = "";
    STB.rec.Instance     = 0;
    STB.rec.ID_Operation = 0;
    STB.rec.ID_Step      = 0;
    STB.rec.HoldPITax    = HoldPITax;

    err = CheckAndSaveSTB(STB);
    if(err)
      err = 1;
      msgbox("Ошибка при создании записи");
    end;
  end;

  if(not err)
    var OperID = OrigSTB.rec.DocID;
    
    if(OrigSTB.rec.DocKind == DL_VEKSELDRAWORDER)
      query =   " select op.t_ID_Operation "
              + "   from doproper_dbt op "
              + "  where op.t_DocKind = ? "
              + "    and op.t_DocumentID = LPAD(?, 10, '0') ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(OrigSTB.rec.DocKind);
      cmd.AddParam(OrigSTB.rec.DocID);

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        OperID = DataSet.ID_Operation;
      end;
    end;
    
    //Формируем НДР

    cmd = DL_RSDCommand();

    query =   " select o.* "
            + "   from dnptxobdc_dbt dc, dnptxobj_dbt o "
            + "  where dc.t_DocID = ? "
            + "    and o.t_ObjID = dc.t_ObjID "
            + "    and o.t_Client = ? "
            + "    and o.t_Level = 8 ";

    cmd.AddParam(OperID);
    cmd.AddParam(OrigSTB.rec.ClientID);        

    ObjKind  = 0;
    ObjKind2 = 0;

    GetPaidNptxObjKindByTaxRate(OrigSTB.rec.BCCHoldPITax, OrigSTB.rec.TaxBaseKind, OrigSTB.rec.RateHoldPITax, @ObjKind, @ObjKind2, STB.rec.SpecialTag, STB.rec.TaxPeriod);

    if(ObjKind > 0)
      query = query + " and o.t_Kind = ? ";
      cmd.AddParam(ObjKind);
    end;

    DataSet = cmd.Execute(query);

    if(DataSet.moveNext())
      AnaliticKind1 = DataSet.AnaliticKind1;
      Analitic1     = DataSet.Analitic1;    
      AnaliticKind2 = DataSet.AnaliticKind2;
      Analitic2     = DataSet.Analitic2;    
      AnaliticKind3 = DataSet.AnaliticKind3;
      Analitic3     = DataSet.Analitic3;    
      AnaliticKind4 = DataSet.AnaliticKind4;
      Analitic4     = DataSet.Analitic4;    
      AnaliticKind5 = DataSet.AnaliticKind5;
      Analitic5     = DataSet.Analitic5;    
      AnaliticKind6 = DataSet.AnaliticKind6;
      Analitic6     = DataSet.Analitic6;    
    end;

    Sum = STB.rec.HoldPITax - OrigSTB.rec.HoldPITax;

    if(Sum != 0)
    
      if(ObjKind)
        err = DL_NPTX_InsertTaxObject(STB.rec.IncDate    // Дата НДР
                                     ,STB.rec.ClientID   // Клиент
                                     ,TXOBJ_DIR_OUT      // Направление
                                     ,8                  // Уровень
                                     ,""                 // Признак "Пользовательский"
                                     ,ObjKind            // Вид НДР
                                     ,Sum                // Сумма дохода/расхода
                                     ,NATCUR             // Валюта дохода/расхода
                                     ,AnaliticKind1      // Вид аналитики 1
                                     ,Analitic1          // Аналитика 1
                                     ,TXOBJ_KIND2040      // Вид аналитики 2
                                     ,int(substr(STB.rec.BCCHoldPITax,8,3))          // Аналитика 2
                                     ,AnaliticKind3      // Вид аналитики 3
                                     ,Analitic3          // Аналитика 3
                                     ,AnaliticKind4      // Вид аналитики 4
                                     ,Analitic4          // Аналитика 4
                                     ,AnaliticKind5      // Вид аналитики 5
                                     ,Analitic5          // Аналитика 5
                                     ,AnaliticKind6      // Вид аналитики 6
                                     ,Analitic6          // Аналитика 6
                                     ,"Объект, созданный при корректировке события СНОБ" // Комментарий
                                     ,ID_Operation       // ID операции
                                     ,ID_Step            // ID шага операции
                                     ,1                  // Признак вызова не из операции расчета НОБ
                                     );

       end;

       if((not err) and (ObjKind2))
         err = DL_NPTX_InsertTaxObject( STB.rec.IncDate      // Дата НДР
                                       ,STB.rec.ClientID     // Клиент
                                       ,TXOBJ_DIR_OUT        // Направление
                                       ,8                    // Уровень
                                       ,""                   // Признак "Пользовательский"
                                       ,ObjKind2             // Вид НДР
                                       ,Sum                  // Сумма дохода/расхода
                                       ,NATCUR               // Валюта дохода/расхода
                                       ,AnaliticKind1        // Вид аналитики 1
                                       ,Analitic1            // Аналитика 1
                                       ,TXOBJ_KIND2040      // Вид аналитики 2
                                       ,int(substr(STB.rec.BCCHoldPITax,8,3))          // Аналитика 2
                                       ,AnaliticKind3        // Вид аналитики 3
                                       ,Analitic3            // Аналитика 3
                                       ,AnaliticKind4        // Вид аналитики 4
                                       ,Analitic4            // Аналитика 4
                                       ,AnaliticKind5        // Вид аналитики 5
                                       ,Analitic5            // Аналитика 5
                                       ,AnaliticKind6        // Вид аналитики 6
                                       ,Analitic6            // Аналитика 6
                                       ,"Объект, созданный при корректировке события СНОБ"  // Комментарий
                                       ,ID_Operation          // ID операции
                                       ,ID_Step               // ID шага операции
                                       ,1                  // Признак вызова не из операции расчета НОБ
                                      );

        end;

        if((not err) and (STB.rec.SpecialTag == "МАТ"))
          err = CreateDueMaterial(ID_Operation, STB.rec.ClientID, STB.rec.IncDate, STB.rec.TaxPeriod, ID_Step, STB.rec.BCCHoldPITax, Sum, NULL, true);
        end;
    end;
  end;

  if(not err)
    if(not Opr_InsertBranch("N", "I", true))
       msgbox( "Ошибка при вставке цепочки" );        
       err = 1;
    end;
  end;

  if(not err)
    err = DL_NPTX_SaveTbMes();
  end;

  return err;
end;


