/*
$Name:        mvcb25.mac
$Module:      Ценные бумаги
$Description: Операция "Перевод ценных бумаг". Шаг "Перевод лотов"
*/
import  InsCarryDoc, DealsInter, OprInter, SecInter, "dlcarry.inc", "sp_categ.mac", "sp_cartr.mac", "scservop.mac";

RECORD dl_comm_trans("dlcomsum.str"); 

private var CourseDateCris2014 = date(1,10,2014);

MACRO ПоменятьПортфельСрочнСд( comm:variant )
   var deal = TRecHandler("dl_tick.dbt"); 
   var FD, dataset, Sel, SqlSel;

   if( not IsTransferGlobal(comm.OperSubKind) ) 
      return true;
   end;

   SqlSel = " SELECT TICK.* " +
                     "   FROM DPMWRTSUM_DBT WRTSUM, DDL_TICK_DBT TICK " +
                     "  WHERE WRTSUM.T_PARTY       = -1 " +
                     "    AND WRTSUM.T_DEPARTMENT  = ? " +
                     "    AND WRTSUM.T_CONTRACT    = 0 " +
                     "    AND WRTSUM.T_BUY_SALE    = ? " +
                     "    AND (                        " +
                     "          WRTSUM.T_PORTFOLIO = ? ";

   if( comm.OperSubKind == DLCOMM_OPERSUBKIND_TO_CONTROL )
     SqlSel = SqlSel +
                     "          OR WRTSUM.T_PORTFOLIO = ? ";
   elif( comm.OperSubKind == DLCOMM_OPERSUBKIND_UNRETIRE )
     SqlSel = SqlSel +
                     "          OR WRTSUM.T_PORTFOLIO = ? " +
                     "          OR WRTSUM.T_PORTFOLIO = ? ";
   end;

   SqlSel = SqlSel + "        )                        " +
                     "    AND WRTSUM.T_STATE       = ? " + 
                     "    AND WRTSUM.T_ENTERDATE   <= ? " +    
                     "    AND WRTSUM.T_FIID        = ? " +              
                     "    AND WRTSUM.T_AMOUNT      > 0 " +
                     "    AND TICK.t_DEALID        = WRTSUM.T_DealID";

   sel =  RSDCommand(SqlSel);

   sel.addParam("", RSDBP_IN, comm.Division);
   sel.addParam("", RSDBP_IN, PM_WRITEOFF_SUM_BUY);              
 
   if( comm.OperSubKind == DLCOMM_OPERSUBKIND_TO_CONTROL )
      sel.addParam("", RSDBP_IN, KINDPORT_SALE);
      sel.addParam("", RSDBP_IN, KINDPORT_TRADE);
   elif( comm.OperSubKind == DLCOMM_OPERSUBKIND_UNRETIRE )
      sel.addParam("", RSDBP_IN, KINDPORT_SALE);
      sel.addParam("", RSDBP_IN, KINDPORT_TRADE);
      sel.addParam("", RSDBP_IN, KINDPORT_RETIRE);
   else
      sel.addParam("", RSDBP_IN, KINDPORT_CONTR);
   end;

   sel.addParam("", RSDBP_IN, PM_WRTSUM_NOTFORM);
   sel.addParam("", RSDBP_IN, comm.CommDate);
   sel.addParam("", RSDBP_IN, comm.FIID);

   sel.execute();

   dataset = TRsbDataSet(sel); 

   while( dataset.MoveNext() )

      deal.Clear();
      dataset.GetRecord().CopyTo( deal.rec );

      FD = SPFirstDoc( deal );

      if( comm.OperSubKind == DLCOMM_OPERSUBKIND_TO_CONTROL )
         FD.tick.rec.PortfolioID = KINDPORT_CONTR;
      elif( comm.OperSubKind == DLCOMM_OPERSUBKIND_UNRETIRE )
         FD.tick.rec.PortfolioID = KINDPORT_PROMISSORY;
      else
         if(comm.DestPortofolio != KINDPORT_UNDEF)
            FD.tick.rec.PortfolioID = comm.DestPortofolio;
         else
            if(ExistNOSS(FD.avoiriss, comm.CommDate))
               FD.tick.rec.PortfolioID = KINDPORT_TRADE;
            else
               FD.tick.rec.PortfolioID = KINDPORT_SALE;
            end;
         end;
      end;

      if( OprInsertSPTKCHNG( comm.CommDate, SPTKCHNG_GLOBMOV, FD.tick, FD.dl_leg, FD.dl_leg ) == false )
         msgbox("Ошибка при обновлении сделки с ID = " + FD.tick.rec.DealID);
         return false;
      end;

   end;
   
   return true;            
END;

MACRO ExecuteStep( d, adr, dockind, ID_Operation, ID_Step )

  record comm (dl_comm);
  record PosOvervlAcc( account );
  record NegOvervlAcc( account );

  var wrtsum = TBFile("pmwrtsum", "R", 1);
  var FD = "", KindPort, Course, CourseDate;
  SetBuff( comm, adr );
  var
      SumOnOvervlPos = $0, SumOnOvervlNeg = $0, Друб = $0, локДруб = $0, SCакт = $0, SТСС = $0, locSТСС = $0,
      ArrCatDeb = TArray(), ArrCatCred = TArray(), Sum = TArray(), i = 0, FIRole = -1, СО = $0,
      wrtcalc = TRecHandler( "pmwrtsum.dbt" ), Ground, СП = $0, SО = $0, SСакт = $0;

  if(comm.OperSubKind > 0)
    msgbox("Исполнение данного вида операции больше не поддерживается");
    return 1;
  end;

  if( ПолучитьПервичныйДокументДляПеревода( comm, dl_comm_trans, FD ) == false )
     return 1;
  end;

  if( IsTransferLocal(comm.OperSubKind) ) //локальное перемещение

     wrtsum.Clear();
     wrtsum.rec.DocKind  = comm.DocKind;
     wrtsum.rec.DocID    = comm.DocumentID;
     wrtsum.rec.Buy_Sale = PM_WRITEOFF_SUM_SALE;
     wrtsum.rec.PartNum  = 0;

     if (wrtsum.GetEQ())
        if( not ОбновитьЛот( wrtsum, PM_WRTSUM_UPDTMODE_DELIVERY, comm.CommDate, FD ) )
           msgbox("Ошибка при обновлении лота");
           return 1;
        end;
     else
        msgbox("Не найден лот");
        return 1;
     end;
             
  end;

  if( not ПоменятьПортфельСрочнСд(comm) )
     return 1;
  end;

  return 0;

end;
