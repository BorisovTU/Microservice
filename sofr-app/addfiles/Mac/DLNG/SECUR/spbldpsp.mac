/*
$Name:         spbldpsp.mac
$Module:       Ценные бумаги
$Description:  Отчет "Сальдовая ведомость по размещению ценных бумаг"
*/

import spColArr, spReport, CTInter, deposerv, CurrInter, FIInter, "spserv.mac", cb_sql;

var Rep = CMakeReport();
var
    HeadTable = "",
    is_ap;

const RepFontStyleTitel = "ex_FS( b )";

var
    prevFIID = ALLFININSTR,
    first = true,
    SumDebet = $0,
    SumCredit = $0,
    SumRestA = $0,
    SumRestP = $0;

const Depo_chapt = 5;

macro SumToStr( sum ) : string
    if( sum == $0 )
        return "";
    end;

    if( is_ap == true )
        return String( sum : a );
    end;

    return String( sum );
end;

macro PrintFooter( turn )
    file fin( "fininstr" );
    var SumPrecision = 0;

    if( prevFIID > ALLFININSTR )
        fin.FIID = prevFIID;
        if( GetEQ( fin ) )
            SumPrecision = fin.SumPrecision;
        end;
    end;

    if( turn )
        DP_AddPrintCell( Rep, "Итого:", 0, 0, "c:" + RepFontStyleTitel );
        DP_AddPrintCell( Rep, SumDebet, 0, DP_GetPrecision( SumDebet, SumPrecision ), "r:" + RepFontStyleTitel, is_ap );
        DP_AddPrintCell( Rep, SumCredit, 0, DP_GetPrecision( SumCredit, SumPrecision ), "r:" + RepFontStyleTitel, is_ap );
        DP_AddPrintCell( Rep, SumRestA, 0, DP_GetPrecision( SumRestA, SumPrecision ), "r:" + RepFontStyleTitel, is_ap );
        DP_AddPrintCell( Rep, SumRestP, 0, DP_GetPrecision( SumRestP, SumPrecision ), "r:" + RepFontStyleTitel, is_ap );
        DP_AddPrintCell( Rep, "", Rep.GetColWidthTable( 5 ) + Rep.GetColWidthTable( 6 ) + 1, 0, "r:" + RepFontStyleTitel );
        Rep.AddStr();
    else
        DP_AddPrintCell( Rep, "Итого:", 0, 0, "c:" + RepFontStyleTitel );
        DP_AddPrintCell( Rep, SumRestA, 0, DP_GetPrecision( SumRestA, SumPrecision ), "r:" + RepFontStyleTitel, is_ap );
        DP_AddPrintCell( Rep, SumRestP, 0, DP_GetPrecision( SumRestP, SumPrecision ), "r:" + RepFontStyleTitel, is_ap );
        DP_AddPrintCell( Rep, "", Rep.GetColWidthTable( 3 ) + Rep.GetColWidthTable( 4 ) + 1, 0, "c:" + RepFontStyleTitel );
        Rep.AddStr();
    end;

    Rep.AddEmptyStr();
    SumDebet = $0;
    SumCredit = $0;
    SumRestA = $0;
    SumRestP = $0;
end;

macro PrintReport( DataSet, rep_date, turn, null_dat, is_ap, tablewidth )
    var
        RestA = $0,
        RestP = $0,
        Debet = $0,
        Credit = $0,
        buf_str = "",
        LastCarryDate = Date( 0, 0, 0 );

    if( ( first != true ) and ( prevFIID != DataSet.Code_Currency ) )
        PrintFooter( turn );
    end;

    // заголовок - только при изменении FIID
    if( prevFIID != DataSet.Code_Currency )
        DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Сальдовая ведомость по размещению ценных бумаг", 0, 0, false, rep_date, NULL, tablewidth );
        buf_str = "Выпуск: " + DataSet.LSIN + "   Эмитент: " + DataSet.ShortName + "  ";
        DP_AddPrintCell( Rep, buf_str, StrLen( buf_str ), 0, "l:" + RepFontStyleTitel,is_ap, REP_ELEM_STR );
        DP_AddPrintCell( Rep, "Номинал: ", 9, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
        DP_AddPrintCell( Rep, SumToStr( DataSet.FaceValue : 10 : 2 : t ), 12, 0, "r:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
        DP_AddPrintCell( Rep, " (", 2, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
        DP_AddPrintCell( Rep, DataSet.FV_Code, 3, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
        DP_AddPrintCell( Rep, ")", 1, 0, "l:" + RepFontStyleTitel, is_ap, REP_ELEM_STR );
        Rep.AddStr();
    end;

    if( DataSet.Kind_Account == "А" )
        RestA = SQL_ConvTypeSum( DataSet.Rest );
        SumRestA = SumRestA + RestA;
        RestP = $0;
    else
        RestA = $0;
        RestP = SQL_ConvTypeSum( DataSet.Rest );
        SumRestP = SumRestP + RestP;
    end;

    if( turn )
        Debet = SQL_ConvTypeSum( DataSet.Debet );
        SumDebet = SumDebet + Debet;

        Credit = SQL_ConvTypeSum( DataSet.Credit );
        SumCredit = SumCredit + Credit;
    end;

    prevFIID = DataSet.Code_Currency;

    first = false;

    // печатаем строку
    GetLastCarryDate( DataSet.Chapter, DataSet.Code_Currency, DataSet.Account, LastCarryDate, rep_date );
    DP_AddPrintCell( Rep, DataSet.Account, 0, 0, "l:" + RepFontStyleTitel );
    if( turn )
        DP_AddPrintCell( Rep, Debet, 0, DP_GetPrecision( Debet, DataSet.SumPrecision ), "r:" + RepFontStyleTitel, is_ap );
        DP_AddPrintCell( Rep, Credit, 0, DP_GetPrecision( Credit, DataSet.SumPrecision ), "r:" + RepFontStyleTitel, is_ap );
    end;
    DP_AddPrintCell( Rep, RestA, 0, DP_GetPrecision( RestA, DataSet.SumPrecision ), "r:" + RepFontStyleTitel, is_ap );
    DP_AddPrintCell( Rep, RestP, 0, DP_GetPrecision( RestP, DataSet.SumPrecision ), "r:" + RepFontStyleTitel, is_ap );
    DP_AddPrintCell( Rep, LastCarryDate, 0, 0, "l:f:" + RepFontStyleTitel );
    DP_AddPrintCell( Rep, DataSet.AccName, 0, 0, "c:w:" + RepFontStyleTitel );
    Rep.AddStr();
end;

macro saldofile(
    dprt_num : integer,
    rep_date : date,
    emi_id : integer,
    fi_id : integer,
    turn : bool,
    null_dat : bool,
    ToExcel : bool,
    _is_ap : bool
)

    var query = "";
    var DataSet;
    var stat = 0;
    var count = 0,
        i = 0;

    Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
    DP_BeginProtocol(); // начать протоколирование - подмена MsgBox

    is_ap = _is_ap;

    if( turn )
        HeadTable =
            "┌──────────────────────────┬──────────┬──────────┬───────────────┬───────────────┬──────────┬────────────────────┐\n" +
            "│       Лицевой счет       │   Дебет  │  Кредит  │     Актив     │    Пассив     │   Дата   │   Название счета   │\n" +
            "├──────────────────────────┼──────────┼──────────┼───────────────┼───────────────┼──────────┼────────────────────┤";
    else
        HeadTable =
            "┌──────────────────────────┬───────────────┬───────────────┬──────────┬────────────────────┐\n" +
            "│       Лицевой счет       │     Актив     │    Пассив     │   Дата   │   Название счета   │\n" +
            "├──────────────────────────┼───────────────┼───────────────┼──────────┼────────────────────┤";
    end;

    var tablewidth = Index( HeadTable, "\n" );

    Rep.Init( HeadTable );

    // делаем выборку по л/с
    query =
        " SELECT "
          + " acc.t_Account "
         + " ,acc.t_Kind_Account "
         + " ,acc.t_Code_Currency "
         + " ,acc.t_NameAccount "
         + " ,acc.t_Chapter "
         + " ,fin.t_FaceValue "
         + " ,fin.t_CCY "
         + " ,fin.t_SumPrecision "
         + " ,iss.t_LSIN "
         + " ,acnt.t_Name AS AccName "
         + " ,pt.t_ShortName "
         + " ,vf.t_CCY AS FV_Code "
         + " ,ABS( rsb_account.RestAC( acc.t_Account, acc.t_Code_Currency, " + GetSQLDate( rep_date ) + ", acc.t_Chapter, null ) ) AS Rest ";

    if( turn )
        query = query +
           " ,rsb_account.KreditAC( acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, " + GetSQLDate( rep_date ) + ", " + GetSQLDate( rep_date ) + " ) AS Credit "
         + " ,rsb_account.DebetAC( acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, " + GetSQLDate( rep_date ) + ", " + GetSQLDate( rep_date ) + " ) AS Debet ";
    end;

    query = query +
        " FROM "
          + " daccount_dbt acc "
         + " ,dfininstr_dbt fin "
         + " ,davrkinds_dbt avr "
         + " ,davoiriss_dbt iss "
         + " ,ddepoacnt_dbt acnt "
         + " ,dparty_dbt pt "
         + " ,dfininstr_dbt vf "
      + " WHERE "
          + " acc.t_Chapter = " + Depo_chapt + " "
      + " AND acc.t_Open_Close = CHR( 0 ) "
      + " AND fin.t_FIID = acc.t_Code_Currency "
      + " AND fin.t_Fi_Kind = " + FIKIND_AVOIRISS
      + " AND avr.t_AvoirKind = fin.t_AvoirKind "
      + " AND avr.t_Fi_Kind = " + FIKIND_AVOIRISS
      + " AND avr.t_IsIndividual <> 'X' " // только неиндивидуальные
      + " AND iss.t_FIID = fin.t_FIID "
      + " AND acnt.t_AutoKey = acc.t_DepoAcc "
      + " AND pt.t_PartyID = fin.t_Issuer "
      + " AND vf.t_FIID = fin.t_FaceValueFi ";

    if( fi_id > ALLFININSTR )
        query = query +
        " AND acc.t_Code_Currency = " + fi_id + " ";
    end;

    if( emi_id > 0 )
        query = query +
        " AND fin.t_Issuer = " + emi_id + " ";

        if( not CheckTaxDepositoryMode() )
            query = query +
        " AND iss.t_SPIsClosed <> 'X' ";
        else
            query = query +
        " AND fin.t_IsClosed <> 'X' ";
        end;
    end;

    if( dprt_num > 0 )
        query = query +
        " AND acc.t_Department = " + dprt_num + " ";
    end;

    // проверка оборотов
    if( null_dat == false )
        query = query +
        " AND ( ABS( rsb_account.RestAC( acc.t_Account, acc.t_Code_Currency, " + GetSQLDate( rep_date ) + ", acc.t_Chapter, null ) ) > 0 "
         + " OR ABS( rsb_account.KreditAC( acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, " + GetSQLDate( rep_date ) + ", " + GetSQLDate( rep_date ) + " ) ) > 0 "
         + " OR ABS( rsb_account.DebetAC( acc.t_Account, acc.t_Chapter, acc.t_Code_Currency, " + GetSQLDate( rep_date ) + ", " + GetSQLDate( rep_date ) + " ) ) > 0 )";
    end;

    query = query +
        " ORDER BY "
          + " acc.t_Code_Currency "
         + " ,acc.t_Account ";

    DataSet = TRsbDataSet( query, RSDVAL_CLIENT, RSDVAL_STATIC );
    DataSet.MoveLast();

    count = DataSet.GetRecCount();

    if( count <= 0 )
        if( not DP_ProtocolIsEmpty() )
            DP_AddPrintCell( Rep, "На дату отчета нет данных соответствующих указанным параметрам", 65, 0, "l:" + RepFontStyleTitel, false, REP_ELEM_STR );
            Rep.AddStr();

            DP_AddProtocol( Rep, "protocol" ); // добавим протокол к уже сформированному отчету
            DP_EndProtocol(); // закончить протоколирование

            if( ToExcel )
                Rep.PrintWinRep();
                Rep.ShowWinRep();
            else
                Rep.PrintRep();
            end;
        else
            DP_EndProtocol(); // закончить протоколирование
        end;

        MsgBox( "На дату отчета нет данных соответствующих указанным параметрам" );
        return 1;
    end;

    InitProgress( count, "Выполнение отчета", "Формирование сальдовой ведомости" );

    stat = DataSet.MoveFirst();
    while( stat )
        PrintReport ( DataSet, rep_date, turn, null_dat, is_ap, tablewidth );
        stat = DataSet.MoveNext();
        UseProgress( i = i + 1 );
    end;

    if( count )
        PrintFooter( turn );
        DP_StdFooter_Ex( Rep, RepFontStyleTitel, NULL, NULL, tablewidth );

        DP_AddProtocol( Rep, "protocol" ); // добавим протокол к уже сформированному отчету
        DP_EndProtocol(); // закончить протоколирование

        if( ToExcel )
            Rep.PrintWinRep();
            Rep.ShowWinRep();
        else
            Rep.PrintRep();
        end;
    else
        DP_EndProtocol(); // закончить протоколирование
    end;

    RemProgress();
end;
