/*
$Name:        ClientPlanPos_Report.mac
$Module:      Ценные бумаги
$Description: Базовый стандарт - Отчет о плановых позициях клиента. Подготовка отчета
*/
import "ClientPlanPos_Form.mac", "sprepfun.mac", "sqlconv.mac", "CbReport_h.mac", "scsrvrepfun.mac", "func_lib.mac";
import RsbFormsInter, Global;
import or_rep_h;
import or_tpl_h;
import or_const;

var printEngine, csvFile, csvTable, fName;


PRIVATE CLASS (DL_CReportTemplate) TRepClPlanPos_Report(RepDate:date)
  private var m_RepDate = RepDate;

  PRIVATE MACRO addHeader()
    printEngine.SetValue_NameCell("templateDate", m_RepDate);
    printEngine.CopyAllSheetInTotalBook(null, false, "templateHeader", 0, "Отчет о план-х позициях клиента");
  END;

  PRIVATE MACRO initRep()
    printEngine = CTemplateSXLSX(NULL);
    if(printEngine.UsePoi())
       printEngine.SetXLSXFormat();
    end;
    printEngine.CreateTotalBook();
    InitProgress(-1, "Esc отмена", "Обработка"); 
    printEngine.OpenTemplate("Report_ClPlanPos.xlsx", true);
    printEngine.SheetChange(1);
    csvTable = printEngine.RegisterTable("templateTable", "templateTableHeader", true);
    csvFile = C_CSVFile();
    if (printEngine.UsePoi())
        csvFile.SetLimitRow(FLUSH_COUNT_XLSX);
    end;
    fName = csvFile.CreateFullFileName("Report_ClPlanPos_csv.txt");
    csvFile.FileOpen(fName, true);
    addHeader();
  END;

  PRIVATE MACRO showRep()
  	csvFile.FileClose();
    csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
    csvTable.EndTable();
    printEngine.CopyAllSheetInTotalBook(null, false, csvTable.GetTableRange(), 0, "Отчет о план-х позициях клиента");
    printEngine.Close();
    var  day, mon, year;
    var hour, min, sec;
    datesplit( {curdate}, day, mon, year);
    TimeSplit ( Time(), hour, min, sec );
    printEngine.SaveTotalBook("Report_ClPlanPos_" + year + mon + day + hour + min + sec);
    remProgress ();
  END;

  PRIVATE MACRO ПечатьБлокаДанных(DataSet, cnt);
    var i = 0;
    InitProgress(cnt, "Подготовка отчета", "Подготовка отчета");
    while(DataSet.movenext)
      csvFile.AddCell(DataSet.CFT_ID);  
      csvFile.AddCell(DataSet.Number);
      csvFile.AddCell(date(DataSet.DlcDateConc)); 
      csvFile.AddCell(IIF(DataSet.TKS_Number_1 != "", DataSet.TKS_Number_1, DataSet.TKS_Number_2));
      csvFile.AddCell(date(DataSet.SfDateConc));
      csvFile.AddCell(date(DataSet.ApplDate)); 
      csvFile.AddCell(IIF(DataSet.IsOpenPos == 0, "Нет", "Да")); 
      csvFile.AddCell(date(DataSet.Maturity));
      csvFile.AddCell(IIF(DataSet.IsRestAvr == 0, "Нет", "Да"));
      csvFile.AddCell(DataSet.RiskLevelName);
      csvFile.AddRow();      
      UseProgress(i = i + 1); 
    end;
  END;

  MACRO Run()
    var query, cmd, DataSet;
    var cnt = 0;
    initRep();
    
    query =   " select sf_dlc.t_Number, sf_dlc.t_DateConc as DlcDateConc, "
            + "        sf.t_DateConc as SfDateConc, "
            + "        AtCor.t_ValidFromDate as ApplDate, "
            + "        rsb_secur.SC_GetObjCodeOnDate("+OBJTYPE_PARTY+","+PARTY_CODEKIND_ABS+", sf.t_PartyID) as CFT_ID, "
            + "        NVL(rsb_struct.getString(rsi_rsb_kernel.GetNote(" + OBJTYPE_SFCONTR + ", LPAD(sf.t_ID, 10, '0'), (CASE WHEN mp.t_MarketID = "+MMVB_ID()+" THEN 5 ELSE 10 END), "+GetSQLDate(date(31,12,9999))+")), CHR(1)) as TKS_Number_1, "
            + "        NVL((SELECT lm.t_DepoAcc "
            + "               FROM ddl_limitprm_dbt lm "
            + "              WHERE lm.t_MarketKind = 1 "
            + "                AND lm.t_MarketID = mp.t_MarketID "
            + "                AND lm.t_ImplKind = 1 "
            + "            ), CHR(1)) as TKS_Number_2, "
            + "        NVL((select 1 "
            + "               from ddl_tick_dbt tk "
            + "              where tk.t_BOfficeKind = " + DL_SECURITYDOC
            + "                and tk.t_ClientID = sf.t_PartyID "
            + "                and tk.t_ClientContrID = sf.t_ID "
            + "                and tk.t_DealStatus = " + DL_READIED
            + "                and ROWNUM = 1 " 
            + "             union "
            + "             select /*+ index(tk DDL_TICK_DBT_IDX_U2)*/ 1 "
            + "               from ddl_tick_dbt tk "
            + "              where tk.t_BOfficeKind = " + DL_SECURITYDOC
            + "                and tk.t_IsPartyClient = 'X' "
            + "                and tk.t_PartyID = sf.t_PartyID "
            + "                and tk.t_PartyContrID = sf.t_ID "
            + "                and tk.t_DealStatus = " + DL_READIED
            + "                and ROWNUM = 1 "
            + "            ), 0) as IsOpenPos, "
            + "        NVL((select MAX(q.Maturity) "
            + "               from (select MAX(leg.t_Maturity) as Maturity "
            + "                       from ddl_tick_dbt tk, ddl_leg_dbt leg "
            + "                      where tk.t_BOfficeKind = " + DL_SECURITYDOC
            + "                        and tk.t_ClientID = sf.t_PartyID "
            + "                        and tk.t_ClientContrID = sf.t_ID "
            + "                        and tk.t_DealStatus = " + DL_READIED
            + "                        and leg.t_DealID = tk.t_DealID "
            + "                        and leg.t_LegID = 0 "
            + "                        and leg.t_LegKind in ("+LEG_KIND_DL_TICK+","+LEG_KIND_DL_TICK_BACK+") "
            + "                      union "
            + "                     select /*+ index(tk DDL_TICK_DBT_IDX_U2)*/ MAX(leg.t_Maturity) as Maturity"
            + "                       from ddl_tick_dbt tk, ddl_leg_dbt leg "
            + "                      where tk.t_BOfficeKind = " + DL_SECURITYDOC
            + "                        and tk.t_IsPartyClient = 'X' "
            + "                        and tk.t_PartyID = sf.t_PartyID "
            + "                        and tk.t_PartyContrID = sf.t_ID "
            + "                        and tk.t_DealStatus = " + DL_READIED
            + "                        and leg.t_DealID = tk.t_DealID "
            + "                        and leg.t_LegID = 0 "
            + "                        and leg.t_LegKind in ("+LEG_KIND_DL_TICK+","+LEG_KIND_DL_TICK_BACK+") "
            + "                    ) q "
            + "            ), "+GetSQLDate(date(0,0,0))+") as Maturity, "
            + "            rsb_secur.GetObjAttrName("+OBJTYPE_BROKERCONTR_DL+", 103, rsb_secur.GetMainObjAttrNoDate("+OBJTYPE_BROKERCONTR_DL+", LPAD(dlc.t_DlContrID, 34, '0'), 103)) as RiskLevelName, "
            + "        NVL((select 1 "
            + "               from dmccateg_dbt cat, dmcaccdoc_dbt mcacc "
            + "              where cat.t_LevelType = 1 "
            + "                and cat.t_Code = 'ЦБ Клиента, ВУ' "
            + "                and mcacc.t_CatID = cat.t_ID "
            + "                and mcacc.t_Owner = sf.t_PartyID "
            + "                and mcacc.t_ClientContrID = sf.t_ID "  
            + "                and rsb_account.restac(mcacc.t_Account, mcacc.t_Currency, "+GetSQLDate(date(31,12,9999))+", mcacc.t_Chapter, null) <> 0 "
            + "                and ROWNUM = 1 "
            + "            ), 0) as IsRestAvr "
            + "   from dsfcontr_dbt sf "
            + "        inner join ddlcontrmp_dbt mp on mp.t_SfContrID = sf.t_ID "
            + "        inner join ddlcontr_dbt dlc on dlc.t_DlContrID = mp.t_DlContrID "
            + "        inner join dsfcontr_dbt sf_dlc on sf_dlc.t_ID = dlc.t_SfContrID "
            + "        left  join dobjatcor_dbt AtCor on     AtCor.t_ObjectType  = " + OBJTYPE_SFCONTR
            + "                                          and AtCor.t_GroupID     = 6 " //Предоставлять брокеру право использования ценных бумаг в его интересах
            + "                                          and AtCor.t_Object      = LPAD(sf.t_ID, 10, '0') " 
            + "                                          and AtCor.t_ValidFromDate = (select MAX(t.t_ValidFromDate) "
            + "                                                                         from dobjatcor_dbt t "
            + "                                                                        where t.t_ObjectType     = " + OBJTYPE_SFCONTR
            + "                                                                          and t.t_GroupID        = 6 "
            + "                                                                          and t.t_Object         = LPAD(sf.t_ID, 10, '0') "
            + "                                                                      ) "
            + "        left  join dobjatcor_dbt AtCor1 on    AtCor1.t_ObjectType  = " + OBJTYPE_SFCONTR
            + "                                          and AtCor1.t_GroupID     = 7 " //Перевод активов на новый номер ТКС произведен
            + "                                          and AtCor1.t_Object      = LPAD(sf.t_ID, 10, '0') " 
            + "                                          and AtCor1.t_ValidFromDate = (select MAX(t2.t_ValidFromDate) "
            + "                                                                          from dobjatcor_dbt t2 "
            + "                                                                         where t2.t_ObjectType     = " + OBJTYPE_SFCONTR
            + "                                                                           and t2.t_GroupID        = 7 "
            + "                                                                           and t2.t_Object         = LPAD(sf.t_ID, 10, '0') "
            + "                                                                       ) "
            + "  where sf.t_ServKind = " + PTSK_STOCKDL
            + "    and sf.t_ServKindSub = 8 " //Биржевой рынок
            + "    and ((AtCor.t_AttrID = 2 /*Нет*/ and AtCor1.t_AttrID = 2 /*Нет*/) or (AtCor.t_AttrID = 1 /*Да*/ and AtCor1.t_AttrID = 1 /*Да*/))";

    cmd = DL_RSDCommand(query);
    cnt = cmd.GetCount();

    if(cnt > 0)
      DataSet = cmd.Execute();
      ПечатьБлокаДанных(DataSet, cnt);
      RemProgress();
      showRep();
    end;
  END;
END;


PRIVATE MACRO ReportRun()
  
  var Form = TRepClPlanPos_Panel();

  while(Form.Run())
    TRepClPlanPos_Report(Form.m_RepDate).Run();    
  end;
END;


ReportRun();
exit(1);