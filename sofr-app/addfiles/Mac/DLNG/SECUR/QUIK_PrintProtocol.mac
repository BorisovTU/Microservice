/*                           
$Name:        QUIK_PrintProtocol.mac
$Module:      Ценные бумаги
$Description: Печать протокола выполнения сверки и расчета
*/
import BankInter, XmlRpcInter, "dllog_xml.mac", "DlReport_h.mac", "dldlngfun.mac";


private var N1RepHeaderStr = " ПРОТОКОЛ РАСЧЕТА ЛИМИТОВ QUIK\n" +
                             " Биржа: ###################################\n" +
                             " Дата расчета: ##########\n" +
                             " \n";

private var N1Time = " \n Выполнялся запуск расчета: время начала ######## время окончания ########\n";

private var N1TableHeader = "┌────────────────────┬────────────────┬───────────────────┬───────────────┬─────────────────────────┬────────────────────┐\n"+
                            "│ Выполнен расчет    │ Расcчитано ли- │ по денежным сред- │ по финансовым │    Выявлены ошибки:     │ Результат обработ- │\n"+
                            "│ лимитов по рынкам: │ митов всего:   │ ствам:            │ инструментам: │                         │ ки ошибок:         │\n"
                            "├────────────────────┼────────────────┼───────────────────┼───────────────┼─────────────────────────┼────────────────────┤";

private var N2Time = " \n Выполнялся запуск сверки: время начала ######## время окончания ########\n";

private var N2TableHeader = "┌────────────────┬─────────────────────┬─────────────────┬─────────────────────────┬────────────────────┐\n"+
                            "│  Загружено ли- │ Рассчитано          │ Найдено         │    Выявлены ошибки:     │ Результат обработ- │\n"+
                            "│ митов из QUIK: │ системой:           │ расхождений:    │                         │ ки ошибок:         │\n"
                            "├────────────────┼─────────────────────┼─────────────────┼─────────────────────────┼────────────────────┤";


//класс с данными для строки протокола
class QUIK_DataLogParm(_MarketKind, _CalcTotal, _CalcCash, _CalcSecurities, _Error, _HandErrResult)
  var MarketKind      = _MarketKind;
  var CalcTotal       = _CalcTotal;
  var CalcCash        = _CalcCash;
  var CalcSecurities  = _CalcSecurities;
  var Error           = _Error;
  var HandErrResult   = _HandErrResult;
end;

/*лог данных*/
PRIVATE CLASS (DL_LOG_FROM_XML) QUIK_LOG_DATA_FROM_XML(pDocKind:integer, pID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()
     var ProtocolData:QUIK_DataLogParm;

     if( m_ReportVersion == 1 )

        if(DL_ReadTagAttribut(m_child_ReportNumber, "MARKETKIND", V_STRING, ProtocolData.MarketKind) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CALCTOTAL", V_INTEGER, ProtocolData.CalcTotal) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CALCCASH", V_INTEGER, ProtocolData.CalcCash) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "CALCSECURITIES", V_INTEGER, ProtocolData.CalcSecurities) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERROR", V_STRING, ProtocolData.Error) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "HANDERRRESULT", V_STRING, ProtocolData.HandErrResult) == false)
        end;
        
        return ProtocolData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pID, pType, pOnlyLastReport);

END;

PRIVATE MACRO GetLogDataXML( DocKind:integer, ID:integer, Type:integer, OnlyLastReport:bool )
  var xml_obj = null;

  if( Type == LOG_TYPE_DATA )
     xml_obj = QUIK_LOG_DATA_FROM_XML(DocKind, ID, Type, OnlyLastReport);
  end;

  return xml_obj;
END;

PRIVATE MACRO GetMarketCode(MarketID)
  var MarketCode = "";
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  partcode.Clear();
  partcode.rec.PartyID  = MarketID;
  partcode.rec.CodeKind = PTCK_CONTR;
  if(partcode.GetEQ())
    MarketCode = partcode.rec.Code;
  end;
  return MarketCode;
END;

PRIVATE CLASS (DL_CReportTemplate) QUIK_Protocol( limithist, Mode)
  PRIVATE VAR m_PrintMode;
  PRIVATE VAR m_limithist = TRecHandler("dl_limithist");
  private var SheetName = "Протокол расчета лимитов";   

  PRIVATE MACRO PrintMode():INTEGER
     return m_PrintMode;
  END;
  
  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    CreateTotalBook();
  END;

  PRIVATE MACRO EndReport()
    SaveTotalBook();
  END;
  
  private macro PrintProtocolLinesCalc( ReportLineData_XML:variant)
    var ProtocolData:QUIK_DataLogParm;
    var IsFirst = true;
    while ( ReportLineData_XML.Next )
        ProtocolData = ReportLineData_XML.GetReportLineData();
        if(ProtocolData.MarketKind != " ")
          if ( IsFirst )
            PrintFormatString(N1Time,
                     "N1_CalcTimeFrom", m_limithist.rec.CalcTimeFrom,
                     "N1_CalcTimeTo",   m_limithist.rec.CalcTimeTo
                     );
            CopyAllSheetInTotalBook(SheetName, false, "N1_Time", 0, SheetName);
            CopyAllSheetInTotalBook(SheetName, false, "N1_TableHeader", 1, SheetName);

            RegisterTable( "N1_MainTable", N1TableHeader,
                          "N1_MarketKind",
                          "N1_CalcTotal",
                          "N1_CalcCash",
                          "N1_CalcSecurities",
                          "N1_Error",
                          "N1_HandErrResult"
                          );            

              IsFirst = false;
          end;
        
          PrintTableLine( "N1_MarketKind",      ProtocolData.MarketKind,      null,
                          "N1_CalcTotal",       ProtocolData.CalcTotal,       null,
                          "N1_CalcCash",        ProtocolData.CalcCash,        null,
                          "N1_CalcSecurities",  ProtocolData.CalcSecurities,  null,
                          "N1_Error",           ProtocolData.Error,           null,
                          "N1_HandErrResult",   ProtocolData.HandErrResult,   null 
                        );
        end;
    end;
    
    if ( not IsFirst )
       EndTable();
       CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);
    end;

    return (not IsFirst);
  end;

    private macro PrintProtocolLinesRecon( ReportLineData_XML:variant)
    var ProtocolData:QUIK_DataLogParm;
    var IsFirst = true;
    while ( ReportLineData_XML.Next )
        ProtocolData = ReportLineData_XML.GetReportLineData();
        if(ProtocolData.MarketKind == " ")
          if ( IsFirst )
            PrintFormatString(N2Time,
                     "N2_ReconTimeFrom", m_limithist.rec.ReconTimeFrom,
                     "N2_ReconTimeTo",   m_limithist.rec.ReconTimeTo
                     );
            CopyAllSheetInTotalBook(SheetName, false, "N2_Time", 0, SheetName);
            CopyAllSheetInTotalBook(SheetName, false, "N2_TableHeader", 1, SheetName);

            RegisterTable( "N2_MainTable", N2TableHeader,
                          "N2_QUIKReload",
                          "N2_TotalCalc",
                          "N2_Diverg",
                          "N2_Error",
                          "N2_HandErrResult"
                          );            

              IsFirst = false;
          end;
        
          PrintTableLine( "N2_QUIKReload",      ProtocolData.CalcTotal,       null,
                          "N2_TotalCalc",       ProtocolData.CalcCash,        null,
                          "N2_Diverg",          ProtocolData.CalcSecurities,  null,
                          "N1_Error",           ProtocolData.Error,           null,
                          "N1_HandErrResult",   ProtocolData.HandErrResult,   null 
                        );
        end;
    end;
    
    if ( not IsFirst )
       EndTable();
       CopyAllSheetInTotalBook(SheetName, false, GetTableRange() , 0, SheetName);
    end;

    return (not IsFirst);
  end;

  PRIVATE MACRO Create()
    var ProtocolLineData_XML:variant;

    SetActiveSheet(SheetName);
    PrintFormatString(N1RepHeaderStr,
                     "N1_MarketCode",   GetMarketCode(m_limithist.rec.MarketID),
                     "N1_CalcDate",     m_limithist.rec.CalcDate
                     );
    CopyAllSheetInTotalBook(SheetName, false, "N1_RepHead", 0, SheetName);

    ProtocolLineData_XML = GetLogDataXML(m_limithist.rec.DocKind, m_limithist.rec.ID, LOG_TYPE_DATA, false);
    ProtocolLineData_XML.SetReportNumber(0);
    PrintProtocolLinesCalc(ProtocolLineData_XML);
    ProtocolLineData_XML.Rewind();
    PrintProtocolLinesRecon(ProtocolLineData_XML);
  END;

  m_PrintMode = Mode;
  m_limithist = limithist;
    
  initDL_CReportTemplate( NULL, "Report_LimitProtocol.xls", true, null, null, false);
END;

private MACRO PrintQUIKProtocolByXML(lh, Mode)
  record limithist("dl_limithist");
  var rec_limithist = TRecHandler("dl_limithist");
  var file_lh = TBFile( "dl_limithist.dbt",  "R", 0 );

  if(Valtype(lh) == V_INTEGER)
    file_lh.Clear();
    file_lh.rec.ID  = lh;
    file_lh.GetEQ();
    copy(rec_limithist, file_lh);
  else
    SetBuff(limithist, lh);
    copy(rec_limithist, limithist);
  end;
    
  QUIK_Protocol(rec_limithist, Mode).Run();
  return true;

END;

MACRO PrintQUIKProtocolByXMLToExcel(lh)
  return PrintQUIKProtocolByXML(lh, DL_OUTREPORT_EXCEL);
END;

MACRO PrintQUIKProtocolByXMLToSTD(lh)
    return PrintQUIKProtocolByXML(lh, DL_OUTREPORT_STD);
END;