/* 
$Name:             ws_txdeferred_taxes.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Макрос сервисов редактируемых скроллингов отложенных налогов
*/
/*
  Fatkov E.A.
  17.09.2014
*/
import FIInter;
import "ws_orderbygen.mac";
import "ws_datafilter.mac";
import "mccateg_widget.mac";
import "cb_sql.mac", "ws_common.mac", "sp_class.mac";


/* класс Объектов НОБ*/
class CTaxObjKinds
  var Number:Integer;      /* Идентификатор записи */
  var Code:String;         /* Вид справочника */
  var Comment:String;      /* Дата начала действия */
  var IsSystem:Bool;        /* Значение */
  var IsSystemReadOnly:Bool;
end;

/* класс Настроечная таблица*/
class CTaxObjSet
  var ID:Integer;           /* Идентификатор записи */
  var ObjKind:Integer;      /* Вид НОБ */
  var CatNum:Integer;       /* Категория учета */
  var Mask:String;          /* Маска счета */
end;

/* класс списка для заполнения настроечной таблицы*/
class CTaxObjSetList
  var TaxesBase:CTaxObjKinds;   /* Идентификатор записи */
  var ObjKindsList = TArray();  /* Массив CTaxObjKinds*/
  var TaxObjSet:CTaxObjSet;     /* Вид справочника */
  var CatCode:String;           /* Дата начала действия */
  var CatCodeObj:TWebMCCateg;   /* Категория учета*/ 

  TaxesBase = CTaxObjKinds();
  TaxObjSet = CTaxObjSet();
  CatCodeObj = TWebMCCateg();  
  CatCode = "";
end;

private const BEdupkey = 5;
private const ExistRecordInObjSet = 31883;
private const MCCATEG_LEVEL_TYPE_CATEGORY = 1;
private const INCORRECTMASK = 9020;

macro TxDeferredTaxesRunError( err, stat:integer )
  RunError( "Макрос: ws_txdeferred_taxes.mac, Строка: " + err.line + ", Сообщение: " + err.message, stat );
end;

/*Проверка значение маски*/
macro TxCheckAccountMask (mask: String)
  var stat:Integer = -1;
  var isCorrectMask = false;

  if(( mask != null ) and (ValType( mask ) != V_UNDEF) and (mask != "") )
    isCorrectMask = Web_StringIsMaskEx( mask, CSMFL_Mask );
  end;

  return isCorrectMask;

OnError( err )
  TxDeferredTaxesRunError(err, stat);
end;

/*Выборка скроллинга Настроечная таблица*/
private macro GetQueryTxObjSetList()
  return string("	  SELECT TaxObjKind.t_code,	"+
                                "	         TaxObjKind.t_Num,	"+
                                "	         TaxObjKind.t_Comment,	"+
                                "	         TaxObjKind.t_System,	"+
                                "	         categ.t_code AS t_CatCode,	"+
                                "	         categ.t_Number AS t_CatNumber,	"+
                                "	         categ.t_LevelType,	"+
                                "	         categ.t_Id AS t_CatId,	"+
                                "	         TaxObjSet.t_mask,	"+
                                "	         TaxObjSet.t_ID,	"+
                                "	         TaxObjSet.t_ObjKind,	"+
                                "	         TaxObjSet.t_CatNum	"+
                                "	    FROM DTAXOBJSET_DBT TaxObjSet,	"+
                                "	         DTAXOBJKIND_DBT TaxObjKind,	"+
                                "	         dmccateg_dbt categ	"+
                                "	   WHERE TaxObjKind.t_Num = TaxObjSet.t_ObjKind	"+
                                "	     AND categ.t_Number = TaxObjSet.t_CatNum	"+
                                "	     AND categ.t_LevelType = " + string(MCCATEG_LEVEL_TYPE_CATEGORY) + " "); 
end;

/* отбор данных для скроллинга НАСТРОЕЧНАЯ ТАБЛИЦА*/
/* можно перенести в  ws_datafilter.mac для универсальности.*/
private macro GetSQLTxObjKindConditionEx(
  table:String
 ,field:String
 ,condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond:String = "";
  var valueCount:Integer = 0;

  if( ( ValType( field ) == V_STRING )
  and ( field != "" )
  and ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].Number ) == V_INTEGER ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        if( ( ValType( table ) == V_STRING )
        and ( table != "" ) )
          strSQLCond = strSQLCond + " " + table + "." + field + " ";
        else
          strSQLCond = strSQLCond + " " + field + " ";
        end;

        strSQLCond = strSQLCond + " = " + String( value[valueCount].Number ) + " ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;

  return strSQLCond;
end;

private macro GetSQLObjCatCodeConditionEx(
  table:String
 ,field:String
 ,condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String
  var strSQLCond:String = "";
  var valueCount:Integer = 0;

  if( ( ValType( field ) == V_STRING )
  and ( field != "" )
  and ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].Id ) == V_INTEGER ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        if( ( ValType( table ) == V_STRING )
        and ( table != "" ) )
          strSQLCond = strSQLCond + " " + table + "." + field + " ";
        else
          strSQLCond = strSQLCond + " " + field + " ";
        end;

        strSQLCond = strSQLCond + " = " + String( value[valueCount].Id ) + " ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;

  return strSQLCond;
end;


private macro GetSQLObjNumberCondition(
  condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String

  var strSQLCond : String = GetSQLTxObjKindConditionEx( "TaxObjKind", "t_Num", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " TaxObjKind.t_Num = 0 ";
  end;

  return strSQLCond;
end;


private macro GetSQLObjCatCodeCondition(
  condition:Integer
 ,value/*:TArray of <type>*/
 ,type:Integer
):String

  var strSQLCond : String = GetSQLObjCatCodeConditionEx( "categ", "t_Id", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " categ.t_Id = 0 ";
  end;

  return strSQLCond;
end;

private macro GetTxObjSetListAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String

  var fp = FilterParser( FilterItems );

  fp.AddUserFieldCondition( 0, @GetSQLObjNumberCondition );  // Вид НОБ
  fp.AddUserFieldCondition( 1, @GetSQLObjCatCodeCondition );  // Категория учета

  //fp.GetFullSQLConditionDebug( "ws_txdeferred_taxes_debug" );


  return fp.GetFullSQLCondition();
end;

/*Скроллинг "Настроечная табилца"*/
macro GetTxObjSetList(
  PageSize:integer // Размер страницы 
 ,PageNum:integer  // Номер страницы
 ,FilterItems      //: TArray of FilterConditionItem  // Значения фильтрации
 ,OrderItems)       //: TArray of OrderItem  // Параметры сортировки)
  var result = TArray();
  var stat:integer = -1;
  var TaxObjSetList;

  var query =  GetQueryTxObjSetList();

  /*Фильтры*/
  var strAddWhere = GetTxObjSetListAddWhereCondition( FilterItems );
  if( strAddWhere != "" )
    query = query + " AND " + strAddWhere;
  end;

  query = query + " ORDER BY " + SP_GetSortStr(OrderItems, " TaxObjSet.t_ObjKind asc, TaxObjSet.t_CatNum ASC ");
  query = SQL_WrapSelect(query, PageNum, PageSize);

    
  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  while( ds.MoveNext() )
     TaxObjSetList = CTaxObjSetList();

     TaxObjSetList.TaxesBase.Number = SQL_ConvTypeInteger(ds.Num);
     TaxObjSetList.TaxesBase.Code = SQL_ConvTypeStr(ds.Code);
     TaxObjSetList.TaxesBase.Comment  = SQL_ConvTypeStr(ds.Comment);
     TaxObjSetList.TaxesBase.IsSystem = ( SQL_ConvTypeStr( ds.system ) == SET_CHR );

     TaxObjSetList.CatCode = SQL_ConvTypeStr(ds.CatCode);

     TaxObjSetList.TaxObjSet.ID = SQL_ConvTypeInteger(ds.ID);
     TaxObjSetList.TaxObjSet.ObjKind = SQL_ConvTypeInteger(ds.ObjKind);
     TaxObjSetList.TaxObjSet.CatNum = SQL_ConvTypeInteger(ds.CatNum);
     TaxObjSetList.TaxObjSet.Mask = SQL_ConvTypeStr(ds.Mask);

     TaxObjSetList.ObjKindsList[0] = TaxObjSetList.TaxesBase;

     TaxObjSetList.CatCodeObj = WebCore_CreateTWebMCCateg( SQL_ConvTypeInteger(ds.CatId),
                                                           SQL_ConvTypeInteger(ds.CatNumber),
                                                           SQL_ConvTypeStr(ds.CatCode),
                                                           SQL_ConvTypeInteger(ds.LevelType)
                                                          );

     result.value(result.Size) = TaxObjSetList;
  end;

  return result;

OnError( err )

  TxDeferredTaxesRunError(err, stat);
end;            

/*Получить общее количество записей скроллинга Настроечная таблица*/
macro GetTxObjSetListCount(
  FilterItems//:TArray of FilterCondItem // Массив элементов фильтра скроллинга
):Integer

  var result:integer = 0;
  var stat:integer = -1;
  var strAddWhere:string = "";

  var query = GetQueryTxObjSetList(); 

  strAddWhere = GetTxObjSetListAddWhereCondition( FilterItems );
  if( strAddWhere != "" )
     query = query + " AND " + strAddWhere;
  end;

  query = " SELECT COUNT(1) RecCount FROM " +
          "  ( " +  string (query) + " ) ";

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if( ds.MoveNext() )
     result = SQL_ConvTypeInteger(ds.RecCount);
  end;

  return result;

OnError( err )
  TxDeferredTaxesRunError(err, stat);
end;

private macro TxCheckTaxobjsetDupkey( Buf /* :CTaxObjSetList Буфер записи */):integer

  var stat = 0;
  var sql, query, ds;
  query = RSDCommand(" SELECT t_ObjKind " +
                     "   FROM dtaxobjset_dbt " +
                     "  WHERE t_objkind = ? AND t_CatNum = ? AND t_Id <> ?" );

  query.addParam( "", RSDBP_IN, Buf.TaxObjSet.ObjKind );
  query.addParam( "", RSDBP_IN, Buf.CatCodeObj.Number );
  query.addParam( "", RSDBP_IN, Buf.TaxObjSet.ID );

  query.execute();
  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  if( ds.MoveNext() )
     stat = BEdupkey;
  end;

  return stat;   
end;


macro TxCheckObjSetList( Action:integer,  /* Вид действия */
                         Buf              /* :CTaxObjSetList Буфер записи */
                        ):integer
  var stat = 0;

  if( (Action == OperationKind_Insert)  or (Action == OperationKind_Update ) )
     stat = TxCheckTaxobjsetDupkey(Buf);
  end;

  if((Action != OperationKind_Delete) and (Buf.TaxObjSet.Mask != null) and (Buf.TaxObjSet.Mask != "") 
      and (not TxCheckAccountMask(Buf.TaxObjSet.Mask) ) 
    )
      stat = INCORRECTMASK; // Неверное значение поля
  end;

  return stat;   
end;

/*Сохранение данных скроллинга Настроечная таблица*/
macro TxSaveObjSetList( Action:integer,  /* Вид действия */
                        Buf              /* :CTaxObjSetList Буфер записи */
                     ):integer
  var stat = 0;
  var sql, query, ds;
  if( (Action == OperationKind_Insert)  or (Action == OperationKind_Update ) )
     stat = TxCheckTaxobjsetDupkey(Buf);
  end;

  if (not stat )
    if((Action != OperationKind_Delete) and (Buf.TaxObjSet.Mask != null) and (Buf.TaxObjSet.Mask != "") 
       and (not TxCheckAccountMask(Buf.TaxObjSet.Mask) ) 
      )
      stat = INCORRECTMASK; // Неверное значение поля
    end;

  end;

  if(not stat)
    if( (Action == OperationKind_Insert) and (Buf.TaxObjSet.ID == 0) 
         and  (Buf.CatCodeObj.Number > 0) and (Buf.TaxObjSet.ObjKind > 0) )
       sql = RSDCommand(" INSERT INTO dtaxobjset_dbt(T_CATNUM, T_MASK, T_OBJKIND ) VALUES(?, ?, ?) ");
       sql.NullConversion = true;
       sql.addParam( "", RSDBP_IN, Buf.CatCodeObj.Number );
       sql.addParam( "", RSDBP_IN, Buf.TaxObjSet.Mask );
       sql.addParam( "", RSDBP_IN, Buf.TaxObjSet.ObjKind );
       sql.execute();

    elif( Action == OperationKind_Update )
       query = RSDCommand(" SELECT t_Id " +
                          "   FROM dtaxobjset_dbt " +
                          "  WHERE t_Id = ? " );
       query.addParam( "", RSDBP_IN, Buf.TaxObjSet.ID );
       query.execute();
       ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
       if( ds.MoveNext() )
          sql = RSDCommand(" UPDATE dtaxobjset_dbt SET T_ID = ?, T_CATNUM = ?, T_MASK = ?, T_OBJKIND = ? " 
                            + "  WHERE T_ID = ?  ");
              sql.NullConversion = true;
              sql.addParam( "", RSDBP_IN, Buf.TaxObjSet.ID );
              sql.addParam( "", RSDBP_IN, Buf.CatCodeObj.Number );
              sql.addParam( "", RSDBP_IN, Buf.TaxObjSet.Mask );
              sql.addParam( "", RSDBP_IN, Buf.TaxObjSet.ObjKind );
              sql.addParam( "", RSDBP_IN, Buf.TaxObjSet.ID );
              sql.execute();
       else
          stat = 70;
          RunError("Запись конкурентно изменена");
       end;

    elif( Action == OperationKind_Delete )
       query = RSDCommand(" SELECT t_Id " +
                          "   FROM dtaxobjset_dbt " +
                          "  WHERE t_Id = ? " );
       query.addParam( "", RSDBP_IN, Buf.TaxObjSet.ID );
       query.execute();
       ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

       if( ds.MoveNext() )
          sql = RSDCommand(" DELETE FROM dtaxobjset_dbt WHERE t_Id = ? ");
          sql.addParam( "", RSDBP_IN, Buf.TaxObjSet.ID );
          sql.execute();
       else
          stat = 70;
          RunError("Запись конкурентно изменена");
       end;
    end;

  end;
  
  return stat;

OnError( err )
  TxDeferredTaxesRunError(err, stat);
end;

/* отбор данных для скроллинга ВИДЫ НОБ*/
macro GetTxObjKindsList()
  var result = TArray();
  var stat:integer = -1;
  var TxObjKinds;

  var query = RSDCommand("  SELECT objkind.t_num, "
                          +"       objkind.t_code, "
                          +"       objkind.t_comment, "
                          +"       objkind.t_system "
                          +"  FROM dTAXOBJKIND_dbt objkind Order by objkind.t_num");
  query.execute();
    
  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  while( ds.MoveNext() )
     TxObjKinds = CTaxObjKinds();
     
     TxObjKinds.Number = SQL_ConvTypeInteger(ds.Num);
     TxObjKinds.Code = SQL_ConvTypeStr(ds.Code);
     TxObjKinds.Comment  = SQL_ConvTypeStr(ds.Comment);
     TxObjKinds.IsSystem = ( SQL_ConvTypeStr( ds.system ) == SET_CHR );

     result.value(result.Size) = TxObjKinds;
  end;

  return result;

OnError( err )

  TxDeferredTaxesRunError(err, stat);
end;

/*Сохрение видов НОБ*/
macro SaveTxObjKinds( Action:integer,  /* Вид действия */
                       Buf             /* :CTaxObjKinds Буфер записи */
                     ):integer
  var stat:integer = -1;
  var sql, query, ds;

  if( (Action == OperationKind_Insert) )
     query = RSDCommand(" SELECT t_Num " +
                        "   FROM dtaxobjkind_dbt " +
                        "  WHERE t_Num = ? " );

     query.addParam( "", RSDBP_IN, Buf.Number );
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        return BEdupkey;
     end;
  end;

  if( (Action == OperationKind_Insert) and (Buf.Code != "") )
     sql = RSDCommand(" INSERT INTO dtaxobjkind_dbt(t_Num, t_Code, t_Comment, t_System ) VALUES(?, ?, ?, ?) ");
     sql.addParam( "", RSDBP_IN, Buf.Number );
     sql.addParam( "", RSDBP_IN, Buf.Code );
     sql.addParam( "", RSDBP_IN, Buf.Comment );
     sql.addParam( "", RSDBP_IN, IIF( Buf.IsSystem , SET_CHR, UNSET_CHR) );
     sql.execute();
  elif( Action == OperationKind_Update )
     query = RSDCommand(" SELECT t_Num " +
                        "   FROM dtaxobjkind_dbt " +
                        "  WHERE t_Num = ? " );
     query.addParam( "", RSDBP_IN, Buf.Number );
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        sql = RSDCommand(" UPDATE dtaxobjkind_dbt SET t_Num = ?, t_Code = ?, t_Comment = ? , t_System = ? " 
                          + "  WHERE t_Num = ?  ");
        sql.addParam( "", RSDBP_IN, Buf.Number );
        sql.addParam( "", RSDBP_IN, Buf.Code);
        sql.addParam( "", RSDBP_IN, Buf.Comment );
        sql.addParam( "", RSDBP_IN, IIF( Buf.IsSystem , SET_CHR, UNSET_CHR) );
        sql.addParam( "", RSDBP_IN, Buf.Number );
            sql.execute();
     else
        stat = 70;
        RunError("Запись конкурентно изменена");
     end;
  elif( Action == OperationKind_Delete )
     /*При удалении проверим наличие записей в настроечной таблице на основе выбранного НОБ*/
     query = RSDCommand(" SELECT t_id " +
                        "   FROM dtaxobjset_dbt " +
                        "  WHERE t_ObjKind = ? " );

     query.addParam( "", RSDBP_IN, Buf.Number );
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
     if( ds.MoveNext() )
        return ExistRecordInObjSet;
     end;

     query = RSDCommand(" SELECT t_Num " +
                        "   FROM dtaxobjkind_dbt " +
                        "  WHERE t_Num = ? " );

     query.addParam( "", RSDBP_IN, Buf.Number);
     query.execute();
     ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

     if( ds.MoveNext() )
        sql = RSDCommand(" DELETE FROM dtaxobjkind_dbt WHERE t_Num = ? ");
        sql.addParam( "", RSDBP_IN, Buf.Number );
        sql.execute();
     else
        stat = 70;
        RunError("Запись конкурентно изменена");
     end;
  end;

  return 0;

OnError( err )
  TxDeferredTaxesRunError(err, stat);
end;

/*Подкачка списка объектов НОБ в виджет*/
macro TxGetCodeListByCode(
  Code:String
 ,IsStrictEq:Bool
 ,RecNumber:Integer
)
  var stat:Integer = -1;
  var UseExtCode:Bool = false;
  var Query:String = "";
  var DataSet = null;
  var TxObjKindsArray = TArray();
  var TxObjKinds = null;

  InitError();

  if( ( Code == null ) or ( Code == "" ) )
    RunError( "Не задан обязательный параметр функции: вид НОБ" );
  end;

  if( ( RecNumber == null ) or ( RecNumber <= 0 ) )
     RunError("Не задан обязательный параметр функции: количество записей списка");
  end;

  Query =  "  SELECT objkind.t_num, "
          +"       objkind.t_code, "
          +"       objkind.t_comment, "
          +"       objkind.t_system "
          +"  FROM dTAXOBJKIND_dbt objkind "
          +"  WHERE  objkind.t_code  " + " LIKE " + "'" + IIF( IsStrictEq, "", "%" ) + Code + IIF( IsStrictEq, "", "%" ) + "' "
         + "         AND ROWNUM < " + String( RecNumber + 1 );

  DataSet = TRsbDataSet( Query );

  while( DataSet.MoveNext() )
     TxObjKinds = CTaxObjKinds();
     
     TxObjKinds.Number = SQL_ConvTypeInteger(DataSet.Num);
     TxObjKinds.Code = SQL_ConvTypeStr(DataSet.Code);
     TxObjKinds.Comment  = SQL_ConvTypeStr(DataSet.Comment);
     TxObjKinds.IsSystem = ( SQL_ConvTypeStr( DataSet.system ) == SET_CHR );

     TxObjKindsArray[TxObjKindsArray.Size] = TxObjKinds;
  end;

  return TxObjKindsArray;

OnError( err )
  TxDeferredTaxesRunError( err, stat );
end;


/*Подкачка списка объектов НОБ в виджет*/
macro TxGetCodeListByID(
  IDArray//:TArray of Integer // Массив идентификаторов сделки
)
  var stat:Integer = -1;
  var UseExtCode:Bool = false;
  var Query:String = "";
  var DataSet = null;
  var TxObjKindsArray = TArray();
  var TxObjKinds = null;

  InitError();

  if( ( IDArray == null ) or ( IDArray.Size <= 0 ) )
     RunError("Не задан обязательный параметр функции: идентификатор НОБ");
  end;

  var IDInStr:String = String( IDArray[0] );

  var IdCount:Integer = 1;
  while( IdCount < IDArray.Size )
    IDInStr = IDInStr + ", " + String( IDArray[IdCount] );
    IdCount = IdCount + 1;
  end;

  Query =  "  SELECT objkind.t_num, "
          +"       objkind.t_code, "
          +"       objkind.t_comment, "
          +"       objkind.t_system "
          +"  FROM dTAXOBJKIND_dbt objkind "
          +"  WHERE  objkind.t_Num IN ( " +  IDInStr + " ) ";

  DataSet = TRsbDataSet( Query );

  while( DataSet.MoveNext() )
     TxObjKinds = CTaxObjKinds();
     
     TxObjKinds.Number = SQL_ConvTypeInteger(DataSet.Num);
     TxObjKinds.Code = SQL_ConvTypeStr(DataSet.Code);
     TxObjKinds.Comment  = SQL_ConvTypeStr(DataSet.Comment);
     TxObjKinds.IsSystem = ( SQL_ConvTypeStr( DataSet.system ) == SET_CHR );

     TxObjKindsArray[TxObjKindsArray.Size] = TxObjKinds;
  end;

  return TxObjKindsArray;

OnError( err )
  TxDeferredTaxesRunError( err, stat );
end;
