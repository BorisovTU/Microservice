/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.0                                          R-Style Software Lab

  File Name   : spscan.mac
  Created     : 08.04.99
  Programmer  : Сабитов Р.Р.
  Description : Печать отчета сканирования

└───────────────────────────────────────────────────────────────────────────*/
import secinter, DealsInter;   

RECORD deal_firstdoc (dl_tick); 
FILE   dl_tick(dl_tick); 

PRIVATE CONST DL_TKLNK_TYPE_RETIREDST = 1;

const Ошибка = 0,
      Первый = 1,
   Последний = 2;

PRIVATE MACRO ParallelExecuteReport( Вызов,firstdoc,Статус,Сообщение, Action )
  VAR rs;

  if( Вызов != Последний )
    return;
  end;

[             Отчет о параллельном выполнении операций для сделок.];
[+-------------------------------+-------+----------------------------------------------------------------------------------------+];
[|              Код              |№ошибки|                 Сообщение                                                              |];
[|            сделки             |       |                                                                                        |];
  rs = RsdRecordSet( " SELECT Deal.t_DealCode, NVL(lp.T_RESULTNUM,0) RESULTNUM, NVL(lp.T_RESULTTEXT,chr(1)) RESULTTEXT " +
                      "   FROM DPRLPCKS_DBT lp, ddl_tick_dbt Deal " +
                      "  WHERE     Deal.T_BofficeKind = lp.T_DOCKIND " +
                      "        AND Deal.T_DealID      = lp.T_DOCUMENTID " +
                      "        AND lp.T_SESSIONID = (select SYS_CONTEXT('USERENV','SESSIONID') from dual) " +
                      " ORDER BY Deal.t_DealCode "
                    );
            
  while( rs.MoveNext() )
[|-------------------------------|-------|----------------------------------------------------------------------------------------|
 |###############################|#######|########################################################################################|]
( SQL_ConvTypeStr( rs.value("t_DealCode") ), int(SQL_ConvTypeInteger(rs.value("RESULTNUM"))), SQL_ConvTypeStr(rs.value("RESULTTEXT")):w );
  end;
[+-------------------------------+-------+----------------------------------------------------------------------------------------+];
END; 

PRIVATE MACRO NormalExecuteReport( Вызов,firstdoc,Статус,Сообщение, Action )
     var continue_cicle, RetCount = 0;
     var cmd, DataSet;

     setbuff (deal_firstdoc,firstdoc);
     if (Вызов==Первый)
[             Отчет о групповом создании операций для сделок.];
[+----------+-------+----------------------------------------------------------------------------------------+];
[|    Код   |№ошибки|                 Сообщение                                                              |];
[|  сделки  |       |                                                                                        |];
[+----------+-------+----------------------------------------------------------------------------------------+];
     elif (Вызов==Ошибка)
        if(Статус == 0) 
           if(deal_firstdoc.DealID)
              dl_tick.DealID = deal_firstdoc.DealID;
              if(GetEQ(dl_tick))
                 if(dl_tick.DealStatus == DL_CLOSED ) /*20*/
                    Сообщение = "Операция создана успешно. Сделка закрыта.";
                 elif(dl_tick.DealStatus == DL_READIED ) /*10*/
                    Сообщение = "Операция создана успешно. Сделка не закрыта.";
                 elif( dl_tick.DealStatus == DL_PREPARING ) /*0*/
                    if( dl_tick.BofficeKind == DL_RETIREMENT_DST )
                       cmd = DL_RSDCommand(  " select tick.t_DealCode "+                    
                                             " from ddl_tklnk_dbt tklnk, ddl_tick_dbt tick "+
                                             " where tklnk.t_parentid = ? "+
                                             "   and tklnk.t_Type     = ? "+
                                             "   and tick.t_DealID    = tklnk.t_ChildID "+
                                             " order by tklnk.t_id "                       
                                          );
                     
                       cmd.AddParam(dl_tick.DealID);
                       cmd.AddParam(DL_TKLNK_TYPE_RETIREDST);
                     
                       DataSet = cmd.Execute();
                       while(DataSet.moveNext())
                          RetCount = RetCount + 1;
                          if( RetCount != 1 )
                             Сообщение = Сообщение + ", ";
                          end;
                          Сообщение = Сообщение + DataSet.DealCode;
                       end;

                       if( RetCount != 0 )
                          Сообщение = "По распределению созданы отложенные операции погашения: " + Сообщение;
                       else 
                          Сообщение = "По распределению не создано ни одной операции погашения.";
                       end;
                    else
                       Сообщение = "Операция не создана.";
                    end;
                 end;
              end;
           end;
        end;
[|##########|#######|########################################################################################|]
(deal_firstdoc.DealCode,Статус,Сообщение:w);
     elif (Вызов==Последний)            
[+----------+-------+----------------------------------------------------------------------------------------+];
     end;
END; 

PRIVATE MACRO UserFuncExecuteReport( Вызов,firstdoc,Статус,Сообщение, Action )
   if (Вызов==Первый)
[             Отчет о групповом выполнении функции пользователя.];
   end;
/*не понятно что здесь должно быть*/
END;

/*Action = 2 - параллельное выполнение операций*/
/*Action = 3 - выполнение функции пользователя*/
MACRO Печать_ОтчетСканирования (Вызов,firstdoc,Статус,Сообщение, Action)
  if(Action == 3)
     UserFuncExecuteReport( Вызов,firstdoc,Статус,Сообщение, Action );
  elif( Action == 2 )
     ParallelExecuteReport( Вызов,firstdoc,Статус,Сообщение, Action );
  else
     NormalExecuteReport( Вызов,firstdoc,Статус,Сообщение, Action );
  end;
END;
