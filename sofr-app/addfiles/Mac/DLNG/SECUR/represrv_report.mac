/*
$Name:             represrv_report.mac
$Module:           АРНУ
$Description:      Расходы на формирование резервов под обесценение.
*/

import "represrv_form.mac", "secinter.mac", "spserv.mac", "sccomiss.mac", "spRepFun.mac", "globals.mac";

PRIVATE VAR   ReservReg_Form;
PRIVATE VAR   ReservReg_Report;
PRIVATE VAR   WebMenuItem; // Информация о запуске отчета из веб

private const ALG_NUM_FOR_PERIOD = 2263;
private const ALG_SCTX_LOTORIGIN = 7306;
private var flag = false;

private var party =TRecHandler("party.dbt");

PRIVATE VAR ReportHeader =
"\n\n##############################  ###########################\n"+
"Аналитический регистр налогового учета №38\n"+
"\nРасходы на формирование резервов под обесценение ценных бумаг у профессиональных участников рынка ценных бумаг, занимающихся дилерской деятельностью\n"+
"\nза период: #\n"+
"\n#\n"+
"\n#\n";

PRIVATE VAR Footer = "\n\nРегистр составлен # __________________________________________________ (подпись, дата, ФИО)\n"+
                     "\nКонтроль __________________________________________________ (подпись, дата, ФИО)";
                                                                                                    

private var
  TableHeader = 
          "┌───────────────┬──────────┬──────────────────────────────┬──────────┬──────────┬──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┬───────────┬───────────┬─────────────────────────────┬────────────────────────────┬──────────┬──────────┬────────────┬────────────┬────────────┬────────────┬─────────────────────────┬────────────┐\n" +
          "│  Вид/выпуск   │   Дата   │          № пакета            │   Вид    │ Участие  │                                     Цена приобретения                                                                                                                                                        │ Количество│ Количество│  Рыночная котировка ценных  │  Рыночная котировка ценных │Номинал на│Номинал на│ Доходы     │ Доходы     │   Сумма    │   Сумма    │  Корректировка резерва  │ Взаимоза-  │\n" +
          "│ ценной бумаги │ покупки  │                              │  сделки  │в корпора-│                                                                                                                                                                                                              │ ценных бу │ ценных бу │    бумаг на отчетную дату   │   бумаг на предыдущую      │отчетную  │предыдущую│ полученные │ полученные │ расчетного │ резерва на │     на отчетную дату    │ висимость  │\n" +
          "│               │          │                              │          │ тивных   ├──────────────────────┬──────────────────────┬──────────────────────┬──────────────────────┬────────────────────────────────────────────────────────────────────┬──────────────────────┬──────────────────────┤ маг на    │ маг на    │                             │   отчетную дату            │дату      │отчетную  │ при частич-│ при частич-│ резерва на │ предыдущую ├────────────┬────────────┤ контрагента│\n" +
          "│               │          │                              │          │действиях │         вал.         │         руб.         │    Номинал на дату   │ расходы, связанные с │            Максимальная цена на дату заключения сделки,            │ Величина отклонения  │ итоговая цена        │ отчетную  │ предыдущую├──────────────┬──────────────┼─────────────┬──────────────┤          │дату      │ ном погаше-│ ном погаше-│ отчетную   │ отчетную   │ досоздание │ восстанов  │ по сделке  │\n" +
          "│               │          │                              │          │ эмитента │                      │                      │                      │ приобретением ц/б    │                       сложившаяся на ОРЦБ                          │ от рынка на одну     │ приобретения для     │ дату      │ отчетную  │              │              │             │              │          │          │ нии номина-│ нии номина-│   дату     │    дату    │            │   ление    │ приобрете- │\n" +
          "│               │          │                              │          │          │                      │                      │     приобретения     │                      ├──────────────────────┬──────────────────────┬──────────────────────┤ ц/б, руб.            │ целей расчета резерва│           │ дату      │     вал.     │    руб.      │    вал.     │     руб.     │          │          │ ла, вал.   │ ла, руб.   │            │            │            │            │ ния с нашим│\n" +
          "│               │          │                              │          │          │                      │                      │                      │                      │         Цена         │       Источник       │        Дата          │                      │                      │           │           │              │              │             │              │          │          │            │            │            │            │            │            │ банком     │\n" +
          "│               │          │                              │          │          │                      │                      │                      │                      │                      │                      │                      │                      │                      │           │           │              │              │             │              │          │          │            │            │            │            │            │            │            │\n" +
          "├───────────────┼──────────┼──────────────────────────────┼──────────┼──────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼───────────┼───────────┼──────────────┼──────────────┼─────────────┼──────────────┼──────────┼──────────┼────────────┼────────────┼────────────┼────────────┼────────────┼────────────┼────────────┤\n" +
          "│      1        │    2     │             3                │    4     │    5     │           6          │           7          │          8           │           9          │           10         │          11          │          12          │          13          │          14          │     15    │     16    │      17      │      18      │     19      │      20      │    21    │    22    │     23     │     24     │     25     │     26     │     27     │     28     │     29     │\n" +
          "├───────────────┼──────────┼──────────────────────────────┼──────────┼──────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼───────────┼───────────┼──────────────┼──────────────┼─────────────┼──────────────┼──────────┼──────────┼────────────┼────────────┼────────────┼────────────┼────────────┼────────────┼────────────┤";

                                                                                                                                                                              
PRIVATE CLASS CResrvReg( Report:OBJECT )
  VAR m_Report = Report;

  MACRO BeginReport()
     m_Report.PrintHeader();
     m_Report.RegisterTable("N1_MainTable", TableHeader,
                            "N1_Issuer", "N1_BuyDate", "N1_PacketNum", "N1_DealKind", "N1_TGO", "N1_CurPrice", "N1_RurPrice", "N1_NomOnBuyDate", "N1_Outlay", "N1_MaxPrice", "N1_MaxSource", "N1_MaxDate", "N1_Difference", "N1_ItogPrice", "N1_AmountOnDate", 
                            "N1_AmountOnPrevDate", "N1_CurMarket", "N1_RurMarket", "N1_PrevCurMarket", "N1_PrevRurMarket",
                            "N1_NomOnDate", "N1_NomOnPrevDate", "N1_PartIncomeCur", "N1_PartIncomeRur", "N1_SumOnDate", "N1_SumOnPrevDate", "N1_CorrSum1", "N1_CorrSum2","N1_InterDepend"
                           );

  END;

  MACRO EndReport()
     m_Report.EndTable();
  END;

  MACRO PrintIssuerName(IssuerName:STRING)
      m_Report.merge("N1_Issuer", "N1_InterDepend");
      m_Report.PrintTableLine("N1_Issuer:l", "Выпуск: "+string(IssuerName), null);
  END;

  MACRO Print()

        m_Report.SetCurrentLineFont( 8, false, false );
        m_Report.PrintTableLine(
                                  "N1_Issuer:c",           m_Report.Issuer(),           null,
                                  "N1_BuyDate:c",          m_Report.BuyDate(),          null,
                                  "N1_PacketNum:c",        m_Report.PacketNum(),        null,
                                  "N1_DealKind:c",         m_Report.DealKind(),         null,
                                  "N1_TGO:c",              m_Report.TGO(),              null,
                                  "N1_CurPrice:r",         m_Report.CurPrice(),         null,
                                  "N1_RurPrice:r",         m_Report.RurPrice(),         null,
                                  "N1_NomOnBuyDate:r",     m_Report.NomOnBuyDate(),     null,
                                  "N1_Outlay:r",           m_Report.OutLay(),           null,
                                  "N1_MaxPrice:r",         m_Report.MaxPrice(),         null,
                                  "N1_MaxSource:r",        m_Report.MaxSource(),        null,
                                  "N1_MaxDate:r",          m_Report.MaxDate(),          null,
                                  "N1_Difference:r",       m_Report.Difference(),       null,
                                  "N1_ItogPrice:r",        m_Report.ItogPrice(),        null,
                                  "N1_AmountOnDate:r",     m_Report.AmountOnDate(),     m_Report.AmountPrecision(),
                                  "N1_AmountOnPrevDate:r", m_Report.AmountOnPrevDate(), m_Report.AmountPrecision(),
                                  "N1_CurMarket:r",        m_Report.CurMarket(),        null, 
                                  "N1_RurMarket:r",        m_Report.RurMarket(),        null,
                                  "N1_PrevCurMarket:r",    m_Report.PrevCurMarket(),    null,
                                  "N1_PrevRurMarket:r",    m_Report.PrevRurMarket(),    null,
                                  "N1_NomOnDate:r",        m_Report.NomOnDate(),        null,
                                  "N1_NomOnPrevDate:r",    m_Report.NomOnPrevDate(),    null,
                                  "N1_PartIncomeCur:r",    m_Report.PartIncomeCur(),    null,
                                  "N1_PartIncomeRur:r",    m_Report.PartIncomeRur(),    null,
                                  "N1_SumOnDate:r",        m_Report.SumOnDate(),        null,
                                  "N1_SumOnPrevDate:r",    m_Report.SumOnPrevDate(),    null,
                                  "N1_CorrSum1:r",         m_Report.CorrSum1(),         null,
                                  "N1_CorrSum2:r",         m_Report.CorrSum2(),         null,
                                  "N1_InterDepend:r",      m_Report.InterDepend(),      null
                               );
  END;         
END;

PRIVATE CLASS (DL_CReportTemplate) SP_ReservReg_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  PRIVATE VAR Period, Year, RepDate, PrevRepDate, AvrType, AllSecur, AvrKindRUR, AvrKindCUR, AvrID, IssuerName, IsApp, PeriodName, DealKindNum;
  PRIVATE VAR MarketPriceRUR, MarketPriceCUR, PrevMarketPriceRUR, PrevMarketPriceCUR;
  PRIVATE VAR SecFIID, DealDate, DealTime, DateBuy, DealCode, DealKindName = "", PriceRUR = 0, PriceCUR = 0, Qost = 0, QostPrev = 0, OutlayRUR = 0, 
              MaximumPrice = $0, MaximumSource = "", MaximumDate, PriceDifference = $0, Reserv = 0, ReservPrev = 0,  ItogPriceRUR = 0,
              NominalOnRepDate = $0, NominalOnBuyDate = $0, NominalOnPrevRepDate = $0, PartialIncomeCur = $0, PartialIncomeRur = $0, IsPercent = false,
              LotOrigin = -1;
  PRIVATE VAR ItogSumOnDate, ItogSumOnPrevDate, ItogCorrSum1, ItogCorrSum2;
  PRIVATE VAR FullItogSumOnDate = 0, FullItogSumOnPrevDate = 0, FullItogCorrSum1 = 0, FullItogCorrSum2 = 0, IsPrintFull = false;
  PRIVATE VAR InterDependVal = "";

PRIVATE VAR m_IsDataExist = false; /*для Web, если нет данных, то ничего не показываем*/
/* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsDataExist or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END; 

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport()
     Period      = ReservReg_Form.GetFieldValue( PNFLD_RESERVREG_PERIOD);
     Year        = ReservReg_Form.GetFieldValue( PNFLD_RESERVREG_YEAR);
     RepDate     = ReservReg_Form.GetFieldValue( PNFLD_RESERVREG_REPDATE);
     PrevRepDate = ReservReg_Form.GetFieldValue( PNFLD_RESERVREG_PREVREPDATE);
     AvrType     = ReservReg_Form.GetFieldValue( PNFLD_RESERVREG_SECURKIND);
     AllSecur    = ReservReg_Form.GetFieldValue( PNFLD_RESERVREG_ALLSECUR);
     AvrKindRUR  = ReservReg_Form.GetFieldValue( PNFLD_RESERVREG_RURSECUR);
     AvrKindCUR  = ReservReg_Form.GetFieldValue( PNFLD_RESERVREG_CURSECUR);
     if(ValType(AvrType) != V_Integer )  // если веб
       if(AvrKindRUR and AvrKindCUR)
         AllSecur    = "X";
         AvrKindRUR  = "";
         AvrKindCUR  = "";
       elif(AvrKindRUR)
         AllSecur    = "";
         AvrKindRUR  = "X";
         AvrKindCUR  = "";
       elif(AvrKindCUR)
         AllSecur    = "";
         AvrKindRUR  = "";
         AvrKindCUR  = "X";
       end;
     end;
     AvrID       = ReservReg_Form.GetFieldValue( PNFLD_RESERVREG_ISSUER);
     IssuerName  = ReservReg_Form.GetFieldValue( PNFLD_RESERVREG_ISSUERNAME);
     DealKindNum = ReservReg_Form.GetFieldValue( PNFLD_RESERVREG_DEALKIND);
     IsApp       = ReservReg_Form.GetFieldValue( PNFLD_RESERVREG_ISAPP);
  END;

  MACRO PrintHeader( )
     VAR INN = "", KPP = "", AvrTypeName = "", Tmp_AvrTypeName = "", CurRur = "", i = 0;
     SplitFullINN( ПолучитьКодСубъекта({OurBank}, PTCK_INN), INN, KPP );

     if(ValType(AvrType) == V_Integer )
       if( AvrType != -1)
         AvoirKindName( AvrType, NULL, AvrTypeName );
       end;
     else
       if( ( AvrType != null ) and ( AvrType.Size > 0 ))
         while( i < AvrType.Size)
           AvoirKindName( AvrType[i], NULL, Tmp_AvrTypeName );
           if( AvrTypeName == "" )
             AvrTypeName = Tmp_AvrTypeName;
           else
             AvrTypeName = AvrTypeName +", " + Tmp_AvrTypeName;
           end;
           i = i + 1;
         end;
       end;
     end;

     PeriodName = DL_GetNameAlg( ALG_NUM_FOR_PERIOD, Period );

     if( AvrKindRUR == "X" )
        CurRur = "Ценные бумаги, номинированные в валюте РФ";
     elif( AvrKindCUR == "X" )
        CurRur = "Ценные бумаги, номинированные в ин.валюте";
     end;

     SetActiveSheet( "Отчет" );
     PrintFormatString( ReportHeader,
                        "N1_H0_1:l" , ПолучитьКороткоеИмяСубъекта({OurBank})+"      "+INN + " / " + KPP,
                        "N1_H0_3"   , PeriodName + " " + String(Year) + " года",
                        "N1_H0_4"   , IIF(AvrType != -1, "Вид ценных бумаг: " + AvrTypeName, ""),
                        "N1_H0_5"   , IIF(AllSecur != "X", CurRur, "")
                      );
  END;
    /* Получаем курс вида "Котировка для расчета налогового резерва" на дату.
      FIID           - код ценной бумаги
      ConvDate       - дата, на которую получаем курс
      MarketPriceRUR - сюда возвращаем цену в рублях
      MarketPriceCUR - сюда возвращаем цену в валюте */
     private macro GetMarketPrice( FIID, ConvDate,
                              MarketPriceRUR:@double, MarketPriceCUR:@double )
        var
           AvrPrice, sql, rsd, Select;
   /* Проверяем все валюты по очереди. Ищем курс вида "Котировка для расчета налогового резерва".
      Если курс нашли и при необходимости удалось перевести его в рубли, то
      берем. Иначе проверяем следующий. */

        sql = " select fin.t_FIID "
            + "   from dfininstr_dbt fin, dratedef_dbt rd "
            + "  where fin.t_FI_Kind = " + string(FIKIND_CURRENCY)
            + "    and rd.t_type = " + string(SP_RATETYPE_TaxReserve) 
            + "    and rd.t_otherfi =  ? "
            + "    and fin.t_FIID = rd.t_FIID";
       
        Select = RSDCommand(sql);
        Select.addParam( "", RSDBP_IN, FIID );
        Select.execute();

        rsd = TRsbDataSet(Select);

        while( rsd.MoveNext() )
           if( ConvSumDbl( AvrPrice, 1.0, ConvDate, FIID, rsd.FIID, false, SP_RATETYPE_TaxReserve ) )
         /* Нашли курс вида "Средневзвешенная цена". */
              if( rsd.FIID == NATCUR )
            /* Нашли курс в рублях. Выходим. */
                 MarketPriceCUR = 0.0;
                 MarketPriceRUR = AvrPrice;
                 return true;
              else
                 if( SmartConvertSumDbl( MarketPriceRUR, AvrPrice,
                                    ConvDate, rsd.FIID, NATCUR ) )
               /* Нашли курс в валюте и смогли перевести в рубли. Выходим. */
                    MarketPriceCUR = AvrPrice;
                    return true;
                 end;
              end;
           end;
        end;

        MarketPriceRUR = 0.0;
        MarketPriceCUR = 0.0;
        return false;
     end;

/* Проверить, что для выпуска задан курс вида "Котировка для расчета налогового резерва" за дату в одной из валют */
  private macro CheckRate( FIID, _RepDate )
     var
        CPriceRUR, CPriceCUR;

     return GetMarketPrice( FIID, _RepDate, CPriceRUR, CPriceCUR );
  end;

  private macro GetItogPrice(FIID, IDate:Date, IPriceRUR, IOutlayRUR, INominalOnDate, IPartialIncomeRur )
     var _ItogPrice = 0;

     if( IDate <= REPRES_OLDFORMDATE )
        _ItogPrice = IPriceRUR + IOutlayRUR - PriceDifference;
     else
        if( GetCalcWithRetPart() AND FI_IsBond(FIID) AND (NominalOnBuyDate != 0) )                     
           /*(А3 -- <А4-4>) *<A13-1> / <А3-1> + А4*/                                                   
           _ItogPrice = IOutlayRUR + (IPriceRUR - PriceDifference) * INominalOnDate / NominalOnBuyDate;
        else                                                                                           
           /*А3 + А4 - A4-4*/                                                                          
           _ItogPrice = IPriceRUR + IOutlayRUR - PriceDifference - IPartialIncomeRur;                                        
        end;                                                                                           
     end;

     return _ItogPrice;
  end;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )
     private file dl_tick(dl_tick);
     private record dl_leg(dl_leg);

     private macro GetOutlayBuy( CalcDate, wrtsum )
     /* ** Величина SZ (расходы, связанные с приобретением) рассчитывается как сумма следующих параметров лота покупки:
        "  сумма предварительных затрат лота, приходящаяся на одну ц/б,
        "  сумма комиссий лота (без НДС), приходящаяся на одну ц/б, независимо от того, были ли оплачены эти комиссии,
        "  сумма распределенных комиссий (без НДС), приходящаяся на одну ц/б, независимо от того, были ли оплачены эти комиссии,
        "  сумма регистрационного сбора (без НДС), приходящаяся на одну ц/б, независимо от того, был ли он оплачен.
        
        При этом, если отчетная дата (или предыдущая отчетная дата, если расчет делаем для суммы резерва на предыдущую отчетную дату) меньше
        или равна 31.12.2009, то затраты переводятся из валюты затрат в рубли по курсу на отчетную дату, если отчетная дата больше
        31.12.2009, то затраты переводятся в рубли по курсу на дату признания затрат (предварительные затраты и рег. сбор - на дату заключения сделки, 
        все комиссии - на дату оплаты комиссии из сделки). */

        var RCommSumBank = $0, RCommSumAgent = $0,
            PreOutlay;
        var RegSborDate, CommDate, OutLayDate, sql, cmd;

       if(CalcDate <= REPRES_OLDFORMDATE)
         RegSborDate = CalcDate;
         CommDate = CalcDate;
         OutLayDate = CalcDate;
       else
         RegSborDate = dl_tick.DealDate;
         CommDate = dl_tick.CommDate;
         OutLayDate = dl_tick.DealDate;
       end;

       VAR sum_com = $0, nds_com = $0;                                                              

       /*переводим на отчетную дату, если комиссиия не оплачена*/
       SP_GetSumCom( sum_com, nds_com, null, null,
                     dl_tick.BofficeKind, dl_tick.DealID, null, null, null, 0, 1,   
                     1, 1, 1, 1, null, NATCUR, true, 
                     IIF(GetLegKindByLotDeal(wrtsum, dl_tick) == LEG_KIND_DL_TICK,true,false), 
                     IIF(GetLegKindByLotDeal(wrtsum, dl_tick) == LEG_KIND_DL_TICK_BACK,true,false)
                   );

       ПолучитьСуммуРаспределенныхКомиссийПоСделке( dl_tick, 
                                                    RCommSumAgent, NULL, RCommSumBank, NULL, 
       
                                              NULL, NULL, NATCUR, CommDate, -1/*все, расчитанные или оплаченные*/ );
       
       PreOutlay = dl_tick.PreOutlay;

       if(dl_tick.BOfficeKind == DL_AVRWRT)
       
         sql = "SELECT t_sum FROM ddlsum_dbt WHERE t_dockind = " + dl_tick.BOfficeKind + 
               " AND t_docid = " + dl_tick.DealID + " AND t_kind = 1100";    /* затраты по зачислению */
         cmd = TrsbDataSet(sql);
         if(cmd.movenext())
            PreOutlay = PreOutlay + DoubleToMoney(cmd.t_sum);
         end;
       end;

       SmartConvertSumDbl( PreOutlay, PreOutlay, OutLayDate,
                           dl_tick.PreOutlayFIID, NATCUR, true );

       return ( PreOutlay + 
                RCommSumAgent + RCommSumBank +
                sum_com
              )/double(dl_leg.Principal);
     end;

     // является ли ц/б облигацией с АД
     private macro FI_IsAvrKindBond_AD(FIID)
        var Query;
        var select = RSDCommand("select count(1) amount " +
                                 " from  dfiwarnts_dbt " +
                                 "where  t_FIID = ? " + 
                                 "  and  t_IsPartial = 'X'");

        select.addParam( "", RSDBP_IN, FIID );
        select.execute();

        Query = TRsbDataSet(select);
        if( Query.MoveNext() )
            if( Query.amount > 0 )
                return true;
            end;
        end;
        return false;
     end;

   /* Вставляем информацию о лоте во временный файл.
      Amount      - к-во ц/б на лоте на конец дня отчетной даты
      AmountPrev  - к-во ц/б на лоте на конец дня предыдущей отчетной даты */
     private macro InsertLotInReport( Amount, AmountPrev, wrtsum, lot_data )

        var
           Price, OutlayPrev = 0, PricePrev = 0, Outlay = $0, P = $0;
        var BuyingPrice = $0, KK = 1, Amort = $0, S = $0;

        record finin(fininstr);
        record avoir(avoiriss);

        PriceCUR = 0;
        PriceRUR = 0;
        OutlayRUR = 0;

        LotOrigin = lot_data.Origin;

        SecFIID = wrtsum.FIID;
        if(lot_data.Origin != TXLOTORIGIN_DEAL)
          SecFIID = lot_data.FIID;
        end;

   /* A1-2 - Дата покупки
      Дата поставки сделки покупки */
       DateBuy = wrtsum.Date;
       if(lot_data.Origin != TXLOTORIGIN_DEAL)
         DateBuy = date(lot_data.BuyDate);
       end;
   /* A1-3 - № пакета
      № сделки внутренний */
       DealCode = dl_tick.DealCode;

     /* A1-4 - Вид сделки */
       if(lot_data.Type == TXLOTS_BUY)
         if(dl_tick.BOfficeKind == DL_AVRWRT)
           DealKindName = "FB";
         else
           DealKindName = "B";
         end;
       elif(lot_data.Type == TXLOTS_BACKREPO)
         DealKindName = "РО";
       elif(lot_data.Type == TXLOTS_LOANGET)
         DealKindName = "ЗО";
       end;

     /*A2 - Цена приобретения/вал.
       для акций: 
          поле заполняется, только если валюта цены ц/б <> руб: выводится цена покупки одной ц/б сделки в этой валюте
       Для облигаций 
          всегда выводится цена в процентах от номинала. Если в сделке цена указана в абсолютном выражении, 
          она переводится в проценты от номинала, исходя из значений номинала на дату <А1-2> и курсов на дату <A1-2>*/
     /*A3 - Цена приобретения/руб.
       Для акций:
          Если валюта цены = рубли, то выводится цена покупки одной ц/б в руб.,
          если валюта цены <> руб., то цена покупки переводится из валюты цены в рубли по кур-су на дату <A1-2>
       Для облигаций:
       -  если отчетная дата <С3> меньше или равна 31.12.2009, то <A3>=<А2>*<A13-1> в рублях по курсу на отчетную дату
       -  если отчетная дата <С3> больше 31.12.2009, то <A3>=<А2>*<A3-1> в рублях по курсу на <A1-2>
       */
        Price = dl_leg.Price;
            /*A13-1 - Номинал на отчетную дату
              Заполняется только для облигаций
              Выводится номинал в валюте номинала, действующий на отчетную дату (расчет аналогичен указанному в поле <A3-1>)*/
        NominalOnRepDate = GetFaceValue(wrtsum.FIID, RepDate);/*номинал на дату отчета*/
           /*A3-1  - Номинал на дату приобрете-ния
             Заполняется только для облигаций
             Выводится номинал в валюте номинала, действующий на дату <A1-2> 
             (расчет значения номинала делается на основании данных анкеты ценной бумаги, 
             при этом признаки "погашен"/"не погашен" в справочнике частичных погашений игнорируются*/
        NominalOnBuyDate = GetFaceValue(wrtsum.FIID, SQL_ConvTypeDate(wrtsum.Date));/*номинал на дату приобретения*/ 

        /*А13-2 Номинал на предыдущую отчетную дату Заполняется только для облигаций
          Выводится номинал в валюте номинала, действующий на предыдущую отчетную дату (расчет аналогичен указанному в поле <A3-1>)*/
        NominalOnPrevRepDate = GetFaceValue(wrtsum.FIID, PrevRepDate);/*номинал на дату отчета*/
       /*Ценная бумага*/
        PricePrev = 0;

        if( not ПолучитьФинИн( wrtsum.FIID, finin ) ) 
           if( FI_IsShare( finin ) OR FI_IsInvestmentShare(finin) )/*акции, ДР и паи*/
              IsPercent = false;
              if( dl_leg.CFI != NATCUR )
                 if(lot_data.Origin == TXLOTORIGIN_DEAL)
                   PriceCUR = Price;
                 else
                   SmartConvertSumDbl( S, lot_data.Cost, DateBuy,
                                       dl_leg.CFI, finin.FaceValueFI, true );

                   if(lot_data.Qost != 0)
                     PriceCUR = S/lot_data.Qost;
                   end;
                 end;
                 
                 SmartConvertSumDbl(  PriceRUR, Price, DateBuy,
                                      dl_leg.CFI, NATCUR, true );
              else
                 if(lot_data.Origin == TXLOTORIGIN_DEAL)
                   PriceRUR =  Price;
                 else
                   if(lot_data.Origin == TXLOTORIGIN_GLOBCONVERT)
                     SmartConvertSumDbl( S, lot_data.Cost, DateBuy,
                                         dl_leg.CFI, NATCUR, true );
                     PriceRUR = S/lot_data.Qost; 
                   elif(lot_data.Origin == TXLOTORIGIN_MODIFFACEVALUE)
                     PriceRUR = Price/lot_data.Qost;
                   end;
                 end;
              end;
              if( wrtsum.Date <= PrevRepDate )
                 PricePrev = PriceRUR;
              end;
           elif( FI_IsBond( finin ) )
               IsPercent = true;

               if(lot_data.Origin == TXLOTORIGIN_DEAL)
                 if( dl_leg.RelativePrice == "X")
                    PriceCUR = Price;
                 else
                    /*переводим в валюту номинала и берем процент*/
                    SmartConvertSumDbl(  PriceCUR, PriceCUR, DateBuy,
                                        dl_leg.CFI, finin.FaceValueFI, true );

                    PriceCUR = (Price / NominalOnBuyDate) * 100;
                 end;
               else
                 
                 SmartConvertSumDbl( S, lot_data.Cost/*Стоимость без НКД в ВЦ*/, DateBuy,
                                     dl_leg.CFI, finin.FaceValueFI, true );

                 PriceCUR = S/(lot_data.Qost*NominalOnBuyDate);
               end;

               if(lot_data.Origin == TXLOTORIGIN_DEAL)
                 if(RepDate <= REPRES_OLDFORMDATE)
                   PriceRUR = (PriceCUR * NominalOnRepDate)/100;
                   if( dl_leg.CFI != NATCUR )
                      SmartConvertSumDbl(  PriceRUR, PriceRUR, RepDate,
                                           dl_leg.CFI, NATCUR, true );
                   end;
                 else
                   PriceRUR = (PriceCUR * NominalOnBuyDate)/100;
                   if( dl_leg.CFI != NATCUR )
                      SmartConvertSumDbl(  PriceRUR, PriceRUR, DateBuy,
                                           dl_leg.CFI, NATCUR, true );
                   end;
                 end;

                 if( wrtsum.Date <= PrevRepDate )
                    if(PrevRepDate <= REPRES_OLDFORMDATE)
                      PricePrev = (PriceCUR * NominalOnPrevRepDate)/100;
                      if( dl_leg.CFI != NATCUR )
                         SmartConvertSumDbl(  PricePrev, PricePrev, PrevRepDate,
                                              dl_leg.CFI, NATCUR, true );
                      end;
                    else
                      PricePrev = (PriceCUR * NominalOnBuyDate)/100;
                      if( dl_leg.CFI != NATCUR )
                         SmartConvertSumDbl(  PricePrev, PricePrev, DateBuy,
                                              dl_leg.CFI, NATCUR, true );
                      end;
                    end;
                 end;

               else
                 if(lot_data.Origin == TXLOTORIGIN_DEAL)
                   PriceRUR =  Price;
                 else
                   if(lot_data.Origin == TXLOTORIGIN_GLOBCONVERT)
                     SmartConvertSumDbl( S, lot_data.Cost, DateBuy,
                                         dl_leg.CFI, NATCUR, true );
                     PriceRUR = S/lot_data.Qost; 
                   elif(lot_data.Origin == TXLOTORIGIN_MODIFFACEVALUE)
                     PriceRUR = Price/lot_data.Qost;
                   end;
                 end;

                 if( PrevRepDate <= wrtsum.Date )
                    PricePrev = PriceRUR;
                 end;

               end;
           end;
        end;
   /* A6 - Qost */
         Qost = Amount; 
   /* A6-1 Qostprev */
         QostPrev = AmountPrev; 
   /* A4 - расходы, связанные с приобретением ц/б(
      Если отчетная дата <С3> меньше или равна 31.12.2009, то <A4> = SZ * <A13-1> / <А3-1> 
      Если отчетная дата <С3> больше 31.12.2009, то <A4> = SZ )
      Сумма затрат лота в руб. (расчет см. в **) */
      if(lot_data.Origin == TXLOTORIGIN_DEAL)
         Outlay = GetOutlayBuy( RepDate, wrtsum );
         if(RepDate <= REPRES_OLDFORMDATE)
           if( NominalOnBuyDate != 0)
            OutlayRUR = Outlay * NominalOnRepDate / NominalOnBuyDate; 
           end;
         else
           OutlayRUR = Outlay;
         end;
    
          /*для расчета резерва на предыдущую дату*/
          if( wrtsum.Date <= PrevRepDate )       
             OutlayPrev = GetOutlayBuy( PrevRepDate, wrtsum );
             if(PrevRepDate <= REPRES_OLDFORMDATE)
                if( NominalOnBuyDate != 0)
                   OutlayPrev = OutlayPrev * NominalOnPrevRepDate / NominalOnBuyDate; 
                end;
             end;
          end;
    
      end;
   /* А4-1 Максимальная цена на дату заключения сделки, сложивш. на ОРЦБ -Цена 
      Параметр MarketPrice для сделки покупки. */
      if(lot_data.Origin == TXLOTORIGIN_DEAL)
        MaximumPrice = lot_data.BuyMarketPrice; 
      end;

   /* А4-2 Максимальная цена на дату заключения сделки, сложивш. на ОРЦБ - Источник 
      Параметр Market для сделки покупки. */
      if(lot_data.Origin == TXLOTORIGIN_DEAL)
        MaximumSource = lot_data.BuyMarket;
      end;
                                         
   /* А4-2 Максимальная цена на дату заключения сделки, сложивш. на ОРЦБ - Дата 
      Параметр DateMarket для сделки покупки. */
      if(lot_data.Origin == TXLOTORIGIN_DEAL)
        MaximumDate = lot_data.BuyDateMarket;
      end;

   /* A4-4 Величина отклонения от рынка на одну ц/б, руб.
      Если Price <=< A4-1 >, то < А4-4 > = 0
      Если Price > < A4-1 >, то < А4-4 > = (Price-< A4-1 >) * КК */   

      /* Price - цена покупки:
         "  для рублевых акций, паев и деп. расписок Price=<А3>,
         "  для валютных акций, паев и деп. расписок Price=<А2>*курс цены на дату заключения сделки покупки,
         "  для облигаций Price=<А2>. */

      if(lot_data.Origin == TXLOTORIGIN_DEAL)
        if(FI_IsBond( finin )) // Облигация
          BuyingPrice = PriceCUR;
        else // все остальное
          if(finin.FaceValueFI != NATCUR)
            SmartConvertSumDbl( BuyingPrice, PriceCUR, dl_tick.DealDate,
                                dl_leg.CFI, NATCUR, true );
          else
            BuyingPrice = PriceRUR;
          end;
        end;

        if(BuyingPrice <= MaximumPrice)
          PriceDifference = $0;
        else

          /* 4.   KК определяется следующим образом:
             "  для акций, депозитарных расписок и паев KК=1 
             "  для облигаций с ВН != рубли KК= NomB *(курс на дату поставки <A3>)/100
             "  для облигаций с ВН = рубли, то KК = NomB / 100. */
          if(FI_IsBond( finin )) // Облигация
            if(finin.FaceValueFI != NATCUR)
              SmartConvertSumDbl( KK, NominalOnBuyDate, DateBuy,
                                  finin.FaceValueFI, NATCUR, true );
              KK = KK/100;
            else
              KK = NominalOnBuyDate/100;
            end;
          else // все остальное
            KK = 1;
          end;

          PriceDifference = (BuyingPrice - MaximumPrice) * KK;
        end;
      end;
      

   /* A7 - Рыночная котировка ц/б на отчетную дату/вал.
      Поле заполняется, если для данного выпуска на отчетную дату курс вида
      "Средневзвешенная цена" задан только в валюте, отличной от руб. */
         /* MarketPriceCUR */

   /* A8 - Рыночная котировка ц/б на отчетную дату/руб.
      Если поле <А7> заполнено, то выводится значение из поля <A7>,
      переведенное в рубли по курсу на отчетную дату, иначе выводится курс
      вида "Средневзвешенная цена" для данного выпуска в рублях за отчетную
      дату */
         /*MarketPriceRUR */

   /* А7-1  Рыночная котировка ц/б на предыдущую отчетную дату/вал.  
      Поле заполняется, если для данного выпуска на предыдущую отчетную дату курс 
      "средневзвешенная цена" задан только в валюте, отличной от руб. */
         /*PrevMarketPriceCUR*/
   /* А8-1  Рыночная котировка ц/б на предыдущую отчетную дату/руб.  
      Если поле <А7-1> заполнено, то выводится значение из поля <A7-1>, переведенное в рубли по курсу на предыдущую отчетную дату,
      иначе выводится курс вида "средневзвешенная цена" для данного выпуска в рублях за предыдущую отчетную дату */
         /*PrevMarketPriceRUR*/ 

   /* A14-1, A14-2*/
      if (FI_IsAvrKindBond_AD(finin.FIID))
        /* A14-1 Доходы, полученные при частичном погашении номинала, вал. 
           Заполняется только для валютных облигаций с АД. 
           <А14-1>=<А6>*(Сумма частичных погашений на одну ц/б, выплата ко-торых приходится на период между  <А1-2>+1  и <С3> включительно) */
        if(finin.FaceValueFI != NATCUR) 
          PartialIncomeCur = GetPartialSumByPeriod( finin.FIID, 1, DateBuy+1, RepDate, null, null, null, true );
        end;

        /* A14-2 Доходы, полученные при частичном погашении номинала, руб.
           Заполняется только для облигаций с АД.
           <А14-2>= <А6>*(Сумма частичных погашений на одну ц/б, выплата которых приходится на период между  <А1-2>+1 и <С3> включительно, в рублях по курсу ЦБ на даты частичных погашений) */
        PartialIncomeRur = GetPartialSumByPeriod_Rub( finin.FIID, 1, DateBuy+1, RepDate, null, null, null, true );
   
         if( wrtsum.Date <= PrevRepDate )       
            Amort = GetPartialSumByPeriod_Rub( finin.FIID, 1, DateBuy+1, PrevRepDate, null, null, null, true );
         end;
      else
         PartialIncomeCur = PartialIncomeRur = Amort = $0;
      end;


   /* A9 - Сумма расчетного резерва на отчетную дату
      Если <A5>  >  <A8>, то 
      - если отчетная дата <С3> меньше или равна 31.12.2009, то <A9> = (<A5> - <A8>) * <A6>,
      - если отчетная дата <С3> больше 31.12.2009, то <A9> = (<A5> - <A8>- <А14-2>) * <A6>.
      иначе - <A9> = 0
      Если поле A8 - не заполнено, то <A9>=0 */
       ItogPriceRUR = GetItogPrice(finin.FIID, RepDate, PriceRUR, OutlayRUR, NominalOnRepDate, PartialIncomeRur );
      if( (MarketPriceRUR != 0) AND (ItogPriceRUR > MarketPriceRUR) )
         Reserv = (ItogPriceRUR - MarketPriceRUR) * Amount;
      else
         Reserv = 0;
      end;

   /* A10 - Сумма резерва на предыдущую отчетную дату
       Если P  >  <A8-1>, то:
       -  если предыдущая отчетная дата <С4> меньше или равна 31.12.2009, то <A10> = (P - <A8-1>) * <A6-1>,
       -  если предыдущая отчетная дата <С4> больше 31.12.2009, то <A10> = (P - <A8-1>- Amort) * <A6-1>,
       иначе - <A10> = 0
       Если поле A8-1 - не заполнено, то <A10>=0
       Для облигаций:
       -  если предыдущая отчетная дата <С4> меньше или равна 31.12.2009, то Р= (<А2>*<A13-2> в рублях по курсу на предыдущую отчетную дату) +SZ*<A13-2>/<A3-1>, 
       -  если предыдущая отчетная дата <С4> больше 31.12.2009, то Р= (<А2>*<A13-2> в рублях по курсу на <A1-2>)+SZ- <А4-4>, 
       Для акций:
       -  если предыдущая отчетная дата <С4> меньше или равна 31.12.2009, то Р= (цена сделки <A1-3> в рублях по курсу на предыдущую отчетную дату )+ SZ,
       -  если предыдущая отчетная дата <С4> больше 31.12.2009, то Р= (цена сделки <A1-3> в рублях по курсу на <A1-2>)+SZ- <А4-4>,
       Amort - Сумма частичных погашений на одну ц/б, выплата которых при-ходится на период между  <А1-2>+1 и <С4> включительно, в рублях по курсу ЦБ на даты частичных погашений */

         if (wrtsum.Date > PrevRepDate)       
            ReservPrev = 0;
         else
          P = GetItogPrice(finin.FIID, PrevRepDate, PricePrev, OutlayPrev, NominalOnPrevRepDate, Amort );

            if( (PrevMarketPriceRUR != 0) AND (P > PrevMarketPriceRUR) )
               ReservPrev = (P - PrevMarketPriceRUR) * AmountPrev;
            else
               ReservPrev = 0 ;
            end;
         end;

         InterDependVal = "";                             
         if( (lot_data.Origin == TXLOTORIGIN_DEAL) AND (ПолучитьСубъекта(GetContrInDeal(dl_tick), party) == 0) )
           DL_GetObjAttrName(OBJTYPE_PARTY, PARTY_ATTR_GROUP_UNICHANGE, DL_GetMainObjAttr(party, PARTY_ATTR_GROUP_UNICHANGE, OBJTYPE_PARTY ),
                             null, null, @InterDependVal);
           InterDependVal = IIF(InterDependVal=="1","Да","Нет");
         end;

         this.ClearCacheOneRecord();
         ObjReport.Print();
         m_IsDataExist = true;

     end;

     macro FillFullItog()
         FullItogSumOnDate     = FullItogSumOnDate + ItogSumOnDate;
         FullItogSumOnPrevDate = FullItogSumOnPrevDate + ItogSumOnPrevDate;
         FullItogCorrSum1      = FullItogCorrSum1 + ItogCorrSum1;
         FullItogCorrSum2      = FullItogCorrSum2 + ItogCorrSum2;
     end;

     macro ClearItog()
         ItogSumOnDate = 0;
         ItogSumOnPrevDate = 0;
         ItogCorrSum1 = 0;
         ItogCorrSum2 = 0;
     end;

     macro ClearFullItog()
         FullItogSumOnDate = 0;
         FullItogSumOnPrevDate = 0;
         FullItogCorrSum1 = 0;
         FullItogCorrSum2 = 0;
     end;

     private macro ОтчетПоБумаге( AvoirissFI ) 
 
        var
          sql_count, sql_sel, sql, rsd, iCount, IsPrint = false, LotType = "",
          wrtsum   = TRecHandler( "pmwrtsum" ),
          continue_cicle, Num = 0;
        var IsMarketPrice = true, IsPrevMarketPrice = true;

        if( not GetMarketPrice( AvoirissFI.FIID, RepDate,
                                @MarketPriceRUR,
                                @MarketPriceCUR ) )
           MarketPriceRUR = 0;
           MarketPriceCUR = 0;
           IsMarketPrice = false;
        end;

        if( not GetMarketPrice( AvoirissFI.FIID, PrevRepDate,
                                @PrevMarketPriceRUR,
                                @PrevMarketPriceCUR ) )
           PrevMarketPriceRUR = 0;
           PrevMarketPriceCUR = 0;
           IsPrevMarketPrice = false;
        end;

        if(IsPrevMarketPrice == false)
          //Если ц/б не является обращающейся на какую либо из дат создания резерва (отчетную или предыдущую отчетную), то в протокол выводится сообщение
          MsgBox( "Бумага \"" + AvoirissFI.Name + "\" не является обращающейся на ОРЦБ на дату " + PrevRepDate);
        end;
        
        if(IsMarketPrice == false)
          //Если ц/б не является обращающейся на какую либо из дат создания резерва (отчетную или предыдущую отчетную), то в протокол выводится сообщение
          MsgBox( "Бумага \"" + AvoirissFI.Name + "\" не является обращающейся на ОРЦБ на дату " + RepDate);
        end;

        if((IsMarketPrice == false) and (IsPrevMarketPrice == false))
          //В отчет отбираются только выпуски, обращающиеся хотя бы на одну из дат создания резерва (отчетную или предыдущую отчетную).
          return;
        end;

/*      Отчет формируется на основании таблицы остатков и списаний ценных бумаг
        В отчет выводятся сделки обычных (не-РЕПО) покупок, по которым есть ненулевой остаток ц/б в собственности банка 
             хотя бы в одну из дат - отчетную или предыдущую отчетную дату. 
        Остаток ц/б в собственности банка Qост по покупке В на дату Dost определяется так:  по формуле: 
            Qост =суммарное количество ц\б в строках таблицы остатков и списаний где:
            дата зачисления <= Dost
            дата списания > Dost или отсутствует
            количество >0
            исходная покупка = данная сделка B*/
       sql_count = " SELECT count(1) iCount ";

      sql_sel = " SELECT lot.t_DealID, lot.t_BUYDATE BuyDate, lot.t_Type, NVL(Qost.t_Amount,0) Qost, lot.t_FIID, lot.t_Origin, "+
                " lot.t_BuyDate, lot.t_TotalCost, "
                " rsb_sctx.TXGetLotCost(lot.t_ID) as Cost," 
                " NVL(QostPrev.t_Amount,0) Qostprev, " +
                " Rsb_SCTX.GetMarketPrice( lot.t_ID, 0 ) AS BuyMarketPrice, " + 
                " Rsb_SCTX.GetMarket( lot.t_ID, 0 ) AS BuyMarket, " +
                " Rsb_SCTX.GetDateMarket( lot.t_ID, 0 ) AS BuyDateMarket ";

       if(DealKindNum == REPRES_DKIND_BUY)
         LotType = string(TXLOTS_BUY);
       elif(DealKindNum == REPRES_DKIND_REPO_LOAN)
         LotType = string(TXLOTS_BACKREPO, ", ", TXLOTS_LOANGET);
       else
         LotType = string(TXLOTS_BUY, ", ", TXLOTS_BACKREPO, ", ", TXLOTS_LOANGET);
       end;

       sql =      " FROM dsctxlot_dbt currlot, dsctxlot_dbt lot "
           + " LEFT JOIN (select rest.t_SourceID,Sum(rest.t_Amount) t_Amount "
           +              " from dsctxrest_dbt rest, dsctxlot_dbt lot "
           +             " WHERE lot.t_Type IN ( " + LotType + ")"
           +               " AND lot.t_ID = rest.t_SourceID "
           +               " AND rest.t_BuyDate <= " + GetSQLDate(RepDate)
           +               " AND (rest.t_SaleDate > " + GetSQLDate(RepDate)
           +               "      or rest.t_SaleDate = " + GetSQLDate(ZeroDate) + " ) "
           +          " GROUP BY rest.t_SourceID ) Qost ON lot.t_ID = Qost.t_SourceID "
           + " LEFT JOIN (select rest.t_SourceID,Sum(rest.t_Amount) t_Amount "
           +              " from dsctxrest_dbt rest, dsctxlot_dbt lot "
           +             " WHERE lot.t_Type IN ( " + LotType + ")"
           +               " AND lot.t_ID = rest.t_SourceID "
           +               " AND rest.t_BuyDate <= " + GetSQLDate(PrevRepDate)
           +               " AND (rest.t_SaleDate > " + GetSQLDate(PrevRepDate)
           +               "      or rest.t_SaleDate = " + GetSQLDate(ZeroDate) + " ) "
           +          " GROUP BY rest.t_SourceID ) QostPrev ON lot.t_ID = QostPrev.t_SourceID "
           +     " WHERE lot.t_ID = currlot.t_BegLotID "
           +  "      AND lot.t_Type IN ( " + LotType + " )"
           +       " AND lot.t_FIID = " + string(AvoirissFI.FIID)
           +       " AND ( (lot.t_BuyDate <= " + GetSQLDate(RepDate)     + " AND NVL(Qost.t_Amount,0) > 0) OR "
           +             " (lot.t_BuyDate <= " + GetSQLDate(PrevRepDate) + " AND NVL(QostPrev.t_Amount,0) > 0) ) "
           +       " AND lot.t_DealID <> 0 "
           + "ORDER BY lot.t_FIID, lot.t_DealDate, lot.t_DealTime "; /*сортируем по дате и времени заключения сделки*/


   /* подсчет кол-ва строк */
       rsd = TRsbDataSet(sql_count + sql);
          if( rsd.MoveNext() )
             iCount = int(rsd.iCount);
          else
             iCount = -1;
       end;
       ClearItog();
       var ProgressIndicator = CreateProgressIndicator(m_IsWebService);

       if( m_IsWebService and (WebMenuItem != null) )
         var header = "Формирование отчета " + WebMenuItem.szNameItem;
         ProgressIndicator.Start(iCount, header, header);
       else
         ProgressIndicator.Start(iCount, "Отчет \"Расходы на формирование резервов под обесценение.\"", "Отбор данных для отчета" );
       end;

   /* отбор данных */
       rsd = TRsbDataSet(sql_sel + sql);
       while(rsd.MoveNext())
          ClearRecord( dl_tick );
          dl_tick.DealID = rsd.DealID;
          if( GetEQ( dl_tick ) )

              if( FindDL_LEG(   GetLegKindByDealBuy(dl_tick),
                               dl_tick.DealID, 0, dl_leg ) == 0 )

                  ClearRecord(wrtsum.rec);
                  wrtsum.rec.FIID = AvoirissFI.FIID;
                  wrtsum.rec.Date = SQL_ConvTypeDate(rsd.BuyDate);
                  wrtsum.rec.Buy_Sale = PM_WRITEOFF_SUM_BUY;

                  if( IsPrint != true )
                     ObjReport.PrintIssuerName(AvoirissFI.Name); /*выводим наименование выпуска ц\б*/
                     IsPrint = true;
                  end;

                  InsertLotInReport( rsd.Qost, rsd.Qostprev, wrtsum.rec, rsd );

              end;
          end;
          Num = Num + 1;
          ProgressIndicator.Update(Num,iCount); // всего 3 парамера Update(index, count, text)
       end;

       if( IsPrint != false )
           merge("N1_Issuer", "N1_PrevRurMarket");
           PrintTableLine( "N1_Issuer:c", "Итого по выпуску: " + AvoirissFI.Name, null,
                           "N1_SumOnDate:r", NumPrec(ItogSumOnDate, 2, IIF(IsApp, "a", ""), false), null,
                           "N1_SumOnPrevDate:r", NumPrec(ItogSumOnPrevDate, 2, IIF(IsApp, "a", ""), false), null,
                           "N1_CorrSum1:r", NumPrec(ItogCorrSum1, 2, IIF(IsApp, "a", ""), false), null,
                           "N1_CorrSum2:r", NumPrec(ItogCorrSum2, 2, IIF(IsApp, "a", ""), false), null
                         );
           if( IsPrintFull == false)
               IsPrintFull = true;
           end;
       end;

       FillFullItog();

       ProgressIndicator.Stop();
     end;
 
     private macro ОтчетПоВсемБумагам()
        var
           AvoirissFI = TBFile( "fininstr", "R", 3 ),
           Query, stat, datenotfind, AddWhere = "", i = 0;
        var ProgressValue = CTxProgressValue();

        Query =  "t_FI_Kind = " + string(FIKIND_AVOIRISS)
               + " and t_IsSYS != 'X'"
               + " and t_Issued <= " + GetSQLDate(RepDate)
               + " and ( t_ExpiryDate = " + GetSQLDate(ZeroDate)
               + "       or t_ExpiryDate >= " + GetSQLDate(PrevRepDate)
               + "         )";


        if((ValType(AvrType) != V_Integer ) and (AvrID.Size == 0))
          while( i < AvrType.Size )
            if(AddWhere == "" )
              AddWhere = string(AvrType[i]);
            else
              AddWhere = AddWhere + ", " + string(AvrType[i]) ;
            end;
            i = i + 1;
          end;
        else
          AddWhere = string(AvrType);
        end;


        if(((ValType(AvrType) == V_Integer ) and ( AvrType >= 0 )) or 
           ((ValType(AvrType) != V_Integer ) and (AvrType.Size != 0)))
           Query = Query + " and t_AvoirKind in ( " +
                                                " SELECT listavr.t_AvoirKind " +
                                                " FROM davrkinds_dbt listavr " +
                                                " START WITH listavr.t_FI_Kind = 2 AND listavr.t_AvoirKind in (" + AddWhere + ") " +
                                                " CONNECT BY listavr.t_FI_Kind = PRIOR listavr.t_FI_Kind AND PRIOR listavr.t_AvoirKind = listavr.t_Parent " +
                                                " ) ";
        end;

        if( ValType(AvrID) != V_Integer )
          AddWhere = "";
          while( i < AvrID.Size )
            if(AddWhere == "" )
              AddWhere = " and  t_FIID in (" + string(AvrID[i]);
            else
              AddWhere = AddWhere + " ," + string(AvrID[i]) ;
            end;
            i = i + 1;
          end;
          if( AddWhere != "" )
            AddWhere = AddWhere + " )";
          end;
          Query = Query + AddWhere;
        end;

        if( AvrKindRUR )
            Query = Query + " and t_FaceValueFI = " + string(NATCUR);
        end;

        if( AvrKindCUR )
           Query = Query + " and t_FaceValueFI != " + string(NATCUR);
        end;

        AvoirissFI.Clear;
        AvoirissFI.AddFilter( Query );

        ProgressValue.Maximum = AvoirissFI.NRecords;
        ProgressValue.Current = 0;

        var ProgressIndicator = CreateProgressIndicator(m_IsWebService);

        if( m_IsWebService and (WebMenuItem != null) )
          var header1 = "Формирование отчета " + WebMenuItem.szNameItem;
          ProgressIndicator.Start(ProgressValue.Maximum, header1, header1);
        else
          ProgressIndicator.Start(  ProgressValue.Maximum, "Отчет \"Расходы на формирование резервов под обесценение.\"", "Просмотр ценных бумаг" );
        end;

        while( AvoirissFI.Next )

          ОтчетПоБумаге( AvoirissFI.rec );

             ProgressValue.Current = ProgressValue.Current + 1;
          ProgressIndicator.Update(ProgressValue.Current,ProgressValue.Maximum); // всего 3 парамера Update(index, count, text)
          end;

        ProgressIndicator.Stop();
     end; 
                        

     var err, TaxRegDate_str:string, TaxRegDate:date,;
     file AvoirissFI(fininstr) key 3;
     var errcode, errtext;
 
     ObjReport.BeginReport(); 

     if( RepDate > {CurDate} )
        MsgBoxEx( "Отчет не может быть сформирован за дату, превышающую текущую операционную дату." ); /*!!!*/
        exit(1);
     end;

     GetRegistryValue("SECUR\\DATE_BUILD_TAXREG", V_STRING, TaxRegDate_str, err);
     if(err == 0)
        TaxRegDate = date(TaxRegDate_str);
        if(TaxRegDate < RepDate)
           MsgBoxEx( "Не построены связи налогового учета на дату отчета" );
        end;
     else
        MsgBoxEx( "Ошибка при определении настройки \"SECUR\\DATE_BUILD_TAXREG\"" );
        exit(1);
     end;

     if(( AvrID != -1 ) AND ( ValType(AvrID) == V_Integer ))
        message( "Проверка параметров очета ..." );

        if( ПолучитьФинИн( AvrID, AvoirissFI ) != 0 ) 
           MsgBoxEx( "Не найдена ценная бумага, заданная в параметрах отчета." );
           exit(1);
        end;
     end;

     Message( "Выполнение отчета \"Расходы на формирование резервов под обесценение\" ..." );

     ClearFullItog();

     if(( AvrID != -1 ) AND ( ValType(AvrID) == V_Integer ))
        ОтчетПоБумаге( AvoirissFI );
     else
        ОтчетПоВсемБумагам();
     end; 

     if( IsPrintFull != false )
        merge("N1_Issuer", "N1_PartIncomeRur");
        PrintTableLine( "N1_Issuer:c", "Итого " , null,
                           "N1_SumOnDate:r", NumPrec(FullItogSumOnDate, 2, IIF(IsApp, "a", ""), false), null,
                           "N1_SumOnPrevDate:r", NumPrec(FullItogSumOnPrevDate, 2, IIF(IsApp, "a", ""), false), null,
                           "N1_CorrSum1:r", NumPrec(FullItogCorrSum1, 2, IIF(IsApp, "a", ""), false), null,
                           "N1_CorrSum2:r", NumPrec(FullItogCorrSum2, 2, IIF(IsApp, "a", ""), false), null
                         );
     end;

     ObjReport.EndReport();
     PrintFormatString( Footer, "N1_F0_1:l", RepDate);

  END;

  PRIVATE MACRO Create()
     ExecuteReport( CResrvReg(this) );
  END;                       
  /*Кэшируемые данные для 1 записи отчета */
  PRIVATE VAR m_Issuer, m_BuyDate, m_PacketNum, m_DealKind, m_CurPrice, m_RurPrice, m_NomOnBuyDate, m_OutLay, m_MaxPrice, m_MaxSource, m_MaxDate, 
              m_Difference, m_ItogPrice, m_AmountOnDate, m_AmountOnPrevDate,
              m_CurMarket, m_RurMarket, m_PrevCurMarket, m_PrevRurMarket, m_NomOnDate, m_NomOnPrevDate, m_PartIncomeCur, m_PartIncomeRur,
              m_SumOnDate, m_SumOnPrevDate, m_CorrSum1, m_CorrSum2, m_InterDepend;

  MACRO ClearCacheOneRecord()
     m_Issuer           = null;
     m_BuyDate          = null;
     m_PacketNum        = null;
     m_DealKind         = null;
     m_CurPrice         = null;
     m_RurPrice         = null;
     m_NomOnBuyDate     = null;
     m_OutLay           = null;
     m_MaxPrice         = null;
     m_MaxSource        = null;
     m_MaxDate          = null;
     m_Difference       = null;
     m_ItogPrice        = null;
     m_AmountOnDate     = null;
     m_AmountOnPrevDate = null;
     m_CurMarket        = null;
     m_RurMarket        = null;
     m_PrevCurMarket    = null;
     m_PrevRurMarket    = null;
     m_NomOnDate        = null;
     m_NomOnPrevDate    = null;
     m_PartIncomeCur    = null;
     m_PartIncomeRur    = null;
     m_SumOnDate        = null; 
     m_SumOnPrevDate    = null;
     m_CorrSum1         = null;
     m_CorrSum2         = null;
     m_InterDepend      = null;
  END;

  MACRO Issuer():STRING
   file AvoirissFI(fininstr) key 3;
   file Avoiriss(avoiriss); 
      if( ПолучитьФинИн( SecFIID, AvoirissFI, Avoiriss ) == 0 )
         m_Issuer = Avoiriss.ISIN;
      else
         m_Issuer = "";
      end;
     return m_Issuer;
  END;

  MACRO BuyDate():DATE
     m_BuyDate = DateBuy;
     return m_BuyDate;
  END;

  MACRO PacketNum():STRING
     m_PacketNum = DealCode;
     return m_PacketNum;
  END;

  MACRO DealKind():STRING
     m_DealKind = DealKindName;
     return m_DealKind;
  END;

  MACRO TGO():STRING
     if(LotOrigin != TXLOTORIGIN_DEAL)
       return DL_GetNameAlg( ALG_SCTX_LOTORIGIN, LotOrigin );
     end;
     return "";
  END;

  MACRO CurPrice():STRING     
     m_CurPrice = NumPrec(PriceCUR, 4, IIF(IsApp, "a", ""), true);
     return m_CurPrice;
  END;

  MACRO RurPrice():STRING     
     m_RurPrice = NumPrec(PriceRUR, 4, IIF(IsApp, "a", ""), false);
     return m_RurPrice;
  END;


  MACRO NomOnBuyDate():STRING
     m_NomOnBuyDate = NumPrec(NominalOnBuyDate, 4, IIF(IsApp, "a", ""), true);
     return m_NomOnBuyDate;
  END;

  MACRO OutLay():STRING
     m_OutLay = NumPrec(OutlayRUR, 4, IIF(IsApp, "a", ""), false);
     return m_OutLay;
  END;

  MACRO MaxPrice():STRING
     m_MaxPrice = NumPrec(MaximumPrice, 4, IIF(IsApp, "a", ""), false);
     return m_MaxPrice;
  END;

  MACRO MaxSource():STRING
     m_MaxSource = MaximumSource;
     return m_MaxSource;
  END;

  MACRO MaxDate():DATE
     m_MaxDate = MaximumDate;
     return m_MaxDate;
  END;

  MACRO Difference():STRING
     m_Difference = NumPrec(PriceDifference, 4, IIF(IsApp, "a", ""), false);
     return m_Difference;
  END;

  MACRO ItogPrice():STRING
      m_ItogPrice = NumPrec(ItogPriceRUR, 4, IIF(IsApp, "a", ""), false);
      return m_ItogPrice;
  END;

  MACRO AmountPrecision():INTEGER
      If( (IsHaveFractPart(Qost)) OR (IsHaveFractPart(QostPrev)) )
         return 6;
      else
         return 0;
      end;
  END;

  MACRO AmountOnDate():STRING
      m_AmountOnDate = NumPrec(Qost, AmountPrecision(), IIF(IsApp, "a", ""), true);
      return m_AmountOnDate;
  END;

  MACRO AmountOnPrevDate():STRING
      m_AmountOnPrevDate = NumPrec(QostPrev, AmountPrecision(), IIF(IsApp, "a", ""), true);
      return m_AmountOnPrevDate;
  END;

  MACRO CurMarket():STRING
      m_CurMarket = NumPrec(MarketPriceCUR, 4, IIF(IsApp, "a", ""), true);       
      return m_CurMarket;
  END;

  MACRO RurMarket():STRING
      m_RurMarket = NumPrec(MarketPriceRUR, 4, IIF(IsApp, "a", ""), false);
      return m_RurMarket;
  END;

  MACRO PrevCurMarket():STRING
      m_PrevCurMarket = NumPrec(PrevMarketPriceCUR, 4, IIF(IsApp, "a", ""), true);
      return m_PrevCurMarket;
  END;

  MACRO PrevRurMarket():STRING
      m_PrevRurMarket = NumPrec(PrevMarketPriceRUR, 4, IIF(IsApp, "a", ""), false);
      return m_PrevRurMarket;
  END;

  MACRO NomOnDate():STRING    
     m_NomOnDate = NumPrec(NominalOnRepDate, 4, IIF(IsApp, "a", ""), true);
     return m_NomOnDate;
  END;

  MACRO NomOnPrevDate():STRING    
     m_NomOnPrevDate = NumPrec(NominalOnPrevRepDate, 4, IIF(IsApp, "a", ""), true);
     return m_NomOnPrevDate;
  END;

  MACRO PartIncomeCur():STRING    
     m_PartIncomeCur = NumPrec(PartialIncomeCur, 4, IIF(IsApp, "a", ""), true);
     return m_PartIncomeCur;
  END;

  MACRO PartIncomeRur():STRING    
     m_PartIncomeRur = NumPrec(PartialIncomeRur, 4, IIF(IsApp, "a", ""), true);
     return m_PartIncomeRur;
  END;


  MACRO SumOnDate():STRING
     m_SumOnDate = NumPrec(Reserv, 2, IIF(IsApp, "a", ""), false);
     ItogSumOnDate = ItogSumOnDate + Reserv;  
     return m_SumOnDate;
  END;

  MACRO SumOnPrevDate():STRING
     m_SumOnPrevDate = NumPrec(ReservPrev, 2, IIF(IsApp, "a", ""), false);
     ItogSumOnPrevDate = ItogSumOnPrevDate + ReservPrev;
     return m_SumOnPrevDate;
  END;

  MACRO CorrSum1():STRING
     Var Corr;
     /*Если <A9>  >   <A10>, то <A11> = <A9> - <A10>,
       иначе -  <A11> = 0*/
     if( Reserv > ReservPrev ) 
        Corr = Reserv - ReservPrev;
     else
        Corr = 0;
     end;
     m_CorrSum1 = NumPrec(Corr, 2, IIF(IsApp, "a", ""), false);
     ItogCorrSum1 = ItogCorrSum1 + Corr;
     return m_CorrSum1;
  END;

  MACRO CorrSum2():STRING
     Var Corr;
     /*Если <A9>  <   <A10>, то <A12> = <A10> - <A9>,
       иначе -  <A12> = 0*/
     if( Reserv < ReservPrev ) 
        Corr = ReservPrev - Reserv;
     else
        Corr = 0;
     end; 
     m_CorrSum2 = NumPrec(Corr, 2, IIF(IsApp, "a", ""), false);
     ItogCorrSum2 = ItogCorrSum2 + Corr;
     return m_CorrSum2;
  END;
 
  MACRO InterDepend():STRING
     m_InterDepend = InterDependVal;
     return m_InterDepend;
  END;

  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );  

END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  if( not IsWebService )
      ReservReg_Form = SP_ReservReg_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      ReservReg_Form = WS_ReservReg_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      if( CheckTaxDepositoryMode() == false ) /*если не включен режим хранилища данных для налогового учета*/
       stat = ReservReg_Form.Run();
      else
        msgbox("Отчет не доступен в режиме хранилища");
        stat = false;
      end;
    end;

    if( stat )
      ReservReg_Report = SP_ReservReg_Report( null, "Report_TxResrvAvr.xls", null, IsWebService, ReportHashCode );      
      ReservReg_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat( DL_OUTREPORT_EXCEL, menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
        FullReportFileNames = ReservReg_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat( DL_OUTREPORT_EXCEL );
      end;
    end;
  end;

  return FullReportFileNames;
END;
