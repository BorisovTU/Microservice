/*
$Name:          ws_dealhist.mac
$Module:        АРНУ
$Description:   Макрос сервисов скроллинга и панели сделок, погашений и конвертаций ц/б
*/

import "cb_sql.mac";
import "ws_validation.mac";

// класс CScDealHistList для заполнения скроллинга "Список изменений условий сделки"
class CScDealHistList
  var Instance:Integer;       // Экземпляр
  var ChangeDate:Date;        // Дата изменения
  var ChangeKind:Integer;     // Действие
  var ChangeKindName:String;  // Действие
  var ID:Integer;             // Ссылка на запись в dsptkchng_dbt
  var DealID:Integer;         // Идентификатор сделки
  var PrevID:Integer;         // ID предыдущей записи
end;

macro ScDealHistRunError( err, stat:integer )
  WSRunError( "ws_dealhist.mac", err, stat );
end;

// отбор данных для скроллинга "Список изменений условий сделки"
macro GetScDealHistList(
  DealID:integer // ID сделки
)
  var result = TArray();
  var query = "";
  var cmd = null;
  var ds = null;
  var ScDealHistList = null;
  var stat:integer = -1;

  if( (DealID == null) or (DealID <= 0) )
     RunError("Не задан обязательный параметр функции: идентификатор сделки ");
  end;

  query = " SELECT "
            + " hist.t_Instance "
           + " ,hist.t_ChangeDate "
           + " ,hist.t_ChangeKind "
           + " ,hist.t_ID "
           + " ,NVL(LAG(hist.t_ID) OVER (PARTITION BY hist.t_DealID ORDER BY hist.t_Instance), 0) AS t_PrevID "
        + " FROM "
            + " sp_tickhist hist "
        + " WHERE "
            + " hist.t_DealID = ? "
        + " ORDER BY "
            + " hist.t_Instance ASC ";

  cmd = RSDCommand( query );
  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, DealID );
  cmd.execute();

  ds = TRsbDataSet( cmd );

  while( ds.MoveNext() )
     ScDealHistList = CScDealHistList();

     ScDealHistList.DealID         = DealID;
     ScDealHistList.Instance       = SQL_ConvTypeInteger(ds.Instance);
     ScDealHistList.ChangeDate     = SQL_ConvTypeDate(ds.ChangeDate);
     ScDealHistList.ChangeKind     = SQL_ConvTypeInteger(ds.ChangeKind);
     ScDealHistList.ID             = SQL_ConvTypeInteger(ds.ID);
     ScDealHistList.PrevID         = SQL_ConvTypeInteger(ds.PrevID);

     if( ScDealHistList.ChangeKind == 0 )
        ScDealHistList.ChangeKindName = "Исходные условия";
     elif( ScDealHistList.ChangeKind == 1 )
        ScDealHistList.ChangeKindName = "Изменение условий";
     elif( ScDealHistList.ChangeKind == 2 )
        ScDealHistList.ChangeKindName = "Отказ от исполнения второй части";
     elif( ScDealHistList.ChangeKind == 3 )
        ScDealHistList.ChangeKindName = "Отказ от исполнения сделки";
     elif( ScDealHistList.ChangeKind == 4 )
        ScDealHistList.ChangeKindName = "Изменение срока";
     elif( ScDealHistList.ChangeKind == 5 )
        ScDealHistList.ChangeKindName = "Учет погашения купона";
     elif( ScDealHistList.ChangeKind == 6 )
        ScDealHistList.ChangeKindName = "Конвертация выпусков";
     elif( ScDealHistList.ChangeKind == 7 )
        ScDealHistList.ChangeKindName = "Учет частичного погашения";
     elif( ScDealHistList.ChangeKind == 8 )
        ScDealHistList.ChangeKindName = "Учет компенсационного платежа";
     elif( ScDealHistList.ChangeKind == 9 )
        ScDealHistList.ChangeKindName = "Отказ от исполнения просроченной сделки";
     elif( ScDealHistList.ChangeKind == 10 )
        ScDealHistList.ChangeKindName = "Глобальное перемещение";
     elif( ScDealHistList.ChangeKind == 11 )
        ScDealHistList.ChangeKindName = "Изменение номинала";
     elif( ScDealHistList.ChangeKind == 12 )
        ScDealHistList.ChangeKindName = "Конвертация выпусков";
     elif( ScDealHistList.ChangeKind == 13 )
        ScDealHistList.ChangeKindName = "Частичная поставка/оплата";
     elif( ScDealHistList.ChangeKind == 14 )
        ScDealHistList.ChangeKindName = "Исполнение отложенного расчета по DVP";
     end;

     result[result.Size] = ScDealHistList;
  end;

  return result;

OnError( err )
  ScDealHistRunError( err, stat );
end;
