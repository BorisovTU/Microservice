/*
$Name:        scinc300.mac
$Module:      Ценные бумаги
$Description: Операция начисления ПДД. Шаг "начисление ПДД"
*/
import "spserv.mac", "scincfun.mac";
private record Avoiriss( avoiriss );
private var Fininstr = TRecHandler("fininstr");

private macro IsAverageMetod()
   var cmd, query;
   var IsAverage = 0;

   cmd=RSDCommand( " select NVL(t_MethodID, -1) met from dpmwrtmet_dbt " +
                   "  where t_Party = -1 and t_Contract = 0  ");
   cmd.execute();

   query = TRsbDataSet(cmd);
   if( query.movenext() and  query.Met == PM_WRITEOFF_AVERAGE )
     IsAverage = 1;
   end;                            

   return IsAverage;
end;

private macro isRateOnDateExists( CurDate:Date )
  var Select, DataSet, Query;
    
  Query = " select 1 from dratedef_dbt rtd where rtd.t_OtherFI = ? AND rtd.t_Type = 15 /*НКД на одну ц/б*/ AND (rtd.t_SinceDate = ? "+
          " or exists (Select 1 from dratehist_dbt rth where rtd.t_RateID = rth.t_RateID AND rth.t_SinceDate = ?))";
  Select = DL_RSDCommand(Query);
  Select.AddParam(Avoiriss.FIID);
  Select.AddParam(CurDate);
  Select.AddParam(CurDate);
  DataSet = Select.Execute(Query);
  if(DataSet.MoveNext())
    return true;
  end;  
  return false;
end;

private macro isNominalOnDateExists( CurDate:Date )
  var Select, DataSet, Query;
    
  Query = " SELECT 1 FROM DFIVLHIST_DBT WHERE t_FIID = ? AND t_ValKind = 1 AND t_EndDate = ?";
  Select = DL_RSDCommand(Query);
  Select.AddParam(Avoiriss.FIID);
  Select.AddParam(CurDate);
  DataSet = Select.Execute(Query);
  if(DataSet.MoveNext())
    return true;
  end;  
  return false;
end;


private macro checkNDR_FaceValue(CommDate:Date, EndDate:Date, FIID:Integer)
    var cmd;

    cmd = RSDCommand(  
    " declare l_result number; " +
    "   begin " +
    "      l_result := it_rudata.checkNDR_FaceValue(:date, :end_date, :fiid); " +
    "      ?:= l_result;  " +
    "   end; "
    );
    cmd.addParam("date",     RSDBP_IN, CommDate);
    cmd.addParam("end_date", RSDBP_IN, EndDate);
    cmd.addParam("fiid",     RSDBP_IN, FIID);
    cmd.addParam("out_result", RSDBP_OUT, V_INTEGER);
    cmd.execute();

    var outResult = cmd.value("out_result"); 

    if(outResult == 1)
        return true;
    end;

    return false;

end; 

private macro GetLastDayInMonth( CurDate:Date )
    var day, mon, year;

    DateSplit(CurDate,day,mon,year);
    if( mon == 12 )
      return Date(1,1,year+1) - 1;
    else
      return Date(1,mon+1,year) - 1;
    end;
end;

private macro ЕстьВРепоКупон( ScOprServDoc:variant, Avoriss:variant):integer
  var query, cmd, DataSet;
  var RetVal:integer = 0;

  query =   " WITH "
            + "    q "
            + "    AS "
            + "        (SELECT oblig.t_FIID, "
            + "                oblig.t_FI_Code, "
            + "                oblig.t_Name, "
            + "                oblig_fiw.T_DRAWINGDATE "
            + "           FROM dfininstr_dbt oblig, dfiwarnts_dbt oblig_fiw "
            + "          WHERE     RSB_FIInstr.FI_AvrKindsGetRoot (oblig.t_FI_Kind, "
            + "                                                    oblig.t_AvoirKind) = " + AVOIRISSKIND_BOND
            + "                AND oblig.t_FIID = ? "
            + "                AND oblig_fiw.t_FIID = oblig.t_FIID "
            + "                AND oblig_fiw.T_DRAWINGDATE =  "
            + "             (SELECT MIN (tmp.T_DRAWINGDATE) "
            + "                 FROM dfiwarnts_dbt tmp "
            + "                WHERE     tmp.t_FIID = oblig.t_FIID "
            + "                      AND tmp.t_SPIsClosed = CHR (0) "
            + "                      AND tmp.t_IsPartial = CHR (0))) "
            + "SELECT q.t_FI_Code, "
            + "       q.t_Name, "
            + "       q.T_DRAWINGDATE "
            + "  FROM q, ddl_tick_dbt tk "
            + " WHERE     EXISTS "
            + "               (SELECT leg1.* "
            + "                  FROM ddl_leg_dbt leg1 "
            + "                 WHERE     leg1.t_pfi = q.t_FIID "
            + "                       AND leg1.t_dealid = tk.t_dealid "
            + "                       AND leg1.t_legid = 0 "
            + "                       AND leg1.t_legkind = " + LEG_KIND_DL_TICK_BACK
            + "                       AND leg1.t_expiry > ?) "
            + "       AND EXISTS "
            + "               (SELECT leg2.* "
            + "                  FROM ddl_leg_dbt leg2 "
            + "                 WHERE     leg2.t_pfi = q.t_FIID "
            + "                       AND leg2.t_dealid = tk.t_dealid "
            + "                       AND leg2.t_legid = 0 "
            + "                       AND leg2.t_legkind = " + LEG_KIND_DL_TICK_BACK
            + "                       AND leg2.t_expiry > q.T_DRAWINGDATE) "
            + "       AND ? > q.T_DRAWINGDATE "
            + "       AND RSB_SECUR.IsREPO ( "
            + "               rsb_secur.get_OperationGroup ( "
            + "                   rsb_secur.get_OperSysTypes (tk.t_DealType, "
            + "                                               tk.t_BofficeKind))) = 1 "
            + "       AND tk.T_DEALSTATUS = 10 "
            + "       AND NOT EXISTS "
            + "               (SELECT grdeal.* "
            + "                  FROM ddlgrdeal_dbt grdeal, ddlgracc_dbt gracc"
            + "                 WHERE     grdeal.t_DocKind = tk.t_BOfficeKind "
            + "                       AND grdeal.t_DocID = tk.t_DealID "
            + "                       AND grdeal.t_TemplNum = " + DLGR_TEMPL_COUP 
            + "                       AND gracc.t_GrDealID = grdeal.t_ID "
            + "                       AND gracc.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING
            + "                       AND gracc.t_State = " + DLGRACC_STATE_FACTEXEC
            + "                 )";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(Avoiriss.FIID);
  cmd.AddParam(ScOprServDoc.EndDate);
  cmd.AddParam(ScOprServDoc.EndDate);

 
  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    ScOpReportRepoIncomeIn[ScOpReportRepoIncomeIn.size] = CSC_RepoIncomeIN(RepoIncomeIn_Kind, DataSet.FI_Code, DataSet.Name, SQL_ConvTypeDate(DataSet.DrawingDate));
    RetVal = 1;
  end;

  return RetVal;
end;

macro ExecuteStep( D, FirstDoc, _DocKind, ID_Operation, ID_Step )
  var query, cmd, DataSet;
  SetBuff( Avoiriss, FirstDoc );
  if( ПолучитьФинИн( Avoiriss.FIID, Fininstr ) )
    msgbox( "Не найден финансовый инструмент FIID = " + string(Avoiriss.FIID) );
    return 1;
  end;

  ScOpReportLineData.size = 0;
  ScOpReportNullFiwarnts.size = 0;
  ScOpReportRepoIncomeIn.size = 0;

  if(ExistPlanGrDealBeforeDate(Avoiriss.FIID, ScOprServDoc.CommDate) == true)
    msgbox("В строках графиков исполнения сделок по обрабатываемой бумаге есть запланированные действия за более раннюю дату");
    return 1;
  end;

  if( IsAverageMetod() )   // для средневзвеса проверяем за предыдущую дату
    if( ExistPlanGrDealBeforeDate2(Avoiriss.FIID, ScOprServDoc.EndDate-1) == true)
      msgbox("В строках графиков исполнения сделок по обрабатываемой бумаге обработаны не все действия за даты, предшествующие периоду начисления");
      return 1;
    end;
  else
    if( ExistPlanGrDealBeforeDate2(Avoiriss.FIID, ScOprServDoc.EndDate) == true)
      msgbox("В строках графиков исполнения сделок по обрабатываемой бумаге обработаны не все действия за дату окончания периода начисления или предшествующие");
      return 1;
    end;
  end;

  if( ((Avoiriss.IndexNom == SET_CHAR) OR (Avoiriss.FloatingRate == SET_CHAR)) AND (ScOprServDoc.EndDate == GetLastDayInMonth(ScOprServDoc.EndDate)) AND IsHoliday(ScOprServDoc.EndDate) )
    if(not isRateOnDateExists(ScOprServDoc.EndDate))
      msgbox("По выпуску "+Fininstr.rec.Name+" отсутствует курс вида 'НКД на одну ц/б' за дату "+string(ScOprServDoc.EndDate)+", являющуюся последним днем месяца и выходным");
      return 1;
    end;
    /*
    if ((Avoiriss.IndexNom == SET_CHAR) AND (not isNominalOnDateExists(ScOprServDoc.EndDate)))
      msgbox("По выпуску облигации с ИН "+Fininstr.rec.Name+" отсутствует номинал за дату "+string(ScOprServDoc.EndDate)+", являющуюся последним днем месяца и выходным");
      return 1;
    end;
    */
  end;

  if(Avoiriss.IndexNom == SET_CHAR)
    if(checkNDR_FaceValue(ScOprServDoc.CommDate, ScOprServDoc.EndDate, Avoiriss.FIID)) 
      msgbox("По выпуску  облигации с ИН " + Avoiriss.ISIN  + " отсутствует номинал за дату " + ScOprServDoc.EndDate + ", являющуюся последним/первым днем месяца и выходным");
      return 1;
    end;
  end;
  
  if( (ПолучитьНКДПоКурсу( Avoiriss.FIID, ScOprServDoc.EndDate ) <= 0) and РАСЧЕТ_НКД_ЧЕРЕЗ_КУРС() )
     if( not GetTrue( TRUE, "Для ценной бумаги с кодом " + ПолучитьКодФинИн(Avoiriss.FIID) + " не найден курс вида " + int(ВИД_КУРСА_НКД_ДЛЯ_ЦБ()) + "\n Продолжить расчет ПД по анкете ценной бумаги?" ) )
        return 0;
     end;
  end;

  if( ЕстьНулевыеКупоны( Avoiriss.FIID, ScOprServDoc.EndDate ) )
     if( not GetTrue(TRUE, "Для ценной бумаги с кодом " + ПолучитьКодФинИн(Avoiriss.FIID) + " есть купоны с нулевой ставкой и суммой дохода. \n Продолжить расчет ПД по анкете ценной бумаги?" ) )
        return 0;
     end;
  end;

  query =   " select fin.t_FI_Code, fin.t_Name, "
          + "        (select count(1) "
          + "           from dfiwarnts_dbt fiw "
          + "          where fiw.t_FIID = fin.t_FIID "
          + "            and fiw.t_IsPartial = CHR(0) "
          + "            and fiw.t_IncomeRate = 0 "
          + "            and fiw.t_IncomeVolume = 0 "
          + "            and fiw.t_FirstDate <= ? "
          + "            and rownum = 1"
          + "        ) as CntNullCoup, "
          + "        (select count(1) "
          + "           from dfiwarnts_dbt fiw "
          + "          where fiw.t_FIID = fin.t_FIID "
          + "            and fiw.t_IsPartial = 'X' "
          + "            and fiw.t_IncomeRate = 0 "
          + "            and fiw.t_IncomeVolume = 0 "
          + "            and fiw.t_FirstDate <= ? "
          + "            and rownum = 1"
          + "        ) as CntNullPart "
          + "   from dfininstr_dbt fin "
          + "  where fin.t_FIID = ? "
          + "    and RSB_FIInstr.FI_AvrKindsGetRoot(fin.t_FI_Kind, fin.t_AvoirKind ) = " + AVOIRISSKIND_BOND;

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ScOprServDoc.EndDate);
  cmd.AddParam(ScOprServDoc.EndDate);
  cmd.AddParam(Avoiriss.FIID);
  
  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    if((DataSet.CntNullCoup > 0) or (DataSet.CntNullPart > 0))
      ScOpReportNullFiwarnts[ScOpReportNullFiwarnts.size] = CSC_NullFiwarnts(INCOME_NULLFIWARNTS, DataSet.FI_Code, DataSet.Name, DataSet.CntNullCoup, DataSet.CntNullPart);
    end;
  end;

  if(ЕстьВРепоКупон(ScOprServDoc, Avoiriss))
    return 0;
  end;
  
  return ВыполнитьНачислениеПоБумаге( ScOprServDoc, Avoiriss, ID_Operation, ID_Step, (ScOprServDoc.Flag1 == SET_CHAR), (ScOprServDoc.Flag4 == SET_CHAR) );
end;

/* Макрос постобработки */
MACRO PostStep( CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,           /* Статус выполнения шага-!0-произошла ошибка */
                FirstDoc,         /* Указатель на первичный документ */
                ID_Operation,     /* Номер экземпляра операции */
                Num_Step,         /* Номер шага операции(из настроек) */
                Kind_Operation,   /* Вид операции */
                KindDoc,          /* Вид первичного документа */
                KindStep,         /* Вид шага операции */
                ID_Step )         /* Номер шага операции */

  SetBuff( Avoiriss, FirstDoc ); 

  InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
  ClearErrorArr();
  if( errTrn OR (CommitOrRollback == 2) ) /* Произошла ошибка или происходит откат */
     if( CommitOrRollback == 2 )
        DL_DeleteDataByStep_XML(ID_Operation, ID_Step);
     end;
     return;
  else
     if( ScOpReportLineData.size > 0 )
        DL_SaveDataForReport_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind, ScOpReportLineData, LOG_TYPE_DATA, ID_Operation, ID_Step);
     end;

     if( ScOpReportNullFiwarnts.size > 0 )
        DL_SaveDataForReport_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind, ScOpReportNullFiwarnts, LOG_TYPE_DATA, ID_Operation, ID_Step);
     end;

     If ( ScOpReportRepoIncomeIn.size > 0 )
        DL_SaveDataForReport_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind, ScOpReportRepoIncomeIn, LOG_TYPE_DATA, ID_Operation, ID_Step);
     End;
  end;

  return 0;
END;
