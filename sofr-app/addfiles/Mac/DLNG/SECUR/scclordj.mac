/**
 @file 		mac\dlng\secur\scclordj.mac
 @brief 	Отчет "Журнал регистрации поручений клиента"

 # menu
 - ЦБ \ Отчеты \ РСХБ \ [ВУ06 Журнал поручений кл на соверш сделки]

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |24.09.2025 |Велигжанин А.В.|DEF-102467                                      |Доработка для поручений по сделкам ОТС на бирже
 |           |               |                                                |Создание
*/
import "sp_class.mac", "dlclordcj.mac";

private const Debug_IncludeSelfDeals = false;  /* Отладка - включить собственные сделки.
                                               Нужно для проверки работы отчета с
                                               историей изменений сделки. */
private record sfcontr(sfcontr);

macro GetDealChangeInfo( deal, IsExistBack, SPGroundID:integer )
  var
     Leg1           = TRecHandler( "dl_leg" ),
     Leg2           = TRecHandler( "dl_leg" ),
     ChangeInfo     = "",
     RejectInfo     = "",
     ArrDateChange, counter;

  /* A5 - Отметки об изменениях
     Для включенной настройки "Отображать изменение условий
     сделок в отчетах", если по сделке были изменения условий,
     то выводится текст "изм. условий: " и далее через запятую
     даты изменения условий сделок в формате DD.MM.YY */
  if( /*Data.ShowDealChange*/GetOption_ShowDealChange )

     /* Получим список дат изменений. */
     ArrDateChange = GetArrayDateDealChange( deal );
     if( ArrDateChange.Size > 0 )

        ChangeInfo = "изм. условий: ";

        counter = 0;
        while( counter < ArrDateChange.Size )

           if( counter > 0 )
              ChangeInfo = ChangeInfo + ", ";
           end;

           ChangeInfo = ChangeInfo + DateToStr( ArrDateChange[counter] );

           inc( counter );
        end;
     end;
  end;

       
  /* A6 = Если по сделке был выполнен отказ от второй части, то
     выводится текст "отк. от  2ч". Если был выполнен отказ
     сделки, то выводится текст "отк. от сделки" */

  /* D6 = Если по сделке был выполнен отказ от второй части, то
     выводится дата фактического выполнения шага сделки "Отказ от
     исполнения 2 части". Если был выполнен отказ сделки, то
     выводится дата фактического выполнения шага сделки "Отказ от
     сделки" */
  GetDlLegByDealID( deal.DealID, LEG_KIND_DL_TICK, true, false, Leg1 );
  if( Leg1.rec.RejectDate != ZeroDate )
     RejectInfo = "отк. от сделки " + DateToStr( Leg1.rec.RejectDate );
  elif( IsExistBack )
     GetDlLegByDealID( deal.DealID, LEG_KIND_DL_TICK_BACK, true, false, Leg2 );
     if( Leg2.rec.RejectDate != ZeroDate )
        RejectInfo = "отк. от 2ч " + DateToStr( Leg2.rec.RejectDate );
     end;
  end;

  if( (RejectInfo != "") and (ChangeInfo != "") )
     return ChangeInfo + "\n" + RejectInfo;
  else
     return ChangeInfo + RejectInfo;
  end;
end;

Private Macro CheckReq(Data, clientid, IsExchange)
     if((Data.ClientID > 0) AND (ClientID != Data.ClientID))
       return false;
     end;
     if( ((Data.PlaceExec == DLCLORDCJ_PLACEEXEC_EXCHANGE) and (IsExchange == UNSET_CHAR)) or
         ((Data.PlaceExec == DLCLORDCJ_PLACEEXEC_OUTEXCHANGE) and (IsExchange == SET_CHAR))
       )
       return false;
     end;
       return true;
End;

private macro CheckDeal( Data, tick, OperGroup )

  /* Cписания/зачисления должны быть открытыми и закрытыми*/
  if( (tick.BOfficeKind == DL_AVRWRT) and (tick.DealStatus != DL_CLOSED) AND (tick.DealStatus != DL_READIED) )
     return false;
  end;

  if( not Debug_IncludeSelfDeals )

     /*По клиентам*/
     if(tick.ClientID < 0)
        return false;
     end;

     /*Клиент*/
     if((Data.ClientID > 0) AND (tick.ClientID != Data.ClientID))
       return false;
     end;

  end;

  /*Договор обслуживания*/
  if( not ПолучитьДоговорПоСделке( tick, sfcontr, true ) )
     ClearRecord(sfcontr);
  end;

  if((Data.ContrID > 0) AND (Data.ContrID != sfcontr.Id) )
     return false;
  end;

  /*признак "ПФИ" не установлен */
  if( tick.IsPFI == SET_CHAR )
     return false;
  end;

  if( ((Data.PlaceExec == DLCLORDCJ_PLACEEXEC_EXCHANGE) and (not IsEXCHANGE(OperGroup, true))) or
      ((Data.PlaceExec == DLCLORDCJ_PLACEEXEC_OUTEXCHANGE) and (not IsOUTEXCHANGE(OperGroup, true)))
    )
     return false;
  end;

  return true;
end;

private macro CheckDealForContrD( Data, tick, OperGroup )

  /* Cписания/зачисления должны быть открытыми и закрытыми*/
  if( (tick.BOfficeKind == DL_AVRWRT) and (tick.DealStatus != DL_CLOSED) AND (tick.DealStatus != DL_READIED) )
     return false;
  end;

  /*Контрагент в сделке - Клиент*/
  if( (Data.ClientID > 0) AND (tick.PartyID != Data.ClientID) )
    return false;
  end;

  /*Договор обслуживания*/
  if( (Data.ContrID > 0) AND (Data.ContrID != tick.PartyContrID) )
     return false;
  end;

  if( ((Data.PlaceExec == DLCLORDCJ_PLACEEXEC_EXCHANGE) and (not IsEXCHANGE(OperGroup, true))) or
      ((Data.PlaceExec == DLCLORDCJ_PLACEEXEC_OUTEXCHANGE) and (not IsOUTEXCHANGE(OperGroup, true)))
    )
     return false;
  end;

  return true;
end;

private macro GetDealInfo(deal, ScRepData:ClOrdRegData, OperGroup:integer,  IsContrD:bool, ClientID:@integer, ContrID:@integer, 
                          OperKind:@string, DealChangeInfo:@string, OperID:@integer, Status:@string, SPGroundID:integer )
  var BackPart, ExistBack;

  ExistBack = IsREPO(OperGroup);

  if (IsContrD == false)
     ClientID = deal.ClientID;
     if( ПолучитьДоговорПоСделке( deal, sfcontr ) )
        ContrID = sfcontr.ID;
     end;
  else
     /*Для сделки контрагента Д - клиент=контрагент*/
     ClientID = deal.PartyID;
     /*Договор*/
     ContrID = deal.PartyContrID;
  end;

  OperKind = ПолучитьВидОперации( OperGroup, (ScRepData.ArrCntOrdByDeals[deal.DealID] > 1), 
                                  (ScRepData.ArrCntOrdByDeals[deal.DealID] == 2), true );

  /* Отметки об изменениях. */
  DealChangeInfo = GetDealChangeInfo( deal, ExistBack, SPGroundID );
  OperID = deal.Oper;

  if( (deal.BOfficeKind == DL_SECURITYDOC) and IsEXCHANGE(OperGroup) )
     Status = DL_GetTextStatusReq(SPGroundID);
  else
     Status = "Исполнено";
  end;
end;

macro RunReport( ScRepData: ClOrdRegData )
  file FDeals( "dl_tick" ); 
  var Group, BackPart, ExistBack;
  var OperGroup, ClientID = 0, ContrID = 0, OperKind = "", DealChangeInfo = "", OperID = 0, Status = ""; 
  var sqlQuery = "", sqlData, sql = "";
  var ReqStatus = "";
  if( (ScRepData.SourceFIkind > 0) and (ScRepData.SourceFIkind != DLCLORDCJ_SOURCEFIKIND_AVOIRISS) )
     return; // все сделки только на ЦБ
  end;

  if( ScRepData.PFIKind > 0 )
     return;
  end;

  sql = " select tick.t_DealID, /*ground.t_Xld*/ Req.t_codets t_Xld, ground.t_RegistrDate, ground.t_RegistrTime, ground.t_SPGroundID,  " +
        "        Req.T_AMOUNT Amount, "+
        "        (CASE (Req.T_PRICETYPE) "+
        "              WHEN 1 THEN Req.T_PRICE "+
        "              ELSE (      Req.T_PRICE / 100 * RSB_FIInstr.FI_GetNominalOnDate(Req.t_FIID, tick.t_DealDate) ) "+
        "        END) Price, " +             
        "        (CASE (Req.T_PRICETYPE) "+
        "             WHEN 1 THEN (SELECT f.T_CCY from dfininstr_dbt f where f.t_fiid = Req.t_pricefiid) "+
        "             ELSE        (SELECT f1.T_CCY from dfininstr_dbt f1 where f1.t_fiid = (SELECT f0.t_facevaluefi FROM dfininstr_dbt f0 WHERE f0.t_fiid = Req.t_fiid)) "+
        "        END) PriceName, "+
        "        (SELECT f2.T_FI_CODE from dfininstr_dbt f2 where f2.t_fiid = Req.t_fiid) FICode " +   
        "   from dspground_dbt ground, dspgrdoc_dbt grdoc,  dspgrdoc_dbt grreq, ddl_tick_dbt tick, dparty_dbt Pty, ddl_req_dbt Req " +
        "  where (ground.t_DocLog = " + LLCLASS_KINDDOC_SALEBUYDEAL +
        "         or ground.t_DocLog = " + LLCLASS_KINDDOC_BACKSALEDEAL +
        "         or ground.t_DocLog = " + LLCLASS_KINDDOC_SECUR + ") " +
        "   and tick.t_ClientID = Pty.T_PARTYID " +
        "   and ground.t_RegistrDate = ? " +
        "   and ground.t_Kind = " + DOCKIND_ORD_CLIENTOP +
        "   and grdoc.t_SPGroundID = ground.t_SPGroundID " +
        "   and grreq.t_SPGroundID = ground.t_SPGroundID " +
        "   and grreq.t_SourceDocID <> grdoc.t_SourceDocID " +
        "   and grreq.t_SourceDocKind <> grdoc.t_SourceDocKind " +
        "   and grreq.t_SourceDocKind = Req.T_KIND " +
        "   and grreq.t_SourceDocID = Req.T_ID " +        
        "   and (grdoc.t_SourceDocKind = " + DL_SECURITYDOC +
        "        or grdoc.t_SourceDocKind = " + DL_AVRWRT +
        "        or grdoc.t_SourceDocKind = " + DL_CONVAVR + ") " +
        "   and tick.t_DealID = grdoc.t_SourceDocID ";

  if( (ScRepData.IsForm > 0) and (ScRepData.FormSub != 0) )
    if(ScRepData.IsForm == 1)
      sql = sql + " and Pty.T_LEGALFORM = " + String (ScRepData.FormSub);
    end;
    if(ScRepData.IsForm == 2)             
      sql = sql + " and Pty.T_LEGALFORM != " + String (ScRepData.FormSub);
    end;
  end;
  
  if( (ScRepData.IsVid > 0) and (ScRepData.VidSub != 0) )            
    if(ScRepData.IsVid == 1)
      sql = sql + " and EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (ScRepData.VidSub) +" and Pty.T_PARTYID = Pto.T_PARTYID) ";
    end;
    if(ScRepData.IsVid == 2)              
      sql = sql + " and NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (ScRepData.VidSub)+ " and Pty.T_PARTYID = Pto.T_PARTYID) ";
    end;
  end;

  if(ScRepData.NoResident)
    sql = sql + " and Pty.T_NOTRESIDENT = 'X' ";
  end;

  sql = sql + " and 1 = 2 ";// Golovkin

  sql = sql + " order by tick.t_DealID, ground.t_RegistrTime ";

  sqlQuery = RSDCommand(sql);

  sqlQuery.addParam("", RSDBP_IN, string(ScRepData.CurDate));
  sqlQuery.execute();

  sqlData = TRsbDataSet(sqlQuery);
  while(sqlData.MoveNext())
     ClearRecord(FDeals);
     FDeals.DealID = sqlData.DealID;
     if( GetEQ(FDeals) ) /*если нашли сделку*/
        OperGroup = GetOperationGroup( FDeals.DealType );
        /* Проверим тип сделки - вывод/зачисление*/
        if( IsBUY(OperGroup) OR
            IsAVRWRTIN(OperGroup) OR
            IsCONVERT_SHARE(OperGroup) OR
            IsCONVERT_RECEIPT(OperGroup) OR
            IsSALE(OperGroup) OR
            IsAVRWRTOUT(OperGroup) 
          )
           if( CheckDeal(ScRepData, FDeals, OperGroup) == true )
              /* Подсчитываем к-во поручений по сделке. */
              inc(ScRepData.ArrCntOrdByDeals[FDeals.DealID]);

              if( IsCONVERT_SHARE(OperGroup) and (ScRepData.ArrCntOrdByDeals[FDeals.DealID] > 1) )
                 continue;
              end;
              if( IsCONVERT_RECEIPT(OperGroup) and (ScRepData.ArrCntOrdByDeals[FDeals.DealID] <2) )
                 continue;
              end;

              GetDealInfo(FDeals, ScRepData, OperGroup, false, @ClientID, @ContrID, @OperKind, @DealChangeInfo, @OperID, @Status, sqlData.SPGroundID);

              ScRepData.ReportData[ScRepData.ReportData.Size] = ClOrdRegRepData(1, FDeals.DealID, 
                                                                                sqlData.Xld, SQL_ConvTypeDate(sqlData.RegistrDate), sqlData.RegistrTime,
                                                                                ClientID, ContrID, OperID, OperKind, DealChangeInfo, Status, sqlData.Amount, sqlData.Price, sqlData.PriceName, 
                                                                                sqlData.FICode, sqlData.MethodApplic);
           end;
        end;

        /* Проверим тип сделки - вывод/зачисление - только для сделок контрагента Д */
         if( (FDeals.IsPartyClient == SET_CHAR) and (FDeals.PartyContrID != 0) ) 
            if( IsSALE(OperGroup) OR
                IsBUY(OperGroup)
               )
                 if( CheckDealForContrD(ScRepData, FDeals, OperGroup) == true )
            
                    /* Подсчитываем к-во поручений по сделке. */
                    inc(ScRepData.ArrCntOrdByDeals[FDeals.DealID]);

                    GetDealInfo(FDeals, ScRepData, OperGroup, true, @ClientID, @ContrID, @OperKind, @DealChangeInfo, @OperID, @Status, sqlData.SPGroundID);

                    ScRepData.ReportData[ScRepData.ReportData.Size] = ClOrdRegRepData(1, FDeals.DealID,
                                                                                      sqlData.Xld, SQL_ConvTypeDate(sqlData.RegistrDate), sqlData.RegistrTime,
                                                                                      ClientID, ContrID, OperID, OperKind, DealChangeInfo, Status, sqlData.Amount, sqlData.Price, sqlData.PriceName,  
                                                                                      sqlData.FICode, sqlData.MethodApplic);
                 end;
            end;
         end;

      end;
  end;

/*неисполненные заявки*/
  sql = " select req.*, "+
        "        (CASE (Req.T_PRICETYPE) "+
        "             WHEN 1 THEN (SELECT f.T_CCY from dfininstr_dbt f where f.t_fiid = Req.t_pricefiid) "+
        "             ELSE        (SELECT f1.T_CCY from dfininstr_dbt f1 where f1.t_fiid = (SELECT f0.t_facevaluefi FROM dfininstr_dbt f0 WHERE f0.t_fiid = Req.t_fiid)) "+
        "        END) PriceName, (SELECT f2.T_FI_CODE from dfininstr_dbt f2 where f2.t_fiid = Req.t_fiid) FICode, Req.t_MethodApplic, "+
        "        GROUND.T_RECEPTIONIST t_oper, " +
        "        NVL(RTRIM(rsb_struct.getString(rsi_rsb_kernel.GetNote(149, LPAD(req.t_id, 34, '0'), 102, req.t_date)), CHR(1)), CHR(1)) trader_code " +
        "  from ddl_req_dbt req,  dparty_dbt Pty, dspgrdoc_dbt grreq, dspground_dbt ground  where req.t_client <> -1 /*and req.t_status <> 'M' Golovkin отбираем все заявки */ AND Pty.t_partyid = req.t_client " +
        "   and req.t_Date = ? " +
        "   AND grreq.t_SPGroundID = ground.t_SPGroundID " +
        "   AND grreq.t_SourceDocKind = Req.T_KIND       " +
        "   AND  grreq.t_SourceDocID = Req.T_ID          ";

  if( (ScRepData.IsForm > 0) and (ScRepData.FormSub != 0) )
    if(ScRepData.IsForm == 1)
      sql = sql + " and Pty.T_LEGALFORM = " + String (ScRepData.FormSub);
    end;
    if(ScRepData.IsForm == 2)             
      sql = sql + " and Pty.T_LEGALFORM != " + String (ScRepData.FormSub);
    end;
  end;
  
  if( (ScRepData.IsVid > 0) and (ScRepData.VidSub != 0) )            
    if(ScRepData.IsVid == 1)
      sql = sql + " and EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (ScRepData.VidSub) +" and Pty.T_PARTYID = Pto.T_PARTYID) ";
    end;
    if(ScRepData.IsVid == 2)              
      sql = sql + " and NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (ScRepData.VidSub)+ " and Pty.T_PARTYID = Pto.T_PARTYID) ";
    end;
  end;

  if(ScRepData.NoResident)
    sql = sql + " and Pty.T_NOTRESIDENT = 'X' ";
  end;

  sql = sql + " AND req.t_SourceKind not in (192,199,4813,4815) ";

  sql = sql + " AND (req.t_isexchange = 'X' or req.t_marketkind <> 0) "; // DEF-66528, DEF-102467
 
  sqlQuery = RSDCommand(sql);

  sqlQuery.addParam("", RSDBP_IN, string(ScRepData.CurDate));
  sqlQuery.execute();
  sqlData = TRsbDataSet(sqlQuery);

  while(sqlData.MoveNext())
     ReqStatus = GetStatusReq(sqlData.t_id);//"Отменена";
     
     if( ReqStatus == "Отклонено" ) // Golovkin 20.08.2019 ID : 493172 в столбце "Информация об исполнении поручения" нужно изменить значение с "Отклонено" на "Отменено" 
       ReqStatus = "Отменено";
     end;

     if( CheckReq(ScRepData, sqlData.client, sqlData.IsExchange) == true)
          ScRepData.ReportData[ScRepData.ReportData.Size] = ClOrdRegRepData(1, 0,
                                                                            sqlData.codets, SQL_ConvTypeDate(sqlData.Date), sqlData.Time,
                                                                            sqlData.Client, sqlData.Clientcontr, sqlData.trader_code, "", "", ReqStatus, sqlData.Amount, sqlData.Price, sqlData.PriceName, 
                                                                            sqlData.FICode, sqlData.MethodApplic);

     end;

   end;

end;
