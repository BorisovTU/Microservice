/*
$Name:        nptxhold025.mac
$Module:      Ценные бумаги
$Description: Операция удержания НДФЛ. Шаг "Создание заявления на возврат налога"
*/
IMPORT InsCarryDoc, DealsInter, "secinter.mac", "nptxcalcfun.mac", "nptxreqfun.mac";

PRIVATE VAR Oper = TRecHandler( "nptxop.dbt" );

PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

PRIVATE MACRO MessToRep( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
END;

private macro ExistsNptxopCode(DocKind, Code)
  var query, cmd;

  query = "select 1 from dnptxop_dbt where t_DocKind = ? and t_Code = ?";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(DocKind);
  cmd.AddParam(Code);

  if(cmd.GetCount() > 0)
    return true;
  end;

  return false;
end;

private macro ExistsDlContr(ClientID, CheckDate)
  var query, cmd, DataSet;

  query =   " select 1 "
          + "   from dsfcontr_dbt sf, ddlcontr_dbt dlc "
          + "  where sf.t_PartyID = ? "
          + "    and dlc.t_SfContrID = sf.t_ID "
          + "    and sf.t_DateBegin <= ? "
          + "    and (sf.t_DateClose = " +GetSQLDate(date(0,0,0))+ " or sf.t_DateClose > ?) ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ClientID);
  cmd.AddParam(CheckDate);
  cmd.AddParam(CheckDate);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    return true;
  end;

  return false;
end;

//Найти заяввление на возврат, сформированное в операции удержания с типом "Окончание года"
macro НайтиСуществующееЗаявление(ClientID:integer, TaxPeriod:integer, Req:@TRecHandler):integer
  var query, cmd, DataSet;

  Req.Clear();

  query =    "   SELECT req.* "
           + "     FROM dnptxop_dbt req, doprdocs_dbt oprdoc, doproper_dbt op, dnptxop_dbt nptxop "
           + "    WHERE req.t_DocKind = "+DL_RETREQ
           + "      AND req.t_Client = ? "
           + "      AND Extract(Year from req.t_PrevDate) = ? "
           + "      AND oprdoc.t_DocKind = 4752 /*DL_INSERT_NPTXOP*/ "
           + "      AND oprdoc.t_DocumentID = LPAD(req.t_ID, 34, '0') "
           + "      AND op.t_ID_Operation = oprdoc.t_ID_Operation "
           + "      AND op.t_DocKind = " + DL_HOLDNDFL
           + "      AND nptxop.t_ID = TO_NUMBER(op.t_DocumentID) "
           + " ORDER BY req.t_ID DESC ";

  cmd = DL_RSDCommand();

  cmd.AddParam(ClientID);
  cmd.AddParam(TaxPeriod);

  DataSet = cmd.Execute(query);
  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo(Req.rec);
  end;
end;

PRIVATE MACRO СоздатьЗаявлениеНаВозврат(taxe, taxe15)
  var nptxop_new = TRecHandler( "nptxop" );
  var Req = TRecHandler("nptxop");
  var query, cmd, DataSet;
  var RefID, TaxPeriod;
  var GenByClientList = false;
  var NeedNewReq = false;
  var err = 0;

  datesplit(Oper.rec.PrevDate, null, null, TaxPeriod);

  НайтиСуществующееЗаявление(Oper.rec.Client, TaxPeriod, @Req);

  query = " select * from dnptxholdgen_dbt where t_NptxOpID = ? ";
  cmd = DL_RSDCommand(query);

  cmd.AddParam(Oper.rec.ID);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    //Операция была сгенерирована автоматически
    if(DataSet.ByClientList == SET_CHAR)
       //Если генерация была по списку клиентов
       GenByClientList = true;
    end;
  end;

  if(GenByClientList == true)
    //" Если заявление формируется в ходе Генерации операций удержания / возврата НДФЛ с типом "окончание года" по СПИСКУ клиентов 

    //- Если по клиенту уже существует заявление на возврат в статусе "Отложено" или "Проверено", созданное в операции удержания за этот же налоговый период (сумму к возврату не проверяем), то проверяем статус такого заявления
    if(Req.rec.ID > 0)
      if((Req.rec.Status == DLNPTXRETREQ_REJECTED) or (Req.rec.Status == DLNPTXRETREQ_EXECUTED) or (Req.rec.Status == DLNPTXRETREQ_EXECUTEDBART))
        //если заявление на возврат в статусе "Отклонено" или "Исполнено" или "Исполнено зачетом", то формируем новое заявление на возврат
        NeedNewReq = true;
      elif((Req.rec.Status == DLNPTXRETREQ_PREP) or (Req.rec.Status == DLNPTXRETREQ_CHECKED))
        //"     если заявление на возврат в статусе "Отложено" или "Проверено", И сумма к возврату Налога к возврату по основной ставке или сумма к возврату Налога к возврату по повышенной ставке изменилась, 
        //то новое заявление не формируем, в существующем заявлении корректируем суммы к возврату, заменяя их на суммы новых значений taxe и  taxe15, номер и дата заявления не изменяются;
        NeedNewReq = false;

        if((Req.rec.TaxToPay != taxe) or (Req.rec.TaxDp != taxe15))
          DL_RslChangeNptxRetReq(Req.rec.ID, 
                                 "ToRetSumMain", abs(taxe),
                                 "ToRetSumHigh", abs(taxe15),
                                 "ToRetSumTotal", abs(taxe + taxe15)
                                );

          MessToRep("Рассчитался налог к возврату. Заявление на возврат уже существует. Изменилась сумма заявления на возврат.");
        else
          MessToRep("Рассчитался налог к возврату. Заявление на возврат уже существует.");
        end;
      else
        NeedNewReq = false; 
        MessToRep("Рассчитался налог к возврату. Заявление на возврат налога не создалось, т.к. существует частично исполненное заявление на возврат.");
      end;
    else
      //Формируем заявление
      NeedNewReq = true;
    end;
  else
    //" Если заявление формируется в ходе Генерации операций удержания / возврата НДФЛ с типом "окончание года" по ВСЕМ клиентам 
    //или операция введена вручную
    
    if(Req.rec.ID > 0)
      NeedNewReq = true;
      
      //-   Если по клиенту уже существует заявление на возврат в статусе "Отложено" или "Проверено", созданное в операции удержания за этот же налоговый период (сумму к возврату не проверяем), то заявление на возврат не формируем
      if((Req.rec.Status == DLNPTXRETREQ_PREP) or (Req.rec.Status == DLNPTXRETREQ_CHECKED))
        NeedNewReq = false;
        MessToRep("Рассчитался налог к возврату. Заявление на возврат уже существует.");
      end;
    else
      //Формируем заявление
      NeedNewReq = true;
    end;
  end;

  if(NeedNewReq == true)
    nptxop_new.Clear();

    GenerateNumberByReference( OBJTYPE_NPTXRETREQ, 1, @RefID, @nptxop_new.rec.Code );

    if(nptxop_new.rec.Code != "")
      var i = 0;
      while(ExistsNptxopCode(DL_RETREQ, nptxop_new.rec.Code))
        if(i == 100) //На всякий случай, чтобы не зависла операция
          return Error("Ошибка при генерации номера заявления на возврат НДФЛ");
        end;
        
        GenerateNumberByReference( OBJTYPE_NPTXRETREQ, 1, @RefID, @nptxop_new.rec.Code );
        i = i + 1;
      end;
    end;
        
    nptxop_new.rec.DocKind           = DL_RETREQ;
    nptxop_new.rec.OperDate          = Oper.rec.OperDate;
    nptxop_new.rec.Time              = time();
    nptxop_new.rec.Department        = {OperDprt};
    nptxop_new.rec.Oper              = {oper};
    nptxop_new.rec.Status            = DLNPTXRETREQ_CHECKED; //Проверено
    nptxop_new.rec.Recalc            = UNSET_CHAR;
    nptxop_new.rec.FIID              = ALLFININSTR;
    nptxop_new.rec.Client            = Oper.rec.Client;
    nptxop_new.rec.Tax               = abs(taxe + taxe15);
    nptxop_new.rec.TaxToPay          = abs(taxe);
    nptxop_new.rec.TaxDp             = abs(taxe15);

    nptxop_new.rec.PrevDate          = date(1,1,TaxPeriod);

    nptxop_new.rec.Kind_Operation    = 2041;//Сопровождение заявлений на возврат налога

    if ( not CreateNptxOpStep(nptxop_new, false))
      err = 1;
    end;
  end;

  return err;
END;

PRIVATE MACRO RunAction()
  var stat = 0;

  if(ExistsDlContr(Oper.rec.Client, Oper.rec.OperDate) == true) //Заявление формируем, если есть дейсвующий ДБО
    if(СоздатьЗаявлениеНаВозврат((Oper.rec.Tax - Oper.rec.TaxDp), Oper.rec.TaxDp) != 0)
      return Error( "Ошибка при создании заявления на возврат налога" );
    end;  
  end;

  return stat;
END;

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR Stat = 0;

  Oper.SetRecordAddr( FDoc );

  stat = DL_NPTX_PutMsg( "Расчет сумм" );
        
  if( not stat )
     stat = RunAction();
     DL_NPTX_SaveOperLog( Oper.rec.ID, 25 );
  end;

  return stat;
END;
