/*
$Name: euroImpRests.mac
$Module: Ценные бумаги
$Description: Процедура сверки остатков на корсчетах на основании отчета EuroClear
*/

import BankInter, PTInter, CTInter, DpInter, rsexts;
import dlquery, DlReport_h;
import "dpimp.mac", secinter, globals, dprepsec, "rsberror.mac";
import "qracctools.mac";

 private var r_Rests = TArray;

// Запись остатка ц/б
private class CEURRest(DepoPartID, DepoAccEURCode, LSIN, ISIN, Amount, SumPrecision, EURAmount)
  if (ValType(EURAmount)==V_UNDEF)
    EURAmount=$0.0;
  end;
  var m_DepoPartID = DepoPartID;
  var m_LSIN = LSIN; // № гос. рег.
  var m_ISIN = ISIN; // ISIN
  var m_Amount = Amount; // Количество ц/б

  var m_DepoAccEURCode = DepoAccEURCode; // Код счета в НРД
  var m_EUR_Amount = EURAmount; // Остаток в НРД

  var m_SumPrecision = SumPrecision;
end;

// Запись ошибки
private class CEURProtocolStr(DepoAcc, LSIN, ISIN, Reason)
  var m_DepoAcc  = DepoAcc;
  var m_LSIN     = LSIN;
  var m_ISIN     = ISIN;
  var m_Reason   = Reason;
end;

// Протокол процедуры сверки
private class (DL_CReportTemplate) CEURProtocol(Reg_No:string, End_Date:date)
  var m_ErrArr = TArray();
  var m_TotalCnt = 0;
  var m_Reg_No = Reg_No;
  var m_End_Date = End_Date;

  var m_IsFinish: bool ,m_Rests : TArray, m_MisNum : integer;

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
    CreateTotalBook();
  END;

  macro SetCount(cnt)
    m_TotalCnt = cnt;
  end;

  macro SetError(DepoAcc, /*FI_Code,*/ LSIN, ISIN, /*FI_Name,*/ Reason)
    m_ErrArr[m_ErrArr.size] = CEURProtocolStr(DepoAcc, LSIN, ISIN, Reason);
  end;

  macro GetCntErr()
    return m_ErrArr.size;
  end;

  macro SetParams(IsFinish: bool ,Rests : TArray, MisNum : integer)
    m_IsFinish = IsFinish;
    m_Rests = Rests;
    m_MisNum = MisNum;
  end;

    /*создать отчет*/
  PRIVATE MACRO Create( NumCopy:INTEGER )
  //macro Print(IsFinish: bool ,Rests : TArray, MisNum : integer)
    var i = 0, mis = 0;
    var wh = 30;
    var diff = $0;
    var Header = "Протокол сверки остатков ценных бумаг в EuroClear (";

    if(m_IsFinish)
      Header = Header + "На конец операционного дня";
    else
      Header = Header + "Операционный день не закрыт!";
    end;
    Header = Header + ")";

    SetActiveSheet("Отчет");
    PrintFormatString(NULL, "N0_H1", Header);
    PrintFormatString(NULL, "N0_H2", m_Reg_No);
    PrintFormatString(NULL, "N0_H3", m_End_Date);
    PrintFormatString(NULL, "N0_H4", m_TotalCnt);
    PrintFormatString(NULL, "N0_H5", m_MisNum);

    CopyAllSheetInTotalBook("Отчет", false, "N0_HEADER", 0, "Отчет");

 
      CopyAllSheetInTotalBook("Отчет", false, "N0_TABLEHEADER_1", 1, "Отчет");
      RegisterTable("N0_MainTable_1", "", "N0_A1_Mis", "N0_A1_DepoAccEURCode", "N0_A1_LSIN", "", "N0_A1_ISIN", "N0_A1_EUR_Amount", "N0_A1_Amount", "N0_A1_Diff");

      while(i < r_Rests.size)
        mis = mis + 1;

        if(r_Rests[i].m_Amount == r_Rests[i].m_EUR_Amount )
          PrintTableLine( "N0_A1_Mis",            mis,                                           NULL,
                          "N0_A1_DepoAccEURCode", r_Rests[i].m_DepoAccEURCode,                   NULL,
                          "N0_A1_LSIN",           r_Rests[i].m_LSIN,                             NULL,
                          "N0_A1_ISIN",           r_Rests[i].m_ISIN,                             NULL,
                          "N0_A1_EUR_Amount",     double(r_Rests[i].m_EUR_Amount),               DP_GetPrecision(r_Rests[i].m_EUR_Amount, r_Rests[i].m_SumPrecision),
                          "N0_A1_Amount",         double(r_Rests[i].m_Amount),                   DP_GetPrecision(r_Rests[i].m_Amount, r_Rests[i].m_SumPrecision),
                          "N0_A1_Diff",           r_Rests[i].m_EUR_Amount - r_Rests[i].m_Amount, r_Rests[i].m_SumPrecision
                         );
        end;

        i = i + 1;
      end;

      EndTable();
      CopyAllSheetInTotalBook("Отчет", false, GetTableRange(), 0, "Отчет");



mis = 0;    
i = 0; 
    if(m_MisNum)
      CopyAllSheetInTotalBook("Отчет", false, "N0_TABLEHEADER_2", 1, "Отчет");
      RegisterTable("N0_MainTable_2", "", "N0_A2_Mis", "N0_A2_DepoAccEURCode", "N0_A2_LSIN", "", "N0_A2_ISIN", "N0_A2_EUR_Amount", "N0_A2_Amount", "N0_A2_Diff");

      while(i < m_Rests.size)
        if(m_Rests[i].m_Amount != m_Rests[i].m_EUR_Amount )
          mis = mis + 1;

          PrintTableLine( "N0_A2_Mis",            mis,                                           NULL,
                          "N0_A2_DepoAccEURCode", m_Rests[i].m_DepoAccEURCode,                   NULL,
                          "N0_A2_LSIN",           m_Rests[i].m_LSIN,                             NULL,
                          "N0_A2_ISIN",           m_Rests[i].m_ISIN,                             NULL,
                          "N0_A2_EUR_Amount",     double(m_Rests[i].m_EUR_Amount),               DP_GetPrecision(m_Rests[i].m_EUR_Amount, m_Rests[i].m_SumPrecision),
                          "N0_A2_Amount",         double(m_Rests[i].m_Amount),                   DP_GetPrecision(m_Rests[i].m_Amount, m_Rests[i].m_SumPrecision),
                          "N0_A2_Diff",           m_Rests[i].m_EUR_Amount - m_Rests[i].m_Amount, m_Rests[i].m_SumPrecision
                         );
        end;

        i = i + 1;
      end;

      EndTable();
      CopyAllSheetInTotalBook("Отчет", false, GetTableRange(), 0, "Отчет");
    end;

    if(m_ErrArr.size)
      SetActiveSheet("Ошибки процедуры сверки");
      CopyAllSheetInTotalBook("Ошибки процедуры сверки", false, "N1_TABLEHEADER_1", 0, "Ошибки процедуры сверки");
      RegisterTable( "N1_MainTable_1", "", "N1_A1_ErrNum", "N1_A1_DepoAcc", "N1_A1_LSIN", "", "N1_A1_ISIN", "N1_A1_Reason" );

      i = 0;
      while(i < m_ErrArr.size)
        PrintTableLine( "N1_A1_ErrNum",  i+1,                   NULL,
                        "N1_A1_DepoAcc", m_ErrArr[i].m_DepoAcc, NULL,
                        "N1_A1_LSIN",    m_ErrArr[i].m_LSIN,    NULL,
                        "N1_A1_ISIN",    m_ErrArr[i].m_ISIN,    NULL,
                        "N1_A1_Reason",  m_ErrArr[i].m_Reason,  NULL
                      );
        i = i + 1;
      end;

      EndTable();
      CopyAllSheetInTotalBook("Ошибки процедуры сверки", false, GetTableRange(), 0, "Ошибки процедуры сверки");
    end;
  END;

  PRIVATE MACRO EndReport( NumCopy:INTEGER )
    SaveTotalbook();
  END;

  initDL_CReportTemplate(NULL, "Report_EuroImpRests.xlsx");
end;

class CEURImportRests()
  private var path:string = ""; // Путь до папки импорта с завершающим \\
  private var protocol = NULL;
  private var ImpDate, ImpNo;
  private var m_Rests = TArray;
  private var isDepo, isKDU;

  private macro full_error(depoacc,lsin,isin,errstr)
    protocol.SetError(depoacc,
                      lsin,
                      isin,
                      errstr);
  end;

  private macro text_error(errstr)
    protocol.SetError("",
                      "",
                      "",
                      errstr);
  end;

  private macro GetFIIDbyISIN(ISIN)
    var query, cmd, DataSet;
    var FIID = -1;

    if(ISIN != "")
      query =   " select t_FIID "
              + "   from davoiriss_dbt "
              + "  where t_ISIN = ? ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(ISIN);

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        FIID = int(DataSet.FIID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIIDbyLSIN(LSIN)
    var query, cmd, DataSet;
    var FIID = -1;

    if(LSIN != "")
      query =   "select t_FIID "
              + "  from davoiriss_dbt "
              + " where t_LSIN = ? ";

      cmd = DL_RSDCommand(query);
      cmd.AddParam(LSIN);

      DataSet = cmd.Execute();
      if(DataSet.moveNext())
        FIID = int(DataSet.FIID);
      end;
    end;

    return FIID;
  end;

  private macro GetFIID(ISIN, LSIN)
    var FIID = -1;

    FIID = GetFIIDbyISIN(string(trim(ISIN)));
    if(FIID == -1)
      FIID = GetFIIDbyLSIN(string(trim(LSIN)));
    end;

    return FIID;
  end;

  private macro GetNominal(FIID,DateNominal)
    var _rs = RSDCommand("select rsb_fiinstr. FI_GetNominalOnDate(:FIID,:Data) nominal from dual");
    var nominal;
    _rs.AddParam("FIID", RSDBP_IN, FIID );
    _rs.AddParam("Data", RSDBP_IN, DateNominal );
    _rs = RSDREcordset(_rs, rsdval_client, rsdval_static);

    if(_rs.moveNext())
        nominal = double(_rs.value("nominal"));
    end;

    _rs = RSDCommand(" select  t_facevalue from dfininstr_dbt where t_fiid = :FIID");
    _rs.AddParam("FIID", RSDBP_IN, FIID );
    _rs = RSDREcordset(_rs, rsdval_client, rsdval_static);
    if(_rs.movenext())
       nominal = double(_rs.value("t_facevalue"));
    end;

    return nominal;
  end;

  private macro ExecuteGetAccEuroClear(NumRep:@string,StateDate:@Date,Finish:@string,SafeDepo:@string,ExtActi:@string)
    private var err=0;
    private var cmd1,query1;
    private var cmd2,query2;


      query1 = "begin\n EXECUTE IMMEDIATE 'TRUNCATE TABLE DACCREPORTEUROCLEAR_TMP';\n end; ";
      query2 = "begin\n ? := RSP_SECURITY.GetAccEuroClear(?,?,?,?,?);\n end; ";

      cmd1 = RSDCommand(query1);
      cmd1.Execute();

      cmd2 = RSDCommand(query2);
      cmd2.AddParam("p_Stat", RSDBP_OUT, V_STRING );
      cmd2.AddParam("p_NumRep", RSDBP_OUT, V_STRING );
      cmd2.AddParam("p_StateDate", RSDBP_OUT, V_DATE );
      cmd2.AddParam("p_Finish", RSDBP_OUT, V_STRING );
      cmd2.AddParam("p_SafeDepo", RSDBP_OUT, V_STRING );
      cmd2.AddParam("p_ExtActi", RSDBP_OUT, V_STRING );
      cmd2.Execute();
      err=SQL_ConvTypeInteger(cmd2.value("p_Stat"));

      var rs_tmp = RSDRecordset("Select * from DACCREPORTEUROCLEAR_TMP", rsdval_client, rsdval_static);
     // runscroll(rs_tmp);

// if (err == 1)  err = 0;  else  err = 1;  end;
     
      if (err==0)   //if (not err)

        NumRep=SQL_ConvTypeStr(cmd2.value("p_NumRep"));
        StateDate=SQL_ConvTypeDate(cmd2.value("p_StateDate"));
        Finish=SQL_ConvTypeStr(cmd2.value("p_Finish")); //Finish=SQL_ConvTypeStr(cmd2.value("p_NumRep"));
        SafeDepo=SQL_ConvTypeStr(cmd2.value("p_SafeDepo"));//SafeDepo=SQL_ConvTypeStr(cmd2.value("p_NumRep"));
        ExtActi=SQL_ConvTypeStr(cmd2.value("p_ExtActi"));//ExtActi=SQL_ConvTypeStr(cmd2.value("p_NumRep"));




      end;
    
    return err;

  OnError(ErrObj)
    if (IsEqClass ("TrslError", ErrObj) and IsEqClass ("TDbError", ErrObj.err))
      return ErrObj.err.Stat;
    end;
    return 1;
  end;

  private macro loadRestsQr(EurPartyId:integer)

    var err = 0;
    var cmd, query, DataSet;

    if(not err)
      query = "select rownum as a1, scqr.T_DEPOACCCODE as a2, "+
              "       avoir.t_Lsin as a3, avoir.t_Isin as a4, "+
              "       RSB_SECUR.GetQRRestOnDate(scqr.T_QRID, ?) as a5, "+
              "       round(ABS(RSI_RSB_ACCOUNT.RestAC(ac.T_ACCOUNT, ac.T_CODE_CURRENCY, ?, ac.T_CHAPTER, 0))) as a6 "+ 
              "  from DSCQRACC_DBT scqr, DAVOIRISS_DBT avoir, DACCOUNT_DBT ac "+
              " where avoir.t_Fiid(+) = scqr.t_Fiid "+
              "   and ac.t_AccountId(+) = scqr.t_AccountId "+
              "   and round(ABS(RSI_RSB_ACCOUNT.RestAC(ac.T_ACCOUNT, ac.T_CODE_CURRENCY, ?, ac.T_CHAPTER, 0))) != "+
              "       RSB_SECUR.GetQRRestOnDate(scqr.T_QRID, ?) "+
              "   and scqr.t_StorageId = ?";
      
      query = "select q1.*, (q1.a5-q1.a6) as a7 from ("+query+") q1";

      cmd = DL_RSDCommand(query);
      cmd.AddParam({curdate});
      cmd.AddParam({curdate});
      cmd.AddParam({curdate});
      cmd.AddParam({curdate});
      cmd.AddParam(EurPartyId);
      
      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        m_Rests[m_Rests.size] = CEURRest(DataSet.a1, DataSet.a2, DataSet.a3, DataSet.a4, DataSet.a6, DataSet.a7, DataSet.a5);
        r_Rests[r_Rests.size] = CEURRest(DataSet.a1, DataSet.a2, DataSet.a3, DataSet.a4, DataSet.a6, DataSet.a7, DataSet.a5);
      end;

    end;

  if(not err)
      query = "select rownum as a1, scqr.T_DEPOACCCODE as a2, "+
              "       avoir.t_Lsin as a3, avoir.t_Isin as a4, "+
              "       RSB_SECUR.GetQRRestOnDate(scqr.T_QRID, ?) as a5, "+
              "       round(ABS(RSI_RSB_ACCOUNT.RestAC(ac.T_ACCOUNT, ac.T_CODE_CURRENCY, ?, ac.T_CHAPTER, 0))) as a6 "+ 
              "  from DSCQRACC_DBT scqr, DAVOIRISS_DBT avoir, DACCOUNT_DBT ac "+
              " where avoir.t_Fiid(+) = scqr.t_Fiid "+
              "   and ac.t_AccountId(+) = scqr.t_AccountId "+
              "   and round(ABS(RSI_RSB_ACCOUNT.RestAC(ac.T_ACCOUNT, ac.T_CODE_CURRENCY, ?, ac.T_CHAPTER, 0))) = "+
              "       RSB_SECUR.GetQRRestOnDate(scqr.T_QRID, ?)  and RSB_SECUR.GetQRRestOnDate(scqr.T_QRID, ?) > 0 "+
              "   and scqr.t_StorageId = ?";
      
      query = "select q1.*, (q1.a5-q1.a6) as a7 from ("+query+") q1";

      cmd = DL_RSDCommand(query);
      cmd.AddParam({curdate});
      cmd.AddParam({curdate});
      cmd.AddParam({curdate});
      cmd.AddParam({curdate});
      cmd.AddParam({curdate});
      cmd.AddParam(EurPartyId);
      
      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        r_Rests[r_Rests.size] = CEURRest(DataSet.a1, DataSet.a2, DataSet.a3, DataSet.a4, DataSet.a6, DataSet.a7, DataSet.a5);
      end;

    end;




    return err;
  end;

  private macro FoundCreateQrRest(ScQrAcc:TRecHandler, Rest:money, RestDate:date, ErrStr:@string)
    var err=0, bufStr="";
    var ScQrRestTB = TBFile("scqrrest.dbt","w",1);
    ScQrRestTB.rec.QrId = ScQrAcc.rec.QrId;
    ScQrRestTB.rec.Date = RestDate;
    if (ScQrRestTB.GetEQ())
      ScQrRestTB.rec.Rest = Rest;
      if (not ScQrRestTB.Update())
        RunError("Ошибка обновления записи таблицы scqrrest.dbt:|"+status(bufStr)+"-"+bufStr);
      end;
    else
      ScQrRestTB.rec.QrId = ScQrAcc.rec.QrId;
      ScQrRestTB.rec.Date = RestDate;
      ScQrRestTB.rec.Rest = Rest;
      if (not ScQrRestTB.Insert())
        RunError("Ошибка вставки записи в таблицу scqrrest.dbt:|"+status(bufStr)+"-"+bufStr);
      end;
    end;
  OnError(ErrObj)
    if (IsEqClass ("TrslError", ErrObj))
      ErrStr=ErrObj.Message;
      if(IsEqClass ("TDbError", ErrObj.err))
        ErrStr=ErrStr+":|"+ErrObj.err.Stat+"-"+ErrObj.err.Oper+"-"+ErrObj.err.Table+"-"+ErrObj.err.Message;
      end;
    elif (not ErrStr)
      ErrStr="Ошибка выполнения FoundOpenScQr";
    end;
    return 1;
  end;

  private macro FoundOpenScQr(EurPartyId:integer, DepoAcc:string, DepoPart:string, 
    Fiid:integer, ScQrAcc:TRecHandler, AvoirRec:TRecHandler, FininstRec:TRecHandler, Rest:money, RestDate:date, ErrStr:@string)
    var err=0, bufStr="";
    var cmd, DataSet, sqlScQR;
    var ScQrAccTB = TBFile("scqracc.dbt","w");
    ScQrAcc.Clear();
    FininstRec.Clear();
    AvoirRec.Clear();

    sqlScQR = "select * from dscqracc_dbt qracc"
                               + " where qracc.t_StorageId  = ? "
                               + "   and qracc.t_Fiid  = ? "
                               + "   and qracc.t_DepoAccCode = ? ";

                           if( valtype(DepoPart) != v_undef)
                             sqlScQR = sqlScQR  + "   and qracc.t_DepoPartCode = ? ";
	                   else
			     sqlScQR = sqlScQR + "   and qracc.t_DepoPartCode = chr(1) ";
                           end;

                            sqlScQR = sqlScQR + "   and rownum = 1";

    cmd = DL_RSDCommand(  sqlScQR ); 
    cmd.AddParam(EurPartyId);
    cmd.AddParam(Fiid);
    cmd.AddParam(DepoAcc);
 if( valtype(DepoPart) != v_undef)
    cmd.AddParam(DepoPart);
end;
    DataSet = cmd.Execute();
    if (DataSet.moveNext())
      DataSet.GetRecord().CopyTo(ScQrAcc.rec);
      err=FoundCreateQrRest(ScQrAcc, Rest, RestDate, @ErrStr);
    else
      ScQrAcc.rec.StorageId = EurPartyId;
      ScQrAcc.rec.Fiid = Fiid;
      ScQrAcc.rec.DepoAccCode = DepoAcc;
      ScQrAcc.rec.DepoPartCode = DepoPart;
      err = СоздатьЗаписьВРегистреКДУ(ScQrAcc, RestDate, Rest, @ErrStr);
    end;
    if (not err)
      ПолучитьФинИн(Int(Fiid),FininstRec,AvoirRec);
    end;
    return err;
  OnError(ErrObj)
    if (IsEqClass ("TrslError", ErrObj))
      ErrStr=ErrObj.Message;
      if(IsEqClass ("TDbError", ErrObj.err))
        ErrStr=ErrStr+":|"+ErrObj.err.Stat+"-"+ErrObj.err.Oper+"-"+ErrObj.err.Table+"-"+ErrObj.err.Message;
      end;
    elif (not ErrStr)
      ErrStr="Ошибка выполнения FoundOpenScQr";
    end;
    return 1;
  end;

  private macro GetMisNum()
    var i = 0, mis = 0;

    while(i < m_Rests.size)
      if(m_Rests[i].m_Amount != m_Rests[i].m_EUR_Amount )
        mis = mis + 1;
      end;
      i = i + 1;
    end;

    return mis;
  end;

  private macro processReportKDU(EurPartyId:integer)
    var FINominal;
    var err = 0;
    var ErrStr = "";
    var NumRep,StateDate,Finish,SafeDepo,ExtActi;
    var Fiid = 0;
    var cmd, DataSet;
    var ScQrAcc = TRecHandler("scqracc.dbt");
    var AvoirRec = TRecHandler("avoiriss.dbt");
    var FininstRec = TRecHandler("fininstr.dbt"); 

    err = ExecuteGetAccEuroClear(@NumRep,@StateDate,@Finish,@SafeDepo,@ExtActi);
    
    protocol = CEURProtocol(NumRep, StateDate);

    if (err==0)   //if (not err)
      if (not NumRep)
        MsgBox("Нет данных для загрузки");
        err=1;
      end;
      if (ExtActi == "N")
        MsgBox("На счете "+SafeDepo+" согласно "+NumRep+" нет остатков");
        err=1;
      end;
      if (err==0)   //if (not err)
        if ((Finish != "COMP") and (not CU_YesNoWin("День не закрыт! Продолжить загрузку?")))
          err=1;
        end;
      end;
    end;

    if (err==0)   //if (not err)
      cmd = DL_RSDCommand(  "select * from DACCREPORTEUROCLEAR_TMP");

      protocol.SetCount(cmd.GetCount());

      DataSet = cmd.Execute();

      while (DataSet.moveNext())

        Fiid = GetFIID(DataSet.ISIN,DataSet.LSIN);
        if (Fiid == -1)
          full_error(SafeDepo,DataSet.LSIN,DataSet.ISIN,"В справочнике ценных бумаг не найден выпуск по указанным в отчёте EuroClear кодам");
        end;

        FINominal = GetNominal(Fiid, {Curdate});

        err = FoundOpenScQr(EurPartyId, SafeDepo, /*DataSet.SafeDepoPart*/"", 
                Fiid, ScQrAcc, AvoirRec, FininstRec, (DataSet.Quantity/FINominal), StateDate, @ErrStr);
        if (err)
          full_error(SafeDepo,DataSet.LSIN,DataSet.ISIN,ErrStr);
        end;

      end;
    end;
    loadRestsQr(EurPartyId);
    err = protocol.GetCntErr();
    protocol.SetParams(Trim(String(Finish))=="COMP",m_Rests, GetMisNum());
    protocol.Run();

    return err;
  end;

  // Запуск отчета
  macro Run() : bool
    var err = 0,TransportFlag=0;
    var CodeStr = "";
    var EurPartyId = 0;
    GetRegistryValue("COMMON\\WORK_MODE\\WORK_WITH_DEPOSITARY", V_BOOL, isDepo, err);
    GetRegistryValue("SECUR\\ВЕДЕНИЕ КДУ", V_BOOL, isKDU, err);
    GetRegistryValue("DEPO\\TRANSPORT", V_INTEGER, TransportFlag, err );
    if (err)
      MsgBox("Ошибка получение значений настроек \"WORK_WITH_DEPOSITARY\", \"ВЕДЕНИЕ КДУ\", \"TRANSPORT\"");
    end;
    if (isDepo and isKDU)
      CreateErrMsg(err=28055);
    elif ((not isDepo) and (not isKDU))
      CreateErrMsg(err=28056);
    end;

/****************/
    var p_State;
    var _qr,  _cmd; 
    _qr = "begin\n ? := RSHB_USER.EuroImpNeedKDU(?,?);\n end; ";
    _cmd = RSDCommand(_qr);
    _cmd.AddParam("p_Stat", RSDBP_OUT, V_STRING );
    _cmd.AddParam("p_Date", RSDBP_IN, {CurDate} );
    _cmd.AddParam("p_Txt", RSDBP_OUT, V_STRING, 2000 );
    _cmd.Execute();

    p_State = SQL_ConvTypeInteger(_cmd.value("p_Stat"));
    if(p_state > 0)
       msgbox(_cmd.value("p_Txt"));
    end;
/********************/

    if (not err)

      if (isKDU)
        if (TransportFlag==1)
          GetRegistryValue( "SECUR\\EUROCLEAR_CODE", V_STRING, CodeStr, err );
          if (not err)
            EurPartyId = ПолучитьКодСубъекта(String(CodeStr), PTCK_CLIENT);
            EurPartyId = IIF(EurPartyId,Int(EurPartyId),0);
          end;
          if (EurPartyId == 0)
            MsgBox("Не найден субъект из EUROCLEAR_CODE");
            err=1;
          end;
          if (not err)
            err=processReportKDU(EurPartyId);
          end;
        else
          MsgBox("Работа через Payments отключена, продолжение не возможно");
        end;
      end;

    end;

    if (err>1)
      DisplayError();
    end;
    return err;
  end;
  
end;

CEURImportRests().Run();
Exit(1);
