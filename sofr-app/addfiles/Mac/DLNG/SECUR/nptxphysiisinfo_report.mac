/*
$Name:        nptxphysiisinfo.mac
$Module:      Ценные бумаги
$Description: Печать отчета "Сведения о ФЛ и его ИИС"
*/

import "nptxphysiisinfo_form.mac", "spRepFun.mac", "RepPersonalIncomeTax.mac", "scsrvrepfun.mac";
import "dlmisc.mac";

private macro ТелефонДляСправкиПоИИС()
   var ErrCode = 0, Str = "";

   GetRegistryValue( "COMMON\\НДФЛ\\ТЕЛЕФОН_ДЛЯ_СПРАВКИ_ПО_ИИС", V_STRING, Str, ErrCode );
   if( ErrCode != 0 )
      MsgBox( "Ошибка при получении значения настройки \"COMMON\\НДФЛ\\ТЕЛЕФОН_ДЛЯ_СПРАВКИ_ПО_ИИС\"");
   end;

   return Str;
end;

PRIVATE MACRO GetRepPath()
  var RegistryPath = "РСХБ\\ДИРЕКТОРИИ\\OUT\\СВЕДЕНИЯ_О_ФЛ_И_ЕГО_ИИС";
  var Path = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if((stat) or (Path == "")) 
    msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  else
    if(substr(Path, strlen(Path), 1) != "\\")
      Path = Path + "\\";
    end;
  end;

  return Path;
END;

//Cписок адресов для отправки скрытой копии
PRIVATE MACRO GetNPTXBccList()
  var RegistryPath = "COMMON\\НДФЛ\\ПОЧТА ДЛЯ СПРАВОК ПО НДФЛ";
  var BccList:string = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, BccList, stat);
  if(stat) 
    BccList = "";      
  end;

  return BccList;
END;

private macro GetClientEMails(ClientID:integer, DlContrID:integer, RepDate:date, MainEmail:@string)
  var query, cmd, DataSet;

  MainEmail = "";

  cmd = DL_RSDCommand();
  
  query =   " SELECT TRIM(NVL(dlc.t_Email, CHR(1))) Email "
          + "   FROM ddlcontr_dbt dlc "
          + "  WHERE dlc.t_DlContrID = " + cmd.AddParam(DlContrID);

  DataSet = cmd.Execute(query);
  if(DataSet.moveNext())
    MainEmail = DataSet.Email;
  end;

  if(MainEmail == "")
    cmd = DL_RSDCommand();
    
    query =   " SELECT DISTINCT TRIM(t.t_Email) Email "
            + "   FROM (SELECT dlc.t_Email " 
            + "           FROM dsfcontr_dbt sf, ddlcontr_dbt dlc "
            + "          WHERE sf.t_PartyID = " + cmd.AddParam(ClientID)
            + "            AND sf.t_DateBegin <= " + cmd.AddParam(RepDate)
            + "            AND (sf.t_DateClose = " + GetSQLDate(date(0,0,0)) + " or sf.t_DateClose >= "+cmd.AddParam(RepDate)+")"
            + "            AND dlc.t_SfContrID = sf.t_ID "
            + "       ORDER BY sf.t_DateBegin DESC"
            + "        ) t "
            + "  WHERE t.t_Email IS NOT NULL AND t.t_Email <> chr(1) ";

    DataSet = cmd.Execute(query);
    while(DataSet.moveNext())
      if(MainEmail == "")
        MainEmail = DataSet.Email;
      else
        MainEmail = MainEmail + ";" + DataSet.Email;
      end;
    end;
  end;

  if(MainEmail == "")
    cmd = DL_RSDCommand();

    query =   " SELECT c.t_Value as email"
            + "   FROM dcontact_dbt c "
            + "  WHERE c.t_PartyID = " + cmd.AddParam(ClientID)
            + "    AND c.t_ContactKind = " + CNTK_EMAIL
            + "    AND c.t_Value IS NOT NULL "
            + "    AND c.t_Value <> CHR(1) "
            + "  order by (case when c.t_ContactType = 8 then 1 when c.t_ContactType = 1001 then 2 else 3 end) asc";

    DataSet = cmd.Execute(query);
    if(DataSet.moveNext())
      MainEmail = DataSet.Email;
    end;
  end;
end;


//значения доходов и вычетов
/*PRIVATE CLASS NPTXCodeParm(_PCode, _PSum, _MCode, _MSum)
  var PSCode  = _PCode;
  var PSnnn   = _PSum; 
  var MSCode  = _MCode;
  var MSnnn   = _MSum;
END;*/
                                
PRIVATE CLASS (NPTXRepCalcCommon) NPTXPHYSIISINFO( _RepDate, _Investor, _DlContrID, _IsConsolidate )
  var m_DlContrID = _DlContrID;
  var m_IsConsolidate = _IsConsolidate;

  MACRO FIO():STRING
     return (LastName()+" "+FirstName()+" "+MiddleName());
  END;

  MACRO ClientINN():STRING
     return m_RS.ClientINN;
  END;

  MACRO Document():STRING
     var v_Document = "";
     if( ClientPersnIdcMain().rec.PersonID > 0 )
        v_Document = DocumentName()+", "+DocumentNumber()+", "+ClientPersnIdcMain().rec.PaperIssuer + " " + ClientPersnIdcMain().rec.PaperIssuerCode + ", "+string(ClientPersnIdcMain().rec.PaperIssuedDate);
     end;
     return v_Document;
  END;

  MACRO ResidenceAdress():STRING
    return GetPartyAddress(m_RS.PartyID, true, false, PTADDR_LEGAL);
  END;

  MACRO OurAdress():STRING
    return GetPartyAddress({OurBank}, true, false, PTADDR_LEGAL);
  END;

  PRIVATE MACRO LoadPSnnn( InfoRate, CodeStr,NotRoundPaid, Rate):MONEY
     var PSsum = $0;
     var PSKind = "";

     if( CodeStr != null )
        if( (InfoRate == INFORATE9_15) or (InfoRate == INFORATE9_NOTRES_DIV) )
           if(this.ResidenceStatus() == 1)
             PSKind = "Plus9_"+string(CodeStr);
           else
             PSKind = "Plus15_"+string(CodeStr);
           end;
        elif( (InfoRate == INFORATE_35) and (this.ResidenceStatus() == 1) )
           PSKind = "Plus35_"+string(CodeStr);
        else
           if( CodeStr == "3023" )
              if( this.ResidenceStatus() != 1 )
                 PSKind = "Plus35_"+string(CodeStr);
              end;
           else
              PSKind = "PlusG_"+string(CodeStr);
           end;
        end;

        if( (PSKind != "")  )
           var AddWhere = "";
           
           AddWhere = "     RSI_NPTO.CheckObjIIS (obj.t_AnaliticKind6, obj.t_Analitic6 )  = 1 "
                    + " and obj.t_Analitic6 IN (select mp.t_SfContrID from ddlcontrmp_dbt mp where mp.t_DlContrID = "+m_DlContrID+")";

           DL_GetNPTXOBJSumByClient(this.ClientID(),
                                    GetNDRStrKind(PSKind),
                                    m_BeginDate,
                                    m_EndDate,
                                    TXOBJ_DIR_ALL,
                                    NULL,
                                    @PSsum,
                                    NULL,
                                    1,
                                    NULL,
                                    AddWhere,NotRoundPaid,Rate);

        end;
     end;

     return PSsum;
  END;

  MACRO MinusSum()
    return m_MinusSum;
  END;

  private macro I_ИИС(pClient, pBegDate, pEndDate, Str5, Str4)
    var StrSQL, Query, DataSet;
    var Sum1 = 0, Sum2 = 0;
   
    Query = DL_RSDCommand();
   
    StrSQL =   " select NVL(sum(N.t_Sum0), 0) Sum1"
              + "   from dnptxobj_dbt N "
              + "  where N.t_Kind = " + TXOBJ_PLUS_SEC
              + "    and N.t_Direction = " + TXOBJ_DIR_IN
              + "    and N.t_Client = ? "
              + "    and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6 )  = 1 "
              + "    and N.t_Analitic6 IN (select mp.t_SfContrID from ddlcontrmp_dbt mp where mp.t_DlContrID = ?)"
              + "    and N.t_Date >= ? " 
              + "    and N.t_Date <= ? "; 
   
    Query.addParam( pClient );
    Query.addParam( m_DlContrID);
    Query.addParam( pBegDate );
    Query.addParam( pEndDate );
   
    if((ValType(Str5) != V_UNDEF) and (Str5 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and N.t_Analitic5 " + Str5;
    end;
   
    if((ValType(Str4) != V_UNDEF) and (Str4 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind4 = " + TXOBJ_KIND4010
                      + " and N.t_Analitic4 " + Str4;
    end;
   
    DataSet = Query.Execute(StrSQL);
    if(DataSet.moveNext())
      Sum1 = DataSet.Sum1;
    end;
   
    Query = DL_RSDCommand();
   
    StrSQL =   " select NVL(sum(DECODE(N.t_Direction, "+TXOBJ_DIR_IN+", N.t_Sum0, -N.t_Sum0)), 0) Sum2"
              + "   from dnptxobj_dbt N, dparty_dbt pt "
              + "  where N.t_Kind = " + TXOBJ_PROC_SEC
              + "    and N.t_Client = ? "
              + "    and pt.t_PartyID = N.t_Client " 
              + "    and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6 )  = 1 "
              + "    and N.t_Analitic6 IN (select mp.t_SfContrID from ddlcontrmp_dbt mp where mp.t_DlContrID = ?)"
              + "    and N.t_Date >= ? " 
              + "    and N.t_Date <= ? "; 
   
    Query.addParam( pClient );
    Query.addParam( m_DlContrID);
    Query.addParam( pBegDate );
    Query.addParam( pEndDate );
   
    if((ValType(Str5) != V_UNDEF) and (Str5 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and N.t_Analitic5 " + Str5;
    end;
   
    if((ValType(Str4) != V_UNDEF) and (Str4 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind4 = " + TXOBJ_KIND4010
                      + " and N.t_Analitic4 " + Str4;
    end;
   
    DataSet = Query.Execute(StrSQL);
    if(DataSet.moveNext())
      Sum2 = DataSet.Sum2;
    end;
   
    return Sum1 + Sum2;
  end;

  private macro D_ИИС(pClient, pBegDate, pEndDate, Str5, Str4)
    var StrSQL, Query, DataSet;
    var Sum1 = 0;
   
    Query = DL_RSDCommand();
   
    StrSQL =   " select NVL(sum(N.t_Sum0), 0) Sum1"
              + "   from dnptxobj_dbt N "
              + "  where N.t_Kind IN ("+TXOBJ_MINUS_SEC+","+TXOBJ_OTHER_SEC+") "
              + "    and N.t_Direction = " + TXOBJ_DIR_OUT
              + "    and N.t_Client = ? "
              + "    and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6 )  = 1 "
              + "    and N.t_Analitic6 IN (select mp.t_SfContrID from ddlcontrmp_dbt mp where mp.t_DlContrID = ?)"
              + "    and N.t_Date >= ? " 
              + "    and N.t_Date <= ? "; 
   
    Query.addParam( pClient );
    Query.addParam( m_DlContrID);
    Query.addParam( pBegDate );
    Query.addParam( pEndDate );
   
    if((ValType(Str5) != V_UNDEF) and (Str5 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and N.t_Analitic5 " + Str5;
    end;
   
    if((ValType(Str4) != V_UNDEF) and (Str4 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind4 = " + TXOBJ_KIND4010
                      + " and N.t_Analitic4 " + Str4;
    end;
   
    DataSet = Query.Execute(StrSQL);
    if(DataSet.moveNext())
      Sum1 = DataSet.Sum1;
    end;
   
    return Sum1;
  end;

  MACRO U_ИИС(Str5, Str4)
    var AddWhereCond = " obj.t_AnaliticKind6 = " + TXOBJ_KIND6020 + " and obj.t_Analitic6 IN (select mp.t_SfContrID from ddlcontrmp_dbt mp where mp.t_DlContrID = "+m_DlContrID+") ";
    var _G_ИИС = GetRubSumByClient(m_Investor, string(TXOBJ_GENERAL_IIS), m_BeginDate, m_EndDate, null, 1, null, AddWhereCond);
    var _TI_ИИС = GetRubSumByClient(m_Investor, string(TXOBJ_PLUS_SEC), m_BeginDate, m_EndDate, null, 1, null, AddWhereCond);
    var _I_ИИС = I_ИИС(m_Investor, m_BeginDate, m_EndDate, Str5, Str4/*"= "+NPTX_FI_CIRCULATE*/);
    var _D_ИИС = D_ИИС(m_Investor, m_BeginDate, m_EndDate, Str5, Str4/*"= "+NPTX_FI_CIRCULATE*/);

    if(_TI_ИИС == 0)
      return _D_ИИС;
    else
      return _D_ИИС + _G_ИИС * _I_ИИС / _TI_ИИС;
    end;
  END;

  private macro StrMaxFactDate2()
      return
      " (NVL( (SELECT max(dlrq.t_FactDate) " +
      "  FROM ddlrq_dbt dlrq " + 
      "  WHERE dlrq.t_DocKind    = tick.t_BOfficeKind " +
      "      AND dlrq.t_DocID = tick.t_DealID " +
      "      AND dlrq.t_Type    IN (" + DLRQ_TYPE_DELIVERY + "," + DLRQ_TYPE_PAYMENT + ") " +
      "      AND dlrq.t_DealPart    = " + LEG_KIND_DL_TICK_BACK + 
      "      AND dlrq.t_State = " + DLRQ_STATE_EXEC +
      "      AND dlrq.t_FactDate > TO_DATE('01-01-0001','DD-MM-YYYY') " +
      "      ), TO_DATE('01-01-0001','DD-MM-YYYY') ) " +
      " ) ";
   end;

  MACRO RD_ИИС()
    var AddWhereCond = " obj.t_AnaliticKind6 = " + TXOBJ_KIND6020 + " and obj.t_Analitic6 IN (select mp.t_SfContrID from ddlcontrmp_dbt mp where mp.t_DlContrID = "+m_DlContrID+") ";
    var RD = GetRubSumByClient(m_Investor, string(TXOBJ_PROCREPOTAX), m_BeginDate, m_EndDate, TXOBJ_DIR_OUT, 1, null, AddWhereCond);

    /*плюс (рублевая сумма НДР вида <Com> по сделкам РЕПО, у которых максимальная из дат: фактической поставки по 2 части и фактической оплаты по 2 части входит в период расчета, и дата НДР после 31.12.2011)*/
    var QueryCond =   "select NVL(sum(obj.t_Sum0),0) as S0 "
                    + "  from dnptxobj_dbt obj, ddl_tick_dbt tick "                                                         
                    + " where obj.t_Kind = ? "
                    + "   and obj.t_AnaliticKind1 = ? "
                    + "   and tick.t_DealID = obj.t_Analitic1 "
                    + "   and obj.t_Date >= ? "
                    + "   and obj.t_Date <= ? "
                    + "   and obj.t_Direction = ? "
                    + "   and obj.t_Client = ? "
                    + "   and RSI_NPTO.CheckObjIIS ( ?, obj.t_Analitic6 ) = 1 "
                    + "   and obj.t_Analitic6 IN (select mp.t_SfContrID from ddlcontrmp_dbt mp where mp.t_DlContrID = ?)"
                    + "   and rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1 "
                    + "   and RSI_NPTX.CheckCateg(?, 23, LPAD(Tick.t_DealID, 34, '0'), 2)<>1 " //категория "Является налоговым Репо" на сделке DDL_TICK.T_DEALID не задана или не равна False
                    + "   and Tick.t_BOfficeKind = ? "
                    + "   and (" + StrMaxFactDate2() + " between ? and ?) ";
 
    var Query = DL_RSDCommand(QueryCond);
    Query.addParam( TXOBJ_COM );
    Query.addParam( TXOBJ_KIND1010 );
    Query.addParam( m_BeginDate );
    Query.addParam( m_EndDate );
    Query.addParam( TXOBJ_DIR_OUT );
    Query.addParam( m_Investor );
    Query.addParam( TXOBJ_KIND6020 );
    Query.addParam( m_DlContrID );
    Query.addParam( OBJTYPE_SECDEAL );
    Query.addParam( DL_SECURITYDOC );
    Query.addParam( m_BeginDate );
    Query.addParam( m_EndDate );
 
    var DataSet = Query.execute();
    
    if( DataSet.MoveNext() )
      return RD + Round(money(DataSet.S0));
    else
      return 0;
    end;
  END;

  MACRO LoadMSnnn(InfoRate, CodeStr)
    var AddWhereCond = " obj.t_AnaliticKind6 = " + TXOBJ_KIND6020 + " and obj.t_Analitic6 IN (select mp.t_SfContrID from ddlcontrmp_dbt mp where mp.t_DlContrID = "+m_DlContrID+") ";
    var MSsum = $0;
    var MSKind = "";
    var MSsum1 = $0, MSsum2 = $0;

    if(CodeStr != NULL)
      if(InfoRate == INFORATE_TOTAL)
        if((CodeStr == "225") or (CodeStr == "Minus_225"))
          return U_ИИС("not in ("+TXGROUP_80+","+TXGROUP_90+")", " = " + NPTX_FI_CIRCULATE);
        elif((CodeStr == "226") or (CodeStr == "Minus_226"))
          return U_ИИС("not in ("+TXGROUP_80+","+TXGROUP_90+")", " = " + NPTX_FI_NOCIRCULATE);
        elif((CodeStr == "227") or (CodeStr == "Minus_227"))
          return U_ИИС("not in ("+TXGROUP_80+","+TXGROUP_90+")", " = " + NPTX_FI_LOSTCIRCULATE);
        elif((CodeStr == "228") or (CodeStr == "Minus_228"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_FULLMINUS_228), m_BeginDate, m_EndDate, null, 1, null, AddWhereCond);
        elif((CodeStr == "229") or (CodeStr == "Minus_229"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_FULLMINUS_229), m_BeginDate, m_EndDate, null, 1, null, AddWhereCond);
        elif((CodeStr == "230") or (CodeStr == "Minus_230"))
          return RD_ИИС();
        elif((CodeStr == "231") or (CodeStr == "Minus_231"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_MINUS_SEC_SHORT), m_BeginDate, m_EndDate, null, 1, null, " obj.t_Analitic5 <> " + TXGROUP_30 + " and " + AddWhereCond);
        elif((CodeStr == "233") or (CodeStr == "Minus_233"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_MINUSG_233), m_BeginDate, m_EndDate, null, 1, null, AddWhereCond);
        elif((CodeStr == "234") or (CodeStr == "Minus_234"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_MINUSG_234), m_BeginDate, m_EndDate, null, 1, null, AddWhereCond);
        elif((CodeStr == "235") or (CodeStr == "Minus_235"))
          return U_ИИС(" in ("+TXGROUP_80+","+TXGROUP_90+")", " = " + NPTX_FI_NOCIRCULATE);
        end;
      elif(InfoRate == INFORATE9_15)
        if((CodeStr == "233") or (CodeStr == "Minus_233"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_MINUS9_233), m_BeginDate, m_EndDate, null, 1, null, AddWhereCond);
        elif((CodeStr == "234") or (CodeStr == "Minus_234"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_MINUS9_234), m_BeginDate, m_EndDate, null, 1, null, AddWhereCond);
        end;

        return 0;
      end;

      return 0;
    end;

    return 0;
  END;

  MACRO MSnnn(InfoRate, MSCode, MSCodeStr)
    var MSsum = $0;
    
    var CodeStr = "";

    if( MSCode != null )
      if( ValType(MSCodeStr) == V_STRING )
        var _index = Index(MSCodeStr, "+");

        if( _index > 0 )
          while( _index > 0 )

            CodeStr = substr(MSCodeStr, 1, _index - 1);
            MSsum = MSsum + LoadMSnnn(InfoRate, CodeStr);

            MSCodeStr = substr(MSCodeStr, _index + 1);
            _index = Index(MSCodeStr, "+");
          end;
        end;

        CodeStr = MSCodeStr;
        MSsum = MSsum + LoadMSnnn(InfoRate, CodeStr);
      else
        CodeStr = string(MSCode);
        MSsum = LoadMSnnn(InfoRate, CodeStr);
      end;

    end;

    return MSsum;
  END;
  
  PRIVATE MACRO ДобавитьПараметры( InfoRate, PSCode, PSCodeStr, MSCode, MSCodeStr )

    var PSnnn = $0, MSnnn = $0;

    PSnnn = this.PSnnn(InfoRate, PSCode, PSCodeStr);
    MSnnn = this.MSnnn(InfoRate, MSCode, MSCodeStr);

    if( (PSCode != null ) or (MSnnn != $0) )

       AddInArrCodeParms(m_ArrCodeParms, Month(m_BeginDate), PSCode, PSnnn, MSCode, MSnnn, 0.0, 0.0, 0.0, 0.0, 0.0, Rate(InfoRate), PSnnn - MSnnn);

       m_PlusSum  = m_PlusSum  + PSnnn;
       m_MinusSum = m_MinusSum + MSnnn;
    end;
  END;

  PRIVATE MACRO ДобавитьПараметрыДрБрок(InfoRate)
    var query, cmd, DataSet;

    query =   " select frob.* "
            + "   from ddlcontrfrob_dbt frob "
            + "  where frob.t_DlContrID = ? "
            + "    and frob.t_TaxPeriod = ? ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_DlContrID);
    cmd.AddParam(m_CurrYear);

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      AddInArrCodeParms(m_ArrCodeParms, 1, DataSet.IncomeCode, DataSet.IncomeSum, DataSet.DeductionCode, DataSet.DeductionSum, 0.0, 0.0, 0.0, 0.0, 0.0, Rate(InfoRate), 0, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, 1);

      m_PlusSum  = m_PlusSum  + DataSet.IncomeSum;
      m_MinusSum = m_MinusSum + DataSet.DeductionCode;
    end;

  END;

  MACRO ДобавитьВсеПараметры(RateIndexes)
    var OldSize = m_ArrCodeParms.Size;
 
    ДобавитьПараметры(m_InfoRate, 1011, "1011_ИИС", null);

    ДобавитьПараметры(m_InfoRate, 1110, null, null);

    ДобавитьПараметры(m_InfoRate, 1544, null, 225, "225+214_ИИС");
    ДобавитьПараметры(m_InfoRate, null, null/*, 251*/);
    ДобавитьПараметры(m_InfoRate, null, null, 233);
    ДобавитьПараметры(m_InfoRate, null, null/*, 239*/);

    ДобавитьПараметры(m_InfoRate, 1545, null, 226);
    ДобавитьПараметры(m_InfoRate, null, null, 234);
    ДобавитьПараметры(m_InfoRate, null, null/*, 240*/);

    ДобавитьПараметры(m_InfoRate, 1546, null/*, 250*/);
    ДобавитьПараметры(m_InfoRate, null, null, 228);
    ДобавитьПараметры(m_InfoRate, null, null/*, 252*/);

    ДобавитьПараметры(m_InfoRate, 1547, null, 235);

    ДобавитьПараметры(m_InfoRate, 1548, null, 229);
    ДобавитьПараметры(m_InfoRate, null, null/*, 241*/);

    ДобавитьПараметры(m_InfoRate, 1549, null, 227);
    ДобавитьПараметры(m_InfoRate, null, null/*, 236*/);

    ДобавитьПараметры(m_InfoRate, 1551, null, 230);

    ДобавитьПараметры(m_InfoRate, 1553, null, 231);

    ДобавитьПараметры(m_InfoRate, 2640, "2640_ИИС", null);
    ДобавитьПараметры(m_InfoRate, 2641, "2641_ИИС", null);

    if(OldSize < m_ArrCodeParms.Size)
       AddRateIndex(RateIndexes, OldSize);
       OldSize = m_ArrCodeParms.Size;
    end;

    if(m_IsConsolidate)
      ДобавитьПараметрыДрБрок(m_InfoRate);
    end;

    if(OldSize < m_ArrCodeParms.Size)
       AddRateIndex(RateIndexes, OldSize);
    end;
  END;

  PRIVATE MACRO РассчитатьДоходыИВычеты(pEndDate:date, RateIndexes)
    var OldCurrYear = m_CurrYear;

    m_PlusSum  = $0; 
    m_MinusSum = $0;

    if(RateIndexes == NULL)
       RateIndexes = TArray();
    end;

    DateSplit(pEndDate, null, null, m_CurrYear);

    m_BeginDate = date(1,1,m_CurrYear);
    m_EndDate = pEndDate;

    ДобавитьВсеПараметры(RateIndexes);

    РассчитатьПараметрыДляПечати(RateIndexes);

    m_CurrYear = OldCurrYear;

    return RateIndexes;
  END; 

  MACRO CalcData(pEndDate:date, InfoRate, Sign, RateIndexes)
    m_CurrSign = Sign;
    m_InfoRate = InfoRate;
    ClearData();
    RateIndexes= РассчитатьДоходыИВычеты(pEndDate, RateIndexes);
    return RateIndexes;
  END;

  InitNPTXRepCalcCommon(_RepDate, _Investor, null, null, null, false, true);
END;

class (DL_CReportTemplate) NpTxPhysIISInfoReport(RepDate,       
                                                 ClientID,      
                                                 DlContrID,     
                                                 IsConsolidate, 
                                                 DocumentNumber,
                                                 Post,
                                                 Signer
                                                )

    private var m_dlcontr = TRecHandler("dlcontr.dbt");
    private var m_dlcSfContr = TRecHandler("sfcontr.dbt");
    private var m_RepDate = RepDate;
    private var m_Investor = ClientID;
    private var m_DlContrID = DlContrID;
    private var m_DocNumber = DocumentNumber; 
    private var m_IsConsolidate = IsConsolidate;
    private var m_RepCalc, m_TaxeYearClose = null;
    private var m_Post = Post, m_Signer = Signer;
    private var m_NewSheetName = "";

    macro PrintMode():Integer
       return DL_OUTREPORT_EXCEL;
    end;

    private macro BeginReport(NumCopy:Integer)
       var query, cmd, DataSet;

       Dl_CallInsertStat(PrintMode());

       IIS_CONTRACTS   = SET_CHAR;
       OTHER_CONTRACTS = UNSET_CHAR;

       query = "select * from ddlcontr_dbt where t_DlContrID = ? ";
       cmd = DL_RSDCommand(query);

       cmd.AddParam(m_DlContrID);

       DataSet = cmd.Execute();
       if(DataSet.moveNext())
         DataSet.GetRecord().CopyTo(m_dlcontr.rec);
       end;

       query = "select * from dsfcontr_dbt where t_ID = ? ";
       cmd = DL_RSDCommand(query);

       cmd.AddParam(m_dlcontr.rec.SfContrID);

       DataSet = cmd.Execute();
       if(DataSet.moveNext())
         DataSet.GetRecord().CopyTo(m_dlcSfContr.rec);
       end;                        

       CreateTotalBook();
       SetActiveSheet( "ФЛ и его ИИС" );

       SetIsShowAppl(false); //Всегда не показываем отчет на экран

       m_NewSheetName = IIF(m_IsConsolidate, "Консолид. данные", "Неконсолид. данные");

    end;

    macro AccountOpenDate()
      var Query = "";                                                                                                                          
      var DataSet = null, cmd;
      var AccountOpenDate = date(0,0,0);                                                                                                                      

      Query = " select acc.t_open_date "
             +"   from ddlcontracc_dbt dlcacc, daccount_dbt acc "                                                           
             +"  where dlcacc.t_DlContrID = ?  "
             +"    and acc.t_AccountID = dlcacc.t_AccountID "                                                                            
             +" order by acc.t_open_date asc";

      cmd = DL_RSDCommand(Query);

      cmd.AddParam(m_DlContrID);
                                                                        
      DataSet = cmd.Execute();                                                                                                         
      if( DataSet.MoveNext() )                                                                                                              
        AccountOpenDate = date(DataSet.open_date);                                                                                     
      end;                                                                                                                                  

      return AccountOpenDate;
    end;

    macro AccountCloseDate()
      var Query = "";                                                                                                                          
      var DataSet = null, cmd;
      var AccountCloseDate = date(0,0,0);                                                                                                                      

      Query = " select acc.t_close_date "
             +"   from ddlcontracc_dbt dlcacc, daccount_dbt acc "                                                           
             +"  where dlcacc.t_DlContrID = ?  "
             +"    and acc.t_AccountID = dlcacc.t_AccountID "                                                                         
             +" order by acc.t_close_date desc";

      cmd = DL_RSDCommand(Query);

      cmd.AddParam(m_DlContrID);
                                                                        
      DataSet = cmd.Execute();                                                                                                         
      if( DataSet.MoveNext() )                                                                                                              
        AccountCloseDate = date(DataSet.close_date);                                                                                     
      end;                                                                                                                                  

      return AccountCloseDate;
    end;

    PRIVATE MACRO SumOst()                                                                                                         
       var Query = "";                                                                                                                          
       var DataSet = null, cmd;
       var SumOst = $0;                                                                                                                     

       Query = " select NVL(SUM(case when acc.t_close_date is not NULL and acc.t_close_date != to_date('01.01.0001','DD.MM.YYYY') "
              +"                          then NVL((rsb_fiinstr.ConvSum(RSB_ACCOUNT.RESTAC(acc.T_ACCOUNT,acc.T_CODE_CURRENCY,acc.t_close_date-1,acc.T_Chapter, null), acc.t_Code_Currency, " + string(NATCUR) + ", acc.t_close_date-1 )), 0) "
              +"                     else NVL((rsb_fiinstr.ConvSum(RSB_ACCOUNT.RESTAC(acc.T_ACCOUNT,acc.T_CODE_CURRENCY,?,acc.T_Chapter, null), acc.t_Code_Currency, " + string(NATCUR) + ", ?)), 0) "
              + "                end), 0) SumOst "                                                                
              + "   from (select distinct ac.* "
              + "           from ddlcontracc_dbt dlcacc, daccount_dbt ac "
              + "          where dlcacc.t_DlContrID = ? "                 
              + "            and ac.t_AccountID = dlcacc.t_AccountID) acc ";

       cmd = DL_RSDCommand(Query);

       cmd.AddParam(m_RepDate);
       cmd.AddParam(m_RepDate);
       cmd.AddParam(m_DlContrID);
                                                                         
       DataSet = cmd.Execute();                                                                                                         
       if( DataSet.MoveNext() )                                                                                                              
         SumOst = SQL_ConvTypeSum(DataSet.SumOst);
       end;

       if(SumOst < 0)
         SumOst = $0;
       end;                                                                                                                    

       return SumOst;                                                                                                                               
    END;

    macro TaxeYearClose()
       DateSplit(m_dlcSfContr.rec.DateClose, null, null, m_TaxeYearClose);
       return m_TaxeYearClose;
    end;

    macro ContractFirstNumber():string
       return m_dlcontr.rec.IISFirstNumber;
    end;

    macro ContractFirstOpenDate():date
       return m_dlcontr.rec.IISFirstDate;
    end;

    macro AccountFirstOpenDate()
       if(m_dlcontr.rec.IISTransformDate != date(0,0,0))
         return m_dlcontr.rec.IISTransformDate;
       elif(m_dlcontr.rec.IISTransformDateTaxAgent != date(0,0,0))
         return m_dlcontr.rec.IISTransformDateTaxAgent;
       end;

       return date(0,0,0);
    end;

    macro SumFromBegYear()
       var v_SumFromBegYear = $0;

       var Query = " SELECT SUM (DECODE(nptxop.t_IIS, CHR(88), nptxop.t_TaxSum2, nptxop.t_OutSum)) Sum "
                 + "   FROM dnptxop_dbt nptxop "
                 + "  WHERE nptxop.t_DocKind = ? "
                 + "    AND nptxop.t_SubKind_Operation = " + DL_NPTXOP_WRTKIND_ENROL
                 + "    AND nptxop.t_Contract IN (select mp.t_SfContrID from ddlcontrmp_dbt mp where mp.t_DlContrID = ?) "
                 + "    AND extract(year from (nptxop.t_OperDate)) = ? "
                 + "    AND (select rq.t_State from ddlrq_dbt rq "
                 + "          where rq.t_DocKind = nptxop.t_DocKind "
                 + "            and rq.t_DocID = nptxop.t_ID "
                 + "            and rq.t_DealPart = 1 "
                 + "            and rq.t_SubKind = ? "
                 + "            and rq.t_Type = ? "
                 + "            and rq.t_Num = 0 "
                 + "        ) = ?";/*считаем, что шаг <Формирование проводок> выполнен, если ТО по операции исполнено*/

       var cmd = DL_RSDCommand(query);

       cmd.AddParam(DL_WRTMONEY);
       cmd.AddParam(m_DlContrID);
       cmd.AddParam(TaxeYearClose());
       cmd.AddParam(DL_GetRQSubKindbyType(DLRQ_TYPE_PAYMENT));
       cmd.AddParam(DLRQ_TYPE_PAYMENT);
       cmd.AddParam(DLRQ_STATE_EXEC);         
       
       var DataSet = cmd.Execute();
       
       if(DataSet.moveNext())
          v_SumFromBegYear = SQL_ConvTypeSum(DataSet.Sum);
       end;

       return v_SumFromBegYear;
    end;

    macro SumFromBegYearPrevBrok()
       var v_SumFromBegYearPrevBrok = $0;

       if(m_dlcontr.rec.IISLastYear == this.TaxeYearClose())
         v_SumFromBegYearPrevBrok = m_dlcontr.rec.IISLastInSum;
       end;

       return v_SumFromBegYearPrevBrok;
    end;

    macro WithdrawnSum()
       var v_WithdrawnSum = $0;

       var Query = " SELECT SUM (DECODE(nptxop.t_IIS, CHR(88), nptxop.t_TaxSum2, nptxop.t_OutSum)) Sum "
                 + "   FROM dnptxop_dbt nptxop "
                 + "  WHERE nptxop.t_DocKind = ? "
                 + "    AND nptxop.t_SubKind_Operation = " + DL_NPTXOP_WRTKIND_WRTOFF
                 + "    AND nptxop.t_Contract IN (select mp.t_SfContrID from ddlcontrmp_dbt mp where mp.t_DlContrID = ?) "
                 + "    AND nptxop.t_PayMedical = 'X' " //Цель операции = <Оплата дорогостоящего лечения с ИИС-3>
                 + "    AND extract(year from (nptxop.t_OperDate)) = ? "
                 + "    AND (select rq.t_State from ddlrq_dbt rq "
                 + "          where rq.t_DocKind = nptxop.t_DocKind "
                 + "            and rq.t_DocID = nptxop.t_ID "
                 + "            and rq.t_DealPart = 1 "
                 + "            and rq.t_SubKind = ? "
                 + "            and rq.t_Type = ? "
                 + "            and rq.t_Num = 0 "
                 + "        ) = ?";/*считаем, что шаг <Формирование проводок> выполнен, если ТО по операции исполнено*/

       var cmd = DL_RSDCommand(query);

       cmd.AddParam(DL_WRTMONEY);
       cmd.AddParam(m_DlContrID);
       cmd.AddParam(TaxeYearClose());
       cmd.AddParam(DL_GetRQSubKindbyType(DLRQ_TYPE_PAYMENT));
       cmd.AddParam(DLRQ_TYPE_PAYMENT);
       cmd.AddParam(DLRQ_STATE_EXEC);         
       
       var DataSet = cmd.Execute();
       
       if(DataSet.moveNext())
          v_WithdrawnSum = SQL_ConvTypeSum(DataSet.Sum);
       end;

       return v_WithdrawnSum;
    end;

    macro WithdrawnSumPrevBrok()
       var v_WithdrawnSumPrevBrok = $0;

       if(m_dlcontr.rec.IISLastYear == this.TaxeYearClose())
         v_WithdrawnSumPrevBrok = m_dlcontr.rec.ISSLastOutSum;
       end;

       return v_WithdrawnSumPrevBrok;
    end;

    macro GetBegDate()

       var v_BegDate = date(0,0,0);

       var Query = " select min(NOBJ.T_DATE) MinDate "                    
                 + "   from dnptxobj_dbt nobj        "                    
                 + "  where NOBJ.T_CLIENT = ?        "                    
                 + "    and NOBJ.T_ANALITICKIND6 = " + TXOBJ_KIND6020
                 + "    and NOBJ.T_ANALITIC6 IN (SELECT MP.T_SFCONTRID FROM DDLCONTRMP_DBT MP WHERE MP.T_DLCONTRID = ?)";
 
       var cmd = DL_RSDCommand(query);
       
       cmd.AddParam(m_Investor);
       cmd.AddParam(m_DlContrID);
       
       var DataSet = cmd.Execute();
       
       if(DataSet.moveNext())
          v_BegDate = SQL_ConvTypeDate(DataSet.MinDate);
       end;

       if(m_IsConsolidate)
         Query =   " select NVL(min(t_TaxPeriod), 0) as MinTaxPeriod "
                 + "   from ddlcontrfrob_dbt "
                 + "  where t_DlContrID = ? ";

         cmd = DL_RSDCommand(query);
         cmd.AddParam(m_DlContrID);

         DataSet = cmd.Execute();
         if((DataSet.moveNext()) and (DataSet.MinTaxPeriod > 0))
           if((v_BegDate == date(0,0,0)) or (v_BegDate > date(1,1,int(DataSet.MinTaxPeriod))))
             v_BegDate = date(1,1,int(DataSet.MinTaxPeriod));
           end;
         end;
       end;
      
       return v_BegDate;
    end;

    macro getPositionOper(_oper:Integer)
       var local_PositionOper;
       var Query = "  SELECT typeac.t_Name_Type                      "+
                   "  FROM dtypeac_dbt typeac, dperson_dbt t         "+ 
                   "  WHERE                                          "+
                   "        t.T_CTYPEPERSON = TYPEAC.T_TYPE_ACCOUNT  "+
                   "    AND typeac.t_iNumType = 13                   "+
                   "    AND t.t_Oper = "+_oper;
       var DataSet = TRsbDataSet(query);
       if( DataSet.MoveNext() ) 
         local_PositionOper = (DataSet.Name_Type);
       end;
       return local_PositionOper;
    end;

    private macro PrintPlusMinusSum(Year,InfoRate,IsPrintYear:bool)

       var i = 0;
       var arr = null;
      
       arr = m_RepCalc.ПолучитьМассивПараметровДляПечати();

       if( not IsPrintYear )

          PrintFormatString( NULL,
                             "N1_TaxeYear", Year
                           );
          CopyAllSheetInTotalBook(null, false, "N1_TaxeYearHeader", 0, m_NewSheetName);
          IsPrintYear = true;
       end;

       CopyAllSheetInTotalBook(null, false, "N1_TableHeader", 0, m_NewSheetName);

       while( i < arr.size() )
         var j = 0;
         while(j < arr[i].NPTXCodeParmArr.Size)
      
           // Строка с кодом дохода выводится в отчет, если сумма дохода не нулевая или если сумма дохода нулевая и хотя бы одна из сумм вычета, входящих в одну группу с этой строкой дохода ненулевая. 
           // Если сумма вычета нулевая, то в отчет не выводится информация с кодом вычета.    
           // Если код вычета не стоит в одной строке с кодом дохода и сумма вычета нулевая, то строка с таким кодом вычета в отчет не выводится.
        
           if( ((arr[i].NPTXCodeParmArr[j].PSnnn == null) or (arr[i].NPTXCodeParmArr[j].PSnnn == $0)) and (arr[i].NPTXCodeParmArr[j].MinusCodes.Size == 0) )
              //не печатать
           else
              if((arr[i].NPTXCodeParmArr[j].MinusCodes.Size > 0) and (arr[i].NPTXCodeParmArr[j].MinusCodes[0].MSCode != NULL))
                 PrintFormatString( null,
                                    "N1_Plus",     arr[i].NPTXCodeParmArr[j].PSCode, 
                                    "N1_SumPlus",  arr[i].NPTXCodeParmArr[j].PSnnn,  
                                    "N1_Minus",    arr[i].NPTXCodeParmArr[j].MinusCodes[0].MSCode, 
                                    "N1_SumMinus", arr[i].NPTXCodeParmArr[j].MinusCodes[0].MSnnn
                                  );
              else
                 PrintFormatString( null,
                                    "N1_Plus",     arr[i].NPTXCodeParmArr[j].PSCode, 
                                    "N1_SumPlus",  arr[i].NPTXCodeParmArr[j].PSnnn,  
                                    "N1_Minus",    "", 
                                    "N1_SumMinus", ""
                                  );
              end;
              CopyAllSheetInTotalBook(null, false, "N1_Table_3_10", 0, m_NewSheetName);

              var k = 1;
              while(k < arr[i].NPTXCodeParmArr[j].MinusCodes.Size)
                 PrintFormatString( null,
                                    "N1_Plus",     "", 
                                    "N1_SumPlus",  0.0,  
                                    "N1_Minus",    arr[i].NPTXCodeParmArr[j].MinusCodes[k].MSCode, 
                                    "N1_SumMinus", arr[i].NPTXCodeParmArr[j].MinusCodes[k].MSnnn  
                               );
                 CopyAllSheetInTotalBook(null, false, "N1_Table_3_10", 0, m_NewSheetName);
                 k = k + 1;
              end;
           end;

           j = j + 1;
         end;
      
         i = i + 1;
       end;

    end;

    private macro PrintByNPTXTS()
       var OnDate = iif(m_dlcSfContr.rec.DateClose==date(0,0,0),m_RepDate,m_dlcSfContr.rec.DateClose);
   
       var cmd = DL_RSDCommand(" SELECT ts.t_Amount Qnty, BEGLOT.T_BUYDATE DateB, "
                              +"        BEGLOT.T_PRICE PrVp,                     "
                              +"        round(CURRLOT.T_NKD * ts.t_Amount / BEGLOT.T_AMOUNT,2) NKDVN,                     "
                              +"        (select t_shortname from dparty_dbt where t_partyid = FI.T_ISSUER) Issuer, "
                              +"        (select AVRKINDS.T_NAME from davrkinds_dbt avrkinds where AVRKINDS.T_FI_KIND = FI.T_FI_KIND and  AVRKINDS.T_AVOIRKIND = FI.T_AVOIRKIND) TypeSec, "
                              +"        case when av.t_isin = chr(1) then av.t_lsin else av.t_isin end ISIN,                                                                             "
                              +"        AV.T_INDEXNOM, (select decode(VP.T_ISO_NUMBER,810,643,VP.T_ISO_NUMBER) from dfininstr_dbt vp where vp.t_fiid = BEGLOT.T_PRICEFIID) VPrB,         "
                              +"        round((case when BEGLOT.T_PRICEFIID != 0 then Rsb_FIInstr.ConvSum(BEGLOT.T_TOTALCOST,BEGLOT.T_PRICEFIID,"+NATCUR+",BEGLOT.T_BUYDATE) else BEGLOT.T_TOTALCOST end) * ts.t_Amount / BEGLOT.T_AMOUNT,2) SumBRub, "
                              +"        rsb_fiinstr.FI_AvrKindsGetRoot( FI.T_FI_KIND, FI.T_AVOIRKIND ) AvrKindsRoot, "
                              +"        round((select NVL( Sum( RSB_FIInstr.ConvSum(Comis.t_Sum, M.T_FIID_COMM, "+NATCUR+", Comis.t_FactPayDate)),0)  "
                              +"                 from V_DlComisAll  Comis, DSFCOMISS_DBT M                                                   "
                              +"                where Comis.t_DocID = beglot.T_DOCID                                                         "
                              +"                  and Comis.t_DocKind = beglot.T_DOCKIND                                                     "
                              +"                  and (COMIS.T_FACTPAYDATE <= ? and COMIS.T_FACTPAYDATE != to_date('01.01.0001','DD.MM.YYYY')) " 
                              +"                  and Comis.t_IsBankExpenses <> 'X' "
                              +"                  and M.T_FEETYPE = Comis.T_FEETYPE                                                          "
                              +"                  and M.T_NUMBER  = Comis.T_COMNUMBER) * ts.t_Amount / BEGLOT.T_AMOUNT,2) Com                "
                              +"   FROM dnptxts_dbt ts, dnptxlot_dbt currlot, dnptxlot_dbt beglot, dfininstr_dbt fi, davoiriss_dbt av        "
                              +"  WHERE ts.T_CLIENT = ?                                         "
                              +"    and ts.T_CONTRACT IN (select mp.t_SfContrID from ddlcontrmp_dbt mp where mp.t_DlContrID = ?) "
                              +"    and ts.T_TYPE = ?                                           "
                              +"    AND ts.t_BegDate < ?       "
                              +"    AND ( ts.t_EndDate IS NULL  OR                              "
                              +"          ts.t_EndDate = to_date('01.01.0001','DD.MM.YYYY') OR  "
                              +"          ts.t_EndDate >= ?    "
                              +"        )                                                       "
                              +"    and CURRLOT.T_ID = ts.t_BuyID                               "
                              +"    and BEGLOT.T_ID = (case when NPTX.IsVirtual(CURRLOT.t_Type) = 1 then CURRLOT.t_RealID else CURRLOT.T_BEGLOTID end)"
                              +"    and begLOT.T_KIND = ? "
                              +"    and fi.t_FIID = ts.t_FIID "
                              +"    and AV.T_FIID = ts.t_FIID "
                              +" order by BEGLOT.T_BUYDATE "
                            ); 
                               
       cmd.addParam( OnDate );
       cmd.addParam( m_Investor );
       cmd.addParam( m_DlContrID );
       cmd.addParam( NPTXTS_REST);
       cmd.addParam( OnDate );
       cmd.addParam( OnDate );
       cmd.addParam( NPTXLOTS_BUY );
   
       var DataSet = cmd.execute();

       CopyAllSheetInTotalBook(null, false, "N1_3_11_Header", 0, m_NewSheetName);

       while( DataSet.MoveNext() )

          PrintFormatString( null,
                         "N1_DateB",         SQL_ConvTypeDATE(DataSet.DateB),   
                         "N1_IssuerTypeSec", SQL_ConvTypeStr(DataSet.Issuer) + ", " + SQL_ConvTypeStr(DataSet.TypeSec)+iif(DataSet.INDEXNOM," ИН",""), 
                         "N1_ISIN",          SQL_ConvTypeStr(DataSet.ISIN),     
                         "N1_Qnty",          SQL_ConvTypeSum(DataSet.Qnty),     
                         "N1_VPrB",          int(DataSet.VPrB),                 
                         "N1_PrVp",          SQL_ConvTypeSum(DataSet.PrVp),     
                         "N1_NKDVN",         iif(DataSet.AvrKindsRoot==AVOIRISSKIND_BOND,SQL_ConvTypeSum(DataSet.NKDVN),$0), 
                         "N1_Com",           SQL_ConvTypeSum(DataSet.Com),
                         "N1_StorageCost",   $0,
                         "N1_SumBRub",       SQL_ConvTypeSum(DataSet.SumBRub)
                        );

          CopyAllSheetInTotalBook(null, false, "N1_Table_3_11", 0, m_NewSheetName);

       end;
   
    END;

    private macro PrintInfBrok()
      var query, cmd, DataSet;

      query =   " select t_BrokerName as BrokerName, "
              + "        t_ContrNumber as BrokerContraktNumber, "
              + "        t_ContrDate as BrokerContraktDate, "
              + "        t_NumAndDateRef as BrokerDocument, "
              + "        t_ListCount as NumberOfSheets "
              + "   from ddlcontrinfbrok_dbt "
              + "  where t_DlContrID = ? "
              + " order by t_RecNum ASC";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(m_DlContrID);

      DataSet = cmd.Execute();

      CopyAllSheetInTotalBook(null, false, "N1_Part4Header", 0, m_NewSheetName);

      while( DataSet.MoveNext() )
        PrintFormatString( null,
                          "N1_BrokerName",            DataSet.BrokerName,                                
                          "N1_BrokerContraktNumber",  DataSet.BrokerContraktNumber,        
                          "N1_BrokerContraktDate",    date(DataSet.BrokerContraktDate):f,                
                          "N1_BrokerDocument",        DataSet.BrokerDocument,                                    
                          "N1_NumberOfSheets",        int(DataSet.NumberOfSheets)     
                         );

        CopyAllSheetInTotalBook(null, false, "N1_Table_4_1", 0, m_NewSheetName);
      end;

      CopyAllSheetInTotalBook(null, false, "N1_Part4Footer", 0, m_NewSheetName);
    end;
    
    private macro Create()
       var Query = "", count = 0;
       var DataSet = null;

       m_RepCalc = NPTXPHYSIISINFO(m_RepDate, m_Investor, m_DlContrID, m_IsConsolidate);
       count = m_RepCalc.Count();  
    
       if( (count > 0) and m_RepCalc.Next() )

          PrintFormatString(null, "N1_OurNumber", IIF(m_DocNumber != "", m_DocNumber, "Б/н"),
                                  "N1_DateOfSignature", m_RepDate,
                                  "N1_ConsolidatTextStr", IIF(m_IsConsolidate, "Консолидированные данные*", "Неконсолидированные данные*")
                           );
        
          PrintFormatString(null, "N1_FIO", m_RepCalc.FIO(),
                                  "N1_DateBirth", string(m_RepCalc.DateBirth():f),
                                  "N1_PlaceOfBirth", m_RepCalc.BirthPlace(),
                                  "N1_ClientINN", m_RepCalc.ClientINN(),
                                  "N1_ResidenceAdress", m_RepCalc.ResidenceAdress(),
                                  "N1_Document", m_RepCalc.Document());
        
          PrintFormatString(null, "N1_OurName", m_RepCalc.OurName(),
                                  "N1_OurInnKPP", m_RepCalc.OurInn() + "/" + m_RepCalc.OurKPP(),
                                  "N1_OurAdress", m_RepCalc.OurAdress(),
                                  "N1_OurContactPhone", ТелефонДляСправкиПоИИС());

          PrintFormatString(null, "N1_ContractNumberOpenDate", string(m_dlcSfContr.rec.Number) + " от " + string(m_dlcSfContr.rec.DateBegin:f),
                                  "N1_ContractCloseDate", string(m_dlcSfContr.rec.DateClose:f),
                                  "N1_AccountOpenDate", string(AccountOpenDate():f),//
                                  "N1_AccountCloseDate", string(AccountCloseDate():f),//
                                  "N1_ContractFirstNumberOpenDate", 
                                   IIF (string(this.ContractFirstNumber())      != "" , 
                                   IIF (string(this.ContractFirstOpenDate():f)  != "" ,
                                       (string(this.ContractFirstNumber()) +" от " + string(this.ContractFirstOpenDate():f)      ),
                                       ""),
                                   ""),
                                  "N1_AccountFirstOpenDate", string(this.AccountFirstOpenDate():f),//
                                  "N1_TaxeYearClose", iif(TaxeYearClose()>0,TaxeYearClose(),"")
                           );

          CopyAllSheetInTotalBook(null, false, "N1_Header", 0, m_NewSheetName);

          if(m_IsConsolidate)
            PrintFormatString(null, "N1_SumPrevBrok", SumFromBegYearPrevBrok(),
                                    "N1_SumBank",     SumFromBegYear()
                            );
            CopyAllSheetInTotalBook(null, false, "N1_Line3_8_Consolidate", 0, m_NewSheetName);

            PrintFormatString(null, "N1_WithdrawnSumPrevBrok", WithdrawnSumPrevBrok(),
                                    "N1_WithdrawnSumBank", WithdrawnSum()
                            );
            CopyAllSheetInTotalBook(null, false, "N1_Line3_9_Consolidate", 0, m_NewSheetName);
          else
            PrintFormatString(null, "N1_Sum", SumFromBegYear());
            CopyAllSheetInTotalBook(null, false, "N1_Line3_8", 0, m_NewSheetName);

            PrintFormatString(null, "N1_WithdrawnSum", WithdrawnSum());
            CopyAllSheetInTotalBook(null, false, "N1_Line3_9", 0, m_NewSheetName);
          end;

          var v_CurrYear, v_EndYear, v_EndDate, v_IsPrintYear = false;
          var RateIndexes;
          
          DateSplit(GetBegDate(), null, null, v_CurrYear);
          DateSplit(m_RepDate, null, null, v_EndYear);

          CopyAllSheetInTotalBook(null, false, "N1_Header3_10", 0, m_NewSheetName);

          if( v_CurrYear > 0 )
             //собрать значения доходов и вычетов
             while(v_CurrYear <= v_EndYear)
                if( v_EndYear == v_CurrYear )
                   v_EndDate = m_RepDate;
                else
                   v_EndDate = date(31,12,v_CurrYear); //последний день года
                end;
               
                //справка по общей ставке
                RateIndexes = m_RepCalc.CalcData(v_EndDate, INFORATE_TOTAL, 1);
                if((RateIndexes.Size > 0) and ((m_RepCalc.PlusSum(RateIndexes[0]) != $0) or (m_RepCalc.MinusSum(RateIndexes[0]) != $0)))
                  PrintPlusMinusSum(v_CurrYear,INFORATE_TOTAL,v_IsPrintYear);
                end;
               
                v_CurrYear = v_CurrYear + 1; //переходим в следующий год
            
                v_IsPrintYear = false;
             end;
          end;

          PrintByNPTXTS();

          PrintFormatString(null, "N1_SumOst", SumOst());
          CopyAllSheetInTotalBook(null, false, "N1_OtherHeader", 0, m_NewSheetName);

          //Печать сведений о других брокерах (раздел 4)
          PrintInfBrok();

          PrintFormatString(null, "N1_FooterConsolidateText", IIF(m_IsConsolidate, "*в п. 3.8, 3.9, 3.10 включены данные иных профучастников (при наличии)", "*в п. 3.8, 3.9, 3.10 НЕ включены данные иных профучастников (при наличии)"),
                                  "N1_PositionOper", m_Post,
                                  "N1_Oper", m_Signer,
                                  "N1_MP", "М.П.");

          CopyAllSheetInTotalBook(null, false, "N1_RepFooter", 0, m_NewSheetName);

       else
          PrintFormatString( NULL, "N1_MsgNotData", "Нет данных, удовлетворяющих параметрам отчета", m_NewSheetName);
          CopyAllSheetInTotalBook( NULL, false, "N1_MsgNotData", 0 );
       end;

    end;

    private macro EndReport()
       SaveTotalBook();
    end;

    MACRO Run()
      Run();
      
      for (var fileName, GetFullReportFileNames())
        return fileName;
      end;

      return "";
    END;


    initDL_CReportTemplate(NULL, "Report_NpTxPhysIISInfo.xlsx");
end;


macro CreateNpTxPhysIISInfoRep(RepDate,       
                               ClientID,      
                               DlContrID,     
                               IsConsolidate, 
                               ToExcel,       
                               ToPDF,         
                               Signatory,     
                               SendToClient,
                               AskToSend,  
                               DocumentNumber,
                               ExcelFullRepPath:@string,
                               PDFFullRepPath:@string,
                               ShowRep:bool
                              )
   var SignImgPath = "";
   var Post = "";
   var Signer = "";
   var query, cmd, DataSet;
   var ClientFullName = "", ClientShortName = "", DlContrNumber = "";
   var ToDir = GetRepPath(), FromDir = "";
   var Year = 0;
   var day, mon, y;
   var hour, mn, sec;
   var FileName, FileExt;
   var FullRepFileNameXLSXOnTerm = "", NewFileName = "", NewFileNameXLSX = "", NewFileNamePDF = "";
   var err = 0;

   cmd = DL_RSDCommand();
   query = "select t_Name, t_ShortName from dparty_dbt where t_PartyID = " + cmd.AddParam(ClientID);

   DataSet = cmd.Execute(query);
   if(DataSet.moveNext())
     ClientShortName = DataSet.ShortName;
     ClientFullName = DataSet.Name;
   end;

   cmd = DL_RSDCommand();
   query = "select t_Number from ddlcontr_dbt dlc, dsfcontr_dbt sf where dlc.t_DlContrID = "+cmd.AddParam(DlContrID)+" and sf.t_ID = dlc.t_SfContrID ";
   DataSet = cmd.Execute(query);
   if(DataSet.moveNext())
     DlContrNumber = DataSet.Number;
   end;

   if(Signatory > 0)
     cmd = DL_RSDCommand();

     query =   " select pt.t_ShortName, ofc.t_Post, "
             + "        NVL((select img.t_ImageID "
             + "               from dimgdata_dbt img "
             + "              where img.t_ObjectType = " + OBJTYPE_PARTY
             + "                and img.t_ObjectID = LPAD(pt.t_PartyID, 10, 0) " 
             + "                and img.t_ImageType = 4 "
             + "                and img.t_CloseDate = " + GetSQLDate(date(0,0,0))
             + "                and ROWNUM = 1 "
             + "            ), 0) as ImgID "
             + "   from dperson_dbt ps, dparty_dbt pt, dofficer_dbt ofc "
             + "  where ps.t_Oper = " + cmd.AddParam(Signatory)
             + "    and pt.t_PartyID = ps.t_PartyID "
             + "    and ofc.t_PersonID = pt.t_PartyID ";

     DataSet = cmd.Execute(query);
     if(DataSet.moveNext())
       Post        = DataSet.Post;
       Signer      = DataSet.ShortName;
       
       if((DataSet.ImgID > 0) and (ToPDF == SET_CHAR))
         WEB_ShowImageByID(int(DataSet.ImgID), @SignImgPath); 
       end;
     end;
   end;

   ExcelFullRepPath = "";
   PDFFullRepPath = "";

   datesplit(RepDate, null, null, Year);

   var Rep =  NpTxPhysIISInfoReport(RepDate,       
                                    ClientID,      
                                    DlContrID,     
                                    IsConsolidate, 
                                    DocumentNumber,
                                    Post,
                                    Signer
                                   );

   FullRepFileNameXLSXOnTerm = Rep.Run();

   if(not isStandAlone())
     FullRepFileNameXLSXOnTerm = "$"+FullRepFileNameXLSXOnTerm;

     if(SubStr(ToDir, 1, 1 ) != "$")
       ToDir = "$"+ToDir;
     end;
   end;

   Rep = NULL;

   DateSplit(date(), day, mon, y);
   TimeSplit(time(), hour, mn, sec);
   
   FromDir = SplitFile(FullRepFileNameXLSXOnTerm, FileName, FileExt);
   FromDir = StrSubst(FromDir, "\\\\","\\");
   
   NewFileName = "Сведения о ФЛ и его ИИС_"+ClientShortName+"_Договор № "+StrSubst(DlContrNumber,"/","-")+
                 "_"+string(day:o:2)+string(mon:o:2)+string(y)+"_"+string(hour:o:2)+string(mn:o:2)+string(sec:o:2);
   NewFileNameXLSX = NewFileName + ".xlsx"; 

   var ExcelRepFullFileName = ToDir+NewFileNameXLSX;
   var AppServExcelRepFullFileName = ExcelRepFullFileName;
   var PDFRepFullFileName = "";
   var AppServPDFRepFullFileName = "";
   var toPath = "";
   var t    = NULL;
   var cr   = NULL;
   var book = NULL;
   
   if(not RenameFile(FullRepFileNameXLSXOnTerm, FromDir+NewFileNameXLSX))
      msgbox("Ошибка при переименовании файла отчета");
   end;

   if(not ExistFile(ExcelRepFullFileName))
     err = SC_CopyFile(FromDir, ToDir, NewFileNameXLSX, NewFileNameXLSX);
     if(err)
       msgbox("Ошибка при перемещении файла отчета");
     end;
   end;

   //Копируем Excel файл на СП при необходимости
   if (SubStr(ExcelRepFullFileName, 1, 1 ) == "$")
     toPath = PathCombine(GetAbsoluteTxtPath(),NewFileNameXLSX);
     if( not CopyFile(ExcelRepFullFileName, toPath) )
       RunError( "Ошибка при копировании файла", CustomError("Ошибка при копировании файла " + ExcelRepFullFileName + " в " + toPath));
     end;
     AppServExcelRepFullFileName = toPath;
   end;

   t    = CTemplateXLS(NULL, NULL, NULL, NULL, NULL, false, true /*POI*/);
   cr   = t.GetReportCreator();
   book = cr.openWorkbook(AppServExcelRepFullFileName);
    
   book.setActiveSheet(0);

   AppServExcelRepFullFileName = toANSI(book.getWorkbookPath() + "\\" + book.getWorkbookName());
   
   if(ToPDF)
      var PathToPDF = toANSI(book.getWorkbookPath() + "\\PDF" + book.getWorkbookName());

      //DEF-108271
      var SheetName = IIF(IsConsolidate == SET_CHAR, "Консолид. данные", "Неконсолид. данные");
      var poiCurrentSheet = book.{getSheet@(Ljava/lang/String;)Lorg/apache/poi/ss/usermodel/Sheet;}(SheetName);
   
      var lastRow = poiCurrentSheet.getLastRowNum() + 1;
      var row = 0;
      var SumHeight = 0;
      var SumHeight4_2 = 0;
      var Start4_2 = 0;
      var EndRow = 0;

      while(row < lastRow)
        var poiCurrentRow = poiCurrentSheet.{getRow@(I)Lorg/apache/poi/ss/usermodel/Row;}(row);
        var HeightInPoints = poiCurrentRow.getHeightInPoints();

        if((SumHeight + HeightInPoints) > 725)
          SumHeight = HeightInPoints;
        else
          SumHeight = SumHeight + HeightInPoints;
        end;

        var val = book.getCellValue("A" + (row+1));
        if((val == "4.2.") and (Start4_2 == 0))
          Start4_2 = row;
          SumHeight4_2 = SumHeight;
        end;

        val = book.getCellValue("C" + (row+1));
        if(val == "М.П.")
          EndRow = row + 1;
        end;

        row = row + 1; 
      end;

      if((SumHeight4_2 + 420) > 725)
        SumHeight = SumHeight4_2;
        var shift = 1;
        while(SumHeight < 725)
          SumHeight = SumHeight + 14.5;
          shift = shift + 1;
        end;

        poiCurrentSheet.shiftRows(Start4_2, EndRow, shift);
        EndRow = EndRow + shift;
      end;
 
     //Добавить печать подписанта в отчет
     if(SignImgPath != "")
       book.addPictureBase(0, SignImgPath, book.getRangeFirstCol("C" + EndRow), book.getRangeFirstRow("C" + EndRow));
     end;

     book.saveAs(PathToPDF);

     NewFileNamePDF = NewFileName + ".pdf";
     PDFRepFullFileName = ToDir+NewFileNamePDF;

     if (IsRunFromWine(true))
       if (not WR_ConvertToFormatLibreOffice( PathToPDF, PDFRepFullFileName, WINREP_FORMAT_PDF, false))
         err = 1;
       end;
     else
       err = SC_ConvertToFormat(PathToPDF, PDFRepFullFileName, SC_SRVREPFORMAT_PDF, false);
     end;
     if(err)
       msgbox("Ошибка при конвертации отчета в PDF");
     end;
   end;

   book.close();
   book = null;
   t.Close();

   if(ToExcel == UNSET_CHAR)
     RemoveFile(ExcelRepFullFileName);
     if(ExcelRepFullFileName != PathToPDF)
       RemoveFile(PathToPDF);
     end;
     NewFileNameXLSX = "";
   end;

   if(ToExcel)
     ExcelFullRepPath = ExcelRepFullFileName;
     if(ShowRep)
       SC_ShowFile("", ExcelFullRepPath);
     end;
   end;

   if(ToPDF)
     PDFFullRepPath = PDFRepFullFileName;
     if(ShowRep)
       SC_ShowFile("", PDFFullRepPath);
     end;
   end;

   if((SendToClient == SET_CHAR) and (AskToSend == SET_CHAR))
     SendToClient = UNSET_CHAR;
     
     if(GetTrue(true, "Отправить Сведения о ФЛ и его ИИС клиенту?"))
       SendToClient = SET_CHAR;
     end;
   end;

   if(SendToClient == SET_CHAR)
     var MainEmail;

     GetClientEMails(ClientID, DlContrID, RepDate, @MainEmail);

     if(MainEmail == "")
       msgbox("Не найден адрес электронной почты. Справка не отправлена");
     else
       var v_email_processor, Subject, emailText;
       Subject = ClientFullName+". Сведения о ФЛ и его ИИС. Договор №" + DlContrNumber;
       
       emailText =   "Уважаемый(мая) <b>"+ClientFullName+"</b>!"
                   + "<br><br>"
                   + "В связи с переводом ИИС другому Брокеру направляем Вам справку <Сведения о физическом лице и о его индивидуальном инвестиционном счете>.";

       emailText =   "<div>"
                   + emailText
                   + "<br>" 
                   + "<br>"
                   + "С уважением," 
                   + "<br>АО \x22Россельхозбанк\x22" 
                   + "</div>";

       v_email_processor = c_email_proc_env();

       v_email_processor.m_add_email_to_list(MainEmail);
       
       var BccList:string = GetNPTXBccList(); //список адресов для отправки скрытой копии
       if(BccList != "")
         v_email_processor.m_add_email_to_bcc_list(BccList);
       end;

       if(NewFileNameXLSX != "")
         v_email_processor.m_add_attach_to_list(AppServExcelRepFullFileName);
       end;

       if(NewFileNamePDF != "")
         AppServPDFRepFullFileName = PDFRepFullFileName;
         //Копируем PDF файл на СП
         if (SubStr(PDFRepFullFileName, 1, 1 ) == "$")
           toPath = PathCombine(GetAbsoluteTxtPath(),NewFileNamePDF);
           if( not CopyFile(PDFRepFullFileName, toPath) )
             RunError( "Ошибка при копировании файла", CustomError("Ошибка при копировании файла " + PDFRepFullFileName + " в " + toPath));
           end;
           AppServPDFRepFullFileName = toPath;
         end;
         v_email_processor.m_add_attach_to_list(AppServPDFRepFullFileName);
       end;

       //Прикрепить все справки от других брокеров, если они есть
       cmd = DL_RSDCommand();

       query = " select img.t_ImageID "
             + "   from dimgdata_dbt img "
             + "  where img.t_ObjectType = " + OBJTYPE_BROKERCONTR_DL
             + "    and img.t_ObjectID = LPAD("+cmd.AddParam(DlContrID)+", 34, 0) " 
             + "    and img.t_ImageType = 102 " //Сведения о ФЛ и его ИИС, предоставл. пред. брокером
             + "    and img.t_CloseDate = " + GetSQLDate(date(0,0,0));

       DataSet = cmd.Execute(query);
       while(DataSet.moveNext())
         var FileInfoPath = "";
         WEB_ShowImageByID(int(DataSet.ImageID), @FileInfoPath);
         if(FileInfoPath != "")
           v_email_processor.m_add_attach_to_list(FileInfoPath);
         end;
       end;
      
       v_email_processor.m_set_msg_head(Subject);
       v_email_processor.m_add_row_to_msg_text(emailText);

       while(1 == 1)
         var OldDialogFlag = SetDialogFlag(0); //Включаем безынтерфейсный режим
         
         v_email_processor.m_save_to_submit_synch(true);
         
         var ErrStr = GetErrMsg();

         SetDialogFlag(OldDialogFlag);

         if(index(ErrStr, "Процесс не может получить доступ к файлу") > 0)
           msgbox("Файл отчета не закрыт. Чтобы выполнить отправку закройте его");
         else
           break;
         end;
       end;
       
       
       v_email_processor.m_submit_email_synch();

       if(v_email_processor.m_get_error_status())
         msgbox("Справка не отправлена");
       else
         msgbox("Справка отправлена");
       end;
     end;
   end;

   if((NewFileNamePDF != "") and (AppServPDFRepFullFileName != PDFRepFullFileName) and (AppServPDFRepFullFileName != ""))
     RemoveFile(AppServPDFRepFullFileName);
   end;

   if((NewFileNameXLSX != "") and (ExcelRepFullFileName != AppServExcelRepFullFileName) and (AppServExcelRepFullFileName != ""))
     RemoveFile(AppServExcelRepFullFileName);
   end;

end;
