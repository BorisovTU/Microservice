/*******************************************************************************
 DESCRIPTION  :   Отчет "Регистры по РЕПО" (НДФЛ) - форма ввода данных
 PROGRAMMED BY:   Макаров А.Г.
 CREATION DATE:   24.01.11
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_REPOREG_PERIOD     = 0,
      PNFLD_REPOREG_YEAR       = 1,
      PNFLD_REPOREG_GROWTH     = 2,
      PNFLD_REPOREG_BEGINDATE  = 3,
      PNFLD_REPOREG_ENDDATE    = 4,
      PNFLD_REPOREG_CLIENTCODE = 5,
      PNFLD_REPOREG_CLIENTNAME = 6,
      PNFLD_REPOREG_AVRGROUP   = 7,
      PNFLD_REPOREG_AVRKIND    = 8,
      PNFLD_REPOREG_AVRCODE    = 9,
      PNFLD_REPOREG_AVRNAME    = 10,
      PNFLD_REPOREG_DEALSTATUS = 11,
      PNFLD_REPOREG_PRINTMETHOD= 12;

/*Подвид отчета*/

CONST REPOREG_0501   = 0, // рублевые обратные РЕПО
      REPOREG_0502   = 1, // рублевые прямые РЕПО
      REPOREG_0503   = 2, // валютные обратные РЕПО
      REPOREG_0504   = 3, // валютные прямые РЕПО
      REPOREG_0505   = 4, // обратные РЕПО, ВР не совпадает с ВН
      REPOREG_0506   = 5; // прямые РЕПО, ВР не совпадает с ВН


//Статусы отбираемых сделок
CONST REPOREG_DEALSTATUS_ALL     = 0,  //Все
      REPOREG_DEALSTATUS_CLOSE   = 1,  //Закрытые
      REPOREG_DEALSTATUS_NOCLOSE = 2;  //Незакрытые

PRIVATE VAR REPOREG_DEALSTATUS_NAME = TArray();
REPOREG_DEALSTATUS_NAME[REPOREG_DEALSTATUS_ALL]     = "Все";
REPOREG_DEALSTATUS_NAME[REPOREG_DEALSTATUS_CLOSE]   = "Закрытые";
REPOREG_DEALSTATUS_NAME[REPOREG_DEALSTATUS_NOCLOSE] = "Незакрытые";


/*Статус отбираемых сделок*/
PRIVATE MACRO FldProc_DealStatus( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = REPOREG_DEALSTATUS_ALL;
     end;
     FldShowValue = REPOREG_DEALSTATUS_NAME[FldRealValue];

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    REPOREG_DEALSTATUS_NAME[REPOREG_DEALSTATUS_ALL],     REPOREG_DEALSTATUS_ALL,
                    REPOREG_DEALSTATUS_NAME[REPOREG_DEALSTATUS_CLOSE],   REPOREG_DEALSTATUS_CLOSE,
                    REPOREG_DEALSTATUS_NAME[REPOREG_DEALSTATUS_NOCLOSE], REPOREG_DEALSTATUS_NOCLOSE
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = REPOREG_DEALSTATUS_ALL;
     FldShowValue = REPOREG_DEALSTATUS_NAME[FldRealValue];
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  end; 
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) NP_RepoReg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     AddFieldProc( 0, PNFLD_REPOREG_PERIOD,      DL_PNFLD_PERIOD,              @DL_FldProc_Period, PrevQuartal + 12, 17, 5 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_REPOREG_YEAR,        DL_PNFLD_YEAR,                @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_REPOREG_GROWTH,      DL_PNFLD_GROWTH,              @DL_FldProc_Growth);
     AddFieldProc( 0, PNFLD_REPOREG_BEGINDATE,   DL_PNFLD_BEGINDATE,           @DL_FldProc_BeginDate );
     AddFieldProc( 0, PNFLD_REPOREG_ENDDATE,     DL_PNFLD_ENDDATE,             @DL_FldProc_EndDate );
     AddFieldProc( 0, PNFLD_REPOREG_CLIENTCODE,  DL_PNFLD_CLIENT_STOCKDL_CODE, @DL_FldProc_Client_STOCKDL_Code );
     AddFieldProc( 0, PNFLD_REPOREG_CLIENTNAME,  DL_PNFLD_CLIENT_STOCKDL_NAME, @Default );
     AddFieldProc( 0, PNFLD_REPOREG_AVRGROUP,    DL_PNFLD_AVRGROUP,            @DL_FldProc_NpTxGroup, null, 10, 5 );
     AddFieldProc( 0, PNFLD_REPOREG_AVRKIND,     DL_PNFLD_AVRKIND,             @DL_FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_REPOREG_AVRCODE,     DL_PNFLD_AVRCODE,             @DL_FldProc_NpTxAvrCode );
     AddFieldProc( 0, PNFLD_REPOREG_AVRNAME,     DL_PNFLD_AVRNAME,             @Default );
     AddFieldProc( 0, PNFLD_REPOREG_DEALSTATUS,  DL_USERFIELD,                 @FldProc_DealStatus, null, 36, 19 );
     AddFieldProc( 0, PNFLD_REPOREG_PRINTMETHOD, DL_PNFLD_PRINTMETHOD,         @DL_FldProc_NpTxPrintMethod, DL_OUTREPORT_EXCEL, 36, 21 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "NPREGREP" ); 
END;
