/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank v5.0                                     R-Style Software Lab Ltd.

ФАЙЛ: sp_ord.mac

ОПИСАНИЕ:      Отчеты бэк-офиса ценных бумаг.

          Макрос печати договора на заключение сделки.
          Программый интерфейс обеспечивает возможность вызова из
          модуля операций.

                Макрос пользуется константами и функциями макрофайла
                secinter.mac, поэтому для его нормальной работы необходимо
                указать в файле secinter.mac константу {OurBank}, являющуюся
                идентификатором банка.Эта константа необходима для определения
                наличия или отсутствия комитента.

                Также необходимо указать константы настройки макроса,
                предоставленные ниже в секции "КОНСТАНТЫ настройки макроса".

ПРОГРАММИСТ: Сабитов Р.Р.

СОЗДАН: 31.08.98

└───────────────────────────────────────────────────────────────────────────*/
Import FIInter,PTInter,BankInter,secinter, dlcnst, "adress.mac";

/*******************************************************************************
КОНСТАНТЫ настройки макроса
*******************************************************************************/
Const 
   Days_Reregestration=1,     /*Кол-во дней для перерегистрации*/
   Days_Pay=1,           /*Кол-во дней для оплаты суммы сделки*/
   Days_Pay_Error=1;          /*Кол-во дней для оплаты суммы сделки в случае
                                  ненадлежащего выполнения Продавцом обязательств*/

Const          /*Константы, определяющие местонахождение данных в массиве*/
     Name=0,             /*0-    Имя покупателя */
     Name_Boss=1,        /*1- Должность ответственного лица*/
     FIO_Boss=2,         /*2- ФИО ответственного лица*/
     UAdress=3,          /*3- Юридический адрес*/
     PAdress=4,          /*4- Почтовый адрес*/
     INN=5,              /*5- ИНН*/
     OKPO=6,             /*6- Код ОКПО*/
     OKONH=7,       /*7- Код ОКОНХ*/
     RasAcc=8,      /*8- Расчетный счет*/
     Bank=9,             /*9- Банк*/
     CorAcc=10,          /*10-     кор. счет*/
     Registry=11,        /*11-     Св-во о регистрации*/
     TelFax=12;          /*12-     Телефон/Факс*/



/*******************************************************************************
Find_Var - макрос ищет переменные для вывода в отчет
*******************************************************************************/
macro Print_dl_ord (FirstDoc)
     record r_pmpaym (pmpaym);   /* платежи сделок */
     record r_dl_tick (dl_tick);   /* первичный документ */
     record r_party (party);       /* инф. о сторонах */   
     record r_fininstr (fininstr); /* инф. о финансовых инструментах */
     record r_avoiriss (avoiriss);   /* инф. о ценных бумагах */
     record RecordAdress ( adress );

     file f_dl_order (dl_order) ; /* Договора для сделок с ЦБ */
     file f_oprkoper (oprkoper) ; /* Виды договоров */
     


          /*Переменные*/
  var 
    IsBuyer,        /*Флаг:  TRUE - мы покупаем, FALSE - мы продаем*/
    IsComitent,          /* TRUE - комитент есть, FALSE - комитента нет, банк действует от своего имени*/
    IsPrepare,      /* TRUE - готовим договор , FALSE - не готовим */

    Order_Date,          /*Дата договора*/
    Order_Num,      /*Номер договора*/

    Deal_Date,      /*Дата заключения сделки*/
    Amount,         /*Количество ЦБ*/
    Deal_Sum,       /*Сумма сделки*/
    Price,          /*Цена ЦБ*/
    Price_Curr_Name,     /*Валюта цены ЦБ*/
    Pay_Curr_Name,  /*Валюта платежа*/

    FI_Emitent_Name,     /*Эмитент*/
    FI_Name,        /*Ценная бумага*/
    FI_Nominal:double = 0.0,          /*Номинал ценной бумаги*/
    FI_Curr_Name,   /*Валюта номинала ЦБ */

    Order_Comiss=1, /*Договор комиссии*//*Еще нет таблиц*/
    Order_Comiss_Date=1;/*Дата договора комиссии*//*Еще нет таблиц*/

    Array Trader,Buyer,Comitent; /*Массивы продавца,покупателя,клиента соответственно*/

   macro Find_Var () /* Поиск информации */
             /*Файлы и структуры*/
       
          /**************************
          Инициализация буферов
          **************************/
   var 
      Pay_CurrID,   /*Идентификатор валюты платежа*/
      Price_CurrID, /*Идентификатор валюты цены ЦБ*/
      FI_CurrID,    /*Идентификатор валюты номинала ЦБ*/
      FI_EmitentID;  /*Идентификатор эмитента */
       

    /* FirstDoc - структура типа dl_tick */
    SetBuff (r_dl_tick,FirstDoc) ;
    Deal_Date=r_dl_tick.DealDate;

    /*Выясняем,мы покупатель, продавец или кто?*/
    f_oprkoper.Kind_Operation=r_dl_tick.DealType;
    if (not GetEQ(f_oprkoper))
     MsgBox ("Отсутствует информация о виде операции (DealType=",r_dl_tick.DealType,")");
     return FALSE;
    end;
    if ((Index(f_oprkoper.SysTypes,"B")!=0) and
        (Index(f_oprkoper.SysTypes,"S")==0)) IsBuyer=TRUE;/* Мы- покупатель*/
      elif ((Index(f_oprkoper.SysTypes,"S")!=0) and
           (Index(f_oprkoper.SysTypes,"B")==0)) IsBuyer=FALSE; /*Мы-продавец*/
      else MsgBox ("Неясен тип операции|",f_oprkoper.Name,":|покупка (B) или продажа (S).");
           return FALSE; /*Мы - неизвестно кто, это ошибка*/
    end;

    /* Выясняем, надо ли готовить договор */
    if (r_dl_tick.flag2=="X")      /*договор готовим мы*/
        IsPrepare=TRUE;
    else            /* договор готовим не мы,*/
        IsPrepare=FALSE;
        return 0;              /*поэтому выходим из макроса*/
    end;
    /* Ищем в платежах поставку основного актива*/
    if (not GetPaymentInfo(r_dl_tick.DealID,PurpBase,r_pmpaym))
       MsgBox ("Отсутствуют платеж на поставку основного актива(Код сделки=",r_dl_tick.DealCode,")");
       return FALSE;
    end;   
    Amount=r_pmpaym.Amount;
    
     /*Ищем инф. о фин. инстр*/
    if ((ПолучитьФинИн(r_pmpaym.FIID,r_fininstr,r_avoiriss, NULL ))!=0) 
     MsgBox ("Отсутствует финансовый инструмент (FIID=",r_pmpaym.FIID,")");
     return FALSE;
    end;
    FI_EmitentID=r_fininstr.Issuer;
    FI_Name=r_fininstr.Name;
    FI_GetNominal(r_fininstr.FIID, FI_Nominal);
    FI_CurrID=r_fininstr.FaceValueFI;
    if ((ПолучитьСубъекта  (FI_EmitentID, r_party))!=0)
     MsgBox ("Отсутствует эмитент (PartyID=",FI_EmitentID,")");
     return FALSE;
    end;
    FI_Emitent_Name=r_party.Name;

    /* Ищем в платежах поставку контрактива*/
    if (not GetPaymentInfo(r_dl_tick.DealID,PurpContr,r_pmpaym))
     MsgBox ("Отсутствуют платеж на поставку контрактива (Код сделки=",r_dl_tick.DealCode,")");     return FALSE;
    end;
    Deal_Sum=r_pmpaym.Amount;
    Price_CurrID=r_pmpaym.FIID;
    Pay_CurrID=r_pmpaym.PayFIID;

     /*Ищем инф. о валютах*/
    if ((ПолучитьФинИн(FI_CurrID,r_fininstr,r_avoiriss, NULL ))!=0)
     MsgBox ("Отсутствует финансовый инструмент (FIID=",FI_CurrID,")");    
     return FALSE;
    end;    
    FI_Curr_Name=r_fininstr.Name;

    if ((ПолучитьФинИн(Pay_CurrID,r_fininstr,r_avoiriss, NULL ))!=0)
     MsgBox ("Отсутствует финансовый инструмент (FIID=",Pay_CurrID,")");   
     return FALSE;
    end;    
    Pay_Curr_Name=r_fininstr.Name;

    if ((ПолучитьФинИн(Price_CurrID,r_fininstr,r_avoiriss, NULL ))!=0)
     MsgBox ("Отсутствует финансовый инструмент (FIID=",Price_CurrID,")"); 
     return FALSE;
    end;    
    Price_Curr_Name=r_fininstr.Name;

    /* Ищем информацию о договоре*/
    f_dl_order.DealID=r_dl_tick.DealID;
    if (not GetEQ(f_dl_order))
     MsgBox ("Отсутствует информация о договоре (Код сделки=",r_dl_tick.DealCode,")");
     return FALSE;
    end; 
    Order_Date=f_dl_order.CreateDate;
    Order_Num=f_dl_order.OrderNumber;

    Price=Deal_Sum/Amount;

    /* Поиск данных об участниках сделки */
    IsComitent = IsClientDeal( r_dl_tick );

    /* ищем контрагента */
    if ((ПолучитьСубъекта  (r_dl_tick.PartyID, r_party))!=0)
     MsgBox ("Отсутствует контрагент (PartyID=",r_dl_tick.PartyID,")");
     return FALSE;
    end;

    if (IsBuyer)         /*Мы- покупатель*/

     /*Наши данные-данные покупателя*/
      Buyer(Name)={Name_Bank};
      Buyer(Name_Boss)={Name_Boss};
      Buyer(FIO_Boss)={FIO_Boss};
      Buyer(PAdress)={Post_Addr};

     /*Данные продавца*/
      ClearRecord(RecordAdress);
      НайтиЮридическийАдресСубъекта(r_party.PartyID,RecordAdress);
      Trader(Name)=r_party.Name;
      Trader(Name_Boss)="Директор";
      Trader(FIO_Boss)="Иванов В.Ю.";
      Trader(PAdress)=RecordAdress.Adress;
      Trader(INN)=r_party.INN;
      Trader(OKPO)=r_party.OKPO;

    else       /*Мы-продавец*/

     /*Наши данные-данные продавца*/
      Trader(Name)={Name_Bank};
      Trader(Name_Boss)={Name_Boss};
      Trader(FIO_Boss)={FIO_Boss};
      Trader(PAdress)={Post_Addr};

     /*Данные покупателя*/
      ClearRecord(RecordAdress);
      НайтиЮридическийАдресСубъекта(r_party.PartyID,RecordAdress);
      buyer(Name)=r_party.Name;
      buyer(Name_Boss)="Директор";
      buyer(FIO_Boss)="Иванов В.Ю.";
      buyer(PAdress)=RecordAdress.Adress;
      buyer(INN)=r_party.INN;
      buyer(OKPO)=r_party.OKPO;
    end;

     /*Данные клиента=комитента*/
      if (IsComitent)
        if ((ПолучитьСубъекта  ( r_dl_tick.ClientID, r_party))!=0)
        MsgBox ("Отсутствует комитент (PartyID=",r_dl_tick.ClientID,")");
        return FALSE;
     end;
        ClearRecord(RecordAdress);
        НайтиЮридическийАдресСубъекта(r_party.PartyID,RecordAdress);
        Comitent(Name)=r_party.Name;
        Comitent(PAdress)=RecordAdress.Adress;
        Comitent(INN)=r_party.INN;
        Comitent(OKPO)=r_party.OKPO;
      end;

    return TRUE; /* Все нормально */
   end;/*of macro Find_Var*/

/*******************************************************************************
Print_Header - макрос печати начала отчета
*******************************************************************************/
   macro Print_Header () /* Печать начала отчета "Договор купли - продажи ЦБ" */
   var str;
   /******************************************************
    Flush_Form - макрос вывода строки в отчет .Строка режется на сегменты 
                 длиной str_size
   ********************************************************/
   macro Flush_Form(str_size)
   Array PreForm ;  /* Массив для подготовки отчета */
   var i=0,ArrSize;
    StrSplit (str,PreForm,str_size);    
    ArrSize=Asize(PreForm);
    While (i<ArrSize)
      println(PreForm(i));
      i=i+1;
    end;
   end;/*of macro Flush_Form*/

[                 ДОГОВОР №#](Order_Num);
[               купли-продажи ценных бумаг
                               #.
     ](Order_Date:m);

    if (not IsBuyer)/* Мы- продавец */
     str="   "+Trader(Name)+", именуемое в дальнейшем \"Продавец\", ";
        if (IsComitent) str=str+"действующее согласно договора-комиссии №"+
          Order_Comiss+" от "+Order_Comiss_Date+" между Продавцом и "+Comitent(Name)+
             " (далее \"Комитент\"), ";
     end;
     str=str+"в лице "+Trader(Name_Boss)+" "+Trader(FIO_Boss)+
        " с одной стороны, и компания "+Buyer(Name)+", именуемая  в дальнейшем \"Покупатель\", в лице "+
        Buyer(Name_Boss)+" "+Buyer(FIO_Boss)+" с другой стороны (вместе \"Стороны\"), заключили настоящий договор (в дальнейшем \"Договор\") о нижеследующем:";

    else  /* Мы- покупатель */
     str="   "+Buyer(Name)+", именуемое в дальнейшем \"Покупатель\", ";
        if (IsComitent) str=str+"действующее согласно договора-комиссии №"+
          Order_Comiss+" от "+Order_Comiss_Date+" между Продавцом и "+Comitent(Name)+
             " (далее \"Комитент\"), ";
     end;
        str=str+"в лице "+Buyer(Name_Boss)+" "+Buyer(FIO_Boss)+
        " с одной стороны, и компания "+Trader(Name)+", именуемая  в дальнейшем \"Продавец\", в лице "+
        Trader(Name_Boss)+" "+Trader(FIO_Boss)+" с другой стороны (вместе \"Стороны\"), заключили настоящий договор (в дальнейшем \"Договор\") о нижеследующем:";
    end;
    Flush_Form(80);
    str="   Договор составлен и трактуется в полном соответствии с Торговым"+
        " соглашением Некоммерческого партнерства \"Торговая система \"РТС\","+
        " действующим на момент подписания Договора. Подписями под Договором"+
        " Стороны согласились ко всем отношениям, вытекающим из Договора,"+
        " применять условия и принципы Торгового соглашения Партнерства. Любой"+
        " спор по Договору подлежит рассмотрению в Третейском суде НАУФОР в"+
        " соответствии с его Регламентом, действующим на момент подачи искового заявления.";
    Flush_Form(80);
[
   1. Предмет договора
  ];
    str=" Продавец обязуется передать Пакет ЦБ в собственность Покупателю, а Покупатель обязуется принять и оплатить Пакет ЦБ:";
    Flush_Form(80);

[   Ценная бумага   #](FI_Name);    
[   Эмитент         #](FI_Emitent_Name);
[   Номинал         #              валюта: #](FI_Nominal:l,FI_Curr_Name);
    str=Amount+" шт.";  
[   Количество      #](str); 
[   Цена       #              валюта: #](Price:l,Price_Curr_Name);
[   Сумма сделки    #              валюта: #](Deal_Sum:l,Price_Curr_Name);
[   Валюта платежа  #](Pay_Curr_Name);
[           ];
[   2. Порядок исполнения обязательств];
[           ];
[   2.1 Стороной, ответственной за перерегистрацию, является Продавец];
[           ];
   str="   2.2 Продавец обязан в течение "+Days_Reregestration+
       " рабочих дней, следующих за днем подписания Договора, перерегистрировать Пакет ЦБ на имя Покупателя и предоставить Покупателю документ о передаче Пакета ЦБ.";
   Flush_Form(80);
[           ];
   str="   2.3 Покупатель обязан оплатить сумму сделки в течение "+Days_Pay+
       " банковских дней со дня подписания Договора по банковским реквизитам";
   if (IsComitent) str=str+" Комитента";
   else            str=str+" Продавца";
   end;
   str=str+", указанным в настоящем Договоре, при условии выполнения "+
       "Продавцом своих обязательств по предоставлению документа о передаче"+
       " Пакета ЦБ. В случае ненадлежащего выполнения Продавцом своих обязательств"+
       " по предоставлению документа о передаче Пакета ЦБ, Покупатель обязан"+
       " оплатить сумму сделки в течение "+Days_Pay_Error+" банковских дней со"+
       " дня предоставления документа о передаче Пакета ЦБ по банковским реквизитам ";
   if (IsComitent) str=str+" Комитента";
   else            str=str+" Продавца";
   end;
   str=str+", указанным в настоящем Договоре.";
   Flush_Form(80);
[           ];
[   3. Реквизиты сторон.];
[           ];
   return TRUE;
   end; /* of macro Print_header*/

/*******************************************************************************
 Print_Rekv - макрос печати реквизитов
*******************************************************************************/
   macro Print_Rekv (
               Party_name) /* Наименование участника сделки */ 
   if (IsBuyer) /*Мы-покупатель*/
     if (Party_Name=="Покупатель")
     [   Покупатель:          #](Buyer(Name));
/*   [Юридический адрес  #](Buyer(UAdress));*/
     [Почтовый адрес:    #](Buyer(PAdress));
/*   [ИНН           #](Buyer(INN):l);
     [Св-во о регистрации     #](Buyer(Registry));
     [Телефон/Факс       #](Buyer(TelFax));*/
     println();
     return TRUE; /*Все нормально */
     end;
     if (Party_Name=="Продавец") /* Продавец - контрагент */
     [   Продавец:       #](Trader(Name));
/*   [Юридический адрес  #](Trader(UAdress));*/
     [Почтовый адрес          #](Trader(PAdress));
     [ИНН           #](Trader(INN):l);
     [Код ОКПО      #](Trader(OKPO));
/*   [Код ОКОНХ          #](Trader(OKONH));
     [Расчетный счет          #](Trader(RasAcc));
     [в банке       #](Trader(Bank));
     [кор. счет          #](Trader(CorAcc));
     [Св-во о регистрации     #](Trader(Registry));*/
     println();
     return TRUE; /*Все нормально */
     end;
   else        /* Мы - продавец */
     if (Party_Name=="Продавец")
     [   Продавец:       #](Trader(Name));
/*   [Юридический адрес  #](Trader(UAdress));*/
     [Почтовый адрес:    #](Trader(PAdress));
/*   [ИНН           #](Trader(INN):l);
     [Св-во о регистрации     #](Trader(Registry));
     [Телефон/Факс       #](Trader(TelFax));*/
     println();
     return TRUE; /*Все нормально */
     end;
     if (Party_Name=="Покупатель") /* Покупатель - контрагент */
     [   Покупатель:          #](Buyer(Name));
/*   [Юридический адрес  #](Buyer(UAdress));*/
     [Почтовый адрес          #](Buyer(PAdress));
     [ИНН           #](Buyer(INN):l);
     [Код ОКПО      #](Buyer(OKPO));
/*   [Код ОКОНХ          #](Buyer(OKONH));
     [Расчетный счет          #](Buyer(RasAcc));
     [в банке       #](Buyer(Bank));
     [кор. счет          #](Buyer(CorAcc));
     [Св-во о регистрации     #](Buyer(Registry));*/
     println();
     return TRUE; /*Все нормально */
     end;
   end;

   if (IsComitent and (Party_Name=="Комитент"))
     [   Комитент:       #](Comitent(Name));
/*   [Юридический адрес  #](Comitent(UAdress));*/
     [ИНН           #](Comitent(INN):l);
/*   [Св-во о регистрации     #](Comitent(Registry));
     [Расчетный счет          #](Comitent(RasAcc));
     [В банке       #](Comitent(Bank));
     [Кор счет      #](Comitent(CorAcc));*/
     return TRUE; /*Все нормально */
   end;

   MsgBox ("Неправильный параметр при вызове макроса Print_Rekv");
   return FALSE;
   end;/*of macro Print_Rekv*/

   macro Execute ()

      if (not Find_Var ())
         return -1;     /*Выход из макроса - ошибка */ 
      end;
      if (IsPrepare)
         if (not Print_Header)
            return -1; /*Выход из макроса - ошибка*/
         elif (not Print_Rekv("Продавец"))
            return -1; /*Выход из макроса - ошибка*/
         elif (not Print_Rekv("Покупатель"))
            return -1; /*Выход из макроса - ошибка*/
         elif (IsComitent and (not Print_Rekv("Комитент")))
            return -1; /*Выход из макроса - ошибка*/
         else
           return 0;
         end;
      end;
      return 0;
   end;
   return Execute ();
end;