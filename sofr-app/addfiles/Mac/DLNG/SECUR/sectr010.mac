/*
$Name:         sectr010.mac
$Module:       Ценные бумаги
$Description:  Операция передачи обеспечения.
*/
IMPORT InsCarryDoc, FIInter, "sp_class.mac", "oprmassexec.mac","sp_car2.mac", "sp_car.mac", "sp_secagrfd.mac";
IMPORT "secinter.mac","commonutil.mac","globals.mac","dlquery.mac","dl_car.mac";

macro GetRealRate(RateKind:integer, Fiid:integer, BaseRate:integer, RateDate:date):numeric
  var retValue = $0;
  var rec = TRecHandler("ratedef.dbt");
  var err = 0;
  err = ПолучитьКурс (rec, Fiid, BaseRate, RateKind);
  if (not err)
    err = ПолучитьЗначениеКурса (rec, RateDate);
    if (not err)
      ПолучитьСтрокуЗначенияКурса(rec,1,retValue);
    end;
  end;
  return retValue;
end;

macro GetTrnCollatAmount(dlComm:TRecHandler, operSubKind:integer)
  var cmd, DataSet;
  cmd = DL_RSDCommand(  "select nvl(sum(com.t_hidden_sum),0) as Sum from ddl_comm_dbt com "
                + " where com.t_dockind = ? "
                + "   and com.t_contractid = ? "
                + "   and com.t_fiid = ? "
                + "   and com.t_division = ? "
                + "   and com.t_commdate = ? "
                + "   and com.t_commstatus = 2 "
                + "   and com.t_opersubkind = ? "
               );
  cmd.AddParam(dlComm.rec.DocKind);
  cmd.AddParam(dlComm.rec.ContractId);
  cmd.AddParam(dlComm.rec.Fiid);
  cmd.AddParam(dlComm.rec.Division);
  cmd.AddParam(dlComm.rec.CommDate);
  cmd.AddParam(operSubKind);
  DataSet = cmd.Execute();
  if (DataSet.moveNext())
    return DataSet.Sum;
  end;
  return $0;
end;

macro GetAccountRest(accNumb,accChap,accCur,restDate)
  var cmd;
  cmd=RsdCommand(RslDefCon,"begin ? := RSB_ACCOUNT.restall(?,?,?,?,?); end;");
  cmd.AddParam("retval",RSDBP_RETVAL,V_NUMERIC);
  cmd.AddParam("p_account",RSDBP_IN,accNumb);
  cmd.AddParam("p_chapter",RSDBP_IN,accChap);
  cmd.AddParam("p_cur",RSDBP_IN,accCur);
  cmd.AddParam("p_date",RSDBP_IN,restDate);
  cmd.AddParam("p_rest_cur",RSDBP_IN,accCur);
  cmd.execute();
  return cmd.value("retval");
OnError(er)
  return numeric(0);
end;

//Сумма по договору залога в МБК
private macro GetSumFromMBK (dlcommID):money
  var query = "select secur.t_cost as cost "
            + "from ddl_grnt_dbt grnt, dnotetext_dbt note, ddl_secur_dbt secur "
            + "where note.t_objecttype = 212 "
            + "      and to_number(t_documentid) = " + dlcommID
            + "      and rsb_struct.getint(t_text) = grnt.t_dealid "
            + "      and secur.t_contractid = grnt.t_contractid "
            + "      and secur.t_contractkind = 126 ";
  var cmd = DL_RSDCommand(query);
  var DataSet = cmd.Execute();
  if (DataSet.moveNext())
    return DataSet.Cost;
  end;
  return $0;
onerror()
  return $0;
End;

MACRO ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step)
  var cmd, DataSet;
  var amount = $0;
  var err = 0, errStr = "", tr, tssKind, flg;
  var dlComm = TRecHandler("dl_comm.dbt");
  var corAcc = TRecHandler("account.dbt");
  var agrAcc = TRecHandler("account.dbt");
  SetBuff( dlComm, FDoc ); 

  if (dlComm.rec.OperSubKind == 2)
    amount = ПолучитьКоличествоЦБвОбеспечении(dlComm.rec.ContractId, dlComm.rec.Fiid, dlComm.rec.Division, dlComm.rec.CommDate);
    if (amount < dlComm.rec.hidden_sum)
      MsgBox("Недостаточно переведенных в обеспечение ценных бумаг. Было переведено "+GetTrnCollatAmount(dlComm,1)+" шт., возвращено "+GetTrnCollatAmount(dlComm,2)+" шт., остаток "+amount+" шт.");
      err=1;
    end;
  end;
  if (not err)
    var spSecAgrFD = SP_SecAgrFD(dlComm.rec.ContractId);
    GetRegistryValue( "SECUR\\ВИД КУРСА СПРАВЕДЛ. СТОИМОСТЬ", V_INTEGER, tssKind, err );
    if (err)
      MsgBox("Ошибка поиска настройки \"ВИД КУРСА СПРАВЕДЛ. СТОИМОСТЬ\"");
    end;
    spSecAgrFD.SetFI(dlComm.rec.Fiid);
    flg = spSecAgrFD.OpenAccount("ВнебалСчетКорресп", NULL, NULL, corAcc, dlComm.rec.CommDate, NATCUR);
    flg = flg and spSecAgrFD.OpenAccount("ЦБ в обеспечении", NULL, NULL, agrAcc, dlComm.rec.CommDate, dlComm.rec.Currency);
    if (flg and (not err))
      var sPayer=$0,sReceiver=$0,qnty=$0,rest=$0;
      var payerAccount,receiverAccount;
      if (dlComm.rec.OperSubKind==1)
        payerAccount=agrAcc;
        receiverAccount=corAcc;
        qnty=dlComm.rec.hidden_sum;
        /*Simanov. Сумма будет задаваться в обеспечении МБК. Оттуда сумму и берём
        var realRateSum = GetRealRate(tssKind, dlComm.rec.Fiid, NATCUR, dlComm.rec.CommDate);
        if (realRateSum <= $0)
          MsgBox("Сумма по курсу справедливой стоимости меньше или равна нулю");
        end;
        sPayer = GetRealRate(tssKind, dlComm.rec.Fiid, NATCUR, dlComm.rec.CommDate) * qnty + НКД(dlComm.rec.Fiid, qnty, dlComm.rec.CommDate);
        */
        sPayer = GetSumFromMBK(dlComm.rec.DocumentID);
        //<<Simanov.
        if( not SmartConvertSum( sReceiver, sPayer, dlComm.rec.CommDate, dlComm.rec.Currency, NATCUR, false) )
          MsgBox("Ошибка при конвертации суммы из "+ ПолучитьКодФинИн(dlComm.rec.Fiid)+" в " + ПолучитьКодФинИн(NATCUR));
          err=1;
        end;
      elif (dlComm.rec.OperSubKind==2)
        payerAccount=corAcc;
        receiverAccount=agrAcc;
        qnty=dlComm.rec.hidden_sum;
        rest=ABS(GetAccountRest(AgrAcc.rec.Account,AgrAcc.rec.Chapter,AgrAcc.rec.Code_Currency,dlComm.rec.CommDate));
        sReceiver= rest * qnty / amount;
        if( not SmartConvertSum( sPayer, sReceiver, dlComm.rec.CommDate, dlComm.rec.Currency, NATCUR, false) )
          MsgBox("Ошибка при конвертации суммы из "+ ПолучитьКодФинИн(dlComm.rec.Fiid)+" в " + ПолучитьКодФинИн(NATCUR));
          err=1;
        end;
      end;
      tr = RsbAccTransaction();
      if(tr == null)
        RunError("Ошибка при формировании проводки");
      end;
      tr.Date_Carry = dlComm.rec.CommDate;

      tr.Chapter         = payerAccount.rec.Chapter;
      tr.Department      = {OperDprt};
      tr.Oper            = {Oper};

      tr.Number_Pack     = 0;
      tr.Numb_Document   = dlComm.rec.CommCode;
      tr.Ground          = IIF(dlComm.rec.OperSubKind==1,
        "Передача ц/б в обеспечение в операции №"+dlComm.rec.CommCode,
        "Возврат ц/б из обеспечения в операции №"+dlComm.rec.CommCode);
      tr.FIIDPayer       = payerAccount.rec.Code_Currency;
      tr.FIIDReceiver    = receiverAccount.rec.Code_Currency;
      tr.SumPayer        = sPayer;
      tr.SumReceiver     = sReceiver;
      tr.AccountPayer    = payerAccount.rec.Account;
      tr.AccountReceiver = receiverAccount.rec.Account;

      tr.Shifr_Oper = DL_GetShifrOper(payerAccount.rec.Chapter, payerAccount.rec, receiverAccount.rec);
      if( not tr.Carry() )
        RunError("Ошибка при выполнении проводки");
      end;
    else
      RunError("Ошибка открытия счетов по категориям");
    end;
  end;
  return err;
OnError(errObj)
  if (IsEqClass ("TrslError", errObj))
    errStr=errObj.Message;
    if(IsEqClass ("TDbError", errObj.err))
      errStr=errStr+":|"+errObj.err.Stat+"-"+errObj.err.Oper+"-"+errObj.err.Table+"-"+errObj.err.Message;
    end;
  elif (not errStr)
    errStr="Ошибка выполнения";
  end;
  MsgBox(errStr);
  return 1;
END;