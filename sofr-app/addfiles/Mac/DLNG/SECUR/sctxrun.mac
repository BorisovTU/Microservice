/*
$Name:          sctxrun.mac
$Module:        АРНУ 
$Description:   Запуск процедуры формирования налоговых связей
*/

import FIInter, SPInter, "cb_sql.mac", "secinter.mac";

// Для отладки и оптимизации.
var IsDebug = 0; // 0 - отладка = нет, 1 - отладка = да
var IsOptim = 0; // 0 - отладка для оптимизации = нет, 1 - отладка для оптимизации = да

/* выполнить построение связей */
private macro CalcTX(TaxRegDate, dateST, Parallel)
  if(ValType(Parallel) != V_INTEGER)
    Parallel = 0;
  end;
  SQL_Execute("BEGIN rsb_sctx.TXCreateLots( " + GetSQLDate(TaxRegDate) + " , " + GetSQLDate(dateST) + ", -1, -1, " + 
               string(IsDebug) + ", " + string(IsOptim) + ", 0 " + IIF(Parallel > 1, ", " + Parallel + " ", " ") + ");END;",
              "Формирование налоговых связей за период с " + TaxRegDate + " по " + dateST);
end;

/* выполнить построение связей по выпуску и налоговой группе*/
private macro CalcTXbyFin(dateST, TaxRegDate, TaxGroup, FIID)
  var fininstr = TRecHandler( "fininstr" );

  ClearRecord(fininstr.rec);

  if( FIID != -1 ) 
    if( ПолучитьФинИн(FIID, fininstr) == 0 )
      ClearRecord(fininstr.rec);
    end;
  end;

  SQL_Execute("BEGIN rsb_sctx.TXCreateLots( " + GetSQLDate(dateST) + " , " + GetSQLDate(TaxRegDate-1) + ", " + TaxGroup + ", " + FIID + ", " +
               string(IsDebug) + ", " + string(IsOptim) + ", 1);END;",
              "Формирование налоговых связей за период с " + dateST + " по " + (TaxRegDate-1) + 
              IIF( TaxGroup > 0, " по налоговой группе \"" + GetLLVALname (2903, TaxGroup) + "\"", "") +
              " по выпуску \"" + fininstr.rec.Name + "\"");
end;

/* выполнить построение связей с даты по дату */
private macro CalcTXbyDate(dateST, dateEN, Parallel)
  if(ValType(Parallel) != V_INTEGER)
    Parallel = 0;
  end;
  SQL_Execute("BEGIN rsb_sctx.TXCreateLots( " + GetSQLDate(dateST) + " , " + GetSQLDate(dateEN) + ", -1, -1, " + 
               string(IsDebug) + ", " + string(IsOptim) + ", 1 " + IIF(Parallel > 1, ", " + Parallel + " ", " ") + ");END;",
              "Формирование налоговых связей за период с " + dateST + " по " + dateEN);
end;

/* закрыть налоговый период */
private macro CloseTX(dateST)
  SQL_Execute("BEGIN rsb_sctx.TXClosePeriod( " + GetSQLDate(dateST) + " );END;",
              "Закрытие налогового периода по " + dateST, true);
end;


macro GetMesType(Type)
  if( Type == /*TXMES_ERROR*/ 10 )
    return "Ошибка";
  elif( Type == /*TXMES_WARNING*/ 20 )
    return "Предупреждение";
  elif( Type == /*TXMES_DEBUG*/ 30 )
    return "Отладка";
  elif( Type == /*TXMES_OPTIM*/ 60 )
    return "Отладка";
  end;

  return "Сообщение";
end;

macro RunSCTX(mode:integer,
              dateST:date,
              dateEN:date,
              TaxGroup:integer,
              FIID:integer,
              Parallel)

  var TaxRegDate:date,
      err;
  var sql, rsd;

  if(ValType(Parallel) != V_INTEGER)
    Parallel = 0;
  end;

  /* GetRegistryValue("SECUR\\DATE_BUILD_TAXREG", V_STRING, TaxRegDate_str, err) читает кэшированные данные, что неприемлемо */
  sql = " SELECT utl_raw.cast_to_varchar2(t_fmtblobdata_xxxx) TaxRegDate "
      + "   FROM dregval_dbt "
      + "  WHERE t_KeyID = rsb_tools.find_regkey('SECUR\\DATE_BUILD_TAXREG') ";
  rsd = TRsbDataSet(sql);
  if(rsd.MoveNext())
    TaxRegDate = date(rsd.TaxRegDate) + 1;
  else
    MsgBox( "Ошибка при определении настройки \"SECUR\\DATE_BUILD_TAXREG\"" );
    TaxRegDate = date(0,0,0);
  end;

  if  (mode == TXRUN_MODE_ONDATE)
    CalcTX(TaxRegDate, dateST, Parallel);
  elif(mode == TXRUN_MODE_BYFIID)
    CalcTXbyFin(dateST, TaxRegDate, TaxGroup, FIID);
  elif(mode == TXRUN_MODE_BYDATE)
    CalcTXbyDate(dateST, dateEN, Parallel);
  elif(mode == TXRUN_MODE_CLOSE)
    CloseTX(dateST);
  else
    msgbox("Выбран неизвестный режим");
    return false;
  end;

  /* покажем ошибки формирования данных, если были*/
  sql = " SELECT * FROM dsctxmes_dbt WHERE t_ID > 0 order by t_id ";
  rsd = TRsbDataSet(sql);
  println("Протокол работы");
  while(rsd.MoveNext())
    println(SQL_ConvTypeDate(rsd.t_MesDate), ", " + SQL_ConvTypeTime(rsd.t_MesDate), ", ", GetMesType(rsd.t_Type), ": ", rsd.t_Message);
  end;

  return true;

OnError
  return false;
end;

macro Begin_RunSCTX( mode:integer,
                     dateST:date,
                     dateEN:date,
                     TaxGroup:integer,
                     FIID:integer,
                     ErrMess:@string)

   VAR rsd = TRsbDataSet( "select t_Message ProcName from DSCTXMES_DBT where T_ID = -2" );

   if( rsd.MoveNext() )
      VAR Mes = "Выполнение запрещено т.к. уже запущена процедура \"" + rsd.ProcName +"\".\nВ случае, если предыдущий запуск процедуры был завершен некорректно, перед следующим запуском необходимо удалить записи в таблице DSCTXMES_DBT.";
      if(ErrMess != null)
         ErrMess = Mes;
      end;
      MsgBoxEX( Mes, 0, 0, "Ошибка запуска процедуры", "Ошибка запуска процедуры"  );
      println( Mes );
      return false;
   end;

   return true;
end;

macro End_RunSCTX( mode:integer,
                   dateST:date,
                   dateEN:date,
                   TaxGroup:integer,
                   FIID:integer)

   SQL_Execute( "BEGIN rsb_sctx.EndCalculate; END;" );
   return true;
end;

