//SecurOTCUpdDealSchedulePanel.mac
import "RsbPanelConstants.mac", RsbFormsInter, "KeyCodes.mac", Календарь, PTInter, globals, "CbForms_h.mac";

/*
╔═════════════════Обновление графика сделок════════╗
║           Параметры отбора сделок                ║
║ Вид сделки:                 ___________________  ║
║ Дата заключения:            [00.00.0000]         ║
║ Контрагент:                 ___________________  ║
║                                                  ║
║          Корректировка графика                   ║
║ Планируемая дата поставки:  [00.00.0000]         ║
║ Планируемая дата оплаты:    [00.00.0000]         ║
║                                                  ║
╚══════════════════════════════════════════════════╝
*/

class (TRsbPanel) SecurOTCUpdDealSchedulePanel()
  private var rows:integer = 0;

  //статичные координаты панели и полей
  private var panelPositionX:integer = 45;
  private var panelPositionY:integer = 10;
  private var editableFldsPosition   = 15;
  private var panelWidth:integer     = 43;

  //статичные размеры полей
  private var stringFldLength = 25;
  private var dateFldLength = 10;

  //выбранные фильтры
  private var choosedDealType:integer = 0;
  private var choosedContractorID:integer = 0;

  private macro NewPartName(labelText:string)
    addLabel(TRsbLabel(8, rows = rows + 1, labelText));
  end;

  private macro NewLabel(labelText:string)
    addLabel(TRsbLabel(2, rows = rows + 1, labelText));
  end;

  private macro NewFld (labelText:string, name:string, value, editable:bool, handlerName:string, dataType:integer, length:integer, textLength:integer)
    if (labelText != null) 
      NewLabel(labelText);
    end;
    var fld = TRsbEditField(dataType);
    fld.setPosition(editableFldsPosition, rows);
    fld.setSize(length, 1);
    fld.textLength = textLength;
    fld.Editable   = editable;
    fld.Name       = name;
    fld.value      = value;
    if (handlerName != null)
      fld.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, handlerName));
    end;
    addControl(fld);
    return fld;
  end;

  private macro NewFldString(labelText:string, name:string, value:string, editable:bool, handlerName:string)
    return NewFld(labelText, name, value, editable, handlerName, RsbPanelFiledTypes.FT_STRING, stringFldLength, stringFldLength + 10);
  end;

  private macro NewFldDate(labelText:string, name:string, value:date, editable:bool)
    return NewFld(labelText, name, value, editable, "DateKeyPressed", RsbPanelFiledTypes.FT_DATE, dateFldLength, dateFldLength);
  end;

  private macro NewButton(pos_x:integer, pos_y:integer, name:string, enable:bool, handlerName:string)
    var button = TRsbPushButton(name);
    button.setPosition(pos_x, pos_y);
    button.setSize(7,1);
    button.onClicked(R2M(this, handlerName));
    button.enabled = enable;
    addControl(button);
  end;

  InitTRsbPanel();

  SetCaption("Обновление графика сделок");
  SetStatus("~Esc~ Выход ~F2~ Применить");
  SetPosition(panelPositionX, panelPositionY);

  NewPartName("Параметры отбора сделок");
  var dealTypeCntrl = NewFldString("Вид сделки:", "dealTypeFld", "", true, "DealTypeKeyPressed");
  var dealDateCntrl = NewFldDate("Дата заключения:", "dealDate", {curdate}, true);
  var contractorCntrl = NewFldString("Контрагент:", "contractorFld", "", true, "ContractorKeyPressed");

  editableFldsPosition = 25;
  rows = rows + 1;
  NewPartName("Корректировка графика");
  var supplyDateCntrl = NewFldDate("Планируемая дата поставки:", "supplyDate", date(0, 0, 0), true);
  var payDateCntrl = NewFldDate("Планируемая дата оплаты:", "payDate", date(0, 0, 0), true);

  rows = rows + 2;
  NewButton(4, rows, "Старт", true, "StartBtn");
  NewButton(panelWidth - 11, rows, "Отмена", true, "CancelBtn");

  rows = rows + 2;
  SetSize(panelWidth, rows);

  addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "KeyHandler"));

  macro StartBtn(RsbEvent)
    if (RsbEvent.id == RSB_EV_BUTTON_CLICKED)
      close(1);
    end;
    return true;
  end;

  macro CancelBtn(RsbEvent)
    if (RsbEvent.id == RSB_EV_BUTTON_CLICKED)
      close(0);
    end;
    return true;
  end;

  macro DateKeyPressed(RsbEvent:Object)
    if (RsbEvent.KeyCode == KeyCodes.F3)
      RsbEvent.Source.value = GetDateByCalendar(RsbEvent.Source.value);
    end;
  end;

  macro DealTypeKeyPressed(RsbEvent:Object)
    var choosed;
    if (RsbEvent.KeyCode == KeyCodes.F3)
      var query = "select o.t_kind_operation, o.t_name "
                + "  from doprkoper_dbt o "
                + " where o.t_dockind = 101 "
                + "   and o.t_kind_operation > 0 "
                + "   and o.t_notinuse != 'X' "
                + " order by o.t_kind_operation ";
      choosed = Dl_Scroll(query, "Виды сделок", panelPositionX + 10, panelPositionY,
                         "t_kind_operation", "Вид сделки",
                         "t_name", "Название вида сделки");
      if (choosed != null)
        dealTypeCntrl.value = choosed.value("t_name");
        choosedDealType = choosed.value("t_kind_operation")
      end;
    end;
  end;

  macro ContractorKeyPressed(RsbEvent:Object)
    if (RsbEvent.KeyCode == KeyCodes.F3)
      var party = TRecHandler("party.dbt");
      var codeKind = 1;
      if( ListPT( Party, null, null, PTLIST_CONTR, 0, codeKind) )
        RsbEvent.Source.value = ПолучитьКодСубъекта(party.rec.PartyID, codeKind);
        choosedContractorID = party.rec.PartyID;
      end;
    end;
  end;

  //Основной обработчик нажатий на панели
  macro KeyHandler(RsbEvent:Object)
    if (RsbEvent.KeyCode == KeyCodes.F2)
      Close(1);
    end;
  end;

  macro GetDealType()
    return choosedDealType;
  end;

  macro GetDealDate()
    return dealDateCntrl.value;
  end;

  macro GetContractor()
    return choosedContractorID;
  end;

  macro GetSupplyDate()
    return supplyDateCntrl.value;
  end;

  macro GetPayDate()
    return payDateCntrl.value;
  end;
end;