/* Операция списания/зачисления ц/б
   Шаг ожидания статуса обработки */

import InsCarryDoc, "sp_categ.mac", "sp_car.mac";

/**
  @brief it_log - сохранение в лог текстового сообщения
  @param[in] text - текстовое сообщение
*/
private macro it_log(text)
  var sql = "begin it_log.log(:1); end;";
  var cmd = RSDCommand(sql);
  cmd.AddParam("",RSDBP_IN,text);
  cmd.execute;
end;


private macro msgDOPRTEMP_TMPcnt(str1)

    var Query = "";
    Query = " SELECT count(*) "
          + " FROM ddl_tick_dbt tick ,doprtemp_view oprtemp "
          + " WHERE "
              + " tick.t_DealID = TO_NUMBER(oprtemp.t_DocumentID) " // корректно т.к. обрабатываются только сделки
          + " AND tick.t_BOfficeKind = oprtemp.t_DocKind ";

  var cmd1 = RSDCOMMAND(Query);
  var rs1 = RSDRecordSet(cmd1);
  if (rs1.movenext)
    it_log(str1+int(rs1.value(0)) );
  end;
end;

macro _ExecuteStep( Doc, ticket, DocKind, OperationID )
  record deal(dl_tick);
  var    FD, dat;

  SetBuff( deal, ticket );
  FD = SPFirstDoc( deal );
  GetOprDate( 0, dat );

  //найти в Картотеке запись об созданной операции
  var sql = "select * " + 
            " from pko_writeoff pko where dealid = :1";
  var cmd = RSDCommand(sql);
  cmd.AddParam(":1",RSDBP_IN,int(FD.tick.rec.DealID));
  cmd.Execute;
  var rs = RSDRecordSet(cmd);

  if (not rs.movenext)
    MsgBox("Операция не найдена в Картотеке pko_writeoff");
    return 1; //если не нашли в Картотеке операцию, то выполнить операцию нельзя
  else
    if (rs.value("step1_waitstatus")!=1)
      MsgBoxEx("Если в Картотеке поле step1_waitstatus не равнo 1, то выполнение данного шага невозможно");
      return 1;  //если в Картотеке поле step1_waitstatus не равен 1, то выполнение данного шага невозможно
    end;
  end;

  var OprStat = 1; //следующий блок Списания или Отказа будет запущен

  var choiceBlok;
  if(rs.value("step2_reject")==1)
    choiceBlok=484121; // выполнить дистрибутивный шаг Отказ 
  elif(rs.value("step3_writeoff")==1) // пойти по ветке Списания
    choiceBlok=484111; // выполнить шаги Списания и Завершения
  else
    MsgBoxEx("В картотеке pko_writeoff не установлены значения step2_reject и step3_writeoff ");
    return 1;
  end;

  if( not InsertOprStatus( choiceBlok, OprStat) )   
    msgbox( "Ошибка при установке статуса операции" );
    return 1;
  end;

  it_log("Шаг операции выполнен wrtavr23PKO.mac");
  return 0;
end;

macro ExecuteStep( Doc, ticket, DocKind, OperationID )
  it_log("ExecuteStep wrtavr23PKO.mac");
  return _ExecuteStep( Doc, ticket, DocKind, OperationID );
end;

/* Массовое исполнение */
MACRO MassExecuteStep(Doc, ticket, DocKind, OperationID)
  it_log("MassExecuteStep wrtavr23PKO.mac");
  return _ExecuteStep( Doc, ticket, DocKind, OperationID );
END;


MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    
   record Deal_Tick (dl_tick);
   SetBuff( deal_tick, FDoc );

   return 0;

end;