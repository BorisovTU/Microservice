/*******************************************************************************
 DESCRIPTION  :   Отчет "Расходы банка по операциям клиентов" (НУ) - форма ввода данных
 PROGRAMMED BY:   Никольская В.В.
 CREATION DATE:   25.01.08
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_COMMREG_PERIOD         = 0, //Период
      PNFLD_COMMREG_YEAR           = 1, 
      PNFLD_COMMREG_OPERNUM        = 2, //номер операциониста
      PNFLD_COMMREG_COMPRINTMETHOD = 3; 

CONST H_PNFLD_PERIOD       = 20,
      H_PNFLD_OPERNUM    = 21; //Наименовани комиссии

VAR ComFeeType;

PRIVATE MACRO ListOper( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER ):BOOL
  VAR Choice = DL_Scroll("select  prs.t_Oper Oper, prs.t_Name NameOper, dep.t_Name NameDep from dperson_dbt prs, ddp_dep_dbt dep "  +
                         " where prs.t_CodeDepart = dep.t_Code " +
                         " order by prs.t_oper",
                         "Список операционистов", X, Y,
                         "Oper","Номер","NameOper","ФИО","NameDep","Узел ТС"
                        );
  if( Choice != NULL )
     FldRealValue = Choice.Value("Oper"); 
     FldShowValue = FldRealValue;
  end;

  return (Choice != NULL);
END;                              


MACRO FldProc_OperNumber( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
 VAR SubKind;

 if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = {oper};
     end;
     FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
      ListOper( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY());
  end;

  return CM_DEFAULT;
END;

MACRO FldProc_Period( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  VAR BeginDate, EndDate, Month, Year, BegD, EndD;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        DateSplit( {curdate}, null, Month, null );
        FldRealValue = ((Month - 1)/3 + 1)*3;
     end;
     FldShowValue = DL_GetNameAlg( ALG_CB_PERIOD_IN_QUART, FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( ALG_CB_PERIOD_IN_QUART, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_COMMReg23_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = ((CurMonth - 1)/3 + 1)*3; 
     if( PrevQuartal == 0 )
        PrevQuartal = 12;
        Year        = Year - 1;
     end;

     AddFieldProc( 0, PNFLD_COMMREG_PERIOD,      H_PNFLD_PERIOD,      @FldProc_Period, PrevQuartal, 29, 11 ); 
     AddFieldProc( 0, PNFLD_COMMREG_YEAR,        DL_PNFLD_YEAR,       @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_COMMREG_OPERNUM,     H_PNFLD_OPERNUM,     @FldProc_OperNumber, {oper});
    /* AddFieldProc( 0, PNFLD_COMMREG_COMPRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 26, 15 ); */
     AddFieldProc( 0, PNFLD_COMMREG_COMPRINTMETHOD,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox, "X" );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_res.lbr", "OUTLCLOP" ); 
END;