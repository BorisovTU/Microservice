/*
$Name:        nptxskipcat.mac
$Module:      Ценные бумаги
$Description: 
*/
import "nptxskipcat_form.mac", "dlutils.mac", "poisimplerepo.mac", "xls_parser.mac", "partysql.mac";

macro GetIntGeneralMainObjAttr(p_objectType,p_object,p_groupid, p_date)
   var v_object = L_Z(p_object,10);
   var v_result:integer = 0;
   var query = "select RSB_SECUR.GetGeneralMainObjAttr( ?, ?, ?, ? ) t from dual;";
   var cmd = RSDCommand(query); 
   cmd.AddParam("", RSDBP_IN, p_objectType);
   cmd.AddParam("", RSDBP_IN, v_object);
   cmd.AddParam("", RSDBP_IN, p_groupid);
   cmd.AddParam("", RSDBP_IN, p_date);
   var rs = RSDRecordSet(cmd);
   if (rs.movenext)
     v_result = SQL_ConvTypeInteger(rs.value("t"));
   end;
   return v_result;
end;

macro SkipCatOperationRun(OperDate, LoadFile, ClientType, ChangeCatTo, ProtDir)
  var day, mon, year;
  var hour, min, sec;
  DateSplit(date(),day,mon,year);
  TimeSplit(time(),hour,min,sec);

  var simpleRepo = PoiSimpleRepo("Протокол ошибок при изменении категории \"Исключить из автоматического удержания НДФЛ по итогам года\"",
                                 makeArray(
                                   DL_KeyPair("ID СОФР", 15),
                                   DL_KeyPair("ID ЦФТ", 15),
                                   DL_KeyPair("Ошибка", 50)
                                 ));


  var objExcel = CExcelPoi();
  var fileMov = TempFileToServerMover();
  fileMov.CreateTempFromAbsolutPath(LoadFile);
  if (not objExcel.open(fileMov.GetTempFilePath()))
    RunError("Не удалось открыть excel файл " + LoadFile);
  end;
  var lastRowNumb = objExcel.GetLastRowNum()+1;
  var curRow = 0;
  var hasData = false;
  InitProgress(lastRowNumb, "Чтение файла со списком клиентов", "Чтение файла со списком клиентов");
  while ((curRow=curRow+1) <= lastRowNumb)
    var cellValue = objExcel.CellValue("A"+curRow);
    if (ValType(cellValue) == V_UNDEF)
      continue;
    end;
    var curRowData = Trim(string(cellValue));
    var dotIdx = Index(curRowData, ".");
    if (dotIdx > 0)
      curRowData = SubStr(curRowData, 1, dotIdx-1);
    end;
    var sofrId = "";
    var cftId = "";
    var errorText = "";

    if ((ClientType == 1) and (not StrIsNumber(curRowData)))
      sofrId = curRowData;
      errorText = "Некорректный ID СОФР";
    else
      if (ClientType == 1)
        sofrId = string(curRowData);
        if (not CheckSofrIdExists(int(curRowData)))
          errorText = "Такого ID СОФР не существует";
        else
          cftId = GetCftIDFromSofrID(int(curRowData));
        end;
      elif (ClientType == 2)
        cftId = curRowData;
        var intSofrId = GetSofrIDFromCftId(curRowData);
        if (intSofrId == 0)
          errorText = "Такого ID ЦФТ не существует";
        else
          sofrId = string(intSofrId);
        end;
      end;
      hasData = true;
    end;

    if (errorText == "")
      var party = TBFile("party.dbt");
      party.rec.PartyID = int(sofrId);
      party.GetEQ();
      var attr = GetIntGeneralMainObjAttr(OBJTYPE_PARTY, int(sofrId), 130, OperDate);
      var isAttrSet = attr == 1;
      var needSet = ChangeCatTo == 0;
      if (isAttrSet != needSet)
        if (not needSet)
          if (not DisconnectObjAttr (OBJTYPE_PARTY,  UniID(party, OBJTYPE_PARTY), 130, attr, OperDate))
            errorText = "Ошибка снятия признака";
          end;
        else
          var stat;
          ConnectObjAttr (stat, OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 130, 1, NULL, NULL, OperDate); 
          if (stat)
            errorText = "Ошибка установки признака";
          end;
        end;
      end;
    end;

    if (errorText != "")
      simpleRepo.PrintLine(sofrId, cftId, errorText);
    end;
    UseProgress(curRow);
  end;

  MsgBox("Выполнение завершено.");

  if (simpleRepo.IsHasData())
    var protFileName = "Протокол_изменение_категории_исключить_из_автоматического_удержания_НДФЛ_"+string(day:o:2)+string(mon:o:2)+string(year)+"_"+string(hour:o:2)+string(min:o:2) + ".xlsx";
    protFileName = PathCombine(ProtDir,protFileName);
    simpleRepo.SaveAs(protFileName);
    simpleRepo.View();
  end;
end;

MACRO SkipCatOperationMenuRun()
  var Form = Sp_NPTXSKIPCAT_Panel();
  if (Form.Run())
    SkipCatOperationRun(Form.m_Date, Form.m_LoadFile, Form.m_ClientType, Form.m_ChangeCatTo, Form.m_ProtDir )
  end;
OnError(er)
  MsgBox("Ошибка! " + GetFullErrorMessage(er));
END;

