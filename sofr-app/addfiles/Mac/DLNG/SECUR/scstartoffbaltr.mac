/*
$Name:        scstartoffbaltr.mac
$Module:      Ценные бумаги
$Description: Запуск выполнения операции переноса по счетам срочности на внебалансе
*/

import DealsInter, SPInter, InsCarryDoc, FIInter, OprInter, "dlcnst.inc", "sp_class.mac", "spserv.mac", "sp_grfun.mac", "scservop.mac", "sp_car.mac";
IMPORT "role_model.mac";//ролевая модель

//класс с данными для строки протокола
class CSC_OffBalTrRepParm(_DealCode:string,       
                          _DealKindName:string,   
                          _PartyCode:string,      
                          _PayerAccount:string,   
                          _ReceiverAccount:string,
                          _Amount:money,         
                          _FICode:string)

  var DealCode        = _DealCode;        //Код сделки
  var DealKindName    = _DealKindName;    //Вид сделки
  var PartyCode       = _PartyCode;       //Субъект
  var PayerAccount    = _PayerAccount;    //Счет Дт
  var ReceiverAccount = _ReceiverAccount; //Счет Кт
  var Amount          = _Amount;          //Сумма
  var FICode          = _FICode;          //Валюта
end;

//класс с данными для списка ошибок в протоколе
private class CSC_OffBalTrRepErr(_DealCode:string, _ErrCode:integer, _ErrStr:string)
 var DealCode = _DealCode;
 var ErrCode  = _ErrCode;
 var ErrStr   = _ErrStr;
end;

/*лог ошибок*/
PRIVATE CLASS (DL_LOG_FROM_XML) SC_ERROR_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()
     var ErrorData:CSC_OffBalTrRepErr;

     if( m_ReportVersion == 1 )

        if(DL_ReadTagAttribut(m_child_ReportNumber, "DEALCODE", V_STRING, ErrorData.DealCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRCODE", V_STRING, ErrorData.ErrCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRSTR", V_STRING, ErrorData.ErrStr) == false)
        end;
        
        return ErrorData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

/*лог ошибок СО*/
PRIVATE CLASS (DL_LOG_DATA_XML) SC_ERROR_LOG_DATA_XML(pDocKind:integer, pDocumentID:integer, pDataParms:TArray, pType:integer, pVersion:integer)

  PRIVATE MACRO СоздатьУзелОшибок(i)
     var УзелОшибок = null;

     if( m_Version == 1 )
        УзелОшибок = CreateElement("Property",
                                   "DealCode",  m_DataParms[i].DealCode, 
                                   "ErrCode",   m_DataParms[i].ErrCode, 
                                   "ErrStr",    m_DataParms[i].ErrStr
                                  );
     else
        УзелОшибок = CreateElement("Property");
     end;
     
     return УзелОшибок;
  END;


  initDL_LOG_DATA_XML(pDocKind, pDocumentID, pDataParms, pType, pVersion);

END;

CLASS (DL_LOG_FROM_XML) OFFBALTR_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()

     var CarryData:CSC_OffBalTrRepParm;

     if( m_ReportVersion == 1 )
     
        if( DL_ReadTagAttribut(m_child_ReportNumber, "DEALCODE", V_STRING, CarryData.DealCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "DEALKINDNAME", V_STRING, CarryData.DealKindName) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "PARTYCODE", V_STRING, CarryData.PartyCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "PAYERACCOUNT", V_STRING, CarryData.PayerAccount) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "RECEIVERACCOUNT", V_STRING, CarryData.ReceiverAccount) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "AMOUNT", V_MONEY, CarryData.Amount) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "FICODE", V_STRING, CarryData.FICode) == false)
        end;
       
        return CarryData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

PRIVATE MACRO GetLogDataXML( DocKind:integer, DocumentID:integer, Type:integer, OnlyLastReport:bool )
   var xml_obj = null;

   if( Type == LOG_TYPE_DATA )
      xml_obj = OFFBALTR_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   elif(Type == LOG_TYPE_ERROR )
      xml_obj = SC_ERROR_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   end;

   return xml_obj;
END;

/*печать отчета по данным xml*/
MACRO PrintOffBalTrByXML(comm)
  var ReportLineData_XML:variant;
  var IsExistsCarry = false, IsExistsErr = false;
  var HeadPrinted = false;
  var CarryData:CSC_OffBalTrRepParm;
  var ErrData:CSC_OffBalTrRepErr;
  var Executer = DepoExecuter( {oper} );
  record DlComm("dl_comm");
  VAR ReportFileName, errtext, errcode;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/
  var stat = 0;

  SetBuff(DlComm, comm);
  
  ReportFileName = GetTxtFileName( "offbaltr_" + DlComm.CommCode );
  if( not Open( ReportOutFile, ReportFileName ) )
     errcode = status(errtext);
     RunError( "Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext );
  end;
  SetOutput( ReportFileName );

  println("");
  println("                        ПЕРЕНОС ПО СЧЕТАМ СРОЧНОСТИ НА ВНЕБАЛАНСОВОМ УЧЕТЕ");

  ////////////////////////////////////////////////////////////////////////////////////////////////////////
  //таблица проводок
  ReportLineData_XML = GetLogDataXML(DlComm.DocKind, DlComm.DocumentID, LOG_TYPE_DATA, true);
  ReportLineData_XML.SetReportNumber(0);
 
  while( ReportLineData_XML.Next )

     if(not HeadPrinted)
        println("┌──────────┬────────────────────────┬────────┬────────────────────┬────────────────────┬──────────┬────┐");
        println("│Код сделки│Вид сделки              │Субъект │Счет Дт             │ Счет Кт            │ Сумма    │Вал │");
        println("├──────────┼────────────────────────┼────────┼────────────────────┼────────────────────┼──────────┼────┤");

        HeadPrinted = true;
     end;

     CarryData = ReportLineData_XML.GetReportLineData();
                [│##########│########################│########│####################│####################│##########│####│]
                 (CarryData.DealCode, CarryData.DealKindName, CarryData.PartyCode, CarryData.PayerAccount, CarryData.ReceiverAccount, CarryData.Amount, CarryData.FICode);

     IsExistsCarry = true;
  end;

  if(HeadPrinted)
        println("└──────────┴────────────────────────┴────────┴────────────────────┴────────────────────┴──────────┴────┘");
        println("");
  end;
  ////////////////////////////////////////////////////////////////////////////////////////////////////////

  ////////////////////////////////////////////////////////////////////////////////////////////////////////
  //таблица ошибок
  ReportLineData_XML = GetLogDataXML(DlComm.DocKind, DlComm.DocumentID, LOG_TYPE_ERROR, true);
  ReportLineData_XML.SetReportNumber(0);
  
  stat = ReportLineData_XML.Next();
  if((stat) or (IsExistsCarry == false))

    println("");
    println("┌──────────┬──────────┬──────────────────────────────────────────────────────────────┐");
    println("│Код сделки│№ ошибки  │Сообщение                                                     │");
    println("├──────────┼──────────┼──────────────────────────────────────────────────────────────┤");

    HeadPrinted = true;

    if(not stat)
            [│##########│##########│##############################################################│]
            ("", "", "Нет подходящих сделок");
    else
      while(stat)
        ErrData = ReportLineData_XML.GetReportLineData();
            [│##########│##########│##############################################################│]
            (ErrData.DealCode, ErrData.ErrCode, ErrData.ErrStr:w);


        IsExistsErr = true;
        stat = ReportLineData_XML.Next();
      end;
    end;

    println("└──────────┴──────────┴──────────────────────────────────────────────────────────────┘");
    println("");
  else
    println("Операция завершена успешно");
    println("");
  end;

  /////////////////////////////////////////////////////////////////////////////////////////////////////////
  println("Составил ( " + Executer.Post + " ):\n" + 
    string( Executer.Name ) + "                 " + String( {curdate} ) + "\n" +
    DL_StrCut( "", IIF(strlen(Executer.Name) <= 10, 10, strlen(Executer.Name) ), "─" ) + " ───────────────   дата" + "\n" +
    DL_StrCut("", IIF(strlen(Executer.Name) <= 10, 0, (strlen(Executer.Name) - 10) / 2 ), " ") + "   ФИО    " + DL_StrCut("", IIF(strlen(Executer.Name) <= 10, 0, (strlen(Executer.Name) - 10) / 2), " " ) + "    подпись" );

  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );

  return true;
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//ВЫПОЛНЕНИЕ ОПЕРАЦИИ
private var FD = null;
private var RepErrArr = TArray();

private macro SaveErrForReport_XML( DocumentID:integer, DocKind:integer )

  var RepXML = "";

  if( RepErrArr.Size > 0 )
     RepXML = SC_ERROR_LOG_DATA_XML(DocKind,DocumentID,RepErrArr,LOG_TYPE_ERROR).GetDataXML();
  end;

  if( RepXML != "" )
     var Header = DL_LOG_HEADER_XML(DocKind).GetHeaderXML();

     DL_InsertHeaderLog(DocumentID, DocKind, {Oper}, Header);
     DL_InsertLogXMLData( DocumentID, DocKind, LOG_TYPE_ERROR, {Oper}, RepXML);
  end;

end;

macro OffBalTr_msgbox(StrErr)
  RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, 1, StrErr);
end;

private macro GetErrorMes(stat:integer):string
   var mes = GetErrMsg();
   if (strlen(mes) == 0)
      InitError();
      MemoryError(stat);
      mes = GetErrMsg();
   end;
   return mes;
end;
//начало выполнения
macro StartOffBalTransf(_DocumentID)
 var cmd, DataSet, query;
 var dl_comm = TBFile("dl_comm.dbt");
 var N = 0;
 var err = 0, stat = 0;
 var Dпс = date(0,0,0), Dпл = date(0,0,0), Dпс_реал = date(0,0,0), Dпл_реал = date(0,0,0);
 var Tr=false, Ob=false, K1=0, K2=0;
 var AccTrOld = TRecHandler("account.dbt"), AccObOld = TRecHandler("account.dbt");
 var AccTrNew = TRecHandler("account.dbt"), AccObNew = TRecHandler("account.dbt");
 var corr = TRecHandler("account.dbt");
 var trn, sum = $0, sum_2 = $0;
 var GrDealID = 0;
 var cnt = 0, i = 0;
 var RepDataArr = TArray() ;
 var CarryTr, CarryOb, CarryTr_2, CarryOb_2;
 var ErrorMessage = "";
 Var ground = "";
 var retvalGetMO;

 record PeriodTr(mcperiod);
 record PeriodOb(mcperiod);
 
 RepDataArr.size = 0;
 RepErrArr.size = 0;
 dl_comm.rec.DocumentID = _DocumentID;
 if(dl_comm.GetEQ())

   cmd = DL_RSDCommand();

   query =   " select tk.t_DealID, tk.t_BOfficeKind, tk.t_DealCode, grdeal.t_ID as GrDealID, koper.t_Name as OprName "
           + "   from ddl_tick_dbt tk, ddlgrdeal_dbt grdeal, doprkoper_dbt koper "
           + "  where tk.t_Department = ? "
           + "    and grdeal.t_DocKind = tk.t_BOfficeKind "
           + "    and grdeal.t_DocID = tk.t_DealID "
           + "    and grdeal.t_TemplNum = " + DLGR_TEMPL_TRANSFER
           + "    and Exists(select 1 from ddlgracc_dbt gracc "
           + "                where gracc.t_GrDealID = grdeal.t_ID "
           + "                  and gracc.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING
           + "                  and gracc.t_State = " + DLGRACC_STATE_PLAN
           + "              )"
           + "    and koper.t_Kind_Operation = tk.t_DealType ";

   cmd.addParam(dl_comm.rec.Division);

   if(dl_comm.rec.Flag1 == SET_CHAR)
     query = query + " and grdeal.t_PlanDate <= ? ";
   else
     query = query + " and grdeal.t_PlanDate = ? ";
   end;
   cmd.addParam(dl_comm.rec.CommDate);

   if(dl_comm.rec.OperSubKind > 0)
     query = query + " and tk.t_DealType = ? ";
     cmd.addParam(dl_comm.rec.OperSubKind);
   end;

   if(dl_comm.rec.Deal1 > 0)
     query = query + " and tk.t_DealID = ? ";
     cmd.addParam(dl_comm.rec.Deal1);
   end;

   cnt = cmd.GetCount(query);
   if(cnt > 0)
     InitProgress( cnt, "Перенос по счетам срочности на внебалансе", "Перенос по счетам срочности на внебалансе" );
     
     ReplaceMacro( "msgbox", "OffBalTr_msgbox" );

     DataSet = cmd.execute(query);
     
     while(DataSet.moveNext())

       stat = 0;
       ErrorMessage = "";

       FD = SPFirstDoc( LEG_KIND_DL_TICK, DataSet.DealID );

       N = GetNRecsTransfer(FD.tick.rec.BOfficekind, FD.tick.rec.DealID);

       if(N == 0)
         stat = 1;
         RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Не найдена строка переноса по срокам в графике обработки сделки");
       elif(N > 1)
         stat = 1; 
         RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Найдено более одной строки переноса по срокам в графике обработки сделки");
       end;

       if(not stat)
         GrDealID = DataSet.GrDealID;
         Dпс = GetPlanTransferDate(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID);
         Dпс_реал = GetRealPlanTransferDate(FD);
         if(Dпс_реал == date(0,0,0))
           Dпс_реал = Dпс;
         end;
         while((not stat) and (Dпс != date(0,0,0)) and (Dпс <= dl_comm.rec.CommDate))

           RslDefCon.BeginTrans();

           CarryTr = null;
           CarryOb = null;
           stat = GetNeedNextTransfer(FD, Dпс_реал, @Tr, @Ob, @K1, @K2, AccTrOld, AccObOld);
           if(stat)
             RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка поиска даты следующего переноса по срокам");
           else
             if(Ob)
               Sum = ABS(GetRestAccount(AccObOld.rec, Dпс));

               if( not FD.ActualCheckPeriod( "-Форвард, дрейф", null, null, FIROLE_FICOM, AccObNew, Dпс_реал, null, null, null, Dпс ) )
                 stat = 1;//Ошибка при актуализации счета
                 RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при актуализации счета по категории \"-Форвард, дрейф\" на дату "+string(Dпс:f)+". "+GetErrMsg());
               end;

               if( КОРРЕСП_ГЛАВА_Г() == true )
                  if(not stat)
                    trn = RsbAccTransaction();

                    If(FD.tick.rec.ispfi == "X")
                       ground = "ПФИ (). Перенос обязательств по сделке N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);
                    else
                       ground = "Перенос обязательств по сделке N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);
                    end;


/*DAN.Ролевая модель. Определяем конечного операциониста для проводки */
      retvalGetMO = GetMainOperInGroup(FD.Kind);
      if (retvalGetMO > 0)
	 Trn.Oper = retvalGetMO;
      else
         Trn.Oper = {Oper};
      end;
/*DAN*/

                    trn.Number_Pack  = 10/*БОЦБ*/;                 
                    trn.Chapter         = AccObNew.rec.Chapter;
                    trn.Date_Carry      = DL_GetBalanceDateAfterWorkDayByCalendar(Dпс, 0, dl_calendar_book);
                    trn.Numb_Document   = "";
                    trn.ResultCarry     = 1;
                    trn.Kind_Oper       = "";
                    trn.Ground          = ground/*"Перенос обязательств по сделке " + DataSet.DealCode*/;
                    trn.Department      = {OperDprt};
                    trn.FIIDPayer       = AccObOld.rec.Code_Currency;
                    trn.FIIDReceiver    = AccObNew.rec.Code_Currency;
                    trn.SumPayer        = Sum;
                    trn.SumReceiver     = Sum;
                    trn.AccountPayer    = AccObOld.rec.Account;
                    trn.AccountReceiver = AccObNew.rec.Account;
                 
                    if( not trn.Carry(NULL, ErrorMessage) )
                      stat = 1; 
                      RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при выполнении проводки \""+trn.Ground+"\". "+ErrorMessage);
                    else
                      CarryOb = CSC_OffBalTrRepParm(FD.tick.rec.DealCode,       
                                                    DataSet.OprName,   
                                                    ПолучитьКодСубъекта( FD.tick.rec.PartyID, PTCK_CONTR ),      
                                                    trn.AccountPayer,   
                                                    trn.AccountReceiver,
                                                    trn.SumPayer,         
                                                    GetFICode(trn.FIIDPayer, null, FICK_ISOSTRING));
                    end;
                    
                    if((not stat) and (DL_SetDocGrDeal(GrDealID, DLDOC_CARRY, trn.AccTrnID, dl_comm.rec.DocKind, dl_comm.rec.DocumentID) != 0))
                      stat = 1;  
                      RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при привязке проводки \""+trn.Ground+"\" к строке графика");
                    end;
                  end;
               else

                    If(FD.tick.rec.ispfi == "X")
                       ground = "ПФИ (). Перенос обязательств по сделке N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);
                    else
                       ground = "Перенос обязательств по сделке N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);
                    end;

                  FD.SetFIIDForCorAccNum(AccObNew.rec.Code_Currency);
                  if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, true, FIROLE_CORACC_PASSIVE, corr, null, Dпс ) )
                     if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, true, FIROLE_CORACC_PASSIVE, corr, Dпс ) ) )
                        stat = 1;
                        RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при актуализации счета по категории \"СчКорреспГлаваГ\" на дату "+string(Dпс:f)+". "+GetErrMsg());
                     end;   
                  end; 

                  if(not stat)
                    trn = RsbAccTransaction();

 /*DAN.Ролевая модель. Определяем конечного операциониста для проводки */
      retvalGetMO = GetMainOperInGroup(FD.Kind);
      if (retvalGetMO > 0)
	 Trn.Oper = retvalGetMO;
      else
         Trn.Oper = {Oper};
      end;
/*DAN*/


                    trn.Number_Pack  = 10/*БОЦБ*/;                 
                    trn.Chapter         = AccObNew.rec.Chapter;
                    trn.Date_Carry      = DL_GetBalanceDateAfterWorkDayByCalendar(Dпс, 0, dl_calendar_book);
                    trn.Numb_Document   = "";
                    trn.ResultCarry     = 1;
                    trn.Kind_Oper       = "";
                    trn.Ground          = ground/*"Перенос обязательств по сделке " + DataSet.DealCode*/;
                    trn.Department      = {OperDprt};
                    trn.FIIDPayer       = AccObOld.rec.Code_Currency;
                    trn.FIIDReceiver    = corr.rec.Code_Currency;
                    trn.SumPayer        = Sum;

                    if( SmartConvertSum(sum_2, sum, Dпс, AccObOld.rec.Code_Currency, corr.rec.Code_Currency, false) == false )
                       stat = 1;
                       RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, GetCurrencyConvertErrorMsg(AccObOld.rec.Code_Currency, corr.rec.Code_Currency,Dпс));
                    end;

                    trn.SumReceiver     = sum_2;
                    trn.AccountPayer    = AccObOld.rec.Account;
                    trn.AccountReceiver = corr.rec.Account;
                  
                    if( not stat )
                       if( not trn.Carry(NULL, ErrorMessage) )
                         stat = 1; 
                         RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при выполнении проводки \""+trn.Ground+"\". "+ErrorMessage);
                       else
                         CarryOb = CSC_OffBalTrRepParm(FD.tick.rec.DealCode,       
                                                       DataSet.OprName,   
                                                       ПолучитьКодСубъекта( FD.tick.rec.PartyID, PTCK_CONTR ),      
                                                       trn.AccountPayer,   
                                                       trn.AccountReceiver,
                                                       trn.SumPayer,         
                                                       GetFICode(trn.FIIDPayer, null, FICK_ISOSTRING));
                       end;
                    end;
                    
                    if((not stat) and (DL_SetDocGrDeal(GrDealID, DLDOC_CARRY, trn.AccTrnID, dl_comm.rec.DocKind, dl_comm.rec.DocumentID) != 0))
                      stat = 1;  
                      RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при привязке проводки \""+trn.Ground+"\" к строке графика");
                    end;
                  end;

                  if(not stat)
                    trn = RsbAccTransaction();

                    If(FD.tick.rec.ispfi == "X")
                       ground = "ПФИ (). Перенос обязательств по сделке N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);
                    else
                       ground = "Перенос обязательств по сделке N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);
                    end;
/*DAN.Ролевая модель Определяем конечного операциониста для проводки */
      retvalGetMO = GetMainOperInGroup(FD.Kind);
      if (retvalGetMO > 0)
	 Trn.Oper = retvalGetMO;
      else
         Trn.Oper = {Oper};
      end;
/*DAN*/
                  
                    trn.Number_Pack  = 10/*БОЦБ*/;                 

                    trn.Chapter         = AccObNew.rec.Chapter;
                    trn.Date_Carry      = DL_GetBalanceDateAfterWorkDayByCalendar(Dпс, 0, dl_calendar_book);
                    trn.Numb_Document   = "";
                    trn.ResultCarry     = 1;
                    trn.Kind_Oper       = "";
                    trn.Ground          = ground/*"Перенос обязательств по сделке " + DataSet.DealCode*/;
                    trn.Department      = {OperDprt};
                    trn.FIIDPayer       = corr.rec.Code_Currency;
                    trn.FIIDReceiver    = AccObNew.rec.Code_Currency;

                    if( SmartConvertSum(sum_2, sum, Dпс, AccObNew.rec.Code_Currency, corr.rec.Code_Currency, false) == false )
                       stat = 1;
                       RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, GetCurrencyConvertErrorMsg(AccObNew.rec.Code_Currency, corr.rec.Code_Currency,Dпс));
                    end;

                    trn.SumPayer        = Sum_2;
                    trn.SumReceiver     = Sum;
                    trn.AccountPayer    = corr.rec.Account;
                    trn.AccountReceiver = AccObNew.rec.Account;
                  
                    if( not stat )
                       if( not trn.Carry(NULL, ErrorMessage) )
                         stat = 1; 
                         RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при выполнении проводки \""+trn.Ground+"\". "+ErrorMessage);
                       else
                         CarryOb_2 = CSC_OffBalTrRepParm(FD.tick.rec.DealCode,       
                                                       DataSet.OprName,   
                                                       ПолучитьКодСубъекта( FD.tick.rec.PartyID, PTCK_CONTR ),      
                                                       trn.AccountPayer,   
                                                       trn.AccountReceiver,
                                                       trn.SumReceiver,         
                                                       GetFICode(trn.FIIDReceiver, null, FICK_ISOSTRING));
                       end;
                    end;
                    
                    if((not stat) and (DL_SetDocGrDeal(GrDealID, DLDOC_CARRY, trn.AccTrnID, dl_comm.rec.DocKind, dl_comm.rec.DocumentID) != 0))
                      stat = 1;  
                      RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при привязке проводки \""+trn.Ground+"\" к строке графика");
                    end;
                  end;

               end;

             end;

             if((not stat) and Tr)
               Sum = ABS(GetRestAccount(AccTrOld.rec, Dпс));

               if( not FD.ActualCheckPeriod( "+Форвард, дрейф", null, null, FIROLE_FIREQ, AccTrNew, Dпс_реал, null, null, null, Dпс ) )
                 stat = 1;
                 RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при актуализации счета по категории \"+Форвард, дрейф\" на дату "+string(Dпс:f)+". "+GetErrMsg());
               end;

               if( КОРРЕСП_ГЛАВА_Г() == true )
                  if(not stat)
                    trn = RsbAccTransaction();

                    If(FD.tick.rec.ispfi == "X")
              /*PDV 498521*/         ground = "ПФИ ().Перенос по срокам для сделки N " + UserGetDealCode(FD) + " ЦБ " + GetFinName(FD.fininstr.rec) + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);
                    else
                       ground = "Перенос требований по сделке N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);
                    end;

                    trn.Number_Pack  = 10/*БОЦБ*/;                 

/*DAN.Ролевая модель Определяем конечного операциониста для проводки */
      retvalGetMO = GetMainOperInGroup(FD.Kind);
      if (retvalGetMO > 0)
	 Trn.Oper = retvalGetMO;
      else
         Trn.Oper = {Oper};
      end;
/*DAN*/
                 
                    trn.Chapter         = AccTrNew.rec.Chapter;
                    trn.Date_Carry      = DL_GetBalanceDateAfterWorkDayByCalendar(Dпс, 0, dl_calendar_book);
                    trn.Numb_Document   = "";
                    trn.ResultCarry     = 1;
                    trn.Kind_Oper       = "";
                    trn.Ground          = ground/*"Перенос требований по сделке " + DataSet.DealCode*/;
                    trn.Department      = {OperDprt};
                    trn.FIIDPayer       = AccTrNew.rec.Code_Currency;
                    trn.FIIDReceiver    = AccTrOld.rec.Code_Currency;
                    trn.SumPayer        = Sum;
                    trn.SumReceiver     = Sum;
                    trn.AccountPayer    = AccTrNew.rec.Account;
                    trn.AccountReceiver = AccTrOld.rec.Account;
                 
                    if( not trn.Carry(NULL, ErrorMessage) )
                      stat = 1; 
                      RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при выполнении проводки \""+trn.Ground+"\". "+ErrorMessage);
                    else
                      CarryTr = CSC_OffBalTrRepParm(FD.tick.rec.DealCode,       
                                                    DataSet.OprName,   
                                                    ПолучитьКодСубъекта( FD.tick.rec.PartyID, PTCK_CONTR ),      
                                                    trn.AccountPayer,   
                                                    trn.AccountReceiver,
                                                    trn.SumPayer,         
                                                    GetFICode(trn.FIIDPayer, null, FICK_ISOSTRING));
                    end;
                 
                    if((not stat) and (DL_SetDocGrDeal(GrDealID, DLDOC_CARRY, trn.AccTrnID, dl_comm.rec.DocKind, dl_comm.rec.DocumentID) != 0))
                      stat = 1;  
                      RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при привязке проводки \""+trn.Ground+"\" к строке графика");
                    end;
                  end;
               else

                  FD.SetFIIDForCorAccNum(AccTrNew.rec.Code_Currency);
                  if( not FD.IsExistAccount( "СчКорреспГлаваГ", null, true, FIROLE_CORACC_ACTIVE, corr, null, Dпс ) )
                     if( (not FD.OpenAccount( "СчКорреспГлаваГ", null, true, FIROLE_CORACC_ACTIVE, corr, Dпс ) ) )
                        stat = 1;
                        RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при актуализации счета по категории \"СчКорреспГлаваГ\" на дату "+string(Dпс:f)+". "+GetErrMsg());
                     end;   
                  end; 

                  if(not stat)
                    trn = RsbAccTransaction();
                    If(FD.tick.rec.ispfi == "X")
                       ground = "ПФИ (). Перенос требований по сделке N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);
                    else
                       ground = "Перенос требований по сделке N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);
                    end;

                    trn.Number_Pack  = 10/*БОЦБ*/;                 

/*DAN.Ролевая модель Определяем конечного операциониста для проводки */
      retvalGetMO = GetMainOperInGroup(FD.Kind);
      if (retvalGetMO > 0)
	 Trn.Oper = retvalGetMO;
      else
         Trn.Oper = {Oper};
      end;
/*DAN*/
                 
                    trn.Chapter         = AccTrNew.rec.Chapter;
                    trn.Date_Carry      = DL_GetBalanceDateAfterWorkDayByCalendar(Dпс, 0, dl_calendar_book);
                    trn.Numb_Document   = "";
                    trn.ResultCarry     = 1;
                    trn.Kind_Oper       = "";
                    trn.Ground          = ground/*"Перенос требований по сделке " + DataSet.DealCode*/;
                    trn.Department      = {OperDprt};
                    trn.FIIDPayer       = corr.rec.Code_Currency;
                    trn.FIIDReceiver    = AccTrOld.rec.Code_Currency;

                    if( SmartConvertSum(sum_2, sum, Dпс, AccTrOld.rec.Code_Currency, corr.rec.Code_Currency, false) == false )
                       stat = 1;
                       RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, GetCurrencyConvertErrorMsg(AccTrOld.rec.Code_Currency, corr.rec.Code_Currency,Dпс));
                    end;

                    trn.SumPayer        = Sum_2;
                    trn.SumReceiver     = Sum;
                    trn.AccountPayer    = corr.rec.Account;
                    trn.AccountReceiver = AccTrOld.rec.Account;
                 
                    if( not stat )
                       if( not trn.Carry(NULL, ErrorMessage) )
                         stat = 1; 
                         RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при выполнении проводки \""+trn.Ground+"\". "+ErrorMessage);
                       else
                         CarryTr = CSC_OffBalTrRepParm(FD.tick.rec.DealCode,       
                                                       DataSet.OprName,   
                                                       ПолучитьКодСубъекта( FD.tick.rec.PartyID, PTCK_CONTR ),      
                                                       trn.AccountPayer,   
                                                       trn.AccountReceiver,
                                                       trn.SumReceiver,         
                                                       GetFICode(trn.FIIDReceiver, null, FICK_ISOSTRING));
                       end;
                    end;
                 
                    if((not stat) and (DL_SetDocGrDeal(GrDealID, DLDOC_CARRY, trn.AccTrnID, dl_comm.rec.DocKind, dl_comm.rec.DocumentID) != 0))
                      stat = 1;  
                      RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при привязке проводки \""+trn.Ground+"\" к строке графика");
                    end;
                  end;

                  if(not stat)
                    trn = RsbAccTransaction();

                    If(FD.tick.rec.ispfi == "X")
                       ground = "ПФИ (). Перенос требований по сделке N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);
                    else
                       ground = "Перенос требований по сделке N " + UserGetDealCode(FD) + " от " + FD.tick.rec.DealDate + ". К/агент: " + UserGetPartyShortName(FD.tick.rec.partyid);
                    end;

                    trn.Number_Pack  = 10/*БОЦБ*/;                 
/*DAN.Ролевая модель Определяем конечного операциониста для проводки */
      retvalGetMO = GetMainOperInGroup(FD.Kind);
      if (retvalGetMO > 0)
	 Trn.Oper = retvalGetMO;
      else
         Trn.Oper = {Oper};
      end;
/*DAN*/
                 
                    trn.Chapter         = AccTrNew.rec.Chapter;
                    trn.Date_Carry      = DL_GetBalanceDateAfterWorkDayByCalendar(Dпс, 0, dl_calendar_book);
                    trn.Numb_Document   = "";
                    trn.ResultCarry     = 1;
                    trn.Kind_Oper       = "";
                    trn.Ground          = ground/*"Перенос требований по сделке " + DataSet.DealCode*/;
                    trn.Department      = {OperDprt};
                    trn.FIIDPayer       = AccTrNew.rec.Code_Currency;
                    trn.FIIDReceiver    = corr.rec.Code_Currency;
                    trn.SumPayer        = Sum;

                    if( SmartConvertSum(sum_2, sum, Dпс, AccObNew.rec.Code_Currency, corr.rec.Code_Currency, false) == false )
                       stat = 1;
                       RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, GetCurrencyConvertErrorMsg(AccObNew.rec.Code_Currency, corr.rec.Code_Currency,Dпс));
                    end;

                    trn.SumReceiver     = Sum_2;
                    trn.AccountPayer    = AccTrNew.rec.Account;
                    trn.AccountReceiver = corr.rec.Account;
                 
                    if( not stat )
                       if( not trn.Carry(NULL, ErrorMessage) )
                         stat = 1; 
                         RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при выполнении проводки \""+trn.Ground+"\". "+ErrorMessage);
                       else
                         CarryTr_2 = CSC_OffBalTrRepParm(FD.tick.rec.DealCode,       
                                                       DataSet.OprName,   
                                                       ПолучитьКодСубъекта( FD.tick.rec.PartyID, PTCK_CONTR ),      
                                                       trn.AccountPayer,   
                                                       trn.AccountReceiver,
                                                       trn.SumPayer,         
                                                       GetFICode(trn.FIIDPayer, null, FICK_ISOSTRING));
                       end;
                    end;
                 
                    if((not stat) and (DL_SetDocGrDeal(GrDealID, DLDOC_CARRY, trn.AccTrnID, dl_comm.rec.DocKind, dl_comm.rec.DocumentID) != 0))
                      stat = 1;  
                      RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при привязке проводки \""+trn.Ground+"\" к строке графика");
                    end;
                  end;
               end;

             end;
           
             if((not stat) and (DL_UpdateGrDealAccByID(GrDealID, DLGR_ACCKIND_ACCOUNTING, DLGRACC_STATE_FACTEXEC) != 0))
               stat = 1; 
               RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при изменении статуса действия по строке графика");
             end;

           end;

           if(not stat)
             Dпл      = GetNextTransferDate(FD, K1, K2, Dпс_реал);
             Dпл_реал = GetNextPlanTransferDate(FD, K1, K2, Dпс_реал, PeriodTr, PeriodOb);

             if(Dпл != date(0,0,0))
               Dпс      = Dпл;
               Dпс_реал = Dпл_реал;
               if(DL_InsertGrDealRet(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_TEMPL_TRANSFER, Dпс, FD.tick.rec.DealTime, FD.tick.rec.PFI, GrDealID) != 0)
                 stat = 1; //ошибка при создании строки графика
                 RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, stat, "Ошибка при создании строки графика");
               end;
             else
               Dпс      = date(0,0,0);
               Dпс_реал = date(0,0,0);
             end;

           end; 

           if(stat)
             RslDefCon.RollbackTrans(); //Откатить транзакцию
           else
             if(CarryTr != null)
               RepDataArr[RepDataArr.size] = CarryTr;
             end;
             if(CarryTr_2 != null)
               RepDataArr[RepDataArr.size] = CarryTr_2;
             end;
             if(CarryOb != null)
               RepDataArr[RepDataArr.size] = CarryOb;
             end;
             if(CarryOb_2 != null)
               RepDataArr[RepDataArr.size] = CarryOb_2;
             end;

             RslDefCon.CommitTrans(); //Завершить транзакцию
           end;

         end;
       end;
       
       UseProgress(i = i + 1);
     end;

     RemProgress();

     DL_SaveDataForReport_XML(dl_comm.rec.DocumentID, dl_comm.rec.DocKind, RepDataArr, LOG_TYPE_DATA);
     SaveErrForReport_XML( dl_comm.rec.DocumentID, dl_comm.rec.DocKind );

     ReplaceMacro ( "msgbox", NULL );
   end;
 else
   msgbox("Ошибка поиска сервисной операции");
   err = 1; 
 end;

 return err;

OnError(er)
  
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  DL_SaveDataForReport_XML(dl_comm.rec.DocumentID, dl_comm.rec.DocKind, RepDataArr, LOG_TYPE_DATA);
  RepErrArr[RepErrArr.size] = CSC_OffBalTrRepErr(FD.tick.rec.DealCode, 1, er.message);
  SaveErrForReport_XML( dl_comm.rec.DocumentID, dl_comm.rec.DocKind );
  return 1;
end;
