/* Операция займа 
   Шаг оплаты единовременных комиссий */
import InsCarryDoc, "sp_class.mac", "sp_carcm.mac";

private record SpChangeDates("spchdate.str");/*глобальная структура с данными для расчетов*/

macro ExecuteStep( doc, FDoc, DocKind, ID_Op )
   record  deal(dl_tick);  
   var     FD, 
           Dk = SpChangeDates.FactDate; /*дата оплаты комиссии*/ 

   if( SpChangeDates.Action <= 0 )
      msgbox("Шаг оплаты комиссий из списка шагов выполнять запрещено."); 
      return 1;
   end; 

   /*Проверить наличие шага оплаты комиссий*/
   if( SP_IsExistOprStep( ID_Op, 110, true ) ) 
      msgbox( "Оплата комиссий по данной операции уже выполнена." );
      return 1;
   end;

   /*Не должно быть выполненных шагов с более поздней планируемой датой*/
   if( Dk < SP_GetMaxPlanStepDate( ID_Op, Dk ) )
      msgbox("Есть выполненные шаги с планируемой датой больше|чем дата оплаты комиссии.|Запрещено выполнять операцию."); 
      return 1;
   end;

   SetBuff( deal, FDoc );

   FD = SPFirstDoc( deal );

   /*В панели расчетов задана дата оплаты комиссии != ранее установленной дате оплаты*/
   if( Dk != FD.DateArray[DATE_DEALCOMISS] )  
      if( not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALCOMISS), Dk ) )
         msgbox("Ошибка при попытке переинициализировать дату оплаты комиссии." );         
         return 1;
      end;

      if( Dk > FD.DateArray[DATE_DEALEEND] ) /*Переинитить дату завершения сделки*/
         if( not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALEEND), Dk ) )
            msgbox("Ошибка при попытке переинициализировать дату завершения операции." );         
            return 1;
         end;
      end;
   end;

   return 0;
end;
