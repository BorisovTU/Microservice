/* Операция погашения выпуска
   Шаг - настройка операции      */
import InsCarryDoc, "sp_categ.mac", "sp_car.mac", "sp_voop.mac", "spRetire.mac";

macro executeParamStep( kind_oper, BOf_kind, FDoc, ID_Operation, ID_Step )

  record  deal(dl_tick);
  var     FD, Amount, cmd, DS, Select = "";

  SetBuff( deal, FDoc ); 
  FD = SPFirstDoc( deal, false, true );

  if( CheckTaxDepositoryMode() == false ) /*если не включен режим хранилища данных для налогового учета*/
     if( РазрешеноВыполнятьПогашение( FD ) == false 
       )
        return 1;
     end;

     /*Все лоты должны иметь статус ПОСТАВЛЕН*/
     Select = "select count(1) NumLots from dpmwrtsum_dbt where t_state = ? AND t_FIID = ? AND t_Party = ?";
     if( IsClientDeal(FD.tick.rec) )
        Select = Select + " and t_Portfolio = ?";
     else
        Select = Select + " and t_Portfolio in(?,?,?,?)";
     end;

     cmd = RSDCommand( Select );
     cmd.addParam( "", RSDBP_IN, PM_WRTSUM_NOTFORM );
     cmd.addParam( "", RSDBP_IN, FD.dl_leg.rec.PFI );
     cmd.addParam( "", RSDBP_IN, FD.tick.rec.ClientID );

     if( IsClientDeal(FD.tick.rec) )
        cmd.addParam( "", RSDBP_IN, KINDPORT_CLIENT );
     else
        cmd.addParam( "", RSDBP_IN, KINDPORT_RETIRE );
        cmd.addParam( "", RSDBP_IN, KINDPORT_TRADE );
        cmd.addParam( "", RSDBP_IN, KINDPORT_SALE );
        cmd.addParam( "", RSDBP_IN, KINDPORT_PROMISSORY );
     end;

     cmd.execute();
     DS = TRsbDataSet(cmd);
     if( DS.movenext() AND (DS.NumLots > 0) )
        MsgBox( "Все лоты погашаемого выпуска должны иметь статус \"Поставлен\"." );
        /*выдаем предупреждение, но погашение для поставленных выполняем*/
     end;
  end;

  DefineOprDate( FD.GetKindDate(DATE_DEALDATE),        FD.DateArray[DATE_DEALDATE]   );
  DefineOprDate( FD.GetKindDate(DATE_DEALSETAVOIRISS), FD.DateArray[DATE_DEALSETAVOIRISS] );
  DefineOprDate( FD.GetKindDate(DATE_DEALPAY),         FD.DateArray[DATE_DEALPAY]  );
  DefineOprDate( FD.GetKindDate(DATE_DEALEEND),        maxDate( maxDate(FD.DateArray[DATE_DEALPAY], FD.DateArray[DATE_DEALSETAVOIRISS]), FD.DateArray[DATE_DEALCOMISS]) );
  DefineOprDate( FD.GetKindDate(DATE_DEALCOMISS),      FD.DateArray[DATE_DEALCOMISS]      );

  if( ИнициализироватьОперациюПогашения( FD ) != 0 )
     return 1;
  end;

  return 0;
end;

