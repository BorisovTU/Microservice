/*******************************************************************************
 FILE         :   SPACC.MAC

 COPYRIGHT    :   R-Style, 1998 

 DESCRIPTION  :   RS-Bank, БО ЦБ, всякое для счетов

 PROGRAMMED BY:   Alex Kormukhin

 CREATION DATE:   30.11.1998
*******************************************************************************/

 import spOOP,spDebug,BankInter,CurrInter;

  /* класс для пролистывания всех счетов по заданному балансовому */
  class(TForwardSequenceBase) TBAccByBalFilteredBase(blncFName,accFName,_planId:integer,_chapter:integer,_balAcc:string)
    InitTForwardSequenceBase();
    var planId:integer=_planId,
        chapter:integer=_chapter,
        balAcc:string=_balAcc;
/*    println(blncFName,accFName,planId,chapter,balAcc);*/

    var cAccBlnc=TBFile(blncFName,null,planId), /* by Chapter, Open_Close, Client, Sort */
        cAcc=TBFile(accFName,null,0); /* by Chapter, Account, Code_Currency - PK */
        
    macro conformed():bool
      cAcc.rec.Chapter=cAccBlnc.rec.Chapter;
      cAcc.rec.Account=cAccBlnc.rec.Account;
      cAcc.rec.Code_Currency=cAccBlnc.rec.Code_Currency;
      if(cAcc.getEQ() != true)
        RunError("Нет счета для балансового"); return false; end;
      return true;
    end;

    macro _conformed():bool
      if(cAccBlnc.rec.Chapter != chapter)
        return false; end;
      /*println ("cnf"); */
      if((cAccBlnc.rec()(2+planId) != balAcc) and (balAcc != ""))
        return false; end;
      return conformed ();
    end;

    macro first()
      cAccBlnc.rec.Chapter=chapter;
      cAccBlnc.rec.Balance0=balAcc;
      cAccBlnc.rec.Balance1=balAcc;
      cAccBlnc.rec.Balance2=balAcc;
      cAccBlnc.rec.Balance3=balAcc;
      cAccBlnc.rec.Balance4=balAcc;
      cAccBlnc.rec.Balance5=balAcc;
      cAccBlnc.rec.Balance6=balAcc;
      cAccBlnc.rec.Balance7=balAcc;
      cAccBlnc.rec.Balance8=balAcc;
      cAccBlnc.rec.Balance9=balAcc;
      cAccBlnc.rec.Balance10=balAcc;
      cAccBlnc.rec.Balance11=balAcc;
      cAccBlnc.rec.Account="";
      cAccBlnc.rec.Code_Currency=0;
      var stat=cAccBlnc.getGE();
/*      println(stat);*/
      while ((stat == true) AND (_conformed() != true))
        stat=cAccBlnc.next(); end;
      /*spdPrintRec(cAccBlnc);
      spdPrintRec(cAcc);*/
      return stat;
    end;

    macro next()
      var stat=cAccBlnc.next();
      while ((stat == true) AND (_conformed() != true))
        stat=cAccBlnc.next(); end;
      /*spdPrintRec(cAccBlnc);
      spdPrintRec(cAcc);*/
      return stat;
    end;

    macro _rec()
      return cAccBlnc.rec;
    end;

    macro rec()
      return cAcc.rec;
    end;

  end; /* class TBAccByBalFilteredBase */


  class(TBAccByBalFilteredBase) TBAccByBalFiltered(_planId:integer,_chapter:integer,_balAcc:string)
    InitTBAccByBalFilteredBase("accblnc","account",_planId,_chapter,_balAcc);

    macro rest(dateT,dateB)
      return RestAC(rec().Account,rec().Code_Currency,dateT,dateB,rec().Chapter);
    end;
  end; /* class TBAccByBalFiltered */
