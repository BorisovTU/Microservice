/*******************************************************************************
 FILE         :   spjr_prs.mac
 DESCRIPTION  :   Отчет "Журнал лицевого учета облигаций"
 PROGRAMMED BY:   Леонов И. Е.
 CREATION DATE:   27.04.02
*******************************************************************************/
import Календарь, SecInter, DealsInter, "sp_categ.mac", "spserv.mac", "sccomiss.mac", "sp_class.mac";

/*Временный файл отчета*/
file SpjrPrs_tmp( spjr_prs, "sp_tmp.def" ) key 0 WRITE;
file SpjrPrs_tmp_sale( spjr_prs, "sp_tmp.def" );
file SpjrPrs_tmp_Buy( spjr_prs, "sp_tmp.def" );

/* Структура с данными для отчета*/
class SourceData( _DprtID: integer, _BegDate : date, _EndDate : date,  
                  _BondKind: integer, _FIID: integer, _apostrSum:bool )

  var DprtID     = _DprtID,
      BegDate    = _BegDate,      
      EndDate    = _EndDate,     
      BondKind   = _BondKind,
      FIID       = _FIID,
      apostrSum  = _apostrSum;

  var ReportRun  = false,
      IsCouponAvr,
      PrnDprtID,  
      PrnRepDate, 
      PrnFIID;

  var AmountInPortf, SummInPortf, Nominal, AmountBuy, 
      SumBuy, NKDBuy, AmountSale, SumSale, NKDSale, ComissionMMVB;  

  macro SetData( DprtID, RepDate, FIID )
      PrnDprtID  = DprtID; 
      PrnRepDate = RepDate; 
      PrnFIID    = FIID;

      AmountBuy = $0; SumBuy = $0; NKDBuy = $0;
      AmountSale = $0; SumSale = $0; NKDSale = $0;  
      SummInPortf = $0; Nominal = $0; ComissionMMVB = $0;
      AmountInPortf = 0;
  end;

  macro PrintSum( Sum, Prn0 )
    if( Sum == $0 ) 
      if( Prn0 != true ) return ""; end;
      return $0;
    elif( apostrSum == true )  return string( Sum:a );
    else return string(Sum);
    end;
  end;

  SetData( -1, Date(0,0,0), -1 );
end;

macro GetAmountForAllPrtf(Data)
  file llvalue( "llvalues.dbt" );
  var Amount, ll_stat, FD;
  Data.AmountInPortf = $0;
  ClearRecord( llvalue );
  llvalue.List = OBJTYPE_KINDPORT;
  ll_stat = GetGE( llvalue );
  while( ll_stat AND (llvalue.List == OBJTYPE_KINDPORT) )
     Amount = $0;
     FD = SPFirstDocFI( 0, 0, Data.PrnFIID, llvalue.Element, {OurBank}, null, null, null );
     FD.GetAmountInPortofolio( GetDateAfterWorkDays( Data.PrnRepDate, -1), Amount, false ); 
     Data.AmountInPortf = Data.AmountInPortf + Amount;
     ll_stat = Next( llvalue );
  end;
end;

macro PrintHeadDprt()
   file   Dprt( dp_dep );
   record rParty( party );
   var    Name;
   
   /* Название филиала или нашего банка */
   if( SpjrPrs_tmp.DprtID != {OurBank} )
      ClearRecord( Dprt );
      Dprt.Code = SpjrPrs_tmp.DprtID;
      if( not GetEQ( Dprt ) )
        RunError( "Не найден филиал" );    
      end;
      Name = "Филиал " + Dprt.name;
   else
      if( ПолучитьСубъекта( {OurBank}, rParty ) ) 
         RunError( "Не найден наш банк");
      end;
      Name = rParty.ShortName;
   end;

   println( Name, "\n\t\t\t\tЖУРНАЛ ЛИЦЕВОГО УЧЕТА ОБЛИГАЦИЙ\n\n" );
end;

macro GetDeal( Data, DealID, IsBack, Amount, Price, Sum, NKD )
   file   tick( dl_tick );
   record avr_p( pmpaym );
   record cur_p( pmpaym );
   record avr( avoiriss );
   record FI( fininstr );
   record dl_leg( dl_leg );
   var    Sums = ComissionSums(), SumRUR = $0, FD, AgentComiss = $0;

   ClearRecord( tick );
   tick.DealID = DealID;
   if( not GetEQ(tick) )
      RunError( "Не найдена сделка" );
   end;

   FD = SPFirstDoc(tick, IsBack);

   NKD = FD.dl_leg.rec.NKD;

   if( FD.pm_money.rec.FIID != 0 )
     /*Получим сумму платежа в рублях*/
     if( ConvSum( SumRUR, (FD.pm_money.rec.Amount), FD.tick.rec.DealDate, FD.pm_money.rec.FIID, 0) )
       MsgBox( "Ошибка при конвертации суммы сделки|", FD.tick.rec.DealCode );
     end;
   else SumRUR = FD.pm_money.rec.Amount;
   end;    

   Amount = FD.pm_avoir.rec.Amount;
   Price  = Money( SumRUR / FD.pm_avoir.rec.Amount );
   Sum    = SumRUR;


   ПолучитьКомиссииПоСделке( tick, FD.DateArray[DATE_DEALDATE], false, false, Sums );
   
   AgentComiss = Sums.AgentSum + Sums.AgentNDS;

   Data.ComissionMMVB = Data.ComissionMMVB + AgentComiss;

   SetParm( 3, Amount );
   SetParm( 4, Price );
   SetParm( 5, Sum );
   SetParm( 6, NKD );
end;

macro PrintHeadAvr( Data )
  record avr( avoiriss );
  record FI( fininstr );
  var    PrevPrice = $0, NominalRUR = $0, PrevPriceNominal = $0, CourceNominal = $0;
  var    FaceValueMoney = $0;

  if( ПолучитьФинИн( Data.PrnFIID, FI, avr ) ) 
     RunError("Не найдена ц/б" );
  end;

  /*кол-во бумаг в портфеле на начало дня (конец предыдущего)*/
  GetAmountForAllPrtf(Data);

  FI_GetNominal(Data.PrnFIID, FaceValueMoney);

  /*Рыночная стоимость ц/б предыдущего рабочего дня в валюте номинала*/
  if( ConvSum( PrevPriceNominal, $1, GetDateAfterWorkDays( Data.PrnRepDate, -1), FI.FIID, FI.FaceValueFI) )
    Msgbox( "Не найден рыночный курс на дату " + GetDateAfterWorkDays( Data.PrnRepDate, -1) + " для ц/б|\""+ FI.FI_Code +"\"" );
    exit(1);
  end;

  /*курс номинала к рублю за предыдущий день*/
  if( ConvSum( CourceNominal, $1, GetDateAfterWorkDays( Data.PrnRepDate, -1), FI.FaceValueFI, 0) )
    Msgbox( "Не найден курс валюты номинала ц/б к национальной валюте на дату " + GetDateAfterWorkDays( Data.PrnRepDate, -1));
    exit(1);
  end;

  /*Цена ц/б в предыдущий день в рублях*/
  PrevPrice = ((double(CourceNominal)) * (double(PrevPriceNominal)));
  

  if( FI.FaceValueFI != 0 )
    if( ConvSum( NominalRUR, FaceValueMoney, Data.PrnRepDate, FI.FaceValueFI, 0) )
      Msgbox( "Ошибка при конвертации суммы номинала ц/б|", FI.FI_Code );
      exit(1);
    end;
  else NominalRUR = FaceValueMoney;
  end;
  Data.Nominal = NominalRUR;

  if( Data.Nominal == 0)
    RunError("Ошибка при определении номинала ц/б ", avr.LSIN );
  end;

  Data.SummInPortf = money(PrevPrice*Data.AmountInPortf);
  Data.IsCouponAvr = FI_IsCouponAvoiriss( FI );
  println( "\n\nВыпуск  ", avr.LSIN );
  if( Data.IsCouponAvr ) /*Для купонных бумаг*/
[┌──────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┐
 │Дата, слу-│   Куплено (штук,   │    Цена сделки     │   Сумма платежа    │   НКД уплаченный   │   Продано (штук,   │    Цена сделки     │    Сумма выручки   │    НКД полученный  │ 
 │жебные от-│    номиналом по    │ ( в % от номинала )│       (в руб.)     │       (в руб.)     │    номиналом по    │  ( в % от номинала)│       (в руб.)     │       (в руб.)     │  
 │  метки   │############### руб)│                    │                    │                    │############### руб)│                    │                    │                    │  
 ├──────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤
 │    1     │         2          │         3          │         4          │        4a          │         5          │          6         │          7         │        7a          │
 ├──────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤
 │##########│                    │                    │                    │                    │                    │                    │                    │                    │
 │Остаток на│                    │                    │                    │                    │                    │                    │                    │                    │
 │начало    │####################│####################│####################│                    │                    │                    │                    │                    │
 ├──────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤
]( Data.PrintSum(NominalRUR, true):r, 
   Data.PrintSum(NominalRUR, true):r, 
   Data.PrnRepDate, 
   int(double(Data.AmountInPortf)):r,
   Data.PrintSum(PrevPrice, true):r, 
   Data.PrintSum( Data.SummInPortf,true):r);

else /*Не купонные бумаги*/
[┌──────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┬────────────────────┐
 │Дата, слу-│   Куплено (штук,   │    Цена сделки     │   Сумма платежа    │   Продано (штук,   │    Цена сделки     │    Сумма выручки   │ 
 │жебные от-│    номиналом по    │ ( в % от номинала )│       (в руб.)     │    номиналом по    │  ( в % от номинала)│       (в руб.)     │  
 │  метки   │############### руб)│                    │                    │############### руб)│                    │                    │  
 ├──────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤
 │    1     │         2          │         3          │         4          │         5          │          6         │          7         │
 ├──────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤
 │##########│                    │                    │                    │                    │                    │                    │
 │Остаток на│                    │                    │                    │                    │                    │                    │
 │начало    │####################│####################│####################│                    │                    │                    │
 ├──────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤
]( Data.PrintSum(NominalRUR, true):r, 
   Data.PrintSum(NominalRUR, true):r, 
   Data.PrnRepDate, 
   int(double(Data.AmountInPortf)):r,
   Data.PrintSum(PrevPrice, true):r, 
   Data.PrintSum( Data.SummInPortf, true):r);
  end;
end;

macro PrintLine( Data, DealIDBuy, BuyIsBack, DealIDSale, SaleIsBack )
  var AmountBuy = $0, PriceBuy = $0, SumBuy = $0, NKDBuy = $0,
      AmountSale = $0, PriceSale = $0, SumSale = $0, NKDSale = $0;  

  if( DealIDBuy != 0 )
     GetDeal( Data, DealIDBuy, BuyIsBack, AmountBuy, PriceBuy, SumBuy, NKDBuy );
     /*% от номинала*/
     PriceBuy = PriceBuy * 100/Data.Nominal;
  end;

  if( DealIDSale != 0 )
     GetDeal( Data, DealIDSale, SaleIsBack, AmountSale, PriceSale, SumSale, NKDSale );
     PriceSale = PriceSale * 100/Data.Nominal;
  end;

  if( Data.IsCouponAvr ) /*Для купонных бумаг*/
[│          │####################│####################│####################│####################│####################│####################│####################│####################│
 ├──────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤
](Data.PrintSum( AmountBuy ):r, PriceBuy:20:2:r, 
  Data.PrintSum( SumBuy):r, Data.PrintSum( NKDBuy):r, 
  Data.PrintSum( AmountSale):r, Data.PrintSum( PriceSale):r, 
  Data.PrintSum( SumSale ):r, Data.PrintSum( NKDSale ):r );

  else
[│          │####################│####################│####################│####################│####################│####################│
 ├──────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤
](Data.PrintSum( AmountBuy ):r, PriceBuy:20:2:r, 
  Data.PrintSum( SumBuy ):r, Data.PrintSum( AmountSale ):r, 
  Data.PrintSum( PriceSale ):r, Data.PrintSum( SumSale ):r );
  end;

  Data.AmountBuy = Data.AmountBuy + AmountBuy;
  Data.SumBuy    = Data.SumBuy + SumBuy;
  Data.NKDBuy    = Data.NKDBuy + NKDBuy;
  Data.AmountSale= Data.AmountSale + AmountSale; 
  Data.SumSale   = Data.SumSale + SumSale; 
  Data.NKDSale   = Data.NKDSale +NKDSale;  

end;

macro PrintHeadFoot( Data )
  record avr( avoiriss );
  record FI( fininstr );
  var Price, EndRest, Overvalue, ChangePort, Saldo, PriceNominal = $0,
      CourceNominal = $0;

  if( ПолучитьФинИн( Data.PrnFIID, FI, avr ) ) 
     RunError("Не найдена ц/б" );
  end;

  /*Рыночная стоимость ц/б в валюте номинала*/
  if( ConvSum( PriceNominal, $1, Data.PrnRepDate, Data.PrnFIID, FI.FaceValueFI) )
    Msgbox( "Не найден рыночный курс на дату " + Data.PrnRepDate + " для ц/б|\""+ FI.FI_Code +"\"" );
    exit(1);
  end;

  /*курс валюты номинала к рублю*/
  if( ConvSum( CourceNominal, $1, Data.PrnRepDate, FI.FaceValueFI, 0) )
    Msgbox( "Не найден курс валюты номинала ц/б к национальной валюте на дату " + Data.PrnRepDate );
    exit(1);
  end;

  Price = ((double(CourceNominal)) * (double(PriceNominal)));

  EndRest = Data.AmountInPortf + Data.AmountBuy - Data.AmountSale;
  Overvalue = money(EndRest*Price);

  if( Data.IsCouponAvr ) /*Для купонных бумаг*/
[│Итого     │####################│                    │####################│####################│####################│                    │####################│####################│
 ├──────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤
 │Остаток на│                    │                    │                    │                    │                    │                    │                    │                    │
 │конец     │####################│####################│####################│                    │                    │                    │                    │                    │
 └──────────┴────────────────────┴────────────────────┴────────────────────┴────────────────────┴────────────────────┴────────────────────┴────────────────────┴────────────────────┘
]( Data.PrintSum( Data.AmountBuy, true ):r, Data.PrintSum( Data.SumBuy, true ):r, 
   Data.PrintSum( Data.NKDBuy, true ):r, Data.PrintSum( Data.AmountSale, true ):r,
   Data.PrintSum( Data.SumSale, true ):r, Data.PrintSum( Data.NKDSale, true ):r,
   Data.PrintSum( EndRest, true ):r, Data.PrintSum( Price, true ):r, 
   Data.PrintSum( Overvalue, true ):r);

  else /*Не купонные бумаги*/
[│Итого     │####################│                    │####################│####################│                    │####################│
 ├──────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┼────────────────────┤
 │Остаток на│                    │                    │                    │                    │                    │                    │
 │конец     │####################│####################│####################│                    │                    │                    │
 └──────────┴────────────────────┴────────────────────┴────────────────────┴────────────────────┴────────────────────┴────────────────────┘
]( Data.PrintSum( Data.AmountBuy, true ):r,  Data.PrintSum( Data.SumBuy, true ):r, 
   Data.PrintSum( Data.AmountSale, true ):r, Data.PrintSum( Data.SumSale, true ):r,
   Data.PrintSum( EndRest, true ):r, Data.PrintSum( Price, true ):r, 
   Data.PrintSum( Overvalue, true ):r );
  end;

  ChangePort = Overvalue - Data.SummInPortf; 
  Saldo = Data.SumSale - Data.SumBuy;

[Изменение портфеля  #################### руб.
 Сальдо расчетов     #################### руб.
 Сумма переоценки    #################### руб.
 Комиссия ММВБ       #################### руб.
 Налог               #################### руб.
]( Data.PrintSum( ChangePort, true ):r,
   Data.PrintSum( Saldo, true ):r,
   Data.PrintSum( ChangePort + Saldo, true ):r,
   Data.PrintSum( -(Data.ComissionMMVB), true ):r,
   Data.PrintSum( Data.SumBuy, true ):r
 );

  if( Data.IsCouponAvr ) /*Для купонных бумаг*/
[Сальдо расчетов по купонному доходу    #################### руб.
]( Data.PrintSum( Data.NKDSale - Data.NKDBuy, true ):r );
  end; 

end;

macro ПечататьОтчетПоБумаге( Data )
   var   ContinueBuy, ContinueSale, DealIDBuy, DealIDSale, BuyIsBack, SaleIsBack;

   Data.SetData( SpjrPrs_tmp.DprtID, SpjrPrs_tmp.RepDate, SpjrPrs_tmp.FIID );

   PrintHeadAvr( Data );

   ClearRecord( SpjrPrs_tmp_Buy );
   SpjrPrs_tmp_Buy.DprtID   = Data.PrnDprtID;
   SpjrPrs_tmp_Buy.FIID     = Data.PrnFIID;
   SpjrPrs_tmp_Buy.RepDate  = Data.PrnRepDate;
   SpjrPrs_tmp_Buy.Buy_Sale = PM_WRITEOFF_SUM_BUY;

   ClearRecord( SpjrPrs_tmp_sale );
   SpjrPrs_tmp_sale.DprtID   = Data.PrnDprtID;
   SpjrPrs_tmp_sale.FIID     = Data.PrnFIID;
   SpjrPrs_tmp_sale.RepDate  = Data.PrnRepDate;
   SpjrPrs_tmp_sale.Buy_Sale = 2; /*0-покупка, 1-продажа 2- что бы взять последнюю продажу*/

   /* В одной строке отчета должна быть и покупка и продажа */
   ContinueBuy  = GetGE( SpjrPrs_tmp_Buy );
   ContinueSale = GetLE( SpjrPrs_tmp_sale );
   while( ContinueBuy OR ContinueSale )

      DealIDBuy = DealIDSale = 0;
      if( ContinueBuy AND
          (SpjrPrs_tmp_Buy.DprtID   == Data.PrnDprtID) AND
          (SpjrPrs_tmp_Buy.FIID     == Data.PrnFIID) AND
          (SpjrPrs_tmp_Buy.RepDate  == Data.PrnRepDate) AND
          (SpjrPrs_tmp_Buy.Buy_Sale == PM_WRITEOFF_SUM_BUY) )

         DealIDBuy   = SpjrPrs_tmp_Buy.DealID;
         if(SpjrPrs_tmp_Buy.IsBack == "X")
           BuyIsBack = true;
         else
           BuyIsBack = false;
         end;
         ContinueBuy = Next( SpjrPrs_tmp_Buy );
      else
         ContinueBuy = false;
      end;

      if( ContinueSale AND
          (SpjrPrs_tmp_sale.DprtID   == Data.PrnDprtID) AND
          (SpjrPrs_tmp_sale.FIID     == Data.PrnFIID) AND
          (SpjrPrs_tmp_sale.RepDate  == Data.PrnRepDate) AND
          (SpjrPrs_tmp_sale.Buy_Sale == PM_WRITEOFF_SUM_SALE ) )

         DealIDSale   = SpjrPrs_tmp_sale.DealID;
         if(SpjrPrs_tmp_sale.IsBack == "X")
           SaleIsBack = true;
         else
           SaleIsBack = false;
         end;
         ContinueSale = Prev( SpjrPrs_tmp_sale );
      else
         ContinueSale = false;
      end;

      if( (DealIDBuy != 0)  OR  (DealIDSale != 0) )
         PrintLine( Data, DealIDBuy, BuyIsBack, DealIDSale, SaleIsBack );
      end;
   end;

   PrintHeadFoot( Data );
end;

macro ПечататьОтчет( Data )
  var Continue_cicle;

  ClearRecord( SpjrPrs_tmp );
  Continue_cicle = GetGE( SpjrPrs_tmp );
  Data.ReportRun = Continue_cicle;
  while( Continue_cicle )

     if( Data.PrnDprtID != SpjrPrs_tmp.DprtID )
        PrintHeadDprt();
        ПечататьОтчетПоБумаге( Data );
     elif( Data.PrnFIID != SpjrPrs_tmp.FIID )
        ПечататьОтчетПоБумаге( Data );
     elif( Data.PrnRepDate != SpjrPrs_tmp.RepDate )
        ПечататьОтчетПоБумаге( Data );
     end;

     Continue_cicle = Next( SpjrPrs_tmp );
  end;
end;

macro CheckDeal(Data, FD)

   record avr( avoiriss );
   record FI( fininstr );

   var DealType, Bond_Kind;

   if(IsRetirementCoupon( FD.tick.rec ))
     return false;
   end;
   
   if( (FD.tick.rec.DealStatus != DL_CLOSED) AND (FD.tick.rec.DealStatus != DL_READIED) )
     return false;
   end;
   
   if(FD.pmwrtsum.rec.State < PM_WRTSUM_FORM ) /*Статус лота должен быть не ниже "готов" */
      return false;
   end;
   
   DealType = GetDealBuySale( FD.tick.rec );
   if( (DealType != DEAL_TYPE_SALE) AND (DealType != DEAL_TYPE_BUY) ) 
      return false;
   end;
   
   /* Отбор сделок теперь производится не по дате заключения, а по дате поставки ц/б*/
   if((Data.BegDate > FD.pm_avoir.rec.ValueDate) OR (Data.EndDate < FD.pm_avoir.rec.ValueDate) )  
      return false;
   end;
  
   if( ПолучитьФинИн( FD.pm_avoir.rec.FIID, FI, avr ) ) 
      RunError("Не найдена ц/б по сделке|", FD.tick.rec.DealCode );
   end;

   if(Data.FIID < 0)
     if( (not FI_IsBond( FI )) OR
         ( Data.BondKind != ПолучитьВидОблигации( avr ) )) /*В отчете только облигации*/
        return false;
     end;
   end;

   if( ( ((Data.DprtID > 0) AND (Data.DprtID != FD.tick.rec.Department))) OR
       ( ((Data.FIID   > 0) AND (Data.FIID   != FD.pm_avoir.rec.FIID) )) )
      return false;
   end;

   Bond_Kind = ПолучитьВидОблигации( avr );
   if((Bond_Kind != ГКО) AND (Bond_Kind != ОФЗ) AND (Bond_Kind != ОБР)) /*В отчет должны попадать только эти виды облигаций*/
     return false;
   end;

   return true;
end;

macro FillDeal(FD, IsBack)
   
   ClearRecord( SpjrPrs_tmp );
   SpjrPrs_tmp.DealID   = FD.tick.rec.DealID;
   SpjrPrs_tmp.DprtID   = FD.tick.rec.Department;
   SpjrPrs_tmp.FIID     = FD.pm_avoir.rec.FIID;  
   SpjrPrs_tmp.RepDate  = FD.pm_avoir.rec.ValueDate;
   SpjrPrs_tmp.Buy_Sale = FD.pmwrtsum.rec.Buy_Sale;
   if(IsBack == true)
     SpjrPrs_tmp.IsBack = "X";
   else
     SpjrPrs_tmp.IsBack = "";
   end;

   if( not Insert( SpjrPrs_tmp ) )
      RunError( "Ошибка при вставке данных во временный файл");
   end;
end;

macro CделкиПоТипу( Data, BofficeKind )
  record avr_p (pmpaym);
  record pmwrtsum( pmwrtsum );
  file   tick( dl_tick ) key 6;
  var Continue_cicle, DealType, DealOK, Num = 0, Bond_Kind, FD, FD_Back, ExistBack;
  var Group;

  InitProgress( NRecords(tick), "Отчет \"Журнал лицевого учета облигаций\"", 
                 "Просмотр сделок" );


  ClearRecord( tick );
  tick.BofficeKind = BofficeKind;
  tick.ClientID    = -1; /*В отчете только наши сделки */

  Continue_cicle = GetGE( tick );
  while( Continue_cicle AND
         (tick.BofficeKind == BofficeKind) AND
         (tick.ClientID    == -1) )

     Num = Num + 1;
     UseProgress( Num );

     Group = SP_GetOperationGroup( tick);
     /*Если есть  еще и обратная часть, то она должна попасть в отчет*/
     if((IsBACKSALE(Group) == true) OR (IsREPO(Group) == true))
       ExistBack = true;
     else
       ExistBack = false;
     end;

     FD = SPFirstDoc(tick);
     if(ExistBack == true)
       FD_back = SPFirstDoc(tick, ExistBack);
     end;

     DealOK = CheckDeal(Data, FD);
 
     if(DealOK == true)
       FillDeal(FD, false);
     end;

     /*Для обратной части (если есть)*/
     if(ExistBack == true)
       DealOK = CheckDeal(Data, FD_Back);
       if(DealOK == true)
         FillDeal(FD_Back, true);
       end;
     end;

     Continue_cicle = Next( tick );
  end;
  RemProgress;
end;

macro СоздатьОтчет( Data )
  CделкиПоТипу( Data, BofficeKindSP ); /*Покупка/продажа*/
  CделкиПоТипу( Data, BofficeKindRT ); /*Погашения*/
end;

/*Запуск отчета*/
macro facekeep( DprtID: integer, BegDate : date, EndDate : date,
                BondKind: integer, FIID: integer, apostrSum:bool )

  var Data = SourceData( DprtID, BegDate, EndDate, BondKind, FIID, apostrSum ); 
  var TmpFName = GetWorkFileName("spjr_prs");

  Message( TmpFName + " Создание временного файла ..." );
  if( (not Create( SpjrPrs_tmp, TmpFName )) OR 
      (not Open( SpjrPrs_tmp,      TmpFName )) OR
      (not Open( SpjrPrs_tmp_sale, TmpFName )) OR
      (not Open( SpjrPrs_tmp_Buy,  TmpFName )) )
     RunError( "Невозможно создать временный файл ", TmpFName );
  end;
  Message( "Выполнение отчета \"Журнал лицевого учета облигаций\"" );

  СоздатьОтчет( Data );
  ПечататьОтчет( Data );

  if( Data.ReportRun == false )
     println( "\n\nНет данных, удовлеворяющих параметрам отчета.\n");
  else
     after_44_footer();
  end;
end;