/*
$Name:             ws_txfilistcond.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Возвращает строку с доп. условием. Используется виджетом FIUniListWidget из прикладного кода или листалкой TxAvoirIssList
*/
import "dldlngfun.mac", "spRepFun.mac";

// Режимы работы панели отчетов (виды отчетов)
// Регистры реализации
const REGISTRS_REALIZATION                    : Integer = 0;  // 1  Регистры реализации (201-206)
const REGISTRS_URGENT_DEALS                   : Integer = 1;  // 2  Регистры срочных сделок (301-306)
const REGISTRS_CLOSED_SHORT_POSITIONS         : Integer = 2;  // 6  Регистры закрытых коротких позиций (601-606)
const REGISTRS_INCH_STATE                     : Integer = 3;  // 22 Доходы и расходы от реализации ц/б (гос. и муниц.)
const REGISTRS_INCH_NOT_STATE                 : Integer = 4;  // 23 Доходы и расходы от реализации ц/б (кроме гос. и муниц.)
const REGISTRS_REALIZATION_2014               : Integer = 5;  // 29 Регистры реализации 2014 (211, 212, 215)
const REGISTRS_SHREP_REG_2014                 : Integer = 6;  // 30 Регистры коротких позиций 2014 (611, 612, 615)
// Регистры по начислениям
const REGISTRS_CHARGES_SECURS_BALANCE         : Integer = 7;  // 3  Регистры начислений по ц/б в остатке (401-404)
const REGISTRS_CHARGES_SECURS_REPO            : Integer = 8;  // 4  Регистры начислений по ц/б в РЕПО (405-408)
const REGISTRS_CHARGES_SHORT_POSITIONS        : Integer = 9;  // 7  Регистры начислений по коротким позициям (701-704)
const REGISTRS_CHARGES_2014                   : Integer = 10; // 28 Регистр по начислению 2014 (411)
const REGISTRS_OUTLAY_SP_REG_2014             : Integer = 11; // 31 Регистр по начислению расхода по коротким позициям 2014 (721)
// Прочие регистры
const REGISTRS_REPO                           : Integer = 12; // 5  Проценты по РЕПО (501-506, 508)
const REGISTRS_REPO_2014                      : Integer = 13; // 27 Проценты по РЕПО 2014 (507-508, 511-514)
const REGISTRS_OPEN_SHORT_POSITIONS           : Integer = 14; // 8  Регистр открытых коротких позиций (720)
const REPORT_EXCHANGE_DIFFERENCES             : Integer = 15; // 11 Отчет по курсовым разницам
const REPORT_NOTIF_CONTR_DEAL                 : Integer = 16; // 25 Уведомление о контролируемых сделках
const SHEET_CALC_DEFER_ACTIV                  : Integer = 17; // 26 Ведомость расчета отложенных НО и НА
const REGISTRS_OIN                            : Integer = 18; // 32 Регистры по ОИН (0292, 0491, 0692, 0791)
// Регистры резервов
const REGISTRS_RESERVES                       : Integer = 19; // 9  Регистры резервов (801,802)
const BRIEF_REGISTRS_RESERVES                 : Integer = 20; // 10 Краткие регистры резервов (901,902)
const REGISTRS_CHARGE_RESERV                  : Integer = 21; // 24 Расходы по формированию резервов под обесценение ц/б
// Проверочные регистры
const BALANCE_SECURS_PORTFOLIO                : Integer = 22; // 12 Остаток ценных бумаг в портфеле
const PLACEMENT_SECURS                        : Integer = 23; // 13 Размещение ценных бумаг
const PAYMENTS_BALANCE_SECURS_BANKS_PORTFOLIO : Integer = 24; // 14 Выплаты по остаткам ц/б в портфеле банка
const PAYMENTS_ISSUER_BUY_SALE                : Integer = 25; // 15 Выплаты от эмитента по сделкам покупки-продажи
const PAYMENTS_DEALS_REPO                     : Integer = 26; // 16 Выплаты по сделкам РЕПО
const BINDING_DEALS_TAXES                     : Integer = 27; // 17 Связывание сделок в налоговом учете
const CURR_REAPPRAISAL_REST_PLACEMENT         : Integer = 28; // 18 Валютная переоценка ц/б в остатке и размещении
const MOVING_REPO_BASKET                      : Integer = 29; // 19 Движение ц/б по сделкам РЕПО с корзиной ц/б
const TAX_PORTFOLIO_ONDATE                    : Integer = 30; // 20 Налоговый портфель на дату
const REESTR_PAYMENTS_REPO_BASKET             : Integer = 31; // 21 Реестр выплат от эмитента по сделкам РЕПО с корзиной ц/б
const REPORT_NOIN                             : Integer = 32; // 32 Номинал по облигациям с ИН
// Регистры налогового учета
const REGISTRS_OWN_BILLS                      : Integer = 33; // 33 Отчет Регистр по собственным векселям
const REGISTRS_VA_BILLS                       : Integer = 34; // 34 Отчет Регистр по учтеннным векселям
const REGISTRS_OWN_BONDS                      : Integer = 35; // 35 Отчет Регистр по собственным облигациям
const TAXACCSECUR                             : Integer = 36; // 36 Налоговый учет эмиссионных ценных бумаг


// Дополнительные параметры фильтрации финансовых инструментов
class CTxAvoirIssFiltrParm
  var ByReportForm:Bool; // Использовать для ниверсальной формы отчетов

  var FIID:Integer;
  var AvoirKind:Integer;
  var Issuer:Integer;
  var FaceValue:Money;
  var FaceValueFI:Integer;
  var IsGeneralized:Bool;
  var GeneralizedFIID:Integer;
  var IsAdditional:Bool;
  var SPIsClosed:Bool;
  var IssuedCondUsed:Bool;
  var Issued:Date;
  var IsEmissive:Bool;
  var IsKSU:Bool;
  var IsBasket:Bool;
  var IsIndexNominal:Bool;
  var DealDateCondUsed:Bool;
  var DealDate:Date;
  var FIKinds:TArray;
  var Settlement_Code:Integer;
  var DrawingDateBegin:Date;

  var ReportNumber  :  Integer;
  var ReportKindList = TArray();//: TArray of TWsNamealg // Список подвидов регистра
end;

private macro GetAddWhereCommon(
  AddParm//:CTxAvoirIssFiltrParm  // Дополнительные параметры фильтрации
)
  var StrSqlAddWhere:String = " 1 = 1 ";

  if( ValType( AddParm ) != V_UNDEF )
    if( ValType( AddParm.FIID ) == V_INTEGER )
      StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_FIID <> " + String( AddParm.FIID ) + " ";
    end;

    if( ( ValType( AddParm.AvoirKind ) == V_INTEGER )
    and ( AddParm.AvoirKind > FIKIND_ALLAVOIRISS ) )
      StrSqlAddWhere = StrSqlAddWhere + " AND RSB_FIInstr.FI_AvrKindsEQ (" + String( FIKIND_AVOIRISS ) + ", " + String( AddParm.AvoirKind ) + ", fi.t_AvoirKind) > 0 ";
    end;

    if( ( ValType( AddParm.Issuer ) == V_INTEGER )
    and ( AddParm.Issuer > 0 ) )
      StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_Issuer = " + String( AddParm.Issuer ) + " ";
    end;

    if( ValType( AddParm.FaceValue ) == V_NUMERIC )
      StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_FaceValue = " + ЧислоCЗаданнойТочностью( AddParm.FaceValue, AVOIRISS_SUM_PRECISION ) + " ";
    end;

    if( ValType( AddParm.FaceValueFI ) == V_INTEGER )
      StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_FaceValueFI = " + String( AddParm.FaceValueFI ) + " ";
    end;

    if( ValType( AddParm.IsGeneralized ) == V_BOOL )
      if( AddParm.IsGeneralized )
        StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_IsGeneralized = CHR (88) ";
      else
        StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_IsGeneralized = CHR (0) ";
      end;
    end;

    if( ( ValType(AddParm.GeneralizedFIID ) != V_UNDEF ) and ( AddParm.GeneralizedFIID > ALLFININSTR ) )
      StrSqlAddWhere = StrSqlAddWhere +
          " AND fi.t_GeneralizedFIID = " + string( AddParm.GeneralizedFIID ) + " ";
    end;

    if( ValType( AddParm.IsAdditional ) == V_BOOL )
      if( AddParm.IsAdditional )
        StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_IsAdditional = CHR (88) ";
      else
        StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_IsAdditional = CHR (0) ";
      end;
    end;

    if( ValType( AddParm.SPIsClosed ) == V_BOOL )
      if( AddParm.SPIsClosed )
        StrSqlAddWhere = StrSqlAddWhere + " AND (fi.t_FIID IN ( SELECT fi.t_FIID FROM dfininstr_dbt fi ,davoiriss_dbt av "
                                        + " WHERE av.t_FIID = fi.t_FIID AND av.t_SPIsClosed = CHR( 88 ) ) ) " ;
      else
        StrSqlAddWhere = StrSqlAddWhere + " AND (fi.t_FIID IN ( SELECT fi.t_FIID FROM dfininstr_dbt fi ,davoiriss_dbt av "
                                        + " WHERE av.t_FIID = fi.t_FIID AND av.t_SPIsClosed = CHR( 0 ) ";
        //получение статуса финансового инструмента на заданную дату (закрыт или нет)
        if( ValType( AddParm.DealDateCondUsed ) == V_BOOL )
          if( AddParm.DealDateCondUsed )
            StrSqlAddWhere = StrSqlAddWhere +
              " AND ( "
              + " CASE " 
                 + " WHEN ((av.t_BegPlacementDate != TO_DATE('01-01-0001', 'DD-MM-YYYY')) AND (av.t_BegPlacementDate < fi.t_Issued)) "
                    + " THEN "
                       + " CASE "
                          + " WHEN ( (TO_DATE( '" + string(AddParm.DealDate) + "' , 'DD-MM-YYYY') < av.t_BegPlacementDate) OR (fi.t_ExpiryDate != TO_DATE('01-01-0001', 'DD-MM-YYYY') AND TO_DATE( '" + string(AddParm.DealDate) + "' , 'DD-MM-YYYY') > fi.t_ExpiryDate)) "
                             + " THEN " + string(FI_STATE_CLOSE)
                          + " ELSE " + string(FI_STATE_UNDEFINE)
                       + " END "
                    + " ELSE "
                       + " CASE "
                          + " WHEN ((TO_DATE( '" + string(AddParm.DealDate) + "' , 'DD-MM-YYYY') < fi.t_Issued) OR (fi.t_ExpiryDate != TO_DATE('01-01-0001', 'DD-MM-YYYY') AND TO_DATE( '" + string(AddParm.DealDate) + "' , 'DD-MM-YYYY') > fi.t_ExpiryDate)) "
                             + " THEN " + string(FI_STATE_CLOSE)
                          + " ELSE " + string(FI_STATE_UNDEFINE)
                       + " END " 
              + " END "
              + " ) != " + string(FI_STATE_CLOSE);
          end;
        end;

        StrSqlAddWhere = StrSqlAddWhere +  " ) )";
      end;
    end;

    if( ( ValType( AddParm.IssuedCondUsed ) == V_BOOL )
    and ( AddParm.IssuedCondUsed ) )
      if( ValType( AddParm.Issued ) == V_DATE )
        if( AddParm.Issued != Date( 0, 0, 0 ) )
          StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_Issued <= TO_DATE ('" + String( AddParm.Issued ) + "', 'DD.MM.YYYY') ";
        else
          StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_Issued = TO_DATE ('01.01.0001', 'DD.MM.YYYY') ";
        end;
      end;
    end;

    if ( ValType(AddParm.IsEmissive ) == V_BOOL ) 
      StrSqlAddWhere = StrSqlAddWhere +
          "  AND RSB_FIINSTR.FI_IsSecurEmissive( fi.t_FIID ) = " + IIF(AddParm.IsEmissive, 1, 0) +  "  " ;
    end;

    if ( ValType(AddParm.IsKSU ) == V_BOOL ) 
      StrSqlAddWhere = StrSqlAddWhere +
          "  AND RSB_FIINSTR.FI_IsKSU( fi.t_FIID ) = " + IIF(AddParm.IsKSU, 1, 0) +  "  " ;
    end;

    if ( ValType(AddParm.IsBasket ) == V_BOOL ) 
      StrSqlAddWhere = StrSqlAddWhere +
          "  AND RSB_FIINSTR.FI_IsBasket( fi.t_FIID ) = " + IIF(AddParm.IsBasket, 1, 0) +  "  " ;
    end;


    if( ( ValType( AddParm.FIKinds ) == 19/*TARRAY*/ )
    and ( AddParm.FIKinds.Size > 0 ) )
      StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_FI_KIND IN (" + String( AddParm.FIKinds[0] );

      var i:Integer = 1;
      while( i < AddParm.FIKinds.Size )
        StrSqlAddWhere = StrSqlAddWhere + "," + String( AddParm.FIKinds[i] );
        i = i + 1;
      end;

      StrSqlAddWhere = StrSqlAddWhere + ") ";
    end;

    if( ( ValType( AddParm.Settlement_Code ) == V_INTEGER )
    and ( AddParm.Settlement_Code > ALLFININSTR ) )
      StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_Settlement_Code = " + String( AddParm.Settlement_Code ) + " ";
    end;

    if( ValType( AddParm.IsIndexNominal ) == V_BOOL )
      if( AddParm.IsIndexNominal )
        StrSqlAddWhere = StrSqlAddWhere + " AND (fi.t_FIID IN ( SELECT fi1.t_FIID FROM dfininstr_dbt fi1 ,davoiriss_dbt av1 "
                                        + " WHERE av1.t_FIID = fi1.t_FIID AND av1.t_IndexNom = CHR( 88 ) ) ) " ;
      else
        StrSqlAddWhere = StrSqlAddWhere + " AND (fi.t_FIID IN ( SELECT fi1.t_FIID FROM dfininstr_dbt fi1 ,davoiriss_dbt av1 "
                                        + " WHERE av1.t_FIID = fi1.t_FIID AND av1.t_IndexNom = CHR( 0 ) ) ) " ;
      end;
    end;

    if((ValType( AddParm.DrawingDateBegin ) == V_DATE) and (AddParm.DrawingDateBegin != ZeroDate))
      StrSqlAddWhere = StrSqlAddWhere + " AND fi.t_DrawingDate > " + GetSQLDate(AddParm.DrawingDateBegin);
    end;

  end;

  return StrSqlAddWhere;
end;

private macro GetAddWhereReportForm(
  AddParm//:CTxAvoirIssFiltrParm // Дополнительные параметры фильтрации
)
  var Stat:Integer = -1;
  var MacroName:String = "";
  var AddWhereCond:String = " 1 = 1 ";
  var WhereCond:String = " 1 = 1 ";
  var RepSubKind:Integer = 0;

  if(AddParm.ReportNumber == REGISTRS_REALIZATION)                     // 1  Регистры реализации (201-206)
    MacroName = "RealizReg_Form.mac";
  elif(AddParm.ReportNumber == REGISTRS_URGENT_DEALS)                  // 2  Регистры срочных сделок (301-306)
    MacroName = "FuturesReg_Form.mac";
  elif(AddParm.ReportNumber == REGISTRS_CLOSED_SHORT_POSITIONS)        // 6  Регистры закрытых коротких позиций (601-606)
    MacroName = "ShRep_Reg_Form.mac";
  elif(AddParm.ReportNumber == REGISTRS_REALIZATION_2014)              // 29 Регистры реализации (211)
    MacroName = "RealizReg_Form_2014.mac";

  elif(AddParm.ReportNumber == REGISTRS_CHARGES_SECURS_BALANCE)        // 3  Регистры начислений по ц/б в остатке (401-404)
    MacroName = "Inc_Reg_Form.mac";
  elif(AddParm.ReportNumber == REGISTRS_CHARGES_SECURS_REPO)           // 4  Регистры начислений по ц/б в РЕПО (405-408)
    MacroName = "IncR_Reg_Form.mac";
  elif(AddParm.ReportNumber == REGISTRS_CHARGES_SHORT_POSITIONS)       // 7  Регистры начислений по коротким позициям (701-704)
    MacroName = "OutlaySPReg_Form.mac";
  elif(AddParm.ReportNumber == REGISTRS_CHARGES_2014)                  // 28 Регистр по начислению 2014 (411)
    MacroName = "IncC_Reg_Form.mac";
  elif(AddParm.ReportNumber == REGISTRS_OUTLAY_SP_REG_2014)            // 31 Регистр по начислению расхода по коротким позициям 2014 (721)
    MacroName = "OutlaySPReg2014_Form.mac";

  elif(AddParm.ReportNumber == REGISTRS_REPO)                          // 5  Проценты по РЕПО (501-506, 508)
    MacroName = "RepoRegister_Form.mac";
  elif(AddParm.ReportNumber == REGISTRS_REPO_2014)                     // 25 Проценты по РЕПО 2014 (507-508, 511-514)
    MacroName = "RepoRegister_Form_2014.mac";
  elif(AddParm.ReportNumber == REGISTRS_SHREP_REG_2014)                // 30 Регистры коротких позиций 2014 (611-612, 615)
    MacroName = "shrep_reg2014_form.mac";
  elif(AddParm.ReportNumber == REGISTRS_OIN)                           // 32 Регистры по ОИН (0292, 0491, 0692, 0791)
    MacroName = "IndNomBondReg_Report.mac";
  elif(AddParm.ReportNumber == REPORT_EXCHANGE_DIFFERENCES)            // 11 Отчет по курсовым разницам
    MacroName = "CompareSumCup_report.mac";

  elif(AddParm.ReportNumber == REGISTRS_RESERVES)                      // 9  Регистры резервов (801,802)
    MacroName = "ReservReg_Form.mac";
  elif(AddParm.ReportNumber == BRIEF_REGISTRS_RESERVES)                // 10 Краткие регистры резервов (901,902)
    MacroName = "ShortResReg_Form.mac";
  elif(AddParm.ReportNumber == TAX_PORTFOLIO_ONDATE)                   // 20 Налоговый портфель на дату
    MacroName = "TaxPortOnDate_Form.mac";
  elif(AddParm.ReportNumber == REPORT_NOIN)                            // 33 Номинал по облигациям с ИН
    MacroName = "IndBondNominal_Report.mac";
  elif(AddParm.ReportNumber == REGISTRS_OWN_BILLS)                     // Отчет Регистр по собственным векселям    
    MacroName = "vsregister.mac";    
  elif(AddParm.ReportNumber == REGISTRS_VA_BILLS)                      // Отчет Регистр по учтенным векселям    
    MacroName = "varegister.mac";    
  elif(AddParm.ReportNumber == REGISTRS_OWN_BONDS)                     // Отчет Регистр по собственным облигациям
    MacroName = "ownbondsregister.mac";
  end;

  if(MacroName != "")
    if((AddParm.ReportKindList != null) and (AddParm.ReportKindList.Size == 1))
      RepSubKind = AddParm.ReportKindList[0].NumberAlg;
    end;
    WhereCond = ExecMacroFile(MacroName, "GetAvoirListAddWhere", RepSubKind);

    if(WhereCond != " ")
      AddWhereCond =
        " fi.t_FIID IN "
        + " ( "
        + " SELECT "
          + " t.t_FIID "
        + " FROM "
          + " dfininstr_dbt t "
         + " ,davoiriss_dbt av "
         + " ,davrkinds_dbt AKind "
        + " WHERE "
              + " av.t_FIID = t.t_FIID ";

      if( (AddParm.ReportNumber != REGISTRS_SHREP_REG_2014) // 30
      AND (AddParm.ReportNumber != REGISTRS_OUTLAY_SP_REG_2014)   // 31
      AND (AddParm.ReportNumber != REGISTRS_OIN)  // 32
      AND (AddParm.ReportNumber != REPORT_NOIN) )   // 33
        AddWhereCond = AddWhereCond
              + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind) "
              + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind AND AKind.t_IsIndividual != 'X') " 
              + " AND ( AKind.t_IsEmissive != 'X' OR ( t.t_IsGeneralized = 'X' OR t.t_GeneralizedFIID < 0 ) )";
      end;
      
      if((AddParm.ReportNumber == REGISTRS_RESERVES) or (AddParm.ReportNumber == BRIEF_REGISTRS_RESERVES))
          AddWhereCond = AddWhereCond + "AND ((SELECT COUNT(1)" +
                 "          FROM dobjatcor_dbt objatcor" +
                 "         WHERE objatcor.t_ObjectType = 12" +
                 "           AND objatcor.t_GroupID = 57" + //Включать ц/б в расчётрезервов для НУ
                 "           AND objatcor.t_AttrID = 1" +  //Да
                 "           AND objatcor.t_Object = LPAD(fi.t_FIID, 10, '0' )) > 0";

         if( SC_IncludeSecurInCalcOfReservTax() == true )
            AddWhereCond = AddWhereCond + 
                "         OR (SELECT COUNT(1)" +
                "          FROM dobjatcor_dbt objatcor" +
                "         WHERE objatcor.t_ObjectType = 12" +
                "           AND objatcor.t_GroupID = 57" +
                "           AND objatcor.t_Object = LPAD(fi.t_FIID, 10, '0' )) = 0";
         end;
         WhereCond = WhereCond + " )";
      end;
      
      AddWhereCond = AddWhereCond + WhereCond
        + ") ";
    end;
  end;

  return AddWhereCond;
end;

macro TxGetAddWhere(
  AddParm//:CTxAvoirIssFiltrParm  // Дополнительные параметры фильтрации
):String
  var queryWhere:string = "1=1";

  InitError();

  if( AddParm != null )
     if( AddParm.ByReportForm )
        queryWhere = GetAddWhereReportForm( AddParm );
     else
        queryWhere = GetAddWhereCommon( AddParm );
     end;
  end;
  
  return queryWhere;
end;

