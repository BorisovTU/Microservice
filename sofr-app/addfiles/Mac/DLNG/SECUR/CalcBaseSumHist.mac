/**                                                                                                             
 @file CalcBaseSumHist.mac                                                                                           
 @brief Скроллинг истории запусков сервисной операции Расчета базовой суммы для периодических комиссий и связанный с ним функционал

 #menu 
  - БО ЦБ\Сервисные операции\Расчет базовой суммы для периодических комиссий\F6                                                                                                                                                  
                                                                                                                
 # tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:GUI                                                                                                
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2023.12.27 |Жиц М.В.       |DEF-59219           |Добавление столбца "ID операции удаления" в скроллинг базовых сумм                                                                                                                                                
 |2023.12.12 |Жиц М.В.       |BIQ-14887           |Создание макроса скроллинга                                                                                                                                                 
*/ 

/*Виды действий*/
CONST ACTION_CALC   = 1, /*расчет*/
      ACTION_RECALC = 2, /*пересчет*/
      ACTION_DELETE = 3, /*удаление*/
      ACTION_INFO   = 4, /*информационный расчет*/
      ACTION_CHECK  = 5; /*проверка СЧА при подключении опции*/

/**
 @brief Вывод протокола по операции
 @param[in] OperID идентификатор операции
*/
PRIVATE MACRO ShowLog(OperID:integer)
  var query, cmdLog, rsLog;
  FILE ReportOutFile() txt write;
  var ReportFileName:string;
  var strLog:string = "";
  var ind:integer = 1, deltaind:integer = 1024, idec:integer = 0;

  ReportFileName = GetTxtFileName("protocol");
  if(not Open(ReportOutFile, ReportFileName))
    msgbox("Ошибка при открытии файла отчета|" + ReportFileName);
  end;

  while(ind > 0)
    query = "select nvl(dbms_lob.substr(basesumop.t_log, ?, ?),' ') t_log " +
            "  from ddl_basesumop_dbt basesumop " +
            " where basesumop.t_id = ? ";
    cmdLog = DL_RsdCommand(query);
    cmdLog.AddParam(deltaind);
    cmdLog.AddParam(1 + deltaind * (ind - 1));
    cmdLog.AddParam(OperID);
    rsLog = cmdLog.Execute();
    if((rsLog.moveNext()) and (rsLog.value("t_log") != " "))
      strLog = strLog + rsLog.value("t_log");
      while(index(strLog, "\n") > 0)
        idec = index(strLog, "\n");
        insert(ReportOutFile, substr(strLog, 1, idec-1));
        strLog = substr(strLog, idec+1);
      end;
      ind = ind + 1;
    else
      if(strlen(strLog) > 0)
        insert(ReportOutFile, strLog);
      end;
      ind = 0;
    end;
  end;
  Close(ReportOutFile);
  ViewFile(ReportFileName);
END;

/**
 @brief Формирование скроллинга расшифровки с детализацией базовой суммы
 @param[in] BaseSumID идентификатор базовой суммы
*/                    
PRIVATE MACRO ShowBaseSumValHist(BaseSumID:integer)
  var cmdQuery, cmdScroll, rsScroll, Ar;

  /**
   @brief Обработчик нажатия клавиш
   @param[in] rs объект скроллинга
   @param[in] cmd код действия
   @param[in] id идентификатор поля
   @param[in] key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO eRunConvHistProcessDialog(rs, cmd, id, key)
    if(cmd == DLG_INIT)
    elif(cmd == DLG_KEY)
      if(key == 27/*KEY_ESC*/)       
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  END;

  cmdQuery = " select bs.t_Date, val.t_Amount, val.t_RateDate, val.t_Rate, val.t_NKD, val.t_Cost, val.t_CbRate, val.t_CostRub, " +
             "        case when val.t_Kind = 1 then 'Денежные средства' " +
             "             when val.t_Kind = 2 then 'Ценные бумаги' " +
             "             when val.t_Kind = 3 then 'Требования к клиенту' " +
             "             when val.t_Kind = 4 then 'Задолженность клиента' " +
             "             else '' end t_Kind, " +
             "        case when val.t_ObjKind = 1 then nvl((select acc.t_Account from daccount_dbt acc where acc.t_AccountID = val.t_ObjectID), '') " +
             "             when val.t_ObjKind = 2 then nvl((select fin.t_Name from dfininstr_dbt fin where fin.t_FIID = val.t_ObjectID), '') " +
             "             else '' end t_Name_Acc, " +
             "        case when val.t_ObjKind = 1 then nvl((select fin.t_CCY from dfininstr_dbt fin where t_FIID = val.t_CostCur), '') " +
             "             when val.t_ObjKind = 2 then nvl((select avoir.t_ISIN from davoiriss_dbt avoir where avoir.t_FIID = val.t_ObjectID), '') " +
             "             else '' end t_ISIN_ISO, " +
             "        nvl((select rt.t_TypeName from dratetype_dbt rt where rt.t_Type = nvl(ratedef.t_Type, -1)), '') t_RateType, " +
             "        nvl((select pt.t_ShortName from dparty_dbt pt where pt.t_PartyID = nvl(ratedef.t_Market_Place, -1)), '') t_RateMarket, " +
             "        nvl((select fin.t_CCY from dfininstr_dbt fin where t_FIID = val.t_CostCur), '') t_Currency, " +
             "        case when val.t_MarketID = 0  then 'ЕДП' " +
             "             when val.t_MarketID = -1 then 'Внебиржа' " +
             "             else nvl((select pt.t_ShortName from dparty_dbt pt where pt.t_PartyID = val.t_MarketID), '') end t_Market " +
             "   from ddl_basesum_dbt bs " + 
             "   join ddl_basesumval_dbt val " +
             "     on val.t_BaseSumID = bs.t_ID " +
             "   left join dratedef_dbt ratedef " + 
             "     on ratedef.t_RateID = val.t_RateID " +
             "  where bs.t_ID = ? " +
             "order by t_Kind asc, t_Name_Acc asc ";

  cmdScroll = RsdCommand(cmdQuery);
  cmdScroll.AddParam("", RSDBP_IN, BaseSumID);

  rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
  Ar = MakeArray("t_Date",       "Дата",               10, 2, -1, 0,
                 "t_Kind",       "Вид актива",         18, 2, -1, 0,  
                 "t_Name_Acc",   "Наименование/Счет",  30, 2, -1, 0,
                 "t_ISIN_ISO",   "ISIN/ISO",           10, 2, -1, 0,
                 "t_Market",     "Площадка",            8, 2, -1, 0,
                 "t_Amount",     "Количество актива",  15, 2,  6, 0,
                 "t_RateType",   "Вид котировки",      12, 2, -1, 0,
                 "t_RateDate",   "Дата котировки",     12, 2, -1, 0,
                 "t_RateMarket", "Площадка котировки", 15, 2, -1, 0,
                 "t_Rate",       "Котировка",          10, 2,  2, 0, 
                 "t_NKD",        "НКД",                10, 2,  4, 0, 
                 "t_Cost",       "Стоимость",          15, 2,  4, 0, 
                 "t_Currency",   "Валюта",              8, 2, -1, 0,
                 "t_CbRate",     "Курс ЦБ РФ",         10, 2,  4, 0,
                 "t_CostRub",    "Стоимость в руб.",   15, 2,  2, 0
                );
  while(RunScroll(rsScroll, ar.size/6, Ar, "BaseSumValHist", "eRunConvHistProcessDialog", "Расшифровки базовой суммы", "~Esc~ Выход", true, 15, 15))
  end;
END;

/**
 @brief Формирование скроллинга рассчитанных базовых сумм по операции Расчета базовой суммы для периодических комиссий
 @param[in] OperID идентификатор операции
*/                    
PRIVATE MACRO ShowBaseSumHist(OperID:integer)
  var cmdQuery, cmdScroll, rsScroll, Ar;

  /**
   @brief Обработчик нажатия клавиш
   @param[in] rs объект скроллинга
   @param[in] cmd код действия
   @param[in] id идентификатор поля
   @param[in] key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO eRunConvHistProcessDialog(rs, cmd, id, key)
    if(cmd == DLG_INIT)
    elif(cmd == DLG_KEY)
      if(key == 320/*KEY_F6*/)
        ShowBaseSumValHist(int(rs.value("t_BaseSumID")));
        return CM_SELECT;
      elif(key == 27/*KEY_ESC*/)       
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  END;

  cmdQuery = " select bs.t_ID t_BaseSumID, bs.t_Date, sfcom.t_Code t_ComCode, " +
             "        case when bs.t_LnkOpID > -1 then to_char(bs.t_LnkOpID) else '' end t_DelOpID, " +
             "        RSI_RSBPARTY.GetPartyCode(bs.t_ClientID, " + PTCK_CONTR + ") t_ClntCode, " +
             "        nvl((select t_ShortName from dparty_dbt where t_PartyID = bs.t_ClientID), '') t_ClntName, " +
             "        nvl((select sf.t_Number from ddlcontr_dbt dl, dsfcontr_dbt sf where dl.t_DlContrID = bs.t_DlContrID and sf.t_ID = dl.t_SfContrID), '') t_Contract, " + 
             "        nvl((select fin.t_CCY from dfininstr_dbt fin where t_FIID = bs.t_Currency), '') t_Currency, "
             "        bs.t_Sum, to_char(bs.t_ComRate)||'%' t_ComRate, bs.t_ComSum, bs.t_LnkOpID, "
             "        case when bs.t_State = CHR(0) then 'Актуальное значение' " +    
             "             when bs.t_State = 'D'    then 'Удаленное значение' " +                              
             "             when bs.t_State = 'I'    then 'Информационный расчет' " +  
             "             when bs.t_State = 'N'    then 'Проверка при подключении опции' " + 
             "             when bs.t_State = 'E'    then 'Ошибка расчета Базовой суммы' " + 
             "             else '' end t_State "                                                      
             "   from ddl_basesum_dbt bs, dsfcomiss_dbt sfcom " +
             "  where (bs.t_OpID = ? or bs.t_LnkOpID = ?) " +
             "    and sfcom.t_FeeType = bs.t_FeeType " +
             "    and sfcom.t_Number = bs.t_ComNumber " +
             "order by t_Contract asc, t_Date asc, t_LnkOpID desc ";

  cmdScroll = RsdCommand(cmdQuery);
  cmdScroll.AddParam("", RSDBP_IN, OperID);
  cmdScroll.AddParam("", RSDBP_IN, OperID);

  rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
  Ar = MakeArray("t_Date",      "Дата расчета",         10, 2, -1, 0,
                 "t_ComCode",   "Комиссия",             12, 2, -1, 0,
                 "t_ClntCode",  "Код клиента",          12, 2, -1, 0,
                 "t_ClntName",  "Наименование клиента", 20, 2, -1, 0,
                 "t_Contract",  "Номер договора",       15, 2, -1, 0,
                 "t_Sum",       "Базовая сумма",        15, 2,  2, 0, 
                 "t_Currency",  "Валюта",               10, 2, -1, 0, 
                 "t_ComRate",   "Ставка",               10, 2, -1, 0, 
                 "t_ComSum",    "Сумма комиссии",       15, 2,  2, 0,
                 "t_State",     "Статус записи",        25, 2, -1, 0,
                 "t_BaseSumID", "ID",                    5, 2, -1, 0,
                 "t_DelOpID",   "ID операции удаления", 17, 2, -1, 0
                );
  while(RunScroll(rsScroll, ar.size/6, Ar, "BaseSumHist", "eRunConvHistProcessDialog", "Рассчитанные базовые суммы по операции", "~Esc~ Выход ~F6~ Просмотр расшифровки базовой суммы", true, 10, 10))
  end;
END;

/**
 @brief Формирование скроллинга истории запусков операции Расчета базовой суммы для периодических комиссий
*/                    
MACRO ShowBaseSumOpHist()
  var cmdQuery, cmdScroll, rsScroll, Ar;

  /**
   @brief Обработчик нажатия клавиш
   @param[in] rs объект скроллинга
   @param[in] cmd код действия
   @param[in] id идентификатор поля
   @param[in] key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO eRunConvHistProcessDialog(rs, cmd, id, key)
    if(cmd == DLG_INIT)
    elif(cmd == DLG_KEY)
      if(key == KEY_ENTER)
        ShowLog(int(rs.value("t_OpID")));
        return CM_SELECT;
      elif(key == 320/*KEY_F6*/)
        ShowBaseSumHist(int(rs.value("t_OpID")));
        return CM_SELECT;
      elif(key == 27/*KEY_ESC*/)       
        return CM_CANCEL;
      end;
    end;
    return CM_DEFAULT;
  END;

  cmdQuery = " select op.t_ID t_OpID, op.t_BegDate, op.t_EndDate, sfcom.t_Code t_ComCode, " +
             "        case when op.t_ClientID > -1 then RSI_RSBPARTY.GetPartyCode(op.t_ClientID, " + PTCK_CONTR + ") " +
             "             else 'Все' end t_ClntCode, " +
             "        case when op.t_ClientID > -1 then nvl((select t_ShortName from dparty_dbt where t_PartyID = op.t_ClientID), '') " +
             "             else 'Все' end t_ClntName, " +
             "        case when op.t_DlContrID > -1 then nvl((select sf.t_Number from ddlcontr_dbt dl, dsfcontr_dbt sf where dl.t_DlContrID = op.t_DlContrID and sf.t_ID = dl.t_SfContrID), '') " +
             "             else 'Все' end t_Contract, " +  
             "        case when op.t_Action = " + ACTION_CALC   + " then 'Расчет' " +    
             "             when op.t_Action = " + ACTION_RECALC + " then 'Пересчет' " +                              
             "             when op.t_Action = " + ACTION_DELETE + " then 'Удаление' " + 
             "             when op.t_Action = " + ACTION_INFO   + " then 'Информационный расчет' " +  
             "             when op.t_Action = " + ACTION_CHECK  + " then 'Проверка при подключении' " +    
             "             else '' end t_Action, "                                                      
             "        op.t_PrintLog, op.t_Oper, " +
             "        to_char(op.t_SysDate,'dd.mm.yyyy hh24:mi:ss') t_SysDate " +
             "   from ddl_basesumop_dbt op, dsfcomiss_dbt sfcom " +
             "  where sfcom.t_FeeType = op.t_FeeType " +
             "    and sfcom.t_Number = op.t_ComNumber " +
             "order by t_ID desc ";

  cmdScroll = RsdCommand(cmdQuery);
  rsScroll = RSDRecordset(cmdScroll, RSDVAL_CLIENT, RSDVAL_STATIC);
  Ar = MakeArray("t_BegDate",  "Дата начала",          10, 2, -1, 0,
                 "t_EndDate",  "Дата окончания",       10, 2, -1, 0,  
                 "t_ComCode",  "Комиссия",             12, 2, -1, 0,
                 "t_ClntCode", "Код клиента",          12, 2, -1, 0,
                 "t_ClntName", "Наименование клиента", 20, 2, -1, 0,
                 "t_Contract", "Номер договора",       15, 2, -1, 0,
                 "t_Action",   "Вид действия",         20, 2, -1, 0, 
                 "t_PrintLog", "Расширенный протокол", 10, 2, -1, 0, 
                 "t_Oper",     "Операционист",         10, 2, -1, 0,                                                                                            
                 "t_SysDate",  "Системная дата",       20, 2, -1, 0,
                 "t_OpID",     "ID",                    5, 2, -1, 0
                );
  while(RunScroll(rsScroll, ar.size/6, Ar, "BaseSumOpHist", "eRunConvHistProcessDialog", "История запусков", "~Esc~ Выход ~Enter~ Просмотр протокола по операции ~F6~ Просмотр рассчитанных базовых сумм по операции", true))
  end;
END;
