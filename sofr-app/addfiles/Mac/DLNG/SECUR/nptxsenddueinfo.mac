import rsd, secinter, "or_exl_h.mac", "u_common_email_utils.mac";

PRIVATE CONST OBJTYPE_DLCONTRMSG_NPTXDUEINFO = 505;   // Значение справочника "Вид сообщения ДБО"(1143)->"Сообщение клиенту о недоплате/переплате НДФЛ"

private const firstrow = 10;

private var outdir, repfilename, repfile, r = firstrow-1, succnt = 0, miscnt = 0, errcnt = 0, totalcnt = 0;
private var y, mon, d, h, m, s;
private var v_email_processor;
private var dlcontrmsg = TBFile("dlcontrmsg.dbt", "W");

file protocol() txt write;


GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\WORKDIR", V_STRING, outdir );
DateSplit(Date(), d, mon, y);
TimeSplit(Time(), h, m, s); 

Open(protocol, outdir + "\\" + "КорректНДФЛ_Рассылка_" +string(y:4:o,".",mon:2:o,".",d:2:o) + "_" + string(h:2:o,".",m:2:o,".",s:2:o) + ".log");

private macro ProcessRow()
  var client, clientId, clientIdLen, clientName, clientNameLen, IIS, contrNumber, email, isSent, due, head, body, statMes;

  private macro GenMailHeadDue
    return "Информационное письмо о недоплате НДФЛ по договору " + contrNumber;
  end;

  private macro GenMailHeadOverdue
    return "Информационное письмо о переплате НДФЛ по договору " + contrNumber;
  end;

  private macro GenMailBodyDue
    return "   Уважаемый(мая) "+ clientName +", за 2022 налоговый год у Вас обнаружена недоплата по НДФЛ по договору N " + contrNumber + " в размере " + string(due:0:2)+" руб., в связи с чем Вам необходимо в срок до 20 января 2023 обеспечить указанную сумму на своем брокерском счете (биржевой рынок). При отсутствии средств для удержания налога в указанный срок, Банк направит в налоговый орган Сообщение о невозможности удержания суммы налога.\n"
           "В этом случае уплата налога осуществляется Вами самостоятельно на основании уведомления, полученного от налогового органа. ";
  end;

  private macro GenMailBodyOverDue
    return "   Уважаемый(мая) "+ clientName +", за 2022 налоговый год у Вас обнаружена переплата по НДФЛ по договору N " + contrNumber + " в размере " + string(-due:0:2) + " руб., в связи с чем Вам необходимо подать заявление на возврат излишне удержанного НДФЛ в Банк.\n"
           "Заявление можно скачать по ссылке: https://www.rshb.ru/download-file/553493/, заполнить, подписать и направить скан-копию заявления на электронный адрес: broker@rshb.ru , в теме письма указать \"Возврат НДФЛ, ФИО, номер соглашения\"\n"
           "Возврат излишне удержанного НДФЛ налоговым агентом осуществляется в течение 3-х месяцев с момента получения заявления. Обращаем внимание, что в случае, если Вы уже направляли заявление на возврат НДФЛ - повторно направлять заявление не требуется. ";
  end;

  var cmd, rs;

  client = repfile.GetValue(r,0);

  if(client == "")
    return false;
  end;

  totalcnt = totalcnt + 1;

  due = double(repfile.GetVariant(r,17));

  if (due == 0)
    miscnt = miscnt + 1;
    return true;
  end;

  clientIdLen = StrBrk(client, " ") - 1;
  clientId = int(SubStr(client, 1, clientIdLen));
  if(SubStr(client, StrLen(client)-3) == " ИИС")
    clientNameLen = StrLen(client) - (clientIdLen + 2 + 4);
    clientName = SubStr(client, clientIdLen + 2, clientNameLen);
    IIS = true;
  else
    clientName = SubStr(client, clientIdLen + 2);
    IIS = false;
  end;

  cmd = RsdCommand(" SELECT sfcontr.t_Number, sfcontr.t_DateClose, dlcontr.t_DlContrId, dlcontr.t_Email, dlcontr.t_IIS, "
                 + "        NVL( (SELECT 1 FROM ddlcontrmsg_dbt msg WHERE msg.t_DlContrId = dlcontr.t_DlContrId AND msg.t_Kind = " + OBJTYPE_DLCONTRMSG_NPTXDUEINFO + " AND ROWNUM = 1), 0) t_isSent "
                 + "   FROM ddlcontr_dbt dlcontr, dsfcontr_dbt sfcontr "
                 + "  WHERE sfcontr.t_Id = dlcontr.t_SfContrId "
                 + "    AND sfcontr.t_PartyId = ? "
    + iif(not IIS, "    AND dlcontr.t_IIS = CHR(0) ", "") +
                   "  ORDER BY " + iif(IIS, "CASE WHEN dlcontr.t_IIS = CHR(88) THEN 1 ELSE 2 END, ", "") + " sfcontr.t_DateBegin DESC ");
  cmd.AddParam("", RSDBP_IN, clientId);
  cmd.NullConversion = true;
  rs = RsdRecordset(cmd);

  if(rs.MoveNext())
    contrNumber = rs.Value("t_Number");
    email = rs.Value("t_Email");
    isSent = iif( rs.Value("t_isSent") == 1, true, false);
  else
    Insert(protocol, "Для клиента " + clientId + " не найден договор обслуживания");
    miscnt = miscnt + 1;
    return true;
  end;

  if(    (rs.Value("t_IIS") == SET_CHAR) AND ((rs.Value("t_DateClose") == ZeroDate) OR (rs.Value("t_DateClose") > Date(1,1,2023)))
      OR (rs.Value("t_IsSent") == 1)
    )
    miscnt = miscnt + 1;
    return true;
  end;

  if(due > 0)
    head = GenMailHeadDue;
    body = GenMailBodyDue;
  else
    head = GenMailHeadOverdue;
    body = GenMailBodyOverdue;
  end;

  v_email_processor = c_email_proc_env();
  v_email_processor.m_add_email_to_list(email);
  v_email_processor.m_add_email_to_bcc_list("Backoffice@rshb.ru");
  v_email_processor.m_set_msg_head(head);
  v_email_processor.m_add_row_to_msg_text(body);
  v_email_processor.m_save_to_submit_synch();
  v_email_processor.m_submit_email_synch();

  if(v_email_processor.m_get_error_status())
    statMes = "Сообщение сформировано, но не отправлено: " + v_email_processor.get_service_msg();
    errcnt = errcnt + 1;
  else
    statMes = "Сообщение отправлено";

    dlcontrmsg.Clear();
    dlcontrmsg.rec.DlContrId = rs.Value("t_DlcontrId");
    dlcontrmsg.rec.Kind = OBJTYPE_DLCONTRMSG_NPTXDUEINFO;
    dlcontrmsg.rec.CreateDate = Date();
    dlcontrmsg.rec.CreateTime = Time();
    dlcontrmsg.Insert();

    succnt = succnt + 1;
  end;

  Insert(protocol, clientId + ";" + clientName + ";" + contrNumber + ";" + statMes);
  return true;
OnError(e)
  errcnt = errcnt + 1;
  Insert(protocol, e.Message,"(Модуль:",e.Module,", строка:",e.Line,")");
  return true;
end;

if (SelectFile(repfilename, "..\\Import\\*.xls*"))
  repfile = CDAOMSExcel();

  repfile.Open(repfilename);
  repfile.SheetChange("Контроль");

  while (ProcessRow())
    r = r + 1;
  end;

  Insert(protocol);
  Insert(protocol, "количество успешно обработанных записей (отправленных сообщений): " + succnt);
  Insert(protocol, "количество ошибок обработки: " + errcnt);
  Insert(protocol, "количество пропущенных (необработанных) записей: " + miscnt);
  Insert(protocol, "всего записей: " + totalcnt);
end;

Close(protocol);
ViewFile(protocol);
exit(1);