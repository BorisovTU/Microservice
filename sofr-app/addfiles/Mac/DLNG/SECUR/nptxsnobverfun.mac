/**
  @file nptxsnobver005.mac
  @brief Шаг инициализации операции технической сверки СНОБ

  #menu
  - БО ЦБ\Налоговый учет\НДФЛ\СНОБ\Операции технической сверки СНОБ

  #tag
  - functional_block: СНОБ
  - code_type: GUI
  - code_type: Operation
  - Сверка
  - Техническая сверка

  #link
  [BIQ-7294. Анализ. Техническая сверка] (https://sdlc.go.rshbank.ru/confluence/pages/viewpage.action?pageId=223940937)

  #changeLog
  |date      |author       |tasks    |note                                                             |
  |----------|-------------|---------|-----------------------------------------------------------------|
  |04.12.2023|Хромеев Д. С.|BOSS-1489|BOSS-48.9 СОФР. Реализация Технической сверки данных. Разработка.|
*/

import BankInter, OprInter, DealsInter, InsCarryDoc, dlquery, likepy, dlmisc, "dlutils.mac", "DLReport_h.mac", "func_lib.mac";

macro SnobverGetOldMessageId(verId)
  var ds = ExecSQLSelect("select t_messageid from dnptxsnobver_dbt where t_id = ?", MakeArray(SQLParam("verId", verId)), false);
  if (ds.MoveNext())
    return SQL_ConvTypeInteger(ds.value("t_messageid"));
  end;
  return 0;
end;

macro HasSnobverWithMessageByDate(checkDate)
  var ds = ExecSQLSelect("select 1 from dnptxsnobver_dbt where t_operdate = ? and t_messageid > 0", MakeArray(SQLParam("checkDate", checkDate)), true);
  return ds.MoveNext();
end;

macro UpdateSnobverStatus(SnobverRec, NewStatus)
  return UpdateFDOnStepByStorProc("DNPTXSNOBVER_DBT", "T_ID",
                                  string(SnobverRec.rec.ID),
                                  makeArray(
                                    DL_UpdateFDStruct(
                                       "T_STATUS",
                                       string(SnobverRec.rec.Status),
                                       string(NewStatus)
                                    )
                                  )
                                 );
end;


//Пока берём старый messageid select-ом, потому что его нету в Си структурах
macro UpdateSnobverMessageId(SnobverRec, NewMessageId)
  return UpdateFDOnStepByStorProc("DNPTXSNOBVER_DBT", "T_ID",
                                  string(SnobverRec.rec.ID),
                                  makeArray(
                                    DL_UpdateFDStruct(
                                       "T_MESSAGEID",
                                       string(SnobverGetOldMessageId(SnobverRec.rec.ID)),
                                       string(NewMessageId)
                                    )
                                  )
                                 );
end;

PRIVATE CLASS (DL_CReportTemplate) VerNOBNDFL_Report(pOperationID)
  private var m_CurrSheetName = "Протокол";
  private var m_OperationID = pOperationID;
  private var m_cnt = 0;

  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран
    SetActiveSheet(m_CurrSheetName);
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  MACRO GetCnt():INTEGER
    return m_cnt;
  END;

  PRIVATE MACRO Create()
    var query, cmd, DataSet;
    var cnt, i;

    RegisterTable( "N1_MainTable", NULL,
                   "N1_ID",  
                   "N1_CODE",
                   "N1_Income",   
                   "N1_Expense", 
                   "N1_NOB_NDR",  
                   "N1_NOB_SKR",
                   "N1_NOB13",
                   "N1_NOB30",   
                   "N1_NOB15", 
                   "N1_NOB18",
                   "N1_NOB20",
                   "N1_NOB22",   
                   "N1_CHECK_NOB", 
                   "N1_NDFL_NDR",  
                   "N1_NDFL_SKR",
                   "N1_NDFL13",
                   "N1_NDFL30",   
                   "N1_NDFL15", 
                   "N1_NDFL18",  
                   "N1_NDFL20",
                   "N1_NDFL22",
                   "N1_CHECK_NDFL",   
                   "N1_CHECK_STG"
                 );


    query =   "SELECT t1.*, (SELECT t_code FROM DOBJCODE_DBT t2 WHERE t2.T_OBJECTID  = t1.t_clientid AND t2.T_CODEKIND  = 101 AND t2.t_state = 0) as code "
            + "  FROM DNPTXCHECK_NDR_SKR_DBT t1 "
            + " WHERE (T_CHECK_NDFL <> 0 OR T_CHECK_NOB <> 0) AND T_OPERATION_ID = ?";                              

    cmd = DL_RSDCommand(query);
    cmd.AddParam(m_OperationID);

    cnt = cmd.GetCount();
    if(cnt > 0)
      InitProgress(cnt, "Печать протокола", "Печать протокола");
      m_cnt = cnt;
      i = 0;
      DataSet = cmd.Execute();
      while(DataSet.moveNext())

      PrintTableLine("N1_ID",        DataSet.CLIENTID,  null,
                     "N1_CODE",      DataSet.CODE,      null,
                     "N1_Income",    DataSet.Income,    null,
                     "N1_Expense",   DataSet.Expense,   null,
                     "N1_NOB_NDR",   DataSet.NOB_NDR,   null,
                     "N1_NOB_SKR",   DataSet.NOB_SKR,   null,
                     "N1_NOB13",     DataSet.NOB13,     null,
                     "N1_NOB30",     DataSet.NOB30,     null,
                     "N1_NOB15",     DataSet.NOB15,     null,
                     "N1_NOB18",     DataSet.NOB18,     null,
                     "N1_NOB20",     DataSet.NOB20,     null,
                     "N1_NOB22",     DataSet.NOB22,     null,
                     "N1_CHECK_NOB", DataSet.CHECK_NOB, null,
                     "N1_NDFL_NDR",  DataSet.NDFL_NDR,  null,
                     "N1_NDFL_SKR",  DataSet.NDFL_SKR,  null,
                     "N1_NDFL13",    DataSet.NDFL_13,    null,
                     "N1_NDFL30",    DataSet.NDFL_30,    null,
                     "N1_NDFL15",    DataSet.NDFL_15,    null,
                     "N1_NDFL18",    DataSet.NDFL_18,    null,
                     "N1_NDFL20",    DataSet.NDFL_20,    null,
                     "N1_NDFL22",    DataSet.NDFL_22,    null,
                     "N1_CHECK_NDFL", DataSet.CHECK_NDFL, null,
                     "N1_CHECK_STG",  DataSet.CHECK_STG,  null
                      );

        UseProgress(i = i + 1);
      end;

      RemProgress();
    end;

    EndTable();
  END;

  MACRO Run()
    Run();
    
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_VerNOBNDFL.xlsx", true, null, null, true, true );
  SetRowFlushCount(2000);  
END;

PRIVATE MACRO CreateReportNOBNDFL(OperationID, Cnt:@INTEGER)
  var RepObj = VerNOBNDFL_Report(OperationID);

  var FullRepFileNameXLSX = RepObj.Run();
  Cnt = RepObj.GetCnt();

  if(not isStandAlone())
    FullRepFileNameXLSX = "$"+FullRepFileNameXLSX;
  end;
  
  RepObj = NULL;
  
  FullRepFileNameXLSX = StrSubst(FullRepFileNameXLSX, "\\\\","\\");
  
  return FullRepFileNameXLSX;
END;

MACRO MakeXlsAndSendNOBNDFL(OperationID)
  var err = 0; 
  var Cnt = 0;
  var FileNameXLSX = CreateReportNOBNDFL(OperationID, @Cnt);

  var emailText, Subject, v_email_processor;
      
  Subject = "Результат сверки данных в СНОБ для 6-НДФЛ на " + {curdate} + " ";

  if(Cnt > 0)
      emailText =   "<div>"
                  + "Обнаружены расхождения в данных. Требуется завести дефект"
                  + "</div>";
  else
      emailText =   "<div>"
                  + "Расхождения в сверке отсутствуют" 
                  + "</div>";
  end;

  v_email_processor = c_email_proc_env();

  v_email_processor.m_get_email_group(89);
  v_email_processor.m_add_attach_to_list(FileNameXLSX);
  v_email_processor.m_set_msg_head(Subject);
  v_email_processor.m_add_row_to_msg_text(emailText);
  v_email_processor.m_save_to_submit_synch(true);
  v_email_processor.m_submit_email_synch();

  if(v_email_processor.m_get_error_status())
    err = 1; 
  end;

  return err;
END;