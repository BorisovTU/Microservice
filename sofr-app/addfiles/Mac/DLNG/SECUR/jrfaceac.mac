/*
$Name:        jrfaceac.mac
$Module:      Ценные бумаги
$Description: Отчет "Журнал лицевого учета"
*/
import "spserv.mac", "mctplprm.mac", "spRepFn2.mac", "DLReport_h.mac", "globals.mac", "secinter.mac", "dlmisc.mac";

private var ReportData;
                                           
private file AvoirissFI(fininstr) key 3;
private var ReportTMP = TBFile( "jrfaceac.tmp", "W", 0 );
private var ReportTMPCheckTorg = TBFile( "jrfaceac.tmp", "R", 0 );

private var ChargeBon = CheckChargeBonus();

private record FaceFalueFI( fininstr );

private const RECORD_INREST = 0, /*инф. о входящем остатке*/
              RECORD_LOT    = 1, /*инф. о лоте*/
              RECORD_LOT_UN = 2; /*инф. о лоте ГО(глобальной опреации)*/

private const StatLine = "Выпуск отчета \"Журнал лицевого учета\"";

private CONST CALCCOST_CATEGORY  = 27;/*Возможность надежного расчета TCC*/

private CONST PM_WRTSUM_KIND_RRWAB2 = 210;
private CONST PM_WRTSUM_KIND_LPB = 250;

private var TableHeader=
   "┌──────────────┬────────┬───────────┬───────────┬──────────────────────────┬──────────────────────┬──────────────────────┬──────────────────────────┬──────────────────────┬──────────────────────┬──────────────────────┬──────────────────────┐\n"+
   "│    Номер     │  Время │  Покупка  │  Продажа  │     Цена за единицу в    │  Стоимость в валюте  │      Сумма НКД       │    Сумма начисленного    │  Сумма начисленного  │   Сумма переоценки   │   Сумма начисленной  │    Остаток премии    │\n"+
   "│   сделки     │поставки│   (шт.)   │   (шт.)   │    валюте номинала ц/б   │     номинала ц/б     │                      │     купонного дохода     │  дисконтного дохода  │                      │   премии             │                      │\n"+
   "├──────────────┼────────┼───────────┼───────────┼──────────────────────────┼──────────────────────┼──────────────────────┼──────────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┤\n"+
   "│       1      │    2   │     3     │     4     │             5            │           6          │           7          │            8             │           9          │          10          │          11          │          12          │\n"+
   "├──────────────┼────────┼───────────┼───────────┼──────────────────────────┼──────────────────────┼──────────────────────┼──────────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┼──────────────────────┤";

private var
   cap      =  "                                              ЖУРНАЛ ЛИЦЕВОГО УЧЕТА\n",
   Pdate    =  "Дата: #\n",
   codeFI   =  "Код выпуска: #\n",
   nameFI   =  "Наименование: #\n",
   ISIN     =  "ISIN: #\n",
   TCC      =  "ТСС: #\n",
   nomCCY   =  "Валюта номинала: #\n";

private class SourceData( _DateBeg:date, _DateEnd:date, _AvrID:Integer, _Portf:Integer, _IsApp: bool )
   var DateBeg = _DateBeg,
       DateEnd = _DateEnd,
       AvrID   = _AvrID,
       Portf   = _Portf,
       IsApp   = _IsApp;      

   var NumRecords   = 0, ReportRun = false,

       PrintDate = false, PrintAvr = false, PrintPortf = false, 
       CurDate = Date(0,0,0), NumPortf = 0, 
       
       /*Итоги по портфелю*/
       PortfInRest,      PortfInCost,       PortfInNKD,        PortfInIntIncome,        PortfInDisIncome,        PortfInOverAmount, PortfInBonus, PortfInBonusRest, PortfNDPBonusRest,
       PortfItogBuy,     PortfItogBuySum,   PortfItogBuyNKD,   PortfItogBuyBonusRest,
       PortfItogSale,    PortfItogSaleSum,  PortfItogSaleNKD,  PortfItogSaleBonus, PortfItogSaleBonusRest,
       PortfItogWrite,   PortfItogWriteSum, PortfItogWriteNKD, PortfItogWriteIntIncome, PortfItogWriteDisIncome, PortfItogWriteOverAmount, PortfItogWriteBonus, PortfItogWriteBonusRest,
                                                               PortfNDPIntIncome,       PortfNDPDisIncome,       PortfNDPOverAmount, PortfNPDBonus,
       PortfOutCalcRest, PortfOutCalcCost,  PortfOutCalcNKD,   PortfOutCalcIntIncome,   PortfOutCalcDisIncome,   PortfOutCalcOverAmount, PortfOutCalcBonus, PortfOutCalcBonusRest,
       PortfOutBalRest,  PortfOutBalCost,   PortfOutBalNKD,    PortfOutBalIntIncome,    PortfOutBalDisIncome,    PortfOutBalOverAmount, PortfOutBalBonus, PortfOutBalBonusRest,

       /*Итоги по бумаге*/
       AvrInRest,        AvrInCost,         AvrInNKD,          AvrInIntIncome,          AvrInDisIncome,          AvrInOverAmount, AvrInBonus, AvrInBonusRest,
       AvrItogBuy,       AvrItogBuySum,     AvrItogBuyNKD, AvrItogBuyBonusRest,                                                       
       AvrItogSale,      AvrItogSaleSum,    AvrItogSaleNKD, AvrItogSaleBonus, AvrItogSaleBonusRest,                                                   
       AvrItogWrite,     AvrItogWriteSum,   AvrItogWriteNKD,   AvrItogWriteIntIncome,   AvrItogWriteDisIncome,   AvrItogWriteOverAmount, AvrItogWriteBonus, AvrItogWriteBonusRest,
                                                               AvrNDPIntIncome,         AvrNDPDisIncome,         AvrNDPOverAmount, AvrNPDBonus,
       AvrOutRestCalc,   AvrOutCalcCost,    AvrOutCalcNKD,     AvrOutCalcIntIncome,     AvrOutCalcDisIncome,     AvrOutCalcOverAmount, AvrOutCalcBonus, AvrOutCalcBonusRest,
       AvrOutRestBal,    AvrOutBalCost,     AvrOutBalNKD,      AvrOutBalIntIncome,      AvrOutBalDisIncome,      AvrOutBalOverAmount, AvrOutBalBonus, AvrOutBalBonusRest;


   macro ClearPortfSums()
      PortfInRest       = PortfInCost       = PortfInNKD         = PortfInIntIncome        = PortfInDisIncome        = PortfInOverAmount = PortfInBonus = PortfInBonusRest = PortfNDPBonusRest =
      PortfItogBuy      = PortfItogBuySum   = PortfItogBuyNKD    = PortfItogBuyBonusRest = 
      PortfItogSale     = PortfItogSaleSum  = PortfItogSaleNKD   = PortfItogSaleBonus = PortfItogSaleBonusRest =
      PortfItogWrite    = PortfItogWriteSum = PortfItogWriteNKD  = PortfItogWriteIntIncome = PortfItogWriteDisIncome = PortfItogWriteOverAmount = PortfItogWriteBonus = PortfItogWriteBonusRest =
                                                                   PortfNDPIntIncome =       PortfNDPDisIncome       = PortfNDPOverAmount = PortfNPDBonus = 
      PortfOutCalcRest  = PortfOutCalcCost  = PortfOutCalcNKD    = PortfOutCalcIntIncome   = PortfOutCalcDisIncome   = PortfOutCalcOverAmount = PortfOutCalcBonus = PortfOutCalcBonusRest =
      PortfOutBalRest   = PortfOutBalCost   = PortfOutBalNKD     = PortfOutBalIntIncome    = PortfOutBalDisIncome    = PortfOutBalOverAmount =  PortfOutBalBonus = PortfOutBalBonusRest = $0;
   end;

   macro ClearAvrSums()
      ClearPortfSums();
      NumPortf = 0;
      AvrInRest       = AvrInCost       = AvrInNKD         = AvrInIntIncome        = AvrInDisIncome        = AvrInOverAmount = AvrInBonus = AvrInBonusRest =
      AvrItogBuy      = AvrItogBuySum   = AvrItogBuyNKD    = AvrItogBuyBonusRest = 
      AvrItogSale     = AvrItogSaleSum  = AvrItogSaleNKD   = AvrItogSaleBonus = AvrItogSaleBonusRest =
      AvrItogWrite    = AvrItogWriteSum = AvrItogWriteNKD  = AvrItogWriteIntIncome = AvrItogWriteDisIncome = AvrItogWriteOverAmount = AvrItogWriteBonus = AvrItogWriteBonusRest =
                                                             AvrNDPIntIncome       = AvrNDPDisIncome       = AvrNDPOverAmount = AvrNPDBonus =
      AvrOutRestCalc  = AvrOutCalcCost  = AvrOutCalcNKD    = AvrOutCalcIntIncome   = AvrOutCalcDisIncome   = AvrOutCalcOverAmount = AvrOutCalcBonus = AvrOutCalcBonusRest =
      AvrOutRestBal   = AvrOutBalCost   = AvrOutBalNKD     = AvrOutBalIntIncome    = AvrOutBalDisIncome    = AvrOutBalOverAmount = AvrOutBalBonus = AvrOutBalBonusRest = $0;
   end;
   ClearAvrSums();
end;

/* Информация о части лота продажи, относящейся к одному из портфелей. */
private class SaleWriteData()
  var Amount         = $0, 
      BalanceCost    = $0, 
      SumSale        = $0,
      NKDSaleAmount  = $0, 
      NKDBuyAmount   = $0,

      InterestIncAdd = $0,
      DiscountIncAdd = $0, 
      OverChange     = $0,
      Bonus          = $0,
      BonusRest      = $0,
      BonusSum       = $0;
end;

private macro PrintSum( Val, NoPrn0 )
   if( Val == 0 ) 
      if( NoPrn0 == true )
         return ""; 
      else
         return 0; 
      end;
   elif( ReportData.IsApp == true )  return string( Val:a );
   else return string(Val);
   end;
end;

private macro ISBackFromBpp(sumid,instance)
   var sql, dat;

   instance = instance-1;

   sql = RSDCommand( "Select t_portfolio portf from dpmwrtbc_dbt where t_sumid = ? and t_instance = ? " );

   sql.addParam( "", RSDBP_IN, sumid );
   sql.addParam( "", RSDBP_IN, instance );
   sql.execute();

   dat = TrsbDataSet(sql); 

   if ( dat.movenext() )
      if ( (dat.portf != 1) AND (dat.portf != 2) )
         return false;
      else
         return true;
      end;
   else 
     return false;
   end;

end;

private macro PrintSumInvShare( Val, NoPrn0 )
   if( Val == 0 ) 
      if( NoPrn0 == true )
         return ""; 
      else
         return 0; 
      end;
   elif( ReportData.IsApp == true )  return string( ЧислоCЗаданнойТочностью ( Val, AvoirissFI.sumprecision, "a" ) );
   else return string( ЧислоCЗаданнойТочностью ( Val, AvoirissFI.sumprecision, "" ) );
   end;
end;

/*Проверяем, евляется ли инв.паем, если да, то печатаем с точностью указанной, в анкете пая*/
private macro PrintAvoirInvShare ( Val, NoPrn0)
      return PrintSumInvShare( Val, NoPrn0 );
end;

private macro PrintDouble( Val )
   if( ReportData.IsApp == true )  
      return ЧислоCЗаданнойТочностью(Val, 10, "a");
   else 
      return ЧислоCЗаданнойТочностью(Val, 10, "" );
   end;
end;

private macro ConvLotPortfToReportPortf( portf:Integer, PortfName:@String )
   if( portf == KINDPORT_TRADE ) 
      PortfName = TRADEPORTFNAME;
      return KINDPORT_TRADE;

   elif( portf == KINDPORT_SALE )
      PortfName = SALEPORTFNAME;
      return KINDPORT_SALE;

   elif( portf == KINDPORT_CONTR ) 
      PortfName = CONTRPORTFNAME;
      return KINDPORT_CONTR;

   elif( (portf == KINDPORT_PROMISSORY) )
      PortfName = PROMISSPORTFNAME;
      return KINDPORT_PROMISSORY;

   elif( portf == KINDPORT_RETIRE )
      PortfName = RETIREPORTFNAME;
      return KINDPORT_RETIRE;

   elif( portf == KINDPORT_BACK )
      PortfName = BACKPORTFNAME;
      return KINDPORT_BACK;

   elif( portf == KINDPORT_BASICDEBT )
      PortfName = "Ц/б, полученные на возвратной основе и затем проданные";
      return KINDPORT_BASICDEBT;

   elif( portf == KINDPORT_UNADMITTED )
      PortfName = UNADMITEDPORTFNAME;
      return KINDPORT_UNADMITTED;
	  
   elif( portf == KINDPORT_BACK_KSU )
      PortfName = BACKKSUPORTFNAME;
      return KINDPORT_BACK_KSU;
	  
   elif( portf == KINDPORT_BACK_BPP_KSU )
      PortfName = BACKBPPKSUPORTFNAME;
      return KINDPORT_BACK_BPP_KSU;

   else 
      PortfName = "неопределенный портфель";
      return KINDPORT_UNDEF;
   end;
end;

private macro InsertInTmp()
   InsertRecTempTBFile( ReportTMP );
   ReportData.NumRecords = ReportData.NumRecords + 1;
end;


private macro UpdateInTmp()
   UpdateRecTempFile( ReportTMP );
end;

/* Добавляем данные о лоте покупки или части лота продажи во временный файл.
      InsWrtsum   -  лот
      WriteSums   -  информация о части лота продажи
      RepPortf    -  отчетный портфель */
private macro InsertLotInReportByDeal( InsWrtsum:Variant, WriteSums:SaleWriteData, RepPortf )
   file fininstr( fininstr );

   var rq = TRecHandler("dlrq.dbt");
   record dl_leg(dl_leg);
   file   Deal(dl_tick);
   file   ParentLot(pmwrtsum);
   var    LegKind, Cost, Cnv:bool = false;

   ClearRecord( Deal );
   ClearRecord( ParentLot );

   if(InsWrtsum.kind != PM_WRTSUM_KIND_RRWAB2)
      Deal.DealID = InsWrtsum.DealID;
   else
      ParentLot.SumID = InsWrtsum.Parent;
      if( not GetEQ( ParentLot ) ) 
         MsgBox( "!!! Не найден лот ParentLot.SumID = " + string( InsWrtsum.Parent ) );
         return;
      end;
      Deal.DealID = ParentLot.DealID;
   end;
   
   if( not GetEQ( Deal ) ) 
      MsgBox( "!!! Не найдена сделка по лоту pmwrsum.DealID = " + string( InsWrtsum.DealID ) );
      return;
   end;

   ReportTMP.Clear;
   ReportTMP.rec.KindRecord = RECORD_LOT;
   ReportTMP.rec.DealCode   = Deal.DealCode;

   if(InsWrtsum.kind != PM_WRTSUM_KIND_RRWAB2)
      ReportTMP.rec.Date = InsWrtsum.DateP;
   else
      ReportTMP.rec.Date = InsWrtsum.changedate;
   end;

   ReportTMP.rec.FIID       = InsWrtsum.FIID;
   ReportTMP.rec.Time       = InsWrtsum.Time;
   ReportTMP.rec.BuySale    = InsWrtsum.Buy_Sale;

   ReportTMP.rec.ReportPortf = RepPortf;
   if( not ПолучитьТО( InsWrtsum.DocID, rq ) ) 
      MsgBox( "!!! Не найдено ТО на поставку основного актива (Код сделки=", Deal.DealCode, ")" ); 
      return;
   end;

   if( rq.rec.DealPart == 2 ) /*лот по обратной части сделки*/    
      LegKind = LEG_KIND_DL_TICK_BACK;
   else
      LegKind = LEG_KIND_DL_TICK;
   end;

   if( FindDL_LEG( LegKind, Deal.DealID, 0, dl_leg ) )
      MsgBox( "!!! Не найдены условия сделки dl_leg.dbt (код сделки=", Deal.DealCode,")" );
      return;
   end;

   if( ПолучитьФинИн( InsWrtsum.FIID, fininstr ) ) 
      MsgBox( "!!!  Не найдена ценная бумага по лоту покупки сделки " + Deal.DealCode );
      return;
   end;

   if( (ReportTMP.rec.BuySale == PM_WRITEOFF_SUM_BUY)  or (ReportTMP.rec.BuySale == PM_WRITEOFF_SUM_BUY_BO) )

      ReportTMP.rec.Amount       = InsWrtsum.Amount; 
      ReportTMP.rec.NKD          = InsWrtsum.NKDAMOUNT;
      Cost                       = InsWrtsum.Cost;
      ReportTMP.rec.BonusRest    = InsWrtsum.BEGBONUS;
      Cnv = true;
   else /*продажа*/
      ReportTMP.rec.Amount  = WriteSums.Amount;
      ReportTMP.rec.NKD     = WriteSums.NKDSaleAmount;
      Cost                  = WriteSums.SumSale;
      /*Суммы, списанные с покупки*/
      ReportTMP.rec.NKDBuy  = WriteSums.NKDBuyAmount;
      ReportTMP.rec.SumBuy = WriteSums.BalanceCost;
      ReportTMP.rec.InterestIncBuy  = WriteSums.InterestIncAdd;
      ReportTMP.rec.DiscountIncBuy  = WriteSums.DiscountIncAdd;
      ReportTMP.rec.OverChangeBuy   = WriteSums.OverChange;
      ReportTMP.rec.Bonus           = WriteSums.Bonus;
      ReportTMP.rec.BonusSum        = WriteSums.BonusSum;/*Доначисленная в продаже премия*/
      ReportTMP.rec.BonusRest       = WriteSums.BonusRest;
   end;

   /* A5 - Цена одной ц/б из параметров сделки покупки/операции зачисления
           или сделки продажи/операции списания, переведенная в валюту
           номинала по курсу на дату поставки этой сделки. */
   SmartConvertSumDbl(ReportTMP.rec.Price, GetPriceByDlTickDlLeg(Deal, dl_leg), ReportTMP.rec.Date, dl_leg.CFI, fininstr.FaceValueFI);

   if( not SmartConvertSum(ReportTMP.rec.Cost, Cost, ReportTMP.rec.Date, IIF((RepPortf == KINDPORT_CONTR) and Cnv, NATCUR, dl_leg.CFI), fininstr.FaceValueFI, false) )
      MsgBox( "!!! Ошибка при конвертации стоимости лота из " + IIF((RepPortf == KINDPORT_CONTR) and Cnv, "национальной валюты ", "валюты цены ") + ПолучитьКодФинИн(IIF((RepPortf == KINDPORT_CONTR) and Cnv, NATCUR, dl_leg.CFI)) + " в валюту номинала ", ПолучитьКодФинИн(fininstr.FaceValueFI) + ". Сделка " + Deal.DealCode );
      return;
   end;

   InsertInTmp();
end;

private macro IsExistsInOutByUN(ReportTMP:variant,RepRecord:@variant)
  var cmd, RRec;

      cmd = RSDCommand("select * from djrfaceac_tmp " +
                       "where t_date  = ? " +
                       "  and t_fiid = ? " +
                       "  and t_reportportf = ? " +
                       "  and t_kindrecord = ? " +
                       "  and t_BuySale = ? "
                       "  and t_dealcode = ?");

      cmd.addParam( "", RSDBP_IN, ReportTMP.rec.date );
      cmd.addParam( "", RSDBP_IN, ReportTMP.rec.fiid );
      cmd.addParam( "", RSDBP_IN, ReportTMP.rec.reportportf );
      cmd.addParam( "", RSDBP_IN, ReportTMP.rec.kindrecord );
      cmd.addParam( "", RSDBP_IN, ReportTMP.rec.BuySale );
      cmd.addParam( "", RSDBP_IN, ReportTMP.rec.dealcode );

      cmd.execute();

      RRec = TRsbDataSet(cmd);

      if(  RRec.MoveNext() )
         RepRecord =RRec.GetRecord();
         return true;
      end;

      return false;
end;

private macro InsertLotInReportByComm( InsWrtsum:Variant, WriteSums:SaleWriteData, RepPortf )
   file fininstr( fininstr );
   var  RepRecord;
   var  Cost, Cnv:bool = false;
   var FD_UN = SPFirstDocDLCOMMGlobal( InsWrtsum.DocKind, InsWrtsum.DocID );

   if( not FD_UN.comm.rec.DocumentID )
      MsgBox( "!!! Не найдена операция по лоту pmwrsum.DocID = " + string( InsWrtsum.DocID ) );
      return;
   end;

   ReportTMP.Clear;
   ReportTMP.rec.KindRecord = RECORD_LOT_UN;
   ReportTMP.rec.DealCode   = FD_UN.comm.rec.CommCode;

   ReportTMP.rec.Date = InsWrtsum.changedate;

   ReportTMP.rec.FIID       = InsWrtsum.FIID;
   ReportTMP.rec.Time       = InsWrtsum.Time;
   ReportTMP.rec.BuySale    = InsWrtsum.Buy_Sale;

   ReportTMP.rec.ReportPortf = RepPortf;

   if( ПолучитьФинИн( InsWrtsum.FIID, fininstr ) ) 
      MsgBox( "!!!  Не найдена ценная бумага по лоту зачисления глобальной операции с выпусками ц/б с кодом " + FD_UN.comm.rec.CommCode );
      return;
   end;

   if( (ReportTMP.rec.BuySale == PM_WRITEOFF_SUM_BUY)  or (ReportTMP.rec.BuySale == PM_WRITEOFF_SUM_BUY_BO) )

      ReportTMP.rec.Amount       = InsWrtsum.Amount; 
      ReportTMP.rec.NKD          = InsWrtsum.NKDAMOUNT;
      Cost                       = InsWrtsum.Cost;
      ReportTMP.rec.BonusRest    = InsWrtsum.BEGBONUS;
      Cnv = true;
   else /*продажа*/
      ReportTMP.rec.Amount  = WriteSums.Amount;
      ReportTMP.rec.NKD     = WriteSums.NKDSaleAmount;
      Cost                  = WriteSums.SumSale;
      /*Суммы, списанные с покупки*/
      ReportTMP.rec.NKDBuy  = WriteSums.NKDBuyAmount;
      ReportTMP.rec.SumBuy   = WriteSums.BalanceCost;
      ReportTMP.rec.InterestIncBuy  = WriteSums.InterestIncAdd;
      ReportTMP.rec.DiscountIncBuy  = WriteSums.DiscountIncAdd;
      ReportTMP.rec.OverChangeBuy   = WriteSums.OverChange;
      ReportTMP.rec.Bonus           = WriteSums.Bonus;
      ReportTMP.rec.BonusSum        = WriteSums.BonusSum;
      ReportTMP.rec.BonusRest       = WriteSums.BonusRest;
   end;

   if( (RepPortf == KINDPORT_CONTR) and Cnv )
      if( not SmartConvertSum(ReportTMP.rec.Cost, Cost, ReportTMP.rec.Date, NATCUR, fininstr.FaceValueFI, false) )
         MsgBox( "!!! Ошибка при конвертации стоимости лота из национальной валюты " + ПолучитьКодФинИн(NATCUR) + " в валюту номинала ", ПолучитьКодФинИн(fininstr.FaceValueFI));
         return ;
      end;
   else
      ReportTMP.rec.Cost = Cost;
   end;

   if( IsExistsInOutByUN(ReportTMP,@RepRecord) )

      ReportTMP.rec.AutoInc = RepRecord.AutoInc; 

      if( (ReportTMP.rec.BuySale == PM_WRITEOFF_SUM_BUY)  or (ReportTMP.rec.BuySale == PM_WRITEOFF_SUM_BUY_BO) )
      
         inc(ReportTMP.rec.Amount,RepRecord.Amount); 
         inc(ReportTMP.rec.NKD,RepRecord.NKD);
         inc(ReportTMP.rec.Cost,RepRecord.Cost);
         inc(ReportTMP.rec.BonusSum,RepRecord.BonusSum);
         inc(ReportTMP.rec.BonusRest,RepRecord.BonusRest);
      else /*продажа*/
         inc(ReportTMP.rec.Amount,RepRecord.Amount); 
         inc(ReportTMP.rec.NKD,RepRecord.NKD);
         inc(ReportTMP.rec.Cost,RepRecord.Cost);
         /*Суммы, списанные с покупки*/
         inc(ReportTMP.rec.NKDBuy,RepRecord.NKDBuy);
         inc(ReportTMP.rec.SumBuy,RepRecord.SumBuy);
         inc(ReportTMP.rec.InterestIncBuy,RepRecord.InterestIncBuy);
         inc(ReportTMP.rec.DiscountIncBuy,RepRecord.DiscountIncBuy);
         inc(ReportTMP.rec.OverChangeBuy,RepRecord.OverChangeBuy);
         inc(ReportTMP.rec.Bonus,RepRecord.Bonus);
         inc(ReportTMP.rec.BonusSum,RepRecord.BonusSum);
         inc(ReportTMP.rec.BonusRest,RepRecord.BonusRest);
      end;

      UpdateInTmp();
   else
      InsertInTmp();
   end;
end;

private macro InsertLotInReport( InsWrtsum:Variant, WriteSums:SaleWriteData, RepPortf )
   if( InsWrtsum.DocKind == DLDOC_PAYMENT )
      return InsertLotInReportByDeal( InsWrtsum, WriteSums, RepPortf );
   end;
   return InsertLotInReportByComm(InsWrtsum, WriteSums, RepPortf);
end;

private macro InsertLotInReportBPP( InsWrtsum:Variant )
   file fininstr( fininstr );

   var rq =  TRecHandler("dlrq.dbt");
   record dl_leg(dl_leg);
   file   Deal(dl_tick);
   file   ParentLot(pmwrtsum);
   var    LegKind, Cost;

   ClearRecord( Deal );
   ClearRecord( ParentLot );

   ParentLot.SumID = InsWrtsum.Parent;
   if( not GetEQ( ParentLot ) )
      MsgBox( "!!! Не найден лот ParentLot.SumID = " + string( InsWrtsum.Parent ) );
      return;
   end;
   Deal.DealID = ParentLot.DealID;

   if( not GetEQ( Deal ) ) 
      MsgBox( "!!! Не найдена сделка по лоту pmwrsum.DealID = " + string( InsWrtsum.DealID ) );
      return;
   end;

   ReportTMP.Clear;
   ReportTMP.rec.KindRecord = RECORD_LOT;
   ReportTMP.rec.DealCode   = Deal.DealCode;
   ReportTMP.rec.Date       = InsWrtsum.changedate;
   ReportTMP.rec.FIID       = InsWrtsum.FIID;
   ReportTMP.rec.Time       = InsWrtsum.Time;
   ReportTMP.rec.BuySale    = PM_WRITEOFF_SUM_SALE;

   ReportTMP.rec.ReportPortf = KINDPORT_UNADMITTED;

   if( not ПолучитьТО( InsWrtsum.DocID, rq ) )
      MsgBox( "!!! Не найдено ТО на поставку основного актива (Код сделки=", Deal.DealCode, ")" );
      return;
   end;

   if( rq.rec.DealPart == 2 ) /*лот по обратной части сделки*/
      LegKind = LEG_KIND_DL_TICK_BACK;
   else
      LegKind = LEG_KIND_DL_TICK;
   end;

   if( FindDL_LEG( LegKind, Deal.DealID, 0, dl_leg ) )
      MsgBox( "!!! Не найдены условия сделки dl_leg.dbt (код сделки=", Deal.DealCode,")" );
      return;
   end;

   if( ПолучитьФинИн( InsWrtsum.FIID, fininstr ) ) 
      MsgBox( "!!!  Не найдена ценная бумага по лоту покупки сделки " + Deal.DealCode );
      return;
   end;

   ReportTMP.rec.Amount = InsWrtsum.Amount;
   ReportTMP.rec.NKD    = InsWrtsum.NKDAMOUNT;
   Cost                 = InsWrtsum.Cost;

   /* A5 - Цена одной ц/б из параметров сделки покупки/операции зачисления
           или сделки продажи/операции списания, переведенная в валюту
           номинала по курсу на дату поставки этой сделки. */
   SmartConvertSumDbl(ReportTMP.rec.Price, GetPriceByDlTickDlLeg(Deal, dl_leg), ReportTMP.rec.Date, dl_leg.CFI, fininstr.FaceValueFI);

   if( not SmartConvertSum( ReportTMP.rec.Cost, Cost, ReportTMP.rec.Date, dl_leg.CFI, fininstr.FaceValueFI, false ) )
      MsgBox( "!!! Ошибка при конвертации стоимости лота из валюты цены " + ПолучитьКодФинИн(dl_leg.CFI) + " в валюту номинала ", ПолучитьКодФинИн(fininstr.FaceValueFI) + ". Сделка " + Deal.DealCode );
      return ;
   end;    

   InsertInTmp();
end;


private class TPortfSummary
   var
      SAmount            = $0,
      SBalanceCost       = $0,
      SNKD               = $0,
                         
      SAmountOut         = $0,
      SBalanceCostOut    = $0,
      SNKDOut            = $0,
                         
      SInterestIncome    = $0,
      SDiscountIncome    = $0,
      SOverAmount        = $0,
      SBonus             = $0,
      SBonusRest         = $0,
      
      SInterestIncomeOut = $0,
      SDiscountIncomeOut = $0,
      SOverAmountOut     = $0,
      SBonusOut          = $0,
      SBonusRestOut      = $0,
      
      SNDPIntIncome      = $0,
      SNDPDisIncome      = $0,
      SNDPOverAmount     = $0,
      SNPDBonus          = $0;
end;

/* Вставить остаток бумаги на начало/конец дня в отчет.
      FIID           -  бумага
      Portf          -  портфель
      RestDate       -  дата
      PortfSummary   -  данные по портфелю */
private macro InsertAvrRestInReport( FIID, Portf, RestDate, PortfSummary )

   ReportTMP.Clear;
   ReportTMP.rec.KindRecord        = RECORD_INREST;
   ReportTMP.rec.Date              = RestDate;
   ReportTMP.rec.FIID              = FIID;
   ReportTMP.rec.ReportPortf       = Portf;

   ReportTMP.rec.Amount            = PortfSummary.SAmount;
   ReportTMP.rec.Cost              = PortfSummary.SBalanceCost;
   ReportTMP.rec.NKD               = PortfSummary.SNKD;

   ReportTMP.rec.InterestIncome    = PortfSummary.SInterestIncome;
   ReportTMP.rec.DiscountIncome    = PortfSummary.SDiscountIncome;
   ReportTMP.rec.OverAmount        = PortfSummary.SOverAmount;

   ReportTMP.rec.Bonus             = PortfSummary.SBonus;
   ReportTMP.rec.BonusRest         = PortfSummary.SBonusRest;

   ReportTMP.rec.AmountOut         = PortfSummary.SAmountOut;
   ReportTMP.rec.BalanceCostOut    = PortfSummary.SBalanceCostOut;
   ReportTMP.rec.NKDOut            = PortfSummary.SNKDOut;

   ReportTMP.rec.InterestIncomeOut = PortfSummary.SInterestIncomeOut;
   ReportTMP.rec.DiscountIncomeOut = PortfSummary.SDiscountIncomeOut;
   ReportTMP.rec.OverAmountOut     = PortfSummary.SOverAmountOut;
   ReportTMP.rec.BonusOut          = PortfSummary.SBonusOut;
   ReportTMP.rec.BonusRestOut      = PortfSummary.SBonusRestOut;

   ReportTMP.rec.NDPIntIncome      = PortfSummary.SNDPIntIncome;
   ReportTMP.rec.NDPDisIncome      = PortfSummary.SNDPDisIncome;
   ReportTMP.rec.NDPOverAmount     = PortfSummary.SNDPOverAmount;
   ReportTMP.rec.NPDBonus          = PortfSummary.SNPDBonus;

   InsertInTmp();
end;

/* Вставляем лот продажи во временный файл.
   ТЗ: Если в одной сделке продажи списываются ц/б сразу из двух портфелей
   (торгового и инвестиционного), то такое списание отражается в двух
   разделах отчета по соответствующим портфелям, причем в каждом разделе
   выводятся данные только для количества ц/б, списываемых из этого
   портфеля.
      InsWrtsum   - лот продажи */
macro InsertLotSaleInReport( InsWrtsum:Variant )
   file wrtsumBuy( pmwrtsum );
   file fininstr( fininstr );
   var
      wrtlnk   = TBFile( "pmwrtlnk", "R", 1 ),
      Sales    = TArray(),
      i, LotBuyPortf, RepPortf;

   Sales[KINDPORT_TRADE]      = SaleWriteData();
   Sales[KINDPORT_SALE]       = SaleWriteData();
   Sales[KINDPORT_CONTR]      = SaleWriteData();
   Sales[KINDPORT_PROMISSORY] = SaleWriteData();
   Sales[KINDPORT_RETIRE]     = SaleWriteData();
   Sales[KINDPORT_BACK]       = SaleWriteData();
   Sales[KINDPORT_BASICDEBT]  = SaleWriteData();
   Sales[KINDPORT_UNADMITTED] = SaleWriteData();
   Sales[KINDPORT_BACK_KSU]   = SaleWriteData();
   Sales[KINDPORT_BACK_BPP_KSU] = SaleWriteData();
   
   wrtlnk.Clear();
   wrtlnk.AddFilter( "t_SaleID = " + string(InsWrtsum.SumID) );

   while( wrtlnk.Next() )

      ClearRecord( wrtsumBuy );
      wrtsumBuy.SumID  = wrtlnk.rec.BuyID;
      wrtsumBuy.Buy_Sale = PM_WRITEOFF_SUM_BUY;
      if( GetEQ(wrtsumBuy) )
         /* Получили лот покупки, связанный с лотом продажи. */
         /* Определяем портфель лота покупки на момент списания. */
         LotBuyPortf = GetLotPortf( wrtsumBuy );
         RepPortf    = ConvLotPortfToReportPortf( LotBuyPortf );
         if( (ReportData.Portf <= 0) OR (ReportData.Portf == RepPortf))
            inc( Sales[RepPortf].Amount, wrtlnk.rec.Amount );

            if( (RepPortf == KINDPORT_CONTR) and (wrtlnk.rec.CreateDate >= date(1,11,2014)) )
               if( ПолучитьФинИн( InsWrtsum.FIID, fininstr ) ) 
                  MsgBox("!!! Не найдена ценная бумага c FIID=" + string(InsWrtsum.FIID));
                  continue;
               end;
            end;

            if( wrtlnk.rec.Action == 421 )

               var BalanceCostBuy;
               if( (RepPortf == KINDPORT_CONTR) and (wrtlnk.rec.CreateDate >= date(1,11,2014)) )
                  if( not SmartConvertSum(BalanceCostBuy, wrtlnk.rec.BalanceCostBuy,  InsWrtsum.DateP, NATCUR, fininstr.FaceValueFI, false) )
                     MsgBox("!!! Ошибка при конвертации из национальной валюты " + ПолучитьКодФинИн(NATCUR) + " в валюту номинала ", ПолучитьКодФинИн(fininstr.FaceValueFI) );
                     continue;
                  end;
               else
                  BalanceCostBuy = wrtlnk.rec.BalanceCostBuy;
               end;

               inc( Sales[RepPortf].BalanceCost,    BalanceCostBuy );
               inc( Sales[RepPortf].NKDBuyAmount,   wrtlnk.rec.NKDBuyAmount );
               inc( Sales[RepPortf].NKDSaleAmount,  wrtlnk.rec.NKDSaleAmount );
               inc( Sales[RepPortf].SumSale,        wrtlnk.rec.SumBuy );
            else

               var BalanceCostSale;
               if( (RepPortf == KINDPORT_CONTR) and (wrtlnk.rec.CreateDate >= date(1,11,2014)) )
                  if( not SmartConvertSum(BalanceCostSale, wrtlnk.rec.BalanceCostSale, InsWrtsum.DateP, NATCUR, fininstr.FaceValueFI, false) )
                     MsgBox("!!! Ошибка при конвертации из национальной валюты " + ПолучитьКодФинИн(NATCUR) + " в валюту номинала ", ПолучитьКодФинИн(fininstr.FaceValueFI) );
                     continue;
                  end;
               else
                  BalanceCostSale = wrtlnk.rec.BalanceCostSale;
               end;

               inc( Sales[RepPortf].BalanceCost,    BalanceCostSale );
               inc( Sales[RepPortf].NKDBuyAmount,   wrtlnk.rec.NKDBuyAmount );
               inc( Sales[RepPortf].NKDSaleAmount,  wrtlnk.rec.NKDSaleAmount );
               inc( Sales[RepPortf].SumSale,        wrtlnk.rec.SumSale );
            end;

            inc( Sales[RepPortf].InterestIncAdd, wrtlnk.rec.INTERESTINCOMEBUY + wrtlnk.rec.INTERESTINCOMEADD );
            inc( Sales[RepPortf].DiscountIncAdd, wrtlnk.rec.DISCOUNTINCOMEBUY + wrtlnk.rec.DISCOUNTINCOMEADD );
            inc( Sales[RepPortf].OverChange,     wrtlnk.rec.OVERCHANGE );
            inc( Sales[RepPortf].Bonus,          wrtlnk.rec.BonusBuy);
            inc( Sales[RepPortf].BonusRest,      wrtlnk.rec.BEGBONUSCHANGE );
            inc( Sales[RepPortf].BonusSum,       wrtlnk.rec.BONUSADD );/*Доначисленная в продаже премия */
         end;
      else 
         MsgBox( "!!! Не найден списанный лот покупки для лота продажи ( pmwrtlnk.SaleID = " + string(wrtlnk.SaleID) + " pmwrtlnk.BuyID = " + string(wrtlnk.BuyID) + " )" );
      end;
   end;

   i = 0;
   while( i < Sales.Size() )
      if( (Sales[i] != null) AND (Sales[i].Amount != 0) )
         InsertLotInReport( InsWrtsum, Sales[i], i );
      end;
      i = i + 1;
   end;
end;

private macro ОтчетПоЛотам

   var wrtsum   = TBFile( "pmwrtsum", "R" ),
       Progress = TProgressBar,
       LotBuyPortf, RepPortf;

   var rep_date;
   var cmd = "", select = "", Lot;

   rep_date = ReportData.DateBeg;

   while( rep_date <= ReportData.DateEnd )
      
      select = " SELECT t.*, (SELECT t_factdate " +
                               "FROM ddlrq_dbt " +
                              "WHERE t_id = t.t_docid) as DateP " +
               "   FROM v_scwrthistex t " +
               "  WHERE t.t_buy_sale in(?, ?, ?) " +
               "    AND t.t_party = -1 " +
               "    AND t.t_dockind in(?,?) " +
               "    AND t.t_dealid > 0 " +
               "    AND (   (    (t.t_kind != 210) " +
               "             AND ((SELECT t_factdate " +
               "                     FROM ddlrq_dbt " +
               "                    WHERE t_id = t.t_docid) = ? " +
               "                 ) " +
               "            ) " +
               "         OR (    (t.t_kind = 210 or t.t_kind = 120 or t.t_kind = 130) " +
               "             AND ( t.t_changedate= ? " +
               "                 ) " +
               "            ) " +
               "        ) " +
               "    AND t.t_changedate = ? " +
               "    AND (t.t_amount > 0 OR (t.t_kind = ? and t.t_Action = ?)) " +
               "    AND (t.t_Action = 10 OR t.t_Action = 50 OR (t.t_kind = 130 and t.t_Action = 422) OR (t.t_kind = ? and t.t_Action = ?))";

      if( ReportData.AvrID > 0 )
         select = select + " AND t.t_fiid = " + string(ReportData.AvrID);
      end;

      cmd = RSDCommand(select);

      cmd.addParam( "", RSDBP_IN, PM_WRITEOFF_SUM_BUY );
      cmd.addParam( "", RSDBP_IN, PM_WRITEOFF_SUM_SALE );
      cmd.addParam( "", RSDBP_IN, PM_WRITEOFF_SUM_BUY_BO );
      cmd.addParam( "", RSDBP_IN, DLDOC_PAYMENT );
      cmd.addParam( "", RSDBP_IN, DL_ISSUE_UNION );
      cmd.addParam( "", RSDBP_IN, rep_date );
      cmd.addParam( "", RSDBP_IN, rep_date );
      cmd.addParam( "", RSDBP_IN, rep_date );
      cmd.addParam( "", RSDBP_IN, PM_WRTSUM_KIND_RRWAS1 );
      cmd.addParam( "", RSDBP_IN, PM_WRTSUM_UPDTMODE_DISCARD );
      cmd.addParam( "", RSDBP_IN, PM_WRTSUM_KIND_RRWAS1 );
      cmd.addParam( "", RSDBP_IN, PM_WRTSUM_UPDTMODE_DISCARD );
      cmd.execute();

      Lot = TRsbDataSet(cmd);
      Progress.Init( Lot.GetRecCount(), StatLine, "Просмотр лотов" );

      while( Lot.movenext() )
         /*поставка в отчетную дату*/
         if( (Lot.Buy_Sale == PM_WRITEOFF_SUM_BUY) OR (Lot.Buy_Sale == PM_WRITEOFF_SUM_BUY_BO) )

            LotBuyPortf  = GetLotPortf( Lot );
            RepPortf     = ConvLotPortfToReportPortf( LotBuyPortf );

            if( (ReportData.Portf <= 0) OR (ReportData.Portf == RepPortf) )
               InsertLotInReport(Lot, null, RepPortf);

               if( ((Lot.kind == PM_WRTSUM_KIND_RRWAB2) OR (Lot.kind == PM_WRTSUM_KIND_LPB)) AND
                   ((Lot.portfolio == KINDPORT_TRADE) OR (Lot.portfolio == KINDPORT_SALE)) AND
                   (Lot.action == PM_WRTSUM_UPDTMODE_DELIVERY) AND
                   (ISBackFromBpp(Lot.sumid, Lot.instance))
                 )
                  InsertLotInReportBPP(Lot);
               end;
            end;
         else
            InsertLotSaleInReport(Lot);
         end;

         Progress.Use;
      end;

      rep_date = rep_date + 1;

      Progress.Remove;
   end;

end;

/* Вычисляем остатки, НКД и балансовую стоимость указанной бумаги по всем
   нужным портфельям на начало дня и конец дня.
   Сохраняем данные во временный файл.
      FIID     -  айди бумаги
      CalcDate -  дата, на которую вычисляем данные
      Mes      -  сообщение в статус строке */
private macro GetRestOnDate( FIID, CalcDate, Mes )

   /* Алгоритм работы:
      Перебираем все лоты покупки указанной бумаги, дата поставки которых
      меньше нужной даты и статус выше или равен "Поставлен".
      Для каждого подходящего лота определяем портфель на дату (на начало дня).
      Проверяем его. Если лот подходит, то вычисляем к-во бумаг на дату.
      балансовую стоимость и НКД. */

   macro CaclRest( FIID, OnDate, PortfFlagArray, PortfSummaryAr )

      VAR wrtcalc   = TRecHandler( "pmwrtsum.dbt" );

      macro GetBonusSumByDate(Portf:INTEGER,CalcDate:DATE,FIID:INTEGER,ReportPortf:INTEGER,Delivery:BOOL,Accept:BOOL)
         var DataSet;
         var select = RSDCommand(" SELECT NVL(sum(ROUND(T.T_BONUS,2)),0) Bonus " +
                                 "   FROM v_scwrthistex t " +
                                 "  WHERE t.t_Portfolio = ? " +
                                 "    AND t.t_party = -1 " +
                                 "    AND t.t_fiid = ? "+
                                 "    AND t.t_Department = ? "+
                                 "    AND (t.t_Action = 440 OR t.t_Action = 441 OR t.t_Action = 442)"/*начисление ПДД*/
                                 "    AND t.t_changedate = ?"
                                 "    and t.t_bonusdate = ?");
      
         select.addParam( "", RSDBP_IN, Portf );
         select.addParam( "", RSDBP_IN, FIID );
         select.addParam( "", RSDBP_IN, {OperDprt} );
         select.addParam( "", RSDBP_IN, CalcDate );
         select.addParam( "", RSDBP_IN, CalcDate );
         select.execute();
      
         DataSet = TRsbDataSet(select);
      
         if( DataSet.MoveNext() AND (DataSet.Bonus>$0.) )
            wrtcalc.Clear();
            if( WRT_Calc( {OperDprt}, FIID, -1, 0, Portf, CalcDate-1, wrtcalc, Delivery, Accept ) != true )
               RunError( "Ошибка при определении количества ц/б в портфеле." );      
            end;
            /*доначисление премии в дату CalcDate*/
            PortfSummaryAr[ReportPortf].SNPDBonus = PortfSummaryAr[ReportPortf].SNPDBonus  + (money(DataSet.Bonus) - wrtcalc.rec.BONUS) ;
         end;
      end;

      MACRO CalcRestByPortf( IsInRest, ReportPortf:INTEGER, Portf:INTEGER, CalcDate:DATE, Delivery:BOOL, Accept:BOOL )
         file fininstr( fininstr );
         if (PortfFlagArray[ReportPortf])
            wrtcalc.Clear();

            if( WRT_Calc( {OperDprt}, FIID, -1, 0, Portf, IIF( IsInRest != true, CalcDate, CalcDate-1 ), wrtcalc, Delivery, Accept ) != true )
               RunError( "Ошибка при определении количества ц/б в портфеле." );      
            end;

            var BalanceCost;
            if( ReportPortf == KINDPORT_CONTR )
               if( ПолучитьФинИн( FIID, fininstr ) ) 
                  RunError("!!! Не найдена ценная бумага c FIID=" + string(FIID));
               end;
               if( not SmartConvertSum(BalanceCost, wrtcalc.rec.BalanceCost, CalcDate, NATCUR, fininstr.FaceValueFI, false) )
                  RunError( "!!! Ошибка при конвертации стоимости лота из национальной валюты " + ПолучитьКодФинИн(NATCUR) + " в валюту номинала ", ПолучитьКодФинИн(fininstr.FaceValueFI) );
               end;
            else
               BalanceCost = wrtcalc.rec.BalanceCost;
            end;

            if( IsInRest == true )
               PortfSummaryAr[ReportPortf].SAmount           = PortfSummaryAr[ReportPortf].SAmount         + wrtcalc.rec.AMOUNT ;
               PortfSummaryAr[ReportPortf].SBalanceCost      = PortfSummaryAr[ReportPortf].SBalanceCost    + BalanceCost;
               PortfSummaryAr[ReportPortf].SNKD              = PortfSummaryAr[ReportPortf].SNKD            + wrtcalc.rec.NKDAMOUNT ;
               PortfSummaryAr[ReportPortf].SInterestIncome   = PortfSummaryAr[ReportPortf].SInterestIncome + wrtcalc.rec.INTERESTINCOME ;
               PortfSummaryAr[ReportPortf].SDiscountIncome   = PortfSummaryAr[ReportPortf].SDiscountIncome + wrtcalc.rec.DISCOUNTINCOME ;
               PortfSummaryAr[ReportPortf].SOverAmount       = PortfSummaryAr[ReportPortf].SOverAmount     + wrtcalc.rec.OVERAMOUNT ;
               PortfSummaryAr[ReportPortf].SBonusRest        = PortfSummaryAr[ReportPortf].SBonusRest      + wrtcalc.rec.BEGBONUS;
               PortfSummaryAr[ReportPortf].SBonus            = PortfSummaryAr[ReportPortf].SBonus          + wrtcalc.rec.BONUS;
            else 
               PortfSummaryAr[ReportPortf].SAmountOut        = PortfSummaryAr[ReportPortf].SAmountOut         + wrtcalc.rec.Amount ;
               PortfSummaryAr[ReportPortf].SBalanceCostOut   = PortfSummaryAr[ReportPortf].SBalanceCostOut    + BalanceCost;
               PortfSummaryAr[ReportPortf].SNKDOut           = PortfSummaryAr[ReportPortf].SNKDOut            + wrtcalc.rec.NKDAMOUNT ;
               PortfSummaryAr[ReportPortf].SInterestIncomeOut= PortfSummaryAr[ReportPortf].SInterestIncomeOut + wrtcalc.rec.INTERESTINCOME ;
               PortfSummaryAr[ReportPortf].SDiscountIncomeOut= PortfSummaryAr[ReportPortf].SDiscountIncomeOut + wrtcalc.rec.DISCOUNTINCOME ;
               PortfSummaryAr[ReportPortf].SOverAmountOut    = PortfSummaryAr[ReportPortf].SOverAmountOut     + wrtcalc.rec.OVERAMOUNT ;
               PortfSummaryAr[ReportPortf].SBonusOut         = PortfSummaryAr[ReportPortf].SBonusOut          + wrtcalc.rec.BONUS;
               PortfSummaryAr[ReportPortf].SBonusRestOut     = PortfSummaryAr[ReportPortf].SBonusRestOut      + wrtcalc.rec.BEGBONUS;

               GetBonusSumByDate(Portf,CalcDate,FIID,ReportPortf,Delivery,Accept);
             end;
          end;
      END;

      CalcRestByPortf( true, KINDPORT_UNADMITTED, KINDPORT_TRADE,      OnDate, false, true  );
      CalcRestByPortf( true, KINDPORT_UNADMITTED, KINDPORT_SALE,       OnDate, false, true  );
      CalcRestByPortf( true, KINDPORT_TRADE,      KINDPORT_TRADE,      OnDate, true,  false );
      CalcRestByPortf( true, KINDPORT_SALE,       KINDPORT_SALE,       OnDate, true,  false );
      CalcRestByPortf( true, KINDPORT_CONTR,      KINDPORT_CONTR,      OnDate, true,  true  );
      CalcRestByPortf( true, KINDPORT_PROMISSORY, KINDPORT_PROMISSORY, OnDate, true,  true  );
      CalcRestByPortf( true, KINDPORT_RETIRE,     KINDPORT_RETIRE,     OnDate, true,  true  );
      CalcRestByPortf( true, KINDPORT_BACK,       KINDPORT_BACK,       OnDate, true,  true  );
      CalcRestByPortf( true, KINDPORT_BASICDEBT,  KINDPORT_BASICDEBT,  OnDate, true,  true  );

      CalcRestByPortf( false, KINDPORT_UNADMITTED, KINDPORT_TRADE,      OnDate, false, true  );
      CalcRestByPortf( false, KINDPORT_UNADMITTED, KINDPORT_SALE,       OnDate, false, true  );
      CalcRestByPortf( false, KINDPORT_TRADE,      KINDPORT_TRADE,      OnDate, true,  false );
      CalcRestByPortf( false, KINDPORT_SALE,       KINDPORT_SALE,       OnDate, true,  false );
      CalcRestByPortf( false, KINDPORT_CONTR,      KINDPORT_CONTR,      OnDate, true,  true  );
      CalcRestByPortf( false, KINDPORT_PROMISSORY, KINDPORT_PROMISSORY, OnDate, true,  true  );
      CalcRestByPortf( false, KINDPORT_RETIRE,     KINDPORT_RETIRE,     OnDate, true,  true  );
      CalcRestByPortf( false, KINDPORT_BACK,       KINDPORT_BACK,       OnDate, true,  true  );
      CalcRestByPortf( false, KINDPORT_BASICDEBT,  KINDPORT_BASICDEBT,  OnDate, true,  true  );
   end;

   macro InitPortf( RepData, PortfID, FlagArray, SummaryAr )
      FlagArray[PortfID] = (RepData.Portf < 0) or (RepData.Portf == PortfID);
      SummaryAr[PortfID] = TPortfSummary;
   end;

   var
      PortfFlagArray    = TArray,
      PortfSummaryAr    = TArray,
      Counter;

   /* Инициализируем массивы с данными по портфелям. */
   InitPortf( ReportData, KINDPORT_TRADE,      PortfFlagArray, PortfSummaryAr );
   InitPortf( ReportData, KINDPORT_SALE,       PortfFlagArray, PortfSummaryAr );
   InitPortf( ReportData, KINDPORT_CONTR,      PortfFlagArray, PortfSummaryAr );
   InitPortf( ReportData, KINDPORT_PROMISSORY, PortfFlagArray, PortfSummaryAr );
   InitPortf( ReportData, KINDPORT_RETIRE,     PortfFlagArray, PortfSummaryAr );
   InitPortf( ReportData, KINDPORT_BACK,       PortfFlagArray, PortfSummaryAr );
   InitPortf( ReportData, KINDPORT_BASICDEBT,  PortfFlagArray, PortfSummaryAr );
   InitPortf( ReportData, KINDPORT_UNADMITTED, PortfFlagArray, PortfSummaryAr );

   /* Вычисляем остатки на начало и конец дня. */
   CaclRest( FIID, CalcDate, PortfFlagArray, PortfSummaryAr );

   /* Сохраняем данные по портфелям во временном файле. */
   Counter = 0;
   while( Counter < PortfFlagArray.Size )

      if( PortfFlagArray[Counter] )
         InsertAvrRestInReport(  FIID, Counter, CalcDate,
                                 PortfSummaryAr[Counter] );
      end;

      inc( Counter );
   end;
end;

private macro ОстатокПоБумаге( fininstr )
   var
      CurDate = ReportData.DateBeg,
      Mes;

   while( CurDate <= ReportData.DateEnd )

      Mes = "Определение остатка по ц/б " + fininstr.FI_Code
            + " на дату " + string( CurDate );

      GetRestOnDate( fininstr.FIID, CurDate, Mes );

      CurDate = CurDate + 1;
   end;
end; 

private macro ОстатокПоВсемБумагам()
   var
      fininstr = TBFile( "fininstr", "R", 3 ),
      progress = TProgressBar;

   fininstr.Clear();
   fininstr.AddFilter(  "t_FI_Kind = " + string(FIKIND_AVOIRISS)
                        + " and t_IsSYS != 'X'" );

   progress.Init( fininstr.NRecords, StatLine, "Определение остатков по ц/б" );

   while( fininstr.Next )
      ОстатокПоБумаге( fininstr.rec );
      progress.Use;
   end;

   progress.Remove;
end; 

private macro СоздатьОтчет()
   file fininstr( fininstr );

   if( ReportData.AvrID != -1 )
      if( ПолучитьФинИн( ReportData.AvrID, fininstr ) != 0 ) 
         MsgBox( "Не найдена ценная бумага, заданная в параметрах отчета." );
         return;
      end;
      ОстатокПоБумаге( fininstr );
   else
      ОстатокПоВсемБумагам();
   end; 

   ОтчетПоЛотам();
end;

private class (DL_CReportTemplate) JR_faceac_Report (_toExcel:bool, _DateBeg:Date, _DateEnd:Date)

   private var toExcel = false;

   private var DateBeg:Date;
   private var DateEnd:Date;

   private macro PrintMode():INTEGER
      if(toExcel)
         return DL_OUTREPORT_EXCEL;
      else
         return DL_OUTREPORT_STD;
      end;
   end;   

   private macro Register_Table()
      RegisterTable( 
         "N1_TableLine", TableHeader,
         "N1_A1", 
         "N1_A2", 
         "N1_A3", 
         "N1_A4", 
         "N1_A5", 
         "N1_A6", 
         "N1_A7", 
         "N1_A8", 
         "N1_A9", 
         "N1_A10",         
         "N1_A11",         
         "N1_A12"         
      );     
   end;

   private macro BeginReport()
      Dl_CallInsertStat(PrintMode());
      CreateTotalBook();
      SetActiveSheet( "Журнал лицевого учета" );
      PrintFormatString( 
         cap//,
         //"N1_H0", DateBeg 
         //"N1_H0_2", DateEnd
      );
      CopyAllSheetInTotalBook( NULL, false, "N1_Header", 0 );
      //Register_Table();
   end;

   private macro EndCopyTable()      
      EndTable();
      CopyAllSheetInTotalBook( NULL, false, GetTableRange(), 0 );      
   end;

   private macro EndReport()
      SaveTotalBook();
   end;  

   private macro PrintLineType1
      (
         НомерСделки,
         ВремяПоставки,
         Покупка,
         Продажа,
         Цена,
         Стоимость,
         СуммаНКД,
         НачисленныйНКД,
         НачисленныйДисконтный,
         Переоценка,
         Премия,
         ОстатокПремия
      )

      PrintTableLine( 
         "N1_A1",  НомерСделки,           null,
         "N1_A2",  ВремяПоставки,         null,
         "N1_A3",  Покупка,               SetPrecisionByFractPart( Покупка, 6, 0 ),
         "N1_A4",  Продажа,               SetPrecisionByFractPart( Продажа, 6, 0 ),
         "N1_A5",  Цена,                  null,
         "N1_A6",  Стоимость,             null,
         "N1_A7",  СуммаНКД,              null,
         "N1_A8",  НачисленныйНКД,        null,
         "N1_A9",  НачисленныйДисконтный, null,
         "N1_A10", Переоценка,            null,
         "N1_A11", IIF(ChargeBon,Премия,""),       null,
         "N1_A12", IIF(ChargeBon,ОстатокПремия,""),null
      );
   end;

   private macro PrintLineType2
      (
         НазваниеИтога,
         Покупка,
         Продажа,
         Цена,
         Стоимость,
         СуммаНКД,
         НачисленныйНКД,
         НачисленныйДисконтный,
         Переоценка,
         Премия,
         ОстатокПремия
      )

      if(toExcel)//это временно, наверное, :) т.к. пока инструмент неправильно склеивает ячейки
         Merge("N1_A1", "N1_A2");
      end;

      PrintTableLine( 
         "N1_A1",  НазваниеИтога,   null,         
         "N1_A3",  Покупка,               SetPrecisionByFractPart( Покупка, 6, 0 ),
         "N1_A4",  Продажа,               SetPrecisionByFractPart( Продажа, 6, 0 ),
         "N1_A5",  Цена,                  null,
         "N1_A6",  Стоимость,             null,
         "N1_A7",  СуммаНКД,              null,
         "N1_A8",  НачисленныйНКД,        null,
         "N1_A9",  НачисленныйДисконтный, null,
         "N1_A10", Переоценка,            null,
         "N1_A11", IIF(ChargeBon,Премия,""), null,
         "N1_A12", IIF(ChargeBon,ОстатокПремия,""), null
      );
   end;

   private macro PrintLineType3 (НазваниеРаздела)      
      if(toExcel)//это временно, наверное, :) т.к. пока инструмент неправильно склеивает ячейки
         Merge("N1_A1", "N1_A12");      
      end;      
      PrintTableLine("N1_A1", НазваниеРаздела, null);
   end;

   private macro PrintAvrHead()
      file Avoiriss(avoiriss);
      var K, Continue_cicle, SinceDate, NeedMarketPrice = false, Price = $0;

      if( ReportData.PrintAvr == true )
         return;
      end;
      ReportData.PrintAvr = true;

      if( ReportData.PrintDate == false )               
              PrintFormatString( PDate, "N1_H0", ReportData.CurDate);
              CopyAllSheetInTotalBook( NULL, false, "N1_PDate", 1 );
         ReportData.PrintDate = true;
      end;

      ReportData.ReportRun = true; 

      if( ПолучитьФинИн( ReportTMP.rec.FIID, AvoirissFI, Avoiriss ) != 0 ) 
         RunError( "Не найдена ценная бумага, данные по которой отобраны в отчет." );
      end;
      if( ПолучитьФинИн( AvoirissFI.FaceValueFI, FaceFalueFI ) )
         ClearRecord( AvoirissFI );
      end;
      
      PrintFormatString( codeFI, "N1_H1_0",  AvoirissFI.FI_Code);
      PrintFormatString( nameFI, "N1_H1_1",  AvoirissFI.Name);
      PrintFormatString( ISIN,   "N1_H1_2",  Avoiriss.ISIN);

      var NumInList = "";

      /*Если не нашли, или нашли что "нет" - такая бумага не подходит.*/
      if ( 
            not GetMainObjAttr(
                  null, 
                  OBJTYPE_AVOIRISS, 
                  UniID(Avoiriss, OBJTYPE_AVOIRISS), 
                  CALCCOST_CATEGORY, 
                  null, null, 
                  NumInList
               ) 

            or (NumInList == "0") /*0 - нет*/
         )
        PrintFormatString( TCC, "N1_H1_3", ""); 
      else

        if( not SmartConvertSumDbl(Price, 1.0, GetDateAfterWorkDays(ReportData.CurDate, -1),ReportTMP.rec.FIID, FaceFalueFI.FIID, false, ПолучитьВидКурса(SP_RATETYPE_RightCost), SinceDate ))
             Price = 0.0;    
        end;

        InitError(); /*после ConvSum остается ошибка*/
        PrintFormatString( TCC, "N1_H1_3", ЧислоCЗаданнойТочностью(Price,10) + "  " + FaceFalueFI.Ccy);
      end;

      PrintFormatString( nomCCY, "N1_H1_4", FaceFalueFI.Ccy);       

      CopyAllSheetInTotalBook( NULL, false, "N1_TableHeader", 1 );
      Register_Table();
         
   end;

   private macro PrintPortfInRest()
      var Price = 0.0, PortfName; 
      var InRest = "";
      var PorfHead:string="";

      PrintAvrHead();
      
      if( ReportData.PrintPortf == true )
         return;
      end;
      ReportData.PrintPortf = true;

      ConvLotPortfToReportPortf( ReportTMP.rec.ReportPortf, @PortfName );

      ReportData.NumPortf        = ReportData.NumPortf + 1;
      ReportData.AvrInRest       = ReportData.AvrInRest       + ReportData.PortfInRest;
      ReportData.AvrInCost       = ReportData.AvrInCost       + ReportData.PortfInCost;
      ReportData.AvrInNKD        = ReportData.AvrInNKD        + ReportData.PortfInNKD;

      ReportData.AvrInIntIncome  = ReportData.AvrInIntIncome  + ReportData.PortfInIntIncome;
      ReportData.AvrInDisIncome  = ReportData.AvrInDisIncome  + ReportData.PortfInDisIncome;
      ReportData.AvrInOverAmount = ReportData.AvrInOverAmount + ReportData.PortfInOverAmount;
      ReportData.AvrInBonus      = ReportData.AvrInBonus      + ReportData.PortfInBonus;
      ReportData.AvrInBonusRest  = ReportData.AvrInBonusRest  + ReportData.PortfInBonusRest;

      if( ReportData.PortfInRest != 0 )
        Price = ReportData.PortfInCost/ReportData.PortfInRest;
      end;
      
      PrintLineType3("Портфель: "+PortfName);
      PrintLineType2(
         "Входящий остаток",
         PrintAvoirInvShare(ReportData.PortfInRest),
         "",
         PrintDouble(Price),
         PrintSum(ReportData.PortfInCost)       +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfInNKD)        +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfInIntIncome)  +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfInDisIncome)  +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfInOverAmount) +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfInBonus)      +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfInBonusRest)  +" "+FaceFalueFI.Ccy
      );

   end;

   private macro PrintPortfOutRest()
      var Price = 0.0, PriceBal= 0.0;

      if( ReportData.PrintPortf == true )
         ReportData.PrintPortf = false;
      else
         return;
      end;

      if( (ReportData.PortfItogBuy != 0) OR (ReportData.PortfItogSale != 0) )

          PrintLineType3("Итого по портфелю");

          PrintLineType2(
            "Покупка",
            ReportData.PortfItogBuy,
            "", "",
            ReportData.PortfItogBuySum +" "+FaceFalueFI.Ccy, 
            ReportData.PortfItogBuyNKD +" "+FaceFalueFI.Ccy,
            "", "", "", "",
            ReportData.PortfItogBuyBonusRest +" "+FaceFalueFI.Ccy
          );

          PrintLineType2(
            "Продажа",
            ReportData.PortfItogSale,
            "", "",
            ReportData.PortfItogSaleSum +" "+FaceFalueFI.Ccy,
            ReportData.PortfItogSaleNKD +" "+FaceFalueFI.Ccy,
            "", "", "",
            ReportData.PortfItogSaleBonus +" "+FaceFalueFI.Ccy,
            ReportData.PortfItogSaleBonusRest +" "+FaceFalueFI.Ccy
          );

          PrintLineType2(
            "В т.ч. списано", 
            ReportData.PortfItogWrite,
            "",
            "",
            ReportData.PortfItogWriteSum        +" "+FaceFalueFI.Ccy,
            ReportData.PortfItogWriteNKD        +" "+FaceFalueFI.Ccy,
            ReportData.PortfItogWriteIntIncome  +" "+FaceFalueFI.Ccy,
            ReportData.PortfItogWriteDisIncome  +" "+FaceFalueFI.Ccy,
            ReportData.PortfItogWriteOverAmount +" "+FaceFalueFI.Ccy,
            ReportData.PortfItogWriteBonus      +" "+FaceFalueFI.Ccy,
            ReportData.PortfItogWriteBonusRest  +" "+FaceFalueFI.Ccy
          );
      end;                                                                

      ReportData.PortfOutCalcRest       = ReportData.PortfInRest          + ReportData.PortfItogBuy       - ReportData.PortfItogWrite; 
      ReportData.PortfOutCalcCost       = ReportData.PortfInCost          + ReportData.PortfItogBuySum    - ReportData.PortfItogWriteSum;
      ReportData.PortfOutCalcNKD        = ReportData.PortfInNKD           + ReportData.PortfItogBuyNKD    - ReportData.PortfItogWriteNKD;
      ReportData.PortfOutCalcIntIncome  = ReportData.PortfInIntIncome     + ReportData.PortfNDPIntIncome  - ReportData.PortfItogWriteIntIncome; 
      ReportData.PortfOutCalcDisIncome  = ReportData.PortfInDisIncome     + ReportData.PortfNDPDisIncome  - ReportData.PortfItogWriteDisIncome;
      ReportData.PortfOutCalcOverAmount = ReportData.PortfInOverAmount    + ReportData.PortfNDPOverAmount - ReportData.PortfItogWriteOverAmount;
      ReportData.PortfOutCalcBonus      = ReportData.PortfInBonus         + ReportData.PortfNPDBonus      - ReportData.PortfItogWriteBonus;
      ReportData.PortfOutCalcBonusRest  = ReportData.PortfInBonusRest + ReportData.PortfItogBuyBonusRest -
                                          ReportData.PortfItogSaleBonusRest - (ReportData.PortfNPDBonus - ReportData.PortfItogSaleBonus);

      ReportData.AvrOutRestCalc         = ReportData.AvrOutRestCalc       + ReportData.PortfOutCalcRest;
      ReportData.AvrOutCalcCost         = ReportData.AvrOutCalcCost       + ReportData.PortfOutCalcCost;
      ReportData.AvrOutCalcNKD          = ReportData.AvrOutCalcNKD        + ReportData.PortfOutCalcNKD;
      ReportData.AvrOutCalcIntIncome    = ReportData.AvrOutCalcIntIncome  + ReportData.PortfOutCalcIntIncome;
      ReportData.AvrOutCalcDisIncome    = ReportData.AvrOutCalcDisIncome  + ReportData.PortfOutCalcDisIncome;
      ReportData.AvrOutCalcOverAmount   = ReportData.AvrOutCalcOverAmount + ReportData.PortfOutCalcOverAmount;
      ReportData.AvrOutCalcBonus        = ReportData.AvrOutCalcBonus      + ReportData.PortfOutCalcBonus;
      ReportData.AvrOutCalcBonusRest    = ReportData.AvrOutCalcBonusRest  + ReportData.PortfOutCalcBonusRest;
                                        
      ReportData.AvrOutRestBal          = ReportData.AvrOutRestBal        + ReportData.PortfOutBalRest;
      ReportData.AvrOutBalCost          = ReportData.AvrOutBalCost        + ReportData.PortfOutBalCost;
      ReportData.AvrOutBalNKD           = ReportData.AvrOutBalNKD         + ReportData.PortfOutBalNKD;
      ReportData.AvrOutBalIntIncome     = ReportData.AvrOutBalIntIncome   + ReportData.PortfOutBalIntIncome;
      ReportData.AvrOutBalDisIncome     = ReportData.AvrOutBalDisIncome   + ReportData.PortfOutBalDisIncome;
      ReportData.AvrOutBalOverAmount    = ReportData.AvrOutBalOverAmount  + ReportData.PortfOutBalOverAmount;
      ReportData.AvrOutBalBonus         = ReportData.AvrOutBalBonus       + ReportData.PortfOutBalBonus;
      ReportData.AvrOutBalBonusRest     = ReportData.AvrOutBalBonusRest   + ReportData.PortfOutBalBonusRest;
                                                                      
      ReportData.AvrNDPIntIncome        = ReportData.AvrNDPIntIncome      + ReportData.PortfNDPIntIncome;  
      ReportData.AvrNDPDisIncome        = ReportData.AvrNDPDisIncome      + ReportData.PortfNDPDisIncome;
      ReportData.AvrNDPOverAmount       = ReportData.AvrNDPOverAmount     + ReportData.PortfNDPOverAmount;
      ReportData.AvrNPDBonus            = ReportData.AvrNPDBonus          + ReportData.PortfNPDBonus;
      
      if( ReportData.PortfOutCalcRest != 0)
         Price = ReportData.PortfOutCalcCost/ReportData.PortfOutCalcRest;
      end;

      if( ReportData.PortfOutBalRest != 0)
         PriceBal = ReportData.PortfOutBalCost/ReportData.PortfOutBalRest;
      end;     


      PrintLineType2(
         "Начисление дохода и переоценка",
         "", "", "", "", "",
         ReportData.PortfNDPIntIncome  +" "+FaceFalueFI.Ccy,
         ReportData.PortfNDPDisIncome  +" "+FaceFalueFI.Ccy,
         ReportData.PortfNDPOverAmount +" "+FaceFalueFI.Ccy,
         ReportData.PortfNPDBonus      +" "+FaceFalueFI.Ccy,
         ""
      );

      PrintLineType2(
         "Исходящий остаток (расчетный)",
         PrintAvoirInvShare(ReportData.PortfOutCalcRest),
         "",          
         PrintDouble(Price)                           +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutCalcCost)        +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutCalcNKD)         +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutCalcIntIncome)   +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutCalcDisIncome)   +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutCalcOverAmount)  +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutCalcBonus)       +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutCalcBonusRest)   +" "+FaceFalueFI.Ccy
      );

      PrintLineType2(
         "Исходящий остаток (балансовый)",
         PrintAvoirInvShare(ReportData.PortfOutBalRest),
         "",          
         PrintDouble(PriceBal)                     +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutBalCost)      +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutBalNKD)       +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutBalIntIncome) +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutBalDisIncome) +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutBalOverAmount)+" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutBalBonus)     +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.PortfOutBalBonusRest) +" "+FaceFalueFI.Ccy
      );

   end;

   private macro PrintAvrItog()
      var PriceIn = 0.0, PriceOutCalc = 0.0, PriceOutBal = 0.0;

      if( ReportData.PrintAvr == true )
         ReportData.PrintAvr = false;
      else
         return;
      end; 

      PrintPortfOutRest();

      if( ReportData.AvrInRest != 0 )
         PriceIn = ReportData.AvrInCost/ReportData.AvrInRest;
      end;

      if( ReportData.AvrOutRestCalc != 0 )
         PriceOutCalc = ReportData.AvrOutCalcCost/ReportData.AvrOutRestCalc;
      end;

      if( ReportData.AvrOutRestBal != 0 )
         PriceOutBal = ReportData.AvrOutBalCost/ReportData.AvrOutRestBal;
      end;

      PrintLineType3("Итого по выпуску: "+AvoirissFI.Name);
      
      PrintLineType2(
         "Входящий остаток",
         PrintAvoirInvShare(ReportData.AvrInRest),
         "",
         PrintDouble(PriceIn) +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrInCost)      +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrInNKD)       +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrInIntIncome) +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrInDisIncome) +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrInOverAmount)+" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrInBonus)     +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrInBonusRest) +" "+FaceFalueFI.Ccy
      );

      PrintLineType2(
         "Покупка",                     
         PrintAvoirInvShare(ReportData.AvrItogBuy),
         "","",
         PrintSum(ReportData.AvrItogBuySum) +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrItogBuyNKD) +" "+FaceFalueFI.Ccy,
         "","","","",
         PrintSum(ReportData.AvrItogBuyBonusRest) +" "+FaceFalueFI.Ccy
      );

      PrintLineType2(
         "Продажа",
         PrintAvoirInvShare(ReportData.AvrItogSale),
         "","",
         PrintSum(ReportData.AvrItogSaleSum) +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrItogSaleNKD) +" "+FaceFalueFI.Ccy,
         "","","",
         PrintSum(ReportData.AvrItogSaleBonus) +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrItogSaleBonusRest) +" "+FaceFalueFI.Ccy
      );

      PrintLineType2(
         "В т.ч. списано",
         PrintAvoirInvShare(ReportData.AvrItogWrite),
         "","",
         PrintSum(ReportData.AvrItogWriteSum)         +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrItogWriteNKD)         +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrItogWriteIntIncome)   +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrItogWriteDisIncome)   +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrItogWriteOverAmount)  +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrItogWriteBonus)       +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrItogWriteBonusRest)   +" "+FaceFalueFI.Ccy
      );

      PrintLineType2(
         "Начисление дохода и переоценка",         
         "","","","","",         
         PrintSum(ReportData.AvrNDPIntIncome) +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrNDPDisIncome) +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrNDPOverAmount)+" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrNPDBonus)     +" "+FaceFalueFI.Ccy
      );

      PrintLineType2(
         "Исходящий остаток (расчетный)",         
         PrintAvoirInvShare(ReportData.AvrOutRestCalc),
         "",
         PrintDouble(PriceOutCalc)                 +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutCalcCost)       +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutCalcNKD)        +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutCalcIntIncome)  +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutCalcDisIncome)  +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutCalcOverAmount) +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutCalcBonus)      +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutCalcBonusRest)  +" "+FaceFalueFI.Ccy
      );

      PrintLineType2(
         "Исходящий остаток (балансовый)",         
         PrintAvoirInvShare(ReportData.AvrOutRestBal),
         "",
         PrintDouble(PriceOutBal),
         PrintSum(ReportData.AvrOutBalCost)        +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutBalNKD)         +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutBalIntIncome)   +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutBalDisIncome)   +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutBalOverAmount)  +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutBalBonus)       +" "+FaceFalueFI.Ccy,
         PrintSum(ReportData.AvrOutBalBonusRest)   +" "+FaceFalueFI.Ccy
      );

      EndCopyTable();
   end;

   private macro PrintLot( PrintedLots:Integer )
     var Price = 0.0, AmountBuy = $0, AmountSale = $0;

     PrintPortfInRest();   

     if( ReportTMP.rec.BuySale == PM_WRITEOFF_SUM_SALE )

        AmountSale                          = ReportTMP.rec.Amount;
        ReportData.PortfItogSale            = ReportData.PortfItogSale            + AmountSale;
        ReportData.PortfItogSaleSum         = ReportData.PortfItogSaleSum         + ReportTMP.rec.Cost;
        ReportData.PortfItogSaleNKD         = ReportData.PortfItogSaleNKD         + ReportTMP.rec.NKD;
        ReportData.PortfItogSaleBonus       = ReportData.PortfItogSaleBonus       + ReportTMP.rec.Bonus;
        ReportData.PortfItogSaleBonusRest   = ReportData.PortfItogSaleBonusRest   + ReportTMP.rec.BonusRest;

        ReportData.PortfItogWrite           = ReportData.PortfItogWrite           + AmountSale;
        ReportData.PortfItogWriteSum        = ReportData.PortfItogWriteSum        + ReportTMP.rec.SumBuy;
        ReportData.PortfItogWriteNKD        = ReportData.PortfItogWriteNKD        + ReportTMP.rec.NKDBuy;

        ReportData.PortfItogWriteIntIncome  = ReportData.PortfItogWriteIntIncome  + ReportTMP.rec.InterestIncBuy;
        ReportData.PortfItogWriteDisIncome  = ReportData.PortfItogWriteDisIncome  + ReportTMP.rec.DiscountIncBuy;
        ReportData.PortfItogWriteOverAmount = ReportData.PortfItogWriteOverAmount + ReportTMP.rec.OverChangeBuy;
        ReportData.PortfItogWriteBonus      = ReportData.PortfItogWriteBonus      + ReportTMP.rec.Bonus;
        ReportData.PortfItogWriteBonusRest  = ReportData.PortfItogWriteBonusRest  + ReportTMP.rec.BonusRest;

        ReportData.AvrItogSale              = ReportData.AvrItogSale              + AmountSale;
        ReportData.AvrItogSaleSum           = ReportData.AvrItogSaleSum           + ReportTMP.rec.Cost;
        ReportData.AvrItogSaleNKD           = ReportData.AvrItogSaleNKD           + ReportTMP.rec.NKD;
        ReportData.AvrItogSaleBonusRest     = ReportData.AvrItogSaleBonusRest     + ReportTMP.rec.BonusRest;

        ReportData.AvrItogWrite             = ReportData.AvrItogWrite             + AmountSale;
        ReportData.AvrItogWriteSum          = ReportData.AvrItogWriteSum          + ReportTMP.rec.SumBuy;
        ReportData.AvrItogWriteNKD          = ReportData.AvrItogWriteNKD          + ReportTMP.rec.NKDBuy;

        ReportData.AvrItogWriteIntIncome    = ReportData.AvrItogWriteIntIncome    + ReportTMP.rec.InterestIncBuy;
        ReportData.AvrItogWriteDisIncome    = ReportData.AvrItogWriteDisIncome    + ReportTMP.rec.DiscountIncBuy;
        ReportData.AvrItogWriteOverAmount   = ReportData.AvrItogWriteOverAmount   + ReportTMP.rec.OverChangeBuy;
        ReportData.AvrItogWriteBonus        = ReportData.AvrItogWriteBonus        + ReportTMP.rec.Bonus;
        ReportData.AvrItogWriteBonusRest    = ReportData.AvrItogWriteBonusRest    + ReportTMP.rec.BonusRest;

        ReportData.PortfNPDBonus            = ReportData.PortfNPDBonus            + ReportTMP.rec.BonusSum;/*Доначисленная в продаже премия */

     else

        AmountBuy                           = ReportTMP.rec.Amount; 
        ReportData.PortfItogBuy             = ReportData.PortfItogBuy    + AmountBuy;
        ReportData.PortfItogBuySum          = ReportData.PortfItogBuySum + ReportTMP.rec.Cost;
        ReportData.PortfItogBuyNKD          = ReportData.PortfItogBuyNKD + ReportTMP.rec.NKD;
        ReportData.PortfItogBuyBonusRest    = ReportData.PortfItogBuyBonusRest + ReportTMP.rec.BonusRest;
                                            
        ReportData.AvrItogBuy               = ReportData.AvrItogBuy      + AmountBuy;     
        ReportData.AvrItogBuySum            = ReportData.AvrItogBuySum   + ReportTMP.rec.Cost;
        ReportData.AvrItogBuyNKD            = ReportData.AvrItogBuyNKD   + ReportTMP.rec.NKD; 
        ReportData.AvrItogBuyBonusRest      = ReportData.AvrItogBuyBonusRest + ReportTMP.rec.BonusRest; 

     end;

     PrintLineType1(
        ReportTMP.rec.DealCode,
        ReportTMP.rec.Time,
        PrintAvoirInvShare(AmountBuy,true),
        PrintAvoirInvShare(AmountSale,true),
        PrintDouble(ReportTMP.rec.Price)       +" "+FaceFalueFI.Ccy,
        PrintSum(ReportTMP.rec.Cost)           +" "+FaceFalueFI.Ccy,
        PrintSum(ReportTMP.rec.NKD)            +" "+FaceFalueFI.Ccy,
        PrintSum(ReportTMP.rec.InterestIncBuy) +" "+FaceFalueFI.Ccy,
        PrintSum(ReportTMP.rec.DiscountIncBuy) +" "+FaceFalueFI.Ccy,
        PrintSum(ReportTMP.rec.OverChangeBuy)  +" "+FaceFalueFI.Ccy,
        PrintSum(ReportTMP.rec.Bonus)          +" "+FaceFalueFI.Ccy,
        PrintSum(ReportTMP.rec.BonusRest)      +" "+FaceFalueFI.Ccy
     );

   end;

   private macro ПечатьОтчета()

      var continue_cicle, Num = 0, PrevDate = Date(0,0,0), PrevFIID = -1, NumberOfPrintedLots = 0;
      
      if( ReportData.NumRecords > 0 )
         InitProgress( ReportData.NumRecords, StatLine, "Печать отчета" );
         ReportTMP.Clear;
         ReportTMP.Rewind;
         continue_cicle = ReportTMP.Next;

         while( continue_cicle )

            if( PrevDate != ReportTMP.rec.Date )               
               PrintAvrItog();
               ReportData.CurDate   = ReportTMP.rec.Date;
               ReportData.PrintDate = false;
               PrevDate = ReportTMP.rec.Date;
               PrevFIID = -1;
            end;

            if( PrevFIID != ReportTMP.rec.FIID )
               PrintAvrItog();
               ReportData.ClearAvrSums();
               PrevFIID = ReportTMP.rec.FIID;
            end;

            if( ReportTMP.rec.KindRecord == RECORD_INREST )
               PrintPortfOutRest();
               ReportData.ClearPortfSums();
               ReportData.PortfInRest           = ReportTMP.rec.Amount; 
               ReportData.PortfInCost           = ReportTMP.rec.Cost;
               ReportData.PortfInNKD            = ReportTMP.rec.NKD;

               ReportData.PortfInIntIncome      = ReportTMP.rec.InterestIncome; 
               ReportData.PortfInDisIncome      = ReportTMP.rec.DiscountIncome;
               ReportData.PortfInOverAmount     = ReportTMP.rec.OverAmount;
               ReportData.PortfInBonus          = ReportTMP.rec.Bonus;
               ReportData.PortfInBonusRest      = ReportTMP.rec.BonusRest;

               /*С учетом переоценки*/
               ReportData.PortfOutBalRest       = ReportTMP.rec.AmountOut; 
               ReportData.PortfOutBalCost       = ReportTMP.rec.BalanceCostOut;   
               ReportData.PortfOutBalNKD        = ReportTMP.rec.NKDOut;    

               ReportData.PortfOutBalIntIncome  = ReportTMP.rec.InterestIncomeOut;
               ReportData.PortfOutBalDisIncome  = ReportTMP.rec.DiscountIncomeOut;
               ReportData.PortfOutBalOverAmount = ReportTMP.rec.OverAmountOut;
               ReportData.PortfOutBalBonus      = ReportTMP.rec.BonusOut;
               ReportData.PortfOutBalBonusRest  = ReportTMP.rec.BonusRestOut;

               ReportData.PortfNDPIntIncome     = ReportTMP.rec.NDPIntIncome;
               ReportData.PortfNDPDisIncome     = ReportTMP.rec.NDPDisIncome;
               ReportData.PortfNDPOverAmount    = ReportTMP.rec.NDPOverAmount;

               /*Доначисленная в покупке премия,
                 донач в продаже(если была) возьмем с лота в PrintLot*/
               ReportData.PortfNPDBonus         = ReportTMP.rec.NPDBonus;

               NumberOfPrintedLots = 0;

               if( (ReportData.PortfInRest != 0) OR (ReportData.PortfOutBalRest != 0) )
                  PrintPortfInRest();
               end;
            else
               PrintLot(NumberOfPrintedLots);
               NumberOfPrintedLots = NumberOfPrintedLots + 1;
            end;            

            continue_cicle = ReportTMP.Next;
            UseProgress( Num = Num + 1 );

            if( Num == ReportData.NumRecords )               
               PrintAvrItog();               
            end;
         end;
         RemProgress();
      end;

      if( ReportData.ReportRun == false )
        MsgBox("Нет данных, удовлетворяющих параметрам отчета.");    
      end;     
   end;

   private macro Create()      
      СоздатьОтчет();
      ПечатьОтчета();            
      
      AddStandardFooter( "N1_" );      

      CopyAllSheetInTotalBook( NULL, false, "N1_Footer", 1 );
   end;

   toExcel = _toExcel;
   DateBeg = _DateBeg;
   DateEnd = _DateEnd;
      
   initDL_CReportTemplate( NULL, "Report_jrfaceac.xls" );   
end;


macro ReportRun( DateBeg:date, DateEnd:date, AvrID:Integer, Portf:Integer, IsApp:bool, ToExcel:bool)
   var errcode, errtext, TmpFName = GetWorkFileName("jrfaceac");

   ReportData = SourceData( DateBeg, DateEnd, AvrID, Portf, IsApp );

   ClearGlobalTmp( "djrfaceac_tmp" );

   Message( "Выполнение отчета \"Журнал лицевого учета\" ..." );

   JR_faceac_Report(ToExcel, DateBeg, DateEnd).Run();
end;
