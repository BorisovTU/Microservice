/*
$Name:        nptxctrl_form.mac
$Module:      Ценные бумаги
$Description: Отчет "Контроль задолженности " - форма ввода данных
 PROGRAMMED BY:   Nikolskya V. 
 CREATION DATE:   06.05.2011
*/
import "DlRepForm_h.mac";

CONST PNFLD_NPTXCTRL_PERIOD          = 0,
      PNFLD_NPTXCTRL_YEAR            = 1,
      PNFLD_NPTXCTRL_ENDDATE         = 2,
      PNFLD_NPTXCTRL_CLIENTCODE      = 3,
      PNFLD_NPTXCTRL_CLIENTNAME      = 4,
      PNFLD_NPTXCTRL_FROMSOFR        = 5,
      PNFLD_NPTXCTRL_FROMDEPO        = 6,
      PNFLD_NPTXCTRL_ISACTIVECLIENT  = 7,
      PNFLD_NPTXCTRL_SHOWCURBROACC   = 8,
      PNFLD_NPTXCTRL_BYCHANGES       = 9,
      PNFLD_NPTXCTRL_CHANGEBEGDATE   = 10,
      PNFLD_NPTXCTRL_CHANGEENDDATE   = 11,
      PNFLD_NPTXCTRL_PRINTMETHOD     = 12;

PRIVATE CONST
  H_ClientCode      = 101,
  H_ClientName      = 102,
  H_FromSOFR        = 103,
  H_FromDEPO        = 104,
  H_IsActiveClient  = 105,
  H_IsShowCurBroAcc = 106,
  H_ByChanges       = 107,
  H_ChBegDate       = 108,
  H_ChEndDate       = 109;

PRIVATE CONST SelCodeKind = 1; // Общий параметр для листалок Бирж,Субьектов,Брокеров,Клиентов     

/*Год*/
MACRO DLUSR_FldProc_Year( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Day, Month, OldYear;

  if( Cmd == DLG_INIT ) /*Инициализация по умолчанию*/
     if( FldRealValue == null )
        DateSplit( {curdate}, null, null, FldRealValue );
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if( (FldRealValue < 1990) OR (FldRealValue > 2990) ) /*максимально исключить опечатки*/
        MsgBox( "Неверное значение в поле \"Год\".");
        return CM_IGNORE;
     end;

     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        DateSplit( pThis.GetLinkedValue(DL_PNFLD_BEGINDATE), Day, Month, OldYear );
        if( (DL_GetDaysInYear(OldYear) == 366) and 
            (Month == 2) and
            (Day == 29) and
            (DL_GetDaysInYear(FldRealValue) != DL_GetDaysInYear(OldYear)) )
            Day = 28;
        elif( (DL_GetDaysInYear(FldRealValue) == 366) and 
            (Month == 2) and
            (Day == 28) and
            (DL_GetDaysInYear(FldRealValue) != DL_GetDaysInYear(OldYear)) )
            Day = 29;
        end;
        pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, Date( Day, Month, FldRealValue) );
     end;

     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
        DateSplit( pThis.GetLinkedValue(DL_PNFLD_ENDDATE), Day, Month, OldYear );
        if( (DL_GetDaysInYear(OldYear) == 366) and 
            (Month == 2) and
            (Day == 29) and
            (DL_GetDaysInYear(FldRealValue) != DL_GetDaysInYear(OldYear)) )
            Day = 28;
        elif( (DL_GetDaysInYear(FldRealValue) == 366) and 
            (Month == 2) and
            (Day == 28) and
            (DL_GetDaysInYear(FldRealValue) != DL_GetDaysInYear(OldYear)) )
            Day = 29;
        end;
        pThis.SetLinkedValue( DL_PNFLD_ENDDATE, Date( Day, Month, FldRealValue) );
     end;
     
     if(FldRealValue < 2023)
        msgbox("Год не может быть меньше 2023");
        return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
     end;
  end;

  return CM_DEFAULT;
END;


/*Конечная дата выпуска отчета
  Для НДФЛ может потребоваться, чтобы если
   после редактирования значение поля больше, чем последний день периода, 
   выбранного в полях Период и Год, то поля Период и Год не пересчитывались*/
MACRO DLUSR_FldProc_EndDate_NpTx( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, Day, BeginDate, EndDate, Period, PeriodYear;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     DateSplit( FldRealValue, null, Month, Year);
     /*если есть поле "начальная дата, то год относится к нему"*/

     if( pThis.ExistField(DL_PNFLD_BEGINDATE) == false )
        if(Year < pThis.GetLinkedValue(DL_PNFLD_YEAR))
          pThis.SetLinkedValue( DL_PNFLD_YEAR, Year);
        end;   
     end;

     PeriodYear = pThis.GetLinkedValue(DL_PNFLD_YEAR);
     Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);

     if( Period > 12 ) /*указан квартал*/
        Period = Period - 12;
        /*если дата не входит в заданный квартал - переставить период*/
        if( ((Month < Period*3-2) and (Year <= PeriodYear)) OR ((Month > Period*3) and (Year < PeriodYear))  )
           pThis.SetLinkedValue( DL_PNFLD_PERIOD, ((Month - 1)/3 + 1) + 12 );
        end;
     elif( (Period > Month) AND (Year <= PeriodYear) )
        pThis.SetLinkedValue( DL_PNFLD_PERIOD, Month);
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  if((pThis.GetLinkedValue(DL_PNFLD_YEAR) > 0) and (FldRealValue == date(31,12,pThis.GetLinkedValue(DL_PNFLD_YEAR))))
    EnableFields( pThis.Data(), PNFLD_NPTXCTRL_FROMDEPO );
  else
    pThis.SetLinkedValue( H_FromDEPO, UNSET_CHAR);
    DisableFields( pThis.Data(), PNFLD_NPTXCTRL_FROMDEPO );
  end;

  return CM_DEFAULT;
END;

/*Листалка клиентов
  является физическими лицами,
  вид обслуживания которых <Фондовый дилинг> или <Срочные контракты> или <Депозитарное обслуживание>,
  категория "Является плательщиком НДФЛ" <> "Нет".*/
private MACRO DLUSR_FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string = "", DepCode;
  record clnt(party);

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_ClientName, STR_ALLVALUE);
     else
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
          pThis.SetLinkedValue( H_ClientName, Party.rec.ShortName);
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_ClientName, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     whereCond =  "     t.T_LegalForm = " + 2/*legal_form_phys*/ + // Физические лица
                  " AND t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                  "                        FROM dclient_dbt c2 " +
                  "                       WHERE t.T_PARTYID = c2.T_PARTYID " + 
                  "                         AND c2.t_servicekind IN (" + PTSK_STOCKDL + "," + PTSK_DV + "," + PTSK_DEPOS + ", "+PTSK_SVO+" ) " + // вид обслуживания
                  "                    ) ";
     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_ClientName, Party.rec.ShortName);
     end;
  end;

  if(FldRealValue == -1)
    EnableFields( pThis.Data(), PNFLD_NPTXCTRL_BYCHANGES );
  else
    pThis.SetLinkedValue( H_ByChanges, UNSET_CHAR);
    DisableFields( pThis.Data(), PNFLD_NPTXCTRL_BYCHANGES );
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_CheckBoxByChanges( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) 
        FldRealValue = UNSET_CHAR;
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if(Key == KEY_SPACE) 
        if( FldRealValue == UNSET_CHAR )
          FldShowValue = FldRealValue = SET_CHAR;
          pThis.SetLinkedValue( H_ChBegDate, date(1,1,pThis.GetLinkedValue(DL_PNFLD_YEAR)));
          pThis.SetLinkedValue( H_ChEndDate, date(31,12,pThis.GetLinkedValue(DL_PNFLD_YEAR)));
        else
          FldShowValue = FldRealValue = UNSET_CHAR;
        end;
     end;
  end;

  if( FldShowValue == SET_CHAR)
     EnableFields( pThis.Data(), PNFLD_NPTXCTRL_CHANGEBEGDATE );
     EnableFields( pThis.Data(), PNFLD_NPTXCTRL_CHANGEENDDATE );
  else
     DisableFields( pThis.Data(), PNFLD_NPTXCTRL_CHANGEBEGDATE );
     DisableFields( pThis.Data(), PNFLD_NPTXCTRL_CHANGEENDDATE );

     pThis.SetLinkedValue( H_ChBegDate, date(0,0,0));
     pThis.SetLinkedValue( H_ChEndDate, date(0,0,0));
  end;                                
END;

/*Способ формирования отчета*/
PRIVATE MACRO DLUSR_FldProc_PrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == DL_OUTREPORT_EXCEL )
        return "Excel";
     elif( Id == DL_OUTREPORT_TXT )
        return "CSV";
     end;                                           
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_OUTREPORT_EXCEL;
     end;
     FldShowValue = Name(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(DL_OUTREPORT_EXCEL),DL_OUTREPORT_EXCEL,
                    Name(DL_OUTREPORT_TXT),  DL_OUTREPORT_TXT
                  );

  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) Sp_NPTXCTRL_Panel()
  var m_Period;         
  var m_Year;           
  var m_EndDate;        
  var m_ClientID;
  var m_FromSOFR;
  var m_FromDEPO;     
  var m_IsCurBrocAcc;
  var m_IsActiveClient;
  var m_ByChanges;
  var m_ChBegDate;
  var m_ChEndDate; 
  var m_PrintMethod;    

  PRIVATE MACRO Init()
     VAR Year, Period;

     DateSplit( {curdate}, null, null, Year );
     
     if(Year < 2023)
       Year = 2023;
     end;

     Period = 12; //Пока всегда декабрь
     
     AddFieldProc( 0, PNFLD_NPTXCTRL_PERIOD,          DL_PNFLD_PERIOD,          @DL_FldProc_Period,           Period, 11, 4 ); 
     AddFieldProc( 0, PNFLD_NPTXCTRL_YEAR,            DL_PNFLD_YEAR,            @DLUSR_FldProc_Year,          Year );
     AddFieldProc( 0, PNFLD_NPTXCTRL_ENDDATE,         DL_PNFLD_ENDDATE,         @DLUSR_FldProc_EndDate_NpTx      );
     AddFieldProc( 0, PNFLD_NPTXCTRL_CLIENTCODE,      H_ClientCode,             @DLUSR_FldProc_Client,        -1 );
     AddFieldProc( 0, PNFLD_NPTXCTRL_CLIENTNAME,      H_ClientName,             @Default,                     "" );
     AddFieldProc( 0, PNFLD_NPTXCTRL_FROMSOFR,        H_FromSOFR,               @DL_FldProc_CheckBox,         SET_CHAR );
     AddFieldProc( 0, PNFLD_NPTXCTRL_FROMDEPO,        H_FromDEPO,               @DL_FldProc_CheckBox,         "" );
     AddFieldProc( 0, PNFLD_NPTXCTRL_SHOWCURBROACC,   H_IsShowCurBroAcc,        @DL_FldProc_CheckBox,         "" );     
     AddFieldProc( 0, PNFLD_NPTXCTRL_ISACTIVECLIENT,  H_IsActiveClient,         @DL_FldProc_CheckBox,         "" );
     AddFieldProc( 0, PNFLD_NPTXCTRL_BYCHANGES,       H_ByChanges,              @FldProc_CheckBoxByChanges,   "" );
     AddFieldProc( 0, PNFLD_NPTXCTRL_CHANGEBEGDATE,   H_ChBegDate,              @Default      );
     AddFieldProc( 0, PNFLD_NPTXCTRL_CHANGEENDDATE,   H_ChEndDate,              @Default      );
     AddFieldProc( 0, PNFLD_NPTXCTRL_PRINTMETHOD,     DL_PNFLD_PRINTMETHOD,     @DLUSR_FldProc_PrintMethod,   DL_OUTREPORT_EXCEL, 30, 18 );
  END;

  PRIVATE MACRO Check():BOOL
    if(GetFieldValue(PNFLD_NPTXCTRL_YEAR) < 2023)
      msgbox("Год не может быть меньше 2023");
      return false;
    end;

    if((GetFieldValue(PNFLD_NPTXCTRL_FROMSOFR) == UNSET_CHAR) and (GetFieldValue(PNFLD_NPTXCTRL_FROMDEPO) == UNSET_CHAR))
      msgbox("Необходимо указать систему, по данным которой формируется отчет");
      return false;
    end;

    return true;
  END;

  MACRO Run()
    var stat = Run();

    m_Period          = GetFieldValue(PNFLD_NPTXCTRL_PERIOD);
    m_Year            = GetFieldValue(PNFLD_NPTXCTRL_YEAR);
    m_EndDate         = GetFieldValue(PNFLD_NPTXCTRL_ENDDATE);
    m_ClientID        = GetFieldValue(PNFLD_NPTXCTRL_CLIENTCODE);
    m_FromSOFR        = GetFieldValue(PNFLD_NPTXCTRL_FROMSOFR);
    m_FromDEPO        = GetFieldValue(PNFLD_NPTXCTRL_FROMDEPO);
    m_IsCurBrocAcc    = GetFieldValue(PNFLD_NPTXCTRL_SHOWCURBROACC);
    m_IsActiveClient  = GetFieldValue(PNFLD_NPTXCTRL_ISACTIVECLIENT);
    m_ByChanges       = GetFieldValue(PNFLD_NPTXCTRL_BYCHANGES);
    m_ChBegDate       = GetFieldValue(PNFLD_NPTXCTRL_CHANGEBEGDATE);
    m_ChEndDate       = GetFieldValue(PNFLD_NPTXCTRL_CHANGEENDDATE);
    m_PrintMethod     = GetFieldValue(PNFLD_NPTXCTRL_PRINTMETHOD);
    
    return stat;
  END;

  initDL_CPanel( "sp_rep.lbr", "nptxctrl" );
END;