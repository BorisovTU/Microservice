/*
$Name:        SCImpRateNSD.mac
$Module:      Ценные бумаги
$Description: Импорт котировок Ценового центра НРД
*/
IMPORT "XMLLoader.mac", "SCImpRate.mac", "Rs_json.mac";

PRIVATE VAR   FlagCtrlBrk = false;
PRIVATE CONST CtrlBrkErr  = 17;
PRIVATE CONST share       = "Акция";
PRIVATE CONST bond        = "Облигация";


PRIVATE CLASS (SC_ImportRate) SC_ImportRateNSD()

  PRIVATE CLASS DataRates()
     var nsd_code:string,
         reg_number:string,
         isin:string,
         rate:double,
         price:double,
         currency_code:string,
         //dflt_flg:string,
         type:string,
         val_date:date;

     macro Init()
        rate = price = 0.0;
        nsd_code = reg_number = isin = currency_code = /*dflt_flg = */type = "";
        val_date = date(0,0,0);
     end;

     macro InitRecords()
        rate = price = 0.0;
        nsd_code = reg_number = isin = currency_code = /*dflt_flg = */type = "";
     end;
  END;

  PRIVATE VAR Rates = DataRates();

  /* настраиваемый пользователем макрос формирования имен файла котировок */
  MACRO GetDataFileName( datafilepath:@string, fname:@string ):BOOL
     var ErrStr = "";

     fname = "";
     datafilepath = GetRegImportDir("", @ErrStr, "SECUR\\NSD-CC_PATH");

     if( ErrStr != "" )
        MsgBox(ErrStr);
        return false;
     end;

     var datafilemask = "*.json";
     var List = TDirList(datafilepath + datafilemask, "f");

     List.Sort(2);

     if( List.Count == 0 )
        MsgBox("Ошибка открытия файла данных. Не найден файл по маске " + datafilepath + datafilemask);
        return false;
     end;
     fname = List.name(0);

     return true;
  END;

  PRIVATE MACRO GetKindSetCourse( IsRelative:string )
     return IIF(IsRelative == SET_CHAR, "%", "ВН");
  END;

  PRIVATE MACRO S( Str )
     return IIF(Str == "", "chr(1)", "?");
  END;

  PRIVATE MACRO AddLineToTMP( Type:string, NSDCode:string, ISIN:string, RegN:string, FiCode:string, KindSetCourse:string, OldCourse:string, NewCourse:string, DfltFlg:string, Msg:string )

     var exec = RSDCommand( " insert into dscimprate_tmp ( t_Type, t_NSDCode, t_ISIN, t_RegN, t_FiCode, t_KindSetCourse, t_OldCourse, t_NewCourse, t_DfltFlg, t_Message ) " +
                            "                     values (" + S(Type) + ", " + S(NSDCode) + ", " + S(ISIN) + ", " + S(RegN) + ", " + S(FiCode) + ", " + S(KindSetCourse) + ", ?, ?, " + S(DfltFlg) + ", " + S(Msg) + ") "
                          );

     if( Type != "" )
        exec.addParam("", RSDBP_IN, Type);
     end;
     if( NSDCode != "" )
        exec.addParam("", RSDBP_IN, NSDCode);
     end;
     if( ISIN != "" )
        exec.addParam("", RSDBP_IN, ISIN);
     end;
     if( RegN != "" )
        exec.addParam("", RSDBP_IN, RegN);
     end;
     if( FiCode != "" )
        exec.addParam("", RSDBP_IN, FiCode);
     end;
     if( KindSetCourse != "" )
        exec.addParam("", RSDBP_IN, KindSetCourse);
     end;
     exec.addParam("", RSDBP_IN, OldCourse);
     exec.addParam("", RSDBP_IN, NewCourse);
     if( DfltFlg != "" )
        exec.addParam("", RSDBP_IN, DfltFlg);
     end;
     if( Msg != "" )
        exec.addParam("", RSDBP_IN, Msg);
     end;
     exec.execute();

  OnError(ErObj)
     Error("Ошибка вставки записи протокола");
  END;

  PRIVATE MACRO GetCCY(fiid)
     var ccy = "", cmd, ds;

     cmd = RSDCommand(" select t_CCY from dfininstr_dbt where t_FIID = ? ");
     cmd.addParam("", RSDBP_IN, fiid);
     cmd.execute();

     ds = TRsbDataSet(cmd);
     if( ds.MoveNext() )
        ccy = ds.ccy;
     end;
     
     return ccy;
  END;

  PRIVATE MACRO RunImp():BOOL
     var RateDef  = TRecHandler("ratedef");
     var QuotFIID = -1, FinError, CatErr = 0, Rate:double = 0.0, OldRate:double = 0.0, InsertRate:bool = false, DfltFlg:string = "", IsCatErr:bool = false;
     var FI_Code:string = "", FI_CodeKind:integer = -1, FaceValueCCY = "";
     var ДатаКурса:date = Rates.val_date;

     PRIVATE MACRO GetValP8P9(val:double)
        if( val != 0.0 )
           return ЧислоCЗаданнойТочностью(val, 6) + IIF(m_RateParm.rec.IsRelative == SET_CHAR, "", " " + FaceValueCCY);
        else
           return "";
        end;
     END;

     m_AllRecords = m_AllRecords + 1;

     m_RateParm.Clear();

     m_RateParm.rec.Type = ПолучитьВидКурса(RATETYPE_SSWITHOUTNKD);

     /* Базовый инструмент */
     if( (Rates.nsd_code != NULL) and (Rates.nsd_code != "") )
        if( ПолучитьФинИн(Rates.nsd_code, m_BaseFI, m_BaseAvoir, NULL, NULL, FICK_NRD) != 0 )
           //Warning("В ГКБО не найден ФИ с кодом \"" + Rates.nsd_code + "\" вида " + FICK_NRD);
        else
           FI_Code     = Rates.nsd_code;
           FI_CodeKind = FICK_NRD;
        end;
     end;
     if( (FI_Code == "") and (Rates.isin != NULL) and (Rates.isin != "") )
        if( ПолучитьФинИн(Rates.isin, m_BaseFI, m_BaseAvoir, NULL, NULL, FICK_ISIN) != 0 )
           //Warning("В ГКБО не найден ФИ с кодом \"" + Rates.isin + "\" вида " + FICK_ISIN);
        else
           FI_Code     = Rates.isin;
           FI_CodeKind = FICK_ISIN;
        end;
     end;
     if( (FI_Code == "") and (Rates.reg_number != NULL) and (Rates.reg_number != "") )
        if( ПолучитьФинИн(Rates.reg_number, m_BaseFI, m_BaseAvoir, NULL, NULL, FICK_LSIN) != 0 )
           //Warning("В ГКБО не найден ФИ с кодом \"" + Rates.reg_number + "\" вида " + FICK_LSIN);
        else
           FI_Code     = Rates.reg_number;
           FI_CodeKind = FICK_LSIN;
        end;
     end;
     if( FI_Code == "" ) /*не считаем ошибкой*/
        return true;
     end;
     m_RateParm.rec.OtherFI = m_BaseFI.rec.FI_Code;

     /* Установить категорию "Дефолтная"*/
     if( /*ИмпортСведенийОДефолтеЦБ()*/false )

        var CurVal, NewVal = IIF(Rates.dflt_flg == "Y", 2, 1);

        if( (((not GetMainObjAttr(null, OBJTYPE_AVOIRISS, L_Z(m_BaseFI.rec.FIID, 10), OBJGROUP_AVRDEFAULT, CurVal, null, null, ДатаКурса)) or (ValType(CurVal) == V_UNDEF)) and (NewVal == 2)) or
            ((ValType(CurVal) != V_UNDEF) and (CurVal != NewVal))
          )
           DfltFlg = Rates.dflt_flg;
           if( (not ConnectObjAttr(CatErr, OBJTYPE_AVOIRISS, L_Z(m_BaseFI.rec.FIID, 10), OBJGROUP_AVRDEFAULT, NewVal, null, null, ДатаКурса))
               OR
               (CatErr != 0)
             )
              IsCatErr = true;
           end;
        end;
     end;

     /* Котируемый инструмент */
     if( (Rates.currency_code != NULL) AND (Rates.currency_code != "") )
        QuotFIID = ПолучитьКодФинИн(Rates.currency_code, FinError, FICK_ISOSTRING);
        if( (FinError != 0) OR (QuotFIID < 0) )
           AddLineToTMP(Rates.type, Rates.nsd_code, Rates.isin, Rates.reg_number, m_BaseFI.rec.FI_Code, "", "", "", DfltFlg, "В ГКБО не найден ФИ с кодом \"" + Rates.currency_code + "\" вида " + FICK_ISOSTRING);
           return 1;
        end;
     else
        QuotFIID = m_BaseFI.rec.FaceValueFI;
     end;
     m_RateParm.rec.FI_Code = ПолучитьКодФинИн(QuotFIID, FinError, FICK_USERCODE);
     if( (FinError != 0) OR (m_RateParm.rec.FI_Code == "") )
        AddLineToTMP(Rates.type, Rates.nsd_code, Rates.isin, Rates.reg_number, m_BaseFI.rec.FI_Code, "", "", "", DfltFlg, "Не найден котируемый ФИ (FIID = " + QuotFIID + ")." + GetErrMsg());
        return 1;
     end;

     m_RateParm.rec.SinceDate = ДатаКурса;
     m_RateParm.rec.Scale = 1;
     m_RateParm.rec.Point = 6;

     if( ПолучитьКурс(RateDef, m_RateParm.rec.FI_Code, m_RateParm.rec.OtherFI, m_RateParm.rec.Type) != 0 )
        InsertRate = true;

        if( ((Rates.type == share) and (Rates.price != 0.0)) or
            ((Rates.type == bond) and (Rates.rate == 0.0) and (Rates.price != 0.0))
          )
           m_RateParm.rec.IsRelative = UNSET_CHAR;
        elif( ((Rates.type == share) and (Rates.price == 0.0) and (Rates.rate != 0.0)) or
              ((Rates.type == bond) and (Rates.rate != 0.0))
            )
           m_RateParm.rec.IsRelative = SET_CHAR;
        else
           AddLineToTMP(Rates.type, Rates.nsd_code, Rates.isin, Rates.reg_number, m_BaseFI.rec.FI_Code, "", "", "", DfltFlg, "В файле не указаны значения курса");
           return 1;
        end;
     else
        if( (ПолучитьЗначениеКурса(RateDef, m_RateParm.rec.SinceDate) == 0) and
            (RateDef.rec.SinceDate == m_RateParm.rec.SinceDate)
          )
           AddLineToTMP(Rates.type, Rates.nsd_code, Rates.isin, Rates.reg_number, m_BaseFI.rec.FI_Code, GetKindSetCourse(RateDef.rec.IsRelative), "", "", DfltFlg, "Значение курса на дату <" + string(m_RateParm.rec.SinceDate) + "> уже задано");
           return 1;
        elif( RateDef.rec.IsInverse == SET_CHAR )
           AddLineToTMP(Rates.type, Rates.nsd_code, Rates.isin, Rates.reg_number, m_BaseFI.rec.FI_Code, GetKindSetCourse(RateDef.rec.IsRelative), "", "", DfltFlg, "В системе курс задан с признаком обратной котировки");
           return 1;
        elif( (RateDef.rec.IsRelative == UNSET_CHAR) and (Rates.price == 0.0) )
           AddLineToTMP(Rates.type, Rates.nsd_code, Rates.isin, Rates.reg_number, m_BaseFI.rec.FI_Code, GetKindSetCourse(RateDef.rec.IsRelative), "", "", DfltFlg, "В файле нет курса в валюте номинала");
           return 1;
        elif( (RateDef.rec.IsRelative == SET_CHAR) and (Rates.rate == 0.0) )
           AddLineToTMP(Rates.type, Rates.nsd_code, Rates.isin, Rates.reg_number, m_BaseFI.rec.FI_Code, GetKindSetCourse(RateDef.rec.IsRelative), "", "", DfltFlg, "В файле нет курса в процентах от номинала");
           return 1;
        else
           m_RateParm.rec.IsRelative = RateDef.rec.IsRelative;
        end;
     end;

     if( m_RateParm.rec.IsRelative == UNSET_CHAR )
        Rate = Rates.price;
     else
        Rate = Rates.rate;
     end;

     m_RateParm.rec.Rate = floor(Rate * pow(10,m_RateParm.rec.Point)+0.5);

     if( m_RateParm.rec.IsRelative == SET_CHAR ) /* при заданном признаке отностиельной котировки валюта котировки должна совпадать с валютой номинала */
        /* если это не так выведем в лог предупреждение */
        if( m_BaseFI.rec.FaceValueFI != QuotFIID )
           AddLineToTMP(Rates.type, Rates.nsd_code, Rates.isin, Rates.reg_number, m_BaseFI.rec.FI_Code, GetKindSetCourse(m_RateParm.rec.IsRelative), "", "", DfltFlg,
                        "Задан признак относительной котировки. Валюта котировки "+GetFICode(QuotFIID) +" не совпадает с валютой номинала ц/б " + GetFICode(m_BaseFI.rec.FaceValueFI));
           return 1;
        end;
     end;

     if( УстановитьКурс(m_RateParm) != 0 ) /*внутри УстановитьКурс транзакция*/
        AddLineToTMP(Rates.type, Rates.nsd_code, Rates.isin, Rates.reg_number, m_BaseFI.rec.FI_Code, GetKindSetCourse(m_RateParm.rec.IsRelative), "", "", DfltFlg, "Ошибка при установке курса." + GetErrMsg());
        return 1;
     end;

     /* т.к. УстановитьКурс при вставке курса пока не может заполнять поля "Название курса" и "Источник информации" заполним их сами */
     if( InsertRate )
        if( ПолучитьКурс(RateDef, m_RateParm.rec.FI_Code, m_RateParm.rec.OtherFI, m_RateParm.rec.Type) == 0 )
           var sql = RSDCommand(" UPDATE dratedef_dbt " +
                                "    SET t_Name = ?, " +
                                "        t_Informator = RSB_Derivatives.DV_GetPartyIDNRD " +
                                "  WHERE t_RateID = ? " );
      
           sql.addParam("", RSDBP_IN, "СС без НКД из ЦЦ НРД");
           sql.addParam("", RSDBP_IN, RateDef.rec.RateID);
           sql.execute();
        end;
     end;

     if( (RateDef.rec.Rate != 0.0) and (RateDef.rec.Scale != 0.0) )
        OldRate = RateDef.rec.Rate/RateDef.rec.Scale/pow(10, RateDef.rec.Point);
     end;

     FaceValueCCY = GetCCY( m_BaseFI.rec.FaceValueFI );

     if( IsCatErr )
        AddLineToTMP(Rates.type, Rates.nsd_code, Rates.isin, Rates.reg_number, m_BaseFI.rec.FI_Code, GetKindSetCourse(m_RateParm.rec.IsRelative), GetValP8P9(OldRate), GetValP8P9(Rate), DfltFlg, "Ошибка при установке категории <" + ИмяКатегории(OBJTYPE_AVOIRISS, OBJGROUP_AVRDEFAULT) + ">.");
     else
        AddLineToTMP(Rates.type, Rates.nsd_code, Rates.isin, Rates.reg_number, m_BaseFI.rec.FI_Code, GetKindSetCourse(m_RateParm.rec.IsRelative), GetValP8P9(OldRate), GetValP8P9(Rate), DfltFlg, "");
     end;

     m_RatesCount = m_RatesCount + 1; 
     return true;

  OnError(ErObj)

     Error( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );

     if(ErObj.Code == CtrlBrkErr)
        FlagCtrlBrk = true;
     else
        RemProgress();
        RunError( "Модуль: " + string(ErObj.Module) + "\nСтрока: " + string(ErObj.Line) + "\nКод ошибки: " + string(ErObj.Code) + "\n" + ErObj.Message );
     end;
     return ErObj.Code;
  END;

  PRIVATE MACRO PrintRepLine( WhereMsg:string )
     var QueryErr = "", DataErr;

     QueryErr = RSDCommand( " select * " +
                            "   from dscimprate_tmp " +
                            "  where t_Message " + WhereMsg + " chr(1) " +
                            " order by t_Type, t_FiCode "
                          );

     QueryErr.execute();

     DataErr = TRsbDataSet(QueryErr);
     while( DataErr.MoveNext() )
        if( /*ИмпортСведенийОДефолтеЦБ()*/false )
           PrintTableLine( "t_A1",  DataErr.Type,          null,
                           "t_A2",  DataErr.NSDCode,       null,
                           "t_A3",  DataErr.ISIN,          null,
                           "t_A4",  DataErr.RegN,          null,
                           "t_A5",  DataErr.FiCode,        null,
                           "t_A6",  DataErr.KindSetCourse, null,
                           "t_A7",  DataErr.OldCourse,     null,
                           "t_A8",  DataErr.NewCourse,     null,
                           "t_A9",  DataErr.DfltFlg,       null,
                           "t_A10", DataErr.Message,       null
                         );
        else
           PrintTableLine( "t_A1",  DataErr.Type,          null,
                           "t_A2",  DataErr.NSDCode,       null,
                           "t_A3",  DataErr.ISIN,          null,
                           "t_A4",  DataErr.RegN,          null,
                           "t_A5",  DataErr.FiCode,        null,
                           "t_A6",  DataErr.KindSetCourse, null,
                           "t_A7",  DataErr.OldCourse,     null,
                           "t_A8",  DataErr.NewCourse,     null,
                           "t_A10", DataErr.Message,       null
                         );
        end;
     end;
  END;

  PRIVATE MACRO ПолучитьТипБумаги(type)
     var t = "oct";
     
     if( type == "share" )
        t = share;
     elif( type == "bond" )
        t = bond;
     end;

     return t;
  END;

  MACRO LoadFromFile():bool
     FILE fdata() txt;
     var json:object, strJson = "", i=0;

     SQL_Truncate("dscimprate_tmp");

     Rates.Init();

     if (open(fdata, DataFileName(), "utf8"))
        while (next (fdata))
           strJson = strJson + fdata.str;
        end;
        close (fdata);
     else
        return Error(string( "Неверный формат файла импорта|", DataFileName(), "|Невозможно загрузить json-документ" ));
     end;

    json = jsn.JsonDoc(strJson);

    if( json.length > 0 )
       InitProgress(json.length, "Загрузка внешнего файла котировок", "Просмотр записей котировок...");

       for( var val, jsn.GetValue( json ) )
          Rates.InitRecords();
          
          Rates.currency_code = val.currency_code;
          Rates.isin          = val.isin;
          Rates.nsd_code      = val.nsd_code;
          Rates.price         = double(val.price);
          Rates.rate          = double(val.rate);
          Rates.reg_number    = val.reg_number;
          Rates.type          = ПолучитьТипБумаги(val.type);
          Rates.val_date      = XML_ПолучитьДату(val.val_date);

          if( Rates.type != "otc" )
             RunImp();
          end;

          i = i + 1;
          UseProgress(i);
          if( FlagCtrlBrk == true )
             break;
          end;
       end;

       RemProgress();
    end;

     // Печатем протокол
     PrintRepLine("!=");
     PrintRepLine("=");

     return true;

  OnError( RslErrObj )
     RemProgress();RemProgress();
     MsgBox( "Модуль: ", RslErrObj.Module, "\nСтрока: ", RslErrObj.Line, "\nКод ошибки: ", RslErrObj.Code, "\n", RslErrObj.Message );
     RunError;
  END;

  initSC_ImportRate(true, "", true);
END;

PRIVATE VAR Rate = SC_ImportRateNSD();
if( Rate != NULL )
   Rate.Run();
   Rate = NULL;
end;

Exit(1);
