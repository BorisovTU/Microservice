/*
$Name: nptxwrt051.mac
$Module: Ценные бумаги
$Description: Расчет сумм (налога и вывода СНОБ) для операций списания/зачисления денежных средств
*/

import InsCarryDoc, "sp_categ.mac", "sp_car.mac", RsbDataSet, "nptxcalcfun.mac", "nptxwrt_func.mac";

private macro Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr );
  MsgBox( ErrStr );
  return 1;
end;

private macro ExistSavedVal(ID_Operation:integer, ValKind:integer)
  var query, cmd, DataSet;

  query =   " select 1 "
          + "   from dnptxval_dbt v "
          + "  where v.t_ID_Operation = ? "
          + "    and v.t_Kind = ? " 
          + "    and ROWNUM = 1 ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ID_Operation);
  cmd.AddParam(ValKind);

  if(cmd.GetCount() > 0)
    return true;
  end;

  return false;
end;

private macro UseCorrForAddKBK()
  DL_NPTX_PutMsg("X", 205);
end;

macro ExecuteStep( kind_oper, FDoc, DocKind, ID_Operation, ID_Step )
  record nptxop("nptxop");
  var nptxopFile = TRecHandLer("nptxop.dbt");
  var НОБ_опер = $0, НОБ_30 = $0, НОБ_13 = $0, НОБ_15 = $0, taxe = $0, taxe15 = $0;
  var Dg = $0, Dp = $0;
  var ToutNat = $0;
  var AccAccDS;
  var err = 0;
  var TxArr = TArray();
  var CorrTxArr = TArray();
  var Year = 0;
  var prev_taxe = $0, prev_taxe15 = $0;
  var MaxTaxe1 = $0, MaxTaxeNat = $0;
  var StartProgressScaleDate = GetStartProgressScaleDate();
  var TaxHoldArr = TArray();
  var AmountSNOB = 0;
  var query, cmd, DataSet;
  var NumberOp = "";
  var TaxRateParmMain = NULL, TaxRateParm15 = NULL;
  var i = 0;
  var SpecialTag = "";
  var TaxRateParmAddArr = TArray();

  SetBuff( nptxop, FDoc );

  if(ЕстьНеисполненнаяПорожденнаяОперацияНОБ(nptxop.ID, @NumberOp))
    if(MsgBoxEx( "Порождённая операция расчёта НОБ № "+ NumberOp +" не выполнена. Продолжить выполнение текущей операции зачисления/списания? ",
                   MB_YES+MB_NO, IND_NO) != IND_YES )
       return 1;
    end;
  end;
  
  if(nptxop.SubKind_Operation == DL_NPTXOP_WRTKIND_WRTOFF)
    
    nptxopFile.Clear();
    Copy(nptxopFile, nptxop);

    DateSplit(nptxop.OperDate, null, null, Year);
    
    SmartConvertSum(ToutNat, nptxop.OutSum, nptxop.OperDate, nptxop.Currency, NATCUR);

    MaxTaxeNat = ToutNat;

    if(nptxop.Currency != NATCUR)
      var AccAccQuery = "select t_Chapter, t_Code_Currency, t_Account from daccount_dbt where t_Account = ?";
      var AccAccSelect = DL_RSDCommand(AccAccQuery);
      
      AccAccSelect.AddParam(nptxop.AccountTax);
      AccAccDS = AccAccSelect.execute();
      if(AccAccDS.MoveNext())
        SmartConvertSum(MaxTaxe1, DL_GetRestAccount(AccAccDS, nptxop.OperDate), nptxop.OperDate, AccAccDS.Code_Currency, NATCUR);
        MaxTaxe1 = round(money(MaxTaxe1), 0, 2);

        MaxTaxeNat = min(ToutNat, MaxTaxe1);
      end;
    end;
    
    if((ПРОГРЕССИВНАЯ_ШКАЛА_ДЛЯ_НДФЛ() == true) and (StartProgressScaleDate > date(0,0,0)) and (nptxop.OperDate >= StartProgressScaleDate))
      TaxHoldArr.size = 0;

      if(ExistSavedVal(ID_Operation, NPTXVAL_KIND_TB_Oper))
        НОБ_опер = STB_GetSavedNptxval(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_TB_Oper);
        SpecialTag = GetSpecialTagByOper(nptxop, STB_GetTaxBaseKindByDocKind(nptxop.DocKind, IIF(nptxop.IIS == SET_CHAR, true, false)));
      else
        CalcNOBoper(nptxop, @НОБ_опер, NULL, NULL, @SpecialTag);
      end;
      
      AmountSNOB = STB_GetSavedNptxval(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_STB);
      
      CalcHoldTaxByRanges(nptxop.Client,
                          Year,
                          STB_GetTaxBaseKindByDocKind(nptxop.DocKind, IIF(nptxop.IIS == SET_CHAR, true, false)),
                          НОБ_опер,
                          AmountSNOB,
                          nptxop.OperDate,
                          SpecialTag
                         );

      query = " select * from dnptxtaxbaseranges_tmp order by t_range asc ";
      cmd = DL_RSDCommand(query);
      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        if(int(DataSet.TaxRate) == int(100*NPTXGENTAXRATE_RESIDENT_15))
          taxe15 = taxe15 + money(DataSet.TaxHold);
          НОБ_15 = НОБ_15 + money(DataSet.BaseSum);

          if((taxe15 < 0) and (НОБ_опер > 0))
            continue;
          end;

          if(DataSet.KBK == NPTXKBK_INCR_218)
            TaxRateParm15 = STB_TaxRateParm(int(DataSet.TaxRate),
                                            money(DataSet.BaseSum), 
                                            money(DataSet.TaxHold),
                                            DataSet.KBK, 
                                            DataSet.TaxBaseType, 
                                            IIF(nptxop.IIS == SET_CHAR, true, false), 
                                            NULL, 
                                            0, 
                                            money(DataSet.TaxCalc),
                                            NULL,
                                            NULL,
                                            DataSet.SpecialTag
                                            );
          else
            TaxRateParmAddArr[TaxRateParmAddArr.size] = STB_TaxRateParm(int(DataSet.TaxRate),
                                                                        money(DataSet.BaseSum), 
                                                                        money(DataSet.TaxHold),
                                                                        DataSet.KBK, 
                                                                        DataSet.TaxBaseType, 
                                                                        IIF(nptxop.IIS == SET_CHAR, true, false), 
                                                                        NULL, 
                                                                        0, 
                                                                        money(DataSet.TaxCalc),
                                                                        NULL,
                                                                        NULL,
                                                                        DataSet.SpecialTag
                                                                        );
          end;
        else
          taxe = money(DataSet.TaxHold);

          if((taxe < 0) and (НОБ_опер > 0))
            continue;
          end;

          if(int(DataSet.TaxRate) == int(100*NPTXGENTAXRATE_NOTRESIDENT)) 
            НОБ_30 = money(DataSet.BaseSum);
          else
            НОБ_13 = money(DataSet.BaseSum);
          end;

          TaxRateParmMain = STB_TaxRateParm(int(DataSet.TaxRate),
                                            money(DataSet.BaseSum), 
                                            money(DataSet.TaxHold),
                                            DataSet.KBK, 
                                            DataSet.TaxBaseType, 
                                            IIF(nptxop.IIS == SET_CHAR, true, false), 
                                            NULL, 
                                            0, 
                                            money(DataSet.TaxCalc),
                                            NULL,
                                            NULL,
                                            DataSet.SpecialTag
                                            );
        end;
      end;

      Dg = taxe;
      Dp = taxe15;
      
      if((НОБ_опер <= 0) and ((taxe + taxe15 <= 0)))
        taxe = 0;
        taxe15 = 0;
        НОБ_13 = 0;
        НОБ_30 = 0;
        НОБ_15 = 0;

        if((TaxRateParmMain != NULL) and (TaxRateParmMain.Налог != 0))
          TaxRateParmMain.Налог = 0;
          TaxRateParmMain.TaxCalc = 0;
          TaxHoldArr[TaxHoldArr.size] = TaxRateParmMain;
        end;

        if((TaxRateParm15 != NULL) and (TaxRateParm15.Налог != 0))
          TaxRateParm15.Налог = 0;
          TaxRateParm15.TaxCalc = 0;
          TaxHoldArr[TaxHoldArr.size] = TaxRateParm15;
        end;
      else
        if(TaxRateParmMain != NULL)
          TaxHoldArr[TaxHoldArr.size] = TaxRateParmMain;
        end;

        if(TaxRateParm15 != NULL)
          TaxHoldArr[TaxHoldArr.size] = TaxRateParm15;
        end;
      end;

      if((ToutNat != NULL) and (ToutNat != 0))
        if(nptxop.CloseContr == SET_CHAR)
          if(ToutNat < (taxe + taxe15))
            if(taxe15 > 0)
              taxe15 = min(taxe15, max(0, ToutNat-taxe));
            end;
            if(taxe > 0)
              taxe = min(taxe, ToutNat-taxe15);
            end;
          end;
        end;
      end;

      if(nptxop.Currency != NATCUR)
        taxe   = MIN(taxe, MaxTaxe1);
        taxe15 = MIN(taxe15, MaxTaxe1 - taxe);
      end;


      prev_taxe   = MAX(taxe, $0);
      prev_taxe15 = MAX(taxe15, $0);

      i = 0;
      while(i < TaxHoldArr.size)
        if(TaxHoldArr[i].Ставка == int(100*NPTXGENTAXRATE_RESIDENT_15))
          TaxHoldArr[i].Налог = taxe15;
        else
          TaxHoldArr[i].Налог = taxe;
        end;

        i = i + 1;
      end;

      STB_ExecuteCorrOnStepPRGS(nptxop.Client, Year, IIF(nptxop.IIS == SET_CHAR, true, false), @TaxHoldArr, nptxop.DocKind, nptxop.ID, nptxop.OperDate, MaxTaxeNat, MaxTaxeNat, @CorrTxArr, nptxop.CloseContr, false, NULL, NULL, SpecialTag);

      taxe = 0;
      taxe15 = 0;

      i = 0;
      while(i < TaxHoldArr.size)
        if(TaxHoldArr[i].Ставка == int(100*NPTXGENTAXRATE_RESIDENT_15))
          taxe15 = TaxHoldArr[i].Налог;
        else
          taxe = TaxHoldArr[i].Налог;
        end;

        i = i + 1;
      end;
      
    else
      CalcTaxSTB(nptxop, @НОБ_опер, @НОБ_30, @НОБ_13, @НОБ_15, @taxe, @taxe15, ToutNat, NULL, @Dg, @Dp);
    
      if(nptxop.Currency != NATCUR)
        taxe   = MIN(MAX(taxe, $0), MaxTaxe1);
        taxe15 = MIN(MAX(taxe15, $0), MaxTaxe1 - taxe);
      end;

      prev_taxe   = taxe; 
      prev_taxe15 = taxe15;

      STB_ExecuteCorrOnStep(MaxTaxeNat, MaxTaxeNat, НОБ_30, НОБ_13, НОБ_15, @taxe, @taxe15, nptxop.DocKind, nptxop.ID, nptxop.OperDate, IIF(nptxop.IIS == SET_CHAR, true, false), nptxop.Client, Year, @CorrTxArr, nptxop.CloseContr, Dg, Dp);
    
    end;


    if(nptxop.Currency != NATCUR)
      if(ToutNat < (taxe + taxe15))
        taxe = round(min(max(taxe, 0), ToutNat*NPTXGENTAXRATE_RESIDENT_15), 0, 2);
        taxe15 = round(min(taxe15, round(ToutNat*NPTXGENTAXRATE_RESIDENT_15, 0) - taxe), 0, 2);
      end;

      taxe   = MIN(MAX(Round(taxe, 0, 2), $0), MaxTaxe1);
      taxe15 = MIN(MAX(Round(taxe15,0, 2), $0), MaxTaxe1 - taxe);
    else
      taxe   = Round(taxe, 0, 2);
      taxe15 = Round(taxe15, 0, 2);
      prev_taxe   = Round(prev_taxe, 0, 2);
      prev_taxe15 = Round(prev_taxe15, 0, 2);
    end;

    nptxopFile.rec.Tax   = taxe + taxe15;
    nptxopFile.rec.TaxDp = taxe15;

    //Временное решение. Если отсутствуют корректировки, то кладем туда 224 КБК.
    //Т.е. у нас либо 224 КБК, либо корректировки
    if((Dg == taxe) and (Dp == taxe15) and (CorrTxArr.size == 0) and (TaxRateParmAddArr.size > 0)) //DEF-108625
      i = 0;
      UseCorrForAddKBK();//Признак дополнительных КБК в корректировках
      while(i < TaxRateParmAddArr.size)
        taxe15 = taxe15 - TaxRateParmAddArr[i].Налог;
        prev_taxe15 = prev_taxe15 - TaxRateParmAddArr[i].Налог;
        НОБ_15 = НОБ_15 - TaxRateParmAddArr[i].НОБ;
        CorrTxArr[CorrTxArr.size] = TaxRateParmAddArr[i];
        i = i + 1;
      end;
    end;

    SmartConvertSum(nptxopFile.rec.Tout, (ToutNat - nptxopFile.rec.Tax), nptxopFile.rec.OperDate, NATCUR, nptxopFile.rec.Currency);

    if(nptxopFile.rec.Currency != NATCUR)
      nptxopFile.rec.TaxSum2 = nptxopFile.rec.OutSum;
    else
      if(nptxopFile.rec.Tax > 0)
         nptxopFile.rec.TaxSum2 = nptxopFile.rec.OutSum - nptxopFile.rec.Tax;
      else
         nptxopFile.rec.TaxSum2 = nptxopFile.rec.OutSum;
      end;
    end;

    if(nptxopFile.rec.ID > 0)
      if( DL_NPTX_UpdateSumm( nptxopFile ) != 0 )
        return Error( "Ошибка при обновлении сумм в dnptxop_dbt" );
      end;

      if(DL_InsertNPTXVAL(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_TB_Oper, date(0,0,0), time(0), НОБ_опер, 0) != 0)
        return Error( "Ошибка при сохранении значения СНОБ" );
      end;

      if(DL_InsertNPTXVAL(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_TB_30, date(0,0,0), time(0), НОБ_30, 0) != 0)
        return Error( "Ошибка при сохранении значения СНОБ" );
      end;

      if(DL_InsertNPTXVAL(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_TB_13, date(0,0,0), time(0), НОБ_13, 0) != 0)
        return Error( "Ошибка при сохранении значения СНОБ" );
      end;

      if(DL_InsertNPTXVAL(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_TB_15, date(0,0,0), time(0), НОБ_15, 0) != 0)
        return Error( "Ошибка при сохранении значения СНОБ" );
      end;

      if(DL_InsertNPTXVAL(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_TAXE, date(0,0,0), time(0), IIF(taxe < prev_taxe, taxe, prev_taxe), 0) != 0)
        return Error( "Ошибка при сохранении значения СНОБ" );
      end;

      if(DL_InsertNPTXVAL(nptxop.DocKind, nptxop.ID, NPTXVAL_KIND_TAXE_15, date(0,0,0), time(0), IIF(taxe15 < prev_taxe15, taxe15, prev_taxe15), 0) != 0)
        return Error( "Ошибка при сохранении значения СНОБ" );
      end;
    end;

    if(DL_InsertDL_VALUE(nptxop.DocKind, nptxop.ID, DL_VALUE_KIND_NPTXHOLD_PREV_DS, nptxop.OperDate, 0, NATCUR, 0) != 0)
      return Error( "Ошибка при сохранении значения Ds" );
    end;
    if(DL_InsertDL_VALUE(nptxop.DocKind, nptxop.ID, DL_VALUE_KIND_NPTXHOLD_PREV_DG, nptxop.OperDate, Dg, NATCUR, 0) != 0)
      return Error( "Ошибка при сохранении значения Dg" );
    end;
    if(DL_InsertDL_VALUE(nptxop.DocKind, nptxop.ID, DL_VALUE_KIND_NPTXHOLD_PREV_DP, nptxop.OperDate, Dp, NATCUR, 0) != 0)
      return Error( "Ошибка при сохранении значения Dp" );
    end;

    if(CorrTxArr.size > 0)
      STB_SaveTxArrInMsg(CorrTxArr);

      DL_NPTX_SaveOperLog( nptxop.ID, 51 );
    end;
  end;

  return err;
end;