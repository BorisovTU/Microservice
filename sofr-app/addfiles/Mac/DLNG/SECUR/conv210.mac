/*
$Name:        conv210.mac
$Module:      Ценные бумаги
$Description: Операция конвертации ц/б. Шаг списания стоимости
*/
import InsCarryDoc, "sp_categ.mac", "scservop.mac", "sp_car.mac", "sp_cars.mac";

macro ExecuteStep( doc, FDoc)
  record deal(dl_tick);
  var    FIRoles = TArray();
  var    FD_in, FD_out, dat;
  var    CostBuy = TArray(), OverAmountBuy = $0, ReservAmountBuy = TArray();
  var    _CostBuy = $0, _OverAmountBuy = $0, _ReservAmountBuy = $0;
  var    i = 0, k = 0;
  var    WriteOffGroups = TArray();

  SetBuff( deal, FDoc );
  FD_out = SPFirstDoc( deal );/*списание*/

  GetOprDate( FD_out.GetKindDate(DATE_CONVAVR_OUT), dat );
  if( dat > {curdate} )
     MsgBox ("Преждевременное выполнение шага запрещено.");
     return 1;
  end;

  if( ПолучитьГруппыСписания( FD_out, WriteOffGroups ) == false )
     return SayErrorBuySale( deal, "Ошибка при определении групп списания" );
  end; 

  while( k < WriteOffGroups.size )
     WRT_GetSaleFinResult( FD_out.pmwrtsum.rec.SumID, WriteOffGroups[k], 0, _CostBuy, null,
                           null, null, null, null, null, null, null, null, null, null, null, 
                           null, null, null, null, _OverAmountBuy, _ReservAmountBuy
                         );
     CostBuy[k] = _CostBuy;
     ReservAmountBuy[k] = _ReservAmountBuy;
     if( WriteOffGroups[k] == KINDPORT_SALE )
        OverAmountBuy = _OverAmountBuy;
     end;

     k = k + 1;
  end;

  k = 0;
  FD_in = SPFirstDoc( deal, true );/*зачисление*/
  if( OprInsertSPTKCHNG( dat, SPTKCHNG_RECALC, FD_out.tick, FD_out.dl_leg, FD_in.dl_leg ) == false )
     return 1;
  end;

  if( not IsClientDeal(deal) ) /*собственные сделки*/
     /*списание ц\б*/
     while( WriteOffGroups[k] != null )

        if( not ПроводкаПоКатегориямУчета( FD_out, 
                                           "+Форвард, расчеты", "Реализация, ц/б",  /* КатегДеб , КатегКредит*/
                                           dat,              /* Дата */
                                           1 ,               /* Глава */
                                           iif((WriteOffGroups[k] == KINDPORT_CONTR), NATCUR, FD_out.FaceFIID()), /* Валюта */
                                           abs(CostBuy[k]),  /* балансовая стоимость списываемых лотов (включая затраты на приобретение) */
                                           Doc,              /* Документ */
                                           SPTRANSFER,       /* Result_Carry */
                                           " 1",             /* Kind_Oper */
                                           "",               /* Numb_Document */
                                           "Сумма списанной балансовой стоимости", /* Ground */
                                           null, null, null, FD_out.FaceFIID
                                         )
          )
           return SayErrorBuySale( FD_out.tick.rec, SC_GetCarryError() );
        end;

        if( not ПроводкаПоКатегориямУчета( FD_out,
                                           "Реализация, ц/б", iif((WriteOffGroups[k] == KINDPORT_CONTR), "Наш портфель ПКУ, ц/б", "Наш портфель ц/б"),  /* КатегДеб , КатегКредит */
                                           dat,                      /* Дата */
                                           1 ,                       /* Глава */
                                           iif((WriteOffGroups[k] == KINDPORT_CONTR), NATCUR, FD_out.GetAccFIID("Наш портфель ц/б") ), /* Валюта */
                                           abs(CostBuy[k]),          /* балансовая стоимость списываемых лотов (включая затраты на приобретение) */
                                           Doc,                      /* Документ */
                                           SPTRANSFER,               /* Result_Carry */
                                           " 1",                     /* Kind_Oper */
                                           FD_out.tick.rec.DealCode, /* Numb_Document */
                                           "Сумма списанной балансовой стоимости из портфеля", /* Ground */
                                           null, null,
                                           ПолучитьРольДляКонвертацииДР_ПоПортфелю(WriteOffGroups[k]),
                                           NATCUR, iif((WriteOffGroups[k] == KINDPORT_CONTR), NATCUR, FD_out.GetAccFIID("Наш портфель ц/б"))
                                         )
          )
           return SayErrorBuySale(FD_out.tick.rec, SC_GetCarryError());
        end; 

        FIRoles.Size = 0;
        ПолучитьРолиКатегорийРезерваДляКонвертацииДР( WriteOffGroups[k], FIRoles );
        i = 0;
        while( i < FIRoles.Size )
           if( not ПроводкаПоКатегориямУчета( FD_out, 
                                              "Резерв ц/б", iif((WriteOffGroups[k] == KINDPORT_CONTR), "+РС, д/с", "+РС,ц/б"),  /* КатегДеб , КатегКредит*/
                                              dat,                        /* Дата */
                                              1 ,                         /* Глава */
                                              NATCUR,                     /* Валюта */
                                              abs(ReservAmountBuy[k]),    /* Списанная сумма резерва (Сумма созданного резерва)*/
                                              Doc,                        /* Документ */
                                              SPTRANSFER,                 /* Result_Carry */
                                              " 1",                       /* Kind_Oper */
                                              "",                         /* Numb_Document */
                                              "Сумма списанного резерва", /* Ground */
                                              null, FIRoles[i], null, NATCUR, NATCUR
                                            )
             )
              return SayErrorBuySale( FD_out.tick.rec, SC_GetCarryError() );
           end;
           i = i + 1;
        end;

        k = k + 1;
     end;

     /*Списание лота из ППР*/
     if( ВыполнитьСписаниеПереоценки( FD_out, KINDPORT_SALE, OverAmountBuy, dat ) == false) 
        return SayErrorBuySale( FD_out.tick.rec, SC_GetCarryError() );
     end;

     if(SetOverRegistrValue(FD_out, FD_out.fininstr.rec.FIID, dat) != 0)
       return SayErrorBuySale(FD_out.tick.rec, "Ошибка при сохранении сумм в регистре переоценки");
     end;
  end;

  return 0;
end;
