/*
$Name:             ws_txreplavoir.mac
$Module:           АРНУ
$Description:      WEB. АРНУ. Макрос сервисов скроллинга ц/б для репликации
*/

import FIInter;
import "ws_common.mac";
import "ws_orderbygen.mac";
import "ws_datafilter.mac";

macro TxReplAvoirissRunError(err:Variant, stat:Integer)
  WSRunError("ws_txreplavoir.mac", err, stat);
end;

// Класс скроллинга Ц/Б для репликации
class CTxReplAvoirissList()
  var AvoirissID:String;       // Идентификатор
  var InstanceDate:DateTime;   // Дата загрузки
  var Action:Integer;          // Действие
  var ActionName:String;       // Наименование действия
  var ReplState:Integer;       // Статус репликации
  var ReplStateName:String;    // Статус репликации
  var Code:String;             // Код
  var CodeInAcc:String;        // Код в номере счёта
  var Kind:Integer;            // Вид ценной бумаги
  var KindName:String;         // Вид ценной бумаги
  var ShortName:String;        // Краткое наименование
  var Definition:String;       // Полное наименование
  var InvstName:String;        // Наименование инвест. фонда (для паёв)
  var Settlement_Code:Integer; // Форма
  var SubKind:Integer;         // Вид облигации по справочнику видов облигаций
  var SubKindName:String;      // Вид облигации по справочнику видов облигаций
  var Emissive:Integer;        // Признак эмиссионной ц/б
  var EmissiveName:String;     // Признак эмиссионной ц/б
  var TaxGroup:Integer;        // Группа НУ
  var InvstType:Integer;       // Тип ПИФ
  var LSIN:String;             // Рег. номер
  var ISIN:String;             // Ид. номер
  var IssuerID:String;         // Эмитент
  var Issued:Date;             // Дата выпуска
  var InCirculationDate:Date;  // Дата ввода в обращение
  var DrawingDate:Date;        // Дата погашения плановая
  var Qty:Money;               // Объем выпуска
  var FaceValue:Money;         // Номинал
  var FaceValueFIID:String;    // Идентификатор валюты номинала
  var FaceValueCurName:String; // Валюта номинала
  var FirstPrice:Money;        // Цена первичного размещения
  var IncomeRate:Money;        // Годовой доход
  var BaseFIID:String;         // Ссылка на базовый выпуск
  var NkdBase_Kind:Integer;    // Вид базиса расчета НКД
  var NkdBase_Name:String;     // Вид базиса расчета НКД
  var NkdRound_Kind:Integer;   // Вид округления при расчете НКД
  var NkdRound_Name:String;    // Вид округления при расчете НКД
  var NkdNonStandart:Integer;  // Признак наличия нестандартных правил расчета купонных выплат
  var SumPrecision:Integer;    // Точность суммы
  var Scale:Integer;           // Масштаб курса
  var Point:Integer;           // Точность округления
  var Version:Integer;         // Версия записи
end;

class TWsReplAvoirListItem()
  var AvoirissID:String;
  var InstanceDate:DateTime;
  var Code:String;
  var Name:String;
  var Kind:Integer;
  var KindName:String;
  var Issuer:String;
end;

class TWsReplAvoirFltrParm()
  var FindAvoirissID:String;
  var LikeAvoirCode:String;
  var AvoirCode:String;
end;

private macro GetSQLAvoirKindCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLAvoirKindConditionEx("txavoiriss", "t_Kind", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " RSB_FIInstr.FI_AvrKindsEQ(" + FIKIND_AVOIRISS + ", 0, txavoiriss.t_Kind) = 1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLGNUCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLLlValuesConditionEx("txavoiriss", "t_TaxGroup", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txavoiriss.t_Taxgroup = 0 ";
  end;

  return strSQLCond;
end;

private macro GetReplAvoirissAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String
  var fp = null;

  fp = FilterParser(FilterItems);

  fp.AddFieldCondition(0, "txavoiriss", "t_InstanceDate");         // Записи за период
  fp.AddFieldCondition(1, "txavoiriss", "t_ReplState");            // Статус записи
  fp.AddFieldCondition(2, "txavoiriss", "t_Action");               // Вид действия
  fp.AddFieldCondition(3, "txavoiriss", "t_AvoirissID", " = -1 "); // Идентификатор записи
  fp.AddUserFieldCondition(4, @GetSQLAvoirKindCondition);          // Вид ценной бумаги
  fp.AddFieldCondition(5, "txavoiriss", "t_Code");                 // Код
  fp.AddFieldCondition(6, "txavoiriss", "t_Definition");           // Полное наименование
  fp.AddFieldCondition(7, "txavoiriss", "t_Issued");               // Дата выпуска
  fp.AddFieldCondition(8, "txavoiriss", "t_DrawingDate");          // Дата погашения плановая
  fp.AddFieldCondition(9, "txavoiriss", "t_LSIN");                 // Рег. номер
  fp.AddFieldCondition(10, "txavoiriss", "t_ISIN");                // Ид. номер
  fp.AddFieldCondition(11, "txavoiriss", "t_IssuerID", " = -1 ");  // Эмитент
  fp.AddUserFieldCondition(12, @GetSQLGNUCondition);               // Группа налогового учета

  return fp.GetFullSQLCondition();
end;

private macro GetReplAvoirissSortOrder(
  OrderItems//:TArray of OrderItem // Параметры сортировки
 ,DefaultSQLSortOrder:String       // Строка сортировки по умолчанию
):String
  var ordrGen = null;

  ordrGen = OrderByGenerator(OrderItems, DefaultSQLSortOrder, false, true);

  return ordrGen.GetFullSQLSortOrder();
end;

// Получить общее количество записей скроллинга Ц/Б
macro TxGetReplAvoirissCount(
  FltrParm//:TWsReplAvoirFltrParm             // Значения фильтрации
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
):Integer
  var Query = "", AddWhere = "";
  var Count = 0;

  Query = " SELECT "
            + " 1 "
        + " FROM "
            + " dtxavoiriss_dbt txavoiriss "
        + " WHERE "
            + " 1 = 1 ";

  if(FltrParm != null)
    if((FltrParm.FindAvoirissID != null) and (FltrParm.FindAvoirissID != ""))
      Query = Query + " AND txavoiriss.t_AvoirissID = " + FltrParm.FindAvoirissID + " ";
    end;

    if(FltrParm.LikeAvoirCode != null)
      Query = Query + " AND TO_CHAR(txavoiriss.t_AvoirissID) LIKE '%" + FltrParm.LikeAvoirCode + "%' ";
    end;

    if(FltrParm.AvoirCode != null)
      Query = Query + " AND txavoiriss.t_Code = '" + FltrParm.FindAvoirissID + "' ";
    end;
  end;

  AddWhere = GetReplAvoirissAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  Count = SQL_GetNRecs(Query);

  return Count;

OnError(err)
  TxReplAvoirissRunError(err, -1);
end;

private class (RsdRecordset) CReplAvoirissSet(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FltrParm//:TWsReplAvoirFltrParm             // Значения фильтрации
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
)
  private var m_Query = "", m_AddWhere = "", m_OrderBy = "", m_Cmd = null;

  macro GetSortOrder(
    OrderItems//:TArray of OrderItem // Параметры сортировки
  ):String
    return GetReplAvoirissSortOrder(OrderItems, " txavoiriss.t_AvoirissID ASC, txavoiriss.t_InstanceDate ASC, txavoiriss.t_Action ASC, txavoiriss.t_ReplState ASC ");
  end;

  if(PageNum == null)
    RunError("Не задан обязательный параметр функции: номер страницы");
  end;

  if(PageSize == null)
    RunError("Не задан обязательный параметр функции: размер страницы");
  end;

  m_Query = " SELECT "
              + " TO_CHAR(txavoiriss.t_AvoirissID) AS t_AvoirissID "
             + " ,txavoiriss.t_InstanceDate "
             + " ,txavoiriss.t_Action "
             + " ,NVL((SELECT "
                       + " namealg.t_szNameAlg "
                   + " FROM "
                       + " dnamealg_dbt namealg "
                   + " WHERE "
                       + " namealg.t_iTypeAlg = 7643 "
                   + " AND namealg.t_iNumberAlg = txavoiriss.t_Action), CHR(0)) AS t_ActionName "
             + " ,txavoiriss.t_ReplState "
             + " ,NVL((SELECT "
                       + " namealg.t_szNameAlg "
                   + " FROM "
                       + " dnamealg_dbt namealg "
                   + " WHERE "
                       + " namealg.t_iTypeAlg = 7642 "
                   + " AND namealg.t_iNumberAlg = txavoiriss.t_ReplState), CHR(0)) AS t_ReplStateName "
             + " ,txavoiriss.t_Code "
             + " ,txavoiriss.t_CodeInAcc "
             + " ,txavoiriss.t_Kind "
             + " ,NVL((SELECT "
                       + " avrkind.t_Name "
                   + " FROM "
                       + " davrkinds_dbt avrkind "
                   + " WHERE "
                       + " avrkind.t_FI_Kind = " + FIKIND_AVOIRISS + " "
                   + " AND avrkind.t_AvoirKind = txavoiriss.t_Kind), CHR(0)) AS t_KindName "
             + " ,txavoiriss.t_ShortName "
             + " ,txavoiriss.t_Definition "
             + " ,txavoiriss.t_InvstName "
             + " ,txavoiriss.t_Settlement_Code "
             + " ,txavoiriss.t_SubKind "
             + " ,NVL((SELECT "
                       + " namealg.t_szNameAlg "
                   + " FROM "
                       + " dnamealg_dbt namealg "
                   + " WHERE "
                       + " namealg.t_iTypeAlg = 7651 "
                   + " AND namealg.t_iNumberAlg = txavoiriss.t_SubKind), CHR(0)) AS t_SubKindName "
             + " ,txavoiriss.t_Emissive "
             + " ,NVL((SELECT "
                       + " namealg.t_szNameAlg "
                   + " FROM "
                       + " dnamealg_dbt namealg "
                   + " WHERE "
                       + " namealg.t_iTypeAlg = 3581 "
                   + " AND namealg.t_iNumberAlg = txavoiriss.t_Emissive), CHR(0)) AS t_EmissiveName "
             + " ,txavoiriss.t_TaxGroup "
             + " ,txavoiriss.t_InvstType "
             + " ,txavoiriss.t_LSIN "
             + " ,txavoiriss.t_ISIN "
             + " ,TO_CHAR(txavoiriss.t_IssuerID) AS t_IssuerID "
             + " ,txavoiriss.t_Issued "
             + " ,txavoiriss.t_InCirculationDate "
             + " ,txavoiriss.t_DrawingDate "
             + " ,txavoiriss.t_Qty "
             + " ,txavoiriss.t_FaceValue "
             + " ,TO_CHAR(txavoiriss.t_FaceValueFIID) AS t_FaceValueFIID "
             + " ,NVL((SELECT "
                       + " MAX(txcur.t_Name) "
                   + " FROM "
                       + " dtxcurrency_dbt txcur "
                   + " WHERE "
                       + " txcur.t_fiid = txavoiriss.t_FaceValueFIID), CHR(0)) AS t_FaceValueCurName "
             + " ,txavoiriss.t_FirstPrice "
             + " ,txavoiriss.t_IncomeRate "
             + " ,TO_CHAR(txavoiriss.t_BaseFIID) AS t_BaseFIID "
             + " ,txavoiriss.t_NkdBase_Kind "
             + " ,NVL((SELECT "
                       + " namealg.t_szNameAlg "
                   + " FROM "
                       + " dnamealg_dbt namealg "
                   + " WHERE "
                       + " namealg.t_iTypeAlg = 7652 "
                   + " AND namealg.t_iNumberAlg = txavoiriss.t_NkdBase_Kind), CHR(0)) AS t_NkdBase_Name "
             + " ,txavoiriss.t_NkdRound_Kind "
             + " ,NVL((SELECT "
                       + " namealg.t_szNameAlg "
                   + " FROM "
                       + " dnamealg_dbt namealg "
                   + " WHERE "
                       + " namealg.t_iTypeAlg = 3109 "
                   + " AND namealg.t_iNumberAlg = txavoiriss.t_NkdRound_Kind), CHR(0)) AS t_NkdRound_Name "
             + " ,txavoiriss.t_NkdNonStandart "
             + " ,txavoiriss.t_SumPrecision "
             + " ,txavoiriss.t_Scale "
             + " ,txavoiriss.t_Point "
             + " ,txavoiriss.t_Version "
          + " FROM "
              + " dtxavoiriss_dbt txavoiriss "
          + " WHERE "
              + " 1 = 1 ";

  if(FltrParm != null)
    if(FltrParm.AvoirCode != null)
      m_Query = m_Query + " AND txavoiriss.t_Code = '" + FltrParm.FindAvoirissID + "' ";
    end;

    if((FltrParm.FindAvoirissID != null) and (FltrParm.FindAvoirissID != ""))
      m_Query = m_Query + " AND txavoiriss.t_AvoirissID  = " + FltrParm.FindAvoirissID + " ";
    end;

    if(FltrParm.LikeAvoirCode != null)
      m_Query = m_Query + " AND LOWER(txavoiriss.t_Code) LIKE LOWER('%" + FltrParm.LikeAvoirCode + "%') ";
    end;
  end;

  m_AddWhere = GetReplAvoirissAddWhereCondition(FilterItems);
  if(m_AddWhere != "")
    m_Query = m_Query + " AND " + m_AddWhere;
  end;

  m_OrderBy = GetSortOrder(OrderItems);
  if(m_OrderBy != "")
    m_Query = m_Query + " ORDER BY " + m_OrderBy;
  end;

  m_Query = SQL_WrapSelect(m_Query, PageNum, PageSize);

  m_Cmd = RSDCommand(m_Query);
  m_Cmd.NullConversion = true;
  m_Cmd.Execute();

  initRsdRecordset(m_Cmd);
end;

// Отбор данных для скроллинга Ц/Б
macro TxGetReplAvoirissList(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FltrParm//:TWsReplAvoirFltrParm             // Значения фильтрации
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
)
  var ReplAvoirissSet = null;
  var ReplAvoiriss = null;
  var ReplAvoirissList = TArray();

  ReplAvoirissSet = CReplAvoirissSet(PageSize, PageNum, FltrParm, FilterItems, OrderItems);

  while(ReplAvoirissSet.MoveNext())
    ReplAvoiriss = CTxReplAvoirissList();

    ReplAvoiriss.AvoirissID = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_AvoirissID"));
    ReplAvoiriss.InstanceDate = SQL_ConvTypeDateTime(ReplAvoirissSet.Value("t_InstanceDate"));
    ReplAvoiriss.Action = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_Action"));
    ReplAvoiriss.ActionName = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_ActionName"));
    ReplAvoiriss.ReplState = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_ReplState"));
    ReplAvoiriss.ReplStateName = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_ReplStateName"));
    ReplAvoiriss.Code = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_Code"));
    ReplAvoiriss.CodeInAcc = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_CodeInAcc"));
    ReplAvoiriss.Kind = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_Kind"));
    ReplAvoiriss.KindName = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_KindName"));
    ReplAvoiriss.ShortName = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_ShortName"));
    ReplAvoiriss.Definition = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_Definition"));
    ReplAvoiriss.InvstName = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_InvstName"));
    ReplAvoiriss.Settlement_Code = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_Settlement_Code"));
    ReplAvoiriss.SubKind = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_SubKind"));
    ReplAvoiriss.SubKindName = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_SubKindName"));
    ReplAvoiriss.Emissive = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_Emissive"));
    ReplAvoiriss.EmissiveName = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_EmissiveName"));
    ReplAvoiriss.TaxGroup = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_TaxGroup"));
    ReplAvoiriss.InvstType = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_InvstType"));
    ReplAvoiriss.LSIN = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_LSIN"));
    ReplAvoiriss.ISIN = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_ISIN"));
    ReplAvoiriss.IssuerID = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_IssuerID"));
    ReplAvoiriss.Issued = SQL_ConvTypeDate(ReplAvoirissSet.Value("t_Issued"));
    ReplAvoiriss.InCirculationDate = SQL_ConvTypeDate(ReplAvoirissSet.Value("t_InCirculationDate"));
    ReplAvoiriss.DrawingDate = SQL_ConvTypeDate(ReplAvoirissSet.Value("t_DrawingDate"));
    ReplAvoiriss.Qty = SQL_ConvTypeSum(ReplAvoirissSet.Value("t_Qty"));
    ReplAvoiriss.FaceValue = SQL_ConvTypeSum(ReplAvoirissSet.Value("t_FaceValue"));
    ReplAvoiriss.FaceValueFIID = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_FaceValueFIID"));
    ReplAvoiriss.FaceValueCurName = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_FaceValueCurName"));
    ReplAvoiriss.FirstPrice = SQL_ConvTypeSum(ReplAvoirissSet.Value("t_FirstPrice"));
    ReplAvoiriss.IncomeRate = SQL_ConvTypeSum(ReplAvoirissSet.Value("t_IncomeRate"));
    ReplAvoiriss.BaseFIID = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_BaseFIID"));
    ReplAvoiriss.NkdBase_Kind = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_NkdBase_Kind"));
    ReplAvoiriss.NkdBase_Name = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_NkdBase_Name"));
    ReplAvoiriss.NkdRound_Kind = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_NkdRound_Kind"));
    ReplAvoiriss.NkdRound_Name = SQL_ConvTypeStr(ReplAvoirissSet.Value("t_NkdRound_Name"));
    ReplAvoiriss.NkdNonStandart = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_NkdNonStandart"));
    ReplAvoiriss.SumPrecision = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_SumPrecision"));
    ReplAvoiriss.Scale = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_Scale"));
    ReplAvoiriss.Point = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_Point"));
    ReplAvoiriss.Version = SQL_ConvTypeInteger(ReplAvoirissSet.Value("t_Version"));

    ReplAvoirissList[ReplAvoirissList.Size] = ReplAvoiriss;
  end;

  return ReplAvoirissList;

OnError(err)
  TxReplAvoirissRunError(err, -1);
end;

private macro CheckVersion(
  AvoirissID:String
 ,InstanceDate:DateTime
 ,Version:Integer
)
  var err = 0;
  var Query = "", Cmd = null, Set = null;

  Query = " SELECT "
            + " * "
        + " FROM "
            + " dtxavoiriss_dbt "
        + " WHERE "
            + " t_AvoirissID = ? "
        + " AND t_InstanceDate = ? "
        + " AND t_Version = ? "
        + " FOR UPDATE ";

  Cmd = RsdCommand(Query);
  Cmd.AddParam("", RSDBP_IN, AvoirissID);
  Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(InstanceDate));
  Cmd.AddParam("", RSDBP_IN, Version);
  Cmd.Execute();

  Set = RsdRecordset(Cmd);
  if(not Set.MoveNext())
    err = 70;
  end;

  return err;
end;

private macro UpdateTxAvoirissImpl(
  StateAndAction//:CTxReplStateAndAction
 ,Avoiriss//:CTxReplAvoirissList
)
  var err = 0;
  var Query = "", Cmd = null;

  err = CheckVersion(Avoiriss.AvoirissID, Avoiriss.InstanceDate, Avoiriss.Version);

  if(err == 0)
    Query = " UPDATE "
              + " dtxavoiriss_dbt "
          + " SET ";
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Query = Query +
                " t_ReplState = ? "
             + " ,t_Action = ? ";
    elif(StateAndAction.NeedUpdateReplState)
      Query = Query +
                " t_ReplState = ? ";
    else
      Query = Query +
                " t_Action = ? ";
    end;
    Query = Query +
            " WHERE "
              + " t_AvoirissID = ? "
          + " AND t_InstanceDate = ? ";

    Cmd = RsdCommand(Query);
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    elif(StateAndAction.NeedUpdateReplState)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
    else
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    end;
    Cmd.AddParam("", RSDBP_IN, Avoiriss.AvoirissID);
    Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(Avoiriss.InstanceDate));
    Cmd.Execute();
  end;

  return err;
end;

macro TxUpdateAvoirissList(
  StateAndAction//:CTxReplStateAndAction
 ,AvoirissList//:TArray of CTxReplAvoirissList
)
  var err = 0;
  var i = 0;

  if((StateAndAction.NeedUpdateReplState
   or StateAndAction.NeedUpdateAction)
 and (AvoirissList != null))
    while((err == 0) and (i < AvoirissList.Size))
      err = UpdateTxAvoirissImpl(StateAndAction, AvoirissList[i]);
      i = i + 1;
    end;
  end;

  return err;

OnError(err)
  TxReplAvoirissRunError(err, -1);
end;

macro TxGetAvoirListItemCount(
  FltrParm
 ,FilterItems
)
  return TxGetReplAvoirissCount(FltrParm, FilterItems);
end;

private class (CReplAvoirissSet) CReplAvoirListSet(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FltrParm//:TWsReplAvoirFltrParm             // Значения фильтрации
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
)
  macro GetSortOrder(
    OrderItems//:TArray of OrderItem // Параметры сортировки
  ):String
    return GetReplAvoirissSortOrder(OrderItems, " t_Kind ASC ");
  end;

  initCReplAvoirissSet(PageSize, PageNum, FltrParm, FilterItems, OrderItems);
end;

macro TxGetAvoirListItems(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FltrParm//:TWsReplAvoirFltrParm             // Значения фильтрации
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
)
  var ReplAvoirListSet = null;
  var ReplAvoirListItem = null;
  var ReplAvoirList = TArray();

  ReplAvoirListSet = CReplAvoirListSet(PageSize, PageNum, FltrParm, FilterItems, OrderItems);

  while(ReplAvoirListSet.MoveNext())
    ReplAvoirListItem = TWsReplAvoirListItem();

    ReplAvoirListItem.AvoirissID = SQL_ConvTypeStr(ReplAvoirListSet.Value("t_AvoirissID"));
    ReplAvoirListItem.InstanceDate = SQL_ConvTypeDateTime(ReplAvoirListSet.Value("t_InstanceDate"));
    ReplAvoirListItem.Code = SQL_ConvTypeStr(ReplAvoirListSet.Value("t_Code"));
    ReplAvoirListItem.Name = SQL_ConvTypeStr(ReplAvoirListSet.Value("t_Definition"));
    ReplAvoirListItem.Kind = SQL_ConvTypeInteger(ReplAvoirListSet.Value("t_Kind"));
    ReplAvoirListItem.KindName = SQL_ConvTypeStr(ReplAvoirListSet.Value("t_KindName"));
    ReplAvoirListItem.Issuer = SQL_ConvTypeStr(ReplAvoirListSet.Value("t_IssuerID"));

    ReplAvoirList[ReplAvoirList.Size] = ReplAvoirListItem;
  end;

  return ReplAvoirList;

OnError(err)
  TxReplAvoirissRunError(err, -1);
end;

///////////////////////////////////////////////////////////
// Возвращает список длинны listLen по
// частичному совпадению значения
///////////////////////////////////////////////////////////
macro LookupReplAvoir(
  FltrParm
 ,ListLen
)
  return TxGetAvoirListItems(ListLen, 0, FltrParm, null, null);
end;
