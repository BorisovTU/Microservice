/*
$Name:             txproc_avr_fun.mac
$Module:           БОЦБ 
$Description:      Процедуры для репликации данных в БОЦБ из внешних систем
*/
IMPORT secinter, spserv, oralib, txrepl_func;
IMPORT RsbDataSet;

PRIVATE CONST  CODE_CFI = 18;

/*данные с информацие по ц/б для передачи в процедуру ОбработатьЦБ*/
CLASS TAvoirData
   var   T_AVOIRISSID:numeric;   /* идентификатор ценной бумаги */

   var   T_CODE:string;          /* Код ценной бумаги Должен быть уникальным для ценной бумаги. */
   var   T_CODEINACCOUNT:string; /* Код ценной бумаги используемый для подстановки в счета*/ 
   var   T_KIND:integer;         /* Вид ценной бумаги в классификации целевой системы:          */
   var   T_SHORTNAME:string;        /* Краткое наименование ц/б Для пая -  название пая (фонда)                                                    */
   var   T_DEFINITION:string;       /* Описание (полное наименование) ценной бумаги Для пая -  название пая (фонда)                                */
   var   T_INVSTNAME:string;        /* Наименование инвестиционного фонда Обяз. для инв. паев, для прочих не задается.*/
   var   T_SETTLEMENT_CODE:integer; /* Числовой код, характеризующий форму инструмента:  1 - Документарная  0  - Бездокументарная Для акций - бездокументарная*/
   var   T_SUBKIND:integer;         /* Вид облигации по справочнику видов облигаций.*/
   var   T_TAXGROUP:integer;        /* Группа налогового учета по справочнику групп налогового учета*/
   var   T_INVSTTYPE:integer;       /* Тип ПИФ: 0 - Интервальный,  1 - Открытый,  2 - Закрытый По умолчанию - Интервальный*/
   var   T_LSIN:string;             /* Государственный регистрационный номер */
   var   T_ISIN:string;             /* Международный идентификационный номер  */
   var   T_ISSUERID:integer;        /* Идентификатор эмитента Для ДР - эмитент ДР Для пая - управляющая компания  */
   var   T_ISSUED:date;             /* Дата выпуска ценной бумаги */
   var   T_INCIRCULATIONDATE:date;  /* Дата ввода в обращение */
   var   T_DRAWINGDATE:date;        /* Дата погашения плановая (по проспекту) Для облигаций - обяз. Для пая - задается тлолько для закрытого */
                                    /* Примечание: дата погашения фактическая - определяется из операции погашения выпуска.                  */
   var   T_QTY:money;               /* Объем выпуска */ 
   var   T_FACEVALUE:money;         /* Номинал.  Для паев не задается, для остальных бумаг обязателен. */
   var   T_FACEVALUEFIID:integer;   /* Идентификатор валюты номинала\валюты цены размещения паев. */
   var   T_FIRSTPRICE:money;        /* Цена первичного размещения бумаг выпуска в валюте котировки Для пая -  стоимость при формировании */
   var   T_INCOMERATE:money;        /* Годовой доход в процентах от номинала */
   var   T_MAINFIID:integer;        /* Ссылка на основной выпуск Указывается в выпуске по дополнительной эмиссии*/
   var   T_BASEFIID:numeric;        /* Ссылка на базовый выпуск.  Для депозитарный расписок указывается выпуск акций, на базе которого они выпуущены. Для прочих видов ценных бумаг не используется*/
   var   T_ISSUE:string;            /* Номер выпуска. Указывается для выпуска по дополнительной эмиссиии.*/
   var   T_NKDBASE_KIND:integer;    /* Вид базиса расчета НКД - Номер значения справочника в целевой системе, определяющего купонный год */
                                    /*   1 -  360 дней в году, 30 в месяце 2 -  360 дней в году, в месяце - по календарю 3 -  В году дней - по календарю,30 - в месяце 4 -  В году и месяце дней - по календарю*/
   var   T_NKDROUND_KIND:integer;   /* Вид округления при расчете НКД - ссылка на справочник видов округления при расчете НКД, возможные значения: */
                                    /* 0 -  Без округления 1 -  Округление одного купона  2 -  Округление платежа по НКД*/
   var   T_NKDNONSTANDART:string;   /* Признак наличия нестандартных правил расчета купонных выплат (по всем купонам выпуска). */ 
   var   T_MARKETCODE:string;       /* Торговый код - Код ценной бумаги на торговой площадке.*/
   var   T_MARKETID:integer;        /* Торговая площадка - Идентификатор торговой площадки (биржи), на которой торгуется данная бумага, и код в кодировке которой приводится*/
   var   T_SUMPRECISION:integer;    /* Точность суммы - Точность задания суммы в данном инструменте Для паев - может быть больше 0 (обычно - 5) Для прочих бумаг -  строго 0*/
   var   T_SCALE:integer;           /* Масштаб курса - Мастаб курса  По умолчанию - 1 Используется как умолчание при вводе курса. */
   var   T_POINT:integer;           /* Точность округления - Точность округления курса (цены) По умолчанию - 2 Используется как умолчание при вводе курса. */
   var   T_EMISSIVE:integer;        /* Признак эмиссионной ц/б для целей налогового учета: 0 По умолчанию (определяется видом), 1 Эмиссионная, 2 Неэмиссионная*/
   var   T_NOTSTANDART:bool;        /* признак нестандартных условий выпуска бумаги */
   var   T_VTINCLUDEDATE:date;      /* признак Дата включения бумаги в список ВТС */
   var   T_VTEXCLUDEDATE:date;      /* признак Дата исключения бумаги из списка ВТС */

   var   T_INSTANCEDATE:date;       /* Дата начала действия признака */

   var   T_CFI;			    /* Код CFI  */

   /**/
   var   InTrn:bool; /*признак выполнения процедуры уже в начатой транзакции*/

   /**/
   var   AvoirissExist:bool;  /* признак наличия в системе обрабатываемой ц/б*/   
   var   Action:integer;      /* выполняемое действие (1 - вставка, 2 - изменение, 3 - удаление.)*/
   var   IsShare:bool;        /* признак акции */
   var   IsBond:bool;         /* признак облигации */
   var   Error:TArray;        /* массив строк с текстами ошибок*/


   MACRO MakeError( ErrorMes:string )
      this.Error[this.Error.Size] = string(this.Error.Size) + ":" + ErrorMes;
   END;

   MACRO PrintErrors()
      var i = 0;
      while( this.Error[i] != null )
         println( this.Error[i] );
         i = i + 1;   
      end;     
   END;  

   MACRO Init()
      this.InTrn           =  false;
      this.IsShare         =  false;
      this.IsBond          =  false;
      this.AvoirissExist   =  false;
      this.Error           =  TArray;
      this.Action          =  0;
   END;

   Init();
END;

/* Задать связь с базовым объектом для депозитарной расписки */
PRIVATE MACRO SetLinkForDepoReceipt( BaseFIID:integer, Avoiriss ):bool
  file basefi(avoiriss) key 0;
 
  if( BaseFIID <= 0 )
    if( UnlinkObject( 10 /*связь деп.расписки с базовым объектом*/, OBJTYPE_AVOIRISS, Avoiriss ) != 0 )
       return false;
    end;
  else
    ClearRecord(basefi);
    basefi.FIID = BaseFIID;
    if( GetEQ(basefi) )
       UnlinkObject( 10 /*связь деп.расписки с базовым объектом*/, OBJTYPE_AVOIRISS, Avoiriss );
       if( SetLinkedObject( 10 /*связь деп.расписки с базовым объектом*/, OBJTYPE_AVOIRISS, Avoiriss, OBJTYPE_AVOIRISS, basefi, true ) != 0 )   
          return false;
       end;
    end;
  end;
  
  return true;
END;

/* Функция обработки ценной бумаги */
/* Action - обязательный. IN. Integer. Выполняемое действие (1 - вставка, 2 - изменение, 3 - удаление.)  */
/* Error - необязательный. OUT. String. */
/* Функция возвращает TRUE (0) в случае успешного выполнения и FALSE (1) в случае ошибки. */

/* Функция создания, изменения и удаления ценной бумаги. Функция должна сама запускать транзакцию.*/

PRIVATE FILE fininstr ("fininstr.dbt") write;   /*фин.инструмент*/
PRIVATE FILE avoiriss ("avoiriss.dbt") write;   /*ценная бумага*/
PRIVATE FILE avrinvst ("avrinvst.dbt") write;   /*доп.информация для инвестиц. паев*/
PRIVATE FILE objcode  ("objcode.dbt") write;    /*кода фин.инструментов*/
PRIVATE FILE objalreg ("objalreg.dbt") write;    /*в нашем случае - связка субъектов и видов кодов*/
PRIVATE FILE objkcode ("objkcode.dbt") write;    /*виды кодов*/
PRIVATE FILE objrgdoc ("objrgdoc.dbt") write;    /*привязка кодов*/
PRIVATE FILE partyown ("partyown.dbt") write;
PRIVATE var  party       =  TRecHandler("party.dbt");
PRIVATE var  gAvoirData  =  TAvoirData;

PRIVATE MACRO СделатьСубъектаЭмитентом( PARTYID:integer )
   ClearRecord(partyown);
   partyown.PartyID     = PARTYID;
   partyown.PartyKind   = PTK_ISSUER; 

   return Insert(partyown);
END;

MACRO ProcessAvoiriss()
   var   IsWrongParms:bool = false,
         isFind:bool       = false, 
         isNext:bool       = false,
         ret:bool          = true;

   var   sqlQuery:string = "";
   var   DataSet;
   var   RefID:integer, Number:string = "";
   var   CatErr:integer = 0;
   var   strcmd:string = "";
   var   IsEmissiveAvoiriss:integer = 0;
   var   NumInList = "", ValidFromDate = DATE(0,0,0);
 
   /*чистим используемые структуры*/
   ClearRecord( fininstr );
   ClearRecord( avoiriss );
   ClearRecord( avrinvst );
   ClearRecord( objcode );
   ClearRecord( objrgdoc );             
   ClearRecord( objalreg );
   ClearRecord( objkcode );

   if( (gAvoirData.T_KIND == null) OR (gAvoirData.T_KIND <= 0)  )
      gAvoirData.MakeError("Ошибка: не задан вид ценной бумаги, параметр T_KIND" );
      ЗанестиЗаписьВПротокол(501, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: не задан вид ценной бумаги, параметр T_KIND, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
      AbortTrn;
   else
      if( (gAvoirData.T_KIND == 1) OR (gAvoirData.T_KIND == 2) OR (gAvoirData.T_KIND == 7) OR (gAvoirData.T_KIND == 10) OR (gAvoirData.T_KIND == 20) )
         gAvoirData.IsShare = true;
      elif( (gAvoirData.T_KIND == 23) OR (gAvoirData.T_KIND == 24) OR (gAvoirData.T_KIND == 25) OR
            (gAvoirData.T_KIND == 26) OR (gAvoirData.T_KIND == 27) OR (gAvoirData.T_KIND == 28) OR (gAvoirData.T_KIND == 29) OR
            (gAvoirData.T_KIND == 30) OR (gAvoirData.T_KIND == 31) OR (gAvoirData.T_KIND == 32) )
         gAvoirData.IsBond = true;
      elif( gAvoirData.T_KIND == 17 )
         gAvoirData.T_KIND = 51; // КСУ в целевой системе
      end;
   end;

   if( gAvoirData.Action != 1 ) /*Не вставка*/
      /*проверим существование ценной бумаги в системе */
      if( (gAvoirData.T_AVOIRISSID == null) OR (gAvoirData.T_AVOIRISSID <= 0) )
         gAvoirData.MakeError("Ошибка: не найдена ценная бумага" );
         ЗанестиЗаписьВПротокол(502, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: не не найдена ценная бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         IsWrongParms = true;
      else
         KeyNum( avoiriss, 0 ); 
         avoiriss.FIID = gAvoirData.T_AVOIRISSID;  
         if( GetEQ( avoiriss ) )
            gAvoirData.AvoirissExist = true;
            KeyNum( fininstr, 0 );
            fininstr.FIID = avoiriss.FIID;
            if( not GetEQ( fininstr ) )
               gAvoirData.MakeError("Ошибка: не найдена запись по фининструменту для ценной бумаги с T_AVOIRISSID = " + string(gAvoirData.T_AVOIRISSID) );
               ЗанестиЗаписьВПротокол(503, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: не найдена запись по фининструменту, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
               AbortTrn;
            end;

            if( gAvoirData.T_KIND == 16 ) /*для инвест. пая подкачаем доп.иформацию*/
               KeyNum( avrinvst, 0 );
               avrinvst.FIID = avoiriss.FIID;
               if( not GetEQ( avrinvst ) )
                  gAvoirData.MakeError("Ошибка: не найдена запись c дополнительной информацией по инвестиционному паю для ценной бумаги с T_AVOIRISSID = " + string(gAvoirData.T_AVOIRISSID) );
                  ЗанестиЗаписьВПротокол(504, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: не найдена запись c дополнительной информацией по инвестиционному паю, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
                  AbortTrn;
               end;
            end;
         else
            gAvoirData.MakeError("Ошибка: не найдена ценная бумага с T_AVOIRISSID = " + string(gAvoirData.T_AVOIRISSID) );
            ЗанестиЗаписьВПротокол(505, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: не найдена ценная бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
            AbortTrn;  
         end;
      end;
   else /*вставка*/
      /*если код не задан, сформируем его по референсу */
      if( (gAvoirData.T_CODE == null) OR (gAvoirData.T_CODE == "") )
         if( (GetReferenceIDByType(
                  12, /*OBJTYPE_AVOIRISS*/
                  1,  /*REFOBJ_AVOIR_CODE*/
                  RefID ) == 0) AND (RefID != 0) )
            if( GenerateReference( RefID, Number ) != 0 )
               gAvoirData.MakeError("Ошибка: ошибка при генерации кода для ценной бумаги" );
               ЗанестиЗаписьВПротокол(603, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при генерации кода для ценной бумаги " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
               IsWrongParms = true;
            else
               fininstr.FI_Code = Substr(Number,1,15);
            end;
         end;
      else
         fininstr.FI_Code = Substr(gAvoirData.T_CODE,1,15);            
      end;
 
      if( (gAvoirData.T_CODEINACCOUNT == null) OR (gAvoirData.T_CODEINACCOUNT == "") )
         Number = "";
         RefID = 0;

         /*сгенерируем через референсы код в номере счета*/
         if( (GetReferenceIDByType(
                  12,   /*OBJTYPE_AVOIRISS*/
                  2,    /*REFOBJ_AVOIR_ACCOUNTCODE*/                    
                  RefID ) == 0) AND (RefID != 0) )
            if( GenerateReference( RefID, Number ) != 0 )
               gAvoirData.MakeError("Ошибка: ошибка при генерации кода в номере счета для ценной бумаги" );
               ЗанестиЗаписьВПротокол(604, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при генерации кода в номере счета для ценной бумаги " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
               IsWrongParms = true;         
            else
               fininstr.CodeInAccount = Substr( Number,1,7); 
            end;
         end;   
      else
         fininstr.CodeInAccount = Substr( gAvoirData.T_CODEINACCOUNT,1,7); 
      end;
   end;

   if( IsWrongParms == false )
      if( gAvoirData.Action  == 1  )  /* вставка   */
         if( gAvoirData.AvoirissExist )
            gAvoirData.MakeError("Ошибка: ценная бумага уже существует в целевой системе");
            ЗанестиЗаписьВПротокол(409, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ценная бумага уже существует в целевой системе, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
            AbortTrn;
         end;

         fininstr.FIID  =  0;
         avoiriss.FIID  =  0;
      elif( gAvoirData.Action  == 2  )  /* изменение */
         if( gAvoirData.AvoirissExist == false )
            gAvoirData.MakeError("Ошибка: ценная бумага не найдена в целевой системе");
            ЗанестиЗаписьВПротокол(410, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ценная бумага не найдена в целевой системе, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
            AbortTrn;
         end;
      elif( gAvoirData.Action  == 3  )  /* удаление  */
         if( gAvoirData.AvoirissExist == false )
            gAvoirData.MakeError("Ошибка: ценная бумага уже удалена в целевой системе");
            ЗанестиЗаписьВПротокол(411, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ценная бумага уже удалена в целевой системе, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
            AbortTrn;
         end;
      else
         gAvoirData.MakeError( "Ошибка: неверный вид выполняемого действия: " + string( gAvoirData.Action ) );
         ЗанестиЗаписьВПротокол(506, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: неверный вид выполняемого действия, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         AbortTrn;
      end;
   end;

   /*инициализация данных операции для вставки\изменения*/
   if( (IsWrongParms == false) AND ((gAvoirData.Action == 1) OR (gAvoirData.Action == 2)) )
      fininstr.FI_Kind  = FIKIND_AVOIRISS;
      fininstr.generalizedfiid = -1;

      if( gAvoirData.T_SHORTNAME != null )
          fininstr.name = substr(gAvoirData.T_SHORTNAME,1,25);
      end;
      if( gAvoirData.T_DEFINITION != null )
          fininstr.Definition = substr(gAvoirData.T_DEFINITION,1,164);
      end;
      if( gAvoirData.T_INVSTNAME != null )
         avrinvst.Name = substr(gAvoirData.T_INVSTNAME,1,80);
      end;
      /*для паев наименование инвест. фонда обязательно*/
      if( (gAvoirData.T_KIND == 16) and ((gAvoirData.T_INVSTNAME == null) or (gAvoirData.T_INVSTNAME == "")) )
         gAvoirData.MakeError("Ошибка: для пая не задано наименовлание инвестиционного фонда, параметр gAvoirData.T_INVSTNAME" );
         ЗанестиЗаписьВПротокол(507, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: для пая не задано наименовлание инвестиционного фонда, параметр gAvoirData.T_INVSTNAME, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         IsWrongParms = true;
      end;

      if( gAvoirData.IsShare )
         fininstr.Settlement_Code = 0;
      else
         if( (gAvoirData.T_SETTLEMENT_CODE != null) and ((gAvoirData.T_SETTLEMENT_CODE == 0) OR (gAvoirData.T_SETTLEMENT_CODE == 1)) )
            fininstr.Settlement_Code = gAvoirData.T_SETTLEMENT_CODE;
         else
            gAvoirData.MakeError("Ошибка: не задан или задан некорректно числовой код, характеризующий форму инструмента, параметр T_SETTLEMENT_CODE" );
            ЗанестиЗаписьВПротокол(508, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: не задан или задан некорректно числовой код, характеризующий форму инструмента, параметр T_SETTLEMENT_CODE, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
            IsWrongParms = true;
         end;
      end;

      if( gAvoirData.T_INVSTTYPE != null )
         avrinvst.Type = gAvoirData.T_INVSTTYPE;
      end;

      if( gAvoirData.T_LSIN != null )
         avoiriss.LSIN = substr(gAvoirData.T_LSIN,1,25);
      end;

      if( gAvoirData.T_ISIN != null )
         avoiriss.ISIN = substr(gAvoirData.T_ISIN,1,25);
      end;

      if( (gAvoirData.T_ISSUERID != null) and (gAvoirData.T_ISSUERID > 0) )
         if( ПолучитьСубъекта( gAvoirData.T_ISSUERID, party ) == 0 )
            if( ВидСубъекта( gAvoirData.T_ISSUERID,  PTK_ISSUER) == true )
               fininstr.Issuer = gAvoirData.T_ISSUERID;
            else
               if( СделатьСубъектаЭмитентом( gAvoirData.T_ISSUERID ) != true )
                  gAvoirData.MakeError(" Ошибка: ошибка при добавлениии субъекту с T_ISSUERID = " + string(gAvoirData.T_ISSUERID) + " вида - эмитент ценных бумаг" );
                  ЗанестиЗаписьВПротокол(605, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), " Ошибка: ошибка при добавлениии субъекту вида - эмитент ценных бумаг, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
                  IsWrongParms = true;
               else
                  fininstr.Issuer = gAvoirData.T_ISSUERID;
               end;
            end;
         else
            gAvoirData.MakeError("Предупреждение: не найден эмитент с PartyID = " + string(gAvoirData.T_ISSUERID) ); 
            ЗанестиЗаписьВПротокол(509, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Предупреждение: не найден эмитент, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         end;
      else
         gAvoirData.MakeError("Ошибка: не найден эмитент");
         ЗанестиЗаписьВПротокол(510, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: не найден эмитент, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         IsWrongParms = true;
      end;

      if( gAvoirData.T_KIND != 16 )
         if( gAvoirData.T_ISSUED != null )
            fininstr.Issued = gAvoirData.T_ISSUED;
         end;

         if( gAvoirData.T_INCIRCULATIONDATE != null )
            avoiriss.InCirculationDate = gAvoirData.T_INCIRCULATIONDATE;
         end;
      else
         /*для паев - всегда нулевые даты*/
         fininstr.Issued            =  date(0,0,0);
         avoiriss.InCirculationDate =  date(0,0,0);
      end;

      if( gAvoirData.T_DRAWINGDATE != null )
         fininstr.DrawingDate = gAvoirData.T_DRAWINGDATE;
      end;

      if( gAvoirData.T_QTY != null )
         avoiriss.Qty = gAvoirData.T_QTY;
      end;

      if( gAvoirData.T_KIND != 10 )
         if( gAvoirData.T_FACEVALUE != null )
            fininstr.FaceValue = gAvoirData.T_FACEVALUE;
         end;
      end;

      if( gAvoirData.T_FACEVALUEFIID != null )
         fininstr.FaceValueFI = gAvoirData.T_FACEVALUEFIID;
      end;

      if( gAvoirData.T_KIND == 16 ) /*для паев*/
         avrinvst.FormValueFIID  =  fininstr.FaceValueFI;
         if( (gAvoirData.T_FIRSTPRICE != null) and (gAvoirData.T_FIRSTPRICE != Money(0)) )
            avrinvst.FormValue      =  gAvoirData.T_FIRSTPRICE;
         end;
         /*для других бумаг сохраняем ниже в примечания*/
      end;

      if( gAvoirData.T_INCOMERATE != null )
         avoiriss.IncomeRate = gAvoirData.T_INCOMERATE;
      end;

      if( gAvoirData.T_MAINFIID != null )
         fininstr.MainFIID = gAvoirData.T_MAINFIID;
      end;

      if( gAvoirData.T_ISSUE != null )
         avoiriss.Issue = substr( gAvoirData.T_ISSUE, 1, 3 );
      end;

      if( gAvoirData.T_NKDBASE_KIND != null )
         avoiriss.NKDBase_Kind = gAvoirData.T_NKDBASE_KIND;
      end;

      if( gAvoirData.T_NKDROUND_KIND != null )
         avoiriss.NKDRound_Kind = gAvoirData.T_NKDROUND_KIND;
      else
         if( (gAvoirData.Action == 1) AND (gAvoirData.IsBond == true) ) /*для вставки облигаций заполняем по-умолчанию*/
            avoiriss.NKDRound_Kind = 1;
         end;
      end;

      if( (avoiriss.NKDRound_Kind != 1) AND (avoiriss.NKDRound_Kind != 2) )
         gAvoirData.MakeError("Предупреждение: Не задан вид округления при расчете НКД, параметр T_NKDROUND_KIND. По-умолчанию установлено: 1 - округление НКД для одного купона.");
         ЗанестиЗаписьВПротокол(699, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Предупреждение: Не задан вид округления при расчете НКД, параметр T_NKDROUND_KIND. По-умолчанию установлено: 1 - округление НКД для одного купона. Бумага - " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         avoiriss.NKDRound_Kind = 1;
      end;

      if( gAvoirData.T_SUMPRECISION != null )
         if( gAvoirData.T_KIND == 16 ) /*для паев*/
            fininstr.SumPrecision = gAvoirData.T_SUMPRECISION;
         else
            fininstr.SumPrecision = 0;
         end;
      end;

      if( gAvoirData.T_SCALE != null )
         fininstr.Scale = gAvoirData.T_SCALE;
      else
         fininstr.Scale = 1;
      end;

      if( gAvoirData.T_POINT != null )
         fininstr.Point = gAvoirData.T_POINT;
      else
         fininstr.Point = 2;
      end;

      if( gAvoirData.T_KIND == 10 ) /*для депозитарной расписки*/
         if( gAvoirData.T_BASEFIID != null )
            fininstr.ParentFI = gAvoirData.T_BASEFIID;
         end;
         if( gAvoirData.T_FaceValue != null )
            avoiriss.NumBaseFI = gAvoirData.T_FaceValue;
         end;
      end;
   end;

   /*некорректные входящие параметры - прерываем транзакцию*/
   if( IsWrongParms == true )
      AbortTrn;
   end;

   fininstr.AvoirKind = gAvoirData.T_KIND;
   avoiriss.TAXGROUP = gAvoirData.T_TAXGROUP;

   if( gAvoirData.Action == 1  )  /* вставка   */
      /*определяем новый FIID*/
      sqlQuery = "SELECT max(fin.t_FIID) as MaxFIID FROM dfininstr_dbt fin";
      DataSet = TRsbDataSet( sqlQuery );

      if( DataSet.MoveNext() )
         fininstr.FIID = DataSet.MaxFIID + 1;
      else
         fininstr.FIID = 1;
      end;

      if( Insert( fininstr ) == false )
         gAvoirData.MakeError( "Ошибка: ошибка при вставке записи о фининструменте в dfininstr_dbt" );
         ЗанестиЗаписьВПротокол(606, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при вставке записи о фининструменте в dfininstr_dbt, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         AbortTrn;
      end;
      /*для возврата ID вставленной цб*/
      gAvoirData.T_AVOIRISSID = fininstr.FIID;
      avoiriss.FIID = fininstr.FIID;

      if( Insert( avoiriss ) == false )
         gAvoirData.MakeError( "Ошибка: ошибка при вставке записи о ценной бумаге в davoiriss_dbt" );
         ЗанестиЗаписьВПротокол(607, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при вставке записи о ценной бумаге в davoiriss_dbt, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         AbortTrn;
      end;

      if( gAvoirData.T_KIND == 16 ) /*для инвест. пая вставим доп.иформацию*/
         avrinvst.FIID = avoiriss.FIID;
         if( Insert( avrinvst ) == false )
            gAvoirData.MakeError( "Ошибка: ошибка при вставке записи с доп.информацией по инвест. паю в davrinvst_dbt" );
            ЗанестиЗаписьВПротокол(608, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при вставке записи с доп.информацией по инвест. паю в davrinvst_dbt, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
            AbortTrn;
         end;
      end;
   elif( gAvoirData.Action  == 2  )  /* изменение */
      if( Update( avoiriss ) == false )
         gAvoirData.MakeError( "Ошибка: ошибка при обновлении записи о ценной бумаге в davoiriss_dbt" );
         ЗанестиЗаписьВПротокол(609, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при обновлении записи о ценной бумаге в davoiriss_dbt, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         AbortTrn;
      end;

      if( Update( fininstr ) == false )
         gAvoirData.MakeError( "Ошибка: ошибка при обновлении записи о фининструменте в dfininstr_dbt" );
         ЗанестиЗаписьВПротокол(610, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при обновлении записи о фининструменте в dfininstr_dbt, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         AbortTrn;
      end;

      if( gAvoirData.T_KIND == 16 ) /*для инвест. пая обновим доп.иформацию*/
         if( Update( avrinvst ) == false )
            gAvoirData.MakeError( "Ошибка: ошибка при обновлении записи с доп.информацией по инвест. паю в davrinvst_dbt" );
            ЗанестиЗаписьВПротокол(611, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при обновлении записи с доп.информацией по инвест. паю в davrinvst_dbt, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
            AbortTrn;
         end;
      end;
   elif( gAvoirData.Action  == 3  )  /* удаление  */
      /*удалим примечания к ценной бумаге*/
      strcmd =  "delete from dnotetext_dbt note where note.t_ObjectType = " + string(OBJTYPE_AVOIRISS) + " and note.t_DocumentID = '" + string(UniID(avoiriss, OBJTYPE_AVOIRISS)) + "'";
      execSQL(strcmd);

      if( gAvoirData.T_KIND == 16 ) /*для инвест. пая удалим доп.иформацию*/
         if( Delete( avrinvst ) == false )
            gAvoirData.MakeError( "Ошибка: ошибка при удалении записи с доп.информацией по инвест. паю в davrinvst_dbt" );
            ЗанестиЗаписьВПротокол(612, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при удалении записи с доп.информацией по инвест. паю в davrinvst_dbt, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
            AbortTrn;
         end;
      end;

      if( gAvoirData.IsBond )
         /*удалить значение категории "Вид облигации" для облигации*/
         if( not DisconnectObjAttr( OBJTYPE_AVOIRISS, UniID( avoiriss, OBJTYPE_AVOIRISS ), BOUND_KIND ) )
            gAvoirData.MakeError("Ошибка: ошибка при удалении значения категории \"Вид облигации\" для ценной бумаги с FI_Code = " + string(fininstr.FI_Code) );
            ЗанестиЗаписьВПротокол(613, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при удалении значения категории 'Вид облигации', бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
            AbortTrn;
         end;
      end;

      if( not DisconnectObjAttr( OBJTYPE_AVOIRISS, UniID( avoiriss, OBJTYPE_AVOIRISS  ), 19 ) )
         gAvoirData.MakeError("Ошибка: ошибка при удалении значения категории \"Группа налогового учета\" для ценной бумаги с FI_Code = " + string(fininstr.FI_Code) );
         ЗанестиЗаписьВПротокол(614, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при удалении значения категории 'Группа налогового учета', бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         AbortTrn;
      end;

      /*удалить значение категории "Является бумагой ВТС"*/
      if ( not DisconnectObjAttr( OBJTYPE_AVOIRISS, UniID( avoiriss, OBJTYPE_AVOIRISS ), 56))
         ЗанестиЗаписьВПротокол(633, 21, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при удалении значения категории 'Группа налогового учета', бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         AbortTrn;
      end;
      /*удаление истории значение категории "Является бумагой ВТС"*/
      strcmd =  "delete from dOBJATCOR_dbt obj where obj.t_ObjectType = 12 and obj.t_GroupID = 56 and obj.t_Object = " + UniID( avoiriss, OBJTYPE_AVOIRISS );
      execSQL(strcmd);

      if( gAvoirData.T_KIND == 10 ) /*для депозитарной расписки*/
         if( SetLinkForDepoReceipt( 0, Avoiriss ) == false )
            gAvoirData.MakeError( "Ошибка: ошибка при удалении привязки депозитарной расписки к базовому фининструменту" );
            ЗанестиЗаписьВПротокол(615, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при удалении привязки депозитарной расписки к базовому фининструменту, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
            AbortTrn;
         end;
      end;

      /*удаление всех кодов ценной бумаги по биржам*/
      strcmd =  "delete from dobjcode_dbt obj where obj.t_ObjectType = 9 and obj.t_ObjectID = " + string(avoiriss.FIID);
      execSQL(strcmd);
      strcmd =  "delete from dobjrgdoc_dbt obj where obj.t_ObjectType = 9 and obj.t_ObjectID = " + string(avoiriss.FIID);
      execSQL(strcmd);

      if( Delete( avoiriss ) == false )
         gAvoirData.MakeError( "Ошибка: ошибка при удалении записи о ценной бумаге в davoiriss_dbt" );
         ЗанестиЗаписьВПротокол(616, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при удалении записи о ценной бумаге в davoiriss_dbt, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         AbortTrn;
      end;

      if( Delete( fininstr ) == false )
         gAvoirData.MakeError( "Ошибка: ошибка при удалении записи о фининструменте в dfininstr_dbt" );
         ЗанестиЗаписьВПротокол(617, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при удалении записи о фининструменте в dfininstr_dbt, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         AbortTrn;
      end;
   else
      gAvoirData.MakeError( "Ошибка: неверный вид выполняемого действия: " + string( gAvoirData.Action ) );
      ЗанестиЗаписьВПротокол(506, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: неверный вид выполняемого действия, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
      AbortTrn;
   end;

   /*общие действия для вставки или обновления - которые желательно выполнять когда уже известен FIID инструмента*/
   if( (gAvoirData.Action == 1) OR (gAvoirData.Action == 2) )
      if( gAvoirData.T_KIND == 10 ) /*для депозитарной расписки*/
         if( gAvoirData.T_BASEFIID != null )
            if( SetLinkForDepoReceipt( gAvoirData.T_BASEFIID, avoiriss ) != true )
               gAvoirData.MakeError("Ошибка: ошибка при добавлении ссылки на базовый фининструмент (T_BASEFIID) для депозитарного сертификата");
               ЗанестиЗаписьВПротокол(618, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при добавлении ссылки на базовый фининструмент (T_BASEFIID) для депозитарного сертификата, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
               AbortTrn;
            end;
         end;
      end;

      /*  Добавление нового кода CFI   */
      if (trim(gAvoirData.T_CFI) != "")
         strcmd = "delete from dobjcode_dbt where t_objecttype = 9 and t_codekind = " + CODE_CFI + " and t_objectid = " + string(avoiriss.FIID);
         execSQL(strcmd);
         strcmd = "insert into dobjcode_dbt(t_objecttype,t_codekind, t_objectid, t_code, t_state, t_sysdate, t_bankdate, t_bankclosedate, t_userid) values(9," + CODE_CFI +
                  "," + string(avoiriss.FIID) + ",'" + gAvoirData.T_CFI + "',0,to_date('01.01.0001','dd.mm.yyyy'),to_date('01.01.0001','dd.mm.yyyy'),to_date('01.01.0001','dd.mm.yyyy')," + {oper} + ")";
         execSQL(strcmd);
      end;

      /*добавим\обновим примечание*/
      if (gAvoirData.T_NKDNONSTANDART == "0")
         strcmd =  "delete from dnotetext_dbt where t_ObjectType = " + string(OBJTYPE_AVOIRISS) + " and t_DocumentID = " + string(UniID(avoiriss, OBJTYPE_AVOIRISS)) + " and t_NoteKind = 9";
         execSQL(strcmd);
      end;

      if(( gAvoirData.T_NKDNONSTANDART != null ) and (gAvoirData.T_NKDNONSTANDART != "0"))
        if( addNoteForObject( OBJTYPE_AVOIRISS, UniID(avoiriss, OBJTYPE_AVOIRISS),
                              9 /*Пользовательский расчет НКД*/, gAvoirData.T_NKDNONSTANDART ) )
           gAvoirData.MakeError( " Предупреждение: ошибка при добавлении примечания \"Пользовательский расчет НКД\" для ценной бумаги " );
           ЗанестиЗаписьВПротокол(619, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), " Предупреждение: ошибка при добавлении примечания 'Пользовательский расчет НКД' для ценной бумаги " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
        end;
      end;

      /*добавим\обновим примечание - цена первичного размещения в процентах*/
      if( (gAvoirData.T_FIRSTPRICE != null) and ( double(gAvoirData.T_FIRSTPRICE) > 0.0 ))
        if( addNoteForObject( OBJTYPE_AVOIRISS, UniID(avoiriss, OBJTYPE_AVOIRISS),
                              10 /*Цена первичного размещения в процентах*/, double(gAvoirData.T_FIRSTPRICE) ) )
           gAvoirData.MakeError( " Предупреждение: ошибка при добавлении примечания \"Цена первичного размещения\" для ценной бумаги " );
           ЗанестиЗаписьВПротокол(620, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), " Предупреждение: ошибка при добавлении примечания 'Цена первичного размещения' для ценной бумаги " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
        end;
      end;

      /*вставка\обновление кода на бирже*/
      if( (gAvoirData.T_MARKETID != null) and (gAvoirData.T_MARKETID > 0) and
          (gAvoirData.T_MARKETCODE != null) )
         if( ПолучитьСубъекта( gAvoirData.T_MARKETID, party ) != 0 )
            gAvoirData.MakeError("Ошибка: не найдена биржа с PartyID = " + string(gAvoirData.T_MARKETID) + " для которой задан код ценной бумаги (T_MARKETCODE)" );
            ЗанестиЗаписьВПротокол(511, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: не найдена биржа для которой задан код ценной бумаги (T_MARKETCODE), бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
            AbortTrn;
         end;

         KeyNum( objalreg, 1 );
         objalreg.ObjectType     =  9;    /*ФИ*/
         objalreg.RegPartyKind   =  15;   /*субъект*/
         objalreg.RegDocKind     =  1;    /*код ФИ для субъекта*/
         objalreg.PartyID        =  gAvoirData.T_MARKETID;
         if( GetEQ(objalreg) )
            /*подкачаем код фин.инcтрумента*/
            keynum(objcode, 0); 
            objcode.ObjectType   = 9; /*ФИ*/
            objcode.CodeKind     = objalreg.CodeKind;
            objcode.ObjectID     = avoiriss.FIID;
            if( GetEQ( objcode ) == false )
               /*не нашли!  вставим код ценной бумаге для биржи*/
               ClearRecord(objcode);
               objcode.ObjectType   =  9; /*ФИ*/
               objcode.CodeKind     =  objalreg.CodeKind;
               objcode.ObjectID     =  avoiriss.FIID;
               objcode.Code         =  gAvoirData.T_MARKETCODE;
               objcode.SysDate      =  Date();
               objcode.BankCloseDate=  Date(0,0,0);
               objcode.BankDate     =  {curdate};
               objcode.UserID       =  {oper};
               if( Insert(objcode) == false )
                  gAvoirData.MakeError("Ошибка: ошибка при добавлении кода (dobjcode_dbt) ценных бумаг для биржи " + string(party.rec.ShortName) );
                  ЗанестиЗаписьВПротокол(621, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при добавлении кода (dobjcode_dbt) ценных бумаг для биржи, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
                  AbortTrn;
               end;

               ClearRecord(objrgdoc);
               objrgdoc.ObjectType     =  9;
               objrgdoc.RegPartyKind   =  15;
               objrgdoc.RegDocKind     =  1;
               objrgdoc.CodeKind       =  objalreg.CodeKind;
               objrgdoc.ObjectID       =  avoiriss.FIID;
               objrgdoc.RegPartyID     =  party.rec.PartyID;
               objrgdoc.StartDate      =  {curdate};
               if( Insert(objrgdoc) == false )
                  gAvoirData.MakeError("Ошибка: ошибка при добавлении записи о привязке (dobjrgdoc_dbt) кода ц/б " + string(gAvoirData.T_MARKETCODE) +  "  к бирже " + string(party.ShortName) );
                  ЗанестиЗаписьВПротокол(622, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при добавлении записи о привязке (dobjrgdoc_dbt) кода ц/б к бирже, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
                  AbortTrn;
               end;
            else
               objcode.Code = gAvoirData.T_MARKETCODE;

               if( Update( objcode ) == false )
                  gAvoirData.MakeError( "Ошибка: ошибка при обновлении записи кода ценной бумаги для биржи в dobjcode_dbt" );
                  ЗанестиЗаписьВПротокол(623, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при обновлении записи кода ценной бумаги для биржи в dobjcode_dbt, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
                  AbortTrn;
               end;
            end;
         else
            ClearRecord(objkcode);
            objkcode.ObjectType  =  9; /*ФИ*/
            objkcode.Name        =  "Код биржи " + party.rec.ShortName;
            objkcode.ShortName   =  substr(party.rec.ShortName,1,4);
            objkcode.MaxCodeLen  =  35;
            objkcode.Definition  =  objkcode.Name;

            /*определяем новый номер кода*/
            sqlQuery = "SELECT max(objkcode.t_CodeKind) MaxCodeKind FROM dobjkcode_dbt objkcode WHERE objkcode.t_ObjectType = 9";
            DataSet = TRsbDataSet( sqlQuery );

            if( DataSet.MoveNext() )
               objkcode.CodeKind = DataSet.MaxCodeKind + 1;
            else
               objkcode.CodeKind = 1;
            end;

            if( Insert(objkcode) == false )
               gAvoirData.MakeError("Ошибка: ошибка при добавлении нового вида кода (dobjkcode_dbt) ценных бумаг для биржи " + string(party.rec.ShortName) );
               ЗанестиЗаписьВПротокол(624, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при добавлении нового вида кода (dobjkcode_dbt) ценных бумаг для биржи, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
               AbortTrn;
            end;

            ClearRecord(objalreg);
            objalreg.ObjectType     =  9;    /*ФИ*/
            objalreg.RegPartyKind   =  15;   /*субъект*/
            objalreg.RegDocKind     =  1;    /*код ФИ для субъекта*/
            objalreg.PartyID        =  gAvoirData.T_MARKETID;
            objalreg.CodeKind       =  objkcode.CodeKind;
            if( Insert(objalreg) == false )
               gAvoirData.MakeError("Ошибка: ошибка при добавлении привязки (dobjalreg_dbt) нового вида кода ценных бумаг к бирже " + string(party.rec.ShortName) );
               ЗанестиЗаписьВПротокол(625, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при добавлении привязки (dobjalreg_dbt) нового вида кода ценных бумаг к бирже, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
               AbortTrn;
            end;

            ClearRecord(objcode);
            objcode.ObjectType   =  9; /*ФИ*/
            objcode.CodeKind     =  objkcode.CodeKind;
            objcode.ObjectID     =  avoiriss.FIID;
            objcode.Code         =  gAvoirData.T_MARKETCODE;
            objcode.SysDate      =  Date();
            objcode.BankDate     =  {curdate};
            objcode.BankCloseDate=  Date(0,0,0);
            objcode.UserID       =  {oper};
            if( Insert(objcode) == false )
               gAvoirData.MakeError("Ошибка: ошибка при добавлении кода (dobjcode_dbt) ценных бумаг для биржи " + string(party.rec.ShortName) );
               ЗанестиЗаписьВПротокол(626, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при добавлении кода (dobjcode_dbt) ценных бумаг для биржи, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
               AbortTrn;
            end;

            ClearRecord(objrgdoc);
            objrgdoc.ObjectType     =  9;
            objrgdoc.RegPartyKind   =  15;
            objrgdoc.RegDocKind     =  1;
            objrgdoc.CodeKind       =  objkcode.CodeKind;
            objrgdoc.ObjectID       =  avoiriss.FIID;
            objrgdoc.RegPartyID     =  party.rec.PartyID;
            objrgdoc.StartDate      =  {curdate};
            if( Insert(objrgdoc) == false )
               gAvoirData.MakeError("Ошибка: ошибка при добавлении записи о привязке (dobjrgdoc_dbt) кода ц/б " + string(gAvoirData.T_MARKETCODE) +  "  к бирже " + string(party.rec.ShortName) );
               ЗанестиЗаписьВПротокол(627, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Ошибка: ошибка при добавлении записи о привязке (dobjrgdoc_dbt) кода ц/б к бирже, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
               AbortTrn;
            end;
         end;
      end;/*вставка\обновление кода на бирже*/

      if( gAvoirData.IsBond and (gAvoirData.T_SUBKIND != null) )
         /*Установить значение категории "Вид облигации" для облигации*/
         if( ( not DisconnectObjAttr( OBJTYPE_AVOIRISS,
                                      UniID( avoiriss, OBJTYPE_AVOIRISS ),
                                      BOUND_KIND )
             ) OR
             ( not ConnectObjAttr( CatErr, OBJTYPE_AVOIRISS,
                                   UniID( avoiriss, OBJTYPE_AVOIRISS  ),
                                   BOUND_KIND,
                                   gAvoirData.T_SUBKIND, NULL, NULL, Date(0,0,0)/*gAvoirData.T_INSTANCEDATE*/ )
             ) OR
             ( CatErr != 0 )
           )
            gAvoirData.MakeError("Предупреждение: ошибка при установке категории \"Вид облигации\" для ценной бумаги с FI_Code = " + string(fininstr.FI_Code) );
            ЗанестиЗаписьВПротокол(628, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Предупреждение: ошибка при установке категории 'Вид облигации' для ценной бумаги " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         end;
      end;

      /* Установить значение категории 101 - "Нестандартные условия выпуска" для ценной бумаги */
      DisconnectObjAttr( OBJTYPE_AVOIRISS, UniID( avoiriss, OBJTYPE_AVOIRISS ), 101 );
      if ((gAvoirData.T_NOTSTANDART != null) and (gAvoirData.T_NOTSTANDART == "X"))
         if( ( not ConnectObjAttr( CatErr, OBJTYPE_AVOIRISS,
                                   UniID( avoiriss, OBJTYPE_AVOIRISS  ),
                                   101, ПолучитьIDАтрибута(OBJTYPE_AVOIRISS, 101, 1), NULL, NULL, Date(0,0,0)/*gAvoirData.T_INSTANCEDATE*/ )
             ) OR
             ( CatErr != 0 )
           )
            gAvoirData.MakeError("Предупреждение: ошибка при установке категории \"Нестандартные условия выпуска\" для ценной бумаги с FI_Code = " + string(fininstr.FI_Code) );
            ЗанестиЗаписьВПротокол(629, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Предупреждение: ошибка при установке категории 'Нестандартные условия выпуска' для ценной бумаги " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         end;
      end;

      /*Установить значение категории 20 - "Эмиссионная для налогового учета" для ценной бумаги*/
      IsEmissiveAvoiriss = -1;

      if( (gAvoirData.T_EMISSIVE == null) OR (gAvoirData.T_EMISSIVE <= 0) )  /*  Если в буферной схеме признак эмиссионности не задан */
         /*
         "Если в буферной схеме признак эмиссионности не задан и: 
         - вид бумаги - акция обыкновенная или акция привелигированная или акция с правом конвертации, то бумага эмиссионная, или 
         - вид бумаги - облигация и валюта номинала - рубли, то бумага эмиссионная; 
         - паи - неэмиссионная" 
         */
         if( (gAvoirData.T_KIND == 1) OR (gAvoirData.T_KIND == 2) OR (gAvoirData.T_KIND == 7) OR (gAvoirData.T_KIND == 20) )
            IsEmissiveAvoiriss = 1;
         elif( gAvoirData.IsBond AND (fininstr.FaceValueFI == NATCUR) )
            IsEmissiveAvoiriss = 1;
         elif( gAvoirData.T_KIND == 16 )
            IsEmissiveAvoiriss = 0;
         end;
      elif( gAvoirData.T_EMISSIVE == 1 )
         IsEmissiveAvoiriss = 1;
      else
         IsEmissiveAvoiriss = 0;
      end;

      if( IsEmissiveAvoiriss != -1 )
	     GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(avoiriss, OBJTYPE_AVOIRISS), 20 /*Эмиссионная для НУ*/, null, null, NumInList, gAvoirData.T_INSTANCEDATE, ValidFromDate);
	     if (NumInList == NullVal) 
            if (NumInList != IsEmissiveAvoiriss)
               ЗанестиЗаписьВПротокол(630, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Предупреждение: изменился признак эмиссионности, бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
            end; 
	     end;

         if( ( not DisconnectObjAttr( OBJTYPE_AVOIRISS,
                                      UniID( avoiriss, OBJTYPE_AVOIRISS ),
                                      20 )
             ) OR
             ( not ConnectObjAttr( CatErr, OBJTYPE_AVOIRISS,
                                   UniID( avoiriss, OBJTYPE_AVOIRISS  ),
                                   20, ПолучитьIDАтрибута(OBJTYPE_AVOIRISS, 20, IsEmissiveAvoiriss), NULL, NULL, Date(0,0,0)/*gAvoirData.T_INSTANCEDATE*/ )
             ) OR
             ( CatErr != 0 )
           )
            gAvoirData.MakeError("Предупреждение: ошибка при установке категории \"Эмиссионная для налогового учета\" для ценной бумаги с FI_Code = " + string(fininstr.FI_Code) );
            ЗанестиЗаписьВПротокол(630, 20, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Предупреждение: ошибка при установке категории 'Эмиссионная для налогового учета', бумага " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         end;
      end;

      /*Установить значение категории 56 - "Является бумагой ВТС"*/
      if( (gAvoirData.T_VTINCLUDEDATE != null) and (gAvoirData.T_VTINCLUDEDATE != Date(0,0,0)) ) /*Дата включения бумаги в список ВТС> */
         if ( not DisconnectObjAttr( OBJTYPE_AVOIRISS, UniID(avoiriss, OBJTYPE_AVOIRISS), 56))
            ЗанестиЗаписьВПротокол(631, 21, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Предупреждение: ошибка при обновлении признака ценной бумаги " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         end;
         if ( not ConnectObjAttr( CatErr, OBJTYPE_AVOIRISS, UniID(avoiriss, OBJTYPE_AVOIRISS), 56, ПолучитьIDАтрибута(OBJTYPE_AVOIRISS, 56, 1/*T_VALUE*/), NULL, NULL, gAvoirData.T_VTINCLUDEDATE))
            ЗанестиЗаписьВПротокол(632, 21, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Предупреждение: ошибка при вставке признака ценной бумаги " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         end;
      end;

      if( (gAvoirData.T_VTEXCLUDEDATE != null) and (gAvoirData.T_VTEXCLUDEDATE != Date(0,0,0)) ) /* Дата исключения бумаги из списка ВТС */
         if ( not DisconnectObjAttr( OBJTYPE_AVOIRISS, UniID(avoiriss, OBJTYPE_AVOIRISS), 56, 0, gAvoirData.T_VTEXCLUDEDATE + 1))
            ЗанестиЗаписьВПротокол(631, 21, gAvoirData.T_AVOIRISSID, 1, StrFor(1), "Предупреждение: ошибка при обновлении признака ценной бумаги " + gAvoirData.T_DEFINITION, gAvoirData.T_INSTANCEDATE);
         end;
      end;
   end;
END;

MACRO ОбработатьЦБ( AvoirData:TAvoirData, Action:integer, Error:TArray ) : BOOL
   var   ret:bool = true;

   gAvoirData         = AvoirData;
   gAvoirData.Action  = Action;

   if( (Error != null) and (valtype(Error) == V_GENOBJ) )
      gAvoirData.Error   = Error;
   end;

   if( gAvoirData.InTrn == false )
      if (ProcessTrn(0, "ProcessAvoiriss", fininstr, avoiriss, avrinvst, objcode, objrgdoc, objkcode, objcode, objalreg, partyown ) == false )
         ret = false;
      end;
   else
      ProcessAvoiriss();
   end;

   return ret;
END;