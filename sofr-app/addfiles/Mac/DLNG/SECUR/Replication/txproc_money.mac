/*
$Name:             txproc_money.mac
$Module:           БОЦБ 
$Description:      Процедуры для репликации данных в БОЦБ из внешних систем
*/
IMPORT  txrepl_func, txproc_money_fun;
import "cb_sql.mac";

PRIVATE VAR SQL, cmd, rs, Счетчик, DealIsClose, SQL2,rs2,cmd2, MaxVal;

// Заполняет структуру памяти содержимым очередной строки DTXMONEY_DBT
MACRO ProcessMonOperation()
   var OperData = TMoneyInfo;
   var SQL1, cmd1, rs1;
   var IsErr = false; // обнаружена ошибка в исходных данных
    
   if (rs.Value("T_ACTION") == 1)
      OperData.T_DEALID    =  rs.Value("T_DEALID");
   else 
      OperData.T_DEALID    =  ПолучитьСоответствие(80, rs.Value("T_DEALID"));

      if (OperData.T_DEALID == -1)          
         ЗанестиЗаписьВПротокол(207, 120, rs.value("T_DEALID"), 1, StrFor(1), "Ошибка: обновление невозможно. операция " + String(rs.value("T_DEALID"):11:0) + " не найдена среди реплицированных", rs.Value("T_INSTANCEDATE"));
         IsErr = true;
      end;
   end;

   OperData.OldDealID = rs.Value("T_DEALID");

   if (rs.Value("T_TYPE") != NullVal)
       OperData.T_TYPE =  rs.Value("T_TYPE");
   else 
      IsErr = true;
      ЗанестиЗаписьВПротокол(208, 120, rs.value("T_DEALID"), 1, StrFor(1), "Ошибка: не указан тип записи (поле T_TYPE)", rs.Value("T_INSTANCEDATE"));
      return false;
   end;

   if (rs.Value("T_FUTOPERTYPE") != NullVal)
      OperData.T_FUTOPERTYPE =  rs.Value("T_FUTOPERTYPE");
   end;

   if ((rs.Value("T_CODE") != NullVal) and (rs.Value("T_CODE") != StrFor(32)) and (rs.Value("T_CODE") != StrFor(1)) and (rs.Value("T_CODE") != StrFor(0)))
      OperData.T_CODE    =  trim(rs.Value("T_CODE"));
   end;
   if ((rs.Value("T_EXTCODE") != NullVal) and (rs.Value("T_EXTCODE") != StrFor(32)) and (rs.Value("T_EXTCODE") != StrFor(1)) and (rs.Value("T_EXTCODE") != StrFor(0)))
      OperData.T_EXTCODE   =  trim(rs.Value("T_EXTCODE"));
   end;

   OperData.T_CLIENTID =  ПолучитьСоответствие(30, SQL_ConvTypeInteger(rs.Value("T_CLIENTID")));
   if ((OperData.T_CLIENTID == -1) and (rs.Value("T_ACTION") != 3))
        ЗанестиЗаписьВПротокол(208, 120, SQL_ConvTypeInteger(rs.value("T_CLIENTID")), 1, StrFor(1), "Ошибка: клиент " + String(rs.value("T_CLIENTID"):11:0) + " не найден.", rs.Value("T_INSTANCEDATE"));
      IsErr = true; 
   end;

   if ((rs.Value("T_CLIENTACCOUNT") != NullVal) and (rs.Value("T_CLIENTACCOUNT") != StrFor(32)) and (rs.Value("T_CLIENTACCOUNT") != StrFor(1)) and (rs.Value("T_CLIENTACCOUNT") != StrFor(0)))
      OperData.T_CLIENTACCOUNT =  rs.Value("T_CLIENTACCOUNT");
   end;

   if ((OperData.T_TYPE == 2) and (rs.Value("T_TAXPAYTYPE") != NullVal) and (rs.Value("T_TAXPAYTYPE") != StrFor(32)) and (rs.Value("T_TAXPAYTYPE") != StrFor(1)) and (rs.Value("T_TAXPAYTYPE") != StrFor(0)))
      OperData.T_TAXPAYTYPE  =  rs.Value("T_TAXPAYTYPE");
   else
      OperData.T_TAXPAYTYPE  =  0;
   end;

   OperData.T_TAXYEAR  =  SQL_ConvTypeDate(rs.Value("T_TAXYEAR"));
   if (rs.Value("T_DATE") != NullVal)
      OperData.T_DATE  =  rs.Value("T_DATE");
   end;
   OperData.T_SUM  =  SQL_ConvTypeSum(rs.Value("T_SUM"));
   OperData.T_FIID  =  ПолучитьСоответствие(10, SQL_ConvTypeInteger(rs.Value("T_FIID")));
   if ((OperData.T_FIID == -1) and (rs.Value("T_ACTION") != 3))
        ЗанестиЗаписьВПротокол(209, 120, SQL_ConvTypeInteger(rs.value("T_FIID")), 1, StrFor(1), "Ошибка: неизвестная валюта " + String(rs.value("T_FIID"):11:0), rs.Value("T_INSTANCEDATE"));
      IsErr = true; 
   end;

   OperData.T_INSTANCEDATE = Date(rs.Value("T_INSTANCEDATE"));

   if (IsErr == true) 
      return false; 
   end;

   OperData.InTRN    = TRUE;
   if (( ExecuteOperation( @OperData, rs.Value("T_ACTION"), rs) == false ) or (IsErr))
      Return FALSE;
   else
      if (rs.Value("T_ACTION") == 1)
         SQL1 = "delete from DTXREPLOBJ_DBT where T_OBJECTTYPE=120 and t_objstate = 2 and t_objectid = " + rs.Value("T_DEALID");
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;

         SQL1 = "insert into DTXREPLOBJ_DBT (T_OBJECTTYPE, T_OBJECTID, T_SUBOBJNUM, T_DESTID, T_DESTSUBOBJNUM,";
         SQL1 = SQL1 + "T_OBJSTATE) values (120, " + rs.Value("T_DEALID") + "," + string(OperData.T_TYPE) + ",";
         SQL1 = SQL1 + OperData.T_DEALID + " ,1 , 0)";
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;

         SQL1 = "update DTXMONEY_DBT set t_replstate = 1 where t_DEALID = " + rs.value("T_DEALID") + " and t_action = 1 and t_replstate = 0";
         SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;
      elif (rs.Value("T_ACTION") == 2)
         SQL1 = "update DTXMONEY_DBT set t_replstate = 1 where t_DEALID = " + rs.value("T_DEALID") + " and t_action = 2 and t_replstate = 0";
         SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;
      elif (rs.Value("T_ACTION") == 3)
         SQL1 = "update DTXMONEY_DBT set t_replstate = 1 where T_DEALID = " + rs.value("T_DEALID") + " and t_action = 3 and t_replstate = 0";
         SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;

         SQL1 = "update DTXREPLOBJ_DBT ots set ots.T_OBJSTATE = 2 where ots.T_OBJECTTYPE=120 and ots.T_OBJECTID=" + rs.value("T_DEALID");
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;
      end;

      Return TRUE;
   end;
OnError(er)
   if (Trim(er.message) != "Error:")
      ЗанестиЗаписьВПротокол(600, 120, rs.value("T_DEALID"), 1, StrFor(1), "Ошибка: " + er.message, rs.Value("T_INSTANCEDATE"));
   end;
   Aborttrn;
END;

MACRO Обработать_Денежную_Операцию(Action, t_instancedate, Init_Action:String)
   
   SQL = "select count(*) from DTXMONEY_DBT where t_replstate = 0 and t_action = " + Action + " and trunc(t_instancedate) = " + t_instancedate;
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);

   if (rs.Movenext)
      Счетчик = rs.Value(0);
      MaxVal = rs.Value(0);
   end;

   var ProgressIndicator = CreateProgressIndicator(true);
   ProgressIndicator.Start(Int(MaxVal), "Обработка денежных операций...");
   
   Счетчик = 0;

   SQL = "select * from DTXMONEY_DBT where t_replstate = 0 /*and t_type =4*/ and t_action = " + Action + " and trunc(t_instancedate) = " + t_instancedate + " order by t_dealid";
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);
  
   while (rs.Movenext)
      Счетчик = Счетчик + 1;
      ProgressIndicator.Update(Int(Счетчик), MaxVal);

      if (not ProcessTrn(Null, "ProcessMonOperation"))
         ЗанестиЗаписьВПротокол(600, 120, rs.value("T_DEALID"), 1, StrFor(1), "Ошибка при обработке денежной операции ");
      end;
   end;
   ProgressIndicator.Stop();

   return TRUE;
OnError(er)
   ЗанестиЗаписьВПротокол(600, 120, "", "", StrFor(1), "Ошибка: " + er.message, date(Init_Action));
END;
