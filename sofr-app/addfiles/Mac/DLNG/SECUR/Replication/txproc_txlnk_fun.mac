/*
$Name:             txproc_txlnk_fun.mac
$Module:           БОЦБ 
$Description:      Процедуры для репликации данных в БОЦБ из внешних систем
                   Процедура - ОбработатьСвязь
*/
IMPORT "sprepfun.mac";

/*данные с информацие по купону для передачи в процедуру ОбработатьКупон*/
CLASS TTXLinkData
   var   T_ID:integer;       /* ID связи */
   var   T_TYPE:integer;     /* тип связи */
   var   T_BUYID:integer;    /* ID лота покупки */
   var   T_SALEID:integer;   /* ID лота продажи */
   var   T_SOURCEID:integer; /* ID лота исходной покупки */
   var   T_DESTID:integer;   /* ID лота окончательной продаже*/
   var   T_LOT1ID:integer;   /* ID 1го дополнительного лота*/
   var   T_LOT2ID:integer;   /* ID 2го дополнительного лота*/
   var   T_DATE:date;        /* Дата связи */
   var   T_AMOUNT:money;     /* Колличество ЦБ в лоте */

   var   InTrn:bool;         /* признак выполнения процедуры уже в начатой транзакции */
   var   Action:integer;     /* выполняемое действие (1 - вставка, 2 - изменение, 3 - удаление.)*/
   var   Error:TArray;       /* массив строк с текстами ошибок*/

   MACRO MakeError( ErrorMes:string )
      this.Error[this.Error.Size] = string(this.Error.Size) + ":" + ErrorMes;
   END;

   MACRO PrintErrors()
      var i = 0;
      while( this.Error[i] != null )
         println( this.Error[i] );
         i = i + 1;   
      end;     
   END;  

   MACRO Init()
      this.InTrn           =  false;
      this.Error           =  TArray;
      this.Action          =  0;
   END;

   Init();   
END;  

/* Функция обработки налогового лота */
/* Action - обязательный. IN. Integer. Выполняемое действие (1 - вставка, 2 - изменение, 3 - удаление.)  */
/* Error - необязательный. OUT. String. */
/* Функция возвращает TRUE (0) в случае успешного выполнения и FALSE (1) в случае ошибки. */
/* Функция создания, изменения и удаления налогового лота. Функция должна сама запускать транзакцию.*/

PRIVATE  var   gTXLinkData  =  TTXLinkData;

MACRO ProcessTXLink()
  var Rsd;
   macro FindSCTXLOT( DealID )
     var query = TRsbDataSet( "select t_ID from dsctxlot_dbt where t_DealID = " + string(DealID), RSDVAL_CLIENT, RSDVAL_STATIC );
      if( query.MoveNext() )
         return query.ID;
      else
         gTXLinkData.MakeError("Ошибка: не найдена сделка с T_DEALID = " + string(DealID) );
         AbortTrn;
         return 0;
      end;
   end;

   macro GetNewIDLnk()
     var query = TRsbDataSet( "select MAX(t_ID) ID from dsctxlnk_dbt", RSDVAL_CLIENT, RSDVAL_STATIC );
      if( query.MoveNext() )
         return query.ID;
      else
         return 0
      end;
   end;

   var ParentID = 0, ParentDate = Date(0,0,0);

   macro GetParentLnk()
     var query = NULL, RetVal;

      if( (gTXLinkData.T_TYPE == TXLNK_CLSREPO) or (gTXLinkData.T_TYPE == TXLNK_CLSLOAN) )
         query = "select t_id, t_date from dsctxlnk_dbt " +
                 " where t_type = " + IIF(gTXLinkData.T_TYPE == TXLNK_CLSREPO, TXLNK_OPSREPO, TXLNK_OPSLOAN) + " and " +
                 "       t_buyid = " + string(gTXLinkData.T_SOURCEID) + " and " +
                 "       t_saleid = " + string(gTXLinkData.T_SALEID);
      elif( gTXLinkData.T_TYPE == TXLNK_DELRET )
         query = "select lnk.t_id, lnk.t_date from v_sctx_retbuy v_buy dsctxlnk_dbt lnk" +
                 " where lnk.t_id = v_buy.t_linkid "+
                 "       v_buy.t_saleid = " + string(gTXLinkData.T_BUYID) + " and " +
                 "       v_buy.t_buyid  = " + string(gTXLinkData.T_SOURCEID) + " and " +
                 "       v_buy.t_lot1id = " + string(gTXLinkData.T_LOT1ID);
      elif( gTXLinkData.T_TYPE == TXLNK_RETSPOS )
         query = "select lnk.t_id, lnk.t_date from v_sctx_retspbuy v_buy dsctxlnk_dbt lnk" +
                 " where lnk.t_id = v_buy.t_linkid "+
                 "       v_buy.t_saleid = " + string(gTXLinkData.T_BUYID) + " and " +
                 "       v_buy.t_buyid  = " + string(gTXLinkData.T_SOURCEID) + " and " +
                 "       v_buy.t_lot1id = " + string(gTXLinkData.T_LOT1ID);
      elif( gTXLinkData.T_TYPE == TXLNK_DELRET2 )
         query = "select t_id, t_date from dsctxlnk_dbt " +
                 " where t_type = " + TXLNK_DELRET + " and " +
                 "       t_buyid = " + string(gTXLinkData.T_BUYID) + " and " +
                 "       t_saleid = " + string(gTXLinkData.T_SALEID) + " and " +
                 "       t_sourceid = " + string(gTXLinkData.T_SOURCEID) + " and " +
                 "       t_lot1id = " + string(gTXLinkData.T_LOT1ID);
      end;

      if( query )
         RetVal = TRsbDataSet( query, RSDVAL_CLIENT, RSDVAL_STATIC );
         if( RetVal.MoveNext() )
            ParentID = RetVal.ID;
            ParentDate = RetVal.Date;
         end;
      end;
   end;
   
   
   var   IsWrongParms:bool = false,
         ret:bool          = true;
   var   sqlcmd:string     = "";

   if( (gTXLinkData.t_ID == null) and 
       (gTXLinkData.Action != 1) )
      gTXLinkData.MakeError("Ошибка: не задан T_ID связи в налоговом учете для обновления." );
      AbortTrn;
   end;

   if( (gTXLinkData.T_BUYID != null) and (gTXLinkData.T_BUYID > 0) )
      gTXLinkData.T_BUYID = FindSCTXLOT(gTXLinkData.T_BUYID);
   else
      gTXLinkData.MakeError("Ошибка: параметр T_BUYID должен быть обязательно заполнен." );
      AbortTrn;
   end;

   if( (gTXLinkData.T_SALEID != null) and (gTXLinkData.T_SALEID > 0) )
      gTXLinkData.T_SALEID = FindSCTXLOT(gTXLinkData.T_SALEID);
   else
      gTXLinkData.MakeError("Ошибка: параметр T_SALEID должен быть обязательно заполнен." );
      AbortTrn;
   end;

   if( (gTXLinkData.T_SOURCEID != null) and (gTXLinkData.T_SOURCEID > 0) )
      gTXLinkData.T_SOURCEID = FindSCTXLOT(gTXLinkData.T_SOURCEID);
   else
      gTXLinkData.T_SOURCEID = 0;
   end;

   if( (gTXLinkData.T_DESTID != null) and (gTXLinkData.T_DESTID > 0) )
      gTXLinkData.T_DESTID = FindSCTXLOT(gTXLinkData.T_DESTID);
   else
      gTXLinkData.T_DESTID = 0;
   end;

   if( (gTXLinkData.T_LOT1ID != null) and (gTXLinkData.T_LOT1ID > 0) )
      gTXLinkData.T_LOT1ID = FindSCTXLOT(gTXLinkData.T_LOT1ID);
   else
      gTXLinkData.T_LOT1ID = 0;
   end;

   if( (gTXLinkData.T_LOT2ID != null) and (gTXLinkData.T_LOT2ID > 0) )
      gTXLinkData.T_LOT2ID = FindSCTXLOT(gTXLinkData.T_LOT2ID);
   else
      gTXLinkData.T_LOT2ID = 0;
   end;

   if( (gTXLinkData.T_BUYID== 0) and
       (gTXLinkData.T_SALEID== 0) and
       (gTXLinkData.T_SOURCEID== 0) and
       (gTXLinkData.T_DESTID== 0) and
       (gTXLinkData.T_LOT1ID== 0) and
       (gTXLinkData.T_LOT2ID== 0) )
      gTXLinkData.MakeError("Ошибка: должна быть задана хотя бы одна сделка в связи." );
      AbortTrn;
   end;

   if( (gTXLinkData.T_TYPE == null) or (gTXLinkData.T_TYPE == 0) )
      gTXLinkData.MakeError("Ошибка: тип связи должен быть задан." );
      AbortTrn;
   end;

   if( (gTXLinkData.T_AMOUNT == null) or (gTXLinkData.T_AMOUNT == $0) )
      gTXLinkData.MakeError("Ошибка: количество ценных бумаг в связи не может быть нулевым." );
      AbortTrn;
   end;

   if( (gTXLinkData.T_DATE == null) or (gTXLinkData.T_DATE == Date(0,0,0)) )
      gTXLinkData.MakeError("Ошибка: должна быть задана дата связи." );
      AbortTrn;
   end;

   GetParentLnk();

   if( gTXLinkData.Action == 1 )
      sqlcmd = "insert into dsctxlnk_dbt (t_Type,t_BuyID,t_SaleID,t_SourceID,t_DestID,t_Lot1ID,t_Lot2ID,t_Amount,t_Date,t_Short,t_Ret,t_Ret2,t_RetSP,t_ParentID,t_ParentDate,t_BegDate,t_EndDate,t_RetFlag) " +
               "            values("+gTXLinkData.T_TYPE+","+
                                     gTXLinkData.T_BUYID+","+
                                     gTXLinkData.T_SALEID+","+
                                     gTXLinkData.T_SOURCEID+","+
                                     gTXLinkData.T_DESTID+","+
                                     gTXLinkData.T_LOT1ID+","+
                                     gTXLinkData.T_LOT2ID+","+
                                     gTXLinkData.T_AMOUNT+","+
                                     GetSQLDate(gTXLinkData.T_DATE)+","+
                                     "0,0,0,0,"+
                                     ParentID+", "+ 
                                     GetSQLDate(ParentDate)+","+
                                     GetSQLDate(Date(0,0,0))+","+
                                     GetSQLDate(Date(0,0,0))+","+
                                     "CHR(0));";
      Rsd = SQL_Execute(sqlcmd);
      if( Rsd.connection.environment.ErrorCount > 0 )
         gTXLinkData.MakeError("Ошибка: ошибка при вставке записи в базу данных." );
         AbortTrn;
      end;

      gTXLinkData.T_ID = GetNewIDLnk();

    elif( gTXLinkData.Action == 3 )
      sqlcmd = "delete from dsctxlnk_dbt where t_ID = " + string(gTXLinkData.T_ID) + ";";
      SQL_Execute(sqlcmd);
    end;

   Return TRUE;
END;

MACRO ОбработатьСвязь( TXLinkData:TTXLinkData, Action:integer, Error:TArray ) : BOOL
   var   ret:bool = true;

   gTXLinkData         = TXLinkData;
   gTXLinkData.Action  = Action;

   if( (Error != null) and (valtype(Error) == V_GENOBJ) )
      gTXLinkData.Error   = Error;
   end;

   if( gTXLinkData.InTrn == false )
      if (ProcessTrn(0, "ProcessTXLink" ) == false )   
         ret = false;
      end;
   else
      ProcessTXLink();
   end;

   return ret;
END;