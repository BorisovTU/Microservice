/*
$Name:             txloadlog_excel.mac
$Module:           БОЦБ
$Description:      Отчет о репликации объектов
*/
import "txrepl_func.mac", "ws_report.mac", "ws_progress.mac";

private var Header = 
"┌─────────────┬─────────────────────────┬───────────────┬────────────┬──────────────┬──────────────┬─────────────┬──────────────┬───────────────────────────────┬─────┬───────┬──────────┬─────────────────────┬──────────────┬────────────┬────────────┬──────┬──────┬──────────┬────────┐\n"+
"│   Код       │   Наименование бумаги   │  Тип объекта  │ ID объекта │ Вид операции │ Номер сделки │ ID операции │ Дата в учете │           Сообщение           │Важно│  Код  │   Дата   │   Наименование поля │ T_CORRECTION │ T_CORRUSER │ T_CORRTIME │  ID  │  ID  │   Дата   │ Время  │\n"+
"│  бумаги     │                         │               │            │   (сделки)   │              │  (сделки)   │              │                               │ сть │Сообщен│ выгрузки │                     │              │            │            │записи│подобъ│ записи в │записи в│\n"+
"│             │                         │               │            │              │              │             │              │                               │     │   ия  │   в БС   │                     │              │            │            │      │ екта │  отчете  │ отчете │\n"+
"├─────────────┼─────────────────────────┼───────────────┼────────────┼──────────────┼──────────────┼─────────────┼──────────────┼───────────────────────────────┼─────┼───────┼──────────┼─────────────────────┼──────────────┼────────────┼────────────┼──────┼──────┼──────────┼────────┤\n"+
"│      1      │            2            │       3       │      4     │       5      │       6      │      7      │       8      │               9               │  10 │   11  │    12    │          13         │      14      │     15     │     16     │  17  │  18  │    19    │   20   │\n"+
"├─────────────┼─────────────────────────┼───────────────┼────────────┼──────────────┼──────────────┼─────────────┼──────────────┼───────────────────────────────┼─────┼───────┼──────────┼─────────────────────┼──────────────┼────────────┼────────────┼──────┼──────┼──────────┼────────┤";

private macro PrintLine( Rep, str0:String, strNom:String, str1:String, str2:String, str3:String, str4:String, str5:String, str6:String, str7:Integer, str8:Integer, str9:Date,
                         str10:String, str11:String, str12:String, str13:Time, str14:Integer, str15:String, str16:Date, str17:Time, BalanceDate )
   if (str9 == Date(2,1,1) - 1)
      str9 = Date(0,0,0);
   end;

   Rep.AddPrintCell( str0,        0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str1,        0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str2,        0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str3,        0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str4,        0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( strNom,      0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str5,        0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( BalanceDate, 0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str6,        0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str7,        0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str8,        0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str9,        0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str10,       0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str11,       0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str12,       0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str13,       0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str14,       0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str15,       0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str16,       0, 0, "c:ex_FZ(8)" );
   Rep.AddPrintCell( str17,       0, 0, "c:ex_FZ(8)" );
   Rep.AddStr();
end;

private macro RunReport(Date_Begin, Time_Begin, Date_End, Time_End, Severity, Err_OR_Warn, isWeb, FullReportFileName: @string)
   private var SQL, cmd, rs, SQL_Time = "", SQL_Severity = "", SQL_Err_OR_Warn = "", SQL_Diap = "", SQL_Date_ToBS = "";
   private var total = 0, MaxVal = 0, objtype, НаименованиеБумаги, ВидОперации, IDОперации, КодБумаги, НомерСделки, BalanceDate;

   if ((Time_Begin != Time(0,0)) or (Time_End != Time(0,0)))
      SQL_Time = " and to_char(t_msgtime, 'HH24:MI') between '" + Time_Begin + "' and '" + Time_End + "'";
   end;

   if (Severity != 0)
      SQL_Severity = " and t_Severity = " + Severity;
   end;

   if (Err_OR_Warn == 1)
      SQL_Err_OR_Warn = " and t_message like 'Предупреждение%'";
   elif (Err_OR_Warn == 2)
      SQL_Err_OR_Warn = " and t_message like 'Ошибка%'";
   end;

   SQL = " SELECT count(*) "
       + "   FROM dtxloadlog_dbt "
       + "  WHERE trunc(t_msgtime) between " + getsqlDate_SK(Date_Begin) + " and " + getsqlDate_SK(Date_End) + SQL_Time + SQL_Severity + SQL_Err_OR_Warn + SQL_Diap + SQL_Date_ToBS;

   cmd = RsdCommand(SQL);
   rs = RsdRecordSet(cmd);
   rs.Movenext;
   total = int(rs.Value(0));
   MaxVal = total;

   SQL = " SELECT t_correction, t_corrtime, CASE t_corruser WHEN CHR (1) THEN chr(0) ELSE t_corruser END t_corruser, "
       + "        CASE t_field WHEN CHR (1) THEN chr(0) ELSE t_field END t_field, "
       + "        t_id, t_instancedate, t_message, t_msgcode, t_msgtime, t_objectid, t_objtype, t_severity, t_subobjnum "
       + "   FROM dtxloadlog_dbt"
       + "  WHERE trunc(t_msgtime) between " + getsqlDate_SK(Date_Begin) + " and " + getsqlDate_SK(Date_End) + SQL_Time + SQL_Severity + SQL_Err_OR_Warn + SQL_Diap + SQL_Date_ToBS
       + " ORDER BY t_id";

   cmd = RsdCommand(SQL);
   rs = RsdRecordSet(cmd);
   
   var ProgressIndicator = CreateProgressIndicator(true);
   ProgressIndicator.Start(Int(MaxVal), "Обработка...");

   var Rep = CMakeReport(Header);
   total = 0;

   while( rs.MoveNext )
      total = total + 1;
      ProgressIndicator.Update(Int(total), MaxVal, "Обработка...");

      НаименованиеБумаги = ВидОперации = IDОперации = КодБумаги = НомерСделки = BalanceDate = "";

      if (rs.Value("T_OBJTYPE") == 0)
         objtype = "0";
      elif (rs.Value("T_OBJTYPE") == 10)
         objtype = "Валюта";
      elif (rs.Value("T_OBJTYPE") == 20)
         objtype = "Ценная бумага";
         НаименованиеБумаги = ПолучитьНаименованиеБумаги(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
        КодБумаги = ПолучитьКодБумаги(rs.Value("T_OBJECTID"));
      elif (rs.Value("T_OBJTYPE") == 21)
         objtype = "Код ценной бумаги";
      elif (rs.Value("T_OBJTYPE") == 30)
         objtype = "Субъект";
      elif (rs.Value("T_OBJTYPE") == 40)
         objtype = "Сектор";
      elif (rs.Value("T_OBJTYPE") == 50)
         objtype = "Купон";
      elif (rs.Value("T_OBJTYPE") == 60)
         objtype = "Амортизация";
      elif (rs.Value("T_OBJTYPE") == 70)
         objtype = "Курс/Котировка";
      elif (rs.Value("T_OBJTYPE") == 80)
         objtype            = "Сделка";
         НаименованиеБумаги = ПолучитьНаименованиеБумагиПоСделке(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
         КодБумаги          = ПолучитьКодБумагиПоСделке(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
         ВидОперации        = ПолучитьВидОперацииSK(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
         IDОперации         = Trim(String(rs.Value("T_OBJECTID"):15:0));
         BalanceDate        = ПолучитьДатуУчетаСделки(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
      elif (rs.Value("T_OBJTYPE") == 90)
         objtype = "Требование/Обязательство";
         НаименованиеБумаги = ПолучитьНаименованиеБумагиПоТО(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
         КодБумаги = ПолучитьКодБумагиПоТО(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
         ВидОперации = ПолучитьВидОперацииТО(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
         IDОперации = ПолучитьIDОперацииПоТО(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
      elif (rs.Value("T_OBJTYPE") == 100)
         objtype = "Комиссия";
         IDОперации = ПолучитьIDОперацииПоКомиссии(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
         НаименованиеБумаги = ПолучитьНаименованиеБумагиПоКомиссии(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
         КодБумаги = ПолучитьКодБумагиПоКомиссии(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
         ПолучитьКодБумагиПоКомиссии(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
         ВидОперации = ПолучитьВидОперацииПоКомиссии(rs.Value("T_OBJECTID"), rs.Value("T_INSTANCEDATE"));
      end;

      if (IDОперации != "")
         НомерСделки = НомерОперацииПоID(IDОперации);
      end;

      PrintLine(Rep, КодБумаги, НомерСделки, НаименованиеБумаги,
                     objtype,
                     Trim(String(rs.Value("T_OBJECTID"):15:0)),
                     ВидОперации,
                     IDОперации,
                     rs.Value("T_MESSAGE"),
                     rs.Value("T_SEVERITY"),
                     rs.Value("T_MSGCODE"),
                     SQL_ConvTypeDate(rs.Value("T_INSTANCEDATE")),
                     rs.Value("T_FIELD"),
                     SQL_ConvTypeStr(rs.Value("T_CORRECTION")),
                     rs.Value("T_CORRUSER"),
                     rs.Value("T_CORRTIME"),
                     rs.Value("T_ID"),
                     Trim(String(rs.Value("T_SUBOBJNUM"):15:0)),
                     rs.Value("T_MSGTIME"),
                     rs.Value("T_MSGTIME"),
                     BalanceDate);
   end;

   ProgressIndicator.Stop();

   if (isWeb) 
      FullReportFileName = WebReportGenerator(Rep).Generate(true);
   else   
      Rep.PrintWinRep();
      Rep.ShowWinRep();
      Rep.PrintRep();
   end;
end;

macro RunWebReport(model)
   var FullReportFileName = "";
   if (model != null)
      RunReport(Date(model.DateMin),
                model.TimeMin,
                Date(model.DateMax),
                model.TimeMax,
                model.ImpMes,
                model.CatMes,
                true,
                @FullReportFileName);
   end;
   return PackWebReport(FullReportFileName);
end;