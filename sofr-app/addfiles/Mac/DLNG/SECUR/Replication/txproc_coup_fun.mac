/*
$Name:             txproc_coup_fun.mac
$Module:           БОЦБ 
$Description:      Процедуры для репликации данных в БОЦБ из внешних систем
                   Процедура - ОбработатьКупон
                   обработка купона\аммортизации по ценной бумаге
*/
IMPORT spserv, txrepl_func;

CONST OBJTYPE_CUPON = 17; /*тип объекта - купон ценной бумаги, вынес сюда, пока не импортировано в RSL*/

/*данные с информацие по купону для передачи в процедуру ОбработатьКупон*/
CLASS TCouponData
   var   T_ID:integer;           /* Идентификатор купона/частичного погашения в целевой системе */
   var   T_TYPE:integer;         /* Тип погашения: 50 - Купон 60 - Амортизация 120 - Дивиденд */
   var   T_FIID:integer;         /* Идентификатор ценной бумаги, к которой относится купон   */
   var   T_NUMBER:integer;       /* Номер купона или частичного погашения ценной бумаги.  */
   var   T_OLDNUMBER:integer;    /* Старый номер купона - на случай, если он менялся */
   var   T_FIRSTDATE:date;       /* Дата начала купонного периода/дата предыдущего частичного погашения. По умолчанию равна дате погашения предыдущего купона/погашения.*/
   var   T_DRAWINGDATE:date;     /* Дата погашения купона (окончание купонного периода)/дата частичного погашения (амортизации)  */
                                 /* Дата фактического погашения определяется по операции погашения купона/частичного погашения   */
   var   T_OWNFIXDATE:date;      /* Дата фиксации владельца */
   var   T_RELATIVEINCOME:string;/* Признак - Означает, что для всех купонов выпуска доход начисляется в процентах. Задается только для купонных бумаг.*/   
   var   T_INCOME:money;         /* Сумма годового дохода (если он задается не в процентах), или процент годового дохода (если он задается в процентах).*/
   var   T_INCOMEPOINT:integer;  /* Точность дохода. По умолчанию 4  */
   var   T_INCOMESCALE:integer;  /* Масштаб дохода. По умолчанию 1   */
   var   T_ISCLOSED:string;      /* Признак погашения. Означает, что купон погашен/амортизация исполнена */
   var   T_WARRANTID:numeric;    /* Идентификатор купона/частичного погашения в исходной системе */
   var   T_INSTANCEDATE:date;

   var   NameAvoiriss:string;
   var   InTrn:bool; /*признак выполнения процедуры уже в начатой транзакции*/

   var   CouponExist:bool;  /* признак наличия в системе обрабатываемого платежа (требования/обязательства)*/   
   var   Action:integer;      /* выполняемое действие (1 - вставка, 2 - изменение, 3 - удаление.)*/
   var   Error:TArray;        /* массив строк с текстами ошибок*/

   MACRO MakeError( ErrorMes:string )
      this.Error[this.Error.Size] = string(this.Error.Size) + ":" + ErrorMes;
   END;

   MACRO PrintErrors()
      var i = 0;
      while( this.Error[i] != null )
         println( this.Error[i] );
         i = i + 1;
      end;
   END;

   MACRO Init()
      this.InTrn         =  false;
      this.CouponExist   =  false;
      this.Error         =  TArray;
      this.Action        =  0;
   END;

   Init();
END;

/* Функция обработки купона */
/* Action - обязательный. IN. Integer. Выполняемое действие (1 - вставка, 2 - изменение, 3 - удаление.)  */
/* Error - необязательный. OUT. String. */
/* Функция возвращает TRUE (0) в случае успешного выполнения и FALSE (1) в случае ошибки. */
/* Функция создания, изменения и удаления купона\аммортизации по ц/б. Функция должна сама запускать транзакцию.*/

PRIVATE FILE fiwarnt  ("fiwarnts.dbt") write;     /* купоны   */
PRIVATE FILE avoiriss ("avoiriss.dbt");
PRIVATE var  gCouponData  =  TCouponData;

MACRO ProcessCoupon()
   var   IsWrongParms:bool = false,
         ret:bool          = true;
   var   strcmd:string     = "";

   /*чистим используемые структуры*/
   ClearRecord( fiwarnt );
   KeyNum( fiwarnt, 3 );

   if( gCouponData.Action != 1 ) /*Не вставка - проверим существование купона в целевой системе*/
      fiwarnt.ID = gCouponData.T_ID;
      if( GetEQ( fiwarnt ) )
         gCouponData.CouponExist = true;               
      else 
         gCouponData.CouponExist = false;   
      end;
   end;

   if ( IsWrongParms == false )  
      if   (gCouponData.Action  == 1  )  /* вставка   */
         if(gCouponData.CouponExist )
            gCouponData.MakeError("Ошибка: купон\\амортизация\\дивиденд уже существуют в целевой системе");
            ЗанестиЗаписьВПротокол(415, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: купон/аммортизация/дивиденд № " + gCouponData.T_NUMBER + " уже существут в целевой системе, бумага " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
            AbortTrn;
         end;
      elif (gCouponData.Action  == 2  )  /* изменение */
         if(gCouponData.CouponExist == false )
            gCouponData.MakeError("Ошибка: купон\\амортизация\\дивиденд не найден в целевой системе");
            ЗанестиЗаписьВПротокол(416, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: купон/амортизация/дивиденд № " + gCouponData.T_NUMBER + " не найден в целевой системе, бумага " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
            AbortTrn;
         end;
      elif (gCouponData.Action  == 3  )  /* удаление  */
         if(gCouponData.CouponExist == false )
            gCouponData.MakeError("Ошибка: купон\\амортизация\\дивиденд уже удален в целевой системе");
            ЗанестиЗаписьВПротокол(417, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: купон/амортизация/дивиденд № " + gCouponData.T_NUMBER + " уже удален в целевой системе, бумага " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
            AbortTrn;        
         end;
      else
         gCouponData.MakeError( "Ошибка: неверный вид выполняемого действия: " + string( gCouponData.Action ) ); 
         ЗанестиЗаписьВПротокол(521, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: неверный вид выполняемого действия, купон/амортизация/дивиденд № " + gCouponData.T_NUMBER + " по бумаге " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
         AbortTrn;
      end;
   end;

   /*FIWARNTS*/
   /*инициализация данных операции для вставки\изменения*/
   if( (IsWrongParms == false) AND ((gCouponData.Action == 1) OR (gCouponData.Action == 2)) )
      if( gCouponData.T_TYPE == null )
         gCouponData.MakeError("Ошибка: не задан тип купона/амортизации/дивиденда, параметр T_TYPE" );
         ЗанестиЗаписьВПротокол(515, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: не задан тип купона/амортизации/дивиденда № " + gCouponData.T_NUMBER + " , параметр T_TYPE, купон/амортизация по бумаге " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
         AbortTrn;
      elif( (gCouponData.T_TYPE != 50) and (gCouponData.T_TYPE != 60) and (gCouponData.T_TYPE != 120) )
         gCouponData.MakeError("Ошибка: неверно задан параметр - тип купона/амортизации/дивиденда, T_TYPE" );
         ЗанестиЗаписьВПротокол(516, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: неверно задан параметр - тип купона/амортизации/дивиденда №" + gCouponData.T_NUMBER + " , T_TYPE, купон/амортизация по бумаге " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
         AbortTrn;
      else
         if (( gCouponData.T_TYPE == 50 ) OR ( gCouponData.T_TYPE == 120 ))
            fiwarnt.IsPartial = UNSET_CHAR; /* купон, дивиденд */
         else
            fiwarnt.IsPartial = SET_CHAR;   /* амортизация */
         end;

         if( (gCouponData.T_FIID != null) and (gCouponData.T_FIID > 0) )
            ClearRecord(avoiriss);
            KeyNum(avoiriss,0);
            avoiriss.FIID = gCouponData.T_FIID;
            if( not GetEQ(avoiriss) )
               gCouponData.MakeError("Ошибка: не найдена для купона/амортизации/дивиденда ценная бумага с FIID = " + string(gCouponData.T_FIID) );
               ЗанестиЗаписьВПротокол(517, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: не найдена для купона/амортизации/дивиденда ценная бумага, купон/амортизация/дивиденд № " + gCouponData.T_NUMBER + " по бумаге " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
               AbortTrn;            
            end;
   
            fiwarnt.FIID = gCouponData.T_FIID;
         else
            gCouponData.MakeError("Ошибка: не найдена ценная бумага" );
            ЗанестиЗаписьВПротокол(519, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: не найдена ценная бумага " + gCouponData.NameAvoiriss + ", купон/амортизация/дивиденд № " + gCouponData.T_NUMBER, gCouponData.T_INSTANCEDATE);
            AbortTrn;
         end;
   
         if( (gCouponData.T_OLDNUMBER != null) and (gCouponData.T_OLDNUMBER != "") )  
            fiwarnt.Number    =  substr(string(gCouponData.T_OLDNUMBER),1,15);
         else
            gCouponData.MakeError("Ошибка: не задан номер купона/амортизации/дивиденда, T_NUMBER" );
            ЗанестиЗаписьВПротокол(520, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: не задан номер купона/амортизации/дивиденда, T_NUMBER, купон/амортизация/дивиденд № " + gCouponData.T_NUMBER + " по бумаге " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
            AbortTrn;
         end;
      end;
   
      fiwarnt.Name      =  substr(string(gCouponData.T_NUMBER),1,25);
      fiwarnt.Definition=  fiwarnt.Name;
      fiwarnt.Number    =  substr(string(gCouponData.T_NUMBER),1,15);

      if( (gCouponData.T_FIRSTDATE != null) and (gCouponData.T_FIRSTDATE != date(0,0,0)) )
         fiwarnt.FirstDate = (gCouponData.T_FIRSTDATE + 1);
      else
         gCouponData.MakeError("Ошибка: не задана дата начала купона/амортизации/дивиденда, T_FIRSTDATE" );
         ЗанестиЗаписьВПротокол(522, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: не задана дата начала купона/амортизации/дивиденда № " + gCouponData.T_NUMBER + ", T_FIRSTDATE, бумага " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
         AbortTrn;         
      end;

      if( (gCouponData.T_DRAWINGDATE != null) and (gCouponData.T_DRAWINGDATE != date(0,0,0)) )
         fiwarnt.DrawingDate = gCouponData.T_DRAWINGDATE;
      else
         gCouponData.MakeError("Ошибка: не задана дата погашения купона/амортизации/дивиденда, T_DRAWINGDATE" );
         ЗанестиЗаписьВПротокол(523, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: не задана дата погашения купона/амортизации/дивиденда № " + gCouponData.T_NUMBER + ", T_DRAWINGDATE, бумага " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
         AbortTrn;         
      end;

      if( (gCouponData.T_RELATIVEINCOME != null) )
         fiwarnt.RelativeIncome  =  gCouponData.T_RELATIVEINCOME;
      else
         fiwarnt.RelativeIncome  =  "";
      end;
      if( (gCouponData.T_INCOME != null) and (gCouponData.T_INCOME > $0) )
         if( fiwarnt.RelativeIncome != "" )
            fiwarnt.IncomeRate   =  gCouponData.T_INCOME;
            fiwarnt.IncomeVolume =  $0;
         else
            fiwarnt.IncomeRate   =  $0;
            fiwarnt.IncomeVolume =  gCouponData.T_INCOME;
            if (gCouponData.T_TYPE == 60)
               fiwarnt.IncomeRate   =  ПолучитьПроцентДляЧП(gCouponData.T_INCOME, gCouponData.T_FIID);
            end;
         end;
      else
         gCouponData.MakeError("Предупреждение: не задана величина годового дохода по купону/аммортизации/дивиденд, T_INCOME" );
         ЗанестиЗаписьВПротокол(524, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Предупреждение: не задана величина годового дохода по купону/аммортизации/дивиденду № " + gCouponData.T_NUMBER + ", T_INCOME, бумага " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
         fiwarnt.IncomeRate   = 0;
         fiwarnt.IncomeVolume = 0;
      end;

      if( gCouponData.T_INCOMEPOINT != null )
         fiwarnt.IncomePoint = gCouponData.T_INCOMEPOINT;
      else
         fiwarnt.IncomePoint = 4;
      end;

      if( gCouponData.T_INCOMESCALE != null )
         fiwarnt.IncomeScale = gCouponData.T_INCOMESCALE;
      else
         fiwarnt.IncomeScale = 1;
      end;

      if( gCouponData.T_ISCLOSED != null )
         fiwarnt.IsClosed = gCouponData.T_ISCLOSED;
         fiwarnt.SpIsClosed = gCouponData.T_ISCLOSED;         
         fiwarnt.TsIsClosed = gCouponData.T_ISCLOSED;
      end;
   end;

   /*некорректные входящие параметры - прерываем транзакцию*/
   if( IsWrongParms == true )
      AbortTrn;
   end;

   fiwarnt.listdate = gCouponData.T_OWNFIXDATE;

   if( gCouponData.Action == 1  )  /* вставка   */
      if( Insert( fiwarnt ) == false )
         gCouponData.MakeError( "Ошибка: ошибка при вставке записи о купоне в dfiwarnts_dbt" );
         ЗанестиЗаписьВПротокол(634, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: ошибка при вставке записи о купоне/амортизации/дивиденде № " + gCouponData.T_NUMBER + " в dfiwarnts_dbt, бумага " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
         AbortTrn;
      end;
      gCouponData.T_ID = fiwarnt.ID;
   elif( gCouponData.Action  == 2  )  /* изменение */
      if( Update( fiwarnt ) == false )
         gCouponData.MakeError( "Ошибка: ошибка при обновлении записи о купоне в dfiwarnts_dbt" );
         ЗанестиЗаписьВПротокол(635, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: ошибка при обновлении записи о купоне/амортизации/дивиденде № " + gCouponData.T_NUMBER + " в dfiwarnts_dbt, бумага " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
         AbortTrn;
      end;
   elif( gCouponData.Action  == 3  )  /* удаление  */
      /*удалим примечания к купону*/
      strcmd =  "delete from dnotetext_dbt note where note.t_ObjectType = " + string(OBJTYPE_CUPON) + " and note.t_DocumentID = '" + string(UniID(fiwarnt, OBJTYPE_CUPON)) + "'";
      execSQL(strcmd);

      if( Delete( fiwarnt ) == false )
         gCouponData.MakeError( "Ошибка: ошибка при удалении записи о купоне в dfiwarnts_dbt" );
         ЗанестиЗаписьВПротокол(636, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: ошибка при удалении записи о купоне/амортизации/дивиденде №" + gCouponData.T_NUMBER + " в dfiwarnts_dbt, бумага " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
         AbortTrn;
      elif (gCouponData.T_INSTANCEDATE > gCouponData.T_FIRSTDATE)
         ЗанестиЗаписьВПротокол(637, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Предупреждение: удален исторический купон/амортизация/дивиденд № " + gCouponData.T_NUMBER + ", бумага " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
      end;
   else
      gCouponData.MakeError( "Ошибка: неверный вид выполняемого действия: " + string( gCouponData.Action ) ); 
      ЗанестиЗаписьВПротокол(521, gCouponData.T_TYPE, gCouponData.t_WarrantID, 1, StrFor(1), "Ошибка: неверный вид выполняемого действия, купон/амортизация/дивиденд №" + gCouponData.T_NUMBER + " по бумаге " + gCouponData.NameAvoiriss, gCouponData.T_INSTANCEDATE);
      AbortTrn;
   end;
END;

MACRO ОбработатьКупон( CouponData:TCouponData, Action:integer, Error:TArray ) : BOOL
   var   ret:bool = true;

   gCouponData         = CouponData;
   gCouponData.Action  = Action;

   if( (Error != null) and (valtype(Error) == V_GENOBJ) )
      gCouponData.Error   = Error;
   end;

   if( gCouponData.InTrn == false )
      if (ProcessTrn(0, "ProcessCoupon", fiwarnt ) == false )   
         ret = false;
      end;
   else
      ProcessCoupon();
   end;
   return ret;
END;