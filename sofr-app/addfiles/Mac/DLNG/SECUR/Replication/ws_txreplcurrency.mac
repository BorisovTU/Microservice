/*
$Name:             ws_txreplcurrency.mac
$Module:           АРНУ
$Description:      WEB. АРНУ. Макрос сервисов виджета валют для репликации
*/

import "cb_sql.mac";
import "ws_validation.mac";
import "ws_datafilter.mac";
import "ws_orderbygen.mac";

macro TxReplCurrencyRunError(err:Variant, stat:Integer)
  WSRunError("ws_txreplcurrency.mac", err, stat);
end;

class TWsReplCurrencyListItem()
  var FIID:String;
  var InstanceDate:DateTime;
  var Name:String;
  var ISO_Number:String;
end;

class TWsReplCurrencyFltrParm()
  var FindFIID:String;
  var LikeName:String;
  var CurrencyName:String;
end;

private macro GetReplCurrencyAddWhereCondition(
  FilterItems//:TArray of FilterCondItem // Массив элементов фильтра скроллинга
):String
  var fp = null;

  fp = FilterParser(FilterItems);

  return fp.GetFullSQLCondition();
end;

private macro GetReplCurrencySortOrder(
  OrderItems//:TArray of OrderItem // Параметры сортировки
 ,DefaultSQLSortOrder:String       // Строка сортировки по умолчанию
):String
  var ordrGen = null;

  ordrGen = OrderByGenerator(OrderItems, DefaultSQLSortOrder, false, true);

  return ordrGen.GetFullSQLSortOrder();
end;

macro TxGetCurrencyListItemCount(
  FltrParm
 ,FilterItems
):Integer
  var Query = "", AddWhere = "";
  var Count = 0;

  Query = " SELECT "
            + " 1 "
        + " FROM "
            + " dtxcurrency_dbt txcurrency "
        + " WHERE "
            + " 1 = 1 ";

  if(FltrParm != null)
    if((FltrParm.FindFIID != null) and (FltrParm.FindFIID != ""))
      Query = Query + " AND txcurrency.t_FIID = " + FltrParm.FindFIID + " ";
    end;

    if(FltrParm.LikeName != null)
      Query = Query + " AND LOWER(txcurrency.t_Name) LIKE LOWER('%" + FltrParm.LikeName + "%') ";
    end;

    if(FltrParm.CurrencyName != null)
      Query = Query + " AND txcurrency.t_Name = '" + FltrParm.CurrencyName + "' ";
    end;
  end;

  AddWhere = GetReplCurrencyAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  Count = SQL_GetNRecs(Query);

  return Count;

OnError(err)
  TxReplCurrencyRunError(err, -1);
end;

macro TxGetCurrencyListItems(
  PageSize:Integer
 ,PageNum:Integer
 ,FltrParm//:TWsReplCurrencyFltrParm
 ,FilterItems//:TArray of FilterCondItem
 ,OrderItems//:TArray of OrderItem
)
  var Query = "", AddWhere = "", OrderBy = "", Cmd = null, Set = null;
  var ReplCurrencyListItem = null;
  var ReplCurrencyList = TArray();

  Query = " SELECT "
            + " TO_CHAR(t_FIID) AS t_FIID "
           + " ,t_InstanceDate "
           + " ,t_Name "
           + " ,t_ISO_Number "
        + " FROM "
            + " dtxcurrency_dbt txcurrency "
        + " WHERE "
            + " 1 = 1 ";

  if(FltrParm != null)
    if((FltrParm.FindFIID != null) and (FltrParm.FindFIID != ""))
      Query = Query + " AND txcurrency.t_FIID = " + FltrParm.FindFIID + " ";
    end;

    if(FltrParm.LikeName != null)
      Query = Query + " AND LOWER(txcurrency.t_Name) LIKE LOWER('%" + FltrParm.LikeName + "%') ";
    end;

    if(FltrParm.CurrencyName != null)
      Query = Query + " AND txcurrency.t_Name = '" + FltrParm.CurrencyName + "' ";
    end;
  end;

  AddWhere = GetReplCurrencyAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  OrderBy = GetReplCurrencySortOrder(OrderItems, " txcurrency.t_ISO_Number ASC ");
  if(OrderBy != "")
    Query = Query + " ORDER BY " + OrderBy;
  end;

  Query = SQL_WrapSelect(Query, PageNum, PageSize);

  Cmd = RSDCommand(Query);
  Cmd.NullConversion = true;
  Cmd.Execute();

  Set = RsdRecordset(Cmd);

  while(Set.MoveNext())
    ReplCurrencyListItem = TWsReplCurrencyListItem();

    ReplCurrencyListItem.FIID = SQL_ConvTypeStr(Set.Value("t_FIID"));
    ReplCurrencyListItem.InstanceDate = SQL_ConvTypeDate(Set.Value("t_InstanceDate"));
    ReplCurrencyListItem.Name = SQL_ConvTypeStr(Set.Value("t_Name"));
    ReplCurrencyListItem.ISO_Number  = SQL_ConvTypeInteger(Set.Value("t_ISO_Number"));

    ReplCurrencyList[ReplCurrencyList.Size] = ReplCurrencyListItem;
  end;

  return ReplCurrencyList;

OnError(err)
  TxReplCurrencyRunError(err, -1);
end;

///////////////////////////////////////////////////////////
// Возвращает список длинны listLen по
// частичному совпадению значения
///////////////////////////////////////////////////////////
macro LookupReplCurrency(
  FltrParm
 ,ListLen
):TArray
  return TxGetCurrencyListItems(ListLen, 0, FltrParm, null, null);
end;
