/*
$Name:             txproc_avr_code.mac
$Module:           БОЦБ 
$Description:      Макрос репликации кодов ц/б
*/
import txproc_avr_fun, txrepl_func;

private var SQL, cmd, rs, Счетчик, MaxVal;

private macro ПолучитьCodeKind(Субъект)
   private var SQL, cmd, rs;

   SQL = "select t_codekind from dobjalreg_dbt where t_objecttype = 9 and t_partyid = " + Субъект;
   cmd = RsdCommand(SQL);
   rs = RsdRecordSet(cmd);

   if (rs.Movenext)
      Return rs.Value(0);
   else
      Return -1;
   end;
end;

private macro ПолучитьИмяСубъекта(T_PARTYID)
   private var SQL, cmd, rs;

   SQL = "select t_name from dparty_dbt where t_partyid = " + T_PARTYID;
   cmd = RsdCommand(SQL);
   rs = RsdRecordSet(cmd);

   if (rs.Movenext)
      Return rs.Value(0);
   end;
end;

private macro ДобавитьВидКода(T_PARTYID)
   private var SQL, cmd, rs, Number, Имя;

   SQL = "select max(t_codekind) + 1 from dobjkcode_dbt where t_objecttype = 9";
   cmd = RsdCommand(SQL);
   rs = RsdRecordSet(cmd);

   rs.Movenext;
   Number = rs.Value(0);

   if (Number - 100 < 0)
      Number = Number + 100;
   end;

   Имя = ПолучитьИмяСубъекта(T_PARTYID) + " (DIA5NT)";

   SQL = "select count(*) from dobjkcode_dbt where t_objecttype = 9 and T_NAME = 'Код на " + Имя + "'";
   cmd = RsdCommand(SQL);
   rs = RsdRecordSet(cmd);
   rs.Movenext;
   if (rs.value(0) < 1)
      SQL = "insert into dobjkcode_dbt values (9, " + Number + ", 'Код на " + Имя + "', '";
      SQL = SQL + Substr(Имя, 1, 4) + "', 35, chr(0), 'Код на " + Имя + "',chr(1),chr(1),chr(0))";

      cmd = RsdCommand(SQL);
      cmd.execute;
   end;

   SQL = "select count(*) from dobjalcod_dbt where t_objecttype = 9 and t_objectkind = 0 and t_objsubkind = 0 and t_codekind = " + Number;
   cmd = RsdCommand(SQL);
   rs = RsdRecordSet(cmd);
   rs.Movenext;
   if (rs.value(0) < 1)
      SQL = "insert into dobjalcod_dbt values (9, 0, 0, " + Number + ")";
      cmd = RsdCommand(SQL);
      cmd.execute;
   end;

   SQL = "select count(*) from dobjkdoc_dbt where t_objecttype = 9 and T_REGDOCKIND = " + Number;
   cmd = RsdCommand(SQL);
   rs = RsdRecordSet(cmd);
   rs.Movenext;
   if (rs.value(0) < 1)
      SQL = "insert into dobjkdoc_dbt values (9, " + Number + ", 'Код на " + Имя + "', '" + Substr(Имя, 1, 10) + "', chr(0))";
      cmd = RsdCommand(SQL);
      cmd.execute;
   end;

   SQL = "select count(*) from dobjkrgpt_dbt where T_REGPARTYKIND = " + Number;
   cmd = RsdCommand(SQL);
   rs = RsdRecordSet(cmd);
   rs.Movenext;
   if (rs.value(0) < 1)
      SQL = "insert into dobjkrgpt_dbt values (" + Number + ", 'Код на " + Имя + "', '" + Substr(Имя, 1, 10) + "')";
      cmd = RsdCommand(SQL);
      cmd.execute;
   end;

   SQL = "select count(*) from dobjalpt_dbt where T_REGPARTYKIND = " + Number + " and T_PARTYKIND = 3";
   cmd = RsdCommand(SQL);
   rs = RsdRecordSet(cmd);
   rs.Movenext;
   if (rs.value(0) < 1)
      SQL = "insert into dobjalpt_dbt values (" + Number + ", 3)";
      cmd = RsdCommand(SQL);                                      
      cmd.execute;
   end;

   SQL = "insert into dobjalreg_dbt values (0, 9, " + Number + ", " + Number + ", " + Number + ", " + T_PARTYID + ", chr(0), chr(0), chr(1))";
   cmd = RsdCommand(SQL);
   cmd.execute;

   Return TRUE;
OnError
   AbortTrn;
end;

private macro ПроверитьВидКода(T_PARTYID)
   private var SQL, cmd, rs;

   SQL = "select count(*) from dobjalreg_dbt where t_objecttype = 9 and t_partyid =" + T_PARTYID;
   cmd = RsdCommand(SQL);
   rs = RsdRecordSet(cmd);
   rs.Movenext;

   if (rs.Value(0) > 0)
      Return TRUE;
   else
      Return ДобавитьВидКода(T_PARTYID);
   end;
end;

private macro ВидКода()
   private var SQL, cmd, rs, T_PARTYID;

   SQL = "select distinct t_marketid from DTXAVRCODE_DBT where t_replstate = 0";
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);

   while (rs.Movenext)
      T_PARTYID = ПолучитьСоответствие(30, rs.Value(0));

      if (T_PARTYID == -1)
         Continue;
      end;

      if (not ПроверитьВидКода(T_PARTYID))
         Return False;
      end;
   end;

   Return TRUE;
end;

macro Process_Avr_Code()
   var SQL1, cmd1, rs1, CodeKind, Субъект, count, FIID, innertime, НаименованиеБумаги;

   Субъект = ПолучитьСоответствие(30, rs.Value("T_MARKETID"));
   НаименованиеБумаги = ПолучитьНаименованиеБумаги(rs.Value("T_AVOIRISSID"));

   if (Субъект == -1)
      ЗанестиЗаписьВПротокол(512, 22, rs.Value("T_AVOIRISSID"), 1, StrFor(1), "Ошибка: невозможно ничего сделать с кодом - не найден субъект, бумага " + НаименованиеБумаги, rs.Value("T_INSTANCEDATE"));
      AbortTrn;
   end;

   CodeKind = ПолучитьCodeKind(Субъект);

   if (CodeKind == -1)
      ЗанестиЗаписьВПротокол(513, 22, rs.Value("T_AVOIRISSID"), 1, StrFor(1), "Ошибка: невозможно ничего сделать с кодом такого вида - не найден вид кода, бумага " + НаименованиеБумаги, rs.Value("T_INSTANCEDATE"));
      AbortTrn;
   end;

   FIID = ПолучитьСоответствие(20, rs.Value("T_AVOIRISSID"));

   if (FIID == -1)
      ЗанестиЗаписьВПротокол(514, 22, rs.Value("T_AVOIRISSID"), 1, StrFor(1), "Ошибка: невозможно ничего сделать с кодом - не найдена ЦБ, бумага " + НаименованиеБумаги, rs.Value("T_INSTANCEDATE"));
      AbortTrn;
   end;

   SQL1 = "select count(*) from dobjcode_dbt where t_objecttype = 9 and t_codekind = " + CodeKind + " and t_objectid = " + FIID;
   cmd1 = RsdCommand(SQL1);
   rs1 = RsdRecordSet(cmd1);
   
   if (rs1.Movenext)
      count = rs1.Value(0);
   end;

   if (rs.Value("T_ACTION") == 1)
      if (count != 0 )
         ЗанестиЗаписьВПротокол(412, 22, rs.Value("T_AVOIRISSID"), 1, StrFor(1), "Ошибка: невозможно вставить существующий код такого вида, бумага " + НаименованиеБумаги, rs.Value("T_INSTANCEDATE"));
         AbortTrn;
      end;

      SQL1 = "INSERT INTO dobjcode_dbt(T_OBJECTTYPE,T_CODEKIND,T_OBJECTID,T_CODE,T_STATE,T_BANKDATE,T_SYSDATE,T_SYSTIME,T_USERID,T_BRANCH,T_BANKCLOSEDATE,T_NUMSESSION)  ";
      SQL1 = SQL1 + " VALUES (9, ";                                                     /*T_OBJECTTYPE  NUMBER (5)*/
      SQL1 = SQL1 + CodeKind + ", ";                                                    /*T_CODEKIND    NUMBER (5)*/
      SQL1 = SQL1 + FIID + ", '";                                                       /*T_OBJECTID    NUMBER (10)*/
      SQL1 = SQL1 + Trim(rs.Value("T_MARKETCODE")) + "', ";                             /*T_CODE        VARCHAR2 (35)*/
      SQL1 = SQL1 + "0, ";                                                              /*T_STATE       NUMBER (5)*/
      SQL1 = SQL1 + getsqlDate_SK({CurDate})+", ";                                      /*T_BANKDATE    DATE*/
      SQL1 = SQL1 + getsqlDate_SK(DateStamp(ПолучитьTimeStamp()/*DtTm(Date, time)*/, innertime))+", ";          /*T_SYSDATE     DATE*/
      SQL1 = SQL1 + GetSQLTime(innertime)+", ";                                         /*T_SYSTIME     DATE*/
      SQL1 = SQL1 + {Oper}+", ";                                                        /*T_USERID      NUMBER (5)*/
      SQL1 = SQL1 + "0, ";                                                              /*T_BRANCH      NUMBER (5)*/
      SQL1 = SQL1 + "to_date('01.01.0001','dd.mm.yyyy'), ";                             /*T_BANKCLOSEDATE   DATE*/
      SQL1 = SQL1 + "0";                                                                /*T_NUMSESSION  NUMBER (10)*/
      SQL1 = SQL1 + ")";
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      SQL1 = "update DTXAVRCODE_DBT set t_replstate = 1 where T_AVOIRISSID = " + rs.value("T_AVOIRISSID") + " and t_action = 1 and t_replstate = 0";
      SQL1 = SQL1 + " and t_marketid = " + rs.value("t_marketid") + " and trunc(t_instancedate) = " + getsqlDate_SK(Date(rs.Value("T_INSTANCEDATE")));
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;
   elif (rs.Value("T_ACTION") == 2)
      if (count == 0 )
         ЗанестиЗаписьВПротокол(413, 22, rs.Value("T_AVOIRISSID"), 1, StrFor(1), "Ошибка: невозможно обновить несуществующий код такого вида, бумага " + НаименованиеБумаги, rs.Value("T_INSTANCEDATE"));
         AbortTrn;
      end;

      SQL1 = "update dobjcode_dbt set t_CODE = '" + Trim(rs.Value("T_MARKETCODE"))  + "' where T_OBJECTTYPE = 9 and T_CODEKIND = ";
      SQL1 = SQL1 + CodeKind + " and T_OBJECTID = " + FIID;
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      SQL1 = "update DTXAVRCODE_DBT set t_replstate = 1 where T_AVOIRISSID = " + rs.value("T_AVOIRISSID") + " and t_action = 2 and t_replstate = 0";
      SQL1 = SQL1 + " and t_marketid = " + rs.value("t_marketid") + " and trunc(t_instancedate) = " + getsqlDate_SK(Date(rs.Value("T_INSTANCEDATE")));
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;
   elif (rs.Value("T_ACTION") == 3)
      if (count == 0 )
         ЗанестиЗаписьВПротокол(414, 22, rs.Value("T_AVOIRISSID"), 1, StrFor(1), "Ошибка: невозможно удалить несуществующий код такого вида, бумага " + НаименованиеБумаги, rs.Value("T_INSTANCEDATE"));
         AbortTrn;
      end;

      SQL1 = "delete from dobjcode_dbt where T_OBJECTTYPE = 9 and T_CODEKIND = " + CodeKind + " and T_OBJECTID = " + FIID;
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      SQL1 = "update DTXAVRCODE_DBT set t_replstate = 1 where T_AVOIRISSID = " + rs.value("T_AVOIRISSID") + " and t_action = 3 and t_replstate = 0";
      SQL1 = SQL1 + " and t_marketid = " + rs.value("t_marketid") + " and trunc(t_instancedate) = " + getsqlDate_SK(Date(rs.Value("T_INSTANCEDATE")));
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;
   end;

OnError(er)
   if (trim(er.message) != "Error:")
      ЗанестиЗаписьВПротокол(600, 21, rs.value("T_AVOIRISSID"), 1, StrFor(1), "Ошибка: " + er.message, rs.Value("T_INSTANCEDATE"));
   end;
   AbortTrn;
end;

macro Обработать_КодЦБ(Action, t_instancedate, Init_Action:String)
   if (not ВидКода())
      Return FALSE;
   end;

   SQL = "select count(*) from DTXAVRCODE_DBT where t_replstate = 0 and t_action = " + Action + " and trunc(t_instancedate) = " + t_instancedate;
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);

   if (rs.Movenext)
      Счетчик = rs.Value(0);
      MaxVal = rs.Value(0);
   end;

   var ProgressIndicator = CreateProgressIndicator(true);
   ProgressIndicator.Start(Int(MaxVal), "Обработка кодов ценных бумаг...");

   Счетчик = 0;

   SQL = "select * from DTXAVRCODE_DBT where t_replstate = 0 and t_action = " + Action + " and trunc(t_instancedate) = " + t_instancedate + " order by t_avoirissid, t_marketid";
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);
  
   while (rs.Movenext)
      Счетчик = Счетчик + 1;

      ProgressIndicator.Update(Int(Счетчик), MaxVal); // Update(index, count, text)
   
      if (not ProcessTrn(Null, "Process_Avr_Code"))
      end;
   end;

   ProgressIndicator.Stop();

   return TRUE;
OnError(er)
   ЗанестиЗаписьВПротокол(600, 21, "", "", StrFor(1), "Ошибка: " + er.message, date(t_instancedate));
end;