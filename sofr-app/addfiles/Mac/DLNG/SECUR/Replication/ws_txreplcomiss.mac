/*
$Name:             ws_txreplcomiss.mac
$Module:           БОЦБ
$Description:      Макрос сервисов скроллинга комиссий для репликации
*/

import FIInter;
import "ws_common.mac";
import "ws_orderbygen.mac";
import "ws_datafilter.mac";

macro TxReplComissRunError(err:Variant, stat:Integer)
  WSRunError("ws_txreplcomiss.mac", err, stat);
end;

class CTxReplComissList()
  var ComissID:String;       // Идентификатор
  var InstanceDate:DateTime; // Дата загрузки
  var Action:Integer;        // Действие
  var ActionName:String;     // Наименование действия
  var ReplState:Integer;     // Состояние репликации
  var ReplStateName:String;  // Наименование состояния репликации
  var DealID:String;         // Идентификатор сделки
  var Type:Integer;          // Тип комиссии
  var TypeName:String;       // Наименование типа комиссии
  var Sum:Money;             // Сумма
  var NDS:Money;             // НДС
  var CurrencyID:String;     // Идентификатор валюты оплаты
  var CurrencyName:String;   // Валюта оплаты
  var Date:Date;             // Дата
  var Version:Integer;       // Версия записи
end;

private macro GetSQLComissTypeCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLNameAlgConditionEx("txcomiss", "t_Type", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcomiss.t_Type = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLCurrencyCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLFIConditionEx("txcomiss", "t_CurrencyID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcomiss.t_CurrencyID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLDealIDCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLTxReplDealConditionEx("txcomiss", "t_DealID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcomiss.t_DealID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetReplComissAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String
  var fp = null;

  fp = FilterParser(FilterItems);

  fp.AddFieldCondition(0, "txcomiss", "t_InstanceDate");      // Записи за период
  fp.AddFieldCondition(1, "txcomiss", "t_ReplState");         // Статус записи
  fp.AddFieldCondition(2, "txcomiss", "t_Action");            // Вид действия
  fp.AddFieldCondition(3, "txcomiss", "t_ComissID", " = 0 "); // Идентификатор записи
  fp.AddUserFieldCondition(4, @GetSQLDealIDCondition);        // Идентификатор операции
  fp.AddUserFieldCondition(5, @GetSQLComissTypeCondition);    // Тип комиссии
  fp.AddUserFieldCondition(6, @GetSQLCurrencyCondition);      // Валюта комиссии
  fp.AddFieldCondition(7, "txcomiss", "t_Date");              // Дата

  return fp.GetFullSQLCondition();
end;

private macro GetReplComissSortOrder(
  OrderItems//:TArray of OrderItem // Параметры сортировки
 ,DefaultSQLSortOrder:String       // Строка сортировки по умолчанию
):String
  var ordrGen = null;

  ordrGen = OrderByGenerator(OrderItems, DefaultSQLSortOrder, false, true);

  return ordrGen.GetFullSQLSortOrder();
end;

// Получить общее количество записей скроллинга ТО для репликации
macro TxGetReplComissCount(
  FilterItems//:TArray of FilterCondItem // Массив элементов фильтра скроллинга
):Integer
  var Query = "", AddWhere = "";
  var Count = 0;

  Query = " SELECT "
            + " 1 "
        + " FROM "
            + " dtxcomiss_dbt txcomiss "
        + " WHERE "
            + " 1 = 1 ";

  AddWhere = GetReplComissAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  Count = SQL_GetNRecs(Query);

  return Count;

OnError(err)
  TxReplComissRunError(err, -1);
end;

// Отбор данных для скроллинга ТО
macro TxGetReplComissList(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
)
  var Query = "", AddWhere = "", OrderBy = "", Cmd = null, Set = null;
  var ReplComissList = TArray();
  var ReplComiss = null;

  if(PageNum == null)
    RunError("Не задан обязательный параметр функции: номер страницы");
  end;

  if(PageSize == null)
    RunError("Не задан обязательный параметр функции: размер страницы");
  end;

  Query = " SELECT "
            + " TO_CHAR(txcomiss.t_ComissID) AS t_ComissID "
           + " ,txcomiss.t_InstanceDate "
           + " ,txcomiss.t_Action "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7643 "
                 + " AND namealg.t_iNumberAlg = txcomiss.t_Action), CHR(0)) AS t_ActionName "
           + " ,txcomiss.t_ReplState "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7642 "
                 + " AND namealg.t_iNumberAlg = txcomiss.t_ReplState), CHR(0)) AS t_ReplStateName "
           + " ,TO_CHAR(txcomiss.t_DealID) AS t_DealID "
           + " ,NVL(txcomiss.t_Type, -1) t_Type "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7649 "
                 + " AND namealg.t_iNumberAlg = txcomiss.t_Type), CHR(0)) AS t_TypeName "
           + " ,txcomiss.t_Sum "
           + " ,txcomiss.t_NDS "
           + " ,TO_CHAR(txcomiss.t_CurrencyID) AS t_CurrencyID "
           + " ,NVL((SELECT "
                     + " MAX(txcur.t_name) "
                 + " FROM "
                     + " dtxcurrency_dbt txcur "
                 + " WHERE "
                     + " txcur.t_FIID = txcomiss.t_CurrencyID), CHR(0)) AS t_CurrencyName "
           + " ,txcomiss.t_Date "
           + " ,txcomiss.t_Version "
        + " FROM "
            + " dtxcomiss_dbt txcomiss "
        + " WHERE "
            + " 1 = 1 ";

  AddWhere = GetReplComissAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  OrderBy = GetReplComissSortOrder(OrderItems, " txcomiss.t_ComissID ASC, txcomiss.t_InstanceDate ASC, txcomiss.t_Action ASC, txcomiss.t_ReplState ASC ");
  if(OrderBy != "")
    Query = Query + " ORDER BY " + OrderBy;
  end;

  Query = SQL_WrapSelect(Query, PageNum, PageSize);

  Cmd = RSDCommand(Query);
  Cmd.Execute();

  Set = RsdRecordset(Cmd);
  while(Set.MoveNext())
    ReplComiss = CTxReplComissList();

    ReplComiss.ComissID = SQL_ConvTypeStr(Set.Value("t_ComissID"));
    ReplComiss.InstanceDate = SQL_ConvTypeDateTime(Set.Value("t_InstanceDate"));
    ReplComiss.Action = SQL_ConvTypeInteger(Set.Value("t_Action"));
    ReplComiss.ActionName = SQL_ConvTypeStr(Set.Value("t_ActionName"));
    ReplComiss.ReplState = SQL_ConvTypeInteger(Set.Value("t_ReplState"));
    ReplComiss.ReplStateName = SQL_ConvTypeStr(Set.Value("t_ReplStateName"));
    ReplComiss.DealID = SQL_ConvTypeStr(Set.Value("t_DealID"));
    ReplComiss.Type = SQL_ConvTypeInteger(Set.Value("t_Type"));
    ReplComiss.TypeName = SQL_ConvTypeStr(Set.Value("t_TypeName"));
    ReplComiss.Sum = SQL_ConvTypeSum(Set.Value("t_Sum"));
    ReplComiss.NDS = SQL_ConvTypeSum(Set.Value("t_NDS"));
    ReplComiss.CurrencyID = SQL_ConvTypeStr(Set.Value("t_CurrencyID"));
    ReplComiss.CurrencyName = SQL_ConvTypeStr(Set.Value("t_CurrencyName"));
    ReplComiss.Date = SQL_ConvTypeDate(Set.Value("t_Date"));
    ReplComiss.Version = SQL_ConvTypeInteger(Set.Value("t_Version"));

    ReplComissList[ReplComissList.Size] = ReplComiss;
  end;

  return ReplComissList;

OnError(err)
  TxReplComissRunError(err, -1);
end;

private macro CheckVersion(
  ComissID:String
 ,InstanceDate:DateTime
 ,Version:Integer
)
  var err = 0;
  var Query = "", Cmd = null, Set = null;

  Query = " SELECT "
            + " * "
        + " FROM "
            + " dtxcomiss_dbt "
        + " WHERE "
            + " t_ComissID = ? "
        + " AND t_InstanceDate = ? "
        + " AND t_Version = ? "
        + " FOR UPDATE ";

  Cmd = RsdCommand(Query);
  Cmd.AddParam("", RSDBP_IN, ComissID);
  Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(InstanceDate));
  Cmd.AddParam("", RSDBP_IN, Version);
  Cmd.Execute();

  Set = RsdRecordset(Cmd);
  if(not Set.MoveNext())
    err = 70;
  end;

  return err;
end;

private macro UpdateTxComissImpl(
  StateAndAction//:CTxReplStateAndAction
 ,Comiss//:CTxReplComissList
)
  var err = 0;
  var Query = "", Cmd = null;

  err = CheckVersion(Comiss.ComissID, Comiss.InstanceDate, Comiss.Version);

  if(err == 0)
    Query = " UPDATE "
              + " dtxcomiss_dbt "
          + " SET ";
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Query = Query +
                " t_ReplState = ? "
             + " ,t_Action = ? ";
    elif(StateAndAction.NeedUpdateReplState)
      Query = Query +
                " t_ReplState = ? ";
    else
      Query = Query +
                " t_Action = ? ";
    end;
    Query = Query +
            " WHERE "
              + " t_ComissID = ? "
          + " AND t_InstanceDate = ? ";

    Cmd = RsdCommand(Query);
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    elif(StateAndAction.NeedUpdateReplState)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
    else
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    end;
    Cmd.AddParam("", RSDBP_IN, Comiss.ComissID);
    Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(Comiss.InstanceDate));
    Cmd.Execute();
  end;

  return err;
end;

macro TxUpdateComissList(
  StateAndAction//:CTxReplStateAndAction
 ,ComissList//:TArray of CTxReplComissList
)
  var err = 0;
  var i = 0;

  if((StateAndAction.NeedUpdateReplState
   or StateAndAction.NeedUpdateAction)
 and (ComissList != null))
    while((err == 0) and (i < ComissList.Size))
      err = UpdateTxComissImpl(StateAndAction, ComissList[i]);
      i = i + 1;
    end;
  end;

  return err;

OnError(err)
  TxReplComissRunError(err, -1);
end;
