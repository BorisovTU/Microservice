/*
$Name:             ws_txreplcourse.mac
$Module:           АРНУ
$Description:      WEB. АРНУ. Макрос сервисов скроллинга курсов ц/б для репликации
*/

import FIInter;
import "ws_common.mac";
import "ws_orderbygen.mac";
import "ws_datafilter.mac";

macro TxReplCourseRunError(err:Variant, stat:Integer)
  WSRunError("ws_txreplcourse.mac", err, stat);
end;

// Класс скроллинга курсов ц/б для репликации
class CTxReplCourseList()
  var CourseID:String;         // Идентификатор
  var InstanceDate:DateTime;   // Дата загрузки
  var Action:Integer;          // Действие
  var ActionName:String;       // Наименование действия
  var ReplState:Integer;       // Статус репликации
  var ReplStateName:String;    // Статус репликации
  var Type:Integer;            // Тип курса
  var TypeName:String;         // Тип курса
  var BaseFIKind:Integer;      // Вид ФИ
  var BaseFIKindName:String;   // Вид ФИ
  var BaseFIID:String;         // ФИ, в единицах которого задан курс
  var BaseFIIDName:String;     // ФИ, в единицах которого задан курс
  var FIID:String;             // ФИ, для  которого задан курс
  var FIIDName:String;         // ФИ, для  которого задан курс
  var MarketID:String;         // Торговая площадка
  var MarketName:String;       // Торговая площадка
  var MarketSectorID:String;   // Сектор торговой площадки
  var MarketSectorName:String; // Сектор торговой площадки
  var Point:Integer;           // Точность округления
  var Scale:Integer;           // Масштаб курса
  var RateDate:Date;           // Дата курса
  var Rate:Money;              // Значение курса
  var Version:Integer;         // Версия записи
end;

private macro GetSQLMarketIDCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLPartyConditionEx("txcourse", "t_MarketID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcourse.t_MarketID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLMarketSectorCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLTxMarketSectorConditionEx("txcourse", "t_MarketSectorID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcourse.t_MarketSectorID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLAvoirissCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLTxAvoirssConditionEx("txcourse", "t_BaseFIID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcourse.t_BaseFIID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLCurrencyCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLFIConditionEx("txcourse", "t_BaseFIID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcourse.t_BaseFIID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLFIIDCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLFIConditionEx("txcourse", "t_FIID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcourse.t_FIID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLCourseKindCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLNameAlgConditionEx("txcourse", "t_Type", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcourse.t_Type = 0 ";
  end;

  return strSQLCond;
end;

private macro GetReplCourseAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String
  var fp = null;

  fp = FilterParser(FilterItems);

  fp.AddFieldCondition(0, "txcourse", "t_InstanceDate");      // Записи за период
  fp.AddFieldCondition(1, "txcourse", "t_ReplState");         // Статус записи
  fp.AddFieldCondition(2, "txcourse", "t_Action");            // Вид действия
  fp.AddFieldCondition(3, "txcourse", "t_CourseID", " = -1"); // Идентификатор записи
  fp.AddUserFieldCondition(4, @GetSQLCourseKindCondition);    // Тип курса
  fp.AddFieldCondition(5, "txcourse", "t_BaseFIKind");        // Вид ФИ
  fp.AddFieldCondition(6, "txcourse", "t_RateDate");          // Дата курса
  fp.AddUserFieldCondition(7, @GetSQLMarketIDCondition);      // Торговая площадка
  fp.AddUserFieldCondition(8, @GetSQLMarketSectorCondition);  // Сектор торговой площадки
  fp.AddUserFieldCondition(9, @GetSQLAvoirissCondition);      // базовый инструмент - ценная бумага
  fp.AddUserFieldCondition(10, @GetSQLCurrencyCondition);     // базовый инструмент - валюта
  fp.AddUserFieldCondition(11, @GetSQLFIIDCondition);         // котируемый инструмент - валюта

  return fp.GetFullSQLCondition();
end;

private macro GetReplCourseSortOrder(
  OrderItems//:TArray of OrderItem // Параметры сортировки
 ,DefaultSQLSortOrder:String       // Строка сортировки по умолчанию
):String
  var ordrGen = null;

  ordrGen = OrderByGenerator(OrderItems, DefaultSQLSortOrder, false, true);

  return ordrGen.GetFullSQLSortOrder();
end;

// Получить общее количество записей скроллинга Ц/Б
macro TxGetReplCourseCount(
  FilterItems//:TArray of FilterCondItem // Массив элементов фильтра скроллинга
):Integer
  var Query = "", AddWhere = "";
  var Count = 0;

  Query = " SELECT "
            + " 1 "
        + " FROM "
            + " dtxcourse_dbt txcourse "
        + " WHERE "
            + " 1 = 1 ";

  AddWhere = GetReplCourseAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  Count = SQL_GetNRecs(Query);

  return Count;

OnError(err)
  TxReplCourseRunError(err, -1);
end;

// Отбор данных для скроллинга Ц/Б
macro TxGetReplCourseList(
  PageSize:Integer                             // Размер страницы
 ,PageNum:Integer                              // Номер страницы
 ,FilterItems//: TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//: TArray of OrderItem            // Параметры сортировки
):TArray
  var Query = "", AddWhere = "", OrderBy = "", Cmd = null, Set = null;
  var ReplCourse = null;
  var ReplCourseList = TArray();

  if(PageNum == null)
    RunError("Не задан обязательный параметр функции: номер страницы");
  end;

  if(PageSize == null)
    RunError("Не задан обязательный параметр функции: размер страницы");
  end;

  Query = " SELECT "
            + " TO_CHAR(txcourse.t_CourseID) AS t_CourseID "
           + " ,txcourse.t_InstanceDate "
           + " ,txcourse.t_Action "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_itypealg = 7643 "
                 + " AND namealg.t_iNumberAlg = txcourse.t_Action), CHR(0)) AS t_ActionName "
           + " ,txcourse.t_ReplState "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_itypealg = 7642 "
                 + " AND namealg.t_iNumberAlg = txcourse.t_ReplState), CHR(0)) AS t_ReplStateName "
           + " ,txcourse.t_Type "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_itypealg = 7654 "
                 + " AND namealg.t_iNumberAlg = txcourse.t_Type), CHR(0)) AS t_TypeName "
           + " ,txcourse.t_BaseFIKind "
           + " ,DECODE(txcourse.t_BaseFIKind, 10, 'Валюта' "
                                         + " ,20, 'Ценная бумага' "
                                         + " ,CHR(0)) AS t_BaseFIKindName "
           + " ,TO_CHAR(txcourse.t_BaseFIID) AS t_BaseFIID "
           + " ,DECODE(txcourse.t_BaseFIKind, 10, NVL((SELECT "
                                                       + " MAX(txcur.t_Name) "
                                                   + " FROM "
                                                       + " dtxcurrency_dbt txcur "
                                                   + " WHERE "
                                                       + " txcur.t_FIID = txcourse.t_BaseFIID), CHR(0)) "
                                         + " ,20, NVL((SELECT "
                                                       + " MAX(txavoiriss.t_ShortName) "
                                                   + " FROM "
                                                       + " dtxavoiriss_dbt txavoiriss "
                                                   + " WHERE "
                                                       + " txavoiriss.t_AvoirissID = txcourse.t_BaseFIID), CHR(0)) "
                                         + " ,CHR(0)) AS t_BaseFIIDName "
           + " ,TO_CHAR(txcourse.t_FIID) AS t_FIID "
           + " ,NVL((SELECT "
                     + " MAX(txcur.t_Name) "
                 + " FROM "
                     + " dtxcurrency_dbt txcur "
                 + " WHERE "
                     + " txcur.t_FIID = txcourse.t_FIID), CHR(0)) AS t_FIIDName "
           + " ,TO_CHAR(txcourse.t_MarketID) AS t_MarketID "
           + " ,NVL((SELECT "
                     + " MAX(txparty.t_ShortName) "
                 + " FROM "
                     + " dtxparty_dbt txparty "
                 + " WHERE "
                     + " txparty.t_PartyID = txcourse.t_MarketID), CHR(0)) AS t_MarketName "
           + " ,TO_CHAR(txcourse.t_MarketSectorID) AS t_MarketSectorID "
           + " ,NVL((SELECT "
                     + " MAX(txmarksect.t_Name) "
                 + " FROM "
                     + " dtxmarksect_dbt txmarksect "
                 + " WHERE "
                     + " txmarksect.t_MarketSectorID = txcourse.t_MarketSectorID), CHR(0)) AS t_MarketSectorName "
           + " ,txcourse.t_Point "
           + " ,txcourse.t_Scale "
           + " ,txcourse.t_RateDate "
           + " ,txcourse.t_Rate "
           + " ,txcourse.t_Version "
        + " FROM "
            + " dtxcourse_dbt txcourse "
        + " WHERE "
            + " 1 = 1 ";

  AddWhere = GetReplCourseAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  OrderBy = GetReplCourseSortOrder(OrderItems, " txcourse.t_CourseID ASC, txcourse.t_InstanceDate ASC, txcourse.t_Action ASC, txcourse.t_ReplState ASC ");
  if(OrderBy != "")
    Query = Query + " ORDER BY " + OrderBy;
  end;

  Query = SQL_WrapSelect(Query, PageNum, PageSize);

  Cmd = RSDCommand(Query);
  Cmd.NullConversion = true;
  Cmd.Execute();

  Set = RsdRecordset(Cmd);

  while(Set.MoveNext())
    ReplCourse = CTxReplCourseList();

    ReplCourse.CourseID = SQL_ConvTypeStr(Set.Value("t_CourseID"));
    ReplCourse.InstanceDate = SQL_ConvTypeDateTime(Set.Value("t_InstanceDate"));
    ReplCourse.Action = SQL_ConvTypeInteger(Set.Value("t_Action"));
    ReplCourse.ActionName = SQL_ConvTypeStr(Set.Value("t_ActionName"));
    ReplCourse.ReplState = SQL_ConvTypeInteger(Set.Value("t_ReplState"));
    ReplCourse.ReplStateName = SQL_ConvTypeStr(Set.Value("t_ReplStateName"));
    ReplCourse.Type = SQL_ConvTypeInteger(Set.Value("t_Type"));
    ReplCourse.TypeName = SQL_ConvTypeStr(Set.Value("t_TypeName"));
    ReplCourse.BaseFIKind = SQL_ConvTypeInteger(Set.Value("t_BaseFIKind"));
    ReplCourse.BaseFIKindName = SQL_ConvTypeStr(Set.Value("t_BaseFIKindName"));
    ReplCourse.BaseFIID = SQL_ConvTypeStr(Set.Value("t_BaseFIID"));
    ReplCourse.BaseFIIDName = SQL_ConvTypeStr(Set.Value("t_BaseFIIDName"));
    ReplCourse.FIID = SQL_ConvTypeStr(Set.Value("t_FIID"));
    ReplCourse.FIIDName = SQL_ConvTypeStr(Set.Value("t_FIIDName"));
    ReplCourse.MarketID = SQL_ConvTypeStr(Set.Value("t_MarketID"));
    ReplCourse.MarketName = SQL_ConvTypeStr(Set.Value("t_MarketName"));
    ReplCourse.MarketSectorID = SQL_ConvTypeStr(Set.Value("t_MarketSectorID"));
    ReplCourse.MarketSectorName = SQL_ConvTypeStr(Set.Value("t_MarketSectorName"));
    ReplCourse.Point = SQL_ConvTypeInteger(Set.Value("t_Point"));
    ReplCourse.Scale = SQL_ConvTypeInteger(Set.Value("t_Scale"));
    ReplCourse.RateDate = SQL_ConvTypeInteger(Set.Value("t_RateDate"));
    ReplCourse.Rate = SQL_ConvTypeDate(Set.Value("t_Rate"));
    ReplCourse.Version = SQL_ConvTypeInteger(Set.Value("t_Version"));

    ReplCourseList[ReplCourseList.Size] = ReplCourse;
  end;

  return ReplCourseList;

OnError(err)
  TxReplCourseRunError(err, -1);
end;

private macro CheckVersion(
  CourseID:String
 ,Type:Integer
 ,InstanceDate:DateTime
 ,Version:Integer
)
  var err = 0;
  var Query = "", Cmd = null, Set = null;

  Query = " SELECT "
            + " * "
        + " FROM "
            + " dtxcourse_dbt "
        + " WHERE "
            + " t_CourseID = ? "
        + " AND t_Type = ? "
        + " AND t_InstanceDate = ? "
        + " AND t_Version = ? "
        + " FOR UPDATE ";

  Cmd = RsdCommand(Query);
  Cmd.AddParam("", RSDBP_IN, CourseID);
  Cmd.AddParam("", RSDBP_IN, Type);
  Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(InstanceDate));
  Cmd.AddParam("", RSDBP_IN, Version);
  Cmd.Execute();

  Set = RsdRecordset(Cmd);
  if(not Set.MoveNext())
    err = 70;
  end;

  return err;
end;

private macro UpdateTxCourseImpl(
  StateAndAction//:CTxReplStateAndAction
 ,Course//:CTxReplCourseList
)
  var err = 0;
  var Query = "", Cmd = null;

  err = CheckVersion(Course.CourseID, Course.Type, Course.InstanceDate, Course.Version);

  if(err == 0)
    Query = " UPDATE "
              + " dtxcourse_dbt "
          + " SET ";
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Query = Query +
                " t_ReplState = ? "
             + " ,t_Action = ? ";
    elif(StateAndAction.NeedUpdateReplState)
      Query = Query +
                " t_ReplState = ? ";
    else
      Query = Query +
                " t_Action = ? ";
    end;
    Query = Query +
            " WHERE "
              + " t_CourseID = ? "
          + " AND t_Type = ? "
          + " AND t_InstanceDate = ? ";

    Cmd = RsdCommand(Query);
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    elif(StateAndAction.NeedUpdateReplState)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
    else
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    end;
    Cmd.AddParam("", RSDBP_IN, Course.CourseID);
    Cmd.AddParam("", RSDBP_IN, Course.Type);
    Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(Course.InstanceDate));
    Cmd.Execute();
  end;

  return err;
end;

macro TxUpdateCourseList(
  StateAndAction//:CTxReplStateAndAction
 ,CourseList//:TArray of CTxReplCourseList
)
  var err = 0;
  var i = 0;

  if((StateAndAction.NeedUpdateReplState
   or StateAndAction.NeedUpdateAction)
 and (CourseList != null))
    while((err == 0) and (i < CourseList.Size))
      err = UpdateTxCourseImpl(StateAndAction, CourseList[i]);
      i = i + 1;
    end;
  end;

  return err;

OnError(err)
  TxReplCourseRunError(err, -1);
end;
