/*
$Name:             txproc_rate.mac
$Module:           БОЦБ 
$Description:      Макрос репликации курсов
*/
import txrepl_func, FIInter;

private var MaxVal;
private const NominalOnDate = 100; // репликация номиналов ц/б на дату (реплицируются из таблицы курсов)
private const NKDonDate = 15; // НКД на дату


macro RTYPE(tp)
   if   (tp == 26) return 23;
   elif (tp == 10) return 23;
   elif (tp == 27) return 1001;
   elif (tp == 28) return 1002;
   elif (tp == 29) return 1003;
   else return tp;
   end;
end;

macro ProcessRate(rs);
   var SQL1, SQL3, Rate_date, cmd1, rs1, SECT_NUM, count, BASEFIID, FIID, MARKETID, innertime, RateID, count_date, SQL2, cmd2, rs2, SECTION = -1, count_repl, NameFIID;
   var T_ISDOMINANT = "chr(0)";
   var T_ISRELATIVE = "chr(0)";
   var V_BASEFIKIND, V_BASEFIID, V_FIID, V_MARKETID, V_RATEDATE, V_RATE, V_MARKETSECTORID;

   if( rs.Value("T_TYPE") != NominalOnDate )
      if (rs.Value("T_ACTION") == 3)
         SQL1 = "select * from DTXCOURSE_DBT where t_courseid = " + rs.Value("T_COURSEID") + " and t_type = " + rs.Value("T_TYPE") + " and t_action in (1,2) and t_replstate = 1 order by t_instancedate desc"; 
         
      cmd1 = RsdCommand( SQL1);
         rs1 = RsdRecordSet(cmd1);
         if (rs1.Movenext())                    
            V_BASEFIKIND = rs1.Value("T_BASEFIKIND");
            V_BASEFIID = rs1.Value("T_BASEFIID");
            V_FIID = rs1.Value("T_FIID");
            V_MARKETID = rs1.Value("T_MARKETID");
            V_RATEDATE = rs1.Value("T_RATEDATE");
            V_RATE = rs1.Value("T_RATE");
            V_MARKETSECTORID = rs1.Value("T_MARKETSECTORID");
         else
            ЗанестиЗаписьВПротокол(525, 70, rs.value("T_COURSEID"), rs.Value("T_TYPE"), StrFor(1), "Ошибка: в буферной схеме отсутствует удаляемый курс", rs.Value("T_INSTANCEDATE"));
            return false;
         end;
      else
         V_BASEFIKIND = rs.Value("T_BASEFIKIND");
         V_BASEFIID = rs.Value("T_BASEFIID");
         V_FIID = rs.Value("T_FIID");
         V_MARKETID = rs.Value("T_MARKETID");
         V_RATEDATE = rs.Value("T_RATEDATE");
         V_RATE = rs.Value("T_RATE");
         V_MARKETSECTORID = rs.Value("T_MARKETSECTORID");
      end;
   
      SQL1 = "select t_destid from DTXREPLOBJ_DBT where t_objecttype = 30 and t_objectid = " + V_MARKETID;     
      cmd1 = RsdCommand( SQL1);
      rs1 = RsdRecordSet(cmd1);
      if (rs1.Movenext)
         MARKETID = rs1.Value(0);
         SECTION = ПолучитьСоответствие(40, V_MARKETID, V_MARKETSECTORID);
      else
         ЗанестиЗаписьВПротокол(525, 70, rs.value("T_COURSEID"), rs.Value("T_TYPE"), StrFor(1), "Ошибка: невозможно ничего сделать с курсом для несуществующей торговой площадки, финансовый инструмент " + NameFIID, rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      end;
   
      if ((rs.Value("T_ACTION") == 1) or (rs.Value("T_ACTION") == 2) or (rs.Value("T_ACTION") == 3))
         SQL1 = "select t_destid from DTXREPLOBJ_DBT where t_objecttype = " + V_BASEFIKIND + " and t_objectid = " + V_BASEFIID;
         cmd1 = RsdCommand( SQL1);
         rs1 = RsdRecordSet(cmd1);
   
         if (rs1.Movenext)
            BASEFIID = rs1.Value(0);
            if (V_BASEFIKIND == 10)
               FIID = ПолучитьСоответствие(10, V_FIID);
               NameFIID = ПолучитьНаименованиеВалюты(V_BASEFIID);
            elif ((V_BASEFIKIND == 20) and (V_FIID == 0))
               FIID = ПолучитьСоответствие(10, ПолучитьВалютуНоминала(V_BASEFIID));
               NameFIID = ПолучитьНаименованиеБумаги(V_BASEFIID);
            elif ((V_BASEFIKIND == 20) and (V_FIID > 0))
               FIID = ПолучитьСоответствие(10, V_FIID);
               NameFIID = ПолучитьНаименованиеБумаги(V_BASEFIID);
            end;
   
            if (FIID == -1)
               ЗанестиЗаписьВПротокол(527, 70, rs.value("T_COURSEID"), rs.Value("T_TYPE"), StrFor(1), "Ошибка: невозможно ничего сделать с курсом для несуществующего котируемого финансового инструмента, базовый финансовый инструмент " + NameFIID, rs.Value("T_INSTANCEDATE"));
               Return FALSE;
            end;
         else
            ЗанестиЗаписьВПротокол(528, 70, rs.value("T_COURSEID"), rs.Value("T_TYPE"), StrFor(1), "Ошибка: невозможно ничего сделать с курсом для несуществующего базового финансового инструмента " + NameFIID, rs.Value("T_INSTANCEDATE"));
            Return FALSE;
         end;
   
         if ((V_BASEFIKIND == 10) and rs.Value("T_TYPE") == 6)
            T_ISDOMINANT = "chr(88)";
         else
            T_ISDOMINANT = "chr(0)";
         end;
   
         if ((V_BASEFIKIND == 20) and (FI_IsBond(int(BASEFIID)) == TRUE) and (rs.Value("T_TYPE") != NKDonDate))
            T_ISRELATIVE = "chr(88)";
         else
            T_ISRELATIVE = "chr(0)";
         end;
      end; 
   
      // Ищем в целевой системе ID курса и дату последнего значения
      SQL1 = "select t_rateid, t_sincedate from dratedef_dbt where T_FIID = " + FIID + " and T_OTHERFI = " + BASEFIID + " and T_TYPE = ";
      SQL1 = SQL1 + RTYPE(rs.Value("T_TYPE")) + " and t_market_place = " + MARKETID;
      if ((RTYPE(rs.Value("T_TYPE")) != 23) AND (SECTION != -1))
         SQL1 = SQL1 + " and t_section = " + SECTION;  
      end;
   
      // Торговая площадка и сектор - их надо перекодировать. Сур.
      cmd1 = RsdCommand(SQL1);
      rs1 = RsdRecordSet(cmd1);
      
      if (rs1.movenext)
         count = Rs1.Value(0);
         Rate_Date = Rs1.Value(1);
      else
         count = 0;
         Rate_Date = Date(01,01,1950); 
      end;
   
      // По ID курса cмотрим, есть ли значение курса за дату.
      SQL1 = " SELECT nvl((SELECT max(t_rate/power(10,t_point)) ";
      SQL1 = SQL1 +  "           FROM dratedef_dbt";
      SQL1 = SQL1 +  "          WHERE t_rateid = " + count + " and t_sincedate = " + getsqlDate_SK(DateStamp(V_RATEDATE));
      SQL1 = SQL1 +  "),0) + nvl((SELECT max(t_rate/power(10,t_point)) ";
      SQL1 = SQL1 +  "                                   FROM dratehist_dbt";
      SQL1 = SQL1 +  "                                  WHERE t_rateid = " + count + " and t_sincedate = ";
      SQL1 = SQL1 + getsqlDate_SK(DateStamp(V_RATEDATE)) + "),0)";
      SQL1 = SQL1 +  "   FROM DUAL";
      cmd1 = RsdCommand(SQL1);
      rs1 = RsdRecordSet(cmd1);
      
      rs1.Movenext;         
      count_date = rs1.Value(0);
      rs1.Close();
   
      if (rs.Value("T_ACTION") == 1)
         if (count_date > 0)
            // Ошибка: уже существует данный курс за такую дату
            if ( count_date != V_RATE )  // Если новый конфликтует по значению со старым - сообщаем
             ЗанестиЗаписьВПротокол(418, 70, rs.value("T_COURSEID"), rs.Value("T_TYPE"), StrFor(1), "Ошибка: уже существует данный курс за такую дату " + Date(V_RATEDATE) + ", финансовый инструмент " + NameFIID, rs.Value("T_INSTANCEDATE"));
             return false;
           end;
   
            SQL1 = "update DTXCOURSE_DBT set t_replstate = 2 where T_COURSEID = " + rs.value("T_COURSEID") + " and t_action = 1 and t_replstate = 0";
            SQL1 = SQL1 + " and t_type = " + rs.value("t_type") + " and trunc(t_ratedate) = " + getsqlDate_SK(DateStamp(V_RATEDATE));
            SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
            cmd1 = RsdCommand( SQL1);
            cmd1.execute;
            Return TRUE;  // если новый курс полностью аналогичен старому - ничего не делаем.
         end;
   
         // Если вносим самое свежее значение курса..          
         if (Rate_Date < DateStamp(V_RATEDATE))
            // Перекинем текущее значение в историю.
            SQL1 = " INSERT INTO dratehist_dbt(t_rateid, t_isinverse, t_rate, t_scale, t_point, t_inputdate, t_inputtime, t_oper, t_sincedate, t_ismanualinput) " +
                   "      SELECT t_rateid, t_isinverse, t_rate, t_scale, t_point, t_inputdate, t_inputtime, t_oper, t_sincedate, t_ismanualinput " +
                   "        FROM dratedef_dbt where t_rateid = " + count;
            cmd1 = RsdCommand(SQL1);
            cmd1.execute;
   
            // Если в целевой системе такого курса нет вообще - подберем для него ID.
            if (count <= 0)                                       
               SQL1 = "select dratedef_dbt_seq.nextval FROM dual";
               cmd1 = RsdCommand(SQL1);
               rs1 = RsdRecordSet(cmd1);  
               rs1.Movenext;     
               RateID = rs1.Value(0);           
                  
               SQL1 = "insert into dratedef_dbt (T_RATEID, T_FIID, T_OTHERFI, T_NAME, T_DEFINITION, T_TYPE, T_ISDOMINANT, T_ISRELATIVE, T_INFORMATOR, T_MARKET_PLACE, T_ISINVERSE, T_RATE, T_SCALE, T_POINT, T_INPUTDATE, T_INPUTTIME, T_OPER, T_SINCEDATE, T_SECTION, T_ISMANUALINPUT) values( "
                    + RateID + ", "                                                      /*T_RATEID              NUMBER(10)*/
                    + FIID + ", "                                                        /*T_FIID                NUMBER(10)*/
                    + BASEFIID + ", "                                                    /*T_OTHERFI             NUMBER(10)*/
                    + "chr(1), "                                                         /*T_NAME                VARCHAR2(10)*/
                    + "chr(1), "                                                         /*T_DEFINITION          VARCHAR2(164)*/
                    + RTYPE(rs.Value("T_TYPE")) + ", "                                   /*T_TYPE                NUMBER(5)*/
                    + T_ISDOMINANT + ", "                                                /*T_ISDOMINANT          CHAR(1)*/
                    + T_ISRELATIVE + ", "                                                /*T_ISRELATIVE          CHAR(1)*/
                    + MARKETID + ", "                                                    /*T_INFORMATOR          NUMBER(10)*/
                    + MARKETID + ", "                                                    /*T_MARKET_PLACE        NUMBER(10)*/
                    + "chr(0), "                                                         /*T_ISINVERSE           CHAR(1)*/
                    + rs.Value("T_RATE") * pow(10,rs.Value("T_POINT")) + ", "            /*T_RATE                FLOAT(22)*/
                    + rs.Value("T_SCALE") + ", "                                         /*T_SCALE               NUMBER(10)*/
                    + rs.Value("T_POINT") + ", "                                         /*T_POINT               NUMBER(5)*/
                    + getsqlDate_SK(DateStamp(ПолучитьTimeStamp(), innertime)) + ", "    /*T_INPUTDATE           DATE*/
                    + GetSQLTime(innertime) + ", "                                       /*T_INPUTTIME           DATE*/
                    + {Oper} + ", "                                                      /*T_OPER                NUMBER(5)*/
                    + getsqlDate_SK(DateStamp(V_RATEDATE)) + ", "                        /*T_SINCEDATE           DATE*/
                    + abs(SECTION) + ", "                                                /*T_SECTION             NUMBER(10)*/
                    + "chr(0) "                                                          /*T_ISMANUALINPUT       CHAR(1)*/
                    + ")";
               cmd1 = RsdCommand(SQL1);
               cmd1.execute;
            else
               SQL1 = "update dratedef_dbt set  T_RATE = " + rs.Value("T_RATE") * pow(10,rs.Value("T_POINT")) + ", T_SCALE = " + rs.Value("T_SCALE");
               SQL1 = SQL1 + ", T_POINT = " + rs.Value("T_POINT") + ", T_INPUTDATE = " + getsqlDate_SK(DateStamp(ПолучитьTimeStamp(), innertime));
               SQL1 = SQL1 + ", T_INPUTTIME = " + GetSQLTime(innertime) + ", T_OPER = " + {Oper} + ", T_SINCEDATE = " + getsqlDate_SK(DateStamp(V_RATEDATE));
               SQL1 = SQL1 + " where t_rateid = " + count;
               cmd1 = RsdCommand(SQL1);
               cmd1.execute;
            end;
         else  // если значение не самое свежее - вносим его в историю.
            SQL1 = "insert into dratehist_dbt(t_rateid, t_isinverse, t_rate, t_scale, t_point, t_inputdate, t_inputtime, t_oper, t_sincedate, t_ismanualinput) values( "
                 + count                                                              // T_RATEID
                 + ",chr(0),"                                                         // T_ISINVERSE
                 + rs.Value("T_RATE") * pow(10,rs.Value("T_POINT"))                   // T_RATE
                 + "," + rs.Value("T_SCALE")                                          // T_SCALE
                 + "," + rs.Value("T_POINT") + ","                                    // T_POINT
                 + getsqlDate_SK(DateStamp(ПолучитьTimeStamp(), innertime)) + ", "    // T_INPUTDATE
                 + GetSQLTime(innertime) + ", "                                       // T_INPUTTIME
                 + {Oper} + ", "                                                      // T_OPER
                 + getsqlDate_SK(DateStamp(V_RATEDATE))                               // T_SINCEDATE
                 + ",chr(0) )";                                                       // T_ISMANUALINPUT
            cmd1 = RsdCommand(SQL1);
            cmd1.execute;
         end;
   
         SQL1 = "delete from DTXREPLOBJ_DBT where T_OBJECTTYPE=70 and t_objstate = 2 and t_objectid = " + rs.Value("T_COURSEID");
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;
                                             
         SQL2 = "insert into DTXREPLOBJ_DBT (T_OBJECTTYPE, T_OBJECTID, T_SUBOBJNUM, T_DESTID, T_DESTSUBOBJNUM,";
         SQL2 = SQL2 + "T_OBJSTATE) values (70, " + rs.Value("T_COURSEID") + ", " + rs.value("T_TYPE") + ", ";
         SQL2 = SQL2 + count + " ,1 , 0)";
         cmd2 = RsdCommand( SQL2);
         cmd2.execute;
           
         SQL1 = "update DTXCOURSE_DBT set t_replstate = 1 where T_COURSEID = " + rs.value("T_COURSEID") + " and t_action = 1 and t_replstate = 0";
         SQL1 = SQL1 + " and t_type = " + rs.value("t_type") + " and trunc(t_ratedate) = " + getsqlDate_SK(DateStamp(V_RATEDATE));
         SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;
         // Успешно вставлен курс.
      elif (rs.Value("t_Action") == 2)
         if (count_date == 0)
            // Ошибка: невозможно обновить несуществующий курс
            ЗанестиЗаписьВПротокол(419, 70, rs.value("T_COURSEID"), rs.Value("T_TYPE"), StrFor(1), "Ошибка: невозможно обновить несуществующий курс, финансовый инструмент " + NameFIID, rs.Value("T_INSTANCEDATE"));
            Return FALSE;
         end;
   
         if (ПроверитьНаРучноеИзменение(70, rs.value("T_COURSEID")))
            // Ошибка: объект находится в режиме ручного редактирования
            ЗанестиЗаписьВПротокол(205, 70, rs.value("T_COURSEID"), rs.Value("T_TYPE"), StrFor(1), "Ошибка: объект находится в режиме ручного редактирования, финансовый инструмент " + NameFIID, rs.Value("T_INSTANCEDATE"));
            Return FALSE;
         end;
   
         if (СущественныйАтрибут(70, rs.value("T_COURSEID"), "T_RATE", rs.Value("T_RATE"), rs.value("T_TYPE")))
            ЗанестиЗаписьВПротокол(131, 70, rs.value("T_COURSEID"), rs.Value("T_TYPE"), "T_RATE", "Предупреждение: изменение существенного атрибута 'Курс', финансовый инструмент " + NameFIID, rs.Value("T_INSTANCEDATE"));
         end;
   
         if (СущественныйАтрибут(70, rs.value("T_COURSEID"), "T_SCALE", rs.Value("T_SCALE"), rs.value("T_TYPE")))
            ЗанестиЗаписьВПротокол(132, 70, rs.value("T_COURSEID"), rs.Value("T_TYPE"), "T_SCALE", "Предупреждение: изменение существенного атрибута 'Масштаб', финансовый инструмент " + NameFIID, rs.Value("T_INSTANCEDATE"));
         end;
   
         if (Rate_Date == DateStamp(V_RATEDATE))  // Если обновляется последнее значение курса
            SQL1 = "update dratedef_dbt set t_fiid = " + FIID + ", t_otherfi = " + BASEFIID + ", T_TYPE = " + RTYPE(rs.value("T_TYPE")) + ", T_MARKET_PLACE = ";
            SQL1 = SQL1 + MARKETID + ", T_RATE = " + rs.Value("T_RATE") * pow(10, rs.Value("T_POINT")) + ", T_SCALE = " + rs.Value("T_SCALE") + ", T_POINT = " + rs.Value("T_POINT");
            SQL1 = SQL1 + ", T_INPUTDATE = " + getsqlDate_SK(DateStamp(ПолучитьTimeStamp(), innertime)) + ", T_INPUTTIME = ";
            SQL1 = SQL1 + GetSQLTime(innertime) + ", T_OPER = " + {Oper} + ", T_SECTION = " + abs(SECTION) + ", T_ISDOMINANT = " + T_ISDOMINANT + ", T_ISRELATIVE = " + T_ISRELATIVE + " where T_RATEID = " + count;
            SQL1 = SQL1 + " and trunc(T_SINCEDATE) = " + getsqlDate_SK(DateStamp(V_RATEDATE));
         else    // если обновляется история
            SQL1 = "update dratehist_dbt set T_RATE=" + rs.Value("T_RATE") * pow(10, rs.Value("T_POINT")) + ", T_SCALE=" + rs.Value("T_SCALE") + ", T_POINT=" + rs.Value("T_POINT");
            SQL1 = SQL1 + ", T_INPUTDATE=" + getsqlDate_SK(DateStamp(ПолучитьTimeStamp(), innertime)) + ", T_INPUTTIME=";
            SQL1 = SQL1 + GetSQLTime(innertime) + ", T_OPER=" + {Oper} + " where T_RATEID = " + count;
            SQL1 = SQL1 + " and trunc(T_SINCEDATE) = " + getsqlDate_SK(DateStamp(V_RATEDATE));
         end;
   
         cmd1 = RsdCommand(SQL1);
         cmd1.execute;
   
         SQL1 = "update DTXCOURSE_DBT set t_replstate = 1 where T_COURSEID = " + rs.value("T_COURSEID") + " and t_action = 2 and t_replstate = 0";
         SQL1 = SQL1 + " and t_type = " + rs.value("t_type") + " and trunc(t_ratedate) = " + getsqlDate_SK(DateStamp(V_RATEDATE));
         SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;
         // Успешно обновлен курс.
      elif (rs.Value("t_Action") == 3)
         if (count_date == 0)
            // Ошибка: невозможно удалить несуществующий курс.
            ЗанестиЗаписьВПротокол(420, 70, rs.value("T_COURSEID"), rs.Value("T_TYPE"), StrFor(1), "Ошибка: невозможно удалить несуществующий курс, финансовый инструмент " + NameFIID, rs.Value("T_INSTANCEDATE"));
            Return FALSE;
         end;
         SQL1 = "delete from dratehist_dbt where t_rateid = " + count + " and t_sincedate = " + getsqlDate_SK(DateStamp(V_RATEDATE));
         cmd1 = RsdCommand(SQL1);
         cmd1.execute;
   
         SQL1 = "update DTXCOURSE_DBT set t_replstate = 1 where T_COURSEID = " + rs.value("T_COURSEID") + " and t_action = 3 and t_replstate = 0";
         SQL1 = SQL1 + " and t_type = " + rs.value("t_type") + " and trunc(t_ratedate) = " + getsqlDate_SK(DateStamp(V_RATEDATE));
         SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;
   
         SQL1 = " SELECT (SELECT COUNT (*)";
         SQL1 = SQL1 +  "           FROM dratedef_dbt";
         SQL1 = SQL1 +  "          WHERE t_rateid = " + count + ") + (SELECT COUNT (*)";
         SQL1 = SQL1 +  "                                   FROM dratehist_dbt";
         SQL1 = SQL1 +  "                                  WHERE t_rateid = " + count + ")";
         SQL1 = SQL1 +  "   FROM DUAL";
         cmd1 = RsdCommand(SQL1);
         rs1 = RsdRecordSet(cmd1);
   
         rs1.Movenext;
         if (rs1.Value(0) == 0)
            SQL2 = "update DTXREPLOBJ_DBT ots set ots.T_OBJSTATE = 2 where ots.T_OBJECTTYPE = 70 and ots.T_OBJECTID = " + rs.value("T_COURSEID");
            cmd2 = RsdCommand( SQL2);
            cmd2.execute;
         end;
   
         if ( rs.Value("T_INSTANCEDATE") >= rs.value("T_SINCEDATE"))
            ЗанестиЗаписьВПротокол(131, 70, rs.value("T_COURSEID"), rs.Value("T_TYPE"), StrFor(1), "Предупреждение: удаление исторического значения курса по бумаге " + NameFIID, rs.Value("T_INSTANCEDATE"));
         end;
         // Успешно удален курс.
      end;
   end;


   // Репликация номиналов ц/б на дату (реплицируются из таблицы курсов)
   if( rs.Value("T_TYPE") == NominalOnDate )
      var FaceValueFIID = -1;
      var V_ID = rs.Value("T_COURSEID"); // ID номинала
      var V_TYPE = rs.Value("T_TYPE");
      var V_destid = -1; // ID номинала в целевой системе
      var sql_InitNom = ""; // начальное значение номинала по бумаге

      V_FIID = rs.Value("T_BASEFIID"); // ФИ, для которого задан номинал
      V_BASEFIID = rs.Value("T_FIID"); // ФИ, в единицах которого задан номинал

      V_RATEDATE = rs.Value("T_RATEDATE");
      V_RATE = double(rs.Value("T_RATE"));

     // для номинала в системе должна существовать бумага
      SQL1 = "select t_objectid from DTXREPLOBJ_DBT where T_OBJSTATE != 2 and t_objecttype = " + 20 + " and t_objectid = " + V_FIID;
      cmd1 = RsdCommand(SQL1);
      rs1 = RsdRecordSet(cmd1);

      if (rs1.Movenext)
         FaceValueFIID = ПолучитьСоответствие(10, V_BASEFIID);
         FIID = ПолучитьСоответствие(20, V_FIID);
         NameFIID = ПолучитьНаименованиеБумаги(V_FIID);

         SQL1 = "select t_fiid, t_FaceValueFI from dfininstr_dbt where t_fiid = " + FIID;
         cmd1 = RsdCommand(SQL1);
         rs1 = RsdRecordSet(cmd1);

         if( rs1.Movenext )
           if( FaceValueFIID != rs1.value("t_FaceValueFI") )
              ЗанестиЗаписьВПротокол(527, 70, V_ID, V_TYPE, StrFor(1), "Ошибка: значение номинала указано не в валюте номинала " + NameFIID, rs.Value("T_INSTANCEDATE"));
              Return FALSE;
           end;
         else
            ЗанестиЗаписьВПротокол(527, 70, V_ID, V_TYPE, StrFor(1), "Ошибка: невозможно ничего сделать с номиналом для несуществующего финансового инструмента, финансовый инструмент " + NameFIID, rs.Value("T_INSTANCEDATE"));
            Return FALSE;
         end;
      else
         ЗанестиЗаписьВПротокол(528, 70, V_ID, V_TYPE, StrFor(1), "Ошибка: невозможно ничего сделать с номиналом для несуществующего финансового инструмента " + NameFIID, rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      end;

      sql_InitNom = "select t_FaceValue t_value from DV_FI_FACEVALUE_HIST where T_ID = 0 and T_FIID = " + FIID + " and t_BegDate = " + getsqlDate_SK(DateStamp(V_RATEDATE));

      // изменение/удалаение
      if((rs.Value("T_ACTION") == 2) or (rs.Value("T_ACTION") == 3))
         SQL1 = sql_InitNom;
         cmd1 = RsdCommand(SQL1);
         rs1 = RsdRecordSet(cmd1);
         if (rs1.Movenext())
            SQL1 = "update DTXCOURSE_DBT set t_replstate = 2 where T_COURSEID = " + V_ID + " and t_action = " + rs.Value("T_ACTION") + " and t_replstate = 0";
            SQL1 = SQL1 + " and t_type = " + V_TYPE + " and trunc(t_ratedate) = " + getsqlDate_SK(DateStamp(V_RATEDATE));
            SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
            cmd1 = RsdCommand(SQL1);
            cmd1.execute;

            ЗанестиЗаписьВПротокол(525, 70, V_ID, V_TYPE, StrFor(1), "Предупреждение: Изменение начального номинала бумаги нужно выполнять при репликации данных из DTXAVOIRISS_DBT", rs.Value("T_INSTANCEDATE"));
            return true;
         end;

         SQL1 = "select T_DESTID from DTXREPLOBJ_DBT where T_OBJSTATE != 2 and T_OBJECTTYPE = 70 and T_OBJECTID = " + V_ID;
         cmd1 = RsdCommand(SQL1);
         rs1 = RsdRecordSet(cmd1);
         if (rs1.Movenext())                    
            V_destid = rs1.Value("T_DESTID");
         else
            ЗанестиЗаписьВПротокол(525, 70, V_ID, V_TYPE, StrFor(1), "Ошибка: изменяемый/удаляемый номинал не реплицирован в целевую систему", rs.Value("T_INSTANCEDATE"));
            return false;
         end;

         SQL1 = "select T_ID from DFIVLHIST_DBT where T_ID = " + V_destid;
         cmd1 = RsdCommand(SQL1);
         rs1 = RsdRecordSet(cmd1);
         if (not rs1.Movenext())                    
            ЗанестиЗаписьВПротокол(525, 70, V_ID, V_TYPE, StrFor(1), "Ошибка: изменяемый/удаляемый номинал отсутствует в системе", rs.Value("T_INSTANCEDATE"));
            return false;
         end;
      end;

      if (rs.Value("T_ACTION") == 1)
         // Ищем в целевой системе номинал на дату
         // сначала сравним с начальным номиналом по бумаге
         SQL1 = sql_InitNom;
         cmd1 = RsdCommand(SQL1);
         rs1 = RsdRecordSet(cmd1);

         count_date = Money(0);
         if (rs1.movenext)
            count_date = double(Rs1.Value("t_value"));
         else
            SQL1 = "select t_value from DFIVLHIST_DBT where t_ValKind = " + 1/*изменение номинала*/ + " and T_FIID = " + FIID + " and t_EndDate = " + getsqlDate_SK(DateStamp(V_RATEDATE));
            cmd1 = RsdCommand(SQL1);
            rs1 = RsdRecordSet(cmd1);
            if (rs1.movenext)
               count_date = double(Rs1.Value("t_value"));
            end;
         end;

         if (count_date > 0)
            // Ошибка: уже существует номинал за такую дату
            if ( count_date != V_RATE )  // Если новый конфликтует по значению со старым - сообщаем
               ЗанестиЗаписьВПротокол(418, 70, V_ID, V_TYPE, StrFor(1), "Ошибка: уже существует номинал за такую дату " + Date(V_RATEDATE) + ", финансовый инструмент " + NameFIID, rs.Value("T_INSTANCEDATE"));
                return false;
              end;

            SQL1 = "update DTXCOURSE_DBT set t_replstate = 2 where T_COURSEID = " + V_ID + " and t_action = 1 and t_replstate = 0";
            SQL1 = SQL1 + " and t_type = " + V_TYPE + " and trunc(t_ratedate) = " + getsqlDate_SK(DateStamp(V_RATEDATE));
            SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
            cmd1 = RsdCommand(SQL1);
            cmd1.execute;
            Return TRUE;  // если новый номинал полностью аналогичен старому - ничего не делаем.
         end;

         // подберем для него ID.
         SQL1 = "select DFIVLHIST_DBT_seq.nextval FROM dual";
         cmd1 = RsdCommand(SQL1);
         rs1 = RsdRecordSet(cmd1);
         rs1.Movenext;
         RateID = rs1.Value(0);

         SQL1 = "insert into DFIVLHIST_DBT (T_ID, T_FIID, T_VALKIND, T_ENDDATE, T_VALUE, T_INTVALUE) values(";
         SQL1 = SQL1 + RateID + ", ";                                                      /*T_ID                  NUMBER(10)*/
         SQL1 = SQL1 + FIID + ", ";                                                        /*T_FIID                NUMBER(10)*/
         SQL1 = SQL1 + "1, ";                                                              /*T_VALKIND             NUMBER(5)*/
         SQL1 = SQL1 + getsqlDate_SK(DateStamp(V_RATEDATE)) + ", ";                        /*T_ENDDATE             DATE*/
         SQL1 = SQL1 + V_RATE + ", ";                                                      /*T_VALUE               NUMBER(32,12)*/
         SQL1 = SQL1 + "0 ";                                                               /*T_INTVALUE            NUMBER(10)*/
         SQL1 = SQL1 + ")";
         cmd1 = RsdCommand(SQL1);
         cmd1.execute;

         SQL1 = "delete from DTXREPLOBJ_DBT where T_OBJECTTYPE=70 and t_objstate = 2 and t_objectid = " + V_ID;
         cmd1 = RsdCommand(SQL1);
         cmd1.execute;
                                             
         SQL2 = "insert into DTXREPLOBJ_DBT (T_OBJECTTYPE, T_OBJECTID, T_SUBOBJNUM, T_DESTID, T_DESTSUBOBJNUM,";
         SQL2 = SQL2 + "T_OBJSTATE) values (70, " + V_ID + ", " + V_TYPE + ", ";
         SQL2 = SQL2 + RateID + " ,1 , 0)";
         cmd2 = RsdCommand(SQL2);
         cmd2.execute;

         SQL1 = "update DTXCOURSE_DBT set t_replstate = 1 where T_COURSEID = " + V_ID + " and t_action = 1 and t_replstate = 0";
         SQL1 = SQL1 + " and t_type = " + V_TYPE + " and trunc(t_ratedate) = " + getsqlDate_SK(DateStamp(V_RATEDATE));
         SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
         cmd1 = RsdCommand(SQL1);
         cmd1.execute;
         // Успешно вставлен номинал.
      elif (rs.Value("t_Action") == 2)
         if (ПроверитьНаРучноеИзменение(70, V_ID))
            // Ошибка: объект находится в режиме ручного редактирования
            ЗанестиЗаписьВПротокол(205, 70, V_ID, V_TYPE, StrFor(1), "Ошибка: объект находится в режиме ручного редактирования, финансовый инструмент " + NameFIID, rs.Value("T_INSTANCEDATE"));
            Return FALSE;
         end;

         SQL1 = "update DFIVLHIST_DBT set T_FIID = " + FIID + ", T_VALKIND = " + 1/*изменение номинала*/ + ", T_ENDDATE = " + getsqlDate_SK(DateStamp(V_RATEDATE)) + ", T_VALUE = ";
         SQL1 = SQL1 + V_RATE + ", T_INTVALUE = 0 where t_ID = " + V_destid;
         cmd1 = RsdCommand(SQL1);
         cmd1.execute;

         SQL1 = "update DTXCOURSE_DBT set t_replstate = 1 where T_COURSEID = " + V_ID + " and t_action = 2 and t_replstate = 0";
         SQL1 = SQL1 + " and t_type = " + V_TYPE + " and trunc(t_ratedate) = " + getsqlDate_SK(DateStamp(V_RATEDATE));
         SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
         cmd1 = RsdCommand(SQL1);
         cmd1.execute;
         // Успешно обновлен номинал.
      elif (rs.Value("t_Action") == 3)
         SQL1 = "delete from DFIVLHIST_DBT where t_id = " + V_destid;
         cmd1 = RsdCommand(SQL1);
         cmd1.execute;

         SQL1 = "update DTXCOURSE_DBT set t_replstate = 1 where T_COURSEID = " + V_ID + " and t_action = 3 and t_replstate = 0";
         SQL1 = SQL1 + " and t_type = " + V_TYPE + " and trunc(t_ratedate) = " + getsqlDate_SK(DateStamp(V_RATEDATE));
         SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
         cmd1 = RsdCommand(SQL1);
         cmd1.execute;

         SQL2 = "update DTXREPLOBJ_DBT set T_OBJSTATE = 2 where T_OBJECTTYPE = 70 and T_OBJECTID = " + V_ID;
         cmd2 = RsdCommand(SQL2);
         cmd2.execute;
         // Успешно удален номинал.
      end;
   end;

   return TRUE;
OnError(er) 
   RslDefCon.RollbackTrans();
   ЗанестиЗаписьВПротокол(601, 70, rs.value("T_COURSEID"), rs.Value("T_TYPE"), StrFor(1), "Ошибка: " + er.message, rs.Value("T_INSTANCEDATE"));
   AbortTrn;
end;

// Обрабатывает курсы и изменение номинала(если T_TYPE == 100)
macro ОбработатьКурс(Action, t_instancedate, Init_Action:String)
   var SQL, cmd, rs, Счетчик;

   SQL = "select count(*) from DTXCOURSE_DBT where t_replstate = 0 and t_action = " + Action + " and trunc(t_instancedate) = " + t_instancedate;
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);

   if (rs.Movenext)
      Счетчик = rs.Value(0);
      MaxVal = rs.Value(0);
   end;

   var ProgressIndicator = CreateProgressIndicator(true);
   ProgressIndicator.Start(Int(MaxVal), "Обработка курсов...");

   Счетчик = 0;

   SQL = "select * from DTXCOURSE_DBT where t_replstate = 0 and t_action = " + Action + " and trunc(t_instancedate) = " + t_instancedate + " order by t_basefiid, t_marketsectorid, t_type, trunc(t_ratedate)";
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);

   while (rs.Movenext)
      Счетчик = Счетчик + 1;

      ProgressIndicator.Update(Int(Счетчик), MaxVal);

      if(RslDefCon.IsInTRans == false)
         RslDefCon.BeginTrans();
      end;

      if (ProcessRate(rs))
         RslDefCon.CommitTrans();
      else
         RslDefCon.RollbackTrans();
      end;
   end;

   ProgressIndicator.Stop();

   return TRUE;
OnError(er)
   ЗанестиЗаписьВПротокол(601, 70, "", "", StrFor(1), "Ошибка: " + er.message, date(t_instancedate));
end;