/*
$Name:             txproc_sector.mac
$Module:           БОЦБ 
$Description:      Макрос репликации секторов
*/
import txrepl_func;

private var MaxVal;

macro ProcessSector(rs);
   var SQL1, cmd1, rs1, count, MarkedId, NewSectorID, NameParty;

   NameParty = ПолучитьНаименованиеСубъекта(rs.value("t_markedid"));

   SQL1 = "SELECT T_DESTID FROM DTXREPLOBJ_DBT where T_OBJECTTYPE = 40 and T_OBJECTID = " + rs.Value("T_MARKEDID");
   SQL1 = SQL1 + " AND T_SUBOBJNUM = " + rs.Value("T_MARKETSECTORID") + " AND T_OBJSTATE != 2";

   cmd1 = RsdCommand( SQL1);
   rs1 = RsdRecordSet(cmd1);

   if (rs1.movenext)
      count = Rs1.Value(0);
   else
      count = 0;
   end;

   SQL1 = "select t_destid from DTXREPLOBJ_DBT where t_objecttype = 30 and t_objectid = " + rs.Value("t_markedid");
   cmd1 = RsdCommand( SQL1);
   rs1 = RsdRecordSet(cmd1);

   if (rs1.Movenext)
      MarkedId = rs1.Value(0);
   else
      ЗанестиЗаписьВПротокол(500, 40, rs.Value("T_MARKEDID"), rs.Value("T_MARKETSECTORID"), StrFor(1), "Ошибка: невозможно ничего сделать с сектором " + trim(rs.Value("T_NAME")) + " несуществующей биржи " + NameParty, rs.Value("T_INSTANCEDATE"));
      Return FALSE;
   end;

   if (rs.Value("T_ACTION")==1)
      if (count > 0)
         ЗанестиЗаписьВПротокол(403, 40, rs.Value("T_MARKEDID"), rs.Value("T_MARKETSECTORID"), StrFor(1), "Ошибка: невозможно вставить существующий сектор " + trim(rs.Value("T_NAME")) + " биржи " + NameParty, rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      end;

      SQL1 = "SELECT nvl(MAX(t_officeid), 0)+1 FROM dptoffice_dbt ptof WHERE ptof.t_partyid = " + MarkedId;
      cmd1 = RsdCommand(SQL1);
      rs1 = RsdRecordSet(cmd1);
      rs1.movenext;
      NewSectorID = rs1.Value(0);

      SQL1 = "insert into dptoffice_dbt values(";
      SQL1 = SQL1 + MarkedId + ", ";                                            /*T_PARTYID             NUMBER(10)*/
      SQL1 = SQL1 + NewSectorID + ", '";                                        /*T_OFFICEID            NUMBER(10)*/
      SQL1 = SQL1 + trim(rs.Value("T_CODE")) + "', '";                          /*T_OFFICECODE          VARCHAR2(15)*/
      SQL1 = SQL1 + trim(rs.Value("T_NAME")) + "', ";                           /*T_OFFICENAME          VARCHAR2(80)*/
      SQL1 = SQL1 + "chr(1), ";                                                 /*T_DESCRIPTION         VARCHAR2(160)*/
      SQL1 = SQL1 + "chr(1), ";                                                 /*T_KPP                 VARCHAR2(9)*/
      SQL1 = SQL1 + "chr(1), ";                                                 /*T_REGNUMBER           VARCHAR2(35)*/
      SQL1 = SQL1 + getsqlDate_SK(DateStamp(rs.Value("T_OPENDATE")))+", ";      /*T_REGDATE             DATE*/
      SQL1 = SQL1 + "0, ";                                                      /*T_DOCKINDID           NUMBER(10)*/
      SQL1 = SQL1 + "chr(1), ";                                                 /*T_SERIES              VARCHAR2(20)*/
      SQL1 = SQL1 + "chr(1), ";                                                 /*T_NUMBER              VARCHAR2(20)*/
      SQL1 = SQL1 + "chr(1), ";                                                 /*T_MAINCASHIER         VARCHAR2(40)*/
      SQL1 = SQL1 + getsqlDate_SK(DateStamp(rs.Value("T_CLOSEDATE")))+", ";     /*T_DATECLOSE           DATE*/
      SQL1 = SQL1 + "chr(1), ";                                                 /*T_POSTADDRESS         VARCHAR2(99)*/
      SQL1 = SQL1 + "0, ";                                                      /*T_STATUSBRANCH        NUMBER(5)*/
      SQL1 = SQL1 + "chr(0), ";                                                 /*T_NOAUTO              CHAR(1)*/
      SQL1 = SQL1 + "chr(1), ";                                                 /*T_STRINGFORODBACC     VARCHAR2(4)*/
      SQL1 = SQL1 + "0, ";                                                      /*T_DBACCESS            NUMBER(5)*/
      SQL1 = SQL1 + "0, ";                                                      /*T_PLACEMENT           NUMBER(10)*/
      SQL1 = SQL1 + "0, ";                                                      /*T_MAINRATE            NUMBER(10)*/
      SQL1 = SQL1 + "chr(1), ";                                                 /*T_PHONE               VARCHAR2(20)*/
      SQL1 = SQL1 + "chr(0), ";                                                 /*T_OWNCURRRATE         CHAR(1)*/
      SQL1 = SQL1 + "chr(0), ";                                                 /*T_OWNEXPOPERSET       CHAR(1)*/
      SQL1 = SQL1 + "chr(0), ";                                                 /*T_STATUPREGIME        CHAR(1)*/
      SQL1 = SQL1 + "0, ";                                                      /*T_BANKID              NUMBER(10)*/
      SQL1 = SQL1 + "0, ";                                                      /*T_DEPID               NUMBER(10)*/
      SQL1 = SQL1 + "chr(0), ";                                                 /*T_CLIENT51            CHAR(1)*/
      SQL1 = SQL1 + "chr(1) ";                                                  /*T_RESERVE             VARCHAR2(299)*/
      SQL1 = SQL1 + ")";
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      SQL1 = "insert into dptoffisu_dbt values (";
      SQL1 = SQL1 + MarkedId + ", ";                                                    /*T_PARTYID     NUMBER(10)*/
      SQL1 = SQL1 + NewSectorID + ", ";                                                 /*T_OFFICEID    NUMBER(10)*/
      SQL1 = SQL1 + getsqlDate_SK(DateStamp(rs.Value("T_INSTANCEDATE")))+", ";          /*T_DATEBEGIN   DATE*/
      SQL1 = SQL1 + "0, ";                                                              /*T_SUPERIORID  NUMBER(10)*/
      SQL1 = SQL1 + NewSectorID + ", ";                                                 /*T_SORT        NUMBER(5)*/
      SQL1 = SQL1 + "chr(1) ";                                                          /*T_RESERV      VARCHAR2(129)*/
      SQL1 = SQL1 + ")";
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      if (rs.Value("T_ISFONDMARKET") == StrFor(88))
         SQL1 = "insert into dobjatcor_dbt(T_OBJECTTYPE,T_GROUPID,T_ATTRID,T_OBJECT,T_GENERAL,T_VALIDFROMDATE,T_OPER,T_VALIDTODATE)   values(";
         SQL1 = SQL1 + "10, ";                                                  /*T_OBJECTTYPE          NUMBER(5)*/
         SQL1 = SQL1 + "1, ";                                                   /*T_GROUPID             NUMBER(10)*/
         SQL1 = SQL1 + "1, ";                                                   /*T_ATTRID              NUMBER(10)*/
         SQL1 = SQL1 + "(select lpad(" + MarkedId + ", 17, '0') || lpad(" + NewSectorID;
         SQL1 = SQL1 + ", 17, '0') from dual), ";                                       /*T_OBJECT              VARCHAR2(35)*/
         SQL1 = SQL1 + "chr(88), ";                                                     /*T_GENERAL             CHAR(1)*/
         SQL1 = SQL1 + /*getsqlDate_SK(DateStamp(rs.Value("T_INSTANCEDATE")))*/nulldate + ", ";       /*T_VALIDFROMDATE       DATE*/
         SQL1 = SQL1 + {Oper}+", ";                                                     /*T_OPER                NUMBER(5)*/
         SQL1 = SQL1 + getsqlDate_SK(Date(31, 12, 9999));                          /*T_VALIDTODATE         DATE*/
         SQL1 = SQL1 + ")";
         cmd1 = RsdCommand(SQL1);
         cmd1.execute;
      end;

      SQL1 = "delete from DTXREPLOBJ_DBT where T_OBJECTTYPE=40 and t_objstate = 2 and t_objectid = " + rs.Value("T_MARKEDID");
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;

      SQL1 = "insert into DTXREPLOBJ_DBT (T_OBJECTTYPE, T_OBJECTID, T_SUBOBJNUM, T_DESTID, T_DESTSUBOBJNUM,";
      SQL1 = SQL1 + "T_OBJSTATE) values (40, " + rs.Value("T_MARKEDID") + ", " + rs.value("T_MARKETSECTORID") + ", ";
      SQL1 = SQL1 + MarkedId + " ," + NewSectorID + " , 0)";
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;

      SQL1 = "update DTXMARKSECT_DBT set t_replstate = 1 where t_MARKETSECTORID = " + rs.value("t_MARKETSECTORID");
      SQL1 = SQL1 + " and T_MARKEDID = " + rs.value("t_markedid") + " and t_action = 1 and t_replstate = 0";
      SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;
   elif (rs.Value("t_Action") == 2)
      if (count == 0)
         ЗанестиЗаписьВПротокол(404, 40, rs.Value("T_MARKEDID"), rs.Value("T_MARKETSECTORID"), StrFor(1), "Ошибка: невозможно обновить несуществующий сектор " + trim(rs.Value("T_NAME")) + ", биржа " + NameParty, rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      end;

      if (ПроверитьНаРучноеИзменение(40, rs.value("T_MARKETSECTORID")))
         ЗанестиЗаписьВПротокол(201, 40, rs.value("T_MARKETSECTORID"), 1, StrFor(1), "Ошибка: объект находится в режиме ручного редактирования, сектор " + trim(rs.Value("T_NAME")) + " биржа " + NameParty, rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      end;

      SQL1 = "update dptoffice_dbt set T_OFFICECODE = '" + trim(rs.Value("T_CODE")) + "', T_OFFICENAME = '" + trim(rs.Value("T_NAME")) + "', ";
      SQL1 = SQL1 + "T_REGDATE = " + getsqlDate_SK(DateStamp(rs.Value("T_OPENDATE"))) + ", T_DATECLOSE = ";
      SQL1 = SQL1 + getsqlDate_SK(DateStamp(rs.Value("T_CLOSEDATE")));
      SQL1 = SQL1 + " where T_PARTYID = " + MarkedId + " and T_OFFICEID = " + ПолучитьСоответствие(40, rs.Value("T_MARKEDID"), rs.Value("T_MARKETSECTORID"));
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      SQL1 = "delete from dobjatcor_dbt where T_OBJECTTYPE = 10 and T_GROUPID = 1 and T_OBJECT = '" + ДополнениеСлева(Trim(String(numeric(MarkedId):15:0)), 17, "0" ) + 
               ДополнениеСлева(Trim(String(numeric(ПолучитьСоответствие(40, rs.Value("T_MARKEDID"), rs.Value("T_MARKETSECTORID"))):15:0)), 17, "0" ) + "'";
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      if (Trim(rs.Value("T_ISFONDMARKET")) == StrFor(88))
         SQL1 = "insert into dobjatcor_dbt(T_OBJECTTYPE,T_GROUPID,T_ATTRID,T_OBJECT,T_GENERAL,T_VALIDFROMDATE,T_OPER,T_VALIDTODATE) values(";
         SQL1 = SQL1 + "10, ";                                                  /*T_OBJECTTYPE          NUMBER(5)*/
         SQL1 = SQL1 + "1, ";                                                   /*T_GROUPID             NUMBER(10)*/
         SQL1 = SQL1 + "1, '";                                                   /*T_ATTRID              NUMBER(10)*/
         SQL1 = SQL1 + ДополнениеСлева(Trim(String(numeric(MarkedId):15:0)), 17, "0" );
         SQL1 = SQL1 + ДополнениеСлева(Trim(String(numeric(ПолучитьСоответствие(40, rs.Value("T_MARKEDID"), rs.Value("T_MARKETSECTORID"))):15:0)), 17, "0" );
         SQL1 = SQL1 + "', ";                                                                          /*T_OBJECT              VARCHAR2(35)*/
         SQL1 = SQL1 + "chr(88), ";                                                                    /*T_GENERAL             CHAR(1)*/
         SQL1 = SQL1 + /*getsqlDate_SK(DateStamp(rs.Value("T_INSTANCEDATE")))*/nulldate + ", ";        /*T_VALIDFROMDATE       DATE*/
         SQL1 = SQL1 + {Oper} + ", ";                                                                  /*T_OPER                NUMBER(5)*/
         SQL1 = SQL1 + getsqlDate_SK(Date(31, 12, 9999)) ;                                       /*T_VALIDTODATE         DATE*/
         SQL1 = SQL1 + ")";
         cmd1 = RsdCommand(SQL1);
         cmd1.execute;
      end;

      SQL1 = "update DTXMARKSECT_DBT set t_replstate = 1 where t_MARKETSECTORID = " + rs.value("t_MARKETSECTORID");
      SQL1 = SQL1 + " and T_MARKEDID = " + rs.value("t_markedid") + " and t_action = 2 and t_replstate = 0";
      SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;
   elif (rs.Value("t_Action") == 3)
      if (count == 0)
         ЗанестиЗаписьВПротокол(405, 40, rs.Value("T_MARKEDID"), rs.Value("T_MARKETSECTORID"), StrFor(1), "Ошибка: невозможно удалить несуществующий сектор " + trim(rs.Value("T_NAME")) + ", биржа " + NameParty, rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      end;

      SQL1 = "delete from dptoffice_dbt where T_PARTYID="+MarkedId+" and T_OFFICEID="+rs.Value("T_MARKETSECTORID");
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      SQL1 = "delete from dptoffisu_dbt where T_PARTYID="+MarkedId+" and T_OFFICEID="+rs.Value("T_MARKETSECTORID");
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      SQL1 = "delete from dobjatcor_dbt where T_OBJECTTYPE=10 and T_GROUPID=1 and T_ATTRID=1 and T_OBJECT=";
      SQL1 = SQL1 + "(select lpad("+MarkedId+", 17, '0') || lpad("+rs.Value("T_MARKETSECTORID")+", 17, '0') from dual)";
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      SQL1 = "update DTXMARKSECT_DBT set t_replstate = 1 where t_MARKETSECTORID = " + rs.value("t_MARKETSECTORID");
      SQL1 = SQL1 + " and T_MARKEDID="+rs.value("t_markedid")+" and t_action=3 and t_replstate = 0";
      SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;

      SQL1 = "update DTXREPLOBJ_DBT ots set ots.T_OBJSTATE = 2 where ots.T_OBJECTTYPE=40 and ots.T_OBJECTID="+rs.value("t_markedid");
      SQL1 = SQL1 + " and T_SUBOBJNUM="+rs.Value("T_MARKETSECTORID");
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;
   end;

   return TRUE;
OnError(er)
   ЗанестиЗаписьВПротокол(601, 40, rs.Value("T_MARKEDID"), rs.Value("T_MARKETSECTORID"), StrFor(1), "Ошибка: " + er.message, rs.Value("T_INSTANCEDATE"));
   AbortTrn;
end;

macro ОбработатьСектор(Action, t_instancedate)
   var SQL, cmd, rs;

   SQL = "select * from dtxMARKSECT_DBT where t_replstate = 0 and t_action = " + Action + " and trunc(t_instancedate) = " + t_instancedate + " order by t_action, T_MARKETSECTORID";
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);
  
   while (rs.Movenext)
      RslDefCon.BeginTrans();

      if (ProcessSector(rs))
         RslDefCon.CommitTrans();
      else
         RslDefCon.RollbackTrans();
      end;
   end;

   return TRUE;
OnError(er)
   ЗанестиЗаписьВПротокол(602, 40, "", "", StrFor(1), "Ошибка: " + er.message, date(t_instancedate));
end;