/*
$Name:             txproc_partykind.mac
$Module:           БОЦБ 
$Description:      Макрос репликации видов субъектов
*/
import txrepl_func;

private var MaxVal;

macro ProcessPartyKind(rs)
   var SQL1, cmd1, rs1, Count, NameParty;

   NameParty = ПолучитьНаименованиеСубъекта(rs.value("T_PARTYID"));

   SQL1 = "SELECT T_DESTID FROM DTXREPLOBJ_DBT where T_OBJECTTYPE = 30 and T_OBJECTID = " + rs.value("T_PARTYID") + " AND T_OBJSTATE != 2";
   cmd1 = RsdCommand( SQL1);
   rs1 = RsdRecordSet(cmd1);

   if (rs1.movenext)
      count = Rs1.Value(0);
   else
      count = 0;
   end;

   if (rs.Value("T_ACTION") == 1)

      if (count == 0)
         ЗанестиЗаписьВПротокол(593, 30, rs.value("T_PARTYID"), 1, StrFor(1), "Ошибка: невозможно вставить вид несуществующему субъекту " + NameParty, rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      else
         SQL1 = "select count(*) from dpartyown_dbt where t_partyid = " + Count + " and t_partykind = " + rs.value("t_partykind");
         cmd1 = RsdCommand(SQL1);
         rs1 = RsdRecordSet(cmd1);

         rs1.Movenext;

         if (rs1.Value(0) != 0)
            ЗанестиЗаписьВПротокол(194, 30, rs.value("T_PARTYID"), 1, StrFor(1), "Предупреждение: у субъекта " + NameParty + " уже есть такой вид", rs.Value("T_INSTANCEDATE"));

            SQL1 = "update DTXPARTYKIND_DBT set t_replstate = 2 where t_partyid = " + rs.value("t_partyid") + " and t_action = 1 and t_replstate = 0";
            SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));      
            cmd1 = RsdCommand( SQL1);
            cmd1.execute;
      
            Return true;
         end;
      end;

      SQL1 = "insert into dpartyown_dbt values (";
      SQL1 = SQL1 + Count+", ";                         /*T_PARTYID     NUMBER (10)*/
      SQL1 = SQL1 + rs.Value("T_PARTYKIND")+", ";       /*T_PARTYKIND   NUMBER (5)*/
      SQL1 = SQL1 + "0, ";                              /*T_SUPERIOR    NUMBER (10)*/
      SQL1 = SQL1 + "0, ";                              /*T_SUBKIND     NUMBER (10)*/
      SQL1 = SQL1 + "0, ";                              /*T_BRANCH      NUMBER (5)*/
      SQL1 = SQL1 + "0 ";                               /*T_NUMSESSION  NUMBER (10)*/
      SQL1 = SQL1 + ")";
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      if( rs.Value("T_PARTYKIND") == 1/*PTK_CLIENT*/ )
         var Depart = 0;
         if( CB_GetParentFilial( {OperDprtNode}, Depart ) == 0 )
            SQL1 = "insert into DPTSVDP_DBT values (";
            SQL1 = SQL1 + Count+", ";                         /*T_PARTYID     NUMBER (10)*/
            SQL1 = SQL1 + rs.Value("T_PARTYKIND")+", ";       /*T_PARTYKIND   NUMBER (5)*/
            SQL1 = SQL1 + Depart;                             /*T_DEPARTMENT  NUMBER (5)*/
            SQL1 = SQL1 + ")";
            cmd1 = RsdCommand(SQL1);
            cmd1.execute;
         end;
      end;

      SQL1 = "update DTXPARTYKIND_DBT set t_replstate = 1 where t_partyid = " + rs.value("t_partyid") + " and t_action = 1 and t_replstate = 0";
      SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;
   elif (rs.Value("T_ACTION") == 3)
      if (count == 0)
         ЗанестиЗаписьВПротокол(595, 30, rs.value("T_PARTYID"), 1, StrFor(1), "Ошибка: невозможно удалить вид несуществующему субъекту " + NameParty, rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      end;

      SQL1 = "delete from dpartyown_dbt where T_PARTYID = " + count + " and T_PARTYKIND = " + rs.Value("T_PARTYKIND");
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      SQL1 = "update DTXPARTYKIND_DBT set t_replstate = 1 where t_partyid = " + rs.value("t_partyid") + " and t_action = 3 and t_replstate = 0";
      SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;
   end;

   RETURN TRUE;
OnError(er)
   ЗанестиЗаписьВПротокол(601, 30, rs.value("T_PARTYID"), 1, StrFor(1), "Ошибка: " + er.message, rs.Value("T_INSTANCEDATE"));
   AbortTrn;
end;

macro ОбработатьВидСубъекта(Action, t_instancedate, Init_Action)
   var SQL, cmd, rs, Счетчик;

   SQL = "select count(*) from DTXPARTYKIND_DBT where t_replstate = 0 and t_action = " + Action + " and trunc(t_instancedate) = " + t_instancedate;
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);

   if (rs.Movenext)
      Счетчик = rs.Value(0);
      MaxVal = rs.Value(0);
   end;

   var ProgressIndicator = CreateProgressIndicator(true);
   ProgressIndicator.Start(Int(MaxVal), "Обработка видов субъектов...");   

   Счетчик = 0;

   SQL = "select * from DTXPARTYKIND_DBT where t_replstate = 0 and t_action = " + Action + " and trunc(t_instancedate) = " + t_instancedate + " order by t_partyid, t_partykind";
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);
  
   while (rs.Movenext)
      Счетчик = Счетчик + 1;

      ProgressIndicator.Update(Int(Счетчик), MaxVal);
   
      if (RslDefCon.isintrans == false)
         RslDefCon.BeginTrans();
      end;

      if (ProcessPartyKind(rs))
         RslDefCon.CommitTrans();
      else
         RslDefCon.RollbackTrans();
      end;
   end;

   ProgressIndicator.Stop();

   return TRUE;
OnError(er)
   ЗанестиЗаписьВПротокол(601, 30, "", "", StrFor(1), "Ошибка: " + er.message, date(t_instancedate));
end;