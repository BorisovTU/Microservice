/*
$Name:             txproc_paym_fun.mac
$Module:           БОЦБ 
$Description:      Процедуры для репликации данных в БОЦБ из внешних систем
                   Процедура - ОбработатьТребование
                   обработка требований/обязательств                   
*/
IMPORT spserv, txrepl_func;

/*данные с информацие по требованию/обязательству для передачи в процедуру ОбработатьТребование*/
CLASS TDemandData
   var   T_DEMANDID:numeric;  /* идентификатор требования/обязательства*/
   var   T_DEALID:integer;    /* идентификатор операции, к которой относится требование/обязательство */
   var   T_PART:integer;      /* номер части сделки Задается в Репо, а так же для поставки ценных бумаг в займе (поставка - 1 часть, возврат - 2-я) В прочих Т/О для определенности устанавливается в 1*/
   var   T_ISFACT:string;     /* если установлен, то Т/О является фатическим (платеж), иначе - плановым. */
   var   T_KIND:integer;      /* вид Т/О: 10 - Поставка ц/б 20 - Аванс 30 -  Задаток 40 - Оплата 50 - Оплата процентов 60 - Итоговая поставка 70 - Итоговая оплата 80 - Выплата купонного дохода 90 - Частичное погашение 100 - Выплата дивидендов 110 - Выплата при изменении рыночной стоимости ц/б 120 - Поставка ц/б при изменении рыночной стоимости ц/б  80, 90, 100, 110, 120 - зарезервировано  10, 60, 120 - Т/О по ценным бумагам, остальные по деньгам  В операциях создаются следующие Т/О: - Покупка/продажа, Репо: 10, 20, 30, 40 (в Репо - по 2-м частям) - Займ: 10 (поставка и возврат), 50 - Неттинг: 60 (неттинг по ц/б), 70 (неттинг по валюте) Итоговые Т/О по неттингу создаются, если не все включенные в него Т/О по сделкам снеттингованы полностью.*/
                              /* в погашениях и выплате дивидендов, а так же в биржевых операциях Т/О не создаются.  */
                              /* в операциях при необходимости могут создаваться дополнительные Т/О 70, 80, 90, 100  */
   var   T_DIRECTION:integer; /* направление: 1 - Требование (Приход) 2 - Обязательства (Расход) Направление коррелирует с видом операции (номером части) и видом Т/О*/
   var   T_FIKIND:integer;    /* вид базового актива Т/О 10 -  Валюта (Т/О по оплате) 20 -  Ценная бумага (Т/О по поставке ценным бумагам) */
                              /* в Т/О по валюте базовым активом является ценная бумага операции. В Т/О по деньгам базовым активом является валюта операции.   */
                              /* данное поле является избыточным (оно определяется видом Т/О), однако оно  оставлено для наглядности и упрощения обработки.    */
   var   T_DATE:date;         /* дата исполнения Т/О */
   var   T_SUM:money;         /* сумма в ед. базового актива: количество ценных бумаг или сумма вкл. НКД. в валюте операции (цены), в зависимости от вида ФИ.*/
   var   T_PAYCURRENCYID:integer;   /* идентификатор валюты оплаты. В Т/О по ценным бумагам не задается. В денежных может не совпадать с валютой базового актива*/
   var   T_PAYSUM:money;      /* сумма Т/О в валюте оплаты. В  Т/О по ценным бумагам не задается. */
   var   T_PAYRATE:money;     /* курс пересчета суммы в валюте операции в сумму в валюте оплаты. Задается только для денежных Т/О, если валюты не совпадают.*/
   var   T_NETTING:integer;   /* идентификатор операции неттинга, в которой погашается данное Т/О В неттинг по ц/б могут быть включены только Т/О:по Поставке с одной ц/б В неттинг по валюте могут быть включены только денежные Т/О  с одной валютой Т/О:  -  денежных Т/О с одной валютой БА - Т/О по Доходам с одной валютой цены (валютой начисления доходов)*/
   var   T_PLANDEMEND:integer;/* идентификатор планового требования/обязательства, которое исполняется данным фактическим. Может быть указано только в фактическом Т/О.*/
   var   T_NOTE:string;       /* примечание (пользовательское примечание 101) */
   var   T_BALANCERATE:double;/* курс постановки на баланс (пользовательское примечание 102) */
   var   T_STATE:integer;     /* состояние Т/О: 1 - Не исполнено 2 - Частично исполнено 3 - Исполнено 4 - Исполнено в неттинге 5 -  Частино исполнено в неттинге 6 - Отвергнуто*/

   var   T_INSTANCEDATE:date;

   var   InTrn:bool; /*признак выполнения процедуры уже в начатой транзакции*/
   var   IsBack:bool;            /* признак платежа по второй части сделки */
   var   IsAvoirissPaym:bool;    /* признак платежа по ценным бумагам   */
   var   IsPaymentWithLot:bool;  /* признак платежа по ценным бумагам   */
   var   BofficeKind:integer;    /* вид первичного документа операции - к которой привязан платеж */
   var   OperGroup:integer;      /* группа к которой отностися операция - для определения вида операции  */   
   var   Purpose:integer;        /* назначение платежа - вид*/
   var   SubPurpose:integer;     /* назначение платежа - подвид*/
   var   LotBuy_Sale:integer;    /* вид лота */

   var   Department:integer;     /* филиал - из сделки */
   var   Oper:integer;           /* операционист - из сделки */
   var   MarketID:integer;       /* биржа - из сделки */
   var   PartyID:integer;        /* контрагент - из сделки */
   var   DealCode:string;        /* строковый код\номер сделки */
   var   BaseFIID:integer;       /* валюта актива  */
   var   PayFIID:integer;        /* валюта оплаты  */
   var   SavePaymDocID:integer;  /* ID документа, к которому привязан платеж, до выполнения процедуры репликации*/
   var   SavePaymDocKind:integer;/* Kind документа, к которому привязан платеж, до выполнения процедуры репликации */

   var   DemandExist:bool;       /* признак наличия в системе обрабатываемого платежа (требования/обязательства)*/   
   var   ExistNetting:bool;      /* признак привязки к сделке неттинга*/
   var   Action:integer;         /* выполняемое действие (1 - вставка, 2 - изменение, 3 - удаление.)*/
   var   Error:TArray;           /* массив строк с текстами ошибок*/
   var   OldDealID:numeric;      /* номер сделки в Diasoft 5 NT */
   var   OldDemandID:numeric;

   MACRO MakeError( ErrorMes:string )
      this.Error[this.Error.Size] = string(this.Error.Size) + ":" + ErrorMes;
   END;

   MACRO PrintErrors()
      var i = 0;
      while( this.Error[i] != null )
         println( this.Error[i] );
         i = i + 1;
      end;     
   END;  

   MACRO Init()
      this.InTrn           =  false;
      this.IsBack          =  false;
      this.IsAvoirissPaym  =  false;
      this.IsPaymentWithLot=  false;
      this.BofficeKind     =  0;
      this.OperGroup       =  0;
      this.Purpose         =  0;
      this.SubPurpose      =  0;
      this.LotBuy_Sale     =  PM_WRITEOFF_SUM_UNDEF;
      this.Department      =  0;
      this.Oper            =  0;
      this.MarketID        =  -1;
      this.PartyID         =  -1;
      this.DealCode        =  "";
      this.BaseFIID        =  -1;
      this.PayFIID         =  -1;
      this.SavePaymDocID   =  0;
      this.SavePaymDocKind =  0;
      this.DemandExist     =  false;
      this.Error           =  TArray;
      this.Action          =  0;
   END;

   Init();
END;

/* Функция обработки требований\обязательств */
/* Action - обязательный. IN. Integer. Выполняемое действие (1 - вставка, 2 - изменение, 3 - удаление.)  */
/* Error - необязательный. OUT. String. */
/* Функция возвращает TRUE (0) в случае успешного выполнения и FALSE (1) в случае ошибки. */

/* Функция создания, изменения и удаления требования/обязательства. Функция должна сама запускать транзакцию.*/

PRIVATE FILE   pmlink("pmlink.dbt") write; /*привязки платежа к неттингу*/
PRIVATE FILE   nett ("dl_nett.dbt"); /*сделки неттинга*/
PRIVATE FILE   rq ("dlrq.dbt")    write; /*требование/обязательство*/
PRIVATE RECORD deal ("dl_tick.dbt"); /*описание сделки*/
PRIVATE var    gDemandData   =  TDemandData;

MACRO ProcessDemand()
   var   IsWrongParms:bool = false,
         ret:bool          = true;
   var   strcmd:string     = "";
   var   sqlQuery, DataSet, DataSetDeal, DataSetLeg, LegKind:integer;
   var   SqlCorrect, CorrectSet;
   var   AvanceDate:date;
   var   OurShortName:string = "", PartyShortName:string = "", MarketShortName:string = "";
   PRIVATE var CorrectCmd;
   var payer, receiver;
 
   /*чистим используемые структуры*/
   ClearRecord( rq );

   if( (gDemandData.T_DEALID == null) OR (gDemandData.T_DEALID <= 0) )
      gDemandData.MakeError("Не найдена сделка" );
      ЗанестиЗаписьВПротокол(570, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: не найдена сделка " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
      IsWrongParms = true;
      AbortTrn;
   end;

   /*обрабатываем платежи привязанные к сделке по ценным бумагам*/
   /* общие действия для всех видов обработки: удаление,обновление,вставка */
   if( (gDemandData.T_KIND != 60) AND (gDemandData.T_KIND != 70) )
      if( (gDemandData.T_DEALID == null) OR (gDemandData.T_DEALID <= 0) )
         gDemandData.MakeError("Ошибка: не найдена сделка" );
         ЗанестиЗаписьВПротокол(570, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: не найдена сделка " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
         AbortTrn;
      end;

      if( (gDemandData.T_PART == null) OR (gDemandData.T_PART <= 0) )
         gDemandData.MakeError("Ошибка: не задан параметр T_PART - номер части сделки" );
         ЗанестиЗаписьВПротокол(569, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: не задан параметр T_PART - номер части сделки в Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
         AbortTrn;
      else
         if( gDemandData.T_PART == 1 )
            gDemandData.IsBack = false;
         else
            gDemandData.IsBack = true;
         end;
      end;

      sqlQuery =  "  SELECT  dl.T_BOfficeKind, dl.T_Department, dl.T_Oper, dl.T_MarketID, dl.T_PartyID, dl.T_DealCode, dl.T_DealType, dl.T_Flag1, dl.T_Flag4 " + 
                   "  FROM ddl_tick_dbt dl " +             
                   "  WHERE    dl.T_DEALID =  "  +  string(gDemandData.T_DEALID);
      DataSetDeal = TRsbDataSet( sqlQuery );
   
      SqlCorrect =               " SELECT deal.t_kind ";
      SqlCorrect = SqlCorrect +  "  FROM DTXDEMAND_DBT demand, DTXDEAL_DBT deal";
      SqlCorrect = SqlCorrect +  " WHERE TRUNC (demand.t_instancedate) = TRUNC (deal.t_instancedate)";
      SqlCorrect = SqlCorrect +  "   AND deal.t_replstate = 0";
      SqlCorrect = SqlCorrect +  "   AND demand.t_replstate = 0";
      SqlCorrect = SqlCorrect +  "   AND deal.t_action = 2";
      SqlCorrect = SqlCorrect +  "   AND demand.t_action = 1"; 
      SqlCorrect = SqlCorrect +  "   AND demand.t_kind in (10,40)";
      SqlCorrect = SqlCorrect +  "   AND demand.t_demandid = " + string(gDemandData.T_DEMANDID);
      SqlCorrect = SqlCorrect +  "   AND demand.t_dealid = deal.t_dealid";
   
      CorrectCmd = RsdCommand( SqlCorrect);
      CorrectSet = RsdRecordSet(CorrectCmd);
   
      if( DataSetDeal.MoveNext() != false )
         ClearRecord( deal );
   
         deal.DealType = DataSetDeal.DealType;
         deal.Flag1    = DataSetDeal.Flag1;
         deal.Flag4    = DataSetDeal.Flag4;
   
         gDemandData.OperGroup   = GetOpGroup( deal ); 
    
         if( IsRET_PARTLY(gDemandData.OperGroup) ) 
            gDemandData.LotBuy_Sale = PM_WRITEOFF_SUM_PARTIAL;
         elif( IsRET_COUPON(gDemandData.OperGroup) ) 
            gDemandData.LotBuy_Sale = PM_WRITEOFF_SUM_COUPON;
         elif( CorrectSet.MoveNext() != false)
            if( gDemandData.T_DIRECTION == 1 )          /*direction=1 - Направление платежа: требование (приход)*/
               gDemandData.LotBuy_Sale = PM_WRITEOFF_SUM_BUY;
            else                                         /*direction=2 - Направление платежа: обязательства (расход)*/
               gDemandData.LotBuy_Sale = PM_WRITEOFF_SUM_SALE;
            end;
         else 
            if( IsBUY(gDemandData.OperGroup) ) 
               if( gDemandData.IsBack == false )
                  gDemandData.LotBuy_Sale = PM_WRITEOFF_SUM_BUY;
               else
                  gDemandData.LotBuy_Sale = PM_WRITEOFF_SUM_SALE;
               end;
            else
               if( gDemandData.IsBack == false )
                  gDemandData.LotBuy_Sale = PM_WRITEOFF_SUM_SALE;
               else
                  gDemandData.LotBuy_Sale = PM_WRITEOFF_SUM_BUY;
               end;
            end;
         end;
      else
         gDemandData.MakeError("Ошибка: не найдена сделка с T_DEALID = " + string(gDemandData.T_DEALID) );
         ЗанестиЗаписьВПротокол(573, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: не найдена сделка Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
         AbortTrn;         
      end;
   end;

   /*позиционирование на обновляемых записях, для обновления и вставки*/
   if( gDemandData.Action != 3 ) /*НЕ удаление*/
      /*итоговые платежи неттинга*/
      if( (gDemandData.T_KIND == 60) OR (gDemandData.T_KIND == 70) )
         ClearRecord( nett );

         /*проверим существование операции неттинга в системе - заодно спозиционируемся на ней*/
         KeyNum( nett, 0 );
         nett.NettingID = gDemandData.T_DEALID;  

         if( GetEQ( nett ) )
            gDemandData.OperGroup   = 0; 
            gDemandData.BOfficeKind = nett.DocKind; 
            gDemandData.Department  = nett.Department;
            gDemandData.Oper        = nett.Oper;
            gDemandData.MarketID    = nett.Contractor;
            gDemandData.PartyID     = nett.Contractor;
            gDemandData.DealCode    = nett.DealNumber;
            gDemandData.BaseFIID    = nett.BaseFIID;
            gDemandData.PayFIID     = nett.PayFIID; 
            gDemandData.LotBuy_Sale = PM_WRITEOFF_SUM_UNDEF;
         else
            gDemandData.MakeError("Ошибка: не найдена сделка неттинга (ddl_nett_dbt) с T_NETTINGID = " + string(gDemandData.T_DEALID) );
            ЗанестиЗаписьВПротокол(573, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: не найдена сделка неттинга (ddl_nett_dbt) в Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
            AbortTrn;
         end;           
      /*платежи по сделкам с ц/б*/
      else
         gDemandData.BOfficeKind = DataSetDeal.BOfficeKind;
         gDemandData.Department  = DataSetDeal.Department;
         gDemandData.Oper        = DataSetDeal.Oper;
         gDemandData.MarketID    = DataSetDeal.MarketID;
         gDemandData.PartyID     = DataSetDeal.PartyID;
         gDemandData.DealCode    = DataSetDeal.DealCode;

         if( (gDemandData.IsBack == false) OR (IsLOAN(gDemandData.OperGroup)) )
            LegKind = LEG_KIND_DL_TICK;
         else
            LegKind = LEG_KIND_DL_TICK_BACK;
         end;

         sqlQuery =  "  SELECT  leg.T_PFI, leg.T_CFI " + 
                      "  FROM ddl_leg_dbt leg " +             
                      "  WHERE    leg.T_LegKind =  " +  string(LegKind) + " AND " + 
                      "           leg.T_DealID  =  " +  string(gDemandData.T_DEALID) + " AND " +
                      "           leg.T_LegID   =  0 ";
         DataSetLeg = TRsbDataSet( sqlQuery );

         if( DataSetLeg.MoveNext() == false )
            gDemandData.MakeError("Ошибка: не найдена запись с условиями сделки (ddl_leg_dbt)");
            ЗанестиЗаписьВПротокол(572, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: не найдена запись с условиями сделки (ddl_leg_dbt) в Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
            AbortTrn;                    
         else
            gDemandData.BaseFIID    = DataSetLeg.PFI;
            gDemandData.PayFIID     = DataSetLeg.CFI; 
         end;

         /*если платеж авансовый - плановый - то в сделке нужно обновить плановую дату аванса */
         if( (gDemandData.T_KIND == 20) OR (gDemandData.T_KIND == 30) )
              if( (gDemandData.T_ISFACT == null) OR (gDemandData.T_ISFACT == "") )
               AvanceDate = date(0,0,0);

               if( gDemandData.Action != 3 ) /*обновление или вставка*/
                  if( gDemandData.T_DATE != null )
                     AvanceDate = gDemandData.T_DATE;
                  end;
               end;

               strcmd  =  "  UPDATE ddl_leg_dbt leg SET leg.T_START = " + GetSQLDate(AvanceDate) + 
                            "  WHERE    leg.T_LegKind =  " +  string(LegKind) + " AND " + 
                            "           leg.T_DealID  =  " +  string(gDemandData.T_DEALID) + " AND " +
                            "           leg.T_LegID   =  0 ";
               execSQL(strcmd);
            end;
         end;
      end;
   end;

   if( gDemandData.Action != 1 ) /*Не вставка*/
      /*проверим существование требования_обязательства в системе */
      if( (gDemandData.T_DEMANDID == null) OR (gDemandData.T_DEMANDID <= 0) )
         gDemandData.MakeError("Ошибка: не найдено требование/обязательство" );
         ЗанестиЗаписьВПротокол(574, 90, gDemandData.OldDemandID, 1, StrFor(1), "Ошибка: не найдено требование/обязательство по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
         IsWrongParms = true;
      else
         KeyNum( rq, 0 );
         rq.ID = gDemandData.T_DEMANDID;
         if( GetEQ( rq ) )
            gDemandData.SavePaymDocID     =  rq.DocID;
            gDemandData.SavePaymDocKind   =  rq.DocKind;
            gDemandData.DemandExist = true;   
            if( (rq.Type == 8) OR (rq.Type == 9) )
               gDemandData.IsAvoirissPaym    =  true;
               gDemandData.IsPaymentWithLot  =  true;
            end;
         else
            gDemandData.MakeError("Ошибка: не найдено Т/О с ID = " + string(gDemandData.T_DEMANDID) );
            ЗанестиЗаписьВПротокол(576, 90, gDemandData.OldDemandID, 1, StrFor(1), "Ошибка: не найдено Т/О по сделке "+ String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
            AbortTrn;  
         end;
      end;
   end;

   /*обновление или вставка*/
   if( gDemandData.Action  != 3  )
      /*определим назначение платежа*/
      /* T_KIND:integer;      вид Т/О: 10 - Поставка ц/б 20 - Аванс 30 -  Задаток 40 - Оплата 50 - Оплата процентов 60 - Итоговая поставка 70 - Итоговая оплата 
                                       80 - Выплата купонного дохода 90 - Частичное погашение 100 - Выплата дивидендов 110 - Выплата при изменении рыночной стоимости ц/б 120 - Поставка ц/б при изменении рыночной стоимости ц/б  
                                       80, 90, 100, 110, 120 - зарезервировано  10, 60, 120 - Т/О по ценным бумагам, остальные по деньгам  
                                       В операциях создаются следующие Т/О: 
                                       - Покупка/продажа, Репо: 10, 20, 30, 40 (в Репо - по 2-м частям) 
                                       - Займ: 10 (поставка и возврат),  50 
                                       - Неттинг: 60 (неттинг по ц/б), 70 (неттинг по валюте) Итоговые Т/О по неттингу создаются, если не все включенные в него Т/О по сделкам снеттингованы полностью.
      */
      if( (gDemandData.T_KIND != null) AND (gDemandData.T_KIND > 0)  )
         gDemandData.SubPurpose = 0;  /*ниже возможно переопределится*/
         if( gDemandData.T_KIND == 10 )
            if( gDemandData.IsBack == false ) 
               gDemandData.Purpose = BAi;
            else
               gDemandData.Purpose = BRi;
            end;
            gDemandData.IsAvoirissPaym    =  true;
            gDemandData.IsPaymentWithLot  =  true;
         elif( gDemandData.T_KIND == 20  )

            if( gDemandData.IsBack == false ) 
               gDemandData.Purpose = PM_PURP_AVANCE;
            else
               gDemandData.Purpose = PM_PURP_BACK_AVANCE;
            end;
            gDemandData.SubPurpose = SP_KINDAVANCE_AVANCE;
         elif( gDemandData.T_KIND == 30 )
            if( gDemandData.IsBack == false ) 
               gDemandData.Purpose = PM_PURP_AVANCE;
            else
               gDemandData.Purpose = PM_PURP_BACK_AVANCE;
            end;
            gDemandData.SubPurpose = SP_KINDAVANCE_DEPOSIT;
         elif( gDemandData.T_KIND == 40 )
            if( gDemandData.IsBack == false ) 
               gDemandData.Purpose = CAi;
            else
               gDemandData.Purpose = CRi;
            end;
         elif( gDemandData.T_KIND == 50 )
            gDemandData.Purpose = PM_PURP_PERCENT;
         elif( (gDemandData.T_KIND == 60) OR (gDemandData.T_KIND == 70) )
            if (gDemandData.T_KIND == 60)
               gDemandData.IsAvoirissPaym = true;
            end;
            gDemandData.Purpose = PM_PURP_NETTING;
         /*дополнительные виды назначений платежей*/
         elif( gDemandData.T_KIND == 80 )
            gDemandData.Purpose = 61; /*выплата купонного дохода*/
         elif( gDemandData.T_KIND == 90 )
            gDemandData.Purpose = 62; /*частичное погашение*/
         elif( gDemandData.T_KIND == 100 )
            gDemandData.Purpose = 63; /*выплата дивидендов*/
         elif( gDemandData.T_KIND == 110 )
            gDemandData.Purpose = 66; /* компенсационный платеж, деньги */
         elif( gDemandData.T_KIND == 120 )
            gDemandData.Purpose = 67; /* компенсационная поставка, бумаги */
            gDemandData.IsAvoirissPaym    =  true;
         elif( gDemandData.T_KIND == 130 )
            gDemandData.Purpose = 64; /*  margin call. выплата при изменении рыночной стоимости ценной бумаги. пока не используется, но в меню будет.*/
         elif( gDemandData.T_KIND == 140 )
            gDemandData.Purpose = 65; /*  margin call. поставка ц/б  при изменении рыночной стоимости ценной бумаги. пока не используется, но в меню будет.*/
            gDemandData.IsAvoirissPaym = true;
         end;
      else
         gDemandData.MakeError("Ошибка: не задан параметр T_KIND - вид требования//обязательства" );
         ЗанестиЗаписьВПротокол(577, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: не задан параметр T_KIND - вид требования/обязательства в Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
         AbortTrn;
      end;

      /*вставка - зануляем структуры*/
      if( gDemandData.Action == 1 ) 
         ClearRecord( rq );
      end;

      /* заполним ключевые поля для вставки или обновления  */
      if (gDemandData.Purpose == 1)
         rq.type      =  8;
         rq.subkind   =  1;
         rq.dealpart  =  1;
      elif (gDemandData.Purpose == 3)
         rq.type      =  8;
         rq.subkind   =  1;
         rq.dealpart  =  2;
      elif (gDemandData.Purpose == 4)
         rq.type      =  2;
         rq.dealpart  =  2;
      elif (gDemandData.Purpose == 2)
         rq.type      =  2;
         rq.dealpart  =  1;
      elif (gDemandData.Purpose == 11)
         rq.type      =  3;
         rq.dealpart  =  2;
      elif (gDemandData.Purpose == 40)
         rq.type      =  6;
         rq.dealpart  =  1;
      elif (gDemandData.Purpose == 67)
         rq.type      =  9;
         rq.subkind   =  1;
         rq.dealpart  =  2;
      elif (gDemandData.Purpose == 42)
         rq.type      =  0;
         rq.dealpart  =  1;
      elif (gDemandData.Purpose == 43)
         rq.type      =  0;
         rq.dealpart  =  2;
      elif (gDemandData.Purpose == 61)
         rq.type      =  4;
         rq.dealpart  =  2;
      elif (gDemandData.Purpose == 62)
         rq.type      =  5;
         rq.dealpart  =  2;
      elif (gDemandData.Purpose == 64)
         rq.type      =  9;
         rq.subkind   =  1;
         rq.dealpart  =  2;
      elif (gDemandData.Purpose == 65)
         rq.type      =  7;
         rq.dealpart  =  2;
      else
         rq.type      =  0;
         rq.dealpart  =  1;
      end;
      rq.dealpart  = gDemandData.T_PART;
      rq.DocKind   =  gDemandData.BOfficeKind;
      rq.DocID     =  gDemandData.T_DEALID;             
      //rq.Num       =  gDemandData.SubPurpose; // !?
   end;

   if( IsWrongParms == false )
      if( gDemandData.Action  == 1  )  /* вставка   */
         rq.ID = 0;
      elif( gDemandData.Action  == 2  )  /* изменение */
         if( gDemandData.DemandExist == false )
            gDemandData.MakeError("Ошибка: требование//обязательство не найдено в целевой системе");
            ЗанестиЗаписьВПротокол(424, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: требование/обязательство по сделке " + String(gDemandData.OldDealID:11:0) + " не найдено в целевой системе", gDemandData.T_INSTANCEDATE);
            AbortTrn;
         end;
      elif( gDemandData.Action  == 3  )  /* удаление  */
         if( gDemandData.DemandExist == false )
            gDemandData.MakeError("Ошибка: требование//обязательство уже удалено в целевой системе");
            ЗанестиЗаписьВПротокол(425, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: требование/обязательство " + String(gDemandData.OldDealID:11:0) + " уже удалено в целевой системе", gDemandData.T_INSTANCEDATE);
            AbortTrn;        
         end;
      else
         gDemandData.MakeError( "Ошибка: неверный вид выполняемого действия: " + string( gDemandData.Action ) ); 
         ЗанестиЗаписьВПротокол(578, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: неверный вид выполняемого действия в Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
         AbortTrn;
      end;
   end;

   /*инициализация данных операции для вставки\изменения*/
   if( (IsWrongParms == false) AND ((gDemandData.Action == 1) OR (gDemandData.Action == 2)) )
      rq.State = 0;

      if( (gDemandData.T_NETTING != NULL) AND (gDemandData.T_NETTING > 0) )
         rq.State = 2;
         rq.NETTING = SET_CHAR;
      else
         if (gDemandData.T_ISFACT == StrFor(88))
            rq.State = 2;
         end;
      end;

      if( (gDemandData.T_DATE != null) )
         if (gDemandData.T_ISFACT == StrFor(88))
            rq.FactDate      = gDemandData.T_DATE;
         end;
         rq.PlanDate      = gDemandData.T_DATE;
         rq.ChangeDate    = gDemandData.T_DATE;
      end;

      if( (gDemandData.T_SUM != null ) )
         rq.Amount = gDemandData.T_SUM;
      else
         gDemandData.MakeError("Ошибка: не задан параметр T_SUM - сумма обязательства//требования в единицах базового актива" );
         ЗанестиЗаписьВПротокол(580, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: не задан параметр T_SUM - сумма обязательства/требования в единицах базового актива по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
         IsWrongParms = true;
      end;

      /*определяем валюту базового актива по данным из условий сделки*/        
      if( gDemandData.IsAvoirissPaym )
         rq.FIID = gDemandData.BaseFIID;
      else
         rq.FIID = gDemandData.PayFIID;
      end;

      var SQL1 = "select t_fi_kind-1 as t_fi_kind from dfininstr_dbt where t_fiid = " + rq.FIID;
      var cmd1 = RsdCommand(SQL1);
      var rs1 = RsdRecordSet(cmd1);

      if (rs1.movenext)
         rq.SubKind = Rs1.Value(0); // для денег 0, для бумаг 1
      end;

      if( gDemandData.T_DIRECTION != null )
         Payer      = {OurBank};
         Receiver   = {OurBank};
         OurShortName = ПолучитьКороткоеИмяСубъекта( {OurBank} );

         /*заполняем реквизиты плательщика получателя банка*/
         if( IsEXCHANGE(gDemandData.OperGroup) )
            if( gDemandData.MarketID <= 0 ) /*предупредим*/
               gDemandData.MakeError("Предупреждение: в биржевой сделке " + string(gDemandData.DealCode) + " не найдена биржа" );
               MarketShortName = "";
               ЗанестиЗаписьВПротокол(581, 80, gDemandData.T_DEMANDID, 1, StrFor(1), "Предупреждение: в биржевой сделке не найдена биржа, Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
            else
               MarketShortName = ПолучитьКороткоеИмяСубъекта( gDemandData.MarketID );
            end;

            if( gDemandData.T_DIRECTION == 1 )   
               Payer           =  gDemandData.MarketID;
            else
               Receiver        =  gDemandData.MarketID;
            end;
         else
            if( gDemandData.PartyID <= 0 ) /*предупредим*/
               gDemandData.MakeError("Предупреждение: во внебиржевой сделке " + string(gDemandData.DealCode) + " не найден контрагент" );
               ЗанестиЗаписьВПротокол(582, 80, gDemandData.T_DEMANDID, 1, StrFor(1), "Предупреждение: во внебиржевой сделке не найден контрагент, Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
               PartyShortName = "";
            else
               PartyShortName = ПолучитьКороткоеИмяСубъекта( gDemandData.PartyID );
            end;

            if( gDemandData.T_DIRECTION == 1 )   
               Payer           =  gDemandData.PartyID;
            else
               Receiver        =  gDemandData.PartyID;
            end;
         end;
      else
         gDemandData.MakeError("Ошибка: не задан параметр T_DIRECTION - направление обязательства//требования" );
         ЗанестиЗаписьВПротокол(583, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: Не задан параметр T_DIRECTION - направление обязательства/требования по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
         AbortTrn;
      end;

      /*обработка параметров специфичных для денежных платежей*/
      if( gDemandData.IsAvoirissPaym != true )
         if( (gDemandData.T_PAYCURRENCYID != null) and (gDemandData.T_PAYCURRENCYID >= 0)  )
            if( gDemandData.T_PAYSUM != null )
               rq.Amount  =  gDemandData.T_PAYSUM; 
               rq.FIID    =  gDemandData.T_PAYCURRENCYID;
            else
           	   gDemandData.MakeError("Ошибка: не задан параметр T_PAYSUM - сумма оплаты обязательства//требования" );
		       ЗанестиЗаписьВПротокол(584, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: не задан параметр T_PAYSUM - сумма оплаты обязательства/требования по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
		       IsWrongParms = true;
            end;
         else                          
            gDemandData.MakeError("Ошибка: не найдена валюта оплаты обязательства//требования" );
            ЗанестиЗаписьВПротокол(585, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: не найдена валюта оплаты обязательства/требования по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
            IsWrongParms = true;         
         end;
      end;

      if( payer == 1 )
         rq.Kind = 1; // Обязательство
      else
         rq.Kind = 0;
      end;
      rq.Party         = payer + receiver - 1;
      rq.ID_Step       = 2908;
      rq.RqAccID       = -1;
      rq.PlaceID       = -1;
      rq.Instance      = 0;
      rq.Action        = 0;
      rq.ID_Operation  = 0;
      rq.Source        = 0;
      rq.SourceObjKind = -1;
      rq.SourceObjID   = 0;
   end;

   /*некорректные входящие параметры - прерываем транзакцию*/
   if( IsWrongParms == true )
      AbortTrn;
   end;

   if( gDemandData.Action == 1  )  /* вставка   */
      if( Insert( rq ) == false )
         gDemandData.MakeError( "Ошибка: ошибка при вставке Т/О в DDLRQ_DBT" );
         ЗанестиЗаписьВПротокол(683, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: ошибка при вставке в DDLRQ_DBT, Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
         AbortTrn;
      end;
      /*для возврата ID вставленного Т/О*/
      gDemandData.T_DEMANDID = rq.ID;
   elif( gDemandData.Action  == 2  )  /* изменение */
      if( Update( rq ) == false )
         gDemandData.MakeError( "Ошибка: ошибка при обновлении платежа в DDLRQ_DBT" );
         ЗанестиЗаписьВПротокол(688, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: ошибка при обновлении Т/О в DDLRQ_DBT, Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
         AbortTrn;
      end;
   elif( gDemandData.Action  == 3  )  /* удаление  */
      /*удалим примечания к платежу*/
      strcmd =  "delete from dnotetext_dbt note where note.t_ObjectType = " + string(993) + " and note.t_DocumentID = '" + string(UniID(rq, 993)) + "'";
      execSQL(strcmd);

      /*удалим все привязки к платежу*/
      strcmd =  "delete from dpmlink_dbt pmlink where pmlink.t_InitialPayment = " + string(rq.ID) + " OR pmlink.t_PurposePayment = " + string(rq.ID);
      execSQL(strcmd);
      
      if( Delete( rq ) == false )
         gDemandData.MakeError( "Ошибка: ошибка при удалении Т/О в DDLRQ_DBT" );
         ЗанестиЗаписьВПротокол(691, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: ошибка при удалении Т/О в DDLRQ_DBT, Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
         AbortTrn;
      end;
   else
      gDemandData.MakeError( "Ошибка: неверный вид выполняемого действия: " + string( gDemandData.Action ) ); 
      ЗанестиЗаписьВПротокол(578, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: неверный вид выполняемого действия, Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
      AbortTrn;
   end;

   /*общие действия для вставки или обновления - выполняемые после вставки\обновления данных в таблицу*/
   if( (gDemandData.Action == 1) OR (gDemandData.Action == 2) )
      /*добавим\обновим примечание*/
      if( (gDemandData.T_NOTE != null) and (gDemandData.T_NOTE != StrFor(1)) )
	removeNoteForObject( 993, UniID(rq, 993), 101);
        if( addNoteForObject( 993, UniID(rq, 993),
                              101 /*примечание по платежу*/, gDemandData.T_NOTE, rq.FactDate ) )
           gDemandData.MakeError( "Предупреждение: ошибка при добавлении пользовательского примечания вида 101 - \"примечание по платежу\" по платежу" );
           ЗанестиЗаписьВПротокол(693, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Предупреждение: ошибка при добавлении пользовательского примечания вида 101 - 'Примечание по платежу' по платежу, Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
        end; 
      end;

      /*добавим\обновим примечание*/
      if(( gDemandData.T_BALANCERATE != null ) and ( gDemandData.T_BALANCERATE > 0 ))
	removeNoteForObject( 993, UniID(rq, 993), 44);
        if( addNoteForObject( 993, UniID(rq, 993),
                              44 /*курс постановки на баланс*/, gDemandData.T_BALANCERATE, rq.FactDate ) )
           gDemandData.MakeError( "Предупреждение: ошибка при добавлении примечания вида 44 - \"курс постановки на баланс\" по платежу" );
           ЗанестиЗаписьВПротокол(694, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Предупреждение: ошибка при добавлении примечания вида 44 - 'Курс постановки на баланс' по платежу, Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
        end; 
      end;

      /*проверим наличие привязки к переданному неттингу*/
      if( (gDemandData.T_NETTING != null) and (gDemandData.T_NETTING > 0) )
         ClearRecord( nett );
         KeyNum(nett,0);
         nett.NettingID = gDemandData.T_NETTING;
         if( GetEQ(nett) == false )
            gDemandData.MakeError( "Ошибка: не найдена сделка неттинга c NettingID = " + string(gDemandData.T_NETTING) + ", задано в параметре T_NETTING"  );            
            ЗанестиЗаписьВПротокол(587, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Ошибка: не найдена сделка неттинга, задано в параметре T_NETTING, Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
            AbortTrn; 
         end;

         KeyNum( pmlink, 1 ); 
         ClearRecord(pmlink);
         pmlink.DocKind       =  DL_NTGDOC;
         pmlink.DocumentID    =  gDemandData.T_NETTING;
         pmlink.LinkKind      =  /*PMLINK_KIND_RQNETTING = */12;
         pmlink.InitialPayment=  rq.ID;

         if( GetEQ(pmlink) == false )
            /*нужно добавить*/
            ClearRecord(pmlink);
            pmlink.DocKind       =  DL_NTGDOC;               
            pmlink.DocumentID    =  gDemandData.T_NETTING;
            pmlink.LinkKind      =  /*PMLINK_KIND_RQNETTING = */12;
            pmlink.InitialPayment=  rq.ID;
            pmlink.FIID          =  rq.FIID;
            pmlink.Amount        =  rq.Amount;
            if( Insert(pmlink) == false )
               gDemandData.MakeError( "Предупреждение: ошибка при добавлении привязки платежа к неттингу (dpmlink_dbt)" );
               ЗанестиЗаписьВПротокол(695, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Предупреждение: ошибка при добавлении привязки платежа к неттингу (dpmlink_dbt), Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
            end;
         else
            /*обновить*/
            pmlink.FIID          =  rq.FIID;
            pmlink.Amount        =  rq.Amount;
            if( Update(pmlink) == false )
               gDemandData.MakeError( "Предупреждение: ошибка при обновлении привязки платежа к неттингу (dpmlink_dbt)" );
               ЗанестиЗаписьВПротокол(696, 90, gDemandData.T_DEMANDID, 1, StrFor(1), "Предупреждение: ошибка при обновлении привязки платежа к неттингу (dpmlink_dbt), Т/О по сделке " + String(gDemandData.OldDealID:11:0), gDemandData.T_INSTANCEDATE);
            end;
         end;
      else
         /*удалим привязки к неттингу*/
         strcmd =  "delete from dpmlink_dbt pmlink where pmlink.t_LinkKind = " + string(/*PMLINK_KIND_RQNETTING = */12) + " and pmlink.t_InitialPayment = " + string(rq.ID);
         execSQL(strcmd);
      end;
   end;
END;

MACRO ОбработатьТребование( DemandData:TDemandData, Action:integer, Error:TArray ) : BOOL
   var   ret:bool = true;

   gDemandData         = DemandData;
   gDemandData.Action  = Action;

   if( (Error != null) and (valtype(Error) == V_GENOBJ) )
      gDemandData.Error   = Error;
   end;

   if( gDemandData.InTrn == false )
      if (ProcessTrn(0, "ProcessDemand", rq, pmlink ) == false )
         ret = false;
      end;
   else
      ProcessDemand();
   end;

   return ret;
END;
