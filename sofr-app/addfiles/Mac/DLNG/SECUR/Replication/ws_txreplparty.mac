/*
$Name:             ws_txreplparty.mac
$Module:           АРНУ
$Description:      WEB. АРНУ. Макрос сервисов скроллинга и виджета субъектов для репликации
*/

import FIInter;
import "ws_common.mac";
import "ws_orderbygen.mac";
import "ws_datafilter.mac";
import "commonutil.mac";

macro TxReplPartyRunError(err:Variant, stat:Integer)
  WSRunError("ws_txreplparty.mac", err, stat);
end;

// Класс скроллинга субъектов для репликации
class CTxReplPartyList()
  var PartyID:String;        // Идентификатор
  var InstanceDate:DateTime; // Дата загрузки
  var Action:Integer;        // Действие
  var ActionName:String;     // Наименование действия
  var ReplState:Integer;     // Статус репликации
  var ReplStateName:String;  // Статус репликации
  var LegalForm:Integer;     // Форма
  var LegalFormName:String;  // Форма
  var Name1:String;          // Фамилия
  var Name2:String;          // Имя
  var Name3:String;          // Отчество
  var ShortName:String;      // Краткое наименование
  var FullName:String;       // Полное наименование
  var NotResident:Bool;      // Признак нерезидента
  var NrCountry:String;      // Страна
  var Address_Ur:String;     // Адрес юридический
  var Address_Post:String;   // Адрес почтовый
  var OKPO:String;           // ОКПО
  var INN:String;            // ИНН
  var BIC:String;            // БИК
  var Contract:String;       // Номер договора
  var ContractDate:Date;     // Дата договора
  var Version:Integer;       // Версия записи
end;

class TWsReplPartyListItem()
  var PartyID:String;
  var InstanceDate:DateTime;
  var ShortName:String;
  var BIC:String;
  var INN:String;
end;

class TWsReplPartyFltrParm()
  var FindPartyID:String;
  var LikeShortName:String;
  var PartyKind:Integer;
  var PartyShortName:String;
end;

private macro GetReplPartyAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String
  var fp = null;

  fp = FilterParser(FilterItems);

  fp.AddFieldCondition(0, "txparty", "t_InstanceDate");      // Записи за период
  fp.AddFieldCondition(1, "txparty", "t_ReplState");         // Статус записи
  fp.AddFieldCondition(2, "txparty", "t_Action");            // Вид действия
  fp.AddFieldCondition(3, "txparty", "t_PartyID", " = -1 "); // Идентификатор записи
  fp.AddFieldCondition(4, "txparty", "t_LegalForm");         // Форма
  fp.AddFieldCondition(5, "txparty", "t_NotResident");       // Признак нерезидента
  fp.AddFieldCondition(6, "txparty", "t_ShortName");         // Краткое наименование
  fp.AddFieldCondition(7, "txparty", "t_OKPO");              // ОКПО
  fp.AddFieldCondition(8, "txparty", "t_INN");               // ИНН
  fp.AddFieldCondition(9, "txparty", "t_BIC");               // БИК
  fp.AddFieldCondition(10, "txparty", "t_Contract");         // Номер договора

  return fp.GetFullSQLCondition();
end;

private macro GetReplPartySortOrder(
  OrderItems//:TArray of OrderItem // Параметры сортировки
 ,DefaultSQLSortOrder:String       // Строка сортировки по умолчанию
):String
  var ordrGen = null;

  ordrGen = OrderByGenerator(OrderItems, DefaultSQLSortOrder, false, true);

  return ordrGen.GetFullSQLSortOrder();
end;

// Получить общее количество записей скроллинга Ц/Б
macro TxGetReplPartyCount(
  FltrParm//:TWsReplAvoirFltrParm             // Значения фильтрации
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
):Integer
  var Query = "", AddWhere = "";
  var Count = 0;

  Query = " SELECT "
            + " 1 "
        + " FROM "
            + " dtxparty_dbt txparty "
        + " WHERE "
            + " 1 = 1 ";

  if(FltrParm != null)
    if((FltrParm.FindPartyID != null) and (FltrParm.FindPartyID != ""))
      Query = Query + " AND txparty.t_PartyID = " + FltrParm.FindPartyID + " ";
    end;

    if(FltrParm.LikeShortName != null)
      Query = Query + " AND LOWER(txparty.t_ShortName) LIKE LOWER('%" + FltrParm.LikeShortName + "%') ";
    end;

    if(FltrParm.PartyKind != null)
      Query = Query + " AND EXISTS (SELECT "
                                    + " 1 "
                                + " FROM "
                                    + " dtxpartykind_dbt txpartykind "
                                + " WHERE "
                                    + " txpartykind.t_PartyID = txparty.t_PartyID "
                                + " AND txpartykind.t_PartyKind = " + FltrParm.PartyKind + ") ";
    end;

    if(FltrParm.PartyShortName != null)
      Query = Query + " AND txparty.t_ShortName = '" + FltrParm.PartyShortName + "' ";
    end;
  end;

  AddWhere = GetReplPartyAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  Count = SQL_GetNRecs(Query);

  return Count;

OnError(err)
  TxReplPartyRunError(err, -1);
end;

private class (RsdRecordset) CReplPartySet(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FltrParm//:TWsReplPartyFltrParm             // Значения фильтрации
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
)
  private var m_Query = "", m_AddWhere = "", m_OrderBy = "", m_Cmd = null;

  macro GetSortOrder(
    OrderItems//:TArray of OrderItem // Параметры сортировки
  ):String
    return GetReplPartySortOrder(OrderItems, " txparty.t_PartyID ASC, txparty.t_InstanceDate ASC, txparty.t_Action ASC, txparty.t_ReplState ASC ");
  end;

  if(PageNum == null)
    RunError("Не задан обязательный параметр функции: номер страницы");
  end;

  if(PageSize == null)
    RunError("Не задан обязательный параметр функции: размер страницы");
  end;

  m_Query = " SELECT "
              + " TO_CHAR(txparty.t_PartyID) AS t_PartyID "
             + " ,txparty.t_InstanceDate "
             + " ,txparty.t_Action "
             + " ,NVL((SELECT "
                       + " namealg.t_szNameAlg "
                   + " FROM "
                       + " dnamealg_dbt namealg "
                   + " WHERE "
                       + " namealg.t_iTypeAlg = 7643 "
                   + " AND namealg.t_iNumberAlg = txparty.t_Action), CHR(0)) AS t_ActionName "
             + " ,txparty.t_ReplState "
             + " ,NVL((SELECT "
                       + " namealg.t_szNameAlg "
                   + " FROM "
                       + " dnamealg_dbt namealg "
                   + " WHERE "
                       + " namealg.t_iTypeAlg = 7642 "
                   + " AND namealg.t_iNumberAlg = txparty.t_ReplState), CHR(0)) AS t_ReplStateName "
             + " ,txparty.t_LegalForm "
             + " ,DECODE(txparty.t_LegalForm, 1, 'Юр. лицо', 2, 'Физ. лицо', CHR(0)) AS t_LegalFormName "
             + " ,txparty.t_Name1 "
             + " ,txparty.t_Name2 "
             + " ,txparty.t_Name3 "
             + " ,txparty.t_ShortName "
             + " ,txparty.t_FullName "
             + " ,txparty.t_NotResident "
             + " ,txparty.t_NrCountry "
             + " ,txparty.t_Address_Ur "
             + " ,txparty.t_Address_Post "
             + " ,txparty.t_OKPO "
             + " ,txparty.t_INN "
             + " ,txparty.t_BIC "
             + " ,txparty.t_Contract "
             + " ,txparty.t_ContractDate "
             + " ,txparty.t_Version "
          + " FROM "
              + " dtxparty_dbt txparty "
          + " WHERE "
              + " 1 = 1 ";

  if(FltrParm != null)
    if((FltrParm.FindPartyID != null) and (FltrParm.FindPartyID != ""))
      m_Query = m_Query + " AND txparty.t_PartyID = " + FltrParm.FindPartyID + " ";
    end;

    if(FltrParm.LikeShortName != null)
      m_Query = m_Query + " AND LOWER(txparty.t_ShortName) LIKE LOWER('%" + FltrParm.LikeShortName + "%') ";
    end;

    if(FltrParm.PartyKind != null)
      m_Query = m_Query + " AND EXISTS (SELECT "
                                        + " 1 "
                                    + " FROM "
                                        + " dtxpartykind_dbt txpartykind "
                                    + " WHERE "
                                        + " txpartykind.t_PartyID = txparty.t_PartyID "
                                    + " AND txpartykind.t_PartyKind = " + FltrParm.PartyKind + ") ";
    end;

    if(FltrParm.PartyShortName != null)
      m_Query = m_Query + " AND txparty.t_ShortName = '" + FltrParm.PartyShortName + "' ";
    end;
  end;

  m_AddWhere = GetReplPartyAddWhereCondition(FilterItems);
  if(m_AddWhere != "")
    m_Query = m_Query + " AND " + m_AddWhere;
  end;

  m_OrderBy = GetSortOrder(OrderItems);
  if(m_OrderBy != "")
    m_Query = m_Query + " ORDER BY " + m_OrderBy;
  end;

  m_Query = SQL_WrapSelect(m_Query, PageNum, PageSize);

  m_Cmd = RSDCommand(m_Query);
  m_Cmd.NullConversion = true;
  m_Cmd.Execute();

  initRsdRecordset(m_Cmd);
end;

// Отбор данных для скроллинга Ц/Б
macro TxGetReplPartyList(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FltrParm//:TWsReplPartyFltrParm             // Значения фильтрации
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
)
  var ReplPartySet = null;
  var ReplParty = null;
  var ReplPartyList = TArray();

  ReplPartySet = CReplPartySet(PageSize, PageNum, FltrParm, FilterItems, OrderItems);

  while(ReplPartySet.MoveNext())
    ReplParty = CTxReplPartyList();

    ReplParty.PartyID = SQL_ConvTypeStr(ReplPartySet.Value("t_PartyID"));
    ReplParty.InstanceDate = SQL_ConvTypeDateTime(ReplPartySet.Value("t_InstanceDate"));
    ReplParty.Action = SQL_ConvTypeInteger(ReplPartySet.Value("t_Action"));
    ReplParty.ActionName = SQL_ConvTypeStr(ReplPartySet.Value("t_ActionName"));
    ReplParty.ReplState = SQL_ConvTypeInteger(ReplPartySet.Value("t_ReplState"));
    ReplParty.ReplStateName = SQL_ConvTypeStr(ReplPartySet.Value("t_ReplStateName"));
    ReplParty.LegalForm = SQL_ConvTypeInteger(ReplPartySet.Value("t_LegalForm"));
    ReplParty.LegalFormName = SQL_ConvTypeStr(ReplPartySet.Value("t_LegalFormName"));
    ReplParty.Name1 = SQL_ConvTypeStr(ReplPartySet.Value("t_Name1"));
    ReplParty.Name2 = SQL_ConvTypeStr(ReplPartySet.Value("t_Name2"));
    ReplParty.Name3 = SQL_ConvTypeStr(ReplPartySet.Value("t_Name3"));
    ReplParty.ShortName = SQL_ConvTypeStr(ReplPartySet.Value("t_ShortName"));
    ReplParty.FullName = SQL_ConvTypeStr(ReplPartySet.Value("t_FullName"));
    ReplParty.NotResident = (SQL_ConvTypeStr(ReplPartySet.Value("t_NotResident")) == SET_CHR);
    ReplParty.NrCountry = SQL_ConvTypeStr(ReplPartySet.Value("t_NrCountry"));
    ReplParty.Address_Ur = SQL_ConvTypeStr(ReplPartySet.Value("t_Address_Ur"));
    ReplParty.Address_Post = SQL_ConvTypeStr(ReplPartySet.Value("t_Address_Post"));
    ReplParty.OKPO = SQL_ConvTypeStr(ReplPartySet.Value("t_OKPO"));
    ReplParty.INN = SQL_ConvTypeStr(ReplPartySet.Value("t_INN"));
    ReplParty.BIC = SQL_ConvTypeStr(ReplPartySet.Value("t_BIC"));
    ReplParty.Contract = SQL_ConvTypeStr(ReplPartySet.Value("t_Contract"));
    ReplParty.ContractDate = SQL_ConvTypeDate(ReplPartySet.Value("t_ContractDate"));
    ReplParty.Version = SQL_ConvTypeInteger(ReplPartySet.Value("t_Version"));

    ReplPartyList[ReplPartyList.Size] = ReplParty;
  end;

  return ReplPartyList;

OnError(err)
  TxReplPartyRunError(err, -1);
end;

private macro CheckVersion(
  PartyID:String
 ,InstanceDate:DateTime
 ,Version:Integer
)
  var err = 0;
  var Query = "", Cmd = null, Set = null;

  Query = " SELECT "
            + " * "
        + " FROM "
            + " dtxparty_dbt "
        + " WHERE "
            + " t_PartyID = ? "
        + " AND t_InstanceDate = ? "
        + " AND t_Version = ? "
        + " FOR UPDATE ";

  Cmd = RsdCommand(Query);
  Cmd.AddParam("", RSDBP_IN, PartyID);
  Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(InstanceDate));
  Cmd.AddParam("", RSDBP_IN, Version);
  Cmd.Execute();

  Set = RsdRecordset(Cmd);
  if(not Set.MoveNext())
    err = 70;
  end;

  return err;
end;

private macro UpdateTxPartyImpl(
  StateAndAction//:CTxReplStateAndAction
 ,Party//:CTxReplPartyList
)
  var err = 0;
  var Query = "", Cmd = null;

  err = CheckVersion(Party.PartyID, Party.InstanceDate, Party.Version);

  if(err == 0)
    Query = " UPDATE "
              + " dtxparty_dbt "
          + " SET ";
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Query = Query +
                " t_ReplState = ? "
             + " ,t_Action = ? ";
    elif(StateAndAction.NeedUpdateReplState)
      Query = Query +
                " t_ReplState = ? ";
    else
      Query = Query +
                " t_Action = ? ";
    end;
    Query = Query +
            " WHERE "
              + " t_PartyID = ? "
          + " AND t_InstanceDate = ? ";

    Cmd = RsdCommand(Query);
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    elif(StateAndAction.NeedUpdateReplState)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
    else
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    end;
    Cmd.AddParam("", RSDBP_IN, Party.PartyID);
    Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(Party.InstanceDate));
    Cmd.Execute();
  end;

  return err;
end;

macro TxUpdatePartyList(
  StateAndAction//:CTxReplStateAndAction
 ,PartyList//:TArray of CTxReplPartyList
)
  var err = 0;
  var i = 0;

  if((StateAndAction.NeedUpdateReplState
   or StateAndAction.NeedUpdateAction)
 and (PartyList != null))
    while((err == 0) and (i < PartyList.Size))
      err = UpdateTxPartyImpl(StateAndAction, PartyList[i]);
      i = i + 1;
    end;
  end;

  return err;

OnError(err)
  TxReplPartyRunError(err, -1);
end;

macro TxGetPartyListItemCount(
  FltrParm
 ,FilterItems
):Integer
  return TxGetReplPartyCount(FltrParm, FilterItems);
end;

private class (CReplPartySet) CReplPartyListSet(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FltrParm//:TWsReplPartyFltrParm             // Значения фильтрации
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
)
  macro GetSortOrder(
    OrderItems//:TArray of OrderItem // Параметры сортировки
  ):String
    return GetReplPartySortOrder(OrderItems, " txparty.t_ShortName ASC ");
  end;

  initCReplPartySet(PageSize, PageNum, FltrParm, FilterItems, OrderItems);
end;

macro TxGetPartyListItems(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FltrParm//:TWsReplPartyFltrParm             // Значения фильтрации
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
)
  var ReplPartyListSet = null;
  var ReplPartyListItem = null;
  var ReplPartyList = TArray();

  ReplPartyListSet = CReplPartyListSet(PageSize, PageNum, FltrParm, FilterItems, OrderItems);

  while(ReplPartyListSet.MoveNext())
    ReplPartyListItem = TWsReplPartyListItem();

    ReplPartyListItem.PartyID = SQL_ConvTypeStr(ReplPartyListSet.Value("t_PartyID"));
    ReplPartyListItem.InstanceDate = SQL_ConvTypeDateTime(ReplPartyListSet.Value("t_InstanceDate"));
    ReplPartyListItem.ShortName = SQL_ConvTypeStr(ReplPartyListSet.Value("t_ShortName"));
    ReplPartyListItem.INN = SQL_ConvTypeStr(ReplPartyListSet.Value("t_INN"));
    ReplPartyListItem.BIC = SQL_ConvTypeStr(ReplPartyListSet.Value("t_BIC"));

    ReplPartyList[ReplPartyList.Size] = ReplPartyListItem;
  end;

  return ReplPartyList;

OnError(err)
  TxReplPartyRunError(err, -1);
end;

///////////////////////////////////////////////////////////
// Возвращает список длинны listLen по
// частичному совпадению значения
///////////////////////////////////////////////////////////
macro LookupReplParty(
  FltrParm
 ,ListLen
):TArray
  return TxGetPartyListItems(ListLen, 0, FltrParm, null, null);
end;
