/*
$Name:             ws_txreplavrcode.mac
$Module:           АРНУ
$Description:      WEB. АРНУ. Макрос сервисов скроллинга субъектов для репликации
*/
import FIInter;
import "ws_common.mac";
import "ws_datafilter.mac";
import "ws_orderbygen.mac";

macro TxReplAvrcodeRunError(err:Variant, stat:Integer)
  WSRunError("ws_txreplavrcode.mac", err, stat);
end;

// Класс скроллинга кодов Ц/Б для репликации
class CTxReplAvrcodeList()
  var AvoirissID:String;     // Идентификатор
  var InstanceDate:DateTime; // Дата загрузки
  var Action:Integer;        // Действие
  var ActionName:String;     // Наименование действия
  var ReplState:Integer;     // Статус репликации
  var ReplStateName:String;  // Статус репликации
  var MarketID:String;       // Биржа
  var MarketName:String;     // Биржа
  var MarketCode:String;     // Код биржи
  var Version:Integer;       // Версия записи
end;

private macro GetSQLMarketIDCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLPartyConditionEx("txavrcode", "t_MarketID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txavrcode.t_MarketID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetReplAvrcodeAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String
  var fp = null;

  fp = FilterParser(FilterItems);

  fp.AddFieldCondition(0, "txavrcode", "t_InstanceDate");        // Записи за период
  fp.AddFieldCondition(1, "txavrcode", "t_ReplState");           // Статус записи
  fp.AddFieldCondition(2, "txavrcode", "t_Action");              // Вид действия
  fp.AddFieldCondition(3, "txavrcode", "t_AvoirissID", " = -1"); // Идентификатор записи
  fp.AddUserFieldCondition(4, @GetSQLMarketIDCondition);         // Биржа

  return fp.GetFullSQLCondition();
end;

private macro GetReplAvrcodeSortOrder(
  OrderItems//:TArray of OrderItem // Параметры сортировки
 ,DefaultSQLSortOrder:String       // Строка сортировки по умолчанию
):String
  var ordrGen = null;

  ordrGen = OrderByGenerator(OrderItems, DefaultSQLSortOrder, false, true);

  return ordrGen.GetFullSQLSortOrder();
end;

// Получить общее количество записей скроллинга Ц/Б
macro TxGetReplAvrcodeCount(
  FilterItems//:TArray of FilterCondItem // Массив элементов фильтра скроллинга
):Integer
  var Query = "", AddWhere = "";
  var Count = 0;

  Query = " SELECT "
            + " 1 "
        + " FROM "
            + " dtxavrcode_dbt txavrcode "
        + " WHERE "
            + " 1 = 1 ";

  AddWhere = GetReplAvrcodeAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  Count = SQL_GetNRecs(Query);

  return Count;

OnError(err)
  TxReplAvrcodeRunError(err, -1);
end;

// Отбор данных для скроллинга Ц/Б
macro TxGetReplAvrcodeList(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
):TArray
  var Query = "", AddWhere = "", OrderBy = "", Cmd = null, Set = null;
  var ReplAvrcode = null;
  var ReplAvrcodeList = TArray();

  if(PageNum == null)
    RunError("Не задан обязательный параметр функции: номер страницы");
  end;

  if(PageSize == null)
    RunError("Не задан обязательный параметр функции: размер страницы");
  end;

  Query = " SELECT "
            + " TO_CHAR(txavrcode.t_AvoirissID) AS t_AvoirissID "
           + " ,txavrcode.t_InstanceDate "
           + " ,txavrcode.t_Action "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7643 "
                 + " AND namealg.t_iNumberAlg = txavrcode.t_Action), CHR(0)) AS t_ActionName "
           + " ,txavrcode.t_ReplState "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7642 "
                 + " AND namealg.t_iNumberAlg = txavrcode.t_ReplState), CHR(0)) AS t_ReplStateName "
           + " ,TO_CHAR(txavrcode.t_MarketID) AS t_MarketID "
           + " ,NVL((SELECT "
                     + " MAX(txparty.t_ShortName) "
                 + " FROM "
                     + " dtxparty_dbt txparty "
                 + " WHERE "
                     + " txparty.t_PartyID = txavrcode.t_MarketID), CHR(0)) AS t_MarketName "
           + " ,txavrcode.t_MarketCode "
           + " ,txavrcode.t_Version "
        + " FROM "
            + " dtxavrcode_dbt txavrcode "
        + " WHERE "
            + " 1 = 1 ";

  AddWhere = GetReplAvrcodeAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  OrderBy = GetReplAvrcodeSortOrder(OrderItems, " txavrcode.t_AvoirissID ASC, txavrcode.t_InstanceDate ASC, txavrcode.t_Action ASC, txavrcode.t_ReplState ASC ");
  if(OrderBy != "")
    Query = Query + " ORDER BY " + OrderBy;
  end;

  Query = SQL_WrapSelect(Query, PageNum, PageSize);

  Cmd = RSDCommand(Query);
  Cmd.NullConversion = true;
  Cmd.Execute();

  Set = RsdRecordset(Cmd);

  while(Set.MoveNext())
    ReplAvrcode = CTxReplAvrcodeList();

    ReplAvrcode.AvoirissID = SQL_ConvTypeStr(Set.Value("t_AvoirissID"));
    ReplAvrcode.InstanceDate = SQL_ConvTypeDateTime(Set.Value("t_InstanceDate"));
    ReplAvrcode.Action = SQL_ConvTypeInteger(Set.Value("t_Action"));
    ReplAvrcode.ActionName = SQL_ConvTypeStr(Set.Value("t_ActionName"));
    ReplAvrcode.ReplState = SQL_ConvTypeInteger(Set.Value("t_ReplState"));
    ReplAvrcode.ReplStateName = SQL_ConvTypeStr(Set.Value("t_ReplStateName"));
    ReplAvrcode.MarketID = SQL_ConvTypeStr(Set.Value("t_MarketID"));
    ReplAvrcode.MarketName = SQL_ConvTypeStr(Set.Value("t_MarketName"));
    ReplAvrcode.MarketCode = SQL_ConvTypeStr(Set.Value("t_MarketCode"));
    ReplAvrcode.Version = SQL_ConvTypeInteger(Set.Value("t_Version"));

    ReplAvrcodeList[ReplAvrcodeList.Size] = ReplAvrcode;
  end;

  return ReplAvrcodeList;

OnError(err)
  TxReplAvrcodeRunError(err, -1);
end;

private macro CheckVersion(
  AvoirissID:String
 ,MarketID:String
 ,InstanceDate:DateTime
 ,Version:Integer
)
  var err = 0;
  var Query = "", Cmd = null, Set = null;

  Query = " SELECT "
            + " * "
        + " FROM "
            + " dtxavrcode_dbt "
        + " WHERE "
            + " t_AvoirissID = ? "
        + " AND t_MarketID = ? "
        + " AND t_InstanceDate = ? "
        + " AND t_Version = ? "
        + " FOR UPDATE ";

  Cmd = RsdCommand(Query);
  Cmd.AddParam("", RSDBP_IN, AvoirissID);
  Cmd.AddParam("", RSDBP_IN, MarketID);
  Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(InstanceDate));
  Cmd.AddParam("", RSDBP_IN, Version);
  Cmd.Execute();

  Set = RsdRecordset(Cmd);
  if(not Set.MoveNext())
    err = 70;
  end;

  return err;
end;

private macro UpdateTxAvrcodeImpl(
  StateAndAction//:CTxReplStateAndAction
 ,Avrcode//:CTxReplAvrcodeList
)
  var err = 0;
  var Query = "", Cmd = null;

  err = CheckVersion(Avrcode.AvoirissID, Avrcode.MarketID, Avrcode.InstanceDate, Avrcode.Version);

  if(err == 0)
    Query = " UPDATE "
              + " dtxavrcode_dbt "
          + " SET ";
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Query = Query +
                " t_ReplState = ? "
             + " ,t_Action = ? ";
    elif(StateAndAction.NeedUpdateReplState)
      Query = Query +
                " t_ReplState = ? ";
    else
      Query = Query +
                " t_Action = ? ";
    end;
    Query = Query +
            " WHERE "
              + " t_AvoirissID = ? "
          + " AND t_MarketID = ? "
          + " AND t_InstanceDate = ? ";

    Cmd = RsdCommand(Query);
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    elif(StateAndAction.NeedUpdateReplState)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
    else
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    end;
    Cmd.AddParam("", RSDBP_IN, Avrcode.AvoirissID);
    Cmd.AddParam("", RSDBP_IN, Avrcode.MarketID);
    Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(Avrcode.InstanceDate));
    Cmd.Execute();
  end;

  return err;
end;

macro TxUpdateAvrcodeList(
  StateAndAction//:CTxReplStateAndAction
 ,AvrcodeList//:TArray of CTxReplAvrcodeList
)
  var err = 0;
  var i = 0;

  if((StateAndAction.NeedUpdateReplState
   or StateAndAction.NeedUpdateAction)
 and (AvrcodeList != null))
    while((err == 0) and (i < AvrcodeList.Size))
      err = UpdateTxAvrcodeImpl(StateAndAction, AvrcodeList[i]);
      i = i + 1;
    end;
  end;

  return err;

OnError(err)
  TxReplAvrcodeRunError(err, -1);
end;
