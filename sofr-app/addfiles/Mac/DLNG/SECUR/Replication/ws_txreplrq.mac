/*
$Name:             ws_txreplrq.mac
$Module:           БОЦБ
$Description:      Макрос сервисов скроллинга Т/О для репликации
*/

import FIInter;
import "ws_common.mac";
import "ws_orderbygen.mac";
import "ws_datafilter.mac";

macro TxReplRQRunError(err:Variant, stat:Integer)
  WSRunError("ws_txreplrq.mac", err, stat);
end;

class CTxReplRQList()
  var DemandID:String;        // Идентификатор
  var InstanceDate:DateTime;  // Дата загрузки
  var Action:Integer;         // Действие
  var ActionName:String;      // Наименование действия
  var ReplState:Integer;      // Состояние репликации
  var ReplStateName:String;   // Наименование состояния репликации
  var DealID:String;          // Идентификатор сделки
  var Part:Integer;           // Номер части сделки
  var IsFact:Bool;            // Фактический
  var Kind:Integer;           // Вид Т/О
  var KindName:String;        // Вид Т/О наименование
  var Direction:Integer;      // Направление
  var DirectionName:String;   // Направление наименование
  var FiKind:Integer;         // Вид базового актива Т/О
  var FiKindName:String;      // Вид базового актива Т/О наименование
  var Date:Date;              // Дата исполнения Т/О
  var Sum:Money;              // Сумма
  var PayCurrencyID:String;   // Идентификатор валюты оплаты
  var PayCurrencyName:String; // Валюта оплаты
  var PaySum:Money;           // Сумма Т/О в валюте оплаты
  var PayRate:Money;          // Курс пересчета суммы в валюте операции в сумму в валюте оплаты
  var BalanceRate:Money;      // Курс постановки на баланс
  var PlanDemand:String;      // Идентификатор планового требования/обязательства, которое исполняется данным фактическим
  var State:Integer;          // Состояние Т/О
  var StateName:String;       // Состояние Т/О наименование
  var Note:String;            // Примечание
  var Version:Integer;        // Версия записи
end;

private macro GetSQLRQKindCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLNameAlgConditionEx("txdemand", "t_Kind", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcomiss.t_Type = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLStatusCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLNameAlgConditionEx("txdemand", "t_State", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcomiss.t_Type = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLDealIDCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLTxReplDealConditionEx("txdemand", "t_DealID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txdemand.t_DealID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetReplRQAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String
  var fp = null;

  fp = FilterParser(FilterItems);

  fp.AddFieldCondition(0, "txdemand", "t_InstanceDate");      // Записи за период
  fp.AddFieldCondition(1, "txdemand", "t_ReplState");         // Статус записи
  fp.AddFieldCondition(2, "txdemand", "t_Action");            // Вид действия
  fp.AddFieldCondition(3, "txdemand", "t_DemandID", " = 0 "); // Идентификатор записи
  fp.AddUserFieldCondition(4, @GetSQLRQKindCondition);        // Вид ТО
  fp.AddUserFieldCondition(5, @GetSQLDealIDCondition);        // Идентификатор операции
  fp.AddFieldCondition(6, "txdemand", "t_Direction");         // Направление
  fp.AddFieldCondition(7, "txdemand", "t_FiKind");            // Вид ФИ
  fp.AddFieldCondition(8, "txdemand", "t_Date");              // Дата
  fp.AddUserFieldCondition(9, @GetSQLStatusCondition);        // Статус
  fp.AddFieldCondition(10,"txdemand", "t_IsFact");            // Фактический

  return fp.GetFullSQLCondition();
end;

private macro GetReplRQSortOrder(
  OrderItems//:TArray of OrderItem // Параметры сортировки
 ,DefaultSQLSortOrder:String       // Строка сортировки по умолчанию
):String
  var ordrGen = null;

  ordrGen = OrderByGenerator(OrderItems, DefaultSQLSortOrder, false, true);

  return ordrGen.GetFullSQLSortOrder();
end;

// Получить общее количество записей скроллинга ТО для репликации
macro TxGetReplRQCount(
  FilterItems//:TArray of FilterCondItem // Массив элементов фильтра скроллинга
):Integer
  var Query = "", AddWhere = "";
  var Count = 0;

  Query = " SELECT "
            + " 1 "
        + " FROM "
            + " dtxdemand_dbt txdemand "
        + " WHERE "
            + " 1 = 1 ";

  AddWhere = GetReplRQAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  Count = SQL_GetNRecs(Query);

  return Count;

OnError(err)
  TxReplRQRunError(err, -1);
end;

// Отбор данных для скроллинга ТО
macro TxGetReplRQList(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
)
  var Query = "", AddWhere = "", OrderBy = "", Cmd = null, Set = null;
  var ReplRQList = TArray();
  var ReplRQ = null;

  if(PageNum == null)
    RunError("Не задан обязательный параметр функции: номер страницы");
  end;

  if(PageSize == null)
    RunError("Не задан обязательный параметр функции: размер страницы");
  end;

  Query = " SELECT "
            + " TO_CHAR(txdemand.t_DemandID) AS t_DemandID "
           + " ,txdemand.t_InstanceDate "
           + " ,txdemand.t_Action "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7643 "
                 + " AND namealg.t_iNumberAlg = txdemand.t_Action), CHR(0)) AS t_ActionName "
           + " ,txdemand.t_ReplState "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7642 "
                 + " AND namealg.t_iNumberAlg = txdemand.t_ReplState), CHR(0)) AS t_ReplStateName "
           + " ,TO_CHAR(txdemand.t_DealID) AS t_DealID "
           + " ,txdemand.t_Part "
           + " ,txdemand.t_IsFact "
           + " ,txdemand.t_Kind "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7641 "
                 + " AND namealg.t_iNumberAlg = txdemand.t_Kind), CHR(0)) AS t_KindName "
           + " ,txdemand.t_Direction "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7646 "
                 + " AND namealg.t_iNumberAlg = txdemand.t_Direction), CHR(0)) AS t_DirectionName "
           + " ,txdemand.t_FiKind "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7647 "
                 + " AND namealg.t_iNumberAlg = txdemand.t_FiKind), CHR(0)) AS t_FiKindName "
           + " ,txdemand.t_Date "
           + " ,txdemand.t_Sum "
           + " ,TO_CHAR(txdemand.t_PayCurrencyID) AS t_PayCurrencyID "
           + " ,NVL((SELECT "
                     + " MAX(txcur.t_Name) "
                 + " FROM "
                     + " dtxcurrency_dbt txcur "
                 + " WHERE "
                     + " txcur.t_FIID = txdemand.t_PayCurrencyID), CHR(0)) AS t_PayCurrencyName "
           + " ,txdemand.t_PaySum "
           + " ,txdemand.t_PayRate "
           + " ,txdemand.t_BalanceRate "
           + " ,DECODE(txdemand.t_PlanDemend, 0, CHR(0), TO_CHAR(txdemand.t_PlanDemend)) AS t_PlanDemand "
           + " ,txdemand.t_State "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7648 "
                 + " AND namealg.t_iNumberAlg = txdemand.t_State), CHR(0)) AS t_StateName "
           + " ,txdemand.t_Note "
           + " ,txdemand.t_Version "
        + " FROM "
            + " dtxdemand_dbt txdemand "
        + " WHERE "
            + " 1 = 1 ";

  AddWhere = GetReplRQAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  OrderBy = GetReplRQSortOrder(OrderItems, " txdemand.t_DemandID ASC, txdemand.t_InstanceDate ASC, txdemand.t_Action ASC, txdemand.t_ReplState ASC ");
  if(OrderBy != "")
    Query = Query + " ORDER BY " + OrderBy;
  end;

  Query = SQL_WrapSelect(Query, PageNum, PageSize);

  Cmd = RSDCommand(Query);
  Cmd.Execute();

  Set = RsdRecordset(Cmd);
  while(Set.MoveNext())
    ReplRQ = CTxReplRQList();

    ReplRQ.DemandID = SQL_ConvTypeStr(Set.Value("t_DemandID"));
    ReplRQ.InstanceDate = SQL_ConvTypeDateTime(Set.Value("t_InstanceDate"));
    ReplRQ.Action = SQL_ConvTypeInteger(Set.Value("t_Action"));
    ReplRQ.ActionName = SQL_ConvTypeStr(Set.Value("t_ActionName"));
    ReplRQ.ReplState = SQL_ConvTypeInteger(Set.Value("t_ReplState"));
    ReplRQ.ReplStateName = SQL_ConvTypeStr(Set.Value("t_ReplStateName"));
    ReplRQ.DealID = SQL_ConvTypeStr(Set.Value("t_DealID"));
    ReplRQ.Part = SQL_ConvTypeInteger(Set.Value("t_Part"));
    ReplRQ.IsFact = SQL_ConvTypeInteger(Set.Value("t_IsFact"));
    ReplRQ.Kind = SQL_ConvTypeInteger(Set.Value("t_Kind"));
    ReplRQ.KindName = SQL_ConvTypeStr(Set.Value("t_KindName"));
    ReplRQ.Direction = SQL_ConvTypeInteger(Set.Value("t_Direction"));
    ReplRQ.DirectionName = SQL_ConvTypeStr(Set.Value("t_DirectionName"));
    ReplRQ.FiKind = SQL_ConvTypeInteger(Set.Value("t_FiKind"));
    ReplRQ.FiKindName = SQL_ConvTypeStr(Set.Value("t_FiKindName"));
    ReplRQ.Date = SQL_ConvTypeDate(Set.Value("t_Date"));
    ReplRQ.Sum = SQL_ConvTypeSum(Set.Value("t_Sum"));
    ReplRQ.PayCurrencyID = SQL_ConvTypeStr(Set.Value("t_PayCurrencyID"));
    ReplRQ.PayCurrencyName = SQL_ConvTypeStr(Set.Value("t_PayCurrencyName"));
    ReplRQ.PaySum = SQL_ConvTypeSum(Set.Value("t_PaySum"));
    ReplRQ.PayRate = SQL_ConvTypeSum(Set.Value("t_PayRate"));
    ReplRQ.BalanceRate = SQL_ConvTypeSum(Set.Value("t_BalanceRate"));
    ReplRQ.PlanDemand = SQL_ConvTypeStr(Set.Value("t_PlanDemand"));
    ReplRQ.State = SQL_ConvTypeInteger(Set.Value("t_State"));
    ReplRQ.StateName = SQL_ConvTypeStr(Set.Value("t_StateName"));
    ReplRQ.Note = SQL_ConvTypeStr(Set.Value("t_Note"));
    ReplRQ.Version = SQL_ConvTypeInteger(Set.Value("t_Version"));

    ReplRQList[ReplRQList.Size] = ReplRQ;
  end;

  return ReplRQList;

OnError(err)
  TxReplRQRunError(err, -1);
end;

private macro CheckVersion(
  DemandID:String
 ,InstanceDate:DateTime
 ,Version:Integer
)
  var err = 0;
  var Query = "", Cmd = null, Set = null;

  Query = " SELECT "
            + " * "
        + " FROM "
            + " dtxdemand_dbt "
        + " WHERE "
            + " t_DemandID = ? "
        + " AND t_InstanceDate = ? "
        + " AND t_Version = ? "
        + " FOR UPDATE ";

  Cmd = RsdCommand(Query);
  Cmd.AddParam("", RSDBP_IN, DemandID);
  Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(InstanceDate));
  Cmd.AddParam("", RSDBP_IN, Version);
  Cmd.Execute();

  Set = RsdRecordset(Cmd);
  if(not Set.MoveNext())
    err = 70;
  end;

  return err;
end;

private macro UpdateTxRQImpl(
  StateAndAction//:CTxReplStateAndAction
 ,RQ//:CTxReplRQList
)
  var err = 0;
  var Query = "", Cmd = null;

  err = CheckVersion(RQ.DemandID, RQ.InstanceDate, RQ.Version);

  if(err == 0)
    Query = " UPDATE "
              + " dtxdemand_dbt "
          + " SET ";
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Query = Query +
                " t_ReplState = ? "
             + " ,t_Action = ? ";
    elif(StateAndAction.NeedUpdateReplState)
      Query = Query +
                " t_ReplState = ? ";
    else
      Query = Query +
                " t_Action = ? ";
    end;
    Query = Query +
            " WHERE "
              + " t_DemandID = ? "
          + " AND t_InstanceDate = ? ";

    Cmd = RsdCommand(Query);
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    elif(StateAndAction.NeedUpdateReplState)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
    else
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    end;
    Cmd.AddParam("", RSDBP_IN, RQ.DemandID);
    Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(RQ.InstanceDate));
    Cmd.Execute();
  end;

  return err;
end;

macro TxUpdateRQList(
  StateAndAction//:CTxReplStateAndAction
 ,RQList//:TArray of CTxReplRQList
)
  var err = 0;
  var i = 0;

  if((StateAndAction.NeedUpdateReplState
   or StateAndAction.NeedUpdateAction)
 and (RQList != null))
    while((err == 0) and (i < RQList.Size))
      err = UpdateTxRQImpl(StateAndAction, RQList[i]);
      i = i + 1;
    end;
  end;

  return err;

OnError(err)
  TxReplRQRunError(err, -1);
end;
