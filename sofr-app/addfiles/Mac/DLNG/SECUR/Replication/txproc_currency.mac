/*
$Name:             txproc_currency.mac
$Module:           БОЦБ 
$Description:      Макрос репликации валют
*/
import txrepl_func;

private var stat, MaxVal;

macro ProcessCurrency(rs);
   var SQL1, cmd1, rs1, count, maxfiid;

   SQL1 = "SELECT COUNT (*) FROM DTXREPLOBJ_DBT where T_OBJECTTYPE = 10 and T_OBJECTID =" + rs.value("T_FIID") + " AND T_OBJSTATE != 2";
   cmd1 = RsdCommand( SQL1);
   rs1 = RsdRecordSet(cmd1);
   rs1.movenext;
   count = Rs1.Value(0);

   if (rs.Value("T_ACTION") == 1)
      if (count > 0)
         ЗанестиЗаписьВПротокол(406, 10, rs.value("T_FIID"), 1, StrFor(1), "Ошибка:  невозможно вставить существующую валюту " + trim(rs.Value("t_name")), rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      end;

      SQL1 = "SELECT MAX (t_fiid) FROM dfininstr_dbt";
      cmd1 = RsdCommand(SQL1);
      rs1 = RsdRecordSet(cmd1);
      rs1.movenext;
      maxfiid = rs1.Value(0);

      if (maxfiid == NullVal)
         maxfiid = 0;
      end;

      if ((rs.Value("t_iso_number") == 810) or (rs.Value("t_iso_number") == 643))
         maxfiid = -1;
      end;

      // При вставке валюты, нужно проверить существование в дистрибутиве валют с таким же "T_FI_CODE"(уникальное поле) или "T_FIID" = 0 при вставке рублей.
      // Если валюта в дистрибутиве есть, просто добавим запись о соответствии в таблицу "DTXREPLOBJ_DBT" и обновим статус реплицируемой валюты в "DTXCURRENCY_DBT".
      if(count == 0)
         if(maxfiid == -1)
            SQL1 = "SELECT t_fiid FROM dfininstr_dbt where t_fiid = " + string(maxfiid + 1);
         else
            SQL1 = "SELECT t_fiid FROM dfininstr_dbt where T_FI_CODE = '" + String(rs.Value("t_iso_number")) + "' ";
         end;

         cmd1 = RsdCommand( SQL1);
         rs1 = RsdRecordSet(cmd1);

         if( rs1.movenext() )
            SQL1 = "insert into DTXREPLOBJ_DBT (T_OBJECTTYPE, T_OBJECTID, T_SUBOBJNUM, T_DESTID, T_DESTSUBOBJNUM,";
            SQL1 = SQL1 + "T_OBJSTATE) values (10, " + rs.Value("t_fiid") + ", 1, ";
            SQL1 = SQL1 + rs1.Value("t_fiid") + " ,1 , 0)";
            cmd1 = RsdCommand( SQL1);
            cmd1.execute;

            SQL1 = "update DTXCURRENCY_DBT set t_replstate = 1 where t_fiid = " + rs.value("t_fiid") + " and t_action = 1 and t_replstate = 0";
            SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
            cmd1 = RsdCommand( SQL1);
            cmd1.execute;

            return true;
         end;
      end;

      SQL1 = "INSERT INTO dfininstr_dbt(T_FIID,T_FI_CODE,T_FI_KIND,T_AVOIRKIND,T_NAME,T_DEFINITION,T_CODEINACCOUNT,T_ISO_NUMBER,T_CCY,T_ISSUER,T_ISSUED,"+
	     "T_SETTLEMENT_CODE,T_FACEVALUEFI,T_FACEVALUE,T_PARENTFI,T_DRAWINGDATE,T_EXPIRYDATE,T_ISCLOSED,T_USERFIELD1,T_USERFIELD2,T_USERFIELD3,T_USERFIELD4,"+
	     "T_ISINVERSE,T_SCALE,T_POINT,T_ISSYS,T_ISADDITIONAL,T_MAINFIID,T_SUMPRECISION,T_ISGENERALIZED,T_GENERALIZEDFIID,T_RESERVE) ";
      SQL1 = SQL1 +  " VALUES ("+(maxfiid+1)+",";                       /*T_FIID             NUMBER(10)*/
      SQL1 = SQL1 +  "        '"+String(rs.Value("t_iso_number"))+"',"; /*T_FI_CODE          VARCHAR2(15 BYTE)*/
      SQL1 = SQL1 +  "          1,";                                    /*T_FI_KIND          NUMBER(5)*/
      SQL1 = SQL1 +  "          0,";                                    /*T_AVOIRKIND        NUMBER(5)*/
      SQL1 = SQL1 +  "         '"+ trim(rs.Value("t_name")) + "','";    /*T_NAME             VARCHAR2(25 BYTE)*/
      SQL1 = SQL1 +  trim(rs.Value("t_name")) + "',";                   /*T_DEFINITION       VARCHAR2(164 BYTE)*/
      SQL1 = SQL1 +  "          CHR (1),";                              /*T_CODEINACCOUNT    VARCHAR2(5 BYTE)*/
      SQL1 = SQL1 +  "          "+rs.Value("t_iso_number")+",'";        /*T_ISO_NUMBER       NUMBER(5)*/
      SQL1 = SQL1 +  trim(rs.Value("t_code"))+"',";                     /*T_CCY              VARCHAR2(3 BYTE)*/
      SQL1 = SQL1 +  "          -1,";                                   /*T_ISSUER           NUMBER(10)*/
      SQL1 = SQL1 +  "          "+nulldate+",";                         /*T_ISSUED           DATE*/
      SQL1 = SQL1 +  "          0,";                                    /*T_SETTLEMENT_CODE  NUMBER(5)*/
      SQL1 = SQL1 +  "          0,";                                    /*T_FACEVALUEFI      NUMBER(10)*/
      SQL1 = SQL1 +  "          0,";                                    /*T_FACEVALUE        NUMBER(32,12)*/
      SQL1 = SQL1 +  "          0,";                                    /*T_PARENTFI         NUMBER(10)*/
      SQL1 = SQL1 +  "          "+nulldate+",";                         /*T_DRAWINGDATE      DATE*/
      SQL1 = SQL1 +  "          "+nulldate+",";                         /*T_EXPIRYDATE       DATE*/
      SQL1 = SQL1 +  "          CHR (0),";                              /*T_ISCLOSED         CHAR(1 BYTE)*/
      SQL1 = SQL1 +  "          CHR (1),";                              /*T_USERFIELD1       VARCHAR2(120 BYTE)*/
      SQL1 = SQL1 +  "          CHR (1),";                              /*T_USERFIELD2       VARCHAR2(40 BYTE)*/
      SQL1 = SQL1 +  "          CHR (1),";                              /*T_USERFIELD3       VARCHAR2(80 BYTE)*/
      SQL1 = SQL1 +  "          CHR (1),";                              /*T_USERFIELD4       VARCHAR2(80 BYTE)*/
      SQL1 = SQL1 +  "          CHR (0),";                              /*T_ISINVERSE        CHAR(1 BYTE)*/
      SQL1 = SQL1 +  "          0,";                                    /*T_SCALE            NUMBER(10)*/
      SQL1 = SQL1 +  "          0,";                                    /*T_POINT            NUMBER(5)*/
      SQL1 = SQL1 +  "          CHR (0),";                              /*T_ISSYS            CHAR(1 BYTE)*/
      SQL1 = SQL1 +  "          CHR (0),";                              /*T_ISADDITIONAL     CHAR(1 BYTE)*/
      SQL1 = SQL1 +  "          -1,";                                   /*T_MAINFIID         NUMBER(10)*/
      SQL1 = SQL1 +  "          2,";                                    /*T_SUMPRECISION     NUMBER(5)*/
      SQL1 = SQL1 +  "          CHR (0),";                              /*T_ISGENERALIZED    CHAR(1 BYTE)*/
      SQL1 = SQL1 +  "          -1,";                                    /*T_GENERALIZEDFIID  NUMBER(10)*/
      SQL1 = SQL1 +  "          CHR (1)";                               /*T_RESERVE          VARCHAR2(177 BYTE)*/
      SQL1 = SQL1 +  "          )";

      cmd1=RsdCommand(SQL1);
      cmd1.execute;

      SQL1 = "delete from DTXREPLOBJ_DBT where T_OBJECTTYPE=10 and t_objstate = 2 and t_objectid = " + rs.Value("t_fiid");
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;

      SQL1 = "insert into DTXREPLOBJ_DBT (T_OBJECTTYPE, T_OBJECTID, T_SUBOBJNUM, T_DESTID, T_DESTSUBOBJNUM,";
      SQL1 = SQL1 + "T_OBJSTATE) values (10, " + rs.Value("t_fiid") + ", 1, ";
      SQL1 = SQL1 + (maxfiid + 1) + " ,1 , 0)";
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;

      SQL1 = "update DTXCURRENCY_DBT set t_replstate = 1 where t_fiid = " + rs.value("t_fiid") + " and t_action = 1 and t_replstate = 0";
      SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;
   elif (rs.Value("t_Action") == 2)
      if (count == 0)
         ЗанестиЗаписьВПротокол(407, 10, rs.value("T_FIID"), 1, StrFor(1), "Ошибка: невозможно обновить несуществующую валюту " + trim(rs.Value("t_name")), rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      end;

      if (ПроверитьНаРучноеИзменение(10, rs.value("T_FIID")))
         ЗанестиЗаписьВПротокол(202, 10, rs.value("T_FIID"), 1, StrFor(1), "Ошибка: объект находится в режиме ручного редактирования, валюта " + trim(rs.Value("t_name")), rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      end;

      SQL1 = "UPDATE dfininstr_dbt fi SET fi.t_fi_code = '" + rs.Value("t_iso_number") + "', fi.t_name = '" + trim(rs.Value("t_name"));
      SQL1 = SQL1 + /*" fi.t_codeinaccount = '"+rs.Value("t_codeinaccount")+*/"', fi.t_iso_number = "+rs.Value("t_iso_number") + ", fi.t_ccy = '" + trim(rs.Value("t_code")) + "' WHERE ";
      SQL1 = SQL1 + " fi.t_fiid = " + ПолучитьСоответствие(10, rs.value("t_fiid"));
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      SQL1 = "update DTXCURRENCY_DBT set t_replstate=1 where t_fiid="+rs.value("t_fiid")+" and t_action=2 and t_replstate = 0";
      SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;
   elif (rs.Value("t_Action") == 3)
      if (count == 0)
         ЗанестиЗаписьВПротокол(408, 10, rs.value("T_FIID"), 1, StrFor(1), "Ошибка: невозможно удалить несуществующую валюту " + trim(rs.Value("t_name")), rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      end;

      SQL1 = "delete from dfininstr_dbt where t_fiid = " + ПолучитьСоответствие(10, rs.value("t_fiid"));
      cmd1 = RsdCommand(SQL1);
      cmd1.execute;

      SQL1 = "update DTXCURRENCY_DBT set t_replstate = 1 where t_fiid = " + rs.value("t_fiid") + " and t_action = 3 and t_replstate = 0";
      SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;

      SQL1 = "update DTXREPLOBJ_DBT ots set ots.T_OBJSTATE = 2 where ots.T_OBJECTTYPE = 10 and ots.T_OBJECTID = " + rs.value("t_fiid");
      cmd1 = RsdCommand( SQL1);
      cmd1.execute;
   end;
   
   return TRUE;
OnError(er)
   ЗанестиЗаписьВПротокол(600, 10, rs.value("T_FIID"), 1, StrFor(1), "Ошибка: " + er.message, rs.Value("T_INSTANCEDATE"));
   AbortTrn;
end;

macro ОбработатьВалюту(Action, t_instancedate)
   var SQL, cmd, rs;

   SQL = "select * from DTXCURRENCY_DBT where t_replstate = 0 and t_action = " + Action + " and trunc(t_instancedate) = " + t_instancedate + "order by t_fiid";
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);
  
   while (rs.Movenext)   
      RslDefCon.BeginTrans();

      if (ProcessCurrency(rs))
	 RslDefCon.CommitTrans();
      else
         RslDefCon.RollbackTrans();
      end;
   end;

   return TRUE;
OnError(er)
   ЗанестиЗаписьВПротокол(600, 10, "", "", StrFor(1), "Ошибка: " + er.message, date(t_instancedate));
end;