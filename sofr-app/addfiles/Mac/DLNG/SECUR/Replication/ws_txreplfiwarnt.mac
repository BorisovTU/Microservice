/*
$Name:             ws_txreplfiwarnt.mac
$Module:           АРНУ
$Description:      WEB. АРНУ. Макрос сервисов скроллинга купонов / ЧП (погашений / амортизаций) Ц/Б для репликации
*/

import FIInter;
import "ws_common.mac";
import "ws_datafilter.mac";
import "ws_orderbygen.mac";

macro TxReplFiwarntRunError(err:Variant, stat:Integer)
  WSRunError("ws_txreplfiwarnt.mac", err, stat);
end;

// Класс скроллинга курсов ц/б для репликации
class CTxReplFiwarntList()
  var WarrantID:String;      // Идентификатор
  var InstanceDate:DateTime; // Дата загрузки
  var Action:Integer;        // Действие
  var ActionName:String;     // Наименование действия
  var ReplState:Integer;     // Статус репликации
  var ReplStateName:String;  // Статус репликации
  var Type:Integer;          // Тип погашения
  var TypeName:String;       // Тип погашения
  var FIID:String;           // Идентификатор ценной бумаги
  var FIIDName:String;       // Идентификатор ценной бумаги
  var Number:Integer;        // Номер купона/ЧП ценной бумаги
  var FirstDate:Date;        // Дата начала купонного периода
  var DrawingDate:Date;      // Дата погашения купона
  var OwnFixDate:Date;       // Дата фиксации владельца
  var RelativeIncome:Bool;   // Доход начисляется в процентах
  var IncomePoint:Integer;   // Точность дохода	
  var IncomeScale:Integer;   // Масштаб дохода	
  var IsClosed:Bool;         // Признак погашения
  var Version:Integer;       // Версия записи
end;

private macro GetSQLAvoirissIDCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLTxAvoirssConditionEx("txfiwarnt", "t_FIID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcourse.t_Fiid = -1 ";
  end;

  return strSQLCond;
end;

private macro GetReplFiwarntAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String
  var fp = null;

  fp = FilterParser(FilterItems);

  fp.AddFieldCondition(0, "txfiwarnt", "t_InstanceDate");        // Записи за период
  fp.AddFieldCondition(1, "txfiwarnt", "t_ReplState");           // Статус записи
  fp.AddFieldCondition(2, "txfiwarnt", "t_Action");              // Вид действия
  fp.AddFieldCondition(3, "txfiwarnt", "t_WarrantID", " = -1 "); // Идентификатор записи
  fp.AddFieldCondition(4, "txfiwarnt", "t_Type");                // Тип погашения
  fp.AddUserFieldCondition(5, @GetSQLAvoirissIDCondition);       // Ценная бумага
  fp.AddFieldCondition(6, "txfiwarnt", "t_DrawingDate");         // Дата погашения купона
  fp.AddFieldCondition(7, "txfiwarnt", "t_OwnFixDate");          // Дата фиксации владельца
  fp.AddFieldCondition(8, "txfiwarnt", "t_IsClosed");            // Признак погашения

  return fp.GetFullSQLCondition();
end;

private macro GetReplFiwarntSortOrder(
  OrderItems//:TArray of OrderItem // Параметры сортировки
 ,DefaultSQLSortOrder:String       // Строка сортировки по умолчанию
):String
  var ordrGen = null;

  ordrGen = OrderByGenerator(OrderItems, DefaultSQLSortOrder, false, true);

  return ordrGen.GetFullSQLSortOrder();
end;

// Получить общее количество записей скроллинга Ц/Б
macro TxGetReplFiwarntCount(
  FilterItems//:TArray of FilterCondItem // Массив элементов фильтра скроллинга
):Integer
  var Query = "", AddWhere = "";
  var Count = 0;

  Query = " SELECT "
            + " 1 "
        + " FROM "
            + " dtxfiwarnts_dbt txfiwarnt "
        + " WHERE "
            + " 1 = 1 ";

  AddWhere = GetReplFiwarntAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  Count = SQL_GetNRecs(Query);

  return Count;

OnError(err)
  TxReplFiwarntRunError(err, -1);
end;

// Отбор данных для скроллинга Ц/Б
macro TxGetReplFiwarntList(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
):TArray
  var Query = "", AddWhere = "", OrderBy = "", Cmd = null, Set = null;
  var ReplFiwarnt = null;
  var ReplFiwarntList = TArray();

  if(PageNum == null)
    RunError("Не задан обязательный параметр функции: номер страницы");
  end;

  if(PageSize == null)
    RunError("Не задан обязательный параметр функции: размер страницы");
  end;

  Query = " SELECT "
            + " TO_CHAR(txfiwarnt.t_WarrantID) AS t_WarrantID "
           + " ,txfiwarnt.t_InstanceDate "
           + " ,txfiwarnt.t_Action "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7643 "
                 + " AND namealg.t_iNumberAlg = txfiwarnt.t_Action), CHR(0)) AS t_ActionName "
           + " ,txfiwarnt.t_ReplState "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7642 "
                 + " AND namealg.t_iNumberAlg = txfiwarnt.t_ReplState), CHR(0)) AS t_ReplStateName "
           + " ,txfiwarnt.t_Type "
           + " ,DECODE(txfiwarnt.t_Type, 50,  'Купон' "
                                    + " ,60,  'Амортизация' "
                                    + " ,120, 'Дивиденд' "
                                    + " ,CHR(0)) AS t_TypeName "
           + " ,TO_CHAR(txfiwarnt.t_FIID) AS t_FIID "
           + " ,NVL((SELECT "
                     + " MAX(txavoiriss.t_ShortName) "
                 + " FROM "
                     + " dtxavoiriss_dbt txavoiriss "
                 + " WHERE "
                     + " txavoiriss.t_AvoirissID = txfiwarnt.t_FIID), CHR(0)) AS t_FIIDName "
           + " ,txfiwarnt.t_Number "
           + " ,txfiwarnt.t_FirstDate "
           + " ,txfiwarnt.t_DrawingDate "
           + " ,txfiwarnt.t_Ownfixdate "
           + " ,txfiwarnt.t_Income "
           + " ,txfiwarnt.t_RelativeIncome "
           + " ,txfiwarnt.t_IncomePoint "
           + " ,txfiwarnt.t_IncomeScale "
           + " ,txfiwarnt.t_IsClosed "
           + " ,txfiwarnt.t_Version "
        + " FROM "
            + " dtxfiwarnts_dbt txfiwarnt "
        + " WHERE "
            + " 1 = 1 ";

  AddWhere = GetReplFiwarntAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  OrderBy = GetReplFiwarntSortOrder(OrderItems, " txfiwarnt.t_WarrantID ASC, txfiwarnt.t_InstanceDate ASC, txfiwarnt.t_Action ASC, txfiwarnt.t_ReplState ASC ");
  if(OrderBy != "")
    Query = Query + " ORDER BY " + OrderBy;
  end;

  Query = SQL_WrapSelect(Query, PageNum, PageSize);

  Cmd = RSDCommand(Query);
  Cmd.NullConversion = true;
  Cmd.Execute();

  Set = RsdRecordset(Cmd);

  while(Set.MoveNext())
    ReplFiwarnt = CTxReplFiwarntList();

    ReplFiwarnt.WarrantID = SQL_ConvTypeStr(Set.Value("t_WarrantID"));
    ReplFiwarnt.InstanceDate = SQL_ConvTypeDateTime(Set.Value("t_InstanceDate"));
    ReplFiwarnt.Action = SQL_ConvTypeInteger(Set.Value("t_Action"));
    ReplFiwarnt.ActionName = SQL_ConvTypeStr(Set.Value("t_ActionName"));
    ReplFiwarnt.ReplState = SQL_ConvTypeInteger(Set.Value("t_ReplState"));
    ReplFiwarnt.ReplStateName = SQL_ConvTypeStr(Set.Value("t_ReplStateName"));
    ReplFiwarnt.Type = SQL_ConvTypeInteger(Set.Value("t_Type"));
    ReplFiwarnt.TypeName = SQL_ConvTypeStr(Set.Value("t_TypeName"));
    ReplFiwarnt.FIID = SQL_ConvTypeStr(Set.Value("t_FIID"));
    ReplFiwarnt.FIIDName = SQL_ConvTypeStr(Set.Value("t_FIIDName"));
    ReplFiwarnt.Number = SQL_ConvTypeInteger(Set.Value("t_Number"));
    ReplFiwarnt.FirstDate = SQL_ConvTypeDate(Set.Value("t_FirstDate"));
    ReplFiwarnt.DrawingDate = SQL_ConvTypeDate(Set.Value("t_DrawingDate"));
    ReplFiwarnt.OwnFixDate = SQL_ConvTypeDate(Set.Value("t_OwnFixDate"));
    ReplFiwarnt.RelativeIncome = (SQL_ConvTypeStr(Set.Value("t_RelativeIncome")) == SET_CHR);
    ReplFiwarnt.IncomePoint = SQL_ConvTypeInteger(Set.Value("t_IncomePoint"));
    ReplFiwarnt.IncomeScale = SQL_ConvTypeInteger(Set.Value("t_IncomeScale"));
    ReplFiwarnt.IsClosed = (SQL_ConvTypeStr(Set.Value("t_IsClosed")) == SET_CHR);
    ReplFiwarnt.Version = SQL_ConvTypeInteger(Set.Value("t_Version"));

    ReplFiwarntList[ReplFiwarntList.Size] = ReplFiwarnt;
  end;

  return ReplFiwarntList;

OnError(err)
  TxReplFiwarntRunError(err, -1);
end;

private macro CheckVersion(
  WarrantID:String
 ,Type:Integer
 ,InstanceDate:DateTime
 ,Version:Integer
)
  var err = 0;
  var Query = "", Cmd = null, Set = null;

  Query = " SELECT "
            + " * "
        + " FROM "
            + " dtxfiwarnts_dbt "
        + " WHERE "
            + " t_WarrantID = ? "
        + " AND t_Type = ? "
        + " AND t_InstanceDate = ? "
        + " AND t_Version = ? "
        + " FOR UPDATE ";

  Cmd = RsdCommand(Query);
  Cmd.AddParam("", RSDBP_IN, WarrantID);
  Cmd.AddParam("", RSDBP_IN, Type);
  Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(InstanceDate));
  Cmd.AddParam("", RSDBP_IN, Version);
  Cmd.Execute();

  Set = RsdRecordset(Cmd);
  if(not Set.MoveNext())
    err = 70;
  end;

  return err;
end;

private macro UpdateTxFiwarntImpl(
  StateAndAction//:CTxReplStateAndAction
 ,Fiwarnt//:CTxReplFiwarntList
)
  var err = 0;
  var Query = "", Cmd = null;

  err = CheckVersion(Fiwarnt.WarrantID, Fiwarnt.Type, Fiwarnt.InstanceDate, Fiwarnt.Version);

  if(err == 0)
    Query = " UPDATE "
              + " dtxfiwarnts_dbt "
          + " SET ";
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Query = Query +
                " t_ReplState = ? "
             + " ,t_Action = ? ";
    elif(StateAndAction.NeedUpdateReplState)
      Query = Query +
                " t_ReplState = ? ";
    else
      Query = Query +
                " t_Action = ? ";
    end;
    Query = Query +
            " WHERE "
              + " t_WarrantID = ? "
          + " AND t_Type = ? "
          + " AND t_InstanceDate = ? ";

    Cmd = RsdCommand(Query);
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    elif(StateAndAction.NeedUpdateReplState)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
    else
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    end;
    Cmd.AddParam("", RSDBP_IN, Fiwarnt.WarrantID);
    Cmd.AddParam("", RSDBP_IN, Fiwarnt.Type);
    Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(Fiwarnt.InstanceDate));
    Cmd.Execute();
  end;

  return err;
end;

macro TxUpdateFiwarntList(
  StateAndAction//:CTxReplStateAndAction
 ,FiwarntList//:TArray of CTxReplFiwarntList
)
  var err = 0;
  var i = 0;

  if((StateAndAction.NeedUpdateReplState
   or StateAndAction.NeedUpdateAction)
 and (FiwarntList != null))
    while((err == 0) and (i < FiwarntList.Size))
      err = UpdateTxFiwarntImpl(StateAndAction, FiwarntList[i]);
      i = i + 1;
    end;
  end;

  return err;

OnError(err)
  TxReplFiwarntRunError(err, -1);
end;
