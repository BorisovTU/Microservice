/*
$Name:             txproc_coupon.mac
$Module:           БОЦБ 
$Description:      Макрос репликации купонов
*/
import txproc_coup_fun, txrepl_func;

private var SQL, cmd, rs, Счетчик, НаименованиеБумаги, MaxVal;

macro Process_Coupon()
   var CouponData = TCouponData();
   var SQL1, cmd1, rs1;

   НаименованиеБумаги = ПолучитьНаименованиеБумаги(rs.Value("T_FIID"));

   CouponData.T_WARRANTID       = rs.Value("T_WARRANTID");
   if (rs.Value("T_ACTION") != 1)
      CouponData.T_ID           = ПолучитьСоответствие(int(rs.Value("T_TYPE")), rs.Value("T_WARRANTID"));
   end;
   CouponData.T_TYPE            = rs.Value("T_TYPE");
   CouponData.T_FIID            = ПолучитьСоответствие(20, rs.Value("T_FIID"));
   CouponData.T_NUMBER          = rs.Value("T_NUMBER");
   CouponData.T_FIRSTDATE       = rs.Value("T_FIRSTDATE");
   CouponData.T_DRAWINGDATE     = rs.Value("T_DRAWINGDATE");
   CouponData.T_OWNFIXDATE      = rs.Value("T_OWNFIXDATE");
   CouponData.T_INCOME          = rs.Value("T_INCOME");
   CouponData.T_INCOMEPOINT     = rs.Value("T_INCOMEPOINT");
   CouponData.T_INCOMESCALE     = rs.Value("T_INCOMESCALE");

   if (rs.Value("T_ACTION") == 2)
      SQL1 = "select t_number CNUMBER from DTXFIWARNTS_DBT WAR WHERE WAR.T_TYPE= " + rs.Value("T_TYPE") + " AND  WAR.T_WARRANTID = " + rs.Value("T_WARRANTID") +
         " and WAR.t_action in (1,2) and WAR.t_replstate=1 " +
         " and WAR.t_instancedate = (select max(war3.t_instancedate) from DTXFIWARNTS_DBT war3 where war3.T_WARRANTID = WAR.t_warrantid and WAR3.t_action in (1,2) and war3.t_replstate=1) ";
      cmd1 = RsdCommand( SQL1);
      rs1 = RsdRecordSet(cmd1);      
      if (rs1.movenext()) 
         CouponData.T_OLDNUMBER = rs1.value("CNUMBER");
      else
         CouponData.T_OLDNUMBER = CouponData.T_NUMBER;      
      end;
   else
      CouponData.T_OLDNUMBER = CouponData.T_NUMBER;      
   end;

   if (rs.Value("T_ISCLOSED") != NullVal)
      CouponData.T_ISCLOSED = rs.Value("T_ISCLOSED");
   end;

   if (rs.Value("T_RELATIVEINCOME") != NullVal)
      CouponData.T_RELATIVEINCOME = rs.Value("T_RELATIVEINCOME");
   else
      CouponData.T_RELATIVEINCOME = "";
   end;

   CouponData.T_INSTANCEDATE    = rs.Value("T_INSTANCEDATE");
   CouponData.NameAvoiriss      = НаименованиеБумаги;
   CouponData.InTRN             = TRUE;

   if (rs.Value("T_ACTION") == 2)
      if (ПроверитьНаРучноеИзменение(rs.Value("T_TYPE"), rs.value("T_WARRANTID")))
         println("Ошибка: объект находится в режиме ручного редактирования");
         ЗанестиЗаписьВПротокол(204, rs.Value("T_TYPE"), rs.value("T_WARRANTID"), 1, StrFor(1), "Ошибка: объект находится в режиме ручного редактирования, купон/амортизация/дивиденд № " + rs.Value("T_NUMBER") + " по бумаге " + НаименованиеБумаги, rs.Value("T_INSTANCEDATE"));
         Return FALSE;
      end;
   end;

   if( ОбработатьКупон( CouponData, rs.Value("T_ACTION") ) == false )
      Return FALSE;
   else
      if (rs.Value("T_ACTION") == 1)
         SQL1 = "delete from DTXREPLOBJ_DBT where T_OBJECTTYPE=" + rs.Value("T_TYPE") + " and t_objstate = 2 and t_objectid = " + rs.Value("T_WARRANTID");
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;

         SQL1 = "insert into DTXREPLOBJ_DBT (T_OBJECTTYPE, T_OBJECTID, T_SUBOBJNUM, T_DESTID, T_DESTSUBOBJNUM,";
         SQL1 = SQL1 + "T_OBJSTATE) values (" + rs.Value("T_TYPE") + ", " + rs.Value("T_WARRANTID") + ", 1, ";
         SQL1 = SQL1 + CouponData.T_ID + " ," + CouponData.T_FIID + " , 0)";
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;

         SQL1 = "update DTXFIWARNTS_DBT set t_replstate = 1 where T_WARRANTID = " + rs.value("T_WARRANTID") + " and t_action = 1 and t_replstate = 0";
         SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;
      elif (rs.Value("T_ACTION") == 2)
         if (СущественныйАтрибут(rs.Value("T_TYPE"), rs.Value("T_WARRANTID"), "T_FIRSTDATE", rs.Value("T_FIRSTDATE")))
            ЗанестиЗаписьВПротокол(127, rs.Value("T_TYPE"), rs.Value("T_WARRANTID"), 1, "T_FIRSTDATE", "Предупреждение: изменение существенного атрибута 'Дата начала', купон/амортизация/дивиденд № " + rs.Value("T_NUMBER") + " по бумаге " + НаименованиеБумаги, rs.Value("T_INSTANCEDATE"));
         end;
         if (СущественныйАтрибут(rs.Value("T_TYPE"), rs.Value("T_WARRANTID"), "T_DRAWINGDATE", rs.Value("T_DRAWINGDATE")))
            ЗанестиЗаписьВПротокол(128, rs.Value("T_TYPE"), rs.Value("T_WARRANTID"), 1, "T_DRAWINGDATE", "Предупреждение: изменение существенного атрибута 'Дата погашения', купон/амортизация/дивиденд № " + rs.Value("T_NUMBER") + " по бумаге " + НаименованиеБумаги, rs.Value("T_INSTANCEDATE"));
         end;
         if (СущественныйАтрибут(rs.Value("T_TYPE"), rs.Value("T_WARRANTID"), "T_OWNFIXDATE", rs.Value("T_OWNFIXDATE")))
            ЗанестиЗаписьВПротокол(129, rs.Value("T_TYPE"), rs.Value("T_WARRANTID"), 1, "T_OWNFIXDATE", "Предупреждение: изменение существенного атрибута 'Дата фиксации владельца', купон/амортизация/дивиденд №" + rs.Value("T_NUMBER") + " по бумаге " + НаименованиеБумаги, rs.Value("T_INSTANCEDATE"));
         end;
         if (СущественныйАтрибут(rs.Value("T_TYPE"), rs.Value("T_WARRANTID"), "T_INCOME", rs.Value("T_INCOME")))
            ЗанестиЗаписьВПротокол(130, rs.Value("T_TYPE"), rs.Value("T_WARRANTID"), 1, "T_INCOME", "Предупреждение: изменение существенного атрибута 'Доход', купон/амортизация/дивиденд № " + rs.Value("T_NUMBER") + " по бумаге " + НаименованиеБумаги, rs.Value("T_INSTANCEDATE"));
         end;

         SQL1 = "update DTXFIWARNTS_DBT set t_replstate = 1 where T_WARRANTID = " + rs.value("T_WARRANTID") + " and t_action = 2 and t_replstate = 0";
         SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;
      elif (rs.Value("T_ACTION") == 3)
         SQL1 = "update DTXFIWARNTS_DBT set t_replstate = 1 where T_WARRANTID = " + rs.value("T_WARRANTID") + " and t_action = 3 and t_replstate = 0";
         SQL1 = SQL1 + " and trunc(t_instancedate) = " + getsqlDate_SK(DateStamp(rs.value("t_instancedate")));
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;

         SQL1 = "update DTXREPLOBJ_DBT ots set ots.T_OBJSTATE = 2 where ots.T_OBJECTTYPE = " + rs.Value("T_TYPE") + " and ots.T_OBJECTID = " + rs.value("T_WARRANTID");
         cmd1 = RsdCommand( SQL1);
         cmd1.execute;
      end;

      Return TRUE;
   end;
OnError(er)
   if (Trim(er.message) != "Error:")
      ЗанестиЗаписьВПротокол(600, rs.Value("T_TYPE"), rs.value("T_WARRANTID"), 1, StrFor(1), "Ошибка: " + er.message, rs.Value("T_INSTANCEDATE"));
   end;
   AbortTrn;
end;

macro Обработать_Купон(Action, t_instancedate, Init_Action:String)
   var SQL1;
   SQL = "select count(*) from DTXFIWARNTS_DBT where t_replstate = 0 and t_action = " + Action + " and trunc(t_instancedate) = " + t_instancedate;
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);

   if (rs.Movenext)
      Счетчик = rs.Value(0);
      MaxVal = rs.Value(0);
   end;

   var ProgressIndicator = CreateProgressIndicator(true);
   ProgressIndicator.Start(Int(MaxVal), "Обработка купонов/амортизаций...");

   Счетчик = 0;
   SQL = "";

   // определим, есть ли "лесенка" с купонах - массовое изменение номеров в связи с разбиением купонного периода или удалением купона из середины.
   // в зависимости от того, какое из двух перечисленных действий мы обнаружим, устанавливаем направление обработки купонов - с первого по времени или с последнего.
   if (Action == 2)
      SQL = "select 1 flag from DTXFIWARNTS_DBT war1, DTXFIWARNTS_DBT war2 " +
       "where war1.t_replstate=0 and war2.t_replstate=1  " +
       "and war1.t_warrantid = war2.t_warrantid " +
       "and war1.t_number < war2.T_NUMBER " +
       "and war2.t_instancedate =  " +
       "(select max(war3.t_instancedate) from DTXFIWARNTS_DBT war3 where war3.T_WARRANTID = war2.t_warrantid and war3.t_replstate=1) ";
      cmd = RsdCommand( SQL);
      rs = RsdRecordSet(cmd);      

      // Если значение нашлось - значит, купоны перескакивают вниз (3й становится 2м), обрабатываем начиная с меньших.
      if (rs.Movenext())
         SQL = ""; 
      else
         SQL = "desc";
      end;
   end;

   SQL = "select * from DTXFIWARNTS_DBT where t_replstate = 0 and t_action = " + Action + " and trunc(t_instancedate) = " + t_instancedate + " order by t_fiid, t_drawingdate " + SQL;
   cmd = RsdCommand( SQL);
   rs = RsdRecordSet(cmd);

   while (rs.Movenext)
      Счетчик = Счетчик + 1;

      ProgressIndicator.Update(Int(Счетчик), MaxVal, "Обработка купонов/амортизаций...");
   
      if (not ProcessTrn(Null, "Process_Coupon"))
      end;
   end;

   ProgressIndicator.Stop();

   return TRUE;
OnError(er)
   ЗанестиЗаписьВПротокол(600, "", "", "", StrFor(1), "Ошибка: " + er.message, date(t_instancedate));
end;