/*
$Name:             ws_txrepldeal.mac
$Module:           БОЦБ
$Description:      Макрос сервисов скроллинга сделок для репликации
*/

import FIInter;
import "ws_common.mac";
import "ws_orderbygen.mac";
import "ws_datafilter.mac";

macro TxReplDealRunError(err:Variant, stat:Integer)
  WSRunError("ws_txrepldeal.mac", err, stat);
end;

class CTxReplDealList()
  var DealID:String;           // Идентификатор
  var InstanceDate:DateTime;   // Дата загрузки
  var Action:Integer;          // Действие
  var ActionName:String;       // Наименование действия
  var ReplState:Integer;       // Наименование состояния репликации
  var ReplStateName:String;    // Состояние репликации
  var Kind:Integer;            // Вид операции
  var KindName:String;         // Наименование вида операции
  var Code:String;             // Внутренний код
  var ExtCode:String;          // Внешний код
  var PartyCode:String;        // Код у контрагента
  var MarketCode:String;       // Код на бирже
  var Amount:Money;            // Количество
  var AvoirissID:String;       // Ценная бумага
  var AvoirissName:String;     // Ценная бумага
  var Date:Date;               // Дата заключения сделки
  var ContrDate:Date;          // Дата договора
  var CurrencyName:String;     // Валюта сделки
  var Price:Money;             // Цена
  var Cost:Money;              // Стоимость
  var Nkd:Money;               // НКД
  var TotalCost:Money;         // Общая сумма
  var SuplDate:Date;           // Плановая дата поставки
  var PayDate:Date;            // Плановая дата оплаты
  var Rate:Money;              // Ставка
  var RepoBase:Integer;        // База расчета
  var Price2:Money;            // Цена 2ч
  var Cost2:Money;             // Стоимость 2ч
  var Nkd2:Money;              // НКД 2ч
  var TotalCost2:Money;        // Общая сумма 2ч
  var SuplDate2:Date;          // Плановая дата поставки 2ч
  var PayDate2:Date;           // Плановая дата оплаты 2ч
  var CloseDate:Date;          // Плановая дата завершения
  var TsKind:String;           // Режим торгов (на ММВБ)
  var AccountTypeName:String;  // Тип расчетов
  var Point:Integer;           // Точность
  var MarketID:String;         // Биржа
  var MarketName:String;       // Наименование биржи
  var SectorName:String;       // Наименование сектора
  var BrokerID:String;         // Брокер
  var BrokerName:String;       // Наименование брокера
  var PartyID:String;          // Контрагент
  var PartyName:String;        // Наименование контрагента
  var WarrantID:String;        // Идентификатор купона
  var PartialID:String;        // Идентификатор ЧП
  var ContrNum:String;         // Номер договора
  var TechType:Integer;        // Признак техн. сделки
  var IsCostChange:Bool;       // Признак изменения стоимости при выплате купонного дохода/дивидентов/частичного погашения ценных бумаг
  var IsCostChangeOnAmor:Bool; // Признак изменения стоимости при выплате амортизации
  var IsCostChangeOnComp:Bool; // Признак изменения стоимости при выплате компенсационного взноса
  var Version:Integer;         // Версия записи
end;

class TWsReplDealListItem
  var DealID:String;
  var InstanceDate:DateTime;
  var ExtCode:String;
  var Code:String;
end;

class TWsReplDealFltrParm()
  var FindDealID:String;
  var LikeDealID:String;
end;

private macro GetSQLAvoirissIDCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLTxAvoirssConditionEx("txdeal", "t_AvoirissID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txdeal.t_AvoirissID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLMarketIDCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLPartyConditionEx("txdeal", "t_MarketID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txdeal.t_MarketID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLPartyIDCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLPartyConditionEx("txdeal", "t_PartyID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txdeal.t_PartyID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLBrokerIDCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLPartyConditionEx("txdeal", "t_BrokerID", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txdeal.t_BrokerID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLDealKindCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLNameAlgConditionEx("txdeal", "t_Kind", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txcomiss.t_Type = 0 ";
  end;

  return strSQLCond;
end;

private macro GetReplDealAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String
  var fp = null;

  fp = FilterParser(FilterItems);

  fp.AddFieldCondition(0, "txdeal", "t_InstanceDate");     // Записи за период
  fp.AddFieldCondition(1, "txdeal", "t_ReplState");        // Статус записи
  fp.AddFieldCondition(2, "txdeal", "t_Action"); 	         // Вид действия
  fp.AddFieldCondition(3, "txdeal", "t_DealID", " = -1 "); // Идентификатор записи
  fp.AddUserFieldCondition(4, @GetSQLDealKindCondition);   // Вид сделки
  fp.AddUserFieldCondition(5, @GetSQLAvoirissIDCondition); // Ценная бумага
  fp.AddFieldCondition(6, "txdeal", "t_Date");             // Дата заключения
  fp.AddFieldCondition(7, "txdeal", "t_Code");             // Внутренний код
  fp.AddFieldCondition(8, "txdeal", "t_ExtCode");          // Внешний код
  fp.AddUserFieldCondition(9, @GetSQLMarketIDCondition);   // Биржа
  fp.AddUserFieldCondition(10, @GetSQLPartyIDCondition);   // Контрагент
  fp.AddUserFieldCondition(11, @GetSQLBrokerIDCondition);  // Брокер

  return fp.GetFullSQLCondition();
end;

private macro GetReplDealSortOrder(
  OrderItems//:TArray of OrderItem // Параметры сортировки
 ,DefaultSQLSortOrder:String       // Строка сортировки по умолчанию
):String
  var ordrGen = null;

  ordrGen = OrderByGenerator(OrderItems, DefaultSQLSortOrder, false, true);

  return ordrGen.GetFullSQLSortOrder();
end;

// Получить общее количество записей скроллинга сделок для репликации
macro TxGetReplDealCount(
  FltrParm
 ,FilterItems//:TArray of FilterCondItem // Массив элементов фильтра скроллинга
):Integer
  var Query = "", AddWhere = "";
  var Count = 0;

  Query = " SELECT "
            + " 1 "
        + " FROM "
            + " dtxdeal_dbt txdeal "
        + " WHERE "
            + " 1 = 1 ";

  if(FltrParm != null)
    if((FltrParm.FindDealID != null) and (FltrParm.FindDealID != ""))
      Query = Query + " AND txdeal.t_DealID = " + FltrParm.FindDealID + " ";
    end;

    if(FltrParm.LikeDealID != null)
      Query = Query + " AND TO_CHAR(txdeal.t_DealID) LIKE '%" + FltrParm.LikeDealID + "%' ";
    end;
  end;

  AddWhere = GetReplDealAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  Count = SQL_GetNRecs(Query);

  return Count;

OnError(err)
  TxReplDealRunError(err, -1);
end;

private class (RsdRecordset) CReplDealSet(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FltrParm//:TWsReplDealFltrParm              // Значения фильтрации
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
)
  private var m_Query = "", m_AddWhere = "", m_OrderBy = "", m_Cmd = null;

  if(PageNum == null)
    RunError("Не задан обязательный параметр функции: номер страницы");
  end;

  if(PageSize == null)
    RunError("Не задан обязательный параметр функции: размер страницы");
  end;

  m_Query = " SELECT "
              + " TO_CHAR(txdeal.t_DealID) AS t_DealID "
             + " ,txdeal.t_InstanceDate "
             + " ,txdeal.t_Action "
             + " ,NVL((SELECT "
                       + " namealg.t_szNameAlg "
                   + " FROM "
                       + " dnamealg_dbt namealg "
                   + " WHERE "
                       + " namealg.t_iTypeAlg = 7643 "
                   + " AND namealg.t_iNumberAlg = txdeal.t_Action), CHR(0)) AS t_ActionName "
             + " ,txdeal.t_ReplState "
             + " ,NVL((SELECT "
                       + " namealg.t_szNameAlg "
                   + " FROM "
                       + " dnamealg_dbt namealg "
                   + " WHERE "
                       + " namealg.t_iTypeAlg = 7642 "
                   + " AND namealg.t_iNumberAlg = txdeal.t_ReplState), CHR(0)) AS t_ReplStateName "
             + " ,txdeal.t_Kind "
             + " ,NVL((SELECT "
                       + " namealg.t_szNameAlg "
                   + " FROM "
                       + " dnamealg_dbt namealg "
                   + " WHERE "
                       + " namealg.t_iTypeAlg = 7644 "
                   + " AND namealg.t_iNumberAlg = txdeal.t_Kind), CHR(0)) AS t_KindName "
             + " ,txdeal.t_Code "
             + " ,txdeal.t_ExtCode "
             + " ,txdeal.t_PartyCode "
             + " ,txdeal.t_MarketCode "
             + " ,txdeal.t_Amount "
             + " ,TO_CHAR(txdeal.t_AvoirissID) AS t_AvoirissID "
             + " ,NVL((SELECT "
                       + " MAX(txavoir.t_ShortName) "
                   + " FROM "
                       + " dtxavoiriss_dbt txavoir "
                   + " WHERE "
                       + " txavoir.t_AvoirissID = txdeal.t_AvoirissID), CHR(0)) AS t_AvoirissName "
             + " ,txdeal.t_Date "
             + " ,txdeal.t_ContrDate "
             + " ,NVL((SELECT "
                       + " MAX(txcur.t_Name) "
                   + " FROM "
                       + " dtxcurrency_dbt txcur "
                   + " WHERE "
                       + " txcur.t_FIID = txdeal.t_CurrencyID), CHR(0)) AS t_CurrencyName "
             + " ,txdeal.t_Price "
             + " ,txdeal.t_Cost "
             + " ,txdeal.t_Nkd "
             + " ,txdeal.t_TotalCost "
             + " ,txdeal.t_SuplDate "
             + " ,txdeal.t_PayDate "
             + " ,txdeal.t_Rate "
             + " ,txdeal.t_RepoBase "
             + " ,txdeal.t_Price2 "
             + " ,txdeal.t_Cost2 "
             + " ,txdeal.t_Nkd2 "
             + " ,txdeal.t_TotalCost2 "
             + " ,txdeal.t_SuplDate2 "
             + " ,txdeal.t_PayDate2 "
             + " ,txdeal.t_CloseDate "
             + " ,DECODE(txdeal.t_TsKind, 'P', 'Аукцион' "
                                     + " ,'T', 'Системная' "
                                     + " ,'N', 'Внесистемная' "
                                     + " ,'L', 'Неполные лоты' "
                                     + " ,'F', 'Перевод' "
                                     + " ,'R', 'РЕПО' "
                                     + " ,CHR(0)) AS t_TsKind "
             + " ,NVL((SELECT "
                       + " namealg.t_szNameAlg "
                   + " FROM "
                       + " dnamealg_dbt namealg "
                   + " WHERE "
                       + " namealg.t_iTypeAlg = 7645 "
                   + " AND namealg.t_iNumberAlg = txdeal.t_AccountType), CHR(0)) AS t_AccountTypeName "
             + " ,txdeal.t_Point "
             + " ,TO_CHAR(NVL(txdeal.t_MarketID, -1)) AS t_MarketID "
             + " ,NVL((SELECT "
                       + " MAX(txparty.t_shortname) "
                   + " FROM "
                       + " dtxparty_dbt txparty "
                   + " WHERE "
                       + " txparty.t_partyid = txdeal.t_MarketID), CHR(0)) AS t_MarketName "
             + " ,NVL((SELECT "
                      + " MAX(txmarksect.t_name) "
                   + " FROM "
                      + " dtxmarksect_dbt txmarksect "
                   + " WHERE "
                      + " txmarksect.t_MarketSectorID = txdeal.t_Sector), CHR(0)) AS t_SectorName "
             + " ,TO_CHAR(NVL(txdeal.t_BrokerID, -1)) AS t_BrokerID "
             + " ,NVL((SELECT "
                       + " MAX(txparty.t_shortname) "
                   + " FROM "
                       + " dtxparty_dbt txparty "
                   + " WHERE "
                       + " txparty.t_PartyID = txdeal.t_BrokerID), CHR(0)) AS t_BrokerName "
             + " ,TO_CHAR(NVL(txdeal.t_PartyID, -1)) AS t_PartyID "
             + " ,NVL((SELECT "
                       + " MAX(txparty.t_shortname) "
                   + " FROM "
                       + " dtxparty_dbt txparty "
                   + " WHERE "
                       + " txparty.t_PartyID = txdeal.t_PartyID), CHR(0)) AS t_PartyName "
             + " ,TO_CHAR(txdeal.t_WarrantID) AS t_WarrantID "
             + " ,TO_CHAR(txdeal.t_PartialID) AS t_PartialID "
             + " ,txdeal.t_ContrNum "
             + " ,txdeal.t_TechType "
             + " ,txdeal.t_CostChange "
             + " ,txdeal.t_CostChangeOnAmor "
             + " ,txdeal.t_CostChangeOnComp "
             + " ,txdeal.t_Version "
          + " FROM "
              + " dtxdeal_dbt txdeal "
          + " WHERE "
              + " 1 = 1 ";

  if(FltrParm != null)
    if((FltrParm.FindDealID != null) and (FltrParm.FindDealID != ""))
      m_Query = m_Query + " AND txdeal.t_DealID = " + FltrParm.FindDealID + " ";
    end;

    if(FltrParm.LikeDealID != null)
      m_Query = m_Query + " AND TO_CHAR(txdeal.t_DealID) LIKE '%" + FltrParm.LikeDealID + "%' ";
    end;
  end;

  m_AddWhere = GetReplDealAddWhereCondition(FilterItems);
  if(m_AddWhere != "")
    m_Query = m_Query + " AND " + m_AddWhere;
  end;

  m_OrderBy = GetReplDealSortOrder(OrderItems, " txdeal.t_DealID ASC, txdeal.t_InstanceDate ASC, txdeal.t_Action ASC, txdeal.t_ReplState ASC ");
  if(m_OrderBy != "")
    m_Query = m_Query + " ORDER BY " + m_OrderBy;
  end;

  m_Query = SQL_WrapSelect(m_Query, PageNum, PageSize);

  m_Cmd = RSDCommand(m_Query);
  m_Cmd.NullConversion = true;
  m_Cmd.Execute();

  initRsdRecordset(m_Cmd);
end;

// Отбор данных для скроллинга сделок
macro TxGetReplDealList(
  PageSize:Integer                            // Размер страницы
 ,PageNum:Integer                             // Номер страницы
 ,FltrParm//:TWsReplDealFltrParm              // Значения фильтрации
 ,FilterItems//:TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//:TArray of OrderItem            // Параметры сортировки
)
  var ReplDealSet = null;
  var ReplDeal = null;
  var ReplDealList = TArray();

  ReplDealSet = CReplDealSet(PageSize, PageNum, FltrParm, FilterItems, OrderItems);

  while(ReplDealSet.MoveNext())
    ReplDeal = CTxReplDealList();

    ReplDeal.DealID = SQL_ConvTypeStr(ReplDealSet.Value("t_DealID"));
    ReplDeal.InstanceDate = SQL_ConvTypeDateTime(ReplDealSet.Value("t_InstanceDate"));
    ReplDeal.Action = SQL_ConvTypeInteger(ReplDealSet.Value("t_Action"));
    ReplDeal.ActionName = SQL_ConvTypeStr(ReplDealSet.Value("t_ActionName"));
    ReplDeal.ReplState = SQL_ConvTypeInteger(ReplDealSet.Value("t_ReplState"));
    ReplDeal.ReplStateName = SQL_ConvTypeStr(ReplDealSet.Value("t_ReplStateName"));
    ReplDeal.Kind = SQL_ConvTypeInteger(ReplDealSet.Value("t_Kind"));
    ReplDeal.KindName = SQL_ConvTypeStr(ReplDealSet.Value("t_KindName"));
    ReplDeal.Code = SQL_ConvTypeStr(ReplDealSet.Value("t_Code"));
    ReplDeal.ExtCode = SQL_ConvTypeStr(ReplDealSet.Value("t_ExtCode"));
    ReplDeal.PartyCode = SQL_ConvTypeStr(ReplDealSet.Value("t_PartyCode"));
    ReplDeal.MarketCode = SQL_ConvTypeStr(ReplDealSet.Value("t_MarketCode"));
    ReplDeal.Amount = SQL_ConvTypeSum(ReplDealSet.Value("t_Amount"));
    ReplDeal.AvoirissID = SQL_ConvTypeStr(ReplDealSet.Value("t_AvoirissID"));
    ReplDeal.AvoirissName = SQL_ConvTypeStr(ReplDealSet.Value("t_AvoirissName"));
    ReplDeal.Date = SQL_ConvTypeDate(ReplDealSet.Value("t_Date"));
    ReplDeal.ContrDate = SQL_ConvTypeDate(ReplDealSet.Value("t_ContrDate"));
    ReplDeal.CurrencyName = SQL_ConvTypeStr(ReplDealSet.Value("t_CurrencyName"));
    ReplDeal.Price = SQL_ConvTypeSum(ReplDealSet.Value("t_Price"));
    ReplDeal.Cost = SQL_ConvTypeSum(ReplDealSet.Value("t_Cost"));
    ReplDeal.Nkd = SQL_ConvTypeSum(ReplDealSet.Value("t_Nkd"));
    ReplDeal.TotalCost = SQL_ConvTypeSum(ReplDealSet.Value("t_TotalCost"));
    ReplDeal.SuplDate = SQL_ConvTypeDate(ReplDealSet.Value("t_SuplDate"));
    ReplDeal.PayDate = SQL_ConvTypeDate(ReplDealSet.Value("t_PayDate"));
    ReplDeal.Rate = SQL_ConvTypeSum(ReplDealSet.Value("t_Rate"));
    ReplDeal.RepoBase = SQL_ConvTypeInteger(ReplDealSet.Value("t_RepoBase"));
    ReplDeal.Price2 = SQL_ConvTypeSum(ReplDealSet.Value("t_Price2"));
    ReplDeal.Cost2 = SQL_ConvTypeSum(ReplDealSet.Value("t_Cost2"));
    ReplDeal.Nkd2 = SQL_ConvTypeSum(ReplDealSet.Value("t_Nkd2"));
    ReplDeal.TotalCost2 = SQL_ConvTypeSum(ReplDealSet.Value("t_TotalCost2"));
    ReplDeal.SuplDate2 = SQL_ConvTypeDate(ReplDealSet.Value("t_SuplDate2"));
    ReplDeal.PayDate2 = SQL_ConvTypeDate(ReplDealSet.Value("t_PayDate2"));
    ReplDeal.CloseDate = SQL_ConvTypeDate(ReplDealSet.Value("t_CloseDate"));
    ReplDeal.TsKind = SQL_ConvTypeStr(ReplDealSet.Value("t_TsKind"));
    ReplDeal.AccountTypeName = SQL_ConvTypeStr(ReplDealSet.Value("t_AccountTypeName"));
    ReplDeal.Point = SQL_ConvTypeInteger(ReplDealSet.Value("t_Point"));
    ReplDeal.MarketID = SQL_ConvTypeStr(ReplDealSet.Value("t_MarketID"));
    ReplDeal.MarketName = SQL_ConvTypeStr(ReplDealSet.Value("t_MarketName"));
    ReplDeal.SectorName = SQL_ConvTypeStr(ReplDealSet.Value("t_SectorName"));
    ReplDeal.BrokerID = SQL_ConvTypeStr(ReplDealSet.Value("t_BrokerID"));
    ReplDeal.BrokerName = SQL_ConvTypeStr(ReplDealSet.Value("t_BrokerName"));
    ReplDeal.PartyID = SQL_ConvTypeStr(ReplDealSet.Value("t_PartyID"));
    ReplDeal.PartyName = SQL_ConvTypeStr(ReplDealSet.Value("t_PartyName"));
    ReplDeal.WarrantID = SQL_ConvTypeStr(ReplDealSet.Value("t_WarrantID"));
    ReplDeal.PartialID = SQL_ConvTypeStr(ReplDealSet.Value("t_PartialID"));
    ReplDeal.ContrNum = SQL_ConvTypeStr(ReplDealSet.Value("t_ContrNum"));
    ReplDeal.TechType = SQL_ConvTypeInteger(ReplDealSet.Value("t_TechType"));
    ReplDeal.IsCostChange = SQL_ConvTypeInteger(ReplDealSet.Value("t_CostChange"));
    ReplDeal.IsCostChangeOnAmor = SQL_ConvTypeInteger(ReplDealSet.Value("t_CostChangeOnAmor"));
    ReplDeal.IsCostChangeOnComp = SQL_ConvTypeInteger(ReplDealSet.Value("t_CostChangeOnComp"));
    ReplDeal.Version = SQL_ConvTypeInteger(ReplDealSet.Value("t_Version"));

    ReplDealList[ReplDealList.Size] = ReplDeal;
  end;

  return ReplDealList;

OnError(err)
  TxReplDealRunError(err, -1);
end;

private macro CheckVersion(
  DealID:String
 ,InstanceDate:DateTime
 ,Version:Integer
)
  var err = 0;
  var Query = "", Cmd = null, Set = null;

  Query = " SELECT "
            + " * "
        + " FROM "
            + " dtxdeal_dbt "
        + " WHERE "
            + " t_DealID = ? "
        + " AND t_InstanceDate = ? "
        + " AND t_Version = ? "
        + " FOR UPDATE ";

  Cmd = RsdCommand(Query);
  Cmd.AddParam("", RSDBP_IN, DealID);
  Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(InstanceDate));
  Cmd.AddParam("", RSDBP_IN, Version);
  Cmd.Execute();

  Set = RsdRecordset(Cmd);
  if(not Set.MoveNext())
    err = 70;
  end;

  return err;
end;

private macro UpdateTxDealImpl(
  StateAndAction//:CTxReplStateAndAction
 ,Deal//:CTxReplDealList
)
  var err = 0;
  var Query = "", Cmd = null;

  err = CheckVersion(Deal.DealID, Deal.InstanceDate, Deal.Version);

  if(err == 0)
    Query = " UPDATE "
              + " dtxdeal_dbt "
          + " SET ";
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Query = Query +
                " t_ReplState = ? "
             + " ,t_Action = ? ";
    elif(StateAndAction.NeedUpdateReplState)
      Query = Query +
                " t_ReplState = ? ";
    else
      Query = Query +
                " t_Action = ? ";
    end;
    Query = Query +
            " WHERE "
              + " t_DealID = ? "
          + " AND t_InstanceDate = ? ";

    Cmd = RsdCommand(Query);
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    elif(StateAndAction.NeedUpdateReplState)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
    else
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    end;
    Cmd.AddParam("", RSDBP_IN, Deal.DealID);
    Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(Deal.InstanceDate));
    Cmd.Execute();
  end;

  return err;
end;

macro TxUpdateDealList(
  StateAndAction//:CTxReplStateAndAction
 ,DealList//:TArray of CTxReplDealList
)
  var err = 0;
  var i = 0;

  if((StateAndAction.NeedUpdateReplState
   or StateAndAction.NeedUpdateAction)
 and (DealList != null))
    while((err == 0) and (i < DealList.Size))
      err = UpdateTxDealImpl(StateAndAction, DealList[i]);
      i = i + 1;
    end;
  end;

  return err;

OnError(err)
  TxReplDealRunError(err, -1);
end;

macro TxGetDealListItemCount(
  FltrParm
 ,FilterItems
)
  return TxGetReplDealCount(FltrParm, FilterItems);
end;

macro TxGetDealListItems(
  PageSize:Integer
 ,PageNum:Integer
 ,FltrParm
 ,FilterItems
 ,OrderItems
)
  var ReplDealSet = null;
  var ReplDealListItem = null;
  var ReplDealList = TArray();

  ReplDealSet = CReplDealSet(PageSize, PageNum, FltrParm, FilterItems, OrderItems);

  while(ReplDealSet.MoveNext())
    ReplDealListItem = TWsReplDealListItem();

    ReplDealListItem.DealID = SQL_ConvTypeStr(ReplDealSet.Value("t_DealID"));
    ReplDealListItem.InstanceDate = SQL_ConvTypeDateTime(ReplDealSet.Value("t_InstanceDate"));
    ReplDealListItem.ExtCode = SQL_ConvTypeStr(ReplDealSet.Value("t_ExtCode"));
    ReplDealListItem.Code = SQL_ConvTypeStr(ReplDealSet.Value("t_Code"));

    ReplDealList[ReplDealList.Size] = ReplDealListItem;
  end;

  return ReplDealList;

OnError(err)
  TxReplDealRunError(err, -1);
end;

///////////////////////////////////////////////////////////
// Возвращает список длинны listLen по
// частичному совпадению значения
///////////////////////////////////////////////////////////
macro LookupReplDeal(
  FltrParm
 ,ListLen
)
  return TxGetDealListItems(ListLen, 0, FltrParm, null, null);
end;
