/*
$Name:             ws_txreplmarketsector.mac
$Module:           АРНУ
$Description:      WEB. АРНУ. Макрос сервисов виджета секторов биржи для репликации
*/

import "cb_sql.mac";
import "ws_common.mac";
import "ws_datafilter.mac";
import "ws_orderbygen.mac";

macro TxReplMarketSectorRunError(err:Variant, stat:Integer)
  WSRunError("ws_txreplmarketsector.mac", err, stat);
end;

class TWsReplMarketSectorListItem()
  var MarketSectorID:String;
  var InstanceDate:DateTime;
  var Name:String;
  var Code:String;
end;

class TWsReplMarketSectorFltrParm()
  var FindMarketSectorID:String;
  var LikeName:String;
end;

private macro GetReplMarketSectorAddWhereCondition(
  FilterItems//:TArray of FilterCondItem // Массив элементов фильтра скроллинга
):String
  var fp = null;

  fp = FilterParser(FilterItems);

  return fp.GetFullSQLCondition();
end;

private macro GetReplMarketSectorSortOrder(
  OrderItems//:TArray of OrderItem // Параметры сортировки
 ,DefaultSQLSortOrder:String       // Строка сортировки по умолчанию
):String
  var ordrGen = null;

  ordrGen = OrderByGenerator(OrderItems, DefaultSQLSortOrder, false, true);

  return ordrGen.GetFullSQLSortOrder();
end;

macro TxGetMarketSectorListItemCount(
  FltrParm
 ,FilterItems
):Integer
  var Query = "", AddWhere = "";
  var Count = 0;

  Query = " SELECT "
            + " 1 "
        + " FROM "
            + " dtxmarksect_dbt txmarksect "
        + " WHERE "
            + " 1 = 1 ";

  if(FltrParm != null)
    if((FltrParm.FindMarketSectorID != null) and (FltrParm.FindMarketSectorID != ""))
      Query = Query + " AND txmarksect.t_MarketSectorID = " + FltrParm.FindMarketSectorID + " ";
    end;

    if(FltrParm.LikeName != null)
      Query = Query + " AND LOWER(txmarksect.t_Name) LIKE LOWER('%" + FltrParm.LikeName + "%') ";
    end;
  end;

  AddWhere = GetReplMarketSectorAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  Count = SQL_GetNRecs(Query);

  return Count;

OnError(err)
  TxReplMarketSectorRunError(err, -1);
end;

macro TxGetMarketSectorListItems(
  PageSize:Integer
 ,PageNum:Integer
 ,FltrParm//:TWsReplMarketSectorFltrParm
 ,FilterItems//:TArray of FilterCondItem
 ,OrderItems//:TArray of OrderItem
):TArray
  var Query = "", AddWhere = "", OrderBy = "", Cmd = null, Set = null;
  var ReplMarketSectorListItem = null;
  var ReplMarketSectorList = TArray();

  Query = " SELECT "
            + " TO_CHAR(txmarksect.t_MarketSectorID) AS t_MarketSectorID "
           + " ,txmarksect.t_InstanceDate "
           + " ,txmarksect.t_Name "
           + " ,txmarksect.t_Code "
        + " FROM "
            + " dtxmarksect_dbt txmarksect "
        + " WHERE "
            + " 1 = 1 ";

  if(FltrParm != null)
    if((FltrParm.FindMarketSectorID != null) and (FltrParm.FindMarketSectorID != ""))
      Query = Query + " AND txmarksect.t_MarketSectorID = " + FltrParm.FindMarketSectorID + " ";
    end;

    if(FltrParm.LikeName != null)
      Query = Query + " AND LOWER(txmarksect.t_Name) LIKE LOWER('%" + FltrParm.LikeName + "%') ";
    end;
  end;

  AddWhere = GetReplMarketSectorAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  OrderBy = GetReplMarketSectorSortOrder(OrderItems, " txmarksect.t_Code ASC ");
  if(OrderBy != "")
    Query = Query + " ORDER BY " + OrderBy;
  end;

  Query = SQL_WrapSelect(Query, PageNum, PageSize);

  Cmd = RSDCommand(Query);
  Cmd.NullConversion = true;
  Cmd.Execute();

  Set = RsdRecordset(Cmd);

  while(Set.MoveNext())
    ReplMarketSectorListItem = TWsReplMarketSectorListItem();

    ReplMarketSectorListItem.MarketSectorID = SQL_ConvTypeStr(Set.Value("t_MarketSectorID"));
    ReplMarketSectorListItem.InstanceDate = SQL_ConvTypeDateTime(Set.Value("t_InstanceDate"));
    ReplMarketSectorListItem.Name = SQL_ConvTypeStr(Set.Value("t_Name"));
    ReplMarketSectorListItem.Code = SQL_ConvTypeStr(Set.Value("t_Code"));

    ReplMarketSectorList[ReplMarketSectorList.Size] = ReplMarketSectorListItem;
  end;

  return ReplMarketSectorList;

OnError(err)
  TxReplMarketSectorRunError(err, -1);
end;

///////////////////////////////////////////////////////////
// Возвращает список длинны listLen по
// частичному совпадению значения
///////////////////////////////////////////////////////////
macro LookupReplMarketSector(
  FltrParm
 ,ListLen
):TArray
  return TxGetMarketSectorListItems(ListLen, 0, FltrParm, null, null);
end;
