/*
$Name:             ws_txreplpartykind.mac
$Module:           АРНУ
$Description:      WEB. АРНУ. Макрос сервисов скроллинга видов субъектов для репликации
*/

import FIInter;
import "ws_common.mac";
import "ws_datafilter.mac";
import "ws_orderbygen.mac";

macro TxReplPartykindRunError(err:Variant, stat:Integer)
  WSRunError("ws_txreplpartykind.mac", err, stat);
end;

// Класс скроллинга видов субъектов для репликации
class CTxReplPartykindList()
  var PartyID:String;        // Идентификатор
  var InstanceDate:DateTime; // Дата загрузки
  var Action:Integer;        // Действие
  var ActionName:String;     // Наименование действия
  var ReplState:Integer;     // Статус репликации
  var ReplStateName:String;  // Статус репликации
  var PartyKind:Integer;     // Принадлежность
  var PartyKindName:String;  // Принадлежность
  var Version:Integer;       // Версия записи
end;

private macro GetSQLPartykindCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond:String = GetSQLNameAlgConditionEx("txpartykind", "t_PartyKind", condition, value, type);

  if(strSQLCond == "")
    strSQLCond = " txpartykind.t_PartyKind = 0 ";
  end;

  return strSQLCond;
end;

private macro GetReplPartykindAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String
  var fp = null;

  fp = FilterParser(FilterItems);

  fp.AddFieldCondition(0, "txpartykind", "t_InstanceDate");      // Записи за период
  fp.AddFieldCondition(1, "txpartykind", "t_ReplState");         // Статус записи
  fp.AddFieldCondition(2, "txpartykind", "t_Action");            // Вид действия
  fp.AddFieldCondition(3, "txpartykind", "t_PartyID", " = -1 "); // Идентификатор записи
  fp.AddUserFieldCondition(4, @GetSQLPartykindCondition);        // Принадлежность

  return fp.GetFullSQLCondition();
end;

private macro GetReplPartykindSortOrder(
  OrderItems//:TArray of OrderItem // Параметры сортировки
 ,DefaultSQLSortOrder:String       // Строка сортировки по умолчанию
):String
  var ordrGen = null;

  ordrGen = OrderByGenerator(OrderItems, DefaultSQLSortOrder, false, true);

  return ordrGen.GetFullSQLSortOrder();
end;

// Получить общее количество записей скроллинга Ц/Б
macro TxGetReplPartykindCount(
  FilterItems//:TArray of FilterCondItem // Массив элементов фильтра скроллинга
):Integer
  var Query = "", AddWhere = "";
  var Count = 0;

  Query = " SELECT "
            + " 1 "
        + " FROM "
            + " dtxpartykind_dbt txpartykind "
        + " WHERE "
            + " 1 = 1 ";

  AddWhere = GetReplPartykindAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  Count = SQL_GetNRecs(Query);

  return Count;

OnError(err)
  TxReplPartykindRunError(err, -1);
end;

// Отбор данных для скроллинга Ц/Б
macro TxGetReplPartykindList(
  PageSize:Integer                             // Размер страницы
 ,PageNum:Integer                              // Номер страницы
 ,FilterItems//: TArray of FilterConditionItem // Значения фильтрации
 ,OrderItems//: TArray of OrderItem            // Параметры сортировки
):TArray
  var Query = "", AddWhere = "", OrderBy = "", Cmd = null, Set = null;
  var ReplPartykind = null;
  var ReplPartykindList = TArray();

  if(PageNum == null)
    RunError("Не задан обязательный параметр функции: номер страницы");
  end;

  if(PageSize == null)
    RunError("Не задан обязательный параметр функции: размер страницы");
  end;

  Query = " SELECT "
            + " TO_CHAR(txpartykind.t_PartyID) AS t_PartyID "
           + " ,txpartykind.t_InstanceDate "
           + " ,txpartykind.t_Action "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7643 "
                 + " AND namealg.t_iNumberAlg = txpartykind.t_Action), CHR(0)) AS t_ActionName "
           + " ,txpartykind.t_ReplState "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7642 "
                 + " AND namealg.t_iNumberAlg = txpartykind.t_ReplState), CHR(0)) AS t_ReplStateName "
           + " ,txpartykind.t_PartyKind "
           + " ,NVL((SELECT "
                     + " namealg.t_szNameAlg "
                 + " FROM "
                     + " dnamealg_dbt namealg "
                 + " WHERE "
                     + " namealg.t_iTypeAlg = 7653 "
                 + " AND namealg.t_iNumberAlg = txpartykind.t_PartyKind), CHR(0)) AS t_PartyKindName "
           + " ,txpartykind.t_Version "
        + " FROM "
            + " dtxpartykind_dbt txpartykind "
        + " WHERE "
            + " 1 = 1 ";

  AddWhere = GetReplPartykindAddWhereCondition(FilterItems);
  if(AddWhere != "")
    Query = Query + " AND " + AddWhere;
  end;

  OrderBy = GetReplPartykindSortOrder(OrderItems, " txpartykind.t_PartyID ASC, txpartykind.t_InstanceDate ASC, txpartykind.t_Action ASC, txpartykind.t_ReplState ASC ");
  if(OrderBy != "")
    Query = Query + " ORDER BY " + OrderBy;
  end;

  Query = SQL_WrapSelect(Query, PageNum, PageSize);

  Cmd = RSDCommand(Query);
  Cmd.NullConversion = true;
  Cmd.Execute();

  Set = RsdRecordset(Cmd);

  while(Set.MoveNext())
    ReplPartykind = CTxReplPartyKindList();

    ReplPartykind.PartyID = SQL_ConvTypeStr(Set.Value("t_PartyID"));
    ReplPartykind.InstanceDate = SQL_ConvTypeDateTime(Set.Value("t_InstanceDate"));
    ReplPartykind.Action = SQL_ConvTypeInteger(Set.Value("t_Action"));
    ReplPartykind.ActionName = SQL_ConvTypeStr(Set.Value("t_ActionName"));
    ReplPartykind.ReplState = SQL_ConvTypeInteger(Set.Value("t_ReplState"));
    ReplPartykind.ReplStateName = SQL_ConvTypeStr(Set.Value("t_ReplStateName"));
    ReplPartykind.PartyKind = SQL_ConvTypeInteger(Set.Value("t_PartyKind"));
    ReplPartykind.PartyKindName = SQL_ConvTypeStr(Set.Value("t_PartyKindName"));
    ReplPartykind.Version = SQL_ConvTypeInteger(Set.Value("t_Version"));

    ReplPartykindList[ReplPartykindList.Size] = ReplPartykind;
  end;

  return ReplPartykindList;

OnError(err)
  TxReplPartykindRunError(err, -1);
end;

private macro CheckVersion(
  PartyID:String
 ,PartyKind:String
 ,InstanceDate:DateTime
 ,Version:Integer
)
  var err = 0;
  var Query = "", Cmd = null, Set = null;

  Query = " SELECT "
            + " * "
        + " FROM "
            + " dtxpartykind_dbt "
        + " WHERE "
            + " t_PartyID = ? "
        + " AND t_PartyKind = ? "
        + " AND t_InstanceDate = ? "
        + " AND t_Version = ? "
        + " FOR UPDATE ";

  Cmd = RsdCommand(Query);
  Cmd.AddParam("", RSDBP_IN, PartyID);
  Cmd.AddParam("", RSDBP_IN, PartyKind);
  Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(InstanceDate));
  Cmd.AddParam("", RSDBP_IN, Version);
  Cmd.Execute();

  Set = RsdRecordset(Cmd);
  if(not Set.MoveNext())
    err = 70;
  end;

  return err;
end;

private macro UpdateTxPartykindImpl(
  StateAndAction//:CTxReplStateAndAction
 ,Partykind//:CTxReplPartykindList
)
  var err = 0;
  var Query = "", Cmd = null;

  err = CheckVersion(Partykind.PartyID, Partykind.PartyKind, Partykind.InstanceDate, Partykind.Version);

  if(err == 0)
    Query = " UPDATE "
              + " dtxpartykind_dbt "
          + " SET ";
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Query = Query +
                " t_ReplState = ? "
             + " ,t_Action = ? ";
    elif(StateAndAction.NeedUpdateReplState)
      Query = Query +
                " t_ReplState = ? ";
    else
      Query = Query +
                " t_Action = ? ";
    end;
    Query = Query +
            " WHERE "
              + " t_PartyID = ? "
          + " AND t_PartyKind = ? "
          + " AND t_InstanceDate = ? ";

    Cmd = RsdCommand(Query);
    if(StateAndAction.NeedUpdateReplState and StateAndAction.NeedUpdateAction)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    elif(StateAndAction.NeedUpdateReplState)
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.ReplState));
    else
      Cmd.AddParam("", RSDBP_IN, SP_GetNameAlgNumberAlg(StateAndAction.Action));
    end;
    Cmd.AddParam("", RSDBP_IN, Partykind.PartyID);
    Cmd.AddParam("", RSDBP_IN, Partykind.PartyKind);
    Cmd.AddParam("", RSDBP_IN, SQL_ConvTypeDateTime(Partykind.InstanceDate));
    Cmd.Execute();
  end;

  return err;
end;

macro TxUpdatePartykindList(
  StateAndAction//:CTxReplStateAndAction
 ,PartykindList//:TArray of CTxReplPartykindList
)
  var err = 0;
  var i = 0;

  if((StateAndAction.NeedUpdateReplState
   or StateAndAction.NeedUpdateAction)
 and (PartykindList != null))
    while((err == 0) and (i < PartykindList.Size))
      err = UpdateTxPartykindImpl(StateAndAction, PartykindList[i]);
      i = i + 1;
    end;
  end;

  return err;

OnError(err)
  TxReplPartykindRunError(err, -1);
end;
