/*
$Name:        TaxTransferred_Report.mac
$Module:      Ценные бумаги
$Description: Уведомление по НДФЛ
*/

import "DLReport_h.mac", "TaxTransferred_Form.mac", "dl_xml.mac", "dlmisc.mac", "TaxReportBase.mac";

private var Form;

CLASS KBKWithTAXPaid
   var KBK;
   var TaxPaid;
end;

CLASS (DL_XML) Tax_Transferred_XML
   macro CreateXMLObj()
      CreateXML();
   end;

   macro GetXMLObj()
      return m_xml;
   end;

   macro SetFileName(fileName)
      if(fileName != NULL)
         m_FileName = fileName;
      end;
   end;

   macro GetFileName()
      return m_FileName;
   end;

   macro CreateAttr(AttrName:String, AttrValue:variant)
      return CreateAttr(AttrName, AttrValue);
   end;

   macro SaveXML()
      var NameFile = GetFileName(); 
      var DirFileLen = 0;
      var fpth, dlmt = "\\";
    
      var NameFileTxt = GetTxtFileName(NameFile); 
      fpth = string("$", GetCurDir(true), dlmt, "TXTFILE", dlmt);

      var nm, ext;
      SplitFile(NameFileTxt, nm, ext);

      var FileNameLen = strlen(nm+ext);
    
      DirFileLen = strlen(NameFileTxt);

      var NamePath = substr(NameFileTxt, 1, DirFileLen - FileNameLen);
      m_xml.save(NamePath + NameFile);
      
      CopyFile(NameFile, string(fpth, NameFile));
   end;

   macro Save()
      SaveXML();
   end;

   macro CreateNode(nodeName /* attrName, attrValue, ... */)
      var node = NULL;
      var numParm = 2;
      var attrName = "", attrValue = "";

      node = m_xml.createNode(1, nodeName, NameSpace);
      while(GetParm(numParm, attrName))
        GetParm(numParm + 1, attrValue);
        node.setAttributeNode(CreateAttr(attrName, attrValue));
        numParm = numParm + 2;
      end;
  
      return node;
   end;

   InitDL_XML();
   m_FileName = "UT_UVISCHSUMNAL_0000_0000_0000000000000000000_00000000_0";
END;

CLASS (/*DL_CReportTemplate*/TaxReportBase) Tax_Transferred_Report()
   private var m_Month;
   private var m_MonthCode;
   private var m_PeriodCode;
   private var m_Year;
   private var m_InXML;
   private var m_SignerPartyID;
   private var m_SignerName;
   private var m_RepDate;
   private var m_BeginDate, m_EndDate;
   private var m_OurINN;
   private var m_OurKPP;
   private var m_OurFNSCode;
   private var m_FIOperson1;
   private var m_FirstPersonName1, m_FirstPersonName2, m_FirstPersonName3;
   private var m_ProcuratoryNumber;
   private var m_ProcuratoryDate;
   private var m_OurName;
   private var m_OurOKTMO;
   private var m_XmlFile;
   private var m_KBKWithTAXPaidArr:TArray;
   private var m_Messages:TArray;
   private var m_NegativeMessages:TArray;

   //Инициализируем массив с КБК из справочника
   private macro InitKBKWithTAXPaidArr()
      var query = RSDCommand("Select t_name from DLLVALUES_DBT WHERE T_LIST = 3522 ORDER BY T_ELEMENT ASC");
      var rs_KBK = RSDRecordset(query);

      while(rs_KBK.MoveNext())
         var tmp_KBKWithTAXPaid = KBKWithTAXPaid();
         tmp_KBKWithTAXPaid.KBK = rs_KBK.value("t_name");

         m_KBKWithTAXPaidArr[m_KBKWithTAXPaidArr.Size] = tmp_KBKWithTAXPaid;
      end;

      if(m_KBKWithTAXPaidArr.Size <= 0)
         MsgBox("Отсутствуют значения КБК");
      end;
   end;

   //Добавление сообщения в массив сообщений для печати (вместо MsgBox)
   macro Message(Mes:String)
      m_Messages[m_Messages.Size] = Mes;
   end;

   macro NegativeMessage(Mes:String)
      m_NegativeMessages[m_NegativeMessages.size] = Mes;
   end;

   private macro GetLastWorkDay():DATE
      var queryDay = "select RSI_NPTX6CALC.GetLastWorkDayYear(?) as t_LastWorkDay " +
                     "from dual";

      var cmdDay = DL_RSDCommand(queryDay);
      cmdDay.AddParam(m_BeginDate);

      var DataSetDay = cmdDay.Execute();

      if(DataSetDay.MoveNext())
         return Date(DataSetDay.LastWorkDay);
      else
         return ZeroDate;
      end;
   end;

   private macro GetLastDayOfMonth(theMonth:DATE):DATE;
      var queryMonthLastDay =  "SELECT LAST_DAY( TO_DATE('" + string(theMonth) + "', 'dd.mm.yyyy')) AS LastMonthDay  FROM DUAL";
      var cmdDay = DL_RSDCommand(queryMonthLastDay);
      var DataSetDay = cmdDay.Execute();

      if(DataSetDay.MoveNext())
         return Date(DataSetDay.LastMonthDay);
      else
         return ZeroDate;
      end;

   end;

   private macro BeginReport()
      m_Month = Form.GetLinkedValue(PNFLD_TAX_TRANSFERRED_MONTH);
      m_MonthCode = string(Form.GetLinkedValue(PNFLD_TAX_TRANSFERRED_MONTH_CODE));
      m_PeriodCode = string(Form.GetLinkedValue(PNFLD_TAX_TRANSFERRED_PERIOD_CODE));
      m_Year = Form.GetLinkedValue(PNFLD_TAX_TRANSFERRED_YEAR);
      m_InXml = Form.GetLinkedValue(PNFLD_TAX_TRANSFERRED_INXML);
      m_RepDate = Form.GetLinkedValue(PNFLD_TAX_TRANSFERRED_REP_DATE);
      m_Messages = TArray();
      m_NegativeMessages = TArray();
      m_KBKWithTAXPaidArr = TArray();

      //Получаем список всех КБК
      InitKBKWithTAXPaidArr();

      if (m_Month != NULL)
         if (m_MonthCode < 10)
            m_BeginDate = Date(1,  (m_Month+1)/2, int(m_Year));
            m_EndDate   = Date(22, (m_Month+1)/2, int(m_Year));
         elif (m_MonthCode > 10)
            m_BeginDate = Date(23, (m_Month+1)/2, int(m_Year));
            m_EndDate   = Date(GetLastDayOfMonth(Date(1, (m_Month+1)/2, int(m_Year))), (m_Month+1)/2, int(m_Year));
         end;
      else
         m_BeginDate = ZeroDate;
         m_EndDate = ZeroDate;
      end;

      m_SignerPartyID = Form.GetLinkedValue(PNFLD_TAX_TRANSFERRED_SIGNER);

      CreateTotalBook();

      Dl_CallInsertStat(m_OutMode);
   end;

   //Получает из БД информаию для Нашего банка, такую, как ИНН, КПП, код налогового органа
   private macro GetOurInfo()
      var ErrCode;
      var RegVal;

      GetRegistryValue("COMMON\\НДФЛ\\ИФНС ПО МЕСТУ НАХОЖ. ДЛЯ 6-НДФЛ", V_BOOL, RegVal, ErrCode);
      RegVal = IIF(RegVal, SET_CHAR, UNSET_CHAR);

      if(ErrCode > 0)
         MsgBox("Ошибка определения значения настройки COMMON\\НДФЛ\\ИФНС ПО МЕСТУ НАХОЖ. ДЛЯ 6-НДФЛ. Код " + ErrCode);
         RegVal = UNSET_CHAR;
      end;

      var queryOur = "select q.* " +
                     "from ( " +
                     "        select ( " +
                     "                  select NVL(q_INN.t_Code, chr(0)) " +
                     "                  from ( " +
                     "                          select substr(NVL(objcode.t_Code, chr(0)), 1, INSTR(NVL(objcode.t_Code, chr(0)), '/') - 1) as t_Code " +
                     "                          from dobjcode_dbt objcode " +
                     "                          where objcode.t_ObjectType = " + string(OBJTYPE_PARTY) +
                     "                            and objcode.t_ObjectID = party.t_PartyID " +
                     "                            and objcode.t_CodeKind = " + string(PTCK_INN) +
                     "                            and objcode.t_BankDate <= ? " +  //1
                     "                          order by objcode.t_BankDate DESC " +
                     "                       ) q_INN " +
                     "                  where ROWNUM = 1" +
                     "               ) as t_OurINN, " +
                     "               NVL( " +
                     "                     case " +
                     "                        when '" + RegVal + "' = '" + SET_CHAR + "'" +
                     "                        then NVL( " +
                     "                                   ( " +
                     "                                      select substr(RSB_STRUCT.getString(notetext.t_Text), 1, INSTR(RSB_STRUCT.getString(notetext.t_Text), '/') - 1) " +
                     "                                      from dnotetext_dbt notetext " +
                     "                                      where notetext.t_ObjectType = 3 " +
                     "                                        and to_number(notetext.t_DocumentID) = party.t_PartyID " +
                     "                                        and notetext.t_NoteKind = 79 " + // Примечание "КПП/ИФНС для отчетов"
                     "                                   ), " +
                     "                                   ( " +
                     "                                      select q_KPP.t_Code " +
                     "                                      from ( " +
                     "                                              select substr(objcode.t_Code, INSTR(NVL(objcode.t_Code, chr(0)), '/') + 1, LENGTH(NVL(objcode.t_Code, chr(0))) - INSTR(NVL(objcode.t_Code, chr(0)), '/')) as t_Code " +
                     "                                              from dobjcode_dbt objcode " +
                     "                                              where objcode.t_ObjectType = " + string(OBJTYPE_PARTY) +
                     "                                                and objcode.t_ObjectID = party.t_PartyID " +
                     "                                                and objcode.t_CodeKind = " + string(PTCK_INN) +
                     "                                                and objcode.t_BankDate <= ? " + //2
                     "                                              order by objcode.t_BankDate DESC " +
                     "                                           ) q_KPP " +
                     "                                      where ROWNUM = 1 " +
                     "                                   ) " +
                     "                                ) " +
                     "                        else ( " +
                     "                                select q_KPP.t_Code " +
                     "                                from ( " +
                     "                                        select substr(objcode.t_Code, INSTR(NVL(objcode.t_Code, chr(0)), '/') + 1, LENGTH(NVL(objcode.t_Code, chr(0))) - INSTR(NVL(objcode.t_Code, chr(0)), '/')) as t_Code " +
                     "                                        from dobjcode_dbt objcode " +
                     "                                        where objcode.t_ObjectType = " + string(OBJTYPE_PARTY) +
                     "                                          and objcode.t_ObjectID = party.t_PartyID " +
                     "                                          and objcode.t_CodeKind = " + string(PTCK_INN) +
                     "                                          and objcode.t_BankDate <= ? " + //3
                     "                                        order by objcode.t_BankDate DESC " +
                     "                                     ) q_KPP " +
                     "                                where ROWNUM = 1 " +
                     "                             ) " +
                     "                     end, chr(0) " +
                     "                  ) as t_OurKPP, " +
                     "               NVL( " +
                     "                     case " +
                     "                        when '" + RegVal + "' = '" + SET_CHAR + "'" +
                     "                        then NVL( " +
                     "                                   ( " +
                     "                                      select substr(RSB_STRUCT.getString(notetext.t_Text), INSTR(RSB_STRUCT.getString(notetext.t_Text), '/') + 1, LENGTH(RSB_STRUCT.getString(notetext.t_Text)) - INSTR(RSB_STRUCT.getString(notetext.t_Text), '/')) " +
                     "                                      from dnotetext_dbt notetext " +
                     "                                      where notetext.t_ObjectType = 3 " +
                     "                                        and to_number(notetext.t_DocumentID) = party.t_PartyID " +
                     "                                        and notetext.t_NoteKind = 79 " + // Примечание "КПП/ИФНС для отчетов"
                     "                                   ), " +
                     "                                   ( " +
                     "                                      select q_FNS.t_Code " +
                     "                                      from ( " +
                     "                                              select NVL(objcode.t_Code, chr(0)) as t_Code " +
                     "                                              from dobjcode_dbt objcode " +
                     "                                              where objcode.t_ObjectType = " + string(OBJTYPE_PARTY) +
                     "                                                and objcode.t_ObjectID = party.t_TaxInstitution " +
                     "                                                and objcode.t_CodeKind = " + string(PTCK_MNS) + //28 "Код налогового органа"
                     "                                                and objcode.t_BankDate <= ? " + //4
                     "                                              order by objcode.t_BankDate DESC " +
                     "                                           ) q_FNS" +
                     "                                      where ROWNUM = 1 " +
                     "                                   ) " +
                     "                                ) " +
                     "                        else ( " +
                     "                                select q_FNS.t_Code " +
                     "                                from ( " +
                     "                                        select NVL(objcode.t_Code, chr(0)) as t_Code " +
                     "                                        from dobjcode_dbt objcode " +
                     "                                        where objcode.t_ObjectType = " + string(OBJTYPE_PARTY) +
                     "                                          and objcode.t_ObjectID = party.t_TaxInstitution " +
                     "                                          and objcode.t_CodeKind = " + string(PTCK_MNS) + //28 "Код налогового органа"
                     "                                          and objcode.t_BankDate <= ? " + //5
                     "                                        order by objcode.t_BankDate DESC " +
                     "                                     ) q_FNS" +
                     "                                where ROWNUM = 1 " +
                     "                             ) " +
                     "                     end, chr(0) " +
                     "                  ) as t_OurFNSCode, " +
                     /*"                NVL( " +
                     "                      ( " +
                     "                         select FirstPerson.t_Name1 || ' ' || FirstPerson.t_Name2 || ' ' || FirstPerson.t_Name3 " +
                     "                         from dofficer_dbt ofc, dpersn_dbt FirstPerson " +
                     "                         where ofc.t_PartyID = party.t_PartyID " +
                     "                           and ofc.t_IsFirstPerson = '" + SET_CHAR + "'" +
                     "                           and FirstPerson.t_PersonID = ofc.t_PersonID " +
                     "                           and ROWNUM = 1 " +
                     "                      ), " +
                     "                      chr(0) " +
                     "                   ) as t_FIOPerson1, " +*/
                     "                   NVL(FirstPerson.t_Name1 || ' ' || FirstPerson.t_Name2 || case when FirstPerson.t_Name3 <> chr(0) then ' ' || FirstPerson.t_Name3 else chr(0) end, chr(0)) as t_FIOPerson1, " +
                     "                   NVL(FirstPerson.t_Name1, chr(0)) as t_FirstPersonName1, " +
                     "                   NVL(FirstPerson.t_Name2, chr(0)) as t_FirstPersonName2, " +
                     "                   NVL(FirstPerson.t_Name3, chr(0)) as t_FirstPersonName3, " +
                     "                   NVL(legalproxy.t_Number, chr(0)) as t_ProcuratoryNumber, " +
                     "                   NVL(legalproxy.t_BeginDate, to_date('01.01.0001', 'dd.mm.yyyy')) as t_ProcuratoryDate, " +
                     "                   ( " +
                     "                         case " +
                     "                            when party.t_ShortName is not NULL and LENGTH(party.t_ShortName) > 0 " +
                     "                            then party.t_ShortName " +
                     "                            else party.t_Name " +
                     "                         end " +
                     "                   ) as t_OurName, " +
                     "                   ( " +
                     "                      select q_OKTMO.t_Code " +
                     "                      from ( " +
                     "                              select NVL(objcode.t_Code, chr(0)) as t_Code " +
                     "                              from dobjcode_dbt objcode " +
                     "                              where objcode.t_ObjectType = " + string(OBJTYPE_PARTY) +
                     "                                and objcode.t_ObjectID = party.t_PartyID " +
                     "                                and objcode.t_CodeKind = 74 " +
                     "                                and objcode.t_BankDate <= ? " + //6
                     "                              order by objcode.t_BankDate " +
                     "                           ) q_OKTMO " +
                     "                      where ROWNUM = 1 " +
                     "                   ) as t_OurOKTMO " +
                     "         from dparty_dbt party " +
                     "              left outer join ( " +
                     "                                 dofficer_dbt ofc " +
                     "                                 inner join dpersn_dbt FirstPerson " +
                     "                                 on FirstPerson.t_PersonID = ofc.t_PersonID " +
                     "                                    and ( " +
                     "                                           ( " +
                     "                                              ? > 0 " + //7
                     "                                              and ofc.t_PersonID = ? " + //8
                     "                                           ) " +
                     "                                           or ( " +
                     "                                                 ? <= 0 " + //9
                     "                                                 and ofc.t_IsFirstPerson = chr(88) " +
                     "                                              ) " +
                     "                                        ) " +
                     "                              ) " +
                     "              on ofc.t_PartyID = party.t_PartyID " +
                     "              left outer join dlegalproxy_dbt legalproxy " +
                     "              on legalproxy.t_PartyID = party.t_PartyID " +
                     "                 and legalproxy.t_ProxyID = ofc.t_PersonID " +
                     "                 and legalproxy.t_BeginDate < ? " + //10
                     "                 and ( " +
                     "               legalproxy.t_EndDate = to_date('01.01.0001', 'dd.mm.yyyy') " +
                     "               or legalproxy.t_EndDate > ? " + //11
                     "            ) " +
                     "         where party.t_PartyID = ? " + //12
                     "         order by legalproxy.t_BeginDate desc " +
                     "     ) q " +
                     "where ROWNUM = 1 ";

      var cmdOur = DL_RSDCommand(queryOur);
      cmdOur.AddParam(m_EndDate); //1
      cmdOur.AddParam(m_EndDate); //2
      cmdOur.AddParam(m_EndDate); //3
      cmdOur.AddParam(m_EndDate); //4
      cmdOur.AddParam(m_EndDate); //5
      cmdOur.AddParam(m_EndDate); //6
      cmdOur.AddParam(m_SignerPartyID); //7
      cmdOur.AddParam(m_SignerPartyID); //8
      cmdOur.AddParam(m_SignerPartyID); //9
      cmdOur.AddParam(m_EndDate); //10
      cmdOur.AddParam(m_BeginDate); //11
      cmdOur.AddParam({OurBank}); //12

      var DataSetOur = cmdOur.Execute();
      if(DataSetOur.MoveNext())
         m_OurINN = string(DataSetOur.OurINN);
         m_OurKPP = string(DataSetOur.OurKPP);
         m_OurFNSCode = string(DataSetOur.OurFNSCode);
         m_FIOperson1 = StrUpr(string(DataSetOur.FIOPerson1));
         m_FirstPersonName1 = StrUpr(string(DataSetOur.FirstPersonName1));
         m_FirstPersonName2 = StrUpr(string(DataSetOur.FirstPersonName2));
         m_FirstPersonName3 = StrUpr(string(DataSetOur.FirstPersonName3));
         m_ProcuratoryNumber = string(DataSetOur.ProcuratoryNumber);
         m_ProcuratoryDate = Date(DataSetOur.ProcuratoryDate);
         m_OurName = StrUpr(string(DataSetOur.OurName));
         m_OurOKTMO = string(DataSetOur.OurOKTMO);
      end;
   end;

   //Возвращает ИНН нашего банка
   private macro OurINN():String
      if(m_OurINN == NULL)
         GetOurInfo();
      end;

      return m_OurINN;
   end;

   //Возвращает КПП нашего банка
   private macro OurKPP():String
      if(m_OurKPP == NULL)
         GetOurInfo();
      end;

      return m_OurKPP;
   end;

   //Возвращает код ФНС для нашего банка
   private macro OurFNSCode():String
      if(m_OurFNSCode == NULL)
         GetOurInfo();
      end;

      return m_OurFNSCode;
   end;

   //Возвращает ФИО сотрудника нашего банка с признаком первого лица
   private macro FIOperson1():String
      if(m_FIOperson1 == NULL)
         GetOurInfo();
      end;

      return m_FIOperson1;
   end;

   //Возвращает фамилию сотрудника нашего банка с признаком первого лица
   private macro FirstPersonName1()
      if(m_FirstPersonName1 == NULL)
         GetOurInfo();
      end;

      return m_FirstPersonName1;
   end;

   //Возвращает имя сотрудника нашего банка с признаком первого лица
   private macro FirstPersonName2()
      if(m_FirstPersonName2 == NULL)
         GetOurInfo();
      end;

      return m_FirstPersonName2;
   end;

   //Возвращает отчество сотрудника нашего банка с признаком первого лица
   private macro FirstPersonName3()
      if(m_FirstPersonName3 == NULL)
         GetOurInfo();
      end;

      return m_FirstPersonName3;
   end;

   //Возвращает наименование нашего банка
   private macro OurName():String
      if(m_OurName == NULL)
         GetOurInfo();
      end;

      return m_OurName;
   end;

   //Возвращает код ОКТМО нашего банка
   private macro OurOKTMO():String
      if(m_OurOKTMO == NULL)
         GetOurInfo();
      end;

      return m_OurOKTMO;
   end;

   private macro Procuratory():String
      if(m_ProcuratoryNumber == NULL)
         GetOurInfo();
      end;

      if((m_SignerPartyID > 0) and (m_ProcuratoryNumber != ""))
         return "Доверенность " + string(m_ProcuratoryNumber) + " от " + string(m_ProcuratoryDate:f);
      else
         return "";
      end;
   end;

   private macro CreateNodeSvNP()
      var nodeSvNP;
      var nodeNPUL, nodeNPIP;

      nodeSvNP = m_XMLFile.CreateNode("СвНП");

      if(nodeSvNP != NULL)
         nodeNPUL = m_XMLFile.CreateNode("НПЮЛ",
                                         "ИННЮЛ", OurINN(),
                                         "КПП", OurKPP());

         if(nodeNPUL != NULL)
            nodeSvNP.appendChild(nodeNPUL);
         end;

         if(nodeNPIP != NULL)
            nodeSvNP.appendChild(nodeNPIP);
         end;
      end;

      return nodeSvNP;
   end;

   private macro CreateNodeSigner()
      var nodeSigner;
      var nodeFIO, nodeSvPred;

      nodeSigner = m_XMLFile.CreateNode("Подписант",
                                        "ПрПодп", IIF(m_SignerPartyID > 0, "2", "1"));

      if(nodeSigner != NULL)
         nodeFIO = m_XMLFile.CreateNode("ФИО",
                                        "Фамилия", substr(FirstPersonName1(), 1, 60),
                                        "Имя", substr(FirstPersonName2(), 1, 60));
         if(nodeFIO != NULL)
            if(FirstPersonName3() != "")
               nodeFIO.setAttributeNode(m_XMLFile.CreateAttr("Отчество", substr(FirstPersonName3(), 1, 60)));
            end;

            nodeSigner.appendChild(nodeFIO);
         end;

         if((m_SignerPartyID > 0) and (m_ProcuratoryNumber != ""))
            nodeSvPred = m_XMLFile.CreateNode("СвПред",
                                              "НаимДок", Procuratory());
         end;

         if(nodeSvPred != NULL)
            nodeSigner.appendChild(nodeSvPred);
         end;
      end;

      return nodeSigner;
   end;

   private macro CreateNodeUvIschSumNalog(KBK:String, Sum:Money)
      var nodeUvIschSumNalog = m_XMLFile.CreateNode("УвИсчСумНалог",
                                                    "КППДекл", substr(OurKPP(), 1, 9),
                                                    "ОКТМО", substr(OurOKTMO(), 1, 11),
                                                    "КБК", KBK,
                                                    "СумНалогАванс", string(Sum),
                                                    "Период", m_PeriodCode,
                                                    "НомерМесКварт", m_MonthCode,
                                                    "Год", string(m_Year));

      return nodeUvIschSumNalog;
   end;

   private macro CreateNodeDocument()
      var nodeDocument, nodeSvNP, nodeSigner;
      var nodeUvIschSumNalog;

      nodeDocument = m_XMLFile.CreateNode("Документ",
                                          "КНД", "1110355",
                                          "ДатаДок", string(m_RepDate:f),
                                          "КодНО", substr(OurFNSCode(), 1, 4));

      if(nodeDocument != NULL)
         nodeSvNP = CreateNodeSvNP();
         if(nodeSvNP != NULL)
            nodeDocument.appendChild(nodeSvNP);
         end;

         nodeSigner = CreateNodeSigner();
         if(nodeSigner != NULL)
            nodeDocument.appendChild(nodeSigner);
         end;

         var i = 0;
         while ( i < m_KBKWithTAXPaidArr.Size )
            nodeUvIschSumNalog = CreateNodeUvIschSumNalog(m_KBKWithTAXPaidArr[i].KBK, m_KBKWithTAXPaidArr[i].TaxPaid);
            if(nodeSigner != NULL)
               nodeDocument.appendChild(nodeUvIschSumNalog);
            end;
            i = i + 1;
         end;
      end;

      return nodeDocument;
   end;

   private macro CreateNodeFile()
      var FileName, IndexFileName;
      var nodeFile, nodeDocument;

      FileName = m_XmlFile.GetFileName();
      IndexFileName = Index(FileName, ".xml");
      if(IndexFileName > 0)
         FileName = substr(FileName, 1, IndexFileName - 1);
      end;

      nodeFile = m_XMLFile.CreateNode("Файл",
                                      "ИдФайл", FileName,
                                      "ВерсПрог", "RS-Bank",
                                      "ВерсФорм", "5.03");

      if(nodeFile != NULL)
         nodeDocument = CreateNodeDocument();
         if(nodeDocument != NULL)
            nodeFile.appendChild(nodeDocument);
         end;
      end;

      return nodeFile;
   end;

   //Создание XML
   private macro CreateXML()
      var refID;
      var num;
      var day, month, year;
      var nodeFile;

      DateSplit(m_RepDate, day, month, year);
      GetReferenceIDByType(151, 1, refID);
      GenerateReference(refID, num);
      m_XMLFile.SetFileName("UT_UVISCHSUMNAL_" + substr(OurFNSCode(), 1, 4) + "_" +
                                                 substr(OurFNSCode(), 1, 4) + "_" +
                                                 substr(OurINN(), 1, 10) + substr(OurKPP(), 1, 9) + "_" + 
                                                 L_Z(year, 4) + L_Z(month, 2) + L_Z(day, 2) + "_" + num + ".xml");

      nodeFile = CreateNodeFile();
      if(nodeFile != NULL)
         m_XMLFile.GetXMLObj().appendChild(nodeFile);
      end;
   end;

   //Печать титульного листа
   private macro PrintTitle()
      SetActiveSheet("Титульный лист");

      PrintOneRow("N1_OurINN_", OurINN(), 12);
      PrintOneRow("N1_OurKPP_", OurKPP(), 9);
      PrintOneRow("N1_OurFNSCode_", OurFNSCode(), 9);
      PrintOneRow("N1_SignSigner", IIF(m_SignerPartyID > 0, "2", "1"), 1);
      PrintOneRow("N1_FIOperson1_", FIOperson1(), 60);
      PrintOneRow("N1_OurName_", OurName(), 160);
      PrintOneDateRow("N1_Date_", m_RepDate);
      PrintOneRow("N1_Procuratory", Procuratory(), 40);

      CopyAllSheetInTotalBook("Титульный лист", true, "N1_PrintArea", 0, "Титульный лист", true);
   end;

   //Расчет сумм
   private macro CalcData()

      var TaxBaseKind = NPTXTOTALBASE_TAXBASEKIND_ONB + ", " + NPTXTOTALBASE_TAXBASEKIND_BROK + " ," + NPTXTOTALBASE_TAXBASEKIND_IIS + " ,0";
      CONST query = " SELECT ROUND(NVL(SUM(t_HoldPITax), 0), 0) AS SumTotal " +
                    "   FROM DNPTXTOTALBASE_DBT                             " +
                    "  WHERE t_StorState = " + NPTXTOTALBASE_STORSTATE_ACTIVE +
                    "    AND t_IncDate BETWEEN ? AND ?                      " +
                    "    AND t_TaxBaseKind IN (" + TaxBaseKind + ")         " +
                    "    AND t_BCCHoldPITax = '"; //КБК

      var i = 0;
      while ( i < m_KBKWithTAXPaidArr.Size )
         var cmd = RSDCommand(query + m_KBKWithTAXPaidArr[i].KBK + "';");

         cmd.addParam("", RSDBP_IN, m_BeginDate);
         cmd.addParam("", RSDBP_IN, m_EndDate);

         var rs_SumTotal = RSDRecordset(cmd);

         if(rs_SumTotal.MoveNext())
            m_KBKWithTAXPaidArr[i].TaxPaid = rs_SumTotal.value(0L);
            if (m_KBKWithTAXPaidArr[i].TaxPaid < 0)
               NegativeMessage("По КБК " + string(m_KBKWithTAXPaidArr[i].KBK) + " сформировалась отрицательная сумма " + string(m_KBKWithTAXPaidArr[i].TaxPaid));
            end;
         end;

         i = i + 1;
      end;
   end;

   //Печатать лист "Данные"
   private macro PrintData()
      var PartNum = 0;
      InitProgress(m_KBKWithTAXPaidArr.Size + 2, "Печать раздела \"Данные\"", "Печать раздела \"Данные\"");
      
      //Печатаем title
      SetActiveSheet("Данные");

      PrintOneRow("N2_OurInn_", OurINN(), 12);
      PrintOneRow("N2_OurKPP_", OurKPP(), 9);

      CopyAllSheetInTotalBook("Данные", false, "N2_PrintTitleArea", 0, "Данные", true);
      UseProgress(PartNum = PartNum + 1);

      //Печатаем блоки КБК
      var isFirstBlock = true;
      var i = 0;
      while ( i < m_KBKWithTAXPaidArr.Size )
         SetActiveSheet("Данные");

         if (isFirstBlock)
            PrintOneRow("N2_KPPdecl1_", OurKPP(), 9);
            PrintOneRow("N2_OurOKTMO1_", OurOKTMO(), 11);
            PrintOneRow("N2_KBK1_",  m_KBKWithTAXPaidArr[i].KBK, 20);
            PrintOneSumRow("N2_TaxTransferredSum1_", IIF(m_KBKWithTAXPaidArr[i].TaxPaid < 0, 0, m_KBKWithTAXPaidArr[i].TaxPaid), 17, true); //Отрицательные не печатаем, выводятся на отдельный лист
            PrintOneRow("N2_TaxablePeriod1_", m_PeriodCode, 2);
            PrintOneRow("N2_Month1_", m_MonthCode, 2);
            PrintOneRow("N2_Year1_", string(m_Year), 4);

            CopyAllSheetInTotalBook("Данные", false, "N2_PrintFBlockArea", 0, "Данные", true);
            isFirstBlock = false;
         else
            PrintOneRow("N2_KPPdecl2_", OurKPP(), 9);
            PrintOneRow("N2_OurOKTMO2_", OurOKTMO(), 11);
            PrintOneRow("N2_KBK2_", m_KBKWithTAXPaidArr[i].KBK, 20);
            PrintOneSumRow("N2_TaxTransferredSum2_", IIF(m_KBKWithTAXPaidArr[i].TaxPaid < 0, 0, m_KBKWithTAXPaidArr[i].TaxPaid), 17, true); //Отрицательные не печатаем, выводятся на отдельный лист
            PrintOneRow("N2_TaxablePeriod2_", m_PeriodCode, 2);
            PrintOneRow("N2_Month2_", m_MonthCode, 2);
            PrintOneRow("N2_Year2_", string(m_Year), 4);

            CopyAllSheetInTotalBook("Данные", false, "N2_PrintNBlockArea", 0, "Данные", true);
         end;
         
         UseProgress(PartNum = PartNum + 1);
         i = i + 1;
      end;

      //Печатаем footer
      SetActiveSheet("Данные");
      CopyAllSheetInTotalBook("Данные", false, "N2_PrintFooterArea", 0, "Данные", true);

      RemProgress();
   end;
   
   //Печать сообщений на лист "Протокол по отрицат. суммам"
   macro PrintNegative()
      if (m_NegativeMessages.Size > 0)
         SetActiveSheet("Протокол по отрицат. суммам");
         
         CopyAllSheetInTotalBook("Протокол по отрицат. суммам", false, "N3_NegativeHeader", 0, "Протокол по отрицат. суммам", false);
         RegisterTable("N3_NegativeMessage", "", "N3_NegativeMessage");

         var i = 0;
         while(i < m_NegativeMessages.Size)
            PrintTableLine("N3_NegativeMessage", m_NegativeMessages[i], NULL);
            i = i + 1;
         end;

         EndTable();

         CopyAllSheetInTotalBook("Протокол по отрицат. суммам", false, GetTableRange(), 0, "Протокол по отрицат. суммам", true);
      end;
   end;

   //Печать сообщений на лист "Сообщения", чтобы сообщения не выглядели как ошибки
   macro PrintMessages()
      if(m_Messages.Size > 0)
         SetActiveSheet("Сообщения");

         CopyAllSheetInTotalBook("Сообщения", false, "N3_Header", 0, "Сообщения", false);
         RegisterTable("N3_Message", "", "N3_Message");

         var i = 0;
         while(i < m_Messages.Size)
            PrintTableLine("N3_Message", m_Messages[i], NULL);
            i = i + 1;
         end;

         EndTable();

         CopyAllSheetInTotalBook("Сообщения", false, GetTableRange(), 0, "Сообщения", true);
      end;
   end;

   private macro Create()
      var PageNum = 2;
      InitProgress(-1, "Уведемление НДФЛ", "Идёт отбор данных...");
      CalcData();
      RemProgress();

      PrintTitle();
      PrintData();

      if(m_InXML == SET_CHAR)
         m_XMLFile.CreateXMLObj();
         CreateXML();
         m_XMLFile.Save();
         Message("Сформирован файл " + m_XMLFile.GetFileName());
      end;

      PrintMessages();
      PrintNegative();
   end;

   private macro EndReport()
      SaveTotalBook();
   end;

   initDL_CReportTemplate(NULL, "Report_TaxTransferred.xls", NULL, NULL, NULL, true);
   SetPrintMode(DL_OUTREPORT_EXCEL);
   m_XMLFile = Tax_Transferred_XML();
END;

Form = Tax_Transferred_Form();

while(Form.Run())
   Tax_Transferred_Report().Run();
end;

Exit(1);
