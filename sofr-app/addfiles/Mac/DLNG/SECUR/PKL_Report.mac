/*                           
$Name:             PKL_Report.mac
$Module:           Ценные бумаги
$Description:      Отчет "Расчет показателей краткосрочной ликвидности (ПКЛ)"
*/

IMPORT "PKL_Report_Form.mac", "RegistrReport.mac", "ws_txreportform.mac", "secinter.mac";

PRIVATE VAR PKL_Form;
PRIVATE VAR PKL_Report;

PRIVATE VAR   DecimalSeparator;
private var   Data;
PRIVATE CONST FORMULA  = "formula=";
PRIVATE VAR m_PrevAttrID = 0; // предыдущий обработанный AttrID
PRIVATE VAR m_PklCode = "ПКЛ";

/*имена ячеек по столбцам - на листе ПКЛ*/
PRIVATE VAR C_Num_str0     = "A";
PRIVATE VAR C_Name0        = "B";
PRIVATE VAR C_RQSumma0     = "C";
PRIVATE VAR C_Coeff0       = "D";
PRIVATE VAR C_Summa0       = "E";

/*имена ячеек по столбцам - на листах ВЛА-1, ВЛА-2А, ВЛА-2Б*/
PRIVATE VAR C_Deprec       = "";
PRIVATE VAR C_RQSumma      = "";
PRIVATE VAR C_Coeff        = "";
PRIVATE VAR C_Summa        = "";

PRIVATE VAR Coder = TArray;  // AttrID --> номер строки (1.1.5 - 1.3.3) на листе ПКЛ
PRIVATE VAR Coder0 = TArray; // Номер листа (2 - 4) --> номер строки (1.1 - 1.3) на листе ПКЛ
PRIVATE VAR ArrSubTotal = TArray; // Массив (нумерация с 1, для каждого отчёта) номеров напечатанных строк 1.1.5 - 1.1.9 или 1.2.1 - 1.2.4 или 1.3.1 - 1.3.3
PRIVATE VAR m_NSubKinds = 0; // Количество секций (например, на 1-м листе 1.1.5 - 1.1.9 т.е. 5) на листе отчёта.
PRIVATE VAR m_Offset = 0;    // Смещение. 

/* Структура с данными для отчета*/
class SourceData( _date: date, _FICode: integer )

  var RepDate       =  _date,      
      FaceFIID      =  _FICode;

end;

PRIVATE MACRO WhereCond2()
  return " AND AtCor.t_AttrID IN (1,2,3,4,5) "; 
END;

PRIVATE MACRO WhereCond3()
  return " AND AtCor.t_AttrID IN (6,7,8,9) "; 
END;

PRIVATE MACRO WhereCond4()
  return " AND AtCor.t_AttrID IN (10,11,12) "; 
END;

PRIVATE CLASS CPKL_1( Report:OBJECT )
  VAR m_Report = Report;

  MACRO BeginReport()
     m_Report.SetActiveSheet( m_Report.GetNumSheet() );
     m_Report.PrintHeader( );
  END;

  MACRO EndReport()
  END;
END;

PRIVATE CLASS CPKL_2( Report:OBJECT )
  VAR m_Report = Report;

  MACRO Where():STRING
     return WhereCond2();
  END;

  MACRO BeginReport()
     m_Report.SetActiveSheet( m_Report.GetNumSheet() );

     m_Report.RegisterTable( "N2_MainTable", "",
                             "N2_Num_str",       
                             "N2_Name",       
                             "N2_Issuer", 
                             "N2_Iss_Name",   
                             "N2_RQSumma",   
                             "N2_Coeff",  
                             "N2_Summa"
                           );

     // строка 1.1.
     m_Report.PrintTableLine(
            /* 1    */        "N2_Num_str",                m_Report.Num_str0(m_Report.ReportKind()),      null,
            /* 2    */        "N2_Name",                   m_Report.Name0(m_Report.ReportKind()),         null,
            /* 3    */        "N2_Issuer",                 "",                                            null,
            /* 4    */        "N2_Iss_Name",               "",                                            null,
            /* 5    */        "N2_RQSumma",                0,                                             null, // заполнится в ПечататьПромежуточныеИтоги(), формула
            /* 6    */        "N2_Coeff",                  m_Report.Coeff0(m_Report.ReportKind()),        null,
            /* 7    */        "N2_Summa",                  0,                                             null  // заполнится в ПечататьПромежуточныеИтоги(), формула
                            );
  END;

  // печать i-й строки 1.1.5 - 1.1.9
  MACRO PrintSubTotal(i:INTEGER)

     // сохраняем в массив номер напечатанной i-й строки 1.1.5 - 1.1.9
     m_Report.SetCurrentStrNumberToArray(i);

     // печать i-й строки 1.1.5 - 1.1.9
     m_Report.PrintTableLine(
            /* 1    */        "N2_Num_str",                m_Report.Num_str(i),                  null,
            /* 2    */        "N2_Name",                   m_Report.Name(i),                     null,
            /* 3    */        "N2_Issuer",                 "",                                   null,
            /* 4    */        "N2_Iss_Name",               "",                                   null,
            /* 5    */        "N2_RQSumma",                0,                                    null, // заполнится в ПечататьПромежуточныеИтоги(), формула
            /* 6    */        "N2_Coeff",                  m_Report.Coeff(i),                    null,
            /* 7    */        "N2_Summa",                  0,                                    null  // заполнится в ПечататьПромежуточныеИтоги(), формула
                            );
  END;

  MACRO EndReport()
     var i = 1;
     if (ArrSubTotal.size > 0)
        i = ArrSubTotal.size;
     end;

     while (i <= m_NSubKinds)
        // печать i-й строки 1.1.5 - 1.1.9. Сюда попадём только, если не было данных по последним группам из 1.1.5 - 1.1.9.
        PrintSubTotal(i);
        i = i + 1;
     end;

     m_Report.SetCurrentStrNumberToArray(m_NSubKinds + 1);
 
     m_Report.EndTable();
     m_Report.ПечататьПромежуточныеИтоги();
     m_Report.ПеренестиИтогиНаЛистПКЛ();
  END;

  MACRO Print()
     var i = 0;

     i = m_PrevAttrID + 1;
     while (i <= m_Report.m_RS.AttrID)
        // печать i-й строки 1.1.5 - 1.1.9. В случае, если есть данные. Или нет данных, но есть данные по следующей группе.
        PrintSubTotal(i-m_Offset);
        i = i + 1;
     end;

     m_Report.SetCurrentStrNumber();
     m_Report.PrintTableLine(
            /* 1    */        "N2_Num_str",                "",                                       null,
            /* 2    */        "N2_Name",                   "",                                       null,
            /* 3    */        "N2_Issuer",                 m_Report.Issuer(),                        null,
            /* 4    */        "N2_Iss_Name",               m_Report.Iss_Name(),                      null,
            /* 5    */        "N2_RQSumma",                m_Report.RQSumma(),                       null,
            /* 6    */        "N2_Coeff",                  m_Report.Coeff(m_Report.m_RS.AttrID),     null, // Формула
            /* 7    */        "N2_Summa",                  m_Report.Summa(),                         null  // Формула
                 );
  END;      

END;

PRIVATE CLASS CPKL_3( Report:OBJECT )
  VAR m_Report = Report;

  MACRO Where():STRING
     return WhereCond3();
  END;

  MACRO BeginReport()
     m_Report.SetActiveSheet( m_Report.GetNumSheet() );

     m_Report.RegisterTable( "N3_MainTable", "",
                             "N3_Num_str",       
                             "N3_Name",       
                             "N3_Issuer", 
                             "N3_Iss_Name",   
                             "N3_Deprec",   
                             "N3_RQSumma",   
                             "N3_Coeff",  
                             "N3_Summa"
                           );

     // строка 1.2.
     m_Report.PrintTableLine(
            /* 1    */        "N3_Num_str",                m_Report.Num_str0(m_Report.ReportKind()),      null,
            /* 2    */        "N3_Name",                   m_Report.Name0(m_Report.ReportKind()),         null,
            /* 3    */        "N3_Issuer",                 "",                                            null,
            /* 4    */        "N3_Iss_Name",               "",                                            null,
            /* 5    */        "N3_Deprec",                 "<=10%",                                       null,
            /* 6    */        "N3_RQSumma",                0,                                             null, // заполнится в ПечататьПромежуточныеИтоги(), формула
            /* 7    */        "N3_Coeff",                  m_Report.Coeff0(m_Report.ReportKind()),        null,
            /* 8    */        "N3_Summa",                  0,                                             null  // заполнится в ПечататьПромежуточныеИтоги(), формула
                            );
  END;

  // печать i-й строки 1.2.1 - 1.2.4
  MACRO PrintSubTotal(i:INTEGER)

     // сохраняем в массив номер напечатанной i-й строки 1.2.1 - 1.2.4
     m_Report.SetCurrentStrNumberToArray(i);

     // печать i-й строки 1.2.1 - 1.2.4
     m_Report.PrintTableLine(
            /* 1    */        "N3_Num_str",                m_Report.Num_str(i+m_Offset),         null,
            /* 2    */        "N3_Name",                   m_Report.Name(i+m_Offset),            null,
            /* 3    */        "N3_Issuer",                 "",                                   null,
            /* 4    */        "N3_Iss_Name",               "",                                   null,
            /* 5    */        "N3_Deprec",                 "",                                   null,
            /* 6    */        "N3_RQSumma",                0,                                    null, // заполнится в ПечататьПромежуточныеИтоги(), формула
            /* 7    */        "N3_Coeff",                  m_Report.Coeff(i+m_Offset),           null,
            /* 8    */        "N3_Summa",                  0,                                    null  // заполнится в ПечататьПромежуточныеИтоги(), формула
                            );
  END;

  MACRO EndReport()
     var i = 1;
     if (ArrSubTotal.size > 0)
        i = ArrSubTotal.size;
     end;

     while (i <= m_NSubKinds)
        // печать i-й строки 1.2.1 - 1.2.4. Сюда попадём только, если не было данных по последним группам из 1.2.1 - 1.2.4.
        PrintSubTotal(i);
        i = i + 1;
     end;

     m_Report.SetCurrentStrNumberToArray(m_NSubKinds + 1);
 
     m_Report.EndTable();
     m_Report.ПечататьПромежуточныеИтоги();
     m_Report.ПеренестиИтогиНаЛистПКЛ();
  END;

  MACRO Print()
     var i = 0;

     i = m_PrevAttrID + 1;
     while (i <= m_Report.m_RS.AttrID)
        // печать i-й строки 1.2.1 - 1.2.4. В случае, если есть данные. Или нет данных, но есть данные по следующей группе.
        PrintSubTotal(i-m_Offset);
        i = i + 1;
     end;

     m_Report.SetCurrentStrNumber();
     m_Report.PrintTableLine(
            /* 1    */        "N3_Num_str",                "",                                       null,
            /* 2    */        "N3_Name",                   "",                                       null,
            /* 3    */        "N3_Issuer",                 m_Report.Issuer(),                        null,
            /* 4    */        "N3_Iss_Name",               m_Report.Iss_Name(),                      null,
            /* 5    */        "N3_Deprec",                 m_Report.Deprec(),                        null,
            /* 6    */        "N3_RQSumma",                m_Report.RQSumma(),                       null,
            /* 7    */        "N3_Coeff",                  m_Report.Coeff(m_Report.m_RS.AttrID),     null, // Формула
            /* 8    */        "N3_Summa",                  m_Report.Summa(),                         null  // Формула
                            );
  END;      

END;

PRIVATE CLASS CPKL_4( Report:OBJECT )
  VAR m_Report = Report;

  MACRO Where():STRING
     return WhereCond4();
  END;

  MACRO BeginReport()
     m_Report.SetActiveSheet( m_Report.GetNumSheet() );

     m_Report.RegisterTable( "N4_MainTable", "",
                             "N4_Num_str",       
                             "N4_Name",       
                             "N4_Issuer", 
                             "N4_Iss_Name",   
                             "N4_Deprec",   
                             "N4_RQSumma",   
                             "N4_Coeff",  
                             "N4_Summa"
                           );

     // строка 1.3.
     m_Report.PrintTableLine(
            /* 1    */        "N4_Num_str",                m_Report.Num_str0(m_Report.ReportKind()),      null,
            /* 2    */        "N4_Name",                   m_Report.Name0(m_Report.ReportKind()),         null,
            /* 3    */        "N4_Issuer",                 "",                                            null,
            /* 4    */        "N4_Iss_Name",               "",                                            null,
            /* 5    */        "N4_Deprec",                 "<=20%",                                       null,
            /* 6    */        "N4_RQSumma",                0,                                             null, // заполнится в ПечататьПромежуточныеИтоги(), формула
            /* 7    */        "N4_Coeff",                  m_Report.Coeff0(m_Report.ReportKind()),        null,
            /* 8    */        "N4_Summa",                  0,                                             null  // заполнится в ПечататьПромежуточныеИтоги(), формула
                            );
  END;

  // печать i-й строки 1.3.1 - 1.3.3 
  MACRO PrintSubTotal(i:INTEGER)

     // сохраняем в массив номер напечатанной i-й строки 1.3.1 - 1.3.3
     m_Report.SetCurrentStrNumberToArray(i);

     // печать i-й строки 1.3.1 - 1.3.3
     m_Report.PrintTableLine(
            /* 1    */        "N4_Num_str",                m_Report.Num_str(i+m_Offset),         null,
            /* 2    */        "N4_Name",                   m_Report.Name(i+m_Offset),            null,
            /* 3    */        "N4_Issuer",                 "",                                   null,
            /* 4    */        "N4_Iss_Name",               "",                                   null,
            /* 5    */        "N4_Deprec",                 "",                                   null,
            /* 6    */        "N4_RQSumma",                0,                                    null, // заполнится в ПечататьПромежуточныеИтоги(), формула
            /* 7    */        "N4_Coeff",                  m_Report.Coeff(i+m_Offset),           null,
            /* 8    */        "N4_Summa",                  0,                                    null  // заполнится в ПечататьПромежуточныеИтоги(), формула
                            );
  END;

  MACRO EndReport()
     var i = 1;
     if (ArrSubTotal.size > 0)
        i = ArrSubTotal.size;
     end;

     while (i <= m_NSubKinds)
        // печать i-й строки 1.3.1 - 1.3.3. Сюда попадём только, если не было данных по последним группам из 1.3.1 - 1.3.3.
        PrintSubTotal(i);
        i = i + 1;
     end;

     m_Report.SetCurrentStrNumberToArray(m_NSubKinds + 1);
 
     m_Report.EndTable();
     m_Report.ПечататьПромежуточныеИтоги();
     m_Report.ПеренестиИтогиНаЛистПКЛ();
  END;

  MACRO Print()
     var i = 0;

     i = m_PrevAttrID + 1;
     while (i <= m_Report.m_RS.AttrID)
        // печать i-й строки 1.3.1 - 1.3.3. В случае, если есть данные. Или нет данных, но есть данные по следующей группе.
        PrintSubTotal(i-m_Offset);
        i = i + 1;
     end;

     m_Report.SetCurrentStrNumber();
     m_Report.PrintTableLine(
            /* 1    */        "N4_Num_str",                "",                                       null,
            /* 2    */        "N4_Name",                   "",                                       null,
            /* 3    */        "N4_Issuer",                 m_Report.Issuer(),                        null,
            /* 4    */        "N4_Iss_Name",               m_Report.Iss_Name(),                      null,
            /* 5    */        "N4_Deprec",                 m_Report.Deprec(),                        null,
            /* 6    */        "N4_RQSumma",                m_Report.RQSumma(),                       null,
            /* 7    */        "N4_Coeff",                  m_Report.Coeff(m_Report.m_RS.AttrID),     null, // Формула
            /* 8    */        "N4_Summa",                  m_Report.Summa(),                         null  // Формула
                            );
  END;      

END;


PRIVATE CLASS (TX_RegistrReport) SP_PKL_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  PRIVATE VAR m_CurrStrNum = 0;
  PRIVATE VAR m_SHEET = "";
  PRIVATE VAR m_RQSumma = $0;
  PRIVATE VAR m_Note:double = 0.0;

  PRIVATE VAR m_IsDataExist = false; /*для Web, если нет данных, то ничего не показываем*/

  MACRO ReportKind()
     return m_ReportKind;
  END;

  MACRO Fill_Cell_Name()
     if( m_ReportKind == PKL2 )
        C_RQSumma              = "E";
        C_Coeff                = "F";
        C_Summa                = "G";
  
     elif( m_ReportKind == PKL3 )
        C_Deprec               = "E";
        C_RQSumma              = "F";
        C_Coeff                = "G";
        C_Summa                = "H";

     elif( m_ReportKind == PKL4 )
        C_Deprec               = "E";
        C_RQSumma              = "F";
        C_Coeff                = "G";
        C_Summa                = "H";
     end;
  END;

  MACRO Fill_Coder()
    Coder[1] = 4;
    Coder[2] = 5;
    Coder[3] = 6;
    Coder[4] = 7;
    Coder[5] = 8;
    Coder[6] = 10;
    Coder[7] = 11;
    Coder[8] = 12;
    Coder[9] = 13;
    Coder[10] = 15;
    Coder[11] = 16;
    Coder[12] = 17;

    Coder0[PKL2] = 3;
    Coder0[PKL3] = 9;
    Coder0[PKL4] = 14;
  END;


  MACRO ClearCacheOneRecord()
    m_RQSumma = $0;
    m_Note = 0.0;
    this.ClearCacheOneRecord();
  END;

  /*переопределить, если не только ексель*/
  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  MACRO GetNumSheet()
     return m_SHEET;
  END;

  /*Отчетная дата, указанная в форме запуска отчета*/
  MACRO H0_1():DATE
     return date(Data.RepDate);
  END;

  MACRO Issuer():STRING
     return ПолучитьКороткоеИмяСубъекта( m_RS.Issuer );
  END;

  MACRO Iss_Name():STRING
     return m_RS.Iss_Name;
  END;

  MACRO RQSumma():MONEY
     return m_RQSumma;
  END;

  MACRO Deprec():Double
     return m_Note;
  END;

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )

     Data = SourceData( PKL_Form.GetFieldValue(PNFLD_PKL_DateEnd), 
                        PKL_Form.GetFieldValue(PNFLD_PKL_FICode) );

  END;

  MACRO PrintHeader( )
     PrintFormatString( "",
                        "N1_H0_1",       Data.RepDate // дата
                      );
  END;

  MACRO CReport_DataExist()
     return true;
  END;

  PRIVATE MACRO ReplaceSeparator( Str:@string, Separator:string )
     if( DecimalSeparator != Separator )
        var index_ = Index(Str, Separator);
        while( (index_ != 0) and (index_ <= strlen(Str)) )
           Str = substr(Str, 1, index_ - 1) + DecimalSeparator + substr(Str, index_ + 1, strlen(Str) - 1);
           index_ = Index(Str, Separator);
        end;
     end;
  END;

  MACRO F( Str:string ):string
     /* заменим разделитель целой и дробной частей */
     ReplaceSeparator( @Str, "." );
     ReplaceSeparator( @Str, "," );

     return FORMULA + Str;
  END;


  MACRO Num_str(i:INTEGER):STRING
     return F("'" + m_PklCode + "'!" + "$" + C_Num_str0 + "$" + string(Coder[i]));
  END;

  MACRO Name(i:INTEGER):STRING
     return F("'" + m_PklCode + "'!" + "$" + C_Name0 + "$" + string(Coder[i]));
  END;

  MACRO Coeff(i:INTEGER):STRING
     return F("'" + m_PklCode + "'!" + "$" + C_Coeff0 + "$" + string(Coder[i]));
  END;

  MACRO Num_str0(i:INTEGER):STRING
     return F("'" + m_PklCode + "'!" + "$" + C_Num_str0 + "$" + string(Coder0[i]));
  END;

  MACRO Name0(i:INTEGER):STRING
     return F("'" + m_PklCode + "'!" + "$" + C_Name0 + "$" + string(Coder0[i]));
  END;

  MACRO Coeff0(i:INTEGER):STRING
     return F("'" + m_PklCode + "'!" + "$" + C_Coeff0 + "$" + string(Coder0[i]));
  END;

  /*получить адрес ячейки*/
  MACRO GetDeprec():STRING
     return (C_Deprec+string(m_CurrStrNum));
  END;

  MACRO GetRQSumma():STRING
     return (C_RQSumma+string(m_CurrStrNum));
  END;

  /*получить адрес ячейки*/
  MACRO GetCoeff():STRING
     return (C_Coeff+string(m_CurrStrNum));
  END;

  MACRO Summa():STRING
     if(m_ReportKind == PKL2)
        return F(GetRQSumma()+"*"+GetCoeff());

     elif(m_ReportKind == PKL3)
        return F(FormulaIF()+"("+GetDeprec()+">10;0;"+ GetRQSumma()+"*"+GetCoeff()+")");

     elif(m_ReportKind == PKL4)
        if (m_RS.AttrID == 12)
           return F(FormulaIF()+"("+GetDeprec()+">40;0;"+ GetRQSumma()+"*"+GetCoeff()+")");
        else
           return F(FormulaIF()+"("+GetDeprec()+">20;0;"+ GetRQSumma()+"*"+GetCoeff()+")");
        end;
     end;
  END;

  /*!!!неверно работает при превышении MAXROWSHEET и переходе на дополнительный лист (на дополнительном листе только формулы в таблице будут неверные)*/
  MACRO НомерТекущейСтроки()
     return (m_ReportTable.bRowTable + getCurrentRow());
  END;

  MACRO SetSubTotalEx(StartStrNumber, FinishStrNumber /*...*/ )
     var Formula = "", FormulaTxt = "";
     var SheetName = "";
     var CellName = "", ColumnName = "", TmpAddress="";
     var Pos = 0, i = 3;
     var FromSheetNum = 0, j = 0;
 
     if(m_UseTemplate)

       SheetName = m_ObjTemplateXLS.SheetName();

       while( j < m_UsedSheet.Size )
          if( StrUpr(m_UsedSheet[j]) == StrUpr(SheetName) )
             FromSheetNum = j;
          end;
          j = j + 1;
       end;

       if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "SUBTOTAL";
       else                                                                  Formula = "ПРОМЕЖУТОЧНЫЕ.ИТОГИ";
       end;
       while(GetParm( i, CellName))
         if( m_ObjTemplateXLS.UsePoi() )
           TmpAddress = m_ObjTemplateXLS.GetWorkbook().getRangeAddress(CellName, true); 
         else
           TmpAddress = m_ObjTemplateXLS.Sheet.Range(CellName).Address; // Address имеет вид "$AE$1"
         end;
         if( (Pos = index(TmpAddress, "$")) != 0 )
           TmpAddress = SubStr(TmpAddress, Pos+1);
           if( (Pos = index(TmpAddress, "$")) != 0 )
             ColumnName = SubStr(TmpAddress, 1, Pos-1);
           end;
         end;

         j = FromSheetNum;
         FormulaTxt = "";
         while( j < m_UsedSheet.Size )
            FormulaTxt = "="+Formula+"(9;'"+m_UsedSheet[j]+"'!$"+ColumnName+"$"+StartStrNumber+":"+"$"+ColumnName+"$"+string(FinishStrNumber)+")";
            j = j + 1;
         end;

         m_ObjTemplateXLS.SetValue_NameCell( CellName, FormulaTxt  );

         i = i + 1;
       end;
     end;
  END;

  // ArrForSum - массив номеров строк по которым суммировать
  MACRO SetSumByArray(ArrForSum:TArray /*...*/ )
     var FormulaTxt = "";
     var SheetName = "";
     var CellName = "", ColumnName = "", TmpAddress="";
     var Pos = 0, i = 2;
     var FromSheetNum = 0, j = 0, k = 1;
 
     if(m_UseTemplate)

       SheetName = m_ObjTemplateXLS.SheetName();

       while( j < m_UsedSheet.Size )
          if( StrUpr(m_UsedSheet[j]) == StrUpr(SheetName) )
             FromSheetNum = j;
          end;
          j = j + 1;
       end;

       while(GetParm( i, CellName))
         if( m_ObjTemplateXLS.UsePoi() )
           TmpAddress = m_ObjTemplateXLS.GetWorkbook().getRangeAddress(CellName, true); 
         else
           TmpAddress = m_ObjTemplateXLS.Sheet.Range(CellName).Address; // Address имеет вид "$AE$1"
         end;
         if( (Pos = index(TmpAddress, "$")) != 0 )
           TmpAddress = SubStr(TmpAddress, Pos+1);
           if( (Pos = index(TmpAddress, "$")) != 0 )
             ColumnName = SubStr(TmpAddress, 1, Pos-1);
           end;
         end;

         j = FromSheetNum;
         FormulaTxt = "";
         while( j < m_UsedSheet.Size )
            FormulaTxt = "=";

            k = 1;
            while (k <= m_NSubKinds)
               if (k != 1)
                  FormulaTxt = FormulaTxt + "+";
               end;

               FormulaTxt = FormulaTxt + "'"+m_UsedSheet[j]+"'!$"+ColumnName+"$"+string(ArrForSum[k]);
               k = k + 1;
            end;

            j = j + 1;
         end;

         m_ObjTemplateXLS.SetValue_NameCell( CellName, FormulaTxt );

         i = i + 1;
       end;
     end;
  END;

  // SheetName_From - номер листа, с которого берём данные
  // ColumnName_From - код колонки, с которой берём данные
  // Num_From - номер строки, с которой берём данные
  // CellName_To - номер ячейки, куда пишем
  MACRO SetValueEx(SheetName_From:string, 
                   ColumnName_From:string, 
                   Num_From:Integer,
                   CellName_To:string
                  )
     var FormulaTxt = "";
 
     if(m_UseTemplate)
       FormulaTxt = "='"+SheetName_From+"'!$"+ColumnName_From+"$"+string(Num_From);
       m_ObjTemplateXLS.SetValue_NameCell( CellName_To, FormulaTxt );
     end;
  END;

  MACRO SetCurrentStrNumber()
     m_CurrStrNum = this.НомерТекущейСтроки();
  END;

  MACRO SetCurrentStrNumberToArray(i:INTEGER)
     m_CurrStrNum = this.НомерТекущейСтроки();
     ArrSubTotal[i] = m_CurrStrNum;
  END;

  MACRO ПечататьПромежуточныеИтоги()
     var i = 1, Start = 0, Finish = 0;

     while (i <= m_NSubKinds)
        if ((ArrSubTotal[i+1] - ArrSubTotal[i]) > 1)  // если печаталась какая-то информация

           Start = ArrSubTotal[i] + 1;
           if (i == m_NSubKinds) // в последнем - номер последней строки, а не следующей подстроки!
              Finish = ArrSubTotal[i+1];
           else
              Finish = ArrSubTotal[i+1] - 1;
           end;

           // суммирование для подстрок 1.1.5 - 1.1.9 или 1.2.1 - 1.2.4 или 1.3.1 - 1.3.3
           SetSubTotalEx(Start, Finish,
                         C_RQSumma + string(ArrSubTotal[i]),
                         C_Summa + string(ArrSubTotal[i])
                        );                                           
        end;

        i = i + 1;
     end;

     // суммирование для подстроки 1.1 или 1.2 или 1.3
     SetSumByArray(ArrSubTotal,
                   C_RQSumma + string(ArrSubTotal[1]-1),
                   C_Summa + string(ArrSubTotal[1]-1)
                  );                                           
  END;

  MACRO ПеренестиИтогиНаЛистПКЛ()
     var i = 1;

     SetActiveSheet( m_PklCode );

     // 1.1 или 1.2 или 1.3
     SetValueEx(GetNumSheet(), 
                C_RQSumma, 
                ArrSubTotal[1]-1,
                C_RQSumma0 + string(Coder0[ReportKind()])
               );

     SetValueEx(GetNumSheet(), 
                C_Summa, 
                ArrSubTotal[1]-1,
                C_Summa0 + string(Coder0[ReportKind()])
               );

     // 1.2.1 - 1.2.4
     while (i <= m_NSubKinds)

        SetValueEx(GetNumSheet(), 
                   C_RQSumma, 
                   ArrSubTotal[i],
                   C_RQSumma0 + string(Coder[i+m_Offset])
                  );

        SetValueEx(GetNumSheet(), 
                   C_Summa, 
                   ArrSubTotal[i],
                   C_Summa0 + string(Coder[i+m_Offset])
                  );
        i = i + 1;
     end;
  END;


  PRIVATE MACRO ExecuteReport_Ex( ObjReport:OBJECT )
     VAR DataQuery, DataFIID, PrevFIID = -1;
     VAR AddWhere = "";
     VAR ProgressValue = CTxProgressValue();
     VAR erFlag, SinceDate;
     VAR v_Break = false;

     DataQuery = 
                 " SELECT fininstr.t_FIID, fininstr.t_Name Iss_Name, fininstr.t_Issuer, fininstr.t_FacevalueFI, AtCor.t_AttrID, " +
                          " RSB_PMWRTOFF.WRTGetPortfolioAmount( -1, fininstr.t_FIID, -1, 0, -1, -1, " + GetSQLDate( date(Data.RepDate) ) + ", 1, 0, 0) as v_Amount " +

                 "   FROM dfininstr_dbt fininstr, " +
                          " (select attr.t_AttrID, attr.t_Object " +
                          "    FROM dobjatcor_dbt attr " + 
                          "   WHERE attr.t_ObjectType = " + string(OBJTYPE_AVOIRISS) +
                          "     and attr.t_GroupID = 52 " +
                          "     AND attr.T_VALIDFROMDATE <= " + GetSQLDate( Data.RepDate ) + 
                          "     AND attr.T_VALIDTODATE >= " + GetSQLDate( Data.RepDate ) + ") AtCor " +  

                          //На выпуске за дату отчета задана категория "Код инструмента для ПКЛ"
                 "  WHERE AtCor.t_Object = LPAD(fininstr.t_FIID, 10, '0') and " + 

                          IIF( Data.FaceFIID < 0, "", " fininstr.t_FacevalueFI = " + string(Data.FaceFIID) + " and ") +

                          //На выпуске за дату отчета задана категория "Возможность надежного определения справедливой стоимости"
                          " (select count(1) " +
                          "    FROM dobjatcor_dbt attr " + 
                          "   WHERE attr.t_ObjectType = " + string(OBJTYPE_AVOIRISS) +
                          "     and attr.t_GroupID = 27 " +
                          "     and attr.t_Object = LPAD(fininstr.t_FIID, 10, '0') " +
                          "     AND attr.T_VALIDFROMDATE <= " + GetSQLDate( Data.RepDate ) + 
                          "     AND attr.T_VALIDTODATE >= " + GetSQLDate( Data.RepDate ) + ") > 0 and " +  
                          " RSB_PMWRTOFF.WRTGetPortfolioAmount( -1, fininstr.t_FIID, -1, 0, -1, -1, " + GetSQLDate( Data.RepDate ) + ", 1, 0, 0) > 0 " +
                          ObjReport.Where() + 
                          " ORDER BY AtCor.t_AttrID ASC ";

     ProgressValue.Maximum = SQL_GetNRecs( DataQuery );
     ProgressValue.Current = 0;
     if( m_IsWebService )
        TxPutProgressValue( m_ReportHashCode, ProgressValue );
     end;

     if (ProgressValue.Maximum > 0)
        InitProgress( ProgressValue.Maximum, HTML_StSheetName, HTML_StSheetName );

        m_RS = TRsbDataSet( DataQuery );
        while( m_RS.MoveNext() )
           ClearCacheOneRecord();
           v_Break = false;

           /*Определим курс вида "Текущая справедливая стоимость".*/
           m_RQSumma = m_RS.v_Amount * money(GetRateOnDate( Data.RepDate, m_RS.FIID, m_RS.FaceValueFI, false, ПолучитьВидКурса(SP_RATETYPE_RightCost), erFlag, SinceDate ));

           if ((erFlag == true) or (SinceDate != Data.RepDate))
              msgbox("По выпуску " + string(m_RS.Iss_Name) + " не задан курс ТСС на дату отчета");
              v_Break = true;
           end;

           if ((m_ReportKind == PKL3) or (m_ReportKind == PKL4))
              m_Note = readNoteForObject( OBJTYPE_AVOIRISS, L_Z( m_RS.FIID, 10 ), 17/*Показатель обесценения*/);
              if (m_Note == 0.0)
                 msgbox("По выпуску " + string(m_RS.Iss_Name) + " не задано примечание \"Показатель обесценения\"");
                 v_Break = true;
              end;
           end;
           if (v_Break == false)
              if (m_RS.FaceValueFI != NATCUR)
                 SmartConvertSum(m_RQSumma, m_RQSumma, Data.RepDate, m_RS.FaceValueFI, NATCUR, false);
              end;

              ObjReport.Print();

              m_PrevAttrID = m_RS.AttrID;
           end;

           ProgressValue.Current = ProgressValue.Current + 1;
           UseProgress( ProgressValue.Current );
           if( m_IsWebService )
              TxPutProgressValue( m_ReportHashCode, ProgressValue );
           end;
        end;

        RemProgress();
     end;
  END;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )

     ExecuteReport();
     ObjReport.BeginReport();

     if (m_ReportKind != PKL1)
        ExecuteReport_Ex( ObjReport );
     end;
     ObjReport.EndReport();

     m_Offset = m_Offset + m_NSubKinds;
     m_PrevAttrID = m_Offset;
  END;

  PRIVATE MACRO CreateReportByKind( Kind:INTEGER )
    m_ReportKind = Kind;

    ArrSubTotal.size = 0;
    Fill_Cell_Name();
    Fill_Coder();

    if( Kind == PKL1 )
       m_SHEET = m_PklCode;
       ExecuteReport( CPKL_1(this) );

    elif( Kind == PKL2 )
       m_SHEET = "ВЛА-1";
       m_NSubKinds = 5;
       ExecuteReport( CPKL_2(this) );

    elif( Kind == PKL3 )
       m_SHEET = "ВЛА-2А";
       m_NSubKinds = 4;
       ExecuteReport( CPKL_3(this) );

    elif( Kind == PKL4 )
       m_SHEET = "ВЛА-2Б";
       m_NSubKinds = 3;
       ExecuteReport( CPKL_4(this) );
    end;
  END;

  PRIVATE MACRO Create()
     m_NSubKinds  = 0;
     m_PrevAttrID = 0;
     m_Offset     = 0;
     CreateReportByKind( PKL1 );
     CreateReportByKind( PKL2 );
     CreateReportByKind( PKL3 );
     CreateReportByKind( PKL4 );

  END;

  PRIVATE MACRO EndReport()
     ReplaceAndCalculate( FORMULA, "=", true );
  END;

  initTX_RegistrReport( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
)
  var stat = true;
  var FullReportFileNames = TArray();

  if( stat )
    if( not IsWebService )
      DecimalSeparator = GetLocaleInfo(0,LOCALE_SDECIMAL, false);
      PKL_Form = SP_PKLRep_Panel();
    else
      DecimalSeparator = GetLocaleInfo(0,LOCALE_SDECIMAL, true);
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = PKL_Form.Run();
    end;

    if( stat )
      PKL_Report = SP_PKL_Report( null, "Report_PKL.xls", null, IsWebService, ReportHashCode );
      PKL_Report.Run( );

      if( IsWebService )
        FullReportFileNames = PKL_Report.GetFullReportFileNames();

        stat = false;
      end;
    end;
    Dl_CallInsertStat(DL_OUTREPORT_EXCEL);
  end;

  return FullReportFileNames;
END;
