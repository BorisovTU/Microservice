 /*
$Name:             sp_calcreservground.mac
$Module:           Ценные бумаги 
$Description:      Расчет обеспечения по резерву
*/
import BankInter, RsbDataSet, fiinter, "cb_sql.mac", "secinter.mac", "sprepfun.mac", "dllog_xml.mac";

PRIVATE VAR dl_comm = TrecHandler( "dl_comm" );
PRIVATE VAR RateType:INTEGER;

PRIVATE CONST CALC_BY_REQ = 1; //по требованиям 2 части обр. РЕПО

PRIVATE MACRO FindDLCOMM( OperID:INTEGER ):BOOL
  var   Data = TRsbDataSet( "SELECT * FROM ddl_comm_dbt WHERE t_DocumentID = " + string(OperID) );

  if( Data.MoveNext() )
     Data.GetRecord().CopyTo( dl_comm.rec );
     return true;
  end;
  return false;
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Скроллинг операций
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
PRIVATE VAR Документ       = TRecHandler( "dl_comm" );
PRIVATE VAR СтарыйДокумент = TRecHandler( "dl_comm" ); /* заполняется при редактировании */

/* Макрофункция инициализации новой операции 
   необходимо заполнить структуру "Документ"
   возвращаемое значение
   0 - ошибок не было
   1 - была ошибка
*/
PRIVATE MACRO Новый_Документ():INTEGER
  return 0;
END;

/* Макрофункция проверки операции при вводе, редактировании, удалении, выполнении 
   возвращаемое значение
   0 - ошибок не было
   1 - была ошибка
*/
PRIVATE MACRO Проверить_Документ( Режим:INTEGER ):INTEGER
  return 0;
END;

/* Макрофункция вызывается по Ctrl-Z из скроллинга/панели */
PRIVATE MACRO Функция_Пользователя()
  MsgBox( "Выполняется макрофункция \"sp_calcreservground.mac\\ Функция_Пользователя\"." );
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
PRIVATE MACRO GetDealData( DealID:INTEGER, DealCode:@VARIANT, DealDate:@VARIANT )
  VAR rs, cmd;

  DealCode = "";
  DealDate = date(0,0,0);

  cmd = RSDCommand( "SELECT t_DealCode DealCode, t_DealDate DealDate FROM ddl_tick_dbt WHERE t_DealID = ? ");
  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, DealID ); 
  cmd.execute();
  rs = TRsbDataSet( cmd );
  if( rs.MoveNext() )
     DealCode = rs.DealCode;
     DealDate = date(rs.DealDate)
  end;
  return "";
END;

PRIVATE MACRO GetAvrData( DealID:INTEGER, CommDate:DATE, FI_Code:@VARIANT, FI_Name:@VARIANT, QualityCateg:@VARIANT )
  VAR rs, cmd;
  VAR FIID = -1, NumInList, ValidFromDate;

  FI_Code = "";
  FI_Name = "";
  QualityCateg = "";
                       
  cmd = RSDCommand(   "SELECT fi.t_FIID FIID, fi.t_FI_Code FI_Code, fi.t_Name FI_Name "
                    + "  FROM ddl_tick_dbt deal, dfininstr_dbt fi "
                    + " WHERE deal.t_DealID = ? AND fi.t_FIID = deal.t_PFI");
  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, DealID ); 
  cmd.execute();
  rs = TRsbDataSet( cmd );
  if( rs.MoveNext() )
    FIID    = rs.FIID;
    FI_Code = rs.FI_Code;
    FI_Name = rs.FI_Name;
  end;
  
  if(FIID != -1)
    if( (GetMainObjAttr( null, OBJTYPE_AVOIRISS, L_Z( FIID, 10 ), 13/*Категория качества*/, null, null, NumInList, CommDate, ValidFromDate) )
        AND (ValidFromDate <= CommDate)
      )
      QualityCateg = NumInList;
    end;
  end;

END;

/* Данные о ошибках выполнении */
CLASS ReserveError( _DealID:integer, _Mes:string )
  var DealID = _DealID,
      Mes    = _Mes;
END;

CLASS (DL_LOG_FROM_XML) RESERVE_LOG_FROM_XML( pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool )

  macro GetDataFromXMLByDocKind()

     var ErrorData:ReserveError;

     if( m_ReportVersion == 1 )

        if( DL_ReadTagAttribut(m_child_ReportNumber, "DEALID", V_INTEGER, ErrorData.DealID) == false )
        end;
        if( DL_ReadTagAttribut(m_child_ReportNumber, "MES", V_STRING, ErrorData.Mes) == false )
        end;

        return ReserveError( ErrorData.DealID,
                             ErrorData.Mes
                           );
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);
END;

PRIVATE CLASS CErrorList()
  VAR Errors = TArray();

  MACRO Clear()
     Errors.Size = 0;
  END;

  MACRO Size()
     return Errors.Size;
  END;

  MACRO Add( _DealID:INTEGER, _Mes:STRING )
     Errors[Errors.Size] = ReserveError( _DealID, _Mes );
  END;

  MACRO Save()
     DL_SaveDataForReport_XML(dl_comm.rec.DocumentID, dl_comm.rec.DocKind, Errors, LOG_TYPE_DATA);
  END;

  MACRO Print()
     var FI_Code = "", FI_Name = "", QualityCateg = "";
     var DealCode = "", DealDate = date(0,0,0);
     var ExNext:bool = false;
                             
     var ErrorLineData_XML = RESERVE_LOG_FROM_XML(dl_comm.rec.DocKind, dl_comm.rec.DocumentID, LOG_TYPE_DATA, true);

     if( not ErrorLineData_XML  )
        return;
     end;

     ErrorLineData_XML.SetReportNumber(0);
     ExNext = ErrorLineData_XML.Next();

     if( not ExNext )
        return;
     end;
[

                        Сделки с нерассчитанной суммой обеспечения
┌──────────────────────────────┬───────────┬──────────────────┬───────────────────────────┬──────────────────┬─────────────────────────────────────────────────────┐
│   № сделки                   │Дата сделки│Код ценной бумаги │Наименование ценной бумаги │Категория качества│ Причина невозможности рассчитать сумму обеспечения  │
│                              │           │                  │                           │ценной бумаги     │                                                     │
];
     while( ExNext )
       var LineData = ErrorLineData_XML.GetReportLineData();
       GetDealData(LineData.DealID, @DealCode, @DealDate);
       GetAvrData(LineData.DealID, dl_comm.rec.CommDate, @FI_Code, @FI_Name, @QualityCateg);                                                                        
[├──────────────────────────────┼───────────┼──────────────────┼───────────────────────────┼──────────────────┼─────────────────────────────────────────────────────┤
│##############################│###########│##################│###########################│##################│#####################################################│
]( DealCode, DealDate:f, FI_Code, FI_Name:w, QualityCateg, LineData.Mes:w );
        ExNext = ErrorLineData_XML.Next();
     end;

[└──────────────────────────────┴───────────┴──────────────────┴───────────────────────────┴──────────────────┴─────────────────────────────────────────────────────┘
];
  END;
END;

PRIVATE CLASS CSumList()
  VAR SumList = TArray();

  CLASS CData( _DealID:INTEGER, _Summa:MONEY )
     VAR DealID = _DealID, Summa = _Summa;
  END;

  MACRO Clear()
     SumList.Size = 0;
  END;

  MACRO Add( _DealID:INTEGER, _Summa:MONEY )
     SumList[SumList.Size] = CData( _DealID, _Summa );
  END;

  MACRO Print( Header:STRING )
     var FI_Code = "", FI_Name = "", QualityCateg = "";
     var DealCode = "", DealDate = date(0,0,0);

     if( SumList.Size <= 0 )
        return;
     end;

[
     #
 ┌──────────────────────────────┬───────────┬──────────────────┬───────────────────────────┬──────────────────┬─────────────────────┐
 │   № сделки                   │Дата сделки│Код ценной бумаги │Наименование ценной бумаги │Категория качества│ Сумма обеспечения   │
 │                              │           │                  │                           │ценной бумаги     │                     │
](Header);

     VAR i = 0;
     while( i < SumList.Size )
       GetDealData(SumList[i].DealID, @DealCode, @DealDate);
       GetAvrData(SumList[i].DealID, dl_comm.rec.CommDate, @FI_Code, @FI_Name, @QualityCateg);
                   
[├──────────────────────────────┼───────────┼──────────────────┼───────────────────────────┼──────────────────┼─────────────────────┤
 │##############################│###########│##################│###########################│##################│#####################│
]( DealCode, DealDate:f, FI_Code, FI_Name:w, QualityCateg, SumList[i].Summa );
        i = i + 1;
     end;
[└──────────────────────────────┴───────────┴──────────────────┴───────────────────────────┴──────────────────┴─────────────────────┘

];

  END;

END;

PRIVATE VAR Errors = CErrorList();
PRIVATE VAR SumGuarant = CSumList();

PRIVATE MACRO Convert( DealID:INTEGER, AvrFIID:INTEGER, ToFIID:INTEGER, CalcDate:DATE, Pr:@DOUBLE, MarketID:INTEGER, CentrOffice:INTEGER ):BOOL
  if( SmartConvertSumDbl(  Pr, 1.0, CalcDate, AvrFIID, ToFIID, false, RateType, NULL, MarketID, CentrOffice ) )

     if( ToFIID != NATCUR )
        if( SmartConvertSumDbl(  Pr, Pr, CalcDate, ToFIID, NATCUR, false ) )
           return true;
        else
           Errors.Add( DealID, GetCurrencyConvertErrorMsg( ToFIID, NATCUR, CalcDate ) );
           return false;
        end;
     else
        return true;
     end;
  else
     Errors.Add( DealID, GetCurrencyConvertErrorMsg( AvrFIID, ToFIID, CalcDate ) );
     return false;
  end;

  return true;
END;
                                   
PRIVATE MACRO ПолучитьОбъемТоргов( PFI:INTEGER, ToFIID:INTEGER, CalcDate:DATE, Market_Place:INTEGER, Section:INTEGER, Volume:@DOUBLE ):BOOL
   Volume = 0.0;
   return SmartConvertSumDbl(  Volume, 1.0, CalcDate, PFI, ToFIID, false, 17/*Объём торгов*/, NULL, Market_Place, Section );
END;

PRIVATE MACRO GetCource( Deal/*TRsbDataSet*/, Pr:@double ):BOOL
  VAR sql, rsd, Exists = false, NumCource = 0, LastVolume = 0, Volume = 0, ToFIID, MarketID = -1, CentrOffice = -1;

  sql = RSDCommand(" SELECT count(1) NumRec FROM dratedef_dbt WHERE t_OtherFI = ? AND t_Type = ? " );

  sql.addParam( "", RSDBP_IN, Deal.PFI );
  sql.addParam( "", RSDBP_IN, RateType );
  sql.execute();

  rsd = TRsbDataSet(sql);
  if( rsd.MoveNext() )
     NumCource = rsd.NumRec;
  end;

  if( NumCource > 0 )

     sql = RSDCommand(" SELECT * FROM dratedef_dbt WHERE t_OtherFI = ? AND t_Type = ? " );

     sql.addParam( "", RSDBP_IN, Deal.PFI );
     sql.addParam( "", RSDBP_IN, RateType );
     sql.execute();

     rsd = TRsbDataSet(sql);

     if( NumCource == 1 )
        if( rsd.MoveNext() )
           return Convert( Deal.DealID, Deal.PFI, rsd.FIID, dl_comm.rec.CommDate, @Pr );
        end;

     elif( NumCource > 1 )
        while( rsd.MoveNext() )
           if( ПолучитьОбъемТоргов( Deal.PFI, rsd.FIID, dl_comm.rec.CommDate, rsd.Market_Place, rsd.Section, @Volume ) ) 
              if( LastVolume < Volume )
                 MarketID    = rsd.Market_Place; 
                 CentrOffice = rsd.Section;
                 ToFIID      = rsd.FIID;
                 LastVolume  = Volume;
              end;
           end;
        end;

        if( MarketID < 0 )
           Errors.Add( Deal.DealID, "Для ценной бумаги не заданы объемы торгов за "+string(dl_comm.rec.CommDate)+" ни для одной биржи" );
           return false;                
        end;

        return Convert( Deal.DealID, Deal.PFI, ToFIID, dl_comm.rec.CommDate, @Pr, MarketID, CentrOffice );
     end;
  end;

  Errors.Add( Deal.DealID, "Для ценной бумаги не задан курс вида \""+GetRateTypeName( RateType )+"\"" );
  return false;
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
PRIVATE MACRO Calculate( Deal/*TRsbDataSet*/, TypeReserv:INTEGER ):BOOL
  VAR NoteKind, ValidFromDate, NumInList, dl_leg, 
      K = 0, // коэффициент категории качества обеспечения
      Pr= 0,// цена одной ценной бумаги для расчета обеспечения в рублях
      B, // вид курса, который будет использован при расчете обеспечения
      V= 0, // количество ценных бумаг в сделке
      C:MONEY; // Сумма обеспечения

  if( (not GetMainObjAttr( null, OBJTYPE_AVOIRISS, L_Z( Deal.PFI, 10 ), 50/*Категория качества обеспечения*/, null, null, NumInList, dl_comm.rec.CommDate, ValidFromDate) )
      OR (ValidFromDate > dl_comm.rec.CommDate)
    )
     Errors.Add( Deal.DealID, "Для ценной бумаги не задана категория качества обеспечения" );
     return false;
  elif( NumInList == "1" )
     K = 1;
  elif( NumInList == "2" )
     K = 0.5;
  elif( NumInList == "3" )
     K = 0.2;
  else
     K = 0.0;
  end;

  if( not GetCource( Deal, @Pr ) )
     return false;
  end;

  dl_leg = TrecHandler( "dl_leg.dbt"   ); 
  if( FindDL_LEG( LEG_KIND_DL_TICK, Deal.DealID, 0, dl_leg ) )
     Errors.Add( Deal.DealID, "Не найдены условия по первой части сделки" );
     return false;
  end;
  V = dl_leg.rec.Principal;

  C = K*Pr*V;

  if( TypeReserv == CALC_BY_REQ )
     NoteKind = NOTEKIND_SECDEAL_SUMGUARANT; // 10 Сумма обеспечения
  end;

  if( addNoteForObject( Deal.BofficeKind, 
                        L_Z( Deal.DealID, 34 ),
                        NoteKind, 
                        C, 
                        dl_comm.rec.CommDate ) 
    )
     Errors.Add( Deal.DealID, "Ошибка при установке примечания |\""+ИмяПримечания(Deal.BofficeKind, NoteKind) );
     return false;
  end;

  return true;
END;

PRIVATE MACRO Report( Deal/*TRsbDataSet*/, TypeReserv:INTEGER ):BOOL
  VAR NoteKind, Summa;

  if( TypeReserv == CALC_BY_REQ )
     NoteKind = NOTEKIND_SECDEAL_SUMGUARANT; // 10 Сумма обеспечения
  end;

  Summa = readNoteForObject( Deal.BofficeKind, L_Z( Deal.DealID, 34 ), NoteKind, dl_comm.rec.CommDate );

  if( Summa != 0 )
     if( TypeReserv == CALC_BY_REQ )
        SumGuarant.Add( Deal.DealID, Summa);
     end;
  end;

  return true;
END;

PRIVATE MACRO RunByDeal( RunFun:ProcRef, IsReport:bool ):BOOL
  VAR rs, cmd;

  if( dl_comm.rec.Deal1 > 0 )
     cmd = RSDCommand( "SELECT * FROM ddl_tick_dbt WHERE t_DealID = ? ");
     cmd.NullConversion = true;
     cmd.addParam( "", RSDBP_IN, dl_comm.rec.Deal1 ); 
     cmd.execute();
     rs = TRsbDataSet( cmd );
     if( rs.MoveNext() )
        if( dl_comm.rec.Flag1 == "X" )
           return ExecMacro2( RunFun, rs, CALC_BY_REQ );
        end;
     end;

     MsgBox( "Не найдена сделка, заданная в форме операции" );
     return false;
  end;

  VAR RQ = TRecHandler( "dlrq" ), Query;
                                  
  Query = "SELECT * from ddl_tick_dbt deal, doprkoper_dbt opr "+
                    " WHERE     opr.T_KIND_OPERATION = deal.t_DealType "+
                    "       AND deal.t_BofficeKind = 101 "+
                    "       AND deal.t_DealStatus = 10 "+
                    "       AND deal.t_ClientID <= 0 "+
                    "       AND deal.t_DealDate <= ? "+
                    "       AND rsb_secur.IsRepo(rsb_secur.get_OperationGroup(opr.t_SysTypes))=1 "+
                    "       AND rsb_secur.IsBuy(rsb_secur.get_OperationGroup(opr.t_SysTypes))=1 ";
  if( dl_comm.rec.FIID > 0 )
     Query = Query + " AND deal.t_PFI = ? "
  end;

  cmd = RSDCommand( Query );
  cmd.NullConversion = true;
  cmd.addParam( "", RSDBP_IN, dl_comm.rec.CommDate ); 
  if( dl_comm.rec.FIID > 0 )
     cmd.addParam( "", RSDBP_IN, dl_comm.rec.FIID ); 
  end;

  cmd.execute();
  rs = TRsbDataSet( cmd );

  while( rs.MoveNext() )
     /*Если бумага не задана, то с любой бумагой с категорией Котируемая == Да*/
     if( (dl_comm.rec.FIID <= 0) AND (not FI_IsQuoted(rs.rec.t_PFI)) )
        continue;
     end;

     if( dl_comm.rec.Flag1 == "X" )
        /* Дата ТО по оплате 1ч <= даты операции и ТО по оплате 1ч исполнено */
        if(ПолучитьТОпоДокументу(rs.BOfficeKind, rs.DealID, 1, DLRQ_TYPE_PAYMENT, 0, RQ) == false)
           if( not IsReport )
              Errors.Add( rs.DealID, "Не найдено ТО по первой части" );
           end;
           continue;
        end;

        if( (RQ.rec.FactDate > dl_comm.rec.CommDate) OR (not ТОЗакрыто( RQ )) )
           continue;
        end;

        /*  Дата ТО по оплате 2ч > даты операции */
        if( ПолучитьТОпоДокументу(rs.BOfficeKind, rs.DealID, 2, DLRQ_TYPE_PAYMENT, 0, RQ) == false )
           if( not IsReport )
              Errors.Add( rs.DealID, "Не найден платеж по второй части" );
           end;
           continue;
        end;

        if( RQ.rec.PlanDate <= dl_comm.rec.CommDate )
           continue;
        end;

        ExecMacro2( RunFun, rs, CALC_BY_REQ );
     end;
  end;
  return true;
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Отчет по операции
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
MACRO PrintReport( OperID:INTEGER, IsExecute:BOOL ):BOOL
  VAR ReportFileName, errtext, errcode;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/

  if( not FindDLCOMM( OperID ) )
     MsgBox( "Не найдена операция расчета обеспечения (ID="+string(OperID)+")" );
     return false;
  end;

  ReportFileName = GetTxtFileName( "spcrg_" + dl_comm.rec.CommCode );
  if( not Open( ReportOutFile, ReportFileName ) )
     errcode = status(errtext);
     RunError( "Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext );
  end;
  SetOutput( ReportFileName );

  SumGuarant.Clear();

  RunByDeal(@Report, true);

[                 Протокол операции расчета суммы обеспечения
   Код операции   ##############################   Дата расчета:  ##########
](dl_comm.rec.CommCode, dl_comm.rec.CommDate);

  SumGuarant.Print( "Расчет обеспечения для резервов по требования по 2 части обр. РЕПО" );

  Errors.Print();

  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );
  return true;
END;

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Выполнение операции
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
MACRO CalculateReservGround( OperID:INTEGER ):INTEGER
  VAR ErrCode;

  Errors.Clear();

  if( not FindDLCOMM( OperID ) )
     MsgBox( "Не найдена операция расчета обеспечения (ID="+string(OperID)+")" );
     return false;
  end;

  GetRegistryValue( "SECUR\\ВИД КУРСА РАСЧЕТА ОБЕСПЕЧЕНИЯ", V_INTEGER, RateType, ErrCode );
  if( ErrCode != 0 )
     MsgBox( "Ошибка при получении значения настройки \"SECUR\\ВИД КУРСА РАСЧЕТА ОБЕСПЕЧЕНИЯ\"" );
     return 1;
  end;

  RunByDeal(@Calculate, false);

  if( Errors.Size() > 0 )
     Errors.Save();
  end;

  return 0;
END;
