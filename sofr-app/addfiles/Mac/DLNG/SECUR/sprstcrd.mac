/*
$Name:        sprstcrd.mac
$Module:      Депозитарий
$Description: Отчет "Проверочная ведомость остатков лицевых счетов ДЕПО"
*/

import RsbDataSet, deposerv, FIInter, CurrInter;

/* Глава синтетического учета */
const synt_chapter = 5;
var StdRep, isPrint, isPrintErr,
    IsprintHeader = false, IsPrintSubHeader = false, no_rest = true, data = false;

var depo_acc, acc_kind, dprt_num, rep_date, null_overs, resultbyacc, disp_part, ToExcel, use_ap, LogOprID;

var HeadTable = "┌───────────────┬───────────────────────────┬────────────────────────────────┬────────────────────────────┐\n"+
                "│   Код счета   │   Номер государственной   │     Код лицевого счета депо    │ Остаток ценных бумаг       │\n"+
                "│(раздела счета)│   регистрации выпуска     │                                │                            │\n"+
                "│     ДЕПО      │   ценных бумаг            │                                │                            │\n"+
                "├───────────────┼───────────────────────────┼────────────────────────────────┼────────────────────────────┤";

var tablewidth = Index(HeadTable, "\n");

var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

macro Header(rep_date)
  isPrint = true;

  DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Проверочная ведомость остатков лицевых счетов ДЕПО", LogOprID, 0, false, rep_date, rep_date, tablewidth );
end;

macro sub_header(acc_kind)
     var kind_name;
     if ( acc_kind == depo_acc_active )
          kind_name = "Активные";
     else
          kind_name = "Пассивные";
     end;

     DP_AddPrintCell(Rep, kind_name + " счета:", Rep.GetColWidthTable(0)+Rep.GetColWidthTable(1)+Rep.GetColWidthTable(2)+Rep.GetColWidthTable(3)+3, 0, "l:" + RepFontStyleTitel);
     Rep.AddStr();
end;

macro depo_acc_report( depoacc, rep_date, null_overs, resultbyacc, disp_part, use_ap, acc_rest:@variant, max_point:@variant )
     var counted = 0, iss_LSIN, iss_rest, depo_code, oldBriefCode = "", part_rest = $0, part_max_point = 0;
     var t_point = AVOIRISS_SUM_PRECISION;
     var DataSet, sQuery;
     record iss(avoiriss);
     file fininstr(fininstr);
     var cFin;

     acc_rest = $0.0;
     max_point = 0;

     sQuery =   " select acc.t_Account, acc.t_Code_Currency, dpac.t_BriefCode, "
              + " cast(rsb_account.restac( acc.t_Account, acc.t_Code_Currency, ? , acc.t_Chapter, null ) as number(32,12) ) as Rest "
              + " from daccount_dbt acc, ddepoacnt_dbt dpac"
              + " where acc.t_DepoRoot = ? "
              + " and acc.t_Code_Currency != 0 and acc.t_chapter = ? "
              + " and acc.t_Open_Date <= ? "
              + " and dpac.t_AutoKey = acc.t_DepoAcc";

     if(disp_part)
       sQuery = sQuery + " order by t_DepoAcc, t_Code_Currency, t_Account";
     else
       sQuery = sQuery + " order by t_Code_Currency, t_Account";
     end;

     sQuery = RSDCommand(sQuery);

     sQuery.addParam( "", RSDBP_IN, rep_date );
     sQuery.addParam( "", RSDBP_IN, depoacc.AutoKey );
     sQuery.addParam( "", RSDBP_IN, synt_chapter );
     sQuery.addParam( "", RSDBP_IN, rep_date );
     sQuery.execute();

     depo_code = depoacc.BriefCode;
     DataSet = TRsbDataSet(sQuery);

     while ( DataSet.MoveNext() )
          data = true;

          cfin = CDPFinInstr(DataSet.Code_Currency, rep_date);
          iss_LSIN = cfin.LSIN();
          fininstr.FIID = DataSet.Code_Currency;
          if( GetEQ(fininstr))
             if(iss_LSIN == "")
                iss_LSIN = fininstr.Name;
             end;
             t_point = fininstr.SumPrecision;
          else
             t_point = AVOIRISS_SUM_PRECISION;
          end;

          iss_rest = DataSet.Rest;
          if ( (iss_rest != $0.0) or null_overs )
            if( not IsprintHeader )
              IsprintHeader = true;
              Header(rep_date);
            end;
            if( IsPrintSubHeader )
              IsPrintSubHeader = false;
              sub_header(depoacc.Kind);
            end;

            if(depo_code != "")
              DP_AddPrintCell(Rep, depo_code, 0, 0, "l:");
              if( disp_part)
                DP_AddPrintCell(Rep, "", 0, 0, "l:");
                DP_AddPrintCell(Rep, "", 0, 0, "l:");
                DP_AddPrintCell(Rep, "", 0, 0, "l:");
                Rep.AddStr();
              end;
            end;

            if(disp_part)
              if(DataSet.t_BriefCode != oldBriefCode)
                if(oldBriefCode != "")
                  DP_AddPrintCell(Rep, "", 0, 0, "l:");
                  DP_AddPrintCell(Rep, "", 0, 0, "l:");
                  DP_AddPrintCell(Rep, "  Итого:", 0, 0, "l:" + RepFontStyleTitel);
                  DP_AddPrintCell(Rep, part_rest, 0, DP_GetPrecision(part_rest, part_max_point), "t:r:" + RepFontStyleTitel, use_ap);
                  Rep.AddStr();
                  part_rest = $0;
                  part_max_point = AVOIRISS_SUM_PRECISION;
                end;

                DP_AddPrintCell(Rep, " " + DataSet.t_BriefCode, 0, 0, "l:");
                oldBriefCode = DataSet.t_BriefCode;
              else
                DP_AddPrintCell(Rep, "", 0, 0, "l:");
              end;
            else
              if(depo_code == "" )
                DP_AddPrintCell(Rep, depo_code, 0, 0, "l:");
              end;
            end;
            DP_AddPrintCell(Rep, iss_LSIN, 0, 0, "l:");
            DP_AddPrintCell(Rep, DataSet.Account, 0, 0, "l:");
            DP_AddPrintCell(Rep, abs(iss_rest), 0, DP_GetPrecision(iss_rest, t_point), "t:r:", use_ap);
            Rep.AddStr();
            depo_code = "";
            counted = counted + 1;
            no_rest = false;   //Значит есть остатки
          end;

          acc_rest = acc_rest + abs(iss_rest);
          if(max_point < t_point)
            max_point = t_point;
          end;

          part_rest = part_rest + abs(iss_rest);

          if(part_max_point < t_point)
            part_max_point = t_point;
          end;
     end;

     if ( (counted == 0) and (null_overs) )
       if( not IsprintHeader )
         IsprintHeader = true;
         Header(rep_date);
       end;
       if( IsPrintSubHeader )
         IsPrintSubHeader = false;
         sub_header(depoacc.Kind);
       end;
       DP_AddPrintCell(Rep, depoacc.BriefCode, 0, 0, "l:");
       DP_AddPrintCell(Rep, "", 0, 0, "l:");
       DP_AddPrintCell(Rep, "", 0, 0, "l:");
       DP_AddPrintCell(Rep, "", 0, 0, "l:");
       Rep.AddStr();
     end;

     if((disp_part) AND (oldBriefCode != ""))
       DP_AddPrintCell(Rep, "", 0, 0, "l:");
       DP_AddPrintCell(Rep, "", 0, 0, "l:");
       DP_AddPrintCell(Rep, "  Итого:", 0, 0, "l:" + RepFontStyleTitel);
       DP_AddPrintCell(Rep, part_rest, 0, DP_GetPrecision(part_rest, part_max_point), "t:r:" + RepFontStyleTitel, use_ap);
       Rep.AddStr();
       part_rest = $0;
       part_max_point = AVOIRISS_SUM_PRECISION;
     end;

     if(((counted) OR (null_overs)) AND (resultbyacc)) // Выводить итоги по счету
       DP_AddPrintCell(Rep, "", 0, 0, "l:");
       DP_AddPrintCell(Rep, "", 0, 0, "l:");
       if(disp_part)
         DP_AddPrintCell(Rep, " Итого по счету депо " + depoacc.BriefCode + ":", 0, 0, "l:" + RepFontStyleTitel);
       else
         DP_AddPrintCell(Rep, " Итого:", 0, 0, "l:" + RepFontStyleTitel);
       end;
       DP_AddPrintCell(Rep, acc_rest, 0, DP_GetPrecision(acc_rest, max_point), "t:r:" + RepFontStyleTitel, use_ap);
       Rep.AddStr();
     end;

     return counted;
end;

macro check_depo_acc ( depoacc, acc_kind, rep_date)
     if(depoacc.Superior != 0) /*нам нужны только счета, а разделы не нужны*/
        return false;
     end;
     if ( (acc_kind != depo_acc_any) and (depoacc.Kind != acc_kind) )
        return false;
     end;
     if ( depoacc.OpenDate > rep_date )
        return false;
     end;
     return true;
end;

macro report_group(dprt_num, acc_kind, rep_date, null_overs, resultbyacc, disp_part, use_ap )
     var iDpAcc = 0, iAcc = 0;
     record depoacc(depoacnt);
     var DataSet, sQuery;
     var err = 0;
     var total_acc_rest = $0, total_max_point = 0, acc_rest = $0, max_point = 0;

     ClearRecord( depoacc );

     sQuery =   " select t_AutoKey, t_BriefCode, t_Kind"
              + " from ddepoacnt_dbt "
              + " where t_Superior = 0 and t_OpenDate <= " + GetSQLDate(rep_date)
              + "   and t_Department = " + dprt_num;

     if ( acc_kind != depo_acc_any )
        sQuery = sQuery + " and t_Kind = " + acc_kind;
     end;

     sQuery = sQuery + " order by t_Code";

     DataSet = TRsbDataSet(sQuery);

     InitProgress(-1, "Поиск счетов для отчета", "Просмотр счетов ДЕПО");

     IsPrintSubHeader = true;

     while ( DataSet.MoveNext() )
        UseProgress( iDpAcc = iDpAcc + 1 );
        depoacc.AutoKey   = DataSet.AutoKey;
        depoacc.BriefCode = DataSet.BriefCode;
        depoacc.Kind      = DataSet.Kind;

        iAcc = iAcc + depo_acc_report( depoacc, rep_date, null_overs, resultbyacc, disp_part, use_ap, @acc_rest, @max_point );

        total_acc_rest = total_acc_rest + acc_rest;
        if(max_point > total_max_point)
          total_max_point = max_point;
        end;
     end;

     if(resultbyacc) // Выводить итоги по счету
       DP_AddPrintCell(Rep, "", 0, 0, "l:");
       DP_AddPrintCell(Rep, "", 0, 0, "l:");
       if(acc_kind == depo_acc_active)
         DP_AddPrintCell(Rep, "Итого за активные счета:", 0, 0, "l:" + RepFontStyleTitel);
       else
         DP_AddPrintCell(Rep, "Итого за пассивные счета:", 0, 0, "l:" + RepFontStyleTitel);
       end;
       DP_AddPrintCell(Rep, total_acc_rest, 0, DP_GetPrecision(total_acc_rest, total_max_point), "t:r:" + RepFontStyleTitel, use_ap);
       Rep.AddStr();
     end;

     if( (iDpAcc == 0) or (iAcc == 0) )
        isPrintErr = true;
        RemProgress();
        return 1;
     end;

     RemProgress();

     return err;
end;

private macro CreateRep()
     record depoacc(depoacnt);
     var err = 0;

     isPrint = false;
     isPrintErr = false;
     IsPrintSubHeader = true;

     DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

     if ( depo_acc != "" )
          if ( GetDepoAcc( depo_acc, depoacc) != 0 )
               DP_EndProtocol();                 //закончить протоколирование

               MsgBox("Не могу получить счет депо указанный в параметрах отчета");
               return;
          end;
          if ( check_depo_acc(depoacc, acc_kind, rep_date) )
               depo_acc_report( depoacc, rep_date, null_overs, resultbyacc, disp_part, use_ap );
          end;
     else
          if ( acc_kind == depo_acc_any )
               err = report_group(dprt_num, depo_acc_active, rep_date, null_overs, resultbyacc, disp_part, use_ap );
               if( err == 0 )
                 err = report_group(dprt_num, depo_acc_passive, rep_date, null_overs, resultbyacc, disp_part, use_ap );
               end;
          else
               err = report_group(dprt_num, acc_kind, rep_date, null_overs, resultbyacc, disp_part, use_ap );
          end;
     end;

     if ( (data) and (no_rest) and (not null_overs) )
       if( not IsprintHeader )
         IsprintHeader = true;
         Header(rep_date);
       end;

       DP_AddPrintCell(Rep, " ", 0, 0, "l:");
       DP_AddPrintCell(Rep, " ", 0, 0, "l:");
       DP_AddPrintCell(Rep, " ", 0, 0, "l:");
       DP_AddPrintCell(Rep, " ", 0, 0, "l:");
       Rep.AddStr();
       Rep.AddEmptyStr();
       DP_AddPrintCell(Rep, "На дату " + string(rep_date) + " остатки по всем лицевым счетам депо нулевые", tablewidth, 0, "l:w::" + RepFontStyleTitel, REP_ELEM_STR);
       Rep.AddStr();
     end;

     if(isPrint)
       DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);
     end;

     DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
     DP_EndProtocol();                 //закончить протоколирование

     return isPrint;
end;

private macro CreateEmptyRep()

  Header(rep_date);
  Rep.AddEmptyStr();
  DP_AddPrintCell( Rep,  "Нет данных, удовлетворяющих параметрам отчета.", tablewidth, 0, "l:" + RepFontStyleTitel, true, REP_ELEM_STR );
  Rep.AddEmptyStr();
  DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);
  return 1;
end;

macro rest_card(_depo_acc, _acc_kind, _dprt_num, _rep_date, _null_overs, _resultbyacc, _disp_part, _ToExcel, _use_ap, _LogOprID)

  depo_acc    =  _depo_acc;
  acc_kind    =  _acc_kind;
  dprt_num    =  _dprt_num;
  rep_date    =  _rep_date;
  null_overs  =  _null_overs;
  resultbyacc =  _resultbyacc;
  disp_part   =  _disp_part;
  ToExcel     =  _ToExcel;
  use_ap      =  _use_ap;
  LogOprID    =  _LogOprID;

  Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
  return DP_StdReportControl( _LogOprID, CreateRep(), @CreateEmptyRep, 0, ToExcel, Rep );
end;
