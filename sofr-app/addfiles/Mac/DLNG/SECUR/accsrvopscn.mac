/*
$Name:        accsrvopscn.mac
$Module:      Ценные бумаги
$Description: Массовое выполнение действий над операциями бухгалтерского учета
*/
IMPORT SPInter, DealsInter;   
 
PRIVATE VAR Oper = TRecHandler( "dl_comm.dbt" );

PRIVATE CONST ERROR_EXEC = 0,
              PRINT_HEADER = 1,
              PRINT_FOOTER = 2;

PRIVATE MACRO PrintHead( Head:STRING )
[
                   #
┌──────────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────┐
│            Код           │  №   │                      Сообщение                                                                    │
│         операции         │ошибки│                                                                                                   │
├──────────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────┤](Head);
END;


PRIVATE MACRO PrintFooter()
[└──────────────────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────┘
];

END;

PRIVATE MACRO PrintLine( ErrStatus:INTEGER, ErrMessage:STRING )
[│##########################│######│###################################################################################################│]
( Oper.rec.CommCode:w, ErrStatus, ErrMessage:w );
END;

PRIVATE VAR OperByID = TBFile( "dl_comm.dbt", "R", 0 );
PRIVATE MACRO GetCurrentCommStatus():INTEGER
  OperByID.Clear();
  OperByID.rec.DocumentID = Oper.rec.DocumentID;
  if( OperByID.GetEQ() )
     return OperByID.rec.CommStatus;
  end;
  return -1;
END; 

PRIVATE MACRO ExecuteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  VAR CommStatus;

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом создании операций бухгалтерского учета." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        CommStatus = GetCurrentCommStatus();
        if( CommStatus == DL_COMM_CLOSED )
           ErrMessage = "Операция закрыта.";
        elif( CommStatus == DL_COMM_CLOSED_ERROR ) 
           ErrMessage = "Операция закрыта с ошибкой.";
        elif( CommStatus == DL_COMM_PREPARING ) 
           ErrMessage = "Операция не создана.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO DeleteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  VAR CommStatus;

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом удалении операций бухгалтерского учета." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        CommStatus = GetCurrentCommStatus();
        if( (CommStatus == DL_COMM_READIED) OR (CommStatus == DL_COMM_CLOSED) OR (CommStatus == DL_COMM_CLOSED_ERROR) )
           ErrMessage = "Операция не удалена.";
        elif( CommStatus == DL_COMM_PREPARING ) 
           ErrMessage = "Операция перемещена в отложенные.";
        else
           ErrMessage = "Операция удалена успешно.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO MoveToPrepOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  VAR CommStatus;

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом перемещении в отложенные операций бухгалтерского учета." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        CommStatus = GetCurrentCommStatus();
        if( CommStatus != DL_COMM_PREPARING ) 
           ErrMessage = "Операция не перемещена в отложенные.";
        else
           ErrMessage = "Операция перемещена в отложенные.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

/* Action = 1 - начало/продолжение операции по договору (F2 из скроллинга )
   Action = 2 - перевод в отложенные (Alt+F8 из скроллинга )
   Action = 3 - удаление (F8 из скроллинга )
*/
MACRO Печать_ОтчетСканирования( NumExec:INTEGER, FDoc:VARIANT, ErrStatus:INTEGER, ErrMessage:STRING, Action:INTEGER )

  Oper.SetRecordAddr( FDoc );

  if( Action == 1 )
     ExecuteOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 2 )
     MoveToPrepOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 3 )
     DeleteOperation( NumExec, ErrStatus, ErrMessage );
  end;

END;
