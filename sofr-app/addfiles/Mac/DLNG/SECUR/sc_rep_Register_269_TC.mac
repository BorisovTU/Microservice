/*
$Name:             sc_rep_Register_269_TC.mac
$Module:           Ценные бумаги 
$Description:      Отчет "Регистр налогового учета по ст. 269 НК"
*/

IMPORT "secinter.mac", "sp_class.mac", "dltxrate.mac";

private const ДОХОД  = 1,
              РАСХОД = 2;

private const НЕ_ОПРЕДЕЛЕНО           = 0,
              ЗАДАНА_ПО_КОНТРАГЕНТУ   = 1,
              ЗАДАНА_ПО_СДЕЛКЕ        = 2;

private class CProcDate(pCurBegDate:date,pCurEndDate:date,pCalcKind:integer)

  var CurBegDate:date;
  var CurEndDate:date;
  var CalcKind:integer;

  CurBegDate = pCurBegDate;
  CurEndDate = pCurEndDate;
  CalcKind   = pCalcKind;

end;

/* Процедура сравнения элементов массива
   Использууется при сортировке. */
private macro CompareForSort( key, elem )
   if( key.CurEndDate > elem.CurEndDate ) return 1;   end;
   if( key.CurEndDate < elem.CurEndDate ) return -1;  end;

   return 0;
end;

private var IsChangeRate = false;/*признак того, что ставка меняеется в данном календарном периоде*/
private class (TArray) CProcPeriod()

  /* Очистить массив */
  macro ClearArray
     this.Size = 0;
  end;

  macro AddProcPeriod(pEndDate:date,pIsChangeRate:bool)
     var
        Counter     = this.Size, i = 0, IsExists = false;

     if( Counter > 0 )
        while( i < Counter )
           if( this[i].CurEndDate == pEndDate )
              IsExists = true;
              break;
           end;
           i = i + 1;
        end;

        if( not IsExists )
           if( (pIsChangeRate != null) and (pIsChangeRate != false) )
              IsChangeRate = true;
           end;
           this[Counter] = CProcDate(Date(0,0,0),pEndDate,-1);
        end;
     else
        if( (pIsChangeRate != null) and (pIsChangeRate != false) )
           IsChangeRate = true;
        end;
        this[Counter] = CProcDate(Date(0,0,0),pEndDate,-1);
     end;

  end;

  /*разобъем процентные периоды в зависимости от категории "Вид взаимозависимости"*/
  /*BegCalendarDate - начало календ периода*/
  macro DividedProcPeriodByIndepend(NewArr:CProcPeriod,BegCalendarDate:Date,ContrID:integer,ContrRes:integer,ContrClientID:integer,ContrClientRes:integer)
     var
         Counter = this.Size, i = 0, j = 0, BegDate = Date(0,0,0), EndDate = Date(0,0,0), CurBegDate = Date(0,0,0), CurEndDate = Date(0,0,0);
     var DataSet, Sql,
         FromDate = Date(0,0,0),
         ToDate = Date(0,0,0), ToDatePrev = Date(0,0,0), ЗаданКлиентКонтрагента = IIF(ContrClientID > 0,true,false),
         SqlQuery = " SELECT attr.T_ValidFromDate FromDate, attr.T_ValidToDate ToDate " +
                         "   FROM dobjatcor_dbt attr " +
                         "  WHERE attr.t_ObjectType = " + string(OBJTYPE_PARTY) +
                         "    AND attr.t_GroupID = " + string(PARTY_ATTR_GROUP_KIND_INTERDEPENDENCE) + 
                         "    AND attr.t_Object = LPAD(?, 10, '0') " +
                         "    AND attr.T_ValidFromDate <= ? " +
                         "    AND attr.T_ValidToDate >= ? " +
                         " ORDER BY attr.T_ValidFromDate, attr.T_ValidToDate ";

     macro FillNewArr()
 
        if( CurBegDate <= CurEndDate )
           Sql = RSDCommand(SqlQuery);
           
           Sql.addParam( "", RSDBP_IN, ContrID);
           Sql.addParam( "", RSDBP_IN, CurEndDate );
           Sql.addParam( "", RSDBP_IN, CurBegDate );
           
           Sql.execute();
           
           DataSet = TRsbDataSet( Sql );
           while(DataSet.MoveNext())
          
              FromDate = SQL_ConvTypeDate(DataSet.FromDate);
              if( FromDate == Date(0,0,0) )/*может быть и такое*/
                 FromDate = CurBegDate;
              end;
             
              ToDate = SQL_ConvTypeDate(DataSet.ToDate);

              if( FromDate == ToDatePrev )
                 NewArr[NewArr.Size-1].CurEndDate = min(ToDate,CurEndDate);
              else
                 NewArr[NewArr.Size] = CProcDate(max(FromDate,CurBegDate),min(ToDate,CurEndDate),ContrRes);
              end;

              ToDatePrev = ToDate;
           end;
        end;

     end;

     this.Sorting;

     BegDate = BegCalendarDate;

     NewArr.ClearArray;

     while( i < Counter )

        CurEndDate = this[i].CurEndDate;
        
        Sql = RSDCommand(SqlQuery);
        
        Sql.addParam( "", RSDBP_IN, IIF(ЗаданКлиентКонтрагента,ContrClientID,ContrID));/*сначала по клиенту контрагента, т.к. он по сравнению с контрагентом по сделке приоритетнее*/
        Sql.addParam( "", RSDBP_IN, this[i].CurEndDate );
        Sql.addParam( "", RSDBP_IN, BegDate );
        
        Sql.execute();
        
        DataSet = TRsbDataSet( Sql );

        while(DataSet.MoveNext())
       
           FromDate = SQL_ConvTypeDate(DataSet.FromDate);
           if( FromDate == Date(0,0,0) )/*может быть и такое*/
              FromDate = BegDate;
           end;

           ToDate = SQL_ConvTypeDate(DataSet.ToDate);

           if( FromDate == ToDatePrev )
              NewArr[NewArr.Size-1].CurEndDate = min(ToDate,CurEndDate);
           else
              NewArr[NewArr.Size] = CProcDate(max(FromDate,BegDate),min(ToDate,CurEndDate),ContrClientRes);
           end;

           ToDatePrev = ToDate;
        end;

        i = i + 1;

     end;

     EndDate = CurEndDate;
     /*по категории клиента контрагента выше разбили процентный период, 
       а теперь заполним "дыры" в процентном периоде(те временные интервалы, в которых не была заполнена категория по клиенту контрагента). 
       Заполним "дыры", если категория по контрагенту заполнена*/
     if(ЗаданКлиентКонтрагента)

        Counter = NewArr.Size;
        CurBegDate = BegDate;
        i = 0;
        while( i < Counter )
           CurEndDate = NewArr[i].CurBegDate - 1;
           FillNewArr();
           CurBegDate = NewArr[i].CurEndDate + 1;
           i = i + 1;
        end;

        CurEndDate = EndDate;
        FillNewArr();
        NewArr.Sorting;

     end;

  end;

  macro FillCurBegDate(BegCalendarDate:Date,ContrRes:integer)
     var
        Counter = this.Size, i = 0, BegDate = Date(0,0,0);

     this.Sorting;

     BegDate = BegCalendarDate;

     while( i < Counter )
         this[i].CurBegDate = BegDate;
         this[i].CalcKind   = ContrRes;
         BegDate  = this[i].CurEndDate + 1;
         i = i + 1;
     end;

  end;

  /* Сортировка данных в массиве */
  macro Sorting
     qsort( this, "CompareForSort" );
  end;

end;

/*строка с выводимыми параметрами*/
private class CStrData()

  var A1,  A2,  A3,  A4,  A5,  A6, A7, A8, A9, A10, A11, A12,
      A13, A14, A15, A16, A17, A18,
      A19, A20, A21, A22, A23, A24,
      A25, A26, A27;

  macro clear()
     A1 = A2 = A3 = A4 = A5 = A6 =A7 =A8 =A9 =A10 =A11 =A12 =
     A13 =A14 =A15 =A16 =A17 =A18 =
     A19 =A20 =A21 =A22 =A23 =A24 =
     A25 =A26 =A27 = "";
  end;

end;

private class CCalendarDate(pBegDate:date, pEndDate:date)

  var BegDate:date, EndDate:date;

  BegDate = pBegDate;
  EndDate = pEndDate;

end;

/*массив календарных периодов для конкретной сделки*/
private class (TArray) CCalendarPeriod()
  /* Очистить массив */
  macro ClearArray
     this.Size = 0;
  end;

  macro FormPeriod(pBegDate:date, pEndDate:date)
     var
        Counter = 0, BegMonth, EndMonth, Year, CurEndDate, CurBegDate;

     this.Size = 0;

     DateSplit( pBegDate, null, BegMonth, Year );
     DateSplit( pEndDate, null, EndMonth, null );
     /*период = одному месяцу*/
     if( BegMonth == EndMonth )
        this[Counter] = CCalendarDate(pBegDate,pEndDate);
     else/*период составляет несколько календарных месяцев*/
        CurBegDate = pBegDate;
        while( BegMonth <= EndMonth )
           if( EndMonth == BegMonth )
              CurEndDate = pEndDate;
           else
              CurEndDate = Date(1,BegMonth + 1,Year) - 1;/*последний день очередного календарного периода*/
           end;
           this[Counter] = CCalendarDate(CurBegDate,CurEndDate);
           BegMonth = BegMonth + 1;
           CurBegDate = CurEndDate + 1;
           Counter = Counter + 1;
        end;
     end;

  end;

end;

PRIVATE MACRO Get_str_PaymDate(Type, DealPart)
   var sql_PlanDate:String = 
      " (NVL((SELECT min(dlrq.t_PlanDate) " +
      "  FROM ddlrq_dbt dlrq " + 
      "  WHERE dlrq.t_DocKind   = tick.t_BOfficeKind " +
      "     AND dlrq.t_DocID    = tick.t_DealID " +
      "     AND dlrq.t_Type     = " + string(Type) + 
      "     AND dlrq.t_DealPart = " + string(DealPart) + 
      "     AND dlrq.t_FactDate = "+ GetSQLDate(date(0,0,0)) +
      "     ), TO_DATE('01-01-0001','DD-MM-YYYY') ) " +
      " ) ";

   var sql_FactDate:String = 
      " (NVL((SELECT min(dlrq.t_FactDate) " +
      "  FROM ddlrq_dbt dlrq " + 
      "  WHERE dlrq.t_DocKind    = tick.t_BOfficeKind " +
      "     AND dlrq.t_DocID     = tick.t_DealID " +
      "     AND dlrq.t_Type      = " + string(Type) + 
      "     AND dlrq.t_DealPart  = " + string(DealPart) + 
      "     AND dlrq.t_State     = " + string(DLRQ_STATE_EXEC) +
      "     AND dlrq.t_FactDate  > "+ GetSQLDate(date(0,0,0)) +
      "     ), TO_DATE('01-01-0001','DD-MM-YYYY') ) " +
      " ) ";

   return 
      " (DECODE( " +
           sql_FactDate + ", " +                   // Выражение
      "    TO_DATE('01-01-0001','DD-MM-YYYY'), " + // search1
           sql_PlanDate + ", " +                   // result1
           sql_FactDate + ") " +                   // default
      " ) ";
END;

PRIVATE MACRO Get_strFactPaymDate(Type, DealPart)
  return
      " (NVL ( (SELECT min(dlrq.t_FactDate) " +
      "         FROM ddlrq_dbt dlrq " + 
      "        WHERE dlrq.t_DocKind  = tick.t_BOfficeKind " +
      "          AND dlrq.t_DocID    = tick.t_DealID " +
      "          AND dlrq.t_Type     = " + string(Type) + 
      "          AND dlrq.t_DealPart = " + string(DealPart) + 
      "          AND dlrq.t_State    = " + string(DLRQ_STATE_EXEC) +
      "          AND dlrq.t_FactDate > "+ GetSQLDate(date(0,0,0)) + "),"+
      "      TO_DATE('01-01-0001','DD-MM-YYYY') ) )";
END;

PRIVATE MACRO CheckIndependence( PartyID:integer, pBegDate:date, pEndDate:date, MinDate:@Date, MaxDate:@Date )
   var DataSet;

   var Sql = RSDCommand(" SELECT nvl(min(attr.T_ValidFromDate),to_date('01.01.0001','DD.MM.YYYY')) MinDate, nvl(max(attr.T_ValidToDate),to_date('01.01.0001','DD.MM.YYYY')) MaxDate " +
                        "   FROM dobjatcor_dbt attr " +
                        "  WHERE attr.t_ObjectType = " + string(OBJTYPE_PARTY) +
                        "    AND attr.t_GroupID = " + string(PARTY_ATTR_GROUP_KIND_INTERDEPENDENCE) + 
                        "    AND attr.t_Object = LPAD(?, 10, '0') " +
                        "    AND attr.T_ValidFromDate <= ? " +
                        "    AND attr.T_ValidToDate >= ? "
                       );

   Sql.addParam( "", RSDBP_IN, PartyID );
   Sql.addParam( "", RSDBP_IN, pEndDate );
   Sql.addParam( "", RSDBP_IN, pBegDate );

   Sql.execute();

   DataSet = TRsbDataSet( Sql );

   if(DataSet.MoveNext() and (SQL_ConvTypeDate(DataSet.MaxDate) != Date(0,0,0)))
      MinDate = SQL_ConvTypeDate(DataSet.MinDate);
      MaxDate = SQL_ConvTypeDate(DataSet.MaxDate);
      return true;
   end;

   return false;
END;

/*подходит ли сделка под условия отбора*/
MACRO SecurDealIsFit(FD,pBegDate:date,pEndDate:date,ContrID:integer,CategFromDate:@Date,CategToDate:@Date,ContrClientID:@integer)
  record contrclient("party.dbt");
  var v_NumInList = "";
  var CategFromDate_2 = Date(0,0,0), CategToDate_2 = Date(0,0,0);
  var ret = НЕ_ОПРЕДЕЛЕНО;

  CategFromDate = CategToDate = Date(0,0,0);
  ContrClientID = -1;/*клиент контрагента(связаннй субъект)*/

  DL_GetObjAttrName(OBJTYPE_SECDEAL, OBJGROUP_TICKCONTROL, DL_GetMainObjAttr(FD.tick, OBJGROUP_TICKCONTROL, OBJTYPE_SECDEAL ), null, null, @v_NumInList);
  if( (v_NumInList == null) or (v_NumInList == "") )
     if( GetLinkedObject( 1, OBJTYPE_SECDEAL, UniID( FD.tick, OBJTYPE_SECDEAL ), OBJTYPE_PARTY, contrclient) == 0 )
        if( CheckIndependence(contrclient.PartyID,pBegDate,pEndDate,@CategFromDate,@CategToDate) )
           ContrClientID = contrclient.PartyID;
           ret = ЗАДАНА_ПО_КОНТРАГЕНТУ;
        end;
     end;
     /*после проверки по клиенту контрагента проверим по контрагенту*/
     if( CheckIndependence(ContrID,pBegDate,pEndDate,@CategFromDate_2,@CategToDate_2) )
        if( CategFromDate_2 < CategFromDate )
           CategFromDate = CategFromDate_2;
        end;
        if( CategToDate_2 > CategToDate )
           CategToDate = CategToDate_2;
        end;
        ret = ЗАДАНА_ПО_КОНТРАГЕНТУ;
     end;
  else
     ret = ЗАДАНА_ПО_СДЕЛКЕ;
  end;

  return ret;
END;

PRIVATE MACRO CheckChangePlusMinusRate(FD2, pBegDate:date, pEndDate:date)

   var Query, DataSet, PlusRate = false, MinusRate = false;

   /*соберем изменения ставки в историии по сделке*/
   Query = RSDCommand( " select count(1) NRec "+                                                                                                          
                       "   from dsptkchng_dbt sptk "+                                                                                                           
                       "  where sptk.t_DealID = ? "+
                       "    and sptk.T_OLDINCOMERATE < 0 " +
                       "    and case when sptk.T_OLDCHANGEDATE = to_date('01.01.0001', 'DD.MM.YYYY') then ? else sptk.T_OLDCHANGEDATE end >= ? " +
                       "    and case when sptk.T_OLDCHANGEDATE = to_date('01.01.0001', 'DD.MM.YYYY') then ? else sptk.T_OLDCHANGEDATE end <= ? "
                     );

   Query.addParam( "", RSDBP_IN, FD2.tick.rec.DealID );
   Query.addParam( "", RSDBP_IN, FD2.tick.rec.DealDate );
   Query.addParam( "", RSDBP_IN, pBegDate );
   Query.addParam( "", RSDBP_IN, FD2.tick.rec.DealDate );
   Query.addParam( "", RSDBP_IN, pEndDate );
   Query.execute();

   DataSet = TRsbDataSet( Query );

   if(DataSet.MoveNext() and (int(DataSet.NRec) > 0))
      MinusRate = true;
   end;

   Query = RSDCommand( " select count(1) NRec "+                                                                                                          
                       "   from dsptkchng_dbt sptk "+                                                                                                           
                       "  where sptk.t_DealID = ? "+
                       "    and sptk.T_OLDINCOMERATE > 0 " +
                       "    and case when sptk.T_OLDCHANGEDATE = to_date('01.01.0001', 'DD.MM.YYYY') then ? else sptk.T_OLDCHANGEDATE end >= ? " +
                       "    and case when sptk.T_OLDCHANGEDATE = to_date('01.01.0001', 'DD.MM.YYYY') then ? else sptk.T_OLDCHANGEDATE end <= ? "
                     );

   Query.addParam( "", RSDBP_IN, FD2.tick.rec.DealID );
   Query.addParam( "", RSDBP_IN, FD2.tick.rec.DealDate );
   Query.addParam( "", RSDBP_IN, pBegDate );
   Query.addParam( "", RSDBP_IN, FD2.tick.rec.DealDate );
   Query.addParam( "", RSDBP_IN, pEndDate );
   Query.execute();

   DataSet = TRsbDataSet( Query );

   if(DataSet.MoveNext() and (int(DataSet.NRec) > 0))
      PlusRate = true;
   end;

   if( ((MinusRate == true) and (PlusRate == true)) OR
       ( ((FD2.tick.rec.ChangeDate > pBegDate) and (FD2.tick.rec.ChangeDate <= pEndDate)) and
        ( ((MinusRate == true) and (FD2.dl_leg.rec.IncomeRate > 0.0)) or
          ((PlusRate == true) and (FD2.dl_leg.rec.IncomeRate < 0.0))
        )
       )
     )
      return true;
   end;

   return false;
END;

PRIVATE var CalendarPeriodArr = CCalendarPeriod();
PRIVATE var ProcPeriodArr_Add;
PRIVATE var ProcPeriodArr;

PRIVATE MACRO FillProcPeriodArr(FD2, pBegDate:date, pEndDate:date, pCategoryInstalled:integer, ContrID:integer, ContrRes:integer, ContrClientID:integer, ContrClientRes:integer)
   var Query, DataSet;

   /*признак того, что ставка меняеется в данном календарном периоде*/
   IsChangeRate = false;

   ProcPeriodArr_Add = CProcPeriod();
   ProcPeriodArr = CProcPeriod();    

   /*соберем изменения ставки в историии по сделке*/
   Query = RSDCommand( "select (SPTK_NEXT.T_OLDCHANGEDATE - 1) OLDCHANGEDATE "+                                                                                                          
                       "  from dsptkchng_dbt sptk_prev,  dsptkchng_dbt sptk_next "+                                                                                                           
                       " where sptk_prev.t_DealID = ? "+
                       "   and sptk_prev.t_DealID = sptk_next.t_DealID "+
                       "   and SPTK_NEXT.T_OLDINSTANCE = SPTK_PREV.T_OLDINSTANCE + 1 "+
                       "   and sptk_prev.T_OLDINCOMERATE != SPTK_NEXT.T_OLDINCOMERATE "+
                       "   and (SPTK_NEXT.T_OLDCHANGEDATE - 1) >= ? " +
                       "   and (SPTK_NEXT.T_OLDCHANGEDATE - 1) <= ? " +
                       " order by OLDCHANGEDATE, SPTK_NEXT.T_OLDINSTANCE");

   Query.addParam( "", RSDBP_IN, FD2.tick.rec.DealID );
   Query.addParam( "", RSDBP_IN, pBegDate );
   Query.addParam( "", RSDBP_IN, pEndDate );
   Query.execute();

   DataSet = TRsbDataSet( Query );

   while(DataSet.MoveNext())
      ProcPeriodArr_Add.AddProcPeriod(SQL_ConvTypeDate(DataSet.OLDCHANGEDATE),true);
   end;

   /*проверим последнее изменение по сделке*/
   if( (FD2.tick.rec.ChangeDate > pBegDate) and (FD2.tick.rec.ChangeDate <= pEndDate) )
      Query = RSDCommand( " select SPTK.T_OLDINCOMERATE "+                                                                                                         
                          "   from dsptkchng_dbt sptk "+                                                                                                          
                          "  where sptk.t_DealID = ? "+
                          "    and SPTK.T_OLDINSTANCE = (select nvl(max(SPTK1.T_OLDINSTANCE),-1) "+                                                                                                         
                          "                                from dsptkchng_dbt sptk1 "+                                                                                                          
                          "                               where sptk1.t_DealID = sptk.t_DealID) "
                        );
     
      Query.addParam( "", RSDBP_IN, FD2.tick.rec.DealID );
      Query.execute();
                                                                                     
      DataSet = TRsbDataSet( Query );
     
      if(DataSet.MoveNext() and (SQL_ConvTypeSum(DataSet.OLDINCOMERATE) != FD2.dl_leg.rec.IncomeRate))
         ProcPeriodArr_Add.AddProcPeriod(FD2.tick.rec.ChangeDate-1,true);
      end;
   end;

   /*собереим даты выплат по процентам, комп. платежам, купону и ЧП*/
   Query = RSDCommand(" select distinct RQ.T_FACTDATE "+
                      "   from ddlrq_dbt rq "+
                      "  where RQ.T_DOCID = ? "+
                      "    and rq.t_DocKind = ? "+
                      "    and rq.t_DealPart = 2 "+
                      "    and (RQ.T_TYPE = ? or RQ.T_TYPE = ? or RQ.T_TYPE = ? ) "+
                      "    and RSI_DLRQ.GetReturnIncomeKindByRQ(RQ.t_ID) = ? "+
                      "    and RQ.T_FACTDATE >= ? " +
                      "    and RQ.T_FACTDATE <= ? "
                     );

   Query.addParam( "", RSDBP_IN, FD2.tick.rec.DealID );
   Query.addParam( "", RSDBP_IN, DL_SECURITYDOC );

   Query.addParam( "", RSDBP_IN, DLRQ_TYPE_PAYMREPOCOUP );
   Query.addParam( "", RSDBP_IN, DLRQ_TYPE_PAYMREPOPART );
   Query.addParam( "", RSDBP_IN, DLRQ_TYPE_COMPPAYM );

   Query.addParam( "", RSDBP_IN, SP_RETURNINCOME_WRTOFF );

   Query.addParam( "", RSDBP_IN, pBegDate );
   Query.addParam( "", RSDBP_IN, pEndDate );

   Query.execute();
                                                                                  
   DataSet = TRsbDataSet( Query );

   while(DataSet.MoveNext())
      ProcPeriodArr_Add.AddProcPeriod(SQL_ConvTypeDate(DataSet.FACTDATE));
   end;

   /*дату окончания календарного периода тоже заносим в массив*/
   ProcPeriodArr_Add.AddProcPeriod(pEndDate);

   if( pCategoryInstalled == ЗАДАНА_ПО_СДЕЛКЕ )
      ProcPeriodArr_Add.FillCurBegDate(pBegDate,ContrRes);
      ProcPeriodArr = ProcPeriodArr_Add;
   else
      ProcPeriodArr_Add.DividedProcPeriodByIndepend(ProcPeriodArr,pBegDate,ContrID,ContrRes,ContrClientID,ContrClientRes);
   end;
END;

PRIVATE MACRO GetPayOutByDeal(FD, pBegDate:date, pEndDate:date, Error:@string )
   var Query, DataSet;
   var PaySum:money = $0, Amount = $0;                       
   Error = "";
   /*собереим даты выплат по процентам, комп. платежам, купону и ЧП*/
   Query = RSDCommand(" select RQ.T_FACTDATE, RQ.T_Kind, RQ.T_Amount, RQ.T_Type, RQ.T_FIID "+
                      "   from ddlrq_dbt rq "+
                      "  where RQ.T_DOCID = ? "+
                      "    and rq.t_DocKind = ? "+
                      "    and rq.t_DealPart = 2 "+
                      "    and (RQ.T_TYPE = ? or RQ.T_TYPE = ? or RQ.T_TYPE = ?) "+
                      "    and RSI_DLRQ.GetReturnIncomeKindByRQ(RQ.t_ID) = ? "+
                      "    and RQ.T_FACTDATE >= ? " +
                      "    and RQ.T_FACTDATE <= ? "
                     );

   Query.addParam( "", RSDBP_IN, FD.tick.rec.DealID );
   Query.addParam( "", RSDBP_IN, DL_SECURITYDOC );

   Query.addParam( "", RSDBP_IN, DLRQ_TYPE_PAYMREPOCOUP );
   Query.addParam( "", RSDBP_IN, DLRQ_TYPE_PAYMREPOPART );
   Query.addParam( "", RSDBP_IN, DLRQ_TYPE_COMPPAYM );

   Query.addParam( "", RSDBP_IN, SP_RETURNINCOME_WRTOFF );

   Query.addParam( "", RSDBP_IN, pBegDate );
   Query.addParam( "", RSDBP_IN, pEndDate );

   Query.execute();
                                                                                  
   DataSet = TRsbDataSet( Query );

   while(DataSet.MoveNext())

      Amount = SQL_ConvTypeSum(DataSet.Amount);
      if( (SQL_ConvTypeInteger(DataSet.Type) == DLRQ_TYPE_COMPPAYM) and
         ( (IsBUY(FD.Group) and (SQL_ConvTypeInteger(DataSet.Kind) == DLRQ_KIND_COMMIT)) OR (IsSALE(FD.Group) and (SQL_ConvTypeInteger(DataSet.Kind) == DLRQ_KIND_REQUEST)) )
        )
         Amount = -Amount;/*учитываем знак для отражения увеличения\уменьшения для случа РЕПО на корзину (знак "минус" для увеличения суммы 2 ч)*/
      end;

      if( not SmartConvertSum( Amount, Amount, SQL_ConvTypeDate(DataSet.FACTDATE), SQL_ConvTypeInteger(DataSet.FIID), FD.dl_leg.rec.CFI, false ) )
         Error = GetCurrencyConvertErrorMsg(SQL_ConvTypeInteger(DataSet.FIID), FD.dl_leg.rec.CFI, SQL_ConvTypeDate(DataSet.FACTDATE));
         return 0;
      else
         PaySum = PaySum + Amount;
      end;
   end;

   return PaySum;
END;

private macro GetAccountByREPO( FD, CategCode:string, EndDate:Date, FindAccount:@string, Error:@string )

   record v_accdoc( mcaccdoc );
   record v_categ( mccateg );

   Error = "";

   if( not MC_FindMCCATEG(CategCode,v_categ) )                                                 
      Error = "Не найдена категория счета \""+CategCode+"\"";                                      
      return false;          
   end;                                                                                      
   
   /*ищем актуальные на дату счета*/                                                                                  
   MC_FindMaxMCACCDOC(FD.Kind,FD.ID,v_categ.ID,FD.GetPayFIID(),EndDate,v_accdoc);

   FindAccount = v_accdoc.Account;

   return true;                                             
end;

MACRO FillBodyForSecur(Report, Kind:integer)

  VAR DealChange = TRecHandler( "sptkchng" );                                                                                      

  var cmd, cnt, Num = 0, WhereCond = "", FD, FD2, BegCurDate = Date(0,0,0), EndCurDate = Date(0,0,0), CurYear, CurMonth, PeriodEnd = Date(0,0,0);
  var StrData = CStrData(), Error = "", i = 0, j = 0, PaySum = $0, FindAccount = "", str_IncomeRate = "", CategoryInstalled = НЕ_ОПРЕДЕЛЕНО, 
      CategFromMinDate = Date(0,0,0), CategToMaxDate = Date(0,0,0), CategFromDate = Date(0,0,0), CategToDate = Date(0,0,0), 
      ContrID = 0, ContrClientID = 0, ContrClientRes, ContrRes, KindExc = iif(Kind == ДОХОД,СТ_269_РАЗМЕЩЕНИЕ,СТ_269_ПРИВЛЕЧЕНИЕ);

  DateSplit(Report.BeginDate,null, null, CurYear);

  /*изменение ставки смотрим в течение периода отчета*/
  str_IncomeRate = " case when tick.t_ChangeDate = to_date('01.01.0001', 'DD.MM.YYYY') or (tick.t_ChangeDate >= ? and tick.t_ChangeDate <= ?) "+
                   "      then leg.t_IncomeRate "+
                   "      else (select T_OLDINCOMERATE "+                                                                                                          
                   "              from dsptkchng_dbt sptk "+                                                                                                           
                   "             where sptk.t_DealID = tick.t_DealID "+
                   "               and sptk.T_OLDINSTANCE = (select nvl(max(sptk1.T_OLDINSTANCE),-1) "+                                                                                                          
                   "                                           from dsptkchng_dbt sptk1 "+                                                                                                           
                   "                                          where sptk1.t_DealID = sptk.t_DealID "+
                   "                                            and case when sptk1.T_OLDCHANGEDATE = to_date('01.01.0001', 'DD.MM.YYYY') then tick.t_DealDate else sptk1.T_OLDCHANGEDATE end >= ? "+
                   "                                            and case when sptk1.T_OLDCHANGEDATE = to_date('01.01.0001', 'DD.MM.YYYY') then tick.t_DealDate else sptk1.T_OLDCHANGEDATE end <= ?) "+
                   "           ) "+
                   "      end ";

  if( Kind == ДОХОД )/*доход*/
     WhereCond = " and ( (rsb_secur.IsBuy(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1 and "+str_IncomeRate+" >= 0) " +
                 "      or " +
                 "       (rsb_secur.IsSale(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1 and "+str_IncomeRate+" < 0) " +
                 "     )";
  else/*расход*/
     WhereCond = " and ( (rsb_secur.IsBuy(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1 and "+str_IncomeRate+" < 0) " +
                 "      or " +
                 "       (rsb_secur.IsSale(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1 and "+str_IncomeRate+" >= 0) " +
                 "     )";
  end;

  cmd = DL_RSDCommand( " select tick.t_DealID, "+Get_str_PaymDate(DLRQ_TYPE_PAYMENT,2)+" A8, "+Get_strFactPaymDate(DLRQ_TYPE_PAYMENT,1)+" A7 "+
                       "   from ddl_tick_dbt tick, ddl_leg_dbt leg "+
                       "  where rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1 "+
                       "    and leg.t_DealID = tick.t_DealID "+
                       "    and leg.t_LegKind = ? "+
                       "    and LEG.T_LEGID = 0  "+
                       "    and tick.t_ClientID = -1 "+/*только собственные*/
                       "    and " + Get_str_PaymDate(DLRQ_TYPE_PAYMENT,2) + " >= ? " +
                       "    and " + Get_strFactPaymDate(DLRQ_TYPE_PAYMENT,1) + " <= ? "+
                       "    and " + Get_strFactPaymDate(DLRQ_TYPE_PAYMENT,1) + " > TO_DATE('01-01-0001','DD-MM-YYYY') "+ WhereCond
                     );

  cmd.addParam( LEG_KIND_DL_TICK_BACK );
  cmd.addParam( Report.BeginDate );
  cmd.addParam( Report.EndDate );

  cmd.addParam( Report.BeginDate );
  cmd.addParam( Report.EndDate );
  cmd.addParam( Report.BeginDate );
  cmd.addParam( Report.EndDate );

  cmd.addParam( Report.BeginDate );
  cmd.addParam( Report.EndDate );
  cmd.addParam( Report.BeginDate );
  cmd.addParam( Report.EndDate );

  cmd.addParam( Report.BeginDate );
  cmd.addParam( Report.EndDate );
  cmd.addParam( Report.BeginDate );
  cmd.addParam( Report.EndDate );

  cmd.addParam( Report.BeginDate );
  cmd.addParam( Report.EndDate );
  cmd.addParam( Report.BeginDate );
  cmd.addParam( Report.EndDate );
  
  cnt = cmd.GetCount();
  
  if( cnt > 0 )
     var DataSet = cmd.Execute();
  
     InitProgress( cnt, "Вкладка \"Регистр "+iif(Kind == ДОХОД,"доходов","расходов")+" (ст. 269)\"", "Отбор сделок РЕПО бэк-офиса" );
     while(DataSet.moveNext())

         FD = SPFirstDoc(LEG_KIND_DL_TICK, dataSet.DealID);
         StrData.Clear;
         /*Контрагент*/
         StrData.A2 = FD.tick.rec.PartyID;
         if( (StrData.A2 < 0) AND IsEXCHANGE( FD.Group ) )
            StrData.A2 = FD.tick.rec.MarketID;
         end;

         ContrID = StrData.A2;
         /*Дата начала обяза-тельства*/
         StrData.A7 = SQL_ConvTypeDate(DataSet.A7);
         /*Дата окончания обяза-тельства*/
         StrData.A8 = SQL_ConvTypeDate(DataSet.A8);

         /*проверим, установлены ли категории, и получим ID клиента контрагента, но в том случае, если на время действия сделки на нем хотя бы 1 день была установлена категория*/
         CategoryInstalled = SecurDealIsFit(FD,max(StrData.A7,Report.BeginDate),min(StrData.A8,Report.EndDate),ContrID,@CategFromMinDate,@CategToMaxDate,@ContrClientID);
         /*установим резидентность контрагента и клиента контрагента(если есть клиент контрагента)*/
         ContrClientRes = -1;
         if( ContrClientID > 0 )
            ContrClientRes = IIF((GetPartyByID( ContrClientID ).NotResident == SET_CHAR),СТ_269_НЕРЕЗИДЕНТ,СТ_269_РЕЗИДЕНТ);
         end;

         ContrRes = -1;
         if( StrData.A2 > 0 )
            ContrRes = IIF((GetPartyByID( ContrID ).NotResident == SET_CHAR),СТ_269_НЕРЕЗИДЕНТ,СТ_269_РЕЗИДЕНТ);
         end;

         if( CategoryInstalled != НЕ_ОПРЕДЕЛЕНО )

           FD2 = SPFirstDoc( FD.tick, true );
           if( not CheckChangePlusMinusRate(FD2, Report.BeginDate, Report.EndDate) )
              /*Под календарным периодом будем понимать календарный месяц, входящий в период отчета и в период действия сделки. 
                При этом начало первого календарного периода определяется как  мах (гр.7; Н0.1), 
                а окончание последнего календарного периода -  мин (гр.8;Н0.2).*/
              /*заполним массив календарных периодов*/
              if( CategoryInstalled == ЗАДАНА_ПО_СДЕЛКЕ )
                 PeriodEnd = min(StrData.A8,Report.EndDate);
                 CalendarPeriodArr.FormPeriod(max(StrData.A7,Report.BeginDate),PeriodEnd);
              else
                 PeriodEnd = min(min(StrData.A8,Report.EndDate),CategToMaxDate);
                 CalendarPeriodArr.FormPeriod(max(max(StrData.A7,Report.BeginDate),CategFromMinDate),PeriodEnd);
              end;
              /*заполним не зависящие от процентного периода параметры*/
              /*Вид долгового обязательства*/
              if( IsBUY(FD.Group) )
                 StrData.A1 = "обратное РЕПО";
              else
                 StrData.A1 = "прямое РЕПО";
              end;
              StrData.A2 = DL_getPartyShortName(StrData.A2);
              /*Номер договора (Для БО ЦБ это внутренний номер сделки)*/
              StrData.A3 = FD.tick.rec.DealCode;
              /*Номер лицевого счёта, открытого по сделке по КУ <-ОД>*/
              StrData.A4 = "";
              if( not GetAccountByREPO(FD,iif(IsBUY(FD.Group),"+ОД","-ОД"),PeriodEnd,@StrData.A4,@Error) )
                 Report.SetErrorForSecur(Error);
              end;
              /*Валюта цены из панели сделки РЕПО*/
              StrData.A5 = DL_getCCode(FD.dl_leg.rec.CFI);
              /*Общий срок размеще-ния (в днях, как Столбец <8> - Столбец <7>)*/
              if( StrData.A7 == StrData.A8 )
                 StrData.A6 = 1;
              else
                 StrData.A6 = int(StrData.A8 - StrData.A7);
              end;

              /*Номер лицевого счета, открытого по сделке по КУ <+% к погашению> и/или <Задолженность % (внебаланс)>. 
                Если таких счетов по сделке открыто более одного, нужно все их вывести в строку через запятую. */
              StrData.A13 = "";
              if( Kind == ДОХОД )
                 if( not GetAccountByREPO(FD2,"+% к погашению",PeriodEnd,@StrData.A13,@Error) )
                    Report.SetErrorForSecur(Error);
                 end;
                 FindAccount = "";
                 if( not GetAccountByREPO(FD2,"Задолж.% (внебаланс)",PeriodEnd,@FindAccount,@Error) )
                    Report.SetErrorForSecur(Error);
                 end;
                 if( (StrData.A13 != "") and (FindAccount != "") )
                    StrData.A13 = StrData.A13 + ", " + FindAccount;
                 elif( FindAccount != "" )
                    StrData.A13 = FindAccount;
                 end;
              else
                 if( not GetAccountByREPO(FD2,"-% к погашению",PeriodEnd,@StrData.A13,@Error) )
                    Report.SetErrorForSecur(Error);
                 end;
              end;
              /*Базис  расчета % из панели сделки  - дней в году (либо календарное количество - 365 или366 в зависимости от текущего отчетного года, либо 360).*/
              /*
                Получим базис расчета
                Basis = 0 : SP_KINDBASISCALC_365
                Basis = 1 : SP_KINDBASISCALC_360
                Basis = 2 : по календарю
              */
              if (FD.dl_leg.rec.Basis == 0)
                 StrData.A26 = 365;
              elif (FD.dl_leg.rec.Basis == 1)
                 StrData.A26 = 360;
              else
                 StrData.A26 = GetDaysInYear(CurYear);
              end;
              StrData.A9 = FD.dl_leg.rec.TotalCost;
              PaySum = $0;
              i = 0;
              while( i < CalendarPeriodArr.Size  )
                  /*заполним даты процентного периода внутри календарного*/
                  FillProcPeriodArr(FD2, CalendarPeriodArr[i].BegDate, CalendarPeriodArr[i].EndDate, CategoryInstalled, ContrID, ContrRes, ContrClientID, ContrClientRes);
                  j = 0;
                  while( j < ProcPeriodArr.Size )

                     BegCurDate = ProcPeriodArr[j].CurBegDate;
                     EndCurDate = ProcPeriodArr[j].CurEndDate;

                     DateSplit(BegCurDate,null, CurMonth, null);
                     /*получим условия по сделке на дату окончания проц периода*/
                     SP_GetDealParmsByDate( FD.tick, EndCurDate, DealChange, false, false );                                                     
                     /*Сумма (Стоимость по первой части сделки РЕПО за вычетом сумм всех компенсационных выплат или 
                       возврата дохода (купона, ЧП) по данной сделке, выполненных ранее начала текущего процентного периода )*/
                     StrData.A9 = StrData.A9 - PaySum;
                     /*Период пользования в отчетном периоде*/
                     StrData.A10 = DL_GetNameAlg( 1005/*LIST_MONTH_IM*/, CurMonth );
                     
                     /*Кол-во календарных дней*/
                     if( StrData.A7 == StrData.A8 )
                        StrData.A11 = 1;
                     elif( StrData.A7 == BegCurDate )
                        StrData.A11 = int(EndCurDate - BegCurDate);
                     else
                        StrData.A11 = int(EndCurDate - BegCurDate) + 1;
                     end;

                     /*Ставка РЕПО в данном процентном периоде*/
                     StrData.A12 = abs(DealChange.rec.OldIncomeRate);
                     
                     if( StrData.A5 == {CCYNatCur} )
                        StrData.A16 = 1;
                     else
                        StrData.A16 = GetRateOnDateCrossDbl( CalendarPeriodArr[i].EndDate, FD.dl_leg.rec.CFI, NATCUR, false );
                        if( StrData.A16 == 0.0 )
                           Report.SetErrorForSecur(GetCurrencyConvertErrorMsg(FD.dl_leg.rec.CFI, NATCUR, CalendarPeriodArr[i].EndDate));
                        end;
                     end;
                     
                     /*Столбец <9>* Столбец <12>/ Столбец <26>* Столбец <11>*курс валюты сделки (столбец  <5> ) на дату окончания календарного  периода (т.е. или на конец календарного месяца или на дату окончания обязательств).
                       Если валюта сделки в Столбце <5> = RUR, значение курса валюты сделки  = 1.
                     */
                     StrData.A14 = (StrData.A9 * StrData.A12 / StrData.A26 * StrData.A11 * StrData.A16) / 100;
                     
                     /*Заполняется только если валюта сделки в Столбце <5> ? RUR.
                       Столбец <9>* Столбец <12>/ Столбец <26>* Столбец <11>.*/
                     StrData.A15 = "";
                     if( StrData.A5 != {CCYNatCur} )
                        /*БО ЦБ: Если срок сделки полностью входит в календарный период (см. порядок формирования отчёта) и 
                               календарный период не делится на процентные периоды (нет изменений ставки, нет выплат в РЕПО и возврата дохода контрагенту), 
                               то сумма начисленных процентов =  разнице стоимости ценных бумаг по первой и второй части.*/
                        if( (StrData.A7 >= CalendarPeriodArr[i].BegDate) and ( StrData.A8 <= CalendarPeriodArr[i].EndDate) and (CalendarPeriodArr[i].BegDate == BegCurDate) and (CalendarPeriodArr[i].EndDate == EndCurDate) )
                           StrData.A15 = abs(FD.dl_leg.rec.TotalCost - FD2.dl_leg.rec.TotalCost);
                        else
                           StrData.A15 = (StrData.A9 * StrData.A12 / StrData.A26 * StrData.A11) / 100;
                        end;
                     end;

                     if(IsChangeRate)
                        StrData.A27 = "Да";
                     else
                        StrData.A27 = "Нет";
                     end;
                     
                     if( StrData.A27 == "Да" )
                        StrData.A17 = 0.0;
                        StrData.A19 = 0.0;
                        /*если заполнено примечание*/
                        if( IsFilledNoteByDeal(FD.tick.rec.DealID,NOTEKIND_SECDEAL_MAXRATE/*Предельно допустимая ставка*/) )
                           StrData.A19 = readNoteForObject( OBJTYPE_SECDEAL, UniID( FD.tick, OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_MAXRATE/*Предельно допустимая ставка*/);
                        elif( not DL_GetTxRate_Limit(CalendarPeriodArr[i].EndDate, KindExc, FD.dl_leg.rec.CFI, StrData.A6, @StrData.A19, @Error, StrData.A7, ProcPeriodArr[j].CalcKind) )
                           Report.SetErrorForSecur(Error);
                        end;
                        StrData.A18 = 0.0;
                        if( not DL_GetTxRate(CalendarPeriodArr[i].EndDate, FD.dl_leg.rec.CFI, StrData.A6, @StrData.A18, @Error, KindExc, StrData.A7, ProcPeriodArr[j].CalcKind) )
                           Report.SetErrorForSecur(Error);
                        end;
                     else
                        StrData.A18 = 0.0;
                        StrData.A17 = 0.0;
                        if( not DL_GetTxRate(StrData.A7, FD.dl_leg.rec.CFI, StrData.A6, @StrData.A17, @Error, KindExc, StrData.A7, ProcPeriodArr[j].CalcKind) )
                           Report.SetErrorForSecur(Error);
                        end;
                        StrData.A19 = 0.0;
                        /*если заполнено примечание*/
                        if( IsFilledNoteByDeal(FD.tick.rec.DealID,NOTEKIND_SECDEAL_MAXRATE/*Предельно допустимая ставка*/) )
                           StrData.A19 = readNoteForObject( OBJTYPE_SECDEAL, UniID( FD.tick, OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_MAXRATE/*Предельно допустимая ставка*/);
                        elif( not DL_GetTxRate_Limit(StrData.A7, KindExc, FD.dl_leg.rec.CFI, StrData.A6, @StrData.A19, @Error, StrData.A7, ProcPeriodArr[j].CalcKind) )
                           Report.SetErrorForSecur(Error);
                        end;
                     end;
                     
                     if( Kind == ДОХОД )
                        /*Max {Столбец<19>; Столбец<12>}*/
                        StrData.A20 = max(StrData.A19,StrData.A12);
                     else
                        /*Max {Столбец<19>; Столбец<12>}*/
                        StrData.A20 = min(StrData.A19,StrData.A12);
                     end;
                     /*Заполняется только если валюта сделки в Столбце <5> ? RUR.
                       Столбец <9>* Столбец <19>/ Столбец <26>* Столбец <11>.*/
                     StrData.A21 = "";
                     if( StrData.A5 != {CCYNatCur} )
                        StrData.A21 = StrData.A9 * StrData.A19 / StrData.A26 * StrData.A11;
                     end;
                     /*Столбец <9>* Столбец <19>/ Столбец <26>* Столбец <11>* Столбец <16>.*/
                     StrData.A22 = StrData.A9 * StrData.A19 / StrData.A26 * StrData.A11 * StrData.A16;
                     /*Столбец <9>* Столбец <20>/ Столбец <26>* Столбец <11>* Столбец <16>.*/
                     StrData.A23 = (StrData.A9 * StrData.A20 / StrData.A26 * StrData.A11 * StrData.A16) / 100;
                     /*Заполняется только если валюта сделки в Столбце <5> ? RUR.
                       Столбец <9>* Столбец <20>/ Столбец <26>* Столбец <11>.
                     */
                     StrData.A24 = "";
                     if( StrData.A5 != {CCYNatCur} )
                        StrData.A24 = (StrData.A9 * StrData.A20 / StrData.A26 * StrData.A11) / 100;
                     end;
                     /*MAX{0; (Столбец <23> - Столбец <14> )}*/
                     StrData.A25 = max(0,StrData.A23-StrData.A14);
                     
                     if(Kind == ДОХОД)
                        Report.PrintProfitForSecur(StrData);
                     else
                        Report.PrintCostForSecur(StrData);
                     end;
                     
                     PaySum = GetPayOutByDeal(FD,BegCurDate,EndCurDate,@Error);
                     if( Error != "" )
                        Report.SetErrorForSecur(Error);
                     end;

                     j = j + 1;
                  end;
                 
                  i = i + 1;
               end;
            end;
         end;

         Num = Num + 1;
         UseProgress( Num );
     end;

     RemProgress;

  end;

  return;
END;
