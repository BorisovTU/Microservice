/*
$Name:             NpTxClOpenPos_Form.mac
$Module:           Ценные бумаги
$Description:      <Отчеты для НДФЛ>/ <Открытые позиции по клиенту> - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "dlquery.mac", SFInter;

CONST
      PNFLD_NPTXCLOPENPOS_REPDATE             = 0,    
      PNFLD_NPTXCLOPENPOS_CLIENT_CODE         = 1, 
      PNFLD_NPTXCLOPENPOS_CLIENT_NAME         = 2,   
      PNFLD_NPTXCLOPENPOS_CLIENT_CONTR        = 3;    

private macro GetOneClientContrStock( ClientID, ADate )
   var ContrID = 0;
   var  Query =  " select t_ID "
                +  "  from  dsfcontr_dbt"
                +  "  where t_ServKind = ? "
                +  "    and t_ContractorID = ? "
                +  "    and t_PartyID = ? "
                +  "    and t_DateBegin <= ? "
                +  "    and ( t_DateClose = to_date('01.01.0001','DD.MM.YYYY') "
                +  "          or t_DateClose >= ? "
                +  "        )";

   var cmd = DL_RSDCommand();

   cmd.AddParam(PTSK_STOCKDL);
   cmd.AddParam({OurBank});
   cmd.AddParam(ClientID);
   cmd.AddParam(ADate);
   cmd.AddParam(ADate);

   if( cmd.GetCount(Query) == 1 )
      var dataSet = cmd.Execute(Query);
      if( dataSet.MoveNext() )
         ContrID = dataSet.ID;
      end;
   end;

   return ContrID;
end;

// Код физлица - клиента фондового дилинга
private MACRO DL_FldProc_Client_STOCKDL_Code( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "", ContrID = -1;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = 0;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = "";
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, "");
        pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1);
     else
        partcode.Clear();
        partcode.rec.PartyID  = FldRealValue;
        partcode.rec.CodeKind = PTCK_CONTR;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;

        if( not ПолучитьСубъекта(FldRealValue, Party) )
           pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName);
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = "";
     pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, "" );
     pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1);

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )

     whereCond = " t.t_LegalForm = 2 AND " +
                 " t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                                 "    FROM dclient_dbt c2 " +
                                 "   WHERE t.T_PARTYID = c2.T_PARTYID " + 
                                 "     AND c2.t_servicekind = " + string(PTSK_STOCKDL) + 
                                 ") ";

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = PTCK_CONTR;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName);
        pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, -1 );
        /*Если ДО - один, то подставляется номер этого ДО.*/
        ContrID = GetOneClientContrStock( Party.rec.PartyID, pThis.GetLinkedValue( DL_PNFLD_BEGINDATE ) );
        if( ContrID > 0 )
           pThis.SetLinkedValue( DL_PNFLD_CONTRACT_OFBU, ContrID );
        end;
     end;
  end;

  return CM_DEFAULT;
END;

/*листалка договоров*/
private MACRO DL_ListSfContrByServKind( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, PartyID:@INTEGER ):BOOL
  VAR Choice; 
  VAR Select;

  Select = "select sfc.t_id, sfc.t_DateConc, sfc.t_Name, sfc.t_Number, prt1.t_ShortName Payer, prt2.t_ShortName Contractor, sfc.T_PARTYID " + 
               "  from dsfcontr_dbt sfc, dparty_dbt prt1, dparty_dbt prt2 "+
               " where sfc.t_ServKind = " + PTSK_STOCKDL +
               "   and prt1.t_PartyID = sfc.t_partyID " +
               "   and prt2.t_PartyID = sfc.t_ContractorID "+
               "   and sfc.T_ContractorID = " + {OurBank} +
               "   and prt1.t_LegalForm = 2 ";

  if( PartyID > 0 )
      Select = Select + " and sfc.T_PARTYID = " + PartyID;
  end;

  Choice = DL_Scroll(Select,
                     "Договора обслуживания", X, Y,
                     "t_Number","№ договора","t_Name","Наименование договора","t_DateConc","Дата закл.",
                     "Payer","Плательщик","Contractor","Контрагент"
                    );

  if( Choice != NULL )
     FldRealValue = Choice.Value("t_id"); 
     FldShowValue = Choice.Value("t_Number");
     PartyID      = Choice.Value("T_PARTYID");
  end;

  return (Choice != NULL);
END;

private MACRO DL_FldProc_Contract_BO_Dep( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var  SfContr = TRecHandler("sfcontr.dbt");
  var ClientID = pThis.GetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE );

  if( Cmd == DLG_INIT )
      if( FldRealValue == null )
         FldRealValue = -1;
      end;
      if( FldRealValue <= 0 )      
         FldShowValue = "";
      else
         if( SfGetContr(FldRealValue, SfContr) )
            FldShowValue = SfContr.rec.Number;
         end;
      end;
   elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
      FldRealValue = -1;
      FldShowValue = "";
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE, -1);
   elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
      DL_ListSfContrByServKind( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @ClientID );
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE, ClientID);
   end;
   return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;

    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SC_NpTxClopenPos_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_NPTXCLOPENPOS_REPDATE,             DL_PNFLD_BEGINDATE,           @DL_FldProc_BeginDate,          {curdate});
     AddFieldProc( 0, PNFLD_NPTXCLOPENPOS_CLIENT_CODE,         DL_PNFLD_CLIENT_STOCKDL_CODE, @DL_FldProc_Client_STOCKDL_Code);
     AddFieldProc( 0, PNFLD_NPTXCLOPENPOS_CLIENT_NAME,         DL_PNFLD_CLIENT_STOCKDL_NAME, @Default);
     AddFieldProc( 0, PNFLD_NPTXCLOPENPOS_CLIENT_CONTR,        DL_PNFLD_CONTRACT_OFBU,       @DL_FldProc_Contract_BO_Dep);
  END;

  PRIVATE MACRO Check():BOOL
     var RetVal = true;

     if( GetFieldValue(PNFLD_NPTXCLOPENPOS_CLIENT_CODE) <= 0 )
        msgbox("Необходимо заполнить поле \"Инвестор\"");
        RetVal = false;
     end;

     if( RetVal and (GetFieldValue(PNFLD_NPTXCLOPENPOS_CLIENT_CONTR) <= 0) )
        msgbox("Необходимо заполнить поле \"Договор обслуживания\"");
        RetVal = false;
     end;

     return RetVal;
  END;
                                
  initDL_CPanel( "sp_rep.lbr", "CLPOSREP" );
 
END;
