/*
$Name:        scoverdr.mac
$Module:      Ценные бумаги
$Description: Шаг цепочки - переоценка внебаланса
*/
import "scservop.mac", "sp_categ.mac", InsCarryDoc, "sp_car.mac", SPInter;

private record deal(dl_tick);       
private record corr(account);
private record forw(account);
private record forw_t(account); /*счет переоценки по обязательствам*/
private record forw_o(account); /*счет переоценки по  требованиям*/
private record forw_r(account); /*счет переоценки с которого берем старый остаток при валютной переоценки*/
private record deb(account);
private record kred(account);
private var CatForvard = "";

private macro SayError( num, errmsg )
   ScOpInsertError( DL_SECURITYDOC, ScOprServDoc, deal, num, errmsg );
   InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
   ClearErrorArr();
   return 1;
end;

private macro ПлатежБылОтложен( paym )
  return (ТОБылоПролонгировано(paym) or (paym.rec.State == DLRQ_STATE_DELAYED));
end;

private macro ПроверитьОтложенностьТребованияОбязательства( FD, IsReq )

   if( IsReq )
      if( FD.BuySale == DEAL_TYPE_BUY )/*покупка*/
         if( ПлатежБылОтложен(FD.GetRQ(DLRQ_TYPE_DELIVERY) ) )
            return true;
         end;
      elif( FD.BuySale == DEAL_TYPE_SALE ) /*продажа*/
         if( ПлатежБылОтложен(FD.GetRQ(DLRQ_TYPE_PAYMENT) ) )
            return true;
         end;         
      end;      
   else
      if( FD.BuySale == DEAL_TYPE_BUY )/*покупка*/
         if( ПлатежБылОтложен(FD.GetRQ(DLRQ_TYPE_PAYMENT) ) )
            return true;
         end;         
      elif( FD.BuySale == DEAL_TYPE_SALE ) /*продажа*/
         if( ПлатежБылОтложен(FD.GetRQ(DLRQ_TYPE_DELIVERY) ) )
            return true;
         end;
      end;      
   end;
   return false;  
end;

/*процедура для определения и актуализации переоцениваемого счета*/
private macro ОпределитьПереоцениваемыйСчет( FD, dat, Kind ) 


   var FIRole;
   CatForvard = "";
   /*переоценка требований*/
   if( Kind == "Т" )
      FIRole = FIROLE_FIREQ;
      if(FD.tick.rec.BOfficeKind == DL_SECUROWN)
         
        if( FD.ВидСрочностиСделки() == СделкаПрочая )
           CatForvard = "+Форвард, прочие"; 
        elif( FD.ВидСрочностиСделки() == СделкаНеРанееТретьегоДня )
           CatForvard = "+Форвард, дрейф"; 
        end;

      else
        if( false /*BPV больше не переносим на "+Форвард, ОИ" ПроверитьОтложенностьТребованияОбязательства( FD, true ) */)
           CatForvard = "+Форвард, ОИ";
        else 
           /*признак "Исполнение ПФИ" не установлен*/ 
           if( FD.DealAsResultDVExe == false )
              CatForvard = "+Форвард, прочие";
           elif( (FD.DealAsResultDVExe == true) OR (FD.IsBack == true)  )
              CatForvard = "+Форвард, дрейф";
           end;  
        end;
      end;

   /*переоценка обязательств*/
   elif( Kind == "О" )
      FIRole = FIROLE_FICOM;
      
      if(FD.tick.rec.BOfficeKind == DL_SECUROWN)
         
        if( FD.ВидСрочностиСделки() == СделкаПрочая )
           CatForvard = "-Форвард, прочие"; 
        elif( FD.ВидСрочностиСделки() == СделкаНеРанееТретьегоДня )
           CatForvard = "-Форвард, дрейф"; 
        end;

      else
        if( false /*BPV больше не переносим на "-Форвард, ОИ" ПроверитьОтложенностьТребованияОбязательства( FD, false )*/ )
           CatForvard = "-Форвард, ОИ";
        else 
           /*признак "Исполнение ПФИ" не установлен*/ 
           if( FD.DealAsResultDVExe == false )
              CatForvard = "-Форвард, прочие";
           elif( (FD.DealAsResultDVExe == true) OR (FD.IsBack == true)  )
              CatForvard = "-Форвард, дрейф";
           end;  
        end;
     end;
   end;

   if( CatForvard != "" )
      if( not FD.OpenAccount( CatForvard, null, true, FIRole, forw, dat ) )
         return SayError( 1, "Ошибка при актуализации счета по категории \"" + CatForvard +"\"" );
      end;
      if( Kind == "Т" )
         copy( forw_t, forw ); 
      end;
      if( Kind == "О" )
         copy( forw_o, forw ); 
      end;
   else
      return SayError( 1, "Не определен переоцениваемый счет" );
   end;

   return 0;              
end;

/*общая для всех видов переоценки процедура проводки корректировки остатков переоцениваемого счета*/
private macro ВыполнитьПроводкуКорректировки( FD, Doc, dat, D, Kind, OvervalPapers )

   var Ground = "", Cat = "";
   var DebFIID = NATCUR, CredFIID = NATCUR, SumDeb = $0, SumCred = $0, FiRole = 0;

   if( Kind == "Т" )        
      Copy( forw, forw_t ); 
   else
      Copy( forw, forw_o ); 
   end;
   
   if( ( (Kind == "Т") AND ( D > 0 )) OR
       ( (Kind == "О") AND ( D < 0 )) 
   )
      Cat = "СчКорреспГлаваГ";
   elif(  ((Kind == "Т") AND ( D < 0 )) OR
          ((Kind == "О") AND ( D > 0 )) 
   )
      Cat = "СчКорреспГлаваГ";
   end; 

   if( Kind == "Т" )
      FiRole = FIROLE_CORACC_ACTIVE;
   else
      FiRole = FIROLE_CORACC_PASSIVE;
   end;

   FD.SetFIIDForCorAccNum(forw.Code_Currency);
 
   if( not FD.IsExistAccount( Cat, null, true, FIRole, corr, null, dat ) )
      if( (not FD.OpenAccount( Cat, null, true, FiRole, corr, dat ) ) )
         return SayError( 1, "Ошибка при актуализации счета по категории \"" + Cat + "\"" );
      end;   
   end; 

   if( D > 0 ) 
      if( Kind == "Т" )
         Copy( deb, forw ); 
         Copy( kred, corr ); 
         DebFIID = forw.Code_Currency;
         CredFIID = corr.Code_Currency;

         SumDeb = Abs(D);
         if( not SmartConvertSum( SumCred, SumDeb, dat, DebFIID, CredFIID, false) )
            return SayError( 1,  "Ошибка при конвертации суммы из "+ПолучитьКодФинИн(DebFIID)+" в "+ПолучитьКодФинИн(CredFIID) );
         end;

//         Ground = "Корректировка требований на сумму положительной разницы"; 
         Ground = "Переоценка по сделке " + UserGetDealCode(FD) + " от " + FD.tick.rec.dealdate + " с ЦБ " + FD.fininstr().rec.Name + " " + iif( TRIM(FD.avoiriss.rec.ISIN) == "", FD.avoiriss.rec.LSIN, FD.avoiriss.rec.ISIN);;

      elif( Kind == "О" ) 
         Copy( deb, corr ); 
         Copy( kred, forw ); 
         DebFIID = corr.Code_Currency;
         CredFIID = forw.Code_Currency;

         SumCred = Abs(D);
         if( not SmartConvertSum( SumDeb, SumCred, dat, CredFIID, DebFIID, false) )
            return SayError( 1,  "Ошибка при конвертации суммы из "+ПолучитьКодФинИн(CredFIID)+" в "+ПолучитьКодФинИн(DebFIID) );
         end;

//         Ground = "Корректировка обязательств на сумму положительной разницы"; 
         Ground = "Переоценка по сделке " + UserGetDealCode(FD) + " от " + FD.tick.rec.dealdate + " с ЦБ " + FD.fininstr().rec.Name + " " + iif( TRIM(FD.avoiriss.rec.ISIN) == "", FD.avoiriss.rec.LSIN, FD.avoiriss.rec.ISIN);

      end;
   elif( D < 0 )
      if( Kind == "Т" )
         Copy( deb, corr ); 
         Copy( kred, forw ); 
         DebFIID = corr.Code_Currency;
         CredFIID = forw.Code_Currency;

         SumCred = Abs(D);
         if( not SmartConvertSum( SumDeb, SumCred, dat, CredFIID, DebFIID, false) )
            return SayError( 1,  "Ошибка при конвертации суммы из "+ПолучитьКодФинИн(CredFIID)+" в "+ПолучитьКодФинИн(DebFIID) );
         end;

//         Ground = "Корректировка требований на сумму отрицательной разницы"; 
         Ground = "Переоценка по сделке " + UserGetDealCode(FD) + " от " + FD.tick.rec.dealdate + " с ЦБ " + FD.fininstr().rec.Name + " " + iif( TRIM(FD.avoiriss.rec.ISIN) == "", FD.avoiriss.rec.LSIN, FD.avoiriss.rec.ISIN);;
      elif( Kind == "О" ) 
         Copy( deb, forw ); 
         Copy( kred, corr ); 
         DebFIID = forw.Code_Currency;
         CredFIID = corr.Code_Currency;

         SumDeb = Abs(D);
         if( not SmartConvertSum( SumCred, SumDeb, dat, DebFIID, CredFIID, false) )
            return SayError( 1,  "Ошибка при конвертации суммы из "+ПолучитьКодФинИн(DebFIID)+" в "+ПолучитьКодФинИн(CredFIID) );
         end;

//         Ground = "Корректировка обязательств на сумму отрицательной разницы"; 
         Ground = "Переоценка по сделке " + UserGetDealCode(FD) + " от " + FD.tick.rec.dealdate + " с ЦБ " + FD.fininstr().rec.Name + " " + iif( TRIM(FD.avoiriss.rec.ISIN) == "", FD.avoiriss.rec.LSIN, FD.avoiriss.rec.ISIN);;

      end;
   end; 

   if( not ПроводкаПоКатегориямУчета( FD, 
           deb, kred, dat,
           deb.Chapter, 
           DebFIID, 
           SumDeb, 
           Doc, 
           INPCARRY, " 1", FD.tick.rec.DealCode, Ground,
           0, null, null, 
           DebFIID, CredFIID,
           CredFIID, SumCred /*сумма в валюте второго счета*/
     ) )
      return SayError( 1, SC_GetCarryError() );      
   end; 

   return 0;
end;


macro ExecuteStep( Doc, FDoc )

  var    FD, dat, error; 
  var    SumNKD = $0, AvoirCost = $0, NewSum = $0, OldSum = $0, D = $0, Kind = ""; 
  var    ВыполненоВидовПереоценки = 0, ExistsCourse = false, SinceDate; 
  var    RateRec = $0, err;

  var    DlMarket = TBFile( "dlmarket.dbt", "R", 0 ), MarketID = null, CentrOffice = null; 

  SetBuff( deal, FDoc ); 
  dat = ScOprServDoc.CommDate; 
  FD = SPFirstDoc( deal );

  var датаОстатка = DL_GetBalanceDateAfterWorkDayByCalendar(dat, 0, FD.ПолучитьКалендСвязанный());

  /*переоценка по валюте*/ 
  /*4. Если Flag2 ==да и в сделке (ВЦ != НацВ и ВР = НацВ и ВН = НацВ) */
  /* выполнить переоценку по валюте*/ 
  if( (ScOprServDoc.Flag2 == SET_CHAR) AND 
      (FD.dl_leg.rec.CFI != NATCUR) AND (FD.GetParametr(MC_TYPE_PARAMETR_PAYCURRENCY) == NATCUR) AND (FD.fininstr.rec.FaceValueFI == NATCUR)
  )        
     /*ВыполненоВидовПереоценки = ВыполненоВидовПереоценки + 1;        */

     /*счет переоценки требований*/
     if( ОпределитьПереоцениваемыйСчет( FD, dat, "Т" ) != 0 )
        return 1;
        InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
        ClearErrorArr();
     end;
     /*счет переоценки обязательств*/
     if( ОпределитьПереоцениваемыйСчет( FD, dat, "О" ) != 0 )
        return 1;
        InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
        ClearErrorArr();
     end;

     /*определим с какого счета берем старый остаток*/
     if( FD.BuySale == DEAL_TYPE_BUY ) 
        copy( forw_r, forw_o ); /*в покупках со счета обязательств*/
     else
        copy( forw_r, forw_t ); /*в продажах со счета требований*/
     end;  
        /*4.2. Расчитать Новую сумму S как сумма сделки (TotalCost) * курс рубля к ВЦ  за дату операции*/
        /*для универсальности кода считаем не покурсу рубля, а по курсу валюты счета "Форвард"*/
     if( not SmartConvertSum( NewSum, FD.dl_leg.rec.TotalCost, dat, FD.dl_leg.rec.CFI, forw_r.Code_Currency, false) )
        return SayError( 1,  "Ошибка при получении информации о котировке ценной бумаги в валюте "+ПолучитьКодФинИн(forw_r.Code_Currency) );
     end;
     NewSum = Round(NewSum, 2);
     
     if ((CatForvard == "+Форвард, прочие") OR (CatForvard == "-Форвард, прочие"))
       OldSum = GetSumTrnOverFromGraph(FD.tick.rec.DealID, FD.tick.rec.BOfficeKind, DLGR_TEMPL_OFFBALANCE, CatForvard) + GetSumTrnOverFromDocs(FD.tick.rec.DealID, FD.tick.rec.BOfficeKind, dat, false);
     else
       OldSum = Round(ABS(GetRestAccount(forw_r, датаОстатка)), 2);
     end;

     /*посчитать разницу, на которую нужно сделать корректировку*/ 
     D = NewSum - OldSum; 
     
     err = ConvSum(RateRec, $10000000000, dat, FD.dl_leg.rec.CFI, NATCUR, 0);
     if( D != 0 ) 
        ВыполненоВидовПереоценки = ВыполненоВидовПереоценки + 1;        

        /*переоценка по требованиям*/ 
        if( ВыполнитьПроводкуКорректировки( FD, Doc, dat, D, "Т", false ) != 0 )
          InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
          ClearErrorArr();
          return 1;
        end; 

        ScOpReportLineData[ScOpReportLineData.Size] = SvOpOvervalue_RD( deal.DealCode, FD.pm_avoir.rec.FIID, "Т", OldSum, ABS(NewSum), GetFICode(forw_t.Code_Currency,  null, FICK_ISOSTRING), FD.pm_avoir.rec.Amount, Double(RateRec)/10000000000.0, deb.Account, kred.Account, ABS(D), SumNKD);

        /*переоценка по обязательствам*/ 
        if( ВыполнитьПроводкуКорректировки( FD, Doc, dat, D, "О", false ) != 0 )
           InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
           ClearErrorArr();
           return 1;
        end;
        ScOpReportLineData[ScOpReportLineData.Size] = SvOpOvervalue_RD( deal.DealCode, FD.pm_avoir.rec.FIID, "О", OldSum, ABS(NewSum), GetFICode(forw_o.Code_Currency,  null, FICK_ISOSTRING), FD.pm_avoir.rec.Amount, Double(RateRec)/10000000000.0, deb.Account, kred.Account, ABS(D), SumNKD);

     end;

  end;

  /* переоценка по выпускам ц/б*/ 
  /*5. Если Flag1 == да и ц/б релевантна */
  /* ==>   Если в параметрах задана ц/б, то с заданным выпуском */
  /* ==>   Если задан вид ц/б, то  с ц/б заданного вида         */
  /* ==>   С котируемым ФИ                                      */
  if( (ScOprServDoc.Flag1 == SET_CHAR) AND 
/*HF 47.2*/
      /*по выпуску ц/б*/ 
      ( (ScOprServDoc.FIID <= 0) OR (FD.fininstr.rec.FIID == ScOprServDoc.FIID) ) AND
      /*по виду ц/б*/ 
      ( (ScOprServDoc.FIID > 0) OR 
        (ScOprServDoc.AvoirKind == 0) OR 
        /*(FD.fininstr.rec.AvoirKind == ScOprServDoc.AvoirKind) */
        (FI_AvrKindsEQ( FIKIND_AVOIRISS, ScOprServDoc.AvoirKind, FD.fininstr.rec.AvoirKind ) == true)
      ) /*AND
      /*если задана бумага, то по ней, иначе по цб котируемым и в обращении*/
      ( (ScOprServDoc.FIID > 0 ) OR FI_IsQuoted( FD.fininstr.rec.FIID, dat ) )*/
  ) 
     /*ВыполненоВидовПереоценки = ВыполненоВидовПереоценки + 1;*/

     /*переоценка требований*/
     if( FD.BuySale == DEAL_TYPE_BUY )

        Kind = "Т";
     /*переоценка обязательств*/
     elif( FD.BuySale == DEAL_TYPE_SALE )

        Kind = "О";
     end;

     if( Kind != "" )

        if( ОпределитьПереоцениваемыйСчет( FD, dat, Kind ) != 0 )
           InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
           ClearErrorArr();
           return 1;
        end;

        if( deal.MarketID > 0 )
           MarketID = deal.MarketID;

           if( deal.MarketSchemeID > 0 )
              DlMarket.Clear();                                     
              DlMarket.rec.ID = deal.MarketSchemeID;                     
              if( not DlMarket.GetEQ() )                            
                 MsgBox( "Не найдена схема расчетов с биржей" );    
              else
                 CentrOffice = DlMarket.rec.CentrOffice;
              end;                                                  
           end;
        end;

        /*Сумма НКД в валюте счета Форвард*/
        if( not SmartConvertSumDbl(
                     SumNKD, MoneyToDouble(FD.dl_leg.rec.NKD),
                     FD.tick.rec.DealDate, FD.NKDFIID(),
                     forw.Code_Currency, false,
                     ПолучитьВидКурса(SP_RATETYPE_MarketPrice), SinceDate, MarketID, CentrOffice) )
           return SayError( 1,  "Ошибка при получении суммы НКД в валюте "+ПолучитьКодФинИн(forw.Code_Currency) );
        end;
        SumNKD = DoubleToMoney( SumNKD );

        /*
        Разбор вариантов:
        ц/б котируемая и курс задан за дату и не равен нулю - считаем переоценку
        ц/б не котируемая и курс задан за дату и не равен нулю - считаем переоценку
        ц/б котируемая и курс задан за дату и равен нулю - ругаемся
        ц/б не котируемая и курс задан за дату и равен нулю - не отбираем
         
        ц/б котируемая и курс задан за пред. дату и не равен нулю - ругаемся
        ц/б не котируемая и курс задан за пред. дату и не равен нулю - ругаемся
        ц/б котируемая и курс задан за пред. дату и равен нулю - ругаемся
        ц/б не котируемая и курс задан за пред. дату и равен нулю - не отбираем

        ц/б котируемая и курс не задан вообще  - ругаемся
        ц/б не котируемая и курс не задан вообще - не отбираем
        */

        AvoirCost = GetCourse_OVERVALUE_RD(FD.fininstr.rec.FIID, forw.Code_Currency, dat, MoneyToDouble(FD.pm_avoir.rec.Amount), MarketID, CentrOffice, ExistsCourse, SinceDate);
        if (SinceDate != dat)
          return SayError( 1, "Для выпуска \"" + ПолучитьКодФинИн(FD.fininstr.rec.FIID) + "\" не найдено значение курса 'Справедливая стоимость' за дату " + dat );
        end;
        /*Не существует ненулевой курс за дату - ругаемся*/
        if((ExistsCourse == true) and (money(AvoirCost) != $0) )
//           If(FD.DealAsResultDVExe == false)
              /*Существует ненулевой курс - делаем переоценку*/

              AvoirCost = DoubleToMoney( AvoirCost );

              NewSum = AvoirCost;
              NewSum = Round(NewSum, 2);

              if ((CatForvard == "+Форвард, прочие") OR (CatForvard == "-Форвард, прочие"))
                OldSum = GetSumTrnOverFromGraph(FD.tick.rec.DealID, FD.tick.rec.BOfficeKind, DLGR_TEMPL_OFFBALANCE, CatForvard) + GetSumTrnOverFromDocs(FD.tick.rec.DealID, FD.tick.rec.BOfficeKind, dat, true);
              else
                OldSum = Round(ABS(GetRestAccount(forw, датаОстатка)), 2);
              end;

              D = NewSum - OldSum; 

              if( D != 0 ) 
                 ВыполненоВидовПереоценки = ВыполненоВидовПереоценки + 1;

                 if( ВыполнитьПроводкуКорректировки( FD, Doc, dat, D, Kind, true ) != 0 )
                    InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
                    ClearErrorArr();
                    return 1;
                 end;                        
                 ScOpReportLineData[ScOpReportLineData.Size] = SvOpOvervalue_RD( deal.DealCode, FD.pm_avoir.rec.FIID, Kind, OldSum, ABS(NewSum), GetFICode(forw.Code_Currency,  null, FICK_ISOSTRING), FD.pm_avoir.rec.Amount, AvoirCost/FD.pm_avoir.rec.Amount, deb.Account, kred.Account, ABS(D), SumNKD);
     
              end;

              if (not(SinceDate == dat))
                   SayError( 1, "При переоценке использовался курс на " + SinceDate);
              end;           
//           end;   
        else /*курс существует, но равен 0*/
           return SayError( 1, "Курс вида \"Рыночная цена\" для выпуска \"" + ПолучитьКодФинИн(FD.fininstr.rec.FIID) + "\" в валюте " + ПолучитьКодФинИн(forw.Code_Currency) + " за дату операции равен нулю " );
        end;
     end; 
  end;

  if( ВыполненоВидовПереоценки == 0 ) 
     SayError( 0, "Сделке " + FD.tick.rec.DealCode + " не требуется переоценка." );  
     return 0;     
  end;
  DL_SaveDataForReport_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind, ScOpReportLineData, LOG_TYPE_DATA);
  ScOpReportLineData.Size = 0;
  
  InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
  ClearErrorArr();


  return 0;
end;
