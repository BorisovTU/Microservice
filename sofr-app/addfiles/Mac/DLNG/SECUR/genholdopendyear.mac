/**
  @file genholdopendyear.mac
  @brief Генерация операций удержания НДФЛ по окончанию года

  

  #tag
  - functional_block:НДФЛ

  #changelog
*/

import "sprepfun.mac", "sqlconv.mac", "CbReport_h.mac", "scsrvrepfun.mac", "func_lib.mac", "cblogger.mac", "nptxskipcat.mac";

private macro ExistsNptxopCode(DocKind, Code)
  var query, cmd;

  query = "select 1 from dnptxop_dbt where t_DocKind = ? and t_Code = ?";
  cmd = DL_RSDCommand(query);
  cmd.AddParam(DocKind);
  cmd.AddParam(Code);

  if(cmd.GetCount() > 0)
    return true;
  end;

  return false;
end;

MACRO GetDeathClientDate(ClientID)
  var sql, cmd, DS; 
  sql = "select t_Death from dpersn_dbt where t_personid = ? ";
    
  cmd = DL_RSDCommand(sql);
  cmd.AddParam(ClientID);
    
  DS = cmd.Execute();
    
  if(DS.movenext())
    return DS.Death;
  end; 

  return ZeroDate;	
END;

PRIVATE MACRO GetYear(_Date)
  var Year = 0;
  var Date = _Date;

  if(ValType(Date) ==  V_DTTM)
    DtTmSplit(_Date, Date, NULL);
  end;

  DateSplit(Date, NULL, NULL, Year);

  return Year;
END;

private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

macro insert_hold_ndfl_oper(ClientID, SubKind_Operation, TaxYear, OperDate, IIS, ErrMsg:@string, FromSheduler, ByClientList:bool, Prefix:string)
  var cmd;
  var SystDate = date(0,0,0), SystTime = time(0);
  var RefID = 0;
  var OperCode = "";

  if(Prefix == NULL)
    Prefix = "";
  end;

  GetSystDateTime(@SystDate, @SystTime);

  GenerateNumberByReference( 133/*OBJTYPE_NPTXHOLD*/, 1, @RefID, @OperCode );
  if(OperCode != "")
    var i = 0;
    while(ExistsNptxopCode(DL_HOLDNDFL, OperCode))
      if(i == 100) //На всякий случай, чтобы не зависла операция
        ErrMsg = "Ошибка при генерации номера операции удержания НДФЛ";
        return;
      end;
      
      GenerateNumberByReference( 133/*OBJTYPE_NPTXHOLD*/, 1, @RefID, @OperCode );
      i = i + 1;
    end;

    if(Prefix != "")
      OperCode = string(Prefix) + string(OperCode);
    end;
  end;

  
  RslDefCon.BeginTrans();

  cmd = RSDCommand(  " DECLARE ID NUMBER(10); " 
                   + " BEGIN " 
                   + " INSERT INTO dnptxop_dbt (T_ID, "
                   + "                          T_DOCKIND, "
                   + "                          T_KIND_OPERATION, "
                   + "                          T_SUBKIND_OPERATION, "
                   + "                          T_CODE,"
                   + "                          T_OPERDATE, "
                   + "                          T_CLIENT, "
                   + "                          T_CONTRACT, "
                   + "                          T_PREVDATE, "
                   + "                          T_PLACEKIND, "
                   + "                          T_PLACE, "
                   + "                          T_TAXBASE, "
                   + "                          T_OUTSUM,"
                   + "                          T_OUTCOST,"
                   + "                          T_TOUT,"
                   + "                          T_TOTALTAXSUM,"
                   + "                          T_PREVTAXSUM,"
                   + "                          T_TAXSUM,"
                   + "                          T_TAX, "
                   + "                          T_METHOD,"
                   + "                          T_ACCOUNT, "
                   + "                          T_CURRENCY, "
                   + "                          T_STATUS, "
                   + "                          T_OPER, "
                   + "                          T_DEPARTMENT, "
                   + "                          T_IIS) "
                   + " VALUES (to_char(DNPTXOP_DBT_SEQ.NextVal), " //T_ID                    
                   + "         ?, "                                //T_DOCKIND               
                   + "         ?, "                                //T_KIND_OPERATION        
                   + "         ?, "                                //T_SUBKIND_OPERATION     
                   + "         ?, "                                //T_CODE                  
                   + "         ?, "                                //T_OPERDATE             
                   + "         ?, "                                //T_CLIENT               
                   + "         0, "                                //T_CONTRACT             
                   + "         ?, "                                //T_PREVDATE             
                   + "         0, "                                //T_PLACEKIND             
                   + "         0, "                                //T_PLACE                 
                   + "         0, "                                //T_TAXBASE               
                   + "         0, "                                //T_OUTSUM                
                   + "         0, "                                //T_OUTCOST               
                   + "         0, "                                //T_TOUT                  
                   + "         0, "                                //T_TOTALTAXSUM           
                   + "         0, "                                //T_PREVTAXSUM            
                   + "         0, "                                //T_TAXSUM                
                   + "         0, "                                //T_TAX                  
                   + "         10, "                               //T_METHOD                
                   + "         CHR(1), "                           //T_ACCOUNT              
                   + "         0, "                                //T_CURRENCY             
                   + "         0, "                                //T_STATUS               
                   + "         ?, "                                //T_OPER                 
                   + "         ?, "                                //T_DEPARTMENT            
                   + "         ?) "                                //T_IIS                 
                   + "         RETURNING T_ID INTO ID; " 
                   + " INSERT INTO DFUNCOBJ_DBT  ( T_ID, T_OBJECTTYPE, T_OBJECTID, T_FUNCID, T_PRIORITY) VALUES( 0, ?, ID, 550, 40); "
                   + " INSERT INTO DNPTXHOLDGEN_DBT (T_NPTXOPID, T_YEAR, T_GENSYSTDATE, T_GENSYSTTIME, T_FROMSHEDULER, T_BYCLIENTLIST) VALUES(ID, ?, ?, ?, ?, "+IIF(ByClientList == true, "'X'", "CHR(0)")+"); "
                   + " END; "
                  );
  
  cmd.addParam( "", RSDBP_IN, DL_HOLDNDFL );              // T_DOCKIND
  cmd.addParam( "", RSDBP_IN, 2038 );                     // T_KIND_OPERATION
  cmd.addParam( "", RSDBP_IN, SubKind_Operation );        // T_SUBKIND_OPERATION
  cmd.addParam( "", RSDBP_IN, OperCode );                 // T_CODE
  cmd.addParam( "", RSDBP_IN, OperDate);                  // T_OPERDATE
  cmd.addParam( "", RSDBP_IN, ClientID );                 // T_CLIENT
  cmd.addParam( "", RSDBP_IN, date(1,1,TaxYear) );        // T_PREVDATE
  cmd.addParam( "", RSDBP_IN, {oper} );                   // T_OPER
  cmd.addParam( "", RSDBP_IN, {OperDprt});                // T_DEPARTMENT
  cmd.addParam( "", RSDBP_IN, IIS);                       // T_DEPARTMENT
  cmd.addParam( "", RSDBP_IN, DL_HOLDNDFL );              // T_DOCKIND
  cmd.addParam( "", RSDBP_IN, TaxYear);
  cmd.addParam( "", RSDBP_IN, SystDate);
  cmd.addParam( "", RSDBP_IN, SystTime);
  cmd.addParam( "", RSDBP_IN, FromSheduler);
  cmd.execute();

  
  RslDefCon.CommitTrans(); /*Завершить транзакцию*/

OnError(er)
  ErrMsg = "Возникла ошибка при сиздании операции. Операция не создалась. " + er.message;
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  return 1;
end;

macro insert_hold_ndfl_oper_endyear(ClientID, TaxYear, OperDate, IIS, ErrMsg:@string, FromSheduler, ByClientList:bool, Prefix:string)
  return insert_hold_ndfl_oper(ClientID, DL_TXHOLD_OPTYPE_ENDYEAR, TaxYear, OperDate, IIS, @ErrMsg, FromSheduler, ByClientList, Prefix);
end;

PRIVATE MACRO AddNpTxMes(ClientCode:string, ClientName:string, Msg:string)
  var query, cmd;

  query =   " INSERT INTO DNPTXTBMES_TMP (T_ID, T_TBID, T_DATE, T_TIME, T_TYPE, T_MESSAGE) "
          + "                VALUES (0, 0, ?, ?, ?, ?) ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(date(0,0,0));
  cmd.AddParam(time(0));
  cmd.AddParam(0);
  cmd.AddParam(string(ClientCode)+";"+string(ClientName)+";"+Msg);

  cmd.ExecuteCMD();

OnError(er)
  msgbox(er.Message);
END;

PRIVATE CLASS (DL_CReportTemplate) GenHoldOpProtocol_Report(GenSystDate:date, GenSystTime:time)
  var m_GenSystDate = GenSystDate;
  var m_GenSystTime = GenSystTime;

  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран

    CreateTotalBook();
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;


  PRIVATE MACRO EndReport()
    SaveTotalbook();
  END;

  PRIVATE MACRO Create()
    var i = 0;
    var SheetName = "Протокол";
    var query, cmd, DataSet;
    var cnt = 0;

    SetActiveSheet(SheetName);

    PrintFormatString( NULL, "N1_GenDate", m_GenSystDate,
                             "N1_GenTime", m_GenSystTime
                     );

    CopyAllSheetInTotalBook(SheetName, false, "N1_Header", 0, SheetName);

    query = "select t_Message as Msg from dnptxtbmes_tmp";

    cmd = DL_RSDCommand(query);

    cnt = cmd.GetCount();
    if(cnt > 0)

      InitProgress(cnt, "Печать протокола", "Печать протокола");

      
      CopyAllSheetInTotalBook(SheetName, false, "N1_TableHeader", 1, SheetName);

      RegisterTable( "N1_MainTable", NULL,
                     "N1_ClientCode",
                     "N1_ClientName",       
                     "N1_Msg"       
                   );
      i = 0;
      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        var arr = SplitString(DataSet.Msg, ";");
          
        if(arr.size > 0)
          PrintTableLine("N1_ClientCode", arr[0], null,
                         "N1_ClientName", arr[1], null,      
                         "N1_Msg",        arr[2], null   
                      );
        end;

        UseProgress(i = i + 1);
      end;

      EndTable();

      CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);


      RemProgress();
    end;

  END;

  MACRO Run()
    Run();
    
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_ProtocolGenHoldOp.xlsx", true, null, null, true ); 
END;

MACRO GenerateHoldTaxOpEndYear(TaxYear:integer, OperDate:date, FromSheduler, ByClientList:bool)
  var query, cmd, DataSet;
  var StartDate = date(1,1,TaxYear);
  var EndDate   = date(31,12,TaxYear);
  var cnt = 0, i = 0;
  var ErrStr = "";
  var SystDate = date(0,0,0), SystTime = time(0);
  var IsAlreadyExecuted = false;
  var IsGetSNOBProcExecuted = false;

  if(ByClientList == NULL)
    ByClientList = false;
  end;

  GetSystDateTime(@SystDate, @SystTime);

  DL_RSDCommand("DELETE FROM DNPTXTBMES_TMP").ExecuteCMD();

  if(FromSheduler == SET_CHAR)
    
    // Если запустили процедуру генерации из планировщика, то проверяем, что в дату OperDate по всем клиентам была выполнена процедура запросов к хранилищу СНОБ
    var queryIsExecGetSNOBProc = "select 1 " +
                                 "from dnptxop_dbt op " +
                                 "where op.t_OperDate = ? " +
                                 "  and op.t_DocKind = " + string(DL_GETSNOBPROC) +
                                 "  and op.t_Client = -1 " +
                                 "  and op.t_Status = 2 ";

    var cmdIsExecGetSNOBProc = DL_RSDCommand(queryIsExecGetSNOBProc);
    cmdIsExecGetSNOBProc.AddParam(SystDate);
    var dsIsExecGetSNOBProc = cmdIsExecGetSNOBProc.Execute();

    if(dsIsExecGetSNOBProc.MoveNext())
      IsGetSNOBProcExecuted = true;

      // Проверяем, что процедура еще не запускалась в текущем опер. дне
      var queryIsExec = "select 1 " +
                        "from dss_history_dbt " +
                        "where t_Service = 10084 " +
                        "  and to_char(t_BeginStamp, 'dd.mm.yyyy') = to_char(?, 'dd.mm.yyyy') " +
                        "  and t_State = 4 ";

      var cmdIsExec = DL_RSDCommand(queryIsExec);
      cmdIsExec.AddParam(SystDate);
      var dsIsExec = cmdIsExec.Execute();

      if(dsIsExec.MoveNext())
        IsAlreadyExecuted = true;
      else
        IsAlreadyExecuted = false;
      end;
    end;
  end;

  if((not FromSheduler) or ((not IsAlreadyExecuted) and (IsGetSNOBProcExecuted)))
    query =   " select q1.*, party.t_Name as ClientName, NVL(RSB_SECUR.SC_GetObjCodeOnDate("+OBJTYPE_PARTY+", 1, q1.t_PartyID), CHR(1)) as ClientCode "
            + "   from (select q.t_PartyID, "
            + "                NVL((select MAX(op.t_OperDate) "
            + "                       from dnptxop_dbt op "
            + "                      where op.t_DocKind = " + DL_CALCNDFL
            + "                        and op.t_Client = q.t_PartyID "
            + "                        and Extract(Year from op.t_OperDate) = ? "
            + "                        and op.t_SubKind_Operation = " + DL_TXBASECALC_OPTYPE_ENDYEAR
            + "                        and op.t_Status = 2 "
            + "                    ), "+GetSQLDate(date(0,0,0))+") as CalcNdflOpDate, "
            + "                (select count(1) "
            + "                   from dsfcontr_dbt sf, ddlcontr_dbt dlc "
            + "                  where sf.t_PartyID = q.t_PartyID "
            + "                    and dlc.t_SfContrID = sf.t_ID "
            + "                    and dlc.t_IIS <> 'X' "
            + "                    and sf.t_DateBegin <= ? "
            + "                    and (sf.t_DateClose = " +GetSQLDate(date(0,0,0))+ " or sf.t_DateClose > ?)"
            + "                ) as ExistBOContr "
            + "           from (select DISTINCT cl.t_PartyID "
            + "                   from dclient_dbt cl, dparty_dbt pt "
            + "                  where cl.t_ServiceKind IN ("+PTSK_STOCKDL+", "+PTSK_DV+", "+PTSK_SVO+", "+PTSK_DEPOS+") "
            + "                    and cl.t_StartDate <= ? "
            + "                    and (cl.t_FinishDate = " + GetSQLDate(date(0,0,0)) + " or cl.t_FinishDate > ? ) "
            + "                    and pt.t_PartyID = cl.t_PartyID "
            + "                    and pt.t_LegalForm = 2 "
            + "                ) q "
            + "        ) q1, dparty_dbt party "
            + "  where party.t_PartyID = q1.t_PartyID "
            + "    and Not Exists(select 1 "
            + "                     from dnptxop_dbt op "
            + "                    where op.t_DocKind = " + DL_HOLDNDFL
            + "                      and op.t_Client = q1.t_PartyID "
            + "                      and ((op.t_Status = 0 and op.t_OperDate = ?) or (op.t_Status > 0 and op.t_OperDate >= ?))"
            + "                  ) "
            + "    and 1 = ( case when q1.CalcNdflOpDate > "+GetSQLDate(date(0,0,0))+" then 1 " //У клиента есть операция расчета НОБ по окончанию года
            + "                   when q1.CalcNdflOpDate = "+GetSQLDate(date(0,0,0))+" and q1.ExistBOContr > 0 then 1 " //У клиента нет операции расчета НОБ по окончанию года, но есть ДБО. Отбирам, чтобы вывести сообщение в протокол
            + "                   when Exists(select 1 "  //Или у клиента есть только договор ИИС
            + "                                 from dsfcontr_dbt sf, ddlcontr_dbt dlc, ddlcontrmp_dbt mp, dsfcontr_dbt sf_mp "
            + "                                where sf.t_PartyID = q1.t_PartyID "
            + "                                  and dlc.t_SfContrID = sf.t_ID "
            + "                                  and dlc.t_IIS = 'X' "
            + "                                  and mp.t_DlContrID = dlc.t_DlContrID "
            + "                                  and sf_mp.t_ID = mp.t_SfContrID "
            + "                                  and sf_mp.t_DateBegin <= ? "
            + "                                  and (sf_mp.t_DateClose = " +GetSQLDate(date(0,0,0))+ " or sf_mp.t_DateClose > ?)"
            + "                              ) "
            + "                        and Not Exists(select 1 "
            + "                                         from dsfcontr_dbt sf, ddlcontr_dbt dlc, ddlcontrmp_dbt mp, dsfcontr_dbt sf_mp "
            + "                                        where sf.t_PartyID = q1.t_PartyID "
            + "                                          and dlc.t_SfContrID = sf.t_ID "
            + "                                          and dlc.t_IIS <> 'X' "
            + "                                          and mp.t_DlContrID = dlc.t_DlContrID "
            + "                                          and sf_mp.t_ID = mp.t_SfContrID "
            + "                                          and sf_mp.t_DateBegin <= ? "
            + "                                          and (sf_mp.t_DateClose = " +GetSQLDate(date(0,0,0))+ " or sf_mp.t_DateClose > ?)"
            + "                                      ) then 1 "
            + "                   when Exists(select 1 "  //Или клиент только на обслуживании СВО
            + "                                 from dclient_dbt cl "
            + "                                where cl.t_PartyID = q1.t_PartyID "
            + "                                  and cl.t_ServiceKind = " + PTSK_SVO
            + "                                  and cl.t_StartDate <= ? "
            + "                                  and (cl.t_FinishDate = " + GetSQLDate(date(0,0,0)) + " or cl.t_FinishDate > ? ) "
            + "                              ) "
            + "                        and Not Exists(select 1 "
            + "                                 from dclient_dbt cl "
            + "                                where cl.t_PartyID = q1.t_PartyID "
            + "                                  and cl.t_ServiceKind IN ("+PTSK_STOCKDL+", "+PTSK_DV+", "+PTSK_DEPOS+") "
            + "                                  and cl.t_StartDate <= ? "
            + "                                  and (cl.t_FinishDate = " + GetSQLDate(date(0,0,0)) + " or cl.t_FinishDate > ? ) "
            + "                              ) then 1 "
            + "                   else 0 end) "; 

    cmd = DL_RSDCommand(query);

    cmd.AddParam(TaxYear);
    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);
    cmd.AddParam(OperDate);
    cmd.AddParam(OperDate);
    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);
    cmd.AddParam(EndDate);

    InitProgress(-1, "Определение количества клиентов", "Определение количества клиентов");
    cnt = cmd.GetCount(query);
    RemProgress();

    if(cnt > 0)
      InitProgress(cnt, "Генерация операций", "Генерация операций");
   
      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        ErrStr = "";

        if (GetIntGeneralMainObjAttr(OBJTYPE_PARTY, DataSet.PartyID, 130, {curdate}) == 1)
          AddNpTxMes(DataSet.ClientCode, DataSet.ClientName, "На клиенте установлена категория \"Исключить из автоматического удержания НДФЛ по итогам года\" = ДА");
        elif((date(DataSet.CalcNdflOpDate) == date(0,0,0)) and (DataSet.ExistBOContr > 0))
          AddNpTxMes(DataSet.ClientCode, DataSet.ClientName, "Не выполнена операция расчета НОБ с типом расчета \"Окончание года\" за указанный налоговый год");
        elif(GetDeathClientDate(DataSet.PartyID) != ZeroDate)
          AddNpTxMes(DataSet.ClientCode, DataSet.ClientName, "У клиента в анкете заполнена дата смерти. Операция не создалась");
        else
          insert_hold_ndfl_oper_endyear(DataSet.PartyID, TaxYear, OperDate, UNSET_CHAR, @ErrStr, FromSheduler, ByClientList);
          if(ErrStr == "")
            ErrStr = "Операция создана успешно";
          end;

          AddNpTxMes(DataSet.ClientCode, DataSet.ClientName, ErrStr);
        end;

        UseProgress(i = i + 1);
      end;

      RemProgress();
    end;
  elif(IsAlreadyExecuted == true)
    AddNpTxMes("", "", "В текущем системном дне ранее уже была выполнена процедура генерации операций удержания НДФЛ. Новая генерация операций удержания НДФЛ не выполнена.");
  elif(IsGetSNOBProcExecuted == false)
    AddNpTxMes("", "", "В текущем системном дне не выполнена процедура запроса данных к Хранилищу СНОБ. Генерация операций удержания НДФЛ не выполнена.");
  end;

  //Печать протокола
  var ProtocolRepObj = GenHoldOpProtocol_Report(SystDate, SystTime);
        
  var FullProtocolFileNameXLSX = ProtocolRepObj.Run();

  ProtocolRepObj = NULL;

  if(not isStandAlone())
    FullProtocolFileNameXLSX = "$"+FullProtocolFileNameXLSX;
  end;
  
  if(FullProtocolFileNameXLSX != "")
    var day, mon, year;
    var hour, mn, sec;
    var FName, FExt;
    
    DateSplit(SystDate, day, mon, year);
    TimeSplit(SystTime, hour, mn, sec);
    
    var FromDir = SplitFile(FullProtocolFileNameXLSX, FName, FExt);
    
    var NewFileNameXLSX = "Протокол_генерации_операций_удержания_НДФЛ_"+string(day:o:2)+"."+string(mon:o:2)+"."+string(year)+" "+string(hour:f:2)+"-"+string(mn:f:2)+"-"+string(sec:f:2) + ".xlsx";

    if(not RenameFile(FullProtocolFileNameXLSX, FromDir+NewFileNameXLSX))
      msgbox("Ошибка при переименовании файла протокола");
    elif(FromSheduler == UNSET_CHAR)
      SC_ShowFile(FromDir, NewFileNameXLSX);
    end;
  end;

  DL_RSDCommand("DELETE FROM DNPTXTBMES_TMP").ExecuteCMD();

END;