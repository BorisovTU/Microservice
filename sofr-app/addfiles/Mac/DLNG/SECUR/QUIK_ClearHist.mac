/*
$Name:        QUIK_ClearHist.mac
$Module:      Ценные бумаги
$Description: Очистка истории расчетов и сверки QUIK
*/
IMPORT SPInter, DealsInter, spserv;   


MACRO ExecClearLimitHist( MarketID:INTEGER,
                 MarketCode:STRING,
                 FlagStockDl:BOOL,
                 CheckFlagStockDl,
                 CalcDateStockDl:DATE,
                 FlagDeriv:BOOL,
                 CheckFlagDeriv,
                 CalcDateDeriv:DATE,
                 FlagCurr:BOOL,
                 CheckFlagCurr,
                 CalcDateCurr:DATE,
                 CheckFlagProtocol,
                 CalcDateProtocol:DATE
               )
  var stat = 0;
  var cmd;
  var ErrStr = "", errorCount = 0, i = 0;

  cmd = RsdCommand("{CALL RSI_SCLIMIT.RSI_ClearLimitHistory(?, ?, ?, chr(?), ?, ?, chr(?), ?, ?, chr(?), ?, chr(?), ?) }");

  cmd.addParam("p_MarketID",          RSDBP_IN, MarketID );
  cmd.addParam("p_MarketCode",        RSDBP_IN, IIF(MarketCode == "", "", MarketCode) );
  cmd.addParam("p_ByStock",           RSDBP_IN, IIF(FlagStockDl == true, 1, 0) );
  cmd.addParam("p_CondStock",         RSDBP_IN, CheckFlagStockDl );
  cmd.addParam("p_DateStock",         RSDBP_IN, CalcDateStockDl );
  cmd.addParam("p_ByCurr",            RSDBP_IN, IIF(FlagCurr == true, 1, 0) );
  cmd.addParam("p_CondCurr",          RSDBP_IN, CheckFlagCurr );
  cmd.addParam("p_DateCurr",          RSDBP_IN, CalcDateCurr );
  cmd.addParam("p_ByDeriv",           RSDBP_IN, IIF(FlagDeriv == true, 1, 0) );
  cmd.addParam("p_CondDeriv",         RSDBP_IN, CheckFlagDeriv );
  cmd.addParam("p_DateDeriv",         RSDBP_IN, CalcDateDeriv );
  cmd.addParam("p_CondProtocol",      RSDBP_IN, CheckFlagProtocol );
  cmd.addParam("p_DateProtocol",      RSDBP_IN, CalcDateProtocol );

  RslDefCon.BeginTrans();

  InitProgress(-1, "Выполняется очистка истории расчетов лимитов", "Очистка истории расчетов лимитов");
  
  cmd.execute();
  
  RemProgress();

  RslDefCon.CommitTrans();

  msgbox("Очистка истории расчетов лимитов выполнена успешно!");

  return stat;

OnError(er)
  
  RemProgress();

  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();                    
  end;

  if (IsEqClass ("TrslError", er))
    ErrStr=er.Message;
    if(IsEqClass ("TDbError", er.err))
      ErrStr=ErrStr+":|"+er.err.Stat+"-"+er.err.Oper+"-"+er.err.Table+"-"+er.err.Message;
    elif (isEqClass("TRsdError", er.err))

      errorCount = er.err.environment.ErrorCount;
      while (i < errorCount)
          ErrStr=ErrStr+er.err.environment.Error(i).Descr;
          i = i + 1;
      end;
    end;
  end;

  if(ErrStr == "")
    ErrStr=er.module+ " "+er.line+" "+er.message;
  end;

  msgbox(ErrStr);

  return 1;
END;
