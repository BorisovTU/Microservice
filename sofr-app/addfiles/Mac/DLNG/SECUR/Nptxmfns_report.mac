/*
$Name:        nptxmfns_report.mac
$Module:      Ценные бумаги
$Description: Отчет "Сообщение в ФНС по счетам ИИС"
*/

import "nptxmfns_form.mac", "RegistrReport.mac", "dl_xml.mac", "dlerr_log.mac", "RepPersonalIncomeTax.mac", "Adress.mac", "TaxReportBase.mac";
import RepPersonalIncomeTaxU;

PRIVATE CONST ZERO_DATE    = date(0, 0, 0);
PRIVATE CONST UnknownParty = -1;

PRIVATE CONST NOTEKIND_NEW_CFT_CODE     = 176; //Примечание "Код ЦФТ субъекта Брокера, к которому осуществляется перевод ДБО ИИС"
PRIVATE CONST NOTEKIND_NEW_CONTR_NUMBER = 177; //Примечание "Номер договора ИИС, на который осуществляется перевод активов"
PRIVATE CONST NOTEKIND_NEW_CONTR_DATE   = 178; //Примечание "Дата открытия договора ИИС, на который осуществляется перевод активов"
PRIVATE CONST NOTEKIND_NEW_ACCOUNT_DATE = 179; //Примечание "Дата открытия счета по договору ИИС, на который осуществляется перевод активов"

private var Form;
private var stat;
private var ErrLog;
private var Rep;
private var log;

CLASS CNptxMesFnsData()
   var EndDate:date;
   var InvestorID:integer;
   var ContrID:integer;
   var ContrNum:string;
   var SignerID:integer;
   var MessNum:string;
   var MessKind:integer;
   var PersonID:integer;
   var CorrectNum:integer;

   macro Constructor()
      EndDate = ZERO_DATE;
      InvestorID = UnknownParty;
      ContrID = 0;
      MessNum = "";
      MessKind = -1;
      PersonID = -1;
      CorrectNum = 0;
   end;

   this.Constructor();
END;

private macro PersnIsFirst(PersonId)
  var officer    = TBfile("officer.dbt");
  officer.rec.PersonId = PersonId;
  officer.rec.PartyID = {OurBank};
  if ((officer.GetGE()) and (officer.rec.PersonId == PersonId) and (officer.rec.PartyID == {OurBank}))
    return officer.rec.isFirstPerson;
  end;
  return false;
end;

//Получить имя папки текстовых файлов
PRIVATE MACRO GetTxtPath()
  var RegistryPath = "РСХБ\\ДИРЕКТОРИИ\\TAX";

  var Path = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if(stat) 
     msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  end;

  return Path;
END;

PRIVATE MACRO processPath( p_path )
  var l_str, l_p, l_path = "";

  p_path = trim( p_path );

  if( substr( p_path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
   p_path = substr( p_path, 3 );

   l_str  = getCWD(false);    
   l_p    = strbrk( l_str, "\\" );
   while( l_p != 0 )
    l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
    l_str  = substr( l_str, l_p + 1 );
    l_p    = strbrk( l_str, "\\" );
   end;
   return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );

  elif( substr( p_path, 1, 1 ) == "." ) /* ссылка на текущий каталог */
   return ( getCWD + substr( p_path, 2 ) );
  end;

  return p_path;
END;

PRIVATE MACRO GetRepFullPath(isRemote:bool)
  var Path = "";
  
  if(isRemote)
    var txtPath = GetTxtPath();

    if( substr( txtPath, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
      txtPath = substr(txtPath, 3 );
    end;

    if( substr( txtPath, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
      txtPath = substr(txtPath, 2 );
    end;

    Path = GetCurDir(true) + "\\" + txtPath + "\\"; 
  else
    Path = processPath(GetTxtPath()) + "\\"  ;
  end;

  // получить полный путь
  return Path;
END;

MACRO ПроверитьПутьДБО_ФНС()
   var err = "";
   var dl = null;
   dl = TDirList(GetRepFullPath(), "FD");
   if(dl.Count == 0)
     err = "Нет каталога или нет доступа к каталогу: " + GetRepFullPath();
   end;
   return err;
END;

MACRO GetPassData(PartyID:integer, Name1:@string, Name2:@string, Name3:@string,
                                   BirthDate:@date, PlaceOfBirth:@string, CodeDocum:@string,
                                   Series:@string, Number:@string, DocumentDate:@date, PaperIssuer:@string)
                                   
   var SqlPersn = RSDCommand("select PERSN.T_BORN, PERSN.T_NAME1, PERSN.T_NAME2, PERSN.T_NAME3, PERSN.T_BIRSPLASE" +                                                        
                 "  from DPERSN_DBT PERSN"+                                          
                 " where PERSN.t_PersonId = ?");

   SqlPersn.addParam( "", RSDBP_IN, PartyID );                                                                              
   SqlPersn.execute();                                                                                                                    
   var SqlSetPersn = TRsbDataSet(SqlPersn);

   var SqlDoc = RSDCommand("select KIND.T_CODEDOCUM, PERSNIDC.T_PAPERSERIES, PERSNIDC.T_PAPERNUMBER, PERSNIDC.T_PAPERISSUEDDATE, PERSNIDC.T_PAPERISSUER " +                                   
                 "  from DPERSNIDC_DBT PERSNIDC, DPAPRKIND_DBT KIND "+                                          
                 " where PERSNIDC.t_PersonId = ? " +                                                                              
                 "   and PERSNIDC.T_ISMAIN = 'X' " +                                                                              
                 "   and PERSNIDC.t_PaperKind = KIND.t_PaperKind");

   SqlDoc.addParam( "", RSDBP_IN, PartyID );                                                                              
   SqlDoc.execute();                                                                                                                    
   var SqlSetDoc = TRsbDataSet(SqlDoc);
        
   Name1 = ""; Name2 = ""; Name3 = ""; BirthDate = ZeroDate; PlaceOfBirth = ""; CodeDocum = ""; Series = ""; Number = ""; DocumentDate = ZeroDate; PaperIssuer = "";
   if(SqlSetPersn.MoveNext())
       if(Name1 != NULL)
           Name1 = SqlSetPersn.Name1;
       end;
       if(Name2 != NULL)
           Name2 = SqlSetPersn.Name2;
       end;
       if(Name3 != NULL)
           Name3 = SqlSetPersn.Name3;
       end;
       if(BirthDate != NULL)
           BirthDate = Sql_ConvTypeDate(SqlSetPersn.Born);
       end;
       if(PlaceOfBirth != NULL)
           PlaceOfBirth = SqlSetPersn.BirsPlase;
       end;
   end;

   if(SqlSetDoc.MoveNext())
       if(CodeDocum != NULL)
           CodeDocum = SqlSetDoc.CodeDocum;
       end;
       if(Series != NULL)
         if(strlen(SqlSetDoc.PaperSeries) == 4)
           Series = substr(SqlSetDoc.PaperSeries,1,2) + " " + substr(SqlSetDoc.PaperSeries,3);
         else
           Series = SqlSetDoc.PaperSeries;
         end;
       end;
       if(Number != NULL)
           Number = SqlSetDoc.PaperNumber;
       end;
       if(DocumentDate != NULL)
           DocumentDate = SqlSetDoc.PaperIssuedDate;
       end;
       if(PaperIssuer != NULL)
           PaperIssuer = SqlSetDoc.PaperIssuer;
       end;
   end;
END;

MACRO GetAccountDate(IISMesKind:integer, SfContrID:integer, AccountDate:@date)
    AccountDate = ZeroDate;
    if((IISMesKind == MES_KIND_IISOPEN) or (IISMesKind == MES_KIND_IISOPENMOVE) or  (IISMesKind == MES_KIND_IISCORRECT))
        var Query = "select NVL(MIN(acc.t_OPEN_DATE), to_date('01.01.0001', 'dd.mm.yyyy')) OpenDate"+
                    "   FROM DACCOUNT_DBT acc, DDLCONTRACC_DBT contracc, DDLCONTR_DBT contr " +
                    "  WHERE     contracc.T_ACCOUNTID = acc.T_ACCOUNTid " +
                    "        AND contracc.T_DLCONTRID = contr.T_DLCONTRID " +
                    "        AND contr.T_SFCONTRID = ? ";

        var Sql = RSDCommand(Query);
        Sql.AddParam("", RSDBP_IN, SfContrID);
        Sql.execute();
        var SqlData = TRsbDataSet(Sql);
        if(SqlData.MoveNext())
            AccountDate = SqlData.OpenDate;
        end;
    else
        Query = "select NVL(MAX(acc.t_CLOSE_DATE), to_date('01.01.0001', 'dd.mm.yyyy')) CloseDate"+
                    " from DACCOUNT_DBT acc, DSFCONTR_DBT sfcontr, DSFSSI_DBT sfssi, DSETTACC_DBT settacc"+
                    " where ( " +
                    "          sfcontr.t_ID in ( " +
                    "                             select dlcontrmp.t_SfContrID " +
                    "                             from DDLCONTR_DBT dlcontr, DDLCONTRMP_DBT dlcontrmp " +
                    "                             where dlcontr.t_SfContrID = ? " +
                    "                               and dlcontrmp.t_DlContrID = dlcontr.t_DlContrID " +
                    "                          ) " +
                    "          OR sfcontr.T_ID = ? " +
                    "       ) " +
                    "   AND sfssi.T_OBJECTTYPE = ?"+
                    "   AND sfssi.T_OBJECTID = lpad(to_char(SFCONTR.T_ID),10,'0')"+
                    "   AND settacc.T_SETTACCID = sfssi.T_SETACCID"+
                    "   AND acc.T_ACCOUNT = settacc.T_ACCOUNT"+
                    "   AND acc.T_CODE_CURRENCY = sfssi.T_FIID";
        Sql = RSDCommand(Query);
        Sql.AddParam("", RSDBP_IN, SfContrID);
        Sql.AddParam("", RSDBP_IN, SfContrID);
        Sql.AddParam("", RSDBP_IN, OBJTYPE_SFCONTR);
        Sql.execute();
        SqlData = TRsbDataSet(Sql);
        if(SqlData.MoveNext())
            AccountDate = SqlData.CloseDate;
        end;
    end;
END;

MACRO GetContrDate(SfContrID:integer, BeginDate:@date, CloseDate:@date)
  var SfContr = TBFile("sfcontr.dbt", "R", 0);
  SfContr.rec.ID = SfContrID;
  SfContr.GetEQ();

  BeginDate = SfContr.rec.DateBegin;      
  CloseDate = SfContr.rec.DateClose;
END;

private macro GetSumIncomeArr(SfContrID:integer, BeginDate:date, CloseDate:date)
  var incomeSums = TArray();
  if (((ValType(CloseDate) == V_DATE)) and (CloseDate > date(1,1,1900)))
    var curYear:integer = 0, closeYear:integer = 0;
    DateSplit(BeginDate, null, null, curYear);
    DateSplit(CloseDate, null, null, closeYear);
    
    while(curYear <= closeYear) 
      var query = 
        "   SELECT NVL(SUM(op.T_OUTSUM), 0) AS T_SUM "
       +"     FROM DNPTXOP_DBT op "
       +"    WHERE     op.T_DOCKIND = 4607 "
       +"      AND op.T_KIND_OPERATION = 2037 "
       +"      AND op.T_SUBKIND_OPERATION = 10 "
       +"      AND (    op.T_CONTRACT = ? "
       +"            or op.T_CONTRACT in ( select dlcontrmp.T_SFCONTRID "
       +"                                    from DDLCONTR_DBT dlcontr, DDLCONTRMP_DBT dlcontrmp "
       +"                                   where dlcontr.t_SfContrID = ? "
       +"                                     and dlcontrmp.t_DlContrID = dlcontr.t_DlContrID "
       +"                                ) "
       +"          ) "
       +"      AND EXTRACT (YEAR FROM op.T_OPERDATE) = ? ";
      var cmd = DL_RSDCommand(query);
      cmd.addParam(SfContrID);
      cmd.addParam(SfContrID);
      cmd.addParam(curYear);

      var Dataset = cmd.execute();
      if(Dataset.moveNext())
        incomeSums[incomeSums.size] = DL_KeyPair(curYear, SQL_ConvTypeSum(Dataset.Sum));
      end;

      curYear = curYear + 1;
    end;
  end;
  return incomeSums;
end;

private macro GetIISMesKindPrint(IISMesKind)
   return IIF((IISMesKind == MES_KIND_IISCORRECT), MES_KIND_IISOPEN, IISMesKind);
end;

private macro GetContrNumber(contrID)
    var sfContr = TRecHandler( "sfcontr.dbt" );
    
    if(DL_FindSfContrByOrder(contrID, sfContr))
       return sfContr.rec.Number;
    else
       return "";
    end;
end;

PRIVATE CLASS (NPTXRepCalcCommon) NPTXRepCalcMesFNS( _RepDate, _Investor, _IsConsiderDEPO, _PersonID )
   VAR m_OurKPP;
   VAR m_PersonID;
   VAR m_LegalAdr = TRecHandler("adress.dbt");
   VAR m_ExistsPTAddr;
   private var m_procFirst = null;

   MACRO OurKPP()
      if((m_OurKPP == NULL) or (m_OurKPP == ""))
        var RegVal:bool = false;
        GetRegistryValue("COMMON\\НДФЛ\\ИФНС ПО МЕСТУ НАХОЖ. ДЛЯ 6-НДФЛ", V_BOOL, RegVal);

        if(RegVal)
          var party = TRecHandler("party");
          if(ПолучитьСубъекта({OurBank}, party) == 0)
            m_OurKPP = readNoteForObject(OBJTYPE_PARTY, UniID(party, OBJTYPE_PARTY), 79, m_RepDate);
            var SlashIndex = Index(m_OurKPP, "/");
            if(SlashIndex > 0)
              m_OurKPP = SubStr(m_OurKPP, 1, SlashIndex-1);
            end;
          end;
        else
          var Query = "select objcode.t_Code" +
                      "  from dobjcode_dbt objcode" +
                      " where objcode.t_ObjectType = " + string(OBJTYPE_PARTY) +
                      "   and objcode.t_CodeKind = " + string(PTCK_INN) +
                      "   and objcode.t_ObjectID = ?" +
                      "   and objcode.t_State = 0";

          var Select = DL_RSDCommand(Query);
          Select.AddParam({OurBank});
          var DataSet = Select.Execute();

          if(DataSet.MoveNext())
            SplitFullINN(DataSet.Code, NULL, m_OurKPP);
          end;
        end;
      end;

      return m_OurKPP;
   END;

   MACRO FirstPersonLastName()
      var LastName = "";
      
      if(m_PersonID > 0)
         var Query = "select persn.t_Name1 from dpersn_dbt persn where t_PersonID = ?";
         var Select = DL_RSDCommand(Query);
         Select.AddParam(m_PersonID);
         var DataSet = Select.Execute();
         if(DataSet.MoveNext())
            LastName = DataSet.Name1;
         end;
      else
         LastName = FirstPersonLastName();
      end;

      return LastName;
   END;

   MACRO FirstPersonFirstName()
      var FirstName = "";
      
      if(m_PersonID > 0)
         var Query = "select persn.t_Name2 from dpersn_dbt persn where t_PersonID = ?";
         var Select = DL_RSDCommand(Query);
         Select.AddParam(m_PersonID);
         var DataSet = Select.Execute();
         if(DataSet.MoveNext())
            FirstName = DataSet.Name2;
         end;
      else
         FirstName = FirstPersonFirstName();
      end;

      return FirstName;
   END;

   MACRO FirstPersonMiddleName()
      var MiddleName = "";
      
      if(m_PersonID > 0)
         var Query = "select persn.t_Name3 from dpersn_dbt persn where t_PersonID = ?";
         var Select = DL_RSDCommand(Query);
         Select.AddParam(m_PersonID);
         var DataSet = Select.Execute();
         if(DataSet.MoveNext())
            MiddleName = DataSet.Name3;
         end;
      else
         MiddleName = FirstPersonMiddleName();
      end;

      return MiddleName;
   END;

   MACRO GetPersnIsFirst()
     if(m_PersonID <= 0)
         var Query = "select officer.t_personid"+
                     " from dofficer_dbt officer "+
                     " join dlegalproxy_dbt legalproxy on "+
                     " legalproxy.t_partyid = officer.t_partyid and "+
                     " legalproxy.t_proxyid = officer.t_personid and "+
                     " legalproxy.t_begindate <= ? and  "+
                     " legalproxy.t_enddate >= ? "+
                     " left join dperson_dbt person on person.t_partyid = officer.t_personid  "+
                     " ORDER BY CASE WHEN person.t_oper = 1010 AND (officer.t_isfirstperson = CHR(88) OR officer.t_issecondperson = CHR (88)) THEN 0 "+
                     " WHEN person.t_oper = 1010 AND (officer.t_isfirstperson != CHR (88) AND officer.t_issecondperson != CHR (88)) THEN 2 ELSE 1 END ";
         var Select = DL_RSDCommand(Query);
         Select.AddParam(m_RepDate);
         Select.AddParam(m_RepDate);
         var DataSet = Select.Execute();
         if(DataSet.MoveNext())
           m_PersonID = DataSet.personid;
         end;
      end;
      return "";
   END;

   MACRO Procuratory()
     if((m_PersonID > 0) and (m_procFirst == null))
       var legalProxy = TBfile("legalproxy.dbt");
       if (not PersnIsFirst(m_PersonID))
         legalProxy.AddFilter("t_partyid = " + string({OurBank}) + " and T_PROXYID = " + string(m_PersonID) + " and (T_ISCLOSED <> chr(88) or T_ISCLOSED is NULL)");
         if (legalProxy.prev())
           m_procFirst = trim(string(legalProxy.rec.number));
         end;
       end;
     end;

     if(m_procFirst != null)
       return m_procFirst;
     end;

      return "";
   END;

   MACRO MailIndex():STRING
     return m_LegalAdr.rec.PostIndex;
   END;
  
   MACRO RegionCode()
     return m_LegalAdr.rec.RegionNum;
   END;
  
   /*Район*/
   MACRO District():STRING
     return m_LegalAdr.rec.Province;
   END;
  
   /*Город*/
   MACRO City():STRING
     return m_LegalAdr.rec.District;
   END;
  
   /*Населенный пункт*/
   MACRO Town():STRING
     return m_LegalAdr.rec.Place;
   END;
  
   MACRO Street():STRING
     return m_LegalAdr.rec.Street;
   END;
  
   MACRO House():STRING
     return m_LegalAdr.rec.House;
   END;
  
   MACRO Building():STRING
     return m_LegalAdr.rec.NumCorps;
   END;
  
   MACRO Appartment():STRING
     return m_LegalAdr.rec.Flat;
   END;

   private macro GetPTAddr( RecFromSelect:TRecHandler, PartyID ):BOOL
      var Query = "SELECT * FROM DADRESS_DBT WHERE T_PARTYID = " + PartyID + " AND T_TYPE = " + PTADDR_REAL;
      RecFromSelect.Clear();
      if(DL_GetRecHandler(RecFromSelect, Query))
         return true;
      end;
      return false;
   end;

   macro InitExt(_Investor, _PersonID)
      m_PersonID = _PersonID;
      m_ExistsPTAddr = GetPTAddr(m_LegalAdr, _Investor);
   end;

   initNPTXRepCalcCommon( _RepDate, _Investor, _IsConsiderDEPO, null, null, false, false ); 
   this.InitExt(_Investor, _PersonID);
END;

PRIVATE CLASS (TaxReportBase) NPTX_MFNS_Report(TemplName)
    VAR NeedBreak = false;
    VAR m_RepDate;
    VAR m_RepCalc;
    VAR IsRepCalc = false;

    MACRO CReportDataExist()
        return not NeedBreak;
    end;

    PRIVATE MACRO PrintMode()
        return Form.GetFieldValue(PNFLD_TXMFNS_PRINTMETHOD);
    end;

    PRIVATE MACRO BeginReport(NumCopy:INTEGER)
        CreateTotalBook();
    end;

    PRIVATE MACRO EndReport()
        if(not NeedBreak)
            SaveTotalBook();
        else
            m_ObjTemplateXLS.SheetDelete(1);
        end;
    end;

    PRIVATE MACRO ПечататьЗаголовок(Отступ)
        PrintOneRow( "N1_OurINN",    m_RepCalc.OurInn(), 10  );
        PrintOneRow( "N1_KPP",       m_RepCalc.OurKpp(), 9   );
    end;
    
    PRIVATE MACRO ПечатьСообщения()
        var MesNum = Form.GetFieldValue(PNFLD_TXMFNS_MES_NUM);
        var A1 = IIF((m_RepCalc.m_PersonId <=0) or PersnIsFirst(m_RepCalc.m_PersonId), "1", "2");
        
        PrintOneRow( "N1_OurNumber",     MesNum,                 15  );
        PrintOneRow( "N1_OurFnsCode",    m_RepCalc.OurFNScode(), 4   );
        PrintOneRow( "N1_OurName",       m_RepCalc.OurNameU(),   120 );
        PrintOneRow( "N1_OurPhone",      m_RepCalc.OurPhone(),   20  );
        PrintOneRow( "N1_CorrectNum",    string(Form.GetFieldValue(PNFLD_TXMFNS_CORRECT_NUM)), 3  );

        var IISMesKind = Form.GetFieldValue(PNFLD_TXMFNS_MES_KIND);
        PrintFormatString( NULL, "N1_A", GetIISMesKindPrint(IISMesKind) );

        var ShowValue:string;
        var SfContrID = Form.GetFieldValue(PNFLD_TXMFNS_CONTR_NUM, @ShowValue);
        PrintOneRow("N1_ContractNumber", ShowValue, 20);

        var AccountDateNodeNum = "N1_AccountDate";
        var dacc:date = ZERO_DATE;
        GetAccountDate(IISMesKind, SfContrID, @dacc);
        var daccStr = string(dacc:f);

        PrintOneDateRow("N1_AccountDate", daccStr);

        var SfContrDateNodeNum = "N1_ContractDate";
        var BeginDate:date = ZeroDate, CloseDate:date = ZeroDate;
        GetContrDate(SfContrID, @BeginDate, @CloseDate);
        var d:date = IIF((IISMeskind == MES_KIND_IISOPEN) or (IISMeskind == MES_KIND_IISOPENMOVE) or (IISMeskind == MES_KIND_IISCORRECT), BeginDate, CloseDate);
        var dSfContrStr:string = string(d:f);

        PrintOneDateRow("N1_ContractDate", dSfContrStr);

        var i = 0;

        if ((IISMesKind == MES_KIND_IISCLOSE) or (IISMesKind == MES_KIND_IISCLOSEMOVE))
          var yearAndSum = GetSumIncomeArr(SfContrID, BeginDate, CloseDate);

          i = 0;
          while (i < yearAndSum.size)
            PrintOneRow("N1_SumYear"+string(i+1)+"_", yearAndSum[i].Key, 4);
            PrintOneSumRow("N1_Sum"+string(i+1)+"_", yearAndSum[i].Value, 9, true);
            i = i + 1;
          end;
        end;

        var Name1 = "", Name2 = "", Name3 = "", PlaceOfBirth = "", CodeDocum = "", Series = "", Number = "", DocumentDateStr = "", PaperIssuer = "";
        var BirthDate:date;
        var DocumDate:date;
        GetPassData(PartyID, @Name1, @Name2, @Name3, @BirthDate, @PlaceOfBirth, @CodeDocum, @Series, @Number, @DocumDate, @PaperIssuer);
        DocumentDateStr = string(DocumDate:f);

        var Name1NodeNum = "N1_Surname";
        var Name2NodeNum = "N1_Name";
        var Name3NodeNum = "N1_Surname";
        i = 1;
        while(i <= 35)
            Name1NodeNum = "N1_Surname"     +   string(i);
            Name2NodeNum = "N1_Name"        +   string(i);
            Name3NodeNum = "N1_MiddleName"  +   string(i);
            
            if(i <= strlen(Name1))
                PrintFormatString( NULL, Name1NodeNum, substr(Name1, i, 1) );
            else
                PrintFormatString( NULL, Name1NodeNum, "-" );
            end;

            if(i <= strlen(Name2))
                PrintFormatString( NULL, Name2NodeNum, substr(Name2, i, 1) );
            else
                PrintFormatString( NULL, Name2NodeNum, "-" );
            end;

            if(i <= strlen(Name3))
                PrintFormatString( NULL, Name3NodeNum, substr(Name3, i, 1) );
            else
                PrintFormatString( NULL, Name3NodeNum, "-" );
            end;
            
            i = i + 1;
        end;

        PrintOneDateRow( "N1_DateBirth", BirthDate );

        PrintOneRow( "N1_ClientINN", string(ПолучитьКодСубъекта(PartyID, PTCK_INN)),  17 );
        PrintOneRow( "N1_DocumentType", CodeDocum, 2 );

        var SeriesAndNumber = Series + " " + Number;
        PrintOneRow("N1_DocumentNumber", SeriesAndNumber, 20);

        PrintOneRow("N1_DocumentDate", DocumentDateStr);

        PrintOneRow( "N1_A", A1, 1 );

        if(IsRepCalc)
            PrintOneRow( "N1_NamePerson", m_RepCalc.FirstPersonLastName(), 20 );
            PrintOneRow( "N1_MiddleNamePerson", m_RepCalc.FirstPersonFirstName(), 20 );
            PrintOneRow( "N1_Name2Person", m_RepCalc.FirstPersonMiddleName(), 20 );
        else
            PrintOneRow( "N1_NamePerson", "", 20 );
            PrintOneRow( "N1_MiddleNamePerson", "", 20 );
            PrintOneRow( "N1_Name2Person", "", 20 );
        end;

        var SignDate = string(Form.GetFieldValue(PNFLD_TXMFNS_ENDDATE):f);
        PrintOneDateRow("N1_DateOfSignature", SignDate);
    END;
    
    PRIVATE MACRO Create()
        var query, Num = 0, count = 0;
        var Investor = Form.GetFieldValue(PNFLD_TXMFNS_INVESTORCODE);

        m_RepDate = date(Form.GetFieldValue(PNFLD_TXMFNS_ENDDATE));
        m_RepCalc = NPTXRepCalcMesFNS(m_RepDate, Investor, null, Form.GetFieldValue(PNFLD_TXMFNS_FIOPERSON)); 
        SetActiveSheet("Лист1");
        ПечататьЗаголовок(0);
        ПечатьСообщения();
        CopyAllSheetInTotalBook( NULL, true, "A1:AN72", 0, "Лист1", false );
    end;

    initDL_CReportTemplate( NULL, TemplName );
    
END;

CLASS (DL_XML) NPTX_MFNS_Report_XML( data, ErrLog )
  private var m_FileNode;
  private var m_RepCalc;
  private var m_RepCalc2;
  private var m_FormData;
  private var m_ErrLog;
  private var m_IsRepCalc = false;

  PRIVATE MACRO GetFileName()
    var RefID, Num;
    var Year, Month, Day;

    if(m_FileName == NULL)
      //R_T
      m_FileName = "ON_INSCHET";

      //A_K
      m_FileName = m_FileName + "_" + m_RepCalc.FNSCodeA() + "_" + m_RepCalc.FNSCodeK();

      //O
      m_FileName = m_FileName + "_" + m_RepCalc.OurInn()+m_RepCalc2.OurKPP();

      //GGGGMMDD
      m_FileName = m_FileName + "_" + m_RepCalc.GenDate();

      m_FileName = m_FileName + "_" + m_RepCalc.MessNum();
    end;
    
    return m_FileName;
  END;

  PRIVATE MACRO СоздатьУзелФайл()
    return CreateNode("Файл",
                      "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance",
                      "ИдФайл",    GetFileName(),
                      "ВерсПрог",  "RS-BANK",
                      "ВерсФорм",  "5.03"
                     );
  END;

  PRIVATE MACRO СоздатьУзелСвНП()
    var УзелСвНП = CreateNode("СвНП",
                              "Тлф",     m_RepCalc.OurPhone());
    var УзелНПЮЛ = CreateNode("НПЮЛ",
                              "НаимОрг", m_RepCalc.OurNameU(),
                              "ИННЮЛ",   m_RepCalc.OurINN(),
                              "КПП",     m_RepCalc2.OurKPP());
    
    УзелСвНП.AppendChild(УзелНПЮЛ);

    return УзелСвНП;
  END;

  PRIVATE MACRO СоздатьУзелПодписант()
    var ПрПодп = 2;//IIF((m_FormData.PersonID <=0) or PersnIsFirst(m_FormData.PersonID), 1, 2);
    var УзелПодписант = CreateNode("Подписант",
                                   "ПрПодп", ПрПодп);
    if (m_FormData.PersonID <= 0)
       m_RepCalc2.GetPersnIsFirst();
    end;
    var УзелФИО = CreateNode("ФИО",
                             "Фамилия",  m_RepCalc2.FirstPersonLastName(),
                             "Имя",      m_RepCalc2.FirstPersonFirstName(),
                             "Отчество", m_RepCalc2.FirstPersonMiddleName());


    УзелПодписант.AppendChild(УзелФИО);
    if (ПрПодп == 2)
      var УзелСвПред = CreateNode("СвПред",
                                  "НаимДок", m_RepCalc2.Procuratory());
      УзелПодписант.AppendChild(УзелСвПред);
    end;

    return УзелПодписант;
  END;

  PRIVATE MACRO СоздатьУзелСведИИС()
    var BeginDate:date = ZeroDate, CloseDate:date = ZeroDate;
    GetContrDate(m_FormData.ContrID, @BeginDate, @CloseDate);
    var ContrDate:date = IIF((m_FormData.MessKind == MES_KIND_IISOPEN) or (m_FormData.MessKind == MES_KIND_IISOPENMOVE) or (m_FormData.MessKind == MES_KIND_IISCORRECT), BeginDate, CloseDate);

    var УзелСведИИС = CreateNode("СведИИС",
                                 "ПрИИС",     GetIISMesKindPrint(m_FormData.MessKind),
                                 "НомДог",    GetContrNumber(m_FormData.ContrID),
                                 "ДатаИИС",   string(ContrDate:f), /*DEF-51805 берем дату открытия договора*/
                                 "ДатаДог",   string(ContrDate:f));

    if ((m_FormData.MessKind == MES_KIND_IISCLOSE) or (m_FormData.MessKind == MES_KIND_IISCLOSEMOVE))
      
      var yearAndSum = GetSumIncomeArr(m_FormData.ContrID, BeginDate, CloseDate);

      var i = 0;
      while (i < yearAndSum.size)
        var УзелСумВнесДенСр = CreateNode("СумВнесДенСр",
                                          "Год", yearAndSum[i].Key,
                                          "Сумма", string(yearAndSum[i].Value));
        УзелСведИИС.AppendChild(УзелСумВнесДенСр);
        i = i + 1;
      end;
    end;

    return УзелСведИИС;
  END;

  PRIVATE MACRO СоздатьУзелВладелец()
    var BirthDate:date, DocumentDate:date;
    var Name1 = "", Name2 = "", Name3 = "", PlaceOfBirth = "", CodeDocum = "", Series = "", Number = "", PaperIssuer = "";
    var INN = string(ПолучитьКодСубъекта(m_FormData.InvestorID, PTCK_INN));
    GetPassData(m_FormData.InvestorID, @Name1, @Name2, @Name3, @BirthDate, @PlaceOfBirth, @CodeDocum, @Series, @Number, @DocumentDate, @PaperIssuer);

    var УзелВладелец = CreateNode("Владелец");

    var УзелФИО = CreateNode("ФИО",
                             "Фамилия",  Name1,
                             "Имя",      Name2,
                             "Отчество", Name3);

    var УзелУдЛичн = CreateNode("УдЛичн",
                                "КодВидДок",  CodeDocum,
                                "СерНомДок",  Series + " " + Number,
                                "ДатаВыдач",  string(DocumentDate:f));

    var УзелСведФЛ;
    if (strlen(INN) > 0)
      //В теории при передаче данных по ИНН в элементе "ИННФЛ", данные по дате рождения (элемент "ДатаРожд") и по удостоверению личности (тег "УдЛичн") могут отсутствовать в формируемом сообщении
      if ((BirthDate != NULL) and (BirthDate != ZeroDate))
        УзелСведФЛ = CreateNode("СведФЛ1",
                                "ИННФЛ",     INN,
                                "ДатаРожд",  string(BirthDate:f));
      else
        УзелСведФЛ = CreateNode("СведФЛ1",
                                "ИННФЛ",     INN);
      end;
      if ((CodeDocum != NULL) and (CodeDocum != ""))
        УзелСведФЛ.AppendChild(УзелУдЛичн);  
      end;     
    else
      УзелСведФЛ = CreateNode("СведФЛ2",
                              "ДатаРожд",  string(BirthDate:f));
      УзелСведФЛ.AppendChild(УзелУдЛичн);
    end;

    УзелВладелец.AppendChild(УзелФИО);
    УзелВладелец.AppendChild(УзелСведФЛ);

    return УзелВладелец;
  END;

  /**
   @brief Получение идентификатора субъекта по коду вида "Код ЦФТ"
   @param[in] CftID код ЦФТ
   @return идентификатор субъекта в СОФР
  */
  PRIVATE MACRO GetSofrIDFromCftID(CftID:string):integer
    var oldDialog = SetDialogFlag(0);
    var query = 
     " SELECT t_ObjectID " +
     "   FROM dobjcode_dbt " +
     "  WHERE t_ObjectType = 3 " +
     "    AND t_CodeKind = 101 " +
     "    AND t_Code = ? " +
     "    AND t_state = 0 ";
    var cmd = DL_RSDCommand(query);
    cmd.addParam(CftID);
    var ds = cmd.Execute();
    SetDialogFlag(oldDialog);
    if(ds.moveNext())
      return SQL_ConvTypeInteger(ds.ObjectID);
    end;
    return 0;
  OnError(er)
    SetDialogFlag(oldDialog);
    return 0;
  END;

  /**
   @brief Получение идентификатора ДБО по root-овому SfContrID
   @param[in] root-овый SfContrID
   @return идентификатор ДБО
  */
  PRIVATE MACRO GetDlContrID(SfContrID:integer):integer
    var oldDialog = SetDialogFlag(0);
    var query = 
     " SELECT t_DlContrID " +
     "   FROM ddlcontr_dbt " +
     "  WHERE t_SfContrID = ? ";
    var cmd = DL_RSDCommand(query);
    cmd.addParam(SfContrID);
    var ds = cmd.Execute();
    SetDialogFlag(oldDialog);
    if(ds.moveNext())
      return SQL_ConvTypeInteger(ds.DlContrID);
    end;
    return 0;
  OnError(er)
    SetDialogFlag(oldDialog);
    return 0;
  END;

  PRIVATE MACRO СоздатьУзелЛицоВедИИСПер()
    record Party("party");
    var DlContrID:integer = GetDlContrID(m_FormData.ContrID);
    var OrgID:integer = UnknownParty;
    var OrgCFTID = "", OrgName = "", OrgINN = "", OrgKPP = "", NewContrNumber = "";
    var NewContrDate:date, NewAccountDate:date;
    
    //BOSS-7198 Для хранения данных по реквизитам другого брокера и договору у другого брокера в реализации первой очереди будут использоваться примечания на ДБО 
    OrgCFTID = readNoteForObject(OBJTYPE_BROKERCONTR_DL, L_Z(DlContrID, 34), NOTEKIND_NEW_CFT_CODE);
    if((OrgCFTID != NULL) and (OrgCFTID != ""))
      OrgID = GetSofrIDFromCftID(OrgCFTID);
      SplitFullINN(ПолучитьКодСубъекта(OrgID, PTCK_INN), OrgINN, OrgKPP);

      if(ПолучитьСубъекта(OrgID, Party) == 0)
        OrgName = Party.Name;
      end;
    end;    

    NewContrNumber = readNoteForObject(OBJTYPE_BROKERCONTR_DL, L_Z(DlContrID, 34), NOTEKIND_NEW_CONTR_NUMBER);
    NewContrNumber = IIF(NewContrNumber == NULL, "", NewContrNumber);

    NewContrDate = readNoteForObject(OBJTYPE_BROKERCONTR_DL, L_Z(DlContrID, 34), NOTEKIND_NEW_CONTR_DATE);
    NewContrDate = IIF(NewContrDate == NULL, ZeroDate, NewContrDate);

    NewAccountDate = readNoteForObject(OBJTYPE_BROKERCONTR_DL, L_Z(DlContrID, 34), NOTEKIND_NEW_ACCOUNT_DATE);
    NewAccountDate = IIF(NewAccountDate == NULL, ZeroDate, NewAccountDate);

    var УзелЛицоВедИИСПер = CreateNode("ЛицоВедИИСПер",
                                       "НаимОрг",      OrgName,
                                       "ИННЮЛ",        OrgINN,
                                       "КПП",          OrgKPP,
                                       "НомДогПерев",  NewContrNumber,
                                       "ДатаОткрСч",   string(NewAccountDate:f),
                                       "ДатаДог",      string(NewContrDate:f));

    return УзелЛицоВедИИСПер;
  END;

  PRIVATE MACRO СоздатьУзелСообщОткрИИС()
    var УзелСообщОткрИИС = CreateNode("СообщОткрИИС");

    УзелСообщОткрИИС.AppendChild(СоздатьУзелСведИИС());
    УзелСообщОткрИИС.AppendChild(СоздатьУзелВладелец());
    if(m_FormData.MessKind == MES_KIND_IISCLOSEMOVE) 
      УзелСообщОткрИИС.AppendChild(СоздатьУзелЛицоВедИИСПер());
    end;

    return УзелСообщОткрИИС;
  end;

  PRIVATE MACRO СоздатьУзелДокумент()
/*ak - сквозной порядковый номер
    var Num, query, cmd, ds;
    query = "select count(1) t_count "
          + "from ddlcontrmsg_dbt dlcontrmsg "
          + "join dimgdata_dbt imgdata on imgdata.t_objecttype = 208 and "
          + "                             imgdata.t_objectid = lpad(dlcontrmsg.t_id, 34, '0') "
          + "where dlcontrmsg.t_dlcontrid in (select dlcontr.t_dlcontrid t_dlcontrid "
          + "                                 from ddlcontr_dbt dlcontr "
          + "                                 where dlcontr.t_sfcontrid = ? "
          + "                                 union all "
          + "                                 select dlcontrmp.t_dlcontrid "
          + "                                 from ddlcontrmp_dbt dlcontrmp "
          + "                                 where dlcontrmp.t_sfcontrid = ?) and "
          + "dlcontrmsg.t_kind in (5,6)";
    cmd = RSDCommand(query);
    cmd.addParam("", RSDBP_IN, m_FormData.ContrID);
    cmd.addParam("", RSDBP_IN, m_FormData.ContrID);
    cmd.Execute();
    ds = TRsbDataSet(cmd);
    ds.moveNext();
    Num = int(ds.value("t_count")) + 1;*/
    var Num, RefID;
    GetReferenceIDByType(134, 1, RefID);
    GenerateReference(RefID, Num);

    var УзелДокумент = CreateNode("Документ",
                                  "КНД",         "1114605",
                                  "ДатаДок",     string(Date():f),
                                  "НомерСооб",   Num,
                                  "НомКорр",     m_FormData.CorrectNum,
                                  "КодНО",       m_RepCalc.FNSCodeK);
                                  
    УзелДокумент.AppendChild(СоздатьУзелСвНП());
    УзелДокумент.AppendChild(СоздатьУзелПодписант());
    УзелДокумент.AppendChild(СоздатьУзелСообщОткрИИС());

    return УзелДокумент;
  END;

  PRIVATE MACRO CreateXML()
    var stat = CreateXML();
    
    m_FileNode = null;

    return stat;
  END;

  PRIVATE MACRO SaveXML()
     var XMLFileName = "";

     if(m_FileNode != null)
        AddMainNode(m_FileNode);

        var Path = GetRepFullPath();
        XMLFileName = Path + GetFileName()+".xml";

        if(ExistDir(Path) != 0)
          if(MakeDir(Path) == false)
            msgbox("Ошибка при создании директории " + Path);
          end;
        end;

        m_xml.save(XMLFileName);

        if(m_ErrLog != null)
           //m_ErrLog.LogError("Сформирован файл " + GetFileName()+".xml");
           log.addInfo("Сформирован файл " + GetFileName()+".xml");
        end;
      end;

     return XMLFileName;
  END;

  MACRO Run()
    var stat = true, XMLFileName = "";

    m_RepCalc = NPTXRepCalcCommonU(m_FormData.EndDate, m_FormData.InvestorID, m_FormData.ContrID, m_FormData.SignerID, m_FormData.MessKind, m_FormData.MessNum, @ErrLog);
    m_RepCalc2 = NPTXRepCalcMesFNS(m_FormData.EndDate, m_FormData.InvestorID, null, m_FormData.SignerID);
    m_IsRepCalc = m_RepCalc.Next();
    stat = CreateXml();

    if(stat)
        m_FileNode = СоздатьУзелФайл();
        if(m_FileNode != NULL)
            m_FileNode.AppendChild(СоздатьУзелДокумент());
        end;
        XMLFileName = SaveXML();
    end;

    return XMLFileName;
  END;

   macro Construct( data, ErrLog );
      m_FormData = data;
      m_ErrLog = ErrLog;
   end;

   this.Construct( data, ErrLog );
END;

private macro GetNptxMesFnsData() : CNptxMesFnsData
   var data = CNptxMesFnsData();

   data.EndDate = Form.GetFieldValue(PNFLD_TXMFNS_ENDDATE);
   data.InvestorID = Form.GetFieldValue(PNFLD_TXMFNS_INVESTORCODE);

   data.ContrNum = "";
   data.ContrID = Form.GetFieldValue(PNFLD_TXMFNS_CONTR_NUM, @data.ContrNum);
   data.SignerID = Form.GetFieldValue(PNFLD_TXMFNS_FIOPERSON);

   data.MessKind = Form.GetFieldValue(PNFLD_TXMFNS_MES_KIND);
   data.MessNum = Form.GetFieldValue(PNFLD_TXMFNS_MES_NUM);
   data.PersonID   = Form.GetFieldValue(PNFLD_TXMFNS_FIOPERSON);
   data.CorrectNum = Form.GetFieldValue(PNFLD_TXMFNS_CORRECT_NUM);

   return data;
end;

private class c_protocol ()
  private var Rprt = CMakeReport();

  macro Header ()
    Rprt.AddPrintCell("Сообщения, полученные в процессе работы: ", 100, 0, "l:ex_FS(b)", REP_ELEM_STR );
    Rprt.AddStr();
  End;

  private macro newLine (text:string)
    Rprt.AddPrintCell(text, 100, 0, "l:ex_FS(b)", REP_ELEM_STR);
    Rprt.AddStr();
  End;

  macro addInfo (text:string)
    newLine(text);
  End;

  macro Print ()
    var usr_filename = "NPTXMFNS_log_"+StrSubst(string(date()),".","")+StrSubst(string(time()),":","");
    usr_filename = strsubst(usr_filename, ".", "");

    var log_file = GetTxtFileName(usr_filename);

    Rprt.SetFileNameTxt(usr_filename);
    Rprt.PrintRep();

    ViewFile( log_file );
  End;

END;

macro ReportRun()
   Form   = NPTX_MES_FNS_Panel();
   stat   = Form.Run();
   ErrLog = ErrorLoger();

   log = c_protocol();
   log.Header();

   if((stat) and (Form.IsMessageBoxNo == false))
       Rep = CmakeReport();
       ErrLog.InitErrLog(Rep, /*true*/ false);
       Dl_CallInsertStat(Form.GetFieldValue(PNFLD_TXMFNS_PRINTMETHOD));
       
       if(Form.GetFieldValue(PNFLD_TXMFNS_PRINTMETHOD) == DL_OUTREPORT_XML)
           //Rep.GetCurSheet().SheetName = "Сообщения";
           ErrLog.StartErrLog();
           var XmlRep = NPTX_MFNS_Report_XML(GetNptxMesFnsData(), ErrLog);
           XmlRep.Run();
           ErrLog.FinishErrLog();

           //ErrLog.ShowErrLog(true);
           //Rep.SetZoomType( ZOOM_TYPE_A4L );// альбомная ориентация
           /*Rep.PrintWinRep();
           Rep.ShowWinRep();*/

           log.Print();
    
       else
           NPTX_MFNS_Report("Report_NPTXMFNS.xls").Run();
       end;
   end;
end;