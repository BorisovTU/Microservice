/*
  nptxwrt020.mac
  Отправка неторгового поручения по списанию в QUIK
*/

import "nontrading_orders_quik_messages.mac", "nptxwrt_func.mac", "cblogger2.mac";
import oralib, likepy, DealsInter;

private macro CheckRegvalBool(RegistryPath:string):bool
  var result = false;
  var error = 0;
 
  GetRegistryValue(RegistryPath, V_BOOL, result, error);
  if ( error > 0 )
    return result;
  end;

  return result;
end;

macro ExecuteStep( Doc, FDoc, DocKind, OperationID )
  var logger = NewLogger(c_logger.IT_LOG_TYPE, "NPTXWRT020.MAC");
  record nptxop("nptxop");
  SetBuff( nptxop, FDoc );

  //Поручение на списание может быть в рамках операции списания или операции перевода денежных средств
  if (nptxop.subkind_operation == DL_NPTXOP_WRTKIND_ENROL)
    return 0;
  end;

  //Если шагом ранее было принято решение об отказе в операции, то нет смысла отправлять поручение в квик
  var notes = c_notes(nptxop.ID);
  if (notes.GetRejectValue() != "") 
    return 0;
  end;

  var RegistryPath = "РСХБ\\ИНТЕГРАЦИЯ\\НЕТОРГОВЫЕ ПОРУЧЕНИЯ\\ВЫГРУЗКА В QUIK\\СПИСАНИЯ";
  var msgSender:MoneyOrderQuikController = MoneyOrderQuikController();
  var isNeedSendOrder:bool = true;

  isNeedSendOrder = isExchangeAccount(nptxop.account, nptxop.operDate); //процедура подходит для переводов и списаний

  if (isNeedSendOrder and not CheckRegvalBool(RegistryPath))
    MsgBox("Настройка \"Передача поручений по списаниям в QUIK\" (" + RegistryPath + ") выключена");
    isNeedSendOrder = false;
  end;

  if (not isNeedSendOrder) 
    return 0;
  end;

  if (msgSender.IsExists(nptxop.id, MoneyOrderQuikTypes.WRITEOFF))
    isNeedSendOrder = MsgBoxEx("Поручения по данной операции уже отправлялись в QUIK ранее. Выполнить повторную отправку?", MB_YES + MB_NO, IND_NO) == IND_YES;
  end;

  if (isNeedSendOrder)
    msgSender.PrepareWriteOff(nptxop.id);
    if (not msgSender.SaveAndSendByOperation(nptxop.id))
      MsgBox("Ошибка передачи неторгового поручения в QUIK");
      return 1;
    end;
  end;

  return 0;

onError(er)
  logger.ErrorClob("id = " + nptxop.ID, GetFullErrMsg(er));
  return 1;
end;