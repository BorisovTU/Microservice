/*****************************************************************************
 FILE         : sccommkt.mac
 DESCRIPTION  : Определение базовых величин для расчета единовременных комиссий
 PROGRAMMED BY: Леонов И. Е.  
 CREATION DATE: 05.02.02
*****************************************************************************/
import "sccomalg.mac", SfInter, "sp_class.mac";

var MacroError :integer = 0; 

private record rFI(fininstr);
private record avr_p( pmpaym );  

/*Кол-во дней до погашения ц/б по сделке */
private macro ДнейДоПогашения( Deal, Quont )

   if( not GetPaymentInfo( Deal, PurpBase, avr_p ) )
      MsgBox( "Отсутствуют платеж по ценным бумагам (Код сделки=", Deal.DealCode,")" );
      return 1;
   end;

   if( ПолучитьФинИн( avr_p.FIID, rFi ) )
     msgbox( "Ошибка при определении параметров ц/б|(Код сделки=", Deal.DealCode,")" );
     return 1;
   end;   
   Quont = rFi.DrawingDate - Deal.DealDate;
   if( Quont < 0 )
      Quont = 0;
   end;
   SetParm( 1, Quont );
   return 0;
end;

private macro ПолучитьДатыИсполненияЧастей(FD, FD_back, _Date1:@date, _Date2:@date)

  if(IsBUY(FD.Group)==true) /*РЕПО покупка*/
     if(ПлатежЗакрыт( FD.pm_money.rec ))
        _Date1 = FD.DateArray[DATE_DEALPAY];
     else
        _Date1 = FD.DateArray[DATE_DEALPAY_PLAN];
     end;

     if(ПлатежЗакрыт( FD_back.pm_money.rec ))
        _Date2 = FD_back.DateArray[DATE_DEALPAY];
     else
        _Date2 = FD_back.DateArray[DATE_DEALPAY_PLAN];
     end;
  else
     if(ПлатежЗакрыт( FD.pm_avoir.rec ))
        _Date1 = FD.DateArray[DATE_DEALSETAVOIRISS];
     else
        _Date1 = FD.DateArray[DATE_DEALSETAVOIRISS_PLAN];
     end;

     if(ПлатежЗакрыт( FD_back.pm_avoir.rec ))
        _Date2 = FD_back.DateArray[DATE_DEALSETAVOIRISS];
     else
        _Date2 = FD_back.DateArray[DATE_DEALSETAVOIRISS_PLAN];
     end;
  end;
end;

private macro СрокСделкиРЕПО( rDeal, Group )

   var Period = 0, FD1, FD2, Date1 = date(0,0,0), Date2 = date(0,0,0);

   if( IsREPO( Group ) )   
      FD1 = SPFirstDoc( rDeal, false );      /*по первой части*/
      FD2 = SPFirstDoc( rDeal, true, true ); /*по второй части*/

      ПолучитьДатыИсполненияЧастей(FD1, FD2, @Date1, @Date2 );
      
      if( (Date1 != date(0,0,0)) AND (Date2 != date(0,0,0)) )
         Period = Date2 - Date1;
      end;
   end;     
   return Period;   
end;


/* Получить базовую сумму для расчета единовременной комиссии
       docKind - Тип перв.документа
           Doc - Буфер перв.документа
     sfalg_adr - Алгоритм расчета
   sfcontr_adr - Договор обслуживания*/
macro CalcCommissionSum( docKind, Doc, sfalg_adr, sfcontr_adr )
   file    fAlgBase( "sfcalcal.dbt" );
   record  rAlgBase( "sfcalcal.dbt" );
   record  rAlg( "sfclusr.str" );
   record  rBaseSum( "sfbassum.str" );
   record  rContr( sfcontr );
   record  rDeal( dl_tick );
   var     dlsum = TRecHandler( "dlsum" );
   var     FIID, AlgName, Group, Quont, SummCom = $0; 
   var     Summa     = $0, /*сумма сделки*/
           AllSumma  = $0; /*сумма сделки + НКД по сделке*/
                               
   /*прочитаем переменную часть алгоритма*/
   SetBuff( rAlgBase, sfalg_adr );
   ClearRecord(fAlgBase);
   fAlgBase.FeeType     = rAlgBase.FeeType;
   fAlgBase.CommNumber  = rAlgBase.CommNumber;
   fAlgBase.Kind        = rAlgBase.Kind;
   fAlgBase.Number      = rAlgBase.Number;
   if( GetEQ(fAlgBase) == false )
      MacroError = 1;
      return $0;  /* алгоритм в файле не найден */
   end;
   SetRecordAddr( rAlg, fAlgBase );

   SetBuff( rContr, sfcontr_adr );
   SetBuff( rDeal, Doc );

   ClearRecord(rBaseSum);

   Group = SP_GetOperationGroup( rDeal); 

   /*в погашениях должны обрабатываться только комиссии с соотвествующим признаком*/
   if( (rDeal.BofficeKind == DL_RETIREMENT) AND (SfCheckInRetireComiss( rAlgBase.FeeType, rAlgBase.CommNumber ) == false) )
      rBaseSum.baseType    = rAlg.CalcMacroRet;
      rBaseSum.baseType2   = SF_BASETYPE_SUM;
      rBaseSum.baseSum     = $0;
      rBaseSum.baseSum2    = $0;
   else 
      AlgName = АлгЕдиноврКомиссии( rDeal, rAlg );       
      if( AlgName == COMM_ALG_ERROR )
         MacroError = 1;
         return;
      end;
                        
      if( AlgName == rAlg.Name ) 
         if( not СуммаСделки( rDeal, Summa, FIID, null,null,null,null,null, AllSumma ) ) 
           MacroError = 1;
           return;
         end;

         /*алгоритмы зависящие только от суммы сделки*/
         /* на них нет тарифной сетки */
         if(   
             (AlgName == COMM_ALG_X1)  OR  
             (AlgName == COMM_ALG_X2a) OR  
             (AlgName == COMM_ALG_X2b) OR  
             (AlgName == COMM_ALG_Y1)  OR  
             (AlgName == COMM_ALG_Y2a) OR  
             (AlgName == COMM_ALG_Y2b) OR  
             (AlgName == COMM_ALG_Z1)  OR  
             (AlgName == COMM_ALG_Z2a) OR  
             (AlgName == COMM_ALG_Z2b) OR  
             (AlgName == COMM_ALG_F)   OR
             (AlgName == COMM_ALG_L)
         )

            rBaseSum.baseType  = SF_BASETYPE_SUM;
            rBaseSum.baseType2 = SF_BASETYPE_SUM;
            rBaseSum.baseSum   = Summa;
            rBaseSum.baseSum2  = Summa;
            rBaseSum.FIID_baseSum  = FIID;
            rBaseSum.FIID_baseSum2 = FIID;

         /*алгоритмы зависящие от количества дней до погашения или от срока сделки РЕПО*/
         elif( (AlgName == COMM_ALG_A1)   OR 
               (AlgName == COMM_ALG_A2)   OR
               (AlgName == COMM_ALG_X3)   OR 
               (AlgName == COMM_ALG_Y3)   OR 
               (AlgName == COMM_ALG_Z3) 
         )

           /*количество дней до погашения бумаги*/ 
           if( AlgName == COMM_ALG_A1 ) 
              if( ДнейДоПогашения( rDeal, Quont ) )
                 MacroError = 1;
                 return;
              end;
           /*срок сделки РЕПО*/ 
           else 
              Quont = СрокСделкиРЕПО( rDeal, Group );
           end; 

           rBaseSum.baseType  = SF_BASETYPE_QUONT;
           rBaseSum.baseType2 = SF_BASETYPE_SUM;
           rBaseSum.baseQuont = Quont;
           if( AlgName == COMM_ALG_A2 ) 
              rBaseSum.baseSum2  = AllSumma
           else
              rBaseSum.baseSum2  = Summa;
           end;

           /* Специфика алгоритмов комиссии ММВБ - Алгоритм A1 и Алгоритм A2                          */ 
           /* Если базовое количество дней <= 200 , то процент из тарифа домножается                  */
           /*  на базовое количество дней, для учета этого мы домножаем базовую сумму на количество.  */
           /* Поэтому недостаточно поправить в тарифной сетки для этой комиссии условие, нужно еще    */ 
           /*  поправить условие здесь - в макросе.                                                   */ 
           if( (Quont <= 200) AND ((AlgName == COMM_ALG_A1) OR (AlgName == COMM_ALG_A2)) ) 
              rBaseSum.baseSum2 = money (rBaseSum.baseSum2 * Quont);
           end; 

           rBaseSum.FIID_baseSum2   = FIID;

         /*алгоритмы для импортированных комиссий (например через шлюз)*/
         elif( (AlgName == COMM_ALG_I) OR (AlgName == COMM_ALG_IC) OR (AlgName == COMM_ALG_II) OR (AlgName == COMM_ALG_M) OR (AlgName == COMM_ALG_O))
            rBaseSum.baseType       = SF_BASETYPE_SUM;
            rBaseSum.baseType2      = SF_BASETYPE_SUM;
            if( AlgName == COMM_ALG_IC )/*клиринговая комиссия*/
               if( not FindDLSUM( rDeal.BOfficeKind, rDeal.DealID, DLSUM_KIND_GT_CLRCOMM, rDeal.DealDate, dlsum ) )
                  rBaseSum.baseSum       = dlsum.rec.Sum;
                  rBaseSum.baseSum2      = dlsum.rec.Sum;
                  rBaseSum.FIID_baseSum  = dlsum.rec.Currency;
                  rBaseSum.FIID_baseSum2 = dlsum.rec.Currency;
               end;
            elif( AlgName == COMM_ALG_II )/*комиссия ИТС*/
               if( not FindDLSUM( rDeal.BOfficeKind, rDeal.DealID, DLSUM_KIND_GT_ITSCOMM, rDeal.DealDate, dlsum ) )
                  rBaseSum.baseSum       = dlsum.rec.Sum;
                  rBaseSum.baseSum2      = dlsum.rec.Sum;
                  rBaseSum.FIID_baseSum  = dlsum.rec.Currency;
                  rBaseSum.FIID_baseSum2 = dlsum.rec.Currency;
               end;
            else
            if( not FindDLSUM( rDeal.BOfficeKind, rDeal.DealID, DLSUM_KIND_IMPORT_CONTR_ONCE, rDeal.DealDate, dlsum ) )
               rBaseSum.baseSum       = dlsum.rec.Sum;
               rBaseSum.baseSum2      = dlsum.rec.Sum;
               rBaseSum.FIID_baseSum  = dlsum.rec.Currency;
               rBaseSum.FIID_baseSum2 = dlsum.rec.Currency;

               end;
               if( AlgName == COMM_ALG_I )/*биржевая*/
                 /*общая сумма импортированной = биржевая + клиринговая + ИТС*/
                  clearrecord(dlsum);
                  if( (not FindDLSUM( rDeal.BOfficeKind, rDeal.DealID, DLSUM_KIND_GT_ITSCOMM, rDeal.DealDate, dlsum )) and ( (rBaseSum.baseSum - dlsum.rec.Sum) > $0 ) )
                     rBaseSum.baseSum    = rBaseSum.baseSum - dlsum.rec.Sum;
                     rBaseSum.baseSum2   = rBaseSum.baseSum2 - dlsum.rec.Sum;
                  end;

                  clearrecord(dlsum);
                  if( (not FindDLSUM( rDeal.BOfficeKind, rDeal.DealID, DLSUM_KIND_GT_CLRCOMM, rDeal.DealDate, dlsum )) and ( (rBaseSum.baseSum - dlsum.rec.Sum) > $0 ) )
                     rBaseSum.baseSum    = rBaseSum.baseSum - dlsum.rec.Sum;
                     rBaseSum.baseSum2   = rBaseSum.baseSum2 - dlsum.rec.Sum;
                  end;
               end;
            end;
         elif(AlgName == COMM_ALG_N)
            rBaseSum.baseType       = SF_BASETYPE_SUM;
            rBaseSum.baseType2      = SF_BASETYPE_SUM;
            if( not FindDLSUM( rDeal.BOfficeKind, rDeal.DealID, DLSUM_KIND_BANK_ONCE_IMPORT, rDeal.CommDate, dlsum ) )
               rBaseSum.baseSum       = dlsum.rec.Sum;
               rBaseSum.baseSum2      = dlsum.rec.Sum;
               rBaseSum.FIID_baseSum  = dlsum.rec.Currency;
               rBaseSum.FIID_baseSum2 = dlsum.rec.Currency;
            end;
         end;
      /*комиссия регистратору*/
      elif( rAlg.Name == COMM_ALG_K )
         rBaseSum.baseType       = SF_BASETYPE_QUONT;
         rBaseSum.baseQuont      = 1;
         rBaseSum.baseType2      = SF_BASETYPE_QUONT;
         rBaseSum.baseQuont2     = 1;
      else
         rBaseSum.baseType    = rAlg.CalcMacroRet;
         rBaseSum.baseType2   = SF_BASETYPE_SUM;
         rBaseSum.baseSum     = $0;
         rBaseSum.baseSum2    = $0;
      end;
   end;
   if( InsertSumList(rBaseSum) )
     MacroError = 1;
     MsgBox("Ошибка при вставке базовой суммы");
   end;
   return;

end;
