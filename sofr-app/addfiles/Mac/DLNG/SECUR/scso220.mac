/* Операция внебиржевой продажи ц/б
   шаг постановки на баланс */

import InsCarryDoc, "sp_class.mac", "sp_car.mac";

macro ExecuteStep( doc, FDoc)

  record deal(dl_tick);
  var    FD, Dat;

  SetBuff( deal, FDoc );
  FD = SPFirstDoc( deal, IsBackExec(deal, SP_EXECUTE_STEP_SALE) );

  GetOprDate( FD.GetKindDate(DATE_DEALBEGINEXEC), dat );
  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага запрещено.");
    return 1;
  end;

  if( (deal.Flag2 == SET_CHAR) AND (bAND( FD.dl_leg.rec.BitMask, DL_LEG_PREPARE_CONTR ) == 0) ) /*признак - готовим договор*/
     if( (IsREPO(FD.Group) == false) OR (FD.IsBack == false) )
        msgbox( "Не выполнен шаг подписания договора" );
        return 1; /*договор не готов*/
     end;
  end;

  if(CheckTransfer(FD) != 0)
    return 1;
  end;

  if( not ОбновитьСтатусЧастиСделки( FD.dl_leg, DL_LEG_BALANCE ) )
    msgbox("Ошибка при изменении статуса сделки");
    return 1;
  end;

  if( not СконвертироватьСуммыСделки(FD, dat, true) )
     return 1;
  end;

  if( FD.IsOldDeal() )
     /* БУ = "Завершить" */
     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_BU, SP_OPERSTATUS_SECURDEALS_BU_FINISH) )
        msgbox( "Ошибка при установке статуса <Балансовый учет> операции" );
        return 1;
     end;
  end;

  if( FD.ExistAvance == true ) 
     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_A1, SP_OPERSTATUS_SECURDEALS_A1_PAY) ) 
        msgbox( "Ошибка при установке статуса <Оплата аванса по 1 ч> операции" );
        return 1;
     end;
  else
     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_A1, SP_OPERSTATUS_SECURDEALS_A1_FINISH) ) 
        msgbox( "Ошибка при установке статуса <Оплата аванса по 1 ч> операции" );
        return 1;
     end;
  end;

  if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_O1, SP_OPERSTATUS_SECURDEALS_O1_PAY) ) 
     msgbox( "Ошибка при установке статуса <Оплата> операции" );
     return 1;
  end;

  if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_P1, SP_OPERSTATUS_SECURDEALS_P1_SET) ) 
     msgbox( "Ошибка при установке статуса <Поставка> операции" );
     return 1;
  end;

  return 0;
end;
