/*
$Name:             ReservReg_Form.mac
$Module:           АРНУ
$Description:      Отчет "Регистры по резервам" (НУ) - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_RESERVREG_PERIOD     = 0,
      PNFLD_RESERVREG_YEAR       = 1,
      PNFLD_RESERVREG_ENDDATE    = 2,
      PNFLD_RESERVREG_BY_MONTH   = 3,
      PNFLD_RESERVREG_BY_QUARTER = 4,
      PNFLD_RESERVREG_SUBKIND    = 5,
      PNFLD_RESERVREG_AVRGROUP   = 6,
      PNFLD_RESERVREG_AVRKIND    = 7,
      PNFLD_RESERVREG_AVRCODE    = 8,
      PNFLD_RESERVREG_AVRNAME    = 9,
      PNFLD_RESERVREG_DEALKIND   = 10, 
      PNFLD_RESERVREG_EXCLUDE    = 11;

CONST H_PNFLD_SUBKIND  = 100, 
      H_PNFLD_AVRGROUP = 101,
      H_PNFLD_AVRKIND  = 102,
      H_PNFLD_DEALKIND = 103;

/*Подвид отчета*/
CONST RESERVREG_ALL    = 0,
      RESERVREG_0801   = 1, 
      RESERVREG_0802   = 2;

PRIVATE VAR RESERVREG_NAME = TArray();
RESERVREG_NAME[RESERVREG_ALL]  = "Все";
RESERVREG_NAME[RESERVREG_0801] = "0801 - акции";
RESERVREG_NAME[RESERVREG_0802] = "0802 - облигации   ";

//Виды сделок
CONST RESERVREG_DEALKIND_ALL = 0, //Все (не задано)
      RESERVREG_DEALKIND_BUY = 1, //Покупки
      RESERVREG_DEALKIND_BACKREPO = 2; //обратные репо и займы

PRIVATE VAR RESERVREG_DEALKIND_NAME = TArray();
RESERVREG_DEALKIND_NAME[RESERVREG_DEALKIND_ALL]      = "";
RESERVREG_DEALKIND_NAME[RESERVREG_DEALKIND_BUY]      = "покупки";
RESERVREG_DEALKIND_NAME[RESERVREG_DEALKIND_BACKREPO] = "обратные репо и займы";

macro GetAvoirListAddWhere(RepSubKind)

  var WhereCond = " ";

  if(RepSubKind == RESERVREG_0801)
    WhereCond = WhereCond + " AND (   RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_SHARE+", t.t_AvoirKind) > 0 "
                          + "      OR RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_DEPOSITORY_RECEIPT+", t.t_AvoirKind) > 0 )";
  elif(RepSubKind == RESERVREG_0802)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 ";
  end;

  WhereCond = WhereCond + " AND AV.t_TaxGroup not in (13, 35, 919, 943, 888, 999 ) ";       

  return WhereCond;
end;

/*Подвид регистра - 0801 - 0802*/
PRIVATE MACRO FldProc_Subkind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = RESERVREG_ALL;
     end;
     FldShowValue = RESERVREG_NAME[FldRealValue];

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    RESERVREG_NAME[RESERVREG_ALL],  RESERVREG_ALL,
                    RESERVREG_NAME[RESERVREG_0801], RESERVREG_0801,
                    RESERVREG_NAME[RESERVREG_0802], RESERVREG_0802
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = RESERVREG_ALL;
     FldShowValue = RESERVREG_NAME[FldRealValue];
  end;
  return CM_DEFAULT;
END;

/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR RetVal = CM_DEFAULT;
  VAR WhereCond = "1=1", AddTable = "", AvrKind, AvrGroup;
  VAR RepSubKind = 0;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoiriss = TRecHandler( "AVOIRISS.dbt" );

  if( not ((Cmd == DLG_KEY) AND (Key == KEY_F3)) )
    DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( FldRealValue <= 0 ) /*инициализация по умолчанию*/
        EnableFields( pThis.Data(), PNFLD_RESERVREG_EXCLUDE );
     else 
        DisableFields( pThis.Data(), PNFLD_RESERVREG_EXCLUDE );
        pThis.Data.rec.Exclude = "";
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
   
    AvrKind = pThis.GetLinkedValue(H_PNFLD_AVRKIND);
    
    AddTable  = AddTable  + "davoiriss_dbt AV";
    WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID " ;                  
    
    RepSubKind = pThis.GetLinkedValue(H_PNFLD_SUBKIND);

    WhereCond = WhereCond + GetAvoirListAddWhere(RepSubKind);

    AvrGroup = pThis.GetLinkedValue(H_PNFLD_AVRGROUP);
    if( (AvrGroup != NULL) AND (AvrGroup > 0) )
       WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrGroup;
    end;

    if( ListAvoiriss( AvrKind, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
       FldRealValue = Fininstr.rec.FIID;
       FldShowValue = Fininstr.rec.FI_Code;
       pThis.SetLinkedValue( H_PNFLD_AVRGROUP, Avoiriss.rec.TaxGroup );
    end;

  end;
  return RetVal;
END;

/*Группа налогового учета*/
PRIVATE MACRO FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;
  VAR Avoiriss, AvrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
              + "  and t_Element <> 13 and t_Element <> 35 and t_Element <> 919 and t_Element <> 943 and t_Element <> 888 and t_Element <> 999 order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Name");    
        FldRealValue = Choice.Value("t_Element");
     end;

  end;

  return CM_DEFAULT;
END;

/*Виды сделок*/
PRIVATE MACRO FldProc_Dealkind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = RESERVREG_DEALKIND_BUY;
     end;
     FldShowValue = RESERVREG_DEALKIND_NAME[FldRealValue];

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
       pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    RESERVREG_DEALKIND_NAME[RESERVREG_DEALKIND_BUY], RESERVREG_DEALKIND_BUY,
                    RESERVREG_DEALKIND_NAME[RESERVREG_DEALKIND_BACKREPO], RESERVREG_DEALKIND_BACKREPO
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = RESERVREG_DEALKIND_ALL;
     FldShowValue = RESERVREG_DEALKIND_NAME[FldRealValue];
  end;
  return CM_DEFAULT;
END;


PRIVATE MACRO FldProc_AvrKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var MenuValues   = TArray,
      ReturnValues = TArray,
      Choice, Query, AvrKindsDataSet;

  if( Cmd == DLG_INIT )
    if( FldRealValue == null )
       FldRealValue = 0;
    end;
    if( FldRealValue <= 0 )
       FldShowValue = STR_ALLVALUE;
    else
       DL_GetAvrKindName( FIKIND_AVOIRISS, FldRealValue, null, @FldShowValue );
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
    FldRealValue = 0;
    FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) 
    DL_ListAvrKinds( FIKIND_AVOIRISS, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_ReservReg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year, BegDate, RepDate;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;

     /*Получим отчетную дату - последнюю дату из отчетного периода.*/
     Period_GetInterval( PrevQuartal + 12, Year, @BegDate, @RepDate );

     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,H_PNFLD_AVRGROUP) )
        SetLinkedValue( H_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     AddFieldProc( 0, PNFLD_RESERVREG_PERIOD,      DL_PNFLD_PERIOD,      @DL_FldProc_Period, PrevQuartal + 12, 17, 6 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_RESERVREG_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_RESERVREG_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate, RepDate );
     AddFieldProc( 0, PNFLD_RESERVREG_BY_MONTH,    DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox );
     AddFieldProc( 0, PNFLD_RESERVREG_BY_QUARTER,  DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox );
     AddFieldProc( 0, PNFLD_RESERVREG_SUBKIND,     H_PNFLD_SUBKIND,      @FldProc_Subkind, RESERVREG_ALL, 16, 14 );
     AddFieldProc( 0, PNFLD_RESERVREG_AVRGROUP,    H_PNFLD_AVRGROUP,     @FldProc_AvrGroupFltr, null, 10, 5 );
     AddFieldProc( 0, PNFLD_RESERVREG_AVRKIND,     H_PNFLD_AVRKIND,      @FldProc_AvrKind, null, 10, 5 );
     AddFieldProc( 0, PNFLD_RESERVREG_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_RESERVREG_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_RESERVREG_DEALKIND,    H_PNFLD_DEALKIND,     @FldProc_Dealkind, null, 21, 22 );
     AddFieldProc( 0, PNFLD_RESERVREG_EXCLUDE,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "REGRESRV" ); 
END;

CLASS (TxReportForm) WS_TX_ReservReg_Panel(
  FormData/*: CTxReportForm*/
)
  PRIVATE VAR ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_RESERVREG_PERIOD]      = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_RESERVREG_YEAR]        = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_RESERVREG_ENDDATE]     = TXREPORTFORM_FLD_REPORTDATE;
  ReDefineFieldNum[PNFLD_RESERVREG_BY_MONTH]    = TXREPORTFORM_FLD_BY_MONTH;
  ReDefineFieldNum[PNFLD_RESERVREG_BY_QUARTER]  = TXREPORTFORM_FLD_BY_QUARTER;
  ReDefineFieldNum[PNFLD_RESERVREG_SUBKIND]     = TXREPORTFORM_WIDGET_KIND;
  ReDefineFieldNum[PNFLD_RESERVREG_AVRGROUP]    = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_RESERVREG_AVRCODE]     = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_RESERVREG_AVRNAME]     = TXREPORTFORM_FAKEFLD_AVRNAME;
  ReDefineFieldNum[PNFLD_RESERVREG_DEALKIND]    = TXREPORTFORM_WIDGET_DEALKIND;
  ReDefineFieldNum[PNFLD_RESERVREG_EXCLUDE]     = TXREPORTFORM_FLD_EXCLUDE;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;