/*
$Name:             FuturesReg_Form.mac
$Module:           АРНУ
$Description:      Отчет "Регистры срочных сделок" (НУ) - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_FUTURES_REG_PERIOD     = 0,
      PNFLD_FUTURES_REG_YEAR       = 1,
      PNFLD_FUTURES_REG_GROWTH     = 2,
      PNFLD_FUTURES_REG_BEGINDATE  = 3,
      PNFLD_FUTURES_REG_ENDDATE    = 4,
      PNFLD_FUTURES_REG_SUBKIND    = 5,
      PNFLD_FUTURES_REG_AVRGROUP   = 6,
      PNFLD_FUTURES_REG_AVRKIND    = 7,
      PNFLD_FUTURES_REG_AVRCODE    = 8,
      PNFLD_FUTURES_REG_AVRNAME    = 9,
      PNFLD_FUTURES_REG_EXCLUDE    = 10,
      PNFLD_FUTURES_REG_PRINTMETHOD= 11;

CONST H_PNFLD_SUBKIND = 100, 
      H_PNFLD_AVRGROUP = 101;


/*Подвид отчета*/
CONST FUTURES_REG_ALL    =  0, /*Все*/
      FUTURES_REG_0301   =  1, 
      FUTURES_REG_0302   =  2, 
      FUTURES_REG_0303   =  3, 
      FUTURES_REG_0304   =  4, 
      FUTURES_REG_0305   =  5, 
      FUTURES_REG_0306   =  6,
      FUTURES_REG_0310   = 10;

PRIVATE VAR FUTURES_REG_NAME = TArray();
FUTURES_REG_NAME[FUTURES_REG_ALL ] = "Все";
FUTURES_REG_NAME[FUTURES_REG_0301] = "0301 - рублевые акции";                           
FUTURES_REG_NAME[FUTURES_REG_0302] = "0302 - рублевые процентные облигации";            
FUTURES_REG_NAME[FUTURES_REG_0303] = "0303 - рублевые дисконтные облигации";            
FUTURES_REG_NAME[FUTURES_REG_0304] = "0304 - валютные акции и депозитарные расписки";   
FUTURES_REG_NAME[FUTURES_REG_0305] = "0305 - валютные процентные облигации";            
FUTURES_REG_NAME[FUTURES_REG_0306] = "0306 - валютные дисконтные облигации";            
FUTURES_REG_NAME[FUTURES_REG_0310] = "0310 - покупки и неисполненные сделки";

macro GetAvoirListAddWhere(RepSubKind)

 var WhereCond = " ";

  if(RepSubKind == FUTURES_REG_0301)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_SHARE+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI = " + NATCUR; 
  elif(RepSubKind == FUTURES_REG_0302)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI = " + NATCUR
                          + " AND  Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1";
  elif(RepSubKind == FUTURES_REG_0303)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI = " + NATCUR
                          + " AND  Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) = 1";
  elif(RepSubKind == FUTURES_REG_0304)
    WhereCond = WhereCond + " AND  ( RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_SHARE+", t.t_AvoirKind) > 0 "
                          + "      OR RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_DEPOSITORY_RECEIPT+", t.t_AvoirKind) > 0 )"
                          + " AND  t_FaceValueFI <> " + NATCUR; 
  elif(RepSubKind == FUTURES_REG_0305)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI <> " + NATCUR
                          + " AND  Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1";
  elif(RepSubKind == FUTURES_REG_0306)
    WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                          + " AND  t_FaceValueFI <> " + NATCUR
                          + " AND  Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) = 1";
  end;

  WhereCond = WhereCond + " AND AV.t_TaxGroup not in (13, 35, 919, 943) ";  

      

 return WhereCond;
end;


/*Тип отчета - 0301 - 0306, 0310*/
PRIVATE MACRO FldProc_Subkind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = FUTURES_REG_ALL;
     end;
     FldShowValue = FUTURES_REG_NAME[FldRealValue];
     
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
  
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    FUTURES_REG_NAME[FUTURES_REG_ALL ], FUTURES_REG_ALL ,
                    FUTURES_REG_NAME[FUTURES_REG_0301], FUTURES_REG_0301,
                    FUTURES_REG_NAME[FUTURES_REG_0302], FUTURES_REG_0302,
                    FUTURES_REG_NAME[FUTURES_REG_0303], FUTURES_REG_0303,
                    FUTURES_REG_NAME[FUTURES_REG_0304], FUTURES_REG_0304,
                    FUTURES_REG_NAME[FUTURES_REG_0305], FUTURES_REG_0305,
                    FUTURES_REG_NAME[FUTURES_REG_0306], FUTURES_REG_0306,
                    FUTURES_REG_NAME[FUTURES_REG_0310], FUTURES_REG_0310
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = FUTURES_REG_ALL;
     FldShowValue = FUTURES_REG_NAME[FldRealValue];
  end;
  return CM_DEFAULT;
END;


/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR RetVal = CM_DEFAULT;
  VAR WhereCond = "1=1", AddTable = "", AvrKind, AvrGroup;
  VAR RepSubKind = 0;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoiriss = TRecHandler( "AVOIRISS.dbt" );

  if( not ((Cmd == DLG_KEY) AND (Key == KEY_F3)) )
    DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( FldRealValue <= 0 ) /*инициализация по умолчанию*/
        EnableFields( pThis.Data(), PNFLD_FUTURES_REG_EXCLUDE );
     else 
        DisableFields( pThis.Data(), PNFLD_FUTURES_REG_EXCLUDE );
        pThis.Data.rec.Exclude = "";
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
   
    AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
    if( (AvrKind == NULL) OR (AvrKind < 0) )
       AvrKind = 0;
    end;

    AddTable  = AddTable  + "davoiriss_dbt AV";
    WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID ";

    RepSubKind = pThis.GetLinkedValue(H_PNFLD_SUBKIND);
    
    WhereCond = WhereCond + GetAvoirListAddWhere(RepSubKind);

    AvrGroup = pThis.GetLinkedValue(H_PNFLD_AVRGROUP);
    if( (AvrGroup != NULL) AND (AvrGroup > 0) )
       WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrGroup;
    end;

    if( ListAvoiriss( AvrKind, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
       FldRealValue = Fininstr.rec.FIID;
       FldShowValue = Fininstr.rec.FI_Code;
       pThis.SetLinkedValue( H_PNFLD_AVRGROUP, Avoiriss.rec.TaxGroup );
    end;

  end;
  return RetVal;
END;

/*Группа налогового учета*/
PRIVATE MACRO FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;
  VAR Avoiriss, AvrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
              + "  and t_Element not in (13, 35, 919, 943) order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Name");    
        FldRealValue = Choice.Value("t_Element");
     end;

  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_FuturesReg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,H_PNFLD_AVRGROUP) )
        SetLinkedValue( H_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     if( ВОЗМОЖНОСТЬ_ВЫБОРА_ВИДА_ЦБ() == false)
        DisableFields(This.Data(), PNFLD_FUTURES_REG_AVRKIND);
     end;

     AddFieldProc( 0, PNFLD_FUTURES_REG_PERIOD,      DL_PNFLD_PERIOD,      @DL_FldProc_Period, PrevQuartal + 12, 17, 8 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_FUTURES_REG_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_FUTURES_REG_GROWTH,      DL_PNFLD_GROWTH,      @DL_FldProc_Growth);
     AddFieldProc( 0, PNFLD_FUTURES_REG_BEGINDATE,   DL_PNFLD_BEGINDATE,   @DL_FldProc_BeginDate );
     AddFieldProc( 0, PNFLD_FUTURES_REG_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate );
     AddFieldProc( 0, PNFLD_FUTURES_REG_SUBKIND,     H_PNFLD_SUBKIND,      @FldProc_Subkind, FUTURES_REG_ALL, 9, 13 );
     AddFieldProc( 0, PNFLD_FUTURES_REG_AVRGROUP,    H_PNFLD_AVRGROUP,     @FldProc_AvrGroupFltr );
     AddFieldProc( 0, PNFLD_FUTURES_REG_AVRKIND,     DL_PNFLD_AVRKIND,     @DL_FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_FUTURES_REG_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_FUTURES_REG_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_FUTURES_REG_EXCLUDE,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox );
     AddFieldProc( 0, PNFLD_FUTURES_REG_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 36, 25 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "REGFUTUR" ); 
END;

CLASS (TxReportForm) WS_TX_FuturesReg_Panel(
  FormData/*: CTxReportForm*/
)
  PRIVATE VAR ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_FUTURES_REG_PERIOD]      = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_FUTURES_REG_YEAR]        = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_FUTURES_REG_GROWTH]      = TXREPORTFORM_FLD_ADDITOG;
  ReDefineFieldNum[PNFLD_FUTURES_REG_BEGINDATE]   = TXREPORTFORM_FLD_DATEBEGIN;
  ReDefineFieldNum[PNFLD_FUTURES_REG_ENDDATE]     = TXREPORTFORM_FLD_DATEEND;
  ReDefineFieldNum[PNFLD_FUTURES_REG_SUBKIND]     = TXREPORTFORM_WIDGET_KIND;
  ReDefineFieldNum[PNFLD_FUTURES_REG_AVRGROUP]    = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_FUTURES_REG_AVRKIND]     = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_FUTURES_REG_AVRCODE]     = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_FUTURES_REG_AVRNAME]     = TXREPORTFORM_FAKEFLD_AVRNAME;
  ReDefineFieldNum[PNFLD_FUTURES_REG_EXCLUDE]     = TXREPORTFORM_FLD_EXCLUDE;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx(ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue);
  END;

  initTxReportForm( FormData );
END;
