/*
$Name:        nptxskipcat_form.mac
$Module:      Ценные бумаги
$Description: 
*/
import "DlRepForm_h.mac", "nptxstbfun.mac", "dlutils.mac";

CONST PNFLD_NPTXSKIPCAT_DATE             = 0,
      PNFLD_NPTXSKIPCAT_LOADFILE         = 1,
      PNFLD_NPTXSKIPCAT_CLIENTTYPE       = 2,
      PNFLD_NPTXSKIPCAT_CHANGECATTO      = 3,
      PNFLD_NPTXSKIPCAT_PROTDIR          = 4;

PRIVATE CONST
  H_DATE            = 101,
  H_LOADFILE        = 102,
  H_CLIENTTYPE      = 103,
  H_CHANGECATTO     = 104,
  H_PROTDIR         = 105;

private var changeToArr = TArray();
changeToArr[0] = "Установить";
changeToArr[1] = "Снять";

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_File( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    var FilePath = "";
    var isTerm = false;
    if (not isStandalone())
      var choise = DL_Menu(makeArray("Терминал","Сервер"),"Где ищем файл?");
      if (choise == -1)
        return CM_DEFAULT;
      end;
      if (choise == 0)
        isTerm = true;
      end;
    end;
    if (not SelectFile ( FilePath, "*.xlsx,*.xls", "Выберите файл к обработке", 0, isTerm ))
      return CM_DEFAULT;
    end;
    FldRealValue = IIF(isTerm, "$", "") + FilePath;
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Dir( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    var folderPath = FldRealValue;
    var isTerm = false;
    if (not isStandalone())
      var choise = DL_Menu(makeArray("Терминал","Сервер"),"Где ищем каталог?");
      if (choise == -1)
        return CM_DEFAULT;
      end;
      if (choise == 0)
        isTerm = true;
      end;
    end;
    if (not SelectFolder ( folderPath, FldRealValue, "Выберите каталог сохранения", isTerm ))
      return CM_DEFAULT;
    end;
    FldRealValue = IIF(isTerm, "$", "") + folderPath;
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

MACRO DL_FldProc_ClientType( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = 1;
    end;
    FldShowValue = DL_GetNameAlg( 7355, FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    DL_ComboBox2( "select t_szNameAlg, t_iNumberAlg from dnamealg_dbt where t_itypealg = " + string(7355),
                "t_szNameAlg", "t_iNumberAlg", @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), "Выберите тип данных, содержащихся в файле" )
  end;

  return CM_DEFAULT;
END;

MACRO DL_FldProc_ChangeTo( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = 0;
    end;
    FldShowValue = changeToArr[FldRealValue];
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    DL_ListString( null, 39, 11, @FldShowValue, @FldRealValue,
                    changeToArr[0],  0,
                    changeToArr[1],  1
                  );
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Date( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;


CLASS (DL_CPanel) Sp_NPTXSKIPCAT_Panel()
  var m_Date;
  var m_LoadFile;
  var m_ClientType;
  var m_ChangeCatTo;
  var m_ProtDir;

  PRIVATE MACRO Init()
     DisableFields( this.Data(), PNFLD_NPTXSKIPCAT_DATE );

     AddFieldProc( 0, PNFLD_NPTXSKIPCAT_DATE,            H_DATE,             @FldProc_Date,            {curdate});
     AddFieldProc( 0, PNFLD_NPTXSKIPCAT_LOADFILE,        H_LOADFILE,         @FldProc_File,            "");
     AddFieldProc( 0, PNFLD_NPTXSKIPCAT_CLIENTTYPE,      H_CLIENTTYPE,       @DL_FldProc_ClientType,   1 );
     AddFieldProc( 0, PNFLD_NPTXSKIPCAT_CHANGECATTO,     H_CHANGECATTO,      @DL_FldProc_ChangeTo,     0 );
     AddFieldProc( 0, PNFLD_NPTXSKIPCAT_PROTDIR,         H_PROTDIR,          @FldProc_Dir,             GetAbsoluteTxtPath() );
  END;

  PRIVATE MACRO Check():BOOL
    if( this.GetFieldValue( PNFLD_NPTXSKIPCAT_LOADFILE ) == "" )
      msgbox("Дальнейшее выполнение операции невозможно, необходимо задать путь к файлу со списком клиентов");
      return false;
    end;
    return true;
  END;

  MACRO Run()
    var stat = Run();

    m_Date             = GetFieldValue(PNFLD_NPTXSKIPCAT_DATE);
    m_LoadFile         = GetFieldValue(PNFLD_NPTXSKIPCAT_LOADFILE);
    m_ClientType       = GetFieldValue(PNFLD_NPTXSKIPCAT_CLIENTTYPE);
    m_ChangeCatTo      = GetFieldValue(PNFLD_NPTXSKIPCAT_CHANGECATTO);
    m_ProtDir          = GetFieldValue(PNFLD_NPTXSKIPCAT_PROTDIR);

    return stat;
  END;

  initDL_CPanel( "txctrl.lbr", "nptxctsk" );
END;