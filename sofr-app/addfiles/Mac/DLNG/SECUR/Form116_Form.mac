/*
$Name:        Form116_Form.mac
$Module:      Ценные бумаги
$Description: Отчет "Сведения о ц/б, приобретенных КО (форма 116)" - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac";

// Номера полей в форме
CONST PNFLD_FORM116_REG_MONTH           = 0,
      PNFLD_FORM116_REG_YEAR            = 1,
      PNFLD_FORM116_REG_DATE            = 2,
      PNFLD_FORM116_REG_BYSC            = 3,
      PNFLD_FORM116_REG_BYVA            = 4,
      PNFLD_FORM116_REG_TO_EXEL         = 5,
      PNFLD_FORM116_REG_KLIKO           = 6;

CLASS Form116Data()
  VAR Month,
      Year,
      RepDate;

  // Значение по умолчанию
  Month   = 0;
  Year    = 0;
  RepDate = null;
END;

PRIVATE CONST
  H_PNFLD_PERIOD    = 100,
  H_PNFLD_YEAR      = 101,
  H_PNFLD_DATE      = 102,
  H_PNFLD_CHECKBOX1 = 103,
  H_PNFLD_CHECKBOX2 = 104;

VAR FormData = Form116Data();

// Период
PRIVATE MACRO FldProc_Period( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Day, Month, Year;

  if( Cmd == DLG_INIT ) // Установка значения в поле
     if( FldRealValue == null ) // инициализация по умолчанию
        DateSplit( FormData.RepDate, null, Month, null );
        FldRealValue = Month; // номер для NameAlg.dbt
     end;
     FldShowValue = DL_GetNameAlg( 2260, FldRealValue );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 2260, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );

     DateSplit( pThis.GetLinkedValue(H_PNFLD_DATE), Day, null, null );

     Year = pThis.GetLinkedValue(H_PNFLD_YEAR);

     if (FldRealValue == 12)
        FormData.RepDate = Date( Day, 1, Year + 1);
     else
       if(Day == 1)
         FormData.RepDate = Date( Day, FldRealValue + 1, Year);
       else
         FormData.RepDate = Date( Day, FldRealValue, Year);
       end;
     end;
     pThis.SetLinkedValue( H_PNFLD_PERIOD, FldRealValue );
     pThis.SetLinkedValue( H_PNFLD_DATE, FormData.RepDate );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Year( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Day, Month;

  if( Cmd == DLG_INIT ) // Инициализация по умолчанию
     if( FldRealValue == null )
        DateSplit( {curdate}, null, null, FldRealValue );
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if( (FldRealValue < 1990) OR (FldRealValue > 2990) ) // максимально исключить опечатки
        MsgBox( "Неверное значение в поле \"Год\".");
        return CM_IGNORE;
     end;

     DateSplit( pThis.GetLinkedValue(H_PNFLD_DATE), Day, null, null);
     Month =  pThis.GetLinkedValue(H_PNFLD_PERIOD);

     if( Month == 12)
       FormData.RepDate = Date( Day, 1, FldRealValue + 1);
     else
       if(Day == 1)
         FormData.RepDate = Date( Day, Month + 1, FldRealValue);
       else
         FormData.RepDate = Date( Day, Month, FldRealValue);
       end;
     end;

     pThis.SetLinkedValue( H_PNFLD_YEAR, FldRealValue );
     pThis.SetLinkedValue( H_PNFLD_DATE, FormData.RepDate );
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_Date( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var MinDate, MaxDate;

  if( Cmd == DLG_INIT )
     FldRealValue = FormData.RepDate;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) // значение поля было изменено
     FldRealValue = FldShowValue;
     FormData.RepDate = FldRealValue;
     MinDate = Date( 1, pThis.GetLinkedValue(H_PNFLD_PERIOD), pThis.GetLinkedValue(H_PNFLD_YEAR) );

     if (pThis.GetLinkedValue(H_PNFLD_PERIOD) == 12)
        MaxDate = Date( 1, 1, pThis.GetLinkedValue(H_PNFLD_YEAR) + 1 );
     else
        MaxDate = Date( 1, pThis.GetLinkedValue(H_PNFLD_PERIOD) + 1, pThis.GetLinkedValue(H_PNFLD_YEAR) );
     end;

     if( (FldRealValue < MinDate) or (FldRealValue > MaxDate) )
        MsgBox( "Дата должна входить в отчетный период.");
        return CM_IGNORE;
     end;

     pThis.SetLinkedValue( H_PNFLD_DATE, FormData.RepDate );

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
     FormData.RepDate = FldRealValue;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_CheckBox1( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) and ( Key == KEY_SPACE ) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
     elif( pThis.GetLinkedValue( H_PNFLD_CHECKBOX2 ) == "X" )
        FldShowValue = FldRealValue = "";
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO FldProc_CheckBox2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) and ( Key == KEY_SPACE ) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
     elif( pThis.GetLinkedValue( H_PNFLD_CHECKBOX1 ) == "X" )
        FldShowValue = FldRealValue = "";
     end;
  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_Form116_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, CurYear;

     DateSplit( {curdate}, null, CurMonth, CurYear );
     if (CurMonth == 1)
        FormData.Month = 12;
        FormData.Year  = CurYear - 1;
     else
        FormData.Month = CurMonth - 1;
        FormData.Year  = CurYear;
     end;

     if(FormData.RepDate == null)
       FormData.RepDate = date(1, CurMonth, CurYear);
     end;

     AddFieldProc( 0, PNFLD_FORM116_REG_MONTH,   H_PNFLD_PERIOD,    @FldProc_Period,       FormData.Month, 41, 6 );
     AddFieldProc( 0, PNFLD_FORM116_REG_YEAR,    H_PNFLD_YEAR,      @FldProc_Year,         FormData.Year );
     AddFieldProc( 0, PNFLD_FORM116_REG_DATE,    H_PNFLD_DATE,      @FldProc_Date,         FormData.RepDate );
     AddFieldProc( 0, PNFLD_FORM116_REG_BYSC,    H_PNFLD_CHECKBOX1, @FldProc_CheckBox1,    "X" );
     AddFieldProc( 0, PNFLD_FORM116_REG_BYVA,    H_PNFLD_CHECKBOX2, @FldProc_CheckBox2,    "X" );
     AddFieldProc( 0, PNFLD_FORM116_REG_TO_EXEL, DL_PNFLD_CHECKBOX, @DL_FldProc_CheckBox,  "X" );
     AddFieldProc( 0, PNFLD_FORM116_REG_KLIKO,   DL_PNFLD_CHECKBOX, @DL_FldProc_CheckBox,  "" );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "FORM116" );
END;
