/*
  nptxwrt021.mac
  Получение результата обработки неторгового поручения по списанию в QUIK
*/
import oralib, "nontrading_orders_quik_messages.mac", "nptxwrt_func.mac", "cblogger2.mac";

macro ExecuteStep( Doc, FDoc, DocKind, OperationID )
  var logger = NewLogger(c_logger.IT_LOG_TYPE, "NPTXWRT021.MAC");
  record nptxop("nptxop");
  SetBuff( nptxop, FDoc );

  var msgController:MoneyOrderQuikController = MoneyOrderQuikController();
  var quikResult = msgController.GetQUIKResult(nptxop.id, MoneyOrderQuikTypes.WRITEOFF);
  if ( (quikResult == "" ) ) //"" - не нашли такую строку в таблице => поручение даже не отправляли
    return 0;
  end;

  
  var notes = c_notes(nptxop.ID);
  var categ = c_categ(nptxop.ID);
  if (quikResult == "Успешно")
    notes.SaveQUIKResult("Успешно");
    notes.SaveUnloadedToQUIKSign();
    categ.SetUnloadedToQUIK();
    return 0;
  end;

  //Дальнейшая обработка в безынтерфейсом режиме не нужна, потому что:
  // Если ещё не получили ответ, то прерываемся
  // Если ответ уже получили и это ошибка, то тоже прерываемся
  if (GetDialogFlag() == 0) //безынтерфейсный режим
    return 1;
  end;

  var EDescription = "";
  var Action:integer;
  Array Text;
  Array Buttons;

  //обработка ошибки
  if (quikResult != "null")
    notes.SaveQUIKResult("Есть ошибки." + quikResult);
    Text(0) = "Поручение не принято QUIK с ошибкой: " + quikResult + "\nПродолжить выполнение операции?";

    Buttons(0) = " Да ";
    Buttons(1) = " Нет ";

    Action = ConfWin(Text, Buttons, 0) + 1;

    if (Action == 1)
      return 0;
    elif (Action == 2)
      return 1;
    end;
  end;

  Text(0) = "Было ли успешно обработано поручение по списанию в QUIK?";

  Buttons(0) = " Да ";
  Buttons(1) = " Нет ";
  Buttons(2) = " Отмена ";

  Action = ConfWin(Text, Buttons, 0) + 1;

  if (Action == 1)
    notes.SaveQUIKResult("Успешно");
    notes.SaveUnloadedToQUIKSign();
    categ.SetUnloadedToQUIK();
  elif (Action == 2)
    GetString(EDescription, "Введите описание ошибки"); 
    notes.SaveQUIKResult("Есть ошибки." + EDescription);
  elif (Action == 3)
    return 1; ///<ничего не делаем 
  end;

  return 0;

onError(er)
  logger.ErrorClob("id = " + nptxop.ID, GetFullErrMsg(er));
  return 1;
end;