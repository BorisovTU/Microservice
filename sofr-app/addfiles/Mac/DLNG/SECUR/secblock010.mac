/*
$Name:         secblock010.mac
$Module:       Ценные бумаги
$Description:  Операция блокировки партий.
*/
/* Используется для операций:
     2822 Блокировка партий
*/
IMPORT InsCarryDoc, "sp_class.mac", "oprmassexec.mac","sp_car2.mac", "sp_car.mac";
IMPORT "secinter.mac","commonutil.mac","globals.mac","dlquery.mac","dl_car.mac";

macro GetBlockDealCode(dealId)
  var cmd, DataSet;
  var retValue = $0;
  cmd = DL_RSDCommand(  "select * from ddl_tick_dbt "
                + " where t_dealid = ? "
               );
  cmd.AddParam(dealId);
  DataSet = cmd.Execute();
  if (DataSet.moveNext())
    return DataSet.DealCode;
  end;
  return "";
end;

macro GetBlockUnblockAmount(dlComm:TRecHandler, operSubKind)
  var cmd, DataSet;
  var retValue = $0;
  cmd = DL_RSDCommand(  "select nvl(sum(lnk.t_amount),0) as Sum from dscblcklnk_dbt lnk, ddl_comm_dbt com "
                + " where com.t_dockind = ? "
                + "   and com.t_contractid = ? "
                + "   and com.t_fiid = ? "
                + "   and com.t_commdate = ? "
                + "   and com.t_commstatus = ? "
                + "   and com.t_opersubkind = ? "
                + "   and lnk.t_dockind = com.t_dockind "
                + "   and lnk.t_docid = com.t_documentid "
               );
  cmd.AddParam(4623);
  cmd.AddParam(dlComm.rec.ContractId);
  cmd.AddParam(dlComm.rec.fiid);
  cmd.AddParam(dlComm.rec.CommDate);
  cmd.AddParam(2);
  cmd.AddParam(operSubKind);
  DataSet = cmd.Execute();
  if (DataSet.moveNext())
    retValue = DataSet.Sum;
  end;
  return retValue;
end;

MACRO ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step)
  var cmd, DataSet;
  var cmd2, DataSet2;
  var amount = $0;
  var err = 0, errStr = "";
  var dlComm = TRecHandler("dl_comm.dbt");
  var wrtSum = TRecHandler("pmwrtsum.dbt");
  SetBuff( dlComm, FDoc ); 
  cmd = DL_RSDCommand(  "select * from dscblcklnk_dbt "
                  + " where t_dockind = ? "
                  + "   and t_docid = ? "
                 ); 
  cmd.AddParam(dlComm.rec.Dockind);
  cmd.AddParam(dlComm.rec.DocumentId);
  DataSet = cmd.Execute();
  while (DataSet.moveNext())
    amount = DataSet.Amount;
    cmd2 = DL_RSDCommand(  "select * from dpmwrtsum_dbt "
                  + " where t_dealid = ? "
                  + "   and t_buy_sale = ? "
                  + "   and t_fiid = ? "
                  + "   and "+IIF(dlComm.rec.OperSubKind==1,"(t_amount - NVL(t_blockamount,0))","(NVL(t_blockamount,0))")+" > 0 "
                  + "   and t_state in (?, ?) "
                  + " order by t_sumid asc "
                 ); 
    cmd2.AddParam(DataSet.DealId);
    cmd2.AddParam(PM_WRITEOFF_SUM_BUY);
    cmd2.AddParam(dlComm.rec.fiid);
    cmd2.AddParam(PM_WRTSUM_FORM);
    cmd2.AddParam(PM_WRTSUM_SALE_BPP);
    DataSet2 = cmd2.Execute();
    while (DataSet2.moveNext() and (amount > $0))
      DataSet2.GetRecord().CopyTo(wrtSum.rec);
      if (dlComm.rec.OperSubKind==1)
        var lotAmountToBlock = wrtSum.rec.Amount - wrtSum.rec.BlockAmount;
        lotAmountToBlock = IIF(lotAmountToBlock > amount,amount,lotAmountToBlock);
        wrtSum.rec.BlockAmount = wrtSum.rec.BlockAmount + lotAmountToBlock;
        if(not DL_UpdatePmWrtSum(PM_WRTSUM_UPDTMODE_BLCKAMNT,wrtSum))
          err = 1;
        end;
        amount = amount - lotAmountToBlock;
      elif (dlComm.rec.OperSubKind==2)
        var unblockAmount = MIN(wrtSum.rec.BlockAmount, Amount); 
        wrtSum.rec.BlockAmount = wrtSum.rec.BlockAmount - unblockAmount;
        if(not DL_UpdatePmWrtSum(PM_WRTSUM_UPDTMODE_RMBLCKAMNT,wrtSum))
          err = 1;
        end;
        amount = amount - unblockAmount;
      end;
    end;
    if ((amount > $0) and (dlComm.rec.OperSubKind==1))
      MsgBox("Недостаточно имеющихся ц\б для блокировки. "+
        "По сделке №"+GetBlockDealCode(DataSet.DealId)+" имеется "+(DataSet.Amount-amount)+" шт. непроданных ц/б, при этом необходимо заблокировать "+DataSet.Amount+" шт.");
      err=1;
    end;
    if ((amount > $0) and (dlComm.rec.OperSubKind==2))
      MsgBox("Недостаточно заблокированных бумаг. "+
        "По сделке №"+GetBlockDealCode(DataSet.DealId)+" было заблокировано "+GetBlockUnblockAmount(dlComm,1)+" шт., и снято блокировок "+GetBlockUnblockAmount(dlComm,2)+" шт.");
      err=1;
    end;
  end;
  return err;
OnError(errObj)
  if (IsEqClass ("TrslError", errObj))
    errStr=errObj.Message;
    if(IsEqClass ("TDbError", errObj.err))
      errStr=errStr+":|"+errObj.err.Stat+"-"+errObj.err.Oper+"-"+errObj.err.Table+"-"+errObj.err.Message;
    end;
  elif (not errStr)
    errStr="Ошибка выполнения";
  end;
  MsgBox(errStr);
  return 1;
END;