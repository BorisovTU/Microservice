/*
$Name:        scincfun.mac
$Module:      Ценные бумаги
$Description: Общие ф-и макросов шагов операции начисления ПДД
*/
import "sp_caracc.mac", "scservop.mac";

macro ВыполнитьНачислениеПоБумаге( ScOprServDoc, Avoiriss, ID_Operation, ID_Step, Interest, Discount)

  if( not WRTChargeIncomToLots( ScOprServDoc.CommDate,
                                ScOprServDoc.EndDate,
                                Avoiriss.FIID,
                                ScOprServDoc.Division,
                                ID_Operation,
                                ID_Step,
                                -1,
                                0,
                                IIF(ScOprServDoc.Flag7 == SET_CHAR, true, false)
                              )
    )
     return 1;
  end;

  return 0;
end;

macro ВыполнитьПроводкиНачисленияПоБумаге( Doc, ScOprServDoc, Avoiriss, ID_Operation, ID_Step, Interest, Discount )
   var corr_arr = TArray(50);
   var Amount_arr = TArray(50);
   var calendId = DL_GetCalendByDynCurOper();

   macro SayError( num, errmsg )
      ScOpInsertError( DLDOC_ISSUE, ScOprServDoc, Avoiriss, num, errmsg );
      return 1;
   end;

   macro ВыполнитьПроводкуПоКорректировкам(Group)
     if((ValType(corr_arr[Group]) == V_UNDEF) or (corr_arr[Group] == 0))
       return 0 ;
     end;

     var CorrIntToEIR = corr_arr[Group];
     var AmountToEIR  = amount_arr[Group];
     var Sum          = Abs(CorrIntToEIR);
     var Amount       = AmountToEIR;
     var GroupBank;
     var FIIDDebet = NATCUR, FIIDCredit = NATCUR;
     var FIRoleDeb  = GetFIRoleByPortfolio(Group);
     var FIRoleCred = GetFIRoleByPortfolio(Group);
     var FD = SPFirstDocFI(DLDOC_ISSUE, Avoiriss.FIID, Avoiriss.FIID, Group, {OurBank}, null, null, null);
     var ground = "Корректировка % до ЭПС";
/*KD Для оснований проводок*/
      If(Group == 5)
         GroupBank = 3;
      else
         GroupBank = Group;
      end;
        Ground = "Корректировка до ЭПС по ЦБ " + GetFinName(FD.fininstr.rec) + ", портфель " + GroupBank + "-я категория, количество " + UserGetKol(Amount, FD.fininstr.rec.avoirkind) + ". Номер гос. рег." + User_GetLsin(FD.fininstr.rec.fiid);
     var DebAccNumber = "", CredAccNumber = "";
     var CatDeb, CatCred;
     
     if (CorrIntToEIR > 0)
        CatDeb = "+Корректировка, ц/б";
        CatCred = "+КорЭПС_%, ц/б";
     else
        CatDeb =  "-КорЭПС_%, ц/б";
        CatCred = "-Корректировка, ц/б";
     end;

     var dat = DL_GetBalanceDateAfterWorkDayByCalendar(ScOprServDoc.CommDate, 0, calendId);
     /*var*/ FD = SPFirstDocFI(DLDOC_ISSUE, Avoiriss.FIID, Avoiriss.FIID, Group, {OurBank}, null, null, null);

     if( not ПроводкаПоКатегориямУчета( FD, 
                                        CatDeb, CatCred,             /* КатегДеб , КатегКредит*/
                                        dat,                         /* Дата */
                                        1,                           /* Глава */
                                        FIIDDebet,                   /* Валюта */
                                        Sum,                         /* Сумма */
                                        Doc,                         /* Документ */
                                        INPCARRY,                    /* Result_Carry */
                                        " 1",                        /* Kind_Oper */
                                        "",                          /* Numb_Document */
                                        ground,
                                        null,
                                        FIRoleDeb, FIRoleCred,
                                        FIIDDebet, FIIDCredit,
                                        null, null, null, null, null, null, null,
                                        @DebAccNumber,
                                        @CredAccNumber
                                      ) 
       )
        return SayError( 1, "Ошибка при выполнении проводки" );
     end;

     ScOpReportLineData[ScOpReportLineData.Size] =  SvOpIncome( INCOME_OWN_PAPERS,     
                                                                -1,       
                                                                GetFICode( FD.fininstr.rec.FIID ),     
                                                                0,   
                                                                0,    
                                                                0,
                                                                0+Sum,
                                                                Sum,      
                                                                GetFinInstrCcy( NATCUR, true ),   
                                                                DebAccNumber,    
                                                                CredAccNumber,    
                                                                Ground   
                                                              );
     return 0;
   end;

   macro ВыполнитьДляГруппы( Group, Previous_Step, Delivered, WithoutAccept, Action )
      var cmd, Sum = $0, CatDeb = "", CatCred = "", FIRoleDeb, FIRoleCred, FD, ground, DebFIID, CredFIID, dat;
      var AccCredit, FIIDCredit, AccDebet, FIIDDebet, Глава, WrtBonus = (ScOprServDoc.Flag6 == SET_CHAR), DecisionDate = date(0,0,0), NonRefundActive = false;
      var BalanceCost = $0, DefDiff = $0, CorrIntToEIR = $0, Bpp = false,
          InterestIncome = $0, DiscountIncome = $0, Bonus = $0,
          OldInterestIncome = $0, OldDiscountIncome = $0, OldBonus = $0, Amount = $0, BonusAmount = $0, DiscountAmount = $0, InterestAmount = $0;

      var rsd, DS, FD_Deal;
      var DebAccNumber = "", CredAccNumber = "";
      var GroupBank; 
      record acc(account);
      var AccruedNKDAcc = TRecHandler("account");
      var AccruedDiscAcc = TRecHandler("account");
      var AccruedBonusAcc = TRecHandler("account");
      var Sum_tmp = $0;

      if( WithoutAccept == 1 )
         Bpp = true;
      end;

      dat = DL_GetBalanceDateAfterWorkDayByCalendar(ScOprServDoc.CommDate, 0, calendId);
      FD = SPFirstDocFI(DLDOC_ISSUE, Avoiriss.FIID, Avoiriss.FIID, Group, {OurBank}, null, null, null);
      
      cmd = RsdCommand("begin\n RSB_PMWRTOFF.WRTGetServiceFinResult(?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?,?);\n end; ");

      cmd.addParam("p_ID_Operation",  RSDBP_IN, ID_Operation  );
      cmd.addParam("p_ID_Step",       RSDBP_IN, Previous_Step );
      cmd.addParam("p_Action",        RSDBP_IN, Action        );
      cmd.addParam("p_Party",         RSDBP_IN, -1            );
      cmd.addParam("p_Contract",      RSDBP_IN, 0             );
      cmd.addParam("p_FIID",          RSDBP_IN, ALLFININSTR   );
      cmd.addParam("p_Portfolio",     RSDBP_IN, Group         );
      cmd.addParam("p_Delivered",     RSDBP_IN, Delivered     );
      cmd.addParam("p_WithoutAccept", RSDBP_IN, WithoutAccept );
      cmd.addParam("p_BalanceCost",            RSDBP_OUT, V_DOUBLE );
      cmd.addParam("p_InterestIncome",         RSDBP_OUT, V_DOUBLE );
      cmd.addParam("p_DiscountIncome",         RSDBP_OUT, V_DOUBLE );
      cmd.addParam("p_Bonus",                  RSDBP_OUT, V_DOUBLE );
      cmd.addParam("p_DefDiff",                RSDBP_OUT, V_DOUBLE );
      cmd.addParam("p_CorrIntToEIR",           RSDBP_OUT, V_DOUBLE );
      cmd.addParam("p_OldInterestIncome",      RSDBP_OUT, V_DOUBLE );
      cmd.addParam("p_OldDiscountIncome",      RSDBP_OUT, V_DOUBLE );
      cmd.addParam("p_OldBonus",               RSDBP_OUT, V_DOUBLE );
      cmd.addParam("p_Amount",                 RSDBP_OUT, V_DOUBLE );
      cmd.addParam("p_BonusAmount",            RSDBP_OUT, V_DOUBLE );
      cmd.addParam("p_DiscountAmount",         RSDBP_OUT, V_DOUBLE );
      cmd.addParam("p_InterestAmount",         RSDBP_OUT, V_DOUBLE );
      cmd.execute();

      BalanceCost          = cmd.value( 9);
      InterestIncome       = cmd.value(10);
      DiscountIncome       = cmd.value(11);
      Bonus                = cmd.value(12);
      DefDiff              = cmd.value(13);
      CorrIntToEIR         = cmd.value(14);
      OldInterestIncome    = cmd.value(15);
      OldDiscountIncome    = cmd.value(16);
      OldBonus             = cmd.value(17);
      Amount               = cmd.value(18);
      BonusAmount          = cmd.value(19);
      DiscountAmount       = cmd.value(20);
      InterestAmount       = cmd.value(21);
      DecisionDate = readNoteForObject( OBJTYPE_AVOIRISS, L_Z( Int(FD.fininstr.rec.FIID), 10 ), 108 );
      if ((ValType(DecisionDate) == V_DATE) and (DecisionDate != date(0,0,0)))
         NonRefundActive = true;
      end;

      AccDebet  = "Начисл.ПДД, ц/б";
       
      if (NonRefundActive)
        FIIDDebet = NATCUR;
      else
        FIIDDebet = FD.fininstr.rec.FaceValueFI;
      end;
      AccCredit  = "+%ДЦ/б";
      FIIDCredit = NATCUR;
     
      Глава = 1;

/*KD Для оснований проводок*/
      If(Group == 5)
         GroupBank = 3;
      else
         GroupBank = Group;
      end;
      if( Discount )
         if( DiscountIncome != $0 )
            Sum = DiscountIncome;
            if (NonRefundActive)
              Sum_tmp = Sum + OldDiscountIncome;
              SmartConvertSum (OldDiscountIncome, Money(OldDiscountIncome), DecisionDate, FD.fininstr.rec.FaceValueFI, FIIDDebet, true);
              SmartConvertSum (Sum_tmp, Money(Sum_tmp), DecisionDate, FD.fininstr.rec.FaceValueFI, FIIDDebet, true);
              if (FD.IsExistAccount("Начисл.ПДД, ц/б", null, true, GetFIRoleByPortfolio(Group, true, false), AccruedDiscAcc, null, dat, NATCUR))
                   Sum = abs(GetRestAccount(AccruedDiscAcc.rec, dat));
              end;

              Sum = Sum_tmp - Sum;
            end;

            FIRoleDeb  = GetFIRoleByPortfolio(Group, true, false, Bpp);
            FIRoleCred = GetFIRoleByPortfolio(Group, true, false);
//            ground = "Доначисление дисконтного дохода";
            Ground = "Начисление дисконтного дохода по ЦБ " + GetFinName(FD.fininstr.rec) + ", портфель " + GroupBank + "-я категория, количество " + UserGetKol(Amount, FD.fininstr.rec.avoirkind) + ". Номер гос. рег." + User_GetLsin(FD.fininstr.rec.fiid);

            DebAccNumber = CredAccNumber = "";

            if( not ПроводкаПоКатегориямУчета( FD, 
                                               AccDebet, AccCredit,         /* КатегДеб , КатегКредит*/
                                               dat,                         /* Дата */
                                               Глава,                       /* Глава */
                                               FIIDDebet,                    /* Валюта */
                                               Sum,                         /* Сумма */
                                               Doc,                         /* Документ */
                                               INPCARRY,                    /* Result_Carry */
                                               " 1",                        /* Kind_Oper */
                                               "",                          /* Numb_Document */
                                               ground,
                                               null,
                                               FIRoleDeb, FIRoleCred,
                                               FIIDDebet, FIIDCredit,
                                               null, null, null, null, null, null, null,
                                               @DebAccNumber,
                                               @CredAccNumber
                                             ) 
              )
               return SayError( 1, "Ошибка при выполнении проводки" );
            end;

            ScOpReportLineData[ScOpReportLineData.Size] =  SvOpIncome( INCOME_OWN_PAPERS,     
                                                                       -1,       
                                                                       GetFICode( FD.fininstr.rec.FIID ),     
                                                                       DiscountAmount,   
                                                                       0,    
                                                                       OldDiscountIncome,
                                                                       OldDiscountIncome+Sum,
                                                                       Sum,      
                                                                       GetFinInstrCcy( FIIDDebet, true ),   
                                                                       DebAccNumber,    
                                                                       CredAccNumber,    
                                                                       Ground   
                                                                     );

         end;
      end;

      if( Interest or WrtBonus )
         var q, d, sel;
         var coup = TRecHandler("fiwarnts.dbt");

         coup.Clear();
         
         if((Interest) and (Amount > 0))
           q = " select * from dfiwarnts_dbt where t_FIID = ? and t_IsPartial = CHR(0) and t_DrawingDate = ? ";
           sel = DL_RSDCommand(q);

           sel.AddParam(FD.fininstr.rec.FIID);
           sel.AddParam(ScOprServDoc.EndDate);

           d = sel.Execute();
           if(d.moveNext()) //Есть купон с датой погашения = дате начисления
             d.GetRecord().CopyTo(coup.rec);

             if(((InterestIncome + OldInterestIncome) != $0) and ((coup.rec.IsClosed == SET_CHAR) or (coup.rec.SPIsClosed == SET_CHAR)))
               return SayError( 1, "Купон имеет признак \"Погашен\"" );
             elif(( WithoutAccept != 1 ) and ((InterestIncome + OldInterestIncome) == $0) and (not ((coup.rec.IsClosed == SET_CHAR) or (coup.rec.SPIsClosed == SET_CHAR))))
               return SayError( 1, "Купон не \"Погашен\", но сумма начисления равна 0" );
             end;
           end;
         end;
         
         if( InterestIncome != $0 )
            Sum = InterestIncome;
            if (NonRefundActive)
              Sum_tmp = InterestIncome + OldInterestIncome;
              SmartConvertSum (OldInterestIncome, Money(OldInterestIncome), DecisionDate, FD.fininstr.rec.FaceValueFI, FIIDDebet, true);
              SmartConvertSum (Sum_tmp, Money(Sum_tmp), DecisionDate, FD.fininstr.rec.FaceValueFI, FIIDDebet, true);

              if (FD.IsExistAccount("Начисл.ПДД, ц/б", null, true, GetFIRoleByPortfolio(Group, false, true), AccruedNKDAcc, null, dat, NATCUR))
                   Sum = abs(GetRestAccount(AccruedNKDAcc.rec, dat));
              end;

              Sum = Sum_tmp - Sum;
            end;

            FIRoleDeb  = GetFIRoleByPortfolio(Group, false, true, Bpp);
            FIRoleCred = GetFIRoleByPortfolio(Group, false, true);
//            ground = "Доначисление процентного дохода";
            Ground = "Начисление купонного дохода по ЦБ " + GetFinName(FD.fininstr.rec) + ", портфель " + GroupBank + "-я категория, количество " + UserGetKol(Amount, FD.fininstr.rec.avoirkind) + ". Номер гос. рег." + User_GetLsin(FD.fininstr.rec.fiid);

            DebAccNumber = CredAccNumber = "";

            if( not ПроводкаПоКатегориямУчета( FD, 
                                               AccDebet, AccCredit,         /* КатегДеб , КатегКредит*/
                                               dat,                         /* Дата */
                                               Глава,                       /* Глава */
                                               FIIDDebet, /* Валюта */
                                               Sum,                         /* Сумма */
                                               Doc,                         /* Документ */
                                               INPCARRY,                    /* Result_Carry */
                                               " 1",                        /* Kind_Oper */
                                               "",                          /* Numb_Document */
                                               ground,
                                               null,
                                               FIRoleDeb, FIRoleCred,
                                               FIIDDebet, FIIDCredit,
                                               null, null, null, null, null, null, null,
                                               @DebAccNumber,
                                               @CredAccNumber
                                             )
              )
               return SayError( 1, "Ошибка при выполнении проводки" );
            end;

            ScOpReportLineData[ScOpReportLineData.Size] =  SvOpIncome( INCOME_OWN_PAPERS,     
                                                                       -1,       
                                                                       GetFICode( FD.fininstr.rec.FIID ),     
                                                                       InterestAmount,   
                                                                       0,    
                                                                       OldInterestIncome,
                                                                       OldInterestIncome+Sum,
                                                                       Sum,      
                                                                       GetFinInstrCcy( FIIDDebet, true ),   
                                                                       DebAccNumber,    
                                                                       CredAccNumber,    
                                                                       Ground   
                                                                     );

         end;
         
         if( Bonus != $0 )
            FIIDDebet = NATCUR;          
            FIIDCredit = FD.fininstr.rec.FaceValueFI;
            Sum = Bonus;

            if (NonRefundActive)
              FIIDCredit = NATCUR;
              Sum_tmp = Sum + OldBonus;
              SmartConvertSum (OldBonus, Money(OldBonus), DecisionDate, FD.fininstr.rec.FaceValueFI, FIIDCredit, true);
              SmartConvertSum (Sum_tmp, Money(Sum_tmp), DecisionDate, FD.fininstr.rec.FaceValueFI, FIIDCredit, true);
              if (FD.IsExistAccount("Премия, ц/б", null, true, GetFIRoleByPortfolioBonus(Group, Bpp), AccruedBonusAcc, null, dat, NATCUR))
                   Sum = abs(GetRestAccount(AccruedBonusAcc.rec, dat));
              end;

              Sum = (Sum_tmp + Sum) - (OldBonus + Sum);
            end;

            FIRoleCred = GetFIRoleByPortfolioBonus(Group, Bpp);
            FIRoleDeb = GetFIRoleByPortfolio(Group, false, false, false, false, true);
//            ground = "Доначисление премии";
            Ground = "Списание премии по ЦБ " + GetFinName(FD.fininstr.rec) + ", портфель " + GroupBank + "-я категория, количество " + UserGetKol(Amount, FD.fininstr.rec.avoirkind) + ". Номер гос. рег." + User_GetLsin(FD.fininstr.rec.fiid);

            CatDeb = "-Премия, ц/б";
            CatCred = "Премия, ц/б";

            DebAccNumber = CredAccNumber = "";
            /*BPV вместо суммы и валюты дебета передаем сумму и валюту кредита, иначе ядро конверитрует из р в валюту и плодит расхождение*/
            if( not ПроводкаПоКатегориямУчета( FD,
                                               CatDeb, CatCred,             /* КатегДеб, КатегКредит*/
                                               dat,                         /* Дата */
                                               1,                           /* Глава */
                                               null, /*FIIDDebet,*/ /* Валюта */
                                               null, /*Sum,*/                         /* Сумма */
                                               Doc,                         /* Документ */
                                               INPCARRY,                    /* Result_Carry */
                                               " 1",                        /* Kind_Oper */
                                               "",                          /* Numb_Document */
                                               ground,
                                               null,
                                               FIRoleDeb, FIRoleCred,
                                               FIIDDebet, FIIDCredit,
                                               /*null, null, */ FIIDCredit, Sum, null, null, null, null, null,
                                               @DebAccNumber,
                                               @CredAccNumber
                                             )
              )
               return SayError( 1, "Ошибка при выполнении проводки" );
            end;

            ScOpReportLineData[ScOpReportLineData.Size] =  SvOpIncome( INCOME_OWN_PAPERS,     
                                                                       -1,       
                                                                       GetFICode( FD.fininstr.rec.FIID ),     
                                                                       BonusAmount,
                                                                       0,    
                                                                       OldBonus,
                                                                       OldBonus+Sum,
                                                                       Sum,      
                                                                       GetFinInstrCcy( FIIDCredit, true ),   
                                                                       DebAccNumber,    
                                                                       CredAccNumber,    
                                                                       Ground   
                                                                     );
         end;
      end;

      if( DefDiff != $0 )
            Sum = Abs(DefDiff);
            FIIDDebet = FIIDCredit = NATCUR;
            FIRoleDeb  = GetFIRoleByPortfolio(Group);
            FIRoleCred = GetFIRoleByPortfolio(Group);
//            ground = "Доначисление отсроченной разницы";
            Ground = "Доначисление отсроченной разницы по ЦБ " + GetFinName(FD.fininstr.rec) + ", портфель " + GroupBank + "-я категория, количество " + UserGetKol(Amount, FD.fininstr.rec.avoirkind) + ". Номер гос. рег." + User_GetLsin(FD.fininstr.rec.fiid);

            if (DefDiff > 0)
               CatDeb = "-КорЭПС_%, ц/б";
               CatCred = "+Маржа, ц/б";
            else
               CatDeb = "-Маржа, ц/б";
               CatCred = "+КорЭПС_%, ц/б";
            end;
            DebAccNumber = CredAccNumber = "";

            if( not ПроводкаПоКатегориямУчета( FD, 
                                               CatDeb, CatCred,             /* КатегДеб , КатегКредит*/
                                               dat,                         /* Дата */
                                               Глава,                       /* Глава */
                                               FIIDDebet,                   /* Валюта */
                                               Sum,                         /* Сумма */
                                               Doc,                         /* Документ */
                                               INPCARRY,                    /* Result_Carry */
                                               " 1",                        /* Kind_Oper */
                                               "",                          /* Numb_Document */
                                               ground,
                                               null,
                                               FIRoleDeb, FIRoleCred,
                                               FIIDDebet, FIIDCredit,
                                               null, null, null, null, null, null, null,
                                               @DebAccNumber,
                                               @CredAccNumber
                                             ) 
              )
               return SayError( 1, "Ошибка при выполнении проводки" );
            end;

            ScOpReportLineData[ScOpReportLineData.Size] =  SvOpIncome( INCOME_OWN_PAPERS,     
                                                                       -1,       
                                                                       GetFICode( FD.fininstr.rec.FIID ),     
                                                                       0,   
                                                                       0,    
                                                                       0,
                                                                       0+Sum,
                                                                       Sum,      
                                                                       GetFinInstrCcy( FD.fininstr.rec.FaceValueFI, true ),   
                                                                       DebAccNumber,    
                                                                       CredAccNumber,    
                                                                       Ground   
                                                                     );

      end;
      if( CorrIntToEIR != $0 )
        if(ValType(corr_arr[Group]) == V_UNDEF)
          corr_arr[Group] = CorrIntToEIR;
          amount_arr[Group] = amount;
            else
          corr_arr[Group] = corr_arr[Group] + CorrIntToEIR;
          amount_arr[Group] = amount_arr[Group] + Amount;
            end;
      end;

      return 0;
   end;

   ScOpReportLineData.Size = 0;
   corr_arr.size = 0;
   amount_arr.size = 0;

   var Previous_Step = 0, Action;
   var Query = RSDCommand(" Select t_Previous_Step From doprstep_dbt Where t_ID_Operation = ? " + 
                                                          " and t_ID_Step = ? ");
   Query.addParam( "", RSDBP_IN, ID_Operation );
   Query.addParam( "", RSDBP_IN, ID_Step );
   Query.execute();
   var DataSet = TRsbDataSet(Query);

   if( DataSet.MoveNext() )
      Previous_Step = DataSet.Previous_Step;
   else
      return 1;
   end;

   if( Interest or Discount or (ScOprServDoc.Flag6 == SET_CHAR) )
      Action = PM_WRTSUM_UPDTMODE_INCPDD;
   else
      return SayError( 1, "Неверные параметры" );
   end;

   if ((ВыполнитьДляГруппы(KINDPORT_TRADE,  Previous_Step, 0, 1, Action) != 0) or
       (ВыполнитьДляГруппы(KINDPORT_TRADE,  Previous_Step, 1, 0, Action) != 0) or
       (ВыполнитьПроводкуПоКорректировкам(KINDPORT_TRADE) != 0) or
       (ВыполнитьДляГруппы(KINDPORT_SALE,   Previous_Step, 0, 1, Action) != 0) or
       (ВыполнитьДляГруппы(KINDPORT_SALE,   Previous_Step, 1, 0, Action) != 0) or
       (ВыполнитьПроводкуПоКорректировкам(KINDPORT_SALE) != 0) or
       (ВыполнитьДляГруппы(KINDPORT_RETIRE, Previous_Step, 0, 1, Action) != 0) or
       (ВыполнитьДляГруппы(KINDPORT_RETIRE, Previous_Step, 1, 0, Action) != 0) or
       (ВыполнитьПроводкуПоКорректировкам(KINDPORT_RETIRE) != 0) 
      )
      return 1;
   end;

   return 0;
end;

macro ОЭБ_НачислитьКомиссииЦБ(FIID:integer,
                              OperDate:date,
                              CalcDate:date,
                              ID_Operation:integer,
                              ID_Step:integer,
                              FullWrt:bool,
                              PrevComSum:@money,
                              PrevNDSSum:@money,
                              AddComSum:@money,
                              AddNDSSum:@money,
                              ErrStr:@string
                             )
  var query, cmd, DataSet;
  var fin = TRecHandler("fininstr.dbt");
  var Dначисл, Dпг;
  var NewComSum = $0, NewNDSSum = $0;
  var Month1 = 0, Month2 = 0;
  var Doffer = ZeroDate;

  Dначисл = CalcDate;
  ErrStr = "";
  PrevComSum = $0;
  PrevNDSSum = $0;
  AddComSum  = $0;
  AddNDSSum  = $0;
  
  if(ПолучитьФинИн(FIID, fin) != 0)
     ErrStr = "Не найден финансовый инструмент с идентификатором FIID = " + FIID;
     return 1; 
  end;

  cmd = RSDCommand("call RSB_PMWRTOFF.RSI_СalcOwnFiComTMP(?,?,?,?)");
  cmd.addParam( "", RSDBP_IN, FIID );
  cmd.addParam( "", RSDBP_IN, OperDate );
  cmd.addParam( "", RSDBP_IN, CalcDate );
  cmd.addParam( "", RSDBP_IN, IIF(FullWrt==true, 1, 0) );
  cmd.execute();

  
  query =   " select dlc.t_ID, "
          + "        NVL((select SUM(s.t_Sum) "
          + "               from ddlsum_dbt s "
          + "              where s.t_DocKind = " + DL_SECURITYCOM
          + "                and s.t_DocID = dlc.t_ID "
          + "            ), 0) as PrevComSumRub, "
          + "        NVL((select SUM(s.t_NDS) "
          + "               from ddlsum_dbt s "
          + "              where s.t_DocKind = " + DL_SECURITYCOM
          + "                and s.t_DocID = dlc.t_ID "
          + "            ), 0) as PrevNDSSumRub, "
          + "        NVL((select SUM(s.t_Sum) "
          + "               from ddlsum_tmp s "
          + "              where s.t_DocKind = " + DL_SECURITYCOM
          + "                and s.t_DocID = dlc.t_ID "
          + "            ), 0) as AddComSumRub, "
          + "        NVL((select SUM(s.t_NDS) "
          + "               from ddlsum_tmp s "
          + "              where s.t_DocKind = " + DL_SECURITYCOM
          + "                and s.t_DocID = dlc.t_ID "
          + "            ), 0) as AddNDSSumRub "
          + "   from ddlcomis_dbt dlc, dsfcomiss_dbt cm "
          + "  where dlc.t_DocKind = " + DLDOC_ISSUE
          + "    and dlc.t_DocID = ? "
          + "    and dlc.t_StartAmortDate <= ? "
          + "    and dlc.t_StartAmortDate <> " + GetSQLDate(date(0,0,0))
          + "    and cm.t_FeeType = dlc.t_FeeType "
          + "    and cm.t_Number = dlc.t_ComNumber ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(FIID);
  cmd.AddParam(CalcDate);

  DataSet = cmd.Execute();

  while(DataSet.moveNext())
      if((ID_Operation != 0) or (ID_Step != 0))
        if((ID_Operation > 0) and (ID_Step == -1)) //СОБУ
        if( not БУ_СохранитьСуммуКомиссии(DL_SECURITYCOM, DataSet.ID, DLSUM_KIND_OWNAVR_AMORTCOM, CalcDate, money(DataSet.AddComSumRub), money(DataSet.AddNDSSumRub), NATCUR ) )
           ErrStr = "Ошибка при сохранении суммы комиссии по ц/б";
             return 1;
          end;
        else
        if( not СохранитьСуммуКомиссии(DL_SECURITYCOM, DataSet.ID, DLSUM_KIND_OWNAVR_AMORTCOM, CalcDate, money(DataSet.AddComSumRub), money(DataSet.AddNDSSumRub), NATCUR) )
          ErrStr = "Ошибка при сохранении суммы комиссии по ц/б";
            return 1;
          end;
        end;
      end;

    PrevComSum = PrevComSum + money(DataSet.PrevComSumRub);
    PrevNDSSum = PrevNDSSum + money(DataSet.PrevNDSSumRub);
                           
    AddComSum  = AddComSum + money(DataSet.AddComSumRub);
    AddNDSSum  = AddNDSSum + money(DataSet.AddNDSSumRub);
  end;

  return 0;
end;
