/*
$Name:        srvrepopsc.mac
$Module:      Ценные бумаги
$Description: Макрос скролинга операций отправки отчета брокера 
*/
import DealsInter, SPInter, PTInter, globals;
import RsbDataSet, "dlutils.mac", "scsrvrepfun.mac";

/* Уже заполненные структуры, все изменения отразятся в базах */
PRIVATE VAR Документ         = TRecHandler( "scsrvrep" );
PRIVATE VAR СтарыйДокумент   = TRecHandler( "scsrvrep" ); /* заполняется при редактировании */

/* Макрофункция инициализации нового актива 
   необходимо заполнить структуру "Документ"
   возвращаемое значение
   0 - ошибок не было
   1 - была ошибка
*/
PRIVATE MACRO Новый_Документ()
  /*bpv*/
  Документ.rec.pdfformat = "X";
  Документ.rec.excelformat = "X";
  Документ.rec.autosign = "";
  Документ.rec.AutoSendKind = SCSRVREP_AUTOSENDKIND_NOSEND;
  Документ.rec.BYOUTEXCHANGE = "X";
  Документ.rec.BYCM = "X";//05.02.2020 RAS:504760
  Документ.rec.ExcludeUK = "X";
  return 0;
END;

private macro Проверить_Документ( Режим:integer )
  if(Режим == 3) //редактирование операции

    //здесь должны быть проверки при редактировании

  elif(Режим == 1) //удаление
    
    //здесь должны быть проверки при удалении

  end;

  return 0;
end;

/* Установка подсказки для скролингов из макроса */
private MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string

  //пример
  //return "/*+FIRST_ROWS LEADING(t) INDEX(t dscsrvrep_dbt_idx0)*/";

  return DefaultHint;

END;

private macro СконвертироватьМассивОперацийВСписок(elem)
  if (ValType(elem) != V_UNDEF)
    return "Операция " + elem.rec.Code + " за дату " + elem.date;
  end;
  return NULL;
end;

private macro СгенерироватьНомерОперации():String
  var RefID;
  const REFOBJ_SVOP_SRVREP = 21;
  var RefNum;
  if( GetReferenceIDByType( OBJTYPE_SECURSERVOP, REFOBJ_SVOP_SRVREP, RefID) )
    return "";
  end;
  if (GenerateReference( RefID, RefNum ) )
      return "";
  end;                                                                                                
  return RefNum;
end;

private macro ПолучитьПроблемнуюПоКоду(Code)
  var problemOperArr = TArray();
  var problemOperCmd = DL_RSDCommand("select * from DSCSRVREP_DBT where T_CODE = ?");
  problemOperCmd.addParam( Code );
  var problemCount = problemOperCmd.GetCount();
  if (problemCount == 0)
    RunError("Не найдено операций отправки ОБ с кодом " + Code);
  end;
  var problemOperDs = problemOperCmd.Execute();
  while (problemOperDs.MoveNext())
    var problemRecHandl = TRecHandler("scsrvrep.dbt");
    problemOperDs.GetRecord().CopyTo( problemRecHandl.rec);
    problemOperArr[problemOperArr.size] = problemRecHandl;
  end;
  var choosenProblem = NULL;
  if (problemOperArr.size > 1)
    var choice = DL_Menu(map(problemOperArr,@СконвертироватьМассивОперацийВСписок),"Выберите операцию");
    if (choice < 0)
      RunError("Отменено пользователем");
    end;
    choosenProblem = problemOperArr[choice];
  elif (problemOperArr.size == 1)
    choosenProblem = problemOperArr[0];
  end;
  return choosenProblem;
end;

private MACRO loc_PrcPath( p_path )
  var l_str, l_p, l_path = "";

  p_path = trim( p_path );

  if( substr( p_path, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
    p_path = substr( p_path, 3 );
 
    l_str  = getCWD(false);
    l_p    = strbrk( l_str, "\\" );
    while( l_p != 0 )
      l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
      l_str  = substr( l_str, l_p + 1 );
      l_p    = strbrk( l_str, "\\" );
    end;
    return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );
  elif( substr( p_path, 1, 1 ) == "." ) /* ссылка на текущий каталог */
    return ( getCWD + substr( p_path, 2 ) );
  end;

  return p_path;
END;

private macro loc_GetTxtPathFromReg()
  const txtRegPath = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";
  var Path = "";
  var err = 0;

  GetRegistryValue(txtRegPath, V_STRING, Path, err);
  if(err != 0) 
    RunError("Ошибка настройки", CustomError("Ошибка при получении значения настройки \"" + txtRegPath + "\""));
  end;
  return Path;
END;

private macro Loc_GetAbsoluteTxtPath()
  return loc_PrcPath(loc_GetTxtPathFromReg());
end;

private macro СоздатьОперациюДоотправки()
  var operCode = "";
  var ErrCode, ErrText;
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  RslDefCon.BeginTrans();
  if (not GetString(operCode, "Введите код незавершенной операции"))
    RunError("Отменено пользователем");
  end;
  var choosenProblem = ПолучитьПроблемнуюПоКоду(operCode);

  if ((choosenProblem.rec.RepStatus == 0) and (choosenProblem.rec.SignStatus == 0) and (choosenProblem.rec.SendStatus == 0))
    RunError("Новая операция", CustomError("По выбранной операции не осуществлялось действий. Создание операции доотправки не поддерживается."));
  end;

  var cntrCountCmd = DL_RSDCommand("select * from DSCSRVREPCNTR_DBT where T_SERVDOCID = ?");
  cntrCountCmd.addParam(choosenProblem.rec.ID);

  var choosenProblemID = choosenProblem.rec.ID;

  if (cntrCountCmd.GetCount() == 0)
    RunError("Операция не поддерживается", CustomError("Список договоров по операции пуст. Предположительно это старая операция. Доотправка по старым операция не поддерживается."));
  end;

  var newOperNumb = operCode+"-1";
  if (not GetString(newOperNumb, "Номер новой операции"))
    RunError("Отменено пользователем");
  end;
  var newDate = {curdate};
  if (not GetDate(newDate, "Введите новую дату отчета"))
    RunError("Отменено пользователем");
  end;
  choosenProblem.rec.Date = newDate;
  choosenProblem.rec.ID = 0;
  choosenProblem.rec.Code = newOperNumb;
  choosenProblem.rec.RepStatus = 0;
  choosenProblem.rec.SignStatus = 0;
  choosenProblem.rec.SendStatus = 0;
  var srvRepFile = TBFile("scsrvrep.dbt","W");

  copy( srvRepFile, choosenProblem );
  if (GenPropID(srvrepFile.rec, "WasManClntEdit") >= 0)
    srvrepFile.rec.WasManClntEdit = SET_CHAR;
  end;
  if (not srvRepFile.Insert())
    ErrCode = Status(ErrText);
    RunError("Ошибка вставки", CustomError(GetErrMsg() + "; Код: "+ ErrCode + "; Ошибка: " + ErrText ));
  end;

  BegAction(2000, "Добавление незавершенных договоров", false);
  var insertSql = 
   " INSERT INTO DSCSRVREPCNTR_DBT (T_SERVDOCID, "
  +"                                T_DLCONTRID, "
  +"                                T_DLCONTRNUMBER, "
  +"                                T_CLIENTNAME, "
  +"                                T_CLIENTCODE, "
  +"                                T_EKK, "
  +"                                T_GROUPNUMBER, "
  +"                                T_ROWNUM) "
  +"    SELECT ?, "
  +"           cntr.T_DLCONTRID, "
  +"           cntr.T_DLCONTRNUMBER, "
  +"           cntr.T_CLIENTNAME, "
  +"           cntr.T_CLIENTCODE, "
  +"           cntr.T_EKK, "
  +"           cntr.T_GROUPNUMBER, "
  +"           cntr.T_ROWNUM "
  +"      FROM DSCSRVREPCNTR_DBT cntr, ddlcontr_dbt dlc, DSCSRVREP_DBT rep "
  +"     WHERE     CNTR.T_SERVDOCID = ? "
  +"           AND dlc.t_DlContrID = CNTR.T_DLCONTRID "
  +"           AND rep.t_id = CNTR.T_SERVDOCID "
  +"           AND NOT EXISTS "
  +"                      (SELECT 1 "
  +"                         FROM DSCSRVREPOBJ_DBT obj "
  +"                        WHERE obj.t_Format = "
  +"                                 CASE "
  +"                                    WHEN (rep.T_PDFFORMAT IS NOT NULL AND rep.T_PDFFORMAT = CHR (88)) "
  +"                                    THEN " + SC_SRVREPFORMAT_PDF
  +"                                    ELSE " + SC_SRVREPFORMAT_EXCEL_XLSX
  +"                                 END "
  +"                              AND obj.T_SERVDOCID = CNTR.T_SERVDOCID "
  +"                              AND obj.T_CONTRACTID = dlc.t_SfContrID "
  +"                              AND obj.T_ISSEND = CHR (88)) ";
  execSQL(insertSql, makeArray(SQLParam("srvdoc1", srvRepFile.rec.ID), SQLParam("srvdoc2", choosenProblemID)), true);
  EndAction();

  cntrCountCmd = DL_RSDCommand("select * from DSCSRVREPCNTR_DBT where T_SERVDOCID = ?");
  cntrCountCmd.addParam(srvRepFile.rec.ID);

  if (cntrCountCmd.GetCount() == 0)
    if(RslDefCon.IsInTrans)
      RslDefCon.RollbackTrans();
    end;
    MsgBox("В операции "+string(operCode)+" нет неотправленных отчетов.|Новая операция не вставлена.");
  else
    RslDefCon.CommitTrans();
    MsgBox("Успешно вставлена операция " + newOperNumb);
  end;
OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  MsgBox(GetFullErrorMessage(er));
end;

private macro ПроверитьЗавершение()
  var operCode = "";
  if (not GetString(operCode, "Введите код операции"))
    RunError("Отменено пользователем");
  end;
  var choosenProblem = ПолучитьПроблемнуюПоКоду(operCode);

  var notSendedCmd = DL_RSDCommand(
   "    SELECT 1 "
  +"      FROM DSCSRVREPCNTR_DBT cntr, ddlcontr_dbt dlc, DSCSRVREP_DBT rep "
  +"     WHERE     CNTR.T_SERVDOCID = ? "
  +"           AND dlc.t_DlContrID = CNTR.T_DLCONTRID "
  +"           AND rep.t_id = CNTR.T_SERVDOCID "
  +"           AND NOT EXISTS "
  +"                      (SELECT 1 "
  +"                         FROM DSCSRVREPOBJ_DBT obj "
  +"                        WHERE obj.t_Format = "
  +"                                 CASE "
  +"                                    WHEN (rep.T_PDFFORMAT IS NOT NULL AND rep.T_PDFFORMAT = CHR (88)) "
  +"                                    THEN " + SC_SRVREPFORMAT_PDF
  +"                                    ELSE " + SC_SRVREPFORMAT_EXCEL_XLSX
  +"                                 END "
  +"                              AND obj.T_SERVDOCID = CNTR.T_SERVDOCID "
  +"                              AND obj.T_CONTRACTID = dlc.t_SfContrID "
  +"                              AND obj.T_ISSEND = CHR (88)) "
  );
  notSendedCmd.addParam(choosenProblem.rec.id);
  MsgBox("Договоров с неотправленными отчетами: " + string(notSendedCmd.GetCount()));
OnError(er)
  MsgBox(GetFullErrorMessage(er));
end;

private macro СформироватьОтчетПоНеотправленным()
  var operCode = "";
  if (not GetString(operCode, "Введите код операции"))
    RunError("Отменено пользователем");
  end;
  var choosenProblem = ПолучитьПроблемнуюПоКоду(operCode);

  BegAction(2000, "Формируем список", false);

  var jvm = CreateObject("rsjvm", "TJavaHost", "GlobalJavaHost");
  var poiRepCreator = jvm.callStaticMethod("ru.softlab.rsbank.poireports.ReportCreator", "getInstance");
  var poiBook = poiRepCreator.addWorkbookXLSX();
  var poiWorkBook = poiBook.getCurrentWorkbook();
  var poiCurrentSheet = poiWorkBook.{getSheetAt@(I)Lorg/apache/poi/xssf/usermodel/XSSFSheet;}(0); 
  var headerStyle = poiWorkBook.{createCellStyle@()Lorg/apache/poi/ss/usermodel/CellStyle;}();
  var headerFont = poiWorkBook.{createFont@()Lorg/apache/poi/xssf/usermodel/XSSFFont;}();
  headerFont.setBold(true);
  headerStyle.setFont(headerFont);
  var poiCurrentRow = poiCurrentSheet.{createRow@(I)Lorg/apache/poi/xssf/usermodel/XSSFRow;}(0);
  var poiCurrentCell = null;

  private macro AddCell(colID, Style, Value, width)
    poiCurrentCell = poiCurrentRow.{createCell@(I)Lorg/apache/poi/xssf/usermodel/XSSFCell;}(colID);
    poiCurrentCell.setCellStyle(Style);
    poiCurrentCell.{setCellValue@(Ljava/lang/String;)V}(Value);
    if (ValType(width) != V_UNDEF)
      poiCurrentSheet.setColumnWidth(colID, width * 256);
    end;
  end;
  AddCell(0, headerStyle, "ИД ДБО", 10);
  AddCell(1, headerStyle, "Номер ДБО", 20);
  AddCell(2, headerStyle, "Клиент", 40);
  AddCell(3, headerStyle, "Код клиента", 15);
  AddCell(4, headerStyle, "ЕКК", 10);

  var regStyle = poiWorkBook.{createCellStyle@()Lorg/apache/poi/ss/usermodel/CellStyle;}();
  var rowId = 1;

  var notSendedCmd = DL_RSDCommand(
   "     SELECT "
  +"           cntr.T_DLCONTRID, "
  +"           cntr.T_DLCONTRNUMBER, "
  +"           cntr.T_CLIENTNAME, "
  +"           cntr.T_CLIENTCODE, "
  +"           cntr.T_EKK, "
  +"           cntr.T_GROUPNUMBER, "
  +"           cntr.T_ROWNUM "
  +"      FROM DSCSRVREPCNTR_DBT cntr, ddlcontr_dbt dlc, DSCSRVREP_DBT rep "
  +"     WHERE     CNTR.T_SERVDOCID = ? "
  +"           AND dlc.t_DlContrID = CNTR.T_DLCONTRID "
  +"           AND rep.t_id = CNTR.T_SERVDOCID "
  +"           AND NOT EXISTS "
  +"                      (SELECT 1 "
  +"                         FROM DSCSRVREPOBJ_DBT obj "
  +"                        WHERE obj.t_Format = "
  +"                                 CASE "
  +"                                    WHEN (rep.T_PDFFORMAT IS NOT NULL AND rep.T_PDFFORMAT = CHR (88)) "
  +"                                    THEN " + SC_SRVREPFORMAT_PDF
  +"                                    ELSE " + SC_SRVREPFORMAT_EXCEL_XLSX
  +"                                 END "
  +"                              AND obj.T_SERVDOCID = CNTR.T_SERVDOCID "
  +"                              AND obj.T_CONTRACTID = dlc.t_SfContrID "
  +"                              AND obj.T_ISSEND = CHR (88)) "
  );
  notSendedCmd.addParam(choosenProblem.rec.id);
  var notSendedDs = notSendedCmd.Execute();
  while (notSendedDs.MoveNext())
    poiCurrentRow = poiCurrentSheet.{createRow@(I)Lorg/apache/poi/xssf/usermodel/XSSFRow;}(rowId);
    AddCell(0, regStyle, string(SQL_ConvTypeInteger(notSendedDs.DLCONTRID)));
    AddCell(1, regStyle, string(SQL_ConvTypeStr(notSendedDs.DLCONTRNUMBER)));
    AddCell(2, regStyle, string(SQL_ConvTypeStr(notSendedDs.CLIENTNAME)));
    AddCell(3, regStyle, string(SQL_ConvTypeStr(notSendedDs.CLIENTCODE)));
    AddCell(4, regStyle, string(SQL_ConvTypeStr(notSendedDs.EKK)));
    rowId = rowId + 1;
  end;
  var dirPath = Loc_GetAbsoluteTxtPath() + "\\";
  var fileName = "NoSended_"+CreateGUID()+".xlsx";
  if (not poiBook.saveAs(PathCombine(dirPath,fileName)))
    RunError("Ошибка сохранения файла по пути " + PathCombine(dirPath,fileName));
  end;

  EndAction();
  SC_ShowFile(dirPath, fileName, false);
OnError(er)
  MsgBox(GetFullErrorMessage(er));
end;

private macro GenerateNumberByReference( ObjType:INTEGER, RefType:INTEGER)
  var refId = 0;
  var refNum = "";
  if( GetReferenceIDByType( ObjType, RefType, refId) )
    RunError( "Не найден описатель референса" );
  elif( GenerateReference( refId, RefNum ) )
    RunError( "Ошибка при генерации референса по описателю " + string(refId) );
  end;
  return refNum;
END;

private macro СоздатьОперациюОтправкиПоВечерним()
  var ErrCode, ErrText;
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;

  var srvRepFile = TBFile("scsrvrep.dbt","W");

  var dealDate = {curdate};
  if (not GetDate(dealDate, "Введите дату поиска вечерних сделок"))
    RunError("Отменено пользователем");
  end;

  var arMenu = makeArray(
    "Отчет брокера ФЛиЮЛ",
    "Отчет брокера ФЛиЮЛ v2.0"
  );
  var choice = DL_Menu(arMenu,"Выберите вид операции");
  if (choice < 0)
    RunError("Отменено пользователем");
  end;

  srvRepFile.rec.dockind = IIF(choice == 0, 4635, 4660);
  srvRepFile.rec.PeriodKind = 1;
  srvRepFile.rec.date = {curdate};
  srvRepFile.rec.begdate = dealDate;
  srvRepFile.rec.enddate = dealDate;
  srvRepFile.rec.clientid = -1;
  srvRepFile.rec.byexchange = SET_CHAR;
  srvRepFile.rec.byoutexchange = SET_CHAR;
  srvRepFile.rec.bymoving = SET_CHAR;
  srvRepFile.rec.excludeUK = SET_CHAR;
  srvRepFile.rec.bycm = SET_CHAR;
  srvRepFile.rec.pdfformat = SET_CHAR;
  srvRepFile.rec.excelformat = SET_CHAR;
  srvRepFile.rec.oper = {oper};
  srvRepFile.rec.department = {NumDprt};
  srvRepFile.rec.AutoSendKind = SCSRVREP_AUTOSENDKIND_TOGETHER;
  if (GenPropID(srvrepFile.rec, "WasManClntEdit") >= 0)
    srvrepFile.rec.WasManClntEdit = SET_CHAR;
  end;

  RslDefCon.BeginTrans();

  srvRepFile.rec.code = GenerateNumberByReference(107, 21)+"_СР";

  if (not srvRepFile.Insert())
    ErrCode = Status(ErrText);
    RunError("Ошибка вставки", CustomError(GetErrMsg() + "; Код: "+ ErrCode + "; Ошибка: " + ErrText ));
  end;

  BegAction(2000, "Добавление договоров по вечерним сделкам", false);

  var dealQuery =
     " SELECT DISTINCT mp.T_DLCONTRID "
     "   FROM ddvdeal_dbt dv "
     "        INNER JOIN dsfcontr_dbt sf "
     "           ON sf.t_id = DV.T_CLIENTCONTR "
     "        INNER JOIN DDLCONTRMP_DBT mp "
     "           ON MP.T_SFCONTRID = sf.t_id "
     "  WHERE dv.t_time >= TO_DATE ('01.01.0001 19:00:00', 'dd.mm.yyyy hh24:mi:ss') "
     "        AND dv.t_client > 0 "
     "        AND dv.t_date = " + GetSQLDate(dealDate);

  var sql = 
     "  INSERT INTO DSCSRVREPCNTR_DBT (T_SERVDOCID, "
    +"                                 T_DLCONTRID, "
    +"                                 T_EKK, "
    +"                                 T_DLCONTRNUMBER, "
    +"                                 T_CLIENTNAME, "
    +"                                 T_CLIENTCODE, "
    +"                                 T_ROWNUM, "
    +"                                 T_GROUPNUMBER) "
    +"   SELECT "+string(srvRepFile.rec.id)+", "
    +"          dlc.T_DLCONTRID, "
    +"          NVL ( "
    +"             (SELECT dlobjcode.t_Code "
    +"                FROM ddlobjcode_dbt dlobjcode "
    +"               WHERE dlobjcode.t_ObjectType = 207 "
    +"                     AND dlobjcode.t_CodeKind = 1 "
    +"                     AND dlobjcode.t_BankDate <= "+ GetSQLDate({curdate})
    +"                     AND (dlobjcode.t_BankCloseDate = "
    +"                             TO_DATE ('01 01 0001', 'DD MM YYYY') "
    +"                          OR dlobjcode.t_BankCloseDate >= "+GetSQLDate({curdate})+") "
    +"                     AND dlobjcode.t_ObjectID = dlc.t_DlContrID), "
    +"             CHR (1)) "
    +"             AS t_ekk, "
    +"          sfdl.T_NUMBER, "
    +"          prt.t_shortname AS t_clientname, "
    +"          (SELECT t_code "
    +"             FROM dobjcode_dbt "
    +"            WHERE     t_objecttype = 3 "
    +"                  AND t_codekind = 1 "
    +"                  AND t_state = 0 "
    +"                  AND t_objectid = prt.t_partyid) "
    +"             AS T_CLIENTCODE, "
    +"          0 AS t_rownum, "
    +"          0 AS t_groupnum "
    +"     FROM (SELECT * "
    +"             FROM ddlcontr_dbt "
    +"            WHERE T_DLCONTRID IN (" + dealQuery + ")) dlc "
    +"          INNER JOIN dsfcontr_dbt sfdl "
    +"             ON SFDL.T_ID = DLC.T_SFCONTRID "
    +"          INNER JOIN dparty_dbt prt "
    +"             ON PRT.T_PARTYID = SFDL.T_PARTYID ";
  execSQL(sql);
  EndAction();

  var cntrCountCmd = DL_RSDCommand("select * from DSCSRVREPCNTR_DBT where T_SERVDOCID = ?");
  cntrCountCmd.addParam(srvRepFile.rec.ID);

  if (cntrCountCmd.GetCount() == 0)
    if(RslDefCon.IsInTrans)
      RslDefCon.RollbackTrans();
    end;
    MsgBox("Нет договоров по вечерним сделкам.|Новая операция не вставлена.");
  else
    RslDefCon.CommitTrans();
    MsgBox("Успешно вставлена операция " + srvRepFile.rec.code);
  end;
OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  MsgBox(GetFullErrorMessage(er));
end;

/* Вызывается по CTRL+Z из скроллинга */
private macro Функция_Пользователя()
  var oldDialog = SetDialogFlag(1);
  var arMenu = makeArray(
    "Создать операцию доотправки для незавершенной операции",
    "Сформировать список клиентов с неотправленными отчетами",
    "Проверить завершенность операции",
    "Создать операцию отправки для клиентов вечерней сессии"
  );
  var choice = DL_Menu(arMenu,"Выберите действие");
  if (choice == 0)
    СоздатьОперациюДоотправки();
  elif (choice == 1)
    СформироватьОтчетПоНеотправленным();
  elif (choice == 2)
    ПроверитьЗавершение();
  elif (choice == 3)
    СоздатьОперациюОтправкиПоВечерним();
  end;
  SetDialogFlag(oldDialog);
  return 0;
end;

