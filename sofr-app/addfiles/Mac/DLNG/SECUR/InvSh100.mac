/*
$Name:        InvSh100.mac
$Module:      Ценные бумаги
$Description: Операция получения паев. Шаг оплаты 
*/

/* Операция получения паев
   Шаг оплаты*/
import InsCarryDoc, "sp_class.mac", "sp_carcm.mac";

private record   SpChangeDates("spchdate.str");/*глобальная структура с данными по изменению сроков*/

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  record deal(dl_tick);
  var    FD, Action = SpChangeDates.Action, FactDate = SpChangeDates.FactDate;
  var DtAccount = TRecHandler("account");
  var CtAccount = TRecHandler("account");
  var AccountBuff = TRecHandler("account");
  var rq_money = TRecHandler("dlrq");

  if( Action <= 0 )
     msgbox("Шаг оплаты из списка шагов выполнять запрещено. Необходимо выполнить расчеты по сделке."); 
     return 1;
  end; 

  SetBuff( deal, FDoc ); 
  /*Не должно быть выполненных шагов с более поздней планируемой датой*/
  if( (FactDate != Date(0,0,0)) AND (FactDate < SP_GetMaxPlanStepDate(ID_Operation, FactDate)) )
     msgbox("Есть выполненные шаги с планируемой датой больше|чем дата оплаты.|Запрещено выполнять операцию."); 
     return 1;
  end;

  FD = SPFirstDoc( deal );

  /*Обновить дату оплаты*/
  if( FD.dl_leg.rec.MaturityIsPrincipal == SET_CHAR )
     FD.dl_leg.rec.Expiry = FactDate;
  else
     FD.dl_leg.rec.Maturity = FactDate;
  end;         
  if( OprInsertSPTKCHNG( FactDate, SPTKCHNG_RECALC, FD.tick, FD.dl_leg, FD.dl_leg ) == false )
     return 1;
  end;

  rq_money = FD.GetRQ(DLRQ_TYPE_PAYMENT);

  DtAccount.Clear();
  CtAccount.Clear();
  if(ПолучитьСчетКонтрагентаПоТООплаты( rq_money, FD, FactDate, DtAccount, CtAccount ) != 0)
    return 1;
  end;

  if( not IsClientDeal(FD.tick.rec) )
    if( ПолучитьНашСчетТОПоОплате( rq_money, FD, FactDate, DtAccount, CtAccount ) != 0)
      return 1;
    end;                                                                              
  else     
    if( ПолучитьСчетСубъектаПоСделке(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.ClientID, DLRQ_SUBKIND_CURRENCY, FD.GetPayFIID(), AccountBuff) != 0)
      return 1;
    end;

    if(SP_RqIsSale( rq_money, FD.tick ))
      CtAccount = AccountBuff;
    else
      DtAccount = AccountBuff;
    end;
  end;

  if( ОбработатьДенежноеТО_ИП( rq_money, DtAccount, CtAccount, FD, FactDate, Doc, "Сумма ТО по оплате" ) != 0 )
    return 1;
  end;
  
  if( not ПроводкаПоКатегориямВнутреннегоУчета( 20,
                                                FD,
                                                Doc,
                                                FactDate,
                                                rq_money.rec.FIID,
                                                rq_money.rec.Amount
                                              ) )
      return 1;
  end;  

  if( not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALPAY), FactDate) )
     msgbox("Ошибка при попытке переинициализировать дату оплаты ц/б." );         
     return 1;
  end;

  return 0;
end;
