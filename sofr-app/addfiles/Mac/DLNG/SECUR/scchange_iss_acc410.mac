/*
 $Name: scchange_iss_acc410.mac
 $Module: Ценные бумаги
 $Description: Операция изменения эмитента
*/

import InsCarryDoc, RsbDataSet, "scincfun.mac", "secinter.mac", "sp_car.mac", "scservop.mac", "spserv.mac";

private record Avoiriss( avoiriss );
private record fininstr(fininstr);

private const FI_CHANGE_ISSUER = 3;
private const KIND_BONUS = 6;
private const KIND_DISCOUNT = 2;
private const KIND_PERCENT = 1;

private macro  Получить_FiRole(accdoc, CatCode): INTEGER
   record mccateg( mccateg );
   var mctempl = TBfile("mctempl", "R", 0);
   var FiRole = -1;

   macro НайтиЗначениеПараметраДляДанногоКлассификатора( mccateg, mctempl, ClassID )
      if (mccateg.Class1 == ClassID)
         return mctempl.Value1;
      elif (mccateg.Class2 == ClassID)
         return mctempl.Value2;
      elif (mccateg.Class3 == ClassID)
         return mctempl.Value3;
      elif (mccateg.Class4 == ClassID)
         return mctempl.Value4;
      elif (mccateg.Class5 == ClassID)
         return mctempl.Value5;
      elif (mccateg.Class6 == ClassID)
         return mctempl.Value6;
      elif (mccateg.Class7 == ClassID)
         return mctempl.Value7;
      elif (mccateg.Class8 == ClassID)
         return mctempl.Value8;
      end;

      return -1;
   end;

   if( MC_FindMCCATEG( CatCode, mccateg ) == true )

      var TypePort, TypeIsBpp, TypeIncome;
      var Bonus = false;
      var Discount = false;
      var Percent = false;

      mctempl.Clear();
      mctempl.rec.CatID = accdoc.rec.CatID;
      mctempl.rec.Number = accdoc.rec.TemplNum;

      if (mctempl.GetEQ())
         TypePort   = НайтиЗначениеПараметраДляДанногоКлассификатора( mccateg, mctempl.rec, LLCLASS_KINDPORT );
         TypeIsBpp  = НайтиЗначениеПараметраДляДанногоКлассификатора( mccateg, mctempl.rec, LLCLASS_IS_AVOIR_BPP );
         TypeIncome = НайтиЗначениеПараметраДляДанногоКлассификатора( mccateg, mctempl.rec, LLCLASS_KIND_ACC_PDD );

         if(TypeIncome == KIND_BONUS)
            Bonus = true;
         end;

         if(TypeIncome == KIND_PERCENT)
            Percent = true;
         elif(TypeIncome == KIND_DISCOUNT)
            Discount = true;
         end;
     end;

   end;
     
   if(CatCode == "Премия, ц/б")
     FiRole = GetFIRoleByPortfolioBonus(TypePort, IIF(TypeIsBpp == 1, true, false));
   else
     FiRole = GetFIRoleByPortfolio(TypePort, Discount, Percent, IIF(TypeIsBpp == 1, true, false), false, Bonus);
   end;

   return FiRole;                 

end;


private macro GetCatCode(CatID: INTEGER)

   var CatCode = -1;
   var select = Dl_RSDCommand("SELECT CATEG.T_CODE Code  FROM dmccateg_dbt categ  WHERE CATEG.T_ID = ?");
   Select.AddParam(CatID);
  
   var DataSet = Select.Execute();
  
  if(DataSet.moveNext() )
    CatCode = DataSet.Code;
  end;

  return CatCode;

end;

private macro ВыполнитьУчетИзмененияЭмитента(FD, Doc, FIID: INTEGER, OperDate: Date)
  record Acc(account);
  record Account(account);
  file   mcconacc( mcconacc );
  var stat = 0;
  var Accdoc = TRecHandler("mcaccdoc");
  var Rest = 0.0;

  var query =  "	SELECT Accdoc.* 	"+
               "	  FROM dmcaccdoc_dbt Accdoc	"+
               "	 WHERE ACCDOC.T_ID IN	"+
               "	          (SELECT CONACC.T_ACCDOCID	"+
               "	             FROM dmcconacc_dbt conacc	"+
               "	            WHERE     CONACC.T_DOCKIND = ? 	"+   
               "	                  AND CONACC.T_ACCOUNT = CHR (1)	"+
               "	                  AND CONACC.T_DOCID = ?) ";	


  var Select = Dl_RSDCommand(query);

  Select.AddParam(DLDOC_ISSUE);
  Select.AddParam(FIID);
 
  var AccDataSet = Select.Execute();
  
  while(AccDataSet.moveNext() )

     AccDataSet.GetRecord().CopyTo( Accdoc.rec );
     ClearRecord( Acc );  

     if( GetAccount( accdoc.rec.Chapter, accdoc.rec.Currency, accdoc.rec.Account, Acc ) AND (Acc.Open_Close != "З") 
        AND ((Rest = GetRestAccount(Acc, OperDate)) != 0.0) )        

         var V_CatCode = GetCatCode(Accdoc.rec.CatID);

         var FIROLE = Получить_FiRole(Accdoc, V_CatCode);
         if(not( FD.OpenAccount(V_CatCode, NULL, NULL, FIROLE , Account, OperDate)))
           return 1;
         end;
          /*Выполнить проводку с Account на Acc суммы Rest
          (счета активные, поэтому направление проводки именно такое!)*/
         if(not ПроводкаПоКатегориямУчета( FD, 
                        Account, Acc,/* КатегДеб , КатегКредит*/
                        OperDate,              /* Дата */
                        1,                           /* Глава */
                        accdoc.rec.Currency,       /* Валюта */
                        abs(Rest),                      /* Сумма */
                        Doc,                         /* Документ */
                        INPCARRY,                    /* Result_Carry */
                        " 1",                        /* Kind_Oper */
                        "",                          /* Numb_Document */
                        "Перенос остатка лицевого счета"                     
                       ) 
                   )
           return 1;
         end;

         ClearRecord(mcconacc);
         mcconacc.DocKind  = DLDOC_ISSUE;
         mcconacc.DocID    = FIID;
         mcconacc.AccDocID = AccDataSet.ID;
         mcconacc.NewAccount = Account.Account;

         OprUpdateMCCONACC( mcconacc );
     end;     
  end;

  return stat;
end;

macro ExecuteStep( Doc, FirstDoc, _DocKind, ID_Operation, ID_Step )

   SetBuff( Avoiriss, FirstDoc );
   var FD = SPFirstDocFI( DLDOC_ISSUE, Avoiriss.FIID, Avoiriss.FIID, -1, {OurBank}, null, null, null ); 
   // Выполняем с текущей даты всегда
   var dat = {curdate};

   if( ВыполнитьУчетИзмененияЭмитента(FD, Doc, Avoiriss.FIID, dat))
      msgbox("Ошибка при выполнении учёта изменения эмитента. Нужно откатить операцию изменения эмитента.");
      return 1;
   end;

  return 0;
end;