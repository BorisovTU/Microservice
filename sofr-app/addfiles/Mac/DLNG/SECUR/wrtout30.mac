/*
$Name:        wrtout30.mac
$Module:      Ценные бумаги
$Description: Операция списания затрат. Шаг списания затрат.
*/
import InsCarryDoc, "sp_car.mac";

macro ExecuteStep( doc, adr )
  
  record fininstr(fininstr);  
  record comm( dl_comm );
  record OutlAcc( account );
  record wrtcalc(pmwrtsum);
  file   avoiriss( avoiriss );
  var    dat, FD, Zp = $0, OutlSale = $0, Z = $0, P = $0, O = $0, П = $0; 
  var    i = 0, NumRec;

  SetBuff ( comm, adr );
  FD  = SPFirstDocDLCOMM( comm );
  dat = comm.CommDate;

  /* списание затрат на продажу*/
  if( not FD.IsExistAccount( "ЗатратыТП", null, null, FIROLE_SALEACTIVE, OutlAcc, null, dat))
     return 1;
  end;
  OutlSale = ABS( GetRestAccount( OutlAcc, dat ) );

  NumRec = NRecords( avoiriss );
  InitProgress(NumRec, "Выполнение...", "Списание затрат");

  rewind( avoiriss );
  while( next(avoiriss) )

    if( NOT ПолучитьФинИн( avoiriss.FIID, fininstr ) )

      if( NOT НеэмиссионныйФинИн( avoiriss.FIID ) )
          if( ( fininstr.DrawingDate >= comm.BeginDate ) OR 
              ( ( fininstr.DrawingDate == date (0, 0, 0) ) AND 
                ( ( fininstr.ExpiryDate >= comm.BeginDate ) OR
                  ( fininstr.ExpiryDate == date (0, 0, 0) ) ) ) )                            
  
                    if( not SP_SaleForPeriod( wrtcalc, {OperDprt}, -1, avoiriss.FIID, KINDPORT_TRADE,
                                             comm.BeginDate, comm.EndDate, true ) 
                      )
                        msgbox( "Ошибка при определении количества проданных ц/б|"+GetErrMsg());
                        return 1;
                    end;
                    P = P + wrtcalc.Amount;

                    if( not SP_BuyForPeriod( wrtcalc, {OperDprt}, {OurBank}, avoiriss.FIID, KINDPORT_TRADE,
                                            comm.BeginDate, comm.EndDate, true, true ) 
                      )
                        msgbox( "Ошибка при определении количества купленных ц/б|"+GetErrMsg() );
                        return 1;
                    end;
                    П = П + wrtcalc.Amount;

                    if( not SP_RestOnDate( wrtcalc, {OperDprt}, {OurBank}, avoiriss.FIID, KINDPORT_TRADE,
                                           comm.BeginDate-1, true, true ) 
                      )
                       msgbox( "Ошибка при определении остатка ц/б|"+GetErrMsg() );
                       return 1;
                    end;
                    O = O + wrtcalc.Amount;
          end; 
      end;

    else
      msgbox( "Не найден фин. инструмент с идентификатором FIID = ", avoiriss.FIID );
    end;

    i = i + 1;
    UseProgress( i );

  end; 

  RemProgress();
  if( O+П != 0 )
     /* списание затрат на покупку*/
     if( not FD.IsExistAccount( "ЗатратыТП", null, null, FIROLE_BUYACTIVE, OutlAcc, null, dat))
        return 1;
     end;
     Z = ABS( GetRestAccount( OutlAcc, dat ) );

     Zp = Z*P/(O+П);
  end;

  if(not ПроводкаПоКатегориямУчета( FD, 
         "-МаржаОРЦБ","ЗатратыТП", /* КатегДеб , КатегКредит*/
         dat,                  /* Дата */
         1,                    /* Глава */
         0,                    /* Валюта */
         OutlSale,             /* Сумма */
         Doc,                  /* Документ */
         INPCARRY,             /* Result_Carry */
         " 1",                 /* Kind_Oper */
         comm.CommCode,        /* Numb_Document */
         "Списание затрат на продажу", /* Ground */
         null, null, FIROLE_SALEACTIVE
        ) )
      return 1;
  end;

  if(not ПроводкаПоКатегориямУчета( FD, 
         "-МаржаОРЦБ","ЗатратыТП", /* КатегДеб , КатегКредит*/
         dat,                  /* Дата */
         1,                    /* Глава */
         0,                    /* Валюта */
         Zp,                   /* Сумма */
         Doc,                  /* Документ */
         INPCARRY,             /* Result_Carry */
         " 1",                 /* Kind_Oper */
         comm.CommCode,        /* Numb_Document */
         "Списание затрат на покупку", /* Ground */
         null, null, FIROLE_BUYACTIVE
        ) )
      return 1;
  end;
 
  return 0;
end;
