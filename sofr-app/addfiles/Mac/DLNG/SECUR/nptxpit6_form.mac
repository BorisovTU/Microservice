/*
$Name:        nptxpit6_form.mac
$Module:      Ценные бумаги
$Description: Форма 6-НДФЛ
*/

import "DlRepForm_h.mac", "dlmisc.mac";

const NPTX_PIT6_FLDNUM_P1BEGDATE = 0;
const NPTX_PIT6_FLDNUM_P1ENDDATE = 1;
const NPTX_PIT6_FLDNUM_P2BEGDATE = 2;
const NPTX_PIT6_FLDNUM_P2ENDDATE = 3;
const NPTX_PIT6_FLDNUM_BYCB      = 4;
const NPTX_PIT6_FLDNUM_BYDEPO    = 5;
const NPTX_PIT6_FLDNUM_BYVS      = 6;
const NPTX_PIT6_FLDNUM_INXML     = 7;
const NPTX_PIT6_FLDNUM_REPDATE   = 8;

private const PNFLD_P1BEGDATE = 100;
private const PNFLD_P1ENDDATE = 101;
private const PNFLD_P2BEGDATE = 102;
private const PNFLD_P2ENDDATE = 103;
private const PNFLD_BYCB      = 104;
private const PNFLD_BYDEPO    = 105;
private const PNFLD_BYVS      = 106;
private const PNFLD_INXML     = 107;
private const PNFLD_REPDATE   = 108;

private macro GetPeriodsInterval(repDate:Date, p1BegDate:@Date, p1EndDate:@Date, p2BegDate:@Date, p2EndDate:@Date)
  var dd = 0, mm = 0, yyyy = 0;

  DateSplit(repDate, dd, mm, yyyy);

  if((mm >= 1) and (mm <= 3))
    Period_GetInterval(16, yyyy - 1, @p2BegDate, @p2EndDate);
    p1BegDate = Date(1, 1, yyyy - 1);
  else
    Period_GetInterval(Int((mm - 1) / 3) + 12, yyyy, @p2BegDate, @p2EndDate);
    p1BegDate = Date(1, 1, yyyy);
  end;
  p1EndDate = p2EndDate;
end;

private macro CheckYear(p1BegDate:Date, p1EndDate:Date, p2BegDate:Date)
  var yyyy1 = 0, yyyy2 = 0, yyyy3 = 0;

  DateSplit(p1BegDate, null, null, yyyy1);
  DateSplit(p1EndDate, null, null, yyyy2);
  DateSplit(p2BegDate, null, null, yyyy3);

  if((yyyy1 != yyyy2) or (yyyy2 != yyyy3) or (yyyy3 != yyyy1))
    MsgBox("Все даты границ периода отчета должны принадлежать одному календарному году");
    return true;
  end;

  return false;
end;

private macro CheckModule(byCB:String, byDEPO:String, byVS:String)
  if((byCB == UNSET_CHAR) and (byDEPO == UNSET_CHAR) and (byVS == UNSET_CHAR))
    MsgBox("Хотя бы один из флагов формирования отчета по данным модулей, должен быть установлен");
    return true;
  end;

  return false;
end;

private macro FldProc_P1EndDate(pThis:DL_CPanel, cmd:Integer, key:Integer, fldShowValue:@Variant, fldRealValue:@Variant):Integer
  if(cmd == DLG_CHANGEVALUE) // Значение поля было изменено
    fldRealValue = fldShowValue;

    if(pThis.GetLinkedValue(PNFLD_P2BEGDATE) > fldRealValue)
      pThis.SetLinkedValue(PNFLD_P2BEGDATE, fldRealValue);
    end;

    pThis.SetLinkedValue(PNFLD_P2ENDDATE, fldRealValue);

    return CM_DEFAULT;
  end;

  return DL_FldProc_EndDate(pThis, cmd, key, @fldShowValue, @fldRealValue);
end;

private macro FldProc_P2BegDate(pThis:DL_CPanel, cmd:Integer, key:Integer, fldShowValue:@Variant, fldRealValue:@Variant):Integer
  if(cmd == DLG_CHANGEVALUE) // Значение поля было изменено
    fldRealValue = fldShowValue;

    if(pThis.GetLinkedValue(PNFLD_P1BEGDATE) > fldRealValue)
      pThis.SetLinkedValue(PNFLD_P1BEGDATE, fldRealValue);
    end;

    return CM_DEFAULT;
  end;

  return DL_FldProc_BeginDate(pThis, cmd, key, @fldShowValue, @fldRealValue);
end;

class (DL_CPanel) NPTX_Pit6_Form()
  private macro Init()
    var p1BegDate = ZeroDate, p1EndDate = ZeroDate, p2BegDate = ZeroDate, p2EndDate = ZeroDate, repDate = Date();

    GetPeriodsInterval(repDate, @p1BegDate, @p1EndDate, @p2BegDate, @p2EndDate);

    AddFieldProc(0, NPTX_PIT6_FLDNUM_P1BEGDATE, PNFLD_P1BEGDATE, @DL_FldProc_BeginDate, p1BegDate);
    AddFieldProc(0, NPTX_PIT6_FLDNUM_P1ENDDATE, PNFLD_P1ENDDATE, @FldProc_P1EndDate,    p1EndDate);
    AddFieldProc(0, NPTX_PIT6_FLDNUM_P2BEGDATE, PNFLD_P2BEGDATE, @FldProc_P2BegDate,    p2BegDate);
    AddFieldProc(0, NPTX_PIT6_FLDNUM_P2ENDDATE, PNFLD_P2ENDDATE, @DL_FldProc_EndDate,   p2EndDate);
    AddFieldProc(0, NPTX_PIT6_FLDNUM_BYCB,      PNFLD_BYCB,      @DL_FldProc_CheckBox,  SET_CHAR);
    AddFieldProc(0, NPTX_PIT6_FLDNUM_BYDEPO,    PNFLD_BYDEPO,    @DL_FldProc_CheckBox,  SET_CHAR);
    AddFieldProc(0, NPTX_PIT6_FLDNUM_BYVS,      PNFLD_BYVS,      @DL_FldProc_CheckBox,  SET_CHAR);
    AddFieldProc(0, NPTX_PIT6_FLDNUM_INXML,     PNFLD_INXML,     @DL_FldProc_CheckBox,  UNSET_CHAR);
    AddFieldProc(0, NPTX_PIT6_FLDNUM_REPDATE,   PNFLD_REPDATE,   @DL_FldProc_BeginDate, repDate);
  end;

  private macro Check():Bool
    var p1BegDate = ZeroDate, p1EndDate = ZeroDate, p2BegDate = ZeroDate, p2EndDate = ZeroDate;

    if(GetFieldValue(NPTX_PIT6_FLDNUM_P1BEGDATE) > GetFieldValue(NPTX_PIT6_FLDNUM_P2BEGDATE))
      MsgBox("Дата начала отчетного периода для формирования первого раздела не должна быть больше даты начала отчетного периода для формирования второго раздела");
      return false;
    end;

    if(GetFieldValue(NPTX_PIT6_FLDNUM_P2BEGDATE) > GetFieldValue(NPTX_PIT6_FLDNUM_P1ENDDATE))
      MsgBox("Дата начала отчетного периода для формирования второго раздела не должна быть больше даты окончания отчетного периода для формирования первого раздела");
      return false;
    end;

    if(CheckYear(GetFieldValue(NPTX_PIT6_FLDNUM_P1BEGDATE), GetFieldValue(NPTX_PIT6_FLDNUM_P1ENDDATE), GetFieldValue(NPTX_PIT6_FLDNUM_P2BEGDATE)))
      return false;
    end;

    if(CheckModule(GetFieldValue(NPTX_PIT6_FLDNUM_BYCB), GetFieldValue(NPTX_PIT6_FLDNUM_BYDEPO), GetFieldValue(NPTX_PIT6_FLDNUM_BYVS)))
      return false;
    end;

    if(GetFieldValue(NPTX_PIT6_FLDNUM_INXML) == SET_CHAR)
      GetPeriodsInterval(GetFieldValue(NPTX_PIT6_FLDNUM_REPDATE), @p1BegDate, @p1EndDate, @p2BegDate, @p2EndDate);

      if(((GetFieldValue(NPTX_PIT6_FLDNUM_P1BEGDATE) != p1BegDate)
       or (GetFieldValue(NPTX_PIT6_FLDNUM_P1ENDDATE) != p1EndDate)
       or (GetFieldValue(NPTX_PIT6_FLDNUM_P2BEGDATE) != p2BegDate))
     and (MsgBoxEx("Период отчета не совпадает с требованиями по предоставлению отчета. Продолжить?", MB_YES + MB_NO, IND_YES) == IND_NO))
        return false;
      end;
    end;

    return true;
  end;

  initDL_CPanel("sp_rep.lbr", "NPTXPIT6"); 
end;

