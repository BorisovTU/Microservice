/**
@file 		677_limout.mac
@brief 		КОСТЫЛЬ!!!  Копия limout.mac с минорными изменениями для исключения регресса по задаче BOSS-5467 Корректировка лимитов по заявкам клиентов в рамках 677 указа
$Module:      	Ценные бумаги
*/

import globals, oralib, likepy, cb_sql, DealsInter, CTInter, RsbFormsInter;
import rsexts;
import "u_common_email_utils.mac";

//private const REG_EXPORT_PATH = "РСХБ\\ИНТЕГРАЦИЯ\\ДИРЕКТОРИИ\\EXPORT\\QUIK_ЛИМИТЫ"; //путь для выгрузки файла
private const REG_EXPORT_PATH = "РСХБ\\ДИРЕКТОРИИ\\QUIK_OUT"; //путь для выгрузки файла
private const REG_EXPORTCOR_PATH = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ДИРЕКТОРИЯ КОРРЕКТИРОВОК QUIK"; //путь для выгрузки файла
private const REG_EXPORTCOR_LIMIT = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ЛИМИТ КОРРЕКТИРОВОК QUIK"; //
private const REG_EXPORTCOR_CUR = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ВАЛЮТЫ К ВЫГРУЗКЕ КОРРЕКТ. QUIK"; //
private const REG_LEV_DEF = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\QUIK\\LEVERAGE_DEFAULT";
private const REG_LEV_DEF_ENTITY = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\QUIK\\LEVERAGE_DEFAULT_ENTITY";

/*-1 - списания, 0 - все, 1 - зачисления*/
private const MODE_WRITE_OFF = -1;
private const MODE_CREDITING = 1;
private const MODE_BOTH = 0;



private var RegVal_Default_Leverage, RegVal_Default_Leverage_Entity;
private var errCode;

GetRegistryValue(REG_LEV_DEF, V_STRING, RegVal_Default_Leverage, ErrCode);
if( ErrCode > 0 )
  RegVal_Default_Leverage = -1;
end;

if (StrIsNumber(RegVal_Default_Leverage))
  RegVal_Default_Leverage = int(RegVal_Default_Leverage);
end;

/*BOSS-4379 Доступ к сложным ФИ для ЮЛ неквалифицированных инвесторов */
GetRegistryValue(REG_LEV_DEF_ENTITY, V_STRING, RegVal_Default_Leverage_Entity, ErrCode);
if( ErrCode > 0 )
  RegVal_Default_Leverage_Entity = -1;
end;

if (StrIsNumber(RegVal_Default_Leverage_Entity))
  RegVal_Default_Leverage_Entity = int(RegVal_Default_Leverage_Entity);
else 
  RegVal_Default_Leverage_Entity = -1;
end;

private macro iif (c,a,b)
   if (c) return a else return b end;
end;

class c_sfcontr(ContrID)
   var _sfcontr = TRecHandler("sfcontr");
   var _party = TBFile("party");

   macro isBlocked(LimitDate)
      var Dt = TRsbDataSet
      ( " SELECT (CASE WHEN RSB_SECUR.GetGeneralMainObjAttr (207,LPAD (t_DlContrID, 34, '0'),1,"+GetSQLDate(LimitDate)+") = 1 THEN 'X' " + 
        "            ELSE CHR (0) END) IsBlocked                                                                         " +
        "   FROM ddlcontrmp_dbt WHERE t_sfcontrid = " + _sfcontr.rec.id);
      if (Dt.movenext())
         return Dt.IsBlocked;
      end;
      return StrFor(0);
   end;

   macro isExchange()
      if (_sfcontr.rec.servkindsub == 8)
         return true;
      else
         return false;
      end;
   end;

   macro isStock()
      if (_sfcontr.rec.servkind == 1)
         return true;
      else
         return false;
      end;
   end;

   macro isFuture()
      if (_sfcontr.rec.servkind == 15)
         return true;
      else
         return false;
      end;
   end;

   macro isCurMarket()
      if (_sfcontr.rec.servkind == 21)
         return true;
      else
         return false;
      end;
   end;

   macro isEdp()
      var Dt = TRsbDataSet("SELECT rshb_rsi_sclimit.SfcontrIsEDP("+_sfcontr.rec.id+") as t_isedp FROM dual");
      if (Dt.movenext())
         return Dt.IsEDP == 1;
      end;
      return false;
   end;

   macro isAccY()
      var note = ReadNoteForObject(659, UniID(_sfcontr,659), 5 /*Счет клиента на ММВБ Фондовый сектор*/);
      if (note != "")
         if (index(note,"Y") == 1)
            return true;
         end;
      end;
      return false
   end;

   macro GetTagFromNote(OperDate)
      var note = ReadNoteForObject(659, UniID(_sfcontr,659), 105, OperDate);
      if ((ValType(note) == V_STRING) and (note != ""))
        return Trim(note);
      end;
      return "";
   end;

   macro GetTestResult()
      var note = ReadNoteForObject(3, UniID(_party,3), 108);
      if ((ValType(note) != V_INTEGER) or ((ValType(note) == V_INTEGER) and (note == 0)))
        note = -1;
      end;
      return note;
   end;

   macro GetMarketId()
     var Dt = TRsbDataSet(" select t_marketid from ddlcontrmp_dbt where t_sfcontrid = " +_sfcontr.rec.id);
     if (Dt.movenext())
        return dt.marketid;
     end;
     return 0;
   end;

   macro IsQI()
     var Dt = TRsbDataSet(" select t_state from dscqinv_dbt where t_partyid = " +_sfcontr.rec.partyid);
     if (Dt.movenext())
        return SQL_ConvTypeInteger(dt.state) == 1;
     end;
     return false;
   end;

   macro rec()
      return _sfcontr.rec;
   end;

   /*
   if (_sfcontr.rec.id != 0)
      return _sfcontr.rec;
   end;*/
   var Dt = TRsbDataSet(" select * from dsfcontr_dbt where t_id = " + ContrID);
   if (Dt.movenext())
     Dt.GetRecord.CopyTo(_sfcontr.rec);
     //return _sfcontr.rec;
   end;
   if (_sfcontr.rec.partyid > 0)
     _party.rec.partyid = _sfcontr.rec.partyid ;
     _party.GetEQ();
   end;
end;

macro GetSpbexID()
   var Dt = TRsbDataSet("SELECT rshb_rsi_sclimit.GetSpbexID() as t_spbid FROM dual");
   if (Dt.movenext())
      return SQL_ConvTypeInteger(dt.spbid);
   end;
   return 0;
end;

// DEF-68258, возвращает валюту для нулевых лимитов, по соглашению -1
macro GetCurrencyByZeroLimit()
   return -1;
end;

// DEF-68258, возвращает код для нулевых лимитов из ddl_limitprm_dbt
macro GetCodeSCZeroLimit(p_MarketKind, p_MarketID, p_ImplKind)
    var sql, cmd, p_Code;
   
    sql =  "DECLARE "
         + "BEGIN "
         + "   :p_Code := RSHB_RSI_SCLIMIT.GetCodeSCZeroLimit(:p_MarketKind, :p_MarketID, :p_ImplKind); "
         + "END; "
    ;
      
    cmd = RSDCommand(sql);
    cmd.addParam("p_Code", RSDBP_OUT, V_STRING);
    cmd.addParam("p_MarketKind", RSDBP_IN, p_MarketKind);
    cmd.addParam("p_MarketID", RSDBP_IN, p_MarketID);
    cmd.addParam("p_ImplKind", RSDBP_IN, p_ImplKind);
    cmd.execute();

    p_Code = cmd.value("p_Code");
    return p_Code;
end;

macro GetMicexCode(ContrID)
   var Dt = TRsbDataSet(" select t_mpcode from ddlcontrmp_dbt where t_sfcontrid = " + ContrID);
   if (Dt.movenext())
      return dt.mpcode;
   end;
   return "";
end;

/*DEF-32047*/
macro GetMicexCode_FB(ContrID)
   var Dt = TRsbDataSet(
      " SELECT T_CODE "
     +"   FROM DDLOBJCODE_DBT dlcode "
     +"  WHERE     dlcode.T_CODEKIND = " + DLCCK_USC
     +"        AND dlcode.T_OBJECTTYPE = " + OBJTYPE_BROKERCONTR_DL
     +"        AND dlcode.T_OBJECTID = (SELECT T_DLCONTRID "
     +"                                   FROM ddlcontrmp_dbt mp "
     +"                                  WHERE mp.T_SFCONTRID = " + ContrID +  ") "
     +"        AND (   dlcode.t_bankclosedate IS NULL "
     +"             OR dlcode.T_BANKCLOSEDATE = TO_DATE ('01.01.0001', 'DD.MM.YYYY')) "
   ); /*DEF-34787*/
   if (Dt.movenext())
      return dt.code;
   end;
   return "";
end;
/*DEF-32047*/

macro NeedCorrLimit( contract )
    var sfcontr = c_sfcontr(contract);

    if (sfcontr.isExchange()        /*только биржевые*/
        and (not sfcontr.isFuture()) /*по срочному не выгружаем*/
        and (not sfcontr.isAccY())  /*по договорам в спецдепо не выводим*/
       )
       return true;
    end;

    return false;

end;

private macro LimitCorOutGetSettings(filePath:@string, limitRub:@money, unloadCur:@string)
   var ErrCode;

   GetRegistryValue(REG_EXPORTCOR_PATH, V_STRING, filePath, ErrCode);
    if( ErrCode > 0 )
     filePath = "..\\QUIK_EXCHNG\\out";
    end;

   GetRegistryValue(REG_EXPORTCOR_LIMIT, V_DOUBLE, limitRub, ErrCode);
   if( ErrCode > 0 )
      msgbox("Не установлен лимит для выгрузки RUB. Будут выгружены все корректировки");
      limitRub = $9999999999999999999999.0;
   end;

   GetRegistryValue(REG_EXPORTCOR_CUR, V_DOUBLE, unloadCur, ErrCode);
   if( ErrCode > 0 )
      unloadCur = "SUR";
   end;
end;

private macro LimitCorOutBuildQuery():string
   var sql:string = "";

   sql = 
         "select t.*                                            " +
         "  from ddl_limitadjust_dbt t                          " +
         " where t_market = 1 and t_id_step = -99              " +
         "   and t_date = :dt " +
         " order by t_id_oper desc , t_client_code, t_curr_code, t_limit_kind";

   return sql;
end; // LimitCorOutBuildQuery

macro LimitCorOutInternal( dt )
   var StrPath :string = "",
       limitRow :string = "",
       prev_id_oper = 0,
       limit_rub = $0.0, unload_cur = "", i_lim = $0.0, overlimit = $0.0, unloaded = 0, isOverLimitMsgPrint = true;
   var operState = 0;
   var cnt = 1; 
   var hasLines = false;
   var isNextOperation:bool;

   var v_email_processor, emailText = "";

   //получим значение пути выгрузки из реестра
   LimitCorOutGetSettings(@StrPath, @limit_rub, @unload_cur);

   //makeFile
   MakeDir(string(StrPath));
   MakeDir(string(StrPath+"\\", "Archive"));
   var fullf = StrPath+"\\limits.lci";
   var fileStream = TStreamDoc(fullf, "C");

   //getData
   var query = LimitCorOutBuildQuery();
   var cmd = RsdCommand(query);
   cmd.addParam("dt", RSDBP_IN, dt);
   cmd.Execute();
   var data = trsbdataset(cmd);
   
   initprogress(-1, "Выгрузка в файл", "Выгрузка в файл : " + fullf);

   while (data.movenext())
      hasLines = true;
      isNextOperation = (prev_id_oper == 0) or (prev_id_oper != data.Id_Oper);
      prev_id_oper = data.id_oper;
      
      if  (data.limit_type == "DEPO")
         /* LIMIT_TYPE = DEPO; LIMIT_ID = 11616550; FIRM_ID = MB0134700000; SECCODE = USD000UTSTOM; CLIENT_CODE = CU413098; OPEN_BALANCE = 0.00;
            OPEN_LIMIT = 0.00; CURRENT_LIMIT = 0.00; LIMIT_OPERATION = CORRECT_LIMIT; LIMIT_KIND = 0; LEVERAGE = 0.00;*/
         limitRow = 
                    "LIMIT_TYPE = "     +data.LIMIT_TYPE  + 
                  "; LIMIT_ID = "       +data.LIMITID     +
                  "; FIRM_ID = "        +data.FIRM_ID     +
                  "; SECCODE = "        +data.seccode     +
                  "; CLIENT_CODE = "    +data.client_code +
                  "; OPEN_BALANCE = "   +trim(string(data.open_balance:0:0))+
                  "; OPEN_LIMIT = "     +trim(string(data.open_limit:0:0))  +
                  "; CURRENT_LIMIT = "  +trim(string(data.current_limit:0:0))+
                  "; LIMIT_OPERATION = "+data.limit_operation+
                  "; LIMIT_KIND = "     +data.limit_kind;

         limitRow = limitRow + "; TRDACCID = " + data.trdaccid + ";";
      end;

      if (data.isblocked == "X")
         println( "Блокировка: " + limitRow );
         continue;
      end;

      if(isNextOperation) 
         unloaded = unloaded + 1;
         useprogress(unloaded);
      end;

      fileStream.WriteLine( limitRow );
      println( limitRow );
   end;

   fileStream = null;

   if (not hasLines)
     RemoveFile(fullf);
   end;

   RemProgress();

   if (ExistFile("$"+fullf))
      var day, month, year;
      var hh, mm, ss;
      DateSplit(date(), day, month, year);
      TimeSplit(Time(), hh, mm, ss);

      CopyFile("$"+fullf, "$"+StrPath+"\\Archive\\limits"+string(dt:fileStream)+"_"+string(day:2:0:o)+string(month:2:0:o)+string(year:0:o)+string(hh:2:0:o)+string(mm:2:0:o)+string(ss:2:0:o)+".lci");
   end;
   if (hasLines)
      msgbox(fullf+"| Выгружено: " + unloaded);
   else
      msgbox("Файл не сформирован| Выгружено: " + unloaded);
   end;
end; //LimitCorOutInternal


/**
  @brief    Создать корректировку лимитов 
*/


macro InsertLimitAdjust677(ContrId, LimitKind, OpenBalance, Fiid, LimitDate, ID_Operation, LimitOperation, err:@string)

  private var limitadjust = TRecHandler("dl_limitadjust.dbt");
  private var _dockind = -1;

  private macro CreateLimitAdJust(limitadjust)
     var sql = 
          "DECLARE " +
          "   p_LimitAdj             DDL_LIMITADJUST_DBT%ROWTYPE; " +
          "   UnknownDate   CONSTANT DATE := TO_DATE ('01.01.0001', 'DD.MM.YYYY'); " +
          "   UnknownTime   CONSTANT DATE := TO_DATE ('01.01.0001 00:00:00', 'DD.MM.YYYY HH24:MI:SS'); " +
          "BEGIN " +
          "   p_LimitAdj.t_ID              := 0; " +
          "   p_LimitAdj.T_LIMITID         :=  USR_REQ677LIMITID_SEQ.NextVal; " +
          "   p_LimitAdj.T_LIMIT_KIND       := NVL (:T_LIMIT_KIND, 0); " +
          "   p_LimitAdj.T_DATE            := NVL (:T_DATE, UnknownDate); " +
          "   p_LimitAdj.T_TIME            := NVL (:T_TIME, UnknownTime); " +
          "   p_LimitAdj.T_MARKET          := NVL (:T_MARKET, -1); " +
          "   p_LimitAdj.T_CLIENT          := NVL (:T_CLIENT, 0); " +
          "   p_LimitAdj.T_INTERNALACCOUNT := NVL (:T_INTERNALACCOUNT, 0); " +
          "   p_LimitAdj.T_LIMIT_TYPE      := NVL (:T_LIMIT_TYPE, CHR (1)); " +
          "   p_LimitAdj.T_FIRM_ID         := NVL (:T_FIRM_ID, CHR (1)); " +
          "   p_LimitAdj.T_CLIENT_CODE     := NVL (:T_CLIENT_CODE, CHR (1)); " +
          "   p_LimitAdj.T_OPEN_BALANCE    := NVL (:T_OPEN_BALANCE, 0); " +
          "   p_LimitAdj.T_OPEN_LIMIT      := NVL (:T_OPEN_LIMIT, 0); " +
          "   p_LimitAdj.T_CURRENT_LIMIT   := NVL (:T_CURRENT_LIMIT, 0); " +
          "   p_LimitAdj.T_LIMIT_OPERATION := NVL (:T_LIMIT_OPERATION, CHR (1)); " +
          "   p_LimitAdj.T_TRDACCID        := NVL (:T_TRDACCID, CHR (1)); " +
          "   p_LimitAdj.T_SECCODE         := NVL (:T_SECCODE, CHR (1)); " +
          "   p_LimitAdj.T_TAG             := NVL (:T_TAG, CHR (1)); " +
          "   p_LimitAdj.T_CURRID          := NVL (:T_CURRID, -1); " +
          "   p_LimitAdj.T_CURR_CODE       := NVL (:T_CURR_CODE, CHR (1)); " +
  //        "   p_LimitAdj.T_LIMIT_KIND      := NVL (:T_LIMIT_KIND, 0); " +
          "   p_LimitAdj.T_LEVERAGE        := NVL (:T_LEVERAGE, 0); " +
          "   p_LimitAdj.T_ID_OPER         := NVL (:T_ID_OPER, 0); " +
          "   p_LimitAdj.T_ID_STEP         := NVL (:T_ID_STEP, 0); " +
          "   p_LimitAdj.T_ISBLOCKED       := NVL (:T_ISBLOCKED, CHR (0)); " +
          " " +
          "   rshb_rsi_sclimit.RSI_CreateLimitAdJust (p_LimitAdj, :p_id_operation, :p_id_step); " +
          "END; " ;

     var cmd = RsdCommand(sql);
     //    cmd.addParam("T_LIMITID         ",RSDBP_IN, limitadjust.rec.LIMITID        );
         cmd.addParam("T_LIMIT_KIND       ",RSDBP_IN, limitadjust.rec.LIMIT_KIND      );
         cmd.addParam("T_DATE            ",RSDBP_IN, limitadjust.rec.DATE           );
         cmd.addParam("T_TIME            ",RSDBP_IN, limitadjust.rec.TIME           );
         cmd.addParam("T_MARKET          ",RSDBP_IN, limitadjust.rec.MARKET         );
         cmd.addParam("T_CLIENT          ",RSDBP_IN, limitadjust.rec.CLIENT         );
         cmd.addParam("T_INTERNALACCOUNT ",RSDBP_IN, limitadjust.rec.INTERNALACCOUNT);
         cmd.addParam("T_LIMIT_TYPE      ",RSDBP_IN, limitadjust.rec.LIMIT_TYPE     );
         cmd.addParam("T_FIRM_ID         ",RSDBP_IN, limitadjust.rec.FIRM_ID        );
         cmd.addParam("T_CLIENT_CODE     ",RSDBP_IN, limitadjust.rec.CLIENT_CODE    );
         cmd.addParam("T_OPEN_BALANCE    ",RSDBP_IN, limitadjust.rec.OPEN_BALANCE   );
         cmd.addParam("T_OPEN_LIMIT      ",RSDBP_IN, limitadjust.rec.OPEN_LIMIT     );
         cmd.addParam("T_CURRENT_LIMIT   ",RSDBP_IN, limitadjust.rec.CURRENT_LIMIT  );
         cmd.addParam("T_LIMIT_OPERATION ",RSDBP_IN, limitadjust.rec.LIMIT_OPERATION);
         cmd.addParam("T_TRDACCID        ",RSDBP_IN, limitadjust.rec.TRDACCID       );
         cmd.addParam("T_SECCODE         ",RSDBP_IN, limitadjust.rec.SECCODE        );
         cmd.addParam("T_TAG             ",RSDBP_IN, limitadjust.rec.TAG            );
         cmd.addParam("T_CURRID          ",RSDBP_IN, limitadjust.rec.CURRID         );
         cmd.addParam("T_CURR_CODE       ",RSDBP_IN, limitadjust.rec.CURR_CODE      );
  //       cmd.addParam("T_LIMIT_KIND      ",RSDBP_IN, limitadjust.rec.LIMIT_KIND     );
         cmd.addParam("T_LEVERAGE        ",RSDBP_IN, limitadjust.rec.LEVERAGE       );
         cmd.addParam("T_ID_OPER         ",RSDBP_IN, limitadjust.rec.ID_OPER        );
         cmd.addParam("T_ID_STEP         ",RSDBP_IN, limitadjust.rec.ID_STEP        );
         cmd.addParam("T_ISBLOCKED       ",RSDBP_IN, limitadjust.rec.ISBLOCKED      );
         cmd.addParam("p_id_operation    ",RSDBP_IN, limitadjust.rec.ID_OPER        );
         cmd.addParam("p_id_step         ",RSDBP_IN, limitadjust.rec.ID_STEP        );
     cmd.Execute();

  end;


  private macro nvl(val, retval)
    if (ValType(Val) == V_UNDEF)
       return retval;
    else
       return val;
    end;
  end;
    
  private macro GetFiCode(Curr, fi_kind)
     var Dt = TRsbDataSet(" select decode(t_fi_kind,1,decode(t_ccy,'RUB','SUR',t_ccy),t_code) curr_code,  t_fi_kind from dfininstr_dbt f " + 
                          " left join dobjcode_dbt a on a.t_objectid = f.t_fiid and a.t_codekind = 11 and t_objecttype = 9 where t_fiid = " + Curr + 
                          " and t_fi_kind = " + fi_kind );
     if (Dt.movenext())
        return dt.curr_code;
     end;
     return "";
  end;
  
  private macro GetCurDepoCode(Curr)
     var Dt = TRsbDataSet(" select t_code from  " + 
                          " dobjcode_dbt where t_objectid =  "+curr+" and t_codekind = 106 and t_objecttype = 9 and t_state = 0 " );
     if (Dt.movenext())
        return dt.code;
     end;
     return "";
  end;

  private macro GetDocKind(id_operation)
     if (_dockind == -1)
        var Dt = TRsbDataSet(" select t_dockind from doproper_dbt where t_id_operation = " + id_operation);
        if (Dt.Movenext())
           _dockind = Dt.dockind;
        else
           _dockind = 0;
        end;
     end;
     return _dockind;
  end;

  /* DEF-62480, код участника торгов, код позиции и торговый счет получаем из параметров расчета лимитов 
   */
  private macro GetLimitPrm( p_SfContrID: INTEGER, p_LimitDate: DATE, p_FirmID:@STRING, p_Tag:@STRING, p_TrdAcc:@STRING )

    var sql, cmd;
   
    sql =  "DECLARE "
         + "BEGIN "
         + "    RSHB_RSI_SCLIMIT.GetLimitPrm(:SfContrID, :LimitDate, :FirmID, :Tag, :TrdAcc); "
         + "END; "
    ;
      
    cmd = RSDCommand(sql);
    cmd.addParam("SfContrID", RSDBP_IN, p_SfContrID);
    cmd.addParam("LimitDate", RSDBP_IN, p_LimitDate);
    cmd.addParam("FirmID", RSDBP_OUT, V_STRING);
    cmd.addParam("Tag", RSDBP_OUT, V_STRING);
    cmd.addParam("TrdAcc", RSDBP_OUT, V_STRING);
    cmd.execute();

    p_FirmID = cmd.value("FirmID");
    p_Tag = cmd.value("Tag");
    p_TrdAcc = cmd.value("TrdAcc");
  end; // GetLimitPrm()

  var _dt;
  var sfcontr = c_sfcontr(ContrID);
  var limitprm = TRsbDataSet(" select * from ddl_limitprm_dbt where t_marketkind = 3");/*для валютного*/
  var existsCurParm = limitprm.movenext();
  var sfcontrIsEDP = sfcontr.isEdp();




  if (sfcontrIsEDP)
     limitadjust.rec.client_code = GetMicexCode_FB(ContrID)
  else
     limitadjust.rec.client_code = GetMicexCode(ContrID);
  end;

  if (limitadjust.rec.client_code == "")
     err = "Не удалось получить Код клиента на Бирже. "+sfcontr.rec.number;
     return false;
  end;

   
  limitadjust.rec.open_balance    = OpenBalance;  
  limitadjust.rec.limit_kind      = LimitKind;
  limitadjust.rec.limit_Type      = "DEPO";
  limitadjust.rec.date            = LimitDate;
  //limitadjust.rec.time          = time();
  limitadjust.rec.market          = 1;
  limitadjust.rec.client          = sfcontr.rec.partyID;
  limitadjust.rec.curr_code       = GetFiCode(Fiid, 1);
  limitadjust.rec.currid          = Fiid;
  if (sfcontr.IsCurMarket() and (OpenBalance == 0) and (fiid == -1) and (LimitKind == "0") )  // DEF-68258
     limitadjust.rec.seccode         = GetCodeSCZeroLimit(3, 2, 1);
  elif (sfcontr.IsCurMarket()) 
     limitadjust.rec.seccode         = GetCurDepoCode(Fiid);
  else
     limitadjust.rec.seccode         = GetFiCode(Fiid, 2);
  end;
  if ((limitadjust.rec.seccode == ""))
     err = "Не удалось получить Код ценной бумаги на МБ. ID инструмента = "+ Fiid;
     return false;
  end;
  limitadjust.rec.Limit_Operation = nvl(LimitOperation,"CORRECT_LIMIT");

  limitadjust.rec.Firm_ID         = "MC0134700000";
  
  /*DEF-32047*/
  if (sfcontrIsEDP)
      limitadjust.rec.Firm_ID     = "MC0134700000";
  end; 
  /*DEF-32047*/

  /* DEF-62480, код участника торгов, код позиции и торговый счет получаем из параметров расчета лимитов  */
  GetLimitPrm( ContrID, LimitDate, @limitadjust.rec.Firm_ID, @limitadjust.rec.Tag, @limitadjust.rec.trdaccid );

  //Костыль. GetLimitPrm не должна пересчитывать тэг, это ошибка. В качестве временной затычки тег перезапишем. Далее последует полноценное решение
  limitadjust.rec.Tag = "";
  if (sfcontr.GetMarketId() == GetSpbexID())
    limitadjust.rec.Tag = sfcontr.GetTagFromNote(LimitDate);
  end;
  if ((limitadjust.rec.Tag == "") and (sfcontr.IsCurMarket() and (not sfcontrIsEDP)))
    limitadjust.rec.Tag = "RTOD";
  end;
  if (limitadjust.rec.Tag == "")
    limitadjust.rec.Tag = "EQTV";
  end;
  
  limitadjust.rec.isBlocked       = sfcontr.IsBlocked(LimitDate);

  limitadjust.rec.leverage = -1;

  
  limitadjust.rec.id_oper = id_operation;
  limitadjust.rec.id_step = -99;
  CreateLimitAdJust(limitadjust);

  return true;
end;




