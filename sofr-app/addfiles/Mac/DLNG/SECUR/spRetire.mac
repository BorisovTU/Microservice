/*
$Name:         spRetire.mac
$Module:       Ценные бумаги
$Description:  Обслуживание погашений ценных бумаг (купонов)
*/

/*
Programmed by: AV 05.10.01
*/

import rsd, rsbdataset, Payments, Globals, secinter, "sp_categ.mac", "spcomcalc.mac";
import "calcndr.mac";
/* Контексты, в которых вызвана функция "Проверить_Сделку" (параметр Mode) */
private const modeInsert        =  1, /* Функция вызвана при вставке новой сделки */
      modeUpdate        =  2, /* -"- при обновлении сделки */
      modeDelete        =  3, /* -"- при удалении сделки */
      modeBeginOper     =  4, /* -"- перед началом операции для сделки */
      modeMoveToPrep    =  5, /* -"- при переводе сделки в отложенные */

      modePreInsert     =  9, /* -"- перед вызовом панели сделки для вставки сделки по F9 */
      modePreUpdate     =  6, /* -"- перед вызовом панели сделки для редактирования\просмотра по Enter */
      modeAfterInsert   =  7, /* -"- после выхода из панели вставки сделки. Если вставка была выполнена, DealID будет не нулевой */
      modeAfterUpdate   =  8, /* -"- после выхода из панели редактирования сделки. Обновление сделки и платежей уже выполнено */
      modeAfterDelete   = 10; /* -"- после успешного удаления сделки. */

private record Сделка("dl_tick");
private record СтараяСделка("dl_tick");

private record УсловияСделки("dl_leg");
private record СтарыеУсловияСделки("dl_leg");

private var УсловияСделки_Обр; /*не используется, но нужна для компиляции spop_in.mac, т.к. 
                         макрос инициализации общий для сделок и погашений*/

/*проверка - включен ли режим хранилища данных для НУ*/
private macro CheckTaxDepositoryMode()
   var ErrCode, DepMode;

   GetRegistryValue( "SECUR\\РЕЖИМ ХРАНИЛИЩА ДАННЫХ ДЛЯ НУ", V_BOOL, DepMode, ErrCode );

   return DepMode and (ErrCode == 0);
end;

private macro Новая_Сделка(Type:integer):integer
  return 0;
end;

private macro Проверить_документ(Type:integer):integer
/* Константы важности внесенных изменений:           */
/* CHANG_NOTIMPORTANT   - изменения неважные (сохранение без отката операции)*/
/* CHANG_IMPORTANT      - изменения важны (сохранение изменений возможно только при откате) */
/* CHANG_NOTKEEP        - не сохранять изменения */
/* Если возвращаемое значение  > 0, то это оно интерпритируется как номер поля с ошибочным параметром*/
/* Если возвращаемое значение  = 0, то cчитается, что проверка прошла успешно
   и сохранение изменений можно производить без отката операции      */
  return CHANG_NOTIMPORTANT;
end;

private macro ФункцияПользователя(Type:integer):integer

  private macro AddCol (ar,ind, fld, head, width, rdonly, ty, dp)
     ar.value (ind * 6)     = fld;
     ar.value (ind * 6 + 1) = head;
     ar.value (ind * 6 + 2) = width;
     ar.value (ind * 6 + 3 ) = rdonly;   // fldType
     ar.value (ind * 6 + 4 ) = dp;//   decPoint
     ar.value (ind * 6 + 5 ) = 0;   // reserv
  end;

  var FD = SPFirstDoc( Сделка );
if (FD.kind == 4832)
  var scr_sql  = "  SELECT ownhist.t_listid, " +
                 "         ownhist.t_ownerid, " +
                 "         pt.t_shortname, " +
                 "         CASE pt.t_legalform " +
                 "            WHEN 1 THEN 'Юр. лицо' " +
                 "            ELSE 'Физ. лицо' " +
                 "         END " +
                 "            AS T_personType, " +
                 "         PT.T_NOTRESIDENT, " +
                 "         scownlist.t_ListNum, " +
                 "         scownlist.t_ListDate, " +
                 "         ownhist.t_accnumber, " +
                 "         fininstr.t_FI_Code, " +
                 "         AVR.T_ISIN, " +
                 "         FININSTR.T_DEFINITION, " +
                 "         ownhist.t_total, " +
                 "         OWNTAX.T_PAYSUM, " +
                 "         OWNTAX.T_TAXSUM, " +
                 "         OWNTAX.T_TAXRATE, " +
                 "         OWNTAX.T_FACTRECEIVERID, " +
                 "         rsb_secur.GetDealOperName (ownhist.t_Oper) AS t_oper, " +
                 "         op.\* " +
                 "    FROM dscownhist_dbt ownhist, " +
                 "         dfininstr_dbt fininstr, " +
                 "         davoiriss_dbt avr, " +
                 "         dscownlist_dbt scownlist, " +
                 "         dparty_dbt pt, " +
                 "         dscowntax_dbt owntax, " +
                 "         doproper_dbt op " +
                 "   WHERE     pt.t_partyid = ownhist.t_ownerid " +
                 "         AND FININSTR.T_FIID = AVR.T_FIID " +
                 "         AND OWNTAX.T_ID_OPERATION = OP.T_ID_OPERATION " +
                 "         AND OWNTAX.T_SHID = OWNHIST.T_ID " +
                 "         AND (    scownlist.t_ListID = ownhist.t_ListID " +
                 "              AND fininstr.t_FIID = scownlist.t_FIID " +
                 "              AND OP.T_docKIND = ?  " +
                 "              AND TO_NUMBER (OP.T_DOCUMENTID) = ? ) " +
                 "ORDER BY ownhist.t_id " ;



  var scr_cmd = RSDCommand(scr_sql);

      scr_cmd.AddParam("", rsdbp_in, FD.kind);
      scr_cmd.AddParam("", rsdbp_in, FD.id); 
  var scr_rs = RSDRecordset(scr_cmd, rsdval_client, rsdval_static);
  var col = TArray();
  
  AddCol (col, 0, "t_ownerid",    "ID Субъекта",   5 ,  0,0);   
  AddCol (col, 1, "t_shortname",    "ФИО",   30 ,  0,0);
  AddCol (col, 2, "T_personType",    "Юр.форма",   10 ,  0,0);
  AddCol (col, 3, "T_NOTRESIDENT",    "Нерезидент",   7 ,  0,0);
  AddCol (col, 4, "t_ListNum",    "Номер списка",   10 ,  0,0);
  AddCol (col, 5, "t_ListDate",    "Дата списка",   10 ,  0,0);
  AddCol (col, 6, "t_accnumber",    "Счет",   10 ,  0,0);   
  AddCol (col, 7, "t_FI_Code",    "Код Ц/Б",   10 ,  0,0);
  AddCol (col, 8, "T_ISIN",    "ISIN Ц/Б",   10 ,  0,0);
  AddCol (col, 9, "T_DEFINITION",    "Наименование Ц/Б",   10 ,  0,0);
  AddCol (col, 10, "t_total",    "Количество Ц/Б",   10 ,  0,0); 
  AddCol (col, 11, "T_PAYSUM",    "Рас. сумма",   10 ,  0,0); 
  AddCol (col, 12, "T_TAXSUM",    "Рас. налог",   10 ,  0,0); 
  AddCol (col, 13, "T_TAXRATE",    "Н.ставка",   10 ,  0,0); 



  RunScroll(scr_rs, 14, col, "Результаты расчета по операции", null, "Результаты расчета по операции", "Результаты расчета по операции", true );

end;

  return 0;
end;

PRIVATE MACRO GetCCY(fiid)
  var ccy = "", cmd, ds;

  cmd = RSDCommand(" select t_CCY from dfininstr_dbt where t_FIID = ? ");
  cmd.addParam("", RSDBP_IN, fiid);
  cmd.execute();

  ds = TRsbDataSet(cmd);
  if( ds.MoveNext() )
     ccy = ds.ccy;
  end;
     
  return ccy;
END;

private macro Проверить_Сделку(Type:integer,Mode:integer, IsWeb:bool):integer

  var  FD, ptoffice, Group, ContrNum = ""; 

  if( Mode == modePreInsert )

     return 0;

  elif( Mode == modePreUpdate )

     return 0;

  elif( Mode == modeBeginOper )

     FD = SPFirstDoc( Сделка );

     if( FI_GetStatus( УсловияСделки.PFI, Сделка.DealDate ) == FI_STATE_CLOSE )
        MsgBox( "Ценная бумага " + ПолучитьКодФинИн( УсловияСделки.PFI ) + " закрыта.|Запрещено проведение операции." );
        return 1;
     end;

     if( not FD.CheckCliring( true ) ) 
        return 1;
     end;

     if( Сделка.BofficeKind == DL_RETIREMENT_DST )
     /*Проверки для распределений*/
        return 0;
     elif( Сделка.BofficeKind == DL_RETIREMENT_OWN )
     /*Проверки для погашений СЭБ*/
        return 0;
     else
     /*Проверки для погашений*/

        if( НужнаПеренумерацияСделокЗаДату(Сделка.DealDate) )
           if( MsgBoxEx("Требуется выполнить перенумерацию сделок в дате " + string(Сделка.DealDate:f) + ". Продолжить выполнение без перенумерации?", MB_YES+MB_NO, IND_NO) != IND_YES )
              return 1;
           end;
        end;
        
        var DecisionDate = date(0,0,0), NonRefundActive = false;
        DecisionDate = readNoteForObject( OBJTYPE_AVOIRISS, L_Z( Int(УсловияСделки.PFI), 10 ), 108 );
        if ((ValType(DecisionDate) == V_DATE) and (DecisionDate != date(0,0,0)))
          NonRefundActive = true;
        end;
        
        if(NonRefundActive and (УсловияСделки.PayFIID != NATCUR))
          if (not (GetTrue(true, "Выполняется погашение для невозмещаемых активов. Валютой платежа должны быть рубли.\n Сейчас указана валюта платежа " + GetCCY(УсловияСделки.PayFIID) + ". Вы желаете продолжить выполнение операции?")))
             return 1;
          end;
        end;

        Group = SP_GetOperationGroup ( Сделка); 

        /*для биржевых сделок дополнительные проверки*/
        if( Сделка.Flag1 == SET_CHAR )  

            if( Сделка.MarketID < 0 )
               msgbox( "Не задан организатор торговли" );      
               return 1; 
            end; 
            /*проверка сектора организатора торговли*/
            ptoffice = TBFile("ptoffice.dbt");
            ptoffice.KeyNum = 0;
            ptoffice.rec.PartyID = Сделка.MarketID;
            if (GetGE(ptoffice) AND
                (ptoffice.rec.PartyID == Сделка.MarketID) AND
                (Сделка.MarketOfficeID == 0))
              msgbox("Поле \"Сектор\" должно быть заполнено");
              return 1; 
            end; 
        elif( Сделка.Flag4 == SET_CHAR )/* брокерские сделки*/
            if(Сделка.BrokerID < 0)
               msgbox( "Для операции погашения через брокера необходимо задать поле Брокер" );
               return 1; 
            end; 
        end;

        if( (not IsClientDeal(Сделка)) OR (Сделка.BofficeKind == DL_TRUSTRETIREMENT) )
           Сделка.PortfolioID = -1;
        else
           Сделка.PortfolioID = KINDPORT_CLIENT;
        end;
     end;

  elif( Mode == modeDelete )
     /*проверки при удалении сделки в режиме хранилища данных*/
     if( CheckTaxDepositoryMode() AND (Сделка.BofficeKind != DL_RETIREMENT_DST) )
        if(not IsWeb)
           FD = SPFirstDoc( Сделка );
           if (IsRET_ISSUE(FD.Group))//только в погашении выпуска есть поставка
              return IIF(SP_CheckTaxDatePreDel(FD.GetRQ(DLRQ_TYPE_DELIVERY, NULL, NULL, false, false)),0,1);
           else
              return 0;
           end;
        else //для веб реализована своя аналогичная проверка
           return 0;
        end;
     else
        return 0;
     end;

  elif( Mode == modeAfterInsert )

     return 0;

  elif( Mode == modeAfterUpdate )
     if( Сделка.BofficeKind == DL_RETIREMENT_OWN )
        /*Проверки для погашений СЭБ*/
        return 0;
     end;
  
     if( CheckTaxDepositoryMode() == true )
        Group = SP_GetOperationGroup (Сделка); 
        if (IsRET_ISSUE(Group))
           if (((СтараяСделка.MarketID != Сделка.MarketID) or 
                (СтараяСделка.BrokerID != Сделка.BrokerID) or 
                (СтараяСделка.PartyID != Сделка.PartyID)
               ) and 
               (Сделка.MarketID < 0) and 
               (Сделка.BrokerID < 0) and 
               (Сделка.PartyID < 0)
              )
              msgbox( "В операции погашения должны быть заданы поля Платежный агент и Депозитарий" );

           elif (((СтараяСделка.MarketID != Сделка.MarketID) or 
                  (СтараяСделка.BrokerID != Сделка.BrokerID)
                 ) and 
                 (Сделка.MarketID < 0) and 
                 (Сделка.BrokerID < 0)
                )
              msgbox( "В операции погашения должно быть задано поле Платежный агент" );

           elif ((СтараяСделка.PartyID != Сделка.PartyID) and (Сделка.PartyID < 0))
              msgbox( "В операции погашения должно быть задано поле Депозитарий" );
           end; 
        else
           if (((СтараяСделка.MarketID != Сделка.MarketID) or 
                (СтараяСделка.BrokerID != Сделка.BrokerID)
               ) and 
               (Сделка.MarketID < 0) and 
               (Сделка.BrokerID < 0)
              )
              msgbox( "В операции погашения должно быть задано поле Платежный агент" );
           end; 
        end; 
     end; 

     return 0;

  elif( Mode == modeAfterDelete )

     return 0;

  end; 

  return 0;
end;

/* Установка подсказки для скролингов из макроса */
PRIVATE MACRO УстановитьПодсказку( TableName:string, IndexNum:integer, DefaultHint:string, ScrolStatus:integer, ScrolKind:integer ):string
  //  Возможные значения ScrolStatus:
  //0 //SC_vmNotClosed, 
  //1 //SC_vmClosed, 
  //2 //SC_vmNotOperated,
  //3 //SC_vmAll


  // Возможные значения ScrolKind:
  // DL_RETIREMENT     = 117,   //Погашение выпуска
  // DL_RETIREMENT_DST = 130,   // Распределение суммы погашение
  // DL_TRUSTRETIREMENT= 940,   // погашение ц/б ДУ

  //пример
  //return "/*+FIRST_ROWS LEADING(t) INDEX(t ddl_tick_dbt_idx0)*/";

  return DefaultHint;

END;
