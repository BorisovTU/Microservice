/*
$Name:        BrokerPrognos_Form.mac
$Module:      Ценные бумаги
$Description: Отчет брокера по проведенным операциям. 
              Форма ввода параметров выпуска отчета.
*/

import "DlRepForm_h.mac";

private const PNFLD_BROKERMARKET_DPRTCODE      = 0,
              PNFLD_BROKERMARKET_DPRTNAME      = 1, 
              PNFLD_BROKERMARKET_REPORTDATE    = 2,
              PNFLD_BROKERMARKET_OPERATIONDATE = 3,
              PNFLD_BROKERMARKET_CLNTCODE      = 4,
              PNFLD_BROKERMARKET_CLNTNAME      = 5,
              PNFLD_BROKERMARKET_CONTRACT      = 6,
              PNFLD_BROKERMARKET_PROCMARKET    = 7,
              PNFLD_BROKERMARKET_PROCOUTMARKET = 8,
              PNFLD_BROKERMARKET_APOSTROF      = 9,
              PNFLD_BROKERMARKET_ISEXCEL       = 10;

private macro Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
end;

/* Обработчик клиента */
private macro FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var Party    = TRecHandler("party.dbt");
  var partcode = TBFile( "partcode.dbt",  "R", 0 );
  var saveFldRealValue = FldRealValue;
  if( Cmd == DLG_INIT )
    if( FldRealValue == null )
      FldRealValue = -1;
    end;
    if( FldRealValue <= 0 )
      FldShowValue = "";
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, STR_ALLVALUE);
    else
      partcode.Clear();
      partcode.rec.PartyID  = FldRealValue;
      partcode.rec.CodeKind = 1;
      if (partcode.GetEQ())
       FldShowValue = partcode.rec.Code;
       if( ПолучитьСубъекта( FldRealValue, Party) == 0 )
         pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName );
       end;
      end;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
    FldRealValue = -1;
    FldShowValue = "";
    pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    Party = TRecHandler("party.dbt");
    if( ListPT( Party, 1, "", PTLIST_STOCKDLCLIENT, PTCK_ALL) == true ) // было PTLIST_STOCKDLCLIENT или PTLIST_ALLCLIENT
      FldRealValue = Party.rec.PartyID;
      partcode.Clear();
      partcode.rec.PartyID  = Party.rec.PartyID;
      partcode.rec.CodeKind = 1;
      if (partcode.GetEQ())
        FldShowValue = partcode.rec.Code;
      end;
      pThis.SetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_NAME, Party.rec.ShortName );
    end;
  end;

  if (saveFldRealValue != FldRealValue)
    if (pThis.GetLinkedValue(DL_PNFLD_CONTRACT_OFBU) != -1)
      pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, -1);
    end;
  end;
  return CM_DEFAULT;
end;

/* Листалка договоров */
private macro ListSfContrByServKind( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER, PartyID:@INTEGER, ServKind:TArray, Department:@integer ):BOOL
  VAR Choice, ServCond = "", i = 0, flag = false; 
  VAR Select;
 
  if( (ServKind != null) and (ServKind.Size>0) )
    while( i < ServKind.Size )
      if( flag == true )
        ServCond = ServCond + ",";
      else
        flag = true;
      end;

      ServCond = ServCond + string(ServKind[i]);
      i = i + 1;
    end;
  else
    ServCond = 0;
  end;

  Select = "select sfc.t_id, sfc.t_DateConc, sfc.t_Name, sfc.t_Number, prt1.t_ShortName Payer, prt2.t_ShortName Contractor, prt1.t_PartyID as t_PartyID, sfc.t_Department as t_Department" + 
               "  from dsfcontr_dbt sfc, dparty_dbt prt1, dparty_dbt prt2 "+
               " where sfc.t_ServKind in(" + ServCond + ")" +
               "   and prt1.t_PartyID = sfc.t_partyID " +
               "   and prt2.t_PartyID = sfc.t_ContractorID ";

  if( PartyID > 0 )
    Select = Select + " and sfc.T_PARTYID = " + PartyID;
  end;

//  if( (ContractorID != null) and (ContractorID > 0) )
//      Select = Select + " and sfc.T_ContractorID = " + ContractorID;
//  end;

  if( (Department != null) and (Department > 0) )
    Select = Select + "   and sfc.t_Department = "+string(Department)+
                      "   and sfc.t_Branch = 0";
  end;

  Choice = DL_Scroll(Select,
                     "Договора обслуживания", X, Y,
                     "t_Number","№ договора","t_Name","Наименование договора","t_DateConc","Дата закл.",
                     "Payer","Плательщик","Contractor","Контрагент"
                    );

  if( Choice != NULL )
    FldRealValue = Choice.Value("t_id"        ); 
    FldShowValue = Choice.Value("t_Number"    );
    PartyID      = Choice.Value("t_PartyID"   );
    Department   = Choice.Value("t_Department");
  end;
end;

/* Обработчик договра */
private macro FldProc_Contract_BO_Dep( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var ClientName = "";
  var DepCode = pThis.GetLinkedValue( DL_PNFLD_DEPARTCODE );
  var ClientID = pThis.GetLinkedValue( DL_PNFLD_CLIENT_STOCKDL_CODE );
  var ServKind = TArray();

  if( Cmd == DLG_INIT )
    if( FldRealValue == null )
      FldRealValue = -1;
    end;
    if( FldRealValue <= 0 )
      FldShowValue = STR_ALLVALUE;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
    FldRealValue = -1;
    FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    ServKind[ServKind.Size] = PTSK_STOCKDL;
    ListSfContrByServKind( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @ClientID, ServKind, @DepCode );
    pThis.SetLinkedValue(DL_PNFLD_DEPARTCODE, DepCode);
    pThis.SetLinkedValue(DL_PNFLD_CLIENT_STOCKDL_CODE, ClientID);
  end;
  return CM_DEFAULT;
end;
/* Обработчик филиала */
private macro FldProc_DepartCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var saveFldRealValue = FldRealValue;
  var stat = DL_FldProc_DepartCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );

  if (saveFldRealValue != FldRealValue)
    if (pThis.GetLinkedValue(DL_PNFLD_CONTRACT_OFBU) != -1)
      pThis.SetLinkedValue(DL_PNFLD_CONTRACT_OFBU, -1);
    end;
  end;
  return stat;
end;

/* Обработчик отчетной даты */ 
private macro FldProc_ReportDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
    var choiceDate = GetDateByCalendar(FldRealValue);
    if (choiceDate > date())
      msgbox("Отчетная дата не может превышать системную.");
    else       
      FldRealValue = FldShowValue = choiceDate;
    end;
  elif( (Cmd == DLG_KEY) AND (Key != 27/*Esc*/) )
    if (FldShowValue > date())
      msgbox("Отчетная дата не может превышать системную.");
      return CM_IGNORE;
    else       
      FldRealValue = FldShowValue;
    end;
  end;
  return CM_DEFAULT;
end;

/* ***************************************** */
/* Форма ввода параметров отчета             */
/* ***************************************** */
class (Dl_CPanel) СBrokerMarket_Panel()
  var Department:integer = -1,        // Филиал
      ReportDate:date    = {curdate}, // Отчетная дата
      ClientID:integer   = -1,        // Клиент
      CntrID:integer     = -1,        // Договор
      IsMarket:bool      = true,      // на бирже?
      IsOutMarket:bool   = false,     // на внебирж.?
      IsApp:bool         = true,      // апострофы?
      IsExcel:bool       = true;      // в Экселе?
  
  private macro Init()
    AddFieldProc( 0, PNFLD_BROKERMARKET_DPRTCODE,      DL_PNFLD_DEPARTCODE,          @FldProc_DepartCode,      -1 );
    AddFieldProc( 0, PNFLD_BROKERMARKET_DPRTNAME,      DL_PNFLD_DEPARTMENT,          @DL_FldProc_Department,   -1 );
    AddFieldProc( 0, PNFLD_BROKERMARKET_REPORTDATE,    DL_PNFLD_ENDDATE,             @FldProc_ReportDate,      ReportDate );
    AddFieldProc( 0, PNFLD_BROKERMARKET_OPERATIONDATE, DL_PNFLD_ENDDATE,             @Default,                 ReportDate );
    AddFieldProc( 0, PNFLD_BROKERMARKET_CLNTCODE,      DL_PNFLD_CLIENT_STOCKDL_CODE, @FldProc_Client,          ClientID   );
    AddFieldProc( 0, PNFLD_BROKERMARKET_CLNTNAME,      DL_PNFLD_CLIENT_STOCKDL_NAME, @Default,                 ClientID   );
    AddFieldProc( 0, PNFLD_BROKERMARKET_CONTRACT,      DL_PNFLD_CONTRACT_OFBU,       @FldProc_Contract_BO_Dep, CntrID     );
    AddFieldProc( 0, PNFLD_BROKERMARKET_PROCMARKET,    DL_PNFLD_CHECKBOX,            @Default/*DL_FldProc_CheckBox*/, "X");
    AddFieldProc( 0, PNFLD_BROKERMARKET_PROCOUTMARKET, DL_PNFLD_CHECKBOX,            @Default/*DL_FldProc_CheckBox*/, "" );
    AddFieldProc( 0, PNFLD_BROKERMARKET_APOSTROF,      DL_PNFLD_CHECKBOX,            @DL_FldProc_CheckBox,            "X");
    AddFieldProc( 0, PNFLD_BROKERMARKET_ISEXCEL,       DL_PNFLD_EXCEL,               @DL_FldProc_CheckBox,            "X");
  end;

  private macro GetFieldValue(fldNum:integer):variant
    var showValue:variant, fldValue:variant;

    GetFieldValue(fldNum, @showValue, @fldValue);
    return fldValue;
  end;

  macro Run()
    var stat = Run();
    Department = GetFieldValue(PNFLD_BROKERMARKET_DPRTCODE);
    ReportDate = GetFieldValue(PNFLD_BROKERMARKET_REPORTDATE);
    ClientID   = GetFieldValue(PNFLD_BROKERMARKET_CLNTCODE);
    CntrID     = GetFieldValue(PNFLD_BROKERMARKET_CONTRACT);
    IsMarket   = GetFieldValue(PNFLD_BROKERMARKET_PROCMARKET);
    IsOutMarket= GetFieldValue(PNFLD_BROKERMARKET_PROCOUTMARKET);
    IsApp      = GetFieldValue(PNFLD_BROKERMARKET_APOSTROF);
    IsExcel    = GetFieldValue(PNFLD_BROKERMARKET_ISEXCEL);

    return stat;
  end;

  initDL_CPanel("sp_rep.lbr", "BROMARKP");
end;