/*
$Name: nptxwrt050.mac
$Module: Ценные бумаги
$Description: Расчёт сумм для операций списания/зачисления денежных средств
*/

import InsCarryDoc, "sp_categ.mac", "sp_car.mac", RsbDataSet, BankInter, "nptxcalcfun.mac", "nptxwrt_func.mac";

private macro Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr );
  MsgBox( ErrStr );
  return 1;
end;

private macro ВозвратПереплатыВТечениеГода()
   var ErrCode, Mode;

   GetRegistryValue( "COMMON\\НДФЛ\\ВОЗВРАТ ПЕРЕПЛАТ В ТЕЧЕНИЕ ГОДА", V_BOOL, Mode, ErrCode );

   return Mode and (ErrCode == 0);
end;

private macro ContrIsIIS(ID:integer)
  var cmd, DataSet;
  var isIIS = UNSET_CHAR;

  cmd = DL_RSDCommand("select rsi_npto.CheckContrIIS(?) as IsIIS from dual");

  cmd.AddParam(ID);
  DataSet = cmd.execute();

  if(DataSet.moveNext())
    isIIS = int(DataSet.IsIIS);
  end;

  return IIF(isIIS == 1, SET_CHAR, UNSET_CHAR);
end;

macro ExecuteStep( kind_oper, FDoc, DocKind, ID_Operation, ID_Step )
  record nptxop("nptxop");
  var err = 0;
  var nptxopFile = TRecHandLer("nptxop.dbt");
  var Bg = $0.0, Bs = $0.0, Pm = $0, Pg = $0.0, Pd = $0, Ps = $0.0, Po = $0.0, Tout = $0.0, ToutNat = $0.0, Dg = $0.0, Ds = $0.0, Bm = $0.0, Dm = $0.0, Bd = $0.0, Pp = $0, Bp = $0, Dp = $0;
  var SO = $0.0;
  var Rg = 0.0, Rs = 0.0, Rp = 0.0;
  var DataSet;
  var sfcontr = TBFile("sfcontr.dbt");
  var IIS = UNSET_CHAR;
  var AccAccDS;
  var AccTaxDS;
  var MaxTaxe1 = $0, MaxTaxe2 = $0;
  var year = 0;
  var NumberOp = "";
  var taxe, taxe15;
  
  SetBuff( nptxop, FDoc );
  var Dbeg:date, Dend:date = nptxop.OperDate;

  DateSplit(nptxop.OperDate, NULL, NULL, year);

  nptxopFile.Clear();

  if(ЕстьНеисполненнаяПорожденнаяОперацияНОБ(nptxop.ID, @NumberOp))
    if(isOprMultiExec()OR (MsgBoxEx( "Порождённая операция расчёта НОБ № "+ NumberOp +" не выполнена. Продолжить выполнение текущей операции зачисления/списания? ",
                   MB_YES+MB_NO, IND_NO) != IND_YES ))
       return 1;
    end;
  end;

  if(nptxop.FlagTax == SET_CHAR)

    sfcontr.rec.ID = nptxop.Contract;
    if(sfcontr.GetEQ())
      IIS = ContrIsIIS(sfcontr.rec.ID);
    end;

    Copy(nptxopFile, nptxop);    
    if(nptxop.CloseContr != SET_CHAR)      
      if(nptxop.SubKind_Operation == DL_TXHOLD_OPTYPE_OUTMONEY)
          Tout = nptxop.OutSum;
          SmartConvertSum(ToutNat, Tout, nptxopFile.rec.OperDate, nptxopFile.rec.Currency, NATCUR);
      end;
      
      //Внимание!!! Резидентство клиента не проверяется на дату операции
      CalcPaidSum(nptxop.Client, nptxop.Contract, nptxop.DocKind, nptxop.SubKind_Operation, date(1, 1, year), Dend, SO, $0.0, ToutNat, @Bg, @Bm, @Bs, @Dm, @Ds, @Dg, IIS, @Po, @Bd, @Bp, @Dp,
                  @Pg, @Pm, @Ps, @Pp, @Pd, @Rg, @Rs, @Rp);
  
      var AccAccQuery = "select t_Chapter, t_Code_Currency, t_Account from daccount_dbt where t_Account = ?";
      var AccAccSelect = DL_RSDCommand(AccAccQuery);
      AccAccSelect.AddParam(nptxopFile.rec.Account);
      AccAccDS = AccAccSelect.execute();
      AccAccDS.MoveNext();
  
      var AccTaxQuery = "select t_Chapter, t_Code_Currency, t_Account from daccount_dbt where t_Account = ?";
      var AccTaxSelect = DL_RSDCommand(AccTaxQuery);
      AccTaxSelect.AddParam(nptxopFile.rec.AccountTax);
      AccTaxDS = AccTaxSelect.execute();
      AccTaxDS.MoveNext();
      
      if(Tout < nptxopFile.rec.Tax)
        nptxopFile.rec.Tax = Tout * Rg;
      else

        if(nptxopFile.rec.Currency != NATCUR)
          SmartConvertSum(MaxTaxe1, DL_GetRestAccount( AccAccDS, Dend ), nptxopFile.rec.OperDate, AccAccDS.Code_Currency, NATCUR);
          SmartConvertSum(MaxTaxe2, DL_GetRestAccount( AccTaxDS, Dend ), nptxopFile.rec.OperDate, AccTaxDS.Code_Currency, NATCUR);

          MaxTaxe1 = round(money(MaxTaxe1), 0, 2);
          MaxTaxe2 = round(money(MaxTaxe2), 0, 2);

          taxe   = min(max(Ds + Dg, 0), MaxTaxe1);
          taxe15 = min(max(Dp, 0), MaxTaxe1 - taxe);
        else
          taxe   = max(Ds + Dg, 0);
          taxe15 = max(Dp, 0);
        end;

        nptxopFile.rec.Tax = taxe + taxe15;
      end;

      nptxopFile.rec.TaxDp = taxe15;

      /*>>BPV всегда делаем вывод за минусом налога*/
     /*PNV 528830 сумма может быть в валюте. Сначала вычтем налог из рублевого эквивалента суммы вывода, а потом пересчитаем в валюту вывода*/
      SmartConvertSum(Tout, (ToutNat - nptxopFile.rec.Tax), nptxopFile.rec.OperDate, NATCUR, nptxopFile.rec.Currency);

      if (nptxopFile.rec.Currency != NATCUR)
          nptxopFile.rec.TaxSum2 = nptxopFile.rec.OutSum;
      else
          if(nptxopFile.rec.Tax > 0)
             nptxopFile.rec.TaxSum2 = nptxopFile.rec.OutSum - nptxopFile.rec.Tax;
          else
             nptxopFile.rec.TaxSum2 = nptxopFile.rec.OutSum;
          end;
      end;
      /*<<*/

    else
      if(nptxop.SubKind_Operation == DL_TXHOLD_OPTYPE_OUTMONEY)
          Tout = nptxop.OutSum;
          SmartConvertSum(ToutNat, Tout, nptxopFile.rec.OperDate, nptxopFile.rec.Currency, NATCUR);
      end;

      CalcPaidSum(nptxop.Client, nptxop.Contract, nptxop.DocKind, nptxop.SubKind_Operation, date(1, 1, year), Dend, SO, $0.0, ToutNat, @Bg, @Bm, @Bs, @Dm, @Ds, @Dg, IIS, @Po, @Bd, @Bp, @Dp);
      nptxopFile.rec.Tax = Dm + Ds + Dg + Dp;
    
      var Rest = $0.0, PlanRest = $0.0;
      /*BPV всегда делаем вывод за минусом налога*/
      /*GAA: 543999 Перед вычитанием налога проверяем валюту вывода и знак налога:
       если  валюта  - не национальная не вычитаем сумму налога, иначе если сумма налога положительная вычитаем из суммы выводв, иначе  - не вычитаемм*/
      /*GAA*/
      if (nptxopFile.rec.Currency != NATCUR)
          nptxopFile.rec.TaxSum2 = nptxopFile.rec.OutSum;
      else
          if(nptxopFile.rec.Tax > 0)
             nptxopFile.rec.TaxSum2 = nptxopFile.rec.OutSum - nptxopFile.rec.Tax;
          else
             nptxopFile.rec.TaxSum2 = nptxopFile.rec.OutSum;
          end;
      end; /*GAA, old:
      nptxopFile.rec.TaxSum2 = nptxopFile.rec.OutSum - nptxopFile.rec.Tax;*/
      
      /*<<*/
      nptxopFile.rec.TaxDp = Dp;
    end;
  end;

  if(DL_InsertDL_VALUE(nptxop.DocKind, nptxop.ID, DL_VALUE_KIND_NPTXHOLD_PREV_DS, nptxop.OperDate, Ds, NATCUR, 0) != 0)
    return Error( "Ошибка при сохранении значения Ds" );
  end;
  if(DL_InsertDL_VALUE(nptxop.DocKind, nptxop.ID, DL_VALUE_KIND_NPTXHOLD_PREV_DG, nptxop.OperDate, Dg, NATCUR, 0) != 0)
    return Error( "Ошибка при сохранении значения Dg" );
  end;
  if(DL_InsertDL_VALUE(nptxop.DocKind, nptxop.ID, DL_VALUE_KIND_NPTXHOLD_PREV_DP, nptxop.OperDate, Dp, NATCUR, 0) != 0)
    return Error( "Ошибка при сохранении значения Dp" );
  end;

  if(nptxopFile.rec.ID > 0)
    nptxopFile.rec.Tout = TOut;

    if( DL_NPTX_UpdateSumm( nptxopFile ) != 0 )
      return Error( "Ошибка при обновлении сумм в dnptxop_dbt" );
    end;
  end;

  return err;
end;