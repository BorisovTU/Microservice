/*
$Name:        nptxphysiisinfo_form.mac
$Module:      Ценные бумаги
$Description: Форма отчета "Сведения о ФЛ и его ИИС"
*/

import "DlRepForm_h.mac", "spserv.mac";
import SfInter;

const PNFLD_NPTXPIIS_REPORTDATE     = 0;
const PNFLD_NPTXPIIS_INVESTORCODE   = 1;
const PNFLD_NPTXPIIS_INVESTORNAME   = 2;
const PNFLD_NPTXPIIS_CONTRACT       = 3;
const PNFLD_NPTXPIIS_CONSOLIDATE    = 4;
const PNFLD_NPTXPIIS_EXCEL          = 5;
const PNFLD_NPTXPIIS_PDF            = 6;
const PNFLD_NPTXPIIS_SIGNATORY      = 7;
const PNFLD_NPTXPIIS_SENDTOCLIENT   = 8;
const PNFLD_NPTXPIIS_DOCUMENTNUMBER = 9;

const H_REPDATE          = 101,
      H_INVESTORCODE     = 102,
      H_INVESTORNAME     = 103,
      H_CONTRACT         = 104,
      H_CONSOLIDATE      = 105,
      H_EXCEL            = 106,
      H_PDF              = 107,
      H_SIGNATORY        = 108,
      H_SENDTOCLIENT     = 109,
      H_DOCUMENTNUMBER   = 110;


PRIVATE MACRO Generate_DocumentNumber()
   var RefNum = "";                                               
   if( GenerateNumberByReference( OBJTYPE_PERSN, 1, null, @RefNum) )                 
   end;                                                                                        
   return RefNum;                                                                     
END;

PRIVATE MACRO Generate_Mes(DlContrID:integer)
   var Query = "";
   var DataSet = null, cmd, Mes = "";

   query =   " select sf.t_Number, sf.t_DateClose, sf.t_PartyID "
           + "   from ddlcontr_dbt dlc, dsfcontr_dbt sf "
           + "  where dlc.t_DlContrID = ? "
           + "    and sf.t_ID = dlc.t_SfContrID ";

   cmd = DL_RSDCommand(query);

   cmd.AddParam(DlContrID);

   DataSet = cmd.Execute();

   if(DataSet.moveNext())
      var ContrNumber = DataSet.Number;
      var DateClose = date(DataSet.DateClose);
      var PartyID = DataSet.PartyID;

      if( DateClose == date(0,0,0) )
        Mes = "Не закрыт Договор ИИС. Сведения о ФЛ и его ИИС не сформированы";
      end;

      if(Mes == "")
        Query =  " select acc.t_Account, acc.t_Close_Date "
               + "   from ddlcontracc_dbt dlcacc, daccount_dbt acc "
               + "  where dlcacc.t_DlContrID = ? "                 
               + "    and acc.t_AccountID = dlcacc.t_AccountID "
               + "    and acc.t_Close_Date = " + GetSQLDate(date(0,0,0))                
               + " order by acc.t_open_date desc";  
       
        cmd = DL_RSDCommand(query);

        cmd.AddParam(DlContrID);

        DataSet = cmd.Execute();
        if(DataSet.MoveNext())
          Mes = "Не закрыт счет ИИС. Сведения о ФЛ и его ИИС не сформированы";
        end;
      end;

      if(Mes == "")
        Query =   " select t_ID "
                + "   from dnptxop_dbt "
                + "  where t_DocKind = " + DL_CALCNDFL
                + "    and t_Client = ? "
                + "    and t_Contract = ? "
                + "    and t_OperDate <= ? "
                + "    and t_SubKind_Operation = " + DL_TXBASECALC_OPTYPE_ENDYEAR
                + "    and t_Status = " + DL_TXOP_Close;

        cmd = DL_RSDCommand(query);

        cmd.AddParam(PartyID);
        cmd.AddParam(DlContrID);
        cmd.AddParam(DateClose);

        DataSet = cmd.Execute();
        if(not DataSet.MoveNext())
          Mes = "Не производился расчет НОБ. Сведения о ФЛ и его ИИС не сформированы";
        end;
      end;
   end;

   return Mes;
END;
                            
private macro GetOneClientContrStock(PartyID, OnDate)
    var Query = "", cmd, DataSet = null;
    var DlContrID = 0;

    Query = " SELECT dlc.t_DlContrID, sfc.t_Number "
          + "   FROM dsfcontr_dbt sfc, ddlcontr_dbt dlc "
          + "  WHERE sfc.t_ServKind = 0 "
          + "    AND sfc.t_PartyID = ? " 
          + "    AND dlc.t_SfContrID = sfc.t_ID "
          + "    AND dlc.t_IIS = 'X' "
          + "    AND sfc.t_DateBegin <= ? "
          + "    AND (sfc.t_DateClose = TO_DATE('01.01.0001','DD.MM.YYYY') OR sfc.t_DateClose >= ?) "
          + " order by sfc.t_DateBegin desc";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(PartyID);
    cmd.AddParam(OnDate);
    cmd.AddParam(OnDate);

    DataSet = cmd.Execute();
    if(DataSet.MoveNext())
      DlContrID = SQL_ConvTypeInteger(DataSet.DlContrID);
    end;

    return DlContrID;
end;

// Код физлица - клиента фондового дилинга
private macro FldProc_Client_STOCKDL_Code(pThis:DL_CPanel, Cmd:Integer, Key:Integer, FldShowValue:@Variant, FldRealValue:@Variant):Integer
    var Party = TRecHandler("party.dbt");
    var AddWhere = "";
    var DlContrID = -1;

    if(Cmd == DLG_INIT)
        if(FldRealValue == null)
            FldRealValue = 0;
        end;
        if(FldRealValue <= 0)
            FldShowValue = STR_ALLVALUE;
            pThis.SetLinkedValue(H_INVESTORNAME, STR_ALLVALUE);
            pThis.SetLinkedValue(H_CONTRACT, -1);
        else
            FldShowValue = ПолучитьКодСубъекта(FldRealValue, PTCK_CONTR);
            if(not ПолучитьСубъекта(FldRealValue, Party))
                pThis.SetLinkedValue(H_INVESTORNAME, Party.rec.ShortName);
            end;
        end;
    elif((Cmd == DLG_KEY) and (Key == KEY_SPACE))
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue(H_INVESTORNAME, STR_ALLVALUE);
        pThis.SetLinkedValue(H_CONTRACT, -1);
    elif((Cmd == DLG_KEY) and (Key == KEY_F3))
        var RepDate = Date(pThis.GetFieldValue(PNFLD_NPTXPIIS_REPORTDATE));

        AddWhere = " t.t_LegalForm = 2 "
                 + " and exists( "
                 + "              select 1 "
                 + "              from dclient_dbt cl "
                 + "              where cl.t_PartyID = t.t_PartyID "
                 + "                and cl.t_ServiceKind = " + string(PTSK_STOCKDL)
                 + "                and cl.t_StartDate < " + GetSQLDate(RepDate)
                 + "           ) ";

        if(ListPText(Party, FldRealValue, AddWhere))
            FldRealValue = Party.rec.PartyID;
            FldShowValue = ПолучитьКодСубъекта(FldRealValue, PTCK_CONTR);
            pThis.SetLinkedValue(H_INVESTORNAME, Party.rec.ShortName);
            pThis.SetLinkedValue(H_CONTRACT, -1);

            DlContrID = GetOneClientContrStock(Party.rec.PartyID, pThis.GetLinkedValue(H_REPDATE));
            if(DlContrID > 0)
                pThis.SetLinkedValue(H_CONTRACT, DlContrID);
            end;
        end;
    end;

    return CM_DEFAULT;
end;

// листалка договоров
private macro ListSfContrByServKind(FldShowValue:@Variant, FldRealValue:@Variant, X:Integer, Y:Integer, PartyID:@Integer):Bool
  var Query = "";
  var RecordSet = null; 

  Query = "select sfc.t_DateBegin, sfc.t_DateClose, sfc.t_Number, sfc.t_Name, prt1.t_ShortName ClientName, prt1.t_PartyID as t_PartyID, " + 
           "   dlc.t_IIS, dlc.t_DlContrID " +
           "  from ddlcontr_dbt dlc, dsfcontr_dbt sfc, dparty_dbt prt1 "+
           " where sfc.t_ID = dlc.t_SfContrID " +
           "   and dlc.t_IIS = 'X' " +
           "   and prt1.t_PartyID = sfc.t_partyID " ;

  if( PartyID > 0 )
    Query = Query + " and sfc.T_PARTYID = " + PartyID;
  end;
 
  RecordSet = DL_ScrollW(Query, "Список ДБО ИИС", X, Y,
                        "t_Number", "№ договора", 13,
                        "t_Name", "Наименование договора", 20,
                        "t_DateBegin", "Дата открытия", 10,
                        "t_DateClose", "Дата закрытия", 10,
                        "ClientName","Клиент", 30
                        );

  if(RecordSet != null)
    FldRealValue = RecordSet.Value("t_DlContrID"); 
    FldShowValue = RecordSet.Value("t_Number");
    PartyID = RecordSet.Value("t_PartyID");
  end;

  return (RecordSet != null);
end;

private macro FldProc_Contract_BO_Dep(pThis:DL_CPanel, Cmd:Integer, Key:Integer, FldShowValue:@Variant, FldRealValue:@Variant):Integer
    var SfContr = TRecHandler("sfcontr.dbt");
    var ClientID = pThis.GetLinkedValue(H_INVESTORCODE);
    var query, sel, DataSet;

    if(Cmd == DLG_INIT)
        if(FldRealValue == null)
            FldRealValue = -1;
        end;

        if(FldRealValue <= 0)
          FldShowValue = STR_ALLVALUE;
          DisableFields(pThis.Data(), PNFLD_NPTXPIIS_CONSOLIDATE);
          pThis.SetLinkedValue(H_CONSOLIDATE, NULL);
        else
          query =   " select sf.t_Number, dlc.t_IISTransfer "
                  + "   from ddlcontr_dbt dlc, dsfcontr_dbt sf "
                  + "  where dlc.t_DlContrID = ? "
                  + "    and sf.t_ID = dlc.t_SfContrID ";

          sel = DL_RSDCommand(query);

          sel.AddParam(FldRealValue);

          DataSet = sel.Execute();

          if(DataSet.moveNext())
            FldShowValue = DataSet.Number;

            if(DataSet.IISTransfer == SET_CHAR)
              EnableFields(pThis.Data(), PNFLD_NPTXPIIS_CONSOLIDATE);
            else
              DisableFields(pThis.Data(), PNFLD_NPTXPIIS_CONSOLIDATE);
              pThis.SetLinkedValue(H_CONSOLIDATE, NULL);
            end;
          end;
        end;
    elif((Cmd == DLG_KEY) and (Key == KEY_SPACE)) 
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue(H_INVESTORCODE, -1);
    elif((Cmd == DLG_KEY) and (Key == KEY_F3))
        ListSfContrByServKind(@FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), @ClientID);
        pThis.SetLinkedValue(H_INVESTORCODE, ClientID);

        query =   " select sf.t_Number, dlc.t_IISTransfer "
                + "   from ddlcontr_dbt dlc, dsfcontr_dbt sf "
                + "  where dlc.t_DlContrID = ? "
                + "    and sf.t_ID = dlc.t_SfContrID ";

        sel = DL_RSDCommand(query);

        sel.AddParam(FldRealValue);

        DataSet = sel.Execute();

        if(DataSet.moveNext())
          FldShowValue = DataSet.Number;

          if(DataSet.IISTransfer == SET_CHAR)
            EnableFields(pThis.Data(), PNFLD_NPTXPIIS_CONSOLIDATE);
          else
            DisableFields(pThis.Data(), PNFLD_NPTXPIIS_CONSOLIDATE);
            pThis.SetLinkedValue(H_CONSOLIDATE, NULL);
          end;
        end;
    end;

    return CM_DEFAULT;
end;

private macro FldProc_Signatory(pThis:DL_CPanel, cmd:Integer, key:Integer, fldShowValue:@Variant, fldRealValue:@Variant):INTEGER

   if(cmd == DLG_INIT)
      if(fldRealValue == NULL)
         FldRealValue = -1;
         fldShowValue = "";
      elif(fldRealValue > 0)
        var query, sel, DataSet;

        fldShowValue = "";

        sel = DL_RSDCommand();
        query = "select t_Name from dperson_dbt where t_Oper = " + sel.AddParam(fldRealValue);

        DataSet = sel.Execute(query);
        if(DataSet.moveNext())
          fldShowValue = DataSet.Name;
        end;
      end;
   elif(cmd == DLG_CHANGEVALUE)
   elif(cmd == DLG_KEY)
      if(key == KEY_SPACE)
         FldRealValue = -1;
         fldShowValue = "";
      elif(key == KEY_F3)
         VAR Choice,Select;

         Select = "select ps.t_Oper, ps.t_Name" +
                  "  from dacsgroup_dbt grp, dacsgroupoper_dbt grpop, dperson_dbt ps " +
                  " where grp.t_Name = 'Подписанты налоговой отчетности' " +
                  "   and grpop.t_GroupID = grp.t_GroupID "
                  "   and ps.t_Oper = grpop.t_Oper";


         Choice = DL_ScrollW(Select,
                             "Подписанты налоговой отчетности", 10, 10,
                             "t_Oper","Номер", 15,
                             "t_Name","ФИО", 60
                            );

         if( Choice != NULL )
            FldRealValue = Choice.Value("t_Oper"); 
            FldShowValue = Choice.Value("t_Name");
         end;
      end;
   end;
end;


private macro FldProc_DocumentNumber(pThis:DL_CPanel, Cmd:Integer, Key:Integer, FldShowValue:@Variant, FldRealValue:@Variant):Integer
    if(Cmd == DLG_INIT)
//        if(FldRealValue == null)
            FldRealValue = Generate_DocumentNumber();
//        end;
        FldShowValue = FldRealValue;
    elif(Cmd == DLG_CHANGEVALUE)
        FldRealValue = FldShowValue;
    elif(Cmd == DLG_KEY)
        if(Key == KEY_SPACE)
            FldShowValue = FldRealValue = "";
        end;
    end;

    return CM_DEFAULT;
end;

MACRO FldProc_CheckBoxPDF( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
        end;
     end;
  end;

  if(FldRealValue == SET_CHAR)
    EnableFields(pThis.Data(), PNFLD_NPTXPIIS_SENDTOCLIENT);
  else
    DisableFields(pThis.Data(), PNFLD_NPTXPIIS_SENDTOCLIENT);
    pThis.SetLinkedValue(H_SENDTOCLIENT, UNSET_CHAR);
  end;

  return CM_DEFAULT;
END;

class (DL_CPanel) NpTxPhysIISInfoPanel()

    private macro Init()
        AddFieldProc(0, PNFLD_NPTXPIIS_REPORTDATE,      H_REPDATE,        @DL_FldProc_EndDate);
        AddFieldProc(0, PNFLD_NPTXPIIS_INVESTORCODE,    H_INVESTORCODE,   @FldProc_Client_STOCKDL_Code);
        AddFieldProc(0, PNFLD_NPTXPIIS_INVESTORNAME,    H_INVESTORNAME,   @DL_FldProc_Client_STOCKDL_Name);
        AddFieldProc(0, PNFLD_NPTXPIIS_CONTRACT,        H_CONTRACT,       @FldProc_Contract_BO_Dep);
        AddFieldProc(0, PNFLD_NPTXPIIS_CONSOLIDATE,     H_CONSOLIDATE,    @DL_FldProc_CheckBox, UNSET_CHAR);
        AddFieldProc(0, PNFLD_NPTXPIIS_EXCEL,           H_EXCEL,          @DL_FldProc_CheckBox, UNSET_CHAR);
        AddFieldProc(0, PNFLD_NPTXPIIS_PDF,             H_PDF,            @FldProc_CheckBoxPDF, SET_CHAR);
        AddFieldProc(0, PNFLD_NPTXPIIS_SIGNATORY,       H_SIGNATORY,      @FldProc_Signatory, 1559);
        AddFieldProc(0, PNFLD_NPTXPIIS_SENDTOCLIENT,    H_SENDTOCLIENT,   @DL_FldProc_CheckBox, UNSET_CHAR);
        AddFieldProc(0, PNFLD_NPTXPIIS_DOCUMENTNUMBER,  H_DOCUMENTNUMBER, @FldProc_DocumentNumber);
    end;

    private macro Check():BOOL
        var Stat = true, Mes = "";
        var Key = 0;

        if(GetFieldValue(PNFLD_NPTXPIIS_INVESTORCODE) <= 0)
            MsgBox("Необходимо заполнить поле \"Инвестор\"");
            Stat = false;
        end;

        if(Stat and (GetFieldValue(PNFLD_NPTXPIIS_CONTRACT) <= 0))
            MsgBox("Необходимо заполнить поле \"Договор обслуживания\"");
            Stat = false;
        end;

        if(Stat and (GetFieldValue(PNFLD_NPTXPIIS_EXCEL) != SET_CHAR) and (GetFieldValue(PNFLD_NPTXPIIS_PDF) != SET_CHAR))
            MsgBox("Необходимо указать способ формирования отчета");
            Stat = false;
        end;

        if(Stat and (GetFieldValue(PNFLD_NPTXPIIS_PDF) == SET_CHAR) and (GetFieldValue(PNFLD_NPTXPIIS_SENDTOCLIENT) != SET_CHAR))
          Stat = GetTrue(true, "Чек-бокс \"Отправить клиенту\" не активирован. Справка не будет отправлена. Продолжить?");
        end;

        if(Stat)
           Mes = Generate_Mes(GetFieldValue(PNFLD_NPTXPIIS_CONTRACT));
           if( Mes != "" )
              Key = MsgBox(Mes);
              Stat = false;
           end;
        end;

        return Stat;
    end;

    initDL_CPanel("sp_rep.lbr", "NPTXPIIS");
end;
