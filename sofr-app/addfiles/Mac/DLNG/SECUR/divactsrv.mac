/*
$Name:        divactsrv.mac
$Module:      Ценные бумаги
$Description: Макрос процедуры дозаполнения операций возврата дивидендов
*/
import "globals.mac", secinter, DealsInter, BankInter;

PRIVATE CLASS TActDivOpInfo(_DivOpCode:string, 
                            _RepoDealCode:string,
                            _FI_Code:string, 
                            _ReestrDate:date, 
                            _CurrCode:string,
                            _IsReady:string,
                            _oldD1:money,
                            _newD1:money,
                            _oldD2:money,
                            _newD2:money,
                            _oldDivSumOneFI:money,
                            _newDivSumOneFI:money,
                            _oldDivSumDeal:money,
                            _newDivSumDeal:money,
                            _oldDivSumPay:money,
                            _newDivSumPay:money
                           )
  var DivOpCode       = _DivOpCode;      //Код операции выплаты
  var RepoDealCode    = _RepoDealCode;   //Код сделки РЕПО
  var FI_Code         = _FI_Code;        //Код ц/б
  var ReestrDate      = _ReestrDate;     //Дата сбора реестра
  var CurrCode        = _CurrCode;       //Код валюты суммы дивидендов
  var IsReady         = _IsReady;        //Операция готова к дальнейшей обработке 
  var oldD1           = _oldD1;          //Старое значение Д1
  var newD1           = _newD1;          //Новое значение Д1
  var oldD2           = _oldD2;          //Старое значение Д2
  var newD2           = _newD2;          //Новое значение Д2
  var oldDivSumOneFI  = _oldDivSumOneFI; //Старая сумма дивидендов на одну ц/б
  var newDivSumOneFI  = _newDivSumOneFI; //Новая сумма дивидендов на одну ц/б
  var oldDivSumDeal   = _oldDivSumDeal;  //Старая сумма дивидендов по сделке
  var newDivSumDeal   = _newDivSumDeal;  //Новая сумма дивидендов по сделке
  var oldDivSumPay    = _oldDivSumPay;   //Старая сумма выплаты получателю
  var newDivSumPay    = _newDivSumPay;   //Новая сумма выплаты получателю
END;

PRIVATE CLASS TActDivOpErr(_DvDealCode:string, _RepoDealCode:string, _FI_Code:string, _ReestrDate:date, _ErrStr:string)
  var DvDealCode   = _DvDealCode;   //Код операции возврата 
  var RepoDealCode = _RepoDealCode; //Код сделки РЕПО
  var FI_Code      = _FI_Code;      //Код ц/б
  var ReestrDate   = _ReestrDate;   //Дата реестра     
  var ErrStr       = _ErrStr;       //Сообщение об ошибке
END;

PRIVATE VAR ActDivOpInfoArr = TArray();
PRIVATE VAR ActDivOpErrArr  = TArray();


PRIVATE MACRO PrintProtocol()
  var i = 0;

  [###############################################################################################################################################################################################]
  ("Протокол процедуры дозаполнения операций с дивидендами":c);

  println("");
  println("Дата запуска: " + string({curdate}:f));
  println("");
  if((ActDivOpInfoArr.size == 0) and (ActDivOpErrArr.size == 0))
    println("Не найдено подходящих для дозаполнения операций с дивидендами");
  else
    if(ActDivOpInfoArr.size > 0)
      [ #####################################
      ┌────────────────┬───────────────┬───────────────┬────────────┬──────────┬──────────┬──────────────────┬──────────────────┬──────────────────────┬──────────────────────┬──────────────────────┐
      │       Код      │   Код сделки  │    Код ц/б    │ Дата сбора │ Готова к │  Валюта  │    Величина Д1   │    Величина Д2   │   Сумма дивидендов   │   Сумма дивидендов   │    Сумма выплаты     │
      │    операции    │      РЕПО     │               │   реестра  │дальнейшей│дивидендов│                  │                  │      на одну ц/б     │      по сделке       │       по сделке      │    
      │                │               │               │            │обработке │          ├─────────┬────────┼─────────┬────────┼───────────┬──────────┼───────────┬──────────┼───────────┬──────────┤
      │                │               │               │            │          │          │   До    │ После  │   До    │ После  │     До    │   После  │     До    │   После  │    До     │   После  │
      ├────────────────┼───────────────┼───────────────┼────────────┼──────────┼──────────┼─────────┼────────┼─────────┼────────┼───────────┼──────────┼───────────┼──────────┼───────────┼──────────┤]
      ("Успешно обработанные операции");

      i = 0;
      while(i < ActDivOpInfoArr.size)
       [│################│###############│###############│############│##########│##########│#########│########│#########│########│###########│##########│###########│##########│###########│##########│]
         (ActDivOpInfoArr[i].DivOpCode:c,
          ActDivOpInfoArr[i].RepoDealCode:c, 
          ActDivOpInfoArr[i].FI_Code:c,     
          ActDivOpInfoArr[i].ReestrDate:f:c,  
          ActDivOpInfoArr[i].IsReady:c,
          ActDivOpInfoArr[i].CurrCode:c,
          ActDivOpInfoArr[i].oldD1:r,
          ActDivOpInfoArr[i].newD1:r,
          ActDivOpInfoArr[i].oldD2:r,
          ActDivOpInfoArr[i].newD2:r,
          ActDivOpInfoArr[i].oldDivSumOneFI:r,
          ActDivOpInfoArr[i].newDivSumOneFI:r,
          ActDivOpInfoArr[i].oldDivSumDeal:r, 
          ActDivOpInfoArr[i].newDivSumDeal:r, 
          ActDivOpInfoArr[i].oldDivSumPay:r,  
          ActDivOpInfoArr[i].newDivSumPay:r
         );
  
        i = i + 1;
      end;

      [└────────────────┴───────────────┴───────────────┴────────────┴──────────┴──────────┴─────────┴────────┴─────────┴────────┴───────────┴──────────┴───────────┴──────────┴───────────┴──────────┘];

      println("");
    end;

    if(ActDivOpErrArr.size > 0)
      [ #####################################
      ┌─────────────────┬─────────────────┬────────────────┬──────────────┬───────────────────────────────────────────────────────────────────┐
      │   Код операции  │    Код сделки   │     Код ц/б    │ Дата выплаты │                     Сообщение                                     │
      │     возврата    │       РЕПО      │                │              │                                                                   │
      ├─────────────────┼─────────────────┼────────────────┼──────────────┼───────────────────────────────────────────────────────────────────┤]
      ("Сообщения");

      i = 0;
      while(i < ActDivOpErrArr.size)
        [│#################│#################│################│##############│###################################################################│]
         (ActDivOpErrArr[i].DealCode:c,
          ActDivOpErrArr[i].RepoDealCode:c,  
          ActDivOpErrArr[i].FI_Code:c,
          ActDivOpErrArr[i].ListDate:f:c,     
          ActDivOpErrArr[i].ErrStr:l:w
         );
  
        i = i + 1;
      end;
     
      [└─────────────────┴─────────────────┴────────────────┴──────────────┴───────────────────────────────────────────────────────────────────┘];
    end;
  end;

END;

PRIVATE MACRO StartActDiv()
  var query, cmd, DataSet;
  var cnt = 0, i = 0;
  var err = 0, ErrStr = "";

  query =   " select dvt.t_DealID, dvt.t_DealCode, fiw.t_ID as FiWarntID, fin.t_FI_Code, fiw.t_ListDate, "
          + "        leg.t_D1, leg.t_D2, leg.t_Price, leg.t_Cost, "
          + "        RSB_SECUR.IsGetDividRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(dvt.t_DealType, dvt.t_BofficeKind))) IsDividRepo, "
          + "        case  when (RSB_SECUR.IsGetDividRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(dvt.t_DealType, dvt.t_BofficeKind))) = 1) "
          + "            then leg.t_ReceiptAmount   else   dvt.t_SumPay "
          +"         end  as SumPay, "
          + "        cr.t_Ccy, "
          + "        NVL((select tk.t_DealCode from ddl_tick_dbt tk where tk.t_DealID = dvt.t_ParentID ),  chr(1))  as RepoDealCode "
          + "   from ddl_tick_dbt dvt, ddl_leg_dbt leg, dfiwarnts_dbt fiw, dfininstr_dbt fin, dfininstr_dbt cr    /*, ddl_tick_dbt tk */ "
          + "  where dvt.t_BOfficeKind = " + DL_RETURN_DIVIDEND
          + "    and dvt.t_DealStatus  = " + DL_PREPARING
          + "    and leg.t_DealID      = dvt.t_DealID " 
          + "    and leg.t_LegID       = 0 " 
          + "    and leg.t_LegKind     = " + LEG_KIND_DL_TICK
          + "    and fiw.t_FIID        = dvt.t_PFI "
          + "    and fiw.t_ListDate    = leg.t_Start "
          + "    and fin.t_FIID        = fiw.t_FIID "
          + "    and cr.t_FIID         = fiw.t_IncomeFI "
          + "    and ( NVL(rsb_struct.getMoney(rsi_rsb_kernel.GetNote("+OBJTYPE_DIVIDENDS+", LPAD(fiw.t_ID, 34, '0'), 1, ?)), NULL) IS NOT NULL "
          + "          or NVL(rsb_struct.getMoney(rsi_rsb_kernel.GetNote("+OBJTYPE_DIVIDENDS+", LPAD(fiw.t_ID, 34, '0'), 2, ?)), NULL) IS NOT NULL"
          + "        ) "
          + "    and (  NVL(rsb_struct.getMoney(rsi_rsb_kernel.GetNote("+OBJTYPE_DIVIDENDS+", LPAD(fiw.t_ID, 34, '0'), 1, ?)), 0) <> leg.t_D1 "
          + "        or NVL(rsb_struct.getMoney(rsi_rsb_kernel.GetNote("+OBJTYPE_DIVIDENDS+", LPAD(fiw.t_ID, 34, '0'), 2, ?)), 0) <> leg.t_D2 "
          + "        ) ";
 //         + "    and tk.t_DealID = dvt.t_ParentID ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam({curdate});
  cmd.AddParam({curdate});
  cmd.AddParam({curdate});
  cmd.AddParam({curdate});
  
  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "Дозаполнение операций с дивидендами", "Дозаполнение операций с дивидендами");

    SP_BeginCreateDeal(DL_RETURN_DIVIDEND);

    DataSet = cmd.Execute();
    while(DataSet.moveNext())

      err = SP_ActuateReturnDivOp(DataSet.DealID, DataSet.FiWarntID, ErrStr);
      if(err)

      else
        var dvt = TBFile("dl_tick.dbt");
        var leg = TBFile("dl_leg.dbt");

        dvt.Clear();
        dvt.rec.DealID = DataSet.DealID;

        leg.KeyNum(0);
        leg.rec.LegKind   =  LEG_KIND_DL_TICK;
        leg.rec.DealID    =  DataSet.DealID;
        leg.rec.LegID     =  0;

        if((dvt.GetEQ()) and (leg.GetEQ()))
          ActDivOpInfoArr[ActDivOpInfoArr.size] = TActDivOpInfo(DataSet.DealCode, 
                                                                DataSet.RepoDealCode,
                                                                DataSet.FI_Code, 
                                                                date(leg.rec.Start), 
                                                                DataSet.Ccy,
                                                                dvt.rec.IsReady,
                                                                DataSet.D1,
                                                                leg.rec.D1,
                                                                DataSet.D2,
                                                                leg.rec.D2,
                                                                DataSet.Price,
                                                                leg.rec.Price,
                                                                DataSet.Cost,
                                                                leg.rec.Cost,
                                                                DataSet.SumPay,
                                                                IIF(DataSet.IsDividRepo==1, leg.rec.ReceiptAmount, dvt.rec.SumPay)
                                                               );
        else
          err = 1;
          ErrStr = "Ошибка поиска операции с кодом " + DataSet.DealCode;
        end;

        if(err)
          ActDivOpErrArr[ActDivOpErrArr.size] = TActDivOpErr(DataSet.DealCode, DataSet.RepoDealCode, DataSet.FI_Code, date(DataSet.ListDate), ErrStr);
        end;
      end;

      UseProgress(i = i + 1);
    end;

    SP_EndCreateDeal();

    RemProgress();
  end;

  PrintProtocol();

  return 1; 
END;


StartActDiv();