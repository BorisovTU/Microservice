/*
$Name:        sccrpayms.mac
$Module:      Ценные бумаги
$Description: Макрос выполнения сервисной операции выгрузки платежей
*/
import DealsInter, SPInter, PTInter, globals, "sp_class.mac", "settl.mac", "dlquery.mac", "pt_lib.mac", "dlntfd.mac", "dllog_xml.mac", "spretownfun.mac";
import RsbDataSet;
import role_model, usr_connect_attr, "CFTDocumentTypeCodes.mac";

private var CurFD = NULL;
private var CurRQ = NULL;

private var RepErrArr = TArray(); //массив ошибок

//класс с данными для списка ошибок в протоколе
class CSC_CreatePaymsRepErr(_DealCode:string, _RqInfo:string, _ErrCode:integer, _ErrStr:string)
  var DealCode = _DealCode;
  var RqInfo   = _RqInfo;
  var ErrCode  = _ErrCode;
  var ErrStr   = _ErrStr;
end;

// Установка переменной, если она не определена
private macro ByDefault(variable, value) 
  if (ValType(variable) == V_UNDEF) 
    setparm(0, value);
  end;
end;

private var RQTypeNames = TArray();
private macro GetRQTypeName(Type:integer)
  var RQTypeName = "";
  if(ValType(RQTypeNames[Type]) != V_UNDEF)
    RQTypeName = RQTypeNames[Type];
  else
    RQTypeNames[Type] = RQTypeName = DL_GetNameAlg( 7513 /*ALG_DLRQ_TYPE*/, Type );
  end;

  return RQTypeName;
end;

macro CrPayms_msgbox(StrErr)
  if((CurFD != NULL) and (CurRQ != NULL))
    if(StrUpr( GenClassName( CurFD )) == StrUpr("SPFirstDoc"))
      var RQInfo = GetRQTypeName(CurRQ.rec.Type);

      if(CurFD.ExistBack())
        RQInfo = RQInfo + " " + string(CurRQ.rec.DealPart)+"ч.";
      end;

      RepErrArr[RepErrArr.size] = CSC_CreatePaymsRepErr(CurFD.tick.rec.DealCode, RQInfo, 1, StrErr);
    elif(StrUpr( GenClassName( CurFD )) == StrUpr("DL_NettingDoc"))
      RepErrArr[RepErrArr.size] = CSC_CreatePaymsRepErr(CurFD.dl_nett.rec.DealNumber, GetRQTypeName(CurRQ.rec.Type), 1, StrErr);
    elif(StrUpr( GenClassName( CurFD )) == StrUpr("SPFirstDocDLCOMM"))
      RepErrArr[RepErrArr.size] = CSC_CreatePaymsRepErr(CurFD.comm.rec.CommCode, GetRQTypeName(CurRQ.rec.Type), 1, StrErr);
    elif(StrUpr( GenClassName( CurFD )) == StrUpr("DLFirstDocNPTXOP"))
      RepErrArr[RepErrArr.size] = CSC_CreatePaymsRepErr(CurFD.nptxop.rec.Code, GetRQTypeName(CurRQ.rec.Type), 1, StrErr);
    else
      RepErrArr[RepErrArr.size] = CSC_CreatePaymsRepErr("", "", 1, StrErr);
    end;
  else
    RepErrArr[RepErrArr.size] = CSC_CreatePaymsRepErr("", "", 1, StrErr);
  end;
end;


/*лог ошибок*/
PRIVATE CLASS (DL_LOG_FROM_XML) SC_ERROR_LOG_FROM_XML(pDocKind:integer, pDocumentID:integer, pType:integer, pOnlyLastReport:bool)

  macro GetDataFromXMLByDocKind()
     var ErrorData:CSC_CreatePaymsRepErr;

     if( m_ReportVersion == 1 )

        if(DL_ReadTagAttribut(m_child_ReportNumber, "DEALCODE", V_STRING, ErrorData.DealCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "RQINFO", V_STRING, ErrorData.RqInfo) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRCODE", V_STRING, ErrorData.ErrCode) == false)
        end;
        if(DL_ReadTagAttribut(m_child_ReportNumber, "ERRSTR", V_STRING, ErrorData.ErrStr) == false)
        end;
        
        return ErrorData;
     end;

     return null;
  end;

  initDL_LOG_FROM_XML(pDocKind, pDocumentID, pType, pOnlyLastReport);

END;

/*лог ошибок СО*/
CLASS (DL_LOG_DATA_XML) SC_CREATEPAYMS_ERROR_LOG_DATA_XML(pDocKind:integer, pDocumentID:integer, pDataParms:TArray, pType:integer, pVersion:integer)

  PRIVATE MACRO СоздатьУзелОшибок(i)
     var УзелОшибок = null;

     if( m_Version == 1 )
        УзелОшибок = CreateElement("Property",
                                   "DealCode",  m_DataParms[i].DealCode,
                                   "RqInfo",    m_DataParms[i].RqInfo, 
                                   "ErrCode",   m_DataParms[i].ErrCode, 
                                   "ErrStr",    m_DataParms[i].ErrStr
                                  );
     else
        УзелОшибок = CreateElement("Property");
     end;
     
     return УзелОшибок;
  END;


  initDL_LOG_DATA_XML(pDocKind, pDocumentID, pDataParms, pType, pVersion);

END;

PRIVATE MACRO GetLogDataXML( DocKind:integer, DocumentID:integer, Type:integer, OnlyLastReport:bool, ReportKind:integer )
   var xml_obj = null;

   if( Type == LOG_TYPE_DATA )

   elif(Type == LOG_TYPE_ERROR )
      xml_obj = SC_ERROR_LOG_FROM_XML(DocKind, DocumentID, Type, OnlyLastReport);
   end;

   return xml_obj;
END;

macro SaveCrPaymsErrForReport_XML( RepErrArr, DocumentID:integer, DocKind:integer )

  var RepXML = "";

  if( RepErrArr.Size > 0 )
     RepXML = SC_CREATEPAYMS_ERROR_LOG_DATA_XML(DocKind,DocumentID,RepErrArr,LOG_TYPE_ERROR).GetDataXML();
  end;

  if( RepXML != "" )
     var Header = DL_LOG_HEADER_XML(DocKind).GetHeaderXML();

     DL_InsertHeaderLog(DocumentID, DocKind, {Oper}, Header);
     DL_InsertLogXMLData( DocumentID, DocKind, LOG_TYPE_ERROR, {Oper}, RepXML);
  end;

end;


//Печать протокола
MACRO PrintProtocol(comm)
  //var IsExistsErr = false;
  var ReportLineData_XML:variant;
  var ErrData:CSC_CreatePaymsRepErr;
  record DlComm("dl_comm");
  var reccomm = TRecHandler("dl_comm");
  VAR ReportFileName, errtext, errcode;
  FILE ReportOutFile() txt write; /*файл вывода для отчета*/
  var query, cmd, DataSet;
  var cnt = 0;
  var stat = 0;
  
  SetBuff(DlComm, comm);
  copy(reccomm, dlcomm);

  ReportFileName = GetTxtFileName( "sccrpayms_" + reccomm.rec.CommCode );
  if( not Open( ReportOutFile, ReportFileName ) )
     errcode = status(errtext);
     RunError( "Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext );
  end;
  SetOutput( ReportFileName );


  [#############################################################################]
  (string("ПРОТОКОЛ ОПЕРАЦИИ ВЫГРУЗКИ ПЛАТЕЖЕЙ"):c);
  [#############################################################################]
  (string("Дата операции: "+string(reccomm.rec.CommDate:f)+" Код: "+ reccomm.rec.CommCode):c);

  query =   " select q.t_Factdate, q.t_DealCode, NVL(pm.t_PaymentID, 0) as t_PaymentID, NVL(purp.t_ShortName, 'Платеж удален') as PurpShortName"
          + "   from dpmpaym_dbt pm, dpmpurp_dbt purp, "
          + "       (select rq.t_Factdate, dt.t_DealCode, rl.t_PaymentID "
          + "          from drqpmlink_dbt rl, ddlrq_dbt rq, dv_sctotaldeals dt "
          + "         where rl.t_ServDocKind = ? "
          + "           and rl.t_ServDocID = ? "
          + "           and rq.t_ID = rl.t_RQID "
          + "           and dt.t_DocKind = rq.t_DocKind "
          + "           and dt.t_DocID = rq.t_DocID "
          + "        UNION "
          + "        select rq.t_Factdate, tk.t_DealCode, rl.t_PaymentID "
          + "          from drqpmlink_dbt rl, ddlrq_dbt rq, ddl_tick_dbt tk "
          + "         where rl.t_ServDocKind = ? "
          + "           and rl.t_ServDocID = ? "
          + "           and rq.t_ID = rl.t_RQID "
          + "           and rq.t_DocKind = " + DL_INVESTSHARE
          + "           and tk.t_BOfficeKind = rq.t_DocKind "
          + "           and tk.t_DealID = rq.t_DocID "
          + "        UNION "
          + "        select rq.t_Factdate, cm.t_CommCode, rl.t_PaymentID "
          + "          from drqpmlink_dbt rl, ddlrq_dbt rq, ddl_comm_dbt cm "
          + "         where rl.t_ServDocKind = ? "
          + "           and rl.t_ServDocID = ? "
          + "           and rq.t_ID = rl.t_RQID "
          + "           and cm.t_DocKind = rq.t_DocKind "
          + "           and cm.t_DocumentID = rq.t_DocID "
          + "        UNION "
          + "        select rq.t_Factdate, np.t_Code, rl.t_PaymentID "
          + "          from drqpmlink_dbt rl, ddlrq_dbt rq, dnptxop_dbt np "
          + "         where rl.t_ServDocKind = ? "
          + "           and rl.t_ServDocID = ? "
          + "           and rq.t_ID = rl.t_RQID "
          + "           and np.t_DocKind = rq.t_DocKind "
          + "           and np.t_ID = rq.t_DocID "
          + "        UNION "
          + "        select rq.t_Factdate, tk.t_DealCode, rl.t_PaymentID "
          + "          from drqpmlink_dbt rl, ddlrq_dbt rq, ddl_tick_dbt tk "
          + "         where rl.t_ServDocKind = ? "
          + "           and rl.t_ServDocID = ? "
          + "           and rq.t_ID = rl.t_RQID "
          + "           and rq.t_DocKind IN ("+DL_SECUROWN+","+DL_RETIREMENT_OWN+") "
          + "           and tk.t_DealID = rq.t_DocID "
          + "        ) q"
          + " where pm.t_PaymentID(+) = q.t_PaymentID "
          + "   and purp.t_Purpose(+) = pm.t_Purpose ";

  query = query + " order by q.t_PaymentID ASC, q.t_Factdate ASC ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(reccomm.rec.DocKind);
  cmd.AddParam(reccomm.rec.DocumentID);
  cmd.AddParam(reccomm.rec.DocKind);
  cmd.AddParam(reccomm.rec.DocumentID);
  cmd.AddParam(reccomm.rec.DocKind);
  cmd.AddParam(reccomm.rec.DocumentID);
  cmd.AddParam(reccomm.rec.DocKind);
  cmd.AddParam(reccomm.rec.DocumentID);
  cmd.AddParam(reccomm.rec.DocKind);
  cmd.AddParam(reccomm.rec.DocumentID);

  cnt = cmd.GetCount();
  if(cnt > 0)

    println("");
    println(" Успешно сформированы платежи");
    println("┌────────────────┬────────────┬────────────┬────────────────────────────────┐");
    println("│Фактическая дата│  № сделки  │ ID платежа │Назначение платежа              │");
    println("├────────────────┼────────────┼────────────┼────────────────────────────────┤");

    DataSet = cmd.Execute();

    while(DataSet.moveNext())
            [│################│############│############│################################│]
            (date(DataSet.FactDate):f:c, DataSet.DealCode:c, int(DataSet.PaymentID):r, DataSet.PurpShortName:l);


    end;

    println("└────────────────┴────────────┴────────────┴────────────────────────────────┘");
    println("");
  end;


  ReportLineData_XML = GetLogDataXML(reccomm.rec.DocKind, reccomm.rec.DocumentID, LOG_TYPE_ERROR, false);
  ReportLineData_XML.SetReportNumber(0);
  
  stat = ReportLineData_XML.Next();
  if(stat)

    println("");
    println("Сообщения");
    println("┌──────────┬──────────────────┬────────────┬──────────────────────────────────────────────────────────────────────┐");
    println("│ № сделки │        ТО        │ № ошибки   │Примечание                                                            │");
    println("├──────────┼──────────────────┼────────────┼──────────────────────────────────────────────────────────────────────┤");
    while(stat)
      ErrData = ReportLineData_XML.GetReportLineData();
            [│##########│##################│############│######################################################################│]
            (string(ErrData.DealCode):c, string(ErrData.RqInfo):c:w, string(ErrData.ErrCode):r, string(ErrData.ErrStr):l:w);

      stat = ReportLineData_XML.Next();
    end;
    println("└──────────┴──────────────────┴────────────┴──────────────────────────────────────────────────────────────────────┘");
    
  end;
  
  SetOutput( NULL, true );
  close( ReportOutFile );
  ViewFile( ReportOutFile );

  return true;
END;

PRIVATE MACRO GetDealCode(DocKind, DocID)
  var cmd, DataSet;
  var DealCode = "";

  cmd = DL_RSDCommand(  " select dt.t_DealCode "
                      + "   from dv_sctotaldeals dt "
                      + "  where dt.t_DocKind = ? "
                      + "    and dt.t_DocID = ?"
                     );
  cmd.AddParam(DocKind);
  cmd.AddParam(DocID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DealCode = DataSet.DealCode;
  end;

  return DealCode;
END;

private macro GetRQ_INCREPO(RQ, RQ_INCREPO:@VARIANT):integer
  var RetVal = 0;
  var cmd = DL_RSDCommand();
  var query = " SELECT * " +
              "   FROM DDLRQ_DBT " +
              "  WHERE T_DOCKIND = ? " +
              "    AND T_DOCID = ? " +
              "    AND T_TYPE = ? " +
              "    AND T_FIID = ? " +
              "    AND T_DEALPART = 2 ";

  cmd.AddParam(RQ.rec.DOCKIND);
  cmd.AddParam(RQ.rec.DOCID);
  cmd.AddParam(DLRQ_TYPE_INCREPO);
  cmd.AddParam(RQ.rec.FIID);
  var DataSet = cmd.Execute(query);
  if( DataSet.moveNext() )
      DataSet.GetRecord().CopyTo( RQ_INCREPO.rec );
      RetVal = 1;
  end;
  return RetVal;
end;

private macro isPlusLeg(RQ):integer
  var RetVal = 0;
  var cmd = DL_RSDCommand();
  var query = " SELECT T_INCOMERATE as Rate " +
              "   FROM DDL_LEG_DBT " +
              "  WHERE T_DEALID = ? " +
              "    AND T_LEGKIND = 2 " ;

  cmd.AddParam(RQ.rec.DOCID);
  var DataSet = cmd.Execute(query);
  if( DataSet.moveNext() )
     if(DataSet.Rate>0)
        RetVal = 1;
     end;
  end;
  return RetVal;
end;

private macro isClient(RQ):integer
  var RetVal = 0;
  var cmd = DL_RSDCommand();
  var query = " SELECT T_CLIENTID as CLIENTID " +
              "   FROM DDL_TICK_DBT " +
              "  WHERE T_DEALID = ? " +
              "    AND T_BOFFICEKIND = 101 " ;

  cmd.AddParam(RQ.rec.DOCID);
  var DataSet = cmd.Execute(query);
  if( DataSet.moveNext() )
     if(DataSet.ClientID>0)
        RetVal = 1;
     end;
  end;
  return RetVal;
end;

private macro ПолучитьСубъектаПоКоду(CodeKind, Code)
  var query, cmd, DataSet;
  var PartyID = -1;
  
  query =   " select objcode.t_ObjectID "
          + "   from dobjcode_dbt objcode "
          + "  where objcode.t_ObjectType = " + OBJTYPE_PARTY
          + "    and objcode.t_CodeKind = ? " 
          + "    and objcode.t_State = 0 "
          + "    and objcode.t_Code = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(CodeKind);
  cmd.AddParam(Code);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    PartyID = DataSet.ObjectID;
  end;

  return PartyID;
end;

//Simanov. MackeCarry - Делать проводку по платежу
MACRO CreatePaymByRQ(dl_comm, CurRQ:TRecHandler, CurFD, REPO2_Own, Amount, FIID, ObjKind, ObjID, Ground, ownhist, IsStep, MakeCarry)
  var RQPrc2 = TRecHandler("dlrq.dbt");
  var IsOwn2 = false;
  var err = 0;
  var d, m, y;
  var cmd;

  //Simanov. Чтобы не передавать целую кучу параметров, когда не надо
  ByDefault(REPO2_Own, false);
  ByDefault(Amount, 0);
  ByDefault(Ground, "");
  ByDefault(IsStep, true);
  ByDefault(MakeCarry, false);

  RQPrc2.clear;

  if(CurFD != NULL)
      
    var pmrq  = DLRQ_PAYM(CurRQ, CurFD, true);
    var pmObj = RsbPayment(0);

    pmObj.DocKind    = CurRQ.rec.DocKind;
    pmObj.DocumentID = CurRQ.rec.DocID;

    pmObj.Purpose    = pmrq.Purpose;
    pmObj.SubPurpose = pmrq.SubPurpose;

    pmObj.ValueDate  = 
    pmObj.Date       = pmrq.ValueDate;

    if(Amount > 0)
      pmObj.OrderFIID   = FIID;
      pmObj.OrderAmount = Amount;

      pmObj.BaseFIID   = FIID;  
      pmObj.BaseAmount = Amount;

    else
      pmObj.OrderFIID   = pmrq.OrderFIID;
      pmObj.OrderAmount = pmrq.OrderAmount;

      pmObj.BaseFIID   = pmrq.BaseFIID;
      pmObj.BaseAmount = pmrq.BaseAmount;
      if( (REPO2_Own) and (CurRQ.rec.Dealpart = 2) and (GetRQ_INCREPO(CurRQ, RQPrc2)) and (not isClient(CurRQ)))
         IsOwn2 = true;
         if(isPlusLeg(CurRQ)) //положительная ставка
            pmObj.BaseAmount = pmrq.BaseAmount + RQPrc2.rec.Amount;
         else
            pmObj.BaseAmount = pmrq.BaseAmount - RQPrc2.rec.Amount;
         end;
      end;
    end;

    pmObj.PayerAmount = pmObj.ReceiverAmount = pmObj.BaseAmount; //Simanov. Не для налоговых платежей суммы плательщика и получателя не помешают тоже

    if((CurRQ.rec.Type == DLRQ_TYPE_TAX_ULN) or (CurRQ.rec.Type == DLRQ_TYPE_TAX_FLN) or (CurRQ.rec.Type == DLRQ_TYPE_TAX_FLR) or (CurRQ.rec.Type == DLRQ_TYPE_TAX_FLR_ADD))
      pmObj.TaxAuthorState = "02";

      pmObj.PayerAmount = pmObj.ReceiverAmount = pmObj.BaseAmount;

      if(CurRQ.rec.Type == DLRQ_TYPE_TAX_ULN)
        pmObj.BttTICode = "18210101030011000110";
        pmObj.PaymentByOtherPerson = PM_OTHRPRSN_INST;
      elif(CurRQ.rec.Type == DLRQ_TYPE_TAX_FLR_ADD)
        pmObj.BttTICode = "18210102080011000110";
        pmObj.PaymentByOtherPerson = PM_OTHRPRSN_PRSN;
      else
        pmObj.BttTICode = "18210102010010000110";
        pmObj.PaymentByOtherPerson = PM_OTHRPRSN_PRSN;
      end;

      pmObj.TaxPmGround = "ТП";


      DateSplit(pmrq.ValueDate, null, m, y);

      if(m <= 9)
        m = "0"+m;
      end;

      pmObj.OKATOCode = ПолучитьКодСубъекта({OurBank}, 74/*Код ОКТМО*/);
      pmObj.TaxPmPeriod = "МС."+m+"."+y;
      pmObj.TaxPmNumber = "0";
      pmObj.TaxPmDate   = pmrq.ValueDate;
      pmObj.TaxPmType   = "0";
      pmObj.PayType     = BPT_TAX;

      pmObj.PayerBankEnterDate = pmrq.ValueDate;
      pmObj.PayerChargeOffDate = pmrq.ValueDate;
      
    end;

    if(ValType(ownhist) != V_UNDEF)
      pmObj.Number  = CurFD.tick.rec.DealCode + "/" + string(ObjID);
    elif(pmrq.IsTick())
      pmObj.Number  = CurFD.tick.rec.DealCode;
    elif (pmrq.IsNptx() ) //Simanov. Для операций списания/зачисления ДС
      pmObj.Number = CurRQ.rec.ID;
      pmObj.NumberPack = 11;
    else
      pmObj.Number  = GetDealCode(CurRQ.rec.DocKind, CurRQ.rec.DocID);
    end;
    if(pmObj.Number == "")
      pmObj.Number   = dl_comm.rec.CommCode;
    end;

    pmObj.SetPayerPI (PAYMENTS_GROUP_UNDEF,                      
                      pmrq.PayerBankID,                          
                      pmrq.PayerBankCodeKind,                    
                      pmrq.PayerBankCode,                        
                      pmrq.PayerBankName,                        
                      pmrq.PayerCorrAcc,                         
                      pmrq.PayerAccountRec.rec.Code_Currency,
                      1,                                         
                      pmrq.PayerAccount,                         
                      pmrq.Payer,                                
                      pmrq.PayerName,                            
                      pmrq.PayerINN,                             
                      pmrq.PayerCodeKind,                        
                      pmrq.PayerCode,                            
                      pmrq.PayerCorSchem,                        
                      0);                                       

    if(ValType(ownhist) != V_UNDEF)

      pmObj.SetReceiverPI (PAYMENTS_GROUP_UNDEF, 
                           ПолучитьСубъектаПоКоду(PTCK_BIC, ownhist.rec.ReceiverBankBIC),
                           PTCK_BIC,
                           ownhist.rec.ReceiverBankBIC,
                           ownhist.rec.ReceiverBankName,
                           ownhist.rec.ReceiverCorrAccount,
                           FIID,
                           1,
                           ownhist.rec.ReceiverAccount,
                           -1,
                           ownhist.rec.ReceiverName,
                           ownhist.rec.ReceiverINN,
                           0,
                           "",
                           -1,
                           0);
    else
    
      pmObj.SetReceiverPI (PAYMENTS_GROUP_UNDEF, 
                           pmrq.ReceiverBankID,
                           pmrq.ReceiverBankCodeKind,
                           pmrq.ReceiverBankCode,
                           pmrq.ReceiverBankName,
                           pmrq.ReceiverCorrAcc,
                           pmrq.ReceiverAccountRec.rec.Code_Currency,
                           1,
                           pmrq.ReceiverAccount,
                           pmrq.Receiver,
                           pmrq.ReceiverName,
                           pmrq.ReceiverINN,
                           pmrq.ReceiverCodeKind,
                           pmrq.ReceiverCode,
                           pmrq.ReceiverCorSchem,
                           0);
    end;                                                           

    
    
    pmObj.Department  = pmrq.Department;
    pmObj.PaymStatus  = pmrq.PaymStatus;
    pmObj.Origin      = PAYMENT_OR_AUTO;
    pmObj.Netting     = UNSET_CHAR;
    pmObj.Priority    = 5;

    if(ValType(ownhist) != V_UNDEF)
      pmObj.ShifrOper  = "01";
    else
      pmObj.ShifrOper  = pmrq.ShifrOper; 
    end;
    
    pmObj.Oper = GetMainOperInGroup(pmObj.DocKind);
    if (pmObj.Oper <= 0)
      pmObj.Oper = {oper};
    end;

    if(Ground == "")
      pmObj.Ground = pmrq.Ground;

      if(pmrq.IsTick())
        SP_SetPaymentGround( pmObj.GetPM_PAYM(), -1, CurFD );
      end;
    else
      pmObj.Ground = Ground;
    end;

    if(Not IsStep)
       RslDefCon.BeginTrans();
    end;

    pmObj.IsFactPaym  = "X";
    pmObj.PrimDocKind = DOC_BO_PAYMENT;
    pmObj.Actuate();

    //err = pmObj.Update(); //Simanov. Без этого не сохраниться? Если нет, то юзать только если isstep == false, т.к. это обновление платежа вне операции.

    if((not err) AND (Not IsStep))
      if(PM_BOOperation(pmObj) == false)
        err = 1;
      end;
    end;

    if ( not err and (valtype(dl_comm) != V_UNDEF) ) //Simanov. Для операций списания/зачисления ДС dl_comm не существует
      var ins_rqpmlink = RsbSQLInsert("rqpmlink.dbt");
      var RqPmLink = TRecHandler("rqpmlink.dbt");

      cmd = DL_RSDCommand("DELETE FROM drqpmlink_dbt WHERE t_rqid = ? and t_ServDocKind = ? and t_ServDocID = ? and t_ObjKind = ? and t_ObjID = ?");
      cmd.addParam(CurRQ.rec.ID);
      cmd.addParam(dl_comm.rec.DocKind);
      cmd.addParam(dl_comm.rec.DocumentID);
      cmd.addParam(ObjKind);
      cmd.addParam(ObjID);
      cmd.ExecuteCMD();

      RqPmLink.Clear();

      RqPmLink.rec.RQID        = CurRQ.rec.ID;
      RqPmLink.rec.ServDocKind = dl_comm.rec.DocKind;
      RqPmLink.rec.ServDocID   = dl_comm.rec.DocumentID;
      RqPmLink.rec.PaymentID   = pmObj.PaymentID;
      RqPmLink.rec.ObjKind     = ObjKind;
      RqPmLink.rec.ObjID       = ObjID;
      
      ins_rqpmlink.AddRecord(RqPmLink);
      if(not ins_rqpmlink.Insert())
        msgbox("Ошибка при создании записей в drqpmlink_dbt");
        err = 1;
      end;
      
    end;

    if( (not err) and (IsOwn2) and (valtype(dl_comm) != V_UNDEF) )  //Simanov. Сюда в операции списания/зачисления попасть не должны. Но вдруг!
      ins_rqpmlink = RsbSQLInsert("rqpmlink.dbt");
      RqPmLink = TRecHandler("rqpmlink.dbt");

      cmd = DL_RSDCommand("DELETE FROM drqpmlink_dbt WHERE t_rqid = ? and t_ServDocKind = ? and t_ServDocID = ? and t_ObjKind = ? and t_ObjID = ?");
      cmd.addParam(RQPrc2.rec.ID);
      cmd.addParam(dl_comm.rec.DocKind);
      cmd.addParam(dl_comm.rec.DocumentID);
      cmd.addParam(ObjKind);
      cmd.addParam(ObjID);
      cmd.ExecuteCMD();

      RqPmLink.Clear();

      RqPmLink.rec.RQID        = RQPrc2.rec.ID;
      RqPmLink.rec.ServDocKind = dl_comm.rec.DocKind;
      RqPmLink.rec.ServDocID   = dl_comm.rec.DocumentID;
      RqPmLink.rec.PaymentID   = pmObj.PaymentID;
      RqPmLink.rec.ObjKind     = ObjKind;
      RqPmLink.rec.ObjID       = ObjID;
      
      ins_rqpmlink.AddRecord(RqPmLink);
      if(not ins_rqpmlink.Insert())
        msgbox("Ошибка при создании записей в drqpmlink_dbt");
        err = 1;
      end;
    end;

    //Simanov. Создание проводки
    if ( (not err) and MakeCarry)
      var tr = pmobj.MakeTransaction();
      if( tr == NULL )
        msgbox("Ошибка при создании проводки по платежу");
        err = 1;
      else
        tr.Number_Pack = pmobj.NumberPack;
        if( not tr.Carry() )
          msgbox("Ошибка при выполнении проводки");
          err = 1;
        end;
      end;

      //Прикрепить код документа БИС для проводки по списанию
      if (pmrq.IsNptx() )
        Connect_attr_to_trn (tr.acctrnid, ATTR_GROUP_DOCUMENT_BIS, CFTDocumentTypeCodes.BANK_ORDER);
      end;
    end;

    if(Not IsStep)
      if(not err)
        RslDefCon.CommitTrans();
      else
        RslDefCon.RollbackTrans();
      end;
    end;
  end;

  return err;

END;

//Запуск выгрузки платежей
PRIVATE MACRO _StartCreatingPayms(dl_comm)
  var err = 0, cnt = 0, i = 0;
  var query, cmd, DataSet;
  var q, cmd1, ds, cnt1 = 0;
  var j = 0;
  var PrevDocKind = 0, PrevDocID = 0, PrevDealPart = 0;
  var RqPmLinkArr = TArray();
  var REPO2_Own  = ЕдиныйПлатежПо2ЧастиСобРЕПО();
  var leg2;
  var tick;
  var Ground = "";

  cmd = DL_RSDCommand();

  query =   " select rq.* "
          + "   from ddlrq_dbt rq, ddlrqacc_dbt rqacc "
          + "  where rq.t_Kind = " + DLRQ_KIND_COMMIT
          + "    and rq.t_SubKind = " + DLRQ_SUBKIND_CURRENCY
          + "    and rq.t_State = " + DLRQ_STATE_EXEC
          + "    and rq.t_FactDate >= ? /*1*/"
          + "    and rq.t_FactDate <= ? /*2*/"
          + "    and rq.t_Netting = CHR(0) "
          + "    and (rq.t_type = " + DLRQ_TYPE_TAX_ULN
          + "         or not Exists(select 1 from drqpmlink_dbt rl where rl.t_RQID = rq.t_ID)"
          + "        )"
          + "    and not Exists(select 1 from ddlrq_upgr_dbt rqup where rqup.t_RQID = rq.t_ID)"
          + "    and rqacc.t_ID = rq.t_RqAccID ";
  if(REPO2_Own)
    query = query + "and not Exists(select 1 from ddlrq_dbt rq1, ddl_tick_dbt tick where rq1.t_Type = " + DLRQ_TYPE_INCREPO 
                  + " and rq1.t_ID = rq.t_ID and rq1.t_DealPart = 2 "
                  + " and tick.t_DealID = rq1.t_docID "
                  + " and tick.t_BofficeKind = " + DL_SECURITYDOC   
                  + " and tick.t_ClientID = -1 ) "; 
  end;                                                                                  
          
  cmd.AddParam(dl_comm.rec.BeginDate); /*1*/
  cmd.AddParam(dl_comm.rec.EndDate);   /*2*/

  if(dl_comm.rec.Value == 1) //только внешние
    query = query + " and rqacc.t_BankID > 0 and rqacc.t_BankID <> ? ";
    cmd.AddParam({OurBank});
  elif(dl_comm.rec.Value == 2) //только внутренние
    query = query + " and rqacc.t_BankID = ? ";
    cmd.AddParam({OurBank});
  end;

  query = query + " order by rq.t_DocKind ASC, rq.t_DocID ASC, rq.t_FactDate ASC, rq.t_ID ASC ";

  cnt = cmd.GetCount(query);
  if(cnt > 0)
    InitProgress( cnt, "Выполнение операции ...", "Выгрузка платежей" );
    
    DataSet = cmd.Execute(query);
    while(DataSet.moveNext())

      CurRQ = TRecHandler("dlrq.dbt");
      DataSet.GetRecord().CopyTo( CurRQ.rec );
      
      if((PrevDocKind != CurRQ.rec.DocKind) OR (PrevDocID != CurRQ.rec.DocID) OR (PrevDealPart != CurRQ.rec.DealPart))
        if((CurRQ.rec.DocKind == DL_SECURITYDOC) or 
           (CurRQ.rec.DocKind == DL_RETIREMENT) or 
           (CurRQ.rec.DocKind == DL_INVESTSHARE) or
           (CurRQ.rec.DocKind == DL_SECUROWN) or
           (CurRQ.rec.DocKind == DL_RETIREMENT_OWN)
          ) //Сделки и погашения
          CurFD = SPFirstDoc( IIF(CurRQ.rec.DealPart == 2, LEG_KIND_DL_TICK_BACK, LEG_KIND_DL_TICK), CurRQ.rec.DocID );
        elif(CurRQ.rec.DocKind == DL_SETTLEMENT) //Расчеты на бирже
          CurFD = SPFirstDocDLCOMM( CurRQ.rec.DocKind, CurRQ.rec.DocID );
        elif(CurRQ.rec.DocKind == DL_WRTMONEY) //Зачисление/списание ДС
          CurFD = DLFirstDocNPTXOP(CurRQ.rec.DocKind, CurRQ.rec.DocID);
        elif(CurRQ.rec.DocKind == DL_NTGSEC) //Неттинг
          CurFD = DL_NettingDoc(CurRQ.rec.DocKind, CurRQ.rec.DocID);
        else
          CurFD = NULL;
          msgbox("Документ вида "+CurRQ.rec.DocKind+" не обрабатывается");
        end;
      end;

      if((CurRQ.rec.DocKind == DL_RETIREMENT_OWN) and
         ((CurRQ.rec.Type == DLRQ_TYPE_PAYMENT) or (CurRQ.rec.Type == DLRQ_TYPE_PAYINCOME)) and 
         (CurRQ.rec.Party == {OurBank}) 
        )
        var hs = TRecHandler("scownhist.dbt");
        //Формируем платеж для каждого получателя дохода

        q =   " select hs.*, tx.t_SHID, tx.t_NomSum, tx.t_TaxSum, tx.t_PayCur, fin.t_Name, "
            + "        (tx.t_CoupSum "
            + "         - ROUND((CASE WHEN tx.t_TaxSum > 0 THEN NVL(RSB_FIInstr.ConvSum(tx.t_TaxSum, "+NATCUR+", tx.t_PayCur, ?), 0) ELSE 0 END), 2) "
            + "         - ROUND((CASE WHEN tx.t_TaxSum15 > 0 THEN NVL(RSB_FIInstr.ConvSum(tx.t_TaxSum15, "+NATCUR+", tx.t_PayCur, ?), 0) ELSE 0 END), 2)) as PayCoupSum "
            + "   from doproper_dbt op, dscowntax_dbt tx, dscownhist_dbt hs, dscownlist_dbt ls, dfininstr_dbt fin "
            + "  where op.t_DocKind = ? " 
            + "    and op.t_DocumentID = LPAD(?, 34, '0') "
            + "    and tx.t_ID_Operation = op.t_ID_Operation "
            + "    and tx.t_PaySum > 0 "
            + "    and not Exists(select 1 from drqpmlink_dbt lnk where lnk.t_RQID = ? and lnk.t_ObjKind = "+OBJTYPE_SCOWNHIST+" and lnk.t_ObjID = tx.t_SHID)"
            + "    and hs.t_ID = tx.t_SHID "
            + "    and ls.t_ListID = hs.t_ListID "
            + "    and fin.t_FIID = ls.t_FIID ";

        cmd1 = DL_RSDCommand(q);

        cmd1.AddParam(CurRQ.rec.FactDate);
        cmd1.AddParam(CurRQ.rec.FactDate);
        cmd1.AddParam(CurRQ.rec.DocKind);
        cmd1.AddParam(CurRQ.rec.DocID);
        cmd1.AddParam(CurRQ.rec.ID);

        cnt1 = cmd1.GetCount();
        if(cnt1 > 0)
          var PaymSum = $0;
          var PaymCur = -1;

          InitProgress( cnt1, "Выполнение операции ...", "Выгрузка платежей для владельцев облигаций" );

          j = 0;
          ds = cmd1.Execute();
          while(ds.moveNext())
            ds.GetRecord().CopyTo(hs.rec);

            if(CurRQ.rec.Type == DLRQ_TYPE_PAYINCOME)
              Ground = "Выплата дохода по купону облигации "+ds.Name+".";

              PaymSum = ds.PayCoupSum;
              PaymCur = ds.PayCur;
            else
              Ground = "Выплата номинальной стоимости облигации "+ds.Name+".";

              PaymSum = ds.NomSum;
              PaymCur = ds.PayCur;
            end;

            CreatePaymByRQ(dl_comm, CurRQ, CurFD, REPO2_Own, PaymSum, PaymCur, OBJTYPE_SCOWNHIST, ds.SHID, Ground, hs, false); // Golovkin upg + пользовательский параметр , false 

            UseProgress(j = j + 1);
          end;

          RemProgress();
        end;
      elif(CurRQ.rec.Type == DLRQ_TYPE_TAX_ULN)
        //формируем платеж для каждого ЮЛН

        q =   " select tx.t_SHID, tx.t_TaxRate, tx.t_TaxSum, pt.t_Name, "
            + "        rsi_rsbparty.GetPartyCode(pt.t_PartyID, "+PTCK_INN+") as INN "
            + "   from doproper_dbt op, dscowntax_dbt tx, dparty_dbt pt "
            + "  where op.t_DocKind = ? " 
            + "    and op.t_DocumentID = LPAD(?, 34, '0') "
            + "    and tx.t_ID_Operation = op.t_ID_Operation "
            + "    and pt.t_PartyID = tx.t_FactReceiverID "
            + "    and pt.t_LegalForm = " + PTLEGF_INST
            + "    and tx.t_TaxSum > 0 "
            + "    and not Exists(select 1 from drqpmlink_dbt lnk where lnk.t_ObjKind = "+OBJTYPE_SCOWNHIST+" and lnk.t_ObjID = tx.t_SHID)";

        cmd1 = DL_RSDCommand(q);

        cmd1.AddParam(CurRQ.rec.DocKind);
        cmd1.AddParam(CurRQ.rec.DocID);

        cnt1 = cmd1.GetCount();
        if(cnt1 > 0)
        
          InitProgress( cnt1, "Выполнение операции ...", "Выгрузка платежей для ЮЛН" );

          j = 0;
          ds = cmd1.Execute();
          while(ds.moveNext())
            Ground = string(ds.Name + " // ИНН/КПП " + ds.INN + " // Налог на доходы иностранных организаций (" + ds.TaxRate + ") при погашении купона по облигациям " +  CurFD.fininstr().rec.Name + " серии " + CurFD.avoiriss().rec.Series + ", гос.рег.№ " + CurFD.avoiriss().rec.LSIN + ".");

            CreatePaymByRQ(dl_comm, CurRQ, CurFD, REPO2_Own, ds.TaxSum, NATCUR, OBJTYPE_SCOWNHIST, ds.SHID, Ground, null, false); // Golovkin upg + дистрибутивный параметр , null

            UseProgress(j = j + 1);
          end;

          RemProgress();
        end;

      elif((CurRQ.rec.Type == DLRQ_TYPE_TAX_FLR) or (CurRQ.rec.Type == DLRQ_TYPE_TAX_FLR_ADD))
        //Сначала формируем платеж на само ТО
        CreatePaymByRQ(dl_comm, CurRQ, CurFD, REPO2_Own, 0, -1, 0, 0, "");

        if(CurRQ.rec.DocKind == DL_RETIREMENT_OWN)
          //Если это операция погашения ОЭБ, то возможны перераспределения сумм налога в операции расчета НОБ, сформированной на шаге.
          //Для них сформируем также платежи.

          q =   " select (select count(1) from ddlrq_dbt where t_DocKind = ? and t_DocID = ? and t_Type = "+DLRQ_TYPE_TAX_FLR+") as CntFLR, "
              + "        (select count(1) from ddlrq_dbt where t_DocKind = ? and t_DocID = ? and t_Type = "+DLRQ_TYPE_TAX_FLR_ADD+") as CntFLR_Add "
              + "   from dual";

          cmd1 = DL_RSDCOmmand(q);
          cmd1.AddParam(CurRQ.rec.DocKind);
          cmd1.AddParam(CurRQ.rec.DocID);
          cmd1.AddParam(CurRQ.rec.DocKind);
          cmd1.AddParam(CurRQ.rec.DocID);

          ds = cmd1.Execute();
          if(ds.moveNext())
            //Если в погашении есть ТО обоих видов налога, то каждое перераспределение привязываем к соответствующему ТО.
            //Если только одно ТО, то все перераспределения привязываем к нему.
            var RedistrMainTax = $0, RedistrTax15 = $0;
            var saveRQKind = CurRQ.rec.Kind;

            ОЭБ_ПолучитьПераспределенныеСуммыНалога(CurRQ.rec.DocKind, CurRQ.rec.DocID, @RedistrMainTax, @RedistrTax15);

            if((ds.CntFLR > 0) and (ds.CntFLR_Add > 0))
              if((CurRQ.rec.Type == DLRQ_TYPE_TAX_FLR) and (RedistrMainTax != 0))
                if(RedistrMainTax < 0)
                  //Подменяем тип ТО, чтобы был платеж на возврат
                  CurRQ.rec.Kind = DLRQ_KIND_REQUEST;
                end;
                
                CreatePaymByRQ(dl_comm, CurRQ, CurFD, REPO2_Own, abs(RedistrMainTax), NATCUR, OBJTYPE_DLRQ, -1, "Перераспределение сумм налога. "+IIF(RedistrMainTax < 0, "Возврат", "Удержание"));
                CurRQ.rec.Kind = saveRQKind;
              end;

              if((CurRQ.rec.Type == DLRQ_TYPE_TAX_FLR_ADD) and (RedistrTax15 != 0))
                if(RedistrTax15 < 0)
                  //Подменяем тип ТО, чтобы был платеж на возврат
                  CurRQ.rec.Kind = DLRQ_KIND_REQUEST;
                end;
                
                CreatePaymByRQ(dl_comm, CurRQ, CurFD, REPO2_Own, abs(RedistrTax15), NATCUR, OBJTYPE_DLRQ, -2, "Перераспределение сумм налога. "+IIF(RedistrTax15 < 0, "Возврат", "Удержание"));
                CurRQ.rec.Kind = saveRQKind;
              end;
            else
              if(RedistrMainTax != 0)
                if(RedistrMainTax < 0)
                  //Подменяем тип ТО, чтобы был платеж на возврат
                  CurRQ.rec.Kind = DLRQ_KIND_REQUEST;
                end;
                
                CreatePaymByRQ(dl_comm, CurRQ, CurFD, REPO2_Own, abs(RedistrMainTax), NATCUR, OBJTYPE_DLRQ, -1, "Перераспределение сумм налога. "+IIF(RedistrMainTax < 0, "Возврат", "Удержание"));
                CurRQ.rec.Kind = saveRQKind;
              end;

              if(RedistrTax15 != 0)
                if(RedistrTax15 < 0)
                  //Подменяем тип ТО, чтобы был платеж на возврат
                  CurRQ.rec.Kind = DLRQ_KIND_REQUEST;
                end;
                
                CreatePaymByRQ(dl_comm, CurRQ, CurFD, REPO2_Own, abs(RedistrTax15), NATCUR, OBJTYPE_DLRQ, -2, "Перераспределение сумм налога. "+IIF(RedistrTax15 < 0, "Возврат", "Удержание"));
                CurRQ.rec.Kind = saveRQKind;
              end;
            end;

          end;
        end;
      else
        CreatePaymByRQ(dl_comm, CurRQ, CurFD, REPO2_Own, 0, -1, 0, 0, "", null, false); // Golovkin upg + дистрибутивный параметр , null
      end;

      PrevDocKind  = CurRQ.rec.DocKind; 
      PrevDocID    = CurRQ.rec.DocID; 
      PrevDealPart = CurRQ.rec.DealPart;

      UseProgress(i = i + 1);
    end;

    RemProgress();
  else
    msgbox("Не найдено подходящих ТО для формирования платежей. Сервисная операция закрыта.");
  end;  

  return err;
OnError(er)
   msgbox(er.module+ " "+er.line+" "+er.message);
   if(RslDefCon.IsInTrans)
     RslDefCon.RollbackTrans();
   end;
   return 1;
END;

MACRO StartCreatingPayms(DocumentID)
  var dl_comm = TBFile("dl_comm.dbt");
  var stat = 0;

  dl_comm.rec.DocumentID = DocumentID;
  if(not dl_comm.GetEQ())
    msgbox("Ошибка поиска операции");
    return 1;
  end;

  ReplaceMacro( "msgbox", "CrPayms_msgbox" );

  RepErrArr.size = 0;

  stat = _StartCreatingPayms(dl_comm);

  SaveCrPaymsErrForReport_XML( RepErrArr, dl_comm.rec.DocumentID, dl_comm.rec.DocKind );

  ReplaceMacro( "msgbox", NULL );

  return stat
END;