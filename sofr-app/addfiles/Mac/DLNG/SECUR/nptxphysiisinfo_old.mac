/*
$Name:        nptxphysiisinfo.mac
$Module:      Ценные бумаги
$Description: Печать отчета "Сведения о ФЛ и его ИИС"
*/

import "nptxphysiisinfo_old_form.mac", "spRepFun.mac", "RepPersonalIncomeTax.mac";
import "dlmisc.mac";

//значения доходов и вычетов
PRIVATE CLASS NPTXCodeParm(_PCode, _PSum, _MCode, _MSum)
  var PSCode  = _PCode;
  var PSnnn   = _PSum; 
  var MSCode  = _MCode;
  var MSnnn   = _MSum;
END;
                                
PRIVATE CLASS (NPTXRepCalcCommon) NPTXPHYSIISINFO( _RepDate, _Investor )

  MACRO FIO():STRING
     return (LastName()+" "+FirstName()+" "+MiddleName());
  END;

  MACRO ClientINN():STRING
     return m_RS.ClientINN;
  END;

  MACRO Document():STRING
     var v_Document = "";
     if( ClientPersnIdcMain().rec.PersonID > 0 )
        v_Document = DocumentName()+", "+DocumentNumber()+", "+ClientPersnIdcMain().rec.PaperIssuer + " " + ClientPersnIdcMain().rec.PaperIssuerCode + ", "+string(ClientPersnIdcMain().rec.PaperIssuedDate);
     end;
     return v_Document;
  END;

  MACRO ResidenceAdress():STRING
    return GetPartyAddress(m_RS.PartyID, true, false, PTADDR_LEGAL);
  END;

  MACRO OurAdress():STRING
    return GetPartyAddress({OurBank}, true, false, PTADDR_LEGAL);
  END;

  MACRO MinusSum()
    return m_MinusSum;
  END;

  private macro I_ИИС(pClient, pBegDate, pEndDate, Str5, Str4)
    var StrSQL, Query, DataSet;
    var Sum1 = 0, Sum2 = 0;
   
    Query = DL_RSDCommand();
   
    StrSQL =   " select NVL(sum(N.t_Sum0), 0) Sum1"
              + "   from dnptxobj_dbt N "
              + "  where N.t_Kind = " + TXOBJ_PLUS_SEC
              + "    and N.t_Direction = " + TXOBJ_DIR_IN
              + "    and N.t_Client = ? "
              + "    and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6 )  = 1 "
              + "    and N.t_Date >= ? " 
              + "    and N.t_Date <= ? "; 
   
    Query.addParam( pClient );
    //Query.addParam( pIIS);
    Query.addParam( pBegDate );
    Query.addParam( pEndDate );
   
    if((ValType(Str5) != V_UNDEF) and (Str5 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and N.t_Analitic5 " + Str5;
    end;
   
    if((ValType(Str4) != V_UNDEF) and (Str4 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind4 = " + TXOBJ_KIND4010
                      + " and N.t_Analitic4 " + Str4;
    end;
   
    DataSet = Query.Execute(StrSQL);
    if(DataSet.moveNext())
      Sum1 = DataSet.Sum1;
    end;
   
    Query = DL_RSDCommand();
   
    StrSQL =   " select NVL(sum(DECODE(N.t_Direction, "+TXOBJ_DIR_IN+", N.t_Sum0, -N.t_Sum0)), 0) Sum2"
              + "   from dnptxobj_dbt N, dparty_dbt pt "
              + "  where N.t_Kind = " + TXOBJ_PROC_SEC
              + "    and N.t_Client = ? "
              + "    and pt.t_PartyID = N.t_Client " 
              + "    and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6 )  = 1 "
              + "    and N.t_Date >= ? " 
              + "    and N.t_Date <= ? "; 
   
    Query.addParam( pClient );
    //Query.addParam( pIIS);
    Query.addParam( pBegDate );
    Query.addParam( pEndDate );
   
    /*if(pIIS == 0)
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and (N.t_Analitic5 = " + string(TXGROUP_20) + " or (N.t_Analitic5 = " + string(TXGROUP_30) + " and pt.t_NotResident = CHR(88)))";
    end;*/
   
    if((ValType(Str5) != V_UNDEF) and (Str5 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and N.t_Analitic5 " + Str5;
    end;
   
    if((ValType(Str4) != V_UNDEF) and (Str4 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind4 = " + TXOBJ_KIND4010
                      + " and N.t_Analitic4 " + Str4;
    end;
   
    DataSet = Query.Execute(StrSQL);
    if(DataSet.moveNext())
      Sum2 = DataSet.Sum2;
    end;
   
    return Sum1 + Sum2;
  end;

  private macro D_ИИС(pClient, pBegDate, pEndDate, Str5, Str4)
    var StrSQL, Query, DataSet;
    var Sum1 = 0;
   
    Query = DL_RSDCommand();
   
    StrSQL =   " select NVL(sum(N.t_Sum0), 0) Sum1"
              + "   from dnptxobj_dbt N "
              + "  where N.t_Kind IN ("+TXOBJ_MINUS_SEC+","+TXOBJ_OTHER_SEC+") "
              + "    and N.t_Direction = " + TXOBJ_DIR_OUT
              + "    and N.t_Client = ? "
              + "    and RSI_NPTO.CheckObjIIS (N.t_AnaliticKind6, N.t_Analitic6 )  = 1 "
              + "    and N.t_Date >= ? " 
              + "    and N.t_Date <= ? "; 
   
    Query.addParam( pClient );
    //Query.addParam( pIIS);
    Query.addParam( pBegDate );
    Query.addParam( pEndDate );
   
    if((ValType(Str5) != V_UNDEF) and (Str5 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind5 = " + TXOBJ_KIND5010
                      + " and N.t_Analitic5 " + Str5;
    end;
   
    if((ValType(Str4) != V_UNDEF) and (Str4 != ""))
      StrSQL = StrSQL + " and N.t_AnaliticKind4 = " + TXOBJ_KIND4010
                      + " and N.t_Analitic4 " + Str4;
    end;
   
    DataSet = Query.Execute(StrSQL);
    if(DataSet.moveNext())
      Sum1 = DataSet.Sum1;
    end;
   
    return Sum1;
  end;

  MACRO U_ИИС(Str5, Str4)
    var _G_ИИС = GetRubSumByClient(m_Investor, string(TXOBJ_GENERAL_IIS), m_BeginDate, m_EndDate, null, 1);
    var _TI_ИИС = GetRubSumByClient(m_Investor, string(TXOBJ_PLUS_SEC), m_BeginDate, m_EndDate, null, 1);
    var _I_ИИС = I_ИИС(m_Investor, m_BeginDate, m_EndDate, Str5, Str4/*"= "+NPTX_FI_CIRCULATE*/);
    var _D_ИИС = D_ИИС(m_Investor, m_BeginDate, m_EndDate, Str5, Str4/*"= "+NPTX_FI_CIRCULATE*/);

    if(_TI_ИИС == 0)
      return _D_ИИС;
    else
      return _D_ИИС + _G_ИИС * _I_ИИС / _TI_ИИС;
    end;
  END;

  private macro StrMaxFactDate2()
      return
      " (NVL( (SELECT max(dlrq.t_FactDate) " +
      "  FROM ddlrq_dbt dlrq " + 
      "  WHERE dlrq.t_DocKind    = tick.t_BOfficeKind " +
      "      AND dlrq.t_DocID = tick.t_DealID " +
      "      AND dlrq.t_Type    IN (" + DLRQ_TYPE_DELIVERY + "," + DLRQ_TYPE_PAYMENT + ") " +
      "      AND dlrq.t_DealPart    = " + LEG_KIND_DL_TICK_BACK + 
      "      AND dlrq.t_State = " + DLRQ_STATE_EXEC +
      "      AND dlrq.t_FactDate > TO_DATE('01-01-0001','DD-MM-YYYY') " +
      "      ), TO_DATE('01-01-0001','DD-MM-YYYY') ) " +
      " ) ";
   end;

  MACRO RD_ИИС()
    var RD = GetRubSumByClient(m_Investor, string(TXOBJ_PROCREPOTAX), m_BeginDate, m_EndDate, TXOBJ_DIR_OUT, 1);

    /*плюс (рублевая сумма НДР вида <Com> по сделкам РЕПО, у которых максимальная из дат: фактической поставки по 2 части и фактической оплаты по 2 части входит в период расчета, и дата НДР после 31.12.2011)*/
    var QueryCond =   "select NVL(sum(obj.t_Sum0),0) as S0 "
                    + "  from dnptxobj_dbt obj, ddl_tick_dbt tick "                                                         
                    + " where obj.t_Kind = ? "
                    + "   and obj.t_AnaliticKind1 = ? "
                    + "   and tick.t_DealID = obj.t_Analitic1 "
                    + "   and obj.t_Date >= ? "
                    + "   and obj.t_Date <= ? "
                    + "   and obj.t_Direction = ? "
                    + "   and obj.t_Client = ? "
                    + "   and RSI_NPTO.CheckObjIIS ( ?, obj.t_Analitic6 ) = 1 "
                    + "   and rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1 "
                    + "   and RSI_NPTX.CheckCateg(?, 23, LPAD(Tick.t_DealID, 34, '0'), 2)<>1 " //категория "Является налоговым Репо" на сделке DDL_TICK.T_DEALID не задана или не равна False
                    + "   and Tick.t_BOfficeKind = ? "
                    + "   and (" + StrMaxFactDate2() + " between ? and ?) ";
 
    var Query = DL_RSDCommand(QueryCond);
    Query.addParam( TXOBJ_COM );
    Query.addParam( TXOBJ_KIND1010 );
    Query.addParam( m_BeginDate );
    Query.addParam( m_EndDate );
    Query.addParam( TXOBJ_DIR_OUT );
    Query.addParam( m_Investor );
    Query.addParam( TXOBJ_KIND6020 );
    //Query.addParam( pIIS );
    Query.addParam( OBJTYPE_SECDEAL );
    Query.addParam( DL_SECURITYDOC );
    Query.addParam( m_BeginDate );
    Query.addParam( m_EndDate );
 
    var DataSet = Query.execute();
    
    if( DataSet.MoveNext() )
      return RD + Round(money(DataSet.S0));
    else
      return 0;
    end;
  END;

  MACRO LoadMSnnn(InfoRate, CodeStr)
    var MSsum = $0;
    var MSKind = "";
    var MSsum1 = $0, MSsum2 = $0;

    if(CodeStr != NULL)
      if(InfoRate == INFORATE_TOTAL)
        if((CodeStr == "225") or (CodeStr == "Minus_225"))
          return U_ИИС("not in ("+TXGROUP_80+","+TXGROUP_90+")", " = " + NPTX_FI_CIRCULATE);
        elif((CodeStr == "226") or (CodeStr == "Minus_226"))
          return U_ИИС("not in ("+TXGROUP_80+","+TXGROUP_90+")", " = " + NPTX_FI_NOCIRCULATE);
        elif((CodeStr == "227") or (CodeStr == "Minus_227"))
          return U_ИИС("not in ("+TXGROUP_80+","+TXGROUP_90+")", " = " + NPTX_FI_LOSTCIRCULATE);
        elif((CodeStr == "228") or (CodeStr == "Minus_228"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_FULLMINUS_228), m_BeginDate, m_EndDate, null, 1, null);
        elif((CodeStr == "229") or (CodeStr == "Minus_229"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_FULLMINUS_229), m_BeginDate, m_EndDate, null, 1, null);
        elif((CodeStr == "230") or (CodeStr == "Minus_230"))
          return RD_ИИС();
        elif((CodeStr == "231") or (CodeStr == "Minus_231"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_MINUS_SEC_SHORT), m_BeginDate, m_EndDate, null, 1, null, " obj.t_Analitic5 <> " + TXGROUP_30);
        elif((CodeStr == "233") or (CodeStr == "Minus_233"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_MINUSG_233), m_BeginDate, m_EndDate, null, 1, null);
        elif((CodeStr == "234") or (CodeStr == "Minus_234"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_MINUSG_234), m_BeginDate, m_EndDate, null, 1, null);
        elif((CodeStr == "235") or (CodeStr == "Minus_235"))
          return U_ИИС(" in ("+TXGROUP_80+","+TXGROUP_90+")", " = " + NPTX_FI_NOCIRCULATE);
        end;
      elif(InfoRate == INFORATE9_15)
        if((CodeStr == "233") or (CodeStr == "Minus_233"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_MINUS9_233), m_BeginDate, m_EndDate, null, 1, null);
        elif((CodeStr == "234") or (CodeStr == "Minus_234"))
          return GetRubSumByClient(m_Investor, string(TXOBJ_MINUS9_234), m_BeginDate, m_EndDate, null, 1, null);
        end;

        return 0;
      end;

      return 0;
    end;

    return 0;
  END;

  MACRO MSnnn(InfoRate, MSCode, MSCodeStr)
    var MSsum = $0;
    
    var CodeStr = "";

    if( MSCode != null )
      if( ValType(MSCodeStr) == V_STRING )
        var _index = Index(MSCodeStr, "+");

        if( _index > 0 )
          while( _index > 0 )

            CodeStr = substr(MSCodeStr, 1, _index - 1);
            MSsum = MSsum + LoadMSnnn(InfoRate, CodeStr);

            MSCodeStr = substr(MSCodeStr, _index + 1);
            _index = Index(MSCodeStr, "+");
          end;
        end;

        CodeStr = MSCodeStr;
        MSsum = MSsum + LoadMSnnn(InfoRate, CodeStr);
      else
        CodeStr = string(MSCode);
        MSsum = LoadMSnnn(InfoRate, CodeStr);
      end;

    end;

    return MSsum;
  END;
  
  PRIVATE MACRO ДобавитьПараметры( InfoRate, PSCode, PSCodeStr, MSCode, MSCodeStr )

    var PSnnn = $0, MSnnn = $0;

    PSnnn = this.PSnnn(InfoRate, PSCode, PSCodeStr);
    MSnnn = this.MSnnn(InfoRate, MSCode, MSCodeStr);

    if( (PSCode != null ) or (MSnnn != $0) )

       AddInArrCodeParms(m_ArrCodeParms, Month(m_BeginDate), PSCode, PSnnn, MSCode, MSnnn, 0.0, 0.0, 0.0, 0.0, 0.0, Rate(InfoRate), PSnnn - MSnnn);

       m_PlusSum  = m_PlusSum  + PSnnn;
       m_MinusSum = m_MinusSum + MSnnn;
    end;
  END;

  MACRO ДобавитьВсеПараметры(RateIndexes)
    var OldSize = m_ArrCodeParms.Size;
    if(m_InfoRate == INFORATE9_15)

       if( OTHER_CONTRACTS == SET_CHAR )
          ДобавитьПараметры(m_InfoRate, IIF(m_CurrSign == 2, null, 1010), null/*, 601*/);
       end;

       if( (OTHER_CONTRACTS == SET_CHAR) and (IIS_CONTRACTS == SET_CHAR) )
          ДобавитьПараметры(m_InfoRate, 1110, "1110+1110_ИИС"/*, 218*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 219*/);
          ДобавитьПараметры(m_InfoRate, null, null, 233);
          ДобавитьПараметры(m_InfoRate, null, null, 234);
       elif( OTHER_CONTRACTS == SET_CHAR )
          ДобавитьПараметры(m_InfoRate, 1110, null/*, 218*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 219*/);
       elif( IIS_CONTRACTS == SET_CHAR )
          ДобавитьПараметры(m_InfoRate, 1110, "1110_ИИС", 233);
          ДобавитьПараметры(m_InfoRate, null, null, 234);
       end;

    elif( m_InfoRate == INFORATE_35 )

       if( this.ResidenceStatus() == 1 )
          if( OTHER_CONTRACTS == SET_CHAR )
             ДобавитьПараметры(m_InfoRate, IIF(m_CurrSign == 2, null, 3021), null, null);
          end;
       end;

    else
      
       if( OTHER_CONTRACTS == SET_CHAR )
         ДобавитьПараметры(m_InfoRate, IIF(m_CurrSign == 2, null, 1010), null/*, 601*/);
       end;

       if( (OTHER_CONTRACTS == SET_CHAR) and (IIS_CONTRACTS == SET_CHAR) )
          ДобавитьПараметры(m_InfoRate, 1011, "1011+1011_ИИС", null);
       elif( OTHER_CONTRACTS == SET_CHAR )
          ДобавитьПараметры(m_InfoRate, 1011, null, null);
       elif( IIS_CONTRACTS == SET_CHAR )
          ДобавитьПараметры(m_InfoRate, 1011, "1011_ИИС", null);
       end;

       if( OTHER_CONTRACTS == SET_CHAR )
          ДобавитьПараметры(m_InfoRate, 1110, null, null);

          ДобавитьПараметры(m_InfoRate, 1530, null/*, 201, "201+214"*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 208*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 212*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 218, "218+218_1"*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 222*/);

          ДобавитьПараметры(m_InfoRate, 1531, null/*, 202*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 219, "219+219_1"*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 223*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 620, "620_3"*/);

          ДобавитьПараметры(m_InfoRate, 1532, null/*, 205*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 206*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 209*/);

          ДобавитьПараметры(m_InfoRate, 1533, null/*, 620, "620_2"*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 220*/);

          ДобавитьПараметры(m_InfoRate, 1535, null/*, 207*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 210*/);

          ДобавитьПараметры(m_InfoRate, 1536, null/*, 203*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 620, "620_1"*/);
          ДобавитьПараметры(m_InfoRate, null, null/*, 224*/);

          ДобавитьПараметры(m_InfoRate, 1537, null/*, 211*/);

          ДобавитьПараметры(m_InfoRate, 1539, null/*, 213*/);
       end;

       if( IIS_CONTRACTS == SET_CHAR )
          ДобавитьПараметры(m_InfoRate, 1110, null, null);

          ДобавитьПараметры(m_InfoRate, 1544, null, 225, "225+214_ИИС");
          ДобавитьПараметры(m_InfoRate, null, null/*, 251*/);
          ДобавитьПараметры(m_InfoRate, null, null, 233);
          ДобавитьПараметры(m_InfoRate, null, null/*, 239*/);

          ДобавитьПараметры(m_InfoRate, 1545, null, 226);
          ДобавитьПараметры(m_InfoRate, null, null, 234);
          ДобавитьПараметры(m_InfoRate, null, null/*, 240*/);

          ДобавитьПараметры(m_InfoRate, 1546, null/*, 250*/);
          ДобавитьПараметры(m_InfoRate, null, null, 228);
          ДобавитьПараметры(m_InfoRate, null, null/*, 252*/);

          ДобавитьПараметры(m_InfoRate, 1547, null, 235);

          ДобавитьПараметры(m_InfoRate, 1548, null, 229);
          ДобавитьПараметры(m_InfoRate, null, null/*, 241*/);

          ДобавитьПараметры(m_InfoRate, 1549, null, 227);
          ДобавитьПараметры(m_InfoRate, null, null/*, 236*/);

          ДобавитьПараметры(m_InfoRate, 1551, null, 230);

          ДобавитьПараметры(m_InfoRate, 1553, null, 231);
       end;

       if( (OTHER_CONTRACTS == SET_CHAR) and (IIS_CONTRACTS == SET_CHAR) )
          ДобавитьПараметры(m_InfoRate, 2640, "2640+2640_ИИС", null);
          ДобавитьПараметры(m_InfoRate, 2641, "2641+2641_ИИС", null);
       elif( OTHER_CONTRACTS == SET_CHAR )
          ДобавитьПараметры(m_InfoRate, 2640, null, null);
          ДобавитьПараметры(m_InfoRate, 2641, null, null);
       elif( IIS_CONTRACTS == SET_CHAR )
          ДобавитьПараметры(m_InfoRate, 2640, "2640_ИИС", null);
          ДобавитьПараметры(m_InfoRate, 2641, "2641_ИИС", null);
       end;

       if( (OTHER_CONTRACTS == SET_CHAR) and (this.ResidenceStatus() != 1) )
          ДобавитьПараметры(m_InfoRate, IIF(m_CurrSign == 2, null, 3021), null, null);
       end;

    end;

    if(OldSize < m_ArrCodeParms.Size)
       AddRateIndex(RateIndexes, OldSize);
    end;
  END;

  PRIVATE MACRO РассчитатьДоходыИВычеты(pEndDate:date, RateIndexes)
    var OldCurrYear = m_CurrYear;

    m_PlusSum  = $0; 
    m_MinusSum = $0;

    if(RateIndexes == NULL)
       RateIndexes = TArray();
    end;

    DateSplit(pEndDate, null, null, m_CurrYear);

    m_BeginDate = date(1,1,m_CurrYear);
    m_EndDate = pEndDate;

    ДобавитьВсеПараметры(RateIndexes);

    РассчитатьПараметрыДляПечати(RateIndexes);

    m_CurrYear = OldCurrYear;

    return RateIndexes;
  END; 

  MACRO CalcData(pEndDate:date, InfoRate, Sign, RateIndexes)
    m_CurrSign = Sign;
    m_InfoRate = InfoRate;
    ClearData();
    RateIndexes= РассчитатьДоходыИВычеты(pEndDate, RateIndexes);
    return RateIndexes;
  END;

  InitNPTXRepCalcCommon(_RepDate, _Investor, null, null, null, false, true);
END;

class (DL_CReportTemplate) NpTxPhysIISInfoReport()

    private var m_SfContr = TRecHandler("sfcontr.dbt");
    private var m_RepDate = ZeroDate;
    private var m_Investor = -1;
    private var m_Contract = -1;
    private var m_DocNumber = "", m_GetAccOpenCloseDate = false, m_AccountOpenDate = Date(0,0,0), m_AccountCloseDate = Date(0,0,0), m_SumOst;
    private var m_RepCalc, m_TaxeYearClose = null;

    macro PrintMode():Integer
       return DL_OUTREPORT_EXCEL;
    end;

    private macro BeginReport(NumCopy:Integer)

       Dl_CallInsertStat(PrintMode());

       IIS_CONTRACTS   = SET_CHAR;
       OTHER_CONTRACTS = UNSET_CHAR;

       m_RepDate = m_Form.GetFieldValue(PNFLD_NPTXPIIS_REPORTDATE);
       m_Investor = m_Form.GetFieldValue(PNFLD_NPTXPIIS_INVESTORCODE);
       m_Contract = m_Form.GetFieldValue(PNFLD_NPTXPIIS_CONTRACT);
       m_DocNumber = m_Form.GetFieldValue(PNFLD_NPTXPIIS_DOCUMENTNUMBER);

       if( SfGetContr(m_Contract, m_SfContr) )

       end;

       CreateTotalBook();
       SetActiveSheet( "ФЛ и его ИИС" );

    end;

    PRIVATE MACRO GetAccOpenCloseDate()                                                                                                         
       var Query = "";                                                                                                                          
       var DataSet = null;                                                                                                                      

       //if( not m_GetAccOpenCloseDate )                                                                                                                                         
       //поскольку форма не закрывается после выпуска, может измениться инвестор. Следовательно, выборку делать каждый раз.
          Query = " select acc.t_open_date, acc.t_close_date, "
                 +"        case when acc.t_close_date is not NULL and acc.t_close_date != to_date('01.01.0001','DD.MM.YYYY') "
                 +"             then RSB_ACCOUNT.RESTAC(acc.T_ACCOUNT,acc.T_CODE_CURRENCY,acc.t_close_date-1,acc.T_Chapter, null) "
                 +"             else RSB_ACCOUNT.RESTAC(acc.T_ACCOUNT,acc.T_CODE_CURRENCY,"+GetSQLDate(m_RepDate)+",acc.T_Chapter, null) end SumOst "                                                                 
                 +"   from dsfssi_dbt sfssi, dsettacc_dbt settacc, daccount_dbt acc "                                                           
                 +"  where SFSSI.T_OBJECTTYPE = "+OBJTYPE_SFCONTR                                                                               
                 +"    and SFSSI.T_OBJECTID = lpad("+m_SfContr.rec.ID+",10,'0')     "                                                           
                 +"    and SETTACC.T_SETTACCID = SFSSI.T_SETACCID  "                                                                            
                 +"    and ACC.T_ACCOUNT = SETTACC.T_ACCOUNT       "                                                                            
                 +"    and ACC.T_CHAPTER = SETTACC.T_CHAPTER       "                                                                            
                 +"    and ACC.T_CODE_CURRENCY = SETTACC.T_FIID    "                                                                            
                 +" order by acc.t_open_date desc";
                                                                                                                                                
          DataSet = TRsbDataSet(Query);                                                                                                         
          if( DataSet.MoveNext() )                                                                                                              
             m_AccountOpenDate = SQL_ConvTypeDATE(DataSet.open_date);                                                                                     
             m_AccountCloseDate = SQL_ConvTypeDATE(DataSet.close_date);
             m_SumOst = SQL_ConvTypeSum(DataSet.SumOst);
             m_GetAccOpenCloseDate = true;
          else 
             m_AccountOpenDate = Date(0,0,0);                                                                                     
             m_AccountCloseDate = Date(0,0,0);
             m_GetAccOpenCloseDate = false;
          end;                                                                                                                                  
          
       //end;

       return m_GetAccOpenCloseDate;                                                                                                                               
    END;                                                                                                                                        

    macro AccountOpenDate()
       GetAccOpenCloseDate();
       return m_AccountOpenDate;
    end;

    macro AccountCloseDate()
       GetAccOpenCloseDate();
       return m_AccountCloseDate;
    end;

    macro SumOst()
       GetAccOpenCloseDate();
       return m_SumOst;
    end;

    macro TaxeYearClose()
       DateSplit(m_SfContr.rec.DateClose, null, null, m_TaxeYearClose);
       return m_TaxeYearClose;
    end;

    macro ContractFirstNumber()
       var vContractFirstNumber = readNoteForObject(OBJTYPE_SFCONTR, UniID(m_SfContr, OBJTYPE_SFCONTR), 2, date(31,12,9999)); // не историчное примечание
         if((ValType(vContractFirstNumber) != V_UNDEF) and (vContractFirstNumber != ""))
           vContractFirstNumber = string(vContractFirstNumber);
         end;
       return vContractFirstNumber;
    end;

    macro ContractFirstOpenDate()
       var vContractFirstOpenDate = readNoteForObject(OBJTYPE_SFCONTR, UniID(m_SfContr, OBJTYPE_SFCONTR), 3, date(31,12,9999)); // не историчное примечание
         if((ValType(vContractFirstOpenDate) != V_UNDEF) and (vContractFirstOpenDate != ""))
           vContractFirstOpenDate = Date(vContractFirstOpenDate);
         end;
       return vContractFirstOpenDate;
    end;

    macro AccountFirstOpenDate()
       var vAccountFirstOpenDate = readNoteForObject(OBJTYPE_SFCONTR, UniID(m_SfContr, OBJTYPE_SFCONTR), 4, date(31,12,9999)); // не историчное примечание
         if((ValType(vAccountFirstOpenDate) != V_UNDEF) and (vAccountFirstOpenDate != ""))
           vAccountFirstOpenDate = Date(vAccountFirstOpenDate);
         end;
       return vAccountFirstOpenDate;
    end;

    macro SumFromBegYear()
       var v_SumFromBegYear = $0;

       var Query = " SELECT SUM (DECODE(nptxop.t_IIS, CHR(88), nptxop.t_TaxSum2, nptxop.t_OutSum)) Sum "
                 + "   FROM dnptxop_dbt nptxop "
                 + "  WHERE nptxop.t_DocKind = ? "
                 + "    AND nptxop.t_SubKind_Operation = 10 "
                 + "    AND nptxop.t_Contract = ? "
                 + "    AND extract(year from (nptxop.t_OperDate)) = ? "
                 + "    AND (select rq.t_State from ddlrq_dbt rq "
                 + "          where rq.t_DocKind = nptxop.t_DocKind "
                 + "            and rq.t_DocID = nptxop.t_ID "
                 + "            and rq.t_DealPart = 1 "
                 + "            and rq.t_SubKind = ? "
                 + "            and rq.t_Type = ? "
                 + "            and rq.t_Num = 0 "
                 + "        ) = ?";/*считаем, что шаг <Формирование проводок> выполнен, если ТО по операции исполнено*/

       var cmd = DL_RSDCommand(query);

       cmd.AddParam(DL_WRTMONEY);
       cmd.AddParam(m_SfContr.rec.ID);
       cmd.AddParam(TaxeYearClose());
       cmd.AddParam(DL_GetRQSubKindbyType(DLRQ_TYPE_PAYMENT));
       cmd.AddParam(DLRQ_TYPE_PAYMENT);
       cmd.AddParam(DLRQ_STATE_EXEC);         
       
       var DataSet = cmd.Execute();
       
       if(DataSet.moveNext())
          v_SumFromBegYear = SQL_ConvTypeSum(DataSet.Sum);
       end;

       return v_SumFromBegYear;
    end;

    macro GetBegDate()

       var v_BegDate = date(0,0,0);

       var Query = " select min(NOBJ.T_DATE) MinDate "                    
                 + "   from dnptxobj_dbt nobj        "                    
                 + "  where NOBJ.T_CLIENT > 0        "                    
                 + "    and NOBJ.T_ANALITICKIND6 = ? "
                 + "    and RSI_NPTO.CheckContrIIS(NOBJ.T_ANALITIC6) = 1";
 
       var cmd = DL_RSDCommand(query);
       
       cmd.AddParam(TXOBJ_KIND6020);
       
       var DataSet = cmd.Execute();
       
       if(DataSet.moveNext())
          v_BegDate = SQL_ConvTypeDate(DataSet.MinDate);
       end;
      
       return v_BegDate;
    end;

    macro getPositionOper(_oper:Integer)
       var local_PositionOper;
       var Query = "  SELECT typeac.t_Name_Type                      "+
                   "  FROM dtypeac_dbt typeac, dperson_dbt t         "+ 
                   "  WHERE                                          "+
                   "        t.T_CTYPEPERSON = TYPEAC.T_TYPE_ACCOUNT  "+
                   "    AND typeac.t_iNumType = 13                   "+
                   "    AND t.t_Oper = "+_oper;
       var DataSet = TRsbDataSet(query);
       if( DataSet.MoveNext() ) 
         local_PositionOper = (DataSet.Name_Type);
       end;
       return local_PositionOper;
    end;
    private macro PrintPlusMinusSum(Year,InfoRate,IsPrintYear:bool)

       var i = 0;
       var arr = null;
      
       arr = m_RepCalc.ПолучитьМассивПараметровДляПечати();

       if( not IsPrintYear )

          PrintFormatString( NULL,
                             "N1_TaxeYear", Year
                           );
          CopyAllSheetInTotalBook(null, false, "N1_TaxeYearHeader", 0, null);
          IsPrintYear = true;
       end;

       if( (IsPrintYear and (InfoRate==INFORATE_TOTAL)) or (InfoRate==INFORATE9_15) )

          PrintFormatString( NULL,
                             "N1_Rate", m_RepCalc.Rate(InfoRate)*0.01
                           );
          CopyAllSheetInTotalBook(null, false, "N1_RateHeader", 0, null);
       end;

       CopyAllSheetInTotalBook(null, false, "N1_TableHeader", 0, null);

       while( i < arr.size() )
         var j = 0;
         while(j < arr[i].NPTXCodeParmArr.Size)
      
           // Строка с кодом дохода выводится в отчет, если сумма дохода не нулевая или если сумма дохода нулевая и хотя бы одна из сумм вычета, входящих в одну группу с этой строкой дохода ненулевая. 
           // Если сумма вычета нулевая, то в отчет не выводится информация с кодом вычета.    
           // Если код вычета не стоит в одной строке с кодом дохода и сумма вычета нулевая, то строка с таким кодом вычета в отчет не выводится.
        
           if( ((arr[i].NPTXCodeParmArr[j].PSnnn == null) or (arr[i].NPTXCodeParmArr[j].PSnnn == $0)) and (arr[i].NPTXCodeParmArr[j].MinusCodes.Size == 0) )
              //не печатать
           else
              if((arr[i].NPTXCodeParmArr[j].MinusCodes.Size > 0) and (arr[i].NPTXCodeParmArr[j].MinusCodes[0].MSCode != NULL))
                 PrintFormatString( null,
                                    "N1_Plus",     arr[i].NPTXCodeParmArr[j].PSCode, 
                                    "N1_SumPlus",  arr[i].NPTXCodeParmArr[j].PSnnn,  
                                    "N1_Minus",    arr[i].NPTXCodeParmArr[j].MinusCodes[0].MSCode, 
                                    "N1_SumMinus", arr[i].NPTXCodeParmArr[j].MinusCodes[0].MSnnn
                                  );
              else
                 PrintFormatString( null,
                                    "N1_Plus",     arr[i].NPTXCodeParmArr[j].PSCode, 
                                    "N1_SumPlus",  arr[i].NPTXCodeParmArr[j].PSnnn,  
                                    "N1_Minus",      "", 
                                    "N1_SumMinus", ""
                                  );
              end;
              CopyAllSheetInTotalBook(null, false, "N1_Table_3_10", 0, null);

              var k = 1;
              while(k < arr[i].NPTXCodeParmArr[j].MinusCodes.Size)
                 PrintFormatString( null,
                                    "N1_Plus",     "", 
                                    "N1_SumPlus",  0.0,  
                                    "N1_Minus",    arr[i].NPTXCodeParmArr[j].MinusCodes[k].MSCode, 
                                    "N1_SumMinus", arr[i].NPTXCodeParmArr[j].MinusCodes[k].MSnnn  
                               );
                 CopyAllSheetInTotalBook(null, false, "N1_Table_3_10", 0, null);
                 k = k + 1;
              end;
           end;

           j = j + 1;
         end;
      
         i = i + 1;
       end;

    end;

    private macro PrintByNPTXTS()
       var OnDate = iif(m_SfContr.rec.DateClose==date(0,0,0),m_RepDate,m_SfContr.rec.DateClose);
   
       var cmd = DL_RSDCommand(" SELECT ts.t_Amount Qnty, BEGLOT.T_BUYDATE DateB, "
                              +"        BEGLOT.T_PRICE PrVp,                     "
                              +"        round(CURRLOT.T_NKD * ts.t_Amount / BEGLOT.T_AMOUNT,2) NKDVN,                     "
                              +"        (select t_shortname from dparty_dbt where t_partyid = FI.T_ISSUER) Issuer, "
                              +"        (select AVRKINDS.T_NAME from davrkinds_dbt avrkinds where AVRKINDS.T_FI_KIND = FI.T_FI_KIND and  AVRKINDS.T_AVOIRKIND = FI.T_AVOIRKIND) TypeSec, "
                              +"        case when av.t_isin = chr(1) then av.t_lsin else av.t_isin end ISIN,                                                                             "
                              +"        AV.T_INDEXNOM, (select decode(VP.T_ISO_NUMBER,810,643,VP.T_ISO_NUMBER) from dfininstr_dbt vp where vp.t_fiid = BEGLOT.T_PRICEFIID) VPrB,         "
                              +"        round((case when BEGLOT.T_PRICEFIID != 0 then Rsb_FIInstr.ConvSum(BEGLOT.T_TOTALCOST,BEGLOT.T_PRICEFIID,"+NATCUR+",BEGLOT.T_BUYDATE) else BEGLOT.T_TOTALCOST end) * ts.t_Amount / BEGLOT.T_AMOUNT,2) SumBRub, "
                              +"        rsb_fiinstr.FI_AvrKindsGetRoot( FI.T_FI_KIND, FI.T_AVOIRKIND ) AvrKindsRoot, "
                              +"        round((select NVL( Sum( RSB_FIInstr.ConvSum(Comis.t_Sum, M.T_FIID_COMM, "+NATCUR+", Comis.t_FactPayDate)),0)  "
                              +"                 from V_DlComisAll  Comis, DSFCOMISS_DBT M                                                   "
                              +"                where Comis.t_DocID = beglot.T_DOCID                                                         "
                              +"                  and Comis.t_DocKind = beglot.T_DOCKIND                                                     "
                              +"                  and (COMIS.T_FACTPAYDATE <= ? and COMIS.T_FACTPAYDATE != to_date('01.01.0001','DD.MM.YYYY')) " 
                              +"                  and Comis.t_IsBankExpenses <> 'X' "
                              +"                  and M.T_FEETYPE = Comis.T_FEETYPE                                                          "
                              +"                  and M.T_NUMBER  = Comis.T_COMNUMBER) * ts.t_Amount / BEGLOT.T_AMOUNT,2) Com                "
                              +"   FROM dnptxts_dbt ts, dnptxlot_dbt currlot, dnptxlot_dbt beglot, dfininstr_dbt fi, davoiriss_dbt av        "
                              +"  WHERE ts.T_CLIENT = ?                                         "
                              +"    and ts.T_CONTRACT = ? "
                              +"    and ts.T_TYPE = ?                                           "
                              +"    AND ts.t_BegDate < ?       "
                              +"    AND ( ts.t_EndDate IS NULL  OR                              "
                              +"          ts.t_EndDate = to_date('01.01.0001','DD.MM.YYYY') OR  "
                              +"          ts.t_EndDate >= ?    "
                              +"        )                                                       "
                              +"    and CURRLOT.T_ID = ts.t_BuyID                               "
                              +"    and BEGLOT.T_ID = (case when NPTX.IsVirtual(CURRLOT.t_Type) = 1 then CURRLOT.t_RealID else CURRLOT.T_BEGLOTID end)"
                              +"    and begLOT.T_KIND = ? "
                              +"    and fi.t_FIID = ts.t_FIID "
                              +"    and AV.T_FIID = ts.t_FIID "
                              +" order by BEGLOT.T_BUYDATE "
                            ); 
                               
       cmd.addParam( OnDate );
       cmd.addParam( m_Investor );
       cmd.addParam( m_Contract );
       cmd.addParam( NPTXTS_REST);
       cmd.addParam( OnDate );
       cmd.addParam( OnDate );
       cmd.addParam( NPTXLOTS_BUY );
   
       var DataSet = cmd.execute();

       CopyAllSheetInTotalBook(null, false, "N1_3_11_Header", 0, null);

       while( DataSet.MoveNext() )

          PrintFormatString( null,
                         "N1_DateB",         SQL_ConvTypeDATE(DataSet.DateB),   
                         "N1_IssuerTypeSec", SQL_ConvTypeStr(DataSet.Issuer) + ", " + SQL_ConvTypeStr(DataSet.TypeSec)+iif(DataSet.INDEXNOM," ИН",""), 
                         "N1_ISIN",          SQL_ConvTypeStr(DataSet.ISIN),     
                         "N1_Qnty",          SQL_ConvTypeSum(DataSet.Qnty),     
                         "N1_VPrB",          int(DataSet.VPrB),                 
                         "N1_PrVp",          SQL_ConvTypeSum(DataSet.PrVp),     
                         "N1_NKDVN",         iif(DataSet.AvrKindsRoot==AVOIRISSKIND_BOND,SQL_ConvTypeSum(DataSet.NKDVN),""), 
                         "N1_Com",           SQL_ConvTypeSum(DataSet.Com),      
                         "N1_SumBRub",       SQL_ConvTypeSum(DataSet.SumBRub)
                        );

          CopyAllSheetInTotalBook(null, false, "N1_Table_3_11", 0, null);

       end;
   
    END;
    
    private macro Create()
       var Query = "", count = 0;
       var DataSet = null;

       m_RepCalc = NPTXPHYSIISINFO(m_RepDate,m_Investor);
       count = m_RepCalc.Count();  
    
       if( (count > 0) and m_RepCalc.Next() )

          PrintFormatString(null, "N1_OurNumber", IIF(m_DocNumber != "", m_DocNumber, "Б/н"),
                                  "N1_DateOfSignature", m_RepDate);
        
          PrintFormatString(null, "N1_FIO", m_RepCalc.FIO(),
                                  "N1_DateBirth", string(m_RepCalc.DateBirth():f),
                                  "N1_PlaceOfBirth", m_RepCalc.BirthPlace(),
                                  "N1_ClientINN", m_RepCalc.ClientINN(),
                                  "N1_ResidenceAdress", m_RepCalc.ResidenceAdress(),
                                  "N1_Document", m_RepCalc.Document());
        
          PrintFormatString(null, "N1_OurName", m_RepCalc.OurName(),
                                  "N1_OurInnKPP", m_RepCalc.OurInn() + "/" + m_RepCalc.OurKPP(),
                                  "N1_OurAdress", m_RepCalc.OurAdress(),
                                  "N1_OurPhone", m_RepCalc.OurPhone());

          PrintFormatString(null, "N1_ContractNumberOpenDate", string(m_SfContr.rec.Number) + " от " + string(m_SfContr.rec.DateConc:f),
                                  "N1_ContractCloseDate", string(m_SfContr.rec.DateClose:f),
                                  "N1_AccountOpenDate", string(AccountOpenDate():f),//
                                  "N1_AccountCloseDate", string(AccountCloseDate():f),//
                                  "N1_ContractFirstNumberOpenDate", 
                                   IIF (string(this.ContractFirstNumber())      != "" , 
                                   IIF (string(this.ContractFirstOpenDate():f)  != "" ,
                                       (string(this.ContractFirstNumber()) +" от " + string(this.ContractFirstOpenDate():f)      ),
                                       ""),
                                   ""),
                                  "N1_AccountFirstOpenDate", string(this.AccountFirstOpenDate():f),//
                                  "N1_TaxeYearClose", iif(TaxeYearClose()>0,TaxeYearClose(),""),
                                  "N1_Sum", SumFromBegYear()
                           );

          CopyAllSheetInTotalBook(null, false, "N1_Header", 0, null);

          var v_CurrYear, v_EndYear, v_EndDate, v_IsPrintYear = false;
          var RateIndexes;
          
          DateSplit(GetBegDate(), null, null, v_CurrYear);
          DateSplit(m_RepDate, null, null, v_EndYear);

          if( v_CurrYear > 0 )
             //собрать значения доходов и вычетов
             while(v_CurrYear <= v_EndYear)
                if( v_EndYear == v_CurrYear )
                   v_EndDate = m_RepDate;
                else
                   v_EndDate = date(31,12,v_CurrYear); //последний день года
                end;
               
                //справка для ставки 9 (15)%
                RateIndexes = m_RepCalc.CalcData(v_EndDate, INFORATE9_15, 1);
                if((RateIndexes.Size > 0) and ((m_RepCalc.PlusSum(RateIndexes[0]) != $0) or (m_RepCalc.MinusSum(RateIndexes[0]) != $0)))
                  PrintPlusMinusSum(v_CurrYear,INFORATE9_15,v_IsPrintYear);
                  v_IsPrintYear = true;
                end;
             
                //справка по общей ставке
                RateIndexes = m_RepCalc.CalcData(v_EndDate, INFORATE_TOTAL, 1);
                if((RateIndexes.Size > 0) and ((m_RepCalc.PlusSum(RateIndexes[0]) != $0) or (m_RepCalc.MinusSum(RateIndexes[0]) != $0)))
                  PrintPlusMinusSum(v_CurrYear,INFORATE_TOTAL,v_IsPrintYear);
                end;
               
                v_CurrYear = v_CurrYear + 1; //переходим в следующий год
            
                v_IsPrintYear = false;
             end;
          end;

          PrintByNPTXTS();

          PrintFormatString(null, "N1_SumOst", SumOst(),
                                  "N1_PositionOper", getPositionOper ( {oper} ),
                                  "N1_Oper", {Name_Oper});

          CopyAllSheetInTotalBook(null, false, "N1_OtherHeader", 0, null);

       else
          PrintFormatString( NULL, "N1_MsgNotData", "Нет данных, удовлетворяющих параметрам отчета", null);
          CopyAllSheetInTotalBook( NULL, false, "N1_MsgNotData", 0 );
       end;

    end;

    private macro EndReport()
       SaveTotalBook();
    end;

    initDL_CReportTemplate(NpTxPhysIISInfoPanel(), "Report_NpTxPhysIISInfo_old.xls");
end;

NpTxPhysIISInfoReport().Run();
Exit(1);
