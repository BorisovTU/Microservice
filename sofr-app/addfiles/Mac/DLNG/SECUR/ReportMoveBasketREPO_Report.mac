/*
$Name:             ReportMoveBasketREPO_Report.mac
$Module:           АРНУ
$Description:      Отчет "Движение ценных бумаг по сделкам РЕПО с корзиной"
*/

import "ReportMoveBasketREPO_Form.mac", "RegistrReport.mac", "nptxfun.mac", "spRepFun.mac", "ws_txreportform.mac";

PRIVATE VAR   Basket_Form;
PRIVATE VAR   Basket_Report;
PRIVATE VAR   WebMenuItem; // Информация о запуске отчета из веб

PRIVATE CLASS (TX_RegistrReport) ReportMoveBasketREPO(Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER)

  CONST TAX_GROUP_REPO_BASKET = 99;

  PRIVATE VAR m_IsExistsData = false,
              m_ReportDate = ZeroDate,
              m_BeginReportDate = ZeroDate,
              m_AvrFIID = null,
              m_AvrName = "",
              m_RQ,
              m_CountRQInDeal = 0;

/* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsExistsData or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END; 

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport()
    m_ReportDate  = Basket_Form.GetFieldValue(PNFLD_REPO_BASKET_MOVE_ENDDATE); 
    if( not m_IsWebService )
        m_AvrFIID = TArray();
        m_AvrFIID[m_AvrFIID.Size] = Basket_Form.GetFieldValue(PNFLD_REPO_BASKET_MOVE_CODE);
     else
        m_AvrFIID = Basket_Form.GetFieldValue(PNFLD_REPO_BASKET_MOVE_CODE);
     end;
    m_AvrName     = Basket_Form.GetFieldValue(PNFLD_REPO_BASKET_MOVE_NAME);
    m_BeginReportDate = this.InitBeginReportDate();
    m_IsExistsData = false;
  END;  

  MACRO InitBeginReportDate()
    var Day, Month, Year;
    datesplit( m_ReportDate, Day, Month, Year );
    return date(1, 1, Year);
  END; 

/*Создание отчета*/
  MACRO Create()
    this.RegisterReportTableWithHead();
    this.PrintReportMoveBasket();
    EndTable();

    if( (m_IsExistsData == false) and (m_IsWebService == false) )
       msgbox("Нет данных для формирования отчета.");
    end;  
  END;

  MACRO RegisterReportTableWithHead()

    SetActiveSheet("Движение цб в РЕПО с корзиной");
    PrintFormatString( "",               
                       "Today", string(Date() + " " + string( Time() ) ),
                       "Num_Oper", string({oper}),
                       "Oper", this.Операционист(),    
                       "H0_1", this.H0_1(),  
                       "H0_2", this.H0_2(), 
                       "H0_5", this.GetAvrName() );
    
    RegisterTable(  "MainTable", "",
                    "SecType",
                    "N",
                    "SecName",
                    "CodeR",
                    "TypeRepoShort",
                    "DZ",
                    "N_DD1",
                    "N_DD2",
                    "QntyOnDate", 
                    "Tot1vp",
                    "Tot2vp",
                    "Rate", 
                    "VP", 
                    "Cont",
                    "CodeR_1",
                    "ISIN"
                  );   
  END;

  MACRO Операционист()
    var vRS = TRsbDataSet("SELECT person.t_Name " +
                         "  FROM dperson_dbt person " +
                         " WHERE person.t_oper = " + string({oper})
                        );

    if( vRS.MoveNext() )
      return vRS.Name;
    end;
    return "";
  END;
  /*Начальная дата*/
  MACRO H0_1():DATE
     return m_BeginReportDate;
  END;
  /*Конечная дата*/
  MACRO H0_2():DATE
     return m_ReportDate;
  END;
  /*Выпуск ц/б*/
  MACRO GetAvrName():STRING
     return m_AvrName;
  END;
/*
  PRIVATE MACRO ТочностьКолва(pAmount)
     return DL_MeanSignAfterPoint(pAmount,12);
  END;
*/
  
  MACRO PrintReportMoveBasket()

    PRIVATE MACRO Get_str_PaymDate(Type, DealPart)
         var sql_PlanDate:String = 
            " (NVL( (SELECT min(dlrq.t_PlanDate) " +
            "  FROM ddlrq_dbt dlrq " + 
            "  WHERE dlrq.t_DocKind    = tick.t_BOfficeKind " +
            "     AND dlrq.t_DocID = tick.t_DealID " +
            "     AND dlrq.t_Type    = " + string(Type) + 
            "     AND dlrq.t_DealPart    = " + string(DealPart) + 
            "     AND dlrq.t_State = " + string(DLRQ_STATE_PLAN) +  
            "     ), TO_DATE('01-01-0001','DD-MM-YYYY') )" +
            " ) ";
    
         var sql_FactDate:String = 
            " (NVL( (SELECT min(dlrq.t_FactDate) " +
            "  FROM ddlrq_dbt dlrq " + 
            "  WHERE dlrq.t_DocKind    = tick.t_BOfficeKind " +
            "     AND dlrq.t_DocID = tick.t_DealID " +
            "     AND dlrq.t_Type    = " + string(Type) + 
            "     AND dlrq.t_DealPart    = " + string(DealPart) + 
            "     AND dlrq.t_State = " + string(DLRQ_STATE_EXEC) +
            "     AND dlrq.t_FactDate > TO_DATE('01-01-0001','DD-MM-YYYY') " +
            "     ), TO_DATE('01-01-0001','DD-MM-YYYY') ) " +
            " ) ";
    
         return 
            " (DECODE( " +
                 sql_FactDate + ", " +                   // Выражение
            "    TO_DATE('01-01-0001','DD-MM-YYYY'), " + // search1
                 sql_PlanDate + ", " +                   // result1
                 sql_FactDate + ") " +                   // default
            " ) ";
    END;

    Private macro CondDatePart1()
      return 
      " ( "+
      "   ( " + Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  1) + " >= " + GetSQLDate( m_BeginReportDate) + " ) " +   
      "     AND (" + Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  1) + " <= " + GetSQLDate( m_ReportDate) +  " ) "
      "  ) ";
    end;

    Private macro CondDatePart2()
      return
      " ( "+
      "   ( " + Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  2) + " >= " + GetSQLDate( m_BeginReportDate) + " ) " +   
      "     AND (" + Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  2) + " <= " + GetSQLDate( m_ReportDate) +  " ) "
      "  ) ";
    end;

    Private macro CondDate3()
      return
      " ( "+
      "   ( " + Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  2) + " < " + GetSQLDate( m_BeginReportDate) + " ) " +   
      "     AND (" + Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  2) + " > " + GetSQLDate( m_ReportDate) +  " ) "
      "  ) ";
    end;

 

    VAR DataQuery, Num = 0, WhereCondAvoir = "", AvrFIIDCount = 0, AvrFIIDAddWhere = "";
    VAR ProgressValue = CTxProgressValue(); 

    if( (m_AvrFIID != null) AND (m_AvrFIID.Size > 0 ) ) /*задана бумага*/

      while( AvrFIIDCount < m_AvrFIID.Size )
         if( ( ValType( m_AvrFIID[AvrFIIDCount] ) == V_INTEGER )
         and ( m_AvrFIID[AvrFIIDCount] > 0 ) )
            if( AvrFIIDAddWhere == "" )
               AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
            else
               AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
            end;
            AvrFIIDAddWhere = AvrFIIDAddWhere + " Fin.t_FIID = " + String( m_AvrFIID[AvrFIIDCount] ) + " ";
         end;
         AvrFIIDCount = AvrFIIDCount + 1;
      end;
      if( AvrFIIDAddWhere != "" )
         AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
      end;
      WhereCondAvoir = WhereCondAvoir + AvrFIIDAddWhere;
    end;

    DataQuery = "SELECT tick.*, " +
                " fin.t_FIID, "
                " fin.t_FI_CODE, "
                " fin.t_Name, "
                " (case when avoir.t_ISIN IS null OR avoir.t_ISIN IN (chr(0), chr(1)) then (case when avoir.t_LSIN IS null OR avoir.t_LSIN IN (chr(0), chr(1)) then chr(0) else avoir.t_LSIN end) else avoir.t_ISIN end) AS t_ISIN, " +
                " LEG_S.T_TotalCOST TotalCostSale, "+
                " LEG_B.T_TotalCOST TotalCostBuy, "+
                " (CASE " +
                "    WHEN LEG_B.T_INCOMERATE = 0 THEN LEG_S.T_INCOMERATE " +
                "    ELSE LEG_B.T_INCOMERATE "+
                " END) "+
                "  Rate, "+
                Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  1) + " DD1, " +
                Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  2) + " DD2, " +
                " rsb_secur.IsSale(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) IsSale " +
                "   FROM ddl_tick_dbt tick, ddl_leg_dbt leg_b, ddl_leg_dbt leg_s, "+
                "        dfininstr_dbt fin, davoiriss_dbt avoir " +      
                "  WHERE RSB_SECUR.IsBasket( "  +
  		          "          rsb_secur.get_OperationGroup( "  +
  		          "            rsb_secur.get_OperSysTypes( "  +
  			        "             Tick.t_DealType, Tick.t_BofficeKind))) = 1 " +
                "       AND LEG_S.T_LEGKIND =  " + LEG_KIND_DL_TICK +
                "       AND LEG_B.T_LEGKIND =  " + LEG_KIND_DL_TICK_BACK +
                "       AND leg_b.t_dealId = tick.t_dealId " +
                "       AND leg_s.t_dealId = tick.t_dealId " +
                "       AND fin.t_fiid = tick.t_PFI  " +
                "       AND avoir.t_fiid = fin.t_fiid " +
                /*Есть движение по бумагам - т.е. есть ТО по поставке*/
                "       AND EXISTS  "+
                "                 (SELECT RQ.T_ID "+
                "                   FROM ddlrq_dbt rq "+
                "                  WHERE     rq.t_DocKind = tick.t_bofficeKind "+
                "                        AND rq.t_DocID = TICK.T_DEALID "+
                "                        AND rq.t_Type IN ( " + DLRQ_TYPE_DELIVERY + " , " + DLRQ_TYPE_COMPDELIVERY  + ")  "+
                "                        AND rq.t_FactDate != TO_DATE ('01-01-0001', 'DD-MM-YYYY')) "+
                "       AND ( " + 
                "             ( " + CondDatePart1() + " OR " +  CondDatePart2() + " ) "+
                "             OR " + CondDate3() +
                "           ) " +    WhereCondAvoir +
                " ORDER BY tick.t_DealDate, tick.t_DealTime " ;

     ProgressValue.Maximum = SQL_GetNRecs( DataQuery );
     ProgressValue.Current = 0;

     var ProgressIndicator = CreateProgressIndicator(m_IsWebService);

     if( m_IsWebService and (WebMenuItem != null) )
       var header = "Формирование отчета " + WebMenuItem.szNameItem;
       ProgressIndicator.Start(ProgressValue.Maximum, header, header);
     else
       ProgressIndicator.Start(ProgressValue.Maximum, HTML_StSheetName, HTML_StSheetName);
     end;

     m_RS = TRsbDataSet( DataQuery );

     while( m_RS.MoveNext() )
       this.PrintReportLineForDeal();
       this.ReportRQInEnsuring();
       m_IsExistsData = true;
        ProgressValue.Current = ProgressValue.Current + 1;
        ProgressIndicator.Update(ProgressValue.Current, ProgressValue.Maximum); // всего 3 парамера Update(index, count, text)
     end;

     ProgressIndicator.Stop();
  END;

  /*Параметры сделки РЕПО на корзину (одна строка на сделку)*/
  MACRO PrintReportLineForDeal()
    PrintTableLine(
      "SecType", this.SecType(),	null,                  /*Группа налогового учета <SecCode> (корзина)*/
      "N",	this.N(),	null,                              /*Константа "N"*/
      "SecName",	this.SecName(),	null,                  /*Краткое наименование ценной бумаги из сделки <CodeR>*/
      "CodeR",	this.CodeR(),	null,                      /*Внешний код сделки  РЕПО*/
      "TypeRepoShort",	this.TypeRepoShort(),	null,      /*"РОК" - обратное репо на корзину "РПК" - прямое репо на корзину*/
      "DZ",	this.DZ(),	null,                            /*Дата заключения сделки <CodeR>*/
      "N_DD1",	this.DD1(),	null,                        /*Дата фактической оплаты 1-й части сделки <CodeR>*/
      "N_DD2",	this.DD2(),	null,                        /*Фактическая дата исполне-ния 2-й части (поставки ценных бумаг) сделки РЕПО на корзину, при ее от-сутствии - плановая.*/
      "QntyOnDate", this.QntyOnDate()	,	null,            /*Количество ценных бумаг в обеспечении на отчётную дату в сделке <CodeR> , вычисляется по фактиче-ским ТО поставки ценных бумаг, ТО компенсационных поставок, уменьшающих или увеличивающих  обеспе-чение*/
      "Tot1vp",	this.Tot1vp(),	null,                    /*Стоимость 1-й части сделки <CodeR> в валюте сделки */
      "Tot2vp",	this.Tot2vp(),	null,                    /*Стоимость 2-й части сделки <CodeR> в валюте сделки */
      "Rate", 	this.Rate(),	null,                      /*Ставка РЕПО из карточки сделки <CodeR>*/
      "VP", 	this.VP(),	null,                          /*Код валюты ТО по оплате 1-й части <CodeR> */
      "Cont",	this.Cont(),	null,                        /*Полное наименование контрагента по сделке <CodeR>*/
      "CodeR_1",	this.CodeR(),	null,                    /*Код сделки РЕПО на корзину из карточки сделки*/
      "ISIN",	this.ISIN(),	null);                     /*ISIN*/
  END;

  /*Печать строк по каждому ТО в обеспечении*/
  MACRO ReportRQInEnsuring()
    var sql = RSDCommand("SELECT rq.*, FIN.T_FI_CODE, FIN.T_NAME, AVR.T_TAXGROUP, "+
                               " (case when AVR.t_ISIN IS null OR AVR.t_ISIN IN (chr(0), chr(1)) then (case when AVR.t_LSIN IS null OR AVR.t_LSIN IN (chr(0), chr(1)) then chr(0) else AVR.t_LSIN end) else AVR.t_ISIN end) AS t_ISIN "+
                           " FROM ddlrq_dbt rq, dfininstr_dbt fin, davoiriss_dbt avr" +
                          " WHERE rq.t_DocKind    = ?  " + 
                                " AND fin.t_FIID = rq.t_FIID " + 
                                " AND AVR.T_FIID = FIN.T_FIID "+
                                " AND rq.t_DocID      = ?  " + 
                                " AND rq.t_Type      IN(?,?)  " + 
                                " AND rq.t_FactDate != TO_DATE('01-01-0001','DD-MM-YYYY') "+
                                " AND rq.t_FactDate <= ? " +
                          " ORDER BY rq.t_FactDate "
                        );

    sql.addParam( "", RSDBP_IN, m_RS.BOfficeKind );
    sql.addParam( "", RSDBP_IN, m_RS.DealID );
    sql.addParam( "", RSDBP_IN, DLRQ_TYPE_DELIVERY );
    sql.addParam( "", RSDBP_IN, DLRQ_TYPE_COMPDELIVERY );
    sql.addParam( "", RSDBP_IN, m_ReportDate );
    sql.execute();
    
    m_RQ  = TRsbDataSet( sql );

    this.InitCountRQInDeal();

    while( m_RQ.MoveNext() )
      this.PrintReportLineForRQ();
      this.IncreaceCountRQInDeal();
    end;
  END;

  MACRO InitCountRQInDeal()
    m_CountRQInDeal = 1;
  END;

  MACRO IncreaceCountRQInDeal()
    m_CountRQInDeal = m_CountRQInDeal + 1;
  END;

  MACRO GetCountRQInDeal()
    return m_CountRQInDeal;
  END;

  /*Параметры текущего ТО из обеспечения (одна строка на каждое ТО)*/
  MACRO PrintReportLineForRQ()
    PrintTableLine(
      "SecType",	this.SecTypeTO(),	null,                /*Группа налогового учета  <SecCodeTO> (выпуск из ТО)*/
      "N",	this.SecCodeTO(),	null,                      /*Код ценной бумаги из теку-щего ТО*/
      "SecName",	this.SecNameTO(),	null,                /*Краткое наименование ценной бумаги из сделки <CodeR>*/
      "CodeR",	this.NumTO(),	null,                      /*<CodeR> - Порядковый номер ТО в отчёте: все ТО сортируются по дате исполнения (по возрастанию) и нумеруются, начиная с 1*/
      "TypeRepoShort",	this.TypeTOShort(),	null,        /*B - для ТО по приёму цен-ных бумаг от контрагента (вид ТО = "требование") S - для ТО по передаче ценных бумаг контрагенту (вид ТО = "обязательство")*/
      "DZ",	this.DZ(),	null,                          
      "N_DD1",	this.DDTO(),	null,                      /*Дата поставки ценных бумаг текущего ТО <CodeR>*/
      "N_DD2",	"",	null,
      "QntyOnDate", this.QntyTO()	,	null,                /*Количество ценных бумаг в ТО. Если <TypeTOShort> = S, количество выводится со знаком "минус"*/
      "Tot1vp",	"",	null,
      "Tot2vp",	"",	null,
      "Rate", 	"",	null,
      "VP", 	"",	null,
      "Cont",	"",	null,
      "CodeR_1",	this.CodeR(),	null,
      "ISIN",	this.ISINTO(),	null);
  END;                

  /*Группа налогового учета <SecCode> (корзина)*/
  MACRO SecType():STRING  
     return string(TAX_GROUP_REPO_BASKET);
  END;

  MACRO N():STRING
     return "N";
  END;

  /*Краткое наименование ценной бумаги из сделки <CodeR>*/ 
  MACRO SecName():STRING
     return string(m_RS.Name);
  END;   

  /*"РОК" - обратное репо на корзину "РПК" - прямое репо на корзину*/
  MACRO TypeRepoShort():STRING
    var type_repo = "";
    if(m_RS.IsSale)
      type_repo = "РПК";
    else
      type_repo = "РОК";
    end; 
    return type_repo;
  END;

  /*Дата заключения сделки <CodeR>*/
  MACRO DZ():DATE
    return date(m_RS.DealDate);
  END;

  /*Дата фактической оплаты 1-й части сделки <CodeR>*/
  MACRO DD1():DATE
    return date(m_RS.DD1);
  END;
  
  /*Фактическая дата исполне-ния 2-й части (поставки ценных бумаг) сделки РЕПО на корзину, при ее от-сутствии - плановая.*/
  MACRO DD2():DATE
    return date(m_RS.DD2);
  END;

  /*Количество ценных бумаг в обеспечении на отчётную дату в сделке <CodeR> ,
  вычисляется по фактическим ТО поставки ценных бумаг,
  ТО компенсационных поставок, уменьшающих или увеличивающих  обеспе-чение*/
  MACRO QntyOnDate()
    var Principal = 0.0;

    var sql = RSDCommand ( "SELECT SUM(CASE WHEN rq.T_KIND = ? THEN NVL(rq.t_Amount,0) ELSE NVL(-rq.t_Amount,0) END) PRINCIPAL  "+
                           " FROM ddlrq_dbt rq " +
                           " WHERE rq.t_DocKind    = ?  " + 
                                " AND rq.t_DocID      = ?  " + 
                                " AND rq.t_Type      IN(?,?)  " + 
                                " AND rq.t_FactDate != TO_DATE('01-01-0001','DD-MM-YYYY') "+
                                " AND rq.t_FactDate <= ? " +
                          " ORDER BY rq.t_FactDate " ) ;

    sql.addParam( "", RSDBP_IN, DLRQ_KIND_REQUEST);
    sql.addParam( "", RSDBP_IN, m_RS.BOfficeKind );
    sql.addParam( "", RSDBP_IN, m_RS.DealID );
    sql.addParam( "", RSDBP_IN, DLRQ_TYPE_DELIVERY );
    sql.addParam( "", RSDBP_IN, DLRQ_TYPE_COMPDELIVERY );
    sql.addParam( "", RSDBP_IN, m_ReportDate );
    sql.execute();

    var DataSet = TRsbDataSet( sql );
    if(DataSet.moveNext() )
      Principal = SQL_ConvTypeSum(DataSet.Principal);
    end;

    return Principal;
  END;

  /*Стоимость 1-й части сделки <CodeR> в валюте сделки */
  MACRO Tot1vp()
    return money(m_RS.TotalCostSale);
  END;

  /*Стоимость 2-й части сделки <CodeR> в валюте сделки */
  MACRO Tot2vp()
    return money(m_RS.TotalCostBuy);
  END;
 
  /*Ставка РЕПО из карточки сделки <CodeR>*/
  MACRO Rate()
     return m_RS.Rate;
  END;

  /*Код валюты ТО по оплате 1-й части <CodeR> */
  MACRO VP()  
    var VP = "";

/*  если сделку отобрали, значит ТО нам уже подходит. Нужно ТО по первой части - должно быть уже испонено,
    или до начала отчетного в периода или в отчетный период.*/
    var sql = RSDCommand(" SELECT t_FIID "+
                           " FROM ddlrq_dbt " +
                          " WHERE t_DocKind    = ? AND " + 
                                " t_DocID      = ? AND " + 
                                " t_DealPart   = 1 AND "+
                                " t_Type      in(?,?,?) "
                        );

    sql.addParam( "", RSDBP_IN, m_RS.BOfficeKind );
    sql.addParam( "", RSDBP_IN, m_RS.DealID );
    sql.addParam( "", RSDBP_IN, DLRQ_TYPE_PAYMENT );
    sql.addParam( "", RSDBP_IN, DLRQ_TYPE_AVANCE );
    sql.addParam( "", RSDBP_IN, DLRQ_TYPE_DEPOSIT );

    sql.execute();
    
    VAR vRS = TRsbDataSet( sql );
   
    if( vRS.MoveNext() )
       VP = m_CacheFinInstr.GetISO( vRS.FIID ).m_Number;
    end;   
   
    return VP;  
  END;

  /*Полное наименование контрагента по сделке <CodeR>*/
  MACRO Cont()
    return string( GetPartyFullNameByID(m_RS.PartyID) );
  END;

  /*Код сделки РЕПО на корзину из карточки сделки*/
  MACRO CodeR()
     return m_RS.DealCodeTS;
  END;

  /*Группа налогового учета  <SecCodeTO> (выпуск из ТО)*/
  MACRO SecTypeTO()
     return m_Rq.TaxGroup;  
  END;

  /*Код ценной бумаги из теку-щего ТО*/
  MACRO SecCodeTO()
     return m_RQ.FI_CODE;
  END;

  /*Краткое наименование ценной бумаги из сделки <CodeR>*/
  MACRO SecNameTO()
     return m_Rq.T_NAME;
  END;

 /*<CodeR> - Порядковый номер ТО в отчёте: все ТО сортируются по дате исполнения (по возрастанию) и нумеруются, начиная с 1*/
  MACRO NumTO()
     return string(this.CodeR() + "-" +GetCountRQInDeal());
  END;

  /*B - для ТО по приёму цен-ных бумаг от контрагента (вид ТО = "требование") S - для ТО по передаче ценных бумаг контрагенту (вид ТО = "обязательство")*/
  MACRO TypeTOShort()
    var typeRQ ="";
    if(m_Rq.Kind == 0)
      typeRQ = "B";
    elif(m_Rq.Kind == 1)
      typeRQ = "S";
    end;
    return typeRQ;
  END;

  /*Дата поставки ценных бумаг текущего ТО <CodeR>*/
  MACRO DDTO():DATE
     return m_Rq.FactDate;
  END;

  /*Количество ценных бумаг в ТО. Если <TypeTOShort> = S, количество выводится со знаком "минус"*/
  MACRO QntyTO()
    var sign ="";
    if(m_Rq.Kind == 0)
      sign = "+";
    elif(m_Rq.Kind == 1)
      sign = "-";
    end;

    return string(sign + string(m_Rq.Amount) );
  END;

  MACRO ISINTO():STRING
     return m_Rq.ISIN;
  END;

/*Вызов панели отчета и установка шаблона*/
  initTX_RegistrReport( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;


MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)

  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  if( not IsWebService )
      Basket_Form = SP_Basket_Move_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      Basket_Form = WS_TX_TxBasket_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = Basket_Form.Run();
    end;

    if( stat )
      Basket_Report = ReportMoveBasketREPO( null, "Report_MoveBasketREPO.xls", false, IsWebService, ReportHashCode );
      Basket_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat( Basket_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
        FullReportFileNames = Basket_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat( Basket_Report.PrintMode() );
      end;
    end;
  end;

  return FullReportFileNames;
END;
