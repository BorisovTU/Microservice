/*
$Name: CheckPrimaryData_Form.mac
$Module: Ценные бумаги
$Description: Форма для отчета "Проверочный отчет первичных данных"
PROGRAMMED BY:   Dubovoy A.A.
CREATION DATE:   23/12/2014
*/

import "DlRepForm_h.mac", "secinter.mac";

// Номера полей в форме отчета
CONST
      PNFLD_CHECKPD_DateBegin            = 0,
      PNFLD_CHECKPD_DateEnd              = 1,
      PNFLD_CHECKPD_IsOurDeal            = 2,
      PNFLD_CHECKPD_IsClientDeal         = 3,
      PNFLD_CHECKPD_ClientCode           = 4,
      PNFLD_CHECKPD_ClientName           = 5,
      PNFLD_CHECKPD_IsCheckDeal          = 6,
      PNFLD_CHECKPD_IsCheckAvoir         = 7,
      PNFLD_CHECKPD_IsCheckClientByDeal  = 8,
      PNFLD_CHECKPD_IsUserCheck          = 9,
      PNFLD_CHECKPD_UserMacro            = 10;

CLASS CheckPDFormData()
   var                     
     DateBegin = {curdate},
     DateEnd = Date(0,0,0),
     IsOurDeal = "X",
     IsClientDeal = "X",
     ClientCode = -1,
     IsCheckDeal = "X",
     IsCheckAvoir = "X",
     IsCheckClientByDeal = "X",
     IsUserCheck = "",
     UserMacro = "user_prior_check.mac",
     IsExcel = "X",

     IsSetEndDate = false;
END;

VAR RepFormData = CheckPDFormData();

PRIVATE CONST
  H_IsOurDeal           = 101,
  H_IsClient            = 102,
  H_ClientCode          = 103, // !?
  H_ClientName          = 104, // !?
  H_IsCheckDeal         = 105,
  H_IsCheckAvoir        = 106,
  H_IsCheckClientByDeal = 107,
  H_IsUserCheck         = 108,
  H_UserMacro           = 109;

PRIVATE CONST SelCodeKind = 1; // Общий параметр для листалок Бирж,Субьектов,Брокеров,Клиентов     


/*Начальная дата выпуска отчета*/
PRIVATE MACRO DLUSR_FldProc_BeginDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
       if( FldRealValue > pThis.GetLinkedValue(DL_PNFLD_ENDDATE) )
         if( pThis.GetLinkedValue(DL_PNFLD_ENDDATE) != Date(0,0,0) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
         end;
       end;
     end; 
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

/*Конечная дата выпуска отчета*/
PRIVATE MACRO DLUSR_FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     /*инициализация по умолчанию*/
     FldRealValue = RepFormData.DateEnd;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;
     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( FldRealValue != Date(0,0,0) )
           if( FldRealValue < pThis.GetLinkedValue(DL_PNFLD_BEGINDATE) )
              MsgBox( "Дата окончания не может быть меньше даты начала периода.");
              return CM_IGNORE;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_IsOurDeal( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
     else
        FldShowValue = FldRealValue = "";

        var s, r;
        pThis.GetLinkedValue( H_IsClient, @s, @r);
        if( s == "" )
          pThis.SetLinkedValue( H_IsClient, "X");
        end;
     end;                                                         
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_IsClientDeal( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == "" ) /*инициализация по умолчанию*/
        DisableFields( pThis.Data(), PNFLD_CHECKPD_ClientCode );
     end;
     if( FldRealValue == "X" )
        EnableFields( pThis.Data(), PNFLD_CHECKPD_ClientCode );
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        EnableFields( pThis.Data(), PNFLD_CHECKPD_ClientCode );
     else
        FldShowValue = FldRealValue = "";
        DisableFields( pThis.Data(), PNFLD_CHECKPD_ClientCode );

        var s, r;
        pThis.GetLinkedValue( H_IsOurDeal, @s, @r);
        if( s == "" )
          pThis.SetLinkedValue( H_IsOurDeal, "X");
        end;
     end;                                                         
  end;
  return CM_DEFAULT;
END;

/*ищем код субъекта*/
PRIVATE MACRO  GetPartCode( PartyID:integer )
  var sqlPartcode, PartCode;

  sqlPartcode = RSDCommand("select t_Code from dpartcode_dbt " +
                           " where t_PartyID = ? " +
                           "   and t_CodeKind = 1");

  sqlPartcode.addParam("", RSDBP_IN, PartyID);
  sqlPartcode.execute();

  PartCode = TRsbDataSet(sqlPartcode);
  if( PartCode.MoveNext() )
     return PartCode.Code;
  end;

  return "";
END;

/*Листалка клиентов
  является физическими лицами,
  вид обслуживания которых <Фондовый дилинг> или <Срочные контракты>,
  категория "Является плательщиком НДФЛ" <> "Нет".*/
private MACRO DLUSR_FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  var Party     = TRecHandler("party.dbt");
  var partcode  = TBFile( "partcode.dbt",  "R", 0 );
  var whereCond:string  = "", DepCode;
  record clnt(party);

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_ClientName, STR_ALLVALUE);
     else
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
          pThis.SetLinkedValue( H_ClientName, Party.rec.ShortName);
        end;
     end;

  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_ClientName, STR_ALLVALUE );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     whereCond =  " Exists(SELECT attr.* " +
                  "          FROM dobjatcor_dbt attr " +
                  "         WHERE attr.t_ObjectType = " + string(OBJTYPE_PARTY) +
                  "           AND attr.t_GroupID = 42 " + // категория "НДФЛ"
                  "           AND attr.t_Object = LPAD(t.T_PARTYID, 10, '0') " +
                  "           AND attr.t_AttrID in (select objattr.t_AttrID from dobjattr_dbt objattr " +
                  "                                 where objattr.t_ObjectType = attr.t_ObjectType " + 
                  "                                   and objattr.t_GroupID = attr.t_GroupID " +
                  "                                   and objattr.t_Name not like 'Нет') " +
                  "           AND (SELECT count(1) " +
                  "                  FROM DOBJATCOR_DBT t " +
                  "                 WHERE t.T_ObjectType = attr.t_ObjectType " +
                  "                   AND t.T_GroupID    = attr.t_GroupID " +
                  "                   AND t.t_Object     = attr.t_Object " +
//                  "                   AND t.T_ValidFromDate <= " + GetSQLDate(pThis.GetFieldValue(DL_PNFLD_ENDDATE)) +
                  "                ) > 0 ) " +
                  "           AND t.T_LegalForm = " + string(legal_form_phys) + " " + // Физические лица
                  "           AND t.T_PARTYID IN ( SELECT c2.T_PARTYID " +
                                  "    FROM dclient_dbt c2 " +
                                  "   WHERE t.T_PARTYID = c2.T_PARTYID " + 
                                  "     AND c2.t_servicekind IN (" + string(PTSK_STOCKDL) + ", " + string(15/*PTSK_DV*/) + " ) "; // вид обслуживания

/*     if( pThis.ExistField(DL_PNFLD_DEPARTCODE) )
        DepCode = pThis.GetLinkedValue( DL_PNFLD_DEPARTCODE );
        if( (DepCode != null) and (DepCode > 0) )
           whereCond = whereCond + "     AND c2.t_Department = " + string(DepCode);
        end;
     end;*/

     whereCond = whereCond + "     AND c2.t_Branch = 0 "+
                                  ") ";

     if( ListPText( Party, Party.rec.PartyID, whereCond) == true )
        FldRealValue = Party.rec.PartyID;
        partcode.Clear();
        partcode.rec.PartyID  = Party.rec.PartyID;
        partcode.rec.CodeKind = SelCodeKind;
        if (partcode.GetEQ())
          FldShowValue = partcode.rec.Code;
        end;
        pThis.SetLinkedValue( H_ClientName, Party.rec.ShortName);
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) 
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DLUSR_FldProc_IsUserCheck( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == "" ) /*инициализация по умолчанию*/
        DisableFields( pThis.Data(), PNFLD_CHECKPD_UserMacro );
     end;
     if( FldRealValue == "X" )
        EnableFields( pThis.Data(), PNFLD_CHECKPD_UserMacro );
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldShowValue = FldRealValue = "X";
        EnableFields( pThis.Data(), PNFLD_CHECKPD_UserMacro );
     else
        FldShowValue = FldRealValue = "";
        DisableFields( pThis.Data(), PNFLD_CHECKPD_UserMacro );
     end;                                                         
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_CheckPD_Panel()

  PRIVATE MACRO Init()
     AddFieldProc( 0, PNFLD_CHECKPD_DateBegin,           DL_PNFLD_BEGINDATE,        @DLUSR_FldProc_BeginDate,    RepFormData.DateBegin);
     AddFieldProc( 0, PNFLD_CHECKPD_DateEnd,             DL_PNFLD_ENDDATE,          @DLUSR_FldProc_EndDate,      RepFormData.DateEnd);
     AddFieldProc( 0, PNFLD_CHECKPD_IsOurDeal,           H_IsOurDeal,               @DLUSR_FldProc_IsOurDeal,    RepFormData.IsOurDeal );
     AddFieldProc( 0, PNFLD_CHECKPD_IsClientDeal,        H_IsClient,                @DLUSR_FldProc_IsClientDeal, RepFormData.IsClientDeal );
     AddFieldProc( 0, PNFLD_CHECKPD_ClientCode,          H_ClientCode,              @DLUSR_FldProc_Client,       RepFormData.ClientCode );         
     AddFieldProc( 0, PNFLD_CHECKPD_ClientName,          H_ClientName,              @Default,                    "" );
     AddFieldProc( 0, PNFLD_CHECKPD_IsCheckDeal,         H_IsCheckDeal,             @DL_FldProc_CheckBox,        RepFormData.IsCheckDeal );
     AddFieldProc( 0, PNFLD_CHECKPD_IsCheckAvoir,        H_IsCheckAvoir,            @DL_FldProc_CheckBox,        RepFormData.IsCheckAvoir );
     AddFieldProc( 0, PNFLD_CHECKPD_IsCheckClientByDeal, H_IsCheckClientByDeal,     @DL_FldProc_CheckBox,        RepFormData.IsCheckClientByDeal );
     AddFieldProc( 0, PNFLD_CHECKPD_IsUserCheck,         H_IsUserCheck,             @DLUSR_FldProc_IsUserCheck,  RepFormData.IsUserCheck );
     AddFieldProc( 0, PNFLD_CHECKPD_UserMacro,           H_UserMacro,               @Default,                    RepFormData.UserMacro );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_res.lbr", "CHECKPD" ); 
END;      
