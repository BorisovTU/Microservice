/*
$Name:        uniavr30.mac
$Module:      Ценные бумаги
$Description: Операция конвертации ц/б. Шаг списания стоимости
*/

import InsCarryDoc, "sp_car.mac", RsbDataSet, "sp_cars.mac", qracctools;

private record CredCatAcc(account);
private record NewAcnt(account);
private record OldAcnt(account);

private var UniAvrObj = NULL;

private macro ВыполнитьПроводкиДляНепоставленных(comm, Doc)
  var Query, DataSet, FD_Deal, FD_Deal_New, Rest, N, CatCode, CarSum, CarFIID, CurNumber = 0;
  file   mcconacc( mcconacc );

  /*Если будем открывать/закрывать счета - нужно для сохранения инфы для протокола*/
  Query = RSDCommand(
                     " SELECT NVL(MAX(t_AccDocID), 0) MaxNumber " + 
                     "   FROM dmcconacc_dbt " + 
                     "  WHERE T_DOCKIND = ? " +
                     "    AND T_DOCID = ? "
                    );

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, comm.DocumentID );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if ((not DataSet.MoveNext()) or (DataSet.MaxNumber < 1) )
     CurNumber = 0;
  else
     CurNumber = DataSet.MaxNumber;
  end;

  Query = RSDCommand(
                     " SELECT Count(1) v_Count " + 
                     "   FROM dscdlfi_dbt " + 
                     "  WHERE T_DEALKIND = ? " +
                     "    AND T_DEALID   = ? "
                    );

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, comm.DocumentID );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if ((not DataSet.MoveNext()) or (DataSet.v_Count < 1) )
     N = 0;
  else
     N = DataSet.v_Count;
  end;

  if ((comm.Flag1 == SET_CHAR) and (N == 1))

     Query = DL_RSDCommand(
                        " SELECT s.t_DealID, pmwr.t_NewFIID, pmwr.t_NewCost, pmwr.t_OldCost, s.t_SumID, s.t_FIID " + 
                        "   FROM dscdlpmwr_dbt pmwr, dpmwrtsum_dbt s " + 
                        "  WHERE pmwr.T_DEALKIND   = ?  " +
                        "    AND pmwr.T_DEALID     = ?  " +
                        "    AND pmwr.T_STATE      = " + SCDLPMWR_STATE_NOT_READY +
                        "    AND pmwr.T_PARTY      = -1 " +
                        "    AND s.T_DEPARTMENT    = ?  " +
                        "    AND pmwr.T_SUMID      = s.T_SUMID " +
                        "    AND NOT EXISTS(SELECT 1 " +
                        "                     FROM DSCUNIAVRSTOBJ_DBT o " +
                        "                    WHERE o.t_ID_Operation = ? " +
                        "                      AND o.t_IsWrt = 1 " +
                        "                      AND o.t_ObjKind = "+UniAvrObj.OBJKIND_SUMID+
                        "                      AND o.t_ObjID = s.t_SumID " +
                        "                      AND o.t_FIID = s.t_FIID " +
                        "                  )"
                       );

     Query.addParam(comm.DocKind );
     Query.addParam(comm.DocumentID );
     Query.addParam(comm.Division );
     Query.addParam(UniAvrObj.GetID_Operation());

     DataSet = Query.execute();

     while (DataSet.MoveNext())
        if(UniAvrObj.NeedBreak())
          UniAvrObj.SetRecurseStep();
          break;
        else
          UniAvrObj.AddRec(UniAvrObj.OBJKIND_SUMID, DataSet.SumID, DataSet.FIID);
        end;
        
        
        FD_Deal = SPFirstDoc( LEG_KIND_DL_TICK, DataSet.DealID );
        if ((FD_Deal.ExistAvance and (not ТОЗакрыто(FD_Deal.GetRQ(DLRQ_TYPE_AVANCE)))) or 
            (not FD_Deal.ExistAvance and (not ТОЗакрыто(FD_Deal.GetRQ(DLRQ_TYPE_PAYMENT)))) or 
            (FD_Deal.GetRQ(DLRQ_TYPE_DELIVERY).rec.State == DLRQ_STATE_OVERDUE)
           )
        else
           FD_Deal_New = SPFirstDoc( LEG_KIND_DL_TICK, DataSet.DealID );

           FD_Deal_New.v_BaseFIID = DataSet.NewFIID;

           if (IsBuy(FD_Deal_New.Group))
              CatCode = "+Форвард, расчеты";
           else
              CatCode = "-Форвард, расчеты";
           end;

           if (comm.BeginDate == comm.EndDate)
              if( ( not FD_Deal_New.IsExistAccount( CatCode, null, null, FIROLE_SRCA, NewAcnt, null, comm.EndDate ) ) AND
                  ( not FD_Deal_New.OpenAccount( CatCode, null, null, FIROLE_SRCA, NewAcnt, comm.EndDate ) ))
                 return 1;
              else
                 CurNumber = CurNumber + 1;
                 // сохраняем для протокола
                 ClearRecord(mcconacc);
                 mcconacc.DocKind  = comm.DocKind;
                 mcconacc.DocID    = comm.DocumentID;
                 mcconacc.AccDocID = CurNumber;
                 mcconacc.NewAccount = NewAcnt.Account;
                 mcconacc.IsOpen   = SET_CHAR;

                 OprUpdateMCCONACC( mcconacc );
              end;
           end;

           CarSum = FD_Deal.dl_leg.rec.TotalCost;
           if( IsRelativePrice( FD_Deal.tick.rec, FD_Deal.dl_leg.rec ) )
              CarFIID = FD_Deal.FaceFIID();
           else
              CarFIID = FD_Deal.dl_leg.rec.CFI;
           end;

           if (IsSale(FD_Deal_New.Group))
              if (comm.BeginDate == comm.EndDate)
                 if(not ПроводкаПоКатегориямУчета( FD_Deal, 
                        CatCode, NewAcnt,/* КатегДеб , КатегКредит*/
                        comm.BeginDate,              /* Дата */
                        1,                           /* Глава */
                        CarFIID,                     /* Валюта */
                        CarSum,                      /* Сумма */
                        Doc,                         /* Документ */
                        INPCARRY,                    /* Result_Carry */
                        " 1",                        /* Kind_Oper */
                        "",                          /* Numb_Document */
                        "Перенос остатка лицевого счета",
                        null,
                        null, null, 
                        FD_Deal.GetPayFIID(), FD_Deal.GetPayFIID()
                       ) 
                   )
                     return 1;
                 end;
              else
                 if(not ПроводкаПоКатегориямУчета( FD_Deal, 
                        CatCode, "-Расчеты, ГО",/* КатегДеб , КатегКредит*/
                        comm.BeginDate,              /* Дата */
                        1,                           /* Глава */
                        CarFIID,                     /* Валюта */
                        CarSum,                      /* Сумма */
                        Doc,                         /* Документ */
                        INPCARRY,                    /* Result_Carry */
                        " 1",                        /* Kind_Oper */
                        "",                          /* Numb_Document */
                        "Перенос остатка лицевого счета",
                        null,
                        null, null, 
                        FD_Deal.GetPayFIID(), FD_Deal.GetPayFIID()
                       ) 
                   )
                     return 1;
                 end;
              end;
           else
              if (comm.BeginDate == comm.EndDate)
                 if(not ПроводкаПоКатегориямУчета( FD_Deal, 
                        NewAcnt, CatCode,/* КатегДеб , КатегКредит*/
                        comm.BeginDate,              /* Дата */
                        1,                           /* Глава */
                        CarFIID,                     /* Валюта */
                        CarSum,                      /* Сумма */
                        Doc,                         /* Документ */
                        INPCARRY,                    /* Result_Carry */
                        " 1",                        /* Kind_Oper */
                        "",                          /* Numb_Document */
                        "Перенос остатка лицевого счета",
                        null,
                        null, null, 
                        FD_Deal.GetPayFIID(), FD_Deal.GetPayFIID()
                       ) 
                   )
                     return 1;
                 end;
              else
                 if(not ПроводкаПоКатегориямУчета( FD_Deal, 
                        "+Расчеты, ГО", CatCode,/* КатегДеб , КатегКредит*/
                        comm.BeginDate,              /* Дата */
                        1,                           /* Глава */
                        CarFIID,                     /* Валюта */
                        CarSum,                      /* Сумма */
                        Doc,                         /* Документ */
                        INPCARRY,                    /* Result_Carry */
                        " 1",                        /* Kind_Oper */
                        "",                          /* Numb_Document */
                        "Перенос остатка лицевого счета",
                        null,
                        null, null, 
                        FD_Deal.GetPayFIID(), FD_Deal.GetPayFIID()
                       ) 
                   )
                     return 1;
                 end;
              end;
           end;

           Rest = $0;
           if( FD_Deal.IsExistAccount( CatCode, null, true, null, OldAcnt, null, comm.BeginDate, FD_Deal.GetPayFIID() ) )
              Rest = abs(money(GetRestAccount(OldAcnt, comm.BeginDate)));
           end;

           if (Rest == $0)
              if( FD_Deal.CloseAccount( CatCode, comm.BeginDate, null ) == false )
                 return 1;
              else
                 CurNumber = CurNumber + 1;
                 // сохраняем для протокола
                 ClearRecord(mcconacc);
                 mcconacc.DocKind  = comm.DocKind;
                 mcconacc.DocID    = comm.DocumentID;
                 mcconacc.AccDocID = CurNumber;
                 mcconacc.NewAccount = OldAcnt.Account;
                 mcconacc.IsClose  = SET_CHAR;

                 OprUpdateMCCONACC( mcconacc );
              end;
           end;

        end;
     end;
  end;

  return 0;
end;

private macro ВыполнитьПроводкиСписания( comm, Doc )
  var FD, dat, cmd, Query, SaleID, cmd2, Query2, GroupID;
  var AccCredit, FIIDCredit, AccDebet, FIIDDebet, Глава, IsResponsible:bool = false;
  var Cost, BonusRest, CarSum = $0, NKDRest = $0;
  var ALL_CostBuy             = $0, CostBuy             = $0, // Списанная чистая стоимость (Счист)
      ALL_BalanceCostBuy      = $0, BalanceCostBuy      = $0, // Списанная текущая стоимость 
      ALL_NKDBuy              = $0, NKDBuy              = $0, // НКД ( S(НКД упл.))
      ALL_InterestIncomeBuy   = $0, InterestIncomeBuy   = $0, // Списанный начисленный ПД 
      ALL_DiscountIncomeBuy   = $0, DiscountIncomeBuy   = $0, // Cписанный начисленный ДД 
      ALL_NotCarryInterestBuy = $0, NotCarryInterestBuy = $0, // Списанный не отнесенный на доходы ПД  
      ALL_NotCarryDiscountBuy = $0, NotCarryDiscountBuy = $0, // Списанный не отнесенный на доходы ДД  
      ALL_InterestIncomeAdd   = $0, InterestIncomeAdd   = $0, // Доначисленный ПД S(Sдонач.ПДi)
      ALL_DiscountIncomeAdd   = $0, DiscountIncomeAdd   = $0, // Доначисленный ДД S(Sдонач.ДДi)
      ALL_NotCarryInterestAdd = $0, NotCarryInterestAdd = $0, // Доначисленный не отнесенный на доходы ПД  
      ALL_NotCarryDiscountAdd = $0, NotCarryDiscountAdd = $0, // Доначисленный не отнесенный на доходы ДД  
      ALL_InterestIncomeSum   = $0, InterestIncomeSum   = $0, // Суммарный начисленный ПД S(ПДначисл)
      ALL_DiscountIncomeSum   = $0, DiscountIncomeSum   = $0, // Суммарный начисленный ДД S(ДДначисл)
      ALL_NotCarryInterestSum = $0, NotCarryInterestSum = $0, // Суммарный не отнесенный на доходы ПД  S(ПДне_отн)
      ALL_NotCarryDiscountSum = $0, NotCarryDiscountSum = $0, // Суммарный не отнесенный на доходы ДД  S(ДДне_отн)
      ALL_BalanceCostSum      = $0, BalanceCostSum      = $0, // Суммарная текущая стоимость ( (Стек))
      ALL_BalanceCostBD       = $0, BalanceCostBD       = $0, // Списанная стоимость в ОД (Часть остатка счета "- ОД", пропорциональная количеству ц/б, проданно-му из ТП и ППР)
      ALL_OutlayBuy           = $0, OutlayBuy           = $0, // Списанные затраты
      ALL_OverAmountBuyPPR    = $0, OverAmountBuy       = $0, // Списанная сумма переоценки S(ПО)
      ReservAmountBuy     = $0, // Списанная сумма резерва (Сумма созданного резерва)
      ALL_AmountNotPVO        = $0, ALL_Amount          = $0,
      IncomeReservBuy         = $0, 
      BonusAdd                = $0,
      ALL_CostSum             = $0, CostSum             = $0, CostSale = $0, NotWrtBonus = $0;

  FD = SPFirstDocDLCOMMGlobal(comm);
  dat = comm.BeginDate;

  if( FI_IsResponsible(comm.FIID, dat) )
     AccDebet  = "Начисл.ПДД, ц/б";
     FIIDDebet = FD.FaceFIID();

     AccCredit  = "+%ДЦ/б";
     FIIDCredit = NATCUR;

     Глава = 1;
     IsResponsible = true;
  else
     AccDebet  = "%Ндоход внебаланс, ц/б";
     FIIDDebet = FD.FaceFIID();

     AccCredit  = "ВнебалСчетКорресп";
     FIIDCredit = NATCUR;

     Глава = 3;
     IsResponsible = false;
  end;

  //Найти лот списания по dl_comm
  cmd = DL_RSDCommand("SELECT wrt.t_SumID SumID, wrt.t_FIID " +
                      "  FROM dpmwrtsum_dbt wrt " +
                      " WHERE wrt.t_DocKind  = ? " +
                      "   AND wrt.t_DocID    = ? " +
                      "   AND wrt.t_Buy_Sale = " + PM_WRITEOFF_SUM_SALE + 
                      "   AND wrt.t_Party    = -1    " +
                      "   AND NOT EXISTS(SELECT 1 " +
                      "                    FROM DSCUNIAVRSTOBJ_DBT o " +
                      "                   WHERE o.t_ID_Operation = ? " + 
                      "                     AND o.t_IsWrt = 1 " + 
                      "                     AND o.t_ObjKind = " + UniAvrObj.OBJKIND_SUMID +
                      "                     AND o.t_ObjID = wrt.t_SumID " +
                      "                     AND o.t_FIID = wrt.t_FIID " +
                      "                 )"
                     );

  cmd.addParam(comm.DocKind );
  cmd.addParam(comm.DocumentID );
  cmd.addParam(UniAvrObj.GetID_Operation());

  Query = cmd.execute();
  while( Query.movenext() )
     if(UniAvrObj.NeedBreak())
       UniAvrObj.SetRecurseStep();
       break;
     else
       UniAvrObj.AddRec(UniAvrObj.OBJKIND_SUMID, Query.SumID, Query.FIID);
     end;
     
     SaleID = Query.SumID;
     /* DEF-37215 Закомментировала, т.к. выдавало ошибку при группировке. По коду ниже не используют этот параметр*/
     cmd2 = DL_RSDCommand("SELECT Buy.t_GroupID GroupID/*, lnk.t_LnkID*/ " +
                          "  FROM dpmwrtsum_dbt Buy, dpmwrtlnk_dbt Lnk " +
                          " WHERE Lnk.t_SaleID  = ? " +
                          "   AND Lnk.t_BuyID   = Buy.t_SumID " +
                          " GROUP BY Buy.t_GroupID " );

     cmd2.addParam(SaleID);

     Query2 = cmd2.execute();
     while( Query2.movenext() )
        
        GroupID = Query2.GroupID;

        if( WRT_GetSaleFinResult_EX( SaleID, 
                                     GroupID, 
                                     0,
                                     @CostBuy            ,
                                     @BalanceCostBuy     ,
                                     @NKDBuy             ,
                                     @InterestIncomeBuy  ,
                                     @DiscountIncomeBuy  ,
                                     @NotCarryInterestBuy,
                                     @NotCarryDiscountBuy,
                                     @InterestIncomeAdd  ,
                                     @DiscountIncomeAdd  ,
                                     @NotCarryInterestAdd,
                                     @NotCarryDiscountAdd,
                                     @InterestIncomeSum  ,
                                     @DiscountIncomeSum  ,
                                     @NotCarryInterestSum,
                                     @NotCarryDiscountSum,
                                     @BalanceCostSum     ,
                                     @OutlayBuy          ,
                                     @OverAmountBuy      ,
                                     NULL                ,
                                     NULL                ,
                                     @ReservAmountBuy    ,
                                     NULL                ,
                                     NULL                ,
                                     @BalanceCostBD      ,
                                     @CostSale           ,
                                     NULL                ,
                                     NULL                ,
                                     @IncomeReservBuy    ,
                                     NULL                ,
                                     NULL                ,
                                     @BonusAdd           ,
                                     @CostSum            ,
                                     null                ,
                                     null                ,
                                     null                ,
                                     null                ,
                                     null                ,
                                     @BonusRest          ,
                                     @NotWrtBonus
                                   ) != true )
           return 1;
        end;

        //Доначисление ПДД
        if( DiscountIncomeAdd != 0 )
           if( not ПроводкаПоКатегориямУчета( FD, 
                                              AccDebet, AccCredit,         /* КатегДеб, КатегКредит*/
                                              dat,                         /* Дата */
                                              Глава,                       /* Глава */
                                              FD.FaceFIID(),               /* Валюта */
                                              DiscountIncomeAdd,           /* Сумма Sдонач.ДДi*/
                                              Doc,                         /* Документ */
                                              INPCARRY,                    /* Result_Carry */
                                              " 1",                        /* Kind_Oper */
                                              "",                          /* Numb_Document */
                                              "Доначисление дисконтного дохода",
                                              null,
                                              IIF(IsResponsible, GetFIRoleByPortfolio(GroupID, true), null),
                                              IIF(IsResponsible, GetFIRoleByPortfolio(GroupID, true), null),
                                              FIIDDebet, FIIDCredit
                                            )
             )
              return 1;
           end;
        end;

        if( InterestIncomeAdd != 0 )
           if( not ПроводкаПоКатегориямУчета( FD, 
                                              AccDebet, AccCredit,         /* КатегДеб, КатегКредит*/
                                              dat,                         /* Дата */
                                              Глава,                       /* Глава */
                                              FD.FaceFIID(),               /* Валюта */
                                              InterestIncomeAdd,           /* Сумма Sдонач.ПДi*/
                                              Doc,                         /* Документ */
                                              INPCARRY,                    /* Result_Carry */
                                              " 1",                        /* Kind_Oper */
                                              "",                          /* Numb_Document */
                                              "Доначисление процентного дохода",
                                              null,
                                              IIF(IsResponsible, GetFIRoleByPortfolio(GroupID, false, true), null),
                                              IIF(IsResponsible, GetFIRoleByPortfolio(GroupID, false, true), null),
                                              FIIDDebet, FIIDCredit
                                            )
             )
              return 1;
           end;
        end;

        if( CheckChargeBonus() and (BonusAdd != 0) )
           if( not ПроводкаПоКатегориямУчета( FD, 
                                              "Начисл.ПДД, ц/б", "Премия, ц/б", /* КатегДеб , КатегКредит*/
                                              dat,                              /* Дата */
                                              1,                                /* Глава */
                                              FD.FaceFIID(),                    /* Валюта */
                                              BonusAdd,                         /* Сумма Sдонач.Прi*/
                                              Doc,                              /* Документ */
                                              INPCARRY,                         /* Result_Carry */
                                              " 1",                             /* Kind_Oper */
                                              "",                               /* Numb_Document */
                                              "Доначисление премии",
                                              null,
                                              GetFIRoleByPortfolio(GroupID, false, false, false, false, true),
                                              GetFIRoleByPortfolioBonus(GroupID)
                                            )
             )
              return 1;
           end;
        end;

        //Отнесение оставшегося начисленного дохода на доходы банка
        if( NotCarryDiscountSum != 0 )
           if( not ПроводкаПоКатегориямУчета( FD,
                                              "ВнебалСчетКорресп", "%Ндоход внебаланс, ц/б", /* КатегДеб, КатегКредит*/
                                              dat,                         /* Дата */
                                              3,                           /* Глава */
                                              FD.FaceFIID(),               /* Валюта */
                                              Abs(NotCarryDiscountSum),    /* Сумма  - начисленный, но не отнесенный на доходы ДД*/
                                              Doc,                         /* Документ */
                                              INPCARRY,                    /* Result_Carry */
                                              " 1",                        /* Kind_Oper */
                                              "",                          /* Numb_Document */
                                              "Списание с внебаланса начисленного ДД",
                                              null,
                                              null,
                                              null,
                                              NATCUR, FD.FaceFIID()
                                            )
             )
              return 1;
           end;

           if( not ПроводкаПоКатегориямУчета( FD,
                                              "Начисл.ПДД, ц/б", "+%ДЦ/б", /* КатегДеб, КатегКредит*/
                                              dat,                         /* Дата */
                                              1,                           /* Глава */
                                              FD.FaceFIID(),               /* Валюта */
                                              Abs(NotCarryDiscountSum),    /* Сумма  - начисленный, но не отнесенный на доходы ДД*/
                                              Doc,                         /* Документ */
                                              INPCARRY,                    /* Result_Carry */
                                              " 1",                        /* Kind_Oper */
                                              "",                          /* Numb_Document */
                                              "Отнесение на доходы начисленного ДД",
                                              null,
                                              GetFIRoleByPortfolio(GroupID, true),
                                              GetFIRoleByPortfolio(GroupID, true),
                                              FD.FaceFIID(), NATCUR
                                            )
             )
              return 1;
           end;
        end;

        if( NotCarryInterestSum != 0 )
           if( not ПроводкаПоКатегориямУчета( FD, 
                                              "ВнебалСчетКорресп", "%Ндоход внебаланс, ц/б", /* КатегДеб, КатегКредит*/
                                              dat,                         /* Дата */
                                              3,                           /* Глава */
                                              FD.FaceFIID(),               /* Валюта */
                                              Abs(NotCarryInterestSum),    /* Сумма  - начисленный, но не отнесенный на доходы ПД*/
                                              Doc,                         /* Документ */
                                              INPCARRY,                    /* Result_Carry */
                                              " 1",                        /* Kind_Oper */
                                              "",                          /* Numb_Document */
                                              "Списание с внебаланса начисленного ПД",
                                              null,
                                              null,
                                              null,
                                              NATCUR, FD.FaceFIID()
                                            )
             )
              return 1;
           end;

           if( not ПроводкаПоКатегориямУчета( FD, 
                                              "Начисл.ПДД, ц/б", "+%ДЦ/б", /* КатегДеб, КатегКредит*/
                                              dat,                         /* Дата */
                                              1,                           /* Глава */
                                              FD.FaceFIID(),               /* Валюта */
                                              Abs(NotCarryInterestSum),    /* Сумма  - начисленный, но не отнесенный на доходы ПД*/
                                              Doc,                         /* Документ */
                                              INPCARRY,                    /* Result_Carry */
                                              " 1",                        /* Kind_Oper */
                                              "",                          /* Numb_Document */
                                              "Отнесение на доходы начисленного ПД",
                                              null,
                                              GetFIRoleByPortfolio(GroupID, false, true),
                                              GetFIRoleByPortfolio(GroupID, false, true),
                                              FD.FaceFIID(), NATCUR
                                            )
             )
              return 1;
           end;
        end;

        // Списание премии на расходы
        if( NotWrtBonus != 0 )
           if( not ПроводкаПоКатегориямУчета( FD, 
                                              "-Премия, ц/б", IIF(CheckChargeBonus(), "Начисл.ПДД, ц/б", "Премия, ц/б"), /* КатегДеб, КатегКредит */
                                              dat,                               /* Дата */
                                              1,                                 /* Глава */
                                              FD.FaceFIID(),                     /* Валюта */
                                              NotWrtBonus,                       /* Сумма Sдонач.Прi */
                                              Doc,                               /* Документ */
                                              INPCARRY,                          /* Result_Carry */
                                              " 1",                              /* Kind_Oper */
                                              "",                                /* Numb_Document */
                                              "Списание премии на расходы",
                                              null,
                                              GetFIRoleByPortfolio(GroupID, false, false, false, false, true),
                                              IIF(CheckChargeBonus(), GetFIRoleByPortfolio(GroupID, false, false, false, false, true), GetFIRoleByPortfolioBonus(GroupID))
                                            )
             )
              return 1;
           end;
        end;

        //списание стоимости покупки
        if (not FI_IsCouponAvoiriss(FD.fininstr))
           Cost = CostBuy;
        else
           Cost = CostSum;
        end;

        if( ОтдельныйУчетУплНКД() )
           NKDRest = NKDBuy;
        else
           NKDRest = $0;
        end;

        if( (Cost - BonusRest - NKDRest) != 0 )
           var ConvSum = Cost - BonusRest - NKDRest;
           if(FD.FaceFIID()!= FD.GetAccFIID("Наш портфель ц/б"))
              SmartConvertSum(ConvSum, ConvSum, dat, FD.FaceFIID(), FD.GetAccFIID("Наш портфель ц/б"));
           end;
           if(not ПроводкаПоКатегориямУчета( FD, 
                  "Реализация, ц/б", "Наш портфель ц/б",/* КатегДеб , КатегКредит*/
                  dat,                         /* Дата */
                  1,                           /* Глава */
                  /*FD.FaceFIID(),*/ FD.GetAccFIID("Наш портфель ц/б"), /* Валюта */
                  ConvSum                   ,  /* Сумма  Счист*/
                  Doc,                         /* Документ */
                  INPCARRY,                    /* Result_Carry */
                  " 1",                        /* Kind_Oper */
                  "",                          /* Numb_Document */
                  "Списание балансовой стоимости",
                  null,
                  null,
                  GetFIRoleByPortfolio( GroupID )
                 ) 
             )
               return 1;
           end;
        end;

        if( ОтдельныйУчетУплНКД() )
           if( NKDRest != 0 )
              if(not ПроводкаПоКатегориямУчета( FD, 
                     "Реализация, ц/б", "Уплаченный НКД",/* КатегДеб , КатегКредит*/
                     dat,                         /* Дата */
                     1,                           /* Глава */
                     FD.FaceFIID(),               /* Валюта */
                     NKDRest,                     /* Сумма  Счист*/
                     Doc,                         /* Документ */
                     INPCARRY,                    /* Result_Carry */
                     " 1",                        /* Kind_Oper */
                     "",                          /* Numb_Document */
                     "Списание НКД",
                     null,
                     null,
                     GetFIRoleByPortfolio( GroupID)
                    ) 
                )
                  return 1;
              end;
           end;
        end;

        if( BonusRest != 0 )
           if(not ПроводкаПоКатегориямУчета( FD, 
                  "Реализация, ц/б", "Премия, ц/б",/* КатегДеб , КатегКредит*/
                  dat,                         /* Дата */
                  1,                           /* Глава */
                  FD.FaceFIID(),               /* Валюта */
                  BonusRest,                   /* Сумма  Счист*/
                  Doc,                         /* Документ */
                  INPCARRY,                    /* Result_Carry */
                  " 1",                        /* Kind_Oper */
                  "",                          /* Numb_Document */
                  "Списание премии",
                  null,
                  null,
                  GetFIRoleByPortfolioBonus(GroupID)
                 ) 
             )
              return 1;
           end;
        end;

        //списание начисленного ПДД
        if( FD.IsExistAccount( "Начисл.ПДД, ц/б", null, true, GetFIRoleByPortfolio( GroupID, false, true ), CredCatAcc, null, dat, FD.FaceFIID() ) )
           CarSum = abs(money(GetRestAccount(CredCatAcc, dat)));

           if( CarSum != 0 )
              if(not ПроводкаПоКатегориямУчета( FD, 
                     "Реализация, ц/б", CredCatAcc,/* КатегДеб , КатегКредит*/
                     dat,                         /* Дата */
                     1,                           /* Глава */
                     FD.FaceFIID(),               /* Валюта */
                     CarSum,                      /* Сумма  ДДначисл*/
                     Doc,                         /* Документ */
                     INPCARRY,                    /* Result_Carry */
                     " 1",                        /* Kind_Oper */
                     "",                          /* Numb_Document */
                     "Списание процентного дохода",
                     null,
                     null,
                     GetFIRoleByPortfolio( GroupID, false, true )
                    ) 
                )
                  return 1;
              end;
           end;
        end;

        if( FD.IsExistAccount( "Начисл.ПДД, ц/б", null, true, GetFIRoleByPortfolio( GroupID, true ), CredCatAcc, null, dat, FD.FaceFIID() ) )
           CarSum = abs(money(GetRestAccount(CredCatAcc, dat)));

           if( CarSum != 0 )
              if(not ПроводкаПоКатегориямУчета( FD, 
                     "Реализация, ц/б", CredCatAcc,/* КатегДеб , КатегКредит*/
                     dat,                         /* Дата */
                     1,                           /* Глава */
                     FD.FaceFIID(),               /* Валюта */
                     CarSum,                      /* Сумма  ДДначисл*/
                     Doc,                         /* Документ */
                     INPCARRY,                    /* Result_Carry */
                     " 1",                        /* Kind_Oper */
                     "",                          /* Numb_Document */
                     "Списание дисконтного дохода",
                     null,
                     null,
                     GetFIRoleByPortfolio( GroupID, true )
                    ) 
                )
                  return 1;
              end;
           end;
        end;

        if( GroupID == KINDPORT_SALE ) /*продажа лота из ППР*/ 
           if( ВыполнитьСписаниеПереоценки( FD, GroupID, OverAmountBuy, dat ) == false) 
              return 1;
           end;
        elif( GroupID == KINDPORT_TRADE ) /*продажа лота из ТП*/ 
           if( ВыполнитьСписаниеПереоценкиТП( FD, GroupID, OverAmountBuy, dat ) == false) 
              return 1;
           end;
        end;

        if( СписаниеРезерваПоЦБ( FD, dat, Doc, GroupID, ReservAmountBuy, IncomeReservBuy ) == false )
           return 1;
        end;

        //Перенос остатка счета реализации на транзитный счет
        if (comm.BeginDate != comm.EndDate)
           if( FD.IsExistAccount( "Реализация, ц/б", null, true, null, CredCatAcc, null, dat, NATCUR ) )
              CarSum = abs(money(GetRestAccount(CredCatAcc, dat)));

              if( CarSum != 0 )
                 if(not ПроводкаПоКатегориямУчета( FD, 
                        "+Расчеты, ГО", CredCatAcc,/* КатегДеб , КатегКредит*/
                        dat,                         /* Дата */
                        1,                           /* Глава */
                        NATCUR,                      /* Валюта */
                        CarSum,                      /* Сумма  ДДначисл*/
                        Doc,                         /* Документ */
                        INPCARRY,                    /* Result_Carry */
                        " 1",                        /* Kind_Oper */
                        "",                          /* Numb_Document */
                        "Перенос остатка счета реализации на транзитный счет",
                        null,
                        null,
                        null,
                        FD.FaceFIID(), NATCUR 
                       ) 
                   )
                     return 1;
                 end;
              end;
           end;
        end;
     end;
  end;

  if( SetOverRegistrValue(FD, FD.fininstr.rec.FIID, dat) != 0 )
     return 1;
  end;

  return 0;
end;

private macro CheckAccount(Account, ADate)
  var Query, DataSet;

  /*Проверим что по этому счету не было проводок за более поздние даты.*/
  Query = RSDCommand(" SELECT t_Chapter "
        + "   FROM dacctrn_dbt "
        + "  WHERE t_Result_Carry != ? " /*отражение проводки перенесенной в архив, нам не нужно*/
        + "    and t_State = 1 "/*фактическая проводка*/
        + "    and (t_Account_Payer    = ? OR "
        + "         t_Account_Receiver = ? )"
        + "    and t_Date_Carry > ? " );

  Query.addParam( "", RSDBP_IN, ARHCARRY );
  Query.addParam( "", RSDBP_IN, Account );
  Query.addParam( "", RSDBP_IN, Account );
  Query.addParam( "", RSDBP_IN, ADate );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if (DataSet.MoveNext())
     MsgBox( "По счету " + Account + " есть проводки за более поздние даты" );
     return 1;
  end;
  return 0;
end;

PRIVATE MACRO GetTemplNumClient(CatCode:string, CatID:INTEGER, Number:INTEGER, TemplNum:@INTEGER)
  var query, DataSet;
  var Select;

  /* У клиента 2 цифра в балансовом счете зависит от вида обслуживания. */
  /* Для этого в sql запросе есть условие t1.t_value1 = t2.t_value1. */
  query =   " SELECT t2.t_Number TemplNum"
          + " FROM dmctempl_dbt t1, dmctempl_dbt t2 "
          + " WHERE t1.t_CatID = ? "
          + "   and t1.t_Number = ? "
          + "   and t1.t_value1 = t2.t_value1 "
          + "   and t2.t_CatID = (SELECT t_ID FROM dmccateg_dbt WHERE t_Code = ?) ";

  Select = RSDCommand( query );

  Select.addParam( "", RSDBP_IN, CatID ); 
  Select.addParam( "", RSDBP_IN, Number ); 
  Select.addParam( "", RSDBP_IN, CatCode ); 

  Select.execute();

  DataSet = TRsbDataSet(Select);

  if(DataSet.MoveNext())
    TemplNum = DataSet.TemplNum;
    return TRUE;
  end;

  return FALSE;
END;

private macro ОткрытьОбщесистСчет(CatCode:string,ActionDate:Date,AccBuf:Variant,FIID:Integer,AccdocID:integer)
   var BackOutAccount = false, ChangeOpenDate  = false;
   var    RealOpenMode, FindAccount = "";
   var TemplNum;

   file   mcaccdoc( mcaccdoc );

   mcaccdoc.ID=AccdocID;

   if(GetGE( mcaccdoc ) and (mcaccdoc.ID == AccdocID))

      mcaccdoc.FIID=FIID;

      MC_GetAccountOpenParms( CatCode, @BackOutAccount, @ChangeOpenDate );
      /* Добавила поиск номера шаблона для КУ "ЦБ, Расч. с клиентом, ВУ", */
      /* т.к. номера шаблона (TemplNum) счета mcaccdoc и КУ "ЦБ, Расч. с клиентом, ВУ" не совпадают.*/
      /* У банка такой проблемы нет.*/
      if (CatCode == "ЦБ, Расч. с клиентом, ВУ")
         if (not GetTemplNumClient(CatCode, mcaccdoc.CatID, mcaccdoc.TemplNum, @TemplNum))
            MsgBox("Счет категории \"" + CatCode + "\" неопределен или недоступен в данной операции");
            return false;
         end;
      else
         TemplNum = mcaccdoc.TemplNum;
      end;

      FindAccount = MC_FindAndOpenCommonAcc( CatCode,
                                        ActionDate,
                                        MC_OPENACC_CREATE,
                                        TemplNum,
                                        mcaccdoc,
                                        mcaccdoc.PeriodID,
                                        FIID,
                                        NULL,
                                        mcaccdoc.DepartmentID,
                                        AccBuf,
                                        RealOpenMode,
                                        null,
                                        MC_PAIRACCMODE_MANUAL,
                                        BackOutAccount,
                                        ChangeOpenDate 
                                      );

   end;

   if ( FindAccount=="" )
      MsgBox("Счет категории \"" + CatCode + "\" неопределен или недоступен в данной операции");
      return false;
   end;

   return true;
end;

private macro CorrectAccountName(category: String, accBuf: Variant)
  var newAccountName: String = "";

  if (Index(accBuf.nameAccount, category) > 0)
    if (category == "ЦБ Клиента, ВУ")
      newAccountName = StrSubst(accBuf.nameAccount, category, "ЦБ-3 Места хранения ЦБ клиентов, ВУ");
    elif (category == "ЦБ, Расч. с клиентом, ВУ")
      newAccountName = StrSubst(accBuf.nameAccount, category, "ЦБ-3 Клиента, ВУ");
    end;
  end;

  if (StrLen(newAccountName) > 0)
    var cmd = RSDCommand("UPDATE dAccount_dbt SET t_nameAccount = :accname WHERE t_account = :acc");

    cmd.AddParam("accname", RSDBP_IN, newAccountName);
    cmd.AddParam("acc",     RSDBP_IN, accBuf.account);
    cmd.Execute();
  end;
end;

private macro ПроводкиПоВУ(comm, Doc)

    record acc( account );
    record acc2( account );
    record acc3( account );

    var cmd, rset, rest;

    var n = 0, cnt = 0, sql = "", query1 = "", dt;

    var FD = SPFirstDocDLCOMM(comm);

    if( not ВестиВнутреннийУчет() )
        return true;
    end;

    sql =    "SELECT acc.t_AccountID, " +
             "      accdoc.t_ID,        accdoc.t_Chapter,           accdoc.t_Currency, " +
             "      accdoc.t_Account,   categ.T_NUMBER numCateg,    categ.T_Code codeCateg, " +
             "      accdoc.t_FIID,      accdoc.t_templNum, accdoc.t_Owner, accdoc.t_ClientContrID " +

             "FROM dmcaccdoc_dbt accdoc, dMCTPLELM_dbt tpelm, DMCCATEG_DBT categ, DACCOUNT_DBT acc " +

             "WHERE " +
             "      accdoc.t_IsCommon  = 'X' " +
             "      AND accdoc.t_Chapter  = 22  " + //and accdoc.t_clientcontrid =  725229" +
             "      AND accdoc.t_FIID      = ? " +
             "      AND accdoc.t_DepartmentID = ? " +
             "      AND accdoc.t_Account  != chr(1) " +

             "      AND tpelm.t_CatID      = accdoc.t_CatID " +
             "      AND tpelm.t_IDElement  = 11 " +

             "      AND accdoc.t_CatID = categ.T_ID " +
             "      AND categ.T_NUMBER in (550, 560)" +

             "      AND acc.t_Account = accdoc.t_Account " + 
             "      AND acc.t_Chapter = accdoc.t_Chapter " + 
             "      AND acc.t_Code_Currency = accdoc.t_Currency " +

             "      AND NOT EXISTS(SELECT 1 " + 
             "                       FROM DSCUNIAVRSTOBJ_DBT o " + 
             "                      WHERE o.t_ID_Operation = ? " + 
             "                        AND o.t_IsWrt = 1 " + 
             "                        AND o.t_ObjKind = " + UniAvrObj.OBJKIND_ACCID +
             "                        AND o.t_ObjID = acc.t_AccountID " +
             "                        AND o.t_FIID = accdoc.t_FIID " +
             "                    ) "+
             "      AND (categ.T_NUMBER in (550) "+
             "          OR (categ.T_NUMBER in (560)  and EXISTS (  "+
             "                    SELECT 1  "+
             "                      FROM dpmwrtsum_dbt s, dscdlpmwr_dbt pmwr  "+
             "                     WHERE     pmwr.T_DEALKIND = ?  "+
             "                           AND pmwr.T_DEALID = ?  "+
             "                           AND pmwr.T_SUMID = s.T_SUMID   "+
             "                           AND s.t_party = accdoc.t_Owner  "+
             "                           AND s.t_contract = accdoc.t_ClientContrID  "+
             "          )  )  )  "+
             " ORDER BY categ.T_NUMBER ";

    var query = DL_RSDCommand(sql);
    query.addParam(comm.FIID );
    query.addParam(comm.Division );
    query.addParam(UniAvrObj.GetID_Operation());
    query.addParam(comm.DocKind );
    query.addParam(comm.DocumentID );
    cnt = query.GetCount();

    if((UniAvrObj.GetLimit() > 0) and (cnt > (UniAvrObj.GetLimit() - UniAvrObj.GetCount())))
      cnt = (UniAvrObj.GetLimit() - UniAvrObj.GetCount());
    end;

    if(cnt > 0)
      InitProgress(cnt, "Формирование проводок ВУ", "Формирование проводок ВУ");

      rset = query.execute();

      while (rset.MoveNext())
          if(UniAvrObj.NeedBreak())
            UniAvrObj.SetRecurseStep();
            break;
          else
            UniAvrObj.AddRec(UniAvrObj.OBJKIND_ACCID, rset.AccountID, rset.FIID);
          end;

          if( not GetAccount( rset.Chapter, rset.Currency, rset.Account, acc, true ) )
             MsgBox( "Не найден лицевой счет " + rset.Account );
             return false;
          end;

          if( acc.Open_Close == "З" )
             MsgBox( "Лицевой счет " + acc.Account + " уже закрыт" );
             return false;
          end;


          if ( rset.numCateg == 550 ) //ЦБ Банка, ВУ
             /*Проверим что по этому счету не было проводок за более поздние даты.*/
             if (CheckAccount(rset.Account, comm.CommDate) == 1)
                return false;
             end;

             Rest = GetRestAccount( acc, comm.CommDate );

             if(Rest!=0)

                if(not ОткрытьОбщесистСчет("ЦБ, Прч. счета банка, ВУ",comm.CommDate,acc2,comm.FIID,rset.ID) )
                    return false;
                end;

                if( not ПроводкаПоКатегориямВнутреннегоУчета( 79,
                           FD,
                           Doc,                 /* Документ */
                           comm.CommDate,       /* Дата */
                           acc2.CODE_CURRENCY,  /* Валюта */
                           abs(Rest),           /* Сумма */
                           null, null, null, null,
                           acc2, acc
                        )
                    )
                    return false;
                end;
             else
                if( MC_FindAndCloseAccountByAccDoc( rset.ID, comm.CommDate, MC_ACC_CLOSE_ALL ) )
                    return false;
                end;            
             end;
          elif ( rset.numCateg == 560 ) //ЦБ Клиента, ВУ
              /*bpv проводки только по тем клиентам, лоты которых отобраны в операцию*/
             query1 =
                  RSDCommand(
                    "SELECT S.T_PARTY, S.T_CONTRACT \n" +
                    "  FROM dpmwrtsum_dbt s, dscdlpmwr_dbt pmwr \n" +
                    " WHERE     pmwr.T_DEALKIND = ? \n" +
                    "       AND pmwr.T_DEALID = ? \n" +
                    "       AND pmwr.T_SUMID = s.T_SUMID \n" +
                    "       AND s.t_party = ? \n" +
                    "       AND s.t_contract = ? \n" 
                 );

             query1.addParam( "", RSDBP_IN, comm.DocKind );
             query1.addParam( "", RSDBP_IN, comm.DocumentID );
             query1.addParam( "", RSDBP_IN, rset.Owner );
             query1.addParam( "", RSDBP_IN, rset.ClientContrID );
             query1.execute();

             dt = TRsbDataSet(query1);
             if (Dt.movenext())

                /*Проверим что по этому счету не было проводок за более поздние даты.*/
                if (CheckAccount(rset.Account, comm.CommDate) == 1)
                   return false;
                end;

                Rest = GetRestAccount( acc, comm.CommDate );

                if(Rest!=0)

                   if(not ОткрытьОбщесистСчет("ЦБ, Расч. с клиентом, ВУ",comm.CommDate,acc2,comm.FIID,rset.ID) )
                       return false;
                   end;
                   
                   CorrectAccountName("ЦБ, Расч. с клиентом, ВУ", acc2);

                   if( not ПроводкаПоКатегориямВнутреннегоУчета( 79,
                              FD,
                              Doc,                 /* Документ */
                              comm.CommDate,       /* Дата */
                              acc2.CODE_CURRENCY,  /* Валюта */
                              abs(Rest),           /* Сумма */
                              null, null, null, null,
                              acc2, acc
                           )
                       )
                       return false;
                   end;
                else
                   if( MC_FindAndCloseAccountByAccDoc( rset.ID, comm.CommDate, MC_ACC_CLOSE_ALL ) )
                       return false;
                   end;            
                end;

             end;
          end;
          UseProgress(n=n+1);

      end;
      RemProgress;
    end;

    return true;

end;

macro ВыполнитьПроводкиСписанияКДУ(comm, Doc)
  var stat = 0;
  
  var QrAcc = TRecHandler("account.dbt");
  var CorrAcc = TRecHandler("account.dbt");
  var FD;

  var query = 
    " SELECT scqr.*, acc.t_AccountID, "
   +"        ABS (RSI_RSB_ACCOUNT.RestAC (acc.t_account, "
   +"                                     ACC.T_CODE_CURRENCY, "
   +"                                     ?, "
   +"                                     ACC.T_CHAPTER, "
   +"                                     0)) "
   +"           AS t_AccRest, "
   +"        ACC.T_CHAPTER "
   +"   FROM DSCQRACC_DBT scqr, DACCOUNT_DBT acc "
   +"  WHERE ACC.T_ACCOUNTID = SCQR.T_ACCOUNTID AND t_fiid = ? "
   +"    AND NOT EXISTS(SELECT 1 " 
   +"                     FROM DSCUNIAVRSTOBJ_DBT o "
   +"                    WHERE o.t_ID_Operation = ? "
   +"                      AND o.t_IsWrt = 1 "
   +"                      AND o.t_ObjKind = "+UniAvrObj.OBJKIND_ACCID
   +"                      AND o.t_ObjID = acc.t_AccountID"
   +"                      AND o.t_FIID = scqr.t_FIID"
   +"                  ) ";

  var cmd = DL_RSDCommand(query);
  cmd.addParam(comm.CommDate);
  cmd.addParam(comm.Fiid);
  cmd.addParam(UniAvrObj.GetID_Operation());

  var DataSet = cmd.execute();

  while (DataSet.MoveNext())
    if (DataSet.AccRest > 0)
      if(UniAvrObj.NeedBreak())
        UniAvrObj.SetRecurseStep();
        break;
      else
        UniAvrObj.AddRec(UniAvrObj.OBJKIND_ACCID, DataSet.AccountID, DataSet.FIID);
      end;
      
      FD = SC_QRAccFD(DataSet.QRID);
      if(not ПроводкаПоКатегориямУчета( FD, 
            "КорСчет КДУ", "ЛицевойСчет КДУ",/* КатегДеб , КатегКредит*/
            comm.CommDate,               /* Дата */
            DataSet.Chapter,             /* Глава */
            comm.Fiid,                   /* Валюта */
            DataSet.AccRest,             /* Сумма */
            Doc,                         /* Документ */
            INPCARRY,                    /* Result_Carry */
            " 1",                        /* Kind_Oper */
            string(DataSet.QRID),        /* Numb_Document */
            "Списание остатка лицевого счета КДУ"
            ) 
        )
        return 1;
      end;
    end;
  end;

  return stat;
end;


macro ExecuteStep( Doc, FDoc, DocKind, ID_Operation, ID_Step )

  record comm( dl_comm );
  SetBuff( comm, FDoc );

  UniAvrObj = UniAvrStObj(ID_Operation, ID_Step, true);

  if (ВыполнитьПроводкиДляНепоставленных(comm, Doc) == 1)
     return 1;
  end;

  if((comm.Flag3 == SET_CHAR) and (UniAvrObj.NeedBreak() == false))

     if(comm.OperSubKind != SC_GLOBAL_OPER_UNIAVR /*Объединение*/)
       if (ВыполнитьПроводкиСписания(comm, Doc) == 1)
          return 1;
       end;
     end;
  end;

  if( (UniAvrObj.NeedBreak() == false) and (not ПроводкиПоВУ(comm, Doc)) )
     RemProgress; //tmp
     return 1;
  end;

  if ((GetDepoMode() == 2) and (UniAvrObj.NeedBreak() == false)) //Работаем с КДУ
    if (ВыполнитьПроводкиСписанияКДУ(comm, Doc) != 0)
       return 1;
    end;
  end;

  if(UniAvrObj.Save() != 0)
    return 1;
  end;

  var StatusKind = SP_OPERSTATUS_GLOBALOPER_SO_WRTIN;

  if(UniAvrObj.NeedRecurseStep()) 
    StatusKind = SP_OPERSTATUS_GLOBALOPER_SO_CARRYOUT;
  end;

  if( not InsertOprStatus( SP_OPERSTATUS_GLOBALOPER_KIND_SO, StatusKind) ) 
     msgbox( "Ошибка при установке статуса операции" );
     return 1;
  end;

  return 0;
end; 

/* Макрос постобработки */
MACRO PostStep (CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,          /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

  UniAvrObj = UniAvrStObj(ID_Operation, ID_Step, true);
  
  if( errTrn OR (CommitOrRollback == 2) ) /* Произошла ошибка или происходит откат */ 
    UniAvrObj.DeleteStepDocs();
  end;
  
  return 0;

END;
