/**                                                                                                             
 @file scsrvrepfun.mac                                                                                           
 @brief Сервисная операция отправки отчета брокера. Вспомогательные функции.

 #menu 
  - БО ЦБ\Сервисные операции\Операции отправки отчета брокера                                                                        
                                                                                                                
 #tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:GUI 
 - code_type:API                                                                                                                                                                                               
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.01.17 |Жиц М.В.       |BIQ-14887           |Добавление возможности открытия файла с низким приоритетом 
 |?          |?              |?                   |Создание макроса                                                                                                                                                
*/ 

import BankInter;
import "or_tools.mac";
import "dldlngfun.mac", "spRepFun.mac";
import "u_common_email_utils.mac";
import "file_archive.mac"; //shev 29.01.2020 для архивации
import "dlutils.mac";

const Arhive = false; //shev 29.01.2020 чтобы отключить архивацию поставить false

const RemoteRun = True;//Режим запуска

const BROKER_REPORT_EMAIL_GROUP = 40;

MACRO UseExcelApp( IsRemote )
  if( ValType( IsRemote ) == V_UNDEF )
    IsRemote = false;
  end;

  if( IsRemote )
    return not IsRunFromWine(false);
  else
    return not IsRunFromWine(true);
  end;
END;

var SrvRepErrArr = TArray(); //массив ошибок

class CSC_SrvRepCurrData()
  var m_ClientID;        
  var m_ClientCode;      
  var m_ClientShortName; 


  macro Clear()
    m_ClientID        = -1;
    m_ClientCode      = "";
    m_ClientShortName = "";
  end;
  
  macro SetClient(ClientID:integer)
    if(ClientID == -1)
      Clear();
    elif(ClientID != m_ClientID)
      m_ClientID        = ClientID;
      m_ClientCode      = ПолучитьКодСубъекта(m_ClientID, PTCK_CONTR);
      m_ClientShortName = ПолучитьКороткоеИмяСубъекта(m_ClientID);
    end;
  end;

  macro GetClientID():INTEGER
    return m_ClientID;
  end;

  macro GetClientCode():STRING
    return m_ClientCode;
  end;

  macro GetClientShortName():STRING
    return m_ClientShortName;
  end;

  Clear();
end;

var SrvRepCurrData = CSC_SrvRepCurrData();

/*класс с данными для списка ошибок в протоколе*/
class CSC_SrvRepErr(_ErrStr:string)
  var ClientID        = SrvRepCurrData.GetClientID();
  var ClientCode      = SrvRepCurrData.GetClientCode;
  var ClientShortName = SrvRepCurrData.GetClientShortName;
  var ErrStr          = _ErrStr;
end;

MACRO GetDefaultSigner(RetVal:@integer, err:@string)
  var RegistryPath = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\AUTOMATION\\DEFAULT_SIGNER";
  var stat = 0, dt;
  GetRegistryValue(RegistryPath, V_INTEGER, RetVal, stat);
  if(stat) 
     RetVal = -1;
     err = "Не задана настройка в реестре банка:|" + RegistryPath;  
  else
     Dt = TRsbDataSet("select t_partyid, t_userClosed from dperson_dbt where t_oper = " + RetVal);
     if (Dt.movenext())
        if (Dt.Partyid != 1)
            if(Dt.UserClosed == SET_CHAR)
               err = "Операционист " + RetVal + " уволен";  
               RetVal = -1; 
               stat = 1;
            else
            RetVal = Dt.PartyID;
            end;
        else
           err = "Операционист " + RetVal + " не является сотрудником банка";  
           RetVal = -1; 
           stat = 1;
        end;
     else
        err = "Операционист " + RetVal + " не найден. Проверьте настройку " + RegistryPath;  
        RetVal = -1;
        stat = 1;
     end;
  end;
  return stat;
END;

//класс с данными для конвейера
CLASS SrvRepCnvData(_ID, _Signer, _IsSaveErrors)
  var ID = _ID;
  var Signer = _Signer;
  var IsSaveErrors = _IsSaveErrors;
END;

MACRO ErrorFromCnvpack( cnv )
  var Cmd, Query, DataSet, err = 0;
  Query = " select t_Error, t_ErrMsg " +
          "   from dcnvpack_dbt " +
          "  where t_ExecID = ? "  + 
          "  group by t_Error, t_ErrMsg" ;
 
  Cmd = DL_RSDCommand( Query );      
  Cmd.AddParam(cnv.ExecID);
  
  DataSet = Cmd.Execute();

  while( DataSet.MoveNext() )
    if( DataSet.Error != 0 )      
      err = err + 1;    

      if((DataSet.ErrMsg != "") OR (DataSet.Error != 1))
      SrvRepErrArr[SrvRepErrArr.size] = CSC_SrvRepErr(IIF( DataSet.ErrMsg, DataSet.ErrMsg, "" ));
      end;
    end; 
  end; 
  return err;
END;

macro ThisIsStandAlone(
):Bool
  return true;
end;

macro TryToAxAppQuit(AxObj)
  AxObj.Quit();
  return 0;
OnError(er)
  return 0;
end;

macro GetLargeErrorMessage(ObjError)
    var errCode, errMes, count, i;

    if (IsEqClass ("TRslError",ObjError))
        errMes = ObjError.Message; 
        errCode = ObjError.Code; 
    else
        errMes = ObjError.getMessage(); 
        errCode = ObjError.getErrorCode(); 
    end;

    var Cause = ObjError.err;

    if (IsEqClass ("TRsComErr", Cause))
        count = Cause.Count;
        i = 0;
        while (i < count)
            errMes = errMes + ";"+ Cause.Message (i) + " (Code = " + Cause.Code (i) +
                                                      ", Level = " + Cause.Level (i) + ")" ;
            i = i + 1
        end;
    end;

    if(IsEqClass("TDbError", Cause))
      errMes = errMes + ";" + Cause.Stat + "-" + Cause.Oper + "-" + Cause.Table + "-" + Cause.Message;
    end;

    if(isEqClass("TRsdError", Cause))
        i = 0;
        while(i < Cause.environment.ErrorCount)
            errMes = errMes + ";" + Cause.environment.Error(i).Descr;
            i = i + 1;
        end;
    end;

    if(errCode != 17)
      var retErrMsg =
          String( "Error!", 
                " ;Code: ",  errCode,
                " ;Message: ",  errMes,
                " ;Module: ",  ObjError.Module,
                " ;Line: ",  ObjError.Line,
                " ;AxCode: ",  ObjError.AxCode,
                " ;Err: ",  Cause,
                " ;AxMes: ",  ObjError.AxMes); 
      return retErrMsg;
    else 
        return "Прерывание пользователя!"; // прерывание пользователя
    end;
end;

MACRO SC_SrvObjExistsFormat(ServDocID:integer, ClientID:integer, Format:integer)
  var cmd, DataSet;
  var exists = false;

  cmd = DL_RSDCommand(  " select 1"
                      + "   from dscsrvrepobj_dbt "
                      + "  where t_ServDocID = ? "
                      + "    and t_ClientID = ? "
                      + "    and t_Format = ?"
                     );
  cmd.AddParam(ServDocID);
  cmd.AddParam(ClientID);
  cmd.AddParam(Format);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    exists = true;
  end;

  return exists;
END;


PRIVATE MACRO GetClientEmailByContactType(ClientID:integer, ContactType:integer)
  var cmd, DataSet;
  var email = "";

  cmd = DL_RSDCommand(    " SELECT c.t_Value as email"
                        + "   FROM dcontact_dbt c "
                        + "  WHERE c.t_PartyID = ? "
                        + "    AND c.t_ContactKind = " + CNTK_EMAIL
                        + "    AND c.t_ContactType = ? " 
                        //+ "    AND c.t_IsMain = 'X' "
                        //+ "    AND ROWNUM = 1 "
                        + "  order by c.t_IsMain desc " // Golovkin 
                       );
  cmd.addParam(ClientID);
  cmd.addParam(ContactType);

  DataSet = cmd.execute();
  if(DataSet.moveNext())
    email = DataSet.email;
  end;

  return email;
END;

//Определить email клиента для отправки отчета
MACRO SC_GetClientRepEmail(ClientID:integer, ContractID)
  var cmd, DataSet;
  var email = "", contrnumber;

  if (ValType(ContractID) != V_UNDEF)
     //DataSet = TRsbDataSet(" select t_email from ddlcontr_dbt where t_dlcontrid = (select t_dlcontrid from ddlcontrmp_dbt where t_sfcontrid = " + Contractid + ")"); 
     DataSet = TRsbDataSet(" select t_email from ddlcontr_dbt where t_sfcontrid =  " + Contractid ); 
     if (DataSet.movenext())
        email = DataSet.Email;
     end;
  end;

  if(email == "")
     email = GetClientEmailByContactType(ClientID, CNTT_TOSENDREPORTS);
     if(email == "")
        email = GetClientEmailByContactType(ClientID, 1005);
        if(email == "")
           email = GetClientEmailByContactType(ClientID, 0);
        end;
     end;
  end;


  return email;
END;

CLASS CBrokerRepRSHBData(_NameFile:string, _Format:integer, _CreateDate:date, _CreateTime:time, _DlContrID:integer)
  var NameFile   = _NameFile;      //имя сформированного файла
  var Format     = _Format;        //формат файла
  var CreateDate = _CreateDate;    //дата формирвоания
  var CreateTime = _CreateTime;    //время формирования
  var DlCOntrID  = _DlContrID;     //ДБО

  macro Write(srvRepId, ClientId)
    var srvrepobj = TBFile("scsrvrepobj.dbt", "W");
    srvrepobj.rec.ServDocID  = srvRepId; 
    srvrepobj.rec.ClientID   = ClientId;
    srvrepobj.rec.CreateDate = CreateDate;
    srvrepobj.rec.CreateTime = CreateTime;
    srvrepobj.rec.FileName   = NameFile;
    srvrepobj.rec.Format     = Format;
    if(DlContrID > 0)
      var dlcontr = TBFile("dlcontr.dbt");
      dlcontr.rec.DlContrID = DlContrID;
      if(dlcontr.GetEQ())
        srvrepobj.rec.ContractID = dlcontr.rec.SfContrID;
      end;
    end;
    srvrepobj.rec.email      = SC_GetClientRepEmail(ClientId, dlcontr.rec.SfContrID);
    
    return srvrepobj.Insert();
  end;
END;

MACRO SC_ConvertToFormat(ExcelNameFile, FullResultFileName, Format, IsCnv, CreateOnTerm)
  var WinRepFormat = 0;
  var err = 0;

  if(Format == SC_SRVREPFORMAT_PDF)
    WinRepFormat = WINREP_FORMAT_PDF;
  elif(Format == SC_SRVREPFORMAT_EXCEL_XLSX)
    WinRepFormat = WINREP_FORMAT_XLSX;
  end;

  if( CreateOnTerm )
    ExcelNameFile = "$" + ExcelNameFile;
    FullResultFileName = "$" + FullResultFileName;
  end;

  if( WR_ConvertToFormatLibreOffice( ExcelNameFile, FullResultFileName, WinRepFormat, CreateOnTerm,  false ) == false)
    msgbox("Ошибка при сохранении файла отчета");
    err = 1;
  end;


  return err;
END;


MACRO SC_PrintExcelFile(ExcelNameFile, Copies, IsCnv)
  var err = 0;
  
  if(ValType(IsCnv) == V_UNDEF)
    IsCnv = false;
  end;

  if(IsCnv == true)
    ReplaceMacro("isStandAlone", "ThisIsStandAlone"); //!!!Обязательно
  end;
  
  var obj = CDAOMSExcel(ExcelNameFile, true);

  obj.PrintOut(Copies);
  obj = NULL;

  if(IsCnv == true)
    ReplaceMacro("isStandAlone", NULL);
  end;

  return err;
END;

private macro GetAbsolutePathForRep(FilePath:string, isRemote:bool)
  FilePath = StrSubst(FilePath,"/","\\");
  var Path = "", pLen = strlen(FilePath);

  if((pLen > 4) and (substr(FilePath, 1, 2) == "\\\\"))
    Path = FilePath;
  else
    if(isRemote)
      if( substr( FilePath, 1, 2 ) == ".." )
        FilePath = substr(FilePath, 3 );
      end;

      if( substr( FilePath, 1, 1 ) == "\\" )
        FilePath = substr(FilePath, 2 );
      end;

      Path = GetCurDir(true) + "\\" + FilePath; 
    else
      Path = PrcPath(FilePath);

      if( substr( FilePath, 1, 1 ) == "\\" )
        Path = GetCurDir(true) + Path;
      end;
    end;
  end;

  if (substr(Path, pLen, 1) == "\\")
    Path = substr(Path, 1, pLen - 1);
  end;

  return DLM_PathCanonicalize(Path);
end;


macro CheckPathEqual(Path1, Path2)
  var isRemoteFrom = SubStr(Path1,1,1) == "$";
  var isRemoteTo = SubStr(Path2,1,1) == "$";
  return ((isRemoteFrom == isRemoteTo) and (
    StrLwr(GetAbsolutePathForRep(IIF(isRemoteFrom,SubStr(Path1,2),Path1), isRemoteFrom)) == 
    StrLwr(GetAbsolutePathForRep(IIF(isRemoteTo,SubStr(Path2,2),Path2), isRemoteTo))
  ));
end;

//Скопировать файл из одной директории в другую
MACRO SC_CopyFile(FromDir, ToDir, FName, New_FName)
  var err = 0;
  
  if(ExistDir(ToDir) != 0)
    CreateAllDirs(ToDir);
  end;

  if(not err)
    if((ValType(New_FName) == V_UNDEF) or (New_FName == ""))
      New_FName = FName;
    end;

    var fromPath = PathCombine(FromDir,FName);
    var toPath =  PathCombine(ToDir,New_FName);
    if (CheckPathEqual(fromPath, toPath))
      //если пути и местонахождения - одинаковы, то ничего не делаем (CopyFile при копировании в самого себя вовращает ошибку)
      return 0;
    else
      if(not CopyFile(fromPath, toPath))
        msgbox("Ошибка при копировании файла "+FromDir+FName+" в " + ToDir+New_FName);
        err = 1;
      end;
    end;
  end;

  return err;
END;

//Переместить файл из одной директории в другую
MACRO SC_MoveFile(FromDir, ToDir, FName, New_FName)
  var err = 0;
  
  err = SC_CopyFile(FromDir, ToDir, FName, New_FName);

 if((ValType(New_FName) == V_UNDEF) or (New_FName == ""))
   New_FName = FName;
 end;

  if ((not err) and (not CheckPathEqual(FromDir+FName, ToDir + New_FName)))
    RemoveFile(PathCombine(FromDir,FName));
  end;

  return err;
END;

PRIVATE MACRO GetRepFullPath(isRemote:bool)
  var Path = "";

  if(isRemote)
    var txtPath = GetTxtPathFromReg();

    if( substr( txtPath, 1, 2 ) == ".." )
      txtPath = substr(txtPath, 3 );
    end;

    if( substr( txtPath, 1, 1 ) == "\\" )
      txtPath = substr(txtPath, 2 );
    end;

    Path = GetCurDir(true) + "\\" + txtPath + "\\"; 
  else
    Path = PrcPath(GetTxtPathFromReg()) + "\\";
  end;

  return Path;
END;

MACRO SC_GetFullPath(isRemote:bool)
  return GetRepFullPath(isRemote);
END;

MACRO SC_RunCmd(CmdStr)
  var cmdFName     = SP_GetGUID()+".cmd";
  var cmdFullPath  = GetRepFullPath(false);
  var cmdFullFName = PathCombine(cmdFullPath,cmdFName); //полный путь до исполняемого файла (на СП)
  var SAcmdFullFName = cmdFullFName;      //полный путь до исполняемого файла на СП
  var err = 0;
  var f = TStreamDoc(cmdFullFName, "C");

  f.WriteLine(CmdStr);
  f = NULL;

  if(not isStandAlone())
    //скопируем файл на терминал
    var termDir = GetRepFullPath(true); //полный путь до директории отчетов на терминале

    err = SC_CopyFile(cmdFullPath, "$" + termDir, cmdFName);

    if(err)
      msgbox("Ошибка при копировании исполняемого файла на терминал");
      cmdFullFName = "";
    else
      cmdFullFName = "$" + termDir+cmdFName;
    end;
  end;

  if((cmdFullFName != ""))
    StartProg(cmdFullFName, "", false);
  end;

  RemoveFile(cmdFullFName); //Удаляем исполняемый файл на СП или терминале
  if(not isStandAlone())
    RemoveFile(SAcmdFullFName); //Удаляем исполняемый фалй на СП
  end;

  return err;
END;

/**
 @brief Открытие любого файла на просмотр
 @param[in]  FullPath     директория файла 
 @param[in]  FName        наименование файла 
 @param[in]  IsMinimized  признак вывода файла с низким приоритетом (не разворачивать его сразу на экран)
*/
MACRO SC_ShowFile(FullPath:string, FName:string, IsMinimized:bool)
  var FullFileName   = PathCombine(FullPath,FName);    //полный путь до файла отчета

  if(ValType(IsMinimized) == V_UNDEF)
    IsMinimized = false;
  end;

  if(SubStr(FullFileName,1,1) != "$") //Если указан файл на СП
    if(not isStandAlone())
      //скопируем файл на терминал
      var termDir = PathCombine(GetCurDir(true),substr(NameDirTerm,2)); //полный путь до директории отчетов на терминале

      if(SC_CopyFile(FullPath, "$" + termDir, FName) != 0)
        msgbox("Ошибка при копировании файла отчета на терминал");
        FullFileName = "";
      else
        FullFileName = PathCombine(termDir,FName);
      end;
    end;
  elif(Index(FullFileName, "$") == 1) //Если указан файл на терминале
    FullFileName = substr(FullFileName, 2);
  end;

  if(FullFileName != "")
    if (not IsRunFromWine())
      SC_RunCmd("start"+IIF(IsMinimized," /MIN /LOW","")+" \"\" \""+FullFileName+"\"" );
    else
      StartShellProgram( IIF((not isStandAlone()),"$","") + FullFileName );
    end;
  end;
END;

//Область настроек
macro GetSettGENERATED_REPORTS()
  return GetStringRegValueOrDefValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\GENERATED_REPORTS",GetTxtPathFromReg());
end;

macro GetSettBROKERREP()
  return GetStringRegValueOrDefValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\BROKERREP",GetSettGENERATED_REPORTS());
end;

macro GetSettBRERROR()
  return GetStringRegValueOrDefValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\BROKERREP\\PATHS\\BRERROR","BRERROR");
end;
macro GetSettBROKERREPARCH()
  return GetStringRegValueOrDefValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\BROKERREP\\PATHS\\BROKERREPARCH","BROKERREPARCH");
end;
macro GetSettSENT_REPORTS()
  return GetStringRegValueOrDefValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\BROKERREP\\PATHS\\SENT_REPORTS","SENT_REPORTS");
end;
macro GetSettSIGNED_REPORTS()
  return GetStringRegValueOrDefValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\BROKERREP\\PATHS\\SIGNED_REPORTS","SIGNED_REPORTS");
end;
macro GetSettTERM_BROKERREP()
  return GetStringRegValueOrDefValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\TERM_BROKERREP","TxtFile");
end;
//Конец области настроек

//Область с логикой с настройками

const BROKER_REPORT_FILE_STATUS_GENERATED = 1;
const BROKER_REPORT_FILE_STATUS_SAVED = 2;
const BROKER_REPORT_FILE_STATUS_SIGNED = 3;
const BROKER_REPORT_FILE_STATUS_SENT = 4;
const BROKER_REPORT_FILE_STATUS_ARCH = 5;
const BROKER_REPORT_FILE_STATUS_ERR = 6;

class BrokerRepFileStatusManager(FileName, UserRep, InitStat)
  var m_fileName = FileName;
  var m_status = BROKER_REPORT_FILE_STATUS_GENERATED;
  var m_current_file_path = "";
  var m_isUserRep = false;

  private var m_netpathregex = RsRegex("^\\\\\\\\.+");
  private var m_abspathregex = RsRegex("^\\w:.+");
  private var m_userpathregex = RsRegex("\\w:.+");

  if (ValType(InitStat) == V_INTEGER)
    m_status = InitStat;
  end;

  if (ValType(UserRep) == V_BOOL)
    m_isUserRep = UserRep;
  end;

  private macro GetPathForServRepo(path)
    path = StrSubst(path,"$","");
    var fullPath = "";
    //если путь сетевой то просто туда копируем
    if (m_netpathregex.match(path))
      fullPath = path;
    //если путь абсолютный то преобразовываем путь в корректный
    elif (m_abspathregex.match(path))
      fullPath = DLM_PathCanonicalize(path);
    else
      fullPath = DLM_PathCanonicalize(PathCombine(GetCurDir(false) + "\\",path));
    end;
    return fullPath;
  end;

  private macro GetPathForUserRepo(path)
    path = StrSubst(path,"$","");
    var fullPath = "";
    //если путь сетевой то просто туда копируем
    if (m_netpathregex.match(path))
      fullPath = path;
    //если путь абсолютный то добавляем только символ терминала, и преобразовываем путь в корректный
    elif (m_abspathregex.match(path))
      fullPath = "$" + DLM_PathCanonicalize(path);
    else
      //если путь относительный, то считаем его относительным домашней папки пользователя, которая хранится в WINEHOMEDIR переменных среды
      var homeDir = CallRemoteRsl("system", "GetEnv", "WINEHOMEDIR");
      if (not(m_userpathregex.match(homeDir) and m_userpathregex.first()))
        RunError("Не удалось считать значение домашнего каталога");
      end;
      fullPath = "$" + DLM_PathCanonicalize(PathCombine(m_userpathregex.cap(0) + "\\",path));
    end;
    return fullPath;
  end;

  private macro Get_Path_By_Status(status)
    var retPath = "";
    var rootPath = "";
    if (status == BROKER_REPORT_FILE_STATUS_GENERATED)
      rootPath = GetPathForServRepo(GetSettGENERATED_REPORTS());
    else
      if (m_isUserRep)
        rootPath = GetPathForUserRepo(GetSettTERM_BROKERREP());
      else
        rootPath = GetPathForServRepo(GetSettBROKERREP());
      end;
    end;
    if ((status == BROKER_REPORT_FILE_STATUS_GENERATED) or (status == BROKER_REPORT_FILE_STATUS_SAVED))
      retPath = PathCombine(rootPath, m_fileName);
    elif (status == BROKER_REPORT_FILE_STATUS_SIGNED)
      retPath = PathCombine2(rootPath, GetSettSIGNED_REPORTS(), m_fileName);
    elif (status == BROKER_REPORT_FILE_STATUS_SENT)
      retPath = PathCombine2(rootPath, GetSettSENT_REPORTS(), m_fileName);
    elif (status == BROKER_REPORT_FILE_STATUS_ARCH)
      retPath = PathCombine2(rootPath, GetSettBROKERREPARCH(), m_fileName);
    elif (status == BROKER_REPORT_FILE_STATUS_ERR)
      retPath = PathCombine2(rootPath, GetSettBRERROR(), m_fileName);
    end;
    return retPath;
  end;

  macro Move_File(new_status)
    if (new_status == m_status)
      return;
    end;
    if (not GetFileInfo(m_current_file_path))
      RunError("Ошибка поиска файла", CustomError("Не удалось найти файл по пути \""+m_current_file_path+"\""));
    end;

    var new_path = Get_Path_By_Status(new_status);
    if (Trim(new_path) == "")
      RunError("Каталог к перемещению не определен");
    end;
    if (not GetFileInfo(m_current_file_path))
      RunError("Не найден текущий файл");
    end;

    if (SC_MoveFile(SplitFile(m_current_file_path), SplitFile( new_path ), this.Get_CURRENT_filename_with_ext() ))
      RunError("Ошибка копирования", CustomError("Не удалось переместить файл из \""+m_current_file_path+"\" в \""+new_path+"\""));
    end;

    m_status = new_status;
    m_current_file_path = new_path;
  end;

  macro Get_GENERATED_REPORTS_path()
    var path = Get_Path_By_Status(BROKER_REPORT_FILE_STATUS_GENERATED);
    CreateAllDirs(path);
    return path;
  end;

  macro Get_CURRENT_path()
    return m_current_file_path;
  end;

  macro Get_CURRENT_path_no_file_name()
    var path = Get_CURRENT_path();
    return SplitFile( path);
  end;

  macro Get_CURRENT_filename_with_ext()
    var path = Get_CURRENT_path();
    var fileName, fileExt;
    SplitFile( path, fileName, fileExt );
    return fileName + fileExt;
  end;

  macro Get_CURRENT_filename_with_no_ext()
    var path = Get_CURRENT_path();
    var fileName, fileExt;
    SplitFile( path, fileName, fileExt );
    return fileName;
  end;

  macro Get_CURRENT_fileext()
    var path = Get_CURRENT_path();
    var fileName, fileExt;
    SplitFile( path, fileName, fileExt );
    return fileExt;
  end;

  macro ShowFile()
    SC_ShowFile(Get_CURRENT_path_no_file_name(), Get_CURRENT_filename_with_ext());
  end;

  macro Remove_CURRENT_file()
    RemoveFile(m_current_file_path);
  end;

  macro Init()
    if ((m_isUserRep) and (isStandalone() or DL_IsWebService()))
      RunError("Пользовательские (интерфейсные) отчеты не могут работать в двузвенке или в режими сервиса");
    end;
    m_current_file_path = Get_Path_By_Status(m_status);
    if (StrBrk(m_fileName,"/\\:"))
      var fileName, fileExt;
      SplitFile( m_fileName, fileName, fileExt );
      m_fileName = fileName + fileExt;
    end;
  end;

  Init();
end;

//Конец области с логикой с настройками

//Получить путь к файлу сертификата
PRIVATE MACRO GetCertPath()
  var RegistryPath = "SECUR\\KRIPTOPRO\\CERTIF";
  var Path = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if(stat) 
     msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  end;

  return Path;
END;

//PIN-код для доступа к закрытому ключу 
PRIVATE MACRO GetKeyPINCode()
  var RegistryPath = "SECUR\\KRIPTOPRO\\PIN-CODE";
  var PIN = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, PIN, stat);
  if(stat) 
     msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  end;

  return PIN;
END;

//Cписок адресов для отправки скрытой копии
MACRO GetBrokerBccList()
  var RegistryPath = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\EMAIL_БРОКЕРА_ДЛЯ_РАССЫЛКИ";
  var BrokerBccList:string = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, BrokerBccList, stat);
  if(stat) 
     BrokerBccList = "";      
  end;

  return BrokerBccList;
END;

PRIVATE MACRO GetCountFilesInDir(DirName:string)
  var DirList = TDirList(DirName+"*.*", "F");

  return DirList.Count;
END;

//Подписать все файлы в директории
PRIVATE MACRO SignAllFilesInDir(DirName:string)

  //Сделано так, потому что подписание не используется в РСХБ
  //И переделывать структуру сохранения по каталогам с неработающим подписанием - не вижу смысла
  //Банально протестировать - никак
  RunError("Подписание не реализовано");
  var SANotWritenDir = DirName;
  var SAWritenDir    = "";
  var NotWritenDir   = SANotWritenDir;
  var WritenDir      = SAWritenDir;   
  var termNotWritenDir = "";
  var termWritenDir    = "";
  var err = 0;
  var DirList = TDirList();
  var CountForWrite = GetCountFilesInDir(DirName);
  var CountWriten = 0;

  if(CountForWrite > 0)

    if(not isStandalone())

      termNotWritenDir = "";
      termWritenDir    = "";

      DirList.Copy(DirName+"*.*", "F", "$"+termNotWritenDir);

      NotWritenDir = termNotWritenDir;
      WritenDir    = termWritenDir;
    end;

    var CertPath = GetCertPath();

    if((CertPath != ""))
      var PIN = GetKeyPINCode();

      SC_RunCmd("cppdfutil.exe sign --skip-errors --overwrite-files -i "+NotWritenDir+" -o "+WritenDir+" -c "+CertPath + IIF(PIN != "", " --pin "+PIN, ""));

      CountWriten = GetCountFilesInDir(IIF(not isStandalone(), "$", "")+WritenDir);
      if(CountWriten < CountForWrite)
        err = 1;
      end;
    end;

    DirList.Copy(IIF(not isStandalone(), "$", "")+WritenDir+"*.*", "F", "");

    if(not isStandalone())
      DirList.Remove("$"+termNotWritenDir+"*.*", "F");
      RemoveDir("$"+termNotWritenDir);

      DirList.Remove("$"+termWritenDir+"*.*", "F");
      RemoveDir("$"+termWritenDir);
    end;
         
    DirList.Remove("$"+SAWritenDir+"*.*", "F");
    RemoveDir("$"+SAWritenDir);
  end;

  return err;
END;

PRIVATE MACRO SetSignInfo(RepObjID:integer, SignDate:date, SignTime:time)
  var exec = RSDCommand(  " UPDATE DSCSRVREPOBJ_DBT "
                        + "    SET T_ISSIGN = 'X', "
                        + "        T_SIGNDATE = ?, "
                        + "        T_SIGNTIME = ? "
                        + "  WHERE T_ID = ? ");
  exec.addParam( "", RSDBP_IN, SignDate );
  exec.addParam( "", RSDBP_IN, SignTime );
  exec.addParam( "", RSDBP_IN, RepObjID );
  exec.execute();

  return 0;
OnError(er)
  msgbox(er.module+ " "+er.line+" "+er.message);
  return 1;
END;

//Подписать все отчеты, которые помещены во временный файл
MACRO SC_SignAllRepObjTmp()
  var query, cmd, DataSet;
  var cnt = 0;
  var err = 0;

  //Сделано так, потому что подписание не используется в РСХБ
  //И переделывать структуру сохранения по каталогам с неработающим подписанием - не вижу смысла
  //Банально протестировать - никак
  RunError("Подписание не реализовано");

  var FullPathSignedReports    = "";
  var FullPathGeneratedReports = "";

  cmd = DL_RSDCommand(" select * from dscsrvrepobj_tmp where t_Format = " + SC_SRVREPFORMAT_PDF);

  cnt = cmd.GetCount();
  if(cnt > 0)

    if(ExistDir(FullPathSignedReports) != 0)
      if(MakeDir(FullPathSignedReports) == false)
        msgbox("Ошибка при создании директории " + FullPathSignedReports);
        err = 1;
      end;
    end;
    
    //создаем временную директорию, куда скопируем файлы для подписи
    var tmpDirName = FullPathSignedReports + SP_GetGUID() + "\\";
    if(MakeDir(tmpDirName) == false)
      msgbox("Ошибка при создании директории " + tmpDirName);
      err = 1;
    end;
    
    if(not err)
      DataSet = cmd.Execute();

      while(DataSet.moveNext())
      
        if(CopyFile(FullPathGeneratedReports + DataSet.FileName,
                    tmpDirName + DataSet.FileName
                   ) == false )
          msgbox("Ошибка при копировании файла " + DataSet.FileName + " во временную директорию");
        end;
      end;

      //подписать все файлы в директории
      err = SignAllFilesInDir(tmpDirName);

      //даже если была ошибка, то удалим файлы из временной директории, а также отметим те, которые подписались успешно
      DataSet = cmd.Execute();
      
      while(DataSet.moveNext())
        var DirList = TDirList(FullPathSignedReports + DataSet.FileName, "F");
         
        if(DirList.Count > 0)
          //файл нашли в подписанных
          var e = SetSignInfo(DataSet.ID, DirList.FDate(0), DirList.FTime(0));
          err = IIF((e and (not err)), e, err);
        else
          msgbox("Не удалось подписать файл " + DataSet.FileName);
          SC_SrvRepObjSignErrAuditLog(DataSet.ID, "Не удалось подписать файл " + DataSet.FileName);
        end;
        
        RemoveFile(tmpDirName + DataSet.FileName);
      end;
      RemoveDir(tmpDirName);
    end;
  end;

  return err;

OnError(er)
  msgbox(er.module+ " "+er.line+" "+er.message);
  return 1;
END;



macro GetFileName (Nam, Ext, ServDocID)
/*   var y,m,d;
   var dat = date();
   DateSplit(dat, d,m,y);*/
   var Dt = TRsbDataSet(" select to_char(t_begdate,'YYYYMMDD') d  from dscsrvrep_dbt where t_id = " + ServDocId);
   if (Dt.movenext())
      Nam = Nam + "_" + Dt.d;
   end;

   var Dt1 = TRsbDataSet( " select 1 from dscsrvrepobj_dbt where t_servdocid = " + ServDocID + " and t_filename = '" +StrSubst(Nam,"/","-")+ "." + Ext +  "'"  );
   var i = 0;
   if (Dt1.MoveNext())
      i = i + 1;
      return GetFileName(Nam+" (" + string(i) + ")", Ext, ServDocid);
   else
      return StrSubst(Nam,"/","-") + "." + Ext;
   end;
end;


PRIVATE MACRO GetBrkRepStringRegValue(RegName:string)
  var RegistryPath = "SECUR\\BROKER_REPORT\\" + RegName;
  var str = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, str, stat);
  if(stat) 
    msgbox("Не найдена настройка в реестре банка:", RegistryPath);      
  elif(str == "")
    msgbox("Не задано значение настройки:", RegistryPath);
  end;

  return str;
END;


//SMTP-сервер для отправки почты 
PRIVATE MACRO GetMailSMTPServer()
  return GetBrkRepStringRegValue("MAIL_SMTPSERVER");
END;

//Логин для доступа к почтовому серверу (e-mail, с которого выполняется отправка) 
PRIVATE MACRO GetMailUserName()
  return GetBrkRepStringRegValue("MAIL_USERNAME");
END;

//Пароль для доступа к почтовому серверу 
PRIVATE MACRO GetMailUserPassword()
  return GetBrkRepStringRegValue("MAIL_PASSWORD");
END;

//Порт SMTP-сервера
PRIVATE MACRO GetMailServerPort()
  var RegistryPath = "SECUR\\BROKER_REPORT\\MAIL_SERVERPORT";
  var val = 0;
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_INTEGER, val, stat);
  if(stat) 
    msgbox("Не найдена настройка в реестре банка:", RegistryPath);      
  elif(val <= 0)
    msgbox("Не задано значение настройки:", RegistryPath);
  end;

  return val;
END;

//Используется ли шифрование на SMTP-сервере?
PRIVATE MACRO GetMailUseSSL()
  var RegistryPath = "SECUR\\BROKER_REPORT\\MAIL_USESSL";
  var val = false;
  var stat = 0;
  
  GetRegistryValue(RegistryPath, V_BOOL, val, stat);
  if(stat) 
    msgbox("Не найдена настройка в реестре банка:", RegistryPath);      
  end;

  return val;
END;


PRIVATE MACRO SetSendInfo(RepObjID:integer, Email:string, SendDate:date, SendTime:time)
  var exec = RSDCommand(  " UPDATE DSCSRVREPOBJ_DBT "
                        + "    SET T_ISSEND = 'X', "
                        + "        T_SENDDATE = ?, "
                        + "        T_SENDTIME = ?, "
                        + "        T_EMAIL = ? "
                        + "  WHERE T_ID = ? ");
  exec.addParam( "", RSDBP_IN, SendDate );
  exec.addParam( "", RSDBP_IN, SendTime );
  exec.addParam( "", RSDBP_IN, Email );
  exec.addParam( "", RSDBP_IN, RepObjID );
  exec.execute();

  return 0;
OnError(er)
  msgbox(er.module+ " "+er.line+" "+er.message);
  return 1;
END;


private macro GetFileNameByServDocIdAndClientContrId(ServDocId, ClientId, FormatKind, ContrId)
  var fileName = "";
  var rs = ExecSQLSelect("select T_FILENAME from DSCSRVREPOBJ_DBT where T_SERVDOCID = ? and T_CLIENTID = ? and T_FORMAT = ? and T_CONTRACTID = ?",
                         MakeArray(SQLParam("servDoc", ServDocId),SQLParam("clientId", ClientId),SQLParam("format", FormatKind),SQLParam("contrid", ContrId) ));
  if (rs.MoveNext())
    fileName = Trim(SQL_ConvTypeStr(rs.value("T_FILENAME")));
  end;
  return fileName;
end;

MACRO SC_SendOneRepClientContr(srvrep, SfContrID)
  private macro IsUK (ClientID)
    if(ClientID == 114800)
      return 1;
    end;
    return 0;
  end;

  var brokFileManager = null;
  var bothFileManager = null;

  var err = 0;
  var ArchName;
  var contrName = "";

  var srvobj = TRecHandler("scsrvrepobj.tmp");
  var arh = NULL;

  var email = c_email_proc_env();
  var emailText =
         "\n\nДобрый день!"
       + "\n\n"   
       + "\n В соответствии с условиями регламента оказания брокерских услуг АО 'Россельхозбанк' направляем Вам отчет брокера."
       + "\n Данное письмо было отправлено автоматически."
       + "\n ";
  email.m_add_row_to_msg_text(emailText);
  var BrokerBccList = GetBrokerBccList(); //список адресов для отправки скрытой копии
  if(BrokerBccList != "")
    email.m_add_email_to_bcc_list(BrokerBccList);
  end;

  var Dt = TRsbDataSet("select t_number from dsfcontr_dbt where t_id = "+SfContrID);
  if (Dt.movenext())
    contrName = Dt.number;
  else
    RunError("В БД не найден договор "+ SfContrID);
  end;

  email.m_set_msg_head("Отчет по брокерскому договору " + contrName + ".");
  email.m_add_row_to_msg_text(" Приложен отчет по договору " + contrName); 

  var query =   " select obj.* "
              + "   from dscsrvrepobj_dbt obj "
              + "  where obj.T_SERVDOCID = ? and obj.T_CONTRACTID = ? AND obj.T_FORMAT = ? AND obj.T_ISSEND = CHR(0)";

  var cmd = DL_RSDCommand(query);
  cmd.AddParam(srvrep.rec.ID);
  cmd.AddParam(SfContrID);
  cmd.addParam(IIF((srvrep.rec.PdfFormat == SET_CHAR), SC_SRVREPFORMAT_PDF, SC_SRVREPFORMAT_EXCEL_XLSX) );
  var DataSet = cmd.ExecuteStat();

  if (DataSet.moveNext())
    var fileFormatsToUpdate = TArray();
    DataSet.GetRecord().CopyTo( srvobj.rec );
    brokFileManager = BrokerRepFileStatusManager(srvobj.rec.FileName, false, IIF((srvobj.rec.IsSign == SET_CHAR), BROKER_REPORT_FILE_STATUS_SIGNED, BROKER_REPORT_FILE_STATUS_SAVED));
    var emailAddress = srvobj.rec.Email;
    //если PDF формат указан на операции, и excel существует, то также добавляем excel
    Dt = TRsbDataSet("select 1 from dscsrvrepobj_dbt where t_Format = 2 and t_ServDocID = "+srvobj.rec.servdocid+" AND t_ClientID = " + srvobj.rec.ClientID + " and T_CONTRACTID = " + SfContrID);
    var SendBoth = (srvrep.rec.PdfFormat == SET_CHAR) and Dt.movenext();
    var DirList = TDirList(brokFileManager.Get_CURRENT_path(), "F");

    fileFormatsToUpdate[fileFormatsToUpdate.size] = srvobj.rec.format;

    if(DirList.Count == 0)
      RunError("Файл "+brokFileManager.Get_CURRENT_path()+" не найден");
    end;

    if(emailAddress == "")
      emailAddress = SC_GetClientRepEmail(srvobj.rec.ClientID, srvobj.rec.contractid);
      if(emailAddress == "")
        MsgBox("Для клиента  с ID = " + srvobj.rec.ClientID + " не задан E-mail");
        return 0;
      elif((Index(emailAddress, "@") == 0) or (Index(emailAddress, ".") == 0))
        MsgBox("Для клиента с ID = " + srvobj.rec.ClientID + " указан некорректный E-mail");
        return 0;
      end;
    end;

    email.m_add_email_to_list(emailAddress);
    var FileNameXls = "";

    if (SendBoth)
      FileNameXls = GetFileNameByServDocIdAndClientContrId(srvobj.rec.ServDocId, srvobj.rec.ClientID, SC_SRVREPFORMAT_EXCEL_XLSX, SfContrID);
      bothFileManager = BrokerRepFileStatusManager(FileNameXls, false, IIF((srvobj.rec.IsSign == SET_CHAR), BROKER_REPORT_FILE_STATUS_SIGNED, BROKER_REPORT_FILE_STATUS_SAVED));
      fileFormatsToUpdate[fileFormatsToUpdate.size] = SC_SRVREPFORMAT_EXCEL_XLSX;
    end;

    if(not IsUK(srvobj.rec.ClientID))
      email.m_add_attach_to_list(brokFileManager.Get_CURRENT_path());
      if (SendBoth)
        email.m_add_attach_to_list(bothFileManager.Get_CURRENT_path());
      end;
    else
      ArchName = "Отчет_" +
                 srvobj.rec.ClientID +
                 "_" +
                 strsubst(string(srvrep.rec.enddate:f),".","") +
                 SubStr(srvobj.rec.filename, 1, Index(srvobj.rec.filename, "_") - 1);
      arh = c_inarch_kit(PathCombine(brokFileManager.Get_CURRENT_path_no_file_name(), ArchName), brokFileManager.Get_CURRENT_path_no_file_name(), false);
      arh.add_file_name (srvobj.rec.FileName);
      if( not (arh.Add_to_Archive("zip7")))
         RunError("Ошибка архивирования файла " + srvobj.rec.FileName);
      end;
      if (SendBoth)
        arh.add_file_name (FileNameXls);
        arh.Add_to_Archive_filename("zip7",FileNameXls );
      end;
    end;

    email.m_save_to_submit_synch();
    email.m_submit_email_synch();

    err = IIF(email.m_get_error_status() == true,1,0);

    if (not err)
      var exec = RSDCommand( " UPDATE DSCSRVREPOBJ_DBT              " +
                             "    SET T_ISSEND = 'X',               " +
                             "        T_SENDDATE = :senddate,       " +
                             "        T_SENDTIME = :sendtime,       " +
                             "        T_EMAIL = :email              " +
                             "  WHERE     t_servdocid = :servdocid4 " +
                             "        AND t_clientid = :clientid    " +
                             "        AND T_CONTRACTID = :contract  " +
                             "        AND t_format in ("+join(fileFormatsToUpdate,",")+")" );
      exec.addParam( "", RSDBP_IN, date() );
      exec.addParam( "", RSDBP_IN, time() );
      exec.addParam( "", RSDBP_IN, emailAddress );
      exec.addParam( "", RSDBP_IN, srvrep.rec.ID );
      exec.addParam( "", RSDBP_IN, srvobj.rec.ClientID );
      exec.addParam( "", RSDBP_IN, SfContrID);
      exec.execute();
      if (brokFileManager != null)
        brokFileManager.Move_File(BROKER_REPORT_FILE_STATUS_SENT);
      end;
      if (bothFileManager != null)
        bothFileManager.Move_File(BROKER_REPORT_FILE_STATUS_SENT);
      end;
    else
      MsgBox(email.get_service_msg());
    end;
  end;

  return err;

OnError(er)
  var msgstr = "Ошибка при отправке: " + GetFullErrorMessage(er);
  msgbox(msgstr);
  AddDBLogWithErrorObj("SC_SendOneRepClient",er);
  return 1;
END;


macro SC_SendBrkRepErrMail(Subject:string, Body:string)

  var email = c_email_proc_env();
  email.m_set_msg_head(Subject);
  email.m_add_row_to_msg_text(Body);
  email.m_save_to_submit_grp(BROKER_REPORT_EMAIL_GROUP, true);
  email.m_submit_email_synch();

  if(email.m_get_error_status())
    msgbox("Ошибка при отправке: " + email.get_service_msg());
    return 1;
  end;

  return 0;

OnError(er)
  var msgstr = "";
  if(IsEqClass("TRsComErr", er.err))
    msgstr = "Ошибка при отправке: " + er.err.Message(1);
  elif(er.AxCode)
    msgstr = "Ошибка при отправке: " + er.AxMes;
  end;
  
  if(msgstr == "")
    msgstr = er.module+ " "+er.line+" "+er.message;
  end;

  msgbox(msgstr);

  return 1;
end;

macro BrkRepIsSpitGen(): bool
  const splitGenPath = "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\РАЗДЕЛЯТЬ_ФОРМИРОВАНИЕ";
  var res, err;

  GetRegistryValue( splitGenPath, V_BOOL, res, err );
  if ( err != 0 )
    //если настройки нет, значит false
    return false;
  end;
  //выполнит автоматически приведение типов, где все != 0 равны true, а == 0 равно false
  return res == true;
end;

// Добавление картинок в сохранённый файл xls
private MACRO BRR_AddPictureCell(ob:CDAOMSExcel, wb, CellName:string, CellSheet:integer, FilePath:string, FlagLinkToPicFile:bool, FlagDeletePicFile:bool, isCnv) : bool
   var stat:bool = TRUE;  

   if(  FilePath == "" )
      msgbox( "Не указан путь к файлу" );
      return FALSE;
   end;

   if( not existFile ( FilePath ) )
      msgbox( "Не найден файл ", FilePath, ".|Проверьте правильность пути" );
      return FALSE;
   end;

   if (CellName == "")
      MsgBox("[AddPicture] Ошибка!|Не задан адрес ячейки.");
      Return FALSE;
   end;
   
   if(valType(CellSheet) == v_Undef)
     CellSheet = 1;
   end; 
   
   ob.SheetChange(CellSheet);

   var Picture:Object = CPicture(FilePath, wb.Sheets(CellSheet).Range(CellName).Left, wb.Sheets(CellSheet).Range(CellName).Top);

   // Если работаем в трехзвенке, пошлем файлы рисунков на терминал
   if (NOT isStandAlone() and not IsCnv )
      if( NOT CopyFile( toANSI(Picture.GetFileName()), toANSI(Picture.GetTermFileName()) ) )
         MsgBox( "Ошибка при передаче файла <" + toAnsi(Picture.GetFileName()) + "> на терминал.|Из|<" + toAnsi(Picture.GetFileName()) + ">|в|<"+toAnsi(Picture.GetTermFileName()) + ">" );
         return false;
      end;
   end;

   // Вставим рисунок на лист
   stat = ob.AddPicture(IIF(IsCnv, GetCurDir(false)+"\\"+ FilePath, Picture.GetAbsTermFileName()), 
                        FlagLinkToPicFile, 
                        Picture.GetLeftPos(), 
                        Picture.GetTopPos() ); 

   // Если работаем в трехзвенке, удадалим лишние файлы
   if( (NOT isStandAlone()) AND OR_ExistFile(toANSI(Picture.GetTermFileName())) and not IsCnv )
      OR_DeleteFile( Picture.GetTermFileName() ); 
   end;
    
   Return stat;
OnError (ObjError)

   If (Index(ObjError.Message, "Range") > 0)
      // Скорее всего не найдена именованная ячейка
      Return TRUE;
   else
      ErrorMessage(ObjError, "[AddPicture] Oшибка!");
      Return FALSE;
   end;
end;  // BRR_AddPictureCell

private macro GetPrinterBrkRep(ActvPrinter)
//   var wsp = CreateObject("rsax", "TRsAxServer", "RsAxServer", false).CreateComObject("WScript.Network", false);
   var wsp = ActiveX("WScript.Network");
   var printers = wsp.EnumPrinterConnections;
   var c = 0;
   var port = "", RetVal = "";
   for (var i, printers)
      if (mod(c,2)==0) port = i;end;
      if ((index(i,"Microsoft XPS Document Writer") > 0) and 
          (index(i,"redirected") == 0))
         RetVal = i;
         break;
      end;
      c = c + 1;
   end;
   if (port == "")
      return ActvPrinter;
   elif (index(port, "PORTPROM") > 0)
      return RetVal+" (Ne00:)";
   else
      return RetVal+" ("+port+")";
   end;
end;

private macro getLastPb(sht)
      return sht.HPageBreaks.Item(sht.HPageBreaks.Count);
  OnError(er)
      return sht.HPageBreaks.Item(sht.HPageBreaks.Count-1);
end;

// Shabashov. 5.11.2019 добавил вызов функции сохранения картинки
private var ActvPrint;
MACRO SC_CustomConvertToFormatOnePDF(ExcelNameFile, FilePath, ResultFileName:@string, IsCnv, SignerParty)
   var err = 0, str = "", is2c = isStandAlone;
   var sWorkDir:String;
   var ob, wb, mainind; 
   var nSign = 0, sLogo:String;
   var cSheet, sheetIdx, TegClass;

   macro ins_pagebreak()
      var sht, last_pb, stmp_row;

      var CurSheet = 1;
      while (CurSheet <= ob.SheetCount) 
          //sht = ob.Sheet;
          sht = ob.book.sheets(CurSheet);
          ob.application.worksheets(CurSheet).activate;
          ActvPrint = GetPrinterBrkRep(ActvPrint);

          if (ActvPrint == null)
              ActvPrint = ob.Application.ActivePrinter;
          end;    
          //ob.Application.PrintCommunication = false;
          //sht.PageSetup.PaperSize = 9
          //sht.PageSetup.Zoom = 38;
          ob.Application.ActivePrinter = ActvPrint;
          //ob.Application.PrintCommunication = true;   
          // страничный режим
          ob.Application.ActiveWindow.View = 2;
          // если страниц больше одной
          if (sht.PageSetup.Pages.Count > 1) 
            stmp_row = sht.Range("STAMP__" + CurSheet).Cells.Row;
             //https://support.microsoft.com/ru-ru/help/210663/you-receive-a-subscript-out-of-range-error-message-when-you-use-hpageb
             //bpv обход косяка Excel из-за котрого иногда неверно определяется количество разрывов
            ob.Application.ActiveCell.specialcells(11/*xlLastCell*/).select;
            ob.Application.ActiveCell = "1";//заполни любую ячейку в конце документа
             //
            if (sht.HPageBreaks.Count !=0)/*CHVA*/
                        
               // последний разрыв страницы
               //last_pb = sht.HPageBreaks.Item(sht.HPageBreaks.Count);
                last_pb = getLastPb(sht);/*CHVA*/
               // если разрыв после ячейки со штампом
               if (last_pb.Location.Cells.Row > stmp_row - 16)
                  // изменение положения разрыва
                  last_pb.Location = sht.Rows(stmp_row - 18);
               end;
              
               ob.Application.ActiveCell.delete;//удалить костыль 
            
             end;/*CHVA*/
          end;
          CurSheet = CurSheet + 1;
      end;
      ob.Application.ActiveWindow.View = 1;
      return true;
   onerror(err)
      str = GetLargeErrorMessage(err);
      msgbox(str);
      ob.Application.ActiveWindow.View = 1;
      runerror(str,str);
      return 1; //Прерываем при возникновении ошибки
//      return false;
   end;

   
   GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\WORKDIR", V_STRING, sWorkDir);

   if (ValType(IsCnv) == V_UNDEF)
      IsCnv = false;
   end;
   if (IsCnv == true)
      ReplaceMacro("isStandAlone", "ThisIsStandAlone"); //!!!Обязательно
   end;
   /*Открываем приложение*/
   ob = CDAOMSExcel(null, true);
   // Shabashov. Другой способ открытия книги
   ob.Open(ExcelNameFile);
   ob.Application.DisplayAlerts = false;
   ob.Application.Visible = false;
   wb = ob.Book;
   // Добавить картинку в файл

   for (var itemNameRange, wb.Names)
     sheetIdx = Index(itemNameRange.Name, "__");
     cSheet = Int(SubStr(itemNameRange.Name,sheetIdx+2));
     TegClass = SubStr(itemNameRange.Name,1,sheetIdx-1);
     if (TegClass == "LOGO")
       GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\LOGO", V_STRING, sLogo);
       if (NOT BRR_AddPictureCell(ob, wb, /*"LOGO"*/itemNameRange.Name, cSheet, sLogo, FALSE, TRUE, IsCnv))
          MsgBox("Ошибка добавления картинки");
          err = 1;
       end;
       sLogo = "";
     elif(TegClass == "SIGN_1")  
      GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\SIGN_1", V_INTEGER, nSign);/*номер примечания в котором лежит путь*/
      sLogo = ReadNoteForObject(OBJTYPE_PARTY , string(SignerParty:o:10), nSign, Date());
      if(sLogo == "")
        GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\SIGN_1", V_STRING, sLogo);
      end;  
       if (NOT BRR_AddPictureCell(ob, wb, itemNameRange.Name, cSheet, sLogo, FALSE, TRUE, IsCnv))
          MsgBox("Ошибка добавления картинки");
          err = 1;
       end;
       sLogo = "";
     end;
   end;

   sLogo = "";

   // вставить разрыв страницы
   if (not ins_pagebreak())
      err = 1;
   end;
   /*Определяем имя файла*/
   var FullRepFileName = PathCombine(FilePath,ResultFileName); //Полный путь до файла отчета
   ob.Check_SaveAs(FullRepFileName); //Проверка на то, что при повторном выпуске отчета перезаписываемый файл отчета занят - в этом случае в FullRepFileName возвращается новое наименование  
   if(wb.ExportAsFixedFormat(0, FullRepFileName, 0, 0, 0, 1, 99, 0)== false)/*CHVA 515949*/
      msgbox("Ошибка при сохранении файла отчета");
      err = 1;
   end;
   SplitFile(FullRepFileName, ResultFileName); //Вычленяем из полного пути до файла отчета наименование самого файла отчета
   ResultFileName = ResultFileName + ".pdf";

   /*Закрываем книгу*/
   wb.Close(false);
   TryToAxAppQuit(ob.Application);
   wb = null;
   ob = null;
   if(IsCnv == true)
      ReplaceMacro("isStandAlone", NULL);
   end;
   Return err;
onerror(err)   
   str =  GetLargeErrorMessage(err);
   msgbox(str);
   if (wb)
      wb.Close;
   end;
   TryToAxAppQuit(ob.Application);
   wb = null;
   ob = null;
   if(IsCnv == true)
      ReplaceMacro("isStandAlone", NULL);
   end;
   return 1
END;
/*~ak*/

macro DropPrevBrkRepConvertError(DocId, DocKind, Oper)
  var query = 
    " DELETE FROM ddl_logdata_dbt "
   +"       WHERE t_logid IN "
   +"                (SELECT T_ID "
   +"                   FROM DDL_LOG_DBT "
   +"                  WHERE t_docid = ? AND t_dockind = ? AND t_oper = ?) "
   +"             AND TO_CHAR (T_LOGDATA) LIKE '%BrkConvert: %' ";
  execSQL(query, MakeArray(SQLParam("DocId", DocId), SQLParam("DocKind", DocKind), SQLParam("Oper", Oper)));
end;

//Отправить все отчеты, которые помещены во временный файл
MACRO SendAllRepObjTmp(srvrep)
  var OperID = 14; //Операция для конвейера - Пакетная операция отправки отчета брокера в БО ЦБ
  var ConvID = 18; //Вид конвейера Отправка отчета брокера БО ЦБ
  var rslObj = SrvRepCnvData(srvrep.rec.ID);
  var UseConveyer = SecurUseConveyer(ConvID, OperID);
  var err = 0;

  UseConveyer = false;
  if(UseConveyer)
    var cnv = RsbConveyerExec(ConvID);

    cnv.AddParamsForConveyer(ConvID, rslObj, 0);
    cnv.AddParamsForOperation(ConvID, OperID, rslObj, "", 0);
    cnv.showPanelMonitoring(1);
    cnv.Run();
    
    if( ErrorFromCnvpack( cnv ) > 0 )
      err = 1;
    end;
  else
    err = ExecMacroFile("sc_srvrepbrk.mac", "ExecuteSendSrvRepBrk", 0, 0, 0, 0, 0, rslObj);
  end;

  return err;
END;

CLASS CSrvRepExec()
  private var m_srvrep = TBFile("scsrvrep.dbt", "w");
  private var m_CurrAction = 0;

  macro Load(ID)
    var err = 0;

    m_srvrep.rec.ID = ID;
    if(not m_srvrep.GetEQ())
      err = 1;
      msgbox("Ошибка поиска операции");
    end;

    return err;
  end;

  macro SetStatus(NewStatus:integer)   
    var err = 0;

    if(m_CurrAction == SCSRVREP_ACTION_CREATE)
      m_srvrep.rec.RepStatus = NewStatus;
    elif(m_CurrAction == SCSRVREP_ACTION_SIGN)
      m_srvrep.rec.SignStatus = NewStatus;
    elif(m_CurrAction == SCSRVREP_ACTION_SEND)
      m_srvrep.rec.SendStatus = NewStatus;
    end;

    if(not m_srvrep.Update())
      err = 1;
      msgbox("Ошибка при установке статуса действия по операции");
    end;

    return err;
  end;

  macro StartAction(Action:integer)
    m_CurrAction = Action;
  end;

  macro srvrep()
    return m_srvrep;
  end;

END;

macro SignAndSendForAutoSend(srvrep, SfContrID, inp_err)
  var err = inp_err;
  var exec,query,cmd,DataSet;

  if (not err)
    if (srvrep.rec.AutoSendKind == SCSRVREP_AUTOSENDKIND_TOGETHER)
      //вначале подпишем, прежде чем отправить
      if (srvrep.rec.AutoSign == SET_CHAR)
        SQL_Execute("DELETE FROM DSCSRVREPOBJ_TMP", "", false );
        exec = RSDCommand(  " INSERT INTO DSCSRVREPOBJ_TMP "
                          + " SELECT * "
                          + "   FROM DSCSRVREPOBJ_DBT "
                          + " WHERE T_SERVDOCID = ? "
                          + "   AND T_ISSIGN = CHR(0) "
                          + "   AND T_ISSEND = CHR(0) "
                          + "   AND T_FORMAT = " + SC_SRVREPFORMAT_PDF
                          + "   AND T_CONTRACTID = ?");
        exec.addParam( "", RSDBP_IN, srvrep.rec.ID );
        exec.addParam( "", RSDBP_IN, SfContrID );
        exec.execute();
        err = SC_SignAllRepObjTmp();
        SQL_Execute("DELETE FROM DSCSRVREPOBJ_TMP", "", false );
      end;

      if (not err)
        SQL_Execute("DELETE FROM DSCSRVREPOBJ_TMP", "", false );
        exec = RSDCommand(  " INSERT INTO DSCSRVREPOBJ_TMP "
                          + " SELECT * "
                          + "   FROM DSCSRVREPOBJ_DBT "
                          + " WHERE T_SERVDOCID = ? "
                          + "   AND T_ISSEND = CHR(0) "
                          + "   AND T_FORMAT = ? "
                          + "   AND T_CONTRACTID = ?");
        exec.addParam( "", RSDBP_IN, srvrep.rec.ID );
        exec.addParam( "", RSDBP_IN, IIF((srvrep.rec.PdfFormat == SET_CHAR), SC_SRVREPFORMAT_PDF, SC_SRVREPFORMAT_EXCEL_XLSX) );
        exec.addParam( "", RSDBP_IN, SfContrID);
        exec.execute();
        err = SendAllRepObjTmp(srvrep);

        SQL_Execute("DELETE FROM DSCSRVREPOBJ_TMP", "", false );
      end;
    end;
  end;

  return err;
end;


MACRO POIAddPictureAndConvertToPDF(repPoiObj, GenPath, SignerParty, fileNameNoFormat)
  var err = 0;
  var nSign = 0, sLogo:String;
  var sheetIdx, TegClass;

  for (var nameAddr, repPoiObj.nameAddrArr)
    sheetIdx = Index(nameAddr.Name, "__");
    TegClass = SubStr(nameAddr.Name,1,sheetIdx-1);
    if (TegClass == "LOGO")
      GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\LOGO", V_STRING, sLogo);
      if (trim(sLogo) == "")
        MsgBox("В настройке РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\LOGO не задан файл изображения логотипа");
      else
        sLogo = PrcPath(sLogo);
        repPoiObj.poiBook.addPictureBase(nameAddr.SheetIndex, sLogo, nameAddr.ColId, nameAddr.RowId);
        sLogo = "";
      end;
    elif(TegClass == "SIGN_1")  
      GetRegistryValue("РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\ОТЧЕТ БРОКЕРА\\SIGN_1", V_INTEGER, nSign);/*номер примечания в котором лежит путь*/
      sLogo = ReadNoteForObject(OBJTYPE_PARTY , string(SignerParty:o:10), nSign );
      if (trim(sLogo) == "")
        MsgBox("Для субъекта с ID " + SignerParty + " не задан файл изображения подписи в примечании субъекта " + string(nSign));
      else
        sLogo = PrcPath(sLogo);
        repPoiObj.poiBook.addPictureBase(nameAddr.SheetIndex, sLogo, nameAddr.ColId, nameAddr.RowId);
        sLogo = "";
      end;
    end;
  end;

  sLogo = "";

  //удалим предыдущий, если такой есть
  var tempFile = PathCombine(GenPath,"temp_"+fileNameNoFormat+".xlsx");
  RemoveFile(tempFile);
  //сохраняем со вставленными картинками
  repPoiObj.Save(tempFile);

  //конвертируем его в PDF
  if (not WR_ConvertToFormatLibreOffice( tempFile, PathCombine2(GenPath,fileNameNoFormat,".pdf"), FORMAT_PDF_ONLY, false, null, false))
    err = 1;
  end;

  Return err;
END;
/*~ak*/