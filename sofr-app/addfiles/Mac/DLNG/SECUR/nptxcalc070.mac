/*
$Name:        nptxcalc070.mac
$Module:      Ценные бумаги
$Description: Расчет НОБ для НДФЛ. Шаг "Расчет НДР 5 уровня"
*/

/* Операция расчета НОБ для НДФЛ
   Шаг "Расчет НДР 5 уровня"*/
IMPORT InsCarryDoc, "secinter.mac", "nptxcalcfun.mac";

PRIVATE VAR Oper = TRecHandler( "nptxop.dbt" );

PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR OperationStatus;
  VAR Stat = 0;
  VAR vBegDate:DATE, 
      vEndDate:DATE;
  VAR query, cmd, DataSet;
  VAR count;
  VAR TaxPeriod = 0;

  Oper.SetRecordAddr( FDoc );

  datesplit(Oper.rec.OperDate, null, null, TaxPeriod);

  var TaxTeriodToLucre = CheckTaxTeriodToLucre(TaxPeriod);

  ClearGTT_DNPTXOBJ_TMP();
  if((Oper.rec.IIS == SET_CHAR) and (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_ENDYEAR)) //ИИС "Окончание года"
    var IsEndPeriod = 0;
    
    //Рассчитываем по периодам
    GetDateCalcIIS37(Oper, NPTXOPSTEP7, @vBegDate, @vEndDate, @IsEndPeriod);

    if(IsEndPeriod == 0)
      Oper.rec.subkind_operation = DL_TXBASECALC_OPTYPE_NORMAL;
    end;

    stat = CreateTaxObjects5(Oper, ID_Step, vBegDate, vEndDate, NULL, NULL, NULL, NULL, NULL, true);
    
    if((not stat) and (TaxTeriodToLucre == false))
      stat = CreateMaterialTaxObjects5(Oper, ID_Step, vBegDate, vEndDate);
    end;

    if(not stat)
      stat = DL_NPTX_StartInsertTaxObject(Oper.rec.ID, ID_Step);
    end;

    if(not stat)
      stat = DL_NPTX_PutMsg( string(date(vEndDate)), 1000 + 1 );
    end;

    DL_NPTX_SaveOperLog( Oper.rec.ID, (NPTXOPSTEP7*1000) + 1 );

    if(not stat)
      if(IsEndPeriod == 0)//если остались необработанные периоды, то добавляем текущий блок, иначе- следующий
        OperationStatus = 12; //Пересчет НДР 3-5 уровней
      else
        OperationStatus = 11; //Пересчет НДР8
      end;
      if( not InsertOprStatus( 46052 /*Расчет НОБ*/, OperationStatus) ) 
        return Error( "Ошибка при установке статуса операции" );
      end;
    end;

  elif(Oper.rec.Recalc == SET_CHAR)//если пересчет, то по периодам пересчитываем НДР с 3 по 7 уровень.
    vBegDate = Oper.rec.BegRecalcDate;
    query = GetIncomeDateQuery(Oper.rec.ID, NPTXOPSTEP7, Oper.rec.EndRecalcDate);
    cmd = DL_RSDCommand(query);        
    count = cmd.GetCount();
    DataSet = cmd.Execute();
    if((DataSet.moveNext()) AND (not stat))
      vEndDate = DataSet.t_operdate;
      if((vEndDate != Oper.rec.OperDate) and ((Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_LUCRE) or (TaxTeriodToLucre == false)))
        Oper.rec.subkind_operation = DL_TXBASECALC_OPTYPE_NORMAL;
      end;

      if((Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_CLOSE_IIS) and IsCalcNOBCloseISS() and (Oper.rec.Contract > 0))
        stat = CreateTaxObjectsIIS5(Oper, ID_Step);
        if((not stat) and (TaxTeriodToLucre == false))
          stat = CreateMaterialTaxObjectsIIS5(Oper, ID_Step);
        end;
      else
        if(TaxTeriodToLucre == false)
          stat = CreateTaxObjects5(Oper, ID_Step);
          if(not stat)
            stat = CreateMaterialTaxObjects5(Oper, ID_Step);
          end;
        else
          if(Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_LUCRE)
            stat = CreateTaxObjects5(Oper, ID_Step, vBegDate, vEndDate);
          else
            stat = CreateMaterialTaxObjects5(Oper, ID_Step, vBegDate, vEndDate);
          end;
        end;
      end;
    end;
    if(not stat)
      stat = DL_NPTX_StartInsertTaxObject(Oper.rec.ID, ID_Step);
    end;
    if(not stat)
      stat = DL_NPTX_PutMsg( string(date(vEndDate)), 1000 + count );
    end;
    DL_NPTX_SaveOperLog( Oper.rec.ID, (NPTXOPSTEP7*1000) + count );

  else
    stat = DL_NPTX_PutMsg( "Расчет НДР 5 уровня", 40);
    if( not stat )
      if((Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_CLOSE_IIS) and IsCalcNOBCloseISS() and (Oper.rec.Contract > 0))
        stat = CreateTaxObjectsIIS5(Oper, ID_Step);
        if((not stat) and (TaxTeriodToLucre == false))
          stat = CreateMaterialTaxObjectsIIS5(Oper, ID_Step);
        end;
      else
        if(TaxTeriodToLucre == false)
          stat = CreateTaxObjects5(Oper, ID_Step);
          if(not stat)
            stat = CreateMaterialTaxObjects5(Oper, ID_Step);
          end;
        else
          if(Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_LUCRE)
            stat = CreateTaxObjects5(Oper, ID_Step);
          else
            stat = CreateMaterialTaxObjects5(Oper, ID_Step);
          end;
        end;
      end;

      if(not stat)
        stat = DL_NPTX_StartInsertTaxObject(Oper.rec.ID, ID_Step);
      end;
  
      DL_NPTX_SaveOperLog( Oper.rec.ID, 70 );
  
      if(not stat)
        if(TaxTeriodToLucre == false)
          OperationStatus = 6; //Расчет НДР6
        else
          if(Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_LUCRE)
            OperationStatus = 7; //Расчет НДР7 
          else
            OperationStatus = 6; //Расчет НДР6
          end;
        end;

        if( not InsertOprStatus( 46052 /*Расчет НОБ*/, OperationStatus) ) 
           return Error( "Ошибка при установке статуса операции" );
        end;
      end;
    end;
  end;

  return stat;
END;
