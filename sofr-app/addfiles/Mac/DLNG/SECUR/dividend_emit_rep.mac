/*
$Name:         dividend_emit_rep.mac
$Module:       Ценные бумаги
$Description:  Отчет Генерация РЕПО в форме "Получения дивидендов от Эмитента"
*/
/*     */

import OprInter, RsbDataSet, "spservsql.mac";


private macro GetErrMessage(err_code)
  var err_msg = "";
  
  var cmd = RsdCommand(
      "select t_Contents from dsbbank_msg "
      "where t_Number = ? "
  );
  cmd.addParam("Number", RSDBP_IN, err_code);
  cmd.execute();

  var rs = RsdRecordset(cmd);
  if (rs.moveNext())
    err_msg = rs.value(0);
  end;

  return err_msg;
end; 

private macro _print(deals, stat: Integer)

    if ( 1 == stat )
        println("Повторная генерация операций получения невозможна, так как по некоторым из операций уже начата обработка\n\n");
        return;
    elif (stat != 0)
        println( "Код ошибки: " + string(stat) + ". " + GetErrMessage(stat) );
        return;
    end;

    println(" Генерация операций по РЕПО\n");
    println( "┌──────────┬─────────────┬──────────────┬────────────────┐\n" +
             "│   Номер  │ ID Операции │ Код Операции │      РЕПО      │\n" +
             "│          │             │              │                │\n" +
             "├──────────┼─────────────┼──────────────┼────────────────┤");
        
      
    var n = 0;
  
    for ( var id, deals )
        var q = "select tick.t_DealCode DealCode, tickRepo.t_DealCode Repo"
                + " from ddl_tick_dbt tick, ddl_tick_dbt tickRepo"
                + " where  tick.t_DealID = "+id+" and "
                + " tick.t_ParentID = tickRepo.t_DealID";
        var cmd = DL_RSDCommand(q);
        var DataSet = cmd.Execute();

        while( DataSet.moveNext() )
            //DataSet.GetRecord().CopyTo( tlnk );
            n = n + 1;
            [│ ######## │ ########### │ ############ │ ############## │](n, id, DataSet.DealCode, DataSet.Repo);
        end;
    end;

    println("└──────────┴─────────────┴──────────────┴────────────────┘");  

end;

MACRO PrintReport(DealIDs, IDOper, pfi, dfix, stat)
    var errcode, errtext;
    var ReportFileName;

    FILE ReportOutFile() txt write; /*файл вывода для отчета*/ 

    ReportFileName = GetTxtFileName( "dividend_emitrepo_" + IDOper + "_" + pfi );
    if( not Open( ReportOutFile, ReportFileName ) )
        errcode = status(errtext);
        RunError( "Ошибка при создании файла|"+ReportFileName+"|(" + errcode + ") " + errtext );
    end;
    SetOutput( ReportFileName );

    _print(DealIDs, stat);

    SetOutput( NULL, true );
    close( ReportOutFile );
    ViewFile( ReportOutFile );
    return 0;

END;
          
