/*
$Name:             RealizReg_Form.mac
$Module:           АРНУ
$Description:      Отчет "Регистры по реализации" (НУ) - форма ввода данных
*/

import "DlRepForm_h.mac", "globals.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_REALIZREG_PERIOD     = 0,
      PNFLD_REALIZREG_YEAR       = 1,
      PNFLD_REALIZREG_GROWTH     = 2,
      PNFLD_REALIZREG_BEGINDATE  = 3,
      PNFLD_REALIZREG_ENDDATE    = 4,
      PNFLD_REALIZREG_SUBKIND    = 5,
      PNFLD_REALIZREG_AVRGROUP   = 6,
      PNFLD_REALIZREG_AVRKIND    = 7,
      PNFLD_REALIZREG_AVRCODE    = 8,
      PNFLD_REALIZREG_AVRNAME    = 9,
      PNFLD_REALIZREG_BYQUOTED   = 10,  
      PNFLD_REALIZREG_BYNOQUOTED = 11,
      PNFLD_REALIZREG_BYTECH     = 12,    
      PNFLD_REALIZREG_EXCLUDE    = 13,
      PNFLD_REALIZREG_PRINTMETHOD= 14;

CONST H_PNFLD_SUBKIND = 100, 
      H_PNFLD_AVRGROUP = 101;
 
/*Подвиды регистров*/
CONST REALIZREG_ALL    = 0, //201-206 - Регистры реализации
      REALIZREG_0201   = 1,
      REALIZREG_0202   = 2,
      REALIZREG_0203   = 3,
      REALIZREG_0204   = 4,
      REALIZREG_0205   = 5,
      REALIZREG_0206   = 6;

PRIVATE VAR REALIZREG_NAME = TArray();
REALIZREG_NAME[REALIZREG_ALL]  = "Все";
REALIZREG_NAME[REALIZREG_0201] = "0201 - рублевые акции, депозитарные расписки и паи";
REALIZREG_NAME[REALIZREG_0202] = "0202 - рублевые процентные облигации" ;
REALIZREG_NAME[REALIZREG_0203] = "0203 - рублевые дисконтные облигации" ;
REALIZREG_NAME[REALIZREG_0204] = "0204 - валютные акции, депозитарные расписки и паи"; 
REALIZREG_NAME[REALIZREG_0205] = "0205 - валютные процентные облигации" ;
REALIZREG_NAME[REALIZREG_0206] = "0206 - валютные дисконтные облигации" ;

macro GetAvoirListAddWhere(RepSubKind)

 var WhereCond = " ";

   if(RepSubKind == REALIZREG_0201)
        WhereCond = WhereCond + " AND (   RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_SHARE+", t.t_AvoirKind) > 0 "
                              + "      OR RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_DEPOSITORY_RECEIPT+", t.t_AvoirKind) > 0 "
                              + "      OR RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_INVESTMENT_SHARE+", t.t_AvoirKind) > 0 )"
                              + " AND t_FaceValueFI = " + NATCUR; 
      elif(RepSubKind == REALIZREG_0202)
        WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                              + " AND  t_FaceValueFI = " + NATCUR
                              + " AND  Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1"; 
      elif(RepSubKind == REALIZREG_0203)
        WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                              + " AND  t_FaceValueFI = " + NATCUR
                              + " AND  Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) = 1";
      elif(RepSubKind == REALIZREG_0204)
        WhereCond = WhereCond + " AND (   RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_SHARE+", t.t_AvoirKind) > 0 "
                              + "      OR RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_DEPOSITORY_RECEIPT+", t.t_AvoirKind) > 0 "
                              + "      OR RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_INVESTMENT_SHARE+", t.t_AvoirKind) > 0 )"
                              + " AND t_FaceValueFI <> " + NATCUR;
      elif(RepSubKind == REALIZREG_0205)
        WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                              + " AND  t_FaceValueFI <> " + NATCUR
                              + " AND  Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1";
      elif(RepSubKind == REALIZREG_0206)
        WhereCond = WhereCond + " AND  RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", t.t_AvoirKind) > 0 "
                              + " AND  t_FaceValueFI <> " + NATCUR
                              + " AND  Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) = 1";
      end;

      WhereCond = WhereCond + " AND AV.t_TaxGroup <> 919 and AV.t_TaxGroup <> 943 ";

 return WhereCond;
end;

/*Тип отчета - 0201 - 0206*/
PRIVATE MACRO FldProc_Subkind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = REALIZREG_ALL;
     end;
     FldShowValue = REALIZREG_NAME[FldRealValue];

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    REALIZREG_NAME[REALIZREG_ALL],  REALIZREG_ALL,
                    REALIZREG_NAME[REALIZREG_0201], REALIZREG_0201,
                    REALIZREG_NAME[REALIZREG_0202], REALIZREG_0202,
                    REALIZREG_NAME[REALIZREG_0203], REALIZREG_0203,
                    REALIZREG_NAME[REALIZREG_0204], REALIZREG_0204,
                    REALIZREG_NAME[REALIZREG_0205], REALIZREG_0205,
                    REALIZREG_NAME[REALIZREG_0206], REALIZREG_0206
                  );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = REALIZREG_ALL;
     FldShowValue = REALIZREG_NAME[FldRealValue];
  end;
  return CM_DEFAULT;
END;


/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR RetVal = CM_DEFAULT;
  VAR WhereCond = "1=1", AddTable = "", AvrKind, AvrGroup;
  VAR RepSubKind = 0;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoiriss = TRecHandler( "avoiriss.dbt" );

  if( not ((Cmd == DLG_KEY) AND (Key == KEY_F3)) )
    DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( FldRealValue <= 0 ) /*инициализация по умолчанию*/
        EnableFields( pThis.Data(), PNFLD_REALIZREG_EXCLUDE );
     else 
        DisableFields( pThis.Data(), PNFLD_REALIZREG_EXCLUDE );
        pThis.Data.rec.Exclude = "";
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
   
    AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
    if( (AvrKind == NULL) OR (AvrKind < 0) )
       AvrKind = 0;
    end;

    AddTable  = AddTable  + "davoiriss_dbt AV";
    WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID ";

    RepSubKind = pThis.GetLinkedValue(H_PNFLD_SUBKIND);
    
    WhereCond = WhereCond + GetAvoirListAddWhere(RepSubKind);

    AvrGroup = pThis.GetLinkedValue(H_PNFLD_AVRGROUP);
    if( (AvrGroup != NULL) AND (AvrGroup > 0) )
       WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrGroup;
    end;

    if( ListAvoiriss( AvrKind, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
       FldRealValue = Fininstr.rec.FIID;
       FldShowValue = Fininstr.rec.FI_Code;
       pThis.SetLinkedValue( H_PNFLD_AVRGROUP, Avoiriss.rec.TaxGroup );
    end;

  end;
  return RetVal;
END;

/*Группа налогового учета*/
PRIVATE MACRO FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;
  VAR Avoiriss, AvrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
              + "  and t_Element <> 919 and t_Element <> 943 order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Name");    
        FldRealValue = Choice.Value("t_Element");
     end;

  end;

  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_RealizReg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;
     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,H_PNFLD_AVRGROUP) )
        SetLinkedValue( H_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     if( ВОЗМОЖНОСТЬ_ВЫБОРА_ВИДА_ЦБ() == false)
        DisableFields(This.Data(), PNFLD_REALIZREG_AVRKIND);
     end;

     /*только в excel*/
     DisableFields( This.Data(), PNFLD_REALIZREG_PRINTMETHOD );

     AddFieldProc( 0, PNFLD_REALIZREG_PERIOD,      DL_PNFLD_PERIOD,      @DL_FldProc_Period, PrevQuartal + 12, 17, 5 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_REALIZREG_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_REALIZREG_GROWTH,      DL_PNFLD_GROWTH,      @DL_FldProc_Growth);
     AddFieldProc( 0, PNFLD_REALIZREG_BEGINDATE,   DL_PNFLD_BEGINDATE,   @DL_FldProc_BeginDate );
     AddFieldProc( 0, PNFLD_REALIZREG_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate );
     AddFieldProc( 0, PNFLD_REALIZREG_SUBKIND,     H_PNFLD_SUBKIND,      @FldProc_Subkind, REALIZREG_ALL, 10, 10 );
     //AddFieldProc( 0, PNFLD_REALIZREG_AVRGROUP,    DL_PNFLD_AVRGROUP,    @DL_FldProc_AvrGroup );
     AddFieldProc( 0, PNFLD_REALIZREG_AVRGROUP,    H_PNFLD_AVRGROUP,     @FldProc_AvrGroupFltr, null, 10, 5 );
     AddFieldProc( 0, PNFLD_REALIZREG_AVRKIND,     DL_PNFLD_AVRKIND,     @DL_FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_REALIZREG_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_REALIZREG_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_REALIZREG_BYQUOTED,    DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox, SET_CHAR );
     AddFieldProc( 0, PNFLD_REALIZREG_BYNOQUOTED,  DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox, SET_CHAR );
     AddFieldProc( 0, PNFLD_REALIZREG_BYTECH,      DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox );
     AddFieldProc( 0, PNFLD_REALIZREG_EXCLUDE,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox );
     AddFieldProc( 0, PNFLD_REALIZREG_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 36, 20 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "REGREAL" ); 
END;

CLASS (TxReportForm) WS_TX_RealizReg_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей
  ReDefineFieldNum[PNFLD_REALIZREG_PERIOD]      = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_REALIZREG_YEAR]        = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_REALIZREG_GROWTH]      = TXREPORTFORM_FLD_ADDITOG;
  ReDefineFieldNum[PNFLD_REALIZREG_BEGINDATE]   = TXREPORTFORM_FLD_DATEBEGIN;
  ReDefineFieldNum[PNFLD_REALIZREG_ENDDATE]     = TXREPORTFORM_FLD_DATEEND;
  ReDefineFieldNum[PNFLD_REALIZREG_SUBKIND]     = TXREPORTFORM_WIDGET_KIND;
  ReDefineFieldNum[PNFLD_REALIZREG_AVRGROUP]    = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_REALIZREG_AVRKIND]     = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_REALIZREG_AVRCODE]     = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_REALIZREG_AVRNAME]     = TXREPORTFORM_FAKEFLD_AVRNAME;
  ReDefineFieldNum[PNFLD_REALIZREG_BYQUOTED]    = TXREPORTFORM_FLD_INORCB;
  ReDefineFieldNum[PNFLD_REALIZREG_BYNOQUOTED]  = TXREPORTFORM_FLD_NOTORCB;
  ReDefineFieldNum[PNFLD_REALIZREG_BYTECH]      = TXREPORTFORM_FLD_DETAIL;
  ReDefineFieldNum[PNFLD_REALIZREG_EXCLUDE]     = TXREPORTFORM_FLD_EXCLUDE;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
