/*******************************************************************************
 DESCRIPTION  :   Создание заявок/поручений по журналу внебиржевого рынка
 PROGRAMMED BY:   Levchenko
 CREATION DATE:   10.10.18
*******************************************************************************/


import oralib, likepy, ptinter, globals, fiinter, rcw, oprinter, rsexts;

const KIND          = 350, //Тип документа в связке заявки-поручения
      ПоручКлНаОпер = 251,
      FORTS         = 192, //Срочный рынок
      DOCLOG_SECUR  = 513,
      DOCLOG_FORTS  = 250;



file infile () txt;

const IMP468_REGISTRY_DIRECTORY  = "РСХБ\\ДИРЕКТОРИИ\\IMPORT_ORDERS"; //DEF-62575 
var errCode: Integer;
var PathIn: String;
//в реестр настроек добавлять можно без экранирования слешей
GetRegistryValue(IMP468_REGISTRY_DIRECTORY, V_STRING, PathIn, errCode);     
    
var CountERR = 0, CountOK = 0;
var IsPerform = FALSE; //Делать связку со сделкой 


Private Macro FindNameAlg ( val:string )
    var sql;
    var inumberalg = 1;
    const itypealg = 3172; // способ подачи

    if ( val == NULL ) 
       inumberalg =  1; 
    elif ( ValType (val) == 26 ) 
       inumberalg =  1; 
    else 
       sql = "select t_inumberalg from dnamealg_dbt where t_itypealg = :ptypealg and upper(t_sznamealg) = upper(:pval) ";
       sql = ExecSqlSelect(sql, MakeArray(SqlParam("ptypealg", itypealg),
                                          SqlParam("pval",     val     )), false);
       if (sql.MoveNext() )
          inumberalg = sql.value("t_inumberalg");
       end;
    end;
    return inumberalg;
End;


Private Macro NotNull ( val )
    if ( val == NULL ) return false; end;
    if ( ValType (val) == 26 ) return false; end;
    return true;
End;

Private macro CnvInt (text)
    var ret = string (text), ind = index (ret,".");
    if (ind == 0)
       return ret;
    else
       return substr (ret, 1, ind-1);
    end;
End;


macro IdentifyDirection (OperType)
   var type = 0;

   if (OperType == "B") 
      type = 1;
   elif (OperType == "S")
      type = 2
   end;

   return type;
End;


macro ПроверитьПоручениеНаОперацию(DealID:integer, MarketType)
   var spgroundid = 0;
   var sql = "select spground.t_spgroundid id  "
           + "from dspgrdoc_dbt grdoc, dspground_dbt spground "
           + "where grdoc.t_sourcedockind = :dockind "
           + "      and grdoc.t_sourcedocid = :dealid "
           + "      and spground.t_kind = :ordertype "
           + "      and spground.t_spgroundid = grdoc.t_spgroundid ";
   sql = ExecSqlSelect(sql, MakeArray(SqlParam("dockind",   MarketType    ),
                                      SqlParam("dealid",    DealID        ),
                                      SqlParam("ordertype", ПоручКлНаОпер )), false);
   if (sql.MoveNext() )
      spgroundid = sql.value("id");
   end;
   return spgroundid;
End;

macro СоздатьПоручениеНаОперацию (DealID,
                                  Code,
                                  OrderDate,
                                  OrderTime,
                                  ContrAgent,
                                  codets,
                                  doclog,
                                  MarketType,
                                  xld,
                                  Perform,
                                  TransType
                                 )

   var Spground = Tbfile ("spground", "W");
   var Spgrdoc  = Tbfile ("spgrdoc", "W");
   var partyname = GetPartyNames (ContrAgent, PTKN_FULLNAME, false);
   var spgroundid = 0;
   
   partyname = partyname(0);
   spgroundid = ПроверитьПоручениеНаОперацию(DealID, MarketType);

   if (spgroundid > 0) //Поручение для данной сделки уже существует!
      return spgroundid;
   end;

   
   Spground.rec.doclog       = doclog;     //Не знаю что это, и почему такое значение
   Spground.rec.kind         = ПоручКлНаОпер;
   Spground.rec.direction    = 1;          //0-взодящий, 1-исходящий, 2-исходящий и отправлен
   Spground.rec.xld          = xld;        //Номер в банке
   Spground.rec.registrdate  = OrderDate;  //Дата регистрации
   Spground.rec.registrtime  = OrderTime;  //время регистрации
   Spground.rec.party        = ContrAgent; //Контрагент по поручению.
   Spground.rec.altxld       = xld;        //Номер у составителя/получателя (внешний)
   Spground.rec.signeddate   = OrderDate;  //Дата с которой действует поручение
   Spground.rec.receptionist = {oper};
   Spground.rec.partyname    = partyname;  //Наименование контрагента
   Spground.rec.partycode    = ПолучитьКодСубъекта(ContrAgent, PTCK_CONTR); //Код контрагента
   Spground.rec.department   = 1;          //филиал. пока всегда 1
   Spground.rec.branch       = 1;
   Spground.rec.methodapplic = TransType;
   
   if (not Spground.insert()) 
      return 0;
   end;
   
   spgroundid = Spground.rec.spgroundid;
   
   if (DealID == 0) 
      return spgroundid;
   end;
   Spgrdoc.rec.sourcedockind = MarketType;
   Spgrdoc.rec.sourcedocid   = DealID;
   Spgrdoc.rec.spgroundid    = spgroundid;
   if (not Spgrdoc.insert()) 
      spgroundid = 0;
   end;

   return spgroundid;
End;

macro СоздатьНовоеПоручение (code,             //код
                             codets,           //код внешний
                             OrderDate,        //Дата
                             OrderTime,        //Время заявки
                             ContrAgent,       //Контрагент
                             Client,           //Клиент
                             fiid,             //Ценная бумага
                             Amount,           //Количество ц/б в штуках
                             Price,            //цена за 1 ц/б
                             pricetype,        //Тип цены. Для облигаций в %, для срочных не указан, для остальных обычный
                             PriceFiID,        //Валюта (цены за 1 ц/б)
                             DealID,           //ИД сделки
                             Currency,         //Валюта рассчётов
                             Direction,        //Направление. Значения описаны в начале макроса
                             ClientContr,      //Ид договора клиента
                             RepoRate,         //Ставка РЕПО
                             IsExchange,       //Признак биржи
                             pfikind,          //Вид производного финансового инструмента
                             doclog,
                             MarketType,
                             xld,
                             Perform,
                             oper,
                             TransType,
                             MarketKind,       //Вид рынка
                             MarketID          //Идентификатор биржи
                            ) 

   var reqid = 0;             
   var req     = TBFile("dl_req", "W");
   var Spgrdoc = Tbfile ("spgrdoc", "W");
   var spgroundid;            
   var sql;    
               
   sql = ExecSqlSelect("select t_id id from ddl_req_dbt where t_kind = :kind and t_code = :code and t_marketkind = :marketkind and t_marketid = :marketid", 
                       MakeArray(SqlParam("kind", KIND), SqlParam("code", code), SqlParam("marketkind", MarketKind), SqlParam("marketid", MarketID)), 
                       false
                      );

   if(sql.MoveNext())       
      return int(sql.value("id"));
   end;                       

   rslDefCon.beginTrans ();   

   spgroundid = СоздатьПоручениеНаОперацию(DealID,
                                            Code,
                                            OrderDate,
                                            OrderTime,
                                            Client,
                                            codets,
                                            doclog,
                                            MarketType,
                                            xld,
                                            Perform,
                                            TransType
                                            );
    
   if (spgroundid == 0)       
      if ( rslDefCon.isInTrans () )
         rslDefCon.rollbackTrans ();
      end;                    
      return 0;               
   end;
   
   req.rec.kind        = KIND;
   req.rec.code        = code;
   req.rec.codets      = "";
   req.rec.date        = OrderDate;
   req.rec.time        = OrderTime;
   req.rec.client      = Client;
   req.rec.party       = ContrAgent;

   req.rec.fiid        = fiid;
   req.rec.Amount      = Amount;
   req.rec.Price       = Price;
   req.rec.PriceType   = PriceType;

   req.rec.PriceFiID   = PriceFiID;
   req.rec.sourcekind  = MarketType;
   
   req.rec.SourceID    = DealID;

   req.rec.CurrencyID  = Currency;
   req.rec.IsExchange  = IsExchange;
   req.rec.Direction   = Direction;
   req.rec.ClientContr = ClientContr;
   req.rec.RepoRate    = RepoRate;
   req.rec.reportlog   = 1;
   req.rec.pfikind     = -1;
   req.rec.methodapplic = TransType; 
   req.rec.MarketKind  = MarketKind; 
   req.rec.MarketID    = MarketID; 

   if (DealID > 0) 
      req.rec.status = "M"; //(М = заключена сделка)
   end;

   if (not Perform)
      req.rec.status = "W"; //(W = снята участником)
   end;

   if (not req.Insert() )
      reqid = 0;
   else
      reqid = req.rec.id;
      addnoteforobject(149,string(reqid:34:o),101,oper)
   end;

   Spgrdoc.clear();
   Spgrdoc.rec.sourcedockind = KIND; 
   Spgrdoc.rec.sourcedocid   = reqid;//ид из dl_req
   Spgrdoc.rec.spgroundid    = spgroundid; //ид документа в dspground_dbt
   Spgrdoc.rec.status        = "";

   if (not Spgrdoc.insert())
      reqid = 0;
   end;

   if (reqid > 0)
      rslDefCon.commitTrans ();
   else
      rslDefCon.rollbackTrans ();
   end;

   return reqid;

OnError ( err )
   if ( rslDefCon.isInTrans () )
      rslDefCon.rollbackTrans ();
   end;
   return 0;
End;

Macro Splt (str_,char_):tarray;
   Var splitted=tarray;
   Var ind=1;
   Var pos=1;
   splitted[1]="";
   while(pos<=strlen(str_))
      if (substr(str_,pos,1)==char_)
         splitted[ind]=trim(splitted[ind]);
         ind=ind+1;
         splitted[ind]="";
         pos=pos+1;
      else
         splitted[ind]=splitted[ind]+substr(str_,pos,1);
         pos=pos+1;
      end;
   end;
   return splitted;
End;

Macro NormValue (vl)
  if (valtype (vl) == 26)
     return "";
  end;
  if (vl == "")
     return "Undefined";
  end;
  
  return vl;
End;

Macro getFi (id)

   var ret ="";
   var req = rsdcommand ("select t_avoirkind from dfininstr_dbt f where f.t_fiid = :id");
   req.AddParam (":id", RSDBP_IN, id);
   var y = rsdrecordset (req);
   if (y.movenext)
      ret = y.value (0);
   end;
   return ret;

End;

Macro Check_req (id)
   var sql = rsdcommand ("select * from ddl_req_dbt ddl_req where ddl_req.t_code = :id");
   sql.AddParam (":id", RSDBP_IN, id);

   var z = rsdrecordset (sql);

   if (z.MoveNext())
      println ("Сделка с кодом "+id+" уже заведена");
      return FALSE;
   end;
   return TRUE;
End;

Macro CheckTypePaper (id)
   var get = rsdcommand  ("select 1 from dfininstr_dbt f, davrkinds_dbt k where f.t_fi_kind = 2 and f.t_fiid = :1 and k.t_fi_kind =2 and k.t_root = 17 and k.t_avoirkind = f.t_avoirkind ");
   get.AddParam (":1", RSDBP_IN, id);
   var m = rsdrecordset (get);
   if (m.movenext)
      return FALSE;
   end;
   return TRUE;

End;

Macro CheckClient (id, code)
  var get = rsdcommand ("select cd.t_objectid from dobjcode_dbt cd where cd.t_objecttype = 3 and cd.t_codekind = 8 and cd.t_code = :1");
   get.AddParam (":1", RSDBP_IN, code);
   var m = rsdrecordset (get);
   if (m.movenext)
      if (m.value ("t_objectid") == id)
        return TRUE;
      else
        msgbox ("Не найден клиент с кодом "+code);
        return FALSE;
      end;
   end;
   return FALSE;
End;

/*Поиск сделки, если заявка исполнена, если не исполнена то ищем клиента и бумагу*/
Macro Get_tick (Dealdate,dealtype, price, principal, isin, code, nameClient, tm, Perform, oper, p_Cnt, p_Trans_Type)
   var _DealID = -1,_Code,  ErrorText, _CFI, sql, x, _Client, _Fiid, _ClientContr, _ContrAgent, x_ReqID;
   var _Codets     = "";
   var _Price      = price;
   var _Amount     = principal;
   var _OrderTime  = tm;
   var _OrderDt    = dealdate;
   var _doclog     = DOCLOG_SECUR;
   var _MarketType = DL_SECURITYDOC;
   var _pfikind    = 0;
   var _Currency   = 0;
   var _Pricetype  = -1;
   var _RepoRate   = 0;
   var isError     = false;
   var _Direction  = 0;
   var _MarketKind = 0; /*вообще должен быть DV_MARKETKIND_STOCK, но не будем ничего менять*/
   var _MarketID   = -1;  
   
   Var fininstr = TRecHandler ("fininstr");

   if (dealtype == 1)
      _Direction = 1;
   else
      _Direction = 2;
   end;

//   if (perform)
   sql ="select count (1) over () cnt, tick.t_dealdate, tick.t_clientcontrid, tick.t_dealid, tick.t_dealcode, tick.t_dealcodets, tick.t_partyid,  "+
   "                     tick.t_clientid, tick.t_pfi, leg.t_cfi, leg.t_principal, leg.t_price, upper(party.t_name) name                                  "+
   "              from                                                                                                                                   "+
   "               ddl_tick_dbt tick                                                                                                                     "+
   "               left join ddl_leg_dbt leg on TICK.T_DEALID = LEG.T_DEALID                                                                             "+
   "               left join davoiriss_dbt  avo on TICK.T_PFI = AVO.T_FIID                                                                               "+
   "               left join dparty_dbt party on tick.t_clientid = party.t_partyid                                                                       "+
   "               left join ddlcontrmp_dbt mp  on tick.t_clientcontrid = MP.T_SFCONTRID                                                                 "+
   "               where                                                                                                                                 "+
   "                  rsb_secur.isbuy(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(tick.t_dealtype, 101))) = :1                               "+
   "                  and round(leg.t_price, 4) = Round(:2, 4)                                                                                           "+/*PNV 531181 было <=*/
   "                  and leg.t_principal = :3                                                                                                           "+
   "                  and avo.t_isin = :4                                                                                                                "+
   "                  and mp.t_dlcontrid = (select distinct dl1.t_dlcontrid  from ddlcontrmp_dbt dl1 where dl1.t_mpcode = :5)                            "+
   "                  and tick.t_dealdate = :6                                                                                                           ";

   if (Perform) /*PNV 529982 отмененные заявки к отмененным сделкам*/
      sql = sql +   "                  and leg.t_rejectdate = TO_DATE('01.01.0001', 'dd.mm.yyyy') ";
   else
      sql = sql +   "                  and leg.t_rejectdate <> TO_DATE('01.01.0001', 'dd.mm.yyyy') ";
   end;
      
   sql = rsdcommand (sql);
   
   sql.AddParam (":1", RSDBP_IN, dealtype);
   sql.AddParam (":2", RSDBP_IN, price);
   sql.AddParam (":3", RSDBP_IN, principal);
   sql.AddParam (":4", RSDBP_IN, isin);
   sql.AddParam (":5", RSDBP_IN, code);
   sql.AddParam (":6", RSDBP_IN, dealdate);
   x = rsdrecordset (sql);
   
   if (x.movenext)
      fininstr.clear();
      _DealID     = x.value("t_dealid");
      _Code       = x.value("t_dealcode");
      _Client     = x.value("t_clientid");
      _Fiid       = x.value("t_Pfi");
      _ClientContr= x.value("t_clientcontrid");
      _ContrAgent = x.value("t_partyid");
      _CFI        = x.value("t_cfi"); 
      if (x.value("cnt") >1)
         iserror = true;
         ErrorText = string ("Ошибка импорта заявки. найдено "+x.value("cnt")+ "сделок для клиента "+x.value("name")+" /"+code +"|ценная бумага "+isin+"|цена "+price+" количество "+principal);
      end;
   else
      iserror = true;
      ErrorText =string("Ошибка импорта заявки. Не найдено ни одной сделки |для клиента "+NameClient+" /"+code +"|ценная бумага "+isin+"|цена "+price+" количество "+principal+"| Заявка не будет сформирована");
   end;
//   end;

   if (_DealID > 0)
      if (not iserror)
         if (CheckTypePaper (_Fiid))
            _pfikind    = getFi (_fiid);
            _Currency   = _CFI;
         else
            _Pricetype  = 2;
            _pfikind    = 2;
            _Currency   = -1;
         end;
   
         if (ПолучитьФинИн (_Fiid, fininstr) != 0)
            continue;
         end;
   
         var _IsExchange = "";
         var _PriceFiID  = fininstr.rec.facevaluefi;
      
         if (FI_IsBond(fininstr.rec.fiid)) 
             _pricetype = 2;
         else
             _pricetype = 1;
         end;
     
      end;
   end;


   if (isError)
      Println (errortext);
      CountERR = CountERR + 1;
   else
      //if (perform)
         x_ReqID = СоздатьНовоеПоручение(_Code,
                                   _Codets,
                                   _OrderDt,
                                   tm,
                                   _ContrAgent,
                                   _Client,
                                   _fiid,
                                   _Amount,
                                   _Price,
                                   _pricetype,
                                   _PriceFiID,
                                   _DealID,
                                   _Currency,
                                   _Direction,
                                   _ClientContr,
                                   _RepoRate,
                                   _IsExchange,
                                   _pfikind,
                                   _doclog,
                                   _MarketType,
                                   _Code,
                                   Perform,
                                   oper,
                                   p_Trans_Type,
                                   _MarketKind,
                                   _MarketID   
                                  );
      //else
      //   stat = 1;
      //end;

      if (x_ReqID == 0)
         Println ("Ошибка импорта заявки. Клиент "+NormValue(x.value("name"))+" /"+code +"|ценная бумага "+isin+"|цена "+price+" количество "+principal);
         CountERR = CountERR + 1;
      else
         CountOK = CountOK + 1;

      end;

   end;
   [
   ================================================================================================================================
   ];
   [Загружено заявок с ошибками #######] (CountERR);
   [Всего загружено             #######] (CountOK);

End;


private macro DeleteFileToAppServ(FullNameFile)
  if(not isStandAlone()) // когда мы на терминале, удаляем файл на СП
    if (not RemoveFile (FullNameFile))
      println ("Error remove file");
    end;
  end;
end;

private macro SaveFileToAppServ(filepath:string, FileName):string
    
  if(not isStandAlone()) // когда мы на терминале, копируем файл на СП     
    var path_appserv = "..\\workfile\\" + FileName;
      
    if(not CopyFile("$" + filepath, path_appserv))
      msgbox("Ошибка при передаче файла на сервер приложений");
    end;
    filepath = path_appserv;
  end;
    
  return filepath;
end;

private macro CheckInt(val : string) : integer
  if ( StrIsNumber(val) )
    return int(val);
  end;
  RunError("Wrong number");
end;

macro ConvStrToDate(val : string) : date
  if (  ( StrLen(val)        != 10    )
     or ( SubStr(val, 3, 1)  != "."   )
     or ( SubStr(val, 6, 1)  != "."   )
     )
    RunError("FormatError");
  end;
  return date (  CheckInt( SubStr(val, 1, 2) )
              ,  CheckInt( SubStr(val, 4, 2) )
              ,  CheckInt( SubStr(val, 7, 4) )
              );
end;

private macro ConvStrToTime(val : string) : time
  if (  ( SubStr(val, 3, 1)  != ":" )
     or ( SubStr(val, 6, 1)  != ":" )
     )
    RunError("FormatError");
  end;
  return time (  CheckInt( SubStr(val, 1, 2) )
              ,  CheckInt( SubStr(val, 4, 2) )
              ,  CheckInt( SubStr(val, 7, 2) )
              );
end;


private MACRO DefaultParm( parm, value )
    if ( parm == NULL )
       SetParm( 0, value );
    end;
END;

private MACRO SplitString( str, delim, save_delim, fieldsCount )
    DefaultParm(delim, ";");
    DefaultParm(save_delim, false);
    DefaultParm(fieldsCount, 15);

    var arr = TArray(fieldsCount, fieldsCount), i = StrBrk( str, delim ), escCharPos = 0;

    while( i > 0 )
        if (save_delim)
            if (substr( str, 1, 1 ) == "\"")
                escCharPos = StrBrk( str, "\"" );
                arr( arr.size ) = substr( str, 1, escCharPos+1 );
                str = substr( str, escCharPos + 1 );
            else
                arr( arr.size ) = substr( str, 1, i );
                str = substr( str, i + 1 );
            end;
        else
            if (substr(str, 1, 1) == "\"")
                escCharPos = StrBrk( substr(str,2), "\"" );
                arr( arr.size ) = substr( str, 2, escCharPos - 1 ); // забираем без ""
                str = substr( str, escCharPos + 2 );
            else
                arr( arr.size ) = trim( substr( str, 1, i - 1 ) );
                str = trim( substr( str, i + 1 ) );
            end;
        end;
        i = StrBrk( str, delim );
    end;

    if( str != "" )
      if (save_delim)
        arr( arr.size ) = str;
      else
        arr( arr.size ) = trim( str );
      end;
    end;

    return arr;
  END;

Private macro LoadData

    var fName, fNameOnAppServ, fileName, extName, line = 0;
    var tmpStr = "", save_delim = false;
    var tmpElementsArr:tArray = tarray();
    const NMarket = 8;  //Внебиржевой рынок - колонок в файле

    var N0 = ""; //Дата поручения
    var N1 = ""; //Время поручения                      
    var N2 = ""; //Наименование (ФИО)/ Код клиента             
    var N3 = ""; //ФИО работника,принявшего поручение
    var N4 = ""; //Способ подачи поручения
    var N5 = ""; //Наименование электронной системы
    var N6 = ""; //Информация об операции, на осуществление которой клиентом подано поручение (ISIN (номер государственной регистрации), эмитент, цена, количество)
    var N7 = ""; //Примечание не исполнена или исполнена

    if ( not SelectFile (fName, (PathIn+"*.*"), NULL, NULL, TRUE) )
        return FALSE;
    end;
    
    //Отделяем путь файла от имени
    splitfile(fName, fileName, extName);
    fileName = fileName + extName;
    fNameOnAppServ = SaveFileToAppServ(fName, fileName);

    var ob = TStreamDoc(fNameOnAppServ, "R", "lcansi");
    var v;

    ob.readLine(@v);
    tmpStr = v;
    tmpElementsArr = SplitString( tmpStr, ";", save_delim );

    if (tmpElementsArr.Size() == NMarket)
       if (msgboxex ("Загрузка заявок по журналу Внебиржевого рынка ?", MB_YES+MB_NO, IND_YES)==IND_YES)      
           InitProgress(-1, NULL, "Чтение файла");
           while(ob.readLine(@v))
               UseProgress(line = line+1);
               tmpStr = v;
               tmpElementsArr = SplitString( tmpStr, ";", save_delim, NMarket );
               
               if ( NotNull (tmpElementsArr[0]) )
                  N0 = ConvStrToDate (tmpElementsArr[0]);
                  N1 = ConvStrToTime (tmpElementsArr[1]); 
                  N2 = NormValue(trim (tmpElementsArr[2])); 
                  N3 = NormValue(trim (tmpElementsArr[3])); 
                  N4 = NormValue(trim (tmpElementsArr[4])); 
                  N5 = NormValue(trim (tmpElementsArr[5])); 
                  N6 = NormValue(trim (tmpElementsArr[6])); 
                  N7 = NormValue(trim (tmpElementsArr[7]));
               end;

               println (n0, " ",N1," ",N2," ","-",N3," ",N4," ",N5," ",N6," ",N7);

               Var data = Splt (N6,";");
               Var price = money (trim(substr(strSubst(data[2],",","."), 6)));
               Var amount = int (trim(STRSUBST (substr(data[3], 5), " ", "")));
               Var type_operation = 0;

               if (substr(trim(data[4]),1,1) == "B")
                  type_operation = 1;
               else
                  type_operation = 0;
               end;
               Var isin = trim(data[1]);
               Var Cln = Splt (N2,"/");
               Var FIO = Cln[1];
               Var ObjCode = Cln[2];

               if (strupr(substr(N7, 1, 2)) == "НЕ")
                  IsPerform = FALSE;
               else
                  IsPerform = TRUE;
               end;

               //DEF-66488 способ передачи
               var Trans_type = FindNameAlg ( N4 );

               if ( NotNull (tmpElementsArr[0]) )
                  Get_tick (N0, type_operation, price, amount, isin, ObjCode, FIO, N1, IsPerform, N3, line, Trans_type);
               end;
           end;
       RemProgress ();
       ob = NULL;
       DeleteFileToAppServ(fNameOnAppServ);
       else
           msgboxex ("Отказ от загрузки заявок по журналу Внебиржевого рынка");
           ob = NULL;
           DeleteFileToAppServ(fNameOnAppServ);
           return FALSE;
       end;
    else
        MsgBox ("Нарушена структура файла.|Заголовок таблицы не содержит необходимое количество колонок "+NMarket);
        ob = NULL;
        DeleteFileToAppServ(fNameOnAppServ);
        return FALSE;
    end;

End;

LoadData;