/*
$Name:             NpTxRepoReg_Data.mac
$Module:           Ценные бумаги 
$Description:      Данные для отчет "Регистры РЕПО" (НДФЛ)
*/

IMPORT Проценты, "globals.mac", "spCache.mac", "DLReport_h.mac", "spserv.mac", "spRepFun.mac", "nptxfun.mac";
import "RegistrReport.mac";

/*значения категорий объектов*/
PRIVATE CONST STR_NO  = "Нет",
              STR_YES = "Да";

PRIVATE CONST ItogsLineName = "Всего:";

PRIVATE CONST PRC_CONST = 100.0;

PRIVATE CONST CL_STATE_RES    = 1; //резидент
PRIVATE CONST CL_STATE_NOTRES = 2; //нерезидент
PRIVATE CONST CL_STATE_NOTPAY = 3; //не плательщик

//Статусы отбираемых сделок
PRIVATE CONST REPOREG_DEALSTATUS_ALL     = 0,  //Все
              REPOREG_DEALSTATUS_CLOSE   = 1,  //Закрытые
              REPOREG_DEALSTATUS_NOCLOSE = 2;  //Незакрытые


PRIVATE VAR   T_Dep:T_Dep_Collector;

//Отбор и подготовка данных для отчета
//Исключительно данные - никакой привязки к форме и печате отчета
CLASS (TX_RegistrReport) NPTX_RepoReg_Data(Year, BeginDate, EndDate, ClientID, AvrFIID, AvrKind, AvrGroup, DealStatus, IIS, ContractIDsStr)
  PRIVATE VAR m_cmd;

  PRIVATE VAR m_Year           = Year,
              m_BeginDate      = BeginDate,
              m_ClientID       = ClientID,
              m_AvrFIID        = AvrFIID,
              m_AvrKind        = AvrKind,
              m_AvrGroup       = AvrGroup,
              m_DealStatus     = DealStatus,
              m_IIS            = IIS,
              m_ContractIDsStr = ContractIDsStr;

  PRIVATE VAR m_ClientStatusID, m_Tot1vp, m_PayVp, m_PayRub, m_PayN, 
              m_PaySecCnt, m_PaySecTot, m_Deal2vp, 
              m_DohVp, m_DohRub, m_RashVp, m_RashRub,
              m_TaxPashVp, m_TaxPashRub, m_GenRub, m_MrktSum, m_MrktSumVp, 
              m_ComVp, m_Com, m_Yrepo,
              m_CoupCalcVn, m_AmortCalcVn, m_StRate, m_DifRate, m_SecType;

  m_EndDate = EndDate;


  MACRO ClearCacheOneRecord()
     m_ClientStatusID = NULL;
     m_Tot1vp = NULL;
     m_PayVp  = NULL;
     m_PayRub = NULL;
     m_PayN   = NULL;
     m_PaySecCnt = NULL;
     m_PaySecTot = NULL;
     m_Deal2vp   = NULL;
     m_DohVp   = NULL;
     m_DohRub  = NULL;
     m_RashVp  = NULL;
     m_RashRub = NULL;
     m_TaxPashVp  = NULL;
     m_TaxPashRub = NULL;
     m_GenRub     = NULL;
     m_MrktSum    = NULL;
     m_MrktSumVp  = NULL;
     m_Com     = NULL;
     m_ComVp   = NULL;
     m_Yrepo   = NULL;
     m_CoupCalcVn  = NULL;
     m_AmortCalcVn = NULL;
     m_StRate  = NULL;
     m_DifRate = NULL;
     m_SecType = NULL;
  END;

  PRIVATE MACRO Get_str_PaymDate(Type, DealPart, IsMax:bool)
     var MinMax = iif(IsMax,"max","min");

     var sql_PlanDate:String = 
        " (NVL( (SELECT "+MinMax+"(dlrq.t_PlanDate) " +
        "  FROM ddlrq_dbt dlrq " + 
        "  WHERE dlrq.t_DocKind    = Deal.t_BOfficeKind " +
        "     AND dlrq.t_DocID = Deal.t_DealID " +
        "     AND dlrq.t_Type    = " + string(Type) + 
        "     AND dlrq.t_DealPart    = " + string(DealPart) + 
        "     AND dlrq.t_State = " + string(DLRQ_STATE_PLAN) +  
        "     ), TO_DATE('01-01-0001','DD-MM-YYYY') )" +
        " ) ";

     var sql_FactDate:String = 
        " (NVL( (SELECT "+MinMax+"(dlrq.t_FactDate) " +
        "  FROM ddlrq_dbt dlrq " + 
        "  WHERE dlrq.t_DocKind    = Deal.t_BOfficeKind " +
        "      AND dlrq.t_DocID = Deal.t_DealID " +
        "      AND dlrq.t_Type    = " + string(Type) + 
        "      AND dlrq.t_DealPart    = " + string(DealPart) + 
        "      AND dlrq.t_State = " + string(DLRQ_STATE_EXEC) +
        "      AND dlrq.t_FactDate > TO_DATE('01-01-0001','DD-MM-YYYY') " +
        "      ), TO_DATE('01-01-0001','DD-MM-YYYY') ) " +
        " ) ";

     return 
        " (DECODE( " +
             sql_FactDate + ", " +                   // Выражение
        "    TO_DATE('01-01-0001','DD-MM-YYYY'), " + // search1
             sql_PlanDate + ", " +                   // result1
             sql_FactDate + ") " +                   // default
        " ) ";
  END;


  PRIVATE MACRO CreateQuery()
     VAR DataQuery, ClientID, AvrFIID, Num = 0, AvrKind, AddWhere = "", AvrGroup;

     T_Dep.ClearArray();

     /*Клиент*/
     if( (m_ClientID != null) AND (m_ClientID > 0 ) ) /*задан клиент*/
        AddWhere = AddWhere + " AND Party.t_PartyID = " + string(m_ClientID);
     end;

     /*ценные бумаги в соединенных сделках  должны удовлетворять условиям, заданным панели подготовки отчета*/
     if( (m_AvrFIID != null) AND (m_AvrFIID > 0 ) ) /*задана бумага*/
        AddWhere = AddWhere + " AND Deal.t_PFI = " + string(m_AvrFIID);
     else
        if( (m_AvrKind != null) AND (m_AvrKind > 0 ) ) /*задан вид бумаги*/
           AddWhere = AddWhere + " AND RSB_FIInstr.FI_AvrKindsEQ(2, " + string(m_AvrKind) + ", FinInstr.t_AvoirKind) = 1 ";
        end;

        if( (m_AvrGroup != null) AND (m_AvrGroup > 0 ) ) /*задана категория бумаги*/
           AddWhere = AddWhere + " and "+ Get_NpTxAvrGroup_Query(m_AvrGroup,"FinInstr.t_FIID");
        end;
     end;
                                                          
     if(m_DealStatus == REPOREG_DEALSTATUS_ALL)
        /* a. меньшая из дат по оплате или поставке по 1 части меньше или равна дате окончания отчетного периода */
        AddWhere = AddWhere + " AND "
                              " (case when "+Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 1)+" < "+Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  1)+
                              "       then "+Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 1)+
                              "       else "+Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  1)+
                              "   end "+
                              " ) <= " + GetSQLDate(date(m_EndDate)) + " AND " +

                              /* b. большая из дат по оплате или поставке по 2 части больше или равна дате начала отчетного периода */
                              " (case when "+Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 2, true)+" > "+Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  2, true)+
                              "       then "+Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 2, true)+
                              "       else "+Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  2, true)+
                              "   end "+
                              " ) >= " + GetSQLDate(date(m_BeginDate)); 

     elif(m_DealStatus == REPOREG_DEALSTATUS_CLOSE)
        /* b. большая из фактических дат по оплате или поставке по 2 части входит в отчетный период */
        AddWhere = AddWhere + " AND "
                " (SELECT MAX( dlrq.t_FactDate ) " +
                "    FROM ddlrq_dbt dlrq " + 
                "   WHERE dlrq.t_DocKind    = Deal.t_BOfficeKind " +
                "     AND dlrq.t_DocID = Deal.t_DealID " +
                "     AND (dlrq.t_Type   = " + string(DLRQ_TYPE_DELIVERY) + " OR dlrq.t_Type   = " + string(DLRQ_TYPE_PAYMENT) + ") " +
                "     AND dlrq.t_DealPart = 2 "+
                " ) BETWEEN " + GetSQLDate(date(m_BeginDate)) + " AND "  + GetSQLDate(date(m_EndDate)); 

     elif(m_DealStatus == REPOREG_DEALSTATUS_NOCLOSE)
        AddWhere = AddWhere + " AND "
                 /*c. меньшая из фактических (при отсутствии - плановых) дат по оплате или поставке по 1 части меньше или равна дате окончания отчетного периода*/
                " (case when "+Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 1)+" < "+Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  1)+
                "       then "+Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 1)+
                "       else "+Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  1)+
                "   end "+
                " ) <= " + GetSQLDate(date(m_EndDate)) + " AND " +

                /* большая из фактических (при отсутствии - плановых) дат по оплате или поставке по 2 части больше дате окончания отчетного периода*/
                " (case when "+Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 2, true)+" > "+Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  2, true)+
                "       then "+Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 2, true)+
                "       else "+Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  2, true)+
                "   end "+
                " ) > " + GetSQLDate(date(m_EndDate));
     end;

     if(m_IIS != null)
        AddWhere = AddWhere + " AND RSI_NPTO.CheckContrIIS(Deal.t_ClientContrID) = " + string(m_IIS);
     end;

     if((m_ContractIDsStr != NULL) and (m_ContractIDsStr != ""))
       AddWhere = AddWhere + " and (Deal.t_ClientContrID IN ("+m_ContractIDsStr+") ";
     end;
     
     DataQuery = 
       " SELECT   Party.t_PartyID Investor, Party.t_ShortName ClientName, " +
                " FinInstr.t_FIID FIID, FinInstr.t_SumPrecision SumPrecision, FinInstr.t_FI_Code AS AvrCode, FinInstr.t_Name AS AvrName, FinInstr.t_FaceValueFI, " +
                " Deal.t_DealID, Deal.t_BOfficeKind, Deal.t_Department, Deal.t_DealCodeTS, Deal.t_DealDate DZ, Deal.t_Blocked, Deal.t_IsPartyClient, " +
                " dlleg_1.t_Principal, dlleg_2.t_IncomeRate Rate, dlleg_1.t_Basis, dlleg_1.t_Point Point, dlleg_1.t_CFI VP, dlleg_1.t_PayFIID, " +
                "decode(" +
                "         RSB_SECUR.IsRepo(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(Deal.t_DealType, Deal.t_BOfficeKind))), " +
                "         1, " +
                "         RSB_SECUR.IsSale(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(Deal.t_DealType, Deal.t_BOfficeKind))), " +
                "         0" +
                "      ) as t_IsRepo, "

                " NPTO.IfMarket(FinInstr.t_FIID,Deal.t_DealDate) IfMarket, " +

                "        NVL( (Select p.t_Name " +
                "                From dparty_dbt p " +
                "               Where p.t_PartyID = case when Deal.t_IsPartyClient = 'X' then Deal.t_ClientID else Deal.t_PartyID end " +
                "             ), CHR(1) " +
                "           ) as DealPartyName, " +
                Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 1) + " DD1, " +
                Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  1) + " DP1, " +
                Get_str_PaymDate(DLRQ_TYPE_DELIVERY, 2) + " DD2, " +
                Get_str_PaymDate(DLRQ_TYPE_PAYMENT,  2) + " DP2,  " +
                " case when Deal.t_IsPartyClient = 'X' then 1 else 0 end IsPartyClient, " +
                " NVL((SELECT MAX(rq.t_FactDate) " +
                "        FROM ddlrq_dbt rq " +
                "       WHERE rq.t_DocKind = Deal.t_BOfficeKind " +
                "         AND rq.t_DocID = Deal.t_DealID " +
                "         AND rq.t_Type = " + DLRQ_TYPE_PAYMENT+
                "     ), "+GetSQLDate(date(0,0,0))+") as MaxPayRqFactDate, "+
                " COALESCE((SELECT MAX (T_OPERDATE) " +
                "           FROM DNPTXOP_DBT txop " +
                "          WHERE     txop.t_client = Party.t_PartyID " +
                "                AND txop.t_dockind = " + DL_CALCNDFL +
                "                AND txop.t_Recalc = CHR(0) "
                "                AND txop.T_STATUS = " + DL_TXOP_Close +
                "                AND txop.T_OPERDATE BETWEEN " + GetSQLDate(date(m_BeginDate)) + " AND "  + GetSQLDate(date(m_EndDate)) +
                "                AND NOT EXISTS " +
                "                           (SELECT 1 " +
                "                              FROM dobjatcor_dbt objatcor " +
                "                             WHERE objatcor.t_Object = LPAD (txop.t_id, 34, 0) " +
                "                                   AND objatcor.t_objecttype = " + OBJTYPE_NPTXCALC +
                "                                   AND objatcor.T_GROUPID = 1 " +
                "                                   AND objatcor.T_ATTRID = 1) " +
                "             ), "+GetSQLDate(date(0,0,0))+") AS t_LastNoTechCalc, "+
                " NVL((SELECT sf.t_Number " +
                "        FROM ddlcontrmp_dbt mp, ddlcontr_dbt dlc, dsfcontr_dbt sf " +
                "       WHERE mp.t_SfContrID = Deal.t_ClientContrID " + 
                "         AND dlc.t_DlContrID = mp.t_DlContrID " + 
                "         AND sf.t_ID = dlc.t_SfContrID " + 
                "     ), CHR(1)) as ContractS " +

       "   FROM " +
                " DDL_TICK_DBT  Deal," +
                " DFININSTR_DBT FinInstr, " + 
                " DDL_LEG_DBT   dlleg_1, " + 
                " DDL_LEG_DBT   dlleg_2, " + 
                " DPARTY_DBT    Party " + 
       "  WHERE " +
                " dlleg_1.t_LegKind = " + string(LEG_KIND_DL_TICK) +" AND " +
                " dlleg_1.t_DealID  = Deal.t_DealID AND " +
                " dlleg_1.t_LegID   = 0 AND "+
                " dlleg_2.t_LegKind = " + string(LEG_KIND_DL_TICK_BACK) +" AND "+
                " dlleg_2.t_DealID  = Deal.t_DealID AND " +
                " dlleg_2.t_LegID   = 0 AND "+

                " FinInstr.t_FIID = dlleg_1.t_PFI AND " +
                " Party.t_PartyID = Deal.t_ClientID AND " +
                " RSB_SECUR.IsRepo(RSB_SECUR.get_OperationGroup(RSB_SECUR.get_OperSysTypes(Deal.t_DealType, Deal.t_BOfficeKind))) = 1 " +
                AddWhere;

     DataQuery = "select q.* from ("+DataQuery+") q order by q.DZ ASC";
     
     m_cmd = DL_RSDCommand(DataQuery);

     m_RS = NULL;

     m_CacheFinInstr = TX_CacheFinInstr();
  END;

  MACRO GetNext()
    this.ClearCacheOneRecord();

    if(m_RS == NULL)
      m_RS = m_cmd.Execute();
    end;

    RETURN m_Rs.moveNext();
  END;

  MACRO GetCount();
    return m_cmd.GetCount();
  END;

  PRIVATE MACRO IsREPO() // прямое РЕПО
     return (m_RS.IsRepo == 1);
  END;

  PRIVATE MACRO IsBackREPO() // обратные РЕПО
     return (m_RS.IsRepo == 0);
  END;

  MACRO H0_1():DATE
     return m_BeginDate;
  END;

  /*Отчетная дата, указанная в форме запуска отчета*/
  MACRO H0_2():DATE
     return m_EndDate;
  END;

  MACRO D1R():DATE
     return IIF(this.DD1 > this.DP1, this.DD1, this.DP1);
  END;

  MACRO D2R():DATE
     return IIF(this.DD2 > this.DP2, this.DD2, this.DP2);
  END;

  PRIVATE MACRO GetCurrentLotAmountOnDate(DealID, DrawingDate)
     var cmd, rsd;
     var Amount = 0.0;

     cmd = RSDCommand(
           " SELECT NVL(Sum(t_Amount), 0) as Amount FROM DNPTXLOT_DBT "
         + "  WHERE t_DocID = ? "
         + "    AND ? > (CASE WHEN t_Kind = "+NPTXLOTS_BACKREPO+" THEN t_BuyDate ELSE t_SaleDate END) "
         + "    AND ( ? <= (CASE WHEN t_Kind = "+NPTXLOTS_BACKREPO+" THEN t_SaleDate ELSE t_BuyDate END) "
         + "        OR "+GetSQLDate(date(0,0,0))+" = (CASE WHEN t_Kind = "+NPTXLOTS_BACKREPO+" THEN t_SaleDate ELSE t_BuyDate END)"
         + "        ) "
                     );

     cmd.addParam( "", RSDBP_IN, DealID );
     cmd.addParam( "", RSDBP_IN, DrawingDate );
     cmd.addParam( "", RSDBP_IN, DrawingDate );

     cmd.execute();

     rsd = TRsbDataSet(cmd);
     if(rsd.MoveNext())
       Amount = rsd.Amount;
     end;

     return Amount;
  END;


  MACRO GetCouponSumByPeriodUseLots(DealID, FIID, DateBeg, DateEnd, IsPartial)
      var cmd, rsd;
      var Sum = 0.0, S, NumCoupon = 0, Amount = 0.0;

      cmd = RSDCommand(
            " SELECT FI.t_FaceValue, warnt.t_DrawingDate as DrawingDate, "
          + "        warnt.t_IncomeRate, warnt.t_IncomeScale "
          + "   FROM dfiwarnts_dbt warnt, dfininstr_dbt FI "
          + "  WHERE warnt.t_FIID = ? and "
          + "        warnt.t_IsPartial = " + IIF(IsPartial==true,"chr(88)", "chr(0)") + " and "
          + "        warnt.t_DrawingDate <= ? and "
          + "        warnt.t_DrawingDate >= ? and "
          + "        FI.t_FIID = warnt.t_FIID "
          + " ORDER BY warnt.t_DrawingDate ASC "
                      );

      cmd.addParam( "", RSDBP_IN, FIID );
      cmd.addParam( "", RSDBP_IN, DateEnd );
      cmd.addParam( "", RSDBP_IN, DateBeg );

      cmd.execute();
      rsd = TRsbDataSet(cmd);
      while(rsd.MoveNext())
         //для каждого купона (ЧП) находим количество ц/б из действующего лота
         Amount = GetCurrentLotAmountOnDate(DealID, date(rsd.DrawingDate));

         /*Сумма частичных погашений*/
         if (IsPartial == true)
            S = Amount * rsd.FaceValue * rsd.IncomeRate/MAX( 1, rsd.IncomeScale)/100.0;
         /*Сумма купонов*/
         else
            S = СуммаКупона(FIID, Amount, SQL_ConvTypeDate(rsd.DrawingDate) );
         end;

         Sum = Sum + S;
      end;                                                                  

      return Sum;
  END;

  MACRO MaxPayRqFactDate():DATE
     return m_RS.MaxPayRqFactDate;
  END;

  MACRO LastNoTechCalc():DATE
     return m_RS.t_LastNoTechCalc;
  END;


  MACRO Client():STRING
     return m_RS.ClientName;
  END;

  MACRO ClientStatusID():INTEGER
     var party, NumInList;

     if( m_ClientStatusID == NULL )
        party = TBFile("party.dbt");
        m_ClientStatusID = CL_STATE_RES;

        party.rec.PartyID = m_Rs.Investor;

        if( GetMainObjAttr( null, OBJTYPE_PARTY, UniID( party, OBJTYPE_PARTY), 42/*Является плательщиком НДФЛ*/, null, null, NumInList))
          if(NumInList == "1")
             m_ClientStatusID = CL_STATE_RES;
          elif(NumInList == "2")
             m_ClientStatusID = CL_STATE_NOTRES;
          else
             m_ClientStatusID = CL_STATE_NOTPAY;
          end;  
        end;
     end;
     return m_ClientStatusID;
  END;

  MACRO ClientStatus():STRING
    if (ClientStatusID() == CL_STATE_RES)
       return "резидент";

    elif (ClientStatusID() == CL_STATE_NOTRES)
       return "нерезидент";

    else
       return "не плательщик";
    end;
  END;

  MACRO SecCode():STRING
     return m_RS.AvrCode;
  END;
              
  MACRO SecName():STRING
     return m_RS.AvrName;
  END;

  MACRO ContractS():STRING
     return m_RS.ContractS;
  END;

  MACRO CodeR():STRING
     return m_RS.DealCodeTS;
  END;

  MACRO DZ():DATE
     return SQL_ConvTypeDate( m_RS.DZ );
  END;

  MACRO Qnty():MONEY
     return m_RS.Principal;
  END;

  MACRO QntyPrecision():INTEGER
     return m_RS.SumPrecision;
  END;

  MACRO VN():STRING
     return m_CacheFinInstr.GetISO( m_RS.FaceValueFI ).m_Number;
  END;

  MACRO VP():STRING
     return m_CacheFinInstr.GetISO( m_RS.VP ).m_Number;
  END;

  MACRO Dir1():STRING
     return IIF( IsBackREPO(), "покупка", "продажа" );
  END;

  MACRO DD1():DATE
     return SQL_ConvTypeDate( m_RS.DD1 );
  END;

  MACRO DP1():DATE
     return SQL_ConvTypeDate( m_RS.DP1 );
  END;

  MACRO Tot1vp():MONEY
     var Direction;
     if( m_Tot1vp == NULL )
        if( not IsBackREPO() )
           Direction = TXOBJ_DIR_IN;
        else
           Direction = TXOBJ_DIR_OUT;
        end;
        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_MAIN) + ", " + string(TXOBJ_NKD), 
                               null, this.H0_2(), Direction, @m_Tot1vp, null, null, m_Rs.Investor, m_Rs.PayFIID, true);
     end;
     return m_Tot1vp;
  END;

  MACRO Rate():DOUBLE
     return m_RS.Rate / PRC_CONST;
  END;

  PRIVATE MACRO GetPayVpAndPayN()
     var PayVpIn:money = $0.0, PayVpOut:money = $0.0;
     var PayRubIn:money = $0.0, PayRubOut:money = $0.0;
     var PayNIn:integer = 0, PayNOut:integer = 0;
     DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_PAY), null, this.H0_2(), TXOBJ_DIR_IN,  @PayVpIn,  @PayRubIn,  @PayNIn,  m_Rs.Investor, m_Rs.VP, true);
     DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_PAY), null, this.H0_2(), TXOBJ_DIR_OUT, @PayVpOut, @PayRubOut, @PayNOut, m_Rs.Investor, m_Rs.VP, true);

     if( IsBackREPO() )
        m_PayVp  = PayVpOut - PayVpIn;
        m_PayRub = PayRubOut - PayRubIn;
     else
        m_PayVp  = PayVpIn - PayVpOut;
        m_PayRub = PayRubIn - PayRubOut;
     end;

     m_PayN = PayNIn + PayNOut;
  END;

  MACRO PayN():INTEGER
     if( m_PayN == NULL )
        GetPayVpAndPayN();
     end;
     return m_PayN;
  END;

  MACRO PayVp():MONEY
     if( m_PayVp == NULL )
        GetPayVpAndPayN();
     end;
     return m_PayVp;
  END;

  /*Сумма и кол-во ТО вида */
  PRIVATE MACRO GetPaySecTotAndPaySecCnt( EndDate:DATE, PaymCount:@INTEGER, Summa:@MONEY )

     PaymCount = 0;
     Summa     = 0;

     var sql = RSDCommand(" SELECT dlrq.t_Amount, dlrq.t_Kind " +
                          "   FROM ddlrq_dbt dlrq " +
                          "  WHERE t_DocKind  = ? AND " +
                          "        t_DocID    = ? AND " +
                          "        t_Type     = ? AND " +
                          "        t_FactDate <= ? "
                         );

     sql.addParam( "", RSDBP_IN, m_RS.BOfficeKind );
     sql.addParam( "", RSDBP_IN, m_RS.DealID );
     sql.addParam( "", RSDBP_IN, DLRQ_TYPE_COMPDELIVERY );
     sql.addParam( "", RSDBP_IN, EndDate );
     sql.execute();

     VAR RS = TRsbDataSet(sql);

     while( RS.MoveNext() )
        if( IsBackREPO() )
           if( RS.Kind == DLRQ_KIND_REQUEST )
              Summa = Summa + RS.Amount;
           else //if( RS.Kind == DLRQ_KIND_COMMIT )
              Summa = Summa - RS.Amount;
           end;
        else
           if( RS.Kind == DLRQ_KIND_REQUEST )
              Summa = Summa - RS.Amount;
           else //if( RS.Kind == DLRQ_KIND_COMMIT )
              Summa = Summa + RS.Amount;
           end;
        end;

        PaymCount = PaymCount + 1;
     end;
  END;

  MACRO PaySecCnt():INTEGER
     if( m_PaySecCnt == NULL )
        GetPaySecTotAndPaySecCnt(min(this.DD2, this.H0_2()), @m_PaySecCnt, @m_PaySecTot);
     end;
     return m_PaySecCnt;
  END;

  MACRO PaySecTot():MONEY
     if( m_PaySecTot == NULL )
        GetPaySecTotAndPaySecCnt(min(this.DD2, this.H0_2()), @m_PaySecCnt, @m_PaySecTot);
     end;
     return m_PaySecTot;
  END;

  MACRO Dir2():STRING
     return IIF( IsBackREPO(), "продажа", "покупка" );
  END;

  MACRO DD2():DATE
     return SQL_ConvTypeDate( m_RS.DD2 );
  END;

  MACRO DP2():DATE
     return SQL_ConvTypeDate( m_RS.DP2 );
  END;

  MACRO Deal2vp():MONEY
     var Direction, Sm = $0, Sp_in = $0, Sp_out = $0;

     if( m_Deal2vp == NULL )

        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_PROCREPOPAY), 
                               null, this.H0_2(), TXOBJ_DIR_IN, @Sp_in, null, null, m_Rs.Investor, NULL, true);

        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_PROCREPOPAY), 
                               null, this.H0_2(), TXOBJ_DIR_OUT, @Sp_out, null, null, m_Rs.Investor, NULL, true);

        if( not IsBackREPO() )
           Direction = TXOBJ_DIR_OUT;
        else
           Direction = TXOBJ_DIR_IN;
        end;

        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_MAIN) + ", " + string(TXOBJ_NKD), 
                               null, this.H0_2(), Direction, @Sm, null, null, m_Rs.Investor, m_Rs.PayFIID, true);

        if(Direction == TXOBJ_DIR_OUT)
          m_Deal2vp = -Sp_in + Sp_out + Sm;
        elif(Direction == TXOBJ_DIR_IN)
          m_Deal2vp = -Sp_out + Sp_in + Sm;
        end;
     end;
     return m_Deal2vp;
  END;

  MACRO Tot2vp():MONEY
     return this.Deal2vp() - this.PayVp();
  END;

  MACRO Trepo():INTEGER
     if (this.D1R == this.D2R)
        return 1;
     else
        return this.D2R - this.D1R;
     end;
  END;

  MACRO DohVp():MONEY
     if( m_DohVp == NULL )
        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_PROCFACT), 
                               this.H0_1(), this.H0_2(), TXOBJ_DIR_IN, @m_DohVp, @m_DohRub, null, m_Rs.Investor);
     end;
     return m_DohVp;
  END;

  MACRO DohRub():MONEY
     if( m_DohRub == NULL )
        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_PROCFACT), 
                               this.H0_1(), this.H0_2(), TXOBJ_DIR_IN, @m_DohVp, @m_DohRub, null, m_Rs.Investor, NULL, true);
     end;
     return m_DohRub;
  END;

  MACRO RashVP():MONEY
     if( m_RashVp == NULL )
        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_PROCFACT), 
                               this.H0_1(), this.H0_2(), TXOBJ_DIR_OUT, @m_RashVp, @m_RashRub, null, m_Rs.Investor);
     end;
     return m_RashVp;
  END;

  MACRO RashRub():MONEY
     if( m_RashRub == NULL )
        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_PROCFACT), 
                               this.H0_1(), this.H0_2(), TXOBJ_DIR_OUT, @m_RashVp, @m_RashRub, null, m_Rs.Investor, NULL, true);
     end;
     return m_RashRub;
  END;

  MACRO TaxPashRub():MONEY
     if( m_TaxPashRub == NULL )
        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_PROCREPOTAX), 
                               this.H0_1(), this.H0_2(), TXOBJ_DIR_OUT, @m_TaxPashVp, @m_TaxPashRub, null, m_Rs.Investor, NULL, true);
     end;
     return m_TaxPashRub;
  END;

  MACRO GenRub():MONEY
     var ComSum = 0.0;
     var OutSum = 0.0;
     if( m_GenRub == NULL )
        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_PROCREPOTAX),
                               this.H0_1(), this.H0_2(), TXOBJ_DIR_IN, NULL, @m_GenRub, null, m_Rs.Investor, NULL, true);
        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_PROCREPOTAX),
                               this.H0_1(), this.H0_2(), TXOBJ_DIR_OUT, NULL, @OutSum, null, m_Rs.Investor, NULL, true);
        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_COM),
                               this.H0_1(), this.H0_2(), TXOBJ_DIR_ALL, NULL, @ComSum, null, m_Rs.Investor, NULL, true);

        m_GenRub = m_GenRub - OutSum - ComSum;
     end;
     return m_GenRub;
  END;

  MACRO DifRashRub():MONEY
     if( ( ( IsBackREPO() ) AND (this.Rate() < 0) ) OR
         ( ( not IsBackREPO() ) AND (this.Rate() > 0) )
       )
        return this.RashRub() - this.TaxPashRub();
     else
        return $0;
     end;
  END;

  MACRO MrktSum():MONEY
     if( m_MrktSum == NULL )
        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_COST), 
                               null, null, null, @m_MrktSumVp, @m_MrktSum, null, m_Rs.Investor);
     end;
     return m_MrktSum;
  END;

  MACRO bMrkt():STRING
     return IIF(m_RS.IfMarket == SET_CHAR, "обращается", "не обращается");
  END;

  MACRO Cont():STRING
     return m_RS.DealPartyName;
  END;

  MACRO Com():MONEY
     if( m_Com == NULL )
        DL_GetNPTXOBJSumByDeal(m_RS.DealID, string(TXOBJ_COM), 
                               null, this.H0_2(), null, @m_ComVp, @m_Com, null, m_Rs.Investor, NULL, true);
     end;
     return m_Com;
  END;

  MACRO Yrepo():INTEGER
     if( m_Yrepo == NULL )
        if( (m_RS.Basis == SP_KINDBASISCALC_365_CAL) or (m_RS.Basis == SP_KINDBASISCALC_365_30) )
           m_Yrepo = 365;
        elif( (m_RS.Basis == SP_KINDBASISCALC_360_30) or (m_RS.Basis == SP_KINDBASISCALC_360_CAL) )
           m_Yrepo = 360;
        else
           m_Yrepo = GetDaysInYear(m_Year);
        end;
     end;
     return m_Yrepo;
  END;

  MACRO MonthRepo():STRING
     if( (m_RS.Basis == SP_KINDBASISCALC_365_30) OR 
         (m_RS.Basis == SP_KINDBASISCALC_CAL_30) OR 
         (m_RS.Basis == SP_KINDBASISCALC_360_30) 
       )
        return "30";
     elif( (m_RS.Basis == SP_KINDBASISCALC_365_CAL) OR 
           (m_RS.Basis == SP_KINDBASISCALC_CAL_CAL) OR 
           (m_RS.Basis == SP_KINDBASISCALC_360_CAL) 
         )
        return "Actual";
     else
        return "";
     end;
  END;

  MACRO ifDone2():STRING
     if( this.D2R <= this.H0_2 ) 
        return STR_YES;
     else
        return STR_NO;
     end;
  END;

  MACRO CoupCalcVn():MONEY
     if( m_CoupCalcVn == NULL )
        m_CoupCalcVn = $0;
        if(FI_IsBond(m_RS.FIID))
          m_CoupCalcVn = GetCouponSumByPeriodUseLots( m_RS.DealID, m_RS.FIID, this.DD1+1, min( this.DD2, this.H0_2() ), false );
        end;
     end;
     return m_CoupCalcVn;
  END;

  MACRO AmortCalcVn():MONEY
     if( m_AmortCalcVn == NULL )
        m_AmortCalcVn = $0;
        if(FI_IsBond(m_RS.FIID))
          m_AmortCalcVn = GetCouponSumByPeriodUseLots( m_RS.DealID, m_RS.FIID, this.DD1+1, min( this.DD2, this.H0_2() ), true );
        end;
     end;
     return m_AmortCalcVn;
  END;

  MACRO DfixDiv():DATE
     VAR RS, sql;

//Заполняется только , если <SecCode> относится к группам 10
     if( this.SecType() == TXGROUP_10 )

        sql = RSDCommand("SELECT NVL( MAX(t_ListDate), TO_DATE('01-01-0001', 'DD-MM-YYYY') ) DfixDiv" +
                         "  FROM DFIWARNTS_DBT " +
                         " WHERE t_IsPartial = CHR(0) " +
                         "   AND t_FIID = ? "
                         "   AND t_ListDate BETWEEN ? AND ? ");
                                                                              
        sql.addParam( "", RSDBP_IN, m_RS.FIID );
        sql.addParam( "", RSDBP_IN, this.D1R );
        sql.addParam( "", RSDBP_IN, this.D2R );
        sql.execute();
        
        RS = TRsbDataSet( sql );

        if( RS.MoveNext() )
           return SQL_ConvTypeDate( RS.DfixDiv );
        end;
     end;
     return DATE( 0,0,0);
  END;

  MACRO StRate():DOUBLE
     if( m_StRate == NULL )
           if (not СтавкаРефинансированияЦБ (NATCUR, this.DP2, m_StRate))
              Msgbox("Не найдена ставка рефинансирования ЦБ РФ на ", this.DP2);
              m_StRate = 0.0;
           end;
        if( m_RS.PayFIID == NATCUR )
          m_StRate = m_StRate * 1.8;
        else
          m_StRate = m_StRate * 0.8;
        end;

        m_StRate = m_StRate / PRC_CONST;
     end;

     return m_StRate;
  END;

  MACRO DifRate():DOUBLE
     if( m_DifRate == NULL )
        if( ( ( IsBackREPO() ) AND (this.Rate() < this.StRate()) ) OR
            ( ( not IsBackREPO() ) AND (this.Rate() > this.StRate()) )
          )
            m_DifRate = abs(abs(this.Rate()) - this.StRate());
        else
            m_DifRate = 0.0;
        end;
     end;

     return m_DifRate;
  END;

  MACRO IsBlock()
    if( (IsREPO() or IsBackREPO()) and (m_RS.IsPartyClient == SET_CHAR) and (m_RS.Blocked == SET_CHAR) )
      return "Да";
    end;
    return null;
  END;

  MACRO Fil()
     return T_Dep.Get_Dep(m_RS.Department);
  END;

  MACRO SecType()
     if( m_SecType == null )
        m_SecType = DL_GetNPTXSecType(m_RS.FIID);
     end;
     return m_SecType;
  END;

  MACRO TaxRateGeneral()
    if (ClientStatusID() == CL_STATE_RES)
       return "13%";

    elif (ClientStatusID() == CL_STATE_NOTRES)
       var _stat = 1;
       var TaxRate = 0.0;
       if (m_EndDate != NULL)
         _stat = ПолучитьЗначениеПримечанияНаДату(m_ClientID, PARTY_NOTE_KIND_NPTX_RATE_NOTRES, m_EndDate, @TaxRate);
       end;
       if (_stat != 0)
         TaxRate = 30; //для нерезидента      
       end;    
       
       return string(TaxRate) + "%";

    else
       return "нет";
    end;
  END;

  MACRO Point():INTEGER //  точность ставки РЕПО
     return m_RS.Point;
  END;

  CreateQuery();

END;
