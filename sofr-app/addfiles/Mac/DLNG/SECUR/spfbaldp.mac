/*
$Name:         spfbaldp.mac
$Module:       Депозитарий
$Description:  Отчет "Полный баланс депо"
*/

import RsbDataSet, FIInter, CurrInter, deposerv;

var chapter = 5, num_plan = 0;
var IsPrintHead = false;
var type_rep_fi, SecurQual;
var rep_date;

private var LogOprID,
            fi_id,
            dprt_num,
            ToExcel,
            apostr;

var HeadTable = "┌───────────────────────────────────────┬───────────────────────────────────────┐\n"+
                "│Код синтетического счета депо          │ Остаток на дату составления отчета    │\n"+
                "├───────────────────────────────────────┼───────────────────────────────────────┤";

var tablewidth =Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

/*============================================================*/

macro header( IsIndividual, isQualified, lsin, finname, avrkname )

   IF(IsIndividual == "X")
     Rep.AddEmptyStr();
     Rep.AddEmptyStr();
     DP_AddPrintCell(Rep, "Наименование ценной бумаги: ", 28, 0, "l:" + RepFontStyleTitel, true, REP_ELEM_STR );
     DP_AddPrintCell(Rep, avrkname + IIF(isQualified, "", " (НКЦБ)"), tablewidth-28, 0, "l:w:" + RepFontStyleTitel, true, REP_ELEM_STR );
     Rep.AddStr();
     Rep.AddEmptyStr();
   ELSE /*эмиссионная*/
     Rep.AddEmptyStr();
     Rep.AddEmptyStr();
     DP_AddPrintCell(Rep, "Номер государственной регистрации выпуска ценных бумаг: ", 56, 0, "l:" + RepFontStyleTitel, true, REP_ELEM_STR );
     DP_AddPrintCell(Rep, lsin, tablewidth-56, 0, "l:w:" + RepFontStyleTitel, true, REP_ELEM_STR );
     Rep.AddStr();
     DP_AddPrintCell(Rep, "Наименование выпуска ценных бумаг: ", 44, 0, "l:" + RepFontStyleTitel, true, REP_ELEM_STR );
     DP_AddPrintCell(Rep, finname + IIF(isQualified, "", " (НКЦБ)"), tablewidth-44, 0, "l:w:" + RepFontStyleTitel, true, REP_ELEM_STR );
     Rep.AddStr();
     Rep.AddEmptyStr();
   END;

end;

macro WriteLine(Balance, rest, apostr, t_point)

   if ( rest != $0 )
        DP_AddPrintCell(Rep, Balance, 0, 0, "l:" + RepFontStyleTitel );
        DP_AddPrintCell(Rep, rest, 0, DP_GetPrecision(rest, t_point), "r:" + RepFontStyleTitel, apostr);
        Rep.AddStr();
   end;

end;

macro Footer(kind, rest_sum, apostr, t_point)

   var str;

   if ( kind == bal_acc_active )
      str = "активу";
   else
      str = "пассиву";
   end;

   if ((rest_sum != $0) or (type_rep_fi == 0 ))
        DP_AddPrintCell(Rep,"ИТОГО по\n" + str, 0, 0, "l:" + RepFontStyleTitel );
        DP_AddPrintCell(Rep, rest_sum, 0, DP_GetPrecision(rest_sum, t_point), "r:" + RepFontStyleTitel, apostr);
        Rep.AddStr();
     if( (rest_sum == $0) and (Kind == bal_acc_passive))
         DP_AddPrintCell(Rep, "На дату "+rep_date+" остатки по данному выпуску ценных бумаг нулевые.", tablewidth, 0, "l:" + RepFontStyleTitel,apostr, REP_ELEM_STR );
         Rep.AddStr();
         Rep.AddEmptyStr();
     end;

   end;

/*   if (rest_sum != $0)
        middle_line;
        if ( apostr )
[│ИТОГО по                               │                                       │
│ #######:                              │#######################################│]
             ( str , (Abs(Floor_Money(rest_sum))) :15:0:a );
        else
[│ИТОГО по                               │                                       │
│ #######:                              │#######################################│]
             ( str , (Abs(Floor_Money(rest_sum))) :15:0 );
        end;
   end; */

end;

class BlncStruct
  var Balance;
  var Sum;
end;

macro InsertInBlncList( list, elem )
  var i = 0;

  while( i < list.Size )
    if( list[i].Balance == elem.Balance )
      list[i].Sum = list[i].Sum + elem.Sum;
      return;
    end;
    i = i + 1;
  end;

  list[list.Size] = elem;
end;

// вывод заголовка по выпуску
private macro PrintHeaderAvr(LogOprID, fi_id, rep_date, DataSetFin)
   var LSIN_ISIN = "", NAME = "", AVRKIND = "";
   var cfin;

   Rep.AddEmptyStr();
   DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Полный баланс депо", LogOprID, 0, false, rep_date, rep_date, tablewidth );
   cfin = CDPFinInstr(fi_id, rep_date);
   LSIN_ISIN = cfin.LSIN();
   if (LSIN_ISIN == "") // если LSIN не задан, то выводим ISIN 207568
      LSIN_ISIN = cfin.ISIN();
   end;
   NAME = cfin.NAME();
   AVRKIND = cfin.AvoirKind();
   header(DataSetFin.IsIndividual, QualifiedSecur(cfin.avr, cfin.fins, rep_date), LSIN_ISIN, NAME, AVRKIND);
end;

macro CalcFiidRestsAccKind(LogOprID, Kind, fi_id, dprt_num, rep_date, chapter, num_plan, apostr, DataSetFin, iFiidCount)

   var DataSetBlnc, DataSetAcc, sQueryBlnc, sQueryAcc;
   var n = 0;
   var IsHead = false;
   var Sum = 0, SumBlnc = 0;
   var BlncList = TArray();
   var BlncElem;
   var stat = true;

   /* для неэмисионных сначала группируем данные пл выпускам, только потом печатаем */
   if( iFiidCount > 1 )
     stat = DataSetFin.MoveFirst();
     if( stat )
       fi_id = DataSetFin.fiid;
     end;
   end;

   while( stat )

     sQueryBlnc =   RSDCommand(" select distinct blnc.t_Balance0 "
              + " from daccblnc_dbt blnc "
              + " where blnc.t_Chapter = ? "
              + " and blnc.t_Code_Currency = ? "
              + " and exists( select t_account from daccount_dbt acc "
              + "             where acc.t_account = blnc.t_account and acc.t_Chapter = ? "
              + "             and acc.t_Code_Currency = ? "
              + "             and acc.t_Kind_Account = ?"
              + "             and acc.t_Department = ? "
              +  "          ) order by blnc.t_Balance0 ");

     sQueryBlnc.addParam( "", RSDBP_IN, chapter );
     sQueryBlnc.addParam( "", RSDBP_IN, fi_id );
     sQueryBlnc.addParam( "", RSDBP_IN, chapter );
     sQueryBlnc.addParam( "", RSDBP_IN, fi_id );
     sQueryBlnc.addParam( "", RSDBP_IN, Kind );
     sQueryBlnc.addParam( "", RSDBP_IN, dprt_num );
     sQueryBlnc.execute();

     DataSetBlnc = TRsbDataSet(sQueryBlnc);

     while( DataSetBlnc.MoveNext() )

       SumBlnc = 0;

       sQueryAcc =   RSDCommand(" select abs( sum ( rsb_account.restac( acc.t_Account, acc.t_Code_Currency, ? , acc.t_Chapter, null ) ) ) as Rest "
                + " from daccount_dbt acc, daccblnc_dbt blnc "
                + " where acc.t_Account = blnc.t_Account "
                + " and blnc.t_Balance0 = ?"
                + " and acc.t_Code_Currency = ? and acc.t_Chapter = ? "
                + " and acc.t_Department = ? " );

       sQueryAcc.addParam( "", RSDBP_IN, rep_date );
       sQueryAcc.addParam( "", RSDBP_IN, DataSetBlnc.Balance0 );
       sQueryAcc.addParam( "", RSDBP_IN, fi_id );
       sQueryAcc.addParam( "", RSDBP_IN, chapter );
       sQueryAcc.addParam( "", RSDBP_IN, dprt_num );
       sQueryAcc.execute();

       DataSetAcc = TRsbDataSet(sQueryAcc);

       if( DataSetAcc.MoveNext() and ( ValType(DataSetAcc.Rest) != V_UNDEF))
         Sum = Sum + DataSetAcc.Rest;
         SumBlnc = SumBlnc + DataSetAcc.Rest;
       end;

       if( not IsPrintHead and (DataSetAcc.Rest != 0) )
          PrintHeaderAvr(LogOprID, fi_id, rep_date, DataSetFin);
         IsPrintHead = true;
       end;

       if( iFiidCount <= 1 )
         WriteLine(DataSetBlnc.Balance0, SumBlnc, apostr, DataSetFin.SumPrecision);
       else
         BlncElem = BlncStruct;
         BlncElem.Balance = DataSetBlnc.Balance0;
         BlncElem.Sum     = SumBlnc;
         InsertInBlncList( BlncList, BlncElem );
       end;

     end;

     if( iFiidCount > 1 )
       stat = DataSetFin.MoveNext();
       if( stat )
         fi_id = DataSetFin.fiid;
       end;
     else
       stat = false;
     end;
   end;

   if( iFiidCount > 1 )
      n = 0;
      while( n < BlncList.Size )
        WriteLine(BlncList[n].Balance, BlncList[n].Sum, apostr, DataSetFin.SumPrecision);
        n = n + 1;
      end;
   end;

   if( (not IsPrintHead) and (type_rep_fi == 0 ) and (fi_id > 0))
     //если ничего не было напечатано, то значит остатки нулевые.
     //но если указали, что нужно печатать все бумаги, то печатаем.
     PrintHeaderAvr(LogOprID, fi_id, rep_date, DataSetFin);
     IsPrintHead = true;
     //так как остатков никаких нет, то печатаем только итоги (нулевые)
   end;

   if( IsPrintHead )
     Footer(Kind, Sum, apostr, DataSetFin.SumPrecision);
   end;

   return Sum;
end;

macro CalcFIIDRests(LogOprID, fi_id, dprt_num, rep_date, chapter, num_plan, apostr)

   var DataSetFin, sQuery, query;
   var ActiveSum = 0, PassiveSum = 0;

   query = " select fin.t_SumPrecision as SumPrecision, fin.t_Name as finname, avr.t_LSIN, avrk.t_Name as avrkname, avrk.t_IsIndividual "
         + " from dfininstr_dbt fin, davoiriss_dbt avr, davrkinds_dbt avrk "
         + " where fin.t_FIID = ? and avr.t_FIID = fin.t_FIID and avrk.t_AvoirKind = fin.t_AvoirKind and avrk.t_Fi_Kind = 2";

   if( SecurQual == SECURQUAL_QUAL)
     query = query + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, ? ) <> 0";
   elif(SecurQual == SECURQUAL_NOQUAL)
     query = query + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, ? ) = 0";
   end;

   sQuery = RSDCommand(query);
   sQuery.addParam( "", RSDBP_IN, fi_id );
   if((SecurQual == SECURQUAL_NOQUAL) or (SecurQual == SECURQUAL_QUAL))
     sQuery.addParam( "rep_date", RSDBP_IN, rep_date );
   end;
   sQuery.execute();
   DataSetFin = TRsbDataSet(sQuery);

   IsPrintHead = false;

   if( DataSetFin.MoveNext() )
     ActiveSum  = Abs(CalcFiidRestsAccKind( LogOprID, bal_acc_active,  fi_id, dprt_num, rep_date, chapter, num_plan, apostr, DataSetFin,1));
     PassiveSum = Abs(CalcFiidRestsAccKind( LogOprID, bal_acc_passive, fi_id, dprt_num, rep_date, chapter, num_plan, apostr, DataSetFin,1));
   end;

   Close(DataSetFin);
   return ActiveSum + PassiveSum;
end;

macro CalcALLRests(LogOprID, dprt_num, rep_date, chapter, num_plan, apostr)

   var DataSet, DataSetAvrKind, sQuery, sQuery2, sQueryStr, query;
   var n=0, iFiid = 0;
   var Sum = 0, lSum = 0;
   var Progress = CProgressBar;

   Progress.Init(-1, "Анализ выпусков... ", "ФОРМИРОВАНИЕ БАЛАНСА ДЕПО");

   /* эмисионные */
   query = " select distinct fin.t_FIID, fin.t_name, fin.t_SumPrecision"
         + " from dfininstr_dbt fin, davrkinds_dbt avrk, davoirsrv_dbt srv "
         + " where fin.t_fi_kind = 2 and avrk.t_AvoirKind = fin.t_AvoirKind "
         + " and avrk.t_IsIndividual <> 'X' and avrk.t_Fi_Kind = fin.t_fi_kind"
         + " and srv.t_FIID = fin.t_FIID "
         + " and srv.t_Department = ? "
         + " and srv.t_ServiceStartDate > TO_DATE('01-01-0001', 'DD-MM-YYYY') "
         + " and srv.t_ServiceStartDate <= ? ";

   if( SecurQual == SECURQUAL_QUAL)
     query = query + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, ? ) <> 0";
   elif(SecurQual == SECURQUAL_NOQUAL)
     query = query + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, ? ) = 0";
   end;

   sQuery = RSDCommand(query);

   sQuery.addParam( "", RSDBP_IN, dprt_num );
   sQuery.addParam( "", RSDBP_IN, rep_date );
   if((SecurQual == SECURQUAL_NOQUAL) or (SecurQual == SECURQUAL_QUAL))
     sQuery.addParam( "rep_date", RSDBP_IN, rep_date );
   end;
   sQuery.execute();
   DataSet = TRsbDataSet(sQuery);

   while( DataSet.MoveNext() )
      Sum = Sum + CalcFIIDRests(LogOprID, DataSet.fiid, dprt_num, rep_date, chapter, num_plan, apostr);
      n = Progress.Use();
   end;

   /* неэмисионные */
   sQueryStr = " select avrk.t_AvoirKind "
             + " from davrkinds_dbt avrk "
             + " where avrk.t_IsIndividual = 'X' and avrk.t_Fi_Kind = 2 "
             + " and exists(select 1 from dfininstr_dbt fin where fin.t_Fi_Kind = avrk.t_Fi_Kind and fin.t_AvoirKind = avrk.t_AvoirKind";

   if( SecurQual == SECURQUAL_QUAL)
     sQueryStr = sQueryStr + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, "+GetSQLDate(rep_date)+" ) <> 0";
   elif(SecurQual == SECURQUAL_NOQUAL)
     sQueryStr = sQueryStr + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, "+GetSQLDate(rep_date)+" ) = 0";
   end;

   sQueryStr = sQueryStr + ") "
               + " order by avrk.t_AvoirKind ";

   DataSetAvrKind = TRsbDataSet(sQueryStr);

   while( DataSetAvrKind.MoveNext() )

     query = " select distinct fin.t_FIID, fin.t_Name as finname, fin.t_SumPrecision as SumPrecision, avr.t_LSIN, avrk.t_Name as avrkname, avrk.t_IsIndividual "
           + " from dfininstr_dbt fin, davoiriss_dbt avr, davrkinds_dbt avrk "
           + " where fin.t_fi_kind = 2 and fin.t_AvoirKind = ? "
           + "   and avrk.t_AvoirKind = fin.t_AvoirKind and avrk.t_Fi_Kind = fin.t_fi_kind"
           + "   and avr.t_FIID = fin.t_FIID "
           + "   and (   0 = rsb_fiinstr.FI_IsSecurEmissive(fin.t_FIID) "
           + "        OR Exists(select 1 from davoirsrv_dbt srv "
           + "                   where srv.t_FIID = fin.t_FIID "
           + "                     and srv.t_Department = ? "
           + "                     and srv.t_ServiceStartDate > TO_DATE('01-01-0001', 'DD-MM-YYYY') "
           + "                     and srv.t_ServiceStartDate <= ? ) "
           + "       ) ";

     if( SecurQual == SECURQUAL_QUAL)
       query = query + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, ? ) <> 0";
     elif(SecurQual == SECURQUAL_NOQUAL)
       query = query + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, ? ) = 0";
     end;

     sQuery2 = RSDCommand(query);

     sQuery2.addParam( "", RSDBP_IN, DataSetAvrKind.AvoirKind );
     sQuery2.addParam( "", RSDBP_IN, dprt_num );
     sQuery2.addParam( "", RSDBP_IN, rep_date );
     if((SecurQual == SECURQUAL_NOQUAL) or (SecurQual == SECURQUAL_QUAL))
       sQuery2.addParam( "rep_date", RSDBP_IN, rep_date );
     end;
     sQuery2.execute();

     DataSet = TRsbDataSet(sQuery2, RSDVAL_CLIENT, RSDVAL_STATIC);

     lSum = 0;
     iFiid = 0;

     if( DataSet.Movelast() )
       iFiid = DataSet.GetRecCount();
     end;

     IsPrintHead = false;

     if( iFiid > 0 )
       lSum = lSum + Abs(CalcFiidRestsAccKind(LogOprID, bal_acc_active,  DataSet.fiid, dprt_num, rep_date, chapter, num_plan, apostr, DataSet, iFiid));
       lSum = lSum + Abs(CalcFiidRestsAccKind(LogOprID, bal_acc_passive, DataSet.fiid, dprt_num, rep_date, chapter, num_plan, apostr, DataSet, iFiid));

     end;

     Sum = Sum + lSum;
     n = Progress.Use();
   end;

   Progress.Remove();

   return Sum;
end;

macro CalcRests(LogOprID, fi_id, dprt_num, rep_date, chapter, num_plan, apostr)
   var Sum = 0;

   if(fi_id == -1)
      Sum = CalcALLRests(LogOprID, dprt_num, rep_date, chapter, num_plan, apostr);
   else
      Sum = CalcFIIDRests(LogOprID, fi_id, dprt_num, rep_date, chapter, num_plan, apostr);
   end;

   return Sum;
end;

private macro CreateRep(IsEmpty)
   var stat;

   DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

   if( (CalcRests(LogOprID, fi_id, dprt_num, rep_date, chapter, num_plan, apostr) == 0) and (type_rep_fi != 0) )
      stat = 1;
   else
      DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);
      stat = 0;
   end;

   if( (stat == 1) and (IsEmpty) )
     DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Полный баланс депо", LogOprID, 0, false, rep_date, rep_date, tablewidth );

     DP_AddPrintCell(Rep, "На дату "+rep_date+" остатки по выпускам ценных бумаг нулевые.", tablewidth, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR );
     Rep.AddStr();
     Rep.AddEmptyStr();
     DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);
   end;

   DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
   DP_EndProtocol();                 //закончить протоколирование

   return (not stat);
end;

private macro CreateEmptyRep()
  return CreateRep(true);
end;

/*============================================================*/

macro full_balance( _LogOprID,
                    _type_rep_fi,
                    _SecurQual,
                    _fi_id,
                    _dprt_num,
                    _rep_date,
                    _ToExcel,
                    _apostr )

   type_rep_fi = _type_rep_fi;
   SecurQual   = _SecurQual;
   rep_date    = _rep_date;
   LogOprID    = _LogOprID;
   fi_id       = _fi_id;
   dprt_num    = _dprt_num;
   ToExcel     = _ToExcel;
   apostr      = _apostr;

   Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
   Rep.SetFlagShowZeroValue(true);
   return DP_StdReportControl( LogOprID, CreateRep(false), @CreateEmptyRep, 0, ToExcel, Rep );
end;
