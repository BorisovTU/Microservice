/*
$Name:        scovernvpi.mac
$Module:      Ценные бумаги
$Description: Шаг цепочки - переоценка НВПИ
*/
import "scservop.mac", "sp_categ.mac", InsCarryDoc, "sp_car.mac", SPInter, "scoverntg.mac";

private record deal(dl_tick);       
private record rNtg( dl_nett );
private record Debet(account);
private record Credit(account);

private macro SayError( num, errmsg )
   ScOpInsertError( DL_SECURITYDOC, ScOprServDoc, deal, num, errmsg );
   return 1;
end;

private macro ВыполнитьПереоценкуДляРепоНаВнебалансе(FD, FD2, dat, Doc)
   var LastDate = Date(0,0,0), DebFIID, CredFIID, CatDeb = "", CatCred = "", Kind, str_err = "";
   var S1_учт = $0, S1о1_учт = $0, S1a1_учт = $0,
       S1_тек = $0, S1о1_тек = $0, S1a1_тек = $0, Rs1 = $0;

   if (FD.ExistAvance)
      if( ТОЗакрыто( FD.GetRQ(DLRQ_TYPE_AVANCE) ) != true) 
         S1a1_учт = ПолучитьУчтеннуюСуммуТОвВРпоРЕПО(FD, dat, FD.GetRQ(DLRQ_TYPE_AVANCE), FD.tick.rec.DealDate, str_err);
         if (str_err != "")
            return SayError( 1, str_err );
         end;

         S1a1_тек = ПолучитьИСохранитьТекущуюСуммуТОвВРпоРЕПО(FD, dat, FD.GetRQ(DLRQ_TYPE_AVANCE), str_err);
         if (str_err != "")
            return SayError( 1, str_err );
         end;
      end;
   end; /*аванс*/


   if( ТОЗакрыто( FD.GetRQ(DLRQ_TYPE_PAYMENT) ) != true) 
      S1о1_учт = ПолучитьУчтеннуюСуммуТОвВРпоРЕПО(FD, dat, FD.GetRQ(DLRQ_TYPE_PAYMENT), FD.tick.rec.DealDate, str_err);
      if (str_err != "")
         return SayError( 1, str_err );
      end;

      S1о1_тек = ПолучитьИСохранитьТекущуюСуммуТОвВРпоРЕПО(FD, dat, FD.GetRQ(DLRQ_TYPE_PAYMENT), str_err);
      if (str_err != "")
         return SayError( 1, str_err );
      end;
   end; /*оплата*/

   S1_учт = S1о1_учт + S1a1_учт;
   S1_тек = S1о1_тек + S1a1_тек;
   Rs1    = S1_учт - S1_тек;

   /*прямое репо*/
   if (FD.BuySale == DEAL_TYPE_SALE)
      Kind = "Т";
      if (Rs1 < $0)
         CatDeb   = "+Кредит, неисп.";
         CatCred  = "ВнебалСчетКорресп";
         DebFIID  = FD.GetPayFIID();
         CredFIID = NATCUR;
      elif (Rs1 > $0)
         CatDeb   = "ВнебалСчетКорресп";
         CatCred  = "+Кредит, неисп.";
         DebFIID  = NATCUR;
         CredFIID = FD.GetPayFIID();
      end; 

   /*обратное репо*/
   elif (FD.BuySale == DEAL_TYPE_BUY)
      Kind = "О";
      if (Rs1 < $0)
         CatDeb   = "ВнебалСчетКорресп";
         CatCred  = "-Кредит, неисп.";
         DebFIID  = NATCUR;
         CredFIID = FD.GetPayFIID();
      elif (Rs1 > $0)
         CatDeb   = "-Кредит, неисп.";
         CatCred  = "ВнебалСчетКорресп";
         DebFIID  = FD.GetPayFIID();
         CredFIID = NATCUR;
      end; 
   end; 

   if (Rs1 != $0)
      if( (not FD.OpenAccount( CatDeb, null, true, null, Debet, dat ) ) )
         return SayError( 1,  "Ошибка при актуализации счета по категории \"" + CatDeb + "\"" );
      end;   

      if( (not FD.OpenAccount( CatCred, null, true, null, Credit, dat ) ) )
         return SayError( 1,  "Ошибка при актуализации счета по категории \"" + CatCred + "\"" );
      end;   

      if( not ПроводкаПоКатегориямУчета( FD, Debet, Credit, 
                     dat,
                     3, 
                     CredFIID,
                     ABS(Rs1), 
                     Doc, INPCARRY, " 1", ScOprServDoc.CommCode, "Переоценка Т/О по неиспользованным кредитным линиям", 
                     null, null, null,
                     DebFIID, CredFIID
                ) )
         return SayError( 1, "Ошибка при выполнении проводки " + SC_GetCarryError() );
      end;
      ScOpReportLineData[ScOpReportLineData.Size] = SvOpOvervalue_NVPI( FD.tick.rec.DealCode, GetFICode(FD.pm_avoir.rec.FIID),
                Kind, S1_тек, GetFICode(FD.GetPayFIID(), null, FICK_ISOSTRING), Debet.Account, Credit.Account, ABS(Rs1));
   end;

   return 0;
end;

private macro ВыполнитьПереоценкуДляСделокИзОднойЧасти(FD, dat, Doc)
   var Sто_учт = $0, Sто_тек = $0, Rто = $0, Sums = TArray(), Arr_Overdue = TArray(), Arr_TO = TArray(), Arr_S = TArray(), str_err = "";
   var LastDate = Date(0,0,0), i = 0, ground = "", DebFIID, CredFIID, CatDeb = "", CatCred = "", DateExec = Date(0,0,0);

   GetOprDate( FD.GetKindDate(DATE_DEALBEGINEXEC), DateExec );

   if (FD.ExistAvance)
      if( ТОЗакрыто( FD.GetRQ(DLRQ_TYPE_AVANCE) ) != true) 
         Sто_учт = ПолучитьУчтеннуюСуммуТОвВР(FD, dat, FD.GetRQ(DLRQ_TYPE_AVANCE).rec.ID, DateExec, str_err);
         if (str_err != "")
            return SayError( 1, str_err );
         end;

         Sто_тек = ПолучитьИСохранитьТекущуюСуммуТОвВР(FD, dat, FD.GetRQ(DLRQ_TYPE_AVANCE).rec.ID, str_err);
         if (str_err != "")
            return SayError( 1, str_err );
         end;
         Arr_S[Arr_S.size] = Sто_тек;

         Rто = Sто_учт - Sто_тек;
         Sums[Sums.size] = Rто;
         if (ТОБылоПросрочено(FD.GetRQ(DLRQ_TYPE_AVANCE)))
            Arr_Overdue[Arr_Overdue.size] = true;
         else
            Arr_Overdue[Arr_Overdue.size] = false;
         end;

         if( FD.BuySale == DEAL_TYPE_SALE ) 
            Arr_TO[Arr_TO.size] = "Т";
         else
            Arr_TO[Arr_TO.size] = "О";
         end;
      end;
   end; /*аванс*/


   if( ТОЗакрыто( FD.GetRQ(DLRQ_TYPE_PAYMENT) ) != true) 
      Sто_учт = ПолучитьУчтеннуюСуммуТОвВР(FD, dat, FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.ID, DateExec, str_err);
      if (str_err != "")
         return SayError( 1, str_err );
      end;

      Sто_тек = ПолучитьИСохранитьТекущуюСуммуТОвВР(FD, dat, FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.ID, str_err);
      if (str_err != "")
         return SayError( 1, str_err );
      end;
      Arr_S[Arr_S.size] = Sто_тек;

      Rто = Sто_учт - Sто_тек;
      Sums[Sums.size] = Rто;
      if (ТОБылоПросрочено(FD.GetRQ(DLRQ_TYPE_PAYMENT)))
         Arr_Overdue[Arr_Overdue.size] = true;
      else
         Arr_Overdue[Arr_Overdue.size] = false;
      end;

      if( FD.BuySale == DEAL_TYPE_SALE ) 
         Arr_TO[Arr_TO.size] = "Т";
      else
         Arr_TO[Arr_TO.size] = "О";
      end;
   end; /*оплата*/


   if( ТОЗакрыто( FD.GetRQ(DLRQ_TYPE_DELIVERY) ) != true) 
      Sто_учт = ПолучитьУчтеннуюСуммуТОвВР(FD, dat, FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.ID, DateExec, str_err);
      if (str_err != "")
         return SayError( 1, str_err );
      end;

      Sто_тек = ПолучитьИСохранитьТекущуюСуммуТОвВР(FD, dat, FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.ID, str_err);
      if (str_err != "")
         return SayError( 1, str_err );
      end;
      Arr_S[Arr_S.size] = Sто_тек;

      Rто = Sто_учт - Sто_тек;
      Sums[Sums.size] = Rто;
      if (ТОБылоПросрочено(FD.GetRQ(DLRQ_TYPE_DELIVERY)))
         Arr_Overdue[Arr_Overdue.size] = true;
      else
         Arr_Overdue[Arr_Overdue.size] = false;
      end;

      if( FD.BuySale == DEAL_TYPE_SALE ) 
         Arr_TO[Arr_TO.size] = "О";
      else
         Arr_TO[Arr_TO.size] = "Т";
      end;
   end; /*поставка*/

   i = 0;
   while( i < Sums.Size )
      if( Sums[i] != $0 )
         if (Arr_TO[i] == "Т")
            ground = "Переоценка требования";
            if (Sums[i] < 0)
               DebFIID  = FD.GetPayFIID();
               CredFIID = NATCUR;
               if (Arr_Overdue[i] == false)
                  CatDeb   = "+Форвард, расчеты";
                  CatCred  = "+МаржаНВПИ, ИВ";
               else
                  CatDeb  = IIF(IsBasket(FD.Group), "Треб. с н.с., корзина", "Треб. с н.с.");
                  CatCred = "+МаржаНВПИ, ИВ";
               end;
            else
               DebFIID = NATCUR;
               CredFIID  = FD.GetPayFIID();
               if (Arr_Overdue[i] == false)
                  CatDeb  = "-МаржаНВПИ, ИВ";
                  CatCred   = "+Форвард, расчеты";
               else
                  CatDeb  = "-МаржаНВПИ, ИВ";
                  CatCred   = IIF(IsBasket(FD.Group), "Треб. с н.с., корзина", "Треб. с н.с.");
               end;
            end;
         else
            ground = "Переоценка обязательства";
            if (Sums[i] < 0)
               DebFIID = NATCUR;
               CredFIID  = FD.GetPayFIID();
               if (Arr_Overdue[i] == false)
                  CatDeb  = "-МаржаНВПИ, ИВ";
                  CatCred   = "-Форвард, расчеты";
               else
                  CatDeb  = "-МаржаНВПИ, ИВ";
                  CatCred = IIF(IsBasket(FD.Group), "Обяз. с н.с., корзина", "Обяз. с н.с.");
               end;
            else
               DebFIID  = FD.GetPayFIID();
               CredFIID = NATCUR;
               if (Arr_Overdue[i] == false)
                  CatDeb   = "-Форвард, расчеты";
                  CatCred  = "+МаржаНВПИ, ИВ";
               else
                  CatDeb  = IIF(IsBasket(FD.Group), "Обяз. с н.с., корзина", "Обяз. с н.с.");
                  CatCred = "+МаржаНВПИ, ИВ";
               end;
            end;
         end;

         if( (not FD.IsExistAccount( CatDeb, null, false, null, Debet, null, dat ) ) and
             (not FD.OpenAccount( CatDeb, null, true, null, Debet, dat ) ) )
            return SayError( 1,  "Ошибка при актуализации счета по категории \"" + CatDeb + "\"" );
         end;   

         if( (not FD.IsExistAccount( CatCred, null, false, null, Credit, null, dat ) ) AND 
             (not FD.OpenAccount( CatCred, null, true, null, Credit, dat ) ) )
            return SayError( 1,  "Ошибка при актуализации счета по категории \"" + CatCred + "\"" );
         end;   

         if( not ПроводкаПоКатегориямУчета( FD, Debet, Credit, 
                        dat,
                        1, 
                        CredFIID,
                        ABS(Sums[i]), 
                        Doc, INPCARRY, " 1", ScOprServDoc.CommCode, ground, 
                        null, null, null,
                        DebFIID, CredFIID
                   ) )
            return SayError( 1, "Ошибка при выполнении проводки " + SC_GetCarryError() );
         end;
         ScOpReportLineData[ScOpReportLineData.Size] = SvOpOvervalue_NVPI( FD.tick.rec.DealCode, GetFICode(FD.pm_avoir.rec.FIID),
                   Arr_TO[i], Arr_S[i], GetFICode(FD.GetPayFIID(), null, FICK_ISOSTRING), Debet.Account, Credit.Account, ABS(Sums[i]));
      end;
      i = i + 1;
   end; /*цикл по полученным суммам, проводки*/
   return 0;
end;

private macro ВыполнитьПереоценкуДляРепоПоОплате(FD, FD2, dat, Doc)
   var Sто_учт_н = $0, Sто_учт_п = $0, Sa1_учт = $0, Sо1_учт = $0, Sa2_учт = $0, Sо2_учт = $0;
   var Sто_тек_н = $0, Sто_тек_п = $0, Sa1_тек = $0, Sо1_тек = $0, Sa2_тек = $0, Sо2_тек = $0;
   var Rн = $0, Rп = $0;
   var CatDeb_н, CatCred_н, DebFIID_н, CredFIID_н, CatDeb_п, CatCred_п, DebFIID_п, CredFIID_п;
   var LastDate = Date(0,0,0), ground = "", Kind, str_err = "";

   if (FD.ExistAvance)
      if( ТОЗакрыто( FD.GetRQ(DLRQ_TYPE_AVANCE) ) ) 
         Sa1_учт = ПолучитьУчтеннуюСуммуТОвВРпоРЕПО(FD, dat, FD.GetRQ(DLRQ_TYPE_AVANCE), FD.GetRQ(DLRQ_TYPE_AVANCE).rec.FactDate, str_err);
         if (str_err != "")
            return SayError( 1, str_err );
         end;

         Sa1_тек = ПолучитьИСохранитьТекущуюСуммуТОвВРпоРЕПО(FD, dat, FD.GetRQ(DLRQ_TYPE_AVANCE), str_err);
         if (str_err != "")
            return SayError( 1, str_err );
         end;
      end;
   end; /*аванс1*/


   if( ТОЗакрыто( FD.GetRQ(DLRQ_TYPE_PAYMENT) ) ) 
      Sо1_учт = ПолучитьУчтеннуюСуммуТОвВРпоРЕПО(FD, dat, FD.GetRQ(DLRQ_TYPE_PAYMENT), FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.FactDate, str_err);
      if (str_err != "")
         return SayError( 1, str_err );
      end;

      Sо1_тек = ПолучитьИСохранитьТекущуюСуммуТОвВРпоРЕПО(FD, dat, FD.GetRQ(DLRQ_TYPE_PAYMENT), str_err);
      if (str_err != "")
         return SayError( 1, str_err );
      end;
   end; /*оплата1*/


   if (FD2.ExistAvance)
      if( ТОЗакрыто( FD2.GetRQ(DLRQ_TYPE_AVANCE) ) or ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_AVANCE)) ) 
         Sa2_учт = ПолучитьУчтеннуюСуммуТОвВРпоРЕПО(FD2, dat, FD2.GetRQ(DLRQ_TYPE_AVANCE), FD2.GetRQ(DLRQ_TYPE_AVANCE).rec.PlanDate, str_err);
         if (str_err != "")
            return SayError( 1, str_err );
         end;

         Sa2_тек = ПолучитьИСохранитьТекущуюСуммуТОвВРпоРЕПО(FD2, dat, FD2.GetRQ(DLRQ_TYPE_AVANCE), str_err);
         if (str_err != "")
            return SayError( 1, str_err );
         end;
      end;
   end; /*аванс2*/


   if( ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_PAYMENT)) ) 
      Sо2_учт = ПолучитьУчтеннуюСуммуТОвВРпоРЕПО(FD2, dat, FD2.GetRQ(DLRQ_TYPE_PAYMENT), FD2.GetRQ(DLRQ_TYPE_PAYMENT).rec.PlanDate, str_err);
      if (str_err != "")
         return SayError( 1, str_err );
      end;

      Sо2_тек = ПолучитьИСохранитьТекущуюСуммуТОвВРпоРЕПО(FD2, dat, FD2.GetRQ(DLRQ_TYPE_PAYMENT), str_err);
      if (str_err != "")
         return SayError( 1, str_err );
      end;
   end; /*оплата2*/

   /*Расчитываем старые суммы:
     - Непросроченных Т/О по оплате Sто_учт_н
     - Просроченных Т/О по оплате Sто_учт_п
   */
   Sто_учт_н  = Sa1_учт + Sо1_учт - Sa2_учт - Sо2_учт;
   Sто_учт_п  = IIF((FD2.ExistAvance and ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_AVANCE))), Sa2_учт, $0) +
                IIF(ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_PAYMENT)),  Sо2_учт, $0);

   /*Расчитываем новые суммы:
     - Непросроченных Т/О по оплате Sто_тек_н
     - Просроченных Т/О по оплате Sто_тек_п
   */
   Sто_тек_н = Sa1_тек + Sо1_тек - Sa2_тек - Sо2_тек;
   Sто_тек_п = IIF((FD2.ExistAvance and ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_AVANCE))), Sa2_тек, $0) + 
               IIF(ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_PAYMENT)),  Sо2_тек, $0);

   Rн = Sто_учт_н - Sто_тек_н;
   Rп = Sто_учт_п - Sто_тек_п;

   /*Переоценивается требование по оплате 2 ч*/
   if( FD2.BuySale == DEAL_TYPE_SALE ) 
      ground = "Переоценка требования по оплате";
      Kind = "Т";
      if (Rн < $0)
         CatDeb_н   = IIF(IsBasket(FD2.Group), "+ОД, Корзина", "+ОД");
         CatCred_н  = "+МаржаНВПИ, ИВ";
         DebFIID_н  = FD2.GetPayFIID();
         CredFIID_н = NATCUR;
      elif (Rн > $0)
         CatDeb_н   = "-МаржаНВПИ, ИВ";
         CatCred_н  = IIF(IsBasket(FD2.Group), "+ОД, Корзина", "+ОД");
         DebFIID_н  = NATCUR;
         CredFIID_н = FD2.GetPayFIID();
      end;

      if (Rп < $0)
         CatDeb_п   = IIF(IsBasket(FD2.Group), "Треб. с н.с., корзина", "Треб. с н.с.");
         CatCred_п  = "+МаржаНВПИ, ИВ";
         DebFIID_п  = FD2.GetPayFIID();
         CredFIID_п = NATCUR;
      elif (Rп > $0)
         CatDeb_п   = "-МаржаНВПИ, ИВ";
         CatCred_п  = IIF(IsBasket(FD2.Group), "Треб. с н.с., корзина", "Треб. с н.с.");
         DebFIID_п  = NATCUR;
         CredFIID_п = FD2.GetPayFIID();
      end;
   /*Переоценка обязательств по оплате 2 ч*/
   else
      ground = "Переоценка обязательства по оплате";
      Kind = "О";
      if (Rн < $0)
         CatDeb_н   = "-МаржаНВПИ, ИВ";
         CatCred_н  = IIF(IsBasket(FD2.Group), "-ОД, Корзина", "-ОД");
         DebFIID_н  = NATCUR;
         CredFIID_н = FD2.GetPayFIID();
      elif (Rн > $0)
         CatDeb_н   = IIF(IsBasket(FD2.Group), "-ОД, Корзина", "-ОД");
         CatCred_н  = "+МаржаНВПИ, ИВ";
         DebFIID_н  = FD2.GetPayFIID();
         CredFIID_н = NATCUR;
      end;

      if (Rп < $0)
         CatDeb_п   = "-МаржаНВПИ, ИВ";
         CatCred_п  = IIF(IsBasket(FD2.Group), "Обяз. с н.с., корзина", "Обяз. с н.с.");
         DebFIID_п  = NATCUR;
         CredFIID_п = FD2.GetPayFIID();
      elif (Rп > $0)
         CatDeb_п   = IIF(IsBasket(FD2.Group), "Обяз. с н.с., корзина", "Обяз. с н.с.");
         CatCred_п  = "+МаржаНВПИ, ИВ";
         DebFIID_п  = FD2.GetPayFIID();
         CredFIID_п = NATCUR;
      end;
   end;


   if (Rн != $0)
      if( (not FD.OpenAccount( CatDeb_н, null, true, null, Debet, dat ) ) )
         return SayError( 1,  "Ошибка при актуализации счета по категории \"" + CatDeb_н + "\"" );
      end;   

      if( (not FD.OpenAccount( CatCred_н, null, true, null, Credit, dat ) ) )
         return SayError( 1,  "Ошибка при актуализации счета по категории \"" + CatCred_н + "\"" );
      end;   

      if( not ПроводкаПоКатегориямУчета( FD2, Debet, Credit, 
                     dat,
                     1, 
                     CredFIID_н,
                     ABS(Rн), 
                     Doc, INPCARRY, " 1", ScOprServDoc.CommCode, ground, 
                     null, null, null,
                     DebFIID_н, CredFIID_н
                ) )
         return SayError( 1, "Ошибка при выполнении проводки " + SC_GetCarryError() );
      end;
      ScOpReportLineData[ScOpReportLineData.Size] = SvOpOvervalue_NVPI( FD.tick.rec.DealCode, GetFICode(FD.pm_avoir.rec.FIID), 
                Kind, Sто_тек_н, GetFICode(FD2.GetPayFIID(), null, FICK_ISOSTRING), Debet.Account, Credit.Account, ABS(Rн));
   end;

   if (Rп != $0)
      if( (not FD.OpenAccount( CatDeb_п, null, true, null, Debet, dat ) ) )
         return SayError( 1,  "Ошибка при актуализации счета по категории \"" + CatDeb_п + "\"" );
      end;   

      if( (not FD.OpenAccount( CatCred_п, null, true, null, Credit, dat ) ) )
         return SayError( 1,  "Ошибка при актуализации счета по категории \"" + CatCred_п + "\"" );
      end;   

      if( not ПроводкаПоКатегориямУчета( FD2, Debet, Credit, 
                     dat,
                     1, 
                     CredFIID_п,
                     ABS(Rп), 
                     Doc, INPCARRY, " 1", ScOprServDoc.CommCode, ground, 
                     null, null, null,
                     DebFIID_п, CredFIID_п
                ) )
         return SayError( 1, "Ошибка при выполнении проводки " + SC_GetCarryError() );
      end;

      ScOpReportLineData[ScOpReportLineData.Size] = SvOpOvervalue_NVPI( FD.tick.rec.DealCode, GetFICode(FD.pm_avoir.rec.FIID), 
                Kind, Sто_тек_п, GetFICode(FD2.GetPayFIID(), null, FICK_ISOSTRING), Debet.Account, Credit.Account, ABS(Rп));
   end;
   return 0;
end;

private macro ВыполнитьПереоценкуДляРепоПоПроцентам(FD, FD2, dat, Doc)
   var LastDate = Date(0,0,0), ground = "", DebFIID, CredFIID, CatDeb = "", CatCred = "";
   var IsOverdue = false, Kind, str_err = "";
   var R_PC_б = $0, R_PC_вб = $0, 
       SтоPC_тек_вр = $0, SтоPC_б_учт_вр = $0, SтоPC_вб_учт_вр = $0, SтоPC_б_тек_вр = $0, SтоPC_вб_тек_вр = $0,
       SтоPC_тек_вц = $0, SтоPC_вб_тек_вц = $0, SтоPC_учт_вр = $0;

   if( ТОБылоПросрочено(FD2.GetRQ(DLRQ_TYPE_PAYMENT)) )
      IsOverdue = true;
   end;

   /*Получаем в ВР*/
   SтоPC_учт_вр = ПолучитьУчтеннуюСуммуПроцентовПоРЕПО(FD, dat, DLSUM_KIND_SUM_TO_PERCENT, str_err);
   if (str_err != "")
      return SayError( 1, str_err );
   end;

   /*Требование, если ставка Репо больше 0 и Репо обр, или Ставка Репо меньше 0 и Репо прямое*/
   if( ((FD.dl_leg.rec.IncomeRate > 0.0) and IsBUY(FD.Group)) or 
       ((FD.dl_leg.rec.IncomeRate < 0.0) and IsSALE(FD.Group))
     ) 
      /*Получаем в ВР*/
      SтоPC_вб_учт_вр = ПолучитьУчтеннуюСуммуПроцентовПоРЕПО(FD, dat, DLSUM_KIND_SUM_NOTCARRY_PERCENT, str_err);
      if (str_err != "")
         return SayError( 1, str_err );
      end;
   end;

   SтоPC_б_учт_вр = SтоPC_учт_вр - SтоPC_вб_учт_вр;

   /*Получаем в ВЦ*/
   SтоPC_тек_вц = ПолучитьУчтеннуюСуммуПроцентовПоРЕПО(FD, dat, DLSUM_KIND_SUM_TO_PERCENT_CFI, str_err);
   if (str_err != "")
      return SayError( 1, str_err );
   end;

   /*Сохраняем в ВР*/
   SтоPC_тек_вр = ПолучитьИСохранитьТекущуюСуммуПроцентовПоРЕПО(FD, dat, DLSUM_KIND_SUM_TO_PERCENT, str_err, SтоPC_тек_вц);
   if (str_err != "")
      return SayError( 1, str_err );
   end;

   /*Требование, если ставка Репо больше 0 и Репо обр, или Ставка Репо меньше 0 и Репо прямое*/
   if( ((FD.dl_leg.rec.IncomeRate > 0.0) and IsBUY(FD.Group)) or 
       ((FD.dl_leg.rec.IncomeRate < 0.0) and IsSALE(FD.Group))
     ) 

      SтоPC_вб_тек_вц = ПолучитьУчтеннуюСуммуПроцентовПоРЕПО(FD, dat, DLSUM_KIND_SUM_NOTCARRY_PERCENT_CFI, str_err);
      if (str_err != "")
         return SayError( 1, str_err );
      end;

      if (SтоPC_вб_тек_вц > $0)
         SтоPC_вб_тек_вр = ПолучитьИСохранитьТекущуюСуммуПроцентовПоРЕПО(FD, dat, DLSUM_KIND_SUM_NOTCARRY_PERCENT, str_err, SтоPC_вб_тек_вц);
         if (str_err != "")
            return SayError( 1, str_err );
         end;
      end;
   end;

   SтоPC_б_тек_вр = SтоPC_тек_вр - SтоPC_вб_тек_вр;

   /*Суммы проводок*/
   R_PC_б = SтоPC_б_учт_вр - SтоPC_б_тек_вр;
   R_PC_вб = SтоPC_вб_учт_вр - SтоPC_вб_тек_вр;

   /*Требование, если ставка Репо больше 0 и Репо обр, или Ставка Репо меньше 0 и Репо прямое*/
   if( ((FD.dl_leg.rec.IncomeRate > 0.0) and IsBUY(FD.Group)) or 
       ((FD.dl_leg.rec.IncomeRate < 0.0) and IsSALE(FD.Group))
     ) 
      ground = "Переоценка требования по процентам";
      Kind = "Т";
      if (R_PC_б < $0)
         CatCred  = "+МаржаНВПИ, ИВ";
         DebFIID  = FD.GetPayFIID();
         CredFIID = NATCUR;
         if (not IsOverdue)
            CatDeb   = "+% к погашению";
         else
            CatDeb   = "+% с н.с.";
         end;
      elif (R_PC_б > $0)
         CatDeb   = "-МаржаНВПИ, ИВ";
         DebFIID  = NATCUR;
         CredFIID = FD.GetPayFIID();
         if (not IsOverdue)
            CatCred   = "+% к погашению";
         else
            CatCred   = "+% с н.с.";
         end;
      end;
   elif( ((FD.dl_leg.rec.IncomeRate < 0.0) and IsBUY(FD.Group)) or 
         ((FD.dl_leg.rec.IncomeRate > 0.0) and IsSALE(FD.Group))
       ) 
      ground = "Переоценка обязательства по процентам";
      Kind = "О";
      if (R_PC_б < $0)
         CatDeb   = "-МаржаНВПИ, ИВ";
         DebFIID  = NATCUR;
         CredFIID = FD.GetPayFIID();
         if (not IsOverdue)
            CatCred   = "-% к погашению";
         else
            CatCred   = "-% с н.с.";
         end;
      elif (R_PC_б > $0)
         CatCred  = "+МаржаНВПИ, ИВ";
         DebFIID  = FD.GetPayFIID();
         CredFIID = NATCUR;
         if (not IsOverdue)
            CatDeb   = "-% к погашению";
         else
            CatDeb   = "-% с н.с.";
         end;
      end;
   end;

   if (R_PC_б != $0)
      if( (not FD.OpenAccount( CatDeb, null, true, null, Debet, dat ) ) )
         return SayError( 1,  "Ошибка при актуализации счета по категории \"" + CatDeb + "\"" );
      end;   

      if( (not FD.OpenAccount( CatCred, null, true, null, Credit, dat ) ) )
         return SayError( 1,  "Ошибка при актуализации счета по категории \"" + CatCred + "\"" );
      end;   

      if( not ПроводкаПоКатегориямУчета( FD, Debet, Credit, 
                     dat,
                     1, 
                     CredFIID,
                     ABS(R_PC_б), 
                     Doc, INPCARRY, " 1", ScOprServDoc.CommCode, ground, 
                     null, null, null,
                     DebFIID, CredFIID
                ) )
         return SayError( 1, "Ошибка при выполнении проводки " + SC_GetCarryError() );
      end;

      ScOpReportLineData[ScOpReportLineData.Size] = SvOpOvervalue_NVPI( FD.tick.rec.DealCode, GetFICode(FD.pm_avoir.rec.FIID),
                Kind, SтоPC_б_тек_вр, GetFICode(FD.GetPayFIID(), null, FICK_ISOSTRING), Debet.Account, Credit.Account, ABS(R_PC_б));
   end;

   if ((SтоPC_вб_тек_вц > $0) and (R_PC_вб != $0))
      ground = "Переоценка требования по процентам на внебалансе";
      Kind = "Т";
      if (R_PC_вб < $0)
         if (not IsOverdue)
            CatDeb   = "Задолж.% (внебаланс)";
         else
            CatDeb   = "Задолж.%с н.с (внебаланс)";
         end;
         DebFIID = FD.GetPayFIID();

         CatCred  = "ВнебалСчетКорресп";
         CredFIID  = NATCUR;

      elif (R_PC_вб > $0)

         CatDeb   = "ВнебалСчетКорресп";
         DebFIID  = NATCUR;

         if (not IsOverdue)
            CatCred   = "Задолж.% (внебаланс)";
         else
            CatCred   = "Задолж.%с н.с (внебаланс)";
         end;
         CredFIID = FD.GetPayFIID();
      end;

      if( (not FD.OpenAccount( CatDeb, null, true, IIF(CatDeb=="ВнебалСчетКорресп",FIROLE_CA,null), Debet, dat ) ) )
         return SayError( 1,  "Ошибка при актуализации счета по категории \"" + CatDeb + "\"" );
      end;   

      if( (not FD.OpenAccount( CatCred, null, true, IIF(CatCred=="ВнебалСчетКорресп",FIROLE_CA,null), Credit, dat ) ) )
         return SayError( 1,  "Ошибка при актуализации счета по категории \"" + CatCred + "\"" );
      end;   

      if( not ПроводкаПоКатегориямУчета( FD, Debet, Credit, 
                     dat,
                     3, 
                     CredFIID,
                     ABS(R_PC_вб), 
                     Doc, INPCARRY, " 1", ScOprServDoc.CommCode, ground, 
                     null, null, null,
                     DebFIID, CredFIID
                ) )
         return SayError( 1, "Ошибка при выполнении проводки " + SC_GetCarryError() );
      end;

      ScOpReportLineData[ScOpReportLineData.Size] = SvOpOvervalue_NVPI( FD.tick.rec.DealCode, GetFICode(FD.pm_avoir.rec.FIID),
                Kind, SтоPC_вб_учт_вр, GetFICode(FD.GetPayFIID(), null, FICK_ISOSTRING), Debet.Account, Credit.Account, ABS(R_PC_вб));
   end;

   return 0;
end;

private macro ВыполнитьПереоценкуВложенийВЦБ(avoiriss, Doc, ID_Operation, ID_Step)

  private macro ПолучитьКурсФИ( FD, Type, _Date, CursDate, err )
     var RateDef = TRecHandler("ratedef");
     var CourceDbl1 = 0.0;
     var CourceDbl2 = 0.0;
     var CourceDbl = 0.0;
     var sql, rsd;
     var CursDate1 = date(0,0,0);
     var CursDate2 = date(0,0,0);
  
     ClearRecord(RateDef.rec);

     SetParm(3, date(0,0,0));
     SetParm(4, true);  
  
     sql = RSDCommand(" SELECT * "
                    + "   FROM dratedef_dbt "
                    + "  WHERE t_OtherFI = ? "
                    + "    AND t_Type = ? "
                    + "  ORDER BY t_IsDominant DESC "); /* первым будет основной */
  
     sql.addParam( "", RSDBP_IN, FD.fininstr.rec.FIID );
     sql.addParam( "", RSDBP_IN, Type );
     sql.execute();
  
     rsd = TRsbDataSet(sql);
     if( rsd.MoveNext() )
       RateDef = rsd.GetRecord();
  
       if( (ConvSumDbl(CourceDbl1, 1.0, _Date, FD.fininstr.rec.FIID, RateDef.FIID, false, Type, CursDate1) == true) and
           (ConvSumCrossDbl(CourceDbl2, 1.0, _Date, RateDef.FIID, FD.fininstr.rec.FaceValueFI, false, 0, CursDate2) == true) )
         CourceDbl = CourceDbl1 * CourceDbl2;
         SetParm(3, CursDate1);
         SetParm(4, false);
       end;
     end;

     return CourceDbl;
  end;

  private macro ПолучитьКурсПереоценки(FD, CourseKind, CourceName, _Course)
     var Dbl_Course = 0.0, SinceDate, erFlag, err, Course = $0;

     /*Определим курс вида "Текущая справедливая стоимость".*/
     Dbl_Course = GetRateOnDate( ScOprServDoc.CommDate, FD.fininstr.rec.FIID, FD.fininstr.rec.FaceValueFI, false, ПолучитьВидКурса(CourseKind), erFlag, SinceDate );
     if( erFlag == true )
       Dbl_Course = ПолучитьКурсФИ( FD, ПолучитьВидКурса(CourseKind), ScOprServDoc.CommDate, SinceDate, erFlag );
     end;

     if( erFlag == false )

        if (SinceDate == ScOprServDoc.CommDate)
           Course = ToMoney(Dbl_Course);
        else

           if( isOprMultiExec() == false )
              if( GetTrue( true, "Для ц/б " + FD.fininstr.rec.FI_Code + 
                        " не задан курс вида \"" + CourceName + "\" за дату операции " + ScOprServDoc.CommDate + ". Взять последний введенный курс?" ) == false )
                 return 1;
              else
                 Course = ToMoney(Dbl_Course);
              end;

           else
              Course = ToMoney(Dbl_Course);
           end;
        end;

     else
        return SayError( 1, "Не могу получить значение курса вида \"" + CourceName + "\" для ц/б " + FD.fininstr.rec.FI_Code );
     end;
     SetParm(3, Course);

     return 0;
  end;

  var K = 0;
  var FD = SPFirstDocFI(DLDOC_ISSUE, avoiriss.FIID, avoiriss.FIID, KINDPORT_RETIRE, {OurBank}, null, null, null);
  var FiRole = FIROLE_BA_PUDP;
  var query, cmd, DataSet;
  var СумТСС = 0, ТСС = 0, Сакт = 0, ПО, R = 0, Rc = 0;
  var lot = TRecHandler("pmwrtsum.dbt");
  var СчетНашПортфель = TRecHandler("account.dbt");
  var СчетМаржаНВПИ = TRecHandler("account.dbt");

  /*Определим курс вида "Текущая справедливая стоимость".*/
  if( ПолучитьКурсПереоценки(FD, SP_RATETYPE_RightCost, "Справедливая стоимость", K) != 0 )
     return 1;
  end;

  query =   " select * "
          + "   from dpmwrtsum_dbt "
          + "  where t_Party = -1 "
          + "    and t_Department = ? "
          + "    and t_Contract = 0 "
          + "    and t_Buy_Sale = " + PM_WRITEOFF_SUM_BUY
          + "    and t_Portfolio = " + KINDPORT_RETIRE
          + "    and t_FIID = ? "
          + "    and t_Amount > 0 "
          + "    and t_State = " + PM_WRTSUM_FORM;

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ScOprServDoc.Division);
  cmd.AddParam(avoiriss.FIID);      

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    DataSet.GetRecord().CopyTo( lot.rec );


    ТСС  = lot.rec.Amount * K;
    СумТСС = СумТСС + ТСС;
    Сакт = lot.rec.Cost + lot.rec.NKDAmount + lot.rec.InterestIncome + lot.rec.DiscountIncome;//это балансовая стоимость без учёта переоценки
    ПО = ТСС - Сакт;

    if (ConvSum (ПО, ПО, ScOprServDoc.CommDate, FD.fininstr.rec.FaceValueFI, NATCUR))
      return SayError(1, "Не могу сконвертировать сумму из ", ПолучитьКодФинИн (FD.fininstr.rec.FaceValueFI)," в ", ПолучитьКодФинИн (NATCUR));
    end;

    Rc = lot.rec.OverAmount - ПО;
    R = R + Rc;

    WRTOvervalueLot( ScOprServDoc.CommDate,
                     lot.rec.SumID,
                     ID_Operation,
                     ID_Step,
                     ТСС,
                     -Rc,
                     false
                   );

  end;

  if(R)

    if( not FD.OpenAccount("Наш портфель ц/б", null, null, FiRole, СчетНашПортфель, ScOprServDoc.CommDate, FD.GetAccFIID("Наш портфель ц/б") ) )
      return SayError(1, "Ошибка при поиске/открытии счета по КУ \"Наш портфель ц/б\" ");
    end;

    if(R > 0)

      if( not FD.OpenAccount("+МаржаНВПИ, ИВ", null, null, null, СчетМаржаНВПИ, ScOprServDoc.CommDate, NATCUR) )
        return SayError(1, "Ошибка при поиске/открытии счета по КУ \"+МаржаНВПИ, ИВ\" ");
      end;

      if(not ПроводкаПоКатегориямУчета( FD, 
                                        СчетНашПортфель, СчетМаржаНВПИ,         /* КатегДеб , КатегКредит*/
                                        ScOprServDoc.CommDate,     /* Дата */
                                        1,                       /* Глава */
                                        NATCUR,                  /* Валюта */
                                        R,                       /* Сумма*/
                                        Doc,                     /* Документ */
                                        INPCARRY,                /* Result_Carry */
                                        " 1",                      /* Kind_Oper */
                                        "",                      /* Numb_Document */
                                        "Отрицательная переоценка",
                                        null, null, null,
                                        FD.fininstr.rec.FaceValueFI, NATCUR
                                       ) 
         )
         return SayError(1, "Ошибка при выполнении проводки " + SC_GetCarryError());
       end;

       ScOpReportLineData[ScOpReportLineData.Size] = SvOpOvervalue_NVPI( "", GetFICode(FD.fininstr.rec.FIID),
                "", СумТСС, GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING), СчетНашПортфель.rec.Account, СчетМаржаНВПИ.rec.Account, R);
    elif(R < 0)
      if( not FD.OpenAccount("-МаржаНВПИ, ИВ", null, null, null, СчетМаржаНВПИ, ScOprServDoc.CommDate, NATCUR) )
        return SayError(1, "Ошибка при поиске/открытии счета по КУ \"-МаржаНВПИ, ИВ\" ");
      end;

      if(not ПроводкаПоКатегориямУчета( FD, 
                                        СчетМаржаНВПИ, СчетНашПортфель,        /* КатегДеб , КатегКредит*/
                                        ScOprServDoc.CommDate,     /* Дата */
                                        1,                       /* Глава */
                                        NATCUR,                  /* Валюта */
                                        Abs(R),                  /* Сумма*/
                                        Doc,                     /* Документ */
                                        INPCARRY,                /* Result_Carry */
                                        " 1",                      /* Kind_Oper */
                                        "",                      /* Numb_Document */
                                        "Положительная переоценка",
                                        null, null, null,
                                        NATCUR, FD.fininstr.rec.FaceValueFI
                                       ) 
         )
         return SayError(1, "Ошибка при выполнении проводки " + SC_GetCarryError());
       end;

       ScOpReportLineData[ScOpReportLineData.Size] = SvOpOvervalue_NVPI( "", GetFICode(FD.fininstr.rec.FIID),
                "", СумТСС, GetFICode(FD.fininstr.rec.FaceValueFI, null, FICK_ISOSTRING), СчетМаржаНВПИ.rec.Account, СчетНашПортфель.rec.Account, Abs(R));
    end;
  end;

  return 0;
end;

macro ExecuteStep( Doc, FDoc, DocKind, ID_Operation, ID_Step )
  private record avoiriss( avoiriss );
  var FD, FD2 = null, dat; 
  
  dat = ScOprServDoc.CommDate; 

  if (DocKind == DL_SECURITYDOC)
     if ((ScOprServDoc.Flag3 == SET_CHAR) or (ScOprServDoc.Flag2 == SET_CHAR))
        SetBuff( deal, FDoc ); 

        FD = SPFirstDoc( deal );
        if(FD.ExistBack)
           FD2 = SPFirstDoc( deal, true );
        end;
     end;

     /*в сделках НЕ РЕПО. На момент реализации операции других сделок из 2ч реализовано не было.*/
     if ((ScOprServDoc.Flag3 == SET_CHAR) and (not FD.ExistBack) and (SP_DealNeedOverValueNVPI( ScOprServDoc.CommDate, deal ) == 0))
        /*выполняем переоценку Т/О по всем неисполненным Т/О по платежам - авансу, оплате и поставке*/
        if (ВыполнитьПереоценкуДляСделокИзОднойЧасти(FD, dat, Doc) != 0)
           InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
           ClearErrorArr();
           return 1;
        end;
     end; /*Если взведен Flag3, для сделок из 1 части*/


     /*в сделках РЕПО. На момент реализации операции других сделок из 2ч реализовано не было.*/
     if ((ScOprServDoc.Flag3 == SET_CHAR) and IsREPO(FD.Group) and (SP_DealNeedOverValueNVPI( ScOprServDoc.CommDate, deal ) == 0))

        if (ВыполнитьПереоценкуДляРепоПоОплате(FD, FD2, dat, Doc) != 0)
           InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
           ClearErrorArr();
           return 1;
        end;

        /*Переоценка Т/О по процентам*/
        if (ВыполнитьПереоценкуДляРепоПоПроцентам(FD, FD2, dat, Doc) != 0)
           return 1;
        end;
     end; /*Если взведен Flag3, для сделок РЕПО*/
  end;


  /*Неттинг в макрос попадает только релевантный, поэтому здесь ничего не проверяем.*/
  if (DocKind == DL_NTGDOC)
     if (ScOprServDoc.Flag4 == SET_CHAR)
        SetBuff( rNtg, FDoc );

        if (ВыполнитьПереоценкуСделкиНеттинга(rNtg, Doc) != 0)//    ВыполнитьПереоценкуДляНеттинга(FD, dat, Doc) != 0)
          InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
          ClearErrorArr();
          return 1;
        end;
     end;
  end;

  /*Вложения в ц/б*/
  if(DocKind == DLDOC_ISSUE)
    if(ScOprServDoc.Flag5 == SET_CHAR)
      SetBuff( avoiriss, FDoc );
      if(avoiriss.IndexNom == SET_CHAR)
        if (ВыполнитьПереоценкуВложенийВЦБ(avoiriss, Doc, ID_Operation, ID_Step) != 0)
          InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
          ClearErrorArr();
          return 1;
        end;
      end;
    end;
  end;

  DL_SaveDataForReport_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind, ScOpReportLineData, LOG_TYPE_DATA);
  ScOpReportLineData.Size = 0;
  InsertErrorLog_XML(ScOprServDoc.DocumentID, ScOprServDoc.DocKind);
  ClearErrorArr();

  return 0;
end;
