/*
$Name:             IndBondNominal_Report.mac
$Module:           Ценные бумаги
$Description:      Отчет "Номинал по облигациям с ИН" (НУ)
*/
IMPORT "ws_txreportform.mac";

PRIVATE VAR WebMenuItem; // Информация о запуске отчета из веб
PRIVATE VAR WebFormData;

macro GetAvoirListAddWhere(RepSubKind)
   return " AND AV.t_TaxGroup in(15,25) ";
end;

PRIVATE CLASS (DL_CReportTemplate) TX_IndNomBondNominal_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )

   PRIVATE VAR m_RS, m_H0_1, m_H0_2;
   PRIVATE VAR m_H0_3 = "", m_H0_4 = "", m_DateNom, m_Nom;
   
   /*только ексель*/
   PRIVATE MACRO PrintMode():INTEGER
      return DL_OUTREPORT_EXCEL;
   END;

   PRIVATE MACRO BeginReport( NumCopy:INTEGER )
      m_H0_1 = WebFormData.BeginDate;
      m_H0_2 = WebFormData.EndDate;
   END;

   /*Начало отчетного периода */
   MACRO H0_1():DATE
      return date(m_H0_1);
   END;
  
   /*Отчетная дата, указанная в форме запуска отчета*/
   MACRO H0_2():DATE
      return date(m_H0_2);
   END;
  
   /*Выпуск ц/б*/
   MACRO H0_4():STRING
      return m_H0_4;
   END;
  
   /*Группа налогового учета*/
   MACRO H0_3():STRING
      return m_H0_3;
   END;

   MACRO RegisterTable()

      SetActiveSheet( "Отчет" );
      PrintFormatString( "",
                         "N1_ДатаСоставления", string(Date())+" "+string(Time()),
                         "N1_НомерОпера",      string({oper}) + "  " +GetOperRecByID({oper}).Name,
                         "N1_H0_1",            this.H0_1(),
                         "N1_H0_2",            this.H0_2(),
                         "N1_H0_3",            this.H0_3(),
                         "N1_H0_4",            this.H0_4()
                       );

      RegisterTable( "N1_MainTable", "",
                     "N1_SecType",       
                     "N1_SecCode",       
                     "N1_SecName",       
                     "N1_ISIN",  
                     "N1_SecNum",        
                     "N1_DateNom",       
                     "N1_Nom"
                   );
   END;
 
   MACRO PrintTableLine()

      if( m_Nom == $0 )
         SetCellFont( "N1_Nom", 8, false, false, null, null, DL_REDCOLOR );
      end;

      PrintTableLine(
       /* 1  */     "N1_SecType",        m_RS.TaxGroup,                  null,
       /* 2  */     "N1_SecCode",        m_RS.FI_Code,                   null,
       /* 3  */     "N1_SecName",        m_RS.Name,                      null,
       /* 4  */     "N1_ISIN",           m_RS.ISIN,                      null,
       /* 5  */     "N1_SecNum",         m_RS.LSIN,                      null,
       /* 6  */     "N1_DateNom",        this.DateNom(),                 null,
       /* 7  */     "N1_Nom",            iif(m_Nom == $0,"",this.Nom()), null
                    );
   END;
 
   MACRO GetAddWhereForAvr()

      VAR AvrFIIDCount : Integer = 0;
      VAR AvrFIIDAddWhere : String = "";
    
      m_H0_4 = "";

      if( ( ValType( WebFormData.AvrFIIDList ) != V_UNDEF ) and ( WebFormData.AvrFIIDList.Size > 0 ) )
         while( AvrFIIDCount < WebFormData.AvrFIIDList.Size )
            if( AvrFIIDAddWhere == "" )
               AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
            else
               m_H0_4 = m_H0_4 + ", ";
               AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
            end;
            m_H0_4 = m_H0_4 + WebFormData.AvrFIIDList[AvrFIIDCount].Name;
    
            AvrFIIDAddWhere = AvrFIIDAddWhere + " FI.t_FIID = " + String( WebFormData.AvrFIIDList[AvrFIIDCount].FIID ) + " ";
    
            AvrFIIDCount = AvrFIIDCount + 1;
         end;
         if( AvrFIIDAddWhere != "" )
            AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
         end;
      end;

      if(AvrFIIDAddWhere == "")
         m_H0_4 = "Все";
      end;

      return AvrFIIDAddWhere;
   END;

   MACRO GetAddWhereForTaxGroup()

      VAR GNUAddWhere : String = "";
      VAR GNUCount : Integer = 0;

      m_H0_3 = "";
    
      if( ( ValType( WebFormData.GNUList ) != V_UNDEF ) and ( WebFormData.GNUList.Size > 0 ) )
         while( GNUCount < WebFormData.GNUList.Size )
            if( GNUAddWhere == "" )
               GNUAddWhere = GNUAddWhere + " ( ";
            else
               m_H0_3 = m_H0_3 + ", ";
    
               GNUAddWhere = GNUAddWhere + " OR ";
            end;
    
            m_H0_3 = m_H0_3 + String( WebFormData.GNUList[GNUCount].Name );
    
            GNUAddWhere = GNUAddWhere + " AV.t_TaxGroup = " + String( WebFormData.GNUList[GNUCount].Element ) + " ";
    
            GNUCount = GNUCount + 1;
         end;
         if( GNUAddWhere != "" )
            GNUAddWhere = GNUAddWhere + " ) ";
         end;
      end;

      if(GNUAddWhere == "")
         GNUAddWhere = " AV.t_TaxGroup in(15,25) ";
         m_H0_3 = "Все";
      end;

      return GNUAddWhere;
   END;

   MACRO SC_GetNominalCostByPeriod(OnDate:date)
      var FaceValue;
      var cmd = DL_RSDCommand("select Rsb_Secur.SC_GetNominalCostByPeriod(?, ?, ?) FaceValue from dual");
      cmd.AddParam(m_RS.FIID);
      cmd.AddParam(OnDate);
      cmd.AddParam(OnDate);
      
      var DataSet = cmd.Execute();
      if(DataSet.moveNext())
        FaceValue = DataSet.FaceValue;
      end;
      return FaceValue;
   END;

   MACRO ExecuteReport()

     VAR Select;
     VAR ProgressIndicator, header;
     VAR ProgressValue = CTxProgressValue();
     Var PrintOneString = false;

     Select =    "select av.*, fi.T_FI_CODE, fi.T_NAME "                                                                                                                                        
               + " from (select av.t_fiid, av.t_taxgroup, NVL(av.t_ISIN,'') ISIN, NVL(av.t_LSIN,'') LSIN "
               + "         from davoiriss_dbt av "                                                                                                                      
               + "        where "+GetAddWhereForTaxGroup()+") av, dfininstr_dbt fi "                                                                                            
               + "where fi.T_FIID = av.t_fiid "                                                                                                                            
               + GetAddWhereForAvr()                                                                                                                                              
               + "  and (case when (select count(1) "                                                                                                                   
               + "                    from (SELECT TXREST.T_FIID, TXRest.T_SourceID "                                                                                      
               + "                            FROM DSCTXREST_DBT TXRest "                                                                                               
               + "                           WHERE TXRest.T_AMOUNT > 0 AND "                                                                                            
               + "                                 TXRest.t_BUYDATE <= ? AND "                                                    
               + "                                 TXRest.t_BUYDATE <> TO_DATE('01.01.0001','DD.MM.YYYY')  AND "                                                      
               + "                                 ( TXRest.t_SALEDATE IS NULL  OR "                                                                                    
               + "                                   TXRest.t_SALEDATE = TO_DATE('01.01.0001','DD.MM.YYYY')  OR "                                                     
               + "                                   TXRest.t_SALEDATE >= ? "                                                          
               + "                                 ) "                                                                                                                  
               + "                         ) rest, DSCTXLOT_DBT TXBUYLOT "                                                                     
               + "                   where TXBUYLOT.T_ID = Rest.T_SourceID "                                                                                             
               + "                     AND TXBUYLOT.T_TYPE in(?,?,?) "                                                                                                  
               + "                     and REST.T_FIID = av.t_fiid "                                                                                                     
               + "                 ) > 0 then 1 "                                                                                                                       
               + "            when (SELECT count(1) "                                                                                                                   
               + "                    FROM DSCTXLNK_DBT LNK, DSCTXLOT_DBT CURRSALELOT "                                                                                 
               + "                   WHERE LNK.T_TYPE IN (?,?,?,?) "                                                                                                     
               + "                     AND LNK.T_DATE < ? "                                                                            
               + "                     AND CURRSALELOT.T_ID = LNK.T_SALEID "                                                                                            
               + "                     AND (CURRSALELOT.T_BUYDATE = TO_DATE('01.01.0001','DD.MM.YYYY') OR CURRSALELOT.T_BUYDATE >= ? ) " 
               + "                     and LNK.T_FIID = av.t_fiid "                                                                                                     
               + "                 ) > 0 then 1 "                                                                                                                       
               + "            when (select count(1) "                                                                                                                   
               + "                    from (SELECT dlrq.t_FIID "                                                                                                        
               + "                            FROM ddlrq_dbt dlrq "                                                                                                     
               + "                           WHERE dlrq.t_Type = ? "                                                                                                    
               + "                             AND dlrq.t_DealPart = 1 "                                                                                                
               + "                             and dlrq.t_DocKind = ? "                                                                                               
               + "                             AND dlrq.t_FactDate between ? and ? "                  
               + "                         ) dlrq "                                                                                                                     
               + "                   where dlrq.t_FIId = av.t_fiid "                                                                                                    
               + "                 ) > 0 then 1 "                                                                                                                        
               + "            when (select count(1) "                                                                                                                   
               + "                    FROM ddlrq_dbt dlrq "                                                                                                             
               + "                   WHERE dlrq.t_Type = ? "                                                                                                            
               + "                     AND dlrq.t_DealPart = 1 "                                                                                                        
               + "                     and dlrq.t_DocKind = ? "                                                                                                       
               + "                     AND dlrq.t_FactDate = TO_DATE('01.01.0001','DD.MM.YYYY') "                                                                       
               + "                     and dlrq.t_PlanDate between ? and ? "                          
               + "                     and dlrq.t_FIId = av.t_fiid "                                                                                                    
               + "                 ) > 0 then 1 "                                                                                                                       
               + "            when (select count(1) "                                                                                                                   
               + "                    from (SELECT dlrq.t_FIID, dlrq.t_DocID, dlrq.t_DocKind "                                                                          
               + "                            FROM ddlrq_dbt dlrq "                                                                                                     
               + "                           WHERE dlrq.t_Type = ? "                                                                                                    
               + "                             AND dlrq.t_DealPart = 1 "                                                                                                
               + "                             and dlrq.t_DocKind = ? "                                                                                               
               + "                             and dlrq.t_kind = 0 "                                                                                                    
               + "                         ) dlrq, "                                                                                                                    
               + "                         ( SELECT tick.t_Dealid, tick.t_BofficeKind "                                                                                 
               + "                             FROM ddl_tick_dbt tick "                                                                                                 
               + "                            WHERE tick.t_BofficeKind = ? "                                                                                          
               + "                              AND tick.t_DealDate between ? and ? "                                                                                
               + "                              AND RSB_SECUR.ISBUY(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(TICK.t_DealType, TICK.t_BofficeKind))) = 1 "
               + "                         ) tick "                                                                                                                     
               + "                   where dlrq.t_DocID = tick.t_Dealid "                                                                                               
               + "                     AND dlrq.t_DocKind = tick.t_BofficeKind "                                                                                        
               + "                     and dlrq.t_FIId= av.t_fiid "                                                                                                     
               + "                 ) > 0 then 1 "                                                                                                                       
               + "       else 0 end) = 1 "                                                                                                                              
               + " order by AV.T_TAXGROUP, fi.T_FI_CODE";                                                                                                              

     var cmd = DL_RSDCommand(Select);

     cmd.addParam( H0_1() );
     cmd.addParam( H0_1() );

     cmd.addParam( TXLOTS_BUY );
     cmd.addParam( TXLOTS_BACKREPO );
     cmd.addParam( TXLOTS_LOANGET );

     cmd.addParam( TXLNK_DELREPO );
     cmd.addParam( TXLNK_SUBSTREPO );
     cmd.addParam( TXLNK_LOANPUT );
     cmd.addParam( TXLNK_SUBSTLOAN );

     cmd.addParam( H0_1() );
     cmd.addParam( H0_1() );

     cmd.addParam( DLRQ_TYPE_DELIVERY );
     cmd.addParam( DL_SECURITYDOC );
     cmd.addParam( H0_1() );
     cmd.addParam( H0_2() );

     cmd.addParam( DLRQ_TYPE_DELIVERY );
     cmd.addParam( DL_SECURITYDOC );
     cmd.addParam( H0_1() );
     cmd.addParam( H0_2() );

     cmd.addParam( DLRQ_TYPE_DELIVERY );
     cmd.addParam( DL_SECURITYDOC );
     cmd.addParam( DL_SECURITYDOC );
     cmd.addParam( H0_1() );
     cmd.addParam( H0_2() );

     ProgressValue.Maximum = cmd.GetCount();
     ProgressValue.Current = 0;

     this.RegisterTable();

     if (ProgressValue.Maximum > 0)
        ProgressIndicator = CreateProgressIndicator(true);

        if( (WebMenuItem != null) )
          header = "Формирование отчета " + WebMenuItem.szNameItem;
          ProgressIndicator.Start(ProgressValue.Maximum, header, header);
        else
          ProgressIndicator.Start(ProgressValue.Maximum, HTML_StSheetName, "Отбор выпусков ц/б для отчета \"Номинал по облигациям с ИН\"");
        end;

        m_RS = cmd.Execute();
        while( m_RS.MoveNext() )

           m_DateNom = H0_1();
           while( m_DateNom <= H0_2() )
              m_Nom = SC_GetNominalCostByPeriod(m_DateNom);
              this.PrintTableLine();
              m_DateNom = m_DateNom + 1;
           end;

           ProgressValue.Current = ProgressValue.Current + 1;
           ProgressIndicator.Update(ProgressValue.Current,ProgressValue.Maximum); // всего 3 парамера Update(index, count, text)
        end;

        ProgressIndicator.Stop();
     end;

     EndTable();
     AddStandardFooter_Reg2014( "N1_", null, true );

   END;

   MACRO DateNom():DATE
      return m_DateNom;
   END;

   MACRO Nom()
      if( m_Nom != null )
         return ВыводЧерезФормулу(m_Nom);
      end;
      return null;
   END;

   MACRO Create()
      this.ExecuteReport();
   END;

   MACRO EndReport()
      ReplaceAndCalculate( GetDL_FORMULA(), "=" );
   END;

   initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var FullReportFileNames = TArray();
  var IndBondNominal_Report;

  WebMenuItem = menuItem;
  WebFormData = FormData;

  IndBondNominal_Report = TX_IndNomBondNominal_Report( null, "Report_IndBondNominal.xls", null, IsWebService );
  IndBondNominal_Report.Run();

  FullReportFileNames = IndBondNominal_Report.GetFullReportFileNames();
  Dl_CallInsertStat(DL_OUTREPORT_EXCEL, menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem);

  return FullReportFileNames;
END;
