/*
 $Name: nptxnettop020.mac
 $Module: Ценные бумаги
 $Description: Операция зачета удержанного НДФЛ. Шаг "Отправка в Хранилище"
 */

import DealsInter, "nptxstbfun.mac";

private macro Error( err, ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  if(err)
    MsgBox( ErrStr );
  end;
  return err;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  var err = 0;
  var nptxop = TRecHandler("nptxop.dbt");
  var NeedLoopStep = false;
  var ErrStr = "";
  var query, cmd, DataSet, cnt = 0;
  var CreatedOnStep_ID = 0;
  var NotPlanRecalcStep = 0;
  var STB = TRecHandler("nptxtotalbase.dbt");
  var STBArr = TArray();

  SetBuff( nptxop, FDoc );

  if(РАБОТА_С_ВНЕШНИМ_ХРАНИЛИЩЕМ_СНОБ() == true)
    query =   " select tb.* "
            + "   from dnptxtotalbase_dbt tb "
            + "  where tb.t_ID_Operation = ? "
            + "    and tb.t_ID_Step > (select NVL(MAX(oprs.t_ID_Step), 0) as MaxID_Step "
            + "                         from doprstep_dbt oprs"
            + "                        where oprs.t_ID_Operation = ? "
            + "                          and oprs.t_ID_Step < ? "
            + "                          and oprs.t_Symbol = 'О' "
            + "                          and Exists(select 1 "
            + "                                       from doprstep_dbt oprs1 "
            + "                                      where oprs1.t_ID_Operation = oprs.t_ID_Operation "
            + "                                        and oprs1.t_ID_Step > oprs.t_ID_Step "
            + "                                        and oprs1.t_Symbol = 'З' "
            + "                                    ) "
            + "                    )"
            + "    and tb.t_ConfirmState = " + NPTXTOTALBASE_CONFIRMSTATE_NOTCONFIRMED
            + "    and not (tb.t_StorState = " + NPTXTOTALBASE_STORSTATE_CANCELED + " and tb.t_StorID = CHR(1)) ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(ID_Operation);
    cmd.AddParam(ID_Operation);
    cmd.AddParam(ID_Step);

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      DataSet.GetRecord().CopyTo(STB.rec);

      AddSTBtoArr(STBArr, STB);
    end;

    err = STB_ExecuteSendSTBArrToStorOnStep(STBArr, nptxop.rec.DocKind, nptxop.rec.ID, ID_Operation, ID_Step, @NeedLoopStep, @ErrStr, true);

    if(not err)
      DL_NPTX_PutMsg( "   Выполнена отправка в Хранилище обновленной и новых записей о событиях СНОБ", 50 );
    end;
  end;

  if(not err)
    err = DL_NPTX_SaveTbMes();

    if(not err)
      if((NeedLoopStep == true))
        if( not InsertOprStatus(46491, 5)) //Установить ДО = Отправка в Хранилище
          return Error( "Ошибка при установке статуса вида \"Документооборот\" операции" );        
        end;
      else
        GetLastOpSTBVal(nptxop.rec.DocKind, nptxop.rec.ID, NPTXVAL_KIND_NOTPLANRECALCSTEP, @NotPlanRecalcStep, NULL, NULL);

        if(NotPlanRecalcStep == 0)
          if( not InsertOprStatus(46491, 4)) //Установить ДО = Зачет
             return Error( "Ошибка при установке статуса вида \"Документооборот\" операции" );        
          end;
        else
          if( not InsertOprStatus(46491, 2)) //Установить ДО = Закрыт
             return Error( "Ошибка при установке статуса вида \"Документооборот\" операции" );        
          end;
        end;
      end;
    end;
  end;

  DL_NPTX_SaveOperLog( nptxop.rec.ID, ID_Step );

  return err;
end;

/* Макрос постобработки */
MACRO PostStep (CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,          /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

  record nptxop("nptxop");
  var ErrStr = "";
  var err = 0;

  SetBuff( nptxop, FDoc );

  if(CommitOrRollback == 2)
    err = STB_RollbackSendToStorOnStep(nptxop.DocKind, nptxop.ID, ID_Operation, ID_Step, @ErrStr);
    if(ErrStr != "")
      msgbox(ErrStr);
    end;
  end;

  return err;
END;
