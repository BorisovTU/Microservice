/*
$Name:             ws_txpartylist.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Макрос сервисов (содержащих параметр с доп. условиями), вызываемых виджетом PartyLookupWidget
*/
import "ws_partylistclass.mac";

// Дополнительные параметры фильтрации для виджета PartyLookupWidget
class TxPartyListFiltrParm
  var IsUsedEndDate:Bool;
  var EndDate:Bool;
  var ExcludePartyKindCollection;
end;

private macro GetAddWhere( AddParm )
  var StrSqlAddWhere:String = "";
  var ExcludePartyKindListStr = "";
  var i = 0;

  if( ValType( AddParm ) != V_UNDEF )

     // ограничение для поля "Клиент" отчета "Уведомление о контролируемых сделках"
     if( ( ValType( AddParm.IsUsedEndDate ) == V_BOOL )
     and ( AddParm.IsUsedEndDate ) )
       if( ValType( AddParm.EndDate ) == V_DATE )
         if( AddParm.EndDate != Date( 0, 0, 0 ) )
           StrSqlAddWhere = " AND " + ExecMacroFile("NotifContrDeal_Form.mac", "AddWhereForClientFld", AddParm.EndDate);
         else
           StrSqlAddWhere = " AND " + ExecMacroFile("NotifContrDeal_Form.mac", "AddWhereForClientFld", Date(0,0,0));
         end;
       end;
     end;

     if( (AddParm.ExcludePartyKindCollection != null) and (AddParm.ExcludePartyKindCollection.Size > 0) )
       i = 0;
       while( i < AddParm.ExcludePartyKindCollection.Size)
         if(ExcludePartyKindListStr != "" )
           ExcludePartyKindListStr =  ExcludePartyKindListStr + " , ";
         end;
         ExcludePartyKindListStr = ExcludePartyKindListStr + string(AddParm.ExcludePartyKindCollection[i]);
         i = i + 1;
       end;

       if(ExcludePartyKindListStr != "" )
         StrSqlAddWhere = StrSqlAddWhere + 
                         " AND t.T_PARTYID NOT IN " +
                         " (SELECT PO.T_PARTYID " +
                         " FROM DPARTYOWN_DBT PO " +
                         " WHERE PO.T_PARTYKIND IN ( " + ExcludePartyKindListStr + " )) ";
       end;
     end;

     // ограничение для листалки клиентов 
     if( (AddParm.IsInclude != null) and (AddParm.IsInclude) )
       StrSqlAddWhere = StrSqlAddWhere + " AND T.T_PARTYID IN (SELECT DISTINCT CL.T_PARTYID FROM DRKCLIENT_DBT CL) ";
     else
       StrSqlAddWhere = StrSqlAddWhere + " AND T.T_PARTYID NOT IN (SELECT DISTINCT CL.T_PARTYID FROM DRKCLIENT_DBT CL) ";
     end;
  
     if( (AddParm.Kind_Client != null) and (AddParm.Kind_Client != 0) )
       StrSqlAddWhere = StrSqlAddWhere + " AND T.T_LEGALFORM = " + String(AddParm.Kind_Client) + " ";
     end;

  end;

  return StrSqlAddWhere;
end;

class (PartyList) PartyList_TxPartyListExt

  var Param;//:TxPartyListFiltrParm;

  macro GetList(pagesize         : integer,
                pagenum             : integer,
                listkind            : integer,
                namekind            : integer,
                codekind            : integer,
                paperkind            : integer,
                partykindcollection : string,
                filteritems,
                orderitems):TArray

    var result = TArray();
    var strAddWhere:string = "";
    var p;
    var query = " select * from (SELECT * FROM ( ";

    query = query + GetPartySQL(listkind, namekind, codekind, paperkind);
    strAddWhere = GetPartyAddWhereCondition( FilterItems );

    if( strAddWhere != "" )
       query = query + strAddWhere;
    end;

    query = query + GetAddWhere( Param );

    if( partykindcollection != "" )
      query = query + " AND t.T_PARTYID IN " +
                      " (SELECT PO.T_PARTYID " +
                      " FROM DPARTYOWN_DBT PO " +
                      " WHERE PO.T_PARTYKIND IN ( " + string(partykindcollection) + " )) ";
    end;

    query = query + " ORDER BY t.t_partyid ";
    query = query + " ) AA ) tt ";
    query = query + " ) ";

    if( (orderitems == null) or (OrderItems.Size() > 0) )
      query = query + " ORDER BY " + PT_GetSortStr(orderitems, "");
    end;
    query = query + " ) ";

    if( pagesize > 0 )
      query = query + " WHERE RN BETWEEN " + string(PageSize*(PageNum) + 1) + " AND " + string(PageSize*(PageNum + 1)) ;
    end;

    PT_PrintDebug( "ws_partylistclass.mac.log", "*** GetList ***");
    PT_PrintDebug( "ws_partylistclass.mac.log", query);

    var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
    ds.NullConversion = true;

    while (ds.MoveNext())
      p = TWsPartyEx();
      SetParty(p, ds);
      result.value(result.Size) = p;
    end;

    return result;
  end;

  macro GetCount( listkind        : integer,
                  namekind            : integer,
                  codekind            : integer,
                  partykindcollection : string,
                  filteritems):integer

    var result : integer;
    var strAddWhere:string = "";
    var query = "SELECT COUNT(*) C FROM ( ";

    query = query + GetPartySQL(listkind, namekind, codekind, -1);
    strAddWhere = GetPartyAddWhereCondition( FilterItems );

    if( strAddWhere != "" )
      query = query + strAddWhere;
    end;

    query = query + GetAddWhere( Param );

    if( partykindcollection != "" )
      query = query + " AND t.T_PARTYID IN " +
                      " (SELECT PO.T_PARTYID " +
                      " FROM DPARTYOWN_DBT PO " +
                      " WHERE PO.T_PARTYKIND IN ( " + string(partykindcollection) + " )) ";
    end;

    query = query + " ) AA ) tt ";
    query = query + ")";

    PT_PrintDebug( "ws_partylistclass.mac.log", "*** GetCount ***");
    PT_PrintDebug( "ws_partylistclass.mac.log", query);

    var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
    ds.NullConversion = true;

    if (ds.MoveNext())
      result = ds.c;
    end;

    return result;
  end;

  macro LookUp( mode              : integer,
                listkind            : integer,
                namekind            : integer,
                codeKind            : integer,
                partykindcollection : string,
                sub                 : string,
                listLen             : integer,
                param):TArray

    var result = TArray();
    var p;
    var query = " SELECT * FROM ( " + GetPartySQLold(listkind, namekind, codekind);

    query = query + GetAddWhere( param );    

    if(mode == 0)
      query = query + " ORDER BY T_CODE ";
    end;

    if(mode == 1)
      query = query + " ORDER BY T_NAME ";
    end;    
    
    query = query + " ) AA WHERE ";

    if(mode == 0)
      query = query + " LOWER (AA.T_CODE) ";
    end;

    if(mode == 1)
      query = query + " LOWER (AA.T_NAME) ";
    end;

    query = query + " LIKE LOWER ('%' || ? || '%') ";

    if( listLen != 0 )
      query = query + " AND ROWNUM < " + string(listLen);
    end;

    if( partykindcollection != "" )
      query = query + " AND AA.T_PARTYID IN " +
                      " (SELECT PO.T_PARTYID " +
                      " FROM DPARTYOWN_DBT PO " +
                      " WHERE PO.T_PARTYKIND IN ( " + string(partykindcollection) + " )) ";
    end;
    
    var cmd = RSDCommand(query);
    cmd.addParam( "", RSDBP_IN, sub );
    cmd.NullConversion = true;
    cmd.Execute();

    PT_PrintDebug( "ws_partylistclass.mac.log", "*** LookUp ***");
    PT_PrintDebug( "ws_partylistclass.mac.log", cmd.CmdText);    

    var ds = TRsbDataSet( cmd );

    while (ds.moveNext())
      PT_PrintDebug( "ws_partylistclass.mac.log", ds.partyid);
      p = TWsPartyEx();
      SetParty_old(p, ds);
      result.value(result.Size) = p;
    end;

    return result;
  end;  

end;

///////////////////////////////////////////////////////////
// Получить список пользователей постранично
// PageSize - размер страницы
// PageNum - номер страницы НАЧИНАЯ С 0 !!!
///////////////////////////////////////////////////////////
macro GetPartyList( pagesize      : integer,
                    pagenum             : integer,
                    listkind            : integer,
                    namekind            : integer,
                    codekind            : integer,
                    paperkind           : integer,
                    partykindcollection : string,
                    filteritems,
                    orderitems,
                    param):TArray

  var obj = PartyList_TxPartyListExt();

  obj.Param = param;

  return obj.GetList( pagesize, pagenum, listkind, namekind, codekind, paperkind, partykindcollection, filteritems, orderitems );


end;

///////////////////////////////////////////////////////////
// Получить общее количество субъектов (для паджинации)
///////////////////////////////////////////////////////////
macro GetPartyCount(listkind      : integer,
                    namekind            : integer,
                    codekind            : integer,
                    partykindcollection : string,
                    filteritems,
                    param):integer

  var obj = PartyList_TxPartyListExt();

  obj.Param = param;

  return obj.GetCount( listkind, namekind, codekind, partykindcollection, filteritems);

end;

///////////////////////////////////////////////////////////
// Получить объект TWsPartyEx по CodeName
///////////////////////////////////////////////////////////
macro CheckPartyByCodeName(listkind     : integer,
							  mode                : integer,
                              namekind            : integer,
                              codeKind            : integer,
                              Str                 : string,
                              param):bool

  var obj = PartyList_TxPartyListExt();

  return obj.GetByCheck(listkind, mode, namekind, codeKind, Str );                              

end;

///////////////////////////////////////////////////////////
// Возвращает список субъектов длинны listLen по
// частичному совпадению значения кода вида кода codeKind или наименования
///////////////////////////////////////////////////////////
macro LookupPartyByCodeName(mode      : integer,
                            listkind            : integer,
                            namekind            : integer,
                            codeKind            : integer,
                            partykindcollection : string,
                            sub                 : string,
                            listLen             : integer,
                            param):TArray

  var obj = PartyList_TxPartyListExt();

  return obj.Lookup(mode, listkind, namekind, codeKind, partykindcollection, sub, listLen);

end;

////////////////////////////////////////////////
// Возвращает номер строки для partyid
////////////////////////////////////////////////
macro GetPartyRowNum(partyid       : integer,
                     listkind            : integer,
                     namekind            : integer,
                     codekind            : integer,
                     partykindcollection : string,
                     param) : integer

  var obj = PartyList_TxPartyListExt();

  return obj.GetRowNum(partyid, listkind, namekind, codekind, partykindcollection);

end;