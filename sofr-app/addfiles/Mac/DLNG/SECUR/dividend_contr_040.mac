/*
$Name:         dividend_contr_040.mac
$Module:       Ценные бумаги
$Description:  Операция получения дивидендов пл РЕПО (от Контрагента)
*/
/*  Оплата получателю   */

import InsCarryDoc, "sp_class.mac", "sp_car2.mac", "spservsql.mac";

private macro УчетОплатыНалога(FD:SPFirstDoc, factDate:date, IsExistRest:bool):bool
   var СчОплачНалог  = TrecHandler("account.dbt"), 
       AccountCt     = TRecHandler("account.dbt"), 
       СчБюджет      = TRecHandler("account.dbt"); 
   var leg           = FD.dl_leg.rec;
   var tick          = FD.tick.rec;
   var bAcc60301: bool = Использовать_Сч60301();
   var S = $0;

   if ( not FD.OpenAccount("Оплаченный налог", null, false, null, СчОплачНалог, factDate) )
      return false;
   end;

   if( IsExistRest )
      if ( not FD.OpenAccount( "Начисленные дивиденды", null, false, 
                               IIF(leg.DmaxPay < factDate,FIROLE_DEALS_OVERDUE,null),
                               AccountCt, factDate, tick.CurPay) )
         return false;
      end;
   else
      if ( not FD.OpenAccount( "+Расчеты", null, false, FIROLE_FIREQ, AccountCt, factDate, tick.CurPay ) )
         return false;
      end;
   end;

   if( not bAcc60301 )
      if( not ПроводкаПоКатегориямУчета( 
              FD, 
              СчОплачНалог, AccountCt, 
              factDate,                      /* Дата  */
              1,                             /* Глава */
              tick.CurPay,                   /* Валюта суммы проводки */
              leg.ReturnIncome,              /* Сумма проводки*/    
              Doc,                           /* Документ */
              INPCARRY,                      /* Result_Carry */
              " 1",                          /* Kind_Oper */
              tick.DealCode,                 /* Numb_Document */
              "Учет налога"                  /* Ground */
          ) 
        )
         return false;
      end;
   else
      if ( not FD.OpenAccount( "-Бюджет, ф.налоги", null, false, null, СчБюджет, factDate) )
          return false;
      end;

      S = leg.ReturnIncome;
      if (tick.CurPay != NATCUR)
         if( not SmartConvertSum( S, S, factDate, tick.CurPay, NATCUR, true ) )
            return false;
         end;
      end; 
      
      if( not ПроводкаПоКатегориямУчета( 
              FD, 
              СчОплачНалог, СчБюджет, 
              factDate,                      /* Дата  */
              1,                             /* Глава */
              NATCUR,                        /* Валюта суммы проводки */
              S,                             /* Сумма проводки*/    
              Doc,                           /* Документ */
              INPCARRY,                      /* Result_Carry */
              " 1",                          /* Kind_Oper */
              tick.DealCode,                 /* Numb_Document */
              "Учет налога"                  /* Ground */
          ) 
        )
          return false;
      end; 

      if( not ПроводкаПоКатегориямУчета( 
              FD, 
              СчБюджет, AccountCt,   
              factDate,                      /* Дата  */
              1,                             /* Глава */
              tick.CurPay,                   /* Валюта суммы проводки */
              leg.ReturnIncome,              /* Сумма проводки*/    
              Doc,                           /* Документ */
              INPCARRY,                      /* Result_Carry */
              " 1",                          /* Kind_Oper */
              tick.DealCode,                 /* Numb_Document */
              "Учет налога"                  /* Ground */
          ) 
        )
         return false;
      end;    
   end;

   return true;
end;

private macro DoOperation(deal, leg, tick): bool
     
    var AccountDt = TrecHandler( "account.dbt");
    var AccountCt = TrecHandler( "account.dbt");
    var FD = SPFirstDoc( deal );

    var fiRole            = null;
    var qnty: money       = 0;

    var q = "select Nvl(Sum(dt.t_Qnty),0) qnty from dcontrduties_dbt dt " 
                             + " where dt.t_PartyID = "+tick.PartyID+" and dt.t_PFI = "+tick.PFI+" and dt.t_Dfix = "+GetSQLDate(leg.Start)
                             + " and dt.t_DealRepo = " + tick.ParentID 
                             + " and dt.t_BeforePay = 'X'";
    var cmd = DL_RSDCommand(q);
    var DataSet = cmd.Execute();
    if ( DataSet.moveNext() )
        qnty = DataSet.qnty;
    end;

    // ------------------ Блок проводок КА ----------------------
    if ( qnty != 0 ) //Если есть ненулевой остаток в таблице ?Долг контрагентов по дивидендам? с призна-ком ?До выплаты эми-тентом?
        // -------- КА1 -------      
        if( (FD.dl_leg.rec.ReturnIncome > 0) and (not УчетОплатыНалога(FD, deal.dealDate, true)) )
            return false;
        end;
        // -------- КА2 ------- 
        if(ПолучитьДенежныйСчетСубъектаПоСделке( tick.BOfficeKind, 
                                                 tick.DealID, 
                                                 tick.PartyID, 
                                                 FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.FIID, 
                                                 AccountDt) != 0)
             Msgbox( "Не заполнены платежные реквизиты субъекта с ID = " +string(tick.PartyID) );
             return false;
        end;

        fiRole = null;
        if ( leg.DmaxPay < {CurDate} ) // Определение просроченности для КУ "Начисленные дивиденды"
            fiRole = FIROLE_DEALS_OVERDUE;
        end;
        if ( not FD.OpenAccount( "Начисленные дивиденды", null, false, fiRole, AccountCt, deal.dealDate, tick.CurGet /*CurGet*/) )
            return false;
        end;

        if( not ПроводкаПоКатегориямУчета( 
               FD, 
               AccountDt, AccountCt, 
               deal.dealDate,                      /* Дата */
               1,                                  /* Глава */
               tick.CurGet,       /*CurGet*/       /* Валюта суммы проводки */
               leg.ReceiptAmount, /*SumGet*/       /* Сумма проводки*/    
               Doc,                                /* Документ */
               INPCARRY,                           /* Result_Carry */
               " 1",                               /* Kind_Oper */
               deal.DealCode,                      /* Numb_Document */
               "Зачисление суммы от контрагента",  /* Ground */
               null,null,null,
               AccountDt.rec.code_currency, AccountCt.rec.code_currency  // валюты счетов: Дт - Кт 
            ) 
        )
            MsgBox( "Ошибка при выполнении проводки" );
            return false;
        end; // ------------------ КОНЕЦ - Блок проводок КА ----------------------
    // ------------------ Блок проводок КБ ----------------------
    // Нет ненулевого остаток в таблице "Долг контрагентов по дивидендам" с признаком "До выплаты эмитентом"
    else //elif ( qnty == 0 ) 
         // -------- КБ1 -------      
        if( (FD.dl_leg.rec.ReturnIncome > 0) and (not УчетОплатыНалога(FD, deal.dealDate, false)) )
            return false;
        end;
        // -------- КБ2 ------- 
        if(ПолучитьДенежныйСчетСубъектаПоСделке( tick.BOfficeKind, 
                                                 tick.DealID, 
                                                 tick.PartyID, 
                                                 FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.FIID, 
                                                 AccountDt) != 0)
             Msgbox( "Не заполнены платежные реквизиты субъекта с ID = " +string(tick.PartyID) );
             return false;
        end;

        if ( not FD.OpenAccount( "+Расчеты", null, false, FIROLE_FIREQ, AccountCt, deal.dealDate, tick.CurGet /*CurGet*/) )
            return false;
        end;

        if( not ПроводкаПоКатегориямУчета( 
               FD, 
               AccountDt, AccountCt, 
               deal.dealDate,                      /* Дата */
               1,                                  /* Глава */
               tick.CurGet,       /*CurGet*/       /* Валюта суммы проводки */
               leg.ReceiptAmount, /*SumGet*/       /* Сумма проводки*/    
               Doc,                                /* Документ */
               INPCARRY,                           /* Result_Carry */
               " 1",                               /* Kind_Oper */
               deal.DealCode,                      /* Numb_Document */
               "Зачисление суммы от контрагента",  /* Ground */
               null,null,null,
               AccountDt.rec.code_currency, AccountCt.rec.code_currency  // валюты счетов: Дт - Кт 
            ) 
        )
            MsgBox( "Ошибка при выполнении проводки" );
            return false;
        end;

    end;

    return true;
end;


macro ExecuteStep( Doc, FDoc ) 
    record deal(DL_TICK);
    SetBuff( deal, FDoc ); 
     
    VAR FD   = SPFirstDoc( deal );
    var leg  = FD.dl_leg.rec;
    var tick = FD.tick.rec;
 
    if ( not DoOperation(deal, leg, FD.tick.rec ) )
        return 1;
    end;

    // Формирование записи для бэкапа
    var contrDt = TrecHandler( "contrduties.dbt" );
    contrDt.rec.PFI = tick.PFI;
    contrDt.rec.Dfix = leg.Start;
    contrDt.rec.PartyID = tick.PartyID;
    contrDt.rec.dealRepo = tick.ParentID;
    contrDt.rec.Qnty = 0;
    contrDt.rec.BeforePay = "X";
      
     var q = "select Nvl(dt.t_Qnty,0) qnty, dt.t_BeforePay from dcontrduties_dbt dt " 
                             + " where dt.t_PartyID = "+tick.PartyID+" and dt.t_PFI = "+tick.PFI+" and dt.t_Dfix = "+GetSQLDate(leg.Start)
                             + " and dt.t_DealRepo = " + tick.ParentID
                             + " and RowNum = 1";
    var cmd = DL_RSDCommand(q);
    var DataSet = cmd.Execute();
    if ( DataSet.moveNext() )
        contrDt.rec.Qnty = DataSet.qnty;
        contrDt.rec.BeforePay = DataSet.BeforePay;
    end;

    var qnty: moneyl = leg.Principal;
    var stat = 0;
    DL_CreateContrDuties(contrDt, qnty, stat); // RSI_DLRQ.RSI_UpdateContrDuties|RestoreContrDuties -  установка суммы долга КА с учетом вычета qnty. Также храним "старую" запись для отката

    if ( stat != 0 )
        MsgBox("Ошибка обновления в таблице 'Долг контрагентов по дивидендам'!");
        return 1;
    end;

    if( not InsertOprStatus( 1511, 3) )
        msgbox( "Ошибка при установке статуса <Документооборот> операции" );
        return 1;
    end;     
    
    return stat;

end;  
