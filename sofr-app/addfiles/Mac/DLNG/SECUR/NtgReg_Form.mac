/*******************************************************************************
 DESCRIPTION  :   Отчет "Регистр неттинга" (НУ) - форма ввода данных
 PROGRAMMED BY:   Леонов И. Е.
 CREATION DATE:   05.10.06
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac", "RegistrReport.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_NTGREG_PERIOD     = 0,
      PNFLD_NTGREG_YEAR       = 1,
      PNFLD_NTGREG_GROWTH     = 2,
      PNFLD_NTGREG_BEGINDATE  = 3,
      PNFLD_NTGREG_ENDDATE    = 4,
      PNFLD_NTGREG_AVRGROUP   = 5,
      PNFLD_NTGREG_AVRKIND    = 6,
      PNFLD_NTGREG_AVRCODE    = 7,
      PNFLD_NTGREG_AVRNAME    = 8,
      PNFLD_NTGREG_PRINTMETHOD= 9;

/*Группа налогового учета*/
PRIVATE MACRO FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;
  VAR Avoiriss, AvrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
              + "  and t_Element <> 919 and t_Element <> 943 order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Name");    
        FldRealValue = Choice.Value("t_Element");
     end;

  end;

  return CM_DEFAULT;
END;

/*Ценная бумага*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, AvrIssuer;
  VAR RetVal = CM_DEFAULT;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoiriss = TRecHandler( "avoiriss.dbt" );

  if( Cmd == DLG_KEY ) 
     if( (Key == KEY_F3) OR (Key == KEY_ALTF3) OR (Key == KEY_CTRLF3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

        AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
        if( (AvrKind == NULL) OR (AvrKind < 0) )
           AvrKind = 0;
        end;

        AddTable  = AddTable  + "davoiriss_dbt AV";
        WhereCond = WhereCond + " AND AV.t_FIID = t.t_FIID "
                              + " AND AV.t_TaxGroup not in(919, 943) ";

        AvrAttrID = pThis.GetLinkedValue(DL_PNFLD_AVRGROUP);
        if( (AvrAttrID != NULL) AND (AvrAttrID > 0) )
           WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrAttrID;
        end;

        AvrIssuer = pThis.GetLinkedValue(DL_PNFLD_AVRISSUER);
        if( (AvrIssuer != NULL) AND (AvrIssuer > 0) )
           WhereCond = WhereCond + " AND t.t_Issuer = " + AvrIssuer;
        end;

        /* только неиндивидуальные (FI_IsSecurIndividualNoFind) */    
        if( AddTable != "" )                                          
           AddTable  = AddTable + ",";                                
        end;                                                          
        AddTable  = AddTable  + " davrkinds_dbt AKind";               
        WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind AND AKind.t_IsIndividual != 'X')";
         
        if( Key == KEY_F3 )                                                                                                           
           /*эмиссионные (обобщенные) или (фактические, для которых не указана ссылка на обобщенный) или неэмиссионные любые*/        
           WhereCond = WhereCond + " AND ( AKind.t_IsEmissive != 'X' OR ( t.t_IsGeneralized = 'X' OR t.t_GeneralizedFIID < 0 ) )";    
        elif( Key == KEY_ALTF3 )                                                                                                      
           /* фактические выпуски, у которых указана ссылка на обобщенный*/                                                           
           WhereCond = WhereCond + " AND ( t.t_IsGeneralized != 'X' AND t.t_GeneralizedFIID > 0 )";                                   
        elif( Key == KEY_CTRLF3 )                                                                                                     
           /*все неиндивидуальные и необобщенные (фактические) ц/б*/                                                                  
           WhereCond = WhereCond + " AND t.t_IsGeneralized != 'X' ";                                                                  
        end;                                                                                                                          
 
        if( pThis.ExistField(DL_PNFLD_EMISS_AVR ) ) 
           if( pThis.GetLinkedValue(DL_PNFLD_EMISS_AVR) )
              AddTable  = AddTable  + " davrkinds_dbt AKind1 ";
              WhereCond = WhereCond + " AND (AKind1.t_FI_Kind = t.t_FI_Kind AND AKind1.t_AvoirKind = t.t_AvoirKind AND AKind1.t_IsEmissive = 'X')";
           end;
        end;

        if( ListAvoiriss( AvrKind, Fininstr, Avoiriss, null, WhereCond, AddTable ) )
           FldRealValue = Fininstr.rec.FIID;
           FldShowValue = Fininstr.rec.FI_Code;

           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );
           pThis.SetLinkedValue( DL_PNFLD_AVRGROUP, Avoiriss.rec.TaxGroup );
           pThis.SetLinkedValue( DL_PNFLD_AVRKIND, Fininstr.rec.AvoirKind );
        end;
     end;
     if(Key == KEY_SPACE)
        FldRealValue = -1;
        FldShowValue = STR_ALLVALUE;
        if( pThis.ExistField(DL_PNFLD_AVRNAME) )
           pThis.SetLinkedValue( DL_PNFLD_AVRNAME, STR_ALLVALUE );
        end;
     end;
  else
     RetVal = DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  return RetVal;
END;

CLASS (DL_CPanel) SP_NtgReg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;

     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     AddFieldProc( 0, PNFLD_NTGREG_PERIOD,      DL_PNFLD_PERIOD,      @DL_FldProc_Period, PrevQuartal + 12, 17, 7 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_NTGREG_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_NTGREG_GROWTH,      DL_PNFLD_GROWTH,      @DL_FldProc_Growth);
     AddFieldProc( 0, PNFLD_NTGREG_BEGINDATE,   DL_PNFLD_BEGINDATE,   @DL_FldProc_BeginDate );
     AddFieldProc( 0, PNFLD_NTGREG_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate );
//     AddFieldProc( 0, PNFLD_NTGREG_AVRGROUP,    DL_PNFLD_AVRGROUP,    @DL_FldProc_AvrGroup );
     AddFieldProc( 0, PNFLD_NTGREG_AVRGROUP,    DL_PNFLD_AVRGROUP,    @FldProc_AvrGroupFltr, null, 10, 5 );
     AddFieldProc( 0, PNFLD_NTGREG_AVRKIND,     DL_PNFLD_AVRKIND,     @DL_FldProc_AvrKind );
     AddFieldProc( 0, PNFLD_NTGREG_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_NTGREG_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_NTGREG_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 36, 17 );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "REGNETT" ); 
END;
