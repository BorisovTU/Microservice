/**
  @file CheckRequestSnobStor.mac
  @brief Макрос проверки выполнения процедуры запроса к хранилищу СНОБ через общесистемную плановую процедуру

  #link
  - [BOSS-187.4_Разработка планировщика запроса к Хранилищу СНОБ] (https://sdlc.go.rshbank.ru/confluence/pages/viewpage.action?pageId=248463674)

  #changeLog
  |date      |author       |tasks    |note                                                                                                          
  |----------|-------------|---------|--------------------------------------------------------------------------------------------------------------
  |30.01.2024|Хромеев Д. С.|BOSS-1819|BOSS-253_Разработка планировщика запроса к Хранилищу СНОБ. Разработка
*/

import OprInter, DealsInter, "spserv.mac", "RequestSnobStor_Protocol.mac", "u_common_email_utils.mac";

/**
  @brief Функция определения системной даты и времени
*/
private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

/**
  @brief Попытаться получить ошибку отправки сообщения
  @return Текст ошибки (String)
*/
PRIVATE MACRO GetSendError():String
   var queryError = "select NVL(notify.t_Error, CHR(0)) as t_Error " +
                    "from demail_notify_dbt notify " +
                    "order by notify.t_ID desc ";

   var cmdError = DL_RSDCommand(queryError);
   var DataSetError = cmdError.Execute();

   if(DataSetError.MoveNext())
      return DataSetError.Error;
   end;

   return "";
END;

/**
  @brief Отправить сообщение электронной почты рассылкой по группе "Получатели уведомления о работе процедуры запроса данных от Хранилища СНОБ"
  @param Msg Текст сообщения
*/
PRIVATE MACRO SendEmail(SystDate:Date)
   var ErrorStr = "";
   var v_email_processor = c_email_proc_env();

   v_email_processor.m_get_email_group(79);
   v_email_processor.m_set_msg_head("Внимание! Сбой выполнения планового запроса к Хранилищу СНОБ!");
   v_email_processor.m_add_row_to_msg_text("Добрый день!");
   v_email_processor.m_add_row_to_msg_text("Плановая процедура выполнения запроса к Хранилищу СНОБ не отработала в автоматическом режиме за " + string(SystDate:f) + ". Необходимо выполнить процедуру интерфейсно. Для этого в подсистеме \"Ценные бумаги\" в скроллинге  \"Налоговый учет\"/ \"НДФЛ\"/ \"СНОБ\"/ \"Скроллинг событий СНОБ из ДИАСОФТА\" добавить и исполнить операцию запроса данных из Хранилища СНОБ с параметрами:");
   v_email_processor.m_add_row_to_msg_text("Дата запуска процедуры:  текущая системная дата");
   v_email_processor.m_add_row_to_msg_text("Налоговый год:  Минус 1 от года текущей системной даты Например; Системная дата: 04/01/2024   Налоговый год = 2023");
   v_email_processor.m_add_row_to_msg_text("Клиент : Все.");

   v_email_processor.m_save_to_submit_synch();
   if(v_email_processor.m_get_error_status())
      AddNpTxMes("Ошибка при отправке сообщения E-Mail: " + v_email_processor.get_service_msg());
      return;
   end;
   v_email_processor.m_submit_email_synch();
   if(v_email_processor.m_get_error_status())
      AddNpTxMes("Ошибка при отправке сообщения E-Mail: " + v_email_processor.get_service_msg());
      return;
   end;

   ErrorStr = GetSendError();
   if(ErrorStr != "")
      AddNpTxMes(ErrorStr);
   end;
END;

/**
  @brief Функция контроля выполнения процедуры запроса к хранилищу СНОБ
*/
MACRO Exec_CheckRequestSnobStor()
   var SystDate, SystTime;

   SQL_Execute("DELETE FROM DNPTXTBMES_TMP");

   GetSystDateTime(@SystDate, @SystTime);

   var queryExistCheck = "select 1 " +
                         "from dss_history_dbt hist " +
                         "where hist.t_Service = 10086 " +
                         "  and to_char(hist.t_BeginStamp, 'dd.mm.yyyy') = to_char(?, 'dd.mm.yyyy') " +
                         "  and hist.t_State = 4 ";

   var cmdExistCheck = DL_RSDCommand(queryExistCheck);
   cmdExistCheck.AddParam(SystDate);
   if(cmdExistCheck.GetCount() == 0)
      var queryExistProc = "select 1 " +
                           "from dnptxop_dbt op " +
                           "where op.t_DocKind = " + string(DL_GETSNOBPROC) +
                           "  and op.t_Client = -1 " +
                           "  and op.t_OperDate = ? " +
                           "  and op.t_Status = " + string(DL_COMM_CLOSED);

      var cmdExistProc = DL_RSDCommand(queryExistProc);
      cmdExistProc.AddParam(SystDate);

      if(cmdExistProc.GetCount() == 0)
         AddNpTxMes("Плановая процедура выполнения запроса к Хранилищу СНОБ не отработала в автоматическом режиме за " + string(SystDate:f) + ". Необходимо выполнить процедуру интерфейсно.");
         SendEmail(SystDate);
      else
         AddNpTxMes("Плановая процедура выполнения запроса к Хранилищу СНОБ за " + string(SystDate:f) + " отработала успешно.");
      end;
   else
      AddNpTxMes("В текущем системном дне ранее уже была выполнена процедура контроля выполнения процедуры запроса к хранилищу СНОБ. Новая процедура контроля не выполнена.");
   end;

   PrintProtocol("Протокол_контроля_выполнения_процедуры_запроса_к_хранилищу_СНОБ", "Протокол контроля выполнения процедуры запроса к хранилищу СНОБ", SystDate, SystTime);
END;