/*
 $Name: spfc_scan.mac
 $Module: ?
 $Description: Массовое выполнение действий над операциями формирования комиссий
 */
import DealsInter, SPInter;

PRIVATE RECORD oper( dl_comm );

PRIVATE CONST ERROR_EXEC = 0,
              PRINT_HEADER = 1,
              PRINT_FOOTER = 2;

PRIVATE MACRO PrintHead( Head:STRING )
[
                   #
┌──────────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────┐
│            Код           │  №   │                      Сообщение                                                                    │
│         операции         │ошибки│                                                                                                   │
├──────────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────┤](Head);
END;


PRIVATE MACRO PrintFooter()
[└──────────────────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────┘
];

END;

PRIVATE MACRO PrintLine( ErrStatus:INTEGER, ErrMessage:STRING )
[│##########################│######│###################################################################################################│]
( oper.DocumentId:w, ErrStatus, ErrMessage:w );
END;

PRIVATE MACRO GetOperState( OperID:INTEGER ):INTEGER
  PRIVATE VAR OperByID = TBFile( "dl_comm.dbt" );


  OperByID.Clear();
  OperByID.rec.DocumentID = OperID;
  if( OperByID.GetEQ() )                     
     return OperByID.rec.CommStatus;
  end;

  return -1;
END;

PRIVATE MACRO ExecuteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом создании операции." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 )
        var State = GetOperState( oper.DocumentID );

        if( State ==  SPOPER_CLOSE )
           ErrMessage = "Операция создана успешно.";
        elif( State == SPOPER_OPEN ) 
           ErrMessage = "Действие выполнено частично. Операция перемещена в открытые.";
        else 
           ErrMessage = "Ошибка при выполнении. Операция не создана.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO DeleteOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом удалении операций." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        var State = GetOperState( oper.DocumentID );

        if( State >= 0 )
           if( State == SPOPER_CLOSE ) 
              ErrMessage = "Операция не удалена.";
           elif(State == SPOPER_OPEN)
              ErrMessage = "Операция перемещена в открытые.";
           else
              ErrMessage = "Операция перемещена в отложенные.";
           end;
        else
           ErrMessage = "Операция удалена успешно.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

PRIVATE MACRO MoveToPrepOperation( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )
  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом перемещении в отложенные операций." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
        var State = GetOperState( oper.DocumentID );

        if( State == SPOPER_PREP ) 
           ErrMessage = "Операция успешно переведена в отложенные.";
        else
           ErrMessage = "Ошибка при перемещении операции в отложенные.";
        end;
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

/* Action = 1 - начало/продолжение  (F2 из скроллинга )
   Action = 2 - перевод в отложенные (Alt+F8 из скроллинга )
   Action = 3 - удаление (F8 из скроллинга)
*/
MACRO Печать_ОтчетСканирования( NumExec:INTEGER, FDoc:VARIANT, ErrStatus:INTEGER, ErrMessage:STRING, Action:INTEGER )

  SetBuff( oper, FDoc );   

  if( Action == 1 )
     ExecuteOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 2 )
     MoveToPrepOperation( NumExec, ErrStatus, ErrMessage );
  elif( Action == 3 )
     DeleteOperation( NumExec, ErrStatus, ErrMessage );
  end;

END;
