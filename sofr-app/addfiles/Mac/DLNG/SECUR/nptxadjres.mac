/*
$Name:        nptxhistoprep.mac
$Module:      Ценные бумаги
$Description: 
*/
import "nptxressumbudg.mac", "nptxadjres_form.mac";

var activeAccArr = TArray();
activeAccArr[0] = "НДФЛ к возврату_22%";
activeAccArr[1] = "НДФЛ к возврату_20%";
activeAccArr[2] = "НДФЛ к возврату_18%";
activeAccArr[3] = "НДФЛ к возврату_15%";
activeAccArr[4] = "НДФЛ к возврату";

var passiveAccArr = TArray();
passiveAccArr[0] = "НДФЛ к перечислению 22%";
passiveAccArr[1] = "НДФЛ к перечислению 20%";
passiveAccArr[2] = "НДФЛ к перечислению 18%";
passiveAccArr[3] = "НДФЛ к перечислению 15%";
passiveAccArr[4] = "НДФЛ к перечислению, FLR";


private class TrnResult(_accDeb,_accCred,_sum,_message,_IsOk,_docNumb)
  var accDeb = _accDeb;
  var accCred = _accCred;
  var sum = _sum;
  var message = _message;
  var IsOk = _IsOk;
  var docNumb = _docNumb;
end;

macro AdjResRestMakeTrn(Sum, AccPayer, AccReceiver, PackNumber, CodeGen, OperDate, Ground)
  var saveDialogFlag = SetDialogFlag( 0 );
  var tr = RsbAccTransaction();
  if(tr == null)
    SetDialogFlag( saveDialogFlag );
    RunError("Ошибка при формировании проводки");
  end;
  tr.Date_Carry      = OperDate;
  tr.Chapter         = 1;
  tr.Department      = {OperDprt};
  tr.Number_Pack     = PackNumber;
  tr.Numb_Document   = CodeGen.GetNextValStr();
  tr.Ground          = Ground;//;
  tr.FIIDPayer       = NATCUR;
  tr.FIIDReceiver    = NATCUR;
  tr.SumPayer        = Sum;
  tr.SumReceiver     = Sum;
  tr.AccountPayer    = AccPayer;
  tr.AccountReceiver = AccReceiver;
  var isOk = true;
  if (not tr.Carry())
    isOk = false;
    Ground = GetErrMsg();
  end;
  SetDialogFlag( saveDialogFlag );
  return TrnResult(tr.AccountPayer, tr.AccountReceiver, Sum, Ground, isOk, tr.Numb_Document);
end;

macro AdjResRestOperationAccPrintProtocol(operDate, NumberPack, trnData, fileName)
  var oldOutPut = SetOutPut(fileName);
  PrintLogDepartment({operdprt});
  [                                                   РЕЕСТР                                                     ];
  [        документов по урегулированию лицевых счетов НДФЛ к возврату в дату резервирования ##########          ]( operDate:f );
  [ ];
  [       Глава:                 #                                                                               ]( GetChapterString(1)   );
  [       Пачка:                 #                                                                               ]( string(NumberPack)       );
  [+---------+-----------------------+-----------------------+---------------+-----------------------------------------------------------------------+];
  [|    №    |        Дебет          |       Кредит          |   Сумма, руб  |       Основание проводки                                              |];
  [|  докум  |                       |                       |               |                                                                       |];
  [+---------+-----------------------+-----------------------+---------------+-----------------------------------------------------------------------+];

  var totalSum = $0;
  for (var curLine, trnData)
    var j = 1;
    var grndArr = StrSplit2(SQL_ConvTypeStr(curLine.message), 71);
    [|#########|#######################|#######################|###############|#######################################################################|]
    (curLine.docNumb:r, curLine.accDeb:f, curLine.accCred:f, curLine.sum, grndArr(0));
    while ( StrLen(grndArr(j) ) > 0)
      [|         |                       |                       |               |#######################################################################|](grndArr(j));
      j = j + 1;
    end;
    totalSum = totalSum + curLine.sum;
  end;

  [+---------+-----------------------+-----------------------+---------------+-----------------------------------------------------------------------+];
  [|Итого    |                                               |###############|                                                                       |](totalSum );
  [+---------+-----------------------------------------------+---------------+-----------------------------------------------------------------------+];

  var OperPost;
  var OperName = GetOperString( {oper}, @OperPost );
  [|                                                                                                                                                 |];
  [|##############################     #######                                                                                                       |]( OperPost:w, "(" + string({oper}) + ")" );
  [|                                    #                                                                                                            |]( OperName );
  [|                                                                                                                                                 |];
  [|Контролер                          (                        )                                                                                    |];
  [|                                                                                                                                                 |];
  [|                                                                                                                                                 |];
  [+-------------------------------------------------------------------------------------------------------------------------------------------------+];
  SetOutPut(oldOutPut);
end;

macro AdjResRestOperationRun(OperDate, NumberPack, FirstAccTrnNumber, ShowAccProtocol)
  if (ValType(ShowAccProtocol) != V_BOOL)
    ShowAccProtocol = false;
  end;
  if (ValType(NumberPack) != V_INTEGER)
    NumberPack = GetNumberPackFromReg();
  end;
  if (ValType(FirstAccTrnNumber) != V_INTEGER)
    FirstAccTrnNumber = 1;
  end;
  var codeGen = DL_CodeGenerator(numeric(FirstAccTrnNumber));

  var day, mon, year;
  var hour, mn, sec;
  DateSplit(date(),day,mon,year);
  TimeSplit(time(),hour,mn,sec);

  var trnData = TArray();
  var operLog = ProcessProtocol();
  var operLogName = "Протокол_выполнения_операции_урегулирования_в_дат_резерв_"+string(day:o:2)+string(mon:o:2)+string(year)+string(hour:o:2)+string(mn:o:2)+string(sec:o:2);// + ".txt";
  operLogName = PathCombine(GetAbsoluteTxtPath(),operLogName);
  var operLogNameForSave = operLogName + ".txt";
  operLogName = operLogName + ".log";
  operLog.SetFilePath(operLogName);
  var accProtName = "Реестр_проводок_операции_урегулирования_в_дат_резерв_"+string(day:o:2)+string(mon:o:2)+string(year)+string(hour:o:2)+string(mn:o:2)+string(sec:o:2) + ".txt";
  accProtName = PathCombine(GetAbsoluteTxtPath(),accProtName);

  var nearResDate = FindNearestNextResDate(OperDate);

  operLog.WriteLine("  Протокол выполнения операции урегулирования на " + OperDate);
  operLog.WriteLine("");
  operLog.WriteLine("  Дата формирования: " + date());
  operLog.WriteLine("  Время формирования: " + time());
  operLog.WriteLine("  Операционист: " + {oper} + " " + {Name_Oper});
  operLog.WriteLine("");
  operLog.WriteLine("  Параметры запуска:");
  operLog.WriteLine("  Дата урегулирования - " + nearResDate);
  operLog.WriteLine("  Пачка - " + NumberPack);
  operLog.WriteLine("");

  var noData = true;
  if (nearResDate > OperDate)
    operLog.WriteLine("  Дата резервирования "+nearResDate+" превышает дату операции "+OperDate+", остатки не урегулированы.");
  else
    operLog.WriteLine(" Шаги выполнения:");
    operLog.WriteLine("  1. Отбор парных счетов:");
    var accTrnHasError = false;
    var qr = 
      " SELECT  "
     +"     :acc_cur as t_prm_acc_cur, "
     +"     :oper_date as t_prm_oper_date, "
     +"     :chapter as t_prm_chapter, "
     +"     :pass_cat as t_prm_pass_cat, "
     +"     :act_cat as t_prm_act_cat, "
     +"     main.*, "
     +"     RSB_ACCOUNT.restac( "
     +"         main.pas_account, "
     +"         :acc_cur, "
     +"         :oper_date, "
     +"         :chapter, "
     +"         0 "
     +"     ) AS pas_rest, "
     +"     RSB_ACCOUNT.restac( "
     +"         main.act_account, "
     +"         :acc_cur, "
     +"         :oper_date, "
     +"         :chapter, "
     +"         0 "
     +"     ) AS act_rest "
     +" FROM  "
     +"     ( "
     +"         SELECT  "
     +"             pas.t_pasid, "
     +"             pas.t_pascode, "
     +"             ( "
     +"                 SELECT  "
     +"                     ac.t_account "
     +"                 FROM  "
     +"                     DMCACCDOC_DBT accdoc "
     +"                 JOIN  "
     +"                     DACCOUNT_DBT ac  "
     +"                     ON ac.t_account = accdoc.t_account "
     +"                     AND ac.t_code_currency = accdoc.t_currency "
     +"                     AND ac.t_chapter = accdoc.t_chapter "
     +"                 WHERE  "
     +"                     accdoc.t_catid = pas.t_pasid "
     +"                     AND ac.t_open_date <= :oper_date "
     +"                     AND ( "
     +"                         ac.t_close_date >= :oper_date "
     +"                         OR ac.t_close_date = TO_DATE('01.01.0001', 'DD.MM.YYYY') "
     +"                     ) "
     +"                     AND ac.t_code_currency = :acc_cur "
     +"                     AND ac.t_chapter = :chapter "
     +"                     AND ROWNUM = 1 "
     +"             ) AS pas_account, "
     +"             act.t_actid, "
     +"             act.t_actcode, "
     +"             ( "
     +"                 SELECT  "
     +"                     ac.t_account "
     +"                 FROM  "
     +"                     DMCACCDOC_DBT accdoc "
     +"                 JOIN  "
     +"                     DACCOUNT_DBT ac  "
     +"                     ON ac.t_account = accdoc.t_account "
     +"                     AND ac.t_code_currency = accdoc.t_currency "
     +"                     AND ac.t_chapter = accdoc.t_chapter "
     +"                 WHERE  "
     +"                     accdoc.t_catid = act.t_actid "
     +"                     AND ac.t_open_date <= :oper_date "
     +"                     AND ( "
     +"                         ac.t_close_date >= :oper_date "
     +"                         OR ac.t_close_date = TO_DATE('01.01.0001', 'DD.MM.YYYY') "
     +"                     ) "
     +"                     AND ac.t_code_currency = :acc_cur "
     +"                     AND ac.t_chapter = :chapter "
     +"                     AND ROWNUM = 1 "
     +"             ) AS act_account "
     +"         FROM  "
     +"             ( "
     +"                 SELECT  "
     +"                     NVL(REGEXP_REPLACE(t_code, '[^0-9]+'), '13') AS rate, "
     +"                     cat_pas.t_id AS t_pasid, "
     +"                     cat_pas.t_code AS t_pascode "
     +"                 FROM  "
     +"                     DMCCATEG_DBT cat_pas "
     +"                 WHERE  "
     +"                     cat_pas.t_code = :pass_cat "
     +"             ) pas "
     +"         JOIN  "
     +"             ( "
     +"                 SELECT  "
     +"                     NVL(REGEXP_REPLACE(t_code, '[^0-9]+'), '13') AS rate, "
     +"                     cat_act.t_id AS t_actid, "
     +"                     cat_act.t_code AS t_actcode "
     +"                 FROM  "
     +"                     DMCCATEG_DBT cat_act "
     +"                 WHERE  "
     +"                     cat_act.t_code = :act_cat "
     +"             ) act  "
     +"             ON 1 = 1 "
     +"     ) main "
     +" WHERE  "
     +"     main.pas_account IS NOT NULL  "
     +"     AND main.act_account IS NOT NULL; ";
    var i = 0;
    var j = 0;
    while (i < activeAccArr.size)
      j = 0;
      while (j < passiveAccArr.size)
        var ds = execSQLselect(qr, MakeArray(
                SqlParam("acc_cur", NATCUR),
                SqlParam("oper_date", OperDate),
                SqlParam("chapter", 1),
                SqlParam("pass_cat", passiveAccArr[j]),
                SqlParam("act_cat", activeAccArr[i])
        ), true);

        if (ds.moveNext())
          var Sa_i = SQL_ConvTypeSum(ds.value("act_rest"));
          var Sp_j = SQL_ConvTypeSum(ds.value("pas_rest"));
          if ((Sa_i != $0) and (Sp_j != $0))
            noData = false;
            trnData[trnData.size] = 
              AdjResRestMakeTrn(
                min(abs(Sa_i),abs(Sp_j)),
                SQL_ConvTypeStr(ds.value("pas_account")),
                SQL_ConvTypeStr(ds.value("act_account")),
                NumberPack,
                codeGen,
                OperDate,
                "Урегулирование остатков лицевых счетов по учету НДФЛ к перечислению/возврату в дату резервирования. Дата операции " +OperDate  
              );
            if (not trnData[trnData.size-1].IsOk)
              accTrnHasError = true;
            end; 
          end;
        end;
        j=j+1;
      end;
      i=i+1;
    end;
    if (noData)
      operLog.WriteLine("    В базе нет подходящих счетов для выведения единого результата.");
    else
      operLog.WriteLine("    Отбор счетов для урегулирования выполнен успешно.");
      operLog.WriteLine("");
      operLog.WriteLine("  2. Выполнение проводок по урегулированию:");
      if (accTrnHasError)
        operLog.WriteLine("    Невозможно выполнить проводки по урегулированию.");
      else
        operLog.WriteLine("    Проводки по урегулированию выполнены успешно.");
      end;
    end;
  end;

  if (not IsNoInterfaceRun())
    operLog.View();
  end;
  operLog.CopyLogFileToPath(operLogNameForSave/*operLogName*/);

  if ((ShowAccProtocol) and (not noData))
    AdjResRestOperationAccPrintProtocol(OperDate, NumberPack, trnData, accProtName);
  end;

  if ((not IsNoInterfaceRun()) and (ShowAccProtocol) and (not noData))
    ViewFile(accProtName);
  end;
  //operLog.SetFilePath("nullfilename");
end;

MACRO AdjResRestMenuRun()
  var Form = Sp_NPTXAdjRes_Panel();
  if (Form.Run())
    AdjResRestOperationRun(Form.m_Date, Form.m_PackNumb, Form.m_FirstNum, Form.m_IsProtocol)
  end;
END;

macro AdjResRestSchedRun()
  if (IsWorkDay(date()))
    AdjResRestOperationRun(date(), null, null, true);
  end;
  return SsExecResult(SS_RESPONSE_END);
onError(err)
  return SsExecResult(SS_RESPONSE_FATAL, GetFullErrorMessage(err));
end;