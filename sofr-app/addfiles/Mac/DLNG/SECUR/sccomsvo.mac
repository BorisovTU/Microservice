/*
$Name:        sccomsvo.mac
$Module:      Ценные бумаги
$Description: Сервисная операция "Обработка периодических комиссий"
                Обработка комиссий в дату заключения сделок
 PROGRAMMED BY: Леонов И.Е.
 CREATION DATE: 01.10.07
*/
import InsCarryDoc, CTInter, "scservop.mac", "sp_car.mac", "SCPeriodComm.mac";

PRIVATE MACRO ExecuteCalcComission( rContr:TRecHandler, Doc:VARIANT ):BOOL
  VAR cmdDefCom, rsDefCom, cmdBaseObj, rsBaseObj,
      Ком, ОН/*Объект начисления*/,  ДатаКурса, Sum = $0, NDS = $0,
      СуммаПоКомиссииДоговора, stat, FDContr, ErrorBaseObj,
      dl_leg  = TrecHandler( "dl_leg.dbt"   );
  var err = "";

  FDContr = SPFirstDocContract( DL_STOCKCONTR, rContr );

  СуммаПоКомиссииДоговора  = CSummByContr();

  /*Удерженные периодические комиссии*/
  cmdDefCom = RSDCommand( "SELECT DefCom.t_CommNumber, DefCom.t_ID "+
                          "  FROM DSFDEFCOM_DBT DefCom " +
                          " WHERE     DefCom.t_ConID   = ?  /*0*/ " +
                          "       AND DefCom.t_FeeType = ?  /*1*/ " +
                          "       AND DefCom.t_status  != ? /*2*/" +
                          "       AND DefCom.t_DateFee = ?  /*3*/ " +
                          " ORDER BY DefCom.t_CommNumber "
                        );
  cmdDefCom.addParam( "", RSDBP_IN, rContr.rec.ID );          /*0*/
  cmdDefCom.addParam( "", RSDBP_IN, SF_FEE_TYPE_PERIOD );     /*1*/
  cmdDefCom.addParam( "", RSDBP_IN, SFINV_PAYSTATUS_PAY );    /*2*/
  cmdDefCom.addParam( "", RSDBP_IN, ScOprServDoc.CommDate );    /*3*/
  
  cmdDefCom.execute();
  rsDefCom = TRsbDataSet( cmdDefCom );
  while( rsDefCom.MoveNext() )

    Ком = CPeriodCommission( rContr, rsDefCom.CommNumber, rsDefCom.ID );

    if((Ком.Сумма() != 0) AND (Ком.IsClient()) AND (Ком.ПроверитьМоментРасчета() == true))

      ДатаКурса = Ком.ВычислитьДатуКурса( ScOprServDoc.CommDate );

      cmdBaseObj = RSDCommand( "select NVL(SUM(RSI_RSB_FIInstr.ConvSum( (basobj.t_COMMSUM + basobj.t_NDSSUM), ?, ?, ?, 1 )), 0) as t_Sum, " +
                               "       NVL(SUM(RSI_RSB_FIInstr.ConvSum( (basobj.t_NDSSUM), ?, ?, ?, 1 )), 0) as t_NDS " +
                               "  FROM DSFBASOBJ_DBT basobj, DSFDEFCOM_DBT DefCom, ddl_tick_dbt Deal " +
                               " WHERE     basobj.T_DEFCOMMID = ? " +
                               "       AND basobj.T_FEETYPE = 1 " +
                               "       AND DefCom.T_ID = basobj.T_DEFCOMMID " +
                               "       AND Deal.t_DealID      = basobj.T_BASEOBJECTID "
                             );
      cmdBaseObj.addParam( "", RSDBP_IN, Ком.Валюта() );
      cmdBaseObj.addParam( "", RSDBP_IN, Ком.ВалютаОплаты() );
      cmdBaseObj.addParam( "", RSDBP_IN, ДатаКурса );
      cmdBaseObj.addParam( "", RSDBP_IN, Ком.Валюта() );
      cmdBaseObj.addParam( "", RSDBP_IN, Ком.ВалютаОплаты() );
      cmdBaseObj.addParam( "", RSDBP_IN, ДатаКурса );
      cmdBaseObj.addParam( "", RSDBP_IN, rsDefCom.t_ID );

      cmdBaseObj.execute();
      rsBaseObj = TRsbDataSet( cmdBaseObj );
      ErrorBaseObj = false;

      if(rsBaseObj.MoveNext())
        if( СуммаПоКомиссииДоговора.Добавить( Ком.rComiss().rec.Number, Ком.ВалютаОплаты(), rsBaseObj.Sum - rsBaseObj.NDS, rsBaseObj.NDS) == false )
             ErrorBaseObj = true;
             break;
        end;
      end;
    end;
  end;   

  stat = СуммаПоКомиссииДоговора.First();
  while( stat )
     err = "";

     Ком = CPeriodCommission( rContr, СуммаПоКомиссииДоговора.rec.CommNumber, NULL );

     if( not SP_SaveComSumFromFinalDay( OBJTYPE_SFCONTR, rContr.rec.ID, rContr.rec.ID, СуммаПоКомиссииДоговора.rec.CommNumber, ScOprServDoc.CommDate, СуммаПоКомиссииДоговора.rec.Summa + СуммаПоКомиссииДоговора.rec.NDS, 
                                        СуммаПоКомиссииДоговора.rec.NDS, GetInputMedium(INPUTMEDIUM_CALC), Ком.rComiss.rec.ReceiverID, Ком.rComiss.rec.FIID_COMM, @err ) )
        MsgBox( "Ошибка при сохранении суммы комиссии клиента банку.|" + err ); 
        return false;
     end; 

     stat = СуммаПоКомиссииДоговора.Next();
  end;

  return true;
END;

MACRO ExecuteStep( D, FirstDoc )

  VAR rContr = TRecHandler( "sfcontr.dbt" ),
      errMes:string = "";

  rContr.SetRecordAddr( FirstDoc );

  /* расчет комиссий по договору через механизм ПЗО */ 
  if( CalcPeriodCommByContr( rContr.rec.ID, ScOprServDoc.CommDate, NULL, NULL, NULL, errMes ) != 0 )
     MsgBox( "Ошибка при расчете сумм периодических комиссий.\n" + errMes ); 
  end; 
   
  if( not ExecuteCalcComission( rContr, D ) )
    return 1;
  end;

  return SvOp_AddInReport( SvOpComiss( rContr.rec ) );
END;
