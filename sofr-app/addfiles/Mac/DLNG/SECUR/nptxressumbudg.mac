/*
$Name:        nptxhistoprep.mac
$Module:      Ценные бумаги
$Description: 
*/
import "nptxressumbudg_form.mac", "dlutils.mac", "consrep.mac", "reg_rep_lib.mac", "SimpleService.mac";

macro GetFixDateFromReg()
  const regPath = "SECUR\\НАЛОГОВЫЙ УЧЕТ\\ДАТЫ_ФИКС_ОСТАТКА";
  var retVal;
  var err = 0;
  
  GetRegistryValue(regPath, V_STRING, retVal, err);
  if(err != 0) 
    retVal = "";
  end;
  return retVal;
END;

macro GetFixDateArrayFromReg()
  var dateArray = TArray(); 

  var fixFromReg = GetFixDateFromReg();
  if (trim(fixFromReg) != "")
    dateArray = split(fixFromReg,",");
    var i = -1;
    while ((i=i+1) < dateArray.size)
      dateArray[i] = int(dateArray[i]);
    end;
  end;
  return dateArray;
end;


macro FindNearestNextResDate(curDate)
  var resDate = null;
  var dd, mm, yyyy;
  DateSplit(curDate,dd,mm,yyyy);

  macro GetDateFromDay(day)
    var lastDateOfMonth = DateShift(date(1,mm,yyyy),null,1)-1;
    var resDate = date(day,mm,yyyy);
    if (day >= 31)
      resDate = lastDateOfMonth;
    end;
    return resDate;
  OnError()
    return lastDateOfMonth;
  end;

  var daysArray = GetFixDateArrayFromReg();

  for (var bufDay, daysArray)
    var bufDate = GetDateFromDay(bufDay);
    if ((bufDate >= curDate) and ((resDate == null) or (bufDate < resDate)))
      resDate = bufDate;
    end;
  end;
  if ((resDate == null) and (daysArray.size > 0))
    resDate = DateShift(date(arrMin(daysArray),mm,yyyy),null,1);
  end;
  if (resDate == null)
    RunError("Ошибка. Не найдена ближайшая дата резервирования");
  end;

  var calPrms = makeArray(
    DL_KeyPair("ObjectType",DL_CALLNK_SERVOP)
  );
  var calendId = DL_GetCalendByDynParam(83,calPrms);
  return DL_GetBankDateAfterWorkDayByCalendar(resDate, 0, calendId, false);
end;

macro GetGeneralAccountByCategNum(CategName)
  var qr = 
    " SELECT T_ACCOUNT "
   +"   FROM DMCACCDOC_DBT "
   +"  WHERE     t_catid = (SELECT T_ID "
   +"                         FROM DMCCATEG_DBT "
   +"                        WHERE T_NUMBER = 1636 AND T_LEVELTYPE = 1) "
   +"        AND T_ISCOMMON = CHR (88) "
   +"        AND T_CHAPTER = 1 "
   +"        AND T_CURRENCY = 0 ";
  var ds = execSQLselect(qr, TArray(), true);
  if (ds.MoveNext())
    return ds.value("T_ACCOUNT");
  end;
  RunError("Не найден счет по категории \"Счет для резервирования сумм НДФЛ\"")
end;

private class TrnResult(_accDeb,_accCred,_sum,_message,_IsOk,_docNumb)
  var accDeb = _accDeb;
  var accCred = _accCred;
  var sum = _sum;
  var message = _message;
  var IsOk = _IsOk;
  var docNumb = _docNumb;
end;

macro ResSumMakeTrn(Sum, AccPayer, PackNumber, OperDate, Ground)
  var saveDialogFlag = SetDialogFlag( 0 );
  var tr = RsbAccTransaction();
  if(tr == null)
    RunError("Ошибка при формировании проводки");
  end;
  tr.Date_Carry      = OperDate;
  tr.Chapter         = 1;
  tr.Department      = {OperDprt};
  tr.Number_Pack     = PackNumber;
  tr.Numb_Document   = "";
  tr.Ground          = Ground;//;
  tr.FIIDPayer       = NATCUR;
  tr.FIIDReceiver    = NATCUR;
  tr.SumPayer        = Sum;
  tr.SumReceiver     = Sum;
  tr.AccountPayer    = AccPayer;
  tr.AccountReceiver = GetGeneralAccountByCategNum();
  var isOk = true;
  if (not tr.Carry())
    isOk = false;
    Ground = GetErrMsg();
  end;
  SetDialogFlag( saveDialogFlag );
  return TrnResult(tr.AccountPayer, tr.AccountReceiver, Sum, Ground, isOk, tr.Numb_Document);
end;

macro ResSumBudgOperationAccPrintProtocol(operDate, NumberPack, trnData, fileName)
  var oldOutPut = SetOutPut(fileName);
  PrintLogDepartment({operdprt});
  [                                                   РЕЕСТР                                                     ];
  [      документов по резервированию сумм НДФЛ к перечислению в бюджет в дату резервирования ##########         ]( operDate:f );
  [ ];
  [       Глава:                 #                                                                               ]( GetChapterString(1)   );
  [       Пачка:                 #                                                                               ]( string(NumberPack)       );
  [+---------+-----------------------+-----------------------+---------------+-----------------------------------------------------------------------+];
  [|    №    |        Дебет          |       Кредит          |   Сумма, руб  |       Основание проводки                                              |];
  [|  докум  |                       |                       |               |                                                                       |];
  [+---------+-----------------------+-----------------------+---------------+-----------------------------------------------------------------------+];

  var totalSum = $0;
  for (var curLine, trnData)
    var j = 1;
    var grndArr = StrSplit2(SQL_ConvTypeStr(curLine.message), 71);
    [|#########|#######################|#######################|###############|#######################################################################|]
    (curLine.docNumb:r, curLine.accDeb:f, curLine.accCred:f, curLine.sum, grndArr(0));
    while ( StrLen(grndArr(j) ) > 0)
      [|         |                       |                       |               |#######################################################################|](grndArr(j));
      j = j + 1;
    end;
    totalSum = totalSum + curLine.sum;
  end;

  [+---------+-----------------------+-----------------------+---------------+-----------------------------------------------------------------------+];
  [|Итого    |                                               |###############|                                                                       |](totalSum );
  [+---------+-----------------------------------------------+---------------+-----------------------------------------------------------------------+];

  var OperPost;
  var OperName = GetOperString( {oper}, @OperPost );
  [|                                                                                                                                                 |];
  [|##############################     #######                                                                                                       |]( OperPost:w, "(" + string({oper}) + ")" );
  [|                                    #                                                                                                            |]( OperName );
  [|                                                                                                                                                 |];
  [|Контролер                          (                        )                                                                                    |];
  [|                                                                                                                                                 |];
  [|                                                                                                                                                 |];
  [+-------------------------------------------------------------------------------------------------------------------------------------------------+];
  SetOutPut(oldOutPut);
end;

macro ResSumBudgOperationRun(OperDate, NumberPack, ShowAccProtocol)
  if (ValType(ShowAccProtocol) != V_BOOL)
    ShowAccProtocol = false;
  end;
  if (ValType(NumberPack) != V_INTEGER)
    NumberPack = GetNumberPackFromReg();
  end;

  var day, mon, year;
  var hour, min, sec;
  DateSplit(date(),day,mon,year);
  TimeSplit(time(),hour,min,sec);

  var trnData = TArray();
  var operLog = ProcessProtocol();
  var operLogName = "Протокол_выполнения_операции_резерв_"+string(day:o:2)+string(mon:o:2)+string(year)+string(hour:o:2)+string(min:o:2)+string(sec:o:2) + ".txt";
  operLogName = PathCombine(GetAbsoluteTxtPath(),operLogName);
  operLog.SetFilePath(operLogName);
  var accProtName = "Реестр_проводок_операции_резерв_"+string(day:o:2)+string(mon:o:2)+string(year)+string(hour:o:2)+string(min:o:2)+string(sec:o:2) + ".txt";
  accProtName = PathCombine(GetAbsoluteTxtPath(),accProtName);

  var nearResDate = FindNearestNextResDate(OperDate);

  operLog.WriteLine("  Протокол выполнения операции резервирования сумм НДФЛ к перечислению на " + OperDate);
  operLog.WriteLine("");
  operLog.WriteLine("  Дата формирования: " + date());
  operLog.WriteLine("  Время формирования: " + time());
  operLog.WriteLine("  Операционист: " + {oper} + " " + {Name_Oper});
  operLog.WriteLine("");
  operLog.WriteLine("  Параметры запуска:");
  operLog.WriteLine("  Дата урегулирования - " + nearResDate);
  operLog.WriteLine("  Пачка - " + NumberPack);
  operLog.WriteLine("");

  var noData = true;

  if (nearResDate > OperDate)
    operLog.WriteLine("  Дата резервирования "+nearResDate+" превышает дату операции "+OperDate+", операция завершена.");
  else
    operLog.WriteLine(" Шаги выполнения:");
    operLog.WriteLine("  1. Отбор счетов НДФЛ к перечислению:");
    var accTrnHasError = false;
    var qr = 
      " WITH q1 "
     +"         AS (SELECT DISTINCT ac.T_ACCOUNT, "
     +"                             ac.T_CHAPTER, "
     +"                             ac.T_CODE_CURRENCY, "
     +"                             AC.T_OPEN_DATE, "
     +"                             AC.T_CLOSE_DATE "
     +"               FROM DMCACCDOC_DBT mcdoc, DACCOUNT_DBT ac "
     +"              WHERE MCDOC.T_CATID IN "
     +"                       (SELECT t_id "
     +"                          FROM dmccateg_dbt "
     +"                         WHERE T_CODE = 'НДФЛ к перечислению, FLR' "
     +"                               OR REGEXP_LIKE ( "
     +"                                     T_CODE, "
     +"                                     'НДФЛ к перечислению [0-9]{2}?%')) "
     +"                    AND ac.T_ACCOUNT = mcdoc.T_ACCOUNT "
     +"                    AND ac.T_CODE_CURRENCY = mcdoc.T_CURRENCY "
     +"                    AND ac.T_CHAPTER = mcdoc.T_CHAPTER) "
     +" SELECT q2.* "
     +"   FROM (SELECT q1.*, "
     +"                RSB_ACCOUNT.restac (q1.t_Account, "
     +"                                    q1.t_Code_Currency, "
     +"                                    ?, "
     +"                                    q1.t_Chapter, "
     +"                                    0) "
     +"                   AS t_rest "
     +"           FROM q1) q2 "
     +"  WHERE     q2.t_chapter = 1 "
     +"        AND q2.T_CODE_CURRENCY = 0 "
     +"        AND q2.t_rest <> 0 "
     +"        AND q2.t_open_date <= ? "
     +"        AND (q2.t_close_date >= ? "
     +"             OR q2.t_close_date = TO_DATE ('01.01.0001', 'DD.MM.YYYY')); ";
    var ds = execSQLselect(qr, MakeArray(
            SqlParam("p_restDate", OperDate),
            SqlParam("p_openDate", OperDate),
            SqlParam("p_closeDate", OperDate)
    ), true);
    while (ds.MoveNext())
      noData = false;
      trnData[trnData.size] = 
        ResSumMakeTrn(
          SQL_ConvTypeSum(ds.value("t_rest")),
          SQL_ConvTypeStr(ds.value("t_account")),
          NumberPack,
          OperDate,
          "Резервирование суммы НДФЛ к перечислению в бюджет. Дата операции " + OperDate
        );
      if (not trnData[trnData.size-1].IsOk)
        accTrnHasError = true;
      end; 
    end;
    if (noData)
      operLog.WriteLine("    В базе нет подходящих счетов для переноса остатков на счет резервирования НДФЛ.");
    else
      operLog.WriteLine("    Отбор счетов для урегулирования выполнен успешно.");
      operLog.WriteLine("");
      operLog.WriteLine("  2. Выполнение проводок по переносу остатков:");
      if (accTrnHasError)
        operLog.WriteLine("    Невозможно выполнить проводки по урегулированию.");
      else
        operLog.WriteLine("    Проводки по урегулированию выполнены успешно.");
      end;
    end;
  end;
  if (not IsNoInterfaceRun())
    operLog.View();
  end;

  if ((ShowAccProtocol) and (not noData))
    ResSumBudgOperationAccPrintProtocol(OperDate, NumberPack, trnData, accProtName);
  end;

  if ((not IsNoInterfaceRun()) and (ShowAccProtocol) and (not noData))
    ViewFile(accProtName);
  end;
  operLog.SetFilePath("nullfilename");
end;

MACRO ResSumBudgOperationMenuRun()
  var Form = Sp_NPTXRESSUMBUDG_Panel();
  if (Form.Run())
    ResSumBudgOperationRun(Form.m_Date, Form.m_PackNumb, Form.m_IsProtocol)
  end;
END;

macro ResSumBudgOperationSchedRun()
  if (IsWorkDay(date()))
    ResSumBudgOperationRun(date(), null, true);
  end;
  return SsExecResult(SS_RESPONSE_END);
onError(err)
  return SsExecResult(SS_RESPONSE_FATAL, GetFullErrorMessage(err));
end;