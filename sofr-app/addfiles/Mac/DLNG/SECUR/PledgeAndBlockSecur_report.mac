/*
$Name:             PledgeAndBlockSecur_report.mac
$Module:           Ценные бумаги
$Description:      Отчет "Ц/б в обеспечении и заблокированные"
*/

IMPORT DealsInter;
import or_rep_h;
import or_tpl_h;
import or_const;
IMPORT "dlquery.mac";
IMPORT "PledgeAndBlockSecur_form.mac", "DLReport_h.mac", "CbReport_h.mac";

PRIVATE CONST ZERO_DATE    = date(0, 0, 0);

var NumStr  :integer  = 0;
var Form;
var stat;


class CPldgAndBlckSc()
  var DateRep:date;
  var FIID:integer;

  macro Constructor()
    DateRep = ZERO_DATE;
    FIID = -1;
  end;

  this.Constructor();
end;

private macro GetPanelData(data:CPldgAndBlckSc)
  data.DateRep = Form.GetFieldValue(PNFLD_DATE);
  data.FIID    = Form.GetFieldValue(PNFLD_FIID);
end;

//----CPldgAndBlckScReport----
class (CB_CReportTemplate) CPldgAndBlckScReport(TemplName)
  var DataForm;
  var RepName;
  var query;
  var addres = TArray();
  var i = 0;
  var printEngine, csvFile, csvTable, fName;

  macro CreateQuery()
    query = " WITH q AS (SELECT DDL_COMM_DBT.T_FIID, DDL_COMM_DBT.T_CONTRACTID      " +
            " FROM DDL_COMM_DBT                                                     " +
            " WHERE DDL_COMM_DBT.T_DOCKIND IN ("+SP_TRNCOLLAT+", "+SP_BLOCKBATCH+") " +
            " AND DDL_COMM_DBT.T_COMMDATE <= ?                                      " +
            " AND DDL_COMM_DBT.T_COMMSTATUS = "+DL_COMM_CLOSED;
    if(DataForm.FIID > 0)
      query = query + " AND DDL_COMM_DBT.T_FIID = " + string(DataForm.FIID);
    end;

    query = query + " group by DDL_COMM_DBT.T_FIID, DDL_COMM_DBT.T_CONTRACTID )";

    query = query + " SELECT DFININSTR_DBT.T_FIID AS FIID, DFININSTR_DBT.T_FI_CODE AS FI_CODE, DFININSTR_DBT.T_NAME AS FI_NAME,               " +
                    " DSCSECAGR_DBT.T_NAME AS AGR_NAME,                                                                                       " + 
                    " RSB_SECUR.GetQntySecPledgedOnDate(DSCSECAGR_DBT.T_ID, DFININSTR_DBT.T_FIID, " + string({OperDprt}) + ", ?) as QPledged, " +
                    " RSB_SECUR.GetQntySecBlockedOnDate(DSCSECAGR_DBT.T_ID, DFININSTR_DBT.T_FIID, " + string({OperDprt}) + ", ?) as QBlocked  " + 
                    " FROM DFININSTR_DBT, DSCSECAGR_DBT, q                                                                                    " +
                    " WHERE DFININSTR_DBT.T_FIID = q.T_FIID AND DSCSECAGR_DBT.T_ID = q.T_CONTRACTID                                           " +
                    " AND (RSB_SECUR.GetQntySecPledgedOnDate(DSCSECAGR_DBT.T_ID, DFININSTR_DBT.T_FIID, " + string({OperDprt}) + ", ?) > 0     " +
                    " OR RSB_SECUR.GetQntySecBlockedOnDate(DSCSECAGR_DBT.T_ID, DFININSTR_DBT.T_FIID, " + string({OperDprt}) + ", ?) > 0)      " +
                    " ORDER BY DFININSTR_DBT.T_AVOIRKIND, DFININSTR_DBT.T_FIID, DSCSECAGR_DBT.T_ID                                            ";
  end;

  macro First_Call()
    printEngine = CTemplateSXLSX(NULL);

    if(printEngine.UsePoi())
      printEngine.SetXLSXFormat();
    end;

    printEngine.OpenTemplate("Report_PledgeAndBlockSecur.xlsx", false);
    printEngine.SheetChange(1);

    csvTable = printEngine.RegisterTable("templateTable", "templateTableHeader", true);

    csvFile = C_CSVFile();

    if (printEngine.UseBigReport ())
        csvFile.SetLimitRow (FLUSH_COUNT_XLSX);
    end;

    fName = csvFile.CreateFullFileName("PledgeAndBlockSecur_csv.txt");
    csvFile.FileOpen(fName, true);

  end;

  macro PrintHeader()
    printEngine.SetValue_NameCell("templateTitle", "Ц/б в обеспечении и заблокированные на " + String(DataForm.DateRep));
  end;

  macro PrintLine(A1:string,A2:string,A3:string,A4,A5)
    csvFile.AddCell(String(A1));
    csvFile.AddCell(String(A2));
    csvFile.AddCell(String(A3));
    csvFile.AddCell(A4);
    csvFile.AddCell(A5);
    csvFile.AddRow();
  end;

  macro PrintFinalLine(A1:string, A4, A5)
    csvFile.AddCell(String(A1));
    csvFile.AddCell(" ");
    csvFile.AddCell(" ");
    csvFile.AddCell(A4);
    csvFile.AddCell(A5);
    csvFile.AddRow();
  end;


macro addFront()
  var a = 0;
  while (a < addres.size)
    printEngine.SetDiapazon(addres.value(a));
    printEngine.SetFont(NULL, NULL, true, NULL, NULL, NULL, NULL);
    a = a + 1;
  end;
END;

  macro printTable()
    csvFile.FileClose();
    csvTable.FillTabelFromCSV(csvFile.GetlsFileName());
    csvTable.EndTable();
    addFront();                                    
  
  end;
  

MACRO AddFooter( IsCurDate:BOOL )
  MACRO НачальникОтдела()
         var RS = TRsbDataSet("SELECT party.t_Name " +
                              "  FROM dofficer_dbt officer, dparty_dbt party " +
                              " WHERE officer.t_PartyID  = " + string({OurBank}) + " AND " +
                              "       officer.t_OfficeID = " + string(SelfDivision) + " AND " +
                              "       officer.t_IsFirstOfficePerson = 'X' AND "
                              "       party.t_PartyID = officer.t_PersonID "
                             );

         if( RS.MoveNext() )
            return RS.Name;
         end;
         return "";
     END;

     MACRO Операционист()
         var RS = TRsbDataSet("SELECT person.t_Name " +
                              "  FROM dperson_dbt person " +
                              " WHERE person.t_Oper = " + string({oper})
                             );

         if( RS.MoveNext() )
            return RS.Name;
         end;
         return "";
     END;

     MACRO ДолжностьОперациониста()
         return "Исполнитель";
         var RS = TRsbDataSet("SELECT officer.t_Post " +
                              "  FROM dofficer_dbt officer, dperson_dbt person " +
                              " WHERE officer.t_PartyID  = " + string({OurBank}) + " AND " +
                              "       person.t_Oper = " + string({oper}) + " AND " +
                              "       officer.t_PersonID = person.t_PartyID "
                             );

         if( RS.MoveNext() )
            return RS.Post;
         end;
         return "Ответственный сотрудник";
     END;

     MACRO ТелефонОперациониста()
         var PhoneNumber = "";
         var RS = TRsbDataSet("SELECT person.t_PartyID "
                              "  FROM dperson_dbt person " +
                              " WHERE person.t_Oper = " + string({oper})
                             );

         if( ( RS.MoveNext() ) and ( FindAdressContactValue( RS.PartyID, PTADDR_LEGAL, CNTADR_PHONENUMBER, PhoneNumber ) == 0 ) )
            return PhoneNumber;
         end;
         return "";
     END;

     var RepDate, PostBoss, PostBook;


        if( IsCurDate )
           RepDate = {curdate};
        else
           RepDate = Date();
        end;

        if( {Name_Boss} )
           PostBoss = {Name_Boss};
        else
           PostBoss = "Директор банка";
        end;

        if( {Name_Book} )
           PostBook = {Name_Book};
        else
           PostBook = "Главный бухгалтер банка"
        end;
      printEngine.SetValue_NameCell("ДолжностьДиректора", PostBoss + " _____________ ");
      printEngine.SetValue_NameCell("Директор", {FIO_Boss});
      printEngine.SetValue_NameCell("НачальникОтдела", НачальникОтдела());
      printEngine.SetValue_NameCell("ДолжностьОперациониста", ДолжностьОперациониста() + " _____________ ");
      printEngine.SetValue_NameCell("Операционист", Операционист());
      printEngine.SetValue_NameCell("ТелефонОперациониста", ТелефонОперациониста());
      printEngine.SetValue_NameCell("ДатаСоставления", "Составлено: " + String(DataForm.DateRep));
  END;

  macro Last_Call()
    printEngine.SaveAsTemplate(RepName);
  end;

  macro Run()
    var SumQo = 0, SumQoBlock = 0;
    var PrevFIID = -1, PrevFI_Code;
    var NumStr;
    var DataSet;
    var cmd;
    var NRec;
    var add = 2;
    CreateQuery();
    cmd = DL_RSDCommand( query );
    cmd.AddParam(DataForm.DateRep);
    cmd.AddParam(DataForm.DateRep);
    cmd.AddParam(DataForm.DateRep);
    cmd.AddParam(DataForm.DateRep);
    cmd.AddParam(DataForm.DateRep);

    NRec = cmd.GetCount();
    First_Call();
    PrintHeader();

    if(NRec > 0)
      DataSet = cmd.Execute();

      while(DataSet.MoveNext())
        if(PrevFIID != DataSet.FIID)
          if(PrevFIID > 0 )
            PrintFinalLine("Итого по " + PrevFI_Code, SumQo, SumQoBlock);
            add = add + 1;
            addres.value(i) = "A" + add;
            i = i + 1;
          end;
          PrevFIID = DataSet.FIID;
          PrevFI_Code = DataSet.FI_CODE;
          SumQo = 0;
          SumQoBlock = 0;
        end;
        PrintLine(string(DataSet.FI_CODE), DataSet.FI_NAME, DataSet.AGR_NAME, DataSet.QPledged, DataSet.QBlocked);
        SumQo = SumQo + DataSet.QPledged;
        SumQoBlock = SumQoBlock + DataSet.QBlocked;
        add = add + 1;
      end;
      PrintFinalLine("Итого по " + DataSet.FI_CODE, SumQo, SumQoBlock);
      add = add + 1;
      addres.value(i) = "A" + add;
      i = i + 1;
    else 
      PrintLine("Нет данных, удовлетворяющих параметрам отчета", "", "", "", "");
    end;

    printTable();
    AddFooter();
    Last_Call();
  end;

  macro Constructor(TemplName)
    DataForm = CPldgAndBlckSc();
    GetPanelData(@DataForm);
    RepName = TemplName;
  end;

  this.Constructor(TemplName);
end;

macro CreateReport()
  stat = true;
  Form = PldgAndBlockScPanel();

  while(stat)
    stat = Form.Run();
    if(stat)
      var StrDate  = TrStrSubst(Date(), ".", "");
      var StrTime  = TrStrSubst(Time(), ":", "");
      CPldgAndBlckScReport("PledgeAndBlockSecur_Report_" + StrDate + StrTime).Run();
    end;
  end;
end;
CreateReport();