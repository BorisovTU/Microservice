/*
$Name:        sp_catac.mac
$Module:      Ценные бумаги
$Description: Процедуры для получения счета по категориям учета. Используется на шагах операций
*/
IMPORT OprInter, CTInter, "dlcnst.inc", "sp_categ.mac";

/*виды значений параметра депозитарного учета Consolidate -
  формировать сводное поручение, если формировать, то сколько*/
CONST DEPSET_CONSOLIDATE_NO    = 0, /*  Нет */
      DEPSET_CONSOLIDATE_ONE   = 1, /*  Одно*/
      DEPSET_CONSOLIDATE_TWO   = 2; /*  Два */

/*Найти параметры депозитарного учета*/
MACRO ПолучитьПараметрыДУ( Parm:VARIANT, Market:INTEGER, MarketSchemeID:INTEGER )
   var PartyName = "", DepSet   = TBFile( "dldepset.dbt", "R", 0 ),
                       DlMarket = TBFile( "dlmarket.dbt", "R", 0 ) ;


   if( MarketSchemeID > 0 )
      DlMarket.Clear();
      DlMarket.rec.ID = MarketSchemeID;

      if( not DlMarket.GetEQ() )
         MsgBox( "Не найдена схема расчетов с биржей" );
         ClearRecord( Parm );
         return false;
      end;
   end;

   if( DlMarket.rec.DepSetID > 0 )
      DepSet.Clear();
      DepSet.rec.DepSetID = DlMarket.rec.DepSetID;
   else
      DepSet.KeyNum = 1;
      DepSet.Clear();
      DepSet.rec.Market       = Market;
   end;

   if( DepSet.GetEQ() )
      if( DlMarket.rec.IsTrust == SET_CHAR )
         DepSet.rec.Consolidate       = DEPSET_CONSOLIDATE_NO;
         DepSet.rec.ClientConsolidate = DEPSET_CONSOLIDATE_NO;
      end;
      copy( Parm, DepSet );
      return true;
   end;

   if( Market <= 0 ) 
      PartyName = "Внебирж.";
   else
      PartyName = ПолучитьКодСубъекта(Market, PTCK_CONTR);
   end;

   MsgBox( "Не найдены параметры депозитарного учета для значения:\nБиржа =" + PartyName );
   ClearRecord( Parm );
   return false;
END;

MACRO ПолучитьПараметрыДУ_ПоСделке( Parm:VARIANT, FD:SPFirstDoc )
   if( IsEXCHANGE(FD.Group) AND FD.СделкаОРЦБ() )
      return ПолучитьПараметрыДУ( Parm, FD.tick.rec.MarketID, FD.GetMarketSchemeID() );
   else 
      return ПолучитьПараметрыДУ( Parm, -1, -1 );
   end;
END;

/*такое сохранение счетов в платеже позволяет откатить эти изменения при откате шага*/
MACRO СохранитьСчетаВПлатеже( pmpaym, DtAccFIID:@INTEGER, CtAccFIID:@INTEGER )

   var    pmobj = RsbPayment( pmpaym.rec.PaymentID );

   if( DtAccFIID == null )
      DtAccFIID = pmobj.PayerFIID;
   end;

   if( CtAccFIID == null )
      CtAccFIID = pmobj.ReceiverFIID;
   end;

   if( (pmpaym.rec.PayerAccount != pmobj.PayerAccount) OR (DtAccFIID != pmobj.PayerFIID) )
      pmobj.SetPayerPI( PAYMENTS_GROUP_UNDEF, 
                       {OurBank}, 
                       PTCK_CONTR,
                       ПолучитьКодСубъекта( {OurBank}, PTCK_CONTR), 
                       {Name_Bank}, 
                       "", 
                       DtAccFIID, 
                       1/*CHAPT1*/, 
                       pmpaym.rec.PayerAccount, 
                       pmobj.Payer, 
                       pmobj.PayerName, 
                       pmobj.PayerINN,
                       pmobj.PayerCodeKind, 
                       pmobj.PayerCode
                      );
   end;

   if( (pmpaym.rec.ReceiverAccount != pmobj.ReceiverAccount) OR (CtAccFIID != pmobj.ReceiverFIID) )
      pmobj.SetReceiverPI( PAYMENTS_GROUP_UNDEF, 
                       {OurBank}, 
                       PTCK_CONTR,
                       ПолучитьКодСубъекта( {OurBank}, PTCK_CONTR), 
                       {Name_Bank}, 
                       "", 
                       CtAccFIID, 
                       1/*CHAPT1*/, 
                       pmpaym.rec.ReceiverAccount, 
                       pmobj.Receiver, 
                       pmobj.ReceiverName, 
                       pmobj.ReceiverINN,
                       pmobj.ReceiverCodeKind, 
                       pmobj.ReceiverCode
                      );
   end;

END;


MACRO СчетаПолучателяКомиссииВПогашении( FD )

  if( FD.СделкаОРЦБ() == true ) /*ОРЦБ*/
     return "+Биржа";
  elif( FD.tick.rec.Flag4 == SET_CHAR ) /*БРОКЕР*/
     return "Брокер";
  else /*Не ОРЦб и не Брокер*/
     return "-Расчеты";
  end;
  return "";
END;


PRIVATE record accdoc_plus(mcaccdoc);
PRIVATE record accdoc_minus(mcaccdoc);

private macro ПлатежБылОтложен( paym )
  return (ТОБылоПролонгировано(paym) or (paym.rec.State == DLRQ_STATE_DELAYED));
end;

MACRO АктуализироватьСрочныеСчета( FD, dat, CheckPeriod, acc1, acc2, PeriodID_Plus:@integer, PeriodID_Minus:@integer, NeedReOpenAccounts:bool )

  if( not (FD.DealAsResultDVExe() and FD.tick.rec.OriginID == CodeFor("Ю")) )

     var DealType = FD.ВидСрочностиСделки(), CategForvard;

     if( PeriodID_Plus AND PeriodID_Minus ) 
        PeriodID_Plus = PeriodID_Minus = 0;
     end;

     if( ( (DealType != СделкаToday) OR
           (IsREPO(FD.Group) AND CheckPeriod) /*нужен перенос по счетам ОД*/
         ) AND 
         (not IsClientDeal(FD.tick.rec)) AND 
         (IsREPO(FD.Group) OR FD.ВедетсяВнебалУчетПоСделке()) 
       )
        if( IsREPO(FD.Group) == true )
           if( IsBUY(FD.Group) )
              CategForvard = IIF(IsBasket(FD.Group), "+ОД, Корзина", "+ОД");
           else
              CategForvard = IIF(IsBasket(FD.Group), "-ОД, Корзина", "-ОД");
           end;
        end;

        var ReqCateg, ComCateg, ПлатежТребОтложен = false, ПлатежОбязатОтложен = false;
        
        if( not IsREPO(FD.Group) )
           if( FD.BuySale == DEAL_TYPE_SALE ) 
              ПлатежОбязатОтложен = ПлатежБылОтложен(FD.GetRQ(DLRQ_TYPE_DELIVERY));
              ПлатежТребОтложен   = ПлатежБылОтложен(FD.GetRQ(DLRQ_TYPE_PAYMENT));
           else
              ПлатежОбязатОтложен = ПлатежБылОтложен(FD.GetRQ(DLRQ_TYPE_PAYMENT));
              ПлатежТребОтложен   = ПлатежБылОтложен(FD.GetRQ(DLRQ_TYPE_DELIVERY));
           end;

           if( not FD.БылоОтложенноеИсполнение() )
              if( FD.DealAsResultDVExe == false )
                 ReqCateg = "+Форвард, прочие"; 
                 ComCateg = "-Форвард, прочие";
              else
                 ReqCateg = "+Форвард, дрейф"; 
                 ComCateg = "-Форвард, дрейф";
              end;
           elif( ПлатежОбязатОтложен AND (not ПлатежТребОтложен) ) 
              /*есть (были) только отложенные обязательства, нет отложенных требований*/
              if( FD.DealAsResultDVExe == false )
                 ReqCateg = "+Форвард, прочие"; 
              else
                 ReqCateg = "+Форвард, дрейф"; 
              end;
              ComCateg = "-Форвард, ОИ";
           elif( ПлатежТребОтложен AND (not ПлатежОбязатОтложен) )
              /*есть (были) только отложенные требования, нет отложенных обязательств*/
              ReqCateg = "+Форвард, ОИ"; 
              if( FD.DealAsResultDVExe == false )
                 ComCateg = "-Форвард, прочие";
              else
                 ComCateg = "-Форвард, дрейф";
              end;
           else /*были и отложенные требования и отложенные обязательства*/
              ReqCateg = "+Форвард, ОИ"; 
              ComCateg = "-Форвард, ОИ";
           end;
        end;

        if( CheckPeriod  != true )

           if( IsREPO(FD.Group) )
              if( NeedReOpenAccounts ) 
                 if( not FD.ReOpenAccount( CategForvard, null, null, null, acc1, dat ) )
                    return false;
                 end;
              else
                 if( not FD.OpenAccount( CategForvard, null, null, null, acc1, dat ) ) 
                    return false;
                 end;
              end; 
              copy( acc2, acc1 );
           else
              if( (DealType == СделкаПрочая) or (DealType == СделкаНеРанееТретьегоДня) )
                 if( NeedReOpenAccounts ) 
                    if( (not FD.ReOpenAccount( ReqCateg, null, null, FIROLE_FIREQ, acc1, dat ) ) OR
                        (not FD.ReOpenAccount( ComCateg, null, null, FIROLE_FICOM, acc2, dat ) ) )
                       return false;
                    end;
                 else
                    if( (not FD.OpenAccount( ReqCateg, null, null, FIROLE_FIREQ, acc1, dat ) ) OR
                        (not FD.OpenAccount( ComCateg, null, null, FIROLE_FICOM, acc2, dat ) ) )
                       return false;
                    end;
         
                 end; 
              end;
           end;
        else
           if( IsREPO(FD.Group) )         
              if( not FD.ActualCheckPeriod( CategForvard, null, null, null, acc2, dat, null, null, accdoc_minus ) ) 
                 return false;
              end;
              PeriodID_Plus   = accdoc_plus.PeriodID;
              PeriodID_Minus  = accdoc_minus.PeriodID;
           else
              if( (DealType == СделкаПрочая) or (DealType == СделкаНеРанееТретьегоДня) )
                 if( (not FD.ActualCheckPeriod( ReqCateg, null, null, FIROLE_FIREQ, acc1, dat, null, null, accdoc_plus ) ) OR
                     (not FD.ActualCheckPeriod( ComCateg, null, null, FIROLE_FICOM, acc2, dat, null, null, accdoc_minus ) ) )
                    return false;
                 end;
                 PeriodID_Plus   = accdoc_plus.PeriodID;
                 PeriodID_Minus  = accdoc_minus.PeriodID;

              end;
           end;
        end;         
     end;
  end;

     return true; 
END;
