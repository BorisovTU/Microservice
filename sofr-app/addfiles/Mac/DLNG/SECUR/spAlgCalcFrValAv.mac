/*
$Name:        spAlgCalcFrValAv.mac
$Module:      Ценные бумаги
$Description: Алгоритм расчета справедливой стоимости для ценных бумаг
*/

IMPORT "spserv.mac";

MACRO SPCalcFrVal( pFrVal:TRecHandler, pDEAL:TRecHandler, pLeg:TRecHandler ):integer

  var Fininstr = TRecHandler( "fininstr.dbt" );

  if( pFrVal.rec.Date == date(0,0,0) )
     MsgBox("Не задана дата расчёта.");
  elif( ПолучитьФинИн(pDEAL.rec.PFI, Fininstr) )
     MsgBox("Не найден ФИ c ID = " + pDEAL.rec.PFI + ".");
  else
     if( Fininstr.rec.FI_Kind != FIKIND_AVOIRISS )
        MsgBox("В операции некорректно указан алгоритм расчета. Базовый актив ПФИ не ценная бумага.");
     else
        var Course:double = 0.0;
        var CourceSinceDate:date = date(0,0,0);
        /*Получим курс "ТСС" в валюте номинала*/
        if( SmartConvertSumDbl( Course, 1, pFrVal.rec.Date, 
                                Fininstr.rec.FIID, Fininstr.rec.FaceValueFI, false, 
                                ПолучитьВидКурса(SP_RATETYPE_MediumPrice), CourceSinceDate ) == false 
          )
           MsgBox("Невозможно получить курс вида 'Средневзвешенная цена' для FIID = " + String(Fininstr.rec.FIID) + " на дату " + String(pFrVal.rec.Date));
        else
           var ToContinue = true;
           if( CourceSinceDate != pFrVal.rec.Date )
              ToContinue = false;
              if( MsgBoxEx("На дату "+string(pFrVal.rec.Date)+" не найден курс вида 'Средневзвешенная цена'.| Выполнить расчет по курсу за " + String(CourceSinceDate) + "?", MB_YES+MB_NO, IND_YES) == IND_YES )
                 ToContinue = true;
              end;
           end;

           if( ToContinue == true )

              if( Fininstr.rec.FaceValueFI != NATCUR )
                 var FICourse:double = 0.0;
                 /*если в системе не задан курс валюта БА к рублям, то выдается сообщение.*/
                 if( SmartConvertSumDbl( FICourse, 1, pFrVal.rec.Date, 
                                         Fininstr.rec.FaceValueFI, NATCUR, false ) == false 
                   )
                    MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(Fininstr.rec.FaceValueFI) + " на дату " + String(pFrVal.rec.Date));
                    return 1;
                 else
                    Course = FICourse * Course;
                 end;
              end;

              var v_SuplCost = 0.0, v_PayCost = 0.0;

              v_SuplCost = pLeg.rec.Principal * Course;
              v_SuplCost = money(round(v_SuplCost, 2));

              v_PayCost  = pLeg.rec.Cost;
              if( pLeg.rec.CFI != NATCUR )
                 var PFCourse:double = 0.0;
                 /*если в системе не задан курс валюта БА к рублям, то выдается сообщение.*/
                 if( SmartConvertSumDbl( PFCourse, 1, pFrVal.rec.Date, 
                                         pLeg.rec.CFI, NATCUR, false ) == false 
                   )
                    MsgBox("Невозможно получить курс вида 'ЦБ РФ' для FIID = " + String(pLeg.rec.CFI) + " на дату " + String(pFrVal.rec.Date));
                    return 1;
                 else
                    v_PayCost = v_PayCost * PFCourse;
                 end;
              end;

              v_PayCost = money(round(v_PayCost, 2));

              var Group = SP_GetOperationGroup( pDEAL.rec );

              if( IsBuy(Group) )
                 pFrVal.rec.FairValue = v_SuplCost - v_PayCost;
              else
                 pFrVal.rec.FairValue = v_PayCost - v_SuplCost;
              end;

              pFrVal.rec.SetDemand = pFrVal.rec.SetLiability = pFrVal.rec.SetDemand2 = pFrVal.rec.SetLiability2 = UNSET_CHAR;

              return 0; /*рассчитали значение без ошибок*/

           end;
        end;
     end;
  end;

  return 1; /*по умолчанию ошибка*/
END;
