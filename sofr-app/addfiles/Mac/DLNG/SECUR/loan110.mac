 /*
$Name:         loan110.mac 
$Module:       Ценные бумаги
$Description:  Операция займ. Шаг подписания договора
*/

import InsCarryDoc, "sp_class.mac", "sp_car.mac", "sprpord.mac";

private record SpChangeDates("spchdate.str");/*глобальная структура с данными для расчетов*/
private record Deal_Tick (dl_tick);

macro ExecuteStep( doc, FDoc, DocKind, ID_Op )

  var  FD,
       dat = SpChangeDates.FactDate; /*дата подписания договора*/ 

  if( SpChangeDates.Action <= 0 )
     msgbox("Шаг подписания договора из списка шагов выполнять запрещено."); 
     return 1;
  end; 

  SetBuff( Deal_Tick, FDoc ); 

  FD = SPFirstDoc( Deal_Tick );

  if( dat > FD.DateArray[DATE_DEALEEND] )
     MsgBox( "Дата подписания договора должна быть не больше даты завершения сделки.");
     return 1;
  end;                           

  /*Проверить наличие шага подготовки договора*/
  if( SP_IsExistOprStep( ID_Op, 101, true ) ) 
     msgbox( "Подписание договора по данной операции уже выполнено." );
     return 1;
  end;

  if( ПроверитьДоговор( FD, false ) == false )
     return 1;
  end;

  /*ставим признак - Договор подготовлен*/ 
  if( not ОбновитьФлагиЧастиСделки( FD.dl_leg, DL_LEG_PREPARE_CONTR ) )
     msgbox("Ошибка при обновлении статуса в лоте"); 
     return 1;
  end; 
 
  return 0;
end;

/* Макрос постобработки */
MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */
  if (errTrn OR (CommitOrRollback != 1)) 
      /* Произошла ошибка или происходит откат */
      return 0;
  end;

  message("Идет формирование договора...");
  SetBuff(Deal_Tick, FDoc ); 
  PrintReport( Deal_Tick.DealID, false );
  
  return 0;
END;


/* Макрос печати */
MACRO PrintStepDocs (
                ID_Operation,  /* Номер экземпляра операции */
                ID_Step,       /* Номер шага операции */
                Kind_Operation,/* Вид операции */
                KindStep)      /* Вид шага операции */
    
    message("Идет формирование договора...");
    PrintReport( Deal_Tick.DealID, false );
    exit(1); /*Для того, чтобы не выводился пустой файл */
END;
