/*
$Name:        ws_avoirautocoup.mac
$Module:      Ценные бумаги
$Description: WEB. АРНУ. Макрос сервиса "Параметры процедуры автоматического формирования купонов"
*/
/*
  Avdascheno D. V.
  18.10.2012
*/
import RSD;
import RsbDataSet;
import FIInter;
import "secinter.mac";
import "ws_common.mac";
import "ws_avoiriss.mac";
import "ws_fiunilist.mac";

class CTxAvoirAutoCoup()
  var FirstDate                   : Date;     // Дата начала первого купона
  var DrawingDate                 : Date;     // Дата погашения облигации
  var PeriodFirstCoupon           : Integer;  // Период первого купона
  var PeriodFirstCouponKindDays   : Bool;     // Дней
  var PeriodFirstCouponKindMonths : Bool;     // Месяцев
  var PeriodCoupon                : Integer;  // Купонный период
  var PeriodCouponKindDays        : Bool;     // Дней
  var PeriodCouponKindMonths      : Bool;     // Месяцев
  var NumberCoupons               : Integer;  // Количество купонов
  var DayDrawingCoupon            : Integer;  // День погашения купона
  var IncomePoint                 : Integer;  // Округление
  var IncomeScale                 : Integer;  // Масштаб
  var NonRelativeIncome           : Bool;     // Доход
  var IncomeVolume                : Money;    // Сумма дохода
  var RelativeIncome              : Bool;     // % дохода
  var IncomeRate                  : Double;    // Ставка дохода
  var Definition                  : String;   // Комментарий
end;

private macro TxAvoirAutoCoupRunError(
  err
 ,stat : Integer
)
  RunError("Макрос: ws_avoirautocoup.mac, Строка: " + err.line + ", Сообщение: " + err.message, stat);
end;

private macro IIF(
  Condition   // Условие
 ,ValueTrue   // Значение если условие истинно
 ,ValueFalse  // Значение если условие ложно 
)
  if(Condition)
    return ValueTrue;
  else 
    return ValueFalse;
  end;
end;

macro TxAvoirAutoCoupInit(
  FIID : Integer      // ИД финансового инструмента
) : CTxAvoirAutoCoup  // Поля формы автомаческого ввода купонов
  var stat = 0;
  var _FormData = CTxAvoirAutoCoup();
  var RecHndl = TRecHandler("txavoirautocoup.rec");

  stat = WS_FI_GetInitPrm(FIID, RecHndl);

  if(not stat)
    _FormData.FirstDate                   = RecHndl.rec.FirstDate;
    _FormData.DrawingDate                 = RecHndl.rec.DrawingDate;
    _FormData.PeriodFirstCoupon           = RecHndl.rec.PeriodFirstCoupon;
    _FormData.PeriodFirstCouponKindDays   = IIF(RecHndl.rec.PeriodFirstCouponKindDays, true, false);
    _FormData.PeriodFirstCouponKindMonths = IIF(RecHndl.rec.PeriodFirstCouponKindMonths, true, false);
    _FormData.PeriodCoupon                = RecHndl.rec.PeriodCoupon;
    _FormData.PeriodCouponKindDays        = IIF(RecHndl.rec.PeriodCouponKindDays, true, false);
    _FormData.PeriodCouponKindMonths      = IIF(RecHndl.rec.PeriodCouponKindMonths, true, false);
    _FormData.NumberCoupons               = RecHndl.rec.NumberCoupons;
    _FormData.DayDrawingCoupon            = RecHndl.rec.DayDrawingCoupon;
    _FormData.IncomePoint                 = RecHndl.rec.IncomePoint;
    _FormData.IncomeScale                 = RecHndl.rec.IncomeScale;
    _FormData.NonRelativeIncome           = IIF(RecHndl.rec.NonRelativeIncome, true, false);
    _FormData.IncomeVolume                = RecHndl.rec.IncomeVolume;
    _FormData.RelativeIncome              = IIF(RecHndl.rec.RelativeIncome, true, false);
    _FormData.IncomeRate                  = RecHndl.rec.IncomeRate;
    _FormData.Definition                  = RecHndl.rec.Definition;
  end;

  return _FormData;
end;

private macro CreateFIWarntsTmp()
  var stat : Integer = 0;
  var SQLQuery : String = "";
  var RsdCmd;

  SQLQuery =
    " BEGIN "
    + " EXECUTE IMMEDIATE 'TRUNCATE TABLE DFIWARNTS_TMP'; "
  + " EXCEPTION "
  + " WHEN OTHERS THEN "
    + " IF SQLCODE != -0942 THEN "
      + " RAISE; "
    + " END IF; "
  + " END; ";

  RsdCmd = RsdCommand(SQLQuery);
  RsdCmd.execute();

  SQLQuery =
    " BEGIN "
    + " EXECUTE IMMEDIATE 'DROP TABLE DFIWARNTS_TMP'; "
  + " EXCEPTION "
  + " WHEN OTHERS THEN "
    + " IF (SQLCODE != -0942) and (SQLCODE != -14452) THEN "
      + " RAISE; "
    + " END IF; "
  + " END; ";

  RsdCmd = RsdCommand(SQLQuery);
  RsdCmd.execute();

  SQLQuery =
    " BEGIN "
    + " EXECUTE IMMEDIATE 'CREATE GLOBAL TEMPORARY TABLE DFIWARNTS_TMP ON COMMIT PRESERVE ROWS AS SELECT * FROM DFIWARNTS_DBT WHERE T_FIID = -1'; "
  + " EXCEPTION "
  + " WHEN OTHERS THEN "
    + " IF SQLCODE = -00955 THEN "
      + " EXECUTE IMMEDIATE 'INSERT INTO DFIWARNTS_TMP (SELECT * FROM DFIWARNTS_DBT WHERE T_FIID = -1)'; "
    + " ELSE "
      + " RAISE; "
    + " END IF; "
  + " END; ";

  RsdCmd = RsdCommand(SQLQuery);
  RsdCmd.execute();

  return stat;
end;

private macro InputAutoCoupToFIWarntsTmp(
  FIID       : Integer              // ИД финансового инструмента
 ,FormData /*: CTxAvoirAutoCoup */  // Поля формы автомаческого ввода купонов
)
  var stat : Integer = 0;
  var RecHndl = TRecHandler("txavoirautocoup.rec");
  var _FormData = FormData;

  RecHndl.Clear();

  RecHndl.rec.FirstDate                   = _FormData.FirstDate;
  RecHndl.rec.DrawingDate                 = _FormData.DrawingDate;
  RecHndl.rec.PeriodFirstCoupon           = _FormData.PeriodFirstCoupon;
  RecHndl.rec.PeriodFirstCouponKindDays   = IIF(_FormData.PeriodFirstCouponKindDays, SET_CHR, UNSET_CHR);
  RecHndl.rec.PeriodFirstCouponKindMonths = IIF(_FormData.PeriodFirstCouponKindMonths, SET_CHR, UNSET_CHR);
  RecHndl.rec.PeriodCoupon                = _FormData.PeriodCoupon;
  RecHndl.rec.PeriodCouponKindDays        = IIF(_FormData.PeriodCouponKindDays, SET_CHR, UNSET_CHR);
  RecHndl.rec.PeriodCouponKindMonths      = IIF(_FormData.PeriodCouponKindMonths, SET_CHR, UNSET_CHR);
  RecHndl.rec.NumberCoupons               = _FormData.NumberCoupons;
  RecHndl.rec.DayDrawingCoupon            = _FormData.DayDrawingCoupon;
  RecHndl.rec.IncomePoint                 = _FormData.IncomePoint;
  RecHndl.rec.IncomeScale                 = _FormData.IncomeScale;
  RecHndl.rec.NonRelativeIncome           = IIF(_FormData.NonRelativeIncome, SET_CHR, UNSET_CHR);
  RecHndl.rec.IncomeVolume                = _FormData.IncomeVolume;
  RecHndl.rec.RelativeIncome              = IIF(_FormData.RelativeIncome, SET_CHR, UNSET_CHR);
  RecHndl.rec.IncomeRate                  = _FormData.IncomeRate;
  RecHndl.rec.Definition                  = _FormData.Definition;

  stat = WS_FI_InputAutoCoup(FIID, RecHndl);

  return stat;
end;

private macro GetAutoCoupFromFIWarntsTmp(
  FIID : Integer  // ИД финансового инструмента
 ,AvrPays         // Массив сформированных купонов
)
  var stat : Integer = 0;
  var DtSet = TRsbDataSet(" SELECT DFIWARNTS_TMP.* FROM DFIWARNTS_TMP WHERE T_FIID = " + FIID + " ; ");
  var AvrPay;

  while(DtSet.MoveNext())
    AvrPay = CTxAvoirPay();

    AvrPay.FIID               = DtSet.rec.FIID;
    AvrPay.IsPartial          = DtSet.rec.IsPartial;
    AvrPay.Number             = DtSet.rec.Number;
    AvrPay.DrawingDate        = DtSet.rec.DrawingDate;
    AvrPay.IncomeVolume       = DtSet.rec.IncomeVolume;
    AvrPay.IncomeFI           = GetTWsFinInstrByID(SQL_ConvTypeInteger(DtSet.rec.IncomeFI));
    AvrPay.IsClosed           = DtSet.rec.IsClosed;
    AvrPay.Name               = DtSet.rec.Name;
    AvrPay.FirstDate          = DtSet.rec.FirstDate;
    AvrPay.ListDate           = DtSet.rec.ListDate;
    AvrPay.IncomePoint        = DtSet.rec.IncomePoint;
    AvrPay.IncomeScale        = DtSet.rec.IncomeScale;
    AvrPay.NonRelativeIncome  = IIF(DtSet.rec.RelativeIncome, false, true);
    AvrPay.RelativeIncome     = IIF(DtSet.rec.RelativeIncome, true, false);
    AvrPay.IncomeRate         = DtSet.rec.IncomeRate;
    AvrPay.AllPartialPercent  = 0.0;
    AvrPay.Definition         = DtSet.rec.Definition;
    AvrPay.Action             = OperationKind_Insert;
    AvrPay.ID                 = 0;
    AvrPay.Version            = 0;

    AvrPays[AvrPays.Size] = AvrPay;
  end;

  return stat;
end;

macro TxAvoirAutoCoupRun(
  FIID       : Integer              // ИД финансового инструмента
 ,FormData /*: CTxAvoirAutoCoup */  // Поля формы автомаческого ввода купонов
)
  var stat : Integer = 0;
  var _FormData = FormData;
  var AvrPays = TArray();

  stat = CreateFIWarntsTmp();

  if(not stat)
    stat = InputAutoCoupToFIWarntsTmp(FIID, _FormData);
  end;

  if(not stat)
    stat = GetAutoCoupFromFIWarntsTmp(FIID, AvrPays);
  end;

  return AvrPays;
end;

/*var fd = TxAvoirAutoCoupInit(501);
TxAvoirAutoCoupRun(501, fd);*/
