/*
$Name:        spmrc.mac
$Module:      Ядро Securities
$Description: Отчет "Расчет рыночного риска" 
*/

import "spmrc_form.mac", "spRepFun.mac", "spserv.mac";
import "dvrepfun.mac", "dvRepFun2.mac", "dv_categ.mac", "dv_car.mac";
import SecInter;

PRIVATE CONST
 MaxDate = date(31,12,9999);

PRIVATE CONST INTERVALS_COUNT  = 16,
              RISK_COEFFICIENT = 0.08; 

private const use_poi_const = NULL;              

PRIVATE var TFactName = TArray();

TFactName[1]  = "менее 1 месяца";
TFactName[2]  = "1-3 месяцев";
TFactName[3]  = "3-6 месяцев";
TFactName[4]  = "6-12 месяцев";
TFactName[5]  = "1-2 года";
TFactName[6]  = "2-3 года";
TFactName[7]  = "3-4 года";
TFactName[8]  = "4-5 лет";
TFactName[9]  = "5-7 лет";
TFactName[10] = "7-10 лет";
TFactName[11] = "10-15 лет";
TFactName[12] = "15-20 лет";
TFactName[13] = "более 20 лет";
TFactName[14] = "менее 1 месяца";
TFactName[15] = "1-3 месяцев";
TFactName[16] = "3-6 месяцев";
TFactName[17] = "6-12 месяцев";
TFactName[18] = "1-1,9 года";
TFactName[19] = "1,9-2,8 года";
TFactName[20] = "2,8-3,6 года";
TFactName[21] = "3,6-4,3 года";
TFactName[22] = "4,3-5,7 года";
TFactName[23] = "5,7-7,3 года";
TFactName[24] = "7,3-9,3 года";
TFactName[25] = "9,3-10,6 года";
TFactName[26] = "10,6-12 лет";
TFactName[27] = "12-20 лет";
TFactName[28] = "более 20 лет";

PRIVATE var TCalculateCoef = TArray();

TCalculateCoef[1]  = 0;
TCalculateCoef[2]  = 0.2;
TCalculateCoef[3]  = 0.4;
TCalculateCoef[4]  = 0.7;
TCalculateCoef[5]  = 1.25;
TCalculateCoef[6]  = 1.75;
TCalculateCoef[7]  = 2.25;
TCalculateCoef[8]  = 2.75;
TCalculateCoef[9]  = 3.25;
TCalculateCoef[10] = 3.75;
TCalculateCoef[11] = 4.50;
TCalculateCoef[12] = 5.25;
TCalculateCoef[13] = 6;
TCalculateCoef[14] = 0;
TCalculateCoef[15] = 0.2;
TCalculateCoef[16] = 0.4;
TCalculateCoef[17] = 0.7;
TCalculateCoef[18] = 1.25;
TCalculateCoef[19] = 1.75;
TCalculateCoef[20] = 2.25;
TCalculateCoef[21] = 2.75;
TCalculateCoef[22] = 3.25;
TCalculateCoef[23] = 3.75;
TCalculateCoef[24] = 4.50;
TCalculateCoef[25] = 5.25;
TCalculateCoef[26] = 6;
TCalculateCoef[27] = 8;
TCalculateCoef[28] = 12.5;

PRIVATE var TRiskGroup = TArray();

PRIVATE CONST БЕЗРИСКА         = 1,
              НИЗКИЙ_ДО_6_МЕС  = 2,
              НИЗКИЙ_6_24_МЕС  = 3,
              НИЗКИЙ_ОТ_24_МЕС = 4,
              СРЕДНИЙ          = 5, 
              ВЫСОКИЙ          = 6;  

TRiskGroup[БЕЗРИСКА]         = 0;       /*Без риска (0 процентов)*/
TRiskGroup[НИЗКИЙ_ДО_6_МЕС]  = 0.0025;  /*С низким риском 0.25 ... 1.6*/
TRiskGroup[НИЗКИЙ_6_24_МЕС]  = 0.01;
TRiskGroup[НИЗКИЙ_ОТ_24_МЕС] = 0.016;  
TRiskGroup[СРЕДНИЙ]          = 0.08;             
TRiskGroup[ВЫСОКИЙ]          = 0.12;           

PRIVATE VAR MRCALC_Form = DL_MRCALC_Panel();

PRIVATE CLASS (TArray) TAvrData

  CLASS OneStrAvrData(A6_,A6_1_,A15_,A10_,A11_,A12_,A13_,A14_)
     var A6      = A6_,
         A6_1    = A6_1_,
         A15     = A15_,
         A10     = A10_,
         A11     = A11_,
         A12     = A12_,
         A13     = A13_,
         A14     = A14_;
  END;
  /* Очистить массив. */
  macro ClearArray
     this.Size = 0;
  end;

  /* Добавить строчку в массив. */
  macro AddLine(A6_,A6_1_,A15_,A10_,A11_,A12_,A13_,A14_, UseSummator)
    var
       Counter = 0, i = 0;
    if(UseSummator)
       /* Ищем в массиве счет. */      
       while( (Counter < this.Size) and
              ( (this.(Counter).A10 != A10_) or (this.(Counter).A13 != A13_) ) )
          Counter = Counter + 1;
       end;

       if( (Counter < this.Size) and ( (this.(Counter).A10 == A10_) or (this.(Counter).A13 == A13_) )  )
          /* Нашли счет и интервал. */
         this.(Counter).A6 = this.(Counter).A6 + A6_;
         this.(Counter).A6_1 = this.(Counter).A6_1 + A6_1_;
         this.(Counter).A11 = this.(Counter).A11 + A11_;
         this.(Counter).A12 = this.(Counter).A12 + A12_;
       else
         this.(this.Size) = OneStrAvrData( A6_,A6_1_,A15_,A10_,A11_,A12_,A13_,A14_ );
       end;
    else
       this.(this.Size) = OneStrAvrData( A6_,A6_1_,A15_,A10_,A11_,A12_,A13_,A14_ );
    end;
  end; /*AddLine*/

END;

/*Данные для заполнения общего процентного риска в разрезе валют */
PRIVATE CLASS OPR_Data(TLongP_, TShortP_, Currency_, ЦБвр_)
  var TLongP = TLongP_,
      TShortP = TShortP_,
      Currency = Currency_,
      ЦБвр = ЦБвр_;
END;

PRIVATE CLASS (TArray) TOPR_Collector
  /* Очистить массив. */
  macro ClearArray()
     this.Size = 0;
  end;

  macro Add(TLongP_, TShortP_, Currency_, ЦБвр_, Posistion)
      var
         Counter = 0, i = 0;

      /* Ищем в массиве нужную валюту. */      
      while( (Counter < this.Size) and (this.(Counter).Currency != Currency_) )
         Counter = Counter + 1;
      end;

      if( (Counter < this.Size) and (this.(Counter).Currency == Currency_) )
         /* Нашли валюту, суммируем значения для валюты. */
         while( i < INTERVALS_COUNT )
           this.(Counter).TLongP[i] = this.(Counter).TLongP[i] + TLongP_[i];
           this.(Counter).TShortP[i] = this.(Counter).TShortP[i] + TShortP_[i];
           i = i + 1; 
         end;
         this.(Counter).ЦБвр = this.(Counter).ЦБвр + ЦБвр_;
      else
         /* Запись не нашли. Нужно вставить запись в конец массива. */
         this.(this.Size) = OPR_Data(TLongP_, TShortP_, Currency_, ЦБвр_);
      end;
  end;  
END;

PRIVATE CONST FORMULA_REPLACE_CALC  = "formula=";
PRIVATE CONST FORMULA  = "=";
PRIVATE CONST DecimalSeparator = GetLocaleInfo(0,LOCALE_SDECIMAL, false );
            
PRIVATE CLASS (DL_CReportTemplate) DL_MRCALC_Report

  var wrtcalc = TRecHandler( "pmwrtsum" );
  var FIn = TRecHandler("fininstr");
  var Avr = TRecHandler("avoiriss");
  var Acc = TRecHandler( "account" ), AccPort = TRecHandler( "account" ), Account = "";
  var m_SqlAvr, m_SqlAvrRsd, m_AvrData, m_RepDate, m_LongCource, m_ShortCource, m_IsDataExists = false;
  var m_SqlTick, m_SqlTickRsd, m_TickData;
  var m_T18 = TArray(), AvrData = TAvrData();
  var T_4, T_5, T_8, T_9_val, T_9_txt, T_7 = 1, A_15, T_6,T_15,T_16, T_16_Currency;
  var СПР = $0, СФР = $0, ОФР = $0, m_TSS_FR_SUM = $0;
  var m_Pos_FR_Sum = $0;
  var m_AvrLongCource, m_AvrShortCource, m_RetireDate, m_ExcludeFromCalc, m_NoPortAndDeals;
  var m_AvrLongCourceFIID, m_AvrShortCourceFIID, m_AvrLongCourcePrec, m_AvrShortCourcePrec;
  var m_AmountSumPrecItog = 0;/*точность, скоторой выводим кол-во(может быть дробным после ГО)*/

  /*Длинные и\или короткие ЧП, распределенные по временному интервалу*/
  var TLongP = TArray();
  var TShortP = TArray();
  var ЦБвр;

  var OPR_Collector = TOPR_Collector();
 
  PRIVATE MACRO ClearPosArray(arr, arr_size)
    var i = 0;
    while( i < arr_size )
      arr[i] = $0;
      i = i + 1; 
    end;
  END;
    
  PRIVATE MACRO ClearRiskItog()
     var i = 0;
     ClearPosArray(TLongP, INTERVALS_COUNT);
     ClearPosArray(TShortP, INTERVALS_COUNT);
     СПР = СФР = ОФР = $0; 
     ЦБвр = $0;
  END;

  PRIVATE MACRO GetT_7()
     if(T_7 > 13 )
        return T_7 - 13;
     end;

     return T_7;
  END;

  /*итоговые данные по выпуску на вкладке "Позиции"*/
  PRIVATE MACRO ClearT()
     T_4                  = null;
     T_5                  = null;
     T_8                  = null;
     T_9_val              = null;
     T_9_txt              = null;
     A_15                 = null;
     T_7                  = 1;
     T_15                 = "";
     T_6                  = 0.;
     T_16                 = $0;
     T_16_Currency        = $0;
     m_Pos_FR_Sum         = $0;/*сумма всех (портфель+сделки) позиций (для ФР)*/
     m_AvrLongCource      = null;
     m_AvrShortCource     = null;
     m_RetireDate         = ZeroDate;
     m_AvrLongCourceFIID  = null;
     m_AvrShortCourceFIID = null;
     m_AvrLongCourcePrec  = 0;
     m_AvrShortCourcePrec = 0;
     m_AmountSumPrecItog  = 0;/*точность кол-ва в итоговой троке*/
     m_ExcludeFromCalc    = false;/*исключить ц\б из расчета*/
     m_NoPortAndDeals     = true;/*нет сделок и пусты портфели*/
  END;

  PRIVATE MACRO ReplaceSeparator( Str:@string, Separator:string )
     if( DecimalSeparator != Separator )
        var index_ = Index(Str, Separator);
        while( (index_ != 0) and (index_ <= strlen(Str)) )
           Str = substr(Str, 1, index_ - 1) + DecimalSeparator + substr(Str, index_ + 1, strlen(Str) - 1);
           index_ = Index(Str, Separator);
        end;
     end;
  END;

  PRIVATE MACRO F_RC( Str:string ):string
     /* заменим разделитель целой и дробной частей */
     ReplaceSeparator( @Str, "." );
     ReplaceSeparator( @Str, "," );

     return FORMULA_REPLACE_CALC + Str;
  END;

  PRIVATE MACRO F( Str:string ):string
     /* заменим разделитель целой и дробной частей */
     ReplaceSeparator( @Str, "." );
     ReplaceSeparator( @Str, "," );

     return FORMULA + Str;
  END;

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport()
     m_RepDate = MRCALC_Form.GetFieldValue(PNFLD_MRCALC_REPDATE);
     m_LongCource = MRCALC_Form.GetFieldValue(PNFLD_MRCALC_LONG_COURCE);
     m_ShortCource = MRCALC_Form.GetFieldValue(PNFLD_MRCALC_SHORT_COURCE);
     DL_CallInsertStat(PrintMode());
     CreateTotalBook();
  END;

  MACRO FormulaSUM():string
     var RetVal = "";
     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "SUM";
     else
        RetVal = "СУММ";
     end;

     return RetVal;
  END;

  MACRO FormulaIF():string
     var RetVal = "";
     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "IF";
     else
        RetVal = "ЕСЛИ";
     end;

     return RetVal;
  END;

  MACRO FormulaSUMIF():string
     var RetVal = "";
     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "SUMIF";
     else
        RetVal = "СУММЕСЛИ";
     end;

     return RetVal;
  END;

  MACRO FormulaSIGN():string
     var RetVal = "";
     if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )
        RetVal = "SIGN";
     else
        RetVal = "ЗНАК";
     end;

     return RetVal;
  END;

  PRIVATE MACRO ОтобратьВыпуски(ВПортфелеБанка:bool) 
     var NRec = 0;

     m_SqlAvrRsd = DL_RSDCommand(); 

     m_SqlAvr = " select fin.* "+
                " from dfininstr_dbt fin "+
                " where FIN.T_FI_KIND = ? "+
                " and RSB_FIInstr.FI_AvrKindsGetRoot(FIN.T_FI_KIND,FIN.T_AVOIRKIND) in(?,?,?) ";

     m_SqlAvrRsd.addParam(FIKIND_AVOIRISS);
     m_SqlAvrRsd.addParam(AVOIRISSKIND_BOND);
     m_SqlAvrRsd.addParam(AVOIRISSKIND_SHARE);
     m_SqlAvrRsd.addParam(AVOIRISSKIND_DEPOSITORY_RECEIPT);

     if(ВПортфелеБанка)
        m_SqlAvr = m_SqlAvr +
                " and (SELECT NVL(sum(t.t_Amount),0) amount FROM v_scwrthistex t " +   
                "       WHERE t.t_buy_sale = ? " +  
                "         AND t.t_party = -1 " +   
                "         AND t.t_dockind in(" + string(DLDOC_PAYMENT) + ", " + 
                                                 string(DL_ISSUE_UNION) + ", " + 
                                                 string(DL_MOVINGDOC)  + ") " +
                "         AND t.t_dealid > 0 " +   
                "         and t.t_kind in(20,210,40,110,130,90) " +
                "         and t.t_Amount != 0 "+
                "         AND t.t_state = ? " +    
                "         and t.t_instance = (SELECT MAX (t_instance) " +
                "                               FROM v_scwrthistex " +
                "                              WHERE t_sumid = t.t_sumid " +
                "                                AND t_changedate <= ?) "+
                "        and t.t_Portfolio in (?,?) "+
                "     ) > 0 ";

        m_SqlAvrRsd.addParam(PM_WRITEOFF_SUM_BUY);
        m_SqlAvrRsd.addParam(PM_WRTSUM_FORM);
        m_SqlAvrRsd.addParam(m_RepDate);
        m_SqlAvrRsd.addParam(KINDPORT_TRADE);
        m_SqlAvrRsd.addParam(KINDPORT_SALE);
     end;

     m_SqlAvr = m_SqlAvr + 
                " order by FIN.T_FACEVALUEFI, FIN.T_AVOIRKIND, FIN.T_FI_CODE ";

     NRec = int(m_SqlAvrRsd.GetCount(m_SqlAvr));

     m_AvrData = m_SqlAvrRsd.execute(m_SqlAvr);

     return NRec;
  END;

  PRIVATE MACRO ЭтоДепРасписка()
     return (FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_DEPOSITORY_RECEIPT, FIn.rec.AvoirKind ));
  END;

  PRIVATE MACRO ЭтоОблигация()
     return (FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND, FIn.rec.AvoirKind ));
  END;

  // Есть неопределенный купон (не заданы ставка и доход)
  PRIVATE MACRO СПлавающейСтавкой(FIID:integer)
     var Sql, SqlData;

     Sql = RSDCommand("select Count(1) cnt from DFIWARNTS_DBT where t_FIID = ? and t_IsPartial = chr(0) and t_IncomeRate = 0.0 and t_IncomeVolume = 0.0"); 
     Sql.addParam("", RSDBP_IN, FIID);
     Sql.execute();

     SqlData = TRsbDataSet(Sql);

     if( SqlData.MoveNext() and (SqlData.cnt > 0) )
        return true;
     end;

     return false;
  END;

  PRIVATE MACRO НайтиДатуОферты(FIID:integer, OfferDate:@Date)
     var Sql, SqlData;

     Sql = RSDCommand("SELECT max(t_DateRedemption) OfferDate " +
                      "  FROM DOFFERS_DBT " +
                      " WHERE t_DateRedemption <= (select max(t_DrawingDate) " +
                      "                              from DFIWARNTS_DBT " +
                      "                             where t_FIID = ? and t_IsPartial = CHR(0) and (t_IncomeRate != 0.0 or t_IncomeVolume != 0.0))"); 
     Sql.addParam("", RSDBP_IN, FIID);
     Sql.execute();

     SqlData = TRsbDataSet(Sql);

     if( (SqlData.MoveNext()) and (SQL_ConvTypeDate(SqlData.OfferDate) != ZeroDate) )
        OfferDate = SQL_ConvTypeDate(SqlData.OfferDate);
        return true;
     end;

     return false;
  END;

  PRIVATE MACRO НайтиДатуВыплДВД(FIID:integer,DDate:@Date)
     var Sql, SqlData;

     Sql = RSDCommand("select NVL(min(t_drawingdate),'01.01.0001') Ddate from DFIWARNTS_DBT where t_FIID = ? and t_drawingdate > ?"); 

     Sql.addParam("", RSDBP_IN, FIID);
     Sql.addParam("", RSDBP_IN, m_RepDate);

     Sql.execute();

     SqlData = TRsbDataSet(Sql);

     if( (SqlData.MoveNext()) and (SQL_ConvTypeDate(SqlData.Ddate) != ZeroDate) )
        DDate = SQL_ConvTypeDate(SqlData.Ddate);
        return true;
     end;

     return false;
  END;

  PRIVATE MACRO DrDateOnRepDate(IsPartial:bool,DDate:@Date)
     var select, fiwarnt;
     select = RSDCommand("select min(t_DrawingDate) as DrawingDate from dfiwarnts_dbt " +
                         " where t_fiid = ? " +
                         "   and t_IsPartial "+IIF(IsPartial,"=","!=")+"'X' " +
                         "   and t_DrawingDate > ?");

     select.addParam("", RSDBP_IN, FIn.rec.FIID);
     select.addParam("", RSDBP_IN, m_RepDate);
     select.execute();

     fiwarnt = TRsbDataSet(select);

     if( fiwarnt.MoveNext() )
        if( fiwarnt.DrawingDate != null )
           DDate = SQL_ConvTypeDate(fiwarnt.DrawingDate);
           return true;
        end;
     end;

     return false;
  END;

  private macro ЕстьЧП()
     var ListFiwarnts;
     var Slct = RSDCommand("select count(*) NRec " +
                           " from DFIWARNTS_DBT " +
                           " where t_ISPARTIAL = 'X' " +
                           " and   t_FIID = ?" );

     Slct.addParam("FIID", RSDBP_IN, FIn.rec.FIID);
     Slct.execute();

     ListFiwarnts = TRsbDataSet(Slct);
     if( ListFiwarnts.MoveNext() and (ListFiwarnts.NRec != null) )
        return (ListFiwarnts.NRec > 0);
     end;
     return false;
  end;

  PRIVATE MACRO ПроверитьВозможностьРасчЧП()
     record srcfin("fininstr.dbt");        
     var NumInList = "", ExcludeText = "Выпуск исключён из расчёта. ", NumT18 = 0;

     m_T18.Size = 0;

     if( GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(FIn, OBJTYPE_AVOIRISS), OBJGROUP_AVRRISKEXCLUDE, NULL, NULL, NumInList, m_RepDate) and 
         (NumInList == "1") 
       )
        m_T18.(m_T18.Size) = IIF(NumT18==0,ExcludeText,"")+" Задана категория \"Расчёт РР. Исключить из расчёта\".";
        NumT18 = NumT18 +1;
     end;

     if( ЭтоДепРасписка() )
        if( FIn.rec.ParentFI > ALLFININSTR )
           if( ПолучитьФинИн(FIn.rec.ParentFI, srcfin, null) == 0 )
              if( (srcfin.Avoirkind == AVOIRISSKIND_PREFERENCE_SHARE) and ( not НайтиДатуВыплДВД(srcfin.FIID,@m_RetireDate) ))
                 m_T18.(m_T18.Size) = IIF(NumT18==0,ExcludeText,"")+" Не заполнен график выплат дивидендов.";
                 NumT18 = NumT18 +1;
              elif(srcfin.Avoirkind == AVOIRISSKIND_EQUITY_SHARE)
                 m_RetireDate = MaxDate;
              end;
           end;
        else
           m_T18.(m_T18.Size) = IIF(NumT18==0,ExcludeText,"")+" Для депозитарной расписки не найден базовый выпуск.";
           NumT18 = NumT18 +1;
        end;
     elif( ЭтоОблигация() )
        if( ЕстьЧП() )
           if( not DrDateOnRepDate(true,@m_RetireDate) )
              m_T18.(m_T18.Size) = IIF(NumT18==0,ExcludeText,"")+" Не найдено ЧП с датой, большей даты расчёта рыночного риска.";
              NumT18 = NumT18 +1;
           end;
        elif( СПлавающейСтавкой(FIn.rec.FIID) and НайтиДатуОферты(FIn.rec.FIID, @m_RetireDate) )
           if( m_RetireDate < m_RepDate )
              m_T18.(m_T18.Size) = IIF(NumT18==0,ExcludeText,"")+" Дата оферты ц/б меньше даты расчёта рыночного риска.";
              NumT18 = NumT18 +1;
           end;
        elif( FIn.rec.DrawingDate < m_RepDate )
           m_T18.(m_T18.Size) = IIF(NumT18==0,ExcludeText,"")+" Дата погашения ц/б меньше даты расчёта рыночного риска.";
           NumT18 = NumT18 +1;
        else
           m_RetireDate = FIn.rec.DrawingDate;
        end;
     end;

     if( (FIn.rec.Avoirkind == AVOIRISSKIND_PREFERENCE_SHARE) and (not НайтиДатуВыплДВД(FIn.rec.FIID,@m_RetireDate) ) )
        m_T18.(m_T18.Size) = IIF(NumT18==0,ExcludeText,"")+" Не заполнен график выплат дивидендов.";
        NumT18 = NumT18 +1;
     elif(FIn.rec.Avoirkind == AVOIRISSKIND_EQUITY_SHARE)
        m_RetireDate = MaxDate;
     end;

     if( this.ВычислитьГруппуРиска() == 0 )
        m_T18.(m_T18.Size) = IIF(NumT18==0,ExcludeText,"")+" Для выпуска не задана группа риска.";
        NumT18 = NumT18 +1;
     end;

     if( this.КурсДлПозиций() == null )
        m_T18.(m_T18.Size) = IIF(NumT18==0,ExcludeText,"")+" Для выпуска не найдено значение курса вида \""+DL_GetRateTypeName(m_LongCource)+"\"";
        NumT18 = NumT18 +1;
     end;

     if( (m_LongCource != m_ShortCource) and (this.КурсКрПозиций() == null) )
        m_T18.(m_T18.Size) = IIF(NumT18==0,ExcludeText,"")+" Для выпуска не найдено значение курса вида \""+DL_GetRateTypeName(m_ShortCource)+"\"";
        NumT18 = NumT18 +1;
     end;

     if( NumT18 > 0 )
        return false;
     end;

     return true;
  END;


  PRIVATE MACRO RegTable()

     if( not m_IsDataExists )
        SetActiveSheet( "Позиции" );
        RegisterTable( "N1_MainTable", "#",
                       "N1_A1",       
                       "N1_A2",       
                       "N1_A3",         
                       "N1_A4",           
                       "N1_A5",           
                       "N1_A6",          
                       "N1_A7",          
                       "N1_A8",         
                       "N1_A9",         
                       "N1_A10",         
                       "N1_A11",         
                       "N1_A12",         
                       "N1_A13",         
                       "N1_A14",         
                       "N1_A15",         
                       "N1_A16");      

        m_IsDataExists = true;

     end;
  END;

  PRIVATE MACRO ПечатьИсклЦБ( PrintToLog:BOOL )

     if( PrintToLog )
        msgbox( "Выпуск \"" + FIn.rec.FI_CODE + "\"." + this.T18() );
     else
        RegTable();
        SetCurrentLineFont( 8, true, false, null, null, COLOR_WHITE );

        PrintTableLine("N1_A1",  "НЕТ",           null,       
                       "N1_A2",  FIn.rec.FI_CODE, null,       
                       "N1_A3",  FIn.rec.Name,    null,         
                       "N1_A4",  this.T4(),       null,           
                       "N1_A5",  this.T5(),       null,           
                       "N1_A6",  "",              null,          
                       "N1_A7",  "",              null,         
                       "N1_A8",  "",              null,        
                       "N1_A9",  "",              null,         
                       "N1_A10", "",              null,         
                       "N1_A11", "",              null,        
                       "N1_A12", "",              null,         
                       "N1_A13", "",              null,         
                       "N1_A14", "",              null,         
                       "N1_A15", "",              null,         
                       "N1_A16", this.T18(),      null);

     end;

  END;

  PRIVATE MACRO УчаствуетВРасчетеПР()
     return ( (FIn.rec.Avoirkind == AVOIRISSKIND_PREFERENCE_SHARE) OR 
              (FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND, FIn.rec.Avoirkind )) );
  END;

  PRIVATE MACRO СобратьДанныеДляРасчетаОПР(A_6, A11, Currency, NeedCalcPosition)
     var LongP = TArray();
     var ShortP = TArray();
     var ЦБвр = 0;
     ClearPosArray(LongP, INTERVALS_COUNT);
     ClearPosArray(ShortP, INTERVALS_COUNT);

     if (NeedCalcPosition)
        if( УчаствуетВРасчетеПР() and ( this.ВычислитьГруппуРиска() != ВЫСОКИЙ) )
           if(A11 > 0.0)
              LongP[GetT_7()] = A11; 
            else
              ShortP[GetT_7()] = A11;
            end;
        end;
     else
       LongP[GetT_7()] = 0; 
       ShortP[GetT_7()] = 0;
       if( this.ВычислитьГруппуРиска() == ВЫСОКИЙ)
          ЦБвр = A11*this.ПолучитьКоэфВзвешивания();
       end;
     end;  

      OPR_Collector.Add(LongP, ShortP, Currency, ЦБвр);
  END;


  PRIVATE MACRO ПечатьИтогЦБ()
     var A_7 = IIF(this.T16()>$0.,this.КурсДлПозиций(),this.КурсКрПозиций());
     var A_7_1 = max(2,IIF(this.T16()>$0.,this.ТочностьКурсДлПозиций()(),this.ТочностьКурсКрПозиций()));

     СобратьДанныеДляРасчетаОПР(this.T6(), this.T16_curr(), IIF(this.T16_curr()>=$0.,this.ВалютаКурсДлПозиций(),this.ВалютаКурсКрПозиций()), true);

     SetCurrentLineFont( 8, true, false, null, null, COLOR_WHITE );

     PrintTableLine("N1_A1",  this.ВидРиска(),   null,       
                    "N1_A2",  FIn.rec.FI_CODE, null,       
                    "N1_A3",  FIn.rec.Name,    null,         
                    "N1_A4",  this.T4(),       null,           
                    "N1_A5",  this.T5(),       null,           
                    "N1_A6",  this.T6(),       this.ИтоговаяТочность(),          
                    "N1_A7",  A_7,             A_7_1,         
                    "N1_A8",  this.T8(),       null,        
                    "N1_A9",  this.T9(),       null,         
                    "N1_A10", "",              null,         
                    "N1_A11", "",              null,        
                    "N1_A12", "",              null,         
                    "N1_A13", "",              null,         
                    "N1_A14", this.T16(),      null,
                    "N1_A15", this.T17(),      null,
                    "N1_A16", this.T18(),      null);
  END;

  PRIVATE MACRO ПечатьСтрокиЦБ(Avr_Data)
     var A_7 = IIF(Avr_Data.A11>$0.,this.КурсДлПозиций(),this.КурсКрПозиций());
     var A_7_1 = max(2,IIF(Avr_Data.A11>$0.,this.ТочностьКурсДлПозиций()(),this.ТочностьКурсКрПозиций()));

     PrintTableLine("N1_A1",  "",                                  null,       
                    "N1_A2",  FIn.rec.FI_CODE,                     null,       
                    "N1_A3",  FIn.rec.Name,                        null,         
                    "N1_A4",  this.T4(),                           null,           
                    "N1_A5",  this.T5(),                           null,           
                    "N1_A6",  this.A6(Avr_Data.A6,Avr_Data.A6_1),  Avr_Data.A6_1,          
                    "N1_A7",  A_7,                                 A_7_1,         
                    "N1_A8",  this.T8(),                           null,        
                    "N1_A9",  this.T9(),                           null,         
                    "N1_A10", Avr_Data.A10,                        null,         
                    "N1_A11", Avr_Data.A11,                        null,        
                    "N1_A12", Avr_Data.A13,                        null,         
                    "N1_A13", DL_getCCode(Avr_Data.A14),           null,         
                    "N1_A14", "",                                  null,
                    "N1_A15", this.T17(),                          null,
                    "N1_A16", this.T18(),                          null);
  END;

  PRIVATE MACRO КолвоВПортфелеНаДату(Porfolio:integer,OnDate:Date)
     wrtcalc.Clear();
     if(WRT_TotalCalc(
                    {OperDprt},
                    FIn.rec.FIID, 
                    {OurBank},
                    0,  
                    Porfolio, 
                    OnDate, time(0,0,0), 
                    date(0,0,0), 
                    wrtcalc,
                    true,
                    false 
                 )
       )
        return wrtcalc.rec.Amount;
     end;
     return 0;
  END;

  /*IsBuy == true - Отбор сделок для длинных позиций, иначе -  для коротких*/
  PRIVATE MACRO ОтобратьСделки(IsBuy:bool) 
     m_SqlTick = 
        " select TICK.T_DEALID, TICK.T_DEALDATE AS DEALDATE, leg.t_Principal Amount, TICK.t_PortfolioID Portfolio, 0 IsRepo, -1 IsForward, 0 UsePVOAcc "+
        "   from  ddl_tick_dbt tick, ddl_leg_dbt leg "+
        "  where TICK.T_DEALDATE <= ? ";

     if( IsBuy )
        m_SqlTick = m_SqlTick +                
        "    and ( ( RSB_SECUR.ISBUY(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1 and "+
        "            rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=0)  "+ 
        "        ) "; 

     else
        m_SqlTick = m_SqlTick +                 
        "    and RSB_SECUR.ISSALE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=1 "+ 
        "    and rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)))=0 ";
     end;

     m_SqlTick = m_SqlTick +                 
        "    and TICK.T_CLIENTID = -1 "+
        "    and TICK.T_DEALSTATUS > 0 "+
        "    and TICK.T_PFI = ? "+
        "    AND TICK.t_PortfolioID IN (?, ?) "+                                                      
        "    AND leg.t_LegKind = " + string(LEG_KIND_DL_TICK) +
        "    AND leg.t_DealID = tick.t_DealID "
        "    AND leg.t_LegID = 0 ";

     m_SqlTick = m_SqlTick +
        "    AND (select count(1) from v_rqhistex allrq "                               +
        "          where allrq.t_DocID = tick.t_dealid "                                +
        "            and allrq.t_DocKind = tick.t_BOfficeKind "                         +
        "            and allrq.t_Type = ? "                                             +
        "            and (allrq.t_FactDate = to_date('01.01.0001','DD.MM.YYYY') "       +
        "                 or allrq.t_FactDate > ?) "                                    +
        "            and allrq.t_Instance = (select MAX(h1.t_Instance) "                +
        "                                      from v_rqhistex h1 "                     +
        "                                     where h1.t_DocID    = allrq.t_DocID    "  +
        "                                       and h1.t_DocKind  = allrq.t_DocKind  "  +
        "                                       and h1.t_Type     = allrq.t_Type     "  +
        "                                       and h1.t_FIID     = allrq.t_FIID     "  +
        "                                       and h1.t_DealPart = allrq.t_DealPart "  +
        "                                       and h1.t_Num      = allrq.t_Num      "  +
        "                                       and h1.t_ChangeDate <= ? "              +
        "                                   )"                                          +
        "        ) > 0 ";/*проверяем, что нет исполненной поставки*/

    /*Открытые сделки открытые сделки ОРЕПО, бумаги из которых:
               1.1. переданы в ПРЕПО со сроком исполнения больше чем ОРЕПО,
               1.2. переданы в ПРЕПО, по которым выполнен отказ от 2-й части сделки в течение срока ОРЕПО,
               1.3. проданы в сделках продажи
               1.4. переданы в другое ОРЕПО 
               */
      if(not IsBuy)
        m_SqlTick = m_SqlTick +                 
        " union all " +
        "       SELECT LOT2OR.t_DealId as DEALID, LOT2OR.t_DealDate AS DEALDATE,  Lnk.t_Amount as Amount, LOT2OR.t_Portfolio,  1 IsRepo, -1 IsForward,  "+
        "         (CASE " +
        "            WHEN (Rsb_Secur.IsRepo (SaleOpr.oGrp) = 1 AND Rsb_Secur.IsSale (SaleOpr.oGrp) = 1 " +
        "              AND EXISTS " +
        "                     (SELECT 1 " +
        "                        FROM dpmwrtsum_dbt Lot2PR, ddlrq_dbt rq2PR " +
        "                       WHERE Lot2PR.t_DocID = rq2PR.t_ID " +
        "                         AND Lot2PR.t_Source = Buy.t_SumID   " +

        "                         AND Lot2OR.t_Date < DECODE (rq2PR.t_FactDate,TO_DATE ('01.01.0001', 'dd.mm.yyyy'), rq2PR.t_PlanDate, rq2PR.t_FactDate)  " +
        "                         AND (rq2PR.t_FactDate =TO_DATE ('01.01.0001', 'dd.mm.yyyy') OR  rq2PR.t_FactDate > ? )) " +  /*3. RepDate*/
        "            ) " +
        "            THEN   1 " +
        "            ELSE    0    " +
        "         END)   " +
        "           AS UsePVOAcc " +
        "         FROM dpmwrtlnk_dbt Lnk,       "+
        "              dpmwrtsum_dbt Buy,       "+
        "              dpmwrtsum_dbt Sale,      "+
        "              ddl_tick_dbt T,  "+
        "              (SELECT t_Kind_Operation,        "+
        "                      t_DocKind,       "+
        "                      rsb_secur.get_OperationGroup (t_SysTypes) oGrp   "+
        "                 FROM doprkoper_dbt) SaleOpr,  "+
        "              dpmwrtsum_dbt Lot2OR,    "+
        "        ddlrq_dbt RqSale  "+

        "        WHERE SaleOpr.t_Kind_Operation = T.t_DealType  "+
        "          AND SaleOpr.t_DocKind = T.t_BOfficeKind      "+
        "          AND Lot2OR.T_FIID =  ?       "+      /* 4 fiid*/
        "    AND Sale.t_DocID = RqSale.t_ID   "+
        "    AND Sale.t_DocKind = 29          "+
        "    AND RqSale.t_DocID = T.t_DealID  "+
        "          AND Lnk.t_CreateDate <= ? "+   /* 5 repDate количество и суммы берём с этой связи, так что лоты по истории можно не проверять        */
        "          AND Sale.t_SumID = Lnk.t_SaleID      "+
        "          AND Buy.t_SumID = Lnk.t_BuyID        "+
        "          AND Buy.t_Portfolio = ?      "+    /* 6 ПВО (6) и*/
        "          AND Lot2OR.t_Parent = Buy.t_SumID    "+
        "          AND NOT EXISTS       "+
        "                     (SELECT 1 "+
        "                        FROM dpmwrtsum_dbt Lot "+
        "                       WHERE Lot.t_Parent = Buy.t_SumID        "+
        "                         AND Lot.t_Date <= ?   "+  /*7. repDate*/
        "                         AND t_state = ?       "+    /*8. Исполнен - PM_WRTSUM_FORM*/
        "                     ) "+ /* и 2ч исполнена не ранее след даты после отчётной  или не исполнена */
        "       AND (   "+
        /* Бумаги переданы в ПРЕПО, дата исполнения 2ч ПРЕПО больше даты исполнения 2ч ОРЕПО. 1ч ПРЕПО исполнена, 2-я нет. Для этого случая используется счет Ц/б, ПВО. */
        "                   ( "+
        "                     Rsb_Secur.IsRepo (SaleOpr.oGrp) = 1  AND Rsb_Secur.IsSale (SaleOpr.oGrp) = 1      "+
        "                     AND EXISTS        "+
        "                            (SELECT 1  "+
        "                               FROM dpmwrtsum_dbt Lot2PR, ddlrq_dbt rq2PR      "+
        "                              WHERE Lot2PR.t_DocID = rq2PR.t_ID        "+
        "                                AND Lot2PR.t_Source = Buy.t_SumID      "+
        "                                AND Lot2OR.t_Date < (DECODE (rq2PR.t_FactDate,TO_DATE ('01.01.0001', 'dd.mm.yyyy'), rq2PR.t_PlanDate,rq2PR.t_FactDate))        "+
        "                                AND (rq2PR.t_FactDate = TO_DATE ('01.01.0001', 'dd.mm.yyyy') OR  rq2PR.t_FactDate > ? )        "+  /*10. RepDate */
        "                           )   "+
        "                   )   "+
        "                 OR    "+
/* Бумаги переданы в ПРЕПО, по которой зафиксирован отказ от 2ч сделки в течение срока ОРЕПО. Для этого случая используется счет <-ОД> ,который открыт по отбираемой сделке ОРЕПО (та сделка, из которой бумаги переданы).      */
        "                  ( "+
        "                     Rsb_Secur.IsRepo (SaleOpr.oGrp) = 1       "+
        "                     AND Rsb_Secur.IsSale (SaleOpr.oGrp) = 1   "+
        "                     AND EXISTS        "+
        "                            (SELECT 1  "+
        "                               FROM ddl_leg_dbt Leg2PR "+
        "                              WHERE Leg2PR.t_DealID = T.t_DealID AND Leg2PR.t_LegKind = 2      "+
        "                                AND Leg2PR.t_RejectDate !=TO_DATE ('01.01.0001', 'dd.mm.yyyy') "+
        "                                AND Lot2OR.t_Date > Leg2PR.t_RejectDate        "+
        "                                AND Leg2PR.t_RejectDate <= ?  )        "+ /*11. RepDate */
        "                 )     "+
        "               OR      "+
/* Бумаги проданы в сделках продажи. Т.е. сделка продажи исполнена. 2 часть ОРЕПО нет. Для этого случая используется счет <-ОД>, открытый по сделке ОРЕПО.      */
        "                ( "+
        "                 Rsb_Secur.IsRepo (SaleOpr.oGrp) = 0   "+
        "                 AND Rsb_Secur.IsSale (SaleOpr.oGrp) = 1       "+
        "                 AND (Lot2OR.t_date > ? "+ /* 12 RepDate       */
        "                       OR (Lot2OR.t_date <= ?  "+ /*13. RepDate */
        "                           AND Lot2OR.t_State NOT IN (?,? )))  "+ /* 14, 15 поставлен, аннулирован PM_WRTSUM_FORM, PM_WRTSUM_CANCEL*/  
        "               )       "+
        "             OR        "+
/*Бумаги проданы по 2 части другой сделки ОРЕПО (дата исполнения 2ч другой ОРЕПО должна быть меньше даты 2ч исходной ОРЕПО). Для этого случая так же используется счет <-ОД>.*/
        "              (        "+
        "                     Rsb_Secur.IsRepo (SaleOpr.oGrp) = 1       "+
        "                     AND Rsb_Secur.IsBuy (SaleOpr.oGrp) = 1    "+
        "                     AND T.t_DealID != Buy.t_DealID    "+
        "                     AND Sale.t_Date < Lot2OR.t_Date   "+
        "              )        "+
        "           )   ";

      end;

     /* сделки форвард не ПФИ с ценными бумагами указанного выпуска,
        по которым на дату расчета еще не выполнены поставка и оплата. */
        m_SqlTick = m_SqlTick +                 
        " union all " +
        "       SELECT DEAL.T_ID AS DEALID,  DEAL.T_DATE AS DEALDATE, NFI.T_AMOUNT,  -1 Portfolio,   0 IsRepo,   1 IsForward, 0 UsePVOAcc "+
        "         FROM ddvndeal_dbt Deal, ddvnfi_dbt nfi        "+
        "        WHERE Deal.T_DVKind =  "+ string(DV_FORWARD_T3) +
        "    AND nfi.T_DealID = Deal.T_ID "+
        "    AND NFI.T_FIID = ?    " +
        "          AND DEAL.T_DATE <= ? "; /*m_RepDate*/
        if( IsBuy )
           m_SqlTick = m_SqlTick +                 
           "       AND DEAL.T_TYPE = " + string(ALG_DV_BUY);/*только покупки*/   
        else
           m_SqlTick = m_SqlTick +                 
           "       AND DEAL.T_TYPE = " + string(ALG_DV_SALE);/*только продажи*/ 
        end;

        m_SqlTick = m_SqlTick +                 
        " AND RSB_Derivatives.DV_IsExistOperStep( DEAL.t_ID, ?, 'Ф', chr(88) , ?) >  0 "+
        " AND RSB_Derivatives.DV_IsExistOperStep( DEAL.t_ID, ?, 'i', chr(88) , ?) <= 0 "+
        " AND RSB_Derivatives.DV_IsExistOperStep( DEAL.t_ID, ?, 'Б', chr(88) , ?) <= 0 "+
        " AND RSB_Derivatives.DV_IsExistOperStep( DEAL.t_ID, ?, 'b', chr(88) , ?) <= 0 "+
        "          AND NOT EXISTS       "+
        "                 (SELECT TK.T_DEALID   "+
        "                    FROM ddl_tick_dbt tk       "+
        "                   WHERE TK.T_PARENTID = DEAL.T_ID)    ";


     m_SqlTick = m_SqlTick +                 
        "  order by DEALDATE";
            
     m_SqlTickRsd = RSDCommand(m_SqlTick); 
     
     m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);
     m_SqlTickRsd.addParam("", RSDBP_IN, FIn.rec.FIID);
     m_SqlTickRsd.addParam("", RSDBP_IN, KINDPORT_TRADE);
     m_SqlTickRsd.addParam("", RSDBP_IN, KINDPORT_SALE);

     m_SqlTickRsd.addParam("", RSDBP_IN, DLRQ_TYPE_DELIVERY);
     m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);
     m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);

     if(not IsBuy )
       m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);        
       m_SqlTickRsd.addParam("", RSDBP_IN, FIn.rec.FIID);
       m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);
       m_SqlTickRsd.addParam("", RSDBP_IN, KINDPORT_BACK);
       m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);
       m_SqlTickRsd.addParam("", RSDBP_IN, PM_WRTSUM_FORM);
       m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);
       m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);
       m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);
       m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);
       m_SqlTickRsd.addParam("", RSDBP_IN, PM_WRTSUM_FORM);
       m_SqlTickRsd.addParam("", RSDBP_IN, PM_WRTSUM_CANCEL);
     end;

     m_SqlTickRsd.addParam("", RSDBP_IN, FIn.rec.FIID);
     m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);

     m_SqlTickRsd.addParam("", RSDBP_IN, DL_DVDEALT3);
     m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);
     m_SqlTickRsd.addParam("", RSDBP_IN, DL_DVDEALT3);
     m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);
     m_SqlTickRsd.addParam("", RSDBP_IN, DL_DVDEALT3);
     m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);
     m_SqlTickRsd.addParam("", RSDBP_IN, DL_DVDEALT3);
     m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);


     m_SqlTickRsd.execute();

     m_TickData = TRsbDataSet(m_SqlTickRsd);
  END;

  PRIVATE MACRO ОтобратьПортфели() 
     m_SqlTick = 
                " SELECT t.* " +
                "   FROM v_scwrthistex t " +   
                "  WHERE t.t_buy_sale = ? " +  
                "    AND t.t_party = -1 " +   
                "    AND t.t_fiid = ? " +   
                "    AND t.t_dockind in(" + string(DLDOC_PAYMENT) + ", " + 
                                            string(DL_ISSUE_UNION) + ", " + 
                                            string(DL_MOVINGDOC)  + ") " +
                "    AND t.t_dealid > 0 " +   
                "    and t.t_kind in(20,210,40,110,130,90) " +
                "    and t.t_Amount != 0 "+
                "    AND t.t_state = ? " +    
                "    and t.t_instance = (SELECT MAX (t_instance) " +
                "                          FROM v_scwrthistex " +
                "                         WHERE t_sumid = t.t_sumid " +
                "                           AND t_changedate <= ?) "+
                "   and t.t_Portfolio in (?,?) "+
                " order by t.t_Date, t.t_Portfolio";/*по дате поставки(приобретения)*/

     m_SqlTickRsd = RSDCommand(m_SqlTick); 

     m_SqlTickRsd.addParam("", RSDBP_IN, PM_WRITEOFF_SUM_BUY);
     m_SqlTickRsd.addParam("", RSDBP_IN, FIn.rec.FIID);
     m_SqlTickRsd.addParam("", RSDBP_IN, PM_WRTSUM_FORM);
     m_SqlTickRsd.addParam("", RSDBP_IN, m_RepDate);
     m_SqlTickRsd.addParam("", RSDBP_IN, KINDPORT_TRADE);
     m_SqlTickRsd.addParam("", RSDBP_IN, KINDPORT_SALE);

     m_SqlTickRsd.execute();

     m_TickData = TRsbDataSet(m_SqlTickRsd);
  END;

  /*Если все значение <A15> для данного выпуска одинаковы - выводится значение <A5>, 
    Иначе - выводится "1/1,5"
    т.е. сейчас считаем, что будут только два значения 1 и\или 1,5*/
  PRIVATE MACRO Определить_Т15(A15)
     var A15str = IIF(A15==1.5,"1,5","1");

     if( index(T_15,A15str) == 0 )
        if( (strlen(T_15)>0) and (A15str=="1") )
           T_15 = A15str +"/"+T_15;
        else
           T_15 = T_15 +IIF(strlen(T_15)>0,"/","") + A15str;
        end;
     elif( (strlen(T_15) != strlen(A15str)) and (T_15 == "1,5") )
        T_15 = A15str +"/"+T_15;
     end;
  END;

  PRIVATE MACRO УчаствуетВРасчетеФР()
     return ( (FIn.rec.Avoirkind == AVOIRISSKIND_EQUITY_SHARE) OR ЭтоДепРасписка() );
  END;

  PRIVATE MACRO СобратьИтоги(A_6,A12, IsLongPosition, A12Curr)
     if( УчаствуетВРасчетеПР() and ( this.ВычислитьГруппуРиска() != ВЫСОКИЙ) )
        if(IsLongPosition)
           TLongP[GetT_7()]  = TLongP[GetT_7()] + A12; 
         else
           TShortP[GetT_7()] = TShortP[GetT_7()] + A12; 
         end;
      end;

     if( УчаствуетВРасчетеФР() and ( this.ВычислитьГруппуРиска() != ВЫСОКИЙ) )
        m_Pos_FR_Sum = m_Pos_FR_Sum + abs(A12);
     end;

     T_16 = T_16 + A12; /*Участвует в расчете ОФР с учетом конвертации пересчитанных в рубли на дату расчета рыночного риска*/
     T_16_Currency = T_16_Currency + A12Curr;
     T_6 = T_6 + A_6;

     if( this.ВычислитьГруппуРиска() == ВЫСОКИЙ)
        ЦБвр = ЦБвр + abs(A12)*this.ПолучитьКоэфВзвешивания();
     end;

  END;

  PRIVATE MACRO ОпределитьТочностьКолва(PosAmount)

     var AmountSumPrec = DL_MeanSignAfterPoint(PosAmount,12);

     if( AmountSumPrec > m_AmountSumPrecItog )
        m_AmountSumPrecItog = AmountSumPrec;
     end;

     return AmountSumPrec;
  END;

  MACRO ЦБ_ИсключенаИзРасчета()
     return m_ExcludeFromCalc;
  END;

  MACRO НетСделокИПортфелей()
     return m_NoPortAndDeals;
  END;

  PRIVATE MACRO ОтобратьПозицииПоПортфелям()
     var FD = null, AmountSumPrec = 0;
     var A11 = $0, A12 = $0, A12_Curr = 0, factor = 1;
     var PortAccount = "",  PrevPort = -1, PrevAmount = 0., PrevDate = ZeroDate, IsFirstUse = true;

     macro PutPositionData()

        AmountSumPrec = ОпределитьТочностьКолва(PrevAmount) ; 

        A11 = PrevAmount*this.КурсДлПозиций();
        A12_Curr = A11;
        /*Собираем данные в разрезе валют (по сделкам)*/
        СобратьДанныеДляРасчетаОПР(PrevAmount, A11, this.ВалютаКурсДлПозиций(), false);

        if(A11 != 0.0) 
          if( not SmartConvertSumDbl( A11, A11, m_RepDate, this.ВалютаКурсДлПозиций(), NATCUR, false) )
             m_T18.(m_T18.Size) = "Не могу сконвертировать сумму из "+DL_getCCode(this.ВалютаКурсДлПозиций())+" в "+DL_getCCode(NATCUR)+".";
          end;
        end;

        A11 = round(A11, 2 );

        A12 = A11 * factor;

        AvrData.AddLine(PrevAmount,AmountSumPrec,factor,PortAccount,A11,A12,SQL_ConvTypeDate(m_TickData.Date),this.ВалютаКурсДлПозиций(), true);
        
        СобратьИтоги(PrevAmount,A12, true, A12_Curr);
     end;

     ОтобратьПортфели();

     while( m_TickData.MoveNext() )

        /*печатаем информацию об исключении только по тем выпускам, по которым не пусты портфели или есть срочные сделки*/
        if( ЦБ_ИсключенаИзРасчета() )
           ПечатьИсклЦБ( false );
           return false;
        end;

        if( PrevPort != m_TickData.Portfolio )
          if( not IsFirstUse )
             PutPositionData();
          end;
          PrevPort = m_TickData.Portfolio;
          PortAccount = "";
          PrevAmount = 0.;
          PrevDate = ZeroDate;
        end;

        if( PortAccount == "")
           FD = SPFirstDoc(LEG_KIND_DL_TICK,int(m_TickData.DealID));
           AccPort = ЛСчетНаКоторомУчитываетсяЛот( m_TickData, m_RepDate, FD );
           PortAccount = AccPort.Account;
        end;

        PrevAmount = PrevAmount + m_TickData.Amount;

        PrevDate = SQL_ConvTypeDate(m_TickData.Date);
        IsFirstUse = false;
        m_NoPortAndDeals = false;
     end;

     if( not IsFirstUse )
        PutPositionData();
     end;

     return true;
  END;

  PRIVATE MACRO РасчетДлП()
     var FD = null, PayExec = false, PaymStatus = -1;
     var A11 = $0, A12 = $0, A12_Curr = $0, factor = 1, AmountSumPrec = 0;
     var СчетТреб = TRecHandler("account");
     var DebAcc:string  = "";  var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
     var CredAcc:string = "";  var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;


     if( not ОтобратьПозицииПоПортфелям() )
        return false;
     end;
     /*отбираем непоставленные на дату расчета сделки (обычные покупки в ТП и ППР и ПРЕПО 2ч (БПП))*/
     ОтобратьСделки(true);

     while( m_TickData.MoveNext() )

       Acc.Clear();
       Account = "";

       if(m_TickData.IsForward > 0)
          FD = DVFirstDocNDeal(DL_DVDEALT3,int( m_TickData.DealID) );

          /*печатаем информацию об исключении только по тем выпускам, по которым не пусты портфели или есть срочные сделки*/
          if( ЦБ_ИсключенаИзРасчета() )
             ПечатьИсклЦБ( false );
             return false;
          end;

          DebAcc  = "";  DebFIID  = ALLFININSTR;  DebFiRole  = FIROLE_UNDEF;
          CredAcc = "";  CredFIID = ALLFININSTR;  CredFiRole = FIROLE_UNDEF;
          СчетТреб.Clear();

          ПолучитьСрочныеСчетаВнб( FD, @CredAcc, @CredFIID, @CredFiRole, @DebAcc, @DebFIID, @DebFiRole );

          FD.IsExistAccount( CredAcc, null, false, CredFiRole, СчетТреб, null, m_RepDate, CredFIID );

          Account = СчетТреб.rec.Account;

          if( Account  == "" )
             msgbox("Не найден счет требований по сделке с кодом "+ FD.Deal.rec.Code) ;
          end;

       else

          /*берем 1 часть для обычных сделок и 2 ч для ПРЕПО*/
          FD = SPFirstDoc(IIF(m_TickData.IsRepo,LEG_KIND_DL_TICK_BACK,LEG_KIND_DL_TICK),int(m_TickData.DealID));

          /*печатаем информацию об исключении только по тем выпускам, по которым не пусты портфели или есть срочные сделки*/
          if( ЦБ_ИсключенаИзРасчета() )
             ПечатьИсклЦБ( false );
             return false;
          end;

          /*проверим срочность(сразу в селекте сложно сделать) */
          if( FD.ВидСрочностиСделки() != СделкаНеРанееТретьегоДня )  
             continue;
          end;

          /*наличие оплаты*/
          PayExec = false;
          PaymStatus = DL_GetRQStateOnDate(FD.pm_money.dlreq.rec.ID, m_RepDate );
          if( PaymStatus == DLRQ_STATE_EXEC )            /*ТО исполнено*/
             PayExec = true;
          end;

          if( m_TickData.IsRepo )    
             if(ВедениеСчетовБПППоВыпуску() == true)
               FD.IsExistAccount( "Наш портфель ц/б", null, true, GetFIRoleByPortfolio( m_TickData.Portfolio, null, null, true ), Acc, null, m_RepDate );
             else
               FD.IsExistAccount( "Ц/б, БПП", null, true, GetFIRoleByPortfolio( m_TickData.Portfolio ), Acc, null, m_RepDate );
             end;
          else
             if( not PayExec )
                /*не было оплаты, значит берем внебалансовый счет требований*/
                FD.IsExistAccount( "+Форвард, дрейф", null, true, FIROLE_FIREQ, Acc, null, m_RepDate );
             else
                /*была оплата, определяется счет балансовых требований (47408)*/
                FD.IsExistAccount( "+Форвард, расчеты", null, true, null, Acc, null, m_RepDate );
             end;
          end;

          Account = Acc.rec.Account;

          if( Account  == "" )
             msgbox("Не найден счет требований по сделке с кодом "+ FD.tick.rec.DealCode);
          end;

       end; /*if(m_TickData.IsForward > 0)*/

       AmountSumPrec = ОпределитьТочностьКолва(m_TickData.Amount ); 

       A11 = m_TickData.Amount * this.КурсДлПозиций();
       A12_Curr = A11;
       /*Собираем данные в разрезе валют*/
       СобратьДанныеДляРасчетаОПР(m_TickData.Amount, A11, this.ВалютаКурсДлПозиций(), false);

       if(A11 != $0.0) 
         if( not SmartConvertSumDbl( A11, A11, m_RepDate, this.ВалютаКурсДлПозиций(), NATCUR, false) )
            m_T18.(m_T18.Size) = "Не могу сконвертировать сумму из "+DL_getCCode(this.ВалютаКурсДлПозиций())+" в "+DL_getCCode(NATCUR)+".";
         end;
       end;

       A11 = round( A11, 2);

       A12 = A11 * factor;

       AvrData.AddLine(m_TickData.Amount,AmountSumPrec,factor, Account, A11,A12,SQL_ConvTypeDate(m_TickData.DealDate),this.ВалютаКурсДлПозиций(), false);

       СобратьИтоги(m_TickData.Amount,A12, true, A12_Curr);

       m_NoPortAndDeals = false;

     end;  /* while( m_TickData.MoveNext() )*/

     return true;
  END;

  PRIVATE MACRO РасчетКрП()
     var FD = null, PayExec = false, PaymStatus = -1, AmountSumPrec = 0;
     var A11 = $0, A12 = $0, A12_Curr = $0, factor = 1;

     var СчетОбяз = TRecHandler("account");
     var DebAcc:string  = "";  var DebFIID:integer  = ALLFININSTR;  var DebFiRole  = FIROLE_UNDEF;
     var CredAcc:string = "";  var CredFIID:integer = ALLFININSTR;  var CredFiRole = FIROLE_UNDEF;

     ОтобратьСделки(false);

     while( m_TickData.MoveNext() )

       Acc.Clear();
       Account = "";

       if(m_TickData.IsForward > 0)
           FD = DVFirstDocNDeal(DL_DVDEALT3, int(m_TickData.DealID));

           /*печатаем информацию об исключении только по тем выпускам, по которым не пусты портфели или есть срочные сделки*/
           if( ЦБ_ИсключенаИзРасчета() )
              ПечатьИсклЦБ( false );
              return false;
           end;
 
           DebAcc  = "";  DebFIID  = ALLFININSTR;  DebFiRole  = FIROLE_UNDEF;
           CredAcc = "";  CredFIID = ALLFININSTR;  CredFiRole = FIROLE_UNDEF;
           СчетОбяз.Clear();

           ПолучитьСрочныеСчетаВнб( FD, @CredAcc, @CredFIID, @CredFiRole, @DebAcc, @DebFIID, @DebFiRole );

           FD.IsExistAccount( DebAcc,  null, false, DebFiRole,  СчетОбяз, null, m_RepDate, DebFIID );
 
           Account = СчетОбяз.rec.Account;   

          if( Account  == "" )
             msgbox("Не найден счет обязательств по сделке с кодом "+ FD.Deal.rec.Code);
          end;
       else
           FD = SPFirstDoc(LEG_KIND_DL_TICK, int(m_TickData.DealID));

           FD.SetCurPFI(FIn.rec.FIID);

           /*печатаем информацию об исключении только по тем выпускам, по которым не пусты портфели или есть срочные сделки*/
           if( ЦБ_ИсключенаИзРасчета() )
              ПечатьИсклЦБ( false );
              return false;
           end;
           if( not m_TickData.IsRepo)
             /*проверим срочность(сразу в селекте сложно сделать) */
             if( FD.ВидСрочностиСделки() != СделкаНеРанееТретьегоДня )
                continue;
             end;
           end;

           /*наличие оплаты*/
           PayExec = false;
           PaymStatus = DL_GetRQStateOnDate(FD.pm_money.dlreq.rec.ID, m_RepDate );
           if( PaymStatus == DLRQ_STATE_EXEC )            /*ТО исполнено*/
              PayExec = true;
           end;
          /*Для сделок Обратного РЕПО - номер счета обязательств по обратной поставке бумаг:
            - КУ "-ОД" ("-ОД, Корзина"), если бумаги из ОРЕПО проданы в сделке продажи.
            - КУ "Ц/б, ПВО" ("Ц/б ПВО, Корзина"), если бумаги из ОРЕПО были переданы в сделки ПРЕПО,
           срок исполнения которых больше срока исполнения сделки ОРЕПО 
          или по сделкам ПРЕПО был оформлен отказ от 2 части сделки в течении срока ОРЕПО
          */
          if( m_TickData.IsRepo ) 
             if(m_TickData.UsePVOAcc)
               FD.IsExistAccount( IIF(IsBasket(FD.Group), "Ц/б, Корзина ПВО", "Ц/б, ПВО"), null, true, GetFIRoleByPortfolio( m_TickData.Portfolio ), Acc, null, m_RepDate );
             else
               FD.IsExistAccount( IIF(IsBasket(FD.Group), "-ОД, Корзина", "-ОД"), null, true, GetFIRoleByPortfolio( m_TickData.Portfolio ), Acc, null, m_RepDate );
             end;
          else  
            if( not PayExec )
               /*не было оплаты, значит берем внебалансовый счет обязательств*/
               FD.IsExistAccount( "-Форвард, дрейф", null, true, FIROLE_FICOM, Acc, null, m_RepDate );
            else
               /*была оплата, определяется счет балансовых обязательств (47407)*/
               FD.IsExistAccount( "-Форвард, расчеты", null, true, null, Acc, null, m_RepDate );
            end;
          end;

           Account = Acc.rec.Account;

           if( Account  == "" )
             msgbox("Не найден счет обязательств по сделке с кодом "+ FD.tick.rec.DealCode);
           end;
       end; /* if(m_TickData.IsForward > 0)*/

       AmountSumPrec = ОпределитьТочностьКолва(m_TickData.Amount ); 

       A11 = m_TickData.Amount * this.КурсКрПозиций();

       A11 = -1 * A11;
       m_TickData.Amount = -m_TickData.Amount;
       A12_Curr = A11;

       /*Собираем данные в разрезе валют*/
       СобратьДанныеДляРасчетаОПР(m_TickData.Amount, A11, this.ВалютаКурсКрПозиций(), false);

       if(A11 != $0.0) 
         if( not SmartConvertSumDbl( A11, A11, m_RepDate, this.ВалютаКурсКрПозиций(), NATCUR, false) )
            m_T18.(m_T18.Size) = "Не могу сконвертировать сумму из "+DL_getCCode(this.ВалютаКурсКрПозиций())+" в "+DL_getCCode(NATCUR)+".";
         end;
       end;

       A11 = round( A11, 2);

       A12 = A11 * factor;

       AvrData.AddLine(m_TickData.Amount,AmountSumPrec,factor,Account,A11,A12,SQL_ConvTypeDate(m_TickData.DealDate),this.ВалютаКурсКрПозиций(), false);

       СобратьИтоги(m_TickData.Amount,A12, false, A12_Curr);

       m_NoPortAndDeals = false;

     end; /*while( m_TickData.MoveNext() )*/

     return true;
  END;

  PRIVATE MACRO ДействующийКупонНаДату(СтавкаКупона:@money)
    var cmd, rsd, cmd1, rsd1;
    var IsExists = false;

    СтавкаКупона = $0;

    cmd = RSDCommand(
          " SELECT warnt.t_DrawingDate as DrawingDate, "
        + "        warnt.t_IncomeRate, warnt.t_IncomeScale, warnt.t_RelativeIncome "
        + "   FROM dfiwarnts_dbt warnt "
        + "  WHERE warnt.t_FIID = ? and "
        + "        warnt.t_IsPartial !=  'X' and "
        + "        warnt.t_DrawingDate >= ? and "
        + "        warnt.t_FirstDate <= ? "
        + " ORDER BY warnt.t_DrawingDate ASC, warnt.t_ID "
                    );
  
    cmd.addParam( "", RSDBP_IN, FIn.rec.FIID );
    cmd.addParam( "", RSDBP_IN, m_RepDate );
    cmd.addParam( "", RSDBP_IN, m_RepDate );
  
    cmd.execute();
  
    rsd = TRsbDataSet(cmd);
    if(rsd.MoveNext())
       IsExists = true;
       if( rsd.RelativeIncome == "X" )
          СтавкаКупона = rsd.IncomeRate;
       else
          СуммаКупона(FIn.rec.FIID, 1, SQL_ConvTypeDate(rsd.DrawingDate) );/*ф-ю СуммаКупона здесь вызываем специально для того, чтобы она нам посчитала ставку по купону (ставка не хранится в купонах, если доход задан в абс. величине)*/
          cmd1 = RSDCommand("select RSI_RSB_FIInstr.FI_ReturnIncomeRate() IncomeRate from dual");
          cmd1.execute();
          rsd1 = TRsbDataSet(cmd1);
          if(rsd1.MoveNext())
             СтавкаКупона = rsd1.IncomeRate;
          end;
       end;
    end;
  
    return IsExists;
  END;

  PRIVATE MACRO ДействующийЧПНаДату(СтавкаЧП:@double)
    var cmd, rsd, cmd1, rsd1;
    var IsExists = false;
    var RelativeIncome, IncomeVolume;

    СтавкаЧП = 0.;

    cmd = RSDCommand(
          " SELECT warnt.t_ID, warnt.t_IncomeRate, warnt.t_IncomeVolume, warnt.t_IncomeScale, warnt.t_RelativeIncome "
        + "   FROM dfiwarnts_dbt warnt "
        + "  WHERE warnt.t_FIID = ? and "
        + "        warnt.t_IsPartial =  'X' and "
        + "        warnt.t_DrawingDate >= ? and "
        + "        warnt.t_FirstDate <= ? and "
        + "        rownum = 1  "
        + " ORDER BY warnt.t_DrawingDate ASC, warnt.t_ID "
                    );
  
    cmd.addParam( "", RSDBP_IN, FIn.rec.FIID );
    cmd.addParam( "", RSDBP_IN, m_RepDate );
    cmd.addParam( "", RSDBP_IN, m_RepDate );

    cmd.execute();

    rsd = TRsbDataSet(cmd);
    if(rsd.MoveNext())
       IsExists = true;

       RelativeIncome = ( SQL_ConvTypeStr( rsd.RelativeIncome ) == "X" );
       IncomeVolume = SQL_ConvTypeSum( rsd.IncomeVolume );
       СтавкаЧП = SQL_ConvTypeSum( rsd.IncomeRate );

       if( RelativeIncome )
          СтавкаЧП = rsd.IncomeRate;
       else
          FI_PartialPumpIncomeRateVolume( FIn.rec.FIID, FIn.rec.FaceValue, int(rsd.ID), IncomeVolume, СтавкаЧП );
       end;
    end;
  
    return IsExists;
  END;

  PRIVATE MACRO ОпределитьВременнойИнтервал_ПрочиеФИ()
     var Rborder = ZeroDate, LBorder = ZeroDate, Day, Year, Mon;

     if( m_RetireDate == MaxDate )
        T_7 = 13;
        return;
     end;

     LBorder = m_RepDate;
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 1, MONTH_CALENDAR); 
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 1;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 1, MONTH_CALENDAR);
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 3, MONTH_CALENDAR);
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 2;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 3, MONTH_CALENDAR);
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 6, MONTH_CALENDAR);
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 3;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 6, MONTH_CALENDAR);
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 12, MONTH_CALENDAR); 
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 4;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 1*12, MONTH_CALENDAR); 
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 2*12, MONTH_CALENDAR); 
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 5;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 2*12, MONTH_CALENDAR); 
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 3*12, MONTH_CALENDAR); 
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 6;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 3*12, MONTH_CALENDAR); 
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 4*12, MONTH_CALENDAR);
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 7;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 4*12, MONTH_CALENDAR); 
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 5*12, MONTH_CALENDAR);
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 8;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 5*12, MONTH_CALENDAR); 
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 7*12, MONTH_CALENDAR);
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 9;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 7*12, MONTH_CALENDAR); 
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 10*12, MONTH_CALENDAR);
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 10;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 10*12, MONTH_CALENDAR); 
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 15*12, MONTH_CALENDAR);
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 11;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 15*12, MONTH_CALENDAR); 
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 20*12, MONTH_CALENDAR);
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 12;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 20*12, MONTH_CALENDAR); 
     if( m_RetireDate > LBorder )
        T_7 = 13;
        return;
     end;   

  END;

  PRIVATE MACRO КолвоДнейДоПогашенияВГодах()
     var cmd = RsdCommand(RslDefCon, "begin\n ? := Rsb_Secur.SC_Years(?, ?, ?);\n end; ");

     cmd.addParam("ret_val",  RSDBP_RETVAL, V_DOUBLE );
     cmd.addParam("start_dt", RSDBP_IN,     m_RepDate );
     cmd.addParam("end_dt",   RSDBP_IN,     m_RetireDate );
     cmd.addParam("basis",    RSDBP_IN,     4/*cnst.BASIS_ACTACT*/ );/*в месяце и в году по календарю*/

     cmd.execute();

     return SQL_ConvTypeSum(cmd.Value(0));
  END;

  PRIVATE MACRO ОпределитьВременнойИнтервал_СтавкаМенее3()
     var Rborder = ZeroDate, LBorder = ZeroDate, Day, Year, Mon, КолвоДнейВГодах = 0.0;

     if( m_RetireDate == MaxDate )
        T_7 = 28;
        return;
     end;

     LBorder = m_RepDate;
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 1, MONTH_CALENDAR); 
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 14;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 1, MONTH_CALENDAR);
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 3, MONTH_CALENDAR);
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 15;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 3, MONTH_CALENDAR);
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 6, MONTH_CALENDAR);
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 16;
        return;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 6, MONTH_CALENDAR);
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 12, MONTH_CALENDAR); 
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        T_7 = 17;
        return;
     end;

     КолвоДнейВГодах = КолвоДнейДоПогашенияВГодах();

     if( (КолвоДнейВГодах > 1) and (КолвоДнейВГодах <= 1.9) )
        T_7 = 18;
        return;
     end;

     if( (КолвоДнейВГодах > 1.9) and (КолвоДнейВГодах <= 2.8) )
        T_7 = 19;
        return;
     end;

     if( (КолвоДнейВГодах > 2.8) and (КолвоДнейВГодах <= 3.6) )
        T_7 = 20;
        return;
     end;

     if( (КолвоДнейВГодах > 3.6) and (КолвоДнейВГодах <= 4.3) )
        T_7 = 21;
        return;
     end;

     if( (КолвоДнейВГодах > 4.3) and (КолвоДнейВГодах <= 5.7) )
        T_7 = 22;
        return;
     end;

     if( (КолвоДнейВГодах > 5.7) and (КолвоДнейВГодах <= 7.3) )
        T_7 = 23;
        return;
     end;

     if( (КолвоДнейВГодах > 7.3) and (КолвоДнейВГодах <= 9.3) )
        T_7 = 24;
        return;
     end;

     if( (КолвоДнейВГодах > 9.3) and (КолвоДнейВГодах <= 10.6) )
        T_7 = 25;
        return;
     end;

     if( (КолвоДнейВГодах > 10.6) and (КолвоДнейВГодах <= 12) )
        T_7 = 26;
        return;
     end;

     if( (КолвоДнейВГодах > 12) and (КолвоДнейВГодах <= 20) )
        T_7 = 27;
        return;
     end;

     if( КолвоДнейВГодах > 20 )
        T_7 = 28;
        return;
     end;   

  END;

  PRIVATE MACRO СтавкаМенее3()
     var СтавкаКупона = $0;
     if( ЭтоОблигация() and
         ( ((Avr.rec.IncomeRate > $0) and (Avr.rec.IncomeRate < $3)) OR
           ( (Avr.rec.IncomeRate == $0) and
             ((not ДействующийКупонНаДату(@СтавкаКупона)) or ((СтавкаКупона > $0) and (СтавкаКупона < $3))) 
           )
         )
       )
         return true;
     end;
     return false;
  END;

  PRIVATE MACRO ОпределитьВременнойИнтервал()
     if( СтавкаМенее3() )
        ОпределитьВременнойИнтервал_СтавкаМенее3();
     else
        ОпределитьВременнойИнтервал_ПрочиеФИ();
     end;
  END;

  PRIVATE MACRO РассчитатьСФР()
     var i = 0, СФР1 = $0.;

     if( УчаствуетВРасчетеФР() )
        if( AvrData.Size > 0)
           while( AvrData.Size > i )
              СФР1 = СФР1 + AvrData[i].A12 * RISK_COEFFICIENT;
              i = i + 1;
           end;
        end;
        СФР = СФР +abs(СФР1);
     end;
  END;

  PRIVATE MACRO РассчитатьСПР()
     var СтавкаЧП = 0.;

     if( УчаствуетВРасчетеПР() )
        if( ( (this.ВычислитьГруппуРиска() == НИЗКИЙ_ДО_6_МЕС) or 
              (this.ВычислитьГруппуРиска() == НИЗКИЙ_6_24_МЕС) or
              (this.ВычислитьГруппуРиска() == НИЗКИЙ_ОТ_24_МЕС) ) and
            ДействующийЧПНаДату(@СтавкаЧП)
          )
           СПР = СПР + abs(T_16) * СтавкаЧП/100 * this.ГруппаРискаЧП()
        else
           СПР = СПР + abs(T_16)*this.ГруппаРиска();
        end;
     end;
  END;

  PRIVATE MACRO РассчитатьЧП(Num:integer)
     var i = 0, factor = 1;
     var Progress = TProgressBar;                                                                                                    
 
     Progress.Init( Num, "", "Просмотр выпусков и формирование позиций для расчета РР" );                                                                                 

     OPR_Collector.ClearArray();

     while( m_AvrData.MoveNext() )

        FIn.Clear();
        if( ПолучитьФинИн(m_AvrData.FIID, FIn, Avr) )
        end;

        AvrData.ClearArray;
        ClearT();

        if( not ПроверитьВозможностьРасчЧП() )
           m_ExcludeFromCalc = true;
        end;
           
        if( not ЦБ_ИсключенаИзРасчета() )
           ОпределитьВременнойИнтервал();/*определяем для всех ц\б независимо от ФР и ПР*/
        end;

        if( not РасчетДлП() )
           Progress.Use;                                                                                                                    
           continue;
        end;

        if( not РасчетКрП() )
           Progress.Use;                                                                                                                    
           continue;
        end;

        if( ЦБ_ИсключенаИзРасчета() OR НетСделокИПортфелей() )
           /*мы пришли сюда, значит:
             пусты портфели и нет срочных сделок или
             ц\б исключена из расчета и пусты портфели и нет срочных сделок, 
             идем дальше (ничего не пачатая о данной ц\б)*/
           Progress.Use;                                                                                                                    
           continue;
        end;

        RegTable();
        РассчитатьСФР();
        РассчитатьСПР();

        if( УчаствуетВРасчетеФР() )
           ОФР = ОФР + T_16*RISK_COEFFICIENT;  
        end;

        ПечатьИтогЦБ();

        if( AvrData.Size > 0)
           i = 0;
           while( AvrData.Size > i )
              ПечатьСтрокиЦБ(AvrData[i]);
              i = i + 1;
           end;
        end;

        Progress.Use;                                                                                                                    
     end;
     Progress.Remove;                                                                                                                                                                                                                                                               
  END;

  /*сначала ищем курс типа Type в рублях; если не нашли ищем первый попавшийся (основной) в валюте номинала*/
  PRIVATE MACRO ПоискКурса(Type:integer,FiidTo:@integer,SumTo:@double,err:@string)
     var AvrTSS = 0.;

     FiidTo = -1; 
     SumTo  = AvrTSS;
     err    = "";

     if( not SmartConvertSumDbl( AvrTSS, 1., m_RepDate, FIn.rec.FIID, NATCUR, false, Type) )
       /*в рублях не нашли - ищем подходящий в валюте номинала*/
        if( FIn.rec.FaceValueFI != NATCUR )
           if( not SmartConvertSumDbl( AvrTSS, 1., m_RepDate, FIn.rec.FIID, FIn.rec.FaceValueFI, false, Type ) ) 
              err = "Не могу получить курс вида \""+DL_GetRateTypeName(Type)+"\" ц/б с кодом "+string(FIn.rec.FI_CODE)+".";
              return false;
           else
              FiidTo = FIn.rec.FaceValueFI; 
              SumTo  = AvrTSS;
              return true;
           end;
        else
           err = "Не могу получить курс вида \""+DL_GetRateTypeName(Type)+"\" ц/б с кодом "+string(FIn.rec.FI_CODE)+".";
           return false;
        end;
     end;

     FiidTo = NATCUR; 
     SumTo  = AvrTSS;

     return true;
  END;                                   

  PRIVATE MACRO ПолучитьФормулуОПРитог()
    var i = 0;
    var course = $0;
    var opr_itog =  "(";
    while (i < OPR_Collector.Size)
      if( not SmartConvertSumDbl( course, 1, m_RepDate, OPR_Collector[i].Currency, NATCUR, false) )
         m_T18.(m_T18.Size) = "Не могу сконвертировать сумму из "+DL_getCCode(OPR_Collector[i].Currency)+" в "+DL_getCCode(NATCUR)+".";
         course = 0;
      end;
      
      opr_itog = opr_itog + "'Расчёт ОПР'!E"+string(22+25*i) + "*"+string(course);

      if( i != (OPR_Collector.Size - 1) )
         opr_itog = opr_itog + ";";
      end;
      i = i + 1;
    end;
    opr_itog = opr_itog + ")";

    return string(opr_itog);
  END;

  /*Расчет Общего Процентного Риска*/
  PRIVATE MACRO ПечататьРасчетОПР()
     var i = 1, КолвоСтрок = 15, j = 0;
     var OPRItogN, OPRItogV;

     SetActiveSheet("Расчёт ОПР");
     var n_rec = 0;
     if(OPR_Collector.Size == 0)
        PrintFormatString("", "N2_Currency","Нет данных для формирования отчета расчета общего процентного риска.");
        CopyAllSheetInTotalBook( "Расчёт ОПР", false, "N2_Currency", 0 );
     else
        while ( n_rec < OPR_Collector.Size )
           i = 1; j = 0;
           OPRItogN = ""; OPRItogV = "";

           PrintFormatString("", "N2_Currency", GetFinInstrCcy(IIF(OPR_Collector.Size == 0, 0, OPR_Collector[n_rec].Currency), null, FICK_ISONUMBER));

           while( i <= КолвоСтрок )
              PrintFormatString("","N2_Дл"+string(i),OPR_Collector[n_rec].TLongP[i] ,
                                   "N2_Кр"+string(i),OPR_Collector[n_rec].TShortP[i] 
                               );

              i = i + 1;
           end;

           PrintFormatString("","N2_ДлИ",F( FormulaSUM()+"(E5:E19)" ),
                                "N2_КрИ",F( FormulaSUM()+"(F5:F19)" ) );

           i = 5;
           j = 1;
           while( i < (КолвоСтрок+5) )
              PrintFormatString("","N2_З"+string(j),F( FormulaIF()+"(H"+string(i)+"<=ABS(I"+string(i)+");H"+string(i)+";ABS(I"+string(i)+"))" ));
              i = i + 1;
              j = j + 1;
           end;

           PrintFormatString("","N2_А", F( FormulaSUM()+"(J5:J19)" ),
                                "N2_B", F( FormulaIF()+"("+"(ABS("+FormulaSUMIF()+"(K5:K8;\"<=0\"))>"+FormulaSUMIF()+"(K5:K8;\">0\"));"+FormulaSUMIF()+"(K5:K8;\">0\");"+FormulaSUMIF()+"(K5:K8;\"<=0\"))"),
                                "N2_C", F( FormulaIF()+"("+"(ABS("+FormulaSUMIF()+"(K9:K11;\"<=0\"))>"+FormulaSUMIF()+"(K9:K11;\">0\"));"+FormulaSUMIF()+"(K9:K11;\">0\");"+FormulaSUMIF()+"(K9:K11;\"<=0\"))"),
                                "N2_D", F( FormulaIF()+"("+"(ABS("+FormulaSUMIF()+"(K12:K19;\"<=0\"))>"+FormulaSUMIF()+"(K12:K19;\">0\"));"+FormulaSUMIF()+"(K12:K19;\">0\");"+FormulaSUMIF()+"(K12:K19;\"<=0\"))"),
                                "N2_B1", F( FormulaSUM()+"(K5:K8)" ),
                                "N2_C1", F( FormulaSUM()+"(K9:K11)" ),
                                "N2_D1", F( FormulaSUM()+"(K12:K19)" ),
                                "N2_H",  F( FormulaSUM()+"(M5:M19)" ),
                                "N2_E",  F( FormulaIF()+"("+FormulaSIGN()+"(M5*M9)>0;0;M5)" ),
                                "N2_F",  F( FormulaIF()+"("+FormulaSIGN()+"(M9*M12)>0;0;M9)" ),        
                                "N2_G",  F( FormulaIF()+"("+FormulaSIGN()+"(M5*M12)>0;0;M5)" )
                            );


          if ( OPR_Collector[n_rec].Currency == NATCUR)
            OPRItogN = "ОПРитог =";
            // OPRItogV = F_RC("'Расчёт РР'!C6");
            // перенёс сюда формулу для ячейки 'Расчёт РР'!C6, т.к. ApachePOI выдаёт ошибку из-за отсутствия таблицы 'Расчёт РР'
            // таблица 'Расчёт РР' копируется в TotalBook позже и содержит формулы ссылающиеся на 'Расчёт ОПР'
            OPRItogV = IIF((OPR_Collector.Size == 0), 0,  F_RC( FormulaSUM() + ПолучитьФормулуОПРитог()));
          end;

          PrintFormatString("", "N2_OPRItogN", OPRItogN);
          PrintFormatString("", "N2_OPRItogV", OPRItogV);
          PrintFormatString("", "N2_opr", IIF((OPR_Collector.Size == 0), 0 , F( FormulaSUM()
                                            +"("+"0.1*ABS(J20);0.4*ABS(L5);0.3*(ABS(L9)+ABS(L12));0.4*(ABS(N5)+ABS(O9));ABS(P5)+ABS(Q20);"+"ABS(" + string(OPR_Collector[n_rec].ЦБвр) +")" +")" ) ) );

          CopyAllSheetInTotalBook( "Расчёт ОПР", false, "N2_TotalOprTable", 0 );

          n_rec = n_rec + 1;
        end;
     end;
  END;

  /*Определил локально, что бы добавить формулу для промежуточных итогов FORMULA_REPLACE_CALC, а не тащить дальше в инстурмент*/
  PRIVATE  MACRO SetSubTotal(StartStrNumber, EndStrNumber /*...*/ )
     var Formula = "";
     var SheetName = "";
     var CellName = "", ColumnName = "", TmpAddress="";
     var Pos = 0, i = 3;
     
     if(m_UseTemplate)
       SheetName = m_ObjTemplateXLS.SheetName();

       if( m_ObjTemplateXLS.ExcelLanguageInterface != EXCEL_INTERFACE_RUS )  Formula = "SUBTOTAL";
       else                                                                  Formula = "ПРОМЕЖУТОЧНЫЕ.ИТОГИ";
       end;

       while(GetParm( i, CellName))
         if( m_ObjTemplateXLS.UsePoi() )
             TmpAddress = m_ObjTemplateXLS.GetWorkbook().getRangeAddress(CellName, true); 
         else
             TmpAddress = m_ObjTemplateXLS.Sheet.Range(CellName).Address; // Address имеет вид "$AE$1"
         end;

         if( (Pos = index(TmpAddress, "$")) != 0 )
           TmpAddress = SubStr(TmpAddress, Pos+1);
           if( (Pos = index(TmpAddress, "$")) != 0 )
             ColumnName = SubStr(TmpAddress, 1, Pos-1);
           end;
         end;

         m_ObjTemplateXLS.SetValue_NameCell( CellName, string(FORMULA_REPLACE_CALC)+Formula+"(9;'"+SheetName+"'!$"+ColumnName+"$"+StartStrNumber+":"+"$"+ColumnName+"$"+EndStrNumber+")" );
         i = i + 1;
       end;
     end;
  END; 

  /*Расчет рыночного риска*/
  PRIVATE MACRO ПечататьРасчетPР()
     SetActiveSheet("Расчёт РР");

     if(OPR_Collector.Size == 0)
        PrintFormatString("", "N3_NoData","Нет данных для формирования cводного отчета о размере рыночного риска.");
        CopyAllSheetInTotalBook( "Расчёт РР", false, "N3_NoData", 0 );
     else 
        PrintFormatString("","N3_RepDate", m_RepDate);
        PrintFormatString("","N3_ОПРитог",IIF((OPR_Collector.Size == 0), 0,  F_RC( FormulaSUM() + ПолучитьФормулуОПРитог())),
                             "N3_СПР",СПР,
                             "N3_ОФР",abs(ОФР),
                             "N3_СФР",СФР);

        SetSubTotal(6, 7,"C5" );
        SetSubTotal(9, 10,"C8" );

        CopyAllSheetInTotalBook( "Расчёт РР", false, "N3_MainTable", 0);
     end;
  END;
         
  PRIVATE MACRO Create()
     var Num = 0, err = "";

     ClearRiskItog(true);

     m_IsDataExists = false;

     Num = ОтобратьВыпуски(false);  
     РассчитатьЧП(Num);
     if( m_IsDataExists )
        EndTable();

        CopyAllSheetInTotalBook( "Позиции", false, GetTableRange(),2);
        CopyAllSheetInTotalBook( "Позиции", false, "N1_MainHeader", "A1" );        

        AddStandardFooter( "N1_" );
        CopyAllSheetInTotalBook( "Позиции", false, "N1_Footer", 1);

        ПечататьРасчетОПР();
        ПечататьРасчетPР();
     else
        SetActiveSheet("NoData");
        PrintFormatString("","N4_NoData","Нет данных для формирования отчета.");

        CopyAllSheetInTotalBook( "NoData", false, "N4_NoData", 0 );
     end;
  END;

  MACRO ВидРиска()
     if( УчаствуетВРасчетеФР() )
        return "ФР";
     elif( УчаствуетВРасчетеПР() )
        return "ПР";
     end;
     return "";
  END;

  MACRO T4():string
     var Name = "", AttrID = 0;
     if( T_4 == null )
        if( GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(FIn, OBJTYPE_AVOIRISS), 1, AttrID, NULL, NULL, m_RepDate) )
           DL_GetObjAttrName(OBJTYPE_AVOIRISS, 1, AttrID, null, @Name);
        end;
        T_4 = Name;
     end;
     return T_4;
  END;                                   

  MACRO T5():string
     Var pt = TRecHandler ("party");
     if(  T_5 == null )
        T_5 = "";
        if( not ПолучитьСубъекта(FIn.rec.Issuer,pt) )
           T_5 = pt.rec.ShortName;
        end;
     end;
     return T_5;
  END;

  MACRO T8():string
     Var pt = TRecHandler ("party");
     var Name = "", AttrID = 0;
     if( T_8 == null )
        if( (not ПолучитьСубъекта(FIn.rec.Issuer,pt)) and GetMainObjAttr( null, OBJTYPE_PARTY, UniID(pt, OBJTYPE_PARTY), 19, AttrID, NULL, NULL, m_RepDate) )
           DL_GetObjAttrName(OBJTYPE_PARTY, 19, AttrID, @Name);
        end;
        T_8 = Name;
     end;
     return T_8;
  END;

  MACRO ПовышающийФактор()
     var NumInList = "";
     if( A_15 == null )
        A_15 = 0;
        GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(FIn, OBJTYPE_AVOIRISS), OBJGROUP_AVRRISKFACTOR, NULL, NULL, NumInList);
        if( NumInList == "1" )
           A_15 = 1.5;
        else
           A_15 = 1;
        end;
     end;
     return A_15;
  END;

  MACRO ВычислитьГруппуРиска()
     var AttrID = 0;
     if( T_9_val == null )
        T_9_val = 0;
        T_9_txt = "";
        GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(FIn, OBJTYPE_AVOIRISS), OBJGROUP_AVRRISKGROUP, AttrID, T_9_txt, T_9_val, m_RepDate);
        T_9_txt = DL_GetObjAttrName( OBJTYPE_AVOIRISS, OBJGROUP_AVRRISKGROUP, AttrID );
     end;
     return T_9_val;
  END;

  MACRO T9()
     ВычислитьГруппуРиска();
     return T_9_txt;
  END;

  MACRO ГруппаРиска()
     return TRiskGroup[ВычислитьГруппуРиска()];
  END;

  PRIVATE MACRO ВычислитьГруппуРискаЧП()
     var Rborder = ZeroDate, LBorder = ZeroDate, Day, Year, Mon;

     LBorder = m_RepDate;
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 6, MONTH_CALENDAR); 
     if( (m_RetireDate > LBorder) and (m_RetireDate < RBorder) )
        return НИЗКИЙ_ДО_6_МЕС;
     end;

     LBorder = GetDateAfterMonthsWBranch(m_RepDate, 6,    MONTH_CALENDAR);
     Rborder = GetDateAfterMonthsWBranch(m_RepDate, 2*12, MONTH_CALENDAR);
     if( (m_RetireDate > LBorder) and (m_RetireDate <= RBorder) )
        return НИЗКИЙ_6_24_МЕС;
     end;

     if( (m_RetireDate > RBorder) )
        return НИЗКИЙ_ОТ_24_МЕС;
     end;
  END;

  MACRO ГруппаРискаЧП()
     return TRiskGroup[ВычислитьГруппуРискаЧП()];
  END;

  MACRO T16()
     return T_16;
  END;

  MACRO T16_curr()
     return T_16_Currency;
  END;


  MACRO T15()
     return T_15;
  END;

  MACRO ИтоговаяТочность()
     return m_AmountSumPrecItog;
  END;

  MACRO A6(A_6,A_6_1)
     return DL_GetPrintNumber(A_6,A_6_1);
  END;

  MACRO T6()
     return DL_GetPrintNumber(T_6,ИтоговаяТочность());
  END;

  MACRO T17()
     return TFactName[T_7];/*здесь именно T_7, а не GetT_7*/
  END;

  MACRO ПолучитьКоэфВзвешивания()
     return (TCalculateCoef[GetT_7()] / 100);
  END;


  MACRO ТочностьКурсДлПозиций()
     return m_AvrLongCourcePrec;
  END;

  MACRO ТочностьКурсКрПозиций()
     return m_AvrShortCourcePrec;
  END;

  MACRO ВалютаКурсДлПозиций()
     return m_AvrLongCourceFIID;
  END;

  MACRO ВалютаКурсКрПозиций()
     return m_AvrShortCourceFIID;
  END;

  MACRO КурсДлПозиций()
     var AvrTSS = 0., AvrTSSFIID = -1, AvrTSSErr = "";
     if( m_AvrLongCource == null )
        if( ПоискКурса(m_LongCource,@AvrTSSFIID,@AvrTSS,@AvrTSSErr) == true )
           m_AvrLongCource = AvrTSS;
           m_AvrLongCourceFIID = AvrTSSFIID;
           m_AvrLongCourcePrec = DL_MeanSignAfterPoint(m_AvrLongCource,9);/*панели max точность 9*/
           if( m_LongCource == m_ShortCource)
              m_AvrShortCource = m_AvrLongCource;
              m_AvrShortCourceFIID = m_AvrLongCourceFIID;
              m_AvrShortCourcePrec = m_AvrLongCourcePrec;
           end;
        end;
     end;
     return m_AvrLongCource;
  END;

  MACRO КурсКрПозиций()
     var AvrTSS = 0., AvrTSSFIID = -1, AvrTSSErr = "";
     if( m_AvrShortCource == null )
        if( ПоискКурса(m_ShortCource,@AvrTSSFIID,@AvrTSS,@AvrTSSErr) == true )
           m_AvrShortCource = AvrTSS;
           m_AvrShortCourceFIID = AvrTSSFIID;
           m_AvrShortCourcePrec = DL_MeanSignAfterPoint(m_AvrShortCource,9);/*панели max точность 9*/
           if( m_LongCource == m_ShortCource)
              m_AvrLongCource = m_AvrShortCource;
              m_AvrLongCourceFIID = m_AvrShortCourceFIID;
              m_AvrLongCourcePrec = m_AvrShortCourcePrec;
           end;
        end;
     end;
     return m_AvrShortCource;
  END;

  MACRO T18():string
     var ExcludeNote = "", i = 0;

     while( m_T18.Size > i )
        ExcludeNote = ExcludeNote + m_T18[i];
        ExcludeNote = ExcludeNote + " ";
        i = i + 1;
     end;

     return ExcludeNote;
  END;

  PRIVATE MACRO EndReport()
     SaveTotalBook();
    if( m_UseTemplate )
      ReplaceAndCalculate( FORMULA_REPLACE_CALC, "=" );
    end;
  END;         

  initDL_CReportTemplate( MRCALC_Form(), "report_spmrc.xls", NULL, NULL, NULL, use_poi_const );
END;

DL_MRCALC_Report().run();
exit(1);
