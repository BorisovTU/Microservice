/*
$Name:        nptxctrl_refsend.mac
$Module:      Ценные бумаги
$Description: Формирование и отправка справки по НДФЛ
*/
import "nptxctrl_refsend_form.mac", "sprepfun.mac", "sqlconv.mac", "CbReport_h.mac", "scsrvrepfun.mac", "func_lib.mac";

PRIVATE CONST CL_STATE_RES    = "резидент",
              CL_STATE_NOTRES = "нерезидент";


//Получить имя папки csv-файлов
PRIVATE MACRO GetCSVPath()
  var RegistryPath = "РСХБ\\ДИРЕКТОРИИ\\OUT\\КОНТРОЛЬ_ЗАДОЛЖЕННОСТИ";
  var Path = "";
  var stat = 0;
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if((stat) or (Path == "")) 
    msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  else
    if(substr(Path, strlen(Path)-1, 1) != "\\")
      Path = Path + "\\";
    end;
  end;

  return Path;
END;

PRIVATE MACRO GetCntrlRepPath()
  var RegistryPath = "РСХБ\\ДИРЕКТОРИИ\\OUT\\КЛИЕНТ_СПРАВКА_НДФЛ";
  var Path = "";
  var stat = 0;
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if((stat) or (Path == "")) 
    msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  else
    if(substr(Path, strlen(Path)-1, 1) != "\\")
      Path = Path + "\\";
    end;
  end;

  return Path;
END;

PRIVATE CLASS TClientProtocolRowData(ClientName, ClientIDCFT, Contract, Sum18, Error)
  var m_ClientName  = ClientName; 
  var m_ClientIDCFT = ClientIDCFT;
  var m_Contract    = Contract;   
  var m_Sum18       = Sum18;      
  var m_Error       = Error;      
END;

PRIVATE CLASS TClientRepData(ClientID:integer, RepDate:date, RepYear:integer, DueTotal:money)
  var m_ClientID  = ClientID, 
      m_RepDate   = RepDate,  
      m_RepYear   = RepYear,  
      m_DueTotal  = DueTotal;      

  var m_ClientName      = "";
  var m_ClientShortName = "";
  var m_ClientIDCFT     = "";

  var m_ContractsStr  = "";
  var m_DlContrIDStr  = "";
  var m_EmailStr      = "";

  PRIVATE MACRO LoadContrStr()
    var query, cmd, DataSet;
    
    m_ContractsStr = "";
    m_DlContrIDStr = "";
    m_EmailStr     = "";

    query =   " SELECT NVL(LISTAGG(TRIM(sf.t_Number), ', ') WITHIN GROUP (ORDER BY sf.t_ID ASC), CHR(1)) as ContractsStr, "
            + "        NVL(LISTAGG(dlc.t_DlContrID, ',') WITHIN GROUP (ORDER BY sf.t_ID ASC), CHR(1)) as DlContrIDStr, "
            + "        NVL(LISTAGG((CASE WHEN dlc.t_Email = CHR(1) THEN '' ELSE TRIM(dlc.t_Email) END), ',') WITHIN GROUP (ORDER BY sf.t_ID ASC), CHR(1)) as EmailStr "
            + "   FROM dsfcontr_dbt sf, ddlcontr_dbt dlc "
            + "  WHERE sf.t_PartyID = ? " 
            + "    AND (sf.t_DateClose = "+GetSQLDate(date(0,0,0))+" OR sf.t_DateClose >= ?) "
            + "    AND dlc.t_SfContrID = sf.t_ID ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_ClientID);
    cmd.AddParam(m_RepDate);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      m_ContractsStr  = DataSet.ContractsStr;
      m_DlContrIDStr  = DataSet.DlContrIDStr;
      m_EmailStr      = DataSet.EmailStr;
    end;
  END;

  macro GetDlContrIDArr()
    return SplitString(m_DlContrIDStr, ",");
  end;

  macro OnlyIISContr()
    var query, cmd, DataSet;
    var flag = false;

    query =   " select 1 "
            + "   from ddlcontr_dbt dlc, dsfcontr_dbt sf "
            + "  where sf.t_PartyID = ? " 
            + "    and (sf.t_DateClose = "+GetSQLDate(date(0,0,0))+" OR sf.t_DateClose >= ?) "
            + "    and dlc.t_SfContrID = sf.t_ID "
            + "    and dlc.t_IIS = 'X' "
            + "    and NOT EXISTS(select 1 "
            + "                     from ddlcontr_dbt dlc1, dsfcontr_dbt sf1 "
            + "                    where sf1.t_PartyID = sf.t_PartyID " 
            + "                      and (sf1.t_DateClose = "+GetSQLDate(date(0,0,0))+" OR sf1.t_DateClose >= ?) "
            + "                      and dlc1.t_SfContrID = sf1.t_ID "
            + "                      and dlc1.t_IIS = CHR(0) "
            + "                  ) ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_ClientID);
    cmd.AddParam(m_RepDate);
    cmd.AddParam(m_RepDate);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      flag = true;
    end;

    return flag;
  end;

  macro GetEmailArr()
    var arr = SplitString(m_EmailStr, ",");
    var ret_arr = TArray();
    var i = 0;

    while(i < arr.size)
      if(i == 0)
        ret_arr[ret_arr.size] = arr[i];
      else
        var j = 0;
        var find = false;
        while(j < ret_arr.size)
          if(ret_arr[j] == arr[i])
            find = true;
            break;
          end;
          j = j + 1;
        end;

        if(find == false)
          ret_arr[ret_arr.size] = arr[i];
        end;
      end;

      i = i + 1;
    end;

    return ret_arr;
  end;

  var pt = TRecHandler("party.dbt");
  if(ПолучитьСубъекта(m_ClientID, pt) == 0)
    m_ClientName      = pt.rec.Name;
    m_ClientShortName = pt.rec.ShortName;
  end;

  m_ClientIDCFT = ПолучитьКодСубъекта(m_ClientID, PARTY_CODEKIND_ABS);
  LoadContrStr();      
END;


PRIVATE CLASS (DL_CReportTemplate) Sp_NPTXCTRL_Ref_Report(ClientRepData)
  private var m_ClientRepData = ClientRepData;
  
  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран
    
    CreateTotalBook();
    SetActiveSheet("Контроль");
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO PrintRepHeader()
     PrintFormatString( NULL, "H0_1", m_ClientRepData.m_RepDate,
                              "H0_2", m_ClientRepData.m_RepYear
                      );

     CopyAllSheetInTotalBook("Контроль", false, "N1_Header", 0, "Контроль");

     PrintFormatString( NULL, "ClientShortName", m_ClientRepData.m_ClientName
                      );

     CopyAllSheetInTotalBook("Контроль", false, "N1_ClientData", 0, "Контроль");

     CopyAllSheetInTotalBook("Контроль", false, "N1_MainTableHeader", 1, "Контроль");

  END;

    /*Убрано наименование листа из формулы итогов*/
  MACRO GetFormulaSumm()
     GetFormulaSummREG();
  END;

  PRIVATE MACRO RegistrTableCtrl()
     RegisterTable( "MainTable", NULL,
                    "Client",            
                    "ClientStatus",      
                    "RateGeneral",       
                    "RateSpecial",       
                    "TaxSpecial0",       
                    "TaxSpecial",        
                    "TaxGeneral",        
                    "DEPOTaxGeneral",    
                    "TaxGeneral15",      
                    "DEPOTaxGeneral15",  
                    "CalcSpecial",       
                    "CalcGeneral",       
                    "DEPOCalcGeneral",   
                    "CalcGeneral15",     
                    "DEPOCalcGeneral15",    
                    "PaidSpecial",         
                    "PaidGeneral",           
                    "DEPOPaidGeneral",   
                    "PaidGeneral15",     
                    "DEPOPaidGeneral15", 
                    "SOFRDueTotal",      
                    "DEPODueTotal",      
                    "DueTotal"           
                  );

     SetCellAutoSumma( "Всего", 
                         "TaxSpecial0",       
                         "TaxSpecial",        
                         "TaxGeneral",        
                         "DEPOTaxGeneral",    
                         "TaxGeneral15",      
                         "DEPOTaxGeneral15",  
                         "CalcSpecial",       
                         "CalcGeneral",       
                         "DEPOCalcGeneral",   
                         "CalcGeneral15",     
                         "DEPOCalcGeneral15",    
                         "PaidSpecial",         
                         "PaidGeneral",           
                         "DEPOPaidGeneral",   
                         "PaidGeneral15",     
                         "DEPOPaidGeneral15", 
                         "SOFRDueTotal",      
                         "DEPODueTotal",      
                         "DueTotal");

     PrintTableLine( "Client", "Всего", null );
     PrintTableLine( "Client", "В том числе", null );
  END;

  PRIVATE MACRO EndReport()
    SaveTotalbook();
    ReplaceAndCalculate();
  END;

  PRIVATE MACRO Create()
    var query, cmd, DataSet;
    
    query =   " select csv.* "
            + "   from dnptxctrlcsv_tmp csv "
            + "  where t_ClientID = ? "
            + "  order by t_IIS DESC ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_ClientRepData.m_ClientID);

    PrintRepHeader();
    RegistrTableCtrl();

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
      PutRowToCSV(DataSet.CSVStr);
    end;

    EndTable();
    CopyAllSheetInTotalBook("Контроль", false, GetTableRange(), 0, "Контроль");

    PrintFormatString(NULL, "N1_ReportDate", date());

    CopyAllSheetInTotalBook("Контроль", false, "N1_RepDateFooter", 1, "Контроль");
  END;

  MACRO Run()
    Run();
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_nptxctrl.xlsx", true, null, null, true ); 
END;

private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

PRIVATE MACRO GetUniqNumber():integer
  var cmd = RsdCommand("SELECT DNPTXCTRL_REFSEND_CODE_SEQ.NEXTVAL NextV FROM DUAL;");
  var ds = TRsbDataSet(cmd);

  if(ds.MoveNext())
    return Int(ds.NextV);
  end;

  return 0;
END;

PRIVATE MACRO CreateEndSendRep(ClientRepData, ErrStr:@string, TopUpDate:date, IsExcel:string, IsPDF:string, IsEmail:string)
  var RepObj = NULL;
  var FullRepFileNameXLSX = "", NewFileName = "", NewFileNameXLSX = "", NewFileNamePDF = "";
  var FileName, FileExt;
  var FromDir, RepDir;
  var day, mon, year;
  var hour, min, sec;
  var err = 0;
  var i = 0;

  ErrStr = "";

  RepObj = Sp_NPTXCTRL_Ref_Report(ClientRepData);

  FullRepFileNameXLSX = RepObj.Run();
  
  RepObj = NULL;

  if(FullRepFileNameXLSX)
    DateSplit(date(), day, mon, year);
    TimeSplit(time(), hour, min, sec);
    
    FromDir = SplitFile(FullRepFileNameXLSX, FileName, FileExt);
    RepDir  = GetCntrlRepPath();
    
    NewFileName = "Отчет_Контроль_задолженности_"+ClientRepData.m_ClientShortName+"_"+IIF(ClientRepData.m_ClientIDCFT, ClientRepData.m_ClientIDCFT+"_", "")+string(day:o:2)+string(mon:o:2)+string(year)+"_"+string(hour:f:2)+string(min:f:2);
    NewFileNameXLSX = NewFileName + ".xlsx"; 

    err = SC_MoveFile("$txtfile\\", RepDir, FileName+FileExt, NewFileNameXLSX);
    if(err)
      msgbox("Ошибка при перемещении файла отчета");
    end;

    if(not err)
      if(IsPDF)
        NewFileNamePDF = NewFileName + ".pdf";

        err = SC_ConvertToFormat(RepDir+NewFileNameXLSX, RepDir+NewFileNamePDF, SC_SRVREPFORMAT_PDF, false, false);
        if(err)
          msgbox("Ошибка при конвертации отчета в PDF");
        end;
      end;

      if(not IsExcel)
        RemoveFile(RepDir+NewFileNameXLSX);
        NewFileNameXLSX = "";
      end;
    end;

    if((not err) and (IsEmail))
      //Добавить сообщение о формировании справки
      var DlContrIDArr = ClientRepData.GetDlContrIDArr();
      var MsgIDarr = TArray();

      if((DlContrIDArr != NULL) and (DlContrIDArr.size > 0))
        var ins_dlcontrmsg = RsbSQLInsert("dlcontrmsg.dbt");
        var dlcontrmsg_new = TRecHandler("dlcontrmsg.dbt");
        
        i = 0;
        while(i < DlContrIDArr.size)

          if(trim(DlContrIDArr[i]) != "")

            dlcontrmsg_new.Clear();

            dlcontrmsg_new.rec.DlContrID  = int(trim(DlContrIDArr[i]));
            dlcontrmsg_new.rec.Kind       = 505;
            dlcontrmsg_new.rec.CreateDate = date();
            dlcontrmsg_new.rec.CreateTime = time();
            dlcontrmsg_new.rec.MarketID   = -1;

            ins_dlcontrmsg.AddRecord(dlcontrmsg_new);
          end;

          i = i + 1;
        end;

        ins_dlcontrmsg.AddReturning("t_id", V_INTEGER);
        if(not ins_dlcontrmsg.Insert())
          msgbox("Ошибка при создании записей в ddlcontrmsg_dbt");
          err = 1;
        else
          ins_dlcontrmsg.GetReturning(MsgIDarr);
        end;
      end;

      //Отправить письма
      if(not err)
        var EmailArr = ClientRepData.GetEmailArr();
        var emailText, Subject, v_email_processor;

        if(ClientRepData.m_DueTotal > 0)
          Subject = "Информационное письмо о недоплате НДФЛ. Клиент "+ClientRepData.m_ClientName;

          emailText =   "Уважаемый(мая) <b>"+ClientRepData.m_ClientName+"</b>, за <b>"+string(ClientRepData.m_RepYear)+"</b> налоговый год "
                      + "у Вас обнаружена <u>недоплата</u> по НДФЛ в размере <b>"+string(abs(ClientRepData.m_DueTotal))+"</b> рублей, "
                      + "в связи с чем Вам необходимо в срок до <b>"+string(TopUpDate:f)+"</b> обеспечить указанную сумму на своем брокерском счете "
                      + "(биржевой рынок). При отсутствии средств для удержания налога в указанный срок, Банк направит в налоговый орган "
                      + "Сообщение о невозможности удержания суммы налога. В этом случае уплата налога осуществляется Вами самостоятельно "
                      + "на основании уведомления, полученного от налогового органа.";
        elif(ClientRepData.m_DueTotal < 0)
          var DueTotalStr:string = "";
          RubToStrAlt(abs(ClientRepData.m_DueTotal), DueTotalStr, NULL, NULL);

          Subject = "Уведомление об излишне удержанной сумме налога на доходы физических лиц. Клиент "+ClientRepData.m_ClientName;

          emailText =   "<b>Уведомление <br>"
                      + "об излишне удержанной сумме налога на доходы физических лиц</b> <br>"
                      + "<br>"
                      + string(Date():f)+" г. № "+GetUniqNumber()+"/"+string(Date():f)+"<br>"                
                      + "<br>"
                      + "В соответствии со статьей 231 Налогового кодекса Российской Федерации, на основании п. 7.1.12 Регламента оказания брокерских услуг "
                      + "и услуг по инвестиционному консультированию АО \x22Россельхозбанк\x22 № 15-Р, АО \x22Россельхозбанк\x22 (далее - Банк) "
                      + "как налоговый агент уведомляет Вас об излишне удержанной сумме налога на доходы физических лиц (далее - налог) за период "
                      + "с <b>01.01."+string(ClientRepData.m_RepYear)+"</b> по <b>31.12."+string(ClientRepData.m_RepYear)+"</b> (включительно).<br>"
                      + "<br>" 
                      + "Клиент <b>"+ClientRepData.m_ClientName+"</b>.<br>"
                      + "<br>" 
                      + "Излишне удержанная сумма налога по операциям с ценными бумагами, производными финансовыми инструментами на "
                      + "<b>01.01."+string(ClientRepData.m_RepYear+1)+"</b> составляет <b>"+string(abs(ClientRepData.m_DueTotal))+"</b> "
                      + "(<b>"+StrLwr(DueTotalStr)+"</b>) рублей.<br>"
                      + "Излишне удержанная сумма налога возвращается Банком Клиенту в соответствии с п. 7.1.12 Регламента оказания брокерских услуг "
                      + "и услуг по инвестиционному консультированию АО \x22Россельхозбанк\x22 № 15-Р."
                      + "<br><br><br> "
                      + "<hr> "
                      + "Возврат излишне удержанной суммы налога осуществляется на текущий рублевый счет, открытый в Банке. В случае если у Вас нет текущего рублевого счета в Банке, "
                      + "Вам необходимо открыть рублевый текущий счет и подать заявление на возврат излишне удержанной суммы налога с указанием реквизитов Вашего счета "
                      + "(путем направления скана заявления с Вашей подписью на эл. почту Банка broker@rshb.ru)";
        end;

        emailText =   "<div>"
                    + emailText
                    + "<br>" 
                    + "<br>"
                    + "С уважением," 
                    + "<br>АО \x22Россельхозбанк\x22" 
                    + "</div>";

        v_email_processor = c_email_proc_env();

        i = 0;
        while(i < EmailArr.size)
          var Email = trim(EmailArr[i]);
          
          if(Email != "")
            v_email_processor.m_add_email_to_list(EmailArr[i]);
          end;

          i = i + 1;
        end;

        if(NewFileNameXLSX != "")
          v_email_processor.m_add_attach_to_list(RepDir + NewFileNameXLSX);
        end;

        if(NewFileNamePDF != "")
          v_email_processor.m_add_attach_to_list(RepDir + NewFileNamePDF);
        end;

        v_email_processor.m_set_msg_head(Subject);
        v_email_processor.m_add_row_to_msg_text(emailText);
        v_email_processor.m_save_to_submit_synch(true);
        v_email_processor.m_submit_email_synch();

        if(v_email_processor.m_get_error_status())
          ErrStr = Subject + " не отправлено"; 
          err = 1; 
        else
          //Обновить дату отправки в сообщениях ДБО
          if(MsgIDarr.size > 0)
            var SendDate = date();
            var SendTime = time();
            
            i = 0;
            while(i < MsgIDarr.size)
              var upd = DL_RSDCommand("UPDATE DDLCONTRMSG_DBT SET T_SENDDATE = ?, T_SENDTIME = ? WHERE T_ID = ?");

              upd.AddParam(SendDate);
              upd.AddParam(SendTime);
              upd.AddParam(MsgIDarr[i]);

              upd.ExecuteCMD();

              i = i + 1;
            end;

          end;
        end;
      end;
    end;
  end;

  return err;

OnError(er)
  ErrStr = er.Message;
  return 1;
END;

PRIVATE CLASS (DL_CReportTemplate) Protocol_Report(RepDate:date, RepYear:integer, ProtocolArr:TArray, ProtocolArrErr:TArray)
  private var m_ProtocolArr    = ProtocolArr;
  private var m_ProtocolArrErr = ProtocolArrErr;
  private var m_RepDate = RepDate;
  private var m_RepYear = RepYear;
  
  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран

    CreateTotalBook();
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;


  PRIVATE MACRO EndReport()
    SaveTotalbook();
  END;

  PRIVATE MACRO Create()
    var i = 0;
    var SheetName = "";

    if(m_ProtocolArr.size > 0)
      SheetName = "Протокол";
      
      InitProgress(m_ProtocolArr.size, "Печать протокола", "Печать протокола");

      SetActiveSheet(SheetName);

      PrintFormatString( NULL, "N1_SendDate", date(),
                               "N1_RepDate",  m_RepDate,
                               "N1_Year",     m_RepYear
                       );

      CopyAllSheetInTotalBook(SheetName, false, "N1_Header", 0, SheetName);
      CopyAllSheetInTotalBook(SheetName, false, "N1_TableHeader", 1, SheetName);

      RegisterTable( "N1_MainTable", NULL,
                     "N1_Client",
                     "N1_ClientIDCFT",       
                     "N1_Contract",       
                     "N1_Sum18"
                   );
      i = 0;
      while(i < m_ProtocolArr.size)
        
        PrintTableLine("N1_Client",      m_ProtocolArr[i].m_ClientName,  null,
                       "N1_ClientIDCFT", m_ProtocolArr[i].m_ClientIDCFT, null,      
                       "N1_Contract",    m_ProtocolArr[i].m_Contract,    null,   
                       "N1_Sum18",       m_ProtocolArr[i].m_Sum18,       null
                    );

        UseProgress(i = i + 1);
      end;

      EndTable();

      CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);

      AddStandardFooter("N1_");

      CopyAllSheetInTotalBook(SheetName, false, "N1_Footer", 0, SheetName);

      RemProgress();

    end;

    if(m_ProtocolArrErr.size > 0)
      SheetName = "Протокол (ошибки)";
      
      InitProgress(m_ProtocolArrErr.size, "Печать протокола (ошибки)", "Печать протокола (ошибки)");

      SetActiveSheet(SheetName);

      PrintFormatString( NULL, "N2_SendDate", date(),
                               "N2_RepDate",  m_RepDate,
                               "N2_Year",     m_RepYear
                       );

      CopyAllSheetInTotalBook(SheetName, false, "N2_Header", 0, SheetName);
      CopyAllSheetInTotalBook(SheetName, false, "N2_TableHeader", 1, SheetName);

      RegisterTable( "N2_MainTable", NULL,
                     "N2_Client",
                     "N2_ClientIDCFT",       
                     "N2_Contract",       
                     "N2_Error"
                   );
      i = 0;
      while(i < m_ProtocolArrErr.size)

        PrintTableLine("N2_Client",      m_ProtocolArrErr[i].m_ClientName,  null,
                       "N2_ClientIDCFT", m_ProtocolArrErr[i].m_ClientIDCFT, null,      
                       "N2_Contract",    m_ProtocolArrErr[i].m_Contract,    null,   
                       "N2_Error",       m_ProtocolArrErr[i].m_Error,       null
                    );

        UseProgress(i = i + 1);
      end;

      EndTable();

      CopyAllSheetInTotalBook(SheetName, false, GetTableRange(), 0, SheetName);

      AddStandardFooter("N2_");

      CopyAllSheetInTotalBook(SheetName, false, "N2_Footer", 0, SheetName);

      RemProgress();

    end;
  END;

  MACRO Run()
    Run();
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_ProtocolRefSend.xlsx", true, null, null, true ); 
END;
macro SaveFileToAppServ(FullName:string)
    var FileName = "";
    var DirName = "";
    var ExtName = "";
    var Path_AppServ = "";

    if(not isStandAlone())
      splitfile(FullName, FileName, ExtName);
      FileName = FileName + ExtName;
      DirName = substr(FullName, 1, index(FullName, FileName) - 1);
      Path_AppServ = "..\\txtfile\\" + FileName;

      if (not copyfile("$" + DirName + FileName, Path_AppServ) )
        msgbox("ошибка при копировании файла на СП");
        return false;
      end;

      return Path_AppServ;
    else
      return FullName;
    end;
end;
PRIVATE MACRO ReportRun()
  var CSV_file;
  var query, cmd, DataSet;
  var Form = Sp_NPTXCTRL_RefSend_Panel();
  var cnt = 0, i = 0;
  var csvstr = "", str = "";
  var RepDate = date(0,0,0);
  var RepYear = 0;
  var NeedFormReps = true;
  var ClientID, IIS, DueTotal;
  var ProtocolArr = TArray();
  var ProtocolArrErr = TArray();
  
  while(Form.Run())
    DL_RSDCommand("TRUNCATE TABLE DNPTXCTRLCSV_TMP").ExecuteCMD();

    ProtocolArr.size = 0; 
    ProtocolArrErr.size = 0;
    
    if(Form.m_CSVFileName != "")
      //Загрузить строки из CSV-файла во временную таблицу
      var ins_nptxctrlcsv = RsbSQLInsert("nptxctrlcsv.tmp");
      var nptxctrlcsv_new = TRecHandler( "nptxctrlcsv.tmp" );
      
      NeedFormReps = true;
      var fileservpath  = SaveFileToAppServ(Form.m_CSVFileName);
      CSV_File = TStreamDoc(fileservpath, "R", "rsansi");
         
      InitProgress(-1, "Чтение CSV-файла", "Чтение CSV-файла");
      i = 0;
      while(CSV_File.ReadLine(@csvstr))

        if(csvstr != "")
          var arr = SplitString(csvstr, ";");
          
          if(arr.size > 0)
            if(i == 0)
              if(arr[0] == "\"Дата\"")
                str = strsubst(arr[1], "\"", "");
                RepDate = date(str);
              else
                msgbox("Указан CSV-файл неправильного формата");
                NeedFormReps = false;
                break;
              end;
            end;

            if(i == 1)
              if(arr[0] == "\"Налоговый год\"")
                str = strsubst(arr[1], "\"", "");
                RepYear = int(str);
              else
                msgbox("Указан CSV-файл неправильного формата");
                NeedFormReps = false;
                break;
              end;
            end;

            if(i >= 2)
              str = arr[0];
              IIS = UNSET_CHAR;
              if(index(str, " ИИС\""))
                IIS = SET_CHAR;
              end;

              str = substr(str, 1, index(str, " ")-1);
              str = strsubst(str, "\"", "");
              ClientID = int(str);

              DueTotal = money(strsubst(arr[arr.size-1], ",", "."));

              if(ClientID > 0)
                nptxctrlcsv_new.Clear();

                nptxctrlcsv_new.rec.ClientID = ClientID;
                nptxctrlcsv_new.rec.IIS      = IIS;
                nptxctrlcsv_new.rec.DueTotal = DueTotal;
                nptxctrlcsv_new.rec.CSVStr   = csvstr;

                ins_nptxctrlcsv.AddRecord(nptxctrlcsv_new);
              end;
            end;
          end;
        end;
        UseProgress(i = i + 1);
      end;

      if(not ins_nptxctrlcsv.Insert())
        msgbox("Ошибка при создании записей в dnptxctrlcsv_tmp");
        NeedFormReps = false;
      end;

      RemProgress();
      CSV_File = NULL;
      RemoveFile(fileservpath);
      
      //Формировать справки клиентам
      if(NeedFormReps == true)
        if(Form.m_ClientID == -1)
          query =    " select p.t_PersonID as t_ClientID, "
                   + "        NVL(SUM(csv.t_DueTotal), 0) as DueTotal "
                   + "   from dpersn_dbt p "
                   + "        left join dnptxctrlcsv_tmp csv on csv.t_ClientID = p.t_PersonID "
                   + "  where Exists(select 1 " 
                   + "                 from dclient_dbt cl "
                   + "                where cl.t_PartyID = p.t_PersonID "
                   + "                  and cl.t_ServiceKind in ("+PTSK_STOCKDL+","+PTSK_DV+","+PTSK_DEPOS+","+PTSK_SVO+") "
                   + "              ) "
                   + "  group by p.t_PersonID "; 

        else
          query =    " select selpt.t_ClientID,  "
                   + "        NVL(SUM(csv.t_DueTotal), 0) as DueTotal "
                   + "   from ddlselectpt_tmp selpt "
                   + "        left join dnptxctrlcsv_tmp csv on csv.t_ClientID = selpt.t_ClientID "
                   + "  where selpt.t_SelectFlag = 'X' "
                   + "  group by selpt.t_ClientID ";
        end;

        query =   " select q.t_ClientID, q.DueTotal, pt.t_Name "
                + "   from ("+query+") q, dparty_dbt pt "
                + "  where pt.t_PartyID = q.t_ClientID "
                + "  order by pt.t_Name ASC ";

        cmd = DL_RSDCommand(query);

        cnt = cmd.GetCount();
        if(cnt > 0)
          InitProgress(cnt, "Подготовка "+IIF(Form.m_IsEmail, "и отправка ", "")+"справок НДФЛ", "Подготовка "+IIF(Form.m_IsEmail, "и отправка ", "")+"справок НДФЛ");

          DataSet = cmd.Execute();
          i = 0;
          while(DataSet.moveNext())
            var err = 0;
            var ErrStr = "";

            var ClientRepData = TClientRepData(DataSet.ClientID, RepDate, RepYear, DataSet.DueTotal);
            var EmailArr = ClientRepData.GetEmailArr();

            if(trim(ClientRepData.m_ContractsStr) == "")
              ErrStr = "Отутствует действующий договор брокерского обслуживания";
              err = 1;
            elif(DataSet.DueTotal == 0)
              ErrStr = "Нулевая сумма задолженности/возврата";
              err = 1;
            elif((DataSet.DueTotal > 0) and (ClientRepData.OnlyIISContr() == true))
              ErrStr = "Отсутствует действующий договор брокерского обслуживания";
              err = 1;
            elif((Form.m_IsEmail) and ((EmailArr == NULL) or (EmailArr.size == 0)))
              ErrStr = "Нет адреса эл. почты";
              err = 1;
            else
              err = CreateEndSendRep(ClientRepData, @ErrStr, Form.m_TopUpDate, Form.m_IsExcel, Form.m_IsPDF, Form.m_IsEmail);
            end;

            if(err)
              ProtocolArrErr[ProtocolArrErr.size] = TClientProtocolRowData(ClientRepData.m_ClientName, ClientRepData.m_ClientIDCFT, ClientRepData.m_ContractsStr, DataSet.DueTotal, ErrStr);
            else
              ProtocolArr[ProtocolArr.size] = TClientProtocolRowData(ClientRepData.m_ClientName, ClientRepData.m_ClientIDCFT, ClientRepData.m_ContractsStr, DataSet.DueTotal, "");
            end;

            UseProgress(i = i + 1);
          end;

          RemProgress();
        end;
      
        //Формируем протокол
        if(Form.m_IsEmail)
          var ProtocolRepObj = Protocol_Report(RepDate, RepYear, ProtocolArr, ProtocolArrErr);
          
          var FullProtocolFileNameXLSX = ProtocolRepObj.Run();

          ProtocolRepObj = NULL;

          if(not isStandAlone())
            FullProtocolFileNameXLSX = "$"+FullProtocolFileNameXLSX;
          end;
          
          if(FullProtocolFileNameXLSX != "")
            var day, mon, year;
            var hour, min, sec;
            var FileName, FileExt;
            
            DateSplit(date(), day, mon, year);
            TimeSplit(time(), hour, min, sec);
            
            var FromDir = SplitFile(FullProtocolFileNameXLSX, FileName, FileExt);
            
            var NewFileNameXLSX = "Протокол_рассылки_Справок_по_НДФЛ_"+string(day:o:2)+string(mon:o:2)+string(year)+"_"+string(hour:f:2)+string(min:f:2) + ".xlsx";

            if(not RenameFile(FullProtocolFileNameXLSX, FromDir+NewFileNameXLSX))
              msgbox("Ошибка при переименовании файла протокола");
            else
              SC_ShowFile(FromDir, NewFileNameXLSX);
            end;
          end;
          
        end;
      
      end;

    end;
  end;
END;

ReportRun();
exit(1);
