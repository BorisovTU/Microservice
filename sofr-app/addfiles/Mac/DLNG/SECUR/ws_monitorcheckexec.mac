/*
$Name:             ws_monitorcheckexec.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Функции проверки исполнения события  
*/
import RSD;
import RsbDataSet;

import "secinter.mac",  "ws_QIValidPeriod.mac";
import "ws_common.mac";
import "ws_monitorexecution.mac", "ws_monitorevents.mac";
import "dlntfd.mac", "spcomcalc.mac";

macro checkexec_fun1() 
  if(Int(Mod(Random(10),2)) == 0)
    return STATE_PROCESSED;
  else
    return STATE_NO_PROCESSED;
  end
end;

macro checkexec_fun2() 
  if(Int(Mod(Random(10),3)) == 0)
    return STATE_PROCESSED;
  else
    return STATE_NO_PROCESSED;
  end
end;

macro checkexec_fun3() 
  if(Int(Mod(Random(10),4)) == 0)
    return STATE_NO_PROCESSED;
  else
    return STATE_PROCESSED;
  end
end;


//Погашение купонов/чп
macro checkexec_retires(MonitorDate, TypeEvent, KindPay) : integer
  var query, str_where, ds, stat = 0, report = "", report_all = "", cmd;

  if((KindPay == Coupon) or (KindPay == Partly))
    if(KindPay == Coupon)
      str_where = "  AND WR.T_ISPARTIAL <> CHR(88)";
    elif(KindPay == Partly)
      str_where = "  AND WR.T_ISPARTIAL = CHR(88)";
    end;
    query = RSDCommand("SELECT FI.T_FIID, FI.T_FI_CODE, FI.T_NAME, AV.T_ISIN, WR.T_NUMBER, AV.T_LSIN, WR.T_LISTDATE, WR.T_DRAWINGDATE, " + 
                       "       CASE WHEN WR.T_ISPARTIAL = CHR(88) THEN 'ЧП' ELSE 'Купон' END AS KIND_PAY " + 
                       "FROM DFININSTR_DBT FI " + 
                       "     INNER JOIN DFIWARNTS_DBT WR ON FI.T_FIID = WR.T_FIID " + 
                       "     INNER JOIN DAVOIRISS_DBT AV ON FI.T_FIID = AV.T_FIID " + 
                       "WHERE WR.T_DRAWINGDATE <= ? " + 
                       "  AND TRUSTAPI.RSI_IsBond(FI.T_FIID) = 1 " +
                       "  AND RSB_PMWRTOFF.WRTGetPortfolioAmount(1, FI.T_FIID, -1, 0, -1, -1, WR.T_LISTDATE, 1, 0, 0) > 0 " + 
                       "  AND WR.T_SPISCLOSED <> CHR(88) " + str_where + " ORDER BY FI.T_FIID" );
  elif(KindPay == Issue)
    query = RSDCommand("SELECT FI.T_FIID, FI.T_FI_CODE, FI.T_NAME, AV.T_ISIN, ' ' AS T_NUMBER, AV.T_LSIN, AV.T_LISTDATE, FI.T_DRAWINGDATE, " + 
                       "       'Полное погашение' AS KIND_PAY " + 
                       "FROM DFININSTR_DBT FI " + 
                       "     INNER JOIN DAVOIRISS_DBT AV ON FI.T_FIID = AV.T_FIID " + 
                       "WHERE FI.T_DRAWINGDATE <= ? " + 
                       "  AND TRUSTAPI.RSI_IsBond(FI.T_FIID) = 1 " +                        
                       "  AND RSB_PMWRTOFF.WRTGetPortfolioAmount(1, FI.T_FIID, -1, 0, -1, -1, AV.T_LISTDATE, 1, 0, 0) > 0 " + 
                       "  AND AV.T_SPISCLOSED <> CHR(88) ORDER BY FI.T_FIID" );
  end;
  query.addParam( "", RSDBP_IN, MonitorDate );
  query.execute();

  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  while( ds.MoveNext() )
    report = report + String("  ",ds.Fiid:9," | ",ds.Name:30, " | ", ds.Lsin:15, " | ", ds.Isin:15, " | ", ds.Kind_pay:16, " | ", ds.Number:7, " | ", SubStr(String(ds.ListDate),1,10):10, " | ", SubStr(String(ds.DrawingDate),1,10):10,"\n");
  end;

  if(report != "")
    report_all = 
    " ---------------------------------------------------------------------------------------------------------------------------------------\n" +
    "    Код     |           Наименование         |     Номер       |      ISIN       |       Вид        |    №    | Дата сбора |  Дата      \n" +    
    "  выпуска   |             выпуска            | гос.регистрации |                 |     выплаты      | выплаты |  реестра   | выплаты    \n" +    
    " ---------------------------------------------------------------------------------------------------------------------------------------\n" + 
    report + 
    " ---------------------------------------------------------------------------------------------------------------------------------------\n";
  end;

  WriteToProtocol(TypeEvent, MonitorDate, report_all );

  if(report != "")
    return STATE_NO_PROCESSED;
  else
    return STATE_PROCESSED;
  end;

end;

macro checkexec_retires_coupon(MonitorDate, TypeEvent) 
  return checkexec_retires(MonitorDate, TypeEvent, Coupon);
end;

macro checkexec_retires_partly(MonitorDate, TypeEvent) 
  return checkexec_retires(MonitorDate, TypeEvent, Partly);
end;

macro checkexec_retires_issue(MonitorDate, TypeEvent) 
  return checkexec_retires(MonitorDate, TypeEvent, Issue);
end;

//Учет купонов/чп
macro checkexec_accounting(MonitorDate, TypeEvent, KindPay, OTC) : integer    // KindPay - Купон/ЧП, OTC true - внебирж. false - бирж.
  var query, str_where, type_paym = "", ds, stat = 0, report = "", report1 = "", headrep, footrep, cmd;
  var cFiid, cDrawingDate, cDeal, nFiid, nNumPay, nDeal;
   headrep = "---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------\n" +
             "                Вид сделки                |  Количество ц/б  |  Номер сделки   | Код клиента  | Наименование клиента |    № договора   | Код к-агента |   Наименование контрагента    |\n" +
             "---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------\n"; 
   footrep = "---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------\n";

  if(KindPay == Coupon)
    str_where = "  AND WR.T_ISPARTIAL <> CHR(88)";
    type_paym = "4 ";
  elif(KindPay == Partly)
    str_where = "  AND WR.T_ISPARTIAL = CHR(88)";
    type_paym = "5 ";
  end;

  if(OTC)
    str_where = str_where + " AND Rsb_Secur.IsOutExchange(Opr.oGrp) = 1 ";
  else
    str_where = str_where + " AND Rsb_Secur.IsExchange(Opr.oGrp) = 1 ";
  end;

  query = RSDCommand("SELECT FI.T_FIID, FI.T_FI_CODE, FI.T_NAME FI_NAME, AV.T_ISIN, WR.T_NUMBER AS NUMBERPAY , AV.T_LSIN, WR.T_LISTDATE, RQ.T_FACTDATE, WR.T_DRAWINGDATE, " +
                     "       CASE WHEN WR.T_ISPARTIAL = CHR(88) THEN 'ЧП' ELSE 'Купон' END AS KIND_PAY, TI.T_DEALID, TI.T_DEALTYPE, OP.T_NAME, LG.T_PRINCIPAL, TI.T_DEALCODE, " +
                     "       CASE WHEN TI.T_CLIENTID > 0 THEN TO_CHAR(TI.T_CLIENTID) ELSE CHR(0) END AS CLIENTID, " + 
                     "       CASE WHEN TI.T_CLIENTID > 0 THEN (SELECT OC.T_CODE FROM DOBJCODE_DBT OC WHERE OC.T_OBJECTTYPE = 3 AND OC.T_CODEKIND = 1 AND OC.T_STATE = 0 AND OC.T_OBJECTID = TI.T_CLIENTID) ELSE CHR(0) END AS CODECLIENT, " +
                     "       CASE WHEN TI.T_CLIENTID > 0 THEN PR.T_SHORTNAME         ELSE CHR(0) END AS CLIENT, " +
                     "       CASE WHEN TI.T_CLIENTCONTRID > 0 THEN CN.T_NUMBER ELSE CHR(0) END AS NUMBERCONTR, " + 
                     "       CASE WHEN TI.T_PARTYID > 0 THEN TO_CHAR(TI.T_PARTYID) ELSE CHR(0) END AS PARTYID, " + 
                     "       CASE WHEN TI.T_PARTYID > 0 THEN (SELECT OC.T_CODE FROM DOBJCODE_DBT OC WHERE OC.T_OBJECTTYPE = 3 AND OC.T_CODEKIND = 1 AND OC.T_STATE = 0 AND OC.T_OBJECTID = TI.T_PARTYID) ELSE CHR(0) END AS CODECONTRAGENT, " +
                     "       CASE WHEN TI.T_PARTYID > 0 THEN PR1.T_SHORTNAME       ELSE CHR(0) END AS CONTRAGENT " +
                     "FROM (SELECT t_Kind_Operation, t_DocKind, rsb_secur.get_OperationGroup(t_SysTypes) oGrp FROM doprkoper_dbt) Opr, " + 
                     "DFIWARNTS_DBT WR " +
                     "INNER JOIN DFININSTR_DBT FI     ON WR.T_FIID = FI.T_FIID " +
                     "INNER JOIN DAVOIRISS_DBT AV     ON FI.T_FIID = AV.T_FIID " +
                     "INNER JOIN DDLRQ_DBT RQ         ON WR.T_FIID = RQ.T_FIID " + 
                     "INNER JOIN DDL_TICK_DBT TI      ON RQ.T_DOCID = TI.T_DEALID " +
                     "INNER JOIN DDL_LEG_DBT LG       ON TI.T_DEALID = LG.T_DEALID " +
                     "FULL OUTER JOIN DPARTY_DBT PR   ON TI.T_CLIENTID = PR.T_PARTYID " +
                     "FULL OUTER JOIN DPARTY_DBT PR1  ON TI.T_PARTYID = PR1.T_PARTYID " +
                     "FULL OUTER JOIN DSFCONTR_DBT CN ON TI.T_CLIENTCONTRID = CN.T_ID " +
                     "INNER JOIN DOPRKOPER_DBT OP     ON TI.T_DEALTYPE = OP.T_KIND_OPERATION " +
                     "WHERE WR.T_DRAWINGDATE <= ? " +
                     "  AND TRUSTAPI.RSI_IsBond(FI.T_FIID) = 1 " +
                     "  AND Opr.t_Kind_Operation = TI.t_DealType " +
                     "  AND Opr.t_DocKind = TI.t_BOfficeKind " +
                     "  AND WR.T_SPISCLOSED <> CHR(88) " +
                     "  AND LG.T_LEGKIND = 2 " +
                     "  AND RQ.T_DEALPART = 2 " + str_where +
                     "  AND ((RQ.T_FACTDATE >= WR.T_LISTDATE) OR (RQ.T_FACTDATE = TO_DATE('01.01.0001', 'dd.mm.yyyy'))) " +
                     "  AND NOT EXISTS (SELECT *  FROM DDLRQ_DBT RQ2 " +
                     "                  WHERE RQ2.T_DOCKIND = RQ.T_DOCKIND " +
                     "                    AND RQ2.T_DOCID = RQ.T_DOCID " +
                     "                    AND RQ2.T_TYPE = " + type_paym +
                     "                    AND CASE WHEN RSI_RSBCALENDAR.ISWORKDAY(WR.T_DRAWINGDATE) = 0 THEN CASE WHEN RQ2.T_FACTDATE >= WR.T_DRAWINGDATE AND RQ2.T_FACTDATE <= RSI_RSBCALENDAR.GETDATEAFTERWORKDAY(WR.T_DRAWINGDATE, 1) THEN 1 ELSE 0 END ELSE " +
                     "                                                                                       CASE WHEN RQ2.T_FACTDATE = WR.T_DRAWINGDATE THEN 1 ELSE 0 END END = 1) " + 
                     "ORDER BY FI.T_FIID, TI.T_DEALID, WR.T_DRAWINGDATE");


  query.addParam( "", RSDBP_IN, MonitorDate );
  query.execute();

  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  cFiid = 0;
  cDrawingDate = Date(0,0,0);
  cDeal = 0;

  while( ds.MoveNext() )
    if(cFiid != ds.Fiid)
      if(report1 != "")
        report = report + headrep + report1 + footrep + "\n";
        report1 = "";
      end;
      cDrawingDate = Date(0,0,0);
      cDeal = 0;
      report = report + String("  Выпуск: ", ds.Fi_Code, "  ", ds.Fi_Name, ", Номер гос. регистрации: ", ds.Lsin, ", ISIN:", ds.Isin, "\n");
    end;
    if(((cDeal < ds.DealID) and (cFiid == ds.Fiid)) or (cFiid != ds.Fiid)) 
      report1 = report1 + String(" ", ds.Name:40," | ",ds.Principal:16:6," | ", ds.DealCode:15, " | ", ds.CodeClient:12, " | ", ds.Client:20, " | ", ds.NumberContr:15, " | ", ds.CodeContragent:12, " | ", ds.Contragent:26, "\n");
      cDeal   = ds.DealID;
    end;
    if(((cDrawingDate < ds.DrawingDate) and (cFiid == ds.Fiid)) or (cFiid != ds.Fiid))
      report = report + String("  ", ds.Kind_Pay," №",ds.NumberPay,", Дата сбора реестра: ", SubStr(String(ds.ListDate),1,10):10,", Дата выплаты: ",SubStr(String(ds.DrawingDate),1,10):10,"\n");
      cFiid   = ds.Fiid;
      cDrawingDate = ds.DrawingDate;
      cDeal   = ds.DealID;
    end;
  end;
  if(report1 != "")
    report = report + headrep + report1 + footrep;
  end;

  WriteToProtocol(TypeEvent, MonitorDate, report );

  if(report != "")
    return STATE_NO_PROCESSED;
  else
    return STATE_PROCESSED;
  end;

end;

macro checkexec_acc_coupon_otc(MonitorDate, TypeEvent) 
  return checkexec_accounting(MonitorDate, TypeEvent, Coupon, true);
end;

macro checkexec_acc_partly_otc(MonitorDate, TypeEvent) 
  return checkexec_accounting(MonitorDate, TypeEvent, Partly, true);
end;

macro checkexec_acc_coupon(MonitorDate, TypeEvent) 
  return checkexec_accounting(MonitorDate, TypeEvent, Coupon, false);
end;

macro checkexec_acc_partly(MonitorDate, TypeEvent) 
  return checkexec_accounting(MonitorDate, TypeEvent, Partly, false);
end;

// Проверка справочников выплат (нулевая ставка купонов/чп)
macro checkexec_guidepaym(MonitorDate, TypeEvent, KindPay) : integer; 
  var report = "", cmd; 

  report = report_guidepaym(MonitorDate, KindPay);
 
  cmd = RSDCommand("UPDATE DEVENT_DBT E SET E.T_FMTCLOBDATA_XXXX = ? WHERE E.T_TYPEEVENT = ? AND E.T_CREATEDATE = ? ");
  cmd.addParam( "", RSDBP_IN, report ); 
  cmd.addParam( "", RSDBP_IN, TypeEvent );
  cmd.addParam( "", RSDBP_IN, MonitorDate );
  cmd.execute();

  if(report != "")
    return STATE_NO_PROCESSED;
  else
    return STATE_PROCESSED;
  end;

end;

macro checkexec_guidepaym_coupon(MonitorDate, TypeEvent) 
  return checkexec_guidepaym(MonitorDate, TypeEvent, Coupon);
end;

macro checkexec_guidepaym_partly(MonitorDate, TypeEvent) 
  return checkexec_guidepaym(MonitorDate, TypeEvent, Partly);
end;


//Погашение купонов/чп/выпуска (Депозитарий)
macro checkexec_dp_retires(MonitorDate, TypeEvent, KindPay) : integer
  var query, q, ds, dss, stat = 0, count = 0, i = 0, str_cond = "", str_select = "", report = "", report_all = "";

  if((KindPay == DP_CRPPAYMENTTYPE_COUPON)
  or (KindPay == DP_CRPPAYMENTTYPE_DIVIDENDS) )
    str_cond = "  AND WR.T_ISPARTIAL <> CHR(88)";
  elif(KindPay == DP_CRPPAYMENTTYPE_NOMINAL)
    str_cond = "  AND WR.T_ISPARTIAL = CHR(88)";
  end;

  if( (KindPay == DP_CRPPAYMENTTYPE_COUPON)
  or (KindPay == DP_CRPPAYMENTTYPE_NOMINAL)
  or (KindPay == DP_CRPPAYMENTTYPE_DIVIDENDS) )
    str_select = "SELECT DISTINCT "
	" fin.t_FI_Code, FIN.T_NAME, AVR.T_ISIN, WR.T_NUMBER, AVR.T_LSIN, WR.T_LISTDATE, WR.T_DRAWINGDATE, "
        "       CASE WHEN WR.T_ISPARTIAL = CHR(88) THEN 'ЧП' ELSE CASE WHEN Rsb_Fiinstr.FI_IsCouponAvoiriss(fin.t_FIID) = 1 THEN 'Купон' ELSE 'Дивиденды' END END AS KIND_PAY "
	" FROM daccount_dbt acc, "
	" ddepoacnt_dbt acc_depo, "
	" davoiriss_dbt avr, "
	" dfininstr_dbt fin, "
	" DFIWARNTS_DBT wr "
	" WHERE acc.t_Chapter = ? "
	" AND fin.t_FIID = acc.t_Code_Currency "
	" AND fin.t_Fiid = avr.t_Fiid "
	+ str_cond +
        " AND wr.T_FIID = fin.T_FIID "
        " AND wr.T_DRAWINGDATE <= ? "
        " AND wr.T_ISCLOSED <> CHR (88) "
	" AND acc_depo.t_AutoKey = acc.t_Deporoot "
	" AND acc_depo.T_KIND = ? "
	" AND RSB_FIInstr.FI_AvrKindsEQ(fin.t_FI_Kind, " + IIF(KindPay == DP_CRPPAYMENTTYPE_DIVIDENDS, AVOIRISSKIND_SHARE, AVOIRISSKIND_BOND) + ", fin.t_AvoirKind) = 1 "
	" AND rsb_depo.GetAvoirServStateOnDate(fin.T_FIID, ?, ? ) = 1 "
	" AND rsb_account.restac (acc.t_Account, acc.t_Code_Currency, wr.t_ListDate, acc.t_Chapter, NULL) <> 0 " 
	" ORDER BY fin.t_FI_Code ";
  elif( KindPay == DP_CRPPAYMENTTYPE_ISSUE )
    str_select = "SELECT DISTINCT "
	" fin.t_FI_Code, fin.T_NAME, AVR.T_ISIN, ' ' AS T_NUMBER, AVR.T_LSIN, AVR.T_LISTDATE, fin.T_DRAWINGDATE, "
        "       'Полное погашение' AS KIND_PAY "
	" FROM daccount_dbt acc, "
	" ddepoacnt_dbt acc_depo, "
	" davoiriss_dbt avr, "
	" dfininstr_dbt fin "
	" WHERE acc.t_Chapter = ? "
	" AND fin.t_FIID = acc.t_Code_Currency "
	" AND fin.t_FIID = avr.t_FIID "
	" AND fin.T_DRAWINGDATE <= ? " 
	" AND fin.T_ISCLOSED <> CHR (88) "
	" AND acc_depo.t_AutoKey = acc.t_Deporoot "
	" AND acc_depo.T_KIND = ? "
	" AND rsb_depo.GetAvoirServStateOnDate(fin.T_FIID, ?, ? ) = 1 "
	" AND rsb_account.restac (acc.t_Account, acc.t_Code_Currency, avr.t_ListDate, acc.t_Chapter, NULL) <> 0 " 
	" ORDER BY fin.t_FI_Code ";
  end;

  query = RSDCommand( str_select );

  query.addParam( "", RSDBP_IN, 5 ); //по 5-й главе (CHAPT5)
  query.addParam( "", RSDBP_IN, MonitorDate );
  query.addParam( "", RSDBP_IN, 1 ); // Активные счета Депо (SIDEBALANCE_ACTIVE)
  query.addParam( "", RSDBP_IN, MonitorDate );
  query.addParam( "", RSDBP_IN, {NumDprt} );
  query.execute();

  ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
  while( ds.MoveNext() )
    report = report + String("  ",ds.FI_Code:15," | ",ds.Name:30, " | ", ds.Lsin:15, " | ", ds.Isin:15, " | ", ds.Kind_pay:16, " | ", ds.Number:7, " | ", SubStr(String(ds.ListDate),1,10):10, " | ", SubStr(String(ds.DrawingDate),1,10):10,"\n");
  end;

  if(report != "")
    report_all = 
    " ---------------------------------------------------------------------------------------------------------------------------------------------\n" +
    "       Код        |           Наименование         |     Номер       |      ISIN       |       Вид        |    №    | Дата сбора |  Дата      \n" +    
    "     выпуска      |             выпуска            | гос.регистрации |                 |     выплаты      | выплаты |  реестра   | выплаты    \n" +    
    " ---------------------------------------------------------------------------------------------------------------------------------------------\n" + 
    report + 
    " ---------------------------------------------------------------------------------------------------------------------------------------------\n";
  end;

  WriteToProtocol(TypeEvent, MonitorDate, report_all );

  if(report != "")
    return STATE_NO_PROCESSED;
  else
    return STATE_PROCESSED;
  end;
end;

macro checkexec_dp_retires_coupon(MonitorDate, TypeEvent) 
  return checkexec_dp_retires(MonitorDate, TypeEvent, DP_CRPPAYMENTTYPE_COUPON);
end;

macro checkexec_dp_retires_partly(MonitorDate, TypeEvent) 
  return checkexec_dp_retires(MonitorDate, TypeEvent, DP_CRPPAYMENTTYPE_NOMINAL);
end;

macro checkexec_dp_retires_issue(MonitorDate, TypeEvent)
  return checkexec_dp_retires(MonitorDate, TypeEvent, DP_CRPPAYMENTTYPE_ISSUE);
end;
macro checkexec_dp_retires_dividend(MonitorDate, TypeEvent)
  return checkexec_dp_retires(MonitorDate, TypeEvent, DP_CRPPAYMENTTYPE_DIVIDENDS);
end;

// Расчет комиссий
macro checkexec_run_calc_comiss(MonitorDate, TypeEvent) : integer
  var report = "", report_all = "";

  if(RunComCalc(MonitorDate,-1,-1,-1,-1,1,false,@report) == 0)
    if(report != "")
      report_all = 
      " Дата расчета комиссий: " + String(MonitorDate,"\n") +                                                             
      " -----------------------------------------------------------------------------------\n" +
      "   Комиссия  |  Договор обслуживания  | Код плательщика | Наименование плательщика |\n" +    
      " -----------------------------------------------------------------------------------\n" + 
      report + 
      " -----------------------------------------------------------------------------------\n";
    else
      report_all = "Подходящих для расчета комиссий не найдено."
    end;

    WriteToProtocol(TypeEvent, MonitorDate, report_all );
  end;

  if(report != "")
    return STATE_NO_PROCESSED;
  else
    return STATE_PROCESSED;
  end;
end;

// Комиссии
macro checkexec_comiss(MonitorDate, TypeEvent, DocKind) : integer
  var query, cmd, DataSet, stat = 0;
  var report = "", report_all = "";                                                                                                    

  cmd = DL_RSDCommand();

  if(DocKind == 153) //DL_SPOPER_COMISS
     query = " SELECT com.t_ID, commiss.t_Code CODE,  " +
             "        contr.t_Number CONTRNUMBER, " + 
             "        (CASE WHEN contr.t_partyID < 1 or contr.t_partyID = " + {OurBank} + " THEN chr(0) ELSE rsi_rsbparty.PT_GetPartyCode (contr.t_partyID, " + PTCK_CONTR + ") END) CODEPAYER, " +
             "        (CASE WHEN contr.t_partyID < 1 or contr.t_partyID = " + {OurBank} + " THEN chr(0) ELSE (SELECT t_Name FROM dparty_dbt WHERE t_PartyID = contr.t_partyID) END) PAYER " +
             " FROM ddlcomis_dbt com, dsfcomiss_dbt commiss, dsfcontr_dbt contr " +
             " WHERE com.t_dockind in (101,117,127,138,155,158) " + 
             "   AND t_factpaydate = ? " +
             "   AND rsb_secur.CheckExistGrDealByCom (Com.T_DocKind, Com.T_DocID, Com.T_Contract, Com.T_PLANPAYDATE, " + DLGRACC_STATE_FACTEXEC + ") = 1 " +
             "   AND com.t_feetype = " + SF_FEE_TYPE_PERIOD +
             "   AND (SELECT COUNT (1) " +
             "        FROM DSFDEF_DBT def " +
             "        WHERE def.t_Feetype = COM.T_FEETYPE " +
             "          AND def.T_COMMNUMBER = COM.T_COMNUMBER " +
             "          AND DEF.T_SFCONTRID = COM.T_CONTRACT " +
             "          AND DEF.T_DATEFEE = COM.T_FACTPAYDATE) = 0 " +
             "   AND COM.T_SERVOPERID = 0 " +
             "   AND commiss.t_feetype=com.t_feetype " +
             "   AND commiss.t_number=com.t_comnumber " +
             "   AND CONTR.T_ID=com.T_Contract " +
             " ORDER BY CONTRNUMBER ";

     cmd.addParam(MonitorDate);
   
     DataSet = cmd.execute(query);
       
     while( DataSet.MoveNext() )
       report = report + String("  ",DataSet.CODE:10," | ",DataSet.CONTRNUMBER:64, " | ", DataSet.CODEPAYER:15, " | ", DataSet.PAYER:24, " |\n");
     end;
  elif(DocKind == 104) //DL_COMMDOC
     query = GetSQL_UnprocessedObjects_OpDL_COMMDOC( MonitorDate );
     DataSet = cmd.execute(query);

     while( DataSet.MoveNext() )
        if( (DataSet.defComCount == 0) and SC_NeedCalcCom(DataSet.sfcontrID, DataSet.CommNumber, MonitorDate, OBJGROUP_SFCOM_CALCBYBO, "TDF"/*SFCOM_CALCBYBO_FINALTRADEDAY*/) )
           report = report + String("  ",DataSet.CODE:15," | ",DataSet.CONTRNUMBER:22, " | ", DataSet.CODEPAYER:15, " | ", DataSet.PAYER:24, " |\n");
        end;
     end;
  end;

  if(report != "")
    if(DocKind == 153) //DL_SPOPER_COMISS
      report_all = " Дата экспорта комиссий в ПЗО: ";
    elif(DocKind == 104) //DL_COMMDOC
      report_all = " Дата расчета комиссий: ";
    end;
    report_all = report_all + String(MonitorDate,"\n") +
    " ----------------------------------------------------------------------------------------\n" +
    "      Комиссия    |  Договор обслуживания  | Код плательщика | Наименование плательщика |\n" +    
    " ----------------------------------------------------------------------------------------\n" + 
    report + 
    " ----------------------------------------------------------------------------------------\n";
  else
    report_all = "Подходящих комиссий не найдено.";
  end;

  WriteToProtocol(TypeEvent, MonitorDate, report_all );

  if(report != "")
    return STATE_NO_PROCESSED;
  else
    return STATE_PROCESSED;
  end;
end;

macro checkexec_calc_period_comiss(MonitorDate, TypeEvent)
  return checkexec_comiss(MonitorDate, TypeEvent, 104); //DL_COMMDOC
end;

macro checkexec_exp_comiss_pzo(MonitorDate, TypeEvent)
  return checkexec_comiss(MonitorDate, TypeEvent, 153); //DL_SPOPER_COMISS
end;

/*События монитора. Проверки статуса КИ*/
macro CheckExecQIValidPeriod(MonitorDate, TypeEvent)
  if( MonitorDate == null )
    RunError( "Не задан обязательный параметр функции: Дата монитора" );
  end;

  if( TypeEvent == null )
    RunError( "Не задан обязательный параметр функции: Тип события" );
  end;

  return  PrintQIValidPeriodReport(MonitorDate, TypeEvent);
end;


macro checkexec_OperationsAccountingCommon(MonitorDate, TypeEvent, str_select) : integer
   
   var query, ds, report = "", report_all = "";
   var FD, SOName = "";

   query = RSDCommand( str_select );
   query.execute();

   ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
   while( ds.MoveNext() )
      report = report + String("  ", ds.OprKindName:40, " | ", SubStr(String(ds.DealDate),1,10):13, " | ", ds.DealCode:14, " | ", ds.FiCode:15, " | ", ds.FiName:27, " | ", ds.ClientCode:15, " | ", SQL_ConvTypeStr(ds.ClientName):20, "\n");
   end;

   if( TypeEvent == 95 )
      SOName = "бухгалтерского";
   elif( TypeEvent == 96 )
      SOName = "внутреннего";
   elif( TypeEvent == 97 )
      SOName = "депозитарного";
   end;

   if(report != "")
      report_all =
      "    Дата сервисной операции " + SOName + " учета: " + string(SubStr(String(MonitorDate),1,10):10) + "\n" +
      " --------------------------------------------------------------------------------------------------------------------------------------------------------------------------\n" +
      "                Вид операции               | Дата операции | Номер операции |     Код ц/б     |       Наименование ц/б      |   Код клиента   |    Наименование клиента    \n" +    
      " --------------------------------------------------------------------------------------------------------------------------------------------------------------------------\n" + 
      report + 
      " --------------------------------------------------------------------------------------------------------------------------------------------------------------------------\n";
   end;

   WriteToProtocol(TypeEvent, MonitorDate, report_all );

   if(report != "")
      return STATE_NO_PROCESSED;
   else
      return STATE_PROCESSED;
   end;
end;

/*Проверка необработанных объектов для СО БУ*/
macro checkexec_OperationsAccounting(MonitorDate, TypeEvent) : integer
   return checkexec_OperationsAccountingCommon(MonitorDate, TypeEvent, GetSQL_UnprocessedObjects_OpAcc(MonitorDate));
end;

/*Проверка необработанных объектов для СО ВУ*/
macro checkexec_OperationsInAccounting(MonitorDate, TypeEvent) : integer
   return checkexec_OperationsAccountingCommon(MonitorDate, TypeEvent, GetSQL_UnprocessedObjects_OpInAcc(MonitorDate));
end;

/*Проверка необработанных объектов для СО ДУ*/
macro checkexec_OperationsDepAccounting(MonitorDate, TypeEvent) : integer
   return checkexec_OperationsAccountingCommon(MonitorDate, TypeEvent, GetSQL_UnprocessedObjects_OpDepAcc(MonitorDate));
end;

/*Проверка необработанных объектов для СО переоценки ц/б*/
macro checkexec_OperationsOverValue(MonitorDate, TypeEvent) : integer
   
   var query, query2, ds, ds2, str_select = "", str_selectDeals = "", report = "", report_all = "";

   str_selectDeals = 
              " SELECT L.t_DealID AS DealID, "
            + "        L.t_FIID AS FIID, "
            + "        L.t_DealCode, "
            + "        L.t_DealDate "
            + "   FROM dpmwrtsum_dbt L "
            + "  WHERE     L.t_Party = -1 "
            + "        AND L.t_Department = " + {NumDprt}
            + "        AND L.t_Contract = 0 "
            + "        AND L.t_Buy_Sale IN (" + PM_WRITEOFF_SUM_BUY_BO + ", " + PM_WRITEOFF_SUM_BUY + ") "
            + "        AND ( (L.t_Portfolio = " + KINDPORT_BACK + " AND L.t_State = " + PM_WRTSUM_FORM + ")"
            + "             OR (L.t_Portfolio = " + KINDPORT_BASICDEBT + " AND L.t_State = " + PM_WRTSUM_NOTFORM + ")) "
            + "        AND L.t_Date <= " + GetSQLDate(date(MonitorDate))
            + "        AND L.t_FIID = ? "
            + "        AND L.t_Amount > 0 "
            + " UNION "
            + " SELECT buy1.t_DealID AS DealID, "
            + "        LOT.t_FIID AS FIID, "
            + "        LOT.t_DealCode, "
            + "        LOT.t_DealDate "
            + "   FROM dpmwrtsum_dbt LOT, dpmwrtsum_dbt buy1, ddlrq_dbt rq "
            + "  WHERE     LOT.t_Party = -1 "
            + "        AND LOT.t_Department = " + {NumDprt}
            + "        AND LOT.t_Contract = 0 "
            + "        AND LOT.t_Buy_Sale = " + PM_WRITEOFF_SUM_BUY
            + "        AND LOT.t_Portfolio = " + KINDPORT_BASICDEBT
            + "        AND LOT.t_FIID = ? "
            + "        AND LOT.t_State NOT IN (" + PM_WRTSUM_FORM + ", " + PM_WRTSUM_CANCEL + ") "
            + "        AND LOT.t_Amount > 0 "
            + "        AND buy1.t_SumID = LOT.t_Source "
            + "        AND buy1.t_Portfolio = " + KINDPORT_BACK
            + "        AND rq.t_ID = LOT.t_DocID "
            + " UNION "
            + " (SELECT q.DealID, "
            + "         q.FIID, "
            + "         q.t_DealCode, "
            + "         q.t_DealDate "
            + "    FROM (SELECT DECODE (L.t_Parent, "
            + "                         0, L.t_DealID, "
            + "                         (SELECT t_DealID "
            + "                            FROM dpmwrtsum_dbt "
            + "                           WHERE t_SumID = L.t_Parent)) "
            + "                    DealID, "
            + "                 L.t_DealCode, "
            + "                 L.t_DealDate, "
            + "                 L.t_FIID AS FIID "
            + "            FROM dpmwrtsum_dbt L "
            + "           WHERE     L.t_Department = " + {NumDprt}
            + "                 AND L.t_Party = -1 "
            + "                 AND L.t_Contract = 0 "
            + "                 AND L.t_Buy_Sale = " + PM_WRITEOFF_SUM_SALE
            + "                 AND L.t_EnterDate <= " + GetSQLDate(date(MonitorDate))
            + "                 AND L.t_FIID = ? "
            + "                 AND L.t_AmountBD > 0 "
            + "                 AND L.t_State = " + PM_WRTSUM_NOTFORM
            + "          UNION ALL "
            + "          SELECT DECODE (L.t_Parent, "
            + "                         0, L.t_DealID, "
            + "                         (SELECT t_DealID "
            + "                            FROM dpmwrtsum_dbt "
            + "                           WHERE t_SumID = L.t_Parent)) "
            + "                    DealID, "
            + "                 L.t_DealCode, "
            + "                 L.t_DealDate, "
            + "                 L.t_FIID AS FIID "
            + "            FROM dpmwrtsum_dbt L, dpmwrtbc_dbt B "
            + "           WHERE     L.t_Department = " + {NumDprt}
            + "                 AND L.t_Party = -1 "
            + "                 AND L.t_Contract = 0 "
            + "                 AND L.t_Buy_Sale = " + PM_WRITEOFF_SUM_SALE
            + "                 AND L.t_EnterDate <= " + GetSQLDate(date(MonitorDate))
            + "                 AND L.t_ChangeDate > " + GetSQLDate(date(MonitorDate))
            + "                 AND B.t_FIID = ? "
            + "                 AND B.t_SumID = L.t_SumID "
            + "                 AND B.t_AmountBD > 0 "
            + "                 AND B.t_State = " + PM_WRTSUM_NOTFORM
            + "                 AND B.t_Instance = "
            + "                        (SELECT MAX (M.t_Instance) "
            + "                           FROM dpmwrtbc_dbt M "
            + "                          WHERE M.t_SumID = L.t_SumID "
            + "                                AND M.t_ChangeDate <= " + GetSQLDate(date(MonitorDate)) + ")) q) ";

   query = RSDCommand( GetSQL_UnprocessedObjects_OpOverValue(MonitorDate) );
   query.execute();

   ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
   while( ds.MoveNext() )
      query2 = RSDCommand( str_selectDeals );
      query2.addParam( "", RSDBP_IN, ds.Fiid );
      query2.addParam( "", RSDBP_IN, ds.Fiid );
      query2.addParam( "", RSDBP_IN, ds.Fiid );
      query2.addParam( "", RSDBP_IN, ds.Fiid );
      query2.execute();

      ds2 = TRsbDataSet(query2, RSDVAL_CLIENT, RSDVAL_STATIC);
      while( ds2.MoveNext() )
         report = report + String("   ", SubStr(String(ds2.DealDate),1,10):13, " | ", ds2.DealCode:14, " | ", ds.Fi_Code:15, " | ", ds.Name:27, "\n");
      end;

      if( int(ds.NEED_OVERVALUE_PAPERS) == 1 )
            report = report + String("   ", "":13, " | ", "":14, " | ", ds.Fi_Code:15, " | ", ds.Name:27, "\n");
      end;
   end;

   if(report != "")
      report_all =

      "   Дата переоценки: " + string(SubStr(String(MonitorDate),1,10):10) + "\n" +
      " ----------------------------------------------------------------------------------\n" +
      "   Дата операции | Номер операции |     Код ц/б     |       Наименование ц/б       \n" +    
      " ----------------------------------------------------------------------------------\n" + 
      report + 
      " ----------------------------------------------------------------------------------\n";
   end;

   WriteToProtocol(TypeEvent, MonitorDate, report_all );

   if(report != "")
      return STATE_NO_PROCESSED;
   else
      return STATE_PROCESSED;
   end;
end;

/*Проверка необработанных объектов для СО переоценки НВПИ*/
macro checkexec_OperationsOverValueNVPI(MonitorDate, TypeEvent) : integer
   
   var query, ds, str_select = "", report = "", report_all = "", FD, FD2, ErrCode, RegValue, Flag2 = false, NeedExecute = false;
   Flag2 = true;

   query = RSDCommand( GetSQL_UnprocessedObjects_OpOverValueNVPI(MonitorDate) );
   query.execute();

   ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
   while( ds.MoveNext() )
      if( ds.DocKind == DL_SECURITYDOC )
         FD = SPFirstDoc( ds.DocKind, ds.DealID );
         if(FD.ExistBack)
            FD2 = SPFirstDoc( LEG_KIND_DL_TICK_BACK, ds.DealID );
         end;

         if( SP_DealNeedOverValueNVPI( MonitorDate, FD.tick ) != 0 )
            NeedExecute = false;
         else
            NeedExecute = true;
         end;

         if( (Flag2 == true) and (NeedExecute == false) )
            if( SP_RepoNeedOverValueNVPI( MonitorDate, FD.tick ) != 0 )
               NeedExecute = false;
            else
               NeedExecute = true;
            end;
         end;

         if( not NeedExecute )
            continue;
         end;

         report = report + String("   ", FD.tick.rec.DealDate:13, " | ", FD.tick.rec.DealCode:14, " | ", FD.fininstr().rec.FI_Code:15, " | ", FD.fininstr().rec.Name:27, "\n");
      elif( ds.DocKind == DL_NTGSEC )
         FD = DL_NettingDoc( ds.DocKind, ds.DealID );

         if( SP_NtgNeedOvervalueNVPI( MonitorDate, FD.dl_nett() ) != 0 )
            continue;
         end;

         report = report + String("   ", FD.dl_nett().rec.SigningDate:13, " | ", FD.DealNumber():14, " | ", "":15, " | ", "":27, "\n");
      end;
   end;

   if(report != "")
      report_all =

      "   Дата переоценки НВПИ: " + string(SubStr(String(MonitorDate),1,10):10) + "\n" +
      " ----------------------------------------------------------------------------------\n" +
      "   Дата операции | Номер операции |     Код ц/б     |       Наименование ц/б       \n" +    
      " ----------------------------------------------------------------------------------\n" + 
      report + 
      " ----------------------------------------------------------------------------------\n";
   end;

   WriteToProtocol(TypeEvent, MonitorDate, report_all );

   if(report != "")
      return STATE_NO_PROCESSED;
   else
      return STATE_PROCESSED;
   end;
end;
