/*
$Name:        nptxhold040.mac
$Module:      Ценные бумаги
$Description: Операция удержания НДФЛ. Шаг "Формирование НДР 8 уровня и проводок по удержанию налога"
*/
IMPORT InsCarryDoc, DealsInter, FIInter, "dl_class.mac", "spserv.mac", "sp_car.mac", "nptxcalcfun.mac", "nptxreqfun.mac", limout;

PRIVATE VAR Oper = TRecHandler( "nptxop.dbt" );
PRIVATE VAR SumReestr = 0;

PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

PRIVATE MACRO GetContractByAcc(Account, Currency)
  var query, cmd, DataSet;
  var Contract = 0;

  query =   " select t_ClientContrID "
          + "   from dmcaccdoc_dbt "
          + "  where t_Account = ? "
          + "    and t_Currency = ? "
          + "    and t_Chapter = 1 "
          + "    and t_IsCommon = 'X' ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(Account);
  cmd.AddParam(Currency);

  DataSet = cmd.Execute();

  if(DataSet.moveNext())
    Contract = DataSet.ClientContrID;
  end;

  return Contract;
END;

PRIVATE MACRO GetTaxeKBK(Oper, ValueKind)
  var query, cmd, DataSet;
  var val = 0;

  query =   " select v.t_Sum as Val"
          + "   from ddl_value_dbt v "
          + "  where v.t_DocKind = ? "
          + "    and v.t_DocID = ? "
          + "    and v.t_Kind = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(Oper.rec.DocKind);
  cmd.AddParam(Oper.rec.ID);
  cmd.AddParam(ValueKind);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    val = DataSet.Val;
  end;

  return val;
END;

private macro НашБик()
  var party = TBfile("party.dbt");
  var partcode = TBfile("partcode.dbt");
  var Ok;

  party.KeyNum = 0;
  party.rec.PartyID = {OurBank};
  Ok = party.GetEQ;
  if(not Ok)
    return "";
  end;
  partcode.KeyNum = 0;
  partcode.rec.PartyID = party.rec.PartyID;
  partcode.rec.CodeKind = PTCK_BIC;
  Ok = partcode.GetEQ;
  return IIF(Ok,partcode.rec.Code,"");
END;


PRIVATE MACRO СоздатьПлатежПоВозвратуНалог(Taxe, Ground, PayerAccount, MFRAccount, DepSA)
  var pmobj = RSBPayment();

  pmobj.DocKind      = Oper.rec.DocKind;
  pmobj.DocumentID   = string(Oper.rec.ID:o:34);
  pmobj.Purpose      = 100;/*Возврат*/
  pmobj.BaseAmount   = Taxe;
  pmobj.BaseFIID     = NATCUR;
  pmObj.OrderFIID    = NATCUR;
  pmObj.OrderAmount  = Taxe;
  pmobj.ReceiverFIID = NATCUR;
  pmobj.PayerFIID    = NATCUR;
  pmobj.ValueDate    = Oper.rec.OperDate;   
  pmobj.PaymStatus   = 3000;  
  pmobj.Ground       = Ground;
  pmobj.Number       = Oper.rec.Code;
  pmobj.PrimDocKind  = DOC_BO_PAYMENT;
  pmobj.NumberPack   = 11;
  pmobj.Priority     = 5;

  pmObj.PayerAmount = pmObj.ReceiverAmount = pmObj.BaseAmount;

  //Проверить опера
  pmobj.Oper = GetMainOperInGroup(pmobj.DocKind);
  if (pmobj.Oper <= 0)
    pmobj.Oper = {oper};
  end;

  Pmobj.SetPayerPI(
                  PAYMENTS_GROUP_UNDEF, // Group
                  {OurBank},            // BankID  // Банк плательщика
                  3,                    // BankCodeKind
                  НашБик(),             // BankCode
                  "",                   // BankName
                  "",                   // CorrAcc
                  pmobj.BaseFIID,       // AccountFI
                  1,                    // AccountChapter
                  PayerAccount,         // Account   // Счет плательщика
                  {OurBank},            // ClientID  // Плательщик
                  "",                   // ClientName,
                  {INN_Bank},           // ClientINN,
                  3,                    // ClientCodeKind,
                  НашБик()              // ClientCode
                 );

  Pmobj.SetReceiverPI(
                     PAYMENTS_GROUP_UNDEF,   // Group
                     DepSA.rec.Bankid,       // BankID
                     DepSA.rec.BankCodeKind, // BankCodeKind
                     DepSA.rec.bankCode,     // BankCode
                     DepSA.rec.BankName,     // BankName
                     DepSA.rec.CorrAcc,      // CorrAcc
                     pmobj.BaseFIID,         // AccountFI
                     DepSA.rec.Chapter,      // AccountChapter
                     DepSA.rec.Account,      // Account
                     DepSA.rec.PartyID,      // ClientID
                     DepSA.rec.RecName,      // ClientName,
                     ПолучитьКодСубъекта(DepSA.rec.PartyID, 16),           // ClientINN,
                     1,                    // ClientCodeKind,
                     ПолучитьКодСубъекта(DepSA.rec.PartyID, 1)            // ClientCode
                  );

  Pmobj.IsFactPaym   = "X"; //Этот флаг должен быть установлен уже после добавления схем расчётов. Иначе по платежу не создаётся операция 4001 (или 14001)

  pmobj.BaseRate.Actuate(pmobj.ValueDate, false);
  pmobj.FactRate.Actuate(pmobj.ValueDate, false);

  pmobj.FuturePayerAccount = MFRAccount;
  pmobj.FutureReceiverAccount = MFRAccount;

  pmobj.Actuate();

  DL_NPTX_PutMsg( "Сформирован платеж: Дт<"+PayerAccount+"> Кт<"+DepSA.rec.Account+"> (банк получателя: "+ DepSA.rec.bankCode + " " + DepSA.rec.BankName+") на сумму " + string(pmobj.BaseAmount) + " " + ПолучитьКодФинИн(pmobj.BaseFIID)
                 , 21 );

  return 0;
END;

PRIVATE CLASS THoldAccContrData(_ContrID, _HoldTax, _AddHoldTax)
  var ContrID    = _ContrID;
  var HoldTax    = _HoldTax;
  var AddHoldTax = _AddHoldTax;
END;

PRIVATE CLASS THoldAccContrCollect()
  var m_arr = TArray();

  private macro find(ContrID)
    var i = 0;

    while(i < m_arr.size)
      if(m_arr[i].ContrID == ContrID)
        return i;
      end;
      i = i + 1;
    end;

    return -1;
  end;

  macro Add(Account, HoldTax, AddHoldTax)
    var ContrID = GetContractByAcc(Account, NATCUR);

    var i = find(ContrID);

    if(i != -1)
      m_arr[i].HoldTax = m_arr[i].HoldTax + HoldTax;
      m_arr[i].AddHoldTax = m_arr[i].AddHoldTax + AddHoldTax;
    else
      m_arr[m_arr.size] = THoldAccContrData(ContrID, HoldTax, AddHoldTax);
    end;
  end;

END; 

PRIVATE MACRO RunAction( Oper, Doc, ID_Operation, ID_Step )
  var DlRqNew = TRecHandler("dlrq");
  var rRqAcc = TRecHandler("dlrqacc.dbt");
  var MainTaxAccount = TRecHandler("account.dbt");
  var AddTaxAccount = TRecHandler("account.dbt");
  var party = TRecHandler("party.dbt");
  var Notres = false;
  var stat = 0;
  var Taxe = $0, Taxe15 = $0;
  var cmd, query, DataSet;
  var Sum = 0;
  var nptxreq = TRecHandler("nptxop.dbt");
  var FD = DLFirstDocNPTXOP( Oper );
  var DtAccount = TrecHandler( "account.dbt");
  var CtAccount = TrecHandler( "account.dbt");
  var ErrStr = "";
  var HoldAccContr = THoldAccContrCollect();
  var i = 0;
  var DateRefund = date(0,0,0);
  var HoldYear = 0, AddGroundEyarStr = "";
  var AddTaxeArr = TArray(), AddTaxDp = $0;
  var AddHoldArr = TArray();

  datesplit(Oper.rec.PrevDate, NULL, NULL, HoldYear);

  var TaxTeriodToLucre = CheckTaxTeriodToLucre(HoldYear);
  
  var CatCode = "";

  if(Oper.rec.Subkind_Operation == DL_TXHOLD_OPTYPE_ENDYEAR)
    AddGroundEyarStr = "за "+string(HoldYear)+"г. ";
  end;
  
  if( (ПолучитьСубъекта(Oper.rec.Client, party) == 0) AND (party.rec.NotResident == SET_CHAR) )
    Notres = True;
  end;
 
  if((Oper.rec.Subkind_Operation == DL_TXHOLD_OPTYPE_ENDYEAR) and (HoldYear >= НП_ВВОДА_ДОРАБОТОК_BOSS_2981()))
    stat = СоздатьНДРУдержания_ПРГС(Oper.rec.DocKind, Oper.rec.ID, Oper.rec.Client, ID_Operation, ID_Step, @Taxe, @Taxe15, @AddTaxDp, @AddHoldArr); //в AddHoldArr только налог по ставкам отличным от 13/15
  else
    //Формирование НДР
    if((Oper.rec.SubKind_Operation == DL_TXHOLD_OPTYPE_LUCRE) and (TaxTeriodToLucre))
      stat = СоздатьНДРУдержания_ПРГС(Oper.rec.DocKind, Oper.rec.ID, Oper.rec.Client, ID_Operation, ID_Step, @Taxe, @Taxe15, @AddTaxDp, @AddHoldArr);
    else
      stat = CreateHoldTaxObjects8(Oper, ID_Step, abs(Oper.rec.Tax), @Taxe, @Taxe15);
    end;
  end;

  if((not stat) and (Oper.rec.Subkind_Operation == DL_TXHOLD_OPTYPE_OUTMONEY))
    stat = СоздатьНДРКоррБал_ПРГС(Oper.rec.DocKind, Oper.rec.ID, Oper.rec.Client, ID_Operation, ID_Step);

    if(not stat)
      stat = УчестьКоррБалНДФЛ_ПРГС(FD, Oper, Oper.rec.Client, @AddTaxeArr, @ErrStr);
    end;
  end;

  if(not stat)
    i = 0;
    while(i < AddTaxeArr.size)
      if(AddTaxeArr[i].КБК == NPTXKBK_MAIN)
        Taxe = Taxe + AddTaxeArr[i].Налог;
      elif(AddTaxeArr[i].Ставка == (100*NPTXGENTAXRATE_RESIDENT_15))
        Taxe15 = Taxe15 + AddTaxeArr[i].Налог;
      else
        if((not stat) and (AddTaxeArr[i].Налог > 0))
          CatCode = GetCatTransfByRate(AddTaxeArr[i].Ставка);

          if(CatCode == "")
            return Error( "Не найдена категория НДФЛ к перечислению для ставки " + AddTaxeArr[i].Ставка + "%" );
          end;

          if( not FD.OpenAccount( CatCode, null, false, null, AddTaxAccount, Oper.rec.OperDate, NATCUR) )
             return Error( "Ошибка при определении счета по категории <"+CatCode+">." );
          end;

          AddTaxDp = AddTaxDp + AddTaxeArr[i].Налог;
          
          if( GetAccount(1, Oper.rec.Currency, Oper.rec.Account, DtAccount, true) == false )
            return Error( "Не найден счет удержания НДФЛ <"+Oper.rec.Account+">, заданный в форме операции");
          end;
          
          copy(CtAccount, AddTaxAccount);

          if( not ПроводкаПоКатегориямУчета( FD,
                                             DtAccount, CtAccount,
                                             Oper.rec.OperDate,             /* Дата */
                                             1,                             /* Глава */
                                             NATCUR,                        /* Валюта суммы проводки */
                                             abs(AddTaxeArr[i].Налог),                        /* Сумма проводки*/
                                             Doc,                           /* Документ */
                                             INPCARRY,                      /* Result_Carry */
                                             " 1",                          /* Kind_Oper */
                                             Oper.rec.Code,                 /* Numb_Document */
                                             "Удержание налога на доходы физических лиц "+AddGroundEyarStr+"дохода по ставке "+AddTaxeArr[i].Ставка+"% - "+party.rec.Name             /* Ground */
                                             )
                )
            return Error( "Ошибка при выполнении проводки" );
          end;
          DL_NPTX_PutMsg( "Выполнена проводка: Дт<"+DtAccount.rec.Account+"> Кт<"+CtAccount.rec.Account+"> на сумму " + string(abs(AddTaxeArr[i].Налог)) + " " + ПолучитьКодФинИн(NATCUR) );

          HoldAccContr.add(Oper.rec.Account, AddTaxeArr[i].Налог, 0);

          AddTaxAccount.Clear();
        end;
      end;

      i = i + 1;
    end;
  end;

  if( (not stat) /*and ( (Oper.rec.Method == DL_TYPEGETNALOG_BROKER_ACC) OR (Oper.rec.Method == DL_TYPEGETNALOG_CURRENT_ACC)) and (Oper.rec.Tax != 0)*/ )

    nptxreq.Clear();
    
    query =  "   SELECT * "
           + "     FROM dnptxop_dbt "
           + "    WHERE t_DocKind = "+DL_RETREQ+" AND t_Status IN ("+DLNPTXRETREQ_CHECKED+", "+DLNPTXRETREQ_PARTEXECUTED+") "
           + "      AND t_Client = ? "
           + " ORDER BY t_OperDate ASC, t_Time ASC";

    cmd = DL_RSDCommand();

    cmd.AddParam(Oper.rec.Client);
     
    DataSet = cmd.execute(query);
    if(DataSet.moveNext()) //есть заявление на возврат с типом проверено или исполнено частично
      DataSet.GetRecord().CopyTo(nptxreq.rec);

      DateRefund = GetNptxReqDateRefund(nptxreq.rec.OperDate);
    end;

    stat = DL_NPTX_PutMsg( "Удержание налога" );
    if( not stat )

      if((Taxe) != 0)
          DlRqNew.Clear();
          DlRqNew.rec.DocKind       = Oper.rec.DocKind;
          DlRqNew.rec.DocID         = Oper.rec.ID;
          DlRqNew.rec.DealPart      = 1;
          if(Oper.rec.Subkind_Operation == DL_TXHOLD_OPTYPE_TAXREF) //возврат налога
            DlRqNew.rec.Kind = DLRQ_KIND_REQUEST;
          else
            DlRqNew.rec.Kind = IIF(Taxe > 0, DLRQ_KIND_REQUEST, DLRQ_KIND_COMMIT);
          end;
          DlRqNew.rec.SubKind       = DLRQ_SUBKIND_CURRENCY;
          DlRqNew.rec.Type          = IIF(Notres==false,DLRQ_TYPE_TAX_FLR,DLRQ_TYPE_TAX_FLN);
          DlRqNew.rec.Amount        = abs(Taxe);
          DlRqNew.rec.FIID          = NATCUR;
          DlRqNew.rec.Party         = Oper.rec.Client;
          DlRqNew.rec.State         = DLRQ_STATE_EXEC;
          DlRqNew.rec.PlanDate      = Oper.rec.OperDate;
          DlRqNew.rec.FactDate      = Oper.rec.OperDate;
          DlRqNew.rec.RQAccID       = 0;
          DlRqNew.rec.PlaceID       = Oper.rec.Place;

          rRqAcc.Clear();
          if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(DlRqNew.rec.DocKind, DlRqNew.rec.DocID, DlRqNew.rec.Party, DlRqNew.rec.SubKind, DlRqNew.rec.FIID, DlRqNew.rec.Type, rRqAcc) != 0) //нет подходящих платежных реквизитов

            if( not FD.OpenAccount( "НДФЛ к перечислению, FLR", null, false, null, MainTaxAccount, Oper.rec.OperDate, NATCUR) )
               return Error( "Ошибка при определении счета по категории <НДФЛ к перечислению, FLR>." );
            end;

            rRqAcc.rec.ID               = 0;     
            rRqAcc.rec.DocKind          = DlRqNew.rec.DocKind;     
            rRqAcc.rec.DocID            = DlRqNew.rec.DocID;  
            rRqAcc.rec.SubKind          = DlRqNew.rec.SubKind;     
            rRqAcc.rec.Type             = DlRqNew.rec.Type;     
            rRqAcc.rec.Party            = DlRqNew.rec.Party;     
            rRqAcc.rec.FIID             = MainTaxAccount.rec.Code_Currency;                 
            rRqAcc.rec.BankID           = {OurBank};               
            rRqAcc.rec.Chapter          = MainTaxAccount.rec.Chapter;              
            rRqAcc.rec.Account          = MainTaxAccount.rec.Account;         
            rRqAcc.rec.BankCodeKind     = PTCK_BIC;         
            rRqAcc.rec.BankCode         = ПолучитьКодСубъекта(rRqAcc.rec.BankID, rRqAcc.rec.BankCodeKind);        
            rRqAcc.rec.BankName         = GetPartyFullNameByID(rRqAcc.rec.BankID, false);        
            rRqAcc.rec.BankCorrID       = UnknownParty;           
            rRqAcc.rec.BankCorrCodeKind = 0;     
            rRqAcc.rec.BankCorrCode     = "";    
            rRqAcc.rec.BankCorrName     = "";    
            rRqAcc.rec.CorrAcc          = "";
          else
            stat = ПолучитьСчетПоПлатежнымРеквизитам(rRqAcc, MainTaxAccount, rRqAcc.rec.FIID);
          end;
        
          if( (not stat) and (DL_CreateDLRQ(DlRqNew, Oper.rec.OperDate, DLRQ_ACTION_CREATE, NULL, rRqAcc) != 0) )
            return Error( "Ошибка при создании ТО" );
          else
            DL_NPTX_PutMsg( "Создано ТО типа \"Оплата\" на сумму " + string(abs(Taxe)) + " " + ПолучитьКодФинИн(NATCUR));
          end;
      end;

      if((not stat) and ((Taxe15 != 0) or (AddTaxDp != 0)))
          
          DlRqNew.Clear();
          DlRqNew.rec.DocKind       = Oper.rec.DocKind;
          DlRqNew.rec.DocID         = Oper.rec.ID;
          DlRqNew.rec.DealPart      = 1;
          if(Oper.rec.Subkind_Operation == DL_TXHOLD_OPTYPE_TAXREF) //возврат налога
            DlRqNew.rec.Kind = DLRQ_KIND_REQUEST;
          else
            DlRqNew.rec.Kind = IIF((Taxe15+AddTaxDp) > 0, DLRQ_KIND_REQUEST, DLRQ_KIND_COMMIT);
          end;
          DlRqNew.rec.SubKind       = DLRQ_SUBKIND_CURRENCY;
          DlRqNew.rec.Type          = DLRQ_TYPE_TAX_FLR_ADD;
          DlRqNew.rec.Amount        = abs(Taxe15+AddTaxDp);
          DlRqNew.rec.FIID          = NATCUR;
          DlRqNew.rec.Party         = Oper.rec.Client;
          DlRqNew.rec.State         = DLRQ_STATE_EXEC;
          DlRqNew.rec.PlanDate      = Oper.rec.OperDate;
          DlRqNew.rec.FactDate      = Oper.rec.OperDate;
          DlRqNew.rec.RQAccID       = 0;
          DlRqNew.rec.PlaceID       = Oper.rec.Place;
        
          rRqAcc.Clear();
          if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(DlRqNew.rec.DocKind, DlRqNew.rec.DocID, DlRqNew.rec.Party, DlRqNew.rec.SubKind, DlRqNew.rec.FIID, DlRqNew.rec.Type, rRqAcc) != 0) //нет подходящих платежных реквизитов
            
            if( not FD.OpenAccount( "НДФЛ к перечислению 15%", null, false, null, AddTaxAccount, Oper.rec.OperDate, NATCUR) )
               return Error( "Ошибка при определении счета по категории <НДФЛ к перечислению 15%>." );
            end;
            
            if((Taxe15 != 0) or (AddTaxDp != 0))
              rRqAcc.rec.ID               = 0;     
              rRqAcc.rec.DocKind          = DlRqNew.rec.DocKind;     
              rRqAcc.rec.DocID            = DlRqNew.rec.DocID;  
              rRqAcc.rec.SubKind          = DlRqNew.rec.SubKind;     
              rRqAcc.rec.Type             = DlRqNew.rec.Type;     
              rRqAcc.rec.Party            = DlRqNew.rec.Party;     
              rRqAcc.rec.FIID             = AddTaxAccount.rec.Code_Currency;                 
              rRqAcc.rec.BankID           = {OurBank};               
              rRqAcc.rec.Chapter          = AddTaxAccount.rec.Chapter;              
              rRqAcc.rec.Account          = AddTaxAccount.rec.Account;         
              rRqAcc.rec.BankCodeKind     = PTCK_BIC;         
              rRqAcc.rec.BankCode         = ПолучитьКодСубъекта(rRqAcc.rec.BankID, rRqAcc.rec.BankCodeKind);        
              rRqAcc.rec.BankName         = GetPartyFullNameByID(rRqAcc.rec.BankID, false);        
              rRqAcc.rec.BankCorrID       = UnknownParty;           
              rRqAcc.rec.BankCorrCodeKind = 0;     
              rRqAcc.rec.BankCorrCode     = "";    
              rRqAcc.rec.BankCorrName     = "";    
              rRqAcc.rec.CorrAcc          = "";
            end;
          else
            stat = ПолучитьСчетПоПлатежнымРеквизитам(rRqAcc, AddTaxAccount, rRqAcc.rec.FIID);
          end;

          if( (not stat) and (DL_CreateDLRQ(DlRqNew, Oper.rec.OperDate, DLRQ_ACTION_CREATE, NULL, rRqAcc) != 0) )
            return Error( "Ошибка при создании ТО" );
          else
            DL_NPTX_PutMsg( "Создано ТО типа \"Оплата\" на сумму " + string(abs(Taxe15+AddTaxDp)) + " " + ПолучитьКодФинИн(NATCUR));
          end;
      end;

      if(not stat and ((Taxe != 0) or (Taxe15 != 0)))
        if(((Oper.rec.Subkind_Operation == DL_TXHOLD_OPTYPE_ENDYEAR) or (Oper.rec.Subkind_Operation == DL_TXHOLD_OPTYPE_LUCRE)) and (Oper.rec.Account == ""))
          var RestTaxe      = Taxe;
          var RestTaxe15    = Taxe15;
          
          query =   " with acc as (select DISTINCT sa.t_Account, sa.t_Chapter, sa.t_FIID as t_Currency, (CASE WHEN mp.t_MarketID > 0 THEN 0 ELSE 1 END) as IsOutMarket, dlc.t_DlContrID "
                  + "                from dsettacc_dbt sa, dmcaccdoc_dbt accdoc, dsfcontr_dbt sf, ddlcontrmp_dbt mp, ddlcontr_dbt dlc "
                  + "               where sa.t_PartyID = ? "
                  + "                 and sa.t_FIKind = " + FIKIND_CURRENCY
                  + "                 and sa.t_FIID = " + NATCUR
                  + "                 and sa.t_Account LIKE '306%' "
                  + "                 and accdoc.t_Account = sa.t_Account "
                  + "                 and accdoc.t_Chapter = sa.t_Chapter "
                  + "                 and accdoc.t_Currency = sa.t_FIID "
                  + "                 and accdoc.t_Owner = sa.t_PartyID "
                  + "                 and sf.t_ID = accdoc.t_ClientContrID "
                  + "                 and mp.t_SfContrID = sf.t_ID "
                  + "                 and dlc.t_DlContrID = mp.t_DlContrID "
                  + "                 and dlc.t_IIS = CHR(0) "
                  + "             ) "
                  + " select q.* "
                  + "   from (select q1.*, GREATEST(LEAST(q1.AccRest, q1.PlanAccRest), 0) as CalcAccRest "
                  + "           from (select acc.*, rsb_account.restac(acc.t_Account, acc.t_Currency, ?, acc.t_Chapter, null) as AccRest, "
                  + "                        rsb_brkrep_rshb.GetPlanRestAcc(acc.t_DlContrID, acc.t_Account, acc.t_Chapter, acc.t_Currency, ?) as PlanAccRest "
                  + "                 from acc "
                  + "                ) q1 "
                  + "        ) q "
                  + "  where q.CalcAccRest > 0 "
                  + " order by q.IsOutMarket ASC, q.CalcAccRest DESC";

          cmd = DL_RSDCommand(query);

          cmd.AddParam(Oper.rec.Client);
          cmd.AddParam(Oper.rec.OperDate);
          cmd.AddParam(Oper.rec.OperDate);

          DataSet = cmd.Execute();

          while((not stat) and (DataSet.moveNext()))
            var RestAccRest = Round(money(DataSet.CalcAccRest),0,2);
            
            if( GetAccount(DataSet.Chapter, DataSet.Currency, DataSet.Account, DtAccount, true) == false )
               return Error( "Не найден счет удержания НДФЛ <"+DataSet.Account+">");
            end;

            copy(CtAccount, MainTaxAccount);

            if(RestAccRest > 0)
              sum = min(RestTaxe, RestAccRest);
              if(sum > 0)
                if( not ПроводкаПоКатегориямУчета( FD,
                                                   DtAccount, CtAccount,
                                                   Oper.rec.OperDate,             /* Дата */
                                                   1,                             /* Глава */
                                                   NATCUR,                        /* Валюта суммы проводки */
                                                   sum,                           /* Сумма проводки*/
                                                   Doc,                           /* Документ */
                                                   INPCARRY,                      /* Result_Carry */
                                                   " 1",                          /* Kind_Oper */
                                                   Oper.rec.Code,                 /* Numb_Document */
                                                   "Удержание налога на доходы физических лиц "+AddGroundEyarStr+"по ставке 13%/30% - "+party.rec.Name             /* Ground */
                                                 )
                  )
                   return Error( "Ошибка при выполнении проводки" );
                end;
                DL_NPTX_PutMsg( "Выполнена проводка: Дт<"+DtAccount.rec.Account+"> Кт<"+CtAccount.rec.Account+"> на сумму " + string(abs(sum)) + " " + ПолучитьКодФинИн(NATCUR) );

                RestTaxe    = RestTaxe - sum;
                RestAccRest = RestAccRest - sum;

                HoldAccContr.add(DataSet.Account, sum, 0);
              end;
            end;

            copy(CtAccount, AddTaxAccount);

            if(RestAccRest > 0)
              sum = min(RestTaxe15, RestAccRest);
              if(sum > 0)
                if( not ПроводкаПоКатегориямУчета( FD,
                                                   DtAccount, CtAccount,
                                                   Oper.rec.OperDate,             /* Дата */
                                                   1,                             /* Глава */
                                                   NATCUR,                        /* Валюта суммы проводки */
                                                   sum,                           /* Сумма проводки*/
                                                   Doc,                           /* Документ */
                                                   INPCARRY,                      /* Result_Carry */
                                                   " 1",                          /* Kind_Oper */
                                                   Oper.rec.Code,                 /* Numb_Document */
                                                   "Удержание налога на доходы физических лиц "+AddGroundEyarStr+"по ставке 15% - "+party.rec.Name             /* Ground */
                                                 )
                  )
                   return Error( "Ошибка при выполнении проводки" );
                end;
                DL_NPTX_PutMsg( "Выполнена проводка: Дт<"+DtAccount.rec.Account+"> Кт<"+CtAccount.rec.Account+"> на сумму " + string(abs(Taxe15)) + " " + ПолучитьКодФинИн(NATCUR) );

                RestTaxe15  = RestTaxe15 - sum;
                RestAccRest = RestAccRest - sum;

                HoldAccContr.add(DataSet.Account, sum, 0);
              end;
            end;

            i = 0;

            while((RestAccRest > 0) and (i < AddHoldArr.size))	
              sum = min(AddHoldArr[i].Налог, RestAccRest);
              CatCode = GetCatTransfByRate(AddHoldArr[i].Ставка);

              if(CatCode == "")
                return Error( "Не найдена категория НДФЛ к перечислению для ставки " + AddTaxeArr[i].Ставка + "%" );
              end;

              if( not FD.OpenAccount( CatCode, null, false, null, AddTaxAccount, Oper.rec.OperDate, NATCUR) )
                return Error( "Ошибка при определении счета по категории <"+CatCode+">." );
              end;

              copy(CtAccount, AddTaxAccount);

              if(sum > 0)
                if( not ПроводкаПоКатегориямУчета( FD,
                                                   DtAccount, CtAccount,
                                                   Oper.rec.OperDate,             /* Дата */
                                                   1,                             /* Глава */
                                                   NATCUR,                        /* Валюта суммы проводки */
                                                   sum,                        /* Сумма проводки*/
                                                   Doc,                           /* Документ */
                                                   INPCARRY,                      /* Result_Carry */
                                                   " 1",                          /* Kind_Oper */
                                                   Oper.rec.Code,                 /* Numb_Document */
                                                   "Удержание налога на доходы физических лиц "+AddGroundEyarStr+"по ставке "+AddHoldArr[i].Ставка+"% - "+party.rec.Name             /* Ground */
                                                 )
                  )
                   return Error( "Ошибка при выполнении проводки" );
                end;
                DL_NPTX_PutMsg( "Выполнена проводка: Дт<"+DtAccount.rec.Account+"> Кт<"+CtAccount.rec.Account+"> на сумму " + string(abs(Taxe15)) + " " + ПолучитьКодФинИн(NATCUR) );

                AddHoldArr[i].Налог = AddHoldArr[i].Налог - sum;
                RestAccRest = RestAccRest - sum;

                HoldAccContr.add(DataSet.Account, sum, 0);
              end;

              i = i + 1;
            end;

            if((RestTaxe == 0) and (RestTaxe15 == 0))
              break;
            end; 

          end;

          if((RestTaxe != 0) or (RestTaxe15 != 0))
            return Error("На счетах клиента недостаточно средств для удержания налога");
          end;

        elif(Oper.rec.Subkind_Operation != DL_TXHOLD_OPTYPE_TAXREF) //НЕ Возврат налога
          if(Taxe > 0)
            if( GetAccount(1, Oper.rec.Currency, Oper.rec.Account, DtAccount, true) == false )
               return Error( "Не найден счет удержания НДФЛ <"+Oper.rec.Account+">, заданный в форме операции");
            end;

            copy(CtAccount, MainTaxAccount);

            if(Taxe > 0)
              var taxeKBK1 = GetTaxeKBK(Oper, 1001);
              var taxeKBK2 = GetTaxeKBK(Oper, 1002);

              //!!!Временное решение, что не править закрыты код
              if(((taxeKBK1 != 0) or (taxeKBK2 != 0)) and (Taxe != (taxeKBK1 + taxeKBK2)))
                //Значит меняли вручную в панели налог
                if((taxeKBK1 != 0) and (taxeKBK2 == 0))
                  taxeKBK1 = Taxe;
                elif((taxeKBK1 == 0) and (taxeKBK2 != 0))
                  taxeKBK2 = Taxe
                else
                  taxeKBK1 = min(taxeKBK1, Taxe);
                  taxeKBK2 = Taxe - taxeKBK1;
                end;
              end;

              sum = taxeKBK1;
              if((taxeKBK1 == 0) and (taxeKBK2 == 0))
                sum = Taxe;
              end;

              if(sum != 0)
                if( not ПроводкаПоКатегориямУчета( FD,
                                                   DtAccount, CtAccount,
                                                   Oper.rec.OperDate,             /* Дата */
                                                   1,                             /* Глава */
                                                   NATCUR,                        /* Валюта суммы проводки */
                                                   sum,                           /* Сумма проводки*/
                                                   Doc,                           /* Документ */
                                                   INPCARRY,                      /* Result_Carry */
                                                   " 1",                          /* Kind_Oper */
                                                   Oper.rec.Code,                 /* Numb_Document */
                                                   "Удержание налога на доходы физических лиц "+AddGroundEyarStr+"по ставке 13%/30% - "+party.rec.Name             /* Ground */
                                                 )
                  )
                   return Error( "Ошибка при выполнении проводки" );
                end;
                DL_NPTX_PutMsg( "Выполнена проводка: Дт<"+DtAccount.rec.Account+"> Кт<"+CtAccount.rec.Account+"> на сумму " + string(abs(sum)) + " " + ПолучитьКодФинИн(NATCUR) );

                HoldAccContr.add(Oper.rec.Account, sum, 0);
              end;

              if(taxeKBK2 != 0)
                if( not ПроводкаПоКатегориямУчета( FD,
                                                   DtAccount, CtAccount,
                                                   Oper.rec.OperDate,             /* Дата */
                                                   1,                             /* Глава */
                                                   NATCUR,                        /* Валюта суммы проводки */
                                                   taxeKBK2,                      /* Сумма проводки*/
                                                   Doc,                           /* Документ */
                                                   INPCARRY,                      /* Result_Carry */
                                                   " 1",                          /* Kind_Oper */
                                                   Oper.rec.Code,                 /* Numb_Document */
                                                   "ДЕПО Удержание налога на доходы физических лиц "+AddGroundEyarStr+"по ставке 13%/30% - "+party.rec.Name             /* Ground */
                                                 )
                  )
                   return Error( "Ошибка при выполнении проводки" );
                end;
                DL_NPTX_PutMsg( "Выполнена проводка: Дт<"+DtAccount.rec.Account+"> Кт<"+CtAccount.rec.Account+"> на сумму " + string(abs(taxeKBK2)) + " " + ПолучитьКодФинИн(NATCUR) );

                HoldAccContr.add(Oper.rec.Account, taxeKBK2, 0);
              end;
            end;
          end;


          if(Taxe15 > 0)
            if( GetAccount(1, Oper.rec.Currency, Oper.rec.Account, DtAccount, true) == false )
                 return Error( "Не найден счет удержания НДФЛ <"+Oper.rec.Account+">, заданный в форме операции");
            end;
            
            copy(CtAccount, AddTaxAccount);

            if(Taxe15 > 0)
              if( not ПроводкаПоКатегориямУчета( FD,
                                                 DtAccount, CtAccount,
                                                 Oper.rec.OperDate,             /* Дата */
                                                 1,                             /* Глава */
                                                 NATCUR,                        /* Валюта суммы проводки */
                                                 Taxe15,                        /* Сумма проводки*/
                                                 Doc,                           /* Документ */
                                                 INPCARRY,                      /* Result_Carry */
                                                 " 1",                          /* Kind_Oper */
                                                 Oper.rec.Code,                 /* Numb_Document */
                                                 "Удержание налога на доходы физических лиц "+AddGroundEyarStr+"дохода по ставке 15% - "+party.rec.Name             /* Ground */
                                               )
                )
                 return Error( "Ошибка при выполнении проводки" );
              end;
              DL_NPTX_PutMsg( "Выполнена проводка: Дт<"+DtAccount.rec.Account+"> Кт<"+CtAccount.rec.Account+"> на сумму " + string(abs(Taxe15)) + " " + ПолучитьКодФинИн(NATCUR) );

              HoldAccContr.add(Oper.rec.Account, Taxe15, 0);
            end;
          end;

        else //Возврат налога

          var S1osn = GetS1осн(FD, Oper.rec.Client, Oper.rec.OperDate);
          var S1pov = GetS1повыш(FD, Oper.rec.Client, Oper.rec.OperDate);
          var ReqYear = 0, ReqDateStr = "00.00.0000";
          var ReqGround = "";
          var DepSA = TRecHandler("settacc.dbt");

          DepSA.Clear();

          if(nptxreq.rec.ID > 0)
            if(DL_SetDocGrDeal(0, nptxreq.rec.DocKind, nptxreq.rec.ID, Oper.rec.DocKind, Oper.rec.ID) != 0)
              return Error( "Ошибка при сохранении информации о заявлении" );
            end;
            
            DateSplit(nptxreq.rec.PrevDate, null, null, ReqYear);
            ReqDateStr = string(nptxreq.rec.OperDate:f);
          end;

          ReqGround = "Возврат излишне удержанной суммы налога на доходы физического лица по операциям с ЦБ и производными инструментами за "+ReqYear+"г., согласно заявлению от "+string(ReqDateStr)+" клиента - "+party.rec.Name;

          var IsOurDep = false;
          var queryOurDep = "select settacc.* " +
                            "from dsettacc_dbt settacc, ddp_dep_dbt dep " +
                            "where settacc.t_Account = ? " +
                            "  and settacc.t_PartyID = ? " +
                            "  and dep.t_PartyID = settacc.t_BankID " +
                            "  and settacc.t_BankID <> ? ";

          var cmdOurDep = DL_RSDCommand(queryOurDep);
          cmdOurDep.AddParam(Oper.rec.Account);
          cmdOurDep.AddParam(Oper.rec.Client);
          cmdOurDep.AddParam({OurBank});

          var DSOurDep = cmdOurDep.Execute();
          if(DSOurDep.MoveNext())
            DSOurDep.GetRecord().CopyTo(DepSA.rec);
            IsOurDep = true;
          end;

          if((Oper.rec.Method == DL_TYPEGETNALOG_CURRENT_ACC) and IsOurDep)
            if(not FD.OpenAccount("Счет МФР для возврата", NULL, false, NULL, CtAccount, Oper.rec.OperDate, NATCUR))
              return Error("Ошибка при определении счета по категории <Счет МФР для возврата>");
            end;
          elif(Oper.rec.Method == DL_TYPEGETNALOG_OTHER_ACC)
            var CorrAcc = "";

            var queryOtherAcc = "select settacc.t_CorrAcc " +
                                "from dsettacc_dbt settacc " +
                                "where settacc.t_PartyID = ? " +
                                "  and settacc.t_Account = ? ";

            var cmdOtherAcc = DL_RSDCommand(queryOtherAcc);
            cmdOtherAcc.AddParam(Oper.rec.Client);
            cmdOtherAcc.AddParam(Oper.rec.Account);

            var DSOtherAcc = cmdOtherAcc.Execute();
            if(DSOtherAcc.MoveNext())
              CorrAcc = DSOtherAcc.CorrAcc;
            end;

            if(CorrAcc == "")
              queryOtherAcc = "select acc.t_Account " +
                              "from daccount_dbt acc " +
                              "where acc.t_Client = ? " +
                              "  and acc.t_Account like '30102%' " +
                              "  and acc.t_Open_Date <= ? " +
                              "  and ( " +
                              "         acc.t_CLose_Date > ? " +
                              "         or acc.t_Close_Date = to_date('01.01.0001', ' dd.mm.yyyy') " +
                              "      ) ";

              cmdOtherAcc = DL_RSDCommand(queryOtherAcc);
              cmdOtherAcc.AddParam({OurBank});
              cmdOtherAcc.AddParam(Oper.rec.OperDate);
              cmdOtherAcc.AddParam(Oper.rec.OperDate);
              DSOtherAcc = cmdOtherAcc.Execute();

              if(DSOtherAcc.MoveNext())
                CorrAcc = DSOtherAcc.Account;
              else
                CorrAcc = Oper.rec.Account;
              end;
            end;

            if( GetAccount(1, Oper.rec.Currency, CorrAcc, CtAccount, true) == false )
               return Error( "Не найден корр. счет <"+CorrAcc+">");
            end;
          else
            if( GetAccount(1, Oper.rec.Currency, Oper.rec.Account, CtAccount, true) == false )
               return Error( "Не найден счет удержания НДФЛ <"+Oper.rec.Account+">, заданный в форме операции");
            end;
          end;

          if(Taxe > 0)
            if(Oper.rec.OperDate < DateRefund)
              Sum = min(Taxe, S1osn);
            else
              Sum = taxe;
            end;

            if(Sum > 0)
              copy(DtAccount, MainTaxAccount);

              if((Oper.rec.Method == DL_TYPEGETNALOG_CURRENT_ACC) and IsOurDep)
                stat = СоздатьПлатежПоВозвратуНалог(Sum, ReqGround, DtAccount.rec.Account, CtAccount.rec.Account, DepSA);
                if(stat != 0)
                  return stat;
                end;
              end;

              if( not ПроводкаПоКатегориямУчета( FD,
                                                 DtAccount, CtAccount,
                                                 Oper.rec.OperDate,             /* Дата */
                                                 1,                             /* Глава */
                                                 NATCUR,                        /* Валюта суммы проводки */
                                                 Sum,                           /* Сумма проводки*/
                                                 Doc,                           /* Документ */
                                                 INPCARRY,                      /* Result_Carry */
                                                 " 1",                          /* Kind_Oper */
                                                 Oper.rec.Code,                 /* Numb_Document */
                                                 ReqGround                      /* Ground */
                                               )
                )
                 return Error( "Ошибка при выполнении проводки" );
              end;
              
              DL_NPTX_PutMsg( "Выполнена проводка: Дт<"+DtAccount.rec.Account+"> Кт<"+CtAccount.rec.Account+"> на сумму " + string(Sum) + " " + ПолучитьКодФинИн(NATCUR) );

              HoldAccContr.add(Oper.rec.Account, Sum, 0);
            end;
          end;


          if(Taxe15 > 0)
            if(Oper.rec.OperDate < DateRefund)
              Sum = min(Taxe15, S1pov);
            else
              Sum = Taxe15;
            end;

            if(Sum > 0)
            
              copy(DtAccount, AddTaxAccount);

              if((Oper.rec.Method == DL_TYPEGETNALOG_CURRENT_ACC) and IsOurDep)
                stat = СоздатьПлатежПоВозвратуНалог(Sum, ReqGround, DtAccount.rec.Account, CtAccount.rec.Account, DepSA);
                if(stat != 0)
                  return stat;
                end;
              end;

              if( not ПроводкаПоКатегориямУчета( FD,
                                                 DtAccount, CtAccount,
                                                 Oper.rec.OperDate,             /* Дата */
                                                 1,                             /* Глава */
                                                 NATCUR,                        /* Валюта суммы проводки */
                                                 Sum,                           /* Сумма проводки*/
                                                 Doc,                           /* Документ */
                                                 INPCARRY,                      /* Result_Carry */
                                                 " 1",                          /* Kind_Oper */
                                                 Oper.rec.Code,                 /* Numb_Document */
                                                 ReqGround                      /* Ground */
                                               )
                )
                 return Error( "Ошибка при выполнении проводки" );
              end;

              DL_NPTX_PutMsg( "Выполнена проводка: Дт<"+DtAccount.rec.Account+"> Кт<"+CtAccount.rec.Account+"> на сумму " + string(Sum) + " " + ПолучитьКодФинИн(NATCUR) );

              HoldAccContr.add(Oper.rec.Account, sum, 0);
            end;
          end;
        end;
      end;

      if( ( Oper.rec.Method == DL_TYPEGETNALOG_BROKER_ACC ) and ( ВестиВнутреннийУчет() )) 
         var SaveContract = FD.nptxop.rec.Contract;
         
         i = 0;
         while(i < HoldAccContr.m_arr.size)
           var DtAccCat;
           var CtAccCat;
           var FDsub = NULL;

           //Подменим договор в FD, т.к. в операции удержания он отсутствует

           FD.nptxop.rec.Contract = HoldAccContr.m_arr[i].ContrID;
           
           if(FD.nptxop.rec.Contract > 0)
             FDsub = DL_SubContrFD(FD.nptxop.rec.Contract);
           end;

           if(Oper.rec.Subkind_Operation == DL_TXHOLD_OPTYPE_TAXREF) //Возврат налога
              
              DtAccCat = FD.ВУ_ПолучитьКатегориюДебет(17, true);
              CtAccCat = FD.ВУ_ПолучитьКатегориюКредит(17, true);

              if(FDsub != NULL)
                if( not FDsub.RecreateAccount(DtAccCat, null, true, DtAccount, Oper.rec.OperDate, NATCUR) )
                  return Error("Ошибка при актуализации счета");
                end;
                
                if( not FDsub.RecreateAccount(CtAccCat, null, true, CtAccount, Oper.rec.OperDate, NATCUR) )
                  return Error("Ошибка при актуализации счета");
                end;
              else
                DtAccount = DtAccCat;
                CtAccount = CtAccCat;
              end;

              if( not ПроводкаПоКатегориямВнутреннегоУчета(17, 
                                                  FD, 
                                                  Doc, 
                                                  Oper.rec.OperDate, 
                                                  NATCUR, 
                                                  abs(HoldAccContr.m_arr[i].HoldTax),
                                                  null,
                                                  null,
                                                  null,
                                                  "Возврат налога по операции " + FD.nptxop.rec.Code,
                                                  DtAccount,
                                                  CtAccount
                                                ) )
                return Error( "Ошибка при выполнении проводки ВУ" );                                             
              end;
           else
             DtAccCat = FD.ВУ_ПолучитьКатегориюДебет(17);
             CtAccCat = FD.ВУ_ПолучитьКатегориюКредит(17);

             if(FDsub != NULL)
               if( not FDsub.RecreateAccount(DtAccCat, null, true, DtAccount, Oper.rec.OperDate, NATCUR) )
                 return Error("Ошибка при актуализации счета");
               end;
               
               if( not FDsub.RecreateAccount(CtAccCat, null, true, CtAccount, Oper.rec.OperDate, NATCUR) )
                 return Error("Ошибка при актуализации счета");
               end;
             else
               DtAccount = DtAccCat;
               CtAccount = CtAccCat;
             end;
             
             if(HoldAccContr.m_arr[i].HoldTax != 0)
               if( not ПроводкаПоКатегориямВнутреннегоУчета(17, 
                                                   FD, 
                                                   Doc, 
                                                   Oper.rec.OperDate, 
                                                   NATCUR, 
                                                   abs(HoldAccContr.m_arr[i].HoldTax),
                                                   null,
                                                   null,
                                                   null,
                                                   "Удержание налога по операции " + FD.nptxop.rec.Code,
                                                   DtAccount,
                                                   CtAccount
                                                 ) )
                 return Error( "Ошибка при выполнении проводки ВУ" );
               end;
             end;

             if(HoldAccContr.m_arr[i].AddHoldTax > 0)
               if( not ПроводкаПоКатегориямВнутреннегоУчета(17, 
                                                   FD, 
                                                   Doc, 
                                                   Oper.rec.OperDate, 
                                                   NATCUR, 
                                                   HoldAccContr.m_arr[i].AddHoldTax,
                                                   null,
                                                   null,
                                                   null,
                                                   "Доудержание НДФЛ по операции " + FD.nptxop.rec.Code,
                                                   DtAccount,
                                                   CtAccount
                                                 ) )
                 return Error( "Ошибка при выполнении проводки ВУ" );
               end;
             end;
           end;

           InsertLimitAdjust(FD.nptxop.rec.Contract, "0",   "MONEY", -1*(HoldAccContr.m_arr[i].HoldTax + HoldAccContr.m_arr[i].AddHoldTax), NATCUR, FD.nptxop.rec.OperDate, ID_Operation);
           InsertLimitAdjust(FD.nptxop.rec.Contract, "1",   "MONEY", -1*(HoldAccContr.m_arr[i].HoldTax + HoldAccContr.m_arr[i].AddHoldTax), NATCUR, FD.nptxop.rec.OperDate, ID_Operation);
           InsertLimitAdjust(FD.nptxop.rec.Contract, "2",   "MONEY", -1*(HoldAccContr.m_arr[i].HoldTax + HoldAccContr.m_arr[i].AddHoldTax), NATCUR, FD.nptxop.rec.OperDate, ID_Operation);
           InsertLimitAdjust(FD.nptxop.rec.Contract, "365", "MONEY", -1*(HoldAccContr.m_arr[i].HoldTax + HoldAccContr.m_arr[i].AddHoldTax), NATCUR, FD.nptxop.rec.OperDate, ID_Operation);
         
           i = i + 1;
         end;
         FD.nptxop.rec.Contract = SaveContract;
      end;


      if(nptxreq.rec.ID > 0)
        if( not InsertOprStatus(46081, 4 /*Обработать заявление*/) ) 
           return Error("Ошибка при установке статуса операции" );
        end;
      end;
    end;
  end;

  return stat;
end;

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR stat;

  Oper.SetRecordAddr( FDoc );

  ClearGTT_DNPTXOBJ_TMP();
  stat = DL_NPTX_PutMsg( "Формирование НДР 8 уровня и проводок по удержанию налога" );
  if( not stat )

    stat = RunAction( Oper, DlDoc, ID_Operation, ID_Step );

    if(not stat)
      stat = DL_NPTX_StartInsertTaxObject(Oper.rec.ID, ID_Step);
    end;

    DL_NPTX_SaveOperLog( Oper.rec.ID, 40 );
  end;

  return stat;
END;