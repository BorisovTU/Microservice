/*  
$Name:        wrtavr40.mac
$Module:      Ценные бумаги
$Description: Операция списания/зачисления ц/б. Шаг списания/зачисления
*/
import InsCarryDoc, "sp_class.mac", "oprmassexec.mac", "sp_car2.mac";
import "u_common_email_utils.mac", "wrtavr_utils.mac";

private const ENROLMENTAVOIR = 2011; //зачисление ЦБ

/*группа списания для операции*/
var WriteOffGroups = TArray; /*данные в массиве используются программой при выполнении шага*/

/**
  @brief it_log - сохранение в лог текстового сообщения
  @param[in] text - текстовое сообщение
*/
private macro it_log(text)
  var sql = "begin it_log.log(:1); end;";
  var cmd = RSDCommand(sql);
  cmd.AddParam("",RSDBP_IN,text);
  cmd.execute;
end;

private var MAIL_GRP = 76; ///< BOSS-1350
private macro sendmailgrp(head, text, grp)
  var emailText = text;
  var v_email = c_email_proc_env();
  v_email.m_set_msg_head(head);
  v_email.m_add_row_to_msg_text(text);
  v_email.m_save_to_submit_grp(grp, true);
  ///v_email.m_submit_email_synch();  отключим отправку, просто ставим в очередь
end;


/*СОВУ*/
macro VU_InAccountingExecOp_Schedul(commdate, opernum, num, dealtype, contractkind/*1-клиенские, 0-собственные*/, clientid, contrid, fiid, dealid, documentid:@integer)
  var cmdD, rsD;
  var comm = TRecHandler("dl_comm.dbt"), stat = 0, nextnumb = 0;

  documentid = 0;

  GenerateReference(227, nextnumb);
  
  comm.rec.dockind = DL_INACCSRVOP/*4613*/;
  comm.rec.oper = opernum;
  comm.rec.division = 1;
  comm.rec.commstatus = 0;
  comm.rec.PartyId = -1;
  comm.rec.issuerID = -1;
  comm.rec.newFIID = -1;//код ФИ заполнять если ц/б, иначе -1
  comm.rec.commcode = nextnumb + num;
  comm.rec.commdate = commdate;//Дата операции
  comm.rec.operationkind = 0;//Вид действия
  comm.rec.opersubkind = dealtype;//Вид операции
  comm.rec.fiid = fiid;// код ФИ заполнять если не ц/б, иначе -1
  comm.rec.contractkind = contractkind;
  comm.rec.clientid = clientid;
  comm.rec.deal1 = dealid;
  comm.rec.contractID = contrid;

  stat = SC_CreateDlCommOperation(comm);

  if(stat == 0)
     documentid = Int(comm.rec.DocumentID);
     stat = SC_ServOperExecuteSilent(DL_INACCSRVOP, Int(comm.rec.DocumentID));
  end;

  if(stat == 0)
    StartCaptureOutput();
    [ 
      select dl_comm.t_commstatus
      from ddl_comm_dbt dl_comm
      where dl_comm.t_documentid = ? and
            dl_comm.t_commstatus = 2
    ];
    cmdD = RSDCommand(EndCaptureOutput());
    cmdD.AddParam("documentid", RSDBP_IN, Int(comm.rec.DocumentID));
    cmdD.Execute();
    rsD = TRsbDataSet(cmdD);
    if(not rsD.movenext)
      return 1;
    end;
  end;

  return stat;
end;


/*СОБУ*/
Private macro WRT_AccountingExecOp_Schedul(commdate, opernum, num, clientid, contractID, fiid, flag1, flag4, flag6, documentid:@integer)
  var cmdD, rsD;
  var comm = TRecHandler("dl_comm.dbt"), stat = 0, nextnumb = 0;
  documentid = 0;

  GenerateReference(230, nextnumb);

  comm.rec.dockind = DL_SCACCOUNTING/*4615*/;
  comm.rec.oper = opernum;
  comm.rec.division = 1;
  comm.rec.commstatus = 0;
  comm.rec.PartyId = -1;
  comm.rec.issuerID = -1;
  comm.rec.FIID = fiid;//Выпуск ц/б
  comm.rec.commcode = nextnumb + num;
  comm.rec.commdate = commdate;//Дата операции
  comm.rec.contractID = contractID;
  comm.rec.clientid = clientid;
  comm.rec.avoirkind = 0;//Вид ц/б
  comm.rec.currency = -1;// Валюта номинала
  if(flag1)
    comm.rec.flag1 = "X";//искл.
  else
    comm.rec.flag1 = " ";//искл.
  end;
  comm.rec.flag2 = " ";//Неттинг
  comm.rec.flag3 = " ";//Клиентские комиссии за обороты
  if(flag4)
    comm.rec.flag4 = "X";//Рассчеты на бирже
  else
    comm.rec.flag4 = " ";//Рассчеты на бирже
  end;

  if(flag6)
    comm.rec.flag6 = "X";
  else
    comm.rec.flag6 = " ";
  end;

  comm.rec.createreport = "X";

  comm.rec.FlagSecur = "X";
  
  stat = SC_CreateDlCommOperation(comm);

  if(stat == 0)
     documentid = Int(comm.rec.DocumentID);
     stat = SC_ServOperExecuteSilent(DL_SCACCOUNTING, Int(comm.rec.DocumentID));
  end;

  if(stat == 0)
    StartCaptureOutput();
    [ 
      select dl_comm.t_commstatus
      from ddl_comm_dbt dl_comm
      where dl_comm.t_documentid = ? and
            dl_comm.t_commstatus = 2
    ];
    cmdD = RSDCommand(EndCaptureOutput());
    cmdD.AddParam("documentid", RSDBP_IN, Int(comm.rec.DocumentID));
    cmdD.Execute();
    rsD = TRsbDataSet(cmdD);
    if(not rsD.movenext)
      return 1;
    end;
  end;

  return stat;
end;

/*
  @brief InformingUser - информирование пользователя
  @param[in] DealIDQuery - SQL-текстовый массив ID 

*/
private macro InformingUser(DealIDQuery:STRING, result_SOBU:integer,result_SOVU:integer)
  var sql = ""+
    " select sfc_s.t_servkindsub, t.*,  "
    "        f.t_fi_code,               "
    "        a.t_isin,                  "
    "        l.t_principal,             "
    "        d.t_shortname,             "
    "        o.t_code                   "
    "   from ddl_tick_dbt t,            "
    "        dsfcontr_dbt sfc_s,        "
    "        dparty_dbt d,              "
    "        dfininstr_dbt f,           "
    "        davoiriss_dbt a,           "
    "        ddl_leg_dbt l,             "
    "        dobjcode_dbt  o            "
    "  WHERE t.t_BofficeKind = 127      "
//    "    AND t.t_dealstatus in (10, 0)   " // статус договора - 20 - закрытый, 10 - отложенный, 1 - открытый
    "    AND t.t_dealid  in (" + DealIDQuery + ")   "  
    "    AND t.t_clientcontrid = sfc_s.t_id  "
    "    and d.t_partyid = t.t_clientid      "
    "    and f.t_fiid = t.t_pfi              "
    "    and f.t_fiid = a.t_fiid             "
    "    and o.t_objectid = d.t_partyid      "
    "    and o.t_codekind = 101              "
    "    and o.t_objecttype = 3              "
    "    and o.t_state = 0                   "
    "    and l.t_dealid = t.t_dealid         ";
  var cmd = rsdCommand(sql);
  //cmd.addParam(":1", RSDBP_IN, int(FD.tick.rec.DealID));
  cmd.Execute;
  var rs = RSDRecordSet(cmd);

  ReplaceMacro ( "msgbox", NULL );
  var saveDialogFlag = SetDialogFlag(1);

  var  Dt, Mn, Year; 
  var text, codeets, RefValue; //текстовые переменные для письма уведомления
  if (rs.movenext)
    
    DateSplit ( date(rs.value("t_dealdate")), Dt, Mn, Year );
    RefValue = StrSubst( string(Dt:2), " ", "0" ) + "." + 
                       StrSubst( string(Mn:2), " ", "0" ) + "." + 
                       SubStr(StrSubst( string(Year), " ", "0" ), 3) ;

    //заранее подготовим текст для предполагаемой ошибки СОБУ или СОВУ
    if ((rs.value("t_dealcodets")==null) or (rs.value("t_dealcodets")==strfor(1)))
      codeets =""; 
    else
      codeets=" ("+rs.value("t_dealcodets")+")";
    end;
    text = string("Ошибка при выполнении операции зачисления  N",
                  rs.value("t_dealcode"),codeets," от ",RefValue, 
                  " по клиенту ",rs.value("t_shortname")," ("+rs.value("t_code"),") ",
                  " по ц/б ",rs.value("t_fi_code")," (",rs.value("t_isin"),") в количестве ",
                  rs.value("t_principal")," штук");    

    if (result_SOBU==1) //ошибка СОБУ
        sendmailgrp("Ошибка при выполнении операции зачисления Ц/Б", text, mail_grp);        
        wrtavr_utils_mac_sendErrorEvent(DealIDQuery,"Ошибка СОБУ при выполнении операции зачисления Ц/Б",text);  
        return;
    end;
    if (result_SOVU==1) //ошибка СОВУ  
        sendmailgrp("Ошибка при выполнении операции зачисления Ц/Б", text, mail_grp);        
        wrtavr_utils_mac_sendErrorEvent(DealIDQuery,"Ошибка СОВУ при выполнении операции зачисления Ц/Б",text);
        return;
    end;

    //Ошибки СОБУ/СОВУ не было. Далее информируем пользователя в соответствии с BIQ-13034 BOSS-1350 
      //При зачислении по субдоговору внебиржевому ничего не происходит, шаг отмечается как выполненный 
    if (rs.value("t_servkindsub")==9)
      //MsgBox("Для внебиржевого субдоговора ничего не происходит, шаг отмечается как выполненный");
      it_log(string("Для внебиржевого субдоговора ничего не происходит, шаг отмечается как выполненный. ", "DealId=",rs.value("t_dealid")));
      return;
      //При зачислении по субдоговору обслуживания на биржевом фондовом рынке  
    elif(rs.value("t_servkindsub")==8)
      it_log(string("Зачисление ц/б выполнено. Рекомендуется выполнить корректировку лимитов ц/б в QUIK.", " DealId=",rs.value("t_dealid")));
      if (DL_IsWebService() or isShedulerRunning())  // запущено из Планировщика
        text = string("Исполнена операция зачисления  N",rs.value("t_dealcode"),codeets," от ",RefValue,
                   " по клиенту ",rs.value("t_shortname")," ("+rs.value("t_code"),") ",
                   " по ц/б ",rs.value("t_fi_code")," (",rs.value("t_isin"),") в количестве ",
                   rs.value("t_principal")," штук. Рекомендуется выполнить корректировку лимитов ц/б в QUIK. ");
        sendmailgrp("Выполнено зачисление ц/б", text, mail_grp);        
      else
        MsgBox("Зачисление ц/б выполнено. Рекомендуется выполнить корректировку лимитов ц/б в QUIK.");
      end;
      SetDialogFlag(saveDialogFlag);
      return;
    end;
  end;
end;

// получить ключ рубильника BOSS-1350  -  DEF-65583
PRIVATE MACRO  GetRelay ( p_RelayName: STRING )
  var ErrCode, FlagValue:bool;

  GetRegistryValue( p_RelayName, V_BOOL, FlagValue, ErrCode );
  if ( ErrCode != 0 )
     FlagValue = false;
  end;

  return FlagValue;
onError(erru)  
  return false;
END;

Private Macro AddCOVUBU  (DealIDQuery)

    Var cmd, cmd1, DataSet, DealidSOBU, DealidSOVU; 

    //проверка рубильника BOSS-1350
    var xUseSwitchBOSS1350:BOOL = GetRelay( "РСХБ\\ИНТЕГРАЦИЯ\\РУБИЛЬНИК-BOSS-1350" );

    var Query = " select tick.*, nvl(A.T_GROUPID,-1) ndfl, nvl(n.t_notekind,-1) depo, nvl(p.dealid,-1) pko from ddl_tick_dbt tick " +
                " left join dnotetext_dbt n on N.T_DOCUMENTID = LPAD (tick.t_dealid, 34, '0') and N.T_OBJECTTYPE = 101 and n.t_notekind = 404  " +
                " left join dobjatcor_dbt a on A.T_GROUPID=210 and A.T_OBJECTTYPE = 101 and A.T_OBJECT = LPAD (tick.t_dealid, 34, '0')         " +
                " left join pko_writeoff p on p.dealid = tick.t_dealid " +            
                "  where tick.t_DealID in (" + DealIDQuery + ") and tick.t_bofficekind = 127 and tick.t_clientid != -1 order by t_dealid asc"; 
    Cmd = DL_RSDCommand(Query);

    DataSet = Cmd.Execute();

    var result_SOBU, result_SOVU;

    while(DataSet.MoveNext())
       /*если нет признаков НДФЛ или ДЕПО зачисления(ручная пользовательская операция), то сразу при исполнении создаем и проводим СОБУ, СОВУ*/ 
       if ( ( (DataSet.ndfl < 0) and (DataSet.depo < 0) ) or (DataSet.pko > 0) ) 
          /* вернет Dealid СОБУ */
          result_SOBU = WRT_AccountingExecOp_Schedul(date(DataSet.dealdate), "1", "", DataSet.clientid, DataSet.clientcontrid, dataset.pfi, 
                                       false, false, true, DealidSOBU);
          result_SOVU = VU_InAccountingExecOp_Schedul(date(DataSet.dealdate), "1", "", DataSet.dealtype, 1,DataSet.clientid, 
                                             DataSet.clientcontrid, dataset.pfi, dataset.dealid, DealidSOVU);

	  //проверка рубильника BOSS-1350 и информируем только по операции зачисления 2011
	  if (xUseSwitchBOSS1350 and (DataSet.dealtype == ENROLMENTAVOIR) )
	    //отправка почтового сообщения или MsgBox для фондового рынка
	    InformingUser(DealIDQuery, result_SOBU, result_SOVU);
	  end;

       elif (DataSet.depo > 0) /*если есть признак ДЕПО-операции, то ничего не делаем. Такие операции необходимо закрывать вручную заведенными СОБУ и СОВУ*/
       elif (DataSet.ndfl > 0) /*если есть признак НДФЛ-операции, то графики закрываем без обработки*/
          cmd1 = RsdCommand("   UPDATE ddlgracc_dbt \n" +
           "   SET t_state = 2 \n" +
           " WHERE t_id IN \n" +
           "          (SELECT a.t_id \n" +
           "             FROM ddl_tick_dbt t, ddlgracc_dbt a, ddlgrdeal_dbt d \n" +
           "            WHERE     D.T_DOCID = t.t_dealid \n" +
           "                  AND d.t_dockind = t.t_bofficekind \n" +
           "                  AND A.T_GRDEALID = d.t_id \n" +
           "                  AND a.t_state = 1 \n" +
           "                   AND a.t_accnum > 1 \n" +
           "                  AND t.t_dealid = ?) \n" );
          cmd1.addParam("",RSDBP_IN, DataSet.DealID);
          cmd1.Execute();
       end;
    end;


End;

PRIVATE MACRO MassInitOperation_Impl(DealIDQuery:STRING, Deal )
  var Group = 0;

  AddCOVUBU(DealIDQuery);

  return 0;

END;

PRIVATE MACRO ExecMassInitOperation(FD)
    var Query = "";
  if (FD == NULL)
    Query = " SELECT tick.t_DealID "
          + " FROM ddl_tick_dbt tick ,doprtemp_view oprtemp "
          + " WHERE "
              + " tick.t_DealID = TO_NUMBER(oprtemp.t_DocumentID) " // корректно т.к. обрабатываются только сделки
          + " AND tick.t_BOfficeKind = oprtemp.t_DocKind ";
  else
    Query = string(FD.tick.rec.DealID);
  end;

    return MassInitOperation_Impl(Query);

OnError(RslErrObj)
    RemProgress();
    SP_Mass_CreateMassError( 8029, "ExecMassInitOperation. " + RslErrObj.Message );
    return 1;
END;  


PRIVATE MACRO __ExecuteStep( FD:SPFirstDoc, dat:DATE ):INTEGER
  if( FD != NULL ) // единичное исполнение
     ExecMassInitOperation(FD);
  else
     ExecMassInitOperation();

  end;

  return 0;
END;

MACRO ExecuteStep( Doc, ticket )


  record deal(dl_tick);
  var FD, dat;

  SetBuff(deal, ticket);
  FD = SPFirstDoc(deal);
  GetOprDate(0, dat);

  return __ExecuteStep(FD, dat);
END;

/* Массовое исполнение */
MACRO MassExecuteStep()


  return __ExecuteStep(NULL);
END;

MACRO PostStep (    
                CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,      /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

    
   record Deal_Tick (dl_tick);
   SetBuff( deal_tick, FDoc );

   Var Dt, cmd, sql = "", sql_upd = "";
   if (errTrn OR (CommitOrRollback == 2)) 
      /*если по графику нет соответствующей СОБУ или СОВУ то график откатываем апдейтом.*/
      sql = "SELECT t.t_dealcode, c.t_commcode, c.t_dockind \n" +
          "  FROM ddl_tick_dbt t, \n" +
          "       ddlgrdeal_dbt gr, \n" +
          "       ddlgrdoc_dbt grdoc, \n" +
          "       ddl_comm_dbt c \n" +
          " WHERE     t.t_dealid = ? \n" +
          "       AND t.t_dealid = gr.t_docid \n" +
          "       AND t.t_bofficekind = gr.t_dockind \n" +
          "       AND GR.T_ID = GRDOC.T_GRDEALID \n" +
          "       AND GRDOC.t_servdockind = C.T_DOCKIND \n" +
          "       AND GRDOC.T_SERVDOCID = c.T_documentid \n" +
          "       AND C.T_DOCKIND = ? " ; 

      sql_upd = "   UPDATE ddlgracc_dbt \n" +
           "   SET t_state = 1 \n" +
           " WHERE t_id IN \n" +
           "          (SELECT a.t_id \n" +
           "             FROM ddl_tick_dbt t, ddlgracc_dbt a, ddlgrdeal_dbt d \n" +
           "            WHERE     D.T_DOCID = t.t_dealid \n" +
           "                  AND d.t_dockind = t.t_bofficekind \n" +
           "                  AND A.T_GRDEALID = d.t_id \n" +
           "                  AND a.t_state = 2 \n" +
           "                  AND a.t_accnum = ? \n" +
           "                  AND t.t_dealid = ?) \n";

      cmd = RsdCommand(sql);
      cmd.AddParam("", RSDBP_IN, deal_tick.dealid);
      cmd.AddParam("", RSDBP_IN, 4615);/*СОБУ*/
      cmd = RsdRecordSet(cmd);
      if (not cmd.Movenext()) 
          cmd = RsdCommand(sql_upd);
          cmd.addParam("",RSDBP_IN, 2);/*БУ*/
          cmd.addParam("",RSDBP_IN, deal_tick.DealID);
          cmd.Execute();
      end;
      cmd = null;
      cmd = RsdCommand(sql);
      cmd.AddParam("", RSDBP_IN, deal_tick.dealid);
      cmd.AddParam("", RSDBP_IN, 4613);/*СОВУ*/
      cmd = RsdRecordSet(cmd);
      if (not cmd.Movenext())
          cmd = RsdCommand(sql_upd);
          cmd.addParam("",RSDBP_IN, 3);/*ВУ*/
          cmd.addParam("",RSDBP_IN, deal_tick.DealID);
          cmd.Execute();
      end;
   end;
   return 0;

end;
