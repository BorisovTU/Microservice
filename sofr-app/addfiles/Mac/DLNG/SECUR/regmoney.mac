/**
 @file regmoney.mac
 @brief ОТЧЕТ: "Регистр внутреннего учета денежных средств и расчетов по операциям с ценными бумагами"(БО)
 
 # menu
 - БО ЦБ\Внутренний учет\Отчеты\Регистры внутреннего учета\Регистр учета денежных средств и расчетов
  
 # tag
 - functional_block: Отчетность_Внутренняя
 - code_type: Report  

 # changeLog
 |date       |author         |tasks               |note                                                        
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2023.10.04 |Велигжанин А.В.|CCBO-7605           | Насыщение кода комментариями

*/

IMPORT SecInter, PtInter, "DlRepForm_h.mac", "sprepfun.mac";
IMPORT  "DLReport_h.mac";

PRIVATE CONST TYPE_CLIENT_ALL = -1;
PRIVATE CONST TYPE_CLIENT_SELECT = 1;

private VAR
   IsMoneyClient = false,
   IsBank = false, IsClient = false;

private var IsBankPrint;

private CONST ALL_ITEM = -1;

private VAR FirstSheet = "";
private VAR RepKind = "";
private VAR RepName = "Регистр внутреннего учета денежных средств и расчетов по операциям с ценными бумагами";

private VAR TableHeader1 =
"┌─────────────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬───────────────┬───────────────────────────────┬────────────────┬────────────┬──────────────┐\n"+
"│ Наименование расчетной  │      Вид        │        №        │      Вид        │       №         │ Вход. остаток │        Сумма операции         │ Комиссия банка │ Расходы по │ Исх. остаток │\n"+
"│       операции          │ сделки/операции │ сделки/операции │ подтверждающего │ подтверждающего │               ├────────────────┬──────────────┤                │  сделке    │              │\n"+
"│                         │     с ц/б       │     с ц/б       │    документа    │    документа    │               │ Сумма операции │ В т.ч. курс. │                │            │              │\n"+
"│                         │                 │                 │                 │                 │               │                │  разница     │                │            │              │\n"+
"├─────────────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼───────────────┼────────────────┼──────────────┼────────────────┼────────────┼──────────────┤";

private VAR TableHeader2 =
"┌─────────────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬────────────────┬────────────────┬───────────────────────────────┬────────────────┬────────────┬────────────────┬────────────────┐\n"+
"│ Наименование расчетной  │      Вид        │        №        │       Вид       │       №         │Вход. остаток по│Вход. остаток по│        Сумма операции         │ Комиссия банка │ Расходы по │ Исх. остаток по│ Исх. остаток по│\n"+
"│       операции          │ сделки/операции │ сделки/операции │ подтверждающего │ подтверждающего │ задолженности  │ обязательствам ├────────────────┬──────────────┤                │  сделке    │ задолженности  │ обязательствам │\n"+
"│                         │     с ц/б       │     с ц/б       │     документа   │    документа    │ банка перед    │ клиента перед  │ Сумма операции │ В т.ч. курс. │                │            │ банка перед    │ клиента перед  │\n"+
"│                         │                 │                 │                 │                 │ клиентом       │ банком         │                │  разница     │                │            │ клиентом       │ банком         │\n"+
"├─────────────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼────────────────┼────────────────┼────────────────┼──────────────┼────────────────┼────────────┼────────────────┼────────────────┤";

/**
@brief Класс реализации отчета, наследник класса DL_CReportTemplate
*/
private class (DL_CReportTemplate) regmoney (_Data)

   private VAR DlRepFormData = _Data, IsFirstUse = true;

   /**
   @brief Возвращает режим вывода отчета
   @return DL_OUTREPORT_EXCEL вывод в excel
   @return DL_OUTREPORT_STD вывод на экран
   */
   private macro PrintMode():INTEGER
      if(DlRepFormData.IsExportToExcel)
         return DL_OUTREPORT_EXCEL;
      else
         return DL_OUTREPORT_STD;
      end;
   end;

   /**
   @brief Завершение копирования таблицы
   */
   private macro EndCopyTable()
      EndTable();
      CopyAllSheetInTotalBook( NULL, false, GetTableRange(), 0 );
   end;

   /**
   @brief Завершение построения отчета
   */
   private macro EndReport()
      SaveTotalBook();
   end;

   /**
   @brief Регистрация таблицы
   */
   private macro Register_Table()
      if(IsBankPrint)
         RegisterTable(
            "N1_TableLine", TableHeader1,
            "N1_A2",    "N1_A3",    "N1_A4",    "N1_A5",    "N1_A6",
            "N1_A7",    "N1_A8",    "N1_A9",    "N1_A10",   "N1_A11",   "N1_A12"
         );
      else
         RegisterTable(
            "N1_TableLine", TableHeader2,
            "N1_A2",    "N1_A3",    "N1_A4",    "N1_A5",    "N1_A6",
            "N1_A7",    "N1_A8",    "N1_A9",    "N1_A10",   "N1_A11",
            "N1_A12",   "N1_A12_1", "N1_A12"
         );
      end;
   end;

   /**
   @brief Печать заголовка
   @param[in] s Наименование заголовка
   @param[in] movTo смещение
   */
   private macro PrintTitle(s:string, movTo)

      if( ValType(movTo) == V_UNDEF )
         movTo = 0;
      end;

      PrintFormatString(
         "#",
         "N1_Title", s
      );
      CopyAllSheetInTotalBook(NULL, false, "N1_Title", movTo);
   end;

   /**
   @brief Печать подзаголовка
   @param[in] s Наименование подзаголовка
   @param[in] movTo смещение
   */
   private macro PrintSubTitle(s:string, movTo)

      if( ValType(movTo) == V_UNDEF )
         movTo = 0;
      end;

      PrintFormatString(
         "#",
         "N1_SubTitle", s
      );
      CopyAllSheetInTotalBook(NULL, false, "N1_SubTitle", movTo);
   end;

   /**
   @brief Печать заголовка Head0
   @param[in] FirstSheet наименование вкладки
   @param[in] DepName наименование подразделения
   @param[in] onDate дата
   */
   private macro PrintHead0(FirstSheet, DepName, onDate) /*печать на новой вкладке*/

      PrintTitle("АО \"РОССЕЛЬХОЗБАНК\"");

      if( IsFirstUse )
         IsFirstUse = false;
      end;

      PrintTitle(RepName);

      if( IsBankPrint )
         PrintTitle("Счета учета денежных средств");
      else
         PrintTitle("Счета учета расчетов с клиентами по денежным средствам");
      end;

      if(ValType(onDate) == V_UNDEF)
         if( DlRepFormData.BeginDate != DlRepFormData.EndDate )
            PrintTitle(
               "Отчетный период c " + String(DlRepFormData.BeginDate) +
               " по " + String(DlRepFormData.EndDate)
            );
         else
            PrintTitle(
               "Отчетный период: " + String(DlRepFormData.BeginDate)
            );
         end;
      else
         PrintTitle(
               "Отчетный период: " + String(onDate)
         );
      end;

   end;

   /**
   @brief Печать заголовка Head1
   @param[in] IsClient Признак денежных средств клиентов
   */
   private macro PrintHead1( IsClient )
     if( IsClient )
       PrintSubTitle("Денежные средства клиентов", 1);
     else
       PrintSubTitle("Собственные денежные средства банка", 1);
     end;
   end;

   /**
   @brief Печать заголовка Head2
   @param[in] IsClient 		Признак денежных средств клиентов
   @param[in] AccNum 		Лицевой счет
   @param[in] Curency 		Валюта счета
   @param[in] ClientName 	Наименование клиента
   @param[in] ClientConc        
   @param[in] Place 		Место учета средств
   @param[in] Broker            Наименование брокера
   @param[in] BrokerContr	№ договора брокера
   @param[in] NameAcc
   */
   private macro PrintHead2( IsClient, AccNum, Curency, ClientName, ClientConc, Place, Broker, BrokerContr, NameAcc  )

      if( IsClient != false )
         PrintSubTitle("Клиент: " + string(ClientName) + " / договор № " + string(ClientConc),1);
      end;

      PrintSubTitle("Лицевой счет: " + string(AccNum) + "  " + string(NameAcc),1);
      PrintSubTitle("Валюта счета: " + string(Curency));

      if( IsMoneyClient != true )
         if( (IsClient != true) AND (Broker != null) )
            PrintSubTitle("Договор с " + string(Broker) + " № " + string(BrokerContr));
         end;
      end;

      CopyAllSheetInTotalBook( NULL, false, "N1_TableHeader", 1 );

      Register_Table();

   end;

   /**
   @brief Печать строки для даты
   @param[in] DDate 		Дата
   */
   private macro PrintLineDate( DDate:date )
      var i = 0, count;

      if( (DlRepFormData.DivideByDates) OR (DlRepFormData.BeginDate == DlRepFormData.EndDate) )
         return;
      end;

      if( IsBankPrint )
         if (DlRepFormData.IsExportToExcel)
            Merge("N1_A2", "N1_A12");
         end;

         PrintTableLine(
            "N1_A2",    " " + DateToStr(DDate), null,
            "N1_A3",    "",      null,
            "N1_A4",    "",      null,
            "N1_A5",    "",      null,
            "N1_A6",    "",      null,
            "N1_A7",    "",      null,
            "N1_A8",    "",      null,
            "N1_A9",    "",      null,
            "N1_A10",   "",      null,
            "N1_A11",   "",      null,
            "N1_A12",   "",      null
         );
      else
         if (DlRepFormData.IsExportToExcel)
            Merge("N1_A2", "N1_A12_1"); //надо бы "N1_A12_2", но на всю линию почему-то не объединяет
         end;

         PrintTableLine(
            "N1_A2",    " " + DateToStr(DDate), null,
            "N1_A3",    "",      null,
            "N1_A4",    "",      null,
            "N1_A5",    "",      null,
            "N1_A6",    "",      null,
            "N1_A7",    "",      null,
            "N1_A8",    "",      null,
            "N1_A9",    "",      null,
            "N1_A10",   "",      null,
            "N1_A11",   "",      null,
            "N1_A12",   "",      null,
            "N1_A12_1", "",      null,
            "N1_A12_2", "",      null
         );
      end;
   end;

   /**
   @brief Печать суммы (если сумма нулевая, она не показывается)
   @param[in] Sum 		Значение
   @param[in] IsPrint 		Флаг печати
   */
   private macro IsPrintNVL( Sum:money, IsPrint:bool )
     if( (not IsPrint) and (Sum == $0) )
        return "";
     end;
     return Sum;
   end;

   /**
   @brief Печать строки
   @param[in] NameOperation	Наименование операции
   @param[in] NumOperation	Номер операции
   @param[in] KindOperation	Вид операции
   @param[in] NumFirstDoc	Номер первичного документа
   @param[in] KindFirstDoc	Вид первичного документа
   @param[in] InRest1		Входящий Остаток 1
   @param[in] InRest2		Входящий Остаток 2
   @param[in] SumOperation	Сумма операции
   @param[in] RateSum		Ставка
   @param[in] BankComiss	Комиссия
   @param[in] PaymDeal		Величина платежа
   @param[in] OutRest1		Исходящий Остаток 1
   @param[in] OutRest2		Исходящий Остаток 2
   */
   private macro PrintLine(
         NameOperation:string,
         NumOperation:string,
         KindOperation:string,
         NumFirstDoc:string,
         KindFirstDoc:string,
         InRest1:money,
         InRest2:money,
         SumOperation:money,
         RateSum:money,
         BankComiss:money,
         PaymDeal:money,
         OutRest1:money,
         OutRest2:money
      )

      if( NumFirstDoc == null )
         NumFirstDoc = " ";
      end;

      if( KindFirstDoc == null )
         KindFirstDoc = " ";
      end;

      if( IsBankPrint )
         PrintTableLine(
            "N1_A2",    NameOperation,                      null,
            "N1_A3",    KindOperation,                      null,
            "N1_A4",    NumOperation,                       null,
            "N1_A5",    KindFirstDoc,                       null,
            "N1_A6",    NumFirstDoc,                        null,
            "N1_A7",    IsPrintNVL( InRest1, true ),        null,
            "N1_A8",    IsPrintNVL( SumOperation, false ),  null,
            "N1_A9",    IsPrintNVL( RateSum, false ),       null,
            "N1_A10",   IsPrintNVL( BankComiss, false ),    null,
            "N1_A11",   IsPrintNVL( PaymDeal, false ),      null,
            "N1_A12",   IsPrintNVL( OutRest1, true ),       null
         );
      else
         PrintTableLine(
            "N1_A2",    NameOperation,                      null,
            "N1_A3",    KindOperation,                      null,
            "N1_A4",    NumOperation,                       null,
            "N1_A5",    KindFirstDoc,                       null,
            "N1_A6",    NumFirstDoc,                        null,
            "N1_A7",    IsPrintNVL( InRest1, true ),        null,
            "N1_A8",    IsPrintNVL( InRest2, true ),        null,
            "N1_A9",    IsPrintNVL( SumOperation, false ),  null,
            "N1_A10",   IsPrintNVL( RateSum, false ),       null,
            "N1_A11",   IsPrintNVL( BankComiss, false ),    null,
            "N1_A12",   IsPrintNVL( PaymDeal, false ),      null,
            "N1_A12_1", IsPrintNVL( OutRest1, true ),       null,
            "N1_A12_2", IsPrintNVL( OutRest2, true ),       null
         );
      end;

   end;

   /**
   @brief Возвращает запрос для счетов для подразделения на дату
   @param[in] DepID		ID подразделения
   @return 			строка запроса
   */
   private macro GetDataAcc_rshb( DepID )

     VAR Query, Data, Categ, NRecData, DataCat;
     var cmdClient, dataClient;
     var IsForm, FormSub, IsResident, ResidentSub, IsRiskLevel, RiskLevelSub;
     var TypeClient = TYPE_CLIENT_ALL;
 
     if(IsBank)
       Categ = "530, 535, 536";/*"ДС Банка, ВУ", "ДС у брокера, ВУ" и "ДС в РЦ ОРЦБ, ВУ"*/
     else
       cmdClient = RSDCommand("SELECT COUNT (1) cnt FROM DDLACCREGCLNT_TMP");

       dataClient = TRsbDataSet(cmdClient);
       if( dataClient.MoveNext() )
         TypeClient = IIF (dataClient.cnt > 0, TYPE_CLIENT_SELECT, TYPE_CLIENT_ALL );
       end;

       Categ = "533";/*"ДС клиента, ВУ"*/
       IsForm = DlRepFormData.IsForm;
       FormSub =  DlRepFormData.FormSub;
       IsResident = DlRepFormData.IsResident;
       ResidentSub = DlRepFormData.ResidentSub;
       IsRiskLevel = DlRepFormData.IsRiskLevel;
       RiskLevelSub = DlRepFormData.RiskLevelSub;
     end;

     DataCat = TRsbDataSet("select listagg(t_id,',') WITHIN GROUP (order by t_id) CatIDList from DMCCATEG_DBT t where t.t_number in ("+Categ+")");
     DataCat.MoveNext() ;
     Categ = DataCat.CatIDList;

     Query = "select /*+ leading(acc_flt acc) index(acc DMCACCDOC_DBT_IDX1) */ distinct acc.t_Departmentid, acc.t_Account, acc.t_Place, acc.t_Chapter, acc.t_Currency ";

     if( IsBank )
       Query = Query + " , acc.t_BankContrID ";
     end;

     if( (IsClient) AND (NOT IsBank) )
       Query = Query + " , acc.t_Owner, acc.t_ClientcontrID, contr.t_ServKind, contrmp.t_MarketID ";
     end;

     Query = Query + " , fin.t_CCY, dacc.t_NameAccount "
                   + " FROM (select t_DebAcc t_Account from inacc_flt union select t_KredAcc from inacc_flt) acc_flt, dmcaccdoc_dbt acc, dfininstr_dbt fin, daccount_dbt dacc ";

     if( (IsClient) AND (NOT IsBank) )
       Query = Query + ", dclient_dbt client, dsfcontr_dbt contr, ddlcontrmp_dbt contrmp, dparty_dbt Pt ";
     end;

     Query = Query + " WHERE acc.t_catid in(" + string(Categ) + ") "+
                      "     and  acc.t_Account  = acc_flt.t_Account";

     if( (IsClient) AND (NOT IsBank) )
       if( TypeClient != TYPE_CLIENT_ALL )
         Query = Query + " AND acc.t_Owner IN (SELECT T_CLIENTID FROM DDLACCREGCLNT_TMP) ";
       else
         Query = Query + " AND ((acc.t_Owner != -1) OR (acc.t_Owner != " + string({OurBank}) + "))";
       end;

       Query = Query + " AND client.t_PartyID = acc.t_Owner " +
                       " AND client.t_Department = acc.t_DepartmentID " +
                       " AND client.t_PartyID = Pt.t_PartyID "+
                       " AND client.t_Branch = 0 ";

       if( (IsForm > 0) and (FormSub != 0) )
         Query = Query + "    and   Pt.T_LEGALFORM "+IIF(IsForm == 1, "= ", "!= ")+String(FormSub);  
       end;

       if( (IsResident > 0) and (ResidentSub != 0) )
         Query = Query + "    and   Pt.T_NOTRESIDENT ";
         if( ((IsResident == 1) and (ResidentSub == 1)) or ((IsResident == 2) and (ResidentSub == 2)) ) //Резидент
           Query = Query + " != 'X' ";
         else
           Query = Query + " = 'X' ";
         end;  
       end;

       if( (IsRiskLevel > 0) and (RiskLevelSub != 0) )
         Query = Query + " and "+IIF(IsRiskLevel == 1, "", "NOT")+" EXISTS (select 1 from dobjatcor_dbt atc where atc.t_ObjectType = 3 /*Субъект*/ and atc.t_GroupID = 95 /*категория риска*/ and atc.t_Object = LPAD(acc.t_Owner, 10, '0') and atc.t_AttrID = " + String (RiskLevelSub) + ")";  
       end;
     else
       Query = Query + " AND acc.t_Owner = -1 ";
     end;

     Query = Query + " AND acc.T_DEPARTMENTID = " + string(DepID) +
                     " AND acc.t_Chapter = 21 ";

     if( IsClient )
       Query = Query + " AND acc.t_ClientcontrID = contr.t_ID "
                     + " AND acc.t_ClientContrID = contrmp.t_SfContrID "
                     + " AND contrmp.t_MarketID = CASE WHEN  (rshb_rsi_sclimit.SfcontrIsEDP(acc.t_ClientContrID) = 1) THEN "+string(MMVB_ID)+" ELSE contrmp.t_MarketID END ";
     end;

     Query = Query + " AND fin.t_FIID = acc.t_Currency "
                    + " AND dacc.t_Account = acc.t_Account "
                    + " AND dacc.t_Chapter = acc.t_Chapter "
                    + " AND dacc.t_Code_Currency = acc.t_Currency ";

     return Query;
   end;


   /**
   @brief сумма проводки  по оплате комиссии бирже или посреднику
   @param[in] DocID		ID документа
   @param[in] DocKind		вид документа
   @param[in] Account		номер счета
   @param[in] ProDate		дата
   @return 			сумма
   */
   private macro GetPaymDeal( DocID:integer, DocKind:integer, Account:string, ProDate:date)
      VAR Select, Data, Comiss = $0;
      Select = RSDCommand("select NVL(Sum(inacc.t_Sum),0) PaymDeal " +
                          "from   ddlinacc_dbt inacc " +
                          " where  inacc.t_Date = ? " +
                          "   and  inacc.t_DocumentID = ? " +
                          "   and  inacc.t_DealKind = ? " +
                          "   and  (inacc.t_DebAcc = ? or inacc.t_KredAcc = ?) " +
                          "   and  inacc.t_Opertype in(9,10,12,14) " +
                          "   AND  (select count(1) from dacctrn_dbt arh " +
                          "          where arh.t_AccTrnID = inacc.t_AccTrnID " +
                          "            and arh.t_State = 1) > 0 ");/*комиссия посреднику*/

      Select.addParam("", RSDBP_IN, ProDate);
      Select.addParam("", RSDBP_IN, DocID);
      Select.addParam("", RSDBP_IN, DocKind);
      Select.addParam("", RSDBP_IN, Account);
      Select.addParam("", RSDBP_IN, Account);
      Select.addParam("", RSDBP_IN, NATCUR);
      Select.execute();

      Data = TRsbDataSet(Select);
      if( Data.MoveNext() )
         Comiss = Comiss + Data.PaymDeal;
      end;
      return Comiss;
   end;

   /**
   @brief сумма проводки по оплате комиссии банку
   @param[in] DocID		ID документа
   @param[in] DocKind		вид документа
   @param[in] Account		номер счета
   @param[in] ProDate		дата
   @return 			сумма
   */
   private macro GetComissBank( DocID:integer, DocKind:integer, Account:string, ProDate:date)
      VAR Select, Data, Comiss = $0;
      Select = RSDCommand( "select NVL(Sum(inacc.t_Sum),0) ComissBank " +
                           "from   ddlinacc_dbt inacc " +
                           " where  inacc.t_Date = ? " +
                           "   and  inacc.t_DocumentID = ? " +
                           "   and  inacc.t_DealKind = ? " +
                           "   and  (inacc.t_DebAcc = ? or inacc.t_KredAcc = ?) " +
                          "   and  inacc.t_Opertype in(8,11,13) " +
                          "   AND  (select count(1) from dacctrn_dbt arh " +
                          "          where arh.t_AccTrnID = inacc.t_AccTrnID " +
                          "            and arh.t_State = 1) > 0 ");/*комиссия банку*/

      Select.addParam("", RSDBP_IN, ProDate);
      Select.addParam("", RSDBP_IN, DocID);
      Select.addParam("", RSDBP_IN, DocKind);
      Select.addParam("", RSDBP_IN, Account);
      Select.addParam("", RSDBP_IN, Account);
      Select.addParam("", RSDBP_IN, NATCUR);
      Select.execute();

      Data = TRsbDataSet(Select);
      if( Data.MoveNext() )
         Comiss = Comiss + Data.ComissBank;
      end;
      return Comiss;
   end;

   /**
   @brief возвращает номер договора обслуживания
   @param[in] ContrID		ID договора
   @return 			номер договора
   */
   private macro ПолучитьДоговор( ContrID:integer )
   var Select, Data;
      Select = RSDCommand( "select t_Number NumConc from dsfcontr_dbt where t_id = ?" );
      Select.addParam("id", RSDBP_IN, ContrID);
      Select.execute();
      Data = TRsbDataSet(Select);
      if( Data.MoveNext() )
         return Data.NumConc;
      end;
      return "";
   end;

   /**
   @brief возвращает расчетную операцию
   @param[in] OperID		ID расчетной операции
   @return 			расчетная операция
   */
   private macro GetDlAcc(OperID)
      var Query, Data;

      Query = RSDCommand("SELECT * " +
                         "FROM ddl_acc_dbt " +
                         "WHERE t_ID = ?");

      Query.addParam("", RSDBP_IN, OperID);
      Query.execute();

      Data  = TRsbDataSet(Query);

      if( Data.MoveNext() )
         return  Data;
      end;

      msgbox("Не найдена РО ВУ с ID = " + string(OperID));
      return "";
   end;

   /**
   @brief возвращает наименование первичного документа
   @param[in] GroundKind	вид документа
   @param[in] XLD		не используется
   @return 			наименование документа
   */
   private macro GetFirstDoc( GroundKind, XLD)
      var query, Firstdoc, Doc = "";

      query = RSDCommand(" select t_Name from dllvalues_dbt " +
                        "  where t_List = " + string(OBJTYPE_KINDDOC) +
                        "    and t_element = ?"
                       );

      query.addParam("", RSDBP_IN, GroundKind);

      query.execute();

      Firstdoc = TRsbDataSet(query);

      if( Firstdoc.MoveNext() )
         Doc = string(FirstDoc.Name);
      end;

      return Doc;
   end;

   /**
   @brief запускает формирование отчета
   @param[in] Dep		подразделение
   */
   private macro ExecuteReports(Dep)
      VAR DataQuery, DataQueryAcc, PrevDep = "", PrevAcc = "", PrevProDate = Date(0, 0, 0), PrevPlace, PrevContr,
          PrevClient = "", PrevCurr, PrintClient = false,
          NameFDoc, NumFDoc, NumOperation, KindOperation,
          PaymDeal, ComissBank, IsPrintRepKind = false,
          InRest = $0, OutRest = $0, QueryDep, DataDep, InRest2 = $0, OutRest2 = $0,
          ExistInfo = false, IsInfo = false, Num = 0,
          continue_cicle = false, continue_cicleAcc = false, select_count, NRec = 0, NRecAcc = 0;

      /**
      @brief получает входящий остаток по счету
      @param[in] ProDate	дата
      @param[in] Currency	валюта
      @param[in] Acc		номер счета
      @return 			остаток
      */
      macro GetInRest( ProDate:date, Currency, Acc)
         var Rest = $0;
         var AccBuf = TRecHandler("account");
         ClearRecord( AccBuf );
         if( GetAccount( 21, Currency, Acc, AccBuf ) )/*по 21 Chapter*/
            Rest = GetRestAccount( AccBuf.rec, ProDate-1 );
         end;
         return Rest;
      end;

      /**
      @brief получает исходящий остаток по счету
      @param[in] InRest		входящий остаток
      @param[in] Sum		сумма
      @param[in] Comiss		комиссия
      @param[in] IsOut		вид операции (зачисление или списание) 
      @param[in] IsBank		признак получения или выплаты комиссии
      @return 			остаток
      */
      macro GetOutRest( InRest:money, Sum:money, Comiss:money, IsOut:integer, IsBank:bool )
         var Rest = $0;

         if( (IsOut == 1) or (IsOut == 2) )
            Rest = InRest + Sum;/*если производится зачисление на счет*/
         else
            Rest = InRest - Sum;/*если производится списание со счета*/
         end;

         if( IsBank )
            Rest = Rest + Comiss;
         else
            Rest = Rest - Comiss;
         end;

         return Rest;
      end;

      /**
      @brief возвращает неотрицательную сумму
      @param[in] SumAcc		сумма
      @return 			возвращает SumAcc, если она больше 0, иначе 0.
      */
      macro GetPositivRest( SumAcc )
         if( SumAcc > $0 )
            return SumAcc;
         else
            return $0;
         end;
      end;

      /**
      @brief возвращает положительную сумму
      @param[in] SumAcc		сумма
      @return 			возвращает абсолютное значение SumAcc, если она меньше 0, иначе 0.
      */
      macro GetAbsRest( SumAcc )
         if( SumAcc < $0 )
            return Abs( SumAcc );
         else
            return $0;
         end;
      end;

      /**
      @brief формирование строки данных
      */
      macro FormDataString()
         var Client = "", ClientContr = "", Place, Broker, BrokerContr, StrError = "";
         var ЭтоКомиссияБанку = ((DataQuery.Opertype == 8) or (DataQuery.Opertype == 11) or (DataQuery.Opertype == 13));
         var ЭтоКомиссияБиржеПосреднику = ((DataQuery.Opertype == 9) or (DataQuery.Opertype == 10) or (DataQuery.Opertype == 12) or (DataQuery.Opertype == 14) );

         if( IsBank != false)
            if( DataQuery.BankContrID != -1 )/*Категория "ДС у Брокера, ВУ"*/
               Broker = ПолучитьКороткоеИмяСубъекта(DataQuery.Place);
               BrokerContr = ПолучитьДоговор( DataQuery.BankContrID );
            else
               Broker = BrokerContr = null;
            end;
         else
            Client = ПолучитьКороткоеИмяСубъекта(DataQuery.Owner);/*имя и договор клиента*/
            ClientContr = ПолучитьДоговор( DataQuery.ClientcontrID );
         end;

         InRest = OutRest;

         Place = ПолучитьКороткоеИмяСубъекта(DataQuery.Place);

         if( (IsBank != false) and  (IsClient == false) and ( (PrevAcc != DataQuery.Acc) or
                    (PrevPlace != DataQuery.Place) or (PrevCurr != int(DataQuery.Curr)))
                  )
                    if(ExistInfo == true)
                       EndCopyTable();
                    end;
                    PrintHead2( false, DataQuery.Acc, DataQuery.CurrName, null, null, Place, Broker, BrokerContr, DataQuery.NameAcc );
                    PrevProDate = Date(0,0,0);
                    PrevAcc = DataQuery.Acc;
                    PrevCurr = int(DataQuery.Curr);
                    PrevPlace = DataQuery.Place;
                    InRest = GetInRest( DataQuery.ProDate, int(DataQuery.Curr), DataQuery.Acc);
         elif( (IsBank == false) and  (IsClient == true) and (IsMoneyClient == false) and
                  ((PrevClient != DataQuery.Owner) or (PrevContr != DataQuery.ClientcontrID) or (PrevAcc != DataQuery.Acc) or
                     (PrevPlace != DataQuery.Place) or (PrevCurr != int(DataQuery.Curr)))
                   )
                    if(ExistInfo == true)
                       EndCopyTable();
                    end;

                    if( (PrevClient != DataQuery.Owner) or (PrevContr != DataQuery.ClientcontrID) )
                         PrintClient = true;
                    end;

                    PrintHead2( PrintClient, DataQuery.Acc, DataQuery.CurrName, Client, ClientContr, Place,  null, null, DataQuery.NameAcc);
                    PrevProDate = Date(0,0,0);
                    PrevAcc = DataQuery.Acc;
                    PrevCurr = int(DataQuery.Curr);
                    PrevClient = DataQuery.Owner;
                    PrevContr = DataQuery.ClientcontrID;
                    PrevPlace = DataQuery.Place;
                    InRest = GetInRest( DataQuery.ProDate, int(DataQuery.Curr), DataQuery.Acc);

                    PrintClient = false;
         elif( (IsBank == false) and  (IsClient == true) and (IsMoneyClient == true) and
                 ( (PrevClient != DataQuery.Owner) or (PrevContr != DataQuery.ClientcontrID) or (PrevAcc != DataQuery.Acc) or
                     (PrevCurr != int(DataQuery.Curr))))

                    if(ExistInfo == true)
                       EndCopyTable();
                    end;
                    if( (PrevClient != DataQuery.Owner) or (PrevContr != DataQuery.ClientcontrID) )
                         PrintClient = true;
                    end;

                    PrintHead2( PrintClient, DataQuery.Acc, DataQuery.CurrName, Client, ClientContr, null, null, null, DataQuery.NameAcc);
                    PrevProDate = Date(0,0,0);
                    PrevAcc = DataQuery.Acc;
                    PrevCurr = int(DataQuery.Curr);
                    PrevClient = DataQuery.Owner;
                    PrevContr = DataQuery.ClientcontrID;
                    PrevPlace = DataQuery.Place;

                    InRest = GetInRest( DataQuery.ProDate, int(DataQuery.Curr), DataQuery.Acc);

                    PrintClient = false;
         end;

         if( PrevProDate != DataQuery.ProDate )
            PrintLineDate( DataQuery.ProDate );
            PrevProDate = DataQuery.ProDate;
         end;

         NameFDoc = GetFirstDoc( DataQuery.GroundKind, DataQuery.GroundNum );
         NumFdoc = DataQuery.GroundNum;
         if ( (NumFdoc == "") or (NumFdoc == null) or (NumFdoc == "Undefined") )
           NumFdoc = " ";
           NameFDoc = " ";
         end;
         if( not SP_GetOperKindAndCodeByInAcc( DataQuery, @KindOperation, @NumOperation, @StrError ) )
           msgbox(StrError);
         end;
         if( (DataQuery.Opertype == 20) OR (DataQuery.Opertype == 21) )/*оплата сделки или аванса*/
            PaymDeal = GetPaymDeal( DataQuery.DocID, DataQuery.DealKind, DataQuery.Acc, DataQuery.ProDate);
            if( IsBank != true)
               ComissBank = GetComissBank( DataQuery.DocID, DataQuery.DealKind, DataQuery.Acc, DataQuery.ProDate);
            else
               ComissBank = $0;
            end;
         else
            if( ЭтоКомиссияБанку )
               ComissBank = DataQuery.Sum;
               DataQuery.Sum = $0;
               PaymDeal = $0;
            elif( ЭтоКомиссияБиржеПосреднику )
               PaymDeal = DataQuery.Sum;
               DataQuery.Sum = $0;
               ComissBank = $0;
            else
               ComissBank = $0;
               PaymDeal = $0;
            end;
         end;

         if( (IsMoneyClient == true) AND (IsClient == true) ) /*Счета учета расчетов с клиентами по денежным средствам*/

            OutRest = GetOutRest( InRest, DataQuery.Sum, (ComissBank + PaymDeal), DataQuery.IsOut, false );

            PrintLine(
               DataQuery.OperName,
               NumOperation, KindOperation,
               NumFDoc,  NameFDoc,
               GetPositivRest(InRest), GetAbsRest(InRest), DataQuery.Sum,
               DataQuery.RateSum, ComissBank,
               PaymDeal,
               GetPositivRest(OutRest), GetAbsRest(OutRest)
            );
         else /*денежные средства банка*/
            OutRest = GetOutRest( InRest, DataQuery.Sum, (ComissBank + PaymDeal), DataQuery.IsOut, true );

            PrintLine(
               DataQuery.OperName,
               NumOperation, KindOperation,
               NumFDoc, NameFDoc,
               InRest, null,
               DataQuery.Sum, DataQuery.RateSum,
               ComissBank, PaymDeal, OutRest, null
            );
         end;
         ExistInfo = true;
         IsInfo = true;
      end;

      /**
      @brief печать отчета по подразделению
      @param[in] DepCode	код подразделения
      @param[in] IsPrintClient	признак печати данных по клиенту
      @param[in] onDate		дата
      */
      macro PrintDep_rshb( DepCode, IsPrintClient:bool, onDate:date )
         BegAction(100,"Вычисление данныx для отчета");
         var first = true;
         var QueryAcc = GetDataAcc_rshb(DepCode);

         continue_cicleAcc = true;

         var Query = " with inacc_flt as ("+
                     " select /*+ materialize index(inacc DDLINACC_DBT_IDX5)*/ lvalues.t_Name OperName, inacc.t_id, inacc.t_debacc, inacc.t_kredacc "+
                     "  from  ddlinacc_dbt inacc, dllvalues_dbt lvalues where " ;
         if( ValType(onDate) == V_UNDEF )
             Query = Query +
                      "   inacc.t_Date >=" + GetSQLDate(DlRepFormData.BeginDate) +
                      "   AND inacc.t_Date <=" + GetSQLDate(DlRepFormData.EndDate);
         else
             Query = Query +
                      "   inacc.t_Date =" + GetSQLDate(onDate);
         end;
         Query = Query + " and 1 = (case when ( (inacc.t_DealKind = "+DL_CALCOPER+" or inacc.t_DocumentKind = "+DL_CALCOPER+") and inacc.t_DocumentID != 0) " +
                         "               then (select count(1) " +
                         "                       from ddl_acc_dbt dlacc" +
                         "                      where dlacc.t_ID = inacc.t_DocumentID and dlacc.t_Boffice = 5 ) else 1 end ) " +
                         "   AND lvalues.T_LIST = " + string(OBJTYPE_CALCOPKIND) +
                         "   AND lvalues.T_ELEMENT = inacc.t_Opertype " +
                         "   AND exists (select 1 from dacctrn_dbt arh " +
                         "         where arh.t_AccTrnID = inacc.t_AccTrnID " +
                         "           and arh.t_State = 1) " ;

         Query = Query + " ) "+
        " SELECT /*+ leading(inacc_flt,DataQueryAcc) */ count(*) over() Rec_count, DataQueryAcc.t_DepartmentID DepID, DataQueryAcc.t_Account Acc, DataQueryAcc.t_NameAccount NameAcc, DataQueryAcc.t_Currency Curr, " +
                           "        inacc.t_Sum Sum, inacc.t_RateSum RateSum, inacc_flt.OperName, DataQueryAcc.t_Place Place, DataQueryAcc.t_CCY CurrName, " +
                           "        inacc.t_GroundKind, inacc.t_GroundNum, " +
                           "        inacc.t_Date ProDate, inacc.t_DocumentID DocID, inacc.t_DocumentKind DocKind, " +
                           "        inacc.t_Opertype Opertype, inacc.t_DealKind DealKind, " +
                           "        case when Rsb_Secur.GetDealID(inacc.t_DocumentKind,inacc.t_DocumentID) != 0 " +
                           "             then Rsb_Secur.GetDealCode(inacc.t_DocumentKind,inacc.t_DocumentID) " +
                           "             else utl_raw.cast_to_varchar2(dbms_lob.substr(inacc.t_FmtBlobData_XXXX,2000)) end DealNumber, "+ /*в DealNumber сохраняются номера сделок в РОВУ и при вводе проводок руками*/
                           "        Rsb_Secur.GetDealID(inacc.t_DocumentKind,inacc.t_DocumentID) DealID, inacc.t_PartyContrID, " + 
                           "        case when inacc.t_DocumentKind IN ("+DL_SECURITYDOC+", "+DL_DVDEAL+", "+DL_DVFXDEAL+") "+
                           "             then 0 " +
                           "             else 1 " + 
                           "        end isNotDeal, " +
                           "        case when inacc.t_DocumentKind = " + DL_SECURITYDOC +
                           "             then (select tickdeal.t_DealTime from ddl_tick_dbt tickdeal where tickdeal.t_DealID = inacc.t_DocumentID) " +
                           "             when inacc.t_DocumentKind = " + DL_DVDEAL +
                           "             then (select dvdeal.t_Time from ddvdeal_dbt dvdeal where dvdeal.t_ID = inacc.t_DocumentID) " +
                           "             when inacc.t_DocumentKind = " + DL_DVFXDEAL +
                           "             then (select dvndeal.t_Time from ddvndeal_dbt dvndeal where dvndeal.t_ID = inacc.t_DocumentID) " +
                           "             else null "+
                           "        end DealTime, ";

         if( NOT IsBank )
            Query = Query + " DataQueryAcc.t_ClientcontrID ClientcontrID, DataQueryAcc.t_Owner Owner, ";
         end;

         if( IsBank )
            Query = Query + " DataQueryAcc.t_BankContrID BankContrID, ";
         end;

         Query = Query +
                      "        (case when(inacc.t_DealKind = "+DL_CALCOPER+" or inacc.t_DocumentKind = "+DL_CALCOPER+") " +
                      "              then DECODE(inacc.t_DebAcc, DataQueryAcc.t_Account ,4,1)              " +
                      "              else DECODE(inacc.t_DebAcc, DataQueryAcc.t_Account ,3,2) end) IsOut,  " +
                      "         DECODE(inacc.t_DebAcc, DataQueryAcc.t_Account,1,0) InOut1 " +
                      "  FROM ("+QueryAcc+") DataQueryAcc, inacc_flt ,ddlinacc_dbt inacc " +
                      " WHERE ((inacc_flt.t_DebAcc = DataQueryAcc.t_Account ) or (inacc_flt.t_KredAcc = DataQueryAcc.t_Account)) "+
                      "   and inacc_flt.t_id = inacc.t_id "  ;

          /*должны попасть и ручные проводки*/
         Query = Query + 
                               " AND (case when inacc.t_opertype in (8,9,10,11,12,13,14) then (select TO_NUMBER(count(1)) " +  /*отбираем проводки по комиссиям, не связанные с оплатой\авансом*/
                               "                                                                 from ddlinacc_dbt com" +
                               "                                                                where com.t_Date = inacc.t_date  " +
                               "                                                                  and com.t_DocumentID = inacc.t_DocumentID " +
                               "                                                                  and com.t_DealKind = inacc.t_DealKind " +
                               "                                                                  and ((com.t_DebAcc = DataQueryAcc.t_Account) or (com.t_KredAcc = DataQueryAcc.t_Account))" +
                               "                                                                  and com.t_Opertype in(20,21)) else 0 end ) = 0 ";

         if( NOT isBank ) //Проверяем договор клиента по операции
           Query = Query + " AND (( DataQueryAcc.t_ClientcontrID = " + 
                           " CASE "+
                           "  WHEN inacc.t_DocumentKind IN ("+DL_DVFXDEAL+", "+DL_DVNDEAL+") THEN "+
                           "   (SELECT t_ClientContr "+
                           "      FROM ddvndeal_dbt "+
                           "     WHERE t_id = inacc.t_DocumentID) "+
                           " WHEN inacc.t_DocumentKind = "+DL_DVDEAL+" THEN "+
                           "   (SELECT t_ClientContr  "+
                           "      FROM ddvdeal_dbt  "+
                           "     WHERE t_id = inacc.t_DocumentID)  "+
                           " WHEN inacc.t_DealKind = "+DL_SECURITYDOC+" THEN "+
                           "   (SELECT t_ClientContrID "+
                           "      FROM ddl_tick_dbt "+
                           "     WHERE t_dealid = inacc.t_DocumentID) "+
                           " WHEN inacc.t_DocumentKind = "+DL_CALCOPER+" THEN "+
                           "   (SELECT t_ClientContr "+
                           "      FROM ddl_acc_dbt "+
                           "     WHERE t_id = inacc.t_DocumentID) "+
                           " WHEN inacc.t_DocumentKind = "+DL_WRTMONEY+" THEN"+
                           "   (SELECT t_Contract "+
                           "      FROM dnptxop_dbt "+
                           "     WHERE t_id = inacc.t_DocumentID) "+
                           " ELSE DataQueryAcc.t_ClientcontrID END )  "+
                           " OR (DataQueryAcc.t_ClientcontrID = " +
                           " CASE "+
                           "  WHEN  inacc.t_DocumentKind = "+DL_WRTMONEY+" AND (rshb_rsi_sclimit.SfcontrIsEDP(DataQueryAcc.t_ClientcontrID) = 1 AND DataQueryAcc.t_servKind = 1 AND DataQueryAcc.t_MarketID = 2) OR (rshb_rsi_sclimit.SfcontrIsEDP( DataQueryAcc.t_ClientcontrID) != 1) THEN "+
                           "   (SELECT cntrmp.t_SfcontrID "+
                           "      FROM dnptxop_dbt   nptxop, ddlcontrmp_dbt cntrmp, ddlcontr_dbt  dlcontr "+
                           "     WHERE nptxop.t_id = inacc.t_DocumentID "+
                           "       AND cntrmp.t_DlContrID = dlcontr.t_DlContrID "+
                           "       AND cntrmp.t_SfContrID = DataQueryAcc.t_ClientcontrID "+
                           "       AND dlcontr.t_SfContrId = nptxop.t_contract) "+
                           " ELSE -1 END ) ) ";
         end;

         Query = Query + " ORDER BY DataQueryAcc.t_DepartmentID, ";
         if( NOT IsBank )
             Query = Query + " DataQueryAcc.t_Owner, DataQueryAcc.t_ClientcontrID, ";
         end;
         Query = Query + " DataQueryAcc.t_Account, inacc.t_Date, InOut1, isNotDeal, DealTime, inacc.t_ID ";
         var sql = RsdCommand(Query);
         sql.execute();
         DataQuery = TRsbDataSet(sql);
         if( DataQuery.MoveNext() )

           continue_cicle = true;

           while( continue_cicle )
              if( first )
                 EndAction(100);
                 InitProgressBar(int(DataQuery.rec_count), "Отчет: " + RepName, "Формирование отчета " + RepKind);

                 if( PrevDep != DepCode )

                    if( ExistInfo == true )
                         ExistInfo = false;
                         EndCopyTable();
                    end;

                    PrintHead0(DepCode, DL_GetDepartmentNameByCode(DepCode), onDate);
                    PrevDep = DepCode;
                 end;

                 if( IsPrintRepKind )
                   if( ExistInfo == true )
                     ExistInfo = false;
                     EndCopyTable();
                   end;
                   PrintHead1(IsPrintClient);
                   IsPrintRepKind = false;
                 end;
                 first = false;
              end;
              UseProgressBar;
              FormDataString();
              continue_cicle = DataQuery.MoveNext();
           end;
           RemProgressBar;
        else
           EndAction(100);
        end;
      end;

      /**
      @brief формирование данных для отчета
      @param[in] DepCode	код подразделения
      @param[in] IsClientCalc	признак формирования данных по клиенту
      @param[in] onDate		дата
      */
      macro FormBankClientMoneyOnDate( DepCode, IsClientCalc:bool, onDate:date )
        if( not IsClientCalc )
          if( DlRepFormData.IsBankAccounts == SET_CHAR )
            IsBank = true;
            IsClient = false;
            IsMoneyClient = false;
            IsPrintRepKind = true;

            PrintDep_rshb(DepCode, IsClient, onDate);
          end;
         else
          if( DlRepFormData.IsClientsAccounts == SET_CHAR )
            IsBank = false;
            IsClient = true;
            IsMoneyClient = true;
            IsPrintRepKind = true;
          
            PrintDep_rshb(DepCode, IsClient, onDate);
          end; 
        end;
      end;

      /**
      @brief формирование данных для отчета
      @param[in] DepCode	код подразделения
      @param[in] IsClientCalc	признак формирования данных по клиенту
      */
      macro FormBankClientMoney(DepCode, IsClientCalc:bool)
         var onDate:date;

         if(DlRepFormData.DivideByDates)
            onDate = DlRepFormData.BeginDate;
            while(onDate <= GetDateAfterWorkDays(DlRepFormData.EndDate,0))
               PrevDep = "";
               PrevAcc = "";
               PrevProDate = Date(0, 0, 0);
               PrevClient = "";
               FormBankClientMoneyOnDate(DepCode,IsClientCalc,onDate);
               onDate = GetDateAfterWorkDays(onDate,1);
            end;
         else
            FormBankClientMoneyOnDate(DepCode,IsClientCalc);
         end;

         if(ExistInfo)
            EndCopyTable();
         end;
      end;

      /**
      @brief формирование данных по подразделению
      @param[in] DepCode	код подразделения
      */
      macro FormDepartment(DepCode)
        /*Счета учета денежных средств*/
        if( (IsBankPrint) AND (DlRepFormData.IsBankAccounts == SET_CHAR) ) 
          ExistInfo = false;
          FormBankClientMoney(DepCode, false);
        end;
        /*Счета учета расчетов с клиентами по денежным средствам*/
        if( (not IsBankPrint) AND (DlRepFormData.IsClientsAccounts == SET_CHAR) )
          ExistInfo = false;
          FormBankClientMoney(DepCode, true);
        end;
      end;

      // Основная часть макроса ExecuteReports
      if( (DlRepFormData.IsBankAccounts == SET_CHAR) OR (DlRepFormData.IsClientsAccounts == SET_CHAR) )
        FormDepartment(Dep);
      end;
      return IsInfo;
   end;

   /**
   @brief Начало отчета
   */
   private macro BeginReport()
      var noData = "Нет данных, удовлетворяющих параметрам отчета.";

      CreateTotalBook();
      SetActiveSheet( "L1" );

      if( not ExecuteReports({OperDprt}) ) //Только по головному подразделению
         PrintFormatString("#\n","N1_NoData", noData);

         CopyAllSheetInTotalBook( NULL, false, "N1_NoData", 0 );
      else
         AddStandardFooter("N1_");
         CopyAllSheetInTotalBook(NULL, false, "N1_Footer", 1);
      end;
   end;

   if(IsBankPrint)
     initDL_CReportTemplate(NULL, "Report_regmoney1.xlsx", NULL, NULL, NULL, true, true);
   else
     initDL_CReportTemplate(NULL, "Report_regmoney2.xlsx", NULL, NULL, NULL, true, true);
   end;

end; /*class regmoney*/

/**
@brief Построение отчета "Счета учета денежных средств"
@param[in] DataFromForm		дата, на которую формируется отчет
*/
macro MakeReport1( DataFromForm )
  RepKind = "Счета учета денежных средств";

  IsBankPrint = true;
  regmoney(DataFromForm).Run();
end;

/**
@brief Построение отчета "Счета учета расчетов с клиентами по денежным средствам"
@param[in] DataFromForm		дата, на которую формируется отчет
*/
macro MakeReport2( DataFromForm )
  RepKind = "Счета учета расчетов с клиентами по денежным средствам";

  IsBankPrint = false;
  regmoney(DataFromForm).Run();
end;
