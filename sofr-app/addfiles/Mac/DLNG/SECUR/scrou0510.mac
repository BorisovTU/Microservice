/*
$Name:        scrou0510.mac
$Module:      Ценные бумаги
$Description: Операция РЕПО без признания ц/б (не ОРЦБ). Оплата по 2 ч
*/
import InsCarryDoc, "sp_class.mac", "sp_car.mac", "sp_cars.mac", "sp_carb.mac";

Private macro IsCloseContr(FD)

    var SelectClose, DataSetClose, QueryClose;
    var cnt = 0;
    var MaxGrDate:date = date(31,12,9999);
    QueryClose = "select * from ddlgrdeal_dbt where t_DocKind = ? and t_DocID = ? and t_TemplNum in(51,52) " ;
    SelectClose = DL_RSDCommand(QueryClose);
    SelectClose.AddParam(FD.tick.rec.BofficeKind);
    SelectClose.AddParam(FD.tick.rec.DealID);

    cnt = SelectClose.GetCount(QueryClose);     // если не запланировано закрытие договора, то ничего не вставляем
    if( cnt == 0 )
      return 0;
    end;
    return 1;
end;

Private macro IsStatusRQ(FD, statusRQ, ID_RQ)     // все ТО или только по сторой части?
    var stat = 0;
    var Select, DataSet, Query;
    Query = "SELECT * FROM ddlrq_dbt"
          + " WHERE  t_DocKind = ?"
          + " AND t_DocID = ? "
          + " AND t_state = ? "            // статус
          + " AND t_type IN (2, 8) "       // интересуют только оплата и поставка?
          + " AND t_dealPart = 2 "         // только по второй части?
          + " AND t_ID <> ? " ;

    Select = DL_RSDCommand(Query);
    Select.AddParam(FD.tick.rec.BofficeKind);
    Select.AddParam(FD.tick.rec.DealID);
    Select.AddParam(statusRQ);
    Select.AddParam(ID_RQ);

    stat = Select.GetCount(query);

    return stat;
end;

Private macro IsExecuteRQ(FD, ID_RQ)     
    var stat = 0;
    var Select, DataSet, Query;
    Query = "SELECT * FROM ddlrq_dbt"
          + " WHERE  t_DocKind = ?"
          + " AND t_DocID = ? "
          + " AND t_state <> 2 "           // неисполненые
          + " AND t_type IN (2, 8) "       // интересуют только оплата и поставка?
          + " AND t_dealPart = 2 "         // только по второй части?
          + " AND t_ID <> ? " ;

    Select = DL_RSDCommand(Query);
    Select.AddParam(FD.tick.rec.BofficeKind);
    Select.AddParam(FD.tick.rec.DealID);
    Select.AddParam(ID_RQ);

    stat = Select.GetCount(query);

    return stat;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step)
  record  deal(dl_tick);
  var FD, dat, FD1;
  var rq_money = TRecHandler("dlrq.dbt");

  var requestTimeout = 0;
//  GetRegistryValue("РЕПОЗИТАРИЙ\\СРОК ОЖИД.ЗАПР. ПРИ КОМБ.ПОДТВ.", V_INTEGER, requestTimeout, null);
  var ОжиданиеЗапроса = false;
  var repozdeal = TBFile("dl_repozdeal.dbt", "R", 1);

  SetBuff( deal, FDoc ); 
  FD = SPFirstDoc( deal, true );
  FD1 = SPFirstDoc( deal, false );

  if( FD.БылОтказОтИсполнения() != false )
     return 0;
  end;

  if( FD1.БылОтказОтИсполнения() != false )
     return 0;
  end;
  GetOprDate( FD.GetKindDate(DATE_DEALPAY), dat );
  if( dat > {curdate} )
    MsgBox ("Преждевременное выполнение шага \"Оплата по 2 ч\" запрещено.");
    return 1;
  end;


  if( FD.ПроверитьОплаченностьАвансаЗадатка() == false ) 
     return 1;
  end; 

  rq_money = FD.GetRQ(DLRQ_TYPE_PAYMENT);

  if( ПроверитьТОНаПросроченность( rq_money, "оплате" ) == false )
     return 1;
  end;

  if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_PAYMENT2, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
    return 1;
  end;

  if( (not ТОСквитовано(rq_money)) and (УстановитьСтатусТО(rq_money, DLRQ_STATE_EXEC, Dat) != 0) )
    return 1;
  end;

  if(FD.ExistPC and (not ТОСквитовано(FD.GetRQ(DLRQ_TYPE_INCREPO))))
    if(УстановитьСтатусТО(FD.GetRQ(DLRQ_TYPE_INCREPO), DLRQ_STATE_EXEC, dat) != 0)
      return 1;
    end;
  end;

  if (FD.IsOldDeal())
     /* О2 = "Завершить" */
     if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_O2, SP_OPERSTATUS_SECURDEALS_O2_FINISH) ) 
        msgbox( "Ошибка при установке статуса <Оплата по 2 ч> операции" );
        return 1;
     end;
  end;


  if ((IsREPO( FD.Group )) and IsCloseContr(FD) and РАБОТА_С_РЕПОЗИТАРИЕМ())
    repozdeal.Clear();
    repozdeal.rec.DocKind = FD.tick.rec.BofficeKind;
    repozdeal.rec.DocID   = FD.tick.rec.DealID;
    if( GetEQ(repozdeal) )
      if( (repozdeal.rec.INITIATOR == 2) and    (repozdeal.rec.METHOD_WAYTWOWAY == 1))
        ОжиданиеЗапроса = true;
        requestTimeout = repozdeal.rec.requestTimeout;
      end;
      if(repozdeal.rec.reporting == SET_CHAR)  //если флаг репортинг в репозитарий установлен
        var CloseRQ = TRecHandler( "dlrq" );
        var CurrRQ = TRecHandler( "dlrq" );
        var DateExecution = Date(0,0,0);

        var Select, DataSet, Query;
        Query = "  SELECT rq.* "
              + "    FROM ddlrq_dbt rq, ddlrqbc_dbt bc "
              + "   WHERE     t_DocKind = ? "
                    + "   AND rq.t_DocID = ? "
                    + "   AND rq.t_dealPart = 2 "
                    + "   AND rq.t_type IN (2, 8) "
                    + "   AND rq.t_state <>  " + DLRQ_STATE_EXEC
                    + "   AND bc.t_rqid(+) = rq.t_id "
                    + "   AND bc.t_instance(+) = 0 "
                    + "   AND (   (rq.t_plandate = "
                                 + "  (SELECT MAX (NVL (bc.t_plandate, rq.t_plandate)) "
                                 + "     FROM ddlrq_dbt rq, ddlrqbc_dbt bc "
                                 + "    WHERE     t_DocKind = ? "
                                        + "   AND rq.t_DocID = ? "
                                        + "   AND rq.t_dealPart =  " + DLRQ_STATE_EXEC
                                        + "   AND rq.t_type IN (2, 8) "
                                        + "   AND rq.t_state <> 2 "
                                        + "   AND bc.t_rqid(+) = rq.t_id "
                                        + "   AND bc.t_instance(+) = 0)) "
                           + " OR bc.t_plandate = "
                               + "   (SELECT MAX (NVL (bc.t_plandate, rq.t_plandate)) "
                               + "      FROM ddlrq_dbt rq, ddlrqbc_dbt bc "
                               + "     WHERE     t_DocKind = ? "
                                     + "     AND rq.t_DocID = ? "
                                     + "     AND rq.t_dealPart =  " + DLRQ_STATE_EXEC
                                     + "     AND rq.t_type IN (2, 8) "
                                     + "     AND rq.t_state <> 2 "
                                     + "     AND bc.t_rqid(+) = rq.t_id "
                                     + "     AND bc.t_instance(+) = 0)) "
                                     ;
      
        Select = DL_RSDCommand(Query);
        Select.AddParam(FD.tick.rec.BofficeKind);
        Select.AddParam(FD.tick.rec.DealID);
        Select.AddParam(FD.tick.rec.BofficeKind);
        Select.AddParam(FD.tick.rec.DealID);
        Select.AddParam(FD.tick.rec.BofficeKind);
        Select.AddParam(FD.tick.rec.DealID);
      
        var cnt = Select.GetCount(query);
        if( cnt == 1) 
          DataSet = Select.Execute();
          if(DataSet.moveNext())
            DataSet.GetRecord().CopyTo( CloseRQ.rec );
          end;
          CurrRQ = FD.GetRQ(DLRQ_TYPE_PAYMENT);
    
          if(CloseRQ.rec.ID == CurrRQ.rec.ID )       // если изменения закрывающего ТО

            if(WeheChangeRQ(CurrRQ.rec.ID))
              return 0;
            end;
    
            if( not IsExecuteRQ(FD, CurrRQ.rec.ID)) // если нет неисполненных
              return 0; 
            elif(IsStatusRQ(FD, DLRQ_STATE_OVERDUE, CurrRQ.rec.ID))  //если есть просроченое ТО

               DateExecution = GetPlanDateGr(FD, DLGR_TEMPL_OVERDUEDELIVERY2);  //дата просрочки поставки

               if( ОжиданиеЗапроса and requestTimeout)
                 if(DL_InsertGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_TEMPL_EXECDELAYMSGWTHREQUEST, GetDateAfterWorkDays(DateExecution, requestTimeout, FD.ПолучитьКалендСвязанный()) , time(0,0,0), FD.tick.rec.PFI) != 0 )
                   return 1;
                 end;
                 if( DL_SetDateGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_CLOSEDEAL, date(31,12,9999)) != 0 )
                   return 1;
                 end;
               else
                 if(DL_InsertGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_TEMPL_EXECDELAYMSG, DateExecution, time(0,0,0), FD.tick.rec.PFI) != 0 )
                   return 1;
                 end;
                 if( DL_SetDateGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_CLOSEDEAL, date(31,12,9999)) != 0 )
                   return 1;
                 end;
               end;
            elif(IsStatusRQ(FD, DLRQ_STATE_DELAYED, CurrRQ.rec.ID))  //отложенное ТО

               DateExecution = GetPlanDateGr(FD, DLGR_TEMPL_PROLONGDELIVERY2); 
               if( ОжиданиеЗапроса and requestTimeout)
                 if(DL_InsertGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_TEMPL_EXECHOLDMSGWTHREQUEST, GetDateAfterWorkDays(DateExecution, requestTimeout, FD.ПолучитьКалендСвязанный()) , time(0,0,0), FD.tick.rec.PFI) != 0 )
                   return 1;
                 end;
                 if( DL_SetDateGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_CLOSEDEAL, date(31,12,9999)) != 0 )
                   return 1;
                 end;
               else
                 if(DL_InsertGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_TEMPL_EXECHOLDMSG, DateExecution, time(0,0,0), FD.tick.rec.PFI) != 0 )
                   return 1;
                 end;
                 if( DL_SetDateGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_CLOSEDEAL, date(31,12,9999)) != 0 )
                   return 1;
                 end;
               end;
            end;
          end;
        end;
      end;
    end;
  end;
  return 0;
end;
