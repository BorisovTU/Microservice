/*
$Name: boswift.mac
$Module: Депозитарий
$Description: создание информационного сообщения
*/

import OprInter, PTInter, PaymInter, CTInter, SPInter, sp_class, "dpimp.mac" , "spRepFun.mac";

import RsbDataSet;

private   var     FD, FD2;
private   var RowNum;
private   var Direct;







macro FillPartyShort( PartyID, PartyCodeKind, PartyCode:@variant, PartyName:@variant )
  record party(party);

  if( PartyCode != NULL )
    PartyCode = ПолучитьКодСубъекта(PartyID, PartyCodeKind);
  end;
  if( ПолучитьСубъекта(PartyID, party) == 0 )
    PartyName = party.ShortName;
  else
    PartyName = "";
  end;
end;

private macro  ЗаполнитьСчетРазделДепо(Account,SELLDEPOACC:@variant,SELLDEPOPART:@variant)                   
  var query, DataSet, cmd;
  var queryAcc, DataSetAcc, cmdAcc;
  query = "Select acnt.t_AutoKey AutoKey, acnt.t_Root Root, acnt.t_BriefCode Code From ddepoacnt_dbt acnt Where acnt.t_BriefCode = ?" ;
  cmd = DL_RSDCommand(query);
  cmd.AddParam(Account);
  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    if(DataSet.AutoKey==DataSet.Root)
      SELLDEPOACC = DataSet.Code;
      SELLDEPOPART = "";
    else
      SELLDEPOPART = DataSet.Code;
      queryAcc = "Select acnt.t_BriefCode Code From ddepoacnt_dbt acnt Where acnt.t_AutoKey = ? ";
      cmdAcc = DL_RSDCommand(queryAcc);
      cmdAcc.AddParam(DataSet.Root);
      DataSetAcc = cmdAcc.Execute();
      if( DataSetAcc.moveNext() )
        SELLDEPOACC = DataSetAcc.Code;
      end;
    end;
  end;
end;

private macro GenGsett(GenagrID, GSETT:@variant);                      // DL_GENAGR
  var query, DataSet, cmd;
  query = "Select gen.t_code Code, gen.t_Start St From ddl_genagr_dbt gen Where gen.t_GenagrID = ?" ;
  cmd = DL_RSDCommand(query);
  cmd.AddParam(GenagrID);
  DataSet = cmd.Execute();
  if( DataSet.moveNext() )
    GSETT =  DataSet.Code + " " + date_as_string(1, DataSet.St);
  end;
end;

class CPMMesDL(DealID)
  var T_DRAFTID           = DealID;
  var T_RECEIVERBIC       = "";
  var T_CA                = "";
  var T_TRADTYPE          = "";
  var T_MIC               = "";
  var T_DEALID            = "";
  var T_DEALDATE          = date(0,0,0);
  var T_DEALTIME          = time(0);
  var T_SETTDATE          = date(0,0,0);
  var T_GSETT             = "";  
  var T_ISSUENAME         = "";
  var T_ISIN              = "";
  var T_CFI               = "";
  var T_MATURITY          = date(0,0,0);
  var T_INTR              = $0.0;
  var T_LSIN              = "";
  var T_ISSUEOTHRCODEKIND = "";
  var T_ISSUEOTHRCODE     = "";
  var T_ISSUENOMINAL      = $0.0;
  var T_ISSUENOMCURRENCY  = "";
  var T_BLOCKED           = "NO";
  var T_NEEDMACH          = "";
  var T_AMOUNT            = $0.0;
  var T_PRICE             = $0.0;
  var T_PRICEPRC          = $0.0;
  var T_DAY               = "";
  var T_SUM               = $0.0;
  var T_SUMCURRENCY       = "";
  var T_COUP              = $0.0;
  var T_COUPCURRENCY      = "";
  var T_SUMNC             = $0.0;
  var T_SUMNCCURRENCY     = "";
  var T_EXCH              = $0.0;
  var T_EXCH1             = $0.0;
  var T_EXCHCUR           = "";
  var T_DIRECTION         = 0;
  var T_DVP               = 0;
  var T_REPO2             = date(0,0,0);
  var T_RATE              = $0.0;
  var T_INTEREST          = $0.0;
  var T_OPERTYPE          = "";
  var T_CLIENT            = "";  
  var T_REPO              = 0;  
  var T_SELLBIC           = "";
  var T_SELLNAME          = "";
  var T_SELLDEPOACC       = "";
  var T_SELLDEPOPART      = "";
  var T_DEAGBIC           = "";
  var T_DEAGNAME          = "";
  var T_DEAGDEPOACC       = "";
  var T_DEAGDEPOPART      = "";
  var T_PSETBIC           = "";
  var T_PSETNAME          = "";
  var T_PSETBIC1          = "";
  var T_PSETNAME1         = "";
  var T_REAGBIC           = "";
  var T_REAGNAME          = "";
  var T_REAGDEPOACC       = "";
  var T_REAGDEPOPART      = "";
  var T_BUYRBIC           = "";
  var T_BUYRNAME          = "";
  var T_BUYRDEPOACC       = "";
  var T_BUYRDEPOPART      = "";
  var T_DEALSUM           = $0.0;
  var T_DEALCURRENCY      = "";
  var T_PAYEBIC           = "";
  var T_PAYENAME          = "";
  var T_PAYECASH          = "";
  var T_ACCWBIC           = "";
  var T_ACCWNAME          = "";
  var T_ACCWCASH          = "";
  var T_BENMBIC           = "";
  var T_BENMNAME          = "";
  var T_BENMCASH          = "";
end;


private macro получитьНашКорСчет()
  var query, DataSet, cmd;
  var queryAcc, DataSetAcc, cmdAcc;
  query = "Select bank.t_Coracc Coracc From dbankdprt_dbt bank Where bank.t_PartyID = ?" ;
  cmd = DL_RSDCommand(query);
  cmd.AddParam({ourbank});
  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    return DataSet.Coracc;
  end;
  return "";
end;


//заполнение структуры
private macro FillPaymentsMessageData( FD , PMMsg, KindMsg , PartTwo)
  var stat = 0, KindOper = 0, ReceiverID, PayerID, PayerDepID, ClientID, RecDepositID, PayDepositID;
  var query, cmd, DataSet;
  var ContrPay, ContrCer;  // договора Продавца, Покупателя.

  var rq      = TRecHandler("dlrq.dbt");               // поставка
  var rq_dev2 = TRecHandler("dlrq.dbt");               // поставка 2ч.
  var rqacc   = TRecHandler("dlrqacc");                // продажа
  var rq_pay  = TRecHandler("dlrq.dbt");               // ТО по оплате
  var rq_payPrc  = TRecHandler("dlrq.dbt");            // ТО по процентам
  var sa      = TRecHandler("settacc");                // СПИ
  var Banksa  = TRecHandler("settacc");                // СПИ
  var PayRqAcc  = TRecHandler("dlrqacc");              // Платежные реквизиты продавца
  var RecRqAcc  = TRecHandler("dlrqacc");              // Платежные реквизиты покупателя
  var PayRqAccBank  = TRecHandler("dlrqacc");          // Платежные реквизиты банка продавца
  var RecRqAccBank  = TRecHandler("dlrqacc");          // Платежные реквизиты банка покупателя
  record fiwarnts(fiwarnts);
  var  DaysInYear   = 0, DaysInCoupon = 0;

  RowNum = RowNum + 1;
  PMMsg.T_DEALSUM = FD.dl_leg.rec.TotalCost;                                         //Сумма расчетов из сделки
  PMMsg.T_DEALCURRENCY = GetFICode( FD.dl_leg.rec.CFI, null, FICK_ISOSTRING );       //Валюта
  PMMsg.T_DEALID = FD.tick.rec.DealCodeTS;                                                  //Номер сделки
  PMMsg.T_DEALDATE = FD.tick.rec.DealDate;                                                  //Дата сделки 
  PMMsg.T_DEALTIME = FD.tick.rec.DealTime;                                                  //Время сделки

  if( IsEXCHANGE(FD.Group) == true )
    PMMsg.T_TRADTYPE = "EXCH";
    FillPartyShort(FD.tick.rec.MarketID, PTCK_MIC, @PMMsg.T_MIC, NULL);
  else
    PMMsg.T_TRADTYPE = "OTCO";
  end;
    /*получить плановую дату поставки ц/б*/
  PMMsg.T_SETTDATE = FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.PlanDate;

  if(FD.tick.rec.GenagrID > 0)
    GenGsett(FD.tick.rec.GenagrID, @PMMsg.T_GSETT);
  end;
  // Покупка
  if(Direct)                             //покупка    Субъект - контрагент
    if(FD.tick.rec.PartyID == -1)              //продавец
      PayerID ={OurBank};
    else
      PayerID = FD.tick.rec.PartyID;
    end;
    if( FD.tick.rec.ClientID != -1 )                    //покупатель
      ReceiverID = FD.tick.rec.ClientID;
      ClientID   = FD.tick.rec.ClientID;
    else
      ReceiverID = {OurBank};
      ClientID   = {OurBank};
    end;
    PMMsg.T_DIRECTION = 1;
    ContrCer = FD.tick.rec.ClientContrID;  //покупатель является клиентом
    ContrPay = FD.tick.rec.PartyContrID;   //продавец является контрагентом
    RecDepositID = FD.Dl_leg.rec.DepositID;
    FillPartyShort(RecDepositID, PTCK_SWIFT, @PMMsg.T_PSETBIC1, @PMMsg.T_PSETNAME1);      // Код / Краткое наименование депозитария покупателя              
  
    if( IsRepo(FD.Group))    
      PayDepositID = FD2.Dl_leg.rec.DepositID;
      FillPartyShort(PayDepositID, PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME);      // Код / Краткое наименование депозитария продавца              
    end;
  end;

  // Продажа
  if( not Direct)                  //продажа   НашБанк или Клиент НашегоБанкам 
    if( FD.tick.rec.ClientID != -1 )                    //проверять ли на то, что клиент- наш банк?
      PayerID  = FD.tick.rec.ClientID;
      ClientID = FD.tick.rec.ClientID;
    else
      ClientID = {OurBank};
      PayerID  = {OurBank};
    end;
    if(FD.tick.rec.PartyID == -1)              //покупатель
      ReceiverID ={OurBank};
    else
      ReceiverID = FD.tick.rec.PartyID;
    end;
    PMMsg.T_DIRECTION = 2;
    ContrPay = FD.tick.rec.ClientContrID;  //покупатель является клиентом
    ContrCer = FD.tick.rec.PartyContrID;   //продавец является контрагентом
  
    PayDepositID = FD.Dl_leg.rec.DepositID;
    FillPartyShort(PayDepositID, PTCK_SWIFT, @PMMsg.T_PSETBIC, @PMMsg.T_PSETNAME);      // Код / Краткое наименование депозитария продавца              
    if( IsRepo(FD.Group))    
      RecDepositID = FD2.Dl_leg.rec.DepositID;
      FillPartyShort(RecDepositID, PTCK_SWIFT, @PMMsg.T_PSETBIC1, @PMMsg.T_PSETNAME1);      // Код / Краткое наименование депозитария покупателя              
    end;
  end;

  FillPartyShort(FD.tick.rec.PartyID, PTCK_SWIFT, @PMMsg.T_RECEIVERBIC, NULL);               //Получатель   // контрагент

  query = " SELECT fi.t_Name t_IssueName, " +
          "        fi.t_DrawingDate,   "
          "        avr.t_ISIN t_ISIN, " +
          "        avr.t_LSIN t_LSIN, " +
          "        CASE " +
          "           WHEN rsi_rsbparty.PT_GetPartyCode (?, 1) = 'НРД' " +
          "           THEN " +
          "              'NSD' " +
          "           ELSE " +
          "              CHR (1) " +
          "        END t_IssueOthrCodeKind, " +
          "        CASE " +
          "           WHEN rsi_rsbparty.PT_GetPartyCode (?, 1) = 'НРД' " +
          "           THEN " +
          "              (SELECT oc.t_Code " +
          "                 FROM DOBJCODE_DBT oc " +
          "                WHERE     oc.t_ObjectType = 9 " +
          "                      AND oc.t_CodeKind = 14 " +
          "                      AND oc.t_ObjectID = fi.t_FIID) " +
          "           ELSE " +
          "              CHR (1) " +
          "        END t_IssueOthrCode, " +
          "        rsi_rsb_fiinstr.FI_GetNominalOnDate (fi.t_FIID, ?, 1) t_IssueNominal, " +
          "        (SELECT fvc.t_Ccy FROM dfininstr_dbt fvc WHERE fvc.t_FIID = fi.t_FaceValueFI) t_IssueNomCurrency " +
          "        from DFININSTR_DBT fi, DAVOIRISS_DBT avr " +
          "        where fi.t_FIID = ? " +
          "        AND avr.t_FIID = fi.t_FIID ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(ReceiverID);
  cmd.AddParam(ReceiverID);
  cmd.AddParam(PMMsg.T_SETTDATE);
  cmd.AddParam(FD.tick.rec.Pfi);

  DataSet = cmd.Execute();

  if( DataSet.moveNext() )
    PMMsg.T_ISSUENAME         = DataSet.T_ISSUENAME        ;
    PMMsg.T_ISIN              = DataSet.T_ISIN             ;
    PMMsg.T_LSIN              = DataSet.T_LSIN             ;
    PMMsg.T_ISSUEOTHRCODEKIND = DataSet.T_ISSUEOTHRCODEKIND;
    PMMsg.T_ISSUEOTHRCODE     = DataSet.T_ISSUEOTHRCODE    ;
    PMMsg.T_ISSUENOMINAL      = DataSet.T_ISSUENOMINAL     ;
    PMMsg.T_ISSUENOMCURRENCY  = DataSet.T_ISSUENOMCURRENCY ;
    PMMsg.T_MATURITY          = DataSet.T_DRAWINGDATE      ;

  end;
  PMMsg.T_CFI = ПолучитьКодФинИн(FD.tick.rec.Pfi, FICK_CFIC) ;
  PMMsg.T_BLOCKED = "NO";
  PMMsg.T_NEEDMACH = "";                          // пока не заполняется

  PMMsg.T_AMOUNT = FD.DL_LEG.rec.Principal;
  if( not (FD.DL_LEG.rec.Relativeprice == SET_CHAR))    
    PMMsg.T_PRICE = FD.DL_LEG.rec.Price;
  else
    PMMsg.T_PRICEPRC = FD.DL_LEG.rec.Price;
  end;

  PMMsg.T_COUP = FD.DL_LEG.rec.NKD;
  PMMsg.T_COUPCURRENCY = GetFICode(FD.DL_LEG.rec.NKDFIID,  null, FICK_ISOSTRING);               // ISO код  для поля NKD         

  PMMsg.T_SUMNC = FD.DL_LEG.rec.COST;
  PMMsg.T_SUMNCCURRENCY = GetFICode(FD.DL_LEG.rec.CFI,  null, FICK_ISOSTRING);                  // ISO код  для поля Стоимость

  PMMsg.T_RATE = FD.DL_LEG.rec.Incomerate;
  PMMsg.T_INTEREST = FD.DL_LEG.rec.ReturnIncome;                     //сумма процентов


  PMMsg.T_OPERTYPE = "TRAD";               //пока только TRAD
  rq_pay = FD.GetRQ(DLRQ_TYPE_PAYMENT);
  var    PayerAccount = TRecHandler("account.dbt"); 
//   ПолучитьНашСчетТОПоОплате( rq_pay, FD, {curdate}, PayerAccount, PayerAccount );
  // Если сделки РЕПО
  if(IsRepo(FD.Group))            
    rq_dev2 = FD.GetRQ(DLRQ_TYPE_DELIVERY,2);            
    PMMsg.T_REPO2 = rq_dev2.rec.PlanDate;                           
    PMMsg.T_DAY = DL_GetNameAlg( 3121 /*ALG_KIND_BASISCALC*/, FD.DL_LEG.rec.Basis);              
    PMMsg.T_REPO = 1;
  end;

  if(FD.DL_LEG.rec.Formula == 50)                  //Поставка против платежа
    PMMsg.T_DVP = 1; 
  elif(FD.DL_LEG.rec.Formula == 49)
    PMMsg.T_DVP = 2;                               //Поставка без платежа
  end;                                             

  PMMsg.T_SUM = rq_pay.rec.Amount;                                                      
  PMMsg.T_SUMCURRENCY = GetFICode(rq_pay.rec.FIID,  null, FICK_ISOSTRING);               // ISO код  для поля Sum
  if(PartTwo)
    rq_payPrc = FD.GetRQ(DLRQ_TYPE_INCREPO);
    PMMsg.T_SUM = PMMsg.T_SUM + rq_payPrc.rec.Amount;
  end;

  if(FD.DL_LEG.rec.CFI != FD.DL_LEG.rec.PAYFIID)
    var curr = $1.0;
    PMMsg.T_EXCHCUR =  GetFICode(FD.DL_LEG.rec.PAYFIID,  null, FICK_ISOSTRING);
    PMMsg.T_EXCH = readNoteForObject( OBJTYPE_SECDEAL, UniID( FD.tick, OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_CALCRATE, FD.tick.rec.DealDate );
    if((PMMsg.T_EXCH == 0) and (FD.DL_LEG.rec.PAYFIID == NATCUR ))
      if(SmartConvertSumDbl(curr,curr, FD.tick.rec.DealDate, FD.DL_LEG.rec.CFI , FD.DL_LEG.rec.PAYFIID))
       PMMsg.T_EXCH = curr;
      end;
    end;
    PMMsg.T_EXCH1 = readNoteForObject( OBJTYPE_SECDEAL, UniID( FD.tick, OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_CALCRATE2, FD.tick.rec.DealDate );
    if((PMMsg.T_EXCH == 0) and (FD.DL_LEG.rec.PAYFIID == NATCUR ))
      if(SmartConvertSumDbl(curr,curr, FD.tick.rec.DealDate, FD.DL_LEG.rec.CFI , FD.DL_LEG.rec.PAYFIID))
       PMMsg.T_EXCH1 = curr;
      end;
    end;

  end;
  // платежные реквизиты продавца  Депозитарные
  if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, PayerID, DLRQ_SUBKIND_AVOIRISS, ALLFININSTR , DLRQ_TYPE_UNKNOWN, PayRqAcc) != 0) /*нет подходящих платежных реквизитов*/
    if(SP_GetPartySETTACC(PayerID, 0, ALLFININSTR, FIKIND_AVOIRISS, 0, sa) == 0)
      ЗаполнитьСчетРазделДепо(sa.rec.Account,@PMMsg.T_SELLDEPOACC,@PMMsg.T_SELLDEPOPART);
      FillPartyShort(sa.rec.BankID, PTCK_SWIFT, @PMMsg.T_DEAGBIC, @PMMsg.T_DEAGNAME);      // Код / Краткое наименование депозитария продавца               
        if(SP_GetPartySETTACC(sa.rec.BankID, 0, ALLFININSTR, FIKIND_AVOIRISS, 0, Banksa) == 0)
          ЗаполнитьСчетРазделДепо(Banksa.rec.Account,@PMMsg.T_DEAGDEPOACC,@PMMsg.T_DEAGDEPOPART);
        end;
    end;
  else
    ЗаполнитьСчетРазделДепо(PayRqAcc.rec.Account,@PMMsg.T_SELLDEPOACC,@PMMsg.T_SELLDEPOPART);
    FillPartyShort(PayRqAcc.rec.BankID, PTCK_SWIFT, @PMMsg.T_DEAGBIC, @PMMsg.T_DEAGNAME);      // Код / Краткое наименование депозитария продавца               
    if(SP_GetPartySETTACC(PayRqAcc.rec.BankID, 0, ALLFININSTR, FIKIND_AVOIRISS, 0, Banksa) == 0)
      ЗаполнитьСчетРазделДепо(Banksa.rec.Account,@PMMsg.T_DEAGDEPOACC,@PMMsg.T_DEAGDEPOPART);
    end;
  end;

  // платежные реквизиты покупателя  Депозитарные           DL_LEG
  if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, ReceiverID, DLRQ_SUBKIND_AVOIRISS, ALLFININSTR , DLRQ_TYPE_UNKNOWN, RecRqAcc) != 0) /*нет подходящих платежных реквизитов*/
    if(SP_GetPartySETTACC(ReceiverID, 0, ALLFININSTR, FIKIND_AVOIRISS, 0, sa) == 0)
      ЗаполнитьСчетРазделДепо(RecRqAcc.rec.Account,@PMMsg.T_BUYRDEPOACC,@PMMsg.T_BUYRDEPOPART);
      FillPartyShort(sa.rec.BankID, PTCK_SWIFT, @PMMsg.T_REAGBIC, @PMMsg.T_REAGNAME);      // Код / Краткое наименование депозитария покупателя               
        if(SP_GetPartySETTACC(sa.rec.BankID, 0, ALLFININSTR, FIKIND_AVOIRISS, 0, Banksa) == 0)
          ЗаполнитьСчетРазделДепо(Banksa.rec.Account,@PMMsg.T_REAGDEPOACC,@PMMsg.T_REAGDEPOPART);
        end;
    end;

  else
    ЗаполнитьСчетРазделДепо(RecRqAcc.rec.Account,@PMMsg.T_BUYRDEPOACC,@PMMsg.T_BUYRDEPOPART);
    FillPartyShort(RecRqAcc.rec.BankID, PTCK_SWIFT, @PMMsg.T_REAGBIC, @PMMsg.T_REAGNAME);      // Код / Краткое наименование депозитария покупателя               
    if(SP_GetPartySETTACC(RecRqAcc.rec.BankID, 0, ALLFININSTR, FIKIND_AVOIRISS, 0, Banksa) == 0)
      ЗаполнитьСчетРазделДепо(Banksa.rec.Account,@PMMsg.T_REAGDEPOACC,@PMMsg.T_REAGDEPOPART);
    end;
  end;
  ///платежные реквизиты продавца ЦБ // лицевой счет    (получателя ДС)
  if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, PayerID, DLRQ_SUBKIND_CURRENCY, FD.DL_LEG.rec.PayFIID , DLRQ_TYPE_UNKNOWN, PayRqAcc) != 0) /*нет подходящих платежных реквизитов*/
    if(SP_GetPartySETTACC(PayerID, ContrPay, FD.DL_LEG.rec.PayFIID, FIKIND_CURRENCY, 0, sa) == 0)
      FillPartyShort(sa.rec.BankID, PTCK_SWIFT, @PMMsg.T_ACCWBIC, @PMMsg.T_ACCWNAME);      // Код / Краткое наименование депозитария продавца  ЦБ (получателя ДС)             
      PMMsg.T_BENMCASH = sa.rec.Account;
        if(SP_GetPartySETTACC(sa.rec.BankID, ContrPay, FD.DL_LEG.rec.PayFIID, FIKIND_CURRENCY, 0, Banksa) == 0)
          PMMsg.T_ACCWCASH = Banksa.rec.CorrAcc;   // корсчет
        end;
      if( Direct)  //продажа
        PMMsg.T_CA = sa.rec.CorrAcc;
      end;
    end;
  else
    FillPartyShort(PayRqAcc.rec.BankID, PTCK_SWIFT, @PMMsg.T_ACCWBIC, @PMMsg.T_ACCWNAME);      // Код / Краткое наименование депозитария продавца ЦБ (получателя ДС)    DL_TICK   DL_LEG        
    PMMsg.T_BENMCASH = PayRqAcc.rec.Account;
    if(SP_GetPartySETTACC(PayRqAcc.rec.BankID, ContrPay, FD.DL_LEG.rec.PayFIID, FIKIND_CURRENCY, 0, Banksa) == 0)
          PMMsg.T_ACCWCASH = Banksa.rec.CorrAcc;      //корсчет
    end;
    if( Direct)  //продажа
      PMMsg.T_CA = PayRqAcc.rec.CorrAcc;
    end;

  end;

  // платежные реквизиты покупателя ЦБ // лицевой счет  (отправителя ДС)
  if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(FD.tick.rec.BofficeKind, FD.tick.rec.DealID, ReceiverID, DLRQ_SUBKIND_CURRENCY, FD.DL_LEG.rec.PayFIID , DLRQ_TYPE_UNKNOWN, RecRqAcc) != 0) /*нет подходящих платежных реквизитов*/
    if(SP_GetPartySETTACC(ReceiverID, ContrCer, FD.DL_LEG.rec.PayFIID, FIKIND_AVOIRISS, 0, sa) == 0)
      PMMsg.T_PAYECASH = sa.rec.Account;
      if( not Direct)  //покупка
        PMMsg.T_CA = sa.rec.CorrAcc;
      end;
    end;

  else
    PMMsg.T_PAYECASH = RecRqAcc.rec.Account;
    if( not Direct)  //покупка
      PMMsg.T_CA = RecRqAcc.rec.CorrAcc;
    end;
  end;
  PMMsg.T_DEALSUM = FD.DL_LEG.rec.Cost;

  if(PMMsg.T_CA == "")
    PMMsg.T_CA = получитьНашКорСчет();
  end;

  FillPartyShort(PayerID, PTCK_SWIFT, @PMMsg.T_SELLBIC, @PMMsg.T_SELLNAME);      // Код / Краткое наименование  продавца  конечного отправителя ЦБ по сделке             
  FillPartyShort(ReceiverID, PTCK_SWIFT, @PMMsg.T_BUYRBIC, @PMMsg.T_BUYRNAME);   // Код / Краткое наименование  покупателя  конечного получателя ЦБ по сделке             
  FillPartyShort(ClientID, PTCK_SWIFT, NULL, @PMMsg.T_CLIENT);                   //  Краткое наименование клиента  ЦБ по сделке             

  FillPartyShort(ReceiverID, PTCK_SWIFT, @PMMsg.T_PAYEBIC, @PMMsg.T_PAYENAME);   // Код / Краткое наименование  отправителя ДС             
  FillPartyShort(PayerID, PTCK_SWIFT, @PMMsg.T_BENMBIC, @PMMsg.T_BENMNAME);      // Код / Краткое наименование  получателя ДС             

  // купон
  var    Nominal = GetFaceValue( FD.fininstr.rec.FIID, {Curdate} );

/*Dan затычка на время тестирования*/
  if (KindMsg == "MT518")
   PMMsg.t_payecash =  substr(PMMsg.t_payecash , strbrk( PMMsg.t_payecash,"/")+1);
   PMMsg.t_psetbic =    PMMsg.t_sellbic;  
   PMMsg.t_psetname =    PMMsg.t_sellname;
  end;


  if (FI_IsCouponAvoiriss(FD.fininstr))
     if (FI_GetCouponOnDate(FD.fininstr.rec.FIID, {Curdate}, fiwarnts) == 0)
        /* Проверка, действует ли купон на дату отчета */
        if( {Curdate} >= fiwarnts.FirstDate )
           PMMsg.T_INTR = fiwarnts.IncomeRate;
           if (PMMsg.T_INTR == 0)
              ПолучитьПараметрыБазисаРасчетаКупона(FD.avoiriss.rec.NKDBase_Kind, 
                                        fiwarnts, {Curdate}, DaysInYear, DaysInCoupon);

              PMMsg.T_INTR = 100.0 * DaysInYear * fiwarnts.IncomeVolume / (Nominal * DaysInCoupon);
           end;
        end;
     end;
  end;
end;

private macro ClearPaymentsMessageData()
  var stat = 0;

  SQL_Execute(" DELETE FROM DDRAFTSINFO1_TMP ", null, false );

  return stat;

  OnError(err);
    return 1;
end;

private macro InsertPaymentsMessageData(PMMsg)
    var cmd = RSDCommand( " INSERT INTO DDRAFTSINFO1_TMP ( " +
                                      " T_DRAFTID, " +                        //Идентификатор поручения 
                                      " T_RECEIVERBIC, " +                    //Получатель
                                      " T_CA, " +                             //Номер корсчета НашБанк
                                      " T_TRADTYPE, " +                       //Место сделки
                                      " T_MIC, " +                            //Место сделки
                                      " T_DEALID, " +                         //Номер сделки
                                      " T_DEALDATE, " +                       //Дата сделки
                                      " T_DEALTIME, " +                       //время сделки
                                      " T_SETTDATE, " +                       //Дата расчета
                                      " T_GSETT, " +                          //Номер и дата заключения ген.согл.

///Параметры ценной бумаги
                                      " T_ISSUENAME, " +                      //Наименование ценной бумаги 
                                      " T_ISIN, " +                           //ISIN из справочника ценных бумаг 
                                      " T_CFI, " +                            //CFI-код 
                                      " T_MATURITY, " +                       //Дата погашения 
                                      " T_INTR, " +                           //Текущая ставка купона 
                                      " T_LSIN, " +                           //Код государственной регистрации из справочника ценных бумаг 
                                      " T_ISSUEOTHRCODEKIND, " +              //Вид кода
                                      " T_ISSUEOTHRCODE, " +                  //Код, указанного вида (заполняется, если заполнен атрибут IssueOthrCodeKind)
                                      " T_ISSUENOMINAL, " +                   //Текущий непогашенный номинал
                                      " T_ISSUENOMCURRENCY, " +               //Валюта номинала ценной бумаги 
                                      " T_BLOCKED, " +                        //Признак наличия обременения ценных бумаг 
                                      " T_NEEDMACH, " +                       //Признак необходимости встречного поручения
                                      " T_AMOUNT, " +                         //Количество ц/б
                                      " T_PRICE, " +                          //Цена одной ценной бумаги
                                      " T_PRICEPRC, " +                       //Цена одной ценной бумаги в процентах 
                                      " T_DAY, " +                            //База дней для расчета процентов
                                      " T_SUM, " +                            //Сумма сделки
                                      " T_SUMCURRENCY, " +                    //ISO-код валюты сделки
                                      " T_COUP, " +                           //Сумма НКД
                                      " T_COUPCURRENCY, " +                   //ISO-код валюты НКД
                                      " T_SUMNC, " +                          //Стоимость
                                      " T_SUMNCCURRENCY, " +                  //ISO-код валюты 
                                      " T_EXCH, " +                           //Курс
                                      " T_EXCH1, " +                          //Курс расчетов 2ч.        
                                      " T_EXCHCUR, " +                        //Валюта расчетов
                                      " T_DIRECTION, " +                      //Направление сделки
                                      " T_DVP, " +                            //Признак платежа
                                      " T_REPO2, " +                          //Дата исполнения второй части сделки РЕПО
                                      " T_RATE, " +                           //Ставка РЕПО
                                      " T_INTEREST, "  +                      //Сумма процентов
                                      " T_OPERTYPE, " +                       //условия поставки ц/б / тип расчетной операции
                                      " T_CLIENT, " +                         //Краткое наименование клиента
                                      " T_REPO, "  +                          //Идентификация сделки РЕПО
//Продавец  конечный отправитель ЦБ 
                                      " T_SELLBIC, " +                        //Идентификация стороны 
                                      " T_SELLNAME, " +
                                      " T_SELLDEPOACC, " +                    //Счет депо стороны
                                      " T_SELLDEPOPART, " +
//Депозитарий продавца (отправителя) ценных бумаг
                                      " T_DEAGBIC, " +                        //идентификация стороны
                                      " T_DEAGNAME, " +
                                      " T_DEAGDEPOACC, " +                    //Счет депо стороны
                                      " T_DEAGDEPOPART, " +
                                      " T_PSETBIC, " +                        //идентификация стороны
                                      " T_PSETNAME, " +
                                      " T_PSETBIC1, " +                        //идентификация стороны
                                      " T_PSETNAME1, " +
//Депозитарий покупателя (получателя) ценных бумаг
                                      " T_REAGBIC, " +
                                      " T_REAGNAME, " +
                                      " T_REAGDEPOACC, " +
                                      " T_REAGDEPOPART, " +
//Покупатель  конечный получатель ЦБ 
                                      " T_BUYRBIC, " +
                                      " T_BUYRNAME, " +
                                      " T_BUYRDEPOACC, " +
                                      " T_BUYRDEPOPART, " +
                                      " T_DEALSUM, " +                        //Сумма расчетов
                                      " T_DEALCURRENCY, " +                   //Валюта расчетов
//Плательщик  отправитель денежных средств
                                      " T_PAYEBIC, " +
                                      " T_PAYENAME, " +
                                      " T_PAYECASH, " +
//Продавец Ц\Б  конечный получатель Д\С
                                      " T_ACCWBIC, " +
                                      " T_ACCWNAME, " +
                                      " T_ACCWCASH, " +
//Продавец Ц\Б  конечный получатель денежных средств
                                      " T_BENMBIC, " +
                                      " T_BENMNAME, " +
                                      " T_BENMCASH, " +
                                      " T_ROWNUM )" +
                          "        VALUES ( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? ) ");

    cmd.NullConversion = true;
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DRAFTID           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_RECEIVERBIC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_CA                );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_TRADTYPE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_MIC               );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALID            );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALDATE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALTIME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SETTDATE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_GSETT             );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUENAME         );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISIN              );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_CFI               );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_MATURITY          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_INTR              );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_LSIN              );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUEOTHRCODEKIND );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUEOTHRCODE     );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUENOMINAL      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ISSUENOMCURRENCY  );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BLOCKED           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_NEEDMACH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_AMOUNT            );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PRICE             );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PRICEPRC          );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DAY               );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SUM               );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SUMCURRENCY       );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_COUP              );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_COUPCURRENCY      );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SUMNC             );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SUMNCCURRENCY     );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_EXCH              );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_EXCH1             );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_EXCHCUR           );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DIRECTION         );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DVP               );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REPO2             );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_RATE              );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_INTEREST          );                                     
    cmd.addParam( "", RSDBP_IN, PMMsg.T_OPERTYPE          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_CLIENT            );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REPO              );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_SELLDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEAGDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETBIC1          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PSETNAME1         );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_REAGDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRDEPOACC       );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BUYRDEPOPART      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALSUM           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_DEALCURRENCY      );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYEBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYENAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_PAYECASH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_ACCWCASH          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BENMBIC           );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BENMNAME          );
    cmd.addParam( "", RSDBP_IN, PMMsg.T_BENMCASH          );
    cmd.addParam( "", RSDBP_IN, RowNum                    );

    SQL_Execute( cmd, "", false );


  OnError(er)
    return 1;
end;

private macro GetMsgLength(DraftID, ProcName, DraftKind)
  var query, Select, Stat;

  query = "declare v_Mes CLOB; begin\n ? := RSP_SECURITY." + ProcName + "(?, ?, v_Mes); ? := DBMS_LOB.GETLENGTH(v_Mes); \n end;";

  Select = RSDCommand( query );

  Select.AddParam("p_Stat", RSDBP_OUT, V_INTEGER );

  Select.AddParam("p_DraftKind", RSDBP_IN, DraftKind );
  Select.AddParam("p_DraftID", RSDBP_IN, DraftID );

  Select.AddParam("p_Len", RSDBP_OUT, V_INTEGER );

  Select.Execute();

  if(NOT SQL_ConvTypeInteger(Select.value("p_Stat")))
    return SQL_ConvTypeInteger(Select.value("p_Len"));
  else
    return 0;
  end;
end;

private macro GetMsgText(DraftID, ProcName, DraftKind)
  var query, Select, Stat, MesLength = GetMsgLength(DraftID, ProcName, DraftKind);
  var Text = "";
  if(MesLength)
    query = "begin\n ? := RSP_SECURITY." + ProcName + "(?, ?, ?); \n end;";

    Select = RSDCommand( query );

    Select.AddParam("p_Stat", RSDBP_OUT, V_INTEGER );

    Select.AddParam("p_DraftKind", RSDBP_IN, DraftKind );
    Select.AddParam("p_DraftID", RSDBP_IN, DraftID );

    Select.AddParam("p_Mes", RSDBP_OUT, V_STRING, MesLength );

    Select.Execute();

    if(NOT SQL_ConvTypeInteger(Select.value("p_Stat")))
//      return SQL_ConvTypeStr(Select.value("p_Mes"));
      Text = SQL_ConvTypeStr(Select.value("p_Mes"));
    end;
  end;
  return Text;
end;

macro PrintSWIFTMessage(DraftID, IsConf, DraftKind)
  VAR ReportFileName, ProcName;
  ReportFileName = GetTxtFileName( "message");
  SetOutput( ReportFileName );

  if(IsConf)
    ProcName = "GetConfirmText";
  else
    ProcName = "GetMsgText";
  end;

  print(GetMsgText(DraftID, ProcName, DraftKind));

  SetOutput( NULL, true );
  ViewFile( ReportFileName );
end;
// печать вх./исх. сообщений в одном месте
macro PrintSWIFTMessageOutIn(DraftID, DraftKind)
  VAR ReportFileName, ProcName;
  var Text = "";
  ReportFileName = GetTxtFileName( "message");
  SetOutput( ReportFileName );

  Text = "Исходящее сообщение: \n\n";
  Text = Text + GetMsgText(DraftID, "GetMsgText", DraftKind);
  Text = Text + "\n\nВходящее сообщение: \n\n";
  Text = Text + GetMsgText(DraftID, "GetConfirmText", DraftKind);
  print(Text);

  SetOutput( NULL, true );
  ViewFile( ReportFileName );
end;

  /* Получить значение из dnamealg_dbt*/
private macro name_alg(Type, Number)
    var cmd, dataSet;
    cmd = DL_RSDCommand("SELECT t_szNameAlg as Name FROM dnamealg_dbt WHERE t_iTypeAlg = ? AND t_iNumberAlg = ?");

    cmd.addParam(Type);
    cmd.addParam(Number);
    dataSet = cmd.execute();

    if (dataSet.moveNext())
      return dataSet.Name;
    else
      return "";
    end;

end;

private macro GET_DRAFTKIND (DRAFTID ,KINDMSG)
var stat = 0 ;
  var query, DataSet, cmd;
  var ID: integer;
  ID  =  DRAFTID;
  var MT = substr(KINDMSG,1,5);
  query = " begin\n ? := RSP_SECURITY.GET_DRAFTKIND(?,?);\n end; ";
  cmd = RSDCommand(query);
  cmd.AddParam("p_Stat", RSDBP_OUT, V_INTEGER );

  cmd.AddParam("p_DraftID", RSDBP_IN, ID );
  cmd.AddParam("p_KindMes", RSDBP_IN, KINDMSG );

  cmd.Execute();

    stat = SQL_ConvTypeInteger(cmd.value("p_Stat"));
return stat;

end;

// печать всех вх./исх. сообщений в одном месте
macro PrintSWIFTMessageOutInALL(DraftID)
  VAR ReportFileName, ProcName;
  var DraftKind;
  var Text = "", отступ = "";
  ReportFileName = GetTxtFileName( "message");
  SetOutput( ReportFileName );

  var query, DataSet, cmd;
  query = "Select msgobj.t_DocID DocID, msgobj.t_DocKind DocKind, msg.t_Kind Kind, msg.t_Format Format from ddlinfmsg_dbt msg, ddlinfmsgobj_dbt msgobj  Where rsb_depo.GetDealIDByID(msgobj.t_DocID, msgobj.t_DocKind, ?) = ? AND msg.t_ID = msgobj.t_MesID Order by msgobj.t_DocKind " ;
  cmd = DL_RSDCommand(query);
  cmd.AddParam(DraftID);
  cmd.AddParam(DraftID);
  DataSet = cmd.Execute();
  while( DataSet.moveNext() )              
    var Type = "";
    Type =  name_alg(3280, DataSet.Kind);

    DraftKind = GET_DRAFTKIND (DataSet.DocID ,Type);
    Text = Text + отступ + "Исходящее сообщение "+Type + ": \n\n";
    Text = Text + GetMsgText(DataSet.DocID, "GetMsgText", DraftKind);
    Type =  name_alg(3280, DataSet.Format);
    Text = Text + "\n\nВходящее сообщение "+Type + ": \n\n";
    Text = Text + GetMsgText(DataSet.DocID, "GetConfirmText", DraftKind);
    отступ = "\n\n";
  end;

  print(Text);

  SetOutput( NULL, true );
  ViewFile( ReportFileName );
end;

// запрос подтверждения получения ответа
macro GetConfirmParamsSEC(DraftKind, DraftID, ConfNumber:@variant, FactValueDate:@variant, InstrStatus:@variant)
  var query, Select, Transport, Stat;

  query = "begin\n ? := RSP_SECURITY.GetConfirmParamsSEC(?,?,?,?,?);\n end; ";

  Select = RSDCommand( query );

  Select.AddParam("p_Stat", RSDBP_OUT, V_INTEGER );

  Select.AddParam("p_DraftKind", RSDBP_IN, DraftKind );
  Select.AddParam("p_DraftID", RSDBP_IN, DraftID );

  Select.AddParam("p_Number", RSDBP_OUT, V_STRING );
  Select.AddParam("p_Date", RSDBP_OUT, V_DATE );
  Select.AddParam("p_Status", RSDBP_OUT, V_INTEGER );

  Select.Execute();

  ConfNumber    = SQL_ConvTypeStr(Select.value("p_Number"));
  FactValueDate = SQL_ConvTypeDate(Select.value("p_Date"));
  InstrStatus   = SQL_ConvTypeInteger(Select.value("p_Status"));

  return SQL_ConvTypeInteger(Select.value("p_Stat"));
end;



private macro PreparePaymentsMessage(FD, KindMsg, Oper, Department, ErrText:@variant)
  var PMMsg = CPMMesDL(FD.tick.rec.DealID);
  RowNum = 0;
  var Kind = 1;  //DraftKind в Payments

  ClearPaymentsMessageData();

  var stat = FillPaymentsMessageData( FD , PMMsg , KindMsg, false );
  if(NOT stat)
    stat = InsertPaymentsMessageData(PMMsg);
  end;

  if( IsRepo(FD.Group))    
    if(Direct)
      Direct = 0;
    else
      Direct = 1;
    end;
    var PMMsg2 = CPMMesDL(FD.tick.rec.DealID);
    stat = FillPaymentsMessageData( FD2 , PMMsg2 , KindMsg, true );
    if(NOT stat)
      stat = InsertPaymentsMessageData(PMMsg2);
    end;
  end;
  if(KindMsg== "MT518")
    Kind = 3;                 // 518
  elif(KindMsg== "MT599")
    Kind = 4;                 // 599 
  end;
 
  stat = DL_CreatePaymentsMessageSEC(Kind, FD.tick.rec.DealID, KindMsg, Oper, Department, FD.tick.rec.BOfficeKind,FD.tick.rec.PartyID, 2);     // пока 2- не по второй части Репо
  return stat;
end;

/*
Получение значения настройки "Транспортная система обработки сообщений"
*/
private macro GetTransportBO( Transport )
  var err;
  var TransportTmp;

  GetRegistryValue( "SECUR\\TRANSPORT", V_INTEGER, TransportTmp, err );
  if( NOT err )
    SetParm( 0, TransportTmp );
  else
    SetParm( 0, 0 );
     msgbox( "Ошибка поиска настройки \"Транспортная система обработки сообщений\"" );

  end;

  return err;
end;

/* формирование исходящего инф. сообщения на шаге операции */
macro DL_CreateMsgInfBO( tick )
  var err = 0, KindMsg, Select, Transport, ErrText, form, stat, AttrID = 0;
  var ClientRec = TRecHandler( "party.dbt" ) ;
  FD  = SPFirstDoc( tick, false );
  ПолучитьСубъекта(FD.tick.rec.PartyID, ClientRec);            	
  if(IsBUY(FD.Group))
    Direct = 1;
  else
    Direct = 0;
  end;
  if( IsRepo(FD.Group))    
    FD2 = SPFirstDoc( tick, true );
  end;
  err = GetTransportBO(Transport);
  KindMsg = "MT518";     // изначально 518

  if(NOT err)
    if(Transport == 2)
       if( GetMainObjAttr( null, OBJTYPE_PARTY, UniID( ClientRec, OBJTYPE_PARTY), 82/*Формирование 518/599*/, null, null, AttrID))
         if(AttrID == 1)
           KindMsg = "MT599";     // если "ДА", то меняем на 599
         end;
       end;

      PreparePaymentsMessage(FD, KindMsg, {Oper}, {NumDprt}, @ErrText);
    else 
       msgbox( "Настройка \"Транспортная система обработки сообщений\" не установлена" );
       err = 1;
    end;
  end;
  return err;
end;



macro MT_CreateMsgInf(_qr_comm )
  var err = 0, KindMsg, Select, Transport, ErrText, form, stat, AttrID = 0;
  var ClientRec = TRecHandler( "party.dbt" ) ;

  err = GetTransportBO(Transport);
  KindMsg = _qr_comm.DocKind;



  if(Transport == 2)

//     PreparePaymentsMessage(_qr_comm, KindMsg, {Oper}, {NumDprt}, @ErrText);
    if (_qr_comm.DockInd == "4621")

var F1D  = SPFirstDocDLCOMM(_qr_comm, false );
var F2D  = SPFirstDocDLCOMM(_qr_comm, true );
       var PMMsg = CPMMesDL(_qr_comm.commcode);
       RowNum = 0;
	Var Kind = 1;  // DraftKind в Payments
	ClearPaymentsMessageData();
	

    end;

  else
       msgbox( "Настройка \"Транспортная система обработки сообщений\" не установлена" );
       err = 1;
  end;


 
 
  return err;
end;


private macro ExecuteStep( p1,p2,p3,p4,p5)
  var err = 0, kindMsg;
  
  record qr_comm(dl_comm);
  setbuff(qr_comm, p2);

  /*Операция междепозитарного перевода*/
  if (qr_comm.DockInd == "4621")
    var select;
    err = MT_CreateMsgInf(qr_comm);

  end;




  return err;
end;















