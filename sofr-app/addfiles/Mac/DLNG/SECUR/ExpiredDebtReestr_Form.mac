/**                                                                                                             
 @file  ExpiredDebtReestr_Form.mac                                                                                           
 @brief Панель редактирования для Реестра прочей задолженности                                                                        
                                                                                                                
 #menu 
  - БО ЦБ\Договоры\Договоры брокерского обслуживания\Реестр прочей задолженности  
                                                                                                                
 #tag                                                                                                          
 - functional_block:СО                                                                             
 - code_type:GUI 
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2025.07.29 |Жиц М.В.       |BOSS-2183           |Создание макроса для обработки формы                                                                                                                                                 
*/ 
import "DebtSumOp_Common.mac";  

PRIVATE CONST PNFLD_DEBTREESTR_CONTRACT  = 0,   
              PNFLD_DEBTREESTR_CONTRDATE = 1,
              PNFLD_DEBTREESTR_DEBTDATE  = 2,    
              PNFLD_DEBTREESTR_CURCODE   = 3,
              PNFLD_DEBTREESTR_CURNAME   = 4,
              PNFLD_DEBTREESTR_DEBTSUM   = 5;

PRIVATE CONST H_PNFLD_CONTRACT  = 100,
              H_PNFLD_CONTRDATE = 101,
              H_PNFLD_DEBTDATE  = 102,
              H_PNFLD_CURCODE   = 103,
              H_PNFLD_CURNAME   = 104,
              H_PNFLD_DEBTSUM   = 105;

CONST KEY_F9 = 323, KEY_F8 = 322, KEY_ALT_H = 291, KEY_ESC = 27;

/**
 @brief Проверки корректности субдоговора 
 @param[in]  SfContrID идентификатор субдоговора
 @param[out] errmsg    сообщение об ошибке
 @return true, если проверки успешно пройдены и можно продолжать выполнение
 @return false, если проверки не пройдены и дальнейшее выполнение должно быть остановлено
*/
MACRO CheckContract(SfContrID:integer, errmsg:@string):bool
  var cmd, DataSet;

  if(SfContrID <= 0)
    errmsg = "Необходимо указать субдоговор";
    return false;
  end;

  cmd = DL_RSDCommand("SELECT NVL(at.t_AttrID, 0) t_IsEDP, sf.t_ServKind, sf.t_ServKindSub, mp.t_MarketID, sf.t_PartyID, sf.t_DateClose " + 
                      "  FROM dsfcontr_dbt sf " +
                      " INNER JOIN ddlcontrmp_dbt mp " +
                      "    ON mp.t_SfContrID = sf.t_ID " +
                      "  LEFT JOIN dobjatcor_dbt at " +
                      "    ON at.t_ObjectType = 659 AND at.t_GroupID = 102 AND at.t_Object = LPAD(sf.t_ID, 10, '0') " +
                      " WHERE sf.t_ID = ? "
                     );
  cmd.AddParam(SfContrID);

  DataSet = cmd.Execute(); 
  if(DataSet.moveNext())
    if(DataSet.DateClose != date(0,0,0))
      errmsg = "Указанный субдоговор закрыт";
      return false;
    elif(DataSet.PartyID == {OurBank})
      errmsg = "Выбранный субдоговор не может принадлежать Нашему банку";
      return false;
    elif((DataSet.IsEDP == 1) AND ((DataSet.ServKind != PTSK_STOCKDL) OR (DataSet.ServKindSub != 8) OR (DataSet.MarketID != 2)))
      errmsg = "Для договоров ЕДП по биржевой площадке необходимо выбирать субдоговор фондового рынка ММВБ";
      return false;
    elif((DataSet.ServKind != PTSK_STOCKDL) AND (DataSet.ServKind != PTSK_DV) AND (DataSet.ServKind != PTSK_CM))
      errmsg = "Вид обслуживания, указанный в субдоговоре, не соответствует ожидаемому";
      return false;
    end;

    return true;
  end;

  errmsg = "Субдоговор не найден";
  return false;
END;

/**
 @brief Проверки корректности даты возникновения задолженности 
 @param[in]  DebtDate дата возникновения задолженности
 @param[out] errmsg   сообщение об ошибке
 @return true, если проверки успешно пройдены и можно продолжать выполнение
 @return false, если проверки не пройдены и дальнейшее выполнение должно быть остановлено
*/
MACRO CheckDebtDate(DebtDate:date, errmsg:@string):bool
  var cmd, DataSet;

  if(DebtDate == date(0,0,0))
    errmsg = "Дата задолженности должна быть отлична от 00.00.0000";
    return false;
  end;

  cmd = DL_RSDCommand("SELECT MAX(t_OperDate) MaxOperDate " +
                      "  FROM dprocess_u_dbt " +
                      " WHERE t_Type = 1 " + /*фондовый рынок ММВБ*/ 
                      "   AND t_SubType = 702 " + /*Расчет Лимитов Фондовый рынок и ЕДП*/
                      "   AND t_Status <> 0 "
                     );
  DataSet = cmd.Execute(); 
  if(DataSet.moveNext())
    if(DebtDate > DataSet.MaxOperDate)
      errmsg = "Дата возникновения задолженности больше последнего обработанного дня в системе";
      return false;
    end;
  end;

  return true;
END;

/**
 @brief Поиск субдоговора по его номеру
 @param[in]  Number   номер субдоговора
 @param[out] DateConc дата начала действия субдоговора 
 @return идентификатор субдоговора
*/                    
PRIVATE MACRO GetSfContrByNumber(Number:string, DateConc:@date):integer
  var cmd, DataSet;
  var SfContrID:integer = -1;
  DateConc = date(0,0,0);
  
  cmd = DL_RSDCommand("SELECT sf.t_ID, sf.t_DateConc " +
                      "  FROM dsfcontr_dbt sf " +
                      " WHERE sf.t_Number = ? " +
                      "   AND sf.t_ServKind <> 0 ");
  cmd.AddParam(Number);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    SfContrID = DataSet.ID;
    DateConc  = DataSet.DateConc;
  end;

  return SfContrID;
END;

/**
 @brief Листалка субдоговоров
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @param[in] X координата левого верхнего угла отображения листалки по горизонтали
 @param[in] Y координата левого верхнего угла отображения листалки по вертикали
*/                    
PRIVATE MACRO ListSfContr(FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER):BOOL
  var Choice, Select;

  Select = "SELECT sf.t_ID, sf.t_DateConc, sf.t_Name, sf.t_Number, pt1.t_ShortName Payer, pt2.t_ShortName Contractor " + 
           "  FROM dsfcontr_dbt sf " +
           " INNER JOIN ddlcontrmp_dbt mp " +
           "    ON mp.t_SfContrID = sf.t_ID " +
           " INNER JOIN dparty_dbt pt1 " +
           "    ON pt1.t_PartyID = sf.t_PartyID " +
           " INNER JOIN dparty_dbt pt2 " +
           "    ON pt2.t_PartyID = sf.t_ContractorID " +
           "  LEFT JOIN dobjatcor_dbt at " +
           "    ON at.t_ObjectType = 659 AND at.t_GroupID = 102 AND at.t_Object = LPAD(sf.t_ID, 10, '0') " +
           " WHERE sf.t_ServKind IN (" + PTSK_STOCKDL + "," + PTSK_DV + "," + PTSK_CM + ") " +
           "   AND sf.t_PartyID <> " + {OurBank} +
           "   AND sf.t_DateClose = TO_DATE('01.01.0001', 'DD.MM.YYYY') " +
           "   AND (    NVL(at.t_AttrID, 0) <> 1 " +
           "         OR (NVL(at.t_AttrID, 0) = 1 AND sf.t_ServKind = " + PTSK_STOCKDL + " AND sf.t_ServKindSub = 8 AND mp.t_MarketID = 2 /*ММВБ*/) " +
           "       ) ";

  Choice = DL_Scroll(Select,
                     "Договора обслуживания", X, Y,
                     "t_Number",   "№ договора",
                     "t_Name",     "Наименование договора",
                     "t_DateConc", "Дата закл.",
                     "Payer",      "Плательщик",
                     "Contractor", "Контрагент"
                    );

  if(Choice != NULL)
    FldRealValue = Choice.Value("t_ID"); 
    FldShowValue = Choice.Value("t_Number");
  end;

  return (Choice != NULL);
END;

/**
 @brief Обработчик поля "Номер субдоговора"
 @param[in] pThis параметры формы
 @param[in] Cmd код действия
 @param[in] Key код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
PRIVATE MACRO FldProc_Contract(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  var DateConc:date = date(0,0,0);

  if(Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == 0))
      FldShowValue = "";
    else
      FldShowValue = GetSfContrNumber(FldRealValue);
    end;
  elif(Cmd == DLG_CHANGEVALUE)
    FldRealValue = GetSfContrByNumber(FldShowValue, @DateConc);
    pThis.SetLinkedValue(H_PNFLD_CONTRDATE, DateConc);
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE)) 
    FldRealValue = 0;
    FldShowValue = "";
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    ListSfContr(@FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY());
  end;

  return CM_DEFAULT;
END;

/**
 @brief Обработчик поля "Дата возникновения задолженности"
 @param[in] pThis параметры формы
 @param[in] Cmd код действия
 @param[in] Key код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
PRIVATE MACRO FldProc_DebtDate(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if(Cmd == DLG_INIT) /*Установка значения в поле*/
    if(FldRealValue == null) /*инициализация по умолчанию*/
      FldRealValue = {curdate};
    end;
    FldShowValue = FldRealValue;
  elif(Cmd == DLG_CHANGEVALUE) 
    FldRealValue = FldShowValue;
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3))
    FldRealValue = GetDateByCalendar(FldRealValue);
    FldShowValue = FldRealValue;
  end;

  return CM_DEFAULT;
END;

/**
 @brief Обработчик поля по-умолчанию
 @param[in] pThis параметры формы
 @param[in] Cmd код действия
 @param[in] Key код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
PRIVATE MACRO Default(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if(Cmd == DLG_INIT)
    if(FldRealValue == null) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;
  elif(Cmd == DLG_CHANGEVALUE) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
  end;
  return CM_DEFAULT;
END;

/**
 @brief Обработчик поля "Валюта"
 @param[in] pThis параметры формы
 @param[in] Cmd код действия
 @param[in] Key код клавиши
 @param[in/out] FldShowValue отображаемое значение поля
 @param[in/out] FldRealValue реальное значение поля
 @return код дальнейшего действия
*/                    
PRIVATE MACRO FldProc_FI(pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  var fi = TRecHandler("fininstr.dbt");

  if(Cmd == DLG_INIT)
    if(FldRealValue == null)
      FldRealValue = -1;
    end;
    if(FldRealValue < 0)
      FldShowValue = "";
    elif(ПолучитьФинИн(FldRealValue, fi) == 0)
      FldShowValue = fi.rec.CodeInAccount;
    end;
  elif(Cmd == DLG_CHANGEVALUE)
    FldRealValue = GetFIIDByCodeInAccount(FldShowValue);
    if(ПолучитьФинИн(FldRealValue, fi) == 0)
      pThis.SetLinkedValue(H_PNFLD_CURNAME, fi.rec.Name);
    else 
      pThis.SetLinkedValue(H_PNFLD_CURNAME, "");
    end;
  elif((Cmd == DLG_KEY) AND (Key == KEY_SPACE))
    FldRealValue = -1;
    FldShowValue = "";
  elif((Cmd == DLG_KEY) AND (Key == KEY_F3)) 
    if(ListFI(FIKIND_CURRENCY, 0, fi))
      FldRealValue = fi.rec.FIID;
      FldShowValue = fi.rec.CodeInAccount;
    end;
  end;

  return CM_DEFAULT;
END;

/**
 @brief Класс формы для Реестра прочей задолженности
 @param[in] DebtID идентификатор общей задолженности из реестра задолженности
*/                    
CLASS (DL_CPanel) ExpiredDebtReestr_Panel(DebtID:integer)
  private var m_FormData = ExpiredDebtReestrParams(DebtID); //Параметры записи реестра

  /**
   @brief Инициализация параметров формы, назначения обработчиков полей
  */                    
  PRIVATE MACRO Init()
    AddFieldProc(0, PNFLD_DEBTREESTR_CONTRACT,  H_PNFLD_CONTRACT,  @FldProc_Contract, m_FormData.SfContrID);
    AddFieldProc(0, PNFLD_DEBTREESTR_CONTRDATE, H_PNFLD_CONTRDATE, @Default,          date(0,0,0));
    AddFieldProc(0, PNFLD_DEBTREESTR_DEBTDATE,  H_PNFLD_DEBTDATE,  @FldProc_DebtDate, m_FormData.DebtDate);                 
    AddFieldProc(0, PNFLD_DEBTREESTR_CURCODE,   H_PNFLD_CURCODE,   @FldProc_FI,       m_FormData.DebtCurrency);
    AddFieldProc(0, PNFLD_DEBTREESTR_CURNAME,   H_PNFLD_CURNAME,   @Default,          "");
    AddFieldProc(0, PNFLD_DEBTREESTR_DEBTSUM,   H_PNFLD_DEBTSUM,   @Default,          m_FormData.DebtSum);

    if(m_FormData.DebtID != 0)
      DisableFields(This.Data(), PNFLD_DEBTREESTR_CONTRACT);
    end;
  END;

  /**
   @brief Переопределение обработчика нажатия клавиш
   @param[in] Pan объект панели
   @param[in] Cmd код действия
   @param[in] FldId идентификатор поля
   @param[in] _Key код клавиши
   @return код дальнейшего действия
  */                    
  MACRO KeyProc(Pan:Variant, Cmd:INTEGER, FldId:INTEGER, _Key:INTEGER)
    var RetKey = KeyProc(Pan, Cmd, FldId, _Key);

    if((Cmd == DLG_KEY) AND (RetKey == CM_SAVE))
      if(m_FormData.DebtID != 0) //Перед сохранением изменений по существующей задолженности сначала проверим, были ли они и точно нужны ли
        if((m_FormData.DebtDate == GetFieldValue(PNFLD_DEBTREESTR_DEBTDATE)) AND (m_FormData.DebtCurrency == GetFieldValue(PNFLD_DEBTREESTR_CURCODE)) AND (m_FormData.DebtSum == GetFieldValue(PNFLD_DEBTREESTR_DEBTSUM))) //Не было изменений
          RetKey = CM_CANCEL;
        elif(not GetTrue(false, "Вы уверены, что хотите изменить строку с задолженностью? Это не приведет к формированию проводок. Данное действие актуально только в случае расхождения данных реестра с реальными данными по остаткам и проводкам выноса на просрочку"))
          RetKey = CM_CANCEL;
        end;  
      end;
    elif((Cmd == DLG_KEY) AND (_Key == KEY_ENTER) AND (FldId == PNFLD_DEBTREESTR_DEBTSUM)) //Костылим баг инструмента, иначе в случае, если последнее поле имеет тип FET, Enter по нему сохраняет данные панели
      if(m_FormData.DebtID != 0)
        SetFocus(Pan, PNFLD_DEBTREESTR_DEBTDATE);
      else
        SetFocus(Pan, PNFLD_DEBTREESTR_CONTRACT);
      end;
      RetKey = CM_IGNORE;
    end;

    return RetKey;
  END;

  /**
   @brief Проверка параметров формы
   @return true, если проверка пройдена успешно
   @return false, если в процессе проверки были обнаружены ошибки
  */                    
  PRIVATE MACRO Check():BOOL
    var errmsg:string = "";
    var ReestrDebtID:integer = 0;

    if(not CheckContract(GetFieldValue(PNFLD_DEBTREESTR_CONTRACT), @errmsg)) 
      MsgBox(errmsg);
      return false;
    end;

    if(GetFieldValue(PNFLD_DEBTREESTR_CURCODE) < 0) 
      MsgBox("Необходимо указать валюту задолженности");
      return false;
    end;

    if(not CheckDebtDate(GetFieldValue(PNFLD_DEBTREESTR_DEBTDATE), @errmsg))
      MsgBox(errmsg);
      return false;
    end;

    if(IsExistsDebtIntoReestr(GetFieldValue(PNFLD_DEBTREESTR_CONTRACT), GetFieldValue(PNFLD_DEBTREESTR_DEBTDATE), GetFieldValue(PNFLD_DEBTREESTR_CURCODE), @ReestrDebtID))
      if(m_FormData.DebtID != ReestrDebtID) 
        MsgBox("Нельзя добавить в реестр несколько прочих задолженностей по субдоговору "+GetSfContrNumber(GetFieldValue(PNFLD_DEBTREESTR_CONTRACT))+" за одну дату. Требуется сначала вручную отредактировать/удалить существующую запись реестра");
        return false;
      end;
    end;

    if(GetFieldValue(PNFLD_DEBTREESTR_DEBTSUM) < 0) 
      MsgBox("Сумма задолженности должна быть >= 0");
      return false;
    end;

    return true;
  END;

  /**
   @brief Дозаполнение и получение параметров панели
   @return параметры панели 
  */ 
  MACRO GetParams()
    m_FormData.FillDebtAccounts();
    if(m_FormData.DebtID == 0) 
      m_FormData.FillInsertableParams();
    end;

    return m_FormData;
  END;

  /**
   @brief Запуск формы с последующей записью полученных параметров
   @return true, если форма отработала успешно и была подтверждена по F2
   @return false, если в процессе работы формы возникли ошибки или из нее вышли по Esc
  */                    
  MACRO Run(RunKey)
    var stat = Run(RunKey);

    m_FormData.SfContrID = GetFieldValue(PNFLD_DEBTREESTR_CONTRACT);
    m_FormData.DebtDate = GetFieldValue(PNFLD_DEBTREESTR_DEBTDATE);
    m_FormData.DebtCurrency = GetFieldValue(PNFLD_DEBTREESTR_CURCODE);
    m_FormData.DebtSum = GetFieldValue(PNFLD_DEBTREESTR_DEBTSUM);

    return stat;
  END;
  
  initDL_CPanel("sp_res.lbr", "DEBTRSTR"); 
END;
                                                                                                             