/*
$Name:             taxpayrepo_Report.mac
$Module:           АРНУ
$Description:      Отчет "Выплаты по сделкам РЕПО"
*/

import "DLReport_h.mac", "taxpayrepo_Form.mac", "ws_txreportform.mac";

private var TaxPayRepo_Form;
private var TaxPayRepo_Report;
PRIVATE VAR WebMenuItem; // Информация о запуске отчета из веб

private const KIND_COUPON   = 1,
              KIND_COMPPAYM = 2;

private var
  Table = "┌───────────────┬─────────────────────────┬──────────┬───────────┬───────────────┬───────────────┬──────────────┬──────────────┬──────────────┬──────────────┬────────────────────┬───────────┬─────────────┬────────────────────┬─────────────┬────────────────────┬────────────────────┬─────────────────┐\n" +
          "│   Код бумаги  │   Наименование бумаги   │    Код   │   Дата    │  Код сделки   │  Вид сделки   │    Дата      │    Дата      │    Дата      │    Дата      │  Количество бумаг  │   Дата    │     Вид     │  Сумма выплаты в   │ Курс ЦБ на  │  Сумма в рублях    │ Группа налогового  │ Код ISIN (номер │\n" +
          "│               │                         │  валюты  │ погашения │     РЕПО      │     РЕПО      │  поставки    │  поставки    │  оплаты по   │  оплаты по   │                    │  выплаты  │   выплаты:  │  валюте номинала   │    дату     │                    │       учета        │ государственной │\n" +
          "│               │                         │ номинала │  бумаги   │               │   (прямое/    │  по 1 части  │  по 2 части  │   1 части    │   2 части    │                    │           │    купон/   │       бумаги       │  выплаты    │                    │                    │ регистрации)    │\n" +
          "│               │                         │          │           │               │   обратное)   │    сделки    │    сделки    │    сделки    │    сделки    │                    │           │ амортизация │                    │             │                    │                    │ ценной бумаги   │\n" +
          "│               │                         │          │           │               │               │ (перехода    │ (перехода    │              │              │                    │           │             │                    │             │                    │                    │                 │\n" +
          "│               │                         │          │           │               │               │    прав      │    прав      │              │              │                    │           │             │                    │             │                    │                    │                 │\n" +
          "│               │                         │          │           │               │               │собственности)│собственности)│              │              │                    │           │             │                    │             │                    │                    │                 │\n" +
          "├───────────────┼─────────────────────────┼──────────┼───────────┼───────────────┼───────────────┼──────────────┼──────────────┼──────────────┼──────────────┼────────────────────┼───────────┼─────────────┼────────────────────┼─────────────┼────────────────────┼────────────────────┼─────────────────┤\n" +
          "│       1       │           2             │     3    │     4     │       5       │       6       │       7      │      8       │       9      │      10      │         11         │     12    │      13     │         14         │      15     │         16         │         17         │         18      │\n" +
          "├───────────────┼─────────────────────────┼──────────┼───────────┼───────────────┼───────────────┼──────────────┼──────────────┼──────────────┼──────────────┼────────────────────┼───────────┼─────────────┼────────────────────┼─────────────┼────────────────────┼────────────────────┼─────────────────┤";

private var ReportHeader =
"Вылаты по сделкам РЕПО\n" +
"За период: с ########## по ##########\n" +
"Группа налогового учета ############################################\n"
"Вид ц\б:    ##########################################\n" +
"Выпуск ц\б: #################################################################\n";


PRIVATE CLASS (DL_CReportTemplate) SP_TaxPayRepo_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  PRIVATE VAR m_SheetCount = 0,
              m_report,
              m_BegDate,
              m_EndDate,
              m_IsExistsData = false,
              m_ProgressValue = CTxProgressValue();
          var ProgressIndicator = CreateProgressIndicator(m_IsWebService);

/* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsExistsData or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END; 

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )

    m_SheetCount = 0;
    m_IsExistsData = false;

    m_BegDate   = TaxPayRepo_Form.GetFieldValue( PNFLD_TAXPAYREPO_BEGDATE );
    m_EndDate   = TaxPayRepo_Form.GetFieldValue( PNFLD_TAXPAYREPO_EndDATE );
  END;

  PRIVATE MACRO HeadReport()
    var
       FiKind = "все",
       FiName = "все",
       GroupName = "все";

    record fins(fininstr);

    TaxPayRepo_Form.GetFieldValue( PNFLD_TAXPAYREPO_AVRCODE, @FiName );
    TaxPayRepo_Form.GetFieldValue( PNFLD_TAXPAYREPO_AVRKIND, @FiKind );
    TaxPayRepo_Form.GetFieldValue( PNFLD_TAXPAYREPO_TAXGROUP, @GroupName );

    SetActiveSheet( "Отчет" );

    PrintFormatString( ReportHeader,
                       "N_H0А", m_BegDate,
                       "N_H0C", m_EndDate,
                       "N_H0B", GroupName,
                       "N_H01", FiKind,
                       "N_H02", FiName
                     );

    RegisterTable( "N_MainTable", Table,
                   "N_SecCode",
                   "N_SecName",
                   "N_VN",
                   "N_RetDate",
                   "N_CodeR",
                   "N_TypeREPO",
                   "N_DDP1",
                   "N_DDP2",
                   "N_DDO1",
                   "N_DDO2",
                   "N_QntyDeal",
                   "N_DatePay",
                   "N_TypePay",
                   "N_SumPayVn",
                   "N_CrCB",
                   "N_SumPayRub",
                   "N_SecType",
                   "N_ISIN");
  END;



 MACRO GetQntyDeal()

    var Summa:MONEY,
    sql = RSDCommand( " select sum(t_amount) PaymSum"+
                      "  from ddlrq_dbt "+
                      "  where t_docid = " + string(m_report.DealID)+
                      "    and t_dealpart = 1 " +
                      "    and Decode (t_FactDate, TO_DATE('01-01-0001','DD-MM-YYYY'), t_PlanDate, t_FactDate) <= " + "TO_DATE( '" + date( m_report.PayDate) +"' , 'DD.MM.YY' )"
                      "    and t_type = "  + string(DLRQ_TYPE_DELIVERY) );
    sql.execute();
    VAR RS = TRsbDataSet( sql );

     if( RS.MoveNext() )
        Summa = RS.PaymSum;
     else
        Summa = 0;
     end;
     return Summa;
  END;



  PRIVATE MACRO PrintLine()
    var TypePay = "",
        DatePay = date(0,0,0),
        SumPayVn = $0,
        CrCB = $0,
        SumPayRub = $0,
        RepoTypeName = "",
        QntyDeal = 0,
        PaymCount = 0;

    if(m_report.isBasket == 0)
       QntyDeal = m_report.Amount;
    else
       QntyDeal = GetQntyDeal();
    end;

    if( m_report.PayType == KIND_COUPON )
      DatePay = date( m_report.PayDate);
      if( string( m_report.IsPartial) == "X" )
        TypePay = "амортизация";
        if( not SP_GetNominal( m_report.FIID, DatePay, SumPayVn, BO_CB ) )
           msgbox("Не могу получить номинал фин. инстумента (FIID = "
                      + String(m_report.FIID) + ") на дату "+string(DatePay));
           return;
      else
           SumPayVn = SumPayVn * m_report.Amount *  m_report.IncomeRate/MAX( 1, m_report.IncomeScale)/100.0;
        end;
      else
        TypePay = "купон";
        SumPayVn = СуммаКупона(m_report.FIID, QntyDeal, DatePay);
      end;
    else
      DatePay = date( m_report.PayDate);
      TypePay = "компенс. взнос";
      SumPayVn =  money( m_report.PayVal);
    end;

    SmartConvertSum(CrCB, $1, DatePay, m_report.FaceValueFI, NATCUR, false);
    SumPayRub = CrCB * SumPayVn;

    if( m_report.Type == TXLOTS_REPO )
      if(m_report.isBasket == 1)
         RepoTypeName = "прямое на корзину ц/б";
      else
         RepoTypeName = "прямое";
      end;
    else
      RepoTypeName = "обратное";
    end;

    if( not m_IsExistsData )
       HeadReport();
    end;

    PrintTableLine( "N_SecCode",   m_report.finCode,              null,
                    "N_SecName",   m_report.finName,              null,
                    "N_VN",        m_report.ValueFI,              null,
                    "N_RetDate",   date(m_report.finDrawingDate), null,
                    "N_CodeR",     m_report.DealCode,             null,
                    "N_TypeREPO",  RepoTypeName,                  null,
                    "N_DDP1",      date(m_report.DDP1),           null,
                    "N_DDP2",      IIF(m_report.isBasket, date(m_report.DDO2), date(m_report.DDP2) ), null,
                    "N_DDO1",      date(m_report.DDO1),           null,
                    "N_DDO2",      date(m_report.DDO2),           null,
                    "N_QntyDeal",  QntyDeal,                      SetPrecisionByFractPart(QntyDeal, m_report.finPrecision, 0),
                    "N_DatePay",   DatePay,                       null,
                    "N_TypePay",   TypePay,                       null,
                    "N_SumPayVn",  SumPayVn,                      null,
                    "N_CrCB",      CrCB,                          null,
                    "N_SumPayRub", SumPayRub,                     null,
                    "N_SecType",   m_report.GroupName,            null,
                    "N_ISIN",      m_report.ISIN,                 null
                 );

    m_IsExistsData = true;

  END;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )
    while( m_report.MoveNext() )
      PrintLine();
      m_ProgressValue.Current = m_ProgressValue.Current + 1;
      ProgressIndicator.Update(m_ProgressValue.Current,m_ProgressValue.Maximum);
    end;

    if( m_IsExistsData )
       EndTable();
       AddStandardFooter( "N_" );
    end;

  END;

  PRIVATE MACRO FormQuery( Kind:integer )
    VAR sql,
        AvrFIIDList = null,
        AvrFIIDCount : Integer = 0,
        AvrFIIDAddWhere : String = "",

        AvrKindList = null,
        AvrKindAddWhere : String = "",
        AvrKindCount : Integer = 0,

        GNUList = null,
        GNUAddWhere : String = "",
        GNUCount : Integer = 0;

    PRIVATE MACRO GetRepo1Date()
      return " DECODE(lot.t_Type, " + string(TXLOTS_REPO) + ", lot.t_SaleDate, lot.t_BuyDate) ";
    END;

    PRIVATE MACRO GetRepo2Date()
      return " DECODE(lot.t_Type, " + string(TXLOTS_BACKREPO) + ", "
           +                      " DECODE(lot.t_SaleDate, TO_DATE('01-01-0001','DD-MM-YYYY'), leg.t_Maturity, lot.t_SaleDate), "
           +                      " DECODE(lot.t_BuyDate, TO_DATE('01-01-0001','DD-MM-YYYY'), leg.t_Maturity, lot.t_BuyDate)) ";
    END;

    if( not m_IsWebService )
       AvrFIIDList = TArray();
       AvrFIIDList[AvrFIIDList.Size] = TaxPayRepo_Form.GetFieldValue( PNFLD_TAXPAYREPO_AVRCODE );

       AvrKindList = TArray();
       AvrKindList[AvrKindList.Size] = TaxPayRepo_Form.GetFieldValue( PNFLD_TAXPAYREPO_AVRKIND );
    else
       AvrKindList = TaxPayRepo_Form.GetFieldValue( PNFLD_TAXPAYREPO_AVRKIND );
       AvrFIIDList = TaxPayRepo_Form.GetFieldValue( PNFLD_TAXPAYREPO_AVRCODE );
    end;

    sql = " SELECT fin.t_FIID, "
        +        " fin.t_FaceValueFI, "
        +        " fin.t_FaceValue, "
        +        " fin.t_FI_Code finCode, "
        +        " fin.t_SumPrecision finPrecision, "
        +        " fin.t_Name finName, "
        +        " fin.t_DrawingDate finDrawingDate, "
        +        " lot.t_Type, "
        +        " lot.t_DealID, "
        +        " Tick.t_BOfficeKind, "
        +        " lot.t_DealCodeTS DealCode, "
        +        GetRepo1Date() + " DDP1, "
        +        GetRepo2Date() + " DDP2, "
        +        " DECODE(pmo1.t_FactDate, TO_DATE('01-01-0001','DD-MM-YYYY'), pmo1.t_PlanDate, pmo1.t_FactDate) DDO1, "
        +        " DECODE(pmo2.t_FactDate, TO_DATE('01-01-0001','DD-MM-YYYY'), pmo2.t_PlanDate, pmo2.t_FactDate) DDO2, "
        +        " lot.t_Amount, "
        +        " NVL((SELECT val.t_FI_Code "
        +               " FROM dfininstr_dbt val "
        +              " WHERE val.t_FIID = fin.t_FaceValueFI), '') ValueFI, "
        +        " av.t_TaxGroup GroupName, "
        +        " (case when av.t_ISIN IS null OR av.t_ISIN IN (chr(0), chr(1)) then (case when av.t_LSIN IS null OR av.t_LSIN IN (chr(0), chr(1)) then chr(0) else av.t_LSIN end) else av.t_ISIN end) AS t_ISIN, "
        +        " RSB_SECUR.IsBasket( "
			  +             " rsb_secur.get_OperationGroup( "
			  +                  " rsb_secur.get_OperSysTypes( "
				+                      " Tick.t_DealType, Tick.t_BofficeKind))) isBasket, " ;
				

    if( Kind == KIND_COUPON )
       sql = sql +" "+ string(KIND_COUPON)+" as PayType, 0.0 as PayVal, warnt.t_DrawingDate as PayDate, warnt.t_IsPartial as IsPartial, warnt.t_IncomeRate, warnt.t_IncomeScale ";
    else
       sql = sql +" "+ string(KIND_COMPPAYM)+" as PayType, (CASE WHEN (rq.t_Kind =" + string(DLRQ_KIND_COMMIT) + ") THEN -rq.t_Amount ELSE rq.t_Amount END) as PayVal, "
        +          " decode(rq.t_FactDate, TO_DATE ('01-01-0001', 'DD-MM-YYYY'), rq.T_PLANDATE, rq.t_FactDate) as PayDate, CHR(0) as IsPartial, " /*ищем компенсации*/
        + "        0.0 as t_IncomeRate, 0.0 as t_IncomeScale ";
    end;

    sql = sql
        +   " FROM dsctxlot_dbt lot, "
        +        " dfininstr_dbt fin, "
        +        " ddl_tick_dbt Tick, "
        +        " ddl_leg_dbt Leg, "
        +        " ddlrq_dbt pmo1, "
        +        " ddlrq_dbt pmo2, "
        +        " DAVOIRISS_DBT av, "
        +        " minlot, ";

    if( Kind == KIND_COUPON )
       sql = sql + " dfiwarnts_dbt warnt ";
    else
       sql = sql + " ddlrq_dbt rq ";
    end;

    sql = sql
        +  " WHERE lot.t_Type IN (" + string(TXLOTS_REPO) + ", " + string(TXLOTS_BACKREPO) + ") "
        +    " AND DECODE  (lot.t_Type, 3, lot.t_SaleDate, minlot.mindate) = minlot.mindate "
        +    " AND " + GetRepo1Date() + " <= " + GetSQLDate(m_EndDate)
        +    " AND " + GetRepo2Date() + " >= " + GetSQLDate(m_BegDate)
        +    " AND Tick.t_DealID = lot.t_DealID "
        +    " AND Leg.t_DealID = lot.t_DealID "
        +    " AND Leg.t_LegKind = 2 "
        +    " AND pmo1.t_DocID = lot.t_DealID "
        +    " AND pmo1.t_DocKind = Tick.t_BOfficeKind "
        +    " AND pmo1.t_Type = " + DLRQ_TYPE_PAYMENT /*CAi*/
        +    " AND pmo1.t_DealPart = 1 "
        +    " AND pmo2.t_DocID = lot.t_DealID "
        +    " AND pmo2.t_DocKind = Tick.t_BOfficeKind "
        +    " AND pmo2.t_Type = " + DLRQ_TYPE_PAYMENT /*CRi*/
        +    " AND pmo2.t_DealPart = 2 "
        +    " AND Fin.t_FIID = av.t_FIID ";

    if(Kind == KIND_COMPPAYM )
      sql = sql +  " AND lot.t_id = minlot.t_id ";
    else
      sql = sql +  " AND lot.t_dealid = minlot.t_dealid ";
    end;
    if( not m_IsWebService )
       GNUList = TArray();
       GNUList[GNUList.Size] = TaxPayRepo_Form.GetFieldValue( PNFLD_TAXPAYREPO_TAXGROUP );
    else
       GNUList = TaxPayRepo_Form.GetFieldValue( PNFLD_TAXPAYREPO_TAXGROUP );
    end;

    /*задана категория бумаги*/
    if( ( ValType( GNUList ) != V_UNDEF ) and ( GNUList.Size > 0 ) )
       while( GNUCount < GNUList.Size )
          if( ( ValType( GNUList[GNUCount] ) == V_INTEGER )
          and ( GNUList[GNUCount] > 0 ) )
           if( GNUAddWhere == "" )
                GNUAddWhere = GNUAddWhere + " AND ( ";
             else
                GNUAddWhere = GNUAddWhere + " OR ";
              end;
              GNUAddWhere = GNUAddWhere + " av.t_TaxGroup = " + String( GNUList[GNUCount] ) + " ";
           end;
           GNUCount = GNUCount + 1;
        end;
        if( GNUAddWhere != "" )
           GNUAddWhere = GNUAddWhere + " ) ";
        end;
        sql = sql + GNUAddWhere;
     end;

    /*задан вид бумаги*/
    AvrKindAddWhere = "";
    if( ( ValType( AvrKindList ) != V_UNDEF ) and ( AvrKindList.Size > 0 ) )

       AvrKindCount = 0;
       while( AvrKindCount < AvrKindList.Size )
          if( ( ValType( AvrKindList[AvrKindCount] ) == V_INTEGER )
                and ( AvrKindList[AvrKindCount] > 0 )
                and( AvrKindList[AvrKindCount] != AVOIRISSKIND_BASKET ) )
             if( AvrKindAddWhere == "" )
                AvrKindAddWhere = AvrKindAddWhere + " AND ( ";
             else
                AvrKindAddWhere = AvrKindAddWhere + " OR ";
             end;
             AvrKindAddWhere = AvrKindAddWhere + " RSB_FIInstr.FI_AvrKindsEQ( " + string(FIKIND_AVOIRISS) + ", " + String( AvrKindList[AvrKindCount] ) + ", fin.t_AvoirKind) > 0 ";

          end;
          AvrKindCount = AvrKindCount + 1;
       end;

       if( AvrKindAddWhere != "" )
          AvrKindAddWhere = AvrKindAddWhere + " ) ";
       end;

       sql = sql + AvrKindAddWhere;
    end;

    if( Kind == KIND_COMPPAYM )
       if( ( ValType( AvrKindList ) != V_UNDEF ) and ( AvrKindList.Size > 0 ) )
          if( ( AvrKindList.Size == 1 ) and  ( AvrKindList[AvrKindCount] == AVOIRISSKIND_BASKET ) )
             AvrKindAddWhere = AvrKindAddWhere +
             " AND RSB_SECUR.IsBasket( "
                 + " rsb_secur.get_OperationGroup( "
                   + " rsb_secur.get_OperSysTypes( Tick.t_DealType, Tick.t_BofficeKind ) "
                 + " ) "
               + " ) = 1 ";
          end;
       end;

       AvrFIIDAddWhere = "";
       if( ( ValType( AvrFIIDList ) != V_UNDEF ) and ( AvrFIIDList.Size > 0 ) )

          AvrFIIDCount = 0;
          while( AvrFIIDCount < AvrFIIDList.Size )

             if( ( ValType( AvrFIIDList[AvrFIIDCount] ) == V_INTEGER )
             and ( AvrFIIDList[AvrFIIDCount] > 0 ) )
                if( AvrFIIDAddWhere == "" )
                   AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
                else
                   AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
                end;

                AvrFIIDAddWhere = AvrFIIDAddWhere +
                   " ( "
                    + " fin.t_FIID = " + String( AvrFIIDList[AvrFIIDCount] ) + " "
                 + " AND "
                    + " RSB_SECUR.IsBasket( "
                       + " rsb_secur.get_OperationGroup( "
                          + " rsb_secur.get_OperSysTypes( Tick.t_DealType, Tick.t_BofficeKind ) "
                       + " ) "
                    + " ) = "
                       + " ( case "
                         + " when ( "
                                + " select t_avoirkind "
                                + " from dfininstr_dbt "
                                + " where t_fiid = " + String( AvrFIIDList[AvrFIIDCount] ) + " "
                              + " )= " + String( AVOIRISSKIND_BASKET ) + " "
                         + " then 1 "
                         + " else 0 "
                         + " end ) "
                 + " ) ";
             end;

             AvrFIIDCount = AvrFIIDCount + 1;
          end;

          if( AvrFIIDAddWhere != "" )
             AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
          end;

          sql = sql + AvrFIIDAddWhere;
       end;
    end; // if( Kind == KIND_COMPPAYM )

    if( Kind == KIND_COUPON )
       sql = sql +
          " AND fin.t_FIID = lot.t_FIID "
        + " AND warnt.t_FIID = fin.t_FIID "
        + " AND warnt.t_DrawingDate >= "+GetRepo1Date()
        + " AND warnt.t_DrawingDate <= "
        + " ( CASE WHEN " + GetRepo2Date() + " < " + GetSQLDate(m_EndDate)
               + " THEN " + GetRepo2Date()
               + " ELSE " + GetSQLDate(m_EndDate) + " END ) ";

       AvrFIIDAddWhere = "";
       if( ( ValType( AvrFIIDList ) != V_UNDEF ) and ( AvrFIIDList.Size > 0 ) )

          AvrFIIDCount = 0;
          while( AvrFIIDCount < AvrFIIDList.Size )

             if( AvrFIIDList[AvrFIIDCount] > ALLFININSTR )
                if( AvrFIIDAddWhere == "" )
                   AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
                else
                   AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
                end;

                AvrFIIDAddWhere = AvrFIIDAddWhere +
                   " ( "
                   + " ( CASE "
                     + " WHEN ( SELECT t_avoirkind "
                            + " FROM dfininstr_dbt  "
                            + " WHERE t_fiid = " + String( AvrFIIDList[AvrFIIDCount] ) + " ) = " + String( AVOIRISSKIND_BASKET ) + " "
                     + " THEN Tick.t_PFI "
                     + " ELSE Fin.t_Fiid "
                     + " END ) = " + String( AvrFIIDList[AvrFIIDCount] ) + " "
                 + " AND "
                   + " ( CASE "
                     + " WHEN RSB_SECUR.IsBasket( "
                             + " rsb_secur.get_OperationGroup( "
                                + " rsb_secur.get_OperSysTypes( t_DealType, t_BofficeKind ) ) ) = 0 "
                     + " THEN " + String( AvrFIIDList[AvrFIIDCount] ) + " "
                     + " ELSE Fin.t_FIID "
                     + " END ) = Fin.t_FIID "
                 + " AND "
                     " ( CASE "
                     + " WHEN ( select t_avoirkind "
                            + " from dfininstr_dbt "
                            + " where t_fiid = " + String( AvrFIIDList[AvrFIIDCount] ) + " )= " + String ( AVOIRISSKIND_BASKET ) + " "
                     + " THEN lot.t_ID "
                     + " ELSE minlot.t_ID "
                     + " END ) = lot.t_id "
                 + " ) ";
             end;

             AvrFIIDCount = AvrFIIDCount + 1;
          end;

          if( AvrFIIDAddWhere != "" )
             AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
          end;

          sql = sql + AvrFIIDAddWhere;
       else
          sql = sql + " AND lot.t_id = minlot.t_id ";
       end;
    else
       sql = sql + " AND fin.t_FIID =( case "
                 +                   " when RSB_SECUR.IsBasket ("
                 +                          " rsb_secur.get_OperationGroup ( "
                 +                             "rsb_secur.get_OperSysTypes (t_DealType, t_BofficeKind)))= 1"
                 +                   " then Tick.t_PFI "
                 +                   " else lot.t_FIID "
                 +                   " end)"
                 + " AND (rq.t_Type = "+string(DLRQ_TYPE_COMPPAYM) + " or rq.t_Type = "+string(DLRQ_TYPE_COMPDELIVERY)+ ") "

                 + " AND rq.t_DocID = lot.t_DealID "
                 + " AND rq.t_DocKind = Tick.t_BOfficeKind "           /*Компенсации сразу исполняются, поэтому проверяем фактическую дату*/
                 + " AND rq.t_FactDate >= DECODE(pmo1.t_FactDate, TO_DATE('01-01-0001','DD-MM-YYYY'), pmo1.t_PlanDate, pmo1.t_FactDate) "
                 + " AND rq.t_FactDate <= (CASE WHEN DECODE(pmo2.t_FactDate, TO_DATE('01-01-0001','DD-MM-YYYY'), pmo2.t_PlanDate, pmo2.t_FactDate) < " + GetSQLDate(m_EndDate)
                 + "                      THEN DECODE(pmo2.t_FactDate, TO_DATE('01-01-0001','DD-MM-YYYY'), pmo2.t_PlanDate, pmo2.t_FactDate) "
                 + "                      ELSE " + GetSQLDate(m_EndDate) + " END) ";
    end;

    return sql;

  END;

  PRIVATE MACRO Create()
    var sql;

    sql =   " with  minlot as( "
          + "   select min (t_saledate) as mindate, "
          + "          min (t_id) as t_id,          "
          + "          t_dealid"
          + "   from dsctxlot_dbt"
          + "   where t_type in( " + string (TXLOTS_REPO) + ", " + string(TXLOTS_BACKREPO) + ") "
          + "   group by t_dealid )"
          + FormQuery(KIND_COUPON)
          + " UNION ALL "
          + FormQuery(KIND_COMPPAYM)
          + " ORDER BY GroupName, "
          + "          finCode, "
          + "          DDP1, "
          + "          PayDate, "
          + "          DealCode, "
          + "          IsPartial, "
          + "          PayType";

    m_ProgressValue.Maximum = SQL_GetNRecs( sql );
    m_ProgressValue.Current = 0;

    m_report = TRsbDataSet(sql);

    if( m_IsWebService and (WebMenuItem != null) )
      var header = "Формирование отчета " + WebMenuItem.szNameItem;
      ProgressIndicator.Start(m_ProgressValue.Maximum, header, header);
    else
      ProgressIndicator.Start(m_ProgressValue.Maximum, "Отбор данных для отчета \"Вылаты по сделкам РЕПО\"", "Вылаты по сделкам РЕПО");
    end;

    ExecuteReport();

    ProgressIndicator.Stop();

    if( (not m_IsExistsData) and (not m_IsWebService) )
      msgbox("Нет данных для формирования отчета.");
    end;

  END;

  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  if( not IsWebService )
    TaxPayRepo_Form = SP_TaxPayRepo_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      TaxPayRepo_Form = WS_TX_TaxPayRepo_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = TaxPayRepo_Form.Run();
    end;

    if( stat )
      TaxPayRepo_Report = SP_TaxPayRepo_Report( null, "Report_TaxPayRepo.xls", false, IsWebService, ReportHashCode );
      TaxPayRepo_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat( TaxPayRepo_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
        FullReportFileNames = TaxPayRepo_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat( TaxPayRepo_Report.PrintMode() );
      end;
    end;
  end;

  return FullReportFileNames;
END;
