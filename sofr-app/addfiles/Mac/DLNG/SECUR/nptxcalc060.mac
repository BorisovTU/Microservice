/*
$Name:        nptxcalc060.mac
$Module:      Ценные бумаги
$Description: Расчет НОБ для НДФЛ. Шаг "Расчет НДР 4 уровня"
*/

/* Операция расчета НОБ для НДФЛ
   Шаг "Расчет НДР 4 уровня"*/
IMPORT "secinter.mac", "nptxcalcfun.mac";

PRIVATE VAR Oper = TRecHandler( "nptxop.dbt" );

PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR Stat = 0;

  Oper.SetRecordAddr( FDoc );

  VAR vBegDate:DATE, vPeriodBegDate:DATE,
      vEndDate:DATE;
  VAR query, cmd, DataSet;
  VAR count;
  VAR TaxPeriod = 0;

  datesplit(Oper.rec.OperDate, null, null, TaxPeriod);

  var TaxTeriodToLucre = CheckTaxTeriodToLucre(TaxPeriod);

  ClearGTT_DNPTXOBJ_TMP();
  if((Oper.rec.IIS == SET_CHAR) and (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_ENDYEAR)) //ИИС "Окончание года"
    var IsEndPeriod = 0;
    
    //Рассчитываем по периодам
    GetDateCalcIIS37(Oper, NPTXOPSTEP6, @vBegDate, @vEndDate, @IsEndPeriod);

    if(IsEndPeriod == 0)
      Oper.rec.subkind_operation = DL_TXBASECALC_OPTYPE_NORMAL;
    end;

    stat = CreateTaxObjects4(Oper, ID_Step, vBegDate, vEndDate, vPeriodBegDate);
    if((not stat) and (TaxTeriodToLucre == false))
      stat = CreateMaterialTaxObjects4(Oper, ID_Step, vBegDate, vEndDate);
    end;

    if(not stat)
      stat = DL_NPTX_StartInsertTaxObject(Oper.rec.ID, ID_Step);
    end;

    if(not stat)
      stat = DL_NPTX_PutMsg( string(date(vEndDate)), 1000 + 1 );
    end;

    DL_NPTX_SaveOperLog( Oper.rec.ID, (NPTXOPSTEP6*1000) + 1 );
  elif(Oper.rec.Recalc == SET_CHAR)//если пересчет, то по периодам пересчитываем НДР с 3 по 7 уровень.
    vBegDate = Oper.rec.BegRecalcDate;
    query = GetIncomeDateQuery(Oper.rec.ID, NPTXOPSTEP6, Oper.rec.EndRecalcDate);
    cmd = DL_RSDCommand(query);        
    count = cmd.GetCount();
    DataSet = cmd.Execute();
    if((DataSet.moveNext()) AND (not stat))
      vPeriodBegDate = GetBegPeriodDateInRecalcOper(Oper.rec.ID, (NPTXOPSTEP6*1000) + count, Oper.rec.BegRecalcDate);
      vEndDate = DataSet.t_operdate;
      if((vEndDate != Oper.rec.OperDate) and ((Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_LUCRE) or (TaxTeriodToLucre == false)))
        Oper.rec.subkind_operation = DL_TXBASECALC_OPTYPE_NORMAL;
      end;
      if(TaxTeriodToLucre == false)
        stat = CreateTaxObjects4(Oper, ID_Step, vBegDate, vEndDate, vPeriodBegDate);
        if(not stat)
          stat = CreateMaterialTaxObjects4(Oper, ID_Step, vBegDate, vEndDate);
        end;
      else
        if(Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_LUCRE)
          stat = CreateTaxObjects4(Oper, ID_Step, vBegDate, vEndDate, vPeriodBegDate);
        else
          stat = CreateMaterialTaxObjects4(Oper, ID_Step, vBegDate, vEndDate);
        end;
      end;
    end;
    if(not stat)
      stat = DL_NPTX_StartInsertTaxObject(Oper.rec.ID, ID_Step);
    end;
    if(not stat)
      stat = DL_NPTX_PutMsg( string(date(vEndDate)), 1000 + count );
    end;
    DL_NPTX_SaveOperLog( Oper.rec.ID, (NPTXOPSTEP6*1000) + count );

  else
    stat = DL_NPTX_PutMsg( "Расчет НДР 4 уровня", 40 );
    if(not stat)
      if(TaxTeriodToLucre == false)
        stat = CreateTaxObjects4(Oper, ID_Step);
        if(not stat)
          stat = CreateMaterialTaxObjects4(Oper, ID_Step);
        end;
      else
        if(Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_LUCRE)
          stat = CreateTaxObjects4(Oper, ID_Step);
        else
          stat = CreateMaterialTaxObjects4(Oper, ID_Step);
        end;
      end;
  
      if(not stat)
        stat = DL_NPTX_StartInsertTaxObject(Oper.rec.ID, ID_Step);
      end;
      DL_NPTX_SaveOperLog( Oper.rec.ID, 60 );
    end;
  end;

  return stat;
END;
