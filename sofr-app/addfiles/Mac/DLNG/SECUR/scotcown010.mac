/*
$Name:         scotcown010.mac
$Module:       Ценные бумаги
$Description:  Операция внебиржевой продажи/покупки ВО. Шаг "Инициализация"
*/

IMPORT InsCarryDoc, "sp_class.mac", "oprmassexec.mac", "sp_car2.mac", "SPQICheckOper.mac";

macro ExecuteStep( doc, FDoc)
  record tick (dl_tick);
  record SfCntr  ("sfcontr.dbt");
  var   sfssidl  = TArray;
  var rqacc = TRecHandler("dlrqacc.dbt");
  var FD, dat;
  var RefID = 0;
  var err = 0;

  ClearRecord( SfCntr );

  SetBuff(tick, FDoc ); 

  FD = SPFirstDoc(tick, false, true );

  DefineOprDate( FD.GetKindDate(DATE_DEALDATE),        FD.DateArray[DATE_DEALDATE]         );
  DefineOprDate( FD.GetKindDate(DATE_DEALEXEC),        FD.DateArray[DATE_DEALSETAVOIRISS]  );
  DefineOprDate( FD.GetKindDate(DATE_DEALSETAVOIRISS), FD.DateArray[DATE_DEALSETAVOIRISS]  );
  DefineOprDate( FD.GetKindDate(DATE_DEALPAY),         FD.DateArray[DATE_DEALPAY]          );
  DefineOprDate( FD.GetKindDate(DATE_DEALBEGINEXEC),   FD.DateArray[DATE_DEALBEGINEXEC]    );

  if( УстановитьНачальныйСтатусСделки( FD ) )
     return 1;
  end;

  if(tick.Flag3 == SET_CHAR)

    //Если контрагент ещё не находится на обслуживании, то поставим его. Без этого не создастся договор обслуживания
    //Данное действие выполняется без отката при откате шага!
    if(PT_BindClientWithBranch(tick.PartyID, PTSK_STOCKDL, tick.Department, NULL, FD.DateArray[DATE_DEALDATE]) == false)
      msgbox("Ошибка установки вида обслуживания ");
      return 1;
    end;

    //Создаём договора ослуживания контрагента, у которого объектом будет текущая сделка
    SfCntr.ID           = 0;                                      /* Идентификатор */
    SfCntr.objectType   = SF_SECUROWN;                            /* Тип объекта начисления SfObjectTypes */
    SfCntr.FIID         = -1;                                     /* Валюта объекта начисления */
    SfCntr.dateBegin    = FD.DateArray[DATE_DEALPAY];             /* Дата начала */
    SfCntr.dateProlong  = ZeroDate;                               /* -- пролонгации */
    SfCntr.dateClose    = ZeroDate;                               /* -- закрытия */
    SfCntr.DateConc     = FD.DateArray[DATE_DEALDATE];            /* -- заключения */
    SfCntr.ServKind     = PTSK_STOCKDL;                           /* TPTSERVKINDS */
    SfCntr.number       = tick.DealCode;                          /* Номер договора */
    SfCntr.payMethod    = SF_PAY_METHOD_PAYM;                     /* Метод формирования оплат */
    SfCntr.partyID      = tick.PartyID;                           /* Идентификатор клиента */
    SfCntr.ContractorID = {OurBank};
    SfCntr.Department   = tick.Department;                        /* Филиал */
    SfCntr.Branch       = tick.Branch;                            /* ВСП */
    SfCntr.object       = tick.DealCode;                          /* Объект начисления */

    if((GetReferenceIDByType(OBJTYPE_SFCONTR, 1/*REFOBJ_SFCONTR_NUMBER*/, RefID) != 0) or (GenerateReference(RefID, SfCntr.number, 0, SfCntr) != 0))
      msgbox("Ошибка при генерации номера договора");
      return 1;
    end;

    if(ПолучитьПлатежныеРеквизитыСубъектаПоСделке(tick.BOfficeKind, tick.DealID, tick.PartyID, DLRQ_SUBKIND_CURRENCY, -1, DLRQ_TYPE_UNKNOWN, rqacc) != 0)
      msgbox("Не найдены платежные реквизиты субъекта по сделке");
      return 1;
    end;
                                
    /*Заполнить параметры для СПИ договора по найденным платежным реквизитам*/
    sfssidl[0]               = TRecHandler("sfssidl.str");
    sfssidl[0].rec.BankID    = rqacc.rec.BankID;
    sfssidl[0].rec.Chapter   = rqacc.rec.Chapter;
    sfssidl[0].rec.FIID      = rqacc.rec.FIID; 
    sfssidl[0].rec.FeeType   = 0;
    sfssidl[0].rec.FeeNumber = 0;
    sfssidl[0].rec.Account   = rqacc.rec.Account; 

    if( not InsertSfContract( SfCntr, 10, sfssidl, 0, 0, NULL, 0 ) )
      MsgBox( "Ошибка при открытии договора обслуживания");
      return  1;
    end;

  end;

  return 0;
END;

