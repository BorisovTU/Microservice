/* SecurityService.mac
*/
import "SqlResultConvertHelper.mac", oralib;

class SecurityStruct
    var fiId:integer;
    var nominalValue:money;
    var nominalCurrency:integer;
    var drawingDate:date;
    var listOwnersDate:date;

    macro ToString()
        return String("SecurityStruct{",
            "  fiId:", fiId,
            ", nominalValue:", nominalValue,
            ", nominalCurrency:", nominalCurrency,
            ", drawingDate:", drawingDate,
            ", listOwnersDate:", listOwnersDate,
        "}");
    end;
end;

class SecurityService()
    private var sqlConverter:SqlResultConvertHelper = SqlResultConvertHelper();
    private var logger:c_logger = LoggerFactory().NewItLog("SecurityService").GetLogger();

    macro FindByFiId(fiId:integer):SecurityStruct
        var query = "select f.t_fiid, "
                  + "       f.t_facevalue, "
                  + "       f.t_facevaluefi, "
                  + "       f.t_drawingdate, "
                  + "       a.t_listdate "
                  + "  from dfininstr_dbt f "
                  + "  join davoiriss_dbt a on a.t_fiid = f.t_fiid "
                  + " where f.t_fiid = :fiid ";
        var sql = ExecSQLselectPrmDyn(query, fiId);

        if (not sql.MoveNext())
            RunError("Не найден инструмент с fiid = " + fiId);
        end;

        var security = SecurityStruct;
        security.fiId = sql.value("t_fiid");
        security.nominalValue = sql.value("t_facevalue");
        security.nominalCurrency = sql.value("t_facevaluefi");
        security.drawingDate = sqlConverter.getDate(sql.value("t_drawingdate"));
        security.listOwnersDate = sqlConverter.getDate(sql.value("t_listdate"));

        return security;
    end;

    macro getFiIdByIsin(isin:string)
        var query = "select t_fiid from davoiriss_dbt where t_isin = :isin";
        var sql = ExecSQLselectPrmDyn(query, isin);

        if (not sql.MoveNext())
            RunError("Не найден инструмент с isin = " + isin);
        end;

        return sql.value("t_fiid");
    end;

    macro FindByIsin(isin:string):SecurityStruct
        var fiId = getFiIdByIsin(isin);
        return FindByFiId(fiId);
    end;

    private macro updateFinInstrInfo(security:SecurityStruct)
        var query = "update dfininstr_dbt "
                  + "   set t_drawingdate = :1 "
                  + " where t_fiid = :fiid ";
        var params = MakeSqlParamsArray(security.drawingDate, security.fiId);
        ExecSql(query, params);
    end;

    private macro updateAvoirissInfo(security:SecurityStruct)
        var query = "update davoiriss_dbt "
                  + "   set t_listdate = :1 "
                  + " where t_fiid = :fiid ";
        var params = MakeSqlParamsArray(security.listOwnersDate, security.fiId);
        ExecSql(query, params);
    end;

    macro Save(security:SecurityStruct):SecurityStruct
        updateFinInstrInfo(security);
        updateAvoirissInfo(security);

        security = FindByFiId(security.fiId);
        return security;

    
    OnError(err)
        logger.ErrorClob("Ошибка при обновлении ценной бумаги. " + security, GetFullErrMsg(err));
        RsbRethrow(err, "Ошибка при обновлении ценной бумаги");
    end;

    /*
        количество ценных бумаг у клиента на дату.
        если clientId = 0 - сумма бумаг у всех клиентов
    */
    macro GetClientsQuantityOnDate(fiId:integer, calcDate:date, clientId:integer, contractId:integer):money
        if (clientId == null)
            clientId = 0;
        end;

        if (contractId == null)
            contractId = -1;
        end;

        var quantity:money = 0;
        var query = "select rsb_pmwrtoff.wrtGetAmountByClient(Department => -1, "
                  + "                                         FIID => :fiid, "
                  + "                                         Party => :clientId, "
                  + "                                         Contract => :contractId, "
                  + "                                         CalcDate => :calcDate) amount "
                  + "  from dual ";
        var sql = ExecSQLselectPrmDyn(query, fiId, clientId, contractId, calcDate);
        if (sql.MoveNext())
            quantity = sql.value("amount");
        end;

        return quantity;
    end;
end;