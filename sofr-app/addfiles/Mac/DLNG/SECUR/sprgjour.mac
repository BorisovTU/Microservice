/*                           
$Name:             sprgjour.mac
$Module:           Депозитарий
$Description:      Журнал регистрации счетов депо
*/

import deposerv, "dlmisc.mac";

VAR __acc_kind, __acc_type, __beg_date, __end_date, __isRewPart, __LogOprID, __dprt_num,_isapp;
var IsHeadPinted = 0, IsPRN = 0, IsprintedUpperHead = 0;

FILE   dacctypes ("depoac");
RECORD OwnerParty("party");

var HeadTable = "┌──────────────────────────────────────────────┬──────────────────────┬─────────────┬─────────────┐\n"+
                "│  Код счета ДЕПО                              │     Тип счета        │Дата открытия│Дата закрытия│\n"+
                "├──────────────────────────────────────────────┼──────────────────────┼─────────────┼─────────────┤";
var width = Index(HeadTable, "\n");

var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

macro DepoKindSubHeader(Kind)
   if ( Kind == depo_acc_active )
      Rep.AddEmptyStr();
      DP_AddPrintCell( Rep, " Счета депо мест хранения", 30, 0, "l:" + RepFontStyleTitel,_isapp, REP_ELEM_STR );
      Rep.AddStr();
   else 
      Rep.AddEmptyStr();
      DP_AddPrintCell( Rep, " Счета депо депонентов", 30, 0, "l:" + RepFontStyleTitel,_isapp, REP_ELEM_STR );
      Rep.AddStr();
   end;
end;

macro TablHeader(Kind)
   if(not IsHeadPinted)
      DepoKindSubHeader(Kind);
      IsPRN = 1;      
      IsHeadPinted = 1;
   end;
end;

macro PrintLine(DepoAccCode, DepoType, OpenDate, CloseDate)
   DP_AddPrintCell( Rep, DepoAccCode, 0, 0, "l:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, DepoType, 0, 0, "l:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, OpenDate, 0, 0, "l:f:" + RepFontStyleTitel );
   DP_AddPrintCell( Rep, CloseDate, 0, 0, "l:" + RepFontStyleTitel );
   Rep.AddStr();
end;

macro PrintOwnerLine(PartyID)
   if (ПолучитьСубъекта  (PartyID, OwnerParty) != 0)
     msgBox ("Не найден владелец счета депо (PartyID=", PartyID, ")");
   end;

   DP_AddPrintCell( Rep, " Владелец    " + ПолучитьКодСубъекта(PartyID, PTCK_CONTR) + "  " + OwnerParty.ShortName, 
                     Rep.GetColWidthTable(0)+Rep.GetColWidthTable(1)+Rep.GetColWidthTable(2)+Rep.GetColWidthTable(3)+3, 0, "l:" + RepFontStyleTitel );
   Rep.AddStr();
end;


macro end_line()
   if(IsHeadPinted)
      Rep.AddStr();
      IsHeadPinted = 0;
   end;
end;

/*=-=-=-=-=-=-=--=-=-==-=-=-=-=-=-=-=-=-=--=-=-=-=-=-==--=-=-=-=-=-=-=-=--=-=-=-=-=-=*/

macro RewDepoSubPart(preAccName, AutoKey, acc_kind)

   var  depoPart, Select, query;
   var  TypeName, CloseDate;
   var  preName = "";

   query =   " select * from ddepoacnt_dbt "
           + "  where t_Superior = ?"
           + "    and t_OpenDate <> " + GetSQLDate(date(0,0,0))
           + "    and t_OpenDate <= ?";
         
   if(__beg_date > date(0,0,0))       
     query = query + " and t_OpenDate >= ? ";
   end;

   if(acc_kind != depo_acc_any)
     query = query + " and t_Kind = ?";
   end;

   query = query + " order by t_BriefCode";

   Select = RsdCommand(query);
  
   Select.addParam( "", RSDBP_IN, AutoKey );
   Select.addParam( "", RSDBP_IN, __end_date );

   if(__beg_date > date(0,0,0))       
     Select.addParam( "", RSDBP_IN, __beg_date );
   end;

   if(acc_kind != depo_acc_any)
     Select.addParam( "", RSDBP_IN, acc_kind );
   end;

   Select.execute();

   depoPart = TRsbDataSet(Select);

   while(depoPart.moveNext())
      preName = preAccName;
      dacctypes.ID = depoPart.Type;
      if ( getEQ( dacctypes ) )
         TypeName = trim( dacctypes.ShortName );
      else
         TypeName = "";
      end;
      if( depoPart.Status == 1 ) /* Закрыт */
         if(date(depoPart.CloseDate) > date(__end_date))
            CloseDate = "НЕ ЗАКРЫТ";
         else
            CloseDate = String(date(depoPart.CloseDate):f);
         end;
      else
         CloseDate = "НЕ ЗАКРЫТ";
      end;
      PrintLine(preAccName+depoPart.BriefCode+"("+depoPart.Name+")", TypeName, date(depoPart.OpenDate), CloseDate);
      preName = preAccName+" ";
      RewDepoSubPart(preName, depoPart.AutoKey, acc_kind);
   end;
end;

macro Depoacc_for_kind(acc_kind)
  var depoacc;
  var query = "";
  
  var TypeName; 
  var i = 0, isFirst = 1;
  var CloseDate, preAccName = " ";
  var Progress = CProgressBar;

  query =  " select acnt.*, dpac.t_ShortName as TypeName"
         + "   from ddepoacnt_dbt acnt, ddepoac_dbt dpac "
         + "  where acnt.t_Superior = 0 "
         + "    and dpac.t_ID = acnt.t_Type "
         + "    and acnt.t_OpenDate <> " + GetSQLDate(date(0,0,0))
         + "    and acnt.t_OpenDate <= " + GetSQLDate(__end_date);
         
  if(__beg_date > date(0,0,0))       
    query = query + " and acnt.t_OpenDate >= " + GetSQLDate(__beg_date);
  end;

  if(acc_kind != depo_acc_any)
    query = query + " and acnt.t_Kind = " + acc_kind;
  end;

  if(__acc_type != 0)
    query = query + " and acnt.t_Type = " + __acc_type;
  end;

  if(__dprt_num > 0)
    query = query + " and acnt.t_Department = " + __dprt_num;
  end;

  query = query + " order by acnt.t_BriefCode";

  Progress.Init(SQL_GetNRecs(query), "Поиск счетов для отчета", "Просмотр счетов ДЕПО");

  depoacc = TRsbDataSet(query);
  while(depoacc.moveNext())
     TypeName = trim( depoacc.TypeName );
     if( depoacc.Status == 1 ) /* Закрыт */
       if(date(depoacc.CloseDate) > date(__end_date))
          CloseDate = "НЕ ЗАКРЫТ";
       else
          CloseDate = String(date(depoacc.CloseDate):f);
       end;
     else
       CloseDate = "НЕ ЗАКРЫТ";
     end;

     if( not IsprintedUpperHead )
       if(__beg_date == date(0,0,0))
         DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Журнал регистрации счетов ДЕПО", __LogOprID, 0, false, __end_date, null, width );
       else
         DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Журнал регистрации счетов ДЕПО", __LogOprID, 0, false, __beg_date, __end_date, width );
       end;
       IsprintedUpperHead = 1;
     end;

     TablHeader(acc_kind);
     PrintOwnerLine(depoacc.Owner);
     PrintLine(depoacc.BriefCode+"("+depoacc.Name+")", TypeName, date(depoacc.OpenDate), CloseDate);
     if(__isRewPart)
        RewDepoSubPart(preAccName, depoacc.AutoKey, acc_kind);
     end;
     i = Progress.Use();
  end;

  Progress.Remove();
  end_line();
end;

private macro CreateEmptyRep()
  if(__beg_date == date(0,0,0))
    DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Журнал регистрации счетов ДЕПО", __LogOprID, 0, false, __end_date, null, width );
  else
    DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Журнал регистрации счетов ДЕПО", __LogOprID, 0, false, __beg_date, __end_date, width );
  end;
  Rep.AddEmptyStr();
  DP_AddPrintCell( Rep, "Нет данных, удовлетворяющих параметрам отчета.", 100, 0, "l:" + RepFontStyleTitel,_isapp, REP_ELEM_STR );
  Rep.AddEmptyStr();
  DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, width);

  return 1;
end;

macro registration_journal( LogOprID, acc_type, acc_kind, dprt_num, beg_date, end_date, isRewPart, ToExcel, isapp)
     var stat;
     Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
     DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

     /*изменение глобализмов*/
     __dprt_num = dprt_num;
     __acc_type = acc_type;
     __beg_date = beg_date;
     __end_date = end_date;
     __isRewPart= isRewPart;
     __LogOprID = LogOprID;
     _isapp = isapp;

     if ( acc_kind == depo_acc_any )
          Depoacc_for_kind(depo_acc_active);
          Depoacc_for_kind(depo_acc_passive);
     else
          Depoacc_for_kind(acc_kind);
     end;

     if ( IsPRN == 0 )
          stat = 1;
     else
          DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, width);
          stat = 0;
     end;

     DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
     DP_EndProtocol();                 //закончить протоколирование

     return DP_StdReportControl( __LogOprID, (not stat), @CreateEmptyRep, 0, ToExcel, Rep );
end;

OnError
     DepoSubAcc_FindClose;
     DepoAcc_FindClose;
     RunError;
end;
