/* SecurRedemptionSpreadCreator.mac

  Макрос для создания операции рапределения сумм в рамках погашения ценных бумаг
  Ценные бумаги - Сервисные операции - Погашения - Распределения сумм погашений
*/
import oralib, likepy;
import "SecurDealsCreator.mac", "cblogger2.mac", "SecurRedemptionService.mac";
import globals, "SecurityService.mac", "BondCoupons.mac";
import "spretirg.mac", "SecurRedemptionLinkService.mac";
import "SecurRedemptionSpreadDealService.mac";

private class SecurRedemptionSpreadClass
    var redemptionDate:date;
    var listOwnersDate:date;
    var depositaryId:integer;
    var fiId:integer;
    var nominalValue:money;
    var nominalCurrency:integer;
    var couponNumber:integer;
    var quantity:integer;
    var nkd:money;
    var dealCode:string;
end;

class SecurRedemptionSpreadCreator
    private var logger:c_logger = LoggerFactory().NewItLog("SecurRedemptionSpreadCreator").GetLogger();
    private var securService:SecurityService = SecurityService();
    private var linkService:SecurRedemptionLinkService = SecurRedemptionLinkService();
    private var redemptionService:SecurRedemptionService = SecurRedemptionService();
    private var couponService:BondCouponService = BondCouponService();
    private var spreadService:SecurRedemptionSpreadDealService = SecurRedemptionSpreadDealService();

    private macro CreateDeal(spreadRedemption:SecurRedemptionSpreadClass):integer
        var dealCreator:SecurDealsCreator = SecurDealsCreator();

        dealCreator.bofficeKind = spreadService.BOFFICE_KIND;
        dealCreator.dealType = spreadService.DEAL_TYPE;
        dealCreator.clientId = -1;
        dealCreator.contractId = -1;
        dealCreator.dealCodeTS = "";
        dealCreator.typeDoc = "T";
        dealCreator.dealTime = Time();

        dealCreator.dealDate = spreadRedemption.redemptionDate;
        dealCreator.payDate = spreadRedemption.redemptionDate;
        dealCreator.supplyDate = spreadRedemption.redemptionDate;
        dealCreator.startDate = spreadRedemption.listOwnersDate;
        dealCreator.partyId = spreadRedemption.depositaryId;
        dealCreator.marketId = spreadRedemption.depositaryId;
        dealCreator.fiId = spreadRedemption.fiId;
        dealCreator.price = spreadRedemption.nominalValue;
        dealCreator.couponNumber = spreadRedemption.couponNumber;
        dealCreator.payFiId = spreadredemption.nominalCurrency;
        dealCreator.principal = spreadRedemption.quantity;
        dealCreator.cost = spreadRedemption.nominalValue * spreadRedemption.quantity;
        dealCreator.nkd = spreadRedemption.nkd;
        dealCreator.totalCost = dealCreator.cost + dealCreator.nkd;
        dealCreator.dealCode = spreadRedemption.dealCode;

        if (not dealCreator.Create())
            RunError("Ошибка при создании операции распределения сумм");
        end;

        return dealCreator.GetID();
    end;

    macro CreateByRedemptionId(redemption:SecurRedemptionStruct):bool
        var spreadRedemption:SecurRedemptionSpreadClass = SecurRedemptionSpreadClass();
        var listOwnersDate:date = IfThenElse(redemption.listOwnersDate == Date(0, 0, 0), redemption.fixDate + 1, redemption.listOwnersDate);

        spreadRedemption.redemptionDate = {curdate};
        spreadRedemption.listOwnersDate = listOwnersDate;
        spreadRedemption.depositaryId = redemption.depositaryId;

        var security = securService.FindByIsin(redemption.isin);
        spreadRedemption.fiId = security.fiId;
        spreadRedemption.nominalValue = security.nominalValue;
        spreadRedemption.nominalCurrency = security.nominalCurrency;

        if (security.listOwnersDate != listOwnersDate)
            security.listOwnersDate = listOwnersDate;
            security = securService.Save(security);
        end;

        if ((redemption.actionType = "MCAL") or (redemption.actionType == "BRUT"))
            couponService.DeleteAfterDate(security.fiId, redemption.fixDate);
        end;

        var coupon:BondCoupon = couponService.FindLast(security.fiId);
        spreadRedemption.couponNumber = coupon.number;

        spreadRedemption.quantity = securService.GetClientsQuantityOnDate(
            spreadRedemption.fiId,
            spreadRedemption.redemptionDate);

        spreadRedemption.nkd = СуммаКупона(security.fiId, spreadRedemption.quantity, security.drawingDate);
        spreadRedemption.dealCode = spreadService.GenerateDealCode();

        var dealId = CreateDeal(spreadRedemption);
        redemption.dealId = dealId;
        redemption = redemptionService.Save(redemption);

        return true;
    OnError(errObj)
        logger.ErrorClob("CreateByRedemptionId. redemptionID = " + redemption.redemptionId, GetFullErrMsg(errObj));
        return false;
    end;

    macro CreateLinks(redemption:SecurRedemptionStruct):bool
        if (linkService.IsExistsByDealId(redemption.dealId))
            RunError("У операции с ид = " + redemption.dealId + " уже существуют распределённые суммы."
                + " На данный момент повторное распределение невозможно");
        end;

        var links = linkService.CalcLinks(redemption.dealId);
        for (var link, links)
            if (link.clientId == {OurBank})
                logger.Info("Не создаётся связь для лотов, где владелец - РСХБ: ИД операции распределения = " + redemption.dealId);
                continue;
            end;

            link.redemptionId = redemption.redemptionId;
            linkService.SaveLink(link);
        end;

        GenerateRetireSumsByDealId(redemption.dealId);
        
        return true;
    OnError(errObj)
        logger.ErrorClob("CreateLinks", GetFullErrMsg(errObj));
        return false;
    end;
end;