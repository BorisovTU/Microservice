/*
$Name:        scavrreg.mac
$Module:      Ценные бумаги
$Description: Отчет " Регистр внутреннего учета денежных средств и расчетов по сделкам и операциям с ценными бумагами"
*/
import RsbDataSet;
import "spRepFun.mac","or_rep_h.mac", "dl_class.mac", "DlReport_h.mac";

PRIVATE VAR TableHeader1 =
"┌──────────────┬────────────────────┬────────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬────────────────┐\n" +
"│ Наименование │        Вид         │         №          │      Вид        │       №         │  Вход. остаток, │    Кол-во ц/б   │ Исх. остаток   │\n" +
"│  расчетной   │  сделки(операции)  │  сделки(операции)  │ подтверждающего │ подтверждающего │       шт.       │                 │      шт.       │\n" +
"│   операции   │       с ц/б        │       с ц/б        │    документа    │    документа    │                 │                 │                │\n" +
"│              │                    │                    │                 │                 │                 │                 │                │\n" +
"├──────────────┼────────────────────┼────────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼────────────────┤";


PRIVATE VAR TableHeader2 =
"┌──────────────┬────────────────────┬────────────────────┬─────────────────┬─────────────────┬─────────────────┬─────────────────┬────────────────┬─────────────────┬──────────────┐\n" +
"│ Наименование │       Вид          │         №          │       Вид       │        №        │Входящий остаток │Вход. остаток    │   Кол-во ц/б   │Исх. остаток     │ Исх. остаток │\n" +
"│  расчетной   │  сделки(операции)  │  сделки(операции)  │ подтверждающего │ подтверждающего │  ц/б, шт.       │по обязательствам│                │по обязательствам│ ц/б, шт.     │\n" +       
"│   операции   │      с ц/б         │       с ц/б        │    документа    │    документа    │                 │клиента          │                │клиента          │              │\n" +
"│              │                    │                    │                 │                 │                 │перед банком     │                │перед банком     │              │\n" +
"│              │                    │                    │                 │                 │                 │                 │                │                 │              │\n" +
"│              │                    │                    │                 │                 │                 │                 │                │                 │              │\n" +
"├──────────────┼────────────────────┼────────────────────┼─────────────────┼─────────────────┼─────────────────┼─────────────────┼────────────────┼─────────────────┼──────────────┤";

PRIVATE CONST ALL_ITEM = -1; // Пункт Меню: ВСЕ

PRIVATE CONST BANKACC   = 0,
              CLIENTACC = 1;

PRIVATE VAR RKIND = TArray();
RKIND[BANKACC] = "Счета учета ценных бумаг";
RKIND[CLIENTACC] = "Счета расчетов с клиентами по ценным бумагам";

PRIVATE VAR RTABLE = TArray();
RTABLE[BANKACC] = TableHeader1;
RTABLE[CLIENTACC] = TableHeader2;

/////////////////////////////////////////////////////////////////////////////////////////////////////////

private var DlAccountsFormData;
private var Rep;
var Sec_Accounts;
var NRecs;
var IsNotEof;
var Head, sqlQuery;

var PartyShNamesArr = TArray();

private var ErrorMesage = DL_TUniqStrCollector();

const FontBold =  "ex_FS(b)";
const FontTitle = "ex_FS(b):ex_FN(Arial):ex_FZ(12)";

/////////////////////////////////////////////////////////////////////////////////////////////////////////
/*<H1.1>*/  
macro PrintHead0_1( PreProDate )
  var  res = false;
  if( PreProDate != Sec_Accounts.ProDate )
     Rep.PrintHead0_2( Sec_Accounts.ProDate, Sec_Accounts.ProDate );
     res = true;
  end;
  return res;
end;
 
private macro DigitIsApp(Digit)
  if(Digit)
    if(DlAccountsFormData.IsUseOpostrofs)
      return string(Digit:a);
    else
      return string(Digit);
    end;
  else
    return "";
  end;
end;

/*
"┌──────────────┬────────────────────┬────────────────────┬────────────┬────────────┬────────────────┬───────────┬────────────────┐\n" +
"│ Наименование │        Вид         │         №          │    Вид     │     №      │  Вход. остаток,│Кол-во ц/б │ Исх. остаток   │\n" +
"│  расчетной   │  сделки(операции)  │  сделки(операции)  │ первичного │ первичного │       шт.      │           │      шт.       │\n" +
"│   операции   │       с ц/б        │       с ц/б        │  документа │  документа │                │           │                │\n" +
"│              │                    │                    │            │            │                │           │                │\n" +
"├──────────────┼────────────────────┼────────────────────┼────────────┼────────────┼────────────────┼───────────┼────────────────┤";
       <A1>              <A2>                  <A3>            <A4>         <A5>            <A6>          <A7>           <A8>
*/

/*                                                                                                          
"┌──────────────┬────────────────────┬────────────────────┬─────────────┬─────────────┬────────────────┬─────────────────┬─────────────┬─────────────────┬──────────────┐\n" +
"│ Наименование │       Вид          │         №          │     Вид     │      №      │Входящий остаток│Вход. остаток    │ Кол-во ц/б  │Исх. остаток     │ Исх. остаток │\n" +
"│  расчетной   │  сделки(операции)  │  сделки(операции)  │ первичного  │ первичного  │  ц/б, шт.      │по обязательствам│             │по обязательствам│ ц/б, шт.     │\n" +       
"│   операции   │      с ц/б         │       с ц/б        │  документа  │  документа  │                │клиента          │             │клиента          │              │\n" +
"│              │                    │                    │             │             │                │перед банком     │             │перед банком     │              │\n" +
"│              │                    │                    │             │             │                │                 │             │                 │              │\n" +
"│              │                    │                    │             │             │                │                 │             │                 │              │\n" +
"├──────────────┼────────────────────┼────────────────────┼─────────────┼─────────────┼────────────────┼─────────────────┼─────────────┼─────────────────┼──────────────┤";
      <A1>               <A2>                 <A3>              <A4>          <A5>           <A6>              <A7>           <A8>             <A8>             <A10>
*/

/*Тело отчета*/                                                                      
////////////////////////////////////////////////////////////////////////////////////////////////////////
macro GetAccounts_1()
  var categ:string = "";

  if( DlAccountsFormData.SubjectID == {OurBank} )
     categ = " = 550 ";
  else
     if( DlAccountsFormData.SubjectID != ALL_ITEM )
        categ = " = 560 ";
     else
        if( DlAccountsFormData.SubjectID == ALL_ITEM )
           categ = " in (550, 560) ";
        end;
     end;
  end;

  var HeadCount = " SELECT inacc.t_ID ";

  Head = " SELECT acc.t_Account Acc, inacc.t_DebAcc DebAcc, acc.t_DepartmentID DepID, acc.t_Owner Owner, " +
             "        acc.t_Place Place, acc.t_Fiid Fiid, acc.t_Clientcontrid Clientcontrid, acc.t_Currency Currency, " +
             "        inacc.t_Opertype Opertype, inacc.t_DealKind DealKind, " +
             "        inacc.t_Date ProDate,  inacc.t_GroundKind, inacc.t_GroundNum," +
             "        inacc.t_DocumentKind DocKind, inacc.t_DocumentID DocID, inacc.t_Sum Sum, inacc.t_Chapter Chapter, " +
             "        sf.t_number numconc, sf.t_dateconc dateconc," +
             "        NVL(avr.T_ISSUE, chr(1)) ISSUE, NVL(avr.T_LSIN, chr(1)) LSIN, NVL(avr.T_SERIES, chr(1)) SERIES, NVL(avr.T_TRANCHE, chr(1)) TRANCHE," +
             "        fin.T_FI_CODE AvrCode, fin.T_NAME AvrName, avrkind.T_Name AvrKindName," +
             "        party.T_SHORTNAME IssuerName, lvalues.T_NAME OperationName, " +
             "        pt.t_ShortName as DepartmentName, " +
             "        case when Rsb_Secur.GetDealID(inacc.t_DocumentKind,inacc.t_DocumentID) != 0 " +
             "             then Rsb_Secur.GetDealCode(inacc.t_DocumentKind,inacc.t_DocumentID) " +
             "             else utl_raw.cast_to_varchar2(dbms_lob.substr(inacc.t_FmtBlobData_XXXX,2000)) end DealNumber, "+ /*в DealNumber сохраняются номера сделок в РОВУ и при вводе проводок руками*/
             "        Rsb_Secur.GetDealID(inacc.t_DocumentKind,inacc.t_DocumentID) DealID, inacc.t_PartyContrID, " +
             "        abs(rsb_account.restac(acc.t_Account, acc.t_Currency, (inacc.t_Date - 1), inacc.t_Chapter, null)) as t_Rest ";

  sqlQuery = " FROM ddlinacc_dbt inacc, davoiriss_dbt avr, dfininstr_dbt fin, " + 
                 "      davrkinds_dbt avrkind, dparty_dbt party, dllvalues_dbt lvalues, ddp_dep_dbt dp, dparty_dbt pt, " +
                 "      (SELECT acc.t_Account, acc.t_DepartmentID, acc.t_Place, acc.t_Owner, acc.t_Fiid, acc.t_Clientcontrid, acc.t_Currency " +
                 "         FROM dmcaccdoc_dbt acc " +
                 "        WHERE acc.t_CatNum" + categ +
                 "          AND acc.t_Chapter = 22 " +
                 "      GROUP BY acc.t_DepartmentID, acc.t_Owner, acc.t_Place, acc.t_Fiid, acc.t_Account, " +
                 "      acc.t_Clientcontrid, acc.t_Currency) acc left join dsfcontr_dbt sf on sf.t_id = acc.t_Clientcontrid " +   

                 "   left outer join dparty_dbt Pty " +
                 "     on  Pty.T_PARTYID =  case when acc.T_Owner  = -1 then " + {OurBank} +"   else acc.T_Owner end "+

 
                 " WHERE ((inacc.t_DebAcc = acc.t_Account) or (inacc.t_KredAcc = acc.t_Account)) " +
                 "   AND inacc.t_DealKind in (101,117,176,127,138,159,135,139) "  +
                 "   AND avr.T_FIID = fin.T_FIID " +
                 "   AND avrkind.T_AVOIRKIND = fin.T_AVOIRKIND " +                   
                 "   AND party.T_PARTYID = RSI_RSB_FIInstr.FI_GetIssuerOnDate( fin.T_FIID , " + GetSQLDate(DlAccountsFormData.EndDate) + "  ) "  +
                 "   AND avrkind.T_FI_KIND = " + string(FIKIND_AVOIRISS) +
                 "   AND avr.T_FIID = acc.t_Fiid " +
                 "   AND lvalues.T_LIST = " + OBJTYPE_CALCOPKIND + ""
                 "   AND lvalues.T_ELEMENT = inacc.t_Opertype " +
                 "   AND inacc.t_Date <=" + GetSQLDate(DlAccountsFormData.EndDate) +
                 "   AND inacc.t_Date >=" + GetSQLDate(DlAccountsFormData.BeginDate) +
                 "   AND dp.t_Code = acc.t_DepartmentID " +
                 "   AND pt.t_PartyID = dp.t_PartyID ";

  if( DlAccountsFormData.DepartmentID != ALL_ITEM )
     sqlQuery = sqlQuery + "AND acc.t_DepartmentID = " + String( DlAccountsFormData.DepartmentID );
  end;
  if( DlAccountsFormData.SubjectID != ALL_ITEM )
     sqlQuery = sqlQuery + "AND acc.t_Owner = " + String( DlAccountsFormData.SubjectID );
  end;

  if(DlAccountsFormData.SubjectID != {OurBank} )

    if( (DlAccountsFormData.IsForm1 > 0) and (DlAccountsFormData.FormSub1 != 0) )
      if(DlAccountsFormData.IsForm1 == 1)
        sqlQuery = sqlQuery + "    and   ((Pty.T_LEGALFORM = " + String (DlAccountsFormData.FormSub1) + " ) or (acc.T_Owner  = -1))" ;
      end;
      if(DlAccountsFormData.IsForm1 == 2)            	
        sqlQuery = sqlQuery + "    and   ((Pty.T_LEGALFORM !=   " + String (DlAccountsFormData.FormSub1) + ") or (acc.T_Owner  = -1))";
      end;
    end;

    if( (DlAccountsFormData.IsVid1 > 0) and (DlAccountsFormData.VidSub1 != 0) )            
      if(DlAccountsFormData.IsVid1 == 1)
        sqlQuery = sqlQuery + "   and   (( EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (DlAccountsFormData.VidSub1) +" and Pty.T_PARTYID = Pto.T_PARTYID)) or ( acc.T_Owner  = -1 ) )  " ;
      end;
      if(DlAccountsFormData.IsVid1 == 2)            	
        sqlQuery = sqlQuery + "    and   ((NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (DlAccountsFormData.VidSub1)+ " and Pty.T_PARTYID = Pto.T_PARTYID)) or (acc.T_Owner  = -1 )) " ;
      end;
    end;

    if(DlAccountsFormData.NoResident1)
      sqlQuery = sqlQuery + " and  ((Pty.T_NOTRESIDENT = 'X') or (acc.T_Owner  = -1))  ";
    end;

  end;


  if( DlAccountsFormData.AccountingPlace != ALL_ITEM )
     sqlQuery = sqlQuery + "AND acc.t_Place = " + String( DlAccountsFormData.AccountingPlace );
  end;
  if( DlAccountsFormData.SecurIssue != ALL_ITEM )
     sqlQuery = sqlQuery + "AND fin.T_FIID = " + String( DlAccountsFormData.SecurIssue );
  end;
  if( DlAccountsFormData.Issuer_1 != ALL_ITEM )
     sqlQuery = sqlQuery + "AND party.T_PARTYID = " + String( DlAccountsFormData.Issuer_1 );  
  end;
  if( DlAccountsFormData.SecurKind_1 != ALL_ITEM )
     sqlQuery = sqlQuery + "AND avrkind.T_AVOIRKIND = " + String( DlAccountsFormData.SecurKind_1 );
  end;

  sqlQuery = sqlQuery + " AND (select count(1) from dacctrn_dbt acctrn " +
                        "       where acctrn.t_AccTrnID = inacc.t_AccTrnID " +
                        "         and acctrn.t_State = 1 ) > 0 ";

  if( DlAccountsFormData.IsSeparatelyDate == "X" )
     sqlQuery = sqlQuery + "ORDER BY acc.t_DepartmentID, inacc.t_Date, acc.t_Owner, fin.T_FI_CODE, acc.t_Account, inacc.t_DebAcc";
  else
     sqlQuery = sqlQuery + "ORDER BY acc.t_DepartmentID, acc.t_Owner, fin.T_FI_CODE, acc.t_Account, inacc.t_Date, inacc.t_DebAcc";
  end;

  InitProgressBar(-1, "Отчет: Регистр внутреннего учета ценных бумаг", "Подсчет количества записей для отчета");
  NRecs = SQL_GetNRecs(HeadCount + sqlQuery);
  RemProgressBar;

  if( NRecs > 0 )
     InitProgressBar(-1, "Отчет: Регистр внутреннего учета ценных бумаг", "Отбор данных");
     Sec_Accounts = TRsbDataSet(Head + sqlQuery);
     RemProgressBar;

     if( Sec_Accounts.MoveNext() )
        return true;
     else
        return false;
     end;
  else
     return false;
  end;
end;

macro GetAccounts_2()

  var HeadCount = " SELECT inacc.t_ID ";

  Head = " SELECT acc.t_Account Acc, inacc.t_DebAcc DebAcc, acc.t_DepartmentID DepID, acc.t_Owner Owner, " +
             "        acc.t_Fiid Fiid, acc.t_Clientcontrid Clientcontrid, acc.t_Currency Currency, " +
             "        inacc.t_Opertype Opertype, inacc.t_DealKind DealKind, " +
             "        inacc.t_Date ProDate,  inacc.t_GroundKind, inacc.t_GroundNum, " +
             "        inacc.t_DocumentKind DocKind, inacc.t_DocumentID DocID, inacc.t_Sum Sum, inacc.t_Chapter Chapter, " +
             "        sf.t_number numconc, sf.t_dateconc dateconc," +
             "        NVL(avr.T_ISSUE, chr(1)) ISSUE, NVL(avr.T_SERIES, chr(1)) SERIES, NVL(avr.T_TRANCHE, chr(1)) TRANCHE," +
             "        fin.T_FI_CODE AvrCode, fin.T_NAME AvrName, NVL(avr.T_LSIN, chr(1)) LSIN, avrkind.T_Name AvrKindName," +
             "        party.T_SHORTNAME IssuerName, lvalues.T_NAME OperationName, " +
             "        pt.t_ShortName as DepartmentName, " +
             "        case when Rsb_Secur.GetDealID(inacc.t_DocumentKind,inacc.t_DocumentID) != 0 " +
             "             then Rsb_Secur.GetDealCode(inacc.t_DocumentKind,inacc.t_DocumentID) " +
             "             else utl_raw.cast_to_varchar2(dbms_lob.substr(inacc.t_FmtBlobData_XXXX,2000)) end DealNumber, "+ /*в DealNumber сохраняются номера сделок в РОВУ и при вводе проводок руками*/
             "        Rsb_Secur.GetDealID(inacc.t_DocumentKind,inacc.t_DocumentID) DealID, inacc.t_PartyContrID," +
             "        abs(rsb_account.restac(acc.t_Account, acc.t_Currency, (inacc.t_Date - 1), inacc.t_Chapter, null)) as t_Rest ";

  sqlQuery = " FROM ddlinacc_dbt inacc, davoiriss_dbt avr, dfininstr_dbt fin, dparty_dbt pty, " + 
                 "      davrkinds_dbt avrkind, dparty_dbt party, dllvalues_dbt lvalues, ddp_dep_dbt dp, dparty_dbt pt, " +
                 "      (SELECT acc.t_Account, acc.t_DepartmentID, acc.t_Owner, acc.t_Fiid, acc.t_Clientcontrid, acc.t_Currency " +
                 "         FROM dmcaccdoc_dbt acc " +
                 "        WHERE acc.t_CatNum = 580 " +
                 "          AND acc.t_Chapter = 22 " +
                 "      GROUP BY acc.t_DepartmentID, acc.t_Owner, acc.t_Fiid, acc.t_Account, " +
                 "      acc.t_Clientcontrid, acc.t_Currency) acc left join dsfcontr_dbt sf on sf.t_id = acc.t_Clientcontrid " +    
                 " WHERE ((inacc.t_DebAcc = acc.t_Account) or (inacc.t_KredAcc = acc.t_Account)) " +
                 "   AND inacc.t_DealKind in (101,117,176,127,138,159,135,139) "  +
                 "   AND avr.T_FIID = fin.T_FIID " +
                 "   AND avrkind.T_AVOIRKIND = fin.T_AVOIRKIND " +                   
                 "   AND party.T_PARTYID = RSI_RSB_FIInstr.FI_GetIssuerOnDate( fin.T_FIID , " + GetSQLDate(DlAccountsFormData.EndDate) + "  ) "  +
                 "   AND avrkind.T_FI_KIND = " + string(FIKIND_AVOIRISS) +
                 "   AND avr.T_FIID = acc.t_Fiid " +
                 "   AND lvalues.T_LIST = " + OBJTYPE_CALCOPKIND + ""
                 "   AND lvalues.T_ELEMENT = inacc.t_Opertype " +
                 "   AND inacc.t_Date <=" + GetSQLDate(DlAccountsFormData.EndDate) +
                 "   AND inacc.t_Date >=" + GetSQLDate(DlAccountsFormData.BeginDate) +
                 "   AND dp.t_Code = acc.t_DepartmentID " +
                 "   AND pty.t_PartyID = acc.T_OWNER " +
                 "   AND pt.t_PartyID = dp.t_PartyID ";

  if( DlAccountsFormData.DepartmentID != ALL_ITEM )
     sqlQuery = sqlQuery + " AND acc.t_DepartmentID = " + String( DlAccountsFormData.DepartmentID );
  end;
  if( DlAccountsFormData.ClientID != ALL_ITEM )
     sqlQuery = sqlQuery + " AND acc.t_Owner = " + String( DlAccountsFormData.ClientID );
  end;

  if( (DlAccountsFormData.IsForm2 > 0) and (DlAccountsFormData.FormSub2 != 0) )
    if(DlAccountsFormData.IsForm2 == 1)
      sqlQuery = sqlQuery + "    and   pty.T_LEGALFORM = " + String (DlAccountsFormData.FormSub2) ;
    end;
    if(DlAccountsFormData.IsForm2 == 2)            	
      sqlQuery = sqlQuery + "    and   pty.T_LEGALFORM !=   " + String (DlAccountsFormData.FormSub2);
    end;
  end;

  if( (DlAccountsFormData.IsVid2 > 0) and (DlAccountsFormData.VidSub2 != 0) )            
    if(DlAccountsFormData.IsVid2 == 1)
      sqlQuery = sqlQuery + "   and    EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (DlAccountsFormData.VidSub2) +" and pty.T_PARTYID = Pto.T_PARTYID)  " ;
    end;
    if(DlAccountsFormData.IsVid2 == 2)            	
      sqlQuery = sqlQuery + "    and   NOT EXISTS (select 1 from dpartyown_dbt Pto where Pto.T_PARTYKIND =" + String (DlAccountsFormData.VidSub2)+ " and pty.T_PARTYID = Pto.T_PARTYID)  " ;
    end;
  end;

  if(DlAccountsFormData.NoResident2)
    sqlQuery = sqlQuery + " and  pty.T_NOTRESIDENT = 'X'  ";
  end;

  if( DlAccountsFormData.ContractID != ALL_ITEM )
     sqlQuery = sqlQuery + " AND acc.t_Clientcontrid = " + String( DlAccountsFormData.ContractID );
  end;
  if( DlAccountsFormData.SecurCode != ALL_ITEM )
     sqlQuery = sqlQuery + " AND fin.T_FIID = " + String( DlAccountsFormData.SecurCode );
  end;
  if( DlAccountsFormData.Issuer_2 != ALL_ITEM )
     sqlQuery = sqlQuery + " AND party.T_PARTYID = " + String( DlAccountsFormData.Issuer_2 );  
  end;
  if( DlAccountsFormData.SecurKind_2 != ALL_ITEM )
     sqlQuery = sqlQuery + " AND avrkind.T_AVOIRKIND = " + String( DlAccountsFormData.SecurKind_2 );
  end;

  sqlQuery = sqlQuery + " AND (select count(1) from dacctrn_dbt acctrn " +
                        "       where acctrn.t_AccTrnID = inacc.t_AccTrnID " +
                        "         and acctrn.t_State = 1 ) > 0 ";

  if( DlAccountsFormData.IsSeparatelyDate == "X" )
     sqlQuery = sqlQuery + "ORDER BY acc.t_DepartmentID, inacc.t_Date, acc.t_Owner, fin.T_FI_CODE, acc.t_Account, inacc.t_DebAcc";
  else
     sqlQuery = sqlQuery + " ORDER BY acc.t_DepartmentID, acc.t_Owner, fin.T_FI_CODE, acc.t_Account, inacc.t_Date, inacc.t_DebAcc";
  end;

  InitProgressBar( -1, "Отчет: Регистр внутреннего учета ценных бумаг", "Подсчет количества записей для отчета" );
  NRecs = SQL_GetNRecs(HeadCount + sqlQuery);
  RemProgressBar;

  if( NRecs > 0 )
     InitProgressBar(-1, "Отчет: Регистр внутреннего учета ценных бумаг", "Отбор данных");
     Sec_Accounts = TRsbDataSet(Head + sqlQuery);
     RemProgressBar;

     if( Sec_Accounts.MoveNext() )
        return true;
     else
        return false;
     end;
  else
     return false;
  end;
end;

PRIVATE MACRO GetFirstDoc( GroundKind/*, XLD*/)
  var query, Firstdoc, Doc = "";

  query = RSDCommand(" select t_Name from dllvalues_dbt " +
                     "  where t_List = " + string(OBJTYPE_KINDDOC) +
                     "    and t_element = ?"
                    );

  query.addParam("", RSDBP_IN, GroundKind);

  query.execute();

  Firstdoc = TRsbDataSet(query);

  if( Firstdoc.MoveNext() )
     Doc = Doc + string(FirstDoc.Name)/* + " № " + string(XLD) + "\n"*/;
   end;

  return Doc;
END;

macro PrintDep( PreDepartment, IsClientAccount )
  if( PreDepartment != Sec_Accounts.DepID )
     Rep.PrintHead0( Sec_Accounts.DepID, Sec_Accounts.DepartmentName, IsClientAccount );
  end;
end;

macro PrintClient( PreClient, IsClientAccount )
  var Subject;
  if( PreClient != Sec_Accounts.Owner )
    if( IsClientAccount == 0 )
      if( (DlAccountsFormData.SubjectID == {OurBank}) OR (Sec_Accounts.Owner == -1) )
        Rep.PrintHead1_11();
      else
        if(ValType(PartyShNamesArr[Sec_Accounts.Owner]) != V_UNDEF)
          Subject = PartyShNamesArr[Sec_Accounts.Owner];
        else
          Subject = ПолучитьКороткоеИмяСубъекта( Sec_Accounts.Owner );
          PartyShNamesArr[Sec_Accounts.Owner] = Subject;
        end;
        Rep.PrintHead1_12( Subject, Sec_Accounts.numconc, Sec_Accounts.dateconc );
      end;
    else
      if(ValType(PartyShNamesArr[Sec_Accounts.Owner]) != V_UNDEF)
        Subject = PartyShNamesArr[Sec_Accounts.Owner];
      else
        Subject = ПолучитьКороткоеИмяСубъекта( Sec_Accounts.Owner );
        PartyShNamesArr[Sec_Accounts.Owner] = Subject;
      end;
      Rep.PrintHead1_2( Subject, Sec_Accounts.numconc, Sec_Accounts.dateconc );
    end;
  end;
end;

macro PrintAccount_1( PreAccount, PreProDate, Rest, Out:@money )
  var Subject;
  var Place;
  var AccountName = "";
  var In = 0;
  var OperKind = "";
  var OperCode, StrError = "";

  Out  = 0;
  if( PreAccount != Sec_Accounts.Acc )
    if(ValType(PartyShNamesArr[Sec_Accounts.Place]) != V_UNDEF)
      Place = PartyShNamesArr[Sec_Accounts.Place];
    else
      Place = ПолучитьКороткоеИмяСубъекта( Sec_Accounts.Place );
      PartyShNamesArr[Sec_Accounts.Place] = Place;
    end;
    
    if( (DlAccountsFormData.SubjectID == {OurBank}) OR (Sec_Accounts.Owner == -1) )
       AccountName = AccountName + "Ц/Б " + String(Sec_Accounts.AvrCode) + " банка " + " в " + String(Place);
    else
       if(ValType(PartyShNamesArr[Sec_Accounts.Owner]) != V_UNDEF)
         Subject = PartyShNamesArr[Sec_Accounts.Owner];
       else
         Subject = ПолучитьКороткоеИмяСубъекта(Sec_Accounts.Owner);
         PartyShNamesArr[Sec_Accounts.Owner] = Subject;
       end;

       AccountName = AccountName + "Ц/Б " + String(Sec_Accounts.AvrCode) + " клиента " + String(Subject) + " в " + String(Place) + " дог. № " + String(Sec_Accounts.numconc);
    end;
    In = Sec_Accounts.Rest;
    Rep.PrintHead4( Sec_Accounts.Acc, AccountName );
    Rep.PrintLineDate1( Sec_Accounts.ProDate );
  else
    if( PreProDate != Sec_Accounts.ProDate )
      Rep.PrintLineDate1( Sec_Accounts.ProDate );
    end;
    In = Rest;
  end;

  if( Sec_Accounts.Acc == Sec_Accounts.DebAcc ) 
      Out = In + Sec_Accounts.Sum;/*зачисление*/
  else
      Out = In - Sec_Accounts.Sum;/*списание*/
  end;                

  if( not SP_GetOperKindAndCodeByInACC(Sec_Accounts, @OperKind, @OperCode, @StrError) )
     ErrorMesage.AddString(StrError);
  end;

 Rep.PrintLine1( Sec_Accounts.OperationName, OperKind, OperCode, GetFirstDoc( Sec_Accounts.GroundKind), Sec_Accounts.GroundNum , In , Sec_Accounts.Sum , Out );
end;                

macro GetPositivRest( SumAcc )
  if( SumAcc > 0 )
    return SumAcc;
  else 
    return 0;
  end;
end;

macro GetAbsRest( SumAcc )
  if( SumAcc < 0 )
    return Abs( SumAcc );
  else
    return 0;
  end;
end;

macro GetRestOut( Amount, SumIn )
  var 
     A8 = GetPositivRest( SumIn ), /*вход. ост.*/
     A9 = GetAbsRest( SumIn ); /*вход. ост. по обязат. клиента перед банком*/

  if( Sec_Accounts.Acc != Sec_Accounts.DebAcc )
     return A8 - A9 + Amount;/*зачисление*/
  else
     return A8 - A9 - Amount;/*списание*/
  end;
end;

macro PrintAccount_2( PreAccount, PreProDate, Rest, Out:@money )
  var Subject;
  var Place;
  var AccountName = "";
  var In = 0;
  var OperKind = "";
  var OperCode, StrError = "";

  Out  = 0;

  if( PreAccount != Sec_Accounts.Acc )   
    if(ValType(PartyShNamesArr[Sec_Accounts.Owner]) != V_UNDEF)
      Subject = PartyShNamesArr[Sec_Accounts.Owner];
    else
      Subject = ПолучитьКороткоеИмяСубъекта(Sec_Accounts.Owner);
      PartyShNamesArr[Sec_Accounts.Owner] = Subject;
    end;
  
    AccountName = AccountName + "Расчеты по " + String(Sec_Accounts.AvrCode) + " с " + String(Subject) + " по договору № " + String(Sec_Accounts.numconc);
    Rep.PrintHead4( Sec_Accounts.Acc, AccountName );
    Rep.PrintLineDate2( Sec_Accounts.ProDate );
    In = Sec_Accounts.Rest;
  else
    if( PreProDate != Sec_Accounts.ProDate )
      Rep.PrintLineDate2( Sec_Accounts.ProDate );
    end;
    In = Rest;
  end;

  Out = GetRestOut( Sec_Accounts.Sum, In );

  if( not SP_GetOperKindAndCodeByInAcc( Sec_Accounts, @OperKind, @OperCode, @StrError ) )
     ErrorMesage.AddString(StrError);
  end;

  Rep.PrintLine2( Sec_Accounts.OperationName, OperKind, OperCode, GetFirstDoc(Sec_Accounts.GroundKind), Sec_Accounts.GroundNum, GetPositivRest( In ), GetAbsRest( In ), Sec_Accounts.Sum, GetAbsRest(Out), GetPositivRest(Out) );  
end; 

PRIVATE MACRO ExcRep( IsBank:bool )
  var PreDepartment = "";                                                                   
  var PreClient = "";
  var PrePlace = "";
  var PreAvoiriss = "";
  var PreAccount = "";
  var PreProDate;
  var In = 0, Out = 0, i = 0;
  var i1 = 0;

  if( ((IsBank) and (GetAccounts_1() != false)) or ((not IsBank) and (GetAccounts_2() != false)) )

      IsNotEof = true;

      InitProgressBar(NRecs, "Отчет: Регистр внутреннего учета ценных бумаг", "Формирование отчета " + RKIND[Rep.RepKind]);

      while( IsNotEof )
        PrintDep( PreDepartment, IIF(IsBank,0,1) );
        if(DlAccountsFormData.IsSeparatelyDate == "X" )
          if( PrintHead0_1( PreProDate ) )
            PreClient = "";
            PrePlace = "";
            PreAvoiriss = "";
            PreAccount = "";
          end;
        else
          if( PreDepartment != Sec_Accounts.DepID )
            Rep.PrintHead0_2( DlAccountsFormData.BeginDate, DlAccountsFormData.EndDate );
          end;
        end;
        PrintClient(PreClient, IIF(IsBank,0,1));
        PreClient = Sec_Accounts.Owner;

        if( PreDepartment != Sec_Accounts.DepID )
          Rep.PrintHead2( IIF(IsBank,0,1) );
        end;

        PreDepartment = Sec_Accounts.DepID;

        if( PreAvoiriss != Sec_Accounts.AvrCode )
          Rep.PrintHead3( Sec_Accounts.AvrName, Sec_Accounts.AvrCode, Sec_Accounts.IssuerName, Sec_Accounts.AvrKindName, Sec_Accounts.ISSUE, Sec_Accounts.SERIES, Sec_Accounts.TRANCHE );
          PreProDate = "";
        end;

        PreAvoiriss = Sec_Accounts.AvrCode;

        if( IsBank )
           PrintAccount_1(PreAccount, PreProDate, In, @Out);
        else
           PrintAccount_2(PreAccount, PreProDate, In, @Out);
        end;

        In = Out;
        PreAccount = Sec_Accounts.Acc;
        PreProDate = Sec_Accounts.ProDate;
        IsNotEof   = Sec_Accounts.MoveNext();
        UseProgressBar;
        
        i1 = i1 + 1;
      end;
      RemProgressBar;

      Rep.PrintRepFooter();

      if( ErrorMesage.size > 0 )
         i= 0;
         while( i < ErrorMesage.size )
            Rep.PrintErrorMessage( ErrorMesage[i] );
            i = i + 1;
         end;
      end;

    else

      Rep.PrintErrorMessage("Не найдено данных для формирования отчета " + RKIND[Rep.RepKind]);
    end;

END;

macro MakeReport( DataFromForm, _Rep:@variant )

  Rep = _Rep;

  ErrorMesage.size = 0;

  DlAccountsFormData = DataFromForm;
  if( DlAccountsFormData.IsSAccounts_Report == "X" )
     Rep.RepKind = BANKACC;
     Rep.SetActiveSheet( "Отчёт" );
     Rep.SetPrefix("N1_");
     ExcRep(true);
  end;

  ErrorMesage.size = 0;
  if( DlAccountsFormData.IsCL_SAccounts_Report == "X" )
     Rep.RepKind = CLIENTACC;
     Rep.SetActiveSheet( "Отчёт_Кл" );
     Rep.SetPrefix("N2_");
     ExcRep(false);
  end;

  _Rep = Rep;

end;
