/*
$Name:        nptxsnobrep.mac
$Module:      Ценные бумаги
$Description: Отчет "Отчет по СНОБ и удержанному НДФЛ"
*/
import "nptxsnobrep_form.mac", "sprepfun.mac", "sqlconv.mac", "CbReport_h.mac", "scsrvrepfun.mac";
import GetReportClientOperationReq;

private CONST REP_NPTX_13_MAX_VALUE = 2400000;
private CONST REP_NPTX_15_MAX_VALUE = 5000000;
private CONST REP_NPTX_18_MAX_VALUE = 20000000;
private CONST REP_NPTX_20_MAX_VALUE = 50000000;

PRIVATE CLASS (DL_CReportTemplate) NPTXSNOBREP_Report(ClientID:integer, TaxPeriod:integer)
  PRIVATE VAR m_ClientID  = ClientID;
  PRIVATE VAR m_TaxPeriod = TaxPeriod;
  PRIVATE VAR m_Req = NULL;
  PRIVATE VAR m_CountNotActiveClients = 0;

  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  MACRO CReport_Show( NumCopy:INTEGER, ReportName:@STRING )

    if(SheetUsed( "Протокол выполнения отчета" ))
      SetActiveSheet("Протокол выполнения отчета");
      RenameSheet("Сообщения");
    end;

    CReport_Show( NumCopy, ReportName );
  END;

  PRIVATE MACRO InitReq()
    m_Req = c_GetReportClientOperationRequest;
    
    m_Req.GetReportClientOperationReq.TaxYear        = m_TaxPeriod;  // Налоговый период, к которому относится событие (налоговый год)
    m_Req.GetReportClientOperationReq.ClientInfoList = TArray;       // Список информации по клиентам
  END;


  PRIVATE MACRO ExistClientTBbyStorState(ClientID, StorState)
    var query, cmd, DataSet;

    query =   " select count(1) as cnt "
            + "   from dnptxtotalbase_dbt tb "
            + "  where tb.t_ClientID = ? "
            + "    and tb.t_TaxPeriod = ? "
            + "    and tb.t_StorState = ? "
            + "    and ROWNUM = 1";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(ClientID);
    cmd.AddParam(m_TaxPeriod);
    cmd.AddParam(StorState);

    DataSet = cmd.Execute();
    if((DataSet.moveNext()) and (DataSet.Cnt > 0))
      return true;
    end;

    return false;
  END;

  PRIVATE MACRO AddClientToReq(ClientID)
    if(ExistClientTBbyStorState(ClientID, NPTXTOTALBASE_STORSTATE_ACTIVE) == false)
      m_CountNotActiveClients = m_CountNotActiveClients + 1;
    else
      var sz = m_Req.GetReportClientOperationReq.ClientInfoList.size;
      
      m_Req.GetReportClientOperationReq.ClientInfoList[sz]                   = c_ClientInfo_GetReportClientOperation;
      m_Req.GetReportClientOperationReq.ClientInfoList[sz].ClientId          = c_IntegrationSymbolicIdentifierXType();
      m_Req.GetReportClientOperationReq.ClientInfoList[sz].ClientId.ObjectId = ClientID; // ID клиента в системе-источнике
    end;
  END;

  PRIVATE MACRO GetDefault(Val, DefaultVal)

    if(Val == NULL)
      return DefaultVal;
    end;

    return Val;
  END;

  PRIVATE MACRO ExecReq()
    var Resp = NULL;
    var ins_nptxsnobrep = RsbSQLInsert("nptxsnobrep.tmp");
    var nptxsnobrep_new = TRecHandler( "nptxsnobrep.tmp" );
    var i = 0, j = 0;

    if(m_Req.GetReportClientOperationReq.ClientInfoList.size == 0)
      return;
    end;
    
    InitProgress(-1, "Отправка запроса в Хранилище", "Отправка запроса в Хранилище");
    Resp = GetReportClientOperationReq(m_Req);
    RemProgress();
    
    if(ValType(Resp) != V_GENOBJ)
      msgbox("Ошибка получения ответа от Хранилища");
      return;                                                                                                                                              
    end;

    if((Resp.ErrorList != NULL) and (Resp.ErrorList[0].ErrorCode != "0") and (Resp.ErrorList[0].ErrorCode != "000"))
      msgbox(Resp.ErrorList[0].ErrorCode + " " + Resp.ErrorList[0].ErrorDesc);
    end;
     
    if(Resp.ReportRecordClientList != NULL)
      InitProgress(Resp.ReportRecordClientList.size, "Сохранение информации по клиентам", "Сохранение информации по клиентам");

      while(i < Resp.ReportRecordClientList.size)

        if((ValType(Resp.ReportRecordClientList[i].ReportRecordList) != V_UNDEF) and (Resp.ReportRecordClientList[i].ReportRecordList.size > 0))

          InitProgress(Resp.ReportRecordClientList[i].ReportRecordList.size, "Сохранение записей по клиенту", "Сохранение записей по клиенту");
          j = 0;
          while(j < Resp.ReportRecordClientList[i].ReportRecordList.size)
            var ReportRecord = Resp.ReportRecordClientList[i].ReportRecordList[j];

            nptxsnobrep_new.Clear();

            nptxsnobrep_new.rec.IncomeRegionDate    = date(ReportRecord.IncomeRegionDateTime);
            nptxsnobrep_new.rec.IncomeRegionTime    = time(ReportRecord.IncomeRegionDateTime);
            nptxsnobrep_new.rec.IncomeDate          = date(ReportRecord.IncomeDateTime);
            nptxsnobrep_new.rec.IncomeTime          = time(ReportRecord.IncomeDateTime);
            nptxsnobrep_new.rec.NobAmount           = GetDefault(ReportRecord.NOBAmount, 0);           
            nptxsnobrep_new.rec.NdflCalculateAmount = GetDefault(ReportRecord.NDFLCalculateAmount, 0); 
            nptxsnobrep_new.rec.NdflTaxRate         = GetDefault(ReportRecord.NDFLTaxRate, 0);         
            nptxsnobrep_new.rec.NdflKeepAmount      = GetDefault(ReportRecord.NDFLKeepAmount, 0);      
            nptxsnobrep_new.rec.NDFLKeepRate        = GetDefault(ReportRecord.NDFLKeepRate, 0);        
            nptxsnobrep_new.rec.KbkCalculate        = GetDefault(ReportRecord.KBKCalculate, "");        
            nptxsnobrep_new.rec.KbkKeep             = GetDefault(ReportRecord.KBKKeep, "");
            if(ReportRecord.TaxBaseType)          
              nptxsnobrep_new.rec.TaxBaseType       = GetDefault(ReportRecord.TaxBaseType.RecordCode, 0);        
            end;
            if(ReportRecord.TaxPayerStatus)
              nptxsnobrep_new.rec.TaxPayerStatus    = GetDefault(ReportRecord.TaxPayerStatus.RecordCode, 0);     
            else
              if(Resp.ReportRecordClientList[i].ClientId)
                nptxsnobrep_new.rec.TaxPayerStatus  = STB_GetClientTaxPayerStatus(Resp.ReportRecordClientList[i].ClientId.ObjectID);
              end;
            end;
            if(ReportRecord.SystemCode)
              nptxsnobrep_new.rec.SystemCode        = GetDefault(ReportRecord.SystemCode.RecordCode, "");         
            end;
            if(Resp.ReportRecordClientList[i].ClientId)
              nptxsnobrep_new.rec.ClientID          = GetDefault(Resp.ReportRecordClientList[i].ClientId.ObjectID, -1);
            end;
            if(ReportRecord.RecordId)
              nptxsnobrep_new.rec.RecordID          = GetDefault(ReportRecord.RecordId.ObjectID, "");           
            end;
            if(ReportRecord.OperationId)
              nptxsnobrep_new.rec.OperationID       = GetDefault(ReportRecord.OperationId.ObjectID, "");        
            end;
            if(ReportRecord.EventId)
              nptxsnobrep_new.rec.EventID           = GetDefault(ReportRecord.EventId.ObjectID, "");            
            end;
            if(ReportRecord.EventType)
              nptxsnobrep_new.rec.EventType         = GetDefault(ReportRecord.EventType.RecordCode, "");
            end;

            ins_nptxsnobrep.AddRecord(nptxsnobrep_new);

            UseProgress(j = j + 1);
          end;

          RemProgress();
        end;

        UseProgress(i = i + 1);
      end;

      ins_nptxsnobrep.Insert();

      RemProgress();
    end;
  END;

  PRIVATE MACRO CreateRepData()
    var query, cmd, DataSet;
    var cnt = 0, i = 0, j = 0;
    var Req = NULL;

    m_CountNotActiveClients = 0;

    DL_RSDCommand("TRUNCATE TABLE DNPTXSNOBREP_TMP").ExecuteCMD();

    cmd = DL_RSDCommand();

    if(m_ClientID == -1)
      var BegDate = date(1,1,m_TaxPeriod);
      var EndDate = date(31,12,m_TaxPeriod);
      
      query =    " select DISTINCT sf.t_PartyID as t_ClientID "
               + "   from dpersn_dbt p, dsfcontr_dbt sf "
               + "  where sf.t_PartyID = p.t_PersonID "
               + "    and sf.t_servkind in ("+PTSK_STOCKDL+","+PTSK_DV+","+PTSK_SVO+") "
               + "    and sf.t_DateBegin <= ? "
               + "    and (sf.t_DateClose = " +GetSQLDate(date(0,0,0))+ " or sf.t_DateClose >= ?)"
               + "  order by sf.t_PartyID ASC "; 

      cmd.AddParam(EndDate);
      cmd.AddParam(BegDate);
    else
      query =    " select selpt.t_ClientID "
               + "   from ddlselectpt_tmp selpt "
               + "  where selpt.t_SelectFlag = 'X' "
               + "  order by selpt.t_ClientID ASC ";
    end;

    cnt = cmd.GetCount(query);
    if(cnt > 0)
      InitProgress(cnt, "Получение информации по клиентам", "Получение информации по клиентам");

      DataSet = cmd.Execute(query);

      i = 0;
      InitReq();
      while(DataSet.moveNext())
        
        AddClientToReq(DataSet.ClientID);

        if(m_Req.GetReportClientOperationReq.ClientInfoList.size == 100)
          ExecReq();
          InitReq();
        end;

        UseProgress(i = i + 1);
      end;

      if(m_Req.GetReportClientOperationReq.ClientInfoList.size > 0)
        ExecReq();
      end;

      if(m_CountNotActiveClients > 0)
        msgbox("Для "+m_CountNotActiveClients+" клиента(ов) записи в хранилище СНОБ от СИ(СОФР) отсутствуют. Ограничение на вывод данных от иных СИ.");
      end;

      RemProgress();
    end;                                    
  END;                                 

  PRIVATE MACRO PrintSheetBySS()
    var query, cmd, DataSet, cnt = 0;
    var i = 0;
    var SheetName = "СВОД НДФЛ в разрезе СИ";

    SetActiveSheet(SheetName);
    
    RegisterTable( "N1_MainTable", NULL,
                   "N1_ClientID",   
                   "N1_ClientName",    
                   "N1_SourceSystem",  
                   "N1_KBK",   
                   "N1_Rate",  
                   "N1_TaxDiff",   
                   "N1_HoldTax",   
                   "N1_CalcTax"
                 );

    query =   "select q.*, pt.t_Name "
            + "  from (select sn.t_ClientID, sn.t_SystemCode, sn.t_KBKKeep, sn.t_NDFLKeepRate, "
            + "               SUM(sn.t_NDFLKeepAmount) as SumNDFLKeepAmount, "
            + "               SUM(sn.t_NDFLCalculateAmount) as SumNDFLCalculateAmount "
            + "          from dnptxsnobrep_tmp sn "
            + "         group by sn.t_ClientID, sn.t_SystemCode, sn.t_KBKKeep, sn.t_NDFLKeepRate "
            + "       ) q, dparty_dbt pt "
            + " where pt.t_PartyID = q.t_ClientID "
            + " order by q.t_ClientID, q.t_SystemCode, q.t_KBKKeep, q.t_NDFLKeepRate ";

    cmd = DL_RSDCommand(query);

    cnt = cmd.GetCount();
    if(cnt > 0)
      InitProgress(cnt, "СВОД НДФЛ в разрезе СИ", "СВОД НДФЛ в разрезе СИ");

      DataSet = cmd.Execute();

      while(DataSet.moveNext())

        PrintTableLine("N1_ClientID",       DataSet.ClientID,                   null,
                       "N1_ClientName",     DataSet.Name,                       null,
                       "N1_SourceSystem",   DataSet.SystemCode,                 null,
                       "N1_KBK",            DataSet.KBKKeep,                    null,
                       "N1_Rate",           double(DataSet.NDFLKeepRate)/100.0, null,
                       "N1_TaxDiff",        DataSet.SumNDFLCalculateAmount-DataSet.SumNDFLKeepAmount, null,
                       "N1_HoldTax",        DataSet.SumNDFLKeepAmount,          null,
                       "N1_CalcTax",        DataSet.SumNDFLCalculateAmount,     null
                      );

        UseProgress(i = i + 1);
      end;

      RemProgress();
    end; 

    EndTable();

    return cnt;
  END;

  PRIVATE MACRO PrintSheetTotal2025_1()
    var query, cmd, DataSet, cnt = 0;
    var i = 0;
    var SheetName = "СВОД ОБЩИЙ - СНОБ 1";

    SetActiveSheet(SheetName);

    SetActiveSheet(SheetName);
    
    RegisterTable( "N2_MainTable", NULL,
                   "N2_ClientID",
                   "N2_ClientName",
                   "N2_SNOB",
                   "N2_CalcTaxDiff13",
                   "N2_CalcTax13",
                   "N2_EstimTax13",
                   "N2_CalcTaxDif15",
                   "N2_CalcTax15",
                   "N2_EstimTax15",
                   "N2_CalcTaxDif18",
                   "N2_CalcTax18",
                   "N2_EstimTax18",
                   "N2_CalcTaxDif20",
                   "N2_CalcTax20",
                   "N2_EstimTax20",
                   "N2_CalcTaxDif22",
                   "N2_CalcTax22",
                   "N2_EstimTax22",
                   "N2_CalcTax30",
                   "N2_NobAsup",
                   "N2_NobDiasoft",
                   "N2_NobSofr",
                   "N2_NobCft"
                 );

    query =   "select q.*, pt.t_Name "
            + "  from (select sn.t_ClientID, "
            + "               SUM((CASE WHEN sn.t_TaxPayerStatus = 1 THEN sn.t_NOBAmount ELSE 0 END)) as ResidentSumNOBAmount, "
            + "               SUM((CASE WHEN sn.t_TaxPayerStatus <> 1 THEN sn.t_NOBAmount ELSE 0 END)) as NotResidentSumNOBAmount, "
            + "               SUM(sn.t_NOBAmount) as SumNOBAmount, "
            + "               SUM((CASE WHEN sn.t_NDFLTaxRate = 13 THEN sn.t_NDFLCalculateAmount ELSE 0 END)) as SumNDFLCalculateAmount13, "
            + "               SUM((CASE WHEN sn.t_NDFLTaxRate = 15 THEN sn.t_NDFLCalculateAmount ELSE 0 END)) as SumNDFLCalculateAmount15, "
            + "               SUM((CASE WHEN sn.t_NDFLTaxRate = 18 THEN sn.t_NDFLCalculateAmount ELSE 0 END)) as SumNDFLCalculateAmount18, "
            + "               SUM((CASE WHEN sn.t_NDFLTaxRate = 20 THEN sn.t_NDFLCalculateAmount ELSE 0 END)) as SumNDFLCalculateAmount20, "
            + "               SUM((CASE WHEN sn.t_NDFLTaxRate = 22 THEN sn.t_NDFLCalculateAmount ELSE 0 END)) as SumNDFLCalculateAmount22, "
            + "               SUM((CASE WHEN sn.t_NDFLTaxRate = 30 THEN sn.t_NDFLCalculateAmount ELSE 0 END)) as SumNDFLCalculateAmount30, "
            + "               SUM((CASE WHEN sn.t_SYSTEMCODE = '"+OUTSYS_SYSTEMCODE_ASUP+"' THEN sn.t_NOBAmount ELSE 0 END)) as NobAsup, "
            + "               SUM((CASE WHEN sn.t_SYSTEMCODE = '"+OUTSYS_SYSTEMCODE_DIASOFT+"' THEN sn.t_NOBAmount ELSE 0 END)) as NobDiasoft, "
            + "               SUM((CASE WHEN sn.t_SYSTEMCODE = '"+OUTSYS_SYSTEMCODE_SOFR+"' THEN sn.t_NOBAmount ELSE 0 END)) as NobSofr, "
            + "               SUM((CASE WHEN sn.t_SYSTEMCODE in ('"+OUTSYS_SYSTEMCODE_CFT_HOZ+"','"+OUTSYS_SYSTEMCODE_CFT_UNI+"','"+OUTSYS_SYSTEMCODE_CFT_ROZ+"') THEN sn.t_NOBAmount ELSE 0 END)) as NobCft "
            + "          from DNPTXSNOBREP_TMP sn "
            + "         where sn.T_TAXBASETYPE = 9 "
            + "         group by sn.t_ClientID "
            + "       ) q, dparty_dbt pt "
            + " where pt.t_PartyID = q.t_ClientID "
            + " order by q.SumNOBAmount DESC ";

    cmd = DL_RSDCommand(query);

    cnt = cmd.GetCount();
    if(cnt > 0)
      InitProgress(cnt, "СВОД НДФЛ в разрезе СИ", "СВОД НДФЛ в разрезе СИ");

      DataSet = cmd.Execute();

      while(DataSet.moveNext())

        var EstimTax13 = 0;
        var EstimTax15 = 0;
        var EstimTax18 = 0;
        var EstimTax20 = 0;
        var EstimTax22 = 0;
        var CalcTaxDiff13 = 0;
        var CalcTaxDif15 = 0;
        var CalcTaxDif18 = 0;
        var CalcTaxDif20 = 0;
        var CalcTaxDif22 = 0;

        EstimTax13 = money(round(min(DataSet.ResidentSumNOBAmount, REP_NPTX_13_MAX_VALUE) * 0.13, 0));
        if ((REP_NPTX_15_MAX_VALUE >= DataSet.ResidentSumNOBAmount) and (DataSet.ResidentSumNOBAmount > REP_NPTX_13_MAX_VALUE))
          EstimTax15 = money(round((DataSet.ResidentSumNOBAmount-REP_NPTX_13_MAX_VALUE) * 0.15, 0));
        elif (DataSet.ResidentSumNOBAmount > REP_NPTX_15_MAX_VALUE)
          EstimTax15 = money(round((REP_NPTX_15_MAX_VALUE-REP_NPTX_13_MAX_VALUE) * 0.15, 0));
        end;

        if ((REP_NPTX_18_MAX_VALUE >= DataSet.ResidentSumNOBAmount) and (DataSet.ResidentSumNOBAmount > REP_NPTX_15_MAX_VALUE))
          EstimTax18 = money(round((DataSet.ResidentSumNOBAmount-REP_NPTX_15_MAX_VALUE) * 0.18, 0));
        elif (DataSet.ResidentSumNOBAmount > REP_NPTX_18_MAX_VALUE)
          EstimTax18 = money(round((REP_NPTX_18_MAX_VALUE-REP_NPTX_15_MAX_VALUE) * 0.18, 0));
        end;

        if ((REP_NPTX_20_MAX_VALUE >= DataSet.ResidentSumNOBAmount) and (DataSet.ResidentSumNOBAmount > REP_NPTX_18_MAX_VALUE))
          EstimTax20 = money(round((DataSet.ResidentSumNOBAmount-REP_NPTX_18_MAX_VALUE) * 0.20, 0));
        elif (DataSet.ResidentSumNOBAmount > REP_NPTX_20_MAX_VALUE)
          EstimTax20 = money(round((REP_NPTX_20_MAX_VALUE-REP_NPTX_18_MAX_VALUE) * 0.20, 0));
          EstimTax22 = money(round((DataSet.ResidentSumNOBAmount - REP_NPTX_20_MAX_VALUE) * 0.22, 0));
        end;

        CalcTaxDiff13 = DataSet.SumNDFLCalculateAmount13 - EstimTax13;
        CalcTaxDif15  = DataSet.SumNDFLCalculateAmount15 - EstimTax15;
        CalcTaxDif18  = DataSet.SumNDFLCalculateAmount18 - EstimTax18;
        CalcTaxDif20  = DataSet.SumNDFLCalculateAmount20 - EstimTax20;
        CalcTaxDif22  = DataSet.SumNDFLCalculateAmount22 - EstimTax22;

        if((DataSet.SumNOBAmount != 0) or
           (CalcTaxDiff13 != 0) or
           (DataSet.SumNDFLCalculateAmount13 != 0) or
           (EstimTax13 != 0) or
           (DataSet.SumNDFLCalculateAmount15 != 0) or
           (EstimTax15 != 0) or
           (DataSet.SumNDFLCalculateAmount30 != 0)
          )

          PrintTableLine("N2_ClientID",        DataSet.ClientID,                               null,
                         "N2_ClientName",      DataSet.Name,                                   null,
                         "N2_SNOB",            DataSet.SumNOBAmount,                           null,
                         "N2_CalcTaxDiff13",   CalcTaxDiff13,                                  null,
                         "N2_CalcTax13",       DataSet.SumNDFLCalculateAmount13,               null,
                         "N2_EstimTax13",      EstimTax13,                                     null,
                         "N2_CalcTaxDif15",    CalcTaxDif15,                                   null,
                         "N2_CalcTax15",       DataSet.SumNDFLCalculateAmount15,               null,
                         "N2_EstimTax15",      EstimTax15,                                     null,
                         "N2_CalcTaxDif18",    CalcTaxDif18,                                   null,
                         "N2_CalcTax18",       DataSet.SumNDFLCalculateAmount18,               null,
                         "N2_EstimTax18",      EstimTax18,                                     null,
                         "N2_CalcTaxDif20",    CalcTaxDif20,                                   null,
                         "N2_CalcTax20",       DataSet.SumNDFLCalculateAmount20,               null,
                         "N2_EstimTax20",      EstimTax20,                                     null,
                         "N2_CalcTaxDif22",    CalcTaxDif22,                                   null,
                         "N2_CalcTax22",       DataSet.SumNDFLCalculateAmount22,               null,
                         "N2_EstimTax22",      EstimTax22,                                     null,
                         "N2_CalcTax30",       DataSet.SumNDFLCalculateAmount30,               null,
                         "N2_NobAsup",         DataSet.NobAsup,                                null,
                         "N2_NobDiasoft",      DataSet.NobDiasoft,                             null,
                         "N2_NobSofr",         DataSet.NobSofr,                                null,
                         "N2_NobCft",          DataSet.NobCft,                                 null
                        );
        end;

        UseProgress(i = i + 1);
      end;

      RemProgress();
    end; 

    EndTable();

    return cnt;

  END;

  PRIVATE MACRO PrintSheetTotal2025_2()
    var query, cmd, DataSet, cnt = 0;
    var i = 0;
    var SheetName = "СВОД ОБЩИЙ - СНОБ 2";

    SetActiveSheet(SheetName);

    SetActiveSheet(SheetName);
    
    RegisterTable( "N3_MainTable", NULL,
                   "N3_ClientID",
                   "N3_ClientName",
                   "N3_SNOB",
                   "N3_CalcTaxDiff13",
                   "N3_CalcTax13",
                   "N3_EstimTax13",
                   "N3_CalcTaxDif15",
                   "N3_CalcTax15",
                   "N3_EstimTax15",
                   "N3_CalcTax30",
                   "N3_NobDiasoft",
                   "N3_NobSofr",
                   "N3_NobCft"
                 );

    query =   "select q.*, pt.t_Name "
            + "  from (select sn.t_ClientID, "
            + "               SUM((CASE WHEN sn.t_TaxPayerStatus = 1 THEN sn.t_NOBAmount ELSE 0 END)) as ResidentSumNOBAmount, "
            + "               SUM((CASE WHEN sn.t_TaxPayerStatus <> 1 THEN sn.t_NOBAmount ELSE 0 END)) as NotResidentSumNOBAmount, "
            + "               SUM(sn.t_NOBAmount) as SumNOBAmount, "
            + "               SUM((CASE WHEN sn.t_NDFLTaxRate = 13 THEN sn.t_NDFLCalculateAmount ELSE 0 END)) as SumNDFLCalculateAmount13, "
            + "               SUM((CASE WHEN sn.t_NDFLTaxRate = 15 THEN sn.t_NDFLCalculateAmount ELSE 0 END)) as SumNDFLCalculateAmount15, "
            + "               SUM((CASE WHEN sn.t_NDFLTaxRate = 30 THEN sn.t_NDFLCalculateAmount ELSE 0 END)) as SumNDFLCalculateAmount30, "
            + "               SUM((CASE WHEN sn.t_SYSTEMCODE = '"+OUTSYS_SYSTEMCODE_DIASOFT+"' THEN sn.t_NOBAmount ELSE 0 END)) as NobDiasoft, "
            + "               SUM((CASE WHEN sn.t_SYSTEMCODE = '"+OUTSYS_SYSTEMCODE_SOFR+"' THEN sn.t_NOBAmount ELSE 0 END)) as NobSofr, "
            + "               SUM((CASE WHEN sn.t_SYSTEMCODE in ('"+OUTSYS_SYSTEMCODE_CFT_HOZ+"','"+OUTSYS_SYSTEMCODE_CFT_UNI+"','"+OUTSYS_SYSTEMCODE_CFT_ROZ+"') THEN sn.t_NOBAmount ELSE 0 END)) as NobCft "
            + "          from DNPTXSNOBREP_TMP sn "
            + "         where sn.T_TAXBASETYPE <> 9 "
            + "         group by sn.t_ClientID "
            + "       ) q, dparty_dbt pt "
            + " where pt.t_PartyID = q.t_ClientID "
            + " order by q.SumNOBAmount DESC ";

    cmd = DL_RSDCommand(query);

    cnt = cmd.GetCount();
    if(cnt > 0)
      InitProgress(cnt, "СВОД НДФЛ в разрезе СИ", "СВОД НДФЛ в разрезе СИ");

      DataSet = cmd.Execute();

      while(DataSet.moveNext())

        var EstimTax13 = 0;
        var EstimTax15 = 0;
        var CalcTaxDiff13 = 0;
        var CalcTaxDif15 = 0;

        EstimTax13 = money(round(min(DataSet.ResidentSumNOBAmount, REP_NPTX_13_MAX_VALUE) * 0.13, 0));
        EstimTax15 = money(round(max(DataSet.ResidentSumNOBAmount - REP_NPTX_13_MAX_VALUE, 0) * 0.15, 0));

        CalcTaxDiff13 = DataSet.SumNDFLCalculateAmount13 - EstimTax13;
        CalcTaxDif15  = DataSet.SumNDFLCalculateAmount15 - EstimTax15;

        if((DataSet.SumNOBAmount != 0) or
           (CalcTaxDiff13 != 0) or
           (DataSet.SumNDFLCalculateAmount13 != 0) or
           (EstimTax13 != 0) or
           (DataSet.SumNDFLCalculateAmount15 != 0) or
           (EstimTax15 != 0) or
           (DataSet.SumNDFLCalculateAmount30 != 0)
          )

          PrintTableLine("N2_ClientID",        DataSet.ClientID,                               null,
                         "N2_ClientName",      DataSet.Name,                                   null,
                         "N2_SNOB",            DataSet.SumNOBAmount,                           null,
                         "N2_CalcTaxDiff13",   CalcTaxDiff13,                                  null,
                         "N2_CalcTax13",       DataSet.SumNDFLCalculateAmount13,               null,
                         "N2_EstimTax13",      EstimTax13,                                     null,
                         "N2_CalcTaxDif15",    CalcTaxDif15,                                   null,
                         "N2_CalcTax15",       DataSet.SumNDFLCalculateAmount15,               null,
                         "N2_EstimTax15",      EstimTax15,                                     null,
                         "N2_CalcTax30",       DataSet.SumNDFLCalculateAmount30,               null,
                         "N2_NobDiasoft",      DataSet.NobDiasoft,                             null,
                         "N2_NobSofr",         DataSet.NobSofr,                                null,
                         "N2_NobCft",          DataSet.NobCft,                                 null
                        );
        end;

        UseProgress(i = i + 1);
      end;

      RemProgress();
    end; 

    EndTable();

    return cnt;

  END;

  PRIVATE MACRO PrintSheetTotal()
    var query, cmd, DataSet, cnt = 0;
    var i = 0;
    var SheetName = "СВОД ОБЩИЙ";

    SetActiveSheet(SheetName);

    SetActiveSheet(SheetName);
    
    RegisterTable( "N2_MainTable", NULL,
                   "N2_ClientID",
                   "N2_ClientName",
                   "N2_SNOB",
                   "N2_CalcTaxDiff13",
                   "N2_CalcTax13",
                   "N2_EstimTax13",
                   "N2_CalcTaxDif15",
                   "N2_CalcTax15",
                   "N2_EstimTax15",
                   "N2_CalcTax30",
                   "N2_NobAsup",
                   "N2_NobDiasoft",
                   "N2_NobSofr",
                   "N2_NobCft"
                 );

    query =   "select q.*, pt.t_Name "
            + "  from (select sn.t_ClientID, "
            + "               SUM((CASE WHEN sn.t_TaxPayerStatus = 1 THEN sn.t_NOBAmount ELSE 0 END)) as ResidentSumNOBAmount, "
            + "               SUM((CASE WHEN sn.t_TaxPayerStatus <> 1 THEN sn.t_NOBAmount ELSE 0 END)) as NotResidentSumNOBAmount, "
            + "               SUM(sn.t_NOBAmount) as SumNOBAmount, "
            + "               SUM((CASE WHEN sn.t_NDFLTaxRate = 13 THEN sn.t_NDFLCalculateAmount ELSE 0 END)) as SumNDFLCalculateAmount13, "
            + "               SUM((CASE WHEN sn.t_NDFLTaxRate = 15 THEN sn.t_NDFLCalculateAmount ELSE 0 END)) as SumNDFLCalculateAmount15, "
            + "               SUM((CASE WHEN sn.t_NDFLTaxRate = 30 THEN sn.t_NDFLCalculateAmount ELSE 0 END)) as SumNDFLCalculateAmount30, "
            + "               SUM((CASE WHEN sn.t_SYSTEMCODE = '"+OUTSYS_SYSTEMCODE_ASUP+"' THEN sn.t_NOBAmount ELSE 0 END)) as NobAsup, "
            + "               SUM((CASE WHEN sn.t_SYSTEMCODE = '"+OUTSYS_SYSTEMCODE_DIASOFT+"' THEN sn.t_NOBAmount ELSE 0 END)) as NobDiasoft, "
            + "               SUM((CASE WHEN sn.t_SYSTEMCODE = '"+OUTSYS_SYSTEMCODE_SOFR+"' THEN sn.t_NOBAmount ELSE 0 END)) as NobSofr, "
            + "               SUM((CASE WHEN sn.t_SYSTEMCODE in ('"+OUTSYS_SYSTEMCODE_CFT_HOZ+"','"+OUTSYS_SYSTEMCODE_CFT_UNI+"','"+OUTSYS_SYSTEMCODE_CFT_ROZ+"') THEN sn.t_NOBAmount ELSE 0 END)) as NobCft "
            + "          from DNPTXSNOBREP_TMP sn "
            + "         group by sn.t_ClientID "
            + "       ) q, dparty_dbt pt "
            + " where pt.t_PartyID = q.t_ClientID "
            + " order by q.SumNOBAmount DESC ";

    cmd = DL_RSDCommand(query);

    cnt = cmd.GetCount();
    if(cnt > 0)
      InitProgress(cnt, "СВОД НДФЛ в разрезе СИ", "СВОД НДФЛ в разрезе СИ");

      DataSet = cmd.Execute();

      while(DataSet.moveNext())

        var EstimTax13 = 0;
        var EstimTax15 = 0;
        var CalcTaxDiff13 = 0;
        var CalcTaxDif15 = 0;

        EstimTax13 = money(round(min(DataSet.ResidentSumNOBAmount, NPTX_LIMINCOME_MAX15) * 0.13, 0));
        EstimTax15 = money(round(max(DataSet.ResidentSumNOBAmount - NPTX_LIMINCOME_MAX15, 0) * 0.15, 0));

        CalcTaxDiff13 = DataSet.SumNDFLCalculateAmount13 - EstimTax13;
        CalcTaxDif15  = DataSet.SumNDFLCalculateAmount15 - EstimTax15;

        if((DataSet.SumNOBAmount != 0) or
           (CalcTaxDiff13 != 0) or
           (DataSet.SumNDFLCalculateAmount13 != 0) or
           (EstimTax13 != 0) or
           (DataSet.SumNDFLCalculateAmount15 != 0) or
           (EstimTax15 != 0) or
           (DataSet.SumNDFLCalculateAmount30 != 0)
          )

          PrintTableLine("N2_ClientID",        DataSet.ClientID,                               null,
                         "N2_ClientName",      DataSet.Name,                                   null,
                         "N2_SNOB",            DataSet.SumNOBAmount,                           null,
                         "N2_CalcTaxDiff13",   CalcTaxDiff13,                                  null,
                         "N2_CalcTax13",       DataSet.SumNDFLCalculateAmount13,               null,
                         "N2_EstimTax13",      EstimTax13,                                     null,
                         "N2_CalcTaxDif15",    CalcTaxDif15,                                   null,
                         "N2_CalcTax15",       DataSet.SumNDFLCalculateAmount15,               null,
                         "N2_EstimTax15",      EstimTax15,                                     null,
                         "N2_CalcTax30",       DataSet.SumNDFLCalculateAmount30,               null,
                         "N2_NobAsup",         DataSet.NobAsup,                                null,
                         "N2_NobDiasoft",      DataSet.NobDiasoft,                             null,
                         "N2_NobSofr",         DataSet.NobSofr,                                null,
                         "N2_NobCft",          DataSet.NobCft,                                 null
                        );
        end;

        UseProgress(i = i + 1);
      end;

      RemProgress();
    end; 

    EndTable();

    return cnt;

  END;

  PRIVATE MACRO Create()

    CreateRepData();

    PrintSheetBySS();

    if (m_TaxPeriod >= 2025)
      PrintSheetTotal2025_1();
      PrintSheetTotal2025_2();
    else
      PrintSheetTotal();
    end;

    DL_RSDCommand("TRUNCATE TABLE DNPTXSNOBREP_TMP").ExecuteCMD();
    
  END;

  MACRO Run()
    Run();
    
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  if (m_TaxPeriod >= 2025)
    initDL_CReportTemplate( NULL, "Report_nptxsnobrep_2025.xlsx", true, null, null, true ); 
  else
    initDL_CReportTemplate( NULL, "Report_nptxsnobrep.xlsx", true, null, null, true ); 
  end;
END;



PRIVATE MACRO ReportRun()
  var Form = Sp_NPTXSNOBREP_Panel();
  
  while(Form.Run())
    var RepObj = NPTXSNOBREP_Report(Form.m_ClientID, Form.m_TaxPeriod);
          
    var FullFileNameXLSX = RepObj.Run();

    RepObj = NULL;

    if(not isStandAlone())
      FullFileNameXLSX = "$"+FullFileNameXLSX;
    end;
    
    if(FullFileNameXLSX != "")
      var day, mon, year;
      var hour, min, sec;
      var FileName, FileExt;
      
      DateSplit(Form.m_RepDate, day, mon, year);
      TimeSplit(time(), hour, min, sec);
      
      var FromDir = SplitFile(FullFileNameXLSX, FileName, FileExt);
      
      var NewFileNameXLSX = "Отчет по СНОБ и удержанному НДФЛ на "+string(day:o:2)+"."+string(mon:o:2)+"."+string(year)+" "+string(hour:o:2)+"-"+string(min:o:2)+"-"+string(sec:o:2) + ".xlsx";

      if(not RenameFile(FullFileNameXLSX, FromDir+NewFileNameXLSX))
        msgbox("Ошибка при переименовании файла отчета");
      else
        SC_ShowFile(FromDir, NewFileNameXLSX);
      end;
    end;

  end;
END;

ReportRun();
exit(1);