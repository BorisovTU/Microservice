/**
 @file 		mac\dlng\secur\SumConfirmExp_Print.mac
 @brief 	Отчет "Суммы подтвержденных расходов"

 # tag
 - functional_block: Отчетность_внутренняя
 - code_type: Report
 - code_type:GUI

 #menu 
  - БО ЦБ\ Внутренний учет \ Отчеты \ Поручения и операции клиентов \ Суммы подтвержденных расходов

 # changelog
 |date       |autor          |tasks                                           |note
 |-----------|---------------|------------------------------------------------|-------------------------------------------------------------
 |13.11.2025 |Велигжанин А.В.|DEF-110778                                      |Поправки для обработки списка заданий
 |10.11.2025 |Велигжанин А.В.|DEF-109328                                      |Поправка для не заполненной repDate
 |07.11.2025 |Велигжанин А.В.|DEF-109328                                      |repDate извлекается из dscsumconfexp_dbt
 |02.05.2025 |Велигжанин А.В.|DEF-88733                                       |Добавлен параметр p_FuncObjFlag (чтобы сохранить возможность
 |           |               |                                                |работы отчета в прежнем варианте, если p_FuncObjFlag == false)

*/
import "DLReport_h.mac", "dlreport.mac", "dlQuery.mac", "SumConfirmExp_Form.mac", "dldlngfun.mac", "scsrvrepfun.mac";
import "CbLogger2.mac";

var logger = LoggerFactory().NewItLog("SumConfirmExp_Print.mac").WithElapsedTime().GetLogger();

PRIVATE CONST POI_MODE = TRUE;
PRIVATE CONST POI_SIZE_PACK = 100;
PRIVATE CONST CNT_SHOW_FILE = 10; //Максимальное количество файлов, при котором возможно открытие их на экран
PRIVATE CONST OBJECT_TYPE_FUNCOBJ_PRINT =  4601;  // OBJECTTYPE

private var ItogSym = "";

CLASS (DL_CReportTemplate) CSumConfirmExp_Report(p_RepDate, p_LaunchMode, p_ClientID, p_DlContrID, p_FIID, p_GUID, p_PackID, p_FuncObjFlag)
  PRIVATE VAR m_cntFiles = 0;

  PRIVATE VAR m_RepDate    	= p_RepDate;   
  PRIVATE VAR m_LaunchMode 	= p_LaunchMode;
  PRIVATE VAR m_ClientID   	= p_ClientID;   //
  PRIVATE VAR m_DlContrID  	= p_DlContrID;  // 
  PRIVATE VAR m_FIID       	= p_FIID;       //
  PRIVATE VAR m_GUID       	= p_GUID;     
  PRIVATE VAR m_PackID     	= p_PackID;
  PRIVATE VAR m_FuncObjFlag	= p_FuncObjFlag;

  PRIVATE MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  macro BeginReport()
    SetIsShowAppl(false);

    CreateTotalBook();
  end;

  private macro PrintItogLines(ClientID, DlContrID, FIID, FI_Name, SumPrecision)
    var query, cmd, DataSet;
    var color = RGB(208,206,206);

    query =   "select NVL(SUM(scs.t_Amount), 0) as SumAmount, "
            + "       NVL(SUM(scs.t_DealSum_PayFI), 0) as SumDealSum_PayFI, "
            + "       NVL(SUM(scs.t_NKD_PayFI), 0) as SumNKD_PayFI, "
            + "       NVL(SUM(scs.t_DealSum_Nat), 0) as SumDealSum_Nat, "
            + "       RSB_SECUR.GetDealFICcy(scs.t_CFI) as CFI_CCY "
            + "  from dscsumconfexp_dbt scs "
            + " where scs.t_GUID = ? "
            + "   and scs.t_ClientID = ? "
            + "   and scs.t_DlContrID = ? "
            + "   and scs.t_FIID = ? "
            + " group by scs.t_GUID, scs.t_ClientID, scs.t_DlContrID, scs.t_FIID, scs.t_CFI ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_GUID);
    cmd.AddParam(ClientID);
    cmd.AddParam(DlContrID);
    cmd.AddParam(FIID);

    DataSet = cmd.Execute();

    while(DataSet.moveNext())
      SetCurrentLineFont( NULL, true, NULL, null, NULL, color );        

      PrintTableLine("N1_Issuer",         "итого",                  null,
                     "N1_FI_Name",        FI_Name,                  null,
                     "N1_ISIN",           null,                     null,
                     "N1_DealDate",       null,                     null,
                     "N1_DealDate_Rate",  null,                     null,
                     "N1_SettlDate",      null,                     null,
                     "N1_SettlDate_Rate", null,                     null,   
                     "N1_Amount",         double(DataSet.SumAmount), SetPrecisionByFractPart(double(DataSet.SumAmount), SumPrecision, 0),
                     "N1_CFI",            DataSet.CFI_CCY,          null,
                     "N1_Price",          null,                     null,
                     "N1_FaceValue",      null,                     null,
                     "N1_DealSum",        DataSet.SumDealSum_PayFI, null,
                     "N1_NKD",            DataSet.SumNKD_PayFI,     null,
                     "N1_DealSum_Nat",    DataSet.SumDealSum_Nat,   null,
                     "N1_SumComiss",      null,                     null,
                     "N1_OutDate",        null,                     null
                    );
    end;

  end;

  macro Create()
    var sql, exec;
    var query, cmd, DataSet;
    var prevClientID = 0, prevDlContrID = 0, prevFIID = -1, prevFI_Name = "", prevSumPrecision = 0;
    var SumAmount = $0;
    var cnt = 0, i = 0;
    var newSheet = true;
    var mainSheetName = "Справка";
    var currSheetName = "";
    var PoiNum = 0;
    var Sym= "";      
    var Sym_= "";      
    var OldSym= ""; 
    var isFirst = true;
    var FV_Point = 2;

    query =   
        "SELECT scs.t_guid, scs.t_clientid, scs.t_dlcontrid, scs.t_fiid, scs.t_sort, scs.t_dealid "
            + "\n, scs.t_dealdate, scs.t_dealdate_rate, scs.t_settldate, scs.t_settldate_rate, scs.t_amount "
            + "\n, scs.t_cfi, scs.t_price, scs.t_facevalue, scs.t_dealsum_payfi, scs.t_nkd_payfi, scs.t_dealsum_nat "
            + "\n, scs.t_sumcomiss, scs.t_outdate, scs.t_sysdate, scs.t_repdate "
            + "\n, sf.t_ID, sf.t_Number, sf.t_DateConc, pt.t_Name as ClientName "
            + "\n, issuer.t_ShortName as IssuerName "
            + "\n, fin.t_Name as Fi_Name, fin.t_SumPrecision, fin.t_Point "
            + "\n, avr.t_ISIN, avr.t_LSIN "
            + "\n, RSB_SECUR.GetDealFICcy(scs.t_CFI) as CFI_CCY "
            + "\nFROM dscsumconfexp_dbt scs, ddlcontr_dbt dlcontr, dsfcontr_dbt sf, dparty_dbt pt"
            + "\n, dfininstr_dbt fin, davoiriss_dbt avr, dparty_dbt issuer "
    ;

    if(m_PackID > 0)
      query = query + "\n, dmasexec_dbt masexec ";
    end;   

    query = query + "\n  where scs.t_GUID = ? "
                  + "\n    and dlcontr.t_DlCOntrID = scs.t_DlContrID "
                  + "\n    and sf.t_ID = dlcontr.t_SfContrID "
                  + "\n    and pt.t_PartyID = scs.t_ClientID "
                  + "\n    and fin.t_FIID = scs.t_FIID "
                  + "\n    and avr.t_FIID = fin.t_FIID "
                  + "\n    and issuer.t_PartyID = fin.t_Issuer ";
            
    if(m_PackID > 0)
      query = query + "\n  and dlcontr.t_DlContrID = masexec.t_ID "
                    + "\n  and masexec.t_guid = ? "
                    + "\n  and masexec.t_packnum = ? ";
    end;   

    // Для отладки
    // query = query + "  AND ROWNUM <= 100000 ";

    cmd = DL_RSDCommand(query);
    cmd.AddParam(m_GUID);

    if(m_PackID > 0)
      cmd.AddParam(m_GUID);    
      cmd.AddParam(m_PackID);    
    end;

    cnt = cmd.GetCount();
    if(cnt > 0)
      InitProgress(cnt, "Идет печать отчета", "Печать отчета");

      query = query + "\n order by scs.t_GUID, pt.t_Name ASC, scs.t_DlContrID, scs.t_FIID, scs.t_OutDate DESC, scs.t_Sort ASC ";

      logger.ErrorClob("Create(), m_GUID: " + string(m_GUID) + ", m_PackID: " + string(m_PackID), query);

      DataSet = cmd.Execute(query);
      var stat = DataSet.moveNext();
      while(stat)
        if(newSheet == true)
          currSheetName = substr(DataSet.ClientName, 1, 20) + " " + StrSubst(DataSet.Number, "/", "_");
          
          if(POI_MODE)
            if(isFirst)
              Sym = Substr(currSheetName,1,3);
            end;
           
            Sym_ = Substr(currSheetName,1,3);
            ItogSym = Sym + "-" + Sym_;
            if ( PoiNum == POI_SIZE_PACK )
              SaveSubTotalBook(ItogSym,FALSE);
              CreateTotalBook();
              PoiNum = 1;
              isFirst = true;
              m_cntFiles = m_cntFiles + 1;
            else
              PoiNum = PoiNum + 1;
              isFirst = false;
            end;
            OldSym =Sym;
          end;

          SetActiveSheet(mainSheetName);
          UseNewSheetInTotalBook();

          PrintFormatString(NULL, "N1_ClientContrInfo", "о суммах фактически произведенных и документально подтвержденных расходов по ценным бумагам по соглашению №" + DataSet.Number + 
                                                        " от " + string(date(DataSet.DateConc):f) +
                                                        " " + DataSet.ClientName +
                                                        " на дату " + IIF(string(DataSet.t_RepDate) == "", "00.00.0000", string(date(DataSet.t_RepDate):f)) + "г."
          );
          CopyAllSheetInTotalBook(mainSheetName, false, "N1_Header", null, currSheetName);

          CopyAllSheetInTotalBook(mainSheetName, false, "N1_MainTableHeader", null, currSheetName);

          RegisterTable("N1_MainTable", NULL,
                        "N1_Issuer",    
                        "N1_FI_Name",   
                        "N1_ISIN",  
                        "N1_DealDate",  
                        "N1_DealDate_Rate", 
                        "N1_SettlDate", 
                        "N1_SettlDate_Rate",    
                        "N1_Amount",    
                        "N1_CFI",   
                        "N1_Price", 
                        "N1_FaceValue", 
                        "N1_DealSum",   
                        "N1_NKD",   
                        "N1_DealSum_Nat",   
                        "N1_SumComiss", 
                        "N1_OutDate");

          newSheet = false;
        end;

        if(prevFIID != DataSet.FIID)
          FV_Point = MAX(DL_MeanSignAfterPoint(double(DataSet.FaceValue), 15), DataSet.Point+2);

          if(FV_Point == 4)
            FV_Point = 5; //!!!Почему-то POI не выводит точность 4
          end;
        end;

        PrintTableLine("N1_Issuer",         DataSet.IssuerName,                                  null,
                       "N1_FI_Name",        DataSet.FI_Name,                                     null,
                       "N1_ISIN",           IIF(DataSet.ISIN == "", DataSet.LSIN, DataSet.ISIN), null,
                       "N1_DealDate:f",     date(DataSet.DealDate),                              null,
                       "N1_DealDate_Rate",  DataSet.DealDate_Rate,                               null,
                       "N1_SettlDate:f",    date(DataSet.SettlDate),                             null,
                       "N1_SettlDate_Rate", DataSet.SettlDate_Rate,                              null,   
                       "N1_Amount",         double(DataSet.Amount),                              SetPrecisionByFractPart(double(DataSet.Amount), DataSet.SumPrecision, 0),
                       "N1_CFI",            DataSet.CFI_CCY,                                     null,
                       "N1_Price",          DataSet.Price,                                       null,
                       "N1_FaceValue",      double(DataSet.FaceValue),                           FV_Point,
                       "N1_DealSum",        DataSet.DealSum_PayFI,                               null,
                       "N1_NKD",            DataSet.NKD_PayFI,                                   null,
                       "N1_DealSum_Nat",    DataSet.DealSum_Nat,                                 null,
                       "N1_SumComiss",      DataSet.SumComiss,                                   null,
                       "N1_OutDate",        date(DataSet.OutDate),                               null
                      );

        prevClientID     = DataSet.ClientID;
        prevDlContrID    = DataSet.DlContrID;
        prevFIID         = DataSet.FIID;
        prevFI_Name      = DataSet.FI_Name;
        prevSumPrecision = DataSet.SumPrecision;

        stat = DataSet.moveNext();
        if((stat == 0) or (prevDlContrID != DataSet.DlContrID) or (prevFIID != DataSet.FIID))
          PrintItogLines(prevClientID, prevDlContrID, prevFIID, prevFI_Name, prevSumPrecision);
        end;

        if((stat == 0) or (prevDlContrID != DataSet.DlContrID))
          //Данные закончились или сменился договор
          EndTable();
          CopyAllSheetInTotalBook(mainSheetName, false, GetTableRange(), null, currSheetName, true);
        end;

        if(prevDlContrID != DataSet.DlContrID)
          newSheet = true;
        end;

        UseProgress(i = i + 1);
      end;

      RemProgress();
    else
      PrintFormatString(NULL, "N1_NoDataMsg", "Нет данных, удовлетворяющих параметрам отчета.");
      CopyAllSheetInTotalBook(mainSheetName, false, "N1_NoDataMsg", null, mainSheetName);
    end;

  end;

  macro EndReport()
    var cmd;
    
    cmd = DL_RSDCommand("DELETE FROM dscsumconfexp_dbt WHERE t_GUID = ?");
    cmd.AddParam(m_GUID);
    cmd.ExecuteCMD(); // удалить при отладке 

    if((POI_MODE) AND (ItogSym!=""))
      SetLastTotalBookName(ItogSym);
      ItogSym = "";
    end;
    SaveTotalBook();
  end;

  macro CReport_Show0( NumCopy:INTEGER )
    CReport_Show(NumCopy);

    if(m_cntFiles > CNT_SHOW_FILE)
      msgbox("Сформировано "+m_cntFiles+" файлов отчета");
    else
      //отображение в отдельных приложениях сформированных файлов
      //для этого ещё было выключено отображение в конце формирования отчёта
      //SetIsShowAppl(false);
      //сделано это т.к. у нас при формировании файлы бьются на части
      //и во время формирования банку не нравилось, что файлы появлялись постепенно
      //да ещё и моргали
      for (var fileName, GetFullReportFileNames())
        //CDAOMSExcel(fileName,true).Show();
        var _fileName, fileExt;
        var filePath = IIF(not IsStandAlone(), "$","") + SplitFile(fileName, _fileName, fileExt);
        SC_ShowFile(filePath, _fileName+fileExt, true);
      end;
    end;
  end;

  macro CReport_ShowFo( NumCopy:INTEGER )
    var cmd;
    CReport_Show(NumCopy); // ??

    if(m_cntFiles > CNT_SHOW_FILE)
      logger.Debug("Сформировано "+m_cntFiles+" файлов отчета");
    else
      for (var fileName, GetFullReportFileNames())
        var FileID = 0;
        logger.Debug("Вставка файла отчета "+fileName);
        if(CB_InsertPrintFuncObjFiles(m_GUID, m_PackID, fileName, FileID ) != 0)
          logger.Debug("Ошибка вставки файла "+fileName);
        end;

        /* Если переделаем на шару   
        var _fileName, fileExt;
        var FromDir = SplitFile(fileName, _fileName, fileExt);
        var ToDir   = "..\\TXTFILE"; // Реализовать Настройку
        if(SC_CopyFile(FromDir, ToDir, _fileName+fileExt) != 0)
          msgbox("Ошибка при копировании файла отчета на терминал или СП");
        end;
        */
      end;
    end; 
  end;

  macro CReport_Show( NumCopy:INTEGER )
    if(m_FuncObjFlag == false)
      // если флаг печати через FuncObj не установлен, работает, как раньше
      CReport_Show0(NumCopy);
    else
      // если флаг печати через FuncObj установлен, работает через FuncObj
      CReport_ShowFo(NumCopy);
    end; 
  end;

  initDL_CReportTemplate(NULL, "Report_SumConfirmExp.xls", true, false, null, POI_MODE);
END;



