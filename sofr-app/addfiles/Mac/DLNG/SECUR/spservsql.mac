/*
$Name:         spservsql.mac
$Module:       Ценные бумаги
$Description:  ОБЩИЕ SQL-ПРОЦЕДУРЫ
*/
IMPORT BankInter, rsd, globals, payminter, fiinter, "secinter.mac";
import RsbDataSet;

/*класс для выполнения sql-команд и получения recordset */
CLASS SP_ExecSQL()

   private var cmd, rs, query;
 
   macro execute( _query:string )
      query  = _query;
      cmd    = RsdCommand( RslDefCon, query );
      cmd.execute();
   end;

   macro get_recordset()
      if( cmd == null )
         this.execute();
      end;
      rs = RsdRecordset( cmd );  
   end;

   /*предыдущая запись в recordset*/
   macro prev()
      if( rs == null ) this.get_recordset(); end;
      return rs.movePrev();        
   end;

   /*следующая запись в recordset*/
   macro next()
      if( rs == null ) this.get_recordset(); end;
      return rs.moveNext();        
   end;

   /*первая запись в recordset*/
   macro first()
      if( rs == null ) this.get_recordset(); end;
      return rs.moveFirst();        
   end;

   /*последняя запись в recordset*/
   macro last()
      if( rs == null ) this.get_recordset(); end;
      return rs.moveLast();        
   end;

   /*кол-во записей в recordset*/
   macro getRecCount()
      if( rs == null ) this.get_recordset(); end;
      return rs.getRecCount();        
   end;

   macro get_value( num:integer )
      return rs.value( num );
   end;

   macro to_date( _date:date )            
      if( _date != Date(0,0,0) )
         return "TO_DATE( '" + string(_date) + "', 'DD.MM.YYYY' )"; 
      else
         return "TO_DATE( '" + "1.1.1" + "', 'DD.MM.YYYY' )"; 
      end;
   end;
END;


PRIVATE MACRO ПолучитьКоличествоГолосующихАкцийЭмитента( Issuer, Ondate )

   var sql, QueryStr = "", Amount = $0;

QueryStr = RSDCommand("SELECT NVL(sum(t_Amount),0) as Amount " +  
           "FROM dpmwrtsum_dbt wsum,dfininstr_dbt fin,davoiriss_dbt av " +                                              
           "WHERE wsum.t_FIID = fin.t_FIID AND wsum.t_FIID = av.t_FIID AND wsum.t_Buy_Sale = 0 AND wsum.t_State = " + string(PM_WRTSUM_FORM) +
           " AND wsum.t_Date <= ? " + 
           " AND fin.t_Issuer = ? AND ( " +
              "          fin.t_AvoirKind = " + string(AVOIRISSKIND_EQUITY_SHARE) +       " OR " +
              "          fin.t_AvoirKind = " + string(AVOIRISSKIND_PREFERENCE_SHARE) +   " OR " +
              "          fin.t_AvoirKind = " + string(AVOIRISSKIND_PREFERENCE_CONV_SHARE) + " ) "
              " AND av.t_IsVoting = 'X'");

   QueryStr.addParam( "", RSDBP_IN, OnDate );
   QueryStr.addParam( "", RSDBP_IN, Issuer );
   QueryStr.execute();

   sql = TRsbDataSet(QueryStr);

   if(sql.MoveNext() AND (ValType(sql.Amount) != V_UNDEF))
      return sql.Amount;
   end;

   return $0;
END;

PRIVATE MACRO ПолучитьВесьОбъемГолосующихАкцийЭмитента( Issuer, Ondate )
   var sql, QueryStr = "", Amount = 0;

  sql = SP_ExecSQL();  
  QueryStr = "SELECT NVL(sum(RSB_FIInstr.FI_GetQTYOnDate( fin.t_FIID, " + sql.to_date(OnDate) + " )),0) " +
           "FROM dfininstr_dbt fin, davoiriss_dbt av " +                                              
           "WHERE fin.t_FIID = av.t_FIID AND fin.t_Issuer = " + string( Issuer ) + " AND fin.t_Issued <= " + sql.to_date(OnDate) + " AND ( " +
              "          fin.t_AvoirKind = " + string(AVOIRISSKIND_EQUITY_SHARE) +       " OR " +
              "          fin.t_AvoirKind = " + string(AVOIRISSKIND_PREFERENCE_SHARE) +   " OR " +
              "          fin.t_AvoirKind = " + string(AVOIRISSKIND_PREFERENCE_CONV_SHARE) + " ) "
              " AND av.t_IsVoting = 'X';";                 
  sql.execute( QueryStr );

  if( sql.next() and (sql.get_value(0) != null) )
    Amount = sql.get_value(0) * 100; /*так как объем выпуска хранится со сдвигом влево на два знака */
  end;
  return Amount;

END;

MACRO ВыполнитьПроверкиДляГолосующихАкций( FD )

   var   AmountAll = $0,  Percent = 0;
                                                
   if( FI_IsShare( FD.fininstr, false ) AND FD.avoiriss.rec.IsVoting ) 

      /*покупка не в ПКУ или выбытие из ПКУ*/
      if( ((FD.BuySale == DEAL_TYPE_BUY) AND (FD.KindPortf != KINDPORT_CONTR)) OR
          ((FD.BuySale != DEAL_TYPE_BUY) AND (FD.KindPortf == KINDPORT_CONTR))
      ) 
                                                                                             
         AmountAll   =  ПолучитьВесьОбъемГолосующихАкцийЭмитента( FD.fininstr.rec.Issuer, FD.tick.rec.DealDate );

         if( AmountAll > 0 )
            VAR Amount =  ПолучитьКоличествоГолосующихАкцийЭмитента( FD.fininstr.rec.Issuer, FD.tick.rec.DealDate );

            if( FD.BuySale == DEAL_TYPE_BUY )
               Percent = ((Amount + FD.dl_leg.rec.Principal) / AmountAll) * 100.;     
               if( Percent > 20 ) /*стало более 20% от общего объема выпуска акций эмитента*/
                  if( GetTrue( true, "Ценные бумаги выпуска должны быть зачислены в портфель контрольного участия. Продолжить?" ) == false )  
                     return false;
                  end;
               end;
            else            
               Percent = ((Amount - FD.dl_leg.rec.Principal) / AmountAll) * 100.;     
               if( (Percent < 20) AND ((Amount - FD.dl_leg.rec.Principal) > 0) )
                  if( GetTrue( true, "Ценные бумаги выпуска должны быть списаны из портфеля контрольного участия. Продолжить?" ) == false )  
                     return false;
                  end;
               end;             
            end;         
         end;
      end;
   end; 
   return true;
END;
