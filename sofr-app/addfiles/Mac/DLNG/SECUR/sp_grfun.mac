/*
$Name:        sp_grfun.mac
$Module:      Ценные бумаги
$Description: Функции работы с графиками
*/

import BankInter, Календарь, CTInter;
import "dlquery.mac", "dltransf.mac", "dlcnst.inc", "dldlngconst.mac", "spserv.mac", "sp_class.mac", "dl_grfun.mac", "nutxfun.mac";


var GrDateArray = TArray( false, 100, 100 ); // !!! Используется  в неттинге
private var ArrGrDeal = DL_GrDealData();
/*****************/
private var ArrRepozDeal = RepozDealData();
/*****************/
private var TemplDateArr = TArray( false, 100, 100 ); //кеш для видов дат по шаблонам

// ID периода "+-Форвард, дрейф", если открывается в текущий момент
private var PeriodIDTrGl:integer = 0;
private var PeriodIDObGl:integer = 0;

macro SetPeriodIDTrGl(period)
  PeriodIDTrGl = period;
end;
macro SetPeriodIDObGl(period)
  PeriodIDObGl = period;
end;

private macro GetLastDayInMonth( CurDate:Date )
    var day, mon, year;

    DateSplit(CurDate,day,mon,year);
    if( mon == 12 )
      return Date(1,1,year+1) - 1;
    else
      return Date(1,mon+1,year) - 1;
    end;
end;

private macro GetLastWorkDayInMonth( CurDate:Date )
   var LastWorkDate;

   LastWorkDate = GetLastDayInMonth(CurDate);
   if( not IsWorkDay(LastWorkDate))
     LastWorkDate = GetDateAfterWorkDays(LastWorkDate,-1);
   end;
   return LastWorkDate;
end;

macro GetDateTemplRevFr(ДатаИсполненияШага, FD)
  var messageSendingMode = 0;

  GetRegistryValue("РЕПОЗИТАРИЙ\\ОТПРАВКА СООБЩЕНИЯ СМ094", V_INTEGER, messageSendingMode, null);
  if(messageSendingMode == 1)//1-при каждой переоценке
    return ДатаИсполненияШага;
  elif(messageSendingMode == 3)//3-по окончании месяца по последней переоценке
    var closeDate = date(31,12,9999);
    var query = " SELECT t_planDate " +
                "   FROM ddlgrdeal_dbt " +
                "  WHERE     t_DocKind = ? " +
                "        AND t_DocID = ? " +
                "        AND t_TemplNum IN (51, 52) ";
    var cmd = DL_RSDCommand(query);
    cmd.AddParam(FD.tick.rec.BofficeKind);
    cmd.AddParam(FD.tick.rec.DealID);
    var DataSet = cmd.Execute();
    if( DataSet.MoveNext() )
      closeDate = DataSet.planDate;
    end;

    if((FD.tick.rec.DealStatus == DL_CLOSED) OR (closeDate <= ДатаИсполненияШага))
      return date(0,0,0);
    end;

    if(ДатаИсполненияШага == GetLastWorkDayInMonth(ДатаИсполненияШага))
      return GetLastDayInMonth(ДатаИсполненияШага);
    elif((ДатаИсполненияШага == GetLastDayInMonth(ДатаИсполненияШага)) and not IsWorkDay(ДатаИсполненияШага) and IsWorkDay(ДатаИсполненияШага - 1) )
      return GetLastDayInMonth(ДатаИсполненияШага);
    elif((ДатаИсполненияШага != GetLastDayInMonth(ДатаИсполненияШага)) and (ДатаИсполненияШага == (GetLastWorkDayInMonth(ДатаИсполненияШага) + 1)) and not IsWorkDay(ДатаИсполненияШага) )
      return GetLastDayInMonth(ДатаИсполненияШага);
    else 
      return date(0,0,0);
    end;
  else//2-по окончании месяца
    return GetLastDayInMonth(ДатаИсполненияШага);
  end;
  
  return date(0,0,0);
end;

//Функция поиска числа запланированных строк переноса по срокам в графике
macro GetPlanTransferDate(DocKind, DocID)
  var DataSet, PlanDate = date(0,0,0);
  var cmd = DL_RSDCommand(  " select gd.t_PlanDate "
                          + "   from ddlgrdeal_dbt gd, ddlgracc_dbt ga "
                          + "  where gd.t_DocKind = ? "
                          + "    and gd.t_DocID = ? "
                          + "    and gd.t_TemplNum = " + DLGR_TEMPL_TRANSFER
                          + "    and ga.t_GrDealID = gd.t_ID "
                          + "    and ga.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING
                          + "    and ga.t_State = " + DLGRACC_STATE_PLAN
                         );
  cmd.AddParam(DocKind);
  cmd.AddParam(DocID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    PlanDate = date(DataSet.PlanDate);
  end;

  return PlanDate;
end;

//Функция поиска числа запланированных строк переноса по срокам в графике
macro GetFactTransferDate(DocKind, DocID)
  var DataSet, PlanDate = date(0,0,0);
  var cmd = DL_RSDCommand(  " select gd.t_PlanDate "
                          + "   from ddlgrdeal_dbt gd, ddlgracc_dbt ga "
                          + "  where gd.t_DocKind = ? "
                          + "    and gd.t_DocID = ? "
                          + "    and gd.t_TemplNum = " + DLGR_TEMPL_TRANSFER
                          + "    and ga.t_GrDealID = gd.t_ID "
                          + "    and ga.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING
                          + "    and ga.t_State = " + DLGRACC_STATE_FACTEXEC
                         );
  cmd.AddParam(DocKind);
  cmd.AddParam(DocID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    PlanDate = date(DataSet.PlanDate);
  end;

  return PlanDate;
end;
  
//Функция поиска числа запланированных строк переноса по срокам в графике
macro GetNRecsTransfer(DocKind, DocID)
  var cmd = DL_RSDCommand(  " select gd.t_ID "
                          + "   from ddlgrdeal_dbt gd, ddlgracc_dbt ga "
                          + "  where gd.t_DocKind = ? "
                          + "    and gd.t_DocID = ? "
                          + "    and gd.t_TemplNum = " + DLGR_TEMPL_TRANSFER
                          + "    and ga.t_GrDealID = gd.t_ID "
                          + "    and ga.t_AccNum = " + DLGR_ACCKIND_ACCOUNTING
                          + "    and ga.t_State = " + DLGRACC_STATE_PLAN
                         );
  cmd.AddParam(DocKind);
  cmd.AddParam(DocID);

  return cmd.GetCount();
end;

//Процедура поиска даты следующего переноса по срокам
macro GetNeedNextTransfer(FD,                    //IN  Объект
                          Dпс:DATE,              //IN  Дата последнего запланированного переноса
                          Tr:@BOOL,              //OUT Признак переноса требований
                          Ob:@BOOL,              //OUT Признак переноса обязательств
                          K1:@INTEGER,           //OUT Количество дней до исполнения оплаты
                          K2:@INTEGER,           //OUT Количество дней до исполнения поставки
                          AccTrOld:TRECHANDLER,  //OUT Старый счёт по требованиям по сделке
                          AccObOld:TRECHANDLER   //OUT Старый счёт по обязательствам по сделке
                         )

  var paym_rq = FD.GetRQ(DLRQ_TYPE_PAYMENT);
  var del_rq = FD.GetRQ(DLRQ_TYPE_DELIVERY);
  var Dо = paym_rq.rec.PlanDate;
  var Dп = del_rq.rec.PlanDate;
  var DealIsBuy = IsBUY(FD.Group);
  var DealIsSale = IsSALE(FD.Group);
  record PeriodTr(mcperiod);
  record PeriodOb(mcperiod);
  
  if(IsClientDeal(FD.tick.rec))
    return 0;
  end;
      
  Tr = false; Ob = false;

  ClearRecord(PeriodTr);
  ClearRecord(PeriodOb);

  AccTrOld.Clear();
  AccObOld.Clear();

  Dо = GetDateAfterWorkDays( Dо, 0 );
  Dп = GetDateAfterWorkDays( Dп, 0 );

  if((paym_rq.rec.State == DLRQ_STATE_DELAYED) OR (paym_rq.rec.State == DLRQ_STATE_OVERDUE))
    K1 = 0;
  else
    K1 = Dо - Dпс; //количество календарных дней, оставшееся до исполнения требований по сделкам продажи или обязательств по сделкам покупки
  end;

  if((del_rq.rec.State == DLRQ_STATE_DELAYED) OR (del_rq.rec.State == DLRQ_STATE_OVERDUE))
    K2 = 0;
  else
    K2 = Dп - Dпс; //количество календарных дней, оставшееся до исполнения требований по сделкам покупки или обязательств по сделкам продажи
  end;

  if(((DealIsBuy) AND (K2 > 0)) OR ((DealIsSale) AND (K1 > 0)))
    if( not FD.IsExistAccount( "+Форвард, дрейф", null, null, FIROLE_FIREQ, AccTrOld, null, Dпс ) )
      msgbox("Ошибка поиска счета по категории \"+Форвард, дрейф\". "+GetErrMsg());
      return 1;
    end;

    if(MC_GetPeriodForCurrAcc( "+Форвард, дрейф", FD.Kind, FD.ID, FD.GetBasisFIRole(FIROLE_FIREQ), AccTrOld.rec.Code_Currency, MC_PERIOD_CURR, false, PeriodTr) != 0)
      msgbox("Ошибка определения периода срочности для счета по категории \"+Форвард, дрейф\". "+GetErrMsg());
      return 1;
    end;
  end;

  if(((DealIsBuy) AND (K1 > 0)) OR ((DealIsSale) AND (K2 > 0)))
    if( not FD.IsExistAccount( "-Форвард, дрейф", null, null, FIROLE_FICOM, AccObOld, null, Dпс ) )
      msgbox("Ошибка поиска счета по категории \"-Форвард, дрейф\". "+GetErrMsg());
      return 1;
    end;

    if(MC_GetPeriodForCurrAcc( "-Форвард, дрейф", FD.Kind, FD.ID, FD.GetBasisFIRole(FIROLE_FICOM), AccObOld.rec.Code_Currency, MC_PERIOD_CURR, false, PeriodOb) != 0)
      msgbox("Ошибка определения периода срочности для счета по категории \"-Форвард, дрейф\". "+GetErrMsg());
      return 1;
    end;
  end;

  if(DealIsBuy)
    if(K1 <= (PeriodOb.LowBound - 1)) 
      Ob = true;
    end;

    if(K2 <= (PeriodTr.LowBound - 1)) 
      Tr = true;
    end;
  elif(DealIsSale)
    if(K1 <= (PeriodTr.LowBound - 1)) 
      Tr = true;
    end;

    if(K2 <= (PeriodOb.LowBound - 1)) 
      Ob = true;
    end;
  end;

  return 0;
end;

//Функция поиска плановой даты следующего переноса по срокам (без коррекции!!!)
macro GetNextPlanTransferDate(FD,                    //IN Объект
                              K1:INTEGER,            //IN Количество дней до исполнения оплаты
                              K2:INTEGER,            //IN Количество дней до исполнения поставки
                              Dпс:DATE,              //IN Дата последнего запланированного переноса
                              PeriodTr:@variant,     //OUT record Период счета требований mcperiod
                              PeriodOb:@variant      //OUT record Период счета обязательств mcperiod
                             ) : date

  var DealIsBuy = IsBUY(FD.Group);
  var DealIsSale = IsSALE(FD.Group);
  var AccTr = TRecHandler("account.dbt");
  var AccOb = TRecHandler("account.dbt");
  var B1 = 0, B2 = 0;
  var Dпс1 = date(0,0,0), Dпс2 = date(0,0,0);
  var Dпл = date(0,0,0), Dплн = date(0,0,0);
  var dealType = FD.ВидСрочностиСделки();
  var accdocTr = TRecHandler ("mcaccdoc");
  var accdocOb = TRecHandler ("mcaccdoc");
  // DEF-71005 На момент открытия сделки счета по КУ +/-Форвард, дрейф не открыты. IsExistAccount не может найти фактический счета
  // Пришлось при откытии получить accdoc, а из него PeriodID счета
  // Поставила условие, что сделка не открыта (это первоначальное построение графика) и то, что это ПФИ. Чтобы избежать установки строки графика
  // для других ситуаций, если они возникнут
  var IsNotOpenDeal = IIF((FD.Tick.rec.DealStatus == 0) and (not(FD.Tick.rec.IsPFI and (FD.Tick.rec.OriginID == CodeFor("Ю")))), true, false);
  
  if(IsClientDeal(FD.tick.rec))
    return date(0,0,0);
  end;

  ClearRecord(PeriodTr);
  ClearRecord(PeriodOb);
  
  ClearRecord(accdocTr);
  ClearRecord(accdocOb);
  
  if(СделкаНеРанееТретьегоДня == dealType)
    if( (PeriodIDTrGl == 0) and (not FD.IsExistAccount( "+Форвард, дрейф", null, true, FIROLE_FIREQ, AccTr, null, Dпс )) )
      if (IsNotOpenDeal)
        if(not FD.OpenAccount( "+Форвард, дрейф", true, false, FIROLE_FIREQ, AccTr, Dпс, null, null, accdocTr))
            msgbox("Ошибка поиска счета по категории \"+Форвард, дрейф\"");
            return date(0,0,0);
        end;
      else
        msgbox("Ошибка поиска счета по категории \"+Форвард, дрейф\"");
        return date(0,0,0);
      end;
    end;

    if( (PeriodIDObGl == 0) and (not FD.IsExistAccount( "-Форвард, дрейф", null, true, FIROLE_FICOM, AccOb, null, Dпс )))
      if (IsNotOpenDeal)
        if(not FD.OpenAccount( "-Форвард, дрейф", true, false, FIROLE_FICOM, AccOb, Dпс, null, null, accdocOb))
            msgbox("Ошибка поиска счета по категории \"-Форвард, дрейф\"");
            return date(0,0,0);
        end;
      else
        msgbox("Ошибка поиска счета по категории \"-Форвард, дрейф\"");
        return date(0,0,0);
      end;
    end;
  else
    return date(0,0,0);
  end;

  // Получить B
  macro GetB(recPeriod)
    var B = 0;
    if (recPeriod.ID == 0) // не нашли запись
      B = 0;
    else
      B = recPeriod.HighBound;
      if(recPeriod.IsHighInclude == UNSET_CHAR)
        B = B - 1;
      end;
    end;
    return B;
  end;

  // Требование
  if (PeriodIDTrGl == 0)
    if (IsNotOpenDeal and (AccTr.rec.AccountID == 0))
        SP_FindPrevPeriod(accdocTr.rec.PeriodID, PeriodTr);
    else
        MC_GetPeriodForCurrAcc( "+Форвард, дрейф", FD.Kind, FD.ID, FD.GetBasisFIRole(FIROLE_FIREQ), AccTr.rec.Code_Currency, MC_PERIOD_PREV, false, PeriodTr);
    end;
  else
    // ID периода известен. Найдем из базы
    SP_FindPrevPeriod(PeriodIDTrGl, PeriodTr);
  end;

  if (DealIsBuy)
    B2 = GetB(PeriodTr);
  else
    B1 = GetB(PeriodTr);
  end;

  // Обеспечение
  if (PeriodIDObGl == 0) 
    if (IsNotOpenDeal and (AccOb.rec.AccountID == 0))
        SP_FindPrevPeriod(accdocOb.rec.PeriodID, PeriodOb);
    else
        MC_GetPeriodForCurrAcc( "-Форвард, дрейф", FD.Kind, FD.ID, FD.GetBasisFIRole(FIROLE_FICOM), AccOb.rec.Code_Currency, MC_PERIOD_PREV, false, PeriodOb);
    end;
  else
    // ID периода известен. Найдем из базы
    SP_FindPrevPeriod(PeriodIDObGl, PeriodOb);
  end;
       
  if (DealIsBuy)
    B1 = GetB(PeriodOb);
  else
    B2 = GetB(PeriodOb);
  end;

  if(K1 == 0)
    B1 = 0;
  elif((B1 - K1) == 0)
    B1 = 1;
  end;

  if(K2 == 0)
    B2 = 0;
  elif((B2 - K2) == 0)
    B2 = 1;
  end;

  if(K1 < B1)
    Dпс1 = date(0,0,0);
  elif((K1 > 0) and (B1 > 0))
    Dпс1 = Dпс + (K1 - B1);
  end;

  if(K2 < B2)
    Dпс2 = date(0,0,0);
  elif((K2 > 0) and (B2 > 0))
    Dпс2 = Dпс + (K2 - B2);
  end;

  if((Dпс1 != date(0,0,0)) and (Dпс2 != date(0,0,0)))
    Dпл = min(Dпс1, Dпс2);
  else
    if((K1 > 0) and (K2 > 0))
      Dпл = date(0,0,0);
    else
      Dпл = max(Dпс1, Dпс2);
    end;
  end;

  return Dпл;

end;


//Функция поиска даты следующего переноса по срокам
macro GetNextTransferDate(FD,                    //IN Объект
                          K1:INTEGER,            //IN Количество дней до исполнения оплаты
                          K2:INTEGER,            //IN Количество дней до исполнения поставки
                          Dпс:DATE               //IN Дата последнего запланированного переноса
                         ) : date

  record PeriodTr(mcperiod);
  record PeriodOb(mcperiod);
  var Dпл = date(0,0,0), Dплн = date(0,0,0);
  var Mode = GetTransferMode();
  var CalendarID = TRANSFER_CALENDAR;

  if(IsClientDeal(FD.tick.rec))
    return date(0,0,0);
  end;

  Dплн = Dпл = GetNextPlanTransferDate(FD, K1, K2, Dпс, PeriodTr, PeriodOb);
  if( IsWorkdayTransfer( Dплн, CalendarID ) == 0 ) /*Дата переноса попала на выходной*/
    if( (Mode == TRANSFERMODE_NO) OR (Mode == TRANSFERMODE_FIRSTDAY) )
       Dплн = GetDateAfterWorkDays( Dпл, 0, CalendarID );
    else
       Dплн = GetDateAfterWorkDays( Dпл, -1, CalendarID );
    end;
  end;

  if((Dплн != date(0,0,0)) and (((FD.ExistAvance) and (Dплн < FD.GetRQ(DLRQ_TYPE_AVANCE).rec.PlanDate)) OR (not FD.ExistAvance)))
    if( Dплн != Dпл )
      if( ((PeriodOb.LowBound <= 1) OR (PeriodOb.Number == 1)) OR ((PeriodTr.LowBound <= 1) OR (PeriodTr.Number == 1))) /* очередной период срочности - до востребования (с нулевым сроком)*/ 
        if( Mode == TRANSFERMODE_NO )
          Dплн = Date( 0, 0, 0 ); /*Перенос по срокам не нужен*/
        end;
      end;
    end;
    
    return Dплн;
  end;

  return date(0,0,0);
end;

MACRO CheckTransfer(FD)
  var N;

  if(FD.dl_leg.rec.OperState == DL_LEG_OUTBAL)
    N = GetNRecsTransfer(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID);
    if( N > 1)
      msgbox("Найдено более одной строки переноса по срокам в  графике обработки сделки");
      return 1;
    elif( N != 0 )
      msgbox("Запрещено выполнять снятие с внебаланса. Ожидается выполнение переноса по срокам");
      return 1;
    end;
  end;

  return 0;
END;

/**
	@brief 		Проверка, что сделка является сделкой today (для доработок по DEF-52640)
                        Сделка today определяется по условию совпадаения дат
                          заключения (tick.DealDate), плановой оплаты и плановой поставки 
	@param[in] 	tick, буфер сделки
	@return 	true -- для сделки today, иначе false
*/
MACRO IsTodayDeal(tick)
  var leg = TBFile("dl_leg.dbt");
  var IsToday = false;
  IF( (FindDL_LEG( 0, tick.DealId, 0, leg ) == 0)
     and (tick.DealDate == SP_GetPayPlanDate( leg.rec ))   		// дата оплаты
     and (tick.DealDate == SP_GetAvoirissPlanDate( leg.rec ))  		// дата поставки
  )
    IsToday = true;
  END;
  return IsToday;
END; // IsTodayDeal


//Процедура инициализации создания строки графика под сделкой в транзакции начала операции
private var grdeal = TRecHandler("dlgrdeal.dbt");
macro SetGrDeal(pDocKind:integer, pDocID:integer, pFIID:integer, pTemplNum:integer, pPlanDate, pPlanTime, pRQID:integer)
  var celendId = DL_GetDefaultCalendar();
  var params, FD, curId;
  if(ValType(pPlanDate) != V_DATE)
    pPlanDate = date(0,0,0);
    //определить вид даты по шаблону и найти значение в массиве
    if(ValType(TemplDateArr[pTemplNum]) != V_UNDEF)
      pPlanDate = GrDateArray[TemplDateArr[pTemplNum]];
    else
      var templ = TBFile("dlgrtempl.dbt");
      templ.Clear();
      templ.rec.Num = pTemplNum;                        
      if((templ.GetEQ()) and (ValType(GrDateArray[templ.rec.DateKind]) != V_UNDEF))
        pPlanDate = GrDateArray[templ.rec.DateKind];

        TemplDateArr[pTemplNum] = templ.rec.DateKind; //кешируем вид даты для шаблона
      end;
    end;
  end;

  if(ValType(pPlanTime) != V_TIME)
    pPlanTime = time(0);
  end;

  if( TemplDateArr[pTemplNum] != DLGR_DATEKIND_TRANSFER )
    if ((ValType(pPlanDate)==V_DATE) and (pPlanDate != date(0,0,0)) and (ValType(TemplDateArr[pTemplNum]) != V_UNDEF))
      var tick = TBFile("dl_tick.dbt");
      tick.rec.DealId = pDocId;
      if (tick.GetEQ() 
        and (tick.rec.BOfficeKind == pDocKind) 
        and (IsTodayDeal(tick.rec) == false) 		// DEF-64540, для сделок today можно не использовать поправку по календарю
      )
        params = makeArray(
          DL_KeyPair("Object",DL_CalendGetOperNameByKind(tick.rec.Dealtype)),
          DL_KeyPair("Market",tick.rec.marketid)
        );
        if (tick.rec.marketid > 0)
          params[params.size] = DL_KeyPair("MarketPlace",DL_CALLNK_MARKETPLACE_SEC);
          FD = DL_CalendGenerateFD(pDocKind,pDocID,TemplDateArr[pTemplNum]);
          if (FD != null)
            curId = FD.ПолучитьКалендВалютуВидаДаты(TemplDateArr[pTemplNum]);
            if (curId > 0)
              params[params.size] = DL_KeyPair("Currency",curId);
              params[params.size] = DL_KeyPair("DayType",DL_CALLNK_MRKTDAY_SETTL);
            end;
          end;
        end;
        var emitCountryId = DL_GetEmitCountryIDFromFiid(pFIID);
        if (emitCountryId > 0)
          params[params.size] = DL_KeyPair("EmitCountry",emitCountryId);
        end;
        var avoirKindId = DL_GetAvoirKindIDFromFiid(pFIID);
        if (avoirKindId > 0)
          params[params.size] = DL_KeyPair("AvoirKind",avoirKindId);
        end;
        celendId = DL_GetCalendByDynParam(83,params);
        pPlanDate = DL_GetBankDateAfterWorkDayByCalendar(pPlanDate,0,celendId);
      end;
    end;
  end;

  grdeal.Clear();
  grdeal.rec.DocKind  = pDocKind;
  grdeal.rec.DocID    = pDocID;
  grdeal.rec.TemplNum = pTemplNum;
  grdeal.rec.PlanDate = pPlanDate;
  grdeal.rec.PlanTime = pPlanTime;
  grdeal.rec.FIID     = pFIID;

  ArrGrDeal.Add(grdeal, pRQID);
end;

private macro SetGrDealByBuff(pTick, pLeg, pLeg2, pTemplNum, pPlanDate, pFIID:integer, pRQID:integer)
  var vTime = time(0);

  if( (pTick.rec.BOfficeKind == DL_AVRWRT) or (pTick.rec.BOfficeKind == DL_AVRWRTOWN) )
    vTime = pTick.rec.DealTime;
    
  elif(pTick.rec.BOfficeKind == DL_SECURITYDOC)
    if(ValType(TemplDateArr[pTemplNum]) == V_UNDEF)
      var templ = TBFile("dlgrtempl.dbt");
      templ.rec.Num = pTemplNum;                        
      if((templ.GetEQ()) and (ValType(GrDateArray[templ.rec.DateKind]) != V_UNDEF))
        TemplDateArr[pTemplNum] = templ.rec.DateKind; //кешируем вид даты для шаблона
      end;
    end;

    if( TemplDateArr[pTemplNum] == DLGR_DATEKIND_DELIVERY ) 
      vTime = pLeg.rec.SupplyTime;
    elif( TemplDateArr[pTemplNum] == DLGR_DATEKIND_DELIVERY2 ) 
      vTime = pLeg2.rec.SupplyTime;
    else
      vTime = pTick.rec.DealTime;

      if( (TemplDateArr[pTemplNum] == DLGR_DATEKIND_PAYMENT) OR (TemplDateArr[pTemplNum] == DLGR_DATEKIND_PAYMENT2) )

         var Group = SP_GetOperationGroup(pTick.rec);

         if( IsREPO(Group) )
            var IsExchangeRepo = IsEXCHANGE(Group);
            var СделкаРЕПОБрокер = (pTick.rec.Flag4 == SET_CHAR) and (pTick.rec.BrokerID > 0);

            if( IsExchangeRepo and (not СделкаРЕПОБрокер) )
               if( TemplDateArr[pTemplNum] == DLGR_DATEKIND_PAYMENT )
                 vTime = pLeg.rec.SupplyTime;
               else
                 vTime = pLeg2.rec.SupplyTime;
               end;     
            end;
         end;
      end;
    end;     
  end;

  var FIID:integer = ALLFININSTR;
  if( pFIID != NULL )
     FIID = pFIID;
  else
     FIID = pTick.rec.PFI;
  end;

  var RQID:integer = 0;
  if( pRQID != NULL )
     RQID = pRQID;
  end;

  SetGrDeal(pTick.rec.BOfficeKind, pTick.rec.DealID, FIID, pTemplNum, pPlanDate, vTime, RQID);
end;

private macro SetCategory(Tick, ObjGroup:integer, AttrID:integer, CatDate:date, ErrorMsg:@string)
  var stat:integer = 0;
 
  if( ( not DisconnectObjAttr(OBJTYPE_SECDEAL,  UniID( Tick, OBJTYPE_SECDEAL), ObjGroup) ) OR
      ( not ConnectObjAttr(stat, OBJTYPE_SECDEAL, UniID( Tick, OBJTYPE_SECDEAL), ObjGroup, AttrID, null, null, CatDate) ) OR
      ( stat != 0 )
    )
     ErrorMsg = "Ошибка при установке категории. " + GetErrMsg();
  end;
 
  return stat;
end;

private macro GetRqID(tick, Type:integer, FIID:integer, DealPart:integer ):integer
  var RetVal:integer = 0;
  var cmd = DL_RSDCommand();
  var query = " SELECT T_ID " +
              "   FROM DDLRQ_DBT " +
              "  WHERE T_DOCKIND = ? " +
              "    AND T_DOCID = ? " +
              "    AND T_TYPE = ? " +
              "    AND T_FIID = ? " +
              "    AND T_DEALPART = ? ";

  cmd.AddParam(tick.rec.BOfficeKind);
  cmd.AddParam(tick.rec.DealID);
  cmd.AddParam(Type);
  cmd.AddParam(FIID);
  cmd.AddParam(DealPart);
  var DataSet = cmd.Execute(query);
  if( DataSet.moveNext() )
     RetVal = SQL_ConvTypeInteger(DataSet.ID);
  end;

  return RetVal;
end;

private macro isBulkMessage(Code:string)
  var cmd = DL_RSDCommand();
  var query = " SELECT * FROM DIR_TYPESMESSAGE_DBT " +
              " WHERE T_MESSAGECODE = ? ";
  cmd.AddParam(Code);        
  var DataSet = cmd.Execute(query);
 
  if( DataSet.moveNext() and ( DataSet.Bulk_Report == "X" ) )
    return true;
  else
    return false;
  end;

end;

PRIVATE MACRO GetProductCode( TypeDeal:integer, TypeProduct:integer, tick):string
    
    var RetVal:string = "";
    
    if( (TypeDeal == 2 /*Term*/) OR (TypeDeal == 3 /*Spot*/) )
        return "UKWN";
    end;

    // 1
    if( TypeProduct > 0 )
        var Select = " SELECT t_TypeCode "
        + "   FROM dir_typeproduct_dbt "
        + "  WHERE t_ID = ? ";
        
        var cmd = DL_RSDCommand(Select);
        cmd.addParam(TypeProduct);
        
        var DataSet = cmd.Execute();
        if( DataSet.MoveNext() )
        RetVal = DataSet.TypeCode;
        else
            RetVal = "_";
        end;
    end;
    
    if( RetVal == "R" ) // РЕПО
        // 2
        var dl_leg = Trechandler("dl_leg.dbt");
        var fin = TBFile("fininstr.dbt");
        FindDL_LEG( LEG_KIND_DL_TICK, tick.rec.DealID, 0, dl_leg );
        fin.rec.FIID = dl_leg.rec.PFI;
        fin.GetEQ();
        if( FI_IsShare(fin) OR FI_IsInvestmentShare(fin) ) /*Долевая ц/б*/
            RetVal = RetVal + "E";
        elif( FI_IsBond(fin) == true ) /*Долговое обязательство*/
            RetVal = RetVal + "D";
        else
            RetVal = RetVal + "X";
        end;
        
        // 3
        RetVal = RetVal + "O";
        
        // 4
        if( dl_leg.rec.IncomeRate != 0.0 )
            RetVal = RetVal + "F";
        else
            RetVal = RetVal + "Z";
        end;
        
        // 5
        if( tick.rec.OpenDate == SET_CHAR )
            RetVal = RetVal + "O";
        else
            RetVal = RetVal + "F";
        end;
    end;
        
    return RetVal;
END;

private macro РАБОТА_С_РЕПОЗИТАРИЕМ()
    const RegPath = "COMMON\\WORK_MODE\\IR_WORK_WITH_SECURITIES";
    var workWithRepository = "", err;
    
    GetRegistryValue( RegPath, V_BOOL, workWithRepository, err );
    
    if ( err != 0 )
        MsgBox( "Ошибка при получении значения настройки \"" + RegPath + "\"" );
    else
        if ( workWithRepository == "X" )
            return true;
        else
            return false;
        end;
    end;
end;

private macro РАБОТА_С_РЕПОЗИТАРИЕМ_ФИССиКО()
    const RegPath = "COMMON\\WORK_MODE\\IR_WORK_WITH_DERIVATIVES";
    var workWithRepository = "", err;
    GetRegistryValue( RegPath, V_BOOL, workWithRepository, err );
    
    if ( err != 0 )
        MsgBox( "Ошибка при получении значения настройки \"" + RegPath + "\"" );
    else
        if ( workWithRepository == "X" )
            return true;
        else
            return false;
        end;
    end;
end;

macro ПРОВЕРКА_РЕПОЗИТАРНЫХ_ПАРАМЕТРОВ(tick, ОжиданиеЗапроса : @bool, requestTimeout : @integer,  DocKind : @integer, DealID : @integer, PFI : @integer )
    var repozdeal = TBFile("dl_repozdeal.dbt", "R", 1);
    repozdeal.Clear();
    if( (tick.rec.ParentID == 0) AND (РАБОТА_С_РЕПОЗИТАРИЕМ()) )
      repozdeal.rec.DocKind = DocKind = tick.rec.BofficeKind;
      repozdeal.rec.DocID = DealID = tick.rec.DealID;
      PFI = tick.rec.PFI;
    elif( (РАБОТА_С_РЕПОЗИТАРИЕМ_ФИССиКО()) )
      // Если сделка является порожденной от сделки в ФИИСиКО, то вставляем действие в график первичной сделки
      repozdeal.rec.DocID = DealID = tick.rec.ParentID;
      var cmd = DL_RSDCommand("SELECT t_DocKind FROM ddvndeal_dbt WHERE t_ID = ?");
      cmd.AddParam( tick.rec.ParentID );
      var DataSet = cmd.Execute();
      if( DataSet.moveNext() )
        repozdeal.rec.DocKind = DocKind = DataSet.DocKind;
      end;
    end;
    if( GetEQ(repozdeal) )
      if( (repozdeal.rec.INITIATOR == 2) and (repozdeal.rec.METHOD_WAYTWOWAY == 1))
        ОжиданиеЗапроса = true;
        requestTimeout = repozdeal.rec.requestTimeout;
      end;
      if(repozdeal.rec.reporting == UNSET_CHAR)  //если флаг репортинг в репозитарий не установлен
        return false;
      end;
    else
      return false;  // если не нашли репозитарные параметры, то действия не вставляем
    end;

    return true;
end;

macro SP_InsertGrDealFairValueRevaluationSQL(FD, ДатаИсполненияШага, OperId, StepId)
  var planDate;
  var reqTimeout = 0, BOfficeKind = 0, DealID = 0, FIID = -1;
  var ОжиданиеЗапроса = false;
  var query, cmd;
 
  if( not ПРОВЕРКА_РЕПОЗИТАРНЫХ_ПАРАМЕТРОВ( FD.tick, @ОжиданиеЗапроса, @reqTimeout, @BOfficeKind, @DealID, @FIID) )
    return 0;
  end;

  planDate = GetDateTemplRevFr(ДатаИсполненияШага, FD);
  if(planDate != date(0,0,0))
    query = "BEGIN "
              +"    RSI_DLGR.RSI_InsertGrDeal(?, ?, ?, ?, ?, ?, ?, ?, ?);"
              +"END;";
    cmd = RsdCommand(query);
    cmd.NullConversion = true;
    cmd.addParam("", RSDBP_IN, BOfficeKind);
    cmd.addParam("", RSDBP_IN, DealID);
    cmd.addParam("", RSDBP_IN, OperId);
    cmd.addParam("", RSDBP_IN, StepId);
    cmd.addParam("", RSDBP_IN, DLGR_TEMPL_REVALUATION_FAIRVALUE);
    cmd.addParam("", RSDBP_IN, planDate);
    cmd.addParam("", RSDBP_IN, time(0,0,0));
    cmd.addParam("", RSDBP_IN, -1);
    cmd.addParam("", RSDBP_IN, NULL);
    cmd.execute();
  end;

  return 0;
end;

macro SP_InsertGrDealFairValueRevaluation(FD, ДатаИсполненияШага)
  var planDate;
  var reqTimeout = 0, BOfficeKind = 0, DealID = 0, FIID = -1;
  var ОжиданиеЗапроса = false;
 
  if( not ПРОВЕРКА_РЕПОЗИТАРНЫХ_ПАРАМЕТРОВ( FD.tick, @ОжиданиеЗапроса, @reqTimeout, @BOfficeKind, @DealID, @FIID) )
    return 0;
  end;

  planDate = GetDateTemplRevFr(ДатаИсполненияШага, FD);
  if(planDate != date(0,0,0))
    if(DL_InsertGrDeal( BOfficeKind, DealID, DLGR_TEMPL_REVALUATION_FAIRVALUE, planDate, time(0,0,0), -1) != 0 )
      return 1;
    end;
  end;

  return 0;
end;

macro SP_InsertGrDealFairValueRevaluationPfiUni(FD)
  var stat = 0, cmd, DataSet;
  if (РАБОТА_С_РЕПОЗИТАРИЕМ())
    if (FD)
      if(FD.tick.rec.IsPFI == SET_CHAR)
        if(SP_InsertGrDealFairValueRevaluation(FD, FD.DateArray[DATE_DEALDATE]))
          msgbox(1, "Ошибка при вствке действия графика \"Переоценка СС\"");
          stat = 1;
        end;
      end;
    else
      stat = ExecMacroFile ("oprmassexec.mac","SP_Mass_SaveDLGRDEALInsert",DLGR_TEMPL_REVALUATION_FAIRVALUE, -1);
      cmd = DL_RSDCommand( " SELECT deal.t_DealID, deal.t_ID_Operation, deal.t_ID_Step"
                              +"   FROM DV_MKDEAL_MASS_EXEC deal ");
      DataSet = cmd.execute();
      while(DataSet.moveNext() and (stat == 0))
        var locFd = SPFirstDoc(LEG_KIND_DL_TICK, DataSet.DealID);
        if(locFd.tick.rec.IsPFI == SET_CHAR)
          stat = SP_InsertGrDealFairValueRevaluationSQL(locFd, locFd.DateArray[DATE_DEALDATE], DataSet.ID_Operation, DataSet.ID_Step);
        end;
      end;
    end;
  end;
  return stat;
end;

private macro Вид_Кода_Сделки()
    const RegPath = "РЕПОЗИТАРИЙ\\ВИД КОДА СДЕЛКИ ДЛЯ НОМ. ДОГ.";
    var По_Референсу = 1, err;
    
    GetRegistryValue( RegPath, V_INTEGER, По_Референсу, err );
    
    if ( err != 0 )
        MsgBox( "Ошибка при получении значения настройки \"" + RegPath + "\"" );
    end;
    return По_Референсу;
end;

private macro getMessages(DealType:integer, FI:integer, PFI:integer, isPFI, isSpot, IsRepo:bool, BulkMessage:@string, EasyMessage:@string)
  var cmd = DL_RSDCommand(),
      query = " SELECT * " +
              "   FROM DIR_SETUPMESSAGE_DBT STPMSG " +
              " WHERE T_KIND_OPERATION = ? " +
              "    AND T_FI_KIND = ? " +
              "    AND T_AVOIRKIND = RSB_FIInstr.FI_AvrKindsGetRoot (T_FI_KIND,  " +
              "       ( SELECT T_AVOIRKIND FROM DFININSTR_DBT WHERE T_FIID = ? AND T_FI_KIND = ? ) ) " +
              "    AND T_REPORTING = 'X'"; 

  if( IsRepo != true )
    if( isPFI == SET_CHAR )
      query = query + " AND T_ISPFI = 'X'";
    else
      query = query + " AND T_ISPFI = CHR(0)";
    end;

    if( isSpot == SET_CHAR )
      query = query + " AND T_ISSPOT = 'X'";
    else
      query = query + " AND T_ISSPOT = CHR(0)";
    end;
  end;

  cmd.AddParam(DealType);
  cmd.AddParam(FI);
  cmd.AddParam(PFI);
  cmd.AddParam(FI);
        
  var DataSet = cmd.Execute(query);
  var stat = 0;
  
  bulkMessage = easyMessage = null;
   
  while (DataSet.moveNext())
    if (isBulkMessage(DataSet.MessageCode))
      if (BulkMessage == null)
        BulkMessage = DataSet.MessageCode;  
      else
        stat = 1;
      end;
    else
      if (EasyMessage == null)
        EasyMessage = DataSet.MessageCode;
      else
        stat = 1;
      end;    
    end;
  end;
  if (( not BulkMessage ) and ( not EasyMessage ))
    stat = 1;
  end;
  return stat;   
end;

/*********************/
private var repozdeal = TRecHandler("dl_repozdeal.dbt");
private macro GetContract(dealid:integer):string
    var cmd = DL_RSDCommand();
    var query = "SELECT SPG.T_XLD "
                + " FROM dspgrdoc_dbt spgd, dspground_dbt spg "
                + " WHERE SPG.T_SIGNEDDATE = "
                + " (SELECT MAX (SPG.T_SIGNEDDATE) "
                + " FROM dspgrdoc_dbt spgd, dspground_dbt spg "
                + " WHERE SPG.T_SPGROUNDID = SPGD.T_SPGROUNDID "
                + " AND SPGD.T_SOURCEDOCID = ? "
                + " AND SPG.T_KIND = 26) "
                + " AND SPG.T_SPGROUNDID = SPGD.T_SPGROUNDID "
                + " AND SPG.T_KIND = 26 "
                + " AND SPGD.T_SOURCEDOCID = ? ";
    cmd.AddParam(dealid);  
    cmd.AddParam(dealid);     
    var DataSet = cmd.Execute(query);
    DataSet.moveNext();
    return DataSet.XLD;
    
end; 

PRIVATE MACRO HasInvalidSymbol( Str )

  var stat = 0, i = 1, ch, ValidSymbols = "QWERTYUIOPASDFGHJKLZXCVBNMqwertyuiopasdfghjklzxcvbnm1234567890";

      while( (not stat) and (i <= strlen(Str)) )
        ch = SubStr( Str, i, 1 );
        if( not Index( ValidSymbols, ch ))
          stat = 1; 
        end;
        i = i + 1;
      end;

  return stat;

end;

private macro РепозБуф(tick:TRecHandler,GA:TRecHandler,BulkMessage:@string,settingValue:integer, IsRepo)

    private macro GenerateIRDealCode(dCode:@string)

        private macro DV_LZ( num, len:integer ):string
           var RetVal:string = "";
           var str1:string = "", len1:integer = 0;

           str1 = trim(string(num));
           len1 = strlen(str1);
           if( len1 >= len )
              RetVal = str1;
           else
              RetVal = mkstr("0", len-len1) + str1; /* слева */
           end;

           return RetVal;
        end;

        private macro DateToStrDDMMYYYY( pDate:date ):string
          var Day:integer = 0, Month:integer = 0, Year:integer = 0;
          DateSplit(pDate, Day, Month, Year);
          return DV_LZ(Day, 2) + DV_LZ(Month, 2) + DV_LZ(Year, 4);
        end;

        const NRDSimbol = "QWERTYUIOPASDFGHJKLZXCVBNM0123456789";
        var RefNum;
        var i = 0;
        var str = "";
        var ar = StrSplit2(dCode,1);
        while (i < ar.size)
          if (index(NRDSimbol,ar[i])>0 )
            str = str + ar[i];
          end;
        i = i + 1;
        end;
        str =DAteToStrDDMMYYYY({Curdate})+ str;
        if(not GenerateReference(1000012, RefNum))
          str = str + RefNum;
        end;
        dCode = str;
    end;

    
    repozdeal.rec.reporting=SET_CHAR;
    repozdeal.rec.dockind=tick.rec.BOfficeKind;
    repozdeal.rec.docid=tick.rec.dealid;
    repozdeal.rec.date_start=tick.rec.dealdate;
    
    if((GrDateArray[DLGR_DATEKIND_DELIVERY2] != NULL) and (GrDateArray[DLGR_DATEKIND_PAYMENT2] != NULL))
      repozdeal.rec.date_end = max(GrDateArray[DLGR_DATEKIND_DELIVERY2],GrDateArray[DLGR_DATEKIND_PAYMENT2]);
    else
      repozdeal.rec.date_end = max(GrDateArray[DLGR_DATEKIND_DELIVERY],GrDateArray[DLGR_DATEKIND_PAYMENT]);
    end;

    if(GA.rec.GENAGRID > 0)
        repozdeal.rec.Reporter_Bank     =   GA.rec.reporter_bank;
        repozdeal.rec.TYPESECUR         =   GA.rec.TYPESECUR;
        repozdeal.rec.FORMSECUR         =   GA.rec.FORMSECUR;
        repozdeal.rec.INITIATOR         =   GA.rec.CREATOR_UTI;
        repozdeal.rec.METHOD_WAYTWOWAY  =   GA.rec.METHOD_WAYTWOWAY;
        repozdeal.rec.METHOD_REG        =   GA.rec.METHOD_REG;
        //repozdeal.rec.CONTRACT          =   GA.rec.CODE;
        //repozdeal.rec.CONTRACT_IR       =   GA.rec.NUMREPOZ;
        repozdeal.rec.REPORTER_CONTR    =   GA.rec.REPORTER_CONTR;
        repozdeal.rec.CREATOR_UTI       =   GA.rec.CREATOR_UTI;
        repozdeal.rec.REPOZID_BANK      =   GA.rec.CODE_DEAL;
        repozdeal.rec.TYPE_RECONCILIATION	=   GA.rec.TYPE_sver;
       // repozdeal.rec.UTI_contract	=   GA.rec.UTI_Code;
    end;
    GetRegistryValue("РЕПОЗИТАРИЙ\\СРОК ОЖИД.ЗАПР. ПРИ КОМБ.ПОДТВ.", V_INTEGER, repozdeal.rec.RequestTimeout, null);
        var contr=GetContract(tick.rec.dealid);
    
    
    if (Вид_Кода_Сделки()==2)
        repozdeal.rec.CONTRACT   =   tick.rec.dealcodets;
    elif (Вид_Кода_Сделки()==1)
        repozdeal.rec.CONTRACT   =   tick.rec.dealcode;
    elif (Вид_Кода_Сделки()==3)
        repozdeal.rec.CONTRACT   =   "";
    end;    
    
    
    // РИК Банка
    var cmdRIC = DL_RSDCommand();
    var queryRIC = "SELECT t_code as RIC"
+"  FROM (  SELECT OBJ.T_CODE"
+"            FROM dobjcode_dbt obj"
                  +"           WHERE     OBJ.T_OBJECTTYPE = " + OBJTYPE_PARTY
+"                 AND OBJ.T_OBJECTID = "+{OurBank}
                  +"                 AND OBJ.T_CODEKIND = " + PTCK_RIC
                  +"                 AND OBJ.T_BANKDATE <= ? "
                  +"                 AND (   OBJ.T_BANKCLOSEDATE = " + GetSQLDate(date(0,0,0)) 
                  +"                      OR OBJ.T_BANKCLOSEDATE > ? )"
+"        ORDER BY OBJ.T_BANKDATE DESC)"
+" WHERE ROWNUM = 1";

    cmdRIC.AddParam(tick.rec.dealdate);
    cmdRIC.AddParam(tick.rec.dealdate);

    var DataSetRIC = cmdRIC.Execute(queryRIC);
    if(DataSetRIC.moveNext())
       repozdeal.rec.RepozID_Bank = DataSetRIC.RIC;
    else
       repozdeal.rec.RepozID_Bank = "";
    end;

    //определяем сообщение
        if ((settingValue==1)and(repozdeal.rec.INITIATOR==2)and(repozdeal.rec.METHOD_WAYTWOWAY==1))
            repozdeal.rec.STATUS_IR     =   11;
    else
            repozdeal.rec.STATUS_IR     =   1;
        end; 
    
    if(repozdeal.rec.TYPESECUR <= 0)
        repozdeal.rec.TYPESECUR         =   4;
        end;
    
    if(repozdeal.rec.FORMSECUR <= 1)
        repozdeal.rec.FORMSECUR         =   3;
        end;
    
    repozdeal.rec.relcodeid=2;
    
    var cmd = DL_RSDCommand(),
      query = " SELECT * " +
              "   FROM DIR_SETUPMESSAGE_DBT STPMSG " +
              " WHERE T_KIND_OPERATION = ? " +
              "    AND T_FI_KIND = " + FIKIND_AVOIRISS + 
              "    AND T_AVOIRKIND = RSB_FIInstr.FI_AvrKindsGetRoot (T_FI_KIND,  " +
              "       ( SELECT T_AVOIRKIND FROM DFININSTR_DBT WHERE T_FIID = ? AND T_FI_KIND = "+FIKIND_AVOIRISS+" ) ) ";
    
    if( IsRepo != true )
      if( tick.rec.isPFI == SET_CHAR )
        query = query + " AND T_ISPFI = 'X'";
      else
        query = query + " AND T_ISPFI = CHR(0)";
      end;

      if( tick.rec.isSpot == SET_CHAR )
        query = query + " AND T_ISSPOT = 'X'";
      else
        query = query + " AND T_ISSPOT = CHR(0)";
      end;
    end;
    
    cmd.AddParam(tick.rec.DealType);
    cmd.AddParam(tick.rec.PFI);
    
    var DataSet = cmd.Execute(query);
    if(DataSet.moveNext())
      repozdeal.rec.TYPEPRODUCT=DataSet.TYPEPRODUCT;
      repozdeal.rec.TYPEDEAL=DataSet.TYPEDEAL;
        repozdeal.rec.messagecode=BulkMessage;
    end;
    
    var cont;
    cmd = DL_RSDCommand();
    query = " SELECT rsi_rsbparty.PT_GetPartyCode(?, "+PTCK_LEI+") AS LEI FROM DUAL" ;
    if(repozdeal.rec.CREATOR_UTI==1)
        cmd.AddParam({OurBank});
        cont=repozdeal.rec.CONTRACT;
    else
        cmd.AddParam(TICK.rec.PARTYID);
        cont=repozdeal.rec.CONTRACT_CONTR;
    end;

    var LEI;
    DataSet = cmd.Execute(query);
    if( DataSet.moveNext() )
            LEI=DataSet.lei;
      if(cont and(valtype(cont)!=26)and (DataSet.lei!=""))
/*DAN*/           GenerateIRDealCode(@cont);
            repozdeal.rec.UTI_contract=DataSet.lei+cont + "1";
    end;
    end;

                // Проверка на уникальность UTI
    cmd = DL_RSDCommand();
    query ="select 1 from ddl_repozdeal_dbt rd where RD.T_UTI_CONTRACT= ? ";
    cmd.AddParam(repozdeal.rec.UTI_contract);
        DataSet = cmd.Execute(query);
        if(DataSet.moveNext())
            repozdeal.rec.UTI_contract="";
            MsgBoxEx("Не удалось сгенерировать уникальный UTI код",0);
        end;
    
    //проверка кодов LEI и RIC
    cmd = DL_RSDCommand();
   query  =  " SELECT kCode1.T_MAXCODELEN LEI_LEN, kCode2.T_MAXCODELEN RIC_LEN"
            +" FROM DOBJKCODE_DBT kCode1, DOBJKCODE_DBT kCode2"
             +" WHERE KCODE2.T_CODEKIND = "+PTCK_RIC+" AND KCODE1.T_CODEKIND = " + PTCK_LEI;
    
    DataSet = cmd.Execute(query);
    if(DataSet.moveNext())  
        if(DataSet.LEI_LEN!=strlen(LEI))
            MsgBoxEx("Длина кода LEI должна быть 20 символов",0);
        end;
        if(DataSet.RIC_LEN!=strlen(repozdeal.rec.RepozID_Bank))
            MsgBoxEx("Длина репозитарного идентификационного кода должна быть 12 символов",0);
        end;        
    end;
    
    repozdeal.rec.CodeClassPFI = GetProductCode(repozdeal.rec.TYPEDEAL, repozdeal.rec.TYPEPRODUCT, tick);
    
    ArrRepozDeal.Add(repozdeal);
    
    return 0;
end;
/*************/

private macro isMemberRepository(PartyID : integer)
  var cmd = DL_RSDCommand();
  var query = " SELECT 1 FROM DPARTYOWN_DBT "
            + " WHERE T_PARTYID = ? AND T_PARTYKIND =  ? ";
  cmd.AddParam(PartyID);
  cmd.AddParam(PTK_REPOSPRT);
  var DataSet = cmd.Execute(query);
  return DataSet.moveNext();
end;

private macro hasGenAgr(PartyID : integer)
  var cmd = DL_RSDCommand();
  var query = " SELECT 1 FROM DDL_GENAGR_DBT "
            + " WHERE T_PARTYID = ? AND T_STATUS = 10 AND T_PSEUDOGA <> ?";
  cmd.AddParam(PartyID);
  cmd.AddParam(SET_CHAR);
  var DataSet = cmd.Execute(query);
  var genAgrExists = DataSet.moveNext();
  return genAgrExists;
end;

private macro getGrPlanDate(pTemplNum, pendingDay, calendId)
  var planDate = date(0, 0, 0);
  if(ValType(TemplDateArr[pTemplNum]) == V_UNDEF)
    var templ = TBFile("dlgrtempl.dbt");
    templ.rec.Num = pTemplNum;                        
    if((templ.GetEQ()) and (ValType(GrDateArray[templ.rec.DateKind]) != V_UNDEF))
      planDate = GrDateArray[templ.rec.DateKind];
      TemplDateArr[pTemplNum] = templ.rec.DateKind; //кешируем вид даты для шаблона
    end;
  end;
  
  if ((ValType(pendingDay) == V_INTEGER) and (pendingDay > 0))
    planDate = DL_GetBankDateAfterWorkDayByCalendar(planDate,pendingDay,calendId);
  end;

  return planDate;
end;

private macro isPendingRequestDeal(GA:TRecHandler)
  return ( GA.rec.INITIATOR == 2 ) and ( GA.rec.METHOD_WAYTWOWAY == 1 );
end;

/*Процедура инициализации создания графика по сделке в транзакции начала операции*/
macro SetAllGrDeal(tick:TRecHandler,
                   leg:TRecHandler,
                   AvrRqID:INTEGER,
                   leg2:TRecHandler, 
                   AvrRqID2:INTEGER,
                   IsBuy, 
                   IsRepo, 
                   IsOutExchange,
                   IsBroker,
                   IsBasket,
                   IsRET_COUPON,
                   IsRET_PARTLY,
                   IsKSU,
                   IsLoan,
                   IsRET_ISSUE,
                   GA:TRecHandler,
                   ClientNeedNUTX,
                   IsRET_ADDINCOME)

  var cmd, query, DataSet;
  var stat;
  var AccTr, AccOb;
  var Dсд, Dо, Dп;
  var Dпс;
  var FD;
  var isKDU;
  var calendId = DL_GetCalendByDynParam(83,makeArray(
          DL_KeyPair("Object",DL_CalendGetOperNameByKind(tick.rec.Dealtype)),
          DL_KeyPair("Market",tick.rec.marketid)
        ));
  var fin = TBFile("fininstr.dbt");

  GetRegistryValue("SECUR\\ВЕДЕНИЕ КДУ", V_BOOL, isKDU, stat);
  if (stat)
    isKDU = false;
    stat = 0;
  end;
  //сначала определить все даты по сделке
  GrDateArray.size = 0;

  //1. Дата заключения
  GrDateArray[DLGR_DATEKIND_DELADATE]  = tick.rec.DealDate;
  //2. Дата поставки
  GrDateArray[DLGR_DATEKIND_DELIVERY]  = SP_GetAvoirissPlanDate( leg.rec );
  //3. Дата оплаты
  GrDateArray[DLGR_DATEKIND_PAYMENT]   = SP_GetPayPlanDate( leg.rec );

  // DEF-64540, для сделок today нужно использовать календарь с типом дней "торговые"
  if(GrDateArray[DLGR_DATEKIND_DELADATE] == GrDateArray[DLGR_DATEKIND_PAYMENT]) 
     calendID = DL_GetCalendByDynParam(83,makeArray(
          DL_KeyPair("ObjectType", string(DL_MARKETDEAL))                      // Биржевая сделка == 1
          , DL_KeyPair("Market", string(tick.rec.marketid))                    // биржа ММВБ 
          , DL_KeyPair("DayType", string(DL_CALLNK_MRKTDAY_TRADE))             // тип дней "торговые" == 1
          , DL_KeyPair("MarketPlace", string(DL_CALLNK_MARKETPLACE_SEC))       // Фондовый рынок == 1
          , DL_KeyPair("Currency", string(leg.rec.CFI))                        // Валюта сделки
     ));
  end;

  //4. Дата оплаты аванса/задатка
  if( leg.rec.Start != Date(0,0,0) )
     GrDateArray[DLGR_DATEKIND_AVANCE]    = leg.rec.Start;
  else
     GrDateArray[DLGR_DATEKIND_AVANCE]    = Date(0,0,0);
  end;
  if(IsRepo == true)
    //5. Дата поставки по 2ч РЕПО
    GrDateArray[DLGR_DATEKIND_DELIVERY2] = SP_GetAvoirissPlanDate( leg2.rec );
    //6. Дата оплаты по 2ч РЕПО
    GrDateArray[DLGR_DATEKIND_PAYMENT2]  = SP_GetPayPlanDate( leg2.rec );
    //7. Дата оплаты аванса/задатка по 2ч РЕПО
    if( leg2.rec.Start != Date(0,0,0) )
       GrDateArray[DLGR_DATEKIND_AVANCE2]   = leg2.rec.Start;
    else
       GrDateArray[DLGR_DATEKIND_AVANCE2]   = Date(0,0,0);
    end;
  end;
  if(IsLoan  == true)
    //5. Дата поставки по 2ч займа
    GrDateArray[DLGR_DATEKIND_DELIVERY2] = leg2.rec.expiry;
  end;
  
  //8. Дата переноса на внебалансе
  GrDateArray[DLGR_DATEKIND_TRANSFER]  = GrDateArray[DLGR_DATEKIND_DELADATE]; //??? пока так. Нужно определять как-то по другому
  //9. Дата постановки на баланс
  GrDateArray[DLGR_DATEKIND_BALANCE]   = min(GrDateArray[DLGR_DATEKIND_DELIVERY], GrDateArray[DLGR_DATEKIND_PAYMENT]);
  if(GrDateArray[DLGR_DATEKIND_AVANCE] > date(0,0,0))
    GrDateArray[DLGR_DATEKIND_BALANCE]   = min(GrDateArray[DLGR_DATEKIND_BALANCE], GrDateArray[DLGR_DATEKIND_AVANCE]);
  end;
  //10. Дата оплаты комиссии
  //GrDateArray[DLGR_DATEKIND_COMISS]    = GetMinComPlanDateByDeal();

  //12. Дата формирования поручения депо
  GrDateArray[DLGR_DATEKIND_DEPO] = GrDateArray[DLGR_DATEKIND_DELIVERY];
  if(IsOutExchange or IsBroker)
    GrDateArray[DLGR_DATEKIND_DEPO] = GrDateArray[DLGR_DATEKIND_DEPO] - DL_NDaysBeforeInsertDepoDraft;

    if(GrDateArray[DLGR_DATEKIND_DEPO] < GrDateArray[DLGR_DATEKIND_DELADATE])
      GrDateArray[DLGR_DATEKIND_DEPO] = GrDateArray[DLGR_DATEKIND_DELADATE];
    end; 
  end;

  if((IsRepo == true ) or (IsLoan == true))
    //13. Дата формирования поручения депо по 2 ч.
    GrDateArray[DLGR_DATEKIND_DEPO2] = GrDateArray[DLGR_DATEKIND_DELIVERY2];

    if(IsOutExchange or IsBroker)
      GrDateArray[DLGR_DATEKIND_DEPO2] = GrDateArray[DLGR_DATEKIND_DEPO2] - DL_NDaysBeforeInsertDepoDraft;

      if(GrDateArray[DLGR_DATEKIND_DEPO2] < GrDateArray[DLGR_DATEKIND_DELADATE])
        GrDateArray[DLGR_DATEKIND_DEPO2] = GrDateArray[DLGR_DATEKIND_DELADATE];
      end; 
    end;
  end;
  //14. Дата закрытия сделки
  if (isRepo == true)
    GrDateArray[DLGR_DATEKIND_CLOSEDEAL] = max(GrDateArray[DLGR_DATEKIND_PAYMENT2], GrDateArray[DLGR_DATEKIND_DELIVERY2]);
  else
    GrDateArray[DLGR_DATEKIND_CLOSEDEAL] = max(GrDateArray[DLGR_DATEKIND_PAYMENT], GrDateArray[DLGR_DATEKIND_DELIVERY]);
  end;

  if(tick.rec.BofficeKind == DL_RETIREMENT_OWN)
     // 15. Дата переноса на счета "к исполнению"
     if((IsRET_ADDINCOME) AND (tick.rec.flag1 == UNSET_CHAR))
       GrDateArray[DLGR_DATEKIND_MOVEACC]   = leg.rec.Start;
     else
     GrDateArray[DLGR_DATEKIND_MOVEACC]   = leg.rec.MoveDate;
     end;
     GrDateArray[DLGR_DATEKIND_DELIVERY]  = leg.rec.Expiry;
     GrDateArray[DLGR_DATEKIND_PAYMENT]   = GrDateArray[DLGR_DATEKIND_MOVEACC];
     GrDateArray[DLGR_DATEKIND_DEPO]      = GrDateArray[DLGR_DATEKIND_DELIVERY];
     
     if(ДатаЯвляетсяПереходящейПогашОЭБ(leg.rec.Expiry, calendId) == true)
       GrDateArray[DLGR_DATEKIND_TRANSFACPAYAGENT] = leg.rec.Expiry;
     else
       GrDateArray[DLGR_DATEKIND_TRANSFACPAYAGENT] = GrDateArray[DLGR_DATEKIND_MOVEACC];
     end;
     
  end;

  if(tick.rec.BofficeKind == DL_SECURITYDOC) //сделки
    //Для собственной сделки из 1ч 
    if((tick.rec.ClientID == -1) and ((not IsRepo) and (not IsLoan)))

      //2.1 Учет предв. Затрат
      if(tick.rec.PreOutlay > 0 )
        SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_PREVCOSTACC);
      end;

      if( not(tick.rec.IsPFI and (tick.rec.OriginID == CodeFor("Ю"))) )
         //4.1 Постановка на внебаланс
         if( (GrDateArray[DLGR_DATEKIND_DELADATE] != GrDateArray[DLGR_DATEKIND_DELIVERY]) and (GrDateArray[DLGR_DATEKIND_DELADATE] != GrDateArray[DLGR_DATEKIND_AVANCE]) and (GrDateArray[DLGR_DATEKIND_DELADATE] != GrDateArray[DLGR_DATEKIND_PAYMENT]) )
   
            SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_OFFBALANCE);
            if( tick.rec.IsPFI )//перенос по срокам только для ПФИ
        
               SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_RECOGNITION_PFI, GrDateArray[DLGR_DATEKIND_DELADATE]);
              //6.1 Перенос по срокам
               if( (СделкаНеРанееТретьегоДня == ВидСрочностиСделкиПоПервойЧасти(tick.rec.BofficeKind, leg.rec.BitMask, GrDateArray[DLGR_DATEKIND_DELADATE], GrDateArray[DLGR_DATEKIND_PAYMENT], GrDateArray[DLGR_DATEKIND_DELIVERY], tick.rec.IsPFI)) )
   
                 stat = 0;
                 AccTr = TRecHandler("account.dbt");
                 AccOb = TRecHandler("account.dbt");
                 Dсд = GrDateArray[DLGR_DATEKIND_DELADATE];
                 Dо = GrDateArray[DLGR_DATEKIND_PAYMENT]; 
                 Dп = GrDateArray[DLGR_DATEKIND_DELIVERY];
                 Dпс = date(0,0,0);
                 FD = SPFirstDoc(LEG_KIND_DL_TICK, tick.rec.DealID);
   
                 //определить начальную дату переноса по срокам
                 if(not FD.OpenAccount( "+Форвард, дрейф", true, false, FIROLE_FIREQ, AccTr, Dсд ))
                   stat = 1;
                 end;
   
                 if((not stat) and (not FD.OpenAccount( "-Форвард, дрейф", true, false, FIROLE_FICOM, AccOb, Dсд )))
                   stat = 1;
                 end;
   
                 if(not stat)
                   Dпс = GetNextTransferDate(FD, (Dо-Dсд), (Dп-Dсд), Dсд);
                    if((Dпс != date(0,0,0)) and (Dпс < min(Dо,Dп)))
                     GrDateArray[DLGR_DATEKIND_TRANSFER] = Dпс;
                     SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_TRANSFER, GrDateArray[DLGR_DATEKIND_TRANSFER]); 
                   end;
                 end;
              end;
           end;
         end;
      end;

      //7.1 Постановка на баланс
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_BALANCE);

    end;

    //3.1 Оплата комиссий
    //Выполняется массово сразу для всех сделок вне этой ф-ции

    if(IsOutExchange)
         
      if( leg.rec.Start != Date(0,0,0) )
        //8.1 Оплата аванса/задатка
        SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_PAYAVANCE);
      end;

      if( leg2.rec.Start != Date(0,0,0) )
        //13.1 Оплата аванса по 2-й части РЕПО
        SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_PAYAVANCE2);
      end;

    end;

    //9 Оплата
    if(IsLoan == false)
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_PAYMENT);
    end;

    /*Поставка*/
    /*Учет поставки*/
    /*Поручение депо*/
    if( IsBasket )
       var v_RQID1:integer = 0;
       /*Если РЕПО на корзину*/
       cmd = DL_RSDCommand();
       query = " SELECT TICK_ENS.T_FIID " +
               "   FROM DDL_TICK_ENS_DBT TICK_ENS, DDLRQ_DBT DLRQ " +
               "  WHERE TICK_ENS.T_DEALID = ? " +
               "    AND DLRQ.T_DOCKIND = ? " +
               "    AND DLRQ.T_DOCID = TICK_ENS.T_DEALID " +
               "    AND DLRQ.T_TYPE = ? " +
               "    AND DLRQ.T_FIID = TICK_ENS.T_FIID " +
               "    AND DLRQ.T_DEALPART = 1 "+
               " group by TICK_ENS.T_FIID";
       cmd.AddParam(tick.rec.DealID);
       cmd.AddParam(tick.rec.BofficeKind);
       cmd.AddParam(DLRQ_TYPE_DELIVERY);
      
       DataSet = cmd.Execute(query);
       while( DataSet.moveNext() )
          v_RQID1 = GetRqID(tick, DLRQ_TYPE_DELIVERY, SQL_ConvTypeInteger(DataSet.FIID), 1);
          SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DELIVERY, NULL, SQL_ConvTypeInteger(DataSet.FIID), v_RQID1 );
          SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_RECDELIVERY, NULL, SQL_ConvTypeInteger(DataSet.FIID), v_RQID1 );
          if(DL_WorkWithDepositary or isKDU)
            SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DEPODRAFT, NULL, SQL_ConvTypeInteger(DataSet.FIID), v_RQID1);
          end;
       end;
    else
      /*Иначе*/
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DELIVERY, NULL, tick.rec.PFI, AvrRqID);

      if(tick.rec.ClientID == -1)
        SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_RECDELIVERY, NULL, tick.rec.PFI, AvrRqID);
      else
        if((IsRepo == false) and (IsLoan == false) and (ClientNeedNUTX == true))
          SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_RECDELFORTAX, NULL, tick.rec.PFI, AvrRqID);
        end;
      end;

      if(DL_WorkWithDepositary or isKDU)
        SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DEPODRAFT, NULL, tick.rec.PFI, AvrRqID);
      end;
    end;
    
    //12 Поставка для клиента-контрагента
    if(tick.rec.IsPartyClient == SET_CHAR)
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DELIVERYCONTR);

      if(DL_WorkWithDepositary)
        SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DEPODRAFTCONTR);
      end;
    end;

    if((IsRepo == true) or (IsLoan == true))
      
      //14 Оплата по 2-й части РЕПО
      if(IsLoan == false)
        SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_PAYMENT2);
      end;
       
      /*Поставка по 2-й части РЕПО*/
      /*Учет поставки по 2-й части РЕПО*/
      /*Поручение депо по 2-й части РЕПО*/
      if( IsBasket )
         var v_RQID2:integer = 0;

         /*15 Если РЕПО на корзину*/
         cmd = DL_RSDCommand();
         query = " SELECT TICK_ENS.T_FIID " +
                 "   FROM DDL_TICK_ENS_DBT TICK_ENS, DDLRQ_DBT DLRQ " +
                 "  WHERE TICK_ENS.T_DEALID = ? " +
                 "    AND DLRQ.T_DOCKIND = ? " +
                 "    AND DLRQ.T_DOCID = TICK_ENS.T_DEALID " +
                 "    AND DLRQ.T_TYPE = ? " +
                 "    AND DLRQ.T_FIID = TICK_ENS.T_FIID " +
                 "    AND DLRQ.T_DEALPART = 2 "+
                 " group by TICK_ENS.T_FIID";
         cmd.AddParam(tick.rec.DealID);
         cmd.AddParam(tick.rec.BofficeKind);
         cmd.AddParam(DLRQ_TYPE_DELIVERY);
        
         DataSet = cmd.Execute(query);
         while( DataSet.moveNext() )
            v_RQID2 = GetRqID(tick, DLRQ_TYPE_DELIVERY, SQL_ConvTypeInteger(DataSet.FIID), 2);
            SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DELIVERY2, NULL, SQL_ConvTypeInteger(DataSet.FIID), v_RQID2 );
            SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_RECDELIVERY2, NULL, SQL_ConvTypeInteger(DataSet.FIID), v_RQID2 );
            if(DL_WorkWithDepositary or isKDU)
              SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DEPODRAFT2, NULL, SQL_ConvTypeInteger(DataSet.FIID), v_RQID2);
            end;
         end;
      else
        /*16 Иначе*/
        SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DELIVERY2, NULL, tick.rec.PFI, AvrRqID2);
     
        if(tick.rec.ClientID == -1)
          SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_RECDELIVERY2, NULL, tick.rec.PFI, AvrRqID2);
        end;

        if(DL_WorkWithDepositary or isKDU)
          SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DEPODRAFT2, NULL, tick.rec.PFI, AvrRqID2);
        end;
      end;

      //17 Поставка для клиента-контрагента по 2ч РЕПО
      if(tick.rec.IsPartyClient == SET_CHAR)
        SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DELIVERYCONTR2);
      end;

      if((DL_WorkWithDepositary) and (tick.rec.IsPartyClient == SET_CHAR))
        SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DEPODRAFTCONTR2);
      end;
      
      if(IsLoan == true)
        if(leg.rec.ReturnIncome != 0)
          v_RQID2 = GetRqID(tick, DLRQ_TYPE_INCREPO, leg.rec.CFI, 2);
          SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_PAYPC, NULL, tick.rec.PFI, v_RQID2 );        
        end;
      end;
    end;   
  elif( tick.rec.BofficeKind == DL_RETIREMENT ) /*погашения*/
    /*Оплата*/
    if((tick.rec.Flag1 == UNSET_CHAR) AND (tick.rec.Flag4 == UNSET_CHAR) AND (tick.rec.CarryWrt == SET_CHAR )) //если не установлены признак "Биржа" и "Брокер" и "Проводка зачисления средств"
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_PAYMENT, date(31,12,9999));
    else
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_PAYMENT);
    end;

    /*Поставка*/
    if( (tick.rec.ClientID == -1) or IsRET_ISSUE )
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DELIVERY);
    end;

    /*Учет поставки*/
    if(tick.rec.ClientID == -1)
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_RECDELIVERY);  
    end;

    /*Поручение депо*/
    if( (not IsRET_COUPON) and (not IsRET_PARTLY) )
       if(DL_WorkWithDepositary)
         SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DEPODRAFT);
       end;
    end;
  elif( tick.rec.BofficeKind == DL_RETIREMENT_OWN ) /*погашения СЭБ*/
    if( Not IsRET_ADDINCOME)
    /*Исполнение СЭБ*/
    SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_EXECOWN);
    SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_MOVEACC);
    elif (tick.rec.flag1 == UNSET_CHAR)
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_EXECOWN);
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_MOVEACC);
    end;

    /*Поставка СЭБ*/
    SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DELIVERYOWN);
    /*Учет поставки*/
    //if(IsRET_ISSUE)
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_RECDELIVERY);
    //end;

    /*Учет погашения купона СЭБ*/
    //if(IsRET_COUPON)
    //  SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_COUPOWN);
    //end;

    /*Перевод средств платежному агенту*/
    //SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_TRANSFACPAYAGENT);

    /*Поручение депо*/
    if(IsRET_ISSUE and DL_WorkWithDepositary)
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DEPODRAFT);
    end;

  elif(tick.rec.BofficeKind == DL_AVRWRT) /*зачисление/списание*/
    if(Index(SC_GetSysTypes(tick.rec.DealType), "T") <= 0)
      /*Поставка*/
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DELIVERY);
    end;

    if((tick.rec.ClientID == -1) and (IsKSU))
      /*Учёт поставки*/
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_RECDELIVERY);
    end;

    /*Поручение депо*/
    if(((DL_WorkWithDepositary) or (isKDU)) and (tick.rec.OriginID != CodeFor("Z")))
     if(tick.rec.ClientID == -1)
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DEPODRAFT);
     end;
    end;
  elif(tick.rec.BofficeKind == DL_AVRWRTOWN) /*зачисление/списание СЭБ*/
    /*Поставка СЭБ*/
    SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DELIVERYOWN);

    /*Поручение депо*/
    if(((DL_WorkWithDepositary) or (isKDU)) and (tick.rec.OriginID != CodeFor("Z")))
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DEPODRAFT);
    end;

  elif(tick.rec.BofficeKind == DL_SECUROWN) //сделки СЭБ
    //4.1 Постановка на внебаланс
    if( (GrDateArray[DLGR_DATEKIND_DELADATE] != GrDateArray[DLGR_DATEKIND_DELIVERY]) and (GrDateArray[DLGR_DATEKIND_DELADATE] != GrDateArray[DLGR_DATEKIND_PAYMENT]) )

      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_OFFBALANCE);

      //6.1 Перенос по срокам
      if( СделкаНеРанееТретьегоДня == ВидСрочностиСделкиПоПервойЧасти(tick.rec.BofficeKind, leg.rec.BitMask, GrDateArray[DLGR_DATEKIND_DELADATE], GrDateArray[DLGR_DATEKIND_PAYMENT], GrDateArray[DLGR_DATEKIND_DELIVERY], tick.rec.IsPFI) )

         stat = 0;
         AccTr = TRecHandler("account.dbt");
         AccOb = TRecHandler("account.dbt");
         Dсд = GrDateArray[DLGR_DATEKIND_DELADATE];
         Dо = GrDateArray[DLGR_DATEKIND_PAYMENT];
         Dп = GrDateArray[DLGR_DATEKIND_DELIVERY];
         Dпс = date(0,0,0);
         FD = SPFirstDoc(LEG_KIND_DL_TICK, tick.rec.DealID);

         //определить начальную дату переноса по срокам
         if(not FD.OpenAccount( "+Форвард, дрейф", true, false, FIROLE_FIREQ, AccTr, Dсд ))
           stat = 1;
         end;

         if((not stat) and (not FD.OpenAccount( "-Форвард, дрейф", true, false, FIROLE_FICOM, AccOb, Dсд )))
           stat = 1;
         end;

         if(not stat)
           Dпс = GetNextTransferDate(FD, (Dо-Dсд), (Dп-Dсд), Dсд);
           if(Dпс != date(0,0,0))
             GrDateArray[DLGR_DATEKIND_TRANSFER] = Dпс;
             SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_TRANSFER, GrDateArray[DLGR_DATEKIND_TRANSFER]); 
           end;
         end;
      end;

      if(IsOutExchange == false)
        SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_OUTOFFBALANCEOWN);
      end;
    end;

    /*Поставка*/
    /*Учет поставки*/
    SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DELIVERYOWN, NULL, tick.rec.PFI, AvrRqID);
    SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_RECDELIVERY, NULL, tick.rec.PFI, AvrRqID);

    
    //Признание ПФИ ВО
    if(not IsBuy)
      fin.Clear();
      fin.rec.FIID = tick.rec.PFI;
      if((fin.GetEQ()) and (FI_AvrKindsEQ(FIKIND_AVOIRISS, AVOIRISSKIND_BOND_BIO, fin.rec.AvoirKind) != 0))
        SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_RECPFIOWN, GrDateArray[DLGR_DATEKIND_DELIVERY]);
      end;
    end;

    if(IsOutExchange)
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_PAYMENT);
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_BALANCE);
    end;

    /*Поручение депо*/
   if(((DL_WorkWithDepositary) or (isKDU)) and (tick.rec.OriginID != CodeFor("Z")))
      SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_DEPODRAFT, NULL, tick.rec.PFI, AvrRqID);
    end;
  end;  

  if( (IsOutExchange == true) and РАБОТА_С_РЕПОЗИТАРИЕМ() and (tick.rec.ParentID == 0) )
    var BulkMessage = null, EasyMessage = null;
      
    if( not getMessages(tick.rec.DealType, FIKIND_AVOIRISS, tick.rec.PFI, tick.rec.isPFI, tick.rec.isSpot, IsRepo, @BulkMessage, @EasyMessage) )
      var settingValue = -1;
      GetRegistryValue("РЕПОЗИТАРИЙ\\БАЗА ФОРМ. СООБЩ. В РЕПОЗИТАРИЙ", V_INTEGER, settingValue, null);
      if( ( settingValue == 1 ) or //сделка банка и клиентов
         (( settingValue == 0 ) and ( tick.rec.ClientID > 0 )) or //сделка с клиентом
         (( settingValue == 0 ) and ( tick.rec.IsPartyClient == SET_CHAR ) and ( tick.rec.ClientID == -1 )) or //сделка с контрагентом
         (( settingValue == 2 ) and ( tick.rec.ClientID == -1 )) //сделка банка без клиента либо с контрагентом
        )
        var NeedReport = true;
        //SetCategory(tick, OBJGROUP_REPOSREPORTING, OBJATTR_REPOSREPORTING_YES, null);
                                                                     
        if( BulkMessage and ( tick.rec.isPartyClient == SET_CHAR ) and ( not isMemberRepository(tick.rec.PartyID) ) and ( not hasGenAgr(tick.rec.PartyID) ) )
          var payDate1 = GrDateArray[DLGR_DATEKIND_PAYMENT],
              payDate2 = GrDateArray[DLGR_DATEKIND_PAYMENT2],
              closeDate = GrDateArray[DLGR_DATEKIND_CLOSEDEAL],
              makeDealDate = tick.rec.DealDate;
          
          if( ( payDate1 == makeDealDate ) or ( payDate1 == DL_GetBankDateAfterWorkDayByCalendar(makeDealDate, 1, calendId) ) )
            if( ( payDate2 == makeDealDate ) or ( payDate2 == DL_GetBankDateAfterWorkDayByCalendar(makeDealDate, 1, calendId) ) or ( payDate2 == DL_GetBankDateAfterWorkDayByCalendar(makeDealDate, 2, calendId) ) )
              if( ( closeDate == makeDealDate ) or ( closeDate == DL_GetBankDateAfterWorkDayByCalendar(makeDealDate, 1, calendId) ) or ( closeDate == DL_GetBankDateAfterWorkDayByCalendar(makeDealDate, 2, calendId) ) )             
                SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_BULKREPORT);
                NeedReport = false;
                РепозБуф(tick,GA,BulkMessage,settingValue,IsRepo);
              end;
            end;
          end;
	  	 
        end;

        var irdoc = TBFile("ir_grdoc");
        irdoc.rec.DealID = tick.rec.DealID;
        irdoc.rec.DealKind = 101;
        var isInitRecurring = irdoc.getEQ;
         
        if( ( NeedReport ) and ( EasyMessage ) )
          var requestTimeout = 0;
          GetRegistryValue("РЕПОЗИТАРИЙ\\СРОК ОЖИД.ЗАПР. ПРИ КОМБ.ПОДТВ.", V_INTEGER, requestTimeout, null);
          if (GA.rec.WAITINGPERIOD > 0)
            requestTimeout = GA.rec.WAITINGPERIOD;
          end;
          if( isPendingRequestDeal(GA) and ( requestTimeout > 0 ) )
            if(isInitRecurring)
              SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_MAKEDEAL_CORRECTION, {curdate});
            else
              SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_MAKEDEALWTHREQUEST, getGrPlanDate(DLGR_TEMPL_MAKEDEALWTHREQUEST, requestTimeout, calendId));
            end;

            if(tick.rec.AutoClose == UNSET_CHAR)
              SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_CLOSECONTRWTHREQUEST, getGrPlanDate(DLGR_TEMPL_CLOSECONTRWTHREQUEST, requestTimeout, calendId));
            end;
          else
            if(isInitRecurring)
              SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_MAKEDEAL_CORRECTION, {curdate});
            else
              SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_MAKEDEAL);
            end;

            if(tick.rec.AutoClose == UNSET_CHAR)
              SetGrDealByBuff(tick, leg, leg2, DLGR_TEMPL_CLOSECONTR);
            end;
          end;
          РепозБуф(tick,GA,EasyMessage,settingValue,IsRepo);         
        end;
      end;       
    end;     
  end;

end;

macro SetTemplDateArrVal(pTemplNum,dateKind)
  TemplDateArr[pTemplNum] = dateKind; //кешируем вид даты для шаблона
end;

/*Процедура создания графика по сделке в транзакции начала операции*/
macro InsertAllGrDeal()
  ArrGrDeal.Insert();
  ArrRepozDeal.Insert();
  TemplDateArr.size=0;
end;

/*Процедура, вызываемая при отказе от сделки/части сделки*/
macro SetRejectGrDeal(pDocKind:integer, pDocID:integer, pFIID:integer, pDate:date, pSkipGrDeal, pIsBack )
  var SkipGrDeal = false;
  var IsBack = false;
  var TemplNum = 0;
  var cmd, DataSet;

  if(ValType(pSkipGrDeal) == V_BOOL)
    SkipGrDeal = pSkipGrDeal;
  end;

  if(ValType(IsBack) == V_BOOL)
    IsBack = pIsBack;
  end;

  if(SkipGrDeal != true)
    TemplNum = DLGR_TEMPL_REJECT;
    if(IsBack == true)
      TemplNum = DLGR_TEMPL_REJECT2;
    end;
    
    if(DL_InsertGrDeal(pDocKind, pDocID, TemplNum, pDate, time(), pFIID) != 0)
      return 1;
    end;
  end;

  cmd = DL_RSDCommand(  " select grdeal.t_TemplNum, grdeal.t_FIID, acc.t_AccNum "
                      + "   from ddlgrdeal_dbt grdeal, ddlgracc_dbt acc "
                      + "  where grdeal.t_DocKind = ? "
                      + "    and grdeal.t_DocID = ? "
                      + "    and acc.t_GrDealID = grdeal.t_ID "
                      + "    and acc.t_State = ? "
                     );
  cmd.AddParam(pDocKind);
  cmd.AddParam(pDocID);
  cmd.AddParam(DLGRACC_STATE_PLAN);

  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    if(DL_UpdateGrDealAcc(pDocKind, pDocID, DataSet.FIID, DataSet.TemplNum, DataSet.AccNum, date(0,0,0), DLGRACC_STATE_NOTNEED) != 0)
      return 1;
    end;
  end;

  return 0;
end;

//Функция поиска плановой даты переноса по срокам без учета коррекции настройками
macro GetRealPlanTransferDate(FD)
  var DataSet, PlanDate = date(0,0,0);
  record PeriodTr(mcperiod);
  record PeriodOb(mcperiod);

  var Dсд = FD.tick.rec.DealDate, Dо = FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.PlanDate, Dп = FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.PlanDate;

  if((СделкаНеРанееТретьегоДня == ВидСрочностиСделкиПоПервойЧасти(FD.tick.rec.BofficeKind, FD.dl_leg.rec.BitMask, Dсд, Dо, Dп, FD.tick.rec.IsPFI)) and (not(FD.tick.rec.IsPFI and (FD.tick.rec.OriginID == CodeFor("Ю")))) )

    var stat = 0;
    var AccTr = TRecHandler("account.dbt"), AccOb = TRecHandler("account.dbt");
    
    var Dпс = date(0,0,0);

    //определить начальную дату переноса по срокам
    if(not FD.OpenAccount( "+Форвард, дрейф", true, false, FIROLE_FIREQ, AccTr, Dсд ))
      stat = 1;
    end;

    if((not stat) and (not FD.OpenAccount( "-Форвард, дрейф", true, false, FIROLE_FICOM, AccOb, Dсд )))
      stat = 1;
    end;

     
    if(not stat)
      Dо = GetDateAfterWorkDays(Dо, 0);
      Dп = GetDateAfterWorkDays(Dп, 0);

      PlanDate = GetNextPlanTransferDate(FD, (Dо-Dсд), (Dп-Dсд), Dсд, PeriodTr, PeriodOb);
    end;
    
  end;

  return PlanDate;
end;


//Процедура, вызываемая для учёта возможного переноса по срокам
macro SetTransferGrDeal(FD, CalcDate, pFIID:integer)
  var N = GetNRecsTransfer(FD.tick.rec.BOfficeKind, Fd.tick.rec.DealID);
  var Dпс = date(0,0,0);
  var Tr=false, Ob=false, K1=0, K2=0;
  var AccTrOld = TRecHandler("account.dbt"), AccObOld = TRecHandler("account.dbt");
  var Dпл = date(0,0,0);
  var Dсд = FD.tick.rec.DealDate;
  var MinDate = date(0,0,0);
 
  if(ValType(CalcDate) == V_UNDEF)
    CalcDate = date(0,0,0);
  end;
  
  Dпс = GetPlanTransferDate(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID);
  if(Dпс == date(0,0,0))
    Dпс = Dсд;
  end;
  if(Dпс > date(0,0,0))
    if(Dпс != Dсд)
      //надо искать не от даты запланированного переноса, а от даты последнего выполненного
      var Dold = GetFactTransferDate(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID);
      if(Dold == date(0,0,0))
        Dold = Dсд;
      end;

      GetNeedNextTransfer(FD, Dold, @Tr, @Ob, @K1, @K2, AccTrOld, AccObOld);
      if((K1 <= 0) OR (K2 <= 0))
        Dпл = date(0,0,0);
      else
        Dпл = GetNextTransferDate(FD, K1, K2, Dold);
      end;

      if(Dпл < Dпс) //В условиях изменившихся дат такое возможно
        Dпл = date(0,0,0);
      end;
    else
      if( FD.БылоДосрочноеИсполнение() )
         Dпл = date(0,0,0);
      else
         if(FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.State == DLRQ_STATE_DELAYED)
           K1 = 0;
         else
           K1 = FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.PlanDate - Dсд;
         end;

         if(FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.State == DLRQ_STATE_DELAYED)
           K2 = 0;
         else
           K2 = FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.PlanDate - Dсд;
         end;

         Dпл = GetNextTransferDate(FD, K1, K2, Dсд);
      end;
    end;
    
    if((CalcDate != date(0,0,0)) and (Dпл != date(0,0,0)) and (Dпл <= CalcDate))
      msgbox("Невозможно выполнение расчетов по сделке, ожидается выполнение переноса по срокам");
      return 1;
    end;

    if((Dпл != date(0,0,0)) and (N == 0))
      if(DL_InsertGrDeal(FD.tick.rec.BOfficeKind, Fd.tick.rec.DealID, DLGR_TEMPL_TRANSFER, Dпл, time(), pFIID) != 0)
        return 1;
      end;
    elif((Dпл != date(0,0,0)) and (N == 1))
      if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, pFIID,  DLGR_TEMPL_TRANSFER, DLGR_ACCKIND_ACCOUNTING, Dпл, DLGRACC_STATE_PLAN) != 0 )
        return 1;
      end;
    elif((Dпл == date(0,0,0)) and (N == 1))
      if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, pFIID, DLGR_TEMPL_TRANSFER, DLGR_ACCKIND_ACCOUNTING, date(0,0,0), DLGRACC_STATE_NOTNEED) != 0 )
        return 1;
      end;
    end;
  end;

  if(FD.ExistsRQ(DLRQ_TYPE_PAYMENT))
    MinDate = min(MinDate, FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.PlanDate);
  end;

  if(FD.ExistsRQ(DLRQ_TYPE_DELIVERY))
    MinDate = min(MinDate, FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.PlanDate);
  end;

  if(FD.ExistsRQ(DLRQ_TYPE_AVANCE))
    MinDate = min(MinDate, FD.GetRQ(DLRQ_TYPE_AVANCE).rec.PlanDate);
  end;

  if(FD.ExistsRQ(DLRQ_TYPE_DEPOSIT))
    MinDate = min(MinDate, FD.GetRQ(DLRQ_TYPE_DEPOSIT).rec.PlanDate);
  end;

  if(MinDate > date(0,0,0))
    if(DL_SetDateGrDeal(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_BALANCE, MinDate) != 0)
      return 1;
    end;
  end;

  return 0;
end;

private macro MinDate( date1, date2, date3 )
  if( date3 != date(0,0,0) )
    return min( min( date1, date2 ), date3 );
  else
    return min( date1, date2 );
  end;
end;

/*Процедура, вызываемая при изменении условий сделки*/
macro SetChangeGrDeal(FD, FD2, pDate, GUID)

  var depodat:date = date(0,0,0);
  var avancedat:date = date(0,0,0);   

  if(ValType(GUID) == V_UNDEF)
    GUID = "";
  end; 

  if( DL_InsertGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_TEMPL_CHANGE, pDate, time(0,0,0)/*для сортировки*/, FD.tick.rec.PFI, GUID ) != 0)
    return 1;
  end;

  /*сделка из 1 части или внебиржевая РЕПО*/
  if( (FD.ExistBack == false) or (IsOUTEXCHANGE(FD.Group)) )

    if(FD.ExistAvance())
      avancedat = FD.GetRQ(DLRQ_TYPE_AVANCE).rec.PlanDate;
    end;
    if(FD.ExistDeposit())
      avancedat = FD.GetRQ(DLRQ_TYPE_DEPOSIT).rec.PlanDate;
    end;
  
    if( FD.RQIsChanged(DLRQ_TYPE_DELIVERY) )
       if( DL_SetDateGrDeal(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_DELIVERY, FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.PlanDate) != 0 )
         return 1;
       end;

       depodat = FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.PlanDate;
       if( IsOUTEXCHANGE(FD.Group) or IsBROKER(FD.Group) )
          depodat = depodat - DL_NDaysBeforeInsertDepoDraft;
          if( depodat < FD.tick.rec.DealDate )
             depodat = FD.tick.rec.DealDate;
          end; 
       end;
       if( DL_SetDateGrDeal(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_DEPO, depodat) != 0 )
          return 1;
       end;


    end;

    if((FD.RQIsChanged(DLRQ_TYPE_PAYMENT)) and 
       (DL_SetDateGrDeal(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_PAYMENT, FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.PlanDate) != 0)
      )
      return 1;
    end;

    if((FD.ExistAvance()) and 
       (FD.RQIsChanged(DLRQ_TYPE_AVANCE)) and 
       (DL_SetDateGrDeal(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_AVANCE, FD.GetRQ(DLRQ_TYPE_AVANCE).rec.PlanDate) != 0)
      )
      return 1;
    end;

    if((FD.ExistDeposit()) and 
       (FD.RQIsChanged(DLRQ_TYPE_DEPOSIT)) and 
       (DL_SetDateGrDeal(FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_AVANCE, FD.GetRQ(DLRQ_TYPE_DEPOSIT).rec.PlanDate) != 0)
      )
      return 1;
    end;
    if( DL_SetDateGrDeal( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, DLGR_DATEKIND_BALANCE, 
          MinDate( FD.GetRQ(DLRQ_TYPE_DELIVERY).rec.PlanDate, FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.PlanDate, avancedat ) ) != 0)      
      return 1;
    end;
  end; 

  if(FD.ExistBack == false)
    if(SetTransferGrDeal(FD, null, FD.tick.rec.PFI) != 0)
      return 1;
    end;
    
    PeriodIDTrGl = 0;
    PeriodIDObGl = 0;
  end; 

  if( (FD.ExistBack == true) and (FD2 != NULL) )
    if( FD2.RQIsChanged(DLRQ_TYPE_DELIVERY) )
       if( DL_SetDateGrDeal(FD2.tick.rec.BOfficeKind, FD2.tick.rec.DealID, DLGR_DATEKIND_DELIVERY2, FD2.GetRQ(DLRQ_TYPE_DELIVERY).rec.PlanDate) != 0 )
          return 1;
       end;

       depodat = FD2.GetRQ(DLRQ_TYPE_DELIVERY).rec.PlanDate;
       if( IsOUTEXCHANGE(FD2.Group) or IsBROKER(FD2.Group) )
          depodat = depodat - DL_NDaysBeforeInsertDepoDraft;
          if( depodat < FD2.tick.rec.DealDate )
             depodat = FD2.tick.rec.DealDate;
          end; 
       end;
       if( DL_SetDateGrDeal(FD2.tick.rec.BOfficeKind, FD2.tick.rec.DealID, DLGR_DATEKIND_DEPO2, depodat) != 0 )
          return 1;
       end;
    end;

    if((FD2.RQIsChanged(DLRQ_TYPE_PAYMENT)) and 
       (DL_SetDateGrDeal(FD2.tick.rec.BOfficeKind, FD2.tick.rec.DealID, DLGR_DATEKIND_PAYMENT2, FD2.GetRQ(DLRQ_TYPE_PAYMENT).rec.PlanDate) != 0)
      )
      return 1;
    end;

    if((FD2.ExistAvance()) and 
       (FD2.RQIsChanged(DLRQ_TYPE_AVANCE)) and 
       (DL_SetDateGrDeal(FD2.tick.rec.BOfficeKind, FD2.tick.rec.DealID, DLGR_DATEKIND_AVANCE, FD2.GetRQ(DLRQ_TYPE_AVANCE).rec.PlanDate) != 0)
      )
      return 1;
    end;
    
    if((FD2.ExistDeposit()) and 
       (FD2.RQIsChanged(DLRQ_TYPE_DEPOSIT)) and 
       (DL_SetDateGrDeal(FD2.tick.rec.BOfficeKind, FD2.tick.rec.DealID, DLGR_DATEKIND_AVANCE, FD2.GetRQ(DLRQ_TYPE_DEPOSIT).rec.PlanDate) != 0)
      )
      return 1;
    end;
  end;

  return 0;
end;


private var SpChangeDlTick    = TRecHandler("dl_tick.dbt");      /*условия сделки*/
private var SpChangeDlLeg     = TRecHandler("dl_leg.dbt");        /*условия первой части сделки*/
private var SpChangeDlLegBack = TRecHandler("dl_leg.dbt");    /*условия второй части сделки*/

private var SaveDlTick        = TRecHandler("dl_tick.dbt");       /*условия сделоки*/
private var SaveDlLeg         = TRecHandler("dl_leg.dbt");         /*условия первой части*/
private var SaveDlLegBack     = TRecHandler("dl_leg.dbt");     /*условия второй части*/

macro GetSpChangeDlTick()
   return SpChangeDlTick;
end;

macro GetSpChangeDlLeg()
   return SpChangeDlLeg;
end;

macro GetSpChangeDlLegBack()
   return SpChangeDlLegBack;
end;

macro GetSaveDlTick()
   return SaveDlTick;
end;

macro GetSaveDlLeg()
   return SaveDlLeg;
end;

macro GetSaveDlLegBack()
   return SaveDlLegBack;
end;

//восстановим условия сделки по данным в записи истории изменений
macro FillFromSPTKCHNG( Ch, Tick, Leg, Leg2 )
   Tick.rec.Instance            = Ch.OldInstance;
   Tick.rec.ChangeDate          = Ch.OldChangeDate;
   Tick.rec.ChangeKind          = Ch.OldChangeKind;
   Leg.rec.CFI                  = Ch.OldCFI1;    
   Leg.rec.Price                = Ch.OldPrice1;
   Leg.rec.Point                = Ch.OldPoint1;                                                  
   Leg.rec.IncomeRate           = Ch.OldIncomeRate;    
   Leg.rec.Cost                 = Ch.OldCost1;    
   Leg.rec.TotalCost            = Ch.OldTotalCost1;     
   Leg.rec.NKD                  = Ch.OldNKD1;    
   Tick.rec.FixSum              = Ch.OldFixSum;
   Leg.rec.PFI                  = Ch.OldPFI1;      
   Leg.rec.Principal            = Ch.OldPrincipal;
   Leg.rec.PayFIID              = Ch.OldPayFIID1;
   Leg.rec.Formula              = Ch.OldFormula1;
   Tick.rec.PartyID             = Ch.OldPartyID; 
   Leg.rec.Start                = Ch.OldStart1; 
   Leg.rec.Maturity             = Ch.OldMaturity1;    
   Leg.rec.Expiry               = Ch.OldExpiry1;      
   Leg.rec.MaturityIsPrincipal  = Ch.OldMaturityIsPrincipal1;
   Tick.rec.PreOutlay           = Ch.OldPreOutlay;
   Leg.rec.PayRegTax            = Ch.OldPayRegTax1;                      
   Leg.rec.Registrar            = Ch.OldRegistrar1;                      
   Tick.rec.DealTime            = Ch.OldDealTime;
   Leg.rec.Basis                = Ch.OldBasis;      
   Leg.rec.RejectDate           = Ch.OldRejectDate1;                     
   Leg.rec.ReturnIncome         = Ch.OldReturnIncome;                    
   Tick.rec.Flag3               = Ch.Flag3;
   Leg.rec.DeliveringFIID       = Ch.OldDeliveringFIID;              
   Tick.rec.PreOutlayFIID       = Ch.OldPreOutlayFIID;   
   Leg.rec.StartDiff            = Ch.OldStartDiff1;                    
   Leg.rec.PrincipalDiff        = Ch.OldPrincipalDiff1;                
   Leg.rec.Diff                 = Ch.OldDiff1;                         
   Tick.rec.Flag5               = Ch.OldFlag5;           
   Tick.rec.ReturnIncomeKind    = Ch.OldReturnIncomeKind;          
   Tick.rec.PortfolioID         = Ch.OldPortfolio;
   Tick.rec.SumPay              = Ch.OldSumPay;
   
   if( Leg2 != NULL )
     Leg2.rec.CFI                 = Ch.OldCFI2;
     Leg2.rec.Price               = Ch.OldPrice2;
     Leg2.rec.Point               = Ch.OldPoint2;
     Leg2.rec.IncomeRate          = Ch.OldIncomeRate;
     Leg2.rec.Cost                = Ch.OldCost2;
     Leg2.rec.TotalCost           = Ch.OldTotalCost2;
     Leg2.rec.NKD                 = Ch.OldNKD2;   
     Leg2.rec.PFI                 = Ch.OldPFI2;
     Leg2.rec.Principal           = Ch.OldPrincipal2;
     Leg2.rec.PayFIID             = Ch.OldPayFIID2;
     Leg2.rec.Formula             = Ch.OldFormula2;
     Leg2.rec.Start               = Ch.OldStart2;
     Leg2.rec.Maturity            = Ch.OldMaturity2;
     Leg2.rec.Expiry              = Ch.OldExpiry2;
     Leg2.rec.MaturityIsPrincipal = Ch.OldMaturityIsPrincipal2;
     Leg2.rec.PayRegTax           = Ch.OldPayRegTax2;
     Leg2.rec.Registrar           = Ch.OldRegistrar2;
     Leg2.rec.RejectDate          = Ch.OldRejectDate2;
     Leg2.rec.ReturnIncome        = Ch.OldReturnIncome;
     Leg2.rec.StartDiff           = Ch.OldStartDiff2;      
     Leg2.rec.PrincipalDiff       = Ch.OldPrincipalDiff2;  
     Leg2.rec.Diff                = Ch.OldDiff2;           
   end;

end;

macro LoadNewDealData(FD, FD2, Instance:integer)
  var query, cmd, DataSet;

  query =   " select ch.* "
          + "   from dsptkchng_dbt ch "
          + "  where ch.t_DealID = ? "
          + "    and ch.t_OldInstance = ? ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(FD.tick.rec.DealID);
  cmd.AddParam(Instance + 1);

  DataSet = cmd.Execute();

  //грузим из текущего состояния
  copy( SpChangeDlTick,    FD.tick );
  copy( SpChangeDlLeg,     FD.dl_leg );
  if(FD2 != NULL)
    copy( SpChangeDlLegBack, FD2.dl_leg );
  end;

  if(DataSet.moveNext)
     //грузим из изменений
     FillFromSPTKCHNG( DataSet, SpChangeDlTick, SpChangeDlLeg, SpChangeDlLegBack );
  end;

end;

//загрузить состояния ДО и ПОСЛЕ изменения
macro LoadSpTkChangeData(FD, FD2, GrDealID)
  var query, cmd, DataSet;

  ClearRecord(SpChangeDlTick   );
  ClearRecord(SpChangeDlLeg    );
  ClearRecord(SpChangeDlLegBack);
  ClearRecord(SaveDlTick       );
  ClearRecord(SaveDlLeg        );
  ClearRecord(SaveDlLegBack    );

  query =   " select ch.* "
          + "   from dsptkchng_dbt ch, ddlgracc_dbt gracc "
          + "  where gracc.t_GrDealID = ? "
          + "    and gracc.t_AccNum = " + DLGR_ACCKIND_BACKOFFICE
          + "    and ch.t_ID_Operation = gracc.t_ID_Operation "
          + "    and ch.t_ID_Step = gracc.t_ID_Step "
          + "    and ch.t_DealID = ? "
          + "   order by ch.t_OldInstance DESC";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(GrDealID);
  cmd.AddParam(FD.tick.rec.DealID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext)
    copy(SaveDlTick,    FD.tick);
    copy(SaveDlLeg,     FD.dl_leg);
    if( FD2 != NULL ) 
      copy(SaveDlLegBack, FD2.dl_leg);
    end;
    FillFromSPTKCHNG( DataSet, SaveDlTick, SaveDlLeg, SaveDlLegBack );

    LoadNewDealData(FD, FD2, DataSet.OldInstance);
  else
    //этого быть не может...
  end;   

end;

macro DL_GetPlanDateGrDeal( pDocKind, pDocID, pTemplNum, pFIID, pTime:@time ):date
  var RetVal:date = date(0,0,0);
  var cmd, DataSet;

  if( pTime != NULL )
     pTime = time(0);
  end;

  cmd = DL_RSDCommand( " select grdeal.t_PlanDate, grdeal.t_PlanTime " +
                       "   from ddlgrdeal_dbt grdeal" +
                       "  where grdeal.t_DocKind  = ? " +
                       "    and grdeal.t_DocID    = ? " +
                       "    and grdeal.t_TemplNum = ? "
                       "    and grdeal.t_FIID     = ? "
                     );
  cmd.AddParam(pDocKind);
  cmd.AddParam(pDocID);
  cmd.AddParam(pTemplNum);
  cmd.AddParam(pFIID);

  DataSet = cmd.Execute();
  if( DataSet.moveNext() )
     RetVal = SQL_ConvTypeDate(DataSet.PlanDate);
     if( pTime != NULL )
        pTime = SQL_ConvTypeTime(DataSet.PlanTime);
     end;
  end;

  return RetVal;
end;

macro ПолучитьПараметрыПоСтрокеГрафика( GrDealID:integer, GrDealPrm:TRecHandler ):integer
  var query, cmd, DataSet;

  query = " select grdealprm.* " +
          "   from ddlgrdealprm_dbt grdealprm " +
          "  where grdealprm.t_GrDealID = ? ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(GrDealID);

  DataSet = cmd.Execute();
  if( DataSet.moveNext() )
     DataSet.GetRecord().CopyTo(GrDealPrm.rec);
  else
     return 1;
  end;

  return 0;
end;
