/**                                                                                                             
 @file InvestRegisterDebt_Report.mac                                                                                           
 @brief Формирование отчета "Реестр задолженностей по инвестиционному консультированию"  

 #menu 
  - БО ЦБ\Отчеты\РСХБ\Инвест.советник\Реестр задолженностей по инвестиционному консультированию                                                                                                                                                                                                                          
                                                                                                                
 # tag                                                                                                          
 - functional_block:Отчетность_Внутренняя                                                                             
 - code_type:Report                                                                                                
                                                                                                                
 # changeLog                                                                                                    
 |date       |author         |tasks               |note                                                         
 |-----------|---------------|--------------------|-------------------------------------------------------------
 |2024.01.24 |Жиц М.В.       |DEF-60393           |BIQ-14887. При отсутвии клиентских задолжностей по оплате комиссии в отчет выводятся переменные                                                                                                                                                 
 |2024.01.17 |Жиц М.В.       |BIQ-14887           |Создание макроса для отчета                                                                                                                                                 
*/ 

import "InvestRegisterDebt_Form.mac", "dldlngfun.mac", "dl_report_log.mac", SFInter;

PRIVATE CONST COMMISS_INVEST_ADVISER = "ИнвестСоветник"; 

/**
 @brief Класс отчета Реестр задолженностей по инвестиционному консультированию
 @param[in] RepDate дата отчета
*/                    
PRIVATE CLASS (DL_CReportTemplate) SP_InvestRegisterDebt_Report(RepDate:date)
  private var m_RepDate = RepDate;
  private var m_IsDataExist = false;
  private var m_CurLog = DL_ReportLog(Report_InvestRegisterDebt, Date(), Time(), m_RepDate); 

  /**
   @brief Переопределение функции задания способа печати: всегда в Excel
  */                    
  private macro PrintMode():integer
    return DL_OUTREPORT_EXCEL;
  end;

  /**
   @brief Переопределение функции создания главной страницы
   @param[in] NumCopy номер страницы
  */                    
  private macro BeginReport(NumCopy:integer)
    Dl_CallInsertStat(PrintMode());

    SetActiveSheet("Лист1");
  end;

  /**
   @brief Печать шапки отчета
  */                    
  private macro PrintReportHead()
    PrintFormatString("", 
                      "N1_RepDate", m_RepDate,
                      "N1_SysDate", string(Date():f) + " " + Time()
                     );

    RegisterTable("N1_MainTable", "", 
                  "N1_ContrNumber", 
                  "N1_EKK", 
                  "N1_ClientName", 
                  "N1_ClientPhone", 
                  "N1_InvestBeginDate",
                  "N1_InvestEndDate", 
                  "N1_ComisBeginDate", 
                  "N1_ComisEndDate", 
                  "N1_ComisDebtSum", 
                  "N1_ComisStatus",
                  "N1_BrokerAcc", 
                  "N1_BrokerRest", 
                  "N1_RequireAcc", 
                  "N1_RequireRest", 
                  "N1_OverdueAcc",
                  "N1_OverdueRest"
                 );
  end;

  /**
   @brief Печать итоговой строки по договору
   @param[in] TotalDebtSum итоговая задолженность по договору
  */                    
  private macro PrintItogLine(TotalDebtSum:money) 
    SetCurrentLineFont(null, true); //Жирный шрифт
    PrintTableLine("N1_ContrNumber",  "ИТОГО",      null,
                   "N1_ComisDebtSum", TotalDebtSum, null 
                  );
    SetCurrentLineFont(null, false); //Выключаем жирный шрифт
  end;      

  /**
   @brief Печать строки отчета
   @param[in] dataSet структура параметров из запроса в рамках одной задолженности
  */                    
  private macro PrintLine(dataSet)
    PrintTableLine("N1_ContrNumber",     dataSet.ContrNumber,           null,
                   "N1_EKK",             dataSet.EKK,                   null,
                   "N1_ClientName",      dataSet.ShortName,             null, 
                   "N1_ClientPhone",     dataSet.ClientPhone,           null,
                   "N1_InvestBeginDate", date(dataSet.InvestBeginDate), null, 
                   "N1_InvestEndDate",   date(dataSet.InvestEndDate),   null,
                   "N1_ComisBeginDate",  date(dataSet.DatePeriodBegin), null, 
                   "N1_ComisEndDate",    date(dataSet.DatePeriodEnd),   null,
                   "N1_ComisDebtSum",    dataSet.DebtSum,               null, 
                   "N1_ComisStatus",     dataSet.Status,                null,
                   "N1_BrokerAcc",       dataSet.BrokerAcc,             null, 
                   "N1_BrokerRest",      IIF(dataSet.BrokerAcc != "", dataSet.BrokerRest, ""),   null,
                   "N1_RequireAcc",      dataSet.RequireAcc,            null, 
                   "N1_RequireRest",     IIF(dataSet.RequireAcc != "", dataSet.RequireRest, ""), null,
                   "N1_OverdueAcc",      dataSet.OverdueAcc,            null, 
                   "N1_OverdueRest",     IIF(dataSet.OverdueAcc != "", dataSet.OverdueRest, ""), null
                  );
  end;      

  /**
   @brief Сбор данных и циклическая печать отчета
  */                    
  private macro ExecuteReport()
    var query, cmd, dataSet;
    var cnt = 0, i = 0;
    var CurDlContrID:integer = 0; //Идентификатор печатаемого договора
    var TotalDebtSum:money = 0.0; //Итоговая задолженность по договору

    query = "  WITH d AS (SELECT ? t_RepDate FROM dual) "
          + "SELECT t_DlContrID, t_ContrNumber, t_EKK, t_ShortName, t_ClientPhone, t_InvestBeginDate, t_InvestEndDate, t_DatePeriodBegin, t_DatePeriodEnd, t_DebtSum, t_Status, "
          + "       t_BrokerAcc, t_RequireAcc, t_OverdueAcc, "
          + "       CASE WHEN t_BrokerAcc = CHR(1) THEN 0 " 
          + "            ELSE NVL(RSI_RSB_ACCOUNT.RESTAC(t_BrokerAcc, 0, t_RepDate, 1, 0), 0) "
          + "        END t_BrokerRest, "
          + "       CASE WHEN t_RequireAcc = CHR(1) THEN 0 "
          + "            ELSE NVL(RSI_RSB_ACCOUNT.RESTAC(t_RequireAcc, 0, t_RepDate, 1, 0), 0) "
          + "        END t_RequireRest, "
          + "       CASE WHEN t_OverdueAcc = CHR(1) THEN 0 "
          + "            ELSE NVL(RSI_RSB_ACCOUNT.RESTAC(t_OverdueAcc, 0, t_RepDate, 1, 0), 0) "
          + "        END t_OverdueRest "
          + "  FROM (SELECT dl.t_DlContrID, d.t_RepDate, sf_root.t_Number t_ContrNumber, "
          + "               RSB_SECUR.SC_GetDlObjCodeOnDate("+OBJTYPE_BROKERCONTR_DL+", 1/*Единый краткий код*/, dl.t_DlContrID, d.t_RepDate) t_EKK, "
          + "               NVL((SELECT pt.t_ShortName "
          + "                      FROM dparty_dbt pt "
          + "                     WHERE pt.t_PartyID = sf.t_PartyID), CHR(1)) t_ShortName, "
          + "               NVL((SELECT LISTAGG(t_Value, ', ') WITHIN GROUP (ORDER BY t_Value) "
          + "                      FROM (SELECT con.t_Value "
          + "                              FROM dcontact_dbt con "	
          + "                             WHERE con.t_PartyID = sf.t_PartyID "
          + "                               AND con.t_ContactKind = " + CNTK_PHONE
          + "                               AND con.t_IsMain = 'X' "
          + "                               AND con.t_ContactType = (SELECT t_ContactType "
          + "                                                          FROM (SELECT con1.t_ContactType "
          + "                                                                  FROM dcontact_dbt con1 "	
          + "                                                                 WHERE con1.t_PartyID = con.t_PartyID "
          + "                                                                   AND con1.t_ContactKind = con.t_ContactKind "
          + "                                                                   AND con1.t_IsMain = con.t_IsMain "
          + "                                                              ORDER BY CASE WHEN con1.t_ContactType = 1004 THEN 0 ELSE con1.t_ContactType END ASC) "
          + "                                                         WHERE ROWNUM = 1) " 
          + "                          GROUP BY con.t_Value) "
          + "                   ), CHR(1)) t_ClientPhone, "
          + "               serv.t_BeginDate t_InvestBeginDate, "
          + "               CASE WHEN serv.t_EndDate > d.t_RepDate THEN to_date('01.01.0001','dd.mm.yyyy') " 
          + "                    ELSE serv.t_EndDate " 
          + "                END t_InvestEndDate, "
          + "               sfdef.t_DatePeriodBegin, sfdef.t_DatePeriodEnd, sfdef.t_Sum t_DebtSum, " 
          + "               NVL((SELECT ll.t_Code " 
          + "                     FROM dllvalues_dbt ll "
          + "                    WHERE ll.t_List = " + OBJTYPE_COMPAYSTATUS 
          + "                      AND ll.t_Element = sfdef.t_Status), CHR(1)) t_Status, "
          + "               NVL((SELECT sfsi.t_Account "
          + "                      FROM dsfsi_dbt sfsi " 
          + "                     WHERE sfsi.t_ObjectType = " + OBJTYPE_SFDEFCOM
          + "                       AND sfsi.t_ObjectID = sfdef.t_ID "  
          + "                       AND sfsi.t_DebetCredit = 0), CHR(1)) t_BrokerAcc, "
          + "               NVL((SELECT sfsi.t_Account "
          + "                      FROM dsfsi_dbt sfsi "
          + "                     WHERE sfsi.t_ObjectType = " + OBJTYPE_SFDEFCOM
          + "                       AND sfsi.t_ObjectID = sfdef.t_ID "
          + "                       AND sfsi.t_DebetCredit = 2), CHR(1)) t_RequireAcc, "
          + "               NVL((SELECT DISTINCT acc.t_Account "
          + "                      FROM dmccateg_dbt cat, dmcaccdoc_dbt mc, daccount_dbt acc "
          + "                     WHERE cat.t_LevelType = 1 "
          + "                       AND cat.t_Code = 'Треб. с н.с. брок' "
          + "                       AND mc.t_CatID = cat.t_ID "
          + "                       AND mc.t_Owner = sf.t_PartyID "
          + "                       AND mc.t_ClientContrID = sf.t_ID "
          + "                       AND (mc.t_DisablingDate = to_date('01.01.0001','dd.mm.yyyy') OR mc.t_DisablingDate > d.t_RepDate) "
          + "                       AND acc.t_Account = mc.t_Account "
          + "                       AND acc.t_Chapter = mc.t_Chapter "
          + "                       AND acc.t_Code_Currency = mc.t_Currency "
          + "                       AND acc.t_Code_Currency = " + NATCUR 
          + "                       AND acc.t_Open_Date <= d.t_RepDate "
          + "                       AND (acc.t_Close_Date = to_date('01.01.0001','dd.mm.yyyy') OR acc.t_Close_Date >= d.t_RepDate) "
          + "                   ), CHR(1)) t_OverdueAcc "
          + "          FROM d, dsfdef_dbt sfdef, dsfcomiss_dbt com, dsfcontr_dbt sf, ddlcontrmp_dbt mp, ddlcontr_dbt dl, dsfcontr_dbt sf_root, ddlcontrserv_dbt serv " 
          + "         WHERE sfdef.t_Status = 10 /*начислена*/ "
          + "           AND LOWER(com.t_Code) = LOWER('"+COMMISS_INVEST_ADVISER+"') " 
          + "           AND com.t_FeeType = " + SF_FEE_TYPE_PERIOD 
          + "           AND sfdef.t_CommNumber = com.t_Number "
          + "           AND sfdef.t_FeeType = com.t_FeeType " 
          + "           AND sfdef.t_DatePeriodBegin <= d.t_RepDate "
          + "           AND sf.t_ID = sfdef.t_SfContrID "
          + "           AND mp.t_SfContrID = sf.t_ID "
          + "           AND mp.t_DlContrID = dl.t_DlContrID "
          + "           AND dl.t_SfContrID = sf_root.t_ID "
          + "           AND serv.t_ServKind = " + DLCONTR_SERVKIND_INVESTADVICE
          + "           AND serv.t_DlContrID = dl.t_DlContrID "
          + "           AND serv.t_BeginDate < sfdef.t_DatePeriodBegin "
          + "           AND (serv.t_EndDate = to_date('01.01.0001','dd.mm.yyyy') OR serv.t_EndDate >= sfdef.t_DatePeriodBegin) "
          + "       ) "
          + " ORDER BY t_ContrNumber ASC, t_DatePeriodEnd ASC ";

    cmd = DL_RSDCommand(query);
    cmd.AddParam(m_RepDate);
    
    cnt = cmd.GetCount();

    if(cnt > 0)
      InitProgress(cnt, "Отчет \"Реестр задолженностей по инвестиционному консультированию\"", "Формирование отчета");

      m_CurLog.AddLogRow("Поиск задолженностей по инвестиционному консультированию на отчетную дату " + m_RepDate, 
                         "Задолженности по инвестиционному консультированию найдены"); 
      
      dataSet = cmd.Execute();
      PrintReportHead(); //Печать шапки отчета
      m_IsDataExist = true;

      while(dataSet.MoveNext())
        if(CurDlContrID != dataSet.DlContrID) //Новый договор
          if(CurDlContrID != 0)
            PrintItogLine(TotalDebtSum); //Печатать итоговой строки
            TotalDebtSum = $0; //Обнуление итоговой задолженности
          end;
          CurDlContrID = dataSet.DlContrID; //Обновление идентификатора печатаемого договора
        end;

        PrintLine(dataSet); //Печать строки отчета
        TotalDebtSum = TotalDebtSum + dataSet.DebtSum; //Суммируем задолженность по договору

        i = i + 1;
        UseProgress(i);
      end;

      PrintItogLine(TotalDebtSum); //Печать последней итоговой строки

      EndTable();
      RemProgress;
    else
      m_CurLog.AddLogRow("Поиск задолженностей по инвестиционному консультированию на отчетную дату " + m_RepDate, 
                         "Задолженностей по инвестиционному консультированию НЕ найдено"); 
      m_IsDataExist = false;
    end;

    m_CurLog.EndLoggingTable(); 
    
  OnError(e)
    m_CurLog.ErrorHistoryRec(); 
    msgbox(cmd.GetErrorString(e));
    RemProgress;
  end;

  /**
   @brief Переопределение функции создания отчета
  */                    
  private macro Create()
    m_CurLog.BeginLogging(); 
    ExecuteReport();
    m_CurLog.EndLogging(); 
  end;

  /**
   @brief Проверка наличия данных в выборке
   @return true, если есть данные для выпуска отчета
   @return false, если данных нет
  */                    
  macro CReport_DataExist()
    return m_IsDataExist;
  end;

  initDL_CReportTemplate(null, "Report_InvestRegisterDebt.xlsx");  
END;

/**
 @brief Вызов панели и самого формирования отчета "Реестр задолженностей по инвестиционному консультированию"
*/                    
MACRO ReportRunEx()
  var Form = InvestRegisterDebt_Panel(); //Обработчик панели
  var Report = null; //Объект отчета

  while(Form.Run())
    Report = SP_InvestRegisterDebt_Report(Form.m_RepDate); 
    Report.Run();
    if(not Report.CReport_DataExist())
      msgbox("Нет данных для выпуска отчета");
    end;
  end;
END;

ReportRunEx();
Exit(1);
