/*
$Name:             ws_txdeals.mac
$Module:           АРНУ
$Description:      WEB. АРНУ. Макрос сервисов скроллинга Налоговые лоты
*/

import "secinter.mac";
import "ws_common.mac";
import "ws_datafilter.mac";
import "ws_orderbygen.mac";
import "sprepfun.mac";
import "ws_avrkinds.mac";

// класс CTxDeal для заполнения скроллинга "Сделки для налогового учета"
class CTxDeal
  var Id:Integer;             // ID сделки/операции
  var DealID:Integer;         // Идентификатор сделки
  var DealCode:String;        // Внутренний № сделки
  var DealCodeTS:String;      // Внешний № сделки
  var Type:Integer;           // Вид
  var TypeName:String;        // Имя вида
  var IsTechDeal:Bool;        // Техническая сделка
  var FIID:Integer;           // ФИ
  var RootAvrKind:Integer;    // Корневой вид цб
  var FIName:String;          // Выпуск ц/б
  var ISIN:String;            // ISIN
  var Amount:Money;           // Количество
  var SortDate:Date;          // Дата сортировки
  var Date1:Date;             // Дата поставки
  var Date2:Date;             // Дата поставки по 2 части
  var DealDate:Date;          // Дата заключения
  var DealTime:Time;          // Время заключения
  var VirtualType:Integer;    // Тип
  var VirtualTypeName:String; // Имя типа
  var Price:Money;            // Цена
  var PriceFIID:Integer;      // Валюта цены
  var PriceCur:String;        // Наименование валюты цены
  var TotalCost:Money;        // Стоимость
  var Origin:Integer;         // Происхожение
  var OriginName:String;      // Имя происхожение
end;

// класс фильтра на скроллинг "Сделки для налогового учета"
class CTxDealFilter
  var IDList = TArray();        // Список идентификаторов налоговых лотов
  var DealCodeList = TArray();  // Список кодов налоговых лотов
  var IsStrictCodeEq : Bool;    // Признак строгого соответсвия коду
  var NeedExternalCode : Bool;  // Использовать внешний код сделок для интерфейса
  var TaxGroupList = TArray();  // Список идентификаторов групп налогового учета
  var AvoirKindList = TArray(); // Список идентификаторов видов ц/б
  var FIIDList = TArray();      // Список идентификаторов ц/б
  var InvstKindList = TArray(); // Список идентификаторов видов размещения
end;

macro TxDealRunError( err, stat:integer )
  WSRunError( "ws_txdeals.mac", err, stat );
end;

private macro GetSQLAvoirKindCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond : String = GetSQLAvoirKindConditionEx( "fininstr", "t_AvoirKind", condition, value, type );
  var valueCount : Integer = 0;

  if( strSQLCond == "" )
    strSQLCond = " RSB_FIInstr.FI_AvrKindsEQ( " + FIKIND_AVOIRISS + ", 0, fininstr.t_AvoirKind ) = 1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLTaxGroupCondition(
  condition : Integer
 ,value//:TArray of <type>
 ,type : Integer
) : String
  var strSQLCond : String = GetSQLLlValuesConditionEx( "avoiriss", "t_TaxGroup", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " avoiriss.t_TaxGroup = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLFICondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond : String = GetSQLFIConditionEx( "sctxdeal", "t_FIID", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxdeal.t_FIID = " + ALLFININSTR + " ";
  end;

  return strSQLCond;
end;

private macro GetSQLTypeCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond : String = GetSQLNameAlgConditionEx( "sctxdeal", "t_Type", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxdeal.t_Type = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLVirtualTypeCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond : String = GetSQLNameAlgConditionEx( "sctxdeal", "t_VirtualType", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxdeal.t_VirtualType = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLIsTechDealCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond : String = "";

  if( ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_Bool )
  and ( ValType( value[0] ) == V_BOOL )
  and ( value[0] == true ) )
    strSQLCond = " RSB_SECUR.RSI_IsTechDeal(sctxdeal.t_DealID, RsbSessionData.curdate) > 0 ";
  else
    strSQLCond = " RSB_SECUR.RSI_IsTechDeal(sctxdeal.t_DealID, RsbSessionData.curdate) = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLOriginNameCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond : String = GetSQLNameAlgConditionEx( "sctxdeal", "t_Origin", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxdeal.t_Origin = 0 ";
  end;

  return strSQLCond;
end;

private macro GetSQLPriceFICondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond : String = GetSQLFIConditionEx( "sctxdeal", "t_PriceFIID", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " sctxdeal.t_PriceFIID = " + ALLFININSTR + " ";
  end;

  return strSQLCond;
end;

private macro GetFilterAddWhere(
  Filter//: CTxDealFilter
) : String
  var valItem = null;
  var fc = null;
  var fcia = TArray();
  var fp = null;

  if( ValType( Filter ) != V_UNDEF )
    if( ( ValType( Filter.IDList ) != V_UNDEF ) and ( Filter.IDList.Size > 0 ) )
      fc = FilterConditionItem();
      fc.Id = 0;
      fc.Condition = FilterCondition_Equal;
      fc.Value = Filter.IDList;
      fc.IsNot = false;
      fc.ValueType = FilterParameterType_Int;
      fcia[fcia.Size] = fc;
    end;

    if( ( ValType( Filter.DealCodeList ) != V_UNDEF ) and ( Filter.DealCodeList.Size > 0 ) )
      fc = FilterConditionItem();
      fc.Id = 1;
      fc.Condition = FilterCondition_Equal;
      if( ( ValType( Filter.IsStrictCodeEq ) != V_UNDEF ) and Filter.IsStrictCodeEq )
        fc.Value = Filter.DealCodeList;
      else
        fc.Value = TArray();
        while( fc.Value.Size < Filter.DealCodeList.Size )
          valItem = "%" + Filter.DealCodeList[fc.Value.Size] + "%";
          fc.Value[fc.Value.Size] = valItem;
        end;
      end;
      fc.IsNot = false;
      fc.ValueType = FilterParameterType_String;
      fcia[fcia.Size] = fc;
    end;

    if( ( ValType( Filter.TaxGroupList ) != V_UNDEF ) and ( Filter.TaxGroupList.Size > 0 ) )
      fc = FilterConditionItem();
      fc.Id = 2;
      fc.Condition = FilterCondition_Equal;
      fc.Value = Filter.TaxGroupList;
      fc.IsNot = false;
      fc.ValueType = FilterParameterType_Int;
      fcia[fcia.Size] = fc;
    end;

    if( ( ValType( Filter.AvoirKindList ) != V_UNDEF ) and ( Filter.AvoirKindList.Size > 0 ) )
      fc = FilterConditionItem();
      fc.Id = 3;
      fc.Condition = FilterCondition_Equal;
      fc.Value = TArray();
      while( fc.Value.Size < Filter.AvoirKindList.Size )
        valItem = CTxAvrKinds();
        valItem.FI_Kind = FIKIND_AVOIRISS;
        valItem.AvoirKind = Filter.AvoirKindList[fc.Value.Size];
        fc.Value[fc.Value.Size] = valItem;
      end;
      fc.IsNot = false;
      fc.ValueType = FilterParameterType_UserType;
      fcia[fcia.Size] = fc;
    end;

    if( ( ValType( Filter.FIIDList ) != V_UNDEF ) and ( Filter.FIIDList.Size > 0 ) )
      fc = FilterConditionItem();
      fc.Id = 4;
      fc.Condition = FilterCondition_Equal;
      fc.Value = Filter.FIIDList;
      fc.IsNot = false;
      fc.ValueType = FilterParameterType_Int;
      fcia[fcia.Size] = fc;
    end;

    if( ( ValType( Filter.InvstKindList ) != V_UNDEF ) and ( Filter.InvstKindList.Size > 0 ) )
      fc = FilterConditionItem();
      fc.Id = 5;
      fc.Condition = FilterCondition_Equal;
      fc.Value = Filter.InvstKindList;
      fc.IsNot = false;
      fc.ValueType = FilterParameterType_Int;
      fcia[fcia.Size] = fc;
    end;
  end;

  fp = FilterParser( fcia );

  fp.AddFieldCondition( 0, "sctxdeal", "t_ID" ); // ID сделки
  fp.AddFieldCondition( 1, "sctxdeal", IIF( Filter.NeedExternalCode, "t_DealCodeTS", "t_DealCodeTS" ) ); // Внешний/Внутренний код сделки
  fp.AddFieldCondition( 2, "avoiriss", "t_TaxGroup" ); // Группа налогового учета
  fp.AddUserFieldCondition( 3, @GetSQLAvoirKindCondition ); // Вид ценной бумаги
  fp.AddFieldCondition( 4, "sctxdeal", "t_FIID" ); // Выпуск ценной бумаги
  fp.AddFieldCondition( 5, "sctxdeal", "t_Type" ); // Вид сделки

  //fp.GetFullSQLConditionDebug( "ws_txdeals_filter_debug" );

  return fp.GetFullSQLCondition();
end;

private macro GetTxDealsAddWhereCondition(
  FilterItems//: TArray of FilterCondItem
) : String

  var fp = FilterParser( FilterItems );

  fp.AddFieldCondition( 0, "sctxdeal", "t_DealDate" ); // Дата заключения
  fp.AddFieldCondition( 1, "sctxdeal", "t_Date1" ); // Дата поставки
  fp.AddFieldCondition( 2, "sctxdeal", "t_Date2" ); // Дата поставки по второй части
  fp.AddUserFieldCondition( 3, @GetSQLAvoirKindCondition ); // Вид ценной бумаги
  fp.AddUserFieldCondition( 4, @GetSQLTaxGroupCondition ); // Группа налогового учета
  fp.AddUserFieldCondition( 5, @GetSQLFICondition ); // Выпуск ценной бумаги
  fp.AddUserFieldCondition( 6, @GetSQLTypeCondition ); // Вид сделки
  fp.AddUserFieldCondition( 7, @GetSQLVirtualTypeCondition ); // Тип сделки
  fp.AddFieldCondition( 8, "sctxdeal", "t_ID" ); // ID сделки
  fp.AddFieldCondition( 9, "sctxdeal", "t_DealCodeTS" ); // Внешний код сделки
  fp.AddUserFieldCondition( 10, @GetSQLIsTechDealCondition ); // Техническая сделка
  fp.AddFieldCondition( 11, "sctxdeal", "t_Amount" ); // Количество
  fp.AddUserFieldCondition( 12, @GetSQLOriginNameCondition ); // Происхождение лота
  fp.AddFieldCondition( 13, "sctxdeal", "t_DealCode" ); // Внутренний код сделки
  fp.AddFieldCondition( 14, "avoiriss", "t_ISIN" ); // ISIN
  fp.AddFieldCondition( 15, "sctxdeal", "t_Price" ); // Цена
  fp.AddUserFieldCondition( 16, @GetSQLPriceFICondition ); // Валюта цены

  //fp.GetFullSQLConditionDebug( "ws_txdeals_filter_debug" );

  return fp.GetFullSQLCondition();
end;

private macro GetDealsSortOrder(
  orderItems//:TArray of OrderItem  // Параметры сортировки
 ,defaultSQLSortOrder:String        // Строка сортировки по умолчанию
):String

  var ordrGen = OrderByGenerator( orderItems, defaultSQLSortOrder, false, true );

  ordrGen.AddObjectSorting( "IsTechDeal", " RSB_SECUR.RSI_IsTechDeal(sctxdeal.t_DealID, RsbSessionData.curdate) ");
  ordrGen.AddObjectSorting( "TypeName", " (SELECT "
                                           + " namealg.t_szNameAlg "
                                       + " FROM "
                                           + " dnamealg_dbt namealg "
                                       + " WHERE "
                                           + " namealg.t_iTypeAlg = " + ALG_SCTX_DEALKIND + " "
                                       + " AND namealg.t_iNumberAlg = sctxdeal.t_Type) " );
  ordrGen.AddObjectSorting( "VirtualTypeName", " (SELECT "
                                                  + " namealg.t_szNameAlg "
                                              + " FROM "
                                                  + " dnamealg_dbt namealg "
                                              + " WHERE "
                                                  + " namealg.t_iTypeAlg = " + ALG_SCTX_DEALTYPE + " "
                                              + " AND namealg.t_iNumberAlg = sctxdeal.t_VirtualType) " );
  ordrGen.AddObjectSorting( "OriginName", " (SELECT "
                                             + " namealg.t_szNameAlg "
                                         + " FROM "
                                             + " dnamealg_dbt namealg "
                                         + " WHERE "
                                             + " namealg.t_iTypeAlg = " + ALG_SCTX_LOTORIGIN + " "
                                         + " AND namealg.t_iNumberAlg = sctxdeal.t_Origin) " );

  //ordrGen.GetFullSQLSortOrder( "ws_scdeals_conv_orderby_debug" );

  return ordrGen.GetFullSQLSortOrder();
end;

private const TypeBuy  = 1;  /*"Покупка", TypeAlg = ALG_SCTX_DEALKIND_FULL*/
private const TypeSale = 2;  /*"Продажа", TypeAlg = ALG_SCTX_DEALKIND_FULL*/
private const FindOther = 3; /*оставшиеся параметры, TypeAlg = ALG_SCTX_DEALKIND_FULL*/

/* Задано ли ограничение на дату поставки по второй части */
private macro IsSetDate2( FilterItems /*: TArray of FilterConditionItem */ /* Значения фильтрации */ ) : Bool
  var stat:integer = -1,
      _IsSetDate2 = false; /* Задана ли дата поставки по второй части ? */

  if( FilterItems != null )
     var i:Integer = 0;
     while( i < FilterItems.Size() )
        if( FilterItems[i].id == 2/*фильтр по "Дата поставки по второй части"*/)
           _IsSetDate2 = true;
           break;
        end;
        i = i + 1;
     end;
  end;

  return _IsSetDate2;

OnError( err )
  TxDealRunError(err, stat);
end;

/* Есть ли в фильтре по "Вид сделки" нужные параметры (покупка, продажа или другие) */
private macro IsExistsParm( FilterItem, isNot:Bool, buy:Integer, sale:Integer, findParm:Integer ) : Bool
  var stat:integer = -1,
      exist:Bool = false;

  if( FilterItem != null )
     var i:Integer = 0;
     while( i < FilterItem.Value.Size() )
        if( not isNot )
           if( findParm == FindOther )
              if( (FilterItem.Value[i].NumberAlg != buy) and (FilterItem.Value[i].NumberAlg != sale) )
                 exist = true;
                 break;
              end;
           elif( findParm == TypeBuy )
              if( FilterItem.Value[i].NumberAlg == TypeBuy )
                 exist = true;
                 break;
              end;
           elif( findParm == TypeSale )
              if( FilterItem.Value[i].NumberAlg == TypeSale )
                 exist = true;
                 break;
              end;
           end;
        else // IsNot = True
           if( findParm == FindOther )
              if( (FilterItem.Value[i].NumberAlg == buy) or (FilterItem.Value[i].NumberAlg == sale) )
                 exist = true;
                 break;
              end;
           elif( findParm == TypeBuy )
              if( IsExistsParm(FilterItem, false, TypeBuy, TypeSale, TypeBuy) )
                 exist = true;
                 break;
              end;
           elif( findParm == TypeSale )
              if( IsExistsParm(FilterItem, false, TypeBuy, TypeSale, TypeSale) )
                 exist = true;
                 break;
              end;
           end;
        end;
        i = i + 1;
     end;
  end;

  return exist;

OnError( err )
  TxDealRunError(err, stat);
end;

/* Если выбрано ограничение на дату поставки по второй части и вид сделки - только */
/* "Покупка" и/или "Продажа", то отфильтрованный скроллинг должен быть пустой */
private macro IsMustBeEmptyTxDeals(
  FilterItems /*: TArray of FilterConditionItem */ /* Значения фильтрации */,
  AdditionalCond:@string /* Если есть ограничение на дату поставки по второй части, и на вид сделки, то нужно исключить вид Покупки и Продажи (у них нет второй части) */
) : Bool
  var stat:integer = -1,
      mustBeEmpty = false;
  if( IsSetDate2( FilterItems ) ) /* Если есть ограничение на дату поставки по второй части */
     if( FilterItems != null )
        var i:Integer = 0;
        while( (i < FilterItems.Size()) and (not mustBeEmpty) )
           if( FilterItems[i].id == 6/*фильтр по "Вид сделки"*/ )
              if( FilterItems[i].Value != null )
                 if( not IsExistsParm( FilterItems[i], FilterItems[i].IsNot, TypeBuy, TypeSale, FindOther ) )
                    mustBeEmpty = true;
                    break;
                 end;
                 if( not FilterItems[i].IsNot )
                    var j:Integer = 0;
                    while( j < FilterItems[i].Value.Size() )
                       if( (FilterItems[i].Value[j].NumberAlg == TypeBuy) or (FilterItems[i].Value[j].NumberAlg == TypeSale) )
                          // нужно исключить параметр фильтра ("Покупка" и/или "Продажа") из выборки
                          FilterItems[i].Value[j].NumberAlg = null;
                       end;
                       j = j + 1;
                    end;
                 else // IsNot = True
                    if( not IsExistsParm( FilterItems[i], true, TypeBuy, TypeSale, TypeBuy ) )
                       AdditionalCond = AdditionalCond + " AND sctxdeal.t_Type != " + string(TypeBuy);
                    end;
                    if( not IsExistsParm( FilterItems[i], true, TypeBuy, TypeSale, TypeSale ) )
                       AdditionalCond = AdditionalCond + " AND sctxdeal.t_Type != " + string(TypeSale);
                    end;
                 end;
              end;
           end;
           i = i + 1;
        end;
     end;
  end;

  return mustBeEmpty;

OnError( err )
  TxDealRunError(err, stat);
end;

/* получить общее количество записей скроллинга "Сделки для налогового учета" */
macro TxGetDealCount(
  Filter /*: CTxDealFilter */ /*Фильтр */
 ,FilterItems /*: TArray of FilterConditionItem */ /* Значения фильтрации */
) : Integer /* Количество записей в скроллинге */

  var stat:integer = -1;
  var Count:integer = 0;
  var AdditionalCond:string = "";

  if( IsMustBeEmptyTxDeals( FilterItems, @AdditionalCond ) )
     Count = 0;
  else
    var Query = " SELECT /*+ PARALLEL */ "
                  + " COUNT(1) AS t_C "
              + " FROM "
                  + " dv_sctxdeal sctxdeal "
                 + " ,davoiriss_dbt avoiriss "
                 + " ,dfininstr_dbt fininstr "
              + " WHERE "
                  + " sctxdeal.t_ID >= 0 "
              + " AND avoiriss.t_FIID = sctxdeal.t_FIID "
              + " AND fininstr.t_FIID = sctxdeal.t_FIID ";

     Query = Query + " AND " + GetFilterAddWhere( Filter );
     Query = Query + " AND " + GetTxDealsAddWhereCondition( FilterItems );
     Query = Query + AdditionalCond;
     var DataSet = TRsbDataSet(Query, RSDVAL_CLIENT, RSDVAL_STATIC);

     if( DataSet.MoveNext() )
        Count = SQL_ConvTypeInteger(DataSet.C);
     end;
  end;

  return Count;

OnError( err )
  TxDealRunError(err, stat);
end;

private var strSelectFields = " sctxdeal.t_ID "
                           + " ,sctxdeal.t_DealID "
                           + " ,sctxdeal.t_DealCode "
                           + " ,sctxdeal.t_DealCodeTS "
                           + " ,sctxdeal.t_FIID "
                           + " ,RSI_RSB_FIINSTR.FI_AvrKindsGetRoot(" + FIKIND_AVOIRISS + ", fininstr.t_AvoirKind) AS t_RootAvrKind "
                           + " ,sctxdeal.t_FIName "
                           + " ,avoiriss.t_ISIN "
                           + " ,sctxdeal.t_Amount "
                           + " ,sctxdeal.t_SortDate "
                           + " ,sctxdeal.t_Date1 "
                           + " ,sctxdeal.t_Date2 "
                           + " ,sctxdeal.t_DealDate "
                           + " ,sctxdeal.t_DealTime "
                           + " ,sctxdeal.t_Price "
                           + " ,sctxdeal.t_PriceFIID "
                           + " ,sctxdeal.t_PriceCur "
                           + " ,sctxdeal.t_TotalCost "
                           + " ,sctxdeal.t_Type "
                           + " ,(SELECT "
                                 + " namealg.t_szNameAlg "
                             + " FROM "
                                 + " dnamealg_dbt namealg "
                             + " WHERE "
                                 + " namealg.t_iTypeAlg = " + ALG_SCTX_DEALKIND + " "
                             + " AND namealg.t_iNumberAlg = sctxdeal.t_Type) AS t_TypeName "
                           + " ,sctxdeal.t_VirtualType "
                           + " ,RSB_SECUR.RSI_IsTechDeal(sctxdeal.t_DealID, RsbSessionData.curdate) as t_IsTechDeal "
                           + " ,(SELECT "
                                 + " namealg.t_szNameAlg "
                             + " FROM "
                                 + " dnamealg_dbt namealg "
                             + " WHERE "
                                 + " namealg.t_iTypeAlg = " + ALG_SCTX_DEALTYPE + " "
                             + " AND namealg.t_iNumberAlg = sctxdeal.t_VirtualType) AS t_VirtualTypeName "
                           + " ,sctxdeal.t_Origin "
                           + " ,(SELECT "
                                 + " namealg.t_szNameAlg "
                             + " FROM "
                                 + " dnamealg_dbt namealg "
                             + " WHERE "
                                 + " namealg.t_iTypeAlg = " + ALG_SCTX_LOTORIGIN + " "
                             + " AND namealg.t_iNumberAlg = sctxdeal.t_Origin) AS t_OriginName ";

private var strFROM = " FROM "
                        + " dv_sctxdeal sctxdeal "
                       + " ,davoiriss_dbt avoiriss "
                       + " ,dfininstr_dbt fininstr ";

private var strWHERE = " WHERE "
                         + " sctxdeal.t_ID >= 0 "
                     + " AND avoiriss.t_FIID = sctxdeal.t_FIID "
                     + " AND fininstr.t_FIID = sctxdeal.t_FIID ";

private macro GetDealSQLQueryBody():String
  return " SELECT " + strSelectFields + strFROM + strWHERE;
end;

/* отбор данных для скроллинга "Сделки для налогового учета" */
macro TxGetDealList(
  Filter/*: CTxDealFilter*/ /* Фильтр */
 ,PageNum : Integer /* Номер страницы */
 ,PageSize : Integer /* Размер страницы */
 ,FilterItems /*: TArray of FilterConditionItem */ /* Значения фильтрации */
 ,OrderItems /*: TArray of OrderItem */ /* Параметры сортировки */
) /*: TArray of CTxDeal */

  var stat:integer = -1;
  var TxDealArray = TArray();
  var TxDeal = null;
  var AdditionalCond:string = "";

  if( PageNum == null )
     RunError("Не задан обязательный параметр функции: номер страницы ");
  end;

  if( PageSize == null )
     RunError("Не задан обязательный параметр функции: размер страницы ");
  end;

  if( not IsMustBeEmptyTxDeals( FilterItems, @AdditionalCond ) )
  var strSELECT:String = "";
  if( (PageSize != null) and (PageSize > 0) )
    strSELECT = " /*+ FIRST_ROWS(" + String(PageSize) + ") */ ";
  end;
  strSELECT = " SELECT " + strSELECT + strSelectFields;

  var strFILTERS = " AND " + GetFilterAddWhere(Filter);

  var strAddWhereCond = " AND " + GetTxDealsAddWhereCondition(FilterItems);

  var strORDERBY = " ORDER BY " + GetDealsSortOrder(OrderItems, "sctxdeal.t_ID");

  var strSUBQUERY = " SELECT /*+ PARALLEL */ sctxdeal.t_ID " + strFROM + strWHERE + strFILTERS + strAddWhereCond + AdditionalCond + strORDERBY;
  strSUBQUERY = " SELECT t2.t_ID FROM ( SELECT ROWNUM t_RowNumber, t1.t_ID FROM ( " + strSUBQUERY + " ) t1 ";
  if( (PageSize != null) and (PageSize > 0) )
    strSUBQUERY = strSUBQUERY + " WHERE ROWNUM <= " + String(PageSize * (PageNum + 1)) + " ) t2 WHERE ";
    if( (PageNum != null) and (PageNum > -1) )
      strSUBQUERY = strSUBQUERY + " t2.t_RowNumber >= " + String( PageSize * PageNum + 1 ) + " ";
    else
      strSUBQUERY = strSUBQUERY + " t2.t_RowNumber >= 1 ";
    end;
  else
    strSUBQUERY = strSUBQUERY + " ) t2 ";
  end;
  strSUBQUERY = " AND sctxdeal.t_ID IN ( " + strSUBQUERY + " ) ";

  var query = strSELECT + strFROM + strWHERE + strSUBQUERY + strFILTERS + strAddWhereCond + AdditionalCond + strORDERBY;

     var DataSet = TRsbDataSet(Query);

     while( DataSet.MoveNext() )
        TxDeal = CTxDeal();

        TxDeal.Id              = SQL_ConvTypeInteger(DataSet.Id);
        TxDeal.DealID          = SQL_ConvTypeInteger(DataSet.DealID);
        TxDeal.DealCode        = SQL_ConvTypeStr(DataSet.DealCode);
        TxDeal.DealCodeTS      = SQL_ConvTypeStr(DataSet.DealCodeTS);
        TxDeal.Type            = SQL_ConvTypeInteger(DataSet.Type);
        TxDeal.TypeName        = SQL_ConvTypeStr(DataSet.TypeName);
        TxDeal.IsTechDeal      = SQL_ConvTypeInteger(DataSet.IsTechDeal) > 0;
        TxDeal.FIID            = SQL_ConvTypeInteger(DataSet.FIID);
        TxDeal.RootAvrKind     = SQL_ConvTypeInteger(DataSet.RootAvrKind);
        TxDeal.FIName          = SQL_ConvTypeStr(DataSet.FIName);
        TxDeal.ISIN            = SQL_ConvTypeStr(DataSet.ISIN);
        TxDeal.Amount          = SQL_ConvTypeSum(DataSet.Amount);
        TxDeal.SortDate        = SQL_ConvTypeDate(DataSet.SortDate);
        TxDeal.Date1           = SQL_ConvTypeDate(DataSet.Date1);
        TxDeal.Date2           = SQL_ConvTypeDate(DataSet.Date2);
        TxDeal.DealDate        = SQL_ConvTypeDate(DataSet.DealDate);
        TxDeal.DealTime        = SQL_ConvTypeTime(DataSet.DealTime);
        TxDeal.VirtualType     = SQL_ConvTypeInteger(DataSet.VirtualType);
        TxDeal.VirtualTypeName = SQL_ConvTypeStr(DataSet.VirtualTypeName);
        TxDeal.Price           = SQL_ConvTypeSum(DataSet.Price);
        TxDeal.PriceFIID       = SQL_ConvTypeInteger(DataSet.PriceFIID);
        TxDeal.PriceCur        = SQL_ConvTypeStr(DataSet.PriceCur);
        TxDeal.TotalCost       = SQL_ConvTypeSum(DataSet.TotalCost);
        TxDeal.Origin          = SQL_ConvTypeInteger(DataSet.Origin);
        TxDeal.OriginName      = SQL_ConvTypeStr(DataSet.OriginName);

        TxDealArray[TxDealArray.Size] = TxDeal;
     end;
  end;

  return TxDealArray;

OnError( err )
  TxDealRunError(err, stat);
end;

// Получение сделки налогового учёта
macro TxGetDealListByID(
  DealIDArray//:TArray of Integer // Массив идентификаторов сделки
)
  var stat:Integer = -1;
  var filter = CTxDealFilter();

  InitError();

  if( ( DealIDArray == null ) or ( DealIDArray.Size <= 0 ) )
     RunError("Не задан обязательный параметр функции: идентификатор сделки");
  end;

  filter.IDList = DealIDArray;

  return TxGetDealList( filter, 0, 0 );

OnError( err )
  TxDealRunError(err, stat);
end;

// Получить список состоящий из RecNumber записей сделок по коду DealCode
macro TxGetDealListByCode(
  DealCode:String
 ,IsStrictEq:Bool
 ,NeedExternalCode:Bool
 ,RecNumber:Integer
)
  var stat:Integer = -1;
  var filter = CTxDealFilter();

  InitError();

  if( ( DealCode == null ) or ( DealCode == "" ) )
    RunError( "Не задан обязательный параметр функции: код сделки" );
  end;

  if( ( RecNumber == null ) or ( RecNumber <= 0 ) )
     RunError("Не задан обязательный параметр функции: количество записей списка");
  end;

  filter.DealCodeList = TArray();
  filter.DealCodeList[filter.DealCodeList.Size] = DealCode;
  filter.IsStrictCodeEq = IsStrictEq;
  filter.NeedExternalCode = NeedExternalCode;

  return TxGetDealList( filter, 0, RecNumber );

OnError( err )
  TxDealRunError( err, stat );
end;
