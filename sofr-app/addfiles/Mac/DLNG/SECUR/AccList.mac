import rsd;
import "DlReport_h.mac";

private const TEMPLATE_NAME = "Report_AccList.xls";
private const TEMPLATE_SHEET_NAME = "Счета";
private const TEMPLATE_HEADER_NAME = "Header";
private const TEMPLATE_TABLE_NAME = "TableMain";
private const XLS_MAX_ROWS = 50000;
private const RSDVAL_FORVARD_ONLY = 0;


var totalCount = 0;
var accSelectSection: String = 
    "SELECT " +
       "acc.t_Account, acc.t_Balance, acc.t_NameAccount, acc.t_Kind_Account, acc.t_Index2, acc.t_Index3, acc.t_Open_Date, acc.t_Close_Date, " +
       "CASE WHEN acc.t_Open_Close = 'З' THEN 'X' ELSE '' END t_IsClosed, " +
       "RSI_RSB_SCRHLP.GetDepartmentName(acc.t_Department) t_DepartmentName, " +
       "RSI_RSB_SCRHLP.GetNodeName(acc.t_Branch) t_NodeName, " +
       "RSI_RSB_SCRHLP.GetFICode(acc.t_Code_Currency) t_CurrencyCode, " +
       "RSI_RSB_SCRHLP.GetFICode(acc.t_CurrencyEq) t_CurrencyEqCode, " +
       "RSI_RSB_SCRHLP.GetFIKind(acc.t_Code_Currency), " +
       "(SELECT party.t_ShortName FROM dparty_dbt party WHERE party.t_PartyID=acc.t_Client) t_ClientShortName, " +
       "(SELECT obchaptr.t_Symbol FROM dobchaptr_dbt obchaptr WHERE obchaptr.t_chapter=acc.t_chapter) t_ChapterCh, " +
       "(SELECT partcode.t_Code FROM dpartcode_dbt partcode WHERE partcode.t_CodeKind=1 AND partcode.t_PartyID = acc.t_Client AND ROWNUM = 1) t_ClientCode, " +
       "NVL(rd.t_Rest, 0) t_RestNat, NVL(rd.t_PlanRest, 0) t_PlanRestNat, " +
       "NVL(rdc.t_Rest, 0) t_RestCur, NVL(rdc.t_PlanRest, 0) t_PlanRestCur, " +
       "NVL((SELECT re.t_Rest FROM dresteqv_dbt re WHERE re.t_AccountId = acc.t_AccountId AND re.t_RestDate = rd.t_RestDate), 0) t_RestEqv, " +
       "CASE WHEN (SELECT count(1) FROM dacclaim_dbt claim, dacclaimstate_dbt acclaimstate " +
                  "WHERE claim.t_Chapter = acc.t_Chapter AND claim.t_Account = acc.t_Account AND claim.t_FIID = acc.t_Code_Currency " +
                     "AND (claim.t_FinishDate = to_date('01.01.0001', 'DD.MM.YYYY') OR claim.t_FinishDate > to_date('" + {curdate} + "', 'DD.MM.YYYY')) " +
                     "AND claim.t_ClaimID = acclaimstate.t_ClaimID " +
                     "AND (acclaimstate.t_State in (1, 2)) " +
                     "AND acclaimstate.t_StateDate = (SELECT max(acst.t_StateDate) " +
                                                     "FROM dacclaimstate_dbt acst " +
                                                     "WHERE acst.t_ClaimID = claim.t_ClaimID AND acst.t_StateDate <= to_date('" + {curdate} + "', 'DD.MM.YYYY'))) " +
       "= 0 THEN '' ELSE 'X' END t_ExistsAccClaim " +
    "FROM daccount_dbt acc " +
         "LEFT JOIN drestdate_dbt rd ON " +
                        "(rd.t_AccountId = acc.t_AccountId " +
                        "AND rd.t_RestCurrency = 0 " +
                        "AND rd.t_RestDate = (SELECT MAX(rd2.t_restdate) FROM drestdate_dbt rd2 WHERE rd2.t_AccountId = acc.t_AccountId AND rd2.t_RestCurrency = 0)) " +
         "LEFT JOIN drestdate_dbt rdc ON " +
                        "(rdc.t_AccountId = acc.t_AccountId " +
                        "AND rdc.t_RestCurrency = acc.t_Code_Currency " +
                        "AND rdc.t_RestDate = (SELECT MAX(rd3.t_restdate) FROM drestdate_dbt rd3 WHERE rd3.t_AccountId = acc.t_AccountId AND rd3.t_RestCurrency = acc.t_Code_Currency)) ";

var accOrderSection: String =
    "ORDER BY acc.t_Chapter, acc.t_Open_Close, acc.t_Sort, acc.t_Department, acc.t_Branch, acc.t_Account, acc.t_AccountId ";
var countSelectSection: String = 
    "SELECT count(1) FROM daccount_dbt acc";
var queryWhereSection: String = "";

private macro GetRecords(query: String): RsdRecordset
  var cmd = RsdCommand(query);
  return RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_FORVARD_ONLY);
end;

private macro GetTotalCount(rs: RsdRecordSet): Integer
  if ((rs != null) and (rs.MoveNext()))
    return Int(rs.value(0));
  end;  
  return 0;
end;

private macro GetWhereSection(chapter: Integer, openClosed: Integer, partyId: Integer, balance: String, mask: String): String
  macro AddCondition(sourceCondition: String, additionalCondition: String): String
    if (StrLen(sourceCondition) > 0)
      return sourceCondition + " AND " + additionalCondition;
    else
      return additionalCondition;
    end;
  end;
  
  var result: String = "";
  
  if (chapter > 0) 
    result = AddCondition(result, "(acc.t_Chapter = " + chapter + ")");
  end;
  
  if (openClosed == 0) // открытые
    result = AddCondition(result, "(acc.t_Open_Close != 'З')");
  elif (openClosed == 135) // закрытые
    result = AddCondition(result, "(acc.t_Open_Close = 'З')");
  end;
  
  if (partyId > 0)
    result = AddCondition(result, "(acc.t_Client = " + partyId + ")");
  end;
  
  if (StrLen(balance) > 0)
    result = AddCondition(result, "(acc.t_Balance = '" + balance + "')");
  end;
  
  if (StrLen(mask) > 0)
    result = AddCondition(result, "(acc.t_Account LIKE '" + mask + "')");
  end;
  
  if (StrLen(result) == 0)
    return "";
  end;
  
  return " WHERE " + result + " ";
end;

private class CAccountInfo(rs: RsdRecordset)
  var DepartmentCode:  String = SQL_ConvTypeStr(rs.value("t_DepartmentName"));
  var BranchCode:      String = SQL_ConvTypeStr(rs.value("t_NodeName"));
  var ChapterCh:       String = SQL_ConvTypeStr(rs.value("t_ChapterCh"));
  var Account:         String = SQL_ConvTypeStr(rs.value("t_Account"));
  var Balance:         String = SQL_ConvTypeStr(rs.value("t_Balance"));
  var NameAccount:     String = SQL_ConvTypeStr(rs.value("t_NameAccount"));
  var Kind_Account:    String = SQL_ConvTypeStr(rs.value("t_Kind_Account"));
  var RestNat:         Money  = SQL_ConvTypeSum(rs.value("t_RestNat"));
  var RestCur:         Money  = SQL_ConvTypeSum(rs.value("t_RestCur"));
  var PlanRestNat:     Money  = SQL_ConvTypeSum(rs.value("t_PlanRestNat"));
  var PlanRestCur:     Money  = SQL_ConvTypeSum(rs.value("t_PlanRestCur"));
  var CurrencyCode:    String = SQL_ConvTypeStr(rs.value("t_CurrencyCode"));
  var Index2:          String = SQL_ConvTypeStr(rs.value("t_Index2"));
  var Index3:          String = SQL_ConvTypeStr(rs.value("t_Index3"));
  var ExistsAcClaim:   String = SQL_ConvTypeStr(rs.value("t_ExistsAccClaim"));
  var Open_Date:       Date   = SQL_ConvTypeDate(rs.value("t_Open_Date"));
  var Close_Date:      Date   = SQL_ConvTypeDate(rs.value("t_Close_Date"));
  var ClientCode:      String = SQL_ConvTypeStr(rs.value("t_ClientCode"));
  var ClientShortName: String = SQL_ConvTypeStr(rs.value("t_ClientShortName"));
  var CurrencyEqCode:  String = SQL_ConvTypeStr(rs.value("t_CurrencyEqCode"));
  var RestEqv:         Money  = SQL_ConvTypeSum(rs.value("t_RestEqv"));
  var IsOpenClose:     String = SQL_ConvTypeStr(rs.value("t_IsClosed"));
onerror (mErr)
  
end; // CAccountInfo

// проверка маски счета - строка mask должна содержать цифры и (при желании) * в начале и/или в конце
private macro CheckMask(mask: String): Bool
  var symbols: TArray;
  var symbol: String;
  var i: Integer;
  var starSymbolCount: Integer = 0;
  
  if (mask == "") 
    return true;
  end;
  
  symbols = StrSplit2(mask, 1, true);
  
  for (i, 0, symbols.Size - 1)
    symbol = symbols(i);
    if ((CodeFor(symbol) < 48) or (CodeFor(symbol) > 57)) // если не цифра
      if ((symbol == "*") and ((i == 0) or (i == symbols.Size - 1))) // '*' в начале или конце
        starSymbolCount = starSymbolCount + 1;
      else
        return false;
      end;
    end;
  end;
  
  if (starSymbolCount == symbols.Size) // введены одни '*'
    return false;
  end;
  
  return true;
end; // CheckMask

private macro GetAccountMask(accountMask: @String, defaultValue: String): Bool
  var input: String = defaultValue;
  var accepted: Bool;
  
  while (not accepted)
    accepted = true;
    if (GetString(input, "Введите маску счета для поиска:|(напр. 40701*, *40701* и т.п.)", 24))
      input = Trim(input);
      if (CheckMask(input))
        accountMask = StrSubst(input, "*", "%");
        if ((Index(accountMask, "%") == 0) and StrLen(accountMask > 0)) //если введены одни цифры
          accountMask = "%" + accountMask + "%"; 
        end;
        return true;
      else
        MsgBox("Неверное значение маски!|Допустим ввод цифр и знака '*' в начале и/или в конце|(напр. 40701*, *40701* и т.п.)");
        accepted = false;
      end;
    end;
  end;
  
  return false;
end; // GetAccountMask

/**
 @brief Конвертировать дату в строковый формат
 @param[in] v_Date Дата, которую нужно конвертировать для вывода в отчёт
 @return           Дата в виде строки
*/
private macro ConvertDateToString(v_Date:date):string
  if(v_Date == zerodate)
    return "00.00.0000";
  else
    return StrSubst(String(v_Date):10," ","0");
  end;
end;

private class (DL_CReportTemplate) CAccountList(templateName: String, recordset: RsdRecordset)
  private var rs;
  
  macro PrintMode(): Integer
    return DL_OUTREPORT_EXCEL;
  end;
  
  private macro BeginReport()
     CreateTotalBook();
  end;

  private macro EndReport()
     SaveTotalBook();
  end;
  
  private macro Create()
    var targetSheetName: String;
    var sheetRowsCount: Integer;
    var sheetCount: Integer;
    var bookRowsCount: Integer;
    var isNewSheet: Bool;
    var isContinue: Bool;
    var acc: CAccountInfo;
        
    SetActiveSheet(TEMPLATE_SHEET_NAME);
    sheetCount = 0;
    bookRowsCount = 0;
    
    isNewSheet = true;
    isContinue = true;
    
    while (isContinue)
      if (isNewSheet)
        sheetCount = sheetCount + 1;
        sheetRowsCount = 0;
        InitProgress(totalCount, "Заполнение файла отчета, лист №" + sheetCount, "Заполнение файла отчета, лист №" + sheetCount);
        UseNewSheetInTotalBook();
        targetSheetName = TEMPLATE_SHEET_NAME + "(" + sheetCount + ")";
        CopyAllSheetInTotalBook(TEMPLATE_SHEET_NAME, false, TEMPLATE_HEADER_NAME, "A1", targetSheetName);
        RegisterTable(TEMPLATE_TABLE_NAME, "", 
                      "N_1", "N_2", "N_3", "N_4", "N_5", "N_6", "N_7", "N_8", "N_9", "N_10", "N_11", "N_12", "N_13", "N_14", "N_15", "N_16", "N_17", "N_18", "N_19");
        isNewSheet = false;
      end;
      
      if (rs.MoveNext()) 
        acc = CAccountInfo(rs);
        sheetRowsCount = sheetRowsCount + 1;
        UseProgress(bookRowsCount = bookRowsCount + 1);
        PrintTableLine("N_1",  acc.DepartmentCode,  null,
                       "N_2",  acc.BranchCode,      null,
                       "N_3",  acc.ChapterCh,       null,
                       "N_4",  acc.Account,         null,
                       "N_5",  acc.Balance,         null,
                       "N_6",  acc.NameAccount,     null,
                       "N_7",  acc.Kind_Account,    null,
                       "N_8",  acc.RestNat,         null,
                       "N_9",  acc.RestCur,         null,
                       "N_10",  acc.PlanRestNat,    null,
                       "N_11", acc.PlanRestCur,     null,
                       "N_12", acc.CurrencyCode,    null,
                       "N_13", ConvertDateToString(acc.Open_Date),  null,
                       "N_14", ConvertDateToString(acc.Close_Date), null,
                       "N_15", acc.ClientCode,      null,
                       "N_16", acc.ClientShortName, null,
                       "N_17", acc.CurrencyEqCode,  null,
                       "N_18", acc.RestEqv,         null,
                       "N_19", acc.IsOpenClose,     null);
        if (sheetRowsCount == XLS_MAX_ROWS)
          isNewSheet = true;
        end;
      else
        isContinue = false;
      end;
      
      if (isNewSheet or (not isContinue))
        RemProgress();
        EndTable();
        CopyAllSheetInTotalBook(TEMPLATE_SHEET_NAME, false, GetTableRange(), 0, targetSheetName);
      end;

    end; // while (isContinue)

  end; //macro Create
  
  //----- конструктор -------
  rs = recordset;
  //                    form, templateName, useCsv, isWebService, reportHashCode, usePOI, useBigReportMode
  initDL_CReportTemplate(null, templateName, true, false, null, true, true);
  
end; //CAccountList

macro AccListSave(chapter: Integer, OpenClosed: Integer, partyId: Integer, balance: String, mask: String)
  var accountMask: String;
  var rs:  RsdRecordset;

  if (GetAccountMask(@accountMask, mask))
    queryWhereSection = GetWhereSection(chapter, OpenClosed, partyId, balance, accountMask);

    rs = GetRecords(countSelectSection + queryWhereSection);
    totalCount = GetTotalCount(rs);

    rs = GetRecords(accSelectSection + queryWhereSection + accOrderSection);
    CAccountList(TEMPLATE_NAME, rs).Run();
  else
    println("Пользователь отказался от выпуска отчета");
  end;
end;

//-------------------------------------------- Точка входа в макрос ----------------------------------
