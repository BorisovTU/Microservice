/*
$Name:             ws_uniavr.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Макрос сервисов скроллинга и панели Глобальных операций
*/
/*
  Fatkov E. A.
  29.04.2014
*/
import FIInter;
import "ws_common.mac";
import "ws_orderbygen.mac";
import "ws_namealg.mac";
import "ws_partylist.mac";
import "ws_avrkinds.mac";
import "ws_fiunilist.mac";
import "ws_llvalues.mac";
import "ws_dprt.mac";
import "ws_chgavr.mac";

private const ALG_SP_GLOBALOPER_SUBKINDS = 7622; // Подвиды операции изменения номинала ц/б
private const ALG_DL_COMM_OPSTATUS      = 3154;  // Статус операции Отложенная 0 Открытая 1 Закрытая 2

private const SC_GLOBAL_OPER_CONV = 1;          //Конвертация
private const SC_GLOBAL_OPER_UNIAVR = 4;        //Объединение выпусков
private const COEFFICIENT_DEFAULT_VALUE = 1;

macro UniAvrRunError( err:Variant, stat:Integer )
  WSRunError( "ws_uniavr.mac", err, stat );
end;

class CUniAvrList
  var DocumentId:Integer;        // ID операции
  var CommCode:String;	         // Код операции
  var CommDate:Date;             // Дата операции
  var State:Integer;             // Статус
  var StateName:String;          // Статус
  var OprFormType:Integer;       // Тип операции
  var FormOperation:String;      // Тип операции
  var AvoirSubKind:Integer;      // Вид ц/б
  var AvoirSubKindName:String;   // Наименование вида ц/б
  var Issuer_obj;//:TWsPartyEx   // Эмитент
  var Issuer:Integer;            // Эмитент
  var IssuerShortName:String;    // Наименование эмитента
  var Nominal:Money;	         // номинал
  var AvoirCode:String;	         // Код ц/б
end;

class CScDlFi
  var ID:Integer;              // Идентификатор
  var DealKind:Integer;        // Вид документа сделки
  var DealID:Integer;          // Идентификатор документа сделки
  var Num:string;              // Порядковый номер
  var Issuer:Integer;          // Эмитент
  var IssuerShortName:String;  // Наименование эмитента
  var Issuer_obj;//:TWsPartyEx // Эмитент
  var AvoirCode:String;	       // Код ц/б
  var Code;//:TWsFinInstr      // ц/б
  var NewFIID:Integer;         // Финансовый инструмент
  var Nominal:Money;           // номинал
  var Numerator:string;        // Числитель
  var Denominator:string;      // Знаменатель
  var Action:Integer;          // Вид действия
  var Version:Integer;         // Версия записи
  var ChangedProperty:String;  // Имя измененного поля
end;

/*класс-контейнер CUniAvrPanel для заполнения панели "Глобальная опреация"*/
class CUniAvrPanel
  var DlComm;       
  var ScDlFi:TArray; 
end;

private macro GetListForDocKind(  List_:Integer, ELEMENTS:String )
  var stat:Integer = -1;

   var whereCond_:string = " AND t_element IN (";

   if( (ELEMENTS != null) and (ELEMENTS != "") )
      whereCond_ = " AND t_element IN (" + ELEMENTS + ") ORDER BY t_element ";
   end;

   if(List_ <= 0)
     RunError("Не задан вид справочника");
   end;

   var query = "select * from dllvalues_dbt where t_List = " + string(List_) + whereCond_;

   var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);
   var result = TArray();
   var p;
   while (ds.MoveNext())
      p = TWsLlvalues(ds.Element, ds.Code, ds.Name, List_);

      result.value(result.Size) = p;
   end;

   return result;

OnError( err )
  UniAvrRunError(err, stat);
end;

private macro GetSQLDealTypeCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String                   
  var strSQLCond : String = GetSQLNameAlgConditionEx( "dl_comm", "t_opersubkind", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " dl_comm.t_opersubkind = 1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLIssuerCondition(
  condition : Integer
 ,value// : TArray of <type>
 ,type : Integer
) : String
  var strSQLCond : String = GetSQLPartyConditionEx( "fininstr", "t_Issuer", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " fininstr.t_Issuer = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLAvoirKindCondition(
  condition:Integer
 ,value//:TArray of <type>
 ,type:Integer
):String
  var strSQLCond : String = GetSQLAvoirKindConditionEx( "avrkinds", "t_AvoirKind", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " RSB_FIInstr.FI_AvrKindsEQ( " + String( FIKIND_AVOIRISS ) + ", 0, avrkinds.t_AvoirKind ) = 1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLIssueCondition(
  condition : Integer
 ,value// : TArray of <type>
 ,type : Integer
) : String
  var strSQLCond : String = GetSQLFIConditionEx( "fininstr", "t_FIID", condition, value, type );

  if( strSQLCond == "" )
    strSQLCond = " fininstr.t_FIID = -1 ";
  end;

  return strSQLCond;
end;

private macro GetSQLDepartmenCondition(
  condition : Integer
 ,value// : TArray of <type>
 ,type : Integer
) : String
  var strSQLCond : String = "";
  var valueCount : Integer = 0;

  if( ( ValType( value ) != V_UNDEF )
  and ( value.Size > 0 )
  and ( ValType( type ) == V_INTEGER )
  and ( type == FilterParameterType_UserType ) )
    while( valueCount < value.Size )
      if( ( ValType( value[valueCount] ) != V_UNDEF )
      and ( ValType( value[valueCount].Code ) == V_INTEGER )
      and ( value[valueCount].Code > 0 ) )
        if( strSQLCond == "" )
          strSQLCond = strSQLCond + " ( ";
        else
          strSQLCond = strSQLCond + " OR ";
        end;

        strSQLCond = strSQLCond + " dl_comm.t_Division = " + String( value[valueCount].Code ) + " ";
      end;
      valueCount = valueCount + 1;
    end;
    if( strSQLCond != "" )
      strSQLCond = strSQLCond + " ) ";
    end;
  end;
  if( strSQLCond == "" )
    strSQLCond = " dl_comm.t_Division = 0 ";
  end;

  return strSQLCond;
end;


private macro GetUniAvrAddWhereCondition(
  FilterItems//:TArray of FilterCondItem
):String

  var fp = FilterParser( FilterItems );

  fp.AddFieldCondition( 0, "dl_comm", "t_commdate" ); // Дата операции
  fp.AddFieldCondition( 1, "dl_comm", "t_commstatus" ); // Статус операции
//  fp.AddFieldCondition( 2, "dl_comm", "t_opersubkind" ); // Тип операции
  fp.AddUserFieldCondition( 2, @GetSQLDealTypeCondition ); // Тип операции
  fp.AddUserFieldCondition( 3, @GetSQLIssuerCondition ); // Эмитент
  fp.AddUserFieldCondition( 4, @GetSQLAvoirKindCondition ); // Вид ценной бумаги  
  fp.AddUserFieldCondition( 5, @GetSQLIssueCondition ); // Ценная бумага
  fp.AddUserFieldCondition( 6, @GetSQLDepartmenCondition ); // Филиал

  //fp.GetFullSQLConditionDebug( "ws_uniavr_filter_debug" );

  return fp.GetFullSQLCondition();
end;


// получить общее количество записей скроллинга "Глобальные операции"
macro GetUniAvrCount(
  FilterItems//:TArray of FilterCondItem // Массив элементов фильтра скроллинга
):Integer

  var result:integer = 0;
  var stat:integer = -1;
  var strAddWhere:string = "";

  var query =
     " SELECT COUNT(dl_comm.t_documentId) RecCount "
     + " FROM ddl_comm_dbt dl_comm, "
          + " davrkinds_dbt avrkinds, "
          + " dfininstr_dbt fininstr, " 
          + " dparty_dbt party "
     + " WHERE dl_comm.t_dockind = " + string(DL_ISSUE_UNION)
     +  " AND fininstr.t_FIID = dl_comm.t_FIID "
     +  " AND avrkinds.t_FI_Kind = " + string(FIKIND_AVOIRISS)
     +  " AND avrkinds.t_AvoirKind = fininstr.t_AvoirKind AND party.t_PartyID=fininstr.t_Issuer";

  strAddWhere = GetUniAvrAddWhereCondition( FilterItems );
  if( strAddWhere != "" )
     query = query + " AND " + strAddWhere;
  end;

  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if( ds.MoveNext() )
     result = SQL_ConvTypeInteger(ds.RecCount);
  end;

  return result;

OnError( err )
  UniAvrRunError(err, stat);
end;

// отбор данных для скроллинга "Глобальные операции"
macro GetUniAvrList(
  PageSize:integer // Размер страницы 
 ,PageNum:integer  // Номер страницы
 ,FilterItems      //: TArray of FilterConditionItem  // Значения фильтрации
 ,OrderItems       //: TArray of OrderItem  // Параметры сортировки
)

  var stat:Integer = -1;
  var strAddWhere:string = "";
  var UniAvrList = TArray();
  var CUniAvr = null;

  if( PageNum == null )
     RunError("Не задан обязательный параметр функции: номер страницы ");
  end;

  if( PageSize == null )
     RunError("Не задан обязательный параметр функции: размер страницы ");
  end;

  var query = "  SELECT 	 /*+ FIRST_ROWS(" + String( PageSize ) + ") */ "+
       "	         dl_comm.t_documentid,	"+
       "	         dl_comm.t_commcode,	"+
       "	         dl_comm.t_commdate,	"+
       "           avrkinds.t_AvoirKind avKind, "+
       "           avrkinds.t_Name t_AvoirSubKindName, "+
       "           FININSTR.T_Issuer Issuer, "+
       "           party.t_shortName t_IssuerShortName, "
       "	         na_form.T_INUMBERALG OprFormType,	"+
       "           na_form.t_szNameAlg t_FormOperation, "+ 
       "	         na_state.T_INUMBERALG t_State,	"+
       "           na_state.t_szNameAlg t_StateName, "+ 
       "           RSI_RSB_FIInstr.FI_GetNominalOnDate (dl_comm.t_FIID, dl_comm.t_BeginDate, 1) t_Nominal, " +
       "           fininstr.t_FI_Code t_AvoirCode "+
       "	    FROM "+
       "           ddl_comm_dbt dl_comm,  "+
       "           dnamealg_dbt na_state, "+
       "           dnamealg_dbt na_form,   "+
       "           dfininstr_dbt fininstr, "+
       "           dparty_dbt party,       "+
       "           davrkinds_dbt avrkinds	"+
       "	   WHERE dl_comm.t_dockind = 	"+ string(DL_ISSUE_UNION) + 
       "           AND fininstr.t_FIID = dl_comm.t_FIID  AND party.t_PartyID = fininstr.t_Issuer " +
       "           AND avrkinds.t_FI_Kind = " + string(FIKIND_AVOIRISS) +
       "           AND avrkinds.t_AvoirKind = fininstr.t_AvoirKind   "+
       "	         AND (na_state.t_iTypeAlg(+) = " + string(ALG_DL_COMM_OPSTATUS) + " AND na_state.t_iNumberAlg(+) = dl_comm.t_CommStatus)	"+
       "           AND (na_form.t_iTypeAlg(+) = " + string(ALG_SP_GLOBALOPER_SUBKINDS) + " AND na_form.t_iNumberAlg(+) = dl_comm.t_OperSubKind)	" ;                                           
  /*Фильтры*/
  strAddWhere = GetUniAvrAddWhereCondition( FilterItems );
  if( strAddWhere != "" )
    query = query + " AND " + strAddWhere;
  end;

  query = query + " ORDER BY " + SP_GetSortStr(OrderItems, " dl_comm.t_dockind ASC,  dl_comm.t_commstatus ASC,  dl_comm.t_commdate DESC, dl_comm.t_documentid ASC ");
  query = SQL_WrapSelect(query, PageNum, PageSize);

  var ds = TRsbDataSet(query);
  while( ds.MoveNext() )
    CUniAvr = CUniAvrList();

    CUniAvr.DocumentId = SQL_ConvTypeInteger(ds.DocumentId);
    CUniAvr.CommCode = SQL_ConvTypeStr(ds.CommCode);
    CUniAvr.CommDate = SQL_ConvTypeDate(ds.CommDate);
    CUniAvr.State = SQL_ConvTypeInteger(ds.State);
    CUniAvr.StateName = SQL_ConvTypeStr(ds.StateName);
    CUniAvr.OprFormType = SQL_ConvTypeInteger(ds.OprFormType);
    CUniAvr.FormOperation = SQL_ConvTypeStr(ds.FormOperation);      
    CUniAvr.AvoirSubKind = SQL_ConvTypeInteger(ds.avKind);
    CUniAvr.AvoirSubKindName = SQL_ConvTypeStr(ds.AvoirSubKindName);
    CUniAvr.Issuer_obj = GetTWsPartyExByID(SQL_ConvTypeInteger(ds.Issuer));
    CUniAvr.Issuer = SQL_ConvTypeStr(ds.Issuer);
    CUniAvr.IssuerShortName = SQL_ConvTypeStr(ds.IssuerShortName);
    CUniAvr.Nominal = SQL_ConvTypeSum(ds.Nominal); 
    CUniAvr.AvoirCode = SQL_ConvTypeStr(ds.AvoirCode);

    UniAvrList[UniAvrList.Size] = CUniAvr;
  end;

  return UniAvrList;
  
OnError( err )
  UniAvrRunError( err, stat );
end;

/*Данные для панели Глобальная операция*/
private macro UniAvrGetDlComm(DocumentId): CDlComm

  var stat:Integer = -1;
  var Iss = TRecHandler( "fininstr" );

  InitError();

  if( (DocumentID == null) or (DocumentID <= 0) )
     RunError("Не задан обязательный параметр функции: ID операции");
  end;

  var query = "	SELECT 	"+
              "	       dl_comm.t_documentid,	"+
              "	       dl_comm.t_dockind,	"+
              "	       dl_comm.t_OperationKind,	"+
              "	       dl_comm.t_commcode,	"+
              "	       dl_comm.t_commdate,	"+
              "	       sk.t_szNameAlg AS FormName,    	"+
              "	       dl_comm.t_begindate,	"+
              "	       dl_comm.t_enddate,	"+
              "	       dl_comm.t_commstatus,       	"+
              "	       RSI_RSB_FIInstr.	"+
              "	        FI_GetNominalOnDate (dl_comm.t_FIID, dl_comm.t_BeginDate, 1)	"+
              "	          Nominal,	"+
              "	       dl_comm.t_fiid,	"+
              "	       dl_comm.t_opersubkind,	"+
              "	       dl_comm.t_avoirkind,	"+
              "	       dl_comm.t_flag1 CarryRest,	"+
              "	       dl_comm.t_flag3 WrtAvr,	"+
              "	       dl_comm.t_flag2 CloseFI,	"+
              "	      dl_comm.t_version AS t_dlcomm_version,       	"+
              "	       avrkinds.t_Name AS t_AvoirSubKindName,       	"+
              "	       fininstr.t_FI_Code AS t_AvoirCode,	"+
              "	       fininstr.t_Issuer,	"+
              "	       avrkinds.t_AvoirKind AS t_avrkinds_AvoirKind,	"+
              "	       avrkinds.t_Root,	"+
              "	       ground.t_kind,	"+
              "	       ground.t_version AS t_ground_version,	"+
              "	       fininstr.t_FaceValueFi,	"+
              "	       dl_comm.T_ground,	"+
              "	       ground.T_ALTXLD,	"+
              "	       ground.t_SignedDate,	"+
              "	       ground.t_SPGroundID,	"+
              "	       ground.t_xld,	"+
              "	       ground.t_references AS t_ground_references,	"+
              "	       ground.t_department AS t_ground_department,	"+
              "	       ground.t_branch,	"+
              "	       dl_comm.t_CreateReport,	"+
              "	       dl_comm.t_Division,	"+
              "	       dl_comm.t_Oper,	"+
              "	       person.t_Name AS t_OperName	"+
              "	  FROM	"+
              "	       ddl_comm_dbt dl_comm,	"+
              "	       davrkinds_dbt avrkinds,	"+
              "	       dfininstr_dbt fininstr,       	"+
              "	       dnamealg_dbt sk,       	"+
              "	       dspground_dbt ground,	"+
              "	       dperson_dbt person	"+
              "	 WHERE     	"+
              "	        dl_comm.t_dockind = " +  string(DL_ISSUE_UNION) + 
              "	       AND dl_comm.T_DOCUMENTID = "+  string(DocumentId) + 
              "	       AND sk.t_iTypeAlg = " +  string(ALG_SP_GLOBALOPER_SUBKINDS)+
              "	       AND sk.t_iNumberAlg = dl_comm.t_OperSubKind       	"+
              "	       AND fininstr.t_FIID = dl_comm.t_FIID	"+
              "	       AND avrkinds.t_FI_Kind = "+ string(FIKIND_AVOIRISS)+  
              "	       AND avrkinds.t_AvoirKind = fininstr.t_AvoirKind       	"+
              "	       AND  ground.T_SPGROUNDID(+) = dl_comm.t_ground	"+
              "	       AND dl_comm.t_oper = person.t_oper	";

  var CDlComm_obj = CDlComm();
  var ds = TRsbDataSet(query);
  
  if( ds.MoveNext() )
     CDlComm_obj.DocumentID           = SQL_ConvTypeInteger(ds.DocumentID);
     CDlComm_obj.DocKind              = SQL_ConvTypeInteger(ds.DocKind);
     CDlComm_obj.OperationKind        = SQL_ConvTypeInteger(ds.OperationKind);
     CDlComm_obj.CommCode             = SQL_ConvTypeStr(ds.CommCode);
     CDlComm_obj.CommDate             = SQL_ConvTypeDate(ds.CommDate);
     CDlComm_obj.BeginDate             = SQL_ConvTypeDate(ds.BeginDate);
     CDlComm_obj.EndDate             = SQL_ConvTypeDate(ds.EndDate);
     CDlComm_obj.OprFormName          = CreateNameAlgFromAppServ(ALG_SP_GLOBALOPER_SUBKINDS, SQL_ConvTypeInteger(ds.opersubkind));
     CDlComm_obj.Iss                  = GetTWsFinInstrByID(SQL_ConvTypeInteger(ds.fiid));

     if( ( SP_GetFIID( CDlComm_obj.Iss ) > ALLFININSTR )
     and ( ПолучитьФинИн( SP_GetFIID( CDlComm_obj.Iss ), Iss ) == 0 ) )
        CDlComm_obj.IssAvoirIss      = TxGetAvoirIss( CDlComm_obj.Iss.Fiid );
        CDlComm_obj.IssKind          = CDlComm_obj.IssAvoirIss.AvoirKindObj;
        CDlComm_obj.IssSubKind       = CDlComm_obj.IssAvoirIss.AvoirSubKindObj;
        CDlComm_obj.Issuer           = GetTWsPartyExByID( SP_GetPartyID( CDlComm_obj.IssAvoirIss.Issuer ) );
        CDlComm_obj.FaceValueFI      = GetTWsFinInstrByID( Iss.rec.FaceValueFI, CODE_Ccy );
     end;
     
     CDlComm_obj.Nominal             = SQL_ConvTypeSum(ds.Nominal);

     CDlComm_obj.DocKindName         = CreateLLValueFromAppServ(1050, SQL_ConvTypeInteger(ds.kind));
     CDlComm_obj.DocKindList         = GetListForDocKind(1050, "2,226,288,296,297");
     CDlComm_obj.SPGroundID          = SQL_ConvTypeInteger(ds.SPGroundID);
     CDlComm_obj.SPGroundID_Old      = SQL_ConvTypeInteger(ds.SPGroundID);
     
     if( SQL_ConvTypeInteger(ds.ground) > 0 )
        CDlComm_obj.AltXld           = SQL_ConvTypeStr(ds.ALTXLD);
        CDlComm_obj.SignedDate       = SQL_ConvTypeDate(ds.SignedDate);
     end;

     CDlComm_obj.CreateReport        = IIF(SQL_ConvTypeStr(ds.CreateReport) == SET_CHAR, true, false);
     CDlComm_obj.CarryRest           = (SQL_ConvTypeStr(ds.CarryRest) == SET_CHAR);
     CDlComm_obj.CloseFI             = (SQL_ConvTypeStr(ds.CloseFI) == SET_CHAR);
     CDlComm_obj.WrtAvr              = (SQL_ConvTypeStr(ds.WrtAvr) == SET_CHAR);
     CDlComm_obj.Department          = GetTWsDepartmentByCode(SQL_ConvTypeInteger(ds.Division));
     CDlComm_obj.Oper                = SQL_ConvTypeInteger(ds.Oper);
     CDlComm_obj.OperName            = SQL_ConvTypeStr(ds.OperName);
     CDlComm_obj.Status              = SQL_ConvTypeStr(ds.CommStatus);
     CDlComm_obj.DlCommVersion       = SQL_ConvTypeInteger(ds.dlcomm_version);
     CDlComm_obj.SPGroundVersion     = SQL_ConvTypeInteger(ds.ground_version);
     CDlComm_obj.Xld                 = SQL_ConvTypeStr(ds.xld);
     CDlComm_obj.GroundReferences    = SQL_ConvTypeInteger(ds.ground_references);
     CDlComm_obj.GroundDepartment    = SQL_ConvTypeInteger(ds.ground_department);
     CDlComm_obj.Branch              = SQL_ConvTypeInteger(ds.branch);
  else
     CDlComm_obj.DocumentID          = -1;
  end;

  return CDlComm_obj;

OnError( err )
  UniAvrRunError( err, stat );
end;

private macro UniAvrInitDlComm():CDlComm
  var stat:integer = -1;
  var CDlComm_obj = CDlComm();
  
  InitError();

  CDlComm_obj.DocKind = DL_ISSUE_UNION;
  stat = DL_GetKindOperation( DL_ISSUE_UNION, CDlComm_obj.OperationKind );
  if( stat != 0 )
     RunError("Не удалось получить номер вида операции");
  end;
  CDlComm_obj.DocumentID = 0;
  CDlComm_obj.SPGroundID = 0;
  CDlComm_obj.SPGroundID_Old = 0;
  CDlComm_obj.CommCode = "";
  CDlComm_obj.CommDate = {CurDate};
  CDlComm_obj.BeginDate = {CurDate};
  CDlComm_obj.EndDate = {CurDate};

  CDlComm_obj.OprFormName = CreateNameAlgFromAppServ(ALG_SP_GLOBALOPER_SUBKINDS, SC_GLOBAL_OPER_CONV);
  CDlComm_obj.Issuer = null;
  CDlComm_obj.Iss = null;
  CDlComm_obj.Nominal = $0;
  CDlComm_obj.FaceValueFI = null;
  CDlComm_obj.DocKindList = GetListForDocKind(1050, "2,226,288,296,297");
  CDlComm_obj.Oper = {Oper};
  CDlComm_obj.OperName = {Name_Oper};
  CDlComm_obj.Department = GetTWsDepartmentByCode({OperDprt});
  CDlComm_obj.DlCommVersion = 0;
  CDlComm_obj.SPGroundVersion = 0;
  CDlComm_obj.Xld = "";    
  CDlComm_obj.AltXld = "";    

  CDlComm_obj.CreateReport = false;
  CDlComm_obj.CloseFI = false;
  CDlComm_obj.CarryRest = false;
  CDlComm_obj.WrtAvr = false;

  CDlComm_obj.GroundReferences = 0;
  CDlComm_obj.GroundDepartment = 0;
  CDlComm_obj.Branch = 0;

  return CDlComm_obj;

OnError( err )
  UniAvrRunError( err, stat );
end;

macro InitCScDlFi(DLComm /*CDL_COMM*/, ScDlFiList /*Array*/,  ScDlFi) : CScDlFI
  var DlFi = CScDlFi();
  var stat:integer = -1;
  var currNominal = $0;
  var Iss = TRecHandler( "fininstr" );

  InitError();

  DlFi.Num = SQL_ConvTypeStr(ScDlFiList.Size);
  DlFi.Numerator = SQL_ConvTypeStr(COEFFICIENT_DEFAULT_VALUE);
  DlFi.Denominator = SQL_ConvTypeStr(COEFFICIENT_DEFAULT_VALUE);

  if( (DLComm != null) and
      (SP_GetNameAlgNumberAlg( DlComm.OprFormName ) == SC_GLOBAL_OPER_UNIAVR) and
      (SP_GetFIID( DlComm.Iss ) > ALLFININSTR) and 
      (ПолучитьФинИн( SP_GetFIID( DlComm.Iss ), Iss ) == 0 )
    )
     if( Iss.rec.Issuer > 0 )
        DlFi.Issuer_obj = GetTWsPartyExByID(Iss.rec.Issuer);

        if((Iss != null) and (Iss.rec.IsAdditional == SET_CHAR) and  (Iss.rec.MainFiid > 0) )
           DlFi.Code = GetTWsFinInstrByID(Iss.rec.MainFiid);
        else
           DlFi.Code = GetTWsFinInstrByID( SP_GetFIID( DlComm.Iss ) );
        end;

     else
        DlFi.Issuer_obj = null;
        DlFi.Code = null;
     end;

     var end_date = {curdate};
     if(DLComm.EndDate != null)
        end_date = DLComm.EndDate;
     end;

     SP_GetNominal( SP_GetFIID( DlComm.Iss ), end_date, currNominal, BO_CB /*0*/ );
     DlFi.Nominal = SQL_ConvTypeSum(currNominal);
  else
    DlFi.Nominal = $0;
    DlFi.Issuer_obj = null;
    DlFi.Code = null;
  end;
  return  DlFi;

OnError( err )
  UniAvrRunError( err, stat );
end;

/*Закладка новые выпуски*/
macro GetNewIssueList(DocumentId)
  var result = TArray();
  var stat:integer = -1;
  var ScDlFi;

  InitError();

  if( DocumentId == null )
     RunError("Не задан обязательный параметр функции: ID операции");
  end;

  var query = RSDCommand( "	SELECT SCDL.T_DEALID,	"+
                          "	       SCDL.T_DEALKIND,	"+
                          "	       SCDL.T_DENOMINATOR,	"+
                          "	       SCDL.T_ID,	"+
                          "	       SCDL.T_NEWFIID,	"+
                          "	       SCDL.T_NUM,	"+
                          "	       SCDL.T_NUMERATOR,	"+
                          "	       SCDL.T_VERSION,	"+
                          "	       FIN.T_ISSUER,	"+
                          "	       party.t_shortName IssuerShortName,	"+
                          "	       RSI_RSB_FIInstr.	"+
                          "	        FI_GetNominalOnDate (SCDL.T_NEWFIID, dl_comm.t_BeginDate, 1)	"+
                          "	          Nominal,	"+
                          "	       fin.t_FI_Code AvoirCode	"+
                          "	  FROM dscdlfi_dbt scdl,	"+
                          "	       dfininstr_dbt fin,	"+
                          "	       ddl_comm_dbt dl_comm,	"+
                          "	       dparty_dbt party	"+
                          "	 WHERE     SCDL.T_DEALID = ?	"+
                          "	       AND SCDL.T_DEALKIND = 	"+   string(DL_ISSUE_UNION) + 
                          "	       AND DL_COMM.T_DOCUMENTID = SCDL.T_DEALID	"+
                          "	       AND party.t_PartyID = fin.t_Issuer	"+
                          "	       AND FIN.T_FIID = SCDL.T_NEWFIID	"
                        );
  query.NullConversion = true;
  query.addParam( "", RSDBP_IN, DocumentId );
  query.execute();

  var ds = TRsbDataSet(query);

  while( ds.MoveNext() )
     ScDlFi = CScDlFi();

     ScDlFi.ID =  SQL_ConvTypeInteger(ds.ID);
     ScDlFi.DealKind = SQL_ConvTypeInteger(ds.DealKind);
     ScDlFi.DealID = SQL_ConvTypeInteger(ds.DealID);
     ScDlFi.Num = SQL_ConvTypeStr(ds.Num);
     ScDlFi.Issuer= SQL_ConvTypeStr(ds.Issuer);
     ScDlFi.IssuerShortName = SQL_ConvTypeStr(ds.IssuerShortName);

     ScDlFi.Issuer_obj = GetTWsPartyExByID(SQL_ConvTypeInteger(ds.Issuer));

     ScDlFi.NewFIID = SQL_ConvTypeInteger(ds.NewFIID);
     ScDlFi.AvoirCode = SQL_ConvTypeStr(ds.AvoirCode);
     ScDlFi.Code = GetTWsFinInstrByID(SQL_ConvTypeInteger(ds.NewFIID));
     ScDlFi.Nominal = SQL_ConvTypeSum(ds.Nominal); 
     ScDlFi.Numerator = SQL_ConvTypeStr(ds.Numerator);
     ScDlFi.Denominator = SQL_ConvTypeStr(ds.Denominator);
     ScDlFi.Action = OperationKind_Undefine;
     ScDlFi.Version = SQL_ConvTypeInteger(ds.Version);

     result.value(result.Size) = ScDlFi;
  end;

  return result;

OnError( err )
  UniAvrRunError(err, stat);

end;

macro UniAvrInitPanel(DocumentId:Integer):CUniAvrPanel
  var stat:integer = -1;
  var UniAvrPanel = CUniAvrPanel();

  InitError();

  if( DocumentId == null)
     RunError("Не задан обязательный параметр функции: DocumentId");
  end;

  if( (DocumentId != null) and (DocumentId > 0) )
     UniAvrPanel.DlComm = UniAvrGetDlComm(DocumentId);
     UniAvrPanel.ScDlFi = GetNewIssueList(DocumentId);
  else
     UniAvrPanel.DlComm = UniAvrInitDlComm();
     UniAvrPanel.ScDlFi = TArray();
  end;

  return UniAvrPanel;

OnError( err )
  UniAvrRunError(err, stat);
end;

/*Обработчики полей ExitField*/
private macro ClearIss( CDlComm_obj );
  CDlComm_obj.Iss = null;
  CDlComm_obj.FaceValueFI = null;
  CDlComm_obj.Nominal = $0;  
end;

private macro SetIss( CDlComm_obj )
  var Iss = TRecHandler( "fininstr" );

  if( ( SP_GetFIID( CDlComm_obj.Iss ) > ALLFININSTR )
  and ( ПолучитьФинИн( SP_GetFIID( CDlComm_obj.Iss ), Iss ) == 0 ) )
    CDlComm_obj.IssAvoirIss = TxGetAvoirIss( CDlComm_obj.Iss.Fiid );
    CDlComm_obj.IssKind = CDlComm_obj.IssAvoirIss.AvoirKindObj;
    CDlComm_obj.IssSubKind = CDlComm_obj.IssAvoirIss.AvoirSubKindObj;
    CDlComm_obj.Issuer = GetTWsPartyExByID( SP_GetPartyID( CDlComm_obj.IssAvoirIss.Issuer ) );
    CDlComm_obj.FaceValueFI = GetTWsFinInstrByID( Iss.rec.FaceValueFI, CODE_Ccy );
    var currNominal = $0;
    SP_GetNominal( CDlComm_obj.Iss.Fiid, CDlComm_obj.BeginDate, currNominal, BO_CB /*0*/ );
    CDlComm_obj.Nominal = SQL_ConvTypeSum(currNominal);
  end;
end;

private macro proc_Iss( CDlComm_obj );
  if( SP_GetFIID( CDlComm_obj.Iss ) > ALLFININSTR )
     SetIss( CDlComm_obj );
  else
     ClearIss( CDlComm_obj ); 
  end;
end;

private macro proc_IssKind( CDlComm_obj )
  ClearIss( CDlComm_obj );
  CDlComm_obj.IssSubKind = CTxAvrKinds();
end;

private macro proc_IssSubKind( CDlComm_obj )
  ClearIss( CDlComm_obj );

  if( CDlComm_obj.IssSubKind != null )
     if( CDlComm_obj.IssKind != null )
        if (CDlComm_obj.IssKind.RootAvrKinds != CDlComm_obj.IssSubKind.RootAvrKinds)
           CDlComm_obj.IssKind = TxGetAvrKindsById( CDlComm_obj.IssSubKind.RootAvrKinds );
        end;
     else
        CDlComm_obj.IssKind = TxGetAvrKindsById( CDlComm_obj.IssSubKind.RootAvrKinds );
     end;
  end;
end;

private macro proc_Issuer( CDlComm_obj )
  if( SP_GetFIID( CDlComm_obj.Iss ) > ALLFININSTR )
     if( (CDlComm_obj.IssAvoirIss != null) and (SP_GetPartyID( CDlComm_obj.IssAvoirIss.Issuer ) != SP_GetPartyID( CDlComm_obj.Issuer ) ) )
        ClearIss( CDlComm_obj );
     end;
  end;
end;
                   
private macro proc_CommDate( CDlComm_obj )
  CDlComm_obj.BeginDate = CDlComm_obj.EndDate = CDlComm_obj.CommDate;
end;
                   
private macro ClearGround( CDlComm_obj )
   CDlComm_obj.SPGroundID = 0;
   CDlComm_obj.Xld = "";
   CDlComm_obj.AltXld = "";    
   CDlComm_obj.GroundReferences = 0;
   CDlComm_obj.GroundDepartment = 0;
   CDlComm_obj.Branch = 0;
end;


macro UniAvrPanelExitField( CUniAvrPanel/*:CUniAvrPanel*/ ):WebServiceResponse
  var stat:integer = -1;
  var ResponseBuilder:SP_WebResponseBuilder = SP_WebResponseBuilder();

  if( CUniAvrPanel == null )
     RunError("Не задан обязательный параметр функции: параметры глобальной операции");
  end;            

  var CDlComm_obj = CUniAvrPanel.DlComm;

  if( CDlComm_obj == null )
     RunError("Не задан обязательный параметр функции: операция");
  end;

  // обработка схода с поля
  if( CDlComm_obj.ChangedProperty != null )

     if( CDlComm_obj.ChangedProperty == "Iss" )
        proc_Iss( CDlComm_obj );
     elif( CDlComm_obj.ChangedProperty == "IssKind" )
        proc_IssKind( CDlComm_obj );
     elif( CDlComm_obj.ChangedProperty == "IssSubKind" )
        proc_IssSubKind( CDlComm_obj );
     elif( CDlComm_obj.ChangedProperty == "Issuer" )
        proc_Issuer( CDlComm_obj );
     elif( (CDlComm_obj.ChangedProperty == "DocKindName")
        or (CDlComm_obj.ChangedProperty == "AltXld")
        or (CDlComm_obj.ChangedProperty == "SignedDate") )
        ClearGround( CDlComm_obj );
     elif( CDlComm_obj.ChangedProperty == "CommDate" )
        proc_CommDate( CDlComm_obj );
     end;
  end;

  /* валидация полей*/
  if( CDlComm_obj.ChangedProperty != null )
     if( CDlComm_obj.ChangedProperty == "CommCode" )
        check_CommCode(CDlComm_obj, ResponseBuilder);
     end;
  end;
  
  if( CDlComm_obj.CommDate > {curdate} ) 
    ResponseBuilder.AddError(8063 /*SEC_ERROR_DATEBIG*/) ;
  elif( CDlComm_obj.EndDate < CDlComm_obj.BeginDate )    
    ResponseBuilder.AddError(8038 /*SEC_ERROR_WRONG_WRT_DATE*/) ;
  end; 

  ResponseBuilder.SetUserObject( CUniAvrPanel );

  return ResponseBuilder.Result;

OnError( err )
  UniAvrRunError( err, stat );
end;

private macro proc_Code( DLComm, ScDlFi )
  var currNominal = $0;
  var Iss = TRecHandler( "fininstr" );

  if( ( SP_GetFIID( ScDlFi.Code ) > ALLFININSTR )
       and ( ПолучитьФинИн( SP_GetFIID( ScDlFi.Code ), Iss ) == 0 ) )

     ScDlFi.Issuer_obj = GetTWsPartyExByID(Iss.rec.Issuer);

     var end_date = {curdate};
     if(DLComm.EndDate != null)
        end_date = DLComm.EndDate;
     end;

     SP_GetNominal( SP_GetFIID( ScDlFi.Code ), end_date, currNominal, BO_CB /*0*/ );
     ScDlFi.Nominal = SQL_ConvTypeSum(currNominal);

  else
    //Очистим поле Эмитент
    ScDlFi.Nominal = $0;  
  end; 
end;

private macro proc_Issuer_obj(DLComm, ScDlFi)            
  ScDlFi.Nominal = $0;  
  ScDlFi.Code = null; 
end;

macro ScDlFiPanelExitField(DLComm /*CDL_COMM*/, ScDlFiList /*Array*/,  ScDlFi):WebServiceResponse
  var stat:integer = -1;
  var ResponseBuilder:SP_WebResponseBuilder = SP_WebResponseBuilder();

  if( DLComm == null )
     RunError("Не задан обязательный параметр функции: параметры глобальной операции");
  end;            

  if( ScDlFiList == null )
     RunError("Не задан обязательный параметр функции: массив новых выпусков");
  end;

  if( ScDlFi == null )
     RunError("Не задан обязательный параметр функции: параметры нового выпуска");
  end;

  // обработка схода с поля
  if( ScDlFi.ChangedProperty != null )
     if( ScDlFi.ChangedProperty == "Code" )
        proc_Code( DLComm, ScDlFi );
     elif( ScDlFi.ChangedProperty == "Issuer_obj" )
        proc_Issuer_obj(DLComm, ScDlFi );
     end;
  end;

  /* валидация полей*/
  if((ScDlFi.Num != null) and  (not StrIsNumber(ScDlFi.Num) )  and ( int(ScDlFi.Num) <= 0 ) ) 
     ResponseBuilder.AddError(37111 )
  elif((ScDlFi.Issuer_obj == null) or ((ScDlFi.Issuer_obj != null) and (ScDlFi.Issuer_obj.partyId <= 0) ) )
     ResponseBuilder.AddError(37014)   /*37014 - 'Не задан эмитент'*/           
  elif( (ScDlFi.Code == null) or ((ScDlFi.Code != null) and (ScDlFi.Code.Name == null))  )
     ResponseBuilder.AddError(37015)    /*37015 - 'Не задан код ценной бумаги'*/
  elif( (ScDlFi.Numerator != null) and (not StrIsNumber(ScDlFi.Numerator))  and ( int(ScDlFi.Numerator) <= 0)  )
     ResponseBuilder.AddError(37112)
  elif( (ScDlFi.Denominator != null) and (not StrIsNumber(ScDlFi.Denominator))  and (int(ScDlFi.Denominator) <= 0)  )
     ResponseBuilder.AddError(37113)
  end; 

  ResponseBuilder.SetUserObject( ScDlFi );

  return ResponseBuilder.Result;

OnError( err )
  UniAvrRunError( err, stat );
end;


/*Сервисы сохранения данных панели*/
private macro SetScDlFi_ByCScDlFi(ScDlFiRec, ScDlFi)
  ScDlFiRec.ID = ScDlFi.ID;
  ScDlFiRec.DealKind = ScDlFi.DealKind;
  ScDlFiRec.DealID = ScDlFi.DealID;
  ScDlFiRec.Num = ScDlFi.Num;   
  ScDlFiRec.NewFIID = ScDlFi.NewFIID;  
  ScDlFiRec.Numerator = ScDlFi.Numerator;
  ScDlFiRec.Denominator = ScDlFi.Denominator;  
  ScDlFiRec.Version = ScDlFi.Version;
end;

private macro GetScDlFiRecId(DocumentID, ScDlDealKind): integer
  var Id = -1;
  var DataSet;
  var Query = RSDCommand(
                     " SELECT t_id " + 
                     "   FROM dscdlfi_dbt " + 
                     "  WHERE T_DEALKIND = ? " +
                     "    AND T_DEALID   = ? "
                    );

  Query.addParam( "", RSDBP_IN, ScDlDealKind );
  Query.addParam( "", RSDBP_IN, DocumentID );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if (DataSet.MoveNext())
     Id = DataSet.Id;
  end;

  return Id;
end;

private macro GetNewFiid( FinInstr /*TWsFinInstr*/ ):integer
  var RetVal = ALLFININSTR;
  var FI = TRecHandler( "fininstr.dbt" );
  if( (FinInstr != NULL) and (FinInstr.Fiid != null) )
     if( (SP_GetFIID(FinInstr) > 0) and (not ПолучитьФинИн(FinInstr.Fiid, FI)) )
		    RetVal = FI.rec.FIID;
	   end;     
  end;                           
  return RetVal;
end;

/*Сервис сохранения данных вкладки новые выпуски*/
private macro SaveScDlFi( ScDlFi, DocumentID, ErrorMsg:@string ):integer
  var stat:integer = 0;
  var ScDlFiRec = Tbfile("scdlfi", "W", 0);
  var ScDlFiOldRec = Tbfile("scdlfi", "W", 0);
  var size:integer = ScDlFi.size();
  var i:integer = 0;
  var ScDlFiItem;

  if( size > 0 )
     /* Удаляем записи*/
     i = 0;
     while( (i < size) AND (stat == 0) )
        ScDlFiItem = ScDlFi(i);

        if( (ScDlFiItem.Action == OperationKind_Delete) and (ScDlFiItem.ID > 0) )
           ScDlFiRec.Clear();
           ScDlFiRec.rec.ID = ScDlFiItem.ID;
           if( not ScDlFiRec.GetEQ() )
              stat = 1;
              ErrorMsg = "Не найдена запись таблицы dScDlFi_dbt с ID=" + string(ScDlFiItem.ID);
           elif( ScDlFiRec.rec.Version != ScDlFiItem.Version )
              stat = 70;
              ErrorMsg = "Конфликт таблицы dlScDlFi_dbt. Документ изменен другим операционистом";
           else
              if( not ScDlFiRec.delete() )
                 stat = 1; 
                 ErrorMsg = "Ошибка удаления записи таблицы dScDlFi_dbt";
              end;
           end;
        end;

        i = i + 1;
     end;

     // Редактируем записи
     i = 0;
     while( (i < size) AND (stat == 0) )
        ScDlFiItem = ScDlFi(i);

        if( (ScDlFiItem.Action == OperationKind_Update) and (ScDlFiItem.ID > 0) )
           ScDlFiOldRec.Clear();
           ScDlFiOldRec.rec.ID = ScDlFiItem.ID;
           if( not ScDlFiOldRec.GetEQ() )
              stat = 1;
              ErrorMsg = "Не найдена запись таблицы dScDlFi_dbt с ID=" + string(ScDlFiItem.ID);
           elif( ScDlFiOldRec.rec.Version != ScDlFiItem.Version )
              stat = 70;
              ErrorMsg = "Конфликт таблицы dScDlFi_dbt. Документ изменен другим операционистом";
           end;

           if( stat == 0 )
              Copy(ScDlFiRec, ScDlFiOldRec);
              if( not ScDlFiRec.GetEQ() )
                 stat = 1;
                 ErrorMsg = "Не найдена запись таблицы dScDlFi_dbt с ID=" + string(ScDlFiItem.ID);
              end;
           end;

           if( stat == 0 )
              SetScDlFi_ByCScDlFi(ScDlFiRec.rec, ScDlFiItem);

              if( SP_CmpRec(ScDlFiRec, ScDlFiOldRec) == false )
                 if( not ScDlFiRec.update() )
                    stat = 1; // ???
                    ErrorMsg = "Ошибка обновления записи таблицы dlScDlFi_dbt";
                 end;
              end;
           end;
        end;

        i = i + 1;
     end;

     // Вставляем записи
     i = 0;
     while((i < size) AND (stat == 0))
        ScDlFiItem = ScDlFi(i);

        if( (ScDlFiItem.Action == OperationKind_Insert) and (ScDlFiItem.ID <= 0) )
           ScDlFiRec.Clear();
           SetScDlFi_ByCScDlFi(ScDlFiRec.rec, ScDlFiItem);
           ScDlFiRec.rec.ID = 0;
           ScDlFiRec.rec.DealId = DocumentID;
           ScDlFiRec.rec.DealKind = DL_ISSUE_UNION;
           ScDlFiRec.rec.NewFIID = GetNewFiid(ScDlFiItem.Code);
           if( not ScDlFiRec.insert() )
              stat = 1;
              ErrorMsg = "Ошибка вставки новой записи в таблицу ddlScDlFi_dbt";
           end;
        end;

        i = i + 1;
     end;
  end;

  return stat;
end;

/*Сервис сохранения данных панели Параметры Глобальной операции (2 закладки)*/
macro UniAvrSaveOperation(
  DocumentID:Integer // ID сделки
 ,DlComm//:CDlComm // Параметры глобальной операции 
 ,ScDlFi//:TArray of CScDlFi // Массив объектов, описывающий новые выпуски
 
):WebServiceResponse
  var stat:integer = 0;
  var MessageStr:string = "";
  var begin_transaction:bool = false;
  var responseBuilder = WebResponseBuilder();

  InitError();

  RslDefCon.BeginTrans();
  begin_transaction = true;

  /* сохранение глобальной операции*/
  if( (stat == 0) and (DlComm != null) )
       stat = SaveDlComm_inTrn(DocumentId, DlComm, @MessageStr);
     if ( (stat == 0) and (DocumentId <= 0))
       DocumentID = GetDocumentIdAfterTrn(DlComm);
     end;      
  end;  
  /*сохранение вкладки "Новые выпуски"*/
  if( (stat == 0) and (ScDlFi != null) )
     stat = SaveScDlFi(ScDlFi, DocumentID, @MessageStr);
  end;

  if( stat != 0 )
     if( MessageStr != "" )
        responseBuilder.AddErrorEx(stat, MessageSeverity.RuntimeError, MessageStr);
     else
        stat = -1;
        var ErrMsg = GetErrMsg();
        if( ErrMsg != "" )
          responseBuilder.AddErrorEx(stat, MessageSeverity.RuntimeError, ErrMsg);
        else
          responseBuilder.AddErrorEx(stat, MessageSeverity.RuntimeError, "Ошибка транзакции сохранения сделки");
        end;
     end;

     if( begin_transaction )
        RslDefCon.RollbackTrans();
        begin_transaction = false;
     end;
  end;

  if( begin_transaction )
     begin_transaction = false;
     RslDefCon.CommitTrans();
  end;

  ResponseBuilder.SetUserObject( DocumentId );

  return ResponseBuilder.Result;

OnError( err )
  if( begin_transaction )
     RslDefCon.RollbackTrans();
  end;
  UniAvrRunError(err, stat);
end;


/*Сервис удаления записи из скроллинга Глобальные операции*/
macro DeleteUniAvr(DocumentID : integer )//:WebResponseBuilder
  var stat:integer = 0;
  var ErrMsg:string = "";
  var query, ds;
  var ResponseBuilder = WebResponseBuilder();
  var begin_transaction:bool = false;
  var needUpd:Bool = false;

  InitError();

  RslDefCon.BeginTrans();
  begin_transaction = true;

  var DLCommRec = Tbfile("dl_comm", "W", 0);
  var SPGroundRec = Tbfile("spground", "R", 0);
  var ScDlFiRec = Tbfile("scdlfi", "W", 0);

  DLCommRec.Clear();
  SPGroundRec.Clear();
  ScDlFiRec.Clear();

  var ScDlDealKind = -1;
  var ScNewFiid = -1;
  
  if( not ( ( ValType( DocumentID ) == V_INTEGER ) and ( DocumentID > 0 ) ) )
    ErrMsg = "Не задан обязательный параметр функции: ID операции";
    stat = 1;
  end;

  DLCommRec.rec.DocumentID = DocumentID;
  if( not DLCommRec.GetEQ() )
     stat = 1;
     ErrMsg = "Не найдена запись таблицы ddl_comm_dbt с DocumentID=" + string(DocumentID);
  end;

  if( stat == 0 )
     ScDlDealKind = DLCommRec.rec.DocKind;
     if( not DLCommRec.Delete() )
        stat = 1; 
        ErrMsg = "Ошибка удаления записи таблицы ddl_comm_dbt";
     end;
  end;

  if( stat == 0 )
     if( DLCommRec.rec.ground > 0 )
        SPGroundRec.rec.SPGroundID = DLCommRec.rec.ground;
        if( not SPGroundRec.GetEQ() )
           stat = 1;
           ErrMsg = "Не найдена запись таблицы dspground_dbt с SPGroundID=" + string(DLCommRec.rec.ground);
        end;
     end;
  end;

  if( stat == 0 )
    stat = WEB_DeleteSPGRDOCbyDocKindExt( DocumentID, DL_CHGAVRNOM );
    if( (stat == 0) and (DLCommRec.rec.ground > 0) )
       stat = WEB_DeleteGroundByID( DLCommRec.rec.ground );
       if( (stat == 4) or (stat == 9) )
          stat = 0;
       end;
    end;
  end;

  if( stat == 0)
    ScDlFiRec.rec.ID = GetScDlFiRecId(DocumentID, ScDlDealKind);
    if( ScDlFiRec.GetEQ() )
      if( not ScDlFiRec.Delete() )
         stat = 1; 
         ErrMsg = "Ошибка удаления записи таблицы ddl_comm_dbt";
      end; 
    end;
  end;


  if( stat != 0 )
     if( ErrMsg != "" )
        ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
     else
        stat = -1;
        ErrMsg = GetErrMsg();
        if( ErrMsg != "" )
           ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, ErrMsg );
        else
           ResponseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, "Ошибка транзакции удаления" );
        end;
     end;
     RslDefCon.RollbackTrans();
  else
     begin_transaction = false;
     RslDefCon.CommitTrans();
  end;

  ResponseBuilder.SetUserObject( DocumentID );

  return ResponseBuilder.Result;

OnError( err )
  if( begin_transaction )
     RslDefCon.RollbackTrans();
  end;
  UniAvrRunError( err, stat );
end;

