/*
$Name:        uniavr40.mac
$Module:      Ценные бумаги
$Description: Операция конвертации ц/б. Шаг зачисления лотов
*/

import InsCarryDoc, "sp_car.mac", RsbDataSet, "SpRepFun.mac";

macro ExecuteStep( Doc, FDoc, DocKind, OperationID )

  record comm( dl_comm );
  var Query, DataSet, Query2, DataSet2, StatusKind, N, RqState, M, nom;
  var pmwrtsum = TRecHandler("pmwrtsum");
  var deal = TRecHandler("dl_tick");
  var leg1 = TRecHandler( "dl_leg" ), leg2 = TRecHandler( "dl_leg" );
  var fin = TRecHandler( "fininstr" );
  var rq = TRecHandler("dlrq.dbt");

  SetBuff( comm, FDoc );

  Query = RSDCommand(
                     " SELECT Count(1) v_Count " + 
                     "   FROM dscdlfi_dbt " + 
                     "  WHERE T_DEALKIND = ? " +
                     "    AND T_DEALID   = ? "
                    );

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, comm.DocumentID );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if ((not DataSet.MoveNext()) or (DataSet.v_Count < 1) )
     N = 0;
  else
     N = DataSet.v_Count;
  end;

  //M = count (*) по всем scdlpmwr с State == Поставлен с KIND = 1 (наш банк)
  Query = RSDCommand(
                     " SELECT Count(1) v_Count " + 
                     "   FROM dscdlpmwr_dbt " + 
                     "  WHERE T_DEALKIND = ? " +
                     "    AND T_DEALID   = ? " +
                     "    AND t_KIND     = 1 " +
                     "    AND t_State    = ? "
                    );

  Query.addParam( "", RSDBP_IN, comm.DocKind );
  Query.addParam( "", RSDBP_IN, comm.DocumentID );
  Query.addParam( "", RSDBP_IN, SCDLPMWR_STATE_READY );
  Query.execute();

  DataSet = TRsbDataSet(Query);

  if ((not DataSet.MoveNext()) or (DataSet.v_Count < 1) )
     M = 0;
  else
     M = DataSet.v_Count;
  end;

  if (N == 1)
     Query = RSDCommand(
                        " SELECT rq.T_ID, s.t_NewAmount, s.t_OldAmount, s.t_NewFIID, rq.t_DocID, l.t_Buy_Sale " + 
                        "   FROM DPMWRTSUM_DBT L, DDLRQ_DBT rq, DSCDLPMWR_DBT S " + 
                        "  WHERE S.T_DEALKIND    = ? " +
                        "    AND S.T_DEALID      = ? " +
                        "    AND S.t_State       = ? " +
                        "    AND S.T_PARTY       = -1 " +
                        "    AND S.t_SumID       = L.t_SumID " +
                        "    AND L.T_DOCKIND     = 29 " +
                        "    AND L.T_DOCID       = rq.t_ID "
                       );

     Query.addParam( "", RSDBP_IN, comm.DocKind );
     Query.addParam( "", RSDBP_IN, comm.DocumentID );
     Query.addParam( "", RSDBP_IN, SCDLPMWR_STATE_NOT_READY );
     Query.execute();

     DataSet = TRsbDataSet(Query);
     while (DataSet.MoveNext())

        ПолучитьТО(DataSet.ID, rq);
        Query2 = RSDCommand(  " SELECT NVL(rqhist1.t_State, "+DLRQ_STATE_UNKNOWN+") RqState "
                            + "   FROM V_RQHIST rqhist1 "
                            + "  WHERE rqhist1.t_RQID = ? "
                            + "    AND rqhist1.t_Instance = (SELECT max(rqhist2.t_Instance) "
                            + "                                FROM V_RQHIST rqhist2 "
                            + "                               WHERE rqhist2.t_RQID = rqhist1.t_RQID "
                            + "                                 AND rqhist2.t_Instance < (SELECT min(rqhist3.t_Instance) "
                            + "                                                             FROM V_RQHIST rqhist3 "
                            + "                                                            WHERE rqhist3.t_RQID = rqhist2.t_RQID "
                            + "                                                              AND rqhist3.t_State = " + DLRQ_STATE_GO_PREP
                            + "                                                          )"
                            + "                             )"
                          );

        Query2.addParam( "", RSDBP_IN, DataSet.ID );
        Query2.execute();

        DataSet2 = TRsbDataSet(Query2);
        if (DataSet2.MoveNext() and (DataSet2.RqState != DLRQ_STATE_UNKNOWN))
           rq.rec.State = DataSet2.RqState;
        end;

        if (rq.rec.Amount == DataSet.OldAmount)
           rq.rec.Amount = DataSet.NewAmount;
        end;

        rq.rec.FIID = DataSet.NewFIID;

        if(DL_ChangeDLRQ(rq, comm.CommDate, DLRQ_ACTION_UPDATE) != 0)
          msgbox("Ошибка при обновлении ТО с ID = " + rq.rec.ID);
          return 1;
        end;

        ПолучитьФинИн( DataSet.NewFIID, fin );

        GetDealByID( DataSet.DocID, NULL, NULL, deal );
        if(deal.rec.DealID != 0)
          GetDlLegByDealID( Deal.rec.DealID, LEG_KIND_DL_TICK, NULL, NULL, leg1);
          if(leg1.rec.ID != 0)
            GetDlLegByDealID( Deal.rec.DealID, LEG_KIND_DL_TICK_BACK, NULL, NULL, leg2);

            pmwrtsum.rec.Buy_Sale = DataSet.Buy_Sale;

            if(GetLegKindByLotDeal(pmwrtsum.rec, deal.rec) == LEG_KIND_DL_TICK)
              leg1.rec.PFI = DataSet.NewFIID;

              if (fin.rec.IsGeneralized)
                 leg1.rec.DeliveringFIID = -1;
              else
                 leg1.rec.DeliveringFIID = DataSet.NewFIID;
              end;

              leg1.rec.Principal = DataSet.NewAmount;

              leg1.rec.NKD = НКД(leg1.rec.PFI, leg1.rec.Principal, comm.EndDate);
              leg1.rec.Cost = leg1.rec.TotalCost - leg1.rec.NKD;

              leg1.rec.Price = leg1.rec.Cost/leg1.rec.Principal;
              if(leg1.rec.RelativePrice == "X") // Цена задана в %
                 nom = GetFaceValue(leg1.rec.PFI, comm.EndDate);
                 if (nom != 0)
                    leg1.rec.Price = leg1.rec.Price / nom * 100.0;
                 end;
              end;
              leg1.rec.Price = Round(leg1.rec.Price, leg1.rec.Point);

            else
              leg2.rec.PFI = DataSet.NewFIID;

              if (fin.rec.IsGeneralized)
                 leg2.rec.DeliveringFIID = -1;
              else
                 leg2.rec.DeliveringFIID = DataSet.NewFIID;
              end;

              leg2.rec.Principal = DataSet.NewAmount;

              leg2.rec.NKD = НКД(leg2.rec.PFI, leg2.rec.Principal, comm.EndDate);
              leg2.rec.Cost = leg2.rec.TotalCost - leg2.rec.NKD;

              leg2.rec.Price = leg2.rec.Cost/leg2.rec.Principal;
              if(leg2.rec.RelativePrice == "X") // Цена задана в %
                 nom = GetFaceValue(leg2.rec.PFI, comm.EndDate);
                 if (nom != 0)
                    leg2.rec.Price = leg2.rec.Price / nom * 100.0;
                 end;
              end;
              leg2.rec.Price = Round(leg2.rec.Price, leg2.rec.Point);
            end;

            if( OprInsertSPTKCHNG( comm.CommDate, SPTKCHNG_GLOBALCONV, deal, leg1, leg2 ) == false )
              msgbox("Ошибка при обновлении сделки с ID = " + deal.rec.DealID);
              return 1;
            end;
          else
            msgbox("Ошибка при поиске транша для сделки с ID = " + DataSet.DocumentID);
            return 1;
          end;
        else
          msgbox("Ошибка при поиске сделки с ID = " + DataSet.DocumentID);
          return 1;
        end;
     end;
  end;


  if ( ((comm.Flag1 == SET_CHAR) and 
        (M > 0) and 
        (N == 1)
       ) or
       ((comm.Flag3 == SET_CHAR) and 
        (M > 0)
       ) or
       (ВестиВнутреннийУчет())
     )
     StatusKind = SP_OPERSTATUS_GLOBALOPER_SO_CARRYIN;
  else
     StatusKind = SP_OPERSTATUS_GLOBALOPER_SO_END;
  end;

  if( not InsertOprStatus( SP_OPERSTATUS_GLOBALOPER_KIND_SO, StatusKind) ) 
     msgbox( "Ошибка при установке статуса операции" );
     return 1;
  end;

  return 0;
end; 
