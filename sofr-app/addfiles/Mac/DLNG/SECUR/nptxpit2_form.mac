/*
$Name:        nptxpit2_form.mac
$Module:      Ценные бумаги
$Description: Форма ввода данных для отчета "2-НДФЛ по ц/б"
*/
import "DlRepForm_h.mac", "globals.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_TXPIT2_ENDDATE      = 0,
      PNFLD_TXPIT2_INVESTORCODE = 1,
      PNFLD_TXPIT2_INVESTORNAME = 2,
      PNFLD_TXPIT2_CONTR_IIS    = 3,
      PNFLD_TXPIT2_CONTR_OTHER  = 4,
      PNFLD_TXPIT2_SING_NUM     = 5,
      PNFLD_TXPIT2_NUMBER_OPER  = 6,
      PNFLD_TXPIT2_SIGN1        = 7,
      PNFLD_TXPIT2_SIGN2        = 8,
      TXFLD_TXPIT2_ISCLIENT     = 9,
      PNFLD_TXPIT2_PRINTMETHOD  = 10,
      PNFLD_TXPIT2_XMLINONEFILE = 11;

CONST H_PNFLD_XMLINONEFILE = 100;
CONST H_PNFLD_NUMBER_OPER = 110;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  end; 
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

PRIVATE MACRO NumOperFlag_ChecBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     FldShowValue = FldRealValue = UNSET_CHAR;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != SET_CHAR )
           FldShowValue = FldRealValue = SET_CHAR;
           EnableFields( pThis.Data(), PNFLD_TXPIT2_NUMBER_OPER );
        else
           FldShowValue = FldRealValue = "";
           DisableFields( pThis.Data(), PNFLD_TXPIT2_NUMBER_OPER );
        end;
     end;
  end;
  return CM_DEFAULT;
END;

MACRO DL_FldProc_CheckBox_IsClient( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if( FldRealValue != "X" )
           FldShowValue = FldRealValue = "X";
        else
           FldShowValue = FldRealValue = "";
        end;
     end;
  end;

  if(FldRealValue == SET_CHAR)
     pThis.SetLinkedValue( DL_PNFLD_PRINTMETHOD, DL_OUTREPORT_EXCEL);
     DisableFields( pThis.Data(), PNFLD_TXPIT2_PRINTMETHOD);
  else
     EnableFields( pThis.Data(), PNFLD_TXPIT2_PRINTMETHOD);
  end;
  
  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_EndDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, BeginDate, EndDate, Period, PeriodYear;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = {curdate};
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     DateSplit( FldRealValue, null, Month, Year);
     /*если есть поле "начальная дата, то год относится к нему"*/
     if( pThis.ExistField(DL_PNFLD_YEAR) AND (pThis.ExistField(DL_PNFLD_BEGINDATE) == false) )
        pThis.SetLinkedValue( DL_PNFLD_YEAR, Year );
     end;
     if( pThis.ExistField(DL_PNFLD_PERIOD) )

        Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);
        if( Period > 12 ) /*указан квартал*/
           Period = Period - 12;
           /*если дата не входит в заданный квартал - переставить период*/
           if( (Month < Period*3-2) OR (Month > Period*3) )
              pThis.SetLinkedValue( DL_PNFLD_PERIOD, ((Month - 1)/3 + 1) + 12 );
           end;
        elif( Period != Month )
           pThis.SetLinkedValue( DL_PNFLD_PERIOD, Month);
        end;
     end;
     if( pThis.ExistField(DL_PNFLD_YEAR) )
       PeriodYear = pThis.GetLinkedValue(DL_PNFLD_YEAR);
       if(Year != PeriodYear)
         pThis.SetLinkedValue( DL_PNFLD_YEAR, Year);
       end;   
     end;
     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( FldRealValue < pThis.GetLinkedValue(DL_PNFLD_BEGINDATE) )
           MsgBox( "Дата окончания не может быть меньше даты начала периода.");
           return CM_IGNORE;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  if(FldRealValue >= date(1, 1, 2018))
     EnableFields( pThis.Data(), TXFLD_TXPIT2_ISCLIENT);
  else
     DisableFields( pThis.Data(), TXFLD_TXPIT2_ISCLIENT);
  end;

  rep_EndDate = FldRealValue;
  
  return CM_DEFAULT;
END;

PRIVATE MACRO OperNumber( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT )
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue = "";
    DisableFields( pThis.Data(), PNFLD_TXPIT2_NUMBER_OPER );
  end; 
  
  if( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
  end;

  FldRealValue = FldShowValue;

  return CM_DEFAULT;
END;


MACRO FldProc_NpTxPrintMethod( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  MACRO Name( Id:INTEGER )
     if( Id == DL_OUTREPORT_EXCEL )
        return "Excel";
     elif( Id == DL_OUTREPORT_XML )
        return "XML";
     end;                                           
     return "";
  END;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = DL_OUTREPORT_EXCEL;
     end;
     FldShowValue = Name(FldRealValue);

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    if(FldRealValue == DL_OUTREPORT_XML)
      EnableFields( pThis.Data(), PNFLD_TXPIT2_XMLINONEFILE );
    else
      pThis.SetLinkedValue( H_PNFLD_XMLINONEFILE, UNSET_CHAR);
      DisableFields( pThis.Data(), PNFLD_TXPIT2_XMLINONEFILE );
    end; 
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                    Name(DL_OUTREPORT_EXCEL),DL_OUTREPORT_EXCEL,
                    Name(DL_OUTREPORT_XML),  DL_OUTREPORT_XML
                  );
  end;
  return CM_DEFAULT;
END;


CLASS (DL_CPanel) NPTX_Pit2_Panel()

  PRIVATE MACRO Init()
     VAR Year, BegDate, RepDate;

     DateSplit( {curdate}, null, null, Year );

     RepDate = date(31, 12, Year-1);

     Report_Kind = "NPTXPIT2";
     rep_EndDate = RepDate;
                                                                        
     AddFieldProc( 0, PNFLD_TXPIT2_ENDDATE,      DL_PNFLD_ENDDATE,         @DL_FldProc_EndDate, RepDate );
     AddFieldProc( 0, PNFLD_TXPIT2_INVESTORCODE, DL_PNFLD_CLIENT_SDD_CODE, @DL_FldProc_Client_SDD_Code );
     AddFieldProc( 0, PNFLD_TXPIT2_INVESTORNAME, DL_PNFLD_CLIENT_SDD_NAME, @Default );

     AddFieldProc( 0, PNFLD_TXPIT2_CONTR_IIS,    DL_PNFLD_CHECKBOX,        @DL_FldProc_CheckBox, UNSET_CHAR );
     AddFieldProc( 0, PNFLD_TXPIT2_CONTR_OTHER,  DL_PNFLD_CHECKBOX,        @DL_FldProc_CheckBox, SET_CHAR );

     AddFieldProc( 0, PNFLD_TXPIT2_SING_NUM,     DL_PNFLD_CHECKBOX,        @NumOperFlag_ChecBox, UNSET_CHAR );
     AddFieldProc( 0, PNFLD_TXPIT2_NUMBER_OPER,  H_PNFLD_NUMBER_OPER,      @OperNumber, "" );


     AddFieldProc( 0, PNFLD_TXPIT2_SIGN1,        DL_PNFLD_CHECKBOX,        @DL_FldProc_CheckBox, SET_CHAR );
     AddFieldProc( 0, PNFLD_TXPIT2_SIGN2,        DL_PNFLD_CHECKBOX,        @DL_FldProc_CheckBox, SET_CHAR );

     AddFieldProc( 0, TXFLD_TXPIT2_ISCLIENT,     DL_PNFLD_CHECKBOX,        @DL_FldProc_CheckBox_IsClient, SET_CHAR );

     AddFieldProc( 0, PNFLD_TXPIT2_PRINTMETHOD,  DL_PNFLD_PRINTMETHOD,     @FldProc_NpTxPrintMethod, DL_OUTREPORT_EXCEL, 36, 15 );

     AddFieldProc( 0, PNFLD_TXPIT2_XMLINONEFILE, H_PNFLD_XMLINONEFILE,     @DL_FldProc_CheckBox, UNSET_CHAR );
  END;

  PRIVATE MACRO Check():BOOL
     if( (this.GetFieldValue( PNFLD_TXPIT2_SIGN1 ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_TXPIT2_SIGN2 ) == UNSET_CHAR) )
        msgbox("Необходимо задать хотя бы один из признаков справки");
        return false;
     end;

     if( (this.GetFieldValue( PNFLD_TXPIT2_SING_NUM ) == SET_CHAR) and
         (this.GetFieldValue( PNFLD_TXPIT2_NUMBER_OPER ) == "") )
        msgbox("Необходимо задать номер");
        return false;
     end;

     if( (this.GetFieldValue( PNFLD_TXPIT2_CONTR_IIS ) == UNSET_CHAR) and
         (this.GetFieldValue( PNFLD_TXPIT2_CONTR_OTHER ) == UNSET_CHAR) )
        msgbox("Необходимо задать хотя бы один из видов договоров");
        return false;
     end;
     return true;
  END;

  initDL_CPanel( "sp_rep.lbr", "NPTXPIT2" ); 
END;
