/**
  @file RequestSnob_funcobj.mac
  @brief Макрос выполнения процедур запроса в хранилище СНОБ, сгенерированных обще системной плановой процедурой "Выполнение процедуры запроса к хранилищу СНОБ"

  #link
  - [BOSS-187.4_Разработка планировщика запроса к Хранилищу СНОБ] (https://sdlc.go.rshbank.ru/confluence/pages/viewpage.action?pageId=248463674)

  #changeLog
  |date      |author       |tasks    |note                                                                                                          
  |----------|-------------|---------|--------------------------------------------------------------------------------------------------------------
  |26.01.2024|Хромеев Д. С.|BOSS-1819|BOSS-253_Разработка планировщика запроса к Хранилищу СНОБ. Разработка
*/

import OprInter, DealsInter, BankInter, "FuncObj.mac", "dlquery.mac";

/**
  @brief Функция определения системной даты и времени
*/
private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;


MACRO Exec_RequestSnobStor(param0, param1, param2)
   var err = 0;
   var ErrStr = "";
   var ReturnObj = FuncObjResult(FUNCOBJ_RESULT_OK, "", 0);
   var OldDialogFlag = SetDialogFlag(0);
   var SystDate, SystTime;

   GetSystDateTime(@SystDate, @SystTime);

   // Проверка 1: В текущем системном дне процедура запроса данных к хранилищу СНОБ еще не отрабатывала. (задание планировщика к данному моменту
   // уже могло выполнятся, но без создания и выполненияпроцедуры запроса)
   var QueryProcExist = "select 1 " +
                        "from dnptxop_dbt op " +
                        "where op.t_DocKind = " + string(DL_GETSNOBPROC) +
                        "  and op.t_Client = -1 " +
                        "  and op.t_OperDate = ? " +
                        "  and op.t_Status = " + string(DL_COMM_CLOSED);

   var CmdProcExist = DL_RSDCommand(QueryProcExist);
   CmdProcExist.AddParam(SystDate);

   if(CmdProcExist.GetCount() == 0)
      err = DL_GetSNOBProcExec(param1, ErrStr);
   else
      err = 1;
      ErrStr = "В текущем системном дне ранее уже была выполнена процедура запроса к хранилищу СНОБ. Новая процедура запроса к хранилищу СНОБ не выполнена.";
   end;

   if((err != 0 ) OR (ErrStr != ""))
      ReturnObj.state = FUNCOBJ_RESULT_ERROR;
      ReturnObj.errorText = ErrStr;
      ReturnObj.errorCode = err;
   end;
   SetDialogFlag(OldDialogFlag);

   return ReturnObj;
END;
