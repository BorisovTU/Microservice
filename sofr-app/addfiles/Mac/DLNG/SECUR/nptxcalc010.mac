/*
$Name:        nptxcalc010.mac
$Module:      Ценные бумаги
$Description: Расчет НОБ для НДФЛ. Шаг "Инициализация"
*/
Import InsCarryDoc, DealsInter, "secinter.mac", "nptxcalcfun.mac";

Private macro InsertLogOper(ID, mes_ );
  var mes = mes_+ "OperDprt = " + {OperDprt} + " Oper = " + {Oper};
  Var timeOp = string(date()) + " " + string(time());
  var InsertQuery = "INSERT INTO DDPACCREPLOG_DBT VALUES(?, ?, ?)";
  var InsertSelect = RSDCommand(InsertQuery);
  InsertSelect.AddParam("", RSDBP_IN, timeOp);
  InsertSelect.AddParam("", RSDBP_IN, ID);
  InsertSelect.AddParam("", RSDBP_IN, mes);
  InsertSelect.execute();
end;

private macro Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
end;

/* клиент - на обслуживании "Фондовый дилинг", "Депозитарное обслуживание", "СВО" или "Срочные контракты" */
private macro ClientSD( PartyID:integer ):bool
  var RetVal:bool = false;
  var Query, SqlQuery, Client;

  Query = "select t_PartyID " +
          "  from dclient_dbt " +
          " where t_ServiceKind in(?,?,?,?) " +
//          "   and t_Department = ? " +
          "   and t_Branch = 0 " +
          "   and t_PartyID = ?";

  SqlQuery = RSDCommand(Query);
  SqlQuery.addParam("", RSDBP_IN, PTSK_STOCKDL);
  SqlQuery.addParam("", RSDBP_IN, PTSK_DEPOS);
  SqlQuery.addParam("", RSDBP_IN, PTSK_SVO);
  SqlQuery.addParam("", RSDBP_IN, PTSK_DV);
//  SqlQuery.addParam("", RSDBP_IN, {OperDprt});
  SqlQuery.addParam("", RSDBP_IN, PartyID);
  SqlQuery.execute();
  Client = TRsbDataSet(SqlQuery);

  if( Client.MoveNext() )
     RetVal = true;
  end;
  return RetVal;
end;

/* для клиента сформирован НДР за отчетный период  */
private macro ClientHasNPTXObject( PartyID:integer, EndDate:date ):bool
  var RetVal:bool = false;
  var Query, SqlQuery, NPTXObj;
  var BeginDate, CurrYear;
  DateSplit(EndDate, null, null, CurrYear);
  BeginDate = date(1,1,CurrYear);

  Query = "select 1 " +
          "  from dnptxobj_dbt " +
          " where T_DATE BETWEEN ? AND ? " +
          "   and t_Client = ?";

  SqlQuery = RSDCommand(Query);
  SqlQuery.addParam("", RSDBP_IN, BeginDate);
  SqlQuery.addParam("", RSDBP_IN, EndDate);
  SqlQuery.addParam("", RSDBP_IN, PartyID);
  SqlQuery.execute();
  NPTXObj = TRsbDataSet(SqlQuery);

  if( NPTXObj.MoveNext() )
     RetVal = true;
  end;
  return RetVal;
end;

macro ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )
  VAR OperationStatus;
  VAR Oper = TRecHandler( "nptxop.dbt" );
  var query, cmd, DataSet;
  var stat = 0;
  VAR TaxPeriod = 0;

  Oper.SetRecordAddr( FDoc );

  datesplit(Oper.rec.OperDate, NULL, NULL, TaxPeriod);

  var TaxTeriodToLucre = CheckTaxTeriodToLucre(TaxPeriod);

  if(Oper.rec.Recalc == UNSET_CHAR)

    /*Если в заданном налоговом периоде есть операции "Расчет НОБ" (с признаком или без признака "Пересчет") в статусе "Открыта", 
      то выводим Пользователю сообщение вида: "У клиента есть открытая операция расчета НОБ. 
      Текущий расчет не возможен". Операцию не закрываем. Сообщение выводим в протокол.
    */
    query =   " select 1"
            + "   from dnptxop_dbt op "
            + "  where op.t_DocKind = " + DL_CALCNDFL
            + "    and op.t_Client = ? "
            + "    and op.t_Contract = ? "
            + "    and op.t_IIS = " + IIF(Oper.rec.IIS == SET_CHAR, "CHR(88)", "CHR(0)")
            + IIF(Oper.rec.IIS == SET_CHAR, " ", " and EXTRACT(YEAR FROM op.t_OperDate) = ? ")
            + "    and op.t_Status = " + DL_TXOP_Open
            + "    and op.t_ID <> ? "   
            + "    and ROWNUM = 1";

    cmd = DL_RSDCommand();
    cmd.AddParam(Oper.rec.Client);
    cmd.AddParam(Oper.rec.Contract);
    if(Oper.rec.IIS == UNSET_CHAR) //DEF-100723
      cmd.AddParam(TaxPeriod);
    end;
    cmd.AddParam(Oper.rec.ID);

    if(TaxTeriodToLucre)
      query = query + "    and op.t_SubKind_Operation " + IIF(Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_LUCRE, " = ", " <> ") + DL_TXBASECALC_OPTYPE_LUCRE;
    end;

    if(cmd.GetCount(query) > 0)
      Error( "У клиента есть открытая операция расчета НОБ. Текущий расчет не возможен" );
      DL_NPTX_SaveOperLog( Oper.rec.ID, 10 );
      return 0;
    end; 

    if(stat == 0)
      /*Если в заданном налоговом периоде есть операция "Расчет НОБ" (с признаком или без признака "Пересчет") в статусе "Отложена" и дата такой операции меньше или равна дате текущей операции,  
        то выводим Пользователю сообщение вида: "У клиента есть отложенная операция расчета НОБ, которую необходимо завершить или удалить".  
          операцию не закрываем. Сообщение выводим в протокол.
      */
      query =   " select 1"
              + "   from dnptxop_dbt op "
              + "  where op.t_DocKind = " + DL_CALCNDFL
              + "    and op.t_Client = ? "
              + "    and op.t_Contract = ? "
              + "    and op.t_IIS = " + IIF(Oper.rec.IIS == SET_CHAR, "CHR(88)", "CHR(0)")
              + "    and op.t_OperDate <= ? "
              + IIF(Oper.rec.IIS == SET_CHAR, " ", " and EXTRACT(YEAR FROM op.t_OperDate) = ? ")
              + "    and op.t_Status = " + DL_TXOP_Prep
              + "    and ROWNUM = 1";

      cmd = DL_RSDCommand();
      cmd.AddParam(Oper.rec.Client);
      cmd.AddParam(Oper.rec.Contract);
      cmd.AddParam(Oper.rec.OperDate);
      if(Oper.rec.IIS == UNSET_CHAR) //DEF-100723
        cmd.AddParam(TaxPeriod);
      end;

      if(TaxTeriodToLucre)
        query = query + "    and op.t_SubKind_Operation " + IIF(Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_LUCRE, " = ", " <> ") + DL_TXBASECALC_OPTYPE_LUCRE;
      end;

      if(cmd.GetCount(query) > 0)
        Error("У клиента есть отложенная операция расчета НОБ, которую необходимо завершить или удалить.");
        DL_NPTX_SaveOperLog( Oper.rec.ID, 10 );
        return 0;
      end;
    end;
  end;

  if(Oper.rec.Recalc == SET_CHAR)
    cmd = DL_RSDCommand();

    query =   " select 1 "
            + "   from dnptxop_dbt "
            + "  where t_Status = " + DL_TXOP_Open
            + "    and t_dockind = " + DL_CALCNDFL
            + "    and t_Client = ? " 
            + "    and t_Contract = ? "
            + "    and t_id <> ? "
            + "    and EXTRACT(YEAR FROM t_OperDate) = EXTRACT(YEAR FROM ?) ";

    cmd.addParam(Oper.rec.Client);
    cmd.addParam(Oper.rec.Contract);
    cmd.addParam(Oper.rec.ID);
    cmd.addParam(Oper.rec.OperDate);

    if(TaxTeriodToLucre)
      query = query + "    and t_SubKind_Operation " + IIF(Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_LUCRE, " = ", " <> ") + DL_TXBASECALC_OPTYPE_LUCRE;
    end;

    var cntOp = cmd.GetCount(query);

    if(cntOp > 0)
      Error( "У клиента есть открытая операция расчета НОБ. Текущий расчет не возможен" ); 
      DL_NPTX_SaveOperLog( Oper.rec.ID, 10 );
      return 0;
    end;

    cmd = DL_RSDCommand();

    query =   " select 1 "
            + "   from dnptxop_dbt "
            + "  where t_Status = " + DL_TXOP_Prep
            + "    and t_dockind = " + DL_CALCNDFL 
            + "    and t_Client = ? " 
            + "    and t_Contract = ? "
            + "    and t_id <> ? "
            + "    and t_OperDate <= ? "
            + "    and EXTRACT(YEAR FROM t_OperDate) = EXTRACT(YEAR FROM ?)";

    cmd.addParam(Oper.rec.Client);
    cmd.addParam(Oper.rec.Contract);
    cmd.addParam(Oper.rec.ID);
    cmd.addParam(Oper.rec.OperDate);
    cmd.addParam(Oper.rec.OperDate);

    if(TaxTeriodToLucre)
      query = query + "    and t_SubKInd_Operation " + IIF(Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_LUCRE, " = ", " <> ") + DL_TXBASECALC_OPTYPE_LUCRE;
    end;

    cntOp = cmd.GetCount(query);

    if(cntOp > 0)
      Error( "У клиента есть отложенная операция расчета НОБ, которую необходимо завершить или удалить" );
      DL_NPTX_SaveOperLog( Oper.rec.ID, 10 );
      return 0;
    end;
  end;

  if(stat == 0)
      /*5. Если НЕ ИИС и операция расчета НОБ для НДФЛ запускается с типом <обычный расчет> без признака <Пересчет> И не вызвана
           безынтерфейсно из операции зачисления/ списания д/с с типом <списание д/с>, то на операции автоматически
           устанавливается категория  <Признак технического расчета> = Технический
           Если в самой операции усановлен признак технического расета, то переносим его в категорию (пока это только для ИИС может быть).
           */
    if(    ((Oper.rec.IIS == UNSET_CHAR) and (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_NORMAL) and (Oper.rec.Recalc != SET_CHAR) and (IsChildOp(Oper.rec.DocKind, Oper.rec.ID) == false))
        or (Oper.rec.Technical == SET_CHAR)
      )
       //Проверяем, если категория уже установлена, то её значение не меняем
       var CategExistsQuery = "select objatcor.t_AttrID " +
                              "from dobjatcor_dbt objatcor " +
                              "where objatcor.t_ObjectType = " + string(OBJTYPE_NPTXCALC) +
                              "  and objatcor.t_GroupID = 1 " +
                              "  and objatcor.t_Object = ? ";

       var CategExistsCmd = DL_RSDCommand(CategExistsQuery);
       CategExistsCmd.AddParam(UniID(Oper, OBJTYPE_NPTXCALC, Oper.rec.DocKind));

       var CategExistsDS = CategExistsCmd.Execute();
       if(not CategExistsDS.MoveNext())
          if(not ConnectCategory( OBJTYPE_NPTXCALC,
                                  1,
                                  Oper.rec.ID,
                                  false,
                                  1/*Технический*/, "", "" ))
            DL_NPTX_PutMsg( "Ошибка при сохранении категории 'Признак технического расчета'", 20 );
          end;
       end;
    end;

    if(TaxTeriodToLucre == true)
      if( (Oper.rec.Subkind_Operation == DL_TXBASECALC_OPTYPE_LUCRE) and (Oper.rec.Recalc != SET_CHAR))
        if( not InsertOprStatus( 46052 /*Расчет НОБ*/, 3 /*Расчет НДР1*/) )
           stat = Error( "Ошибка при установке статуса вида \"Расчет НДР1\" операции" );
        end;
      end;
    elif( Oper.rec.Subkind_Operation == DL_TXBASECALC_OPTYPE_LUCRE )
       InsertLogOper(Oper.rec.ID, "Error1 ");
       stat = Error( "Данный тип расчета не поддерживается" );
    end;

    if(stat == 0)
      if( not (ClientSD(Oper.rec.Client) or ClientHasNPTXObject(Oper.rec.Client, Oper.rec.OperDate) ) )
         InsertLogOper(Oper.rec.ID, "Error2 ");
         stat = Error( "В операции указан клиент с неверным видом обслуживания" );
      end;

      if(stat == 0)
        InsertLogOper(Oper.rec.ID, "");

        if(Oper.rec.Subkind_Operation == DL_TXBASECALC_OPTYPE_DIVIDEND)
           stat = RecalcDividendTaxObjects(Oper, Oper.rec.ID, ID_Step);

           if ( stat == 0 )
              stat = DL_NPTX_StartInsertTaxObject(Oper.rec.ID, ID_Step); 
           end;
        end;
      end;
    end;

    if((stat == 0) and (Oper.rec.Recalc == SET_CHAR) and (Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_LUCRE) and (TaxTeriodToLucre == true))
      msgbox("Для корректного создания НДР после пересчета НОБ по мат. выгоде необходимо провести пересчет НОБ с типом \"обычный расчет\" или \"окончание года\"");
    end;
  end;

  DL_NPTX_SaveOperLog( Oper.rec.ID, 10 );

  return stat;
end;

MACRO PostStep(CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
               errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
               FDoc,          /* Указатель на первичный документ */
               ID_Operation,  /* Номер экземпляра операции */
               Num_Step,      /* Номер шага операции(из настроек) */
               Kind_Operation,/* Вид операции */
               KindDoc,       /* Вид первичного документа */
               KindStep,      /* Вид шага операции */
               ID_Step)       /* Номер шага операции */

   var Oper = TRecHandler("nptxop.dbt");

   Oper.SetRecordAddr(FDoc);

   
END;