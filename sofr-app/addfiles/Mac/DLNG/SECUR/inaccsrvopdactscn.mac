/*Массовое выполнение действий над действиями по операции внутреннего учета БОЦБ*/
IMPORT SPInter, DealsInter;   
 
PRIVATE VAR GrDoc = TRecHandler( "dlgrdoc.dbt" );

PRIVATE CONST ERROR_EXEC = 0,
              PRINT_HEADER = 1,
              PRINT_FOOTER = 2;

PRIVATE MACRO PrintHead( Head:STRING )
[
                   #
┌──────────────────────────┬──────┬───────────────────────────────────────────────────────────────────────────────────────────────────┐
│             ID           │  №   │                      Сообщение                                                                    │
│           действия       │ошибки│                                                                                                   │
├──────────────────────────┼──────┼───────────────────────────────────────────────────────────────────────────────────────────────────┤](Head);
END;


PRIVATE MACRO PrintFooter()
[└──────────────────────────┴──────┴───────────────────────────────────────────────────────────────────────────────────────────────────┘
];

END;

PRIVATE MACRO PrintLine( ErrStatus:INTEGER, ErrMessage:STRING )
[│##########################│######│###################################################################################################│]
( GrDoc.rec.ID:w, ErrStatus, ErrMessage:w );
END;


PRIVATE MACRO DeleteGrDocFromOper( NumExec:INTEGER, ErrStatus:INTEGER, ErrMessage:STRING )

  if( NumExec == PRINT_HEADER )
     PrintHead( "Отчет о массовом удалении действий из операции внутреннего учета БОЦБ." );
  elif( NumExec == PRINT_FOOTER )            
     PrintFooter();
  elif( NumExec == ERROR_EXEC )
     if( ErrStatus == 0 ) 
       ErrMessage = "Действие из операции удалено успешно.";
     end;
     PrintLine( ErrStatus, ErrMessage);
  end;
END;

/*   Action = 3 - удаление (F8 из скроллинга )
*/
MACRO Печать_ОтчетСканирования( NumExec:INTEGER, FDoc:VARIANT, ErrStatus:INTEGER, ErrMessage:STRING, Action:INTEGER )

  GrDoc.SetRecordAddr( FDoc );

  if( Action == 3 )
     DeleteGrDocFromOper( NumExec, ErrStatus, ErrMessage );
  end;

END;
