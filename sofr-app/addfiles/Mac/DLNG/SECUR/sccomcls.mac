/*
$Name:        sccomcls.mac
$Module:      Ценные бумаги
$Description: Комиссии по договору (сделке) - КЛАССЫ
 PROGRAMMED BY: Азарцов В. В.  
 CREATION DATE: //AV 23.04.04
*/
import "sprepfun.mac", CTInter, "scservop.mac", "sccomalg.mac";
import RsbDataSet;

private var Deals = TBFile ("dl_tick.dbt", "R", 6);
private file DefCom( sfdefcom ) key 1; 
private var CalCal = TBFile ("sfcalcal.dbt", "R", 0);

private var rSfSi1 = TRecHandler( "sfsi.dbt" );
private var rSfSi2 = TRecHandler( "sfsi.dbt" );


/* проверим на совпадение СПИ ПЗО для кредита                                    */
/* если один из сравниваемых СПИ имеет незаполненные реквизиты корреспондента -  */
/* считаем, что они совпадают, при этом если не заполнен найденный, то переносим */
/* в него эти реквизиты                                                          */
MACRO CheckSfSI( SfSI, SfSI_In )
   if( (SfSI.rec.PartyID == SfSI_In.rec.PartyID) AND
       (SfSI.rec.FIID == SfSI_In.rec.FIID) AND
       (SfSI.rec.Account == SfSI_In.rec.Account) AND
       (SfSI.rec.BankID == SfSI_In.rec.BankID) )

       if( ((SfSI.rec.CorrAcc    == SfSI_In.rec.CorrAcc) OR (SfSI_In.rec.CorrAcc == "") OR (SfSI.rec.CorrAcc == "")) OR 
           ((SfSI.rec.BankCorrID == SfSI_In.rec.BankCorrID) OR (SfSI_In.rec.BankCorrID == -1) OR (SfSI.rec.BankCorrID == -1) ) )

          if( SfSI.rec.CorrAcc == "" )
             SfSI.rec.CorrAcc = SfSI_in.rec.CorrAcc;
          end;
          if( SfSI.rec.BankCorrID == -1 )
             SfSI.rec.BankCorrID = SfSI_in.rec.BankCorrID;
          end;
         
          return true;
       end;           
   end;
   return false;      
END;

private var sfcomissInc = TBFile ("sfcomiss.dbt", "R", 1);

/* через параметры возвращаются флажки: */
/* InPayDate   - есть комиссии с моментом расчета в дату оплаты сделки */
/* InStartDate - есть комиссии с моментом расчета в дату заключения сделки */
MACRO ПроверитьКорректностьНастроекКомиссии( ServDoc, rContr, rComiss, InPayDate:@bool, InStartDate:@bool ) 

   var   continueInc;
   var   SetDatePayDeal:bool, SetDateStartDeal:bool;
   var   SetDatePayDeal_Calc:bool, SetDateStartDeal_Calc:bool;

   /*проверяем установку момента расчета на комиссии*/               
   SetDatePayDeal_Calc = ПроверитьКатегориюКомиссии( rComiss, "DPD", MOMENT_CALC_COMISS );
   SetDateStartDeal_Calc = ПроверитьКатегориюКомиссии( rComiss, "DSD", MOMENT_CALC_COMISS );

   if( InPayDate != null )
      InPayDate   = SetDatePayDeal_Calc;
   end;
   if( InStartDate != null )
      InStartDate  = SetDateStartDeal_Calc;
   end;

   if( (SetDatePayDeal_Calc == false) AND (SetDateStartDeal_Calc == false) )
      ScOpInsertError( DL_STOCKCONTR, ServDoc, rContr, 1, "В настройках комиссии " + rComiss.Code + "| не указано, в какую дату должен выполняться расчет." );
      return false;
   end;              
   if( (SetDatePayDeal_Calc == true) AND (SetDateStartDeal_Calc == true) )
      ScOpInsertError( DL_STOCKCONTR, ServDoc, rContr, 1, "В настройках комиссии " + rComiss.Code + "| указаны две даты расчета." );
      return false;
   end;   
   if( SetDatePayDeal_Calc == true )
      /* если у комиссии "Момент расчета" = "В дату оплаты" то проверить,  */
      /* что у этой комиссии нет вложенных периодлических комиссий         */

      sfcomissInc.Clear();
      sfcomissInc.AddFilter("t_IncFeeType = "    + string(rComiss.FeeType) +
                       " and t_IncCommNumber = " + string(rComiss.Number)  +
                       " and t_FeeType = "       + string(SF_FEE_TYPE_PERIOD) /*есть вложенная периодическая*/
                           );
      sfcomissInc.Rewind;

      if( sfcomissInc.Next )   
         ScOpInsertError( DL_STOCKCONTR, ServDoc, rContr, 1, "В настройках комиссии " + rComiss.Code + "| указан момент расчета \"Дата оплаты сделки\".| При этом есть вложенные периодические комиссии в данную комиссию.| Такой режим работы не поддерживается." );
         return false;
      end;
      sfcomissInc.DropFilter();
   end;   

   /*проверяем установку момента взимания на комиссии*/               
   SetDatePayDeal = ПроверитьКатегориюКомиссии( rComiss, "DPD", MOMENT_GET_COMISS );
   SetDateStartDeal = ПроверитьКатегориюКомиссии( rComiss, "DSD", MOMENT_GET_COMISS );

   if( (SetDatePayDeal == false) AND (SetDateStartDeal == false) )
      ScOpInsertError( DL_STOCKCONTR, ServDoc, rContr, 1, "В настройках комиссии " + rComiss.Code + "| не указано, в какую дату должна выполняться оплата." );
      return false;
   end;              
   if( (SetDatePayDeal == true) AND (SetDateStartDeal == true) )
      ScOpInsertError( DL_STOCKCONTR, ServDoc, rContr, 1, "В настройках комиссии " + rComiss.Code + "| указаны две даты оплаты." );
      return false;
   end;


   /*  если у клиентской комиссии признак "Момент взимания" = "Дата заключения сделки" */
   /* а "Момент расчета" = "Дата оплаты сделки"                                        */
   /* выдать сообщение об ошибке "Нельзя оплачивать в дату заключения сделки комиссию, расчитываемую в дату оплаты"*/
   if( (SetDateStartDeal == true) AND (SetDatePayDeal_Calc == true) )
      ScOpInsertError( DL_STOCKCONTR, ServDoc, rContr, 1, "Некорректны настройки комиссии " + rComiss.Code + "| Нельзя оплачивать в дату заключения сделки комиссию,| расчитываемую в дату оплаты." );
      return false;
   end;

   return true;
END;


/*класс - суммы комиссий*/
CLASS CommSumma( InSum )
  
  var Sum;        /*сумма комиссии*/
  var NDS;        /*НДС с суммы комиссии*/
  var FIID;       /*валюта списания*/
  var PayFIID;    /*валюта оплаты комиссии*/
  var BaseFIID;   /*базовая валюта комиссии*/ 

  var NumComs;          /* кол-во просуммированных комиссии в текущем объекте             */ 
  var SfCom;            /* массив данных по комиссиям, просуммированных в текущем объекте */
  var CalcComissSumAlg; /* алгоритм пересчета сумм в платеже по комиссиям, если 0, то он в*/
                        /* просуммированных комиссиях разный или не задан                 */      

  var Paym;       /* платеж комиссии сформированный по текущему объекту       */
  var SfSI_Cred;  /* СПИ по комиссии - кредит, реквизиты счета комиссии по Кт */
  var SfSI_Deb;   /* СПИ по комиссии - дебет, реквизиты счета комиссии по Дт  */

  macro ClearCommSumma()
    this.NumComs  = 0;
    this.SfCom    = TArray;
    this.Sum      = this.NDS = $0;
    this.BaseFIID = this.PayFIID    = this.FIID = -1;
    this.Paym     = this.SfSI_Cred  = this.SfSI_Deb = null;  
  end;

  macro CopyCommSumma( InSum )
     var i;
 
     this.ClearCommSumma();

     this.NumComs = InSum.NumComs; 
     this.Sum     = InSum.Sum; 
     this.NDS     = InSum.NDS;
     this.BaseFIID= InSum.BaseFIID;
     this.PayFIID = InSum.PayFIID;
     this.FIID    = InSum.FIID;
     this.CalcComissSumAlg = InSum.CalcComissSumAlg;
   
     if( InSum.Paym != null ) 
        this.Paym = TRecHandler( "pmpaym.dbt" );
        copy( this.Paym, InSum.Paym ); 
     end;       
     if( InSum.SfCom != null ) 
        i = 0; 
        while( InSum.SfCom[i] != null ) 
           this.SfCom[i] = TRecHandler( "sfcomiss.dbt" );
           copy( this.SfCom[i], InSum.SfCom[i] ); 
           i = i + 1;
        end; 
     end; 
     if( InSum.SfSI_Cred != null ) 
        this.SfSI_Cred = TRecHandler( "sfsi.dbt" );
        copy( this.SfSI_Cred, InSum.SfSI_Cred ); 
     end; 
     if( InSum.SfSI_Deb != null ) 
        this.SfSI_Deb = TRecHandler( "sfsi.dbt" );
        copy( this.SfSI_Deb, InSum.SfSI_Deb ); 
     end; 
  end; 
  
  /*проверка - можно ли брать валюту оплаты из заданной комиссии*/  
  /* валюты оплат всех просуммированных в данном объекте комиссий должны совпадать*/ 
  macro CheckSfCom( _SfCom )
     var i = 0; 
     if( (_SfCom != null) AND (_SfCom.rec.FIID_PaySum != -1) )
        while( this.SfCom[i] != null )
           if( this.SfCom[i].rec.FIID_PaySum != _SfCom.rec.FIID_PaySum )  
              return false;
           end; 
           i = i + 1;
        end; 
        return true;
     end; 
     return false; 
  end; 

  /*получить валюту комиссии, в случае если нет данных в sfsi*/ 
  macro _GetComissFIID() 
     if( (this.NumComs > 0) AND (this.CheckSfCom( this.SfCom[this.NumComs-1] ) == true) )
        return this.SfCom[this.NumComs-1].rec.FIID_PaySum;   
     else
        return this.BaseFIID;
     end;  
  end;  

  /*проверяем алгоритмы пересчета сумм для всех комиссий, совпадают ли они c добавляемым*/ 
  /*если нет - зануляем алгоритм и пересчет потом выполняем по дате валютирования*/ 
  macro CheckCalcComissSumAlg( _sfcom )
     var i = 0, equal = true; 
     while( this.SfCom[i] != null )
        if( this.SfCom[i].rec.CalcComissSumAlg != _sfcom.rec.CalcComissSumAlg )  
           equal = false;
        end; 
        i = i + 1;
     end; 
     if( equal == false )
        this.CalcComissSumAlg = 0;
     else 
        this.CalcComissSumAlg = _sfcom.rec.CalcComissSumAlg;
     end; 
  end;     

  /* виды алгоритмов пересчета сумм в комиссиях */
  /*    SFCOMISS_CALCCOMISSSUMALG_CALC  =1,      - на день расчета   */
  /*    SFCOMISS_CALCCOMISSSUMALG_PAY   =2,      - на день оплаты    */
  /*    SFCOMISS_CALCCOMISSSUMALG_BEFOREPAY =3   - за день до оплаты */

  /* установим даты курсов пересчета сумм в платеже                  */ 
  macro SetRateDatesInPaym( DateCalc, DatePay, _paym )
     var paym;
     if( _paym != null ) 
        paym = _paym;
     else
        paym = this.paym; /*если платеж не передали, возьмем платеж - свойство объекта*/
     end;      

     if( this.CalcComissSumAlg == 1 )
        paym.BaseRateDate  = paym.RateDate = DateCalc;
     elif( this.CalcComissSumAlg == 2 )
        paym.BaseRateDate  = paym.RateDate = DatePay;
     elif( this.CalcComissSumAlg == 3 )
        paym.BaseRateDate  = paym.RateDate = DatePay - 1;
     else
        paym.BaseRateDate  = paym.RateDate = paym.ValueDate;
     end;         
  end;

  /*добавить сумму*/
  /* если переданы данные через _SfCom, _SfSI, _SfSI_Deb, то предполагается, что для всех       */ 
  /* комиссий при суммировании они заносятся в объект только один раз, даже данные по комиссии  */ 
  /* так как данные содержащиеся в них, для всех просуммированных комиссий должны совпадать     */ 
  macro AddSum( _Summa, _SummaNDS, _FIID, ResetSums, _SfCom, _SfSI, _SfSI_Deb, proc_deb_err )

     var CheckPayFIID = -1;

     if( ResetSums == true )  
        this.ClearCommSumma()
     end;

     this.Sum = this.Sum + _Summa;
     this.NDS = this.NDS + _SummaNDS;      

     if( (this.BaseFIID != -1) AND (_FIID != this.BaseFIID) ) 
        msgbox("Нельзя суммировать комиссии с разными валютами");
        return false; 
     else 
        this.BaseFIID = _FIID;   
     end;

     if( _SfCom != null )
        this.NumComs = this.NumComs + 1;
        this.SfCom[this.NumComs-1] = TRecHandler( "sfcomiss.dbt" );
        copy( this.SfCom[this.NumComs-1], _SfCom ); 
        this.CheckCalcComissSumAlg( _SfCom );
     end;

     /*СПИ по кредиту - первоначальная инициализация*/
     if( this.SfSI_Cred == null )
        this.SfSI_Cred = TRecHandler( "sfsi.dbt" );
        if( (_SfSI != null) )
           copy( this.SfSI_Cred, _SfSI ); 
           this.PayFIID = this.SfSI_Cred.rec.FIID;         
        end; 
     end; 

     /*СПИ по дебету - первоначальная инициализация*/
     if( this.SfSI_Deb == null )
        this.SfSI_Deb = TRecHandler( "sfsi.dbt" );
        if(_SfSI_Deb != null)
           copy( this.SfSI_Deb, _SfSI_Deb ); 
           this.FIID = this.SfSI_Deb.rec.FIID;         
        end; 
     end;  

     /*проверка на совпадение валюты и счета оплаты в разных комиссиях*/
     /*если в очередной комиссии СПИ не задан, переустанавливаем СПИ для всех*/
     if( (_SfSI == null) OR (this.PayFIID == -1) OR (this.PayFIID != _SfSI.rec.FIID) OR (this.SfSI_Cred.rec.Account != _SfSI.rec.Account) )

        CheckPayFIID = this.PayFIID;      

        this.SfSI_Cred.rec.Account = "";
        this.PayFIID = this._GetComissFIID();

        /*валюты оплаты для всех релевантных комиссий должны совпадать*/
        if( (CheckPayFIID != -1) AND (CheckPayFIID != this.PayFIID ) )
           msgbox("Валюты оплаты просуммированных комиссий должны быть одинаковыми");
           return false; 
        end;            
     end;

     /*проверка на совпадение валюты и счета списания в разных комиссиях*/
     /*если в очередной комиссии СПИ не задан, переустанавливаем СПИ для всех*/
     if( (_SfSI_Deb == null) OR (this.FIID == -1) OR (this.FIID != _SfSI_Deb.rec.FIID) OR (this.SfSI_Deb.rec.Account != _SfSI_Deb.rec.Account) )         
        if( (proc_deb_err != true) OR ((_SfSI_Deb != null) AND (this.SfSI_Deb.rec.Account == _SfSI_Deb.rec.Account)) ) 

           this.SfSI_Deb.rec.Account = "";
           this.FIID = this._GetComissFIID();
        else
           msgbox("Счет списания должен быть определен однозначно");
           return false; 
        end; 
     end;
     return true; 
  end;  

  /*просто переустановить суммы, без изменения остальных свойств*/ 
  macro SetSums( _Summa, _SummaNDS )
     if( _Summa != null ) 
        this.Sum = _Summa;
     end;
     if( _SummaNDS != null ) 
        this.NDS = _SummaNDS;      
     end;
  end; 

  macro AllSum()
     return this.Sum() + this.NDS()  
  end;   

  /*заполнить буфер платежа по комиссии*/
  macro SetPaym( _paym ) 
     if( this.Paym == null )
        this.Paym = TRecHandler("pmpaym.dbt");         
     end;
     copy( this.Paym, _paym );  
  end;

  /*вернуть ID получателя комиссий, просуммированых в объекте*/
  /*возвращаем получателя из первого объекта в списке, т.к. у всех комиссий будет одинаковый получатель*/
  macro GetReceiverID() 
     if( this.SfCom[0] )
        return this.SfCom[0].rec.ReceiverID;
     else
        return -1; 
     end;
  end;  

  macro Constructor( InSum ) 
     if( InSum == null ) 
        ClearCommSumma();
     else  /*конструктор копирования*/
        CopyCommSumma( InSum ); 
     end;  
  end;

  Constructor( InSum ); 
 
END;

/* найти оплаченную комиссию заданного вида за заданную дату для договора */
MACRO FindDefComissByContr( ContrID, CommDate, FeeType, CommNumber, rDefCom )     
   ClearRecord( DefCom );
   DefCom.ConID      = ContrID;
   DefCom.FeeType    = FeeType;
   DefCom.CommNumber = CommNumber;
   DefCom.DateFee    = CommDate;
   if( GetEQ( DefCom ) == true )
      if( rDefCom != null )
         Copy( rDefCom, DefCom );
      end;
      return true; 
   end;  

   return false;   
END;

/*процедура - определяем, попадает ли сделка в данный договор*/
MACRO DealInContr( Deals, rContr, ClientID, FromSccomcls )

   file dl_tick( dl_tick ) key 0; 
   var ContrID = 0, Group = 0;

/* теперь дата оплаты комиссии может задаваться категорией "момент взимания"
   поэтому эта проверка стала лишней

   /*должна быть задана дата оплаты комиссии*/
   if( Deals.CommDate == Date(0,0,0) )
      return false;
   end;
*/

   /*для клиентского договора    - сделка клиентская*/
   /*для договора с посредником  - сделка наша*/   

   if( Deals.ClientID != ClientID )
      return false;
   end;

   Group = SP_GetOperationGroup( Deals);

   /*проверим типы сделок*/
   if( ClientID > 0 )
      if( (not IsEXCHANGE(Group)) AND (not IsOUTEXCHANGE(Group)) AND (not IsBROKER(Group)) )      
         return false;
      end;
   else
      if( (not IsOUTEXCHANGE(Group)) AND (not IsBROKER(Group)) )      
         return false;
      end;
   end;

   if ( (FromSccomcls != true) or (Deals.ClientID <= 0) )

      /*определяем договор*/
      ClearRecord(dl_tick);
      dl_tick.DealID = Deals.DealID;
      if (GetEQ(dl_tick))
         if( (not SP_GetSfContrID( dl_tick, ContrID)) OR (ContrID == 0) )
           /*договора может и не быть, это не ошибка*/
            return false;
         end;
      end;

      if( rContr.ID != ContrID )
         return false;
      end;                
   end;                

   if( ClientID > 0 )
      /*проверим плательщика в клиентском договоре*/
      if( rContr.partyID != ClientID )
         return false;        
      end;
   else  
      /*проверим контрагента в договоре с посредником*/
      if( IsEXCHANGE(Group) OR (Deals.BrokerID < 0) OR (rContr.ContractorID != ClientID) )
         return false;
      end;
   end;
    
   return true;
END;

/*проверка релевантности сделки по отношении к комиссии*/
PRIVATE MACRO IsRelevantDealForComm( Group, InRetire )
   if( InRetire == true )
      if( (IsBUY(Group) == true) OR (IsSALE(Group) == true) OR (IsRET_ISSUE(Group) == true) )
         return true;
      else
         return false;
      end;
   else
      if( (IsBUY(Group) == true) OR (IsSALE(Group) == true) )
         return true;
      else
         return false;
      end;
   end;
END;   

/*проверка - попадает ли сделка в комиссию с указанным номером*/
MACRO DealInComm( Deal, Code, InRetire )
   var Group      = SP_GetOperationGroup( Deal); 
   /*клиентская ММВБ*/
   if( Code == "КлиентскаяММВБ" )      
      if( (Deal.ClientID > -1) AND (IsEXCHANGE(Group) == true) AND 
          (ПолучитьКодГруппыБиржи(Deal.MarketID) == MICEX_CODE) AND
          (IsRelevantDealForComm(Group, InRetire) == true) )
         return true;
      else
         return false;
      end;
   /*клиентская РТС*/
   elif( Code == "КлиентскаяРТС" )
      if( (Deal.ClientID > -1) AND 
          ( 
            ( /*биржевая, проверяем "биржа == РТС"*/
               (IsEXCHANGE(Group) == true) AND 
               (ПолучитьКодСубъекта( Deal.MarketID, PTCK_CONTR ) == RTS_CODE)
            ) OR
            ( /*внебиржевая или брокерская, проверяем "посредник == РТС"*/
               ((IsOUTEXCHANGE(Group) == true) OR IsBROKER(Group)) AND 
               (ПолучитьКодСубъекта( Deal.BrokerID, PTCK_CONTR ) == RTS_CODE)
            )
          ) AND
          (IsRelevantDealForComm(Group, InRetire) == true) )
         return true;
      else
         return false;
      end;
   /*клиентская брокерская*/   
   elif( Code == "КлиентскаяБрок" )
      if( (Deal.ClientID > -1) AND (ПолучитьКодСубъекта( Deal.BrokerID, PTCK_CONTR ) == BROKER_CODE) AND 
          (IsRelevantDealForComm(Group, InRetire) == true) )
         return true;
      else
         return false;
      end;   
   /*брокерская*/   
   elif( Code == "Брокер" )
      if( (ПолучитьКодСубъекта( Deal.BrokerID, PTCK_CONTR ) == BROKER_CODE) AND 
          (IsRelevantDealForComm(Group, InRetire) == true) )
         return true;
      else
         return false;
      end;

   end;
//   RunError( "Комиссия с кодом " + string(Code) + " не описана в макропроцедуре DealInComm" );
   return true;
END;

/* проверка релевантности дат клиентской сделки моменту расчета комиссии*/
/* Deal     - буфер сделки                   */
/* OperDate - дата операции расчета комиссии */
/* rComiss  - буфер комиссии                 */
MACRO ClientDealDateInComm( Deal, OperDate, rComiss )
   var PaymRec = null;

   /*момент расчета - дата заключения сделки*/
   if( ((ПроверитьКатегориюКомиссии( rComiss, "DSD", MOMENT_CALC_COMISS ) == true) AND (Deal.DealDate == OperDate)) )
      return true;

   /*момент расчета - дата оплаты сделки*/
   elif( ПроверитьКатегориюКомиссии( rComiss, "DPD", MOMENT_CALC_COMISS ) == true )
      PaymRec = GetDealPaymContr( Deal.DealID, Deal.BOfficeKind, LEG_KIND_DL_TICK, false, false );
      if( (PaymRec != null) AND (PaymRec.ValueDate == OperDate) )
         return true;
      end;   
   end;
   return false;
END;

/* общая проверка релевантности дат сделки моменту расчета комиссии*/
/* Deal        - буфер сделки                   */
/* OperDate    - дата операции расчета комиссии */
/* Comm        - объект комиссии (Comissions)   */
MACRO DealDateInComm( Deal, OperDate, Comm )
   /* наша сделка или брокерская комиссия*/
   if( ((not IsClientDeal(Deal)) OR (Comm.IsAgent == true)) AND (Deal.DealDate == OperDate) ) 
      return true;
   /* клиентская сделка и клиентская комиссия*/
   elif( IsClientDeal(Deal) AND (Comm.IsClient == true) AND (ClientDealDateInComm( Deal, OperDate, Comm.rComiss ) == true) )
      return true;   
   end;
   return false;   
END;

/*получить валюту возвращаемой суммы комиссии, то есть в какой валюте ПЗО считает данную валюту*/
MACRO GetComissionFIID( sfcom )
   
  return sfcom.FIID_Comm;
END;

PRIVATE MACRO ЗаполнитьПИдляПЗО( si, sa )
   si.PartyID = sa.PartyID;
   si.PartyCodeKind = sa.CodeKind;
   si.PartyCode = sa.Code;
   si.PartyName = sa.RecName;
   si.PartyINN = sa.INN;
   si.BankID  = sa.BankID;
   si.BankCodeKind= sa.BankCodeKind;
   si.BankCode = sa.BankCode;
   si.BankName = sa.BankName;
   si.CorrAcc = sa.CorrAcc;
   si.BankCorrID= sa.BankCorrID;
   si.BankCorrCodeKind= sa.BankCorrCodeKind;
   si.BankCorrCode = sa.BankCorrCode;
   si.BankCorrName = sa.BankCorrName;
   si.FIID    = sa.FIID;
   si.Account = sa.Account;
END;

private var oprstep  = TBfile("oprstep.dbt");
private var sfcom    = TRecHandler( "sfcomiss.dbt" ); 

/*Обработать единовременные комиссий по операции (в валюте оплаты комиссии). 
  Deal         - сделка, по которой ищем сумму комиссии
  InPeriodComm - если true, то получить сумму единовременных, которые вложены
                 в периодические (т.е. при расчете период. комиссий их учитывать 
                 не надо, т.к. эти комиссии уже учлись при выполнении операции)
  mode         - режим подсчета (по умолчанию - ВСЕ) 
  IsBack       - если у сделки есть вторая часть и IsBack = true, то считаем
                 регсбор по второй части, в противном случае по первой.
  _rInPeriodComiss - если задан этот буфер + задан признак InPeriodComm, то ищем единовременные вложенные именно в эту комиссию 
  ExcludeRegCom   - флаг, не брать в расчет регистрационный сбор. Если true, то
                    не берем, если false или не задан, то берем.
  InPerCom_Moment - признак того, что при подсчете вложенных в периодические, нужно брать только 
                  вложенные в периодические с переданным признаком момента взимания комиссии 
                  (DSD - дата заключения сделки или DPD - дата оплаты сделки)
Заполнит массив объектов класса CommSumma - Sums
Вернет 
  false, если были ошибки, true - все OK*/
PRIVATE MACRO ОбработкаЕдиновременныхКомиссий(  Deal, Sums, InPeriodComm,
                                                _mode, IsBack, rInPeriodComm,
                                                ExcludeRegCom, InPerCom_Moment )

  record   Operation( oproper );
  record   OprCom( oprsfcom );               
  record   Contr( sfcontr );
  record   settacc(settacc);

  var      NeedCalculate, InSum;
  var      _OnceSum_In = $0, _OnceNDS_In = $0;
  var      IsBankCom = false, IsBankCCCom = false, IsRegCom = false, IsAgentCom = false;
  var      AgentID = -1, RegContrID = 0;  
  var      mode, Group, ret, Loan, TwoParts, BackPartForReg, GoToNextComis;
  var      rec_sfsi = null, rec_sfsi_deb = null, Sum = null, proc_deb_err = false;

  if( _mode == null )  
    mode = ONCECOM_MODE_CALC_ALL;
  else
    mode = _mode;
  end;   

  /*Определить висит или нет комиссия на договоре*/
  macro КомиссияПоДоговору( ContrID, FeeType, CommNumber )
/*     
     file  concom( sfconcom ) key 0;

     ClearRecord( concom );
     concom.ObjectId   = ContrID; 
     concom.ObjectType = OBJTYPE_SFCONTR;
     concom.FeeType = FeeType; 
     concom.CommNumber = CommNumber;
     return GetEQ(concom);
*/
     var SfConCom = TRecHandler( "sfconcom.dbt" );
     return not FindSfConCom_OnDate( ContrID, FeeType, CommNumber, OBJTYPE_SFCONTR, -1, {curdate}, SfConCom);
  end;

  /*вернет cуммы(включает НДС) вложенных в OprCom комиссий в нац.валюте*/
  macro ОпредСуммуВложенныхЕдиноврКомиссий( OprCom, FIID_Sup, InSum, InSumNDS )
     file   OprComInclude( oprsfcom ) key 1;
     var    sfcomInclude = TRecHandler( "sfcomiss.dbt" );

     InSum = $0;
     ClearRecord( OprComInclude );
     while( SfGetOperCommission( OprCom.ID_Operation, -1, OprComInclude ) )

        if( not SfGetComiss( OprComInclude.FeeType, OprComInclude.CommNumber, sfcomInclude ) )
           MsgBox( "Не найдена единовременная комиссия по операции" );
           return false;
        elif( (sfcomInclude.rec.FeeType == SF_FEE_TYPE_SINGLE ) AND
              (sfcomInclude.rec.IncFeeType == OprCom.FeeType) AND
              (sfcomInclude.rec.IncCommNumber == OprCom.CommNumber) )

           if( not Sums.AddSumToArr( Sums.In, OprComInclude.Sum, OprComInclude.SumNDS, OprComInclude.FIID_Sum, sfcomInclude ) )
              return false;
           end; 

           if( (not SmartConvertSum( OprComInclude.Sum, OprComInclude.Sum, Deal.DealDate, OprComInclude.FIID_Sum, FIID_Sup, true)) OR
               (not SmartConvertSum( OprComInclude.SumNDS, OprComInclude.SumNDS, Deal.DealDate, OprComInclude.FIID_Sum, FIID_Sup, true))
             )
              return false;
           end;
           InSum     = InSum     + OprComInclude.Sum;
           InSumNDS  = InSumNDS  + OprComInclude.SumNDS;
        end;
     end; 
     SetParm( 2, InSum );
     SetParm( 3, InSumNDS );
     return true;
  end;

   /* Проверяем, является ли комиссия регсбором по указанной части сделки.
      Возвращаем true, если это так.
         Deal        - сделка
         sfcom       - комиссия
         BackPart    - если true, то проверяем обратную часть, иначе первую. */
   macro IsRegByDealPart( Deal, sfcom, BackPart, Loan, IsBack )

      record   dl_leg( dl_leg );
      var
         LegKind;

      /*Для займа - 2 лота, но 1 транш. Но и признак 2ч нужно учесть. См.90919. Поэтому так:*/
      if( Loan )
         LegKind = LEG_KIND_DL_TICK;
      else
         if( BackPart )
            LegKind = LEG_KIND_DL_TICK_BACK;
         else
            LegKind = LEG_KIND_DL_TICK;
         end;
      end;

      /* Проверим, что получатель комиссии - регистратор в сделке. */
      if( FindDL_LEG( LegKind, Deal.DealID, 0, dl_leg ) == 0 )
         if( sfcom.rec.ReceiverID == dl_leg.Registrar )

            /*Нашли транш, дальше LegKind используем только для проверок. Для 2ч займа установим = 2.*/
            if( Loan AND BackPart )
               LegKind = LEG_KIND_DL_TICK_BACK;
            end;

            /* проверим системный вид шага регистрации - по какой части сделки
               выполняется регистрация */
            if( OprCom.ID_Step )
               oprstep.Clear(); 
               oprstep.rec.ID_Operation = OprCom.ID_Operation;
               oprstep.rec.ID_Step      = OprCom.ID_Step;
               if( oprstep.getEQ() )
                  return   ((oprstep.rec.Kind_action == 102) AND (LegKind == LEG_KIND_DL_TICK)) /*перерегистрация первой части*/
                           or ((oprstep.rec.Kind_action == 104) AND (LegKind == LEG_KIND_DL_TICK_BACK)); /*перерегистрация второй части*/
               end;
            else
               /*До 90919 часть сделки получали из oprstep, но в займе для 2ч шаг перерегистрации является 
                 первым в блоке, и при его выполнении номер шага еще неизвестен.*/

               return   ((IsBack == false) AND (LegKind == LEG_KIND_DL_TICK)) /*перерегистрация первой части*/
                        or ((IsBack == true) AND (LegKind == LEG_KIND_DL_TICK_BACK)); /*перерегистрация второй части*/
            end;
         end;
      else
         msgbox( "Не найдены условия сделки при расчете единовременных комиссий" );
      end;

      return false;
   end;


  if( not ПолучитьОперациюПоСделке( deal, Operation, TRUE ) )
     return false;
  end;
  
  ClearRecord( OprCom );
  while( SfGetOperCommission( Operation.ID_Operation, -1, OprCom ) )

     GoToNextComis = false;

     if( OprCom.Sum != $0 ) 

        if( not SfGetComiss( OprCom.FeeType, OprCom.CommNumber, sfcom ) )
           MsgBox( "Не найдена комиссия по операции" );
           return false;
        end;
        IsBankCom = IsBankCCCom = IsAgentCom = IsRegCom = proc_deb_err = false;
        rec_sfsi = rec_sfsi_deb = Sum = null;

        if( (sfcom.rec.ReceiverID == 0) OR (sfcom.rec.ReceiverID == {OurBank}) )

           /*комиссия банку только для клиентских сделок*/ 
           if( IsClientDeal(Deal) AND ((mode == ONCECOM_MODE_CALC_ALL) OR (mode == ONCECOM_MODE_CALC_BANK)))
             if(OprCom.SfContrID == Deal.PartyContrID)
               IsBankCCCom = true;
             else
               IsBankCom = true;
             end;
           end;

        else 
           /*комиссии посреднику или регистратору*/
           AgentID = ОпределитьПосредникаСделки( Deal );
           if( (sfcom.rec.ReceiverID == AgentID) OR (ПолучитьКодГруппыБиржи(sfcom.rec.ReceiverID) == ПолучитьКодГруппыБиржи(deal.MarketID) ) ) /*комиссия посреднику*/         
              if( (mode == ONCECOM_MODE_CALC_ALL) OR (mode == ONCECOM_MODE_CALC_AGENT) )          
                 IsAgentCom = true;
              end;
           else /*комиссии для оплаты регистрационного сбора регистратору*/
               if( (mode == ONCECOM_MODE_CALC_ALL) OR (mode == ONCECOM_MODE_CALC_AGENT) OR (mode == ONCECOM_MODE_CALC_REG) )

                  Group          = SP_GetOperationGroup( Deal ); 
                  Loan           = IsLOAN(Group);
                  TwoParts       = IsBACKSALE(Group) OR IsREPO(Group) OR Loan;
                  BackPartForReg = TwoParts AND IsBack;
                  IsRegCom       = IsRegByDealPart(   Deal, sfcom,
                                                      BackPartForReg, Loan, IsBack );

                  if( IsRegCom == false )
                     /* Проверяемая комиссия не является регсбором. Возможно
                        она является регсбором по другой части. Если регсбор
                        по другой части, то пропускаем ее, если нет то ниже
                        крикнем об ошибке. Проверять другую часть можно, если
                        в сделке две части. */
                     if( TwoParts )
                        IsRegCom = IsRegByDealPart(   Deal, sfcom,
                                                      not BackPartForReg, Loan, IsBack );
                        GoToNextComis = IsRegCom;
                     end;
                  end;
               end;
           end;  

           if( (mode == ONCECOM_MODE_CALC_ALL) OR (mode == ONCECOM_MODE_CALC_AGENT) )          
              /*посредник в сделке задан, но у комиссии посреднику получатель не посредник*/
              if( (AgentID != -1) AND (IsAgentCom == false) AND (IsRegCom == false) )
                 msgbox("Получатель единовременной комиссии посреднику | не совпадает с посредником в сделке");
                 return false;                         
              end;
              /*посредник не задан, а комиссия именно посреднику, а не рег.сбор*/ 
              if( (AgentID == -1) AND (IsRegCom == false) ) 
                 msgbox( "Посредник в сделке не задан | единовременных комиссий посреднику не должно быть" );
                 return false;
              end;
           end;

           if( IsRegCom and ExcludeRegCom )
              GoToNextComis = true;
           end;

           /*в режиме расчета ONCECOM_MODE_CALC_AGENT признак рег.сбора надо сбросить, */
           /* так как дальше он не нужен, дальше по этому признаку в этом случае будет */
           /* суммирование, а нам этого не надо                                        */
           if( mode == ONCECOM_MODE_CALC_AGENT ) 
              IsRegCom = false;      
           end; 
        end;

        if( not GoToNextComis )

           NeedCalculate = false;
   
           if( (IsAgentCom == true) OR (IsRegCom == true) )
   
             if( SfGetSI( OBJTYPE_OPRSFCOM, OprCom, rSfSi1, rSfSi2 ) == 0 )/*OBJTYPE_OPRSFCOM - удержанная единовременная комиссия*/
                rec_sfsi = rSfSi2;
             else
                rec_sfsi = null;
             end;
           else
              rec_sfsi = null;
           end;
   
           /*для клиентской сделки для всех комиссий кроме рег.сбора (там используется договор с регистратором)*/
           if(IsClientDeal(Deal)) 
             ret = -1;
             if( IsRegCom != true )
               ret = SfGetSI( OBJTYPE_OPRSFCOM, OprCom, rSfSi1, rSfSi2 ); /*OBJTYPE_OPRSFCOM - удержанная единовременная комиссия*/
             else
               if( ПолучитьДоговорПоСделке( Deal, Contr ) == true )            
                  ret = SfSelectSETTACC( OprCom.FIID_Sum, OBJTYPE_SFCONTR, Contr, settacc );
                  if( ret == 0 )
                     ЗаполнитьПИдляПЗО( rSfSi1.rec, settacc ); 
                  end;
               end;
             end; 
   
             if( ret == 0 )
                rec_sfsi_deb = rSfSi1;
                if( (rSfSi1.rec.BankID != {OurBank}) AND (rSfSi1.rec.BankID != 0) )
                   if( IsRegCom != true )
                      msgbox( "Счет клиента - плательщика комиссии должен быть внутренним" );
                   else
                      msgbox( "Счет клиента - плательщика рег.сбора должен быть внутренним" );
                   end;  
                   return false;         
                end;
             else
                rec_sfsi_deb = null;
             end;
           else
              rec_sfsi_deb = null; 
           end;
   
           /* если ищем сумму единовременных, вложенных в периодические, то
              получаем вышестоящую периодическую, по этому же договору*/
           if( InPeriodComm == true )
              if( (SfFindSuperiorComiss( OprCom.FeeType, OprCom.CommNumber, sfcom ) == true) AND 
                  /*комиссия периодическая?*/
                  (sfcom.rec.FeeType == SF_FEE_TYPE_PERIOD) AND
                  /*комиссия по этому же договору?*/
                  (КомиссияПоДоговору( OprCom.SfContrID, sfcom.rec.FeeType, sfcom.rec.Number ) == true) AND
                  /*момент взимания у вышестоящей периодической соответсвует заданному?*/
                  ((InPerCom_Moment == null) OR (ПроверитьКатегориюКомиссии( sfcom, InPerCom_Moment ) == true)) AND
                  /*вышестоящая периодическая совпадает с заданной?*/
                  ( (rInPeriodComm == null) OR ((rInPeriodComm.rec.feeType == sfcom.rec.FeeType) AND (rInPeriodComm.rec.number == sfcom.rec.Number)) )
                )
                 NeedCalculate = true; /*учитываем сумму этой комиссии*/ 
              end;
           else /* если ищем сумму единовременных по операции, то получаем сумму
                   нижестоящих единовременных комиссий по этой же операции (тех,
                   которые "являются частью" OprCom) и вычитаем эту сумму  из 
                   суммы OprCom*/
              NeedCalculate = true; /*учитываем сумму этой комиссии*/ 
           end;
   
           if( NeedCalculate )
              if( IsBankCom == true )      /*комиссия банку*/
                 Sum = Sums.Bank;
              elif( IsBankCCCom == true )    /*комиссия банку от клиента-контрагента*/
                 Sum = Sums.BankCC;
              elif( IsAgentCom == true )   /*комиссия посреднику*/
                 Sum = Sums.Agent;
              elif( IsRegCom == true )     /*комиссия регистратору*/
                 Sum = Sums.Reg;
                 if( IsClientDeal(Deal) )
                    proc_deb_err = true; /*для комиссии регистратору в клиентской сделке данные по плательщику должны быть заданы однозначно*/
                 end;
              end;
              if( (IsBankCom == true) OR (IsBankCCCom == true) OR (IsAgentCom == true) OR (IsRegCom == true) )
                 if( not Sums.AddSumToArr( Sum, OprCom.Sum, OprCom.SumNDS, OprCom.FIID_Sum, sfcom, rec_sfsi, rec_sfsi_deb, proc_deb_err ) )
                    return false;
                 end; 
              end; 
   
              /*!!! только для комиссии банку посчитаем вложенные единовременные в единовременные комиссии*/ 
              /*!!! для остальных видов вложенные единовременные не поддерживаем */
              if( IsBankCom == true )
                 if( not ОпредСуммуВложенныхЕдиноврКомиссий( OprCom, Sums.BankCommFIID, _OnceSum_In, _OnceNDS_In ) )
                    msgbox( "Ошибка при определении суммы вложенных единовременных комиссий" );
                    return false;
                 else
                    /*вычтем сумму вложенных*/ 
                    if( not Sums.AddSumToArr( Sum, -_OnceSum_In, -_OnceNDS_In, Sums.BankCommFIID, sfcom, rec_sfsi, rec_sfsi_deb, proc_deb_err ) )
                       return false;
                    end; 
                    if( (Sums.BankSum < $0) OR (Sums.BankNDS < $0) )
                       msgbox( "Сумма клиентской комиссии банку меньше суммы вложенных в нее комиссий" );
                       return false;                  
                    end; 
                 end; 
              end;
              if( IsBankCCCom == true )
                 if( not ОпредСуммуВложенныхЕдиноврКомиссий( OprCom, Sums.BankCCCommFIID, _OnceSum_In, _OnceNDS_In ) )
                    msgbox( "Ошибка при определении суммы вложенных единовременных комиссий" );
                    return false;
                 else
                    /*вычтем сумму вложенных*/ 
                    if( not Sums.AddSumToArr( Sum, -_OnceSum_In, -_OnceNDS_In, Sums.BankCCCommFIID, sfcom, rec_sfsi, rec_sfsi_deb, proc_deb_err ) )
                       return false;
                    end; 
                    if( (Sums.BankCCSum < $0) OR (Sums.BankCCNDS < $0) )
                       msgbox( "Сумма клиентской комиссии банку меньше суммы вложенных в нее комиссий" );
                       return false;                  
                    end; 
                 end; 
              end;
           end;   
        end; /* GoToNextComis */
     end;
  end; /*while по удержанным комиссиям операции*/

  return true; 
END;

/* вернуть дату конвертации суммы комиссии для сделки в зависимости от метода пересчета сумм,   */
/* указанного в сделке                                                                          */
MACRO GetConvDateForComiss( CalcComissSumAlg, Deal, IsAgentCom )

   var   ConvDate = null;
   var   PaymRec;     

   if( (CalcComissSumAlg != null) AND (Deal != null) )
      ConvDate = Deal.DealDate;
      /*конвертацию выполним по дате, которую берем в зависимости от алгоритма, заданного в комиссии*/
      /*даты берем из данных по сделке    */
      if( CalcComissSumAlg == 1 )
         ConvDate = Deal.DealDate;
      elif( CalcComissSumAlg == 2 )
         if( IsAgentCom )
            ConvDate = Deal.CommDate;
         else
            PaymRec = GetDealPaymContr( Deal.DealID, Deal.BOfficeKind, LEG_KIND_DL_TICK, false, false );
            if( PaymRec )
               ConvDate = PaymRec.ValueDate; /*дата оплаты контрактива*/
            end;   
         end;
      elif( CalcComissSumAlg == 3 )
         if( IsAgentCom )
            ConvDate = Deal.CommDate-1;
         else
            PaymRec = GetDealPaymContr( Deal.DealID, Deal.BOfficeKind, LEG_KIND_DL_TICK, false, false );
            if( PaymRec )
               ConvDate = PaymRec.ValueDate-1; /*дата оплаты контрактива - 1*/
            end;   
         end;
      end;      
   elif( Deal != null )
      ConvDate = Deal.DealDate;
   end;
   return ConvDate;
END;

/* класс - суммы ПЕРИОДИЧЕСКИХ комиссии для заданного субъекта-получателя комиссий по договору  */
/* IsSuperior - создание вложенного объекта Comissions для вышестоящей комиссии                 */
CLASS Comissions( _rContr, _rComiss, _InitDate, IsSuperior )

   var rContr;    /*договор обслуживания, в который включена комиссия*/
   var rComiss;   /*данные по начисленной комиссии*/
   var rComissSup;/*данные по вышестоящей периодической комиссии*/
   var rDefComSup;/*данные по удержанной вышестоящей периодической комиссии*/
   var ComissSup; /*вложенный объект типа Comissions для вышестоящей комиссии*/

   var InRetire;     /*признак учета погашения для расчета текущей комиссии*/
   var InRetireSup;  /*признак учета погашения для расчета вышестоящей комиссии*/
   var InSuperior;   /*признак вхождения комиссии в вышестоящую комиссиию*/
   var IsClient;     /*клиентская комиссия*/
   var IsAgent;      /*комиссия посреднику*/
   var IsAgentBroker;/*комиссия посреднику-брокеру*/
   var IsAgentMarket;/*комиссия посреднику-бирже*/

   var SumBase;      /*базовая сумма - сумма покупок\продаж\погашений*/
   var BaseFIID;     /*базовая сумма - сумма покупок\продаж\погашений*/
   var AmountBase;   /*базовое кол-во ценных бумаг - сумма покупок\продаж\погашений*/

   var InitDate;     /*дата инициализации комиссии*/

   var MomentCalcInStartDate:bool; /* если true - момент расчета комиссии в дату заключения сделки, иначе - в дату оплаты сделки*/
   var MomentGetInStartDate:bool;  /* если true - момент взимания (оплаты) комиссии в дату заключения сделки, иначе - в дату оплаты сделки*/

   var Sums;         /*сумма комиссии - объект класса CommSumma*/

   /* определение валюты базовой суммы комиссии                   */
   /* если валюта всех алгоритмов комиссии одинаковая, берем ее,  */
   /* иначе берем валюту возвращаемой суммы комисии               */
   macro SetBaseFIID()
      var loop, _FIID = -1;

      CalCal.Clear();
      CalCal.AddFilter("t_FeeType = " + string (this.rComiss.rec.FeeType) +
                  " and t_CommNumber = " + string(this.rComiss.rec.Number) );
      CalCal.Rewind;
      loop = CalCal.Next;

      while( loop )   
         if( _FIID == -1 )
            _FIID = CalCal.rec.FIID_TarScl;
         elif( CalCal.rec.FIID_TarScl != _FIID )
            _FIID = -1;
            loop  = false;
         end;
         if( loop )
            loop = CalCal.Next;
         end;
      end;
      CalCal.DropFilter();

      if( _FIID == -1 )
         _FIID = GetComissionFIID( this.rComiss.rec ); /*валюта возвращаемой суммы*/         
      end;   

      return _FIID;
   end;

   /*---------------------------------------------------------------------------*/
   /*конструктор*/
   /*---------------------------------------------------------------------------*/
   private macro Construct( _rContr, _rComiss, _InitDate, IsSuperor )     
      this.rContr       = Trechandler( "sfcontr.dbt" );
      this.rComiss      = Trechandler( "sfcomiss.dbt" );
      this.rComissSup   = Trechandler( "sfcomiss.dbt" );
      this.rDefComSup   = Trechandler( "sfdefcom.dbt" ); /*буфер удержанной вышестоящей комисс*/

      copy( this.rContr, _rContr );
      copy( this.rComiss, _rComiss );

      this.SumBase   = $0; 
      this.AmountBase= $0;
      this.BaseFIID  = this.SetBaseFIID();
      this.InitDate  = _InitDate;   

      this.Sums      = CommSumma();  /*суммы всех невложенных комиссий*/
      this.IsClient  = false;
      this.IsAgent   = false;
      this.IsAgentBroker = this.IsAgentMarket = false;

      /*определяем виды комиссии*/
      if( (this.rComiss.rec.ReceiverID == 0) OR (this.rComiss.rec.ReceiverID == {OurBank}) )
         this.IsClient = true;
      else
         this.IsAgent = true;
         if( ВидСубъекта( this.rComiss.rec.ReceiverID, PTK_BROKER ) == true )
            this.IsAgentBroker = true;
         elif( ВидСубъекта( this.rComiss.rec.ReceiverID, PTK_MARKETPLASE ) == true )
            this.IsAgentMarket = true;
         end;
      end;

      this.InSuperior   = false;   
      this.ComissSup    = null;

      if( (IsSuperor == null) OR (IsSuperor == false) ) /*поддерживаем только один уровень вложенности*/
         /*комиссия вложена в вышестоящую*/
         if( (SfFindSuperiorComiss( this.rComiss.rec.FeeType, this.rComiss.rec.Number, this.rComissSup ) == true) AND (this.rComissSup.rec.FeeType == 1) ) 
            /*вышестоящая комиссия оплачена по договору?*/
            if( FindDefComissByContr( this.rContr.rec.Id, this.InitDate, this.rComissSup.rec.FeeType, this.rComissSup.rec.Number, this.rDefComSup ) == true )
               this.InSuperior = true;   
            end;
            /*создаем объект Comissions для вышестоящей комиссии*/
            this.ComissSup = Comissions( _rContr, this.rComissSup, this.rDefComSup.rec.dateFee, true );   
         end;
      end;

      /*комиссия взимается при погашениях*/
      if( SfCheckInRetireComiss( this.rComiss.rec.FeeType, this.rComiss.rec.Number ) == true )
         this.InRetire = true;
      end;

      /*момент расчета комиссии*/
      if( ПроверитьКатегориюКомиссии( this.rComiss, "DSD", MOMENT_CALC_COMISS ) )
         this.MomentCalcInStartDate = true;  /* в дату заключения сделки   */
      else
         this.MomentCalcInStartDate = false; /* в дату оплаты сделки       */
      end;
      /*момент оплаты - взимания комиссии */
      if( ПроверитьКатегориюКомиссии( this.rComiss, "DSD", MOMENT_GET_COMISS ) )
         this.MomentGetInStartDate = true;   /* в дату заключения сделки   */
      else
         this.MomentGetInStartDate = false;  /* в дату оплаты сделки       */
      end;

   end;

   macro InitSums( rDefCom, SumCom, SumNDS )

      var rSfSi_deb = null, rSfSi_cred = null;

      /*OBJTYPE_SFDEFCOM = 663 - удержанная периодич. комиссия*/
      if( SfGetSI( 663, rDefCom, rSfSi1, rSfSi2 ) == 0 )
         rSfSi_deb = rSfSi1;               
      else
         rSfSi_deb = null;
      end;

      if( SfGetSI( 663, rDefCom, rSfSi1, rSfSi2 ) == 0 )
         rSfSi_cred = rSfSi2;               
      else
         rSfSi_cred = null;
      end;

      /*добавляем суммы комиссии*/   
      this.Sums.AddSum( SumCom, SumNDS, rDefCom.rec.FIID_commSum, false, this.rComiss, rSfSi_cred, rSfSi_deb );
   end;

   /*---------------------------------------------------------------------------*/
   /*---------------------------------------------------------------------------*/
   macro AddSumBase( BaseSum, AmountBase )
      this.SumBase      = this.SumBase    + BaseSum;
      this.AmountBase   = this.AmountBase + AmountBase;
   end; 

   /*---------------------------------------------------------------------------*/
   /*---------------------------------------------------------------------------*/
   macro GetSumBase()
      return this.SumBase;
   end; 
   /*---------------------------------------------------------------------------*/
   /*---------------------------------------------------------------------------*/
   macro GetAmountBase()
      return this.AmountBase;
   end;

   /*---------------------------------------------------------------------------*/
   /* получить валюту базовой суммы                                              */
   /*---------------------------------------------------------------------------*/
   macro GetBaseSumFIID()
      return BaseFIID; 
   end;

   /*---------------------------------------------------------------------------*/
   /* посчитать суммы сделки , если необходимо сконвертировать их               */
   /*---------------------------------------------------------------------------*/
   macro CalcDealSum( Deal, DealSumma, DealAmount, ToFIID, ConvDate )
      if( not СуммаСделки( Deal, DealSumma, null, DealAmount, null, ToFIID, ConvDate ) )
         return false;
      end;          
      setparm( 2, DealSumma  );
      setparm( 3, DealAmount );
      return true;
   end;

   /*-------------------------------------------------------------------------------------*/
   /* определяем дату конвертации суммы сделки для пересчета сумм сделок в валюты комиссии*/
   /* если комиссия считается в дату заключения сделки, то берем дату заключения сделки   */
   /* иначе - дату выполенния операции расчета\оплаты комиссии                            */
   /*-------------------------------------------------------------------------------------*/
   macro GetDealConvDate( Deal, CommDate ):date

      var ConvDate:date;
      if( this.MomentCalcInStartDate )
         ConvDate = Deal.DealDate;
      else       
         ConvDate = CommDate;
         /*это можно раскомментарить, если для расчета рублевых комиссий для валютных сделок    */
         /* пересчет сумм сделок требуется выполнять по курсу за дату, зависящую от настройки   */
         /* "дата пересчета сумм" на виде периодической комиссии                                */
         /*ConvDate = GetConvDateForComiss( this.rComiss.rec.CalcComissSumAlg, Deal, this.IsAgent );*/
      end;  
   
      return ConvDate;
   end;   
   /*---------------------------------------------------------------------------*/
   /*посчитать часть периодической комиссии на одну сделку\погашение            */
   /* для текущей комиссии в валюте периодической комиссии                      */
   /*---------------------------------------------------------------------------*/
   macro CalcCommForDeal( Deal, CommDate, SumComm, SumNDS )

      var DealSumma = $0, BuySaleOutlay = $0, BuySaleOutlayNDS = $0;

      /* получаем сумму сделки в валюте базовой суммы на дату расчета            */
      /* для получения отношения суммы сделки к базовой сумме этого достаточно   */
      if( this.CalcDealSum( Deal, DealSumma, null, this.GetBaseSumFIID(), this.GetDealConvDate(Deal,CommDate) ) == false )
         return false;
      end;

      if( (this.SumBase != $0) AND (this.Sums.Sum() != $0) )

         if( this.SumBase > DealSumma )
            BuySaleOutlay     = Round( (DealSumma * this.Sums.Sum) / this.SumBase, 2 );
            BuySaleOutlayNDS  = Round( (DealSumma * this.Sums.NDS) / this.SumBase, 2 );
         else /*последняя сделка, просто берем оставшуюся комиссию и НДС без деления*/
            BuySaleOutlay     = this.Sums.Sum;
            BuySaleOutlayNDS  = this.Sums.NDS;
         end;

         this.SumBase   = this.SumBase    - DealSumma;
         this.Sums.Sum  = this.Sums.Sum   - BuySaleOutlay;
         this.Sums.NDS  = this.Sums.NDS   - BuySaleOutlayNDS;   
      end;          

      SetParm( 3, BuySaleOutlay     ); 
      SetParm( 4, BuySaleOutlayNDS  ); 
      return true;
   end;
 
   /*-------------------------------------------------------------------------------*/
   /*подсчет базовых сумм расчета комиссии для заданного вида сделок                */
   /*                                                                               */
   /* если задается диапазон дат - BegDate, EndDate, то датой выполнения операции   */
   /* будет EndDate, иначе BegDate                                                  */
   /*-------------------------------------------------------------------------------*/
   /*----------------------------------------------------------------------------*/
   /*посчитать базовую сумму всех сделок входящих в расчет комиссии              */
   /*----------------------------------------------------------------------------*/
   macro CalcBaseSums( BegDate, EndDate, Deal_Status )

     var ClientID;
     var DealSumma = $0, DealAmount = $0; 
     var OperDate:date; /*дата операции расчета периодических комиссий*/    
     var DealDateInCommQuery, ClientDealDateInCommQuery, check1, check2;
     var Query, DataSet, Tick = TBFile("dl_tick.dbt", "R", 0);
     var SQLDate;

     if( Deal_Status == null )
        Deal_Status = " and d.t_DealStatus  = " + string(DL_READIED);
     end;

     if( EndDate == null ) 
       OperDate = BegDate; 
       EndDate  = BegDate;
     else 
       OperDate = EndDate;
     end;
     SQLDate = GetSQLDate(OperDate);

     if( this.rContr.rec.PartyID == {OurBank} ) 
        ClientID = -1;
     else 
        ClientID = this.rContr.rec.PartyID;
     end;

     /*Дата заключения сделки*/
     check1 = ПроверитьКатегориюКомиссии( this.rComiss, "DSD", MOMENT_CALC_COMISS );
     /*Дата оплаты сделки*/
     check2 = ПроверитьКатегориюКомиссии( this.rComiss, "DPD", MOMENT_CALC_COMISS );

     if ((check1 == true) and (check2 == true))
        ClientDealDateInCommQuery = 
           " and (d.t_DealDate  = " + SQLDate + 
           "      OR (Select paym.t_ValueDate " +
           "            From dpmpaym_dbt paym " +
           "           Where paym.t_DocKind    = d.t_BOfficeKind " +
           "             and paym.t_DocumentID = d.t_DealID " +
           "             and paym.t_Purpose    = " + string(CAi) +
           "         ) = " + SQLDate + 
           "     )";
     elif ((check1 == true) and (check2 == false))
        ClientDealDateInCommQuery = 
           " and (d.t_DealDate  = " + SQLDate + 
           "     )";
     elif ((check1 == false) and (check2 == true))
        ClientDealDateInCommQuery = 
           " and (" + 
           "         (Select paym.t_ValueDate " +
           "            From dpmpaym_dbt paym " +
           "           Where paym.t_DocKind    = d.t_BOfficeKind " +
           "             and paym.t_DocumentID = d.t_DealID " +
           "             and paym.t_Purpose    = " + string(CAi) +
           "         ) = " + SQLDate + 
           "     )";
     else
        ClientDealDateInCommQuery = " ";
     end;

     if ((this.IsAgent == true) and (this.IsClient == true))
        DealDateInCommQuery = 
           " and (d.t_DealDate  = " + SQLDate + 
           "      OR (d.t_ClientID != -1 " + ClientDealDateInCommQuery +
           "         ) " + 
           "     )";
     elif ((this.IsAgent == true) and (this.IsClient == false))
        DealDateInCommQuery = 
           " and (d.t_DealDate  = " + SQLDate + 
           "     )";
     elif ((this.IsAgent == false) and (this.IsClient == true))
        DealDateInCommQuery = 
           " and ( (d.t_DealDate  = " + SQLDate + 
           "        and d.t_ClientID = -1 " +
           "       )" +
           "       OR (d.t_ClientID != -1 " + ClientDealDateInCommQuery +
           "          ) " + 
           "     )";
     else
        DealDateInCommQuery = 
           " and ( (d.t_DealDate  = " + SQLDate + 
           "        and d.t_ClientID = -1 " +
           "       )" +
           "     )";
     end;

     /*суммируем сделки подходящие под этот договор и комиссию*/
     Query = "Select d.t_DealID From ddl_tick_dbt d Where " +
                    "d.t_BofficeKind = " + string(DL_SECURITYDOC) +
               Deal_Status +
               " and d.t_DealDate   >= " + 
               IIF (ClientID == -1, GetSQLDate(BegDate), GetSQLDate(Date(0,0,0))) +
               " and d.t_DealDate   <= " + GetSQLDate(EndDate) +
               " and d.t_ClientID    = " + string(ClientID) +
               /*Перенесено из DealInContr:*/
               " and ( (    d.t_ClientID != -1 " +
               "        and d.t_ClientContrID > 0 and d.t_ClientContrID = " + string(this.rContr.rec.ID) + 
                     " ) or ( d.t_ClientID = -1 " + " ) ) " +
               DealDateInCommQuery; 

     DataSet = TRsbDataSet(Query);
     while (DataSet.MoveNext())
        Tick.Clear();
        Tick.rec.DealID = DataSet.DealID;
        if (Tick.GetEQ())
           /*для нашего банка берем только сделки от начала периода учета комиссии*/ 
           /*для клиентов берем все сделки с самого начала по день операции - так как возможно нужен расчет в дату оплаты сделки*/ 

           /*если сделка для заданного клиента или наша, завершена и выполнена по обрабатываемому договору*/
           if( (DealInContr( Tick.rec, this.rContr.rec, ClientID, true ) == true) and
               (DealInComm( Tick.rec, this.rComiss.rec.Code, this.InRetire ) == true )/* and
               (DealDateInComm( Tick.rec, OperDate, this ) == true )*/ /*перенесено в select*/
           )
              if( this.CalcDealSum( Tick.rec, DealSumma, DealAmount, this.GetBaseSumFIID(), this.GetDealConvDate(Tick.rec,OperDate) ) == false )
                 return false;
              end;
              this.AddSumBase( DealSumma, DealAmount );
           end;
        end;
     end;

     return true;
   end; 

   Construct( _rContr, _rComiss, _InitDate, IsSuperior );
     
END;

/* класс для подсчета и хранения сумм и валют комиссий по сделке */
CLASS ComissionSums( Sums )

   /* суммы - массивы объектов класса CommSumma */
   var Bank,   /*комиссии нам - то есть банку*/ 
       BankCC, /*комиссии нам, т.е. банку, от клиента-контрагента*/
       Agent,  /*комиссии посреднику*/
       Reg,    /*комиссии регистратору*/
       In;     /*вложенные комиссии*/

   /*скопировать массив сумм в другой массив*/
   macro CopyComissionSums( DstSums, SrcSums )
      var i = 0;
      if( (SrcSums != null) AND (DstSums != null) )
         while( SrcSums[i] != null )
            DstSums[i] = CommSumma( SrcSums[i] );
            i = i + 1;
         end;
      else
         RunError("Ошибочные параметры при копировании сумм комиссий");
      end;
   end;

   /*ищем группу комиссий в заданном массиве по заданным параметрам поиска*/
   macro Find( Sums, _FIID, _SfCom, _SfSI, _i )
      var i = 0;
      while( Sums[i] != null )

         if( _SfSI != null )/*проверим СПИ*/                  
            if( ( Sums[i].GetReceiverID() == _SfCom.rec.ReceiverID) AND (CheckSfSI( Sums[i].SfSI_Cred, _SfSI) == true) )
               if( Sums[i].BaseFIID != _FIID )
                  RunError("Ошибка при добавлении суммы комиссии|валюта добавляемой суммы отличается от валюты комиссий данного вида");
               end;
               SetParm( 5, i );
               return true;
            end;
         else
            if( (Sums[i].SfSI_Cred == null) AND (Sums[i].SfCom[0].rec.ReceiverID == _SfCom.rec.ReceiverID) ) /*нашли группу с незаданным СПИ для этого получателя*/
               SetParm( 5, i );
               return true;
            end;
         end;

         i = i + 1;
      end;
      SetParm( 5, i );
      return false;
   end;
   /*увеличим суммы в группе комиссий c заданными параметрами или добавим новую группу если не нашли*/
   macro AddSumToArr( Sums, _Summa, _SummaNDS, _FIID, _SfCom, _SfSI, _SfSI_Deb, proc_deb_err )
     var num = 0, reset_sum = false;
     if( (_SfCom == null) OR (this.Find( Sums, _FIID, _SfCom, _SfSI, num ) == false) ) 
        /*если такой группы комиссий еще нет, добавим*/ 
        Sums[num] = CommSumma();         
        reset_sum = true; /*начальная инициализация сумм*/
     end; 
     return Sums[num].AddSum( _Summa, _SummaNDS, _FIID, reset_sum, _SfCom, _SfSI, _SfSI_Deb, proc_deb_err );
   end;  

   /*получить базовую валюту комиссий в массиве*/
   /*должна быть одинаковая у всех*/
   macro GetFIID( Sums )
      if( Sums[0] != null )/*если есть хоть одна группа комиссий заданного вида*/
         /*во всех группах комиссий одного вида базовая валюта одна и та же*/
         /*это обеспечивается на этапе добавления сумм комиссий*/
         return Sums[0].BaseFIID; 
      else
         return -1;
      end;
   end;
   /*получить сумму комиссий в массиве*/
   macro GetSum( Sums )
      var i = 0, _AllSum = $0;
      while( Sums[i] != null )
         _AllSum = _AllSum + Sums[i].Sum;
         i = i + 1;   
      end;
      return _AllSum;
   end;
   /*получить сумму НДС в массиве*/
   macro GetNDS( Sums )
      var i = 0, _AllNDS = $0;
      while( Sums[i] != null )
         _AllNDS = _AllNDS + Sums[i].NDS;
         i = i + 1;   
      end;
      return _AllNDS;
   end;

   /*суммы комиссий посчитанных на шаге расчета комиссий*/
   macro AllSum()
      return GetSum( this.Bank ) + GetSum( this.BankСС ) + GetSum( this.Agent );/*комиссии банку и посреднику*/
   end;
   macro AllNDS()
      return GetNDS( this.Bank ) + GetSum( this.BankСС ) + GetNDS( this.Agent );/*комиссии банку и посреднику*/
   end;
   /*суммы всех комиссий, посчитанных на всех шагах*/
   macro AllSumFull()
      return this.AllSum() + GetSum( this.Reg );  
   end; 
   macro AllNDSFull()
      return this.AllNDS() + GetNDS( this.Reg );
   end; 

   /* виды алгоритмов пересчета сумм в комиссиях */
   /*    SFCOMISS_CALCCOMISSSUMALG_CALC  =1,      - на день расчета   */
   /*    SFCOMISS_CALCCOMISSSUMALG_PAY   =2,      - на день оплаты    */
   /*    SFCOMISS_CALCCOMISSSUMALG_BEFOREPAY =3   - за день до оплаты */

   macro GetConvDate( Comm, Deal )
      return GetConvDateForComiss( Comm.CalcComissSumAlg, Deal, ID_gr_OurBank(Comm.GetReceiverID()) );
   end;

   /*сконвертировать суммы заданной комиссии в заданную валюту*/
   macro Convert( Sums, dat, ToFIID, SayError, Deal )
      var i = 0, _ToFIID, _dat;
      while( Sums[i] != null )

         if( ToFIID == null )/*если явно не задали во что конвертить, значит конвертим в валюту оплаты комиссии*/
            _ToFIID = Sums[i].PayFIID;
         else
            _ToFIID = ToFIID;
         end;

         if( dat == null )/*если не задана явно дата конвертирования, получим ее в зависимости от алгоритма в комиссии*/
            if( Deal != null )
               _dat = GetConvDate( Sums[i], Deal );
            else
               return false;
            end;
         else
            _dat = dat;
         end;

         if( not SmartConvertSum( Sums[i].Sum, Sums[i].Sum, _dat, Sums[i].BaseFIID, _ToFIID, SayError) )
            return false;
         end;
         if( not SmartConvertSum( Sums[i].NDS, Sums[i].NDS, _dat, Sums[i].BaseFIID, _ToFIID, SayError) )
            return false;
         end;
         Sums[i].BaseFIID = _ToFIID; 
         i = i + 1;   
      end;
      return true;      
   end;
   /*конвертация всех сумм комиссий к заданной валюте*/
   macro ConvertAll( dat, ToFIID, SayError, Deal )
     /*сконвертим банковскую комиссию в заданную валюту*/ 
     if( Convert( this.Bank, dat, ToFIID, SayError, Deal ) == false )
        return false;
     end;
     /*сконвертим банковскую комиссию от клиента-контрагента в заданную валюту*/ 
     if( Convert( this.BankCC, dat, ToFIID, SayError, Deal ) == false )
        return false;
     end;
     /*сконвертим комиссию посреднику в заданную валюту*/ 
     if( Convert( this.Agent, dat, ToFIID, SayError, Deal ) == false )
        return false;
     end;
     /*сконвертим комиссию регистратору в заданную валюту*/ 
     if( Convert( this.Reg, dat, ToFIID, SayError, Deal ) == false )
        return false;
     end;      
     return true; 
   end;

   macro BankSum()
      return this.GetSum( this.Bank );
   end;
   macro BankNDS()
      return this.GetNDS( this.Bank );
   end;
   macro BankCommFIID()
      return this.GetFIID( this.Bank );
   end;
   macro BankCCSum()
      return this.GetSum( this.BankCC );
   end;
   macro BankCCNDS()
      return this.GetNDS( this.BankCC );
   end;
   macro BankCCCommFIID()
      return this.GetFIID( this.BankCC );
   end;
   macro AgentSum()
      return this.GetSum( this.Agent );
   end;
   macro AgentNDS()
      return this.GetNDS( this.Agent );
   end;
   macro AgentCommFIID()
      return this.GetFIID( this.Agent );
   end;
   macro RegSum()
      return this.GetSum( this.Reg );
   end;
   macro RegNDS()
      return this.GetNDS( this.Reg );
   end;
   macro RegCommFIID()
      return this.GetFIID( this.Reg );
   end;
   macro Sum_In()
      return this.GetSum( this.In );
   end;
   macro NDS_In()
      return this.GetNDS( this.In );
   end;
   macro FIID_In()
      return this.GetFIID( this.In );
   end;

   /* подсчитать единовременные комиссии по сделке
      и распределить их по массивам сумм: Bank,Agent,Reg,In
      в массивах учитываются отдельные суммы для разных клиентов и для разных валют оплаты комиссий
      валюты комиссий и валюты счетов списания комиссии должны быть одинаковые

         IsBack         - если у сделки есть вторая часть и IsBack = true, то
                          считаем регсбор по второй части, в противном случае
                          по первой.

         ExcludeRegCom  - флаг, не брать в расчет регистрационный сбор. Если
                          true, то не берем, если false или не задан, то берем. 

         InPerCom_Moment- признак того, что при подсчете вложенных в периодические, нужно брать только 
                  вложенные в периодические с переданным признаком момента взимания комиссии 
                  (DSD - дата заключения сделки или DPD - дата оплаты сделки)   */

   macro CalcOnce(   Deal, InPeriodComm, _mode, IsBack, rInPeriodComm,
                     ExcludeRegCom, InPerCom_Moment )
      return ОбработкаЕдиновременныхКомиссий(   deal, this, InPeriodComm,
                                                _mode, IsBack, rInPeriodComm,
                                                ExcludeRegCom, InPerCom_Moment );
   end;   

   /*найти платежи по единовременным комиссиям*/
   macro GetPaymentsOnce( Deal, Purp, Sums, IsSumPaym /*признак - нужен только один суммарный платеж*/ )
      record _paym( pmpaym );
      var i = 0;
      while( (Sums[i] != null) AND ((IsSumPaym == false) OR (i == 0)) )
         if( not GetPaymentInfo( deal, Purp, _paym, null, null, i ) )
            return false;
         else
            Sums[i].SetPaym( _paym );   
         end;                  
         i = i + 1;
      end;
      return true;
   end;
   /*сконвертировать платежи по единовременным комиссиям*/
   macro ConvertPaymentsOnce( Sums, Dat )
      var i = 0;
      while( Sums[i] != null )

         if( Sums[i].Paym != null )
            КонвертацияСуммПлатежа( Sums[i].Paym.rec, Dat );   
         end;
         i = i + 1;
      end;
      return true;
   end;
   
   /*получить валюту оплаты для комиссии посреднику*/
   macro GetAgentCommissPayFIID( PayToCorrAcc )
      var i = 0, FindFIID = -1;
      while( Agent[i] != null )               
         if( FindFIID == -1 )
            if( (PayToCorrAcc != null) AND (PayToCorrAcc == true) )
               FindFIID = Agent[i].PayFIID;                                     
            else
               FindFIID = Agent[i]._GetComissFIID();
            end;
         else
            if( Agent[i].PayFIID != FindFIID )
               if( (PayToCorrAcc != null) AND (PayToCorrAcc == true) )
                  return Agent[i].BaseFIID;
               else
                  RunError("Валюты оплаты комиссий посреднику должны быть одинаковые");
               end;
            end;
         end;
         i = i + 1;
      end;
      return FindFIID;      
   end;

   macro ClearSums
      this.Bank.Size = 0;
      this.BankCC.Size = 0;
      this.Agent.Size = 0;
      this.Reg.Size = 0;
      this.In.Size = 0;
   end;

   macro construct( CommSums )
      this.Bank      = TArray;  
      this.BankCC    = TArray;
      this.Agent     = TArray;  
      this.Reg       = TArray;  
      this.In        = TArray;  

      if( CommSums != null )
         /*конструктор копирования*/
         this.CopyComissionSums( this.Bank,   CommSums.Bank   );
         this.CopyComissionSums( this.BankCC, CommSums.BankCC );
         this.CopyComissionSums( this.Agent,  CommSums.Agent  );
         this.CopyComissionSums( this.Reg,    CommSums.Reg    );
         this.CopyComissionSums( this.In,     CommSums.In     );
      end;
   end;

   construct( Sums );
END;


/*класс - удержанная комиссия к оплате, используется на шаге оплаты периодических комиссиий */
CLASS (Comissions) ComissionPay( _rContr, _rComiss, _InitDate, _rDefCom )

   var rDefCom;            /*удержанная комиссия*/
   var SumDeals;           /*сумма сделок за дату операции*/
   var SumToPay;           /*сумма к оплате, включая НДС*/
   var ExistOutExchange;   /*признак наличия внебиржевой сделки в перечне сделок по которым взималась эта комиссия*/
   var IsBrokerAgent;      /*признак того, что посредник - брокер*/
   var IsOutAgent;         /*признак того, что посредник - внебиржевой*/

   macro ConstructComissionPay( _rDefCom )
      this.rDefCom  = Trechandler( "sfdefcom.dbt" );
      copy( this.rDefCom, _rDefCom );
      this.SumDeals = this.SumToPay = $0;
      this.ExistOutExchange = this.IsBrokerAgent = this.IsOutAgent = false;
   end;
   
   macro AddSumDeals( _SumDeals )
      this.SumDeals = this.SumDeals + _SumDeals;
   end;

   macro CalcSumToPay()
      if( this.SumBase != $0 )
         this.SumToPay = (rDefCom.rec.CommSum + rDefCom.rec.NDSSum) * (this.SumDeals / this.SumBase);     
      end;
   end;

   /*вызываем конструктор родительского класса*/
   initComissions( _rContr, _rComiss, _InitDate );
   /*свой конструктор*/
   ConstructComissionPay( _rDefCom );
END;

PRIVATE MACRO PrintError( rContr, Deals, StrErr )
   StrErr = StrErr + " | сделка " + Deals.DealCode;
   ScOpInsertError( DL_STOCKCONTR, ScOprServDoc, rContr, 1, StrErr );
END;

/*суммарные клиентские комиссии в разрезе видов комиссий (тип взимания + номер комиссии)*/
/* учитываются вложенные единовременных и клиентские комиссии */
CLASS ClientComissTotal()
   var Comm;         /* объект класса Comissions - суммы периодических комиссий для заданного субъекта-получателя комиссий по договору */

   var Amount, NDS;  /* просуммированные периодические комиссии по сделкам (с учетом вложенных периодических и единовременных) */
   var DebAmount, DebNDS;     /*суммы в валюте дебета комиссии*/
   var CredAmount, CredNDS;   /*суммы в валюте кредита комиссии*/

   var CommArr;      /* массив объектов этого же класса (ClientComissTotal), в который складываем комиссии разных видов*/

   private macro Construct()   
      Comm           = null;
      Amount = NDS   = $0;
      DebAmount = DebNDS = $0;
      CredAmount = CredNDS = $0;
      CommArr        = TArray;  
   end;   

   macro Check( rComiss )
      if( (this.Comm.rComiss.rec.feeType == rComiss.rec.feeType  ) AND 
          (this.Comm.rComiss.rec.number  == rComiss.rec.number   ) 
      )
         return true;
      else
         return false;
      end;
   end;

   macro Add( CurrComm, _Amount, _NDS, _DebAmount, _DebNDS, _CredAmount, _CredNDS )
      this.Comm     = CurrComm;   
      this.Amount   = this.Amount + _Amount;
      this.NDS      = this.NDS    + _NDS;         
      this.DebAmount    = this.DebAmount  + _DebAmount;
      this.DebNDS       = this.DebNDS     + _DebNDS;         
      this.CredAmount   = this.CredAmount + _CredAmount;
      this.CredNDS      = this.CredNDS    + _CredNDS;         
   end;

   /*поиск определенной клиентской комиссии в массиве комиссий*/
   /*возвращает номер найденного элемента или -1, если ничего не нашел*/
   private macro FindInArray( rComiss )   
      var i = 0;
      while( i < this.CommArr.size )
         if( this.CommArr[i].Check( rComiss ) == true )
            return i;      
         end;
         i = i + 1;      
      end;
      return -1;
   end;

   /*вернет номер элемента массива, в который просуммировали комиссию*/
   private macro AddInArray( CurrComm, _Amount, _NDS, _DebAmount, _DebNDS, _CredAmount, _CredNDS )      

      var i = FindInArray( CurrComm.rComiss );              
      if( i == -1 )
         i = this.CommArr.Size;
         this.CommArr[i] = ClientComissTotal();
      end;
      if( i >= 0 )
         this.CommArr[i].Add( CurrComm, _Amount, _NDS, _DebAmount, _DebNDS, _CredAmount, _CredNDS );
      end;
      return i;
   end;

   /* заполним массив комиссий, в разрезе видов комиссий (тип взимания + номер) */
   /* может выполняться при формировании объектов класса Comissions - здесь параметр CurrComm*/
   macro FillArray( Deal, rContr, CommDate, CurrComm, Sums, _SumComm, _SumNDS )
      var   num_in_arr  = -1;
      var   OnceSumsIn  = ComissionSums(); 
      var   DebOnceSumsIn,CredOnceSumsIn;
      var   DebFIID     = CurrComm.Sums.SfSi_Deb.rec.FIID;
      var   CredFIID    = CurrComm.Sums.SfSi_Cred.rec.FIID;
      var   BaseFIID    = CurrComm.Sums.BaseFIID;
      var   ConvDate, _DebAmount = $0, _DebNDS = $0, _CredAmount = $0, _CredNDS = $0;

      if( (CurrComm.IsClient == true) OR (CurrComm.InSuperior == true) )
         
         ConvDate = GetConvDateForComiss( CurrComm.rComiss.rec.CalcComissSumAlg, Deal, (not CurrComm.IsClient) );
         if( not SmartConvertSum( _DebAmount,  _SumComm, ConvDate, BaseFIID, DebFIID, true ) )
            return false;
         end;
         if( not SmartConvertSum( _DebNDS,  _SumNDS, ConvDate, BaseFIID, DebFIID, true ) )
            return false;
         end;
         if( not SmartConvertSum( _CredAmount,  _SumComm, ConvDate, BaseFIID, CredFIID, true ) )
            return false;
         end;
         if( not SmartConvertSum( _CredNDS,  _SumNDS, ConvDate, BaseFIID, CredFIID, true ) )
            return false;
         end;

         num_in_arr = AddInArray( CurrComm, _SumComm, _SumNDS, _DebAmount, _DebNDS, _CredAmount, _CredNDS );
         if( num_in_arr >= 0 )
            DebOnceSumsIn  = ComissionSums(OnceSumsIn);
            CredOnceSumsIn = ComissionSums(OnceSumsIn);
            /*поищем единовременные, вложенные именно в эту периодическую комиссию*/
            OnceSumsIn.CalcOnce( Deal, true, ONCECOM_MODE_CALC_AGENT, false, CurrComm.rComiss );

            if( not OnceSumsIn.ConvertAll( null, BaseFIID, false, Deal ) )
               PrintError( rContr, Deal, "Ошибка при конвертации сумм вложенных единовременных комиссий" );
               return false;
            end;
            /*учтем в периодических сумму вложенных единовременных*/
            this.CommArr[num_in_arr].Amount  = this.CommArr[num_in_arr].Amount   - OnceSumsIn.AllSum();
            this.CommArr[num_in_arr].NDS     = this.CommArr[num_in_arr].NDS      - OnceSumsIn.AllNDS();

            if( not DebOnceSumsIn.ConvertAll( null, DebFIID, false, Deal ) )
               PrintError( rContr, Deal, "Ошибка при конвертации сумм вложенных единовременных комиссий" );
               return false;
            end;
            this.CommArr[num_in_arr].DebAmount  = this.CommArr[num_in_arr].DebAmount   - DebOnceSumsIn.AllSum();
            this.CommArr[num_in_arr].DebNDS     = this.CommArr[num_in_arr].DebNDS      - DebOnceSumsIn.AllNDS();

            if( not CredOnceSumsIn.ConvertAll( null, CredFIID, false, Deal ) )
               PrintError( rContr, Deal, "Ошибка при конвертации сумм вложенных единовременных комиссий" );
               return false;
            end;
            this.CommArr[num_in_arr].CredAmount  = this.CommArr[num_in_arr].CredAmount   - CredOnceSumsIn.AllSum();
            this.CommArr[num_in_arr].CredNDS     = this.CommArr[num_in_arr].CredNDS      - CredOnceSumsIn.AllNDS();
         end;
      end;        

      return true;
   end;  

   /*учет сумм вложенной периодической комиссий в вышестоящей периодической комиссии*/
   private macro _ProcessInSuperior( TotalComm )
      var Amount = $0, NDS = $0, SetDateStartDeal_Calc:bool;

      var i = FindInArray( TotalComm.Comm.rComissSup );/*для учета вложенной периодической ищем в массиве вышестоящую комиссию*/              

      if( i >= 0 )
         /*если у вышестоящей комиссии "Момент расчета" == "Дата заключения сделки", то вычтем из нее вложенные*/
         if( ПроверитьКатегориюКомиссии( this.CommArr[i].Comm.rComiss, "DSD", MOMENT_CALC_COMISS ) == true )
         
            if( TotalComm.Comm.Sums.BaseFIID != this.CommArr[i].Comm.Sums.BaseFIID )                                        
               if( (SmartConvertSum( Amount, TotalComm.Amount, this.CommArr[i].Comm.InitDate, TotalComm.Comm.Sums.BaseFIID, this.CommArr[i].Comm.Sums.BaseFIID, false ) == false) OR
                   (SmartConvertSum( NDS,    TotalComm.NDS,    this.CommArr[i].Comm.InitDate, TotalComm.Comm.Sums.BaseFIID, this.CommArr[i].Comm.Sums.BaseFIID, false ) == false) )
                  return false;
               end;
            else
               Amount = TotalComm.Amount;
               NDS    = TotalComm.NDS;
            end;
            this.CommArr[i].Amount  = this.CommArr[i].Amount   - Amount;
            this.CommArr[i].NDS     = this.CommArr[i].NDS      - NDS;             
         end;

      end;    
      return true;      
   end;   

   /*учет ВСЕХ вложенных комиссий*/
   /*должна вызываться только после того, как все комиссии занесены во внутренний массив CommArr*/
   macro ProcessInSuperior()
      var i = 0;
      while( this.CommArr[i] != null )
         if( this.CommArr[i].Comm.InSuperior == true ) /*вложенная, учтем ее в вышестоящей*/
            if( _ProcessInSuperior( this.CommArr[i] ) == false )
               return false;
            end;
         end;
         i = i + 1;
      end;
      return true;
   end;

   Construct();
END;

