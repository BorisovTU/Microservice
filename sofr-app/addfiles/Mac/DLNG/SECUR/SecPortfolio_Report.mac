/*
$Name:        SecPortfolio_Report.mac
$Module:      Ценные бумаги
$Description: <Отчеты>/ <Внутренние отчеты>/ <Портфель ценных бумаг> - тело отчета
*/
import "SecPortfolio_Form.mac", "spRepFun.mac";

PRIVATE VAR SecPortfolio_Form;

PRIVATE CONST ПОКУПКИ_ТП   = 0,
              ПОКУПКИ_ППР  = 1,
              ПОКУПКИ_ПУДП = 2,
              ПОКУПКИ_ПКУ  = 3,
              ПОКУПКИ_КСУ  = 4;

PRIVATE CONST БПП_ТП   = 0,
              БПП_ППР  = 1,
              БПП_ПУДП = 2,
              БПП_ПКУ  = 3,
              БПП_ПВО  = 4,
              БПП_КСУ  = 5;

PRIVATE CONST РЕПО_ПВО = 0,
              РЕПО_КСУ = 1;

PRIVATE CLASS (DL_CReportTemplate) SP_Portf_Report
  private var m_RS, m_FIID = -1, m_ReportDate = Date(0,0,0),
              m_FIID_Name = "", m_FIID_ISIN = "", m_IsPrintHeader = false;

  private var m_isExistData = false; // есть данные для выпуска отчета?

  PRIVATE VAR        
    Покупки = TArray,
    БПП     = TArray,
    РЕПО    = TArray;

  PRIVATE MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;                                                                                                                                                                         

  PRIVATE MACRO BeginReport()
     m_IsPrintHeader = false;
     m_isExistData = false;
     m_FIID = SecPortfolio_Form.GetFieldValue(PNFLD_SEC_PORTFOLIO_AVRNAME);
     m_ReportDate = SecPortfolio_Form.GetFieldValue(PNFLD_SEC_PORTFOLIO_BEGDATE);
  END;
  
  PRIVATE MACRO EndReport()
  END;

  PRIVATE MACRO CollectDataByFIID()

     m_FIID_Name = m_RS.Name;
     m_FIID_ISIN = m_RS.ISIN;

     if( m_RS.AVOIRKIND == AVOIRISSKIND_KSU )
        if( m_RS.PORTFOLIO == KINDPORT_BACK_KSU )
           РЕПО[РЕПО_КСУ] = m_RS.Amount;
        elif( m_RS.PORTFOLIO == KINDPORT_BACK_BPP_KSU )
           БПП[БПП_КСУ] = m_RS.Amount;
        else
           Покупки[ПОКУПКИ_КСУ] = m_RS.Amount;
        end;
     elif( m_RS.BPP == 0 )
        if( m_RS.PORTFOLIO == KINDPORT_TRADE )
           Покупки[ПОКУПКИ_ТП] = m_RS.Amount;
        elif( m_RS.PORTFOLIO == KINDPORT_SALE )
           Покупки[ПОКУПКИ_ППР] = m_RS.Amount;
        elif( m_RS.PORTFOLIO == KINDPORT_RETIRE )
           Покупки[ПОКУПКИ_ПУДП] = m_RS.Amount;
        elif( m_RS.PORTFOLIO == KINDPORT_CONTR )
           Покупки[ПОКУПКИ_ПКУ] = m_RS.Amount;
        elif( m_RS.PORTFOLIO == KINDPORT_BACK )
           РЕПО[РЕПО_ПВО] = m_RS.Amount;
        end;
     else
        if( m_RS.PORTFOLIO == KINDPORT_TRADE )
           БПП[БПП_ТП] = m_RS.Amount;
        elif( m_RS.PORTFOLIO == KINDPORT_SALE )
           БПП[БПП_ППР] = m_RS.Amount;
        elif( m_RS.PORTFOLIO == KINDPORT_RETIRE )
           БПП[БПП_ПУДП] = m_RS.Amount;
        elif( m_RS.PORTFOLIO == KINDPORT_CONTR )
           БПП[БПП_ПКУ] = m_RS.Amount;
        elif( m_RS.PORTFOLIO == KINDPORT_BASICDEBT )
           БПП[БПП_ПВО] = m_RS.Amount;
        end;
     end;

  END;

  PRIVATE MACRO ClearDataByFIID()

     m_FIID_Name = "";
     m_FIID_ISIN = "";

     Покупки[ПОКУПКИ_ТП]   = $0;
     Покупки[ПОКУПКИ_ППР]  = $0;
     Покупки[ПОКУПКИ_ПУДП] = $0;
     Покупки[ПОКУПКИ_ПКУ]  = $0;
     Покупки[ПОКУПКИ_КСУ]  = $0;

     БПП[БПП_ТП]           = $0;
     БПП[БПП_ППР]          = $0;
     БПП[БПП_ПУДП]         = $0;
     БПП[БПП_ПКУ]          = $0;
     БПП[БПП_ПВО]          = $0;
     БПП[БПП_КСУ]          = $0;

     РЕПО[РЕПО_ПВО]        = $0;
     РЕПО[РЕПО_КСУ]        = $0;

  END;

  PRIVATE MACRO ИмяПортфеляПокупки(i:integer)
     var Name = "";

     if( i == ПОКУПКИ_ТП )
        Name = "ССПУ_ЦБ";
     elif( i == ПОКУПКИ_ППР )
        Name = "СССД_ЦБ";
     elif( i == ПОКУПКИ_ПУДП )
        Name = "АС_ЦБ";
     elif( i == ПОКУПКИ_ПКУ )
        Name = "ПКУ";
     elif( i == ПОКУПКИ_КСУ )
        Name = "";/*Графы <Портфель> для КСУ не заполняются*/
     end;

     return Name;
  END;

  PRIVATE MACRO ИмяПортфеляБПП(i:integer)
     var Name = "";

     if( i == БПП_ТП )
        Name = "ТП_БПП";
     elif( i == БПП_ППР )
        Name = "ППР_БПП";
     elif( i == БПП_ПУДП )
        Name = "ПУДП_БПП";
     elif( i == БПП_ПКУ )
        Name = "ПКУ_БПП";
     elif( i == БПП_ПВО )
        Name = "ПВО_БПП";
     elif( i == БПП_КСУ )
        Name = "";/*Графы <Портфель> для КСУ не заполняются*/
     end;

     return Name;
  END;

  PRIVATE MACRO ИмяПортфеляРЕПО(i:integer)
     var Name = "";

     if( i == РЕПО_ПВО )
        Name = "ПВО";
     elif( i == РЕПО_КСУ )
        Name = "";/*Графы <Портфель> для КСУ не заполняются*/
     end;

     return Name;
  END;

  PRIVATE MACRO PrintAllRowByFIID()

     var IsPrintNameFIID = false, i = 0, j = 0, k = 0, 
         A1 = "", A2 = "", A3 = $0, A4 = "", A5 = $0, A6 = "", A7 = $0, A8 = "", 
         Itog_A3 = $0, Itog_A5 = $0, Itog_A7 = $0, 
         v_continue = true;

     if( not m_IsPrintHeader )
        PrintFormatString( "#",
                           "N1_H0", date_as_string(1,m_ReportDate)
                         );
        RegisterTable( "N1_MainTable", "#",
                       "N1_A1",      
                       "N1_A2",     
                       "N1_A3",    
                       "N1_A4",    
                       "N1_A5",      
                       "N1_A6",   
                       "N1_A7",   
                       "N1_A8"     
                     );
        m_IsPrintHeader = true;
     end;

     while( v_continue )

        A1 = "";
        A2 = "";
        A3 = $0;
        A4 = "";
        A5 = $0;
        A6 = "";
        A7 = $0;
        A8 = "";

        if( not IsPrintNameFIID )
           A1 = m_FIID_Name;
           A2 = m_FIID_ISIN;
           IsPrintNameFIID = true;
        end;

        while( i < Покупки.Size )
           if( Покупки[i] > $0 )
              A3 = Покупки[i];
              A4 = ИмяПортфеляПокупки(i);
              i = i + 1;
              break;
           end;
           i = i + 1;
        end;

        while( j < БПП.Size )
           if( БПП[j] > $0 )
              A5 = БПП[j];
              A6 = ИмяПортфеляБПП(j);
              j = j + 1;
              break;
           end;
           j = j + 1;
        end;

        while( k < РЕПО.Size )
           if( РЕПО[k] > $0 )
              A7 = РЕПО[k];
              A8 = ИмяПортфеляРЕПО(k);
              k = k + 1;
              break;
           end;
           k = k + 1;
        end;

        if( (A3 > $0) OR (A5 > $0) OR (A7 > $0) )

           Itog_A3 = Itog_A3 + A3;
           Itog_A5 = Itog_A5 + A5;
           Itog_A7 = Itog_A7 + A7;

           PrintTableLine( "N1_A1", A1, null,    
                           "N1_A2", A2, null,     
                           "N1_A3", iif(A3>$0,ВыводЧерезФормулу(A3),""), null,    
                           "N1_A4", A4, null,    
                           "N1_A5", iif(A5>$0,ВыводЧерезФормулу(A5),""), null,      
                           "N1_A6", A6, null,   
                           "N1_A7", iif(A7>$0,ВыводЧерезФормулу(A7),""), null,   
                           "N1_A8", A8, null 
                         );
        end;

        if( (i == Покупки.Size) and (j == БПП.Size) and (k == РЕПО.Size) )
           v_continue = false;
        end;

     end;

     SetCurrentLineFont( 11, true, false, null, null, RGB(242,242,242) );
     PrintTableLine( "N1_A1", "Итого "+m_FIID_Name+":", null,    
                     "N1_A2", "", null,     
                     "N1_A3", ВыводЧерезФормулу(Itog_A3), null,    
                     "N1_A4", "", null,    
                     "N1_A5", ВыводЧерезФормулу(Itog_A5), null,      
                     "N1_A6", "", null,   
                     "N1_A7", ВыводЧерезФормулу(Itog_A7), null,   
                     "N1_A8", "", null 
                   );
     
  END;

  PRIVATE MACRO Create()
     var cmd, cnt = 0, PrevFIID = -1, i = 0;

     cmd = DL_RSDCommand();

     /*все данные получаем на утро!! отчетной даты*/
     /*обычные покупки\зачисления в ТП, ППР, ПКУ, ПУДП и ОРЕПО (ПВО и КСУ)*/
     var Query = 
         "select rest.*, avrkroot.T_AVOIRKIND, fin.t_Name, avoiriss.t_ISIN "
        +"  from ( "                                                                                                            
        +"    select sum(m.T_AMOUNT) T_AMOUNT, m.T_FIID, m.T_PORTFOLIO, 0 BPP "                                                                     
        +"      FROM dpmwrtsum_dbt m "                                                                                                         
        +"     where m.T_PARTY = -1 "                                                                                                         
        +"       and m.t_buy_sale in(?,?) "                                                                                
        +"       and (m.T_PORTFOLIO in (?,?,?,?,?) "
        +"            or (m.T_PORTFOLIO = ? and m.t_kind = 180) ) "                                                                               
        +"       and M.T_AMOUNT > 0 "                                                                                                        
        +"       and m.T_STATE = ? "                                                                                                         
        +"       and m.t_changedate < ? ";

     cmd.AddParam(PM_WRITEOFF_SUM_BUY);
     cmd.AddParam(PM_WRITEOFF_SUM_BUY_BO);
     cmd.AddParam(KINDPORT_TRADE);
     cmd.AddParam(KINDPORT_SALE);
     cmd.AddParam(KINDPORT_CONTR);
     cmd.AddParam(KINDPORT_RETIRE);
     cmd.AddParam(KINDPORT_BACK);
     cmd.AddParam(KINDPORT_BACK_KSU);
     cmd.AddParam(PM_WRTSUM_FORM);
     cmd.AddParam(m_ReportDate);

     if( m_FIID != -1 )
        Query = Query +
         "       and m.T_FIID = ? ";                                                                                                         
        cmd.AddParam(m_FIID);
     end;
                                                                  
     Query = Query +
         "    group by m.T_FIID, m.T_PORTFOLIO "                                                                                             
        +"  union all "                                                                                                                        
        +"    select sum(t.T_AMOUNT) T_AMOUNT, t.T_FIID, t.T_PORTFOLIO, 0 BPP "                                                                         
        +"      FROM dpmwrtbc_dbt t, dpmwrtsum_dbt m "                                                                          
        +"     WHERE t.t_sumid IN ( SELECT s.t_sumid "                                                                               
        +"                            FROM dpmwrtsum_dbt s "                                                                         
        +"                           WHERE s.t_buy_sale in(?,?) "                                                                  
        +"                             AND s.t_party = -1 "                                                                     
        +"                             AND (s.t_portfolio in (?,?,?,?,?) "                                                    
        +"                                  or (s.T_PORTFOLIO = ? and s.t_kind = 180) ) "                                                                               
        +"                             AND s.t_changedate >= ? "                              
        +"                         ) "                                                                                          
        +"       and t.t_state = ? "                                                                                        
        +"       and t.T_AMOUNT > 0  "                                                                                      
        +"       AND t.t_instance = ( SELECT MAX (v.t_instance) "                                                               
        +"                              FROM dpmwrtbc_dbt v "                                                                   
        +"                             WHERE v.t_sumid = t.t_sumid "                                                            
        +"                               AND v.t_changedate < ? "
        +"                          ) "                           
        +"       AND m.t_sumid = t.t_sumid ";

     cmd.AddParam(PM_WRITEOFF_SUM_BUY);
     cmd.AddParam(PM_WRITEOFF_SUM_BUY_BO);
     cmd.AddParam(KINDPORT_TRADE);
     cmd.AddParam(KINDPORT_SALE);
     cmd.AddParam(KINDPORT_CONTR);
     cmd.AddParam(KINDPORT_RETIRE);
     cmd.AddParam(KINDPORT_BACK);
     cmd.AddParam(KINDPORT_BACK_KSU);
     cmd.AddParam(m_ReportDate);
     cmd.AddParam(PM_WRTSUM_FORM);
     cmd.AddParam(m_ReportDate);

     if( m_FIID != -1 )
        Query = Query +
         "       and t.T_FIID = ? ";                                                                                                         
        cmd.AddParam(m_FIID);
     end;
                                                                                
     /*зачисления КСУ не в ОРЕПО*/
     Query = Query +
         "    group by t.T_FIID, t.T_PORTFOLIO "                                                                                             
        +"  union all "                                                                                                                        
        +"    select sum(m.T_AMOUNT) T_AMOUNT, m.T_FIID, -1 T_PORTFOLIO, 0 BPP "                                                                     
        +"      FROM dpmwrtsum_dbt m "                                                                                                         
        +"     where m.T_PARTY = -1 "                                                                                                         
        +"       and m.t_buy_sale = ? "                                                                                
        +"       and m.T_PORTFOLIO = ? "                                                                                       
        +"       and M.T_AMOUNT > 0 "                                                                                                        
        +"       and m.t_kind != 180 "
        +"       and m.T_STATE = ? "                                                                                                         
        +"       and m.t_changedate < ? ";

     cmd.AddParam(PM_WRITEOFF_SUM_BUY);
     cmd.AddParam(KINDPORT_BACK_KSU);
     cmd.AddParam(PM_WRTSUM_FORM);
     cmd.AddParam(m_ReportDate);

     if( m_FIID != -1 )
        Query = Query +
         "       and m.T_FIID = ? ";                                                                                                         
        cmd.AddParam(m_FIID);
     end;
                                                                  
     Query = Query +
         "    group by m.T_FIID "                                                                                             
        +"  union all "                                                                                                                        
        +"    select sum(t.T_AMOUNT) T_AMOUNT, t.T_FIID, -1 T_PORTFOLIO, 0 BPP "                                                                         
        +"      FROM dpmwrtbc_dbt t, dpmwrtsum_dbt m "                                                                          
        +"     WHERE t.t_sumid IN ( SELECT s.t_sumid "                                                                               
        +"                            FROM dpmwrtsum_dbt s "                                                                         
        +"                           WHERE s.t_buy_sale = ? "                                                                  
        +"                             AND s.t_party = -1 "                                                                     
        +"                             AND s.t_portfolio = ? "                                                    
        +"                             and s.t_kind != 180 "                                                                               
        +"                             AND s.t_changedate >= ? "                              
        +"                         ) "                                                                                          
        +"       and t.t_state = ? "                                                                                        
        +"       and t.T_AMOUNT > 0  "                                                                                      
        +"       AND t.t_instance = ( SELECT MAX (v.t_instance) "                                                               
        +"                              FROM dpmwrtbc_dbt v "                                                                   
        +"                             WHERE v.t_sumid = t.t_sumid "                                                            
        +"                               AND v.t_changedate < ? "
        +"                          ) "                           
        +"       AND m.t_sumid = t.t_sumid ";

     cmd.AddParam(PM_WRITEOFF_SUM_BUY);
     cmd.AddParam(KINDPORT_BACK_KSU);
     cmd.AddParam(m_ReportDate);
     cmd.AddParam(PM_WRTSUM_FORM);
     cmd.AddParam(m_ReportDate);

     if( m_FIID != -1 )
        Query = Query +
         "       and t.T_FIID = ? ";                                                                                                         
        cmd.AddParam(m_FIID);
     end;
                                                                                
     /*проданные БПП из обычных покупок\зачислений*/
     Query = Query +
         "    group by t.T_FIID "
        +"  union all "                                                                                                                        
        +"    select sum(m.T_AMOUNT) T_AMOUNT, m.T_FIID, m.T_PORTFOLIO, 1 BPP "                                                                     
        +"      FROM dpmwrtsum_dbt m "                                                                                                         
        +"     where m.T_PARTY = -1 "                                                                                                         
        +"       and m.t_buy_sale = ? "                                                                                                      
        +"       and M.T_KIND = 210 "                                                                                                        
        +"       and M.T_AMOUNT > 0 "                                                                                                        
        +"       and m.T_STATE = ? "                                                                                                         
        +"       and m.t_changedate < ? ";

     cmd.AddParam(PM_WRITEOFF_SUM_BUY);
     cmd.AddParam(PM_WRTSUM_SALE_BPP);
     cmd.AddParam(m_ReportDate);

     if( m_FIID != -1 )
        Query = Query +
         "       and m.T_FIID = ? ";                                                                                                         
        cmd.AddParam(m_FIID);
     end;
                                                                  
     Query = Query +
         "    group by m.T_FIID, m.T_PORTFOLIO "                                                                                             
        +"  union all "                                                                                                                        
        +"    select sum(t.T_AMOUNT) T_AMOUNT, t.T_FIID, t.T_PORTFOLIO, 1 BPP "                                                                         
        +"      FROM dpmwrtbc_dbt t, dpmwrtsum_dbt m "                                                                          
        +"     WHERE t.t_sumid IN ( SELECT s.t_sumid "                                                                               
        +"                            FROM dpmwrtsum_dbt s "                                                                         
        +"                           WHERE s.t_buy_sale = ? "                                                                      
        +"                             AND s.t_party = -1 "                                                                     
        +"                             AND s.t_changedate >= ? "                              
        +"                         ) "                                                                                          
        +"       and t.T_AMOUNT > 0 "                                                                                       
        +"       and t.t_state = ? "                                                                                        
        +"       AND t.t_instance = ( SELECT MAX (v.t_instance) "                                                               
        +"                              FROM dpmwrtbc_dbt v "                                                                   
        +"                             WHERE v.t_sumid = t.t_sumid "                                                            
        +"                               AND v.t_changedate < ? "
        +"                          ) "                           
        +"       AND m.t_sumid = t.t_sumid ";

     cmd.AddParam(PM_WRITEOFF_SUM_BUY);
     cmd.AddParam(m_ReportDate);
     cmd.AddParam(PM_WRTSUM_SALE_BPP);
     cmd.AddParam(m_ReportDate);

     if( m_FIID != -1 )
        Query = Query +
         "       and t.T_FIID = ? ";                                                                                                         
        cmd.AddParam(m_FIID);
     end;

     /*проданные БПП из ОРЕПО (портфель ОД)*/
     Query = Query +
         "    group by t.T_FIID, t.T_PORTFOLIO "                                                                                             
        +"  union all "                                                                                                                
        +"    select sum(m.T_AMOUNT) T_AMOUNT, m.T_FIID, m.T_PORTFOLIO, 1 BPP "                                                                     
        +"      FROM dpmwrtsum_dbt m "                                                                                                         
        +"     where m.T_PARTY = -1 "                                                                                                         
        +"       and m.t_Amount > 0 "                                                                                                        
        +"       and m.T_PORTFOLIO = ? "                                                                                                     
        +"       and m.T_STATE = ? "                                                                                                        
        +"       and m.t_changedate < ? ";

     cmd.AddParam(KINDPORT_BASICDEBT);
     cmd.AddParam(PM_WRTSUM_NOTFORM);
     cmd.AddParam(m_ReportDate);

     if( m_FIID != -1 )
        Query = Query +
         "       and m.T_FIID = ? ";                                                                                                         
        cmd.AddParam(m_FIID);
     end;
                                                                  
     Query = Query +
         "    group by m.T_FIID, m.T_PORTFOLIO "                                                                                              
        +"  union all "                                                                                                                        
        +"    select sum(t.T_AMOUNT) T_AMOUNT, t.T_FIID, t.T_PORTFOLIO, 1 BPP "                                                                         
        +"      FROM dpmwrtbc_dbt t, dpmwrtsum_dbt m "                                                                          
        +"     WHERE t.t_sumid IN ( SELECT s.t_sumid "                                                                               
        +"                            FROM dpmwrtsum_dbt s "                                                                          
        +"                           WHERE s.t_party = -1 "                                                                       
        +"                             AND s.t_changedate >= ? "                              
        +"                        ) "                                                                                          
        +"       and t.t_Amount > 0 "                                                                                       
        +"       and t.T_PORTFOLIO = ? "                                                                                    
        +"       and t.t_state = ? "                                                                                        
        +"       AND t.t_instance = (SELECT MAX (v.t_instance) "                                                               
        +"                             FROM dpmwrtbc_dbt v "                                                                   
        +"                            WHERE v.t_sumid = t.t_sumid "                                                            
        +"                              AND v.t_changedate < ? "
        +"                          ) "                           
        +"       AND m.t_sumid = t.t_sumid ";

     cmd.AddParam(m_ReportDate);
     cmd.AddParam(KINDPORT_BASICDEBT);
     cmd.AddParam(PM_WRTSUM_NOTFORM);
     cmd.AddParam(m_ReportDate);

     if( m_FIID != -1 )
        Query = Query +
         "       and t.T_FIID = ? ";                                                                                                         
        cmd.AddParam(m_FIID);
     end;
                                                                                
     /*проданные КСУ БПП*/
     Query = Query +
         "    group by t.T_FIID, t.T_PORTFOLIO "                                                                                             
        +"  union all "                                                                                                                  
        +"    select sum(m.T_AMOUNT) T_AMOUNT, m.T_FIID, m.T_PORTFOLIO, 1 BPP "                                                                     
        +"      FROM dpmwrtsum_dbt m "                                                                                                         
        +"     where m.T_PARTY = -1 "                                                                                                         
        +"       and m.t_buy_sale = ? "                                                                                                     
        +"       and m.T_PORTFOLIO = ? "                                                                                                    
        +"       and M.T_AMOUNT > 0 "                                                                                                        
        +"       and m.T_STATE = ? "                                                                                                         
        +"       and m.t_changedate < ? ";

     cmd.AddParam(PM_WRITEOFF_SUM_BUY);
     cmd.AddParam(KINDPORT_BACK_BPP_KSU);
     cmd.AddParam(PM_WRTSUM_NOTFORM);
     cmd.AddParam(m_ReportDate);

     if( m_FIID != -1 )
        Query = Query +
         "       and m.T_FIID = ? ";                                                                                                         
        cmd.AddParam(m_FIID);
     end;
                                                                  
     Query = Query +
         "    group by m.T_FIID, m.T_PORTFOLIO "                                                                                             
        +"  union all "                                                                                                                        
        +"    select sum(t.T_AMOUNT) T_AMOUNT, t.T_FIID, t.T_PORTFOLIO, 1 BPP "                                                                         
        +"      FROM dpmwrtbc_dbt t, dpmwrtsum_dbt m  "                                                                         
        +"     WHERE t.t_sumid IN ( SELECT s.t_sumid  "                                                                              
        +"                            FROM dpmwrtsum_dbt s "                                                                         
        +"                           WHERE s.t_buy_sale = ? "                                                                      
        +"                             AND s.t_party = -1 "                                                                     
        +"                             AND s.t_changedate >= ? "                              
        +"                        ) "                                                                                          
        +"       and t.T_PORTFOLIO = ? "                                                                                   
        +"       and t.T_AMOUNT > 0 "                                                                                       
        +"       and t.t_state = ? "                                                                                        
        +"       AND t.t_instance = (SELECT MAX (v.t_instance) "                                                                
        +"                             FROM dpmwrtbc_dbt v "                                                                   
        +"                            WHERE v.t_sumid = t.t_sumid "                                                            
        +"                              AND v.t_changedate < ? "
        +"                          ) "
        +"       AND m.t_sumid = t.t_sumid ";

     cmd.AddParam(PM_WRITEOFF_SUM_BUY);
     cmd.AddParam(m_ReportDate);
     cmd.AddParam(KINDPORT_BACK_BPP_KSU);
     cmd.AddParam(PM_WRTSUM_NOTFORM);
     cmd.AddParam(m_ReportDate);

     if( m_FIID != -1 )
        Query = Query +
         "       and t.T_FIID = ? ";                                                                                                         
        cmd.AddParam(m_FIID);
     end;
                                                                                
     Query = Query +
         "    group by t.T_FIID, t.T_PORTFOLIO ) rest, dfininstr_dbt fin, "
        +"    (SELECT AVRKINDS.T_AVOIRKIND, AVRKINDS.T_NUM "
        +"       FROM davrkinds_dbt avrkinds  "                           
        +"      where AVRKINDS.T_PARENT = 0 "                                                                                                      
        +"        and AVRKINDS.T_FI_KIND = ? "                                                                                                       
        +"        and AVRKINDS.T_ISEMISSIVE = 'X') avrkroot, davrkinds_dbt avrkinds, davoiriss_dbt avoiriss "                                                                
        +"  where FIN.T_FIID = rest.t_fiid "                                                                                               
        +"    and AVRKINDS.T_AVOIRKIND = FIN.T_AVOIRKIND "                                                                                   
        +"    and avrkroot.T_AVOIRKIND = AVRKINDS.T_ROOT "
        +"    and avoiriss.t_FIId = rest.t_fiid "                                                                           
        +"  order by avrkroot.T_NUM, fin.t_Name, rest.t_fiid";

     cmd.AddParam(FIKIND_AVOIRISS);

     cnt = cmd.GetCount(Query);
     if( cnt > 0 )
        InitProgress( cnt, "Отчет \"Портфель ценных бумаг\"", "Формирование отчета" );

        ClearDataByFIID();

        m_RS = cmd.Execute(Query);

        while(m_RS.MoveNext())
           if( (PrevFIID != -1) and (PrevFIID != m_RS.FIID) )
              PrintAllRowByFIID();
              ClearDataByFIID();
           end;
           CollectDataByFIID();
           PrevFIID = m_RS.FIID;
           m_isExistData = true;
           i = i + 1;
           UseProgress(i);
        end;

        RemProgress;

        if( m_isExistData )
           PrintAllRowByFIID();
           EndTable();
           AddStandardFooter( "N1_" );
        end;
     end;
                    
  END;

  /* есть данные в выборке? */
  macro CReport_DataExist()
     return m_isExistData;
  end;
  
  initDL_CReportTemplate( NULL, "Report_SecPortfolio.xls" );
END;

macro MakeReports()
  var stat = true;
  SecPortfolio_Form = SP_SecPortfolio_Panel();
  while(stat)
     stat = SecPortfolio_Form.Run();
     if( stat )
        var Portf_Report = SP_Portf_Report();
        Portf_Report.Run();
        if (not Portf_Report.CReport_DataExist())
           msgbox("Нет данных для формирования отчета");
        end;
        DL_CallInsertStat(DL_OUTREPORT_EXCEL);
     end;
  end;
end; 

MakeReports();
exit(1);
