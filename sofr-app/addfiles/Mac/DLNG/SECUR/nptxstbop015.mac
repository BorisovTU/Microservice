/*
 $Name: nptxstbop015.mac
 $Module: Ценные бумаги
 $Description: Сопровождение событий СНОБ. Шаг "Повторная отправка. Пересчет записи в связи с изменением СНОБ"
 */

import DealsInter, "nptxstbfun.mac";

private macro GetSystDateTime(SystDate:@date, SystTime:@time)
  var cmd = DL_RSDCommand("select sysdate dt from dual");
  var DataSet = cmd.execute();

  DataSet.moveNext();

  DtTmSplit(DataSet.dt, SystDate, SystTime);
end;

private macro CheckAndCancelSTB(STB)
  var err = 0;

  if(not((STB.rec.ConfirmState == NPTXTOTALBASE_CONFIRMSTATE_NOTCONFIRMED) and (STB.rec.StorState == NPTXTOTALBASE_STORSTATE_CANCELED)))
    STB.rec.ConfirmState = NPTXTOTALBASE_CONFIRMSTATE_SHIPMENTCANCELED; //Отправка отменена
    STB.rec.StorState    = NPTXTOTALBASE_STORSTATE_CANCELED;        //Отмененная
    STB.rec.IdentErrStr  = "";
    STB.rec.NeedRecalc   = "";
    err = CheckAndSaveSTB(STB);
    if(err)
      msgbox("Ошибка при обновлении записи");
    else
      DL_NPTX_TbAddMsg(STB.rec.TBID, "Событие СНОБ отменено и не будет отправлено в Хранилище.");
    end;
  end;

  return err;
end;

macro ExecuteStep( doc, FDoc, DocKind, ID_Operation, ID_Step )
  var err = 0;
  var OldSTB = TRecHandler("nptxtotalbase.dbt");
  var addOldSTBArr = TArray();
  var STB = TRecHandler("nptxtotalbase.dbt");
  var nptxop = TRecHandler("nptxop.dbt");
  var query, cmd, DataSet;
  var i = 0;
  var ErrStr = "";

  SetBuff( OldSTB, FDoc );

  //Отбор, включая текущую OldSTB
  query =    "select distinct * "
           + "  from dnptxtotalbase_dbt "
           + " start with t_TBID = ? "
           + " connect by t_OrigTBID = prior t_TBID";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(OldSTB.rec.TBID);
  DataSet = cmd.Execute();
  while(DataSet.moveNext())
    DataSet.GetRecord().CopyTo(STB.rec);

    err = CheckAndCancelSTB(STB);
    if(err)
      break;
    end;
  end;

  if(not err)
    STB_LoadAddSTBArr(@addOldSTBArr, OldSTB);
    if(addOldSTBArr.size > 0)
      while((not err) and (i < addOldSTBArr.size))
        if(addOldSTBArr[i].rec.TBID > 0)
          err = CheckAndCancelSTB(addOldSTBArr[i]);
        end;

        i = i + 1;
      end;
    end;
  end;

  query =   " select * "
          + "   from dnptxop_dbt "
          + "  where t_id = ? " 
          + "    and t_dockind = ? ";

  cmd = DL_RSDCommand(query);
  cmd.AddParam(OldSTB.rec.DocID);
  cmd.AddParam(OldSTB.rec.DocKind);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    DataSet.GetRecord().CopyTo(nptxop.rec);
  end;
  
  if(not err)
    if( (ПРОГРЕССИВНАЯ_ШКАЛА_ДЛЯ_НДФЛ() == true) and (nptxop.rec.OperDate >= GetStartProgressScaleDate()) )
      err = STB_Recalc(OldSTB, addOldSTBArr, nptxop, NULL, @ErrStr);
    else
      if(addOldSTBArr.size > 0)
        err = STB_RecalcByChangeSNOB(OldSTB, addOldSTBArr[0], NULL, NULL, @ErrStr);
      else
        var addOldSTB = TRecHandler("nptxtotalbase.dbt");

        addOldSTB.Clear();

        err = STB_RecalcByChangeSNOB(OldSTB, addOldSTB, NULL, NULL, @ErrStr);
      end;
    end;

    if(err)
      msgbox(ErrStr);
    end;
  end;

  if(not err)
    if(not Opr_InsertBranch("S", "I", true))
      msgbox("Ошибка при вставке цепочки");
      err = 1;
    end;
  end;

  if(not err)
    err = DL_NPTX_SaveTbMes();
  end;

  return err;
end;
