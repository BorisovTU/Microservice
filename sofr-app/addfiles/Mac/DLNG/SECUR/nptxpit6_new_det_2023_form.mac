/**
  @file nptxpit6_new_det_form.mac
  @brief Панель запуска отчета 6-НДФЛ с расшифровками

  #menu
  - БО ЦБ\Отчеты\Отчеты для НДФЛ\6-НДФЛ с расшифровками
  - БО ЦБ\Отчеты\6-НДФЛ с расшифровками

  #tag
  - 6-НДФЛ
  - 6-НДФЛ с расшифровками

  #changeLog
  |date      |author       |tasks    |note
  |----------|-------------|---------|--------------------------------------------------------------------
  |18.01.2024|Хромеев Д. С.|BOSS-1937|BOSS-1044.1 Изменение формы запуска (интерфейса запуска). Разработка
*/

import "DlRepForm_h.mac", "dlmisc.mac";

//Номера полей в панели
const NPTX_PIT6_NEW_FLDNUM_P1_CHECKBOX             = 0;
const NPTX_PIT6_NEW_FLDNUM_P1_BEGDATE              = 1;
const NPTX_PIT6_NEW_FLDNUM_P1_ENDDATE              = 2;
const NPTX_PIT6_NEW_FLDNUM_P2_CHECKBOX             = 3;
const NPTX_PIT6_NEW_FLDNUM_P2_BEGDATE              = 4;
const NPTX_PIT6_NEW_FLDNUM_P2_ENDDATE              = 5;
const NPTX_PIT6_NEW_FLDNUM_APP1_CHECKBOX           = 6;
const NPTX_PIT6_NEW_FLDNUM_APP1_ENDDATE            = 7;
const NPTX_PIT6_NEW_FLDNUM_CLIENT_RADIO            = 8;
const NPTX_PIT6_NEW_FLDNUM_CLIENTCODE              = 9;
const NPTX_PIT6_NEW_FLDNUM_CLIENTNAME              = 10;
const NPTX_PIT6_NEW_FLDNUM_LOADFILE_RADIO          = 11;
const NPTX_PIT6_NEW_FLDNUM_LOADFILE                = 12;
const NPTX_PIT6_NEW_FLDNUM_BOCB_CHECKBOX           = 13;
const NPTX_PIT6_NEW_FLDNUM_DEPO_CHECKBOX           = 14;
const NPTX_PIT6_NEW_FLDNUM_VS_CHECKBOX             = 15;
const NPTX_PIT6_NEW_FLDNUM_XLS_CHECKBOX            = 16;
const NPTX_PIT6_NEW_FLDNUM_XML_CHECKBOX            = 17;
const NPTX_PIT6_NEW_FLDNUM_ISDETAILS               = 18;
const NPTX_PIT6_NEW_FLDNUM_SOFR_CHECKBOX           = 19;
const NPTX_PIT6_NEW_FLDNUM_DIASOFT_CHECKBOX        = 20;
const NPTX_PIT6_NEW_FLDNUM_SLICE_CHECKBOX          = 21;
const NPTX_PIT6_NEW_FLDNUM_SLICE_NUM               = 22;
const NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX       = 23;
const NPTX_PIT6_NEW_FLDNUM_DELSLICE_NUM            = 24;
const NPTX_PIT6_NEW_FLDNUM_REPDATE                 = 25;

//"Названия" полей
private const PNFLD_P1_CHECKBOX           = 100;
private const PNFLD_P1_BEGDATE            = 101;
private const PNFLD_P1_ENDDATE            = 102;
private const PNFLD_P2_CHECKBOX           = 103;
private const PNFLD_P2_BEGDATE            = 104;
private const PNFLD_P2_ENDDATE            = 105;
private const PNFLD_APP1_CHECKBOX         = 106;
private const PNFLD_APP1_ENDDATE          = 107;
private const PNFLD_CLIENT_RADIO          = 108;
private const PNFLD_CLIENTCODE            = 109;
private const PNFLD_CLIENTNAME            = 110;
private const PNFLD_LOADFILE_RADIO        = 111;
private const PNFLD_LOADFILE              = 112; 
private const PNFLD_BOCB_CHECKBOX         = 113;
private const PNFLD_DEPO_CHECKBOX         = 114;
private const PNFLD_VS_CHECKBOX           = 115;
private const PNFLD_XLS_CHECKBOX          = 116;
private const PNFLD_XML_CHECKBOX          = 117;
private const PNFLD_ISDETAILS_CHECKBOX    = 118;
private const PNFLD_SOFR_CHECKBOX         = 119;
private const PNFLD_DIASOFT_CHECKBOX      = 120;
private const PNFLD_SLICE_CHECKBOX        = 121;
private const PNFLD_SLICE_NUM             = 122;
private const PNFLD_DELSLICE_CHECKBOX     = 123;
private const PNFLD_DELSLICE_NUM          = 124;
private const PNFLD_REPDATE               = 125;

/**
  @brief Получить дату первого дня предыдущего квартала для curdate
  @return DATE
*/
private macro GetFirstDateOfPrevQuart():DATE
   var FirstDate, CurYear;

   DateSplit({curdate}, NULL, NULL, CurYear);

   if({curdate} >= date(1, 4, CurYear))
      if({curdate} <= date(30, 6, CurYear))
         FirstDate = date(1, 1, CurYear);
      elif(({curdate} >= date(1, 7, CurYear)) and ({curdate} <= date(30, 9, CurYear)))
         FirstDate = date(1, 4, CurYear);
      elif(({curdate} >= date(1, 10, CurYear)) and ({curdate} <= date(31, 12, CurYear)))
         FirstDate = date(1, 7, CurYear);
      end;
   else
      FirstDate = date(1, 10, CurYear - 1);
   end;

   return FirstDate;
end;

/**
  @brief Получить первый день налогового года
  @return Если curdate < 01.04, то налоговый год определяется как год из curdate - 1, иначе налоговый год - год из curdate
*/
private macro GetFirstDateOfYear():DATE
   var CurYear;
   DateSplit({curdate}, NULL, NULL, CurYear);

   return IIF({curdate} >= date(1, 4, CurYear), date(1, 1, CurYear), date(1, 1, CurYear - 1));
end;

/**
  @brief Получить дату последнего дня предыдущего квартала
  @return DATE
*/
private macro GetLastDayOfPrevQuart()
   var LastDate, CurYear;

   DateSplit({curdate}, NULL, NULL, CurYear);

   if({curdate} >= date(1, 4, CurYear))
      if({curdate} <= date(30, 6, CurYear))
         LastDate = date(31, 3, CurYear);
      elif(({curdate} >= date(1, 7, CurYear)) and ({curdate} <= date(30, 9, CurYear)))
         LastDate = date(30, 6, CurYear);
      elif(({curdate} >= date(1, 10, CurYear)) and ({curdate} <= date(31, 12, CurYear)))
         LastDate = date(30, 9, CurYear);
      end;
   else
      LastDate = date(31, 12, CurYear - 1);
   end;

   return LastDate;
end;

/**
 @brief Получить следующий номер среза для периода
 @param[in] pBegDate дата начала периода
 @param[in] pEndDate дата конца периода
*/
PRIVATE MACRO GetSliceNum(pBegDate, pEndDate):INTEGER
  var query = "select NVL(max(T_SLICENUM+1), 0) as t_SliceNum from DNPTXSLICE_DBT where T_DATEFROM = ? and T_DATETO = ? ";
  var cmd = DL_RSDCommand(query);
      cmd.AddParam(pBegDate);
      cmd.AddParam(pEndDate);

  var DataSet = cmd.Execute();
    if(DataSet.MoveNext())
      return DataSet.SliceNum;
    end;
  return 0;
END;

/**
 @brief Проверить существование номера среза для периода
 @param[in] pSliceNum номер среза
 @param[in] pBegDate дата начала периода
 @param[in] pEndDate дата конца периода
 @return 1 - срез существует
 @return 0 - срез отсутствует
*/
PRIVATE MACRO HasSliceNum(pSliceNum, pBegDate, pEndDate):INTEGER
  var query = "select 1 from DNPTXSLICE_DBT where T_SLICENUM = ? and T_DATEFROM = ? and T_DATETO = ? ";
  var cmd = DL_RSDCommand(query);
      cmd.AddParam(pSliceNum);
      cmd.AddParam(pBegDate);
      cmd.AddParam(pEndDate);

  var DataSet = cmd.Execute();
    if(DataSet.MoveNext())
      return 1;
    end;
  return 0;
END;


/**
  @brief Обработчик полей панели по умолчанию
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
  @return CM_DEFAULT
*/
PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  end;
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

/**
  @brief Возвращает системную дату сервера приложений
  @return DATE
*/
PRIVATE MACRO GetSysDate():DATE
   var query = "select sysdate dt from dual ";
   var cmd = DL_RSDCommand(query);
   var ds = cmd.Execute();

   if(ds.MoveNext())
      return DATE(ds.dt);
   end;

   return DATE(0, 0, 0);
END;

/**
  @brief Обработчик checkBox'а "Раздел 2"
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
  @return CM_DEFAULT
*/
PRIVATE MACRO DL_FldProc_P2_CheckBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var App1CheckBoxVal = UNSET_CHAR;
  var P2EndDate:DATE;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "X";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if(FldRealValue == SET_CHAR)
        DisableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_APP1_ENDDATE);
        pThis.GetLinkedValue(PNFLD_P2_ENDDATE, NULL, @P2EndDate);
        pThis.SetLinkedValue(PNFLD_APP1_ENDDATE, P2EndDate);
        EnableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_ISDETAILS);
     else
        pThis.GetLinkedValue(PNFLD_APP1_CHECKBOX, NULL, @App1CheckBoxVal);
        if(App1CheckBoxVal == SET_CHAR)
           EnableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_APP1_ENDDATE);
        end;
        pThis.SetLinkedValue(PNFLD_APP1_ENDDATE, GetLastDayOfPrevQuart());
        pThis.SetLinkedValue(PNFLD_ISDETAILS_CHECKBOX, UNSET_CHAR);
        DisableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_ISDETAILS);
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if(FldRealValue == "X")
           FldShowValue = FldRealValue = " ";
        else
           FldShowValue = FldRealValue = "X";
        end;
     end;
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик checkBox'а "Приложение 1"
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
  @return CM_DEFAULT
*/
PRIVATE MACRO DL_FldProc_Appl1_CheckBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var P2CheckBoxVal:STRING;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        FldRealValue = "X";
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
     FldRealValue = FldShowValue;
     if(FldRealValue == SET_CHAR)
        pThis.GetLinkedValue(PNFLD_P2_CHECKBOX, NULL, @P2CheckBoxVal);
        if(P2CheckBoxVal != SET_CHAR)
           EnableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_APP1_ENDDATE);
        end;
     else
        DisableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_APP1_ENDDATE);
     end;
  elif( Cmd == DLG_KEY )
     if( Key == KEY_SPACE )
        if(FldRealValue == "X")
           FldShowValue = FldRealValue = " ";
        else
           FldShowValue = FldRealValue = "X";
        end;
     end;
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик поля "Клиент" (справка для выдачи клиенту)
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
  @return CM_DEFAULT
*/
private macro DL_FldProc_ClientCode(pThis:DL_CPanel, cmd:Integer, key:Integer, fldShowValue:@Variant, fldRealValue:@Variant):INTEGER
   var Party = TRecHandler("party.dbt");
   var partcode = TBFile("partcode.dbt", "R", 0);
   var ClientsNames = "";
   var whereCond = "";
   var PartyIDArray = TArray();
   var P2BegDate:DATE, P2EndDate:DATE;

   if(cmd == DLG_INIT)
      if(fldRealValue == NULL)
         PartyIDArray[0] = -1;
         FldRealValue = PartyIDArray;
         fldShowValue = STR_ALLVALUE;
         pThis.SetLinkedValue(PNFLD_CLIENTNAME, "");
      end;
   elif(cmd == DLG_CHANGEVALUE)
      if(ValType(FldRealValue) == V_INTEGER)
         PartyIDArray[0] = FldRealValue;
         FldRealValue = PartyIDArray;
         fldShowValue = STR_ALLVALUE;
         pThis.SetLinkedValue(PNFLD_CLIENTNAME, "");
      end;
   elif(cmd == DLG_KEY)
      if(key == KEY_SPACE)
         fldShowValue = STR_ALLVALUE;
         FldRealValue = -1;
         pThis.SetLinkedValue(PNFLD_CLIENTNAME, "");
      elif(key == KEY_F3)
         pThis.GetLinkedValue(PNFLD_P2_BEGDATE, NULL, @P2BegDate);
         pThis.GetLinkedValue(PNFLD_P2_ENDDATE, NULL, @P2EndDate);

         var Year = 0;

         datesplit(P2EndDate, null, null, Year);

         whereCond = "     t.t_LegalForm = 2"
                   + " and (exists( "
                   + "              select 1 "
                   + "              from dnptxobj_dbt objExist "
                   + "              where objExist.t_Client = t.t_PartyID "
                   + "                and objExist.t_Date between " + GetSQLDate(P2BegDate) + " and " + GetSQLDate(P2EndDate)
                   + "            ) "
                   + "       or exists(select 1 from dnptxtotalbase_dbt tb where tb.t_ClientID = t.t_PartyID and (tb.t_IncDate between " + GetSQLDate(P2BegDate) + " and " + GetSQLDate(P2EndDate)+" or tb.t_TaxPeriod = "+Year+"))"
                   + "     )";

         if(ListPText(Party, Party.rec.PartyID, whereCond, NULL, NULL, PartyIDArray, 101) == true)
            if((PartyIDArray.Size() == 0) and (Party.rec.PartyID > 0))
               PartyIDArray[0] = Party.rec.PartyID;
            end;

            fldRealValue = PartyIDArray;
            if(PartyIDArray.Size() == 1)
               partcode.Clear();
               partcode.rec.PartyID = Party.rec.PartyID;
               partcode.rec.CodeKind = 101;
               if(partcode.GetEQ())
                 fldShowValue = partcode.rec.Code;
               else
                 fldShowValue = "";
               end;
            else
               fldShowValue = PartyIDArray.Size();
            end;

            var PartyQuery = "select t_ShortName from dparty_dbt where t_PartyID in (";
            var PartyCMD = DL_RSDCommand();
            var i = 0;
            while(i < PartyIDArray.Size())
               if(i > 0)
                  PartyQuery = PartyQuery + ", ?";
               else
                  PartyQuery = PartyQuery + "?";
               end;
               PartyCMD.AddParam(PartyIDArray[i]);
               
               i = i + 1;
            end;
            PartyQuery = PartyQuery + ")";
            var PartyDS = PartyCMD.Execute(PartyQuery);
            if(PartyDS.MoveNext())
               ClientsNames = ClientsNames + PartyDS.ShortName;
            end;
            while(PartyDS.MoveNext())
               ClientsNames = ClientsNames + ", " + PartyDS.ShortName;
            end;

            pThis.SetLinkedValue(PNFLD_CLIENTNAME, ClientsNames);
         end;
      end;
   end;
end;


PRIVATE MACRO DL_FldProc_RadioLoadFile( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;

    if(FldRealValue == UNSET_CHAR)
      DisableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_LOADFILE);
      pThis.SetLinkedValue(PNFLD_LOADFILE, "");
    end;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      pThis.SetLinkedValue(PNFLD_CLIENT_RADIO, "");
      DisableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_CLIENTCODE);
      pThis.SetLinkedValue(PNFLD_CLIENTCODE, NULL);
      
      FldShowValue = FldRealValue = "X";
      EnableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_LOADFILE);
    end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_RadioClient( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER

  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = "";
    end;
    FldShowValue = FldRealValue;

    if(FldRealValue == UNSET_CHAR)
      DisableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_CLIENTCODE);
      pThis.SetLinkedValue(PNFLD_CLIENTCODE, NULL);
    end;

    DisableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_CLIENTNAME);
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      pThis.SetLinkedValue(PNFLD_LOADFILE_RADIO, "");
      DisableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_LOADFILE);
      pThis.SetLinkedValue(PNFLD_LOADFILE, "");
      
      FldShowValue = FldRealValue = "X";
      EnableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_CLIENTCODE);
    end;
  end;

  return CM_DEFAULT;
END;;

//Получить имя папки csv-файлов
PRIVATE MACRO GetExcelPath()
  var RegistryPath = "РСХБ\\ДИРЕКТОРИИ\\IN\\СПИСОК_ДЛЯ-6-НДФЛ";
  var Path = "";
  var stat = 0;
 
  GetRegistryValue(RegistryPath, V_STRING, Path, stat);
  if((stat) or (Path == "")) 
    msgbox("Не задана настройка в реестре банка:|", RegistryPath);      
  else
    if(substr(Path, strlen(Path)-1, 1) != "\\")
      Path = Path + "\\";
    end;
  end;

  return Path;
END;

/* Обработчик клиента */
private macro DL_FldProc_ExcelFIleName( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var saveFldRealValue = FldRealValue;
  if (Cmd == DLG_INIT)
    if((FldRealValue == null) or (FldRealValue == ""))
      if( FldRealValue == null )
         FldRealValue = "";
      end;
      if( FldRealValue == "" )
         FldShowValue = "";
      end;
    else
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) // Вызов листалки и заполнение FldShowValue и FldRealValue
    var file_name = "";
    if(SelectFile(file_name, MergeFile(GetExcelPath(), "*.xls*"), NULL, 0, true)) 
      FldRealValue = file_name;
      FldShowValue = FldRealValue;
    end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = "";
     FldShowValue = "";
  end;

  return CM_DEFAULT;
end;

/**
  @brief Обработчик номера среза
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
  @return CM_DEFAULT
*/
PRIVATE MACRO DL_FldProc_SLICE_NUM( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = 0;
    end;
    FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    if(pThis.GetLinkedValue(PNFLD_SLICE_CHECKBOX) == SET_CHAR)
      FldRealValue = FldShowValue;
      pThis.SetLinkedValue(PNFLD_DELSLICE_NUM, IIF((FldRealValue-1) > 0, FldRealValue-1, 0));

      if( FldRealValue > 0 )
        EnableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX);
      else
        pThis.SetLinkedValue(PNFLD_DELSLICE_CHECKBOX, UNSET_CHAR);
        DisableFields(pThis.Data(), NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX);
      end;
    end;
  end;

  return CM_DEFAULT;
END;

/**
  @brief Обработчик начальной даты выпуска отчета
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
PRIVATE MACRO DL_FldProc_BeginDate2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var res = DL_FLDProc_BeginDate(pThis, Cmd, Key, @FldShowValue, @FldRealValue);
  var pSliceNum = GetSliceNum(FldRealValue, pThis.GetLinkedValue(PNFLD_P2_ENDDATE));

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    if(pThis.GetLinkedValue(PNFLD_SLICE_CHECKBOX) == SET_CHAR)
      pThis.SetLinkedValue(PNFLD_SLICE_NUM, pSliceNum);
      pThis.SetLinkedValue(PNFLD_DELSLICE_NUM, IIF((pSliceNum-1) > 0, pSliceNum-1, 0));
    end;
  end;
  return res;
END;

/**
  @brief Обработчик конечной даты выпуска отчета
  @param [in] pThis Объект панели
  @param [in] Cmd Действие
  @param [in] Key Нажатая комбинация клавиш
  @param [in, out] FldShowValue Значение, отображаемое в панели
  @param [in, out] FldRealValue Значение, хранящееся в поле
*/
PRIVATE MACRO DL_FldProc_EndDate2( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  var res = DL_FLDProc_EndDate(pThis, Cmd, Key, @FldShowValue, @FldRealValue);
  var pSliceNum = GetSliceNum(pThis.GetLinkedValue(PNFLD_P2_BEGDATE), FldRealValue);

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    if(pThis.GetLinkedValue(PNFLD_SLICE_CHECKBOX) == SET_CHAR)
      pThis.SetLinkedValue(PNFLD_SLICE_NUM, pSliceNum);
      pThis.SetLinkedValue(PNFLD_DELSLICE_NUM, IIF((pSliceNum-1) > 0, pSliceNum-1, 0));
    end;
  end;
  return res;
END;

/**
  @brief Класс панели 6-НДФЛ с расшифровками
*/
CLASS (DL_CPanel) NPTX_Pit6_NEW_FORM()

   /**
     @brief Инициализация панели
     Здесь происходит добавление обработчиков полей
   */
   private macro Init()
      var p1begDate = GetFirstDateOfPrevQuart(), p2BegDate = GetFirstDateOfYear(), p2EndDate = GetLastDayOfPrevQuart(), p1EndDate = p2EndDate;
      var App1EndDate:DATE = GetLastDayOfPrevQuart();
      var pSliceNum = GetSliceNum(p2BegDate, p2EndDate);
      var pSliceNumDel = IIF((pSliceNum-1) > 0, pSliceNum-1, 0);
      var SliceOn = false;

      DisableFields(This.Data(), NPTX_PIT6_NEW_FLDNUM_DEPO_CHECKBOX);

      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_P1_CHECKBOX,          PNFLD_P1_CHECKBOX,          @DL_FldProc_CheckBox,            SET_CHAR   );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_P1_BEGDATE,           PNFLD_P1_BEGDATE,           @DL_FldProc_BeginDate,           p1BegDate  );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_P1_ENDDATE,           PNFLD_P1_ENDDATE,           @DL_FldProc_EndDate,             p1EndDate  );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_P2_CHECKBOX,          PNFLD_P2_CHECKBOX,          @DL_FldProc_P2_CheckBox,         SET_CHAR   );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_P2_BEGDATE,           PNFLD_P2_BEGDATE,           @DL_FldProc_BeginDate2,          p2BegDate  );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_P2_ENDDATE,           PNFLD_P2_ENDDATE,           @DL_FldProc_EndDate2,            p2EndDate  );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_APP1_CHECKBOX,        PNFLD_APP1_CHECKBOX,        @DL_FldProc_Appl1_CheckBox,      UNSET_CHAR );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_APP1_ENDDATE,         PNFLD_APP1_ENDDATE,         @DL_FLDProc_EndDate,             App1EndDate);
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_CLIENT_RADIO,         PNFLD_CLIENT_RADIO,         @DL_FldProc_RadioClient,         SET_CHAR   );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_CLIENTCODE,           PNFLD_CLIENTCODE,           @DL_FldProc_ClientCode                      );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_CLIENTNAME,           PNFLD_CLIENTNAME,           @Default                                    );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_LOADFILE_RADIO,       PNFLD_LOADFILE_RADIO,       @DL_FldProc_RadioLoadFile,       UNSET_CHAR );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_LOADFILE,             PNFLD_LOADFILE,             @DL_FldProc_ExcelFIleName,       ""         );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_BOCB_CHECKBOX,        PNFLD_BOCB_CHECKBOX,        @DL_FldProc_CheckBox,            SET_CHAR   );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_DEPO_CHECKBOX,        PNFLD_DEPO_CHECKBOX,        @DL_FldProc_CheckBox,            UNSET_CHAR );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_VS_CHECKBOX,          PNFLD_VS_CHECKBOX,          @DL_FldProc_CheckBox,            SET_CHAR   );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_XLS_CHECKBOX,         PNFLD_XLS_CHECKBOX,         @DL_FldProc_CheckBox,            UNSET_CHAR );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_XML_CHECKBOX,         PNFLD_XML_CHECKBOX,         @DL_FldProc_CheckBox,            SET_CHAR   );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_ISDETAILS,            PNFLD_ISDETAILS_CHECKBOX,   @DL_FldProc_CheckBox,            UNSET_CHAR );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_SOFR_CHECKBOX,        PNFLD_SOFR_CHECKBOX,        @DL_FldProc_CheckBox,            SET_CHAR   );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_DIASOFT_CHECKBOX,     PNFLD_DIASOFT_CHECKBOX,     @DL_FldProc_CheckBox,            SET_CHAR   );
      AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_REPDATE,              PNFLD_REPDATE,              @Default,                        {curdate}  );
      
      GetRegistryValue("РСХБ\\СОХРАНЕНИЕ СРЕЗА НДФЛ", V_BOOL, @SliceOn, NULL);
      if(SliceOn == false)
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_SLICE_CHECKBOX,       PNFLD_SLICE_CHECKBOX,       @Default,                        UNSET_CHAR );
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_SLICE_NUM,            PNFLD_SLICE_NUM,            @Default,                        0          );
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX,    PNFLD_DELSLICE_CHECKBOX,    @Default,                        UNSET_CHAR );
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_DELSLICE_NUM,         PNFLD_DELSLICE_NUM,         @Default,                        0          );
        DisableFields(Data(), NPTX_PIT6_NEW_FLDNUM_SLICE_CHECKBOX);
        DisableFields(Data(), NPTX_PIT6_NEW_FLDNUM_SLICE_NUM);
        DisableFields(Data(), NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX);
      else
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_SLICE_CHECKBOX,       PNFLD_SLICE_CHECKBOX,       @DL_FldProc_CheckBox,            SET_CHAR   );
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_SLICE_NUM,            PNFLD_SLICE_NUM,            @DL_FldProc_SLICE_NUM,           pSliceNum  );
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX,    PNFLD_DELSLICE_CHECKBOX,    @DL_FldProc_CheckBox,            UNSET_CHAR );
        AddFieldProc(0, NPTX_PIT6_NEW_FLDNUM_DELSLICE_NUM,         PNFLD_DELSLICE_NUM,         @Default,                        pSliceNumDel);
      end;
   end;

   /**
     @brief Проверка полей панели перед запуском отчета
   */
   private macro Check():BOOL
      var p1BegDate = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_P1_BEGDATE),
          p1EndDate = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_P1_ENDDATE),
          p2BegDate = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_P2_BEGDATE),
          p2EndDate = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_P2_ENDDATE),
          IsP1      = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_P1_CHECKBOX),
          IsP2      = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_P2_CHECKBOX),
          IsBO      = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_BOCB_CHECKBOX),
          IsDEPO    = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_DEPO_CHECKBOX),
          IsVS      = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_VS_CHECKBOX),
          IsXLS     = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_XLS_CHECKBOX),
          IsXML     = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_XML_CHECKBOX),
          IsDetails = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_ISDETAILS),
          IsSOFR    = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_SOFR_CHECKBOX),
          IsDiasoft = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_DIASOFT_CHECKBOX),
          LoadFileRadio = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_LOADFILE_RADIO),
          LoadFile      = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_LOADFILE),
          IsApp1    = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_APP1_CHECKBOX),  
          IsSlice     = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_SLICE_CHECKBOX),
          IsDelSlice  = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_DELSLICE_CHECKBOX),
          pSliceNum   = GetFieldValue(NPTX_PIT6_NEW_FLDNUM_SLICE_NUM),
          p1BegDateYear, p1EndDateYear, p2BegDateYear, p2EndDateYear;

      if((IsP2 != SET_CHAR) and (IsDetails == SET_CHAR))
         msgbox("Для печати расшифровки должен быть установлен признак \"Раздел 2\"");
         return false;
      end;

      if(p1BegDate > p1EndDate)
         msgbox("Дата начала отчетного периода для раздела 1 не может быть больше даты конца отчетного периода для раздела 1");
         return false;
      end;

      if(p2BegDate > p2EndDate)
         msgbox("Дата начала отчетного периода для раздела 2 не может быть больше даты конца отчетного периода для раздела 2");
         return false;
      end;

      if(p2BegDate > p1BegDate)
         msgbox("Дата начала отчетного периода для раздела 2 не может превышать дату начала отчетного периода для раздела 1");
         return false;
      end;

      if((IsBO != SET_CHAR) and (IsDEPO != SET_CHAR) and (IsVS != SET_CHAR))
         msgbox("Хотя бы один из флагов формирования отчета по данным модулей должен быть установлен");
         return false;
      end;

      if((IsP1 != SET_CHAR) and (IsP2 != SET_CHAR) and (IsApp1 != SET_CHAR))
         msgbox("Хотя бы один из флагов формирования отчета по разделам или Приложения 1 должен быть установлен");
         return false;
      end;

      if(HasSliceNum(pSliceNum, p2BegDate, p2EndDate) and (IsSlice == SET_CHAR) and (IsDelSlice == UNSET_CHAR))
        if (MsgBoxEx("В системе уже сохранен срез с № "+pSliceNum+" за период "+p2BegDate+" - "+p2EndDate+". Заменить сохраненные данные?", MB_YES + MB_NO, IND_YES) != IND_YES)
          return false;
        else
          this.SetLinkedValue(PNFLD_DELSLICE_NUM, pSliceNum);
          this.SetLinkedValue(PNFLD_DELSLICE_CHECKBOX, SET_CHAR);
        end;
      end;

      DateSplit ( p1BegDate, NULL, NULL, p1BegDateYear );
      DateSplit ( p1EndDate, NULL, NULL, p1EndDateYear );
      DateSplit ( p2BegDate, NULL, NULL, p2BegDateYear );
      DateSplit ( p2EndDate, NULL, NULL, p2EndDateYear );

      if(not ((p1BegDateYear == p1EndDateYear) and (p2BegDateYear == p2EndDateYear) and (p1BegDateYear == p2BegDateYear)))
         msgbox("Все даты должны принадлежать одному календарному году");
         return false;
      end;

      if((LoadFileRadio == SET_CHAR) and (trim(LoadFile) == ""))
        msgbox("Необходимо указать файл со списком клиентов");
        return false;
      end;

      if((IsSOFR != SET_CHAR) and (IsDiasoft != SET_CHAR))
         msgbox("Должен быть установлен хотя бы один из признаков формирования отчета: СОФР или Диасофт");
         return false;
      end;

      if((IsXLS != SET_CHAR) and (IsXML != SET_CHAR))
         msgbox("Должен быть установлен хотя бы один из признаков формирования отчета: XLS или XML");
         return false
      end;

      if((IsXML == SET_CHAR) 
         and  ( (p2BegDate != GetFirstDateOfYear()) 
             or (p2EndDate != GetLastDayOfPrevQuart())
             or (p1BegDate != GetFirstDateOfPrevQuart())))
         return (MsgBoxEx("Период отчета не совпадает с требованиями по предоставлению отчета. Продолжить?", MB_YES + MB_NO) == IND_YES);
      else
        return true;
      end;
   end;

   InitDL_CPanel("sp_rep.lbr", "NPTX6D23");
END;
