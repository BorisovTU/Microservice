/*
$Name:        nptxmass_rep.mac
$Module:      Ценные бумаги
$Description: Печать протокола массового создания операций НОБ
*/

IMPORT globals, ActiveX, oralib, "global_utils_intgr.mac";



PRIVATE CLASS (DL_CReportTemplate) MassProtocol_Report(SubKind)
  var m_SubKind = SubKind;

  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO Create()
    var query, cmd, DataSet;
    var cnt, i;
    var Comment = "";

    SetActiveSheet("Протокол");

    RegisterTable( "N1_MainTable", NULL,
                   "N1_No",  
                   "N1_SOFR_ID",
                   "N1_CFT_ID",   
                   "N1_ClientName", 
                   "N1_ContrNumber",  
                   "N1_Comment"   
                 );


    query =   "SELECT q.* "
            + "  FROM (SELECT MP.T_CLIENTID, "                                   
            + "               NVL(TO_CHAR(MP.T_CLIENTCODE), CHR(1)) T_CLIENTCODE, "
            + "               NVL(MP.T_CLIENTNAME, CHR(1))          T_CLIENTNAME, "
            + "               NVL(MP.T_NPTXNUMBER, CHR(1))          T_NPTXNUMBER, "
            + "               NVL(MP.T_COMMENT, CHR(1))             T_COMMENT, "
            + "               NVL(MP.T_CONTRNUM, CHR(1))            T_CONTRNUM "
            + "          FROM DNPTXMASSPROT_DBT mp "
            + "       ) q "
            + " ORDER BY q.T_CLIENTID ASC, q.T_CLIENTCODE ASC, q.T_CONTRNUM ASC";                              

    cmd = DL_RSDCommand(query);
    cmd.AddParam({curdate});
    cmd.AddParam({curdate});

    cnt = cmd.GetCount();
    if(cnt > 0)
      InitProgress(cnt, "Печать протокола", "Печать протокола");

      i = 0;
      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        var ContrNum = DataSet.CONTRNUM;
        
        Comment  = DataSet.Comment;

        if(index(Comment, "NOT_SHOW_CONTRNUM") > 0)
          ContrNum = "";
          Comment = strsubst(Comment, "NOT_SHOW_CONTRNUM", "");
        end; 

        PrintTableLine("N1_No",          int(i+1),           null,
                       "N1_SOFR_ID",     DataSet.CLIENTID,   null,
                       "N1_CFT_ID",      DataSet.CLIENTCODE, null,
                       "N1_ClientName",  DataSet.CLIENTNAME, null,
                       "N1_ContrNumber", ContrNum,           null,
                       "N1_OpNUm",       DataSet.NPTXNUMBER, null,
                       "N1_Comment",     Comment,            null
                      );

        UseProgress(i = i + 1);
      end;

      RemProgress();
    end;

    EndTable();
    
    if(m_SubKind == DL_TXBASECALC_OPTYPE_LUCRE)
      SetActiveSheet("Нулевая мат. выгода");

      PrintFormatString( NULL, "N2_HeadText", "Внебиржевые и ОТС сделки, по которым рыночная цена равна фактической цене сделки");

      RegisterTable( "N2_MainTable", NULL,
                     "N2_SOFR_ID",
                     "N2_CFT_ID",   
                     "N2_ClientName", 
                     "N2_DealID",
                     "N2_ISIN",
                     "N2_FI_Name",  
                     "N2_Comment"   
                   );

      query =   " select DISTINCT prt.t_ClientID, "
              + "        NVL(TO_CHAR(prt.T_CLIENTCODE), CHR(1)) T_CLIENTCODE, "
              + "        NVL(prt.T_CLIENTNAME, CHR(1))          T_CLIENTNAME, "
              + "        tk.t_DealID, "
              + "        avr.t_ISIN, "
              + "        fin.t_Name as FI_Name "  
              + "   from dnptxmassprot_dbt prt, dnptxop_dbt op, ddl_tick_dbt tk, dfininstr_dbt fin, davoiriss_dbt avr "
              + "  where prt.t_NptxNumber <> CHR(1) "
              + "    and op.t_DocKind = " + DL_CALCNDFL
              + "    and op.t_Code = prt.t_NptxNumber "
              + "    and tk.t_BOfficeKind IN ("+DL_SECURITYDOC+", "+DL_AVRWRT+")"
              + "    and tk.t_ClientID = op.t_Client "
              + "    and tk.t_DealDate between op.t_PrevDate+1 and op.t_OperDate "
              + "    and tk.t_DealDate >= TO_DATE('01.01.'||EXTRACT(YEAR FROM op.t_OperDate), 'DD.MM.YYYY') "
              + "    and RSB_SECUR.ISOUTEXCHANGE(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BOfficeKind)), 1) = 1 "
              + "    and RSB_SECUR.ISBUY(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tk.t_DealType, tk.t_BOfficeKind))) = 1 "
              + "    and UPPER(npto.GetMaxMarket(tk.t_PFI, tk.t_DealDate, tk.t_DealID)) = 'ФАКТ'"
              + "    and fin.t_FIID = tk.t_PFI "
              + "    and avr.t_FIID = fin.t_FIID ";
      
      cmd = DL_RSDCommand(query);

      cnt = cmd.GetCount();
      if(cnt > 0)
        InitProgress(cnt, "Печать нулевой мат. выгоды", "Печать нулевой мат. выгоды");

        i = 0;
        DataSet = cmd.Execute();
        while(DataSet.moveNext())
          Comment  = "Для сделки взята фактическая цена";

          PrintTableLine("N2_SOFR_ID",   DataSet.CLIENTID,   null,
                         "N2_CFT_ID",    DataSet.CLIENTCODE, null,
                         "N2_ClientName",DataSet.CLIENTNAME, null,
                         "N2_DealID",    DataSet.DealID,     null,
                         "N2_ISIN",      DataSet.ISIN,       null,
                         "N2_FI_Name",   DataSet.FI_Name,    null,
                         "N2_Comment",   Comment,            null
                        );

          UseProgress(i = i + 1);
        end;

        RemProgress();
      end;

      EndTable();
    end;
  END;

  MACRO Run()
    Run();
    
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  initDL_CReportTemplate( NULL, "Report_NptxMass.xlsx", true, null, null, true, true );
  SetRowFlushCount(2000);  
END;

MACRO MakeMassProtocolReport(p_SaveTo, p_SubKind)
  var RepObj = MassProtocol_Report(p_SubKind);
          
  var FullFileNameXLSX = RepObj.Run();

  RepObj = NULL;

  if(not isStandAlone())
    FullFileNameXLSX = "$"+FullFileNameXLSX;
    p_SaveTo = "$"+p_SaveTo;
  end;
  
  if(FullFileNameXLSX != "")
    var FileName, FileExt;
    var ToFileName, ToFileExt;
    
    var FromDir = SplitFile(FullFileNameXLSX, FileName, FileExt);
    var ToDir = SplitFile(p_SaveTo, ToFileName, ToFileExt);
    
    if(not RenameFile(FullFileNameXLSX, FromDir+ToFileName+FileExt))
      msgbox("Ошибка при переименовании файла протокола");
    else
      if((ToDir != "") and (ToDir != FromDir))
        if(SC_CopyFile(FromDir, ToDir, ToFileName+FileExt, ToFileName+FileExt) != 0)
          msgbox("Ошибка при сохранении файла протокола");
        end;
      end;

      if((GetDialogFlag() != 0) and (SP_IsShedulerRunning() == false))
        SC_ShowFile(ToDir, ToFileName+FileExt);
      end;
    end;
  end;

  return 0;
END;
