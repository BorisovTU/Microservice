/*
$Name: nptxtrbudg010.mac
$Module: Ценные бумаги
$Description: Операция перечисления НДФЛ в бюджет. Шаг "Исполнение"
*/

import InsCarryDoc, "sp_categ.mac", "sp_car.mac", RsbDataSet, DealsInter, "nptxreqfun.mac", "pmlib.mac";

private macro Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr );
  MsgBox( ErrStr );
  return 1;
end;

/* Возвращает БИК нашего банка
*/
private macro НашБик()
  var party = TBfile("party.dbt");
  var partcode = TBfile("partcode.dbt");
  var Ok;

  party.KeyNum = 0;
  party.rec.PartyID = {OurBank};
  Ok = party.GetEQ;
  if(not Ok)
    return "";
  end;
  partcode.KeyNum = 0;
  partcode.rec.PartyID = party.rec.PartyID;
  partcode.rec.CodeKind = PTCK_BIC;
  Ok = partcode.GetEQ;
  return IIF(Ok,partcode.rec.Code,"");
END;

private macro НашКПП()
  var query, cmd, DataSet;
  var ourKPP = "";
  
  query = "SELECT '/'||SUBSTR(UTL_RAW.CAST_TO_VARCHAR2(T_TEXT), 1, INSTR(UTL_RAW.CAST_TO_VARCHAR2(T_TEXT), '/', 1) - 1) OurKPP "
        + "  FROM DNOTETEXT_DBT "
        + " WHERE T_OBJECTTYPE = 3 AND T_NOTEKIND = 79";

    cmd = DL_RSDCommand(query);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
        ourKPP = DataSet.OurKPP;
    end;

    return ourKPP;

END;

//ищет СПИ для налоговой, с назначением: 38 - налог с дохода
private macro ПолучитьБанкНалоговой( ID, settacc )
  var found = false;
  var sql = " select SettAcc.t_SettAccId, SettAcc.t_Account, pmAuto.t_KindOper " +
            "    from dsettacc_dbt SettAcc,  dpmautoac_dbt pmAuto " +
            "    where "+
            "          SettAcc.t_SettAccId = pmAuto.t_SettAccId "+
            "      and SettAcc.t_PartyID = ? "+
            "      and pmAuto.t_purpose = " + 38 +//назначение платежа - "Налог с дохода" (справочник - таблица *dpmpurp_dbt*)
            "      and SettAcc.t_FIID = "+NATCUR;
  var cmd = DL_RSDCommand(sql);
  cmd.AddParam(ID);
 
  var dataset = cmd.execute();
  if (dataset.MoveNext()) 
    settacc.KeyNum = 0;
    settacc.rec.SettAccId = dataset.SettAccId;
    settacc.GetEQ();
    found = true;
  end;
 
 return found;
end;

//Создаётся платёж по налогу + проводка по нему
private macro СформироватьПлатежНДФЛ (nptxop, PayerAccount, Taxe, Ground)
  var settacc  = TBfile("settacc.dbt");
  var pt_self  = TBFile  ("party.dbt");
  var Pmobj    = RSBPayment();
  var regValue = 0;
  var stat     = 0;
  var tr, m, y;
  var ourKPP;

  ourKpp = НашКПП();

  Pmobj.DocKind      = nptxop.DocKind;
  Pmobj.DocumentID   = string(nptxop.ID:o:34);
  Pmobj.Purpose      = PM_PURP_TAXINCOME_NJ; // Налог с дохода
  Pmobj.BaseAmount   = Taxe;
  Pmobj.BaseFIID     = NATCUR;
  Pmobj.ReceiverFIID = NATCUR;
  Pmobj.PayerFIID    = NATCUR;
  Pmobj.ValueDate    = nptxop.OperDate;   // Дата валютирования
  Pmobj.PaymStatus   = PM_READY_TO_SEND;  // Статус платежа - 3000 - Готов к отправке
  Pmobj.Number       = nptxop.ID;
  pmObj.PrimDocKind  = DOC_BO_PAYMENT;
  pmObj.NumberPack   = 11;

  Pmobj.Oper = GetMainOperInGroup(Pmobj.DocKind);
  if (Pmobj.Oper <= 0)
    Pmobj.Oper = {oper};
  end;

  if(ПереключениРеквизитовЕНП() == 1)
    Pmobj.Ground       = Ground;
    pmobj.Priority     = 3;
    pmObj.TaxPmGround = "ТП"; //платежи текущего года
    
    DateSplit(pmObj.ValueDate, null, m, y);
    
    pmObj.OKATOCode   = ПолучитьКодСубъекта({OurBank}, 74/*Код ОКТМО*/);
    pmObj.TaxPmPeriod = "МС."+m+"."+y;
    pmObj.TaxPmNumber = "0";
    pmObj.TaxPmDate   = string(pmObj.ValueDate:f);

    if(m <= 9)
      m = "0"+m;
    end;
  else
    PmObj.PaymentKind  = "";
    Pmobj.Ground       = Ground;
    pmobj.Priority     = PM_DefaultMaxPriority();
    pmObj.TaxPmGround  = "0"; 
    pmObj.OKATOCode    = "45383000";
    pmObj.TaxPmPeriod  = "0";
    pmObj.TaxPmNumber  = "0";
    pmObj.TaxPmDate    = "0";

  end;


  //Параметры налогового платежа
  pmObj.BttTICode = КБКдляЕНП();
  pmObj.PaymentByOtherPerson = PM_OTHRPRSN_PRSN;
  
  pmObj.TaxPmType   = "0";
  pmObj.PayType     = BPT_TAX;

  pmObj.PayerBankEnterDate = pmObj.ValueDate;
  pmObj.PayerChargeOffDate = pmObj.ValueDate;

  Pmobj.SetPayerPI(
              PAYMENTS_GROUP_UNDEF, // Group
              {OurBank},                       // BankID  // Банк плательщика
              3,                               // BankCodeKind
              НашБик(),                        // BankCode
              "",                              // BankName
              "",                              // CorrAcc
              NATCUR,                          // AccountFI
              1,                               // AccountChapter
              PayerAccount,                    // Account   // Счет плательщика
              IIF(ПереключениРеквизитовЕНП() == 1, {OurBank}, -1),                      // ClientID  // Плательщик
              "",                                                                       // ClientName,
              IIF(ПереключениРеквизитовЕНП() == 1, {INN_Bank}, {INN_Bank} + ourKPP ),   // ClientINN
              IIF(ПереключениРеквизитовЕНП() == 1, 3, 0),                               // ClientCodeKind,
              IIF(ПереключениРеквизитовЕНП() == 1, НашБик(), "")                        // ClientCode
             );
  
  if (not GetRegistryValue( "РСХБ\\БРОКЕРСКОЕ ОБСЛУЖИВАНИЕ\\НАЛОГОВЫЙ_АГЕНТ_ФИЗ_ЛИЦ", V_INTEGER, regValue) )
    regValue = 0;
  end;

  pt_self.KeyNum = 0;
  pt_self.rec.PartyID = regValue;
  if (not pt_self.GetEQ() )
    stat = Error("Не найден налоговый орган нашего банка");
  end;

  if (stat == 0)
    if ( not ПолучитьБанкНалоговой( pt_self.rec.PartyID, settacc ))
      stat = Error("Не найден Банк налогового органа. Проверьте платежные инструкции");
    end;
  end;

  if ( stat == 0 )
    if( settacc.rec.BankID == {OurBank} )
      Pmobj.SetReceiverPI(
                PAYMENTS_GROUP_UNDEF,                // Group
                settacc.rec.BankID,                  // BankID         // Банк налогового органа
                0,                                   // BankCodeKind
                "",                                  // BankCode
                "",                                  // BankName
                "",                                  // CorrAcc
                settacc.rec.FIID,                    // AccountFI
                1,                                   // AccountChapter
                settacc.rec.Account,                 // Account        // счет в Банке налогового органа
                IIF(ПереключениРеквизитовЕНП() == 1, pt_self.rec.PartyID, -1),                             // ClientID       // Налоговый орган
                IIF(ПереключениРеквизитовЕНП() == 1, pt_self.rec.name, pt_self.rec.name),                  // ClientName,
                IIF(ПереключениРеквизитовЕНП() == 1, "", ПолучитьКодСубъекта(pt_self.rec.PartyID, 16)),     // ClientINN,
                IIF(ПереключениРеквизитовЕНП() == 1, 1, 0),                                                // ClientCodeKind,
                IIF(ПереключениРеквизитовЕНП() == 1, ПолучитьКодСубъекта(pt_self.rec.PartyID, 1), "")       // ClientCode
               );
    else
      Pmobj.SetReceiverPI(
                PAYMENTS_GROUP_UNDEF,                // Group
                settacc.rec.BankID,                  // BankID         // Банк налогового органа
                settacc.rec.BankCodeKind,            // BankCodeKind
                settacc.rec.BankCode,                // BankCode
                settacc.rec.BankName,                // BankName
                settacc.rec.CorrAcc,                 // CorrAcc
                settacc.rec.FIID,                    // AccountFI
                1,                                   // AccountChapter
                settacc.rec.Account,                 // Account        // счет в Банке налогового органа
                IIF(ПереключениРеквизитовЕНП() == 1, pt_self.rec.PartyID, pt_self.rec.PartyID),            // ClientID       // Налоговый орган
                IIF(ПереключениРеквизитовЕНП() == 1, pt_self.rec.name, pt_self.rec.name),                  // ClientName,
                IIF(ПереключениРеквизитовЕНП() == 1, "", ПолучитьКодСубъекта(pt_self.rec.PartyID, 16)),     // ClientINN,
                IIF(ПереключениРеквизитовЕНП() == 1, 1, 0),                                                // ClientCodeKind,
                IIF(ПереключениРеквизитовЕНП() == 1, ПолучитьКодСубъекта(pt_self.rec.PartyID, 1), "")       // ClientCode
               );
    end;
  end;

  if ( settacc.rec.BankID != {OurBank} ) 
    pmobj.OutTransferDate = Pmobj.ValueDate; 
  end;
  Pmobj.IsFactPaym   = "X"; //Этот флаг должен бысть установлен уже после добавления схем расчётов. Иначе по платежу не создаётся операция 4001 (или 14001)
  pmObj.TaxAuthorState = IIF(ПереключениРеквизитовЕНП() == 1, "01", "01"); 

  pmobj.BaseRate.Actuate(nptxop.OperDate, false);
  pmobj.FactRate.Actuate(nptxop.OperDate, false);
  pmobj.Actuate();

  //Создание проводки
  tr = pmobj.MakeTransaction();
  if( tr == NULL )
    stat = Error("Ошибка при создании проводки по платежу");
  else
    tr.Shifr_Oper    = "";
    tr.ResultCarry   = 1;
    tr.Number_Pack   = pmobj.NumberPack;
    tr.Chapter       = 1;
    tr.Numb_Document = nptxop.Code;
    tr.Ground        = Ground;

    if( not tr.Carry() )
      stat = Error("Ошибка при выполнении проводки");
    else
      DL_NPTX_PutMsg( "Выполнена проводка: Дт<"+tr.AccountPayer+"> Кт<"+tr.AccountReceiver+"> на сумму " + string(tr.SumPayer) + " " + ПолучитьКодФинИн(tr.FIIDPayer) );
    end;
  end;

  return stat;
End;

private macro GetAccTrnSum(Account:string, StartDate:date, EndDate:date)
  var query, cmd, DataSet;
  var Sum = 0;

  query =   " select acctrn.t_Account_Receiver, SUM(acctrn.t_Sum_Receiver) as S"
          + "   from dacctrn_dbt acctrn "
          + "  where acctrn.t_Account_Receiver = ? "
          + "    and acctrn.t_Date_Carry >= ? "
          + "    and acctrn.t_Date_Carry <= ? "
          + "    and not Exists(select 1 "
          + "                     from doprdocs_dbt odoc, doproper_dbt op "
          + "                    where odoc.t_AccTrnID = acctrn.t_AccTrnID "
          + "                      and op.t_ID_Operation = odoc.t_ID_Operation "
          + "                      and op.t_DocKind in ("+DL_VEKSELDRAWORDER+", "+DL_VSBARTERORDER+", "+DL_VSINTERCHANGE+") "
          + "                  ) "
          + " group by acctrn.t_Account_Receiver";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(Account);
  cmd.AddParam(StartDate);
  cmd.AddParam(EndDate);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    Sum = DataSet.S;
  end;

  return Sum;
end;

private macro ОбработатьСчет(FD, nptxop, CatCode, StartDate, EndDate, RestDate, D1, D2)
  var acc = TrecHandler( "account.dbt");
  var err = 0;
  var addstr = "";
  var AccTrnSum = 0, S1, CarrySum;

  if (CatCode == "НДФЛ к перечислению, FLR")
    addStr = "по ставке 13% ";
  elif(CatCode == "НДФЛ к перечислению 15%")
    addstr = "по ставке 15% ";
  end;

  if( not FD.OpenAccount(CatCode, null, false, null, acc, nptxop.rec.OperDate, NATCUR) )
     return Error( "Ошибка при определении счета по категории <"+CatCode+">." );
  end;
  
  /*if(СрокПеречисленияНДФЛпоЦБ() == 0)
    AccTrnSum = GetAccTrnSum(acc.rec.Account, StartDate, nptxop.rec.OperDate);
  end;*/

  S1 = DL_GetRestAccount(acc.rec, RestDate);

  CarrySum = max(0, (abs(S1) - AccTrnSum));

  if(CarrySum == 0)
    DL_NPTX_PutMsg("Проводка по перечислению налога "+addstr+"в бюджет не сформирована, потому что сумма возвращенного налога больше, чем сумма удержанного налога.", 50);
  else
    err = СформироватьПлатежНДФЛ(nptxop.rec, acc.rec.Account, CarrySum, "Единый налоговый платеж (НДФЛ по операциям с ценными бумагами "+addstr+"за период с "+string(D1:f)+" по "+string(D2:f) + ")");
  end;

  return err;
end;

macro ExecuteStep( doc, FDoc )
  var err = 0;
  var nptxop = TRecHandler("nptxop.dbt");
  var DatePayment, DatePayment13;
  var day, month, year;

  SetBuff( nptxop, FDoc );

  var FD = DLFirstDocNPTXOP(nptxop);

  var transfDataSet;
  var transfDayFrom, transfDayTo, transfMonth;
  var transfBudgCmd = RsdCommand("SELECT T_MONTH Month, T_DATEFROM_TRANSF DayFrom, T_DATETO_TRANSF DayTo FROM DNPTXTRANSFBUDG_DBT WHERE T_NPTXOP_LINK = ? AND ROWNUM = 1");
  transfBudgCmd.addParam("opLink", RSDBP_IN, nptxop.rec.id);
  transfBudgCmd.execute();
  transfDataSet = RsdRecordset(transfBudgCmd);
  transfDataSet.MoveNext();
  transfMonth   = transfDataSet.value("Month");
  transfDayFrom = transfDataSet.value("DayFrom");
  transfDayTo   = transfDataSet.value("DayTo");

  //DatePayment - всегда 22 число любого месяца или следующий рабочий день (если 22-е является выходным)
  //А также последний рабочий день текущего года

  datesplit(nptxop.rec.OperDate, day, month, year);
  DatePayment = GetDateAfterWorkDays(date(22, month, year), 0);

  if(IsWorkday(date(31, 12, year)))
    DatePayment13 = date(31, 12, year);
  else
    DatePayment13 = GetDateAfterWorkDays(date(31, 12, year), -1);
  end;

  if (((month == 1) and (day >= 1) and (day <= 22)) or ((month != 1) and (day >= 6) and (day <= 22)) or ((month != 12) and (day >= 29) and (day <= 31)))
    if (MsgBoxEx("Внимание! Дата перечисления налога еще не наступила. Продолжить операцию?", MB_ERROR + MB_YES + MB_NO, IND_NO) == IND_NO)
      return 0;  
    end;
  end;

  var StartDate = date(1, month, year);

  var RestDate  = date(transfDayTo, transfMonth, year);
  var D1        = date(transfDayFrom, transfMonth, year);
  var D2        = RestDate;

  err = ОбработатьСчет(FD, nptxop, "Счет для резерв.сумм НДФЛ", StartDate, nptxop.rec.OperDate, RestDate, D1, D2);

  DL_NPTX_SaveOperLog( nptxop.rec.ID, 10 );

  return err;
end;
