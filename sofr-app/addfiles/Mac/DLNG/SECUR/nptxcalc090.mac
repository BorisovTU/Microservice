/*
$Name:        nptxcalc090.mac
$Module:      Ценные бумаги
$Description: Расчет НОБ для НДФЛ. Шаг "Расчет НДР 7 уровня"
*/

/* Операция расчета НОБ для НДФЛ
   Шаг "Расчет НДР 7 уровня"*/
IMPORT "secinter.mac", "nptxcalcfun.mac";

PRIVATE VAR Oper = TRecHandler( "nptxop.dbt" );

PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR Stat = 0;
  VAR OperationStatus;
  VAR vBegDate:DATE, 
      vEndDate:DATE;
  VAR query, cmd, DataSet;
  VAR count;
  VAR TaxPeriod = 0;

  Oper.SetRecordAddr( FDoc );

  datesplit(Oper.rec.OperDate, null, null, TaxPeriod);

  var TaxTeriodToLucre = CheckTaxTeriodToLucre(TaxPeriod);

  ClearGTT_DNPTXOBJ_TMP();
  if(Oper.rec.Recalc == SET_CHAR)//если пересчет, то по периодам пересчитываем НДР с 3 по 7 уровень.
    vBegDate = Oper.rec.BegRecalcDate;
    query = GetIncomeDateQuery(Oper.rec.ID, NPTXOPSTEP9, Oper.rec.EndRecalcDate);
    cmd = DL_RSDCommand(query);        
    count = cmd.GetCount();
    DataSet = cmd.Execute();
    if((DataSet.moveNext()) AND (not stat))
      vEndDate = DataSet.t_operdate;
      if((vEndDate != Oper.rec.OperDate) and ((Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_LUCRE) or (TaxTeriodToLucre == false)))
        Oper.rec.subkind_operation = DL_TXBASECALC_OPTYPE_NORMAL;
      end;

      if((Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_CLOSE_IIS) and IsCalcNOBCloseISS() and (Oper.rec.Contract > 0))
        stat = CreateTaxObjectsIIS7(Oper, ID_Step);
      else
        if(TaxTeriodToLucre == false)
          stat = CreateTaxObjects7(Oper, ID_Step, vBegDate, vEndDate);
          if(not stat)
            stat = CreateMaterialTaxObjects7(Oper, ID_Step, vBegDate, vEndDate);
          end;
        else
          if(Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_LUCRE)
            stat = CreateTaxObjects7(Oper, ID_Step, vBegDate, vEndDate);
          else
            stat = CreateMaterialTaxObjects7(Oper, ID_Step, vBegDate, vEndDate);
          end;
        end;
      end;
    end;
    if(not stat)
      count = count-1;//одну запись обработали
      stat = DL_NPTX_StartInsertTaxObject(Oper.rec.ID, ID_Step);
    end;
    if(not stat)
      stat = DL_NPTX_PutMsg( string(date(vEndDate)), 1000 + count );
    end;
    DL_NPTX_SaveOperLog( Oper.rec.ID, (NPTXOPSTEP9*1000) + count );

    if(not stat)
      if(count > 0)//если остались необработанные периоды, то добавляем текущий блок, иначе- следующий
        if((Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_LUCRE) and (TaxTeriodToLucre == true))
          OperationStatus = 13; //Пересчет НДР457
        else
          OperationStatus = 10; //Пересчет НДР 3-7 уровней
        end;
      else
        OperationStatus = 11; //Пересчет НДР8
      end;
      if( not InsertOprStatus( 46052 /*Расчет НОБ*/, OperationStatus) ) 
        return Error( "Ошибка при установке статуса операции" );
      end;
    end;

  else
    stat = DL_NPTX_PutMsg( "Расчет НДР 7 уровня", 40 );
    if( not stat )
      if((Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_CLOSE_IIS) and IsCalcNOBCloseISS() and (Oper.rec.Contract > 0))
        stat = CreateTaxObjectsIIS7(Oper, ID_Step);
      else
        if(TaxTeriodToLucre == false)
          stat = CreateTaxObjects7(Oper, ID_Step);
  
          if(not stat)
            stat = CreateMaterialTaxObjects7(Oper, ID_Step);
          end;
        else
          if(Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_LUCRE)
            stat = CreateTaxObjects7(Oper, ID_Step);
          else
            stat = CreateMaterialTaxObjects7(Oper, ID_Step);
          end;
        end;
      end;

      if(not stat)
        stat = DL_NPTX_StartInsertTaxObject(Oper.rec.ID, ID_Step);
      end;
  
      DL_NPTX_SaveOperLog( Oper.rec.ID, 90 );
    end;
  end;

  return stat;
END;
