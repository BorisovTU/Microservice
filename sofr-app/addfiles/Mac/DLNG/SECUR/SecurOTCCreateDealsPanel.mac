//SecurOTCCreateDealsPanel.mac
import "RsbPanelConstants.mac", RsbFormsInter, "KeyCodes.mac", globals, Календарь, PTInter;

/*
╔═════════════════Создание отложенных сделок═══════════════╗
║                                                          ║
║ Контрагент:                         ___________________  ║
║ Дата заключения:                    [00.00.0000]         ║
║ Время заключения:                   [00.00.0000]         ║
║ Планируемая дата поставки продажи:  [00.00.0000]         ║
║ Планируемая дата оплаты продажи:    [00.00.0000]         ║
║ Планируемая дата поставки покупки:  [00.00.0000]         ║
║ Планируемая дата оплаты покупки:    [00.00.0000]         ║
║                                                          ║
╚══════════════════════════════════════════════════════════╝
*/

class (TRsbPanel) SecurOTCCreateDealsPanel()
  private var rows:integer = 0;

  //статичные координаты панели и полей
  private var panelPositionX:integer = 45;
  private var panelPositionY:integer = 10;
  private var editableFldsPosition   = 30;
  private var panelWidth:integer     = 43;

  //статичные размеры полей
  private var stringFldLength = 10;
  private var dateFldLength = 10;

  //выбранные фильтры
  private var choosedContractorID:integer = 0;

  InitTRsbPanel();

  SetCaption("Создание отложенных сделок");
  SetStatus("~Esc~ Выход ~F2~ Применить");
  SetPosition(panelPositionX, panelPositionY);

  //Общие процедуры, чтобы не писать каждый раз одно и тоже
  private macro NewLabel(labelText:string)
    addLabel(TRsbLabel(2, rows = rows + 1, labelText));
  end;

  private macro NewFld (labelText:string, name:string, value, editable:bool, handlerName:string, dataType:integer, length:integer)
    if (labelText != null) 
      NewLabel(labelText);
    end;
    var fld = TRsbEditField(dataType);
    fld.setPosition(editableFldsPosition, rows);
    fld.setSize(length, 1);
    fld.textLength = length;
    fld.Editable   = editable;
    fld.Name       = name;
    fld.value      = value;
    if (handlerName != null)
      fld.addEventHandler(RSB_EV_KEY_PRESSED , R2M(this, handlerName));
    end;
    addControl(fld);
    return fld;
  end;

  private macro NewFldString(labelText:string, name:string, value:string, editable:bool, handlerName:string)
    return NewFld(labelText, name, value, editable, handlerName, RsbPanelFiledTypes.FT_STRING, stringFldLength);
  end;

  private macro NewFldDate(labelText:string, name:string, value:date, editable:bool)
    return NewFld(labelText, name, value, editable, "DateKeyPressed", RsbPanelFiledTypes.FT_DATE, dateFldLength);
  end;

  private macro NewFldTime(labelText:string, name:string, value:time, editable:bool)
    return NewFld(labelText, name, value, editable, null, RsbPanelFiledTypes.FT_TIME, dateFldLength);
  end;

  private macro NewButton(pos_x:integer, pos_y:integer, name:string, enable:bool, handlerName:string)
    var button = TRsbPushButton(name);
    button.setPosition(pos_x, pos_y);
    button.setSize(7,1);
    button.onClicked(R2M(this, handlerName));
    button.enabled = enable;
    addControl(button);
  end;

  rows = rows + 1;
  NewFldString("Контрагент:", "contractorFld", "", true, "ContractorKeyPressed");

  var dealDateCntrl = NewFldDate("Дата заключения:", "dealDate", {curdate}, true);
  var dealTimeCntrl = NewFldTime("Время заключения:", "dealTime", Time(), true);
  var sellSupplyDateCntrl = NewFldDate("Планируемая дата поставки продажи:", "sellSupplyDate", {curdate}, true);
  var sellPayDateCntrl    = NewFldDate("Планируемая дата оплаты продажи:",   "sellPayDate", {curdate}, true);
  var buySupplyDateCntrl  = NewFldDate("Планируемая дата поставки покупки:", "buySupplyDate", {curdate}, true);
  var buyPayDateCntrl     = NewFldDate("Планируемая дата оплаты покупки:",   "buyPayDate", {curdate}, true);

  rows = rows + 2;
  NewButton(4, rows, "Старт", true, "StartBtn");
  NewButton(panelWidth - 11, rows, "Отмена", true, "CancelBtn");

  rows = rows + 2;
  SetSize(panelWidth, rows);

  addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "KeyHandler"));

  macro StartBtn(RsbEvent)
    if (RsbEvent.id == RSB_EV_BUTTON_CLICKED)
      close(1);
    end;
    return true;
  end;

  macro CancelBtn(RsbEvent)
    if (RsbEvent.id == RSB_EV_BUTTON_CLICKED)
      close(0);
    end;
    return true;
  end;

  macro ContractorKeyPressed(RsbEvent:Object)
    if (RsbEvent.KeyCode == KeyCodes.F3)
      var party = TRecHandler("party.dbt");
      var codeKind = 1;
      if( ListPT( Party, null, null, PTLIST_CONTR, 0, codeKind) )
        RsbEvent.Source.value = ПолучитьКодСубъекта(party.rec.PartyID, codeKind);
        choosedContractorID = party.rec.PartyID;
      end;
    end;
  end;

  macro DateKeyPressed(RsbEvent:Object)
    if (RsbEvent.KeyCode == KeyCodes.F3)
      RsbEvent.Source.value = GetDateByCalendar(RsbEvent.Source.value);
    end;
  end;

  //Основной обработчик нажатий на панели
  macro KeyHandler(RsbEvent:Object)
    if (RsbEvent.KeyCode == KeyCodes.F2)
      Close(1);
    end;
  end;

  macro GetContractorID():integer
    return choosedContractorID;
  end;

  macro GetDealDate():date
    return dealDateCntrl.value;
  end;

  macro GetDealTime():time
    return dealTimeCntrl.value;
  end;

  macro GetSellSupplyDate():date
    return sellSupplyDateCntrl.value;
  end;

  macro GetSellPayDate():date
    return sellPayDateCntrl.value;
  end;

  macro GetBuySupplyDate():date
    return buySupplyDateCntrl.value;
  end;

  macro GetBuyPayDate():date
    return buyPayDateCntrl.value;
  end;
end;