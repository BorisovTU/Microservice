/*
$Name:        OutlaySPReg2014_Form.mac
$Module:      АРНУ
$Description: Форма ввода данных отчета "Регистр по начислению расхода по коротким позициям 2014"
*/

import "DlRepForm_h.mac", "ws_txreportform.mac";

/*Номера полей в форме отчета*/
CONST PNFLD_OUTSPREG2014_PERIOD      = 0,
      PNFLD_OUTSPREG2014_YEAR        = 1,
      PNFLD_OUTSPREG2014_ENDDATE     = 2,
      PNFLD_OUTSPREG2014_AVRGROUP    = 3,
      PNFLD_OUTSPREG2014_AVRCODE     = 4,
      PNFLD_OUTSPREG2014_AVRNAME     = 5,
      PNFLD_OUTSPREG2014_PRINTMETHOD = 6;

/*Группа налогового учета*/
PRIVATE MACRO FldProc_AvrGroupFltr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  record llvalues("llvalues.dbt");
  VAR Choice, Select;
  VAR Avoiriss, AvrID;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = GetLLVALname (2903, FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     if( pThis.ExistField(DL_PNFLD_AVRCODE) )
        if( (FldRealValue == null) OR (FldRealValue < 0 ) )
           pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
        else
           AvrID = pThis.GetLinkedValue(DL_PNFLD_AVRCODE);
           if( (AvrID != null) AND (AvrID > 0) )
              Avoiriss = TRecHandler( "avoiriss.dbt" );
              if( ПолучитьФинИн( AvrID, null, Avoiriss) OR
                  (GetLLVALname (2903, Avoiriss.rec.TaxGroup) != FldRealValue)
                )
                 pThis.SetLinkedValue( DL_PNFLD_AVRCODE, -1 ); /*скинуть поле*/
              end;
           end;
        end;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

     //группы 919, 943  выбрать нельзя

     Select =   " select t_Element, t_Code, t_Name from dllvalues_dbt where t_List = 2903 "
              + "  and t_Element not in (919, 943) order by t_Element asc";

     Choice = DL_Scroll(Select,
                        "Группы налогового учета", pThis.CurrentFldX(), pThis.CurrentFldY(),
                        "t_Element","Номер","t_Code","Код","t_Name","Название"
                       );

     if(Choice != NULL)
        FldShowValue = Choice.Value("t_Name");    
        FldRealValue = Choice.Value("t_Element");
     end;
  end;

  return CM_DEFAULT;
END;

macro GetAvoirListAddWhere()

  return " AND AV.t_TaxGroup not in(919, 943) " +
         " AND ( (RSB_FIInstr.FI_AvrKindsEQ(" + FIKIND_AVOIRISS + ", " + AVOIRISSKIND_SHARE + ", t.t_AvoirKind) > 0) or " +
            "       (RSB_FIInstr.FI_AvrKindsEQ(" + FIKIND_AVOIRISS + ", " + AVOIRISSKIND_INVESTMENT_SHARE + ", t.t_AvoirKind) > 0) or " +
            "       (RSB_FIInstr.FI_AvrKindsEQ(" + FIKIND_AVOIRISS + ", " + AVOIRISSKIND_DEPOSITORY_RECEIPT + ", t.t_AvoirKind) > 0) or " +
            "       ((RSB_FIInstr.FI_AvrKindsEQ(" + FIKIND_AVOIRISS + ", " + AVOIRISSKIND_BOND + ", t.t_AvoirKind) > 0) AND ((Rsb_SCTX.TXIsPercentAvoir(AV.t_TaxGroup) = 1) or (Rsb_SCTX.TXIsDiscountAvoir(AV.t_TaxGroup) = 1))) " +
            "     ) ";
end;

PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR RetVal = CM_DEFAULT;
  VAR WhereCond = "", AddTable = "", AvrGroup;
  VAR Fininstr = TRecHandler( "fininstr.dbt" );
  VAR Avoir    = TRecHandler( "avoiriss.dbt" );

  if( not ((Cmd == DLG_KEY) AND (Key == KEY_F3)) )
     DL_FldProc_AvrCode( pThis, Cmd, Key, @FldShowValue, @FldRealValue );
  end;

  if( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
    AddTable  = " davoiriss_dbt AV ";
    WhereCond = " AV.t_FIID = t.t_FIID ";
    WhereCond = WhereCond + GetAvoirListAddWhere();

    AvrGroup = pThis.GetLinkedValue(DL_PNFLD_AVRGROUP);
    if( (AvrGroup != NULL) AND (AvrGroup > 0) )
       WhereCond = WhereCond + " AND AV.t_TaxGroup = " + AvrGroup;
    end;

    if( ListAvoiriss(FIKIND_ALLAVOIRISS, Fininstr, Avoir, null, WhereCond, AddTable) )
       FldRealValue = Fininstr.rec.FIID;
       FldShowValue = Fininstr.rec.FI_Code;
       pThis.SetLinkedValue(DL_PNFLD_AVRGROUP, Avoir.rec.TaxGroup);
    end;
  end;

  return RetVal;
END;

CLASS (DL_CPanel) SP_OutlaySPReg2014_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year, BegDate, RepDate;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = (CurMonth - 1)/3; /*текущий квартал-1*/
     if( PrevQuartal == 0 )
        PrevQuartal = 4;
        Year        = Year - 1;
     end;

     /*Получим отчетную дату - последнюю дату из отчетного периода.*/
     Period_GetInterval( PrevQuartal + 12, Year, @BegDate, @RepDate );

     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRGROUP) )
        SetLinkedValue( DL_PNFLD_AVRGROUP, null );
     end;
     if( GetFieldByName(0,DL_PNFLD_AVRCODE) )
        SetLinkedValue( DL_PNFLD_AVRCODE, null );
     end;

     /*только в excel*/
     DisableFields( This.Data(), PNFLD_OUTSPREG2014_PRINTMETHOD );

     AddFieldProc( 0, PNFLD_OUTSPREG2014_PERIOD,      DL_PNFLD_PERIOD,      @DL_FldProc_Period, PrevQuartal + 12, 11, 4 ); /*(+12 - для NameAlg.dbt)*/
     AddFieldProc( 0, PNFLD_OUTSPREG2014_YEAR,        DL_PNFLD_YEAR,        @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_OUTSPREG2014_ENDDATE,     DL_PNFLD_ENDDATE,     @DL_FldProc_EndDate, RepDate );
     AddFieldProc( 0, PNFLD_OUTSPREG2014_AVRGROUP,    DL_PNFLD_AVRGROUP,    @FldProc_AvrGroupFltr, null, 10, 8 );     
     AddFieldProc( 0, PNFLD_OUTSPREG2014_AVRCODE,     DL_PNFLD_AVRCODE,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_OUTSPREG2014_AVRNAME,     DL_PNFLD_AVRNAME,     @DL_FldProc_AvrName );
     AddFieldProc( 0, PNFLD_OUTSPREG2014_PRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 31, 13 );
  END;

  initDL_CPanel("sp_rep.lbr", "OUTSP14");
END;

CLASS ( TxReportForm ) WS_TX_OutlaySPReg2014_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  /* Переопределяем номера полей */
  ReDefineFieldNum[PNFLD_OUTSPREG2014_PERIOD]       = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_OUTSPREG2014_YEAR]         = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_OUTSPREG2014_ENDDATE]      = TXREPORTFORM_FLD_REPORTDATE;
  ReDefineFieldNum[PNFLD_OUTSPREG2014_AVRGROUP]     = TXREPORTFORM_WIDGET_GNU;
  ReDefineFieldNum[PNFLD_OUTSPREG2014_AVRCODE]      = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_OUTSPREG2014_AVRNAME]      = TXREPORTFORM_FAKEFLD_AVRNAME;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
