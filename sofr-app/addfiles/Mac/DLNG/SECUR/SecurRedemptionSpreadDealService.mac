/* SecurRedemptionSpreadDealService.mac
*/
import "SqlResultConvertHelper.mac", oralib, "ReferencesClass.mac";

class SecurRedemptionSpreadDealStruct
    var id:integer;
    var payDate:date;
    var deliveryDate:date;
end;

class SecurRedemptionSpreadDealService
    const BOFFICE_KIND = 130;
    const DEAL_TYPE = 12021;

    private var sqlConverter:SqlResultConvertHelper = SqlResultConvertHelper();

    macro FindById(id:integer):SecurRedemptionSpreadDealStruct
        var deal:SecurRedemptionSpreadDealStruct = SecurRedemptionSpreadDealStruct();

        var query = "select t.t_dealid, "
                  + "       l.t_maturity, "
                  + "       l.t_expiry "
                  + "  from ddl_tick_dbt t "
                  + "  join ddl_leg_dbt l on l.t_dealid = t.t_dealid "
                  + " where t.t_dealid = :dealId "
                  + "   and l.t_legkind = 0 "
                  + "   and l.t_legid = 0 ";
        var sql = ExecSQLselectPrmDyn(query, id);

        if (sql.MoveNext())
            deal.id = sql.value("t_dealid");
            deal.payDate = sqlConverter.getDate(sql.value("t_maturity"));
            deal.deliveryDate = sqlConverter.getDate(sql.value("t_expiry"));
        end;

        return deal;
    end;

    private macro UpdateLegInfo(deal:SecurRedemptionSpreadDealStruct)
        var query = "update ddl_leg_dbt "
                  + "   set t_maturity = :payDate, "
                  + "       t_expiry = :deliveryDate "
                  + " where t_dealid = :dealId "
                  + "   and t_legkind = 0 "
                  + "   and t_legid = 0 ";
        
        var params = MakeSqlParamsArray(deal.payDate, deal.deliveryDate, deal.id);
        ExecSql(query, params);
    end;

    macro Save(deal:SecurRedemptionSpreadDealStruct):SecurRedemptionSpreadDealStruct
        if (deal.id != null)
            UpdateLegInfo(deal);
        else
            //todo: create deal
        end;

        deal = FindById(deal.id);
        return deal;
    end;

    private macro isFreeCode(code:string):bool
        var query = "select 1 "
                  + "  from ddl_tick_dbt "
                  + " where t_bofficekind = :bOfficeKind "
                  + "   and t_dealtype = :dealType "
                  + "   and t_dealcode = :code ";
        var sql = ExecSQLselectPrmDyn(query, BOFFICE_KIND, DEAL_TYPE, code);
        return not sql.MoveNext();
    end;

    macro GenerateDealCode():string
        var reference = ReferencesClass();
        var referenceValueIsFree:bool = false;
        var referenceValue;
        
        while (not referenceValueIsFree)
            var err = GenerateReference(reference.SECUR_REDEMPTION_INTERNAL_CODE.id, referenceValue);
            if (err != 0)
                RunError(string("Ошибка генерации референса \"", reference.SECUR_REDEMPTION_INTERNAL_CODE.name, "\": ", GetErrMsg()));
            end;

            referenceValueIsFree = isFreeCode(referenceValue);
        end;

        return string(referenceValue);
    end;

    macro deleteDealByDealId(dealId:integer):bool
        var err = SP_DelDealWithLinks(dealId);
        return err == false;
    end;

    macro IsDealExists(dealId:integer):bool
        var query = "select 1 from ddl_tick_dbt where t_dealid = :dealid";
        var sql = ExecSQLselectPrmDyn(query, dealId);
        return sql.MoveNext();
    end;
end;