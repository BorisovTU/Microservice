/*
 отчет "Обобщенный баланс депо"
 оптимизация под ORACLE Nikonorov Evgeny 26.01.2005
*/
import RsbDataSet;
import FIInter, CurrInter, dprepsec, cb_sql;

var MAX_t_point = 0;//максимальная точность. Если встретится пай, то она заполнится отличным от нуля значением

var HeadTable = "┌──────────────────────────┬──────────────────────────┬──────────────────────────┐\n"+
                "│                          │ По активу                │ По пассиву               │\n"+
                "├──────────────────────────┼──────────────────────────┼──────────────────────────┤";

var tablewidth = Index(HeadTable, "\n");
var Rep = CMakeReport(HeadTable);
const RepFontStyleTitel =  "ex_FS(b)";

macro report ( rest_active, rest_passive, apostr )
  Rep.AddStr();
  Rep.AddEmptyStr();
  Rep.AddStr();
  DP_AddPrintCell(Rep, "Всего ценных бумаг на     хранении в депозитарии:", 0, 0, "l:" + RepFontStyleTitel, apostr );
  DP_AddPrintCell(Rep, rest_active, 0, DP_GetPrecision(rest_active, MAX_t_point), "r:" + RepFontStyleTitel, apostr);
  DP_AddPrintCell(Rep, rest_passive, 0, DP_GetPrecision(rest_passive, MAX_t_point), "r:" + RepFontStyleTitel, apostr);

  Rep.AddStr();
end;

macro GetMaxTPoint( chapter, dprt_num, rep_date, SecurQual )
   var DataSet, query;
   query =   " select MAX(fin.t_SumPrecision) as point from dfininstr_dbt fin, daccount_dbt acc "
           + " where acc.t_Chapter = " + chapter
           + "   and fin.t_FIID = acc.t_Code_Currency ";
   if(dprt_num != 0)
     query = query + " and acc.t_Department = " + dprt_num;
   end;

   if( SecurQual == SECURQUAL_QUAL)
     query = query + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, " + GetSQLDate(rep_date) + " ) <> 0";
   elif(SecurQual == SECURQUAL_NOQUAL)
     query = query + " and rsb_fiinstr.FI_IsQualified(fin.t_FIID, " + GetSQLDate(rep_date) + " ) = 0";
   end;

   DataSet = TRsbDataSet(query);

   if( DataSet.MoveNext() AND (ValType(DataSet.point) != V_UNDEF)) 
      MAX_t_point = DataSet.point;
   end;
end;

macro CalcGlobalRest(dprt_num, rep_date, chapter,
                    RestActiv, RestPassiv, SecurQual)
   var stat, i = 0;
   var query;
   var DataSet;

   //сначала по активным 

   query = " SELECT count(a.t_Account) as count, abs(sum( rsb_account.restac( a.t_Account, a.t_Code_Currency, " + GetSQLDate(rep_date) + ", a.t_Chapter, null )  )) as Rest"
         + " FROM  daccount_dbt a"
         + " WHERE a.t_Kind_Account = 'А' "
         + " and t_Chapter = " + chapter
         + " and t_Open_Date <= " + GetSQLDate(rep_date);

   if( dprt_num > 0 )
     query = query + " and t_Department = " + dprt_num;
   end; 

   if( SecurQual == SECURQUAL_QUAL)
     query = query + " and rsb_fiinstr.FI_IsQualified(a.t_Code_Currency, " + GetSQLDate(rep_date) + " ) <> 0";
   elif(SecurQual == SECURQUAL_NOQUAL)
     query = query + " and rsb_fiinstr.FI_IsQualified(a.t_Code_Currency, " + GetSQLDate(rep_date) + " ) = 0";
   end;

   DataSet = TRsbDataSet( query );
   if( (DataSet.movenext()) and (DataSet.count > 0) )
     RestActiv = DataSet.rest;
            end;

   //теперь пассивные
   query = "";
   
   query = " SELECT count(a.t_Account) as count, sum( rsb_account.restac( a.t_Account, a.t_Code_Currency, " + GetSQLDate(rep_date) + ", a.t_Chapter, null )  ) as Rest"
          + " FROM  daccount_dbt a"
          + " WHERE a.t_Kind_Account = 'П' "
          + " and t_Chapter = " + chapter
          + " and t_Open_Date <= " + GetSQLDate(rep_date);

   if( dprt_num > 0 )
     query = query + " and t_Department = " + dprt_num;
         end;

   if( SecurQual == SECURQUAL_QUAL)
     query = query + " and rsb_fiinstr.FI_IsQualified(a.t_Code_Currency, " + GetSQLDate(rep_date) + " ) <> 0";
   elif(SecurQual == SECURQUAL_NOQUAL)
     query = query + " and rsb_fiinstr.FI_IsQualified(a.t_Code_Currency, " + GetSQLDate(rep_date) + " ) = 0";
   end;

   DataSet = TRsbDataSet( query );

   if( (DataSet.movenext()) and (DataSet.count > 0) )
      RestPassiv = DataSet.rest;
   end;
   setparm(3, RestActiv);
   setparm(4, RestPassiv);
                    
end;

macro generalized_balance( dprt_num, 
                           rep_date, 
                           chapter, 
                           SecurQual,
                           ToExcel,
                           apostr )

     var RestActiv = 0, RestPassiv = 0, CvalStr = "";
     
     Dl_CallInsertStat( IIF( ToExcel, DL_OUTREPORT_EXCEL, DL_OUTREPORT_STD ) );
     DP_BeginProtocol();  //начать протоколирование - подмена MsgBox

     Rep.SetFlagShowZeroValue(true);

     DP_StdHeaderLO_Ex( Rep, RepFontStyleTitel, "Обобщенный баланс депо", 0, 0, false, rep_date, rep_date, tablewidth );

     if(securqual == SECURQUAL_QUAL)    
       CvalStr = "Квалифицированные";   
     elif(securqual == SECURQUAL_NOQUAL)
       CvalStr = "Неквалифицированные"; 
     else                               
       CvalStr = "Все";                 
     end;                               
     Rep.AddPrintCell("Ц/б для отчета: " + CvalStr, tablewidth, 0, "l:" + RepFontStyleTitel, REP_ELEM_STR);
                                               
     GetMaxTPoint(chapter, dprt_num, rep_date, SecurQual);

     CalcGlobalRest(dprt_num, rep_date, chapter,
                    RestActiv, RestPassiv, SecurQual);

     report(RestActiv, RestPassiv, apostr); 
     DP_StdFooter_Ex(Rep, RepFontStyleTitel, NULL, NULL, tablewidth);

     DP_AddProtocol(Rep, "protocol");  //добавим протокол к уже сформированному отчету
     DP_EndProtocol();                 //закончить протоколирование

     if( ToExcel )
       Rep.PrintWinRep();
       Rep.ShowWinRep();
     else
       Rep.PrintRep();
     end;
end;
