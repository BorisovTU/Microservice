/*
$Name:        nptxhistoprep.mac
$Module:      Ценные бумаги
$Description: Отчет "История операций по ФЛ"
*/
import "nptxhistoprep_form.mac", "sprepfun.mac", "sqlconv.mac", "CbReport_h.mac", "scsrvrepfun.mac";

PRIVATE CLASS (DL_CReportTemplate) NPTXHISTOPREP_Report(RepDate:date, TaxPeriod:integer, ClientID:integer, IncludeCancelRec:bool, BaseKind:integer)
  private var m_CurrSheetName = "История операций по ФЛ";
  private var m_RepDate          = RepDate; 
  private var m_TaxPeriod        = TaxPeriod; 
  private var m_ClientID         = ClientID; 
  private var m_IncludeCancelRec = IncludeCancelRec;
  private var m_BaseKind         = BaseKind;


  PRIVATE MACRO BeginReport()
    SetIsShowAppl(false); //Всегда не показываем отчет на экран
    SetActiveSheet(m_CurrSheetName);
  END;

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  MACRO CReport_Show( NumCopy:INTEGER, ReportName:@STRING )
    CReport_Show( NumCopy, ReportName );
  END;

  PRIVATE MACRO GetTaxBaseKindCode(TaxBaseTypeNum)
    var query, cmd, DataSet;
    var TaxBaseKindCode = "";

    query =   " select t_Code "
            + "   from dllvalues_dbt "
            + "  where t_List = " + OBJTYPE_STB_TAXBASEKIND
            + "    and t_Flag = ? ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(int(TaxBaseTypeNum));

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      TaxBaseKindCode = DataSet.Code;
    end;

    return TaxBaseKindCode;
  END;

  PRIVATE MACRO GetEventDescription(EventID:integer)
    var query, cmd, DataSet;
    var EventDesc = "";

    query =   "select t_Description "
            + "  from dnptxtotalbase_dbt "
            + " where t_TBID = ? ";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(EventID);

    DataSet = cmd.Execute();
    if(DataSet.moveNext())
      EventDesc = DataSet.Description;
    end;

    return EventDesc;
  END;

  PRIVATE MACRO ExistClientTBbyStorState(StorState)
    var query, cmd, DataSet;

    query =   " select count(1) as cnt "
            + "   from dnptxtotalbase_dbt tb "
            + "  where tb.t_ClientID = ? "
            + "    and tb.t_TaxPeriod = ? "
            + "    and tb.t_StorState = ? "
            + "    and ROWNUM = 1";

    cmd = DL_RSDCommand(query);

    cmd.AddParam(m_ClientID);
    cmd.AddParam(m_TaxPeriod);
    cmd.AddParam(StorState);

    DataSet = cmd.Execute();
    if((DataSet.moveNext()) and (DataSet.Cnt > 0))
      return true;
    end;

    return false;
  END;

  PRIVATE MACRO Create()
    var Request = c_GetAmountSNOBRequest();
    var party = TRecHandler("party.dbt");
    var llv = TRecHandler("llvalues.dbt");

    if(ПолучитьСубъекта(m_ClientID, party) != 0 )
      msgbox("Не найден субъект с идентификатором " + m_ClientID);
      return 1;
    end;

    var isActiveRecs   = ExistClientTBbyStorState(NPTXTOTALBASE_STORSTATE_ACTIVE);
    var isCanceledRecs = ExistClientTBbyStorState(NPTXTOTALBASE_STORSTATE_CANCELED);
    
    PrintFormatString( NULL, "N1_ClientID",   m_ClientID,
                             "N1_ClientName", party.rec.Name
                     );

    RegisterTable( "N1_MainTable", NULL,
                   "N1_IncDateTime",  
                   "N1_RecDateTime",
                   "N1_EventType",   
                   "N1_EventDesc", 
                   "N1_SourceSystem",  
                   "N1_RecStatus", 
                   "N1_EventID",   
                   "N1_OperID",    
                   "N1_TaxBaseKind",   
                   "N1_CurRecTaxBase", 
                   "N1_CalcTax",   
                   "N1_TaxRate",    
                   "N1_HoldTax",   
                   "N1_HoldTaxRate",   
                   "N1_CalcKBK",   
                   "N1_HoldKBK",   
                   "N1_RecSTaxBase",   
                   "N1_ApplSTaxBaseInclude",
                   "N1_SnobRecId"
                 );

    if((m_IncludeCancelRec == false) and (isActiveRecs == false))
      msgbox(m_ClientID+"; Активные записи для клиента в хранилище СНОБ от СИ(СОФР) отсутствуют. Ограничение на вывод данных от иных СИ");
    elif((m_IncludeCancelRec == true) and (isActiveRecs == false) and (isCanceledRecs == false))
      msgbox(m_ClientID+"; Записи для клиента в хранилище СНОБ от СИ(СОФР) отсутствуют. Ограничение на вывод данных от иных СИ");
    else

      Request.GetAmountSNOBReq = c_GetAmountSNOBReq();
      Request.GetAmountSNOBReq.IsActivRecord      = IIF(m_IncludeCancelRec, false, true);
      Request.GetAmountSNOBReq.IsSNOB             = false;
      Request.GetAmountSNOBReq.ClientId           = c_IntegrationSymbolicIdentifierXType();
      Request.GetAmountSNOBReq.ClientId.ObjectID  = m_ClientID;
      Request.GetAmountSNOBReq.TaxYear            = m_TaxPeriod;

       if( LL_FindLLVALUES(OBJTYPE_STB_TAXBASEKIND, m_BaseKind, llv) == true )
         Request.GetAmountSNOBReq.TaxBaseType = c_IntegrationDictionaryRecordXType();
         Request.GetAmountSNOBReq.TaxBaseType.RecordCode = llv.rec.Flag;
       end;

      InitProgress(-1, "Выполнение запроса к Хранилищу СНОБ", "Запрос к Хранилищу СНОБ");
      var Resp = GetAmountSNOBReq(Request);
      RemProgress();
       
      var isOk = true;
      if((Resp == NULL) or (ValType(Resp) != V_GENOBJ) or (ValType(Resp.GetAmount) == V_UNDEF) or (ValType(Resp.GetAmount.RecordInfoList) == V_UNDEF) or
         ((ValType(Resp.ErrorList) != V_UNDEF) and (Resp.ErrorList.size > 0) and (string(trim(Resp.ErrorList[0].ErrorCode))) != "0")
        )
        isOk = false;
        if(((ValType(Resp.ErrorList) != V_UNDEF) and (Resp.ErrorList.size > 0) and (string(trim(Resp.ErrorList[0].ErrorCode))) != "0"))
          msgbox("Ошибка " + string(trim(Resp.ErrorList[0].ErrorCode)) + ": " + string(trim(Resp.ErrorList[0].ErrorDesc)));
        end;
      end;

      if(isOK)
        if(Resp.GetAmount.RecordInfoList.Size > 0)
          var i = 0;

          InitProgress(Resp.GetAmount.RecordInfoList.Size, "Обработка полученных данных", "Обработка полученных данных");

          while(i < Resp.GetAmount.RecordInfoList.Size)

            if((m_IncludeCancelRec == true) and (isActiveRecs == false) and (isCanceledRecs == true) and (Resp.GetAmount.RecordInfoList[i].SystemCode != NULL) and (Resp.GetAmount.RecordInfoList[i].SystemCode.RecordCode != "SOFR"))
              /**В параметрах отчета задано "Включать отмененные записи": 
                 2.        Если у клиента имеются только отмененные записи СНОБ в хранилище СОФР отправить запрос и построить отчет со всеми полученными в ответ активными и отмененными записями от СОФР .  
                 Т.о. все записи от других СИ не печааем, а пропускаем
                */
              UseProgress(i = i + 1);
              continue;
            end;
            
            var IncDateTime = string(date(Resp.GetAmount.RecordInfoList[i].IncomeRegionDateTime):f) + " " + string(time(Resp.GetAmount.RecordInfoList[i].IncomeRegionDateTime));
            var RecDateTime = string(date(Resp.GetAmount.RecordInfoList[i].RecordDateTime):f) + " " + string(time(Resp.GetAmount.RecordInfoList[i].RecordDateTime));

            var TaxRate = 0;
            if((ValType(Resp.GetAmount.RecordInfoList[i].NDFLTaxRate) != V_UNDEF) and (Resp.GetAmount.RecordInfoList[i].NDFLTaxRate != 0) and (Resp.GetAmount.RecordInfoList[i].NDFLTaxRate != ""))
              TaxRate = double(Resp.GetAmount.RecordInfoList[i].NDFLTaxRate)/100;
            end;

            var HoldTaxRate = 0;
            if((ValType(Resp.GetAmount.RecordInfoList[i].NDFLKeepRate) != V_UNDEF) and (Resp.GetAmount.RecordInfoList[i].NDFLKeepRate != 0) and (Resp.GetAmount.RecordInfoList[i].NDFLKeepRate != ""))
              HoldTaxRate = double(Resp.GetAmount.RecordInfoList[i].NDFLKeepRate)/100;
            end;
            
            var EventDescription = "";
            if(ValType(Resp.GetAmount.RecordInfoList[i].EventDescription) != V_UNDEF)
              EventDescription = string(Resp.GetAmount.RecordInfoList[i].EventDescription);
            else
              if((Resp.GetAmount.RecordInfoList[i].SystemCode) and (Resp.GetAmount.RecordInfoList[i].SystemCode.RecordCode == "SOFR"))
                EventDescription = GetEventDescription(int(Resp.GetAmount.RecordInfoList[i].EventID.ObjectID));
              end;
            end;

            var TaxBaseType = 0;
            if(Resp.GetAmount.RecordInfoList[i].TaxBaseType)
              TaxBaseType = Resp.GetAmount.RecordInfoList[i].TaxBaseType.RecordCode;
            end;

            PrintTableLine("N1_IncDateTime",           IncDateTime,                                             null,
                           "N1_RecDateTime",           RecDateTime,                                             null,
                           "N1_EventType",             Resp.GetAmount.RecordInfoList[i].EventType.Desc,         null,
                           "N1_EventDesc",             EventDescription,                                        null,
                           "N1_SourceSystem",          Resp.GetAmount.RecordInfoList[i].SystemCode.Desc,        null,
                           "N1_RecStatus",             Resp.GetAmount.RecordInfoList[i].RecordStatus.Desc,      null,
                           "N1_EventID",               Resp.GetAmount.RecordInfoList[i].EventID.ObjectID,       null,
                           "N1_OperID",                Resp.GetAmount.RecordInfoList[i].OperationId.ObjectID,   null,
                           "N1_TaxBaseKind",           GetTaxBaseKindCode(TaxBaseType), null,           
                           "N1_CurRecTaxBase",         Resp.GetAmount.RecordInfoList[i].NOBAmount,              null,
                           "N1_CalcTax",               Resp.GetAmount.RecordInfoList[i].NDFLCalculateAmount,    null,
                           "N1_TaxRate",               TaxRate,                                                 null,
                           "N1_HoldTax",               Resp.GetAmount.RecordInfoList[i].NDFLKeepAmount,         null,
                           "N1_HoldTaxRate",           HoldTaxRate,                                             null,
                           "N1_CalcKBK",               Resp.GetAmount.RecordInfoList[i].KBKCalculate,           null,
                           "N1_HoldKBK",               Resp.GetAmount.RecordInfoList[i].KBKKeep,                null,
                           "N1_RecSTaxBase",           Resp.GetAmount.RecordInfoList[i].SNOBAmount,             null,
                           "N1_ApplSTaxBaseInclude",   Resp.GetAmount.RecordInfoList[i].SNOBPayAmount,          null,
                           "N1_SnobRecId",             Resp.GetAmount.RecordInfoList[i].RecordId.ObjectID,      null
                          );                                                    

            UseProgress(i = i + 1);
          end;

          RemProgress();
        end;
      end;
    end;

    EndTable();
  END;

  MACRO Run()
    Run();
    
    for (var fileName, GetFullReportFileNames())
      return fileName;
    end;

    return "";
  END;

  MACRO CReport_PrintLog()
    VAR count = 0;
    var ErrorMessageArr = GetErroMessageArr();
    var LogSheetName = "Сообщения";

    if( __CalculateTimeExecute__ )
       BeginMarkTime();
    end;

    if( m_UseTemplate )
       if( ErrorMessageArr.Size > 0 )
          m_ObjTemplateXLS.SheetAdd();
          m_ObjTemplateXLS.SetSheetColor( XlColorIndex_RED );
          m_ObjTemplateXLS.SheetName( LogSheetName );
          SetActiveSheet( LogSheetName );
          m_ObjTemplateXLS.SetValue(0,0,"ID клиента в СОФР");
          m_ObjTemplateXLS.ColWidth(0, 20);
          m_ObjTemplateXLS.SetValue(0,1,"Ошибки при выполнении отчета");
          m_ObjTemplateXLS.ColWidth(1, 200);
          m_ObjTemplateXLS.SetAlign(0,1,ALIGN_CENTER );
          m_ObjTemplateXLS.SetBkColor(0, 1, 12451773 );
          m_ObjTemplateXLS.SetDiapazon(0, 0, 0, 1);
          m_ObjTemplateXLS.SetFont( null, null, true, null, null );
          while( count < ErrorMessageArr.Size )
             var str = StrSubst( ErrorMessageArr[count], "|", "\n" );
             var clid_str = "";
             var msg_str = "";
             var ind = index(str, ";");
             
             if(ind > 0)
               clid_str = substr(str, 1, ind-1);
             end;

             msg_str = substr(str, ind+1);

             m_ObjTemplateXLS.SetValue( count+1, 0, trim(clid_str) );
             m_ObjTemplateXLS.SetValue( count+1, 1, trim(msg_str) );
             count = count + 1;
          end;

          m_ObjTemplateXLS.SetDiapazon( 0, 0, count, 1 );
          m_ObjTemplateXLS.SetBorder();

       end;
    else
       m_ObjWinRep.CReport_PrintLog();
    end;

    if( __CalculateTimeExecute__ )
       EndMarkTime( "Время печати лога (CReport_PrintLog):" );
    end;
  END;

  initDL_CReportTemplate( NULL, "Report_nptxhistop.xlsx", true, null, null, true ); 
END;

PRIVATE MACRO ReportRun()
  var query, cmd, DataSet;
  var Form = Sp_NPTXHISTOPREP_Panel();
  var cnt = 0, i = 0;
  
  while(Form.Run())
    var RepObj = NPTXHISTOPREP_Report(Form.m_RepDate, Form.m_TaxPeriod, Form.m_ClientID, Form.m_IncludeCancelRec, Form.m_BaseKind);
          
    var FullFileNameXLSX = RepObj.Run();

    RepObj = NULL;

    if(not isStandAlone())
      FullFileNameXLSX = "$"+FullFileNameXLSX;
    end;
    
    if(FullFileNameXLSX != "")
      var day, mon, year;
      var hour, min, sec;
      var FileName, FileExt;
      
      DateSplit(Form.m_RepDate, day, mon, year);
      TimeSplit(time(), hour, min, sec);
      
      var FromDir = SplitFile(FullFileNameXLSX, FileName, FileExt);
      
      var NewFileNameXLSX = "История_операций_по_"+StrSubst( Trim(Form.m_ClientName), " ", "_" )+"_по_состоянию_на_"+string(day:o:2)+"."+string(mon:o:2)+"."+string(year)+"_"+string(hour:o:2)+"-"+string(min:o:2)+"-"+string(sec:o:2) + ".xlsx";

      if(not RenameFile(FullFileNameXLSX, FromDir+NewFileNameXLSX))
        msgbox("Ошибка при переименовании файла отчета");
      else
        SC_ShowFile(FromDir, NewFileNameXLSX);
      end;
    end;
  end;
END;

ReportRun();
exit(1);