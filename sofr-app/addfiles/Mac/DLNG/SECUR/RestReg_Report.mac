/*
$Name:             RestReg_Report.mac
$Module:           АРНУ
$Description:      Отчет "Остаток ценных бумаг в портфеле" (НУ)
*/

IMPORT "spCache.mac", "RestReg_Form.mac", "DLReport_h.mac", PtInter, "dlreport.mac", "cb_sql.mac", "sprepfun.mac", "oralib", "ws_txreportform.mac";

PRIVATE VAR   T_Dep:T_Dep_Collector;

PRIVATE VAR   RestReg_Form;
PRIVATE VAR   RestReg_Report;
PRIVATE VAR   WebMenuItem; // Информация о запуске отчета из веб

PRIVATE CONST POI_MODE = TRUE;
PRIVATE CONST BIGREPORT_MODE = TRUE;

PRIVATE VAR ReportHeader =
"Остаток ценных бумаг в портфеле Банка\n" + 
" \n" +
" \n" +
"Дата                     ##########\n" +
"Группа налогового учета: #################################################################\n" +
"Вид ц/б:                 #################################################################\n" +
"Выпуск ц/б:              #################################################################\n" +
" \n"; 

PRIVATE VAR TableHeader =

"┌──────┬──────────┬──────────────────┬─────────┬─────┬───────────┬───────────┬──────────┬──────────┬───────────┬──────────┬────────────┬──────────┬────────┬───────┬──────────┬───────────┬─────────────┬──────────────────────────────────────┬───────────┬────────────┬──────┬──────┐\n"+
"│Налого│    Код   │  Выпуск ценной   │  Номер  │ Код │Признак    │Дата заклю-│Время зак-│Дата пере-│Кол-во цен-│Продано,  │Кол-во      │Код валюты│  Код   │ Цена  │ Стоимость│   НКД     │Комиссия,    │ Рыночная котировка на отчетную дату, │Отметка о  │Сделки РЕПО │ГО/фи │ Код  │\n"+
"│  вая │  бумаги  │      бумаги      │ сделки  │сдел │срочности  │чения      │ лючения  │хода права│ных бумаг  │   шт.    │ценных      │ номинала │ валюты │приоб  │приобрете-│уплаченный │уплаченная   │       сложивш. на бирже              │возможности│     с      │ лиал │ ISIN │\n"+
"│группа│          │                  │приобре  │ ки  │сделки     │сделки     │ сделки   │собствен- │в сделке,  │          │бумаг       │          │  цены  │ретения│  ния     │    при    │при          │                                      │выплат по  │ограничением│ (для │ценной│\n"+
"│      │          │                  │  тения  │прио │           │приобрете- │          │ности при │шт.        │          │  в         │          │        │       │          │приобрете- │приобретении,├───────────┬─────────────┬────────────┤амортизации│    прав    │сделки│бумаги│\n"+
"│      │          │                  │(полный) │бре  │           │ния ЦБ     │          │приобрете-│           │          │остатке,    │          │        │       │          │   нии     │руб.         │           │             │            │долга      │            │приоб │      │\n"+
"│      │          │                  │         │тения│           │           │          │нии ЦБ    │           │          │ шт.        │          │        │       │          │           │(не включая  │           │             │            │(1 да,     │            │рете  │      │\n"+
"│      │          │                  │         │     │           │           │          │          │           │          │            │          │        │       │          │           │НДС)         │   Цена    │   Источник  │    Дата    │0 -нет)    │            │ ния) │      │\n"+
"│      │          │                  │         │     │           │           │          │          │           │          │            │          │        │       │          │           │             │           │  информации │  котировки │           │            │      │      │\n"+
"│      │          │                  │         │     │           │           │          │          │           │          │            │          │        │       │          │           │             │           │             │            │           │            │      │      │\n"+
"│      │          │                  │         │     │           │           │          │          │           │          │            │          │        │       │          │           │             │           │             │            │           │            │      │      │\n"+
"│      │          │                  │         │     │           │           │          │          │           │          │            │          │        │       │          │           │             │           │             │            │           │            │      │      │\n"+
"├──────┼──────────┼──────────────────┼─────────┼─────┼───────────┼───────────┼──────────┼──────────┼───────────┼──────────┼────────────┼──────────┼────────┼───────┼──────────┼───────────┼─────────────┼───────────┼─────────────┼────────────┼───────────┼────────────┼──────┼──────┤\n"+
"│   1  │     2    │        3         │    4    │  5  │     6     │     7     │     8    │     9    │    10     │    11    │     12     │    13    │   14   │  15   │    16    │     17    │      18     │     19    │     20      │     21     │     22    │     23     │  24  │  25  │\n"+
"├──────┼──────────┼──────────────────┼─────────┼─────┼───────────┼───────────┼──────────┼──────────┼───────────┼──────────┼────────────┼──────────┼────────┼───────┼──────────┼───────────┼─────────────┼───────────┼─────────────┼────────────┼───────────┼────────────┼──────┼──────┤";

/*Отчет "Регистр неттинга"*/
PRIVATE CLASS (DL_CReportTemplate) SP_RestReg_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER, UsePOI:BOOL, UseBigReportMode:BOOL )
  PRIVATE VAR F0_6, F0_9, F0_11; /*суммы строки "Итого"*/
  PRIVATE VAR m_TotalAmountPoint = 0;
  PRIVATE VAR m_AvrGroup, m_QueryRest,
              m_Avoiriss = TRecHandler( "avoiriss.dbt" ),
              m_Fininstr = TRecHandler( "fininstr.dbt" );
  VAR m_CacheFinInstr = TX_CacheFinInstr();
  var m_RS;
  PRIVATE VAR m_LastGetRezMrkt_Result = false, m_LastGetRezMrkt_FIID = -1, m_LastGetRezMrkt_CalcDate = DATE(0,0,0);
  PRIVATE VAR m_DMrktRez, m_RezMrkt, m_MrktRez;

  PRIVATE VAR m_IsDataExist = false; /*для Web, если нет данных, то ничего не показываем*/
/* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsDataExist or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END;

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  PRIVATE MACRO PrintLine( P1,P2,P3,P4,P5,P6,P7,P8,P9,P10,P11,P12,P13,P14,P15,P16,P17,P18,P19,P20,P21,P22,P23,P24,P25 )
     VAR AmountPoint = 0;

     if(IsHaveFractPart(P10) OR IsHaveFractPart(P11) OR IsHaveFractPart(P12))
           AmountPoint = m_Fininstr.rec.SumPrecision;
        end;

     this.PrintTableLine( 
          /*1 */     "N1_SecType",      P1, null,
          /*2  */    "N1_SecCode",      P2, null,
          /*3  */    "N1_SecName",      string(P3), null,
          /*4  */    "N1_CodeB",        P4, null,
          /*5  */    "N1_TypeB",        P5, null,
          /*6  */    "N1_isTime",       P6, null,
          /*7  */    "N1_DZB",          P7, null,
          /*8  */    "N1_TZB",          P8, null,
          /*9  */    "N1_DDB",          P9, null,
          /*10 */    "N1_QntyDeal:r:a", P10, AmountPoint, 
          /*11 */    "N1_QntySold:r:a", P11, AmountPoint, 
          /*12 */    "N1_QntyOst:r:a",  P12,AmountPoint, 
          /*13 */    "N1_VN",           P13, null,
          /*14 */    "N1_VPrB",         P14, null,
          /*15 */    "N1_PrBvpr",       P15, null,
          /*16 */    "N1_SumBvpr",      P16, null,
          /*17 */    "N1_NkdBvр",       P17, null,
          /*18 */    "N1_ComB",         P18, null,
          /*19 */    "N1_RezMrkt",      P19, null,
          /*20 */    "N1_MrktRez",      P20, null,
          /*21 */    "N1_DMrktRez",     P21, null,
          /*22 */    "N1_ifAmort",      P22, null,
          /*23 */    "N1_IsBlock",      P23, null,
          /*24 */    "N1_FilB",         P24, null,
          /*25 */    "N1_ISIN",         P25, null
                     );      

  END;

  PRIVATE MACRO BeginReport()
     VAR AvrGroupName;
     VAR AvrKindShowValue = "";

     this.SetActiveSheet( "01" );

     RestReg_Form.GetFieldValue( PNFLD_RESTREG_AVRGROUP, @AvrGroupName );
     RestReg_Form.GetFieldValue( PNFLD_RESTREG_AVRKIND, @AvrKindShowValue );

     this.PrintFormatString( ReportHeader,
                     "N1_H0_1"  , RestReg_Form.GetFieldValue( PNFLD_RESTREG_ENDDATE ),   // Дата
                     "N1_H0_2"  , AvrGroupName,       // Группа налогового учета
                     "N1_H0_3"  , AvrKindShowValue,   // Вид ц/б
                     "N1_H0_4"  , RestReg_Form.GetFieldValue( PNFLD_RESTREG_AVRNAME )    // Выпуск ц/б
                   );


     this.RegisterTable( "N1_MainTable", TableHeader,
                     /*1  */    "N1_SecType",
                     /*2  */    "N1_SecCode",     
                     /*3  */    "N1_SecName",     
                     /*4  */    "N1_CodeB",       
                     /*5  */    "N1_TypeB",       
                     /*6  */    "N1_isTime",      
                     /*7  */    "N1_DZB",         
                     /*8  */    "N1_TZB",         
                     /*9  */    "N1_DDB",         
                     /*10 */    "N1_QntyDeal",
                     /*11 */    "N1_QntySold",
                     /*12 */    "N1_QntyOst", 
                     /*13 */    "N1_VN",          
                     /*14 */    "N1_VPrB",        
                     /*15 */    "N1_PrBvpr",      
                     /*16 */    "N1_SumBvpr",     
                     /*17 */    "N1_NkdBvр",      
                     /*18 */    "N1_ComB",        
                     /*19 */    "N1_RezMrkt",     
                     /*20 */    "N1_MrktRez",     
                     /*21 */    "N1_DMrktRez",    
                     /*22 */    "N1_ifAmort",
                     /*23 */    "N1_IsBlock",    
                     /*24 */    "N1_FilB",
                     /*25 */    "N1_ISIN"
                     );      

  END;

  MACRO ПечататьПромежуточныеИтоги()

     SetSubtotal(12, 11 + MAXROWSHEET,
                 "J8",      
                 "K8",    
                 "L8",      
                 "P8",
                 "Q8",
                 "R8"       
                 );
  END;

  PRIVATE MACRO EndReport()
     this.EndTable();
     ПечататьПромежуточныеИтоги();
     this.AddStandardFooter( "N1_" );
  END;

  /*  Если для лота покупки BuyID существуют связи "поставки- возврат" с датой связи меньшей, чем отчетная дата,  */
  /*     то выводится 1. В противном случае выводится 0                                                           */
  /*  отбираются связи, для которых покупка является источником - T_SOURCEID */
  PRIVATE MACRO GetDelRet( LotBuyID:integer ) 

     var sql = RSDCommand(" SELECT COUNT(1) as ExistDelRet " + 
                            " FROM dsctxlnk_dbt lnk " +    
                            " WHERE lnk.T_SOURCEID = ? " +
                            "  AND lnk.T_TYPE = " + string(TXLNK_DELRET ) + /*поставки - возврат*/
                            "  AND lnk.T_DATE < ? ");

     sql.addParam( "", RSDBP_IN, LotBuyID );
     sql.addParam( "", RSDBP_IN, RestReg_Form.GetFieldValue( PNFLD_RESTREG_ENDDATE ) );
     sql.execute();

     var Data = TRsbDataSet( sql );
     if( Data.MoveNext() )
        if( Data.ExistDelRet > 0 ) 
           return 1;
        else
           return 0;
        end; 
     end;
     return 0;
  END;


  PRIVATE MACRO GetDeal( Deal:TBFile, DealID:INTEGER ):BOOL
     Deal.Clear();
     Deal.rec.DealID = DealID;
     return Deal.GetEQ();
  END;

  /* Получить наименование филиала из сделки*/
  PRIVATE MACRO GetDepartmentName( DealBuyDepartment ):STRING
     VAR DprtName = "";

     if( DealBuyDepartment > 0 ) 
        DprtName = T_Dep.Get_Dep(DealBuyDepartment);
     end;
     return DprtName;
  END;

  PRIVATE MACRO GetDealType( LotType:INTEGER ):STRING

     if( LotType == TXLOTS_BUY )
        return "B";
     elif( LotType == TXLOTS_SALE )
        return "S";
     elif( LotType == TXLOTS_REPO )
        return "РП";
     elif( LotType == TXLOTS_BACKREPO )
        return "РО";
     elif( LotType == TXLOTS_LOANPUT )
        return "ЗП";
     elif( LotType == TXLOTS_LOANGET )
        return "ЗО";
     else
        return "";
     end;
     
  END;

  PRIVATE MACRO GetNKD( Deal:TBFile, dl_leg:TRecHandler, Ratio:DOUBLE, NKD:@MONEY )
     NKD = "";
     if( (Deal != null) AND (dl_leg != NULL) )
        NKD = dl_leg.rec.NKD*Ratio;
     end;
  END;

  PRIVATE MACRO GetRezMrkt():BOOL
     VAR DataSet, CurrentNominal, MinCourse, SPParams:TArray = TArray();
     VAR ValidFromDate = DATE(0,0,0), NumInList;
     var IsBond = false;
     var Select;
     VAR EndDate = RestReg_Form.GetFieldValue( PNFLD_RESTREG_ENDDATE );

     if( (m_LastGetRezMrkt_Result   == true) AND 
         (m_LastGetRezMrkt_FIID     == m_RS.FIID) AND 
         (m_LastGetRezMrkt_CalcDate == EndDate) 
       )
        return m_LastGetRezMrkt_Result;
     end;
     
     IsBond = FI_IsBond(m_RS.t_FIID);

     /*минимальное значение из всех курсов вида "средневзвешенная цена" на дату <DRez>, но эта дата должна быть не ранее, чем за один год до <DRez>.*/
     SPParams[0] = SQLParam( "pFromFI"   , m_RS.t_FIID            );
     SPParams[1] = SQLParam( "pToFI"     , m_RS.t_FaceValueFI     );
     SPParams[2] = SQLParam( "pType"     , 4                    );
     SPParams[3] = SQLParam( "pDate"     , EndDate     );
     SPParams[5] = SQLParam( "pRateID"   , V_INTEGER, RSDBP_OUT );
     SPParams[6] = SQLParam( "pSinceDate", V_DATE,    RSDBP_OUT );
     SPParams[7] = SQLParam( "pMarketCountry", "" ); //на любых биржах
     SPParams[8] = SQLParam( "pIsForeignMarket", 0); //на любых биржах
     
     if(EndDate <= date(31,12,2009))
       SPParams[4] = SQLParam( "pNMonths", 12);
     else
       SPParams[4] = SQLParam( "pNMonths", 3);
     end;

     if(IsBond)
       SPParams[9] = SQLParam( "pOnlyRate", 1);
     else
       SPParams[9] = SQLParam( "pOnlyRate", 0);
     end;

     MinCourse   = execStoredFunc( "RSB_FIInstr.FI_GetMinRateMonth", V_DOUBLE, SPParams );

     if( (EndDate > date(31,12,2009)) and (not ((MinCourse != NULL) and (MinCourse > 0))) ) //если на российской бирже курса не нашли
       //то проводится поиск среди курсов "цена закрытия" иностранных бирж за тот же период
       SPParams.size = 0;
       SPParams[0] = SQLParam( "pFromFI"   , m_RS.t_FIID            );
       SPParams[1] = SQLParam( "pToFI"     , m_RS.t_FaceValueFI     );
       SPParams[2] = SQLParam( "pType", 18); //"цена закрытия"
       SPParams[3] = SQLParam( "pDate"     , EndDate     );
       SPParams[5] = SQLParam( "pRateID"   , V_INTEGER, RSDBP_OUT );
       SPParams[6] = SQLParam( "pSinceDate", V_DATE,    RSDBP_OUT );
       SPParams[7] = SQLParam( "pMarketCountry", "" ); //на любых биржах
       SPParams[8] = SQLParam( "pIsForeignMarket", 0); //на любых биржах

       if(EndDate <= date(31,12,2009))
         SPParams[4] = SQLParam( "pNMonths", 12);
       else
         SPParams[4] = SQLParam( "pNMonths", 3);
       end;

       if(IsBond)
         SPParams[9] = SQLParam( "pOnlyRate", 1);
       else
         SPParams[9] = SQLParam( "pOnlyRate", 0);
       end;

       MinCourse   = execStoredFunc( "RSB_FIInstr.FI_GetMinRateMonth", V_DOUBLE, SPParams );
     end;

     if( MinCourse != NULL and (MinCourse > 0) )
          
          Select = RSDCommand(" SELECT rate.t_IsRelative, " +
                              "        (CASE WHEN rate.t_Market_Place > 0 THEN (SELECT pt.t_ShortName FROM dparty_dbt pt WHERE pt.t_PartyID = rate.t_Market_Place) ELSE CHR(1) END) as Market_Place_ShortName " +
                              "   FROM dratedef_dbt rate " +
                              "  WHERE rate.t_RateID = ? "
                             );
          
          Select.addParam( "", RSDBP_IN, string(SPParams.value(5).value) );
          Select.execute();
          
          DataSet = TRsbDataSet( Select );
          if( DataSet.MoveNext() )
             m_DMrktRez = SQL_ConvTypeDate( SPParams.value(6).value );
             m_MrktRez  = DataSet.Market_Place_ShortName;
             m_RezMrkt  = MinCourse;

             m_LastGetRezMrkt_Result   = true;
             m_LastGetRezMrkt_FIID     = m_RS.t_FIID;
             m_LastGetRezMrkt_CalcDate = EndDate;

             return m_LastGetRezMrkt_Result;
          end;
       end;

     /*нет подходящего курса */
     m_DMrktRez = DATE(0,0,0);
     m_RezMrkt  = 0; 
     m_MrktRez  = "";

     m_LastGetRezMrkt_Result = false;

     return m_LastGetRezMrkt_Result;
  END;

  MACRO RezMrkt():MONEY
    return m_RezMrkt;
  END;
 
  MACRO MrktRez():STRING
    return m_MrktRez;
  END;
 
  MACRO DMrktRez():DATE
    return m_DMrktRez;
  END;

  PRIVATE MACRO GetTaxGroup()
     return m_RS.TaxGroup;
  END;

  PRIVATE MACRO Create()

     VAR dl_leg      = TRecHandler( "dl_leg.dbt" );

     VAR DataQuery;
     VAR DealBuy;
     VAR AddWhere:string = "";

     VAR AvrFIID:integer, AvrKind:integer;       
     VAR QntySold:money = $0, SumBvpr:money = $0;
     VAR NKD:money = $0;
     VAR BuyDealTime:time, BuyPrice:double = 0.0;
     VAR StrNumbDeal1, StrNumbDeal2;
     VAR IsBlock;
     VAR EndDate = RestReg_Form.GetFieldValue( PNFLD_RESTREG_ENDDATE ),
         AvrFIIDList = null,
         AvrFIIDCount : Integer = 0,
         AvrFIIDAddWhere : String = "",
         WasSetSecurity : Bool = false,

         AvrKindList = null,
         AvrKindAddWhere : String = "",
         AvrKindCount : Integer = 0,
         
         GNUList = null,
         GNUAddWhere : String = "",
         GNUCount : Integer = 0;
     VAR ProgressValue = CTxProgressValue();

     m_CacheFinInstr.Clear();
     T_Dep.ClearArray();
                   
     /*ценные бумаги в соединенных сделках  должны удовлетворять условиям, заданным панели подготовки отчета*/
     if( not m_IsWebService )
        AvrFIIDList = TArray();
        AvrFIIDList[AvrFIIDList.Size] = RestReg_Form.GetFieldValue( PNFLD_RESTREG_AVRCODE );
     else
        AvrFIIDList = RestReg_Form.GetFieldValue( PNFLD_RESTREG_AVRCODE );
     end;
     if( ( ValType( AvrFIIDList ) != V_UNDEF ) and ( AvrFIIDList.Size > 0 ) )
        while( AvrFIIDCount < AvrFIIDList.Size )
           if( ( ValType( AvrFIIDList[AvrFIIDCount] ) == V_INTEGER )
           and ( AvrFIIDList[AvrFIIDCount] > 0 ) )
              if( AvrFIIDAddWhere == "" )
                 AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
              else
                 AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
              end;
              AvrFIIDAddWhere = AvrFIIDAddWhere + " FinInstr.t_FIID = " + String( AvrFIIDList[AvrFIIDCount] ) + " ";
           end;
           AvrFIIDCount = AvrFIIDCount + 1;
        end;
        if( AvrFIIDAddWhere != "" )
           AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
           WasSetSecurity = true;
        end;
        AddWhere = AddWhere + AvrFIIDAddWhere;
     end;

     /*если не задана бумага*/
     if( not WasSetSecurity )
        if( not m_IsWebService )
           AvrKindList = TArray();
           AvrKindList[AvrKindList.Size] = RestReg_Form.GetFieldValue( PNFLD_RESTREG_AVRKIND );
        else
           AvrKindList = RestReg_Form.GetFieldValue( PNFLD_RESTREG_AVRKIND );
        end;
        /*задан вид бумаги*/
        if( ( ValType( AvrKindList ) != V_UNDEF ) and ( AvrKindList.Size > 0 ) )
           while( AvrKindCount < AvrKindList.Size )
              if( ( ValType( AvrKindList[AvrKindCount] ) == V_INTEGER )
              and ( AvrKindList[AvrKindCount] > 0 ) )
                 if( AvrKindAddWhere == "" )
                    AvrKindAddWhere = AvrKindAddWhere + " AND ( ";
                 else
                    AvrKindAddWhere = AvrKindAddWhere + " OR ";
                 end;
                 AvrKindAddWhere = AvrKindAddWhere + " RSB_FIInstr.FI_AvrKindsEQ(2, " + String( AvrKindList[AvrKindCount] ) + ", FinInstr.t_AvoirKind) = 1 ";
              end;
              AvrKindCount = AvrKindCount + 1;
           end;
           if( AvrKindAddWhere != "" )
              AvrKindAddWhere = AvrKindAddWhere + " ) ";
           end;
           AddWhere = AddWhere + AvrKindAddWhere;      
        end;

        if( not m_IsWebService )
           GNUList = TArray();
           GNUList[GNUList.Size] = RestReg_Form.GetFieldValue( PNFLD_RESTREG_AVRGROUP );
        else
           GNUList = RestReg_Form.GetFieldValue( PNFLD_RESTREG_AVRGROUP );
        end;
        /*задана категория бумаги*/
        if( ( ValType( GNUList ) != V_UNDEF ) and ( GNUList.Size > 0 ) )
           while( GNUCount < GNUList.Size )
              if( ( ValType( GNUList[GNUCount] ) == V_INTEGER )
              and ( GNUList[GNUCount] > 0 ) )
               if( GNUAddWhere == "" )
                    GNUAddWhere = GNUAddWhere + " AND ( ";
                 else
                    GNUAddWhere = GNUAddWhere + " OR ";
                 end;
                 GNUAddWhere = GNUAddWhere + " av.t_TaxGroup = " + String( GNUList[GNUCount] ) + " ";
              end;
              GNUCount = GNUCount + 1;
           end;
           if( GNUAddWhere != "" )
              GNUAddWhere = GNUAddWhere + " ) ";
           end;
           AddWhere = AddWhere + GNUAddWhere;
        end;
     end;

     m_QueryRest = " SELECT NVL(TXRest.T_SourceID,0) AS SourceID, max(TXRest.T_SourceDate) AS SourceDate, NVL(sum(TXRest.T_AMOUNT),0) AS Amount," +
                 "        FinInstr.t_FIID "+
                 "   FROM DSCTXREST_DBT   TXRest,"+
                 "        DFININSTR_DBT FinInstr " + 
                 "  WHERE TXRest.T_AMOUNT > 0 AND " +
                 "        TXRest.t_BUYDATE <= " + GetSQLDate( EndDate ) + " AND " +
                 "        TXRest.t_BUYDATE <> " + GetSQLDate( DATE(0,0,0) ) + " AND " +
                 "        ( TXRest.t_SALEDATE IS NULL  OR " +
                 "          TXRest.t_SALEDATE = " + GetSQLDate( DATE(0,0,0) ) + " OR " + 
                 "          TXRest.t_SALEDATE > " + GetSQLDate( EndDate ) +
                 "        ) AND " +
                 "        FinInstr.t_FIID = TXRest.t_FIID " +
                 " GROUP BY FinInstr.t_FIID, TXRest.T_SourceID";

     DataQuery = " SELECT   QueryRest.t_FIID, QueryRest.Amount AS QntyOst, TXBuyLot.t_BuyDate as SourceRestDate, FinInstr.t_FIID, FinInstr.t_FaceValueFI, av.t_TaxGroup, " +
                 "          TXBuyLot.t_ID AS LotBuyID, TXBuyLot.t_Type AS LotBuyType, " +
                 "          TXBuyLot.t_DealCodeTS AS BuyDealCodeTS, TXBuyLot.T_BUYDATE AS BuyDate, " +
                 "          TXBuyLot.T_DEALID AS DealBuyID, TXBuyLot.T_DEALDATE AS DealBuyDate, " +
                 "          TXBuyLot.t_Amount AS QntyDeal, LegBuy.t_Principal AS Q, LegBuy.t_Price AS BuyPrice, TXBuyLot.t_PriceFIID AS BuyPriceFIID, LegBuy.t_Cost as BuyCost, " +
                 "          TXBuyLot.t_DealTime AS BuyDealTime, av.t_TaxGroup AS TaxGroup, DealBuy.t_Department as DealBuyDepartment, " +

                 "          (CASE WHEN TXBuyLot.t_Type = " + string(TXLOTS_BUY)+ " AND rsb_sctx.DealIsTerm(TXBuyLot.t_DealID, TXBuyLot.t_FIID, TXBuyLot.t_BUYDATE) = 1 THEN 'C' "+
                 "                ELSE CHR(0) END) as isTime, "+

                 "          (CASE WHEN     RSB_FIInstr.FI_AvrKindsEQ("+FIKIND_AVOIRISS+", "+AVOIRISSKIND_BOND+", FinInstr.t_AvoirKind) > 0 " +
                 "                     AND Exists(SELECT fiw.t_FIID " +
                 "                              FROM dfiwarnts_dbt fiw " +
                 "                             WHERE fiw.t_FIID         =  TXBuyLot.t_FIID AND " +
                 "                                   fiw.t_IsPartial    = 'X'" +
                 "                            ) THEN 1 " +
                 "                ELSE 0 END) as ifAmort,"

                 "          Rsb_SCTX.TXGetComissionsSum(DealBuy.t_DealID,  DealBuy.t_BOfficeKind,  TXBuyLot.t_BuyDATE, 0) as CommBuy, " +
                 "          NVL(LegBuy.t_NKD,0) NKD, NVL(LegBuy.t_Price,0) as LegPrice, DealBuy.t_Blocked as Blocked, av.t_ISIN " +

                 "   FROM   (" + m_QueryRest + ") QueryRest," +
                 "          DSCTXLOT_DBT  currTXBuyLot, DSCTXLOT_DBT  TXBuyLot, DFININSTR_DBT FinInstr, DAVOIRISS_DBT av, DDL_TICK_DBT DealBuy, DDL_LEG_DBT LegBuy " +
                 "  WHERE   currTXBuyLot.t_ID = QueryRest.SourceID AND " +
                 "          TXBuyLot.t_ID = currTXBuyLot.t_BegLotID AND "+
                 "          ( TXBuyLot.t_Type = " + string(TXLOTS_BUY)        +  " OR "
                 "            TXBuyLot.t_Type = " + string(TXLOTS_BACKREPO)   +  " OR "
                 "            TXBuyLot.t_Type = " + string(TXLOTS_LOANGET)    + 
                 "          ) AND " +
                 "        DealBuy.t_DealID = TXBuyLot.t_DealID AND "
                 "        LegBuy.t_DealID (+)= DealBuy.t_DealID AND" +
                 "        LegBuy.t_LegKind (+)= " + LEG_KIND_DL_TICK + " AND " +
                 "        LegBuy.t_LegID   (+)= 0 AND " +
                 "        FinInstr.t_FIID = QueryRest.t_FIID AND " +
                 "        FinInstr.t_FIID = av.t_FIID " + AddWhere +
                 " ORDER BY av.t_TaxGroup, QueryRest.SourceDate, TXBuyLot.T_BUYDATE, TXBuyLot.t_DealCodeTS";

     ProgressValue.Maximum = SQL_GetNRecs( DataQuery );
     ProgressValue.Current = 0;

 var ProgressIndicator = CreateProgressIndicator(m_IsWebService);

     if( m_IsWebService and (WebMenuItem != null) )
       var header = "Формирование отчета " + WebMenuItem.szNameItem;
       ProgressIndicator.Start(ProgressValue.Maximum, header, header);
     else
       ProgressIndicator.Start(ProgressValue.Maximum,"\"Остаток ценных бумаг в портфеле\"" , HTML_StSheetName);
     end;

     m_RS = TRsbDataSet( DataQuery );
     while( m_RS.MoveNext() )

        if( ПолучитьФинИн( m_RS.t_FIID, m_Fininstr, m_Avoiriss ) ) 
           MsgBox ("Не найден ФИ остатка (DV_SCTXREST.t_FIID =", m_RS.t_FIID,")" );
           continue;
        end;

        //Рыночная котировка
        GetRezMrkt();

        DealBuy   =  null;
        BuyPrice  =  0.0;
        NKD       =  $0;

        if( FI_IsBond( m_Fininstr ) == true )
           /* для облигаций берем цену, напрямую из сделки*/
           /*цена, в том виде в каком она задана в сделке: в процентах от номинала или в валюте цены сделки*/
           NKD = m_RS.NKD*m_RS.QntyOst/m_RS.Q;
        end; 
        
        BuyPrice     =  m_RS.LegPrice;
        QntySold     =  m_RS.QntyDeal - m_RS.QntyOst;
        SumBvpr      =  double(m_RS.BuyCost) * m_RS.QntyOst/m_RS.Q;

        IsBlock = "";
        if((m_RS.Blocked == SET_CHAR) AND (m_RS.LotBuyType == TXLOTS_BACKREPO))
          IsBlock = "Да";
        end;

          m_IsDataExist = true;
          PrintLine( GetTaxGroup(),                      // Налоговая группа
                   m_Fininstr.rec.FI_Code,               // Код ЦБ                             
                   m_Fininstr.rec.Name,                  // Краткое наименование ценной бумаги 
                   m_RS.BuyDealCodeTS,                     // № сделки приобретения          
                   GetDealType(m_RS.LotBuyType),           // Код типа сделки приобретения   
                   m_RS.isTime,                            // Признак сочности сделки 
                   SQL_ConvTypeDate(m_RS.DealBuyDate),     // Дата заключения сделки на приоб-ретение ЦБ 
                   Time(m_RS.BuyDealTime),                 // Время заключения сделки приобретения  
                   SQL_ConvTypeDate(m_RS.SourceRestDate),  // Дата фактической поставки  
                   m_RS.QntyDeal,                          // Количество ц/б в сделке приобретения, шт  
                   QntySold,                               // Продано ц/б, шт    
                   m_RS.QntyOst,                            // Кол-во ценных бумаг в остатке, шт 
                   m_CacheFinInstr.GetISO( m_Fininstr.rec.FaceValueFI ).m_Number, // Код валюта номинала 
                   m_CacheFinInstr.GetISO( m_RS.BuyPriceFIID ).m_Number, // Код валюты цены 
                   double(BuyPrice),                     // цена приобретения
                   SumBvpr,                              // стоимость приобретения 
                   NKD,                                  // НКД уплаченный при приобретении 
                   m_RS.CommBuy*m_RS.QntyOst/m_RS.Q,     //Комиссия, уплаченная при приобретении, руб.
                   this.RezMrkt(),                       // Рыночная котировка на отчетную дату, сложивш. на бирже - Цена:  
                   this.MrktRez(),                       //Рыночная котировка на отчетную дату, сложивш. на бирже - Источ-ник информации  
                   this.DMrktRez(),                      // Рыночная котировка на отчетную дату, сложивш. на бирже - Дата котировки 
                   m_RS.ifAmort,                         // Отметка о возможности выплат по амортизации долга  (1 да, 0 -нет)
                   IsBlock,                              // Сделки РЕПО с ограничением прав
                   GetDepartmentName( m_RS.DealBuyDepartment ),        // Краткое наименование филиала из сделки покупки
                   m_RS.ISIN                             // ISIN
                 );

        ProgressValue.Current = ProgressValue.Current + 1;
        ProgressIndicator.Update(ProgressValue.Current,ProgressValue.Maximum);
     end;
     ProgressIndicator.Stop();

  END;
  
  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode, UsePOI, UseBigReportMode );
  SetRowFlushCount(2000);
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  if( not IsWebService )
    RestReg_Form = SP_RestReg_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      RestReg_Form = WS_TX_RestReg_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = RestReg_Form.Run();
    end;

    if( stat )
      RestReg_Report = SP_RestReg_Report( null, "report_txrest_avoiriss.xlsx", false, IsWebService, ReportHashCode, POI_MODE, BIGREPORT_MODE );
      RestReg_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat( RestReg_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
        FullReportFileNames = RestReg_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat( RestReg_Report.PrintMode() );
      end;
    end;
  end;

  return FullReportFileNames;
END;
