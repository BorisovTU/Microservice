import secinter, CTInter, DealsInter;

/* Настраиваемая пользователем константа категория на которой висит
     переоцениваемая это бумага или нет
 */
const REVIEW_CATEGORY = 101;

/* Настраиваемый пользователем макрос - выдает счет инвестиционных вложений
для фининструмента FIID
*/
macro get_inv_acc( FIID )
     return "1";
end;

/* Настраиваемый пользователем макрос - выдает счет торговых вложений
для фининструмента FIID
*/
macro get_resale_acc( FIID )
     return "2";
end;


/* Настраиваемый пользователем макрос - отвечает на вопрос - счет ли это
торговых вложений
*/
macro is_acc_resale( acc )
     return SubStr(acc,5,1) == "2";
end;

/*Настраиваемая пользователем константа тип операции перемещения
*/
const resale_inv_moving_op = 2000;


const STOCK_DEALING_CLIENT = 1;

file comms(dl_comm) key 0 write;
file comm_check(dl_comm) key 5;
file paym(pmpaym) key 0 write;
record commiss(dl_comm);
record commpm(pmpaym);

var err_mess = "",
    calc_date = {curdate},
    deadline_date,
    num_inserted = 0;

macro six_month_before_date( from_date )
     var day, mon, year, new_date;
     datesplit(from_date,day, mon, year);
     mon = mon - 6;
     if ( mon <= 0 )
          mon = 12 + mon;
          year = year - 1;
     end;
     new_date = date(day,mon,year);
     return new_date;
end;
macro prev_oper_date( from_date, IssueID )
     file moves(dl_comm) key 6;
     moves.DocKind = BofficeKindMOVE;
     moves.CommStatus = DL_COMM_CLOSED;
     moves.ClientID = IssueID;
     moves.OperationKind = resale_inv_moving_op;
     moves.CommDate = from_date;                                      
     moves.DocumentID = 0;
     getGE(moves);

     if ( prev(moves) and
          (moves.DocKind == BofficeKindMOVE) and
          (moves.CommStatus == DL_COMM_CLOSED) and
          (moves.ClientID == IssueID) and
          (moves.OperationKind == resale_inv_moving_op)
        )
          setparm(0,moves.CommDate);
          return true;
     end;
     return false
end;
macro trn_proc
     copy(comms, commiss);
     if ( not insert(comms) )
          err_mess = "Не могу вставить первичный документ перенос ценных бумаг";
          AbortTrn;
     end;
     copy(paym, commpm);
     paym.DocumentID = comms.DocumentID;
     if ( not insert(paym) )
          err_mess = "Не могу вставить платеж по  переносу ценных бумаг";
          AbortTrn;
     end;
end;
macro insert_comm
     copy(comm_check, commiss);
     if ( getGE(comm_check) and
          (comm_check.DocKind == commiss.DocKind) and
          (comm_check.OperationKind == commiss.OperationKind) and
          (comm_check.ClientID == commiss.ClientID) and
          (comm_check.CommDate == commiss.CommDate)
        )
          println("Такая операция уже существует в базе");
          return false;
     end;
     if ( ProcessTrn(null, "trn_proc") )
          println("все в порядке");
          num_inserted = num_inserted + 1;
          return true;
     end;
     println(err_mess);
     return false;
end;

macro insert_moving_op(fin, rest, sum)
     clearrecord(commiss);
     clearrecord(commpm);

     commiss.DocKind = BofficeKindMOVE;
     commiss.Oper = {oper};
     commiss.CommCode = "";
     commiss.OperationKind = resale_inv_moving_op;
     commiss.ClientID = fin.FIID;
     commiss.CommDate = calc_date;
     commiss.CommStatus = DL_COMM_PREPARING;
     commiss.hidden_sum = rest;

     commpm.DocKind = BofficeKindMOVE;
     commpm.Purpose = PurpContr;
     commpm.SubPurpose = 0;
     commpm.FIID = fin.FaceValueFI;
     commpm.Amount = sum;
     commpm.PayFIID = fin.FaceValueFI;
     commpm.PayerAccount = get_inv_acc( fin.FIID );
     commpm.ReceiverAccount = get_resale_acc( fin.FIID );
     commpm.ValueDate = calc_date;
     commpm.PaymStatus = PM_PREPARING;
     commpm.Netting = SET_CHAR;
     commpm.Department = {OperDprt}; /* IL 30.10.2000 {NumDprt};*/
     print("Вставка документа по перемещению "+fin.FI_Code+" из торгового портфеля в инвестиционный: ");
     if ( commpm.Amount != $0 )
          insert_comm;
     else
          println("Нулевой остаток");
     end;
end;


macro move_iss( prev_date, fin, reviewable )
     var continue_cicle, rest:moneyl = $0, sum:moneyl = $0;
     file payms(pmpaym) key 7;
     file check_payms(pmpaym) key 1;
     file wrtsums(pmwrtsum) key 0;

     payms.Receiver = {OurBank};
     payms.DocKind = BofficeKindSP;
     payms.Purpose = PurpBase;
     payms.FIID = fin.FIID;
     payms.ValueDate = prev_date;

     continue_cicle = getGE(payms);

     while ( continue_cicle and
             (payms.Receiver == {OurBank}) and
             (payms.DocKind == BofficeKindSP) and
             (payms.Purpose == PurpBase) and
             (payms.FIID == fin.FIID) and
             (payms.ValueDate < deadline_date)
           )
          check_payms.DocKind    = payms.DocKind   ;
          check_payms.DocumentID = payms.DocumentID;
          check_payms.Purpose    = PurpContr;
          check_payms.SubPurpose = payms.SubPurpose;

          if ( getEQ(check_payms) and
               (check_payms.PaymStatus == PM_FINISHED ) and
               is_acc_resale( check_payms.PayerAccount )
             )

               wrtsums.PaymentID = payms.PaymentID;
               wrtsums.Buy_Sale = 0;
     
               if ( not getEQ(wrtsums) )
                         println("Не могу найти записи о балансовой стоимости для платежа");
                         return;
               end;
               rest = rest + wrtsums.Amount;
     
     
               if ( not reviewable )
                    sum = sum + wrtsums.Amount*check_payms.Amount/payms.Amount;
               end;
          end;
          continue_cicle = next(payms);
     end;
     if ( reviewable )
          ConvSum( sum, rest, calc_date, fin.FIID, fin.FaceValueFI);
     end;
     if ( sum != $0 )
          insert_moving_op(fin, rest, sum);
     end;
end;

macro for_each_issue_calc
     var continue_cicle, prev_op_date = calc_date, num = 0;
     file fin(fininstr) key 2;
     record iss(avoiriss);
     file links(objatcor) key 2;
     file vals(objattr) key 0;
     var AttrID;

     macro num_recs
          var i = 0;
          fin.FI_Kind = FIKIND_AVOIRISS;
          fin.AvoirKind = 0;
          fin.FI_Code = "";
          continue_cicle = getGE(fin);
          while ( continue_cicle and
                  (fin.FI_Kind == FIKIND_AVOIRISS)
                )
               i = i + 1;
               continue_cicle = next(fin);
          end;
          return i;
     end;
     InitProgress(num_recs,"Просмотр справочника ценных бумаг","Просмотр справочника");

     fin.FI_Kind = FIKIND_AVOIRISS;
     fin.AvoirKind = 0;
     fin.FI_Code = "";
     continue_cicle = getGE(fin);

     while ( continue_cicle and
             (fin.FI_Kind == FIKIND_AVOIRISS)
           )
          num = num + 1;
          UseProgress(num);
          if ( ПолучитьФинИн(fin.FIID,null,iss) != 0 )
               println("Отсутствует запись о ценной бумаге ", fin.FI_Code);
               return;
          end;
          /*links.ObjectType = OBJTYPE_AVOIRISS;
          links.GroupID = REVIEW_CATEGORY;
          links.Object = UniID(iss,OBJTYPE_AVOIRISS);
          links.General = SET_CHAR;
          if ( getEQ(links) )*/
          if( GetMainObjAttr(null, OBJTYPE_AVOIRISS, UniID(iss, OBJTYPE_AVOIRISS), REVIEW_CATEGORY,
                             AttrID, null, null) )

               vals.ObjectType = OBJTYPE_AVOIRISS;
               vals.GroupID    = REVIEW_CATEGORY;
               vals.AttrID     = AttrID;
               if ( getEQ(vals) )
                    if ( prev_oper_date(prev_op_date,fin.FIID) )
                         move_iss(prev_op_date,fin, SubStr(vals.NumInList,1,1) == SET_CHAR );
                    else     
                         move_iss(date(0,0,0),fin, SubStr(vals.NumInList,1,1) == SET_CHAR );
                    end;
               else
                    println("Для ",fin.FI_Code," не установлено значение категории переоцениваемости");
               end;
          else
               println("Для ",fin.FI_Code," не установлена категория переоцениваемости");
          end;
          continue_cicle = next(fin);
     end;
     RemProgress;
end;

macro moving_inserter
     if ( not GetDate(calc_date,"Введите дату расчета") ) return; end;
/* IL 03.07.2000 убрал шестимесячный запрет на перевод бумаг */
/*     deadline_date = six_month_before_date(calc_date);*/
     deadline_date = calc_date;

     for_each_issue_calc;
     if ( num_inserted == 0 )
          println("Нет ни одной бумаги которую нужно переносить из торгового портфеля в инвестиционный");
     end;
end;
