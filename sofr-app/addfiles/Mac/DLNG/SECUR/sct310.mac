/* Операция продажи ц/б на ОРЦБ    
   Шаг - списание себестоимости    */

import InsCarryDoc, "sp_categ.mac", "sp_cars.mac", "scservop.mac";

macro ExecuteStep( Doc, FDoc )

  record deal(dl_tick);
  var    FD, dat;
  var    CloseAccArray = DL_AccCollector,
         ErrMsgArray   = DL_TUniqStrCollector;

  SetBuff( deal, FDoc );
  FD = SPFirstDoc ( deal, IsBackExec(deal, SP_EXECUTE_STEP_SALE) );

  GetOprDate( FD.GetKindDate(DATE_DEALSETAVOIRISS), dat ); /*Дата поставки ц/б */

  if( dat > {curdate} )
    return SayErrorBuySale( deal,"Преждевременное выполнение шага запрещено.");
  end;

  if( not СписаниеСебестоимостиПриПродаже(FD, dat, Doc) )
     return 1;
  end;

  GetOprDate( DATE_DEALEEND, dat );

  if( DL_CloseAccountByDeal(FD.tick.rec.DealID,FD.tick.rec.BofficeKind,dat,CloseAccArray,ErrMsgArray) )
     if( ErrMsgArray.Size() > 0 )
        return SayErrorBuySale( deal, ErrMsgArray, true );
     else
        return SayErrorBuySale( deal, "Ошибка при закрытии счетов по сделке." );
     end;
  end;

  SaveDataCloseAccArray(deal,CloseAccArray);

  if(DL_UpdateGrDealAcc( FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, FD.tick.rec.PFI, DLGR_TEMPL_DELIVERY, DLGR_ACCKIND_BACKOFFICE, date(0,0,0), DLGRACC_STATE_FACTEXEC) != 0 )
    return 1;
  end;

  if( not InsertOprStatus( SP_OPERSTATUS_SECURDEALS_F1, SP_OPERSTATUS_SECURDEALS_F1_FINISH) )
     msgbox( "Ошибка при установке статуса <Учет по 1 ч offline> операции" );
     return 1;
  end;

  return 0;
end;
