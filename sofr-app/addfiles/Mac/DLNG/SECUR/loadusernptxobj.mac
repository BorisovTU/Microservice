/*
$Name:        loadusernptxobj.mac
$Module:      Ценные бумаги
$Description: Ф-ции для загузки внешних пользовательских объектов НДР с создание НОБ по ним.
*/
import "nptxcalcfun.mac";

//Класс с данными для заполнения аналитик объекта НДР
class TObjAnaliticData(_AnaliticKind1,  // Вид аналитики 1
                       _Analitic1,      // Аналитика 1    
                       _AnaliticKind2,  // Вид аналитики 2
                       _Analitic2,      // Аналитика 2    
                       _AnaliticKind3,  // Вид аналитики 3
                       _Analitic3,      // Аналитика 3    
                       _AnaliticKind4,  // Вид аналитики 4
                       _Analitic4,      // Аналитика 4    
                       _AnaliticKind5,  // Вид аналитики 5
                       _Analitic5,      // Аналитика 5    
                       _AnaliticKind6,  // Вид аналитики 6
                       _Analitic6       // Аналитика 6    



       )
  var AnaliticKind1 = _AnaliticKind1;
  var Analitic1     = _Analitic1;    
  var AnaliticKind2 = _AnaliticKind2;
  var Analitic2     = _Analitic2;    
  var AnaliticKind3 = _AnaliticKind3;
  var Analitic3     = _Analitic3;    
  var AnaliticKind4 = _AnaliticKind4;
  var Analitic4     = _Analitic4;    
  var AnaliticKind5 = _AnaliticKind5;
  var Analitic5     = _Analitic5;    
  var AnaliticKind6 = _AnaliticKind6;
  var Analitic6     = _Analitic6;    
end;

//Удалить пользовательский объект НДР
macro DeleteUserNPTXOBJ(ObjKind:integer, ObjDirection:integer, OutSystCode:string, OutObjID:integer, ErrStr:@string)
  var query, cmd, DataSet;
  var del;

  query =   " select t_ObjID "
          + "   from dnptxobj_dbt "
          + "  where t_OutSystCode = ? "
          + "    and t_OutObjID = ? "
          + "    and t_SourceObjID = 0 ";

  cmd = DL_RSDCommand();
  
  cmd.addParam(OutSystCode);
  cmd.addParam(OutObjID);

  if(ObjKind > 0)
    query = query + " and t_Kind = ? ";
    cmd.addParam(ObjKind);
  end;

  if(ObjDirection > 0)
    query = query + " and t_Direction = ? ";
    cmd.addParam(ObjDirection);
  end;

  DataSet = cmd.Execute(query);

  RslDefCon.BeginTrans();

  while(DataSet.moveNext())
    query =   " delete from dnptxobdc_dbt dc "
            + "   where dc.t_ObjID = ?";

    del = DL_RSDCommand(query);
    del.addParam(DataSet.ObjID);
    del.executeCMD();

    query =   " delete from dnptxobj_dbt "
            + "  where t_ObjID = ? ";

    del = DL_RSDCommand(query);
    del.addParam(DataSet.ObjID);
    del.executeCMD();


    query =   " delete from dnptxobdc_dbt dc "
          + "   where dc.t_ObjID IN (select obj.t_ObjID "
          + "                          from dnptxobj_dbt obj "
          + "                         where obj.t_OutSystCode = ? "
          + "                           and obj.t_OutObjID = ? "
          + "                           and obj.t_SourceObjID = ? "
          + "                       ) ";

    del = DL_RSDCommand(query);
    del.addParam(OutSystCode);  
    del.addParam(OutObjID);     
    del.addParam(DataSet.ObjID);
    del.executeCMD();


    query =   " delete from dnptxobj_dbt  "
            + "  where t_OutSystCode = ? "
            + "    and t_OutObjID = ? "
            + "    and t_SourceObjID = ? ";

    del = DL_RSDCommand(query);
    del.addParam(OutSystCode);  
    del.addParam(OutObjID);     
    del.addParam(DataSet.ObjID);
    del.executeCMD();
  end;

  RslDefCon.CommitTrans();

  return 0;

OnError(er)
  if(RslDefCon.IsInTrans)
    RslDefCon.RollbackTrans();
  end;
  
  ErrStr = er.module+ " "+er.line+" "+er.message;
  return 1;
end;

//Вставка/изменение пользовательского объекта НДР
//Если объект найдется по идентификатору из внешней системы, то будет изменение, иначе вставка
macro InsertOrChangeUserNPTXOBJ(ClientID:integer,              //Идентификатор клиента
                                ObjKind:integer,               //Вид объекта НДР
                                ObjDate:date,                  //Дата объекта
                                ObjSum:money,                  //Сумма
                                ObjSumCurr:integer,            //Валюта суммы
                                OutSystCode:string,            //Код внешней системы
                                OutObjID:string,               //Идентификатор во внешней системе
                                Comment:string,                //Комментарий для объекта
                                AnaliticData:TObjAnaliticData, //Информация по аналитикам
                                IsIIS:bool,                    //Признак ИИС
                                ErrStr:@string                 //Возвращаемая строка с текстом ошибки
                             )

  var query, cmd, DataSet;
  var nptxkind = TBFile("nptxkind.dbt");
  var nptxobj  = TBFile("nptxobj.dbt");
  var Direction = 0;
  var ObjID = 0;
  var cnt = 0;
  var Kind = 0, BaseG = $0, ObjSum0 = $0;
  var TaxBaseKind = 0;
  var SourceObjID = 0;

  ErrStr = "";

  if(AnaliticData == NULL)
    AnaliticData = TObjAnaliticData(0,-1,0,-1,0,-1,0,-1,0,-1,0,-1);
  end;

  nptxkind.Clear();
  nptxkind.rec.Element = ObjKind;                                               
  if(not nptxkind.GetEQ())
    ErrStr = "Неизвестный вид объекта НДР";
    return 1;
  end;

  if(nptxkind.rec.Income == SET_CHAR)
    Direction = TXOBJ_DIR_IN;
  elif(nptxkind.rec.Expenditure == SET_CHAR)
    Direction = TXOBJ_DIR_OUT;
  end;

  TaxBaseKind = nptxkind.rec.TaxBaseKind;
  if(TaxBaseKind == 0)
    if(nptxkind.rec.Element == TXOBJ_DIV_SEC)
      TaxBaseKind = 1;
    elif(nptxkind.rec.Element == TXOBJ_PROCSEC_TS)
      TaxBaseKind = 2;
      if(IsIIS == true)
        TaxBaseKind = 5;
      end;
    end;
  end;


  //Найти существующий объект
  query =   " select obj.t_ObjID, obj.t_Kind, obj.t_Direction, "
          + "        NVL((select count(1) "
          + "               from dnptxobj_dbt obj1 "
          + "              where obj1.t_OutSystCode = obj.t_OutSystCode "
          + "                and obj1.t_OutObjID = obj.t_OutObjID "
          + "                and obj1.t_SourceObjID = obj.t_ObjID "
          + "            ), 0) as CntChild " 
          + "   from dnptxobj_dbt obj "
          + "  where obj.t_Kind = ? "
          + "    and obj.t_OutSystCode = ? "
          + "    and obj.t_OutObjID = ? ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(ObjKind);
  cmd.AddParam(OutSystCode);
  cmd.AddParam(OutObjID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    //Если объект уже существует, то удаляем его и создаем новый. Пусть это будет такой вариант изменения объекта
    if(DeleteUserNPTXOBJ(IIF(DataSet.CntChild > 0, DataSet.Kind, 0), DataSet.Direction, OutSystCode, OutObjID, @ErrStr) != 0)
      return 1;
    end;
  end;

  //Создать новый объект
  if((0 != DL_NPTX_InsertTaxObjectRetID( ObjDate                                 // Дата НДР
                                        ,ClientID                                // Клиент
                                        ,Direction                               // Направление
                                        ,nptxkind.rec.Level                      // Уровень
                                        ,"X"                                     // Признак "Пользовательский"
                                        ,ObjKind                                 // Вид НДР
                                        ,ObjSum                                  // Сумма дохода/расхода
                                        ,ObjSumCurr                              // Валюта дохода/расхода
                                        ,AnaliticData.AnaliticKind1              // Вид аналитики 1
                                        ,AnaliticData.Analitic1                  // Аналитика 1
                                        ,AnaliticData.AnaliticKind2              // Вид аналитики 2
                                        ,AnaliticData.Analitic2                  // Аналитика 2
                                        ,AnaliticData.AnaliticKind3              // Вид аналитики 3
                                        ,AnaliticData.Analitic3                  // Аналитика 3
                                        ,AnaliticData.AnaliticKind4              // Вид аналитики 4
                                        ,AnaliticData.Analitic4                  // Аналитика 4
                                        ,AnaliticData.AnaliticKind5              // Вид аналитики 5
                                        ,AnaliticData.Analitic5                  // Аналитика 5
                                        ,AnaliticData.AnaliticKind6              // Вид аналитики 6
                                        ,AnaliticData.Analitic6                  // Аналитика 6    
                                        ,Comment                                 // Комментарий
                                        ,0                                       // ID операции
                                        ,0                                       // ID шага операции
                                        ,1                                       // Признак вызова не из операции расчета НОБ
                                        ,ObjID                                   // Возвращаемый идентификатор созданного объекта
                                        ,"X"                                     // Признак внешней системы
                                        ,OutSystCode                             // Код внешней системы
                                        ,OutObjID                                // Идентификатор во внешней системе
                                       )) or (ObjID <= 0))

    ErrStr = GetErrMsg();
    return 1;
  else
    nptxobj.Clear();
    nptxobj.rec.ObjID = ObjID;
    if(not nptxobj.GetEQ)
      ErrStr = "Не найден объект НДР";
      return 1;
    else
      ObjSum0     = nptxobj.rec.Sum0;
      SourceObjID = nptxobj.rec.ObjID;
    end;
  end;

  if((TaxBaseKind > 0) and (nptxkind.rec.Level < 7))
    //Если объект включается в НОБ определенного вида, то сразу же создать объекты с НОБ на эту же сумму
    //Объекты создаются с тем же самым внешним идентификатором
    BaseG = ObjSum0;
    if(Direction == TXOBJ_DIR_OUT)
      BaseG = -BaseG;
    end;

    if(TaxBaseKind == 1)
      Kind = TXOBJ_BASEG1;
    elif(TaxBaseKind == 2)
      Kind = TXOBJ_BASEG2;
    elif(TaxBaseKind == 3)
      Kind = TXOBJ_BASEG3;
    elif(TaxBaseKind == 4)
      Kind = TXOBJ_BASEG4;
    elif(TaxBaseKind == 5)
      Kind = TXOBJ_BASEG5;
    elif(TaxBaseKind == 6)
      Kind = TXOBJ_BASEG6;
    elif(TaxBaseKind == 7)
      Kind = TXOBJ_BASEG7;
    elif(TaxBaseKind == 8)
      Kind = TXOBJ_BASEG8;
    elif(TaxBaseKind == 9)
      Kind = TXOBJ_BASEG9;
    end;

    ObjID = 0;
    if((0 != DL_NPTX_InsertTaxObjectRetID( ObjDate                                 // Дата НДР
                                          ,ClientID                                // Клиент
                                          ,TXOBJ_DIR_IN                            // Направление
                                          ,7                                       // Уровень
                                          ,"X"                                     // Признак "Пользовательский"
                                          ,Kind                                    // Вид НДР
                                          ,BaseG                                   // Сумма дохода/расхода
                                          ,NATCUR                                  // Валюта дохода/расхода
                                          ,0                                       // Вид аналитики 1
                                          ,-1                                      // Аналитика 1
                                          ,0                                       // Вид аналитики 2
                                          ,-1                                      // Аналитика 2
                                          ,0                                       // Вид аналитики 3
                                          ,-1                                      // Аналитика 3
                                          ,0                                       // Вид аналитики 4
                                          ,-1                                      // Аналитика 4
                                          ,0                                       // Вид аналитики 5
                                          ,-1                                      // Аналитика 5
                                          ,0                                       // Вид аналитики 6
                                          ,-1                                      // Аналитика 6
                                          ,Comment                                 // Комментарий
                                          ,0                                       // ID операции
                                          ,0                                       // ID шага операции
                                          ,1                                       // Признак вызова не из операции расчета НОБ
                                          ,ObjID                                   // Возвращаемый идентификатор созданного объекта
                                          ,"X"                                     // Признак внешней системы
                                          ,OutSystCode                             // Код внешней системы
                                          ,OutObjID                                // Идентификатор во внешней системе
                                          ,SourceObjID                             // Идентификатор объекта-источника
                                         )) or (ObjID <= 0))

      ErrStr = GetErrMsg();
      return 1;
    end;

    Kind = TXOBJ_BASEGENERAL;
    if(TaxBaseKind == 5)
      Kind = TXOBJ_BASEGENERAL_IIS;
    end;

    ObjID = 0;
    if((0 != DL_NPTX_InsertTaxObjectRetID( ObjDate                                 // Дата НДР
                                          ,ClientID                                // Клиент
                                          ,TXOBJ_DIR_IN                            // Направление
                                          ,7                                       // Уровень
                                          ,"X"                                     // Признак "Пользовательский"
                                          ,Kind                                    // Вид НДР
                                          ,BaseG                                   // Сумма дохода/расхода
                                          ,NATCUR                                  // Валюта дохода/расхода
                                          ,0                                       // Вид аналитики 1
                                          ,-1                                      // Аналитика 1
                                          ,0                                       // Вид аналитики 2
                                          ,-1                                      // Аналитика 2
                                          ,0                                       // Вид аналитики 3
                                          ,-1                                      // Аналитика 3
                                          ,0                                       // Вид аналитики 4
                                          ,-1                                      // Аналитика 4
                                          ,0                                       // Вид аналитики 5
                                          ,-1                                      // Аналитика 5
                                          ,0                                       // Вид аналитики 6
                                          ,-1                                      // Аналитика 6
                                          ,Comment                                 // Комментарий
                                          ,0                                       // ID операции
                                          ,0                                       // ID шага операции
                                          ,1                                       // Признак вызова не из операции расчета НОБ
                                          ,ObjID                                   // Возвращаемый идентификатор созданного объекта
                                          ,"X"                                     // Признак внешней системы
                                          ,OutSystCode                             // Код внешней системы
                                          ,OutObjID                                // Идентификатор во внешней системе
                                          ,SourceObjID                             // Идентификатор объекта-источника
                                         )) or (ObjID <= 0))

      ErrStr = GetErrMsg();
      return 1;
    end;

    if(TaxBaseKind == 1)
      var Year;
      var PrevSumBaseG1 = $0, PrevSumBaseG1_13 = $0, PrevSumBaseG1_15 = $0, SumBaseG1 = $0;
      var BaseG1_13 = $0, BaseG1_15 = $0;
      
      //Для дивидендов нужно ещё создать объекты НОБ по ставке 13% и 15%
      
      datesplit(ObjDate, NULL, NULL, Year);

      query =   " select NVL(SUM(t_Sum0), 0) as SumBaseG1, t_Kind "
              + "   from dnptxobj_dbt "
              + "  where t_Client = ? "
              + "    and t_Kind IN ("+TXOBJ_BASEG1+", "+TXOBJ_BASEG1_13+", "+TXOBJ_BASEG1_15+")"
              + "    and t_Date >= ? "
              + "    and t_Date <= ? "
              + " group by t_Kind ";

      cmd = DL_RSDCommand(query);

      cmd.AddParam(ClientID);
      cmd.AddParam(date(1,1,Year));
      cmd.AddParam(ObjDate);

      DataSet = cmd.Execute();
      while(DataSet.moveNext())
        if(DataSet.Kind == TXOBJ_BASEG1)
          SumBaseG1 = money(DataSet.SumBaseG1); //Уже включает в себя только что созданный объект BaseG1
        elif(DataSet.Kind == TXOBJ_BASEG1_13)
          PrevSumBaseG1_13 = money(DataSet.SumBaseG1);
        else
          PrevSumBaseG1_15 = money(DataSet.SumBaseG1);
        end;
      end;

      PrevSumBaseG1 = SumBaseG1 - BaseG; //НОБ без включенного только что созданного объекта

      if(SumBaseG1 <= NPTX_LIMINCOME_MAX15)
        BaseG1_13 = SumBaseG1 - PrevSumBaseG1_13;
        BaseG1_15 = -PrevSumBaseG1_15;
      else
        BaseG1_13 = MAX(NPTX_LIMINCOME_MAX15 - PrevSumBaseG1_13, 0);
        BaseG1_15 = SumBaseG1 - NPTX_LIMINCOME_MAX15 - PrevSumBaseG1_15;
      end;

      if(BaseG1_13 != 0)
        ObjID = 0;
        if((0 != DL_NPTX_InsertTaxObjectRetID( ObjDate                                 // Дата НДР
                                              ,ClientID                                // Клиент
                                              ,TXOBJ_DIR_IN                            // Направление
                                              ,7                                       // Уровень
                                              ,"X"                                     // Признак "Пользовательский"
                                              ,TXOBJ_BASEG1_13                         // Вид НДР
                                              ,BaseG1_13                               // Сумма дохода/расхода
                                              ,NATCUR                                  // Валюта дохода/расхода
                                              ,0                                       // Вид аналитики 1
                                              ,-1                                      // Аналитика 1
                                              ,0                                       // Вид аналитики 2
                                              ,-1                                      // Аналитика 2
                                              ,0                                       // Вид аналитики 3
                                              ,-1                                      // Аналитика 3
                                              ,0                                       // Вид аналитики 4
                                              ,-1                                      // Аналитика 4
                                              ,0                                       // Вид аналитики 5
                                              ,-1                                      // Аналитика 5
                                              ,0                                       // Вид аналитики 6
                                              ,-1                                      // Аналитика 6
                                              ,Comment                                 // Комментарий
                                              ,0                                       // ID операции
                                              ,0                                       // ID шага операции
                                              ,1                                       // Признак вызова не из операции расчета НОБ
                                              ,ObjID                                   // Возвращаемый идентификатор созданного объекта
                                              ,"X"                                     // Признак внешней системы
                                              ,OutSystCode                             // Код внешней системы
                                              ,OutObjID                                // Идентификатор во внешней системе
                                              ,SourceObjID                             // Идентификатор объекта-источника
                                             )) or (ObjID <= 0))

          ErrStr = GetErrMsg();
          return 1;
        end;
      end;

      if(BaseG1_15 != 0)
        ObjID = 0;
        if((0 != DL_NPTX_InsertTaxObjectRetID( ObjDate                                 // Дата НДР
                                              ,ClientID                                // Клиент
                                              ,TXOBJ_DIR_IN                            // Направление
                                              ,7                                       // Уровень
                                              ,"X"                                     // Признак "Пользовательский"
                                              ,TXOBJ_BASEG1_15                         // Вид НДР
                                              ,BaseG1_15                               // Сумма дохода/расхода
                                              ,NATCUR                                  // Валюта дохода/расхода
                                              ,0                                       // Вид аналитики 1
                                              ,-1                                      // Аналитика 1
                                              ,0                                       // Вид аналитики 2
                                              ,-1                                      // Аналитика 2
                                              ,0                                       // Вид аналитики 3
                                              ,-1                                      // Аналитика 3
                                              ,0                                       // Вид аналитики 4
                                              ,-1                                      // Аналитика 4
                                              ,0                                       // Вид аналитики 5
                                              ,-1                                      // Аналитика 5
                                              ,0                                       // Вид аналитики 6
                                              ,-1                                      // Аналитика 6
                                              ,Comment                                 // Комментарий
                                              ,0                                       // ID операции
                                              ,0                                       // ID шага операции
                                              ,1                                       // Признак вызова не из операции расчета НОБ
                                              ,ObjID                                   // Возвращаемый идентификатор созданного объекта
                                              ,"X"                                     // Признак внешней системы
                                              ,OutSystCode                             // Код внешней системы
                                              ,OutObjID                                // Идентификатор во внешней системе
                                              ,SourceObjID                             // Идентификатор объекта-источника
                                             )) or (ObjID <= 0))

          ErrStr = GetErrMsg();
          return 1;
        end;
      end;
    end;
  end;

  return 0;

OnError(er)
  ErrStr = er.module+ " "+er.line+" "+er.message;
  return 1;
end;


//////////////////////////////////////////////////////////////////////////////////////////
//Пример использования
/*
var ErrStr = "", err = 0;

//Аналитические признаки объекта НДР
//Для объектов выше 4-го уровня можно не заполнять.
var AnaliticData = TObjAnaliticData(0,-1,0,-1,0,-1,0,-1,0,-1,0,-1);

err = DeleteUserNPTXOBJ(0, 0, "TEST", 123, @ErrStr);
if(err)
  msgbox(ErrStr);
end;

if(not err)
  err = DeleteUserNPTXOBJ(0, 0, "TEST", 124, @ErrStr);
  if(err)
    msgbox(ErrStr);
  end;
end;

if(not err)
  err = DeleteUserNPTXOBJ(0, 0, "TEST", 125, @ErrStr);
  if(err)
    msgbox(ErrStr);
  end;
end; 

if(not err)
  err = InsertOrChangeUserNPTXOBJ(15160,                         //Идентификатор клиента
                                  TXOBJ_PLUSG_1010,              //Вид объекта НДР
                                  {curdate},                     //Дата объекта
                                  4900000,                       //Сумма
                                  NATCUR,                        //Валюта суммы
                                  "TEST",                        //Код внешней системы
                                  "1123",                         //Идентификатор во внешней системе
                                  "Тестовый объект",             //Комментарий для объекта
                                  AnaliticData,                  //Информация по аналитикам
                                  false,                         //Признак ИИС
                                  @ErrStr                        //Возвращаемая строка с текстом ошибки
                               );
  if(err)
    msgbox("Ошибка: "+ErrStr);
  end;
end;

if(not err)
  err = InsertOrChangeUserNPTXOBJ(15160,                         //Идентификатор клиента
                                  TXOBJ_PLUSG_1010,              //Вид объекта НДР
                                  {curdate},                     //Дата объекта
                                  200000,                        //Сумма
                                  NATCUR,                        //Валюта суммы
                                  "TEST",                        //Код внешней системы
                                  "1124",                         //Идентификатор во внешней системе
                                  "Тестовый объект",             //Комментарий для объекта
                                  AnaliticData,                  //Информация по аналитикам
                                  false,                         //Признак ИИС
                                  @ErrStr                        //Возвращаемая строка с текстом ошибки
                               );
  if(err)
    msgbox("Ошибка: "+ErrStr);
  end;
end;

if(not err)
  err = InsertOrChangeUserNPTXOBJ(15160,                         //Идентификатор клиента
                                  TXOBJ_MINUSG_601,              //Вид объекта НДР
                                  {curdate},                     //Дата объекта
                                  400000,                        //Сумма
                                  NATCUR,                        //Валюта суммы
                                  "TEST",                        //Код внешней системы
                                  "1125",                         //Идентификатор во внешней системе
                                  "Тестовый объект",             //Комментарий для объекта
                                  AnaliticData,                  //Информация по аналитикам
                                  false,                         //Признак ИИС
                                  @ErrStr                        //Возвращаемая строка с текстом ошибки
                               );
  if(err)
    msgbox("Ошибка: "+ErrStr);
  end;
end;
*/
