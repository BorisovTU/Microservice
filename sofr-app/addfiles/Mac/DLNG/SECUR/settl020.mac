/* Операция Расчеты на ОРЦБ      
   Шаг актуализации счетов       */

import InsCarryDoc, "sp_categ.mac";

private macro InitAccount( FD, FIID, CreateDate )

  if( 
      (not FD.OpenAccount("-Биржа", null, null, null, null, CreateDate, FIID ) ) OR 
      (not FD.OpenAccount("+Биржа", null, null, null, null, CreateDate, FIID ) ) 
    )
     return false; 
  end; 
  return true;
end;

macro ExecuteStep( doc, adr )
  
  record comm( dl_comm );
  record dlmarket(dlmarket);  
  var    FD; 

  SetBuff( comm, adr );
  FD = SPFirstDocDLCOMM( comm );

  /*рублевые счета актуализируем в любом случае*/
  if( not InitAccount( FD, NATCUR, comm.CommDate ) )
     return 1;
  end;


  /*валютные счета актуализируем, если задали валюту в параметрах операции*/
  if( comm.FIID > NATCUR )
    if( not InitAccount( FD, comm.FIID, comm.CommDate ) )
       return 1;
    end;
  end;

  if( (comm.MarketSchemeID > 0) and (FindDLMARKET( comm.MarketSchemeID, dlmarket) != 0) )
     msgbox( "Не найдена схема расчетов с ID = "+string(comm.MarketSchemeID) );
     return 1;
  end;

  if( dlmarket.Code == Рынок_Тплюс )
     if( not InsertOprStatus( SP_OPERSTATUS_SETTL_K, SP_OPERSTATUS_SETTL_K_DO) )
        msgbox( "Ошибка при установке статуса <Перевод на клиринговый с корсчета> операции" );
        return 1;
     end;
  else
     if( not InsertOprStatus( SP_OPERSTATUS_SETTL_T, SP_OPERSTATUS_SETTL_T_DO) )
        msgbox( "Ошибка при установке статуса <Перевод на торг. и клиринг. счета> операции" );
        return 1;
     end;
  end;

  return 0;
end;

