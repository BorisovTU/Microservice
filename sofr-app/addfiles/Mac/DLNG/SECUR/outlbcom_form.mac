/*******************************************************************************
 DESCRIPTION  :   Отчет "Расходы в виде комиссий по оказанным банку услугам" (НУ) - форма ввода данных
 PROGRAMMED BY:   Никольская В.В.
 CREATION DATE:   22.01.08
*******************************************************************************/
import "DlRepForm_h.mac", "globals.mac"/*, "incbncom_form.mac"*/;

/*Номера полей в форме отчета*/
CONST PNFLD_COMMREG_PERIOD       = 0, //Период
      PNFLD_COMMREG_YEAR         = 1, 
      PNFLD_COMMREG_CONTRSUBKIND = 2, //Подвид договора обслуживания
      PNFLD_COMMREG_CONTRNAME    = 3, //Договор
      PNFLD_COMMREG_COMFREETYPE  = 4, //Номер Комиссии
      PNFLD_COMMREG_COMNUMBER    = 5, //Наименовани комиссии
      PNFLD_COMMREG_COMPRINTMETHOD  = 6; 

CONST H_PNFLD_PERIOD       = 20,
      H_PNFLD_CONTRSUBKIND = 21,
      H_PNFLD_CONTRNAME    = 24, //Договор
      H_PNFLD_COMFREETYPE  = 22, //Номер Комиссии
      H_PNFLD_COMNUMBER    = 23; //Наименовани комиссии

VAR ComFeeType;

/*листалка подвидов обслуживания*/
MACRO ListContrSubKind( FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER ):BOOL
  return DL_ComboBox( "select t_Name, t_Servicekindsub from dservksub_dbt where t_servicekind = " + string(PTSK_STOCKDL),
                      "t_Name", "t_Servicekindsub", 
                      @FldShowValue, @FldRealValue, X, Y
                    );

END;

/*листалка комиссий*/
MACRO ListSfComiss( Name:@VARIANT, FldShowValue:@VARIANT, FldRealValue:@VARIANT, X:INTEGER, Y:INTEGER ):BOOL
  VAR Choice = DL_Scroll("select t_Code, t_Name, t_Number, t_DateBegin, t_DateEnd, t_FeeType from dsfcomiss_dbt "+
                         " where t_receiverid != "+string({OurBank})+" and t_servicekind = " + string(PTSK_STOCKDL),
                         "Комиссия", X, Y,
                         "t_Code","Код","t_Name","Наименование","t_Number","Номер",
                         "t_DateBegin","Дата нач.","t_DateEnd","Дата оконч."
                        );
  if( Choice != NULL )
     FldRealValue = Choice.Value("t_Number"); 
     FldShowValue = Choice.Value("t_Code");
     Name         = Choice.Value("t_Name");
     ComFeeType   = Choice.Value("t_FeeType");
  end;
  return (Choice != NULL);
END;                              
                             
/*обработчик видов обслуживания*/
MACRO FldProc_ContrSubKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     ListContrSubKind( @FldShowValue, @FldRealValue, 28, 12 );
  end;
  return CM_DEFAULT;
END;

MACRO FldProc_ContrNumber( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
 VAR SubKind;
 if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
      pThis.GetLinkedValue( H_PNFLD_CONTRSUBKIND, null, @SubKind );
      DL_ListSfContr( @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY(), SubKind, -1 );
  end;
  return CM_DEFAULT;
END;

/*обработчик поля комиссия*/
MACRO FldProc_ComFreeType( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Name;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_PNFLD_COMNUMBER, "");
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_PNFLD_COMNUMBER, "");
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
      ListSfComiss( @Name, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
      if( (FldRealValue != null) and (FldRealValue > 0) ) 
         pThis.SetLinkedValue( H_PNFLD_COMNUMBER, Name);
      end;
  end;
  return CM_DEFAULT;
END;

MACRO FldProc_Period( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  VAR BeginDate, EndDate, Month, Year, BegD, EndD;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        DateSplit( {curdate}, null, Month, null );
        FldRealValue = ((Month - 1)/3 + 1)*3;
     end;
     FldShowValue = DL_GetNameAlg( ALG_CB_PERIOD_IN_QUART, FldRealValue );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( ALG_CB_PERIOD_IN_QUART, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_COMMReg21_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, PrevQuartal, Year;
     DateSplit( {curdate}, null, CurMonth, Year );

     PrevQuartal = ((CurMonth - 1)/3 + 1)*3; 
     if( PrevQuartal == 0 )
        PrevQuartal = 12;
        Year        = Year - 1;
     end;

     AddFieldProc( 0, PNFLD_COMMREG_PERIOD,      H_PNFLD_PERIOD,      @FldProc_Period, PrevQuartal, 21, 10 ); 
     AddFieldProc( 0, PNFLD_COMMREG_YEAR,        DL_PNFLD_YEAR,       @DL_FldProc_Year, Year );
     AddFieldProc( 0, PNFLD_COMMREG_CONTRSUBKIND,H_PNFLD_CONTRSUBKIND,@FldProc_ContrSubKind, "");
     AddFieldProc( 0, PNFLD_COMMREG_CONTRNAME,   H_PNFLD_CONTRNAME,   @FldProc_ContrNumber, "" );
     AddFieldProc( 0, PNFLD_COMMREG_COMFREETYPE, H_PNFLD_COMFREETYPE, @FldProc_ComFreeType, "" );
     AddFieldProc( 0, PNFLD_COMMREG_COMNUMBER,   H_PNFLD_COMNUMBER,   @Default, "" );
   /*  AddFieldProc( 0, PNFLD_COMMREG_COMPRINTMETHOD, DL_PNFLD_PRINTMETHOD, @DL_FldProc_PrintMethod, DL_OUTREPORT_EXCEL, 26, 16 );*/
     AddFieldProc( 0, PNFLD_COMMREG_COMPRINTMETHOD,     DL_PNFLD_CHECKBOX,    @DL_FldProc_CheckBox, "X" );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_res.lbr", "OUTLBCOM" ); 
END;