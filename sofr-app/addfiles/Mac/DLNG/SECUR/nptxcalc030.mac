/*
$Name:        nptxcalc030.mac
$Module:      Ценные бумаги
$Description: Расчет НОБ для НДФЛ. Шаг "Расчет НДР 1 уровня"
*/

/* Операция расчета НОБ для НДФЛ
   Шаг "Расчет НДР 1 уровня"*/
IMPORT InsCarryDoc, "secinter.mac", "nptxcalcfun.mac";

PRIVATE VAR Oper = TRecHandler( "nptxop.dbt" );

PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

MACRO ExecuteStep( DlDoc, FDoc, DocKind, ID_Operation, ID_Step )

  VAR OperationStatus;
  VAR Stat = 0;
  VAR Select,  Query,  DataSet, BegDate, EndDate;
  VAR Select2, Query2, DataSet2;
  VAR TaxPeriod = 0;

  Oper.SetRecordAddr( FDoc );

  datesplit(Oper.rec.OperDate, null, null, TaxPeriod);

  var TaxTeriodToLucre = CheckTaxTeriodToLucre(TaxPeriod);

  ClearGTT_DNPTXOBJ_TMP();
  stat = DL_NPTX_PutMsg( "Расчет НДР 1 уровня", 40 );
  if( not stat )

    Select = "SELECT comm.*, fin.t_Fi_Code, fin.t_Name " + 
             "  FROM DDL_COMM_DBT comm, dfininstr_dbt fin " + 
             " WHERE comm.t_FIID = fin.t_FIID " +
             "   AND ((    comm.t_DocKind = " + DL_ISSUE_UNION +
             "         AND ((    comm.t_BeginDate >= ? " + 
             "               AND comm.t_BeginDate <= ? " + 
             "              ) OR " + 
             "              (    comm.t_EndDate >= ? " + 
             "               AND comm.t_EndDate <= ? " + 
             "              ) " + 
             "             ) " + 
             "        ) OR " + 
             "        (    comm.t_DocKind = " + DL_CHGAVRNOM + 
             "         AND comm.t_CommDate >= ? " + 
             "         AND comm.t_CommDate <= ? " + 
             "        ) " + 
             "       )  ";

    Query = DL_RSDCommand(Select);
    Query.addParam(Oper.rec.PrevDate );
    Query.addParam(Oper.rec.OperDate );
    Query.addParam(Oper.rec.PrevDate );
    Query.addParam(Oper.rec.OperDate );
    Query.addParam(Oper.rec.PrevDate );
    Query.addParam(Oper.rec.OperDate );

    DataSet = Query.execute();
    while( DataSet.MoveNext() )
       if (DataSet.DocKind == 135)
          BegDate = Date(DataSet.BeginDate);
          EndDate = Date(DataSet.EndDate);
       else
          BegDate = Date(DataSet.CommDate);
          EndDate = Date(DataSet.CommDate);
       end;

       Select2 = "SELECT DISTINCT (case when tick.t_DealCodeTS = CHR(1) or tick.t_DealCodeTS IS NULL then tick.t_DealCode else tick.t_DealCodeTS end) as DealCodeTS " + 
                 "  FROM ddlrq_dbt pm, ddl_tick_dbt tick  " + 
                 " WHERE pm.t_DocKind IN ("+DL_SECURITYDOC+","+DL_RETIREMENT+","+DL_AVRWRT+")"+
                 "   AND pm.t_PlanDate >= ? " +
                 "   AND pm.t_PlanDate <= ? " +
                 "   AND pm.t_State    != " + string(DLRQ_STATE_EXEC) +
                 "   AND pm.t_FactDate  = " + GetSQLDate(date(0,0,0)) +
                 "   AND tick.t_DealID  = pm.t_DocID" +
                 "   AND (   (tick.t_ClientID = ? AND rsi_npto.CheckContrIIS(tick.t_ClientContrID) = "+IIF(Oper.rec.IIS == SET_CHAR, "1", "0")+") " +
                 "        OR (tick.t_IsPartyClient = 'X' and tick.t_PartyID = ? AND rsi_npto.CheckContrIIS(tick.t_PartyContrID) = "+IIF(Oper.rec.IIS == SET_CHAR, "1", "0")+") "+
                 "       ) " +
                 "   AND tick.t_PFI = ? ";

       Query2 = DL_RSDCommand(Select2);
       Query2.addParam(BegDate);
       Query2.addParam(EndDate);
       Query2.addParam(Oper.rec.Client);
       Query2.addParam(Oper.rec.Client);
       Query2.addParam(DataSet.FIID);

       DataSet2 = Query2.Execute();
       while( DataSet2.MoveNext() )
          DL_NPTX_PutMsg( "По бумаге " + string(DataSet.FI_Code) + " " + string(DataSet.Name) + " есть незакрытая сделка № " + 
                          string(DataSet2.DealCodeTS) + " на дату " + string(Date(DataSet.CommDate)) + 
                          ", когда проводилось корпоративное действие", 20);
       end;
    end;

    if(TaxTeriodToLucre == false)
      stat = CreateTaxObjects1(Oper, ID_Step);

      if(not stat)
        stat = CreateMaterialTaxObjects1(Oper, ID_Step);
      end;
    else
      if(Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_LUCRE)
        stat = CreateTaxObjects1(Oper, ID_Step);
      else
        stat = CreateMaterialTaxObjects1(Oper, ID_Step);
      end;
    end;

    if(not stat)
      stat = DL_NPTX_StartInsertTaxObject(Oper.rec.ID, ID_Step);
    end;

    DL_NPTX_SaveOperLog( Oper.rec.ID, 30 );

    if(not stat)
      if((Oper.rec.IIS == SET_CHAR) and (Oper.rec.SubKind_Operation != DL_TXBASECALC_OPTYPE_CLOSE_IIS))
        OperationStatus = 9; //Расчет НДР2
        if( not InsertOprStatus( 46052 /*Расчет НОБ*/, OperationStatus) ) 
           return Error( "Ошибка при установке статуса операции" );
        end;
      else
        if(Oper.rec.Recalc == SET_CHAR)//если пересчет, то по периодам пересчитываем НДР с 3 по 7 уровень.
          if((Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_LUCRE) and (TaxTeriodToLucre == true))
            OperationStatus = 13; //Расчет НДР457
          else
            OperationStatus = 9; //Расчет НДР37
          end;
        else
          if((Oper.rec.SubKind_Operation == DL_TXBASECALC_OPTYPE_LUCRE) and (TaxTeriodToLucre == true))
            OperationStatus = 5; //Расчет НДР4
          else
            OperationStatus = 4; //Расчет НДР2
          end;
        end;
        
        if( not InsertOprStatus( 46052 /*Расчет НОБ*/, OperationStatus) ) 
           return Error( "Ошибка при установке статуса операции" );
        end;
      end;
    end;
  end;

  return stat;
END;
