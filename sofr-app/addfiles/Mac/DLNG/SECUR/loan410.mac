/* Операция займа.
   Шаг "Перерегистрация при возврате". Вставляется из панели расчетов, первый
   шаг цепочки "Возврат ц/б*/
import "sp_class.mac", "sp_carcm.mac", Проценты;

private record SpChangeDates("spchdate.str");/*глобальная структура с данными для расчетов*/

macro ExecuteStep( doc, FDoc)

   record deal(dl_tick);
   var    FD, pmobj, DateLastCalcPC, SubPurposeNKD, PcAccID,
          pmNKD      = TrecHandler( "pmpaym.dbt" ), /* платеж НКД*/
          pm_avoir_1 = TRecHandler( "pmpaym.dbt" ), /* платеж ценными бумагами по части получения ц/б*/
          dat = SpChangeDates.FactDate; /*дата подписания договора*/ 

   if( SpChangeDates.Action <= 0 )
      msgbox("Шаг перерегистрации ц/б при возврате из списка шагов выполнять запрещено."); 
      return 1;
   end; 

   SetBuff( deal, FDoc ); 
   FD = SPFirstDoc( deal, true );

   /*Дата должна быть не меньше фактической даты получения.*/
   if( FindPayment( null, BAi, 0, deal.BOfficeKind, deal.DealID, true, pm_avoir_1 ) != 0 )
      MsgBox( "Не найден платеж по ценным бумагам для части получения ц/б.");
      return 1;
   end;  
   if( dat < pm_avoir_1.rec.ValueDate )
      MsgBox( "Дата возврата ц/б должна быть не меньше фактической даты получения ц/б." );
      return 1;
   end;

   if( not ПлатежЗакрыт( pm_avoir_1.rec ) ) 
      msgbox("Не выполнено получение ц/б."); 
      return 1;
   end;

   if( ПлатежЗакрыт( FD.pm_avoir.rec ) ) 
      msgbox("Возврат ц/б уже выполнен."); 
      return 1;
   end;

   /*Если платеж возврата не просрочен, то не больше плановой даты возврата.
     Если платеж возврата просрочен, то строго больше плановой даты возврата.*/
   if( FD.pm_avoir.rec.PaymStatus == PM_OVERDUE )
      if( dat <= FD.dl_leg.rec.Expiry )
         MsgBox( "Платеж возврата ц/б просрочен.|Дата должна быть больше плановой даты возврата ц/б." );
         return 1;
      end;
   else 
      if( dat > FD.dl_leg.rec.Expiry )
         MsgBox( "Дата должна быть меньше или равна плановой дате возврата ц/б." );
         return 1;
      end;
   end;

   /* Проверить, что новая дата возврата не меньше дат фактических:
    - последнего возврата НКД (по платежам)
    - последнего начисления процентов */
   if( FD.ExistPC == true ) 

      PcAccID = SP_GetPercentAccount( deal );
      if( PcAccID <= 0 )
         MsgBox( "Не найден счет процентов для сделки." );
         return 1;
      end;

      DateLastCalcPC = ПолучитьДатуПоследнегоВыполненногоНачисления( PcAccID );
      if( dat < DateLastCalcPC )
         MsgBox( "Дата возврата ц/б " + string(dat) + " должна быть больше либо равна дате последнего начисления процентов " + string(DateLastCalcPC) + ".");
         return 1;
      end;
   end;

   SubPurposeNKD = 0;
   while( FindPayment( null, INi, SubPurposeNKD, FD.tick.rec.BOfficeKind, FD.tick.rec.DealID, true, pmNKD ) == 0 )
      if( dat < pmNKD.rec.ValueDate )
         MsgBox( "Дата возврата ц/б должна быть больше либо равна дате последнего возврата НКД.");
         return 1;
      end;
      SubPurposeNKD = SubPurposeNKD + 1;
   end;

   if( ИзменитьДатуПоставки( FD, dat ) != 0 )
      return 1;
   end;

   if( dat > FD.DateArray[DATE_DEALEEND] ) /*Переинитить дату завершения сделки*/
      if( not OprReplanStepPlanDates( DATE_DEALEEND, dat ) )
         msgbox("Ошибка при попытке переинициализировать дату завершения операции." );         
         return 1;
      end;
   end;

   if( not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALSETAVOIRISS), dat ) )
      msgbox("Ошибка при попытке переинициализировать дату получения ц/б." );         
      return 1;
   end;

   if( FD.pm_avoir.rec.PaymStatus == PM_OVERDUE ) /*снять с просрочки*/
      pmobj = RsbPayment( FD.pm_avoir.rec.PaymentID );
      pmobj.PaymStatus           = PM_REMOVE_OVERDUE;
      FD.pm_avoir.rec.PaymStatus = PM_REMOVE_OVERDUE;
   end;
   return 0;
end;
