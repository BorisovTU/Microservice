/*
$Name:             sp_carin.mac
$Module:           Ценные бумаги 
$Description:      Настройки внутреннего учета COMMON\\ВНУТРЕННИЙ УЧЕТ
*/
IMPORT "secinter.mac";

private macro ВнутреннийУчетОткрыт(): BOOL   
   var query =  " SELECT t_IsClosed FROM dDLGRACCKIND_dbt  WHERE t_Num = 3";  /*системный номер внутреннего учета*/             
   var Select = Dl_RSDCommand(query);
   var DataSet = Select.Execute();
   var InnerAccIsOpen = true;

   if(DataSet.moveNext())
    if(DataSet.IsClosed == SET_CHAR)
      InnerAccIsOpen = false;
    end;   
   end;   

   return InnerAccIsOpen;
end;

PRIVATE VAR SC_UseInnerCalc = NULL;
MACRO ВестиВнутреннийУчет():BOOL
  VAR ErrCode = 0;

  if( SC_UseInnerCalc == NULL )
    if( GetIdentProgram() == CodeFor("S") ) /*операция выполняется из БОЦБ*/
      //проверим настройку из графика
     SC_UseInnerCalc = ВнутреннийУчетОткрыт();
    else
     GetRegistryValue( "COMMON\\ВНУТРЕННИЙ УЧЕТ\\ФОРМИРОВАНИЕ ПРОВОДОК", V_BOOL, SC_UseInnerCalc, ErrCode );
     if( ErrCode != 0 )
        SC_UseInnerCalc = false;
     end;
  end;
  end;
  return SC_UseInnerCalc;
END;

PRIVATE VAR SC_UseCalculateByDeal = NULL;
MACRO ВУ_ВестиПосделочныйУчетКомиссий():BOOL
  VAR ErrCode = 0;

  if( SC_UseCalculateByDeal == NULL ) 
     GetRegistryValue( "COMMON\\ВНУТРЕННИЙ УЧЕТ\\ОПЛАТА ПЕРИОДИЧЕСКИХ КОМИССИЙ", V_BOOL, SC_UseCalculateByDeal, ErrCode );
     if( ErrCode != 0 )
        SC_UseCalculateByDeal = false;
     end;
  end;
  return SC_UseCalculateByDeal;
END;


/* Получить dl_leg по другой части сделки. Сделка должна быть из двух
   частей. Если не смогли получить данные выводим сообщение об ошибке
   и возвращаем пустой буфер. */
PRIVATE MACRO GetAnotherDLLEG(FD)
  var
     AnotherLeg  = TRecHandler( "dl_leg.dbt" ),
     LegKind     = LEG_KIND_DL_TICK_BACK;

  if( FD.IsBack )
     LegKind = LEG_KIND_DL_TICK;
  end;

  if( FindDL_LEG( LegKind, FD.tick.rec.DealID, 0, AnotherLeg ) )
     MsgBox(  "Не могу получить данные по другой части сделки (",
              "LegKind=", LegKind, ", DealID=", FD.tick.rec.DealID, ")" );
     AnotherLeg.Clear();
  end;
  return AnotherLeg.rec;
END;

PRIVATE MACRO ЕстьРегСборПоДругойЧасти(FD, ReceiverID): bool
  var AnotherLeg = GetAnotherDLLEG(FD);

  return ( (AnotherLeg.PayRegTax == SET_CHAR) and
           ((ReceiverID == AnotherLeg.Registrar) and (ReceiverID > 0)) );
END;

macro ВидРОКомиссииПосреднику( FD, ReceiverID )

   var ЕстьРегСбор: bool;
   /*Регистрационный сбор по сделке или по одной из частей*/
   ЕстьРегСбор = ( (FD.dl_leg.rec.PayRegTax == SET_CHAR) and 
                   ((ReceiverID == FD.dl_leg.rec.Registrar) and (ReceiverID > 0)));

  if( IsEXCHANGE(FD.Group) and (FD.tick.rec.Flag1 == SET_CHAR) ) 
     return 9;
  elif(IsBROKER(FD.Group) ) 
     return 10;
  elif( ЕстьРегСбор or 
       (FD.ExistBack and ЕстьРегСборПоДругойЧасти(FD, ReceiverID)))
     return 11;
  else
     return 12;
  end;
end;

PRIVATE VAR SC_UseCalculateByDealInORCB = NULL;
MACRO ВУ_ПроводкиПоЦБнаОРЦБ():BOOL
  VAR ErrCode = 0;

  if( SC_UseCalculateByDealInORCB == NULL )
     GetRegistryValue( "COMMON\\ВНУТРЕННИЙ УЧЕТ\\СДЕЛКИ С ЦБ В ОПЕР РАСЧ НА ОРЦБ", V_BOOL, SC_UseCalculateByDealInORCB, ErrCode );
     if( ErrCode != 0 )
        SC_UseCalculateByDealInORCB = false;
     end;
  end;
  return SC_UseCalculateByDealInORCB;
END;

PRIVATE VAR SC_UseCarryOnStepInORCB = 1;
MACRO ВУ_БОЦБПроводкиВводаВыводаДСНаОРЦБ():INTEGER
  VAR ErrCode = 0;
  GetRegistryValue( "COMMON\\ВНУТРЕННИЙ УЧЕТ\\ПЕРЕВОД СРЕДСТВ В ОПЕР РАСЧ", V_INTEGER, SC_UseCarryOnStepInORCB, ErrCode );
  if( ErrCode != 0 )
     SC_UseCarryOnStepInORCB = 1;
  end;
  return SC_UseCarryOnStepInORCB;
END;
