/*
$Name:        InvSh300.mac
$Module:      Ценные бумаги
$Description: Операция получения паев. Шаг оплаты комиссий посреднику
*/

/* Операция получения паев
   Шаг оплаты комиссий посреднику*/
import InsCarryDoc, "sp_class.mac", "sp_carcm.mac";

private record   SpChangeDates("spchdate.str");/*глобальная структура с данными по изменению сроков*/

macro ExecuteStep( doc, FDoc, DocKind, ID_Op )
  record  deal(dl_tick);  
  var Dk, Dp, FD, CommStatus = 0,
      Action = SpChangeDates.Action, FactDate = SpChangeDates.FactDate;

  if( Action <= 0 )
     msgbox("Шаг оплаты комиссий из списка шагов выполнять запрещено. Необходимо выполнить расчеты по сделке."); 
     return 1;
  end; 

  SetBuff( deal, FDoc ); 
  /*Не должно быть выполненных шагов с более поздней планируемой датой*/
  if( (FactDate != Date(0,0,0)) AND (FactDate < SP_GetMaxPlanStepDate(ID_Op, FactDate)) )
     msgbox("Есть выполненные шаги с планируемой датой больше|чем дата оплаты.|Запрещено выполнять операцию."); 
     return 1;
  end;

  FD = SPFirstDoc( deal );

  /*здесь только комиссии посреднику (регистратору - на другом шаге)*/

  if( (FD.tick.rec.BrokerID > 0) and (not ИсполнитьТОпоКомиссиям_ИП(FD, FactDate, doc, null, FD.tick.rec.BrokerID)) )
     return 1;
  end;

  if( not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALCOMISS), FactDate) )
     msgbox("Ошибка при попытке переинициализировать дату комиссии ц/б." );         
     return 1;
  end;

  if( not OprReplanStepPlanDates( FD.GetKindDate(DATE_DEALEEND), maxDate(FactDate, FD.DateArray[DATE_DEALSETAVOIRISS]) ) )
     msgbox("Ошибка при попытке переинициализировать дату завершения сделки." );         
     return 1;
  end;
 
  return 0;

end;
