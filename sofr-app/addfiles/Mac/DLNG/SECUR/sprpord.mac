/*
$Name:        sprpord.mac
$Module:      Ценные бумаги
$Description: Печать договоров
RS-Bank 5.1                                          R-Style Software Lab
Programmer  : Иванютин Р.Н.
*/
const ERROR  = 1;
/*const Path   = "../mac/dlng/secur/templs/";*/

Import  dlwreps, sptagfl;

private const DOCKIND_BUYSALEREPORT     = 26,     /*договор купли-продажи*/
              DOCKIND_CONTR_FIRSTPART   = 22,     /* договор по первой части*/
              DOCKIND_CONTR_SECONDPART  = 23;     /* договор по второй части*/


macro GetDealPrice(FD)
   var Price = $0;
   Price = money(FD.dl_leg.rec.TotalCost/double(FD.dl_leg.rec.Principal));
   return Price;
end;

private macro ПолучитьПараметрыДоговораПоСделке(FD, Kind, SpgrFile)
  file spground(spground);
  file spgrdoc(spgrdoc) key 1;
  var continue_cicle;

  ClearRecord(spgrdoc);
  spgrdoc.SourceDocKind = FD.tick.rec.BofficeKind;
  spgrdoc.SourceDocID   = FD.tick.rec.DealID;
  continue_cicle = GetGE(spgrdoc);
  while(continue_cicle AND (spgrdoc.SourceDocKind == FD.tick.rec.BofficeKind) 
                 AND (spgrdoc.SourceDocID == FD.tick.rec.DealID)
       )
    ClearRecord(spground);
    spground.SPGroundID = spgrdoc.SPGroundID;
    if(GetEQ(spground) == true)
      if(spground.Kind == Kind)
        Copy(SpgrFile, spground);
        return true;
      end;
    end;
    continue_cicle = Next(spgrdoc);
  end;
  return false;
end; 


macro MakeFirst (Deal, IsBack)

   record Spgr(spground);
   file Templ(dlgrtemp) key 0;
   record Currency(fininstr);
   
   var FD, Price, Group, Kind;
   /************************ Для текущей части сделки **************************/
   FD = SPFirstDoc(Deal, IsBack);
   
   if(IsBACKSALE(FD.Group) == true)
      if(IsBack == false)
        Kind = DOCKIND_CONTR_FIRSTPART;
      else
        Kind = DOCKIND_CONTR_SECONDPART;
      end;
   else
      Kind = DOCKIND_BUYSALEREPORT;
   end;

   if(ПолучитьПараметрыДоговораПоСделке(FD, Kind, Spgr))

      if(Spgr.Xld == "")
        Msgbox("Не задан номер договора");
        return ERROR;
      end;

      /*Определяем шаблон договора*/
      if(Spgr.DocTemplate < 1)
        MsgBox("Не задан шаблон договора ");
        return ERROR;
      end;

      ClearRecord(Templ);
      Templ.DocLog  = LLCLASS_KINDDOC_CONTRBUYSALE;
      Templ.Num = Spgr.DocTemplate;
      if( GetEQ(Templ) == false )
         MsgBox("Не найден шаблон договора");
         return ERROR;
      end;

      /*Файл шаблона*/
      println(Templ.File_Name);
      [CNT####.rtf](SP_StrTrimLeft(SP_StrBefore(Spgr.Xld,"/"),4));   

      /*Заполняем файл*/
      [~Number];
      println(Spgr.Xld);
      ПолучитьФинИн( FD.GetParametr(MC_TYPE_PARAMETR_PAYCURRENCY), Currency );
      /*цена ц/б в валюте*/
      println("~Price");        
        Price = GetDealPrice(FD);
      println(CheckData(Price));
      /*цена ц/б в валюте прописью*/
      println("~PriceWr");
      println(CheckData(CurToStrAlt (Price, NULL, NULL, Currency.ISO_Number)));
      
      /*Получим параметры договора по обратной части, если есть*/
      if(ПолучитьПараметрыДоговораПоСделке(FD, DOCKIND_CONTR_SECONDPART, Spgr))
         /*Номер обратного договора*/
         [~Number_Back];
         println(CheckData(Spgr.Xld));
         /*Дата заключения обратного договора*/
         [~Date_Back];
         println(CheckData(Spgr.RegistrDate));

         ПолучитьФинИн( FD.GetParametr(MC_TYPE_PARAMETR_PAYCURRENCY), Currency );

         /*цена ц/б в валюте*/
         println("~PriceBack");        
           Price = GetDealPrice(FD);
         println(CheckData(Price));
         /*цена ц/б в валюте прописью*/
         println("~PriceBackWr");
         println(CheckData(CurToStrAlt (Price, NULL, NULL, Currency.ISO_Number)));
      else
         [~Number_Back];
         println("");
         [~Date_Back];
         println("");
      end;
   else
      MsgBox("Не определены параметры договора по сделке");
      return ERROR;
   end;
   return 0;
end;

macro PrintReport (DealID:integer, IsBack:bool)
  file Deal(dl_tick) key 0;
  var TempFile;

  /*Получим сделку по ID*/
  ClearRecord(Deal);
  Deal.DealID = DealID;
  GetEQ(Deal);

  if(MakeFirst (Deal, IsBack) == ERROR) 
     return ERROR;
  end;

  if(Representatives_Of_Sides(Deal, IsBack) == ERROR) /* Представители сторон в сделке */
     return ERROR;
  end;

  if(Object_of_Deal(Deal, IsBack) == ERROR)        /* Предмет сделки*/
    return ERROR;
  end;

  if(DepoAndCorr(Deal, IsBack) == ERROR)      
    return ERROR;
  end;

  if(Properties_Sides(Deal, IsBack) == ERROR)      /*Параметры сторон сделки*/
    return ERROR;
  end;

  TempFile = SetOutput (CreateFileName);

  DLWREPS_PrintReportFromTagFile (TempFile);
  SetOutput(NULL, false);

  return 0;
end;

/*Проверим параметры договора*/
macro ПроверитьДоговор( FD, IsBack )
  record Spgr(spground);
  var Kind;

  if(IsBACKSALE(FD.Group) == true)
     if(IsBack == false)
       Kind = DOCKIND_CONTR_FIRSTPART;
     else
       Kind = DOCKIND_CONTR_SECONDPART;
     end;
  else
     Kind = DOCKIND_BUYSALEREPORT;
  end;

  if(ПолучитьПараметрыДоговораПоСделке(FD, Kind, Spgr))
    if(Spgr.Xld == "")
      Msgbox("Не задан номер договора");
      return false;
    end;

    if( Spgr.DocTemplate <= 0 )
      Msgbox("Не задан шаблон договора");
      return false;
    end;

    if(Spgr.RegistrDate == Date(0,0,0))
      Msgbox("Не задана дата составления договора");
      return false;
    end;
  else
    Msgbox("Не найдены параметры договора");
    return false;
  end;
  return true;
end;