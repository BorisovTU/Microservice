/*
$Name:        nptxhold021.mac
$Module:      Ценные бумаги
$Description: Операция удержания НДФЛ. Шаг "Передача в Хранилище событий зачета ДЕПО"
*/
import InsCarryDoc, "sp_categ.mac", "sp_car.mac", RsbDataSet, "nptxstbfun.mac";

private macro Error( err, ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  if(err)
    MsgBox( ErrStr );
  end;
  return err;
end;

macro ExecuteStep( kind_oper, FDoc, DocKind, ID_Operation, ID_Step )
  record nptxop("nptxop");
  var NeedLoopStep = false;
  var ErrStr = "";
  var err = 0;

  SetBuff( nptxop, FDoc );
  
  err = STB_ExecuteSendToStorOnStep(nptxop.DocKind, nptxop.ID, ID_Operation, ID_Step, @NeedLoopStep, @ErrStr, NULL, true);

  if(ErrStr != "")
    err = Error(err, ErrStr);
  end;

  if(not err)
    err = DL_NPTX_SaveTbMes();

    if(not err)
      var prevOprStatus = GetOprStatus(46081);

      if((NeedLoopStep == true))
        if( not InsertOprStatus(46081, prevOprStatus)) //Документооборот = Передача записи в хранилище
          return Error( "Ошибка при установке статуса вида \"Документооборот\" операции" );        
        end;
      else
        if( not InsertOprStatus(46081, IIF(prevOprStatus == 16/*Передача в Хранилище и форм. завления*/, 14/*Создание заявления на возврат*/, 13/*Корректировка*/))) 
          return Error( "Ошибка при установке статуса вида \"Документооборот\" операции" );        
        end;
      end;
    end;
  end;

  DL_NPTX_SaveOperLog( nptxop.ID, 21 );

  return err;
end;

/* Макрос постобработки */
MACRO PostStep (CommitOrRollback, /*1-выполнение шага,2 -отакат шага*/
                errTrn,        /* Статус выполнения шага-!0-произошла ошибка */
                FDoc,          /* Указатель на первичный документ */
                ID_Operation,  /* Номер экземпляра операции */
                Num_Step,      /* Номер шага операции(из настроек) */
                Kind_Operation,/* Вид операции */
                KindDoc,       /* Вид первичного документа */
                KindStep,      /* Вид шага операции */
                ID_Step)       /* Номер шага операции */

  record nptxop("nptxop");
  var ErrStr = "";
  var err = 0;

  SetBuff( nptxop, FDoc );

  if(CommitOrRollback == 2)
    err = STB_RollbackSendToStorOnStep(nptxop.DocKind, nptxop.ID, ID_Operation, ID_Step, @ErrStr);
    if(ErrStr != "")
      msgbox(ErrStr);
    end;
  end;

  return err;
END;

