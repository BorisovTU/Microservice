/**
  @file nptxsnobver020.mac
  @brief Шаг получения ответа от хранилища СНОБ операции технической сверки СНОБ

  #menu
  - БО ЦБ\Налоговый учет\НДФЛ\СНОБ\Операции технической сверки СНОБ

  #tag
  - functional_block: СНОБ
  - code_type: GUI
  - code_type: Operation
  - Сверка
  - Техническая сверка

  #link
  [BIQ-7294. Анализ. Техническая сверка] (https://sdlc.go.rshbank.ru/confluence/pages/viewpage.action?pageId=223940937)

  #changeLog
  |date      |author       |tasks    |note                                                             |
  |----------|-------------|---------|-----------------------------------------------------------------|
  |13.12.2023|Хромеев Д. С.|BOSS-1489|BOSS-48.9 СОФР. Реализация Технической сверки данных. Разработка.|
*/

/**
  @brief Сообщение об ошибке
  @param [in] ErrStr сообщение об ошибке
  @return 1 Чтобы показать, что возникла ошибка
*/
PRIVATE MACRO Error( ErrStr:STRING ):INTEGER
  DL_NPTX_PutMsg( ErrStr, 10 );
  MsgBox( ErrStr );
  return 1;
END;

/**
  @brief Получить ServiceID сервиса eprshb_to_sofr_verify_snob_result из таблицы SOFR_IPS_SETTINGS
  @return ServiceID, или -1, если не найдено
*/
MACRO GetServiceID()
   var QueryServiceID = "select t_ID " +
                        "from SOFR_IPS_SETTINGS " +
                        "where UPPER(t_ServiceName) = UPPER('eprshb_to_sofr_verify_snob_result') " +
                        "  and t_Direction = 0 " + // Направление - в СОФР
                        "order by t_ID ";

   var CmdServiceID = DL_RSDCommand(QueryServiceID);
   var DSServiceID = CmdServiceID.Execute();

   if(DSServiceID.MoveNext())
      return DSServiceID.ID;
   end;

   return -1;
END;

MACRO StrToInt(str:String)
   return Int(str);
OnError
   return NULL;
END;

MACRO GetSetChar(str:String)
   if(str == "true")
      return SET_CHAR;
   else
      return UNSET_CHAR;
   end;
END;

macro DefineIsChecked(error_, ReturnCanceled, SofrStorState, SnobState, RecRequested, RecFound)
  if ((ValType(error_) == V_STRING) and (Trim(error_) != ""))
    return false; //если есть ошибка, значит точно не прошли сверку
  else
    if (ReturnCanceled == SET_CHAR)
      if ((SofrStorState == 1) and ((ValType(SnobState) != V_STRING) or (SnobState == "0")))
        return false;
      end;
      if ((SofrStorState == 0) and ((SnobState == "1")))
        return false;
      end;
    elif (ReturnCanceled == UNSET_CHAR)
      if ((SofrStorState == 1) and ((ValType(SnobState) != V_STRING) or (SnobState == "0")))
        return false;
      end;
      if ((SofrStorState == 0) and ((ValType(SnobState) != V_STRING) or (SnobState == "1")))
        return false;
      end;
    end;
    if ((RecRequested != SET_CHAR) or (RecFound != SET_CHAR))
      return false;
    end;
    return true;
  end;
end;

MACRO UpdateNPTXVERTB(client_id:INTEGER, operation_id:INTEGER, event_id:INTEGER, Record_Found:STRING, record_id:STRING, record_requested:STRING, error_:STRING, batch_id:INTEGER, record_status,
                          nptxsnobver:TRecHandler)
   if((client_id != NULL) and (operation_id != NULL) and (event_id != NULL) and (batch_id != NULL))
      var querySelect = " select * " +
                        " from DNPTXSNOBVERTB_DBT " +
                        " where t_ClientID = ? "
                        "   and t_Operation_ID = ? " +
                        "   and t_Event_ID = ? " +
                        "   and t_BatchID = ? ";

      var cmdSelect = DL_RSDCommand(querySelect);
      cmdSelect.AddParam(client_id);
      cmdSelect.AddParam(operation_id);
      cmdSelect.AddParam(event_id);
      cmdSelect.AddParam(batch_id);

      var DSSelect = cmdSelect.Execute();

      if(DSSelect.MoveNext())
         var IsChecked = DefineIsChecked(error_,
                                         nptxsnobver.rec.ReturnCanceled,
                                         DSSelect.StorState,
                                         record_status,
                                         record_requested,
                                         Record_Found);

         var queryUpdate = "UPDATE DNPTXSNOBVERTB_DBT " +
                           "SET t_Record_Found = ?, " +
                           "    t_Record_ID = ?, " +
                           "    t_Record_Requested = ?, " +
                           "    t_Error = ?, " +
                           "    t_Record_Status = ?, " +
                           "    t_Checked = ? " +
                           "WHERE t_ClientID = ? " +
                           "  and t_Operation_ID = ? " +
                           "  and t_Event_ID = ? " +
                           "  and t_BatchID = ? ";

         var cmdUpdate = RSDCommand(queryUpdate);
         cmdUpdate.AddParam("", RSDBP_IN, record_found);
         cmdUpdate.AddParam("", RSDBP_IN, record_id);
         cmdUpdate.AddParam("", RSDBP_IN, record_requested);
         cmdUpdate.AddParam("", RSDBP_IN, error_);
         cmdUpdate.AddParam("", RSDBP_IN, IIF(record_status,SET_CHAR,UNSET_CHAR));
         cmdUpdate.AddParam("", RSDBP_IN, IIF(IsChecked,SET_CHAR,UNSET_CHAR));
         cmdUpdate.AddParam("", RSDBP_IN, client_id);
         cmdUpdate.AddParam("", RSDBP_IN, operation_id);
         cmdUpdate.AddParam("", RSDBP_IN, event_id);
         cmdUpdate.AddParam("", RSDBP_IN, batch_id);
         SQL_Execute(cmdUpdate);
      else
         var queryInsert = "INSERT INTO DNPTXSNOBVERTB_DBT ( " +
                           "                                  t_ID, " +
                           "                                  t_BatchID, " +
                           "                                  t_ClientID, " +
                           "                                  t_StartDate, " +
                           "                                  t_EndDate, " +
                           "                                  t_Operation_ID, " +
                           "                                  t_Event_ID, " +
                           "                                  t_Return_Canceled, " +
                           "                                  t_Record_Found, " +
                           "                                  t_Record_ID, " +
                           "                                  t_Record_Requested, " +
                           "                                  t_Error, " +
                           "                                  t_Record_Status " +
                           "                               ) " +
                           "                        VALUES ( " +
                           "                                  0, " + // t_ID
                           "                                  ?, " + // t_BatchID
                           "                                  ?, " + // t_ClientID
                           "                                  ?, " + // t_StartDate
                           "                                  ?, " + // t_EndDate
                           "                                  ?, " + // t_Operation_ID
                           "                                  ?, " + // t_Event_ID
                           "                                  ?, " + // t_Return_Canceled
                           "                                  ?, " + // t_Record_Found
                           "                                  ?, " + // t_Record_ID
                           "                                  ?, " + // t_Record_Requested
                           "                                  ?, " + // t_Error
                           "                                  ?, " + // t_Record_Status
                           "                                  ?  " + // t_Checked
                           "                               ) ";

         var cmdInsert = RSDCommand(queryInsert);
         cmdInsert.AddParam("", RSDBP_IN, batch_id);
         cmdInsert.AddParam("", RSDBP_IN, client_id);
         cmdInsert.AddParam("", RSDBP_IN, nptxsnobver.rec.BeginDate);
         cmdInsert.AddParam("", RSDBP_IN, nptxsnobver.rec.EndDate);
         cmdInsert.AddParam("", RSDBP_IN, operation_id);
         cmdInsert.AddParam("", RSDBP_IN, event_id);
         cmdInsert.AddParam("", RSDBP_IN, nptxsnobver.rec.ReturnCanceled);
         cmdInsert.AddParam("", RSDBP_IN, record_found);
         cmdInsert.AddParam("", RSDBP_IN, record_id);
         cmdInsert.AddParam("", RSDBP_IN, record_requested);
         cmdInsert.AddParam("", RSDBP_IN, error_);
         cmdInsert.AddParam("", RSDBP_IN, record_status);
         cmdInsert.AddParam("", RSDBP_IN, UNSET_CHAR);
         SQL_Execute(cmdInsert);
      end;
   end;
END;

MACRO ExecuteStep(doc, FDoc, DocKind, ID_Operation, ID_Step)
   var nptxsnobver = TRecHandler("nptxsnobver.dbt");
   var ServiceID = GetServiceID();
   FILE infile() txt WRITE;
   var FileName;
   var IsFileExists = true;
   var CSVFile;
   var FilePath;
   var i = 0;
   var csvstr = "";
   var err = 0;

   SetBuff(nptxsnobver, FDoc);

   if(ServiceID > 0)
      var queryAns = "select t_FileName, dbms_lob.substr(lob_loc => t_Message) as t_Message " +
                     "from SOFR_IPS_MESSAGE " +
                     "where t_ServiceID = ? " +
                     "  and t_Direction = 0 " +
                     "order by t_ID desc ";

      var cmdAns = DL_RSDCommand(queryAns);
      cmdAns.AddParam(ServiceID);

      var DSAns = cmdAns.Execute();
      if(DSAns.MoveNext())
         var Message = DSAns.Message;
         if(DSAns.FileName != "")
            FileName = DSAns.FileName;
         else
            FileName = "ТехСверка_" + nptxsnobver.rec.Code;
         end;

         FilePath = "..\\import\\" + FileName;
         if(not Open(infile, FilePath))
            if(not Create(infile, FilePath))
               err = Error("Невозможно создать файл \"" + FilePath + "\"");
               IsFileExists = false;
            end;
         end;

         if(IsFileExists == true)
            infile.str = Message;
            Insert(infile);
            Close(infile);

            CSVFile = TStreamDoc(FilePath, "R", "rsansi");
            InitProgress(-1, "Получен файл " + FileName + ", обработка", "Обработка файла " + FileName);
            while(CSVFile.ReadLine(@csvstr))
               if(csvstr != "")
                  var arr = SplitString(csvstr, ";");
                  var client_id = StrToInt(arr[3]);
                  var operation_id = StrToInt(arr[4]);
                  var event_id = StrToInt(arr[5]);
                  var record_found = GetSetChar(arr[6]);
                  var record_id = arr[7];
                  var record_requested = GetSetChar(arr[8]);
                  var error_ = arr[9];
                  var batch_id = StrToInt(arr[10]);
                  UpdateNPTXVERTB(client_id, operation_id, event_id, record_found, record_id, record_requested, error_, batch_id, arr[11], nptxsnobver);
               end;
            end;
            RemProgress();
         end;
      else
        err = Error("Ответное сообщение не найдено в таблице SOFR_IPS_MESSAGE");
      end;
   else
     err = Error("Записи для сервиса под названием eprshb_to_sofr_verify_snob_result не найдены в таблице SOFR_IPS_SETTINGS");
   end;


   if (err == 0)
      if( not InsertOprStatus(46501, 3)); //Установка статуса Документооборот = Закрытие
         err = Error( "Ошибка при установке статуса вида \"Документооборот\" операции" );
      end;
   end;

   return err;
END;