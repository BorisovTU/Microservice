/*
$Name:             OverSecReg_Report.mac
$Module:           АРНУ
$Description:      Отчет "Валютная переоценка по ц/б в остатке и размещении"
*/

IMPORT "OverSecReg_Form.mac", "RegistrReport.mac", "ws_txreportform.mac";

PRIVATE VAR   OverSec_Form;
PRIVATE VAR   OverSec_Report;
PRIVATE VAR   WebMenuItem; // Информация о запуске отчета из веб

PRIVATE CONST Строка_Итого                = 0,
              Строка_СтрокаДанных         = 1;

PRIVATE CONST SHEET_1 = "Отчет";

PRIVATE CLASS (TX_RegistrReport) SP_OverSecReg_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  PRIVATE VAR m_Pref;
  PRIVATE VAR m_TaxGroup = -1, m_FIID = -1, m_AvrName = "";
  PRIVATE VAR m_CrCoup, m_CrPrev, m_Nom_DDB, m_ValPereocAll, m_ValPereocPrev, m_SumBvnK, m_ControlName, m_Dcoup;
  private var m_ValPereocAll_Sum, m_ValPereocPrev_Sum;
  private var m_ValPereocAll_Nkd, m_ValPereocPrev_Nkd;

  PRIVATE VAR m_IsDataExist = false; /*для Web, если нет данных, то ничего не показываем*/
/* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsDataExist or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END; 

  MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;

  /*По замечаниям ГПБ убрано наименование листа из формулы итогов*/
  MACRO GetFormulaSumm()
     GetFormulaSummREG();
  END;
  
  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
     m_EndDate = OverSec_Form.GetFieldValue(PNFLD_OVERSECR_ENDDATE);
     if( not m_IsWebService )
        m_TaxGroup = TArray();
        m_TaxGroup[m_TaxGroup.Size] = OverSec_Form.GetFieldValue(PNFLD_OVERSECR_AVRGROUP);
     else
        m_TaxGroup = OverSec_Form.GetFieldValue(PNFLD_OVERSECR_AVRGROUP);
     end;
     if( not m_IsWebService )
        m_FIID = TArray();
        m_FIID[m_FIID.Size] = OverSec_Form.GetFieldValue(PNFLD_OVERSECR_AVRCODE);
     else
        m_FIID = OverSec_Form.GetFieldValue(PNFLD_OVERSECR_AVRCODE);
     end;
     m_AvrName = OverSec_Form.GetFieldValue(PNFLD_OVERSECR_AVRNAME);
  END;

  PRIVATE MACRO SetAutoFilter()
     if( ExistsSheet( SHEET_1 ) )
        m_ObjTemplateXLS.SetAutoFilter("A28:AE28", SHEET_1);
     end;
  END;

  PRIVATE MACRO EndReport()
    EndReport();
    SetAutoFilter();
  END;

  PRIVATE MACRO PrintHeader()

     PrintFormatString( "",
                        m_Pref+"H0_1", string(Date():f)+" "+string(Time()),
                        m_Pref+"H0_2", string({oper} + " " + this.OperName()),  // Номер и ФИО операциониста, запустившего отчет
                        m_Pref+"H0_3", this.H0_3(),
                        m_Pref+"H0_4", this.H0_4(),
                        m_Pref+"H0_5", this.AvrTaxGroupName(),
                        m_Pref+"H0_6", this.AvrName()
                      );
  END;

  PRIVATE MACRO ПечататьСтрокуТаблицы( LineType:INTEGER )

     if( LineType == Строка_СтрокаДанных )
        PrintTableLine( m_Pref+"SecType",          this.SecType(),          null,  /*1  */
                        m_Pref+"SecCode",          this.SecCode(),          null,  /*2  */
                        m_Pref+"SecName",          this.SecName(),          null,  /*3  */
                        m_Pref+"CodeB",            this.CodeB(),            null,  /*4  */
                        m_Pref+"DR",               this.DR(),               null,  /*5  */
                        m_Pref+"QntyOst",          this.printQnty(),        null,  /*6  */   /*в ячейке видим число без дробной части, но в строке формул видим дробную часть*/
                        m_Pref+"StatusSec",        this.StatusSec(),        null,  /*7  */
                        m_Pref+"DDB",              this.DDB(),              null,  /*8  */
                        m_Pref+"Dcoup",            this.Dcoup(),            null,  /*9  */
                        m_Pref+"VN",               this.VN(),               null,  /*10 */
                        m_Pref+"SumBvn",           this.SumBvn(),           null,  /*11 */
                        m_Pref+"NkdBvn",           this.NkdBvn(),           null,  /*12 */
                        m_Pref+"CrBD",             this.printCrBD_F(),      null,  /*13 */
                        m_Pref+"CrCoup",           this.printCrCoup(),      null,  /*14 */
                        m_Pref+"CrPrev",           this.printCrPrev(),      null,  /*15 */
                        m_Pref+"CrSD",             this.printCrSD_F(),      null,  /*16 */
                        m_Pref+"ValPereocAll",     this.ValPereocAll(),     null,  /*17 */
                        m_Pref+"ValPereocPrev",    this.ValPereocPrev(),    null,  /*18 */
                        m_Pref+"ValPereocRep",     this.ValPereocRep(),     null,  /*19 */
                        m_Pref+"ValPereocAll_Sum",   this.ValPereocAll_Sum(),   null,  /*17.1 */
                        m_Pref+"ValPereocPrev_Sum",  this.ValPereocPrev_Sum(),  null,  /*18.1 */
                        m_Pref+"ValPereocRep_Sum",   this.ValPereocRep_Sum(),   null,  /*19.1 */
                        m_Pref+"ValPereocAll_Nkd",   this.ValPereocAll_Nkd(),   null,  /*17.2 */
                        m_Pref+"ValPereocPrev_Nkd",  this.ValPereocPrev_Nkd(),  null,  /*18.2 */
                        m_Pref+"ValPereocRep_Nkd",   this.ValPereocRep_Nkd(),   null,  /*19.2 */
                        m_Pref+"AmortVN",          this.AmortVN(),          null,  /*20 */
                        m_Pref+"Amortrub",         this.Amortrub(),         null,  /*21 */
                        m_Pref+"SumBvnK",          this.SumBvnK(),          null,  /*22 */
                        m_Pref+"Control",          this.Control(),          null,  /*23 */
                        m_Pref+"Control_Comment",  this.Control_Comment(),  null,  /*24 */
                        m_Pref+"ISIN",             this.ISIN(),             null   /*25 */
                      );

     elif( LineType == Строка_Итого )

        SetCurrentLineFont( 8, true, false );
        PrintTableLine( m_Pref+"SecType",  "Всего:", null );

        SetCurrentLineFont( 8, false, false );
        PrintTableLine( m_Pref+"SecType",  "в том числе", null );

     end;
  END;

  PRIVATE MACRO Get_FactDateDelivery(Pref, DealPart)
     return " NVL( (SELECT min(rq.t_FactDate) " +
            "         FROM ddlrq_dbt rq " +
            "        WHERE rq.t_DocKind    = "+Pref+".t_BOfficeKind " +
            "          AND rq.t_DocID      = "+Pref+".t_DealID " +
            "          AND rq.t_Type       = " + string(DLRQ_TYPE_DELIVERY) +
            "          AND rq.t_DealPart   = " + string(DealPart) +
            "          AND rq.t_FactDate  != TO_DATE('01-01-0001','DD-MM-YYYY') " +
            "      ), TO_DATE('01-01-0001','DD-MM-YYYY') ) ";
  END;

  PRIVATE MACRO Get_FactDatePay(Pref, DealPart)
     return " NVL( (SELECT min(rq.t_FactDate) " +
            "         FROM ddlrq_dbt rq " +
            "        WHERE rq.t_DocKind    = "+Pref+".t_BOfficeKind " +
            "          AND rq.t_DocID      = "+Pref+".t_DealID " +
            "          AND rq.t_Type       in (" + DLRQ_TYPE_PAYMENT+ ", " + DLRQ_TYPE_AVANCE+") "
            "          AND rq.t_DealPart   = " + string(DealPart) +
            "          AND rq.t_FactDate  != TO_DATE('01-01-0001','DD-MM-YYYY') " +
            "      ), TO_DATE('01-01-0001','DD-MM-YYYY') ) ";
  END;

  PRIVATE MACRO Create()
    var DataSubQuery = "", DataQuery = "", WhereCondFI = "", i = 0, 
        AvrFIIDCount = 0, AvrFIIDAddWhere = "", AvrTaxGroupCount = 0, AvrTaxGroupAddWhere = "";

    SetActiveSheet(SHEET_1);
    m_Pref = "N1_";

    PrintHeader();

    WhereCondFI =   " FROM dfininstr_dbt fin, davoiriss_dbt av, davrinvst_dbt inv "
                + " WHERE     av.t_FIID = fin.t_FIID "
                + "       AND fin.t_FI_Kind = 2 /*FIKIND_AVOIRISS*/  "
                + "       AND av.t_TaxGroup in (31, 32, 33, 34, 35, 36, 37, 43, 130) "
                + "       AND CASE WHEN inv.t_FIID > 0 THEN inv.t_FormValueFIID ELSE fin.t_FaceValueFI END > 0 "
                + "       AND inv.t_FIID (+) = fin.t_FIID ";

    if( (m_FIID != null) AND (m_FIID.Size > 0 ) ) /*задана бумага*/
      while( AvrFIIDCount < m_FIID.Size )
         if( ( ValType( m_FIID[AvrFIIDCount] ) == V_INTEGER )
         and ( m_FIID[AvrFIIDCount] > 0 ) )
            if( AvrFIIDAddWhere == "" )
               AvrFIIDAddWhere = AvrFIIDAddWhere + " AND ( ";
            else
               AvrFIIDAddWhere = AvrFIIDAddWhere + " OR ";
            end;
            AvrFIIDAddWhere = AvrFIIDAddWhere + " fin.t_FIID = " + String( m_FIID[AvrFIIDCount] ) + " ";
         end;
         AvrFIIDCount = AvrFIIDCount + 1;
      end;
      if( AvrFIIDAddWhere != "" )
         AvrFIIDAddWhere = AvrFIIDAddWhere + " ) ";
      end;
      WhereCondFI = WhereCondFI + AvrFIIDAddWhere;
    end;

    if( (m_TaxGroup != null) AND (m_TaxGroup.Size > 0 ) ) /*задана категория бумаги*/
      while( AvrTaxGroupCount < m_TaxGroup.Size )
         if( ( ValType( m_TaxGroup[AvrTaxGroupCount] ) == V_INTEGER )
         and ( m_TaxGroup[AvrTaxGroupCount] > 0 ) )
            if( AvrTaxGroupAddWhere == "" )
               AvrTaxGroupAddWhere = AvrTaxGroupAddWhere + " AND ( ";
            else
               AvrTaxGroupAddWhere = AvrTaxGroupAddWhere + " OR ";
            end;
            AvrTaxGroupAddWhere = AvrTaxGroupAddWhere + " av.t_TaxGroup = " + String( m_TaxGroup[AvrTaxGroupCount] ) + " ";
         end;
         AvrTaxGroupCount = AvrTaxGroupCount + 1;
    end;
      if( AvrTaxGroupAddWhere != "" )
         AvrTaxGroupAddWhere = AvrTaxGroupAddWhere + " ) ";
      end;
      WhereCondFI = WhereCondFI + AvrTaxGroupAddWhere;
    end;
 

    //1. Сделки, имеющиеся в остатке
    DataSubQuery =   " SELECT q.t_ID as t_BuyID, 0 as t_LinkType, q.t_Amount as t_Amount "
                   + "   FROM ( SELECT SourceLot.T_ID, NVL(SUM(Rest.t_Amount),0) AS t_Amount "
                   + "            FROM dsctxrest_dbt Rest, dsctxlot_dbt SourceLot, dsctxlot_dbt currSourceLot, "
                   + "                 ( SELECT fin.t_FIID "+ WhereCondFI + ") FI"
                   + "           WHERE (Rest.T_SALEDATE = TO_DATE('01.01.0001','DD.MM.YYYY') OR Rest.T_SALEDATE > " + GetSQLDate(m_EndDate) + " )"
                   + "             AND Rest.T_BUYDATE <= " + GetSQLDate(m_EndDate)
                   + "             AND Rest.T_BUYDATE > TO_DATE('01.01.0001','DD.MM.YYYY') "
                   + "             AND Rest.T_AMOUNT > 0 "
                   + "             AND currSourceLot.t_ID = Rest.t_SourceID "
                   + "             AND SourceLot.t_ID = currSourceLot.t_BegLotID "
                   + "             AND SourceLot.t_Type = " + TXLOTS_BUY
                   + "             AND FI.T_FIID = Rest.T_FIID "
                   + "           GROUP BY SourceLot.t_ID "
                   + "        ) q "
                   + " UNION ALL "

    //2. Сделки, переданные в прямое РЕПО или в займ
                 + "SELECT sq.t_BuyID, sq.t_Type AS t_LinkType, sq.t_Amount"
                 + "  FROM ("
                 + "    SELECT q.t_BuyID, q.t_Type,"
                 + "           (q.t_Amount - NVL( (SELECT SUM( rsb_sctx.TXGetSumSCTXLSOnDate( RLnk.t_ID, "+GetSQLDate(m_EndDate)+")) "
                 + "                                FROM dsctxlnk_dbt RLnk, "
                 + "                                     dsctxlot_dbt Rcurrsalelot, "
                 + "                                     ddl_tick_dbt Rtk "
                 + "                               WHERE RLnk.t_BuyID = q.t_BuyID "
                 + "                                 AND RLnk.t_Type = q.t_Type "
                 + "                                 AND Rcurrsalelot.t_ID = RLnk.t_SaleID "
                 + "                                 AND RLnk.t_Date <= " + GetSQLDate( m_EndDate )
                 + "                                 AND (RLnk.t_BegDate = " + GetSQLDate(date(0,0,0)) + " OR RLnk.t_BegDate < " + GetSQLDate(m_EndDate) + ") "
                 + "                                 AND (RLnk.t_EndDate = " + GetSQLDate(date(0,0,0)) + " OR RLnk.t_EndDate > " + GetSQLDate(m_EndDate) + ") "
                 + "                                 AND Rcurrsalelot.t_Origin = " + TXLOTORIGIN_DEAL
                 + "                                 AND Rtk.t_DealID = Rcurrsalelot.t_DealID "
                 + "                                 AND ("+GetSQLDate(date(0,0,0))+" = "+Get_FactDateDelivery("Rtk", 2)+" OR "+Get_FactDateDelivery("Rtk", 2)+" > "+GetSQLDate(m_EndDate)+") "
                 + "                                 AND (   Rcurrsalelot.t_BuyDate = " + GetSQLDate(date(0,0,0))
                 + "                                      or Rcurrsalelot.t_BuyDate > " + GetSQLDate( m_EndDate )
                 + "                                     ) "
                 + "                              )  ,0 )"
                 + "           ) AS t_Amount"

                 + "      FROM (  SELECT RLnk.t_BuyID, RLnk.t_Type, NVL( SUM( RLnk.T_AMOUNT) ,0) AS t_Amount"
                         + "   FROM dsctxlnk_dbt RLnk, dsctxlot_dbt BuyLot, ddl_tick_dbt tk, dsctxlot_dbt currsalelot,"
                         + "        ( SELECT fin.t_FIID "+ WhereCondFI + ") FI"
                         + "  WHERE RLnk.t_Type IN ("+TXLNK_DELREPO+","+TXLNK_SUBSTREPO+","+TXLNK_LOANPUT+","+TXLNK_SUBSTLOAN+") "
                         + "    AND FI.t_FIID = RLnk.t_FIID "
                         + "    AND RLnk.t_Date <= " + GetSQLDate(m_EndDate)
                         + "    AND (RLnk.t_BegDate = " + GetSQLDate(date(0,0,0)) + " OR RLnk.t_BegDate < " + GetSQLDate(m_EndDate) + ") "
                         + "    AND (RLnk.t_EndDate = " + GetSQLDate(date(0,0,0)) + " OR RLnk.t_EndDate > " + GetSQLDate(m_EndDate) + ") "
                         + "    AND BuyLot.t_ID = RLnk.t_BuyID  "
                         + "    AND BuyLot.t_type = " + TXLOTS_BUY
                         + "    AND currsalelot.t_ID = RLnk.t_SaleID "
                         + "    AND currsalelot.t_Origin = " + TXLOTORIGIN_DEAL
                         + "    AND tk.t_DealID = currsalelot.t_DealID "
                         + "    AND ("+GetSQLDate(date(0,0,0))+" = "+Get_FactDateDelivery("tk", 2)+" OR "+Get_FactDateDelivery("tk", 2)+" > "+GetSQLDate(m_EndDate)+") "
                         + "    AND (   currsalelot.t_BuyDate = " + GetSQLDate(date(0,0,0))
                         + "         or currsalelot.t_BuyDate > " + GetSQLDate( m_EndDate )
                         + "        ) "
                         + " GROUP BY RLnk.t_BuyID, RLnk.t_Type "
                         + " ) q "
                  + " ) sq"
                  + " WHERE sq.t_Amount > 0";

    DataQuery =   " SELECT BuyLot.t_DealCodeTS as DealBuyCodeTS, "
                + "        FI.t_FI_Code AS AvrCode, FI.t_Name AS AvrName, "
                + "        (CASE WHEN BuyLot.t_Origin = "+TXLOTORIGIN_DEAL+" THEN "+Get_FactDateDelivery("DealBuy", 1)+" ELSE BuyLot.t_BuyDate END) as BuyDate, "
                + "        (CASE WHEN BuyLot.t_Origin = "+TXLOTORIGIN_DEAL+" THEN LegB.t_Principal ELSE BuyLot.t_Amount END) as QB, "
                + "        " +Get_FactDatePay("DealBuy", 1)+" as DP, "
                + "        FI.t_DrawingDate, QueryRest.t_Amount, BuyLot.t_Origin as BuyOrigin, FI.t_FIID, FI.t_ISIN, "
                + "        LegB.t_NKD as NKDBuy, LegB.t_CFI as VprB, LegB.t_Cost as BDealCost, "
                + "        FI.t_FaceValueFI, FI.t_TaxGroup, FI.t_AvoirKind, FI.t_SumPrecision, "
                + "        QueryRest.t_LinkType, DealBuy.t_DealID DealBuyID "
                + "   FROM dsctxlot_dbt BuyLot, ddl_tick_dbt DealBuy, ddl_leg_dbt LegB, "
                         + "        ( SELECT fin.t_FIID, fin.t_FI_Code, fin.t_Name, fin.t_DrawingDate, (CASE WHEN inv.t_FIID > 0 THEN inv.t_FormValueFIID ELSE fin.t_FaceValueFI END) AS t_FaceValueFI, av.t_TaxGroup, fin.t_AvoirKind, fin.t_SumPrecision, "
                         + "                 (case when av.t_ISIN IS null OR av.t_ISIN IN (chr(0), chr(1)) then (case when av.t_LSIN IS null OR av.t_LSIN IN (chr(0), chr(1)) then chr(0) else av.t_LSIN end) else av.t_ISIN end) AS t_ISIN "
                         +              WhereCondFI
                         + "        ) FI,"
                + "        ("+DataSubQuery+") QueryRest "
                + "  WHERE BuyLot.t_ID = QueryRest.t_BuyID "
                + "    AND BuyLot.t_Type = " + TXLOTS_BUY
                + "    AND FI.t_FIID = BuyLot.t_FIID "
                + "    AND DealBuy.t_DealID (+)= BuyLot.t_DealID "
                + "    AND LegB.t_DealID  (+)= DealBuy.t_DealID "
                + "    AND LegB.t_LegKind (+)= " + LEG_KIND_DL_TICK
                + "    AND LegB.t_LegID   (+)= 0 "
                + "  ORDER BY FI.t_FIID, BuyDate, DealBuy.t_DealID ";

    m_RS = TRsbDataSet( DataQuery );

    RegisterTable( m_Pref+"MainTable", "",
          /*1  */  m_Pref+"SecType",
          /*2  */  m_Pref+"SecCode",
          /*3  */  m_Pref+"SecName",
          /*4  */  m_Pref+"CodeB",
          /*5  */  m_Pref+"DR",
          /*6  */  m_Pref+"QntyOst",
          /*7  */  m_Pref+"StatusSec",
          /*8  */  m_Pref+"DDB",
          /*9  */  m_Pref+"Dcoup",
          /*10 */  m_Pref+"VN",
          /*11 */  m_Pref+"SumBvn",
          /*12 */  m_Pref+"NkdBvn",
          /*13 */  m_Pref+"CrBD",
          /*14 */  m_Pref+"CrCoup",
          /*15 */  m_Pref+"CrPrev",
          /*16 */  m_Pref+"CrSD",
          /*17 */  m_Pref+"ValPereocAll",
          /*18 */  m_Pref+"ValPereocPrev",
          /*19 */  m_Pref+"ValPereocRep",
          /*17.1 */m_Pref+"ValPereocAll_Sum",
          /*18.1 */m_Pref+"ValPereocPrev_Sum",
          /*19.1 */m_Pref+"ValPereocRep_Sum",
          /*17.2 */m_Pref+"ValPereocAll_Nkd",
          /*18.2 */m_Pref+"ValPereocPrev_Nkd",
          /*19.2 */m_Pref+"ValPereocRep_Nkd",
          /*20 */  m_Pref+"AmortVN",
          /*21 */  m_Pref+"Amortrub",
          /*22 */  m_Pref+"SumBvnK",
          /*23 */  m_Pref+"Control",
          /*24 */  m_Pref+"Control_Comment",
          /*25 */  m_Pref+"ISIN"
                 );


    SetCellAutoSumma( "Всего:",
                      m_Pref+"QntyOst",
                      m_Pref+"SumBvn",
                      m_Pref+"NkdBvn",
                      m_Pref+"ValPereocAll",
                      m_Pref+"ValPereocPrev",
                      m_Pref+"ValPereocRep",
                      m_Pref+"ValPereocAll_Sum",
                      m_Pref+"ValPereocPrev_Sum",
                      m_Pref+"ValPereocRep_Sum",
                      m_Pref+"ValPereocAll_Nkd",
                      m_Pref+"ValPereocPrev_Nkd",
                      m_Pref+"ValPereocRep_Nkd",
                      m_Pref+"AmortVN",
                      m_Pref+"Amortrub",
                      m_Pref+"SumBvnK"
                     );

    ПечататьСтрокуТаблицы( Строка_Итого );

    var Maximum = SQL_GetNRecs( DataQuery );
    var ProgressIndicator = CreateProgressIndicator(m_IsWebService);
    
    if( m_IsWebService and (WebMenuItem != null) )
      var header = "Формирование отчета " + WebMenuItem.szNameItem;
      ProgressIndicator.Start(Maximum, header, header);
    else
      ProgressIndicator.Start(Maximum, "Идет формирование отчета \"Валютная переоценка ц/б в остатке и размещении\"", "Валютная переоценка ц/б в остатке и  размещении");
    end;

    i = 0;
    while(m_RS.moveNext())

      this.ClearCacheOneRecord();
      m_IsDataExist = true;
      ПечататьСтрокуТаблицы( Строка_СтрокаДанных );
      i = i + 1;
      ProgressIndicator.Update(i,Maximum); // всего 3 парамера Update(index, count, text)

    end;

    ProgressIndicator.Stop();

    EndTable();

    SetSubtotalEx(26, 29,
                "F22",
                "K22",
                "L22",
                "Q22",
                "R22",
                "S22",
                "T22",
                "U22",
                "V22",
                "W22",
                "X22",
                "Y22",
                "Z22",
                "AA22",
                "AB22"
               );


  END;

  MACRO ClearCacheOneRecord()
     this.ClearCacheOneRecord();
     m_CrCoup        = null;
     m_CrPrev        = null;
     m_Nom_DDB       = null;
     m_ValPereocAll  = null;
     m_ValPereocPrev = null;
     m_ValPereocAll_Sum  = null;
     m_ValPereocPrev_Sum = null;
     m_ValPereocAll_Nkd  = null;
     m_ValPereocPrev_Nkd = null;
     m_SumBvnK       = null;
     m_ControlName   = null;
     m_Dcoup         = null;
  END;

  /*Начало отчетного периода */
  MACRO H0_3():DATE
     return date(OverSec_Form.GetFieldValue(PNFLD_OVERSECR_BEGINDATE));
  END;

  /*Отчетная дата, указанная в форме запуска отчета*/
  MACRO H0_4():DATE
     return date(OverSec_Form.GetFieldValue(PNFLD_OVERSECR_ENDDATE));
  END;

  /*Группа налогового учета*/
  MACRO AvrTaxGroupName():STRING
     var GroupName = "", AvrTaxGroupCount = 0; 

     if (m_TaxGroup.Size > 0)
        while( AvrTaxGroupCount < m_TaxGroup.Size )
           if( ( ValType( m_TaxGroup[AvrTaxGroupCount] ) == V_INTEGER )
           and ( m_TaxGroup[AvrTaxGroupCount] > 0 ) )
              if( GroupName == "" )
                 GroupName = GroupName + GetLLVALname (2903, m_TaxGroup[AvrTaxGroupCount]);
     else
                 GroupName = GroupName + ", " + GetLLVALname (2903, m_TaxGroup[AvrTaxGroupCount]);
              end;
           end;
           AvrTaxGroupCount = AvrTaxGroupCount + 1;
        end;
     end;
     if( GroupName == "" )
        GroupName = "Все";
     end;
     return GroupName;
  END;

  /*Выпуск ц/б*/
  MACRO AvrName():STRING
     return m_AvrName;
  END;


  MACRO OperName()
    var cmd, DataSet;

    cmd = DL_RSDCommand("SELECT t_Name FROM dperson_dbt WHERE t_oper = ?" );

    cmd.AddParam({oper});

    DataSet = cmd.Execute();

    if( DataSet.MoveNext() )
       return DataSet.Name;
    end;
    return "";
  END;

  /*Значение примечания сделки "Дата в учете"*/
  MACRO DR():DATE
    var vDR = date(0,0,0);

    vDR = readNoteForObject( OBJTYPE_SECDEAL, UniID( this.DealBuy(), OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_DATEIN);

    if((ValType(vDR) != V_UNDEF) and (vDR != ""))
       vDR = date(vDR);
    end;
    return vDR;
  END;

  MACRO StatusSec():STRING
    if( (m_RS.t_LinkType == TXLNK_DELREPO) or (m_RS.t_LinkType == TXLNK_SUBSTREPO) )
       return "РП";
    elif( (m_RS.t_LinkType == TXLNK_LOANPUT) or (m_RS.t_LinkType == TXLNK_SUBSTLOAN) )
       return "ЗП";
    end;
    return "О";
  END;

  MACRO Dcoup():DATE //дата погашения купона, который гасится первым в период между <DDB>+1 и <H0.4> включительно
     if( m_Dcoup == null )
        m_Dcoup = date(0,0,0);

        if( FI_AvrKindsEQ( FIKIND_AVOIRISS, AVOIRISSKIND_BOND, m_RS.AvoirKind ) )

           VAR sql2   = RSDCommand( " SELECT t_DrawingDate " +
                                    "   FROM dfiwarnts_dbt " +
                                    "  WHERE t_FIID         =  ? AND " +
                                    "        t_IsPartial   != 'X' AND " +
                                    "        t_DrawingDate >=  ? AND " +
                                    "        t_DrawingDate <=  ? " +
                                    " ORDER BY t_DrawingDate ASC "
                                  );
          
           sql2.addParam( "", RSDBP_IN, m_RS.t_FIID );
           sql2.addParam( "", RSDBP_IN, this.DDB()+1 );
           sql2.addParam( "", RSDBP_IN, this.H0_4() );
           sql2.execute();
          
           VAR DataSet   = TRsbDataSet(sql2);
           if( DataSet.MoveNext() )
              m_Dcoup = SQL_ConvTypeDate( DataSet.t_DrawingDate );
           end;

        end;
     end;

     return m_Dcoup;
  END;

  MACRO SumBvp():MONEY //  Стоимость приобретения - в валюте цены
     if( m_SumBvp == NULL )
        m_SumBvp = this.SumBmain()*this.Qnty()/m_RS.QB;
     end;
     return m_SumBvp;
  END;

  MACRO NkdBVN():MONEY //  НКД уплаченный при приобретении - сумма в валюте номинала
     if( m_NkdBvn == NULL )
        m_NkdBvn = this.NKDBuy()*this.Qnty()/m_RS.QB;
        if( CheckTaxDepositoryMode() == true )
          if((this.VprB() == NATCUR) and (m_RS.FaceValueFI != NATCUR)) //если валюта сделки == рубли, а валюта номинала != рубли
            m_NkdBvn = this.NKDBuy()*this.Qnty()/(m_RS.QB*this.CrBD());
          end;
        end;
     end;
     return m_NkdBvn;
  END;

  MACRO CrBD():DOUBLE // Курс ЦБ РФ  - на дату приобретения (14.07.2005)
    if(m_CrBD == NULL)
       if(m_RS.FaceValueFI == NATCUR)
         m_CrBD = 1.0;
       elif(this.DDB() <= date(14,7,2005))
         m_CrBD = GetRateOnDateCrossDbl( date(14,7,2005), m_RS.FaceValueFI, NATCUR, true );
       elif(this.DDB() < date(1,1,2010))
         m_CrBD = GetRateOnDateCrossDbl( this.DDB(), m_RS.FaceValueFI, NATCUR, true );
       else
         if(m_RS.FaceValueFI == this.VprB())
           var DP = date(m_RS.DP);

           if(DP == date(0,0,0))
             DP = date(31,12,9999);
           end;

           m_CrBD = GetRateOnDateCrossDbl( min(this.DDB(), DP), m_RS.FaceValueFI, NATCUR, true );
         else
           m_CrBD = GetRateOnDateCrossDbl( this.DDB(), m_RS.FaceValueFI, NATCUR, true );
         end;
       end;
    end;

    if((m_CrBD == NULL) or (m_CrBD == 0.0))
      m_CrBD = NULL;
      return 1.0;
    end;

    return m_CrBD;
  END;

  MACRO CrCoup():DOUBLE // Курс ЦБ РФ на дату погашения НКД
    if(m_CrCoup == NULL)
      if(m_RS.FaceValueFI == NATCUR)
        m_CrCoup = 1.0;
      else
        if((this.Dcoup() == null) or (this.Dcoup() == date(0,0,0)))
           m_CrCoup = this.CrSD();
        else
           m_CrCoup = GetRateOnDateCrossDbl( this.Dcoup(), m_RS.FaceValueFI, NATCUR, true );
        end;
      end;
    end;

    if((m_CrCoup == NULL) or (m_CrCoup == 0.0))
      m_CrCoup = NULL;
      return 1.0;
    end;

    return m_CrCoup;
  END;


  MACRO CrPrev():DOUBLE // Курс ЦБ РФ на конец предыдущего периода
    if(m_CrPrev == NULL)
      if(m_RS.FaceValueFI == NATCUR)
        m_CrPrev = 1.0;
      else
        m_CrPrev = GetRateOnDateCrossDbl( this.H0_3()-1, m_RS.FaceValueFI, NATCUR, true );
      end;
    end;

    if((m_CrPrev == NULL) or (m_CrPrev == 0.0))
      m_CrPrev = NULL;
      return 1.0;
    end;

    return m_CrPrev;
  END;

  MACRO CrSD():DOUBLE // Курс ЦБ РФ на конец текущего отчетного периода
    if(m_CrSD == NULL)
      if(m_RS.FaceValueFI == NATCUR)
        m_CrSD = 1.0;
      else
        m_CrSD = GetRateOnDateCrossDbl( this.H0_4(), m_RS.FaceValueFI, NATCUR, true );
      end;
    end;

    if((m_CrSD == NULL) or (m_CrSD == 0.0))
      m_CrSD = NULL;
      return 1.0;
    end;

    return m_CrSD;
  END;

  MACRO GetSumPartFW(Cr:DOUBLE, BegDate:DATE, EndDate:DATE)
    var query, cmd, DataSet;
    var A, C, Sum = 0.0;

    query = " SELECT FI.t_FaceValue, warnt.t_DrawingDate as DrawingDate, "
          + "        warnt.t_IncomeRate, warnt.t_IncomeScale "
          + "   FROM dfiwarnts_dbt warnt, dfininstr_dbt FI "
          + "  WHERE warnt.t_FIID = ? "
          + "    AND warnt.t_IsPartial = chr(88) "
          + "    AND warnt.t_DrawingDate >= ? "
          + "    AND warnt.t_DrawingDate <= ? "
          + "    AND FI.t_FIID = warnt.t_FIID ";

    cmd = Dl_RSDCommand(query);

    cmd.addParam( m_RS.FIID );
    cmd.addParam( BegDate );
    cmd.addParam( EndDate );

    DataSet = cmd.execute();
    while(DataSet.MoveNext())
      A = DataSet.FaceValue * DataSet.IncomeRate/MAX( 1, DataSet.IncomeScale)/100.0; //сумма i-го частичного погашения одной бумаги
      C = GetRateOnDateCrossDbl( date(DataSet.DrawingDate), m_RS.FaceValueFI, NATCUR, true );//курс на дату выплаты i-го частичного погашения

      Sum = Sum + A*(Cr - C);
    end;

    return Sum;
  END;

  MACRO Nom_DDB():MONEY // Номинал на дату DDB
    if(m_Nom_DDB == NULL)
      m_Nom_DDB = GetFaceValue( m_RS.FIID, this.DDB() );
    end;

    return m_Nom_DDB;
  END;

  MACRO ValPereocAll():MONEY // Переоценка стоимости с НКД с учетом даты погашения НКД и амортизации (в соответствии с условиями выпуска) за период владения (с учетом НКД)
    if ( m_ValPereocAll == NULL ) 

         m_ValPereocAll = this.SumBvn()*(this.CrSD()-this.CrBD()) +
                          this.NkdBvn()*(this.CrCoup()-this.CrBD());

         if( this.Nom_DDB() != $0 )// у паев нет номинала
            m_ValPereocAll = m_ValPereocAll - this.SumBvn()/this.Nom_DDB()*GetSumPartFW(this.CrSD(), this.DDB()+1, this.H0_4());
         end;

    end;

    if(m_ValPereocAll == NULL)
      m_ValPereocAll = 0;
    end;

    return m_ValPereocAll;
  END;

  MACRO ValPereocAll_Sum():MONEY
    if (m_ValPereocAll_Sum == NULL)
      m_ValPereocAll_Sum = this.SumBvn() * (this.CrSD() - this.CrBD());

      if (this.Nom_DDB() != $0)
        m_ValPereocAll_Sum = m_ValPereocAll_Sum - this.SumBvn() / this.Nom_DDB()
                             * GetSumPartFW(this.CrSD(), this.DDB() + 1,
                                            this.H0_4());
      end;
    end;

    if (m_ValPereocAll_Sum == NULL)
      m_ValPereocAll_Sum = 0;
    end;

    return m_ValPereocAll_Sum;
  end;

  MACRO ValPereocAll_Nkd():MONEY
    if (m_ValPereocAll_Nkd == NULL)
      m_ValPereocAll_Nkd = this.NkdBvn() * (this.CrCoup() - this.CrBD());
    end;

    if (m_ValPereocAll_Nkd == NULL)
      m_ValPereocAll_Nkd = 0;
    end;

    return m_ValPereocAll_Nkd;
  end;

  MACRO ValPereocPrev():MONEY // Переоценка стоимости с НКД  с учетом даты погашения НКД и амортизации (в соответствии с условиями выпуска)  - до НОП
    if(m_ValPereocPrev == NULL)
      if(this.DDB() >= this.H0_3())
        m_ValPereocPrev = 0;
      else 
        var CrCoupPrev = this.CrPrev();

        if((this.Dcoup() < this.H0_3()) and (this.Dcoup() != null) and (this.Dcoup() != date(0,0,0)))
          CrCoupPrev = this.CrCoup();
        end;

        m_ValPereocPrev = this.SumBvn()*(this.CrPrev() - this.CrBD()) +
                          this.NkdBvn()*(CrCoupPrev - this.CrBD());

        if( this.Nom_DDB() != $0 )// у паев нет номинала
           m_ValPereocPrev = m_ValPereocPrev - this.SumBvn()/this.Nom_DDB()*GetSumPartFW(this.CrPrev(), this.DDB()+1, this.H0_3()-1);
        end;

      end;
    end;

    if(m_ValPereocPrev == NULL)
      m_ValPereocPrev = 0;
    end;

    return m_ValPereocPrev;
  END;

  MACRO ValPereocPrev_Sum():MONEY
    if (m_ValPereocPrev_Sum == NULL)
      if (this.DDB() >= this.H0_3())
        m_ValPereocPrev_Sum = 0;
      else
        m_ValPereocPrev_Sum = this.SumBvn() * (this.CrPrev() - this.CrBD());

        if (this.Nom_DDB() != $0)
          m_ValPereocPrev_Sum = m_ValPereocPrev_Sum - 
                                this.SumBvn() / this.Nom_DDB()
                                * GetSumPartFW(this.CrPrev(), this.DDB() + 1,
                                               this.H0_3() - 1);
        end;
      end;
    end;

    if (m_ValPereocPrev_Sum == NULL)
      m_ValPereocPrev_Sum = 0;
    end;

    return m_ValPereocPrev_Sum;
  end;

  MACRO ValPereocPrev_Nkd():MONEY
    if (m_ValPereocPrev_Nkd == NULL)
      if (this.DDB() >= this.H0_3())
        m_ValPereocPrev_Nkd = 0;
      else
        var CrCoupPrev = this.CrPrev();

        if ((this.Dcoup() < this.H0_3()) and (this.Dcoup() != null)
             and (this.Dcoup() != date(0,0,0)))
          CrCoupPrev = this.CrCoup();
        end;

        m_ValPereocPrev_Nkd = this.NkdBvn() * (CrCoupPrev - this.CrBD());
      end;
    end;

    if (m_ValPereocPrev_Nkd == NULL)
      m_ValPereocPrev_Nkd = 0;
    end;

    return m_ValPereocPrev_Nkd;
  end;

  MACRO ValPereocRep():MONEY // Переоценка стоимости с НКД  с учетом даты погашения НКД и амортизации (в соответствии с условиями выпуска)  - за ОП
    return this.ValPereocAll() - this.ValPereocPrev();
  END;

  MACRO ValPereocRep_Sum():MONEY
    return this.ValPereocAll_Sum() - this.ValPereocPrev_Sum();
  end;

  MACRO ValPereocRep_Nkd():MONEY
    return this.ValPereocAll_Nkd() - this.ValPereocPrev_Nkd();
  end;

  MACRO AmortVN():MONEY // Доходы, полученные при частичном погашении номинала :
    if( m_AmortVN == NULL )
       m_AmortVN = GetPartialSumByPeriod( m_RS.FIID, this.Qnty(), this.DDB()+1, this.H0_4() );
    end;
    return m_AmortVN;
  END;

  MACRO AmortRub():MONEY //  Доходы, полученные при частичном погашении номинала- сумма, руб
    if( m_AmortRub == NULL )
       m_AmortRub = GetPartialSumByPeriod_Rub( m_RS.FIID, this.Qnty(), this.DDB()+1, this.H0_4() );
    end;
    return m_AmortRub;
  END;

  MACRO SumBvnK():MONEY // Стоимость, подлежащая переоценке, сложившаяся на конец отчетного периода, в валюте номинала (Без НКД)
    if ((m_SumBvnK == NULL) and (this.Nom_DDB() != 0.0)) // у паев нет номинала, чтобы не делить на 0
      var query, cmd, DataSet;
      var A, C, Sum = 0.0;

      query = " SELECT FI.t_FaceValue, warnt.t_DrawingDate as DrawingDate, "
            + "        warnt.t_IncomeRate, warnt.t_IncomeScale "
            + "   FROM dfiwarnts_dbt warnt, dfininstr_dbt FI "
            + "  WHERE warnt.t_FIID = ? "
            + "    AND warnt.t_IsPartial = chr(88) "
            + "    AND warnt.t_DrawingDate >= ? "
            + "    AND warnt.t_DrawingDate <= ? "
            + "    AND FI.t_FIID = warnt.t_FIID ";

      cmd = Dl_RSDCommand(query);

      cmd.addParam( m_RS.FIID );
      cmd.addParam( this.DDB()+1 );
      cmd.addParam( this.H0_4() );

      DataSet = cmd.execute();
      while(DataSet.MoveNext())
        A = DataSet.FaceValue * DataSet.IncomeRate/MAX( 1, DataSet.IncomeScale)/100.0; //сумма i-го частичного погашения одной бумаги
        
        Sum = Sum + A/this.Nom_DDB();
      end;

      m_SumBvnK = this.SumBvn()*(1 - Sum);
    else
      m_SumBvnK = this.SumBvn();
    end;

    return m_SumBvnK;
  END;

  /*Значение поля <Название> категории сделки <Сделка подлежит дополнительному контролю>*/
  MACRO Control()
    if( m_ControlName == null )
       m_ControlName = "";
       DL_GetObjAttrName(OBJTYPE_SECDEAL, OBJGROUP_TICKCONTROL, DL_GetMainObjAttr( this.DealBuy(), OBJGROUP_TICKCONTROL, OBJTYPE_SECDEAL ), @m_ControlName, null, null);
    end;

    return m_ControlName;
  END;

  /*Значение поля <Примечание> для вида примечания сделки <Коммента-рий для категории <Сделка подлежит дополнительному контролю>>*/
  MACRO Control_Comment():string
    var vControl_Comment = readNoteForObject( OBJTYPE_SECDEAL, UniID( this.DealBuy(), OBJTYPE_SECDEAL), NOTEKIND_SECDEAL_CONTROLCOMMENT);
      if((ValType(vControl_Comment) != V_UNDEF) and (vControl_Comment != ""))
        vControl_Comment = string(vControl_Comment);
      end;
    return vControl_Comment;
  END;

  macro printCrCoup()
     return ВыводЧерезФормулу(this.CrCoup());
  end;

  macro printCrPrev()
     return ВыводЧерезФормулу(this.CrPrev());
  end;

  MACRO VN():STRING // Код валюты номинала (или  Номинал - валюта)
     return m_CacheFinInstr.GetISO( m_RS.FaceValueFI ).m_Number;
  END;

  initTX_RegistrReport( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)

  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;

  if( not IsWebService )
      OverSec_Form = SP_OverSecReg_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      OverSec_Form = WS_OverSec_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      stat = OverSec_Form.Run();
    end;

    if( stat )
      OverSec_Report = SP_OverSecReg_Report( null, "Report_OverSec.xls", true, IsWebService, ReportHashCode );
      OverSec_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat( OverSec_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
        FullReportFileNames = OverSec_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat( OverSec_Report.PrintMode() );
      end;
    end;
  end;

  return FullReportFileNames;
END;