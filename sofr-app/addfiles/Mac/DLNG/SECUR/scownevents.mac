/*
$Name:        scownevents.mac
$Module:      Ценные бумаги
$Description: Отчет "Отчет о предстоящих событиях" для ОЭБ
*/
import "scownevents_form.mac", "sprepfun.mac";

//Типы записей во временной таблице
PRIVATE CONST TYPE_EVENT  = 1, //Событие
              TYPE_ACTION = 2; //Действие

//Виды предстоящих событий 
PRIVATE CONST EVENT_DEGINDATEOFFER = 1, //Начало периода оферты
              EVENT_ENDDATEOFFER   = 2, //Окончание периода оферты
              EVENT_DATEREDEMPTION = 3, //Выкуп ц/б по оферте
              EVENT_COUPRETIREMENT = 4, //Погашение купона
              EVENT_FIXOWNERSLIST  = 5, //Фиксация списка владельцев
              EVENT_OWNPAYMENT     = 6, //Выплата денежных средств владельцам облигаций
              EVENT_AVRRETIREMENT  = 7; //Погашение выпуска

PRIVATE VAR EventNames = TArray();
EventNames[EVENT_DEGINDATEOFFER] = "Начало периода оферты";
EventNames[EVENT_ENDDATEOFFER]   = "Окончание периода оферты";
EventNames[EVENT_DATEREDEMPTION] = "Выкуп ц/б по оферте";
EventNames[EVENT_COUPRETIREMENT] = "Погашение купона";
EventNames[EVENT_FIXOWNERSLIST]  = "Фиксация списка владельцев";
EventNames[EVENT_OWNPAYMENT]     = "Выплата денежных средств владельцам облигаций";
EventNames[EVENT_AVRRETIREMENT]  = "Погашение выпуска";


//Виды действий
PRIVATE CONST ACTION_FORMMSG_LISTDATE  = 1, //Формирование сообщения по раскрытию информации о дате списка в новостные ленты
              ACTION_FORMMSG_F12_2     = 2, //Формирование сообщения по раскрытию информации в НРД по форме 12.2
              ACTION_SETINCOMERATE     = 3, //Установка процентной ставки
              ACTION_FORMMSG_REPAYMENT = 4, //Формирование сообщения по раскрытию информации о выполненном погашении 
              ACTION_FORMMSG_PAYINCOME = 5, //Формирование сообщения по раскрытию информации в новостные ленты о выплаченных доходах
              ACTION_FORMMSG_F12_4     = 6, //Формирование сообщения по раскрытию информации в НРД по форме 12.4 
              ACTION_FORMMSG_F12_6     = 7, //Формирование сообщения по раскрытию информации в НРД по форме 12.6
              ACTION_TRANSFER_PA       = 8; //Перевод денежных средств для выплаты владельцам


PRIVATE VAR ActionNames = TArray();
ActionNames[ACTION_FORMMSG_LISTDATE]  = "Формирование сообщения по раскрытию информации о дате списка в новостные ленты";
ActionNames[ACTION_FORMMSG_F12_2]     = "Формирование сообщения по раскрытию информации в НРД по форме 12.2";
ActionNames[ACTION_SETINCOMERATE]     = "Установка процентной ставки";
ActionNames[ACTION_FORMMSG_REPAYMENT] = "Формирование сообщения по раскрытию информации о выполненном погашении";
ActionNames[ACTION_FORMMSG_PAYINCOME] = "Формирование сообщения по раскрытию информации в новостные ленты о выплаченных доходах";
ActionNames[ACTION_FORMMSG_F12_4]     = "Формирование сообщения по раскрытию информации в НРД по форме 12.4";
ActionNames[ACTION_FORMMSG_F12_6]     = "Формирование сообщения по раскрытию информации в НРД по форме 12.6";
ActionNames[ACTION_TRANSFER_PA]       = "Перевод денежных средств для выплаты владельцам";

//Статусы действий
PRIVATE CONST STATE_PREPARED = 1, //Подготовлено
              STATE_SENT     = 2, //Отправлено
              STATE_OVERDUE  = 3, //Просрочено
              STATE_EXEC     = 4, //Исполнено
              STATE_NOTEXEC  = 5; //Не исполнено

PRIVATE VAR StateNames = TArray();
StateNames[STATE_PREPARED] = "Подготовлено";
StateNames[STATE_SENT]     = "Отправлено";
StateNames[STATE_OVERDUE]  = "Просрочено";
StateNames[STATE_EXEC]     = "Исполнено";
StateNames[STATE_NOTEXEC]  = ""; //Не исполнено. Специально пустое значение, чтобы не выводить в отчет (так требуется по ТЗ)


PRIVATE VAR IsExcel = true;

private macro LoadEventsTmp(BegDate, EndDate, FIID)
  var query, cmd;

  query =   " INSERT INTO DSCOWNEVENTS_TMP(T_TYPE, T_FIID, T_EVENTDATE, T_EVENTKIND, T_ACTIONKIND, T_ACTIONDATE, T_ACTIONSTATE) " 
          + " with f as (select t_FIID "
          + "              from dfininstr_dbt f "
          + "             where f.t_Issuer = " + {OurBank}
          + "               and RSB_FIInstr.FI_AvrKindsGetRoot(f.t_FI_Kind, f.t_AvoirKind ) = " + AVOIRISSKIND_BOND;
  
  if(FIID > 0)
    query = query + " and f.t_FIID = ? ";
  end;

  query = query
          + "           ) "
          + " select "+TYPE_EVENT+", q.FIID, q.EventDate, q.EventKind, 0, "+GetSQLDate(date(0,0,0))+", 0 "
          + "   from (";

  //наличие дат начала периода приема заявок на выкуп
  query = query + " select offer.t_BeginDateOffer as EventDate, "+EVENT_DEGINDATEOFFER+" as EventKind, f.t_FIID as FIID "
                + "   from doffers_dbt offer, f "
                + "  where offer.t_FIID = f.t_FIID "
                + "    and offer.t_BeginDateOffer >= ? "
                + "    and offer.t_BeginDateOffer <= ? ";
  
  //наличие дат окончания периода приема заявок на выкуп
  query = query + " UNION ALL "
                + " select offer.t_EndFateOffer as EventDate, "+EVENT_ENDDATEOFFER+" as EventKind, f.t_FIID as FIID "
                + "   from doffers_dbt offer, f "
                + "  where offer.t_FIID = f.t_FIID "
                + "    and offer.t_EndFateOffer >= ? "
                + "    and offer.t_EndFateOffer <= ? ";
  
  //наличие дат оферты
  query = query + " UNION ALL "
                + " select offer.t_DateRedemption as EventDate, "+EVENT_DATEREDEMPTION+" as EventKind, f.t_FIID as FIID "
                + "   from doffers_dbt offer, f "
                + "  where offer.t_FIID = f.t_FIID "
                + "    and offer.t_DateRedemption >= ? "
                + "    and offer.t_DateRedemption <= ? ";
  
  //погашение купона
  query = query + " UNION ALL "
                + " select fiw.t_DrawingDate as EventDate, "+EVENT_COUPRETIREMENT+" as EventKind, f.t_FIID as FIID "
                + "   from dfiwarnts_dbt fiw, f "
                + "  where fiw.t_FIID = f.t_FIID "
                + "    and fiw.t_IsPartial = CHR(0) "
                + "    and fiw.t_DrawingDate >= ? "
                + "    and fiw.t_DrawingDate <= ? "
                + "    and Exists(select 1 "
                + "                 from dfiwarnts_dbt fiw1 "
                + "                where fiw1.t_FIID = fiw.t_FIID "
                + "                  and fiw1.t_IsPartial = CHR(0) "
                + "                  and fiw1.t_DrawingDate > fiw.t_DrawingDate "
                + "              )";

  //погашение выпуска (определяется по последнему купону)
  query = query + " UNION ALL "
                + " select fiw.t_DrawingDate as EventDate, "+EVENT_AVRRETIREMENT+" as EventKind, f.t_FIID as FIID "
                + "   from dfiwarnts_dbt fiw, f "
                + "  where fiw.t_FIID = f.t_FIID "
                + "    and fiw.t_IsPartial = CHR(0) "
                + "    and fiw.t_DrawingDate >= ? "
                + "    and fiw.t_DrawingDate <= ? "
                + "    and not Exists(select 1 "
                + "                     from dfiwarnts_dbt fiw1 "
                + "                    where fiw1.t_FIID = fiw.t_FIID "
                + "                      and fiw1.t_IsPartial = CHR(0) "
                + "                      and fiw1.t_DrawingDate > fiw.t_DrawingDate "
                + "                  )";

  //фиксация списка владельцев 
  query = query + " UNION ALL "
                + " select fiw.t_ListDate as EventDate, "+EVENT_FIXOWNERSLIST+" as EventKind, f.t_FIID as FIID "
                + "   from dfiwarnts_dbt fiw, f "
                + "  where fiw.t_FIID = f.t_FIID "
                + "    and fiw.t_IsPartial = CHR(0) "
                + "    and fiw.t_ListDate >= ? "
                + "    and fiw.t_ListDate <= ? ";

  //дата выплаты денежных средств владельцам облигаций при погашении купона
  query = query + " UNION ALL "
                + " select RSI_RsbCalendar.GetDateAfterWorkDay(fiw.t_DrawingDate, 0) as EventDate, "+EVENT_OWNPAYMENT+" as EventKind, f.t_FIID as FIID "
                + "   from dfiwarnts_dbt fiw, f "
                + "  where fiw.t_FIID = f.t_FIID "
                + "    and fiw.t_IsPartial = CHR(0) "
                + "    and fiw.t_DrawingDate >= ? "
                + "    and fiw.t_DrawingDate <= ? ";

  query = query + ") q ";


  cmd = RSDCommand(query);

  cmd.NullConversion = true;
  if(FIID > 0)
    cmd.AddParam("", RSDBP_IN, FIID);
  end;

  cmd.AddParam("", RSDBP_IN, BegDate);
  cmd.AddParam("", RSDBP_IN, EndDate);
  cmd.AddParam("", RSDBP_IN, BegDate);
  cmd.AddParam("", RSDBP_IN, EndDate);
  cmd.AddParam("", RSDBP_IN, BegDate);
  cmd.AddParam("", RSDBP_IN, EndDate);
  cmd.AddParam("", RSDBP_IN, BegDate);
  cmd.AddParam("", RSDBP_IN, EndDate);
  cmd.AddParam("", RSDBP_IN, BegDate);
  cmd.AddParam("", RSDBP_IN, EndDate);
  cmd.AddParam("", RSDBP_IN, BegDate);
  cmd.AddParam("", RSDBP_IN, EndDate);
  cmd.AddParam("", RSDBP_IN, BegDate);
  cmd.AddParam("", RSDBP_IN, EndDate);

  SQL_Execute( cmd, "", false );
end;

private macro LoadActionsTmp(BegDate:date, EndDate:date)
  var query, cmd, DataSet;
  var cnt = 0, i = 0;
  var owneventCache = RsbSQLInsert("scownevents.tmp");

  //добавить дейвие
  macro AddAction(ActionKind:integer, ActionDate:date, ActionState:integer)
    var ownevent = TRecHandler("scownevents.tmp");
    
    ownevent.Clear();
    ownevent.rec.Type       = TYPE_ACTION;
    ownevent.rec.FIID       = DataSet.FIID;
    ownevent.rec.EventKind  = DataSet.EventKind;
    ownevent.rec.EventDate  = date(DataSet.EventDate);
    ownevent.rec.ActionKind = ActionKind;
    ownevent.rec.ActionDate = ActionDate;
    ownevent.rec.ActionState= ActionState;

    owneventCache.AddRecord(ownevent);
  end;

  macro GetDateByServ(InitialDate:date, CalendKind:integer, NDays:integer)
    var ResultDate = date(0,0,0);

    if(CalendKind == 1) //Календарных
      ResultDate = InitialDate - NDays;
    elif(CalendKind == 2) //Рабочих
      ResultDate = GetDateAfterWorkDays( InitialDate, -NDays);
    else
      ResultDate = InitialDate;
    end;

    return ResultDate;
  end;

  //одно учетное действие по сообщению
  macro CreateOneActionByMsg(Ди:date, ActionKind:integer, DocKind:integer, Number_Coupon:string)
    var q, ds, sel;
    var state = 0;

    q =   " select t_Sent, t_Prepared"
        + "   from dscownmsg_dbt "
        + "  where t_FIID = ? "
        + "    and t_Kind = ? ";

    sel = DL_RSDCommand();

    sel.AddParam(DataSet.FIID);
    sel.AddParam(DocKind);

    if(Number_Coupon != NULL)
      q = q + " and t_Number_Coupon = ? ";
      sel.AddParam(Number_Coupon);
    end;

    q = q + " order by t_PrepareDate DESC";

    ds = sel.Execute(q);
    if(ds.moveNext())
      if(ds.Sent == SET_CHAR)
        state = STATE_SENT;
      elif(ds.Prepared == SET_CHAR)
        state = STATE_PREPARED;
      end;
    else
      if(Ди < BegDate)
        state = STATE_OVERDUE;
      else
        state = STATE_NOTEXEC;
      end;
    end;

    if(state > 0)
      AddAction(ActionKind, Ди, state);
    end;
  end;

  //все учетные дейсвия по событию "Фиксация списка владельцев" 
  macro CreateActions_FixOwnersList()
    var q, ds, sel;
    var Number_Coupon = NULL;
    var Ди = date(0,0,0);
    
    q =   " select t_Number "
        + "   from dfiwarnts_dbt "
        + "  where t_FIID = ? "
        + "    and t_IsPartial = CHR(0) "
        + "    and t_ListDate = ? "
        + "  order by t_DrawingDate asc ";

    sel = DL_RSDCommand(q);

    sel.AddParam(DataSet.FIID);
    sel.AddParam(DataSet.EventDate);

    ds = sel.Execute();
    if(ds.moveNext())
      Number_Coupon = ds.Number;
    else
      Number_Coupon = "";
    end;

    //формируем действие "Формирование сообщения по раскрытию информации о дате списка в новостные ленты"
    Ди = GetDateByServ(date(DataSet.EventDate), DataSet.Calendar_News, DataSet.NewsTapeNumber);

    CreateOneActionByMsg(Ди,
                         ACTION_FORMMSG_LISTDATE,
                         DOCKIND_MSG_OWNLISTDATE,
                         Number_Coupon
                        );

    //формируем действие "Формирование сообщения по раскрытию информации в НРД по форме 12.2"
    Ди = GetDateByServ(date(DataSet.EventDate), DataSet.Calendar_Web, DataSet.WebSiteNumber);

    CreateOneActionByMsg(Ди,
                         ACTION_FORMMSG_F12_2,
                         DOCKIND_FORM12_2,
                         Number_Coupon
                        );

  end;

  //все учетные дейсвия по событию "Погашение выпуска" 
  macro CreateActions_AvrRetirement()
    var Ди = date(0,0,0);
    var q, ds, sel;
    var state = STATE_NOTEXEC;

    //формируем действие "Установка процентной ставки"
    Ди = GetDateByServ(date(DataSet.EventDate), DataSet.Calendar_Rate, DataSet.InterestRateNumber);

    q =   " select t_IncomeRate, t_IncomeVolume"
        + "   from dfiwarnts_dbt "
        + "  where t_FIID = ? "
        + "    and t_IsPartial = CHR(0) "
        + "  order by t_DrawingDate DESC ";

    sel = DL_RSDCommand(q);

    sel.AddParam(DataSet.FIID);

    ds = sel.Execute();
    if(ds.moveNext())
      if((ds.IncomeRate != 0) or (ds.IncomeVolume != 0))
        state = STATE_EXEC;
      end;
    end;

    AddAction(ACTION_SETINCOMERATE, Ди, state);


    //формируем действие "Формирование сообщения по раскрытию информации о выполненном погашении"
    Ди = GetDateAfterWorkDays(date(DataSet.EventDate), 0);

    CreateOneActionByMsg(Ди,
                         ACTION_FORMMSG_REPAYMENT,
                         DOCKIND_MSG_REPAYMENT,
                         NULL
                        );

  end; 

  //все учетные дейсвия по событию "Погашение купона" 
  macro CreateActions_CoupRetirement()
    var Ди = date(0,0,0);
    var q, ds, sel;
    var state = STATE_NOTEXEC;

    //формируем действие "Установка процентной ставки"
    Ди = GetDateByServ(date(DataSet.EventDate), DataSet.Calendar_Rate, DataSet.InterestRateNumber);

    q =   " select t_IncomeRate, t_IncomeVolume"
        + "   from dfiwarnts_dbt "
        + "  where t_FIID = ? "
        + "    and t_IsPartial = CHR(0) "
        + "    and t_DrawingDate = ? ";

    sel = DL_RSDCommand(q);

    sel.AddParam(DataSet.FIID);
    sel.AddParam(date(DataSet.EventDate));

    ds = sel.Execute();
    if(ds.moveNext())
      if((ds.IncomeRate != 0) or (ds.IncomeVolume != 0))
        state = STATE_EXEC;
      end;
    end;

    AddAction(ACTION_SETINCOMERATE, Ди, state);
  end;


  //все учетные дейсвия по событию "Выплата денежных средств владельцам облигаций" 
  macro CreateActions_OwnPayment()
    var Ди = date(0,0,0);
    var q, ds, sel;
    var Number_Coupon = NULL;
    var state = STATE_NOTEXEC;

    q =   " select t_Number "
        + "   from dfiwarnts_dbt "
        + "  where t_FIID = ? "
        + "    and t_IsPartial = CHR(0) "
        + "    and t_DrawingDate <= ? "
        + "  order by t_DrawingDate desc ";

    sel = DL_RSDCommand(q);

    sel.AddParam(DataSet.FIID);
    sel.AddParam(DataSet.EventDate);

    ds = sel.Execute();
    if(ds.moveNext())
      Number_Coupon = ds.Number;
    else
      Number_Coupon = "";
    end;

    //формируем действие "Формирование сообщения по раскрытию информации в новостные ленты о выплаченных доходах"
    Ди = date(DataSet.EventDate);
    
    CreateOneActionByMsg(Ди,
                         ACTION_FORMMSG_PAYINCOME,
                         DOCKIND_MSG_PAYINCOME,
                         Number_Coupon
                        );

    //формируем действие "Формирование сообщения по раскрытию информации в НРД по форме 12.4"
    Ди = GetDateByServ(date(DataSet.EventDate), DataSet.Calendar_Web, DataSet.WebSiteNumber);

    CreateOneActionByMsg(Ди,
                         ACTION_FORMMSG_F12_4,
                         DOCKIND_FORM12_4,
                         Number_Coupon
                        );

    //формируем действие "Формирование сообщения по раскрытию информации в НРД по форме 12.6"
    Ди = date(DataSet.EventDate);

    CreateOneActionByMsg(Ди,
                         ACTION_FORMMSG_F12_6,
                         DOCKIND_FORM12_6,
                         Number_Coupon
                        );

    //формируем действие "Перевод денежных средств для выплаты владельцам"
    Ди = GetDateByServ(date(DataSet.EventDate), DataSet.Calendar_Coupon, DataSet.PaymentCouponNumber);
    
    q =   " select t_CommStatus "
        + "   from ddl_comm_dbt "
        + "  where t_DocKind = " + SP_TRANSFERPA
        + "    and t_FIID = ? "
        + "    and t_Number_Coupon = ? ";

    sel = DL_RSDCommand(q);

    sel.AddParam(DataSet.FIID);
    sel.AddParam(Number_Coupon);

    ds = sel.Execute();
    if(ds.moveNext())
      if(ds.CommStatus == DL_COMM_CLOSED)
        state = STATE_EXEC;
      end;
    end;

    AddAction(ACTION_TRANSFER_PA, Ди, state);

  end;


  //точка входа
  
  query =   " select ev.t_EventKind, ev.t_EventDate, ev.t_FIID, "
          + "        serv.t_WebSiteNumber, serv.t_Calendar_Web,"
          + "        serv.t_NewsTapeNumber, serv.t_Calendar_News, "
          + "        serv.t_InterestRateNumber, serv.t_Calendar_Rate, "
          + "        serv.t_PaymentCouponNumber, serv.t_Calendar_Coupon "
          + "   from dscownevents_tmp ev, davrserv_dbt serv  "
          + "  where ev.t_Type = " + TYPE_EVENT
          + "    and serv.t_FIID = ev.t_FIID "
          + " order by ev.t_EventDate asc";

  cmd = DL_RSDCommand(query);

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "Определение учетных действий", "Определение учетных действий");


    DataSet = cmd.Execute();
    while(DataSet.moveNext())

      if(DataSet.EventKind == EVENT_FIXOWNERSLIST) //Фиксация списка владельцев
        CreateActions_FixOwnersList();
      elif(DataSet.EventKind == EVENT_AVRRETIREMENT) //Погашение выпуска
        CreateActions_AvrRetirement();
      elif(DataSet.EventKind == EVENT_COUPRETIREMENT) //Погашение купона
        CreateActions_CoupRetirement();
      elif(DataSet.EventKind == EVENT_OWNPAYMENT) //Выплата денежных средств владельцам облигаций
        CreateActions_OwnPayment();
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();

    owneventCache.Insert();
  end;

end;

//Список предстоящих событий в обслуживании эмитентом выпусков ценных бумаг
PRIVATE MACRO CreateRepListEvents(FIID:integer, BegDate:date, EndDate:date)
  var cnt = 0, i = 0;
  var curOutput,prevOutput;
  var query, cmd, DataSet;
  var sizeHeader = 109;
  var sizeH1_1 = 15, sizeH1_2 = 45, sizeH1_3 = 46;
  
  if (not IsExcel)
    curOutput = GetTxtFileName("scownevents");
    prevOutput = SetOutput(curOutput);
  end;
  
  var Rep = CMakeReport();

  Rep.AddPrintCell("Список предстоящих событий в обслуживании эмитентом выпусков ценных бумаг", sizeHeader, 0, "c:ex_FS(b)", REP_ELEM_STR);
  Rep.AddStr();
  Rep.AddPrintCell("за период "+string(BegDate:f)+" - "+string(EndDate:f), sizeHeader, 0, "c:ex_FS(b)", REP_ELEM_STR);
  Rep.AddStr();
  Rep.AddEmptyStr(1, sizeHeader);


  LoadEventsTmp(BegDate, EndDate, FIID);

  query =   " select ev.t_EventDate, ev.t_EventKind, ev.t_FIID, fin.t_Name as FiName "
          + "   from dscownevents_tmp ev, dfininstr_dbt fin "
          + "  where ev.t_Type = " + TYPE_EVENT
          + "    and fin.t_FIID = ev.t_FIID "
          + "  order by ev.t_EventDate ASC, ev.t_EventKind ASC, ev.t_FIID ASC ";

  cmd = DL_RSDCommand(query);

  cnt = cmd.GetCount();
  if(cnt > 0)
    InitProgress(cnt, "Подготовка отчета", "Подготовка отчета");

    Rep.AddPrintCell("Дата", sizeH1_1, 0, "l:ex_FS(b)");
    Rep.AddPrintCell("Событие", sizeH1_2, 0, "l:w:ex_FS(b)");
    Rep.AddPrintCell("Ценная бумага", sizeH1_3, 0, "l:ex_FS(b)");
    Rep.AddStr();

    DataSet = cmd.Execute();
    while(DataSet.moveNext())
        Rep.AddPrintCell(string(date(DataSet.EventDate):f), sizeH1_1, 0, "l");
        Rep.AddPrintCell(EventNames[DataSet.EventKind], sizeH1_2, 0, "l:w");
        Rep.AddPrintCell(DataSet.FiName, sizeH1_3, 0, "l:w");
        Rep.AddStr();

      UseProgress(i = i + 1);
    end;

    RemProgress();
  else
    Rep.AddPrintCell("В указанный период нет событий", sizeHeader, 0, "l", REP_ELEM_STR);
    Rep.AddStr();
  end;

  if (not IsExcel)
    Rep.PrintRep();
    SetOutput(prevOutput);
    ViewFile(curOutput);
  else
    Rep.PrintWinRep();
    Rep.ShowWinRep();
  end;
END;

//События и связанные с ними учетные действия в обслуживании эмитентом выпусков ценных бумаг
PRIVATE MACRO CreateRepNotifyActions(FIID:integer, BegDate:date, EndDate:date)
  var cnt = 0, cnt_e, cnt_a, i = 0;
  var curOutput,prevOutput;
  var Rep;
  var StartDate, LastDate, NDays = 0;
  var query, query_e, query_a, cmd, cmd_e, cmd_a, DataSet, ds;
  var str = "";
  var sizeHeader = 130;
  var sizeH1_1 = 15, sizeH1_2 = 45, sizeH1_3 = 46;
  var sizeH2_1 = 30, sizeH2_2 = 30, sizeH2_3 = 15, sizeH2_4 = 30, sizeH2_5 = 20;
  var printHeader = false;

  if (not IsExcel)
    curOutput = GetTxtFileName("scownevents");
    prevOutput = SetOutput(curOutput);
  end;

  Rep = CMakeReport();

  Rep.AddPrintCell("События и связанные с ними учетные действия в обслуживании эмитентом выпусков", sizeHeader, 0, "c:ex_FS(b)", REP_ELEM_STR);
  Rep.AddStr();
  Rep.AddPrintCell("ценных бумаг с "+string(BegDate:f)+" по "+string(EndDate:f), sizeHeader, 0, "c:ex_FS(b)", REP_ELEM_STR);
  Rep.AddStr();


  query =   " select NVL(MAX(GREATEST(t_WebSiteNumber, t_NewsTapeNumber, t_InterestRateNumber, t_PaymentCouponNumber)), 0) as NDays"
          + "   from davrserv_dbt";

  cmd = DL_RSDCommand(query);
  ds = cmd.Execute();
  if(ds.moveNext())
    NDays = int(ds.NDays);
  end;

  LoadEventsTmp(BegDate, GetDateAfterWorkDays( EndDate, NDays), FIID); 

  LoadActionsTmp(BegDate, EndDate, FIID);

  query =   " select q.StartDate "
          + "   from (select DISTINCT RSI_RsbCalendar.GetDateAfterWorkDay(t_EventDate, DECODE(RSI_RsbCalendar.IsWorkDay(t_EventDate), 0, -1, 0)) as StartDate "
          + "           from dscownevents_tmp "
          + "          where t_Type = " + TYPE_EVENT
          + "            and t_EventDate >= ? "
          + "            and t_EventDate <= ? "
          + "         UNION "
          + "         select DISTINCT RSI_RsbCalendar.GetDateAfterWorkDay(t_ActionDate, DECODE(RSI_RsbCalendar.IsWorkDay(t_ActionDate), 0, -1, 0)) as StartDate " 
          + "           from dscownevents_tmp "
          + "          where t_Type = " + TYPE_ACTION
          + "            and t_ActionDate >= ? "
          + "            and t_ActionDate <= ? "
          + "        ) q "
          + "  order by q.StartDate ASC ";

  cmd = DL_RSDCommand(query);

  cmd.AddParam(BegDate);
  cmd.AddParam(EndDate);
  cmd.AddParam(BegDate);
  cmd.AddParam(EndDate);

  cnt = cmd.GetCount();
  if(cnt > 0 )
    ds = cmd.Execute();

    InitProgress(cnt, "Подготовка отчета", "Подготовка отчета");

    while(ds.moveNext())
      
      StartDate = date(ds.StartDate);

      LastDate = GetDateAfterWorkDays(StartDate, 0);
      if(LastDate > EndDate)
        LastDate = EndDate;
      end;

      Rep.AddEmptyStr(1, sizeHeader);
      Rep.AddPrintCell("Операционная дата: "+string(StartDate:f), sizeH1_1+sizeH1_2+2, 0, "l:ex_FS(b):ex_FC("+COLOR_RED+")", REP_ELEM_STR);
      Rep.AddStr();

      //События
      query_e =   " select ev.t_EventDate, ev.t_EventKind, fin.t_Name as FiName "
                + "   from dscownevents_tmp ev, dfininstr_dbt fin "
                + "  where ev.t_Type = " + TYPE_EVENT
                + "    and ev.t_EventDate >= ? "
                + "    and ev.t_EventDate <= ? "
                + "    and fin.t_FIID = ev.t_FIID "
                + "  order by ev.t_EventDate ASC, ev.t_FIID ASC, ev.t_EventKind ASC";

      cmd_e = DL_RSDCommand(query_e);
      cmd_e.AddParam(StartDate);
      cmd_e.AddParam(LastDate);

      DataSet = cmd_e.Execute();
      
      printHeader = false;
      while(DataSet.moveNext())
        if(printHeader == false)
          Rep.AddPrintCell("События", sizeH1_1+1, 0, "l:ex_FS(b)", REP_ELEM_STR);
          Rep.AddStr();

          Rep.AddPrintCell("Дата", sizeH1_1, 0, "l:ex_FS(b)");
          Rep.AddPrintCell("Событие", sizeH1_2, 0, "l:w:ex_FS(b)");
          Rep.AddPrintCell("Ценная бумага", sizeH1_3, 0, "l:ex_FS(b)");
          Rep.AddStr();

          printHeader = true;
        end;

        Rep.AddPrintCell(string(date(DataSet.EventDate):f), sizeH1_1, 0, "l");
        Rep.AddPrintCell(EventNames[DataSet.EventKind], sizeH1_2, 0, "l:w");
        Rep.AddPrintCell(DataSet.FiName, sizeH1_3, 0, "l:w");
        Rep.AddStr();
      end;

      //Учетные действия
      query_a =   " select ev.t_EventDate, ev.t_EventKind, ev.t_ActionKind, ev.t_ActionState, fin.t_Name as FiName "
                + "   from dscownevents_tmp ev, dfininstr_dbt fin "
                + "  where ev.t_Type = " + TYPE_ACTION
                + "    and ev.t_ActionDate <= ? "
                + "    and fin.t_FIID = ev.t_FIID "
                + "    and (ev.t_ActionDate >= ? or (ev.t_ActionDate < ? and ev.t_ActionState = " + STATE_OVERDUE + "))";

      cmd_a = DL_RSDCommand();
      cmd_a.AddParam(LastDate);
      cmd_a.AddParam(BegDate);
      cmd_a.AddParam(BegDate);

      if(StartDate > BegDate)
        query_a = query_a + "    and ev.t_ActionDate >= ? ";
        cmd_a.AddParam(StartDate);
      end;

      query_a = query_a + "  order by ev.t_FIID ASC, ev.t_ActionDate ASC, ev.t_ActionKind ASC";

      DataSet = cmd_a.Execute(query_a);
      
      printHeader = false;
      while(DataSet.moveNext())
        if(printHeader == false)
          Rep.AddPrintCell("Учетные действия", sizeH2_1 + 1, 0, "l:ex_FS(b)", REP_ELEM_STR);
          Rep.AddStr();

          Rep.AddPrintCell("Учетное действие", sizeH2_1, 0, "l:ex_FS(b)");
          Rep.AddPrintCell("Событие, в связи с которым выполняется учетное действие", sizeH2_2, 0, "l:w:ex_FS(b)");
          Rep.AddPrintCell("Дата события", sizeH2_3, 0, "l:ex_FS(b)");
          Rep.AddPrintCell("Ценная бумага", sizeH2_4, 0, "l:ex_FS(b)");
          Rep.AddPrintCell("Состояние действия", sizeH2_5, 0, "l:w:ex_FS(b)");
          Rep.AddStr();

          printHeader = true;
        end;

        Rep.AddPrintCell(ActionNames[DataSet.ActionKind], sizeH2_1, 0, "l");
        Rep.AddPrintCell(EventNames[DataSet.EventKind], sizeH2_2, 0, "l:w");
        Rep.AddPrintCell(string(date(DataSet.EventDate):f), sizeH2_3, 0, "l:w");
        Rep.AddPrintCell(DataSet.FiName, sizeH2_4, 0, "l:w");
        Rep.AddPrintCell(StateNames[DataSet.ActionState], sizeH2_5, 0, "l:w");
        Rep.AddStr();            
      end;

      UseProgress(i = i + 1);
    end;

    RemProgress();
  end;

  after_44_footer(Rep, sizeHeader);

  if (not IsExcel)
    Rep.PrintRep();
    SetOutput(prevOutput);
    ViewFile(curOutput);
  else
    Rep.PrintWinRep();
    Rep.ShowWinRep();
  end;
END;


private var Form = SCOwnEvents_Panel();

while( Form.Run() )
  var FIID             = Form.GetFieldValue(PNFLD_OWNEVENTS_FICODE);
  var BegDate          = Form.GetFieldValue(PNFLD_OWNEVENTS_BEGDATE);
  var EndDate          = Form.GetFieldValue(PNFLD_OWNEVENTS_ENDDATE);
  var RepListEvents    = Form.GetFieldValue(PNFLD_OWNEVENTS_REPLISTEVENTS);
  var RepNotifyActions = Form.GetFieldValue(PNFLD_OWNEVENTS_REPNOTIFYACTIONS);

  SQL_Truncate("DSCOWNEVENTS_TMP");

  if(RepListEvents == SET_CHAR)
    CreateRepListEvents(FIID, BegDate, EndDate);
  else
    CreateRepNotifyActions(FIID, BegDate, EndDate);
  end;
end;

exit(1);