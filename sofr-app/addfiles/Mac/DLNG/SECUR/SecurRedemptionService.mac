/* SecurRedemptionService.mac
*/
import "SqlResultConvertHelper.mac", oralib;
import "SecurRedemptionStatuses.mac", "FuncObjController.mac";

class SecurRedemptionStruct
    var redemptionId:integer;
    var fixDate:date;
    var completeDate:date;
    var sourceNumber:string;
    var sourceId:numeric;
    var actionType:string;
    var listOwnersDate:date;
    var depositaryId:integer;
    var isin:string; 
    var regNumber:string;
    var nrdCode:string;
    var quantity:money;
    var dealId:integer;
    var statusId:integer;
    var createdTime:datetime;
    var updatedTime:datetime;

    macro IsCompleteDateReached(dt:date):bool
        return ((completeDate != null) and
                (completeDate != Date(0, 0, 0)) and
                (completeDate <= dt)
                );
    end;
end;

class SecurRedemptionService
    private const FuncObjCode = "SecRedemptGlobal";

    private var sqlConverter:SqlResultConvertHelper = SqlResultConvertHelper();
    private var funcObj:FuncObjController;

    macro FindById(id:integer):SecurRedemptionStruct
        var redemption:SecurRedemptionStruct = SecurRedemptionStruct;
        var query = "select redemption_id, "
                  + "       fix_date, "
                  + "       complete_date, "
                  + "       source_number, "
                  + "       source_id, "
                  + "       action_type, "
                  + "       list_owners_date, "
                  + "       depositary_id, "
                  + "       isin, "
                  + "       reg_number, "
                  + "       nrd_code, "
                  + "       quantity, "
                  + "       deal_id, "
                  + "       status_id, "
                  + "       created_time, "
                  + "       updated_time "
                  + "  from secur_redemptions "
                  + " where redemption_id = :id";
        var sql = ExecSQLselectPrmDyn(query, id);

        if (sql.MoveNext())
            redemption.redemptionId   = sql.value("redemption_id");
            redemption.fixDate        = sqlConverter.getDate(sql.value("fix_date"));
            redemption.completeDate   = sqlConverter.getDate(sql.value("complete_date"));
            redemption.sourceNumber   = sql.value("source_number");
            redemption.sourceId       = sql.value("source_id");
            redemption.actionType     = sql.value("action_type");
            redemption.listOwnersDate = sqlConverter.getDate(sql.value("list_owners_date"));
            redemption.depositaryId   = sql.value("depositary_id");
            redemption.isin           = sqlConverter.orNull(sql.value("isin"));
            redemption.regNumber      = sqlConverter.orNull(sql.value("reg_number"));
            redemption.nrdCode        = sqlConverter.orNull(sql.value("nrd_code"));
            redemption.quantity       = sql.value("quantity");
            redemption.dealId         = sql.value("deal_id");
            redemption.statusId       = sql.value("status_id");
            redemption.createdTime    = sqlConverter.getDateTime(sql.value("created_time"));
            redemption.updatedTime    = sqlConverter.getDateTime(sql.value("updated_time"));
        end;

        return redemption;
    end;

    macro Save(redemption:SecurRedemptionStruct):SecurRedemptionStruct
        var query = "update secur_redemptions "
                  + "   set complete_date = :completeDate, "
                  + "       list_owners_date = :listOwnersDate, "
                  + "       depositary_id = :depositaryId, "
                  + "       isin = :isin, "
                  + "       reg_number = :regNumber, "
                  + "       nrd_code = :nrdCode, "
                  + "       quantity = :quantity, "
                  + "       deal_id = :dealId, "
                  + "       updated_time = systimestamp "
                  + " where redemption_id = :id ";
        
        var params = MakeSqlParamsArray(redemption.completeDate,
                                        redemption.listOwnersDate,
                                        redemption.depositaryId,
                                        redemption.isin,
                                        redemption.regNumber,
                                        redemption.nrdCode,
                                        redemption.quantity,
                                        redemption.dealId,
                                        redemption.redemptionId);
        ExecSql(query, params);

        redemption = FindById(redemption.redemptionId); //чтобы обновилась updatedTime
        return redemption;
    end;

    //Отдельно обновляется статус, чтобы можно было проще сохранять историю обновления статусов
    macro SaveStatus(redemption:SecurRedemptionStruct, statusId:integer):SecurRedemptionStruct
        var query = "begin "
                  + "    secur_redemption_utils.save_status(p_redemption_id => :id, "
                  + "                                       p_status_id     => :statusId); "
                  + "end; ";
        var params = MakeSqlParamsArray(redemption.redemptionId, statusId);
        ExecSql(query, params);

        redemption = FindById(redemption.redemptionId); //чтобы обновилась updatedTime
        return redemption;
    end;

    macro getPrevStatus(redemption:SecurRedemptionStruct)
        var query = ""
                + "  select old_status "
                + "    from secur_redemption_status_hist "
                + "  where redemption_id = :redId "
                + "  order by update_time desc ";
        var sql = ExecSQLselectPrmDyn(query, redemption.redemptionId);
        if (sql.MoveNext())
            return sqlConverter.orNull(sql.value("old_status"));
        end;

        return null;
    end;

    //todo: вместо вот такой дички можно в таблице со статусами сделать поле
    //is_cancellation_allowed и брать значение оттуда
    macro isCancellationAllowed(redemption:SecurRedemptionStruct):bool
        var statuses:SecurRedemptionStatuses = SecurRedemptionStatuses();
        var prevStatus = getPrevStatus(redemption);

        return (   (prevStatus == null)
                or (prevStatus == statuses.CREATED)
                or (prevStatus == statuses.SPREAD_CREATED)
                or (prevStatus == statuses.SUMS_SPREADED)
                or (prevStatus == statuses.WAIT_FOR_COMPLETE_DATE)
                or (prevStatus == statuses.AFTER_FIX_DATE)
        );
    end;

    macro unlinkDealId(redemption:SecurRedemptionStruct):SecurRedemptionStruct
        redemption.dealId = -1;
        return Save(redemption);
    end;

    macro GetRedemptionsWithStatus(statusId:integer) //TArray<SecurRedemptionStruct>
        var redemptionList = TArray();
        var query = "select redemption_id "
                + "  from secur_redemptions "
                + " where status_id = :statusId ";
        var sql = ExecSQLselectPrmDyn(query, statusId);

        while (sql.MoveNext())
            redemptionList[redemptionList.size()] = FindById(sql.value("redemption_id"));
        end;

        return redemptionList;
    end;

    macro PushToFuncObj(redemptionId)
        funcObj.SaveByCode(redemptionId, FuncObjCode);
    end;
end;