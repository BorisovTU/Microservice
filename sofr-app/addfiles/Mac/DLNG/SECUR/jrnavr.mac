/*******************************************************************************
 FILE         :   jrnavr.mac

 DESCRIPTION  :   Отчет "Регистр внутреннего учета ценных бумаг". (ФКЦБ)

 PROGRAMMED BY:   Леонов И.Е.

 CREATION DATE:   27.03.03
*******************************************************************************/
import SecInter, "spserv.mac", Календарь, "spRepFun.mac", "DLReport_h.mac";
import RsbDataSet;

private var JrnAvrTmp; /*Временный файл отчета*/

/*Какой отчет выполняется*/
const AVRACCOUNT_REPORT    = 0, /*Счета учета ценных бумаг*/
      CLIENTACCOUNT_REPORT = 1; /*Счета расчетов с клиентами по ценных бумаг*/

private var Dprt    = TBFile( "dp_dep",   "R", 0 );   /*филиал*/
private var Deal    = TBFile( "dl_tick",  "R", 0 );   /*сделка*/
private var AvrPaym;  /*TRsbDataSet платеж*/
private var Number = 0;   /*номер записи во временном файле.*/
private var Corschem= TBFile( "corschem", "R", 3 );   /*корр. схема*/
private var SfContr = TBFile( "sfcontr", "R", 0 );    /*договор обслуживания*/
private var FI      = TRecHandler( "fininstr" );   /*бумага*/
private var FI_KindName = "";
private var Avoir   = TRecHandler( "avoiriss" );   /*бумага*/
private var PmProp  = TRecHandler( "pmprop" );     /*свойства платежа*/
private var Party   = TRecHandler( "party" );      /*клиент*/
private var Issuer  = TRecHandler( "party" );      /*эмитент*/
private var Depos   = TRecHandler( "party" );      /*депозитарий*/
private var wrtcalc = TRecHandler( "pmwrtsum" );   /*для подсчета остатков*/

private var WrtDeal = TBFile( "dl_tick",  "R", 0 );   /*сделка*/
private var WrtPaym = TBFile( "pmpaym",   "R", 0 );   /*платеж*/
record WrtSum( "pmwrtsum" );      /*лот*/

var _Data; /*SourceData*/
var DepID:integer;

/* Структура с исходными данными */
class SourceData( _ReportNum:Integer, _DprtID:integer, _dateMin: date, 
                  _dateMax: date, _ClientID: integer, _DepositaryID: integer, 
                  _EmiID: integer,_AvrKind: integer, _AvrID: integer,
                  _Contract: integer,_IsApp: bool, _IsExcel: bool )

var ReportNum     = _ReportNum,
    DprtID        = _DprtID,          
    dateMin       = _dateMin,         
    dateMax       = _dateMax,         
    ClientID      = _ClientID,          
    DepositaryID  = _DepositaryID,      
    EmiID         = _EmiID,             
    AvrKind       = _AvrKind,           
    AvrID         = _AvrID,             
    Contract      = _Contract,          
    IsApp         = _IsApp,
    IsExcel       = _IsExcel;

var ReportName:String,       /*Вид отчета*/
    CurDepositaryID:Integer, /*Место учета ц/б*/
    ReportRun:bool = false,
    LineSize:integer,

    InRest:Money,
    PrintDprt:Integer            = null,
    PrintAccount:Integer         = null,
    PrintSubAccount:Integer      = null,
    PrintAnaliticAccount:Integer = null;
end;


var TableHeader1 = "┌────────┬──────────────┬──────────────────────────────┬──────────────────────────────┬────────────────┬────────────────┬────────────────┐\n"+
                   "│  Дата  │ Наименование │         Вид и № сделки       │        Вид, № первичного     │      Вход.     │   Количество   │      Исх.      │\n"+
                   "│расчетн.│   расчетной  │        (операции)с ц/б       │            документа         │  остаток, шт.  │     ц/б по     │  остаток, шт.  │\n"+
                   "│операции│   операции   │                              │                              │                │    операции    │                │\n"+
                   "├────────┼──────────────┼──────────────────────────────┼──────────────────────────────┼────────────────┼────────────────┼────────────────┤";

var TableHeader2 = "┌────────┬──────────────┬──────────────────────────────┬──────────────────────────────┬────────────────┬────────────────┬────────────────┬────────────────┬────────────────┐\n"+
                   "│  Дата  │ Наименование │         Вид и № сделки       │        Вид, № первичного     │      Вход.     │Вход.остаток по │   Количество   │Исход.остаток по│    Исходящий   │\n"+
                   "│расчетн.│   расчетной  │        (операции)с ц/б       │            документа         │  остаток, по   │ обязательствам │     ц/б по     │ обязательствам │   остаток, шт  │\n"+
                   "│операции│   операции   │                              │                              │    ц/б, шт.    │ клиента перед  │    операции    │  клиента перед │                │\n"+
                   "│        │              │                              │                              │                │   банком, шт   │                │    банком, шт  │                │\n"+
                   "├────────┼──────────────┼──────────────────────────────┼──────────────────────────────┼────────────────┼────────────────┼────────────────┼────────────────┼────────────────┤";

PRIVATE VAR Cap =
"Филиал: ##############\n"+
"                                               Регистр внутреннего учета ценных бумаг\n"+
"                                               ###########################################################\n"+
"                                               Отчетный период с  ############ по ############\n\n\n";

PRIVATE VAR AccountStr =
"Счет внутреннего учета: ###################################################################################################\n";

PRIVATE VAR SubAccountStr =
"Субсчет: Ценная бумага: ########################################  Код ц/б: ###############################\n"+
"Эмитент: ##############################  Вид: ###############################  Выпуск: ################  Серия/транш: ###################\n";

PRIVATE VAR AnAccountStr =
"Аналитический счет: #########################################################\n";

PRIVATE VAR ReportRunFalse =
"#############################################################################################\n";

PRIVATE CLASS (DL_CReportTemplate) SP_Jrnavr_Report (_Data:SourceData)

  private var Data:SourceData = _Data; 
  private var TableRegister = false;

  PRIVATE MACRO PrintMode():INTEGER
     if(Data.IsExcel)
        return DL_OUTREPORT_EXCEL;
     else
        return DL_OUTREPORT_STD;
     end;
  END;                                                                                                                                                                         
  
  PRIVATE MACRO BeginReport()
     CreateTotalBook();     
     SetActiveSheet( "0101" );
  END;

  PRIVATE MACRO EndReport()
     SaveTotalBook();
  END;

  PRIVATE MACRO EndCopyTable()
     if( TableRegister == true )
        EndTable();
        CopyAllSheetInTotalBook( NULL, false, GetTableRange(), 0 );
        TableRegister = false;
     end;
  END;

  PRIVATE MACRO GetInRest( FIID:INTEGER, ValueDate:date )
     var ClientID;
    
     if( IsClientDeal(Deal.rec) )
        ClientID = Deal.rec.ClientID;
     else
        ClientID = {OurBank};
     end;
    
     wrtcalc.Clear();
     if(not WRT_TotalCalc( DepID,
                           FIID,
                           ClientID,
                           0,
                           -1,
                           ValueDate-1,
                           time(0,0,0),
                           date(0,0,0),
                           wrtcalc,
                           true,
                           false )
       )
        MsgBox( "Ошибка при определении остатка ц/б" );
        Data.InRest = 0;
     else
        Data.InRest = wrtcalc.rec.Amount;
     end;
  END;

  PRIVATE MACRO PrnSum( Sum:Variant )
     if( Data.IsApp )
        return string(Sum:a);
     end;
     return Sum;
  END;

  PRIVATE MACRO PrintLine1 ( ValueDate:string, OperName:string, DealName:string, GroundName:string, InRest:string,
                     Amount:string, OutRest:string )

          PrintTableLine( "N1_A1",  ValueDate,       null,
                          "N1_A2",  OperName,        null,
                          "N1_A3",  DealName,        null,
                          "N1_A4",  GroundName,      null,
                          "N1_A5",  InRest,          null,
                          "N1_A6",  Amount,          null,
                          "N1_A7",  OutRest,         null
                   );
  END;

  PRIVATE MACRO PrintLine2 ( ValueDate:string, OperName:string, DealName:string, GroundName:string, InRest:string,
                    InRestCom:string, Amount:string, OutRestCom:string, OutRest:string )

          PrintTableLine( "N1_A11",  ValueDate,       null,
                          "N1_A22",  OperName,        null,
                          "N1_A33",  DealName,        null,
                          "N1_A44",  GroundName,      null,
                          "N1_A55",  InRestCom,       null,
                          "N1_A66",  InRest,          null,
                          "N1_A77",  Amount,          null,
                          "N1_A88",  OutRestCom,      null,
                          "N1_A99",  OutRest,         null
                   );
  END;

  PRIVATE MACRO PrintDprt()

     Dprt.Clear();
     if( JrnAvrTmp.rec.Department > 0 )
        Dprt.rec.Code = JrnAvrTmp.rec.Department;
        if( not Dprt.GetEQ() )
           msgbox( "Ошибка при определении филиала." );
           return;
        end;
     end;
     Data.PrintDprt = JrnAvrTmp.rec.Department;

     PrintFormatString( Cap,
                        "N1_H0_1", Dprt.rec.name,
                        "N1_H0_2", Data.ReportName,
                        "N1_H0_3", Data.dateMin,
                        "N1_H0_4", Data.dateMax
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_Cap", 1 );

  END;

  PRIVATE MACRO PrintAccount()
     var AccName = "";
    
     if( Data.PrintAccount != JrnAvrTmp.rec.Account )
        if( Data.ReportNum == AVRACCOUNT_REPORT )
           if( JrnAvrTmp.rec.Account > 0 )
              SfContr.Clear();
              SfContr.rec.Id = JrnAvrTmp.rec.Account;
              if( not SfContr.GetEQ() )
                 msgbox( "Ошибка при определении договора обслуживания клиента." );
                 return;
              end;
    
              if( ПолучитьСубъекта( SfContr.rec.PartyID, Party) )
                  msgbox( "Ошибка при определении клиента по договору обслуживания." );
                  return;
              end;
           end;
    
        elif( Data.ReportNum == CLIENTACCOUNT_REPORT )
           if( ПолучитьСубъекта( JrnAvrTmp.rec.Account, Party) )
               msgbox( "Ошибка при определении клиента." );
               return;
           end;
        end;
    
        Data.PrintAccount = JrnAvrTmp.rec.Account;
     end;
    
     if( Data.ReportNum == AVRACCOUNT_REPORT )
        if( JrnAvrTmp.rec.Account > 0 )
           AccName = "\"Ценные бумаги клиента\"      Клиент:" + Party.rec.ShortName +
                     "/договор № " + string(SfContr.rec.Number) + 
                     " от " + string(SfContr.rec.DateConc:f);
        else
           AccName = "\"Ценные бумаги банка\"";
        end;
    
     elif( Data.ReportNum == CLIENTACCOUNT_REPORT )
        AccName = "\"Расчеты с клиентами по ценным бумагам\"      Клиент:" + 
                     Party.rec.ShortName;
     end;

     PrintFormatString( AccountStr,
                        "N1_H0_5", AccName
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_AccountStr", 1 );

  END;

  PRIVATE MACRO PrintSubAccount()
     var Name;
  
     if( Data.PrintSubAccount != JrnAvrTmp.rec.SubAccount )
  
        if( ПолучитьФинИн( JrnAvrTmp.rec.SubAccount, FI, Avoir ) ) 
           MsgBox( "Ошибка при определении финансового инструмента." );
           return;
        end;
        FI_KindName = AvoirKindName(FI.rec.AvoirKind);
  
        if( ПолучитьСубъекта( FI.rec.Issuer, Issuer ) )
            msgbox( "Ошибка при определении эмитента." );
            return;
        end;
        Data.PrintSubAccount = JrnAvrTmp.rec.SubAccount;
     end;

     PrintFormatString( SubAccountStr,
                        "N1_H0_6", FI.rec.Name,
                        "N1_H0_7", FI.rec.FI_Code,
                        "N1_H0_8", Issuer.rec.ShortName,
                        "N1_H0_9", FI_KindName,
                        "N1_H0_10", Avoir.rec.Issue,
                        "N1_H0_11", Avoir.rec.Series + "/" + Avoir.rec.Tranche
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_SubAccountStr", 1 );
  END;

  PRIVATE MACRO RegTable()
     if( Data.ReportNum == AVRACCOUNT_REPORT )
        CopyAllSheetInTotalBook( NULL, false, "N1_TableHeader1", 0 );
        RegisterTable( "N1_MainTable1", TableHeader1,
                       "N1_A1",
                       "N1_A2",
                       "N1_A3",
                       "N1_A4",
                       "N1_A5",
                       "N1_A6",
                       "N1_A7"
                     );
        TableRegister = true;
     elif( Data.ReportNum == CLIENTACCOUNT_REPORT )
        CopyAllSheetInTotalBook( NULL, false, "N1_TableHeader2", 0 );
        RegisterTable( "N1_MainTable2", TableHeader2,
                       "N1_A11",
                       "N1_A22",
                       "N1_A33",
                       "N1_A44",
                       "N1_A55",
                       "N1_A66",
                       "N1_A77",
                       "N1_A88",
                       "N1_A99"
                     );
        TableRegister = true;
     end;
  END;

  PRIVATE MACRO PrintAnaliticAccount()
     var Name, Place;
    
     if( Data.PrintAnaliticAccount != JrnAvrTmp.rec.AnaliticAccount )
    
        if( Data.ReportNum == AVRACCOUNT_REPORT )
           if( ПолучитьСубъекта( JrnAvrTmp.rec.AnaliticAccount, Depos ) )
               msgbox( "Ошибка при определении места хранения ц/б." );
               return;
           else
              /*надо например для случая когда AnaliticAccount == 0 (наш банк)*/                                                            
              /*так как например при проверках в фильтре "JrnAvrLotFilter" подразумевается что наш банк = 0, а не конкретный PartyID из Depos*/
              Depos.rec.PartyID = JrnAvrTmp.rec.AnaliticAccount; 
           end;
    
        elif( Data.ReportNum == CLIENTACCOUNT_REPORT )
           SfContr.Clear();
           SfContr.rec.Id = JrnAvrTmp.rec.AnaliticAccount;
           if( not SfContr.GetEQ() )
              msgbox( "Ошибка при определении договора обслуживания клиента." );
              return;
           end;
        end;
        Data.PrintAnaliticAccount = JrnAvrTmp.rec.AnaliticAccount;
     end;
    
     if( Data.ReportNum == AVRACCOUNT_REPORT )
    
        if( JrnAvrTmp.rec.Account > 0 )
           Place = "клиента"
        else
           Place = "банка"
        end;
    
        Name = "Ценные бумаги " + Place + " в " + Depos.rec.ShortName;
    
     elif( Data.ReportNum == CLIENTACCOUNT_REPORT )
        Name = "Ценные бумаги клиента по договору " +
               string(SfContr.rec.Number) + 
               " от " + string(SfContr.rec.DateConc:f);
     end;

     PrintFormatString( AnAccountStr,
                        "N1_H0_12", Name
                      );
     CopyAllSheetInTotalBook( NULL, false, "N1_AnAccountStr", 0 );
  END;

  PRIVATE MACRO PrintOperation( RecalcInRest:bool  )
     var IsBack, DocNum, DealType, OperName, DealName, GroundName, InRest, 
         InRestCom, Amount, OutRest, OutRestCom, IsPartyClient = false;
     var AvrPaym = TRecHandler( "pmpaym" );   /*платеж*/
    
     Deal.Clear();
     Deal.rec.DealId = JrnAvrTmp.rec.DealID;
     if( not Deal.GetEQ() )
        msgbox( "Ошибка при определении сделки." );
        return;
     end;
    
     if( not GetPaymentInfo( null, 0, AvrPaym, JrnAvrTmp.rec.PaymentID) ) 
        MsgBox( "Ошибка при определении платежа по ценным бумагам|(PaymentID = ", JrnAvrTmp.rec.PaymentID, ")");
        return false;
     end;
    
     if( AvrPaym.rec.Purpose == BAi )
        IsBack = false;
     elif( AvrPaym.rec.Purpose == BRi )
        IsBack = true;
     end;
    
     if( RecalcInRest == true )
        GetInRest( AvrPaym.rec.BaseFIID, AvrPaym.rec.ValueDate );
     end;
    
     InRest = Data.InRest;
     Amount = AvrPaym.rec.Amount; 
     DealType = GetDealBuySale( Deal.rec, IsBack );
    
     /* Определим, является ли эта сделка сделкой контрагента Д
        Лучше, всё же, добавить во временный файл флажок для сделок контрагента Д*/
     if ((Deal.rec.IsPartyClient == SET_CHAR) AND (Deal.rec.PartyContrID != 0))
        if((( Data.ReportNum == AVRACCOUNT_REPORT ) AND 
            ( JrnAvrTmp.rec.Account == Deal.rec.PartyContrID ))
           OR(( Data.ReportNum == CLIENTACCOUNT_REPORT ) AND 
            ( JrnAvrTmp.rec.Account == Deal.rec.PartyID )))
           IsPartyClient = true;
        else 
           IsPartyClient = false;
        end;
     end;
    
     if ( ((not IsPartyClient) AND (DealType == DEAL_TYPE_SALE)) OR 
          ((    IsPartyClient) AND (DealType == DEAL_TYPE_BUY )) )
        OperName = "Списание ц/б";
        OutRest  = InRest - Amount;
     else     
        OperName = "Зачисление ц/б";
        OutRest  = InRest + Amount;
     end; 
     Data.InRest = OutRest;
    
     if( Data.ReportNum == CLIENTACCOUNT_REPORT )
        if( InRest < 0 )
           InRestCom = ABS(InRest);
           InRest    = 0;
        else
           InRestCom  = 0;
        end;
    
        if( OutRest < 0 )
           OutRestCom = ABS(OutRest);
           OutRest    = 0;
        else
           OutRestCom = 0;
        end;
     end;
    
     if (IsPartyClient)
        DealName = ПолучитьВидСделкиКонтрагентаД( GetOpGroup(Deal.rec), IsBack, true )
     else
        DealName = ПолучитьВидОперации( GetOpGroup(Deal.rec), IsBack, true );
     end;
    
     DealName = DealName + " №" + Deal.rec.DealCode;
    
     DocNum = GroundName = "";
     ПарамПодтвДокум( Deal.rec, DocNum, GroundName );
     if( GroundName != "" ) 
        GroundName = GroundName + " №" + DocNum;
     end;
    
     var query, DataSet;
     var dl_tick = TBFile("dl_tick");
    
     dl_tick.rec.DealID = Deal.rec.DealID;
     if(GetEQ(dl_tick))
        if(IsOUTEXCHANGE(GetOpGroup(dl_tick.rec)))
    
           query = RSDCommand(" SELECT SpGround.T_REGISTRTIME, SpGround.T_REGISTRDATE, SpGround.T_XLD " +
                   " FROM dspgrdoc_dbt SpGrDoc, dspground_dbt SpGround " +
                   " WHERE SpGrDoc.T_SOURCEDOCKIND = (select t_bofficekind from ddl_tick_dbt where t_dealid = ? )  and " +
                   " SpGrDoc.T_SOURCEDOCID = ? and " +
                   " SpGround.T_SPGROUNDID = SpGrDoc.T_SPGROUNDID and " +
                   " SpGround.T_KIND = 300");
    
           query.addParam( "", RSDBP_IN, Deal.rec.DealID );
           query.addParam( "", RSDBP_IN, Deal.rec.DealID );
           query.execute();
    
           DataSet = TRSBDataSet(query);
    
           if( DataSet.MoveNext())
              GroundName = GroundName + " отч.об исп.пор.депо" + " № " + string(DataSet.rec.XLD);
           end;
    
           query = RSDCommand(" SELECT SpGround.T_REGISTRTIME, SpGround.T_REGISTRDATE, SpGround.T_XLD " +
                   " FROM dspgrdoc_dbt SpGrDoc, dspground_dbt SpGround " +
                   " WHERE SpGrDoc.T_SOURCEDOCKIND = (select t_bofficekind from ddl_tick_dbt where t_dealid = ? )  and " +
                   " SpGrDoc.T_SOURCEDOCID = ? and " +
                   " SpGround.T_SPGROUNDID = SpGrDoc.T_SPGROUNDID and " +
                   " SpGround.T_KIND = 315");
    
           query.addParam( "", RSDBP_IN, Deal.rec.DealID );
           query.addParam( "", RSDBP_IN, Deal.rec.DealID );
           query.execute();
    
           DataSet = TRSBDataSet(query);
    
           if( DataSet.MoveNext())
              GroundName = GroundName + " подтв.перерег.цб" + " № " + string(DataSet.rec.XLD);
           end;
        end;
     end;
    
     if( Data.ReportNum == AVRACCOUNT_REPORT )
        PrintLine1(date_as_string(1, JrnAvrTmp.rec.ValueDate), OperName, DealName, GroundName, PrnSum(InRest), PrnSum(Amount), PrnSum(OutRest)); 
     else
        PrintLine2(date_as_string(1, JrnAvrTmp.rec.ValueDate), OperName, DealName, GroundName, PrnSum(InRest), PrnSum(InRestCom), PrnSum(Amount), PrnSum(OutRestCom), PrnSum(OutRest)); 
     end;

  END;

  PRIVATE MACRO СохранитьДляОтчета(IsPartyClient)
     JrnAvrTmp.Clear();
     if( IsPartyClient ) /*Для сделок контрагента Д*/
        if( Data.ReportNum == AVRACCOUNT_REPORT )
           JrnAvrTmp.rec.Account          = Deal.rec.PartyContrID;
           JrnAvrTmp.rec.AnaliticAccount  = Data.CurDepositaryID;
        elif( Data.ReportNum == CLIENTACCOUNT_REPORT )
           JrnAvrTmp.rec.Account          = Deal.rec.PartyID;
           JrnAvrTmp.rec.AnaliticAccount  = Deal.rec.PartyContrID;
        else
           msgbox( "Неопределен вид отчета." );
           return false;
        end;
     else
        if( Data.ReportNum == AVRACCOUNT_REPORT )
           JrnAvrTmp.rec.Account          = Deal.rec.ClientContrID;
           JrnAvrTmp.rec.AnaliticAccount  = Data.CurDepositaryID;
        elif( Data.ReportNum == CLIENTACCOUNT_REPORT )
           JrnAvrTmp.rec.Account          = Deal.rec.ClientID;
           JrnAvrTmp.rec.AnaliticAccount  = Deal.rec.ClientContrID;
        else
           msgbox( "Неопределен вид отчета." );
           return false;
        end;
     end;
     JrnAvrTmp.rec.ReportNum  = Data.ReportNum;
     JrnAvrTmp.rec.SubAccount = AvrPaym.rec.BaseFIID;
     JrnAvrTmp.rec.Department = AvrPaym.rec.Department;
    
     JrnAvrTmp.rec.ValueDate = AvrPaym.rec.ValueDate;
    
     /*Раньше Number использовался для сортировки данных по истории лотов. 
       Теперь не нужно, просто заполняем его и не используем. Запрос 124687.
     */
     Number = Number + 1;
     JrnAvrTmp.rec.Number     = Number;
    
     JrnAvrTmp.rec.PaymentID  = int(AvrPaym.rec.PaymentID);
     JrnAvrTmp.rec.DealID     = Deal.rec.DealID;
    
     if( JrnAvrTmp.Insert() )
        Data.LineSize = Data.LineSize + 1;
        return true;
     end;
    
     OnError( er )            
        msgbox( "Ошибка при вставке данных во временный файл отчета (" + er.Code+ "-"+ er.Message +")" );
        RunError();
  END;

  PRIVATE MACRO GetCorschemByAccount( Account, FIID )
    Corschem.Clear;
    Corschem.rec.Account = Account;
    Corschem.rec.FIID    = FIID;
    Corschem.rec.Department = {OperDprt};
  
    if( Corschem.GetEQ() == true )
       return true;
    else
       Corschem.rec.Account = Account;
       Corschem.rec.FIID    = -1;
       Corschem.rec.Department = {OperDprt};
       if( Corschem.GetEQ() == true  )
          return true;
       end;
    end;
    return false;
  END;

  /* IsPartyClient - флаг, производить ли проверку сделки контрагента Д */
  PRIVATE MACRO СделкаПодходит( CheckData:SourceData, CheckDeal:TBFile, CheckAvrPm:VARIANT, IsPartyClient:bool )
     var DealType, IsBack, TypeProp = null, FindAcc = "";
     var FinInstr = TBFile( "fininstr", "R", 0 );
     var query, DataSet, Paymentid;
  
     if( CheckAvrPm.rec.Purpose == BAi )
        IsBack = false;
     elif( CheckAvrPm.rec.Purpose == BRi )
        IsBack = true;
     else
        return false;
     end;
  
     if (IsPartyClient)
        if (CheckDeal.rec.IsPartyClient != SET_CHAR)
           return false;
        else
           if (CheckDeal.rec.PartyContrID == 0)
              return false;
           end;
  
           if( CheckData.ClientID >= 0 )
              if( (CheckData.ClientID != CheckDeal.rec.PartyID) AND
                  ( (CheckData.ClientID > 0) OR (CheckDeal.rec.PartyID > 0) )
                )
                 return false;
              end;
           end;
  
           if( (CheckData.Contract > 0) AND (CheckData.Contract != CheckDeal.rec.PartyContrID) ) 
              return false;
           end;
        end;
     else
        if( CheckData.ClientID >= 0 )
           if( (CheckData.ClientID != CheckDeal.rec.ClientID) AND
               ( (CheckData.ClientID > 0) OR (CheckDeal.rec.ClientID > 0) )
             )
              return false;
           end;
        end;
  
        if( (CheckData.Contract > 0) AND (CheckData.Contract != CheckDeal.rec.ClientContrID) ) 
           return false;
        end;
     end;
  
     DealType = GetDealBuySale( CheckDeal.rec, IsBack );
     if( (DealType != DEAL_TYPE_SALE) AND (DealType != DEAL_TYPE_BUY) )
        return false;
     end;
  
     if( CheckData.ReportNum == AVRACCOUNT_REPORT )
/*        if( WorkMode_DepoCorAccPos ) */
        /* Выполняем позиционирование по корсчетам Депо. В этом случае, депозитарий 
           это домицилиат корсчета (т.е. корреспондент из корсхемы)*/
        if( CheckAvrPm.rec.ReceiverBankID != {OurBank} )
           TypeProp = PMS_CREDIT;
        elif( CheckAvrPm.rec.PayerBankID != {OurBank} )
           TypeProp = PMS_DEBET;
        else
           CheckData.CurDepositaryID = {OurBank};
        end;

        if( TypeProp != NULL )
           query = RSDCommand(" SELECT dspdrmove_dbt.t_paymentid " +
                              "   FROM dspdrmove_dbt, dspdrprop_dbt " +
                              "  WHERE dspdrprop_dbt.t_paymentid = ? " +
                              "    AND dspdrprop_dbt.t_ismain = 'X' " +
                              "    AND dspdrmove_dbt.t_draftid = dspdrprop_dbt.t_draftid ");
       
           query.addParam( "", RSDBP_IN, int(CheckAvrPm.rec.PaymentID) );
           query.execute();
       
           DataSet = TRSBDataSet(query);

           if( DataSet.MoveNext())
              if( not GetPaymentInfo( null, 0, PmProp, SQL_ConvTypeInteger(DataSet.t_paymentid), TypeProp) ) 
                 MsgBox ("Ошибка при определении свойств платежа по ценным бумагам|(PaymentID = ", int(CheckAvrPm.rec.PaymentID), ")");
                 return false;
              else
                 FindAcc = GetCorAcc( CheckAvrPm.rec.FIID, PmProp.rec.Corschem, CORS_ACC_ACCOUNT);
                 ПолучитьФинИн( CheckAvrPm.rec.FIID, FinInstr );
                 if( FinInstr.rec.IsGeneralized != SET_CHAR ) 
                    if(GetCorschemByAccount( FindAcc, CheckAvrPm.rec.FIID ) == true)
                       CheckData.CurDepositaryID = corschem.rec.CorrID;
                    else
                       MsgBox( "По платежу PaymentID = ",PmProp.rec.PaymentID," не найдена корсхема по ц/б с номером ", PmProp.rec.Corschem );
                       return false;
                    end;
                 else
                    return false;
                 end;
              end;
           else
              if( DealType == DEAL_TYPE_SALE )
                 CheckData.CurDepositaryID = CheckAvrPm.rec.ReceiverBankID;
              else
                 CheckData.CurDepositaryID = CheckAvrPm.rec.PayerBankID;
              end;
           end;
        end;
  
        if( (CheckData.DepositaryID >= 0) AND (CheckData.DepositaryID != CheckData.CurDepositaryID) ) 
           return false;
        end;
     elif( Data.ReportNum == CLIENTACCOUNT_REPORT )
        if( CheckDeal.rec.ClientID == -1 ) 
           return false;
        end;
     end;
  
     return true;
  END;

  PRIVATE MACRO СоздатьОтчет()
     var BeginDate, CurProgress = 0, query, count;
  
     Data.LineSize = 0;
   
     /* Платежи нужно перебирать по фактической дате (FactDate) - дате установки 
        статуса PM_FINISHED на платеж). Эта дата может быть больше чем ValueDate,
        т.к. FactDate = GetDateAfterWorkDays(ValueDate,0),
        поэтому начинаем перебирать не от от ValueDate, а от меньшей даты */
     BeginDate = Data.dateMin;
     while( GetDateAfterWorkDays( BeginDate-1, 0 ) >= BeginDate )
        BeginDate = BeginDate - 1;
     end;
     /*Выборка поотдельности полей табл. paym благодаря 106605 */
     query = "SELECT paym.t_DocumentID DocumentID, paym.t_Purpose Purpose, NVL(paym.t_ReceiverBankID, 0) ReceiverBankID, " + 
             "  NVL(paym.t_PayerBankID, 0) PayerBankID, NVL(paym.t_PaymentID, 0) PaymentID, paym.t_FIID FIID, paym.t_Amount Amount," + 
             "  paym.t_BaseFIID BaseFIID, paym.t_Department Department, paym.t_ValueDate ValueDate," + 
             "  fi.t_AvoirKind, fi.t_Issuer, deal.t_ClientContrID, deal.t_ClientID " +
             "  FROM dpmpaym_dbt paym, dfininstr_dbt fi, ddl_tick_dbt deal " +
             " WHERE (paym.t_PaymStatus >= " + string(PM_READY_TO_SEND) + " OR " + 
             "        paym.t_PaymStatus =  " + string(PM_CLOSED_W_M_MOVEMENT) + ")" + " AND " +
             "        paym.t_ValueDate between " + GetSQLDate(BeginDate) + " AND " + GetSQLDate(Data.dateMax) + " AND " +
             "        ( paym.t_DocKind = " + string(DL_AVRWRT)      + " OR " +
             "          paym.t_DocKind = " + string(DL_RETIREMENT)  + " OR " +
             "          paym.t_DocKind = " + string(DL_SECURITYDOC) + " OR " +
             "          paym.t_DocKind = " + string(DL_CONVAVR) + 
             "        )" + " AND " +
             "        ( paym.t_Purpose = " + string(BAi) + " OR " + 
             "          paym.t_Purpose = " + string(BRi) +
             "        ) AND fi.t_FIID = paym.t_BaseFIID AND " +
             "        deal.t_DealId = paym.t_DocumentID";
  
     if( Data.DprtID >= 0 )
        query = query + " AND paym.t_Department = " + string(Data.DprtID); 
     end;
  
     if( Data.AvrID > 0 ) 
        query = query + " AND paym.t_BaseFIID = " + string(Data.AvrID); 
     end;
  
     if( Data.AvrKind > 0 )
        query = query + " AND fi.t_AvoirKind = " + string(Data.AvrKind); 
     end;
  
     if( Data.EmiID >= 0 )
        query = query + " AND fi.t_Issuer = " + string(Data.EmiID); 
     end;
  
     if( Data.ClientID >= 0 )
        if( (Data.ClientID == 0) OR (Data.ClientID == {OurBank}) )
           query = query + " AND deal.t_ClientID = -1"; 
        else
           query = query + " AND deal.t_ClientID = " + string(Data.ClientID); 
        end;
     end;
  
     if( Data.Contract > 0 )
        query = query + " AND deal.t_ClientContrID = " + string(Data.Contract); 
     end;
  
     if( Data.ReportNum == CLIENTACCOUNT_REPORT )
        query = query + " AND deal.t_ClientID > 0"; 
     end;
  
     count   = TRsbDataSet( "SELECT COUNT(1) nRecs FROM (" + Query + ")" );
     if( count.MoveNext() )
  
        InitProgress( Int(count.nRecs), "Отчет \"Регистр внутреннего учета ценных бумаг.\"" + " Просмотр платежей ...", Data.ReportName + " - просмотр платежей" );
  
        AvrPaym = TRsbDataSet( query );
        while( AvrPaym.MoveNext() )  
  
           Deal.Clear();
           Deal.rec.DealId = AvrPaym.rec.DocumentID;
           if( Deal.GetEQ() AND СделкаПодходит( Data, Deal, AvrPaym, false ) )
              СохранитьДляОтчета(false);
           end;
           if( Deal.GetEQ() AND СделкаПодходит( Data, Deal, AvrPaym, true ) )
              СохранитьДляОтчета(true);
           end;
           UseProgress( CurProgress = CurProgress + 1 );
        end;       
        RemProgress();
     end;
  END;

  PRIVATE MACRO PrintReportLine()
     if( Data.PrintDprt != JrnAvrTmp.rec.Department )
        EndCopyTable();
        if( Data.PrintDprt != null )
           AddStandardFooter( "N1_" );
           CopyAllSheetInTotalBook( NULL, false, "N1_Footer", 1 );
        end;
  
        PrintDprt();
        PrintAccount();
        PrintSubAccount();
        PrintAnaliticAccount();
        RegTable();
        PrintOperation( true );
     elif( Data.PrintAccount != JrnAvrTmp.rec.Account )
        EndCopyTable();
        PrintAccount();
        PrintSubAccount();
        PrintAnaliticAccount();
        RegTable();
        PrintOperation( true );
     elif( Data.PrintSubAccount != JrnAvrTmp.rec.SubAccount )
        EndCopyTable();
        PrintSubAccount();
        PrintAnaliticAccount();
        RegTable();
        PrintOperation( true );
     elif( Data.PrintAnaliticAccount != JrnAvrTmp.rec.AnaliticAccount )
        EndCopyTable();
        PrintAnaliticAccount();
        RegTable();
        PrintOperation( true );
     else
        PrintOperation( false );
     end;
     Data.ReportRun = true;
  END;

  PRIVATE MACRO НапечататьОтчет( )
     var cmd, RSD, CurProgress = 0;
    
     InitProgress( Data.LineSize, "Отчет \"Регистр внутреннего учета ценных бумаг.\"" + " Печать отчета ...", Data.ReportName );
    
     cmd = RSDCommand(
                      " SELECT * " +
                      "   FROM djrnavr_tmp j, " +
                      "        (SELECT l.t_time as LotTime, l.t_date as LotDate, l.t_DocID as DocID " +
                      "           FROM dpmwrtsum_dbt l " +
                      "          WHERE l.t_PartNum = (SELECT MIN(l2.t_PartNum) " +
                      "                                 FROM dpmwrtsum_dbt l2 " +
                      "                                WHERE l2.t_DocID = l.t_DocID " +
                      "                              ) " +
                      "        ) sortdata " +
                      "  WHERE j.t_ReportNum = ? AND sortdata.DocID = j.t_PaymentID " +
                      "  ORDER BY j.t_Department, j.t_Account, j.t_SubAccount, j.t_AnaliticAccount, j.t_ValueDate, sortdata.LotDate, sortdata.LotTime "
                     );
    
     cmd.addParam("", RSDBP_IN, Data.ReportNum);
    
     cmd.execute();
    
     RSD = TRsbDataSet(cmd);
    
     while( RSD.MoveNext() )
        JrnAvrTmp.rec.ReportNum  = RSD.ReportNum;
        JrnAvrTmp.rec.Department = RSD.Department;
    
        JrnAvrTmp.rec.Account    = RSD.Account;
        JrnAvrTmp.rec.SubAccount = RSD.SubAccount;
        JrnAvrTmp.rec.AnaliticAccount = RSD.AnaliticAccount;
    
        JrnAvrTmp.rec.ValueDate  = date(RSD.ValueDate);
        JrnAvrTmp.rec.Number     = RSD.Number;
        JrnAvrTmp.rec.PaymentID  = RSD.PaymentID;
        JrnAvrTmp.rec.DealID     = RSD.DealID;
        PrintReportLine();
        UseProgress( CurProgress = CurProgress + 1 );
     end;
     EndCopyTable();
    
     if( Data.ReportRun )
        AddStandardFooter( "N1_" );
        CopyAllSheetInTotalBook( NULL, false, "N1_Footer", 1 );
     end;

     RemProgress();
  END;

  PRIVATE MACRO Create()

     СоздатьОтчет();
     НапечататьОтчет();

     if(Data.ReportRun == false)
        PrintFormatString( ReportRunFalse,
                           "N1_ReportRunFalse", "Для отчета \"" + Data.ReportName + "\" нет подходящих сделок."
                         );
        CopyAllSheetInTotalBook( NULL, false, "N1_ReportRunFalse", 1 );
     end;
  END;

  initDL_CReportTemplate( NULL, "Report_TxJrnavr.xls" );
END;

macro ReportRun( DprtID:integer, 
                 dateMin: date,  dateMax: date,
                 AvrAccount:bool,
                 ClientID: integer,      
                 DepositaryID: integer,      
                 EmiID: integer,         
                 AvrKind: integer, 
                 AvrID: integer, 
                 AvrAccountClient:bool,
                 ClientID_2: integer,      
                 Contract: integer,      
                 EmiID_2: integer,         
                 AvrKind_2: integer, 
                 AvrID_2: integer, 
                 IsApp: bool,
                 IsExcel:bool )

   var TmpFName = GetWorkFileName("jrnavr");
   Message( TmpFName + " Создание временного файла ..." );
   JrnAvrTmp = TBFile( "jrnavr.tmp", "W", 0 );
   ClearGlobalTmp( "djrnavr_tmp" );

   if (DprtID > 0)
      DepID = DprtID;
   else
      DepID = -1;
   end;

   if( AvrAccount )
      _Data = SourceData(  AVRACCOUNT_REPORT, DprtID, dateMin, dateMax,
                          ClientID, DepositaryID, EmiID, AvrKind, AvrID,
                          0, IsApp, IsExcel );
      _Data.ReportName = "Счета учета ценных бумаг";
      SP_Jrnavr_Report(_Data).Run();
   end;

   if( AvrAccountClient )
      _Data = SourceData(  CLIENTACCOUNT_REPORT, DprtID, dateMin, dateMax,
                          ClientID_2, -1, EmiID_2, AvrKind_2,
                          AvrID_2, Contract, IsApp, IsExcel );
      _Data.ReportName = "Счета расчетов с клиентами по ценным бумагам";
      SP_Jrnavr_Report(_Data).Run();
   end;
end;