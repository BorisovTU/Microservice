/*
$Name:        nregisteria_panel.mac
$Module:      Ценные бумаги
$Description: Реестр ВУ
$Programmer:  Врубель В.Л.
*/
import "DlRepForm_h.mac", "globals.mac", rsd, dlquery, dlmisc;
var SelectedClients = 0, SelectedContrs = 0;
/*Номера полей в форме отчета*/
CONST P_RIA_BEGINDATE            = 0,
      P_RIA_ENDDATE              = 1,
      P_RIA_Sector_Brokers       = 2,
      P_RIA_Sector_Dilers        = 3,
      P_RIA_Sector_Clients       = 4,
      P_RIA_Block_Deals          = 5,
      P_RIA_Block_Clients        = 6,
      P_RIA_Block_InAcc          = 7,
      P_RIA_ClientsCode           = 8,
      P_RIA_ClientsName           = 9,
      P_RIA_Contr                = 10,
      P_RIA_StockMarket          = 11,
      P_RIA_FuturesMarket        = 12,
      P_RIA_CurrencyMarket       = 13;

/* Обработчики для нестандарных контролов */
private CONST H_StockMarket          = 101,
              H_FuturesMarket        = 102,
              H_CurrencyMarket       = 103,
              H_ClientCode           = 104,
              H_ClientName           = 105,
              H_Contr                = 106;
              
              
private MACRO FillSessIdAndCalcId(m_sessId:@BigInt, m_calcId:@BigInt)
  var cmd = RsdCommand("begin rsb_dlregisteria.GenCalcIds(?, ?);\n end;");
  cmd.AddParam("SessionID",      RSDBP_OUT, V_NUMERIC );
  cmd.AddParam("CalcID",   RSDBP_OUT, V_NUMERIC );
  cmd.Execute();
  
  m_sessId = BigInt(cmd.value(0));
  m_calcId = BigInt(cmd.value(1));
end;

PRIVATE macro ReFillClients(OnlyTrunc, calcdate, stock, dv, cm, m_sessId, m_calcId)
    var ServKind = "";
    if (stock == SET_CHAR)
        ServKind = PTSK_STOCKDL;
    end;
    if (dv == SET_CHAR)
        ServKind = ServKind + IIF(ServKind != "", ",","") + PTSK_DV;
    end;
    if (cm == SET_CHAR)
        ServKind = ServKind + IIF(ServKind != "", ",","") + PTSK_CM;
    end;
    var cmd = RsdCommand("TRUNCATE TABLE D_RIA_PANELCLIENTS_TMP");
    cmd.execute();
    cmd = RsdCommand("DELETE FROM D_RIA_PANELCLIENTS_DBT WHERE T_SESSIONID = " +String(m_sessId:0:0) + " AND T_CALCID = " + String(m_sessId:0:0));
    cmd.execute();
    if((ServKind != "") and (not OnlyTrunc))
        var sql = 
        " insert into D_RIA_PANELCLIENTS_TMP (T_SETFLAG,T_CLIENTID,T_CLIENTCODE,T_CLIENTNAME) " + 
        "select distinct chr(0), party.t_partyid, objcode.t_Code, party.t_name " +
        " from dparty_dbt party  " +
        " join dsfcontr_dbt sfcontr on  party.t_partyid = sfcontr.t_partyid   and   " +
        "                           sfcontr.t_datebegin <= " + GetSQlDate(calcdate) + "  and   " +
        "                           (sfcontr.t_dateclose = to_date('01.01.0001','dd.mm.yyyy') or  " +
        "                            sfcontr.t_dateclose >= " + GetSQlDate(calcdate) + ")  " +
        "join DOBJCODE_DBT objcode on party.t_partyid= OBJCODE.T_OBJECTID and objcode.t_objecttype = 3 and objcode.t_codekind = 1 and OBJCODE.T_STATE = 0 " +
        " where sfcontr.T_SERVKIND in (" + ServKind + ")  ";
        cmd = null;
        cmd = RsdCommand(sql);
        cmd.execute();
    end;
end;

PRIVATE macro ReFillContrs(OnlyTrunc, calcdate, stock, dv, cm, m_sessId, m_calcId)
    var ServKind = "";
    if (stock == SET_CHAR)
        ServKind = PTSK_STOCKDL;
    end;
    if (dv == SET_CHAR)
        ServKind = ServKind + IIF(ServKind != "", ",","") + PTSK_DV;
    end;
    if (cm == SET_CHAR)
        ServKind = ServKind + IIF(ServKind != "", ",","") + PTSK_CM;
    end;
    var cmd = RsdCommand("TRUNCATE TABLE D_RIA_PANELCONTRS_TMP");
    cmd.execute();
    cmd = null;
    cmd = RsdCommand("DELETE FROM D_RIA_PANELCONTRS_DBT WHERE T_SESSIONID = " +String(m_sessId:0:0) + " AND T_CALCID = " + String(m_sessId:0:0));
    cmd.execute();
    if((ServKind != "") and (not OnlyTrunc))
        var sql = 
        " insert into D_RIA_PANELCONTRS_TMP (T_SETFLAG, T_CLIENTID ,T_DLCONTRID ,T_CONTRNUMBER ,T_CONTRNAME) " + 
        "SELECT CHR (0), " + 
        "       clients.T_CLIENTID, " + 
        "       dlcontr.t_dlcontrid, " + 
        "       sfcontr.t_number, " + 
        "       SFCONTR.T_NAME " + 
        "  FROM ddlcontr_dbt            dlcontr, " + 
        "       dsfcontr_dbt            sfcontr, " + 
        "       D_RIA_PANELCLIENTS_TMP  clients " + 
        " WHERE     clients.T_SETFLAG = CHR (88) " + 
        "       AND clients.T_CLIENTID = sfcontr.t_partyid " + 
        "       AND sfcontr.t_id = dlcontr.t_sfcontrid " + 
        "       AND sfcontr.t_datebegin <=  " + GetSQlDate(calcdate) + 
        "       AND (   sfcontr.t_dateclose = TO_DATE ('01.01.0001', 'dd.mm.yyyy') " + 
        "            OR sfcontr.t_dateclose >= " + GetSQlDate(calcdate) + ") ";
        cmd = null;
        cmd = RsdCommand(sql);
        cmd.execute();
    end;
end;

PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO SetCheckBoxAfterChange(pThis:DL_CPanel, CurField:INTEGER, FldRealValue:@VARIANT )
  if (CurField != -1)
    if (CurField == P_RIA_Sector_Clients)
      if ((FldRealValue == SET_CHAR) OR (pThis.GetFieldValue( P_RIA_Sector_Brokers ) == SET_CHAR))
        EnableFields( pThis.Data(), P_RIA_ClientsCode );
        EnableFields( pThis.Data(), P_RIA_Contr );
      else
        pThis.SetLinkedValue( H_ClientCode, "" );
        pThis.SetLinkedValue( H_ClientName, "" );
        pThis.SetLinkedValue( H_Contr, "" );
        DisableFields( pThis.Data(), P_RIA_ClientsCode );
        DisableFields( pThis.Data(), P_RIA_Contr );
        SelectedClients = 0;
        SelectedContrs = 0;
        ReFillClients(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
        ReFillContrs(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
      end;
    elif(CurField == P_RIA_Sector_Brokers )
      if ((FldRealValue == SET_CHAR) OR (pThis.GetFieldValue( P_RIA_Sector_Clients ) == SET_CHAR))
        EnableFields( pThis.Data(), P_RIA_ClientsCode );
        EnableFields( pThis.Data(), P_RIA_Contr );
      else
        pThis.SetLinkedValue( H_ClientCode, "" );
        pThis.SetLinkedValue( H_ClientName, "" );
        pThis.SetLinkedValue( H_Contr, "" );
        DisableFields( pThis.Data(), P_RIA_ClientsCode );
        DisableFields( pThis.Data(), P_RIA_Contr );
        
        SelectedClients = 0;
        SelectedContrs = 0;
        ReFillClients(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
        ReFillContrs(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
      end;
    end;
  else
    if ((pThis.GetFieldValue( P_RIA_Sector_Brokers ) == SET_CHAR) OR (pThis.GetFieldValue( P_RIA_Sector_Clients ) == SET_CHAR))
      EnableFields( pThis.Data(), P_RIA_ClientsCode );
      EnableFields( pThis.Data(), P_RIA_Contr );
    else
      pThis.SetLinkedValue( H_ClientCode, "" );
      pThis.SetLinkedValue( H_ClientName, "" );
      pThis.SetLinkedValue( H_Contr, "" );
      DisableFields( pThis.Data(), P_RIA_ClientsCode );
      DisableFields( pThis.Data(), P_RIA_Contr );
      SelectedClients = 0;
      SelectedContrs = 0;
      ReFillClients(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
      ReFillContrs(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
    end;
  end;
END;

/**/
private MACRO DL_FldProc_CheckBox( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT )
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = SET_CHAR;
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == SET_CHAR)
        FldShowValue = FldRealValue = UNSET_CHAR;
      else
        FldShowValue = FldRealValue = SET_CHAR;
      end;
    end;
  end;
  SetCheckBoxAfterChange(pThis, -1);
  return CM_DEFAULT;
END;

private MACRO DL_FldProc_CheckBox_Stock( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = SET_CHAR;
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == SET_CHAR)
        FldShowValue = FldRealValue = UNSET_CHAR;
      else
        FldShowValue = FldRealValue = SET_CHAR;
      end;
     pThis.SetLinkedValue( H_ClientCode, "" );
     pThis.SetLinkedValue( H_ClientName, "" );
     pThis.SetLinkedValue( H_Contr, null );
     ReFillClients(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), FldRealValue, pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
     ReFillContrs(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), FldRealValue, pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
     SelectedClients = 0;
     SelectedContrs = 0;
    end;
  end;
  SetCheckBoxAfterChange(pThis, -1);
  return CM_DEFAULT;
END;

private MACRO DL_FldProc_CheckBox_Future( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = SET_CHAR;
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == SET_CHAR)
        FldShowValue = FldRealValue = UNSET_CHAR;
      else
        FldShowValue = FldRealValue = SET_CHAR;
      end;
     pThis.SetLinkedValue( H_ClientCode, "" );
     pThis.SetLinkedValue( H_ClientName, "" );
     pThis.SetLinkedValue( H_Contr, null );
     ReFillClients(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), FldRealValue, pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
     ReFillContrs(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), FldRealValue, pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
     SelectedClients = 0;
     SelectedContrs = 0;
    end;
  end;
  SetCheckBoxAfterChange(pThis, -1);
  return CM_DEFAULT;
END;

private MACRO DL_FldProc_CheckBox_Currency( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = SET_CHAR;
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == SET_CHAR)
        FldShowValue = FldRealValue = UNSET_CHAR;
      else
        FldShowValue = FldRealValue = SET_CHAR;
      end;
     pThis.SetLinkedValue( H_ClientCode, "" );
     pThis.SetLinkedValue( H_ClientName, "" );
     pThis.SetLinkedValue( H_Contr, null );
     ReFillClients(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), FldRealValue, pThis.m_sessId, pThis.m_calcId);
     ReFillContrs(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), FldRealValue, pThis.m_sessId, pThis.m_calcId);
     SelectedClients = 0;
     SelectedContrs = 0;
    end;
  end;
  SetCheckBoxAfterChange(pThis, -1);
  return CM_DEFAULT;
END;

private MACRO DL_FldProc_CheckBoxBr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = SET_CHAR;
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == SET_CHAR)
        FldShowValue = FldRealValue = UNSET_CHAR;
      else
        FldShowValue = FldRealValue = SET_CHAR;
      end;
    end;
  end;
  SetCheckBoxAfterChange(pThis, P_RIA_Sector_Brokers, FldRealValue);
  return CM_DEFAULT;
END;

private MACRO DL_FldProc_CheckBoxCl( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = SET_CHAR;
    end;
    FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( Cmd == DLG_KEY )
    if( Key == KEY_SPACE )
      if(FldRealValue == SET_CHAR)
        FldShowValue = FldRealValue = UNSET_CHAR;
      else
        FldShowValue = FldRealValue = SET_CHAR;
      end;
    end;
  end;
  SetCheckBoxAfterChange(pThis, P_RIA_Sector_Clients, FldRealValue);
  return CM_DEFAULT;
END;


   
PRIVATE macro SelectClient(m_sessId, m_calcId, Changed:@bool)
        var  WasChange = false;
         /*Обработчик скроллинга*/
         macro Client_Scroll(rs, cmd, id, key)
            /*Обработчик*/
            if(cmd == DLG_KEY)
               if(key == 27) /*Esc*/
                  if (WasChange)
                      var sql, cmd_up;
                      if(not GetTrue(true, String("Сохранить изменения ?")))
                         sql =   "UPDATE  D_RIA_PANELCLIENTS_TMP SET T_SETFLAG = CHR(0)";
                         cmd_up = RsdCommand(sql);
                         cmd_up.execute();
                         sql =  " UPDATE  D_RIA_PANELCLIENTS_TMP SET T_SETFLAG = CHR(88) WHERE T_CLIENTID IN (SELECT cl.T_CLIENTID FROM D_RIA_PANELCLIENTS_DBT cl  " +
                                " WHERE cl.T_SESSIONID = " + String(m_sessId:0:0) + 
                                " AND T_CALCID = " + String(m_calcId:0:0) + ")";
                         cmd_up = RsdCommand(sql);
                         cmd_up.execute();
                         Changed = false;
                        return CM_CANCEL;
                      else
                        sql =   "MERGE INTO D_RIA_PANELCLIENTS_DBT p_cl " +
                                    "     USING (SELECT cl_tmp.t_clientid, " + String(m_sessId:0:0) + " sessinoid, " + String(m_calcId:0:0) + " calcid "
                                    "              FROM D_RIA_PANELCLIENTS_tmp cl_tmp " +
                                    "             WHERE     cl_tmp.T_SETFLAG = CHR(88) " +
                                    "                   AND NOT EXISTS " +
                                    "                           (SELECT 1 " +
                                    "                              FROM D_RIA_PANELCLIENTS_DBT cl " +
                                    "                             WHERE     cl.t_clientid = cl_tmp.t_clientid " +
                                    "                                   AND cl.t_sessionid = " + String(m_sessId:0:0) +
                                    "                                   AND cl.t_calcid = " + String(m_calcId:0:0) + ")) diff " +
                                    "        ON ( p_cl.t_clientid = diff.t_clientid AND p_cl.t_sessionid = diff.sessinoid AND p_cl.t_calcid = diff.calcid) " +
                                    "WHEN NOT MATCHED " +
                                    " THEN " +
                                    "    INSERT     (T_SESSIONID, T_CALCID, T_CLIENTID) " +
                                    "        VALUES (" + String(m_sessId:0:0) + ", " + String(m_calcId:0:0) + ", diff.t_clientid)";
                         cmd_up = RsdCommand(sql);
                         cmd_up.execute();
                         cmd_up = null;
                         sql =   "DELETE FROM D_RIA_PANELCLIENTS_DBT WHERE T_SESSIONID = " + String(m_sessId:0:0) + 
                                 " AND T_CALCID = " + String(m_calcId:0:0) +
                                 " AND T_CLIENTID in (SELECT tmp.T_CLIENTID FROM D_RIA_PANELCLIENTS_TMP tmp WHERE tmp.T_SETFLAG = CHR(0))";
                         cmd_up = RsdCommand(sql);
                         cmd_up.execute();
                         Changed = true;
                      end;
                  end;
               elif(key == 32) /*Space*/
                  if(rs.RecCount > 0)
                     rs.edit();
                     if(rs.value("t_setflag") == SET_CHAR)
                        rs.value("t_setflag") = UNSET_CHAR;
                     else                                                                                         
                        rs.value("t_setflag") = SET_CHAR;
                     end;
                     rs.update();
                     updatescroll(rs, 5);
                     WasChange = true;
                     return CM_IGNORE;
                  end;
               elif((key == 323) OR (key == 322))
                  return CM_IGNORE;
               end;
            end;
         end;

         var rs, cmd;

         var que = " select t_setflag, T_CLIENTID, t_clientcode, t_clientname " +
                   " from D_RIA_PANELCLIENTS_TMP " +
                   " order by t_setflag desc, t_clientname";
         cmd = RsdCommand(que);
         rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
         while(RunScroll(rs, 4, MakeArray("t_setflag", "В", 1, 2, -1, 0,  
                                         "t_clientcode", "Код клиента", 30, 2, -1, 0,
                                         "t_clientname", "Наименование клиента", 30, 2, -1, 0,
                                         "t_clientid", "ID", 8, 2, -1, 0 
                                         ), 
                        null, "Client_Scroll", "Выбор клиентов", "~Esc~ Выход ~Пробел~ Отметить ", false))
            cmd = RsdCommand(que);
            rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
         end;
end;


PRIVATE macro SelectContr(m_sessId, m_calcId)
        var WasChange = false;
         /*Обработчик скроллинга*/
         macro Contr_Scroll(rs, cmd, id, key)
            /*Обработчик*/
            if(cmd == DLG_KEY)
               if(key == 27) /*Esc*/
                  if (WasChange)
                      var sql,cmd_up;
                      if(not GetTrue(true, String("Сохранить изменения ?")))
                         sql =   "UPDATE  D_RIA_PANELCONTRS_TMP SET T_SETFLAG = CHR(0)";
                         cmd_up = RsdCommand(sql);
                         cmd_up.execute();
                         sql =  " UPDATE  D_RIA_PANELCONTRS_TMP SET T_SETFLAG = CHR(88) WHERE T_DLCONTRID IN (SELECT cl.T_DLCONTRID FROM D_RIA_PANELCONTRS_DBT cl WHERE cl.t_clientid = t_clientid " +
                                " AND cl.T_SESSIONID = " + String(m_sessId:0:0) + 
                                " AND T_CALCID = " + String(m_calcId:0:0) + ")";
                         cmd_up = RsdCommand(sql);
                         cmd_up.execute();
                        return CM_CANCEL;
                      else
                        sql =   "MERGE INTO D_RIA_PANELCONTRS_DBT p_cl " +
                                    "     USING (SELECT cl_tmp.t_clientid, cl_tmp.t_dlcontrid , " + String(m_sessId:0:0) + " sessinoid, " + String(m_calcId:0:0) + " calcid "
                                    "              FROM D_RIA_PANELCONTRS_TMP cl_tmp " +
                                    "             WHERE     cl_tmp.T_SETFLAG = CHR(88) " +
                                    "                   AND NOT EXISTS " +
                                    "                           (SELECT 1 " +
                                    "                              FROM D_RIA_PANELCONTRS_DBT cl " +
                                    "                             WHERE     cl.t_clientid = cl_tmp.t_clientid " +
                                    "                                    AND cl.t_dlcontrid = cl_tmp.t_dlcontrid " +
                                    "                                   AND cl.t_sessionid = " + String(m_sessId:0:0) +
                                    "                                   AND cl.t_calcid = " + String(m_calcId:0:0) + ")) diff " +
                                    "        ON (    p_cl.t_clientid = diff.t_clientid " +
                                    "            AND p_cl.t_dlcontrid = diff.t_dlcontrid  " +
                                    "            AND p_cl.t_sessionid = diff.sessinoid AND p_cl.t_calcid = diff.calcid) " +
                                    "WHEN NOT MATCHED " +
                                    " THEN " +
                                    "    INSERT     (T_SESSIONID, T_CALCID, T_CLIENTID, T_DLCONTRID) " +
                                    "        VALUES (" + String(m_sessId:0:0) + ", " + String(m_calcId:0:0) + ", diff.t_clientid, diff.t_dlcontrid)";
                         cmd_up = RsdCommand(sql);
                         cmd_up.execute();
                         cmd_up = null;
                         sql =   "DELETE FROM D_RIA_PANELCONTRS_DBT WHERE T_SESSIONID = " + String(m_sessId:0:0) + 
                                 " AND T_CALCID = " + String(m_calcId:0:0) +
                                 " AND t_dlcontrid in (SELECT tmp.t_dlcontrid FROM D_RIA_PANELCONTRS_TMP tmp WHERE tmp.T_SETFLAG = CHR(0) and tmp.T_CLIENTID = T_CLIENTID )";
                         cmd_up = RsdCommand(sql);
                         cmd_up.execute();
                      end;
                  end;
               elif(key == 32) /*Space*/
                  if(rs.RecCount > 0)
                     rs.edit();
                     if(rs.value("t_setflag") == SET_CHAR)
                        rs.value("t_setflag") = UNSET_CHAR;
                     else                                                                                         
                        rs.value("t_setflag") = SET_CHAR;
                     end;
                     rs.update();
                     updatescroll(rs, 5);
                     WasChange = true;
                     return CM_IGNORE;
                  end;
               elif((key == 323) OR (key == 322))
                return CM_IGNORE;
               end;
            end;
         end;

         var rs, cmd;
         var que = " select t_setflag, T_DLCONTRID, T_CONTRNUMBER, T_CONTRNAME " +
                   " from D_RIA_PANELCONTRS_TMP " +
                   " order by t_setflag desc, T_CONTRNUMBER";
         cmd = RsdCommand(que);
         rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
         while(RunScroll(rs, 3, MakeArray("t_setflag", "В", 1, 2, -1, 0,  
                                         "T_CONTRNUMBER", "Номер договора", 30, 2, -1, 0,
                                         "T_CONTRNAME", "Наименование договора", 30, 2, -1, 0
                                         ), 
                        null, "Contr_Scroll", "Выбор договора", "~Esc~ Выход ~Пробел~ Отметить ", false))
            cmd = RsdCommand(que);
            rs = RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_STATIC);
         end;
end;

/*ищем код субъекта*/
PRIVATE MACRO GetPartCode( PartyID:integer )
  var sqlPartcode, PartCode;

  sqlPartcode = RSDCommand("select t_Code from dpartcode_dbt " +
                           " where t_PartyID = ? " +
                           "   and t_CodeKind = 1");

  sqlPartcode.addParam("", RSDBP_IN, PartyID);
  sqlPartcode.execute();

  PartCode = TRsbDataSet(sqlPartcode);
  if( PartCode.MoveNext() )
     return PartCode.Code;
  end;

  return "";
END;


PRIVATE MACRO RIA_FldProc_Client( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if(( FldRealValue <= 0 ) or (FldShowValue == ""))
        FldShowValue = STR_ALLVALUE;
        pThis.SetLinkedValue( H_ClientName, "");
     end;
     EnableFields( pThis.Data(), P_RIA_ClientsCode );
     DisableFields( pThis.Data(), P_RIA_ClientsName );
     ReFillClients(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
     ReFillContrs(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
  elif( Cmd == DLG_CHANGEVALUE )
    if(FldShowValue == "")
        FldRealValue = -1;
    end;
    FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     pThis.SetLinkedValue( H_ClientName, "" );
     pThis.SetLinkedValue( H_Contr, null );
     ReFillClients(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
     ReFillContrs(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
     SelectedClients = 0;
     SelectedContrs = 0;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     var Changed = false;
     SelectClient(pThis.m_sessId, pThis.m_calcId, @Changed);
     var Dt = TRsbDataSet("select count(1) cnt from D_RIA_PANELCLIENTS_TMP WHERE t_setflag = chr(88)");
     if (Dt.movenext() and (dt.cnt > 0))
         if(dt.cnt == 1)
           Dt = TRsbDataSet("select t_clientid from D_RIA_PANELCLIENTS_TMP WHERE t_setflag = chr(88)");
            if (Dt.movenext() and (dt.clientid > 0))
              var Party = TRecHandler("party.dbt");
              if( not ПолучитьСубъекта(dt.clientid, Party) )
                 FldShowValue = GetPartCode(dt.clientid);
                 pThis.SetLinkedValue(H_ClientName, Party.rec.ShortName);
              end;
              SelectedClients = 1;
            end;
         else
           SelectedClients = 1;
           FldShowValue = int(dt.cnt);
           pThis.SetLinkedValue(H_ClientName, "");
         end;
     else
         FldShowValue = STR_ALLVALUE;
         pThis.SetLinkedValue( H_ClientName, "");
         SelectedClients = 0;
     end;
     if(Changed)
         ReFillContrs(false, pThis.GetFieldValue( P_RIA_BEGINDATE ), pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
         if(SelectedClients == 1)
            var DtC = TRsbDataSet("select count(1) cnt from D_RIA_PANELCONTRS_TMP");
            if (DtC.movenext() and (DtC.cnt > 0))
                pThis.SetLinkedValue( H_Contr, int(DtC.cnt));
            else
                pThis.SetLinkedValue( H_Contr, "0");
            end;
         else
            pThis.SetLinkedValue( H_Contr, STR_ALLVALUE);
         end;
     end;
  end;

  return CM_DEFAULT;
END;

PRIVATE MACRO RIA_FldProc_NumContr( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = STR_ALLVALUE;
     end;
     if( FldRealValue <= 0 )                           
        FldShowValue = STR_ALLVALUE;
     end;
	 if( FldRealValue >= 0 )                           
        FldShowValue = FldRealValue;
     end;
     EnableFields( pThis.Data(), P_RIA_Contr );
  elif( Cmd == DLG_CHANGEVALUE )
    FldRealValue = FldShowValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     FldRealValue = -1;
     FldShowValue = STR_ALLVALUE;
     SelectedContrs = 0;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     var ContrID = -1, ContrNumber = STR_ALLVALUE;
     SelectContr(pThis.m_sessId, pThis.m_calcId);
     var Dt = TRsbDataSet("select count(1) cnt from D_RIA_PANELCONTRS_TMP WHERE t_setflag = chr(88)");
     if (Dt.movenext() and (dt.cnt > 0))
        if (dt.cnt == 1)
           Dt = TRsbDataSet("select T_CONTRNUMBER from D_RIA_PANELCONTRS_TMP WHERE t_setflag = chr(88)");
           if(Dt.movenext())
                SelectedContrs = 1;
                FldShowValue = dt.ContrNumber;
           end;
        else
		  FldShowValue = int(dt.cnt);
          SelectedContrs = 1;
        end;
     elif(SelectedClients == 1)
		var DtC = TRsbDataSet("select count(1) cnt from D_RIA_PANELCONTRS_TMP");
        if (DtC.movenext() and (DtC.cnt > 0))
			FldShowValue = int(DtC.cnt);
		else
			FldShowValue = int(0);
		end;
     else
        FldShowValue = STR_ALLVALUE;
        SelectedContrs = 0;
     end;
  end;
  return CM_DEFAULT;
END;

PRIVATE MACRO DL_FldProc_BeginDate_WithContrFill( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
    if( FldRealValue == null ) /*инициализация по умолчанию*/
      FldRealValue = {curdate};
    end;
  FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
    FldRealValue = FldShowValue;
    if( pThis.GetLinkedValue(DL_PNFLD_ENDDATE) < FldRealValue )
      pThis.SetLinkedValue( DL_PNFLD_ENDDATE, FldRealValue );
    end;
    if( FldRealValue > pThis.GetLinkedValue(DL_PNFLD_ENDDATE) )
      MsgBox( "Дата окончания не может быть меньше даты начала периода.");
      return CM_IGNORE;
    end;
    ReFillClients(false, FldRealValue, pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
    ReFillContrs(false, FldRealValue, pThis.GetFieldValue( P_RIA_StockMarket ), pThis.GetFieldValue( P_RIA_FuturesMarket ), pThis.GetFieldValue( P_RIA_CurrencyMarket ), pThis.m_sessId, pThis.m_calcId);
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) RegisterIA_Panel(_StockMarket:String, _FuturesMarket:String, _CurrencyMarket:String)

  var StockMarket = "";
  var FuturesMarket = "";
  var CurrencyMarket = "";
  var ClientsCode, ClientsName, Contr;
  var m_sessId:BigInt = 0, m_calcId:BigInt = 0;
  
  PRIVATE MACRO Init()
     AddFieldProc(0, P_RIA_BEGINDATE,           DL_PNFLD_BEGINDATE,     @DL_FldProc_BeginDate_WithContrFill,    {curdate});
     AddFieldProc(0, P_RIA_ENDDATE,             DL_PNFLD_ENDDATE,       @DL_FldProc_EndDate,                    {curdate});
     AddFieldProc(0, P_RIA_StockMarket,         H_StockMarket,          @DL_FldProc_CheckBox_Stock,             StockMarket );
     AddFieldProc(0, P_RIA_FuturesMarket,       H_FuturesMarket,        @DL_FldProc_CheckBox_Future,            FuturesMarket );
     AddFieldProc(0, P_RIA_CurrencyMarket,      H_CurrencyMarket,       @DL_FldProc_CheckBox_Currency,          CurrencyMarket );
     AddFieldProc(0, P_RIA_Sector_Brokers,      DL_PNFLD_CHECKBOX,      @DL_FldProc_CheckBoxBr,                 SET_CHAR );
     AddFieldProc(0, P_RIA_Sector_Dilers,       DL_PNFLD_CHECKBOX,      @DL_FldProc_CheckBox,                   SET_CHAR );
     AddFieldProc(0, P_RIA_Sector_Clients,      DL_PNFLD_CHECKBOX,      @DL_FldProc_CheckBoxCl,                 SET_CHAR );
     AddFieldProc(0, P_RIA_Block_Deals,         DL_PNFLD_CHECKBOX,      @DL_FldProc_CheckBox,                   SET_CHAR );
     AddFieldProc(0, P_RIA_Block_Clients,       DL_PNFLD_CHECKBOX,      @DL_FldProc_CheckBox,                   SET_CHAR );
     AddFieldProc(0, P_RIA_Block_InAcc,         DL_PNFLD_CHECKBOX,      @DL_FldProc_CheckBox,                   SET_CHAR );
     AddFieldProc(0, P_RIA_ClientsCode,         H_ClientCode,           @RIA_FldProc_Client,                    ClientsCode );
     AddFieldProc(0, P_RIA_ClientsName,         H_ClientName,           @Default,                               ClientsName );
     AddFieldProc(0, P_RIA_Contr,               H_Contr,                @RIA_FldProc_NumContr,                  Contr );
  END;

  PRIVATE MACRO Check():BOOL

    if ((this.GetFieldValue(P_RIA_BEGINDATE) == date(0,0,0)) or (this.GetFieldValue(P_RIA_ENDDATE) == date(0,0,0)))
        msgbox("Необходимо указать период формирования отчета");
        return false;
     end;
    
    if ((this.GetFieldValue(P_RIA_StockMarket) == UNSET_CHAR) and (this.GetFieldValue(P_RIA_FuturesMarket) == UNSET_CHAR) and (this.GetFieldValue(P_RIA_CurrencyMarket) == UNSET_CHAR))  
        msgbox("Не заданы параметры выпуска отчета");
        return false;
    end;
    
    if ((this.GetFieldValue(P_RIA_Sector_Brokers) == UNSET_CHAR) 
         and (this.GetFieldValue(P_RIA_Sector_Dilers) == UNSET_CHAR) 
         and (this.GetFieldValue(P_RIA_Sector_Clients) == UNSET_CHAR)
        )
        msgbox("Не выбраны разделы для выпуска отчета");
        return false;
    end;
    
    if ((this.GetFieldValue(P_RIA_Sector_Brokers) == UNSET_CHAR) and (this.GetFieldValue(P_RIA_Sector_Dilers) == UNSET_CHAR) and (this.GetFieldValue(P_RIA_Sector_Clients) == UNSET_CHAR)
        AND (this.GetFieldValue(P_RIA_Block_Deals) == UNSET_CHAR) and (this.GetFieldValue(P_RIA_Block_Clients) == UNSET_CHAR) and (this.GetFieldValue(P_RIA_Block_InAcc) == UNSET_CHAR))
        msgbox("Не выбраны данные для выпуска отчета");
        return false;
    end;
    
    if ((this.GetFieldValue(P_RIA_Sector_Brokers) == SET_CHAR) 
        AND (this.GetFieldValue(P_RIA_Block_Deals) == UNSET_CHAR) and (this.GetFieldValue(P_RIA_Block_Clients) == UNSET_CHAR) and (this.GetFieldValue(P_RIA_Block_InAcc) == UNSET_CHAR))
        msgbox("Не выбраны данные для выпуска отчета по разделу \"Брокерская деятельность\". Укажите блок.");
        return false;
    end;
    
    if ((this.GetFieldValue(P_RIA_Sector_Dilers) == SET_CHAR)
        AND (this.GetFieldValue(P_RIA_Block_Deals) == UNSET_CHAR))
        msgbox("Не выбраны данные для выпуска отчета по разделу \"Дилерская деятельность\". Укажите блок \"Информация о сделке\"");
        return false;
    end;
    
    if ((this.GetFieldValue(P_RIA_Sector_Clients) == SET_CHAR)
        AND (this.GetFieldValue(P_RIA_Block_Clients) == UNSET_CHAR) and (this.GetFieldValue(P_RIA_Block_InAcc) == UNSET_CHAR))
        msgbox("Не выбраны данные для выпуска отчета по разделу \"Клиентские договоры\". Укажите блок \"Информация о клиенте\" или \"Внутренний учет\"");
        return false;
    end;
    
    return true;
  END;
  
  StockMarket    = _StockMarket;
  FuturesMarket  = _FuturesMarket;
  CurrencyMarket = _CurrencyMarket;
  
  FillSessIdAndCalcId(@m_sessId, @m_calcId);

  initDL_CPanel("nregisteria.lbr", "REGISTER"); 

END;

