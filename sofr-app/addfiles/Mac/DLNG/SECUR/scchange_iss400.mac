/*Опреация замены эмитента.
  Шаг изменения эмитента*/
import InsCarryDoc, RsbDataSet, "scincfun.mac", "secinter.mac", "sp_car.mac", "scservop.mac", "spserv.mac";

private record Avoiriss( avoiriss );
private record fininstr(fininstr); //рекорд ДО изменения

private const FI_CHANGE_ISSUER = 3;

private macro IsExistOperChangeIssue(FIID: INTEGER, EndDate:DATE): BOOL
   
   var query =  " SELECT HIST.T_FIID " +
                "  FROM dfivlhist_dbt hist " +
                " WHERE     HIST.T_FIID = ? " +
                "       AND HIST.T_ENDDATE >= ? "+
                "       AND HIST.T_VALKIND = ?";

   var Select = Dl_RSDCommand(query);

   Select.AddParam(FIID);
   Select.AddParam(EndDate);
   Select.AddParam(FI_CHANGE_ISSUER);

   var DataSet = Select.Execute();

   if(DataSet.moveNext())
     return true;
   else
     return false;
   end;   

end;

/*Проверяется, что по этому выпуску нет операций за эту и более поздние даты.
  Достаточно проверить отсутствие любых движений по собственным лотам*/
private macro IsExistChangesOperAfterDate(FIID: INTEGER, EndDate:DATE)

   var cmd = RsdCommand( RslDefCon, " DECLARE "+
                                    "      res   INTEGER; " +
                                    "   BEGIN             "+ 
                                    "      res := 0;      " +
                                    "      IF (RSB_PMWRTOFF.WRTareChangesAfterDate (?, ?, ?, ?, ?, ?, ?) = true) "+
                                    "      THEN "+
                                    "         res := 1; " +
                                    "      END IF; " +
                                    "   ? := res; " +
                                    "   \n END; ");

   cmd.addParam("p_Department",   RSDBP_IN,     -1  ); 
   cmd.addParam("p_FIID",         RSDBP_IN,     FIID ); 
   cmd.addParam("p_Party",        RSDBP_IN,     -1 ); 
   cmd.addParam("p_Contract",     RSDBP_IN,     0 ); 
   cmd.addParam("p_Portfolio",    RSDBP_IN,     -1 ); 
   cmd.addParam("p_Group",        RSDBP_IN,     -1 ); 
   cmd.addParam("p_CalcDate",     RSDBP_IN,     EndDate  ); 
   cmd.addParam("ret_val",        RSDBP_RETVAL, V_INTEGER );
   cmd.execute();

   return cmd.Value(7);

end;

private macro IsExistAvoirSrv( FIID:integer, dat:date ):BOOL
   var query, DataSet;
   var Select;

   query = " SELECT count(1) cnt " + 
           "   FROM ddepoadmo_dbt " +
           "  WHERE t_FIID = ? " +
           "    AND t_OperDate >= ? ";

   Select = RSDCommand( query );
   Select.AddParam("FIID", RSDBP_IN, FIID );
   Select.AddParam("OperDate", RSDBP_IN, dat );
   Select.Execute();

   DataSet = TRsbDataSet(Select);
   if( DataSet.moveNext() and (SQL_ConvTypeInteger(DataSet.cnt) > 0) )
      return TRUE;
   else
      return FALSE;
   end;
end;

private macro ОбработатьИзменениеЭмитента(FD, FIID: INTEGER, EndDate: DATE)

  record acc( account );
  file   mcconacc( mcconacc );                  

  var query =  " SELECT Accdoc.t_ID ID, Accdoc.t_Chapter Chapter, Accdoc.t_Account Account, Accdoc.t_Currency Currency, "+
               "        Accdoc.t_FiRole FiRole,  Categ.t_Code Code " +
               "  FROM dmcaccdoc_dbt Accdoc, dmccateg_dbt Categ, dmctempl_dbt Templ " +
               " WHERE     Accdoc.t_FIID = ? " +
               "       AND Accdoc.t_IsCommon =  chr(0)"+ 
               "       AND Accdoc.t_IsUsable = 'X' " + 
               "       AND Categ.t_ID = Accdoc.t_CatID " +
               "       AND TEMPL.T_CATID = CATEG.T_ID  "+
               "       AND TEMPL.T_NUMBER = Accdoc.t_TemplNum "+
               "        AND ( ( (Categ.t_Param1 = 1113) and (Categ.t_Class1 = 125) and (Templ.t_Value1 <> -1) ) OR " +
               "              ( (Categ.t_Param2 = 1113) and (Categ.t_Class2 = 125) and (Templ.t_Value2 <> -1) ) OR " +
               "              ( (Categ.t_Param3 = 1113) and (Categ.t_Class3 = 125) and (Templ.t_Value3 <> -1) ) OR " +
               "              ( (Categ.t_Param4 = 1113) and (Categ.t_Class4 = 125) and (Templ.t_Value4 <> -1) ) OR " +
               "              ( (Categ.t_Param5 = 1113) and (Categ.t_Class5 = 125) and (Templ.t_Value5 <> -1) ) OR " +
               "              ( (Categ.t_Param6 = 1113) and (Categ.t_Class6 = 125) and (Templ.t_Value6 <> -1) ) OR " +
               "              ( (Categ.t_Param7 = 1113) and (Categ.t_Class7 = 125) and (Templ.t_Value7 <> -1) ) OR " +
               "              ( (Categ.t_Param8 = 1113) and (Categ.t_Class8 = 125) and (Templ.t_Value8 <> -1) )    " +
               "            )                                                           " ;


  var Select = Dl_RSDCommand(query);

  Select.AddParam(FIID);
 
  var AccDataSet = Select.Execute();
  
  while(AccDataSet.moveNext() )
     // Если л/с найден и он не закрыт 
     if( GetAccount( AccDataSet.Chapter, AccDataSet.Currency, AccDataSet.Account, acc, true ) AND 
         (acc.Open_Close != "З") )

         FD.CloseAccount(AccDataSet.Code, EndDate, AccDataSet.FiRole); 

         ClearRecord(mcconacc);
         mcconacc.DocKind  = DLDOC_ISSUE;
         mcconacc.DocID    = FIID;
         mcconacc.AccDocID = AccDataSet.ID;

         OprUpdateMCCONACC( mcconacc );
     end;     
  end;

end;
                                
macro ExecuteStep( Doc, FirstDoc, _DocKind, ID_Operation, ID_Step )

   var FD, dat;
   SetBuff( Avoiriss, FirstDoc );
   FD = SPFirstDocFI( DLDOC_ISSUE, Avoiriss.FIID, Avoiriss.FIID, -1, {OurBank}, null, null, null ); 
   // Выполняем с текущей даты всегда
   dat = {curdate};

   if(IsExistOperChangeIssue(Avoiriss.FIID, dat))
      msgbox("В анкете ц/б уже было выполнено изменение эмитента");
      return 1;
   end;

   if(IsExistChangesOperAfterDate(Avoiriss.FIID, dat))
      msgbox("По выпуску есть операции за более позднюю дату");
      return 1;
   end;

   if( IsExistAvoirSrv(Avoiriss.FIID, dat+1) )
      msgbox("Ценная бумага находится на обслуживании в депозитарии. Эмитент не может быть изменён");
      return 1;
   end;
                                           /*old_issuer*/          /*new_issuer*/
   if(NOT SP_ChangeFIIssuer(Avoiriss.FIID, fininstr.Issuer,      FD.fininstr.rec.Issuer,  dat - 1) )
     msgbox("Ошибка вставки события изменения эмитента ц/б с ID = " + Avoiriss.FIID);
     return 1;
   end;

   var old_EmiKind = MC_GetKindIssuer(fininstr.Issuer);
   var new_EmiKind = MC_GetKindIssuer(FD.fininstr.rec.Issuer);

   if( (FD.fininstr.rec.Issuer > 0) and (new_EmiKind != old_EmiKind))
      ОбработатьИзменениеЭмитента(FD, Avoiriss.FIID, dat - 1);
   end;    

  return 0;
end;