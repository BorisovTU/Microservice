/*
$Name:        InvSh500.mac
$Module:      Ценные бумаги
$Description: Операция получения паев. Шаг учета затрат
*/
import InsCarryDoc, "sp_class.mac", "scservop.mac", "sp_carcm.mac";

macro ExecuteStep( Doc, FDoc )

  record deal(dl_tick);
  var    FD, dat, PaySum = $0, CostSum = $0, SummaFee = $0, NDSFee  = $0; 
  var    rq_money = TRecHandler("dlrq.dbt");
 
  SetBuff( deal, FDoc );
  FD  = SPFirstDoc( deal );
  dat = FD.DateArray[DATE_DEALSETAVOIRISS];

  if( УчетЗатратПоКомиссиямСделки_ИП( FD, Doc, dat ) == false )
     return 1;
  end; 

  if( not IsClientDeal(FD.tick.rec) ) /* наша сделка */
     rq_money = FD.GetRQ(DLRQ_TYPE_PAYMENT);
     if( SmartConvertSum( PaySum, rq_money.rec.Amount, dat, rq_money.rec.FIID, NATCUR, true ) != true )
        return 1;
     end;    
     if( SmartConvertSum( CostSum, FD.dl_leg.rec.TotalCost, dat, FD.dl_leg.rec.CFI, NATCUR, true ) != true )
        return 1;
     end;    

     SummaFee = PaySum - CostSum;
     if( FD.tick.rec.Flag5 == SET_CHAR )
        NDSFee = SummaFee * (TaxNDS/(100+TaxNDS));
     end;

     /*Проводки суммы вознаграждения*/
     if(FD.tick.rec.Flag5 == SET_CHAR) //FD.tick.rec.Flag5 - "Выделить НДС"
         SummaFee=SummaFee-NDSFee;
     end;
   
     if( not ПроводкаПоКатегориямУчета( FD, 
                                        IIF(FD.pmwrtsum.rec.GroupID == KINDPORT_CONTR, "Наш портфель ПКУ, ц/б", "Наш портфель ц/б"), "+Расчеты", /* КатегДеб , КатегКредит*/
                                        dat,                            /* Дата */
                                        1 ,                             /* Глава */
                                        FD.GetPayFIID(),                /* Валюта */
                                        SummaFee,                       /* Сумма */
                                        Doc,                            /* Документ */
                                        INPCARRY,                       /* Result_Carry */
                                        " 1",                           /* Kind_Oper */
                                        FD.tick.rec.DealCode,           /* Numb_Document */
                                        "Учет затрат на вознаграждение",/* Ground */
                                        0, GetFIRoleByPortfolio(FD.pmwrtsum.rec.GroupID)
                                      )
      )
        return 1;
     end;    

     if( (FD.tick.rec.Flag5 == SET_CHAR) and (NDSFee != $0) )
        if( not ПроводкаПоКатегориямУчета( FD, 
              "+НДС", "+Расчеты",              /* КатегДеб , КатегКредит*/
              dat,                             /* Дата */
              1 ,                              /* Глава */
              NATCUR,                          /* Валюта */
              NDSFee,                          /* Сумма */
              Doc,                             /* Документ */
              INPCARRY,                        /* Result_Carry */
              " 1",                            /* Kind_Oper */
              FD.tick.rec.DealCode,            /* Numb_Document */
              "НДС с суммы вознагражения"      /* Ground */
             ) )
           return 1;
        end;
     end;
  end;
  
  if(FD.tick.rec.ClientID == -1)
     if( ОбработатьЛот( FD, FD.pmwrtsum, dat ) == false )
        return 1;
     end;
  else
     WRTSetClientLot(FD.dl_leg.rec.PFI, FD.tick.rec.Department, FD.tick.rec.ClientID, FD.tick.rec.ClientContrID, FD.dl_leg.rec.Principal, dat, true);
  end;  

  return 0;
end;
