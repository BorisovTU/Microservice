/*
$Name:        scrou0326.mac
$Module:      Ценные бумаги
$Description: РЕПО не ОРЦБ. Шаг "Учет компенсационного платежа", "Компенсационный платеж - уменьшение", "Компенсационный платеж - увеличение"
*/
import "screpocalc.mac";

/*глобальные структуры, через которые передаются изменения условий сделок*/
private record SpChangeDlTick("dl_tick.dbt");      /*условия сделки*/
private record SpChangeDlLeg("dl_leg.dbt");        /*условия первой части сделки*/
private record SpChangeDlLegBack("dl_leg.dbt");    /*условия второй части сделки*/
private record SpChangePmwrtsum("pmwrtsum.dbt");   /*лот по 1 части*/

private var SpChangeRq = TArray; /*Массив всех ТО*/

/*сумма в операции учета купона*/
private var COUPON_SUM;
private var CalcSumFIID;
private var CalcOperDate;
private var CalcPlanDate;
private var CalcWrtTime;
private var NeedKvit;/*Участвует в квитовке*/
private record Deal( dl_tick );
private var ReturnIncomeKind = 0;

private macro ExecuteStep( Doc, FirstDoc, DocKind, ID_Operation, ID_Step )

   var FD, FD1, dat, SumPay2 = $0;

   SetBuff( Deal, FirstDoc );
   FD  = SPFirstDoc( deal, true );
   FD1 = SPFirstDoc( deal, false );

   if( SpChangeDlTick.ChangeDate <= FD1.DateArray[DATE_DEALPAY_PLAN] )
      Msgbox( "Дата компенсационного платежа должна быть больше даты оплаты по 1-й части РЕПО" );
      return 1;
   end;

   dat = SpChangeDlTick.ChangeDate;

   if( OprInsertSPTKCHNG(SpChangeDlTick.ChangeDate, SpChangeDlTick.ChangeKind, SpChangeDlTick, SpChangeDlLeg, SpChangeDlLegBack, COUPON_SUM) == false )
      return 1;
   end;

   /*В РЕПО в ТО по оплате 2ч сумма хранится без процентов - см. 121196 за исключением случая когда установлена галка "Сальдированная проводка по 2 части РЕПО ОРЦБ"*/
   SumPay2 = SpChangeDlLegBack.TotalCost;
   if( FD.ExistAvance )
      SumPay2 = SumPay2 - FD.GetRQ(DLRQ_TYPE_AVANCE).rec.Amount;
   end;

    if( SpChangeDlLegBack.IncomeRate > 0.0 )
      SumPay2 = SumPay2 - SpChangeDlLegBack.ReturnIncome;
   else
      SumPay2 = SumPay2 + SpChangeDlLegBack.ReturnIncome;
   end;

   if( ТОСквитовано(FD.GetRQ(DLRQ_TYPE_PAYMENT)) )
      Msgbox( "Невозможно выполнить действие по сделке: ТО по оплате 2 ч. РЕПО уже сквитовано" );
      return 1;
   end;

   FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.Amount = SumPay2;
   FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.FactAmount = FD.GetRQ(DLRQ_TYPE_PAYMENT).rec.Amount;
   if( DL_ChangeDLRQ(FD.GetRQ(DLRQ_TYPE_PAYMENT), dat, DLRQ_ACTION_PARTEXEC) != 0 )
      return 1;
   end;

   /*только если уже было ТО по процентам*/
   if( SpChangeDlLegBack.ReturnIncome != 0 )
      if( FD.ExistPC() )
         if( ТОСквитовано(FD.GetRQ(DLRQ_TYPE_INCREPO)) )
            Msgbox( "Невозможно выполнить действие по сделке: ТО по процентам 2 ч. РЕПО уже сквитовано" );
            return 1;
         end;
         FD.GetRQ(DLRQ_TYPE_INCREPO).rec.Amount = SpChangeDlLegBack.ReturnIncome;
         FD.GetRQ(DLRQ_TYPE_INCREPO).rec.FactAmount = SpChangeDlLegBack.ReturnIncome;
         if( DL_ChangeDLRQ(FD.GetRQ(DLRQ_TYPE_INCREPO), dat, DLRQ_ACTION_UPDATE) != 0 )
            return 1;
         end;
      end;
   end;

   var Increase:bool = false;
   if( FD.dl_leg.rec.TotalCost < SpChangeDlLegBack.TotalCost ) /*увеличение*/
      Increase = true;
   end;

   if( СоздатьКомпенсационныйПлатежТО(FD, dat, COUPON_SUM, CalcSumFIID, Increase, CalcWrtTime, iif(NeedKvit == SET_CHAR,true,false)) != 0 )
      return 1;
   end;

   /*обновить dlsum*/
   if( not ИзменитьСуммыТребОбяз(FD, COUPON_SUM, FD.GetPayFIID(), dat) )
      return 1;
   end;

   /*если ТО участвует в квитовке, то вставляем шаг ожидания квитовки*/
   if( NeedKvit == SET_CHAR )
      DefineOprDate(DATE_DEALKVIT, dat);
      if( not InsertOprStatus(SP_OPERSTATUS_SECURDEALS_KTO/*Квитовка плановых ТО*/, SP_OPERSTATUS_SECURDEALS_KTO_RUN/*Выполнить*/) )
         MsgBox("Ошибка при установке статуса <Квитовка плановых ТО> для операции.");
         return 1;
      end;
   end;

   return 0;
end;
