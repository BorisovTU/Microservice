/*
$Name:             ws_QIValidPeriod.mac
$Module:           АРНУ 
$Description:      WEB. АРНУ. Функции исполнения, проверки включения КИ в монитор событий 
*/
import "secinter.mac", "dlquery.mac", "DLReport_h.mac", "dlreport.mac", "RegistrReport.mac", "ws_common.mac";
import ws_file;

PRIVATE CONST СТАТУС_ВКЛЮЧЕН = 1;
PRIVATE CONST ВИД_ПО_ДОКУМЕНТАМ = 2;
PRIVATE CONST PTCK_CONTR = 1;

PRIVATE CONST ReportHeader =  
     "┌──────────────────────┬────────────────────────────────┬────────────────────────────────┬─────────────────────────┬─────────────────────┐\n" +
     "│      Код клиента     │      Наименование клиента      │ Номер регистрации в реестре КИ │ Дата включения в реестр │    Дата проверки    │\n" +    
     "├──────────────────────┼────────────────────────────────┼────────────────────────────────┼─────────────────────────┼─────────────────────┤\n" ; 


PRIVATE CONST ReportHeaderForCLOB =  
     " ────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────  \n" +
     "│      Код клиента     │      Наименование клиента      │ Номер регистрации в реестре КИ │ Дата включения в реестр │    Дата проверки    │ \n" +    
     " ────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────  \n" ; 

PRIVATE CONST WhereCondition = 
              "	   WHERE PARTY.T_PARTYID = QI.T_PARTYID     	"+
              "	     AND v_qiHist.T_STATE =   "+ string(СТАТУС_ВКЛЮЧЕН) + 
              "	     AND v_qiHist.T_KIND = 	"+  string (ВИД_ПО_ДОКУМЕНТАМ) +
              "	     AND qi.t_PartyID = v_qiHist.t_PartyID	"+
              "	     AND v_qiHist.t_BegDate = (select max(VHist.t_BegDate) from DV_SCQINVHIST VHist                	"+
              "	                                    where VHist.t_PartyID = v_qiHist.t_PartyID                     	"+
              "	                                      and VHist.t_BegDate < ?                                    	"+
              "	                                      and (VHist.t_EndDate >= ?  or VHist.t_EndDate = TO_DATE ('01.01.0001', 'dd.mm.yyyy'))                                    	"+
              "	                                      and VHist.t_State = v_qiHist.t_State)	";




PRIVATE MACRO GetQIDataSetQuery()
  return (
              "	SELECT 	"+
                      " V_PATRCODE.T_CODE AS ClientCode, "+
              "         PARTY.T_SHORTNAME AS ClientName, "+
              "         QI.T_CODE AS RegistrNumber, "+
              "	         v_qiHist.t_BegDate AS IncludeDate,	"+
              "         QI.T_ControlDate As ControlDate "+
              "	    FROM dscqinv_dbt qi,	"+
              "          dpartcode_dbt v_patrcode, "+
              "	         dparty_dbt party,        	"+
              "	         DV_SCQINVHIST v_qiHist	"+
              WhereCondition + 
              "     AND V_PATRCODE.T_PARTYID(+) = QI.T_PARTYID "+
              "     AND V_PATRCODE.T_CODEKIND(+) = " + string(PTCK_CONTR) +
              "	ORDER BY IncludeDate, ClientName ");
END;

PRIVATE MACRO GetDateNextYear(oldDate)
   var day, month, year;
   var newDate = oldDate;
   DateSplit(oldDate, day, month, year );
   if ( (month == 2) and (day == 29) )
      month = 3;
      day = 1;
   end;

   newDate = date(day, month, year + 1 );

   return newDate;
END;

/*Макрос проверки включения события в монитор*/
MACRO CheckInclQIValidPeriod(MonDate): bool
   var needInclude = false;
   var needScan = true;

   var query =  
              "	SELECT 	"+
              "	         v_qiHist.t_BegDate AS IncludeDate,	"+
              "          qi.t_ControlDate AS ControlDate "+
              "	    FROM dscqinv_dbt qi,	"+
              "	         dparty_dbt party,        	"+
              "	         DV_SCQINVHIST v_qiHist	"+
              WhereCondition + 
              "	ORDER BY IncludeDate	";

   var Select = Dl_RSDCommand(query); 

   Select.AddParam(MonDate);
   Select.AddParam(MonDate);

   var DataSet = Select.Execute();

   while(DataSet.moveNext() and needScan)
     if( (MonDate >= date(DataSet.ControlDate) - GetSecurCheckStatusQIValidPeriod() ) and  (MonDate <= date(DataSet.ControlDate)))
       needInclude = true;
       needScan = false;
     end;
   end;

   return needInclude;
END;

/*Макрос Проверки исполнения. Список необработанных объектов */
MACRO PrintQIValidPeriodReport(MonDate, TypeEvent)
   var report = "";

   var Select = Dl_RSDCommand( GetQIDataSetQuery() ); 
  
   Select.AddParam(MonDate);
   Select.AddParam(MonDate);
                                                                                     
   var DataSet = Select.Execute() ;                            

   while(DataSet.moveNext() )     
     if( (MonDate >= date(DataSet.ControlDate) - GetSecurCheckStatusQIValidPeriod() ) and  (MonDate <= date(DataSet.ControlDate)))
       /*Печать строки*/
       report = report + string("│ ", DataSet.ClientCode:20:c, " │ ", DataSet.ClientName:30:c, " │ ", DataSet.RegistrNumber:30:c, " │ ", Date(DataSet.IncludeDate):23:c," │ ", Date(DataSet.ControlDate):19:c," │", "\n");
     end;
   end;                                                       

  if(report != "")
     report = ReportHeaderForCLOB + report + 
     " ────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────\n" ; 

     var cmd = RSDCommand("UPDATE DEVENT_DBT E SET E.T_FMTCLOBDATA_XXXX = ? WHERE E.T_TYPEEVENT = ? AND E.T_CREATEDATE = ? ");
     cmd.addParam( "", RSDBP_IN, report ); 
     cmd.addParam( "", RSDBP_IN, TypeEvent );
     cmd.addParam( "", RSDBP_IN, MonDate );
     cmd.execute();   
  end;

  return STATE_UNDEFINE; /*установка статуса неопределено по ТЗ*/
END;

/*Выпуск отчета в Excel*/
PRIVATE CLASS (DL_CReportTemplate) QIValidPeriodReportXls(MonitorDate)
  var MonDate;
  var IsWebservice = true;

  PRIVATE MACRO PrintMode():INTEGER
    return DL_OUTREPORT_EXCEL;
  END;                                                                                                                                                                         

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
     this.SetActiveSheet( "Список КИ" );
     this.RegisterTable("N1_MainTable", ReportHeader,
                            "N1_Code", "N1_Name", "N1_Number", "N1_Date","N1_CheckDate" );
  END;

  PRIVATE MACRO EndReport()
     EndTable();
  END;

  PRIVATE MACRO PrintReportLine(DataSet)
     this.PrintTableLine(
                        "N1_Code:c",      string( DataSet.ClientCode ),  null ,
                        "N1_Name:c",      string( DataSet.ClientName ),  null ,
                        "N1_Number:c",    string( DataSet.RegistrNumber ), null ,
                        "N1_Date:c",      date( DataSet.IncludeDate ) ,  null,
                        "N1_CheckDate:c", date( DataSet.ControlDate ) ,  null    );    
  END;

  PRIVATE MACRO Create()
     var itDate;
     var report = "";
                  
     var Select = Dl_RSDCommand( GetQIDataSetQuery() ); 
    
     Select.AddParam(MonDate);
     Select.AddParam(MonDate);

     var DataSet = Select.Execute() ;

     while(DataSet.moveNext() )     
       if( (MonDate >= date(DataSet.ControlDate) - GetSecurCheckStatusQIValidPeriod() ) and  (MonDate <= date(DataSet.ControlDate)))
         /*Печать строки*/
         PrintReportLine(DataSet);
       end;
     end;                                                       

  END;

  MonDate = MonitorDate;
  initDL_CReportTemplate( null, "Report_QIValidPeriod.xls", null, IsWebservice );
END; /*CLASS QIValidPeriodReportXls*/


/*Функция обработки события монитора.
  Отчёт со списком инвесторов, у которых истекает срок действия статуса КИ*/
MACRO ExecQIValidPeriodReport(MonitorDate)
  var FullNames = TArray(), Report;
  Report = QIValidPeriodReportXls(MonitorDate);
  Report.Run();
  FullNames = Report.GetFullReportFileNames(); 

  return CreateFileContainer(FullNames[0]);
END;

