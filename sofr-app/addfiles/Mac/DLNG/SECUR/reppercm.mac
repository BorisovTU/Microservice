/*******************************************************************************
 DESCRIPTION  :   Протокол операции расчета периодических комиссий
 PROGRAMMED BY:   Трафименков Г.М.
 CREATION DATE:   30.01.04
*******************************************************************************/
import "sprepfun.mac", CTInter, "SCPeriodComm.mac";


/* Вывод данных идет через функцию PrintLineFS, которая печатает на экран
   или в файл. Печать в файл нужна для корректного вывода данных при
   выполнении сервисной операции. На 29.01.04 отказаться от печати в файл
   не удалось. */

private const
   /* Типы объектов */
   _OBJTYPE_SFDEFCOM = 663;

private var TempFile = TBFile( "reppercm.tmp","W" );

/* Сбор данных во временный файл.
      CommDate    - дата операции
      ClientID    - субъект, заданный в параметрах операции (может быть -1)
      ConID       - договор, заданный в параметрах операции (может быть -1) */
private macro CollectData( CommDate, ClientID, ConID )
   var
      sfcontr        = TBFile( "sfcontr", "r", 2 ),
      sfconcom       = TBFile( "sfconcom", "r", 0 ),
      sfdefcom       = TBFile( "sfdefcom", "r", 1 ),
      ClientShName, ContractorShName,
      errcode, errname,
      continue_cicle, continue2;

   InitProgressBar( -1, "Выпуск протокола операции", "Сбор данных" );

   ClearGlobalTmp( "dreppercm_tmp" );

   /* Перебираем договора. */
   sfcontr.Clear();
   sfcontr.rec.PartyID  = ClientID;
   sfcontr.rec.ServKind = PTSK_STOCKDL;
   sfcontr.rec.FIID     = -1;
   continue_cicle = sfcontr.GetGE();
   while(   continue_cicle
            and ((ClientID == -1) or (sfcontr.rec.PartyID == ClientID))
            )
      /* Договор нам подходит? */
      if( (sfcontr.rec.ServKind == PTSK_STOCKDL)
          and ((ConID == -1) or (sfcontr.rec.ID == ConID))
            )

         UseProgressBar;
         ClientShName      = GetPartyShortNameByID(sfcontr.rec.PartyID);
         ContractorShName  = GetPartyShortNameByID(sfcontr.rec.ContractorID);

         /* Перебираем комиссии договора */
         sfconcom.Clear;
         sfconcom.rec.ObjectId   = sfcontr.rec.ID;
         sfconcom.rec.ObjectType = OBJTYPE_SFCONTR;
         sfconcom.rec.FeeType = SF_FEE_TYPE_PERIOD;
         continue2 = sfconcom.GetGE();
         while(   continue2
                  and (sfconcom.rec.ObjectId == sfcontr.rec.ID)
                  and (sfconcom.rec.ObjectType == OBJTYPE_SFCONTR)
                  and (sfconcom.rec.FeeType == SF_FEE_TYPE_PERIOD)
                  )
            /* Получим рассчитанную комиссию */
            sfdefcom.Clear();
            sfdefcom.rec.ConID      = sfconcom.rec.ObjectId;
            sfdefcom.rec.FeeType    = sfconcom.rec.FeeType;
            sfdefcom.rec.CommNumber = sfconcom.rec.CommNumber;
            sfdefcom.rec.DateFee    = CommDate;
            if( sfdefcom.GetEQ() )
               ClearRecord(TempFile);
               TempFile.rec.ConID             = sfcontr.rec.ID;
               TempFile.rec.ConNumber         = sfcontr.rec.Number;
               TempFile.rec.ClientID          = sfcontr.rec.PartyID;
               TempFile.rec.ClientShName      = ClientShName;
               TempFile.rec.ContractorID      = sfcontr.rec.ContractorID;
               TempFile.rec.ContractorShName  = ContractorShName;
               TempFile.rec.ComissCode        = GetComissCode(
                                                sfdefcom.rec.FeeType,
                                                sfdefcom.rec.CommNumber );
               TempFile.rec.sfdefcomID        = sfdefcom.rec.ID;
               TempFile.rec.FIID_commSum      = sfdefcom.rec.FIID_commSum;
               TempFile.rec.FIID_Ccy          = GetFinInstrCcy(
                                                sfdefcom.rec.FIID_commSum );
               TempFile.rec.CommSum           = sfdefcom.rec.CommSum;
               TempFile.rec.NDSSum            = sfdefcom.rec.NDSSum;
               TempFile.rec.Status            = sfdefcom.rec.Status;
               if( not Insert(TempFile) )
                  errcode = status(errname);
                  RunError(   "Ошибка при вставке данных во временный файл ("
                              + errcode + ") " + errname );
               end;
            end;
            continue2 = sfconcom.Next();
         end;
      end;
      continue_cicle = sfcontr.Next();
   end;

   RemProgressBar;
end;


/********** Таблицы **********************************************************
      [
                                         РАСЧЕТ КОМИССИЙ

       ┌────────────────────┬────────────────────┬──────────────────┬─────────────────┐
       │    Код комиссии    │ Получатель комиссии│      Сумма       │    В т.ч. НДС   │
       │                    │                    │                  │                 │
       ├────────────────────┴────────────────────┴──────────────────┴─────────────────┤
       │Инвестор ####################  договор № ####                                 │
       ├────────────────────┬────────────────────┬──────────────────┬─────────────────┤
       │####################│####################│############## ###│############# ###│
       ├────────────────────┼────────────────────┼──────────────────┼─────────────────┤
       │####################│####################│############## ###│############# ###│
       ├────────────────────┴────────────────────┼──────────────────┼─────────────────┤
       │        Итого по инвестору за день       │############## ###│############# ###│
       │                                         │############## ###│############# ###│
       ├─────────────────────────────────────────┴──────────────────┴─────────────────┤
       │Инвестор ####################  договор № ####                                 │
       ├────────────────────┬────────────────────┬──────────────────┬─────────────────┤
       │                    │                    │                  │                 │
       ├────────────────────┼────────────────────┼──────────────────┼─────────────────┤
       │                    │                    │                  │                 │
       ├────────────────────┴────────────────────┼──────────────────┼─────────────────┤
       │        Итого по инвестору за день       │                  │                 │
       │                                         │                  │                 │
       └─────────────────────────────────────────┴──────────────────┴─────────────────┘


                                         ОПЛАТА КОМИССИЙ

       ┌────────────────────┬────────────────────┬────────────────────┬──────────────────┐
       │    Код комиссии    │       Счет Дт      │       Счет Кт      │      Сумма       │
       │                    │                    │                    │                  │
       ├────────────────────┴────────────────────┴────────────────────┴──────────────────┤
       │Инвестор ####################  договор № ###############                         │
       ├────────────────────┬────────────────────┬────────────────────┬──────────────────┤
       │                    │                    │                    │                  │
       ├────────────────────┼────────────────────┼────────────────────┼──────────────────┤
       │                    │                    │                    │                  │
       ├────────────────────┴────────────────────┴────────────────────┴──────────────────┤
       │Инвестор ####################  договор № ###############                         │
       ├────────────────────┬────────────────────┬────────────────────┬──────────────────┤
       │                    │                    │                    │                  │
       ├────────────────────┼────────────────────┼────────────────────┼──────────────────┤
       │                    │                    │                    │                  │
       └────────────────────┴────────────────────┴────────────────────┴──────────────────┘
      ];
*****************************************************************************/

private const
   TLine_None           = 0,
   /* линии таблицы "Расчет комиссий" */
   TLine_CalcHead       = 1,
   TLine_CalcClientHead = 2,
   TLine_CalcLine       = 3,
   TLine_CalcSummary    = 4,
   TLine_CalcFoot       = 5,
   /* линии таблицы "Оплата комиссий" */
   TLine_PaymHead       = 6,
   TLine_PaymClientHead = 7,
   TLine_PaymLine       = 8,
   TLine_PaymFoot       = 9;

private var
   PrevRow  = TLine_None;

/* Печатаем горизонтальные линии таблицы.
      NewRow  - строка, которая будет распечатана следующей */
private macro PrintTableLine( PrintToFile, OutFile, NewRow )
   /* таблица расчета */
   if(   (NewRow == TLine_CalcHead) )
      PrintLineFS( PrintToFile, OutFile, "" );
      PrintLineFS( PrintToFile, OutFile, "" );
      PrintLineFS( PrintToFile, OutFile, "                                     РАСЧЕТ КОМИССИЙ" );
      PrintLineFS( PrintToFile, OutFile, "" );
      PrintLineFS( PrintToFile, OutFile, "┌────────────────────┬────────────────────────────────────────────────────────────┬──────────────────┬─────────────────┐" );
      PrintLineFS( PrintToFile, OutFile, "│    Код комиссии    │                   Получатель комиссии                      │      Сумма       │    В т.ч. НДС   │" );
      PrintLineFS( PrintToFile, OutFile, "│                    │                                                            │                  │                 │" );
   elif( (NewRow == TLine_CalcClientHead) and (PrevRow == TLine_CalcHead) )                                              
      PrintLineFS( PrintToFile, OutFile, "├────────────────────┴────────────────────────────────────────────────────────────┴──────────────────┴─────────────────┤" );
   elif( (NewRow == TLine_CalcClientHead) and (PrevRow == TLine_CalcSummary) )                                           
      PrintLineFS( PrintToFile, OutFile, "├─────────────────────────────────────────────────────────────────────────────────┴──────────────────┴─────────────────┤" );
   elif( (NewRow == TLine_CalcLine) and (PrevRow == TLine_CalcClientHead) )                                              
      PrintLineFS( PrintToFile, OutFile, "├────────────────────┬────────────────────────────────────────────────────────────┬──────────────────┬─────────────────┤" );
   elif( (NewRow == TLine_CalcLine) and (PrevRow == TLine_CalcLine) )                                                    
      PrintLineFS( PrintToFile, OutFile, "├────────────────────┼────────────────────────────────────────────────────────────┼──────────────────┼─────────────────┤" );
   elif( (NewRow == TLine_CalcSummary) )                                                                                 
      PrintLineFS( PrintToFile, OutFile, "├────────────────────┴────────────────────────────────────────────────────────────┼──────────────────┼─────────────────┤" );
   elif( (NewRow == TLine_CalcFoot) )                                                                                    
      PrintLineFS( PrintToFile, OutFile, "└─────────────────────────────────────────────────────────────────────────────────┴──────────────────┴─────────────────┘" );
   /* таблица оплаты */
   elif( (NewRow == TLine_PaymHead) )
      PrintLineFS( PrintToFile, OutFile, "" );
      PrintLineFS( PrintToFile, OutFile, "" );
      PrintLineFS( PrintToFile, OutFile, "                                     ОПЛАТА КОМИССИЙ" );
      PrintLineFS( PrintToFile, OutFile, "" );
      PrintLineFS( PrintToFile, OutFile, "┌────────────────────┬────────────────────┬────────────────────┬──────────────────┐" );
      PrintLineFS( PrintToFile, OutFile, "│    Код комиссии    │       Счет Дт      │       Счет Кт      │      Сумма       │" );
      PrintLineFS( PrintToFile, OutFile, "│                    │                    │                    │                  │" );
   elif( (NewRow == TLine_PaymClientHead) )
      PrintLineFS( PrintToFile, OutFile, "├────────────────────┴────────────────────┴────────────────────┴──────────────────┤" );
   elif( (NewRow == TLine_PaymLine) and (PrevRow == TLine_PaymClientHead) )
      PrintLineFS( PrintToFile, OutFile, "├────────────────────┬────────────────────┬────────────────────┬──────────────────┤" );
   elif( (NewRow == TLine_PaymLine) and (PrevRow == TLine_PaymLine) )
      PrintLineFS( PrintToFile, OutFile, "├────────────────────┼────────────────────┼────────────────────┼──────────────────┤" );
   elif( (NewRow == TLine_PaymFoot) )
      PrintLineFS( PrintToFile, OutFile, "└────────────────────┴────────────────────┴────────────────────┴──────────────────┘" );
   end;

   PrevRow = NewRow;  
end;

/* Класс для хранения суммы, НДС и валюты. */
private class TSumInfo( _Sum, _NDS, _FIID, _FIID_Ccy )
   var
      Sum      = _Sum,
      NDS      = _NDS,
      FIID     = _FIID,
      FIID_Ccy = _FIID_Ccy;
end;

/* Печатаем таблицу с расчетом комиссий. */
private macro ПечатьТаблицы_РасчетКомиссий( OutFile, PrintToFile )

   /* Массив для хранения сумм комиссий и НДС в различных валютах. */
   class (TArray) TSumArray

      /* Очистить массив. */
      macro ClearArray
         this.Size = 0;
      end;

      /* Добавить сумму в массив. */
      macro AddSum( Sum, NDS, FIID, FIID_Ccy )
         var
            Counter = 0, Counter2;

         /* Ищем в массиве сумму в нужной валюте. */      
         while( (Counter < this.Size) and (this.(Counter).FIID < FIID) )
            Counter = Counter + 1;
         end;

         if( (Counter < this.Size) and (this.(Counter).FIID == FIID) )
            /* Нашли подходящую запись. Добавим сумму в нее. */
            this.(Counter).Sum   = this.(Counter).Sum + Sum;
            this.(Counter).NDS   = this.(Counter).NDS + NDS;
         else
            /* Запись не нашли. Нужно вставить.
               Перенесем существующие записи на одну вперед. */
            Counter2 = this.Size-1;
            while( Counter2 >= Counter )
               this.(Counter2 + 1) = this.(Counter2);
               Counter2 = Counter2 - 1;
            end;

            /* Вставим запись в освободившееся место. */
            this.(Counter) = TSumInfo(Sum, NDS, FIID, FIID_Ccy);
         end;
      end;
   end; 

   var
      SumArray          = TSumArray,
      CntPrintedRows    = 0,
      PrevClientID      = null,
      PrevContractorID  = null,
      Sum, NDS, Msg;

   /* Печатаем итоги по инвестору. Очищаем итоги. */
   macro PrintClientSummary
      var
         FICounter;

      PrintTableLine( PrintToFile, OutFile, TLine_CalcSummary );
      Msg = "Итого по инвестору за день";

      FICounter = 0;
      while( FICounter < SumArray.Size )
         PrintLineFS( PrintToFile, OutFile,
                        "│" + String(Msg:81:c) + "│"
                        + String(SumArray[FICounter].Sum:14) + " "
                        + String(SumArray[FICounter].FIID_Ccy:3) + "│"
                        + String(SumArray[FICounter].NDS:13) + " "
                        + String(SumArray[FICounter].FIID_Ccy:3) + "│" );
         Msg = "";
         FICounter = FICounter + 1;
      end;

      SumArray.ClearArray();
   end;

   ClearRecord(TempFile);
   TempFile.Rewind();
   while( TempFile.Next() )
   
      /* Изменился инвестор или договор? */
      if(   (TempFile.rec.ClientID != PrevClientID)
            or (TempFile.rec.ContractorID != PrevContractorID)
            )

         if( CntPrintedRows == 0 )
            PrintTableLine( PrintToFile, OutFile, TLine_CalcHead );
         else
            PrintClientSummary;
         end;

         PrintTableLine( PrintToFile, OutFile, TLine_CalcClientHead );
         PrintLineFS( PrintToFile, OutFile,
                        "│Инвестор " + String(TempFile.rec.ClientShName:20)
                        + "  договор № " + String(TempFile.rec.ConNumber:15)
                        + "                                                              │" );

         PrevClientID      = TempFile.rec.ClientID;
         PrevContractorID  = TempFile.rec.ContractorID;
      end;

      NDS = TempFile.rec.NDSSum;
      Sum = TempFile.rec.CommSum + TempFile.rec.NDSSum;
      PrintTableLine( PrintToFile, OutFile, TLine_CalcLine );
      PrintLineFS( PrintToFile, OutFile,
                     "│" + String(TempFile.rec.ComissCode:20) + "│"
                     + String(TempFile.rec.ContractorShName:60) + "│"
                     + String(Sum:14) + " " + String(TempFile.rec.FIID_Ccy:3) + "│"
                     + String(NDS:13) + " " + String(TempFile.rec.FIID_Ccy:3) + "│"
                     );

      if( (Sum > $0) or (NDS > $0) )
         SumArray.AddSum(sum, NDS, TempFile.rec.FIID_CommSum, TempFile.rec.FIID_Ccy);
      end;

      CntPrintedRows = CntPrintedRows + 1;
   end;

   /* Напечатаем конец таблицы */
   if( CntPrintedRows > 0 )
      PrintClientSummary;
      PrintTableLine( PrintToFile, OutFile, TLine_CalcFoot );
   end;
end;

/* Печатаем таблицу с оплатой комиссий. */
private macro ПечатьТаблицы_ОплатаКомиссий(  OutFile, PrintToFile,
                                             NDSReceiverAccount, CommDate:Date )
   var
      sfsi_deb          = TRecHandler( "sfsi.dbt" ),
      sfsi_cred         = TRecHandler( "sfsi.dbt" ),
      sfdefcom          = TBFile( "sfdefcom.dbt" ),
      CntPrintedRows    = 0,
      PrevClientID      = null,
      PrevContractorID  = null,
      NDSAccount;

   VAR rContr = TRecHandler( "sfcontr.dbt" );
   var Ком, CommSum, FIID_CommSumCcy;

   /* Перебираем комиссии */
   ClearRecord(TempFile);
   TempFile.Rewind();
   while( TempFile.Next() )

      /* Только оплаченные нас интересуют. */
      if(   (TempFile.rec.Status == SFDEFCOM_STATUS_PAYED)
            and ((TempFile.rec.CommSum > $0) or (TempFile.rec.NDSSum > $0)) )

         /* Получим sfdefcom */
         sfdefcom.Clear();
         sfdefcom.rec.ID = TempFile.rec.sfdefcomID;
         if( not sfdefcom.GetEQ() )
            RunError( "Не могу получить запись в sfdefcom.dbt" );
         end;

         /* Получим sfsi */
         if( SfGetSI( _OBJTYPE_SFDEFCOM, sfdefcom, sfsi_deb, sfsi_cred) != 0 )
            RunError( "Не могу получить платежные инструкции ПЗО" );
         end;

         /* Изменился инвестор или договор? */
         if(   (TempFile.rec.ClientID != PrevClientID)
               or (TempFile.rec.ContractorID != PrevContractorID)
               )
   
            if( CntPrintedRows == 0 )
               PrintTableLine( PrintToFile, OutFile, TLine_PaymHead );
            end;
   
            PrintTableLine( PrintToFile, OutFile, TLine_PaymClientHead );
            PrintLineFS( PrintToFile, OutFile,
                           "│Инвестор " + String(TempFile.rec.ClientShName:20)
                           + "  договор № " + String(TempFile.rec.ConNumber:15)
                           + "                         │" );
  
            PrevClientID      = TempFile.rec.ClientID;
            PrevContractorID  = TempFile.rec.ContractorID;
         end;

         SfGetContr(TempFile.rec.ConID,rContr);
         Ком = CPeriodCommission( rContr, sfdefcom.rec.CommNumber, sfdefcom.rec.ID );
         FIID_CommSumCcy = GetFinInstrCcy(Ком.ВалютаОплаты());

         if( TempFile.rec.CommSum > $0 )
            CommSum = TempFile.rec.CommSum;

            if( Ком.ВалютаОплаты() != TempFile.rec.FIID_commSum )
               if( not SmartConvertSum( CommSum, CommSum, Ком.ВычислитьДатуКурса(CommDate), TempFile.rec.FIID_commSum, Ком.ВалютаОплаты(), true ) )
                  CommSum = TempFile.rec.CommSum;
               end;
            end;

            PrintTableLine( PrintToFile, OutFile, TLine_PaymLine );
            PrintLineFS( PrintToFile, OutFile,
                           "│" + String(TempFile.rec.ComissCode:20:c) + "│"
                           + String(sfsi_deb.rec.Account:20) + "│"
                           + String(sfsi_cred.rec.Account:20) + "│"
                           + String(CommSum:14) + " "
                           + String(FIID_CommSumCcy:3) + "│" );
         end;

         if( TempFile.rec.NDSSum > $0 )
            /* Если счет получателя НДС не задан, то берем счет по
               категории учета "-НДС". */
            if( sfsi_cred.rec.ReceiverNDSAccount != "" )
               NDSAccount = sfsi_cred.rec.ReceiverNDSAccount;
            else
               NDSAccount = NDSReceiverAccount;
            end;

            CommSum = TempFile.rec.NDSSum;

            if( Ком.ВалютаОплаты() != TempFile.rec.FIID_commSum )
               if( not SmartConvertSum( CommSum, CommSum, Ком.ВычислитьДатуКурса(CommDate), TempFile.rec.FIID_commSum, Ком.ВалютаОплаты(), true ) )
                  CommSum = TempFile.rec.NDSSum;
               end;
            end;

            PrintTableLine( PrintToFile, OutFile, TLine_PaymLine );
            PrintLineFS( PrintToFile, OutFile,
                           "│" + String("НДС":20:c) + "│"
                           + String(sfsi_deb.rec.Account:20) + "│"
                           + String(NDSAccount:20) + "│"
                           + String(CommSum:14) + " "
                           + String(FIID_CommSumCcy:3) + "│" );
         end;

         CntPrintedRows = CntPrintedRows + 1;
      end;
   end;

   /* Напечатаем конец таблицы */
   if( CntPrintedRows > 0 )
      PrintTableLine( PrintToFile, OutFile, TLine_PaymFoot );
   end;
end;

/* Получаем счет для НДС по категории учета. */
private macro GetNDSReceverAccount( DlComm )
   var
      FD, FindAccount;

   FD = SPFirstDocDLCOMM( DlComm );
   if( FD.FindNumberAccount("-НДС", FindAccount) )
      return FindAccount;
   else
      return null;
   end;
end;

/* Печатаем тело протокола */
private macro RepProcessPeriodComm_PrintBody( OutFile, PrintToFile, DlComm )

   CollectData( DlComm.CommDate, DlComm.ClientID, DlComm.ContractID );
   ПечатьТаблицы_РасчетКомиссий( OutFile, PrintToFile );
   ПечатьТаблицы_ОплатаКомиссий( OutFile, PrintToFile,
                                 GetNDSReceverAccount(DlComm), DlComm.CommDate );
end;

/* Печать заголовка протокола. */
private macro RepProcessPeriodComm_PrintHead( OutFile, PrintToFile, DlComm )
   PrintLineFS( PrintToFile, OutFile, "" );
   PrintLineFS( PrintToFile, OutFile, "                     ПРОТОКОЛ ОПЕРАЦИИ ОБРАБОТКИ ПЕРИОДИЧЕСКИХ КОМИССИЙ" );
   PrintLineFS( PrintToFile, OutFile,
                  "                  Дата операции: "
                  + String(DlComm.CommDate:10) + "      Код: "
                  + String(DlComm.CommCode:20:l) );
end;

/* Печать протокола обработки периодических комиссий. */
macro RepProcessPeriodComm_Print( DlComm )
   RepProcessPeriodComm_PrintHead( null, null, DlComm);
   RepProcessPeriodComm_PrintBody( null, null, DlComm);
   /* Стандартная подошва */
   after_44_footer();
end;


/* Заголовок таблицы с результатами обработки периодических комиссий
      OutFile     - файл для вывода
      PrintToFile - флаг, печатать в файл */
macro SvOpProcessPeriodComm_PrintHead( OutFile, PrintToFile )
   PrintLineFS( PrintToFile, OutFile, "                        РАСЧЕТ ПЕРИОДИЧЕСКИХ КОМИССИЙ");
   PrintLineFS( PrintToFile, OutFile, "┌──────────────────────────────┬───────────────────────────────────────────────────────┐" );
   PrintLineFS( PrintToFile, OutFile, "│          № Договора          │                        Сообщение                      │" );
   PrintLineFS( PrintToFile, OutFile, "├──────────────────────────────┼───────────────────────────────────────────────────────┤" );
end;

/* Печать строки таблицы с результатами обработки периодических комиссий.
      OutFile     - файл для вывода
      PrintToFile - флаг, печать в файл
      ConCode     - код договора обслуживания
      Mes         - сообщение                   */
macro SvOpProcessPeriodComm_PrintLine( OutFile, PrintToFile, ConNumber, Mes )

   PrintLineFS(   PrintToFile, OutFile,
                  "│" + String(ConNumber:64) + "│" + String(Mes:55) + "│" );
end;

/* Подвал таблицы с результатами обработки периодических комиссий и
   наш отчет, если не массовое выполнение.
      OutFile     - файл для вывода
      PrintToFile - флаг, печатать в файл
      IsMultiExec - массовое выполнение
      DlComm      - сервисная операция       */
macro SvOpProcessPeriodComm_PrintFoot( OutFile, PrintToFile, IsMultiExec, DlComm )
   PrintLineFS( PrintToFile, OutFile, "└──────────────────────────────┴───────────────────────────────────────────────────────┘");
   if( IsMultiExec != true )
      RepProcessPeriodComm_PrintHead( OutFile, PrintToFile, DlComm);
      RepProcessPeriodComm_PrintBody( OutFile, PrintToFile, DlComm);
   end;
end;
