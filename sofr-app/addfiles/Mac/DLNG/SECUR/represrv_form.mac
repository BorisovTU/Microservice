/*
$Name:             represrv_form.mac
$Module:           АРНУ
$Description:      Отчет "Расходы на формирование резервов под обесценение ценных бумаг" (НУ) - форма ввода данных
*/

import "secinter.mac", "DlRepForm_h.mac", "globals.mac", "SpRepFun.mac", "ws_txreportform.mac";
import RsbDataSet;

CONST REPRES_OLDFORMDATE = date(31, 12, 2009); //Последний день старой формы отчета

/*Номера полей в форме отчета*/
CONST PNFLD_RESERVREG_PERIOD       = 0, //Период
      PNFLD_RESERVREG_YEAR         = 1, //Год
      PNFLD_RESERVREG_REPDATE      = 2, //Отчетная дата
      PNFLD_RESERVREG_PREVREPDATE  = 3, //Предыдущая отчетная дата
      PNFLD_RESERVREG_SECURKIND    = 4, //Вид ц\б
      PNFLD_RESERVREG_ALLSECUR     = 5, //Все ц\б
      PNFLD_RESERVREG_RURSECUR     = 6, //Номинированные в rur
      PNFLD_RESERVREG_CURSECUR     = 7, //в вал.
      PNFLD_RESERVREG_ISSUER       = 8, //Выпуск ц\б
      PNFLD_RESERVREG_ISSUERNAME   = 9, //Наименование ц\б
      PNFLD_RESERVREG_DEALKIND     = 10, //Вид сделок
      PNFLD_RESERVREG_WITHCORRECT  = 11, //корректировка цен по сделкам с взаимозависимыми лицами  
      PNFLD_RESERVREG_ISAPP        = 12; //С апострофами

CONST H_PNFLD_ALLSECUR    = 22,
      H_PNFLD_RURSECUR    = 23,
      H_PNFLD_CURSECUR    = 24,
      H_PNFLD_ISSUER      = 25;


/*Виды сделок*/
CONST REPRES_DKIND_ALL       = 0, /*все*/
      REPRES_DKIND_BUY       = 1, /*покупки*/
      REPRES_DKIND_REPO_LOAN = 2; /*обратные репо и займы*/

PRIVATE VAR REPRES_DKIND_NAME = TArray();
REPRES_DKIND_NAME[REPRES_DKIND_ALL] = "все";
REPRES_DKIND_NAME[REPRES_DKIND_BUY] = "покупки";            
REPRES_DKIND_NAME[REPRES_DKIND_REPO_LOAN] = "обратные репо и займы";


/*************************************************************
                    Последний день месяца
*************************************************************/
macro LastDay ( m, y )/* передаем Месяц, Год */
   var d;
   m = m + 1;
   if (m > 12)
      m = 1;
      y = y + 1;
   end;
   DateSplit (Date(1,m,y)-1,d);
   return d;
end;

/*последний день указанного отчетного периода (период - месяц или квартал)*/
macro GetLastDay( Period:INTEGER, IsQuartal:BOOL, Year:INTEGER, DateOut:@DATE )
   var tmpDate, CurYear, LastMonth, tmpMon, tmpYear;
   DateSplit( {curdate}, null, null, CurYear );

   if( Year <= 0 ) 
      Year = CurYear;
   end;

   if( IsQuartal )
      LastMonth = (Period - 1) * 3 + 3;//последний месяц квартала
      tmpDate = Date( 1, LastMonth, Year);
   else 
      tmpDate = Date( 1, Period, Year );
   end;

   DateSplit( tmpDate, null, tmpMon, tmpYear );                

   DateOut = Date( LastDay(tmpMon,tmpYear), tmpMon, tmpYear );
end;


PRIVATE MACRO DL_ListAvoiriss( AvrKind:INTEGER, WhereCondition:STRING, AdditionalFromTable:STRING, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):BOOL
  VAR Fininstr = TRecHandler( "fininstr.dbt" );

  if( ListAvoiriss( AvrKind, Fininstr, null, null, WhereCondition, AdditionalFromTable ) )
     FldRealValue = Fininstr.rec.FIID;
     FldShowValue = Fininstr.rec.FI_Code;
     return true;
  end;
  return false;
END;

/*отчетная дата*/
MACRO FldProc_RepDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, Month, Period, EndDate = Date(0,0,0), IsQuartal;

  if( Cmd == DLG_INIT )

    if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= Date(0,0,0) )

        IsQuartal = IIF(pThis.GetLinkedValue(DL_PNFLD_PERIOD) > 12, true, false);
        Period = IIF(IsQuartal != false, pThis.GetLinkedValue(DL_PNFLD_PERIOD) - 12, pThis.GetLinkedValue(DL_PNFLD_PERIOD));

        GetLastDay( Period, IsQuartal, pThis.GetLinkedValue(DL_PNFLD_YEAR), @EndDate );
        FldRealValue = EndDate;
     end;
     FldShowValue = FldRealValue;

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     FldRealValue = FldShowValue;

     DateSplit( FldRealValue, null, Month, Year);
     /*если есть поле "начальная дата, то год относится к нему"*/
        pThis.SetLinkedValue( DL_PNFLD_YEAR, Year );
           Period = pThis.GetLinkedValue(DL_PNFLD_PERIOD);
           if( Period > 12 ) /*указан квартал*/
              Period = Period - 12;
        /*если дата не входит в заданный квартал - переставить период*/
              if( (Month < Period*3-2) OR (Month > Period*3) )
                  pThis.SetLinkedValue( DL_PNFLD_PERIOD, ((Month - 1)/3 + 1) + 12 );
              end;
           elif( Period != Month )
              pThis.SetLinkedValue( DL_PNFLD_PERIOD, Month);
           end;

     pThis.SetLinkedValue( DL_PNFLD_BEGINDATE, "" );

     if( pThis.ExistField(DL_PNFLD_BEGINDATE) )
        if( FldRealValue < pThis.GetLinkedValue(DL_PNFLD_BEGINDATE) )
           return CM_IGNORE;
        end;
     end; 
         
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;

  if(FldRealValue <= REPRES_OLDFORMDATE)
    pThis.SetLinkedValue( DL_USERFIELD, REPRES_DKIND_BUY );
    DisableFields(pThis.Data(), PNFLD_RESERVREG_DEALKIND);
  else
    EnableFields(pThis.Data(), PNFLD_RESERVREG_DEALKIND);
  end;

  return CM_DEFAULT;
END;

/*предыдущая отчетная дата*/
MACRO FldProc_PrevRepDate( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Year, CurYear, PrevPeriodYear, PrevPeriod, EndDate = Date(0,0,0), IsQuartal, Period, EndD, Month;
  if( Cmd == DLG_INIT )

     if( FldRealValue == null )
        FldRealValue = -1;
     end;
     if( FldRealValue <= Date(0,0,0) )
        IsQuartal = IIF(pThis.GetLinkedValue(DL_PNFLD_PERIOD) > 12, true, false);
        PrevPeriod = pThis.GetLinkedValue(DL_PNFLD_PERIOD) - 1;

        if( IsQuartal )
           PrevPeriod = PrevPeriod - 12;
        end;

        DateSplit( {curdate}, null, null, CurYear );
        PrevPeriodYear = IIF(pThis.GetLinkedValue(DL_PNFLD_YEAR) > 0, pThis.GetLinkedValue(DL_PNFLD_YEAR), CurYear);

        if( PrevPeriod == 0 )
           PrevPeriod = IIF(IsQuartal != false, 4, 12);
           if( pThis.GetLinkedValue(DL_PNFLD_YEAR) > 0 )
              PrevPeriodYear = PrevPeriodYear - 1;
           end; 
        end;

        GetLastDay( PrevPeriod, IsQuartal, PrevPeriodYear, @EndDate );
        FldRealValue = EndDate;
     end;
     FldShowValue = FldRealValue;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/

     FldRealValue = FldShowValue;

     if( pThis.ExistField(DL_PNFLD_ENDDATE) )
        if( pThis.GetLinkedValue(DL_PNFLD_ENDDATE) < FldRealValue )
           return CM_IGNORE;
        end;
     end; 
         
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     FldRealValue = GetDateByCalendar( FldRealValue );
     FldShowValue = FldRealValue;
  end;
  return CM_DEFAULT;
END;

/*Период*/
PRIVATE MACRO FldProc_Period( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR BeginDate, EndDate, Month, Year, BegD, EndD;

  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
        DateSplit( {curdate}, null, Month, null );
        FldRealValue = ((Month - 1)/3 + 1) + 12; /*текущий квартал (+ 12 - для NameAlg.dbt)*/
     end;
     FldShowValue = DL_GetNameAlg( 2263, FldRealValue );

  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     pThis.SetLinkedValue( DL_PNFLD_ENDDATE, "" );
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) )
     DL_ListNA( 2263, @FldShowValue, @FldRealValue, pThis.CurrentFldX(), pThis.CurrentFldY() );
  end;
  return CM_DEFAULT;
END;

MACRO FldProc_AllSecur( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldRealValue = FldShowValue = "X";
        pThis.SetLinkedValue(H_PNFLD_RURSECUR,"");
        pThis.SetLinkedValue(H_PNFLD_CURSECUR,"");
     end;
  end;
  return CM_DEFAULT;
END;

MACRO FldProc_RurSecur( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldRealValue = FldShowValue = "X";
        pThis.SetLinkedValue(H_PNFLD_ALLSECUR,"");
        pThis.SetLinkedValue(H_PNFLD_CURSECUR,"");
     end;
  end;
  return CM_DEFAULT;
END;

MACRO FldProc_CurSecur( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     FldShowValue = FldRealValue;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) )
     if( FldRealValue != "X" )
        FldRealValue = FldShowValue = "X";
        pThis.SetLinkedValue(H_PNFLD_RURSECUR,"");
        pThis.SetLinkedValue(H_PNFLD_ALLSECUR,"");
     end;
  end;
  return CM_DEFAULT;
END;

/*Код Ц/Б*/
PRIVATE MACRO FldProc_AvrCode( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  VAR Fininstr, Avoiriss, AvrKind, AddTable = "", WhereCond = "1=1", AvrAttrID, RunFromBOCB;

  if( Cmd == DLG_INIT )
     if( FldRealValue == null )
        FldRealValue = -1;
     end;

     if( FldRealValue <= 0 )
        FldShowValue = STR_ALLVALUE;
     else
        FldShowValue = ПолучитьКодФинИн(FldRealValue);
     end;
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
     Fininstr = TRecHandler( "fininstr.dbt" );
     Avoiriss = TRecHandler( "avoiriss.dbt" );

     if( (FldShowValue == STR_ALLVALUE) OR (FldShowValue == "") )
        FldShowValue = STR_ALLVALUE;
        FldRealValue = -1;
     else
        if( ПолучитьФинИн(FldShowValue, Fininstr, Avoiriss) )
           MsgBox( "Неверное значение поля." );
           return CM_IGNORE; /*отменить изменения и остаться в текущем поле*/
        end;
        FldRealValue = Fininstr.rec.FIID;

        pThis.SetLinkedValue( DL_PNFLD_AVRKIND, Fininstr.rec.AvoirKind );
     end;

     pThis.SetLinkedValue( DL_PNFLD_AVRNAME, Fininstr.rec.Name );

  elif( Cmd == DLG_KEY ) 
     RunFromBOCB = (GetIdentProgram() == CodeFor("S"));

     if( (Key == KEY_F3) OR 
         ( (RunFromBOCB == true) AND 
           ( 
             (Key == KEY_ALTF3) OR (Key == KEY_CTRLF3) 
           )
         )
       ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/

        AvrKind = pThis.GetLinkedValue(DL_PNFLD_AVRKIND);
        if( (AvrKind == NULL) OR (AvrKind < 0) )
           AvrKind = 0;
        end;

        if( RunFromBOCB == true )
           /* только неиндивидуальные (FI_IsSecurIndividualNoFind) */

           if( pThis.GetLinkedValue(H_PNFLD_RURSECUR) == "X" ) /*номинированные в валюте РФ*/
              WhereCond = WhereCond + " AND t.t_FaceValueFI = " + string(NATCUR);
           elif( pThis.GetLinkedValue(H_PNFLD_CURSECUR) == "X" ) /*номинированные в ин. валюте РФ*/
              WhereCond = WhereCond + " AND t.t_FaceValueFI != " + string(NATCUR);
           end;

           AddTable  = AddTable  + " davrkinds_dbt AKind";
           WhereCond = WhereCond + " AND (AKind.t_FI_Kind = t.t_FI_Kind AND AKind.t_AvoirKind = t.t_AvoirKind AND AKind.t_IsIndividual != 'X')";
         
           if( Key == KEY_F3 )
              /*эмиссионные (обобщенные) или (фактические, для которых не указана ссылка на обобщенный) или неэмиссионные любые*/
              WhereCond = WhereCond + " AND ( AKind.t_IsEmissive != 'X' OR ( t.t_IsGeneralized = 'X' OR t.t_GeneralizedFIID < 0 ) )";
           elif( Key == KEY_ALTF3 )
              /* фактические выпуски, у которых указана ссылка на обобщенный*/
              WhereCond = WhereCond + " AND ( t.t_IsGeneralized != 'X' AND t.t_GeneralizedFIID > 0 )";
           elif( Key == KEY_CTRLF3 )
              /*все неиндивидуальные и необобщенные (фактические) ц/б*/
              WhereCond = WhereCond + " AND t.t_IsGeneralized != 'X' ";
           end;
        end;
        DL_ListAvoiriss( AvrKind, WhereCond, AddTable, @FldShowValue, @FldRealValue );
     end;
  end;

  return CM_DEFAULT;
END;

/*Вид сделок */
PRIVATE MACRO FldProc_DealKind( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  if( Cmd == DLG_INIT ) /*Установка значения в поле*/
     if( FldRealValue == null ) /*инициализация по умолчанию*/
       if(pThis.GetLinkedValue(DL_PNFLD_ENDDATE) > REPRES_OLDFORMDATE)
         FldRealValue = REPRES_DKIND_ALL;
       else
         FldRealValue = REPRES_DKIND_BUY;
         DisableFields(pThis.Data(), PNFLD_RESERVREG_DEALKIND);
       end;
     end;
     FldShowValue = REPRES_DKIND_NAME[FldRealValue];
     
  elif( Cmd == DLG_CHANGEVALUE ) /*значение поля было изменено*/
  
  elif( (Cmd == DLG_KEY) AND (Key == KEY_F3) ) /*Вызов листалки и заполнение FldShowValue и FldRealValue*/
     if(pThis.GetLinkedValue(DL_PNFLD_ENDDATE) > REPRES_OLDFORMDATE)
       DL_ListString( null, pThis.CurrentFldX(), pThis.CurrentFldY(), @FldShowValue, @FldRealValue,
                      REPRES_DKIND_NAME[REPRES_DKIND_ALL], REPRES_DKIND_ALL,
                      REPRES_DKIND_NAME[REPRES_DKIND_BUY], REPRES_DKIND_BUY,
                      REPRES_DKIND_NAME[REPRES_DKIND_REPO_LOAN], REPRES_DKIND_REPO_LOAN 
                    );
     end;
  elif( (Cmd == DLG_KEY) AND (Key == KEY_SPACE) ) 
     if(pThis.GetLinkedValue(DL_PNFLD_ENDDATE) > REPRES_OLDFORMDATE)
       FldRealValue = REPRES_DKIND_ALL;
       FldShowValue = REPRES_DKIND_NAME[FldRealValue];
     end;
  end;
  return CM_DEFAULT;
END;


PRIVATE MACRO Default( pThis:DL_CPanel, Cmd:INTEGER, Key:INTEGER, FldShowValue:@VARIANT, FldRealValue:@VARIANT ):INTEGER
  FldShowValue = FldRealValue;
  return CM_DEFAULT;
END;

CLASS (DL_CPanel) SP_ReservReg_Panel()

  PRIVATE MACRO Init()
     VAR CurMonth, CurDay, Year, Month, PrevPeriod, PrevPeriodYear, EndDate = Date(0,0,0), BegDate = Date(0,0,0);

     DateSplit( {curdate}, CurDay, CurMonth, Year );

     /* по умолчанию - ближайший месяц, дата окончания которого не 
        превышает текущую операционную дату */
     if( LastDay( CurMonth, Year ) == CurDay )
        Month = CurMonth;
     else
        Month = CurMonth - 1;
        if( Month == 0 ) 
           Month = 12;
           Year  = Year - 1;
        end                                 
     end;
     /*дата отчета - последний день указанного отчетного периода*/
     GetLastDay(Month,false,Year,@EndDate);

     /* предыдущий отчетный период - последний день предыдущего отчетного периода */
     PrevPeriod = Month - 1;

     PrevPeriodYear = Year;

     if( PrevPeriod == 0 ) 
        PrevPeriod = 12;
        if( Year > 0 )
           PrevPeriodYear = PrevPeriodYear - 1;
        end;
     end;

     GetLastDay( PrevPeriod, false, PrevPeriodYear, @BegDate );

     /*сбросить поля, связанные с ц\б*/
     if( GetFieldByName(0,DL_PNFLD_AVRKIND) )
        SetLinkedValue( DL_PNFLD_AVRKIND, null );
     end;
     if( GetFieldByName(0,H_PNFLD_ISSUER) )
        SetLinkedValue( H_PNFLD_ISSUER, null );
     end;

     if( ВОЗМОЖНОСТЬ_ВЫБОРА_ВИДА_ЦБ() == false)
        DisableFields(This.Data(), PNFLD_RESERVREG_SECURKIND);
     end;

     AddFieldProc( 0, PNFLD_RESERVREG_PERIOD,      DL_PNFLD_PERIOD,    @FldProc_Period,      Month, 16, 6 );
     AddFieldProc( 0, PNFLD_RESERVREG_YEAR,        DL_PNFLD_YEAR,      @DL_FldProc_Year,    Year );
     AddFieldProc( 0, PNFLD_RESERVREG_REPDATE,     DL_PNFLD_ENDDATE,   @FldProc_RepDate,    EndDate);
     AddFieldProc( 0, PNFLD_RESERVREG_PREVREPDATE, DL_PNFLD_BEGINDATE, @FldProc_PrevRepDate,  BegDate );

     AddFieldProc( 0, PNFLD_RESERVREG_SECURKIND,   DL_PNFLD_AVRKIND,   @DL_FldProc_AvrKind );

     AddFieldProc( 0, PNFLD_RESERVREG_ALLSECUR,    H_PNFLD_ALLSECUR,   @FldProc_AllSecur, "X" );
     AddFieldProc( 0, PNFLD_RESERVREG_RURSECUR,    H_PNFLD_RURSECUR,   @FldProc_RurSecur, "" );
     AddFieldProc( 0, PNFLD_RESERVREG_CURSECUR,    H_PNFLD_CURSECUR,   @FldProc_Cursecur, "" );
     
     AddFieldProc( 0, PNFLD_RESERVREG_ISSUER,      H_PNFLD_ISSUER,     @FldProc_AvrCode );
     AddFieldProc( 0, PNFLD_RESERVREG_ISSUERNAME,  DL_PNFLD_AVRNAME,   @Default );

     AddFieldProc( 0, PNFLD_RESERVREG_DEALKIND,    DL_USERFIELD,       @FldProc_DealKind );

     AddFieldProc( 0, PNFLD_RESERVREG_WITHCORRECT, DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox, "X" );
     AddFieldProc( 0, PNFLD_RESERVREG_ISAPP,       DL_PNFLD_CHECKBOX,  @DL_FldProc_CheckBox, "" );
  END;

  PRIVATE MACRO Check():BOOL
     return true;
  END;

  initDL_CPanel( "sp_res.lbr", "REPRESRV" );
END;

CLASS ( TxReportForm ) WS_ReservReg_Panel(
  FormData/* : CTxReportForm*/
)
  private var ReDefineFieldNum = TArray;

  // Переопределяем номера полей

  ReDefineFieldNum[PNFLD_RESERVREG_PERIOD]      = TXREPORTFORM_WIDGET_PERIOD;
  ReDefineFieldNum[PNFLD_RESERVREG_YEAR]        = TXREPORTFORM_FLD_YEAR;
  ReDefineFieldNum[PNFLD_RESERVREG_REPDATE]     = TXREPORTFORM_FLD_REPORTDATE;
  ReDefineFieldNum[PNFLD_RESERVREG_PREVREPDATE] = TXREPORTFORM_FLD_PREV_REPORTDATE;
  ReDefineFieldNum[PNFLD_RESERVREG_SECURKIND]   = TXREPORTFORM_WIDGET_AVRKIND;
  ReDefineFieldNum[PNFLD_RESERVREG_RURSECUR]    = TXREPORTFORM_FLD_INRUB;
  ReDefineFieldNum[PNFLD_RESERVREG_CURSECUR]    = TXREPORTFORM_FLD_NOTRUB;
  ReDefineFieldNum[PNFLD_RESERVREG_ISSUER]      = TXREPORTFORM_WIDGET_AVRCODE;
  ReDefineFieldNum[PNFLD_RESERVREG_ISSUERNAME]  = TXREPORTFORM_FAKEFLD_AVRNAME;
  ReDefineFieldNum[PNFLD_RESERVREG_DEALKIND]    = TXREPORTFORM_WIDGET_DEALKIND;

  MACRO GetFieldValue(
    FieldNumber : Integer,
    ShowValue   : @Variant,
    RealValue   : @Variant
  )
    return GetFieldValueEx( ReDefineFieldNum[FieldNumber], @ShowValue, @RealValue );
  END;

  initTxReportForm( FormData );
END;
