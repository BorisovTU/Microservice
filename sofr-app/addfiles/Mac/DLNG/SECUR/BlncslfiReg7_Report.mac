/*
$Name:             BlncslfiReg7_Report.mac
$Module:           АРНУ
$Description:      Отчет "Доходы и расходы от реализации ц/б"
*/

import "blncslfi.mac";

VAR TxReg7_Form;
VAR TxReg7_Report;
PRIVATE VAR WebMenuItem; // Информация о запуске отчета из веб

/* Отчет "Доходы и расходы от реализации ц/б (кроме гос. и муниципальных ц\б, векселей и деп. Сертификатов)" */
PRIVATE CLASS (DL_CReportTemplate) SP_TxReg7_Report( Form:DL_CPanel, TemplateName:STRING, UseCSV:BOOL, IsWebService:BOOL, ReportHashCode:INTEGER )
  PRIVATE VAR m_SheetCount = 0;

  MACRO PrintMode():INTEGER
     return DL_OUTREPORT_EXCEL;
  END;

  VAR m_IsDataExist = false; /*для Web, если нет данных, то ничего не показываем*/
/* не показывать пустые отчет в Web*/
  MACRO CReport_DataExist()
    if(m_IsWebService == true )
      return (m_IsDataExist or ReportHasError() );
    else
      return true;                   /*в EW показываем листы всегда*/
    end;
  END; 

  PRIVATE MACRO BeginReport( NumCopy:INTEGER )
     m_SheetCount = 0;
  END;

  PRIVATE MACRO ExecuteReport( ObjReport:OBJECT )
    VAR AllFI,
        ShareFI,
        DeposFI,
        BondFI;
    AllFI   = TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_ALLFI );

    if( ValType(AllFI) == V_String )
      AllFI   = IIF(TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_ALLFI ) == "X", true, false);
      ShareFI = IIF(TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_SHAREFI ) == "X", true, false);
      DeposFI = IIF(TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_DEPOSFI ) == "X", true, false);
      BondFI  = IIF(TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_BONDFI ) == "X", true, false);
    end;
    ReportRunNGos(  this,
                    TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_PERIOD ),
                    TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_YEAR ),
                    TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_GROWTH ),
                    TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_CIRCINMARKET ),
                    TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_NOTCIRCINMARKET ),
                    TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_ISSUERCORPOP ),
                    TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_NOMINRUR ),
                    TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_NOMINCUR ),
                    AllFI,
                    ShareFI,
                    DeposFI,
                    BondFI,
                    TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_AVRCODE ),
                    TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_AVRGROUP ),
                    TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_BUY_SALE ),
                    TxReg7_Form.GetFieldValue( PNFLD_BLNCSFI7_REG_CLOSE_SHORTPOS ),
                    m_IsWebService,
                    WebMenuItem
                 );
  END;

  PRIVATE MACRO Create()
    ExecuteReport();
  END;

  initDL_CReportTemplate( Form, TemplateName, UseCSV, IsWebService, ReportHashCode );
END;

MACRO ReportRunEx(
  IsWebService    : Bool
 ,FormData      //: CTxReportForm
 ,ReportHashCode  : Integer
 ,menuItem      //: WsMenuItem
)
  var stat = true;
  var FullReportFileNames = TArray();
  WebMenuItem = menuItem;


  if( not IsWebService )
      TxReg7_Form = SP_TxReg7_Panel();
  else
    if( ValType( FormData ) != V_UNDEF )
      TxReg7_Form = WS_TxReg7_Panel( FormData );
    else
      stat = false;
    end;
  end;

  while( stat )
    if( not IsWebService )
      if( CheckTaxDepositoryMode() == false ) /*если не включен режим хранилища данных для налогового учета*/
       stat = TxReg7_Form.Run();
      else
        msgbox("Отчет не доступен в режиме хранилища");
        stat = false;
      end;
    end;

    if( stat )
      TxReg7_Report = SP_TxReg7_Report( null, "Report_TXBlnscfi7.xls", null, IsWebService, ReportHashCode );      
      TxReg7_Report.Run();

      if( IsWebService )
        Dl_CallInsertStat( TxReg7_Report.PrintMode(), menuItem.iCaseItem, menuItem.iIdentProgram, menuItem.szNameItem );
        FullReportFileNames = TxReg7_Report.GetFullReportFileNames();
        stat = false;
      else
        Dl_CallInsertStat( TxReg7_Report.PrintMode() );
      end;
    end;
  end;

  return FullReportFileNames;
END;
