/*
$Name:        sp_categ.mac
$Module:      Ценные бумаги
$Description: Классы для работы c категориями учета в ценных бумагах для разных видов первичных документов
*/
IMPORT "sp_class.mac", "spserv.mac";
import rcw;
/**************************************************************************/
/*  класс - первичные документы для работы с категориями учета            */ 
/*  на основе финансового инструмента                                     */
/**************************************************************************/
CLASS (SPFirst) SPFirstDocFI ( DocKind, DocID, FIID, _KindPortf, _Owner, _Quoted, _isOverValue )
    VAR fininstr,       /* фин. инструмент      */
        avoiriss,       /* фин. инструмент      */
        Owner;         /* владелец ценных бумаг*/

    /************************************************/
    /* инициализация массива параметров              */
    /************************************************/
    MACRO InitParmArray 

        ParmA[MC_TYPE_PARAMETR_FIID]      = fininstr.rec.FIID;
        //ParmA[MC_TYPE_PARAMETR_CURRENCY]  = fininstr.rec.FaceValueFI;
        ParmA[MC_TYPE_PARAMETR_PAYCURRENCY]  = fininstr.rec.FaceValueFI;

        ParmA[MC_TYPE_PARAMETR_OWNER]     = Owner;

        ParmA[MC_TYPE_PARAMETR_DOCKIND]   = this.Kind;
        ParmA[MC_TYPE_PARAMETR_DOCID]     = this.ID;
        ParmA[MC_TYPE_PARAMETR_DEPARTMENT] = {OperDprt};
    END;

    /*инициализация массива ролей ФИ, в классах наследниках может переопределяться*/
    MACRO InitFIRoleBArray
       FIRoleBArray[0] = FIROLE_BA_RESALE;            /* базовый актив для перепродажи*/
       FIRoleBArray[1] = FIROLE_BA_INVEST;            /* базовый актив для инвестирования    */
       FIRoleBArray[2] = FIROLE_BAINCONTR;            /* базовый актив в ПКУ                 */
       FIRoleBArray[3] = FIROLE_BAINPROMISSORY;       /* базовый актив в ПДО */
       FIRoleBArray[4] = FIROLE_BAINPROMISSORYSALE;   /* базовый актив для перепродажи в ПДО */
       FIRoleBArray[5] = FIROLE_BAINPROMISSORYINVEST; /* базовый актив для инвестирования в ПДО       */
       FIRoleBArray[6] = FIROLE_BA_REPO;              /* базовый актив в договорах обратной продажи   */
       FIRoleBArray[7] = FIROLE_BA_LOAN;              /* базовый актив в договорах займа              */
       FIRoleBArray[8] = FIROLE_AJUSTVALOEB;
    END;

    MACRO GetBasisFIRole(FIRole)
      if( (FIRole == null) OR (FIRole == FIROLE_UNDEF) OR (FIRole == FIROLE_BA) )
         /*это есть у всех классов (значение ФИроли по умолчанию)*/
         if((FI_IsBond(fininstr)) and (fininstr.rec.Issuer == {OurBank}))
           return FIROLE_AJUSTVALOEB;
         end;

         return FIROLE_BA_RESALE;
      end;
      return FIRole;
    END;

    MACRO GetAccFIID(Categ:string)
       if (Categ == "Наш портфель ц/б")
          var cmd = DL_RSDCommand( "select RSB_PMWRTOFF.WRTDetermineAccFI(?) DetAccFI from dual" );
          cmd.AddParam(fininstr.rec.FIID);
          var DataSet = cmd.Execute();
          if( DataSet.moveNext() )
            var tmpInt:INTEGER;
            tmpInt = SQL_ConvTypeInteger(DataSet.DetAccFI); 
            return tmpInt;
          else
            return NATCUR;
          end;
       else
         return fininstr.rec.FaceValueFI;
       end;
    END;

    /************************************************/
    /*Получить параметры                            */
    /************************************************/
    MACRO GetParametr (ParmKind, OperDate, CatCode, FIRole)
        
        var Parametr = ParmA[ParmKind];

        if( Parametr == null ) 
           if( ParmKind == MC_TYPE_PARAMETR_CURRENCY )
             if(CatCode == "Наш портфель ц/б")
               Parametr = GetAccFIID(CatCode);
             else
               Parametr = fininstr.rec.FaceValueFI;
             end;
           elif( ParmKind == MC_TYPE_PARAMETR_ISSUER )
              if( ((v_CatCode == "Наш портфель ц/б") OR (CatCode == "Наш портфель ц/б")) AND FI_IsDeposReceipt(this.fininstr.rec.FIID) )
                 Parametr = this.ПолучитьЭмитентаСвязОбъектаДР(this.fininstr);
              else
                 Parametr  = this.fininstr.rec.Issuer;
              end;
           else
               Parametr = -1;
           end;
        end;

        if( ParmKind == MC_TYPE_PARAMETR_BEGDATE )
          if((CatCode == "Размещ. ОЭБ") or (CatCode == "Премия, ОЭБ") or (CatCode == "Дисконт, ОЭБ"))
             Parametr =  this.fininstr.rec.Issued;
          end;
        elif( ParmKind == MC_TYPE_PARAMETR_FINDATE)/* дата базовая*/
          if((CatCode == "Размещ. ОЭБ") or (CatCode == "Премия, ОЭБ") or (CatCode == "Дисконт, ОЭБ"))
             Parametr =  this.fininstr.rec.DrawingDate;
          end;
        end;

        return Parametr;

    END;

    /************************************************/
    /*внутренний конструктор класса                 */
    /************************************************/
    MACRO Construct( FIID, _KindPortf, _Owner, _Quoted, _isOverValue ) 

       this.fininstr = Trechandler( "fininstr.dbt" );
       this.avoiriss = Trechandler( "avoiriss.dbt" );
       this.fininstr.Clear();
       this.avoiriss.Clear();
     
       if( ПолучитьФинИн( FIID, this.fininstr, this.avoiriss ) != 0 ) 
          this.SetError( "Не найдена ценная бумага Id =" + string(FIID) );
       end;

       if( ID == null ) ID = -1;  end; /*Для актуализации бывает не нужен*/

       Quoted       = _Quoted;        /* котируемость ценной бумаги */
       isOverValue  = _isOverValue;   /* признак операции пеероценки */
       v_KindPortf  = _KindPortf;
       InitParmArray(); /*инициализация массива параметров*/
       InitFIRoleBArray();
    
       if( isOverValue == null )
          isOverValue = false; /*это не переоценка*/
       end; 

       if( Owner == null )
          Owner = -1; /*неизвестен*/
       end; 
    END;

    /*основной конструктор класса*/
    MACRO ConstructFI( DocKind, DocID, FIID, _KindPortf, _Owner, _Quoted, _isOverValue )
       /*инициализация объекта*/

       this.Kind  = DocKind; /* вид первичного документа, к которому привязка   */
       this.ID    = DocID;   /* ID первичного документа, к которому привязка    */
       this.Owner = _Owner;

       if( FIID == null  ) /*При вызове для показа счетов*/
          FIID = DocID; /*Для бумаги они совпадают*/
       end;
       this.Construct( FIID, _KindPortf, _Owner, _Quoted, _isOverValue );
    END;  
   
    initSPFirst();
    this.ConstructFI( DocKind, DocID, FIID, _KindPortf, _Owner, _Quoted, _isOverValue );
END;

/**************************************************************************/
/*  класс - первичные документы для работы с категориями учета            */ 
/*  на основе финансового инструмента для операции ПЕРЕМЕЩЕНИЕ            */
/**************************************************************************/
CLASS (SPFirstDocFI) SPFirstDocFI_Transfer( DocKind, DocID, _comm, _comm_trans)
    VAR comm = TBfile("dl_comm.dbt");  /* сервисная операция   */
    VAR comm_trans = TRecHandler("dlcomsum.str"); 

    MACRO InitFIRoleBArray

       if( (comm.rec.OperSubKind == DLCOMM_OPERSUBKIND_TO_CONTROL) OR (comm.rec.OperSubKind == DLCOMM_OPERSUBKIND_FROM_CONTROL) ) 
          FIRoleBArray[0] = FIROLE_BA_TP;
          FIRoleBArray[1] = FIROLE_BA_PPR;
          FIRoleBArray[2] = FIROLE_BAINCONTR;
          FIRoleBArray[3] = FIROLE_BA;
       elif( comm.rec.OperSubKind == DLCOMM_OPERSUBKIND_FROM_CONTROL ) 
          FIRoleBArray[0] = FIROLE_BA_TP;
          FIRoleBArray[1] = FIROLE_BA_PPR;
          FIRoleBArray[2] = FIROLE_BA_PUDP;
          FIRoleBArray[3] = FIROLE_PD_TP;
          FIRoleBArray[4] = FIROLE_PD_PPR;
          FIRoleBArray[5] = FIROLE_PD_PUDP;
          FIRoleBArray[3] = FIROLE_DD_TP;
          FIRoleBArray[4] = FIROLE_DD_PPR;
          FIRoleBArray[5] = FIROLE_DD_PUDP;
          FIRoleBArray[9] = FIROLE_BA;
       elif( (comm.rec.OperSubKind == DLCOMM_OPERSUBKIND_SALE_TO_RETIRE) OR (comm.rec.OperSubKind == DLCOMM_OPERSUBKIND_RETIRE_TO_SALE) )
          FIRoleBArray[0] = FIROLE_BA_PPR;
          FIRoleBArray[1] = FIROLE_BA_PUDP;
          FIRoleBArray[2] = FIROLE_PD_PPR;
          FIRoleBArray[3] = FIROLE_PD_PUDP;
          FIRoleBArray[4] = FIROLE_DD_PPR;
          FIRoleBArray[5] = FIROLE_DD_PUDP;
          FIRoleBArray[6] = FIROLE_BA;

       elif( (comm.rec.OperSubKind == DLCOMM_OPERSUBKIND_TRADE_TO_RETIRE_2129) OR 
             (comm.rec.OperSubKind == DLCOMM_OPERSUBKIND_TRADE_TO_SALE_2129  ) OR 
             (comm.rec.OperSubKind == DLCOMM_OPERSUBKIND_SALE_TO_RETIRE_2129 ) OR
             (comm.rec.OperSubKind == DLCOMM_OPERSUBKIND_TRADE_TO_RETIRE_2014) OR
             (comm.rec.OperSubKind == DLCOMM_OPERSUBKIND_TRADE_TO_SALE_2014  ) OR
             (comm.rec.OperSubKind == DLCOMM_OPERSUBKIND_SALE_TO_RETIRE_2014 )   
           )
          FIRoleBArray[0] = FIROLE_BA_TP;
          FIRoleBArray[1] = FIROLE_BA_PUDP;
          FIRoleBArray[2] = FIROLE_BA_PPR;
       elif( comm.rec.OperSubKind == 0 ) 
          FIRoleBArray[0] = FIROLE_BA_TP;
          FIRoleBArray[1] = FIROLE_BA_PPR;
          FIRoleBArray[2] = FIROLE_BA_PUDP;
          FIRoleBArray[3] = FIROLE_PD_TP;
          FIRoleBArray[4] = FIROLE_PD_PPR;
          FIRoleBArray[5] = FIROLE_PD_PUDP;
          FIRoleBArray[3] = FIROLE_DD_TP;
          FIRoleBArray[4] = FIROLE_DD_PPR;
          FIRoleBArray[5] = FIROLE_DD_PUDP;
          FIRoleBArray[9] = FIROLE_BA;
       end; 

    END;

    MACRO GetBasisFIRole(FIRole)

      if( (FIRole == null) OR (FIRole == FIROLE_UNDEF) )
         return FIROLE_BA;
      end;
      return FIRole;
    END;

    MACRO FaceFIID()
       if( FI_IsInvestmentShare(this.fininstr) )
          return NATCUR;/*у паев нет номинала*/
       else
          return this.fininstr.rec.FaceValueFI;
       end;
    END;

    MACRO ConstructTransfer( FIID, _KindPortf, _Owner, _Quoted, _isOverValue )
        ParmA[MC_TYPE_PARAMETR_CURRENCY]    = FaceFIID();
        ParmA[MC_TYPE_PARAMETR_PAYCURRENCY] = FaceFIID();
    END;

    copy( this.comm, _comm );
    copy( this.comm_trans, _comm_trans);
    initSPFirstDocFI( DocKind, DocID, comm.rec.FIID, null, comm.rec.ClientID );
    this.ConstructTransfer();
END;

CLASS (SPFirstDocFI) SPFirstDocFIEXP(DocID, FIID, BegDate, EndDate)
    macro ConstructEXP(_BegDate, _EndDate)
      ParmA[MC_TYPE_PARAMETR_BEGDATE] = _BegDate;
      ParmA[MC_TYPE_PARAMETR_FINDATE] = _EndDate;
    end;

    initSPFirstDocFI( DLDOC_ISSUE, DocID, FIID, KINDPORT_TRADE, {OurBank}, true, true, null );
    this.ConstructEXP(BegDate, EndDate)
END;

/**************************************************************************/
/*  класс - первичные документы для работы с категориями учета            */ 
/*  на основе финансового инструмента для операции переоценки             */
/**************************************************************************/
CLASS (SPFirstDocFI) SPFirstDocFIOvervalue( DocID, FIID )

    MACRO InitFIRoleBArray
       FIRoleBArray[0] = FIROLE_BA_RESALE;  /* базовый актив для перепродажи*/
    END;

    initSPFirstDocFI( DLDOC_ISSUE, DocID, FIID, KINDPORT_TRADE, {OurBank}, true, true, null );
END;

/**************************************************************************/
/* резерв по бумаге */
/**************************************************************************/
CLASS (SPFirstDocFI) SPFirstDocFIReserv( FIID, _KindReserv )

    MACRO InitFIRoleBArray
       FIRoleBArray[0] = FIROLE_BA_PPR;  
       FIRoleBArray[1] = FIROLE_BA_PUDP;  
       FIRoleBArray[2] = FIROLE_BAINPROMISSORY; 
       FIRoleBArray[3] = FIROLE_BAINCONTR;  
       FIRoleBArray[4] = FIROLE_BA_PPR_BPP;
       FIRoleBArray[5] = FIROLE_PD_PPR;  
       FIRoleBArray[6] = FIROLE_PD_PUDP;  
       FIRoleBArray[7] = FIROLE_PD_PDO; 
       FIRoleBArray[8] = FIROLE_PD_PKU;  
    END;

    initSPFirstDocFI( DLDOC_ISSUE, FIID, FIID, null, {OurBank} );
    this.KindReserv = _KindReserv;
END;

/**************************************************************************/
/*резерв по сделке*/
/**************************************************************************/
CLASS (SPFirstDoc) SPFirstDocDealReserv( Deal, _KindReserv, _IsBack )
    initSPFirstDoc( Deal, _IsBack );
    this.KindReserv = _KindReserv;
END;

/**************************************************************************/
/*резерв по сделке неттинга*/
/**************************************************************************/
CLASS (SPFirst) SPFirstDocNtg( DocKind, DocID, _KindReserv )
    VAR Deal, rq_money;

    MACRO InitParmArray 
        ParmA[MC_TYPE_PARAMETR_FIID]         = Deal.rec.BaseFIID;
        ParmA[MC_TYPE_PARAMETR_PAYCURRENCY]  = Deal.rec.PayFIID;
        ParmA[MC_TYPE_PARAMETR_DOCKIND]      = DL_NTGSEC;
        ParmA[MC_TYPE_PARAMETR_DOCID]        = Deal.rec.NettingID;
        ParmA[MC_TYPE_PARAMETR_CONTRACTOR]   = Deal.rec.Contractor;
        ParmA[MC_TYPE_PARAMETR_PARTY]        = Deal.rec.Contractor;
        ParmA[MC_TYPE_PARAMETR_OWNER]        = {OurBank};
        ParmA[MC_TYPE_PARAMETR_DEPARTMENT]   = Deal.rec.Department;
    END;

    MACRO GetParametr (ParmKind, OperDate, CatCode, FIRole)
        var Parametr = ParmA[ParmKind];
        if( Parametr == null ) 
            Parametr = -1;
        end;
        return Parametr;
    END;

    MACRO Construct( DocKind, DocID ) 

       this.Deal = TBfile( "dl_nett.dbt" );
       this.rq_money = Trechandler( "dlrq.dbt"   ); /* ТО по деньгами           */

       this.Kind  = DocKind; /* вид первичного документа, к которому привязка   */
       this.ID    = DocID;   /* ID первичного документа, к которому привязка    */
     
       ClearRecord (this.Deal);
       ClearRecord (rq_money);
       this.Deal.rec.NettingID = DocID; 
       if( this.Deal.GetEQ() == false ) 
          this.SetError( "Не найдена сделка NettingID = " + string(DocID) );
       end;

       if(ПолучитьТОпоДокументу(DL_NTGSEC, this.Deal.rec.NettingID, 1, DLRQ_TYPE_PAYMENT, 0, this.rq_money) == false)
         this.SetError("Не найдено результирующее ТО неттинга " );
       end;

       InitParmArray(); /*инициализация массива параметров*/
    END;

    initSPFirst();
    this.Construct( DocKind, DocID );
    this.KindReserv = _KindReserv;
END;

/**************************************************************************/
/* Резерв по объекту "Субъект экономики"                                  */
/**************************************************************************/
CLASS (SPFirst) SPFirstDocParty( DocKind, DocID, _KindReserv )
    VAR party;

    MACRO InitFIRoleBArray()
       FIRoleBArray[0] = FIROLE_CALC_ANOTHER_OPER;  /*Расчеты по прочим операциям*/
       FIRoleBArray[1] = FIROLE_CALC_AVOIRISS_OPER; /*Расчеты по операциям с ЦБ*/
    END;

    MACRO InitParmArray 
        ParmA[MC_TYPE_PARAMETR_CONTRACTOR] = this.party.rec.PartyID;
        ParmA[MC_TYPE_PARAMETR_DOCKIND]    = this.Kind;
        ParmA[MC_TYPE_PARAMETR_DOCID]      = this.ID;
    END;

    MACRO GetParametr (ParmKind, OperDate, CatCode, FIRole)
        var Parametr = ParmA[ParmKind];
        if( Parametr == null ) 
            Parametr = -1;
        end;
        return Parametr;
    END;

    MACRO Construct( DocKind, DocID ) 

       this.party = Trechandler( "party.dbt" );

       this.Kind  = DocKind; /* вид первичного документа, к которому привязка   */
       this.ID    = DocID;   /* ID первичного документа, к которому привязка    */
     
       ClearRecord (this.party);
       if( ПолучитьСубъекта( DocID, this.party ) != 0 ) 
          this.SetError( "Не найден субъект = " + string(DocID) );
       end;

       InitParmArray(); /*инициализация массива параметров*/
       InitFIRoleBArray();
    END;

    initSPFirst();
    this.Construct( DocKind, DocID );
    this.KindReserv = _KindReserv;
END;

CONST SP_ADDNUM_TO_FIROLE = 10000;

/**************************************************************************/
/*  класс - первичные документы для работы с категориями учета            */ 
/*  по договору обслуживания                                              */
/**************************************************************************/
CLASS (SPFirst) SPFirstDocContract( DocKind, Contract )
       
    VAR Contr     = TBfile( "sfcontr.dbt" );

    /* инициализация массива параметров              */
    PRIVATE MACRO InitParmArray
        ParmA[MC_TYPE_PARAMETR_FIID]        = 0;
        ParmA[MC_TYPE_PARAMETR_PAYCURRENCY] = 0;
        ParmA[MC_TYPE_PARAMETR_CURRENCY]    = 0;
        ParmA[MC_TYPE_PARAMETR_MARKET_PLACE]= 0;
        ParmA[MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE]= 0;

        ParmA[MC_TYPE_PARAMETR_CLIENT]       = Contr.rec.PartyID;
        ParmA[MC_TYPE_PARAMETR_OWNER]        = Contr.rec.PartyID;
        ParmA[MC_TYPE_PARAMETR_DOCKIND]      = this.Kind;
        ParmA[MC_TYPE_PARAMETR_DOCID]        = this.ID;
        ParmA[MC_TYPE_PARAMETR_CONTR_CLIENT] = this.ID;
        ParmA[MC_TYPE_PARAMETR_CONTR_BANK]   = this.ID;
        ParmA[MC_TYPE_PARAMETR_DEPARTMENT]   = {OperDprt};
    END;

    /*Получить параметры                            */
    MACRO GetParametr ( ParmKind, OperDate, CatCode, FIRole )
      VAR Parametr = ParmA[ParmKind];

      if( Parametr == null ) 

         if( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR )  /*контрактор из договора*/
            Parametr = Contr.rec.ContractorID;            
         elif( ParmKind == MC_TYPE_PARAMETR_PLACE )
           if( FIRole == FIROLE_BANKROLL_BANK )
               Parametr = this.РКЦБанкИзСПИ(ParmA[MC_TYPE_PARAMETR_FIID]);
            else
               Parametr = {OurBank};
            end;
         else
            Parametr = -1;  
         end; 
      end;
      return Parametr;
    END;

    /*конструктор класса                            */
    MACRO Construct( DocKind, _Contr ) 

       if( ValType( _Contr ) == V_INTEGER ) /*Id договора*/
          ClearRecord( Contr );
          Contr.rec.Id = _Contr;
          if( not GetEQ(Contr) )  
             this.SetError( "Не найден договор обслуживания при инициализации объекта" );        
          end;
       elif( (ValType(_Contr) == V_STRUC) OR (ValType(_Contr) ==V_FILE) OR (ValType(_Contr) == V_GENOBJ) )
          copy( Contr, _Contr );
       else
          this.SetError( "Неверные параметры инициализации объекта" );        
       end; 

       this.ID   = Contr.rec.Id;
       this.Kind = DocKind;

       InitParmArray(); /*инициализация массива параметров*/
       InitFIRoleBArray();
    END;

    MACRO InitFIRoleBArray
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_BA; /*базовый актив*/   
       FIRoleBArray[FIRoleBArray.Size] = FIROLE_COMISS_CLIENT;
    END;

    macro ВУ_ПолучитьРольФИ( ВидРО, DebCred )
      var FIRole = FIROLE_UNDEF;

       if( ВидРО == 8 )
          FIRole = FIROLE_COMISS_CLIENT;
       elif( (ВидРО == 9) or (ВидРО == 10) or (ВидРО == 12) )
          FIRole = FIROLE_COMISS_BROKER;
       end;

       return FIRole;
    end;

    macro ВУ_ПолучитьКатегориюДебет( ВидРО )
      var Категория = "";
       
       if( this.Contr.rec.ContractorID != {OurBank} )/*Договор банка*/         
          Категория = "ДС, Прч. счета банка, ВУ";
       else/*Договор клиента*/
          Категория = "ДС, Расч. с клиентом, ВУ";
       end;

       return Категория;
    end;

    macro ВУ_ПолучитьКатегориюКредит( ВидРО )
      var Категория = "";

       if( ВидРО == 8 )
          Категория = "ДС Клиента, ВУ";
       elif( ВидРО == 10 )
          Категория = "ДС у брокера, ВУ";
       else
          Категория = "ДС Банка, ВУ";
       end;

       return Категория;
    end;

    macro ВУ_ОснованиеПроводки( ВидРО )
      var Ground = "";

       if( ВидРО == 8 )
          Ground = "Оплата комиссии банку ";
       elif( (ВидРО == 9) or (ВидРО == 10) or (ВидРО == 12) )
          Ground = "Оплата комиссии посреднику ";
       end;

       if( Ground != "" )
          Ground = Ground + " по договору " + Contr.rec.Number;
       end;

       return Ground;
    end;

    initSPFirst();
    this.Construct( DocKind, Contract );
END;


/**************************************************************************/
/*  класс - первичные документы для работы с категориями учета            */ 
/*    для операции свертки                                                */
/**************************************************************************/
CLASS (SPFirst) SPFirstDocDLCOMM( parm1, parm2 )

    VAR comm  = TBfile("dl_comm.dbt");  /* сервисная операция */

    private var dlmarket_other = TRecHandler("dlmarket.dbt" ); /*схема расчета для другого клирингового счета*/
    private var v_ToClirFromCorrAcc = null;
    private var v_OperationKindName = null;
 
    /************************************************/
    /* инициализация массива параметров              */
    /************************************************/
    PRIVATE MACRO InitParmArray
       
        ParmA[MC_TYPE_PARAMETR_DOCKIND]      = this.Kind;
        ParmA[MC_TYPE_PARAMETR_DOCID]        = this.ID;
        //  ParmA[MC_TYPE_PARAMETR_FIID]         = 0;
        //ParmA[MC_TYPE_PARAMETR_PAYCURRENCY]  = 0;
        //ParmA[MC_TYPE_PARAMETR_CURRENCY]     = 0;
        ParmA[MC_TYPE_PARAMETR_DEPARTMENT]   = {OperDprt};
    END;

    MACRO InitFIRoleBArray
       FIRoleBArray[0] = FIROLE_BUYACTIVE;  /* Приобретаемый актив*/
       FIRoleBArray[1] = FIROLE_SALEACTIVE;  /*Реализуемый актив */

       if( comm.rec.OperSubKind == ALG_SP_MONEY_SOURCE_OWN )
          FIRoleBArray[2] = FIROLE_MONEYSOURCE_OWN;
       elif( comm.rec.OperSubKind == ALG_SP_MONEY_SOURCE_CLIENT )
          FIRoleBArray[2] = FIROLE_MONEYSOURCE_CLIENT;
       else
          FIRoleBArray[2] = FIROLE_MONEYSOURCE_TRUST;
       end;
    END;

    macro ПереводНаКлирНапрямуюСКоррСчета()
       record dlmarket(dlmarket); 
      
       if( v_ToClirFromCorrAcc == null  )

          if( this.Kind != DL_SETTLEMENT )
             v_ToClirFromCorrAcc = FALSE;
          else
             /*проверим, основная схема расчетов из операции Рынок_Т+ или нет. Если Рынок_Т+, то место хранения в счете банка не банк, а РКЦ*/
             if( (GetMarketSchemeID() > 0) and (FindDLMARKET( this.comm.rec.MarketSchemeID, dlmarket) != 0) )
                msgbox( "Не найдена схема расчетов с ID = "+string(this.comm.rec.MarketSchemeID) );
                v_ToClirFromCorrAcc = false;
             end;
             
             v_ToClirFromCorrAcc = ( dlmarket.Code == Рынок_Тплюс );
          end;

       end;

       return v_ToClirFromCorrAcc;
    end;

    MACRO GetOperationKindName()
      if((ValType(v_OperationKindName) != V_STRING) or (v_OperationKindName == ""))
        var cmd, DataSet;

        cmd = DL_RSDCommand("select t_Name from doprkoper_dbt where t_Kind_Operation = ? ");
        cmd.AddParam(comm.rec.OperationKind);
        DataSet = cmd.Execute();
        if(DataSet.moveNext())
          v_OperationKindName = DataSet.Name;
        end;
      end;

      return v_OperationKindName;
    END;

    macro ПолучитьКалендБиржа()
       return comm.rec.partyid;
    end;

    macro GetDealType()
      if (comm.rec.OperationKind > 0)
        return comm.rec.OperationKind;
      elif (comm.rec.OperSubKind > 0)
         return comm.rec.OperSubKind;
      end;
      return 0;
    end;

    /*Получить параметры шаблона                    */
    MACRO GetParametrTemplate
    ( 
        ObjectID,       /*   параметр - номер справочника                 */
        Classificator,  /*   классификатор - номер классификатора, если он задан */
        OperDate,       /*   дата, на которую надо вернуть характеристики*/
        FIRole
    )         
      var Parametr = -1;           /*по умолчанию*/
      var RunFromBO;
      if( (ObjectID == OBJTYPE_POOLKSU) AND (Classificator == LLCLASS_POOLKSU) )
        if (FIRole == FIROLE_KSU)
          var pool_code = this.GetPoolCode();
          
          if(pool_code == "10")
            Parametr = 10;
          elif(pool_code == "11")
            Parametr = 11;
          elif(pool_code == "12")
            Parametr = 12;
          elif(pool_code == "13")
            Parametr = 13;
          end;
        end;

        return Parametr;
      elif( (ObjectID == OBJTYPE_CUR_STOCK_MARKET_CALC) AND (Classificator == LLCLASS_CUR_STOCK_MARKET_CALC) ) /*Расчеты с валютными и фондовыми биржами*/
         return 0;
      end;

      return MC_GetParametrTemplate( this, ObjectID, Classificator, OperDate, FIRole );
    END;

    //Проверить, является ли код пула пулом
    MACRO PoolCodeIsPool(pool_code:string)
      if((pool_code == "10") or (pool_code == "11") or (pool_code == "12") or (pool_code == "13"))
        return true;
      end;

      return false;
    END;

    PRIVATE MACRO GetDepositID(MarketSchemeID:integer)
       var query, sql, DataSet;

       query =   " select DepSet.t_Depositary "        
               + "  from ddlDepSet_dbt DepSet, ddlmarket_dbt market "
               + " where market.t_id = ? "  
               + "   and DepSet.t_depsetid = market.t_depsetid";
       sql = DL_RSDCommand(query);
       sql.addParam(MarketSchemeID);

       DataSet = sql.execute();                                 

       if(DataSet.MoveNext())
          return DataSet.Depositary;               
       end;                                           

       return -1;
    end;

    /************************************************/
    /*Получить параметры                            */
    /************************************************/
    MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )
       VAR Parametr = ParmA[ParmKind];
       if (Parametr==null) 
         /*размещение*/  
         if( ParmKind == MC_TYPE_PARAMETR_PLACE ) // MC_TYPE_PARAMETR_FIID
            if( FIRole == FIROLE_BANKROLL_BANK )
               if( CatCode == "ДС Банка, ВУ" )
                  if( ПереводНаКлирНапрямуюСКоррСчета() )
                     Parametr = this.РКЦБанкИзСПИ(ParmA[MC_TYPE_PARAMETR_FIID]);
                  else
                     Parametr = {OurBank};
                  end;
               else
                  Parametr = this.РКЦБанкИзСПИ(ParmA[MC_TYPE_PARAMETR_FIID]);
               end;
            elif( FIRole == FIROLE_SETTL_AGENT )
               Parametr = comm.rec.TraderID;
            elif( (FIRole == FIROLE_ORCB_OTHCLIRACC) or (FIRole == FIROLE_ORCB_OTHCLIRACC_CLIENT) or (FIRole == FIROLE_ORCB_OTHCLIRACC_TRUST) )
               Parametr = dlmarket_other.rec.Centr;
            elif((this.Kind == DL_SCINOUTPOOL) and (CatCode == "ЦБ Клиента, ВУ"))
               if(FiRole == FIROLE_KSU)
                 if(this.PoolCodeIsPool(this.GetPoolCode(comm.rec.MarketSchemeID)) == true)
                   Parametr = GetDepositID(comm.rec.MarketSchemeID);
                 else
                   if(this.PoolCodeIsPool(this.GetPoolCode(comm.rec.MarketSchemeID_2)) == true)
                     Parametr = GetDepositID(comm.rec.MarketSchemeID_2);
                   end;
                 end;
               else
                 if(this.PoolCodeIsPool(this.GetPoolCode(comm.rec.MarketSchemeID)) == false)
                   Parametr = GetDepositID(comm.rec.MarketSchemeID);
                 else
                   if(this.PoolCodeIsPool(this.GetPoolCode(comm.rec.MarketSchemeID_2)) == false)
                     Parametr = GetDepositID(comm.rec.MarketSchemeID_2);
                   end;
                 end;
               end;
            else
               Parametr = comm.rec.PartyID; 
            end;
         /*организатор торговли*/  
         elif( ParmKind == MC_TYPE_PARAMETR_PARTY )
            Parametr = comm.rec.PartyID;                     
         /*торговая площадка*/
         elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE )     
            if( (FIRole == FIROLE_ORCB_OTHCLIRACC) or (FIRole == FIROLE_ORCB_OTHCLIRACC_CLIENT) or (FIRole == FIROLE_ORCB_OTHCLIRACC_TRUST) )
               Parametr = dlmarket_other.rec.Centr;
            else
               Parametr = comm.rec.TraderID;
            end;
         /*сектор организатора торговли*/
         elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE )
//            if( CatCode == "+Обеспечение" ) // Golovkin "Обеспечение" -> "+Обеспечение"
//               Parametr = DL_GetOfficeID_POOL();
//            else
               if( (FIRole == FIROLE_ORCB_OTHCLIRACC) or (FIRole == FIROLE_ORCB_OTHCLIRACC_CLIENT) or (FIRole == FIROLE_ORCB_OTHCLIRACC_TRUST) )
                  Parametr = dlmarket_other.rec.CentrOffice;
               else
                  Parametr = comm.rec.PartyOfficeID;            
               end;
//            end;
         elif( ParmKind == MC_TYPE_PARAMETR_OWNER )
            Parametr = comm.rec.ClientID;
         elif( ParmKind == MC_TYPE_PARAMETR_CLIENT )
            Parametr = comm.rec.ClientID;           
         elif( ParmKind == MC_TYPE_PARAMETR_CONTR_CLIENT )
            Parametr = comm.rec.ContractID;                     
         elif( ParmKind == MC_TYPE_PARAMETR_FIID )
            Parametr = comm.rec.FIID;
         elif(( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR) and (comm.rec.DocKind == SP_TRANSFERPA))
            Parametr = comm.rec.PartyID;
         elif(( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR) and (comm.rec.DocKind == SP_COMISSION_PA))
            Parametr = comm.rec.PartyID;
            //Parametr = {OurBank};
         elif((ParmKind == MC_TYPE_PARAMETR_CURRENCY) or (ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY))
            Parametr = NATCUR;
            if(CatCode == "ДС в КО")
              if(FIRole == FIROLE_ADDINCOME)
                Parametr = comm.rec.AddCur;
              else
                var fin = TRecHandler("fininstr.dbt");

                if( ПолучитьФинИн(comm.rec.FIID, fin) == 0 ) 
                  Parametr = fin.rec.FaceValueFI;
                end; 
              end;
            end;
         else
            Parametr = -1;
         end;
     end;
     return Parametr;

    END;

    MACRO SetDlMarketOther(parm1)
       dlmarket_other = TRecHandler("dlmarket.dbt" ); /*схема расчета для другого клирингового счета*/

       if ( ValType(parm1)==V_INTEGER )
           if( FindDLMARKET(parm1,dlmarket_other) != 0 )
              this.SetError( "Не найдена схема расчетов для определения другого клирингового счета." );
           end;
       else
          /*Конструктор из структур*/
          if( (ValType(parm1)==V_STRUC) OR (ValType(parm1)==V_FILE) OR (ValType(parm1) == V_GENOBJ) )
              copy ( dlmarket_other, parm1 );
          else
             this.SetError( "Неверные параметры определения другого клирингового счета." );        
          end; 
       end;
    END;

    MACRO GetMarketSchemeIDOther()
       return dlmarket_other.rec.ID;
    END;

    MACRO GetMarketSchemeCentrOther()
       return dlmarket_other.rec.Centr;
    END;

    MACRO GetMarketSchemeCentrOfficeOther()
       return dlmarket_other.rec.CentrOffice;
    END;

    /************************************************/
    /*конструктор класса                            */
    /************************************************/
    MACRO Construct( parm1, parm2 ) 

       ClearRecord (comm);

       /*Конструктор из DocKind, parm2*/
       if ( (ValType(parm1)==V_INTEGER) AND (ValType(parm2)==V_INTEGER) )
           comm.rec.DocumentID = parm2;
           if( comm.GetEQ == false ) 
              this.SetError( "Операция \""+DL_GetDocKindName(parm1)+"\" c ID = "+string(parm2)+" не найдена" );
           end;
       else
          /*Конструктор из структур*/
          if( (ValType(parm1)==V_STRUC) OR (ValType(parm1)==V_FILE) OR (ValType(parm1) == V_GENOBJ) )
              copy ( comm, parm1 );
          else
             this.SetError( "Неверные параметры инициализации объекта" );        
          end; 
       end;

       this.ID   = comm.rec.DocumentID;
       this.Kind = comm.rec.DocKind;

       InitParmArray(); /*инициализация массива параметров*/
       InitFIRoleBArray();
    END;

    MACRO GetMarketSchemeID():INTEGER
       return comm.rec.MarketSchemeID;
    END;
    /*FIROLE_BANKROLL_BANK"Средства в банке", FIROLE_SETTL_AGENT"Расчеты с посредником"*/

    macro ВУ_ПолучитьРольФИ( ВидРО, DebCred )
      var FIRole = FIROLE_UNDEF;

       if( ((ВидРО == 4) and (DebCred != false)) OR ((ВидРО == 2) and (DebCred != true)) )
          FIRole = FIROLE_BANKROLL_BANK;
       elif( ((ВидРО == 2) and (DebCred != false)) OR ((ВидРО == 4) and (DebCred != true)) )
          FIRole = FIROLE_SETTL_AGENT;
       elif( ((ВидРО == 55002) and (DebCred != true)) OR ((ВидРО == 55003) and (DebCred == true)) )
          FIRole = FIROLE_KSU;
       elif((ВидРО == 55001) or (ВидРО == 55002) or (ВидРО == 55003) or (ВидРО == 55004))
          FIRole = FIROLE_BA;
       end;

       return FIRole;
    end;

    macro GetOperSubKind()
       if (comm.rec.OperSubKind == 1)               
          return "конвертации";         
       elif (comm.rec.OperSubKind == 2)             
          return "дробления";           
       elif (comm.rec.OperSubKind == 3)             
          return "консолидации";        
       elif (comm.rec.OperSubKind == 4)             
          return "объединения";
       end;
       return "";                              
    end;

    macro ВУ_ОснованиеПроводки( ВидРО )
      var Ground = "";

       if( ВидРО == 2 )
          Ground = "Перевод средств на биржу";
       elif( ВидРО == 4 )
          Ground = "Вывод средств с биржи";
       elif( (ВидРО == 68) or (ВидРО == 78) )
          Ground = "Зачисление ц/б в операции "+GetOperSubKind()+" выпусков № " +comm.rec.CommCode ;
       elif( (ВидРО == 69) or (ВидРО == 79) )
          Ground = "Списание ц/б в операции "+GetOperSubKind()+" выпусков № " +comm.rec.CommCode;
       elif( (ВидРО == 47) )
          Ground = "Перевод между секторами на РЦ биржи в операции расчетов с биржей № " +comm.rec.CommCode;
       elif((ВидРО == 55001) or (ВидРО == 55002))
          Ground = "Исполнение обязательств/  Поставка ц/б по сделке продажи № " +comm.rec.CommCode;
       elif((ВидРО == 55003) or (ВидРО == 55004))
          Ground = "Исполнение требований/ Поставка ц/б по сделке покупки № " +comm.rec.CommCode;
       end;

       return Ground;
    end;
    
    MACRO ВУ_ПолучитьКатегориюДебет( ВидРО )
      var Категория = "";
       
       if( (ВидРО == 2) or (ВидРО == 47) )         
          Категория = "ДС в РЦ ОРЦБ, ВУ";
       elif( ВидРО == 4 )
          Категория = "ДС Банка, ВУ";
       elif((ВидРО == 55001) or (ВидРО == 55002))
          Категория = "ЦБ, Расч. с клиентом, ВУ";
       elif((ВидРО == 55003) or (ВидРО == 55004))
          Категория = "ЦБ Клиента, ВУ";
       end;

       return Категория;
    END;

    MACRO ВУ_ПолучитьКатегориюКредит( ВидРО )
      var Категория = "";
       
       if( (ВидРО == 4) or (ВидРО == 47) )         
          Категория = "ДС в РЦ ОРЦБ, ВУ";
       elif( ВидРО == 2 )
          Категория = "ДС Банка, ВУ";
       elif((ВидРО == 55001) or (ВидРО == 55002))
          Категория = "ЦБ Клиента, ВУ";
       elif((ВидРО == 55003) or (ВидРО == 55004))
          Категория = "ЦБ, Расч. с клиентом, ВУ";
       end;

       return Категория;
    END;

    macro ВУ_ПолучитьРО( ВидРО )
       if( (ВидРО == 55001) or (ВидРО == 55002) or (ВидРО == 55003) or (ВидРО == 55004) )
          return 55;
       end;

       return ВидРО;
    end;

    macro GetPoolCode(MarketSchemeID:integer):string
      var pool_code = "";
      
      if(ValType(MarketSchemeID) != V_UNDEF)
        var query =   " select o.t_officecode "
                  + "   from ddlmarket_dbt m, dptoffice_dbt o "  
                  + "  where m.t_id = ? "
                  + "    and m.t_centr = o.t_partyid " 
                  + "    and m.t_centroffice = o.t_officeid ";
        var cmd = DL_RSDCommand(query);

        cmd.AddParam(MarketSchemeID);
        var data_set = cmd.Execute();
        if (data_set.moveNext())
          pool_code = SQL_ConvTypeStr(data_set.t_officecode);
          if(strlen(pool_code) == 1)
            pool_code = "0" + pool_code;
          elif(strlen(pool_code) > 2)
            pool_code = substr(pool_code, strlen(pool_code) - 2);
          end;
        end;
      else
        var pc_1 = this.GetPoolCode(comm.rec.MarketSchemeID);
        var pc_2 = this.GetPoolCode(comm.rec.MarketSchemeID_2);
        
        if(this.PoolCodeIsPool(pc_1) == true)
          pool_code = pc_1; 
        elif(this.PoolCodeIsPool(pc_2) == true)
          pool_code = pc_2;
        end;

      end;

      if(pool_code == "")
        pool_code = "00";
      end;

      return pool_code;
    end;

    initSPFirst();
    this.Construct( parm1, parm2 );
END;

class (SPFirstDocDLCOMM) SPFirstDocDLCOMMitog( parm1, parm2, _fiid ) // Golovkin 25.08.2020
    var itogFiid = _fiid;
    
    MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )

       if(ParmKind == MC_TYPE_PARAMETR_FIID)
          return itogFiid;
       end;

       VAR Parametr = ParmA[ParmKind];

       if (Parametr==null) 
         /*размещение*/  
         if( ParmKind == MC_TYPE_PARAMETR_PLACE ) // MC_TYPE_PARAMETR_FIID
            if( FIRole == FIROLE_BANKROLL_BANK )
               if( CatCode == "ДС Банка, ВУ" )
                  if( ПереводНаКлирНапрямуюСКоррСчета() )
                     Parametr = this.РКЦБанкИзСПИ(ParmA[MC_TYPE_PARAMETR_FIID]);
                  else
                     Parametr = {OurBank};
                  end;
               else
                  Parametr = this.РКЦБанкИзСПИ(ParmA[MC_TYPE_PARAMETR_FIID]);
               end;
            elif( FIRole == FIROLE_SETTL_AGENT )
               Parametr = comm.rec.TraderID;
            elif( (FIRole == FIROLE_ORCB_OTHCLIRACC) or (FIRole == FIROLE_ORCB_OTHCLIRACC_CLIENT) or (FIRole == FIROLE_ORCB_OTHCLIRACC_TRUST) )
               Parametr = dlmarket_other.rec.Centr;
            elif((this.Kind == DL_SCINOUTPOOL) and (CatCode == "ЦБ Клиента, ВУ"))
               if(FiRole == FIROLE_KSU)
                 if(this.PoolCodeIsPool(this.GetPoolCode(comm.rec.MarketSchemeID)) == true)
                   Parametr = GetDepositID(comm.rec.MarketSchemeID);
                 else
                   if(this.PoolCodeIsPool(this.GetPoolCode(comm.rec.MarketSchemeID_2)) == true)
                     Parametr = GetDepositID(comm.rec.MarketSchemeID_2);
                   end;
                 end;
               else
                 if(this.PoolCodeIsPool(this.GetPoolCode(comm.rec.MarketSchemeID)) == false)
                   Parametr = GetDepositID(comm.rec.MarketSchemeID);
                 else
                   if(this.PoolCodeIsPool(this.GetPoolCode(comm.rec.MarketSchemeID_2)) == false)
                     Parametr = GetDepositID(comm.rec.MarketSchemeID_2);
                   end;
                 end;
               end;
            else
               Parametr = comm.rec.PartyID; 
            end;
         /*организатор торговли*/  
         elif( ParmKind == MC_TYPE_PARAMETR_PARTY )
            Parametr = comm.rec.PartyID;                     
         /*торговая площадка*/
         elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE )     
            if( (FIRole == FIROLE_ORCB_OTHCLIRACC) or (FIRole == FIROLE_ORCB_OTHCLIRACC_CLIENT) or (FIRole == FIROLE_ORCB_OTHCLIRACC_TRUST) )
               Parametr = dlmarket_other.rec.Centr;
            else
               Parametr = comm.rec.TraderID;
            end;
         /*сектор организатора торговли*/
         elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE )
 //           if( CatCode == "+Обеспечение" ) // Golovkin "Обеспечение" -> "+Обеспечение"
//               Parametr = DL_GetOfficeID_POOL();
 //           else
               if( (FIRole == FIROLE_ORCB_OTHCLIRACC) or (FIRole == FIROLE_ORCB_OTHCLIRACC_CLIENT) or (FIRole == FIROLE_ORCB_OTHCLIRACC_TRUST) )
                  Parametr = dlmarket_other.rec.CentrOffice;
               else
                  Parametr = comm.rec.PartyOfficeID;            
               end;
 //           end;
         elif( ParmKind == MC_TYPE_PARAMETR_OWNER )
            Parametr = comm.rec.ClientID;
         elif( ParmKind == MC_TYPE_PARAMETR_CLIENT )
            Parametr = comm.rec.ClientID;           
         elif( ParmKind == MC_TYPE_PARAMETR_CONTR_CLIENT )
            Parametr = comm.rec.ContractID;                     
         elif( ParmKind == MC_TYPE_PARAMETR_FIID )
            Parametr = comm.rec.FIID;
         elif(( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR) and (comm.rec.DocKind == SP_TRANSFERPA))
            Parametr = comm.rec.PartyID;
         elif(( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR) and (comm.rec.DocKind == SP_COMISSION_PA))
            Parametr = comm.rec.PartyID;
            //Parametr = {OurBank};
         elif((ParmKind == MC_TYPE_PARAMETR_CURRENCY) or (ParmKind == MC_TYPE_PARAMETR_PAYCURRENCY))
            Parametr = NATCUR;
            if(CatCode == "ДС в КО")
              if(FIRole == FIROLE_ADDINCOME)
                Parametr = comm.rec.AddCur;
              else
                var fin = TRecHandler("fininstr.dbt");

                if( ПолучитьФинИн(comm.rec.FIID, fin) == 0 ) 
                  Parametr = fin.rec.FaceValueFI;
                end; 
              end;
            end;
         else
            Parametr = -1;
         end;
     end;
     return Parametr;
    end;

    initSPFirstDocDLCOMM(parm1, parm2);
end;

/**************************************************************************/
/*  класс - первичные документы для работы с категориями учета            */ 
/*    для глобальной операции конвертации                                 */
/**************************************************************************/
CLASS (SPFirstDocDLCOMM) SPFirstDocDLCOMMGlobal( parm1, parm2 )

    PRIVATE VAR 
       v_fininstr:TRecHandler,
       v_avoiriss:TRecHandler;

    comm  = TBfile("dl_comm.dbt");  /* сервисная операция */

    /************************************************/
    /* инициализация массива параметров              */
    /************************************************/
    PRIVATE MACRO InitParmArray(comm)
        ParmA[MC_TYPE_PARAMETR_DOCKIND]      = this.Kind;
        ParmA[MC_TYPE_PARAMETR_DOCID]        = this.ID;
        ParmA[MC_TYPE_PARAMETR_FIID]         = this.fininstr.rec.FIID;
//        ParmA[MC_TYPE_PARAMETR_CURRENCY]     = this.fininstr.rec.FaceValueFI;
        ParmA[MC_TYPE_PARAMETR_PAYCURRENCY]  = this.fininstr.rec.FaceValueFI;
        ParmA[MC_TYPE_PARAMETR_DEPARTMENT]   = comm.rec.Division;
        ParmA[MC_TYPE_PARAMETR_OWNER]        = -1;
    END;

    MACRO InitFIRoleBArray
       FIRoleBArray[0] = FIROLE_BUYACTIVE;  /* Приобретаемый актив*/
       FIRoleBArray[1] = FIROLE_SALEACTIVE;  /*Реализуемый актив */
    END;

    /************************************************/
    /*Получить параметры                            */
    /************************************************/
    MACRO GetAccFIID(Categ:string)
       if (Categ == "Наш портфель ц/б")
          var cmd = DL_RSDCommand( "select RSB_PMWRTOFF.WRTDetermineAccFI(?) DetAccFI from dual" );
          cmd.AddParam(this.fininstr.rec.FIID);
          var DataSet = cmd.Execute();
          if( DataSet.moveNext() )
            var tmpInt:INTEGER;
            tmpInt = SQL_ConvTypeInteger(DataSet.DetAccFI);
            return tmpInt;
          else
            return NATCUR;
          end;
       else
         return this.fininstr.rec.FaceValueFI;
       end;

    END;
    MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )

     VAR Parametr = ParmA[ParmKind];
       
       if (Parametr==null) 
         
         /*размещение*/  
         if( ParmKind == MC_TYPE_PARAMETR_PLACE )
            if( FIRole == FIROLE_BANKROLL_BANK )
               Parametr = this.РКЦБанкИзСПИ(ParmA[MC_TYPE_PARAMETR_FIID]);
            elif( FIRole == FIROLE_SETTL_AGENT )
               Parametr = comm.rec.TraderID;
            else
               Parametr = comm.rec.PartyID; 
            end;
         /*организатор торговли*/  
         elif( ParmKind == MC_TYPE_PARAMETR_PARTY ) 
            Parametr = comm.rec.PartyID;                     
         
         /*торговая площадка*/
         elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE )     
            Parametr = comm.rec.TraderID;

         /*сектор организатора торговли*/
         elif( ParmKind == MC_TYPE_PARAMETR_MARKET_PLACE_OFFICE )
            Parametr = comm.rec.PartyOfficeID;            

         elif( ParmKind == MC_TYPE_PARAMETR_ISSUER )
            if( ((v_CatCode == "Наш портфель ц/б") OR (CatCode == "Наш портфель ц/б")) AND FI_IsDeposReceipt(this.fininstr.rec.FIID) )
               Parametr = this.ПолучитьЭмитентаСвязОбъектаДР(this.fininstr);
            else
               Parametr  = this.fininstr.rec.Issuer;
            end;
         elif( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR )/*для категории +\-Расчеты пока в качестве контрагента берем Наш Банк*/ 
            Parametr = {OurBank};                     
         elif( ParmKind == MC_TYPE_PARAMETR_CURRENCY )
            if((v_CatCode == "Наш портфель ц/б") OR (CatCode == "Наш портфель ц/б"))
               if(v_CatCode == "Наш портфель ц/б")
                 Parametr = GetAccFIID(v_CatCode);
               else
                 Parametr = GetAccFIID(CatCode);
               end;
            else
               Parametr = this.fininstr.rec.FaceValueFI;
            end;
         else
             Parametr = -1;
         end;
     end;
     return Parametr;

    END;

    /************************************************/
    /*конструктор класса                            */
    /************************************************/
    MACRO Construct( parm1, parm2 ) 

       ClearRecord (comm);

       /*Конструктор из DocKind, parm2*/
       if ( (ValType(parm1)==V_INTEGER) AND (ValType(parm2)==V_INTEGER) )
           comm.rec.DocumentID = parm2;
           if( comm.GetEQ == false ) 
              this.SetError( "Глобальная операция не найдена" );
           end;
       else
          /*Конструктор из структур*/
          if ((ValType(parm1)==V_STRUC) OR (ValType(parm1)==V_FILE))
              copy ( comm, parm1 );
          else
             this.SetError( "Неверные параметры инициализации объекта" );        
          end; 
       end;

       this.ID   = comm.rec.DocumentID;
       this.Kind = comm.rec.DocKind;

       InitParmArray(comm); /*инициализация массива параметров*/
       InitFIRoleBArray();
    END;

    macro GetOperSubKind()
       if (comm.rec.OperSubKind == 1)               
          return "конвертации";         
       elif (comm.rec.OperSubKind == 4)             
          return "объединения";
       end;
       return "";                              
    end;

    macro ВУ_ОснованиеПроводки( ВидРО )
      var Ground = "";

       if( ВидРО == 78 )
          Ground = "Зачисление ц/б в операции "+GetOperSubKind()+" выпусков № " +comm.rec.CommCode ;
       elif( ВидРО == 79 )
          Ground = "Списание ц/б в операции "+GetOperSubKind()+" выпусков № " +comm.rec.CommCode;
       end;

       return Ground;
    end;
    
    MACRO fininstr()  
       if( v_fininstr == NULL )
          v_fininstr = Trechandler( "fininstr.dbt" ); /* фин. инструмент           */
          v_avoiriss = Trechandler( "avoiriss.dbt" );

          if( ПолучитьФинИн( comm.rec.FIID, v_fininstr, v_avoiriss ) != 0 ) 
             this.SetError( "Не найдена ценная бумага " + string(comm.rec.FIID) );
          end;
       end;
       return v_fininstr;
    END;

    MACRO avoiriss()  
       if( v_avoiriss == NULL )
          this.fininstr();
       end;
       return v_avoiriss;
    END; 

    MACRO FaceFIID() 
       return this.fininstr.rec.FaceValueFI;
    END;    
    initSPFirst();
    this.Construct( parm1, parm2 );
END;

/**************************************************************************/
/*  класс - первичные документы для работы с категориями учета            */ 
/*    для записи реестра новых выпусков глобальной операции конвертации   */
/**************************************************************************/
CLASS (SPFirstDocDLCOMMGlobal) SPFirstDocScdlfiByGlobalConvert( parm1, parm2 )

    VAR scdlfi  = TBfile("scdlfi.dbt");  /* запись реестра новых выпусков */

    /************************************************/
    /* инициализация массива параметров              */
    /************************************************/
    PRIVATE MACRO InitParmArray(scdlfi)
        ParmA[MC_TYPE_PARAMETR_DOCKIND]      = this.Kind;
        ParmA[MC_TYPE_PARAMETR_DOCID]        = this.ID;
        ParmA[MC_TYPE_PARAMETR_FIID]         = this.fininstr.rec.FIID;
//        ParmA[MC_TYPE_PARAMETR_CURRENCY]     = this.fininstr.rec.FaceValueFI;
        ParmA[MC_TYPE_PARAMETR_PAYCURRENCY]  = this.fininstr.rec.FaceValueFI;
        ParmA[MC_TYPE_PARAMETR_OWNER]        = -1;
    END;

    MACRO InitFIRoleBArray
       FIRoleBArray[0] = FIROLE_BUYACTIVE;  /* Приобретаемый актив*/
       FIRoleBArray[1] = FIROLE_SALEACTIVE;  /*Реализуемый актив */
    END;

    MACRO GetAccFIID(Categ:string)
       if (Categ == "Наш портфель ц/б")
          var cmd = DL_RSDCommand( "select RSB_PMWRTOFF.WRTDetermineAccFI(?) DetAccFI from dual" );
          cmd.AddParam(this.fininstr.rec.FIID);
          var DataSet = cmd.Execute();
          if( DataSet.moveNext() )
            var tmpInt:INTEGER;
            tmpInt = SQL_ConvTypeInteger(DataSet.DetAccFI);
            return tmpInt;
          else
            return NATCUR;
          end;
       else
         return this.fininstr.rec.FaceValueFI;
       end;

    END;

    /************************************************/
    /*Получить параметры                            */
    /************************************************/
    MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )

     VAR Parametr = ParmA[ParmKind];
       
     if (Parametr==null) 
       if( ParmKind == MC_TYPE_PARAMETR_ISSUER )
          if( ((v_CatCode == "Наш портфель ц/б") OR (CatCode == "Наш портфель ц/б")) AND FI_IsDeposReceipt(this.fininstr.rec.FIID) )
             Parametr = this.ПолучитьЭмитентаСвязОбъектаДР(this.fininstr);
          else
             Parametr  = this.fininstr.rec.Issuer;
          end;
       elif( ParmKind == MC_TYPE_PARAMETR_CONTRACTOR )/*для категории +\-Расчеты пока в качестве контрагента берем Наш Банк*/
          Parametr = {OurBank};
       elif( ParmKind == MC_TYPE_PARAMETR_CURRENCY )
          if((v_CatCode == "Наш портфель ц/б") OR (CatCode == "Наш портфель ц/б"))
             if(v_CatCode == "Наш портфель ц/б")
               Parametr = GetAccFIID(v_CatCode);
             else
               Parametr = GetAccFIID(CatCode);
             end;
          else
             Parametr = this.fininstr.rec.FaceValueFI;
          end;
       else
         
          Parametr = -1;
       end;
     end;

     return Parametr;

    END;

    /************************************************/
    /*конструктор класса                            */
    /************************************************/
    MACRO Construct( parm1, parm2 ) 

       ClearRecord (scdlfi);

       /*Конструктор из DocKind, parm2*/
       if ( (ValType(parm1)==V_INTEGER) AND (ValType(parm2)==V_INTEGER) )
           scdlfi.rec.ID = parm2;
           if(  scdlfi.GetEQ == false ) 
              this.SetError( "Запись реестра новых выпусков не найдена" );
           end;
       else
          /*Конструктор из структур*/
          if ((ValType(parm1)==V_STRUC) OR (ValType(parm1)==V_FILE))
              copy ( scdlfi, parm1 );
          else
             this.SetError( "Неверные параметры инициализации объекта" );        
          end; 
       end;

       this.ID   = scdlfi.rec.ID;
       this.Kind = DL_SCDLFI;

       InitParmArray(scdlfi); /*инициализация массива параметров*/
       InitFIRoleBArray();
    END;

    MACRO fininstr()  
       if( v_fininstr == NULL )
          v_fininstr = Trechandler( "fininstr.dbt" ); /* фин. инструмент           */
          v_avoiriss = Trechandler( "avoiriss.dbt" );

          if( ПолучитьФинИн( scdlfi.rec.NewFIID, v_fininstr, v_avoiriss ) != 0 ) 
             this.SetError( "Не найдена ценная бумага " + string(scdlfi.rec.NewFIID) );
          end;
       end;
       return v_fininstr;
    END;

    MACRO FaceFIID() 
       return this.fininstr.rec.FaceValueFI;
    END;    

    initSPFirst();
    this.Construct( parm1, parm2 );
END;

/*получить необходимые первичные документы для перевода*/
MACRO ПолучитьПервичныйДокументДляПеревода( comm, dl_comm_trans, FD )

  FD = SPFirstDocFI_Transfer( comm.DocKind, comm.DocumentID, comm, dl_comm_trans ); 

  SetParm( 2, FD ); 
  return true;

END;

/**************************************************************************/
/*  класс - первичные документы для работы с категориями учета по платежу  */ 
/**************************************************************************/
CLASS (SPFirst) SPFirstDocPayment( parm1, parm2, _IsPaymetSale )

    VAR pm = TBfile("pmpaym.dbt"),  
        IsPaymetSale = _IsPaymetSale;

    PRIVATE MACRO InitParmArray
       ParmA[MC_TYPE_PARAMETR_DOCKIND]      = this.Kind;
       ParmA[MC_TYPE_PARAMETR_DOCID]        = this.ID;
       ParmA[MC_TYPE_PARAMETR_FIID]         = pm.rec.BaseFIID;
       ParmA[MC_TYPE_PARAMETR_PAYCURRENCY]  = pm.rec.PayFIID;
       ParmA[MC_TYPE_PARAMETR_CURRENCY]     = pm.rec.FIID;
       ParmA[MC_TYPE_PARAMETR_DEPARTMENT]   = pm.rec.Department;

       if( IsPaymetSale ) /*платеж списания*/
          ParmA[MC_TYPE_PARAMETR_OWNER]   = pm.rec.Receiver;
          ParmA[MC_TYPE_PARAMETR_PLACE]   = pm.rec.ReceiverBankID;
       else
          ParmA[MC_TYPE_PARAMETR_OWNER]   = pm.rec.Payer;
          ParmA[MC_TYPE_PARAMETR_PLACE]   = pm.rec.PayerBankID;
       end;
    END;

    /************************************************/
    /*Получить параметры                            */
    /************************************************/
    MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )
       VAR Parametr = ParmA[ParmKind];
         
       if( Parametr == null ) 
          Parametr = -1;
       end;

       return Parametr;
    END;

    MACRO Construct( parm1, parm2 ) 

       pm.Clear();

       /*Конструктор из DocKind, parm2*/
       if( (ValType(parm1)==V_INTEGER) AND (ValType(parm2)==V_INTEGER) )
           pm.rec.PaymentID = parm2;
           if( pm.GetEQ() == false ) 
              this.SetError( "Платеж не найден." );
           end;
       else
          /*Конструктор из структур*/
          if( (ValType(parm1) == V_STRUC) OR (ValType(parm1) == V_FILE) OR (ValType(parm1) == V_GENOBJ) ) 
             copy( pm, parm1 );
          else
             this.SetError( "Неверные параметры инициализации объекта" );        
          end; 
       end;

       this.ID   = pm.rec.PaymentID;
       this.Kind = pm.rec.DocKind;

       InitParmArray(); /*инициализация массива параметров*/
    END;

    initSPFirst();
    this.Construct( parm1, parm2 );
END;

/*для вывода по alta скроллинга счетов по конкретному обеспечению по сделке (с возможностью ввода счета именно по обеспечению, а не по сделке)*/
CLASS (SPFirstDoc) SPFirstDocTickEns( parm1, parm2 )

    PRIVATE VAR 
      v_tick_ens = TBfile("dl_tick_ens.dbt"); 

    MACRO Construct( parm1, parm2 ) 
       v_tick_ens.Clear();

       /*Конструктор из DocKind, parm2*/
       if( (ValType(parm1)==V_INTEGER) AND (ValType(parm2)==V_INTEGER) )
           v_tick_ens.rec.ID = parm2;
           if( v_tick_ens.GetEQ() == false ) 
              this.SetError( "Обеспечение по сделке не найдено." );
           end;
       else
          this.SetError( "Неверные параметры инициализации объекта." );        
       end;           

       initSPFirstDoc( DL_SECURITYDOC, v_tick_ens.rec.DealID );
       SetCurPFI(v_tick_ens.rec.FIID);

       CorrectBasket_ID   = this.ID;
       CorrectBasket_Kind = this.Kind; 

       this.ID   = v_tick_ens.rec.ID;   
       this.Kind = DL_TICK_ENS_DOC /*обеспечение по сделке*/; 

    END;

    this.Construct( parm1, parm2 );
END;

CLASS (SPFirst) SP_AvrHdRelationFD(HRID:integer)
  PRIVATE VAR m_dlhdgrelation = TBFile("dlhdgrelation.dbt", "R", 0);
  PRIVATE VAR m_fininstr   = TRecHandler("fininstr.dbt");
  PRIVATE VAR m_avoiriss   = TRecHandler("avoiriss.dbt");

  MACRO hdgrelation()
    return m_dlhdgrelation;
  END;

  MACRO fininstr()
    return m_fininstr;
  END;

  MACRO avoiriss()
    return m_avoiriss;
  END;

  MACRO GetBasisFIRole( FIRole:INTEGER, NoMsgErr:BOOL )
    var i = 0;
    
    if (FIRole == FIROLE_UNDEF)
      return FIROLE_BA;
    end;

    return FIRole;
  END;

  MACRO GetParametrTemplate( ObjectID:INTEGER, Classificator:INTEGER, OperDate:DATE, FIRole:INTEGER )
    var Parm = -1;
    if( (ObjectID == OBJTYPE_BA_DV) AND (Classificator == LLCLASS_KIND_BA) )
       if(m_dlhdgrelation.rec.InstrType == HEDG_AVRHDINSTRTYPE_CURSWAP)
         Parm = 1; //Валюта
       else
         Parm = 2; /*Процентная ставка*/
       end;
    elif( (ObjectID == OBJTYPE_KIND_OPER_DV) AND (Classificator == LLCLASS_KIND_DV) )
       Parm = 4;//Своп
    end;

    return Parm;
  END;

  MACRO GetParametr( ParmKind, OperDate, CatCode, FIRole )
    var Parametr = -1;
    if( ParmKind == MC_TYPE_PARAMETR_DEPARTMENT )
      Parametr = {OperDprt};
    elif( ParmKind == MC_TYPE_PARAMETR_FIID)
      Parametr = m_fininstr.rec.FIID;
    end;

    return Parametr;
  END;

  /*конструктор класса */
  MACRO Construct( HRID:integer )

    m_dlhdgrelation.Clear();
    m_dlhdgrelation.rec.ID = HRID;
    if(not m_dlhdgrelation.GetEQ())
      this.SetError( "Не найдена запись об отношениях хеджирования.|Ошибка при создании класса "+GenClassName(this) );
    else
      if(m_dlhdgrelation.rec.ObjDocKind != OBJTYPE_AVOIRISS)
        this.SetError("Неверный вид инструмента хеджирования.|Ошибка при создании класса "+GenClassName(this));
      end;
    end;

    if( ПолучитьФинИн(m_dlhdgrelation.rec.ObjID, m_fininstr, m_avoiriss ) != 0 ) 
       this.SetError( "Не найдена ценная бумага Id =" + string(m_dlhdgrelation.rec.ObjID) );
    end;

    Kind = SP_AVRHDGRELATION;
    ID   = HRID;

  END;

  initSPFirst();
  this.Construct(HRID);

END;


macro ПолучитьСчетаХеджированияБОЦБ_БООЭБ(DvNDealDocKind:integer, //Вид первичного документа сделки СВОП (ddvndeal_dbt.t_DocKind)
                                          DvNDealID:integer,      //Идентификатор сделки СВОП из (ddvndeal_dbt.t_ID)
                                          dat:date,               //Дата 
                                          PlusAcc:TRecHandler,    //Найденный счет "+" корректировок
                                          MinusAcc:TRecHandler    //Найденный счет "-" корректировок
                                         )
  var FD = NULL;
  var query, cmd, DataSet;
  var CatPlus = "", CatMinus = "";
  var FindAccount = "";
  var err = 0;

  PlusAcc.Clear();
  MinusAcc.Clear();

  query =   " select hdr.t_ID "
          + "   from ddlhdgrelation_dbt hdr "
          + "  where hdr.t_InstrDocKind = ? "
          + "    and hdr.t_InstrID = ? "
          + "    and hdr.t_ObjDocKind = " + OBJTYPE_AVOIRISS;

  cmd = DL_RSDCommand(query);         

  cmd.AddParam(DvNDealDocKind);
  cmd.AddParam(DvNDealID);

  DataSet = cmd.Execute();
  if(DataSet.moveNext())
    FD = SP_AvrHdRelationFD(DataSet.ID);

    if(FD.fininstr.rec.Issuer == {OurBank})
      CatPlus  = "+Корр, ОЭБ_Хедж";
      CatMinus = "-Корр, ОЭБ_Хедж";
    else
      CatPlus  = "+Корректировка, ц/б_Хедж";
      CatMinus = "-Корректировка, ц/б_Хедж";
    end;

    if( not FD.OpenAccount(CatPlus, FindAccount, false, null, PlusAcc, dat ) )
      err = 1;
      msgbox("Ошибка при поиске/открытии счета по категории " + CatPlus + ". " + GetErrMsg());
    end;

    if(not err)
      if( not FD.OpenAccount(CatMinus, FindAccount, false, null, MinusAcc, dat ) )
        err = 1;
        msgbox("Ошибка при поиске/открытии счета по категории " + CatMinus + ". " + GetErrMsg());
      end;
    end;
  else
    err = 1;
    msgbox("Не определен инструмент хеджирования");
  end;

  return err;
end;
