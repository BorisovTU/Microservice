/**
 @file TaxAccSecur_FIReportsReader.mac
 @brief АРНУ. Налоговый учет эмиссионных ценных бумаг. Парсинг отчетов для НУСвод.
 
 Файл содержит функционал загрузки данных файлов отчетов налогового учета
 для выпуска отчета.
 
  # tag
 - functional_block:Налоги_Отчеты
 - code_type:API
 - code_type:Report
 - АРНУ
 
  # changeLog
 |date       |author       |tasks                                                     |note                                                        
 |-----------|-------------|----------------------------------------------------------|-------------------------------------------------------------
 |13.03.2024 |Фаршатов Г.К.|CCBO-7612                                                 | BIQ-12838
 |22.09.2023 |Топорков Д.В.|CCBO-7595 CCBO-7601                                       | Насыщение кода комментариями
 |?          |?            |?                                                         | Создание                   
 */

import xls_parser, osfile, ws_validation, ws_usermenu, ws_txreportform;
import dl_vector,dlquery;

private const NAME_PROTOCOL_AUTO = "Протокол авто создания регистров";

private const TX_MODULE_1021 = 1021; ///> Регистры резервов (801-802)
private const TX_MODULE_1040 = 1040; ///> Проценты по РЕПО 2014 (507-508, 511-514)
private const TX_MODULE_1043 = 1043; ///> Регистр по начислению 2014 (411)
private const TX_MODULE_1046 = 1046; ///> Регистры реализации 2014 (211, 212, 215)
private const TX_MODULE_1051 = 1051; ///> Регистры коротких позиций 2014 (611-612, 615)
private const TX_MODULE_1052 = 1052; ///> Регистр по начислению расхода по коротким позициям 2014 (721)
private const TX_MODULE_1071 = 1071; ///> Регистры по ОИН (0292, 0491, 0692, 0791)
private const TX_MODULE_1076 = 1076; ///> Налоговый учет эмиссионных ценных бумаг

/// Для ассоциации имен файлов при загрузке этих файлов на сервер/авто. формирования для отчета СВОД
private const ARNU_TxRealizReg2014_ORCB      = 1;  ///> Регистры реализации 2014 (0211, 0212, 0215) обр.
private const ARNU_TxRealizReg2014_NotORCB   = 2;  ///> Регистры реализации 2014 (0211, 0212, 0215) необр.
private const ARNU_TXIncC                    = 3;  ///> Регистр по начислению 2014 (411)
private const ARNU_TxRegisterREPO_2014       = 4;  ///> Регистры РЕПО (0507, 0508, 0511, 0512, 0513, 0514)
private const ARNU_ShRepReg2014_ORCB         = 5;  ///> Регистры коротких позиций 2014 (0611, 0612, 0615) обр.
private const ARNU_ShRepReg2014_NotORCB      = 6;  ///> Регистры коротких позиций 2014 (0611, 0612, 0615) необр.
private const ARNU_txoutlay2014              = 7;  ///> Регистр по начислению расхода по коротким позициям 2014 (721)
private const ARNU_TxReserv                  = 8;  ///> Резервы (0801, 0802)
private const ARNU_IndNomBondReg_292_ORCB    = 9;  ///> Регистры ОИН (0292, 0491, 0692, 0791) // 292 обр.
private const ARNU_IndNomBondReg_292_NotORCB = 10; ///> Регистры ОИН (0292, 0491, 0692, 0791) // 292 необр.
private const ARNU_IndNomBondReg_491         = 11; ///> Регистры ОИН (0292, 0491, 0692, 0791) // 491
private const ARNU_IndNomBondReg_692_ORCB    = 12; ///> Регистры ОИН (0292, 0491, 0692, 0791) // 692 обр.
private const ARNU_IndNomBondReg_692_NotORCB = 13; ///> Регистры ОИН (0292, 0491, 0692, 0791) // 692 необр.
private const ARNU_IndNomBondReg_791         = 14; ///> Регистры ОИН (0292, 0491, 0692, 0791) // 791
private const ARNU_TaxAccSecur               = 15; ///> НУНП за предыдущий налоговый период

private const V_SPECVAL = 26;
private const CONST_ROWNUM = 29;
private const CONST_ORCB   = "_ORCB";
private const CONST_NotORCB = "_NotORCB";

/// Наименование загружаемых xls файлов
private const FILENAME_TxRealizReg2014 = "Report_TxRealizReg_2014";
private const FILENAME_TxRealizReg2014_ORCB = FILENAME_TxRealizReg2014 + CONST_ORCB;
private const FILENAME_TxRealizReg2014_NotORCB = FILENAME_TxRealizReg2014 + CONST_NotORCB;
private const FILENAME_ShRepReg2014 = "Report_ShRepReg2014";
private const FILENAME_ShRepReg2014_ORCB = FILENAME_ShRepReg2014 + CONST_ORCB;
private const FILENAME_ShRepReg2014_NotORCB = FILENAME_ShRepReg2014 + CONST_NotORCB;
private const FILENAME_IndNomBondReg = "Report_IndNomBondReg";
private const FILENAME_IndNomBondReg_292_ORCB = FILENAME_IndNomBondReg + "_292" + CONST_ORCB;
private const FILENAME_IndNomBondReg_292_NotORCB = FILENAME_IndNomBondReg + "_292" + CONST_NotORCB;
private const FILENAME_IndNomBondReg_491 = FILENAME_IndNomBondReg + "_491";
private const FILENAME_IndNomBondReg_692_ORCB = FILENAME_IndNomBondReg + "_692" + CONST_ORCB;
private const FILENAME_IndNomBondReg_692_NotORCB = FILENAME_IndNomBondReg + "_692" + CONST_NotORCB;
private const FILENAME_IndNomBondReg_791 = FILENAME_IndNomBondReg + "_791";
private const FILENAME_TXIncC = "Report_TXIncC";
private const FILENAME_txoutlay2014 = "report_txoutlay2014";
private const FILENAME_TxRegisterREPO_2014 = "Report_TxRegisterREPO_2014";
private const FILENAME_TxReserv = "Report_TxReserv";
private const FILENAME_TaxAccSecur = "Report_TaxAccSecur";

/// Листы в загружаемых данных
private const SHEET_215      = "0215";
private const SHEET_612      = "0612";
private const SHEET_615      = "0615";
private const SHEET_NUNP     = "Приложение 2 (НП)";
private const SHEET_212_SVOD = "0212_Свод";
private const SHEET_215_SVOD = "0215_Свод";
private const SHEET_292_SVOD = "0292_Свод";
private const SHEET_612_SVOD = "0612_Свод";
private const SHEET_615_SVOD = "0615_Свод";
private const SHEET_692_SVOD = "0692_Свод";
private const SHEET_411      = "0411";
private const SHEET_212      = "0212";
private const SHEET_292      = "0292";
private const SHEET_692      = "0692";
private const SHEET_491      = "0491";
private const SHEET_791      = "0791";
private const SHEET_721      = "0721";

private const AVOIRKIND_BOND = 17;

private var NALGROUP_NKD    = makeVector("1", "3", "5", "7", "11", "15", "17", "21", "31"); ///> NkdSRub, NkdBRub
private var NALGROUP_NKDRep = makeVector("1", "3", "5", "7", "11", "15", "21", "31"); ///> NkdRepRub

var RepBegDate, RepEndDate;

/**
@brief Функция выбора значения в зависимости от условия
@param[in] condition Условие для выбора (true/false !0/0)
@param[in] valueTrue Значение которое вернет функция при condition = true (тип Variant)
@param[in] valueFalse Значение которое вернет функция при condition = false (тип Variant)
@return valueTrue при condition=true, valueFalse при condition=false (тип Variant)
*/
private macro IIF(condition, valueTrue, valueFalse)
  var value;

  if( condition )
    value = valueTrue;
  else 
    value = valueFalse;
  end;

  return value;
end;

/// Замена стандартного MsgBox на время выполнения
VAR TX_ErrorMesage = TArray(1000,1000);

/**
@brief Процедура формирования сообщения и сохранения его в массиве TX_ErrorMesage
@param[in] Msg Данные для сохранения в массиве сообщений. Массив из одного или более параметра
*/
MACRO TX_MsgBoxFromReport( Msg:VARIANT )
   VAR i = 1, CurVal, FormMessage = string(Msg);

   while( GetParm( i, CurVal) )
      FormMessage = FormMessage + string(CurVal);
      i = i + 1;
   end;
   TX_ErrorMesage[TX_ErrorMesage.Size] = FormMessage;
END;

/**
@brief Класс суммируемых показателей из регистров налогового учета операций
*/
class TFIData
    var FI_CodeStr = ""; ///> Код финансового инструмента
    /**
    211_Свод <SumSrub>
    212_Свод, 215_Свод, 292_Свод SumAllRub
    213, 216 <SumSrub>
    611_Свод, 612_Свод, 615_Свод <SumSrub>
    */
    var SumSrub = 0.0;
    /**
    211_Свод, 212_Свод, 215_Свод, 611_Свод, 612_Свод, 615_Свод, 292_Свод, 692_Свод <RashAll> - (<ComS>+<ComB>) одна графа
    213, 216 <SumBrub> - (<ComS>+<ComB>).
    */
    var SumBrub = 0.0;
    /**
    211_Свод, 212_Свод, 215_Свод, 611_Свод, 612_Свод, 615_Свод, 292_Свод, 692_Свод (<ComS>+<ComB>)
    213, 216 <ComS> + <ComB> суммируем две графы
    */
    var Comiss = 0.0;
    /**
    Для  НГ = 9, 19, 29, 32, 33, 43, 17:
    292_Свод <NkdSvp>
    692_Свод (-)<NkdSvp>
    212 (<NkdSvp>+<CoupVN>)
    612_Свод (-)<NkdSvp>
    215 (<NkdSRub>+<CoupRub>)
    615 (-)<NkdSRub>
    411 <CoupRub>
    */
    var NkdSRub = 0.0;
    /**
    Для  НГ = 9, 19, 29, 32, 33, 43, 17:
    212_Свод, 292_Свод <NkdBvp>
    692_Свод (-)<NkdBvp>
    612_Свод (-)<NkdCloseVp>
    215 <NkdBRub>
    615 (-)<NkdCloseRub>
    411 <NkdBRub>
    */
    var NkdBRub = 0.0;
    /**
    Для НГ = 1, 3, 5, 7, 11, 15, 21, 31:
    212 (<NkdSvp> + <CoupVN> - <NkdBrepRub>)
    292 (<NkdSvp> + <CoupRub> - <NkdBrepRub>)
    692 (-)(<NkdSvp> + <CoupRub> - <NkdBvp>)
    612_Свод (-)(<NkdSvp> + <CoupVN> - <NkdCloseVp>)
    411 <CoupRub> - <NkdBRub>
    491 <CoupRub> - <NkdBvp>
    215 <NkdSrub> + <CoupRub> - <NkdBRub>
    615 (-)(<NkdSrub> + <CoupRub> - <NkdCloseRub>)
    791 (-)(<NkdSvp> + <CoupRub>)
    721 (-)(<NkdSrub> + <CoupRub>)
    */
    var NkdRepRub = 0.0;
    /**
    491 <NkdRepEndRub>
    411 <CoupRub> - <NkdBRub>
    721 (-)(<NkdEndRub>)
    */
    var NkdRub = 0.0;
    /**
    292_Свод, 491_Свод <NkdPrevRub_%>
    692_Свод, 791_Свод (-)<NkdPrevRub_%>
    212_Свод  <NkdPrevVN >
    612_Свод, 615_Свод (-)<ProcRashPrevRub>
    215_Свод <NkdPrevRub>
    Свод_0411 <NkdPrevRub>
    721_свод (-)<ProcPrevRub>
    */
    var NkdPrevRub = 0.0;
    /**
    511, 512 <DohRepVp>
    507, 508, 513, 514 <DohRepRub>
    */
    var DohRepRub = 0.0;
    /**
    511, 512 <RashRepVp>
    507, 508, 513, 514 <RashRepRub>
    */
    var RashRepRub = 0.0;
    var RashRez_Prev = 0.0; ///> 801_Свод, 802_Свод <RezRub31>
    var RashRez_DH = 0.0;   ///> 801_Свод, 802_Свод <RezRubS>

    var NkdRepRub1 = 0.0;   ///> 212_Свод
    var NkdRepRub2 = 0.0;   ///> Свод_0411
    var NkdAllRub1 = 0.0;   ///> 212_Свод
    var NkdAllRub2 = 0.0;   ///> Свод_0411

    var VR1 = 0.0;           ///> НУНП
    var VR2 = 0.0;           ///> НУНП
    var K6  = 0.0;           ///> НУНП
    var K8  = 0.0;           ///> НУНП
    var K10 = 0.0;           ///> НУНП
    var K12 = 0.0;           ///> НУНП
    var K14 = 0.0;           ///> НУНП
    var K16 = 0.0;           ///> НУНП

    var NkdRepChgNumRub = 0.0; // 491
    var NkdRepRub3      = 0.0; // 411 + 491
    var NkdRepVN        = 0.0; // 411 + 491

    /// технические поля
    var FIID = -1,
        AvrKindsRoot = -1;
end;

/// Массив значений TFIData для суммирования полей отчета с группировкой по ценностям 
private var FIDataArr = TArray();

/**
@brief Процедура cохранения сумм для одной ценности в таблицу
@param[in] obj Объект класса TFIData
*/
private macro SaveSumsInDB( obj )
    var cmd = RsdCommand("SELECT 1 FROM DSCTXTOTAL_DBT WHERE t_FI_Code = ?");
    cmd.NullConversion = true;
    cmd.addParam("", RSDBP_IN, obj.FI_CodeStr);
    cmd.execute();
    var rsd = TRsbDataSet(cmd);
    var cmdstr;
    if( rsd.MoveNext() )
        cmdstr = "UPDATE DSCTXTOTAL_DBT SET "
            " t_SumSrub = t_SumSrub + ?, "
            " t_SumBrub = t_SumBrub + ?, "
            " t_Comiss = t_Comiss + ?, "
            " t_NkdSRub = t_NkdSRub + ?, "
            " t_NkdBRub = t_NkdBRub + ?, "
            " t_NkdRepRub = t_NkdRepRub + ?, "
            " t_NkdRub = t_NkdRub + ?, "
            " t_NkdPrevRub = t_NkdPrevRub + ?, "
            " t_DohRepRub = t_DohRepRub + ?, "
            " t_RashRepRub = t_RashRepRub + ?, "
            " t_RashRez_Prev = t_RashRez_Prev + ?, "
            " t_RashRez_DH = t_RashRez_DH + ?, "
            " t_NkdRepRub1 = t_NkdRepRub1 + ?, "
            " t_NkdRepRub2 = t_NkdRepRub2 + ?, "
            " t_NkdAllRub1 = t_NkdAllRub1 + ?, "
            " t_NkdAllRub2 = t_NkdAllRub2 + ?, "
            " t_NkdRepChgNumRub = t_NkdRepChgNumRub + ?,"
            " t_NkdRepRub3 = t_NkdRepRub3 + ?,"
            " t_NkdRepVN = t_NkdRepVN + ?,"
            " t_VR1 = t_VR1 + ?, "
            " t_VR2 = t_VR2 + ?, "
            " t_K6 = t_K6 + ?, "
            " t_K8 = t_K8 + ?, "
            " t_K10 = t_K10 + ?, "
            " t_K12 = t_K12 + ?, "
            " t_K14 = t_K14 + ?, "
            " t_K16 = t_K16 + ? "

            " WHERE t_FI_Code = ?";
    else
        cmdstr = "INSERT INTO DSCTXTOTAL_DBT (" +
            " t_SumSrub, "
            " t_SumBrub, "
            " t_Comiss, "
            " t_NkdSRub, "
            " t_NkdBRub, "
            " t_NkdRepRub, "
            " t_NkdRub, "
            " t_NkdPrevRub, "
            " t_DohRepRub, "
            " t_RashRepRub, "
            " t_RashRez_Prev, "
            " t_RashRez_DH, "
            " t_NkdRepRub1, "
            " t_NkdRepRub2, "
            " t_NkdAllRub1, "
            " t_NkdAllRub2, "
            " t_NkdRepChgNumRub, "
            " t_NkdRepRub3, "
            " t_NkdRepVN, "
            " t_VR1, "
            " t_VR2, "
            " t_K6, "
            " t_K8, "
            " t_K10, "
            " t_K12, "
            " t_K14, "
            " t_K16, "

            " t_FI_CODE) "
            " VALUES (?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, "
            " ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?)";
    end;
    cmd =  RSDCommand(cmdstr);   
    cmd.NullConversion = true; 
    cmd.AddParam("", RSDBP_IN, obj.SumSrub);
    cmd.AddParam("", RSDBP_IN, obj.SumBrub);
    cmd.AddParam("", RSDBP_IN, obj.Comiss);    
    cmd.AddParam("", RSDBP_IN, obj.NkdSRub);  
    cmd.AddParam("", RSDBP_IN, obj.NkdBRub);  
    cmd.AddParam("", RSDBP_IN, obj.NkdRepRub);                                             
    cmd.AddParam("", RSDBP_IN, obj.NkdRub);
    cmd.AddParam("", RSDBP_IN, obj.NkdPrevRub);
    cmd.AddParam("", RSDBP_IN, obj.DohRepRub);
    cmd.AddParam("", RSDBP_IN, obj.RashRepRub);
    cmd.AddParam("", RSDBP_IN, obj.RashRez_Prev);
    cmd.AddParam("", RSDBP_IN, obj.RashRez_DH);
    cmd.AddParam("", RSDBP_IN, obj.NkdRepRub1);      
    cmd.AddParam("", RSDBP_IN, obj.NkdRepRub2);      
    cmd.AddParam("", RSDBP_IN, obj.NkdAllRub1);      
    cmd.AddParam("", RSDBP_IN, obj.NkdAllRub2);
    cmd.AddParam("", RSDBP_IN, obj.NkdRepChgNumRub);
    cmd.AddParam("", RSDBP_IN, obj.NkdRepRub3);
    cmd.AddParam("", RSDBP_IN, obj.NkdRepVN);
    cmd.AddParam("", RSDBP_IN, obj.VR1);
    cmd.AddParam("", RSDBP_IN, obj.VR2);
    cmd.AddParam("", RSDBP_IN, obj.K6);
    cmd.AddParam("", RSDBP_IN, obj.K8);
    cmd.AddParam("", RSDBP_IN, obj.K10);
    cmd.AddParam("", RSDBP_IN, obj.K12);
    cmd.AddParam("", RSDBP_IN, obj.K14);
    cmd.AddParam("", RSDBP_IN, obj.K16);

    cmd.AddParam("", RSDBP_IN, obj.FI_CodeStr);
    SQL_ExecuteTrn(cmd);
end; /*SaveSumsInDB*/

/**
@brief Процедура cохранения данных из массива в таблицу и очистка массива
*/
private macro SaveSumsArrayInDB
    if( FIDataArr.Size > 0 )
        var i = 0;
        while ( i < FIDataArr.Size )
            SaveSumsInDB( FIDataArr(i) );
            i = i + 1;
        end;
        FIDataArr = TArray();
    end;

onerror(x)
    msgbox( "Ошибка при сохранении данных в БД: " + x.Message );
end; /*SaveSumsArrayInDB*/

/**
@brief Функция преобразования строки в вещественное число
@param[in] strSum Строка содержащая запись вещественного числа
@return V_DOUBLE Преобразованное значение 
*/
private macro GetDoubleFromString(strSum: String)
    var res = 0.0;
    strSum = Trim(strSum);
    if( strSum != "" )
      var i = 1; 
      var resultStr = "";
      var str;
      var position = strBrk(strSum, ".");
      
      var sign = IIF(subStr(strSum, 1, 1)=="-", -1, 1);
      if( position < 1 ) ///> число без запятой (целое)
        position = strlen(strSum) + 1;
      end;
      while( i < position )
        str = subStr(strSum, i, 1);
        if( StrIsNumber(str) )
          resultStr = resultStr + str;
        end;
        i = i + 1;
      end;
      if( position > 0 ) ///> число c запятой
        resultStr = resultStr + ".";
        resultStr = resultStr + subStr(strSum, position + 1);
      end;
      res = Double(resultStr)*sign;
    end;      
    return res;
end; /* GetDoubleFromString */

/**
@brief Функция преобразования передаваемого параметра в вещественное число
@param[in] val2 Переменная которую необходимо преобразовать в V_DOUBLE
@return V_DOUBLE Преобразованное значение
@return 0.0 В случае если val2 невозможно преобразовать в V_DOUBLE
*/
private macro GetDoubleFromValue2(val2)
    var t = ValType( val2 );

    /* Передан SQL'ый NULL*/
    if ((t == V_SPECVAL) or (t == V_UNDEF))
      return 0.0;
    elif( ( t == V_DOUBLE ) or ( t == V_DOUBLEL ) )
        return val2;
    elif( ( t == V_MONEY ) or ( t == V_MONEYL ) or ( t == V_INTEGER ) )
        return Double( val2 );
    elif( t == V_STRING )
        return GetDoubleFromString( val2 );
    else
        return 0.0;
    end;
end; /* GetDoubleFromValue2 */

/**
@brief Процедура предварительного суммирования в массиве с разделением по ценностям (ФИ)
@param[in] obj Объект класса TFIData
*/
private macro AddFIValue( obj )
    var i = 0;
    var isFound = false;
    while ( i < FIDataArr.Size )
      if( FIDataArr(i).FI_CodeStr == obj.FI_CodeStr )
        isFound = true;
        FIDataArr(i).SumSrub = FIDataArr(i).SumSrub + obj.SumSrub;
        FIDataArr(i).SumBrub = FIDataArr(i).SumBrub + obj.SumBrub;
        FIDataArr(i).Comiss = FIDataArr(i).Comiss + obj.Comiss;
        FIDataArr(i).NkdSRub = FIDataArr(i).NkdSRub + obj.NkdSRub;
        FIDataArr(i).NkdBRub = FIDataArr(i).NkdBRub + obj.NkdBRub;
        FIDataArr(i).NkdRepRub = FIDataArr(i).NkdRepRub + obj.NkdRepRub;
        FIDataArr(i).NkdRub = FIDataArr(i).NkdRub + obj.NkdRub;
        FIDataArr(i).NkdPrevRub = FIDataArr(i).NkdPrevRub + obj.NkdPrevRub;
        FIDataArr(i).DohRepRub = FIDataArr(i).DohRepRub + obj.DohRepRub;
        FIDataArr(i).RashRepRub = FIDataArr(i).RashRepRub + obj.RashRepRub;
        FIDataArr(i).RashRez_Prev = FIDataArr(i).RashRez_Prev + obj.RashRez_Prev;
        FIDataArr(i).RashRez_DH = FIDataArr(i).RashRez_DH + obj.RashRez_DH;
        FIDataArr(i).NkdRepRub1       = FIDataArr(i).NkdRepRub1  + obj.NkdRepRub1;      
        FIDataArr(i).NkdRepRub2       = FIDataArr(i).NkdRepRub2  + obj.NkdRepRub2;      
        FIDataArr(i).NkdAllRub1       = FIDataArr(i).NkdAllRub1  + obj.NkdAllRub1;      
        FIDataArr(i).NkdAllRub2       = FIDataArr(i).NkdAllRub2  + obj.NkdAllRub2;      
        FIDataArr(i).NkdRepVN         = FIDataArr(i).NkdRepVN  + obj.NkdRepVN;      
        FIDataArr(i).NkdRepRub3       = FIDataArr(i).NkdRepRub3  + obj.NkdRepRub3;      
        FIDataArr(i).NkdRepChgNumRub  = FIDataArr(i).NkdRepChgNumRub  + obj.NkdRepChgNumRub;      
        FIDataArr(i).VR1           = FIDataArr(i).VR1 + obj.VR1;
        FIDataArr(i).VR2           = FIDataArr(i).VR2 + obj.VR2;
        FIDataArr(i).K6            = FIDataArr(i).K6 + obj.K6;
        FIDataArr(i).K8            = FIDataArr(i).K8 + obj.K8;
        FIDataArr(i).K10           = FIDataArr(i).K10 + obj.K10;
        FIDataArr(i).K12           = FIDataArr(i).K12 + obj.K12;
        FIDataArr(i).K14           = FIDataArr(i).K14 + obj.K14;
        FIDataArr(i).K16           = FIDataArr(i).K16 + obj.K16;

        break;
      end;
      i = i + 1;
    end;
    if( isFound == false )
      FIDataArr(FIDataArr.Size) = obj;
    end;
end; /* AddFIValue */

/**
@brief Функция поиска обращаемости ценной бумаги на дату
@param[in] FIID ID финансового инструмента
@param[in] EndDate Дата поиска
@return true Бумага обращаемая
@return false Бумага не обращаемая
*/
MACRO ОбращаемостьБумагиНаДату( FIID:string, EndDate:date ):bool
  var RetVal:bool = false;
  var Select = " SELECT Rsb_SCTX.IsSPGetRate(?, "+
               "                             -1, nvl(Rsb_Common.GetRegIntValue('SECUR\ВИД КУРСА СРЕДНЕВЗВЕШЕННАЯ ЦЕНА', 0),4), ?, ?-add_months(?,-3)) as RetVal " +
               "   FROM DUAL ";

  var Query = DL_RSDCommand(Select);
  Query.addParam(FIID);
  Query.addParam(EndDate);
  Query.addParam(EndDate);
  Query.addParam(EndDate);

  var DataSet = Query.execute();

  if( DataSet.MoveNext() )
     RetVal = int(DataSet.RetVal) == 0;
  end;

  return RetVal;
END;

/**
@brief Процедура загрузки ID и корневого подвида финансового инструмента в класс TFIData
@param[in] FIData Объект класса TFIData
@param[in] FI_Code Код финансового инструмента
*/
MACRO SetFIDataEx( FIData:TFIData, FI_Code:string )
  var Select = " SELECT FI.T_FIID, "+
               "        RSI_RSB_FIInstr.FI_AvrKindsGetRoot(2, FI.t_AvoirKind) AvrKindsRoot " +
               "   FROM DFININSTR_DBT FI " +
               "  WHERE FI.T_FI_CODE = ? ";

  var cmd = DL_RSDCommand(Select);
  cmd.addParam(FI_Code);
  var ds = cmd.execute();

  if( ds.MoveNext() )
    FIData.FIID = int(ds.FIID);
    FIData.AvrKindsRoot = int(ds.AvrKindsRoot);
  end;
END;

/**
@brief Процедура копирования ID и корневого подвида финансового инструмента из одного объекта TFIData в другой
@param[in] FIData Объект класса TFIData в который копируются данные
@param[in] prev_FIData Объект класса TFIData из которого копируются данные
*/
MACRO SetFIDataPrevTech( FIData:TFIData, prev_FIData:TFIData )
  FIData.FIID = prev_FIData.FIID;
  FIData.AvrKindsRoot = prev_FIData.AvrKindsRoot;
END;

/**
@brief Функция получения налоговой группы из прочитанного значения
@param[in] val Значение из листа Excel
@return Значение налоговой группы (V_STRING)
*/
private macro GetNGstr(val)
    var t = ValType( val );

    if( t == V_STRING )
       return Trim(val);
    elif( ( t == V_DOUBLE ) or ( t == V_MONEY ) )
       return string(int(val));
    else
       return "";
    end;
end;

/**
@brief Функция получения значения из ячейки листа Excel в зависимости от режима чтения (POI/Excel)
@param[in] objExcel Объект через который происходит чтение файла
@param[in] cellID ID ячейки листа Excel вида "A1", "D123" и т.п.
@param[in] IsPoi Флаг режима чтения через POI
@return Прочитанное значение из файла Excel
*/
private macro GetCellValue(objExcel: Object, cellID: String, IsPoi: Bool): String
  if (IsPoi)
    return objExcel.CellValue(cellID);
  end;
  return objExcel.Sheet.Range(cellID).Value2; ///> В поле text объекта "Ячейка" лежит отображение, оно может меняться (например от ширины колонки), поэтому правильнее будет использовать именно Value2
end;

private macro AddCellName(objExcel: Object, cellID: String, IsPoi: Bool)
  if (IsPoi)
    objExcel.AddCellName(cellID);
  end;
end;

private macro ClearTargetCell(objExcel: Object, IsPoi: Bool)
  if (IsPoi)
    objExcel.ClearTargetCell();
  end;
end;

/**
@brief Процедура парсинга листа в файле Excel
@param[in] objExcel Объект через который происходит чтение файла
@param[in] Sheet Имя листа Excel
@param[in] IsPoi Флаг режима чтения через POI
*/
private macro ParsingXlsSheet( objExcel:Object, Sheet:string , IsPoi:Bool)
    if( objExcel.SheetChange(Sheet) ) ///> Активируем нужную вкладку отчета
        /// Ищем нужные строки
        var sum, elem = TFIData, FI_CodeStr, NGstr,
            i = CONST_ROWNUM,
            IsItogStrNUNP = false,
            iLastRow, ///> Последняя непустая строка
            parsingFileName,
            prev_elem_tech = TFIData();
        if (IsPoi)
          iLastRow = objExcel.GetLastRowNum() + 1;
          parsingFileName = objExcel.GetWorkbookName();
        else
          iLastRow = objExcel.Sheet.Cells.SpecialCells(objExcel.xlLastCell).Row;
          parsingFileName = objExcel.Book.Name;
        end;
        if(Sheet==SHEET_NUNP)
          i = 8;
        end;
        if(Sheet==SHEET_491)
          i = 14;
        end;
        // Первый этап обработки. Сформировать список ячеек, данные которых нужно получить.
        var j = i;
        InitProgress(iLastRow - i, "... Обработка листа \"" + Sheet + "\" в файле " + parsingFileName, "Обработка листа \"" + Sheet + "\" в файле " + parsingFileName );

        ClearTargetCell(objExcel, IsPoi);
        AddCellName(objExcel, "A"+string(CONST_ROWNUM-2), IsPoi);

        if(strlen(string(GetCellValue(objExcel, "A"+string(CONST_ROWNUM-2), IsPoi))) > 0)
          ClearTargetCell(objExcel, IsPoi);
          while( i <= iLastRow )
            FI_CodeStr = "";

            if( (Sheet == "0801_Свод") OR (Sheet == "0802_Свод") )
                AddCellName(objExcel, "A"+string(i), IsPoi);
                AddCellName(objExcel, "B"+string(i), IsPoi);
            elif( Sheet == SHEET_411 )
                AddCellName(objExcel, "BI"+string(i), IsPoi);
                AddCellName(objExcel, "A"+string(i), IsPoi);
            elif( Sheet == SHEET_212 )
                AddCellName(objExcel, "CC"+string(i), IsPoi);
                AddCellName(objExcel, "A"+string(i), IsPoi);
            elif( Sheet == SHEET_215 )
                AddCellName(objExcel, "CL"+string(i), IsPoi);
                AddCellName(objExcel, "B"+string(i), IsPoi);
            elif( (Sheet == SHEET_292) or (Sheet == SHEET_692) )
                AddCellName(objExcel, "BU"+string(i), IsPoi);
                AddCellName(objExcel, "A"+string(i), IsPoi);
            elif( Sheet == SHEET_615 )
                AddCellName(objExcel, "BW"+string(i), IsPoi);
                AddCellName(objExcel, "A"+string(i), IsPoi);
            elif( Sheet == SHEET_612 )
                AddCellName(objExcel, "BN"+string(i), IsPoi);
                AddCellName(objExcel, "A"+string(i), IsPoi);
            elif( Sheet == SHEET_491 )
                AddCellName(objExcel, "AM"+string(i), IsPoi);
                AddCellName(objExcel, "B"+string(i), IsPoi);
            elif( Sheet == SHEET_791 )
                AddCellName(objExcel, "AO"+string(i), IsPoi);
                AddCellName(objExcel, "A"+string(i), IsPoi);
            elif( Sheet == SHEET_721 )
                AddCellName(objExcel, "AQ"+string(i), IsPoi);
                AddCellName(objExcel, "A"+string(i), IsPoi);
            elif( Index( StrUpr(Sheet), "СВОД" ) > 0 )
                AddCellName(objExcel, "B"+string(i), IsPoi);
                AddCellName(objExcel, "C"+string(i), IsPoi);
            elif( Sheet == SHEET_NUNP )
                AddCellName(objExcel, "A"+string(i), IsPoi);
                AddCellName(objExcel, "BF"+string(i), IsPoi);
            else
                AddCellName(objExcel, "A"+string(i), IsPoi);
                AddCellName(objExcel, "B"+string(i), IsPoi);
            end;
            if( (Sheet == "0211_Свод") OR (Sheet == SHEET_212_SVOD) OR (Sheet == SHEET_215_SVOD) OR (Sheet == SHEET_292_SVOD) OR (Sheet == "0611_Свод") OR (Sheet == SHEET_615_SVOD) )
                AddCellName(objExcel, "M"+string(i), IsPoi);               
                AddCellName(objExcel, "H"+string(i), IsPoi);                
                AddCellName(objExcel, "N"+string(i), IsPoi); 
                if( Sheet == SHEET_212_SVOD )
                    AddCellName(objExcel, "X"+string(i), IsPoi);
                    AddCellName(objExcel, "Q"+string(i), IsPoi);   
                    AddCellName(objExcel, "R"+string(i), IsPoi);    
                elif( Sheet == SHEET_215_SVOD )
                    AddCellName(objExcel, "W"+string(i), IsPoi);
                elif( Sheet == SHEET_292_SVOD )
                    AddCellName(objExcel, "U"+string(i), IsPoi);        
                    AddCellName(objExcel, "Q"+string(i), IsPoi);      
                end;
            elif( Sheet == SHEET_212 )
                AddCellName(objExcel, "I"+string(i), IsPoi);        
                AddCellName(objExcel, "BP"+string(i), IsPoi);
                AddCellName(objExcel, "BK"+string(i), IsPoi);       
            elif( Sheet == SHEET_292 )
                AddCellName(objExcel, "H"+string(i), IsPoi);     
                AddCellName(objExcel, "BN"+string(i), IsPoi);
                AddCellName(objExcel, "BI"+string(i), IsPoi);
                AddCellName(objExcel, "BI"+string(i), IsPoi);
            elif( Sheet == "0213" )
                AddCellName(objExcel, "L"+string(i), IsPoi);               
                AddCellName(objExcel, "U"+string(i), IsPoi);
                AddCellName(objExcel, "AG"+string(i), IsPoi);               
                AddCellName(objExcel, "AA"+string(i), IsPoi);
            elif( Sheet == "0216" )
                AddCellName(objExcel, "N"+string(i), IsPoi);       
                AddCellName(objExcel, "Y"+string(i), IsPoi);
                AddCellName(objExcel, "AM"+string(i), IsPoi);        
                AddCellName(objExcel, "AG"+string(i), IsPoi);
            elif( Sheet == SHEET_215 )
                AddCellName(objExcel, "M"+string(i), IsPoi);       
                AddCellName(objExcel, "BX"+string(i), IsPoi);         
                AddCellName(objExcel, "BQ"+string(i), IsPoi);          
                AddCellName(objExcel, "AB"+string(i), IsPoi);
            elif( Sheet == "Свод_0411" )
                AddCellName(objExcel, "X"+string(i), IsPoi);              
                AddCellName(objExcel, "I"+string(i), IsPoi);          
                AddCellName(objExcel, "J"+string(i), IsPoi);            
            elif( Sheet == SHEET_411 )
                AddCellName(objExcel, "AC"+string(i), IsPoi);
                AddCellName(objExcel, "AE"+string(i), IsPoi);            
                AddCellName(objExcel, "W"+string(i), IsPoi);
                AddCellName(objExcel, "L"+string(i), IsPoi);              
                AddCellName(objExcel, "M"+string(i), IsPoi);               
                AddCellName(objExcel, "O"+string(i), IsPoi);                
                AddCellName(objExcel, "AE"+string(i), IsPoi);           
                AddCellName(objExcel, "AD"+string(i), IsPoi);              
            elif( Sheet == "491_Свод" )
                AddCellName(objExcel, "U"+string(i), IsPoi);            
            elif( Sheet == SHEET_491 )  
                AddCellName(objExcel, "T"+string(i), IsPoi);          
                AddCellName(objExcel, "N"+string(i), IsPoi);
                AddCellName(objExcel, "U"+string(i), IsPoi);                 
                AddCellName(objExcel, "K"+string(i), IsPoi); 
            elif( Sheet == SHEET_612 )
                AddCellName(objExcel, "H"+string(i), IsPoi);               
                AddCellName(objExcel, "S"+string(i), IsPoi);                 
                AddCellName(objExcel, "T"+string(i), IsPoi);
                AddCellName(objExcel, "X"+string(i), IsPoi);
                AddCellName(objExcel, "Y"+string(i), IsPoi);       
                AddCellName(objExcel, "I"+string(i), IsPoi);       
                AddCellName(objExcel, "P"+string(i), IsPoi);
                AddCellName(objExcel, "AA"+string(i), IsPoi);                          
            elif( Sheet == SHEET_615 )
                AddCellName(objExcel, "M"+string(i), IsPoi);          
                AddCellName(objExcel, "W"+string(i), IsPoi);
                AddCellName(objExcel, "AL"+string(i), IsPoi);          
                AddCellName(objExcel, "AH"+string(i), IsPoi);          
            elif( Sheet == SHEET_692_SVOD )
                AddCellName(objExcel, "H"+string(i), IsPoi);                
                AddCellName(objExcel, "N"+string(i), IsPoi); 
                AddCellName(objExcel, "U"+string(i), IsPoi);           
                AddCellName(objExcel, "Q"+string(i), IsPoi);        
                AddCellName(objExcel, "R"+string(i), IsPoi);          
            elif( Sheet == SHEET_692 )
                AddCellName(objExcel, "H"+string(i), IsPoi);         
                AddCellName(objExcel, "AE"+string(i), IsPoi);
                AddCellName(objExcel, "O"+string(i), IsPoi);
            elif( Sheet == "0721_свод" )
                AddCellName(objExcel, "X"+string(i), IsPoi);           
            elif( Sheet == SHEET_721 )
                AddCellName(objExcel, "M"+string(i), IsPoi);          
                AddCellName(objExcel, "U"+string(i), IsPoi);
                AddCellName(objExcel, "Q"+string(i), IsPoi);               
            elif( Sheet == "0791_Свод" )
                AddCellName(objExcel, "U"+string(i), IsPoi);           
            elif( Sheet == SHEET_791 )
                AddCellName(objExcel, "H"+string(i), IsPoi);          
                AddCellName(objExcel, "V"+string(i), IsPoi);
            elif( (Sheet == "0801_Свод") OR (Sheet == "0802_Свод") )
                AddCellName(objExcel, "D"+string(i), IsPoi);         
                AddCellName(objExcel, "E"+string(i), IsPoi);          
            elif( (Sheet == "0507") OR (Sheet == "0508") )
                AddCellName(objExcel, "S"+string(i), IsPoi);           
                AddCellName(objExcel, "W"+string(i), IsPoi);         
            elif( (Sheet == "0511") OR (Sheet == "0512") )
                AddCellName(objExcel, "Y"+string(i), IsPoi);          
                AddCellName(objExcel, "AA"+string(i), IsPoi);           
            elif( (Sheet == "0513") OR (Sheet == "0514") )
                AddCellName(objExcel, "AB"+string(i), IsPoi);          
                AddCellName(objExcel, "AF"+string(i), IsPoi);          
            elif( Sheet == SHEET_NUNP )
                AddCellName(objExcel, "AZ"+string(i), IsPoi);
                AddCellName(objExcel, "BA"+string(i), IsPoi);
                AddCellName(objExcel, "AM"+string(i), IsPoi);
                AddCellName(objExcel, "AN"+string(i), IsPoi);
                AddCellName(objExcel, "AO"+string(i), IsPoi);
                AddCellName(objExcel, "AP"+string(i), IsPoi);
                AddCellName(objExcel, "AQ"+string(i), IsPoi);
                AddCellName(objExcel, "AR"+string(i), IsPoi);
                AddCellName(objExcel, "AS"+string(i), IsPoi);
                AddCellName(objExcel, "AT"+string(i), IsPoi);
                AddCellName(objExcel, "AU"+string(i), IsPoi);
                AddCellName(objExcel, "AV"+string(i), IsPoi);
                AddCellName(objExcel, "AW"+string(i), IsPoi);
                AddCellName(objExcel, "AX"+string(i), IsPoi);
                AddCellName(objExcel, "AL"+string(i), IsPoi);
                AddCellName(objExcel, "AK"+string(i), IsPoi);
                AddCellName(objExcel, "BB"+string(i), IsPoi);
            end;
            UseProgress(i);
            i = i + 1;
          end;
        end;
        RemProgress();

        // Второй этап. Данные ячеек прочитаны, теперь их нужно получить и вставить.
        i = j;
        InitProgress(iLastRow - i, "... Обработка листа \"" + Sheet + "\" в файле " + parsingFileName, "Обработка листа \"" + Sheet + "\" в файле " + parsingFileName );
        while( i <= iLastRow )
            FI_CodeStr = "";

            if( (Sheet == "0801_Свод") OR (Sheet == "0802_Свод") )
                NGstr = GetNGstr(GetCellValue(objExcel, "A"+string(i), IsPoi));
                FI_CodeStr = Trim(GetCellValue(objExcel, "B"+string(i), IsPoi));
            elif( Sheet == SHEET_411 )
                NGstr = GetNGstr(GetCellValue(objExcel, "BI"+string(i), IsPoi));
                FI_CodeStr = Trim(GetCellValue(objExcel, "A"+string(i), IsPoi));
            elif( Sheet == SHEET_212 )
                NGstr = GetNGstr(GetCellValue(objExcel, "CC"+string(i), IsPoi));
                FI_CodeStr = Trim(GetCellValue(objExcel, "A"+string(i), IsPoi));
            elif( Sheet == SHEET_215 )
                NGstr = GetNGstr(GetCellValue(objExcel, "CL"+string(i), IsPoi));
                FI_CodeStr = Trim(GetCellValue(objExcel, "B"+string(i), IsPoi));
            elif( (Sheet == SHEET_292) or (Sheet == SHEET_692) )
                NGstr = GetNGstr(GetCellValue(objExcel, "BU"+string(i), IsPoi));
                FI_CodeStr = Trim(GetCellValue(objExcel, "A"+string(i), IsPoi));
            elif( Sheet == SHEET_615 )
                NGstr = GetNGstr(GetCellValue(objExcel, "BW"+string(i), IsPoi));
                FI_CodeStr = Trim(GetCellValue(objExcel, "A"+string(i), IsPoi));
            elif( Sheet == SHEET_612 )
                NGstr = GetNGstr(GetCellValue(objExcel, "BN"+string(i), IsPoi));
                   FI_CodeStr = Trim(GetCellValue(objExcel, "A"+string(i), IsPoi));
            elif( Sheet == SHEET_491 )
                NGstr = GetNGstr(GetCellValue(objExcel, "AM"+string(i), IsPoi));
                FI_CodeStr = Trim(GetCellValue(objExcel, "B"+string(i), IsPoi));
            elif( Sheet == SHEET_791 )
                NGstr = GetNGstr(GetCellValue(objExcel, "AO"+string(i), IsPoi));
                FI_CodeStr = Trim(GetCellValue(objExcel, "A"+string(i), IsPoi));
            elif( Sheet == SHEET_721 )
                NGstr = GetNGstr(GetCellValue(objExcel, "AQ"+string(i), IsPoi));
                FI_CodeStr = Trim(GetCellValue(objExcel, "A"+string(i), IsPoi));
            elif( (Sheet == "0507") OR (Sheet == "0508") OR (Sheet == "0511") OR (Sheet == "0512") OR (Sheet == "0513") OR (Sheet == "0514") )
                NGstr = "";
                FI_CodeStr = Trim(GetCellValue(objExcel, "B"+string(i), IsPoi));
            elif( Index( StrUpr(Sheet), "СВОД" ) > 0 )
                NGstr = GetNGstr(GetCellValue(objExcel, "B"+string(i), IsPoi));
                FI_CodeStr = Trim(GetCellValue(objExcel, "C"+string(i), IsPoi));
            elif( Sheet == SHEET_NUNP )
                if( Index(GetCellValue(objExcel, "A"+string(i), IsPoi), "Итого по выпуску") > 0 )
                  IsItogStrNUNP = true;
                else
                  IsItogStrNUNP = false;
                end;
                NGstr = "";
                FI_CodeStr = Trim(GetCellValue(objExcel, "BF"+string(i), IsPoi));
            else
                NGstr = "";
                FI_CodeStr = Trim(GetCellValue(objExcel, "A"+string(i), IsPoi));
            end;
            if( ((strlen(FI_CodeStr) == 0) and (Sheet!=SHEET_NUNP)) or
                ((strlen(FI_CodeStr) == 0) and (strlen(elem.FI_CodeStr) == 0) and (Sheet==SHEET_NUNP))
              ) ///> Таблица закончилась
                break;
            end;

            elem = TFIData();
            elem.FI_CodeStr = FI_CodeStr;

            if( (Sheet == SHEET_212_SVOD) or (Sheet == SHEET_215_SVOD) or (Sheet == SHEET_292_SVOD) or
                (Sheet == SHEET_612)      or (Sheet == SHEET_615_SVOD) or (Sheet == SHEET_692_SVOD)
              )
              SetFIDataEx(elem, FI_CodeStr);
            elif( (Sheet == SHEET_411) or (Sheet == SHEET_212) or (Sheet == SHEET_215) or (Sheet == SHEET_292) or (Sheet == SHEET_692) or
                  (Sheet == SHEET_491) or (Sheet == SHEET_615) or (Sheet == SHEET_791) or (Sheet == SHEET_721)
              )
              if( elem.FI_CodeStr == prev_elem_tech.FI_CodeStr )
                SetFIDataPrevTech(elem, prev_elem_tech);
              else
                SetFIDataEx(elem, FI_CodeStr);
                prev_elem_tech = elem;
              end;
            end;

            if( (Sheet==SHEET_NUNP) and ((strlen(FI_CodeStr) == 0) or (not IsItogStrNUNP))
              )
               UseProgress(i);
               i = i + 1;
               continue;
            end;

            if( (Sheet == "0211_Свод") OR (Sheet == SHEET_212_SVOD) OR (Sheet == SHEET_215_SVOD) OR (Sheet == SHEET_292_SVOD) OR
                   (Sheet == "0611_Свод") OR (Sheet == SHEET_615_SVOD) )
                  elem.SumSrub = GetDoubleFromValue2(GetCellValue(objExcel, "M"+string(i), IsPoi));               ///> <SumSrub> или <SumAllRub>
                  elem.Comiss = GetDoubleFromValue2(GetCellValue(objExcel, "H"+string(i), IsPoi));                ///> <ComS>+<ComB>
                  elem.SumBrub = GetDoubleFromValue2(GetCellValue(objExcel, "N"+string(i), IsPoi)) - elem.Comiss; ///> <RashAll> - (<ComS>+<ComB>)

                if( Sheet == SHEET_212_SVOD )
                    elem.NkdPrevRub = GetDoubleFromValue2(GetCellValue(objExcel, "X"+string(i), IsPoi));       ///> <NkdPrevVN>
                    elem.NkdRepRub1 = GetDoubleFromValue2(GetCellValue(objExcel, "Q"+string(i), IsPoi));       ///> <NkdRepRub1>
                    elem.NkdRepRub2 = GetDoubleFromValue2(GetCellValue(objExcel, "R"+string(i), IsPoi));       ///> <NkdRepRub2>

                elif( Sheet == SHEET_215_SVOD )
                /* */   elem.NkdPrevRub = GetDoubleFromValue2(GetCellValue(objExcel, "W"+string(i), IsPoi));    ///> <NkdPrevRub> /*PDV 510249 *//*GAA:517028*/

                elif( Sheet == SHEET_292_SVOD )
                    elem.NkdPrevRub = GetDoubleFromValue2(GetCellValue(objExcel, "U"+string(i), IsPoi));        ///> <NkdPrevRub_%>
                    if( (elem.AvrKindsRoot == AVOIRKIND_BOND) and ((NALGROUP_NKD.contains( NGstr ) == false) or ((NGstr == "17") and (not ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate)))) )
                        elem.NkdSRub = GetDoubleFromValue2(GetCellValue(objExcel, "Q"+string(i), IsPoi));       ///> <NkdSvp>
                    end;
                end;

            elif( Sheet == SHEET_212 )
                if( elem.AvrKindsRoot == AVOIRKIND_BOND )
                  if( (NALGROUP_NKD.contains( NGstr ) == false) or ((NGstr == "17") and (not ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate))) )
                      elem.NkdSRub = GetDoubleFromValue2(GetCellValue(objExcel, "I"+string(i), IsPoi))        ///> <NkdSvp> + <CoupVN>
                                   + GetDoubleFromValue2(GetCellValue(objExcel, "BP"+string(i), IsPoi));
                      elem.NkdBRub = GetDoubleFromValue2(GetCellValue(objExcel, "BK"+string(i), IsPoi));      ///> <NkdBrepRub>
                  end;
                  if( (NALGROUP_NKDRep.contains( NGstr ) == true) or ((NGstr == "17") and ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate)) )
                      elem.NkdRepRub = GetDoubleFromValue2(GetCellValue(objExcel, "I"+string(i), IsPoi))       ///> <NkdSvp> + <CoupVN> - <NkdBrepRub>
                                     + GetDoubleFromValue2(GetCellValue(objExcel, "BP"+string(i), IsPoi))
                                     - GetDoubleFromValue2(GetCellValue(objExcel, "BK"+string(i), IsPoi));
                  end;
                end;

            elif( Sheet == SHEET_292 )
                if( (elem.AvrKindsRoot == AVOIRKIND_BOND) and ((NALGROUP_NKDRep.contains( NGstr ) == true) or ((NGstr == "17") and ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate))) )
                    elem.NkdRepRub = GetDoubleFromValue2(GetCellValue(objExcel, "H"+string(i), IsPoi))     ///> (<NkdSvp> + <CoupRub> - <NkdBrepRub>)
                                   + GetDoubleFromValue2(GetCellValue(objExcel, "BN"+string(i), IsPoi))
                                   - GetDoubleFromValue2(GetCellValue(objExcel, "BI"+string(i), IsPoi));
                end;
                if( (elem.AvrKindsRoot == AVOIRKIND_BOND) and ((NALGROUP_NKD.contains( NGstr ) == false) or ((NGstr == "17") and (not ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate)))) )
                    elem.NkdBRub = GetDoubleFromValue2(GetCellValue(objExcel, "BI"+string(i), IsPoi));       ///> <NkdBrepRub>
                end;

            elif( Sheet == "0213" )
                elem.SumSrub = GetDoubleFromValue2(GetCellValue(objExcel, "L"+string(i), IsPoi));               ///> <SumSrub>
                elem.Comiss = GetDoubleFromValue2(GetCellValue(objExcel, "U"+string(i), IsPoi))
                            + GetDoubleFromValue2(GetCellValue(objExcel, "AG"+string(i), IsPoi));               ///> <ComS>+<ComB>
                elem.SumBrub = GetDoubleFromValue2(GetCellValue(objExcel, "AA"+string(i), IsPoi)) - elem.Comiss;///> <SumBrub> - (<ComS>+<ComB>)

            elif( Sheet == "0216" )
                elem.SumSrub = GetDoubleFromValue2(GetCellValue(objExcel, "N"+string(i), IsPoi));               ///> <SumSrub>
                elem.Comiss = GetDoubleFromValue2(GetCellValue(objExcel, "Y"+string(i), IsPoi))
                            + GetDoubleFromValue2(GetCellValue(objExcel, "AM"+string(i), IsPoi));               ///> <ComS>+<ComB>
                elem.SumBrub = GetDoubleFromValue2(GetCellValue(objExcel, "AG"+string(i), IsPoi)) - elem.Comiss;///> <SumBrub> - (<ComS>+<ComB>)

            elif( Sheet == SHEET_215 )
                if( elem.AvrKindsRoot == AVOIRKIND_BOND )
                  if( (NALGROUP_NKD.contains( NGstr ) == false) or ((NGstr == "17") and (not ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate))) )
                 /**/     elem.NkdSRub = GetDoubleFromValue2(GetCellValue(objExcel, "M"+string(i), IsPoi))       ///> <NkdSRub> + <CoupRub>
                                   + GetDoubleFromValue2(GetCellValue(objExcel, "BX"+string(i), IsPoi));         /*PDV 510249 *//*GAA: 517028*/
                      elem.NkdBRub = GetDoubleFromValue2(GetCellValue(objExcel, "BQ"+string(i), IsPoi));         ///> <NkdBrepRub>  
                  end;
                  if( (NALGROUP_NKDRep.contains( NGstr ) == true) or ((NGstr == "17") and ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate)) )
                      elem.NkdRepRub = GetDoubleFromValue2(GetCellValue(objExcel, "M"+string(i), IsPoi))        ///> (<NkdSrub> + <CoupRub> - <NkdBRub>)
                                     + GetDoubleFromValue2(GetCellValue(objExcel, "BX"+string(i), IsPoi))
                                     - GetDoubleFromValue2(GetCellValue(objExcel, "AB"+string(i), IsPoi));
                  end;
                end;

            elif( Sheet == "Свод_0411" )
                elem.NkdPrevRub = GetDoubleFromValue2(GetCellValue(objExcel, "X"+string(i), IsPoi));            ///> <NkdPrevRub>    
                elem.NkdAllRub1 = GetDoubleFromValue2(GetCellValue(objExcel, "I"+string(i), IsPoi));            ///> <NkdAllRub1>
                elem.NkdAllRub2 = GetDoubleFromValue2(GetCellValue(objExcel, "J"+string(i), IsPoi));            ///> <NkdAllRub2>

            elif( Sheet == SHEET_411 )
                if( elem.AvrKindsRoot == AVOIRKIND_BOND )
                  if( (NALGROUP_NKD.contains( NGstr ) == false) or ((NGstr == "17") and (not ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate))) )
                      elem.NkdSRub = GetDoubleFromValue2(GetCellValue(objExcel, "AC"+string(i), IsPoi));        ///> <CoupRub>
                      if( elem.NkdSRub != 0 )
                         elem.NkdBRub = GetDoubleFromValue2(GetCellValue(objExcel, "W"+string(i), IsPoi));      ///> <NkdBrepRub>
                      end;
                  end;
                  if( (NALGROUP_NKDRep.contains( NGstr ) == true) or ((NGstr == "17") and ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate)) )
                      if (GetDoubleFromValue2(GetCellValue(objExcel, "AC"+string(i), IsPoi)) != 0)
                         elem.NkdRepRub = GetDoubleFromValue2(GetCellValue(objExcel, "AC"+string(i), IsPoi))    ///> <CoupRub> - <NkdBrepRub>
                                        - GetDoubleFromValue2(GetCellValue(objExcel, "W"+string(i), IsPoi));
                      end;
                   end;
                end;
                if (GetDoubleFromValue2(GetCellValue(objExcel, "AC"+string(i), IsPoi)) != 0)
                   elem.NkdRub = GetDoubleFromValue2(GetCellValue(objExcel, "AE"+string(i), IsPoi));            ///> <NkdRepRub>
                else
                   elem.NkdRub = GetDoubleFromValue2(GetCellValue(objExcel, "AE"+string(i), IsPoi))
                               - GetDoubleFromValue2(GetCellValue(objExcel, "W"+string(i), IsPoi));             ///> <NkdRepRub> - <NkdBrepRub>
                end;
                /*GAA:513419, запрос - 285756, ждем реализацию*/
                elem.SumSrub = GetDoubleFromValue2(GetCellValue(objExcel, "L"+string(i), IsPoi));               ///> <SumSrub>
                elem.SumBrub = GetDoubleFromValue2(GetCellValue(objExcel, "M"+string(i), IsPoi));               ///> <SumBrub>
                elem.Comiss = GetDoubleFromValue2(GetCellValue(objExcel, "O"+string(i), IsPoi));                ///> <Comiss>
                elem.NkdRepRub3 = GetDoubleFromValue2(GetCellValue(objExcel, "AE"+string(i), IsPoi));           ///> <NkdRepRub>
                elem.NkdRepVN = GetDoubleFromValue2(GetCellValue(objExcel, "AD"+string(i), IsPoi));             ///> <NkdRepVN> 

                /*GAA*/

            elif( Sheet == "491_Свод" )
                elem.NkdPrevRub = GetDoubleFromValue2(GetCellValue(objExcel, "U"+string(i), IsPoi));            ///> <NkdPrevRub_%>

            elif( Sheet == SHEET_491 )
                if( (elem.AvrKindsRoot == AVOIRKIND_BOND) and ((NALGROUP_NKDRep.contains( NGstr ) == true) or ((NGstr == "17") and ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate))) )
                    elem.NkdRepRub = GetDoubleFromValue2(GetCellValue(objExcel, "T"+string(i), IsPoi))          ///> <CoupRub> - <NkdBvp>
                                   + GetDoubleFromValue2(GetCellValue(objExcel, "O"+string(i), IsPoi));
                end;
                elem.NkdRub = GetDoubleFromValue2(GetCellValue(objExcel, "U"+string(i), IsPoi));                  ///> <NkdRepEndRub>
                elem.NkdRepChgNumRub = GetDoubleFromValue2(GetCellValue(objExcel, "K"+string(i), IsPoi)) 
                                     + GetDoubleFromValue2(GetCellValue(objExcel, "T"+string(i), IsPoi));         ///> <NkdRepRub_IN> + <NkdPrevRub_IN>

               elif( Sheet == SHEET_612 )
                   elem.SumSrub = GetDoubleFromValue2(GetCellValue(objExcel, "H"+string(i), IsPoi));               ///> <SumSrub> или <SumAllRub>
                   elem.Comiss = GetDoubleFromValue2(GetCellValue(objExcel, "S"+string(i), IsPoi))                 ///> <ComS>+<ComB>
                               + GetDoubleFromValue2(GetCellValue(objExcel, "T"+string(i), IsPoi));
                   elem.SumBrub = GetDoubleFromValue2(GetCellValue(objExcel, "X"+string(i), IsPoi)) - elem.Comiss; ///> <RashAll> - (<ComS>+<ComB>)

                   elem.NkdPrevRub = -GetDoubleFromValue2(GetCellValue(objExcel, "Y"+string(i), IsPoi));       ///> (-)<NkdPrevVN>
                   if( elem.AvrKindsRoot == AVOIRKIND_BOND )
                     if( (NALGROUP_NKD.contains( NGstr ) == false) or ((NGstr == "17") and (not ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate))) )
                       elem.NkdSRub = GetDoubleFromValue2(GetCellValue(objExcel, "I"+string(i), IsPoi));       ///> <NkdSvp>
                       elem.NkdBRub = GetDoubleFromValue2(GetCellValue(objExcel, "P"+string(i), IsPoi))
                                    + GetDoubleFromValue2(GetCellValue(objExcel, "AA"+string(i), IsPoi));      ///> <NkdCloseVp> + <CoupRub>
                     end;
                     if( (NALGROUP_NKDRep.contains( NGstr ) == true) or ((NGstr == "17") and ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate)) )
                         elem.NkdRepRub = GetDoubleFromValue2(GetCellValue(objExcel, "I"+string(i), IsPoi))    ///> <NkdSvp> - <NkdCloseVp>
                                        - GetDoubleFromValue2(GetCellValue(objExcel, "AA"+string(i), IsPoi))
                                        - GetDoubleFromValue2(GetCellValue(objExcel, "P"+string(i), IsPoi));
                     end;
                   end;

            elif( Sheet == SHEET_615 )
                if( elem.AvrKindsRoot == AVOIRKIND_BOND )
                  if( (NALGROUP_NKD.contains( NGstr ) == false) or ((NGstr == "17") and (not ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate))) )
                      elem.NkdSRub = GetDoubleFromValue2(GetCellValue(objExcel, "M"+string(i), IsPoi));          ///> <NkdSRub>
                      elem.NkdBRub = GetDoubleFromValue2(GetCellValue(objExcel, "W"+string(i), IsPoi)) +
                                     GetDoubleFromValue2(GetCellValue(objExcel, "AL"+string(i), IsPoi));          ///> <NkdCloseRub> + <CoupVN>
                  end;
                  if( (NALGROUP_NKDRep.contains( NGstr ) == true) or ((NGstr == "17") and ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate)) )
                    /*  elem.NkdRepRub = -(GetDoubleFromValue2(GetCellValue(objExcel, "M"+string(i), IsPoi))         ///> (-)(<NkdSrub> + <CoupRub> - <NkdCloseRub>)
                                     + GetDoubleFromValue2(GetCellValue(objExcel, "AL"+string(i), IsPoi))
                                     - GetDoubleFromValue2(GetCellValue(objExcel, "W"+string(i), IsPoi)));   */         /*PDV*/
              elem.NkdBRub = GetDoubleFromValue2(GetCellValue(objExcel, "W"+string(i), IsPoi));                           ///> (-)<NkdCloseRub>
                          elem.NkdPrevRub = -GetDoubleFromValue2(GetCellValue(objExcel, "AH"+string(i), IsPoi));          ///> (-)<NkdPrevRub>    /*PDV 510249 */
                  end;
                end;
              
            elif( Sheet == SHEET_692_SVOD )
                elem.Comiss = GetDoubleFromValue2(GetCellValue(objExcel, "H"+string(i), IsPoi));                ///> <ComS>+<ComB>
                elem.SumBrub = GetDoubleFromValue2(GetCellValue(objExcel, "N"+string(i), IsPoi)) - elem.Comiss; ///> <RashAll> - (<ComS>+<ComB>)
                elem.NkdPrevRub = -GetDoubleFromValue2(GetCellValue(objExcel, "U"+string(i), IsPoi));           ///> (-)<NkdPrevRub_%>
                if( (elem.AvrKindsRoot == AVOIRKIND_BOND) and ((NALGROUP_NKD.contains( NGstr ) == false) or ((NGstr == "17") and (not ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate)))) )
                    elem.NkdSRub = -GetDoubleFromValue2(GetCellValue(objExcel, "Q"+string(i), IsPoi));          ///> (-)<NkdSvp>
                    elem.NkdBRub = -GetDoubleFromValue2(GetCellValue(objExcel, "R"+string(i), IsPoi));          ///> (-)<NkdBvp>
                end;

            elif( Sheet == SHEET_692 )
                if( (elem.AvrKindsRoot == AVOIRKIND_BOND) and ((NALGROUP_NKDRep.contains( NGstr ) == true) or ((NGstr == "17") and ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate))) )
                    elem.NkdRepRub = -(GetDoubleFromValue2(GetCellValue(objExcel, "H"+string(i), IsPoi))         ///> (-)(<NkdSvp> + <CoupRub> - <NkdBvp>)
                                   + GetDoubleFromValue2(GetCellValue(objExcel, "AE"+string(i), IsPoi))
                                   - GetDoubleFromValue2(GetCellValue(objExcel, "O"+string(i), IsPoi)));
                end;

            elif( Sheet == "0721_свод" )
                elem.NkdPrevRub = -GetDoubleFromValue2(GetCellValue(objExcel, "X"+string(i), IsPoi));            ///> (-)<ProcPrevRub>

            elif( Sheet == SHEET_721 )
                if( (elem.AvrKindsRoot == AVOIRKIND_BOND) and ((NALGROUP_NKDRep.contains( NGstr ) == true) or ((NGstr == "17") and ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate))) )
                    elem.NkdRepRub = -(GetDoubleFromValue2(GetCellValue(objExcel, "M"+string(i), IsPoi))          ///> (-)(<NkdSrub> + <CoupRub>)
                                   + GetDoubleFromValue2(GetCellValue(objExcel, "U"+string(i), IsPoi)));
                end;
                elem.NkdRub = GetDoubleFromValue2(GetCellValue(objExcel, "Q"+string(i), IsPoi));                  ///> <NkdEndRub>

            elif( Sheet == "0791_Свод" )
                elem.NkdPrevRub = -GetDoubleFromValue2(GetCellValue(objExcel, "U"+string(i), IsPoi));           ///> (-)<NkdPrevRub_%>

            elif( Sheet == SHEET_791 )
                if( (elem.AvrKindsRoot == AVOIRKIND_BOND) and ((NALGROUP_NKDRep.contains( NGstr ) == true) or ((NGstr == "17") and ОбращаемостьБумагиНаДату(elem.FIID, RepEndDate))) )
                    elem.NkdRepRub = -(GetDoubleFromValue2(GetCellValue(objExcel, "H"+string(i), IsPoi))          ///> (-)(<NkdSvp> + <CoupRub>)
                                   + GetDoubleFromValue2(GetCellValue(objExcel, "V"+string(i), IsPoi)));
                end;

            elif( (Sheet == "0801_Свод") OR (Sheet == "0802_Свод") )
                elem.RashRez_Prev = -GetDoubleFromValue2(GetCellValue(objExcel, "D"+string(i), IsPoi));         ///> (-)<RezRub31>
                elem.RashRez_DH = -GetDoubleFromValue2(GetCellValue(objExcel, "E"+string(i), IsPoi));           ///> (-)<RezRubS>

            elif( (Sheet == "0507") OR (Sheet == "0508") )
                elem.DohRepRub = GetDoubleFromValue2(GetCellValue(objExcel, "S"+string(i), IsPoi));             ///> <DohRepRub>
                elem.RashRepRub = GetDoubleFromValue2(GetCellValue(objExcel, "W"+string(i), IsPoi));            ///> <RashRepRub>

            elif( (Sheet == "0511") OR (Sheet == "0512") )
                elem.DohRepRub = GetDoubleFromValue2(GetCellValue(objExcel, "Y"+string(i), IsPoi));             ///> <DohRepVp>
                elem.RashRepRub = GetDoubleFromValue2(GetCellValue(objExcel, "AA"+string(i), IsPoi));            ///> <RashRepVp>

            elif( (Sheet == "0513") OR (Sheet == "0514") )
                elem.DohRepRub = GetDoubleFromValue2(GetCellValue(objExcel, "AB"+string(i), IsPoi));            ///> <DohRepRub>
                elem.RashRepRub = GetDoubleFromValue2(GetCellValue(objExcel, "AF"+string(i), IsPoi));           ///> <RashRepRub>
            elif( Sheet == SHEET_NUNP )
                elem.VR1 = GetDoubleFromValue2(GetCellValue(objExcel, "AZ"+string(i), IsPoi));
                elem.VR2 = GetDoubleFromValue2(GetCellValue(objExcel, "BA"+string(i), IsPoi));
                elem.K6 = GetDoubleFromValue2(GetCellValue(objExcel, "AM"+string(i), IsPoi))
                        - GetDoubleFromValue2(GetCellValue(objExcel, "AN"+string(i), IsPoi))
                        + GetDoubleFromValue2(GetCellValue(objExcel, "AO"+string(i), IsPoi))
                        - GetDoubleFromValue2(GetCellValue(objExcel, "AP"+string(i), IsPoi));
                elem.K8 = GetDoubleFromValue2(GetCellValue(objExcel, "AQ"+string(i), IsPoi))
                        - GetDoubleFromValue2(GetCellValue(objExcel, "AR"+string(i), IsPoi));
                elem.K10 = - GetDoubleFromValue2(GetCellValue(objExcel, "AS"+string(i), IsPoi))
                           - GetDoubleFromValue2(GetCellValue(objExcel, "AT"+string(i), IsPoi))
                           + GetDoubleFromValue2(GetCellValue(objExcel, "AU"+string(i), IsPoi))
                           - GetDoubleFromValue2(GetCellValue(objExcel, "AV"+string(i), IsPoi))
                           - GetDoubleFromValue2(GetCellValue(objExcel, "AW"+string(i), IsPoi))
                           + GetDoubleFromValue2(GetCellValue(objExcel, "AX"+string(i), IsPoi));
                elem.K12 = - GetDoubleFromValue2(GetCellValue(objExcel, "AL"+string(i), IsPoi));
                elem.K14 = GetDoubleFromValue2(GetCellValue(objExcel, "AK"+string(i), IsPoi));
                elem.K16 = - GetDoubleFromValue2(GetCellValue(objExcel, "BB"+string(i), IsPoi));
            end;

            AddFIValue( elem );
            UseProgress(i);
            i = i + 1;
        end;
        
        RemProgress();
        SaveSumsArrayInDB();
    end;
end; /* ParsingXlsSheet */

/**
@brief Процедура парсинга файлов в указанном каталоге, имя которых начинается с определенной строки
@param[in] objExcel Объект через который происходит чтение файла
@param[in] PathFiles Путь по которому происходит поиск и чтение файлов
@param[in] FilePrefix Строка с которой начинается имя файла
@param[in] IsPoi Флаг режима чтения через POI
*/
macro ParsingXlsFiles( objExcel:Object, PathFiles:string, FilePrefix:string, IsPoi:Bool )
    var f = 0;
    var MaskFile :string = FilePrefix + "*.xls*"; ///< могут быть xls, xlsx, xlsm
    var ListFiles:TDirList = TDirList;
    ListFiles.List( PathFiles + MaskFile, "F"); ///< Перебираем все файлы в каталоге по маске

    if( ListFiles.Count > 0 )
       while( f < ListFiles.Count )
          InitProgress(-1, "Чтение файла " + ListFiles.Name(f), "Чтение файла " + ListFiles.Name(f));
          if( objExcel.Open(PathFiles + ListFiles.Name(f)) )
             RemProgress();
             if( (FilePrefix == FILENAME_TxRealizReg2014_ORCB) OR
                 (FilePrefix == FILENAME_TxRealizReg2014_NotORCB) )
                ParsingXlsSheet( objExcel, "0211_Свод", IsPoi );
                ParsingXlsSheet( objExcel, SHEET_212_SVOD, IsPoi );
                ParsingXlsSheet( objExcel, SHEET_215_SVOD, IsPoi );
                ParsingXlsSheet( objExcel, "0213", IsPoi );
                ParsingXlsSheet( objExcel, SHEET_215, IsPoi );
                ParsingXlsSheet( objExcel, "0216", IsPoi );
                ParsingXlsSheet( objExcel, SHEET_212, IsPoi );

             elif( FilePrefix == FILENAME_TXIncC )
                ParsingXlsSheet( objExcel, "Свод_0411", IsPoi );
                ParsingXlsSheet( objExcel, SHEET_411, IsPoi );

             elif( FilePrefix == FILENAME_TxRegisterREPO_2014 )
                ParsingXlsSheet( objExcel, "0507", IsPoi );
                ParsingXlsSheet( objExcel, "0508", IsPoi );
                ParsingXlsSheet( objExcel, "0511", IsPoi );
                ParsingXlsSheet( objExcel, "0512", IsPoi );
                ParsingXlsSheet( objExcel, "0513", IsPoi );
                ParsingXlsSheet( objExcel, "0514", IsPoi );

             elif( (FilePrefix == FILENAME_ShRepReg2014_ORCB) OR
                   (FilePrefix == FILENAME_ShRepReg2014_NotORCB) )
                ParsingXlsSheet( objExcel, "0611_Свод", IsPoi );
                ParsingXlsSheet( objExcel, SHEET_612, IsPoi );
                ParsingXlsSheet( objExcel, SHEET_615_SVOD, IsPoi );
                ParsingXlsSheet( objExcel, SHEET_615, IsPoi );

             elif( FilePrefix == FILENAME_txoutlay2014 )
                ParsingXlsSheet( objExcel, "0721_свод", IsPoi );
                ParsingXlsSheet( objExcel, SHEET_721, IsPoi );

             elif( FilePrefix == FILENAME_TxReserv )
                ParsingXlsSheet( objExcel, "0801_Свод", IsPoi );
                ParsingXlsSheet( objExcel, "0802_Свод", IsPoi );

             elif( (FilePrefix == FILENAME_IndNomBondReg_292_ORCB) OR
                   (FilePrefix == FILENAME_IndNomBondReg_292_NotORCB) )
                ParsingXlsSheet( objExcel, SHEET_292_SVOD, IsPoi );
                ParsingXlsSheet( objExcel, SHEET_292, IsPoi );

             elif( FilePrefix == FILENAME_IndNomBondReg_491 )
                ParsingXlsSheet( objExcel, "0491_Свод", IsPoi );
                ParsingXlsSheet( objExcel, SHEET_491, IsPoi );

             elif( (FilePrefix == FILENAME_IndNomBondReg_692_ORCB) OR
                   (FilePrefix == FILENAME_IndNomBondReg_692_NotORCB) )
                ParsingXlsSheet( objExcel, SHEET_692_SVOD, IsPoi );
                ParsingXlsSheet( objExcel, SHEET_692, IsPoi );

             elif( FilePrefix == FILENAME_IndNomBondReg_791 )
                ParsingXlsSheet( objExcel, "0791_Свод", IsPoi );
                ParsingXlsSheet( objExcel, SHEET_791, IsPoi );

             elif( FilePrefix == FILENAME_TaxAccSecur )
                ParsingXlsSheet( objExcel, SHEET_NUNP, IsPoi );

             end;
             if (IsPoi)
               objExcel.Close();
             else
               objExcel.Book.Close();
             end;
          else
            RemProgress();
            MsgBox("Ошибка открытия файла " + PathFiles + ListFiles.Name(f));
          end;

          f = f + 1;
       end;
    else
       msgbox("Файлы по маске <",MaskFile,"> отсутствуют");
    end;
end; /* ParsingXlsFiles */

private var TotalTaxRegDirPath = "SECUR\\КАТАЛОГ РЕГИСТРОВ ДЛЯ СВОД";
/**
@brief Функция чтения из реестра местоположения файлов для импорта
@param[out] ErrMes Сообщение об ошибке (не используется)
@return Путь местоположения файлов для импорта прочитанный из реестра
@return ..\\Import\\NUSVOD\\ в случае невозможности прочитать значение реестра
*/
MACRO SP_GetTotalTaxRegDir( ErrMes:@string ):STRING
  var TotalTaxRegDir = "", err;
  GetRegistryValue(TotalTaxRegDirPath, V_STRING, TotalTaxRegDir, err);
  if(err != 0) 
    TotalTaxRegDir = "..\\Import\\NUSVOD\\"; 
  end;
  if(substr(TotalTaxRegDir, strlen(TotalTaxRegDir)) != "\\")
    TotalTaxRegDir = TotalTaxRegDir + "\\";
  end;
  return TotalTaxRegDir;
END;

private var TotalTaxRegNumPath = "SECUR\\ФАЙЛЫ ОТЧЕТОВ ДЛЯ СВОД";
/**
@brief Функция чтения из реестра перечисления типов файлов для импорта
@param[out] ErrMes Сообщение об ошибке (не используется)
@return Перечисление типов файлов для импорта
@return "" в случае невозможности прочитать значение реестра
*/
MACRO SP_GetTotalTaxRegNum( ErrMes:@string ):STRING
  var TotalTaxRegNum = "", err;
  GetRegistryValue(TotalTaxRegNumPath, V_STRING, TotalTaxRegNum, err);
  return TotalTaxRegNum;
END;

/**
@brief Функция преобразования относительного пути в абсолютный
@param[in] p_path Путь для преобразования
@return Абсолютный путь
*/
PRIVATE MACRO processPath( p_path )
   var l_str, l_p, l_path = "";

   p_path = trim( p_path );

   if( substr( p_path, 1, 2 ) == ".." ) ///< ссылка на вышестоящий каталог
      p_path = substr( p_path, 3 );

      l_str  = getCWD(false);    
      l_p    = strbrk( l_str, "\\" );
      while( l_p != 0 )
         l_path = l_path + substr( l_str, 1, l_p - 1 ) + "\\";
         l_str  = substr( l_str, l_p + 1 );
         l_p    = strbrk( l_str, "\\" );
      end;
      return ( substr( l_path, 1, strlen( l_path ) - 1 ) + p_path );

   elif( substr( p_path, 1, 1 ) == "." ) ///< ссылка на текущий каталог
      return ( getCWD + substr( p_path, 2 ) );
   end;

  return p_path;
END;

/**
@brief Функция получения полного пути к файлу
@param[in] relativePath Относительный путь к файлу
@param[in] isRemote Флаг выполнения на терминале
@return Полный путь
*/
MACRO SP_TXGetFullPath(relativePath:string, isRemote:bool)
   var Path = "";

   if(isRemote)
      if( substr( relativePath, 1, 2 ) == ".." ) /* ссылка на вышестоящий каталог */
         relativePath = substr(relativePath, 3 );
      end;

      if( substr( relativePath, 1, 1 ) == "\\" ) /* ссылка на вышестоящий каталог */
         relativePath = substr(relativePath, 2 );
      end;

      Path = GetCurDir(true) + "\\" + relativePath + "\\"; 
   else
      Path = processPath(relativePath) + "\\";
 
      if( substr( relativePath, 1, 1 ) == "\\" )
         Path = GetCurDir(true) + Path;
      end;
   end;

   return Path;
END;

/**
@brief Функция для подмены макроса isStandAlone
@return V_BOOL Всегда возвращается true
*/
macro ThisIsStandAlone():Bool
  return true; ///> в веб (или конвейере)
end;

/**
@brief Функция показывающая наличие импортированных данных с предыдущего расчета
@return true Если данные с прошлого расчета есть в БД
*/
MACRO SP_ExistsPrevCalcData()
  if( DL_RSDCommand().GetCount("SELECT 1 FROM DSCTXTOTAL_DBT where rownum = 1") > 0 )
    return true;
  else
    return false;
  end;
END;

/**
@brief Процедура парсинга всех файлов
@param[in] IsWebService Флаг - запуск отчета из WEB
@param[in] RepParam Параметры выпуска очтета (CTaxAccSecurParams)
*/
MACRO SP_TaxParseXlsFiles(IsWebService:Bool, RepParam/*:CTaxAccSecurParams*/)
  var IsPoi:bool = true;
  /*if ((ValType(RepParam) != V_UNDEF) and (ValType(RepParam.UsePoi) == V_BOOL))
    IsPoi = RepParam.UsePoi;
  end;*/
  RepBegDate = RepParam.BegDate;
  RepEndDate = RepParam.EndDate;

  ReplaceMacro ( "msgbox", "TX_MsgBoxFromReport" );
  ReplaceMacro ( "println", "TX_MsgBoxFromReport" );
  ReplaceMacro ( "RunError", "TX_MsgBoxFromReport" );
  if(IsWebService == true)
     ReplaceMacro("isStandAlone", "ThisIsStandAlone"); ///< !!!Обязательно
  end;

  TX_ErrorMesage.size = 0;

  var objExcel:Object, ErrMes = "";;
  if (isPoi)
    objExcel = CPoiReader();
  else
    objExcel = CExcel();
    objExcel.CreateApplication();
  end;

  /// Перебираем файлы в каталоге PathFiles
  var PathFiles:string = SP_TXGetFullPath(SP_GetTotalTaxRegDir()),
      TotalTaxRegNum:string = SP_GetTotalTaxRegNum();                              

  if( not RepParam.UsePrevCalcData ) ///> IsDataCollect
          SQL_ExecuteTrn("DELETE FROM DSCTXTOTAL_DBT");
  
    if( PathFiles == "" )
       msgbox("Не задано значение настройки \"" + TotalTaxRegDirPath + "\"");
    end;

    if( (PathFiles != "") and (TotalTaxRegNum != "") )
       /// Регистры реализации 2014 обр. (211, 212, 215)
       if( Index(TotalTaxRegNum, string(ARNU_TxRealizReg2014_ORCB)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_TxRealizReg2014_ORCB, IsPoi);
       end;

       /// Регистры реализации 2014 необр. (211, 212, 215)
       if( Index(TotalTaxRegNum, string(ARNU_TxRealizReg2014_NotORCB)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_TxRealizReg2014_NotORCB, IsPoi);
       end;

       /// Регистр по начислению 2014 (411)
       if( Index(TotalTaxRegNum, string(ARNU_TXIncC)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_TXIncC, IsPoi);
       end;

       /// Проценты по РЕПО 2014 (507-508, 511-514)
       if( Index(TotalTaxRegNum, string(ARNU_TxRegisterREPO_2014)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_TxRegisterREPO_2014, IsPoi);
       end;

       /// Регистры коротких позиций 2014 (611-612, 615) обр.
       if( Index(TotalTaxRegNum, string(ARNU_ShRepReg2014_ORCB)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_ShRepReg2014_ORCB, IsPoi);
       end;

       /// Регистры коротких позиций 2014 (611-612, 615) необр.
       if( Index(TotalTaxRegNum, string(ARNU_ShRepReg2014_NotORCB)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_ShRepReg2014_NotORCB, IsPoi);
       end;

       /// Регистр по начислению расхода по коротким позициям 2014 (721)
       if( Index(TotalTaxRegNum, string(ARNU_txoutlay2014)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_txoutlay2014, IsPoi);
       end;

       /// Резервы (0801, 0802)
       if( Index(TotalTaxRegNum, string(ARNU_TxReserv)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_TxReserv, IsPoi);
       end;

       /// Регистры ОИН (0292, 0491, 0692, 0791) // 292 обр.
       if( Index(TotalTaxRegNum, string(ARNU_IndNomBondReg_292_ORCB)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_IndNomBondReg_292_ORCB, IsPoi);
       end;

       /// Регистры ОИН (0292, 0491, 0692, 0791) // 292 необр.
       if( Index(TotalTaxRegNum, string(ARNU_IndNomBondReg_292_NotORCB)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_IndNomBondReg_292_NotORCB, IsPoi);
       end;

       /// Регистры ОИН (0292, 0491, 0692, 0791) // 491
       if( Index(TotalTaxRegNum, string(ARNU_IndNomBondReg_491)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_IndNomBondReg_491, IsPoi);
       end;

       /// Регистры ОИН (0292, 0491, 0692, 0791) // 692 обр.
       if( Index(TotalTaxRegNum, string(ARNU_IndNomBondReg_692_ORCB)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_IndNomBondReg_692_ORCB, IsPoi);
       end;

       /// Регистры ОИН (0292, 0491, 0692, 0791) // 692 необр.
       if( Index(TotalTaxRegNum, string(ARNU_IndNomBondReg_692_NotORCB)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_IndNomBondReg_692_NotORCB, IsPoi);
       end;

       /// Регистры ОИН (0292, 0491, 0692, 0791) // 791
       if( Index(TotalTaxRegNum, string(ARNU_IndNomBondReg_791)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_IndNomBondReg_791, IsPoi);
       end;

       /// НУНП за предыдущий налоговый период
       if( Index(TotalTaxRegNum, string(ARNU_TaxAccSecur)) > 0 )
          ParsingXlsFiles(objExcel, PathFiles, FILENAME_TaxAccSecur, IsPoi);
       end;
    end;
  end;

  ReplaceMacro ( "msgbox", NULL );
  ReplaceMacro ( "println", NULL );
  ReplaceMacro ( "RunError", NULL );
  if(IsWebService == true)
     ReplaceMacro("isStandAlone", NULL);
  end;
 
  objExcel = null;
END;

/**
@brief Функция получения объекта класса TWsMenuItem (пункт меню для Web)
@param[in] iIdentProgram Идентификатор модуля программы
@param[in] iCaseItem ID системного выбора
@return Объект класса TWsMenuItem
*/
private macro GetMenuItem(iIdentProgram, iCaseItem)
  var menuItem = TWsMenuItem(iIdentProgram,0,0,0,iCaseItem);
  menuItem.iIdentProgram = iIdentProgram;
  menuItem.iCaseItem = iCaseItem;

  var cmd = DL_RSDCommand("SELECT T_SZNAMEITEM FROM DITEMSYST_DBT WHERE T_CIDENTPROGRAM = CHR(?) AND T_ICASEITEM = ?");
  cmd.addParam(iIdentProgram);
  cmd.addParam(iCaseItem);
  var ds = cmd.execute();

  if( ds.MoveNext() )
    menuItem.szNameItem = SQL_ConvTypeStr(ds.szNameItem);
  end;
  return menuItem;
end;

/**
@brief Функция удаления файлов в указанной директории
@param[in] path Путь для удаления файлов
@param[out] msg Сообщение об ошибке
@return 0 В случае успеха
@return 1 В случае ошибки удаления
*/
private macro ClearDir(path, msg:@string)
  var stat=0, i=0,
      dir = TDirList(path+"*.*", "F");

  if (dir.Count != 0)
    while (i < dir.Count)
      if (not RemoveFile(path+dir.name(i)))
        msg = "Не удалось удалить файл: " + ToANSI(path+dir.name(i), true);
        stat = 1;
        break;
      end;
      i = i + 1;
    end;
  end;
  return stat;
end;

/**
@brief Функция преобразования имени файла в соответствии с указанным типом
@param[in] userFileName Начальное имя файла
@param[in] ARNU_kind Тип файла в соответствии с которым происходит преобразование
@return V_STRING Преобразованное имя файла
*/
private macro GetAssociateFileName(userFileName:string, ARNU_kind:integer):string
  var name = userFileName, idx;

  if( ARNU_kind == ARNU_TxRealizReg2014_ORCB ) ///> Регистры реализации 2014 обр.
    if( Index(userFileName, FILENAME_TxRealizReg2014_ORCB) == 0 )
      idx = Index(userFileName, FILENAME_TxRealizReg2014);
      if( idx > 0 )
        name = FILENAME_TxRealizReg2014_ORCB + substr(userFileName, idx+strlen(FILENAME_TxRealizReg2014));
      else
        name = FILENAME_TxRealizReg2014_ORCB + "_" + userFileName;
      end;
    end;
  elif( ARNU_kind == ARNU_TxRealizReg2014_NotORCB ) ///> Регистры реализации 2014 необр.
    if( Index(userFileName, FILENAME_TxRealizReg2014_NotORCB) == 0 )
      idx = Index(userFileName, FILENAME_TxRealizReg2014);
      if( idx > 0 )
        name = FILENAME_TxRealizReg2014_NotORCB + substr(userFileName, idx+strlen(FILENAME_TxRealizReg2014));
       else
        name = FILENAME_TxRealizReg2014_NotORCB + "_" + userFileName;
      end;
       end;
  elif( ARNU_kind == ARNU_TXIncC ) ///> Регистр по начислению 2014 (411)
    if( Index(userFileName, FILENAME_TXIncC) == 0 )
      name = FILENAME_TXIncC + "_" + userFileName;
     end;
  elif( ARNU_kind == ARNU_TxRegisterREPO_2014 ) ///> Регистры РЕПО (0507, 0508, 0511, 0512, 0513, 0514)
    if( Index(userFileName, FILENAME_TxRegisterREPO_2014) == 0 )
      name = FILENAME_TxRegisterREPO_2014 + "_" + userFileName;
    end;
  elif( ARNU_kind == ARNU_ShRepReg2014_ORCB ) ///> Регистры коротких позиций 2014 обр.
    if( Index(userFileName, FILENAME_ShRepReg2014_ORCB) == 0 )
      idx = Index(userFileName, FILENAME_ShRepReg2014);
      if( idx > 0 )
        name = FILENAME_ShRepReg2014_ORCB + substr(userFileName, idx+strlen(FILENAME_ShRepReg2014));
  else
        name = FILENAME_ShRepReg2014_ORCB + "_" + userFileName;
      end;
    end;
  elif( ARNU_kind == ARNU_ShRepReg2014_NotORCB ) ///> Регистры коротких позиций 2014 необр.
    if( Index(userFileName, FILENAME_ShRepReg2014_NotORCB) == 0 )
      idx = Index(userFileName, FILENAME_ShRepReg2014);
      if( idx > 0 )
        name = FILENAME_ShRepReg2014_NotORCB + substr(userFileName, idx+strlen(FILENAME_ShRepReg2014));
     else
        name = FILENAME_ShRepReg2014_NotORCB + "_" + userFileName;
      end;
    end;
  elif( ARNU_kind == ARNU_txoutlay2014 ) ///> Регистр по начислению расхода по коротким позициям 2014 (721)
    if( Index(userFileName, FILENAME_txoutlay2014) == 0 )
      name = FILENAME_txoutlay2014 + "_" + userFileName;
    end;
  elif( ARNU_kind == ARNU_TxReserv ) ///> Резервы (0801, 0802)
    if( Index(userFileName, FILENAME_TxReserv) == 0 )
      name = FILENAME_TxReserv + "_" + userFileName;
    end;
  elif( ARNU_kind == ARNU_IndNomBondReg_292_ORCB ) ///> Регистры ОИН (0292, 0491, 0692, 0791) 292 обр.
    if( Index(userFileName, FILENAME_IndNomBondReg_292_ORCB) == 0 )
      idx = Index(userFileName, FILENAME_IndNomBondReg);
      if( idx > 0 )
        name = FILENAME_IndNomBondReg_292_ORCB + substr(userFileName, idx+strlen(FILENAME_IndNomBondReg));
      else
        name = FILENAME_IndNomBondReg_292_ORCB + "_" + userFileName;
      end;
    end;
  elif( ARNU_kind == ARNU_IndNomBondReg_292_NotORCB ) ///> Регистры ОИН (0292, 0491, 0692, 0791) 292 необр.
    if( Index(userFileName, FILENAME_IndNomBondReg_292_NotORCB) == 0 )
      idx = Index(userFileName, FILENAME_IndNomBondReg);
      if( idx > 0 )
        name = FILENAME_IndNomBondReg_292_NotORCB + substr(userFileName, idx+strlen(FILENAME_IndNomBondReg));
      else
        name = FILENAME_IndNomBondReg_292_NotORCB + "_" + userFileName;
      end;
    end;
  elif( ARNU_kind == FILENAME_IndNomBondReg_491 ) ///> Регистры ОИН (0292, 0491, 0692, 0791) 491
    if( Index(userFileName, FILENAME_IndNomBondReg_491) == 0 )
      idx = Index(userFileName, FILENAME_IndNomBondReg);
      if( idx > 0 )
        name = FILENAME_IndNomBondReg_491 + substr(userFileName, idx+strlen(FILENAME_IndNomBondReg));
      else
        name = FILENAME_IndNomBondReg_491 + "_" + userFileName;
      end;
    end;
  elif( ARNU_kind == ARNU_IndNomBondReg_692_ORCB ) ///> Регистры ОИН (0292, 0491, 0692, 0791) 692 обр.
    if( Index(userFileName, FILENAME_IndNomBondReg_692_ORCB) == 0 )
      idx = Index(userFileName, FILENAME_IndNomBondReg);
      if( idx > 0 )
        name = FILENAME_IndNomBondReg_692_ORCB + substr(userFileName, idx+strlen(FILENAME_IndNomBondReg));
      else
        name = FILENAME_IndNomBondReg_692_ORCB + "_" + userFileName;
      end;
    end;
  elif( ARNU_kind == ARNU_IndNomBondReg_692_NotORCB ) ///> Регистры ОИН (0292, 0491, 0692, 0791) 692 необр.
    if( Index(userFileName, FILENAME_IndNomBondReg_692_NotORCB) == 0 )
      idx = Index(userFileName, FILENAME_IndNomBondReg);
      if( idx > 0 )
        name = FILENAME_IndNomBondReg_692_NotORCB + substr(userFileName, idx+strlen(FILENAME_IndNomBondReg));
      else
        name = FILENAME_IndNomBondReg_692_NotORCB + "_" + userFileName;
      end;
    end;
  elif( ARNU_kind == FILENAME_IndNomBondReg_791 ) ///> Регистры ОИН (0292, 0491, 0692, 0791) 791
    if( Index(userFileName, FILENAME_IndNomBondReg_791) == 0 )
      idx = Index(userFileName, FILENAME_IndNomBondReg);
      if( idx > 0 )
        name = FILENAME_IndNomBondReg_791 + substr(userFileName, idx+strlen(FILENAME_IndNomBondReg));
      else
        name = FILENAME_IndNomBondReg_791 + "_" + userFileName;
      end;
    end;
  elif( ARNU_kind == ARNU_TaxAccSecur ) ///> НУНП за предыдущий налоговый период
    if( Index(userFileName, FILENAME_TaxAccSecur) == 0 )
      name = FILENAME_TaxAccSecur + "_" + userFileName;
     end;
  end;

  return name;
end;

/**
@brief Функция перемещения файла
@param[in] from Файл для перемещения
@param[in] to Новый путь для файла
@return true В случае успеха, иначе false
*/
private MACRO Move(from, to)
   if(ExistFile(from) and CopyFile(from, to))
      RemoveFile(from);
   else
      TX_MsgBoxFromReport("Ошибка при перемещении файла в каталог регистров для СВОД");
                    return false;
   end;
   return true;
END;

/**
@brief Функция переименования файла
@param[in] pathName Файл для переименования
@param[in] newPathName Новое имя файла
@return true В случае успеха, иначе false
*/
private MACRO Rename(pathName, newPathName)
   if (not RenameFile(pathName, newPathName))
      TX_MsgBoxFromReport("Не удалось переименовать файл: " + pathName + " в " + newPathName);
      return false;
   end;
   return true;
end;

/**
@brief Процедура для ассоциации файлов и премещения в папку отчетов для НУ СВОД
@param[in] kind Тип файла с данными
@param[in] fullNames Массив исходных файлов
@param[in] toPath Путь для перемещения
*/
private MACRO RenameAndMove(kind, fullNames, toPath)
   var numRep = 0;
   if(numRep < fullNames.size)
      if(fullNames[numRep] != "")
         var stat = true, path, name, ext, pathName, newName, newPathName;
         path = SplitFile(fullNames[numRep], name, ext);

         newName = GetAssociateFileName(name, kind);
         pathName = MergeFile(path, newName, ext);

         if(name != newName)
            stat = Rename(fullNames[numRep], pathName);
         end;
         if(stat)
            stat = Move(pathName, MergeFile(toPath, newName, ext));
         end;
      end;
      numRep = numRep + 1;
   end;
END;

/**
@brief Функция получения ID сессии
@return ID сессии
*/
MACRO GetSessionID():Integer
  var id:Integer = -1;
  var sql = "SELECT SYS_CONTEXT('USERENV','SESSIONID') SessionID FROM DUAL";
  var cmd = DL_RSDCommand(sql);
  var ds = cmd.execute();
  if( ds.MoveNext() )
     id = int(ds.SessionID);
  end;
  return id;
END;

/**
@brief Функция автоматического формирования регистров для СВОД
@param[in] IsWebService Флаг - выполнение из Web
@param[in] fData Данные формы
@param[in] mItem Объект класса WsMenuItem (пункт меню)
@return Объект класса WebResponseBuilder
*/
MACRO SP_PrepareRegistrsSVOD(IsWebService:Bool, fData, mItem/*:WsMenuItem*/)
  File protocol_file() txt WRITE;

  InitError();

  TX_ErrorMesage.size = 0;
  var ResponseBuilder : WebResponseBuilder = WebResponseBuilder();
  ResponseBuilder.SetUserObject( fData );
  var PathFiles:string = SP_TXGetFullPath(SP_GetTotalTaxRegDir()),
      TotalTaxRegNum:string = SP_GetTotalTaxRegNum(),
      msg:string = "";

  if(ClearDir(PathFiles, @msg) != 0)
     TX_MsgBoxFromReport(msg);
     return;
  end;

    if( PathFiles == "" )
     TX_MsgBoxFromReport("Не задано значение настройки \"" + TotalTaxRegDirPath + "\"");
    end;
   
  if( (PathFiles != "") and (TotalTaxRegNum != "") )
     var menuItem, formData, FullNames;

     formData = CTxReportForm();
     formData.AddItog = fData.AddItog;
     formData.Year = fData.Year;
     formData.Period = fData.Period;
     formData.BeginDate = fData.BeginDate;
     formData.EndDate = fData.EndDate;
     formData.ReportDate = formData.EndDate;
     formData.ByQuarter = true;
     formData.InORCB = false;
     formData.NotORCB = false;

     /// Регистры реализации 2014 обр. (211, 212, 215)
     if( Index(TotalTaxRegNum, string(ARNU_TxRealizReg2014_ORCB)) > 0 )
        formData.InORCB = true;
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1046);
        end;
   
        FullNames = ExecMacroFile(MACRO_REGISTRS_REALIZATION_2014, "ReportRunEx", IsWebService, formData, NULL, menuItem);
        formData.InORCB = false;

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \""+menuItem.szNameItem+"\" обр.");
        else
           RenameAndMove(ARNU_TxRealizReg2014_ORCB, fullNames, PathFiles);
        end;
     end;
   
     /// Регистры реализации 2014 необр. (211, 212, 215)
     if( Index(TotalTaxRegNum, string(ARNU_TxRealizReg2014_NotORCB)) > 0 )
        formData.NotORCB = true;
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1046);
        end;
   
        FullNames = ExecMacroFile(MACRO_REGISTRS_REALIZATION_2014, "ReportRunEx", IsWebService, formData, NULL, menuItem);
        formData.NotORCB = false;

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \""+menuItem.szNameItem+"\" необр.");
        else
           RenameAndMove(ARNU_TxRealizReg2014_NotORCB, fullNames, PathFiles);
        end;
     end;
   
     /// Регистр по начислению 2014 (411)
     if( Index(TotalTaxRegNum, string(ARNU_TXIncC)) > 0 )
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1043);
        end;
   
        FullNames = ExecMacroFile(MACRO_REGISTRS_CHARGES_2014, "ReportRunEx", IsWebService, formData, NULL, menuItem);

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \""+menuItem.szNameItem+"\"");
        else
           RenameAndMove(ARNU_TXIncC, fullNames, PathFiles);
        end;
     end;
   
     /// Проценты по РЕПО 2014 (507-508, 511-514)
     if( Index(TotalTaxRegNum, string(ARNU_TxRegisterREPO_2014)) > 0 )
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1040);
        end;
   
        FullNames = ExecMacroFile(MACRO_REGISTRS_REPO_2014, "ReportRunEx", IsWebService, formData, NULL, menuItem);

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \""+menuItem.szNameItem+"\"");
        else
           RenameAndMove(ARNU_TxRegisterREPO_2014, fullNames, PathFiles);
        end;
     end;
                  
     /// Регистры коротких позиций 2014 (611-612, 615) обр.
     if( Index(TotalTaxRegNum, string(ARNU_ShRepReg2014_ORCB)) > 0 )
        formData.InORCB = true;
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1051);
        end;
   
        FullNames = ExecMacroFile(MACRO_REGISTRS_SHREP_REG_2014, "ReportRunEx", IsWebService, formData, NULL, menuItem);
        formData.InORCB = false;

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \""+menuItem.szNameItem+"\" обр.");
        else
           RenameAndMove(ARNU_ShRepReg2014_ORCB, fullNames, PathFiles);
        end;
     end;
   
     /// Регистры коротких позиций 2014 (611-612, 615) необр.
     if( Index(TotalTaxRegNum, string(ARNU_ShRepReg2014_NotORCB)) > 0 )
        formData.NotORCB = true;
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1051);
        end;
   
        FullNames = ExecMacroFile(MACRO_REGISTRS_SHREP_REG_2014, "ReportRunEx", IsWebService, formData, NULL, menuItem);
        formData.NotORCB = false;

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \""+menuItem.szNameItem+"\" необр.");
        else
           RenameAndMove(ARNU_ShRepReg2014_NotORCB, fullNames, PathFiles);
        end;
     end;
                        
     /// Регистр по начислению расхода по коротким позициям 2014 (721)
     if( Index(TotalTaxRegNum, string(ARNU_txoutlay2014)) > 0 )
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1052);
        end;
   
        FullNames = ExecMacroFile(MACRO_REGISTRS_OUTLAY_SP_REG_2014, "ReportRunEx", IsWebService, formData, NULL, menuItem);

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \""+menuItem.szNameItem+"\"");
        else
           RenameAndMove(ARNU_txoutlay2014, fullNames, PathFiles);
        end;
     end;

     /// Резервы (0801, 0802)
     if( Index(TotalTaxRegNum, string(ARNU_TxReserv)) > 0 )
        formData.DealKind = 0; ///> Все
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1021);
        end;

        FullNames = ExecMacroFile(MACRO_REGISTRS_RESERVES, "ReportRunEx", IsWebService, formData, NULL, menuItem);

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \""+menuItem.szNameItem+"\"");
        else
           RenameAndMove(ARNU_TxReserv, fullNames, PathFiles);
        end;
     end;
 
     /// Регистры ОИН (0292, 0491, 0692, 0791) // 292 обр.
     if( Index(TotalTaxRegNum, string(ARNU_IndNomBondReg_292_ORCB)) > 0 )
        formData.DealKind = 0; ///> Все
        formData.InORCB = true;
        formData.ReportKindList[formData.ReportKindList.size] = CreateNameAlgFromAppServ(7667, 1);
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1071);
        end;

        FullNames = ExecMacroFile(MACRO_REGISTRS_OIN, "ReportRunEx", IsWebService, formData, NULL, menuItem);
        formData.ReportKindList.size = 0;
        formData.InORCB = false;

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \"292 обр.\"");
        else
           RenameAndMove(ARNU_IndNomBondReg_292_ORCB, fullNames, PathFiles);
        end;
     end;

     /// Регистры ОИН (0292, 0491, 0692, 0791) // 292 необр.
     if( Index(TotalTaxRegNum, string(ARNU_IndNomBondReg_292_NotORCB)) > 0 )
        formData.DealKind = 0; ///> Все
        formData.NotORCB = true;
        formData.ReportKindList[formData.ReportKindList.size] = CreateNameAlgFromAppServ(7667, 1);
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1071);
        end;

        FullNames = ExecMacroFile(MACRO_REGISTRS_OIN, "ReportRunEx", IsWebService, formData, NULL, menuItem);
        formData.ReportKindList.size = 0;
        formData.NotORCB = false;

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \"292 необр.\"");
        else
           RenameAndMove(ARNU_IndNomBondReg_292_NotORCB, fullNames, PathFiles);
        end;
     end;

     /// Регистры ОИН (0292, 0491, 0692, 0791) // 491
     if( Index(TotalTaxRegNum, string(ARNU_IndNomBondReg_491)) > 0 )
        formData.DealKind = 0; ///> Все
        formData.ReportKindList[formData.ReportKindList.size] = CreateNameAlgFromAppServ(7667, 2);
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1071);
        end;

        FullNames = ExecMacroFile(MACRO_REGISTRS_OIN, "ReportRunEx", IsWebService, formData, NULL, menuItem);
        formData.ReportKindList.size = 0;

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \"491\"");
        else
           RenameAndMove(ARNU_IndNomBondReg_491, fullNames, PathFiles);
        end;
     end;

     /// Регистры ОИН (0292, 0491, 0692, 0791) // 692 обр.
     if( Index(TotalTaxRegNum, string(ARNU_IndNomBondReg_692_ORCB)) > 0 )
        formData.DealKind = 0; ///> Все
        formData.InORCB = true;
        formData.ReportKindList[formData.ReportKindList.size] = CreateNameAlgFromAppServ(7667, 3);
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1071);
        end;

        FullNames = ExecMacroFile(MACRO_REGISTRS_OIN, "ReportRunEx", IsWebService, formData, NULL, menuItem);
        formData.ReportKindList.size = 0;
        formData.InORCB = false;

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \"692 обр.\"");
        else
           RenameAndMove(ARNU_IndNomBondReg_692_ORCB, fullNames, PathFiles);
        end;
     end;

     /// Регистры ОИН (0292, 0491, 0692, 0791) // 692 необр.
     if( Index(TotalTaxRegNum, string(ARNU_IndNomBondReg_692_NotORCB)) > 0 )
        formData.DealKind = 0; ///> Все
        formData.NotORCB = true;
        formData.ReportKindList[formData.ReportKindList.size] = CreateNameAlgFromAppServ(7667, 3);
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1071);
        end;

        FullNames = ExecMacroFile(MACRO_REGISTRS_OIN, "ReportRunEx", IsWebService, formData, NULL, menuItem);
        formData.ReportKindList.size = 0;
        formData.NotORCB = false;

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \"692 необр.\"");
        else
           RenameAndMove(ARNU_IndNomBondReg_692_NotORCB, fullNames, PathFiles);
        end;
     end;

     /// Регистры ОИН (0292, 0491, 0692, 0791) // 791
     if( Index(TotalTaxRegNum, string(ARNU_IndNomBondReg_791)) > 0 )
        formData.DealKind = 0; ///> Все
        formData.ReportKindList[formData.ReportKindList.size] = CreateNameAlgFromAppServ(7667, 4);
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1071);
        end;

        FullNames = ExecMacroFile(MACRO_REGISTRS_OIN, "ReportRunEx", IsWebService, formData, NULL, menuItem);
        formData.ReportKindList.size = 0;

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \"791\"");
        else
           RenameAndMove(ARNU_IndNomBondReg_791, fullNames, PathFiles);
        end;
     end;

     /// НУНП за предыдущий налоговый период
     if( Index(TotalTaxRegNum, string(ARNU_TaxAccSecur)) > 0 )
        formData.ReportKindList[formData.ReportKindList.size] = CreateNameAlgFromAppServ(7352, 1); ///> SC_NUNP
        formData.Period = fData.Period - 1;
        if(formData.Period == 12)
           formData.Period = 16;
           formData.Year = formData.Year - 1;
        end;
        Period_GetInterval( formData.Period, formData.Year, @formData.BeginDate, @formData.EndDate, formData.AddItog );
        formData.ReportDate = formData.EndDate;
        formData.UsePrevCalcData = false;
        formData.AutoRegistersSVOD = false;
        if(IsWebService)
           menuItem = GetMenuItem(mItem.iIdentProgram, TX_MODULE_1076);
        end;

        FullNames = ExecMacroFile(MACRO_TAXACCSECUR, "ReportRunEx", IsWebService, formData, NULL, menuItem);
        formData.ReportKindList.size = 0;

        if(FullNames.Size == 0)
           TX_MsgBoxFromReport("Нет данных для отчета \"НУНП\" за предыдущий период");
        else
           RenameAndMove(ARNU_TaxAccSecur, fullNames, PathFiles);
        end;
     end;
  end;

  if(TX_ErrorMesage.size > 0)
     fdata.NameProtocolAuto = NAME_PROTOCOL_AUTO+"_"+GetSessionID();
     var PathNameProtocol = MergeFile(PathFiles, fdata.NameProtocolAuto, "txt");
     if( Open(protocol_file, PathNameProtocol) )
        SetOutPut(PathNameProtocol);
        var i = 0;
        while(i < TX_ErrorMesage.size)
           println(TX_ErrorMesage[i]);
           i = i + 1;
        end;
        SetOutput (NULL,TRUE);
        Close( PathNameProtocol );
     end;
  end;

  return ResponseBuilder.Result;
onError(er)
   ResponseBuilder.AddErrorEx( 1, MessageSeverity.RuntimeError, er.Message );
   return ResponseBuilder.Result;
END;

/**
@brief Функция Split - разбиение строки согласно разделителя
@param[in] str Строка для разбиения
@param[in] delim Разделитель
@return TArray Массив строк
*/
private macro SplitString(str, delim)
    var arr = TArray();
    var idx = 0;
    var len = StrLen(delim);

    while(str != "")
        idx = Index(str, delim);

        if(idx == 0)
            arr[arr.size] = str;
            return arr;
        end;

        arr[arr.size] = substr(str, 1, idx - 1);
        str = substr(str, idx + len);
    end;

    return arr;
end;

/**
@brief Функция проверки и создания пути (в случае если путь не существует)
@param[in] path Путь для проверки/создания
@return 0 Путь существует/создан
@return 1 Ошибка выполнения функции
*/
private macro CreateDirIfNotExists(path)
   var stat = 0, CheckDirResult:integer;

   if(strlen(path) > 0)
      CheckDirResult = ExistDir(path);

      if( CheckDirResult == 2 ) ///> каталог с указанным именем не существует
         var dirs = SplitString(path, "\\"),
             dir = "", i = 0;
         while((i < dirs.Size) and (stat == 0))
            dir = dir + dirs[i] + "\\";            
            if((dirs[i] != ".") and (dirs[i] != ".."))
               CheckDirResult = ExistDir(dir);
               if( CheckDirResult == 2)
                  stat = IIF(MakeDir(ToANSI(dir, true))==true, 0, 1);
               end;
            end;
            i = i + 1;
         end;
      elif( CheckDirResult == 1 ) ///> недоступен для записи
         stat = 1;
      end;
   end;

   return stat;
end;

/**
@brief Функция получения файла из хранилища файлов Web клиента
@param[in] ID ID записи в таблице DFILE_STORAGE_DBT
@return CLOB поле записи по указанному ID
*/
macro GetParamCLOB(ID : integer)
  var query = "";
  var ParamCLOB;

  query = "select t_id, replace(t_file_content, chr(13)||chr(10), '') file_content from DFILE_STORAGE_DBT where t_id = "+ ID;
  /// !!!важно!!! инструмент требует, чтобы CLOB-поле не было первым в SELECT'е, иначе ошибка выполнения
  var ds = TRsbDataSet(query, RSDVAL_CLIENT, RSDVAL_STATIC);

  if (ds.MoveNext())
    ParamCLOB = ds.file_content;
  end;
  return ParamCLOB;
end;

/**
@brief Функция загрузки файла с терминала на сервер
@param[in] data Файл для загрузки
@return Объект класса WebServiceResponse
*/
MACRO TxLoadFile(data):WebServiceResponse
  var stat = 0, msgStr = "";
  var File_name = "", responseBuilder, PathToSave;
  InitError();

  responseBuilder = WebResponseBuilder();
  PathToSave = SP_TXGetFullPath(SP_GetTotalTaxRegDir());

  if( PathToSave == "" )
    msgStr = "Укажите корректный путь в настройке " + TotalTaxRegDirPath;
    stat = 1;
    responseBuilder.AddErrorEx( stat, MessageSeverity.Warning, msgStr );
  elif( CreateDirIfNotExists(PathToSave) != 0 )
    msgStr = "Не удалось создать каталог: " + PathToSave;
    stat = 1;
    responseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, msgStr );
  end;

  if( (stat == 0) and (data.IsClearFilesOnServer) )
    var dir = TDirList(PathToSave+"*.*", "F");
    var i = 0;

    if (dir.Count != 0)
      while (i < dir.Count)
        if (not RemoveFile(PathToSave+dir.name(i)))
          msgStr = "Не удалось удалить файл: " + ToANSI(PathToSave+dir.name(i), true);
          stat = 1;
          responseBuilder.AddErrorEx( stat, MessageSeverity.RuntimeError, msgStr );
          break;
        end;
        i = i + 1;
      end;
    end;
  end;

  if( (stat == 0) and (data.StorageID > 0) )
    data.File_content = GetParamCLOB(data.StorageID);
    SQL_Execute("delete from dfile_storage_dbt where t_ID = " + data.StorageID);
  end;

  if( stat == 0 )
    File_name = GetAssociateFileName(data.File_name, data.ARNU_kind);
    DecodeBase64ToFile(ToANSI(PathToSave+File_name, true), data.File_content);
  end;

  data.File_content = "";
  responseBuilder.SetUserObject( data );
  return responseBuilder.Result();

OnError( err )
  data.File_content = "";
  responseBuilder.AddErrorEx( err.code, MessageSeverity.RuntimeError, err.Message );
  responseBuilder.SetUserObject( data );
  return responseBuilder.Result();
END;

