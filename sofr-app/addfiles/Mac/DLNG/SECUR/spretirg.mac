/* 
$Name:             spretirg.mac
$Module:           Ценные бумаги 
$Description:      БО ЦБ, генерация отложенных операций погашения
*/

import SPInter, "secinter.mac", "sp_ptick.mac", "sp_calc.mac";

private record RetireDst("dl_tick.dbt");
private record RetireLnk("dl_tklnk.dbt");
private record Retire("dl_tick.dbt");
private record Leg("dl_leg.dbt");
private record Fin("fininstr.dbt");
private file dealFile("dl_tick.dbt");
private file linkFile("dl_tklnk.dbt") key 4;

private const DL_TKLNK_TYPE_RETIREDST = 1;

macro GenerateRetireOperationsByDealIdAndLinkId(dealId:integer, linkId:integer, dealDate:date)
   ClearRecord(dealFile);
   dealFile.dealId = dealId;

   if (not GetEQ(dealFile))
      MsgBox("Не найдены условия сделки dl_tick.dbt (ид сделки = ", dealId, " )");
      return 1;
   end;

   ClearRecord(linkFile);
   linkFile.id = linkId;

   if (not GetEQ(linkFile))
      MsgBox("Не найдены условия сделки dl_tklnk.dbt (ид связки = ", linkId, " )");
      return 1;
   end;

   copy(Retire, dealFile);
   Retire.BofficeKind = DL_RETIREMENT;
   Retire.DealCode    = Generate_DealCode( DL_RETIREMENT, {curdate} ); 

   Retire.Flag2    = linkFile.CalcTax;

   if( linkFile.ClientID == {OurBank} )
      Retire.ClientID = -1;
   else
      Retire.ClientID = linkFile.ClientID;
      Retire.ClientContrID = linkFile.ClientContrID;
   end;

   if (dealDate != null)
      Retire.dealDate = dealDate;
   end;

   if( FindDL_LEG( LEG_KIND_DL_TICK, dealFile.dealId, 0, leg ) )
      MsgBox( "Не найдены условия сделки dl_leg.dbt (код сделки = ", dealFile.DealCode," )" );
      return 1;
   end;

   if( ПолучитьФинИн( leg.PFI, Fin ) )
      msgbox( "Не найдена ценная бумага с ID = ", leg.PFI );
      return 1; 
   end;

   Retire.DealID = 0;
   leg.DealID = 0;
   leg.ID = 0;
   leg.Principal = linkFile.Amount;
   leg.TotalCost = linkFile.PayAmount;
   leg.NKD = linkFile.IncomeAmount;
   leg.Cost = linkFile.NominalAmount;

   if( SP_CreateDeal( Retire, leg, null, null, null, null, null, true, linkFile.ID, false) )
      return 1;
   end;
  
   return 0;
end;

/* АВТОГЕНЕРАЦИЯ ОПЕРАЦИЯ ПОГАШЕНИЯ */
MACRO GenerateRetireOperations( _RetireDst, _RetireLnk ) 
   SetBuff( RetireDst, _RetireDst );
   SetBuff( RetireLnk, _RetireLnk );

   return GenerateRetireOperationsByDealIdAndLinkId(RetireDst.dealId, RetireLnk.id);
END;

private file LnkDstSum("dl_tklnk.dbt") write key 2;


private record iss(avoiriss);
MACRO SP_CalcNDFLtoFI( FIID, dat ):BOOL
  VAR ValidFromDate = DATE(0,0,0);

  if( ПолучитьФинИн(FIID,null,iss) != 0 )
     return false;
  end;

  return ( (GetMainObjAttr( null, OBJTYPE_AVOIRISS, UniID(iss, OBJTYPE_AVOIRISS), 37 /*Группа налогового учета для НДФЛ*/, null, null, null, dat, ValidFromDate) == true)
          AND (ValidFromDate <= dat)
         );
END;

MACRO DL_IsPayerNPTax( ClientID, dat ):BOOL
  VAR RSD, SQLQuery = RSDCommand(" SELECT NPTAX.TS_IsPayerNPTax( ? , ? ) Attr FROM DUAL ");
   SQLQuery.addParam( "", RSDBP_IN, ClientID );
   SQLQuery.addParam( "", RSDBP_IN, dat );
   SQLQuery.execute();

   RSD = TRsbDataSet(SQLQuery);

   if( RSD.MoveNext() )
      return RSD.Attr > 0;
   end;

   return false;
END;

MACRO СТАВКА_НДФЛ():DOUBLE
  CONST RegPath = "ДОВЕРИТЕЛЬНОЕ УПРАВЛЕНИЕ\\СТАВКА НА ДОХОДЫ ФЛ";
  VAR   err, Rate;

  if( Rate == NULL )
     GetRegistryValue( RegPath, V_DOUBLE, Rate, err );
     if( err != 0 )
        MsgBox( "Ошибка при получении значения настройки \"" + RegPath + "\"");
     end;
  end;
  return Rate;
END;

macro GenerateRetireSumsByDealId(dealId:integer)
   var error, stat, rate, TotalAmount, TotalCost, NominalAmount, IncomeAmount, IsNDFLtoFI, NDFLRate;

   ClearRecord(dealFile);

   dealFile.dealId = dealId;

   if (not GetEQ(dealFile))
      MsgBox("Не найдены условия сделки dl_tick.dbt (ид сделки = ", dealId, " )");
      return 1;
   end;

   if (FindDL_LEG(LEG_KIND_DL_TICK, dealFile.DealID, 0, leg))
      MsgBox("Не найдены условия сделки dl_leg.dbt (код сделки = ", dealFile.DealCode," )");
      return 1;
   end;

   IsNDFLtoFI = SP_CalcNDFLtoFI(Leg.PFI, dealFile.DealDate);
   NDFLRate   = СТАВКА_НДФЛ();

   TotalAmount   = Leg.Principal;
   TotalCost     = Leg.TotalCost;
   IncomeAmount  = Leg.NKD;
   NominalAmount = Leg.Cost;
   ClearRecord(LnkDstSum);
   LnkDstSum.Type        = DL_TKLNK_TYPE_RETIREDST;
   LnkDstSum.ParentID    = dealFile.DealID;
   LnkDstSum.TotalAmount = $9999999999;

   stat = GetGE(LnkDstSum);
   while (stat AND (LnkDstSum.Type == DL_TKLNK_TYPE_RETIREDST) AND (LnkDstSum.ParentID == dealFile.DealID))
      if (LnkDstSum.TotalAmount > 0)
         if (TotalAmount > LnkDstSum.TotalAmount)
            rate = LnkDstSum.TotalAmount/TotalAmount;
            LnkDstSum.Amount        = LnkDstSum.TotalAmount;
            LnkDstSum.PayAmount     = TotalCost*rate;
            LnkDstSum.IncomeAmount  = IncomeAmount*rate;
            LnkDstSum.NominalAmount = NominalAmount*rate;
            TotalAmount = TotalAmount - LnkDstSum.Amount;
            TotalCost   = TotalCost - LnkDstSum.PayAmount;
            NominalAmount = NominalAmount - LnkDstSum.NominalAmount;
            IncomeAmount = IncomeAmount - LnkDstSum.IncomeAmount;
         else
            LnkDstSum.Amount    = TotalAmount;
            LnkDstSum.PayAmount = TotalCost;
            LnkDstSum.IncomeAmount  = IncomeAmount;
            LnkDstSum.NominalAmount = NominalAmount;
            TotalCost = TotalAmount = $0;
         end;

         if (DL_IsPayerNPTax(LnkDstSum.ClientID ,dealFile.DealDate) AND IsNDFLtoFI)
            LnkDstSum.IncomeAmount = SP_CorrectRetNKDbyNDFL(LnkDstSum.ClientID, LnkDstSum.ClientContrID, RetireDst.PFI, RetireDst.Number_Coupon, LnkDstSum.IncomeAmount, Leg.NKDFIID);
            LnkDstSum.PayAmount = LnkDstSum.IncomeAmount + LnkDstSum.NominalAmount;
         end;

         Update(LnkDstSum);
      end;

      stat = Next(LnkDstSum);
   end;

   OnError
      status(error);
      MsgBox("Ошибка обновления записи dl_tklnk.dbt.|" + error);
end;

/* Распределение суммы по получателям */
MACRO GenerateRetireDst(_RetireDst)
   SetBuff(RetireDst, _RetireDst);

   GenerateRetireSumsByDealId(RetireDst.DealID);
END;
