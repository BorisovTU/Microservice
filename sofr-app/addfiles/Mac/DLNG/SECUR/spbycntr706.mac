/*
$Name:        spbycntr706.mac
$Module:      Securities
$Description: Отчет "Сведения об объемах внебиржевых сделок" - часть БОЦБ
*/

IMPORT "secinter.mac", "spserv.mac", DealsInter, "sp_class.mac", "SpRepFun.mac", "globals.mac";

PRIVATE VAR FDlByCntrTmp = TBFile("dlbycntr.tmp", "W", 0);

PRIVATE MACRO СохранитьДляОтчета( FD, NumDep:integer, SubNumDep:integer, DealChange, EndDate, IsPartyClient, Report:variant )

   RECORD REmi( party );
   RECORD DEmi( party );
   var Fininstr = TRecHandler("fininstr.dbt");
   var Categ = 0, EmiID, CFI, Summa = $0,
       NumInList = "";

   ClearRecord( FDlByCntrTmp );
   ClearRecord( REmi );

   /*Замена эмитента возможна только для облигаций, в этом случае получим его из истории*/
   if( FI_IsBond(FD.fininstr))
      EmiID = FI_GetIssuerOnDate(FD.fininstr, EndDate);
   else
      EmiID = FD.fininstr.rec.Issuer;
   end;

   if( ПолучитьСубъекта(EmiID, REmi) != 0 ) /*эмитент*/
      Report.PrintMsgbox("Не найден эмитент для ц/б " + FD.fininstr.rec.FI_Code);
   end;

   if( FI_IsShare(FD.fininstr, false) )
     FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_SHARE;
   elif( FI_IsBond(FD.fininstr) )
     FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_BOND;
   elif( FI_IsInvestmentShare(FD.fininstr) )
     FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_INVESTMENT_SHARE;
   elif( FI_IsDeposReceipt(FD.fininstr) and (FD.fininstr.rec.AvoirKind == AVOIRISSKIND_RUSSIAN_DEPREC) )
     FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_DEPOSITORY_RECEIPT;
   elif(REmi.NotResident == SET_CHAR and ((FD.fininstr.rec.AvoirKind == AVOIRISSKIND_AMERICAN_DEPREC) or (FD.fininstr.rec.AvoirKind == AVOIRISSKIND_EUROPEAN_DEPREC) or (FD.fininstr.rec.AvoirKind == AVOIRISSKIND_GLOBAL_DEPREC)))
     FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_DEPOSITORY_RECEIPT;
   else
      return;
   end;

   FDlByCntrTmp.rec.NotResident = REmi.NotResident;/*резидентность эмитента*/
   if( FDlByCntrTmp.rec.NotResident == SET_CHAR )
      if( DL_GetObjAttrName( OBJTYPE_AVOIRISS, 28/*Квалификация в качестве ценной бумаги*/, DL_GetMainObjAttr(FD.avoiriss,28/*Квалификация в качестве ценной бумаги*/,OBJTYPE_AVOIRISS) ) == "Нет" )
         return;
      end;
   end;

   FDlByCntrTmp.rec.FI_Kind  = FD.fininstr.rec.FI_Kind;
   FDlByCntrTmp.rec.DealDate = FD.tick.rec.DealDate;

   FD.SetCurPFI(DealChange.oldpfi1);/*сделка включается в отчет с той ценной бумагой, которая была указана по состоянию на дату заключения сделки*/ 

   CFI   = IIF(FD.IsBack, DealChange.OldCFI2,  DealChange.OldCFI1);
   Summa = IIF(FD.IsBack, DealChange.OldTotalCost2, DealChange.OldTotalCost1);

   if( CFI != NATCUR )
      if( not SmartConvertSum( Summa, Summa, FD.tick.rec.DealDate, CFI, NATCUR, false) )
         Report.PrintMsgBox( GetCurrencyConvertErrorMsg(CFI, NATCUR, FD.tick.rec.DealDate) );
         return;
      end;
   end;

   FDlByCntrTmp.rec.Summa = Summa;
   FDlByCntrTmp.rec.FIID  = FD.fininstr.rec.FIID;
   
   if (not IsBasket(FD.Group))
      if( FDlByCntrTmp.rec.AvrType == AVOIRISSKIND_INVESTMENT_SHARE )
        FDlByCntrTmp.rec.AvrName = "\"" + REmi.Name + "\"";
      else
        FDlByCntrTmp.rec.AvrName = AvoirKindName(FD.fininstr.rec.AvoirKind);/*здесь сохраняем наименование вида ц\б*/
      end;

      if( FDlByCntrTmp.rec.AvrType == AVOIRISSKIND_DEPOSITORY_RECEIPT )
        // Для РДР запишем в виде
        // <"Сок. наименование эмитента РДР"> на <Вид баз. актива> <"Сок. наименование эмитента баз. актива">
          if( (FD.fininstr.rec.ParentFI > 0) and ПолучитьФинИн(FD.fininstr.rec.ParentFI, Fininstr) )
            Report.PrintMsgbox("Не найдена ц/б (FIID = " + FD.fininstr.rec.ParentFI + ")");
          else
            if( FI_IsBond(Fininstr))
               EmiID = FI_GetIssuerOnDate(Fininstr, EndDate);
            else
               EmiID = Fininstr.rec.Issuer;
            end;

            if( ПолучитьСубъекта(EmiID, DEmi) != 0 ) /*эмитент*/
               Report.PrintMsgbox("Не найден эмитент для ц/б " + Fininstr.rec.FI_Code);
            end;
          end;

        FDlByCntrTmp.rec.IssuerName = REmi.ShortName + " на " + AvoirKindName(Fininstr.rec.AvoirKind) + " " + DEmi.ShortName;

      elif( FDlByCntrTmp.rec.AvrType == AVOIRISSKIND_INVESTMENT_SHARE )
        FDlByCntrTmp.rec.IssuerName = "(\"" + FD.fininstr.rec.Name + "\")";
      else
        FDlByCntrTmp.rec.IssuerName = REmi.Name;/*полное наименование эмитента*/
      end;
   
   end;

   FDlByCntrTmp.rec.ContrKind = -1;
   FDlByCntrTmp.rec.Department = FD.tick.rec.Department;

   if( IsClientDeal(FD.tick.rec) )
      FDlByCntrTmp.rec.TypeDeal = string(NumDep, SubNumDep, "1");
   else
      FDlByCntrTmp.rec.TypeDeal = string(NumDep, SubNumDep, "0");
   end;
   FDlByCntrTmp.rec.BuySale = GetDealBuySale(FD.tick.rec, FD.IsBack);


   if ((NumDep == 2) and IsBasket(FD.Group))
      var query = "select t_principal*" + Summa + "/" + DealChange.OldPrincipal + " as t_AvrSum, fininstr.t_FI_Kind, tick_ens.t_FIID, fininstr.t_Name, fininstr.t_ParentFI " + 
        " from ddl_tick_ens_dbt tick_ens " + 
        " inner join dfininstr_dbt fininstr on fininstr.t_FIID = tick_ens.t_FIID "
         "where tick_ens.t_dealid = " + FD.tick.rec.DealID + 
        " and tick_ens.t_date = " + GetSQLDate(FD.tick.rec.DealDate);

      var sqlQuery = DL_RSDCommand(query);
      var DealEns = sqlQuery.execute();

      while( DealEns.MoveNext() )
	     var FDFininstr = TRecHandler("fininstr.dbt");
		 ПолучитьФинИн(DealEns.FIID, FDFininstr);
		 
         FDlByCntrTmp.rec.Summa   = DealEns.AvrSum;
         FDlByCntrTmp.rec.FIID    = DealEns.FIID;
         FDlByCntrTmp.rec.AvrName = DealEns.Name;
         FDlByCntrTmp.rec.FI_Kind = DealEns.FI_Kind;
		 
		 if( FI_IsShare(FDFininstr, false) )
            FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_SHARE;
         elif( FI_IsBond(FDFininstr) )
            FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_BOND;
         elif( FI_IsInvestmentShare(FDFininstr) )
            FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_INVESTMENT_SHARE;
         elif( FI_IsDeposReceipt(FDFininstr) and (FDFininstr.rec.AvoirKind == AVOIRISSKIND_RUSSIAN_DEPREC) )
            FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_DEPOSITORY_RECEIPT;
         elif(REmi.NotResident == SET_CHAR and ((FDFininstr.rec.AvoirKind == AVOIRISSKIND_AMERICAN_DEPREC) or (FDFininstr.rec.AvoirKind == AVOIRISSKIND_EUROPEAN_DEPREC) or (FDFininstr.rec.AvoirKind == AVOIRISSKIND_GLOBAL_DEPREC)))
            FDlByCntrTmp.rec.AvrType  = AVOIRISSKIND_DEPOSITORY_RECEIPT;
         end;
		 
         if( FDlByCntrTmp.rec.AvrType == AVOIRISSKIND_DEPOSITORY_RECEIPT )
            // Для РДР запишем в виде
            // <"Сок. наименование эмитента РДР"> на <Вид баз. актива> <"Сок. наименование эмитента баз. актива">
            if( (FD.fininstr.rec.ParentFI > 0) and ПолучитьФинИн(DealEns.ParentFI, Fininstr) )
               Report.PrintMsgbox("Не найдена ц/б (FIID = " + DealEns.ParentFI + ")");
            else
               if( FI_IsBond(Fininstr))
                  EmiID = FI_GetIssuerOnDate(Fininstr, EndDate);
               else
                  EmiID = Fininstr.rec.Issuer;
               end;

               if( ПолучитьСубъекта(EmiID, DEmi) != 0 ) /*эмитент*/
                  Report.PrintMsgbox("Не найден эмитент для ц/б " + Fininstr.rec.FI_Code);
               end;
            end;
            FDlByCntrTmp.rec.IssuerName = REmi.ShortName + " на " + AvoirKindName(Fininstr.rec.AvoirKind) + " " + DEmi.ShortName;

         elif( FDlByCntrTmp.rec.AvrType == AVOIRISSKIND_INVESTMENT_SHARE )
            FDlByCntrTmp.rec.IssuerName = "(\"" + DealEns.Name + "\")";
         else
            FDlByCntrTmp.rec.IssuerName = REmi.Name;/*полное наименование эмитента*/
         end;

         if( not Insert( FDlByCntrTmp ) )
             RunError( "Ошибка при вставке данных во временный файл отчета.");
         end;

      end;
      return;
   end;

   if( not Insert( FDlByCntrTmp ) )
      RunError( "Ошибка при вставке данных во временный файл отчета.");
   end;

   if(IsPartyClient)

      FDlByCntrTmp.rec.TypeDeal = string(NumDep, SubNumDep, "1");

      if(GetDealBuySale(FD.tick.rec, FD.IsBack) == DEAL_TYPE_SALE)
         FDlByCntrTmp.rec.BuySale = DEAL_TYPE_BUY;
      else
         FDlByCntrTmp.rec.BuySale = DEAL_TYPE_SALE;
      end;

      if( not Insert( FDlByCntrTmp ) )
         RunError( "Ошибка при вставке данных во временный файл отчета.");
      end;

   end; 

END;

PRIVATE MACRO ReportRun706(Report:variant)
   VAR FDeal = TRecHandler("dl_tick");
   var DealChange  = TRecHandler( "sptkchng" ),
       CurNum = 0, FD, ClientID, IsPartyClient, IsContrAndClient = false;
   var query_common, query, query1_common, query11, query12, query2, SqlQuery, DealData, Num = 0; 
   query_common = 
           " select tick.* " + 
           "   from ddl_tick_dbt tick, ddl_leg_dbt leg " +
           "  where (tick.t_BofficeKind = " + DL_SECURITYDOC + " " +
           "      or tick.t_BofficeKind = " + DL_INVESTSHARE + " ) " +                
           "    and tick.t_DealStatus >= " + DL_READIED + " " +
           "    and leg.t_DealID = tick.t_DealID " +
           "    and leg.t_LegKind = " + LEG_KIND_DL_TICK + " " +
           "    and leg.t_LegID = 0 and tick.T_DEALTYPE != 32732 and tick.T_DEALTYPE != 32742 " + /*517873 PDV исключить технические сделки 32732 из отчета*//*GAA:521141, new:32742*/
           "    and (leg.t_RejectDate > " + GetSQLDate( Report.GetEndDate() ) + " or leg.t_RejectDate = to_date('01.01.0001','DD.MM.YYYY')) " +
           "    and tick.t_DealDate BETWEEN " + GetSQLDate( Report.GetBegDate() ) + " and " + GetSQLDate( Report.GetEndDate() ) + " " +
           "    and tick.t_RequestID = 0 ";

   query1_common = 
           "    and not exists (select 1 " +
           "                      from ddvndeal_dbt dvdeal " +
           "                     where dvdeal.t_ID = tick.t_ParentID " +
           "                       and tick.t_OriginID = " + CodeFor( "Ю" ) + " " +
           "                   ) ";

   query11 = 
           "    and rsb_secur.IsOutExchange(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)), 1) = 1 " +
           "    and (rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) != 1 " +
           "         or ( rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1 " +
           "              and rsb_secur.IsBasket(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) != 1 " +
           "              and NVL((select leg2.t_RejectDate " +
           "                         from ddl_leg_dbt leg2 " +
           "                        where leg2.t_DealID = tick.t_DealID " +
           "                          and leg2.t_LegKind = " + LEG_KIND_DL_TICK_BACK + " " +
           "                          and leg2.t_LegID = 0 " +
           "                          and leg2.t_RejectDate != to_date('01.01.0001','DD.MM.YYYY') " +
           "                      ),to_date('01.01.9999','DD.MM.YYYY')) < " + GetSQLDate( Report.GetEndDate() ) + " " +
           "            ) " +
           "        ) ";

   query12 = 
           "    and rsb_secur.IsBroker(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1 " +
           "    and tick.t_BrokerID <> " + UnknownParty + " " +
           "    and exists (select 1 " +
           "                  from dpartyown_dbt partyown " +
           "                 where partyown.t_PartyKind = " + PTK_BROKER + " " +
           "                   and partyown.t_PartyID = tick.t_BrokerID) " +
           "    and exists (select 1 " +
           "                  from dsfcontr_dbt sfcontr " +
           "                 where sfcontr.t_ServKind = " + PTSK_STOCKDL + " " +
           "                   and sfcontr.t_PartyID = " + {OurBank} + " " +
           "                   and sfcontr.t_ContractorID = tick.t_BrokerID) " +
           "    and tick.t_MarketID = " + UnknownParty + " ";

   query2 =
           "    and rsb_secur.IsOutExchange(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind)), 1) = 1 " +
           "    and (rsb_secur.IsRepo(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1 " +
           "         and (rsb_secur.IsBasket(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) != 1 " +
           "          or (rsb_secur.IsBasket(rsb_secur.get_OperationGroup(rsb_secur.get_OperSysTypes(tick.t_DealType, tick.t_BofficeKind))) = 1 " +
           "         and not exists (select 1 " +
           "                  from dpartyown_dbt partyown " +
           "                 where partyown.t_PartyKind = " + PTK_CENTRBANK + " " +
           "                   and partyown.t_PartyID = tick.t_PartyID))) " +
           "         and exists (select leg2.t_RejectDate " +
           "                       from ddl_leg_dbt leg2 " +
           "                      where leg2.t_DealID = tick.t_DealID " +
           "                        and leg2.t_LegKind = " + LEG_KIND_DL_TICK_BACK + " " +
           "                        and leg2.t_LegID = 0 " +
           "                        and (leg2.t_RejectDate = to_date('01.01.0001','DD.MM.YYYY') or leg2.t_RejectDate >= " + GetSQLDate( Report.GetEndDate() ) + ") " +
           "                    ) " +
           "        ) ";

   macro CollectData(NumDep:integer, SubNumDep:integer)

      if( SubNumDep == null )
         SubNumDep = 1;
      end;

      ClearRecord(FDeal.rec);

      SqlQuery = DL_RSDCommand(query);

      Num = SqlQuery.GetCount();

      DealData = SqlQuery.execute();

      InitProgress( Num, "Отчет \"Сведения об объемах внебиржевых сделок\"", "Просмотр сделок модуля БОЦБ для раздела "+NumDep );

      while( DealData.MoveNext() )

         DealData.GetRecord().CopyTo(FDeal.rec);

         FD = SPFirstDoc(FDeal);

         SP_GetDealParmsByDate( FDeal, FDeal.rec.DealDate, DealChange, false, false );

         СохранитьДляОтчета( FD, NumDep, SubNumDep, DealChange.rec, Report.GetEndDate(), false, Report );

         UseProgress( CurNum = CurNum + 1 );
      end;

      RemProgress;

   end;

   query = query_common + query1_common + query11;
   CollectData(1, 1);

   query = query_common + query1_common + query12;
   CollectData(1, 2);

   query = query_common + query2;
   CollectData(2);
END;
